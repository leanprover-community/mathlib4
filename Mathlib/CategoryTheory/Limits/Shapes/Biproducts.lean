/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Jakob von Raumer
-/
import Mathlib.CategoryTheory.Limits.Shapes.FiniteProducts
import Mathlib.CategoryTheory.Limits.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Limits.Shapes.Kernels

/-!
# Biproducts and binary biproducts

We introduce the notion of (finite) biproducts and binary biproducts.

These are slightly unusual relative to the other shapes in the library,
as they are simultaneously limits and colimits.
(Zero objects are similar; they are "biterminal".)

For results about biproducts in preadditive categories see
`CategoryTheory.Preadditive.Biproducts`.

In a category with zero morphisms, we model the (binary) biproduct of `P Q : C`
using a `BinaryBicone`, which has a cone point `X`,
and morphisms `fst : X âŸ¶ P`, `snd : X âŸ¶ Q`, `inl : P âŸ¶ X` and `inr : X âŸ¶ Q`,
such that `inl â‰« fst = ğŸ™ P`, `inl â‰« snd = 0`, `inr â‰« fst = 0`, and `inr â‰« snd = ğŸ™ Q`.
Such a `BinaryBicone` is a biproduct if the cone is a limit cone, and the cocone is a colimit
cocone.

For biproducts indexed by a `Fintype J`, a `bicone` again consists of a cone point `X`
and morphisms `Ï€ j : X âŸ¶ F j` and `Î¹ j : F j âŸ¶ X` for each `j`,
such that `Î¹ j â‰« Ï€ j'` is the identity when `j = j'` and zero otherwise.

## Notation
As `âŠ•` is already taken for the sum of types, we introduce the notation `X âŠ Y` for
a binary biproduct. We introduce `â¨ f` for the indexed biproduct.

## Implementation notes

Prior to https://github.com/leanprover-community/mathlib3/pull/14046,
`HasFiniteBiproducts` required a `DecidableEq` instance on the indexing type.
As this had no pay-off (everything about limits is non-constructive in mathlib),
 and occasional cost
(constructing decidability instances appropriate for constructions involving the indexing type),
we made everything classical.
-/

noncomputable section

universe w w' v u

open CategoryTheory

open CategoryTheory.Functor

open scoped Classical

namespace CategoryTheory

namespace Limits

variable {J : Type w}
universe uC' uC uD' uD
variable {C : Type uC} [Category.{uC'} C] [HasZeroMorphisms C]
variable {D : Type uD} [Category.{uD'} D] [HasZeroMorphisms D]

/-- A `c : Bicone F` is:
* an object `c.pt` and
* morphisms `Ï€ j : pt âŸ¶ F j` and `Î¹ j : F j âŸ¶ pt` for each `j`,
* such that `Î¹ j â‰« Ï€ j'` is the identity when `j = j'` and zero otherwise.
-/
-- @[nolint has_nonempty_instance] Porting note (https://github.com/leanprover-community/mathlib4/issues/5171): removed
structure Bicone (F : J â†’ C) where
  pt : C
  Ï€ : âˆ€ j, pt âŸ¶ F j
  Î¹ : âˆ€ j, F j âŸ¶ pt
  Î¹_Ï€ : âˆ€ j j', Î¹ j â‰« Ï€ j' =
    if h : j = j' then eqToHom (congrArg F h) else 0 := by aesop

attribute [inherit_doc Bicone] Bicone.pt Bicone.Ï€ Bicone.Î¹ Bicone.Î¹_Ï€

@[reassoc (attr := simp)]
theorem bicone_Î¹_Ï€_self {F : J â†’ C} (B : Bicone F) (j : J) : B.Î¹ j â‰« B.Ï€ j = ğŸ™ (F j) := by
  simpa using B.Î¹_Ï€ j j

@[reassoc (attr := simp)]
theorem bicone_Î¹_Ï€_ne {F : J â†’ C} (B : Bicone F) {j j' : J} (h : j â‰  j') : B.Î¹ j â‰« B.Ï€ j' = 0 := by
  simpa [h] using B.Î¹_Ï€ j j'

variable {F : J â†’ C}

/-- A bicone morphism between two bicones for the same diagram is a morphism of the bicone points
which commutes with the cone and cocone legs. -/
structure BiconeMorphism {F : J â†’ C} (A B : Bicone F) where
  /-- A morphism between the two vertex objects of the bicones -/
  hom : A.pt âŸ¶ B.pt
  /-- The triangle consisting of the two natural transformations and `hom` commutes -/
  wÏ€ : âˆ€ j : J, hom â‰« B.Ï€ j = A.Ï€ j := by aesop_cat
  /-- The triangle consisting of the two natural transformations and `hom` commutes -/
  wÎ¹ : âˆ€ j : J, A.Î¹ j â‰« hom = B.Î¹ j := by aesop_cat

attribute [reassoc (attr := simp)] BiconeMorphism.wÎ¹
attribute [reassoc (attr := simp)] BiconeMorphism.wÏ€

/-- The category of bicones on a given diagram. -/
@[simps]
instance Bicone.category : Category (Bicone F) where
  Hom A B := BiconeMorphism A B
  comp f g := { hom := f.hom â‰« g.hom }
  id B := { hom := ğŸ™ B.pt }

-- Porting note: if we do not have `simps` automatically generate the lemma for simplifying
-- the `hom` field of a category, we need to write the `ext` lemma in terms of the categorical
-- morphism, rather than the underlying structure.
@[ext]
theorem BiconeMorphism.ext {c c' : Bicone F} (f g : c âŸ¶ c') (w : f.hom = g.hom) : f = g := by
  cases f
  cases g
  congr

namespace Bicones

/-- To give an isomorphism between cocones, it suffices to give an
  isomorphism between their vertices which commutes with the cocone
  maps. -/
@[aesop apply safe (rule_sets := [CategoryTheory]), simps]
def ext {c c' : Bicone F} (Ï† : c.pt â‰… c'.pt)
    (wÎ¹ : âˆ€ j, c.Î¹ j â‰« Ï†.hom = c'.Î¹ j := by aesop_cat)
    (wÏ€ : âˆ€ j, Ï†.hom â‰« c'.Ï€ j = c.Ï€ j := by aesop_cat) : c â‰… c' where
  hom := { hom := Ï†.hom }
  inv :=
    { hom := Ï†.inv
      wÎ¹ := fun j => Ï†.comp_inv_eq.mpr (wÎ¹ j).symm
      wÏ€ := fun j => Ï†.inv_comp_eq.mpr (wÏ€ j).symm  }

variable (F) in
/-- A functor `G : C â¥¤ D` sends bicones over `F` to bicones over `G.obj âˆ˜ F` functorially. -/
@[simps]
def functoriality (G : C â¥¤ D) [Functor.PreservesZeroMorphisms G] :
    Bicone F â¥¤ Bicone (G.obj âˆ˜ F) where
  obj A :=
    { pt := G.obj A.pt
      Ï€ := fun j => G.map (A.Ï€ j)
      Î¹ := fun j => G.map (A.Î¹ j)
      Î¹_Ï€ := fun i j => (Functor.map_comp _ _ _).symm.trans <| by
        rw [A.Î¹_Ï€]
        aesop_cat }
  map f :=
    { hom := G.map f.hom
      wÏ€ := fun j => by simp [-BiconeMorphism.wÏ€, â† f.wÏ€ j]
      wÎ¹ := fun j => by simp [-BiconeMorphism.wÎ¹, â† f.wÎ¹ j] }

variable (G : C â¥¤ D)

instance functoriality_full [G.PreservesZeroMorphisms] [G.Full] [G.Faithful] :
    (functoriality F G).Full where
  map_surjective t :=
   âŸ¨{ hom := G.preimage t.hom
      wÎ¹ := fun j => G.map_injective (by simpa using t.wÎ¹ j)
      wÏ€ := fun j => G.map_injective (by simpa using t.wÏ€ j) }, by aesop_catâŸ©

instance functoriality_faithful [G.PreservesZeroMorphisms] [G.Faithful] :
    (functoriality F G).Faithful where
  map_injective {_X} {_Y} f g h :=
    BiconeMorphism.ext f g <| G.map_injective <| congr_arg BiconeMorphism.hom h

end Bicones

namespace Bicone

attribute [local aesop safe tactic (rule_sets := [CategoryTheory])]
  CategoryTheory.Discrete.discreteCases
-- Porting note: would it be okay to use this more generally?
attribute [local aesop safe cases (rule_sets := [CategoryTheory])] Eq

/-- Extract the cone from a bicone. -/
def toConeFunctor : Bicone F â¥¤ Cone (Discrete.functor F) where
  obj B := { pt := B.pt, Ï€ := { app := fun j => B.Ï€ j.as } }
  map {_ _} F := { hom := F.hom, w := fun _ => F.wÏ€ _ }

/-- A shorthand for `toConeFunctor.obj` -/
abbrev toCone (B : Bicone F) : Cone (Discrete.functor F) := toConeFunctor.obj B

-- TODO Consider changing this API to `toFan (B : Bicone F) : Fan F`.

@[simp]
theorem toCone_pt (B : Bicone F) : B.toCone.pt = B.pt := rfl

@[simp]
theorem toCone_Ï€_app (B : Bicone F) (j : Discrete J) : B.toCone.Ï€.app j = B.Ï€ j.as := rfl

theorem toCone_Ï€_app_mk (B : Bicone F) (j : J) : B.toCone.Ï€.app âŸ¨jâŸ© = B.Ï€ j := rfl

@[simp]
theorem toCone_proj (B : Bicone F) (j : J) : Fan.proj B.toCone j = B.Ï€ j := rfl

/-- Extract the cocone from a bicone. -/
def toCoconeFunctor : Bicone F â¥¤ Cocone (Discrete.functor F) where
  obj B := { pt := B.pt, Î¹ := { app := fun j => B.Î¹ j.as } }
  map {_ _} F := { hom := F.hom, w := fun _ => F.wÎ¹ _ }

/-- A shorthand for `toCoconeFunctor.obj` -/
abbrev toCocone (B : Bicone F) : Cocone (Discrete.functor F) := toCoconeFunctor.obj B

@[simp]
theorem toCocone_pt (B : Bicone F) : B.toCocone.pt = B.pt := rfl

@[simp]
theorem toCocone_Î¹_app (B : Bicone F) (j : Discrete J) : B.toCocone.Î¹.app j = B.Î¹ j.as := rfl

@[simp]
theorem toCocone_inj (B : Bicone F) (j : J) : Cofan.inj B.toCocone j = B.Î¹ j := rfl

theorem toCocone_Î¹_app_mk (B : Bicone F) (j : J) : B.toCocone.Î¹.app âŸ¨jâŸ© = B.Î¹ j := rfl

/-- We can turn any limit cone over a discrete collection of objects into a bicone. -/
@[simps]
def ofLimitCone {f : J â†’ C} {t : Cone (Discrete.functor f)} (ht : IsLimit t) : Bicone f where
  pt := t.pt
  Ï€ j := t.Ï€.app âŸ¨jâŸ©
  Î¹ j := ht.lift (Fan.mk _ fun j' => if h : j = j' then eqToHom (congr_arg f h) else 0)
  Î¹_Ï€ j j' := by simp

theorem Î¹_of_isLimit {f : J â†’ C} {t : Bicone f} (ht : IsLimit t.toCone) (j : J) :
    t.Î¹ j = ht.lift (Fan.mk _ fun j' => if h : j = j' then eqToHom (congr_arg f h) else 0) :=
  ht.hom_ext fun j' => by
    rw [ht.fac]
    simp [t.Î¹_Ï€]

/-- We can turn any colimit cocone over a discrete collection of objects into a bicone. -/
@[simps]
def ofColimitCocone {f : J â†’ C} {t : Cocone (Discrete.functor f)} (ht : IsColimit t) :
    Bicone f where
  pt := t.pt
  Ï€ j := ht.desc (Cofan.mk _ fun j' => if h : j' = j then eqToHom (congr_arg f h) else 0)
  Î¹ j := t.Î¹.app âŸ¨jâŸ©
  Î¹_Ï€ j j' := by simp

theorem Ï€_of_isColimit {f : J â†’ C} {t : Bicone f} (ht : IsColimit t.toCocone) (j : J) :
    t.Ï€ j = ht.desc (Cofan.mk _ fun j' => if h : j' = j then eqToHom (congr_arg f h) else 0) :=
  ht.hom_ext fun j' => by
    rw [ht.fac]
    simp [t.Î¹_Ï€]

/-- Structure witnessing that a bicone is both a limit cone and a colimit cocone. -/
-- @[nolint has_nonempty_instance] Porting note (https://github.com/leanprover-community/mathlib4/issues/5171): removed
structure IsBilimit {F : J â†’ C} (B : Bicone F) where
  isLimit : IsLimit B.toCone
  isColimit : IsColimit B.toCocone


attribute [inherit_doc IsBilimit] IsBilimit.isLimit IsBilimit.isColimit

attribute [simp] IsBilimit.mk.injEq

attribute [local ext] Bicone.IsBilimit

instance subsingleton_isBilimit {f : J â†’ C} {c : Bicone f} : Subsingleton c.IsBilimit :=
  âŸ¨fun _ _ => Bicone.IsBilimit.ext (Subsingleton.elim _ _) (Subsingleton.elim _ _)âŸ©

section Whisker

variable {K : Type w'}

/-- Whisker a bicone with an equivalence between the indexing types. -/
@[simps]
def whisker {f : J â†’ C} (c : Bicone f) (g : K â‰ƒ J) : Bicone (f âˆ˜ g) where
  pt := c.pt
  Ï€ k := c.Ï€ (g k)
  Î¹ k := c.Î¹ (g k)
  Î¹_Ï€ k k' := by
    simp only [c.Î¹_Ï€]
    split_ifs with h h' h' <;> simp [Equiv.apply_eq_iff_eq g] at h h' <;> tauto

/-- Taking the cone of a whiskered bicone results in a cone isomorphic to one gained
by whiskering the cone and postcomposing with a suitable isomorphism. -/
def whiskerToCone {f : J â†’ C} (c : Bicone f) (g : K â‰ƒ J) :
    (c.whisker g).toCone â‰…
      (Cones.postcompose (Discrete.functorComp f g).inv).obj
        (c.toCone.whisker (Discrete.functor (Discrete.mk âˆ˜ g))) :=
  Cones.ext (Iso.refl _) (by aesop_cat)

/-- Taking the cocone of a whiskered bicone results in a cone isomorphic to one gained
by whiskering the cocone and precomposing with a suitable isomorphism. -/
def whiskerToCocone {f : J â†’ C} (c : Bicone f) (g : K â‰ƒ J) :
    (c.whisker g).toCocone â‰…
      (Cocones.precompose (Discrete.functorComp f g).hom).obj
        (c.toCocone.whisker (Discrete.functor (Discrete.mk âˆ˜ g))) :=
  Cocones.ext (Iso.refl _) (by aesop_cat)

/-- Whiskering a bicone with an equivalence between types preserves being a bilimit bicone. -/
def whiskerIsBilimitIff {f : J â†’ C} (c : Bicone f) (g : K â‰ƒ J) :
    (c.whisker g).IsBilimit â‰ƒ c.IsBilimit := by
  refine equivOfSubsingletonOfSubsingleton (fun hc => âŸ¨?_, ?_âŸ©) fun hc => âŸ¨?_, ?_âŸ©
  Â· let this := IsLimit.ofIsoLimit hc.isLimit (Bicone.whiskerToCone c g)
    let this := (IsLimit.postcomposeHomEquiv (Discrete.functorComp f g).symm _) this
    exact IsLimit.ofWhiskerEquivalence (Discrete.equivalence g) this
  Â· let this := IsColimit.ofIsoColimit hc.isColimit (Bicone.whiskerToCocone c g)
    let this := (IsColimit.precomposeHomEquiv (Discrete.functorComp f g) _) this
    exact IsColimit.ofWhiskerEquivalence (Discrete.equivalence g) this
  Â· apply IsLimit.ofIsoLimit _ (Bicone.whiskerToCone c g).symm
    apply (IsLimit.postcomposeHomEquiv (Discrete.functorComp f g).symm _).symm _
    exact IsLimit.whiskerEquivalence hc.isLimit (Discrete.equivalence g)
  Â· apply IsColimit.ofIsoColimit _ (Bicone.whiskerToCocone c g).symm
    apply (IsColimit.precomposeHomEquiv (Discrete.functorComp f g) _).symm _
    exact IsColimit.whiskerEquivalence hc.isColimit (Discrete.equivalence g)

end Whisker

end Bicone

/-- A bicone over `F : J â†’ C`, which is both a limit cone and a colimit cocone.
-/
-- @[nolint has_nonempty_instance] -- Porting note (https://github.com/leanprover-community/mathlib4/issues/5171): removed; linter not ported yet
structure LimitBicone (F : J â†’ C) where
  bicone : Bicone F
  isBilimit : bicone.IsBilimit

attribute [inherit_doc LimitBicone] LimitBicone.bicone LimitBicone.isBilimit

/-- `HasBiproduct F` expresses the mere existence of a bicone which is
simultaneously a limit and a colimit of the diagram `F`.
-/
class HasBiproduct (F : J â†’ C) : Prop where mk' ::
  exists_biproduct : Nonempty (LimitBicone F)

attribute [inherit_doc HasBiproduct] HasBiproduct.exists_biproduct

theorem HasBiproduct.mk {F : J â†’ C} (d : LimitBicone F) : HasBiproduct F :=
  âŸ¨Nonempty.intro dâŸ©

/-- Use the axiom of choice to extract explicit `BiproductData F` from `HasBiproduct F`. -/
def getBiproductData (F : J â†’ C) [HasBiproduct F] : LimitBicone F :=
  Classical.choice HasBiproduct.exists_biproduct

/-- A bicone for `F` which is both a limit cone and a colimit cocone. -/
def biproduct.bicone (F : J â†’ C) [HasBiproduct F] : Bicone F :=
  (getBiproductData F).bicone

/-- `biproduct.bicone F` is a bilimit bicone. -/
def biproduct.isBilimit (F : J â†’ C) [HasBiproduct F] : (biproduct.bicone F).IsBilimit :=
  (getBiproductData F).isBilimit

/-- `biproduct.bicone F` is a limit cone. -/
def biproduct.isLimit (F : J â†’ C) [HasBiproduct F] : IsLimit (biproduct.bicone F).toCone :=
  (getBiproductData F).isBilimit.isLimit

/-- `biproduct.bicone F` is a colimit cocone. -/
def biproduct.isColimit (F : J â†’ C) [HasBiproduct F] : IsColimit (biproduct.bicone F).toCocone :=
  (getBiproductData F).isBilimit.isColimit

instance (priority := 100) hasProduct_of_hasBiproduct [HasBiproduct F] : HasProduct F :=
  HasLimit.mk
    { cone := (biproduct.bicone F).toCone
      isLimit := biproduct.isLimit F }

instance (priority := 100) hasCoproduct_of_hasBiproduct [HasBiproduct F] : HasCoproduct F :=
  HasColimit.mk
    { cocone := (biproduct.bicone F).toCocone
      isColimit := biproduct.isColimit F }

variable (J C)

/-- `C` has biproducts of shape `J` if we have
a limit and a colimit, with the same cone points,
of every function `F : J â†’ C`.
-/
class HasBiproductsOfShape : Prop where
  has_biproduct : âˆ€ F : J â†’ C, HasBiproduct F

attribute [instance 100] HasBiproductsOfShape.has_biproduct

/-- `HasFiniteBiproducts C` represents a choice of biproduct for every family of objects in `C`
indexed by a finite type. -/
class HasFiniteBiproducts : Prop where
  out : âˆ€ n, HasBiproductsOfShape (Fin n) C

attribute [inherit_doc HasFiniteBiproducts] HasFiniteBiproducts.out

variable {J}

theorem hasBiproductsOfShape_of_equiv {K : Type w'} [HasBiproductsOfShape K C] (e : J â‰ƒ K) :
    HasBiproductsOfShape J C :=
  âŸ¨fun F =>
    let âŸ¨âŸ¨hâŸ©âŸ© := HasBiproductsOfShape.has_biproduct (F âˆ˜ e.symm)
    let âŸ¨c, hcâŸ© := h
    HasBiproduct.mk <| by
      simpa only [Function.comp_def, e.symm_apply_apply] using
        LimitBicone.mk (c.whisker e) ((c.whiskerIsBilimitIff _).2 hc)âŸ©

instance (priority := 100) hasBiproductsOfShape_finite [HasFiniteBiproducts C] [Finite J] :
    HasBiproductsOfShape J C := by
  rcases Finite.exists_equiv_fin J with âŸ¨n, âŸ¨eâŸ©âŸ©
  haveI : HasBiproductsOfShape (Fin n) C := HasFiniteBiproducts.out n
  exact hasBiproductsOfShape_of_equiv C e

instance (priority := 100) hasFiniteProducts_of_hasFiniteBiproducts [HasFiniteBiproducts C] :
    HasFiniteProducts C where
  out _ := âŸ¨fun _ => hasLimitOfIso Discrete.natIsoFunctor.symmâŸ©

instance (priority := 100) hasFiniteCoproducts_of_hasFiniteBiproducts [HasFiniteBiproducts C] :
    HasFiniteCoproducts C where
  out _ := âŸ¨fun _ => hasColimitOfIso Discrete.natIsoFunctorâŸ©

instance (priority := 100) hasProductsOfShape_of_hasBiproductsOfShape [HasBiproductsOfShape J C] :
    HasProductsOfShape J C where
  has_limit _ := hasLimitOfIso Discrete.natIsoFunctor.symm

instance (priority := 100) hasCoproductsOfShape_of_hasBiproductsOfShape [HasBiproductsOfShape J C] :
    HasCoproductsOfShape J C where
  has_colimit _ := hasColimitOfIso Discrete.natIsoFunctor

variable {C}

/-- The isomorphism between the specified limit and the specified colimit for
a functor with a bilimit.
-/
def biproductIso (F : J â†’ C) [HasBiproduct F] : Limits.piObj F â‰… Limits.sigmaObj F :=
  (IsLimit.conePointUniqueUpToIso (limit.isLimit _) (biproduct.isLimit F)).trans <|
    IsColimit.coconePointUniqueUpToIso (biproduct.isColimit F) (colimit.isColimit _)

end Limits

namespace Limits

variable {J : Type w} {K : Type*}
variable {C : Type u} [Category.{v} C] [HasZeroMorphisms C]

/-- `biproduct f` computes the biproduct of a family of elements `f`. (It is defined as an
   abbreviation for `limit (Discrete.functor f)`, so for most facts about `biproduct f`, you will
   just use general facts about limits and colimits.) -/
abbrev biproduct (f : J â†’ C) [HasBiproduct f] : C :=
  (biproduct.bicone f).pt

@[inherit_doc biproduct]
notation "â¨ " f:20 => biproduct f

/-- The projection onto a summand of a biproduct. -/
abbrev biproduct.Ï€ (f : J â†’ C) [HasBiproduct f] (b : J) : â¨ f âŸ¶ f b :=
  (biproduct.bicone f).Ï€ b

@[simp]
theorem biproduct.bicone_Ï€ (f : J â†’ C) [HasBiproduct f] (b : J) :
    (biproduct.bicone f).Ï€ b = biproduct.Ï€ f b := rfl

/-- The inclusion into a summand of a biproduct. -/
abbrev biproduct.Î¹ (f : J â†’ C) [HasBiproduct f] (b : J) : f b âŸ¶ â¨ f :=
  (biproduct.bicone f).Î¹ b

@[simp]
theorem biproduct.bicone_Î¹ (f : J â†’ C) [HasBiproduct f] (b : J) :
    (biproduct.bicone f).Î¹ b = biproduct.Î¹ f b := rfl

/-- Note that as this lemma has an `if` in the statement, we include a `DecidableEq` argument.
This means you may not be able to `simp` using this lemma unless you `open scoped Classical`. -/
@[reassoc]
theorem biproduct.Î¹_Ï€ [DecidableEq J] (f : J â†’ C) [HasBiproduct f] (j j' : J) :
    biproduct.Î¹ f j â‰« biproduct.Ï€ f j' = if h : j = j' then eqToHom (congr_arg f h) else 0 := by
  convert (biproduct.bicone f).Î¹_Ï€ j j'

@[reassoc] -- Porting note: both versions proven by simp
theorem biproduct.Î¹_Ï€_self (f : J â†’ C) [HasBiproduct f] (j : J) :
    biproduct.Î¹ f j â‰« biproduct.Ï€ f j = ğŸ™ _ := by simp [biproduct.Î¹_Ï€]

@[reassoc (attr := simp)]
theorem biproduct.Î¹_Ï€_ne (f : J â†’ C) [HasBiproduct f] {j j' : J} (h : j â‰  j') :
    biproduct.Î¹ f j â‰« biproduct.Ï€ f j' = 0 := by simp [biproduct.Î¹_Ï€, h]

-- The `simpNF` linter incorrectly identifies these as simp lemmas that could never apply.
-- https://github.com/leanprover-community/mathlib4/issues/5049
-- They are used by `simp` in `biproduct.whiskerEquiv` below.
@[reassoc (attr := simp, nolint simpNF)]
theorem biproduct.eqToHom_comp_Î¹ (f : J â†’ C) [HasBiproduct f] {j j' : J} (w : j = j') :
    eqToHom (by simp [w]) â‰« biproduct.Î¹ f j' = biproduct.Î¹ f j := by
  cases w
  simp

-- The `simpNF` linter incorrectly identifies these as simp lemmas that could never apply.
-- https://github.com/leanprover-community/mathlib4/issues/5049
-- They are used by `simp` in `biproduct.whiskerEquiv` below.
@[reassoc (attr := simp, nolint simpNF)]
theorem biproduct.Ï€_comp_eqToHom (f : J â†’ C) [HasBiproduct f] {j j' : J} (w : j = j') :
    biproduct.Ï€ f j â‰« eqToHom (by simp [w]) = biproduct.Ï€ f j' := by
  cases w
  simp

/-- Given a collection of maps into the summands, we obtain a map into the biproduct. -/
abbrev biproduct.lift {f : J â†’ C} [HasBiproduct f] {P : C} (p : âˆ€ b, P âŸ¶ f b) : P âŸ¶ â¨ f :=
  (biproduct.isLimit f).lift (Fan.mk P p)

/-- Given a collection of maps out of the summands, we obtain a map out of the biproduct. -/
abbrev biproduct.desc {f : J â†’ C} [HasBiproduct f] {P : C} (p : âˆ€ b, f b âŸ¶ P) : â¨ f âŸ¶ P :=
  (biproduct.isColimit f).desc (Cofan.mk P p)

@[reassoc (attr := simp)]
theorem biproduct.lift_Ï€ {f : J â†’ C} [HasBiproduct f] {P : C} (p : âˆ€ b, P âŸ¶ f b) (j : J) :
    biproduct.lift p â‰« biproduct.Ï€ f j = p j := (biproduct.isLimit f).fac _ âŸ¨jâŸ©

@[reassoc (attr := simp)]
theorem biproduct.Î¹_desc {f : J â†’ C} [HasBiproduct f] {P : C} (p : âˆ€ b, f b âŸ¶ P) (j : J) :
    biproduct.Î¹ f j â‰« biproduct.desc p = p j := (biproduct.isColimit f).fac _ âŸ¨jâŸ©

/-- Given a collection of maps between corresponding summands of a pair of biproducts
indexed by the same type, we obtain a map between the biproducts. -/
abbrev biproduct.map {f g : J â†’ C} [HasBiproduct f] [HasBiproduct g] (p : âˆ€ b, f b âŸ¶ g b) :
    â¨ f âŸ¶ â¨ g :=
  IsLimit.map (biproduct.bicone f).toCone (biproduct.isLimit g)
    (Discrete.natTrans (fun j => p j.as))

/-- An alternative to `biproduct.map` constructed via colimits.
This construction only exists in order to show it is equal to `biproduct.map`. -/
abbrev biproduct.map' {f g : J â†’ C} [HasBiproduct f] [HasBiproduct g] (p : âˆ€ b, f b âŸ¶ g b) :
    â¨ f âŸ¶ â¨ g :=
  IsColimit.map (biproduct.isColimit f) (biproduct.bicone g).toCocone
    (Discrete.natTrans fun j => p j.as)

-- We put this at slightly higher priority than `biproduct.hom_ext'`,
-- to get the matrix indices in the "right" order.
@[ext 1001]
theorem biproduct.hom_ext {f : J â†’ C} [HasBiproduct f] {Z : C} (g h : Z âŸ¶ â¨ f)
    (w : âˆ€ j, g â‰« biproduct.Ï€ f j = h â‰« biproduct.Ï€ f j) : g = h :=
  (biproduct.isLimit f).hom_ext fun j => w j.as

@[ext]
theorem biproduct.hom_ext' {f : J â†’ C} [HasBiproduct f] {Z : C} (g h : â¨ f âŸ¶ Z)
    (w : âˆ€ j, biproduct.Î¹ f j â‰« g = biproduct.Î¹ f j â‰« h) : g = h :=
  (biproduct.isColimit f).hom_ext fun j => w j.as

/-- The canonical isomorphism between the chosen biproduct and the chosen product. -/
def biproduct.isoProduct (f : J â†’ C) [HasBiproduct f] : â¨ f â‰… âˆá¶œ f :=
  IsLimit.conePointUniqueUpToIso (biproduct.isLimit f) (limit.isLimit _)

@[simp]
theorem biproduct.isoProduct_hom {f : J â†’ C} [HasBiproduct f] :
    (biproduct.isoProduct f).hom = Pi.lift (biproduct.Ï€ f) :=
  limit.hom_ext fun j => by simp [biproduct.isoProduct]

@[simp]
theorem biproduct.isoProduct_inv {f : J â†’ C} [HasBiproduct f] :
    (biproduct.isoProduct f).inv = biproduct.lift (Pi.Ï€ f) :=
  biproduct.hom_ext _ _ fun j => by simp [Iso.inv_comp_eq]

/-- The canonical isomorphism between the chosen biproduct and the chosen coproduct. -/
def biproduct.isoCoproduct (f : J â†’ C) [HasBiproduct f] : â¨ f â‰… âˆ f :=
  IsColimit.coconePointUniqueUpToIso (biproduct.isColimit f) (colimit.isColimit _)

@[simp]
theorem biproduct.isoCoproduct_inv {f : J â†’ C} [HasBiproduct f] :
    (biproduct.isoCoproduct f).inv = Sigma.desc (biproduct.Î¹ f) :=
  colimit.hom_ext fun j => by simp [biproduct.isoCoproduct]

@[simp]
theorem biproduct.isoCoproduct_hom {f : J â†’ C} [HasBiproduct f] :
    (biproduct.isoCoproduct f).hom = biproduct.desc (Sigma.Î¹ f) :=
  biproduct.hom_ext' _ _ fun j => by simp [â† Iso.eq_comp_inv]

/-- If a category has biproducts of a shape `J`, its `colim` and `lim` functor on diagrams over `J`
are isomorphic. -/
@[simps!]
def HasBiproductsOfShape.colimIsoLim [HasBiproductsOfShape J C] :
    colim (J := Discrete J) (C := C) â‰… lim :=
  NatIso.ofComponents (fun F => (Sigma.isoColimit F).symm â‰ªâ‰«
      (biproduct.isoCoproduct _).symm â‰ªâ‰« biproduct.isoProduct _ â‰ªâ‰« Pi.isoLimit F)
    fun Î· => colimit.hom_ext fun âŸ¨iâŸ© => limit.hom_ext fun âŸ¨jâŸ© => by
      by_cases h : i = j <;>
       simp_all [h, Sigma.isoColimit, Pi.isoLimit, biproduct.Î¹_Ï€, biproduct.Î¹_Ï€_assoc]

theorem biproduct.map_eq_map' {f g : J â†’ C} [HasBiproduct f] [HasBiproduct g] (p : âˆ€ b, f b âŸ¶ g b) :
    biproduct.map p = biproduct.map' p := by
  ext
  dsimp
  simp only [Discrete.natTrans_app, Limits.IsColimit.Î¹_map_assoc, Limits.IsLimit.map_Ï€,
    Category.assoc, â† Bicone.toCone_Ï€_app_mk, â† biproduct.bicone_Ï€, â† Bicone.toCocone_Î¹_app_mk,
    â† biproduct.bicone_Î¹]
  dsimp
  rw [biproduct.Î¹_Ï€_assoc, biproduct.Î¹_Ï€]
  split_ifs with h
  Â· subst h; rw [eqToHom_refl, Category.id_comp]; erw [Category.comp_id]
  Â· simp

@[reassoc (attr := simp)]
theorem biproduct.map_Ï€ {f g : J â†’ C} [HasBiproduct f] [HasBiproduct g] (p : âˆ€ j, f j âŸ¶ g j)
    (j : J) : biproduct.map p â‰« biproduct.Ï€ g j = biproduct.Ï€ f j â‰« p j :=
  Limits.IsLimit.map_Ï€ _ _ _ (Discrete.mk j)

@[reassoc (attr := simp)]
theorem biproduct.Î¹_map {f g : J â†’ C} [HasBiproduct f] [HasBiproduct g] (p : âˆ€ j, f j âŸ¶ g j)
    (j : J) : biproduct.Î¹ f j â‰« biproduct.map p = p j â‰« biproduct.Î¹ g j := by
  rw [biproduct.map_eq_map']
  apply
    Limits.IsColimit.Î¹_map (biproduct.isColimit f) (biproduct.bicone g).toCocone
    (Discrete.natTrans fun j => p j.as) (Discrete.mk j)

@[reassoc (attr := simp)]
theorem biproduct.map_desc {f g : J â†’ C} [HasBiproduct f] [HasBiproduct g] (p : âˆ€ j, f j âŸ¶ g j)
    {P : C} (k : âˆ€ j, g j âŸ¶ P) :
    biproduct.map p â‰« biproduct.desc k = biproduct.desc fun j => p j â‰« k j := by
  ext; simp

@[reassoc (attr := simp)]
theorem biproduct.lift_map {f g : J â†’ C} [HasBiproduct f] [HasBiproduct g] {P : C}
    (k : âˆ€ j, P âŸ¶ f j) (p : âˆ€ j, f j âŸ¶ g j) :
    biproduct.lift k â‰« biproduct.map p = biproduct.lift fun j => k j â‰« p j := by
  ext; simp

/-- Given a collection of isomorphisms between corresponding summands of a pair of biproducts
indexed by the same type, we obtain an isomorphism between the biproducts. -/
@[simps]
def biproduct.mapIso {f g : J â†’ C} [HasBiproduct f] [HasBiproduct g] (p : âˆ€ b, f b â‰… g b) :
    â¨ f â‰… â¨ g where
  hom := biproduct.map fun b => (p b).hom
  inv := biproduct.map fun b => (p b).inv

instance biproduct.map_epi {f g : J â†’ C} [HasBiproduct f] [HasBiproduct g] (p : âˆ€ j, f j âŸ¶ g j)
    [âˆ€ j, Epi (p j)] : Epi (biproduct.map p) := by
  have : biproduct.map p =
      (biproduct.isoCoproduct _).hom â‰« Sigma.map p â‰« (biproduct.isoCoproduct _).inv := by
    ext
    simp only [map_Ï€, isoCoproduct_hom, isoCoproduct_inv, Category.assoc, Î¹_desc_assoc,
      Î¹_colimMap_assoc, Discrete.functor_obj_eq_as, Discrete.natTrans_app, colimit.Î¹_desc_assoc,
      Cofan.mk_pt, Cofan.mk_Î¹_app, Î¹_Ï€, Î¹_Ï€_assoc]
    split
    all_goals aesop
  rw [this]
  infer_instance

instance Pi.map_epi {f g : J â†’ C} [HasBiproduct f] [HasBiproduct g] (p : âˆ€ j, f j âŸ¶ g j)
    [âˆ€ j, Epi (p j)] : Epi (Pi.map p) := by
  rw [show Pi.map p = (biproduct.isoProduct _).inv â‰« biproduct.map p â‰«
    (biproduct.isoProduct _).hom by aesop]
  infer_instance

instance biproduct.map_mono {f g : J â†’ C} [HasBiproduct f] [HasBiproduct g] (p : âˆ€ j, f j âŸ¶ g j)
    [âˆ€ j, Mono (p j)] : Mono (biproduct.map p) := by
  rw [show biproduct.map p = (biproduct.isoProduct _).hom â‰« Pi.map p â‰«
    (biproduct.isoProduct _).inv by aesop]
  infer_instance

instance Sigma.map_mono {f g : J â†’ C} [HasBiproduct f] [HasBiproduct g] (p : âˆ€ j, f j âŸ¶ g j)
    [âˆ€ j, Mono (p j)] : Mono (Sigma.map p) := by
  rw [show Sigma.map p = (biproduct.isoCoproduct _).inv â‰« biproduct.map p â‰«
    (biproduct.isoCoproduct _).hom by aesop]
  infer_instance

/-- Two biproducts which differ by an equivalence in the indexing type,
and up to isomorphism in the factors, are isomorphic.

Unfortunately there are two natural ways to define each direction of this isomorphism
(because it is true for both products and coproducts separately).
We give the alternative definitions as lemmas below.
-/
@[simps]
def biproduct.whiskerEquiv {f : J â†’ C} {g : K â†’ C} (e : J â‰ƒ K) (w : âˆ€ j, g (e j) â‰… f j)
    [HasBiproduct f] [HasBiproduct g] : â¨ f â‰… â¨ g where
  hom := biproduct.desc fun j => (w j).inv â‰« biproduct.Î¹ g (e j)
  inv := biproduct.desc fun k => eqToHom (by simp) â‰« (w (e.symm k)).hom â‰« biproduct.Î¹ f _

lemma biproduct.whiskerEquiv_hom_eq_lift {f : J â†’ C} {g : K â†’ C} (e : J â‰ƒ K)
    (w : âˆ€ j, g (e j) â‰… f j) [HasBiproduct f] [HasBiproduct g] :
    (biproduct.whiskerEquiv e w).hom =
      biproduct.lift fun k => biproduct.Ï€ f (e.symm k) â‰« (w _).inv â‰« eqToHom (by simp) := by
  simp only [whiskerEquiv_hom]
  ext k j
  by_cases h : k = e j
  Â· subst h
    simp
  Â· simp only [Î¹_desc_assoc, Category.assoc, ne_eq, lift_Ï€]
    rw [biproduct.Î¹_Ï€_ne, biproduct.Î¹_Ï€_ne_assoc]
    Â· simp
    Â· rintro rfl
      simp at h
    Â· exact Ne.symm h

lemma biproduct.whiskerEquiv_inv_eq_lift {f : J â†’ C} {g : K â†’ C} (e : J â‰ƒ K)
    (w : âˆ€ j, g (e j) â‰… f j) [HasBiproduct f] [HasBiproduct g] :
    (biproduct.whiskerEquiv e w).inv =
      biproduct.lift fun j => biproduct.Ï€ g (e j) â‰« (w j).hom := by
  simp only [whiskerEquiv_inv]
  ext j k
  by_cases h : k = e j
  Â· subst h
    simp only [Î¹_desc_assoc, â† eqToHom_iso_hom_naturality_assoc w (e.symm_apply_apply j).symm,
      Equiv.symm_apply_apply, eqToHom_comp_Î¹, Category.assoc, bicone_Î¹_Ï€_self, Category.comp_id,
      lift_Ï€, bicone_Î¹_Ï€_self_assoc]
  Â· simp only [Î¹_desc_assoc, Category.assoc, ne_eq, lift_Ï€]
    rw [biproduct.Î¹_Ï€_ne, biproduct.Î¹_Ï€_ne_assoc]
    Â· simp
    Â· exact h
    Â· rintro rfl
      simp at h

attribute [local simp] Sigma.forall in
instance {Î¹} (f : Î¹ â†’ Type*) (g : (i : Î¹) â†’ (f i) â†’ C)
    [âˆ€ i, HasBiproduct (g i)] [HasBiproduct fun i => â¨ g i] :
    HasBiproduct fun p : Î£ i, f i => g p.1 p.2 where
  exists_biproduct := Nonempty.intro
    { bicone :=
      { pt := â¨ fun i => â¨ g i
        Î¹ := fun X => biproduct.Î¹ (g X.1) X.2 â‰« biproduct.Î¹ (fun i => â¨ g i) X.1
        Ï€ := fun X => biproduct.Ï€ (fun i => â¨ g i) X.1 â‰« biproduct.Ï€ (g X.1) X.2
        Î¹_Ï€ := fun âŸ¨j, xâŸ© âŸ¨j', yâŸ© => by
          split_ifs with h
          Â· obtain âŸ¨rfl, rflâŸ© := h
            simp
          Â· simp only [Sigma.mk.inj_iff, not_and] at h
            by_cases w : j = j'
            Â· cases w
              simp only [heq_eq_eq, forall_true_left] at h
              simp [biproduct.Î¹_Ï€_ne _ h]
            Â· simp [biproduct.Î¹_Ï€_ne_assoc _ w] }
      isBilimit :=
      { isLimit := mkFanLimit _
          (fun s => biproduct.lift fun b => biproduct.lift fun c => s.proj âŸ¨b, câŸ©)
        isColimit := mkCofanColimit _
          (fun s => biproduct.desc fun b => biproduct.desc fun c => s.inj âŸ¨b, câŸ©) } }

/-- An iterated biproduct is a biproduct over a sigma type. -/
@[simps]
def biproductBiproductIso {Î¹} (f : Î¹ â†’ Type*) (g : (i : Î¹) â†’ (f i) â†’ C)
    [âˆ€ i, HasBiproduct (g i)] [HasBiproduct fun i => â¨ g i] :
    (â¨ fun i => â¨ g i) â‰… (â¨ fun p : Î£ i, f i => g p.1 p.2) where
  hom := biproduct.lift fun âŸ¨i, xâŸ© => biproduct.Ï€ _ i â‰« biproduct.Ï€ _ x
  inv := biproduct.lift fun i => biproduct.lift fun x => biproduct.Ï€ _ (âŸ¨i, xâŸ© : Î£ i, f i)

section Ï€Kernel

section

variable (f : J â†’ C) [HasBiproduct f]
variable (p : J â†’ Prop) [HasBiproduct (Subtype.restrict p f)]

/-- The canonical morphism from the biproduct over a restricted index type to the biproduct of
the full index type. -/
def biproduct.fromSubtype : â¨ Subtype.restrict p f âŸ¶ â¨ f :=
  biproduct.desc fun j => biproduct.Î¹ _ j.val

/-- The canonical morphism from a biproduct to the biproduct over a restriction of its index
type. -/
def biproduct.toSubtype : â¨ f âŸ¶ â¨ Subtype.restrict p f :=
  biproduct.lift fun _ => biproduct.Ï€ _ _

@[reassoc (attr := simp)]
theorem biproduct.fromSubtype_Ï€ [DecidablePred p] (j : J) :
    biproduct.fromSubtype f p â‰« biproduct.Ï€ f j =
      if h : p j then biproduct.Ï€ (Subtype.restrict p f) âŸ¨j, hâŸ© else 0 := by
  ext i; dsimp
  rw [biproduct.fromSubtype, biproduct.Î¹_desc_assoc, biproduct.Î¹_Ï€]
  by_cases h : p j
  Â· rw [dif_pos h, biproduct.Î¹_Ï€]
    split_ifs with hâ‚ hâ‚‚ hâ‚‚
    exacts [rfl, False.elim (hâ‚‚ (Subtype.ext hâ‚)), False.elim (hâ‚ (congr_arg Subtype.val hâ‚‚)), rfl]
  Â· rw [dif_neg h, dif_neg (show (i : J) â‰  j from fun hâ‚‚ => h (hâ‚‚ â–¸ i.2)), comp_zero]

theorem biproduct.fromSubtype_eq_lift [DecidablePred p] :
    biproduct.fromSubtype f p =
      biproduct.lift fun j => if h : p j then biproduct.Ï€ (Subtype.restrict p f) âŸ¨j, hâŸ© else 0 :=
  biproduct.hom_ext _ _ (by simp)

@[reassoc] -- Porting note: both version solved using simp
theorem biproduct.fromSubtype_Ï€_subtype (j : Subtype p) :
    biproduct.fromSubtype f p â‰« biproduct.Ï€ f j = biproduct.Ï€ (Subtype.restrict p f) j := by
  ext
  rw [biproduct.fromSubtype, biproduct.Î¹_desc_assoc, biproduct.Î¹_Ï€, biproduct.Î¹_Ï€]
  split_ifs with hâ‚ hâ‚‚ hâ‚‚
  exacts [rfl, False.elim (hâ‚‚ (Subtype.ext hâ‚)), False.elim (hâ‚ (congr_arg Subtype.val hâ‚‚)), rfl]

@[reassoc (attr := simp)]
theorem biproduct.toSubtype_Ï€ (j : Subtype p) :
    biproduct.toSubtype f p â‰« biproduct.Ï€ (Subtype.restrict p f) j = biproduct.Ï€ f j :=
  biproduct.lift_Ï€ _ _

@[reassoc (attr := simp)]
theorem biproduct.Î¹_toSubtype [DecidablePred p] (j : J) :
    biproduct.Î¹ f j â‰« biproduct.toSubtype f p =
      if h : p j then biproduct.Î¹ (Subtype.restrict p f) âŸ¨j, hâŸ© else 0 := by
  ext i
  rw [biproduct.toSubtype, Category.assoc, biproduct.lift_Ï€, biproduct.Î¹_Ï€]
  by_cases h : p j
  Â· rw [dif_pos h, biproduct.Î¹_Ï€]
    split_ifs with hâ‚ hâ‚‚ hâ‚‚
    exacts [rfl, False.elim (hâ‚‚ (Subtype.ext hâ‚)), False.elim (hâ‚ (congr_arg Subtype.val hâ‚‚)), rfl]
  Â· rw [dif_neg h, dif_neg (show j â‰  i from fun hâ‚‚ => h (hâ‚‚.symm â–¸ i.2)), zero_comp]

theorem biproduct.toSubtype_eq_desc [DecidablePred p] :
    biproduct.toSubtype f p =
      biproduct.desc fun j => if h : p j then biproduct.Î¹ (Subtype.restrict p f) âŸ¨j, hâŸ© else 0 :=
  biproduct.hom_ext' _ _ (by simp)

@[reassoc]
theorem biproduct.Î¹_toSubtype_subtype (j : Subtype p) :
    biproduct.Î¹ f j â‰« biproduct.toSubtype f p = biproduct.Î¹ (Subtype.restrict p f) j := by
  ext
  rw [biproduct.toSubtype, Category.assoc, biproduct.lift_Ï€, biproduct.Î¹_Ï€, biproduct.Î¹_Ï€]
  split_ifs with hâ‚ hâ‚‚ hâ‚‚
  exacts [rfl, False.elim (hâ‚‚ (Subtype.ext hâ‚)), False.elim (hâ‚ (congr_arg Subtype.val hâ‚‚)), rfl]

@[reassoc (attr := simp)]
theorem biproduct.Î¹_fromSubtype (j : Subtype p) :
    biproduct.Î¹ (Subtype.restrict p f) j â‰« biproduct.fromSubtype f p = biproduct.Î¹ f j :=
  biproduct.Î¹_desc _ _

@[reassoc (attr := simp)]
theorem biproduct.fromSubtype_toSubtype :
    biproduct.fromSubtype f p â‰« biproduct.toSubtype f p = ğŸ™ (â¨ Subtype.restrict p f) := by
  refine biproduct.hom_ext _ _ fun j => ?_
  rw [Category.assoc, biproduct.toSubtype_Ï€, biproduct.fromSubtype_Ï€_subtype, Category.id_comp]

@[reassoc (attr := simp)]
theorem biproduct.toSubtype_fromSubtype [DecidablePred p] :
    biproduct.toSubtype f p â‰« biproduct.fromSubtype f p =
      biproduct.map fun j => if p j then ğŸ™ (f j) else 0 := by
  ext1 i
  by_cases h : p i
  Â· simp [h]
  Â· simp [h]

end

section

variable (f : J â†’ C) (i : J) [HasBiproduct f] [HasBiproduct (Subtype.restrict (fun j => j â‰  i) f)]

/-- The kernel of `biproduct.Ï€ f i` is the inclusion from the biproduct which omits `i`
from the index set `J` into the biproduct over `J`. -/
def biproduct.isLimitFromSubtype :
    IsLimit (KernelFork.ofÎ¹ (biproduct.fromSubtype f fun j => j â‰  i) (by simp) :
    KernelFork (biproduct.Ï€ f i)) :=
  Fork.IsLimit.mk' _ fun s =>
    âŸ¨s.Î¹ â‰« biproduct.toSubtype _ _, by
      apply biproduct.hom_ext; intro j
      rw [KernelFork.Î¹_ofÎ¹, Category.assoc, Category.assoc,
        biproduct.toSubtype_fromSubtype_assoc, biproduct.map_Ï€]
      rcases Classical.em (i = j) with (rfl | h)
      Â· rw [if_neg (Classical.not_not.2 rfl), comp_zero, comp_zero, KernelFork.condition]
      Â· rw [if_pos (Ne.symm h), Category.comp_id], by
      intro m hm
      rw [â† hm, KernelFork.Î¹_ofÎ¹, Category.assoc, biproduct.fromSubtype_toSubtype]
      exact (Category.comp_id _).symmâŸ©

instance : HasKernel (biproduct.Ï€ f i) :=
  HasLimit.mk âŸ¨_, biproduct.isLimitFromSubtype f iâŸ©

/-- The kernel of `biproduct.Ï€ f i` is `â¨ Subtype.restrict {i}á¶œ f`. -/
@[simps!]
def kernelBiproductÏ€Iso : kernel (biproduct.Ï€ f i) â‰… â¨ Subtype.restrict (fun j => j â‰  i) f :=
  limit.isoLimitCone âŸ¨_, biproduct.isLimitFromSubtype f iâŸ©

/-- The cokernel of `biproduct.Î¹ f i` is the projection from the biproduct over the index set `J`
onto the biproduct omitting `i`. -/
def biproduct.isColimitToSubtype :
    IsColimit (CokernelCofork.ofÏ€ (biproduct.toSubtype f fun j => j â‰  i) (by simp) :
    CokernelCofork (biproduct.Î¹ f i)) :=
  Cofork.IsColimit.mk' _ fun s =>
    âŸ¨biproduct.fromSubtype _ _ â‰« s.Ï€, by
      apply biproduct.hom_ext'; intro j
      rw [CokernelCofork.Ï€_ofÏ€, biproduct.toSubtype_fromSubtype_assoc, biproduct.Î¹_map_assoc]
      rcases Classical.em (i = j) with (rfl | h)
      Â· rw [if_neg (Classical.not_not.2 rfl), zero_comp, CokernelCofork.condition]
      Â· rw [if_pos (Ne.symm h), Category.id_comp], by
      intro m hm
      rw [â† hm, CokernelCofork.Ï€_ofÏ€, â† Category.assoc, biproduct.fromSubtype_toSubtype]
      exact (Category.id_comp _).symmâŸ©

instance : HasCokernel (biproduct.Î¹ f i) :=
  HasColimit.mk âŸ¨_, biproduct.isColimitToSubtype f iâŸ©

/-- The cokernel of `biproduct.Î¹ f i` is `â¨ Subtype.restrict {i}á¶œ f`. -/
@[simps!]
def cokernelBiproductÎ¹Iso : cokernel (biproduct.Î¹ f i) â‰… â¨ Subtype.restrict (fun j => j â‰  i) f :=
  colimit.isoColimitCocone âŸ¨_, biproduct.isColimitToSubtype f iâŸ©

end

section

open scoped Classical

-- Per https://github.com/leanprover-community/mathlib3/pull/15067, we only allow indexing in `Type 0` here.
variable {K : Type} [Finite K] [HasFiniteBiproducts C] (f : K â†’ C)

/-- The limit cone exhibiting `â¨ Subtype.restrict pá¶œ f` as the kernel of
`biproduct.toSubtype f p` -/
@[simps]
def kernelForkBiproductToSubtype (p : Set K) :
    LimitCone (parallelPair (biproduct.toSubtype f p) 0) where
  cone :=
    KernelFork.ofÎ¹ (biproduct.fromSubtype f pá¶œ)
      (by
        ext j k
        simp only [Category.assoc, biproduct.Î¹_fromSubtype_assoc, biproduct.Î¹_toSubtype_assoc,
          comp_zero, zero_comp]
        rw [dif_neg k.2]
        simp only [zero_comp])
  isLimit :=
    KernelFork.IsLimit.ofÎ¹ _ _ (fun {_} g _ => g â‰« biproduct.toSubtype f pá¶œ)
      (by
        intro W' g' w
        ext j
        simp only [Category.assoc, biproduct.toSubtype_fromSubtype, Pi.compl_apply,
          biproduct.map_Ï€]
        split_ifs with h
        Â· simp
        Â· replace w := w =â‰« biproduct.Ï€ _ âŸ¨j, not_not.mp hâŸ©
          simpa using w.symm)
      (by aesop_cat)

instance (p : Set K) : HasKernel (biproduct.toSubtype f p) :=
  HasLimit.mk (kernelForkBiproductToSubtype f p)

/-- The kernel of `biproduct.toSubtype f p` is `â¨ Subtype.restrict pá¶œ f`. -/
@[simps!]
def kernelBiproductToSubtypeIso (p : Set K) :
    kernel (biproduct.toSubtype f p) â‰… â¨ Subtype.restrict pá¶œ f :=
  limit.isoLimitCone (kernelForkBiproductToSubtype f p)

/-- The colimit cocone exhibiting `â¨ Subtype.restrict pá¶œ f` as the cokernel of
`biproduct.fromSubtype f p` -/
@[simps]
def cokernelCoforkBiproductFromSubtype (p : Set K) :
    ColimitCocone (parallelPair (biproduct.fromSubtype f p) 0) where
  cocone :=
    CokernelCofork.ofÏ€ (biproduct.toSubtype f pá¶œ)
      (by
        ext j k
        simp only [Category.assoc, Pi.compl_apply, biproduct.Î¹_fromSubtype_assoc,
          biproduct.Î¹_toSubtype_assoc, comp_zero, zero_comp]
        rw [dif_neg]
        Â· simp only [zero_comp]
        Â· exact not_not.mpr k.2)
  isColimit :=
    CokernelCofork.IsColimit.ofÏ€ _ _ (fun {_} g _ => biproduct.fromSubtype f pá¶œ â‰« g)
      (by
        intro W g' w
        ext j
        simp only [biproduct.toSubtype_fromSubtype_assoc, Pi.compl_apply, biproduct.Î¹_map_assoc]
        split_ifs with h
        Â· simp
        Â· replace w := biproduct.Î¹ _ (âŸ¨j, not_not.mp hâŸ© : p) â‰«= w
          simpa using w.symm)
      (by aesop_cat)

instance (p : Set K) : HasCokernel (biproduct.fromSubtype f p) :=
  HasColimit.mk (cokernelCoforkBiproductFromSubtype f p)

/-- The cokernel of `biproduct.fromSubtype f p` is `â¨ Subtype.restrict pá¶œ f`. -/
@[simps!]
def cokernelBiproductFromSubtypeIso (p : Set K) :
    cokernel (biproduct.fromSubtype f p) â‰… â¨ Subtype.restrict pá¶œ f :=
  colimit.isoColimitCocone (cokernelCoforkBiproductFromSubtype f p)

end

end Ï€Kernel

end Limits

namespace Limits

section FiniteBiproducts

variable {J : Type} [Finite J] {K : Type} [Finite K] {C : Type u} [Category.{v} C]
  [HasZeroMorphisms C] [HasFiniteBiproducts C] {f : J â†’ C} {g : K â†’ C}

/-- Convert a (dependently typed) matrix to a morphism of biproducts.
-/
def biproduct.matrix (m : âˆ€ j k, f j âŸ¶ g k) : â¨ f âŸ¶ â¨ g :=
  biproduct.desc fun j => biproduct.lift fun k => m j k

@[reassoc (attr := simp)]
theorem biproduct.matrix_Ï€ (m : âˆ€ j k, f j âŸ¶ g k) (k : K) :
    biproduct.matrix m â‰« biproduct.Ï€ g k = biproduct.desc fun j => m j k := by
  ext
  simp [biproduct.matrix]

@[reassoc (attr := simp)]
theorem biproduct.Î¹_matrix (m : âˆ€ j k, f j âŸ¶ g k) (j : J) :
    biproduct.Î¹ f j â‰« biproduct.matrix m = biproduct.lift fun k => m j k := by
  ext
  simp [biproduct.matrix]

/-- Extract the matrix components from a morphism of biproducts.
-/
def biproduct.components (m : â¨ f âŸ¶ â¨ g) (j : J) (k : K) : f j âŸ¶ g k :=
  biproduct.Î¹ f j â‰« m â‰« biproduct.Ï€ g k

@[simp]
theorem biproduct.matrix_components (m : âˆ€ j k, f j âŸ¶ g k) (j : J) (k : K) :
    biproduct.components (biproduct.matrix m) j k = m j k := by simp [biproduct.components]

@[simp]
theorem biproduct.components_matrix (m : â¨ f âŸ¶ â¨ g) :
    (biproduct.matrix fun j k => biproduct.components m j k) = m := by
  ext
  simp [biproduct.components]

/-- Morphisms between direct sums are matrices. -/
@[simps]
def biproduct.matrixEquiv : (â¨ f âŸ¶ â¨ g) â‰ƒ âˆ€ j k, f j âŸ¶ g k where
  toFun := biproduct.components
  invFun := biproduct.matrix
  left_inv := biproduct.components_matrix
  right_inv m := by
    ext
    apply biproduct.matrix_components

end FiniteBiproducts

universe uD uD'
variable {J : Type w}
variable {C : Type u} [Category.{v} C] [HasZeroMorphisms C]
variable {D : Type uD} [Category.{uD'} D] [HasZeroMorphisms D]

instance biproduct.Î¹_mono (f : J â†’ C) [HasBiproduct f] (b : J) : IsSplitMono (biproduct.Î¹ f b) :=
  IsSplitMono.mk' { retraction := biproduct.desc <| Pi.single b (ğŸ™ (f b)) }

instance biproduct.Ï€_epi (f : J â†’ C) [HasBiproduct f] (b : J) : IsSplitEpi (biproduct.Ï€ f b) :=
  IsSplitEpi.mk' { section_ := biproduct.lift <| Pi.single b (ğŸ™ (f b)) }

/-- Auxiliary lemma for `biproduct.uniqueUpToIso`. -/
theorem biproduct.conePointUniqueUpToIso_hom (f : J â†’ C) [HasBiproduct f] {b : Bicone f}
    (hb : b.IsBilimit) :
    (hb.isLimit.conePointUniqueUpToIso (biproduct.isLimit _)).hom = biproduct.lift b.Ï€ :=
  rfl

/-- Auxiliary lemma for `biproduct.uniqueUpToIso`. -/
theorem biproduct.conePointUniqueUpToIso_inv (f : J â†’ C) [HasBiproduct f] {b : Bicone f}
    (hb : b.IsBilimit) :
    (hb.isLimit.conePointUniqueUpToIso (biproduct.isLimit _)).inv = biproduct.desc b.Î¹ := by
  refine biproduct.hom_ext' _ _ fun j => hb.isLimit.hom_ext fun j' => ?_
  rw [Category.assoc, IsLimit.conePointUniqueUpToIso_inv_comp, Bicone.toCone_Ï€_app,
    biproduct.bicone_Ï€, biproduct.Î¹_desc, biproduct.Î¹_Ï€, b.toCone_Ï€_app, b.Î¹_Ï€]

/-- Biproducts are unique up to isomorphism. This already follows because bilimits are limits,
    but in the case of biproducts we can give an isomorphism with particularly nice definitional
    properties, namely that `biproduct.lift b.Ï€` and `biproduct.desc b.Î¹` are inverses of each
    other. -/
@[simps]
def biproduct.uniqueUpToIso (f : J â†’ C) [HasBiproduct f] {b : Bicone f} (hb : b.IsBilimit) :
    b.pt â‰… â¨ f where
  hom := biproduct.lift b.Ï€
  inv := biproduct.desc b.Î¹
  hom_inv_id := by
    rw [â† biproduct.conePointUniqueUpToIso_hom f hb, â†
      biproduct.conePointUniqueUpToIso_inv f hb, Iso.hom_inv_id]
  inv_hom_id := by
    rw [â† biproduct.conePointUniqueUpToIso_hom f hb, â†
      biproduct.conePointUniqueUpToIso_inv f hb, Iso.inv_hom_id]

variable (C)

-- see Note [lower instance priority]
/-- A category with finite biproducts has a zero object. -/
instance (priority := 100) hasZeroObject_of_hasFiniteBiproducts [HasFiniteBiproducts C] :
    HasZeroObject C := by
  refine âŸ¨âŸ¨biproduct Empty.elim, fun X => âŸ¨âŸ¨âŸ¨0âŸ©, ?_âŸ©âŸ©, fun X => âŸ¨âŸ¨âŸ¨0âŸ©, ?_âŸ©âŸ©âŸ©âŸ©
  Â· intro a; apply biproduct.hom_ext'; simp
  Â· intro a; apply biproduct.hom_ext; simp

section

variable {C}

attribute [local simp] eq_iff_true_of_subsingleton in
/-- The limit bicone for the biproduct over an index type with exactly one term. -/
@[simps]
def limitBiconeOfUnique [Unique J] (f : J â†’ C) : LimitBicone f where
  bicone :=
    { pt := f default
      Ï€ := fun j => eqToHom (by congr; rw [â† Unique.uniq] )
      Î¹ := fun j => eqToHom (by congr; rw [â† Unique.uniq] ) }
  isBilimit :=
    { isLimit := (limitConeOfUnique f).isLimit
      isColimit := (colimitCoconeOfUnique f).isColimit }

instance (priority := 100) hasBiproduct_unique [Subsingleton J] [Nonempty J] (f : J â†’ C) :
    HasBiproduct f :=
  let âŸ¨_âŸ© := nonempty_unique J; .mk (limitBiconeOfUnique f)

/-- A biproduct over an index type with exactly one term is just the object over that term. -/
@[simps!]
def biproductUniqueIso [Unique J] (f : J â†’ C) : â¨ f â‰… f default :=
  (biproduct.uniqueUpToIso _ (limitBiconeOfUnique f).isBilimit).symm

end

variable {C}

/-- A binary bicone for a pair of objects `P Q : C` consists of the cone point `X`,
maps from `X` to both `P` and `Q`, and maps from both `P` and `Q` to `X`,
so that `inl â‰« fst = ğŸ™ P`, `inl â‰« snd = 0`, `inr â‰« fst = 0`, and `inr â‰« snd = ğŸ™ Q`
-/
-- @[nolint has_nonempty_instance] Porting note (https://github.com/leanprover-community/mathlib4/issues/5171): removed
structure BinaryBicone (P Q : C) where
  pt : C
  fst : pt âŸ¶ P
  snd : pt âŸ¶ Q
  inl : P âŸ¶ pt
  inr : Q âŸ¶ pt
  inl_fst : inl â‰« fst = ğŸ™ P := by aesop
  inl_snd : inl â‰« snd = 0 := by aesop
  inr_fst : inr â‰« fst = 0 := by aesop
  inr_snd : inr â‰« snd = ğŸ™ Q := by aesop

attribute [inherit_doc BinaryBicone] BinaryBicone.pt BinaryBicone.fst BinaryBicone.snd
  BinaryBicone.inl BinaryBicone.inr BinaryBicone.inl_fst BinaryBicone.inl_snd
  BinaryBicone.inr_fst BinaryBicone.inr_snd

attribute [reassoc (attr := simp)]
  BinaryBicone.inl_fst BinaryBicone.inl_snd BinaryBicone.inr_fst BinaryBicone.inr_snd


/-- A binary bicone morphism between two binary bicones for the same diagram is a morphism of the
binary bicone points which commutes with the cone and cocone legs. -/
structure BinaryBiconeMorphism {P Q : C} (A B : BinaryBicone P Q) where
  /-- A morphism between the two vertex objects of the bicones -/
  hom : A.pt âŸ¶ B.pt
  /-- The triangle consisting of the two natural transformations and `hom` commutes -/
  wfst : hom â‰« B.fst = A.fst := by aesop_cat
  /-- The triangle consisting of the two natural transformations and `hom` commutes -/
  wsnd : hom â‰« B.snd = A.snd := by aesop_cat
  /-- The triangle consisting of the two natural transformations and `hom` commutes -/
  winl : A.inl â‰« hom = B.inl := by aesop_cat
  /-- The triangle consisting of the two natural transformations and `hom` commutes -/
  winr : A.inr â‰« hom = B.inr := by aesop_cat


attribute [reassoc (attr := simp)] BinaryBiconeMorphism.wfst BinaryBiconeMorphism.wsnd
attribute [reassoc (attr := simp)] BinaryBiconeMorphism.winl BinaryBiconeMorphism.winr

/-- The category of binary bicones on a given diagram. -/
@[simps]
instance BinaryBicone.category {P Q : C} : Category (BinaryBicone P Q) where
  Hom A B := BinaryBiconeMorphism A B
  comp f g := { hom := f.hom â‰« g.hom }
  id B := { hom := ğŸ™ B.pt }

-- Porting note: if we do not have `simps` automatically generate the lemma for simplifying
-- the `hom` field of a category, we need to write the `ext` lemma in terms of the categorical
-- morphism, rather than the underlying structure.
@[ext]
theorem BinaryBiconeMorphism.ext {P Q : C} {c c' : BinaryBicone P Q}
    (f g : c âŸ¶ c') (w : f.hom = g.hom) : f = g := by
  cases f
  cases g
  congr

namespace BinaryBicones

/-- To give an isomorphism between cocones, it suffices to give an
  isomorphism between their vertices which commutes with the cocone
  maps. -/
@[aesop apply safe (rule_sets := [CategoryTheory]), simps]
def ext {P Q : C} {c c' : BinaryBicone P Q} (Ï† : c.pt â‰… c'.pt)
    (winl : c.inl â‰« Ï†.hom = c'.inl := by aesop_cat)
    (winr : c.inr â‰« Ï†.hom = c'.inr := by aesop_cat)
    (wfst : Ï†.hom â‰« c'.fst = c.fst := by aesop_cat)
    (wsnd : Ï†.hom â‰« c'.snd = c.snd := by aesop_cat) : c â‰… c' where
  hom := { hom := Ï†.hom }
  inv :=
    { hom := Ï†.inv
      wfst := Ï†.inv_comp_eq.mpr wfst.symm
      wsnd := Ï†.inv_comp_eq.mpr wsnd.symm
      winl := Ï†.comp_inv_eq.mpr winl.symm
      winr := Ï†.comp_inv_eq.mpr winr.symm }

variable (P Q : C) (F : C â¥¤ D) [Functor.PreservesZeroMorphisms F]

/-- A functor `F : C â¥¤ D` sends binary bicones for `P` and `Q`
to binary bicones for `G.obj P` and `G.obj Q` functorially. -/
@[simps]
def functoriality : BinaryBicone P Q â¥¤ BinaryBicone (F.obj P) (F.obj Q) where
  obj A :=
    { pt := F.obj A.pt
      fst := F.map A.fst
      snd := F.map A.snd
      inl := F.map A.inl
      inr := F.map A.inr
      inl_fst := by rw [â† F.map_comp, A.inl_fst, F.map_id]
      inl_snd := by rw [â† F.map_comp, A.inl_snd, F.map_zero]
      inr_fst := by rw [â† F.map_comp, A.inr_fst, F.map_zero]
      inr_snd := by rw [â† F.map_comp, A.inr_snd, F.map_id] }
  map f :=
    { hom := F.map f.hom
      wfst := by simp [-BinaryBiconeMorphism.wfst, â† f.wfst]
      wsnd := by simp [-BinaryBiconeMorphism.wsnd, â† f.wsnd]
      winl := by simp [-BinaryBiconeMorphism.winl, â† f.winl]
      winr := by simp [-BinaryBiconeMorphism.winr, â† f.winr] }

instance functoriality_full [F.Full] [F.Faithful] : (functoriality P Q F).Full where
  map_surjective t :=
   âŸ¨{ hom := F.preimage t.hom
      winl := F.map_injective (by simpa using t.winl)
      winr := F.map_injective (by simpa using t.winr)
      wfst := F.map_injective (by simpa using t.wfst)
      wsnd := F.map_injective (by simpa using t.wsnd) }, by aesop_catâŸ©

instance functoriality_faithful [F.Faithful] : (functoriality P Q F).Faithful where
  map_injective {_X} {_Y} f g h :=
    BinaryBiconeMorphism.ext f g <| F.map_injective <| congr_arg BinaryBiconeMorphism.hom h

end BinaryBicones

namespace BinaryBicone

variable {P Q : C}

/-- Extract the cone from a binary bicone. -/
def toCone (c : BinaryBicone P Q) : Cone (pair P Q) :=
  BinaryFan.mk c.fst c.snd

@[simp]
theorem toCone_pt (c : BinaryBicone P Q) : c.toCone.pt = c.pt := rfl

@[simp]
theorem toCone_Ï€_app_left (c : BinaryBicone P Q) : c.toCone.Ï€.app âŸ¨WalkingPair.leftâŸ© = c.fst :=
  rfl

@[simp]
theorem toCone_Ï€_app_right (c : BinaryBicone P Q) : c.toCone.Ï€.app âŸ¨WalkingPair.rightâŸ© = c.snd :=
  rfl

@[simp]
theorem binary_fan_fst_toCone (c : BinaryBicone P Q) : BinaryFan.fst c.toCone = c.fst := rfl

@[simp]
theorem binary_fan_snd_toCone (c : BinaryBicone P Q) : BinaryFan.snd c.toCone = c.snd := rfl

/-- Extract the cocone from a binary bicone. -/
def toCocone (c : BinaryBicone P Q) : Cocone (pair P Q) := BinaryCofan.mk c.inl c.inr

@[simp]
theorem toCocone_pt (c : BinaryBicone P Q) : c.toCocone.pt = c.pt := rfl

@[simp]
theorem toCocone_Î¹_app_left (c : BinaryBicone P Q) : c.toCocone.Î¹.app âŸ¨WalkingPair.leftâŸ© = c.inl :=
  rfl

@[simp]
theorem toCocone_Î¹_app_right (c : BinaryBicone P Q) :
    c.toCocone.Î¹.app âŸ¨WalkingPair.rightâŸ© = c.inr := rfl

@[simp]
theorem binary_cofan_inl_toCocone (c : BinaryBicone P Q) : BinaryCofan.inl c.toCocone = c.inl :=
  rfl

@[simp]
theorem binary_cofan_inr_toCocone (c : BinaryBicone P Q) : BinaryCofan.inr c.toCocone = c.inr :=
  rfl

instance (c : BinaryBicone P Q) : IsSplitMono c.inl :=
  IsSplitMono.mk'
    { retraction := c.fst
      id := c.inl_fst }

instance (c : BinaryBicone P Q) : IsSplitMono c.inr :=
  IsSplitMono.mk'
    { retraction := c.snd
      id := c.inr_snd }

instance (c : BinaryBicone P Q) : IsSplitEpi c.fst :=
  IsSplitEpi.mk'
    { section_ := c.inl
      id := c.inl_fst }

instance (c : BinaryBicone P Q) : IsSplitEpi c.snd :=
  IsSplitEpi.mk'
    { section_ := c.inr
      id := c.inr_snd }

/-- Convert a `BinaryBicone` into a `Bicone` over a pair. -/
@[simps]
def toBiconeFunctor {X Y : C} : BinaryBicone X Y â¥¤ Bicone (pairFunction X Y) where
  obj b :=
    { pt := b.pt
      Ï€ := fun j => WalkingPair.casesOn j b.fst b.snd
      Î¹ := fun j => WalkingPair.casesOn j b.inl b.inr
      Î¹_Ï€ := fun j j' => by
        rcases j with âŸ¨âŸ© <;> rcases j' with âŸ¨âŸ© <;> simp }
  map f := {
    hom := f.hom
    wÏ€ := fun i => WalkingPair.casesOn i f.wfst f.wsnd
    wÎ¹ := fun i => WalkingPair.casesOn i f.winl f.winr }

/-- A shorthand for `toBiconeFunctor.obj` -/
abbrev toBicone {X Y : C} (b : BinaryBicone X Y) : Bicone (pairFunction X Y) :=
  toBiconeFunctor.obj b

/-- A binary bicone is a limit cone if and only if the corresponding bicone is a limit cone. -/
def toBiconeIsLimit {X Y : C} (b : BinaryBicone X Y) :
    IsLimit b.toBicone.toCone â‰ƒ IsLimit b.toCone :=
  IsLimit.equivIsoLimit <|
    Cones.ext (Iso.refl _) fun j => by
      cases' j with as; cases as <;> simp

/-- A binary bicone is a colimit cocone if and only if the corresponding bicone is a colimit
    cocone. -/
def toBiconeIsColimit {X Y : C} (b : BinaryBicone X Y) :
    IsColimit b.toBicone.toCocone â‰ƒ IsColimit b.toCocone :=
  IsColimit.equivIsoColimit <|
    Cocones.ext (Iso.refl _) fun j => by
      cases' j with as; cases as <;> simp

end BinaryBicone

namespace Bicone

/-- Convert a `Bicone` over a function on `WalkingPair` to a BinaryBicone. -/
@[simps]
def toBinaryBiconeFunctor {X Y : C} : Bicone (pairFunction X Y) â¥¤ BinaryBicone X Y where
  obj b :=
    { pt := b.pt
      fst := b.Ï€ WalkingPair.left
      snd := b.Ï€ WalkingPair.right
      inl := b.Î¹ WalkingPair.left
      inr := b.Î¹ WalkingPair.right
      inl_fst := by simp [Bicone.Î¹_Ï€]
      inr_fst := by simp [Bicone.Î¹_Ï€]
      inl_snd := by simp [Bicone.Î¹_Ï€]
      inr_snd := by simp [Bicone.Î¹_Ï€] }
  map f :=
    { hom := f.hom }

/-- A shorthand for `toBinaryBiconeFunctor.obj` -/
abbrev toBinaryBicone {X Y : C} (b : Bicone (pairFunction X Y)) : BinaryBicone X Y :=
  toBinaryBiconeFunctor.obj b

/-- A bicone over a pair is a limit cone if and only if the corresponding binary bicone is a limit
    cone. -/
def toBinaryBiconeIsLimit {X Y : C} (b : Bicone (pairFunction X Y)) :
    IsLimit b.toBinaryBicone.toCone â‰ƒ IsLimit b.toCone :=
  IsLimit.equivIsoLimit <| Cones.ext (Iso.refl _) fun j => by rcases j with âŸ¨âŸ¨âŸ©âŸ© <;> simp

/-- A bicone over a pair is a colimit cocone if and only if the corresponding binary bicone is a
    colimit cocone. -/
def toBinaryBiconeIsColimit {X Y : C} (b : Bicone (pairFunction X Y)) :
    IsColimit b.toBinaryBicone.toCocone â‰ƒ IsColimit b.toCocone :=
  IsColimit.equivIsoColimit <| Cocones.ext (Iso.refl _) fun j => by rcases j with âŸ¨âŸ¨âŸ©âŸ© <;> simp

end Bicone

/-- Structure witnessing that a binary bicone is a limit cone and a limit cocone. -/
-- @[nolint has_nonempty_instance] Porting note (https://github.com/leanprover-community/mathlib4/issues/5171): removed
structure BinaryBicone.IsBilimit {P Q : C} (b : BinaryBicone P Q) where
  isLimit : IsLimit b.toCone
  isColimit : IsColimit b.toCocone

attribute [inherit_doc BinaryBicone.IsBilimit] BinaryBicone.IsBilimit.isLimit
  BinaryBicone.IsBilimit.isColimit

/-- A binary bicone is a bilimit bicone if and only if the corresponding bicone is a bilimit. -/
def BinaryBicone.toBiconeIsBilimit {X Y : C} (b : BinaryBicone X Y) :
    b.toBicone.IsBilimit â‰ƒ b.IsBilimit where
  toFun h := âŸ¨b.toBiconeIsLimit h.isLimit, b.toBiconeIsColimit h.isColimitâŸ©
  invFun h := âŸ¨b.toBiconeIsLimit.symm h.isLimit, b.toBiconeIsColimit.symm h.isColimitâŸ©
  left_inv := fun âŸ¨h, h'âŸ© => by dsimp only; simp
  right_inv := fun âŸ¨h, h'âŸ© => by dsimp only; simp

/-- A bicone over a pair is a bilimit bicone if and only if the corresponding binary bicone is a
    bilimit. -/
def Bicone.toBinaryBiconeIsBilimit {X Y : C} (b : Bicone (pairFunction X Y)) :
    b.toBinaryBicone.IsBilimit â‰ƒ b.IsBilimit where
  toFun h := âŸ¨b.toBinaryBiconeIsLimit h.isLimit, b.toBinaryBiconeIsColimit h.isColimitâŸ©
  invFun h := âŸ¨b.toBinaryBiconeIsLimit.symm h.isLimit, b.toBinaryBiconeIsColimit.symm h.isColimitâŸ©
  left_inv := fun âŸ¨h, h'âŸ© => by dsimp only; simp
  right_inv := fun âŸ¨h, h'âŸ© => by dsimp only; simp

/-- A bicone over `P Q : C`, which is both a limit cone and a colimit cocone.
-/
-- @[nolint has_nonempty_instance] Porting note (https://github.com/leanprover-community/mathlib4/issues/5171): removed
structure BinaryBiproductData (P Q : C) where
  bicone : BinaryBicone P Q
  isBilimit : bicone.IsBilimit

attribute [inherit_doc BinaryBiproductData] BinaryBiproductData.bicone
  BinaryBiproductData.isBilimit

/-- `HasBinaryBiproduct P Q` expresses the mere existence of a bicone which is
simultaneously a limit and a colimit of the diagram `pair P Q`.
-/
class HasBinaryBiproduct (P Q : C) : Prop where mk' ::
  exists_binary_biproduct : Nonempty (BinaryBiproductData P Q)

attribute [inherit_doc HasBinaryBiproduct] HasBinaryBiproduct.exists_binary_biproduct

theorem HasBinaryBiproduct.mk {P Q : C} (d : BinaryBiproductData P Q) : HasBinaryBiproduct P Q :=
  âŸ¨Nonempty.intro dâŸ©

/--
Use the axiom of choice to extract explicit `BinaryBiproductData F` from `HasBinaryBiproduct F`.
-/
def getBinaryBiproductData (P Q : C) [HasBinaryBiproduct P Q] : BinaryBiproductData P Q :=
  Classical.choice HasBinaryBiproduct.exists_binary_biproduct

/-- A bicone for `P Q` which is both a limit cone and a colimit cocone. -/
def BinaryBiproduct.bicone (P Q : C) [HasBinaryBiproduct P Q] : BinaryBicone P Q :=
  (getBinaryBiproductData P Q).bicone

/-- `BinaryBiproduct.bicone P Q` is a limit bicone. -/
def BinaryBiproduct.isBilimit (P Q : C) [HasBinaryBiproduct P Q] :
    (BinaryBiproduct.bicone P Q).IsBilimit :=
  (getBinaryBiproductData P Q).isBilimit

/-- `BinaryBiproduct.bicone P Q` is a limit cone. -/
def BinaryBiproduct.isLimit (P Q : C) [HasBinaryBiproduct P Q] :
    IsLimit (BinaryBiproduct.bicone P Q).toCone :=
  (getBinaryBiproductData P Q).isBilimit.isLimit

/-- `BinaryBiproduct.bicone P Q` is a colimit cocone. -/
def BinaryBiproduct.isColimit (P Q : C) [HasBinaryBiproduct P Q] :
    IsColimit (BinaryBiproduct.bicone P Q).toCocone :=
  (getBinaryBiproductData P Q).isBilimit.isColimit

section

variable (C)

/-- `HasBinaryBiproducts C` represents the existence of a bicone which is
simultaneously a limit and a colimit of the diagram `pair P Q`, for every `P Q : C`.
-/
class HasBinaryBiproducts : Prop where
  has_binary_biproduct : âˆ€ P Q : C, HasBinaryBiproduct P Q

attribute [instance 100] HasBinaryBiproducts.has_binary_biproduct

/-- A category with finite biproducts has binary biproducts.

This is not an instance as typically in concrete categories there will be
an alternative construction with nicer definitional properties.
-/
theorem hasBinaryBiproducts_of_finite_biproducts [HasFiniteBiproducts C] : HasBinaryBiproducts C :=
  { has_binary_biproduct := fun P Q =>
      HasBinaryBiproduct.mk
        { bicone := (biproduct.bicone (pairFunction P Q)).toBinaryBicone
          isBilimit := (Bicone.toBinaryBiconeIsBilimit _).symm (biproduct.isBilimit _) } }

end

variable {P Q : C}

instance HasBinaryBiproduct.hasLimit_pair [HasBinaryBiproduct P Q] : HasLimit (pair P Q) :=
  HasLimit.mk âŸ¨_, BinaryBiproduct.isLimit P QâŸ©

instance HasBinaryBiproduct.hasColimit_pair [HasBinaryBiproduct P Q] : HasColimit (pair P Q) :=
  HasColimit.mk âŸ¨_, BinaryBiproduct.isColimit P QâŸ©

instance (priority := 100) hasBinaryProducts_of_hasBinaryBiproducts [HasBinaryBiproducts C] :
    HasBinaryProducts C where
  has_limit F := hasLimitOfIso (diagramIsoPair F).symm

instance (priority := 100) hasBinaryCoproducts_of_hasBinaryBiproducts [HasBinaryBiproducts C] :
    HasBinaryCoproducts C where
  has_colimit F := hasColimitOfIso (diagramIsoPair F)

/-- The isomorphism between the specified binary product and the specified binary coproduct for
a pair for a binary biproduct.
-/
def biprodIso (X Y : C) [HasBinaryBiproduct X Y] : Limits.prod X Y â‰… Limits.coprod X Y :=
  (IsLimit.conePointUniqueUpToIso (limit.isLimit _) (BinaryBiproduct.isLimit X Y)).trans <|
    IsColimit.coconePointUniqueUpToIso (BinaryBiproduct.isColimit X Y) (colimit.isColimit _)

/-- An arbitrary choice of biproduct of a pair of objects. -/
abbrev biprod (X Y : C) [HasBinaryBiproduct X Y] :=
  (BinaryBiproduct.bicone X Y).pt

@[inherit_doc biprod]
notation:20 X " âŠ " Y:20 => biprod X Y

/-- The projection onto the first summand of a binary biproduct. -/
abbrev biprod.fst {X Y : C} [HasBinaryBiproduct X Y] : X âŠ Y âŸ¶ X :=
  (BinaryBiproduct.bicone X Y).fst

/-- The projection onto the second summand of a binary biproduct. -/
abbrev biprod.snd {X Y : C} [HasBinaryBiproduct X Y] : X âŠ Y âŸ¶ Y :=
  (BinaryBiproduct.bicone X Y).snd

/-- The inclusion into the first summand of a binary biproduct. -/
abbrev biprod.inl {X Y : C} [HasBinaryBiproduct X Y] : X âŸ¶ X âŠ Y :=
  (BinaryBiproduct.bicone X Y).inl

/-- The inclusion into the second summand of a binary biproduct. -/
abbrev biprod.inr {X Y : C} [HasBinaryBiproduct X Y] : Y âŸ¶ X âŠ Y :=
  (BinaryBiproduct.bicone X Y).inr

section

variable {X Y : C} [HasBinaryBiproduct X Y]

@[simp]
theorem BinaryBiproduct.bicone_fst : (BinaryBiproduct.bicone X Y).fst = biprod.fst :=
  rfl

@[simp]
theorem BinaryBiproduct.bicone_snd : (BinaryBiproduct.bicone X Y).snd = biprod.snd :=
  rfl

@[simp]
theorem BinaryBiproduct.bicone_inl : (BinaryBiproduct.bicone X Y).inl = biprod.inl :=
  rfl

@[simp]
theorem BinaryBiproduct.bicone_inr : (BinaryBiproduct.bicone X Y).inr = biprod.inr :=
  rfl

end

@[reassoc] -- Porting note: simp can solve both versions
theorem biprod.inl_fst {X Y : C} [HasBinaryBiproduct X Y] :
    (biprod.inl : X âŸ¶ X âŠ Y) â‰« (biprod.fst : X âŠ Y âŸ¶ X) = ğŸ™ X :=
  (BinaryBiproduct.bicone X Y).inl_fst

@[reassoc] -- Porting note: simp can solve both versions
theorem biprod.inl_snd {X Y : C} [HasBinaryBiproduct X Y] :
    (biprod.inl : X âŸ¶ X âŠ Y) â‰« (biprod.snd : X âŠ Y âŸ¶ Y) = 0 :=
  (BinaryBiproduct.bicone X Y).inl_snd

@[reassoc] -- Porting note: simp can solve both versions
theorem biprod.inr_fst {X Y : C} [HasBinaryBiproduct X Y] :
    (biprod.inr : Y âŸ¶ X âŠ Y) â‰« (biprod.fst : X âŠ Y âŸ¶ X) = 0 :=
  (BinaryBiproduct.bicone X Y).inr_fst

@[reassoc] -- Porting note: simp can solve both versions
theorem biprod.inr_snd {X Y : C} [HasBinaryBiproduct X Y] :
    (biprod.inr : Y âŸ¶ X âŠ Y) â‰« (biprod.snd : X âŠ Y âŸ¶ Y) = ğŸ™ Y :=
  (BinaryBiproduct.bicone X Y).inr_snd

/-- Given a pair of maps into the summands of a binary biproduct,
we obtain a map into the binary biproduct. -/
abbrev biprod.lift {W X Y : C} [HasBinaryBiproduct X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y) : W âŸ¶ X âŠ Y :=
  (BinaryBiproduct.isLimit X Y).lift (BinaryFan.mk f g)

/-- Given a pair of maps out of the summands of a binary biproduct,
we obtain a map out of the binary biproduct. -/
abbrev biprod.desc {W X Y : C} [HasBinaryBiproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W) : X âŠ Y âŸ¶ W :=
  (BinaryBiproduct.isColimit X Y).desc (BinaryCofan.mk f g)

@[reassoc (attr := simp)]
theorem biprod.lift_fst {W X Y : C} [HasBinaryBiproduct X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y) :
    biprod.lift f g â‰« biprod.fst = f :=
  (BinaryBiproduct.isLimit X Y).fac _ âŸ¨WalkingPair.leftâŸ©

@[reassoc (attr := simp)]
theorem biprod.lift_snd {W X Y : C} [HasBinaryBiproduct X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y) :
    biprod.lift f g â‰« biprod.snd = g :=
  (BinaryBiproduct.isLimit X Y).fac _ âŸ¨WalkingPair.rightâŸ©

@[reassoc (attr := simp)]
theorem biprod.inl_desc {W X Y : C} [HasBinaryBiproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W) :
    biprod.inl â‰« biprod.desc f g = f :=
  (BinaryBiproduct.isColimit X Y).fac _ âŸ¨WalkingPair.leftâŸ©

@[reassoc (attr := simp)]
theorem biprod.inr_desc {W X Y : C} [HasBinaryBiproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W) :
    biprod.inr â‰« biprod.desc f g = g :=
  (BinaryBiproduct.isColimit X Y).fac _ âŸ¨WalkingPair.rightâŸ©

instance biprod.mono_lift_of_mono_left {W X Y : C} [HasBinaryBiproduct X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y)
    [Mono f] : Mono (biprod.lift f g) :=
  mono_of_mono_fac <| biprod.lift_fst _ _

instance biprod.mono_lift_of_mono_right {W X Y : C} [HasBinaryBiproduct X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y)
    [Mono g] : Mono (biprod.lift f g) :=
  mono_of_mono_fac <| biprod.lift_snd _ _

instance biprod.epi_desc_of_epi_left {W X Y : C} [HasBinaryBiproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W)
    [Epi f] : Epi (biprod.desc f g) :=
  epi_of_epi_fac <| biprod.inl_desc _ _

instance biprod.epi_desc_of_epi_right {W X Y : C} [HasBinaryBiproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W)
    [Epi g] : Epi (biprod.desc f g) :=
  epi_of_epi_fac <| biprod.inr_desc _ _

/-- Given a pair of maps between the summands of a pair of binary biproducts,
we obtain a map between the binary biproducts. -/
abbrev biprod.map {W X Y Z : C} [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] (f : W âŸ¶ Y)
    (g : X âŸ¶ Z) : W âŠ X âŸ¶ Y âŠ Z :=
  IsLimit.map (BinaryBiproduct.bicone W X).toCone (BinaryBiproduct.isLimit Y Z)
    (@mapPair _ _ (pair W X) (pair Y Z) f g)

/-- An alternative to `biprod.map` constructed via colimits.
This construction only exists in order to show it is equal to `biprod.map`. -/
abbrev biprod.map' {W X Y Z : C} [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] (f : W âŸ¶ Y)
    (g : X âŸ¶ Z) : W âŠ X âŸ¶ Y âŠ Z :=
  IsColimit.map (BinaryBiproduct.isColimit W X) (BinaryBiproduct.bicone Y Z).toCocone
    (@mapPair _ _ (pair W X) (pair Y Z) f g)

@[ext]
theorem biprod.hom_ext {X Y Z : C} [HasBinaryBiproduct X Y] (f g : Z âŸ¶ X âŠ Y)
    (hâ‚€ : f â‰« biprod.fst = g â‰« biprod.fst) (hâ‚ : f â‰« biprod.snd = g â‰« biprod.snd) : f = g :=
  BinaryFan.IsLimit.hom_ext (BinaryBiproduct.isLimit X Y) hâ‚€ hâ‚

@[ext]
theorem biprod.hom_ext' {X Y Z : C} [HasBinaryBiproduct X Y] (f g : X âŠ Y âŸ¶ Z)
    (hâ‚€ : biprod.inl â‰« f = biprod.inl â‰« g) (hâ‚ : biprod.inr â‰« f = biprod.inr â‰« g) : f = g :=
  BinaryCofan.IsColimit.hom_ext (BinaryBiproduct.isColimit X Y) hâ‚€ hâ‚

/-- The canonical isomorphism between the chosen biproduct and the chosen product. -/
def biprod.isoProd (X Y : C) [HasBinaryBiproduct X Y] : X âŠ Y â‰… X â¨¯ Y :=
  IsLimit.conePointUniqueUpToIso (BinaryBiproduct.isLimit X Y) (limit.isLimit _)

@[simp]
theorem biprod.isoProd_hom {X Y : C} [HasBinaryBiproduct X Y] :
    (biprod.isoProd X Y).hom = prod.lift biprod.fst biprod.snd := by
      ext <;> simp [biprod.isoProd]

@[simp]
theorem biprod.isoProd_inv {X Y : C} [HasBinaryBiproduct X Y] :
    (biprod.isoProd X Y).inv = biprod.lift prod.fst prod.snd := by
  ext <;> simp [Iso.inv_comp_eq]

/-- The canonical isomorphism between the chosen biproduct and the chosen coproduct. -/
def biprod.isoCoprod (X Y : C) [HasBinaryBiproduct X Y] : X âŠ Y â‰… X â¨¿ Y :=
  IsColimit.coconePointUniqueUpToIso (BinaryBiproduct.isColimit X Y) (colimit.isColimit _)

@[simp]
theorem biprod.isoCoprod_inv {X Y : C} [HasBinaryBiproduct X Y] :
    (biprod.isoCoprod X Y).inv = coprod.desc biprod.inl biprod.inr := by
  ext <;> simp [biprod.isoCoprod]

@[simp]
theorem biprod_isoCoprod_hom {X Y : C} [HasBinaryBiproduct X Y] :
    (biprod.isoCoprod X Y).hom = biprod.desc coprod.inl coprod.inr := by
  ext <;> simp [â† Iso.eq_comp_inv]

theorem biprod.map_eq_map' {W X Y Z : C} [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z]
    (f : W âŸ¶ Y) (g : X âŸ¶ Z) : biprod.map f g = biprod.map' f g := by
  ext
  Â· simp only [mapPair_left, IsColimit.Î¹_map, IsLimit.map_Ï€, biprod.inl_fst_assoc,
      Category.assoc, â† BinaryBicone.toCone_Ï€_app_left, â† BinaryBiproduct.bicone_fst, â†
      BinaryBicone.toCocone_Î¹_app_left, â† BinaryBiproduct.bicone_inl]
    dsimp; simp
  Â· simp only [mapPair_left, IsColimit.Î¹_map, IsLimit.map_Ï€, zero_comp, biprod.inl_snd_assoc,
      Category.assoc, â† BinaryBicone.toCone_Ï€_app_right, â† BinaryBiproduct.bicone_snd, â†
      BinaryBicone.toCocone_Î¹_app_left, â† BinaryBiproduct.bicone_inl]
    simp
  Â· simp only [mapPair_right, biprod.inr_fst_assoc, IsColimit.Î¹_map, IsLimit.map_Ï€, zero_comp,
      Category.assoc, â† BinaryBicone.toCone_Ï€_app_left, â† BinaryBiproduct.bicone_fst, â†
      BinaryBicone.toCocone_Î¹_app_right, â† BinaryBiproduct.bicone_inr]
    simp
  Â· simp only [mapPair_right, IsColimit.Î¹_map, IsLimit.map_Ï€, biprod.inr_snd_assoc,
      Category.assoc, â† BinaryBicone.toCone_Ï€_app_right, â† BinaryBiproduct.bicone_snd, â†
      BinaryBicone.toCocone_Î¹_app_right, â† BinaryBiproduct.bicone_inr]
    simp

instance biprod.inl_mono {X Y : C} [HasBinaryBiproduct X Y] :
    IsSplitMono (biprod.inl : X âŸ¶ X âŠ Y) :=
  IsSplitMono.mk' { retraction := biprod.fst }

instance biprod.inr_mono {X Y : C} [HasBinaryBiproduct X Y] :
    IsSplitMono (biprod.inr : Y âŸ¶ X âŠ Y) :=
  IsSplitMono.mk' { retraction := biprod.snd }

instance biprod.fst_epi {X Y : C} [HasBinaryBiproduct X Y] : IsSplitEpi (biprod.fst : X âŠ Y âŸ¶ X) :=
  IsSplitEpi.mk' { section_ := biprod.inl }

instance biprod.snd_epi {X Y : C} [HasBinaryBiproduct X Y] : IsSplitEpi (biprod.snd : X âŠ Y âŸ¶ Y) :=
  IsSplitEpi.mk' { section_ := biprod.inr }

@[reassoc (attr := simp)]
theorem biprod.map_fst {W X Y Z : C} [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] (f : W âŸ¶ Y)
    (g : X âŸ¶ Z) : biprod.map f g â‰« biprod.fst = biprod.fst â‰« f :=
  IsLimit.map_Ï€ _ _ _ (âŸ¨WalkingPair.leftâŸ© : Discrete WalkingPair)

@[reassoc (attr := simp)]
theorem biprod.map_snd {W X Y Z : C} [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] (f : W âŸ¶ Y)
    (g : X âŸ¶ Z) : biprod.map f g â‰« biprod.snd = biprod.snd â‰« g :=
  IsLimit.map_Ï€ _ _ _ (âŸ¨WalkingPair.rightâŸ© : Discrete WalkingPair)

-- Because `biprod.map` is defined in terms of `lim` rather than `colim`,
-- we need to provide additional `simp` lemmas.
@[reassoc (attr := simp)]
theorem biprod.inl_map {W X Y Z : C} [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] (f : W âŸ¶ Y)
    (g : X âŸ¶ Z) : biprod.inl â‰« biprod.map f g = f â‰« biprod.inl := by
  rw [biprod.map_eq_map']
  exact IsColimit.Î¹_map (BinaryBiproduct.isColimit W X) _ _ âŸ¨WalkingPair.leftâŸ©

@[reassoc (attr := simp)]
theorem biprod.inr_map {W X Y Z : C} [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] (f : W âŸ¶ Y)
    (g : X âŸ¶ Z) : biprod.inr â‰« biprod.map f g = g â‰« biprod.inr := by
  rw [biprod.map_eq_map']
  exact IsColimit.Î¹_map (BinaryBiproduct.isColimit W X) _ _ âŸ¨WalkingPair.rightâŸ©

/-- Given a pair of isomorphisms between the summands of a pair of binary biproducts,
we obtain an isomorphism between the binary biproducts. -/
@[simps]
def biprod.mapIso {W X Y Z : C} [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] (f : W â‰… Y)
    (g : X â‰… Z) : W âŠ X â‰… Y âŠ Z where
  hom := biprod.map f.hom g.hom
  inv := biprod.map f.inv g.inv

/-- Auxiliary lemma for `biprod.uniqueUpToIso`. -/
theorem biprod.conePointUniqueUpToIso_hom (X Y : C) [HasBinaryBiproduct X Y] {b : BinaryBicone X Y}
    (hb : b.IsBilimit) :
    (hb.isLimit.conePointUniqueUpToIso (BinaryBiproduct.isLimit _ _)).hom =
      biprod.lift b.fst b.snd := rfl

/-- Auxiliary lemma for `biprod.uniqueUpToIso`. -/
theorem biprod.conePointUniqueUpToIso_inv (X Y : C) [HasBinaryBiproduct X Y] {b : BinaryBicone X Y}
    (hb : b.IsBilimit) :
    (hb.isLimit.conePointUniqueUpToIso (BinaryBiproduct.isLimit _ _)).inv =
      biprod.desc b.inl b.inr := by
  refine biprod.hom_ext' _ _ (hb.isLimit.hom_ext fun j => ?_) (hb.isLimit.hom_ext fun j => ?_)
  all_goals
    simp only [Category.assoc, IsLimit.conePointUniqueUpToIso_inv_comp]
    rcases j with âŸ¨âŸ¨âŸ©âŸ©
  all_goals simp

/-- Binary biproducts are unique up to isomorphism. This already follows because bilimits are
    limits, but in the case of biproducts we can give an isomorphism with particularly nice
    definitional properties, namely that `biprod.lift b.fst b.snd` and `biprod.desc b.inl b.inr`
    are inverses of each other. -/
@[simps]
def biprod.uniqueUpToIso (X Y : C) [HasBinaryBiproduct X Y] {b : BinaryBicone X Y}
    (hb : b.IsBilimit) : b.pt â‰… X âŠ Y where
  hom := biprod.lift b.fst b.snd
  inv := biprod.desc b.inl b.inr
  hom_inv_id := by
    rw [â† biprod.conePointUniqueUpToIso_hom X Y hb, â†
      biprod.conePointUniqueUpToIso_inv X Y hb, Iso.hom_inv_id]
  inv_hom_id := by
    rw [â† biprod.conePointUniqueUpToIso_hom X Y hb, â†
      biprod.conePointUniqueUpToIso_inv X Y hb, Iso.inv_hom_id]

-- There are three further variations,
-- about `IsIso biprod.inr`, `IsIso biprod.fst` and `IsIso biprod.snd`,
-- but any one suffices to prove `indecomposable_of_simple`
-- and they are likely not separately useful.
theorem biprod.isIso_inl_iff_id_eq_fst_comp_inl (X Y : C) [HasBinaryBiproduct X Y] :
    IsIso (biprod.inl : X âŸ¶ X âŠ Y) â†” ğŸ™ (X âŠ Y) = biprod.fst â‰« biprod.inl := by
  constructor
  Â· intro h
    have := (cancel_epi (inv biprod.inl : X âŠ Y âŸ¶ X)).2 <| @biprod.inl_fst _ _ _ X Y _
    rw [IsIso.inv_hom_id_assoc, Category.comp_id] at this
    rw [this, IsIso.inv_hom_id]
  Â· intro h
    exact âŸ¨âŸ¨biprod.fst, biprod.inl_fst, h.symmâŸ©âŸ©

instance biprod.map_epi {W X Y Z : C} (f : W âŸ¶ Y) (g : X âŸ¶ Z) [Epi f]
    [Epi g] [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] : Epi (biprod.map f g) := by
  rw [show biprod.map f g =
    (biprod.isoCoprod _ _).hom â‰« coprod.map f g â‰« (biprod.isoCoprod _ _).inv by aesop]
  infer_instance

instance prod.map_epi {W X Y Z : C} (f : W âŸ¶ Y) (g : X âŸ¶ Z) [Epi f]
    [Epi g] [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] : Epi (prod.map f g) := by
  rw [show prod.map f g = (biprod.isoProd _ _).inv â‰« biprod.map f g â‰«
    (biprod.isoProd _ _).hom by aesop]
  infer_instance

instance biprod.map_mono {W X Y Z : C} (f : W âŸ¶ Y) (g : X âŸ¶ Z) [Mono f]
    [Mono g] [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] : Mono (biprod.map f g) := by
  rw [show biprod.map f g = (biprod.isoProd _ _).hom â‰« prod.map f g â‰«
    (biprod.isoProd _ _).inv by aesop]
  infer_instance

instance coprod.map_mono {W X Y Z : C} (f : W âŸ¶ Y) (g : X âŸ¶ Z) [Mono f]
    [Mono g] [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] : Mono (coprod.map f g) := by
  rw [show coprod.map f g = (biprod.isoCoprod _ _).inv â‰« biprod.map f g â‰«
    (biprod.isoCoprod _ _).hom by aesop]
  infer_instance

section BiprodKernel

section BinaryBicone

variable {X Y : C} (c : BinaryBicone X Y)

/-- A kernel fork for the kernel of `BinaryBicone.fst`. It consists of the morphism
`BinaryBicone.inr`. -/
def BinaryBicone.fstKernelFork : KernelFork c.fst :=
  KernelFork.ofÎ¹ c.inr c.inr_fst

@[simp]
theorem BinaryBicone.fstKernelFork_Î¹ : (BinaryBicone.fstKernelFork c).Î¹ = c.inr := rfl

/-- A kernel fork for the kernel of `BinaryBicone.snd`. It consists of the morphism
`BinaryBicone.inl`. -/
def BinaryBicone.sndKernelFork : KernelFork c.snd :=
  KernelFork.ofÎ¹ c.inl c.inl_snd

@[simp]
theorem BinaryBicone.sndKernelFork_Î¹ : (BinaryBicone.sndKernelFork c).Î¹ = c.inl := rfl

/-- A cokernel cofork for the cokernel of `BinaryBicone.inl`. It consists of the morphism
`BinaryBicone.snd`. -/
def BinaryBicone.inlCokernelCofork : CokernelCofork c.inl :=
  CokernelCofork.ofÏ€ c.snd c.inl_snd

@[simp]
theorem BinaryBicone.inlCokernelCofork_Ï€ : (BinaryBicone.inlCokernelCofork c).Ï€ = c.snd := rfl

/-- A cokernel cofork for the cokernel of `BinaryBicone.inr`. It consists of the morphism
`BinaryBicone.fst`. -/
def BinaryBicone.inrCokernelCofork : CokernelCofork c.inr :=
  CokernelCofork.ofÏ€ c.fst c.inr_fst

@[simp]
theorem BinaryBicone.inrCokernelCofork_Ï€ : (BinaryBicone.inrCokernelCofork c).Ï€ = c.fst := rfl

variable {c}

/-- The fork defined in `BinaryBicone.fstKernelFork` is indeed a kernel. -/
def BinaryBicone.isLimitFstKernelFork (i : IsLimit c.toCone) : IsLimit c.fstKernelFork :=
  Fork.IsLimit.mk' _ fun s =>
    âŸ¨s.Î¹ â‰« c.snd, by apply BinaryFan.IsLimit.hom_ext i <;> simp, fun hm => by simp [â† hm]âŸ©

/-- The fork defined in `BinaryBicone.sndKernelFork` is indeed a kernel. -/
def BinaryBicone.isLimitSndKernelFork (i : IsLimit c.toCone) : IsLimit c.sndKernelFork :=
  Fork.IsLimit.mk' _ fun s =>
    âŸ¨s.Î¹ â‰« c.fst, by apply BinaryFan.IsLimit.hom_ext i <;> simp, fun hm => by simp [â† hm]âŸ©

/-- The cofork defined in `BinaryBicone.inlCokernelCofork` is indeed a cokernel. -/
def BinaryBicone.isColimitInlCokernelCofork (i : IsColimit c.toCocone) :
    IsColimit c.inlCokernelCofork :=
  Cofork.IsColimit.mk' _ fun s =>
    âŸ¨c.inr â‰« s.Ï€, by apply BinaryCofan.IsColimit.hom_ext i <;> simp, fun hm => by simp [â† hm]âŸ©

/-- The cofork defined in `BinaryBicone.inrCokernelCofork` is indeed a cokernel. -/
def BinaryBicone.isColimitInrCokernelCofork (i : IsColimit c.toCocone) :
    IsColimit c.inrCokernelCofork :=
  Cofork.IsColimit.mk' _ fun s =>
    âŸ¨c.inl â‰« s.Ï€, by apply BinaryCofan.IsColimit.hom_ext i <;> simp, fun hm => by simp [â† hm]âŸ©

end BinaryBicone

section HasBinaryBiproduct

variable (X Y : C) [HasBinaryBiproduct X Y]

/-- A kernel fork for the kernel of `biprod.fst`. It consists of the
morphism `biprod.inr`. -/
def biprod.fstKernelFork : KernelFork (biprod.fst : X âŠ Y âŸ¶ X) :=
  BinaryBicone.fstKernelFork _

@[simp]
theorem biprod.fstKernelFork_Î¹ : Fork.Î¹ (biprod.fstKernelFork X Y) = (biprod.inr : Y âŸ¶ X âŠ Y) :=
  rfl

/-- The fork `biprod.fstKernelFork` is indeed a limit. -/
def biprod.isKernelFstKernelFork : IsLimit (biprod.fstKernelFork X Y) :=
  BinaryBicone.isLimitFstKernelFork (BinaryBiproduct.isLimit _ _)

/-- A kernel fork for the kernel of `biprod.snd`. It consists of the
morphism `biprod.inl`. -/
def biprod.sndKernelFork : KernelFork (biprod.snd : X âŠ Y âŸ¶ Y) :=
  BinaryBicone.sndKernelFork _

@[simp]
theorem biprod.sndKernelFork_Î¹ : Fork.Î¹ (biprod.sndKernelFork X Y) = (biprod.inl : X âŸ¶ X âŠ Y) :=
  rfl

/-- The fork `biprod.sndKernelFork` is indeed a limit. -/
def biprod.isKernelSndKernelFork : IsLimit (biprod.sndKernelFork X Y) :=
  BinaryBicone.isLimitSndKernelFork (BinaryBiproduct.isLimit _ _)

/-- A cokernel cofork for the cokernel of `biprod.inl`. It consists of the
morphism `biprod.snd`. -/
def biprod.inlCokernelCofork : CokernelCofork (biprod.inl : X âŸ¶ X âŠ Y) :=
  BinaryBicone.inlCokernelCofork _

@[simp]
theorem biprod.inlCokernelCofork_Ï€ : Cofork.Ï€ (biprod.inlCokernelCofork X Y) = biprod.snd :=
  rfl

/-- The cofork `biprod.inlCokernelFork` is indeed a colimit. -/
def biprod.isCokernelInlCokernelFork : IsColimit (biprod.inlCokernelCofork X Y) :=
  BinaryBicone.isColimitInlCokernelCofork (BinaryBiproduct.isColimit _ _)

/-- A cokernel cofork for the cokernel of `biprod.inr`. It consists of the
morphism `biprod.fst`. -/
def biprod.inrCokernelCofork : CokernelCofork (biprod.inr : Y âŸ¶ X âŠ Y) :=
  BinaryBicone.inrCokernelCofork _

@[simp]
theorem biprod.inrCokernelCofork_Ï€ : Cofork.Ï€ (biprod.inrCokernelCofork X Y) = biprod.fst :=
  rfl

/-- The cofork `biprod.inrCokernelFork` is indeed a colimit. -/
def biprod.isCokernelInrCokernelFork : IsColimit (biprod.inrCokernelCofork X Y) :=
  BinaryBicone.isColimitInrCokernelCofork (BinaryBiproduct.isColimit _ _)

end HasBinaryBiproduct

variable {X Y : C} [HasBinaryBiproduct X Y]

instance : HasKernel (biprod.fst : X âŠ Y âŸ¶ X) :=
  HasLimit.mk âŸ¨_, biprod.isKernelFstKernelFork X YâŸ©

/-- The kernel of `biprod.fst : X âŠ Y âŸ¶ X` is `Y`. -/
@[simps!]
def kernelBiprodFstIso : kernel (biprod.fst : X âŠ Y âŸ¶ X) â‰… Y :=
  limit.isoLimitCone âŸ¨_, biprod.isKernelFstKernelFork X YâŸ©

instance : HasKernel (biprod.snd : X âŠ Y âŸ¶ Y) :=
  HasLimit.mk âŸ¨_, biprod.isKernelSndKernelFork X YâŸ©

/-- The kernel of `biprod.snd : X âŠ Y âŸ¶ Y` is `X`. -/
@[simps!]
def kernelBiprodSndIso : kernel (biprod.snd : X âŠ Y âŸ¶ Y) â‰… X :=
  limit.isoLimitCone âŸ¨_, biprod.isKernelSndKernelFork X YâŸ©

instance : HasCokernel (biprod.inl : X âŸ¶ X âŠ Y) :=
  HasColimit.mk âŸ¨_, biprod.isCokernelInlCokernelFork X YâŸ©

/-- The cokernel of `biprod.inl : X âŸ¶ X âŠ Y` is `Y`. -/
@[simps!]
def cokernelBiprodInlIso : cokernel (biprod.inl : X âŸ¶ X âŠ Y) â‰… Y :=
  colimit.isoColimitCocone âŸ¨_, biprod.isCokernelInlCokernelFork X YâŸ©

instance : HasCokernel (biprod.inr : Y âŸ¶ X âŠ Y) :=
  HasColimit.mk âŸ¨_, biprod.isCokernelInrCokernelFork X YâŸ©

/-- The cokernel of `biprod.inr : Y âŸ¶ X âŠ Y` is `X`. -/
@[simps!]
def cokernelBiprodInrIso : cokernel (biprod.inr : Y âŸ¶ X âŠ Y) â‰… X :=
  colimit.isoColimitCocone âŸ¨_, biprod.isCokernelInrCokernelFork X YâŸ©

end BiprodKernel

section IsZero

/-- If `Y` is a zero object, `X â‰… X âŠ Y` for any `X`. -/
@[simps!]
def isoBiprodZero {X Y : C} [HasBinaryBiproduct X Y] (hY : IsZero Y) : X â‰… X âŠ Y where
  hom := biprod.inl
  inv := biprod.fst
  inv_hom_id := by
    apply CategoryTheory.Limits.biprod.hom_ext <;>
      simp only [Category.assoc, biprod.inl_fst, Category.comp_id, Category.id_comp, biprod.inl_snd,
        comp_zero]
    apply hY.eq_of_tgt

/-- If `X` is a zero object, `Y â‰… X âŠ Y` for any `Y`. -/
@[simps]
def isoZeroBiprod {X Y : C} [HasBinaryBiproduct X Y] (hY : IsZero X) : Y â‰… X âŠ Y where
  hom := biprod.inr
  inv := biprod.snd
  inv_hom_id := by
    apply CategoryTheory.Limits.biprod.hom_ext <;>
      simp only [Category.assoc, biprod.inr_snd, Category.comp_id, Category.id_comp, biprod.inr_fst,
        comp_zero]
    apply hY.eq_of_tgt

@[simp]
lemma biprod_isZero_iff (A B : C) [HasBinaryBiproduct A B] :
    IsZero (biprod A B) â†” IsZero A âˆ§ IsZero B := by
  constructor
  Â· intro h
    simp only [IsZero.iff_id_eq_zero] at h âŠ¢
    simp only [show ğŸ™ A = biprod.inl â‰« ğŸ™ (A âŠ B) â‰« biprod.fst by simp,
      show ğŸ™ B = biprod.inr â‰« ğŸ™ (A âŠ B) â‰« biprod.snd by simp, h, zero_comp, comp_zero,
      and_self]
  Â· rintro âŸ¨hA, hBâŸ©
    rw [IsZero.iff_id_eq_zero]
    apply biprod.hom_ext
    Â· apply hA.eq_of_tgt
    Â· apply hB.eq_of_tgt

end IsZero

section

variable [HasBinaryBiproducts C]

/-- The braiding isomorphism which swaps a binary biproduct. -/
@[simps]
def biprod.braiding (P Q : C) : P âŠ Q â‰… Q âŠ P where
  hom := biprod.lift biprod.snd biprod.fst
  inv := biprod.lift biprod.snd biprod.fst

/-- An alternative formula for the braiding isomorphism which swaps a binary biproduct,
using the fact that the biproduct is a coproduct.
-/
@[simps]
def biprod.braiding' (P Q : C) : P âŠ Q â‰… Q âŠ P where
  hom := biprod.desc biprod.inr biprod.inl
  inv := biprod.desc biprod.inr biprod.inl

theorem biprod.braiding'_eq_braiding {P Q : C} : biprod.braiding' P Q = biprod.braiding P Q := by
  aesop_cat

/-- The braiding isomorphism can be passed through a map by swapping the order. -/
@[reassoc]
theorem biprod.braid_natural {W X Y Z : C} (f : X âŸ¶ Y) (g : Z âŸ¶ W) :
    biprod.map f g â‰« (biprod.braiding _ _).hom = (biprod.braiding _ _).hom â‰« biprod.map g f := by
  aesop_cat

@[reassoc]
theorem biprod.braiding_map_braiding {W X Y Z : C} (f : W âŸ¶ Y) (g : X âŸ¶ Z) :
    (biprod.braiding X W).hom â‰« biprod.map f g â‰« (biprod.braiding Y Z).hom = biprod.map g f := by
  aesop_cat

@[reassoc (attr := simp)]
theorem biprod.symmetry' (P Q : C) :
    biprod.lift biprod.snd biprod.fst â‰« biprod.lift biprod.snd biprod.fst = ğŸ™ (P âŠ Q) := by
  aesop_cat

/-- The braiding isomorphism is symmetric. -/
@[reassoc]
theorem biprod.symmetry (P Q : C) :
    (biprod.braiding P Q).hom â‰« (biprod.braiding Q P).hom = ğŸ™ _ := by simp

/-- The associator isomorphism which associates a binary biproduct. -/
@[simps]
def biprod.associator (P Q R : C) : (P âŠ Q) âŠ R â‰… P âŠ (Q âŠ R) where
  hom := biprod.lift (biprod.fst â‰« biprod.fst) (biprod.lift (biprod.fst â‰« biprod.snd) biprod.snd)
  inv := biprod.lift (biprod.lift biprod.fst (biprod.snd â‰« biprod.fst)) (biprod.snd â‰« biprod.snd)

/-- The associator isomorphism can be passed through a map by swapping the order. -/
@[reassoc]
theorem biprod.associator_natural {U V W X Y Z : C} (f : U âŸ¶ X) (g : V âŸ¶ Y) (h : W âŸ¶ Z) :
    biprod.map (biprod.map f g) h â‰« (biprod.associator _ _ _).hom
      = (biprod.associator _ _ _).hom â‰« biprod.map f (biprod.map g h) := by
  aesop_cat

/-- The associator isomorphism can be passed through a map by swapping the order. -/
@[reassoc]
theorem biprod.associator_inv_natural {U V W X Y Z : C} (f : U âŸ¶ X) (g : V âŸ¶ Y) (h : W âŸ¶ Z) :
    biprod.map f (biprod.map g h) â‰« (biprod.associator _ _ _).inv
      = (biprod.associator _ _ _).inv â‰« biprod.map (biprod.map f g) h := by
  aesop_cat

end

end Limits

open CategoryTheory.Limits

-- TODO:
-- If someone is interested, they could provide the constructions:
--   HasBinaryBiproducts â†” HasFiniteBiproducts
variable {C : Type u} [Category.{v} C] [HasZeroMorphisms C] [HasBinaryBiproducts C]

/-- An object is indecomposable if it cannot be written as the biproduct of two nonzero objects. -/
def Indecomposable (X : C) : Prop :=
  Â¬IsZero X âˆ§ âˆ€ Y Z, (X â‰… Y âŠ Z) â†’ IsZero Y âˆ¨ IsZero Z

/-- If
```
(f 0)
(0 g)
```
is invertible, then `f` is invertible.
-/
theorem isIso_left_of_isIso_biprod_map {W X Y Z : C} (f : W âŸ¶ Y) (g : X âŸ¶ Z)
    [IsIso (biprod.map f g)] : IsIso f :=
  âŸ¨âŸ¨biprod.inl â‰« inv (biprod.map f g) â‰« biprod.fst,
      âŸ¨by
        have t := congrArg (fun p : W âŠ X âŸ¶ W âŠ X => biprod.inl â‰« p â‰« biprod.fst)
          (IsIso.hom_inv_id (biprod.map f g))
        simp only [Category.id_comp, Category.assoc, biprod.inl_map_assoc] at t
        simp [t], by
        have t := congrArg (fun p : Y âŠ Z âŸ¶ Y âŠ Z => biprod.inl â‰« p â‰« biprod.fst)
          (IsIso.inv_hom_id (biprod.map f g))
        simp only [Category.id_comp, Category.assoc, biprod.map_fst] at t
        simp only [Category.assoc]
        simp [t]âŸ©âŸ©âŸ©

/-- If
```
(f 0)
(0 g)
```
is invertible, then `g` is invertible.
-/
theorem isIso_right_of_isIso_biprod_map {W X Y Z : C} (f : W âŸ¶ Y) (g : X âŸ¶ Z)
    [IsIso (biprod.map f g)] : IsIso g :=
  letI : IsIso (biprod.map g f) := by
    rw [â† biprod.braiding_map_braiding]
    infer_instance
  isIso_left_of_isIso_biprod_map g f

end CategoryTheory

set_option linter.style.longFile 2100
