/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Jakob von Raumer
-/
import Mathlib.CategoryTheory.Limits.Shapes.FiniteProducts
import Mathlib.CategoryTheory.Limits.Shapes.Kernels

/-!
# Biproducts and binary biproducts

We introduce the notion of (finite) biproducts.
Binary biproducts are defined in `CategoryTheory.Limits.Shapes.BinaryBiproducts`.

These are slightly unusual relative to the other shapes in the library,
as they are simultaneously limits and colimits.
(Zero objects are similar; they are "biterminal".)

For results about biproducts in preadditive categories see
`CategoryTheory.Preadditive.Biproducts`.

For biproducts indexed by a `Fintype J`, a `bicone` consists of a cone point `X`
and morphisms `œÄ j : X ‚ü∂ F j` and `Œπ j : F j ‚ü∂ X` for each `j`,
such that `Œπ j ‚â´ œÄ j'` is the identity when `j = j'` and zero otherwise.

## Notation
As `‚äï` is already taken for the sum of types, we introduce the notation `X ‚äû Y` for
a binary biproduct. We introduce `‚®Å f` for the indexed biproduct.

## Implementation notes

Prior to https://github.com/leanprover-community/mathlib3/pull/14046,
`HasFiniteBiproducts` required a `DecidableEq` instance on the indexing type.
As this had no pay-off (everything about limits is non-constructive in mathlib),
and occasional cost
(constructing decidability instances appropriate for constructions involving the indexing type),
we made everything classical.
-/

noncomputable section

universe w w' v u

open CategoryTheory Functor

namespace CategoryTheory.Limits

variable {J : Type w}
universe uC' uC uD' uD
variable {C : Type uC} [Category.{uC'} C] [HasZeroMorphisms C]
variable {D : Type uD} [Category.{uD'} D] [HasZeroMorphisms D]

open scoped Classical in
/-- A `c : Bicone F` is:
* an object `c.pt` and
* morphisms `œÄ j : pt ‚ü∂ F j` and `Œπ j : F j ‚ü∂ pt` for each `j`,
* such that `Œπ j ‚â´ œÄ j'` is the identity when `j = j'` and zero otherwise.
-/
structure Bicone (F : J ‚Üí C) where
  pt : C
  œÄ : ‚àÄ j, pt ‚ü∂ F j
  Œπ : ‚àÄ j, F j ‚ü∂ pt
  Œπ_œÄ : ‚àÄ j j', Œπ j ‚â´ œÄ j' =
    if h : j = j' then eqToHom (congrArg F h) else 0 := by aesop

attribute [inherit_doc Bicone] Bicone.pt Bicone.œÄ Bicone.Œπ Bicone.Œπ_œÄ

@[reassoc (attr := simp)]
theorem bicone_Œπ_œÄ_self {F : J ‚Üí C} (B : Bicone F) (j : J) : B.Œπ j ‚â´ B.œÄ j = ùüô (F j) := by
  simpa using B.Œπ_œÄ j j

@[reassoc (attr := simp)]
theorem bicone_Œπ_œÄ_ne {F : J ‚Üí C} (B : Bicone F) {j j' : J} (h : j ‚â† j') : B.Œπ j ‚â´ B.œÄ j' = 0 := by
  simpa [h] using B.Œπ_œÄ j j'

variable {F : J ‚Üí C}

/-- A bicone morphism between two bicones for the same diagram is a morphism of the bicone points
which commutes with the cone and cocone legs. -/
structure BiconeMorphism {F : J ‚Üí C} (A B : Bicone F) where
  /-- A morphism between the two vertex objects of the bicones -/
  hom : A.pt ‚ü∂ B.pt
  /-- The triangle consisting of the two natural transformations and `hom` commutes -/
  wœÄ : ‚àÄ j : J, hom ‚â´ B.œÄ j = A.œÄ j := by aesop_cat
  /-- The triangle consisting of the two natural transformations and `hom` commutes -/
  wŒπ : ‚àÄ j : J, A.Œπ j ‚â´ hom = B.Œπ j := by aesop_cat

attribute [reassoc (attr := simp)] BiconeMorphism.wŒπ BiconeMorphism.wœÄ

/-- The category of bicones on a given diagram. -/
@[simps]
instance Bicone.category : Category (Bicone F) where
  Hom A B := BiconeMorphism A B
  comp f g := { hom := f.hom ‚â´ g.hom }
  id B := { hom := ùüô B.pt }

-- Porting note: if we do not have `simps` automatically generate the lemma for simplifying
-- the `hom` field of a category, we need to write the `ext` lemma in terms of the categorical
-- morphism, rather than the underlying structure.
@[ext]
theorem BiconeMorphism.ext {c c' : Bicone F} (f g : c ‚ü∂ c') (w : f.hom = g.hom) : f = g := by
  cases f
  cases g
  congr

namespace Bicones

/-- To give an isomorphism between cocones, it suffices to give an
  isomorphism between their vertices which commutes with the cocone
  maps. -/
@[aesop apply safe (rule_sets := [CategoryTheory]), simps]
def ext {c c' : Bicone F} (œÜ : c.pt ‚âÖ c'.pt)
    (wŒπ : ‚àÄ j, c.Œπ j ‚â´ œÜ.hom = c'.Œπ j := by aesop_cat)
    (wœÄ : ‚àÄ j, œÜ.hom ‚â´ c'.œÄ j = c.œÄ j := by aesop_cat) : c ‚âÖ c' where
  hom := { hom := œÜ.hom }
  inv :=
    { hom := œÜ.inv
      wŒπ := fun j => œÜ.comp_inv_eq.mpr (wŒπ j).symm
      wœÄ := fun j => œÜ.inv_comp_eq.mpr (wœÄ j).symm  }

variable (F) in
/-- A functor `G : C ‚•§ D` sends bicones over `F` to bicones over `G.obj ‚àò F` functorially. -/
@[simps]
def functoriality (G : C ‚•§ D) [Functor.PreservesZeroMorphisms G] :
    Bicone F ‚•§ Bicone (G.obj ‚àò F) where
  obj A :=
    { pt := G.obj A.pt
      œÄ := fun j => G.map (A.œÄ j)
      Œπ := fun j => G.map (A.Œπ j)
      Œπ_œÄ := fun i j => (Functor.map_comp _ _ _).symm.trans <| by
        rw [A.Œπ_œÄ]
        aesop_cat }
  map f :=
    { hom := G.map f.hom
      wœÄ := fun j => by simp [-BiconeMorphism.wœÄ, ‚Üê f.wœÄ j]
      wŒπ := fun j => by simp [-BiconeMorphism.wŒπ, ‚Üê f.wŒπ j] }

variable (G : C ‚•§ D)

instance functoriality_full [G.PreservesZeroMorphisms] [G.Full] [G.Faithful] :
    (functoriality F G).Full where
  map_surjective t :=
   ‚ü®{ hom := G.preimage t.hom
      wŒπ := fun j => G.map_injective (by simpa using t.wŒπ j)
      wœÄ := fun j => G.map_injective (by simpa using t.wœÄ j) }, by aesop_cat‚ü©

instance functoriality_faithful [G.PreservesZeroMorphisms] [G.Faithful] :
    (functoriality F G).Faithful where
  map_injective {_X} {_Y} f g h :=
    BiconeMorphism.ext f g <| G.map_injective <| congr_arg BiconeMorphism.hom h

end Bicones

namespace Bicone

attribute [local aesop safe tactic (rule_sets := [CategoryTheory])]
  CategoryTheory.Discrete.discreteCases
-- Porting note: would it be okay to use this more generally?
attribute [local aesop safe cases (rule_sets := [CategoryTheory])] Eq

/-- Extract the cone from a bicone. -/
def toConeFunctor : Bicone F ‚•§ Cone (Discrete.functor F) where
  obj B := { pt := B.pt, œÄ := { app := fun j => B.œÄ j.as } }
  map {_ _} F := { hom := F.hom, w := fun _ => F.wœÄ _ }

/-- A shorthand for `toConeFunctor.obj` -/
abbrev toCone (B : Bicone F) : Cone (Discrete.functor F) := toConeFunctor.obj B

-- TODO Consider changing this API to `toFan (B : Bicone F) : Fan F`.

@[simp] theorem toCone_pt (B : Bicone F) : B.toCone.pt = B.pt := rfl

@[simp] theorem toCone_œÄ_app (B : Bicone F) (j : Discrete J) : B.toCone.œÄ.app j = B.œÄ j.as := rfl

theorem toCone_œÄ_app_mk (B : Bicone F) (j : J) : B.toCone.œÄ.app ‚ü®j‚ü© = B.œÄ j := rfl

@[simp] theorem toCone_proj (B : Bicone F) (j : J) : Fan.proj B.toCone j = B.œÄ j := rfl

/-- Extract the cocone from a bicone. -/
def toCoconeFunctor : Bicone F ‚•§ Cocone (Discrete.functor F) where
  obj B := { pt := B.pt, Œπ := { app := fun j => B.Œπ j.as } }
  map {_ _} F := { hom := F.hom, w := fun _ => F.wŒπ _ }

/-- A shorthand for `toCoconeFunctor.obj` -/
abbrev toCocone (B : Bicone F) : Cocone (Discrete.functor F) := toCoconeFunctor.obj B

@[simp] theorem toCocone_pt (B : Bicone F) : B.toCocone.pt = B.pt := rfl

@[simp]
theorem toCocone_Œπ_app (B : Bicone F) (j : Discrete J) : B.toCocone.Œπ.app j = B.Œπ j.as := rfl

@[simp] theorem toCocone_inj (B : Bicone F) (j : J) : Cofan.inj B.toCocone j = B.Œπ j := rfl

theorem toCocone_Œπ_app_mk (B : Bicone F) (j : J) : B.toCocone.Œπ.app ‚ü®j‚ü© = B.Œπ j := rfl

open scoped Classical in
/-- We can turn any limit cone over a discrete collection of objects into a bicone. -/
@[simps]
def ofLimitCone {f : J ‚Üí C} {t : Cone (Discrete.functor f)} (ht : IsLimit t) : Bicone f where
  pt := t.pt
  œÄ j := t.œÄ.app ‚ü®j‚ü©
  Œπ j := ht.lift (Fan.mk _ fun j' => if h : j = j' then eqToHom (congr_arg f h) else 0)
  Œπ_œÄ j j' := by simp

open scoped Classical in
theorem Œπ_of_isLimit {f : J ‚Üí C} {t : Bicone f} (ht : IsLimit t.toCone) (j : J) :
    t.Œπ j = ht.lift (Fan.mk _ fun j' => if h : j = j' then eqToHom (congr_arg f h) else 0) :=
  ht.hom_ext fun j' => by
    rw [ht.fac]
    simp [t.Œπ_œÄ]

open scoped Classical in
/-- We can turn any colimit cocone over a discrete collection of objects into a bicone. -/
@[simps]
def ofColimitCocone {f : J ‚Üí C} {t : Cocone (Discrete.functor f)} (ht : IsColimit t) :
    Bicone f where
  pt := t.pt
  œÄ j := ht.desc (Cofan.mk _ fun j' => if h : j' = j then eqToHom (congr_arg f h) else 0)
  Œπ j := t.Œπ.app ‚ü®j‚ü©
  Œπ_œÄ j j' := by simp

open scoped Classical in
theorem œÄ_of_isColimit {f : J ‚Üí C} {t : Bicone f} (ht : IsColimit t.toCocone) (j : J) :
    t.œÄ j = ht.desc (Cofan.mk _ fun j' => if h : j' = j then eqToHom (congr_arg f h) else 0) :=
  ht.hom_ext fun j' => by
    rw [ht.fac]
    simp [t.Œπ_œÄ]

/-- Structure witnessing that a bicone is both a limit cone and a colimit cocone. -/
structure IsBilimit {F : J ‚Üí C} (B : Bicone F) where
  isLimit : IsLimit B.toCone
  isColimit : IsColimit B.toCocone

attribute [inherit_doc IsBilimit] IsBilimit.isLimit IsBilimit.isColimit

attribute [simp] IsBilimit.mk.injEq

attribute [local ext] Bicone.IsBilimit

instance subsingleton_isBilimit {f : J ‚Üí C} {c : Bicone f} : Subsingleton c.IsBilimit :=
  ‚ü®fun _ _ => Bicone.IsBilimit.ext (Subsingleton.elim _ _) (Subsingleton.elim _ _)‚ü©

section Whisker

variable {K : Type w'}

/-- Whisker a bicone with an equivalence between the indexing types. -/
@[simps]
def whisker {f : J ‚Üí C} (c : Bicone f) (g : K ‚âÉ J) : Bicone (f ‚àò g) where
  pt := c.pt
  œÄ k := c.œÄ (g k)
  Œπ k := c.Œπ (g k)
  Œπ_œÄ k k' := by
    simp only [c.Œπ_œÄ]
    split_ifs with h h' h' <;> simp [Equiv.apply_eq_iff_eq g] at h h' <;> tauto

/-- Taking the cone of a whiskered bicone results in a cone isomorphic to one gained
by whiskering the cone and postcomposing with a suitable isomorphism. -/
def whiskerToCone {f : J ‚Üí C} (c : Bicone f) (g : K ‚âÉ J) :
    (c.whisker g).toCone ‚âÖ
      (Cones.postcompose (Discrete.functorComp f g).inv).obj
        (c.toCone.whisker (Discrete.functor (Discrete.mk ‚àò g))) :=
  Cones.ext (Iso.refl _) (by simp)

/-- Taking the cocone of a whiskered bicone results in a cone isomorphic to one gained
by whiskering the cocone and precomposing with a suitable isomorphism. -/
def whiskerToCocone {f : J ‚Üí C} (c : Bicone f) (g : K ‚âÉ J) :
    (c.whisker g).toCocone ‚âÖ
      (Cocones.precompose (Discrete.functorComp f g).hom).obj
        (c.toCocone.whisker (Discrete.functor (Discrete.mk ‚àò g))) :=
  Cocones.ext (Iso.refl _) (by simp)

/-- Whiskering a bicone with an equivalence between types preserves being a bilimit bicone. -/
noncomputable def whiskerIsBilimitIff {f : J ‚Üí C} (c : Bicone f) (g : K ‚âÉ J) :
    (c.whisker g).IsBilimit ‚âÉ c.IsBilimit := by
  refine equivOfSubsingletonOfSubsingleton (fun hc => ‚ü®?_, ?_‚ü©) fun hc => ‚ü®?_, ?_‚ü©
  ¬∑ let this := IsLimit.ofIsoLimit hc.isLimit (Bicone.whiskerToCone c g)
    let this := (IsLimit.postcomposeHomEquiv (Discrete.functorComp f g).symm _) this
    exact IsLimit.ofWhiskerEquivalence (Discrete.equivalence g) this
  ¬∑ let this := IsColimit.ofIsoColimit hc.isColimit (Bicone.whiskerToCocone c g)
    let this := (IsColimit.precomposeHomEquiv (Discrete.functorComp f g) _) this
    exact IsColimit.ofWhiskerEquivalence (Discrete.equivalence g) this
  ¬∑ apply IsLimit.ofIsoLimit _ (Bicone.whiskerToCone c g).symm
    apply (IsLimit.postcomposeHomEquiv (Discrete.functorComp f g).symm _).symm _
    exact IsLimit.whiskerEquivalence hc.isLimit (Discrete.equivalence g)
  ¬∑ apply IsColimit.ofIsoColimit _ (Bicone.whiskerToCocone c g).symm
    apply (IsColimit.precomposeHomEquiv (Discrete.functorComp f g) _).symm _
    exact IsColimit.whiskerEquivalence hc.isColimit (Discrete.equivalence g)

end Whisker

end Bicone

/-- A bicone over `F : J ‚Üí C`, which is both a limit cone and a colimit cocone. -/
structure LimitBicone (F : J ‚Üí C) where
  bicone : Bicone F
  isBilimit : bicone.IsBilimit

attribute [inherit_doc LimitBicone] LimitBicone.bicone LimitBicone.isBilimit

/-- `HasBiproduct F` expresses the mere existence of a bicone which is
simultaneously a limit and a colimit of the diagram `F`. -/
class HasBiproduct (F : J ‚Üí C) : Prop where mk' ::
  exists_biproduct : Nonempty (LimitBicone F)

attribute [inherit_doc HasBiproduct] HasBiproduct.exists_biproduct

theorem HasBiproduct.mk {F : J ‚Üí C} (d : LimitBicone F) : HasBiproduct F :=
  ‚ü®Nonempty.intro d‚ü©

/-- Use the axiom of choice to extract explicit `BiproductData F` from `HasBiproduct F`. -/
def getBiproductData (F : J ‚Üí C) [HasBiproduct F] : LimitBicone F :=
  Classical.choice HasBiproduct.exists_biproduct

/-- A bicone for `F` which is both a limit cone and a colimit cocone. -/
def biproduct.bicone (F : J ‚Üí C) [HasBiproduct F] : Bicone F :=
  (getBiproductData F).bicone

/-- `biproduct.bicone F` is a bilimit bicone. -/
def biproduct.isBilimit (F : J ‚Üí C) [HasBiproduct F] : (biproduct.bicone F).IsBilimit :=
  (getBiproductData F).isBilimit

/-- `biproduct.bicone F` is a limit cone. -/
def biproduct.isLimit (F : J ‚Üí C) [HasBiproduct F] : IsLimit (biproduct.bicone F).toCone :=
  (getBiproductData F).isBilimit.isLimit

/-- `biproduct.bicone F` is a colimit cocone. -/
def biproduct.isColimit (F : J ‚Üí C) [HasBiproduct F] : IsColimit (biproduct.bicone F).toCocone :=
  (getBiproductData F).isBilimit.isColimit

instance (priority := 100) hasProduct_of_hasBiproduct [HasBiproduct F] : HasProduct F :=
  HasLimit.mk
    { cone := (biproduct.bicone F).toCone
      isLimit := biproduct.isLimit F }

instance (priority := 100) hasCoproduct_of_hasBiproduct [HasBiproduct F] : HasCoproduct F :=
  HasColimit.mk
    { cocone := (biproduct.bicone F).toCocone
      isColimit := biproduct.isColimit F }

variable (J C)

/-- `C` has biproducts of shape `J` if we have
a limit and a colimit, with the same cone points,
of every function `F : J ‚Üí C`. -/
class HasBiproductsOfShape : Prop where
  has_biproduct : ‚àÄ F : J ‚Üí C, HasBiproduct F

attribute [instance 100] HasBiproductsOfShape.has_biproduct

/-- `HasFiniteBiproducts C` represents a choice of biproduct for every family of objects in `C`
indexed by a finite type. -/
class HasFiniteBiproducts : Prop where
  out : ‚àÄ n, HasBiproductsOfShape (Fin n) C

attribute [inherit_doc HasFiniteBiproducts] HasFiniteBiproducts.out

variable {J}

theorem hasBiproductsOfShape_of_equiv {K : Type w'} [HasBiproductsOfShape K C] (e : J ‚âÉ K) :
    HasBiproductsOfShape J C :=
  ‚ü®fun F =>
    let ‚ü®‚ü®h‚ü©‚ü© := HasBiproductsOfShape.has_biproduct (F ‚àò e.symm)
    let ‚ü®c, hc‚ü© := h
    HasBiproduct.mk <| by
      simpa only [Function.comp_def, e.symm_apply_apply] using
        LimitBicone.mk (c.whisker e) ((c.whiskerIsBilimitIff _).2 hc)‚ü©

instance (priority := 100) hasBiproductsOfShape_finite [HasFiniteBiproducts C] [Finite J] :
    HasBiproductsOfShape J C := by
  rcases Finite.exists_equiv_fin J with ‚ü®n, ‚ü®e‚ü©‚ü©
  haveI : HasBiproductsOfShape (Fin n) C := HasFiniteBiproducts.out n
  exact hasBiproductsOfShape_of_equiv C e

instance (priority := 100) hasFiniteProducts_of_hasFiniteBiproducts [HasFiniteBiproducts C] :
    HasFiniteProducts C where
  out _ := ‚ü®fun _ => hasLimit_of_iso Discrete.natIsoFunctor.symm‚ü©

instance (priority := 100) hasFiniteCoproducts_of_hasFiniteBiproducts [HasFiniteBiproducts C] :
    HasFiniteCoproducts C where
  out _ := ‚ü®fun _ => hasColimit_of_iso Discrete.natIsoFunctor‚ü©

instance (priority := 100) hasProductsOfShape_of_hasBiproductsOfShape [HasBiproductsOfShape J C] :
    HasProductsOfShape J C where
  has_limit _ := hasLimit_of_iso Discrete.natIsoFunctor.symm

instance (priority := 100) hasCoproductsOfShape_of_hasBiproductsOfShape [HasBiproductsOfShape J C] :
    HasCoproductsOfShape J C where
  has_colimit _ := hasColimit_of_iso Discrete.natIsoFunctor

variable {C}

/-- The isomorphism between the specified limit and the specified colimit for
a functor with a bilimit. -/
def biproductIso (F : J ‚Üí C) [HasBiproduct F] : Limits.piObj F ‚âÖ Limits.sigmaObj F :=
  (IsLimit.conePointUniqueUpToIso (limit.isLimit _) (biproduct.isLimit F)).trans <|
    IsColimit.coconePointUniqueUpToIso (biproduct.isColimit F) (colimit.isColimit _)

variable {J : Type w} {K : Type*}
variable {C : Type u} [Category.{v} C] [HasZeroMorphisms C]

/-- `biproduct f` computes the biproduct of a family of elements `f`. (It is defined as an
   abbreviation for `limit (Discrete.functor f)`, so for most facts about `biproduct f`, you will
   just use general facts about limits and colimits.) -/
abbrev biproduct (f : J ‚Üí C) [HasBiproduct f] : C :=
  (biproduct.bicone f).pt

@[inherit_doc biproduct]
notation "‚®Å " f:20 => biproduct f

/-- The projection onto a summand of a biproduct. -/
abbrev biproduct.œÄ (f : J ‚Üí C) [HasBiproduct f] (b : J) : ‚®Å f ‚ü∂ f b :=
  (biproduct.bicone f).œÄ b

@[simp]
theorem biproduct.bicone_œÄ (f : J ‚Üí C) [HasBiproduct f] (b : J) :
    (biproduct.bicone f).œÄ b = biproduct.œÄ f b := rfl

/-- The inclusion into a summand of a biproduct. -/
abbrev biproduct.Œπ (f : J ‚Üí C) [HasBiproduct f] (b : J) : f b ‚ü∂ ‚®Å f :=
  (biproduct.bicone f).Œπ b

@[simp]
theorem biproduct.bicone_Œπ (f : J ‚Üí C) [HasBiproduct f] (b : J) :
    (biproduct.bicone f).Œπ b = biproduct.Œπ f b := rfl

/-- Note that as this lemma has an `if` in the statement, we include a `DecidableEq` argument.
This means you may not be able to `simp` using this lemma unless you `open scoped Classical`. -/
@[reassoc]
theorem biproduct.Œπ_œÄ [DecidableEq J] (f : J ‚Üí C) [HasBiproduct f] (j j' : J) :
    biproduct.Œπ f j ‚â´ biproduct.œÄ f j' = if h : j = j' then eqToHom (congr_arg f h) else 0 := by
  convert (biproduct.bicone f).Œπ_œÄ j j'

@[reassoc] -- Porting note: both versions proven by simp
theorem biproduct.Œπ_œÄ_self (f : J ‚Üí C) [HasBiproduct f] (j : J) :
    biproduct.Œπ f j ‚â´ biproduct.œÄ f j = ùüô _ := by simp [biproduct.Œπ_œÄ]

@[reassoc]
theorem biproduct.Œπ_œÄ_ne (f : J ‚Üí C) [HasBiproduct f] {j j' : J} (h : j ‚â† j') :
    biproduct.Œπ f j ‚â´ biproduct.œÄ f j' = 0 := by simp [h]

@[reassoc (attr := simp)]
theorem biproduct.eqToHom_comp_Œπ (f : J ‚Üí C) [HasBiproduct f] {j j' : J} (w : j = j') :
    eqToHom (by simp [w]) ‚â´ biproduct.Œπ f j' = biproduct.Œπ f j := by
  cases w
  simp

-- TODO?: simp can prove this using `eqToHom_naturality`
-- but `eqToHom_naturality` applies less easily than this lemma
@[reassoc]
theorem biproduct.œÄ_comp_eqToHom (f : J ‚Üí C) [HasBiproduct f] {j j' : J} (w : j = j') :
    biproduct.œÄ f j ‚â´ eqToHom (by simp [w]) = biproduct.œÄ f j' := by
  simp [*]

/-- Given a collection of maps into the summands, we obtain a map into the biproduct. -/
abbrev biproduct.lift {f : J ‚Üí C} [HasBiproduct f] {P : C} (p : ‚àÄ b, P ‚ü∂ f b) : P ‚ü∂ ‚®Å f :=
  (biproduct.isLimit f).lift (Fan.mk P p)

/-- Given a collection of maps out of the summands, we obtain a map out of the biproduct. -/
abbrev biproduct.desc {f : J ‚Üí C} [HasBiproduct f] {P : C} (p : ‚àÄ b, f b ‚ü∂ P) : ‚®Å f ‚ü∂ P :=
  (biproduct.isColimit f).desc (Cofan.mk P p)

@[reassoc (attr := simp)]
theorem biproduct.lift_œÄ {f : J ‚Üí C} [HasBiproduct f] {P : C} (p : ‚àÄ b, P ‚ü∂ f b) (j : J) :
    biproduct.lift p ‚â´ biproduct.œÄ f j = p j := (biproduct.isLimit f).fac _ ‚ü®j‚ü©

@[reassoc (attr := simp)]
theorem biproduct.Œπ_desc {f : J ‚Üí C} [HasBiproduct f] {P : C} (p : ‚àÄ b, f b ‚ü∂ P) (j : J) :
    biproduct.Œπ f j ‚â´ biproduct.desc p = p j := (biproduct.isColimit f).fac _ ‚ü®j‚ü©

/-- Given a collection of maps between corresponding summands of a pair of biproducts
indexed by the same type, we obtain a map between the biproducts. -/
abbrev biproduct.map {f g : J ‚Üí C} [HasBiproduct f] [HasBiproduct g] (p : ‚àÄ b, f b ‚ü∂ g b) :
    ‚®Å f ‚ü∂ ‚®Å g :=
  IsLimit.map (biproduct.bicone f).toCone (biproduct.isLimit g)
    (Discrete.natTrans (fun j => p j.as))

/-- An alternative to `biproduct.map` constructed via colimits.
This construction only exists in order to show it is equal to `biproduct.map`. -/
abbrev biproduct.map' {f g : J ‚Üí C} [HasBiproduct f] [HasBiproduct g] (p : ‚àÄ b, f b ‚ü∂ g b) :
    ‚®Å f ‚ü∂ ‚®Å g :=
  IsColimit.map (biproduct.isColimit f) (biproduct.bicone g).toCocone
    (Discrete.natTrans fun j => p j.as)

-- We put this at slightly higher priority than `biproduct.hom_ext'`,
-- to get the matrix indices in the "right" order.
@[ext 1001]
theorem biproduct.hom_ext {f : J ‚Üí C} [HasBiproduct f] {Z : C} (g h : Z ‚ü∂ ‚®Å f)
    (w : ‚àÄ j, g ‚â´ biproduct.œÄ f j = h ‚â´ biproduct.œÄ f j) : g = h :=
  (biproduct.isLimit f).hom_ext fun j => w j.as

@[ext]
theorem biproduct.hom_ext' {f : J ‚Üí C} [HasBiproduct f] {Z : C} (g h : ‚®Å f ‚ü∂ Z)
    (w : ‚àÄ j, biproduct.Œπ f j ‚â´ g = biproduct.Œπ f j ‚â´ h) : g = h :=
  (biproduct.isColimit f).hom_ext fun j => w j.as

/-- The canonical isomorphism between the chosen biproduct and the chosen product. -/
def biproduct.isoProduct (f : J ‚Üí C) [HasBiproduct f] : ‚®Å f ‚âÖ ‚àè·∂ú f :=
  IsLimit.conePointUniqueUpToIso (biproduct.isLimit f) (limit.isLimit _)

@[simp]
theorem biproduct.isoProduct_hom {f : J ‚Üí C} [HasBiproduct f] :
    (biproduct.isoProduct f).hom = Pi.lift (biproduct.œÄ f) :=
  limit.hom_ext fun j => by simp [biproduct.isoProduct]

@[simp]
theorem biproduct.isoProduct_inv {f : J ‚Üí C} [HasBiproduct f] :
    (biproduct.isoProduct f).inv = biproduct.lift (Pi.œÄ f) :=
  biproduct.hom_ext _ _ fun j => by simp [Iso.inv_comp_eq]

/-- The canonical isomorphism between the chosen biproduct and the chosen coproduct. -/
def biproduct.isoCoproduct (f : J ‚Üí C) [HasBiproduct f] : ‚®Å f ‚âÖ ‚àê f :=
  IsColimit.coconePointUniqueUpToIso (biproduct.isColimit f) (colimit.isColimit _)

@[simp]
theorem biproduct.isoCoproduct_inv {f : J ‚Üí C} [HasBiproduct f] :
    (biproduct.isoCoproduct f).inv = Sigma.desc (biproduct.Œπ f) :=
  colimit.hom_ext fun j => by simp [biproduct.isoCoproduct]

@[simp]
theorem biproduct.isoCoproduct_hom {f : J ‚Üí C} [HasBiproduct f] :
    (biproduct.isoCoproduct f).hom = biproduct.desc (Sigma.Œπ f) :=
  biproduct.hom_ext' _ _ fun j => by simp [‚Üê Iso.eq_comp_inv]

/-- If a category has biproducts of a shape `J`, its `colim` and `lim` functor on diagrams over `J`
are isomorphic. -/
@[simps!]
def HasBiproductsOfShape.colimIsoLim [HasBiproductsOfShape J C] :
    colim (J := Discrete J) (C := C) ‚âÖ lim :=
  NatIso.ofComponents (fun F => (Sigma.isoColimit F).symm ‚â™‚â´
      (biproduct.isoCoproduct _).symm ‚â™‚â´ biproduct.isoProduct _ ‚â™‚â´ Pi.isoLimit F)
    fun Œ∑ => colimit.hom_ext fun ‚ü®i‚ü© => limit.hom_ext fun ‚ü®j‚ü© => by
      classical
      by_cases h : i = j <;>
       simp_all [h, Sigma.isoColimit, Pi.isoLimit, biproduct.Œπ_œÄ, biproduct.Œπ_œÄ_assoc]

theorem biproduct.map_eq_map' {f g : J ‚Üí C} [HasBiproduct f] [HasBiproduct g] (p : ‚àÄ b, f b ‚ü∂ g b) :
    biproduct.map p = biproduct.map' p := by
  classical
  ext
  dsimp
  simp only [Discrete.natTrans_app, Limits.IsColimit.Œπ_map_assoc, Limits.IsLimit.map_œÄ,
    Category.assoc, ‚Üê Bicone.toCone_œÄ_app_mk, ‚Üê biproduct.bicone_œÄ, ‚Üê Bicone.toCocone_Œπ_app_mk,
    ‚Üê biproduct.bicone_Œπ]
  dsimp
  rw [biproduct.Œπ_œÄ_assoc, biproduct.Œπ_œÄ]
  split_ifs with h
  ¬∑ subst h; simp
  ¬∑ simp

@[reassoc (attr := simp)]
theorem biproduct.map_œÄ {f g : J ‚Üí C} [HasBiproduct f] [HasBiproduct g] (p : ‚àÄ j, f j ‚ü∂ g j)
    (j : J) : biproduct.map p ‚â´ biproduct.œÄ g j = biproduct.œÄ f j ‚â´ p j :=
  Limits.IsLimit.map_œÄ _ _ _ (Discrete.mk j)

@[reassoc (attr := simp)]
theorem biproduct.Œπ_map {f g : J ‚Üí C} [HasBiproduct f] [HasBiproduct g] (p : ‚àÄ j, f j ‚ü∂ g j)
    (j : J) : biproduct.Œπ f j ‚â´ biproduct.map p = p j ‚â´ biproduct.Œπ g j := by
  rw [biproduct.map_eq_map']
  apply
    Limits.IsColimit.Œπ_map (biproduct.isColimit f) (biproduct.bicone g).toCocone
    (Discrete.natTrans fun j => p j.as) (Discrete.mk j)

@[reassoc (attr := simp)]
theorem biproduct.map_desc {f g : J ‚Üí C} [HasBiproduct f] [HasBiproduct g] (p : ‚àÄ j, f j ‚ü∂ g j)
    {P : C} (k : ‚àÄ j, g j ‚ü∂ P) :
    biproduct.map p ‚â´ biproduct.desc k = biproduct.desc fun j => p j ‚â´ k j := by
  ext; simp

@[reassoc (attr := simp)]
theorem biproduct.lift_map {f g : J ‚Üí C} [HasBiproduct f] [HasBiproduct g] {P : C}
    (k : ‚àÄ j, P ‚ü∂ f j) (p : ‚àÄ j, f j ‚ü∂ g j) :
    biproduct.lift k ‚â´ biproduct.map p = biproduct.lift fun j => k j ‚â´ p j := by
  ext; simp

/-- Given a collection of isomorphisms between corresponding summands of a pair of biproducts
indexed by the same type, we obtain an isomorphism between the biproducts. -/
@[simps]
def biproduct.mapIso {f g : J ‚Üí C} [HasBiproduct f] [HasBiproduct g] (p : ‚àÄ b, f b ‚âÖ g b) :
    ‚®Å f ‚âÖ ‚®Å g where
  hom := biproduct.map fun b => (p b).hom
  inv := biproduct.map fun b => (p b).inv

instance biproduct.map_epi {f g : J ‚Üí C} [HasBiproduct f] [HasBiproduct g] (p : ‚àÄ j, f j ‚ü∂ g j)
    [‚àÄ j, Epi (p j)] : Epi (biproduct.map p) := by
  classical
  have : biproduct.map p =
      (biproduct.isoCoproduct _).hom ‚â´ Sigma.map p ‚â´ (biproduct.isoCoproduct _).inv := by
    ext
    simp only [map_œÄ, isoCoproduct_hom, isoCoproduct_inv, Category.assoc, Œπ_desc_assoc,
      Œπ_colimMap_assoc, Discrete.functor_obj_eq_as, Discrete.natTrans_app, colimit.Œπ_desc_assoc,
      Cofan.mk_pt, Cofan.mk_Œπ_app, Œπ_œÄ, Œπ_œÄ_assoc]
    split
    all_goals simp_all
  rw [this]
  infer_instance

instance Pi.map_epi {f g : J ‚Üí C} [HasBiproduct f] [HasBiproduct g] (p : ‚àÄ j, f j ‚ü∂ g j)
    [‚àÄ j, Epi (p j)] : Epi (Pi.map p) := by
  rw [show Pi.map p = (biproduct.isoProduct _).inv ‚â´ biproduct.map p ‚â´
    (biproduct.isoProduct _).hom by aesop]
  infer_instance

instance biproduct.map_mono {f g : J ‚Üí C} [HasBiproduct f] [HasBiproduct g] (p : ‚àÄ j, f j ‚ü∂ g j)
    [‚àÄ j, Mono (p j)] : Mono (biproduct.map p) := by
  rw [show biproduct.map p = (biproduct.isoProduct _).hom ‚â´ Pi.map p ‚â´
    (biproduct.isoProduct _).inv by aesop]
  infer_instance

instance Sigma.map_mono {f g : J ‚Üí C} [HasBiproduct f] [HasBiproduct g] (p : ‚àÄ j, f j ‚ü∂ g j)
    [‚àÄ j, Mono (p j)] : Mono (Sigma.map p) := by
  rw [show Sigma.map p = (biproduct.isoCoproduct _).inv ‚â´ biproduct.map p ‚â´
    (biproduct.isoCoproduct _).hom by aesop]
  infer_instance

/-- Two biproducts which differ by an equivalence in the indexing type,
and up to isomorphism in the factors, are isomorphic.

Unfortunately there are two natural ways to define each direction of this isomorphism
(because it is true for both products and coproducts separately).
We give the alternative definitions as lemmas below. -/
@[simps]
def biproduct.whiskerEquiv {f : J ‚Üí C} {g : K ‚Üí C} (e : J ‚âÉ K) (w : ‚àÄ j, g (e j) ‚âÖ f j)
    [HasBiproduct f] [HasBiproduct g] : ‚®Å f ‚âÖ ‚®Å g where
  hom := biproduct.desc fun j => (w j).inv ‚â´ biproduct.Œπ g (e j)
  inv := biproduct.desc fun k => eqToHom (by simp) ‚â´ (w (e.symm k)).hom ‚â´ biproduct.Œπ f _

lemma biproduct.whiskerEquiv_hom_eq_lift {f : J ‚Üí C} {g : K ‚Üí C} (e : J ‚âÉ K)
    (w : ‚àÄ j, g (e j) ‚âÖ f j) [HasBiproduct f] [HasBiproduct g] :
    (biproduct.whiskerEquiv e w).hom =
      biproduct.lift fun k => biproduct.œÄ f (e.symm k) ‚â´ (w _).inv ‚â´ eqToHom (by simp) := by
  simp only [whiskerEquiv_hom]
  ext k j
  by_cases h : k = e j
  ¬∑ subst h
    simp
  ¬∑ simp only [Œπ_desc_assoc, Category.assoc, ne_eq, lift_œÄ]
    rw [biproduct.Œπ_œÄ_ne, biproduct.Œπ_œÄ_ne_assoc]
    ¬∑ simp
    ¬∑ rintro rfl
      simp at h
    ¬∑ exact Ne.symm h

lemma biproduct.whiskerEquiv_inv_eq_lift {f : J ‚Üí C} {g : K ‚Üí C} (e : J ‚âÉ K)
    (w : ‚àÄ j, g (e j) ‚âÖ f j) [HasBiproduct f] [HasBiproduct g] :
    (biproduct.whiskerEquiv e w).inv =
      biproduct.lift fun j => biproduct.œÄ g (e j) ‚â´ (w j).hom := by
  simp only [whiskerEquiv_inv]
  ext j k
  by_cases h : k = e j
  ¬∑ subst h
    simp only [Œπ_desc_assoc, ‚Üê eqToHom_iso_hom_naturality_assoc w (e.symm_apply_apply j).symm,
      Equiv.symm_apply_apply, eqToHom_comp_Œπ, Category.assoc, bicone_Œπ_œÄ_self, Category.comp_id,
      lift_œÄ, bicone_Œπ_œÄ_self_assoc]
  ¬∑ simp only [Œπ_desc_assoc, Category.assoc, ne_eq, lift_œÄ]
    rw [biproduct.Œπ_œÄ_ne, biproduct.Œπ_œÄ_ne_assoc]
    ¬∑ simp
    ¬∑ exact h
    ¬∑ rintro rfl
      simp at h

attribute [local simp] Sigma.forall in
instance {Œπ} (f : Œπ ‚Üí Type*) (g : (i : Œπ) ‚Üí (f i) ‚Üí C)
    [‚àÄ i, HasBiproduct (g i)] [HasBiproduct fun i => ‚®Å g i] :
    HasBiproduct fun p : Œ£ i, f i => g p.1 p.2 where
  exists_biproduct := Nonempty.intro
    { bicone :=
      { pt := ‚®Å fun i => ‚®Å g i
        Œπ := fun X => biproduct.Œπ (g X.1) X.2 ‚â´ biproduct.Œπ (fun i => ‚®Å g i) X.1
        œÄ := fun X => biproduct.œÄ (fun i => ‚®Å g i) X.1 ‚â´ biproduct.œÄ (g X.1) X.2
        Œπ_œÄ := fun ‚ü®j, x‚ü© ‚ü®j', y‚ü© => by
          split_ifs with h
          ¬∑ obtain ‚ü®rfl, rfl‚ü© := h
            simp
          ¬∑ simp only [Sigma.mk.inj_iff, not_and] at h
            by_cases w : j = j'
            ¬∑ cases w
              simp only [heq_eq_eq, forall_true_left] at h
              simp [biproduct.Œπ_œÄ_ne _ h]
            ¬∑ simp [biproduct.Œπ_œÄ_ne_assoc _ w] }
      isBilimit :=
      { isLimit := mkFanLimit _
          (fun s => biproduct.lift fun b => biproduct.lift fun c => s.proj ‚ü®b, c‚ü©)
        isColimit := mkCofanColimit _
          (fun s => biproduct.desc fun b => biproduct.desc fun c => s.inj ‚ü®b, c‚ü©) } }

/-- An iterated biproduct is a biproduct over a sigma type. -/
@[simps]
def biproductBiproductIso {Œπ} (f : Œπ ‚Üí Type*) (g : (i : Œπ) ‚Üí (f i) ‚Üí C)
    [‚àÄ i, HasBiproduct (g i)] [HasBiproduct fun i => ‚®Å g i] :
    (‚®Å fun i => ‚®Å g i) ‚âÖ (‚®Å fun p : Œ£ i, f i => g p.1 p.2) where
  hom := biproduct.lift fun ‚ü®i, x‚ü© => biproduct.œÄ _ i ‚â´ biproduct.œÄ _ x
  inv := biproduct.lift fun i => biproduct.lift fun x => biproduct.œÄ _ (‚ü®i, x‚ü© : Œ£ i, f i)

section œÄKernel

section

variable (f : J ‚Üí C) [HasBiproduct f]
variable (p : J ‚Üí Prop) [HasBiproduct (Subtype.restrict p f)]

/-- The canonical morphism from the biproduct over a restricted index type to the biproduct of
the full index type. -/
def biproduct.fromSubtype : ‚®Å Subtype.restrict p f ‚ü∂ ‚®Å f :=
  biproduct.desc fun j => biproduct.Œπ _ j.val

/-- The canonical morphism from a biproduct to the biproduct over a restriction of its index
type. -/
def biproduct.toSubtype : ‚®Å f ‚ü∂ ‚®Å Subtype.restrict p f :=
  biproduct.lift fun _ => biproduct.œÄ _ _

@[reassoc (attr := simp)]
theorem biproduct.fromSubtype_œÄ [DecidablePred p] (j : J) :
    biproduct.fromSubtype f p ‚â´ biproduct.œÄ f j =
      if h : p j then biproduct.œÄ (Subtype.restrict p f) ‚ü®j, h‚ü© else 0 := by
  classical
  ext i; dsimp
  rw [biproduct.fromSubtype, biproduct.Œπ_desc_assoc, biproduct.Œπ_œÄ]
  by_cases h : p j
  ¬∑ rw [dif_pos h, biproduct.Œπ_œÄ]
    split_ifs with h‚ÇÅ h‚ÇÇ h‚ÇÇ
    exacts [rfl, False.elim (h‚ÇÇ (Subtype.ext h‚ÇÅ)), False.elim (h‚ÇÅ (congr_arg Subtype.val h‚ÇÇ)), rfl]
  ¬∑ rw [dif_neg h, dif_neg (show (i : J) ‚â† j from fun h‚ÇÇ => h (h‚ÇÇ ‚ñ∏ i.2)), comp_zero]

theorem biproduct.fromSubtype_eq_lift [DecidablePred p] :
    biproduct.fromSubtype f p =
      biproduct.lift fun j => if h : p j then biproduct.œÄ (Subtype.restrict p f) ‚ü®j, h‚ü© else 0 :=
  biproduct.hom_ext _ _ (by simp)

@[reassoc] -- Porting note: both version solved using simp
theorem biproduct.fromSubtype_œÄ_subtype (j : Subtype p) :
    biproduct.fromSubtype f p ‚â´ biproduct.œÄ f j = biproduct.œÄ (Subtype.restrict p f) j := by
  classical
  ext
  rw [biproduct.fromSubtype, biproduct.Œπ_desc_assoc, biproduct.Œπ_œÄ, biproduct.Œπ_œÄ]
  split_ifs with h‚ÇÅ h‚ÇÇ h‚ÇÇ
  exacts [rfl, False.elim (h‚ÇÇ (Subtype.ext h‚ÇÅ)), False.elim (h‚ÇÅ (congr_arg Subtype.val h‚ÇÇ)), rfl]

@[reassoc (attr := simp)]
theorem biproduct.toSubtype_œÄ (j : Subtype p) :
    biproduct.toSubtype f p ‚â´ biproduct.œÄ (Subtype.restrict p f) j = biproduct.œÄ f j :=
  biproduct.lift_œÄ _ _

@[reassoc (attr := simp)]
theorem biproduct.Œπ_toSubtype [DecidablePred p] (j : J) :
    biproduct.Œπ f j ‚â´ biproduct.toSubtype f p =
      if h : p j then biproduct.Œπ (Subtype.restrict p f) ‚ü®j, h‚ü© else 0 := by
  classical
  ext i
  rw [biproduct.toSubtype, Category.assoc, biproduct.lift_œÄ, biproduct.Œπ_œÄ]
  by_cases h : p j
  ¬∑ rw [dif_pos h, biproduct.Œπ_œÄ]
    split_ifs with h‚ÇÅ h‚ÇÇ h‚ÇÇ
    exacts [rfl, False.elim (h‚ÇÇ (Subtype.ext h‚ÇÅ)), False.elim (h‚ÇÅ (congr_arg Subtype.val h‚ÇÇ)), rfl]
  ¬∑ rw [dif_neg h, dif_neg (show j ‚â† i from fun h‚ÇÇ => h (h‚ÇÇ.symm ‚ñ∏ i.2)), zero_comp]

theorem biproduct.toSubtype_eq_desc [DecidablePred p] :
    biproduct.toSubtype f p =
      biproduct.desc fun j => if h : p j then biproduct.Œπ (Subtype.restrict p f) ‚ü®j, h‚ü© else 0 :=
  biproduct.hom_ext' _ _ (by simp)

@[reassoc]
theorem biproduct.Œπ_toSubtype_subtype (j : Subtype p) :
    biproduct.Œπ f j ‚â´ biproduct.toSubtype f p = biproduct.Œπ (Subtype.restrict p f) j := by
  classical
  ext
  rw [biproduct.toSubtype, Category.assoc, biproduct.lift_œÄ, biproduct.Œπ_œÄ, biproduct.Œπ_œÄ]
  split_ifs with h‚ÇÅ h‚ÇÇ h‚ÇÇ
  exacts [rfl, False.elim (h‚ÇÇ (Subtype.ext h‚ÇÅ)), False.elim (h‚ÇÅ (congr_arg Subtype.val h‚ÇÇ)), rfl]

@[reassoc (attr := simp)]
theorem biproduct.Œπ_fromSubtype (j : Subtype p) :
    biproduct.Œπ (Subtype.restrict p f) j ‚â´ biproduct.fromSubtype f p = biproduct.Œπ f j :=
  biproduct.Œπ_desc _ _

@[reassoc (attr := simp)]
theorem biproduct.fromSubtype_toSubtype :
    biproduct.fromSubtype f p ‚â´ biproduct.toSubtype f p = ùüô (‚®Å Subtype.restrict p f) := by
  refine biproduct.hom_ext _ _ fun j => ?_
  rw [Category.assoc, biproduct.toSubtype_œÄ, biproduct.fromSubtype_œÄ_subtype, Category.id_comp]

@[reassoc (attr := simp)]
theorem biproduct.toSubtype_fromSubtype [DecidablePred p] :
    biproduct.toSubtype f p ‚â´ biproduct.fromSubtype f p =
      biproduct.map fun j => if p j then ùüô (f j) else 0 := by
  ext1 i
  by_cases h : p i
  ¬∑ simp [h]
  ¬∑ simp [h]

end

section

variable (f : J ‚Üí C) (i : J) [HasBiproduct f] [HasBiproduct (Subtype.restrict (fun j => j ‚â† i) f)]

open scoped Classical in
/-- The kernel of `biproduct.œÄ f i` is the inclusion from the biproduct which omits `i`
from the index set `J` into the biproduct over `J`. -/
def biproduct.isLimitFromSubtype :
    IsLimit (KernelFork.ofŒπ (biproduct.fromSubtype f fun j => j ‚â† i) (by simp) :
    KernelFork (biproduct.œÄ f i)) :=
  Fork.IsLimit.mk' _ fun s =>
    ‚ü®s.Œπ ‚â´ biproduct.toSubtype _ _, by
      apply biproduct.hom_ext; intro j
      rw [KernelFork.Œπ_ofŒπ, Category.assoc, Category.assoc,
        biproduct.toSubtype_fromSubtype_assoc, biproduct.map_œÄ]
      rcases Classical.em (i = j) with (rfl | h)
      ¬∑ rw [if_neg (Classical.not_not.2 rfl), comp_zero, comp_zero, KernelFork.condition]
      ¬∑ rw [if_pos (Ne.symm h), Category.comp_id], by
      intro m hm
      rw [‚Üê hm, KernelFork.Œπ_ofŒπ, Category.assoc, biproduct.fromSubtype_toSubtype]
      exact (Category.comp_id _).symm‚ü©

instance : HasKernel (biproduct.œÄ f i) :=
  HasLimit.mk ‚ü®_, biproduct.isLimitFromSubtype f i‚ü©

/-- The kernel of `biproduct.œÄ f i` is `‚®Å Subtype.restrict {i}·∂ú f`. -/
@[simps!]
def kernelBiproductœÄIso : kernel (biproduct.œÄ f i) ‚âÖ ‚®Å Subtype.restrict (fun j => j ‚â† i) f :=
  limit.isoLimitCone ‚ü®_, biproduct.isLimitFromSubtype f i‚ü©

open scoped Classical in
/-- The cokernel of `biproduct.Œπ f i` is the projection from the biproduct over the index set `J`
onto the biproduct omitting `i`. -/
def biproduct.isColimitToSubtype :
    IsColimit (CokernelCofork.ofœÄ (biproduct.toSubtype f fun j => j ‚â† i) (by simp) :
    CokernelCofork (biproduct.Œπ f i)) :=
  Cofork.IsColimit.mk' _ fun s =>
    ‚ü®biproduct.fromSubtype _ _ ‚â´ s.œÄ, by
      apply biproduct.hom_ext'; intro j
      rw [CokernelCofork.œÄ_ofœÄ, biproduct.toSubtype_fromSubtype_assoc, biproduct.Œπ_map_assoc]
      rcases Classical.em (i = j) with (rfl | h)
      ¬∑ rw [if_neg (Classical.not_not.2 rfl), zero_comp, CokernelCofork.condition]
      ¬∑ rw [if_pos (Ne.symm h), Category.id_comp], by
      intro m hm
      rw [‚Üê hm, CokernelCofork.œÄ_ofœÄ, ‚Üê Category.assoc, biproduct.fromSubtype_toSubtype]
      exact (Category.id_comp _).symm‚ü©

instance : HasCokernel (biproduct.Œπ f i) :=
  HasColimit.mk ‚ü®_, biproduct.isColimitToSubtype f i‚ü©

/-- The cokernel of `biproduct.Œπ f i` is `‚®Å Subtype.restrict {i}·∂ú f`. -/
@[simps!]
def cokernelBiproductŒπIso : cokernel (biproduct.Œπ f i) ‚âÖ ‚®Å Subtype.restrict (fun j => j ‚â† i) f :=
  colimit.isoColimitCocone ‚ü®_, biproduct.isColimitToSubtype f i‚ü©

end

section

-- Per https://github.com/leanprover-community/mathlib3/pull/15067, we only allow indexing in `Type 0` here.
variable {K : Type} [Finite K] [HasFiniteBiproducts C] (f : K ‚Üí C)

/-- The limit cone exhibiting `‚®Å Subtype.restrict p·∂ú f` as the kernel of
`biproduct.toSubtype f p` -/
@[simps]
def kernelForkBiproductToSubtype (p : Set K) :
    LimitCone (parallelPair (biproduct.toSubtype f p) 0) where
  cone :=
    KernelFork.ofŒπ (biproduct.fromSubtype f p·∂ú)
      (by
        classical
        ext j k
        simp only [Category.assoc, biproduct.Œπ_fromSubtype_assoc, biproduct.Œπ_toSubtype_assoc,
          comp_zero, zero_comp]
        rw [dif_neg k.2]
        simp only [zero_comp])
  isLimit :=
    KernelFork.IsLimit.ofŒπ _ _ (fun {_} g _ => g ‚â´ biproduct.toSubtype f p·∂ú)
      (by
        classical
        intro W' g' w
        ext j
        simp only [Category.assoc, biproduct.toSubtype_fromSubtype, Pi.compl_apply,
          biproduct.map_œÄ]
        split_ifs with h
        ¬∑ simp
        ¬∑ replace w := w =‚â´ biproduct.œÄ _ ‚ü®j, not_not.mp h‚ü©
          simpa using w.symm)
      (by aesop_cat)

instance (p : Set K) : HasKernel (biproduct.toSubtype f p) :=
  HasLimit.mk (kernelForkBiproductToSubtype f p)

/-- The kernel of `biproduct.toSubtype f p` is `‚®Å Subtype.restrict p·∂ú f`. -/
@[simps!]
def kernelBiproductToSubtypeIso (p : Set K) :
    kernel (biproduct.toSubtype f p) ‚âÖ ‚®Å Subtype.restrict p·∂ú f :=
  limit.isoLimitCone (kernelForkBiproductToSubtype f p)

/-- The colimit cocone exhibiting `‚®Å Subtype.restrict p·∂ú f` as the cokernel of
`biproduct.fromSubtype f p` -/
@[simps]
def cokernelCoforkBiproductFromSubtype (p : Set K) :
    ColimitCocone (parallelPair (biproduct.fromSubtype f p) 0) where
  cocone :=
    CokernelCofork.ofœÄ (biproduct.toSubtype f p·∂ú)
      (by
        classical
        ext j k
        simp only [Category.assoc, Pi.compl_apply, biproduct.Œπ_fromSubtype_assoc,
          biproduct.Œπ_toSubtype_assoc, comp_zero, zero_comp]
        rw [dif_neg]
        ¬∑ simp only [zero_comp]
        ¬∑ exact not_not.mpr k.2)
  isColimit :=
    CokernelCofork.IsColimit.ofœÄ _ _ (fun {_} g _ => biproduct.fromSubtype f p·∂ú ‚â´ g)
      (by
        classical
        intro W g' w
        ext j
        simp only [biproduct.toSubtype_fromSubtype_assoc, Pi.compl_apply, biproduct.Œπ_map_assoc]
        split_ifs with h
        ¬∑ simp
        ¬∑ replace w := biproduct.Œπ _ (‚ü®j, not_not.mp h‚ü© : p) ‚â´= w
          simpa using w.symm)
      (by aesop_cat)

instance (p : Set K) : HasCokernel (biproduct.fromSubtype f p) :=
  HasColimit.mk (cokernelCoforkBiproductFromSubtype f p)

/-- The cokernel of `biproduct.fromSubtype f p` is `‚®Å Subtype.restrict p·∂ú f`. -/
@[simps!]
def cokernelBiproductFromSubtypeIso (p : Set K) :
    cokernel (biproduct.fromSubtype f p) ‚âÖ ‚®Å Subtype.restrict p·∂ú f :=
  colimit.isoColimitCocone (cokernelCoforkBiproductFromSubtype f p)

end

end œÄKernel

section FiniteBiproducts

variable {J : Type} [Finite J] {K : Type} [Finite K] {C : Type u} [Category.{v} C]
  [HasZeroMorphisms C] [HasFiniteBiproducts C] {f : J ‚Üí C} {g : K ‚Üí C}

/-- Convert a (dependently typed) matrix to a morphism of biproducts. -/
def biproduct.matrix (m : ‚àÄ j k, f j ‚ü∂ g k) : ‚®Å f ‚ü∂ ‚®Å g :=
  biproduct.desc fun j => biproduct.lift fun k => m j k

@[reassoc (attr := simp)]
theorem biproduct.matrix_œÄ (m : ‚àÄ j k, f j ‚ü∂ g k) (k : K) :
    biproduct.matrix m ‚â´ biproduct.œÄ g k = biproduct.desc fun j => m j k := by
  ext
  simp [biproduct.matrix]

@[reassoc (attr := simp)]
theorem biproduct.Œπ_matrix (m : ‚àÄ j k, f j ‚ü∂ g k) (j : J) :
    biproduct.Œπ f j ‚â´ biproduct.matrix m = biproduct.lift fun k => m j k := by
  ext
  simp [biproduct.matrix]

/-- Extract the matrix components from a morphism of biproducts. -/
def biproduct.components (m : ‚®Å f ‚ü∂ ‚®Å g) (j : J) (k : K) : f j ‚ü∂ g k :=
  biproduct.Œπ f j ‚â´ m ‚â´ biproduct.œÄ g k

@[simp]
theorem biproduct.matrix_components (m : ‚àÄ j k, f j ‚ü∂ g k) (j : J) (k : K) :
    biproduct.components (biproduct.matrix m) j k = m j k := by simp [biproduct.components]

@[simp]
theorem biproduct.components_matrix (m : ‚®Å f ‚ü∂ ‚®Å g) :
    (biproduct.matrix fun j k => biproduct.components m j k) = m := by
  ext
  simp [biproduct.components]

/-- Morphisms between direct sums are matrices. -/
@[simps]
def biproduct.matrixEquiv : (‚®Å f ‚ü∂ ‚®Å g) ‚âÉ ‚àÄ j k, f j ‚ü∂ g k where
  toFun := biproduct.components
  invFun := biproduct.matrix
  left_inv := biproduct.components_matrix
  right_inv m := by
    ext
    apply biproduct.matrix_components

end FiniteBiproducts

variable {J : Type w}
variable {C : Type u} [Category.{v} C] [HasZeroMorphisms C]
variable {D : Type uD} [Category.{uD'} D] [HasZeroMorphisms D]

instance biproduct.Œπ_mono (f : J ‚Üí C) [HasBiproduct f] (b : J) : IsSplitMono (biproduct.Œπ f b) := by
  classical exact IsSplitMono.mk' { retraction := biproduct.desc <| Pi.single b (ùüô (f b)) }

instance biproduct.œÄ_epi (f : J ‚Üí C) [HasBiproduct f] (b : J) : IsSplitEpi (biproduct.œÄ f b) := by
  classical exact IsSplitEpi.mk' { section_ := biproduct.lift <| Pi.single b (ùüô (f b)) }

/-- Auxiliary lemma for `biproduct.uniqueUpToIso`. -/
theorem biproduct.conePointUniqueUpToIso_hom (f : J ‚Üí C) [HasBiproduct f] {b : Bicone f}
    (hb : b.IsBilimit) :
    (hb.isLimit.conePointUniqueUpToIso (biproduct.isLimit _)).hom = biproduct.lift b.œÄ :=
  rfl

/-- Auxiliary lemma for `biproduct.uniqueUpToIso`. -/
theorem biproduct.conePointUniqueUpToIso_inv (f : J ‚Üí C) [HasBiproduct f] {b : Bicone f}
    (hb : b.IsBilimit) :
    (hb.isLimit.conePointUniqueUpToIso (biproduct.isLimit _)).inv = biproduct.desc b.Œπ := by
  classical
  refine biproduct.hom_ext' _ _ fun j => hb.isLimit.hom_ext fun j' => ?_
  rw [Category.assoc, IsLimit.conePointUniqueUpToIso_inv_comp, Bicone.toCone_œÄ_app,
    biproduct.bicone_œÄ, biproduct.Œπ_desc, biproduct.Œπ_œÄ, b.toCone_œÄ_app, b.Œπ_œÄ]

/-- Biproducts are unique up to isomorphism. This already follows because bilimits are limits,
    but in the case of biproducts we can give an isomorphism with particularly nice definitional
    properties, namely that `biproduct.lift b.œÄ` and `biproduct.desc b.Œπ` are inverses of each
    other. -/
@[simps]
def biproduct.uniqueUpToIso (f : J ‚Üí C) [HasBiproduct f] {b : Bicone f} (hb : b.IsBilimit) :
    b.pt ‚âÖ ‚®Å f where
  hom := biproduct.lift b.œÄ
  inv := biproduct.desc b.Œπ
  hom_inv_id := by
    rw [‚Üê biproduct.conePointUniqueUpToIso_hom f hb, ‚Üê
      biproduct.conePointUniqueUpToIso_inv f hb, Iso.hom_inv_id]
  inv_hom_id := by
    rw [‚Üê biproduct.conePointUniqueUpToIso_hom f hb, ‚Üê
      biproduct.conePointUniqueUpToIso_inv f hb, Iso.inv_hom_id]

variable (C)

-- see Note [lower instance priority]
/-- A category with finite biproducts has a zero object. -/
instance (priority := 100) hasZeroObject_of_hasFiniteBiproducts [HasFiniteBiproducts C] :
    HasZeroObject C := by
  refine ‚ü®‚ü®biproduct Empty.elim, fun X => ‚ü®‚ü®‚ü®0‚ü©, ?_‚ü©‚ü©, fun X => ‚ü®‚ü®‚ü®0‚ü©, ?_‚ü©‚ü©‚ü©‚ü©
  ¬∑ intro a; apply biproduct.hom_ext'; simp
  ¬∑ intro a; apply biproduct.hom_ext; simp

section

variable {C}

attribute [local simp] eq_iff_true_of_subsingleton in
/-- The limit bicone for the biproduct over an index type with exactly one term. -/
@[simps]
def limitBiconeOfUnique [Unique J] (f : J ‚Üí C) : LimitBicone f where
  bicone :=
    { pt := f default
      œÄ := fun j => eqToHom (by congr; rw [‚Üê Unique.uniq] )
      Œπ := fun j => eqToHom (by congr; rw [‚Üê Unique.uniq] ) }
  isBilimit :=
    { isLimit := (limitConeOfUnique f).isLimit
      isColimit := (colimitCoconeOfUnique f).isColimit }

instance (priority := 100) hasBiproduct_unique [Subsingleton J] [Nonempty J] (f : J ‚Üí C) :
    HasBiproduct f :=
  let ‚ü®_‚ü© := nonempty_unique J; .mk (limitBiconeOfUnique f)

/-- A biproduct over an index type with exactly one term is just the object over that term. -/
@[simps!]
def biproductUniqueIso [Unique J] (f : J ‚Üí C) : ‚®Å f ‚âÖ f default :=
  (biproduct.uniqueUpToIso _ (limitBiconeOfUnique f).isBilimit).symm

end

end CategoryTheory.Limits
