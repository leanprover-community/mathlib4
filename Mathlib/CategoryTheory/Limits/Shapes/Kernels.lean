/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Markus Himmel
-/
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Zero

/-!
# Kernels and cokernels

In a category with zero morphisms, the kernel of a morphism `f : X âŸ¶ Y` is
the equalizer of `f` and `0 : X âŸ¶ Y`. (Similarly the cokernel is the coequalizer.)

The basic definitions are
* `kernel : (X âŸ¶ Y) â†’ C`

* `kernel.Î¹ : kernel f âŸ¶ X`
* `kernel.condition : kernel.Î¹ f â‰« f = 0` and
* `kernel.lift (k : W âŸ¶ X) (h : k â‰« f = 0) : W âŸ¶ kernel f` (as well as the dual versions)

## Main statements

Besides the definition and lifts, we prove
* `kernel.Î¹ZeroIsIso`: a kernel map of a zero morphism is an isomorphism
* `kernel.eq_zero_of_epi_kernel`: if `kernel.Î¹ f` is an epimorphism, then `f = 0`
* `kernel.ofMono`: the kernel of a monomorphism is the zero object
* `kernel.liftMono`: the lift of a monomorphism `k : W âŸ¶ X` such that `k â‰« f = 0`
  is still a monomorphism
* `kernel.isLimitConeZeroCone`: if our category has a zero object, then the map from the zero
  object is a kernel map of any monomorphism
* `kernel.Î¹OfZero`: `kernel.Î¹ (0 : X âŸ¶ Y)` is an isomorphism

and the corresponding dual statements.

## Future work
* TODO: connect this with existing work in the group theory and ring theory libraries.

## Implementation notes
As with the other special shapes in the limits library, all the definitions here are given as
`abbreviation`s of the general statements for limits, so all the `simp` lemmas and theorems about
general limits can be used.

## References

* [F. Borceux, *Handbook of Categorical Algebra 2*][borceux-vol2]
-/


noncomputable section

universe v vâ‚‚ u u' uâ‚‚

open CategoryTheory

open CategoryTheory.Limits.WalkingParallelPair

namespace CategoryTheory.Limits

variable {C : Type u} [Category.{v} C]
variable [HasZeroMorphisms C]

/-- A morphism `f` has a kernel if the functor `ParallelPair f 0` has a limit. -/
abbrev HasKernel {X Y : C} (f : X âŸ¶ Y) : Prop :=
  HasLimit (parallelPair f 0)

/-- A morphism `f` has a cokernel if the functor `ParallelPair f 0` has a colimit. -/
abbrev HasCokernel {X Y : C} (f : X âŸ¶ Y) : Prop :=
  HasColimit (parallelPair f 0)

variable {X Y : C} (f : X âŸ¶ Y)

section

/-- A kernel fork is just a fork where the second morphism is a zero morphism. -/
abbrev KernelFork :=
  Fork f 0

variable {f}

@[reassoc (attr := simp)]
theorem KernelFork.condition (s : KernelFork f) : Fork.Î¹ s â‰« f = 0 := by
  rw [Fork.condition, HasZeroMorphisms.comp_zero]

theorem KernelFork.app_one (s : KernelFork f) : s.Ï€.app one = 0 := by
  simp

/-- A morphism `Î¹` satisfying `Î¹ â‰« f = 0` determines a kernel fork over `f`. -/
abbrev KernelFork.ofÎ¹ {Z : C} (Î¹ : Z âŸ¶ X) (w : Î¹ â‰« f = 0) : KernelFork f :=
  Fork.ofÎ¹ Î¹ <| by rw [w, HasZeroMorphisms.comp_zero]

@[simp]
theorem KernelFork.Î¹_ofÎ¹ {X Y P : C} (f : X âŸ¶ Y) (Î¹ : P âŸ¶ X) (w : Î¹ â‰« f = 0) :
    Fork.Î¹ (KernelFork.ofÎ¹ Î¹ w) = Î¹ := rfl

section

attribute [local aesop safe cases] WalkingParallelPair WalkingParallelPairHom

/-- Every kernel fork `s` is isomorphic (actually, equal) to `fork.ofÎ¹ (fork.Î¹ s) _`. -/
def isoOfÎ¹ (s : Fork f 0) : s â‰… Fork.ofÎ¹ (Fork.Î¹ s) (Fork.condition s) :=
  Cones.ext (Iso.refl _) <| by aesop

/-- If `Î¹ = Î¹'`, then `fork.ofÎ¹ Î¹ _` and `fork.ofÎ¹ Î¹' _` are isomorphic. -/
def ofÎ¹Congr {P : C} {Î¹ Î¹' : P âŸ¶ X} {w : Î¹ â‰« f = 0} (h : Î¹ = Î¹') :
    KernelFork.ofÎ¹ Î¹ w â‰… KernelFork.ofÎ¹ Î¹' (by rw [â† h, w]) :=
  Cones.ext (Iso.refl _)

/-- If `F` is an equivalence, then applying `F` to a diagram indexing a (co)kernel of `f` yields
the diagram indexing the (co)kernel of `F.map f`. -/
def compNatIso {D : Type u'} [Category.{v} D] [HasZeroMorphisms D] (F : C â¥¤ D) [F.IsEquivalence] :
    parallelPair f 0 â‹™ F â‰… parallelPair (F.map f) 0 :=
  let app (j : WalkingParallelPair) :
      (parallelPair f 0 â‹™ F).obj j â‰… (parallelPair (F.map f) 0).obj j :=
    match j with
    | zero => Iso.refl _
    | one => Iso.refl _
  NatIso.ofComponents app <| by rintro âŸ¨iâŸ© âŸ¨jâŸ© <;> rintro (g | g) <;> aesop

end

/-- If `s` is a limit kernel fork and `k : W âŸ¶ X` satisfies `k â‰« f = 0`, then there is some
`l : W âŸ¶ s.X` such that `l â‰« fork.Î¹ s = k`. -/
def KernelFork.IsLimit.lift' {s : KernelFork f} (hs : IsLimit s) {W : C} (k : W âŸ¶ X)
    (h : k â‰« f = 0) : { l : W âŸ¶ s.pt // l â‰« Fork.Î¹ s = k } :=
  âŸ¨hs.lift <| KernelFork.ofÎ¹ _ h, hs.fac _ _âŸ©

/-- This is a slightly more convenient method to verify that a kernel fork is a limit cone. It
only asks for a proof of facts that carry any mathematical content -/
def isLimitAux (t : KernelFork f) (lift : âˆ€ s : KernelFork f, s.pt âŸ¶ t.pt)
    (fac : âˆ€ s : KernelFork f, lift s â‰« t.Î¹ = s.Î¹)
    (uniq : âˆ€ (s : KernelFork f) (m : s.pt âŸ¶ t.pt) (_ : m â‰« t.Î¹ = s.Î¹), m = lift s) : IsLimit t :=
  { lift
    fac := fun s j => by
      cases j
      Â· exact fac s
      Â· simp
    uniq := fun s m w => uniq s m (w Limits.WalkingParallelPair.zero) }

/-- This is a more convenient formulation to show that a `KernelFork` constructed using
`KernelFork.ofÎ¹` is a limit cone.
-/
def KernelFork.IsLimit.ofÎ¹ {W : C} (g : W âŸ¶ X) (eq : g â‰« f = 0)
    (lift : âˆ€ {W' : C} (g' : W' âŸ¶ X) (_ : g' â‰« f = 0), W' âŸ¶ W)
    (fac : âˆ€ {W' : C} (g' : W' âŸ¶ X) (eq' : g' â‰« f = 0), lift g' eq' â‰« g = g')
    (uniq :
      âˆ€ {W' : C} (g' : W' âŸ¶ X) (eq' : g' â‰« f = 0) (m : W' âŸ¶ W) (_ : m â‰« g = g'), m = lift g' eq') :
    IsLimit (KernelFork.ofÎ¹ g eq) :=
  isLimitAux _ (fun s => lift s.Î¹ s.condition) (fun s => fac s.Î¹ s.condition) fun s =>
    uniq s.Î¹ s.condition

/-- This is a more convenient formulation to show that a `KernelFork` of the form
`KernelFork.ofÎ¹ i _` is a limit cone when we know that `i` is a monomorphism. -/
def KernelFork.IsLimit.ofÎ¹' {X Y K : C} {f : X âŸ¶ Y} (i : K âŸ¶ X) (w : i â‰« f = 0)
    (h : âˆ€ {A : C} (k : A âŸ¶ X) (_ : k â‰« f = 0), { l : A âŸ¶ K // l â‰« i = k}) [hi : Mono i] :
    IsLimit (KernelFork.ofÎ¹ i w) :=
  ofÎ¹ _ _ (fun {_} k hk => (h k hk).1) (fun {_} k hk => (h k hk).2) (fun {A} k hk m hm => by
    rw [â† cancel_mono i, (h k hk).2, hm])

/-- Every kernel of `f` induces a kernel of `f â‰« g` if `g` is mono. -/
def isKernelCompMono {c : KernelFork f} (i : IsLimit c) {Z} (g : Y âŸ¶ Z) [hg : Mono g] {h : X âŸ¶ Z}
    (hh : h = f â‰« g) : IsLimit (KernelFork.ofÎ¹ c.Î¹ (by simp [hh]) : KernelFork h) :=
  Fork.IsLimit.mk' _ fun s =>
    let s' : KernelFork f := Fork.ofÎ¹ s.Î¹ (by rw [â† cancel_mono g]; simp [â† hh, s.condition])
    let l := KernelFork.IsLimit.lift' i s'.Î¹ s'.condition
    âŸ¨l.1, l.2, fun hm => by
      apply Fork.IsLimit.hom_ext i; rw [Fork.Î¹_ofÎ¹] at hm; rw [hm]; exact l.2.symmâŸ©

theorem isKernelCompMono_lift {c : KernelFork f} (i : IsLimit c) {Z} (g : Y âŸ¶ Z) [hg : Mono g]
    {h : X âŸ¶ Z} (hh : h = f â‰« g) (s : KernelFork h) :
    (isKernelCompMono i g hh).lift s = i.lift (Fork.ofÎ¹ s.Î¹ (by
      rw [â† cancel_mono g, Category.assoc, â† hh]
      simp)) := rfl

/-- Every kernel of `f â‰« g` is also a kernel of `f`, as long as `c.Î¹ â‰« f` vanishes. -/
def isKernelOfComp {W : C} (g : Y âŸ¶ W) (h : X âŸ¶ W) {c : KernelFork h} (i : IsLimit c)
    (hf : c.Î¹ â‰« f = 0) (hfg : f â‰« g = h) : IsLimit (KernelFork.ofÎ¹ c.Î¹ hf) :=
  Fork.IsLimit.mk _ (fun s => i.lift (KernelFork.ofÎ¹ s.Î¹ (by simp [â† hfg])))
    (fun s => by simp only [KernelFork.Î¹_ofÎ¹, Fork.IsLimit.lift_Î¹]) fun s m h => by
    apply Fork.IsLimit.hom_ext i; simpa using h

/-- `X` identifies to the kernel of a zero map `X âŸ¶ Y`. -/
def KernelFork.IsLimit.ofId {X Y : C} (f : X âŸ¶ Y) (hf : f = 0) :
    IsLimit (KernelFork.ofÎ¹ (ğŸ™ X) (show ğŸ™ X â‰« f = 0 by rw [hf, comp_zero])) :=
  KernelFork.IsLimit.ofÎ¹ _ _ (fun x _ => x) (fun _ _ => Category.comp_id _)
    (fun _ _ _ hb => by simp only [â† hb, Category.comp_id])

/-- Any zero object identifies to the kernel of a given monomorphisms. -/
def KernelFork.IsLimit.ofMonoOfIsZero {X Y : C} {f : X âŸ¶ Y} (c : KernelFork f)
    (hf : Mono f) (h : IsZero c.pt) : IsLimit c :=
  isLimitAux _ (fun _ => 0) (fun s => by rw [zero_comp, â† cancel_mono f, zero_comp, s.condition])
    (fun _ _ _ => h.eq_of_tgt _ _)

lemma KernelFork.IsLimit.isIso_Î¹ {X Y : C} {f : X âŸ¶ Y} (c : KernelFork f)
    (hc : IsLimit c) (hf : f = 0) : IsIso c.Î¹ := isIso_limit_cone_parallelPair_of_eq hf hc

/-- If `c` is a limit kernel fork for `g : X âŸ¶ Y`, `e : X â‰… X'` and `g' : X' âŸ¶ Y` is a morphism,
then there is a limit kernel fork for `g'` with the same point as `c` if for any
morphism `Ï† : W âŸ¶ X`, there is an equivalence `Ï† â‰« g = 0 â†” Ï† â‰« e.hom â‰« g' = 0`. -/
def KernelFork.isLimitOfIsLimitOfIff {X Y : C} {g : X âŸ¶ Y} {c : KernelFork g} (hc : IsLimit c)
    {X' Y' : C} (g' : X' âŸ¶ Y') (e : X â‰… X')
    (iff : âˆ€ â¦ƒW : Câ¦„ (Ï† : W âŸ¶ X), Ï† â‰« g = 0 â†” Ï† â‰« e.hom â‰« g' = 0) :
    IsLimit (KernelFork.ofÎ¹ (f := g') (c.Î¹ â‰« e.hom) (by simp [â† iff])) :=
  KernelFork.IsLimit.ofÎ¹ _ _
    (fun s hs â†¦ hc.lift (KernelFork.ofÎ¹ (Î¹ := s â‰« e.inv)
      (by rw [iff, Category.assoc, Iso.inv_hom_id_assoc, hs])))
    (fun s hs â†¦ by simp)
    (fun s hs m hm â†¦ Fork.IsLimit.hom_ext hc (by simpa [â† cancel_mono e.hom] using hm))

/-- If `c` is a limit kernel fork for `g : X âŸ¶ Y`, and `g' : X âŸ¶ Y'` is a another morphism,
then there is a limit kernel fork for `g'` with the same point as `c` if for any
morphism `Ï† : W âŸ¶ X`, there is an equivalence `Ï† â‰« g = 0 â†” Ï† â‰« g' = 0`. -/
def KernelFork.isLimitOfIsLimitOfIff' {X Y : C} {g : X âŸ¶ Y} {c : KernelFork g} (hc : IsLimit c)
    {Y' : C} (g' : X âŸ¶ Y')
    (iff : âˆ€ â¦ƒW : Câ¦„ (Ï† : W âŸ¶ X), Ï† â‰« g = 0 â†” Ï† â‰« g' = 0) :
    IsLimit (KernelFork.ofÎ¹ (f := g') c.Î¹ (by simp [â† iff])) :=
  IsLimit.ofIsoLimit (isLimitOfIsLimitOfIff hc g' (Iso.refl _) (by simpa using iff))
    (Fork.ext (Iso.refl _))

lemma KernelFork.IsLimit.isZero_of_mono {X Y : C} {f : X âŸ¶ Y}
    {c : KernelFork f} (hc : IsLimit c) [Mono f] : IsZero c.pt := by
  have := Fork.IsLimit.mono hc
  rw [IsZero.iff_id_eq_zero, â† cancel_mono c.Î¹, â† cancel_mono f, Category.assoc,
    Category.assoc, c.condition, comp_zero, zero_comp]

end

namespace KernelFork

variable {f} {X' Y' : C} {f' : X' âŸ¶ Y'}

/-- The morphism between points of kernel forks induced by a morphism
in the category of arrows. -/
def mapOfIsLimit (kf : KernelFork f) {kf' : KernelFork f'} (hf' : IsLimit kf')
    (Ï† : Arrow.mk f âŸ¶ Arrow.mk f') : kf.pt âŸ¶ kf'.pt :=
  hf'.lift (KernelFork.ofÎ¹ (kf.Î¹ â‰« Ï†.left) (by simp))

@[reassoc (attr := simp)]
lemma mapOfIsLimit_Î¹ (kf : KernelFork f) {kf' : KernelFork f'} (hf' : IsLimit kf')
    (Ï† : Arrow.mk f âŸ¶ Arrow.mk f') :
    kf.mapOfIsLimit hf' Ï† â‰« kf'.Î¹ = kf.Î¹ â‰« Ï†.left :=
  hf'.fac _ _

/-- The isomorphism between points of limit kernel forks induced by an isomorphism
in the category of arrows. -/
@[simps]
def mapIsoOfIsLimit {kf : KernelFork f} {kf' : KernelFork f'}
    (hf : IsLimit kf) (hf' : IsLimit kf')
    (Ï† : Arrow.mk f â‰… Arrow.mk f') : kf.pt â‰… kf'.pt where
  hom := kf.mapOfIsLimit hf' Ï†.hom
  inv := kf'.mapOfIsLimit hf Ï†.inv
  hom_inv_id := Fork.IsLimit.hom_ext hf (by simp)
  inv_hom_id := Fork.IsLimit.hom_ext hf' (by simp)

end KernelFork

section

variable [HasKernel f]

/-- The kernel of a morphism, expressed as the equalizer with the 0 morphism. -/
abbrev kernel (f : X âŸ¶ Y) [HasKernel f] : C :=
  equalizer f 0

/-- The map from `kernel f` into the source of `f`. -/
abbrev kernel.Î¹ : kernel f âŸ¶ X :=
  equalizer.Î¹ f 0

@[simp]
theorem equalizer_as_kernel : equalizer.Î¹ f 0 = kernel.Î¹ f := rfl

@[reassoc (attr := simp)]
theorem kernel.condition : kernel.Î¹ f â‰« f = 0 :=
  KernelFork.condition _

/-- The kernel built from `kernel.Î¹ f` is limiting. -/
def kernelIsKernel : IsLimit (Fork.ofÎ¹ (kernel.Î¹ f) ((kernel.condition f).trans comp_zero.symm)) :=
  IsLimit.ofIsoLimit (limit.isLimit _) (Fork.ext (Iso.refl _) (by simp))

/-- Given any morphism `k : W âŸ¶ X` satisfying `k â‰« f = 0`, `k` factors through `kernel.Î¹ f`
via `kernel.lift : W âŸ¶ kernel f`. -/
abbrev kernel.lift {W : C} (k : W âŸ¶ X) (h : k â‰« f = 0) : W âŸ¶ kernel f :=
  (kernelIsKernel f).lift (KernelFork.ofÎ¹ k h)

@[reassoc (attr := simp)]
theorem kernel.lift_Î¹ {W : C} (k : W âŸ¶ X) (h : k â‰« f = 0) : kernel.lift f k h â‰« kernel.Î¹ f = k :=
  (kernelIsKernel f).fac (KernelFork.ofÎ¹ k h) WalkingParallelPair.zero

@[simp]
theorem kernel.lift_zero {W : C} {h} : kernel.lift f (0 : W âŸ¶ X) h = 0 := by
  ext; simp

instance kernel.lift_mono {W : C} (k : W âŸ¶ X) (h : k â‰« f = 0) [Mono k] : Mono (kernel.lift f k h) :=
  âŸ¨fun {Z} g g' w => by
    replace w := w =â‰« kernel.Î¹ f
    simp only [Category.assoc, kernel.lift_Î¹] at w
    exact (cancel_mono k).1 wâŸ©

/-- Any morphism `k : W âŸ¶ X` satisfying `k â‰« f = 0` induces a morphism `l : W âŸ¶ kernel f` such that
`l â‰« kernel.Î¹ f = k`. -/
def kernel.lift' {W : C} (k : W âŸ¶ X) (h : k â‰« f = 0) : { l : W âŸ¶ kernel f // l â‰« kernel.Î¹ f = k } :=
  âŸ¨kernel.lift f k h, kernel.lift_Î¹ _ _ _âŸ©

/-- A commuting square induces a morphism of kernels. -/
abbrev kernel.map {X' Y' : C} (f' : X' âŸ¶ Y') [HasKernel f'] (p : X âŸ¶ X') (q : Y âŸ¶ Y')
    (w : f â‰« q = p â‰« f') : kernel f âŸ¶ kernel f' :=
  kernel.lift f' (kernel.Î¹ f â‰« p) (by simp [â† w])

@[simp]
lemma kernel.map_id {X Y : C} (f : X âŸ¶ Y) [HasKernel f] (q : Y âŸ¶ Y)
    (w : f â‰« q = ğŸ™ _ â‰« f) : kernel.map f f (ğŸ™ _) q w = ğŸ™ _ := by
  aesop

/-- Given a commutative diagram
```
    X --f--> Y --g--> Z
    |        |        |
    |        |        |
    v        v        v
    X' -f'-> Y' -g'-> Z'
```
with horizontal arrows composing to zero,
then we obtain a commutative square
```
   X ---> kernel g
   |         |
   |         | kernel.map
   |         |
   v         v
   X' --> kernel g'
```
-/
theorem kernel.lift_map {X Y Z X' Y' Z' : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [HasKernel g] (w : f â‰« g = 0)
    (f' : X' âŸ¶ Y') (g' : Y' âŸ¶ Z') [HasKernel g'] (w' : f' â‰« g' = 0) (p : X âŸ¶ X') (q : Y âŸ¶ Y')
    (r : Z âŸ¶ Z') (hâ‚ : f â‰« q = p â‰« f') (hâ‚‚ : g â‰« r = q â‰« g') :
    kernel.lift g f w â‰« kernel.map g g' q r hâ‚‚ = p â‰« kernel.lift g' f' w' := by
  ext; simp [hâ‚]

@[simp]
lemma kernel.map_zero {X Y X' Y' : C} (f : X âŸ¶ Y) (f' : X' âŸ¶ Y') [HasKernel f] [HasKernel f']
    (q : Y âŸ¶ Y') (w : f â‰« q = 0 â‰« f') : kernel.map f f' 0 q w = 0 := by
  aesop

/-- A commuting square of isomorphisms induces an isomorphism of kernels. -/
@[simps]
def kernel.mapIso {X' Y' : C} (f' : X' âŸ¶ Y') [HasKernel f'] (p : X â‰… X') (q : Y â‰… Y')
    (w : f â‰« q.hom = p.hom â‰« f') : kernel f â‰… kernel f' where
  hom := kernel.map f f' p.hom q.hom w
  inv :=
    kernel.map f' f p.inv q.inv
      (by
        refine (cancel_mono q.hom).1 ?_
        simp [w])

/-- Every kernel of the zero morphism is an isomorphism -/
instance kernel.Î¹_zero_isIso : IsIso (kernel.Î¹ (0 : X âŸ¶ Y)) :=
  equalizer.Î¹_of_self _

theorem eq_zero_of_epi_kernel [Epi (kernel.Î¹ f)] : f = 0 :=
  (cancel_epi (kernel.Î¹ f)).1 (by simp)

/-- The kernel of a zero morphism is isomorphic to the source. -/
def kernelZeroIsoSource : kernel (0 : X âŸ¶ Y) â‰… X :=
  equalizer.isoSourceOfSelf 0

@[simp]
theorem kernelZeroIsoSource_hom : kernelZeroIsoSource.hom = kernel.Î¹ (0 : X âŸ¶ Y) := rfl

@[simp]
theorem kernelZeroIsoSource_inv :
    kernelZeroIsoSource.inv = kernel.lift (0 : X âŸ¶ Y) (ğŸ™ X) (by simp) := by
  ext
  simp [kernelZeroIsoSource]

/-- If two morphisms are known to be equal, then their kernels are isomorphic. -/
def kernelIsoOfEq {f g : X âŸ¶ Y} [HasKernel f] [HasKernel g] (h : f = g) : kernel f â‰… kernel g :=
  HasLimit.isoOfNatIso (by rw [h])

@[simp]
theorem kernelIsoOfEq_refl {h : f = f} : kernelIsoOfEq h = Iso.refl (kernel f) := by
  ext
  simp [kernelIsoOfEq]

@[reassoc (attr := simp)]
theorem kernelIsoOfEq_hom_comp_Î¹ {f g : X âŸ¶ Y} [HasKernel f] [HasKernel g] (h : f = g) :
    (kernelIsoOfEq h).hom â‰« kernel.Î¹ g = kernel.Î¹ f := by
  subst h; simp

@[reassoc (attr := simp)]
theorem kernelIsoOfEq_inv_comp_Î¹ {f g : X âŸ¶ Y} [HasKernel f] [HasKernel g] (h : f = g) :
    (kernelIsoOfEq h).inv â‰« kernel.Î¹ _ = kernel.Î¹ _ := by
  subst h; simp

@[reassoc (attr := simp)]
theorem lift_comp_kernelIsoOfEq_hom {Z} {f g : X âŸ¶ Y} [HasKernel f] [HasKernel g] (h : f = g)
    (e : Z âŸ¶ X) (he) :
    kernel.lift _ e he â‰« (kernelIsoOfEq h).hom = kernel.lift _ e (by simp [â† h, he]) := by
  subst h; simp

@[reassoc (attr := simp)]
theorem lift_comp_kernelIsoOfEq_inv {Z} {f g : X âŸ¶ Y} [HasKernel f] [HasKernel g] (h : f = g)
    (e : Z âŸ¶ X) (he) :
    kernel.lift _ e he â‰« (kernelIsoOfEq h).inv = kernel.lift _ e (by simp [h, he]) := by
  cases h; simp

@[simp]
theorem kernelIsoOfEq_trans {f g h : X âŸ¶ Y} [HasKernel f] [HasKernel g] [HasKernel h] (wâ‚ : f = g)
    (wâ‚‚ : g = h) : kernelIsoOfEq wâ‚ â‰ªâ‰« kernelIsoOfEq wâ‚‚ = kernelIsoOfEq (wâ‚.trans wâ‚‚) := by
  cases wâ‚; cases wâ‚‚; ext; simp [kernelIsoOfEq]

variable {f}

theorem kernel_not_epi_of_nonzero (w : f â‰  0) : Â¬Epi (kernel.Î¹ f) := fun _ =>
  w (eq_zero_of_epi_kernel f)

theorem kernel_not_iso_of_nonzero (w : f â‰  0) : IsIso (kernel.Î¹ f) â†’ False := fun _ =>
  kernel_not_epi_of_nonzero w inferInstance

instance hasKernel_comp_mono {X Y Z : C} (f : X âŸ¶ Y) [HasKernel f] (g : Y âŸ¶ Z) [Mono g] :
    HasKernel (f â‰« g) :=
  âŸ¨âŸ¨{   cone := _
        isLimit := isKernelCompMono (limit.isLimit _) g rfl }âŸ©âŸ©

/-- When `g` is a monomorphism, the kernel of `f â‰« g` is isomorphic to the kernel of `f`.
-/
@[simps]
def kernelCompMono {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [HasKernel f] [Mono g] :
    kernel (f â‰« g) â‰… kernel f where
  hom :=
    kernel.lift _ (kernel.Î¹ _)
      (by
        rw [â† cancel_mono g]
        simp)
  inv := kernel.lift _ (kernel.Î¹ _) (by simp)

instance hasKernel_iso_comp {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [IsIso f] [HasKernel g] :
    HasKernel (f â‰« g) where
  exists_limit :=
    âŸ¨{  cone := KernelFork.ofÎ¹ (kernel.Î¹ g â‰« inv f) (by simp)
        isLimit := isLimitAux _ (fun s => kernel.lift _ (s.Î¹ â‰« f) (by simp))
            (by simp) fun s (m : _ âŸ¶ kernel _) w => by
          simp_rw [â† w]
          apply equalizer.hom_ext
          simp }âŸ©

/-- When `f` is an isomorphism, the kernel of `f â‰« g` is isomorphic to the kernel of `g`.
-/
@[simps]
def kernelIsIsoComp {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [IsIso f] [HasKernel g] :
    kernel (f â‰« g) â‰… kernel g where
  hom := kernel.lift _ (kernel.Î¹ _ â‰« f) (by simp)
  inv := kernel.lift _ (kernel.Î¹ _ â‰« inv f) (by simp)

/-- Equal maps have isomorphic kernels. -/
@[simps] def kernel.congr {X Y : C} (f g : X âŸ¶ Y) [HasKernel f] [HasKernel g]
    (h : f = g) : kernel f â‰… kernel g where
  hom := kernel.lift _ (kernel.Î¹ f) (by simp [â† h])
  inv := kernel.lift _ (kernel.Î¹ g) (by simp [h])

lemma isZero_kernel_of_mono {X Y : C} (f : X âŸ¶ Y) [Mono f] [HasKernel f] :
    IsZero (kernel f) :=
  KernelFork.IsLimit.isZero_of_mono (c := KernelFork.ofÎ¹ _ (kernel.condition f))
    (kernelIsKernel f)

end

section HasZeroObject

variable [HasZeroObject C]

open ZeroObject

/-- The morphism from the zero object determines a cone on a kernel diagram -/
def kernel.zeroKernelFork : KernelFork f where
  pt := 0
  Ï€ := { app := fun _ => 0 }

/-- The map from the zero object is a kernel of a monomorphism -/
def kernel.isLimitConeZeroCone [Mono f] : IsLimit (kernel.zeroKernelFork f) :=
  Fork.IsLimit.mk _ (fun _ => 0)
    (fun s => by
      rw [zero_comp]
      refine (zero_of_comp_mono f ?_).symm
      exact KernelFork.condition _)
    fun _ _ _ => zero_of_to_zero _

/-- The kernel of a monomorphism is isomorphic to the zero object -/
def kernel.ofMono [HasKernel f] [Mono f] : kernel f â‰… 0 :=
  Functor.mapIso (Cones.forget _) <|
    IsLimit.uniqueUpToIso (limit.isLimit (parallelPair f 0)) (kernel.isLimitConeZeroCone f)

/-- The kernel morphism of a monomorphism is a zero morphism -/
theorem kernel.Î¹_of_mono [HasKernel f] [Mono f] : kernel.Î¹ f = 0 :=
  zero_of_source_iso_zero _ (kernel.ofMono f)

/-- If `g â‰« f = 0` implies `g = 0` for all `g`, then `0 : 0 âŸ¶ X` is a kernel of `f`. -/
def zeroKernelOfCancelZero {X Y : C} (f : X âŸ¶ Y)
    (hf : âˆ€ (Z : C) (g : Z âŸ¶ X) (_ : g â‰« f = 0), g = 0) :
    IsLimit (KernelFork.ofÎ¹ (0 : 0 âŸ¶ X) (show 0 â‰« f = 0 by simp)) :=
  Fork.IsLimit.mk _ (fun _ => 0) (fun s => by rw [hf _ _ (KernelFork.condition s), zero_comp])
    fun s m _ => by dsimp; apply HasZeroObject.to_zero_ext

end HasZeroObject

section Transport

/-- If `i` is an isomorphism such that `l â‰« i.hom = f`, any kernel of `f` is a kernel of `l`. -/
def IsKernel.ofCompIso {Z : C} (l : X âŸ¶ Z) (i : Z â‰… Y) (h : l â‰« i.hom = f) {s : KernelFork f}
    (hs : IsLimit s) :
    IsLimit
      (KernelFork.ofÎ¹ (Fork.Î¹ s) <| show Fork.Î¹ s â‰« l = 0 by simp [â† i.comp_inv_eq.2 h.symm]) :=
  Fork.IsLimit.mk _ (fun s => hs.lift <| KernelFork.ofÎ¹ (Fork.Î¹ s) <| by simp [â† h])
    (fun s => by simp) fun s m h => by
      apply Fork.IsLimit.hom_ext hs
      simpa using h

/-- If `i` is an isomorphism such that `l â‰« i.hom = f`, the kernel of `f` is a kernel of `l`. -/
def kernel.ofCompIso [HasKernel f] {Z : C} (l : X âŸ¶ Z) (i : Z â‰… Y) (h : l â‰« i.hom = f) :
    IsLimit
      (KernelFork.ofÎ¹ (kernel.Î¹ f) <| show kernel.Î¹ f â‰« l = 0 by simp [â† i.comp_inv_eq.2 h.symm]) :=
  IsKernel.ofCompIso f l i h <| limit.isLimit _

/-- If `s` is any limit kernel cone over `f` and if `i` is an isomorphism such that
`i.hom â‰« s.Î¹ = l`, then `l` is a kernel of `f`. -/
def IsKernel.isoKernel {Z : C} (l : Z âŸ¶ X) {s : KernelFork f} (hs : IsLimit s) (i : Z â‰… s.pt)
    (h : i.hom â‰« Fork.Î¹ s = l) : IsLimit (KernelFork.ofÎ¹ l <| show l â‰« f = 0 by simp [â† h]) :=
  IsLimit.ofIsoLimit hs <|
    Cones.ext i.symm fun j => by
      cases j
      Â· exact (Iso.eq_inv_comp i).2 h
      Â· dsimp; rw [â† h]; simp

/-- If `i` is an isomorphism such that `i.hom â‰« kernel.Î¹ f = l`, then `l` is a kernel of `f`. -/
def kernel.isoKernel [HasKernel f] {Z : C} (l : Z âŸ¶ X) (i : Z â‰… kernel f)
    (h : i.hom â‰« kernel.Î¹ f = l) :
    IsLimit (@KernelFork.ofÎ¹ _ _ _ _ _ f _ l <| by simp [â† h]) :=
  IsKernel.isoKernel f l (limit.isLimit _) i h

end Transport

section

variable (X Y)

/-- The kernel morphism of a zero morphism is an isomorphism -/
theorem kernel.Î¹_of_zero : IsIso (kernel.Î¹ (0 : X âŸ¶ Y)) :=
  equalizer.Î¹_of_self _

end

section

/-- A cokernel cofork is just a cofork where the second morphism is a zero morphism. -/
abbrev CokernelCofork :=
  Cofork f 0

variable {f}

@[reassoc (attr := simp)]
theorem CokernelCofork.condition (s : CokernelCofork f) : f â‰« s.Ï€ = 0 := by
  rw [Cofork.condition, zero_comp]

theorem CokernelCofork.Ï€_eq_zero (s : CokernelCofork f) : s.Î¹.app zero = 0 := by
  simp

/-- A morphism `Ï€` satisfying `f â‰« Ï€ = 0` determines a cokernel cofork on `f`. -/
abbrev CokernelCofork.ofÏ€ {Z : C} (Ï€ : Y âŸ¶ Z) (w : f â‰« Ï€ = 0) : CokernelCofork f :=
  Cofork.ofÏ€ Ï€ <| by rw [w, zero_comp]

@[simp]
theorem CokernelCofork.Ï€_ofÏ€ {X Y P : C} (f : X âŸ¶ Y) (Ï€ : Y âŸ¶ P) (w : f â‰« Ï€ = 0) :
    Cofork.Ï€ (CokernelCofork.ofÏ€ Ï€ w) = Ï€ :=
  rfl

/-- Every cokernel cofork `s` is isomorphic (actually, equal) to `cofork.ofÏ€ (cofork.Ï€ s) _`. -/
def isoOfÏ€ (s : Cofork f 0) : s â‰… Cofork.ofÏ€ (Cofork.Ï€ s) (Cofork.condition s) :=
  Cocones.ext (Iso.refl _) fun j => by cases j <;> cat_disch

/-- If `Ï€ = Ï€'`, then `CokernelCofork.of_Ï€ Ï€ _` and `CokernelCofork.of_Ï€ Ï€' _` are isomorphic. -/
def ofÏ€Congr {P : C} {Ï€ Ï€' : Y âŸ¶ P} {w : f â‰« Ï€ = 0} (h : Ï€ = Ï€') :
    CokernelCofork.ofÏ€ Ï€ w â‰… CokernelCofork.ofÏ€ Ï€' (by rw [â† h, w]) :=
  Cocones.ext (Iso.refl _) fun j => by cases j <;> cat_disch

/-- If `s` is a colimit cokernel cofork, then every `k : Y âŸ¶ W` satisfying `f â‰« k = 0` induces
`l : s.X âŸ¶ W` such that `cofork.Ï€ s â‰« l = k`. -/
def CokernelCofork.IsColimit.desc' {s : CokernelCofork f} (hs : IsColimit s) {W : C} (k : Y âŸ¶ W)
    (h : f â‰« k = 0) : { l : s.pt âŸ¶ W // Cofork.Ï€ s â‰« l = k } :=
  âŸ¨hs.desc <| CokernelCofork.ofÏ€ _ h, hs.fac _ _âŸ©

/-- This is a slightly more convenient method to verify that a cokernel cofork is a colimit cocone.
It only asks for a proof of facts that carry any mathematical content -/
def isColimitAux (t : CokernelCofork f) (desc : âˆ€ s : CokernelCofork f, t.pt âŸ¶ s.pt)
    (fac : âˆ€ s : CokernelCofork f, t.Ï€ â‰« desc s = s.Ï€)
    (uniq : âˆ€ (s : CokernelCofork f) (m : t.pt âŸ¶ s.pt) (_ : t.Ï€ â‰« m = s.Ï€), m = desc s) :
    IsColimit t :=
  { desc
    fac := fun s j => by
      cases j
      Â· simp
      Â· exact fac s
    uniq := fun s m w => uniq s m (w Limits.WalkingParallelPair.one) }

/-- This is a more convenient formulation to show that a `CokernelCofork` constructed using
`CokernelCofork.ofÏ€` is a limit cone.
-/
def CokernelCofork.IsColimit.ofÏ€ {Z : C} (g : Y âŸ¶ Z) (eq : f â‰« g = 0)
    (desc : âˆ€ {Z' : C} (g' : Y âŸ¶ Z') (_ : f â‰« g' = 0), Z âŸ¶ Z')
    (fac : âˆ€ {Z' : C} (g' : Y âŸ¶ Z') (eq' : f â‰« g' = 0), g â‰« desc g' eq' = g')
    (uniq :
      âˆ€ {Z' : C} (g' : Y âŸ¶ Z') (eq' : f â‰« g' = 0) (m : Z âŸ¶ Z') (_ : g â‰« m = g'), m = desc g' eq') :
    IsColimit (CokernelCofork.ofÏ€ g eq) :=
  isColimitAux _ (fun s => desc s.Ï€ s.condition) (fun s => fac s.Ï€ s.condition) fun s =>
    uniq s.Ï€ s.condition

/-- This is a more convenient formulation to show that a `CokernelCofork` of the form
`CokernelCofork.ofÏ€ p _` is a colimit cocone when we know that `p` is an epimorphism. -/
def CokernelCofork.IsColimit.ofÏ€' {X Y Q : C} {f : X âŸ¶ Y} (p : Y âŸ¶ Q) (w : f â‰« p = 0)
    (h : âˆ€ {A : C} (k : Y âŸ¶ A) (_ : f â‰« k = 0), { l : Q âŸ¶ A // p â‰« l = k}) [hp : Epi p] :
    IsColimit (CokernelCofork.ofÏ€ p w) :=
  ofÏ€ _ _ (fun {_} k hk => (h k hk).1) (fun {_} k hk => (h k hk).2) (fun {A} k hk m hm => by
    rw [â† cancel_epi p, (h k hk).2, hm])

/-- Every cokernel of `f` induces a cokernel of `g â‰« f` if `g` is epi. -/
def isCokernelEpiComp {c : CokernelCofork f} (i : IsColimit c) {W} (g : W âŸ¶ X) [hg : Epi g]
    {h : W âŸ¶ Y} (hh : h = g â‰« f) :
    IsColimit (CokernelCofork.ofÏ€ c.Ï€ (by rw [hh]; simp) : CokernelCofork h) :=
  Cofork.IsColimit.mk' _ fun s =>
    let s' : CokernelCofork f :=
      Cofork.ofÏ€ s.Ï€
        (by
          apply hg.left_cancellation
          rw [â† Category.assoc, â† hh, s.condition]
          simp)
    let l := CokernelCofork.IsColimit.desc' i s'.Ï€ s'.condition
    âŸ¨l.1, l.2, fun hm => by
      apply Cofork.IsColimit.hom_ext i; rw [Cofork.Ï€_ofÏ€] at hm; rw [hm]; exact l.2.symmâŸ©

@[simp]
theorem isCokernelEpiComp_desc {c : CokernelCofork f} (i : IsColimit c) {W} (g : W âŸ¶ X) [hg : Epi g]
    {h : W âŸ¶ Y} (hh : h = g â‰« f) (s : CokernelCofork h) :
    (isCokernelEpiComp i g hh).desc s =
      i.desc
        (Cofork.ofÏ€ s.Ï€
          (by
            rw [â† cancel_epi g, â† Category.assoc, â† hh]
            simp)) :=
  rfl

/-- Every cokernel of `g â‰« f` is also a cokernel of `f`, as long as `f â‰« c.Ï€` vanishes. -/
def isCokernelOfComp {W : C} (g : W âŸ¶ X) (h : W âŸ¶ Y) {c : CokernelCofork h} (i : IsColimit c)
    (hf : f â‰« c.Ï€ = 0) (hfg : g â‰« f = h) : IsColimit (CokernelCofork.ofÏ€ c.Ï€ hf) :=
  Cofork.IsColimit.mk _ (fun s => i.desc (CokernelCofork.ofÏ€ s.Ï€ (by simp [â† hfg])))
    (fun s => by simp only [CokernelCofork.Ï€_ofÏ€, Cofork.IsColimit.Ï€_desc]) fun s m h => by
      apply Cofork.IsColimit.hom_ext i
      simpa using h

/-- `Y` identifies to the cokernel of a zero map `X âŸ¶ Y`. -/
def CokernelCofork.IsColimit.ofId {X Y : C} (f : X âŸ¶ Y) (hf : f = 0) :
    IsColimit (CokernelCofork.ofÏ€ (ğŸ™ Y) (show f â‰« ğŸ™ Y = 0 by rw [hf, zero_comp])) :=
  CokernelCofork.IsColimit.ofÏ€ _ _ (fun x _ => x) (fun _ _ => Category.id_comp _)
    (fun _ _ _ hb => by simp only [â† hb, Category.id_comp])

/-- Any zero object identifies to the cokernel of a given epimorphisms. -/
def CokernelCofork.IsColimit.ofEpiOfIsZero {X Y : C} {f : X âŸ¶ Y} (c : CokernelCofork f)
    (hf : Epi f) (h : IsZero c.pt) : IsColimit c :=
  isColimitAux _ (fun _ => 0) (fun s => by rw [comp_zero, â† cancel_epi f, comp_zero, s.condition])
    (fun _ _ _ => h.eq_of_src _ _)

lemma CokernelCofork.IsColimit.isIso_Ï€ {X Y : C} {f : X âŸ¶ Y} (c : CokernelCofork f)
    (hc : IsColimit c) (hf : f = 0) : IsIso c.Ï€ :=
  isIso_colimit_cocone_parallelPair_of_eq hf hc

/-- If `c` is a colimit cokernel cofork for `f : X âŸ¶ Y`, `e : Y â‰… Y'` and `f' : X' âŸ¶ Y` is a
morphism, then there is a colimit cokernel cofork for `f'` with the same point as `c` if for any
morphism `Ï† : Y âŸ¶ W`, there is an equivalence `f â‰« Ï† = 0 â†” f' â‰« e.hom â‰« Ï† = 0`. -/
def CokernelCofork.isColimitOfIsColimitOfIff {X Y : C} {f : X âŸ¶ Y} {c : CokernelCofork f}
    (hc : IsColimit c) {X' Y' : C} (f' : X' âŸ¶ Y') (e : Y' â‰… Y)
    (iff : âˆ€ â¦ƒW : Câ¦„ (Ï† : Y âŸ¶ W), f â‰« Ï† = 0 â†” f' â‰« e.hom â‰« Ï† = 0) :
    IsColimit (CokernelCofork.ofÏ€ (f := f') (e.hom â‰« c.Ï€) (by simp [â† iff])) :=
  CokernelCofork.IsColimit.ofÏ€ _ _
    (fun s hs â†¦ hc.desc (CokernelCofork.ofÏ€ (Ï€ := e.inv â‰« s)
      (by rw [iff, e.hom_inv_id_assoc, hs])))
    (fun s hs â†¦ by simp)
    (fun s hs m hm â†¦ Cofork.IsColimit.hom_ext hc (by simpa [â† cancel_epi e.hom] using hm))

/-- If `c` is a colimit cokernel cofork for `f : X âŸ¶ Y`, and `f' : X' âŸ¶ Y is another
morphism, then there is a colimit cokernel cofork for `f'` with the same point as `c` if for any
morphism `Ï† : Y âŸ¶ W`, there is an equivalence `f â‰« Ï† = 0 â†” f' â‰« Ï† = 0`. -/
def CokernelCofork.isColimitOfIsColimitOfIff' {X Y : C} {f : X âŸ¶ Y} {c : CokernelCofork f}
    (hc : IsColimit c) {X' : C} (f' : X' âŸ¶ Y)
    (iff : âˆ€ â¦ƒW : Câ¦„ (Ï† : Y âŸ¶ W), f â‰« Ï† = 0 â†” f' â‰« Ï† = 0) :
    IsColimit (CokernelCofork.ofÏ€ (f := f') c.Ï€ (by simp [â† iff])) :=
  IsColimit.ofIsoColimit (isColimitOfIsColimitOfIff hc f' (Iso.refl _) (by simpa using iff))
    (Cofork.ext (Iso.refl _))

lemma CokernelCofork.IsColimit.isZero_of_epi {X Y : C} {f : X âŸ¶ Y}
    {c : CokernelCofork f} (hc : IsColimit c) [Epi f] : IsZero c.pt := by
  have := Cofork.IsColimit.epi hc
  rw [IsZero.iff_id_eq_zero, â† cancel_epi c.Ï€, â† cancel_epi f,
    c.condition_assoc, comp_zero, comp_zero, zero_comp]

end

namespace CokernelCofork

variable {f} {X' Y' : C} {f' : X' âŸ¶ Y'}

/-- The morphism between points of cokernel coforks induced by a morphism
in the category of arrows. -/
def mapOfIsColimit {cc : CokernelCofork f} (hf : IsColimit cc) (cc' : CokernelCofork f')
    (Ï† : Arrow.mk f âŸ¶ Arrow.mk f') : cc.pt âŸ¶ cc'.pt :=
  hf.desc (CokernelCofork.ofÏ€ (Ï†.right â‰« cc'.Ï€) (by
    erw [â† Arrow.w_assoc Ï†, condition, comp_zero]))

@[reassoc (attr := simp)]
lemma Ï€_mapOfIsColimit {cc : CokernelCofork f} (hf : IsColimit cc) (cc' : CokernelCofork f')
    (Ï† : Arrow.mk f âŸ¶ Arrow.mk f') :
    cc.Ï€ â‰« mapOfIsColimit hf cc' Ï† = Ï†.right â‰« cc'.Ï€ :=
  hf.fac _ _

/-- The isomorphism between points of limit cokernel coforks induced by an isomorphism
in the category of arrows. -/
@[simps]
def mapIsoOfIsColimit {cc : CokernelCofork f} {cc' : CokernelCofork f'}
    (hf : IsColimit cc) (hf' : IsColimit cc')
    (Ï† : Arrow.mk f â‰… Arrow.mk f') : cc.pt â‰… cc'.pt where
  hom := mapOfIsColimit hf cc' Ï†.hom
  inv := mapOfIsColimit hf' cc Ï†.inv
  hom_inv_id := Cofork.IsColimit.hom_ext hf (by simp)
  inv_hom_id := Cofork.IsColimit.hom_ext hf' (by simp)

end CokernelCofork

section

variable [HasCokernel f]

/-- The cokernel of a morphism, expressed as the coequalizer with the 0 morphism. -/
abbrev cokernel : C :=
  coequalizer f 0

/-- The map from the target of `f` to `cokernel f`. -/
abbrev cokernel.Ï€ : Y âŸ¶ cokernel f :=
  coequalizer.Ï€ f 0

@[simp]
theorem coequalizer_as_cokernel : coequalizer.Ï€ f 0 = cokernel.Ï€ f :=
  rfl

@[reassoc (attr := simp)]
theorem cokernel.condition : f â‰« cokernel.Ï€ f = 0 :=
  CokernelCofork.condition _

/-- The cokernel built from `cokernel.Ï€ f` is colimiting. -/
def cokernelIsCokernel :
    IsColimit (Cofork.ofÏ€ (cokernel.Ï€ f) ((cokernel.condition f).trans zero_comp.symm)) :=
  IsColimit.ofIsoColimit (colimit.isColimit _) (Cofork.ext (Iso.refl _))

/-- Given any morphism `k : Y âŸ¶ W` such that `f â‰« k = 0`, `k` factors through `cokernel.Ï€ f`
via `cokernel.desc : cokernel f âŸ¶ W`. -/
abbrev cokernel.desc {W : C} (k : Y âŸ¶ W) (h : f â‰« k = 0) : cokernel f âŸ¶ W :=
  (cokernelIsCokernel f).desc (CokernelCofork.ofÏ€ k h)

@[reassoc (attr := simp)]
theorem cokernel.Ï€_desc {W : C} (k : Y âŸ¶ W) (h : f â‰« k = 0) :
    cokernel.Ï€ f â‰« cokernel.desc f k h = k :=
  (cokernelIsCokernel f).fac (CokernelCofork.ofÏ€ k h) WalkingParallelPair.one

@[reassoc (attr := simp)]
lemma colimit_Î¹_zero_cokernel_desc {C : Type*} [Category C]
    [HasZeroMorphisms C] {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (h : f â‰« g = 0) [HasCokernel f] :
    colimit.Î¹ (parallelPair f 0) WalkingParallelPair.zero â‰« cokernel.desc f g h = 0 := by
  rw [(colimit.w (parallelPair f 0) WalkingParallelPairHom.left).symm]
  simp

@[simp]
theorem cokernel.desc_zero {W : C} {h} : cokernel.desc f (0 : Y âŸ¶ W) h = 0 := by
  ext; simp

instance cokernel.desc_epi {W : C} (k : Y âŸ¶ W) (h : f â‰« k = 0) [Epi k] :
    Epi (cokernel.desc f k h) :=
  âŸ¨fun {Z} g g' w => by
    replace w := cokernel.Ï€ f â‰«= w
    simp only [cokernel.Ï€_desc_assoc] at w
    exact (cancel_epi k).1 wâŸ©

/-- Any morphism `k : Y âŸ¶ W` satisfying `f â‰« k = 0` induces `l : cokernel f âŸ¶ W` such that
`cokernel.Ï€ f â‰« l = k`. -/
def cokernel.desc' {W : C} (k : Y âŸ¶ W) (h : f â‰« k = 0) :
    { l : cokernel f âŸ¶ W // cokernel.Ï€ f â‰« l = k } :=
  âŸ¨cokernel.desc f k h, cokernel.Ï€_desc _ _ _âŸ©

/-- A commuting square induces a morphism of cokernels. -/
abbrev cokernel.map {X' Y' : C} (f' : X' âŸ¶ Y') [HasCokernel f'] (p : X âŸ¶ X') (q : Y âŸ¶ Y')
    (w : f â‰« q = p â‰« f') : cokernel f âŸ¶ cokernel f' :=
  cokernel.desc f (q â‰« cokernel.Ï€ f') (by
    have : f â‰« q â‰« Ï€ f' = p â‰« f' â‰« Ï€ f' := by
      simp only [â† Category.assoc]
      apply congrArg (Â· â‰« Ï€ f') w
    simp [this])

@[simp]
lemma cokernel.map_id {X Y : C} (f : X âŸ¶ Y) [HasCokernel f] (q : X âŸ¶ X)
    (w : f â‰« ğŸ™ _ = q â‰« f) : cokernel.map f f q (ğŸ™ _) w = ğŸ™ _ := by
  aesop

/-- Given a commutative diagram
```
    X --f--> Y --g--> Z
    |        |        |
    |        |        |
    v        v        v
    X' -f'-> Y' -g'-> Z'
```
with horizontal arrows composing to zero,
then we obtain a commutative square
```
   cokernel f ---> Z
   |               |
   | cokernel.map  |
   |               |
   v               v
   cokernel f' --> Z'
```
-/
theorem cokernel.map_desc {X Y Z X' Y' Z' : C} (f : X âŸ¶ Y) [HasCokernel f] (g : Y âŸ¶ Z)
    (w : f â‰« g = 0) (f' : X' âŸ¶ Y') [HasCokernel f'] (g' : Y' âŸ¶ Z') (w' : f' â‰« g' = 0) (p : X âŸ¶ X')
    (q : Y âŸ¶ Y') (r : Z âŸ¶ Z') (hâ‚ : f â‰« q = p â‰« f') (hâ‚‚ : g â‰« r = q â‰« g') :
    cokernel.map f f' p q hâ‚ â‰« cokernel.desc f' g' w' = cokernel.desc f g w â‰« r := by
  ext; simp [hâ‚‚]

@[simp]
lemma cokernel.map_zero {X Y X' Y' : C} (f : X âŸ¶ Y) (f' : X' âŸ¶ Y')
    [HasCokernel f] [HasCokernel f'] (q : X âŸ¶ X') (w : f â‰« 0 = q â‰« f') :
    cokernel.map f f' q 0 w = 0 := by
  aesop

/-- A commuting square of isomorphisms induces an isomorphism of cokernels. -/
@[simps]
def cokernel.mapIso {X' Y' : C} (f' : X' âŸ¶ Y') [HasCokernel f'] (p : X â‰… X') (q : Y â‰… Y')
    (w : f â‰« q.hom = p.hom â‰« f') : cokernel f â‰… cokernel f' where
  hom := cokernel.map f f' p.hom q.hom w
  inv := cokernel.map f' f p.inv q.inv (by
          refine (cancel_mono q.hom).1 ?_
          simp [w])

/-- The cokernel of the zero morphism is an isomorphism -/
instance cokernel.Ï€_zero_isIso : IsIso (cokernel.Ï€ (0 : X âŸ¶ Y)) :=
  coequalizer.Ï€_of_self _

theorem eq_zero_of_mono_cokernel [Mono (cokernel.Ï€ f)] : f = 0 :=
  (cancel_mono (cokernel.Ï€ f)).1 (by simp)

/-- The cokernel of a zero morphism is isomorphic to the target. -/
def cokernelZeroIsoTarget : cokernel (0 : X âŸ¶ Y) â‰… Y :=
  coequalizer.isoTargetOfSelf 0

@[simp]
theorem cokernelZeroIsoTarget_hom :
    cokernelZeroIsoTarget.hom = cokernel.desc (0 : X âŸ¶ Y) (ğŸ™ Y) (by simp) := by
  ext; simp [cokernelZeroIsoTarget]

@[simp]
theorem cokernelZeroIsoTarget_inv : cokernelZeroIsoTarget.inv = cokernel.Ï€ (0 : X âŸ¶ Y) :=
  rfl

/-- If two morphisms are known to be equal, then their cokernels are isomorphic. -/
def cokernelIsoOfEq {f g : X âŸ¶ Y} [HasCokernel f] [HasCokernel g] (h : f = g) :
    cokernel f â‰… cokernel g :=
  HasColimit.isoOfNatIso (by simp [h]; rfl)

@[simp]
theorem cokernelIsoOfEq_refl {h : f = f} : cokernelIsoOfEq h = Iso.refl (cokernel f) := by
  ext; simp [cokernelIsoOfEq]

@[reassoc (attr := simp)]
theorem Ï€_comp_cokernelIsoOfEq_hom {f g : X âŸ¶ Y} [HasCokernel f] [HasCokernel g] (h : f = g) :
    cokernel.Ï€ f â‰« (cokernelIsoOfEq h).hom = cokernel.Ï€ g := by
  cases h; simp

@[reassoc (attr := simp)]
theorem Ï€_comp_cokernelIsoOfEq_inv {f g : X âŸ¶ Y} [HasCokernel f] [HasCokernel g] (h : f = g) :
    cokernel.Ï€ _ â‰« (cokernelIsoOfEq h).inv = cokernel.Ï€ _ := by
  cases h; simp

@[reassoc (attr := simp)]
theorem cokernelIsoOfEq_hom_comp_desc {Z} {f g : X âŸ¶ Y} [HasCokernel f] [HasCokernel g] (h : f = g)
    (e : Y âŸ¶ Z) (he) :
    (cokernelIsoOfEq h).hom â‰« cokernel.desc _ e he = cokernel.desc _ e (by simp [h, he]) := by
  cases h; simp

@[reassoc (attr := simp)]
theorem cokernelIsoOfEq_inv_comp_desc {Z} {f g : X âŸ¶ Y} [HasCokernel f] [HasCokernel g] (h : f = g)
    (e : Y âŸ¶ Z) (he) :
    (cokernelIsoOfEq h).inv â‰« cokernel.desc _ e he = cokernel.desc _ e (by simp [â† h, he]) := by
  cases h; simp

@[simp]
theorem cokernelIsoOfEq_trans {f g h : X âŸ¶ Y} [HasCokernel f] [HasCokernel g] [HasCokernel h]
    (wâ‚ : f = g) (wâ‚‚ : g = h) :
    cokernelIsoOfEq wâ‚ â‰ªâ‰« cokernelIsoOfEq wâ‚‚ = cokernelIsoOfEq (wâ‚.trans wâ‚‚) := by
  cases wâ‚; cases wâ‚‚; ext; simp [cokernelIsoOfEq]

variable {f}

theorem cokernel_not_mono_of_nonzero (w : f â‰  0) : Â¬Mono (cokernel.Ï€ f) := fun _ =>
  w (eq_zero_of_mono_cokernel f)

theorem cokernel_not_iso_of_nonzero (w : f â‰  0) : IsIso (cokernel.Ï€ f) â†’ False := fun _ =>
  cokernel_not_mono_of_nonzero w inferInstance

-- TODO the remainder of this section has obvious generalizations to `HasCoequalizer f g`.
instance hasCokernel_comp_iso {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [HasCokernel f] [IsIso g] :
    HasCokernel (f â‰« g) where
  exists_colimit :=
    âŸ¨{  cocone := CokernelCofork.ofÏ€ (inv g â‰« cokernel.Ï€ f) (by simp)
        isColimit :=
          isColimitAux _
            (fun s =>
              cokernel.desc _ (g â‰« s.Ï€) (by rw [â† Category.assoc, CokernelCofork.condition]))
            (by simp) fun s (m : cokernel _ âŸ¶ _) w => by
            simp_rw [â† w]
            apply coequalizer.hom_ext
            simp }âŸ©

/-- When `g` is an isomorphism, the cokernel of `f â‰« g` is isomorphic to the cokernel of `f`.
-/
@[simps]
def cokernelCompIsIso {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [HasCokernel f] [IsIso g] :
    cokernel (f â‰« g) â‰… cokernel f where
  hom := cokernel.desc _ (inv g â‰« cokernel.Ï€ f) (by simp)
  inv := cokernel.desc _ (g â‰« cokernel.Ï€ (f â‰« g)) (by rw [â† Category.assoc, cokernel.condition])

instance hasCokernel_epi_comp {X Y : C} (f : X âŸ¶ Y) [HasCokernel f] {W} (g : W âŸ¶ X) [Epi g] :
    HasCokernel (g â‰« f) :=
  âŸ¨âŸ¨{   cocone := _
        isColimit := isCokernelEpiComp (colimit.isColimit _) g rfl }âŸ©âŸ©

/-- When `f` is an epimorphism, the cokernel of `f â‰« g` is isomorphic to the cokernel of `g`.
-/
@[simps]
def cokernelEpiComp {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [Epi f] [HasCokernel g] :
    cokernel (f â‰« g) â‰… cokernel g where
  hom := cokernel.desc _ (cokernel.Ï€ g) (by simp)
  inv :=
    cokernel.desc _ (cokernel.Ï€ (f â‰« g))
      (by
        rw [â† cancel_epi f, â† Category.assoc]
        simp)

/-- Equal maps have isomorphic cokernels. -/
@[simps] def cokernel.congr {X Y : C} (f g : X âŸ¶ Y) [HasCokernel f] [HasCokernel g]
    (h : f = g) : cokernel f â‰… cokernel g where
  hom := cokernel.desc _ (cokernel.Ï€ g) (by simp [h])
  inv := cokernel.desc _ (cokernel.Ï€ f) (by simp [â† h])

lemma isZero_cokernel_of_epi {X Y : C} (f : X âŸ¶ Y) [Epi f] [HasCokernel f] :
    IsZero (cokernel f) :=
  CokernelCofork.IsColimit.isZero_of_epi (c := CokernelCofork.ofÏ€ _ (cokernel.condition f))
    (cokernelIsCokernel f)

end

section HasZeroObject

variable [HasZeroObject C]

open ZeroObject

/-- The morphism to the zero object determines a cocone on a cokernel diagram -/
def cokernel.zeroCokernelCofork : CokernelCofork f where
  pt := 0
  Î¹ := { app := fun _ => 0 }

/-- The morphism to the zero object is a cokernel of an epimorphism -/
def cokernel.isColimitCoconeZeroCocone [Epi f] : IsColimit (cokernel.zeroCokernelCofork f) :=
  Cofork.IsColimit.mk _ (fun _ => 0)
    (fun s => by
      erw [zero_comp]
      refine (zero_of_epi_comp f ?_).symm
      exact CokernelCofork.condition _)
    fun _ _ _ => zero_of_from_zero _

/-- The cokernel of an epimorphism is isomorphic to the zero object -/
def cokernel.ofEpi [HasCokernel f] [Epi f] : cokernel f â‰… 0 :=
  Functor.mapIso (Cocones.forget _) <|
    IsColimit.uniqueUpToIso (colimit.isColimit (parallelPair f 0))
      (cokernel.isColimitCoconeZeroCocone f)

/-- The cokernel morphism of an epimorphism is a zero morphism -/
theorem cokernel.Ï€_of_epi [HasCokernel f] [Epi f] : cokernel.Ï€ f = 0 :=
  zero_of_target_iso_zero _ (cokernel.ofEpi f)

end HasZeroObject

section MonoFactorisation

variable {f}

@[simp]
theorem MonoFactorisation.kernel_Î¹_comp [HasKernel f] (F : MonoFactorisation f) :
    kernel.Î¹ f â‰« F.e = 0 := by
  rw [â† cancel_mono F.m, zero_comp, Category.assoc, F.fac, kernel.condition]

end MonoFactorisation

section HasImage

/-- The cokernel of the image inclusion of a morphism `f` is isomorphic to the cokernel of `f`.

(This result requires that the factorisation through the image is an epimorphism.
This holds in any category with equalizers.)
-/
@[simps]
def cokernelImageÎ¹ {X Y : C} (f : X âŸ¶ Y) [HasImage f] [HasCokernel (image.Î¹ f)] [HasCokernel f]
    [Epi (factorThruImage f)] : cokernel (image.Î¹ f) â‰… cokernel f where
  hom :=
    cokernel.desc _ (cokernel.Ï€ f)
      (by
        have w := cokernel.condition f
        conv at w =>
          lhs
          congr
          rw [â† image.fac f]
        rw [â† HasZeroMorphisms.comp_zero (Limits.factorThruImage f), Category.assoc,
          cancel_epi] at w
        exact w)
  inv :=
    cokernel.desc _ (cokernel.Ï€ _)
      (by
        conv =>
          lhs
          congr
          rw [â† image.fac f]
        rw [Category.assoc, cokernel.condition, HasZeroMorphisms.comp_zero])

section

variable (f : X âŸ¶ Y) [HasKernel f] [HasImage f] [HasKernel (factorThruImage f)]

/-- The kernel of the morphism `X âŸ¶ image f` is just the kernel of `f`. -/
def kernelFactorThruImage : kernel (factorThruImage f) â‰… kernel f :=
  (kernelCompMono (factorThruImage f) (image.Î¹ f)).symm â‰ªâ‰« (kernel.congr _ _ (by simp))

@[reassoc (attr := simp)]
theorem kernelFactorThruImage_hom_comp_Î¹ :
    (kernelFactorThruImage f).hom â‰« kernel.Î¹ f = kernel.Î¹ (factorThruImage f) := by
  simp [kernelFactorThruImage]

@[reassoc (attr := simp)]
theorem kernelFactorThruImage_inv_comp_Î¹ :
    (kernelFactorThruImage f).inv â‰« kernel.Î¹ (factorThruImage f) = kernel.Î¹ f := by
  simp [kernelFactorThruImage]

end

end HasImage

section

variable (X Y)

/-- The cokernel of a zero morphism is an isomorphism -/
theorem cokernel.Ï€_of_zero : IsIso (cokernel.Ï€ (0 : X âŸ¶ Y)) :=
  coequalizer.Ï€_of_self _

end

section HasZeroObject

variable [HasZeroObject C]

open ZeroObject

/-- The kernel of the cokernel of an epimorphism is an isomorphism -/
instance kernel.of_cokernel_of_epi [HasCokernel f] [HasKernel (cokernel.Ï€ f)] [Epi f] :
    IsIso (kernel.Î¹ (cokernel.Ï€ f)) :=
  equalizer.Î¹_of_eq <| cokernel.Ï€_of_epi f

/-- The cokernel of the kernel of a monomorphism is an isomorphism -/
instance cokernel.of_kernel_of_mono [HasKernel f] [HasCokernel (kernel.Î¹ f)] [Mono f] :
    IsIso (cokernel.Ï€ (kernel.Î¹ f)) :=
  coequalizer.Ï€_of_eq <| kernel.Î¹_of_mono f

/-- If `f â‰« g = 0` implies `g = 0` for all `g`, then `0 : Y âŸ¶ 0` is a cokernel of `f`. -/
def zeroCokernelOfZeroCancel {X Y : C} (f : X âŸ¶ Y)
    (hf : âˆ€ (Z : C) (g : Y âŸ¶ Z) (_ : f â‰« g = 0), g = 0) :
    IsColimit (CokernelCofork.ofÏ€ (0 : Y âŸ¶ 0) (show f â‰« 0 = 0 by simp)) :=
  Cofork.IsColimit.mk _ (fun _ => 0)
    (fun s => by rw [hf _ _ (CokernelCofork.condition s), comp_zero]) fun s m _ => by
      apply HasZeroObject.from_zero_ext

end HasZeroObject

section Transport

/-- If `i` is an isomorphism such that `i.hom â‰« l = f`, then any cokernel of `f` is a cokernel of
`l`. -/
def IsCokernel.ofIsoComp {Z : C} (l : Z âŸ¶ Y) (i : X â‰… Z) (h : i.hom â‰« l = f) {s : CokernelCofork f}
    (hs : IsColimit s) :
    IsColimit
      (CokernelCofork.ofÏ€ (Cofork.Ï€ s) <| show l â‰« Cofork.Ï€ s = 0 by simp [i.eq_inv_comp.2 h]) :=
  Cofork.IsColimit.mk _ (fun s => hs.desc <| CokernelCofork.ofÏ€ (Cofork.Ï€ s) <| by simp [â† h])
    (fun s => by simp) fun s m h => by
      apply Cofork.IsColimit.hom_ext hs
      simpa using h

/-- If `i` is an isomorphism such that `i.hom â‰« l = f`, then the cokernel of `f` is a cokernel of
`l`. -/
def cokernel.ofIsoComp [HasCokernel f] {Z : C} (l : Z âŸ¶ Y) (i : X â‰… Z) (h : i.hom â‰« l = f) :
    IsColimit
      (CokernelCofork.ofÏ€ (cokernel.Ï€ f) <|
        show l â‰« cokernel.Ï€ f = 0 by simp [i.eq_inv_comp.2 h]) :=
  IsCokernel.ofIsoComp f l i h <| colimit.isColimit _

/-- If `s` is any colimit cokernel cocone over `f` and `i` is an isomorphism such that
`s.Ï€ â‰« i.hom = l`, then `l` is a cokernel of `f`. -/
def IsCokernel.cokernelIso {Z : C} (l : Y âŸ¶ Z) {s : CokernelCofork f} (hs : IsColimit s)
    (i : s.pt â‰… Z) (h : Cofork.Ï€ s â‰« i.hom = l) :
    IsColimit (CokernelCofork.ofÏ€ l <| show f â‰« l = 0 by simp [â† h]) :=
  IsColimit.ofIsoColimit hs <|
    Cocones.ext i fun j => by
      cases j
      Â· dsimp; rw [â† h]; simp
      Â· exact h

/-- If `i` is an isomorphism such that `cokernel.Ï€ f â‰« i.hom = l`, then `l` is a cokernel of `f`. -/
def cokernel.cokernelIso [HasCokernel f] {Z : C} (l : Y âŸ¶ Z) (i : cokernel f â‰… Z)
    (h : cokernel.Ï€ f â‰« i.hom = l) :
    IsColimit (@CokernelCofork.ofÏ€ _ _ _ _ _ f _ l <| by simp [â† h]) :=
  IsCokernel.cokernelIso f l (colimit.isColimit _) i h

end Transport

section Comparison

variable {D : Type uâ‚‚} [Category.{vâ‚‚} D] [HasZeroMorphisms D]
variable (G : C â¥¤ D) [Functor.PreservesZeroMorphisms G]

/-- The comparison morphism for the kernel of `f`.
This is an isomorphism iff `G` preserves the kernel of `f`; see
`CategoryTheory/Limits/Preserves/Shapes/Kernels.lean`
-/
def kernelComparison [HasKernel f] [HasKernel (G.map f)] : G.obj (kernel f) âŸ¶ kernel (G.map f) :=
  kernel.lift _ (G.map (kernel.Î¹ f))
    (by simp only [â† G.map_comp, kernel.condition, Functor.map_zero])

@[reassoc (attr := simp)]
theorem kernelComparison_comp_Î¹ [HasKernel f] [HasKernel (G.map f)] :
    kernelComparison f G â‰« kernel.Î¹ (G.map f) = G.map (kernel.Î¹ f) :=
  kernel.lift_Î¹ _ _ _

@[reassoc (attr := simp)]
theorem map_lift_kernelComparison [HasKernel f] [HasKernel (G.map f)] {Z : C} {h : Z âŸ¶ X}
    (w : h â‰« f = 0) :
    G.map (kernel.lift _ h w) â‰« kernelComparison f G =
      kernel.lift _ (G.map h) (by simp only [â† G.map_comp, w, Functor.map_zero]) := by
  ext; simp [â† G.map_comp]

@[reassoc]
theorem kernelComparison_comp_kernel_map {X' Y' : C} [HasKernel f] [HasKernel (G.map f)]
    (g : X' âŸ¶ Y') [HasKernel g] [HasKernel (G.map g)] (p : X âŸ¶ X') (q : Y âŸ¶ Y')
    (hpq : f â‰« q = p â‰« g) :
    kernelComparison f G â‰«
        kernel.map (G.map f) (G.map g) (G.map p) (G.map q) (by rw [â† G.map_comp, hpq, G.map_comp]) =
      G.map (kernel.map f g p q hpq) â‰« kernelComparison g G :=
  kernel.lift_map _ _ (by rw [â† G.map_comp, kernel.condition, G.map_zero]) _ _
    (by rw [â† G.map_comp, kernel.condition, G.map_zero]) _ _ _
    (by simp only [â† G.map_comp]; exact G.congr_map (kernel.lift_Î¹ _ _ _).symm) _

/-- The comparison morphism for the cokernel of `f`. -/
def cokernelComparison [HasCokernel f] [HasCokernel (G.map f)] :
    cokernel (G.map f) âŸ¶ G.obj (cokernel f) :=
  cokernel.desc _ (G.map (coequalizer.Ï€ _ _))
    (by simp only [â† G.map_comp, cokernel.condition, Functor.map_zero])

@[reassoc (attr := simp)]
theorem Ï€_comp_cokernelComparison [HasCokernel f] [HasCokernel (G.map f)] :
    cokernel.Ï€ (G.map f) â‰« cokernelComparison f G = G.map (cokernel.Ï€ _) :=
  cokernel.Ï€_desc _ _ _

@[reassoc (attr := simp)]
theorem cokernelComparison_map_desc [HasCokernel f] [HasCokernel (G.map f)] {Z : C} {h : Y âŸ¶ Z}
    (w : f â‰« h = 0) :
    cokernelComparison f G â‰« G.map (cokernel.desc _ h w) =
      cokernel.desc _ (G.map h) (by simp only [â† G.map_comp, w, Functor.map_zero]) := by
  ext; simp [â† G.map_comp]

@[reassoc]
theorem cokernel_map_comp_cokernelComparison {X' Y' : C} [HasCokernel f] [HasCokernel (G.map f)]
    (g : X' âŸ¶ Y') [HasCokernel g] [HasCokernel (G.map g)] (p : X âŸ¶ X') (q : Y âŸ¶ Y')
    (hpq : f â‰« q = p â‰« g) :
    cokernel.map (G.map f) (G.map g) (G.map p) (G.map q) (by rw [â† G.map_comp, hpq, G.map_comp]) â‰«
        cokernelComparison _ G =
      cokernelComparison _ G â‰« G.map (cokernel.map f g p q hpq) :=
  cokernel.map_desc _ _ (by rw [â† G.map_comp, cokernel.condition, G.map_zero]) _ _
    (by rw [â† G.map_comp, cokernel.condition, G.map_zero]) _ _ _ _
    (by simp only [â† G.map_comp]; exact G.congr_map (cokernel.Ï€_desc _ _ _))

end Comparison

end CategoryTheory.Limits

namespace CategoryTheory.Limits

variable (C : Type u) [Category.{v} C]
variable [HasZeroMorphisms C]

/-- `HasKernels` represents the existence of kernels for every morphism. -/
class HasKernels : Prop where
  has_limit : âˆ€ {X Y : C} (f : X âŸ¶ Y), HasKernel f := by infer_instance

/-- `HasCokernels` represents the existence of cokernels for every morphism. -/
class HasCokernels : Prop where
  has_colimit : âˆ€ {X Y : C} (f : X âŸ¶ Y), HasCokernel f := by infer_instance

attribute [instance 100] HasKernels.has_limit HasCokernels.has_colimit

instance (priority := 100) hasKernels_of_hasEqualizers [HasEqualizers C] : HasKernels C where

instance (priority := 100) hasCokernels_of_hasCoequalizers [HasCoequalizers C] :
    HasCokernels C where

end CategoryTheory.Limits
