/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Jakob von Raumer
-/
import Mathlib.CategoryTheory.Limits.HasLimits
import Mathlib.CategoryTheory.Thin

/-!
# Wide pullbacks

We define the category `WidePullbackShape`, (resp. `WidePushoutShape`) which is the category
obtained from a discrete category of type `J` by adjoining a terminal (resp. initial) element.
Limits of this shape are wide pullbacks (pushouts).
The convenience method `wideCospan` (`wideSpan`) constructs a functor from this category, hitting
the given morphisms.

We use `WidePullbackShape` to define ordinary pullbacks (pushouts) by using `J := WalkingPair`,
which allows easy proofs of some related lemmas.
Furthermore, wide pullbacks are used to show the existence of limits in the slice category.
Namely, if `C` has wide pullbacks then `C/B` has limits for any object `B` in `C`.

Typeclasses `HasWidePullbacks` and `HasFiniteWidePullbacks` assert the existence of wide
pullbacks and finite wide pullbacks.
-/

universe w w' v u

open CategoryTheory CategoryTheory.Limits Opposite

namespace CategoryTheory.Limits

variable (J : Type w)

/-- A wide pullback shape for any type `J` can be written simply as `Option J`. -/
def WidePullbackShape := Option J

instance : Inhabited (WidePullbackShape J) where
  default := none

/-- A wide pushout shape for any type `J` can be written simply as `Option J`. -/
def WidePushoutShape := Option J

instance : Inhabited (WidePushoutShape J) where
  default := none

namespace WidePullbackShape

variable {J}

-- Don't generate unnecessary `sizeOf_spec` lemma which the `simpNF` linter will complain about.
set_option genSizeOfSpec false in
/-- The type of arrows for the shape indexing a wide pullback. -/
inductive Hom : WidePullbackShape J ‚Üí WidePullbackShape J ‚Üí Type w
  | id : ‚àÄ X, Hom X X
  | term : ‚àÄ j : J, Hom (some j) none
  deriving DecidableEq

-- This is relying on an automatically generated instance name, generated in a `deriving` handler.
-- See https://github.com/leanprover/lean4/issues/2343
attribute [nolint unusedArguments] instDecidableEqHom

instance struct : CategoryStruct (WidePullbackShape J) where
  Hom := Hom
  id j := Hom.id j
  comp f g := by
    cases f
    ¬∑ exact g
    cases g
    apply Hom.term _

instance Hom.inhabited : Inhabited (Hom (none : WidePullbackShape J) none) :=
  ‚ü®Hom.id (none : WidePullbackShape J)‚ü©

open Lean Elab Tactic
/- Pointing note: experimenting with manual scoping of aesop tactics. Attempted to define
aesop rule directing on `WidePushoutOut` and it didn't take for some reason -/
/-- An aesop tactic for bulk cases on morphisms in `WidePushoutShape` -/
def evalCasesBash : TacticM Unit := do
  evalTactic
    (‚Üê `(tactic| casesm* WidePullbackShape _,
      (_ : WidePullbackShape _) ‚ü∂ (_ : WidePullbackShape _) ))

attribute [local aesop safe tactic (rule_sets := [CategoryTheory])] evalCasesBash

instance subsingleton_hom : Quiver.IsThin (WidePullbackShape J) := fun _ _ ‚Ü¶ by
  constructor
  intro a b
  casesm* WidePullbackShape _, (_ : WidePullbackShape _) ‚ü∂ (_ : WidePullbackShape _)
  ¬∑ rfl
  ¬∑ rfl
  ¬∑ rfl

instance category : SmallCategory (WidePullbackShape J) :=
  thin_category

@[simp]
theorem hom_id (X : WidePullbackShape J) : Hom.id X = ùüô X :=
  rfl

variable {C : Type u} [Category.{v} C]

/-- Construct a functor out of the wide pullback shape given a J-indexed collection of arrows to a
fixed object.
-/
@[simps]
def wideCospan (B : C) (objs : J ‚Üí C) (arrows : ‚àÄ j : J, objs j ‚ü∂ B) : WidePullbackShape J ‚•§ C where
  obj j := Option.casesOn j B objs
  map f := by
    obtain - | j := f
    ¬∑ apply ùüô _
    ¬∑ exact arrows j

/-- Every diagram is naturally isomorphic (actually, equal) to a `wideCospan` -/
def diagramIsoWideCospan (F : WidePullbackShape J ‚•§ C) :
    F ‚âÖ wideCospan (F.obj none) (fun j ‚Ü¶ F.obj (some j)) fun j ‚Ü¶ F.map (Hom.term j) :=
  NatIso.ofComponents fun j ‚Ü¶ eqToIso <| by cat_disch

/-- Construct a cone over a wide cospan. -/
@[simps]
def mkCone {F : WidePullbackShape J ‚•§ C} {X : C} (f : X ‚ü∂ F.obj none) (œÄ : ‚àÄ j, X ‚ü∂ F.obj (some j))
    (w : ‚àÄ j, œÄ j ‚â´ F.map (Hom.term j) = f) : Cone F :=
  { pt := X
    œÄ :=
      { app := fun j ‚Ü¶
          match j with
          | none => f
          | some j => œÄ j
        naturality := fun j j' f ‚Ü¶ by
          cases j <;> cases j' <;> cases f <;> simp [w] } }

/-- Wide pullback diagrams of equivalent index types are equivalent. -/
def equivalenceOfEquiv (J' : Type w') (h : J ‚âÉ J') :
    WidePullbackShape J ‚âå WidePullbackShape J' where
  functor := wideCospan none (fun j ‚Ü¶ some (h j)) fun j ‚Ü¶ Hom.term (h j)
  inverse := wideCospan none (fun j ‚Ü¶ some (h.invFun j)) fun j ‚Ü¶ Hom.term (h.invFun j)
  unitIso := NatIso.ofComponents (fun j ‚Ü¶ by cases j <;> exact eqToIso (by simp))
  counitIso := NatIso.ofComponents (fun j ‚Ü¶ by cases j <;> exact eqToIso (by simp))

attribute [local instance] uliftCategory in
/-- Lifting universe and morphism levels preserves wide pullback diagrams. -/
def uliftEquivalence :
    ULiftHom.{w'} (ULift.{w'} (WidePullbackShape J)) ‚âå WidePullbackShape (ULift J) :=
  (ULiftHomULiftCategory.equiv.{w', w', w, w} (WidePullbackShape J)).symm.trans
    (equivalenceOfEquiv _ (Equiv.ulift.{w', w}.symm : J ‚âÉ ULift.{w'} J))

end WidePullbackShape

namespace WidePushoutShape

variable {J}

-- Don't generate unnecessary `sizeOf_spec` lemma which the `simpNF` linter will complain about.
set_option genSizeOfSpec false in
/-- The type of arrows for the shape indexing a wide pushout. -/
inductive Hom : WidePushoutShape J ‚Üí WidePushoutShape J ‚Üí Type w
  | id : ‚àÄ X, Hom X X
  | init : ‚àÄ j : J, Hom none (some j)
  deriving DecidableEq

-- This is relying on an automatically generated instance name, generated in a `deriving` handler.
-- See https://github.com/leanprover/lean4/issues/2343
attribute [nolint unusedArguments] instDecidableEqHom

instance struct : CategoryStruct (WidePushoutShape J) where
  Hom := Hom
  id j := Hom.id j
  comp f g := by
    cases f
    ¬∑ exact g
    cases g
    apply Hom.init _

instance Hom.inhabited : Inhabited (Hom (none : WidePushoutShape J) none) :=
  ‚ü®Hom.id (none : WidePushoutShape J)‚ü©

open Lean Elab Tactic
-- Pointing note: experimenting with manual scoping of aesop tactics; only this worked
/-- An aesop tactic for bulk cases on morphisms in `WidePushoutShape` -/
def evalCasesBash' : TacticM Unit := do
  evalTactic
    (‚Üê `(tactic| casesm* WidePushoutShape _,
      (_ : WidePushoutShape _) ‚ü∂ (_ : WidePushoutShape _) ))

attribute [local aesop safe tactic (rule_sets := [CategoryTheory])] evalCasesBash'

instance subsingleton_hom : Quiver.IsThin (WidePushoutShape J) := fun _ _ ‚Ü¶ by
  constructor
  intro a b
  casesm* WidePushoutShape _, (_ : WidePushoutShape _) ‚ü∂ (_ : WidePushoutShape _)
  repeat rfl

instance category : SmallCategory (WidePushoutShape J) :=
  thin_category

@[simp]
theorem hom_id (X : WidePushoutShape J) : Hom.id X = ùüô X :=
  rfl

variable {C : Type u} [Category.{v} C]

/-- Construct a functor out of the wide pushout shape given a J-indexed collection of arrows from a
fixed object.
-/
@[simps]
def wideSpan (B : C) (objs : J ‚Üí C) (arrows : ‚àÄ j : J, B ‚ü∂ objs j) : WidePushoutShape J ‚•§ C where
  obj j := Option.casesOn j B objs
  map f := by
    obtain - | j := f
    ¬∑ apply ùüô _
    ¬∑ exact arrows j
  map_comp := fun f g ‚Ü¶ by
    cases f
    ¬∑ simp only [hom_id, Category.id_comp]; congr
    ¬∑ cases g
      simp only [hom_id, Category.comp_id]; congr

/-- Every diagram is naturally isomorphic (actually, equal) to a `wideSpan` -/
def diagramIsoWideSpan (F : WidePushoutShape J ‚•§ C) :
    F ‚âÖ wideSpan (F.obj none) (fun j ‚Ü¶ F.obj (some j)) fun j ‚Ü¶ F.map (Hom.init j) :=
  NatIso.ofComponents fun j ‚Ü¶ eqToIso <| by cases j; repeat rfl

/-- Construct a cocone over a wide span. -/
@[simps]
def mkCocone {F : WidePushoutShape J ‚•§ C} {X : C} (f : F.obj none ‚ü∂ X) (Œπ : ‚àÄ j, F.obj (some j) ‚ü∂ X)
    (w : ‚àÄ j, F.map (Hom.init j) ‚â´ Œπ j = f) : Cocone F :=
  { pt := X
    Œπ :=
      { app := fun j ‚Ü¶
          match j with
          | none => f
          | some j => Œπ j
        naturality := fun j j' f ‚Ü¶ by
          cases j <;> cases j' <;> cases f <;> simp [w] } }

/-- Wide pushout diagrams of equivalent index types are equivalent. -/
def equivalenceOfEquiv (J' : Type w') (h : J ‚âÉ J') : WidePushoutShape J ‚âå WidePushoutShape J' where
  functor := wideSpan none (fun j ‚Ü¶ some (h j)) fun j ‚Ü¶ Hom.init (h j)
  inverse := wideSpan none (fun j ‚Ü¶ some (h.invFun j)) fun j ‚Ü¶ Hom.init (h.invFun j)
  unitIso := NatIso.ofComponents (fun j ‚Ü¶ by cases j <;> exact eqToIso (by simp))
  counitIso := NatIso.ofComponents (fun j ‚Ü¶ by cases j <;> exact eqToIso (by simp))

attribute [local instance] uliftCategory in
/-- Lifting universe and morphism levels preserves wide pushout diagrams. -/
def uliftEquivalence :
    ULiftHom.{w'} (ULift.{w'} (WidePushoutShape J)) ‚âå WidePushoutShape (ULift J) :=
  (ULiftHomULiftCategory.equiv.{w', w', w, w} (WidePushoutShape J)).symm.trans
    (equivalenceOfEquiv _ (Equiv.ulift.{w', w}.symm : J ‚âÉ ULift.{w'} J))

end WidePushoutShape

variable (C : Type u) [Category.{v} C]

/-- `HasWidePullbacks` represents a choice of wide pullback for every collection of morphisms -/
abbrev HasWidePullbacks : Prop :=
  ‚àÄ J : Type w, HasLimitsOfShape (WidePullbackShape J) C

/-- `HasWidePushouts` represents a choice of wide pushout for every collection of morphisms -/
abbrev HasWidePushouts : Prop :=
  ‚àÄ J : Type w, HasColimitsOfShape (WidePushoutShape J) C

variable {C J}

/-- `HasWidePullback B objs arrows` means that `wideCospan B objs arrows` has a limit. -/
abbrev HasWidePullback (B : C) (objs : J ‚Üí C) (arrows : ‚àÄ j : J, objs j ‚ü∂ B) : Prop :=
  HasLimit (WidePullbackShape.wideCospan B objs arrows)

/-- `HasWidePushout B objs arrows` means that `wideSpan B objs arrows` has a colimit. -/
abbrev HasWidePushout (B : C) (objs : J ‚Üí C) (arrows : ‚àÄ j : J, B ‚ü∂ objs j) : Prop :=
  HasColimit (WidePushoutShape.wideSpan B objs arrows)

/-- A choice of wide pullback. -/
noncomputable abbrev widePullback (B : C) (objs : J ‚Üí C) (arrows : ‚àÄ j : J, objs j ‚ü∂ B)
    [HasWidePullback B objs arrows] : C :=
  limit (WidePullbackShape.wideCospan B objs arrows)

/-- A choice of wide pushout. -/
noncomputable abbrev widePushout (B : C) (objs : J ‚Üí C) (arrows : ‚àÄ j : J, B ‚ü∂ objs j)
    [HasWidePushout B objs arrows] : C :=
  colimit (WidePushoutShape.wideSpan B objs arrows)

namespace WidePullback

variable {C : Type u} [Category.{v} C] {B : C} {objs : J ‚Üí C} (arrows : ‚àÄ j : J, objs j ‚ü∂ B)
variable [HasWidePullback B objs arrows]

/-- The `j`-th projection from the pullback. -/
noncomputable abbrev œÄ (j : J) : widePullback _ _ arrows ‚ü∂ objs j :=
  limit.œÄ (WidePullbackShape.wideCospan _ _ _) (Option.some j)


/-- The unique map to the base from the pullback. -/
noncomputable abbrev base : widePullback _ _ arrows ‚ü∂ B :=
  limit.œÄ (WidePullbackShape.wideCospan _ _ _) Option.none

@[reassoc (attr := simp)]
theorem œÄ_arrow (j : J) : œÄ arrows j ‚â´ arrows _ = base arrows := by
  apply limit.w (WidePullbackShape.wideCospan _ _ _) (WidePullbackShape.Hom.term j)

variable {arrows} in
/-- Lift a collection of morphisms to a morphism to the pullback. -/
noncomputable abbrev lift {X : C} (f : X ‚ü∂ B) (fs : ‚àÄ j : J, X ‚ü∂ objs j)
    (w : ‚àÄ j, fs j ‚â´ arrows j = f) : X ‚ü∂ widePullback _ _ arrows :=
  limit.lift (WidePullbackShape.wideCospan _ _ _) (WidePullbackShape.mkCone f fs <| w)

variable {X : C} (f : X ‚ü∂ B) (fs : ‚àÄ j : J, X ‚ü∂ objs j) (w : ‚àÄ j, fs j ‚â´ arrows j = f)

@[reassoc]
theorem lift_œÄ (j : J) : lift f fs w ‚â´ œÄ arrows j = fs _ := by
  simp only [limit.lift_œÄ, WidePullbackShape.mkCone_pt, WidePullbackShape.mkCone_œÄ_app]

@[reassoc]
theorem lift_base : lift f fs w ‚â´ base arrows = f := by
  simp only [limit.lift_œÄ, WidePullbackShape.mkCone_pt, WidePullbackShape.mkCone_œÄ_app]

theorem eq_lift_of_comp_eq (g : X ‚ü∂ widePullback _ _ arrows) :
    (‚àÄ j : J, g ‚â´ œÄ arrows j = fs j) ‚Üí g ‚â´ base arrows = f ‚Üí g = lift f fs w := by
  intro h1 h2
  apply
    (limit.isLimit (WidePullbackShape.wideCospan B objs arrows)).uniq
      (WidePullbackShape.mkCone f fs <| w)
  rintro (_ | _)
  ¬∑ apply h2
  ¬∑ apply h1

theorem hom_eq_lift (g : X ‚ü∂ widePullback _ _ arrows) :
    g = lift (g ‚â´ base arrows) (fun j ‚Ü¶ g ‚â´ œÄ arrows j) (by simp) := by
  aesop

@[ext 1100]
theorem hom_ext (g1 g2 : X ‚ü∂ widePullback _ _ arrows) : (‚àÄ j : J,
    g1 ‚â´ œÄ arrows j = g2 ‚â´ œÄ arrows j) ‚Üí g1 ‚â´ base arrows = g2 ‚â´ base arrows ‚Üí g1 = g2 := by
  intro h1 h2
  apply limit.hom_ext
  rintro (_ | _)
  ¬∑ apply h2
  ¬∑ apply h1

end WidePullback

namespace WidePushout

variable {C : Type u} [Category.{v} C] {B : C} {objs : J ‚Üí C} (arrows : ‚àÄ j : J, B ‚ü∂ objs j)
variable [HasWidePushout B objs arrows]

/-- The `j`-th inclusion to the pushout. -/
noncomputable abbrev Œπ (j : J) : objs j ‚ü∂ widePushout _ _ arrows :=
  colimit.Œπ (WidePushoutShape.wideSpan _ _ _) (Option.some j)

/-- The unique map from the head to the pushout. -/
noncomputable abbrev head : B ‚ü∂ widePushout B objs arrows :=
  colimit.Œπ (WidePushoutShape.wideSpan _ _ _) Option.none

@[reassoc, simp]
theorem arrow_Œπ (j : J) : arrows j ‚â´ Œπ arrows j = head arrows := by
  apply colimit.w (WidePushoutShape.wideSpan _ _ _) (WidePushoutShape.Hom.init j)

variable {arrows} in
/-- Descend a collection of morphisms to a morphism from the pushout. -/
noncomputable abbrev desc {X : C} (f : B ‚ü∂ X) (fs : ‚àÄ j : J, objs j ‚ü∂ X)
    (w : ‚àÄ j, arrows j ‚â´ fs j = f) : widePushout _ _ arrows ‚ü∂ X :=
  colimit.desc (WidePushoutShape.wideSpan B objs arrows) (WidePushoutShape.mkCocone f fs <| w)

variable {X : C} (f : B ‚ü∂ X) (fs : ‚àÄ j : J, objs j ‚ü∂ X) (w : ‚àÄ j, arrows j ‚â´ fs j = f)

@[reassoc]
theorem Œπ_desc (j : J) : Œπ arrows j ‚â´ desc f fs w = fs _ := by
  simp only [colimit.Œπ_desc, WidePushoutShape.mkCocone_pt, WidePushoutShape.mkCocone_Œπ_app]

@[reassoc]
theorem head_desc : head arrows ‚â´ desc f fs w = f := by
  simp only [colimit.Œπ_desc, WidePushoutShape.mkCocone_pt, WidePushoutShape.mkCocone_Œπ_app]

theorem eq_desc_of_comp_eq (g : widePushout _ _ arrows ‚ü∂ X) :
    (‚àÄ j : J, Œπ arrows j ‚â´ g = fs j) ‚Üí head arrows ‚â´ g = f ‚Üí g = desc f fs w := by
  intro h1 h2
  apply
    (colimit.isColimit (WidePushoutShape.wideSpan B objs arrows)).uniq
      (WidePushoutShape.mkCocone f fs <| w)
  rintro (_ | _)
  ¬∑ apply h2
  ¬∑ apply h1

theorem hom_eq_desc (g : widePushout _ _ arrows ‚ü∂ X) :
    g =
      desc (head arrows ‚â´ g) (fun j ‚Ü¶ Œπ arrows j ‚â´ g) fun j ‚Ü¶ by
        rw [‚Üê Category.assoc]
        simp := by
  apply eq_desc_of_comp_eq
  ¬∑ simp
  ¬∑ rfl -- Porting note: another missing rfl

@[ext 1100]
theorem hom_ext (g1 g2 : widePushout _ _ arrows ‚ü∂ X) : (‚àÄ j : J,
    Œπ arrows j ‚â´ g1 = Œπ arrows j ‚â´ g2) ‚Üí head arrows ‚â´ g1 = head arrows ‚â´ g2 ‚Üí g1 = g2 := by
  intro h1 h2
  apply colimit.hom_ext
  rintro (_ | _)
  ¬∑ apply h2
  ¬∑ apply h1

end WidePushout

variable (J)

/-- The action on morphisms of the obvious functor
  `WidePullbackShape_op : WidePullbackShape J ‚•§ (WidePushoutShape J)·µí·µñ` -/
def widePullbackShapeOpMap :
    ‚àÄ X Y : WidePullbackShape J,
      (X ‚ü∂ Y) ‚Üí ((op X : (WidePushoutShape J)·µí·µñ) ‚ü∂ (op Y : (WidePushoutShape J)·µí·µñ))
  | _, _, WidePullbackShape.Hom.id X => Quiver.Hom.op (WidePushoutShape.Hom.id _)
  | _, _, WidePullbackShape.Hom.term _ => Quiver.Hom.op (WidePushoutShape.Hom.init _)

/-- The obvious functor `WidePullbackShape J ‚•§ (WidePushoutShape J)·µí·µñ` -/
@[simps]
def widePullbackShapeOp : WidePullbackShape J ‚•§ (WidePushoutShape J)·µí·µñ where
  obj X := op X
  map {X‚ÇÅ} {X‚ÇÇ} := widePullbackShapeOpMap J X‚ÇÅ X‚ÇÇ

/-- The action on morphisms of the obvious functor
`widePushoutShapeOp : WidePushoutShape J ‚•§ (WidePullbackShape J)·µí·µñ` -/
def widePushoutShapeOpMap :
    ‚àÄ X Y : WidePushoutShape J,
      (X ‚ü∂ Y) ‚Üí ((op X : (WidePullbackShape J)·µí·µñ) ‚ü∂ (op Y : (WidePullbackShape J)·µí·µñ))
  | _, _, WidePushoutShape.Hom.id X => Quiver.Hom.op (WidePullbackShape.Hom.id _)
  | _, _, WidePushoutShape.Hom.init _ => Quiver.Hom.op (WidePullbackShape.Hom.term _)

/-- The obvious functor `WidePushoutShape J ‚•§ (WidePullbackShape J)·µí·µñ` -/
@[simps]
def widePushoutShapeOp : WidePushoutShape J ‚•§ (WidePullbackShape J)·µí·µñ where
  obj X := op X
  map := fun {X} {Y} ‚Ü¶ widePushoutShapeOpMap J X Y

/-- The obvious functor `(WidePullbackShape J)·µí·µñ ‚•§ WidePushoutShape J` -/
@[simps!]
def widePullbackShapeUnop : (WidePullbackShape J)·µí·µñ ‚•§ WidePushoutShape J :=
  (widePullbackShapeOp J).leftOp

/-- The obvious functor `(WidePushoutShape J)·µí·µñ ‚•§ WidePullbackShape J` -/
@[simps!]
def widePushoutShapeUnop : (WidePushoutShape J)·µí·µñ ‚•§ WidePullbackShape J :=
  (widePushoutShapeOp J).leftOp

/-- The inverse of the unit isomorphism of the equivalence
`widePushoutShapeOpEquiv : (WidePushoutShape J)·µí·µñ ‚âå WidePullbackShape J` -/
def widePushoutShapeOpUnop : widePushoutShapeUnop J ‚ãô widePullbackShapeOp J ‚âÖ ùü≠ _ :=
  NatIso.ofComponents fun _ ‚Ü¶ Iso.refl _

/-- The counit isomorphism of the equivalence
`widePullbackShapeOpEquiv : (WidePullbackShape J)·µí·µñ ‚âå WidePushoutShape J` -/
def widePushoutShapeUnopOp : widePushoutShapeOp J ‚ãô widePullbackShapeUnop J ‚âÖ ùü≠ _ :=
  NatIso.ofComponents fun _ ‚Ü¶ Iso.refl _

/-- The inverse of the unit isomorphism of the equivalence
`widePullbackShapeOpEquiv : (WidePullbackShape J)·µí·µñ ‚âå WidePushoutShape J` -/
def widePullbackShapeOpUnop : widePullbackShapeUnop J ‚ãô widePushoutShapeOp J ‚âÖ ùü≠ _ :=
  NatIso.ofComponents fun _ ‚Ü¶ Iso.refl _

/-- The counit isomorphism of the equivalence
`widePushoutShapeOpEquiv : (WidePushoutShape J)·µí·µñ ‚âå WidePullbackShape J` -/
def widePullbackShapeUnopOp : widePullbackShapeOp J ‚ãô widePushoutShapeUnop J ‚âÖ ùü≠ _ :=
  NatIso.ofComponents fun _ ‚Ü¶ Iso.refl _

/-- The duality equivalence `(WidePushoutShape J)·µí·µñ ‚âå WidePullbackShape J` -/
@[simps]
def widePushoutShapeOpEquiv : (WidePushoutShape J)·µí·µñ ‚âå WidePullbackShape J where
  functor := widePushoutShapeUnop J
  inverse := widePullbackShapeOp J
  unitIso := (widePushoutShapeOpUnop J).symm
  counitIso := widePullbackShapeUnopOp J

/-- The duality equivalence `(WidePullbackShape J)·µí·µñ ‚âå WidePushoutShape J` -/
@[simps]
def widePullbackShapeOpEquiv : (WidePullbackShape J)·µí·µñ ‚âå WidePushoutShape J where
  functor := widePullbackShapeUnop J
  inverse := widePushoutShapeOp J
  unitIso := (widePullbackShapeOpUnop J).symm
  counitIso := widePushoutShapeUnopOp J

/-- If a category has wide pushouts on a higher universe level it also has wide pushouts
on a lower universe level. -/
theorem hasWidePushouts_shrink [HasWidePushouts.{max w w'} C] : HasWidePushouts.{w} C := fun _ ‚Ü¶
  hasColimitsOfShape_of_equivalence (WidePushoutShape.equivalenceOfEquiv _ Equiv.ulift.{w'})

/-- If a category has wide pullbacks on a higher universe level it also has wide pullbacks
on a lower universe level. -/
theorem hasWidePullbacks_shrink [HasWidePullbacks.{max w w'} C] : HasWidePullbacks.{w} C := fun _ ‚Ü¶
  hasLimitsOfShape_of_equivalence (WidePullbackShape.equivalenceOfEquiv _ Equiv.ulift.{w'})

end CategoryTheory.Limits
