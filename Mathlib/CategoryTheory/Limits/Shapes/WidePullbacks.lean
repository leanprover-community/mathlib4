/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Jakob von Raumer
-/
module

public import Mathlib.CategoryTheory.Limits.HasLimits
public import Mathlib.CategoryTheory.Thin

/-!
# Wide pullbacks

We define the category `WidePullbackShape`, (resp. `WidePushoutShape`) which is the category
obtained from a discrete category of type `J` by adjoining a terminal (resp. initial) element.
Limits of this shape are wide pullbacks (pushouts).
The convenience method `wideCospan` (`wideSpan`) constructs a functor from this category, hitting
the given morphisms.

We use `WidePullbackShape` to define ordinary pullbacks (pushouts) by using `J := WalkingPair`,
which allows easy proofs of some related lemmas.
Furthermore, wide pullbacks are used to show the existence of limits in the slice category.
Namely, if `C` has wide pullbacks then `C/B` has limits for any object `B` in `C`.

Typeclasses `HasWidePullbacks` and `HasFiniteWidePullbacks` assert the existence of wide
pullbacks and finite wide pullbacks.
-/

@[expose] public section

universe w w' v u

open CategoryTheory CategoryTheory.Limits Opposite

namespace CategoryTheory.Limits

variable (J : Type w)

/-- A wide pullback shape for any type `J` can be written simply as `Option J`. -/
def WidePullbackShape := Option J

instance : Inhabited (WidePullbackShape J) where
  default := none

/-- A wide pushout shape for any type `J` can be written simply as `Option J`. -/
def WidePushoutShape := Option J

instance : Inhabited (WidePushoutShape J) where
  default := none

namespace WidePullbackShape

variable {J}

-- Don't generate unnecessary `sizeOf_spec` lemma which the `simpNF` linter will complain about.
set_option genSizeOfSpec false in
/-- The type of arrows for the shape indexing a wide pullback. -/
inductive Hom : WidePullbackShape J â†’ WidePullbackShape J â†’ Type w
  | id : âˆ€ X, Hom X X
  | term : âˆ€ j : J, Hom (some j) none
  deriving DecidableEq

-- See https://github.com/leanprover/lean4/issues/10295
attribute [nolint unusedArguments] instDecidableEqHom.decEq

instance struct : CategoryStruct (WidePullbackShape J) where
  Hom := Hom
  id j := Hom.id j
  comp f g := by
    cases f
    Â· exact g
    cases g
    apply Hom.term _

instance Hom.inhabited : Inhabited (Hom (none : WidePullbackShape J) none) :=
  âŸ¨Hom.id (none : WidePullbackShape J)âŸ©

open Lean Elab Tactic
/- Pointing note: experimenting with manual scoping of aesop tactics. Attempted to define
aesop rule directing on `WidePushoutOut` and it didn't take for some reason -/
/-- An aesop tactic for bulk cases on morphisms in `WidePushoutShape` -/
def evalCasesBash : TacticM Unit := do
  evalTactic
    (â† `(tactic| casesm* WidePullbackShape _,
      (_ : WidePullbackShape _) âŸ¶ (_ : WidePullbackShape _)))

attribute [local aesop safe tactic (rule_sets := [CategoryTheory])] evalCasesBash

instance subsingleton_hom : Quiver.IsThin (WidePullbackShape J) := fun _ _ => by
  constructor
  intro a b
  casesm* WidePullbackShape _, (_ : WidePullbackShape _) âŸ¶ (_ : WidePullbackShape _)
  Â· rfl
  Â· rfl
  Â· rfl

instance category : SmallCategory (WidePullbackShape J) :=
  thin_category

@[simp]
theorem hom_id (X : WidePullbackShape J) : Hom.id X = ðŸ™ X :=
  rfl

variable {C : Type u} [Category.{v} C]

/-- Construct a functor out of the wide pullback shape given a J-indexed collection of arrows to a
fixed object.
-/
@[simps]
def wideCospan (B : C) (objs : J â†’ C) (arrows : âˆ€ j : J, objs j âŸ¶ B) : WidePullbackShape J â¥¤ C where
  obj j := Option.casesOn j B objs
  map f := by
    obtain - | j := f
    Â· apply ðŸ™ _
    Â· exact arrows j

/-- Every diagram is naturally isomorphic (actually, equal) to a `wideCospan` -/
def diagramIsoWideCospan (F : WidePullbackShape J â¥¤ C) :
    F â‰… wideCospan (F.obj none) (fun j => F.obj (some j)) fun j => F.map (Hom.term j) :=
  NatIso.ofComponents fun j => eqToIso <| by cat_disch

/-- Construct a cone over a wide cospan. -/
@[simps]
def mkCone {F : WidePullbackShape J â¥¤ C} {X : C} (f : X âŸ¶ F.obj none) (Ï€ : âˆ€ j, X âŸ¶ F.obj (some j))
    (w : âˆ€ j, Ï€ j â‰« F.map (Hom.term j) = f) : Cone F :=
  { pt := X
    Ï€ :=
      { app := fun j =>
          match j with
          | none => f
          | some j => Ï€ j
        naturality := fun j j' f => by
          cases j <;> cases j' <;> cases f <;> simp [w] } }

/-- Wide pullback diagrams of equivalent index types are equivalent. -/
def equivalenceOfEquiv (J' : Type w') (h : J â‰ƒ J') :
    WidePullbackShape J â‰Œ WidePullbackShape J' where
  functor := wideCospan none (fun j => some (h j)) fun j => Hom.term (h j)
  inverse := wideCospan none (fun j => some (h.invFun j)) fun j => Hom.term (h.invFun j)
  unitIso := NatIso.ofComponents (fun j => by cases j <;> exact eqToIso (by simp))
  counitIso := NatIso.ofComponents (fun j => by cases j <;> exact eqToIso (by simp))

@[simp]
lemma equivalenceOfEquiv_functor_obj_none {Î¹ Î¹' : Type*} (e : Î¹ â‰ƒ Î¹') :
    (WidePullbackShape.equivalenceOfEquiv _ e).functor.obj none = none := rfl

@[simp]
lemma equivalenceOfEquiv_functor_obj_some {Î¹ Î¹' : Type*} (e : Î¹ â‰ƒ Î¹') (i) :
    (WidePullbackShape.equivalenceOfEquiv _ e).functor.obj (some i) = some (e i) := rfl

@[simp]
lemma equivalenceOfEquiv_functor_map_term {Î¹ Î¹' : Type*} (e : Î¹ â‰ƒ Î¹') (i) :
    (WidePullbackShape.equivalenceOfEquiv _ e).functor.map (.term i) = .term (e i) := rfl

attribute [local instance] uliftCategory in
/-- Lifting universe and morphism levels preserves wide pullback diagrams. -/
def uliftEquivalence :
    ULiftHom.{w'} (ULift.{w'} (WidePullbackShape J)) â‰Œ WidePullbackShape (ULift J) :=
  (ULiftHomULiftCategory.equiv.{w', w', w, w} (WidePullbackShape J)).symm.trans
    (equivalenceOfEquiv _ (Equiv.ulift.{w', w}.symm : J â‰ƒ ULift.{w'} J))

/-- Show two functors out of a wide pullback shape are isomorphic by showing their components are
isomorphic. -/
@[simps!]
def functorExt {Î¹ : Type*} {F G : WidePullbackShape Î¹ â¥¤ C}
    (base : F.obj none â‰… G.obj none) (comp : âˆ€ i, F.obj (some i) â‰… G.obj (some i))
    (w : âˆ€ i, F.map (.term i) â‰« base.hom = (comp i).hom â‰« G.map (.term i) := by cat_disch) :
    F â‰… G :=
  NatIso.ofComponents
    (fun i â†¦ match i with
      | none => base
      | some i => comp i)
    (fun f â†¦ by rcases f <;> simp [w])

end WidePullbackShape

namespace WidePushoutShape

variable {J}

-- Don't generate unnecessary `sizeOf_spec` lemma which the `simpNF` linter will complain about.
set_option genSizeOfSpec false in
/-- The type of arrows for the shape indexing a wide pushout. -/
inductive Hom : WidePushoutShape J â†’ WidePushoutShape J â†’ Type w
  | id : âˆ€ X, Hom X X
  | init : âˆ€ j : J, Hom none (some j)
  deriving DecidableEq

-- See https://github.com/leanprover/lean4/issues/10295
attribute [nolint unusedArguments] instDecidableEqHom.decEq

instance struct : CategoryStruct (WidePushoutShape J) where
  Hom := Hom
  id j := Hom.id j
  comp f g := by
    cases f
    Â· exact g
    cases g
    apply Hom.init _

instance Hom.inhabited : Inhabited (Hom (none : WidePushoutShape J) none) :=
  âŸ¨Hom.id (none : WidePushoutShape J)âŸ©

open Lean Elab Tactic
-- Pointing note: experimenting with manual scoping of aesop tactics; only this worked
/-- An aesop tactic for bulk cases on morphisms in `WidePushoutShape` -/
def evalCasesBash' : TacticM Unit := do
  evalTactic
    (â† `(tactic| casesm* WidePushoutShape _,
      (_ : WidePushoutShape _) âŸ¶ (_ : WidePushoutShape _)))

attribute [local aesop safe tactic (rule_sets := [CategoryTheory])] evalCasesBash'

instance subsingleton_hom : Quiver.IsThin (WidePushoutShape J) := fun _ _ => by
  constructor
  intro a b
  casesm* WidePushoutShape _, (_ : WidePushoutShape _) âŸ¶ (_ : WidePushoutShape _)
  repeat rfl

instance category : SmallCategory (WidePushoutShape J) :=
  thin_category

@[simp]
theorem hom_id (X : WidePushoutShape J) : Hom.id X = ðŸ™ X :=
  rfl

variable {C : Type u} [Category.{v} C]

/-- Construct a functor out of the wide pushout shape given a J-indexed collection of arrows from a
fixed object.
-/
@[simps]
def wideSpan (B : C) (objs : J â†’ C) (arrows : âˆ€ j : J, B âŸ¶ objs j) : WidePushoutShape J â¥¤ C where
  obj j := Option.casesOn j B objs
  map f := by
    obtain - | j := f
    Â· apply ðŸ™ _
    Â· exact arrows j
  map_comp := fun f g => by
    cases f
    Â· simp only [hom_id, Category.id_comp]; congr
    Â· cases g
      simp only [hom_id, Category.comp_id]; congr

/-- Every diagram is naturally isomorphic (actually, equal) to a `wideSpan` -/
def diagramIsoWideSpan (F : WidePushoutShape J â¥¤ C) :
    F â‰… wideSpan (F.obj none) (fun j => F.obj (some j)) fun j => F.map (Hom.init j) :=
  NatIso.ofComponents fun j => eqToIso <| by cases j; repeat rfl

/-- Construct a cocone over a wide span. -/
@[simps]
def mkCocone {F : WidePushoutShape J â¥¤ C} {X : C} (f : F.obj none âŸ¶ X) (Î¹ : âˆ€ j, F.obj (some j) âŸ¶ X)
    (w : âˆ€ j, F.map (Hom.init j) â‰« Î¹ j = f) : Cocone F :=
  { pt := X
    Î¹ :=
      { app := fun j =>
          match j with
          | none => f
          | some j => Î¹ j
        naturality := fun j j' f => by
          cases j <;> cases j' <;> cases f <;> simp [w] } }

/-- Wide pushout diagrams of equivalent index types are equivalent. -/
def equivalenceOfEquiv (J' : Type w') (h : J â‰ƒ J') : WidePushoutShape J â‰Œ WidePushoutShape J' where
  functor := wideSpan none (fun j => some (h j)) fun j => Hom.init (h j)
  inverse := wideSpan none (fun j => some (h.invFun j)) fun j => Hom.init (h.invFun j)
  unitIso := NatIso.ofComponents (fun j => by cases j <;> exact eqToIso (by simp))
  counitIso := NatIso.ofComponents (fun j => by cases j <;> exact eqToIso (by simp))

attribute [local instance] uliftCategory in
/-- Lifting universe and morphism levels preserves wide pushout diagrams. -/
def uliftEquivalence :
    ULiftHom.{w'} (ULift.{w'} (WidePushoutShape J)) â‰Œ WidePushoutShape (ULift J) :=
  (ULiftHomULiftCategory.equiv.{w', w', w, w} (WidePushoutShape J)).symm.trans
    (equivalenceOfEquiv _ (Equiv.ulift.{w', w}.symm : J â‰ƒ ULift.{w'} J))

end WidePushoutShape

variable (C : Type u) [Category.{v} C]

/-- A category `HasWidePullbacks` if it has all limits of shape `WidePullbackShape J`, i.e. if it
has a wide pullback for every collection of morphisms with the same codomain. -/
abbrev HasWidePullbacks : Prop :=
  âˆ€ J : Type w, HasLimitsOfShape (WidePullbackShape J) C

/-- A category `HasWidePushouts` if it has all colimits of shape `WidePushoutShape J`, i.e. if it
has a wide pushout for every collection of morphisms with the same domain. -/
abbrev HasWidePushouts : Prop :=
  âˆ€ J : Type w, HasColimitsOfShape (WidePushoutShape J) C

variable {C J}

/-- `HasWidePullback B objs arrows` means that `wideCospan B objs arrows` has a limit. -/
abbrev HasWidePullback (B : C) (objs : J â†’ C) (arrows : âˆ€ j : J, objs j âŸ¶ B) : Prop :=
  HasLimit (WidePullbackShape.wideCospan B objs arrows)

/-- `HasWidePushout B objs arrows` means that `wideSpan B objs arrows` has a colimit. -/
abbrev HasWidePushout (B : C) (objs : J â†’ C) (arrows : âˆ€ j : J, B âŸ¶ objs j) : Prop :=
  HasColimit (WidePushoutShape.wideSpan B objs arrows)

/-- A choice of wide pullback. -/
noncomputable abbrev widePullback (B : C) (objs : J â†’ C) (arrows : âˆ€ j : J, objs j âŸ¶ B)
    [HasWidePullback B objs arrows] : C :=
  limit (WidePullbackShape.wideCospan B objs arrows)

/-- A choice of wide pushout. -/
noncomputable abbrev widePushout (B : C) (objs : J â†’ C) (arrows : âˆ€ j : J, B âŸ¶ objs j)
    [HasWidePushout B objs arrows] : C :=
  colimit (WidePushoutShape.wideSpan B objs arrows)

namespace WidePullback

variable {C : Type u} [Category.{v} C] {B : C} {objs : J â†’ C} (arrows : âˆ€ j : J, objs j âŸ¶ B)
variable [HasWidePullback B objs arrows]

/-- The `j`-th projection from the pullback. -/
noncomputable abbrev Ï€ (j : J) : widePullback _ _ arrows âŸ¶ objs j :=
  limit.Ï€ (WidePullbackShape.wideCospan _ _ _) (Option.some j)


/-- The unique map to the base from the pullback. -/
noncomputable abbrev base : widePullback _ _ arrows âŸ¶ B :=
  limit.Ï€ (WidePullbackShape.wideCospan _ _ _) Option.none

@[reassoc (attr := simp)]
theorem Ï€_arrow (j : J) : Ï€ arrows j â‰« arrows _ = base arrows := by
  apply limit.w (WidePullbackShape.wideCospan _ _ _) (WidePullbackShape.Hom.term j)

variable {arrows} in
/-- Lift a collection of morphisms to a morphism to the pullback. -/
noncomputable abbrev lift {X : C} (f : X âŸ¶ B) (fs : âˆ€ j : J, X âŸ¶ objs j)
    (w : âˆ€ j, fs j â‰« arrows j = f) : X âŸ¶ widePullback _ _ arrows :=
  limit.lift (WidePullbackShape.wideCospan _ _ _) (WidePullbackShape.mkCone f fs <| w)

variable {X : C} (f : X âŸ¶ B) (fs : âˆ€ j : J, X âŸ¶ objs j) (w : âˆ€ j, fs j â‰« arrows j = f)

@[reassoc]
theorem lift_Ï€ (j : J) : lift f fs w â‰« Ï€ arrows j = fs _ := by
  simp only [limit.lift_Ï€, WidePullbackShape.mkCone_pt, WidePullbackShape.mkCone_Ï€_app]

@[reassoc]
theorem lift_base : lift f fs w â‰« base arrows = f := by
  simp only [limit.lift_Ï€, WidePullbackShape.mkCone_pt, WidePullbackShape.mkCone_Ï€_app]

theorem eq_lift_of_comp_eq (g : X âŸ¶ widePullback _ _ arrows) :
    (âˆ€ j : J, g â‰« Ï€ arrows j = fs j) â†’ g â‰« base arrows = f â†’ g = lift f fs w := by
  intro h1 h2
  apply
    (limit.isLimit (WidePullbackShape.wideCospan B objs arrows)).uniq
      (WidePullbackShape.mkCone f fs <| w)
  rintro (_ | _)
  Â· apply h2
  Â· apply h1

theorem hom_eq_lift (g : X âŸ¶ widePullback _ _ arrows) :
    g = lift (g â‰« base arrows) (fun j => g â‰« Ï€ arrows j) (by simp) := by
  aesop

@[ext 1100]
theorem hom_ext (g1 g2 : X âŸ¶ widePullback _ _ arrows) : (âˆ€ j : J,
    g1 â‰« Ï€ arrows j = g2 â‰« Ï€ arrows j) â†’ g1 â‰« base arrows = g2 â‰« base arrows â†’ g1 = g2 := by
  intro h1 h2
  apply limit.hom_ext
  rintro (_ | _)
  Â· apply h2
  Â· apply h1

end WidePullback

/-- A wide pullback cone is a cone on the wide cospan formed by a family of morphisms. -/
abbrev WidePullbackCone {Î¹ : Type*} {X : C} {Y : Î¹ â†’ C} (f : âˆ€ i, Y i âŸ¶ X) :=
  Cone (WidePullbackShape.wideCospan X Y f)

namespace WidePullbackCone

variable {Î¹ : Type*} {X : C} {Y : Î¹ â†’ C} {f : âˆ€ i, Y i âŸ¶ X}

/-- The projection on the components of a wide pullback cone. -/
def Ï€ (s : WidePullbackCone f) (i : Î¹) : s.pt âŸ¶ Y i :=
  (Cone.Ï€ s).app (some i)

/-- The projection to the base of a wide pullback cone. -/
def base (s : WidePullbackCone f) : s.pt âŸ¶ X :=
  (Cone.Ï€ s).app none

@[reassoc (attr := simp)]
lemma condition (s : WidePullbackCone f) (i : Î¹) : s.Ï€ i â‰« f i = s.base := by
  simpa using ((Cone.Ï€ s).naturality (.term i)).symm

/-- Construct a wide pullback cone from the projections. -/
@[simps! pt]
def mk {W : C} (b : W âŸ¶ X) (Ï€ : âˆ€ i, W âŸ¶ Y i) (h : âˆ€ i, Ï€ i â‰« f i = b) :
    WidePullbackCone f :=
  WidePullbackShape.mkCone b Ï€ h

@[simp]
lemma mk_base {W : C} (b : W âŸ¶ X) (Ï€ : âˆ€ i, W âŸ¶ Y i) (h : âˆ€ i, Ï€ i â‰« f i = b) :
    (WidePullbackCone.mk b Ï€ h).base = b := rfl

@[simp]
lemma mk_Ï€ {W : C} (b : W âŸ¶ X) (Ï€ : âˆ€ i, W âŸ¶ Y i) (h : âˆ€ i, Ï€ i â‰« f i = b) (i : Î¹) :
    (WidePullbackCone.mk b Ï€ h).Ï€ i = Ï€ i := rfl

/-- Constructor to show a wide pullback cone is limiting. -/
def IsLimit.mk (s : WidePullbackCone f) (lift : âˆ€ t : WidePullbackCone f, t.pt âŸ¶ s.pt)
    (facbase : âˆ€ t, lift t â‰« s.base = t.base) (facÏ€ : âˆ€ t i, lift t â‰« s.Ï€ i = t.Ï€ i)
    (uniq : âˆ€ (t) (m : t.pt âŸ¶ s.pt), m â‰« s.base = t.base â†’ (âˆ€ i, m â‰« s.Ï€ i = t.Ï€ i) â†’ m = lift t) :
    IsLimit s where
  lift := lift
  fac t j := by
    cases j
    Â· exact facbase t
    Â· exact facÏ€ t _
  uniq t m hm := uniq _ _ (hm none) fun _ â†¦ hm (some _)

lemma IsLimit.hom_ext {s : WidePullbackCone f} (hs : IsLimit s)
    {W : C} {k l : W âŸ¶ s.pt} (hbase : k â‰« s.base = l â‰« s.base)
    (hÏ€ : âˆ€ i, k â‰« s.Ï€ i = l â‰« s.Ï€ i) :
    k = l := by
  apply hs.hom_ext
  rintro (_ | j)
  Â· exact hbase
  Â· exact hÏ€ j

/-- Lift a family of morphisms to a limiting wide pullback cone. -/
def IsLimit.lift {s : WidePullbackCone f} (hs : IsLimit s)
    {W : C} (b : W âŸ¶ X) (a : âˆ€ i, W âŸ¶ Y i) (w : âˆ€ i, a i â‰« f i = b) :
    W âŸ¶ s.pt :=
  hs.lift (WidePullbackCone.mk b a w)

@[reassoc (attr := simp)]
lemma IsLimit.lift_base {s : WidePullbackCone f} (hs : IsLimit s)
    {W : C} (b : W âŸ¶ X) (a : âˆ€ i, W âŸ¶ Y i) (w : âˆ€ i, a i â‰« f i = b) :
    IsLimit.lift hs b a w â‰« s.base = b :=
  hs.fac _ _

@[reassoc (attr := simp)]
lemma IsLimit.lift_Ï€ {s : WidePullbackCone f} (hs : IsLimit s)
    {W : C} (b : W âŸ¶ X) (a : âˆ€ i, W âŸ¶ Y i) (w : âˆ€ i, a i â‰« f i = b) (i : Î¹) :
    IsLimit.lift hs b a w â‰« s.Ï€ i = a i :=
  hs.fac _ _

/-- To show two wide pullback cones are isomorphic, it suffices to give a compatible isomorphism
of their cone points. -/
def ext {Î¹ : Type*}
    {X : C} {Y : Î¹ â†’ C} {f : âˆ€ i, Y i âŸ¶ X} {s t : WidePullbackCone f}
    (e : s.pt â‰… t.pt)
    (base : e.hom â‰« t.base = s.base := by cat_disch)
    (Ï€ : âˆ€ i, e.hom â‰« t.Ï€ i = s.Ï€ i := by cat_disch) :
    s â‰… t :=
  Cones.ext e <| by
    rintro (_ | _)
    Â· exact base.symm
    Â· exact (Ï€ _).symm

/-- Reindex a wide pullback cone. -/
@[simps! pt]
def reindex {Î¹ : Type*} {X : C} {Y : Î¹ â†’ C} {f : âˆ€ i, Y i âŸ¶ X} (s : WidePullbackCone f)
    {Î¹' : Type*} (e : Î¹' â‰ƒ Î¹) :
    WidePullbackCone (fun i â†¦ f (e i)) :=
  .mk s.base (fun i â†¦ s.Ï€ _) (by simp)

@[simp]
lemma reindex_base {Î¹ : Type*} {X : C} {Y : Î¹ â†’ C} {f : âˆ€ i, Y i âŸ¶ X} (s : WidePullbackCone f)
    {Î¹' : Type*} (e : Î¹' â‰ƒ Î¹) :
    (s.reindex e).base = s.base := rfl

@[simp]
lemma reindex_Ï€ {Î¹ : Type*} {X : C} {Y : Î¹ â†’ C} {f : âˆ€ i, Y i âŸ¶ X} (s : WidePullbackCone f)
    {Î¹' : Type*} (e : Î¹' â‰ƒ Î¹) (i : Î¹') :
    (s.reindex e).Ï€ i = s.Ï€ (e i) := rfl

/-- Reindexing a pullback cone preserves being limiting. -/
def reindexIsLimitEquiv {Î¹ : Type*} {X : C} {Y : Î¹ â†’ C} {f : âˆ€ i, Y i âŸ¶ X}
    (s : WidePullbackCone f) {Î¹' : Type*} (e : Î¹' â‰ƒ Î¹) :
    IsLimit (s.reindex e) â‰ƒ IsLimit s :=
  (IsLimit.whiskerEquivalenceEquiv <| WidePullbackShape.equivalenceOfEquiv _ e.symm).trans <|
    IsLimit.equivOfNatIsoOfIso
      (WidePullbackShape.functorExt (Iso.refl X) (fun i â†¦ eqToIso (by simp))
        fun i â†¦ by simp [â† eqToHom_naturality]) _ _
      (WidePullbackCone.ext (Iso.refl _) (by simp [base, reindex, mk])
        (fun i â†¦ by
          simp [Ï€, reindex, mk,
            eqToHom_naturality (fun i â†¦ (Cone.Ï€ s).app (some i)) (e.apply_symm_apply i)]))

end WidePullbackCone

namespace WidePushout

variable {C : Type u} [Category.{v} C] {B : C} {objs : J â†’ C} (arrows : âˆ€ j : J, B âŸ¶ objs j)
variable [HasWidePushout B objs arrows]

/-- The `j`-th inclusion to the pushout. -/
noncomputable abbrev Î¹ (j : J) : objs j âŸ¶ widePushout _ _ arrows :=
  colimit.Î¹ (WidePushoutShape.wideSpan _ _ _) (Option.some j)

/-- The unique map from the head to the pushout. -/
noncomputable abbrev head : B âŸ¶ widePushout B objs arrows :=
  colimit.Î¹ (WidePushoutShape.wideSpan _ _ _) Option.none

@[reassoc, simp]
theorem arrow_Î¹ (j : J) : arrows j â‰« Î¹ arrows j = head arrows := by
  apply colimit.w (WidePushoutShape.wideSpan _ _ _) (WidePushoutShape.Hom.init j)

variable {arrows} in
/-- Descend a collection of morphisms to a morphism from the pushout. -/
noncomputable abbrev desc {X : C} (f : B âŸ¶ X) (fs : âˆ€ j : J, objs j âŸ¶ X)
    (w : âˆ€ j, arrows j â‰« fs j = f) : widePushout _ _ arrows âŸ¶ X :=
  colimit.desc (WidePushoutShape.wideSpan B objs arrows) (WidePushoutShape.mkCocone f fs <| w)

variable {X : C} (f : B âŸ¶ X) (fs : âˆ€ j : J, objs j âŸ¶ X) (w : âˆ€ j, arrows j â‰« fs j = f)

@[reassoc]
theorem Î¹_desc (j : J) : Î¹ arrows j â‰« desc f fs w = fs _ := by
  simp only [colimit.Î¹_desc, WidePushoutShape.mkCocone_pt, WidePushoutShape.mkCocone_Î¹_app]

@[reassoc]
theorem head_desc : head arrows â‰« desc f fs w = f := by
  simp only [colimit.Î¹_desc, WidePushoutShape.mkCocone_pt, WidePushoutShape.mkCocone_Î¹_app]

theorem eq_desc_of_comp_eq (g : widePushout _ _ arrows âŸ¶ X) :
    (âˆ€ j : J, Î¹ arrows j â‰« g = fs j) â†’ head arrows â‰« g = f â†’ g = desc f fs w := by
  intro h1 h2
  apply
    (colimit.isColimit (WidePushoutShape.wideSpan B objs arrows)).uniq
      (WidePushoutShape.mkCocone f fs <| w)
  rintro (_ | _)
  Â· apply h2
  Â· apply h1

theorem hom_eq_desc (g : widePushout _ _ arrows âŸ¶ X) :
    g =
      desc (head arrows â‰« g) (fun j => Î¹ arrows j â‰« g) fun j => by
        rw [â† Category.assoc]
        simp := by
  cat_disch

@[ext 1100]
theorem hom_ext (g1 g2 : widePushout _ _ arrows âŸ¶ X) : (âˆ€ j : J,
    Î¹ arrows j â‰« g1 = Î¹ arrows j â‰« g2) â†’ head arrows â‰« g1 = head arrows â‰« g2 â†’ g1 = g2 := by
  intro h1 h2
  apply colimit.hom_ext
  rintro (_ | _)
  Â· apply h2
  Â· apply h1

end WidePushout

variable (J)

/-- The action on morphisms of the obvious functor
  `WidePullbackShape_op : WidePullbackShape J â¥¤ (WidePushoutShape J)áµ’áµ–` -/
def widePullbackShapeOpMap :
    âˆ€ X Y : WidePullbackShape J,
      (X âŸ¶ Y) â†’ ((op X : (WidePushoutShape J)áµ’áµ–) âŸ¶ (op Y : (WidePushoutShape J)áµ’áµ–))
  | _, _, WidePullbackShape.Hom.id X => Quiver.Hom.op (WidePushoutShape.Hom.id _)
  | _, _, WidePullbackShape.Hom.term _ => Quiver.Hom.op (WidePushoutShape.Hom.init _)

/-- The obvious functor `WidePullbackShape J â¥¤ (WidePushoutShape J)áµ’áµ–` -/
@[simps]
def widePullbackShapeOp : WidePullbackShape J â¥¤ (WidePushoutShape J)áµ’áµ– where
  obj X := op X
  map {Xâ‚} {Xâ‚‚} := widePullbackShapeOpMap J Xâ‚ Xâ‚‚

/-- The action on morphisms of the obvious functor
`widePushoutShapeOp : WidePushoutShape J â¥¤ (WidePullbackShape J)áµ’áµ–` -/
def widePushoutShapeOpMap :
    âˆ€ X Y : WidePushoutShape J,
      (X âŸ¶ Y) â†’ ((op X : (WidePullbackShape J)áµ’áµ–) âŸ¶ (op Y : (WidePullbackShape J)áµ’áµ–))
  | _, _, WidePushoutShape.Hom.id X => Quiver.Hom.op (WidePullbackShape.Hom.id _)
  | _, _, WidePushoutShape.Hom.init _ => Quiver.Hom.op (WidePullbackShape.Hom.term _)

/-- The obvious functor `WidePushoutShape J â¥¤ (WidePullbackShape J)áµ’áµ–` -/
@[simps]
def widePushoutShapeOp : WidePushoutShape J â¥¤ (WidePullbackShape J)áµ’áµ– where
  obj X := op X
  map := fun {X} {Y} => widePushoutShapeOpMap J X Y

/-- The obvious functor `(WidePullbackShape J)áµ’áµ– â¥¤ WidePushoutShape J` -/
@[simps!]
def widePullbackShapeUnop : (WidePullbackShape J)áµ’áµ– â¥¤ WidePushoutShape J :=
  (widePullbackShapeOp J).leftOp

/-- The obvious functor `(WidePushoutShape J)áµ’áµ– â¥¤ WidePullbackShape J` -/
@[simps!]
def widePushoutShapeUnop : (WidePushoutShape J)áµ’áµ– â¥¤ WidePullbackShape J :=
  (widePushoutShapeOp J).leftOp

/-- The inverse of the unit isomorphism of the equivalence
`widePushoutShapeOpEquiv : (WidePushoutShape J)áµ’áµ– â‰Œ WidePullbackShape J` -/
def widePushoutShapeOpUnop : widePushoutShapeUnop J â‹™ widePullbackShapeOp J â‰… ðŸ­ _ :=
  NatIso.ofComponents fun _ => Iso.refl _

/-- The counit isomorphism of the equivalence
`widePullbackShapeOpEquiv : (WidePullbackShape J)áµ’áµ– â‰Œ WidePushoutShape J` -/
def widePushoutShapeUnopOp : widePushoutShapeOp J â‹™ widePullbackShapeUnop J â‰… ðŸ­ _ :=
  NatIso.ofComponents fun _ => Iso.refl _

/-- The inverse of the unit isomorphism of the equivalence
`widePullbackShapeOpEquiv : (WidePullbackShape J)áµ’áµ– â‰Œ WidePushoutShape J` -/
def widePullbackShapeOpUnop : widePullbackShapeUnop J â‹™ widePushoutShapeOp J â‰… ðŸ­ _ :=
  NatIso.ofComponents fun _ => Iso.refl _

/-- The counit isomorphism of the equivalence
`widePushoutShapeOpEquiv : (WidePushoutShape J)áµ’áµ– â‰Œ WidePullbackShape J` -/
def widePullbackShapeUnopOp : widePullbackShapeOp J â‹™ widePushoutShapeUnop J â‰… ðŸ­ _ :=
  NatIso.ofComponents fun _ => Iso.refl _

/-- The duality equivalence `(WidePushoutShape J)áµ’áµ– â‰Œ WidePullbackShape J` -/
@[simps]
def widePushoutShapeOpEquiv : (WidePushoutShape J)áµ’áµ– â‰Œ WidePullbackShape J where
  functor := widePushoutShapeUnop J
  inverse := widePullbackShapeOp J
  unitIso := (widePushoutShapeOpUnop J).symm
  counitIso := widePullbackShapeUnopOp J

/-- The duality equivalence `(WidePullbackShape J)áµ’áµ– â‰Œ WidePushoutShape J` -/
@[simps]
def widePullbackShapeOpEquiv : (WidePullbackShape J)áµ’áµ– â‰Œ WidePushoutShape J where
  functor := widePullbackShapeUnop J
  inverse := widePushoutShapeOp J
  unitIso := (widePullbackShapeOpUnop J).symm
  counitIso := widePushoutShapeUnopOp J

/-- If a category has wide pushouts on a higher universe level it also has wide pushouts
on a lower universe level. -/
theorem hasWidePushouts_shrink [HasWidePushouts.{max w w'} C] : HasWidePushouts.{w} C := fun _ =>
  hasColimitsOfShape_of_equivalence (WidePushoutShape.equivalenceOfEquiv _ Equiv.ulift.{w'})

/-- If a category has wide pullbacks on a higher universe level it also has wide pullbacks
on a lower universe level. -/
theorem hasWidePullbacks_shrink [HasWidePullbacks.{max w w'} C] : HasWidePullbacks.{w} C := fun _ =>
  hasLimitsOfShape_of_equivalence (WidePullbackShape.equivalenceOfEquiv _ Equiv.ulift.{w'})

end CategoryTheory.Limits
