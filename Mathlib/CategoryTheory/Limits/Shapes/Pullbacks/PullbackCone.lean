/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Markus Himmel, Bhavik Mehta, Andrew Yang, Emily Riehl
-/
import Mathlib.CategoryTheory.Limits.Shapes.Pullbacks.Basic

#align_import category_theory.limits.shapes.pullbacks from "leanprover-community/mathlib"@"7316286ff2942aa14e540add9058c6b0aa1c8070"

/-!
# PullbackCone (API for Cones)

This file provides API for interacting with cones (resp. cocones) in the case of pullbacks
(resp. pushouts).

In the following we shall explain the API in the case of pullbacks, but note that the dual API for
pushouts is also available in this file.

# Main definitions
* `PullbackCone f g`. A term `t : PullbackCone f g` provides the data of a cone
```
t.pt ---t.snd---> Y
  |               |
 t.fst            g
  |               |
  v               v
  X -----f------> Z
```
The type `PullbackCone f g` is implemented as an abbrevation for `Cone (cospan f g)`, so general
results about cones are also available for `PullbackCone`.

We provide API for the following results about pullback cones:
* Constructor: .mk


* go back and forth between cone/pullbackcone

* Interaction with `IsLimit`:
* * ....

* flip


Idea:
split file into two (or more) parts:
* general API for Pullback/Pushout cones
* API for IsLimit

## References
* [Stacks: Fibre products](https://stacks.math.columbia.edu/tag/001U)
* [Stacks: Pushouts](https://stacks.math.columbia.edu/tag/0025)
-/

noncomputable section

open CategoryTheory

universe w vâ‚ vâ‚‚ v u uâ‚‚

namespace CategoryTheory.Limits

open WalkingSpan.Hom WalkingCospan.Hom WidePullbackShape.Hom WidePushoutShape.Hom

variable {C : Type u} [Category.{v} C] {W X Y Z : C}

/-- A pullback cone is just a cone on the cospan formed by two morphisms `f : X âŸ¶ Z` and
    `g : Y âŸ¶ Z`. -/
abbrev PullbackCone (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :=
  Cone (cospan f g)
#align category_theory.limits.pullback_cone CategoryTheory.Limits.PullbackCone

namespace PullbackCone

variable {f : X âŸ¶ Z} {g : Y âŸ¶ Z}

/-- The first projection of a pullback cone. -/
abbrev fst (t : PullbackCone f g) : t.pt âŸ¶ X :=
  t.Ï€.app WalkingCospan.left
#align category_theory.limits.pullback_cone.fst CategoryTheory.Limits.PullbackCone.fst

/-- The second projection of a pullback cone. -/
abbrev snd (t : PullbackCone f g) : t.pt âŸ¶ Y :=
  t.Ï€.app WalkingCospan.right
#align category_theory.limits.pullback_cone.snd CategoryTheory.Limits.PullbackCone.snd

@[simp]
theorem Ï€_app_left (c : PullbackCone f g) : c.Ï€.app WalkingCospan.left = c.fst := rfl
#align category_theory.limits.pullback_cone.Ï€_app_left CategoryTheory.Limits.PullbackCone.Ï€_app_left

@[simp]
theorem Ï€_app_right (c : PullbackCone f g) : c.Ï€.app WalkingCospan.right = c.snd := rfl
#align category_theory.limits.pullback_cone.Ï€_app_right CategoryTheory.Limits.PullbackCone.Ï€_app_right

@[simp]
theorem condition_one (t : PullbackCone f g) : t.Ï€.app WalkingCospan.one = t.fst â‰« f := by
  have w := t.Ï€.naturality WalkingCospan.Hom.inl
  dsimp at w; simpa using w
#align category_theory.limits.pullback_cone.condition_one CategoryTheory.Limits.PullbackCone.condition_one

/-- This is a slightly more convenient method to verify that a pullback cone is a limit cone. It
    only asks for a proof of facts that carry any mathematical content -/
def isLimitAux (t : PullbackCone f g) (lift : âˆ€ s : PullbackCone f g, s.pt âŸ¶ t.pt)
    (fac_left : âˆ€ s : PullbackCone f g, lift s â‰« t.fst = s.fst)
    (fac_right : âˆ€ s : PullbackCone f g, lift s â‰« t.snd = s.snd)
    (uniq : âˆ€ (s : PullbackCone f g) (m : s.pt âŸ¶ t.pt)
      (_ : âˆ€ j : WalkingCospan, m â‰« t.Ï€.app j = s.Ï€.app j), m = lift s) : IsLimit t :=
  { lift
    fac := fun s j => Option.casesOn j (by
        rw [â† s.w inl, â† t.w inl, â† Category.assoc]
        congr
        exact fac_left s)
      fun j' => WalkingPair.casesOn j' (fac_left s) (fac_right s)
    uniq := uniq }
#align category_theory.limits.pullback_cone.is_limit_aux CategoryTheory.Limits.PullbackCone.isLimitAux

/-- This is another convenient method to verify that a pullback cone is a limit cone. It
    only asks for a proof of facts that carry any mathematical content, and allows access to the
    same `s` for all parts. -/
def isLimitAux' (t : PullbackCone f g)
    (create :
      âˆ€ s : PullbackCone f g,
        { l //
          l â‰« t.fst = s.fst âˆ§
            l â‰« t.snd = s.snd âˆ§ âˆ€ {m}, m â‰« t.fst = s.fst â†’ m â‰« t.snd = s.snd â†’ m = l }) :
    Limits.IsLimit t :=
  PullbackCone.isLimitAux t (fun s => (create s).1) (fun s => (create s).2.1)
    (fun s => (create s).2.2.1) fun s _ w =>
    (create s).2.2.2 (w WalkingCospan.left) (w WalkingCospan.right)
#align category_theory.limits.pullback_cone.is_limit_aux' CategoryTheory.Limits.PullbackCone.isLimitAux'

/-- A pullback cone on `f` and `g` is determined by morphisms `fst : W âŸ¶ X` and `snd : W âŸ¶ Y`
    such that `fst â‰« f = snd â‰« g`. -/
@[simps]
def mk {W : C} (fst : W âŸ¶ X) (snd : W âŸ¶ Y) (eq : fst â‰« f = snd â‰« g) : PullbackCone f g where
  pt := W
  Ï€ := { app := fun j => Option.casesOn j (fst â‰« f) fun j' => WalkingPair.casesOn j' fst snd
         naturality := by rintro (âŸ¨âŸ© | âŸ¨âŸ¨âŸ©âŸ©) (âŸ¨âŸ© | âŸ¨âŸ¨âŸ©âŸ©) j <;> cases j <;> dsimp <;> simp [eq] }
#align category_theory.limits.pullback_cone.mk CategoryTheory.Limits.PullbackCone.mk

@[simp]
theorem mk_Ï€_app_left {W : C} (fst : W âŸ¶ X) (snd : W âŸ¶ Y) (eq : fst â‰« f = snd â‰« g) :
    (mk fst snd eq).Ï€.app WalkingCospan.left = fst := rfl
#align category_theory.limits.pullback_cone.mk_Ï€_app_left CategoryTheory.Limits.PullbackCone.mk_Ï€_app_left

@[simp]
theorem mk_Ï€_app_right {W : C} (fst : W âŸ¶ X) (snd : W âŸ¶ Y) (eq : fst â‰« f = snd â‰« g) :
    (mk fst snd eq).Ï€.app WalkingCospan.right = snd := rfl
#align category_theory.limits.pullback_cone.mk_Ï€_app_right CategoryTheory.Limits.PullbackCone.mk_Ï€_app_right

@[simp]
theorem mk_Ï€_app_one {W : C} (fst : W âŸ¶ X) (snd : W âŸ¶ Y) (eq : fst â‰« f = snd â‰« g) :
    (mk fst snd eq).Ï€.app WalkingCospan.one = fst â‰« f := rfl
#align category_theory.limits.pullback_cone.mk_Ï€_app_one CategoryTheory.Limits.PullbackCone.mk_Ï€_app_one

@[simp]
theorem mk_fst {W : C} (fst : W âŸ¶ X) (snd : W âŸ¶ Y) (eq : fst â‰« f = snd â‰« g) :
    (mk fst snd eq).fst = fst := rfl
#align category_theory.limits.pullback_cone.mk_fst CategoryTheory.Limits.PullbackCone.mk_fst

@[simp]
theorem mk_snd {W : C} (fst : W âŸ¶ X) (snd : W âŸ¶ Y) (eq : fst â‰« f = snd â‰« g) :
    (mk fst snd eq).snd = snd := rfl
#align category_theory.limits.pullback_cone.mk_snd CategoryTheory.Limits.PullbackCone.mk_snd

@[reassoc]
theorem condition (t : PullbackCone f g) : fst t â‰« f = snd t â‰« g :=
  (t.w inl).trans (t.w inr).symm
#align category_theory.limits.pullback_cone.condition CategoryTheory.Limits.PullbackCone.condition

/-- To check whether a morphism is equalized by the maps of a pullback cone, it suffices to check
  it for `fst t` and `snd t` -/
theorem equalizer_ext (t : PullbackCone f g) {W : C} {k l : W âŸ¶ t.pt} (hâ‚€ : k â‰« fst t = l â‰« fst t)
    (hâ‚ : k â‰« snd t = l â‰« snd t) : âˆ€ j : WalkingCospan, k â‰« t.Ï€.app j = l â‰« t.Ï€.app j
  | some WalkingPair.left => hâ‚€
  | some WalkingPair.right => hâ‚
  | none => by rw [â† t.w inl, reassoc_of% hâ‚€]
#align category_theory.limits.pullback_cone.equalizer_ext CategoryTheory.Limits.PullbackCone.equalizer_ext

theorem IsLimit.hom_ext {t : PullbackCone f g} (ht : IsLimit t) {W : C} {k l : W âŸ¶ t.pt}
    (hâ‚€ : k â‰« fst t = l â‰« fst t) (hâ‚ : k â‰« snd t = l â‰« snd t) : k = l :=
  ht.hom_ext <| equalizer_ext _ hâ‚€ hâ‚
#align category_theory.limits.pullback_cone.is_limit.hom_ext CategoryTheory.Limits.PullbackCone.IsLimit.hom_ext

theorem mono_snd_of_is_pullback_of_mono {t : PullbackCone f g} (ht : IsLimit t) [Mono f] :
    Mono t.snd := by
  refine âŸ¨fun {W} h k i => IsLimit.hom_ext ht ?_ iâŸ©
  rw [â† cancel_mono f, Category.assoc, Category.assoc, condition]
  have := congrArg (Â· â‰« g) i; dsimp at this
  rwa [Category.assoc, Category.assoc] at this
#align category_theory.limits.pullback_cone.mono_snd_of_is_pullback_of_mono CategoryTheory.Limits.PullbackCone.mono_snd_of_is_pullback_of_mono

theorem mono_fst_of_is_pullback_of_mono {t : PullbackCone f g} (ht : IsLimit t) [Mono g] :
    Mono t.fst := by
  refine âŸ¨fun {W} h k i => IsLimit.hom_ext ht i ?_âŸ©
  rw [â† cancel_mono g, Category.assoc, Category.assoc, â† condition]
  have := congrArg (Â· â‰« f) i; dsimp at this
  rwa [Category.assoc, Category.assoc] at this
#align category_theory.limits.pullback_cone.mono_fst_of_is_pullback_of_mono CategoryTheory.Limits.PullbackCone.mono_fst_of_is_pullback_of_mono

/-- To construct an isomorphism of pullback cones, it suffices to construct an isomorphism
of the cone points and check it commutes with `fst` and `snd`. -/
def ext {s t : PullbackCone f g} (i : s.pt â‰… t.pt) (wâ‚ : s.fst = i.hom â‰« t.fst)
    (wâ‚‚ : s.snd = i.hom â‰« t.snd) : s â‰… t :=
  WalkingCospan.ext i wâ‚ wâ‚‚
#align category_theory.limits.pullback_cone.ext CategoryTheory.Limits.PullbackCone.ext

-- Porting note: `IsLimit.lift` and the two following simp lemmas were introduced to ease the port
/-- If `t` is a limit pullback cone over `f` and `g` and `h : W âŸ¶ X` and `k : W âŸ¶ Y` are such that
    `h â‰« f = k â‰« g`, then we get `l : W âŸ¶ t.pt`, which satisfies `l â‰« fst t = h`
    and `l â‰« snd t = k`, see `IsLimit.lift_fst` and `IsLimit.lift_snd`. -/
def IsLimit.lift {t : PullbackCone f g} (ht : IsLimit t) {W : C} (h : W âŸ¶ X) (k : W âŸ¶ Y)
    (w : h â‰« f = k â‰« g) : W âŸ¶ t.pt :=
  ht.lift <| PullbackCone.mk _ _ w

@[reassoc (attr := simp)]
lemma IsLimit.lift_fst {t : PullbackCone f g} (ht : IsLimit t) {W : C} (h : W âŸ¶ X) (k : W âŸ¶ Y)
    (w : h â‰« f = k â‰« g) : IsLimit.lift ht h k w â‰« fst t = h := ht.fac _ _

@[reassoc (attr := simp)]
lemma IsLimit.lift_snd {t : PullbackCone f g} (ht : IsLimit t) {W : C} (h : W âŸ¶ X) (k : W âŸ¶ Y)
    (w : h â‰« f = k â‰« g) : IsLimit.lift ht h k w â‰« snd t = k := ht.fac _ _

/-- If `t` is a limit pullback cone over `f` and `g` and `h : W âŸ¶ X` and `k : W âŸ¶ Y` are such that
    `h â‰« f = k â‰« g`, then we have `l : W âŸ¶ t.pt` satisfying `l â‰« fst t = h` and `l â‰« snd t = k`.
    -/
def IsLimit.lift' {t : PullbackCone f g} (ht : IsLimit t) {W : C} (h : W âŸ¶ X) (k : W âŸ¶ Y)
    (w : h â‰« f = k â‰« g) : { l : W âŸ¶ t.pt // l â‰« fst t = h âˆ§ l â‰« snd t = k } :=
  âŸ¨IsLimit.lift ht h k w, by simpâŸ©
#align category_theory.limits.pullback_cone.is_limit.lift' CategoryTheory.Limits.PullbackCone.IsLimit.lift'

/-- This is a more convenient formulation to show that a `PullbackCone` constructed using
`PullbackCone.mk` is a limit cone.
-/
def IsLimit.mk {W : C} {fst : W âŸ¶ X} {snd : W âŸ¶ Y} (eq : fst â‰« f = snd â‰« g)
    (lift : âˆ€ s : PullbackCone f g, s.pt âŸ¶ W)
    (fac_left : âˆ€ s : PullbackCone f g, lift s â‰« fst = s.fst)
    (fac_right : âˆ€ s : PullbackCone f g, lift s â‰« snd = s.snd)
    (uniq :
      âˆ€ (s : PullbackCone f g) (m : s.pt âŸ¶ W) (_ : m â‰« fst = s.fst) (_ : m â‰« snd = s.snd),
        m = lift s) :
    IsLimit (mk fst snd eq) :=
  isLimitAux _ lift fac_left fac_right fun s m w =>
    uniq s m (w WalkingCospan.left) (w WalkingCospan.right)
#align category_theory.limits.pullback_cone.is_limit.mk CategoryTheory.Limits.PullbackCone.IsLimit.mk

section Flip

variable (t : PullbackCone f g)

/-- The pullback cone obtained by flipping `fst` and `snd`. -/
def flip : PullbackCone g f := PullbackCone.mk _ _ t.condition.symm

@[simp] lemma flip_pt : t.flip.pt = t.pt := rfl
@[simp] lemma flip_fst : t.flip.fst = t.snd := rfl
@[simp] lemma flip_snd : t.flip.snd = t.fst := rfl

/-- Flipping a pullback cone twice gives an isomorphic cone. -/
def flipFlipIso : t.flip.flip â‰… t := PullbackCone.ext (Iso.refl _) (by simp) (by simp)

variable {t}

/-- The flip of a pullback square is a pullback square. -/
def flipIsLimit (ht : IsLimit t) : IsLimit t.flip :=
  IsLimit.mk _ (fun s => ht.lift s.flip) (by simp) (by simp) (fun s m hâ‚ hâ‚‚ => by
    apply IsLimit.hom_ext ht
    all_goals aesop_cat)

/-- A square is a pullback square if its flip is. -/
def isLimitOfFlip (ht : IsLimit t.flip) : IsLimit t :=
  IsLimit.ofIsoLimit (flipIsLimit ht) t.flipFlipIso
#align category_theory.limits.pullback_cone.flip_is_limit CategoryTheory.Limits.PullbackCone.isLimitOfFlip

end Flip

/--
The pullback cone `(ğŸ™ X, ğŸ™ X)` for the pair `(f, f)` is a limit if `f` is a mono. The converse is
shown in `mono_of_pullback_is_id`.
-/
def isLimitMkIdId (f : X âŸ¶ Y) [Mono f] : IsLimit (mk (ğŸ™ X) (ğŸ™ X) rfl : PullbackCone f f) :=
  IsLimit.mk _ (fun s => s.fst) (fun s => Category.comp_id _)
    (fun s => by rw [â† cancel_mono f, Category.comp_id, s.condition]) fun s m mâ‚ _ => by
    simpa using mâ‚
#align category_theory.limits.pullback_cone.is_limit_mk_id_id CategoryTheory.Limits.PullbackCone.isLimitMkIdId

/--
`f` is a mono if the pullback cone `(ğŸ™ X, ğŸ™ X)` is a limit for the pair `(f, f)`. The converse is
given in `PullbackCone.is_id_of_mono`.
-/
theorem mono_of_isLimitMkIdId (f : X âŸ¶ Y) (t : IsLimit (mk (ğŸ™ X) (ğŸ™ X) rfl : PullbackCone f f)) :
    Mono f :=
  âŸ¨fun {Z} g h eq => by
    rcases PullbackCone.IsLimit.lift' t _ _ eq with âŸ¨_, rfl, rflâŸ©
    rflâŸ©
#align category_theory.limits.pullback_cone.mono_of_is_limit_mk_id_id CategoryTheory.Limits.PullbackCone.mono_of_isLimitMkIdId

/-- Suppose `f` and `g` are two morphisms with a common codomain and `s` is a limit cone over the
    diagram formed by `f` and `g`. Suppose `f` and `g` both factor through a monomorphism `h` via
    `x` and `y`, respectively.  Then `s` is also a limit cone over the diagram formed by `x` and
    `y`.  -/
def isLimitOfFactors (f : X âŸ¶ Z) (g : Y âŸ¶ Z) (h : W âŸ¶ Z) [Mono h] (x : X âŸ¶ W) (y : Y âŸ¶ W)
    (hxh : x â‰« h = f) (hyh : y â‰« h = g) (s : PullbackCone f g) (hs : IsLimit s) :
    IsLimit
      (PullbackCone.mk _ _
        (show s.fst â‰« x = s.snd â‰« y from
          (cancel_mono h).1 <| by simp only [Category.assoc, hxh, hyh, s.condition])) :=
  PullbackCone.isLimitAux' _ fun t =>
    have : fst t â‰« x â‰« h = snd t â‰« y â‰« h := by  -- Porting note: reassoc workaround
      rw [â† Category.assoc, â† Category.assoc]
      apply congrArg (Â· â‰« h) t.condition
    âŸ¨hs.lift (PullbackCone.mk t.fst t.snd <| by rw [â† hxh, â† hyh, this]),
      âŸ¨hs.fac _ WalkingCospan.left, hs.fac _ WalkingCospan.right, fun hr hr' => by
        apply PullbackCone.IsLimit.hom_ext hs <;>
              simp only [PullbackCone.mk_fst, PullbackCone.mk_snd] at hr hr' âŠ¢ <;>
            simp only [hr, hr'] <;>
          symm
        exacts [hs.fac _ WalkingCospan.left, hs.fac _ WalkingCospan.right]âŸ©âŸ©
#align category_theory.limits.pullback_cone.is_limit_of_factors CategoryTheory.Limits.PullbackCone.isLimitOfFactors

/-- If `W` is the pullback of `f, g`,
it is also the pullback of `f â‰« i, g â‰« i` for any mono `i`. -/
def isLimitOfCompMono (f : X âŸ¶ W) (g : Y âŸ¶ W) (i : W âŸ¶ Z) [Mono i] (s : PullbackCone f g)
    (H : IsLimit s) :
    IsLimit
      (PullbackCone.mk _ _
        (show s.fst â‰« f â‰« i = s.snd â‰« g â‰« i by
          rw [â† Category.assoc, â† Category.assoc, s.condition])) := by
  apply PullbackCone.isLimitAux'
  intro s
  rcases PullbackCone.IsLimit.lift' H s.fst s.snd
      ((cancel_mono i).mp (by simpa using s.condition)) with
    âŸ¨l, hâ‚, hâ‚‚âŸ©
  refine âŸ¨l, hâ‚, hâ‚‚, ?_âŸ©
  intro m hmâ‚ hmâ‚‚
  exact (PullbackCone.IsLimit.hom_ext H (hmâ‚.trans hâ‚.symm) (hmâ‚‚.trans hâ‚‚.symm) : _)
#align category_theory.limits.pullback_cone.is_limit_of_comp_mono CategoryTheory.Limits.PullbackCone.isLimitOfCompMono

end PullbackCone

/-- A pushout cocone is just a cocone on the span formed by two morphisms `f : X âŸ¶ Y` and
    `g : X âŸ¶ Z`. -/
abbrev PushoutCocone (f : X âŸ¶ Y) (g : X âŸ¶ Z) :=
  Cocone (span f g)
#align category_theory.limits.pushout_cocone CategoryTheory.Limits.PushoutCocone

namespace PushoutCocone

variable {f : X âŸ¶ Y} {g : X âŸ¶ Z}

/-- The first inclusion of a pushout cocone. -/
abbrev inl (t : PushoutCocone f g) : Y âŸ¶ t.pt :=
  t.Î¹.app WalkingSpan.left
#align category_theory.limits.pushout_cocone.inl CategoryTheory.Limits.PushoutCocone.inl

/-- The second inclusion of a pushout cocone. -/
abbrev inr (t : PushoutCocone f g) : Z âŸ¶ t.pt :=
  t.Î¹.app WalkingSpan.right
#align category_theory.limits.pushout_cocone.inr CategoryTheory.Limits.PushoutCocone.inr

@[simp]
theorem Î¹_app_left (c : PushoutCocone f g) : c.Î¹.app WalkingSpan.left = c.inl := rfl
#align category_theory.limits.pushout_cocone.Î¹_app_left CategoryTheory.Limits.PushoutCocone.Î¹_app_left

@[simp]
theorem Î¹_app_right (c : PushoutCocone f g) : c.Î¹.app WalkingSpan.right = c.inr := rfl
#align category_theory.limits.pushout_cocone.Î¹_app_right CategoryTheory.Limits.PushoutCocone.Î¹_app_right

@[simp]
theorem condition_zero (t : PushoutCocone f g) : t.Î¹.app WalkingSpan.zero = f â‰« t.inl := by
  have w := t.Î¹.naturality WalkingSpan.Hom.fst
  dsimp at w; simpa using w.symm
#align category_theory.limits.pushout_cocone.condition_zero CategoryTheory.Limits.PushoutCocone.condition_zero

/-- This is a slightly more convenient method to verify that a pushout cocone is a colimit cocone.
    It only asks for a proof of facts that carry any mathematical content -/
def isColimitAux (t : PushoutCocone f g) (desc : âˆ€ s : PushoutCocone f g, t.pt âŸ¶ s.pt)
    (fac_left : âˆ€ s : PushoutCocone f g, t.inl â‰« desc s = s.inl)
    (fac_right : âˆ€ s : PushoutCocone f g, t.inr â‰« desc s = s.inr)
    (uniq : âˆ€ (s : PushoutCocone f g) (m : t.pt âŸ¶ s.pt)
    (_ : âˆ€ j : WalkingSpan, t.Î¹.app j â‰« m = s.Î¹.app j), m = desc s) : IsColimit t :=
  { desc
    fac := fun s j =>
      Option.casesOn j (by simp [â† s.w fst, â† t.w fst, fac_left s]) fun j' =>
        WalkingPair.casesOn j' (fac_left s) (fac_right s)
    uniq := uniq }
#align category_theory.limits.pushout_cocone.is_colimit_aux CategoryTheory.Limits.PushoutCocone.isColimitAux

/-- This is another convenient method to verify that a pushout cocone is a colimit cocone. It
    only asks for a proof of facts that carry any mathematical content, and allows access to the
    same `s` for all parts. -/
def isColimitAux' (t : PushoutCocone f g)
    (create :
      âˆ€ s : PushoutCocone f g,
        { l //
          t.inl â‰« l = s.inl âˆ§
            t.inr â‰« l = s.inr âˆ§ âˆ€ {m}, t.inl â‰« m = s.inl â†’ t.inr â‰« m = s.inr â†’ m = l }) :
    IsColimit t :=
  isColimitAux t (fun s => (create s).1) (fun s => (create s).2.1) (fun s => (create s).2.2.1)
    fun s _ w => (create s).2.2.2 (w WalkingCospan.left) (w WalkingCospan.right)
#align category_theory.limits.pushout_cocone.is_colimit_aux' CategoryTheory.Limits.PushoutCocone.isColimitAux'

/-- A pushout cocone on `f` and `g` is determined by morphisms `inl : Y âŸ¶ W` and `inr : Z âŸ¶ W` such
    that `f â‰« inl = g â†  inr`. -/
@[simps]
def mk {W : C} (inl : Y âŸ¶ W) (inr : Z âŸ¶ W) (eq : f â‰« inl = g â‰« inr) : PushoutCocone f g where
  pt := W
  Î¹ := { app := fun j => Option.casesOn j (f â‰« inl) fun j' => WalkingPair.casesOn j' inl inr
         naturality := by
          rintro (âŸ¨âŸ©|âŸ¨âŸ¨âŸ©âŸ©) (âŸ¨âŸ©|âŸ¨âŸ¨âŸ©âŸ©) <;> intro f <;> cases f <;> dsimp <;> aesop }
#align category_theory.limits.pushout_cocone.mk CategoryTheory.Limits.PushoutCocone.mk

@[simp]
theorem mk_Î¹_app_left {W : C} (inl : Y âŸ¶ W) (inr : Z âŸ¶ W) (eq : f â‰« inl = g â‰« inr) :
    (mk inl inr eq).Î¹.app WalkingSpan.left = inl := rfl
#align category_theory.limits.pushout_cocone.mk_Î¹_app_left CategoryTheory.Limits.PushoutCocone.mk_Î¹_app_left

@[simp]
theorem mk_Î¹_app_right {W : C} (inl : Y âŸ¶ W) (inr : Z âŸ¶ W) (eq : f â‰« inl = g â‰« inr) :
    (mk inl inr eq).Î¹.app WalkingSpan.right = inr := rfl
#align category_theory.limits.pushout_cocone.mk_Î¹_app_right CategoryTheory.Limits.PushoutCocone.mk_Î¹_app_right

@[simp]
theorem mk_Î¹_app_zero {W : C} (inl : Y âŸ¶ W) (inr : Z âŸ¶ W) (eq : f â‰« inl = g â‰« inr) :
    (mk inl inr eq).Î¹.app WalkingSpan.zero = f â‰« inl := rfl
#align category_theory.limits.pushout_cocone.mk_Î¹_app_zero CategoryTheory.Limits.PushoutCocone.mk_Î¹_app_zero

@[simp]
theorem mk_inl {W : C} (inl : Y âŸ¶ W) (inr : Z âŸ¶ W) (eq : f â‰« inl = g â‰« inr) :
    (mk inl inr eq).inl = inl := rfl
#align category_theory.limits.pushout_cocone.mk_inl CategoryTheory.Limits.PushoutCocone.mk_inl

@[simp]
theorem mk_inr {W : C} (inl : Y âŸ¶ W) (inr : Z âŸ¶ W) (eq : f â‰« inl = g â‰« inr) :
    (mk inl inr eq).inr = inr := rfl
#align category_theory.limits.pushout_cocone.mk_inr CategoryTheory.Limits.PushoutCocone.mk_inr

@[reassoc]
theorem condition (t : PushoutCocone f g) : f â‰« inl t = g â‰« inr t :=
  (t.w fst).trans (t.w snd).symm
#align category_theory.limits.pushout_cocone.condition CategoryTheory.Limits.PushoutCocone.condition

/-- To check whether a morphism is coequalized by the maps of a pushout cocone, it suffices to check
  it for `inl t` and `inr t` -/
theorem coequalizer_ext (t : PushoutCocone f g) {W : C} {k l : t.pt âŸ¶ W}
    (hâ‚€ : inl t â‰« k = inl t â‰« l) (hâ‚ : inr t â‰« k = inr t â‰« l) :
    âˆ€ j : WalkingSpan, t.Î¹.app j â‰« k = t.Î¹.app j â‰« l
  | some WalkingPair.left => hâ‚€
  | some WalkingPair.right => hâ‚
  | none => by rw [â† t.w fst, Category.assoc, Category.assoc, hâ‚€]
#align category_theory.limits.pushout_cocone.coequalizer_ext CategoryTheory.Limits.PushoutCocone.coequalizer_ext

theorem IsColimit.hom_ext {t : PushoutCocone f g} (ht : IsColimit t) {W : C} {k l : t.pt âŸ¶ W}
    (hâ‚€ : inl t â‰« k = inl t â‰« l) (hâ‚ : inr t â‰« k = inr t â‰« l) : k = l :=
  ht.hom_ext <| coequalizer_ext _ hâ‚€ hâ‚
#align category_theory.limits.pushout_cocone.is_colimit.hom_ext CategoryTheory.Limits.PushoutCocone.IsColimit.hom_ext

-- Porting note: `IsColimit.desc` and the two following simp lemmas were introduced to ease the port
/-- If `t` is a colimit pushout cocone over `f` and `g` and `h : Y âŸ¶ W` and `k : Z âŸ¶ W` are
    morphisms satisfying `f â‰« h = g â‰« k`, then we have a factorization `l : t.pt âŸ¶ W` such that
    `inl t â‰« l = h` and `inr t â‰« l = k`, see `IsColimit.inl_desc` and `IsColimit.inr_desc`-/
def IsColimit.desc {t : PushoutCocone f g} (ht : IsColimit t) {W : C} (h : Y âŸ¶ W) (k : Z âŸ¶ W)
    (w : f â‰« h = g â‰« k) : t.pt âŸ¶ W :=
  ht.desc (PushoutCocone.mk _ _ w)

@[reassoc (attr := simp)]
lemma IsColimit.inl_desc {t : PushoutCocone f g} (ht : IsColimit t) {W : C} (h : Y âŸ¶ W) (k : Z âŸ¶ W)
    (w : f â‰« h = g â‰« k) : inl t â‰« IsColimit.desc ht h k w = h :=
  ht.fac _ _

@[reassoc (attr := simp)]
lemma IsColimit.inr_desc {t : PushoutCocone f g} (ht : IsColimit t) {W : C} (h : Y âŸ¶ W) (k : Z âŸ¶ W)
    (w : f â‰« h = g â‰« k) : inr t â‰« IsColimit.desc ht h k w = k :=
  ht.fac _ _

/-- If `t` is a colimit pushout cocone over `f` and `g` and `h : Y âŸ¶ W` and `k : Z âŸ¶ W` are
    morphisms satisfying `f â‰« h = g â‰« k`, then we have a factorization `l : t.pt âŸ¶ W` such that
    `inl t â‰« l = h` and `inr t â‰« l = k`. -/
def IsColimit.desc' {t : PushoutCocone f g} (ht : IsColimit t) {W : C} (h : Y âŸ¶ W) (k : Z âŸ¶ W)
    (w : f â‰« h = g â‰« k) : { l : t.pt âŸ¶ W // inl t â‰« l = h âˆ§ inr t â‰« l = k } :=
  âŸ¨IsColimit.desc ht h k w, by simpâŸ©
#align category_theory.limits.pushout_cocone.is_colimit.desc' CategoryTheory.Limits.PushoutCocone.IsColimit.desc'

theorem epi_inr_of_is_pushout_of_epi {t : PushoutCocone f g} (ht : IsColimit t) [Epi f] :
    Epi t.inr :=
  âŸ¨fun {W} h k i => IsColimit.hom_ext ht (by simp [â† cancel_epi f, t.condition_assoc, i]) iâŸ©
#align category_theory.limits.pushout_cocone.epi_inr_of_is_pushout_of_epi CategoryTheory.Limits.PushoutCocone.epi_inr_of_is_pushout_of_epi

theorem epi_inl_of_is_pushout_of_epi {t : PushoutCocone f g} (ht : IsColimit t) [Epi g] :
    Epi t.inl :=
  âŸ¨fun {W} h k i => IsColimit.hom_ext ht i (by simp [â† cancel_epi g, â† t.condition_assoc, i])âŸ©
#align category_theory.limits.pushout_cocone.epi_inl_of_is_pushout_of_epi CategoryTheory.Limits.PushoutCocone.epi_inl_of_is_pushout_of_epi

/-- To construct an isomorphism of pushout cocones, it suffices to construct an isomorphism
of the cocone points and check it commutes with `inl` and `inr`. -/
def ext {s t : PushoutCocone f g} (i : s.pt â‰… t.pt) (wâ‚ : s.inl â‰« i.hom = t.inl)
    (wâ‚‚ : s.inr â‰« i.hom = t.inr) : s â‰… t :=
  WalkingSpan.ext i wâ‚ wâ‚‚
#align category_theory.limits.pushout_cocone.ext CategoryTheory.Limits.PushoutCocone.ext

/-- This is a more convenient formulation to show that a `PushoutCocone` constructed using
`PushoutCocone.mk` is a colimit cocone.
-/
def IsColimit.mk {W : C} {inl : Y âŸ¶ W} {inr : Z âŸ¶ W} (eq : f â‰« inl = g â‰« inr)
    (desc : âˆ€ s : PushoutCocone f g, W âŸ¶ s.pt)
    (fac_left : âˆ€ s : PushoutCocone f g, inl â‰« desc s = s.inl)
    (fac_right : âˆ€ s : PushoutCocone f g, inr â‰« desc s = s.inr)
    (uniq :
      âˆ€ (s : PushoutCocone f g) (m : W âŸ¶ s.pt) (_ : inl â‰« m = s.inl) (_ : inr â‰« m = s.inr),
        m = desc s) :
    IsColimit (mk inl inr eq) :=
  isColimitAux _ desc fac_left fac_right fun s m w =>
    uniq s m (w WalkingCospan.left) (w WalkingCospan.right)
#align category_theory.limits.pushout_cocone.is_colimit.mk CategoryTheory.Limits.PushoutCocone.IsColimit.mk

section Flip

variable (t : PushoutCocone f g)

/-- The pushout cocone obtained by flipping `inl` and `inr`. -/
def flip : PushoutCocone g f := PushoutCocone.mk _ _ t.condition.symm

@[simp] lemma flip_pt : t.flip.pt = t.pt := rfl
@[simp] lemma flip_inl : t.flip.inl = t.inr := rfl
@[simp] lemma flip_inr : t.flip.inr = t.inl := rfl

/-- Flipping a pushout cocone twice gives an isomorphic cocone. -/
def flipFlipIso : t.flip.flip â‰… t := PushoutCocone.ext (Iso.refl _) (by simp) (by simp)

variable {t}

/-- The flip of a pushout square is a pushout square. -/
def flipIsColimit (ht : IsColimit t) : IsColimit t.flip :=
  IsColimit.mk _ (fun s => ht.desc s.flip) (by simp) (by simp) (fun s m hâ‚ hâ‚‚ => by
    apply IsColimit.hom_ext ht
    all_goals aesop_cat)

/-- A square is a pushout square if its flip is. -/
def isColimitOfFlip (ht : IsColimit t.flip) : IsColimit t :=
  IsColimit.ofIsoColimit (flipIsColimit ht) t.flipFlipIso
#align category_theory.limits.pushout_cocone.flip_is_colimit CategoryTheory.Limits.PushoutCocone.isColimitOfFlip

end Flip

/--
The pushout cocone `(ğŸ™ X, ğŸ™ X)` for the pair `(f, f)` is a colimit if `f` is an epi. The converse is
shown in `epi_of_isColimit_mk_id_id`.
-/
def isColimitMkIdId (f : X âŸ¶ Y) [Epi f] : IsColimit (mk (ğŸ™ Y) (ğŸ™ Y) rfl : PushoutCocone f f) :=
  IsColimit.mk _ (fun s => s.inl) (fun s => Category.id_comp _)
    (fun s => by rw [â† cancel_epi f, Category.id_comp, s.condition]) fun s m mâ‚ _ => by
    simpa using mâ‚
#align category_theory.limits.pushout_cocone.is_colimit_mk_id_id CategoryTheory.Limits.PushoutCocone.isColimitMkIdId

/-- `f` is an epi if the pushout cocone `(ğŸ™ X, ğŸ™ X)` is a colimit for the pair `(f, f)`.
The converse is given in `PushoutCocone.isColimitMkIdId`.
-/
theorem epi_of_isColimitMkIdId (f : X âŸ¶ Y)
    (t : IsColimit (mk (ğŸ™ Y) (ğŸ™ Y) rfl : PushoutCocone f f)) : Epi f :=
  âŸ¨fun {Z} g h eq => by
    rcases PushoutCocone.IsColimit.desc' t _ _ eq with âŸ¨_, rfl, rflâŸ©
    rflâŸ©
#align category_theory.limits.pushout_cocone.epi_of_is_colimit_mk_id_id CategoryTheory.Limits.PushoutCocone.epi_of_isColimitMkIdId

/-- Suppose `f` and `g` are two morphisms with a common domain and `s` is a colimit cocone over the
    diagram formed by `f` and `g`. Suppose `f` and `g` both factor through an epimorphism `h` via
    `x` and `y`, respectively. Then `s` is also a colimit cocone over the diagram formed by `x` and
    `y`.  -/
def isColimitOfFactors (f : X âŸ¶ Y) (g : X âŸ¶ Z) (h : X âŸ¶ W) [Epi h] (x : W âŸ¶ Y) (y : W âŸ¶ Z)
    (hhx : h â‰« x = f) (hhy : h â‰« y = g) (s : PushoutCocone f g) (hs : IsColimit s) :
    have reassocâ‚ : h â‰« x â‰« inl s = f â‰« inl s := by  -- Porting note: working around reassoc
      rw [â† Category.assoc]; apply congrArg (Â· â‰« inl s) hhx
    have reassocâ‚‚ : h â‰« y â‰« inr s = g â‰« inr s := by
      rw [â† Category.assoc]; apply congrArg (Â· â‰« inr s) hhy
    IsColimit (PushoutCocone.mk _ _ (show x â‰« s.inl = y â‰« s.inr from
          (cancel_epi h).1 <| by rw [reassocâ‚, reassocâ‚‚, s.condition])) :=
  PushoutCocone.isColimitAux' _ fun t => âŸ¨hs.desc (PushoutCocone.mk t.inl t.inr <| by
    rw [â† hhx, â† hhy, Category.assoc, Category.assoc, t.condition]),
      âŸ¨hs.fac _ WalkingSpan.left, hs.fac _ WalkingSpan.right, fun hr hr' => by
        apply PushoutCocone.IsColimit.hom_ext hs;
        Â· simp only [PushoutCocone.mk_inl, PushoutCocone.mk_inr] at hr hr' âŠ¢
          simp only [hr, hr']
          symm
          exact hs.fac _ WalkingSpan.left
        Â· simp only [PushoutCocone.mk_inl, PushoutCocone.mk_inr] at hr hr' âŠ¢
          simp only [hr, hr']
          symm
          exact hs.fac _ WalkingSpan.rightâŸ©âŸ©
#align category_theory.limits.pushout_cocone.is_colimit_of_factors CategoryTheory.Limits.PushoutCocone.isColimitOfFactors

/-- If `W` is the pushout of `f, g`,
it is also the pushout of `h â‰« f, h â‰« g` for any epi `h`. -/
def isColimitOfEpiComp (f : X âŸ¶ Y) (g : X âŸ¶ Z) (h : W âŸ¶ X) [Epi h] (s : PushoutCocone f g)
    (H : IsColimit s) :
    IsColimit
      (PushoutCocone.mk _ _
        (show (h â‰« f) â‰« s.inl = (h â‰« g) â‰« s.inr by
          rw [Category.assoc, Category.assoc, s.condition])) := by
  apply PushoutCocone.isColimitAux'
  intro s
  rcases PushoutCocone.IsColimit.desc' H s.inl s.inr
      ((cancel_epi h).mp (by simpa using s.condition)) with
    âŸ¨l, hâ‚, hâ‚‚âŸ©
  refine âŸ¨l, hâ‚, hâ‚‚, ?_âŸ©
  intro m hmâ‚ hmâ‚‚
  exact (PushoutCocone.IsColimit.hom_ext H (hmâ‚.trans hâ‚.symm) (hmâ‚‚.trans hâ‚‚.symm) : _)
#align category_theory.limits.pushout_cocone.is_colimit_of_epi_comp CategoryTheory.Limits.PushoutCocone.isColimitOfEpiComp

end PushoutCocone

/-- This is a helper construction that can be useful when verifying that a category has all
    pullbacks. Given `F : WalkingCospan â¥¤ C`, which is really the same as
    `cospan (F.map inl) (F.map inr)`, and a pullback cone on `F.map inl` and `F.map inr`, we
    get a cone on `F`.

    If you're thinking about using this, have a look at `hasPullbacks_of_hasLimit_cospan`,
    which you may find to be an easier way of achieving your goal. -/
@[simps]
def Cone.ofPullbackCone {F : WalkingCospan â¥¤ C} (t : PullbackCone (F.map inl) (F.map inr)) :
    Cone F where
  pt := t.pt
  Ï€ := t.Ï€ â‰« (diagramIsoCospan F).inv
#align category_theory.limits.cone.of_pullback_cone CategoryTheory.Limits.Cone.ofPullbackCone

/-- This is a helper construction that can be useful when verifying that a category has all
    pushout. Given `F : WalkingSpan â¥¤ C`, which is really the same as
    `span (F.map fst) (F.map snd)`, and a pushout cocone on `F.map fst` and `F.map snd`,
    we get a cocone on `F`.

    If you're thinking about using this, have a look at `hasPushouts_of_hasColimit_span`, which
    you may find to be an easier way of achieving your goal. -/
@[simps]
def Cocone.ofPushoutCocone {F : WalkingSpan â¥¤ C} (t : PushoutCocone (F.map fst) (F.map snd)) :
    Cocone F where
  pt := t.pt
  Î¹ := (diagramIsoSpan F).hom â‰« t.Î¹
#align category_theory.limits.cocone.of_pushout_cocone CategoryTheory.Limits.Cocone.ofPushoutCocone

/-- Given `F : WalkingCospan â¥¤ C`, which is really the same as `cospan (F.map inl) (F.map inr)`,
    and a cone on `F`, we get a pullback cone on `F.map inl` and `F.map inr`. -/
@[simps]
def PullbackCone.ofCone {F : WalkingCospan â¥¤ C} (t : Cone F) :
    PullbackCone (F.map inl) (F.map inr) where
  pt := t.pt
  Ï€ := t.Ï€ â‰« (diagramIsoCospan F).hom
#align category_theory.limits.pullback_cone.of_cone CategoryTheory.Limits.PullbackCone.ofCone

/-- A diagram `WalkingCospan â¥¤ C` is isomorphic to some `PullbackCone.mk` after
composing with `diagramIsoCospan`. -/
@[simps!]
def PullbackCone.isoMk {F : WalkingCospan â¥¤ C} (t : Cone F) :
    (Cones.postcompose (diagramIsoCospan.{v} _).hom).obj t â‰…
      PullbackCone.mk (t.Ï€.app WalkingCospan.left) (t.Ï€.app WalkingCospan.right)
        ((t.Ï€.naturality inl).symm.trans (t.Ï€.naturality inr : _)) :=
  Cones.ext (Iso.refl _) <| by
    rintro (_ | (_ | _)) <;>
      Â· dsimp
        simp
#align category_theory.limits.pullback_cone.iso_mk CategoryTheory.Limits.PullbackCone.isoMk

/-- Given `F : WalkingSpan â¥¤ C`, which is really the same as `span (F.map fst) (F.map snd)`,
    and a cocone on `F`, we get a pushout cocone on `F.map fst` and `F.map snd`. -/
@[simps]
def PushoutCocone.ofCocone {F : WalkingSpan â¥¤ C} (t : Cocone F) :
    PushoutCocone (F.map fst) (F.map snd) where
  pt := t.pt
  Î¹ := (diagramIsoSpan F).inv â‰« t.Î¹
#align category_theory.limits.pushout_cocone.of_cocone CategoryTheory.Limits.PushoutCocone.ofCocone

/-- A diagram `WalkingSpan â¥¤ C` is isomorphic to some `PushoutCocone.mk` after composing with
`diagramIsoSpan`. -/
@[simps!]
def PushoutCocone.isoMk {F : WalkingSpan â¥¤ C} (t : Cocone F) :
    (Cocones.precompose (diagramIsoSpan.{v} _).inv).obj t â‰…
      PushoutCocone.mk (t.Î¹.app WalkingSpan.left) (t.Î¹.app WalkingSpan.right)
        ((t.Î¹.naturality fst).trans (t.Î¹.naturality snd).symm) :=
  Cocones.ext (Iso.refl _) <| by
    rintro (_ | (_ | _)) <;>
      Â· dsimp
        simp
#align category_theory.limits.pushout_cocone.iso_mk CategoryTheory.Limits.PushoutCocone.isoMk

end CategoryTheory.Limits
