/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Markus Himmel, Bhavik Mehta, Andrew Yang, Emily Riehl
-/
import Mathlib.CategoryTheory.Limits.Shapes.Pullbacks.Basic

#align_import category_theory.limits.shapes.pullbacks from "leanprover-community/mathlib"@"7316286ff2942aa14e540add9058c6b0aa1c8070"

/-!
# PullbackCone

This file provides API for interacting with cones (resp. cocones) in the case of pullbacks
(resp. pushouts).

# Main definitions

* `PullbackCone f g`: Given morphisms `f : X ‚ü∂ Z` and `g : Y ‚ü∂ Z`, a term `t : PullbackCone f g`
provides the data of a cone pictured as follows
```
t.pt ---t.snd---> Y
  |               |
 t.fst            g
  |               |
  v               v
  X -----f------> Z
```
The type `PullbackCone f g` is implemented as an abbrevation for `Cone (cospan f g)`, so general
results about cones are also available for `PullbackCone f g`.

* `PushoutCone f g`: Given morphisms `f : X ‚ü∂ Y` and `g : X ‚ü∂ Z`, a term `t : PushoutCone f g`
provides the data of a cocone pictured as follows
```
  X -----f------> Y
  |               |
  g               t.inr
  |               |
  v               v
  Z ---t.inl---> t.pt
```
Similar to `PullbackCone`, `PushoutCone f g` is implemented as an abbreviation for
`Cocone (span f g)`, so general results about cocones are also available for `PushoutCone f g`.

## API
We summarize the most important parts of the API for pullback cones here. The dual notions for
pushout cones is also available in this file.

Various ways of constructing pullback cones:
* `PullbackCone.mk` constructs a term of `PullbackCone f g` given morphisms `fst` and `snd` such
  that `fst ‚â´ f = snd ‚â´ g`.
* `PullbackCone.flip` is the `PullbackCone` obtained by flipping `fst` and `snd`.

* go back and forth between cone/pullbackcone

Interaction with `IsLimit`:
* `PullbackCone.isLimitAux` and `PullbackCone.isLimitAux'` provide two convenient ways to show that
  a given `PullbackCone` is a limit cone.
* `PullbackCone.isLimit.mk` provides a convenient way to show that a `PullbackCone` constructed using
  `PullbackCone.mk` is a limit cone.
* `PullbackCone.IsLimit.lift` and `PullbackCone.IsLimit.lift'` provides convenient ways for
  constructing the morphisms to the point of a limit `PullbackCone` from the universal property.
* `PullbackCone.IsLimit.hom_ext` provides a convenient way to show that two morphisms to the point
  of a limit `PullbackCone` are equal.

# Structure
The file is structured as follows:
1. Definition of `PullbackCone` and some API for it.
2. API for the interaction between `PullbackCone` and `IsLimit`.
3. `PullbackCone.flip`
4. Results about pullbacks of monomorphisms (TODO: move to another file?)
5. Certain constructors of pullback cones from cones.

After this, we repeat the same structure for the dual notions.


TODO:
1. pullbackcone iso API
 (constructor?)

## References
* [Stacks: Fibre products](https://stacks.math.columbia.edu/tag/001U)
* [Stacks: Pushouts](https://stacks.math.columbia.edu/tag/0025)
-/

noncomputable section

open CategoryTheory

universe w v‚ÇÅ v‚ÇÇ v u u‚ÇÇ

namespace CategoryTheory.Limits

open WalkingSpan.Hom WalkingCospan.Hom WidePullbackShape.Hom WidePushoutShape.Hom

variable {C : Type u} [Category.{v} C] {W X Y Z : C}

/-- A pullback cone is just a cone on the cospan formed by two morphisms `f : X ‚ü∂ Z` and
    `g : Y ‚ü∂ Z`. -/
abbrev PullbackCone (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :=
  Cone (cospan f g)
#align category_theory.limits.pullback_cone CategoryTheory.Limits.PullbackCone

namespace PullbackCone

variable {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z}

/-- The first projection of a pullback cone. -/
abbrev fst (t : PullbackCone f g) : t.pt ‚ü∂ X :=
  t.œÄ.app WalkingCospan.left
#align category_theory.limits.pullback_cone.fst CategoryTheory.Limits.PullbackCone.fst

/-- The second projection of a pullback cone. -/
abbrev snd (t : PullbackCone f g) : t.pt ‚ü∂ Y :=
  t.œÄ.app WalkingCospan.right
#align category_theory.limits.pullback_cone.snd CategoryTheory.Limits.PullbackCone.snd

@[simp]
theorem œÄ_app_left (c : PullbackCone f g) : c.œÄ.app WalkingCospan.left = c.fst := rfl
#align category_theory.limits.pullback_cone.œÄ_app_left CategoryTheory.Limits.PullbackCone.œÄ_app_left

@[simp]
theorem œÄ_app_right (c : PullbackCone f g) : c.œÄ.app WalkingCospan.right = c.snd := rfl
#align category_theory.limits.pullback_cone.œÄ_app_right CategoryTheory.Limits.PullbackCone.œÄ_app_right

@[simp]
theorem condition_one (t : PullbackCone f g) : t.œÄ.app WalkingCospan.one = t.fst ‚â´ f := by
  have w := t.œÄ.naturality WalkingCospan.Hom.inl
  dsimp at w; simpa using w
#align category_theory.limits.pullback_cone.condition_one CategoryTheory.Limits.PullbackCone.condition_one

/-- A pullback cone on `f` and `g` is determined by morphisms `fst : W ‚ü∂ X` and `snd : W ‚ü∂ Y`
    such that `fst ‚â´ f = snd ‚â´ g`. -/
@[simps]
def mk {W : C} (fst : W ‚ü∂ X) (snd : W ‚ü∂ Y) (eq : fst ‚â´ f = snd ‚â´ g) : PullbackCone f g where
  pt := W
  œÄ := { app := fun j => Option.casesOn j (fst ‚â´ f) fun j' => WalkingPair.casesOn j' fst snd
         naturality := by rintro (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©) (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©) j <;> cases j <;> dsimp <;> simp [eq] }
#align category_theory.limits.pullback_cone.mk CategoryTheory.Limits.PullbackCone.mk

@[simp]
theorem mk_œÄ_app_left {W : C} (fst : W ‚ü∂ X) (snd : W ‚ü∂ Y) (eq : fst ‚â´ f = snd ‚â´ g) :
    (mk fst snd eq).œÄ.app WalkingCospan.left = fst := rfl
#align category_theory.limits.pullback_cone.mk_œÄ_app_left CategoryTheory.Limits.PullbackCone.mk_œÄ_app_left

@[simp]
theorem mk_œÄ_app_right {W : C} (fst : W ‚ü∂ X) (snd : W ‚ü∂ Y) (eq : fst ‚â´ f = snd ‚â´ g) :
    (mk fst snd eq).œÄ.app WalkingCospan.right = snd := rfl
#align category_theory.limits.pullback_cone.mk_œÄ_app_right CategoryTheory.Limits.PullbackCone.mk_œÄ_app_right

@[simp]
theorem mk_œÄ_app_one {W : C} (fst : W ‚ü∂ X) (snd : W ‚ü∂ Y) (eq : fst ‚â´ f = snd ‚â´ g) :
    (mk fst snd eq).œÄ.app WalkingCospan.one = fst ‚â´ f := rfl
#align category_theory.limits.pullback_cone.mk_œÄ_app_one CategoryTheory.Limits.PullbackCone.mk_œÄ_app_one

@[simp]
theorem mk_fst {W : C} (fst : W ‚ü∂ X) (snd : W ‚ü∂ Y) (eq : fst ‚â´ f = snd ‚â´ g) :
    (mk fst snd eq).fst = fst := rfl
#align category_theory.limits.pullback_cone.mk_fst CategoryTheory.Limits.PullbackCone.mk_fst

@[simp]
theorem mk_snd {W : C} (fst : W ‚ü∂ X) (snd : W ‚ü∂ Y) (eq : fst ‚â´ f = snd ‚â´ g) :
    (mk fst snd eq).snd = snd := rfl
#align category_theory.limits.pullback_cone.mk_snd CategoryTheory.Limits.PullbackCone.mk_snd

@[reassoc]
theorem condition (t : PullbackCone f g) : fst t ‚â´ f = snd t ‚â´ g :=
  (t.w inl).trans (t.w inr).symm
#align category_theory.limits.pullback_cone.condition CategoryTheory.Limits.PullbackCone.condition

/-- To check whether two morphisms are equalized by the maps of a pullback cone, it suffices to
check it for `fst t` and `snd t` -/
theorem equalizer_ext (t : PullbackCone f g) {W : C} {k l : W ‚ü∂ t.pt} (h‚ÇÄ : k ‚â´ fst t = l ‚â´ fst t)
    (h‚ÇÅ : k ‚â´ snd t = l ‚â´ snd t) : ‚àÄ j : WalkingCospan, k ‚â´ t.œÄ.app j = l ‚â´ t.œÄ.app j
  | some WalkingPair.left => h‚ÇÄ
  | some WalkingPair.right => h‚ÇÅ
  | none => by rw [‚Üê t.w inl, reassoc_of% h‚ÇÄ]
#align category_theory.limits.pullback_cone.equalizer_ext CategoryTheory.Limits.PullbackCone.equalizer_ext

/-- To construct an isomorphism of pullback cones, it suffices to construct an isomorphism
of the cone points and check it commutes with `fst` and `snd`. -/
def ext {s t : PullbackCone f g} (i : s.pt ‚âÖ t.pt) (w‚ÇÅ : s.fst = i.hom ‚â´ t.fst)
    (w‚ÇÇ : s.snd = i.hom ‚â´ t.snd) : s ‚âÖ t :=
  WalkingCospan.ext i w‚ÇÅ w‚ÇÇ
#align category_theory.limits.pullback_cone.ext CategoryTheory.Limits.PullbackCone.ext

/-- This is a slightly more convenient method to verify that a pullback cone is a limit cone. It
    only asks for a proof of facts that carry any mathematical content -/
def isLimitAux (t : PullbackCone f g) (lift : ‚àÄ s : PullbackCone f g, s.pt ‚ü∂ t.pt)
    (fac_left : ‚àÄ s : PullbackCone f g, lift s ‚â´ t.fst = s.fst)
    (fac_right : ‚àÄ s : PullbackCone f g, lift s ‚â´ t.snd = s.snd)
    (uniq : ‚àÄ (s : PullbackCone f g) (m : s.pt ‚ü∂ t.pt)
      (_ : ‚àÄ j : WalkingCospan, m ‚â´ t.œÄ.app j = s.œÄ.app j), m = lift s) : IsLimit t :=
  { lift
    fac := fun s j => Option.casesOn j (by
        rw [‚Üê s.w inl, ‚Üê t.w inl, ‚Üê Category.assoc]
        congr
        exact fac_left s)
      fun j' => WalkingPair.casesOn j' (fac_left s) (fac_right s)
    uniq := uniq }
#align category_theory.limits.pullback_cone.is_limit_aux CategoryTheory.Limits.PullbackCone.isLimitAux

/-- This is another convenient method to verify that a pullback cone is a limit cone. It
    only asks for a proof of facts that carry any mathematical content, and allows access to the
    same `s` for all parts. -/
def isLimitAux' (t : PullbackCone f g)
    (create :
      ‚àÄ s : PullbackCone f g,
        { l //
          l ‚â´ t.fst = s.fst ‚àß
            l ‚â´ t.snd = s.snd ‚àß ‚àÄ {m}, m ‚â´ t.fst = s.fst ‚Üí m ‚â´ t.snd = s.snd ‚Üí m = l }) :
    Limits.IsLimit t :=
  PullbackCone.isLimitAux t (fun s => (create s).1) (fun s => (create s).2.1)
    (fun s => (create s).2.2.1) fun s _ w =>
    (create s).2.2.2 (w WalkingCospan.left) (w WalkingCospan.right)
#align category_theory.limits.pullback_cone.is_limit_aux' CategoryTheory.Limits.PullbackCone.isLimitAux'

/-- This is a more convenient formulation to show that a `PullbackCone` constructed using
`PullbackCone.mk` is a limit cone.
-/
def IsLimit.mk {W : C} {fst : W ‚ü∂ X} {snd : W ‚ü∂ Y} (eq : fst ‚â´ f = snd ‚â´ g)
    (lift : ‚àÄ s : PullbackCone f g, s.pt ‚ü∂ W)
    (fac_left : ‚àÄ s : PullbackCone f g, lift s ‚â´ fst = s.fst)
    (fac_right : ‚àÄ s : PullbackCone f g, lift s ‚â´ snd = s.snd)
    (uniq :
      ‚àÄ (s : PullbackCone f g) (m : s.pt ‚ü∂ W) (_ : m ‚â´ fst = s.fst) (_ : m ‚â´ snd = s.snd),
        m = lift s) :
    IsLimit (mk fst snd eq) :=
  isLimitAux _ lift fac_left fac_right fun s m w =>
    uniq s m (w WalkingCospan.left) (w WalkingCospan.right)
#align category_theory.limits.pullback_cone.is_limit.mk CategoryTheory.Limits.PullbackCone.IsLimit.mk

theorem IsLimit.hom_ext {t : PullbackCone f g} (ht : IsLimit t) {W : C} {k l : W ‚ü∂ t.pt}
    (h‚ÇÄ : k ‚â´ fst t = l ‚â´ fst t) (h‚ÇÅ : k ‚â´ snd t = l ‚â´ snd t) : k = l :=
  ht.hom_ext <| equalizer_ext _ h‚ÇÄ h‚ÇÅ
#align category_theory.limits.pullback_cone.is_limit.hom_ext CategoryTheory.Limits.PullbackCone.IsLimit.hom_ext

-- Porting note: `IsLimit.lift` and the two following simp lemmas were introduced to ease the port
/-- If `t` is a limit pullback cone over `f` and `g` and `h : W ‚ü∂ X` and `k : W ‚ü∂ Y` are such that
    `h ‚â´ f = k ‚â´ g`, then we get `l : W ‚ü∂ t.pt`, which satisfies `l ‚â´ fst t = h`
    and `l ‚â´ snd t = k`, see `IsLimit.lift_fst` and `IsLimit.lift_snd`. -/
def IsLimit.lift {t : PullbackCone f g} (ht : IsLimit t) {W : C} (h : W ‚ü∂ X) (k : W ‚ü∂ Y)
    (w : h ‚â´ f = k ‚â´ g) : W ‚ü∂ t.pt :=
  ht.lift <| PullbackCone.mk _ _ w

@[reassoc (attr := simp)]
lemma IsLimit.lift_fst {t : PullbackCone f g} (ht : IsLimit t) {W : C} (h : W ‚ü∂ X) (k : W ‚ü∂ Y)
    (w : h ‚â´ f = k ‚â´ g) : IsLimit.lift ht h k w ‚â´ fst t = h := ht.fac _ _

@[reassoc (attr := simp)]
lemma IsLimit.lift_snd {t : PullbackCone f g} (ht : IsLimit t) {W : C} (h : W ‚ü∂ X) (k : W ‚ü∂ Y)
    (w : h ‚â´ f = k ‚â´ g) : IsLimit.lift ht h k w ‚â´ snd t = k := ht.fac _ _

/-- If `t` is a limit pullback cone over `f` and `g` and `h : W ‚ü∂ X` and `k : W ‚ü∂ Y` are such that
    `h ‚â´ f = k ‚â´ g`, then we have `l : W ‚ü∂ t.pt` satisfying `l ‚â´ fst t = h` and `l ‚â´ snd t = k`.
    -/
def IsLimit.lift' {t : PullbackCone f g} (ht : IsLimit t) {W : C} (h : W ‚ü∂ X) (k : W ‚ü∂ Y)
    (w : h ‚â´ f = k ‚â´ g) : { l : W ‚ü∂ t.pt // l ‚â´ fst t = h ‚àß l ‚â´ snd t = k } :=
  ‚ü®IsLimit.lift ht h k w, by simp‚ü©
#align category_theory.limits.pullback_cone.is_limit.lift' CategoryTheory.Limits.PullbackCone.IsLimit.lift'
section Flip

variable (t : PullbackCone f g)

/-- The pullback cone obtained by flipping `fst` and `snd`. -/
def flip : PullbackCone g f := PullbackCone.mk _ _ t.condition.symm

@[simp] lemma flip_pt : t.flip.pt = t.pt := rfl
@[simp] lemma flip_fst : t.flip.fst = t.snd := rfl
@[simp] lemma flip_snd : t.flip.snd = t.fst := rfl

/-- Flipping a pullback cone twice gives an isomorphic cone. -/
def flipFlipIso : t.flip.flip ‚âÖ t := PullbackCone.ext (Iso.refl _) (by simp) (by simp)

variable {t}

/-- The flip of a pullback square is a pullback square. -/
def flipIsLimit (ht : IsLimit t) : IsLimit t.flip :=
  IsLimit.mk _ (fun s => ht.lift s.flip) (by simp) (by simp) (fun s m h‚ÇÅ h‚ÇÇ => by
    apply IsLimit.hom_ext ht
    all_goals aesop_cat)

/-- A square is a pullback square if its flip is. -/
def isLimitOfFlip (ht : IsLimit t.flip) : IsLimit t :=
  IsLimit.ofIsoLimit (flipIsLimit ht) t.flipFlipIso
#align category_theory.limits.pullback_cone.flip_is_limit CategoryTheory.Limits.PullbackCone.isLimitOfFlip

end Flip

section Monomorphisms

/-- Monomorphisms are stable under pullback in the first argument. -/
theorem mono_snd_of_is_pullback_of_mono {t : PullbackCone f g} (ht : IsLimit t) [Mono f] :
    Mono t.snd := by
  refine ‚ü®fun {W} h k i => IsLimit.hom_ext ht ?_ i‚ü©
  rw [‚Üê cancel_mono f, Category.assoc, Category.assoc, condition]
  have := congrArg (¬∑ ‚â´ g) i; dsimp at this
  rwa [Category.assoc, Category.assoc] at this
#align category_theory.limits.pullback_cone.mono_snd_of_is_pullback_of_mono CategoryTheory.Limits.PullbackCone.mono_snd_of_is_pullback_of_mono

/-- Monomorphisms are stable under pullback in the second argument. -/
theorem mono_fst_of_is_pullback_of_mono {t : PullbackCone f g} (ht : IsLimit t) [Mono g] :
    Mono t.fst := by
  refine ‚ü®fun {W} h k i => IsLimit.hom_ext ht i ?_‚ü©
  rw [‚Üê cancel_mono g, Category.assoc, Category.assoc, ‚Üê condition]
  have := congrArg (¬∑ ‚â´ f) i; dsimp at this
  rwa [Category.assoc, Category.assoc] at this
#align category_theory.limits.pullback_cone.mono_fst_of_is_pullback_of_mono CategoryTheory.Limits.PullbackCone.mono_fst_of_is_pullback_of_mono

/--
The pullback cone `(ùüô X, ùüô X)` for the pair `(f, f)` is a limit if `f` is a mono. The converse is
shown in `mono_of_pullback_is_id`.
-/
def isLimitMkIdId (f : X ‚ü∂ Y) [Mono f] : IsLimit (mk (ùüô X) (ùüô X) rfl : PullbackCone f f) :=
  IsLimit.mk _ (fun s => s.fst) (fun s => Category.comp_id _)
    (fun s => by rw [‚Üê cancel_mono f, Category.comp_id, s.condition]) fun s m m‚ÇÅ _ => by
    simpa using m‚ÇÅ
#align category_theory.limits.pullback_cone.is_limit_mk_id_id CategoryTheory.Limits.PullbackCone.isLimitMkIdId

/--
`f` is a mono if the pullback cone `(ùüô X, ùüô X)` is a limit for the pair `(f, f)`. The converse is
given in `PullbackCone.is_id_of_mono`.
-/
theorem mono_of_isLimitMkIdId (f : X ‚ü∂ Y) (t : IsLimit (mk (ùüô X) (ùüô X) rfl : PullbackCone f f)) :
    Mono f :=
  ‚ü®fun {Z} g h eq => by
    rcases PullbackCone.IsLimit.lift' t _ _ eq with ‚ü®_, rfl, rfl‚ü©
    rfl‚ü©
#align category_theory.limits.pullback_cone.mono_of_is_limit_mk_id_id CategoryTheory.Limits.PullbackCone.mono_of_isLimitMkIdId

/-- Suppose `f` and `g` are two morphisms with a common codomain and `s` is a limit cone over the
    diagram formed by `f` and `g`. Suppose `f` and `g` both factor through a monomorphism `h` via
    `x` and `y`, respectively.  Then `s` is also a limit cone over the diagram formed by `x` and
    `y`.  -/
def isLimitOfFactors (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) (h : W ‚ü∂ Z) [Mono h] (x : X ‚ü∂ W) (y : Y ‚ü∂ W)
    (hxh : x ‚â´ h = f) (hyh : y ‚â´ h = g) (s : PullbackCone f g) (hs : IsLimit s) :
    IsLimit
      (PullbackCone.mk _ _
        (show s.fst ‚â´ x = s.snd ‚â´ y from
          (cancel_mono h).1 <| by simp only [Category.assoc, hxh, hyh, s.condition])) :=
  PullbackCone.isLimitAux' _ fun t =>
    have : fst t ‚â´ x ‚â´ h = snd t ‚â´ y ‚â´ h := by  -- Porting note: reassoc workaround
      rw [‚Üê Category.assoc, ‚Üê Category.assoc]
      apply congrArg (¬∑ ‚â´ h) t.condition
    ‚ü®hs.lift (PullbackCone.mk t.fst t.snd <| by rw [‚Üê hxh, ‚Üê hyh, this]),
      ‚ü®hs.fac _ WalkingCospan.left, hs.fac _ WalkingCospan.right, fun hr hr' => by
        apply PullbackCone.IsLimit.hom_ext hs <;>
              simp only [PullbackCone.mk_fst, PullbackCone.mk_snd] at hr hr' ‚ä¢ <;>
            simp only [hr, hr'] <;>
          symm
        exacts [hs.fac _ WalkingCospan.left, hs.fac _ WalkingCospan.right]‚ü©‚ü©
#align category_theory.limits.pullback_cone.is_limit_of_factors CategoryTheory.Limits.PullbackCone.isLimitOfFactors

/-- If `W` is the pullback of `f, g`, it is also the pullback of `f ‚â´ i, g ‚â´ i` for any mono `i`. -/
def isLimitOfCompMono (f : X ‚ü∂ W) (g : Y ‚ü∂ W) (i : W ‚ü∂ Z) [Mono i] (s : PullbackCone f g)
    (H : IsLimit s) :
    IsLimit
      (PullbackCone.mk _ _
        (show s.fst ‚â´ f ‚â´ i = s.snd ‚â´ g ‚â´ i by
          rw [‚Üê Category.assoc, ‚Üê Category.assoc, s.condition])) := by
  apply PullbackCone.isLimitAux'
  intro s
  rcases PullbackCone.IsLimit.lift' H s.fst s.snd
      ((cancel_mono i).mp (by simpa using s.condition)) with
    ‚ü®l, h‚ÇÅ, h‚ÇÇ‚ü©
  refine ‚ü®l, h‚ÇÅ, h‚ÇÇ, ?_‚ü©
  intro m hm‚ÇÅ hm‚ÇÇ
  exact (PullbackCone.IsLimit.hom_ext H (hm‚ÇÅ.trans h‚ÇÅ.symm) (hm‚ÇÇ.trans h‚ÇÇ.symm) : _)
#align category_theory.limits.pullback_cone.is_limit_of_comp_mono CategoryTheory.Limits.PullbackCone.isLimitOfCompMono

end Monomorphisms

end PullbackCone

/-- This is a helper construction that can be useful when verifying that a category has all
    pullbacks. Given `F : WalkingCospan ‚•§ C`, which is really the same as
    `cospan (F.map inl) (F.map inr)`, and a pullback cone on `F.map inl` and `F.map inr`, we
    get a cone on `F`.

    If you're thinking about using this, have a look at `hasPullbacks_of_hasLimit_cospan`,
    which you may find to be an easier way of achieving your goal. -/
@[simps]
def Cone.ofPullbackCone {F : WalkingCospan ‚•§ C} (t : PullbackCone (F.map inl) (F.map inr)) :
    Cone F where
  pt := t.pt
  œÄ := t.œÄ ‚â´ (diagramIsoCospan F).inv
#align category_theory.limits.cone.of_pullback_cone CategoryTheory.Limits.Cone.ofPullbackCone

/-- Given `F : WalkingCospan ‚•§ C`, which is really the same as `cospan (F.map inl) (F.map inr)`,
    and a cone on `F`, we get a pullback cone on `F.map inl` and `F.map inr`. -/
@[simps]
def PullbackCone.ofCone {F : WalkingCospan ‚•§ C} (t : Cone F) :
    PullbackCone (F.map inl) (F.map inr) where
  pt := t.pt
  œÄ := t.œÄ ‚â´ (diagramIsoCospan F).hom
#align category_theory.limits.pullback_cone.of_cone CategoryTheory.Limits.PullbackCone.ofCone

/-- A diagram `WalkingCospan ‚•§ C` is isomorphic to some `PullbackCone.mk` after
composing with `diagramIsoCospan`. -/
@[simps!]
def PullbackCone.isoMk {F : WalkingCospan ‚•§ C} (t : Cone F) :
    (Cones.postcompose (diagramIsoCospan.{v} _).hom).obj t ‚âÖ
      PullbackCone.mk (t.œÄ.app WalkingCospan.left) (t.œÄ.app WalkingCospan.right)
        ((t.œÄ.naturality inl).symm.trans (t.œÄ.naturality inr : _)) :=
  Cones.ext (Iso.refl _) <| by
    rintro (_ | (_ | _)) <;>
      ¬∑ dsimp
        simp
#align category_theory.limits.pullback_cone.iso_mk CategoryTheory.Limits.PullbackCone.isoMk

/-- A pushout cocone is just a cocone on the span formed by two morphisms `f : X ‚ü∂ Y` and
    `g : X ‚ü∂ Z`. -/
abbrev PushoutCocone (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) :=
  Cocone (span f g)
#align category_theory.limits.pushout_cocone CategoryTheory.Limits.PushoutCocone

namespace PushoutCocone

variable {f : X ‚ü∂ Y} {g : X ‚ü∂ Z}

/-- The first inclusion of a pushout cocone. -/
abbrev inl (t : PushoutCocone f g) : Y ‚ü∂ t.pt :=
  t.Œπ.app WalkingSpan.left
#align category_theory.limits.pushout_cocone.inl CategoryTheory.Limits.PushoutCocone.inl

/-- The second inclusion of a pushout cocone. -/
abbrev inr (t : PushoutCocone f g) : Z ‚ü∂ t.pt :=
  t.Œπ.app WalkingSpan.right
#align category_theory.limits.pushout_cocone.inr CategoryTheory.Limits.PushoutCocone.inr

@[simp]
theorem Œπ_app_left (c : PushoutCocone f g) : c.Œπ.app WalkingSpan.left = c.inl := rfl
#align category_theory.limits.pushout_cocone.Œπ_app_left CategoryTheory.Limits.PushoutCocone.Œπ_app_left

@[simp]
theorem Œπ_app_right (c : PushoutCocone f g) : c.Œπ.app WalkingSpan.right = c.inr := rfl
#align category_theory.limits.pushout_cocone.Œπ_app_right CategoryTheory.Limits.PushoutCocone.Œπ_app_right

@[simp]
theorem condition_zero (t : PushoutCocone f g) : t.Œπ.app WalkingSpan.zero = f ‚â´ t.inl := by
  have w := t.Œπ.naturality WalkingSpan.Hom.fst
  dsimp at w; simpa using w.symm
#align category_theory.limits.pushout_cocone.condition_zero CategoryTheory.Limits.PushoutCocone.condition_zero

/-- A pushout cocone on `f` and `g` is determined by morphisms `inl : Y ‚ü∂ W` and `inr : Z ‚ü∂ W` such
    that `f ‚â´ inl = g ‚Ü† inr`. -/
@[simps]
def mk {W : C} (inl : Y ‚ü∂ W) (inr : Z ‚ü∂ W) (eq : f ‚â´ inl = g ‚â´ inr) : PushoutCocone f g where
  pt := W
  Œπ := { app := fun j => Option.casesOn j (f ‚â´ inl) fun j' => WalkingPair.casesOn j' inl inr
         naturality := by
          rintro (‚ü®‚ü©|‚ü®‚ü®‚ü©‚ü©) (‚ü®‚ü©|‚ü®‚ü®‚ü©‚ü©) <;> intro f <;> cases f <;> dsimp <;> aesop }
#align category_theory.limits.pushout_cocone.mk CategoryTheory.Limits.PushoutCocone.mk

@[simp]
theorem mk_Œπ_app_left {W : C} (inl : Y ‚ü∂ W) (inr : Z ‚ü∂ W) (eq : f ‚â´ inl = g ‚â´ inr) :
    (mk inl inr eq).Œπ.app WalkingSpan.left = inl := rfl
#align category_theory.limits.pushout_cocone.mk_Œπ_app_left CategoryTheory.Limits.PushoutCocone.mk_Œπ_app_left

@[simp]
theorem mk_Œπ_app_right {W : C} (inl : Y ‚ü∂ W) (inr : Z ‚ü∂ W) (eq : f ‚â´ inl = g ‚â´ inr) :
    (mk inl inr eq).Œπ.app WalkingSpan.right = inr := rfl
#align category_theory.limits.pushout_cocone.mk_Œπ_app_right CategoryTheory.Limits.PushoutCocone.mk_Œπ_app_right

@[simp]
theorem mk_Œπ_app_zero {W : C} (inl : Y ‚ü∂ W) (inr : Z ‚ü∂ W) (eq : f ‚â´ inl = g ‚â´ inr) :
    (mk inl inr eq).Œπ.app WalkingSpan.zero = f ‚â´ inl := rfl
#align category_theory.limits.pushout_cocone.mk_Œπ_app_zero CategoryTheory.Limits.PushoutCocone.mk_Œπ_app_zero

@[simp]
theorem mk_inl {W : C} (inl : Y ‚ü∂ W) (inr : Z ‚ü∂ W) (eq : f ‚â´ inl = g ‚â´ inr) :
    (mk inl inr eq).inl = inl := rfl
#align category_theory.limits.pushout_cocone.mk_inl CategoryTheory.Limits.PushoutCocone.mk_inl

@[simp]
theorem mk_inr {W : C} (inl : Y ‚ü∂ W) (inr : Z ‚ü∂ W) (eq : f ‚â´ inl = g ‚â´ inr) :
    (mk inl inr eq).inr = inr := rfl
#align category_theory.limits.pushout_cocone.mk_inr CategoryTheory.Limits.PushoutCocone.mk_inr

@[reassoc]
theorem condition (t : PushoutCocone f g) : f ‚â´ inl t = g ‚â´ inr t :=
  (t.w fst).trans (t.w snd).symm
#align category_theory.limits.pushout_cocone.condition CategoryTheory.Limits.PushoutCocone.condition

/-- To check whether a morphism is coequalized by the maps of a pushout cocone, it suffices to check
  it for `inl t` and `inr t` -/
theorem coequalizer_ext (t : PushoutCocone f g) {W : C} {k l : t.pt ‚ü∂ W}
    (h‚ÇÄ : inl t ‚â´ k = inl t ‚â´ l) (h‚ÇÅ : inr t ‚â´ k = inr t ‚â´ l) :
    ‚àÄ j : WalkingSpan, t.Œπ.app j ‚â´ k = t.Œπ.app j ‚â´ l
  | some WalkingPair.left => h‚ÇÄ
  | some WalkingPair.right => h‚ÇÅ
  | none => by rw [‚Üê t.w fst, Category.assoc, Category.assoc, h‚ÇÄ]
#align category_theory.limits.pushout_cocone.coequalizer_ext CategoryTheory.Limits.PushoutCocone.coequalizer_ext

/-- To construct an isomorphism of pushout cocones, it suffices to construct an isomorphism
of the cocone points and check it commutes with `inl` and `inr`. -/
def ext {s t : PushoutCocone f g} (i : s.pt ‚âÖ t.pt) (w‚ÇÅ : s.inl ‚â´ i.hom = t.inl)
    (w‚ÇÇ : s.inr ‚â´ i.hom = t.inr) : s ‚âÖ t :=
  WalkingSpan.ext i w‚ÇÅ w‚ÇÇ
#align category_theory.limits.pushout_cocone.ext CategoryTheory.Limits.PushoutCocone.ext

/-- This is a slightly more convenient method to verify that a pushout cocone is a colimit cocone.
    It only asks for a proof of facts that carry any mathematical content -/
def isColimitAux (t : PushoutCocone f g) (desc : ‚àÄ s : PushoutCocone f g, t.pt ‚ü∂ s.pt)
    (fac_left : ‚àÄ s : PushoutCocone f g, t.inl ‚â´ desc s = s.inl)
    (fac_right : ‚àÄ s : PushoutCocone f g, t.inr ‚â´ desc s = s.inr)
    (uniq : ‚àÄ (s : PushoutCocone f g) (m : t.pt ‚ü∂ s.pt)
    (_ : ‚àÄ j : WalkingSpan, t.Œπ.app j ‚â´ m = s.Œπ.app j), m = desc s) : IsColimit t :=
  { desc
    fac := fun s j =>
      Option.casesOn j (by simp [‚Üê s.w fst, ‚Üê t.w fst, fac_left s]) fun j' =>
        WalkingPair.casesOn j' (fac_left s) (fac_right s)
    uniq := uniq }
#align category_theory.limits.pushout_cocone.is_colimit_aux CategoryTheory.Limits.PushoutCocone.isColimitAux

/-- This is another convenient method to verify that a pushout cocone is a colimit cocone. It
    only asks for a proof of facts that carry any mathematical content, and allows access to the
    same `s` for all parts. -/
def isColimitAux' (t : PushoutCocone f g)
    (create :
      ‚àÄ s : PushoutCocone f g,
        { l //
          t.inl ‚â´ l = s.inl ‚àß
            t.inr ‚â´ l = s.inr ‚àß ‚àÄ {m}, t.inl ‚â´ m = s.inl ‚Üí t.inr ‚â´ m = s.inr ‚Üí m = l }) :
    IsColimit t :=
  isColimitAux t (fun s => (create s).1) (fun s => (create s).2.1) (fun s => (create s).2.2.1)
    fun s _ w => (create s).2.2.2 (w WalkingCospan.left) (w WalkingCospan.right)
#align category_theory.limits.pushout_cocone.is_colimit_aux' CategoryTheory.Limits.PushoutCocone.isColimitAux'


theorem IsColimit.hom_ext {t : PushoutCocone f g} (ht : IsColimit t) {W : C} {k l : t.pt ‚ü∂ W}
    (h‚ÇÄ : inl t ‚â´ k = inl t ‚â´ l) (h‚ÇÅ : inr t ‚â´ k = inr t ‚â´ l) : k = l :=
  ht.hom_ext <| coequalizer_ext _ h‚ÇÄ h‚ÇÅ
#align category_theory.limits.pushout_cocone.is_colimit.hom_ext CategoryTheory.Limits.PushoutCocone.IsColimit.hom_ext

-- Porting note: `IsColimit.desc` and the two following simp lemmas were introduced to ease the port
/-- If `t` is a colimit pushout cocone over `f` and `g` and `h : Y ‚ü∂ W` and `k : Z ‚ü∂ W` are
    morphisms satisfying `f ‚â´ h = g ‚â´ k`, then we have a factorization `l : t.pt ‚ü∂ W` such that
    `inl t ‚â´ l = h` and `inr t ‚â´ l = k`, see `IsColimit.inl_desc` and `IsColimit.inr_desc`-/
def IsColimit.desc {t : PushoutCocone f g} (ht : IsColimit t) {W : C} (h : Y ‚ü∂ W) (k : Z ‚ü∂ W)
    (w : f ‚â´ h = g ‚â´ k) : t.pt ‚ü∂ W :=
  ht.desc (PushoutCocone.mk _ _ w)

@[reassoc (attr := simp)]
lemma IsColimit.inl_desc {t : PushoutCocone f g} (ht : IsColimit t) {W : C} (h : Y ‚ü∂ W) (k : Z ‚ü∂ W)
    (w : f ‚â´ h = g ‚â´ k) : inl t ‚â´ IsColimit.desc ht h k w = h :=
  ht.fac _ _

@[reassoc (attr := simp)]
lemma IsColimit.inr_desc {t : PushoutCocone f g} (ht : IsColimit t) {W : C} (h : Y ‚ü∂ W) (k : Z ‚ü∂ W)
    (w : f ‚â´ h = g ‚â´ k) : inr t ‚â´ IsColimit.desc ht h k w = k :=
  ht.fac _ _

/-- If `t` is a colimit pushout cocone over `f` and `g` and `h : Y ‚ü∂ W` and `k : Z ‚ü∂ W` are
    morphisms satisfying `f ‚â´ h = g ‚â´ k`, then we have a factorization `l : t.pt ‚ü∂ W` such that
    `inl t ‚â´ l = h` and `inr t ‚â´ l = k`. -/
def IsColimit.desc' {t : PushoutCocone f g} (ht : IsColimit t) {W : C} (h : Y ‚ü∂ W) (k : Z ‚ü∂ W)
    (w : f ‚â´ h = g ‚â´ k) : { l : t.pt ‚ü∂ W // inl t ‚â´ l = h ‚àß inr t ‚â´ l = k } :=
  ‚ü®IsColimit.desc ht h k w, by simp‚ü©
#align category_theory.limits.pushout_cocone.is_colimit.desc' CategoryTheory.Limits.PushoutCocone.IsColimit.desc'

/-- This is a more convenient formulation to show that a `PushoutCocone` constructed using
`PushoutCocone.mk` is a colimit cocone.
-/
def IsColimit.mk {W : C} {inl : Y ‚ü∂ W} {inr : Z ‚ü∂ W} (eq : f ‚â´ inl = g ‚â´ inr)
    (desc : ‚àÄ s : PushoutCocone f g, W ‚ü∂ s.pt)
    (fac_left : ‚àÄ s : PushoutCocone f g, inl ‚â´ desc s = s.inl)
    (fac_right : ‚àÄ s : PushoutCocone f g, inr ‚â´ desc s = s.inr)
    (uniq :
      ‚àÄ (s : PushoutCocone f g) (m : W ‚ü∂ s.pt) (_ : inl ‚â´ m = s.inl) (_ : inr ‚â´ m = s.inr),
        m = desc s) :
    IsColimit (mk inl inr eq) :=
  isColimitAux _ desc fac_left fac_right fun s m w =>
    uniq s m (w WalkingCospan.left) (w WalkingCospan.right)
#align category_theory.limits.pushout_cocone.is_colimit.mk CategoryTheory.Limits.PushoutCocone.IsColimit.mk

section Flip

variable (t : PushoutCocone f g)

/-- The pushout cocone obtained by flipping `inl` and `inr`. -/
def flip : PushoutCocone g f := PushoutCocone.mk _ _ t.condition.symm

@[simp] lemma flip_pt : t.flip.pt = t.pt := rfl
@[simp] lemma flip_inl : t.flip.inl = t.inr := rfl
@[simp] lemma flip_inr : t.flip.inr = t.inl := rfl

/-- Flipping a pushout cocone twice gives an isomorphic cocone. -/
def flipFlipIso : t.flip.flip ‚âÖ t := PushoutCocone.ext (Iso.refl _) (by simp) (by simp)

variable {t}

/-- The flip of a pushout square is a pushout square. -/
def flipIsColimit (ht : IsColimit t) : IsColimit t.flip :=
  IsColimit.mk _ (fun s => ht.desc s.flip) (by simp) (by simp) (fun s m h‚ÇÅ h‚ÇÇ => by
    apply IsColimit.hom_ext ht
    all_goals aesop_cat)

/-- A square is a pushout square if its flip is. -/
def isColimitOfFlip (ht : IsColimit t.flip) : IsColimit t :=
  IsColimit.ofIsoColimit (flipIsColimit ht) t.flipFlipIso
#align category_theory.limits.pushout_cocone.flip_is_colimit CategoryTheory.Limits.PushoutCocone.isColimitOfFlip

end Flip

theorem epi_inr_of_is_pushout_of_epi {t : PushoutCocone f g} (ht : IsColimit t) [Epi f] :
    Epi t.inr :=
  ‚ü®fun {W} h k i => IsColimit.hom_ext ht (by simp [‚Üê cancel_epi f, t.condition_assoc, i]) i‚ü©
#align category_theory.limits.pushout_cocone.epi_inr_of_is_pushout_of_epi CategoryTheory.Limits.PushoutCocone.epi_inr_of_is_pushout_of_epi

theorem epi_inl_of_is_pushout_of_epi {t : PushoutCocone f g} (ht : IsColimit t) [Epi g] :
    Epi t.inl :=
  ‚ü®fun {W} h k i => IsColimit.hom_ext ht i (by simp [‚Üê cancel_epi g, ‚Üê t.condition_assoc, i])‚ü©
#align category_theory.limits.pushout_cocone.epi_inl_of_is_pushout_of_epi CategoryTheory.Limits.PushoutCocone.epi_inl_of_is_pushout_of_epi

/--
The pushout cocone `(ùüô X, ùüô X)` for the pair `(f, f)` is a colimit if `f` is an epi. The converse is
shown in `epi_of_isColimit_mk_id_id`.
-/
def isColimitMkIdId (f : X ‚ü∂ Y) [Epi f] : IsColimit (mk (ùüô Y) (ùüô Y) rfl : PushoutCocone f f) :=
  IsColimit.mk _ (fun s => s.inl) (fun s => Category.id_comp _)
    (fun s => by rw [‚Üê cancel_epi f, Category.id_comp, s.condition]) fun s m m‚ÇÅ _ => by
    simpa using m‚ÇÅ
#align category_theory.limits.pushout_cocone.is_colimit_mk_id_id CategoryTheory.Limits.PushoutCocone.isColimitMkIdId

/-- `f` is an epi if the pushout cocone `(ùüô X, ùüô X)` is a colimit for the pair `(f, f)`.
The converse is given in `PushoutCocone.isColimitMkIdId`.
-/
theorem epi_of_isColimitMkIdId (f : X ‚ü∂ Y)
    (t : IsColimit (mk (ùüô Y) (ùüô Y) rfl : PushoutCocone f f)) : Epi f :=
  ‚ü®fun {Z} g h eq => by
    rcases PushoutCocone.IsColimit.desc' t _ _ eq with ‚ü®_, rfl, rfl‚ü©
    rfl‚ü©
#align category_theory.limits.pushout_cocone.epi_of_is_colimit_mk_id_id CategoryTheory.Limits.PushoutCocone.epi_of_isColimitMkIdId

/-- Suppose `f` and `g` are two morphisms with a common domain and `s` is a colimit cocone over the
    diagram formed by `f` and `g`. Suppose `f` and `g` both factor through an epimorphism `h` via
    `x` and `y`, respectively. Then `s` is also a colimit cocone over the diagram formed by `x` and
    `y`.  -/
def isColimitOfFactors (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) (h : X ‚ü∂ W) [Epi h] (x : W ‚ü∂ Y) (y : W ‚ü∂ Z)
    (hhx : h ‚â´ x = f) (hhy : h ‚â´ y = g) (s : PushoutCocone f g) (hs : IsColimit s) :
    have reassoc‚ÇÅ : h ‚â´ x ‚â´ inl s = f ‚â´ inl s := by  -- Porting note: working around reassoc
      rw [‚Üê Category.assoc]; apply congrArg (¬∑ ‚â´ inl s) hhx
    have reassoc‚ÇÇ : h ‚â´ y ‚â´ inr s = g ‚â´ inr s := by
      rw [‚Üê Category.assoc]; apply congrArg (¬∑ ‚â´ inr s) hhy
    IsColimit (PushoutCocone.mk _ _ (show x ‚â´ s.inl = y ‚â´ s.inr from
          (cancel_epi h).1 <| by rw [reassoc‚ÇÅ, reassoc‚ÇÇ, s.condition])) :=
  PushoutCocone.isColimitAux' _ fun t => ‚ü®hs.desc (PushoutCocone.mk t.inl t.inr <| by
    rw [‚Üê hhx, ‚Üê hhy, Category.assoc, Category.assoc, t.condition]),
      ‚ü®hs.fac _ WalkingSpan.left, hs.fac _ WalkingSpan.right, fun hr hr' => by
        apply PushoutCocone.IsColimit.hom_ext hs;
        ¬∑ simp only [PushoutCocone.mk_inl, PushoutCocone.mk_inr] at hr hr' ‚ä¢
          simp only [hr, hr']
          symm
          exact hs.fac _ WalkingSpan.left
        ¬∑ simp only [PushoutCocone.mk_inl, PushoutCocone.mk_inr] at hr hr' ‚ä¢
          simp only [hr, hr']
          symm
          exact hs.fac _ WalkingSpan.right‚ü©‚ü©
#align category_theory.limits.pushout_cocone.is_colimit_of_factors CategoryTheory.Limits.PushoutCocone.isColimitOfFactors

/-- If `W` is the pushout of `f, g`,
it is also the pushout of `h ‚â´ f, h ‚â´ g` for any epi `h`. -/
def isColimitOfEpiComp (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) (h : W ‚ü∂ X) [Epi h] (s : PushoutCocone f g)
    (H : IsColimit s) :
    IsColimit
      (PushoutCocone.mk _ _
        (show (h ‚â´ f) ‚â´ s.inl = (h ‚â´ g) ‚â´ s.inr by
          rw [Category.assoc, Category.assoc, s.condition])) := by
  apply PushoutCocone.isColimitAux'
  intro s
  rcases PushoutCocone.IsColimit.desc' H s.inl s.inr
      ((cancel_epi h).mp (by simpa using s.condition)) with
    ‚ü®l, h‚ÇÅ, h‚ÇÇ‚ü©
  refine ‚ü®l, h‚ÇÅ, h‚ÇÇ, ?_‚ü©
  intro m hm‚ÇÅ hm‚ÇÇ
  exact (PushoutCocone.IsColimit.hom_ext H (hm‚ÇÅ.trans h‚ÇÅ.symm) (hm‚ÇÇ.trans h‚ÇÇ.symm) : _)
#align category_theory.limits.pushout_cocone.is_colimit_of_epi_comp CategoryTheory.Limits.PushoutCocone.isColimitOfEpiComp

end PushoutCocone

/-- This is a helper construction that can be useful when verifying that a category has all
    pushout. Given `F : WalkingSpan ‚•§ C`, which is really the same as
    `span (F.map fst) (F.map snd)`, and a pushout cocone on `F.map fst` and `F.map snd`,
    we get a cocone on `F`.

    If you're thinking about using this, have a look at `hasPushouts_of_hasColimit_span`, which
    you may find to be an easier way of achieving your goal. -/
@[simps]
def Cocone.ofPushoutCocone {F : WalkingSpan ‚•§ C} (t : PushoutCocone (F.map fst) (F.map snd)) :
    Cocone F where
  pt := t.pt
  Œπ := (diagramIsoSpan F).hom ‚â´ t.Œπ
#align category_theory.limits.cocone.of_pushout_cocone CategoryTheory.Limits.Cocone.ofPushoutCocone

/-- Given `F : WalkingSpan ‚•§ C`, which is really the same as `span (F.map fst) (F.map snd)`,
    and a cocone on `F`, we get a pushout cocone on `F.map fst` and `F.map snd`. -/
@[simps]
def PushoutCocone.ofCocone {F : WalkingSpan ‚•§ C} (t : Cocone F) :
    PushoutCocone (F.map fst) (F.map snd) where
  pt := t.pt
  Œπ := (diagramIsoSpan F).inv ‚â´ t.Œπ
#align category_theory.limits.pushout_cocone.of_cocone CategoryTheory.Limits.PushoutCocone.ofCocone

/-- A diagram `WalkingSpan ‚•§ C` is isomorphic to some `PushoutCocone.mk` after composing with
`diagramIsoSpan`. -/
@[simps!]
def PushoutCocone.isoMk {F : WalkingSpan ‚•§ C} (t : Cocone F) :
    (Cocones.precompose (diagramIsoSpan.{v} _).inv).obj t ‚âÖ
      PushoutCocone.mk (t.Œπ.app WalkingSpan.left) (t.Œπ.app WalkingSpan.right)
        ((t.Œπ.naturality fst).trans (t.Œπ.naturality snd).symm) :=
  Cocones.ext (Iso.refl _) <| by
    rintro (_ | (_ | _)) <;>
      ¬∑ dsimp
        simp
#align category_theory.limits.pushout_cocone.iso_mk CategoryTheory.Limits.PushoutCocone.isoMk

end CategoryTheory.Limits
