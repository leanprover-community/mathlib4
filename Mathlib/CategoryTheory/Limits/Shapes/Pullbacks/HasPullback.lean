/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Markus Himmel, Bhavik Mehta, Andrew Yang, Emily Riehl
-/
import Mathlib.CategoryTheory.Limits.Shapes.Pullbacks.PullbackCone

#align_import category_theory.limits.shapes.pullbacks from "leanprover-community/mathlib"@"7316286ff2942aa14e540add9058c6b0aa1c8070"

/-!
# HasPullback
`HasPullback f g` and `pullback f g` provides API for `HasLimit`...  `HasLimit (cospan f g)`.

This is useful when wanting to pick a pullback.

# Main definitions

* `HasPullback f g`: this is an abbreviation for `HasLimit (cospan f g)`, and is a typeclass used to
  express the fact that a given pair of morphisms has a pullback.

* `pullback f g`: Given a `HasPullback f g` instance, this function returns the choice of a limit
  object corresponding to the pullback of `f` and `g`. It fits into the following diagram:
```
  pullback f g ---pullback.snd f g---> Y
      |                                |
      |                                |
pullback.snd f g                       g
      |                                |
      v                                v
      X --------------f--------------> Z
```

* `HasPushout f g`: this is an abbreviation for `HasColimit (span f g)`, and is a typeclass used to
  express the fact that a given pair of morphisms has a pushout.
* `pushout f g`: Given a `HasPushout f g` instance, this function returns the choice of a colimit
  object corresponding to the pushout of `f` and `g`. It fits into the following diagram:
```
      X --------------f--------------> Y
      |                                |
      g                          pushout.inr f g
      |                                |
      v                                v
      Z ---pushout.inl f g---> pushout f g
```

# API
We provide the following API for using the universal property of `pullback f g`:
-- lift, ext lift' lift_fst lift_snd

-- functoriality pullbackComparison? (TODO: another file for these kind of results?)

-- symmetry


NOTE: golfed some proofs also

## References
* [Stacks: Fibre products](https://stacks.math.columbia.edu/tag/001U)
* [Stacks: Pushouts](https://stacks.math.columbia.edu/tag/0025)
-/

noncomputable section

open CategoryTheory

universe w vâ‚ vâ‚‚ v u uâ‚‚

namespace CategoryTheory.Limits

open WalkingSpan.Hom WalkingCospan.Hom WidePullbackShape.Hom WidePushoutShape.Hom

variable {C : Type u} [Category.{v} C] {W X Y Z : C}

/-- `HasPullback f g` represents a particular choice of limiting cone
for the pair of morphisms `f : X âŸ¶ Z` and `g : Y âŸ¶ Z`.
-/
abbrev HasPullback {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :=
  HasLimit (cospan f g)
#align category_theory.limits.has_pullback CategoryTheory.Limits.HasPullback

/-- `HasPushout f g` represents a particular choice of colimiting cocone
for the pair of morphisms `f : X âŸ¶ Y` and `g : X âŸ¶ Z`.
-/
abbrev HasPushout {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) :=
  HasColimit (span f g)
#align category_theory.limits.has_pushout CategoryTheory.Limits.HasPushout

/-- `pullback f g` computes the pullback of a pair of morphisms with the same target. -/
abbrev pullback {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasPullback f g] :=
  limit (cospan f g)
#align category_theory.limits.pullback CategoryTheory.Limits.pullback

/-- `pushout f g` computes the pushout of a pair of morphisms with the same source. -/
abbrev pushout {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) [HasPushout f g] :=
  colimit (span f g)
#align category_theory.limits.pushout CategoryTheory.Limits.pushout

/-- The first projection of the pullback of `f` and `g`. -/
abbrev pullback.fst {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [HasPullback f g] : pullback f g âŸ¶ X :=
  limit.Ï€ (cospan f g) WalkingCospan.left
#align category_theory.limits.pullback.fst CategoryTheory.Limits.pullback.fst

/-- The second projection of the pullback of `f` and `g`. -/
abbrev pullback.snd {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [HasPullback f g] : pullback f g âŸ¶ Y :=
  limit.Ï€ (cospan f g) WalkingCospan.right
#align category_theory.limits.pullback.snd CategoryTheory.Limits.pullback.snd

/-- The first inclusion into the pushout of `f` and `g`. -/
abbrev pushout.inl {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [HasPushout f g] : Y âŸ¶ pushout f g :=
  colimit.Î¹ (span f g) WalkingSpan.left
#align category_theory.limits.pushout.inl CategoryTheory.Limits.pushout.inl

/-- The second inclusion into the pushout of `f` and `g`. -/
abbrev pushout.inr {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [HasPushout f g] : Z âŸ¶ pushout f g :=
  colimit.Î¹ (span f g) WalkingSpan.right
#align category_theory.limits.pushout.inr CategoryTheory.Limits.pushout.inr

/-- A pair of morphisms `h : W âŸ¶ X` and `k : W âŸ¶ Y` satisfying `h â‰« f = k â‰« g` induces a morphism
    `pullback.lift : W âŸ¶ pullback f g`. -/
abbrev pullback.lift {W X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [HasPullback f g] (h : W âŸ¶ X) (k : W âŸ¶ Y)
    (w : h â‰« f = k â‰« g) : W âŸ¶ pullback f g :=
  limit.lift _ (PullbackCone.mk h k w)
#align category_theory.limits.pullback.lift CategoryTheory.Limits.pullback.lift

/-- A pair of morphisms `h : Y âŸ¶ W` and `k : Z âŸ¶ W` satisfying `f â‰« h = g â‰« k` induces a morphism
    `pushout.desc : pushout f g âŸ¶ W`. -/
abbrev pushout.desc {W X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [HasPushout f g] (h : Y âŸ¶ W) (k : Z âŸ¶ W)
    (w : f â‰« h = g â‰« k) : pushout f g âŸ¶ W :=
  colimit.desc _ (PushoutCocone.mk h k w)
#align category_theory.limits.pushout.desc CategoryTheory.Limits.pushout.desc

-- TODO: typo has been fixed here and in the next
@[simp]
theorem PullbackCone.fst_limit_cone {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasLimit (cospan f g)] :
    PullbackCone.fst (limit.cone (cospan f g)) = pullback.fst := rfl
#align category_theory.limits.pullback_cone.fst_colimit_cocone CategoryTheory.Limits.PullbackCone.fst_limit_cone

@[simp]
theorem PullbackCone.snd_limit_cone {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasLimit (cospan f g)] :
    PullbackCone.snd (limit.cone (cospan f g)) = pullback.snd := rfl
#align category_theory.limits.pullback_cone.snd_colimit_cocone CategoryTheory.Limits.PullbackCone.snd_limit_cone

-- Porting note (#10618): simp can prove this; removed simp
theorem PushoutCocone.inl_colimit_cocone {X Y Z : C} (f : Z âŸ¶ X) (g : Z âŸ¶ Y)
    [HasColimit (span f g)] : PushoutCocone.inl (colimit.cocone (span f g)) = pushout.inl := rfl
#align category_theory.limits.pushout_cocone.inl_colimit_cocone CategoryTheory.Limits.PushoutCocone.inl_colimit_cocone

-- Porting note (#10618): simp can prove this; removed simp
theorem PushoutCocone.inr_colimit_cocone {X Y Z : C} (f : Z âŸ¶ X) (g : Z âŸ¶ Y)
    [HasColimit (span f g)] : PushoutCocone.inr (colimit.cocone (span f g)) = pushout.inr := rfl
#align category_theory.limits.pushout_cocone.inr_colimit_cocone CategoryTheory.Limits.PushoutCocone.inr_colimit_cocone

-- Porting note (#10618): simp can prove this and reassoced version; removed simp
@[reassoc]
theorem pullback.lift_fst {W X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [HasPullback f g] (h : W âŸ¶ X)
    (k : W âŸ¶ Y) (w : h â‰« f = k â‰« g) : pullback.lift h k w â‰« pullback.fst = h :=
  limit.lift_Ï€ _ _
#align category_theory.limits.pullback.lift_fst CategoryTheory.Limits.pullback.lift_fst

-- Porting note (#10618): simp can prove this and reassoced version; removed simp
@[reassoc]
theorem pullback.lift_snd {W X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [HasPullback f g] (h : W âŸ¶ X)
    (k : W âŸ¶ Y) (w : h â‰« f = k â‰« g) : pullback.lift h k w â‰« pullback.snd = k :=
  limit.lift_Ï€ _ _
#align category_theory.limits.pullback.lift_snd CategoryTheory.Limits.pullback.lift_snd

-- Porting note (#10618): simp can prove this and reassoced version; removed simp
@[reassoc]
theorem pushout.inl_desc {W X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [HasPushout f g] (h : Y âŸ¶ W)
    (k : Z âŸ¶ W) (w : f â‰« h = g â‰« k) : pushout.inl â‰« pushout.desc h k w = h :=
  colimit.Î¹_desc _ _
#align category_theory.limits.pushout.inl_desc CategoryTheory.Limits.pushout.inl_desc

-- Porting note (#10618): simp can prove this and reassoced version; removed simp
@[reassoc]
theorem pushout.inr_desc {W X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [HasPushout f g] (h : Y âŸ¶ W)
    (k : Z âŸ¶ W) (w : f â‰« h = g â‰« k) : pushout.inr â‰« pushout.desc h k w = k :=
  colimit.Î¹_desc _ _
#align category_theory.limits.pushout.inr_desc CategoryTheory.Limits.pushout.inr_desc

/-- A pair of morphisms `h : W âŸ¶ X` and `k : W âŸ¶ Y` satisfying `h â‰« f = k â‰« g` induces a morphism
    `l : W âŸ¶ pullback f g` such that `l â‰« pullback.fst = h` and `l â‰« pullback.snd = k`. -/
def pullback.lift' {W X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [HasPullback f g] (h : W âŸ¶ X) (k : W âŸ¶ Y)
    (w : h â‰« f = k â‰« g) : { l : W âŸ¶ pullback f g // l â‰« pullback.fst = h âˆ§ l â‰« pullback.snd = k } :=
  âŸ¨pullback.lift h k w, pullback.lift_fst _ _ _, pullback.lift_snd _ _ _âŸ©
#align category_theory.limits.pullback.lift' CategoryTheory.Limits.pullback.lift'

/-- A pair of morphisms `h : Y âŸ¶ W` and `k : Z âŸ¶ W` satisfying `f â‰« h = g â‰« k` induces a morphism
    `l : pushout f g âŸ¶ W` such that `pushout.inl â‰« l = h` and `pushout.inr â‰« l = k`. -/
def pullback.desc' {W X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [HasPushout f g] (h : Y âŸ¶ W) (k : Z âŸ¶ W)
    (w : f â‰« h = g â‰« k) : { l : pushout f g âŸ¶ W // pushout.inl â‰« l = h âˆ§ pushout.inr â‰« l = k } :=
  âŸ¨pushout.desc h k w, pushout.inl_desc _ _ _, pushout.inr_desc _ _ _âŸ©
#align category_theory.limits.pullback.desc' CategoryTheory.Limits.pullback.desc'

@[reassoc]
theorem pullback.condition {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [HasPullback f g] :
    (pullback.fst : pullback f g âŸ¶ X) â‰« f = pullback.snd â‰« g :=
  PullbackCone.condition _
#align category_theory.limits.pullback.condition CategoryTheory.Limits.pullback.condition

@[reassoc]
theorem pushout.condition {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [HasPushout f g] :
    f â‰« (pushout.inl : Y âŸ¶ pushout f g) = g â‰« pushout.inr :=
  PushoutCocone.condition _
#align category_theory.limits.pushout.condition CategoryTheory.Limits.pushout.condition

/-- Given such a diagram, then there is a natural morphism `W Ã—â‚› X âŸ¶ Y Ã—â‚œ Z`.

    W âŸ¶ Y
      â†˜      â†˜
        S âŸ¶ T
      â†—      â†—
    X âŸ¶ Z

-/
abbrev pullback.map {W X Y Z S T : C} (fâ‚ : W âŸ¶ S) (fâ‚‚ : X âŸ¶ S) [HasPullback fâ‚ fâ‚‚] (gâ‚ : Y âŸ¶ T)
    (gâ‚‚ : Z âŸ¶ T) [HasPullback gâ‚ gâ‚‚] (iâ‚ : W âŸ¶ Y) (iâ‚‚ : X âŸ¶ Z) (iâ‚ƒ : S âŸ¶ T)
    (eqâ‚ : fâ‚ â‰« iâ‚ƒ = iâ‚ â‰« gâ‚) (eqâ‚‚ : fâ‚‚ â‰« iâ‚ƒ = iâ‚‚ â‰« gâ‚‚) : pullback fâ‚ fâ‚‚ âŸ¶ pullback gâ‚ gâ‚‚ :=
  pullback.lift (pullback.fst â‰« iâ‚) (pullback.snd â‰« iâ‚‚)
    (by simp [â† eqâ‚, â† eqâ‚‚, pullback.condition_assoc])
#align category_theory.limits.pullback.map CategoryTheory.Limits.pullback.map

/-- The canonical map `X Ã—â‚› Y âŸ¶ X Ã—â‚œ Y` given `S âŸ¶ T`. -/
abbrev pullback.mapDesc {X Y S T : C} (f : X âŸ¶ S) (g : Y âŸ¶ S) (i : S âŸ¶ T) [HasPullback f g]
    [HasPullback (f â‰« i) (g â‰« i)] : pullback f g âŸ¶ pullback (f â‰« i) (g â‰« i) :=
  pullback.map f g (f â‰« i) (g â‰« i) (ğŸ™ _) (ğŸ™ _) i (Category.id_comp _).symm (Category.id_comp _).symm
#align category_theory.limits.pullback.map_desc CategoryTheory.Limits.pullback.mapDesc

/-- Given such a diagram, then there is a natural morphism `W â¨¿â‚› X âŸ¶ Y â¨¿â‚œ Z`.

        W âŸ¶ Y
      â†—      â†—
    S âŸ¶ T
      â†˜      â†˜
        X âŸ¶ Z

-/
abbrev pushout.map {W X Y Z S T : C} (fâ‚ : S âŸ¶ W) (fâ‚‚ : S âŸ¶ X) [HasPushout fâ‚ fâ‚‚] (gâ‚ : T âŸ¶ Y)
    (gâ‚‚ : T âŸ¶ Z) [HasPushout gâ‚ gâ‚‚] (iâ‚ : W âŸ¶ Y) (iâ‚‚ : X âŸ¶ Z) (iâ‚ƒ : S âŸ¶ T) (eqâ‚ : fâ‚ â‰« iâ‚ = iâ‚ƒ â‰« gâ‚)
    (eqâ‚‚ : fâ‚‚ â‰« iâ‚‚ = iâ‚ƒ â‰« gâ‚‚) : pushout fâ‚ fâ‚‚ âŸ¶ pushout gâ‚ gâ‚‚ :=
  pushout.desc (iâ‚ â‰« pushout.inl) (iâ‚‚ â‰« pushout.inr)
    (by
      simp only [â† Category.assoc, eqâ‚, eqâ‚‚]
      simp [pushout.condition])
#align category_theory.limits.pushout.map CategoryTheory.Limits.pushout.map

/-- The canonical map `X â¨¿â‚› Y âŸ¶ X â¨¿â‚œ Y` given `S âŸ¶ T`. -/
abbrev pushout.mapLift {X Y S T : C} (f : T âŸ¶ X) (g : T âŸ¶ Y) (i : S âŸ¶ T) [HasPushout f g]
    [HasPushout (i â‰« f) (i â‰« g)] : pushout (i â‰« f) (i â‰« g) âŸ¶ pushout f g :=
  pushout.map (i â‰« f) (i â‰« g) f g (ğŸ™ _) (ğŸ™ _) i (Category.comp_id _) (Category.comp_id _)
#align category_theory.limits.pushout.map_lift CategoryTheory.Limits.pushout.mapLift

/-- Two morphisms into a pullback are equal if their compositions with the pullback morphisms are
    equal -/
@[ext 1100]
theorem pullback.hom_ext {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [HasPullback f g] {W : C}
    {k l : W âŸ¶ pullback f g} (hâ‚€ : k â‰« pullback.fst = l â‰« pullback.fst)
    (hâ‚ : k â‰« pullback.snd = l â‰« pullback.snd) : k = l :=
  limit.hom_ext <| PullbackCone.equalizer_ext _ hâ‚€ hâ‚
#align category_theory.limits.pullback.hom_ext CategoryTheory.Limits.pullback.hom_ext

/-- The pullback cone built from the pullback projections is a pullback. -/
def pullbackIsPullback {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasPullback f g] :
    IsLimit (PullbackCone.mk (pullback.fst : pullback f g âŸ¶ _) pullback.snd pullback.condition) :=
  PullbackCone.IsLimit.mk _ (fun s => pullback.lift s.fst s.snd s.condition) (by simp) (by simp)
    (by aesop_cat)
#align category_theory.limits.pullback_is_pullback CategoryTheory.Limits.pullbackIsPullback

/-- The pullback of a monomorphism is a monomorphism -/
instance pullback.fst_of_mono {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [HasPullback f g] [Mono g] :
    Mono (pullback.fst : pullback f g âŸ¶ X) :=
  PullbackCone.mono_fst_of_is_pullback_of_mono (limit.isLimit _)
#align category_theory.limits.pullback.fst_of_mono CategoryTheory.Limits.pullback.fst_of_mono

/-- The pullback of a monomorphism is a monomorphism -/
instance pullback.snd_of_mono {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [HasPullback f g] [Mono f] :
    Mono (pullback.snd : pullback f g âŸ¶ Y) :=
  PullbackCone.mono_snd_of_is_pullback_of_mono (limit.isLimit _)
#align category_theory.limits.pullback.snd_of_mono CategoryTheory.Limits.pullback.snd_of_mono

/-- The map `X Ã—[Z] Y âŸ¶ X Ã— Y` is mono. -/
instance mono_pullback_to_prod {C : Type*} [Category C] {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z)
    [HasPullback f g] [HasBinaryProduct X Y] :
    Mono (prod.lift pullback.fst pullback.snd : pullback f g âŸ¶ _) :=
  âŸ¨fun {W} iâ‚ iâ‚‚ h => by
    ext
    Â· simpa using congrArg (fun f => f â‰« prod.fst) h
    Â· simpa using congrArg (fun f => f â‰« prod.snd) hâŸ©
#align category_theory.limits.mono_pullback_to_prod CategoryTheory.Limits.mono_pullback_to_prod

/-- Two morphisms out of a pushout are equal if their compositions with the pushout morphisms are
    equal -/
@[ext 1100]
theorem pushout.hom_ext {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [HasPushout f g] {W : C}
    {k l : pushout f g âŸ¶ W} (hâ‚€ : pushout.inl â‰« k = pushout.inl â‰« l)
    (hâ‚ : pushout.inr â‰« k = pushout.inr â‰« l) : k = l :=
  colimit.hom_ext <| PushoutCocone.coequalizer_ext _ hâ‚€ hâ‚
#align category_theory.limits.pushout.hom_ext CategoryTheory.Limits.pushout.hom_ext

/-- The pushout cocone built from the pushout coprojections is a pushout. -/
def pushoutIsPushout {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) [HasPushout f g] :
    IsColimit (PushoutCocone.mk (pushout.inl : _ âŸ¶ pushout f g) pushout.inr pushout.condition) :=
  PushoutCocone.IsColimit.mk _ (fun s => pushout.desc s.inl s.inr s.condition) (by simp) (by simp)
    (by aesop_cat)
#align category_theory.limits.pushout_is_pushout CategoryTheory.Limits.pushoutIsPushout

/-- The pushout of an epimorphism is an epimorphism -/
instance pushout.inl_of_epi {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [HasPushout f g] [Epi g] :
    Epi (pushout.inl : Y âŸ¶ pushout f g) :=
  PushoutCocone.epi_inl_of_is_pushout_of_epi (colimit.isColimit _)
#align category_theory.limits.pushout.inl_of_epi CategoryTheory.Limits.pushout.inl_of_epi

/-- The pushout of an epimorphism is an epimorphism -/
instance pushout.inr_of_epi {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [HasPushout f g] [Epi f] :
    Epi (pushout.inr : Z âŸ¶ pushout f g) :=
  PushoutCocone.epi_inr_of_is_pushout_of_epi (colimit.isColimit _)
#align category_theory.limits.pushout.inr_of_epi CategoryTheory.Limits.pushout.inr_of_epi

/-- The map `X â¨¿ Y âŸ¶ X â¨¿[Z] Y` is epi. -/
instance epi_coprod_to_pushout {C : Type*} [Category C] {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z)
    [HasPushout f g] [HasBinaryCoproduct Y Z] :
    Epi (coprod.desc pushout.inl pushout.inr : _ âŸ¶ pushout f g) :=
  âŸ¨fun {W} iâ‚ iâ‚‚ h => by
    ext
    Â· simpa using congrArg (fun f => coprod.inl â‰« f) h
    Â· simpa using congrArg (fun f => coprod.inr â‰« f) hâŸ©
#align category_theory.limits.epi_coprod_to_pushout CategoryTheory.Limits.epi_coprod_to_pushout

instance pullback.map_isIso {W X Y Z S T : C} (fâ‚ : W âŸ¶ S) (fâ‚‚ : X âŸ¶ S) [HasPullback fâ‚ fâ‚‚]
    (gâ‚ : Y âŸ¶ T) (gâ‚‚ : Z âŸ¶ T) [HasPullback gâ‚ gâ‚‚] (iâ‚ : W âŸ¶ Y) (iâ‚‚ : X âŸ¶ Z) (iâ‚ƒ : S âŸ¶ T)
    (eqâ‚ : fâ‚ â‰« iâ‚ƒ = iâ‚ â‰« gâ‚) (eqâ‚‚ : fâ‚‚ â‰« iâ‚ƒ = iâ‚‚ â‰« gâ‚‚) [IsIso iâ‚] [IsIso iâ‚‚] [IsIso iâ‚ƒ] :
    IsIso (pullback.map fâ‚ fâ‚‚ gâ‚ gâ‚‚ iâ‚ iâ‚‚ iâ‚ƒ eqâ‚ eqâ‚‚) := by
  refine âŸ¨âŸ¨pullback.map _ _ _ _ (inv iâ‚) (inv iâ‚‚) (inv iâ‚ƒ) ?_ ?_, ?_, ?_âŸ©âŸ©
  Â· rw [IsIso.comp_inv_eq, Category.assoc, eqâ‚, IsIso.inv_hom_id_assoc]
  Â· rw [IsIso.comp_inv_eq, Category.assoc, eqâ‚‚, IsIso.inv_hom_id_assoc]
  Â· aesop_cat
  Â· aesop_cat
#align category_theory.limits.pullback.map_is_iso CategoryTheory.Limits.pullback.map_isIso

/-- If `fâ‚ = fâ‚‚` and `gâ‚ = gâ‚‚`, we may construct a canonical
isomorphism `pullback fâ‚ gâ‚ â‰… pullback fâ‚‚ gâ‚‚` -/
@[simps! hom]
def pullback.congrHom {X Y Z : C} {fâ‚ fâ‚‚ : X âŸ¶ Z} {gâ‚ gâ‚‚ : Y âŸ¶ Z} (hâ‚ : fâ‚ = fâ‚‚) (hâ‚‚ : gâ‚ = gâ‚‚)
    [HasPullback fâ‚ gâ‚] [HasPullback fâ‚‚ gâ‚‚] : pullback fâ‚ gâ‚ â‰… pullback fâ‚‚ gâ‚‚ :=
  asIso <| pullback.map _ _ _ _ (ğŸ™ _) (ğŸ™ _) (ğŸ™ _) (by simp [hâ‚]) (by simp [hâ‚‚])
#align category_theory.limits.pullback.congr_hom CategoryTheory.Limits.pullback.congrHom

@[simp]
theorem pullback.congrHom_inv {X Y Z : C} {fâ‚ fâ‚‚ : X âŸ¶ Z} {gâ‚ gâ‚‚ : Y âŸ¶ Z} (hâ‚ : fâ‚ = fâ‚‚)
    (hâ‚‚ : gâ‚ = gâ‚‚) [HasPullback fâ‚ gâ‚] [HasPullback fâ‚‚ gâ‚‚] :
    (pullback.congrHom hâ‚ hâ‚‚).inv =
      pullback.map _ _ _ _ (ğŸ™ _) (ğŸ™ _) (ğŸ™ _) (by simp [hâ‚]) (by simp [hâ‚‚]) := by
  ext <;> simp [Iso.inv_comp_eq]
#align category_theory.limits.pullback.congr_hom_inv CategoryTheory.Limits.pullback.congrHom_inv

instance pushout.map_isIso {W X Y Z S T : C} (fâ‚ : S âŸ¶ W) (fâ‚‚ : S âŸ¶ X) [HasPushout fâ‚ fâ‚‚]
    (gâ‚ : T âŸ¶ Y) (gâ‚‚ : T âŸ¶ Z) [HasPushout gâ‚ gâ‚‚] (iâ‚ : W âŸ¶ Y) (iâ‚‚ : X âŸ¶ Z) (iâ‚ƒ : S âŸ¶ T)
    (eqâ‚ : fâ‚ â‰« iâ‚ = iâ‚ƒ â‰« gâ‚) (eqâ‚‚ : fâ‚‚ â‰« iâ‚‚ = iâ‚ƒ â‰« gâ‚‚) [IsIso iâ‚] [IsIso iâ‚‚] [IsIso iâ‚ƒ] :
    IsIso (pushout.map fâ‚ fâ‚‚ gâ‚ gâ‚‚ iâ‚ iâ‚‚ iâ‚ƒ eqâ‚ eqâ‚‚) := by
  refine âŸ¨âŸ¨pushout.map _ _ _ _ (inv iâ‚) (inv iâ‚‚) (inv iâ‚ƒ) ?_ ?_, ?_, ?_âŸ©âŸ©
  Â· rw [IsIso.comp_inv_eq, Category.assoc, eqâ‚, IsIso.inv_hom_id_assoc]
  Â· rw [IsIso.comp_inv_eq, Category.assoc, eqâ‚‚, IsIso.inv_hom_id_assoc]
  Â· aesop_cat
  Â· aesop_cat
#align category_theory.limits.pushout.map_is_iso CategoryTheory.Limits.pushout.map_isIso

theorem pullback.mapDesc_comp {X Y S T S' : C} (f : X âŸ¶ T) (g : Y âŸ¶ T) (i : T âŸ¶ S) (i' : S âŸ¶ S')
    [HasPullback f g] [HasPullback (f â‰« i) (g â‰« i)] [HasPullback (f â‰« i â‰« i') (g â‰« i â‰« i')]
    [HasPullback ((f â‰« i) â‰« i') ((g â‰« i) â‰« i')] :
    pullback.mapDesc f g (i â‰« i') = pullback.mapDesc f g i â‰« pullback.mapDesc _ _ i' â‰«
    (pullback.congrHom (Category.assoc _ _ _) (Category.assoc _ _ _)).hom := by
  aesop_cat
#align category_theory.limits.pullback.map_desc_comp CategoryTheory.Limits.pullback.mapDesc_comp

/-- If `fâ‚ = fâ‚‚` and `gâ‚ = gâ‚‚`, we may construct a canonical
isomorphism `pushout fâ‚ gâ‚ â‰… pullback fâ‚‚ gâ‚‚` -/
@[simps! hom]
def pushout.congrHom {X Y Z : C} {fâ‚ fâ‚‚ : X âŸ¶ Y} {gâ‚ gâ‚‚ : X âŸ¶ Z} (hâ‚ : fâ‚ = fâ‚‚) (hâ‚‚ : gâ‚ = gâ‚‚)
    [HasPushout fâ‚ gâ‚] [HasPushout fâ‚‚ gâ‚‚] : pushout fâ‚ gâ‚ â‰… pushout fâ‚‚ gâ‚‚ :=
  asIso <| pushout.map _ _ _ _ (ğŸ™ _) (ğŸ™ _) (ğŸ™ _) (by simp [hâ‚]) (by simp [hâ‚‚])
#align category_theory.limits.pushout.congr_hom CategoryTheory.Limits.pushout.congrHom

@[simp]
theorem pushout.congrHom_inv {X Y Z : C} {fâ‚ fâ‚‚ : X âŸ¶ Y} {gâ‚ gâ‚‚ : X âŸ¶ Z} (hâ‚ : fâ‚ = fâ‚‚)
    (hâ‚‚ : gâ‚ = gâ‚‚) [HasPushout fâ‚ gâ‚] [HasPushout fâ‚‚ gâ‚‚] :
    (pushout.congrHom hâ‚ hâ‚‚).inv =
      pushout.map _ _ _ _ (ğŸ™ _) (ğŸ™ _) (ğŸ™ _) (by simp [hâ‚]) (by simp [hâ‚‚]) := by
  ext <;> simp [Iso.comp_inv_eq]
#align category_theory.limits.pushout.congr_hom_inv CategoryTheory.Limits.pushout.congrHom_inv

theorem pushout.mapLift_comp {X Y S T S' : C} (f : T âŸ¶ X) (g : T âŸ¶ Y) (i : S âŸ¶ T) (i' : S' âŸ¶ S)
    [HasPushout f g] [HasPushout (i â‰« f) (i â‰« g)] [HasPushout (i' â‰« i â‰« f) (i' â‰« i â‰« g)]
    [HasPushout ((i' â‰« i) â‰« f) ((i' â‰« i) â‰« g)] :
    pushout.mapLift f g (i' â‰« i) =
      (pushout.congrHom (Category.assoc _ _ _) (Category.assoc _ _ _)).hom â‰«
        pushout.mapLift _ _ i' â‰« pushout.mapLift f g i := by
  aesop_cat
#align category_theory.limits.pushout.map_lift_comp CategoryTheory.Limits.pushout.mapLift_comp

section

variable {D : Type uâ‚‚} [Category.{vâ‚‚} D] (G : C â¥¤ D)

/-- The comparison morphism for the pullback of `f,g`.
This is an isomorphism iff `G` preserves the pullback of `f,g`; see
`Mathlib/CategoryTheory/Limits/Preserves/Shapes/Pullbacks.lean`
-/
def pullbackComparison (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasPullback f g] [HasPullback (G.map f) (G.map g)] :
    G.obj (pullback f g) âŸ¶ pullback (G.map f) (G.map g) :=
  pullback.lift (G.map pullback.fst) (G.map pullback.snd)
    (by simp only [â† G.map_comp, pullback.condition])
#align category_theory.limits.pullback_comparison CategoryTheory.Limits.pullbackComparison

@[reassoc (attr := simp)]
theorem pullbackComparison_comp_fst (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasPullback f g]
    [HasPullback (G.map f) (G.map g)] :
    pullbackComparison G f g â‰« pullback.fst = G.map pullback.fst :=
  pullback.lift_fst _ _ _
#align category_theory.limits.pullback_comparison_comp_fst CategoryTheory.Limits.pullbackComparison_comp_fst

@[reassoc (attr := simp)]
theorem pullbackComparison_comp_snd (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasPullback f g]
    [HasPullback (G.map f) (G.map g)] :
    pullbackComparison G f g â‰« pullback.snd = G.map pullback.snd :=
  pullback.lift_snd _ _ _
#align category_theory.limits.pullback_comparison_comp_snd CategoryTheory.Limits.pullbackComparison_comp_snd

@[reassoc (attr := simp)]
theorem map_lift_pullbackComparison (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasPullback f g]
    [HasPullback (G.map f) (G.map g)] {W : C} {h : W âŸ¶ X} {k : W âŸ¶ Y} (w : h â‰« f = k â‰« g) :
    G.map (pullback.lift _ _ w) â‰« pullbackComparison G f g =
      pullback.lift (G.map h) (G.map k) (by simp only [â† G.map_comp, w]) := by
  ext <;> simp [â† G.map_comp]
#align category_theory.limits.map_lift_pullback_comparison CategoryTheory.Limits.map_lift_pullbackComparison

/-- The comparison morphism for the pushout of `f,g`.
This is an isomorphism iff `G` preserves the pushout of `f,g`; see
`Mathlib/CategoryTheory/Limits/Preserves/Shapes/Pullbacks.lean`
-/
def pushoutComparison (f : X âŸ¶ Y) (g : X âŸ¶ Z) [HasPushout f g] [HasPushout (G.map f) (G.map g)] :
    pushout (G.map f) (G.map g) âŸ¶ G.obj (pushout f g) :=
  pushout.desc (G.map pushout.inl) (G.map pushout.inr)
    (by simp only [â† G.map_comp, pushout.condition])
#align category_theory.limits.pushout_comparison CategoryTheory.Limits.pushoutComparison

@[reassoc (attr := simp)]
theorem inl_comp_pushoutComparison (f : X âŸ¶ Y) (g : X âŸ¶ Z) [HasPushout f g]
    [HasPushout (G.map f) (G.map g)] : pushout.inl â‰« pushoutComparison G f g = G.map pushout.inl :=
  pushout.inl_desc _ _ _
#align category_theory.limits.inl_comp_pushout_comparison CategoryTheory.Limits.inl_comp_pushoutComparison

@[reassoc (attr := simp)]
theorem inr_comp_pushoutComparison (f : X âŸ¶ Y) (g : X âŸ¶ Z) [HasPushout f g]
    [HasPushout (G.map f) (G.map g)] : pushout.inr â‰« pushoutComparison G f g = G.map pushout.inr :=
  pushout.inr_desc _ _ _
#align category_theory.limits.inr_comp_pushout_comparison CategoryTheory.Limits.inr_comp_pushoutComparison

@[reassoc (attr := simp)]
theorem pushoutComparison_map_desc (f : X âŸ¶ Y) (g : X âŸ¶ Z) [HasPushout f g]
    [HasPushout (G.map f) (G.map g)] {W : C} {h : Y âŸ¶ W} {k : Z âŸ¶ W} (w : f â‰« h = g â‰« k) :
    pushoutComparison G f g â‰« G.map (pushout.desc _ _ w) =
      pushout.desc (G.map h) (G.map k) (by simp only [â† G.map_comp, w]) := by
  ext <;> simp [â† G.map_comp]
#align category_theory.limits.pushout_comparison_map_desc CategoryTheory.Limits.pushoutComparison_map_desc

end

section PullbackSymmetry

open WalkingCospan

variable (f : X âŸ¶ Z) (g : Y âŸ¶ Z)

/-- Making this a global instance would make the typeclass search go in an infinite loop. -/
theorem hasPullback_symmetry [HasPullback f g] : HasPullback g f :=
  âŸ¨âŸ¨âŸ¨_, PullbackCone.flipIsLimit (pullbackIsPullback f g)âŸ©âŸ©âŸ©
#align category_theory.limits.has_pullback_symmetry CategoryTheory.Limits.hasPullback_symmetry

attribute [local instance] hasPullback_symmetry

/-- The isomorphism `X Ã—[Z] Y â‰… Y Ã—[Z] X`. -/
def pullbackSymmetry [HasPullback f g] : pullback f g â‰… pullback g f :=
  IsLimit.conePointUniqueUpToIso
    (PullbackCone.flipIsLimit (pullbackIsPullback f g)) (limit.isLimit _)
#align category_theory.limits.pullback_symmetry CategoryTheory.Limits.pullbackSymmetry

@[reassoc (attr := simp)]
theorem pullbackSymmetry_hom_comp_fst [HasPullback f g] :
    (pullbackSymmetry f g).hom â‰« pullback.fst = pullback.snd := by simp [pullbackSymmetry]
#align category_theory.limits.pullback_symmetry_hom_comp_fst CategoryTheory.Limits.pullbackSymmetry_hom_comp_fst

@[reassoc (attr := simp)]
theorem pullbackSymmetry_hom_comp_snd [HasPullback f g] :
    (pullbackSymmetry f g).hom â‰« pullback.snd = pullback.fst := by simp [pullbackSymmetry]
#align category_theory.limits.pullback_symmetry_hom_comp_snd CategoryTheory.Limits.pullbackSymmetry_hom_comp_snd

@[reassoc (attr := simp)]
theorem pullbackSymmetry_inv_comp_fst [HasPullback f g] :
    (pullbackSymmetry f g).inv â‰« pullback.fst = pullback.snd := by simp [Iso.inv_comp_eq]
#align category_theory.limits.pullback_symmetry_inv_comp_fst CategoryTheory.Limits.pullbackSymmetry_inv_comp_fst

@[reassoc (attr := simp)]
theorem pullbackSymmetry_inv_comp_snd [HasPullback f g] :
    (pullbackSymmetry f g).inv â‰« pullback.snd = pullback.fst := by simp [Iso.inv_comp_eq]
#align category_theory.limits.pullback_symmetry_inv_comp_snd CategoryTheory.Limits.pullbackSymmetry_inv_comp_snd

end PullbackSymmetry

section PushoutSymmetry

open WalkingCospan

variable (f : X âŸ¶ Y) (g : X âŸ¶ Z)

/-- Making this a global instance would make the typeclass search go in an infinite loop. -/
theorem hasPushout_symmetry [HasPushout f g] : HasPushout g f :=
  âŸ¨âŸ¨âŸ¨_, PushoutCocone.flipIsColimit (pushoutIsPushout f g)âŸ©âŸ©âŸ©
#align category_theory.limits.has_pushout_symmetry CategoryTheory.Limits.hasPushout_symmetry

attribute [local instance] hasPushout_symmetry

/-- The isomorphism `Y â¨¿[X] Z â‰… Z â¨¿[X] Y`. -/
def pushoutSymmetry [HasPushout f g] : pushout f g â‰… pushout g f :=
  IsColimit.coconePointUniqueUpToIso
    (PushoutCocone.flipIsColimit (pushoutIsPushout f g)) (colimit.isColimit _)
#align category_theory.limits.pushout_symmetry CategoryTheory.Limits.pushoutSymmetry

@[reassoc (attr := simp)]
theorem inl_comp_pushoutSymmetry_hom [HasPushout f g] :
    pushout.inl â‰« (pushoutSymmetry f g).hom = pushout.inr :=
  (colimit.isColimit (span f g)).comp_coconePointUniqueUpToIso_hom
    (PushoutCocone.flipIsColimit (pushoutIsPushout g f)) _
#align category_theory.limits.inl_comp_pushout_symmetry_hom CategoryTheory.Limits.inl_comp_pushoutSymmetry_hom

@[reassoc (attr := simp)]
theorem inr_comp_pushoutSymmetry_hom [HasPushout f g] :
    pushout.inr â‰« (pushoutSymmetry f g).hom = pushout.inl :=
  (colimit.isColimit (span f g)).comp_coconePointUniqueUpToIso_hom
    (PushoutCocone.flipIsColimit (pushoutIsPushout g f)) _
#align category_theory.limits.inr_comp_pushout_symmetry_hom CategoryTheory.Limits.inr_comp_pushoutSymmetry_hom

@[reassoc (attr := simp)]
theorem inl_comp_pushoutSymmetry_inv [HasPushout f g] :
    pushout.inl â‰« (pushoutSymmetry f g).inv = pushout.inr := by simp [Iso.comp_inv_eq]
#align category_theory.limits.inl_comp_pushout_symmetry_inv CategoryTheory.Limits.inl_comp_pushoutSymmetry_inv

@[reassoc (attr := simp)]
theorem inr_comp_pushoutSymmetry_inv [HasPushout f g] :
    pushout.inr â‰« (pushoutSymmetry f g).inv = pushout.inl := by simp [Iso.comp_inv_eq]
#align category_theory.limits.inr_comp_pushout_symmetry_inv CategoryTheory.Limits.inr_comp_pushoutSymmetry_inv

end PushoutSymmetry

variable (C)

/-- `HasPullbacks` represents a choice of pullback for every pair of morphisms

See <https://stacks.math.columbia.edu/tag/001W>
-/
abbrev HasPullbacks :=
  HasLimitsOfShape WalkingCospan C
#align category_theory.limits.has_pullbacks CategoryTheory.Limits.HasPullbacks

/-- `HasPushouts` represents a choice of pushout for every pair of morphisms -/
abbrev HasPushouts :=
  HasColimitsOfShape WalkingSpan C
#align category_theory.limits.has_pushouts CategoryTheory.Limits.HasPushouts

/-- If `C` has all limits of diagrams `cospan f g`, then it has all pullbacks -/
theorem hasPullbacks_of_hasLimit_cospan
    [âˆ€ {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z}, HasLimit (cospan f g)] : HasPullbacks C :=
  { has_limit := fun F => hasLimitOfIso (diagramIsoCospan F).symm }
#align category_theory.limits.has_pullbacks_of_has_limit_cospan CategoryTheory.Limits.hasPullbacks_of_hasLimit_cospan

/-- If `C` has all colimits of diagrams `span f g`, then it has all pushouts -/
theorem hasPushouts_of_hasColimit_span
    [âˆ€ {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z}, HasColimit (span f g)] : HasPushouts C :=
  { has_colimit := fun F => hasColimitOfIso (diagramIsoSpan F) }
#align category_theory.limits.has_pushouts_of_has_colimit_span CategoryTheory.Limits.hasPushouts_of_hasColimit_span

/-- The duality equivalence `WalkingSpanáµ’áµ– â‰Œ WalkingCospan` -/
@[simps!]
def walkingSpanOpEquiv : WalkingSpanáµ’áµ– â‰Œ WalkingCospan :=
  widePushoutShapeOpEquiv _
#align category_theory.limits.walking_span_op_equiv CategoryTheory.Limits.walkingSpanOpEquiv

/-- The duality equivalence `WalkingCospanáµ’áµ– â‰Œ WalkingSpan` -/
@[simps!]
def walkingCospanOpEquiv : WalkingCospanáµ’áµ– â‰Œ WalkingSpan :=
  widePullbackShapeOpEquiv _
#align category_theory.limits.walking_cospan_op_equiv CategoryTheory.Limits.walkingCospanOpEquiv

-- see Note [lower instance priority]
/-- Having wide pullback at any universe level implies having binary pullbacks. -/
instance (priority := 100) hasPullbacks_of_hasWidePullbacks (D : Type u) [h : Category.{v} D]
    [h' : HasWidePullbacks.{w} D] : HasPullbacks.{v,u} D := by
  haveI I := @hasWidePullbacks_shrink.{0, w} D h h'
  infer_instance
#align category_theory.limits.has_pullbacks_of_has_wide_pullbacks CategoryTheory.Limits.hasPullbacks_of_hasWidePullbacks

-- see Note [lower instance priority]
/-- Having wide pushout at any universe level implies having binary pushouts. -/
instance (priority := 100) hasPushouts_of_hasWidePushouts (D : Type u) [h : Category.{v} D]
    [h' : HasWidePushouts.{w} D] : HasPushouts.{v,u} D := by
  haveI I := @hasWidePushouts_shrink.{0, w} D h h'
  infer_instance


end CategoryTheory.Limits
