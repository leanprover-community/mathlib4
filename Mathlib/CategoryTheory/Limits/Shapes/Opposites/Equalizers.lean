/-
Copyright (c) 2025 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Floris van Doorn
-/
import Mathlib.CategoryTheory.Limits.Opposites
import Mathlib.CategoryTheory.Limits.Shapes.Equalizers
import Mathlib.CategoryTheory.Limits.Shapes.Opposites.Pullbacks

/-!
# Equalizers and coequalizers in `C` and `Cáµ’áµ–`

We construct equalizers and coequalizers in the opposite categories.

-/

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

noncomputable section

open CategoryTheory

open CategoryTheory.Functor

open Opposite

namespace CategoryTheory.Limits

variable {C : Type uâ‚} [Category.{vâ‚} C]
variable {J : Type uâ‚‚} [Category.{vâ‚‚} J]

instance hasEqualizers_opposite [HasCoequalizers C] : HasEqualizers Cáµ’áµ– :=
  haveI : HasColimitsOfShape WalkingParallelPairáµ’áµ– C :=
    hasColimitsOfShape_of_equivalence walkingParallelPairOpEquiv
  hasLimitsOfShape_op_of_hasColimitsOfShape

instance hasCoequalizers_opposite [HasEqualizers C] : HasCoequalizers Cáµ’áµ– :=
  haveI : HasLimitsOfShape WalkingParallelPairáµ’áµ– C :=
    hasLimitsOfShape_of_equivalence walkingParallelPairOpEquiv
  hasColimitsOfShape_op_of_hasLimitsOfShape

/-- The canonical isomorphism relating `parallelPair f.op g.op` and `(parallelPair f g).op` -/
def parallelPairOpIso {X Y : C} (f g : X âŸ¶ Y) :
    parallelPair f.op g.op â‰… walkingParallelPairOpEquiv.functor â‹™ (parallelPair f g).op :=
  NatIso.ofComponents (fun
    | .zero => .refl _
    | .one => .refl _)
    (by rintro (_ | _ | _) (_ | _ | _) f <;> cases f <;> cat_disch)

@[simp]
lemma parallelPairOpIso_hom_app_zero {X Y : C} (f g : X âŸ¶ Y) :
  (parallelPairOpIso f g).hom.app WalkingParallelPair.zero = ğŸ™ _ := rfl

@[simp]
lemma parallelPairOpIso_hom_app_one {X Y : C} (f g : X âŸ¶ Y) :
  (parallelPairOpIso f g).hom.app WalkingParallelPair.one = ğŸ™ _ := rfl

@[simp]
lemma parallelPairOpIso_inv_app_zero {X Y : C} (f g : X âŸ¶ Y) :
  (parallelPairOpIso f g).inv.app WalkingParallelPair.zero = ğŸ™ _ := rfl

@[simp]
lemma parallelPairOpIso_inv_app_one {X Y : C} (f g : X âŸ¶ Y) :
  (parallelPairOpIso f g).inv.app WalkingParallelPair.one = ğŸ™ _ := rfl

/-- The canonical isomorphism relating `(parallelPair f g).op` and `parallelPair f.op g.op` -/
def opParallelPairIso {X Y : C} (f g : X âŸ¶ Y) :
    (parallelPair f g).op â‰… walkingParallelPairOpEquiv.inverse â‹™ parallelPair f.op g.op :=
  calc
    (parallelPair f g).op â‰… ğŸ­ _ â‹™ (parallelPair f g).op := .refl _
    _ â‰… (walkingParallelPairOpEquiv.inverse â‹™ walkingParallelPairOpEquiv.functor) â‹™ _ :=
      isoWhiskerRight walkingParallelPairOpEquiv.symm.unitIso _
    _ â‰… walkingParallelPairOpEquiv.inverse â‹™ walkingParallelPairOpEquiv.functor â‹™ _ :=
      Functor.associator _ _ _
    _ â‰… walkingParallelPairOpEquiv.inverse â‹™ parallelPair f.op g.op :=
      isoWhiskerLeft _ (parallelPairOpIso f g).symm

@[simp]
lemma opParallelPairIso_hom_app_zero {X Y : C} (f g : X âŸ¶ Y) :
  (opParallelPairIso f g).hom.app (op WalkingParallelPair.zero) = ğŸ™ _ := by simp [opParallelPairIso]

@[simp]
lemma opParallelPairIso_hom_app_one {X Y : C} (f g : X âŸ¶ Y) :
  (opParallelPairIso f g).hom.app (op WalkingParallelPair.one) = ğŸ™ _ := by simp [opParallelPairIso]

@[simp]
lemma opParallelPairIso_inv_app_zero {X Y : C} (f g : X âŸ¶ Y) :
  (opParallelPairIso f g).inv.app (op WalkingParallelPair.zero) = ğŸ™ _ := by simp [opParallelPairIso]

@[simp]
lemma opParallelPairIso_inv_app_one {X Y : C} (f g : X âŸ¶ Y) :
  (opParallelPairIso f g).inv.app (op WalkingParallelPair.one) = ğŸ™ _ := by simp [opParallelPairIso]

namespace Cofork

/-- The obvious map `Cofork f g â†’ Fork f.unop g.unop` -/
def unop {X Y : Cáµ’áµ–} {f g : X âŸ¶ Y} (c : Cofork f g) : Fork f.unop g.unop :=
   Cocone.unop ((Cocones.precompose (opParallelPairIso f.unop g.unop).hom).obj
      (Cocone.whisker walkingParallelPairOpEquiv.inverse c))

lemma unop_Ï€_app_one {X Y : Cáµ’áµ–} {f g : X âŸ¶ Y} (c : Cofork f g) :
    c.unop.Ï€.app .one = Quiver.Hom.unop (c.Î¹.app .zero) := by
  simp [unop]

lemma unop_Ï€_app_zero {X Y : Cáµ’áµ–} {f g : X âŸ¶ Y} (c : Cofork f g) :
    c.unop.Ï€.app .zero = Quiver.Hom.unop (c.Î¹.app .one) := by
  simp [unop]

theorem unop_Î¹ {X Y : Cáµ’áµ–} {f g : X âŸ¶ Y} (c : Cofork f g) :
    c.unop.Î¹ = c.Ï€.unop := by simp [Cofork.unop, Fork.Î¹]

/-- The obvious map `Cofork f g â†’ Fork f.op g.op` -/
def op {X Y : C} {f g : X âŸ¶ Y} (c : Cofork f g) : Fork f.op g.op :=
  (Cones.postcompose (parallelPairOpIso f g).symm.hom).obj
    (Cone.whisker walkingParallelPairOpEquiv.functor (Cocone.op c))

lemma op_Ï€_app_one {X Y : C} {f g : X âŸ¶ Y} (c : Cofork f g) :
    c.op.Ï€.app .one = Quiver.Hom.op (c.Î¹.app .zero) := by
  simp [op]

lemma op_Ï€_app_zero {X Y : C} {f g : X âŸ¶ Y} (c : Cofork f g) :
    c.op.Ï€.app .zero = Quiver.Hom.op (c.Î¹.app .one) := by
  simp [op]

theorem op_Î¹ {X Y : C} {f g : X âŸ¶ Y} (c : Cofork f g) :
    c.op.Î¹ = c.Ï€.op := by simp [Cofork.op, Fork.Î¹]

end Cofork

namespace Fork

/-- The obvious map `Fork f g â†’ Cofork f.unop g.unop` -/
def unop {X Y : Cáµ’áµ–} {f g : X âŸ¶ Y} (c : Fork f g) : Cofork f.unop g.unop :=
  Cone.unop ((Cones.postcompose (opParallelPairIso f.unop g.unop).symm.hom).obj
    (Cone.whisker walkingParallelPairOpEquiv.inverse c))

lemma unop_Î¹_app_one {X Y : Cáµ’áµ–} {f g : X âŸ¶ Y} (c : Fork f g) :
    c.unop.Î¹.app .one = Quiver.Hom.unop (c.Ï€.app .zero) := by
  simp [unop]

lemma unop_Î¹_app_zero {X Y : Cáµ’áµ–} {f g : X âŸ¶ Y} (c : Fork f g) :
    c.unop.Î¹.app .zero = Quiver.Hom.unop (c.Ï€.app .one) := by
  simp [unop]

theorem unop_Ï€ {X Y : Cáµ’áµ–} {f g : X âŸ¶ Y} (c : Fork f g) :
    c.unop.Ï€ = c.Î¹.unop := by simp [Fork.unop, Cofork.Ï€]

/-- The obvious map `Fork f g â†’ Cofork f.op g.op` -/
@[simps!]
def op {X Y : C} {f g : X âŸ¶ Y} (c : Fork f g) : Cofork f.op g.op :=
  (Cocones.precompose (parallelPairOpIso f g).hom).obj
    (Cocone.whisker walkingParallelPairOpEquiv.functor (Cone.op c))

lemma op_Î¹_app_one {X Y : C} {f g : X âŸ¶ Y} (c : Fork f g) :
    c.op.Î¹.app .one = Quiver.Hom.op (c.Ï€.app .zero) := by
  simp [op]

lemma op_Î¹_app_zero {X Y : C} {f g : X âŸ¶ Y} (c : Fork f g) :
    c.op.Î¹.app .zero = Quiver.Hom.op (c.Ï€.app .one) := by
  simp [op]

theorem op_Ï€ {X Y : C} {f g : X âŸ¶ Y} (c : Fork f g) :
    c.op.Ï€ = c.Î¹.op := by simp [Fork.op, Cofork.Ï€]

end Fork

namespace Cofork

theorem op_unop_Ï€ {X Y : C} {f g : X âŸ¶ Y} (c : Cofork f g) : c.op.unop.Ï€ = c.Ï€ := by
  simp [Fork.unop_Ï€, Cofork.op_Î¹]

theorem unop_op_Ï€ {X Y : Cáµ’áµ–} {f g : X âŸ¶ Y} (c : Cofork f g) : c.unop.op.Ï€ = c.Ï€ := by
  simp [Fork.op_Ï€, Cofork.unop_Î¹]

/-- If `c` is a cofork, then `c.op.unop` is isomorphic to `c`. -/
def opUnopIso {X Y : C} {f g : X âŸ¶ Y} (c : Cofork f g) : c.op.unop â‰… c :=
  Cofork.ext (Iso.refl _) (by simp [op_unop_Ï€])

/-- If `c` is a cofork in `Cáµ’áµ–`, then `c.unop.op` is isomorphic to `c`. -/
def unopOpIso {X Y : Cáµ’áµ–} {f g : X âŸ¶ Y} (c : Cofork f g) : c.unop.op â‰… c :=
  Cofork.ext (Iso.refl _) (by simp [unop_op_Ï€])

end Cofork

namespace Fork

theorem op_unop_Î¹ {X Y : C} {f g : X âŸ¶ Y} (c : Fork f g) : c.op.unop.Î¹ = c.Î¹ := by
  simp [Cofork.unop_Î¹, Fork.op_Ï€]

theorem unop_op_Î¹ {X Y : Cáµ’áµ–} {f g : X âŸ¶ Y} (c : Fork f g) : c.unop.op.Î¹ = c.Î¹ := by
  simp [Fork.unop_Ï€, Cofork.op_Î¹]

/-- If `c` is a fork, then `c.op.unop` is isomorphic to `c`. -/
def opUnopIso {X Y : C} {f g : X âŸ¶ Y} (c : Fork f g) : c.op.unop â‰… c :=
  Fork.ext (Iso.refl _) (by simp [op_unop_Î¹])

/-- If `c` is a fork in `Cáµ’áµ–`, then `c.unop.op` is isomorphic to `c`. -/
def unopOpIso {X Y : Cáµ’áµ–} {f g : X âŸ¶ Y} (c : Fork f g) : c.unop.op â‰… c :=
  Fork.ext (Iso.refl _) (by simp [unop_op_Î¹])

end Fork

namespace Cofork

/-- A cofork is a colimit cocone if and only if the corresponding fork in the opposite category is
a limit cone. -/
noncomputable -- just for performance; compilation takes several seconds
def isColimitEquivIsLimitOp {X Y : C} {f g : X âŸ¶ Y} (c : Cofork f g) :
    IsColimit c â‰ƒ IsLimit c.op := by
  apply equivOfSubsingletonOfSubsingleton
  Â· intro h
    exact (IsLimit.postcomposeHomEquiv _ _).invFun ((IsLimit.whiskerEquivalenceEquiv _).toFun h.op)
  Â· intro h
    exact (IsColimit.equivIsoColimit c.opUnopIso).toFun (((IsLimit.postcomposeHomEquiv _ _).invFun
      ((IsLimit.whiskerEquivalenceEquiv walkingParallelPairOpEquiv.symm).toFun h)).unop)

/-- A cofork is a colimit cocone in `Cáµ’áµ–` if and only if the corresponding fork in `C` is
a limit cone. -/
noncomputable -- just for performance; compilation takes several seconds
def isColimitEquivIsLimitUnop {X Y : Cáµ’áµ–} {f g : X âŸ¶ Y} (c : Cofork f g) :
    IsColimit c â‰ƒ IsLimit c.unop := by
  apply equivOfSubsingletonOfSubsingleton
  Â· intro h
    exact ((IsColimit.precomposeHomEquiv _ _).invFun
      ((IsColimit.whiskerEquivalenceEquiv walkingParallelPairOpEquiv.symm).toFun h)).unop
  Â· intro h
    exact (IsColimit.equivIsoColimit c.unopOpIso).toFun
      ((IsColimit.precomposeHomEquiv _ _).invFun ((IsColimit.whiskerEquivalenceEquiv _).toFun h.op))

/-- The canonical isomorphism between `(Cofork.ofÏ€ Ï€ w).op` and `Fork.ofÎ¹ Ï€.op w'`. -/
def ofÏ€OpIsoOfÎ¹ {X Y P : C} {f g : X âŸ¶ Y} (Ï€ Ï€' : Y âŸ¶ P) (w : f â‰« Ï€ = g â‰« Ï€)
    (w' : Ï€'.op â‰« f.op = Ï€'.op â‰« g.op) (h : Ï€ = Ï€') :
    (Cofork.ofÏ€ Ï€ w).op â‰… Fork.ofÎ¹ Ï€'.op w' :=
  Fork.ext (Iso.refl _) (by simp [Cofork.op_Î¹, h])

/-- The canonical isomorphism between `(Cofork.ofÏ€ Ï€ w).unop` and `Fork.ofÎ¹ Ï€.unop w'`. -/
def ofÏ€UnopIsoOfÎ¹ {X Y P : Cáµ’áµ–} {f g : X âŸ¶ Y} (Ï€ Ï€' : Y âŸ¶ P) (w : f â‰« Ï€ = g â‰« Ï€)
    (w' : Ï€'.unop â‰« f.unop = Ï€'.unop â‰« g.unop) (h : Ï€ = Ï€') :
    (Cofork.ofÏ€ Ï€ w).unop â‰… Fork.ofÎ¹ Ï€'.unop w' :=
  Fork.ext (Iso.refl _) (by simp [Cofork.unop_Î¹, h])

/-- `Cofork.ofÏ€ Ï€ w` is a colimit cocone if and only if `Fork.ofÎ¹ Ï€.op w'` in the opposite
category is a limit cone. -/
def isColimitOfÏ€EquivIsLimitOp {X Y P : C} {f g : X âŸ¶ Y} (Ï€ Ï€' : Y âŸ¶ P) (w : f â‰« Ï€ = g â‰« Ï€)
    (w' : Ï€'.op â‰« f.op = Ï€'.op â‰« g.op) (h : Ï€ = Ï€') :
    IsColimit (Cofork.ofÏ€ Ï€ w) â‰ƒ IsLimit (Fork.ofÎ¹ Ï€'.op w') :=
  (Cofork.ofÏ€ Ï€ w).isColimitEquivIsLimitOp.trans (IsLimit.equivIsoLimit (ofÏ€OpIsoOfÎ¹ Ï€ Ï€' w w' h))

/-- `Cofork.ofÏ€ Ï€ w` is a colimit cocone in `Cáµ’áµ–` if and only if `Fork.ofÎ¹ Ï€'.unop w'` in `C` is
a limit cone. -/
def isColimitOfÏ€EquivIsLimitUnop {X Y P : Cáµ’áµ–} {f g : X âŸ¶ Y} (Ï€ Ï€' : Y âŸ¶ P) (w : f â‰« Ï€ = g â‰« Ï€)
    (w' : Ï€'.unop â‰« f.unop = Ï€'.unop â‰« g.unop) (h : Ï€ = Ï€') :
    IsColimit (Cofork.ofÏ€ Ï€ w) â‰ƒ IsLimit (Fork.ofÎ¹ Ï€'.unop w') :=
  (Cofork.ofÏ€ Ï€ w).isColimitEquivIsLimitUnop.trans
    (IsLimit.equivIsoLimit (ofÏ€UnopIsoOfÎ¹ Ï€ Ï€' w w' h))

end Cofork

namespace Fork

/-- A fork is a limit cone if and only if the corresponding cofork in the opposite category is
a colimit cocone. -/
def isLimitEquivIsColimitOp {X Y : C} {f g : X âŸ¶ Y} (c : Fork f g) :
    IsLimit c â‰ƒ IsColimit c.op :=
  (IsLimit.equivIsoLimit c.opUnopIso).symm.trans c.op.isColimitEquivIsLimitUnop.symm

/-- A fork is a limit cone in `Cáµ’áµ–` if and only if the corresponding cofork in `C` is
a colimit cocone. -/
def isLimitEquivIsColimitUnop {X Y : Cáµ’áµ–} {f g : X âŸ¶ Y} (c : Fork f g) :
    IsLimit c â‰ƒ IsColimit c.unop :=
  (IsLimit.equivIsoLimit c.unopOpIso).symm.trans c.unop.isColimitEquivIsLimitOp.symm

/-- The canonical isomorphism between `(Fork.ofÎ¹ Î¹ w).op` and `Cofork.ofÏ€ Î¹.op w'`. -/
def ofÎ¹OpIsoOfÏ€ {X Y P : C} {f g : X âŸ¶ Y} (Î¹ Î¹' : P âŸ¶ X) (w : Î¹ â‰« f = Î¹ â‰« g)
    (w' : f.op â‰« Î¹'.op = g.op â‰« Î¹'.op) (h : Î¹ = Î¹') :
    (Fork.ofÎ¹ Î¹ w).op â‰… Cofork.ofÏ€ Î¹'.op w' :=
  Cofork.ext (Iso.refl _) (by simp [Fork.op_Ï€, h])

/-- The canonical isomorphism between `(Fork.ofÎ¹ Î¹ w).unop` and `Cofork.ofÏ€ Î¹.unop w.unop`. -/
def ofÎ¹UnopIsoOfÏ€ {X Y P : Cáµ’áµ–} {f g : X âŸ¶ Y} (Î¹ Î¹' : P âŸ¶ X) (w : Î¹ â‰« f = Î¹ â‰« g)
    (w' : f.unop â‰« Î¹'.unop = g.unop â‰« Î¹'.unop) (h : Î¹ = Î¹') :
    (Fork.ofÎ¹ Î¹ w).unop â‰… Cofork.ofÏ€ Î¹'.unop w' :=
  Cofork.ext (Iso.refl _) (by simp [Fork.unop_Ï€, h])

/-- `Fork.ofÎ¹ Î¹ w` is a limit cone if and only if `Cofork.ofÏ€ Î¹'.op w'` in the opposite
category is a colimit cocone. -/
def isLimitOfÎ¹EquivIsColimitOp {X Y P : C} {f g : X âŸ¶ Y} (Î¹ Î¹' : P âŸ¶ X) (w : Î¹ â‰« f = Î¹ â‰« g)
    (w' : f.op â‰« Î¹'.op = g.op â‰« Î¹'.op) (h : Î¹ = Î¹') :
    IsLimit (Fork.ofÎ¹ Î¹ w) â‰ƒ IsColimit (Cofork.ofÏ€ Î¹'.op w') :=
  (Fork.ofÎ¹ Î¹ w).isLimitEquivIsColimitOp.trans (IsColimit.equivIsoColimit (ofÎ¹OpIsoOfÏ€ Î¹ Î¹' w w' h))

/-- `Fork.ofÎ¹ Î¹ w` is a limit cone in `Cáµ’áµ–` if and only if `Cofork.ofÏ€ Î¹.unop w.unop` in `C` is
a colimit cocone. -/
def isLimitOfÎ¹EquivIsColimitUnop {X Y P : Cáµ’áµ–} {f g : X âŸ¶ Y} (Î¹ Î¹' : P âŸ¶ X) (w : Î¹ â‰« f = Î¹ â‰« g)
    (w' : f.unop â‰« Î¹'.unop = g.unop â‰« Î¹'.unop) (h : Î¹ = Î¹') :
    IsLimit (Fork.ofÎ¹ Î¹ w) â‰ƒ IsColimit (Cofork.ofÏ€ Î¹'.unop w') :=
  (Fork.ofÎ¹ Î¹ w).isLimitEquivIsColimitUnop.trans
    (IsColimit.equivIsoColimit (ofÎ¹UnopIsoOfÏ€ Î¹ Î¹' w w' h))

end Fork

namespace Cofork

/-- `Cofork.ofÏ€ f pullback.condition` is a colimit cocone if and only if
`Fork.ofÎ¹ f.op pushout.condition` in the opposite category is a limit cone. -/
def isColimitCoforkPushoutEquivIsColimitForkOpPullback
    {X Y : C} {f : X âŸ¶ Y} [HasPullback f f] [HasPushout f.op f.op] :
    IsColimit (Cofork.ofÏ€ f pullback.condition) â‰ƒ IsLimit (Fork.ofÎ¹ f.op pushout.condition) where
  toFun h := Fork.isLimitOfIsos _ (Cofork.isColimitOfÏ€EquivIsLimitOp f f
    pullback.condition (by simp only [â† op_comp, pullback.condition]) rfl h) _ (.refl _)
      (pullbackIsoUnopPushout f f).op.symm (.refl _)
        (by simp [â† op_comp]) (by simp [â† op_comp]) (by simp)
  invFun h := Cofork.isColimitOfIsos _ (Fork.isLimitOfÎ¹EquivIsColimitUnop f.op f.op
    pushout.condition (by rw [â† unop_comp, â† unop_comp, pushout.condition]) rfl h) _
      (pullbackIsoUnopPushout f f).symm ( .refl _) (.refl _) (by simp) (by simp) (by simp)
  left_inv := by cat_disch
  right_inv := by cat_disch

/-- `Cofork.ofÏ€ f pullback.condition` is a colimit cocone in `Cáµ’áµ–` if and only if
`Fork.ofÎ¹ f.unop pushout.condition` in `C` is a limit cone. -/
def isColimitCoforkPushoutEquivIsColimitForkUnopPullback
    {X Y : Cáµ’áµ–} {f : X âŸ¶ Y} [HasPullback f f] [HasPushout f.unop f.unop] :
    IsColimit (Cofork.ofÏ€ f pullback.condition) â‰ƒ IsLimit (Fork.ofÎ¹ f.unop pushout.condition) where
  toFun h := Fork.isLimitOfIsos _ (Cofork.isColimitOfÏ€EquivIsLimitUnop f f pullback.condition
    (by simp only [â† unop_comp, pullback.condition]) rfl h) _ (.refl _)
      (pullbackIsoOpPushout f f).unop.symm (.refl _)
        (by simp [â† unop_comp]) (by simp [â† unop_comp]) (by simp)
  invFun h :=
    Cofork.isColimitOfIsos _ (Fork.isLimitOfÎ¹EquivIsColimitOp f.unop f.unop pushout.condition
      (by rw [â† op_comp, â† op_comp, pushout.condition]) rfl h) _
        (pullbackIsoOpPushout f f).symm ( .refl _) (.refl _) (by simp) (by simp) (by simp)
  left_inv := by cat_disch
  right_inv := by cat_disch

end Cofork

namespace Fork

/-- `Fork.ofÎ¹ f pushout.condition` is a limit cone if and only if
`Cofork.ofÏ€ f.op pullback.condition` in the opposite category is a colimit cocone. -/
def isLimitForkPushoutEquivIsColimitForkOpPullback
    {X Y : C} {f : X âŸ¶ Y} [HasPushout f f] [HasPullback f.op f.op] :
    IsLimit (Fork.ofÎ¹ f pushout.condition) â‰ƒ IsColimit (Cofork.ofÏ€ f.op pullback.condition) where
  toFun h := Cofork.isColimitOfIsos _ (Fork.isLimitOfÎ¹EquivIsColimitOp f f
    pushout.condition (by simp only [â† op_comp, pushout.condition]) rfl h) _
      ((pushoutIsoUnopPullback f f).op.symm â‰ªâ‰« eqToIso rfl) (.refl _) (.refl _)
        (by simp) (by simp) (by simp)
  invFun h := by
    refine Fork.isLimitOfIsos _ (Cofork.isColimitOfÏ€EquivIsLimitUnop f.op f.op
      pullback.condition (by simp only [â† unop_comp, pullback.condition]) rfl h) _ (.refl _)
        ((pushoutIsoUnopPullback f f).symm) (.refl _) ?_ ?_ (by simp)
    Â· rw [Iso.symm_hom, â† Quiver.Hom.unop_op (pushoutIsoUnopPullback f f).inv, â† unop_comp,
        pushoutIsoUnopPullback_inv_fst, Quiver.Hom.unop_op, Iso.refl_hom, Category.id_comp]
    Â· rw [Iso.symm_hom, â† Quiver.Hom.unop_op (pushoutIsoUnopPullback f f).inv, â† unop_comp,
        pushoutIsoUnopPullback_inv_snd, Quiver.Hom.unop_op, Iso.refl_hom, Category.id_comp]
  left_inv := by cat_disch
  right_inv := by cat_disch

/-- `Fork.ofÎ¹ f pushout.condition` is a limit cone in `Cáµ’áµ–` if and only if
`Cofork.ofÏ€ f.op pullback.condition` in `C` is a colimit cocone. -/
def isLimitForkPushoutEquivIsColimitForkUnopPullback
    {X Y : Cáµ’áµ–} {f : X âŸ¶ Y} [HasPushout f f] [HasPullback f.unop f.unop] :
    IsLimit (Fork.ofÎ¹ f pushout.condition) â‰ƒ IsColimit (Cofork.ofÏ€ f.unop pullback.condition) where
  toFun h := Cofork.isColimitOfIsos _ (Fork.isLimitOfÎ¹EquivIsColimitUnop f f pushout.condition
    (by simp only [â† unop_comp, pushout.condition]) rfl h) _
      ((pushoutIsoOpPullback f f).unop.symm â‰ªâ‰« eqToIso rfl) (.refl _) (.refl _)
        (by simp) (by simp) (by simp)
  invFun h := by
    refine Fork.isLimitOfIsos _ (Cofork.isColimitOfÏ€EquivIsLimitOp f.unop f.unop pullback.condition
      (by simp only [â† op_comp, pullback.condition]) rfl h) _ (.refl _)
        ((pushoutIsoOpPullback f f).symm) (.refl _) ?_ ?_ (by simp)
    Â· rw [Iso.symm_hom, â† Quiver.Hom.op_unop (pushoutIsoOpPullback f f).inv, â† op_comp,
        pushoutIsoOpPullback_inv_fst, Quiver.Hom.op_unop, Iso.refl_hom, Category.id_comp]
    Â· rw [Iso.symm_hom, â† Quiver.Hom.op_unop (pushoutIsoOpPullback f f).inv, â† op_comp,
        pushoutIsoOpPullback_inv_snd, Quiver.Hom.op_unop, Iso.refl_hom, Category.id_comp]
  left_inv := by cat_disch
  right_inv := by cat_disch

end Fork

end CategoryTheory.Limits
