/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Bhavik Mehta
-/
import Mathlib.CategoryTheory.PEmpty
import Mathlib.CategoryTheory.Limits.HasLimits
import Mathlib.CategoryTheory.EpiMono
import Mathlib.CategoryTheory.Category.Preorder

#align_import category_theory.limits.shapes.terminal from "leanprover-community/mathlib"@"239d882c4fb58361ee8b3b39fb2091320edef10a"

/-!
# Initial and terminal objects in a category.

## References
* [Stacks: Initial and final objects](https://stacks.math.columbia.edu/tag/002B)
-/


noncomputable section

universe w w' v v‚ÇÅ v‚ÇÇ u u‚ÇÅ u‚ÇÇ

open CategoryTheory

namespace CategoryTheory.Limits

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]

/-- Construct a cone for the empty diagram given an object. -/
@[simps]
def asEmptyCone (X : C) : Cone (Functor.empty.{0} C) :=
  { pt := X
    œÄ :=
    { app := by aesop_cat } }
#align category_theory.limits.as_empty_cone CategoryTheory.Limits.asEmptyCone

/-- Construct a cocone for the empty diagram given an object. -/
@[simps]
def asEmptyCocone (X : C) : Cocone (Functor.empty.{0} C) :=
  { pt := X
    Œπ :=
    { app := by aesop_cat } }
#align category_theory.limits.as_empty_cocone CategoryTheory.Limits.asEmptyCocone

/-- `X` is terminal if the cone it induces on the empty diagram is limiting. -/
abbrev IsTerminal (X : C) :=
  IsLimit (asEmptyCone X)
#align category_theory.limits.is_terminal CategoryTheory.Limits.IsTerminal

/-- `X` is initial if the cocone it induces on the empty diagram is colimiting. -/
abbrev IsInitial (X : C) :=
  IsColimit (asEmptyCocone X)
#align category_theory.limits.is_initial CategoryTheory.Limits.IsInitial

/-- An object `Y` is terminal iff for every `X` there is a unique morphism `X ‚ü∂ Y`. -/
def isTerminalEquivUnique (F : Discrete.{0} PEmpty.{1} ‚•§ C) (Y : C) :
    IsLimit (‚ü®Y, by aesop_cat, by aesop_cat‚ü© : Cone F) ‚âÉ ‚àÄ X : C, Unique (X ‚ü∂ Y) where
  toFun t X :=
    { default := t.lift ‚ü®X, ‚ü®by aesop_cat, by aesop_cat‚ü©‚ü©
      uniq := fun f =>
        t.uniq ‚ü®X, ‚ü®by aesop_cat, by aesop_cat‚ü©‚ü© f (by aesop_cat) }
  invFun u :=
    { lift := fun s => (u s.pt).default
      uniq := fun s _ _ => (u s.pt).2 _ }
  left_inv := by dsimp [Function.LeftInverse]; intro x; simp only [eq_iff_true_of_subsingleton]
  right_inv := by
    dsimp [Function.RightInverse,Function.LeftInverse]
    intro u; funext X; simp only
#align category_theory.limits.is_terminal_equiv_unique CategoryTheory.Limits.isTerminalEquivUnique

/-- An object `Y` is terminal if for every `X` there is a unique morphism `X ‚ü∂ Y`
    (as an instance). -/
def IsTerminal.ofUnique (Y : C) [h : ‚àÄ X : C, Unique (X ‚ü∂ Y)] : IsTerminal Y where
  lift s := (h s.pt).default
  fac := fun _ ‚ü®j‚ü© => j.elim
#align category_theory.limits.is_terminal.of_unique CategoryTheory.Limits.IsTerminal.ofUnique

/-- An object `Y` is terminal if for every `X` there is a unique morphism `X ‚ü∂ Y`
    (as explicit arguments). -/
def IsTerminal.ofUniqueHom {Y : C} (h : ‚àÄ X : C, X ‚ü∂ Y) (uniq : ‚àÄ (X : C) (m : X ‚ü∂ Y), m = h X) :
    IsTerminal Y :=
  have : ‚àÄ X : C, Unique (X ‚ü∂ Y) := fun X ‚Ü¶ ‚ü®‚ü®h X‚ü©, uniq X‚ü©
  IsTerminal.ofUnique Y

/-- If `Œ±` is a preorder with top, then `‚ä§` is a terminal object. -/
def isTerminalTop {Œ± : Type*} [Preorder Œ±] [OrderTop Œ±] : IsTerminal (‚ä§ : Œ±) :=
  IsTerminal.ofUnique _
#align category_theory.limits.is_terminal_top CategoryTheory.Limits.isTerminalTop

/-- Transport a term of type `IsTerminal` across an isomorphism. -/
def IsTerminal.ofIso {Y Z : C} (hY : IsTerminal Y) (i : Y ‚âÖ Z) : IsTerminal Z :=
  IsLimit.ofIsoLimit hY
    { hom := { hom := i.hom }
      inv := { hom := i.inv } }
#align category_theory.limits.is_terminal.of_iso CategoryTheory.Limits.IsTerminal.ofIso

/-- If `X` and `Y` are isomorphic, then `X` is terminal iff `Y` is. -/
def IsTerminal.equivOfIso {X Y : C} (e : X ‚âÖ Y) :
    IsTerminal X ‚âÉ IsTerminal Y where
  toFun h := IsTerminal.ofIso h e
  invFun h := IsTerminal.ofIso h e.symm
  left_inv _ := Subsingleton.elim _ _
  right_inv _ := Subsingleton.elim _ _

/-- An object `X` is initial iff for every `Y` there is a unique morphism `X ‚ü∂ Y`. -/
def isInitialEquivUnique (F : Discrete.{0} PEmpty.{1} ‚•§ C) (X : C) :
    IsColimit (‚ü®X, ‚ü®by aesop_cat, by aesop_cat‚ü©‚ü© : Cocone F) ‚âÉ ‚àÄ Y : C, Unique (X ‚ü∂ Y) where
  toFun t X :=
    { default := t.desc ‚ü®X, ‚ü®by aesop_cat, by aesop_cat‚ü©‚ü©
      uniq := fun f => t.uniq ‚ü®X, ‚ü®by aesop_cat, by aesop_cat‚ü©‚ü© f (by aesop_cat) }
  invFun u :=
    { desc := fun s => (u s.pt).default
      uniq := fun s _ _ => (u s.pt).2 _ }
  left_inv := by dsimp [Function.LeftInverse]; intro; simp only [eq_iff_true_of_subsingleton]
  right_inv := by
    dsimp [Function.RightInverse,Function.LeftInverse]
    intro; funext; simp only
#align category_theory.limits.is_initial_equiv_unique CategoryTheory.Limits.isInitialEquivUnique

/-- An object `X` is initial if for every `Y` there is a unique morphism `X ‚ü∂ Y`
    (as an instance). -/
def IsInitial.ofUnique (X : C) [h : ‚àÄ Y : C, Unique (X ‚ü∂ Y)] : IsInitial X where
  desc s := (h s.pt).default
  fac := fun _ ‚ü®j‚ü© => j.elim
#align category_theory.limits.is_initial.of_unique CategoryTheory.Limits.IsInitial.ofUnique

/-- An object `X` is initial if for every `Y` there is a unique morphism `X ‚ü∂ Y`
    (as explicit arguments). -/
def IsInitial.ofUniqueHom {X : C} (h : ‚àÄ Y : C, X ‚ü∂ Y) (uniq : ‚àÄ (Y : C) (m : X ‚ü∂ Y), m = h Y) :
    IsInitial X :=
  have : ‚àÄ Y : C, Unique (X ‚ü∂ Y) := fun Y ‚Ü¶ ‚ü®‚ü®h Y‚ü©, uniq Y‚ü©
  IsInitial.ofUnique X

/-- If `Œ±` is a preorder with bot, then `‚ä•` is an initial object. -/
def isInitialBot {Œ± : Type*} [Preorder Œ±] [OrderBot Œ±] : IsInitial (‚ä• : Œ±) :=
  IsInitial.ofUnique _
#align category_theory.limits.is_initial_bot CategoryTheory.Limits.isInitialBot

/-- Transport a term of type `is_initial` across an isomorphism. -/
def IsInitial.ofIso {X Y : C} (hX : IsInitial X) (i : X ‚âÖ Y) : IsInitial Y :=
  IsColimit.ofIsoColimit hX
    { hom := { hom := i.hom }
      inv := { hom := i.inv } }
#align category_theory.limits.is_initial.of_iso CategoryTheory.Limits.IsInitial.ofIso

/-- If `X` and `Y` are isomorphic, then `X` is initial iff `Y` is. -/
def IsInitial.equivOfIso {X Y : C} (e : X ‚âÖ Y) :
    IsInitial X ‚âÉ IsInitial Y where
  toFun h := IsInitial.ofIso h e
  invFun h := IsInitial.ofIso h e.symm
  left_inv _ := Subsingleton.elim _ _
  right_inv _ := Subsingleton.elim _ _

/-- Give the morphism to a terminal object from any other. -/
def IsTerminal.from {X : C} (t : IsTerminal X) (Y : C) : Y ‚ü∂ X :=
  t.lift (asEmptyCone Y)
#align category_theory.limits.is_terminal.from CategoryTheory.Limits.IsTerminal.from

/-- Any two morphisms to a terminal object are equal. -/
theorem IsTerminal.hom_ext {X Y : C} (t : IsTerminal X) (f g : Y ‚ü∂ X) : f = g :=
  IsLimit.hom_ext t (by aesop_cat)
#align category_theory.limits.is_terminal.hom_ext CategoryTheory.Limits.IsTerminal.hom_ext

@[simp]
theorem IsTerminal.comp_from {Z : C} (t : IsTerminal Z) {X Y : C} (f : X ‚ü∂ Y) :
    f ‚â´ t.from Y = t.from X :=
  t.hom_ext _ _
#align category_theory.limits.is_terminal.comp_from CategoryTheory.Limits.IsTerminal.comp_from

@[simp]
theorem IsTerminal.from_self {X : C} (t : IsTerminal X) : t.from X = ùüô X :=
  t.hom_ext _ _
#align category_theory.limits.is_terminal.from_self CategoryTheory.Limits.IsTerminal.from_self

/-- Give the morphism from an initial object to any other. -/
def IsInitial.to {X : C} (t : IsInitial X) (Y : C) : X ‚ü∂ Y :=
  t.desc (asEmptyCocone Y)
#align category_theory.limits.is_initial.to CategoryTheory.Limits.IsInitial.to

/-- Any two morphisms from an initial object are equal. -/
theorem IsInitial.hom_ext {X Y : C} (t : IsInitial X) (f g : X ‚ü∂ Y) : f = g :=
  IsColimit.hom_ext t (by aesop_cat)
#align category_theory.limits.is_initial.hom_ext CategoryTheory.Limits.IsInitial.hom_ext

@[simp]
theorem IsInitial.to_comp {X : C} (t : IsInitial X) {Y Z : C} (f : Y ‚ü∂ Z) : t.to Y ‚â´ f = t.to Z :=
  t.hom_ext _ _
#align category_theory.limits.is_initial.to_comp CategoryTheory.Limits.IsInitial.to_comp

@[simp]
theorem IsInitial.to_self {X : C} (t : IsInitial X) : t.to X = ùüô X :=
  t.hom_ext _ _
#align category_theory.limits.is_initial.to_self CategoryTheory.Limits.IsInitial.to_self

/-- Any morphism from a terminal object is split mono. -/
theorem IsTerminal.isSplitMono_from {X Y : C} (t : IsTerminal X) (f : X ‚ü∂ Y) : IsSplitMono f :=
  IsSplitMono.mk' ‚ü®t.from _, t.hom_ext _ _‚ü©
#align category_theory.limits.is_terminal.is_split_mono_from CategoryTheory.Limits.IsTerminal.isSplitMono_from

/-- Any morphism to an initial object is split epi. -/
theorem IsInitial.isSplitEpi_to {X Y : C} (t : IsInitial X) (f : Y ‚ü∂ X) : IsSplitEpi f :=
  IsSplitEpi.mk' ‚ü®t.to _, t.hom_ext _ _‚ü©
#align category_theory.limits.is_initial.is_split_epi_to CategoryTheory.Limits.IsInitial.isSplitEpi_to

/-- Any morphism from a terminal object is mono. -/
theorem IsTerminal.mono_from {X Y : C} (t : IsTerminal X) (f : X ‚ü∂ Y) : Mono f := by
  haveI := t.isSplitMono_from f; infer_instance
#align category_theory.limits.is_terminal.mono_from CategoryTheory.Limits.IsTerminal.mono_from

/-- Any morphism to an initial object is epi. -/
theorem IsInitial.epi_to {X Y : C} (t : IsInitial X) (f : Y ‚ü∂ X) : Epi f := by
  haveI := t.isSplitEpi_to f; infer_instance
#align category_theory.limits.is_initial.epi_to CategoryTheory.Limits.IsInitial.epi_to

/-- If `T` and `T'` are terminal, they are isomorphic. -/
@[simps]
def IsTerminal.uniqueUpToIso {T T' : C} (hT : IsTerminal T) (hT' : IsTerminal T') : T ‚âÖ T' where
  hom := hT'.from _
  inv := hT.from _
#align category_theory.limits.is_terminal.unique_up_to_iso CategoryTheory.Limits.IsTerminal.uniqueUpToIso

/-- If `I` and `I'` are initial, they are isomorphic. -/
@[simps]
def IsInitial.uniqueUpToIso {I I' : C} (hI : IsInitial I) (hI' : IsInitial I') : I ‚âÖ I' where
  hom := hI.to _
  inv := hI'.to _
#align category_theory.limits.is_initial.unique_up_to_iso CategoryTheory.Limits.IsInitial.uniqueUpToIso

variable (C)

/-- A category has a terminal object if it has a limit over the empty diagram.
Use `hasTerminal_of_unique` to construct instances.
-/
abbrev HasTerminal :=
  HasLimitsOfShape (Discrete.{0} PEmpty) C
#align category_theory.limits.has_terminal CategoryTheory.Limits.HasTerminal

/-- A category has an initial object if it has a colimit over the empty diagram.
Use `hasInitial_of_unique` to construct instances.
-/
abbrev HasInitial :=
  HasColimitsOfShape (Discrete.{0} PEmpty) C
#align category_theory.limits.has_initial CategoryTheory.Limits.HasInitial

section Univ

variable (X : C) {F‚ÇÅ : Discrete.{w} PEmpty ‚•§ C} {F‚ÇÇ : Discrete.{w'} PEmpty ‚•§ C}

/-- Being terminal is independent of the empty diagram, its universe, and the cone over it,
    as long as the cone points are isomorphic. -/
def isLimitChangeEmptyCone {c‚ÇÅ : Cone F‚ÇÅ} (hl : IsLimit c‚ÇÅ) (c‚ÇÇ : Cone F‚ÇÇ) (hi : c‚ÇÅ.pt ‚âÖ c‚ÇÇ.pt) :
    IsLimit c‚ÇÇ where
  lift c := hl.lift ‚ü®c.pt, by aesop_cat, by aesop_cat‚ü© ‚â´ hi.hom
  uniq c f _ := by
    dsimp
    rw [‚Üê hl.uniq _ (f ‚â´ hi.inv) _]
    ¬∑ simp only [Category.assoc, Iso.inv_hom_id, Category.comp_id]
    ¬∑ aesop_cat
#align category_theory.limits.is_limit_change_empty_cone CategoryTheory.Limits.isLimitChangeEmptyCone

/-- Replacing an empty cone in `IsLimit` by another with the same cone point
    is an equivalence. -/
def isLimitEmptyConeEquiv (c‚ÇÅ : Cone F‚ÇÅ) (c‚ÇÇ : Cone F‚ÇÇ) (h : c‚ÇÅ.pt ‚âÖ c‚ÇÇ.pt) :
    IsLimit c‚ÇÅ ‚âÉ IsLimit c‚ÇÇ where
  toFun hl := isLimitChangeEmptyCone C hl c‚ÇÇ h
  invFun hl := isLimitChangeEmptyCone C hl c‚ÇÅ h.symm
  left_inv := by dsimp [Function.LeftInverse]; intro; simp only [eq_iff_true_of_subsingleton]
  right_inv := by
    dsimp [Function.LeftInverse,Function.RightInverse]; intro
    simp only [eq_iff_true_of_subsingleton]
#align category_theory.limits.is_limit_empty_cone_equiv CategoryTheory.Limits.isLimitEmptyConeEquiv

theorem hasTerminalChangeDiagram (h : HasLimit F‚ÇÅ) : HasLimit F‚ÇÇ :=
  ‚ü®‚ü®‚ü®‚ü®limit F‚ÇÅ, by aesop_cat, by aesop_cat‚ü©,
    isLimitChangeEmptyCone C (limit.isLimit F‚ÇÅ) _ (eqToIso rfl)‚ü©‚ü©‚ü©
#align category_theory.limits.has_terminal_change_diagram CategoryTheory.Limits.hasTerminalChangeDiagram

theorem hasTerminalChangeUniverse [h : HasLimitsOfShape (Discrete.{w} PEmpty) C] :
    HasLimitsOfShape (Discrete.{w'} PEmpty) C where
  has_limit _ := hasTerminalChangeDiagram C (h.1 (Functor.empty C))
#align category_theory.limits.has_terminal_change_universe CategoryTheory.Limits.hasTerminalChangeUniverse

/-- Being initial is independent of the empty diagram, its universe, and the cocone over it,
    as long as the cocone points are isomorphic. -/
def isColimitChangeEmptyCocone {c‚ÇÅ : Cocone F‚ÇÅ} (hl : IsColimit c‚ÇÅ) (c‚ÇÇ : Cocone F‚ÇÇ)
    (hi : c‚ÇÅ.pt ‚âÖ c‚ÇÇ.pt) : IsColimit c‚ÇÇ where
  desc c := hi.inv ‚â´ hl.desc ‚ü®c.pt, by aesop_cat, by aesop_cat‚ü©
  uniq c f _ := by
    dsimp
    rw [‚Üê hl.uniq _ (hi.hom ‚â´ f) _]
    ¬∑ simp only [Iso.inv_hom_id_assoc]
    ¬∑ aesop_cat
#align category_theory.limits.is_colimit_change_empty_cocone CategoryTheory.Limits.isColimitChangeEmptyCocone

/-- Replacing an empty cocone in `IsColimit` by another with the same cocone point
    is an equivalence. -/
def isColimitEmptyCoconeEquiv (c‚ÇÅ : Cocone F‚ÇÅ) (c‚ÇÇ : Cocone F‚ÇÇ) (h : c‚ÇÅ.pt ‚âÖ c‚ÇÇ.pt) :
    IsColimit c‚ÇÅ ‚âÉ IsColimit c‚ÇÇ where
  toFun hl := isColimitChangeEmptyCocone C hl c‚ÇÇ h
  invFun hl := isColimitChangeEmptyCocone C hl c‚ÇÅ h.symm
  left_inv := by dsimp [Function.LeftInverse]; intro; simp only [eq_iff_true_of_subsingleton]
  right_inv := by
    dsimp [Function.LeftInverse,Function.RightInverse]; intro
    simp only [eq_iff_true_of_subsingleton]
#align category_theory.limits.is_colimit_empty_cocone_equiv CategoryTheory.Limits.isColimitEmptyCoconeEquiv

theorem hasInitialChangeDiagram (h : HasColimit F‚ÇÅ) : HasColimit F‚ÇÇ :=
  ‚ü®‚ü®‚ü®‚ü®colimit F‚ÇÅ, by aesop_cat, by aesop_cat‚ü©,
    isColimitChangeEmptyCocone C (colimit.isColimit F‚ÇÅ) _ (eqToIso rfl)‚ü©‚ü©‚ü©
#align category_theory.limits.has_initial_change_diagram CategoryTheory.Limits.hasInitialChangeDiagram

theorem hasInitialChangeUniverse [h : HasColimitsOfShape (Discrete.{w} PEmpty) C] :
    HasColimitsOfShape (Discrete.{w'} PEmpty) C where
  has_colimit _ := hasInitialChangeDiagram C (h.1 (Functor.empty C))
#align category_theory.limits.has_initial_change_universe CategoryTheory.Limits.hasInitialChangeUniverse

end Univ

/-- An arbitrary choice of terminal object, if one exists.
You can use the notation `‚ä§_ C`.
This object is characterized by having a unique morphism from any object.
-/
abbrev terminal [HasTerminal C] : C :=
  limit (Functor.empty.{0} C)
#align category_theory.limits.terminal CategoryTheory.Limits.terminal

/-- An arbitrary choice of initial object, if one exists.
You can use the notation `‚ä•_ C`.
This object is characterized by having a unique morphism to any object.
-/
abbrev initial [HasInitial C] : C :=
  colimit (Functor.empty.{0} C)
#align category_theory.limits.initial CategoryTheory.Limits.initial

/-- Notation for the terminal object in `C` -/
notation "‚ä§_ " C:20 => terminal C

/-- Notation for the initial object in `C` -/
notation "‚ä•_ " C:20 => initial C

section

variable {C}

/-- We can more explicitly show that a category has a terminal object by specifying the object,
and showing there is a unique morphism to it from any other object. -/
theorem hasTerminal_of_unique (X : C) [h : ‚àÄ Y : C, Unique (Y ‚ü∂ X)] : HasTerminal C :=
  { has_limit := fun F => HasLimit.mk ‚ü®_, (isTerminalEquivUnique F X).invFun h‚ü© }
#align category_theory.limits.has_terminal_of_unique CategoryTheory.Limits.hasTerminal_of_unique

theorem IsTerminal.hasTerminal {X : C} (h : IsTerminal X) : HasTerminal C :=
  { has_limit := fun F => HasLimit.mk ‚ü®‚ü®X, by aesop_cat, by aesop_cat‚ü©,
    isLimitChangeEmptyCone _ h _ (Iso.refl _)‚ü© }
#align category_theory.limits.is_terminal.has_terminal CategoryTheory.Limits.IsTerminal.hasTerminal

/-- We can more explicitly show that a category has an initial object by specifying the object,
and showing there is a unique morphism from it to any other object. -/
theorem hasInitial_of_unique (X : C) [h : ‚àÄ Y : C, Unique (X ‚ü∂ Y)] : HasInitial C :=
  { has_colimit := fun F => HasColimit.mk ‚ü®_, (isInitialEquivUnique F X).invFun h‚ü© }
#align category_theory.limits.has_initial_of_unique CategoryTheory.Limits.hasInitial_of_unique

theorem IsInitial.hasInitial {X : C} (h : IsInitial X) : HasInitial C where
  has_colimit F :=
    HasColimit.mk ‚ü®‚ü®X, by aesop_cat, by aesop_cat‚ü©, isColimitChangeEmptyCocone _ h _ (Iso.refl _)‚ü©
#align category_theory.limits.is_initial.has_initial CategoryTheory.Limits.IsInitial.hasInitial

/-- The map from an object to the terminal object. -/
abbrev terminal.from [HasTerminal C] (P : C) : P ‚ü∂ ‚ä§_ C :=
  limit.lift (Functor.empty C) (asEmptyCone P)
#align category_theory.limits.terminal.from CategoryTheory.Limits.terminal.from

/-- The map to an object from the initial object. -/
abbrev initial.to [HasInitial C] (P : C) : ‚ä•_ C ‚ü∂ P :=
  colimit.desc (Functor.empty C) (asEmptyCocone P)
#align category_theory.limits.initial.to CategoryTheory.Limits.initial.to

/-- A terminal object is terminal. -/
def terminalIsTerminal [HasTerminal C] : IsTerminal (‚ä§_ C) where
  lift s := terminal.from _
#align category_theory.limits.terminal_is_terminal CategoryTheory.Limits.terminalIsTerminal

/-- An initial object is initial. -/
def initialIsInitial [HasInitial C] : IsInitial (‚ä•_ C) where
  desc s := initial.to _
#align category_theory.limits.initial_is_initial CategoryTheory.Limits.initialIsInitial

instance uniqueToTerminal [HasTerminal C] (P : C) : Unique (P ‚ü∂ ‚ä§_ C) :=
  isTerminalEquivUnique _ (‚ä§_ C) terminalIsTerminal P
#align category_theory.limits.unique_to_terminal CategoryTheory.Limits.uniqueToTerminal

instance uniqueFromInitial [HasInitial C] (P : C) : Unique (‚ä•_ C ‚ü∂ P) :=
  isInitialEquivUnique _ (‚ä•_ C) initialIsInitial P
#align category_theory.limits.unique_from_initial CategoryTheory.Limits.uniqueFromInitial

@[ext] theorem terminal.hom_ext [HasTerminal C] {P : C} (f g : P ‚ü∂ ‚ä§_ C) : f = g := by ext ‚ü®‚ü®‚ü©‚ü©

@[ext] theorem initial.hom_ext [HasInitial C] {P : C} (f g : ‚ä•_ C ‚ü∂ P) : f = g := by ext ‚ü®‚ü®‚ü©‚ü©

@[simp]
theorem terminal.comp_from [HasTerminal C] {P Q : C} (f : P ‚ü∂ Q) :
    f ‚â´ terminal.from Q = terminal.from P := by
  simp [eq_iff_true_of_subsingleton]
#align category_theory.limits.terminal.comp_from CategoryTheory.Limits.terminal.comp_from

@[simp]
theorem initial.to_comp [HasInitial C] {P Q : C} (f : P ‚ü∂ Q) : initial.to P ‚â´ f = initial.to Q := by
  simp [eq_iff_true_of_subsingleton]
#align category_theory.limits.initial.to_comp CategoryTheory.Limits.initial.to_comp

/-- The (unique) isomorphism between the chosen initial object and any other initial object. -/
@[simp]
def initialIsoIsInitial [HasInitial C] {P : C} (t : IsInitial P) : ‚ä•_ C ‚âÖ P :=
  initialIsInitial.uniqueUpToIso t
#align category_theory.limits.initial_iso_is_initial CategoryTheory.Limits.initialIsoIsInitial

/-- The (unique) isomorphism between the chosen terminal object and any other terminal object. -/
@[simp]
def terminalIsoIsTerminal [HasTerminal C] {P : C} (t : IsTerminal P) : ‚ä§_ C ‚âÖ P :=
  terminalIsTerminal.uniqueUpToIso t
#align category_theory.limits.terminal_iso_is_terminal CategoryTheory.Limits.terminalIsoIsTerminal

/-- Any morphism from a terminal object is split mono. -/
instance terminal.isSplitMono_from {Y : C} [HasTerminal C] (f : ‚ä§_ C ‚ü∂ Y) : IsSplitMono f :=
  IsTerminal.isSplitMono_from terminalIsTerminal _
#align category_theory.limits.terminal.is_split_mono_from CategoryTheory.Limits.terminal.isSplitMono_from

/-- Any morphism to an initial object is split epi. -/
instance initial.isSplitEpi_to {Y : C} [HasInitial C] (f : Y ‚ü∂ ‚ä•_ C) : IsSplitEpi f :=
  IsInitial.isSplitEpi_to initialIsInitial _
#align category_theory.limits.initial.is_split_epi_to CategoryTheory.Limits.initial.isSplitEpi_to

/-- An initial object is terminal in the opposite category. -/
def terminalOpOfInitial {X : C} (t : IsInitial X) : IsTerminal (Opposite.op X) where
  lift s := (t.to s.pt.unop).op
  uniq s m _ := Quiver.Hom.unop_inj (t.hom_ext _ _)
#align category_theory.limits.terminal_op_of_initial CategoryTheory.Limits.terminalOpOfInitial

/-- An initial object in the opposite category is terminal in the original category. -/
def terminalUnopOfInitial {X : C·µí·µñ} (t : IsInitial X) : IsTerminal X.unop where
  lift s := (t.to (Opposite.op s.pt)).unop
  uniq s m _ := Quiver.Hom.op_inj (t.hom_ext _ _)
#align category_theory.limits.terminal_unop_of_initial CategoryTheory.Limits.terminalUnopOfInitial

/-- A terminal object is initial in the opposite category. -/
def initialOpOfTerminal {X : C} (t : IsTerminal X) : IsInitial (Opposite.op X) where
  desc s := (t.from s.pt.unop).op
  uniq s m _ := Quiver.Hom.unop_inj (t.hom_ext _ _)
#align category_theory.limits.initial_op_of_terminal CategoryTheory.Limits.initialOpOfTerminal

/-- A terminal object in the opposite category is initial in the original category. -/
def initialUnopOfTerminal {X : C·µí·µñ} (t : IsTerminal X) : IsInitial X.unop where
  desc s := (t.from (Opposite.op s.pt)).unop
  uniq s m _ := Quiver.Hom.op_inj (t.hom_ext _ _)
#align category_theory.limits.initial_unop_of_terminal CategoryTheory.Limits.initialUnopOfTerminal

instance hasInitial_op_of_hasTerminal [HasTerminal C] : HasInitial C·µí·µñ :=
  (initialOpOfTerminal terminalIsTerminal).hasInitial
#align category_theory.limits.has_initial_op_of_has_terminal CategoryTheory.Limits.hasInitial_op_of_hasTerminal

instance hasTerminal_op_of_hasInitial [HasInitial C] : HasTerminal C·µí·µñ :=
  (terminalOpOfInitial initialIsInitial).hasTerminal
#align category_theory.limits.has_terminal_op_of_has_initial CategoryTheory.Limits.hasTerminal_op_of_hasInitial

theorem hasTerminal_of_hasInitial_op [HasInitial C·µí·µñ] : HasTerminal C :=
  (terminalUnopOfInitial initialIsInitial).hasTerminal
#align category_theory.limits.has_terminal_of_has_initial_op CategoryTheory.Limits.hasTerminal_of_hasInitial_op

theorem hasInitial_of_hasTerminal_op [HasTerminal C·µí·µñ] : HasInitial C :=
  (initialUnopOfTerminal terminalIsTerminal).hasInitial
#align category_theory.limits.has_initial_of_has_terminal_op CategoryTheory.Limits.hasInitial_of_hasTerminal_op

instance {J : Type*} [Category J] {C : Type*} [Category C] [HasTerminal C] :
    HasLimit ((CategoryTheory.Functor.const J).obj (‚ä§_ C)) :=
  HasLimit.mk
    { cone :=
        { pt := ‚ä§_ C
          œÄ := { app := fun _ => terminal.from _ } }
      isLimit := { lift := fun s => terminal.from _ } }

/-- The limit of the constant `‚ä§_ C` functor is `‚ä§_ C`. -/
@[simps hom]
def limitConstTerminal {J : Type*} [Category J] {C : Type*} [Category C] [HasTerminal C] :
    limit ((CategoryTheory.Functor.const J).obj (‚ä§_ C)) ‚âÖ ‚ä§_ C where
  hom := terminal.from _
  inv :=
    limit.lift ((CategoryTheory.Functor.const J).obj (‚ä§_ C))
      { pt := ‚ä§_ C
        œÄ := { app := fun j => terminal.from _ } }
#align category_theory.limits.limit_const_terminal CategoryTheory.Limits.limitConstTerminal

@[reassoc (attr := simp)]
theorem limitConstTerminal_inv_œÄ {J : Type*} [Category J] {C : Type*} [Category C] [HasTerminal C]
    {j : J} :
    limitConstTerminal.inv ‚â´ limit.œÄ ((CategoryTheory.Functor.const J).obj (‚ä§_ C)) j =
      terminal.from _ := by aesop_cat
#align category_theory.limits.limit_const_terminal_inv_œÄ CategoryTheory.Limits.limitConstTerminal_inv_œÄ

instance {J : Type*} [Category J] {C : Type*} [Category C] [HasInitial C] :
    HasColimit ((CategoryTheory.Functor.const J).obj (‚ä•_ C)) :=
  HasColimit.mk
    { cocone :=
        { pt := ‚ä•_ C
          Œπ := { app := fun _ => initial.to _ } }
      isColimit := { desc := fun s => initial.to _ } }

/-- The colimit of the constant `‚ä•_ C` functor is `‚ä•_ C`. -/
@[simps inv]
def colimitConstInitial {J : Type*} [Category J] {C : Type*} [Category C] [HasInitial C] :
    colimit ((CategoryTheory.Functor.const J).obj (‚ä•_ C)) ‚âÖ ‚ä•_ C where
  hom :=
    colimit.desc ((CategoryTheory.Functor.const J).obj (‚ä•_ C))
      { pt := ‚ä•_ C
        Œπ := { app := fun j => initial.to _ } }
  inv := initial.to _
#align category_theory.limits.colimit_const_initial CategoryTheory.Limits.colimitConstInitial

@[reassoc (attr := simp)]
theorem Œπ_colimitConstInitial_hom {J : Type*} [Category J] {C : Type*} [Category C] [HasInitial C]
    {j : J} :
    colimit.Œπ ((CategoryTheory.Functor.const J).obj (‚ä•_ C)) j ‚â´ colimitConstInitial.hom =
      initial.to _ := by aesop_cat
#align category_theory.limits.Œπ_colimit_const_initial_hom CategoryTheory.Limits.Œπ_colimitConstInitial_hom

/-- A category is an `InitialMonoClass` if the canonical morphism of an initial object is a
monomorphism.  In practice, this is most useful when given an arbitrary morphism out of the chosen
initial object, see `initial.mono_from`.
Given a terminal object, this is equivalent to the assumption that the unique morphism from initial
to terminal is a monomorphism, which is the second of Freyd's axioms for an AT category.

TODO: This is a condition satisfied by categories with zero objects and morphisms.
-/
class InitialMonoClass (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] : Prop where
  /-- The map from the (any as stated) initial object to any other object is a
    monomorphism -/
  isInitial_mono_from : ‚àÄ {I} (X : C) (hI : IsInitial I), Mono (hI.to X)
#align category_theory.limits.initial_mono_class CategoryTheory.Limits.InitialMonoClass

theorem IsInitial.mono_from [InitialMonoClass C] {I} {X : C} (hI : IsInitial I) (f : I ‚ü∂ X) :
    Mono f := by
  rw [hI.hom_ext f (hI.to X)]
  apply InitialMonoClass.isInitial_mono_from
#align category_theory.limits.is_initial.mono_from CategoryTheory.Limits.IsInitial.mono_from

instance (priority := 100) initial.mono_from [HasInitial C] [InitialMonoClass C] (X : C)
    (f : ‚ä•_ C ‚ü∂ X) : Mono f :=
  initialIsInitial.mono_from f
#align category_theory.limits.initial.mono_from CategoryTheory.Limits.initial.mono_from

/-- To show a category is an `InitialMonoClass` it suffices to give an initial object such that
every morphism out of it is a monomorphism. -/
theorem InitialMonoClass.of_isInitial {I : C} (hI : IsInitial I) (h : ‚àÄ X, Mono (hI.to X)) :
    InitialMonoClass C where
  isInitial_mono_from {I'} X hI' := by
    rw [hI'.hom_ext (hI'.to X) ((hI'.uniqueUpToIso hI).hom ‚â´ hI.to X)]
    apply mono_comp
#align category_theory.limits.initial_mono_class.of_is_initial CategoryTheory.Limits.InitialMonoClass.of_isInitial

/-- To show a category is an `InitialMonoClass` it suffices to show every morphism out of the
initial object is a monomorphism. -/
theorem InitialMonoClass.of_initial [HasInitial C] (h : ‚àÄ X : C, Mono (initial.to X)) :
    InitialMonoClass C :=
  InitialMonoClass.of_isInitial initialIsInitial h
#align category_theory.limits.initial_mono_class.of_initial CategoryTheory.Limits.InitialMonoClass.of_initial

/-- To show a category is an `InitialMonoClass` it suffices to show the unique morphism from an
initial object to a terminal object is a monomorphism. -/
theorem InitialMonoClass.of_isTerminal {I T : C} (hI : IsInitial I) (hT : IsTerminal T)
    (_ : Mono (hI.to T)) : InitialMonoClass C :=
  InitialMonoClass.of_isInitial hI fun X => mono_of_mono_fac (hI.hom_ext (_ ‚â´ hT.from X) (hI.to T))
#align category_theory.limits.initial_mono_class.of_is_terminal CategoryTheory.Limits.InitialMonoClass.of_isTerminal

/-- To show a category is an `InitialMonoClass` it suffices to show the unique morphism from the
initial object to a terminal object is a monomorphism. -/
theorem InitialMonoClass.of_terminal [HasInitial C] [HasTerminal C] (h : Mono (initial.to (‚ä§_ C))) :
    InitialMonoClass C :=
  InitialMonoClass.of_isTerminal initialIsInitial terminalIsTerminal h
#align category_theory.limits.initial_mono_class.of_terminal CategoryTheory.Limits.InitialMonoClass.of_terminal

section Comparison

variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] (G : C ‚•§ D)

/-- The comparison morphism from the image of a terminal object to the terminal object in the target
category.
This is an isomorphism iff `G` preserves terminal objects, see
`CategoryTheory.Limits.PreservesTerminal.ofIsoComparison`.
-/
def terminalComparison [HasTerminal C] [HasTerminal D] : G.obj (‚ä§_ C) ‚ü∂ ‚ä§_ D :=
  terminal.from _
#align category_theory.limits.terminal_comparison CategoryTheory.Limits.terminalComparison

-- TODO: Show this is an isomorphism if and only if `G` preserves initial objects.
/--
The comparison morphism from the initial object in the target category to the image of the initial
object.
-/
def initialComparison [HasInitial C] [HasInitial D] : ‚ä•_ D ‚ü∂ G.obj (‚ä•_ C) :=
  initial.to _
#align category_theory.limits.initial_comparison CategoryTheory.Limits.initialComparison

end Comparison

variable {J : Type u} [Category.{v} J]

/-- From a functor `F : J ‚•§ C`, given an initial object of `J`, construct a cone for `J`.
In `limitOfDiagramInitial` we show it is a limit cone. -/
@[simps]
def coneOfDiagramInitial {X : J} (tX : IsInitial X) (F : J ‚•§ C) : Cone F where
  pt := F.obj X
  œÄ :=
    { app := fun j => F.map (tX.to j)
      naturality := fun j j' k => by
        dsimp
        rw [‚Üê F.map_comp, Category.id_comp, tX.hom_ext (tX.to j ‚â´ k) (tX.to j')] }
#align category_theory.limits.cone_of_diagram_initial CategoryTheory.Limits.coneOfDiagramInitial

/-- From a functor `F : J ‚•§ C`, given an initial object of `J`, show the cone
`coneOfDiagramInitial` is a limit. -/
def limitOfDiagramInitial {X : J} (tX : IsInitial X) (F : J ‚•§ C) :
    IsLimit (coneOfDiagramInitial tX F) where
  lift s := s.œÄ.app X
  uniq s m w := by
    conv_lhs => dsimp
    simp_rw [‚Üê w X, coneOfDiagramInitial_œÄ_app, tX.hom_ext (tX.to X) (ùüô _)]
    simp
#align category_theory.limits.limit_of_diagram_initial CategoryTheory.Limits.limitOfDiagramInitial

instance hasLimit_of_domain_hasInitial [HasInitial J] {F : J ‚•§ C} : HasLimit F :=
  HasLimit.mk { cone := _, isLimit := limitOfDiagramInitial (initialIsInitial) F }

-- See note [dsimp, simp]
-- This is reducible to allow usage of lemmas about `cone_point_unique_up_to_iso`.
/-- For a functor `F : J ‚•§ C`, if `J` has an initial object then the image of it is isomorphic
to the limit of `F`. -/
abbrev limitOfInitial (F : J ‚•§ C) [HasInitial J] : limit F ‚âÖ F.obj (‚ä•_ J) :=
  IsLimit.conePointUniqueUpToIso (limit.isLimit _) (limitOfDiagramInitial initialIsInitial F)
#align category_theory.limits.limit_of_initial CategoryTheory.Limits.limitOfInitial

/-- From a functor `F : J ‚•§ C`, given a terminal object of `J`, construct a cone for `J`,
provided that the morphisms in the diagram are isomorphisms.
In `limitOfDiagramTerminal` we show it is a limit cone. -/
@[simps]
def coneOfDiagramTerminal {X : J} (hX : IsTerminal X) (F : J ‚•§ C)
    [‚àÄ (i j : J) (f : i ‚ü∂ j), IsIso (F.map f)] : Cone F where
  pt := F.obj X
  œÄ :=
    { app := fun i => inv (F.map (hX.from _))
      naturality := by
        intro i j f
        dsimp
        simp only [IsIso.eq_inv_comp, IsIso.comp_inv_eq, Category.id_comp, ‚Üê F.map_comp,
          hX.hom_ext (hX.from i) (f ‚â´ hX.from j)] }
#align category_theory.limits.cone_of_diagram_terminal CategoryTheory.Limits.coneOfDiagramTerminal

/-- From a functor `F : J ‚•§ C`, given a terminal object of `J` and that the morphisms in the
diagram are isomorphisms, show the cone `coneOfDiagramTerminal` is a limit. -/
def limitOfDiagramTerminal {X : J} (hX : IsTerminal X) (F : J ‚•§ C)
    [‚àÄ (i j : J) (f : i ‚ü∂ j), IsIso (F.map f)] : IsLimit (coneOfDiagramTerminal hX F) where
  lift S := S.œÄ.app _
#align category_theory.limits.limit_of_diagram_terminal CategoryTheory.Limits.limitOfDiagramTerminal

instance hasLimit_of_domain_hasTerminal [HasTerminal J] {F : J ‚•§ C}
    [‚àÄ (i j : J) (f : i ‚ü∂ j), IsIso (F.map f)] : HasLimit F :=
  HasLimit.mk { cone := _, isLimit := limitOfDiagramTerminal (terminalIsTerminal) F }

-- This is reducible to allow usage of lemmas about `cone_point_unique_up_to_iso`.
/-- For a functor `F : J ‚•§ C`, if `J` has a terminal object and all the morphisms in the diagram
are isomorphisms, then the image of the terminal object is isomorphic to the limit of `F`. -/
abbrev limitOfTerminal (F : J ‚•§ C) [HasTerminal J] [‚àÄ (i j : J) (f : i ‚ü∂ j), IsIso (F.map f)] :
    limit F ‚âÖ F.obj (‚ä§_ J) :=
  IsLimit.conePointUniqueUpToIso (limit.isLimit _) (limitOfDiagramTerminal terminalIsTerminal F)
#align category_theory.limits.limit_of_terminal CategoryTheory.Limits.limitOfTerminal

/-- From a functor `F : J ‚•§ C`, given a terminal object of `J`, construct a cocone for `J`.
In `colimitOfDiagramTerminal` we show it is a colimit cocone. -/
@[simps]
def coconeOfDiagramTerminal {X : J} (tX : IsTerminal X) (F : J ‚•§ C) : Cocone F where
  pt := F.obj X
  Œπ :=
    { app := fun j => F.map (tX.from j)
      naturality := fun j j' k => by
        dsimp
        rw [‚Üê F.map_comp, Category.comp_id, tX.hom_ext (k ‚â´ tX.from j') (tX.from j)] }
#align category_theory.limits.cocone_of_diagram_terminal CategoryTheory.Limits.coconeOfDiagramTerminal

/-- From a functor `F : J ‚•§ C`, given a terminal object of `J`, show the cocone
`coconeOfDiagramTerminal` is a colimit. -/
def colimitOfDiagramTerminal {X : J} (tX : IsTerminal X) (F : J ‚•§ C) :
    IsColimit (coconeOfDiagramTerminal tX F) where
  desc s := s.Œπ.app X
  uniq s m w := by
    conv_rhs => dsimp -- Porting note: why do I need this much firepower?
    rw [‚Üê w X, coconeOfDiagramTerminal_Œπ_app, tX.hom_ext (tX.from X) (ùüô _)]
    simp
#align category_theory.limits.colimit_of_diagram_terminal CategoryTheory.Limits.colimitOfDiagramTerminal

instance hasColimit_of_domain_hasTerminal [HasTerminal J] {F : J ‚•§ C} : HasColimit F :=
  HasColimit.mk { cocone := _, isColimit := colimitOfDiagramTerminal (terminalIsTerminal) F }

lemma IsColimit.isIso_Œπ_app_of_isTerminal {F : J ‚•§ C} {c : Cocone F} (hc : IsColimit c)
    (X : J) (hX : IsTerminal X) :
    IsIso (c.Œπ.app X) := by
  change IsIso (coconePointUniqueUpToIso (colimitOfDiagramTerminal hX F) hc).hom
  infer_instance

-- This is reducible to allow usage of lemmas about `cocone_point_unique_up_to_iso`.
/-- For a functor `F : J ‚•§ C`, if `J` has a terminal object then the image of it is isomorphic
to the colimit of `F`. -/
abbrev colimitOfTerminal (F : J ‚•§ C) [HasTerminal J] : colimit F ‚âÖ F.obj (‚ä§_ J) :=
  IsColimit.coconePointUniqueUpToIso (colimit.isColimit _)
    (colimitOfDiagramTerminal terminalIsTerminal F)
#align category_theory.limits.colimit_of_terminal CategoryTheory.Limits.colimitOfTerminal

/-- From a functor `F : J ‚•§ C`, given an initial object of `J`, construct a cocone for `J`,
provided that the morphisms in the diagram are isomorphisms.
In `colimitOfDiagramInitial` we show it is a colimit cocone. -/
@[simps]
def coconeOfDiagramInitial {X : J} (hX : IsInitial X) (F : J ‚•§ C)
    [‚àÄ (i j : J) (f : i ‚ü∂ j), IsIso (F.map f)] : Cocone F where
  pt := F.obj X
  Œπ :=
    { app := fun i => inv (F.map (hX.to _))
      naturality := by
        intro i j f
        dsimp
        simp only [IsIso.eq_inv_comp, IsIso.comp_inv_eq, Category.comp_id, ‚Üê F.map_comp,
          hX.hom_ext (hX.to i ‚â´ f) (hX.to j)] }
#align category_theory.limits.cocone_of_diagram_initial CategoryTheory.Limits.coconeOfDiagramInitial

/-- From a functor `F : J ‚•§ C`, given an initial object of `J` and that the morphisms in the
diagram are isomorphisms, show the cone `coconeOfDiagramInitial` is a colimit. -/
def colimitOfDiagramInitial {X : J} (hX : IsInitial X) (F : J ‚•§ C)
    [‚àÄ (i j : J) (f : i ‚ü∂ j), IsIso (F.map f)] : IsColimit (coconeOfDiagramInitial hX F) where
  desc S := S.Œπ.app _
#align category_theory.limits.colimit_of_diagram_initial CategoryTheory.Limits.colimitOfDiagramInitial

instance hasColimit_of_domain_hasInitial [HasInitial J] {F : J ‚•§ C}
    [‚àÄ (i j : J) (f : i ‚ü∂ j), IsIso (F.map f)] : HasColimit F :=
  HasColimit.mk { cocone := _, isColimit := colimitOfDiagramInitial (initialIsInitial) F }

lemma IsLimit.isIso_œÄ_app_of_isInitial {F : J ‚•§ C} {c : Cone F} (hc : IsLimit c)
    (X : J) (hX : IsInitial X) :
    IsIso (c.œÄ.app X) := by
  change IsIso (conePointUniqueUpToIso hc (limitOfDiagramInitial hX F)).hom
  infer_instance

-- This is reducible to allow usage of lemmas about `cocone_point_unique_up_to_iso`.
/-- For a functor `F : J ‚•§ C`, if `J` has an initial object and all the morphisms in the diagram
are isomorphisms, then the image of the initial object is isomorphic to the colimit of `F`. -/
abbrev colimitOfInitial (F : J ‚•§ C) [HasInitial J] [‚àÄ (i j : J) (f : i ‚ü∂ j), IsIso (F.map f)] :
    colimit F ‚âÖ F.obj (‚ä•_ J) :=
  IsColimit.coconePointUniqueUpToIso (colimit.isColimit _)
    (colimitOfDiagramInitial initialIsInitial _)
#align category_theory.limits.colimit_of_initial CategoryTheory.Limits.colimitOfInitial

/-- If `j` is initial in the index category, then the map `limit.œÄ F j` is an isomorphism.
-/
theorem isIso_œÄ_of_isInitial {j : J} (I : IsInitial j) (F : J ‚•§ C) [HasLimit F] :
    IsIso (limit.œÄ F j) :=
  ‚ü®‚ü®limit.lift _ (coneOfDiagramInitial I F), ‚ü®by ext; simp, by simp‚ü©‚ü©‚ü©
#align category_theory.limits.is_iso_œÄ_of_is_initial CategoryTheory.Limits.isIso_œÄ_of_isInitial

instance isIso_œÄ_initial [HasInitial J] (F : J ‚•§ C) : IsIso (limit.œÄ F (‚ä•_ J)) :=
  isIso_œÄ_of_isInitial initialIsInitial F
#align category_theory.limits.is_iso_œÄ_initial CategoryTheory.Limits.isIso_œÄ_initial

theorem isIso_œÄ_of_isTerminal {j : J} (I : IsTerminal j) (F : J ‚•§ C) [HasLimit F]
    [‚àÄ (i j : J) (f : i ‚ü∂ j), IsIso (F.map f)] : IsIso (limit.œÄ F j) :=
  ‚ü®‚ü®limit.lift _ (coneOfDiagramTerminal I F), by ext; simp, by simp‚ü©‚ü©
#align category_theory.limits.is_iso_œÄ_of_is_terminal CategoryTheory.Limits.isIso_œÄ_of_isTerminal

instance isIso_œÄ_terminal [HasTerminal J] (F : J ‚•§ C) [‚àÄ (i j : J) (f : i ‚ü∂ j), IsIso (F.map f)] :
    IsIso (limit.œÄ F (‚ä§_ J)) :=
  isIso_œÄ_of_isTerminal terminalIsTerminal F
#align category_theory.limits.is_iso_œÄ_terminal CategoryTheory.Limits.isIso_œÄ_terminal

/-- If `j` is terminal in the index category, then the map `colimit.Œπ F j` is an isomorphism.
-/
theorem isIso_Œπ_of_isTerminal {j : J} (I : IsTerminal j) (F : J ‚•§ C) [HasColimit F] :
    IsIso (colimit.Œπ F j) :=
  ‚ü®‚ü®colimit.desc _ (coconeOfDiagramTerminal I F), ‚ü®by simp, by ext; simp‚ü©‚ü©‚ü©
#align category_theory.limits.is_iso_Œπ_of_is_terminal CategoryTheory.Limits.isIso_Œπ_of_isTerminal

instance isIso_Œπ_terminal [HasTerminal J] (F : J ‚•§ C) : IsIso (colimit.Œπ F (‚ä§_ J)) :=
  isIso_Œπ_of_isTerminal terminalIsTerminal F
#align category_theory.limits.is_iso_Œπ_terminal CategoryTheory.Limits.isIso_Œπ_terminal

theorem isIso_Œπ_of_isInitial {j : J} (I : IsInitial j) (F : J ‚•§ C) [HasColimit F]
    [‚àÄ (i j : J) (f : i ‚ü∂ j), IsIso (F.map f)] : IsIso (colimit.Œπ F j) :=
  ‚ü®‚ü®colimit.desc _ (coconeOfDiagramInitial I F), by
    refine ‚ü®?_, by ext; simp‚ü©
    dsimp; simp only [colimit.Œπ_desc, coconeOfDiagramInitial_pt, coconeOfDiagramInitial_Œπ_app,
      Functor.const_obj_obj, IsInitial.to_self, Functor.map_id]
    dsimp [inv]; simp only [Category.id_comp, Category.comp_id, and_self]
    apply @Classical.choose_spec _ (fun x => x = ùüô F.obj j) _
  ‚ü©‚ü©
#align category_theory.limits.is_iso_Œπ_of_is_initial CategoryTheory.Limits.isIso_Œπ_of_isInitial

instance isIso_Œπ_initial [HasInitial J] (F : J ‚•§ C) [‚àÄ (i j : J) (f : i ‚ü∂ j), IsIso (F.map f)] :
    IsIso (colimit.Œπ F (‚ä•_ J)) :=
  isIso_Œπ_of_isInitial initialIsInitial F
#align category_theory.limits.is_iso_Œπ_initial CategoryTheory.Limits.isIso_Œπ_initial

end

end CategoryTheory.Limits
