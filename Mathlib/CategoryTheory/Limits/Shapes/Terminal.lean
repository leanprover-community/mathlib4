/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Bhavik Mehta
-/
import Mathlib.CategoryTheory.PEmpty
import Mathlib.CategoryTheory.Limits.HasLimits
import Mathlib.CategoryTheory.EpiMono
import Mathlib.CategoryTheory.Category.Preorder

#align_import category_theory.limits.shapes.terminal from "leanprover-community/mathlib"@"239d882c4fb58361ee8b3b39fb2091320edef10a"

/-!
# Initial and terminal objects in a category.

## References
* [Stacks: Initial and final objects](https://stacks.math.columbia.edu/tag/002B)
-/


noncomputable section

universe w w' v vâ‚ vâ‚‚ u uâ‚ uâ‚‚

open CategoryTheory

namespace CategoryTheory.Limits

variable {C : Type uâ‚} [Category.{vâ‚} C]

/-- Construct a cone for the empty diagram given an object. -/
@[simps]
def asEmptyCone (X : C) : Cone (Functor.empty.{0} C) :=
  { pt := X
    Ï€ :=
    { app := by aesop_cat } }

/-- Construct a cocone for the empty diagram given an object. -/
@[simps]
def asEmptyCocone (X : C) : Cocone (Functor.empty.{0} C) :=
  { pt := X
    Î¹ :=
    { app := by aesop_cat } }

/-- `X` is terminal if the cone it induces on the empty diagram is limiting. -/
abbrev IsTerminal (X : C) :=
  IsLimit (asEmptyCone X)

/-- `X` is initial if the cocone it induces on the empty diagram is colimiting. -/
abbrev IsInitial (X : C) :=
  IsColimit (asEmptyCocone X)

/-- An object `Y` is terminal iff for every `X` there is a unique morphism `X âŸ¶ Y`. -/
def isTerminalEquivUnique (F : Discrete.{0} PEmpty.{1} â¥¤ C) (Y : C) :
    IsLimit (âŸ¨Y, by aesop_cat, by aesop_catâŸ© : Cone F) â‰ƒ âˆ€ X : C, Unique (X âŸ¶ Y) where
  toFun t X :=
    { default := t.lift âŸ¨X, âŸ¨by aesop_cat, by aesop_catâŸ©âŸ©
      uniq := fun f =>
        t.uniq âŸ¨X, âŸ¨by aesop_cat, by aesop_catâŸ©âŸ© f (by aesop_cat) }
  invFun u :=
    { lift := fun s => (u s.pt).default
      uniq := fun s _ _ => (u s.pt).2 _ }
  left_inv := by dsimp [Function.LeftInverse]; intro x; simp only [eq_iff_true_of_subsingleton]
  right_inv := by
    dsimp [Function.RightInverse,Function.LeftInverse]
    intro u; funext X; simp only

/-- An object `Y` is terminal if for every `X` there is a unique morphism `X âŸ¶ Y`
    (as an instance). -/
def IsTerminal.ofUnique (Y : C) [h : âˆ€ X : C, Unique (X âŸ¶ Y)] : IsTerminal Y where
  lift s := (h s.pt).default
  fac := fun _ âŸ¨jâŸ© => j.elim

/-- An object `Y` is terminal if for every `X` there is a unique morphism `X âŸ¶ Y`
    (as explicit arguments). -/
def IsTerminal.ofUniqueHom {Y : C} (h : âˆ€ X : C, X âŸ¶ Y) (uniq : âˆ€ (X : C) (m : X âŸ¶ Y), m = h X) :
    IsTerminal Y :=
  have : âˆ€ X : C, Unique (X âŸ¶ Y) := fun X â†¦ âŸ¨âŸ¨h XâŸ©, uniq XâŸ©
  IsTerminal.ofUnique Y

/-- If `Î±` is a preorder with top, then `âŠ¤` is a terminal object. -/
def isTerminalTop {Î± : Type*} [Preorder Î±] [OrderTop Î±] : IsTerminal (âŠ¤ : Î±) :=
  IsTerminal.ofUnique _

/-- Transport a term of type `IsTerminal` across an isomorphism. -/
def IsTerminal.ofIso {Y Z : C} (hY : IsTerminal Y) (i : Y â‰… Z) : IsTerminal Z :=
  IsLimit.ofIsoLimit hY
    { hom := { hom := i.hom }
      inv := { hom := i.inv } }

/-- An object `X` is initial iff for every `Y` there is a unique morphism `X âŸ¶ Y`. -/
def isInitialEquivUnique (F : Discrete.{0} PEmpty.{1} â¥¤ C) (X : C) :
    IsColimit (âŸ¨X, âŸ¨by aesop_cat, by aesop_catâŸ©âŸ© : Cocone F) â‰ƒ âˆ€ Y : C, Unique (X âŸ¶ Y) where
  toFun t X :=
    { default := t.desc âŸ¨X, âŸ¨by aesop_cat, by aesop_catâŸ©âŸ©
      uniq := fun f => t.uniq âŸ¨X, âŸ¨by aesop_cat, by aesop_catâŸ©âŸ© f (by aesop_cat) }
  invFun u :=
    { desc := fun s => (u s.pt).default
      uniq := fun s _ _ => (u s.pt).2 _ }
  left_inv := by dsimp [Function.LeftInverse]; intro; simp only [eq_iff_true_of_subsingleton]
  right_inv := by
    dsimp [Function.RightInverse,Function.LeftInverse]
    intro; funext; simp only

/-- An object `X` is initial if for every `Y` there is a unique morphism `X âŸ¶ Y`
    (as an instance). -/
def IsInitial.ofUnique (X : C) [h : âˆ€ Y : C, Unique (X âŸ¶ Y)] : IsInitial X where
  desc s := (h s.pt).default
  fac := fun _ âŸ¨jâŸ© => j.elim

/-- An object `X` is initial if for every `Y` there is a unique morphism `X âŸ¶ Y`
    (as explicit arguments). -/
def IsInitial.ofUniqueHom {X : C} (h : âˆ€ Y : C, X âŸ¶ Y) (uniq : âˆ€ (Y : C) (m : X âŸ¶ Y), m = h Y) :
    IsInitial X :=
  have : âˆ€ Y : C, Unique (X âŸ¶ Y) := fun Y â†¦ âŸ¨âŸ¨h YâŸ©, uniq YâŸ©
  IsInitial.ofUnique X

/-- If `Î±` is a preorder with bot, then `âŠ¥` is an initial object. -/
def isInitialBot {Î± : Type*} [Preorder Î±] [OrderBot Î±] : IsInitial (âŠ¥ : Î±) :=
  IsInitial.ofUnique _

/-- Transport a term of type `is_initial` across an isomorphism. -/
def IsInitial.ofIso {X Y : C} (hX : IsInitial X) (i : X â‰… Y) : IsInitial Y :=
  IsColimit.ofIsoColimit hX
    { hom := { hom := i.hom }
      inv := { hom := i.inv } }

/-- Give the morphism to a terminal object from any other. -/
def IsTerminal.from {X : C} (t : IsTerminal X) (Y : C) : Y âŸ¶ X :=
  t.lift (asEmptyCone Y)

/-- Any two morphisms to a terminal object are equal. -/
theorem IsTerminal.hom_ext {X Y : C} (t : IsTerminal X) (f g : Y âŸ¶ X) : f = g :=
  IsLimit.hom_ext t (by aesop_cat)

@[simp]
theorem IsTerminal.comp_from {Z : C} (t : IsTerminal Z) {X Y : C} (f : X âŸ¶ Y) :
    f â‰« t.from Y = t.from X :=
  t.hom_ext _ _

@[simp]
theorem IsTerminal.from_self {X : C} (t : IsTerminal X) : t.from X = ğŸ™ X :=
  t.hom_ext _ _

/-- Give the morphism from an initial object to any other. -/
def IsInitial.to {X : C} (t : IsInitial X) (Y : C) : X âŸ¶ Y :=
  t.desc (asEmptyCocone Y)

/-- Any two morphisms from an initial object are equal. -/
theorem IsInitial.hom_ext {X Y : C} (t : IsInitial X) (f g : X âŸ¶ Y) : f = g :=
  IsColimit.hom_ext t (by aesop_cat)

@[simp]
theorem IsInitial.to_comp {X : C} (t : IsInitial X) {Y Z : C} (f : Y âŸ¶ Z) : t.to Y â‰« f = t.to Z :=
  t.hom_ext _ _

@[simp]
theorem IsInitial.to_self {X : C} (t : IsInitial X) : t.to X = ğŸ™ X :=
  t.hom_ext _ _

/-- Any morphism from a terminal object is split mono. -/
theorem IsTerminal.isSplitMono_from {X Y : C} (t : IsTerminal X) (f : X âŸ¶ Y) : IsSplitMono f :=
  IsSplitMono.mk' âŸ¨t.from _, t.hom_ext _ _âŸ©

/-- Any morphism to an initial object is split epi. -/
theorem IsInitial.isSplitEpi_to {X Y : C} (t : IsInitial X) (f : Y âŸ¶ X) : IsSplitEpi f :=
  IsSplitEpi.mk' âŸ¨t.to _, t.hom_ext _ _âŸ©

/-- Any morphism from a terminal object is mono. -/
theorem IsTerminal.mono_from {X Y : C} (t : IsTerminal X) (f : X âŸ¶ Y) : Mono f := by
  haveI := t.isSplitMono_from f; infer_instance

/-- Any morphism to an initial object is epi. -/
theorem IsInitial.epi_to {X Y : C} (t : IsInitial X) (f : Y âŸ¶ X) : Epi f := by
  haveI := t.isSplitEpi_to f; infer_instance

/-- If `T` and `T'` are terminal, they are isomorphic. -/
@[simps]
def IsTerminal.uniqueUpToIso {T T' : C} (hT : IsTerminal T) (hT' : IsTerminal T') : T â‰… T' where
  hom := hT'.from _
  inv := hT.from _

/-- If `I` and `I'` are initial, they are isomorphic. -/
@[simps]
def IsInitial.uniqueUpToIso {I I' : C} (hI : IsInitial I) (hI' : IsInitial I') : I â‰… I' where
  hom := hI.to _
  inv := hI'.to _

variable (C)

/-- A category has a terminal object if it has a limit over the empty diagram.
Use `hasTerminal_of_unique` to construct instances.
-/
abbrev HasTerminal :=
  HasLimitsOfShape (Discrete.{0} PEmpty) C

/-- A category has an initial object if it has a colimit over the empty diagram.
Use `hasInitial_of_unique` to construct instances.
-/
abbrev HasInitial :=
  HasColimitsOfShape (Discrete.{0} PEmpty) C

section Univ

variable (X : C) {Fâ‚ : Discrete.{w} PEmpty â¥¤ C} {Fâ‚‚ : Discrete.{w'} PEmpty â¥¤ C}

/-- Being terminal is independent of the empty diagram, its universe, and the cone over it,
    as long as the cone points are isomorphic. -/
def isLimitChangeEmptyCone {câ‚ : Cone Fâ‚} (hl : IsLimit câ‚) (câ‚‚ : Cone Fâ‚‚) (hi : câ‚.pt â‰… câ‚‚.pt) :
    IsLimit câ‚‚ where
  lift c := hl.lift âŸ¨c.pt, by aesop_cat, by aesop_catâŸ© â‰« hi.hom
  uniq c f _ := by
    dsimp
    rw [â† hl.uniq _ (f â‰« hi.inv) _]
    Â· simp only [Category.assoc, Iso.inv_hom_id, Category.comp_id]
    Â· aesop_cat

/-- Replacing an empty cone in `IsLimit` by another with the same cone point
    is an equivalence. -/
def isLimitEmptyConeEquiv (câ‚ : Cone Fâ‚) (câ‚‚ : Cone Fâ‚‚) (h : câ‚.pt â‰… câ‚‚.pt) :
    IsLimit câ‚ â‰ƒ IsLimit câ‚‚ where
  toFun hl := isLimitChangeEmptyCone C hl câ‚‚ h
  invFun hl := isLimitChangeEmptyCone C hl câ‚ h.symm
  left_inv := by dsimp [Function.LeftInverse]; intro; simp only [eq_iff_true_of_subsingleton]
  right_inv := by
    dsimp [Function.LeftInverse,Function.RightInverse]; intro; funext
    simp only [eq_iff_true_of_subsingleton]

theorem hasTerminalChangeDiagram (h : HasLimit Fâ‚) : HasLimit Fâ‚‚ :=
  âŸ¨âŸ¨âŸ¨âŸ¨limit Fâ‚, by aesop_cat, by aesop_catâŸ©,
    isLimitChangeEmptyCone C (limit.isLimit Fâ‚) _ (eqToIso rfl)âŸ©âŸ©âŸ©

theorem hasTerminalChangeUniverse [h : HasLimitsOfShape (Discrete.{w} PEmpty) C] :
    HasLimitsOfShape (Discrete.{w'} PEmpty) C where
  has_limit _ := hasTerminalChangeDiagram C (h.1 (Functor.empty C))

/-- Being initial is independent of the empty diagram, its universe, and the cocone over it,
    as long as the cocone points are isomorphic. -/
def isColimitChangeEmptyCocone {câ‚ : Cocone Fâ‚} (hl : IsColimit câ‚) (câ‚‚ : Cocone Fâ‚‚)
    (hi : câ‚.pt â‰… câ‚‚.pt) : IsColimit câ‚‚ where
  desc c := hi.inv â‰« hl.desc âŸ¨c.pt, by aesop_cat, by aesop_catâŸ©
  uniq c f _ := by
    dsimp
    rw [â† hl.uniq _ (hi.hom â‰« f) _]
    Â· simp only [Iso.inv_hom_id_assoc]
    Â· aesop_cat

/-- Replacing an empty cocone in `IsColimit` by another with the same cocone point
    is an equivalence. -/
def isColimitEmptyCoconeEquiv (câ‚ : Cocone Fâ‚) (câ‚‚ : Cocone Fâ‚‚) (h : câ‚.pt â‰… câ‚‚.pt) :
    IsColimit câ‚ â‰ƒ IsColimit câ‚‚ where
  toFun hl := isColimitChangeEmptyCocone C hl câ‚‚ h
  invFun hl := isColimitChangeEmptyCocone C hl câ‚ h.symm
  left_inv := by dsimp [Function.LeftInverse]; intro; simp only [eq_iff_true_of_subsingleton]
  right_inv := by
    dsimp [Function.LeftInverse,Function.RightInverse]; intro; funext
    simp only [eq_iff_true_of_subsingleton]

theorem hasInitialChangeDiagram (h : HasColimit Fâ‚) : HasColimit Fâ‚‚ :=
  âŸ¨âŸ¨âŸ¨âŸ¨colimit Fâ‚, by aesop_cat, by aesop_catâŸ©,
    isColimitChangeEmptyCocone C (colimit.isColimit Fâ‚) _ (eqToIso rfl)âŸ©âŸ©âŸ©

theorem hasInitialChangeUniverse [h : HasColimitsOfShape (Discrete.{w} PEmpty) C] :
    HasColimitsOfShape (Discrete.{w'} PEmpty) C where
  has_colimit _ := hasInitialChangeDiagram C (h.1 (Functor.empty C))

end Univ

/-- An arbitrary choice of terminal object, if one exists.
You can use the notation `âŠ¤_ C`.
This object is characterized by having a unique morphism from any object.
-/
abbrev terminal [HasTerminal C] : C :=
  limit (Functor.empty.{0} C)

/-- An arbitrary choice of initial object, if one exists.
You can use the notation `âŠ¥_ C`.
This object is characterized by having a unique morphism to any object.
-/
abbrev initial [HasInitial C] : C :=
  colimit (Functor.empty.{0} C)

/-- Notation for the terminal object in `C` -/
notation "âŠ¤_ " C:20 => terminal C

/-- Notation for the initial object in `C` -/
notation "âŠ¥_ " C:20 => initial C

section

variable {C}

/-- We can more explicitly show that a category has a terminal object by specifying the object,
and showing there is a unique morphism to it from any other object. -/
theorem hasTerminal_of_unique (X : C) [h : âˆ€ Y : C, Unique (Y âŸ¶ X)] : HasTerminal C :=
  { has_limit := fun F => HasLimit.mk âŸ¨_, (isTerminalEquivUnique F X).invFun hâŸ© }

theorem IsTerminal.hasTerminal {X : C} (h : IsTerminal X) : HasTerminal C :=
  { has_limit := fun F => HasLimit.mk âŸ¨âŸ¨X, by aesop_cat, by aesop_catâŸ©,
    isLimitChangeEmptyCone _ h _ (Iso.refl _)âŸ© }

/-- We can more explicitly show that a category has an initial object by specifying the object,
and showing there is a unique morphism from it to any other object. -/
theorem hasInitial_of_unique (X : C) [h : âˆ€ Y : C, Unique (X âŸ¶ Y)] : HasInitial C :=
  { has_colimit := fun F => HasColimit.mk âŸ¨_, (isInitialEquivUnique F X).invFun hâŸ© }

theorem IsInitial.hasInitial {X : C} (h : IsInitial X) : HasInitial C where
  has_colimit F :=
    HasColimit.mk âŸ¨âŸ¨X, by aesop_cat, by aesop_catâŸ©, isColimitChangeEmptyCocone _ h _ (Iso.refl _)âŸ©

/-- The map from an object to the terminal object. -/
abbrev terminal.from [HasTerminal C] (P : C) : P âŸ¶ âŠ¤_ C :=
  limit.lift (Functor.empty C) (asEmptyCone P)

/-- The map to an object from the initial object. -/
abbrev initial.to [HasInitial C] (P : C) : âŠ¥_ C âŸ¶ P :=
  colimit.desc (Functor.empty C) (asEmptyCocone P)

/-- A terminal object is terminal. -/
def terminalIsTerminal [HasTerminal C] : IsTerminal (âŠ¤_ C) where
  lift s := terminal.from _

/-- An initial object is initial. -/
def initialIsInitial [HasInitial C] : IsInitial (âŠ¥_ C) where
  desc s := initial.to _

instance uniqueToTerminal [HasTerminal C] (P : C) : Unique (P âŸ¶ âŠ¤_ C) :=
  isTerminalEquivUnique _ (âŠ¤_ C) terminalIsTerminal P

instance uniqueFromInitial [HasInitial C] (P : C) : Unique (âŠ¥_ C âŸ¶ P) :=
  isInitialEquivUnique _ (âŠ¥_ C) initialIsInitial P

@[simp]
theorem terminal.comp_from [HasTerminal C] {P Q : C} (f : P âŸ¶ Q) :
    f â‰« terminal.from Q = terminal.from P := by aesop

@[simp]
theorem initial.to_comp [HasInitial C] {P Q : C} (f : P âŸ¶ Q) : initial.to P â‰« f = initial.to Q := by
  aesop

/-- The (unique) isomorphism between the chosen initial object and any other initial object. -/
@[simp]
def initialIsoIsInitial [HasInitial C] {P : C} (t : IsInitial P) : âŠ¥_ C â‰… P :=
  initialIsInitial.uniqueUpToIso t

/-- The (unique) isomorphism between the chosen terminal object and any other terminal object. -/
@[simp]
def terminalIsoIsTerminal [HasTerminal C] {P : C} (t : IsTerminal P) : âŠ¤_ C â‰… P :=
  terminalIsTerminal.uniqueUpToIso t

/-- Any morphism from a terminal object is split mono. -/
instance terminal.isSplitMono_from {Y : C} [HasTerminal C] (f : âŠ¤_ C âŸ¶ Y) : IsSplitMono f :=
  IsTerminal.isSplitMono_from terminalIsTerminal _

/-- Any morphism to an initial object is split epi. -/
instance initial.isSplitEpi_to {Y : C} [HasInitial C] (f : Y âŸ¶ âŠ¥_ C) : IsSplitEpi f :=
  IsInitial.isSplitEpi_to initialIsInitial _

/-- An initial object is terminal in the opposite category. -/
def terminalOpOfInitial {X : C} (t : IsInitial X) : IsTerminal (Opposite.op X) where
  lift s := (t.to s.pt.unop).op
  uniq s m _ := Quiver.Hom.unop_inj (t.hom_ext _ _)

/-- An initial object in the opposite category is terminal in the original category. -/
def terminalUnopOfInitial {X : Cáµ’áµ–} (t : IsInitial X) : IsTerminal X.unop where
  lift s := (t.to (Opposite.op s.pt)).unop
  uniq s m _ := Quiver.Hom.op_inj (t.hom_ext _ _)

/-- A terminal object is initial in the opposite category. -/
def initialOpOfTerminal {X : C} (t : IsTerminal X) : IsInitial (Opposite.op X) where
  desc s := (t.from s.pt.unop).op
  uniq s m _ := Quiver.Hom.unop_inj (t.hom_ext _ _)

/-- A terminal object in the opposite category is initial in the original category. -/
def initialUnopOfTerminal {X : Cáµ’áµ–} (t : IsTerminal X) : IsInitial X.unop where
  desc s := (t.from (Opposite.op s.pt)).unop
  uniq s m _ := Quiver.Hom.op_inj (t.hom_ext _ _)

instance hasInitial_op_of_hasTerminal [HasTerminal C] : HasInitial Cáµ’áµ– :=
  (initialOpOfTerminal terminalIsTerminal).hasInitial

instance hasTerminal_op_of_hasInitial [HasInitial C] : HasTerminal Cáµ’áµ– :=
  (terminalOpOfInitial initialIsInitial).hasTerminal

theorem hasTerminal_of_hasInitial_op [HasInitial Cáµ’áµ–] : HasTerminal C :=
  (terminalUnopOfInitial initialIsInitial).hasTerminal

theorem hasInitial_of_hasTerminal_op [HasTerminal Cáµ’áµ–] : HasInitial C :=
  (initialUnopOfTerminal terminalIsTerminal).hasInitial

instance {J : Type*} [Category J] {C : Type*} [Category C] [HasTerminal C] :
    HasLimit ((CategoryTheory.Functor.const J).obj (âŠ¤_ C)) :=
  HasLimit.mk
    { cone :=
        { pt := âŠ¤_ C
          Ï€ := { app := fun _ => terminal.from _ } }
      isLimit := { lift := fun s => terminal.from _ } }

/-- The limit of the constant `âŠ¤_ C` functor is `âŠ¤_ C`. -/
@[simps hom]
def limitConstTerminal {J : Type*} [Category J] {C : Type*} [Category C] [HasTerminal C] :
    limit ((CategoryTheory.Functor.const J).obj (âŠ¤_ C)) â‰… âŠ¤_ C where
  hom := terminal.from _
  inv :=
    limit.lift ((CategoryTheory.Functor.const J).obj (âŠ¤_ C))
      { pt := âŠ¤_ C
        Ï€ := { app := fun j => terminal.from _ } }

@[reassoc (attr := simp)]
theorem limitConstTerminal_inv_Ï€ {J : Type*} [Category J] {C : Type*} [Category C] [HasTerminal C]
    {j : J} :
    limitConstTerminal.inv â‰« limit.Ï€ ((CategoryTheory.Functor.const J).obj (âŠ¤_ C)) j =
      terminal.from _ := by aesop_cat

instance {J : Type*} [Category J] {C : Type*} [Category C] [HasInitial C] :
    HasColimit ((CategoryTheory.Functor.const J).obj (âŠ¥_ C)) :=
  HasColimit.mk
    { cocone :=
        { pt := âŠ¥_ C
          Î¹ := { app := fun _ => initial.to _ } }
      isColimit := { desc := fun s => initial.to _ } }

/-- The colimit of the constant `âŠ¥_ C` functor is `âŠ¥_ C`. -/
@[simps inv]
def colimitConstInitial {J : Type*} [Category J] {C : Type*} [Category C] [HasInitial C] :
    colimit ((CategoryTheory.Functor.const J).obj (âŠ¥_ C)) â‰… âŠ¥_ C where
  hom :=
    colimit.desc ((CategoryTheory.Functor.const J).obj (âŠ¥_ C))
      { pt := âŠ¥_ C
        Î¹ := { app := fun j => initial.to _ } }
  inv := initial.to _

@[reassoc (attr := simp)]
theorem Î¹_colimitConstInitial_hom {J : Type*} [Category J] {C : Type*} [Category C] [HasInitial C]
    {j : J} :
    colimit.Î¹ ((CategoryTheory.Functor.const J).obj (âŠ¥_ C)) j â‰« colimitConstInitial.hom =
      initial.to _ := by aesop_cat

/-- A category is an `InitialMonoClass` if the canonical morphism of an initial object is a
monomorphism.  In practice, this is most useful when given an arbitrary morphism out of the chosen
initial object, see `initial.mono_from`.
Given a terminal object, this is equivalent to the assumption that the unique morphism from initial
to terminal is a monomorphism, which is the second of Freyd's axioms for an AT category.

TODO: This is a condition satisfied by categories with zero objects and morphisms.
-/
class InitialMonoClass (C : Type uâ‚) [Category.{vâ‚} C] : Prop where
  /-- The map from the (any as stated) initial object to any other object is a
    monomorphism -/
  isInitial_mono_from : âˆ€ {I} (X : C) (hI : IsInitial I), Mono (hI.to X)

theorem IsInitial.mono_from [InitialMonoClass C] {I} {X : C} (hI : IsInitial I) (f : I âŸ¶ X) :
    Mono f := by
  rw [hI.hom_ext f (hI.to X)]
  apply InitialMonoClass.isInitial_mono_from

instance (priority := 100) initial.mono_from [HasInitial C] [InitialMonoClass C] (X : C)
    (f : âŠ¥_ C âŸ¶ X) : Mono f :=
  initialIsInitial.mono_from f

/-- To show a category is an `InitialMonoClass` it suffices to give an initial object such that
every morphism out of it is a monomorphism. -/
theorem InitialMonoClass.of_isInitial {I : C} (hI : IsInitial I) (h : âˆ€ X, Mono (hI.to X)) :
    InitialMonoClass C where
  isInitial_mono_from {I'} X hI' := by
    rw [hI'.hom_ext (hI'.to X) ((hI'.uniqueUpToIso hI).hom â‰« hI.to X)]
    apply mono_comp

/-- To show a category is an `InitialMonoClass` it suffices to show every morphism out of the
initial object is a monomorphism. -/
theorem InitialMonoClass.of_initial [HasInitial C] (h : âˆ€ X : C, Mono (initial.to X)) :
    InitialMonoClass C :=
  InitialMonoClass.of_isInitial initialIsInitial h

/-- To show a category is an `InitialMonoClass` it suffices to show the unique morphism from an
initial object to a terminal object is a monomorphism. -/
theorem InitialMonoClass.of_isTerminal {I T : C} (hI : IsInitial I) (hT : IsTerminal T)
    (_ : Mono (hI.to T)) : InitialMonoClass C :=
  InitialMonoClass.of_isInitial hI fun X => mono_of_mono_fac (hI.hom_ext (_ â‰« hT.from X) (hI.to T))

/-- To show a category is an `InitialMonoClass` it suffices to show the unique morphism from the
initial object to a terminal object is a monomorphism. -/
theorem InitialMonoClass.of_terminal [HasInitial C] [HasTerminal C] (h : Mono (initial.to (âŠ¤_ C))) :
    InitialMonoClass C :=
  InitialMonoClass.of_isTerminal initialIsInitial terminalIsTerminal h

section Comparison

variable {D : Type uâ‚‚} [Category.{vâ‚‚} D] (G : C â¥¤ D)

/-- The comparison morphism from the image of a terminal object to the terminal object in the target
category.
This is an isomorphism iff `G` preserves terminal objects, see
`CategoryTheory.Limits.PreservesTerminal.ofIsoComparison`.
-/
def terminalComparison [HasTerminal C] [HasTerminal D] : G.obj (âŠ¤_ C) âŸ¶ âŠ¤_ D :=
  terminal.from _

-- TODO: Show this is an isomorphism if and only if `G` preserves initial objects.
/--
The comparison morphism from the initial object in the target category to the image of the initial
object.
-/
def initialComparison [HasInitial C] [HasInitial D] : âŠ¥_ D âŸ¶ G.obj (âŠ¥_ C) :=
  initial.to _

end Comparison

variable {J : Type u} [Category.{v} J]

/-- From a functor `F : J â¥¤ C`, given an initial object of `J`, construct a cone for `J`.
In `limitOfDiagramInitial` we show it is a limit cone. -/
@[simps]
def coneOfDiagramInitial {X : J} (tX : IsInitial X) (F : J â¥¤ C) : Cone F where
  pt := F.obj X
  Ï€ :=
    { app := fun j => F.map (tX.to j)
      naturality := fun j j' k => by
        dsimp
        rw [â† F.map_comp, Category.id_comp, tX.hom_ext (tX.to j â‰« k) (tX.to j')] }

/-- From a functor `F : J â¥¤ C`, given an initial object of `J`, show the cone
`coneOfDiagramInitial` is a limit. -/
def limitOfDiagramInitial {X : J} (tX : IsInitial X) (F : J â¥¤ C) :
    IsLimit (coneOfDiagramInitial tX F) where
  lift s := s.Ï€.app X
  uniq s m w := by
    conv_lhs => dsimp
    simp_rw [â† w X, coneOfDiagramInitial_Ï€_app, tX.hom_ext (tX.to X) (ğŸ™ _)]
    dsimp; simp

instance hasLimit_of_domain_hasInitial [HasInitial J] {F : J â¥¤ C} : HasLimit F :=
  HasLimit.mk { cone := _, isLimit := limitOfDiagramInitial (initialIsInitial) F }

-- See note [dsimp, simp]
-- This is reducible to allow usage of lemmas about `cone_point_unique_up_to_iso`.
/-- For a functor `F : J â¥¤ C`, if `J` has an initial object then the image of it is isomorphic
to the limit of `F`. -/
@[reducible]
def limitOfInitial (F : J â¥¤ C) [HasInitial J] : limit F â‰… F.obj (âŠ¥_ J) :=
  IsLimit.conePointUniqueUpToIso (limit.isLimit _) (limitOfDiagramInitial initialIsInitial F)

/-- From a functor `F : J â¥¤ C`, given a terminal object of `J`, construct a cone for `J`,
provided that the morphisms in the diagram are isomorphisms.
In `limitOfDiagramTerminal` we show it is a limit cone. -/
@[simps]
def coneOfDiagramTerminal {X : J} (hX : IsTerminal X) (F : J â¥¤ C)
    [âˆ€ (i j : J) (f : i âŸ¶ j), IsIso (F.map f)] : Cone F where
  pt := F.obj X
  Ï€ :=
    { app := fun i => inv (F.map (hX.from _))
      naturality := by
        intro i j f
        dsimp
        simp only [IsIso.eq_inv_comp, IsIso.comp_inv_eq, Category.id_comp, â† F.map_comp,
          hX.hom_ext (hX.from i) (f â‰« hX.from j)] }

/-- From a functor `F : J â¥¤ C`, given a terminal object of `J` and that the morphisms in the
diagram are isomorphisms, show the cone `coneOfDiagramTerminal` is a limit. -/
def limitOfDiagramTerminal {X : J} (hX : IsTerminal X) (F : J â¥¤ C)
    [âˆ€ (i j : J) (f : i âŸ¶ j), IsIso (F.map f)] : IsLimit (coneOfDiagramTerminal hX F) where
  lift S := S.Ï€.app _

instance hasLimit_of_domain_hasTerminal [HasTerminal J] {F : J â¥¤ C}
    [âˆ€ (i j : J) (f : i âŸ¶ j), IsIso (F.map f)] : HasLimit F :=
  HasLimit.mk { cone := _, isLimit := limitOfDiagramTerminal (terminalIsTerminal) F }

-- This is reducible to allow usage of lemmas about `cone_point_unique_up_to_iso`.
/-- For a functor `F : J â¥¤ C`, if `J` has a terminal object and all the morphisms in the diagram
are isomorphisms, then the image of the terminal object is isomorphic to the limit of `F`. -/
@[reducible]
def limitOfTerminal (F : J â¥¤ C) [HasTerminal J] [âˆ€ (i j : J) (f : i âŸ¶ j), IsIso (F.map f)] :
    limit F â‰… F.obj (âŠ¤_ J) :=
  IsLimit.conePointUniqueUpToIso (limit.isLimit _) (limitOfDiagramTerminal terminalIsTerminal F)

/-- From a functor `F : J â¥¤ C`, given a terminal object of `J`, construct a cocone for `J`.
In `colimitOfDiagramTerminal` we show it is a colimit cocone. -/
@[simps]
def coconeOfDiagramTerminal {X : J} (tX : IsTerminal X) (F : J â¥¤ C) : Cocone F where
  pt := F.obj X
  Î¹ :=
    { app := fun j => F.map (tX.from j)
      naturality := fun j j' k => by
        dsimp
        rw [â† F.map_comp, Category.comp_id, tX.hom_ext (k â‰« tX.from j') (tX.from j)] }

/-- From a functor `F : J â¥¤ C`, given a terminal object of `J`, show the cocone
`coconeOfDiagramTerminal` is a colimit. -/
def colimitOfDiagramTerminal {X : J} (tX : IsTerminal X) (F : J â¥¤ C) :
    IsColimit (coconeOfDiagramTerminal tX F) where
  desc s := s.Î¹.app X
  uniq s m w := by
    conv_rhs => dsimp -- Porting note: why do I need this much firepower?
    rw [â† w X, coconeOfDiagramTerminal_Î¹_app, tX.hom_ext (tX.from X) (ğŸ™ _)]
    simp

instance hasColimit_of_domain_hasTerminal [HasTerminal J] {F : J â¥¤ C} : HasColimit F :=
  HasColimit.mk { cocone := _, isColimit := colimitOfDiagramTerminal (terminalIsTerminal) F }

-- This is reducible to allow usage of lemmas about `cocone_point_unique_up_to_iso`.
/-- For a functor `F : J â¥¤ C`, if `J` has a terminal object then the image of it is isomorphic
to the colimit of `F`. -/
@[reducible]
def colimitOfTerminal (F : J â¥¤ C) [HasTerminal J] : colimit F â‰… F.obj (âŠ¤_ J) :=
  IsColimit.coconePointUniqueUpToIso (colimit.isColimit _)
    (colimitOfDiagramTerminal terminalIsTerminal F)

/-- From a functor `F : J â¥¤ C`, given an initial object of `J`, construct a cocone for `J`,
provided that the morphisms in the diagram are isomorphisms.
In `colimitOfDiagramInitial` we show it is a colimit cocone. -/
@[simps]
def coconeOfDiagramInitial {X : J} (hX : IsInitial X) (F : J â¥¤ C)
    [âˆ€ (i j : J) (f : i âŸ¶ j), IsIso (F.map f)] : Cocone F where
  pt := F.obj X
  Î¹ :=
    { app := fun i => inv (F.map (hX.to _))
      naturality := by
        intro i j f
        dsimp
        simp only [IsIso.eq_inv_comp, IsIso.comp_inv_eq, Category.comp_id, â† F.map_comp,
          hX.hom_ext (hX.to i â‰« f) (hX.to j)] }

/-- From a functor `F : J â¥¤ C`, given an initial object of `J` and that the morphisms in the
diagram are isomorphisms, show the cone `coconeOfDiagramInitial` is a colimit. -/
def colimitOfDiagramInitial {X : J} (hX : IsInitial X) (F : J â¥¤ C)
    [âˆ€ (i j : J) (f : i âŸ¶ j), IsIso (F.map f)] : IsColimit (coconeOfDiagramInitial hX F) where
  desc S := S.Î¹.app _

instance hasColimit_of_domain_hasInitial [HasInitial J] {F : J â¥¤ C}
    [âˆ€ (i j : J) (f : i âŸ¶ j), IsIso (F.map f)] : HasColimit F :=
  HasColimit.mk { cocone := _, isColimit := colimitOfDiagramInitial (initialIsInitial) F }

-- This is reducible to allow usage of lemmas about `cocone_point_unique_up_to_iso`.
/-- For a functor `F : J â¥¤ C`, if `J` has an initial object and all the morphisms in the diagram
are isomorphisms, then the image of the initial object is isomorphic to the colimit of `F`. -/
@[reducible]
def colimitOfInitial (F : J â¥¤ C) [HasInitial J] [âˆ€ (i j : J) (f : i âŸ¶ j), IsIso (F.map f)] :
    colimit F â‰… F.obj (âŠ¥_ J) :=
  IsColimit.coconePointUniqueUpToIso (colimit.isColimit _)
    (colimitOfDiagramInitial initialIsInitial _)

/-- If `j` is initial in the index category, then the map `limit.Ï€ F j` is an isomorphism.
-/
theorem isIso_Ï€_of_isInitial {j : J} (I : IsInitial j) (F : J â¥¤ C) [HasLimit F] :
    IsIso (limit.Ï€ F j) :=
  âŸ¨âŸ¨limit.lift _ (coneOfDiagramInitial I F), âŸ¨by ext; simp, by simpâŸ©âŸ©âŸ©

instance isIso_Ï€_initial [HasInitial J] (F : J â¥¤ C) : IsIso (limit.Ï€ F (âŠ¥_ J)) :=
  isIso_Ï€_of_isInitial initialIsInitial F

theorem isIso_Ï€_of_isTerminal {j : J} (I : IsTerminal j) (F : J â¥¤ C) [HasLimit F]
    [âˆ€ (i j : J) (f : i âŸ¶ j), IsIso (F.map f)] : IsIso (limit.Ï€ F j) :=
  âŸ¨âŸ¨limit.lift _ (coneOfDiagramTerminal I F), by ext; simp, by simpâŸ©âŸ©

instance isIso_Ï€_terminal [HasTerminal J] (F : J â¥¤ C) [âˆ€ (i j : J) (f : i âŸ¶ j), IsIso (F.map f)] :
    IsIso (limit.Ï€ F (âŠ¤_ J)) :=
  isIso_Ï€_of_isTerminal terminalIsTerminal F

/-- If `j` is terminal in the index category, then the map `colimit.Î¹ F j` is an isomorphism.
-/
theorem isIso_Î¹_of_isTerminal {j : J} (I : IsTerminal j) (F : J â¥¤ C) [HasColimit F] :
    IsIso (colimit.Î¹ F j) :=
  âŸ¨âŸ¨colimit.desc _ (coconeOfDiagramTerminal I F), âŸ¨by simp, by ext; simpâŸ©âŸ©âŸ©

instance isIso_Î¹_terminal [HasTerminal J] (F : J â¥¤ C) : IsIso (colimit.Î¹ F (âŠ¤_ J)) :=
  isIso_Î¹_of_isTerminal terminalIsTerminal F

theorem isIso_Î¹_of_isInitial {j : J} (I : IsInitial j) (F : J â¥¤ C) [HasColimit F]
    [âˆ€ (i j : J) (f : i âŸ¶ j), IsIso (F.map f)] : IsIso (colimit.Î¹ F j) :=
  âŸ¨âŸ¨colimit.desc _ (coconeOfDiagramInitial I F), by
    refine âŸ¨?_, by ext; simpâŸ©
    dsimp; simp only [colimit.Î¹_desc, coconeOfDiagramInitial_pt, coconeOfDiagramInitial_Î¹_app,
      Functor.const_obj_obj, IsInitial.to_self, Functor.map_id]
    dsimp [inv]; simp only [Category.id_comp, Category.comp_id, and_self]
    apply @Classical.choose_spec _ (fun x => x = ğŸ™ F.obj j) _
  âŸ©âŸ©

instance isIso_Î¹_initial [HasInitial J] (F : J â¥¤ C) [âˆ€ (i j : J) (f : i âŸ¶ j), IsIso (F.map f)] :
    IsIso (colimit.Î¹ F (âŠ¥_ J)) :=
  isIso_Î¹_of_isInitial initialIsInitial F

end

end CategoryTheory.Limits
