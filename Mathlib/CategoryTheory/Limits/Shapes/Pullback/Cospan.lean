/-
Copyright (c) 2018 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Markus Himmel, Bhavik Mehta
-/
import Mathlib.CategoryTheory.Limits.Shapes.WidePullbacks
import Mathlib.CategoryTheory.Limits.Shapes.BinaryProducts

/-!
# Cospan & Span

We define a category `WalkingCospan` (resp. `WalkingSpan`), which is the index category
for the given data for a pullback (resp. pushout) diagram. Convenience methods `cospan f g`
and `span f g` construct functors from the walking (co)span, hitting the given morphisms.

## References
* [Stacks: Fibre products](https://stacks.math.columbia.edu/tag/001U)
* [Stacks: Pushouts](https://stacks.math.columbia.edu/tag/0025)
-/

noncomputable section

open CategoryTheory

universe w v‚ÇÅ v‚ÇÇ v u u‚ÇÇ

namespace CategoryTheory.Limits

-- Porting note: `aesop cases` does not work on type synonyms like `WidePullbackShape`
-- attribute [local aesop safe cases] WidePullbackShape WalkingPair

/-- The type of objects for the diagram indexing a pullback, defined as a special case of
`WidePullbackShape`. -/
abbrev WalkingCospan : Type :=
  WidePullbackShape WalkingPair

/-- The left point of the walking cospan. -/
@[match_pattern]
abbrev WalkingCospan.left : WalkingCospan :=
  some WalkingPair.left

/-- The right point of the walking cospan. -/
@[match_pattern]
abbrev WalkingCospan.right : WalkingCospan :=
  some WalkingPair.right

/-- The central point of the walking cospan. -/
@[match_pattern]
abbrev WalkingCospan.one : WalkingCospan :=
  none

/-- The type of objects for the diagram indexing a pushout, defined as a special case of
`WidePushoutShape`.
-/
abbrev WalkingSpan : Type :=
  WidePushoutShape WalkingPair

/-- The left point of the walking span. -/
@[match_pattern]
abbrev WalkingSpan.left : WalkingSpan :=
  some WalkingPair.left

/-- The right point of the walking span. -/
@[match_pattern]
abbrev WalkingSpan.right : WalkingSpan :=
  some WalkingPair.right

/-- The central point of the walking span. -/
@[match_pattern]
abbrev WalkingSpan.zero : WalkingSpan :=
  none

namespace WalkingCospan

/-- The type of arrows for the diagram indexing a pullback. -/
abbrev Hom : WalkingCospan ‚Üí WalkingCospan ‚Üí Type :=
  WidePullbackShape.Hom

/-- The left arrow of the walking cospan. -/
@[match_pattern]
abbrev Hom.inl : left ‚ü∂ one :=
  WidePullbackShape.Hom.term _

/-- The right arrow of the walking cospan. -/
@[match_pattern]
abbrev Hom.inr : right ‚ü∂ one :=
  WidePullbackShape.Hom.term _

/-- The identity arrows of the walking cospan. -/
@[match_pattern]
abbrev Hom.id (X : WalkingCospan) : X ‚ü∂ X :=
  WidePullbackShape.Hom.id X

instance (X Y : WalkingCospan) : Subsingleton (X ‚ü∂ Y) := by
  constructor; intros; simp [eq_iff_true_of_subsingleton]

end WalkingCospan

namespace WalkingSpan

/-- The type of arrows for the diagram indexing a pushout. -/
abbrev Hom : WalkingSpan ‚Üí WalkingSpan ‚Üí Type :=
  WidePushoutShape.Hom

/-- The left arrow of the walking span. -/
@[match_pattern]
abbrev Hom.fst : zero ‚ü∂ left :=
  WidePushoutShape.Hom.init _

/-- The right arrow of the walking span. -/
@[match_pattern]
abbrev Hom.snd : zero ‚ü∂ right :=
  WidePushoutShape.Hom.init _

/-- The identity arrows of the walking span. -/
@[match_pattern]
abbrev Hom.id (X : WalkingSpan) : X ‚ü∂ X :=
  WidePushoutShape.Hom.id X

instance (X Y : WalkingSpan) : Subsingleton (X ‚ü∂ Y) := by
  constructor; intro a b; simp [eq_iff_true_of_subsingleton]

end WalkingSpan

open WalkingSpan.Hom WalkingCospan.Hom WidePullbackShape.Hom WidePushoutShape.Hom

variable {C : Type u} [Category.{v} C]

/-- To construct an isomorphism of cones over the walking cospan,
it suffices to construct an isomorphism
of the cone points and check it commutes with the legs to `left` and `right`. -/
def WalkingCospan.ext {F : WalkingCospan ‚•§ C} {s t : Cone F} (i : s.pt ‚âÖ t.pt)
    (w‚ÇÅ : s.œÄ.app WalkingCospan.left = i.hom ‚â´ t.œÄ.app WalkingCospan.left)
    (w‚ÇÇ : s.œÄ.app WalkingCospan.right = i.hom ‚â´ t.œÄ.app WalkingCospan.right) : s ‚âÖ t := by
  apply Cones.ext i _
  rintro (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©)
  ¬∑ have h‚ÇÅ := s.œÄ.naturality WalkingCospan.Hom.inl
    dsimp at h‚ÇÅ
    simp only [Category.id_comp] at h‚ÇÅ
    have h‚ÇÇ := t.œÄ.naturality WalkingCospan.Hom.inl
    dsimp at h‚ÇÇ
    simp only [Category.id_comp] at h‚ÇÇ
    simp_rw [h‚ÇÇ, ‚Üê Category.assoc, ‚Üê w‚ÇÅ, ‚Üê h‚ÇÅ]
  ¬∑ exact w‚ÇÅ
  ¬∑ exact w‚ÇÇ

/-- To construct an isomorphism of cocones over the walking span,
it suffices to construct an isomorphism
of the cocone points and check it commutes with the legs from `left` and `right`. -/
def WalkingSpan.ext {F : WalkingSpan ‚•§ C} {s t : Cocone F} (i : s.pt ‚âÖ t.pt)
    (w‚ÇÅ : s.Œπ.app WalkingCospan.left ‚â´ i.hom = t.Œπ.app WalkingCospan.left)
    (w‚ÇÇ : s.Œπ.app WalkingCospan.right ‚â´ i.hom = t.Œπ.app WalkingCospan.right) : s ‚âÖ t := by
  apply Cocones.ext i _
  rintro (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©)
  ¬∑ have h‚ÇÅ := s.Œπ.naturality WalkingSpan.Hom.fst
    dsimp at h‚ÇÅ
    simp only [Category.comp_id] at h‚ÇÅ
    have h‚ÇÇ := t.Œπ.naturality WalkingSpan.Hom.fst
    dsimp at h‚ÇÇ
    simp only [Category.comp_id] at h‚ÇÇ
    simp_rw [‚Üê h‚ÇÅ, Category.assoc, w‚ÇÅ, h‚ÇÇ]
  ¬∑ exact w‚ÇÅ
  ¬∑ exact w‚ÇÇ

/-- `cospan f g` is the functor from the walking cospan hitting `f` and `g`. -/
def cospan {X Y Z : C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) : WalkingCospan ‚•§ C :=
  WidePullbackShape.wideCospan Z (fun j => WalkingPair.casesOn j X Y) fun j =>
    WalkingPair.casesOn j f g

/-- `span f g` is the functor from the walking span hitting `f` and `g`. -/
def span {X Y Z : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) : WalkingSpan ‚•§ C :=
  WidePushoutShape.wideSpan X (fun j => WalkingPair.casesOn j Y Z) fun j =>
    WalkingPair.casesOn j f g

@[simp]
theorem cospan_left {X Y Z : C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) : (cospan f g).obj WalkingCospan.left = X :=
  rfl

@[simp]
theorem span_left {X Y Z : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) : (span f g).obj WalkingSpan.left = Y :=
  rfl

@[simp]
theorem cospan_right {X Y Z : C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    (cospan f g).obj WalkingCospan.right = Y := rfl

@[simp]
theorem span_right {X Y Z : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) : (span f g).obj WalkingSpan.right = Z :=
  rfl

@[simp]
theorem cospan_one {X Y Z : C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) : (cospan f g).obj WalkingCospan.one = Z :=
  rfl

@[simp]
theorem span_zero {X Y Z : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) : (span f g).obj WalkingSpan.zero = X :=
  rfl

@[simp]
theorem cospan_map_inl {X Y Z : C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    (cospan f g).map WalkingCospan.Hom.inl = f := rfl

@[simp]
theorem span_map_fst {X Y Z : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) : (span f g).map WalkingSpan.Hom.fst = f :=
  rfl

@[simp]
theorem cospan_map_inr {X Y Z : C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    (cospan f g).map WalkingCospan.Hom.inr = g := rfl

@[simp]
theorem span_map_snd {X Y Z : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) : (span f g).map WalkingSpan.Hom.snd = g :=
  rfl

theorem cospan_map_id {X Y Z : C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) (w : WalkingCospan) :
    (cospan f g).map (WalkingCospan.Hom.id w) = ùüô _ := rfl

theorem span_map_id {X Y Z : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) (w : WalkingSpan) :
    (span f g).map (WalkingSpan.Hom.id w) = ùüô _ := rfl

/-- Every diagram indexing a pullback is naturally isomorphic (actually, equal) to a `cospan` -/
@[simps (rhsMd := default)]
def diagramIsoCospan (F : WalkingCospan ‚•§ C) : F ‚âÖ cospan (F.map inl) (F.map inr) :=
  NatIso.ofComponents
  (fun j => eqToIso (by rcases j with (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©) <;> rfl))
  (by rintro (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©) (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©) f <;> cases f <;> simp)

/-- Every diagram indexing a pushout is naturally isomorphic (actually, equal) to a `span` -/
@[simps (rhsMd := default)]
def diagramIsoSpan (F : WalkingSpan ‚•§ C) : F ‚âÖ span (F.map fst) (F.map snd) :=
  NatIso.ofComponents
  (fun j => eqToIso (by rcases j with (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©) <;> rfl))
  (by rintro (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©) (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©) f <;> cases f <;> simp)

variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]

/-- A functor applied to a cospan is a cospan. -/
def cospanCompIso (F : C ‚•§ D) {X Y Z : C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    cospan f g ‚ãô F ‚âÖ cospan (F.map f) (F.map g) :=
  NatIso.ofComponents (by rintro (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©) <;> exact Iso.refl _)
    (by rintro (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©) (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©) f <;> cases f <;> simp)

section

variable (F : C ‚•§ D) {X Y Z : C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z)

@[simp]
theorem cospanCompIso_app_left : (cospanCompIso F f g).app WalkingCospan.left = Iso.refl _ := rfl

@[simp]
theorem cospanCompIso_app_right : (cospanCompIso F f g).app WalkingCospan.right = Iso.refl _ :=
  rfl

@[simp]
theorem cospanCompIso_app_one : (cospanCompIso F f g).app WalkingCospan.one = Iso.refl _ := rfl

@[simp]
theorem cospanCompIso_hom_app_left : (cospanCompIso F f g).hom.app WalkingCospan.left = ùüô _ :=
  rfl

@[simp]
theorem cospanCompIso_hom_app_right : (cospanCompIso F f g).hom.app WalkingCospan.right = ùüô _ :=
  rfl

@[simp]
theorem cospanCompIso_hom_app_one : (cospanCompIso F f g).hom.app WalkingCospan.one = ùüô _ := rfl

@[simp]
theorem cospanCompIso_inv_app_left : (cospanCompIso F f g).inv.app WalkingCospan.left = ùüô _ :=
  rfl

@[simp]
theorem cospanCompIso_inv_app_right : (cospanCompIso F f g).inv.app WalkingCospan.right = ùüô _ :=
  rfl

@[simp]
theorem cospanCompIso_inv_app_one : (cospanCompIso F f g).inv.app WalkingCospan.one = ùüô _ := rfl

end

/-- A functor applied to a span is a span. -/
def spanCompIso (F : C ‚•§ D) {X Y Z : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) :
    span f g ‚ãô F ‚âÖ span (F.map f) (F.map g) :=
  NatIso.ofComponents (by rintro (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©) <;> exact Iso.refl _)
    (by rintro (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©) (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©) f <;> cases f <;> simp)

section

variable (F : C ‚•§ D) {X Y Z : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z)

@[simp]
theorem spanCompIso_app_left : (spanCompIso F f g).app WalkingSpan.left = Iso.refl _ := rfl

@[simp]
theorem spanCompIso_app_right : (spanCompIso F f g).app WalkingSpan.right = Iso.refl _ := rfl

@[simp]
theorem spanCompIso_app_zero : (spanCompIso F f g).app WalkingSpan.zero = Iso.refl _ := rfl

@[simp]
theorem spanCompIso_hom_app_left : (spanCompIso F f g).hom.app WalkingSpan.left = ùüô _ := rfl

@[simp]
theorem spanCompIso_hom_app_right : (spanCompIso F f g).hom.app WalkingSpan.right = ùüô _ := rfl

@[simp]
theorem spanCompIso_hom_app_zero : (spanCompIso F f g).hom.app WalkingSpan.zero = ùüô _ := rfl

@[simp]
theorem spanCompIso_inv_app_left : (spanCompIso F f g).inv.app WalkingSpan.left = ùüô _ := rfl

@[simp]
theorem spanCompIso_inv_app_right : (spanCompIso F f g).inv.app WalkingSpan.right = ùüô _ := rfl

@[simp]
theorem spanCompIso_inv_app_zero : (spanCompIso F f g).inv.app WalkingSpan.zero = ùüô _ := rfl

end

section

variable {X Y Z X' Y' Z' : C} (iX : X ‚âÖ X') (iY : Y ‚âÖ Y') (iZ : Z ‚âÖ Z')

section

variable {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z} {f' : X' ‚ü∂ Z'} {g' : Y' ‚ü∂ Z'}

/-- Construct an isomorphism of cospans from components. -/
def cospanExt (wf : iX.hom ‚â´ f' = f ‚â´ iZ.hom) (wg : iY.hom ‚â´ g' = g ‚â´ iZ.hom) :
    cospan f g ‚âÖ cospan f' g' :=
  NatIso.ofComponents
    (by rintro (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©); exacts [iZ, iX, iY])
    (by rintro (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©) (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©) f <;> cases f <;> simp [wf, wg])

variable (wf : iX.hom ‚â´ f' = f ‚â´ iZ.hom) (wg : iY.hom ‚â´ g' = g ‚â´ iZ.hom)

@[simp]
theorem cospanExt_app_left : (cospanExt iX iY iZ wf wg).app WalkingCospan.left = iX := rfl

@[simp]
theorem cospanExt_app_right : (cospanExt iX iY iZ wf wg).app WalkingCospan.right = iY := rfl

@[simp]
theorem cospanExt_app_one : (cospanExt iX iY iZ wf wg).app WalkingCospan.one = iZ := rfl

@[simp]
theorem cospanExt_hom_app_left :
    (cospanExt iX iY iZ wf wg).hom.app WalkingCospan.left = iX.hom := rfl

@[simp]
theorem cospanExt_hom_app_right :
    (cospanExt iX iY iZ wf wg).hom.app WalkingCospan.right = iY.hom := rfl

@[simp]
theorem cospanExt_hom_app_one : (cospanExt iX iY iZ wf wg).hom.app WalkingCospan.one = iZ.hom := rfl

@[simp]
theorem cospanExt_inv_app_left :
    (cospanExt iX iY iZ wf wg).inv.app WalkingCospan.left = iX.inv := rfl

@[simp]
theorem cospanExt_inv_app_right :
    (cospanExt iX iY iZ wf wg).inv.app WalkingCospan.right = iY.inv :=
  rfl

@[simp]
theorem cospanExt_inv_app_one : (cospanExt iX iY iZ wf wg).inv.app WalkingCospan.one = iZ.inv := by
  rfl

end

section

variable {f : X ‚ü∂ Y} {g : X ‚ü∂ Z} {f' : X' ‚ü∂ Y'} {g' : X' ‚ü∂ Z'}

/-- Construct an isomorphism of spans from components. -/
def spanExt (wf : iX.hom ‚â´ f' = f ‚â´ iY.hom) (wg : iX.hom ‚â´ g' = g ‚â´ iZ.hom) :
    span f g ‚âÖ span f' g' :=
  NatIso.ofComponents (by rintro (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©); exacts [iX, iY, iZ])
    (by rintro (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©) (‚ü®‚ü© | ‚ü®‚ü®‚ü©‚ü©) f <;> cases f <;> simp [wf, wg])

variable (wf : iX.hom ‚â´ f' = f ‚â´ iY.hom) (wg : iX.hom ‚â´ g' = g ‚â´ iZ.hom)

@[simp]
theorem spanExt_app_left : (spanExt iX iY iZ wf wg).app WalkingSpan.left = iY := rfl

@[simp]
theorem spanExt_app_right : (spanExt iX iY iZ wf wg).app WalkingSpan.right = iZ := rfl

@[simp]
theorem spanExt_app_one : (spanExt iX iY iZ wf wg).app WalkingSpan.zero = iX := rfl

@[simp]
theorem spanExt_hom_app_left : (spanExt iX iY iZ wf wg).hom.app WalkingSpan.left = iY.hom := rfl

@[simp]
theorem spanExt_hom_app_right : (spanExt iX iY iZ wf wg).hom.app WalkingSpan.right = iZ.hom := rfl

@[simp]
theorem spanExt_hom_app_zero : (spanExt iX iY iZ wf wg).hom.app WalkingSpan.zero = iX.hom := rfl

@[simp]
theorem spanExt_inv_app_left : (spanExt iX iY iZ wf wg).inv.app WalkingSpan.left = iY.inv := rfl

@[simp]
theorem spanExt_inv_app_right : (spanExt iX iY iZ wf wg).inv.app WalkingSpan.right = iZ.inv := rfl

@[simp]
theorem spanExt_inv_app_zero : (spanExt iX iY iZ wf wg).inv.app WalkingSpan.zero = iX.inv := rfl

end

end

end CategoryTheory.Limits
