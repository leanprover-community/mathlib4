/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.CatCommSq
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.Categorical.CatCospanTransform

/-! # Categorical pullbacks

This file defines the basic properties of categorical pullbacks.

Given a pair of functors `(F : A â¥¤ B, G : C â¥¤ B)`, we define the category
`CategoricalPullback F G` as the category of triples
`(a : A, c : C, e : F.obj a â‰… G.obj b)`.

The category `CategoricalPullback F G` sits in a canonical `CatCommSq`, and we formalize that
this square is a "limit" in the following sense: functors `X â¥¤ CategoricalPullback F G` are
equivalent to pairs of functors `(L : X â¥¤ A, R : X â¥¤ C)` equipped with a natural isomorphism
`L â‹™ F â‰… R â‹™ G`.

We formalize this by introducing a category `CatCommSqOver F G X` that encodes
exactly this data, and we prove that the category of functors `X â¥¤ CategoricalPullback F G` is
equivalent to `CatCommSqOver F G X`.

## Main declarations

* `CategoricalPullback F G`: the type of the categorical pullback.
* `Ï€â‚ F G : CategoricalPullback F G` and `Ï€â‚‚ F G : CategoricalPullback F G`: the canonical
  projections.
* `CategoricalPullback.catCommSq`: the canonical `CatCommSq (Ï€â‚ F G) (Ï€â‚‚ F G) F G` which exhibits
  `CategoricalPullback F G` as the pullback (in the (2,1)-categorical sense)
  of the cospan of `F` and `G`.
* `CategoricalPullback.functorEquiv F G X`: the equivalence of categories between functors
  `X â¥¤ CategoricalPullback F G` and `CatCommSqOver F G X`, where the latter is an abbrev for
  `CategoricalPullback (whiskeringRight X A B|>.obj F) (whiskeringRight X C B|>.obj G)`.

## References
* [Kerodon: section 1.4.5.2](https://kerodon.net/tag/032Y)
* [Niles Johnson, Donald Yau, *2-Dimensional Categories*](https://arxiv.org/abs/2002.06055),
  example 5.3.9, although we take a slightly different (equivalent) model of the object.

## TODOs:
* Define a `CatPullbackSquare` typeclass extending `CatCommSq`that encodes the
  fact that a given `CatCommSq` defines an equivalence between the top left
  corner and the categorical pullback of its legs.
* Define a `IsCatPullbackSquare` propclass.
* Define the "categorical fiber" of a functor at an object of the target category.
* Pasting calculus for categorical pullback squares.
* Categorical pullback squares attached to Grothendieck constructions of pseudofunctors.
* Stability of (co)fibered categories under categorical pullbacks.

-/

universe vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ vâ‚… vâ‚† vâ‚‡ vâ‚ˆ vâ‚‰ vâ‚â‚€ vâ‚â‚ vâ‚â‚‚ vâ‚â‚ƒ
universe uâ‚ uâ‚‚ uâ‚ƒ uâ‚„ uâ‚… uâ‚† uâ‚‡ uâ‚ˆ uâ‚‰ uâ‚â‚€ uâ‚â‚ uâ‚â‚‚ uâ‚â‚ƒ

namespace CategoryTheory.Limits

attribute [local simp] CatCommSq.iso_hom_naturality  CatCommSq.iso_inv_naturality

section

variable {A : Type uâ‚} {B : Type uâ‚‚} {C : Type uâ‚ƒ}
  [Category.{vâ‚} A] [Category.{vâ‚‚} B] [Category.{vâ‚ƒ} C]
  (F : A â¥¤ B) (G : C â¥¤ B)

/-- The `CategoricalPullback F G` is the category of triples
`(a : A, c : C, F a â‰… G c)`.
Morphisms `(a, c, e) âŸ¶ (a', c', e')` are pairs of morphisms
`(fâ‚ : a âŸ¶ a', fâ‚‚ : c âŸ¶ c')` compatible with the specified
isomorphisms. -/
@[kerodon 032Z]
structure CategoricalPullback where
  /-- the first component element -/
  fst : A
  /-- the second component element -/
  snd : C
  /-- the structural isomorphism `F.obj fst â‰… G.obj snd` -/
  iso : F.obj fst â‰… G.obj snd

namespace CategoricalPullback

/-- A notation for the categorical pullback. -/
scoped notation:max L:max " âŠ¡ " R:max => CategoricalPullback L R

variable {F G}

/-- The Hom types for the categorical pullback are given by pairs of maps compatible with the
structural isomorphisms. -/
@[ext]
structure Hom (x y : F âŠ¡ G) where
  /-- the first component of `f : Hom x y` is a morphism `x.fst âŸ¶ y.fst` -/
  fst : x.fst âŸ¶ y.fst
  /-- the second component of `f : Hom x y` is a morphism `x.snd âŸ¶ y.snd` -/
  snd : x.snd âŸ¶ y.snd
  /-- the compatibility condition on `fst` and `snd` with respect to the structure
  isompophisms -/
  w : F.map fst â‰« y.iso.hom = x.iso.hom â‰« G.map snd := by aesop_cat

attribute [reassoc (attr := simp)] Hom.w

@[simps! id_fst id_snd comp_fst comp_snd]
instance : Category (CategoricalPullback F G) where
  Hom x y := CategoricalPullback.Hom x y
  id x :=
    { fst := ğŸ™ x.fst
      snd := ğŸ™ x.snd }
  comp f g :=
    { fst := f.fst â‰« g.fst
      snd := f.snd â‰« g.snd }

attribute [reassoc] comp_fst comp_snd

/-- Naturality square for morphisms in the inverse direction. -/
@[reassoc (attr := simp)]
lemma Hom.w' {x y : F âŠ¡ G} (f : x âŸ¶ y) :
    G.map f.snd â‰« y.iso.inv = x.iso.inv â‰« F.map f.fst := by
  rw [Iso.comp_inv_eq, Category.assoc, Eq.comm, Iso.inv_comp_eq, f.w]

/-- Extensionnality principle for morphisms in `CategoricalPullback F G`. -/
@[ext]
theorem hom_ext {x y : F âŠ¡ G} {f g : x âŸ¶ y}
    (hâ‚— : f.fst = g.fst) (háµ£ : f.snd = g.snd) : f = g := by
  apply Hom.ext <;> assumption

section

variable (F G)

/-- `CategoricalPullback.Ï€â‚ F G` is the first projection `CategoricalPullback F G â¥¤ A`. -/
@[simps]
def Ï€â‚ : F âŠ¡ G â¥¤ A where
  obj x := x.fst
  map f := f.fst

/-- `CategoricalPullback.Ï€â‚‚ F G` is the second projection `CategoricalPullback F G â¥¤ C`. -/
@[simps]
def Ï€â‚‚ : F âŠ¡ G â¥¤ C where
  obj x := x.snd
  map f := f.snd

/-- The canonical categorical commutative square in which `CategoricalPullback F G` sits. -/
@[simps!]
instance catCommSq : CatCommSq (Ï€â‚ F G) (Ï€â‚‚ F G) F G where
  iso := NatIso.ofComponents (fun x â†¦ x.iso)

variable {F G} in
/-- Constructor for isomorphisms in `CategoricalPullback F G`. -/
@[simps!]
def mkIso {x y : F âŠ¡ G}
    (eâ‚— : x.fst â‰… y.fst) (eáµ£ : x.snd â‰… y.snd)
    (w : F.map eâ‚—.hom â‰« y.iso.hom = x.iso.hom â‰« G.map eáµ£.hom := by aesop_cat) :
    x â‰… y where
  hom := âŸ¨eâ‚—.hom, eáµ£.hom, wâŸ©
  inv := âŸ¨eâ‚—.inv, eáµ£.inv, by simpa using F.map eâ‚—.inv â‰«= w.symm =â‰« G.map eáµ£.invâŸ©

section

variable {x y : F âŠ¡ G} (f : x âŸ¶ y) [IsIso f]

instance : IsIso f.fst :=
  inferInstanceAs (IsIso ((Ï€â‚ _ _).mapIso (asIso f)).hom)

instance : IsIso f.snd :=
  inferInstanceAs (IsIso ((Ï€â‚‚ _ _).mapIso (asIso f)).hom)

@[simp]
lemma inv_fst : (inv f).fst = inv f.fst := by
  symm
  apply IsIso.inv_eq_of_hom_inv_id
  simpa [-IsIso.hom_inv_id] using congrArg (fun t â†¦ t.fst) (IsIso.hom_inv_id f)

@[simp]
lemma inv_snd : (inv f).snd = inv f.snd := by
  symm
  apply IsIso.inv_eq_of_hom_inv_id
  simpa [-IsIso.hom_inv_id] using congrArg (fun t â†¦ t.snd) (IsIso.hom_inv_id f)

end

lemma isIso_iff {x y : F âŠ¡ G} (f : x âŸ¶ y) :
    IsIso f â†” (IsIso f.fst âˆ§ IsIso f.snd) where
  mp h := âŸ¨inferInstance, inferInstanceâŸ©
  mpr | âŸ¨hâ‚, hâ‚‚âŸ© => âŸ¨âŸ¨inv f.fst, inv f.snd, by aesop_catâŸ©, by aesop_catâŸ©

end

section

open Functor

variable (X : Type uâ‚„) [Category.{vâ‚„} X]

variable (F G) in
/-- The data of a categorical commutative square over a cospan `F, G` with cone point `X` is
that of a functor `T : X â¥¤ A`, a functor `L : X â¥¤ C`, and a `CatCommSqOver T L F G`.
Note that this is *exactly* what an object of
`((whiskeringRight X A B).obj F) âŠ¡ ((whiskeringRight X C B).obj G)` is,
so `CatCommSqOver F G X` is in fact an abbreviation for
`((whiskeringRight X A B).obj F) âŠ¡ ((whiskeringRight X C B).obj G)`. -/
abbrev CatCommSqOver :=
  (whiskeringRight X A B|>.obj F) âŠ¡ (whiskeringRight X C B|>.obj G)

namespace CatCommSqOver

/-- Interpret a `CatCommSqOver F Gâ€¯X` as a `CatCommSq`. -/
@[simps]
instance asSquare (S : CatCommSqOver F G X) : CatCommSq S.fst S.snd F G where
  iso := S.iso

@[reassoc (attr := simp)]
lemma iso_hom_naturality (S : CatCommSqOver F G X) {x x' : X} (f : x âŸ¶ x') :
   F.map (S.fst.map f) â‰« S.iso.hom.app x' =
   S.iso.hom.app x â‰« G.map (S.snd.map f) :=
  S.iso.hom.naturality f

@[reassoc (attr := simp)]
lemma w_app {S S' : CatCommSqOver F G X} (Ï† : S âŸ¶ S') (x : X) :
    F.map (Ï†.fst.app x) â‰« S'.iso.hom.app x =
    S.iso.hom.app x â‰« G.map (Ï†.snd.app x) :=
  NatTrans.congr_app Ï†.w x

variable (F G)

/-- The "first projection" of a CatCommSqOver as a functor. -/
abbrev fstFunctor : CatCommSqOver F G X â¥¤ X â¥¤ A := Ï€â‚ _ _

/-- The "second projection" of a CatCommSqOver as a functor. -/
abbrev sndFunctor : CatCommSqOver F G X â¥¤ X â¥¤ C := Ï€â‚‚ _ _

/-- The structure isompophism of a `CatCommSqOver` as a natural transformation. -/
abbrev e :
    fstFunctor F G X â‹™ (whiskeringRight X A B).obj F â‰…
    sndFunctor F G X â‹™ (whiskeringRight X C B).obj G :=
  NatIso.ofComponents (fun S â†¦ S.iso)

/-- There is a canonical inhabitant of
`CatCommSqOver F G (CategoricalPullback F G)` corresponding to the
canonical square `catCommSq` -/
@[simps]
instance : Inhabited (CatCommSqOver F G <| F âŠ¡ G) where
  default :=
    { fst := CategoricalPullback.Ï€â‚ F G
      snd := CategoricalPullback.Ï€â‚‚ F G
      iso := (catCommSq F G).iso }

-- this is a non-diamond
example : (default : CatCommSqOver F G <| F âŠ¡ G).asSquare = catCommSq F G := rfl

end CatCommSqOver

section functorEquiv

variable (F G)

-- We need to split up the definition of `functorEquiv` to avoid timeouts.

/-- Interpret a functor to the categorical pullback as a `CatCommSqOver`. -/
@[simps]
def toCatCommSqOver : (X â¥¤ F âŠ¡ G) â¥¤ CatCommSqOver F G X where
  obj J :=
    { fst := J â‹™ Ï€â‚ F G
      snd := J â‹™ Ï€â‚‚ F G
      iso :=
        associator _ _ _ â‰ªâ‰«
          isoWhiskerLeft J (catCommSq F G).iso â‰ªâ‰«
          (associator _ _ _).symm }
  map {J J'} F :=
    { fst := whiskerRight F (Ï€â‚ _ _)
      snd := whiskerRight F (Ï€â‚‚ _ _) }

/-- Interpret a `CatCommSqOver` as a functor to the categorical pullback. -/
@[simps]
def CatCommSqOver.toFunctorToCategoricalPullback :
    (CatCommSqOver F G X) â¥¤ X â¥¤ F âŠ¡ G where
  obj S :=
    { obj x :=
        { fst := S.fst.obj x
          snd := S.snd.obj x
          iso := S.iso.app x }
      map {x y} f :=
        { fst := S.fst.map f
          snd := S.snd.map f } }
  map {S S'} Ï† :=
    { app x :=
        { fst := Ï†.fst.app x
          snd := Ï†.snd.app x } }

/-- The universal property of categorical pullbacks, stated as an equivalence
of categories between functors `X â¥¤ (F âŠ¡ G)` and categorical commutative squares
over X. -/
@[simps]
def functorEquiv : (X â¥¤ F âŠ¡ G) â‰Œ CatCommSqOver F G X where
  functor := toCatCommSqOver F G X
  inverse := CatCommSqOver.toFunctorToCategoricalPullback F G X
  unitIso :=
    NatIso.ofComponents
      (fun _ â†¦ NatIso.ofComponents
        (fun _ â†¦ CategoricalPullback.mkIso (.refl _) (.refl _)))
  counitIso :=
    NatIso.ofComponents
      (fun _ â†¦ CategoricalPullback.mkIso
        (NatIso.ofComponents (fun _ â†¦ .refl _)) (NatIso.ofComponents (fun _ â†¦ .refl _)))

/-- The default `CatCommSqOver F G (CategoricalPullback F G)` corresponds to
the identity functor through `functorEquiv`. -/
@[simps!]
def functorEquivFunctorIdIsoDefault :
    (functorEquiv F G <| F âŠ¡ G).functor.obj (ğŸ­ _) â‰… default :=
  CategoricalPullback.mkIso (Functor.leftUnitor _) (Functor.leftUnitor _)

/-- The default `CatCommSqOver F G (CategoricalPullback F G)` corresponds to
the identity functor through `functorEquiv`. -/
@[simps!]
def functorEquivInverseDefaultIso :
    (functorEquiv F G <| F âŠ¡ G).inverse.obj (default) â‰… ğŸ­ (F âŠ¡ G) :=
  .refl _

/-- The isomorphisms `functorEquivInverseDefaultIso` is the one induced
by `functorEquivFunctorIdIsoDefault` through the equivalence `functorEquiv`. -/
lemma functorEquivInverseDefaultIso_eq :
    (functorEquivInverseDefaultIso F G) =
    (functorEquiv F G <| F âŠ¡ G).inverse.mapIso
      (functorEquivFunctorIdIsoDefault F G).symm â‰ªâ‰«
      (functorEquiv F G F âŠ¡ G).unitIso.symm.app _ := by
  aesop_cat

variable {F G X}

/-- A constructor for natural isomorphisms of functors `X â¥¤ CategoricalPullback`: to
construct such an isomorphism, it suffices to produce isomorphisms after whiskering with
the projections, and compatible with the canonical 2-commutative square . -/
@[simps!]
def mkNatIso {J K : X â¥¤ F âŠ¡ G}
    (eâ‚ : J â‹™ Ï€â‚ F G â‰… K â‹™ Ï€â‚ F G) (eâ‚‚ : J â‹™ Ï€â‚‚ F G â‰… K â‹™ Ï€â‚‚ F G)
    (coh :
      whiskerRight eâ‚.hom F â‰« (associator _ _ _).hom â‰«
        whiskerLeft K (CatCommSq.iso (Ï€â‚ F G) (Ï€â‚‚ F G) F G).hom â‰«
        (associator _ _ _).inv =
      (associator _ _ _).hom â‰«
        whiskerLeft J (CatCommSq.iso (Ï€â‚ F G) (Ï€â‚‚ F G) F G).hom â‰«
        (associator _ _ _).inv â‰«
        whiskerRight eâ‚‚.hom G := by aesop_cat) :
    J â‰… K :=
  NatIso.ofComponents
    (fun x â†¦ CategoricalPullback.mkIso (eâ‚.app x) (eâ‚‚.app x)
      (by simpa using NatTrans.congr_app coh x))
    (fun {_ _} f â†¦ by
      ext
      Â· exact eâ‚.hom.naturality f
      Â· exact eâ‚‚.hom.naturality f)

/-- To check equality of two natural transformations of functors to a `CategoricalPullback`, it
suffices to do so after whiskering with the projections. -/
@[ext]
lemma natTrans_ext
    {J K : X â¥¤ F âŠ¡ G} {Î± Î² : J âŸ¶ K}
    (eâ‚ : whiskerRight Î± (Ï€â‚ F G) = whiskerRight Î² (Ï€â‚ F G))
    (eâ‚‚ : whiskerRight Î± (Ï€â‚‚ F G) = whiskerRight Î² (Ï€â‚‚ F G)) :
    Î± = Î² := by
  ext x
  Â· exact congrArg (fun t â†¦ t.app x) eâ‚
  Â· exact congrArg (fun t â†¦ t.app x) eâ‚‚

section

variable {J K : X â¥¤ F âŠ¡ G}
    (eâ‚ : J â‹™ Ï€â‚ F G â‰… K â‹™ Ï€â‚ F G) (eâ‚‚ : J â‹™ Ï€â‚‚ F G â‰… K â‹™ Ï€â‚‚ F G)
    (coh :
      whiskerRight eâ‚.hom F â‰« (associator _ _ _).hom â‰«
        whiskerLeft K (CatCommSq.iso (Ï€â‚ F G) (Ï€â‚‚ F G) F G).hom â‰«
        (associator _ _ _).inv =
      (associator _ _ _).hom â‰«
        whiskerLeft J (CatCommSq.iso (Ï€â‚ F G) (Ï€â‚‚ F G) F G).hom â‰«
        (associator _ _ _).inv â‰«
        whiskerRight eâ‚‚.hom G := by aesop_cat)

@[simp]
lemma toCatCommSqOver_mapIso_mkNatIso_eq_mkIso :
    (toCatCommSqOver F G X).mapIso (mkNatIso eâ‚ eâ‚‚ coh) =
    CategoricalPullback.mkIso eâ‚ eâ‚‚
      (by simpa [functorEquiv, toCatCommSqOver] using coh) := by
  aesop

/-- Comparing mkNatIso with the corresponding construction one can deduce from
`functorEquiv`. -/
lemma mkNatIso_eq :
    mkNatIso eâ‚ eâ‚‚ coh =
    (functorEquiv F G X).fullyFaithfulFunctor.preimageIso
      (CategoricalPullback.mkIso eâ‚ eâ‚‚
        (by simpa [functorEquiv, toCatCommSqOver] using coh)) := by
  rw [â† toCatCommSqOver_mapIso_mkNatIso_eq_mkIso eâ‚ eâ‚‚ coh]
  dsimp [Equivalence.fullyFaithfulFunctor]
  aesop_cat

end

end functorEquiv

end

section Pseudofunctoriality

namespace CatCommSqOver
open Functor

section transform

variable {A' : Type uâ‚„} {B' : Type uâ‚…} {C' : Type uâ‚†}
  [Category.{vâ‚„} A'] [Category.{vâ‚…} B'] [Category.{vâ‚†} C']
  {F' : A' â¥¤ B'} {G' : C' â¥¤ B'}

/-- Transform a `CatCommSqOver F G X` by "whiskering it" with a
`CatCospanTransform`. -/
@[simps]
def transform (X : Type uâ‚‡) [Category.{vâ‚‡} X]
    (Ïˆ : CatCospanTransform F G F' G') :
    CatCommSqOver F G X â¥¤ CatCommSqOver F' G' X where
  obj S :=
    { fst := S.fst â‹™ Ïˆ.left
      snd := S.snd â‹™ Ïˆ.right
      iso :=
        (Functor.associator _ _ _) â‰ªâ‰«
          isoWhiskerLeft S.fst (Ïˆ.squareLeft.iso.symm) â‰ªâ‰«
          (Functor.associator _ _ _).symm â‰ªâ‰«
          isoWhiskerRight S.iso _ â‰ªâ‰«
          isoWhiskerLeft S.snd (Ïˆ.squareRight.iso) â‰ªâ‰«
          (Functor.associator _ _ _).symm }
  map {x y} f :=
    { fst := whiskerRight f.fst Ïˆ.left
      snd := whiskerRight f.snd Ïˆ.right
      w := by
        ext x
        dsimp
        simp only [Category.comp_id, Category.id_comp,
          CatCommSq.iso_inv_naturality_assoc, Category.assoc,
          NatIso.cancel_natIso_inv_left, Functor.comp_obj]
        simp [â† Functor.map_comp_assoc] }

/-- A morphism of `CatCospanTransform` induce a natural transformation of
the functor they induce on `CatCommSqOver`. -/
@[simps]
def transformâ‚‚ (X : Type uâ‚‡) [Category.{vâ‚‡} X]
    {Ïˆ Ïˆ' : CatCospanTransform F G F' G'} (Î· : Ïˆ âŸ¶ Ïˆ') :
    transform X Ïˆ âŸ¶ transform X Ïˆ' where
  app S :=
    { fst := { app y := Î·.left.app (S.fst.obj y) }
      snd := { app y := Î·.right.app (S.snd.obj y) }
      w := by
        ext t
        dsimp
        simp only [Category.comp_id, Category.id_comp, Category.assoc,
          CatCospanTransformMorphism.right_coherence_app, Functor.comp_obj,
          NatTrans.naturality_assoc]
        haveI := Ïˆ.squareLeft.iso.inv.app (S.fst.obj t) â‰«=
          Î·.left_coherence_app (S.fst.obj t)
        simp only [Iso.inv_hom_id_app_assoc] at this
        simp [this] }

section transformâ‚‚

variable (X : Type uâ‚‡) [Category.{vâ‚‡} X]

@[simp]
lemma transformâ‚‚_id (Ïˆ : CatCospanTransform F G F' G') :
    transformâ‚‚ X (ğŸ™ Ïˆ) = ğŸ™ _ := by aesop_cat

@[reassoc, simp]
lemma transformâ‚‚_comp {Ïˆ Ïˆ' Ïˆ'' : CatCospanTransform F G F' G'}
    (Î± : Ïˆ âŸ¶ Ïˆ') (Î² : Ïˆ' âŸ¶ Ïˆ'') :
    transformâ‚‚ X (Î± â‰« Î²) = transformâ‚‚ X Î± â‰« transformâ‚‚ X Î² := by
  aesop_cat

/-- Extend `transformâ‚‚` to isomorphisms. -/
@[simps]
def transformâ‚‚Iso {Ïˆ Ïˆ' : CatCospanTransform F G F' G'} (Î± : Ïˆ â‰… Ïˆ') :
    transform X Ïˆ â‰… transform X Ïˆ' where
  hom := transformâ‚‚ X Î±.hom
  inv := transformâ‚‚ X Î±.inv
  hom_inv_id := by simp [â† transformâ‚‚_comp]
  inv_hom_id := by simp [â† transformâ‚‚_comp]

end transformâ‚‚

variable {A'' : Type uâ‚‡} {B'' : Type uâ‚ˆ} {C'' : Type uâ‚‰}
  [Category.{vâ‚‡} A''] [Category.{vâ‚ˆ} B''] [Category.{vâ‚‰} C'']
  {F'' : A'' â¥¤ B''} {G'' : C'' â¥¤ B''}

/-- The construction `CatCommSqOver.transform` respects vertical composition
of `CatCospanTransform`. -/
@[simps!]
def transformComp (X : Type uâ‚â‚€) [Category.{vâ‚â‚€} X]
    (Ïˆ : CatCospanTransform F G F' G') (Ïˆ' : CatCospanTransform F' G' F'' G'') :
    transform X (Ïˆ.comp Ïˆ') â‰… (transform X Ïˆ) â‹™ (transform X Ïˆ') :=
  NatIso.ofComponents fun _ =>
    CategoricalPullback.mkIso
      (Functor.associator _ _ _).symm
      (Functor.associator _ _ _).symm

/-- The construction `CatCommSqOver.transform` respects the identity
`CatCospanTransform`. -/
@[simps!]
def transformId (X : Type uâ‚„) [Category.{vâ‚„} X]
    (F : A â¥¤ B) (G : C â¥¤ B) :
    transform X (CatCospanTransform.id F G) â‰… ğŸ­ _ :=
  NatIso.ofComponents fun _ =>
    CategoricalPullback.mkIso
      (Functor.rightUnitor _)
      (Functor.rightUnitor _)

open scoped CatCospanTransform

lemma transformâ‚‚_whiskerLeft
    (X : Type uâ‚‡) [Category.{vâ‚‡} X]
    (Ïˆ : CatCospanTransform F G F' G')
    {Ï† Ï†' : CatCospanTransform F' G' F'' G''} (Î± : Ï† âŸ¶ Ï†') :
    transformâ‚‚ X (Ïˆ â— Î±) =
    (transformComp X Ïˆ Ï†).hom â‰«
      whiskerLeft (transform X Ïˆ) (transformâ‚‚ X Î±) â‰«
      (transformComp X Ïˆ Ï†').inv := by
  aesop_cat

lemma transformâ‚‚_whiskerRight
    (X : Type uâ‚‡) [Category.{vâ‚‡} X]
    {Ïˆ Ïˆ' : CatCospanTransform F G F' G'} (Î± : Ïˆ âŸ¶ Ïˆ')
    (Ï† : CatCospanTransform F' G' F'' G'') :
    transformâ‚‚ X (Î± â–· Ï†) =
    (transformComp X Ïˆ Ï†).hom â‰«
      whiskerRight (transformâ‚‚ X Î±) (transform X Ï†) â‰«
      (transformComp X Ïˆ' Ï†).inv := by
  aesop_cat

lemma transformâ‚‚_associator
    {A''' : Type uâ‚â‚€} {B''' : Type uâ‚â‚} {C''' : Type uâ‚â‚‚}
    [Category.{vâ‚â‚€} A'''] [Category.{vâ‚â‚} B'''] [Category.{vâ‚â‚‚} C''']
    {F''' : A''' â¥¤ B'''} {G''' : C''' â¥¤ B'''}
    (X : Type uâ‚â‚ƒ) [Category.{vâ‚â‚ƒ} X]
    (Ïˆ : CatCospanTransform F G F' G') (Ï† : CatCospanTransform F' G' F'' G'')
    (Ï„ : CatCospanTransform F'' G'' F''' G''') :
    transformâ‚‚ X (Î±_ Ïˆ Ï† Ï„).hom =
    (transformComp X (Ïˆ.comp Ï†) Ï„).hom â‰«
      whiskerRight (transformComp X Ïˆ Ï†).hom (transform X Ï„) â‰«
      ((transform X Ïˆ).associator (transform X Ï†) (transform X Ï„)).hom â‰«
      whiskerLeft (transform X Ïˆ) (transformComp X Ï† Ï„).inv â‰«
      (transformComp X Ïˆ (Ï†.comp Ï„)).inv := by
  aesop_cat

lemma transformâ‚‚_leftUnitor (X : Type uâ‚‡) [Category.{vâ‚‡} X]
    (Ïˆ : CatCospanTransform F G F' G') :
    transformâ‚‚ X (Î»_ Ïˆ).hom =
    (transformComp X (.id F G) Ïˆ).hom â‰«
      whiskerRight (transformId X F G).hom (transform X Ïˆ) â‰«
      (transform X Ïˆ).leftUnitor.hom := by
  aesop_cat

lemma transformâ‚‚_rightUnitor (X : Type uâ‚‡) [Category.{vâ‚‡} X]
    (Ïˆ : CatCospanTransform F G F' G') :
    transformâ‚‚ X (Ï_ Ïˆ).hom =
    (transformComp X Ïˆ (.id F' G')).hom â‰«
      whiskerLeft  (transform X Ïˆ) (transformId X F' G').hom â‰«
      (transform X Ïˆ).rightUnitor.hom := by
  aesop_cat

end transform

section precompose

variable (F G)

variable
    {X : Type uâ‚„} {Y : Type uâ‚…} {Z : Type uâ‚†}
    [Category.{vâ‚„} X] [Category.{vâ‚…} Y] [Category.{vâ‚†} Z]

/-- A functor `U : X â¥¤ Y` induces a functor
`CatCommSqOver F G Y â¥¤ CatCommSqOver F G X` by whiskering left the underlying
categorical commutative square by U. -/
@[simps]
def precompose (U : X â¥¤ Y) : CatCommSqOver F G Y â¥¤ CatCommSqOver F G X where
  obj S :=
    { fst := U â‹™ S.fst
      snd := U â‹™ S.snd
      iso :=
        (Functor.associator _ _ _) â‰ªâ‰«
          isoWhiskerLeft U S.iso â‰ªâ‰«
          (Functor.associator _ _ _).symm }
  map {S S'} Ï† :=
    { fst := whiskerLeft U Ï†.fst
      snd := whiskerLeft U Ï†.snd }

variable (X) in
/-- The construction `precompose` respects functor identities. -/
@[simps!]
def precomposeId :
    precompose F G (ğŸ­ X) â‰… ğŸ­ (CatCommSqOver F G X) :=
  NatIso.ofComponents fun _ =>
    CategoricalPullback.mkIso (Functor.leftUnitor _) (Functor.leftUnitor _)

/-- The construction `precompose` respects functor composition. -/
@[simps!]
def precomposeComp (U : X â¥¤ Y) (V : Y â¥¤ Z) :
    precompose F G (U â‹™ V) â‰… precompose F G V â‹™ precompose F G U :=
  NatIso.ofComponents fun _ =>
    CategoricalPullback.mkIso
      (Functor.associator _ _ _)
      (Functor.associator _ _ _)

/-- The construction `precompose` acts on natural transformations. -/
@[simps]
def precomposeâ‚‚ {U V : X â¥¤ Y} (Î± : U âŸ¶ V) :
    precompose F G U âŸ¶ precompose F G V where
  app x :=
    { fst := whiskerRight Î± x.fst
      snd := whiskerRight Î± x.snd }

@[simp]
lemma precomposeâ‚‚_id (U : X â¥¤ Y) : precomposeâ‚‚ F G (ğŸ™ U) = ğŸ™ _ := by aesop_cat

@[simp]
lemma precomposeâ‚‚_comp {U V W: X â¥¤ Y} (Î± : U âŸ¶ V) (Î² : V âŸ¶ W) :
    precomposeâ‚‚ F G (Î± â‰« Î²) = precomposeâ‚‚ F G Î± â‰« precomposeâ‚‚ F G Î² := by
  aesop_cat

/-- Extend `precomposeâ‚‚` to isomorphisms. -/
@[simps]
def precomposeâ‚‚Iso {U V : X â¥¤ Y} (Î± : U â‰… V) :
    precompose F G U â‰… precompose F G V where
  hom := precomposeâ‚‚ F G Î±.hom
  inv := precomposeâ‚‚ F G Î±.inv
  hom_inv_id := by simp [â† precomposeâ‚‚_comp]
  inv_hom_id := by simp [â† precomposeâ‚‚_comp]

lemma precomposeâ‚‚_whiskerLeft (U : X â¥¤ Y) {V W : Y â¥¤ Z} (Î± : V âŸ¶ W) :
    precomposeâ‚‚ F G (whiskerLeft U Î±) =
    (precomposeComp F G U V).hom â‰«
      whiskerRight (precomposeâ‚‚ F G Î±) (precompose F G U) â‰«
      (precomposeComp F G U W).inv := by
  aesop_cat

lemma precomposeâ‚‚_whiskerRight {U V : X â¥¤ Y} (Î± : U âŸ¶ V) (W : Y â¥¤ Z) :
    precomposeâ‚‚ F G (whiskerRight Î± W) =
    (precomposeComp F G U W).hom â‰«
      whiskerLeft (precompose F G W) (precomposeâ‚‚ F G Î±) â‰«
      (precomposeComp F G V W).inv := by
  aesop_cat

lemma precomposeâ‚‚_associator {T : Type uâ‚‡} [Category.{vâ‚‡} T]
    (U : X â¥¤ Y) (V : Y â¥¤ Z) (W : Z â¥¤ T) :
    precomposeâ‚‚ F G (U.associator V W).hom =
    (precomposeComp F G (U â‹™ V) W).hom â‰«
      whiskerLeft (precompose F G W) (precomposeComp F G U V).hom â‰«
      ((precompose F G W).associator _ _).inv â‰«
      whiskerRight (precomposeComp F G V W).inv (precompose F G U) â‰«
      (precomposeComp F G _ _).inv := by
  aesop_cat

lemma precomposeâ‚‚_leftUnitor (U : X â¥¤ Y) :
    precomposeâ‚‚ F G U.leftUnitor.hom =
    (precomposeComp F G (ğŸ­ _) U).hom â‰«
      whiskerLeft (precompose F G U) (precomposeId F G X).hom â‰«
      (Functor.rightUnitor _).hom := by
  aesop_cat

lemma precomposeâ‚‚_rightUnitor (U : X â¥¤ Y) :
    precomposeâ‚‚ F G U.rightUnitor.hom =
    (precomposeComp F G U (ğŸ­ _)).hom â‰«
      whiskerRight (precomposeId F G Y).hom (precompose F G U) â‰«
      (Functor.leftUnitor _).hom := by
  aesop_cat

end precompose

section compatibility

variable {A' : Type uâ‚„} {B' : Type uâ‚…} {C' : Type uâ‚†}
  [Category.{vâ‚„} A'] [Category.{vâ‚…} B'] [Category.{vâ‚†} C']
  {F' : A' â¥¤ B'} {G' : C' â¥¤ B'}

/-- The canonical compatibility square between `precompose` and `transform`.
This is a "naturality square" if we think as `transform _` as the
(app component of the) map component of a pseudofunctor from the bicategory of
categorical cospans with value in pseudofunctors
(its value on the categorical cospan `F, G` being the pseudofunctor
`precompose F G _`). -/
@[simps!]
instance precomposeTransformSquare
    {X : Type uâ‚‡} {Y : Type uâ‚ˆ} [Category.{vâ‚‡} X] [Category.{vâ‚ˆ} Y]
    (Ïˆ : CatCospanTransform F G F' G') (U : X â¥¤ Y) :
    CatCommSq
      (precompose F G U) (transform Y Ïˆ)
      (transform X Ïˆ) (precompose F' G' U) where
  iso := NatIso.ofComponents fun _ =>
    CategoricalPullback.mkIso
      (Functor.associator _ _ _)
      (Functor.associator _ _ _)

-- Compare the next 3 lemmas with the components of a strong natural transform
-- of pseudofunctors

/-- The naturality square `precomposeTransformSquare` is itself natural. -/
lemma precomposeTransformSquare_iso_hom_naturalityâ‚‚
    {X : Type uâ‚‡} {Y : Type uâ‚ˆ} [Category.{vâ‚‡} X] [Category.{vâ‚ˆ} Y]
    (Ïˆ : CatCospanTransform F G F' G')
    {U V : X â¥¤ Y} (Î± : U âŸ¶ V) :
    whiskerRight (precomposeâ‚‚ F G Î±) (transform X Ïˆ) â‰«
      (CatCommSq.iso _ (transform Y Ïˆ) _ (precompose F' G' V)).hom =
    (CatCommSq.iso _ (transform Y Ïˆ) _ (precompose F' G' U)).hom â‰«
      whiskerLeft (transform _ _) (precomposeâ‚‚ F' G' Î±) := by
  aesop_cat

/-- The naturality square `precomposeTransformSquare` respects identities. -/
lemma precomposeTransformSquare_iso_hom_id
    (Ïˆ : CatCospanTransform F G F' G') (X : Type uâ‚‡) [Category.{vâ‚‡} X] :
    (CatCommSq.iso (precompose F G <| ğŸ­ X) (transform X Ïˆ)
      (transform X Ïˆ) (precompose F' G' <| ğŸ­ X)).hom â‰«
      whiskerLeft (transform X Ïˆ) (precomposeId F' G' X).hom =
    whiskerRight (precomposeId F G X).hom (transform X Ïˆ) â‰«
    (Functor.leftUnitor _).hom â‰« (Functor.rightUnitor _).inv := by
  aesop_cat

/-- The naturality square `precomposeTransformSquare` respects compositions. -/
lemma precomposeTransformSquare_iso_hom_comp
    {X : Type uâ‚‡} {Y : Type uâ‚ˆ} {Z : Type uâ‚‰}
    [Category.{vâ‚‡} X] [Category.{vâ‚ˆ} Y] [Category.{vâ‚‰} Z]
    (Ïˆ : CatCospanTransform F G F' G')
    (U : X â¥¤ Y) (V : Y â¥¤ Z) :
    (CatCommSq.iso (precompose F G <| U â‹™ V) (transform Z Ïˆ)
      (transform X Ïˆ) (precompose F' G' <| U â‹™ V)).hom â‰«
      whiskerLeft (transform Z Ïˆ) (precomposeComp F' G' U V).hom =
    whiskerRight (precomposeComp F G U V).hom (transform X Ïˆ) â‰«
      (Functor.associator _ _ _).hom â‰«
      whiskerLeft (precompose F G V)
        (CatCommSq.iso _ (transform _ Ïˆ) _ _).hom â‰«
      (Functor.associator _ _ _).inv â‰«
      whiskerRight (CatCommSq.iso _ _ _ _).hom
        (precompose F' G' U) â‰«
      (Functor.associator _ _ _).hom := by
  aesop_cat

/-- The canonical compatibility square between `transform` and `precompose`.
This is a "naturality square" if we think as `precompose` as the
(app component of the) map component of a pseudofunctor from the opposite
bicategory of categories to pseudofunctors of categorical cospans
(its value on `X` being the pseudofunctor `transform X _`). -/
@[simps!]
instance transformPrecomposeSquare
    {X : Type uâ‚‡} {Y : Type uâ‚ˆ} [Category.{vâ‚‡} X] [Category.{vâ‚ˆ} Y]
    (U : X â¥¤ Y) (Ïˆ : CatCospanTransform F G F' G') :
    CatCommSq
      (transform Y Ïˆ) (precompose F G U)
      (precompose F' G' U) (transform X Ïˆ) where
  iso := NatIso.ofComponents fun _ =>
    CategoricalPullback.mkIso
      (Functor.associator _ _ _).symm
      (Functor.associator _ _ _).symm

-- Compare the next 3 lemmas with the components of a strong natural transform
-- of pseudofunctors

/-- The naturality square `transformPrecomposeSquare` is itself natural. -/
lemma transformPrecomposeSquare_iso_hom_naturalityâ‚‚
    {X : Type uâ‚‡} {Y : Type uâ‚ˆ} [Category.{vâ‚‡} X] [Category.{vâ‚ˆ} Y]
    (U : X â¥¤ Y) {Ïˆ Ïˆ' : CatCospanTransform F G F' G'} (Î· : Ïˆ âŸ¶ Ïˆ') :
    whiskerRight (transformâ‚‚ Y Î·) (precompose F' G' U) â‰«
      (CatCommSq.iso _ (precompose F G U) _ (transform X Ïˆ')).hom =
    (CatCommSq.iso _ (precompose F G U) _ (transform X Ïˆ)).hom â‰«
      whiskerLeft (precompose F G U) (transformâ‚‚ X Î·) := by
  aesop_cat

/-- The naturality square `transformPrecomposeSquare` respects identities. -/
lemma transformPrecomposeSquare_iso_hom_id
    {X : Type uâ‚‡} {Y : Type uâ‚ˆ} [Category.{vâ‚‡} X] [Category.{vâ‚ˆ} Y]
    (U : X â¥¤ Y) (F : A â¥¤ B) (G : C â¥¤ B) :
    (CatCommSq.iso (transform Y (.id F G)) (precompose F G U)
      (precompose F G U) (transform X (.id F G))).hom â‰«
      whiskerLeft (precompose F G U) (transformId X F G).hom =
    whiskerRight (transformId Y F G).hom (precompose F G U) â‰«
      (precompose F G U).leftUnitor.hom â‰«
      (precompose F G U).rightUnitor.inv := by
  aesop_cat

/-- The naturality square `transformPrecomposeSquare` respects compositions. -/
lemma transformPrecomposeSquare_iso_hom_comp
    {A'' : Type uâ‚‡} {B'' : Type uâ‚ˆ} {C'' : Type uâ‚‰}
    [Category.{vâ‚‡} A''] [Category.{vâ‚ˆ} B''] [Category.{vâ‚‰} C'']
    {F'' : A'' â¥¤ B''} {G'' : C'' â¥¤ B''}
    {X : Type uâ‚â‚€} {Y : Type uâ‚â‚} [Category.{vâ‚â‚€} X] [Category.{vâ‚â‚} Y]
    (U : X â¥¤ Y) (Ïˆ : CatCospanTransform F G F' G')
    (Ïˆ' : CatCospanTransform F' G' F'' G'') :
    (CatCommSq.iso (transform Y (Ïˆ.comp Ïˆ')) (precompose F G U)
      (precompose F'' G'' U) (transform X (Ïˆ.comp Ïˆ'))).hom â‰«
      whiskerLeft (precompose F G U) (transformComp X Ïˆ Ïˆ').hom =
    whiskerRight (transformComp Y Ïˆ Ïˆ').hom (precompose F'' G'' U) â‰«
      (Functor.associator _ _ _).hom â‰«
      whiskerLeft (transform Y Ïˆ)
        (CatCommSq.iso _ (precompose F' G' U) _ (transform X Ïˆ')).hom â‰«
      (Functor.associator _ _ _).inv â‰«
      whiskerRight (CatCommSq.iso _ _ _ _).hom (transform X Ïˆ') â‰«
      (Functor.associator _ _ _).hom := by
  aesop_cat

end compatibility

end CatCommSqOver

open CatCommSqOver

-- Note that as `functorEquiv` has @[simps], itâ€™s better in terms of confluence
-- to state the CatCommSq below in terms of `toCatCommSqOver` and
-- `toFunctorToCategoricalPullback` rather than in terms of
-- `functorEquiv _ _ _|>.functor` and `functorEquiv _ _ _|>.inverse`

/-- The equivalence `functorEquiv` identifies the functoriality
on `X` of `X â¥¤ F âŠ¡ G` and `CatCommSqOver F G X`. -/
@[simps!]
instance whiskeringLeftToCatCommSqOverSquare
    {X : Type uâ‚‡} {Y : Type uâ‚ˆ} [Category.{vâ‚‡} X] [Category.{vâ‚ˆ} Y]
    (U : X â¥¤ Y) :
    CatCommSq
      (Functor.whiskeringLeft X Y (F âŠ¡ G)|>.obj U)
      (toCatCommSqOver F G Y)
      (toCatCommSqOver F G X)
      (precompose F G U) where
  iso :=
    NatIso.ofComponents fun _ =>
      CategoricalPullback.mkIso
        (Functor.associator _ _ _)
        (Functor.associator _ _ _)

/-- The equivalence `functorEquiv` identifies the functoriality
on `X` of `X â¥¤ F âŠ¡ G` and `CatCommSqOver F G X` (inverse direction). -/
@[simps!]
instance precomposeToFunctorToCategoricalPullbackSquare
    {X : Type uâ‚‡} {Y : Type uâ‚ˆ} [Category.{vâ‚‡} X] [Category.{vâ‚ˆ} Y]
    (U : X â¥¤ Y) :
    CatCommSq
      (precompose F G U)
      (toFunctorToCategoricalPullback F G Y)
      (toFunctorToCategoricalPullback F G X)
      (Functor.whiskeringLeft X Y (F âŠ¡ G)|>.obj U) :=
  CatCommSq.vInv _ (functorEquiv F G _) (functorEquiv F G _) _
    (whiskeringLeftToCatCommSqOverSquare _)

variable {A' : Type uâ‚„} {B' : Type uâ‚…} {C' : Type uâ‚†}
  [Category.{vâ‚„} A'] [Category.{vâ‚…} B'] [Category.{vâ‚†} C']
  {F' : A' â¥¤ B'} {G' : C' â¥¤ B'}
  {A'' : Type uâ‚‡} {B'' : Type uâ‚ˆ} {C'' : Type uâ‚‰}
  [Category.{vâ‚‡} A''] [Category.{vâ‚ˆ} B''] [Category.{vâ‚‰} C'']
  {F'' : A'' â¥¤ B''} {G'' : C'' â¥¤ B''}

/-- A `CatCospanTransform` induces a functor between the categorical pullbacks. -/
@[simps!]
def functorOfTransform (Ïˆ : CatCospanTransform F G F' G') : F âŠ¡ G â¥¤ F' âŠ¡ G' :=
  functorEquiv F' G' F âŠ¡ G|>.inverse.obj <|
    (CatCommSqOver.transform _ Ïˆ).obj default

/-- The canonical square that expresses that `toCatCommSqOver` maps
(postcomposition by) `functorOfTransform` to `CatCommSqOver.transform`. -/
@[simps!]
instance toCatCommSqOverWhiskeringFunctorOfTransformSquare
    (X : Type uâ‚â‚€) [Category.{vâ‚â‚€} X]
    (Ïˆ : CatCospanTransform F G F' G') :
    CatCommSq
      (toCatCommSqOver F G X)
      (Functor.whiskeringRight X _ _|>.obj <| functorOfTransform Ïˆ)
      (transform _ Ïˆ)
      (toCatCommSqOver F' G' X) where
  iso := NatIso.ofComponents fun _ =>
    CategoricalPullback.mkIso
      (NatIso.ofComponents fun _ â†¦ .refl _)
      (NatIso.ofComponents fun _ â†¦ .refl _)

/-- The horizontal inverse of `toCatCommSqOverWhiskeringFunctorOfTransformSquare` -/
@[simps!]
instance toFunctorToCategoricalPullbackTransformSquare
    (X : Type uâ‚â‚€) [Category.{vâ‚â‚€} X]
    (Ïˆ : CatCospanTransform F G F' G') :
    CatCommSq
      (toFunctorToCategoricalPullback F G X)
      (transform _ Ïˆ)
      ((Functor.whiskeringRight X _ _).obj (functorOfTransform Ïˆ))
      (toFunctorToCategoricalPullback F' G' X) :=
  CatCommSq.hInv (functorEquiv F G X) _ _ (functorEquiv F' G' X)
    (toCatCommSqOverWhiskeringFunctorOfTransformSquare X _)

@[simps!]
instance functorOfTransformFstSquare (Ïˆ : CatCospanTransform F G F' G') :
    CatCommSq (Ï€â‚ F G) (functorOfTransform Ïˆ) Ïˆ.left (Ï€â‚ F' G') where
  iso := .refl _

@[simps!]
instance functorOfTransformSndSquare (Ïˆ : CatCospanTransform F G F' G') :
    CatCommSq (Ï€â‚‚ F G) (functorOfTransform Ïˆ) Ïˆ.right (Ï€â‚‚ F' G') where
  iso := .refl _

/-- A morphism of `CatCospanTransform` induces a natural transformations of
the functor between the categorical pullbacks induced by its source and target. -/
@[simps!]
def functorOfTransformâ‚‚
    {Ïˆ Ïˆ' : CatCospanTransform F G F' G'} (Î± : Ïˆ âŸ¶ Ïˆ') :
    functorOfTransform Ïˆ âŸ¶ functorOfTransform Ïˆ' :=
  functorEquiv F' G' F âŠ¡ G|>.inverse.map <|
    (transformâ‚‚ _ Î±).app default

section functorOfTransformâ‚‚

@[simp]
lemma functorOfTransformâ‚‚_id (Ïˆ : CatCospanTransform F G F' G') :
    functorOfTransformâ‚‚ (ğŸ™ Ïˆ) = ğŸ™ _ := by
  aesop_cat

@[simp]
lemma functorOfTransformâ‚‚_comp {Ïˆ Ïˆ' Ïˆ'' : CatCospanTransform F G F' G'}
    (Î± : Ïˆ âŸ¶ Ïˆ') (Î² : Ïˆ' âŸ¶ Ïˆ'') :
    functorOfTransformâ‚‚ (Î± â‰« Î²) =
    functorOfTransformâ‚‚ Î± â‰« functorOfTransformâ‚‚ Î² := by
  aesop_cat

/-- An isomorphism of `CatCospanTransform` induces an isomorphism of the
corresponding `functorOfTransform`. -/
@[simps]
def functorOfTransformâ‚‚Iso {Ïˆ Ïˆ' : CatCospanTransform F G F' G'} (Î± : Ïˆ â‰… Ïˆ') :
    functorOfTransform Ïˆ â‰… functorOfTransform Ïˆ' where
  hom := functorOfTransformâ‚‚ Î±.hom
  inv := functorOfTransformâ‚‚ Î±.inv
  hom_inv_id := by simp [â† functorOfTransformâ‚‚_comp]
  inv_hom_id := by simp [â† functorOfTransformâ‚‚_comp]

end functorOfTransformâ‚‚

variable (F G) in
/-- `functorOfTransform` repects identities up to isomorphism. -/
@[simps!]
def functorOfTransformId :
    functorOfTransform (.id F G) â‰… ğŸ­ (F âŠ¡ G) :=
  (functorEquiv F G F âŠ¡ G|>.inverse.mapIso <|
    (transformId _ F G).app default) â‰ªâ‰«
    (functorEquivInverseDefaultIso F G)

/-- `functorOfTransform` repects compositions up to isomorphism. -/
@[simps!]
def functorOfTransformComp
    (Ïˆ : CatCospanTransform F G F' G') (Ïˆ' : CatCospanTransform F' G' F'' G'') :
    functorOfTransform (Ïˆ.comp Ïˆ') â‰…
    functorOfTransform Ïˆ â‹™ functorOfTransform Ïˆ' :=
  (functorEquiv F'' G'' F âŠ¡ G|>.inverse.mapIso <|
    (transformComp _ Ïˆ Ïˆ').app default) â‰ªâ‰«
    (toFunctorToCategoricalPullbackTransformSquare _ Ïˆ').iso.symm.app
      (transform (F âŠ¡ G) Ïˆ|>.obj default)

section

open scoped CatCospanTransform
open Functor

lemma functorOfTransformâ‚‚_whiskerLeft
    (Ïˆ : CatCospanTransform F G F' G')
    {Ï† Ï†' : CatCospanTransform F' G' F'' G''} (Î± : Ï† âŸ¶ Ï†') :
    functorOfTransformâ‚‚ (Ïˆ â— Î±) =
    (functorOfTransformComp Ïˆ Ï†).hom â‰«
      whiskerLeft (functorOfTransform Ïˆ) (functorOfTransformâ‚‚ Î±) â‰«
      (functorOfTransformComp Ïˆ Ï†').inv := by
  aesop_cat

lemma functorOfTransformâ‚‚_whiskerRight
    {Ïˆ Ïˆ' : CatCospanTransform F G F' G'} (Î± : Ïˆ âŸ¶ Ïˆ')
    (Ï† : CatCospanTransform F' G' F'' G'') :
    functorOfTransformâ‚‚ (Î± â–· Ï†) =
    (functorOfTransformComp Ïˆ Ï†).hom â‰«
      whiskerRight (functorOfTransformâ‚‚ Î±) (functorOfTransform Ï†) â‰«
      (functorOfTransformComp Ïˆ' Ï†).inv := by
  aesop_cat

lemma functorOfTransformâ‚‚_associator
    {A''' : Type uâ‚â‚€} {B''' : Type uâ‚â‚} {C''' : Type uâ‚â‚‚}
    [Category.{vâ‚â‚€} A'''] [Category.{vâ‚â‚} B'''] [Category.{vâ‚â‚‚} C''']
    {F''' : A''' â¥¤ B'''} {G''' : C''' â¥¤ B'''}
    (Ïˆ : CatCospanTransform F G F' G') (Ï† : CatCospanTransform F' G' F'' G'')
    (Ï„ : CatCospanTransform F'' G'' F''' G''') :
    functorOfTransformâ‚‚ (Î±_ Ïˆ Ï† Ï„).hom =
    (functorOfTransformComp (Ïˆ.comp Ï†) Ï„).hom â‰«
      whiskerRight (functorOfTransformComp Ïˆ Ï†).hom (functorOfTransform Ï„) â‰«
      ((functorOfTransform Ïˆ).associator
        (functorOfTransform Ï†) (functorOfTransform Ï„)).hom â‰«
      whiskerLeft (functorOfTransform Ïˆ) (functorOfTransformComp Ï† Ï„).inv â‰«
      (functorOfTransformComp Ïˆ (Ï†.comp Ï„)).inv := by
  aesop_cat

lemma functorOfTransformâ‚‚_leftUnitor
    (Ïˆ : CatCospanTransform F G F' G') :
    functorOfTransformâ‚‚ (Î»_ Ïˆ).hom =
    (functorOfTransformComp (.id F G) Ïˆ).hom â‰«
      whiskerRight (functorOfTransformId F G).hom (functorOfTransform Ïˆ) â‰«
      (functorOfTransform Ïˆ).leftUnitor.hom := by
  aesop_cat

lemma functorOfTransformâ‚‚_rightUnitor
    (Ïˆ : CatCospanTransform F G F' G') :
    functorOfTransformâ‚‚ (Ï_ Ïˆ).hom =
    (functorOfTransformComp Ïˆ (.id F' G')).hom â‰«
      whiskerLeft  (functorOfTransform Ïˆ) (functorOfTransformId F' G').hom â‰«
      (functorOfTransform Ïˆ).rightUnitor.hom := by
  aesop_cat

end

open Functor in

/-- Picturing the data of `Ïˆ : CatCospanTransform F G F' G'` and
`functorOfTransform Ïˆ` as a "categorical cube" from
`CategoricalPullback.catCommSq F G` to `CategoricalPullback.catCommSq F' G'`,
this is asserting that the cube is fully coherent, i.e that pasting the
front and right face of the cube is, up to the isomorphisms of the top and bottom
faces, the same as pasting the left and back faces. -/
lemma cube_coherence (Ïˆ : CatCospanTransform F G F' G') :
    (catCommSq F G|>.hComp' Ïˆ.squareLeft.flip).iso â‰ªâ‰«
      isoWhiskerLeft _ Ïˆ.squareRight.iso =
    isoWhiskerRight (functorOfTransformFstSquare Ïˆ).iso _ â‰ªâ‰«
      ((functorOfTransformSndSquare Ïˆ).flip.hComp' (catCommSq F' G')).iso := by
  ext x
  simp [CatCommSq.flip]

/-- An adjunction of categorical cospans induce an adjunction between the
functors induced on the categorical pullbacks -/
@[simps!]
def adjunctionOfCatCospanAdjunction (ğ”„ : CatCospanAdjunction F G F' G') :
    functorOfTransform ğ”„.leftAdjoint âŠ£ functorOfTransform ğ”„.rightAdjoint where
  unit :=
    (functorOfTransformId _ _).inv â‰«
      functorOfTransformâ‚‚ ğ”„.unit â‰«
      (functorOfTransformComp _ _).hom
  counit :=
    (functorOfTransformComp _ _).inv â‰«
      functorOfTransformâ‚‚ ğ”„.counit â‰«
      (functorOfTransformId _ _).hom
  left_triangle_components x := by
    ext
    Â· haveI := ğ”„.leftAdjunction.left_triangle_components
      simpa using (this x.fst)
    Â· haveI := ğ”„.rightAdjunction.left_triangle_components
      simpa using (this x.snd)
  right_triangle_components x := by
    ext
    Â· haveI := ğ”„.leftAdjunction.right_triangle_components
      simpa using (this x.fst)
    Â· haveI := ğ”„.rightAdjunction.right_triangle_components
      simpa using (this x.snd)

/-- A `CatCospanEquivalence` induces an equivalence between the categorical
pullbacks. This fully realizes the fact that the categorical pullback respects
equivalences of categories in all of its arguments.
Note that the corresponding fact is *not* true for the strict pullback of
categories (i.e the pullback in the `1`-category `Cat`), and is the principal
motivation behind using the categorical pullback as a replacement for the strict
pullback. -/
@[simps!]
def equivalenceOfCatCospanEquivalence (E : CatCospanEquivalence F G F' G') :
    F âŠ¡ G â‰Œ F' âŠ¡ G' where
  functor := functorOfTransform E.transform
  inverse := functorOfTransform E.inverse
  unitIso :=
    (functorOfTransformId _ _).symm â‰ªâ‰«
      functorOfTransformâ‚‚Iso E.unitIso â‰ªâ‰«
      (functorOfTransformComp _ _)
  counitIso :=
    (functorOfTransformComp _ _).symm â‰ªâ‰«
      functorOfTransformâ‚‚Iso E.counitIso â‰ªâ‰«
      (functorOfTransformId _ _)
  functor_unitIso_comp :=
    (adjunctionOfCatCospanAdjunction
      E.toCatCospanAdjunction).left_triangle_components

end Pseudofunctoriality

end CategoricalPullback

end

end CategoryTheory.Limits
