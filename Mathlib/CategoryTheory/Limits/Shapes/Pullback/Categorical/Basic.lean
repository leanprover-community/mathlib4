/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.CatCommSq
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.Categorical.CatCospanTransform

/-! # Categorical pullbacks

This file defines the basic properties of categorical pullbacks.

Given a pair of functors `(F : A ‚•§ B, G : C ‚•§ B)`, we define the category
`CategoricalPullback F G` as the category of triples
`(a : A, c : C, e : F.obj a ‚âÖ G.obj b)`.

The category `CategoricalPullback F G` sits in a canonical `CatCommSq`, and we formalize that
this square is a "limit" in the following sense: functors `X ‚•§ CategoricalPullback F G` are
equivalent to pairs of functors `(L : X ‚•§ A, R : X ‚•§ C)` equipped with a natural isomorphism
`L ‚ãô F ‚âÖ R ‚ãô G`.

We formalize this by introducing a category `CatCommSqOver F G X` that encodes
exactly this data, and we prove that the category of functors `X ‚•§ CategoricalPullback F G` is
equivalent to `CatCommSqOver F G X`.

## Main declarations

* `CategoricalPullback F G`: the type of the categorical pullback.
* `œÄ‚ÇÅ F G : CategoricalPullback F G` and `œÄ‚ÇÇ F G : CategoricalPullback F G`: the canonical
  projections.
* `CategoricalPullback.catCommSq`: the canonical `CatCommSq (œÄ‚ÇÅ F G) (œÄ‚ÇÇ F G) F G` which exhibits
  `CategoricalPullback F G` as the pullback (in the (2,1)-categorical sense)
  of the cospan of `F` and `G`.
* `CategoricalPullback.functorEquiv F G X`: the equivalence of categories between functors
  `X ‚•§ CategoricalPullback F G` and `CatCommSqOver F G X`, where the latter is an abbrev for
  `CategoricalPullback (whiskeringRight X A B|>.obj F) (whiskeringRight X C B|>.obj G)`.

## References
* [Kerodon: section 1.4.5.2](https://kerodon.net/tag/032Y)
* [Niles Johnson, Donald Yau, *2-Dimensional Categories*](https://arxiv.org/abs/2002.06055),
  example 5.3.9, although we take a slightly different (equivalent) model of the object.

## TODOs:
* 2-functoriality of the construction with respect to "transformation of categorical
  cospans".
* Full equivalence-invariance of the notion (follows from suitable 2-functoriality).
* Define a `CatPullbackSquare` typeclass extending `CatCommSq`that encodes the
  fact that a given `CatCommSq` defines an equivalence between the top left
  corner and the categorical pullback of its legs.
* Define a `IsCatPullbackSquare` propclass.
* Define the "categorical fiber" of a functor at an object of the target category.
* Pasting calculus for categorical pullback squares.
* Categorical pullback squares attached to Grothendieck constructions of pseudofunctors.
* Stability of (co)fibered categories under categorical pullbacks.

-/

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ v‚ÇÑ v‚ÇÖ v‚ÇÜ v‚Çá v‚Çà v‚Çâ v‚ÇÅ‚ÇÄ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÉ
universe u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ u‚ÇÖ u‚ÇÜ u‚Çá u‚Çà u‚Çâ u‚ÇÅ‚ÇÄ u‚ÇÅ‚ÇÅ u‚ÇÅ‚ÇÇ u‚ÇÅ‚ÇÉ

namespace CategoryTheory.Limits

attribute [local simp] CatCommSq.iso_hom_naturality  CatCommSq.iso_inv_naturality

section

variable {A : Type u‚ÇÅ} {B : Type u‚ÇÇ} {C : Type u‚ÇÉ}
  [Category.{v‚ÇÅ} A] [Category.{v‚ÇÇ} B] [Category.{v‚ÇÉ} C]
  (F : A ‚•§ B) (G : C ‚•§ B)

/-- The `CategoricalPullback F G` is the category of triples
`(a : A, c : C, F a ‚âÖ G c)`.
Morphisms `(a, c, e) ‚ü∂ (a', c', e')` are pairs of morphisms
`(f‚ÇÅ : a ‚ü∂ a', f‚ÇÇ : c ‚ü∂ c')` compatible with the specified
isomorphisms. -/
@[kerodon 032Z]
structure CategoricalPullback where
  /-- the first component element -/
  fst : A
  /-- the second component element -/
  snd : C
  /-- the structural isomorphism `F.obj fst ‚âÖ G.obj snd` -/
  iso : F.obj fst ‚âÖ G.obj snd

namespace CategoricalPullback

/-- A notation for the categorical pullback. -/
scoped notation:max L:max " ‚ä° " R:max => CategoricalPullback L R

variable {F G}

/-- The Hom types for the categorical pullback are given by pairs of maps compatible with the
structural isomorphisms. -/
@[ext]
structure Hom (x y : F ‚ä° G) where
  /-- the first component of `f : Hom x y` is a morphism `x.fst ‚ü∂ y.fst` -/
  fst : x.fst ‚ü∂ y.fst
  /-- the second component of `f : Hom x y` is a morphism `x.snd ‚ü∂ y.snd` -/
  snd : x.snd ‚ü∂ y.snd
  /-- the compatibility condition on `fst` and `snd` with respect to the structure
  isompophisms -/
  w : F.map fst ‚â´ y.iso.hom = x.iso.hom ‚â´ G.map snd := by aesop_cat

attribute [reassoc (attr := simp)] Hom.w

@[simps! id_fst id_snd comp_fst comp_snd]
instance : Category (CategoricalPullback F G) where
  Hom x y := CategoricalPullback.Hom x y
  id x :=
    { fst := ùüô x.fst
      snd := ùüô x.snd }
  comp f g :=
    { fst := f.fst ‚â´ g.fst
      snd := f.snd ‚â´ g.snd }

attribute [reassoc] comp_fst comp_snd

/-- Naturality square for morphisms in the inverse direction. -/
@[reassoc (attr := simp)]
lemma Hom.w' {x y : F ‚ä° G} (f : x ‚ü∂ y) :
    G.map f.snd ‚â´ y.iso.inv = x.iso.inv ‚â´ F.map f.fst := by
  rw [Iso.comp_inv_eq, Category.assoc, Eq.comm, Iso.inv_comp_eq, f.w]

/-- Extensionnality principle for morphisms in `CategoricalPullback F G`. -/
@[ext]
theorem hom_ext {x y : F ‚ä° G} {f g : x ‚ü∂ y}
    (h‚Çó : f.fst = g.fst) (h·µ£ : f.snd = g.snd) : f = g := by
  apply Hom.ext <;> assumption

section

variable (F G)

/-- `CategoricalPullback.œÄ‚ÇÅ F G` is the first projection `CategoricalPullback F G ‚•§ A`. -/
@[simps]
def œÄ‚ÇÅ : F ‚ä° G ‚•§ A where
  obj x := x.fst
  map f := f.fst

/-- `CategoricalPullback.œÄ‚ÇÇ F G` is the second projection `CategoricalPullback F G ‚•§ C`. -/
@[simps]
def œÄ‚ÇÇ : F ‚ä° G ‚•§ C where
  obj x := x.snd
  map f := f.snd

/-- The canonical categorical commutative square in which `CategoricalPullback F G` sits. -/
@[simps!]
instance catCommSq : CatCommSq (œÄ‚ÇÅ F G) (œÄ‚ÇÇ F G) F G where
  iso := NatIso.ofComponents (fun x ‚Ü¶ x.iso)

variable {F G} in
/-- Constructor for isomorphisms in `CategoricalPullback F G`. -/
@[simps!]
def mkIso {x y : F ‚ä° G}
    (e‚Çó : x.fst ‚âÖ y.fst) (e·µ£ : x.snd ‚âÖ y.snd)
    (w : F.map e‚Çó.hom ‚â´ y.iso.hom = x.iso.hom ‚â´ G.map e·µ£.hom := by aesop_cat) :
    x ‚âÖ y where
  hom := ‚ü®e‚Çó.hom, e·µ£.hom, w‚ü©
  inv := ‚ü®e‚Çó.inv, e·µ£.inv, by simpa using F.map e‚Çó.inv ‚â´= w.symm =‚â´ G.map e·µ£.inv‚ü©

section

variable {x y : F ‚ä° G} (f : x ‚ü∂ y) [IsIso f]

instance : IsIso f.fst :=
  inferInstanceAs (IsIso ((œÄ‚ÇÅ _ _).mapIso (asIso f)).hom)

instance : IsIso f.snd :=
  inferInstanceAs (IsIso ((œÄ‚ÇÇ _ _).mapIso (asIso f)).hom)

@[simp]
lemma inv_fst : (inv f).fst = inv f.fst := by
  symm
  apply IsIso.inv_eq_of_hom_inv_id
  simpa [-IsIso.hom_inv_id] using congrArg (fun t ‚Ü¶ t.fst) (IsIso.hom_inv_id f)

@[simp]
lemma inv_snd : (inv f).snd = inv f.snd := by
  symm
  apply IsIso.inv_eq_of_hom_inv_id
  simpa [-IsIso.hom_inv_id] using congrArg (fun t ‚Ü¶ t.snd) (IsIso.hom_inv_id f)

end

lemma isIso_iff {x y : F ‚ä° G} (f : x ‚ü∂ y) :
    IsIso f ‚Üî (IsIso f.fst ‚àß IsIso f.snd) where
  mp h := ‚ü®inferInstance, inferInstance‚ü©
  mpr | ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© => ‚ü®‚ü®inv f.fst, inv f.snd, by aesop_cat‚ü©, by aesop_cat‚ü©

end

section

open Functor

variable (X : Type u‚ÇÑ) [Category.{v‚ÇÑ} X]

variable (F G) in
/-- The data of a categorical commutative square over a cospan `F, G` with cone point `X` is
that of a functor `T : X ‚•§ A`, a functor `L : X ‚•§ C`, and a `CatCommSqOver T L F G`.
Note that this is *exactly* what an object of
`((whiskeringRight X A B).obj F) ‚ä° ((whiskeringRight X C B).obj G)` is,
so `CatCommSqOver F G X` is in fact an abbreviation for
`((whiskeringRight X A B).obj F) ‚ä° ((whiskeringRight X C B).obj G)`. -/
abbrev CatCommSqOver :=
  (whiskeringRight X A B|>.obj F) ‚ä° (whiskeringRight X C B|>.obj G)

namespace CatCommSqOver

/-- Interpret a `CatCommSqOver F G‚ÄØX` as a `CatCommSq`. -/
@[simps]
instance asSquare (S : CatCommSqOver F G X) : CatCommSq S.fst S.snd F G where
  iso := S.iso

@[reassoc (attr := simp)]
lemma iso_hom_naturality (S : CatCommSqOver F G X) {x x' : X} (f : x ‚ü∂ x') :
   F.map (S.fst.map f) ‚â´ S.iso.hom.app x' =
   S.iso.hom.app x ‚â´ G.map (S.snd.map f) :=
  S.iso.hom.naturality f

@[reassoc (attr := simp)]
lemma w_app {S S' : CatCommSqOver F G X} (œÜ : S ‚ü∂ S') (x : X) :
    F.map (œÜ.fst.app x) ‚â´ S'.iso.hom.app x =
    S.iso.hom.app x ‚â´ G.map (œÜ.snd.app x) :=
  NatTrans.congr_app œÜ.w x

variable {X} in
/-- Interpret a `CatCommSq` as a `CatCommSqOver`. -/
@[simps]
def ofSquare (T : A ‚•§ B) (L : A ‚•§ C) (R : B ‚•§ X) (B_ : C ‚•§ X)
    [CatCommSq T L R B_] : CatCommSqOver R B_ A where
  fst := T
  snd := L
  iso := CatCommSq.iso T L R B_

variable (F G)

/-- The "first projection" of a CatCommSqOver as a functor. -/
abbrev fstFunctor : CatCommSqOver F G X ‚•§ X ‚•§ A := œÄ‚ÇÅ _ _

/-- The "second projection" of a CatCommSqOver as a functor. -/
abbrev sndFunctor : CatCommSqOver F G X ‚•§ X ‚•§ C := œÄ‚ÇÇ _ _

/-- The structure isompophism of a `CatCommSqOver` as a natural transformation. -/
abbrev e :
    fstFunctor F G X ‚ãô (whiskeringRight X A B).obj F ‚âÖ
    sndFunctor F G X ‚ãô (whiskeringRight X C B).obj G :=
  NatIso.ofComponents (fun S ‚Ü¶ S.iso)

/-- There is a canonical inhabitant of
`CatCommSqOver F G (CategoricalPullback F G)` corresponding to the
canonical square `catCommSq` -/
@[simps]
instance : Inhabited (CatCommSqOver F G <| F ‚ä° G) where
  default := ofSquare (œÄ‚ÇÅ F G) (œÄ‚ÇÇ F G) F G

-- this is a non-diamond
example : (default : CatCommSqOver F G <| F ‚ä° G).asSquare = catCommSq F G := rfl

end CatCommSqOver

section functorEquiv

variable (F G)

-- We need to split up the definition of `functorEquiv` to avoid timeouts.

/-- Interpret a functor to the categorical pullback as a `CatCommSqOver`. -/
@[simps!]
def toCatCommSqOver : (X ‚•§ F ‚ä° G) ‚•§ CatCommSqOver F G X where
  obj J :=
    { fst := J ‚ãô œÄ‚ÇÅ F G
      snd := J ‚ãô œÄ‚ÇÇ F G
      iso :=
        associator _ _ _ ‚â™‚â´
          isoWhiskerLeft J (catCommSq F G).iso ‚â™‚â´
          (associator _ _ _).symm }
  map {J J'} F :=
    { fst := whiskerRight F (œÄ‚ÇÅ _ _)
      snd := whiskerRight F (œÄ‚ÇÇ _ _) }

/-- Interpret a `CatCommSqOver` as a functor to the categorical pullback. -/
@[simps!]
def CatCommSqOver.toFunctorToCategoricalPullback :
    (CatCommSqOver F G X) ‚•§ X ‚•§ F ‚ä° G where
  obj S :=
    { obj x :=
        { fst := S.fst.obj x
          snd := S.snd.obj x
          iso := S.iso.app x }
      map {x y} f :=
        { fst := S.fst.map f
          snd := S.snd.map f } }
  map {S S'} œÜ :=
    { app x :=
        { fst := œÜ.fst.app x
          snd := œÜ.snd.app x } }

/-- The universal property of categorical pullbacks, stated as an equivalence
of categories between functors `X ‚•§ (F ‚ä° G)` and categorical commutative squares
over X. -/
@[simps]
def functorEquiv : (X ‚•§ F ‚ä° G) ‚âå CatCommSqOver F G X where
  functor := toCatCommSqOver F G X
  inverse := CatCommSqOver.toFunctorToCategoricalPullback F G X
  unitIso :=
    NatIso.ofComponents
      (fun _ ‚Ü¶ NatIso.ofComponents
        (fun _ ‚Ü¶ CategoricalPullback.mkIso (.refl _) (.refl _)))
  counitIso :=
    NatIso.ofComponents
      (fun _ ‚Ü¶ CategoricalPullback.mkIso
        (NatIso.ofComponents (fun _ ‚Ü¶ .refl _)) (NatIso.ofComponents (fun _ ‚Ü¶ .refl _)))

variable {F G X}

/-- A constructor for natural isomorphisms of functors `X ‚•§ CategoricalPullback`: to
construct such an isomorphism, it suffices to produce isomorphisms after whiskering with
the projections, and compatible with the canonical 2-commutative square . -/
@[simps!]
def mkNatIso {J K : X ‚•§ F ‚ä° G}
    (e‚ÇÅ : J ‚ãô œÄ‚ÇÅ F G ‚âÖ K ‚ãô œÄ‚ÇÅ F G) (e‚ÇÇ : J ‚ãô œÄ‚ÇÇ F G ‚âÖ K ‚ãô œÄ‚ÇÇ F G)
    (coh :
      whiskerRight e‚ÇÅ.hom F ‚â´ (associator _ _ _).hom ‚â´
        whiskerLeft K (CatCommSq.iso (œÄ‚ÇÅ F G) (œÄ‚ÇÇ F G) F G).hom ‚â´
        (associator _ _ _).inv =
      (associator _ _ _).hom ‚â´
        whiskerLeft J (CatCommSq.iso (œÄ‚ÇÅ F G) (œÄ‚ÇÇ F G) F G).hom ‚â´
        (associator _ _ _).inv ‚â´
        whiskerRight e‚ÇÇ.hom G := by aesop_cat) :
    J ‚âÖ K :=
  NatIso.ofComponents
    (fun x ‚Ü¶ CategoricalPullback.mkIso (e‚ÇÅ.app x) (e‚ÇÇ.app x)
      (by simpa using NatTrans.congr_app coh x))
    (fun {_ _} f ‚Ü¶ by
      ext
      ¬∑ exact e‚ÇÅ.hom.naturality f
      ¬∑ exact e‚ÇÇ.hom.naturality f)

/-- To check equality of two natural transformations of functors to a `CategoricalPullback`, it
suffices to do so after whiskering with the projections. -/
@[ext]
lemma natTrans_ext
    {J K : X ‚•§ F ‚ä° G} {Œ± Œ≤ : J ‚ü∂ K}
    (e‚ÇÅ : whiskerRight Œ± (œÄ‚ÇÅ F G) = whiskerRight Œ≤ (œÄ‚ÇÅ F G))
    (e‚ÇÇ : whiskerRight Œ± (œÄ‚ÇÇ F G) = whiskerRight Œ≤ (œÄ‚ÇÇ F G)) :
    Œ± = Œ≤ := by
  ext x
  ¬∑ exact congrArg (fun t ‚Ü¶ t.app x) e‚ÇÅ
  ¬∑ exact congrArg (fun t ‚Ü¶ t.app x) e‚ÇÇ

section

variable {J K : X ‚•§ F ‚ä° G}
    (e‚ÇÅ : J ‚ãô œÄ‚ÇÅ F G ‚âÖ K ‚ãô œÄ‚ÇÅ F G) (e‚ÇÇ : J ‚ãô œÄ‚ÇÇ F G ‚âÖ K ‚ãô œÄ‚ÇÇ F G)
    (coh :
      whiskerRight e‚ÇÅ.hom F ‚â´ (associator _ _ _).hom ‚â´
        whiskerLeft K (CatCommSq.iso (œÄ‚ÇÅ F G) (œÄ‚ÇÇ F G) F G).hom ‚â´
        (associator _ _ _).inv =
      (associator _ _ _).hom ‚â´
        whiskerLeft J (CatCommSq.iso (œÄ‚ÇÅ F G) (œÄ‚ÇÇ F G) F G).hom ‚â´
        (associator _ _ _).inv ‚â´
        whiskerRight e‚ÇÇ.hom G := by aesop_cat)

@[simp]
lemma toCatCommSqOver_mapIso_mkNatIso_eq_mkIso :
    (toCatCommSqOver F G X).mapIso (mkNatIso e‚ÇÅ e‚ÇÇ coh) =
    CategoricalPullback.mkIso e‚ÇÅ e‚ÇÇ
      (by simpa [functorEquiv, toCatCommSqOver] using coh) := by
  aesop

/-- Comparing mkNatIso with the corresponding construction one can deduce from
`functorEquiv`. -/
lemma mkNatIso_eq :
    mkNatIso e‚ÇÅ e‚ÇÇ coh =
    (functorEquiv F G X).fullyFaithfulFunctor.preimageIso
      (CategoricalPullback.mkIso e‚ÇÅ e‚ÇÇ
        (by simpa [functorEquiv, toCatCommSqOver] using coh)) := by
  rw [‚Üê toCatCommSqOver_mapIso_mkNatIso_eq_mkIso e‚ÇÅ e‚ÇÇ coh]
  dsimp [Equivalence.fullyFaithfulFunctor]
  aesop_cat

end

end functorEquiv

end

section Bifunctoriality

namespace CatCommSqOver
open Functor

section transform

variable {A' : Type u‚ÇÑ} {B' : Type u‚ÇÖ} {C' : Type u‚ÇÜ}
  [Category.{v‚ÇÑ} A'] [Category.{v‚ÇÖ} B'] [Category.{v‚ÇÜ} C']
  {F' : A' ‚•§ B'} {G' : C' ‚•§ B'}

/-- Transform a `CatCommSqOver F G X` by "whiskering it" with a
`CatCospanTransform`. -/
@[simps]
def transform (X : Type u‚Çá) [Category.{v‚Çá} X]
    (œà : CatCospanTransform F G F' G') :
    CatCommSqOver F G X ‚•§ CatCommSqOver F' G' X where
  obj S :=
    { fst := S.fst ‚ãô œà.left
      snd := S.snd ‚ãô œà.right
      iso :=
        (Functor.associator _ _ _) ‚â™‚â´
          isoWhiskerLeft S.fst (œà.squareLeft.iso.symm) ‚â™‚â´
          (Functor.associator _ _ _).symm ‚â™‚â´
          isoWhiskerRight S.iso _ ‚â™‚â´
          isoWhiskerLeft S.snd (œà.squareRight.iso) ‚â™‚â´
          (Functor.associator _ _ _).symm }
  map {x y} f :=
    { fst := whiskerRight f.fst œà.left
      snd := whiskerRight f.snd œà.right
      w := by
        ext x
        dsimp
        simp only [Category.comp_id, Category.id_comp,
          CatCommSq.iso_inv_naturality_assoc, Category.assoc,
          NatIso.cancel_natIso_inv_left, Functor.comp_obj]
        simp [‚Üê Functor.map_comp_assoc] }

/-- A morphism of `CatCospanTransform` induce a natural transformation of
the functor they induce on `CatCommSqOver`. -/
@[simps]
def transform‚ÇÇ (X : Type u‚Çá) [Category.{v‚Çá} X]
    {œà œà' : CatCospanTransform F G F' G'} (Œ∑ : œà ‚ü∂ œà') :
    transform X œà ‚ü∂ transform X œà' where
  app S :=
    { fst := { app y := Œ∑.left.app (S.fst.obj y) }
      snd := { app y := Œ∑.right.app (S.snd.obj y) }
      w := by
        ext t
        dsimp
        simp only [Category.comp_id, Category.id_comp, Category.assoc,
          CatCospanTransformMorphism.right_coherence_app, Functor.comp_obj,
          NatTrans.naturality_assoc]
        haveI := œà.squareLeft.iso.inv.app (S.fst.obj t) ‚â´=
          Œ∑.left_coherence_app (S.fst.obj t)
        simp only [Iso.inv_hom_id_app_assoc] at this
        simp [this] }

section transform‚ÇÇ

variable (X : Type u‚Çá) [Category.{v‚Çá} X]

@[simp]
lemma transform‚ÇÇ_id (œà : CatCospanTransform F G F' G') :
    transform‚ÇÇ X (ùüô œà) = ùüô _ := by aesop_cat

@[reassoc, simp]
lemma transform‚ÇÇ_comp {œà œà' œà'' : CatCospanTransform F G F' G'}
    (Œ± : œà ‚ü∂ œà') (Œ≤ : œà' ‚ü∂ œà'') :
    transform‚ÇÇ X (Œ± ‚â´ Œ≤) = transform‚ÇÇ X Œ± ‚â´ transform‚ÇÇ X Œ≤ := by
  aesop_cat

/-- Extend `transform‚ÇÇ` to isomorphisms. -/
@[simps]
def transform‚ÇÇIso {œà œà' : CatCospanTransform F G F' G'} (Œ± : œà ‚âÖ œà') :
    transform X œà ‚âÖ transform X œà' where
  hom := transform‚ÇÇ X Œ±.hom
  inv := transform‚ÇÇ X Œ±.inv
  hom_inv_id := by simp [‚Üê transform‚ÇÇ_comp]
  inv_hom_id := by simp [‚Üê transform‚ÇÇ_comp]

end transform‚ÇÇ

variable {A'' : Type u‚Çá} {B'' : Type u‚Çà} {C'' : Type u‚Çâ}
  [Category.{v‚Çá} A''] [Category.{v‚Çà} B''] [Category.{v‚Çâ} C'']
  {F'' : A'' ‚•§ B''} {G'' : C'' ‚•§ B''}

/-- The construction `CatCommSqOver.transform` respects vertical composition
of `CatCospanTransform`. -/
@[simps!]
def transformComp (X : Type u‚ÇÅ‚ÇÄ) [Category.{v‚ÇÅ‚ÇÄ} X]
    (œà : CatCospanTransform F G F' G') (œà' : CatCospanTransform F' G' F'' G'') :
    transform X (œà.comp œà') ‚âÖ (transform X œà) ‚ãô (transform X œà') :=
  NatIso.ofComponents fun _ =>
    CategoricalPullback.mkIso
      (Functor.associator _ _ _).symm
      (Functor.associator _ _ _).symm

/-- The construction `CatCommSqOver.transform` respects the identity
`CatCospanTransform`. -/
@[simps!]
def transformId (X : Type u‚ÇÑ) [Category.{v‚ÇÑ} X]
    (F : A ‚•§ B) (G : C ‚•§ B) :
    transform X (CatCospanTransform.id F G) ‚âÖ ùü≠ _ :=
  NatIso.ofComponents fun _ =>
    CategoricalPullback.mkIso
      (Functor.rightUnitor _)
      (Functor.rightUnitor _)

open scoped CatCospanTransform

lemma transform‚ÇÇ_whiskerLeft
    (X : Type u‚Çá) [Category.{v‚Çá} X]
    (œà : CatCospanTransform F G F' G')
    {œÜ œÜ' : CatCospanTransform F' G' F'' G''} (Œ± : œÜ ‚ü∂ œÜ') :
    transform‚ÇÇ X (œà ‚óÅ Œ±) =
    (transformComp X œà œÜ).hom ‚â´
      whiskerLeft (transform X œà) (transform‚ÇÇ X Œ±) ‚â´
      (transformComp X œà œÜ').inv := by
  aesop_cat

lemma transform‚ÇÇ_whiskerRight
    (X : Type u‚Çá) [Category.{v‚Çá} X]
    {œà œà' : CatCospanTransform F G F' G'} (Œ± : œà ‚ü∂ œà')
    (œÜ : CatCospanTransform F' G' F'' G'') :
    transform‚ÇÇ X (Œ± ‚ñ∑ œÜ) =
    (transformComp X œà œÜ).hom ‚â´
      whiskerRight (transform‚ÇÇ X Œ±) (transform X œÜ) ‚â´
      (transformComp X œà' œÜ).inv := by
  aesop_cat

lemma transform‚ÇÇ_associator
    {A''' : Type u‚ÇÅ‚ÇÄ} {B''' : Type u‚ÇÅ‚ÇÅ} {C''' : Type u‚ÇÅ‚ÇÇ}
    [Category.{v‚ÇÅ‚ÇÄ} A'''] [Category.{v‚ÇÅ‚ÇÅ} B'''] [Category.{v‚ÇÅ‚ÇÇ} C''']
    {F''' : A''' ‚•§ B'''} {G''' : C''' ‚•§ B'''}
    (X : Type u‚ÇÅ‚ÇÉ) [Category.{v‚ÇÅ‚ÇÉ} X]
    (œà : CatCospanTransform F G F' G') (œÜ : CatCospanTransform F' G' F'' G'')
    (œÑ : CatCospanTransform F'' G'' F''' G''') :
    transform‚ÇÇ X (Œ±_ œà œÜ œÑ).hom =
    (transformComp X (œà.comp œÜ) œÑ).hom ‚â´
      whiskerRight (transformComp X œà œÜ).hom (transform X œÑ) ‚â´
      ((transform X œà).associator (transform X œÜ) (transform X œÑ)).hom ‚â´
      whiskerLeft (transform X œà) (transformComp X œÜ œÑ).inv ‚â´
      (transformComp X œà (œÜ.comp œÑ)).inv := by
  aesop_cat

lemma transform‚ÇÇ_leftUnitor (X : Type u‚Çá) [Category.{v‚Çá} X]
    (œà : CatCospanTransform F G F' G') :
    transform‚ÇÇ X (Œª_ œà).hom =
    (transformComp X (.id F G) œà).hom ‚â´
      whiskerRight (transformId X F G).hom (transform X œà) ‚â´
      (transform X œà).leftUnitor.hom := by
  aesop_cat

lemma transform‚ÇÇ_rightUnitor (X : Type u‚Çá) [Category.{v‚Çá} X]
    (œà : CatCospanTransform F G F' G') :
    transform‚ÇÇ X (œÅ_ œà).hom =
    (transformComp X œà (.id F' G')).hom ‚â´
      whiskerLeft  (transform X œà) (transformId X F' G').hom ‚â´
      (transform X œà).rightUnitor.hom := by
  aesop_cat

end transform

section precompose

variable (F G)

variable
    {X : Type u‚ÇÑ} {Y : Type u‚ÇÖ} {Z : Type u‚ÇÜ}
    [Category.{v‚ÇÑ} X] [Category.{v‚ÇÖ} Y] [Category.{v‚ÇÜ} Z]

/-- A functor `U : X ‚•§ Y` induces a functor
`CatCommSqOver F G Y ‚•§ CatCommSqOver F G X` by whiskering left the underlying
categorical commutative square by U. -/
@[simps]
def precompose (U : X ‚•§ Y) : CatCommSqOver F G Y ‚•§ CatCommSqOver F G X where
  obj S :=
    { fst := U ‚ãô S.fst
      snd := U ‚ãô S.snd
      iso :=
        (Functor.associator _ _ _) ‚â™‚â´
          isoWhiskerLeft U S.iso ‚â™‚â´
          (Functor.associator _ _ _).symm }
  map {S S'} œÜ :=
    { fst := whiskerLeft U œÜ.fst
      snd := whiskerLeft U œÜ.snd }

variable (X) in
/-- The construction `precompose` respects functor identities. -/
@[simps!]
def precomposeId :
    precompose F G (ùü≠ X) ‚âÖ ùü≠ (CatCommSqOver F G X) :=
  NatIso.ofComponents fun _ =>
    CategoricalPullback.mkIso (Functor.leftUnitor _) (Functor.leftUnitor _)

/-- The construction `precompose` respects functor composition. -/
@[simps!]
def precomposeComp (U : X ‚•§ Y) (V : Y ‚•§ Z) :
    precompose F G (U ‚ãô V) ‚âÖ precompose F G V ‚ãô precompose F G U :=
  NatIso.ofComponents fun _ =>
    CategoricalPullback.mkIso
      (Functor.associator _ _ _)
      (Functor.associator _ _ _)

/-- The construction `precompose` acts on natural transformations. -/
@[simps]
def precompose‚ÇÇ {U V : X ‚•§ Y} (Œ± : U ‚ü∂ V) :
    precompose F G U ‚ü∂ precompose F G V where
  app x :=
    { fst := whiskerRight Œ± x.fst
      snd := whiskerRight Œ± x.snd }

@[simp]
lemma precompose‚ÇÇ_id (U : X ‚•§ Y) : precompose‚ÇÇ F G (ùüô U) = ùüô _ := by aesop_cat

@[simp]
lemma precompose‚ÇÇ_comp {U V W : X ‚•§ Y} (Œ± : U ‚ü∂ V) (Œ≤ : V ‚ü∂ W) :
    precompose‚ÇÇ F G (Œ± ‚â´ Œ≤) = precompose‚ÇÇ F G Œ± ‚â´ precompose‚ÇÇ F G Œ≤ := by
  aesop_cat

/-- Extend `precompose‚ÇÇ` to isomorphisms. -/
@[simps]
def precompose‚ÇÇIso {U V : X ‚•§ Y} (Œ± : U ‚âÖ V) :
    precompose F G U ‚âÖ precompose F G V where
  hom := precompose‚ÇÇ F G Œ±.hom
  inv := precompose‚ÇÇ F G Œ±.inv
  hom_inv_id := by simp [‚Üê precompose‚ÇÇ_comp]
  inv_hom_id := by simp [‚Üê precompose‚ÇÇ_comp]

lemma precompose‚ÇÇ_whiskerLeft (U : X ‚•§ Y) {V W : Y ‚•§ Z} (Œ± : V ‚ü∂ W) :
    precompose‚ÇÇ F G (whiskerLeft U Œ±) =
    (precomposeComp F G U V).hom ‚â´
      whiskerRight (precompose‚ÇÇ F G Œ±) (precompose F G U) ‚â´
      (precomposeComp F G U W).inv := by
  aesop_cat

lemma precompose‚ÇÇ_whiskerRight {U V : X ‚•§ Y} (Œ± : U ‚ü∂ V) (W : Y ‚•§ Z) :
    precompose‚ÇÇ F G (whiskerRight Œ± W) =
    (precomposeComp F G U W).hom ‚â´
      whiskerLeft (precompose F G W) (precompose‚ÇÇ F G Œ±) ‚â´
      (precomposeComp F G V W).inv := by
  aesop_cat

lemma precompose‚ÇÇ_associator {T : Type u‚Çá} [Category.{v‚Çá} T]
    (U : X ‚•§ Y) (V : Y ‚•§ Z) (W : Z ‚•§ T) :
    precompose‚ÇÇ F G (U.associator V W).hom =
    (precomposeComp F G (U ‚ãô V) W).hom ‚â´
      whiskerLeft (precompose F G W) (precomposeComp F G U V).hom ‚â´
      ((precompose F G W).associator _ _).inv ‚â´
      whiskerRight (precomposeComp F G V W).inv (precompose F G U) ‚â´
      (precomposeComp F G _ _).inv := by
  aesop_cat

lemma precompose‚ÇÇ_leftUnitor (U : X ‚•§ Y) :
    precompose‚ÇÇ F G U.leftUnitor.hom =
    (precomposeComp F G (ùü≠ _) U).hom ‚â´
      whiskerLeft (precompose F G U) (precomposeId F G X).hom ‚â´
      (Functor.rightUnitor _).hom := by
  aesop_cat

lemma precompose‚ÇÇ_rightUnitor (U : X ‚•§ Y) :
    precompose‚ÇÇ F G U.rightUnitor.hom =
    (precomposeComp F G U (ùü≠ _)).hom ‚â´
      whiskerRight (precomposeId F G Y).hom (precompose F G U) ‚â´
      (Functor.leftUnitor _).hom := by
  aesop_cat

end precompose

section compatibility

variable {A' : Type u‚ÇÑ} {B' : Type u‚ÇÖ} {C' : Type u‚ÇÜ}
  [Category.{v‚ÇÑ} A'] [Category.{v‚ÇÖ} B'] [Category.{v‚ÇÜ} C']
  {F' : A' ‚•§ B'} {G' : C' ‚•§ B'}

/-- The canonical compatibility square between `precompose` and `transform`.
This is a "naturality square" if we think as `transform _` as the
(app component of the) map component of a pseudofunctor from the bicategory of
categorical cospans with value in pseudofunctors
(its value on the categorical cospan `F, G` being the pseudofunctor
`precompose F G _`). -/
@[simps!]
instance precomposeTransformSquare
    {X : Type u‚Çá} {Y : Type u‚Çà} [Category.{v‚Çá} X] [Category.{v‚Çà} Y]
    (œà : CatCospanTransform F G F' G') (U : X ‚•§ Y) :
    CatCommSq
      (precompose F G U) (transform Y œà)
      (transform X œà) (precompose F' G' U) where
  iso := NatIso.ofComponents fun _ =>
    CategoricalPullback.mkIso
      (Functor.associator _ _ _)
      (Functor.associator _ _ _)

-- Compare the next 3 lemmas with the components of a strong natural transform
-- of pseudofunctors

/-- The naturality square `precomposeTransformSquare` is itself natural. -/
lemma precomposeTransformSquare_iso_hom_naturality‚ÇÇ
    {X : Type u‚Çá} {Y : Type u‚Çà} [Category.{v‚Çá} X] [Category.{v‚Çà} Y]
    (œà : CatCospanTransform F G F' G')
    {U V : X ‚•§ Y} (Œ± : U ‚ü∂ V) :
    whiskerRight (precompose‚ÇÇ F G Œ±) (transform X œà) ‚â´
      (CatCommSq.iso _ (transform Y œà) _ (precompose F' G' V)).hom =
    (CatCommSq.iso _ (transform Y œà) _ (precompose F' G' U)).hom ‚â´
      whiskerLeft (transform _ _) (precompose‚ÇÇ F' G' Œ±) := by
  aesop_cat

/-- The naturality square `precomposeTransformSquare` respects identities. -/
lemma precomposeTransformSquare_iso_hom_id
    (œà : CatCospanTransform F G F' G') (X : Type u‚Çá) [Category.{v‚Çá} X] :
    (CatCommSq.iso (precompose F G <| ùü≠ X) (transform X œà)
      (transform X œà) (precompose F' G' <| ùü≠ X)).hom ‚â´
      whiskerLeft (transform X œà) (precomposeId F' G' X).hom =
    whiskerRight (precomposeId F G X).hom (transform X œà) ‚â´
    (Functor.leftUnitor _).hom ‚â´ (Functor.rightUnitor _).inv := by
  aesop_cat

/-- The naturality square `precomposeTransformSquare` respects compositions. -/
lemma precomposeTransformSquare_iso_hom_comp
    {X : Type u‚Çá} {Y : Type u‚Çà} {Z : Type u‚Çâ}
    [Category.{v‚Çá} X] [Category.{v‚Çà} Y] [Category.{v‚Çâ} Z]
    (œà : CatCospanTransform F G F' G')
    (U : X ‚•§ Y) (V : Y ‚•§ Z) :
    (CatCommSq.iso (precompose F G <| U ‚ãô V) (transform Z œà)
      (transform X œà) (precompose F' G' <| U ‚ãô V)).hom ‚â´
      whiskerLeft (transform Z œà) (precomposeComp F' G' U V).hom =
    whiskerRight (precomposeComp F G U V).hom (transform X œà) ‚â´
      (Functor.associator _ _ _).hom ‚â´
      whiskerLeft (precompose F G V)
        (CatCommSq.iso _ (transform _ œà) _ _).hom ‚â´
      (Functor.associator _ _ _).inv ‚â´
      whiskerRight (CatCommSq.iso _ _ _ _).hom
        (precompose F' G' U) ‚â´
      (Functor.associator _ _ _).hom := by
  aesop_cat

/-- The canonical compatibility square between `transform` and `precompose`.
This is a "naturality square" if we think as `precompose` as the
(app component of the) map component of a pseudofunctor from the opposite
bicategory of categories to pseudofunctors of categorical cospans
(its value on `X` being the pseudofunctor `transform X _`). -/
@[simps!]
instance transformPrecomposeSquare
    {X : Type u‚Çá} {Y : Type u‚Çà} [Category.{v‚Çá} X] [Category.{v‚Çà} Y]
    (U : X ‚•§ Y) (œà : CatCospanTransform F G F' G') :
    CatCommSq
      (transform Y œà) (precompose F G U)
      (precompose F' G' U) (transform X œà) where
  iso := NatIso.ofComponents fun _ =>
    CategoricalPullback.mkIso
      (Functor.associator _ _ _).symm
      (Functor.associator _ _ _).symm

-- Compare the next 3 lemmas with the components of a strong natural transform
-- of pseudofunctors

/-- The naturality square `transformPrecomposeSquare` is itself natural. -/
lemma transformPrecomposeSquare_iso_hom_naturality‚ÇÇ
    {X : Type u‚Çá} {Y : Type u‚Çà} [Category.{v‚Çá} X] [Category.{v‚Çà} Y]
    (U : X ‚•§ Y) {œà œà' : CatCospanTransform F G F' G'} (Œ∑ : œà ‚ü∂ œà') :
    whiskerRight (transform‚ÇÇ Y Œ∑) (precompose F' G' U) ‚â´
      (CatCommSq.iso _ (precompose F G U) _ (transform X œà')).hom =
    (CatCommSq.iso _ (precompose F G U) _ (transform X œà)).hom ‚â´
      whiskerLeft (precompose F G U) (transform‚ÇÇ X Œ∑) := by
  aesop_cat

/-- The naturality square `transformPrecomposeSquare` respects identities. -/
lemma transformPrecomposeSquare_iso_hom_id
    {X : Type u‚Çá} {Y : Type u‚Çà} [Category.{v‚Çá} X] [Category.{v‚Çà} Y]
    (U : X ‚•§ Y) (F : A ‚•§ B) (G : C ‚•§ B) :
    (CatCommSq.iso (transform Y (.id F G)) (precompose F G U)
      (precompose F G U) (transform X (.id F G))).hom ‚â´
      whiskerLeft (precompose F G U) (transformId X F G).hom =
    whiskerRight (transformId Y F G).hom (precompose F G U) ‚â´
      (precompose F G U).leftUnitor.hom ‚â´
      (precompose F G U).rightUnitor.inv := by
  aesop_cat

/-- The naturality square `transformPrecomposeSquare` respects compositions. -/
lemma transformPrecomposeSquare_iso_hom_comp
    {A'' : Type u‚Çá} {B'' : Type u‚Çà} {C'' : Type u‚Çâ}
    [Category.{v‚Çá} A''] [Category.{v‚Çà} B''] [Category.{v‚Çâ} C'']
    {F'' : A'' ‚•§ B''} {G'' : C'' ‚•§ B''}
    {X : Type u‚ÇÅ‚ÇÄ} {Y : Type u‚ÇÅ‚ÇÅ} [Category.{v‚ÇÅ‚ÇÄ} X] [Category.{v‚ÇÅ‚ÇÅ} Y]
    (U : X ‚•§ Y) (œà : CatCospanTransform F G F' G')
    (œà' : CatCospanTransform F' G' F'' G'') :
    (CatCommSq.iso (transform Y (œà.comp œà')) (precompose F G U)
      (precompose F'' G'' U) (transform X (œà.comp œà'))).hom ‚â´
      whiskerLeft (precompose F G U) (transformComp X œà œà').hom =
    whiskerRight (transformComp Y œà œà').hom (precompose F'' G'' U) ‚â´
      (Functor.associator _ _ _).hom ‚â´
      whiskerLeft (transform Y œà)
        (CatCommSq.iso _ (precompose F' G' U) _ (transform X œà')).hom ‚â´
      (Functor.associator _ _ _).inv ‚â´
      whiskerRight (CatCommSq.iso _ _ _ _).hom (transform X œà') ‚â´
      (Functor.associator _ _ _).hom := by
  aesop_cat

end compatibility

end CatCommSqOver

end Bifunctoriality

end CategoricalPullback

end

end CategoryTheory.Limits
