/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.CatCommSq

/-! # Categorical pullback squares

This file defines the basic properties of categorical pullback squares.

Given a pair of functors `(F : A â¥¤ B, G : C â¥¤ B)`, we define the category
`CategoricalPullback F G` as the category of triples
`(a : A, c : C, e : F.obj a â‰… G.obj b)`.

The category `CategoricalPullback F G` sits in a canonical `CatCommSq`, and we formalize that
this square is a "limit" in the following sense: functors `X â¥¤ CategoricalPullback F G` are
equivalent to pairs of functors `(L : X â¥¤ A, R : X â¥¤ C)` equipped with a natural isomorphism
`L â‹™ F â‰… R â‹™ G`.

We formalize this by introducing a category `CatCommSqOver F G X` that encodes
exactly this data, and we prove that the category of functors `X â¥¤ CategoricalPullback F G` is
equivalent to `CatCommSqOver F G X`.

## Main declarations

* `CategoricalPullback F G`: the type of the categorical pullback.
* `Ï€â‚— F G : CategoricalPullback F G` and `Ï€áµ£ F G : CategoricalPullback F G`: the canonical
  projections.
* `CategoricalPullback.catCommSq`: the canonical `CatCommSq (Ï€â‚— F G) (Ï€áµ£ F G) F G` which exhibits
  `CategoricalPullback F G` as the pullback (in the (2,1)-categorical sense)
  of the cospan of `F` and `G`.
* `CategoricalPullback.functorEquiv F G X`: the equivalence of categories between functors
  `X â¥¤ CategoricalPullback F G` and `CatCommSqOver F G X`, where the latter is an abbrev for
  `CategoricalPullback (whiskeringRight X A B|>.obj F) (whiskeringRight X C B|>.obj G)`.

## References
* [Kerodon: section 1.4.5.2](https://kerodon.net/tag/032Y)
* [Niles Johnson, Donald Yau, *2-Dimensional Categories*](https://arxiv.org/abs/2002.06055),
  example 5.3.9, although we take a slightly different (equivalent) model of the object.

## TODOs:
* 2-functoriality of the construction with respect to "transformation of categorical
  cospans".
* Full equivalence-invariance of the notion (follows from suitable 2-functoriality).
* Define a `CatPullbackSquare` typeclass extending `CatCommSq`that encodes the
  fact that a given `CatCommSq` defines an equivalence between the top left
  corner and the categorical pullback of its legs.
* Define a `IsCatPullbackSquare` propclass.
* Define the "categorical fiber" of a functor at an object of the target category.
* Pasting calculus for categorical pullback squares.
* Categorical pullback squares attached to Grothendieck constructions of pseudofunctors.
* Stability of (co)fibered categories under categorical pullbacks.

-/

universe vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ uâ‚ uâ‚‚ uâ‚ƒ uâ‚„

namespace CategoryTheory.Limits

attribute [local simp] CatCommSq.iso_hom_naturality  CatCommSq.iso_inv_naturality

section

variable {A : Type uâ‚} {B : Type uâ‚‚} {C : Type uâ‚ƒ}
  [Category.{vâ‚} A] [Category.{vâ‚‚} B] [Category.{vâ‚ƒ} C]
  (F : A â¥¤ B) (G : C â¥¤ B)

/-- The `CategoricalPullback F G` is the category of triples
`(a : A, c : C, F a â‰… G c)`.
Morphisms `(a, c, e) âŸ¶ (a', c', e')` are pairs of morphisms
`(fâ‚ : a âŸ¶ a', fâ‚‚ : c âŸ¶ c')` compatible with the specified
isomorphisms. -/
@[kerodon 032Z]
structure CategoricalPullback where
  /-- the left element -/
  left : A
  /-- the right element -/
  right : C
  /-- the structural isomorphism `F.obj left â‰… G.obj right` -/
  iso : F.obj left â‰… G.obj right

namespace CategoricalPullback

/-- A notation for the categorical pullback. -/
scoped notation:max L:max " âŠ¡ " R:max => CategoricalPullback L R

variable {F G}

/-- The Hom types for the categorical pullback are given by pairs of maps compatible with the
structural isomorphisms. -/
@[ext]
structure Hom (x y : F âŠ¡ G) where
  /-- the left component of `f : Hom x y` is a morphism `x.left âŸ¶ y.left` -/
  left : x.left âŸ¶ y.left
  /-- the right component of `f : Hom x y` is a morphism `x.right âŸ¶ y.right` -/
  right : x.right âŸ¶ y.right
  /-- the compatibility condition on `left` and `right` with respect to the structure
  isompophisms -/
  w : F.map left â‰« y.iso.hom = x.iso.hom â‰« G.map right := by aesop_cat

attribute [reassoc (attr := simp)] Hom.w

@[simps! id_left id_right comp_left comp_right]
instance : Category (CategoricalPullback F G) where
  Hom x y := CategoricalPullback.Hom x y
  id x :=
    { left := ğŸ™ x.left
      right := ğŸ™ x.right }
  comp f g :=
    { left := f.left â‰« g.left
      right := f.right â‰« g.right }

attribute [reassoc] comp_left comp_right

/-- Naturality square for morphisms in the inverse direction. -/
@[reassoc (attr := simp)]
lemma Hom.w' {x y : F âŠ¡ G} (f : x âŸ¶ y) :
    G.map f.right â‰« y.iso.inv = x.iso.inv â‰« F.map f.left := by
  rw [Iso.comp_inv_eq, Category.assoc, Eq.comm, Iso.inv_comp_eq, f.w]

/-- Extensionnality principle for morphisms in `CategoricalPullback F G`. -/
@[ext]
theorem hom_ext {x y : F âŠ¡ G} {f g : x âŸ¶ y}
    (hâ‚— : f.left = g.left) (háµ£ : f.right = g.right) : f = g := by
  apply Hom.ext <;> assumption

section

variable (F G)

/-- `CategoricalPullback.Ï€â‚— F G` is the left projection `CategoricalPullback F G â¥¤ A`. -/
@[simps]
def Ï€â‚— : F âŠ¡ G â¥¤ A where
  obj x := x.left
  map f := f.left

/-- `CategoricalPullback.Ï€áµ£ F G` is the right projection `CategoricalPullback F G â¥¤ C`. -/
@[simps]
def Ï€áµ£ : F âŠ¡ G â¥¤ C where
  obj x := x.right
  map f := f.right

/-- The canonical categorical commutative square in which `CategoricalPullback F G` sits. -/
instance catCommSq : CatCommSq (Ï€â‚— F G) (Ï€áµ£ F G) F G where
  iso' := NatIso.ofComponents (fun x â†¦ x.iso)

@[simp]
lemma catCommSq_iso_hom_app (x : F âŠ¡ G) :
    (CatCommSq.iso (Ï€â‚— F G) (Ï€áµ£ F G) F G).hom.app x = x.iso.hom := rfl

@[simp]
lemma catCommSq_iso_inv_app (x : F âŠ¡ G) :
    (CatCommSq.iso (Ï€â‚— F G) (Ï€áµ£ F G) F G).inv.app x = x.iso.inv := rfl

variable {F G} in
/-- Constructor for isomorphisms in `CategoricalPullback F G`. -/
@[simps!]
def mkIso {x y : F âŠ¡ G}
    (eâ‚— : x.left â‰… y.left) (eáµ£ : x.right â‰… y.right)
    (w : F.map eâ‚—.hom â‰« y.iso.hom = x.iso.hom â‰« G.map eáµ£.hom := by aesop_cat) :
    x â‰… y where
  hom := âŸ¨eâ‚—.hom, eáµ£.hom, wâŸ©
  inv := âŸ¨eâ‚—.inv, eáµ£.inv, by simpa using F.map eâ‚—.inv â‰«= w.symm =â‰« G.map eáµ£.invâŸ©

instance {x y : F âŠ¡ G} (f : x âŸ¶ y) [IsIso f] : IsIso f.left :=
  inferInstanceAs (IsIso ((Ï€â‚— _ _).mapIso (asIso f)).hom)

instance {x y : F âŠ¡ G} (f : x âŸ¶ y) [IsIso f] : IsIso f.right :=
  inferInstanceAs (IsIso ((Ï€áµ£ _ _).mapIso (asIso f)).hom)

end

section

variable (X : Type uâ‚„) [Category.{vâ‚„} X]

variable (F G) in
/-- The data of a categorical commutative square over a cospan `F, G` with cone point `X` is
that of a functor `T : X â¥¤ A`, a functor `L : X â¥¤ C`, and a `CatCommSqOver T L F G`.
Note that this is *exactly* what an object of
`((whiskeringRight X A B).obj F) âŠ¡ ((whiskeringRight X C B).obj G)` is,
so `CatCommSqOver F G X` is in fact an abbreviation for
`((whiskeringRight X A B).obj F) âŠ¡ ((whiskeringRight X C B).obj G)`. -/
abbrev CatCommSqOver :=
  (whiskeringRight X A B|>.obj F) âŠ¡ (whiskeringRight X C B|>.obj G)

namespace CatCommSqOver

/-- Interpret a `CatCommSqOver F Gâ€¯X` as a `CatCommSq`. -/
@[simps!]
def asSquare (S : CatCommSqOver F G X) : CatCommSq S.left S.right F G where
  iso' := S.iso

@[reassoc (attr := simp)]
lemma iso_hom_naturality (S : CatCommSqOver F G X) {x x' : X} (f : x âŸ¶ x') :
   F.map (S.left.map f) â‰« S.iso.hom.app x' =
   S.iso.hom.app x â‰« G.map (S.right.map f) :=
  S.iso.hom.naturality f

@[reassoc (attr := simp)]
lemma w_app {S S' : CatCommSqOver F G X} (Ï† : S âŸ¶ S') (x : X) :
    F.map (Ï†.left.app x) â‰« S'.iso.hom.app x =
    S.iso.hom.app x â‰« G.map (Ï†.right.app x) :=
  NatTrans.congr_app Ï†.w x

variable (F G)

/-- The "left projection" of a CatCommSqOver as a functor. -/
abbrev L : CatCommSqOver F G X â¥¤ X â¥¤ A := Ï€â‚— _ _

/-- The "right projection" of a CatCommSqOver as a functor. -/
abbrev R : CatCommSqOver F G X â¥¤ X â¥¤ C := Ï€áµ£ _ _

/-- The structure isompophism of a `CatCommSqOver` as a natural transformation. -/
abbrev e :
    (L F G X) â‹™ (whiskeringRight X A B|>.obj F) â‰…
    (R F G X) â‹™ (whiskeringRight X C B|>.obj G) :=
  NatIso.ofComponents
    (fun S â†¦ S.iso)

end CatCommSqOver

section functorEquiv

variable (F G)

-- We need to split up the definition of `functorEquiv` to avoid timeouts.

/-- Interpret a functor to the categorical pullback as a `CatCommSqOver`. -/
@[simps!]
def toCatCommSqOver : (X â¥¤ F âŠ¡ G) â¥¤ CatCommSqOver F G X where
  obj J :=
    { left := J â‹™ Ï€â‚— F G
      right := J â‹™ Ï€áµ£ F G
      iso :=
        Functor.associator _ _ _ â‰ªâ‰«
          isoWhiskerLeft J (catCommSq F G).iso â‰ªâ‰«
          (Functor.associator _ _ _).symm }
  map {J J'} F :=
    { left := whiskerRight F (Ï€â‚— _ _)
      right := whiskerRight F (Ï€áµ£ _ _) }

/-- Interpret a `CatCommSqOver` as a functor to the categorical pullback. -/
@[simps!]
def CatCommSqOver.toFunctorToCategoricalPullback :
    (CatCommSqOver F G X) â¥¤ X â¥¤ F âŠ¡ G where
  obj S :=
    { obj x :=
        { left := S.left.obj x
          right := S.right.obj x
          iso := S.iso.app x }
      map {x y} f :=
        { left := S.left.map f
          right := S.right.map f } }
  map {S S'} Ï† :=
    { app x :=
        { left := Ï†.left.app x
          right := Ï†.right.app x } }

/-- The unit of `CategoricalPullback.functorEquiv`. -/
@[simps!]
def functorEquivUnitIso :
    ğŸ­ (X â¥¤ F âŠ¡ G) â‰…
    toCatCommSqOver F G X â‹™ CatCommSqOver.toFunctorToCategoricalPullback F G X :=
  NatIso.ofComponents
    (fun _ â†¦ NatIso.ofComponents
      (fun _ â†¦ CategoricalPullback.mkIso (.refl _) (.refl _)))

/-- The counit of `CategoricalPullback.functorEquiv`. -/
@[simps!]
def functorEquivCounitIso :
    CatCommSqOver.toFunctorToCategoricalPullback F G X â‹™ toCatCommSqOver F G X â‰…
    ğŸ­ (CatCommSqOver F G X) :=
  NatIso.ofComponents
    (fun _ â†¦ CategoricalPullback.mkIso
      (NatIso.ofComponents (fun _ â†¦ .refl _)) (NatIso.ofComponents (fun _ â†¦ .refl _)))

/-- The universal property of categorical pullbacks, stated as an equivalence
of categories between functors `X â¥¤ (F âŠ¡ G)` and categorical commutative squares
over X. -/
@[simps!]
def functorEquiv : (X â¥¤ F âŠ¡ G) â‰Œ CatCommSqOver F G X where
  functor := toCatCommSqOver F G X
  inverse := CatCommSqOver.toFunctorToCategoricalPullback F G X
  unitIso := functorEquivUnitIso F G X
  counitIso := functorEquivCounitIso F G X

variable {F G X}

/-- A constructor for natural isomorphisms of functors `X â¥¤ CategoricalPullback`: to
construct such an isomorphism, it suffices to produce isomorphisms after whiskering with
the projections, and compatible with the canonical 2-commutative square . -/
@[simps!]
def mkNatIso {J K : X â¥¤ F âŠ¡ G}
    (eâ‚ : J â‹™ Ï€â‚— F G â‰… K â‹™ Ï€â‚— F G) (eâ‚‚ : J â‹™ Ï€áµ£ F G â‰… K â‹™ Ï€áµ£ F G)
    (coh :
      whiskerRight eâ‚.hom F â‰« (Functor.associator _ _ _).hom â‰«
        whiskerLeft K (CatCommSq.iso (Ï€â‚— F G) (Ï€áµ£ F G) F G).hom â‰«
        (Functor.associator _ _ _).inv =
      (Functor.associator _ _ _).hom â‰«
        whiskerLeft J (CatCommSq.iso (Ï€â‚— F G) (Ï€áµ£ F G) F G).hom â‰«
        (Functor.associator _ _ _).inv â‰«
        whiskerRight eâ‚‚.hom G := by aesop_cat) :
    J â‰… K :=
  NatIso.ofComponents
    (fun x â†¦ CategoricalPullback.mkIso (eâ‚.app x) (eâ‚‚.app x)
      (by simpa using NatTrans.congr_app coh x))
    (fun {_ _} f â†¦ by
      ext
      Â· exact eâ‚.hom.naturality f
      Â· exact eâ‚‚.hom.naturality f)

/-- To check equality of two natural transformations of functors to a `CategoricalPullback`, it
suffices to do so after whiskering with the projections. -/
@[ext]
lemma natTrans_ext
    {J K : X â¥¤ F âŠ¡ G} {Î± Î² : J âŸ¶ K}
    (eâ‚ : whiskerRight Î± (Ï€â‚— F G) = whiskerRight Î² (Ï€â‚— F G))
    (eâ‚‚ : whiskerRight Î± (Ï€áµ£ F G) = whiskerRight Î² (Ï€áµ£ F G)) :
    Î± = Î² := by
  ext x
  Â· exact congrArg (fun t â†¦ t.app x) eâ‚
  Â· exact congrArg (fun t â†¦ t.app x) eâ‚‚

/-- Comparing mkNatIso with the corresponding construction one can deduce from
`functorEquiv`. -/
lemma mkNatIso_eq {J K : X â¥¤ F âŠ¡ G}
    (eâ‚ : J â‹™ Ï€â‚— F G â‰… K â‹™ Ï€â‚— F G) (eâ‚‚ : J â‹™ Ï€áµ£ F G â‰… K â‹™ Ï€áµ£ F G)
    (coh :
      whiskerRight eâ‚.hom F â‰« (Functor.associator _ _ _).hom â‰«
        whiskerLeft K (CatCommSq.iso (Ï€â‚— F G) (Ï€áµ£ F G) F G).hom â‰«
        (Functor.associator _ _ _).inv =
      (Functor.associator _ _ _).hom â‰«
        whiskerLeft J (CatCommSq.iso (Ï€â‚— F G) (Ï€áµ£ F G) F G).hom â‰«
        (Functor.associator _ _ _).inv â‰«
        whiskerRight eâ‚‚.hom G := by aesop_cat) :
  mkNatIso eâ‚ eâ‚‚ coh =
    (functorEquiv F G X).fullyFaithfulFunctor.preimageIso
      (CategoricalPullback.mkIso eâ‚ eâ‚‚
        (by simpa [functorEquiv, toCatCommSqOver] using coh)) := by
  ext
  Â· simp [Equivalence.fullyFaithfulFunctor]
  Â· simp [Equivalence.fullyFaithfulFunctor]

end functorEquiv

end

end CategoricalPullback

end

end CategoryTheory.Limits
