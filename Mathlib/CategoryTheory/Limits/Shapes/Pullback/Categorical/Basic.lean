/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.CatCommSq
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.Categorical.CatCospanTransform

/-! # Categorical pullbacks

This file defines the basic properties of categorical pullbacks.

Given a pair of functors `(F : A â¥¤ B, G : C â¥¤ B)`, we define the category
`CategoricalPullback F G` as the category of triples
`(a : A, c : C, e : F.obj a â‰… G.obj b)`.

The category `CategoricalPullback F G` sits in a canonical `CatCommSq`, and we formalize that
this square is a "limit" in the following sense: functors `X â¥¤ CategoricalPullback F G` are
equivalent to pairs of functors `(L : X â¥¤ A, R : X â¥¤ C)` equipped with a natural isomorphism
`L â‹™ F â‰… R â‹™ G`.

We formalize this by introducing a category `CatCommSqOver F G X` that encodes
exactly this data, and we prove that the category of functors `X â¥¤ CategoricalPullback F G` is
equivalent to `CatCommSqOver F G X`.

## Main declarations

* `CategoricalPullback F G`: the type of the categorical pullback.
* `Ï€â‚ F G : CategoricalPullback F G` and `Ï€â‚‚ F G : CategoricalPullback F G`: the canonical
  projections.
* `CategoricalPullback.catCommSq`: the canonical `CatCommSq (Ï€â‚ F G) (Ï€â‚‚ F G) F G` which exhibits
  `CategoricalPullback F G` as the pullback (in the (2,1)-categorical sense)
  of the cospan of `F` and `G`.
* `CategoricalPullback.functorEquiv F G X`: the equivalence of categories between functors
  `X â¥¤ CategoricalPullback F G` and `CatCommSqOver F G X`, where the latter is an abbrev for
  `CategoricalPullback (whiskeringRight X A B|>.obj F) (whiskeringRight X C B|>.obj G)`.

## References
* [Kerodon: section 1.4.5.2](https://kerodon.net/tag/032Y)
* [Niles Johnson, Donald Yau, *2-Dimensional Categories*](https://arxiv.org/abs/2002.06055),
  example 5.3.9, although we take a slightly different (equivalent) model of the object.

## TODOs:
* Define the "categorical fiber" of a functor at an object of the target category.
* Pasting calculus for categorical pullback squares.
* Categorical pullback squares attached to Grothendieck constructions of pseudofunctors.
* Stability of (co)fibered categories under categorical pullbacks.

-/

universe vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ vâ‚… vâ‚† vâ‚‡ vâ‚ˆ vâ‚‰ vâ‚â‚€ vâ‚â‚ vâ‚â‚‚ vâ‚â‚ƒ
universe uâ‚ uâ‚‚ uâ‚ƒ uâ‚„ uâ‚… uâ‚† uâ‚‡ uâ‚ˆ uâ‚‰ uâ‚â‚€ uâ‚â‚ uâ‚â‚‚ uâ‚â‚ƒ

namespace CategoryTheory.Limits

attribute [local simp] CatCommSq.iso_hom_naturality  CatCommSq.iso_inv_naturality

section

variable {A : Type uâ‚} {B : Type uâ‚‚} {C : Type uâ‚ƒ}
  [Category.{vâ‚} A] [Category.{vâ‚‚} B] [Category.{vâ‚ƒ} C]
  (F : A â¥¤ B) (G : C â¥¤ B)

/-- The `CategoricalPullback F G` is the category of triples
`(a : A, c : C, F a â‰… G c)`.
Morphisms `(a, c, e) âŸ¶ (a', c', e')` are pairs of morphisms
`(fâ‚ : a âŸ¶ a', fâ‚‚ : c âŸ¶ c')` compatible with the specified
isomorphisms. -/
@[kerodon 032Z]
structure CategoricalPullback where
  /-- the first component element -/
  fst : A
  /-- the second component element -/
  snd : C
  /-- the structural isomorphism `F.obj fst â‰… G.obj snd` -/
  iso : F.obj fst â‰… G.obj snd

namespace CategoricalPullback

/-- A notation for the categorical pullback. -/
scoped notation:max L:max " âŠ¡ " R:max => CategoricalPullback L R

variable {F G}

/-- The Hom types for the categorical pullback are given by pairs of maps compatible with the
structural isomorphisms. -/
@[ext]
structure Hom (x y : F âŠ¡ G) where
  /-- the first component of `f : Hom x y` is a morphism `x.fst âŸ¶ y.fst` -/
  fst : x.fst âŸ¶ y.fst
  /-- the second component of `f : Hom x y` is a morphism `x.snd âŸ¶ y.snd` -/
  snd : x.snd âŸ¶ y.snd
  /-- the compatibility condition on `fst` and `snd` with respect to the structure
  isompophisms -/
  w : F.map fst â‰« y.iso.hom = x.iso.hom â‰« G.map snd := by aesop_cat

attribute [reassoc (attr := simp)] Hom.w

@[simps! id_fst id_snd comp_fst comp_snd]
instance : Category (CategoricalPullback F G) where
  Hom x y := CategoricalPullback.Hom x y
  id x :=
    { fst := ğŸ™ x.fst
      snd := ğŸ™ x.snd }
  comp f g :=
    { fst := f.fst â‰« g.fst
      snd := f.snd â‰« g.snd }

attribute [reassoc] comp_fst comp_snd

/-- Naturality square for morphisms in the inverse direction. -/
@[reassoc (attr := simp)]
lemma Hom.w' {x y : F âŠ¡ G} (f : x âŸ¶ y) :
    G.map f.snd â‰« y.iso.inv = x.iso.inv â‰« F.map f.fst := by
  rw [Iso.comp_inv_eq, Category.assoc, Eq.comm, Iso.inv_comp_eq, f.w]

/-- Extensionnality principle for morphisms in `CategoricalPullback F G`. -/
@[ext]
theorem hom_ext {x y : F âŠ¡ G} {f g : x âŸ¶ y}
    (hâ‚— : f.fst = g.fst) (háµ£ : f.snd = g.snd) : f = g := by
  apply Hom.ext <;> assumption

section

variable (F G)

/-- `CategoricalPullback.Ï€â‚ F G` is the first projection `CategoricalPullback F G â¥¤ A`. -/
@[simps]
def Ï€â‚ : F âŠ¡ G â¥¤ A where
  obj x := x.fst
  map f := f.fst

/-- `CategoricalPullback.Ï€â‚‚ F G` is the second projection `CategoricalPullback F G â¥¤ C`. -/
@[simps]
def Ï€â‚‚ : F âŠ¡ G â¥¤ C where
  obj x := x.snd
  map f := f.snd

/-- The canonical categorical commutative square in which `CategoricalPullback F G` sits. -/
@[simps!]
instance catCommSq : CatCommSq (Ï€â‚ F G) (Ï€â‚‚ F G) F G where
  iso := NatIso.ofComponents (fun x â†¦ x.iso)

variable {F G} in
/-- Constructor for isomorphisms in `CategoricalPullback F G`. -/
@[simps!]
def mkIso {x y : F âŠ¡ G}
    (eâ‚— : x.fst â‰… y.fst) (eáµ£ : x.snd â‰… y.snd)
    (w : F.map eâ‚—.hom â‰« y.iso.hom = x.iso.hom â‰« G.map eáµ£.hom := by aesop_cat) :
    x â‰… y where
  hom := âŸ¨eâ‚—.hom, eáµ£.hom, wâŸ©
  inv := âŸ¨eâ‚—.inv, eáµ£.inv, by simpa using F.map eâ‚—.inv â‰«= w.symm =â‰« G.map eáµ£.invâŸ©

section

variable {x y : F âŠ¡ G} (f : x âŸ¶ y) [IsIso f]

instance : IsIso f.fst :=
  inferInstanceAs (IsIso ((Ï€â‚ _ _).mapIso (asIso f)).hom)

instance : IsIso f.snd :=
  inferInstanceAs (IsIso ((Ï€â‚‚ _ _).mapIso (asIso f)).hom)

@[simp]
lemma inv_fst : (inv f).fst = inv f.fst := by
  symm
  apply IsIso.inv_eq_of_hom_inv_id
  simpa [-IsIso.hom_inv_id] using congrArg (fun t â†¦ t.fst) (IsIso.hom_inv_id f)

@[simp]
lemma inv_snd : (inv f).snd = inv f.snd := by
  symm
  apply IsIso.inv_eq_of_hom_inv_id
  simpa [-IsIso.hom_inv_id] using congrArg (fun t â†¦ t.snd) (IsIso.hom_inv_id f)

end

lemma isIso_iff {x y : F âŠ¡ G} (f : x âŸ¶ y) :
    IsIso f â†” (IsIso f.fst âˆ§ IsIso f.snd) where
  mp h := âŸ¨inferInstance, inferInstanceâŸ©
  mpr | âŸ¨hâ‚, hâ‚‚âŸ© => âŸ¨âŸ¨inv f.fst, inv f.snd, by aesop_catâŸ©, by aesop_catâŸ©

end

section

open Functor

variable (X : Type uâ‚„) [Category.{vâ‚„} X]

variable (F G) in
/-- The data of a categorical commutative square over a cospan `F, G` with cone point `X` is
that of a functor `T : X â¥¤ A`, a functor `L : X â¥¤ C`, and a `CatCommSqOver T L F G`.
Note that this is *exactly* what an object of
`((whiskeringRight X A B).obj F) âŠ¡ ((whiskeringRight X C B).obj G)` is,
so `CatCommSqOver F G X` is in fact an abbreviation for
`((whiskeringRight X A B).obj F) âŠ¡ ((whiskeringRight X C B).obj G)`. -/
abbrev CatCommSqOver :=
  (whiskeringRight X A B|>.obj F) âŠ¡ (whiskeringRight X C B|>.obj G)

namespace CatCommSqOver

/-- Interpret a `CatCommSqOver F Gâ€¯X` as a `CatCommSq`. -/
@[simps]
instance asSquare (S : CatCommSqOver F G X) : CatCommSq S.fst S.snd F G where
  iso := S.iso

@[reassoc (attr := simp)]
lemma iso_hom_naturality (S : CatCommSqOver F G X) {x x' : X} (f : x âŸ¶ x') :
   F.map (S.fst.map f) â‰« S.iso.hom.app x' =
   S.iso.hom.app x â‰« G.map (S.snd.map f) :=
  S.iso.hom.naturality f

@[reassoc (attr := simp)]
lemma w_app {S S' : CatCommSqOver F G X} (Ï† : S âŸ¶ S') (x : X) :
    F.map (Ï†.fst.app x) â‰« S'.iso.hom.app x =
    S.iso.hom.app x â‰« G.map (Ï†.snd.app x) :=
  NatTrans.congr_app Ï†.w x

@[reassoc (attr := simp)]
lemma Iso.hom_inv_id_fst_app {S S' : CatCommSqOver F G X} (e : S â‰… S') (x : X) :
    e.hom.fst.app x â‰« e.inv.fst.app x = ğŸ™ _ := by
  simp [â† NatTrans.comp_app, â† comp_fst]

@[reassoc (attr := simp)]
lemma Iso.hom_inv_id_snd_app {S S' : CatCommSqOver F G X} (e : S â‰… S') (x : X) :
    e.hom.snd.app x â‰« e.inv.snd.app x = ğŸ™ _ := by
  simp [â† NatTrans.comp_app, â† comp_snd]

@[reassoc (attr := simp)]
lemma Iso.inv_hom_id_fst_app {S S' : CatCommSqOver F G X} (e : S â‰… S') (x : X) :
    e.inv.fst.app x â‰« e.hom.fst.app x = ğŸ™ _ := by
  simp [â† NatTrans.comp_app, â† comp_fst]

@[reassoc (attr := simp)]
lemma Iso.inv_hom_id_snd_app {S S' : CatCommSqOver F G X} (e : S â‰… S') (x : X) :
    e.inv.snd.app x â‰« e.hom.snd.app x = ğŸ™ _ := by
  simp [â† NatTrans.comp_app, â† comp_snd]

@[reassoc (attr := simp)]
lemma Iso.hom_inv_id_app_fst_app {J : Type*} [Category J]
    {S S' : J â¥¤ CatCommSqOver F G X} (e : S â‰… S') (j : J) (x : X) :
    (e.hom.app j).fst.app x â‰« (e.inv.app j).fst.app x = ğŸ™ _ := by
  simp [â† NatTrans.comp_app, â† comp_fst]

@[reassoc (attr := simp)]
lemma Iso.hom_inv_id_app_snd_app {J : Type*} [Category J]
    {S S' : J â¥¤ CatCommSqOver F G X} (e : S â‰… S') (j : J) (x : X) :
    (e.hom.app j).snd.app x â‰« (e.inv.app j).snd.app x = ğŸ™ _ := by
  simp [â† NatTrans.comp_app, â† comp_snd]

@[reassoc (attr := simp)]
lemma Iso.inv_hom_id_app_fst_app {J : Type*} [Category J]
    {S S' : J â¥¤ CatCommSqOver F G X} (e : S â‰… S') (j : J) (x : X) :
    (e.inv.app j).fst.app x â‰« (e.hom.app j).fst.app x = ğŸ™ _ := by
  simp [â† NatTrans.comp_app, â† comp_fst]

@[reassoc (attr := simp)]
lemma Iso.inv_hom_id_app_snd_app {J : Type*} [Category J]
    {S S' : J â¥¤ CatCommSqOver F G X} (e : S â‰… S') (j : J) (x : X) :
    (e.inv.app j).snd.app x â‰« (e.hom.app j).snd.app x = ğŸ™ _ := by
  simp [â† NatTrans.comp_app, â† comp_snd]

variable {X} in
/-- Interpret a `CatCommSq` as a `CatCommSqOver`. -/
@[simps]
def ofSquare (T : A â¥¤ B) (L : A â¥¤ C) (R : B â¥¤ X) (B_ : C â¥¤ X)
    [CatCommSq T L R B_] : CatCommSqOver R B_ A where
  fst := T
  snd := L
  iso := CatCommSq.iso T L R B_

variable (F G)

/-- The "first projection" of a CatCommSqOver as a functor. -/
abbrev fstFunctor : CatCommSqOver F G X â¥¤ X â¥¤ A := Ï€â‚ _ _

/-- The "second projection" of a CatCommSqOver as a functor. -/
abbrev sndFunctor : CatCommSqOver F G X â¥¤ X â¥¤ C := Ï€â‚‚ _ _

/-- The structure isompophism of a `CatCommSqOver` as a natural transformation. -/
abbrev e :
    fstFunctor F G X â‹™ (whiskeringRight X A B).obj F â‰…
    sndFunctor F G X â‹™ (whiskeringRight X C B).obj G :=
  NatIso.ofComponents (fun S â†¦ S.iso)

/-- There is a canonical inhabitant of
`CatCommSqOver F G (CategoricalPullback F G)` corresponding to the
canonical square `catCommSq` -/
@[simps]
instance : Inhabited (CatCommSqOver F G <| F âŠ¡ G) where
  default := ofSquare (Ï€â‚ F G) (Ï€â‚‚ F G) F G

-- this is a non-diamond
example : (default : CatCommSqOver F G <| F âŠ¡ G).asSquare = catCommSq F G := rfl

end CatCommSqOver

section functorEquiv

variable (F G)

-- We need to split up the definition of `functorEquiv` to avoid timeouts.

/-- Interpret a functor to the categorical pullback as a `CatCommSqOver`. -/
@[simps!]
def toCatCommSqOver : (X â¥¤ F âŠ¡ G) â¥¤ CatCommSqOver F G X where
  obj J :=
    { fst := J â‹™ Ï€â‚ F G
      snd := J â‹™ Ï€â‚‚ F G
      iso :=
        associator _ _ _ â‰ªâ‰«
          isoWhiskerLeft J (catCommSq F G).iso â‰ªâ‰«
          (associator _ _ _).symm }
  map {J J'} F :=
    { fst := whiskerRight F (Ï€â‚ _ _)
      snd := whiskerRight F (Ï€â‚‚ _ _) }

/-- Interpret a `CatCommSqOver` as a functor to the categorical pullback. -/
@[simps!]
def CatCommSqOver.toFunctorToCategoricalPullback :
    (CatCommSqOver F G X) â¥¤ X â¥¤ F âŠ¡ G where
  obj S :=
    { obj x :=
        { fst := S.fst.obj x
          snd := S.snd.obj x
          iso := S.iso.app x }
      map {x y} f :=
        { fst := S.fst.map f
          snd := S.snd.map f } }
  map {S S'} Ï† :=
    { app x :=
        { fst := Ï†.fst.app x
          snd := Ï†.snd.app x } }

/-- The universal property of categorical pullbacks, stated as an equivalence
of categories between functors `X â¥¤ (F âŠ¡ G)` and categorical commutative
squares over X.
Note that this equivalence should be considered as an
implementation detail. Should you want to use the universal property, you should
use the equivalence `CatPullbackSquare.functorEquiv` defined in
`Mathlib/CategoryTheory/Limits/Shapes/Pullback/Categorical/Square.lean` instead,
which provides a similar equivalence thanks to a
`CatPullbackSquare (Ï€â‚ F G) (Ï€â‚‚ F G) F G` instance. -/
@[simps]
def functorEquiv : (X â¥¤ F âŠ¡ G) â‰Œ CatCommSqOver F G X where
  functor := toCatCommSqOver F G X
  inverse := CatCommSqOver.toFunctorToCategoricalPullback F G X
  unitIso :=
    NatIso.ofComponents
      (fun _ â†¦ NatIso.ofComponents
        (fun _ â†¦ CategoricalPullback.mkIso (.refl _) (.refl _)))
  counitIso :=
    NatIso.ofComponents
      (fun _ â†¦ CategoricalPullback.mkIso
        (NatIso.ofComponents (fun _ â†¦ .refl _)) (NatIso.ofComponents (fun _ â†¦ .refl _)))

end functorEquiv

end

section Bifunctoriality

namespace CatCommSqOver
open Functor

section transform

variable {A' : Type uâ‚„} {B' : Type uâ‚…} {C' : Type uâ‚†}
  [Category.{vâ‚„} A'] [Category.{vâ‚…} B'] [Category.{vâ‚†} C']
  {F' : A' â¥¤ B'} {G' : C' â¥¤ B'}

/-- Functorially transform a `CatCommSqOver F G X` by whiskering it with a
`CatCospanTransform`. -/
@[simps]
def transform (X : Type uâ‚‡) [Category.{vâ‚‡} X] :
    CatCospanTransform F G F' G' â¥¤
      CatCommSqOver F G X â¥¤ CatCommSqOver F' G' X where
  obj Ïˆ :=
    { obj S :=
        { fst := S.fst â‹™ Ïˆ.left
          snd := S.snd â‹™ Ïˆ.right
          iso :=
            (Functor.associator _ _ _) â‰ªâ‰«
              isoWhiskerLeft S.fst (Ïˆ.squareLeft.iso.symm) â‰ªâ‰«
              (Functor.associator _ _ _).symm â‰ªâ‰«
              isoWhiskerRight S.iso _ â‰ªâ‰«
              isoWhiskerLeft S.snd (Ïˆ.squareRight.iso) â‰ªâ‰«
              (Functor.associator _ _ _).symm }
      map {x y} f :=
        { fst := whiskerRight f.fst Ïˆ.left
          snd := whiskerRight f.snd Ïˆ.right
          w := by
            ext x
            dsimp
            simp only [Category.comp_id, Category.id_comp,
              CatCommSq.iso_inv_naturality_assoc, Category.assoc,
              NatIso.cancel_natIso_inv_left, Functor.comp_obj]
            simp [â† Functor.map_comp_assoc] } }
  map {Ïˆ Ïˆ'} Î· :=
    { app S :=
      { fst := { app y := Î·.left.app (S.fst.obj y) }
        snd := { app y := Î·.right.app (S.snd.obj y) }
        w := by
          ext t
          dsimp
          simp only [Category.comp_id, Category.id_comp, Category.assoc,
            CatCospanTransformMorphism.right_coherence_app, Functor.comp_obj,
            NatTrans.naturality_assoc]
          haveI := Ïˆ.squareLeft.iso.inv.app (S.fst.obj t) â‰«=
            Î·.left_coherence_app (S.fst.obj t)
          simp only [Iso.inv_hom_id_app_assoc] at this
          simp [this] } }

variable {A'' : Type uâ‚‡} {B'' : Type uâ‚ˆ} {C'' : Type uâ‚‰}
  [Category.{vâ‚‡} A''] [Category.{vâ‚ˆ} B''] [Category.{vâ‚‰} C'']
  {F'' : A'' â¥¤ B''} {G'' : C'' â¥¤ B''}

/-- The construction `CatCommSqOver.transform` respects vertical composition
of `CatCospanTransform`s. -/
@[simps!]
def transformObjComp (X : Type uâ‚â‚€) [Category.{vâ‚â‚€} X]
    (Ïˆ : CatCospanTransform F G F' G') (Ïˆ' : CatCospanTransform F' G' F'' G'') :
    (transform X).obj (Ïˆ.comp Ïˆ') â‰… (transform X).obj Ïˆ â‹™ (transform X).obj Ïˆ' :=
  NatIso.ofComponents fun _ =>
    CategoricalPullback.mkIso
      (Functor.associator _ _ _).symm
      (Functor.associator _ _ _).symm

/-- The construction `CatCommSqOver.transform` respects the identity
`CatCospanTransform`s. -/
@[simps!]
def transformObjId (X : Type uâ‚„) [Category.{vâ‚„} X]
    (F : A â¥¤ B) (G : C â¥¤ B) :
    (transform X).obj (CatCospanTransform.id F G) â‰… ğŸ­ _ :=
  NatIso.ofComponents fun _ =>
    CategoricalPullback.mkIso
      (Functor.rightUnitor _)
      (Functor.rightUnitor _)

open scoped CatCospanTransform

lemma transform_map_whiskerLeft
    (X : Type uâ‚‡) [Category.{vâ‚‡} X]
    (Ïˆ : CatCospanTransform F G F' G')
    {Ï† Ï†' : CatCospanTransform F' G' F'' G''} (Î± : Ï† âŸ¶ Ï†') :
    (transform X).map (Ïˆ â— Î±) =
    (transformObjComp X Ïˆ Ï†).hom â‰«
      whiskerLeft (transform X|>.obj Ïˆ) (transform X|>.map Î±) â‰«
      (transformObjComp X Ïˆ Ï†').inv := by
  aesop_cat

lemma transform_map_whiskerRight
    (X : Type uâ‚‡) [Category.{vâ‚‡} X]
    {Ïˆ Ïˆ' : CatCospanTransform F G F' G'} (Î± : Ïˆ âŸ¶ Ïˆ')
    (Ï† : CatCospanTransform F' G' F'' G'') :
    (transform X).map (Î± â–· Ï†) =
    (transformObjComp X Ïˆ Ï†).hom â‰«
      whiskerRight (transform X|>.map Î±) (transform X|>.obj Ï†) â‰«
      (transformObjComp X Ïˆ' Ï†).inv := by
  aesop_cat

lemma transform_map_associator
    {A''' : Type uâ‚â‚€} {B''' : Type uâ‚â‚} {C''' : Type uâ‚â‚‚}
    [Category.{vâ‚â‚€} A'''] [Category.{vâ‚â‚} B'''] [Category.{vâ‚â‚‚} C''']
    {F''' : A''' â¥¤ B'''} {G''' : C''' â¥¤ B'''}
    (X : Type uâ‚â‚ƒ) [Category.{vâ‚â‚ƒ} X]
    (Ïˆ : CatCospanTransform F G F' G') (Ï† : CatCospanTransform F' G' F'' G'')
    (Ï„ : CatCospanTransform F'' G'' F''' G''') :
    (transform X).map (Î±_ Ïˆ Ï† Ï„).hom =
    (transformObjComp X (Ïˆ.comp Ï†) Ï„).hom â‰«
      whiskerRight (transformObjComp X Ïˆ Ï†).hom (transform X|>.obj Ï„) â‰«
      ((transform X|>.obj Ïˆ).associator
        (transform X|>.obj Ï†) (transform X|>.obj Ï„)).hom â‰«
      whiskerLeft (transform X|>.obj Ïˆ) (transformObjComp X Ï† Ï„).inv â‰«
      (transformObjComp X Ïˆ (Ï†.comp Ï„)).inv := by
  aesop_cat

lemma transform_map_leftUnitor (X : Type uâ‚‡) [Category.{vâ‚‡} X]
    (Ïˆ : CatCospanTransform F G F' G') :
    (transform X).map (Î»_ Ïˆ).hom =
    (transformObjComp X (.id F G) Ïˆ).hom â‰«
      whiskerRight (transformObjId X F G).hom (transform X|>.obj Ïˆ) â‰«
      (transform X|>.obj Ïˆ).leftUnitor.hom := by
  aesop_cat

lemma transform_map_rightUnitor (X : Type uâ‚‡) [Category.{vâ‚‡} X]
    (Ïˆ : CatCospanTransform F G F' G') :
    (transform X).map (Ï_ Ïˆ).hom =
    (transformObjComp X Ïˆ (.id F' G')).hom â‰«
      whiskerLeft (transform X|>.obj Ïˆ) (transformObjId X F' G').hom â‰«
      (transform X|>.obj Ïˆ).rightUnitor.hom := by
  aesop_cat

end transform

section precompose

variable (F G)

variable
    {X : Type uâ‚„} {Y : Type uâ‚…} {Z : Type uâ‚†}
    [Category.{vâ‚„} X] [Category.{vâ‚…} Y] [Category.{vâ‚†} Z]

/-- A functor `U : X â¥¤ Y` (functorially) induces a functor
`CatCommSqOver F G Y â¥¤ CatCommSqOver F G X` by whiskering left the underlying
categorical commutative square by U. -/
@[simps]
def precompose :
    (X â¥¤ Y) â¥¤ CatCommSqOver F G Y â¥¤ CatCommSqOver F G X where
  obj U :=
    { obj S :=
        { fst := U â‹™ S.fst
          snd := U â‹™ S.snd
          iso :=
            (Functor.associator _ _ _) â‰ªâ‰«
              isoWhiskerLeft U S.iso â‰ªâ‰«
              (Functor.associator _ _ _).symm }
      map {S S'} Ï† :=
        { fst := whiskerLeft U Ï†.fst
          snd := whiskerLeft U Ï†.snd } }
  map {U V} Î± :=
    { app x :=
      { fst := whiskerRight Î± x.fst
        snd := whiskerRight Î± x.snd } }

variable (X) in
/-- The construction `precompose` respects functor identities. -/
@[simps!]
def precomposeObjId :
    (precompose F G).obj (ğŸ­ X) â‰… ğŸ­ (CatCommSqOver F G X) :=
  NatIso.ofComponents fun _ =>
    CategoricalPullback.mkIso (Functor.leftUnitor _) (Functor.leftUnitor _)

/-- The construction `precompose` respects functor composition. -/
@[simps!]
def precomposeObjComp (U : X â¥¤ Y) (V : Y â¥¤ Z) :
    (precompose F G).obj (U â‹™ V) â‰…
    (precompose F G).obj V â‹™ (precompose F G).obj U :=
  NatIso.ofComponents fun _ =>
    CategoricalPullback.mkIso
      (Functor.associator _ _ _)
      (Functor.associator _ _ _)

lemma precompose_map_whiskerLeft (U : X â¥¤ Y) {V W : Y â¥¤ Z} (Î± : V âŸ¶ W) :
    (precompose F G).map (whiskerLeft U Î±) =
    (precomposeObjComp F G U V).hom â‰«
      whiskerRight (precompose F G|>.map Î±) (precompose F G|>.obj U) â‰«
      (precomposeObjComp F G U W).inv := by
  aesop_cat

lemma precompose_map_whiskerRight {U V : X â¥¤ Y} (Î± : U âŸ¶ V) (W : Y â¥¤ Z) :
    (precompose F G).map (whiskerRight Î± W) =
    (precomposeObjComp F G U W).hom â‰«
      whiskerLeft (precompose F G|>.obj W) (precompose F G|>.map Î±) â‰«
      (precomposeObjComp F G V W).inv := by
  aesop_cat

lemma precompose_map_associator {T : Type uâ‚‡} [Category.{vâ‚‡} T]
    (U : X â¥¤ Y) (V : Y â¥¤ Z) (W : Z â¥¤ T) :
    (precompose F G).map (U.associator V W).hom =
    (precomposeObjComp F G (U â‹™ V) W).hom â‰«
      whiskerLeft (precompose F G|>.obj W) (precomposeObjComp F G U V).hom â‰«
      ((precompose F G|>.obj W).associator _ _).inv â‰«
      whiskerRight (precomposeObjComp F G V W).inv (precompose F G|>.obj U) â‰«
      (precomposeObjComp F G _ _).inv := by
  aesop_cat

lemma precompose_map_leftUnitor (U : X â¥¤ Y) :
    (precompose F G).map U.leftUnitor.hom =
    (precomposeObjComp F G (ğŸ­ _) U).hom â‰«
      whiskerLeft (precompose F G|>.obj U) (precomposeObjId F G X).hom â‰«
      (Functor.rightUnitor _).hom := by
  aesop_cat

lemma precompose_map_rightUnitor (U : X â¥¤ Y) :
    (precompose F G).map U.rightUnitor.hom =
    (precomposeObjComp F G U (ğŸ­ _)).hom â‰«
      whiskerRight (precomposeObjId F G Y).hom (precompose F G|>.obj U) â‰«
      (Functor.leftUnitor _).hom := by
  aesop_cat

end precompose

section compatibility

variable {A' : Type uâ‚„} {B' : Type uâ‚…} {C' : Type uâ‚†}
  [Category.{vâ‚„} A'] [Category.{vâ‚…} B'] [Category.{vâ‚†} C']
  {F' : A' â¥¤ B'} {G' : C' â¥¤ B'}

/-- The canonical compatibility square between (the object components of)
`precompose` and `transform`.
This is a "naturality square" if we think as `transform _|>.obj _` as the
(app component of the) map component of a pseudofunctor from the bicategory of
categorical cospans with value in pseudofunctors
(its value on the categorical cospan `F, G` being the pseudofunctor
`precompose F G|>.obj _`). -/
@[simps!]
instance precomposeObjTransformObjSquare
    {X : Type uâ‚‡} {Y : Type uâ‚ˆ} [Category.{vâ‚‡} X] [Category.{vâ‚ˆ} Y]
    (Ïˆ : CatCospanTransform F G F' G') (U : X â¥¤ Y) :
    CatCommSq
      (precompose F G|>.obj U) (transform Y|>.obj Ïˆ)
      (transform X|>.obj Ïˆ) (precompose F' G'|>.obj U) where
  iso := NatIso.ofComponents fun _ =>
    CategoricalPullback.mkIso
      (Functor.associator _ _ _)
      (Functor.associator _ _ _)

-- Compare the next 3 lemmas with the components of a strong natural transform
-- of pseudofunctors

/-- The square `precomposeObjTransformObjSquare` is itself natural. -/
lemma precomposeObjTransformObjSquare_iso_hom_naturalityâ‚‚
    {X : Type uâ‚‡} {Y : Type uâ‚ˆ} [Category.{vâ‚‡} X] [Category.{vâ‚ˆ} Y]
    (Ïˆ : CatCospanTransform F G F' G')
    {U V : X â¥¤ Y} (Î± : U âŸ¶ V) :
    whiskerRight (precompose F G|>.map Î±) (transform X|>.obj Ïˆ) â‰«
      (CatCommSq.iso _ (transform Y|>.obj Ïˆ) _ (precompose F' G'|>.obj V)).hom =
    (CatCommSq.iso _ (transform Y|>.obj Ïˆ) _ (precompose F' G'|>.obj U)).hom â‰«
      whiskerLeft (transform Y|>.obj Ïˆ) (precompose F' G'|>.map Î±) := by
  aesop_cat

/-- The square `precomposeObjTransformOBjSquare` respects identities. -/
lemma precomposeObjTransformObjSquare_iso_hom_id
    (Ïˆ : CatCospanTransform F G F' G') (X : Type uâ‚‡) [Category.{vâ‚‡} X] :
    (CatCommSq.iso (precompose F G|>.obj <| ğŸ­ X) (transform X|>.obj Ïˆ)
      (transform X|>.obj Ïˆ) (precompose F' G'|>.obj <| ğŸ­ X)).hom â‰«
      whiskerLeft (transform X|>.obj Ïˆ) (precomposeObjId F' G' X).hom =
    whiskerRight (precomposeObjId F G X).hom (transform X|>.obj Ïˆ) â‰«
      (Functor.leftUnitor _).hom â‰« (Functor.rightUnitor _).inv := by
  aesop_cat

/-- The square `precomposeTransformSquare` respects compositions. -/
lemma precomposeObjTransformObjSquare_iso_hom_comp
    {X : Type uâ‚‡} {Y : Type uâ‚ˆ} {Z : Type uâ‚‰}
    [Category.{vâ‚‡} X] [Category.{vâ‚ˆ} Y] [Category.{vâ‚‰} Z]
    (Ïˆ : CatCospanTransform F G F' G')
    (U : X â¥¤ Y) (V : Y â¥¤ Z) :
    (CatCommSq.iso (precompose F G|>.obj <| U â‹™ V) (transform Z|>.obj Ïˆ)
      (transform X|>.obj Ïˆ) (precompose F' G'|>.obj <| U â‹™ V)).hom â‰«
      whiskerLeft (transform Z|>.obj Ïˆ) (precomposeObjComp F' G' U V).hom =
    whiskerRight (precomposeObjComp F G U V).hom (transform X|>.obj Ïˆ) â‰«
      (Functor.associator _ _ _).hom â‰«
      whiskerLeft (precompose F G|>.obj V)
        (CatCommSq.iso _ (transform _|>.obj Ïˆ) _ _).hom â‰«
      (Functor.associator _ _ _).inv â‰«
      whiskerRight (CatCommSq.iso _ _ _ _).hom
        (precompose F' G'|>.obj U) â‰«
      (Functor.associator _ _ _).hom := by
  aesop_cat

/-- The canonical compatibility square between (the object components of)
`transform` and `precompose`.
This is a "naturality square" if we think as `precompose` as the
(app component of the) map component of a pseudofunctor from the opposite
bicategory of categories to pseudofunctors of categorical cospans
(its value on `X` being the pseudofunctor `transform X _`). -/
@[simps!]
instance transformObjPrecomposeObjSquare
    {X : Type uâ‚‡} {Y : Type uâ‚ˆ} [Category.{vâ‚‡} X] [Category.{vâ‚ˆ} Y]
    (U : X â¥¤ Y) (Ïˆ : CatCospanTransform F G F' G') :
    CatCommSq
      (transform Y|>.obj Ïˆ) (precompose F G|>.obj U)
      (precompose F' G'|>.obj U) (transform X|>.obj Ïˆ) where
  iso := NatIso.ofComponents fun _ =>
    CategoricalPullback.mkIso
      (Functor.associator _ _ _).symm
      (Functor.associator _ _ _).symm

-- Compare the next 3 lemmas with the components of a strong natural transform
-- of pseudofunctors

/-- The square `transformObjPrecomposeObjSquare` is itself natural. -/
lemma transformObjPrecomposeObjSquare_iso_hom_naturalityâ‚‚
    {X : Type uâ‚‡} {Y : Type uâ‚ˆ} [Category.{vâ‚‡} X] [Category.{vâ‚ˆ} Y]
    (U : X â¥¤ Y) {Ïˆ Ïˆ' : CatCospanTransform F G F' G'} (Î· : Ïˆ âŸ¶ Ïˆ') :
    whiskerRight (transform Y|>.map Î·) (precompose F' G'|>.obj U) â‰«
      (CatCommSq.iso _ (precompose F G|>.obj U) _ (transform X|>.obj Ïˆ')).hom =
    (CatCommSq.iso _ (precompose F G|>.obj U) _ (transform X|>.obj Ïˆ)).hom â‰«
      whiskerLeft (precompose F G|>.obj U) (transform X|>.map Î·) := by
  aesop_cat

/-- The square `transformObjPrecomposeObjSquare` respects identities. -/
lemma transformObjPrecomposeObjSquare_iso_hom_id
    {X : Type uâ‚‡} {Y : Type uâ‚ˆ} [Category.{vâ‚‡} X] [Category.{vâ‚ˆ} Y]
    (U : X â¥¤ Y) (F : A â¥¤ B) (G : C â¥¤ B) :
    (CatCommSq.iso (transform Y|>.obj <| .id F G) (precompose F G|>.obj U)
      (precompose F G|>.obj U) (transform X|>.obj <| .id F G)).hom â‰«
      whiskerLeft (precompose F G|>.obj U) (transformObjId X F G).hom =
    whiskerRight (transformObjId Y F G).hom (precompose F G|>.obj U) â‰«
      (precompose F G|>.obj U).leftUnitor.hom â‰«
      (precompose F G|>.obj U).rightUnitor.inv := by
  aesop_cat

/-- The square `transformPrecomposeSquare` respects compositions. -/
lemma transformPrecomposeObjSquare_iso_hom_comp
    {A'' : Type uâ‚‡} {B'' : Type uâ‚ˆ} {C'' : Type uâ‚‰}
    [Category.{vâ‚‡} A''] [Category.{vâ‚ˆ} B''] [Category.{vâ‚‰} C'']
    {F'' : A'' â¥¤ B''} {G'' : C'' â¥¤ B''}
    {X : Type uâ‚â‚€} {Y : Type uâ‚â‚} [Category.{vâ‚â‚€} X] [Category.{vâ‚â‚} Y]
    (U : X â¥¤ Y) (Ïˆ : CatCospanTransform F G F' G')
    (Ïˆ' : CatCospanTransform F' G' F'' G'') :
    (CatCommSq.iso (transform Y|>.obj <| Ïˆ.comp Ïˆ') (precompose F G|>.obj U)
      (precompose F'' G''|>.obj U) (transform X|>.obj <| Ïˆ.comp Ïˆ')).hom â‰«
      whiskerLeft (precompose F G|>.obj U) (transformObjComp X Ïˆ Ïˆ').hom =
    whiskerRight (transformObjComp Y Ïˆ Ïˆ').hom (precompose F'' G''|>.obj U) â‰«
      (Functor.associator _ _ _).hom â‰«
      whiskerLeft (transform Y|>.obj Ïˆ)
        (CatCommSq.iso _ (precompose F' G'|>.obj U)
          _ (transform X|>.obj Ïˆ')).hom â‰«
      (Functor.associator _ _ _).inv â‰«
      whiskerRight (CatCommSq.iso _ _ _ _).hom (transform X|>.obj Ïˆ') â‰«
      (Functor.associator _ _ _).hom := by
  aesop_cat

end compatibility

end CatCommSqOver

end Bifunctoriality

end CategoricalPullback

end

end CategoryTheory.Limits
