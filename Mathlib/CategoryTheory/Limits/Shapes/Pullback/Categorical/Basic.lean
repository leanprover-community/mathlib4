/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.CatCommSq
import Mathlib.CategoryTheory.Functor.ReflectsIso.Basic
import Mathlib.CategoryTheory.Products.Basic

/-! # Categorical pullback squares

This file defines the basic properties of categorical pullback squares.

Given a pair of functors `(F : A ‚•§ B, G : C ‚•§ B)`, we define the category
`CategoricalPullback F G` as the category of triples
`(a : A, c : C, e : F.obj a ‚âÖ G.obj b)`.

The category `CategoricalPullback F G` sits in a canonical `CatCommSq`, and we formalize that
this square is a "limit" in the following sense: functors `X ‚•§ CategoricalPullback F G` are
equivalent to pairs of functors `(L : X ‚•§ A, R : X ‚•§ C)` equipped with a natural isomorphism
`L ‚ãô F ‚âÖ R ‚ãô G`.

We formalize this by introducing a category `CatCommSqOver F G X` that encodes
exactly this data, and we prove that the category of functors `X ‚•§ CategoricalPullback F G` is
equivalent to `CatCommSqOver F G X`.

## Main declarations

* `CategoricalPullback F G`: the type of the categorical pullback.
* `œÄ‚Çó F G : CategoricalPullback F G` and `œÄ·µ£ F G : CategoricalPullback F G`: the canonical
  projections.
* `CategoricalPullback.catCommSq`: the canonical `CatCommSq (œÄ‚Çó F G) (œÄ·µ£ F G) F G` which exhibits
  `CategoricalPullback F G` as the pullback (in the (2,1)-categorical sense)
  of the cospan of `F` and `G`.
* `CategoricalPullback.functorEquiv F G X`: the equivalence of categories between functors
  `X ‚•§ CategoricalPullback F G` and `CatCommSqOver F G X`, where the latter is an abbrev for
  `CategoricalPullback (whiskeringRight X A B|>.obj F) (whiskeringRight X C B|>.obj G)`.

## References
* [Kerodon: section 1.4.5.2](https://kerodon.net/tag/032Y)
* [Niles Johnson, Donald Yau, *2-Dimensional Categories*](https://arxiv.org/abs/2002.06055),
  example 5.3.9, although we take a slightly different (equivalent) model of the object.

## TODOs:
* 2-functoriality of the construction with respect to "transformation of categorical
  cospans".
* Full equivalence-invariance of the notion (follows from suitable 2-functoriality).
* Define a `CatPullbackSquare` typeclass extending `CatCommSq`that encodes the
  fact that a given `CatCommSq` defines an equivalence between the top left
  corner and the categorical pullback of its legs.
* Define a `IsCatPullbackSquare` propclass.
* Define the "categorical fiber" of a functor at an object of the target category.
* Pasting calculus for categorical pullback squares.
* Categorical pullback squares attached to Grothendieck constructions of pseudofunctors.
* Stability of (co)fibered categories under categorical pullbacks.

-/

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ v‚ÇÑ u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ

namespace CategoryTheory.Limits

attribute [local simp] CatCommSq.iso_hom_naturality  CatCommSq.iso_inv_naturality

section

variable {A : Type u‚ÇÅ} {B : Type u‚ÇÇ} {C : Type u‚ÇÉ}
  [Category.{v‚ÇÅ} A] [Category.{v‚ÇÇ} B] [Category.{v‚ÇÉ} C]
  (F : A ‚•§ B) (G : C ‚•§ B)

/-- The `CategoricalPullback F G` is the category of triples
`(a : A, c : C, F a ‚âÖ G c)`.
Morphisms `(a, c, e) ‚ü∂ (a', c', e')` are pairs of morphisms
`(f‚ÇÅ : a ‚ü∂ a', f‚ÇÇ : c ‚ü∂ c')` compatible with the specified
isomorphisms. -/
@[kerodon 032Z]
structure CategoricalPullback where
  /-- the left element -/
  left : A
  /-- the right element -/
  right : C
  /-- the structural isomorphism `F.obj left ‚âÖ G.obj right` -/
  iso : F.obj left ‚âÖ G.obj right

namespace CategoricalPullback

scoped notation:max L:max " ‚ä° " R:max => CategoricalPullback L R

variable {F G}

/-- The Hom types for the categorical pullback are given by pairs of maps compatible with the
structural isomorphisms. -/
structure Hom (x y : F ‚ä° G) where
  /-- the left component of `f : Hom x y` is a morphism `x.left ‚ü∂ y.left` -/
  left : x.left ‚ü∂ y.left
  /-- the right component of `f : Hom x y` is a morphism `x.right ‚ü∂ y.right` -/
  right : x.right ‚ü∂ y.right
  /-- the compatibility condition on `left` and `right` with respect to the structure
  isompophisms -/
  w : F.map left ‚â´ y.iso.hom = x.iso.hom ‚â´ G.map right := by aesop_cat

attribute [reassoc (attr := simp)] Hom.w

@[simps! id_left id_right comp_left comp_right]
instance : Category (CategoricalPullback F G) where
  Hom x y := CategoricalPullback.Hom x y
  id x :=
    { left := ùüô x.left
      right := ùüô x.right }
  comp f g :=
    { left := f.left ‚â´ g.left
      right := f.right ‚â´ g.right }

attribute [reassoc (attr := simp)] comp_left comp_right

/-- Naturality square for morphisms in the inverse direction. -/
@[reassoc (attr := simp)]
lemma Hom.w' {x y : F ‚ä° G} (f : x ‚ü∂ y) :
    G.map f.right ‚â´ y.iso.inv = x.iso.inv ‚â´ F.map f.left := by
  rw [Iso.comp_inv_eq, Category.assoc, Eq.comm, Iso.inv_comp_eq, f.w]

attribute [local ext] Hom in
/-- Extensionnality principle for morphisms in `CategoricalPullback F G`. -/
@[ext]
theorem hom_ext {x y : F ‚ä° G} {f g : x ‚ü∂ y}
    (h‚Çó : f.left = g.left) (h·µ£ : f.right = g.right) : f = g := by
  apply Hom.ext <;> assumption

section

variable (F G)

/-- `CategoricalPullback.œÄ‚Çó F G` is the left projection `CategoricalPullback F G ‚•§ A`. -/
@[simps]
def œÄ‚Çó : F ‚ä° G ‚•§ A where
  obj x := x.left
  map f := f.left

/-- `CategoricalPullback.œÄ·µ£ F G` is the right projection `CategoricalPullback F G ‚•§ C`. -/
@[simps]
def œÄ·µ£ : F ‚ä° G ‚•§ C where
  obj x := x.right
  map f := f.right

/-- The canonical categorical commutative square in which `CategoricalPullback F G` sits. -/
instance catCommSq : CatCommSq (œÄ‚Çó F G) (œÄ·µ£ F G) F G where
  iso' := NatIso.ofComponents (fun x ‚Ü¶ x.iso)

@[simp]
lemma catCommSq_iso_hom_app (x : F ‚ä° G) :
    (CatCommSq.iso (œÄ‚Çó F G) (œÄ·µ£ F G) F G).hom.app x = x.iso.hom := rfl

@[simp]
lemma catCommSq_iso_inv_app (x : F ‚ä° G) :
    (CatCommSq.iso (œÄ‚Çó F G) (œÄ·µ£ F G) F G).inv.app x = x.iso.inv := rfl

variable {F G} in
@[simps!]
def mkIso {x y : F ‚ä° G}
    (e‚Çó : x.left ‚âÖ y.left) (e·µ£ : x.right ‚âÖ y.right)
    (w : F.map e‚Çó.hom ‚â´ y.iso.hom = x.iso.hom ‚â´ G.map e·µ£.hom := by aesop_cat) :
    x ‚âÖ y where
  hom := ‚ü®e‚Çó.hom, e·µ£.hom, w‚ü©
  inv := ‚ü®e‚Çó.inv, e·µ£.inv, by simpa using F.map e‚Çó.inv ‚â´= w.symm =‚â´ G.map e·µ£.inv‚ü©

instance {x y : F ‚ä° G} (f : x ‚ü∂ y) [IsIso f] : IsIso f.left :=
  inferInstanceAs (IsIso ((œÄ‚Çó _ _).mapIso (asIso f)).hom)

instance {x y : F ‚ä° G} (f : x ‚ü∂ y) [IsIso f] : IsIso f.right :=
  inferInstanceAs (IsIso ((œÄ·µ£ _ _).mapIso (asIso f)).hom)

end

section

variable (X : Type u‚ÇÑ) [Category.{v‚ÇÑ} X]

variable (F G) in
/-- The data of a categorical commutative square over a cospan `F, G` with cone point `X` is
that of a functor `T : X ‚•§ A`, a functor `L : X ‚•§ C`, and a `CatCommSqOver T L F G`.
Note that this is *exactly* what an object of
`((whiskeringRight X A B).obj F) ‚ä° ((whiskeringRight X C B).obj G)` is,
so `CatCommSqOver F G X` is in fact an abbreviation for
`((whiskeringRight X A B).obj F) ‚ä° ((whiskeringRight X C B).obj G)`. -/
abbrev CatCommSqOver :=
  (whiskeringRight X A B|>.obj F) ‚ä° (whiskeringRight X C B|>.obj G)

namespace CatCommSqOver

@[simps!]
def asSquare (S : CatCommSqOver F G X) : CatCommSq S.left S.right F G where
  iso' := S.iso

@[reassoc (attr := simp)]
lemma iso_hom_naturality (S : CatCommSqOver F G X) {x x' : X} (f : x ‚ü∂ x') :
   F.map (S.left.map f) ‚â´ S.iso.hom.app x' =
   S.iso.hom.app x ‚â´ G.map (S.right.map f) :=
  S.iso.hom.naturality f

@[reassoc (attr := simp)]
lemma w_app {S S' : CatCommSqOver F G X} (œÜ : S ‚ü∂ S') (x : X) :
    F.map (œÜ.left.app x) ‚â´ S'.iso.hom.app x =
    S.iso.hom.app x ‚â´ G.map (œÜ.right.app x) :=
  NatTrans.congr_app œÜ.w x

variable (F G)

/-- The "left projection" of a CatCommSqOver as a functor. -/
abbrev L : CatCommSqOver F G X ‚•§ X ‚•§ A := œÄ‚Çó _ _

/-- The "right projection" of a CatCommSqOver as a functor. -/
abbrev R : CatCommSqOver F G X ‚•§ X ‚•§ C := œÄ·µ£ _ _

/-- The structure isompophism of a `CatCommSqOver` as a natural transformation. -/
abbrev e :
    (L F G X) ‚ãô (whiskeringRight X A B|>.obj F) ‚âÖ
    (R F G X) ‚ãô (whiskeringRight X C B|>.obj G) :=
  NatIso.ofComponents
    (fun S ‚Ü¶ S.iso)

end CatCommSqOver

section functorEquiv

variable (F G)

-- We need to split up the definition of `functorEquiv` to avoid timeouts.

/-- Interpret a functor to the categorical pullback as a `CatCommSqOver`. -/
@[simps!]
def toCatCommSqOver : (X ‚•§ F ‚ä° G) ‚•§ CatCommSqOver F G X where
  obj J :=
    { left := J ‚ãô œÄ‚Çó F G
      right := J ‚ãô œÄ·µ£ F G
      iso :=
        Functor.associator _ _ _ ‚â™‚â´
          isoWhiskerLeft J (catCommSq F G).iso ‚â™‚â´
          (Functor.associator _ _ _).symm }
  map {J J'} F :=
    { left := whiskerRight F (œÄ‚Çó _ _)
      right := whiskerRight F (œÄ·µ£ _ _) }

/-- Interpret a `CatCommSqOver` as a functor to the categorical pullback. -/
@[simps!]
def CatCommSqOver.toFunctorToCategoricalPullback :
    (CatCommSqOver F G X) ‚•§ X ‚•§ F ‚ä° G where
  obj S :=
    { obj x :=
        { left := S.left.obj x
          right := S.right.obj x
          iso := S.iso.app x }
      map {x y} f :=
        { left := S.left.map f
          right := S.right.map f } }
  map {S S'} œÜ :=
    { app x :=
        { left := œÜ.left.app x
          right := œÜ.right.app x } }

/-- The unit of `CategoricalPullback.functorEquiv`. -/
@[simps!]
def functorEquivUnitIso :
    ùü≠ (X ‚•§ F ‚ä° G) ‚âÖ
    toCatCommSqOver F G X ‚ãô CatCommSqOver.toFunctorToCategoricalPullback F G X :=
  NatIso.ofComponents
    (fun _ ‚Ü¶ NatIso.ofComponents
      (fun _ ‚Ü¶ CategoricalPullback.mkIso (.refl _) (.refl _)))

/-- The counit of `CategoricalPullback.functorEquiv`. -/
@[simps!]
def functorEquivCounitIso :
    CatCommSqOver.toFunctorToCategoricalPullback F G X ‚ãô toCatCommSqOver F G X ‚âÖ
    ùü≠ (CatCommSqOver F G X) :=
  NatIso.ofComponents
    (fun _ ‚Ü¶ CategoricalPullback.mkIso
      (NatIso.ofComponents (fun _ ‚Ü¶ .refl _)) (NatIso.ofComponents (fun _ ‚Ü¶ .refl _)))

/-- The universal property of categorical pullbacks, stated as an equivalence
of categories between functors `X ‚•§ (F ‚ä° G)` and categorical commutative squares
over X. -/
@[simps!]
def functorEquiv : (X ‚•§ F ‚ä° G) ‚âå CatCommSqOver F G X where
  functor := toCatCommSqOver F G X
  inverse := CatCommSqOver.toFunctorToCategoricalPullback F G X
  unitIso := functorEquivUnitIso F G X
  counitIso := functorEquivCounitIso F G X

variable {F G X}

/-- A constructor for natural isomorphisms of functors `X ‚•§ CategoricalPullback`: to
construct such an isomorphism, it suffices to produce isomorphisms after whiskering with
the projections, and compatible with the canonical 2-commutative square . -/
@[simps!]
def mkNatIso {J K : X ‚•§ F ‚ä° G}
    (e‚ÇÅ : J ‚ãô œÄ‚Çó F G ‚âÖ K ‚ãô œÄ‚Çó F G) (e‚ÇÇ : J ‚ãô œÄ·µ£ F G ‚âÖ K ‚ãô œÄ·µ£ F G)
    (coh :
      whiskerRight e‚ÇÅ.hom F ‚â´ (Functor.associator _ _ _).hom ‚â´
        whiskerLeft K (CatCommSq.iso (œÄ‚Çó F G) (œÄ·µ£ F G) F G).hom ‚â´
        (Functor.associator _ _ _).inv =
      (Functor.associator _ _ _).hom ‚â´
        whiskerLeft J (CatCommSq.iso (œÄ‚Çó F G) (œÄ·µ£ F G) F G).hom ‚â´
        (Functor.associator _ _ _).inv ‚â´
        whiskerRight e‚ÇÇ.hom G := by aesop_cat) :
    J ‚âÖ K :=
  NatIso.ofComponents
    (fun x ‚Ü¶ CategoricalPullback.mkIso (e‚ÇÅ.app x) (e‚ÇÇ.app x)
      (by simpa using NatTrans.congr_app coh x))
    (fun {_ _} f ‚Ü¶ by
      ext
      ¬∑ exact e‚ÇÅ.hom.naturality f
      ¬∑ exact e‚ÇÇ.hom.naturality f)

/-- To check equality of two natural transformations of functors to a `CategoricalPullback`, it
suffices to do so after whiskering with the projections. -/
@[ext]
lemma natTrans_ext
    {J K : X ‚•§ F ‚ä° G} {Œ± Œ≤ : J ‚ü∂ K}
    (e‚ÇÅ : whiskerRight Œ± (œÄ‚Çó F G) = whiskerRight Œ≤ (œÄ‚Çó F G))
    (e‚ÇÇ : whiskerRight Œ± (œÄ·µ£ F G) = whiskerRight Œ≤ (œÄ·µ£ F G)) :
    Œ± = Œ≤ := by
  ext x
  ¬∑ exact congrArg (fun t ‚Ü¶ t.app x) e‚ÇÅ
  ¬∑ exact congrArg (fun t ‚Ü¶ t.app x) e‚ÇÇ

/-- Comparing mkNatIso with the corresponding construction one can deduce from
`functorEquiv`. -/
lemma mkNatIso_eq {J K : X ‚•§ F ‚ä° G}
    (e‚ÇÅ : J ‚ãô œÄ‚Çó F G ‚âÖ K ‚ãô œÄ‚Çó F G) (e‚ÇÇ : J ‚ãô œÄ·µ£ F G ‚âÖ K ‚ãô œÄ·µ£ F G)
    (coh :
      whiskerRight e‚ÇÅ.hom F ‚â´ (Functor.associator _ _ _).hom ‚â´
        whiskerLeft K (CatCommSq.iso (œÄ‚Çó F G) (œÄ·µ£ F G) F G).hom ‚â´
        (Functor.associator _ _ _).inv =
      (Functor.associator _ _ _).hom ‚â´
        whiskerLeft J (CatCommSq.iso (œÄ‚Çó F G) (œÄ·µ£ F G) F G).hom ‚â´
        (Functor.associator _ _ _).inv ‚â´
        whiskerRight e‚ÇÇ.hom G := by aesop_cat) :
  mkNatIso e‚ÇÅ e‚ÇÇ coh =
    (functorEquiv F G X).fullyFaithfulFunctor.preimageIso
      (CategoricalPullback.mkIso e‚ÇÅ e‚ÇÇ
        (by simpa [functorEquiv, toCatCommSqOver] using coh)) := by
  ext
  ¬∑ simp [Equivalence.fullyFaithfulFunctor]
  ¬∑ simp [Equivalence.fullyFaithfulFunctor]

end functorEquiv

end

end CategoricalPullback

end

end CategoryTheory.Limits
