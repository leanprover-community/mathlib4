/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.Categorical.Square

/-! # Pasting calculus of Categorical pullback squares.

In this file, we prove that the well-known pasting calculus for
pullbacks extends to categorical pullbacks.
More precisely, given a diagram of categories

```
  C‚ÇÅ - T‚ÇÅ -> C‚ÇÇ - T‚ÇÇ -> C‚ÇÉ
  |          |          |
  V‚ÇÅ         V‚ÇÇ         V‚ÇÉ
  |          |          |
  ‚à®          ‚à®          ‚à®
  C‚ÇÑ - B‚ÇÅ -> C‚ÇÖ - B‚ÇÇ -> C‚ÇÜ
```

with natural isomorphisms filling in the squares,
assuming the rightmost square is a categorical pullback square,
then the left square is a categorical pullback square if and only
if the outer square is a categorical pullback square.

### TODOs
- Vertical composition
- Give good (d)simp lemmas when both squares as the default ones, *i.e*
give good lemmas for the equivalence `V‚ÇÉ ‚ä° (B‚ÇÅ ‚ãô B‚ÇÇ) ‚âå (œÄ‚ÇÅ T‚ÇÇ V‚ÇÇ) ‚ä° B‚ÇÅ`.
-/

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ v‚ÇÑ v‚ÇÖ v‚ÇÜ u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ u‚ÇÖ u‚ÇÜ

namespace CategoryTheory.Limits

open Limits.CategoricalPullback
open scoped Limits.CategoricalPullback

variable {C‚ÇÅ : Type u‚ÇÅ} {C‚ÇÇ : Type u‚ÇÇ} {C‚ÇÉ : Type u‚ÇÉ}
  {C‚ÇÑ : Type u‚ÇÑ} {C‚ÇÖ : Type u‚ÇÖ} {C‚ÇÜ : Type u‚ÇÜ}
  [Category.{v‚ÇÅ} C‚ÇÅ] [Category.{v‚ÇÇ} C‚ÇÇ] [Category.{v‚ÇÉ} C‚ÇÉ]
  [Category.{v‚ÇÑ} C‚ÇÑ] [Category.{v‚ÇÖ} C‚ÇÖ] [Category.{v‚ÇÜ} C‚ÇÜ]

namespace CatPullbackSquare

section hComp

-- This prevents some degree of defeq abuse
seal functorEquiv.inverse functorEquiv.counitIsoAppFst
  functorEquiv.counitIsoAppSnd

variable (T‚ÇÅ : C‚ÇÅ ‚•§ C‚ÇÇ) (T‚ÇÇ : C‚ÇÇ ‚•§ C‚ÇÉ)
    (V‚ÇÅ : C‚ÇÅ ‚•§ C‚ÇÑ) (V‚ÇÇ : C‚ÇÇ ‚•§ C‚ÇÖ) (V‚ÇÉ : C‚ÇÉ ‚•§ C‚ÇÜ)
    (B‚ÇÅ : C‚ÇÑ ‚•§ C‚ÇÖ) (B‚ÇÇ : C‚ÇÖ ‚•§ C‚ÇÜ)

section

variable [CatCommSq T‚ÇÅ V‚ÇÅ V‚ÇÇ B‚ÇÅ] [CatCommSq T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÇ]
    [CatCommSq (T‚ÇÅ ‚ãô T‚ÇÇ) V‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ)]
    (h : CatCommSq.iso (T‚ÇÅ ‚ãô T‚ÇÇ) V‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ) =
      (CatCommSq.hComp T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ).iso)
    [CatPullbackSquare T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÇ]

section

variable [CatPullbackSquare T‚ÇÅ V‚ÇÅ V‚ÇÇ B‚ÇÅ]

/-- (impl.) A `CatCommSqOver` that describes a functor `V‚ÇÉ ‚ä° (B‚ÇÅ ‚ãô B‚ÇÇ) ‚•§ C‚ÇÇ` -/
@[simps]
def hComp.S‚ÇÄ : CatCommSqOver V‚ÇÉ B‚ÇÇ (V‚ÇÉ ‚ä° (B‚ÇÅ ‚ãô B‚ÇÇ)) where
  fst := œÄ‚ÇÅ _ _
  snd := œÄ‚ÇÇ _ _ ‚ãô B‚ÇÅ
  iso := CatCommSq.iso (œÄ‚ÇÅ _ _) (œÄ‚ÇÇ _ _) V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ) ‚â™‚â´
    (Functor.associator _ _ _).symm

/-- (impl.) A `CatCommSqOver` that describes the functor `V‚ÇÉ ‚ä° (B‚ÇÅ ‚ãô B‚ÇÇ) ‚•§ C‚ÇÅ`
that will be used as the quasi-inverse to the canonical functor
`C‚ÇÅ ‚•§ V‚ÇÉ ‚ä° (B‚ÇÅ ‚ãô B‚ÇÇ)` induced by the horizontal composite square. -/
@[simps]
def hComp.S‚ÇÅ : CatCommSqOver V‚ÇÇ B‚ÇÅ (V‚ÇÉ ‚ä° (B‚ÇÅ ‚ãô B‚ÇÇ)) where
  fst := functorEquiv T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÇ (V‚ÇÉ ‚ä° (B‚ÇÅ ‚ãô B‚ÇÇ))|>.inverse.obj <|
    hComp.S‚ÇÄ V‚ÇÉ B‚ÇÅ B‚ÇÇ
  snd := œÄ‚ÇÇ _ _
  iso := functorEquivInverseWhiskeringIsoSnd _ _ _ _ _|>.app _

/-- The functor `V‚ÇÉ ‚ä° (B‚ÇÅ ‚ãô B‚ÇÇ) ‚•§ C‚ÇÅ` that
will be used as the quasi-inverse to the canonical functor `C‚ÇÅ ‚•§ V‚ÇÉ ‚ä° (B‚ÇÅ ‚ãô B‚ÇÇ)`
induced by the horizontal composite square. -/
abbrev hCompInverse : V‚ÇÉ ‚ä° (B‚ÇÅ ‚ãô B‚ÇÇ) ‚•§ C‚ÇÅ :=
  functorEquiv T‚ÇÅ V‚ÇÅ V‚ÇÇ B‚ÇÅ _|>.inverse.obj <| hComp.S‚ÇÅ T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ

/-- (Impl.) the counit isomorphism for the `CatPullbackSquare` structure on
the horizontal composition of two categorical pullback squares. -/
def hCompCounitIso :
    hCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ ‚ãô
      (CatCommSqOver.toFunctorToCategoricalPullback _ _ _).obj
        (.ofSquare (T‚ÇÅ ‚ãô T‚ÇÇ) V‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ)) ‚âÖ
    ùü≠ (V‚ÇÉ ‚ä° (B‚ÇÅ ‚ãô B‚ÇÇ)) :=
  mkNatIso (œÄ‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ)) (œÄ‚ÇÇ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ)) V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ) (V‚ÇÉ ‚ä° (B‚ÇÅ ‚ãô B‚ÇÇ))
    ((hCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ).associator _ (œÄ‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ)) ‚â™‚â´
      (hCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ).isoWhiskerLeft
        (Iso.refl _ : _ ‚âÖ T‚ÇÅ ‚ãô T‚ÇÇ) ‚â™‚â´
        ((hCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ).associator T‚ÇÅ T‚ÇÇ).symm ‚â™‚â´
        (Functor.isoWhiskerRight
          (functorEquivInverseWhiskeringIsoFst
            T‚ÇÅ V‚ÇÅ V‚ÇÇ B‚ÇÅ (V‚ÇÉ ‚ä° (B‚ÇÅ ‚ãô B‚ÇÇ))|>.app <| hComp.S‚ÇÅ T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ)
          T‚ÇÇ) ‚â™‚â´
        (functorEquivInverseWhiskeringIsoFst
          T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÇ (V‚ÇÉ ‚ä° (B‚ÇÅ ‚ãô B‚ÇÇ))|>.app <| hComp.S‚ÇÄ V‚ÇÉ B‚ÇÅ B‚ÇÇ) ‚â™‚â´
        (œÄ‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ)).leftUnitor.symm)
    ((hCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ).associator _ (œÄ‚ÇÇ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ)) ‚â™‚â´
      (hCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ).isoWhiskerLeft
        (Iso.refl _ : _ ‚âÖ V‚ÇÅ) ‚â™‚â´
      (functorEquivInverseWhiskeringIsoSnd
        T‚ÇÅ V‚ÇÅ V‚ÇÇ B‚ÇÅ (V‚ÇÉ ‚ä° (B‚ÇÅ ‚ãô B‚ÇÇ))|>.app <| hComp.S‚ÇÅ T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ) ‚â™‚â´
      (œÄ‚ÇÇ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ)).leftUnitor.symm)
    (by
      ext x
      have coh1 := counitCoherence_hom_app T‚ÇÅ V‚ÇÅ (hComp.S‚ÇÅ T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ) x
      have coh2 := counitCoherence_hom_app T‚ÇÇ V‚ÇÇ (hComp.S‚ÇÄ V‚ÇÉ B‚ÇÅ B‚ÇÇ) x
      simp only [Functor.comp_obj, functorEquiv_functor_obj_fst, hComp.S‚ÇÅ_snd,
        Functor.whiskeringRight_obj_obj, œÄ‚ÇÇ_obj, Functor.id_obj, hComp.S‚ÇÅ_fst,
        hComp.S‚ÇÅ_iso, Iso.app_hom,
        functorEquivInverseWhiskeringIsoSnd_hom_app_app, hComp.S‚ÇÄ_snd,
        functorEquiv_functor_obj_snd, hComp.S‚ÇÄ_fst, œÄ‚ÇÅ_obj, hComp.S‚ÇÄ_iso,
        Iso.trans_hom, Iso.symm_hom, NatTrans.comp_app, catCommSq_iso_hom_app,
        Functor.associator_inv_app, Category.comp_id] at coh1 coh2
      simp [h, coh2, ‚Üê Functor.map_comp, coh1])

/-- (Impl.) the unit isomorphism for the `CatPullbackSquare` structure on
the horizontal composition of two categorical pullback squares. -/
def hCompUnitIso :
    ùü≠ C‚ÇÅ ‚âÖ
    (CatCommSqOver.toFunctorToCategoricalPullback _ _ _|>.obj <|
      .ofSquare (T‚ÇÅ ‚ãô T‚ÇÇ) V‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ)) ‚ãô
      hCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ :=
  letI U := (CatCommSqOver.toFunctorToCategoricalPullback V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ) C‚ÇÅ).obj
    (CatCommSqOver.ofSquare (T‚ÇÅ ‚ãô T‚ÇÇ) V‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ))
  letI Œ® :
      ùü≠ C‚ÇÅ ‚ãô T‚ÇÅ ‚âÖ
      U ‚ãô (functorEquiv T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÇ V‚ÇÉ ‚ä° (B‚ÇÅ ‚ãô B‚ÇÇ)).inverse.obj
        (hComp.S‚ÇÄ V‚ÇÉ B‚ÇÅ B‚ÇÇ) :=
    mkNatIso T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÇ _
      ((ùü≠ C‚ÇÅ).associator T‚ÇÅ T‚ÇÇ ‚â™‚â´ (T‚ÇÅ ‚ãô T‚ÇÇ).leftUnitor ‚â™‚â´
        Functor.isoWhiskerLeft U
          (functorEquivInverseWhiskeringIsoFst
            T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÇ (V‚ÇÉ ‚ä° (B‚ÇÅ ‚ãô B‚ÇÇ))|>.app <| hComp.S‚ÇÄ V‚ÇÉ B‚ÇÅ B‚ÇÇ).symm ‚â™‚â´
        (U.associator _ T‚ÇÇ).symm)
      ((ùü≠ C‚ÇÅ).associator T‚ÇÅ V‚ÇÇ ‚â™‚â´ (T‚ÇÅ ‚ãô V‚ÇÇ).leftUnitor ‚â™‚â´
        (CatCommSq.iso T‚ÇÅ V‚ÇÅ V‚ÇÇ B‚ÇÅ) ‚â™‚â´
        (Functor.isoWhiskerRight (Iso.refl _ : _ ‚âÖ V‚ÇÅ) B‚ÇÅ) ‚â™‚â´
        (Functor.associator U (œÄ‚ÇÇ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ)) B‚ÇÅ) ‚â™‚â´
        U.isoWhiskerLeft
          (functorEquivInverseWhiskeringIsoSnd
            T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÇ (V‚ÇÉ ‚ä° (B‚ÇÅ ‚ãô B‚ÇÇ))|>.app <| hComp.S‚ÇÄ V‚ÇÉ B‚ÇÅ B‚ÇÇ).symm ‚â™‚â´
        (U.associator _ V‚ÇÇ).symm)
  mkNatIso T‚ÇÅ V‚ÇÅ V‚ÇÇ B‚ÇÅ C‚ÇÅ
    (Œ® ‚â™‚â´
      U.isoWhiskerLeft
        (functorEquivInverseWhiskeringIsoFst
          T‚ÇÅ V‚ÇÅ V‚ÇÇ B‚ÇÅ (V‚ÇÉ ‚ä° (B‚ÇÅ ‚ãô B‚ÇÇ))|>.app <| hComp.S‚ÇÅ T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ).symm ‚â™‚â´
        (U.associator (hCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ) T‚ÇÅ).symm)
    (V‚ÇÅ.leftUnitor ‚â™‚â´
      U.isoWhiskerLeft
        (functorEquivInverseWhiskeringIsoSnd
          T‚ÇÅ V‚ÇÅ V‚ÇÇ B‚ÇÅ (V‚ÇÉ ‚ä° (B‚ÇÅ ‚ãô B‚ÇÇ))|>.app <| hComp.S‚ÇÅ T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ).symm ‚â™‚â´
      (U.associator (hCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ) V‚ÇÅ).symm)

/-- If two `CatPullbackSquare`s are pasted horizontally, the resulting square
is a `CatPullbackSquare`. -/
def hComp :
    CatPullbackSquare (T‚ÇÅ ‚ãô T‚ÇÇ) V‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ) :=
  { inverse := hCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ
    unitIso := hCompUnitIso T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ h
    counitIso := hCompCounitIso T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ h
    functorEquiv_inverse_obj_unitIso_comp x := by
      ext <;> dsimp [hCompUnitIso, hCompCounitIso]
      ¬∑ simp [‚Üê Functor.map_comp_assoc]
      ¬∑ simp }

end

section

variable [CatPullbackSquare (T‚ÇÅ ‚ãô T‚ÇÇ) V‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ)]

/-- (impl.) A `CatCommSq` that defines a functor `V‚ÇÇ ‚ä° B‚ÇÅ ‚•§ C‚ÇÅ` that
will be inverse to the canonical functor. -/
@[simps]
def ofHComp.S‚ÇÄ : CatCommSqOver V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ) V‚ÇÇ ‚ä° B‚ÇÅ where
  fst := œÄ‚ÇÅ _ _ ‚ãô T‚ÇÇ
  snd := œÄ‚ÇÇ _ _
  iso :=
    Functor.associator _ _ _ ‚â™‚â´
    Functor.isoWhiskerLeft (œÄ‚ÇÅ _ _) (CatCommSq.iso T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÇ) ‚â™‚â´
    (Functor.associator _ _ _).symm ‚â™‚â´
    Functor.isoWhiskerRight (CatCommSq.iso (œÄ‚ÇÅ V‚ÇÇ B‚ÇÅ) (œÄ‚ÇÇ V‚ÇÇ B‚ÇÅ) V‚ÇÇ B‚ÇÅ) B‚ÇÇ ‚â™‚â´
    Functor.associator _ _ _

/-- (impl.) A functor `V‚ÇÇ ‚ä° B‚ÇÅ ‚•§ C‚ÇÅ` that will be inverse of the canonical
morphism to the categorical pullaback. -/
abbrev ofHCompInverse : V‚ÇÇ ‚ä° B‚ÇÅ ‚•§ C‚ÇÅ :=
    functorEquiv (T‚ÇÅ ‚ãô T‚ÇÇ) V‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ) _|>.inverse.obj <|
      ofHComp.S‚ÇÄ T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ

/-- (impl.) The unit isomorphism for the equivalence `V‚ÇÇ ‚ä° B‚ÇÅ ‚âå C‚ÇÅ` that will
exhibit the left square as a categorical pullback square if the outer and right
squares are categorical pullback squares. -/
def ofHCompUnitIso :
    ùü≠ C‚ÇÅ ‚âÖ
    (CatCommSqOver.toFunctorToCategoricalPullback _ _ _|>.obj <|
      .ofSquare T‚ÇÅ V‚ÇÅ V‚ÇÇ B‚ÇÅ) ‚ãô
      (ofHCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ) :=
  letI U := CatCommSqOver.toFunctorToCategoricalPullback V‚ÇÇ B‚ÇÅ C‚ÇÅ|>.obj <|
    .ofSquare T‚ÇÅ V‚ÇÅ V‚ÇÇ B‚ÇÅ
  mkNatIso (T‚ÇÅ ‚ãô T‚ÇÇ) V‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ) _
    ((T‚ÇÅ ‚ãô T‚ÇÇ).leftUnitor ‚â™‚â´
      Functor.isoWhiskerRight (.refl _ : _ ‚âÖ T‚ÇÅ) T‚ÇÇ ‚â™‚â´
      U.associator (œÄ‚ÇÅ V‚ÇÇ B‚ÇÅ) T‚ÇÇ ‚â™‚â´
      Functor.isoWhiskerLeft U
        (functorEquivInverseWhiskeringIsoFst
          (T‚ÇÅ ‚ãô T‚ÇÇ) V‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ) _|>.app <| ofHComp.S‚ÇÄ T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ).symm ‚â™‚â´
      (U.associator (ofHCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ) (T‚ÇÅ ‚ãô T‚ÇÇ)).symm)
    (V‚ÇÅ.leftUnitor ‚â™‚â´
      Functor.isoWhiskerLeft U
        (functorEquivInverseWhiskeringIsoSnd
          (T‚ÇÅ ‚ãô T‚ÇÇ) V‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ) _|>.app <| ofHComp.S‚ÇÄ T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ).symm ‚â™‚â´
      (U.associator (ofHCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ) V‚ÇÅ).symm)
    (by
      ext x
      simpa [U, h] using
        (counitCoherence_inv_app (T‚ÇÅ ‚ãô T‚ÇÇ) V‚ÇÅ (ofHComp.S‚ÇÄ T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ)
          (U.obj x)))

/-- (impl.) The counit isomorphism for the equivalence `V‚ÇÇ ‚ä° B‚ÇÅ ‚âå C‚ÇÅ` that will
exhibit the left square as a categorical pullback square if the outer and right
squares are categorical pullback squares. -/
def ofHCompCounitIso :
    ofHCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ ‚ãô
      (CatCommSqOver.toFunctorToCategoricalPullback _ _ _).obj
        (.ofSquare T‚ÇÅ V‚ÇÅ V‚ÇÇ B‚ÇÅ) ‚âÖ
    ùü≠ (V‚ÇÇ ‚ä° B‚ÇÅ) :=
  letI Œ® : (ofHCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ) ‚ãô T‚ÇÅ ‚âÖ œÄ‚ÇÅ V‚ÇÇ B‚ÇÅ :=
    mkNatIso T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÇ _
      ((ofHCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ).associator T‚ÇÅ T‚ÇÇ ‚â™‚â´
        (functorEquivInverseWhiskeringIsoFst
          (T‚ÇÅ ‚ãô T‚ÇÇ) V‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ) _|>.app <| ofHComp.S‚ÇÄ T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ))
      ((ofHCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ).associator T‚ÇÅ V‚ÇÇ ‚â™‚â´
        (ofHCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ).isoWhiskerLeft
          (CatCommSq.iso T‚ÇÅ V‚ÇÅ V‚ÇÇ B‚ÇÅ) ‚â™‚â´
        (ofHCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ|>.associator V‚ÇÅ B‚ÇÅ).symm ‚â™‚â´
        Functor.isoWhiskerRight
          (functorEquivInverseWhiskeringIsoSnd
            (T‚ÇÅ ‚ãô T‚ÇÇ) V‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ) _|>.app <| ofHComp.S‚ÇÄ T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ) B‚ÇÅ ‚â™‚â´
        (CatCommSq.iso (œÄ‚ÇÅ V‚ÇÇ B‚ÇÅ) (œÄ‚ÇÇ V‚ÇÇ B‚ÇÅ) V‚ÇÇ B‚ÇÅ).symm)
      (by
        ext x
        simpa [h] using
          counitCoherence_hom_app (T‚ÇÅ ‚ãô T‚ÇÇ) V‚ÇÅ
            (ofHComp.S‚ÇÄ T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ) x =‚â´
            (B‚ÇÇ.map x.iso.inv))
  mkNatIso (œÄ‚ÇÅ V‚ÇÇ B‚ÇÅ) (œÄ‚ÇÇ V‚ÇÇ B‚ÇÅ) V‚ÇÇ B‚ÇÅ _
    ((ofHCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ).associator _ (œÄ‚ÇÅ V‚ÇÇ B‚ÇÅ) ‚â™‚â´
      (ofHCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ).isoWhiskerLeft (.refl _ : _ ‚âÖ T‚ÇÅ) ‚â™‚â´
      Œ® ‚â™‚â´ (œÄ‚ÇÅ V‚ÇÇ B‚ÇÅ).leftUnitor.symm)
    ((ofHCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ).associator _ (œÄ‚ÇÇ V‚ÇÇ B‚ÇÅ) ‚â™‚â´
      (ofHCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ).isoWhiskerLeft (.refl _ : _ ‚âÖ V‚ÇÅ) ‚â™‚â´
      (functorEquivInverseWhiskeringIsoSnd
        (T‚ÇÅ ‚ãô T‚ÇÇ) V‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ) _|>.app <| ofHComp.S‚ÇÄ T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ) ‚â™‚â´
        (œÄ‚ÇÇ V‚ÇÇ B‚ÇÅ).leftUnitor.symm)
    (by
      ext x
      simp [Œ®])

/-- If two `CatCommSq`s are pasted horizontally and if the right outer squares
are `CatPullbackSquare`s, then the left square is a `CatPullbackSquare`. -/
def ofHComp :
    CatPullbackSquare T‚ÇÅ V‚ÇÅ V‚ÇÇ B‚ÇÅ :=
  { inverse := ofHCompInverse T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ
    unitIso := ofHCompUnitIso T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ h
    counitIso := ofHCompCounitIso T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ h
    functorEquiv_inverse_obj_unitIso_comp x := by
      ext <;> dsimp [ofHCompUnitIso, ofHCompCounitIso]
      ¬∑ apply IsCatPullbackSquare.hom_ext T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÇ
        ¬∑ simp [‚Üê Functor.comp_map]
        ¬∑ simp [‚Üê Functor.map_comp_assoc]
      ¬∑ simp }

end

end

end hComp

end CatPullbackSquare

namespace IsCatPullbackSquare

lemma isCatPullbackSquare_hComp_iff
    (T‚ÇÅ : C‚ÇÅ ‚•§ C‚ÇÇ) (T‚ÇÇ : C‚ÇÇ ‚•§ C‚ÇÉ)
    (V‚ÇÅ : C‚ÇÅ ‚•§ C‚ÇÑ) (V‚ÇÇ : C‚ÇÇ ‚•§ C‚ÇÖ) (V‚ÇÉ : C‚ÇÉ ‚•§ C‚ÇÜ)
    (B‚ÇÅ : C‚ÇÑ ‚•§ C‚ÇÖ) (B‚ÇÇ : C‚ÇÖ ‚•§ C‚ÇÜ)
    [CatCommSq T‚ÇÅ V‚ÇÅ V‚ÇÇ B‚ÇÅ] [CatCommSq T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÇ]
    [CatCommSq (T‚ÇÅ ‚ãô T‚ÇÇ) V‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ)]
    (h : CatCommSq.iso (T‚ÇÅ ‚ãô T‚ÇÇ) V‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ) =
      (CatCommSq.hComp T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ).iso)
    [h·µ£ : IsCatPullbackSquare T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÇ] :
    IsCatPullbackSquare (T‚ÇÅ ‚ãô T‚ÇÇ) V‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ) ‚Üî
      IsCatPullbackSquare T‚ÇÅ V‚ÇÅ V‚ÇÇ B‚ÇÅ where
  mp h' :=
    letI := h·µ£.nonempty_catPullbackSquare.some
    letI := h'.nonempty_catPullbackSquare.some
    ‚ü®‚ü®CatPullbackSquare.ofHComp T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ h‚ü©‚ü©
  mpr h' :=
    letI := h·µ£.nonempty_catPullbackSquare.some
    letI := h'.nonempty_catPullbackSquare.some
    ‚ü®‚ü®CatPullbackSquare.hComp T‚ÇÅ T‚ÇÇ V‚ÇÅ V‚ÇÇ V‚ÇÉ B‚ÇÅ B‚ÇÇ h‚ü©‚ü©

end IsCatPullbackSquare

end CategoryTheory.Limits
