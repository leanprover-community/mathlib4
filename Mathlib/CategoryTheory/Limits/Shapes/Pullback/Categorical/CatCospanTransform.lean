/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.CatCommSq

/-! # Morphisms of categorical cospans.

Given `F : A â¥¤ B`, `G : C â¥¤ B`, `F' : A' â¥¤ B'` and `G' : C' â¥¤ B'`,
this file defines `CatCospanTransform F G F' G'`, the category of
"categorical transformations" from the (categorical) cospan `F G` to
the (categorical) cospan `F' G'`. Such a transformation consists of a
diagram

```
    F   G
  A â¥¤ B â¥¢ C
Hâ‚|   |Hâ‚‚ |Hâ‚ƒ
  v   v   v
  A'â¥¤ B'â¥¢ C'
    F'  G'
```

with specified `CatCommSq`s expressing 2-commutativity of the squares. These
transformations are used to encode 2-functoriality of categorical pullback squares.
-/

namespace CategoryTheory.Limits

universe vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ vâ‚… vâ‚† vâ‚‡ vâ‚ˆ vâ‚‰ vâ‚â‚€ vâ‚â‚ vâ‚â‚‚ vâ‚â‚ƒ vâ‚â‚„ vâ‚â‚…
universe uâ‚ uâ‚‚ uâ‚ƒ uâ‚„ uâ‚… uâ‚† uâ‚‡ uâ‚ˆ uâ‚‰ uâ‚â‚€ uâ‚â‚ uâ‚â‚‚ uâ‚â‚ƒ uâ‚â‚„ uâ‚â‚…

/-- A `CatCospanTransform F G F' G'` is a diagram
```
    F   G
  A â¥¤ B â¥¢ C
Hâ‚|   |Hâ‚‚ |Hâ‚ƒ
  v   v   v
  A'â¥¤ B'â¥¢ C'
    F'  G'
```
with specified `CatCommSq`s expressing 2-commutativity of the squares. -/
structure CatCospanTransform
    {A : Type uâ‚} {B : Type uâ‚‚} {C : Type uâ‚ƒ}
    [Category.{vâ‚} A] [Category.{vâ‚‚} B] [Category.{vâ‚ƒ} C]
    (F : A â¥¤ B) (G : C â¥¤ B)
    {A' : Type uâ‚„} {B' : Type uâ‚…} {C' : Type uâ‚†}
    [Category.{vâ‚„} A'] [Category.{vâ‚…} B'] [Category.{vâ‚†} C']
    (F' : A' â¥¤ B') (G' : C' â¥¤ B') where
  /-- the functor on the left component -/
  left : A â¥¤ A'
  /-- the functor on the base component -/
  base : B â¥¤ B'
  /-- the functor on the right component -/
  right : C â¥¤ C'
  /-- a `CatCommSq` bundling the natural isomorphism `F â‹™ base â‰… left â‹™ F'`. -/
  squareLeft : CatCommSq F left base F' := by infer_instance
  /-- a `CatCommSq` bundling the natural isomorphism `G â‹™ base â‰… right â‹™ G'`. -/
  squareRight : CatCommSq G right base G' := by infer_instance

namespace CatCospanTransform

section

variable {A : Type uâ‚} {B : Type uâ‚‚} {C : Type uâ‚ƒ}
  [Category.{vâ‚} A] [Category.{vâ‚‚} B] [Category.{vâ‚ƒ} C]
  (F : A â¥¤ B) (G : C â¥¤ B)

attribute [local instance] CatCommSq.vId in
/-- The identity `CatCospanTransform` -/
@[simps]
def id : CatCospanTransform F G F G where
  left := ğŸ­ A
  base := ğŸ­ B
  right := ğŸ­ C

variable {F G}
/-- Composition of `CatCospanTransforms` is defined "componentwise". -/
@[simps]
def comp
    {A' : Type uâ‚„} {B' : Type uâ‚…} {C' : Type uâ‚†}
    [Category.{vâ‚„} A'] [Category.{vâ‚…} B'] [Category.{vâ‚†} C']
    {F' : A' â¥¤ B'} {G' : C' â¥¤ B'}
    {A'' : Type uâ‚‡} {B'' : Type uâ‚ˆ} {C'' : Type uâ‚‰}
    [Category.{vâ‚‡} A''] [Category.{vâ‚ˆ} B''] [Category.{vâ‚‰} C'']
    {F'' : A'' â¥¤ B''} {G'' : C'' â¥¤ B''}
    (Ïˆ : CatCospanTransform F G F' G') (Ïˆ' : CatCospanTransform F' G' F'' G'') :
    CatCospanTransform F G F'' G'' where
  left := Ïˆ.left â‹™ Ïˆ'.left
  base := Ïˆ.base â‹™ Ïˆ'.base
  right := Ïˆ.right â‹™ Ïˆ'.right
  squareLeft := Ïˆ.squareLeft.vComp' Ïˆ'.squareLeft
  squareRight := Ïˆ.squareRight.vComp' Ïˆ'.squareRight

end

end CatCospanTransform

variable {A : Type uâ‚} {B : Type uâ‚‚} {C : Type uâ‚ƒ}
    {A' : Type uâ‚„} {B' : Type uâ‚…} {C' : Type uâ‚†}
    {A'' : Type uâ‚‡} {B'' : Type uâ‚ˆ} {C'' : Type uâ‚‰}
    [Category.{vâ‚} A] [Category.{vâ‚‚} B] [Category.{vâ‚ƒ} C]
    {F : A â¥¤ B} {G : C â¥¤ B}
    [Category.{vâ‚„} A'] [Category.{vâ‚…} B'] [Category.{vâ‚†} C']
    {F' : A' â¥¤ B'} {G' : C' â¥¤ B'}
    [Category.{vâ‚‡} A''] [Category.{vâ‚ˆ} B''] [Category.{vâ‚‰} C'']
    {F'' : A'' â¥¤ B''} {G'' : C'' â¥¤ B''}

/-- A morphism of `CatCospanTransform F G F' G'` is a triple of natural
transformations between the component functors, subjects to
coherence conditions respective to the squares. -/
structure CatCospanTransformMorphism
    (Ïˆ Ïˆ' : CatCospanTransform F G F' G') where
  /-- the natural transformations between the left components -/
  left : Ïˆ.left âŸ¶ Ïˆ'.left
  /-- the natural transformations between the right components -/
  right : Ïˆ.right âŸ¶ Ïˆ'.right
  /-- the natural transformations between the base components -/
  base : Ïˆ.base âŸ¶ Ïˆ'.base
  /-- the coherence condition for the left square -/
  left_coherence :
      Ïˆ.squareLeft.iso.hom â‰« Functor.whiskerRight left F' =
      Functor.whiskerLeft F base â‰« Ïˆ'.squareLeft.iso.hom := by
    cat_disch
  /-- the coherence condition for the right square -/
  right_coherence :
      Ïˆ.squareRight.iso.hom â‰« Functor.whiskerRight right G' =
      Functor.whiskerLeft G base â‰« Ïˆ'.squareRight.iso.hom := by
    cat_disch

namespace CatCospanTransform

attribute [reassoc (attr := simp)]
  CatCospanTransformMorphism.left_coherence
  CatCospanTransformMorphism.right_coherence

@[simps]
instance category : Category (CatCospanTransform F G F' G') where
  Hom Ïˆ Ïˆ' := CatCospanTransformMorphism Ïˆ Ïˆ'
  id Ïˆ :=
    { left := ğŸ™ _
      right := ğŸ™ _
      base := ğŸ™ _ }
  comp Î± Î² :=
    { left := Î±.left â‰« Î².left
      right := Î±.right â‰« Î².right
      base := Î±.base â‰« Î².base}

attribute [local ext] CatCospanTransformMorphism in
@[ext]
lemma hom_ext {Ïˆ Ïˆ' : CatCospanTransform F G F' G'} {Î¸ Î¸' : Ïˆ âŸ¶ Ïˆ'}
    (hl : Î¸.left = Î¸'.left) (hr : Î¸.right = Î¸'.right) (hb : Î¸.base = Î¸'.base) :
    Î¸ = Î¸' := by
  apply CatCospanTransformMorphism.ext <;> assumption

end CatCospanTransform

namespace CatCospanTransformMorphism

@[reassoc (attr := simp)]
lemma left_coherence_app {Ïˆ Ïˆ' : CatCospanTransform F G F' G'}
    (Î± : Ïˆ âŸ¶ Ïˆ') (x : A) :
    Ïˆ.squareLeft.iso.hom.app x â‰« F'.map (Î±.left.app x) =
    Î±.base.app (F.obj x) â‰« Ïˆ'.squareLeft.iso.hom.app x :=
  congr_app Î±.left_coherence x

@[reassoc (attr := simp)]
lemma right_coherence_app {Ïˆ Ïˆ' : CatCospanTransform F G F' G'}
    (Î± : Ïˆ âŸ¶ Ïˆ') (x : C) :
    Ïˆ.squareRight.iso.hom.app x â‰« G'.map (Î±.right.app x) =
    Î±.base.app (G.obj x) â‰« Ïˆ'.squareRight.iso.hom.app x :=
  congr_app Î±.right_coherence x

/-- Whiskering left of a `CatCospanTransformMorphism` by a `CatCospanTransform`. -/
@[simps]
def whiskerLeft (Ï† : CatCospanTransform F G F' G')
    {Ïˆ Ïˆ' : CatCospanTransform F' G' F'' G''} (Î± : Ïˆ âŸ¶ Ïˆ') :
    (Ï†.comp Ïˆ) âŸ¶ (Ï†.comp Ïˆ') where
  left := Functor.whiskerLeft Ï†.left Î±.left
  right := Functor.whiskerLeft Ï†.right Î±.right
  base := Functor.whiskerLeft Ï†.base Î±.base

/-- Whiskering right of a `CatCospanTransformMorphism` by a `CatCospanTransform`. -/
@[simps]
def whiskerRight {Ïˆ Ïˆ' : CatCospanTransform F G F' G'} (Î± : Ïˆ âŸ¶ Ïˆ')
    (Ï† : CatCospanTransform F' G' F'' G'') :
    (Ïˆ.comp Ï†) âŸ¶ (Ïˆ'.comp Ï†) where
  left := Functor.whiskerRight Î±.left Ï†.left
  right := Functor.whiskerRight Î±.right Ï†.right
  base := Functor.whiskerRight Î±.base Ï†.base
  left_coherence := by
    ext x
    dsimp
    simp only [CatCommSq.vComp_iso_hom_app, Category.assoc]
    rw [â† Functor.map_comp_assoc, â† left_coherence_app, Functor.map_comp_assoc]
    simp
  right_coherence := by
    ext x
    dsimp
    simp only [CatCommSq.vComp_iso_hom_app, Category.assoc]
    rw [â† Functor.map_comp_assoc, â† right_coherence_app, Functor.map_comp_assoc]
    simp

end CatCospanTransformMorphism

namespace CatCospanTransform

/-- A constructor for isomorphisms of `CatCospanTransform`'s. -/
@[simps]
def mkIso {Ïˆ Ïˆ' : CatCospanTransform F G F' G'}
    (left : Ïˆ.left â‰… Ïˆ'.left) (right : Ïˆ.right â‰… Ïˆ'.right)
    (base : Ïˆ.base â‰… Ïˆ'.base)
    (left_coherence :
        Ïˆ.squareLeft.iso.hom â‰« Functor.whiskerRight left.hom F' =
        Functor.whiskerLeft F base.hom â‰« Ïˆ'.squareLeft.iso.hom := by
      cat_disch)
    (right_coherence :
        Ïˆ.squareRight.iso.hom â‰« Functor.whiskerRight right.hom G' =
        Functor.whiskerLeft G base.hom â‰« Ïˆ'.squareRight.iso.hom := by
      cat_disch) :
    Ïˆ â‰… Ïˆ' where
  hom :=
    { left := left.hom
      right := right.hom
      base := base.hom }
  inv :=
    { left := left.inv
      right := right.inv
      base := base.inv
      left_coherence := by
        simpa using Ïˆ'.squareLeft.iso.hom â‰«=
          IsIso.inv_eq_inv.mpr left_coherence =â‰«
          Ïˆ.squareLeft.iso.hom
      right_coherence := by
        simpa using Ïˆ'.squareRight.iso.hom â‰«=
          IsIso.inv_eq_inv.mpr right_coherence =â‰«
          Ïˆ.squareRight.iso.hom }

section Iso

variable {Ïˆ Ïˆ' : CatCospanTransform F G F' G'}
  (f : Ïˆ' âŸ¶ Ïˆ') [IsIso f] (e : Ïˆ â‰… Ïˆ')

instance isIso_left : IsIso f.left :=
  âŸ¨(inv f).left, by simp [â† CatCospanTransform.category_comp_left]âŸ©

instance isIso_right : IsIso f.right :=
  âŸ¨(inv f).right, by simp [â† CatCospanTransform.category_comp_right]âŸ©

instance isIso_base : IsIso f.base :=
  âŸ¨(inv f).base, by simp [â† CatCospanTransform.category_comp_base]âŸ©

@[simp]
lemma inv_left : inv f.left = (inv f).left := by
  symm
  apply IsIso.eq_inv_of_inv_hom_id
  simp [â† CatCospanTransform.category_comp_left]

@[simp]
lemma inv_right : inv f.right = (inv f).right := by
  symm
  apply IsIso.eq_inv_of_inv_hom_id
  simp [â† CatCospanTransform.category_comp_right]

@[simp]
lemma inv_base : inv f.base = (inv f).base := by
  symm
  apply IsIso.eq_inv_of_inv_hom_id
  simp [â† CatCospanTransform.category_comp_base]

/-- Extract an isomorphism between left components from an isomorphism in
`CatCospanTransform F G F' G'`. -/
@[simps]
def leftIso : Ïˆ.left â‰… Ïˆ'.left where
  hom := e.hom.left
  inv := e.inv.left
  hom_inv_id := by simp [â† category_comp_left]
  inv_hom_id := by simp [â† category_comp_left]

/-- Extract an isomorphism between right components from an isomorphism in
`CatCospanTransform F G F' G'`. -/
@[simps]
def rightIso : Ïˆ.right â‰… Ïˆ'.right where
  hom := e.hom.right
  inv := e.inv.right
  hom_inv_id := by simp [â† category_comp_right]
  inv_hom_id := by simp [â† category_comp_right]

/-- Extract an isomorphism between base components from an isomorphism in
`CatCospanTransform F G F' G'`. -/
@[simps]
def baseIso : Ïˆ.base â‰… Ïˆ'.base where
  hom := e.hom.base
  inv := e.inv.base
  hom_inv_id := by simp [â† category_comp_base]
  inv_hom_id := by simp [â† category_comp_base]

omit [IsIso f] in
lemma isIso_iff : IsIso f â†” IsIso f.left âˆ§ IsIso f.base âˆ§ IsIso f.right where
  mp h := âŸ¨inferInstance, inferInstance, inferInstanceâŸ©
  mpr h := by
    obtain âŸ¨_, _, _âŸ© := h
    use mkIso (asIso f.left) (asIso f.right) (asIso f.base)
      f.left_coherence f.right_coherence|>.inv
    aesop_cat

end Iso

/-- The left unitor isomorphism for categorical cospan transformations. -/
@[simps!]
def leftUnitor (Ï† : CatCospanTransform F G F' G') :
    (CatCospanTransform.id F G).comp Ï† â‰… Ï† :=
  mkIso Ï†.left.leftUnitor Ï†.right.leftUnitor Ï†.base.leftUnitor

/-- The right unitor isomorphism for categorical cospan transformations. -/
@[simps!]
def rightUnitor (Ï† : CatCospanTransform F G F' G') :
    Ï†.comp (.id F' G') â‰… Ï† :=
  mkIso Ï†.left.rightUnitor Ï†.right.rightUnitor Ï†.base.rightUnitor

/-- The associator isomorphism for categorical cospan transformations. -/
@[simps!]
def associator {A''' : Type uâ‚â‚€} {B''' : Type uâ‚â‚} {C''' : Type uâ‚â‚‚}
    [Category.{vâ‚â‚€} A'''] [Category.{vâ‚â‚} B'''] [Category.{vâ‚â‚‚} C''']
    {F''' : A''' â¥¤ B'''} {G''' : C''' â¥¤ B'''}
    (Ï† : CatCospanTransform F G F' G') (Ï†' : CatCospanTransform F' G' F'' G'')
    (Ï†'' : CatCospanTransform F'' G'' F''' G''') :
    (Ï†.comp Ï†').comp Ï†'' â‰… Ï†.comp (Ï†'.comp Ï†'') :=
  mkIso
    (Ï†.left.associator Ï†'.left Ï†''.left)
    (Ï†.right.associator Ï†'.right Ï†''.right)
    (Ï†.base.associator Ï†'.base Ï†''.base)

section lemmas

-- We scope the notations with notations from bicategories to make life easier.
-- Due to performance issues, these notations should not be in scope at the same time
-- as the ones in bicategories.

@[inherit_doc] scoped infixr:81 " â— " => CatCospanTransformMorphism.whiskerLeft
@[inherit_doc] scoped infixl:81 " â–· " => CatCospanTransformMorphism.whiskerRight
@[inherit_doc] scoped notation "Î±_" => CatCospanTransform.associator
@[inherit_doc] scoped notation "Î»_" => CatCospanTransform.leftUnitor
@[inherit_doc] scoped notation "Ï_" => CatCospanTransform.rightUnitor

variable
    {A''' : Type uâ‚â‚€} {B''' : Type uâ‚â‚} {C''' : Type uâ‚â‚‚}
    [Category.{vâ‚â‚€} A'''] [Category.{vâ‚â‚} B'''] [Category.{vâ‚â‚‚} C''']
    {F''' : A''' â¥¤ B'''} {G''' : C''' â¥¤ B'''}
    {Ïˆ Ïˆ' Ïˆ'' : CatCospanTransform F G F' G'}
    (Î· : Ïˆ âŸ¶ Ïˆ') (Î·' : Ïˆ' âŸ¶ Ïˆ'')
    {Ï† Ï†' Ï†'' : CatCospanTransform F' G' F'' G''}
    (Î¸ : Ï† âŸ¶ Ï†') (Î¸' : Ï†' âŸ¶ Ï†'')
    {Ï„ Ï„' : CatCospanTransform F'' G'' F''' G'''}
    (Î³ : Ï„ âŸ¶ Ï„')

@[reassoc]
lemma whisker_exchange : Ïˆ â— Î¸ â‰« Î· â–· Ï†' = Î· â–· Ï† â‰« Ïˆ' â— Î¸ := by cat_disch

@[simp]
lemma id_whiskerRight : ğŸ™ Ïˆ â–· Ï† = ğŸ™ _ := by cat_disch

@[reassoc]
lemma whiskerRight_id : Î· â–· (.id _ _) = (Ï_ _).hom â‰« Î· â‰« (Ï_ _).inv := by cat_disch

@[simp, reassoc]
lemma comp_whiskerRight : (Î· â‰« Î·') â–· Ï† = Î· â–· Ï† â‰« Î·' â–· Ï† := by cat_disch

@[reassoc]
lemma whiskerRight_comp :
    Î· â–· (Ï†.comp Ï„) = (Î±_ _ _ _).inv â‰« (Î· â–· Ï†) â–· Ï„ â‰« (Î±_ _ _ _).hom := by
  cat_disch

@[simp]
lemma whiskerleft_id : Ïˆ â— ğŸ™ Ï† = ğŸ™ _ := by cat_disch

@[reassoc]
lemma id_whiskerLeft : (.id _ _) â— Î· = (Î»_ _).hom â‰« Î· â‰« (Î»_ _).inv := by cat_disch

@[simp, reassoc]
lemma whiskerLeft_comp : Ïˆ â— (Î¸ â‰« Î¸') = (Ïˆ â— Î¸) â‰« (Ïˆ â— Î¸') := by cat_disch

@[reassoc]
lemma comp_whiskerLeft :
    (Ïˆ.comp Ï†) â— Î³ = (Î±_ _ _ _).hom â‰« (Ïˆ â— (Ï† â— Î³)) â‰« (Î±_ _ _ _).inv := by
  cat_disch

@[reassoc]
lemma pentagon
    {A'''' : Type uâ‚â‚ƒ} {B'''' : Type uâ‚â‚„} {C'''' : Type uâ‚â‚…}
    [Category.{vâ‚â‚ƒ} A''''] [Category.{vâ‚â‚„} B''''] [Category.{vâ‚â‚…} C'''']
    {F'''' : A'''' â¥¤ B''''} {G'''' : C'''' â¥¤ B''''}
    {Ïƒ : CatCospanTransform F''' G''' F'''' G''''} :
    (Î±_ Ïˆ Ï† Ï„).hom â–· Ïƒ â‰« (Î±_ Ïˆ (Ï†.comp Ï„) Ïƒ).hom â‰« Ïˆ â— (Î±_ Ï† Ï„ Ïƒ).hom =
      (Î±_ (Ïˆ.comp Ï†) Ï„ Ïƒ).hom â‰« (Î±_ Ïˆ Ï† (Ï„.comp Ïƒ)).hom := by
  cat_disch

@[reassoc]
lemma triangle :
    (Î±_ Ïˆ (.id _ _) Ï†).hom â‰« Ïˆ â— (Î»_ Ï†).hom = (Ï_ Ïˆ).hom â–· Ï† := by
  cat_disch

@[reassoc]
lemma triangle_inv :
     (Î±_ Ïˆ (.id _ _) Ï†).inv â‰« (Ï_ Ïˆ).hom â–· Ï† = Ïˆ â— (Î»_ Ï†).hom := by
  cat_disch

section Isos

variable {Ïˆ Ïˆ' : CatCospanTransform F G F' G'} (Î· : Ïˆ âŸ¶ Ïˆ') [IsIso Î·]
    {Ï† Ï†' : CatCospanTransform F' G' F'' G''} (Î¸ : Ï† âŸ¶ Ï†') [IsIso Î¸]

instance : IsIso (Ïˆ â— Î¸) :=
    âŸ¨Ïˆ â— inv Î¸, âŸ¨by simp [â† whiskerLeft_comp], by simp [â† whiskerLeft_comp]âŸ©âŸ©

lemma inv_whiskerLeft : inv (Ïˆ â— Î¸) = Ïˆ â— inv Î¸ := by
  apply IsIso.inv_eq_of_hom_inv_id
  simp [â† whiskerLeft_comp]

instance : IsIso (Î· â–· Ï†) :=
    âŸ¨inv Î· â–· Ï†, âŸ¨by simp [â† comp_whiskerRight], by simp [â† comp_whiskerRight]âŸ©âŸ©

lemma inv_whiskerRight : inv (Î· â–· Ï†) = inv Î· â–· Ï† := by
  apply IsIso.inv_eq_of_hom_inv_id
  simp [â† comp_whiskerRight]

end Isos

end lemmas

end CatCospanTransform

end CategoryTheory.Limits
