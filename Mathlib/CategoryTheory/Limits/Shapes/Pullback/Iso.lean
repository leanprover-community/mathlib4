/-
Copyright (c) 2018 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/

import Mathlib.CategoryTheory.Limits.Shapes.Pullback.HasPullback

/-!
# The pullback of an isomorphism

This file provides some basic results about the pullback (and pushout) of an isomorphism.

-/

noncomputable section

open CategoryTheory

universe w v‚ÇÅ v‚ÇÇ v u u‚ÇÇ

namespace CategoryTheory.Limits

variable {C : Type u} [Category.{v} C] {X Y Z : C}

section PullbackLeftIso

open WalkingCospan

variable (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [IsIso f]

/-- If `f : X ‚ü∂ Z` is iso, then `X √ó[Z] Y ‚âÖ Y`. This is the explicit limit cone. -/
def pullbackConeOfLeftIso : PullbackCone f g :=
  PullbackCone.mk (g ‚â´ inv f) (ùüô _) <| by simp

@[simp]
theorem pullbackConeOfLeftIso_x : (pullbackConeOfLeftIso f g).pt = Y := rfl

@[simp]
theorem pullbackConeOfLeftIso_fst : (pullbackConeOfLeftIso f g).fst = g ‚â´ inv f := rfl

@[simp]
theorem pullbackConeOfLeftIso_snd : (pullbackConeOfLeftIso f g).snd = ùüô _ := rfl

theorem pullbackConeOfLeftIso_œÄ_app_none : (pullbackConeOfLeftIso f g).œÄ.app none = g := by simp

@[simp]
theorem pullbackConeOfLeftIso_œÄ_app_left : (pullbackConeOfLeftIso f g).œÄ.app left = g ‚â´ inv f :=
  rfl

@[simp]
theorem pullbackConeOfLeftIso_œÄ_app_right : (pullbackConeOfLeftIso f g).œÄ.app right = ùüô _ := rfl

/-- Verify that the constructed limit cone is indeed a limit. -/
def pullbackConeOfLeftIsoIsLimit : IsLimit (pullbackConeOfLeftIso f g) :=
  PullbackCone.isLimitAux' _ fun s ‚Ü¶ ‚ü®s.snd, by simp [‚Üê s.condition_assoc]‚ü©

theorem hasPullback_of_left_iso : HasPullback f g :=
  ‚ü®‚ü®‚ü®_, pullbackConeOfLeftIsoIsLimit f g‚ü©‚ü©‚ü©

attribute [local instance] hasPullback_of_left_iso

instance pullback_snd_iso_of_left_iso : IsIso (pullback.snd f g) := by
  refine ‚ü®‚ü®pullback.lift (g ‚â´ inv f) (ùüô _) (by simp), ?_, by simp‚ü©‚ü©
  ext
  ¬∑ simp [‚Üê pullback.condition_assoc]
  ¬∑ simp

@[reassoc (attr := simp)]
lemma pullback_inv_snd_fst_of_left_isIso :
    inv (pullback.snd f g) ‚â´ pullback.fst f g = g ‚â´ inv f := by
  rw [IsIso.inv_comp_eq, ‚Üê pullback.condition_assoc, IsIso.hom_inv_id, Category.comp_id]

end PullbackLeftIso

section PullbackRightIso

open WalkingCospan

variable (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [IsIso g]

/-- If `g : Y ‚ü∂ Z` is iso, then `X √ó[Z] Y ‚âÖ X`. This is the explicit limit cone. -/
def pullbackConeOfRightIso : PullbackCone f g :=
  PullbackCone.mk (ùüô _) (f ‚â´ inv g) <| by simp

@[simp]
theorem pullbackConeOfRightIso_x : (pullbackConeOfRightIso f g).pt = X := rfl

@[simp]
theorem pullbackConeOfRightIso_fst : (pullbackConeOfRightIso f g).fst = ùüô _ := rfl

@[simp]
theorem pullbackConeOfRightIso_snd : (pullbackConeOfRightIso f g).snd = f ‚â´ inv g := rfl

theorem pullbackConeOfRightIso_œÄ_app_none : (pullbackConeOfRightIso f g).œÄ.app none = f := by simp

@[simp]
theorem pullbackConeOfRightIso_œÄ_app_left : (pullbackConeOfRightIso f g).œÄ.app left = ùüô _ :=
  rfl

@[simp]
theorem pullbackConeOfRightIso_œÄ_app_right : (pullbackConeOfRightIso f g).œÄ.app right = f ‚â´ inv g :=
  rfl

/-- Verify that the constructed limit cone is indeed a limit. -/
def pullbackConeOfRightIsoIsLimit : IsLimit (pullbackConeOfRightIso f g) :=
  PullbackCone.isLimitAux' _ fun s ‚Ü¶ ‚ü®s.fst, by simp [s.condition_assoc]‚ü©

theorem hasPullback_of_right_iso : HasPullback f g :=
  ‚ü®‚ü®‚ü®_, pullbackConeOfRightIsoIsLimit f g‚ü©‚ü©‚ü©

attribute [local instance] hasPullback_of_right_iso

instance pullback_fst_iso_of_right_iso : IsIso (pullback.fst f g) := by
  refine ‚ü®‚ü®pullback.lift (ùüô _) (f ‚â´ inv g) (by simp), ?_, by simp‚ü©‚ü©
  ext
  ¬∑ simp
  ¬∑ simp [pullback.condition_assoc]

@[reassoc (attr := simp)]
lemma pullback_inv_fst_snd_of_right_isIso :
    inv (pullback.fst f g) ‚â´ pullback.snd f g = f ‚â´ inv g := by
  rw [IsIso.inv_comp_eq, pullback.condition_assoc, IsIso.hom_inv_id, Category.comp_id]

end PullbackRightIso

section PushoutLeftIso

open WalkingSpan

variable (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) [IsIso f]

/-- If `f : X ‚ü∂ Y` is iso, then `Y ‚®ø[X] Z ‚âÖ Z`. This is the explicit colimit cocone. -/
def pushoutCoconeOfLeftIso : PushoutCocone f g :=
  PushoutCocone.mk (inv f ‚â´ g) (ùüô _) <| by simp

@[simp]
theorem pushoutCoconeOfLeftIso_x : (pushoutCoconeOfLeftIso f g).pt = Z := rfl

@[simp]
theorem pushoutCoconeOfLeftIso_inl : (pushoutCoconeOfLeftIso f g).inl = inv f ‚â´ g := rfl

@[simp]
theorem pushoutCoconeOfLeftIso_inr : (pushoutCoconeOfLeftIso f g).inr = ùüô _ := rfl

theorem pushoutCoconeOfLeftIso_Œπ_app_none : (pushoutCoconeOfLeftIso f g).Œπ.app none = g := by
  simp

@[simp]
theorem pushoutCoconeOfLeftIso_Œπ_app_left : (pushoutCoconeOfLeftIso f g).Œπ.app left = inv f ‚â´ g :=
  rfl

@[simp]
theorem pushoutCoconeOfLeftIso_Œπ_app_right : (pushoutCoconeOfLeftIso f g).Œπ.app right = ùüô _ := rfl

/-- Verify that the constructed cocone is indeed a colimit. -/
def pushoutCoconeOfLeftIsoIsLimit : IsColimit (pushoutCoconeOfLeftIso f g) :=
  PushoutCocone.isColimitAux' _ fun s ‚Ü¶ ‚ü®s.inr, by simp [‚Üê s.condition]‚ü©

theorem hasPushout_of_left_iso : HasPushout f g :=
  ‚ü®‚ü®‚ü®_, pushoutCoconeOfLeftIsoIsLimit f g‚ü©‚ü©‚ü©

attribute [local instance] hasPushout_of_left_iso

instance pushout_inr_iso_of_left_iso : IsIso (pushout.inr f g) := by
  refine ‚ü®‚ü®pushout.desc (inv f ‚â´ g) (ùüô _) (by simp), by simp, ?_‚ü©‚ü©
  ext
  ¬∑ simp [‚Üê pushout.condition]
  ¬∑ simp

@[reassoc (attr := simp)]
lemma pushout_inl_inv_inr_of_right_isIso :
    pushout.inl f g ‚â´ inv (pushout.inr f g) = inv f ‚â´ g := by
  rw [IsIso.eq_inv_comp, pushout.condition_assoc, IsIso.hom_inv_id, Category.comp_id]

end PushoutLeftIso

section PushoutRightIso

open WalkingSpan

variable (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) [IsIso g]

/-- If `f : X ‚ü∂ Z` is iso, then `Y ‚®ø[X] Z ‚âÖ Y`. This is the explicit colimit cocone. -/
def pushoutCoconeOfRightIso : PushoutCocone f g :=
  PushoutCocone.mk (ùüô _) (inv g ‚â´ f) <| by simp

@[simp]
theorem pushoutCoconeOfRightIso_x : (pushoutCoconeOfRightIso f g).pt = Y := rfl

@[simp]
theorem pushoutCoconeOfRightIso_inl : (pushoutCoconeOfRightIso f g).inl = ùüô _ := rfl

@[simp]
theorem pushoutCoconeOfRightIso_inr : (pushoutCoconeOfRightIso f g).inr = inv g ‚â´ f := rfl

theorem pushoutCoconeOfRightIso_Œπ_app_none : (pushoutCoconeOfRightIso f g).Œπ.app none = f := by
  simp

@[simp]
theorem pushoutCoconeOfRightIso_Œπ_app_left : (pushoutCoconeOfRightIso f g).Œπ.app left = ùüô _ := rfl

@[simp]
theorem pushoutCoconeOfRightIso_Œπ_app_right :
    (pushoutCoconeOfRightIso f g).Œπ.app right = inv g ‚â´ f := rfl

/-- Verify that the constructed cocone is indeed a colimit. -/
def pushoutCoconeOfRightIsoIsLimit : IsColimit (pushoutCoconeOfRightIso f g) :=
  PushoutCocone.isColimitAux' _ fun s ‚Ü¶ ‚ü®s.inl, by simp [‚Üê s.condition]‚ü©

theorem hasPushout_of_right_iso : HasPushout f g :=
  ‚ü®‚ü®‚ü®_, pushoutCoconeOfRightIsoIsLimit f g‚ü©‚ü©‚ü©

attribute [local instance] hasPushout_of_right_iso

instance pushout_inl_iso_of_right_iso : IsIso (pushout.inl _ _ : _ ‚ü∂ pushout f g) := by
  refine ‚ü®‚ü®pushout.desc (ùüô _) (inv g ‚â´ f) (by simp), by simp, ?_‚ü©‚ü©
  ext
  ¬∑ simp
  ¬∑ simp [pushout.condition]

@[reassoc (attr := simp)]
lemma pushout_inr_inv_inl_of_right_isIso :
    pushout.inr f g ‚â´ inv (pushout.inl f g) = inv g ‚â´ f := by
  rw [IsIso.eq_inv_comp, ‚Üê pushout.condition_assoc, IsIso.hom_inv_id, Category.comp_id]

end PushoutRightIso


end CategoryTheory.Limits
