/-
Copyright (c) 2018 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Andrew Yang
-/

import Mathlib.CategoryTheory.Limits.Shapes.Pullback.Iso

/-!
# Pullbacks and monomorphisms

This file provides some results about interactions between pullbacks and monomorphisms, as well as
the dual statements between pushouts and epimorphisms.

## Main results
* Monomorphisms are stable under pullback. This is available using the `PullbackCone` API as
`mono_fst_of_is_pullback_of_mono` and `mono_snd_of_is_pullback_of_mono`, and using the `pullback`
API as `pullback.fst_of_mono` and `pullback.snd_of_mono`.

* A pullback cone is a limit iff its composition with a monomorphism is a limit. This is available
  as `IsLimitOfCompMono` and `pullbackIsPullbackOfCompMono` respectively.

* Monomorphisms admit kernel pairs, this is `has_kernel_pair_of_mono`.

The dual notions for pushouts are also available.
-/

noncomputable section

open CategoryTheory

universe w vâ‚ vâ‚‚ v u uâ‚‚

namespace CategoryTheory.Limits

open WalkingSpan.Hom WalkingCospan.Hom WidePullbackShape.Hom WidePushoutShape.Hom PullbackCone

variable {C : Type u} [Category.{v} C] {W X Y Z : C}

section Monomorphisms

namespace PullbackCone

variable {f : X âŸ¶ Z} {g : Y âŸ¶ Z}

/-- Monomorphisms are stable under pullback in the first argument. -/
theorem mono_snd_of_is_pullback_of_mono {t : PullbackCone f g} (ht : IsLimit t) [Mono f] :
    Mono t.snd := by
  refine âŸ¨fun {W} h k i => IsLimit.hom_ext ht ?_ iâŸ©
  rw [â† cancel_mono f, Category.assoc, Category.assoc, condition]
  apply reassoc_of% i

/-- Monomorphisms are stable under pullback in the second argument. -/
theorem mono_fst_of_is_pullback_of_mono {t : PullbackCone f g} (ht : IsLimit t) [Mono g] :
    Mono t.fst := by
  refine âŸ¨fun {W} h k i => IsLimit.hom_ext ht i ?_âŸ©
  rw [â† cancel_mono g, Category.assoc, Category.assoc, â† condition]
  apply reassoc_of% i

/--
The pullback cone `(ğŸ™ X, ğŸ™ X)` for the pair `(f, f)` is a limit if `f` is a mono. The converse is
shown in `mono_of_pullback_is_id`.
-/
def isLimitMkIdId (f : X âŸ¶ Y) [Mono f] : IsLimit (mk (ğŸ™ X) (ğŸ™ X) rfl : PullbackCone f f) :=
  IsLimit.mk _ (fun s => s.fst) (fun _ => Category.comp_id _)
    (fun s => by rw [â† cancel_mono f, Category.comp_id, s.condition]) fun s m mâ‚ _ => by
    simpa using mâ‚

/--
`f` is a mono if the pullback cone `(ğŸ™ X, ğŸ™ X)` is a limit for the pair `(f, f)`. The converse is
given in `PullbackCone.is_id_of_mono`.
-/
theorem mono_of_isLimitMkIdId (f : X âŸ¶ Y) (t : IsLimit (mk (ğŸ™ X) (ğŸ™ X) rfl : PullbackCone f f)) :
    Mono f :=
  âŸ¨fun {Z} g h eq => by
    rcases PullbackCone.IsLimit.lift' t _ _ eq with âŸ¨_, rfl, rflâŸ©
    rflâŸ©

/-- Suppose `f` and `g` are two morphisms with a common codomain and `s` is a limit cone over the
    diagram formed by `f` and `g`. Suppose `f` and `g` both factor through a monomorphism `h` via
    `x` and `y`, respectively.  Then `s` is also a limit cone over the diagram formed by `x` and
    `y`. -/
def isLimitOfFactors (f : X âŸ¶ Z) (g : Y âŸ¶ Z) (h : W âŸ¶ Z) [Mono h] (x : X âŸ¶ W) (y : Y âŸ¶ W)
    (hxh : x â‰« h = f) (hyh : y â‰« h = g) (s : PullbackCone f g) (hs : IsLimit s) :
    IsLimit
      (PullbackCone.mk _ _
        (show s.fst â‰« x = s.snd â‰« y from
          (cancel_mono h).1 <| by simp only [Category.assoc, hxh, hyh, s.condition])) :=
  PullbackCone.isLimitAux' _ fun t =>
    have : fst t â‰« x â‰« h = snd t â‰« y â‰« h := by  -- Porting note: reassoc workaround
      rw [â† Category.assoc, â† Category.assoc]
      apply congrArg (Â· â‰« h) t.condition
    âŸ¨hs.lift (PullbackCone.mk t.fst t.snd <| by rw [â† hxh, â† hyh, this]),
      âŸ¨hs.fac _ WalkingCospan.left, hs.fac _ WalkingCospan.right, fun hr hr' => by
        apply PullbackCone.IsLimit.hom_ext hs <;>
              simp only [PullbackCone.mk_fst, PullbackCone.mk_snd] at hr hr' âŠ¢ <;>
            simp only [hr, hr'] <;>
          symm
        exacts [hs.fac _ WalkingCospan.left, hs.fac _ WalkingCospan.right]âŸ©âŸ©

/-- If `W` is the pullback of `f, g`, it is also the pullback of `f â‰« i, g â‰« i` for any mono `i`. -/
def isLimitOfCompMono (f : X âŸ¶ W) (g : Y âŸ¶ W) (i : W âŸ¶ Z) [Mono i] (s : PullbackCone f g)
    (H : IsLimit s) :
    IsLimit
      (PullbackCone.mk _ _
        (show s.fst â‰« f â‰« i = s.snd â‰« g â‰« i by
          rw [â† Category.assoc, â† Category.assoc, s.condition])) := by
  apply PullbackCone.isLimitAux'
  intro s
  rcases PullbackCone.IsLimit.lift' H s.fst s.snd
      ((cancel_mono i).mp (by simpa using s.condition)) with
    âŸ¨l, hâ‚, hâ‚‚âŸ©
  refine âŸ¨l, hâ‚, hâ‚‚, ?_âŸ©
  intro m hmâ‚ hmâ‚‚
  exact (PullbackCone.IsLimit.hom_ext H (hmâ‚.trans hâ‚.symm) (hmâ‚‚.trans hâ‚‚.symm) :)

end PullbackCone

end Monomorphisms

/-- The pullback of a monomorphism is a monomorphism -/
instance pullback.fst_of_mono {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [HasPullback f g] [Mono g] :
    Mono (pullback.fst f g) :=
  PullbackCone.mono_fst_of_is_pullback_of_mono (limit.isLimit _)

/-- The pullback of a monomorphism is a monomorphism -/
instance pullback.snd_of_mono {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [HasPullback f g] [Mono f] :
    Mono (pullback.snd f g) :=
  PullbackCone.mono_snd_of_is_pullback_of_mono (limit.isLimit _)

/-- The map `X Ã—[Z] Y âŸ¶ X Ã— Y` is mono. -/
instance mono_pullback_to_prod {C : Type*} [Category C] {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z)
    [HasPullback f g] [HasBinaryProduct X Y] :
    Mono (prod.lift (pullback.fst f g) (pullback.snd f g)) :=
  âŸ¨fun {W} iâ‚ iâ‚‚ h => by
    ext
    Â· simpa using congrArg (fun f => f â‰« prod.fst) h
    Â· simpa using congrArg (fun f => f â‰« prod.snd) hâŸ©

/-- The pullback of `f, g` is also the pullback of `f â‰« i, g â‰« i` for any mono `i`. -/
noncomputable def pullbackIsPullbackOfCompMono (f : X âŸ¶ W) (g : Y âŸ¶ W) (i : W âŸ¶ Z) [Mono i]
    [HasPullback f g] : IsLimit (PullbackCone.mk (pullback.fst f g) (pullback.snd f g)
      -- Porting note: following used to be _
      (show (pullback.fst f g) â‰« f â‰« i = (pullback.snd f g) â‰« g â‰« i from by
        simp only [â† Category.assoc]; rw [cancel_mono]; apply pullback.condition)) :=
  PullbackCone.isLimitOfCompMono f g i _ (limit.isLimit (cospan f g))

instance hasPullback_of_comp_mono (f : X âŸ¶ W) (g : Y âŸ¶ W) (i : W âŸ¶ Z) [Mono i] [HasPullback f g] :
    HasPullback (f â‰« i) (g â‰« i) :=
  âŸ¨âŸ¨âŸ¨_, pullbackIsPullbackOfCompMono f g iâŸ©âŸ©âŸ©

section

attribute [local instance] hasPullback_of_left_iso

variable (f : X âŸ¶ Z) (i : Z âŸ¶ W) [Mono i]

instance hasPullback_of_right_factors_mono : HasPullback i (f â‰« i) := by
  simpa only [Category.id_comp] using hasPullback_of_comp_mono (ğŸ™ Z) f i

instance pullback_snd_iso_of_right_factors_mono :
    IsIso (pullback.snd i (f â‰« i)) := by
  have := limit.isoLimitCone_hom_Ï€ âŸ¨_, pullbackIsPullbackOfCompMono (ğŸ™ _) f iâŸ© WalkingCospan.right
  convert (congrArg IsIso (show _ â‰« pullback.snd (ğŸ™ Z) f = _ from this)).mp inferInstance
  Â· exact (Category.id_comp _).symm
  Â· exact (Category.id_comp _).symm

attribute [local instance] hasPullback_of_right_iso

instance hasPullback_of_left_factors_mono : HasPullback (f â‰« i) i := by
  simpa only [Category.id_comp] using hasPullback_of_comp_mono f (ğŸ™ Z) i

instance pullback_snd_iso_of_left_factors_mono :
    IsIso (pullback.fst (f â‰« i) i) := by
  have := limit.isoLimitCone_hom_Ï€ âŸ¨_, pullbackIsPullbackOfCompMono f (ğŸ™ _) iâŸ© WalkingCospan.left
  convert (congrArg IsIso (show _ â‰« pullback.fst f (ğŸ™ Z) = _ from this)).mp inferInstance
  Â· exact (Category.id_comp _).symm
  Â· exact (Category.id_comp _).symm

end

section

open WalkingCospan

variable (f : X âŸ¶ Y) [Mono f]

instance has_kernel_pair_of_mono : HasPullback f f :=
  âŸ¨âŸ¨âŸ¨_, PullbackCone.isLimitMkIdId fâŸ©âŸ©âŸ©

theorem PullbackCone.fst_eq_snd_of_mono_eq {f : X âŸ¶ Y} [Mono f] (t : PullbackCone f f) :
    t.fst = t.snd :=
  (cancel_mono f).1 t.condition

theorem fst_eq_snd_of_mono_eq : pullback.fst f f = pullback.snd f f :=
  PullbackCone.fst_eq_snd_of_mono_eq (getLimitCone (cospan f f)).cone

@[simp]
theorem pullbackSymmetry_hom_of_mono_eq : (pullbackSymmetry f f).hom = ğŸ™ _ := by
  ext
  Â· simp [fst_eq_snd_of_mono_eq]
  Â· simp [fst_eq_snd_of_mono_eq]

variable {f} in
lemma PullbackCone.isIso_fst_of_mono_of_isLimit {t : PullbackCone f f} (ht : IsLimit t) :
    IsIso t.fst := by
  refine âŸ¨âŸ¨PullbackCone.IsLimit.lift ht (ğŸ™ _) (ğŸ™ _) (by simp), ?_, by simpâŸ©âŸ©
  apply PullbackCone.IsLimit.hom_ext ht
  Â· simp
  Â· simp [fst_eq_snd_of_mono_eq]

variable {f} in
lemma PullbackCone.isIso_snd_of_mono_of_isLimit {t : PullbackCone f f} (ht : IsLimit t) :
    IsIso t.snd :=
  t.fst_eq_snd_of_mono_eq â–¸ t.isIso_fst_of_mono_of_isLimit ht

instance isIso_fst_of_mono : IsIso (pullback.fst f f) :=
  PullbackCone.isIso_fst_of_mono_of_isLimit (getLimitCone (cospan f f)).isLimit

instance isIso_snd_of_mono : IsIso (pullback.snd f f) :=
  PullbackCone.isIso_snd_of_mono_of_isLimit (getLimitCone (cospan f f)).isLimit
end

namespace PushoutCocone

variable {f : X âŸ¶ Y} {g : X âŸ¶ Z}

theorem epi_inr_of_is_pushout_of_epi {t : PushoutCocone f g} (ht : IsColimit t) [Epi f] :
    Epi t.inr :=
  âŸ¨fun {W} h k i => IsColimit.hom_ext ht (by simp [â† cancel_epi f, t.condition_assoc, i]) iâŸ©

theorem epi_inl_of_is_pushout_of_epi {t : PushoutCocone f g} (ht : IsColimit t) [Epi g] :
    Epi t.inl :=
  âŸ¨fun {W} h k i => IsColimit.hom_ext ht i (by simp [â† cancel_epi g, â† t.condition_assoc, i])âŸ©

/--
The pushout cocone `(ğŸ™ X, ğŸ™ X)` for the pair `(f, f)` is a colimit if `f` is an epi. The converse is
shown in `epi_of_isColimit_mk_id_id`.
-/
def isColimitMkIdId (f : X âŸ¶ Y) [Epi f] : IsColimit (mk (ğŸ™ Y) (ğŸ™ Y) rfl : PushoutCocone f f) :=
  IsColimit.mk _ (fun s => s.inl) (fun _ => Category.id_comp _)
    (fun s => by rw [â† cancel_epi f, Category.id_comp, s.condition]) fun s m mâ‚ _ => by
    simpa using mâ‚

/-- `f` is an epi if the pushout cocone `(ğŸ™ X, ğŸ™ X)` is a colimit for the pair `(f, f)`.
The converse is given in `PushoutCocone.isColimitMkIdId`.
-/
theorem epi_of_isColimitMkIdId (f : X âŸ¶ Y)
    (t : IsColimit (mk (ğŸ™ Y) (ğŸ™ Y) rfl : PushoutCocone f f)) : Epi f :=
  âŸ¨fun {Z} g h eq => by
    rcases PushoutCocone.IsColimit.desc' t _ _ eq with âŸ¨_, rfl, rflâŸ©
    rflâŸ©

/-- Suppose `f` and `g` are two morphisms with a common domain and `s` is a colimit cocone over the
    diagram formed by `f` and `g`. Suppose `f` and `g` both factor through an epimorphism `h` via
    `x` and `y`, respectively. Then `s` is also a colimit cocone over the diagram formed by `x` and
    `y`. -/
def isColimitOfFactors (f : X âŸ¶ Y) (g : X âŸ¶ Z) (h : X âŸ¶ W) [Epi h] (x : W âŸ¶ Y) (y : W âŸ¶ Z)
    (hhx : h â‰« x = f) (hhy : h â‰« y = g) (s : PushoutCocone f g) (hs : IsColimit s) :
    have reassocâ‚ : h â‰« x â‰« inl s = f â‰« inl s := by  -- Porting note: working around reassoc
      rw [â† Category.assoc]; apply congrArg (Â· â‰« inl s) hhx
    have reassocâ‚‚ : h â‰« y â‰« inr s = g â‰« inr s := by
      rw [â† Category.assoc]; apply congrArg (Â· â‰« inr s) hhy
    IsColimit (PushoutCocone.mk _ _ (show x â‰« s.inl = y â‰« s.inr from
          (cancel_epi h).1 <| by rw [reassocâ‚, reassocâ‚‚, s.condition])) :=
  PushoutCocone.isColimitAux' _ fun t => âŸ¨hs.desc (PushoutCocone.mk t.inl t.inr <| by
    rw [â† hhx, â† hhy, Category.assoc, Category.assoc, t.condition]),
      âŸ¨hs.fac _ WalkingSpan.left, hs.fac _ WalkingSpan.right, fun hr hr' => by
        apply PushoutCocone.IsColimit.hom_ext hs
        Â· simp only [PushoutCocone.mk_inl, PushoutCocone.mk_inr] at hr hr' âŠ¢
          simp only [hr, hr']
          symm
          exact hs.fac _ WalkingSpan.left
        Â· simp only [PushoutCocone.mk_inl, PushoutCocone.mk_inr] at hr hr' âŠ¢
          simp only [hr, hr']
          symm
          exact hs.fac _ WalkingSpan.rightâŸ©âŸ©

/-- If `W` is the pushout of `f, g`,
it is also the pushout of `h â‰« f, h â‰« g` for any epi `h`. -/
def isColimitOfEpiComp (f : X âŸ¶ Y) (g : X âŸ¶ Z) (h : W âŸ¶ X) [Epi h] (s : PushoutCocone f g)
    (H : IsColimit s) :
    IsColimit
      (PushoutCocone.mk _ _
        (show (h â‰« f) â‰« s.inl = (h â‰« g) â‰« s.inr by
          rw [Category.assoc, Category.assoc, s.condition])) := by
  apply PushoutCocone.isColimitAux'
  intro s
  rcases PushoutCocone.IsColimit.desc' H s.inl s.inr
      ((cancel_epi h).mp (by simpa using s.condition)) with
    âŸ¨l, hâ‚, hâ‚‚âŸ©
  refine âŸ¨l, hâ‚, hâ‚‚, ?_âŸ©
  intro m hmâ‚ hmâ‚‚
  exact (PushoutCocone.IsColimit.hom_ext H (hmâ‚.trans hâ‚.symm) (hmâ‚‚.trans hâ‚‚.symm) :)

end PushoutCocone

/-- The pushout of an epimorphism is an epimorphism -/
instance pushout.inl_of_epi {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [HasPushout f g] [Epi g] :
    Epi (pushout.inl f g) :=
  PushoutCocone.epi_inl_of_is_pushout_of_epi (colimit.isColimit _)

/-- The pushout of an epimorphism is an epimorphism -/
instance pushout.inr_of_epi {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [HasPushout f g] [Epi f] :
    Epi (pushout.inr _ _ : Z âŸ¶ pushout f g) :=
  PushoutCocone.epi_inr_of_is_pushout_of_epi (colimit.isColimit _)

/-- The map `X â¨¿ Y âŸ¶ X â¨¿[Z] Y` is epi. -/
instance epi_coprod_to_pushout {C : Type*} [Category C] {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z)
    [HasPushout f g] [HasBinaryCoproduct Y Z] :
    Epi (coprod.desc (pushout.inl f g) (pushout.inr f g)) :=
  âŸ¨fun {W} iâ‚ iâ‚‚ h => by
    ext
    Â· simpa using congrArg (fun f => coprod.inl â‰« f) h
    Â· simpa using congrArg (fun f => coprod.inr â‰« f) hâŸ©

/-- The pushout of `f, g` is also the pullback of `h â‰« f, h â‰« g` for any epi `h`. -/
noncomputable def pushoutIsPushoutOfEpiComp (f : X âŸ¶ Y) (g : X âŸ¶ Z) (h : W âŸ¶ X) [Epi h]
    [HasPushout f g] : IsColimit (PushoutCocone.mk (pushout.inl f g) (pushout.inr f g)
    (show (h â‰« f) â‰« pushout.inl f g = (h â‰« g) â‰« pushout.inr f g from by
    simp only [Category.assoc]; rw [cancel_epi]; exact pushout.condition)) :=
  PushoutCocone.isColimitOfEpiComp f g h _ (colimit.isColimit (span f g))

instance hasPushout_of_epi_comp (f : X âŸ¶ Y) (g : X âŸ¶ Z) (h : W âŸ¶ X) [Epi h] [HasPushout f g] :
    HasPushout (h â‰« f) (h â‰« g) :=
  âŸ¨âŸ¨âŸ¨_, pushoutIsPushoutOfEpiComp f g hâŸ©âŸ©âŸ©

section

attribute [local instance] hasPushout_of_left_iso

variable (f : X âŸ¶ Z) (h : W âŸ¶ X) [Epi h]

instance hasPushout_of_right_factors_epi : HasPushout h (h â‰« f) := by
  simpa only [Category.comp_id] using hasPushout_of_epi_comp (ğŸ™ X) f h

instance pushout_inr_iso_of_right_factors_epi :
    IsIso (pushout.inr _ _ : _ âŸ¶ pushout h (h â‰« f)) := by
  convert (congrArg IsIso (show pushout.inr _ _ â‰« _ = _ from colimit.isoColimitCocone_Î¹_inv
    âŸ¨_, pushoutIsPushoutOfEpiComp (ğŸ™ _) f hâŸ© WalkingSpan.right)).mp
    inferInstance
  Â· apply (Category.comp_id _).symm
  Â· apply (Category.comp_id _).symm

attribute [local instance] hasPushout_of_right_iso

instance hasPushout_of_left_factors_epi (f : X âŸ¶ Y) : HasPushout (h â‰« f) h := by
  simpa only [Category.comp_id] using hasPushout_of_epi_comp f (ğŸ™ X) h

instance pushout_inl_iso_of_left_factors_epi (f : X âŸ¶ Y) :
    IsIso (pushout.inl _ _ : _ âŸ¶ pushout (h â‰« f) h) := by
  convert (congrArg IsIso (show pushout.inl _ _ â‰« _ = _ from colimit.isoColimitCocone_Î¹_inv
    âŸ¨_, pushoutIsPushoutOfEpiComp f (ğŸ™ _) hâŸ© WalkingSpan.left)).mp
        inferInstance
  Â· exact (Category.comp_id _).symm
  Â· exact (Category.comp_id _).symm

end

section

open WalkingSpan

variable (f : X âŸ¶ Y) [Epi f]

instance has_cokernel_pair_of_epi : HasPushout f f :=
  âŸ¨âŸ¨âŸ¨_, PushoutCocone.isColimitMkIdId fâŸ©âŸ©âŸ©

theorem PushoutCocone.inl_eq_inr_of_epi_eq {f : X âŸ¶ Y} [Epi f] (t : PushoutCocone f f) :
    t.inl = t.inr :=
  (cancel_epi f).1 t.condition

theorem inl_eq_inr_of_epi_eq : pushout.inl f f = pushout.inr f f :=
  PushoutCocone.inl_eq_inr_of_epi_eq (getColimitCocone (span f f)).cocone

@[simp]
theorem pullback_symmetry_hom_of_epi_eq : (pushoutSymmetry f f).hom = ğŸ™ _ := by
  ext <;> simp [inl_eq_inr_of_epi_eq]

variable {f} in
lemma PushoutCocone.isIso_inl_of_epi_of_isColimit {t : PushoutCocone f f} (ht : IsColimit t) :
    IsIso t.inl := by
  refine âŸ¨âŸ¨PushoutCocone.IsColimit.desc ht (ğŸ™ _) (ğŸ™ _) (by simp), by simp, ?_âŸ©âŸ©
  apply PushoutCocone.IsColimit.hom_ext ht
  Â· simp
  Â· simp [inl_eq_inr_of_epi_eq]

variable {f} in
lemma PushoutCocone.isIso_inr_of_epi_of_isColimit {t : PushoutCocone f f} (ht : IsColimit t) :
    IsIso t.inr :=
  t.inl_eq_inr_of_epi_eq â–¸ t.isIso_inl_of_epi_of_isColimit ht

instance isIso_inl_of_epi : IsIso (pushout.inl f f) :=
  PushoutCocone.isIso_inl_of_epi_of_isColimit (getColimitCocone (span f f)).isColimit

instance isIso_inr_of_epi : IsIso (pushout.inr f f) :=
  PushoutCocone.isIso_inr_of_epi_of_isColimit (getColimitCocone (span f f)).isColimit

end

end CategoryTheory.Limits
