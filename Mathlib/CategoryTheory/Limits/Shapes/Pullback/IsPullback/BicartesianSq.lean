/-
Copyright (c) 2022 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Jo√´l Riou, Calle S√∂nne
-/
module

public import Mathlib.CategoryTheory.Limits.Constructions.ZeroObjects
public import Mathlib.CategoryTheory.Limits.Shapes.BinaryBiproducts
public import Mathlib.CategoryTheory.Limits.Shapes.Pullback.IsPullback.Basic

/-!
# Bi-Cartesian squares

`BicartesianSq f g h i` is the proposition that
```
  W ---f---> X
  |          |
  g          h
  |          |
  v          v
  Y ---i---> Z

```
is a pullback square *and* a pushout square.

We show that the pullback and pushout squares for a biproduct are bi-Cartesian.
-/

@[expose] public section

noncomputable section

open CategoryTheory

open CategoryTheory.Limits

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]

/-- A *bi-Cartesian* square is a commutative square
```
  W ---f---> X
  |          |
  g          h
  |          |
  v          v
  Y ---i---> Z

```
that is both a pullback square and a pushout square.
-/
structure BicartesianSq {W X Y Z : C} (f : W ‚ü∂ X) (g : W ‚ü∂ Y) (h : X ‚ü∂ Z) (i : Y ‚ü∂ Z) : Prop
    extends IsPullback f g h i, IsPushout f g h i


variable [HasZeroObject C] [HasZeroMorphisms C]
open ZeroObject

namespace IsPullback

variable {P X Y Z : C} {fst : P ‚ü∂ X} {snd : P ‚ü∂ Y} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z}

theorem of_hasBinaryProduct [HasBinaryProduct X Y] :
    IsPullback Limits.prod.fst Limits.prod.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) := by
  convert @of_is_product _ _ X Y 0 _ (limit.isLimit _) HasZeroObject.zeroIsTerminal
    <;> subsingleton

/-- The square with `0 : 0 ‚ü∂ 0` on the left and `ùüô X` on the right is a pullback square. -/
@[simp]
theorem zero_left (X : C) : IsPullback (0 : 0 ‚ü∂ X) (0 : (0 : C) ‚ü∂ 0) (ùüô X) (0 : 0 ‚ü∂ X) :=
  { w := by simp
    isLimit' :=
      ‚ü®{  lift := fun _ => 0
          fac := fun s => by
            simpa [eq_iff_true_of_subsingleton] using
              @PullbackCone.equalizer_ext _ _ _ _ _ _ _ s _ 0 (ùüô _)
                (by simpa using (PullbackCone.condition s).symm) }‚ü© }

/-- The square with `0 : 0 ‚ü∂ 0` on the top and `ùüô X` on the bottom is a pullback square. -/
@[simp]
theorem zero_top (X : C) : IsPullback (0 : (0 : C) ‚ü∂ 0) (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ X) (ùüô X) :=
  (zero_left X).flip

/-- The square with `0 : 0 ‚ü∂ 0` on the right and `ùüô X` on the left is a pullback square. -/
@[simp]
theorem zero_right (X : C) : IsPullback (0 : X ‚ü∂ 0) (ùüô X) (0 : (0 : C) ‚ü∂ 0) (0 : X ‚ü∂ 0) :=
  of_iso_pullback (by simp) ((zeroProdIso X).symm ‚â™‚â´ (pullbackZeroZeroIso _ _).symm)
    (by simp [eq_iff_true_of_subsingleton]) (by simp)

/-- The square with `0 : 0 ‚ü∂ 0` on the bottom and `ùüô X` on the top is a pullback square. -/
@[simp]
theorem zero_bot (X : C) : IsPullback (ùüô X) (0 : X ‚ü∂ 0) (0 : X ‚ü∂ 0) (0 : (0 : C) ‚ü∂ 0) :=
  (zero_right X).flip

theorem of_isBilimit {b : BinaryBicone X Y} (h : b.IsBilimit) :
    IsPullback b.fst b.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) := by
  convert IsPullback.of_is_product' h.isLimit HasZeroObject.zeroIsTerminal
    <;> subsingleton

@[simp]
theorem of_has_biproduct (X Y : C) [HasBinaryBiproduct X Y] :
    IsPullback biprod.fst biprod.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) :=
  of_isBilimit (BinaryBiproduct.isBilimit X Y)

theorem inl_snd' {b : BinaryBicone X Y} (h : b.IsBilimit) :
    IsPullback b.inl (0 : X ‚ü∂ 0) b.snd (0 : 0 ‚ü∂ Y) := by
  refine of_right ?_ (by simp) (of_isBilimit h)
  simp

/-- The square
```
  X --inl--> X ‚äû Y
  |            |
  0           snd
  |            |
  v            v
  0 ---0-----> Y
```
is a pullback square.
-/
@[simp]
theorem inl_snd (X Y : C) [HasBinaryBiproduct X Y] :
    IsPullback biprod.inl (0 : X ‚ü∂ 0) biprod.snd (0 : 0 ‚ü∂ Y) :=
  inl_snd' (BinaryBiproduct.isBilimit X Y)

theorem inr_fst' {b : BinaryBicone X Y} (h : b.IsBilimit) :
    IsPullback b.inr (0 : Y ‚ü∂ 0) b.fst (0 : 0 ‚ü∂ X) := by
  apply flip
  refine of_bot ?_ (by simp) (of_isBilimit h)
  simp

/-- The square
```
  Y --inr--> X ‚äû Y
  |            |
  0           fst
  |            |
  v            v
  0 ---0-----> X
```
is a pullback square.
-/
@[simp]
theorem inr_fst (X Y : C) [HasBinaryBiproduct X Y] :
    IsPullback biprod.inr (0 : Y ‚ü∂ 0) biprod.fst (0 : 0 ‚ü∂ X) :=
  inr_fst' (BinaryBiproduct.isBilimit X Y)

theorem of_is_bilimit' {b : BinaryBicone X Y} (h : b.IsBilimit) :
    IsPullback (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) b.inl b.inr := by
  refine IsPullback.of_right ?_ (by simp) (IsPullback.inl_snd' h).flip
  simp

theorem of_hasBinaryBiproduct (X Y : C) [HasBinaryBiproduct X Y] :
    IsPullback (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) biprod.inl biprod.inr :=
  of_is_bilimit' (BinaryBiproduct.isBilimit X Y)

instance hasPullback_biprod_fst_biprod_snd [HasBinaryBiproduct X Y] :
    HasPullback (biprod.inl : X ‚ü∂ _) (biprod.inr : Y ‚ü∂ _) :=
  HasLimit.mk ‚ü®_, (of_hasBinaryBiproduct X Y).isLimit‚ü©

/-- The pullback of `biprod.inl` and `biprod.inr` is the zero object. -/
def pullbackBiprodInlBiprodInr [HasBinaryBiproduct X Y] :
    pullback (biprod.inl : X ‚ü∂ _) (biprod.inr : Y ‚ü∂ _) ‚âÖ 0 :=
  limit.isoLimitCone ‚ü®_, (of_hasBinaryBiproduct X Y).isLimit‚ü©

end IsPullback

namespace IsPushout

variable {Z X Y P : C} {f : Z ‚ü∂ X} {g : Z ‚ü∂ Y} {inl : X ‚ü∂ P} {inr : Y ‚ü∂ P}

theorem of_hasBinaryCoproduct [HasBinaryCoproduct X Y] :
    IsPushout (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) coprod.inl coprod.inr := by
  convert @of_is_coproduct _ _ 0 X Y _ (colimit.isColimit _) HasZeroObject.zeroIsInitial
    <;> subsingleton

/-- The square with `0 : 0 ‚ü∂ 0` on the right and `ùüô X` on the left is a pushout square. -/
@[simp]
theorem zero_right (X : C) : IsPushout (0 : X ‚ü∂ 0) (ùüô X) (0 : (0 : C) ‚ü∂ 0) (0 : X ‚ü∂ 0) :=
  { w := by simp
    isColimit' :=
      ‚ü®{  desc := fun _ => 0
          fac := fun s => by
            have c :=
              @PushoutCocone.coequalizer_ext _ _ _ _ _ _ _ s _ 0 (ùüô _)
                (by simp [eq_iff_true_of_subsingleton]) (by simpa using PushoutCocone.condition s)
            dsimp at c
            simpa using c }‚ü© }

/-- The square with `0 : 0 ‚ü∂ 0` on the bottom and `ùüô X` on the top is a pushout square. -/
@[simp]
theorem zero_bot (X : C) : IsPushout (ùüô X) (0 : X ‚ü∂ 0) (0 : X ‚ü∂ 0) (0 : (0 : C) ‚ü∂ 0) :=
  (zero_right X).flip

/-- The square with `0 : 0 ‚ü∂ 0` on the right left `ùüô X` on the right is a pushout square. -/
@[simp]
theorem zero_left (X : C) : IsPushout (0 : 0 ‚ü∂ X) (0 : (0 : C) ‚ü∂ 0) (ùüô X) (0 : 0 ‚ü∂ X) :=
  of_iso_pushout (by simp) ((coprodZeroIso X).symm ‚â™‚â´ (pushoutZeroZeroIso _ _).symm) (by simp)
    (by simp [eq_iff_true_of_subsingleton])

/-- The square with `0 : 0 ‚ü∂ 0` on the top and `ùüô X` on the bottom is a pushout square. -/
@[simp]
theorem zero_top (X : C) : IsPushout (0 : (0 : C) ‚ü∂ 0) (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ X) (ùüô X) :=
  (zero_left X).flip


theorem of_isBilimit {b : BinaryBicone X Y} (h : b.IsBilimit) :
    IsPushout (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) b.inl b.inr := by
  convert IsPushout.of_is_coproduct' h.isColimit HasZeroObject.zeroIsInitial
    <;> subsingleton

@[simp]
theorem of_has_biproduct (X Y : C) [HasBinaryBiproduct X Y] :
    IsPushout (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) biprod.inl biprod.inr :=
  of_isBilimit (BinaryBiproduct.isBilimit X Y)

theorem inl_snd' {b : BinaryBicone X Y} (h : b.IsBilimit) :
    IsPushout b.inl (0 : X ‚ü∂ 0) b.snd (0 : 0 ‚ü∂ Y) := by
  apply flip
  refine of_left ?_ (by simp) (of_isBilimit h)
  simp

/-- The square
```
  X --inl--> X ‚äû Y
  |            |
  0           snd
  |            |
  v            v
  0 ---0-----> Y
```
is a pushout square.
-/
theorem inl_snd (X Y : C) [HasBinaryBiproduct X Y] :
    IsPushout biprod.inl (0 : X ‚ü∂ 0) biprod.snd (0 : 0 ‚ü∂ Y) :=
  inl_snd' (BinaryBiproduct.isBilimit X Y)

theorem inr_fst' {b : BinaryBicone X Y} (h : b.IsBilimit) :
    IsPushout b.inr (0 : Y ‚ü∂ 0) b.fst (0 : 0 ‚ü∂ X) := by
  refine of_top ?_ (by simp) (of_isBilimit h)
  simp

/-- The square
```
  Y --inr--> X ‚äû Y
  |            |
  0           fst
  |            |
  v            v
  0 ---0-----> X
```
is a pushout square.
-/
theorem inr_fst (X Y : C) [HasBinaryBiproduct X Y] :
    IsPushout biprod.inr (0 : Y ‚ü∂ 0) biprod.fst (0 : 0 ‚ü∂ X) :=
  inr_fst' (BinaryBiproduct.isBilimit X Y)

theorem of_is_bilimit' {b : BinaryBicone X Y} (h : b.IsBilimit) :
    IsPushout b.fst b.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) := by
  refine IsPushout.of_left ?_ (by simp) (IsPushout.inl_snd' h)
  simp

theorem of_hasBinaryBiproduct (X Y : C) [HasBinaryBiproduct X Y] :
    IsPushout biprod.fst biprod.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) :=
  of_is_bilimit' (BinaryBiproduct.isBilimit X Y)

instance hasPushout_biprod_fst_biprod_snd [HasBinaryBiproduct X Y] :
    HasPushout (biprod.fst : _ ‚ü∂ X) (biprod.snd : _ ‚ü∂ Y) :=
  HasColimit.mk ‚ü®_, (of_hasBinaryBiproduct X Y).isColimit‚ü©

/-- The pushout of `biprod.fst` and `biprod.snd` is the zero object. -/
def pushoutBiprodFstBiprodSnd [HasBinaryBiproduct X Y] :
    pushout (biprod.fst : _ ‚ü∂ X) (biprod.snd : _ ‚ü∂ Y) ‚âÖ 0 :=
  colimit.isoColimitCocone ‚ü®_, (of_hasBinaryBiproduct X Y).isColimit‚ü©

end IsPushout

namespace BicartesianSq

variable {W X Y Z : C} {f : W ‚ü∂ X} {g : W ‚ü∂ Y} {h : X ‚ü∂ Z} {i : Y ‚ü∂ Z}

omit [HasZeroObject C] [HasZeroMorphisms C] in
section

theorem of_isPullback_isPushout (p‚ÇÅ : IsPullback f g h i) (p‚ÇÇ : IsPushout f g h i) :
    BicartesianSq f g h i :=
  BicartesianSq.mk p‚ÇÅ p‚ÇÇ.isColimit'

theorem flip (p : BicartesianSq f g h i) : BicartesianSq g f i h :=
  of_isPullback_isPushout p.toIsPullback.flip p.toIsPushout.flip

end


/-- ```
 X ‚äû Y --fst--> X
   |            |
  snd           0
   |            |
   v            v
   Y -----0---> 0
```
is a bi-Cartesian square.
-/
theorem of_is_biproduct‚ÇÅ {b : BinaryBicone X Y} (h : b.IsBilimit) :
    BicartesianSq b.fst b.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) :=
  of_isPullback_isPushout (IsPullback.of_isBilimit h) (IsPushout.of_is_bilimit' h)

/-- ```
   0 -----0---> X
   |            |
   0           inl
   |            |
   v            v
   Y --inr--> X ‚äû Y
```
is a bi-Cartesian square.
-/
theorem of_is_biproduct‚ÇÇ {b : BinaryBicone X Y} (h : b.IsBilimit) :
    BicartesianSq (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) b.inl b.inr :=
  of_isPullback_isPushout (IsPullback.of_is_bilimit' h) (IsPushout.of_isBilimit h)

/-- ```
 X ‚äû Y --fst--> X
   |            |
  snd           0
   |            |
   v            v
   Y -----0---> 0
```
is a bi-Cartesian square.
-/
@[simp]
theorem of_has_biproduct‚ÇÅ [HasBinaryBiproduct X Y] :
    BicartesianSq biprod.fst biprod.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) := by
  convert of_is_biproduct‚ÇÅ (BinaryBiproduct.isBilimit X Y)

/-- ```
   0 -----0---> X
   |            |
   0           inl
   |            |
   v            v
   Y --inr--> X ‚äû Y
```
is a bi-Cartesian square.
-/
@[simp]
theorem of_has_biproduct‚ÇÇ [HasBinaryBiproduct X Y] :
    BicartesianSq (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) biprod.inl biprod.inr := by
  convert of_is_biproduct‚ÇÇ (BinaryBiproduct.isBilimit X Y)

end BicartesianSq
end CategoryTheory
