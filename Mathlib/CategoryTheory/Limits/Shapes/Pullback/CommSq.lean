/-
Copyright (c) 2022 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Jo√´l Riou, Calle S√∂nne
-/

import Mathlib.CategoryTheory.Limits.Constructions.ZeroObjects
import Mathlib.CategoryTheory.Limits.Shapes.BinaryBiproducts
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.Pasting
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.Iso

/-!
# Pullback and pushout squares, and bicartesian squares

We provide another API for pullbacks and pushouts.

`IsPullback fst snd f g` is the proposition that
```
  P --fst--> X
  |          |
 snd         f
  |          |
  v          v
  Y ---g---> Z

```
is a pullback square.

(And similarly for `IsPushout`.)

We provide the glue to go back and forth to the usual `IsLimit` API for pullbacks, and prove
`IsPullback (pullback.fst f g) (pullback.snd f g) f g`
for the usual `pullback f g` provided by the `HasLimit` API.

We don't attempt to restate everything we know about pullbacks in this language,
but do restate the pasting lemmas.

We define bicartesian squares, and
show that the pullback and pushout squares for a biproduct are bicartesian.
-/


noncomputable section

open CategoryTheory

open CategoryTheory.Limits

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]

attribute [simp] CommSq.mk

namespace CommSq

variable {W X Y Z : C} {f : W ‚ü∂ X} {g : W ‚ü∂ Y} {h : X ‚ü∂ Z} {i : Y ‚ü∂ Z}

/-- The (not necessarily limiting) `PullbackCone h i` implicit in the statement
that we have `CommSq f g h i`.
-/
def cone (s : CommSq f g h i) : PullbackCone h i :=
  PullbackCone.mk _ _ s.w

/-- The (not necessarily limiting) `PushoutCocone f g` implicit in the statement
that we have `CommSq f g h i`.
-/
def cocone (s : CommSq f g h i) : PushoutCocone f g :=
  PushoutCocone.mk _ _ s.w

@[simp]
theorem cone_fst (s : CommSq f g h i) : s.cone.fst = f :=
  rfl

@[simp]
theorem cone_snd (s : CommSq f g h i) : s.cone.snd = g :=
  rfl

@[simp]
theorem cocone_inl (s : CommSq f g h i) : s.cocone.inl = h :=
  rfl

@[simp]
theorem cocone_inr (s : CommSq f g h i) : s.cocone.inr = i :=
  rfl

/-- The pushout cocone in the opposite category associated to the cone of
a commutative square identifies to the cocone of the flipped commutative square in
the opposite category -/
def coneOp (p : CommSq f g h i) : p.cone.op ‚âÖ p.flip.op.cocone :=
  PushoutCocone.ext (Iso.refl _) (by simp) (by simp)

/-- The pullback cone in the opposite category associated to the cocone of
a commutative square identifies to the cone of the flipped commutative square in
the opposite category -/
def coconeOp (p : CommSq f g h i) : p.cocone.op ‚âÖ p.flip.op.cone :=
  PullbackCone.ext (Iso.refl _) (by simp) (by simp)

/-- The pushout cocone obtained from the pullback cone associated to a
commutative square in the opposite category identifies to the cocone associated
to the flipped square. -/
def coneUnop {W X Y Z : C·µí·µñ} {f : W ‚ü∂ X} {g : W ‚ü∂ Y} {h : X ‚ü∂ Z} {i : Y ‚ü∂ Z} (p : CommSq f g h i) :
    p.cone.unop ‚âÖ p.flip.unop.cocone :=
  PushoutCocone.ext (Iso.refl _) (by simp) (by simp)

/-- The pullback cone obtained from the pushout cone associated to a
commutative square in the opposite category identifies to the cone associated
to the flipped square. -/
def coconeUnop {W X Y Z : C·µí·µñ} {f : W ‚ü∂ X} {g : W ‚ü∂ Y} {h : X ‚ü∂ Z} {i : Y ‚ü∂ Z}
    (p : CommSq f g h i) : p.cocone.unop ‚âÖ p.flip.unop.cone :=
  PullbackCone.ext (Iso.refl _) (by simp) (by simp)

end CommSq

/-- The proposition that a square
```
  P --fst--> X
  |          |
 snd         f
  |          |
  v          v
  Y ---g---> Z

```
is a pullback square. (Also known as a fibered product or cartesian square.)
-/
structure IsPullback {P X Y Z : C} (fst : P ‚ü∂ X) (snd : P ‚ü∂ Y) (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) : Prop
    extends CommSq fst snd f g where
  /-- the pullback cone is a limit -/
  isLimit' : Nonempty (IsLimit (PullbackCone.mk _ _ w))

/-- The proposition that a square
```
  Z ---f---> X
  |          |
  g         inl
  |          |
  v          v
  Y --inr--> P

```
is a pushout square. (Also known as a fiber coproduct or cocartesian square.)
-/
structure IsPushout {Z X Y P : C} (f : Z ‚ü∂ X) (g : Z ‚ü∂ Y) (inl : X ‚ü∂ P) (inr : Y ‚ü∂ P) : Prop
    extends CommSq f g inl inr where
  /-- the pushout cocone is a colimit -/
  isColimit' : Nonempty (IsColimit (PushoutCocone.mk _ _ w))

section

/-- A *bicartesian* square is a commutative square
```
  W ---f---> X
  |          |
  g          h
  |          |
  v          v
  Y ---i---> Z

```
that is both a pullback square and a pushout square.
-/
structure BicartesianSq {W X Y Z : C} (f : W ‚ü∂ X) (g : W ‚ü∂ Y) (h : X ‚ü∂ Z) (i : Y ‚ü∂ Z) : Prop
    extends IsPullback f g h i, IsPushout f g h i

-- Lean should make these parent projections as `lemma`, not `def`.
attribute [nolint defLemma docBlame] BicartesianSq.toIsPushout

end

/-!
We begin by providing some glue between `IsPullback` and the `IsLimit` and `HasLimit` APIs.
(And similarly for `IsPushout`.)
-/


namespace IsPullback

variable {P X Y Z : C} {fst : P ‚ü∂ X} {snd : P ‚ü∂ Y} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z}

/-- The (limiting) `PullbackCone f g` implicit in the statement
that we have an `IsPullback fst snd f g`.
-/
def cone (h : IsPullback fst snd f g) : PullbackCone f g :=
  h.toCommSq.cone

@[simp]
theorem cone_fst (h : IsPullback fst snd f g) : h.cone.fst = fst :=
  rfl

@[simp]
theorem cone_snd (h : IsPullback fst snd f g) : h.cone.snd = snd :=
  rfl

/-- The cone obtained from `IsPullback fst snd f g` is a limit cone.
-/
noncomputable def isLimit (h : IsPullback fst snd f g) : IsLimit h.cone :=
  h.isLimit'.some

/-- API for PullbackCone.IsLimit.lift for `IsPullback` -/
noncomputable def lift (hP : IsPullback fst snd f g) {W : C} (h : W ‚ü∂ X) (k : W ‚ü∂ Y)
    (w : h ‚â´ f = k ‚â´ g) : W ‚ü∂ P :=
  PullbackCone.IsLimit.lift hP.isLimit h k w

@[reassoc (attr := simp)]
lemma lift_fst (hP : IsPullback fst snd f g) {W : C} (h : W ‚ü∂ X) (k : W ‚ü∂ Y)
    (w : h ‚â´ f = k ‚â´ g) : hP.lift h k w ‚â´ fst = h :=
  PullbackCone.IsLimit.lift_fst hP.isLimit h k w

@[reassoc (attr := simp)]
lemma lift_snd (hP : IsPullback fst snd f g) {W : C} (h : W ‚ü∂ X) (k : W ‚ü∂ Y)
    (w : h ‚â´ f = k ‚â´ g) : hP.lift h k w ‚â´ snd = k :=
  PullbackCone.IsLimit.lift_snd hP.isLimit h k w

lemma hom_ext (hP : IsPullback fst snd f g) {W : C} {k l : W ‚ü∂ P}
    (h‚ÇÄ : k ‚â´ fst = l ‚â´ fst) (h‚ÇÅ : k ‚â´ snd = l ‚â´ snd) : k = l :=
  PullbackCone.IsLimit.hom_ext hP.isLimit h‚ÇÄ h‚ÇÅ

/-- If `c` is a limiting pullback cone, then we have an `IsPullback c.fst c.snd f g`. -/
theorem of_isLimit {c : PullbackCone f g} (h : Limits.IsLimit c) : IsPullback c.fst c.snd f g :=
  { w := c.condition
    isLimit' := ‚ü®IsLimit.ofIsoLimit h (Limits.PullbackCone.ext (Iso.refl _)
      (by simp) (by simp))‚ü© }

/-- A variant of `of_isLimit` that is more useful with `apply`. -/
theorem of_isLimit' (w : CommSq fst snd f g) (h : Limits.IsLimit w.cone) :
    IsPullback fst snd f g :=
  of_isLimit h

/-- Variant of `of_isLimit` for an arbitrary cone on a diagram `WalkingCospan ‚•§ C`. -/
lemma of_isLimit_cone {D : WalkingCospan ‚•§ C} {c : Cone D} (hc : IsLimit c) :
    IsPullback (c.œÄ.app .left) (c.œÄ.app .right) (D.map WalkingCospan.Hom.inl)
      (D.map WalkingCospan.Hom.inr) where
  w := by simp_rw [Cone.w]
  isLimit' := ‚ü®IsLimit.equivOfNatIsoOfIso _ _ _ (PullbackCone.isoMk c) hc‚ü©

lemma hasPullback (h : IsPullback fst snd f g) : HasPullback f g where
  exists_limit := ‚ü®‚ü®h.cone, h.isLimit‚ü©‚ü©

/-- The pullback provided by `HasPullback f g` fits into an `IsPullback`. -/
theorem of_hasPullback (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [HasPullback f g] :
    IsPullback (pullback.fst f g) (pullback.snd f g) f g :=
  of_isLimit (limit.isLimit (cospan f g))

/-- If `c` is a limiting binary product cone, and we have a terminal object,
then we have `IsPullback c.fst c.snd 0 0`
(where each `0` is the unique morphism to the terminal object). -/
theorem of_is_product {c : BinaryFan X Y} (h : Limits.IsLimit c) (t : IsTerminal Z) :
    IsPullback c.fst c.snd (t.from _) (t.from _) :=
  of_isLimit
    (isPullbackOfIsTerminalIsProduct _ _ _ _ t
      (IsLimit.ofIsoLimit h
        (Limits.Cones.ext (Iso.refl c.pt)
          (by
            rintro ‚ü®‚ü®‚ü©‚ü© <;> simp))))

/-- A variant of `of_is_product` that is more useful with `apply`. -/
theorem of_is_product' (h : Limits.IsLimit (BinaryFan.mk fst snd)) (t : IsTerminal Z) :
    IsPullback fst snd (t.from _) (t.from _) :=
  of_is_product h t

variable (X Y)

theorem of_hasBinaryProduct' [HasBinaryProduct X Y] [HasTerminal C] :
    IsPullback Limits.prod.fst Limits.prod.snd (terminal.from X) (terminal.from Y) :=
  of_is_product (limit.isLimit _) terminalIsTerminal

open ZeroObject

theorem of_hasBinaryProduct [HasBinaryProduct X Y] [HasZeroObject C] [HasZeroMorphisms C] :
    IsPullback Limits.prod.fst Limits.prod.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) := by
  convert @of_is_product _ _ X Y 0 _ (limit.isLimit _) HasZeroObject.zeroIsTerminal
    <;> subsingleton

section

variable {P' : C} {fst' : P' ‚ü∂ X} {snd' : P' ‚ü∂ Y}

/-- Any object at the top left of a pullback square is isomorphic to the object at the top left
of any other pullback square with the same cospan. -/
noncomputable def isoIsPullback (h : IsPullback fst snd f g) (h' : IsPullback fst' snd' f g) :
    P ‚âÖ P' :=
  IsLimit.conePointUniqueUpToIso h.isLimit h'.isLimit

@[reassoc (attr := simp)]
theorem isoIsPullback_hom_fst (h : IsPullback fst snd f g) (h' : IsPullback fst' snd' f g) :
    (h.isoIsPullback _ _ h').hom ‚â´ fst' = fst :=
  IsLimit.conePointUniqueUpToIso_hom_comp h.isLimit h'.isLimit WalkingCospan.left

@[reassoc (attr := simp)]
theorem isoIsPullback_hom_snd (h : IsPullback fst snd f g) (h' : IsPullback fst' snd' f g) :
    (h.isoIsPullback _ _ h').hom ‚â´ snd' = snd :=
  IsLimit.conePointUniqueUpToIso_hom_comp h.isLimit h'.isLimit WalkingCospan.right

@[reassoc (attr := simp)]
theorem isoIsPullback_inv_fst (h : IsPullback fst snd f g) (h' : IsPullback fst' snd' f g) :
    (h.isoIsPullback _ _ h').inv ‚â´ fst = fst' := by
  simp only [Iso.inv_comp_eq, isoIsPullback_hom_fst]

@[reassoc (attr := simp)]
theorem isoIsPullback_inv_snd (h : IsPullback fst snd f g) (h' : IsPullback fst' snd' f g) :
    (h.isoIsPullback _ _ h').inv ‚â´ snd = snd' := by
  simp only [Iso.inv_comp_eq, isoIsPullback_hom_snd]

end

variable {X Y}

/-- Any object at the top left of a pullback square is
isomorphic to the pullback provided by the `HasLimit` API. -/
noncomputable def isoPullback (h : IsPullback fst snd f g) [HasPullback f g] : P ‚âÖ pullback f g :=
  (limit.isoLimitCone ‚ü®_, h.isLimit‚ü©).symm

@[reassoc (attr := simp)]
theorem isoPullback_hom_fst (h : IsPullback fst snd f g) [HasPullback f g] :
    h.isoPullback.hom ‚â´ pullback.fst _ _ = fst := by
  dsimp [isoPullback, cone, CommSq.cone]
  simp

@[reassoc (attr := simp)]
theorem isoPullback_hom_snd (h : IsPullback fst snd f g) [HasPullback f g] :
    h.isoPullback.hom ‚â´ pullback.snd _ _ = snd := by
  dsimp [isoPullback, cone, CommSq.cone]
  simp

@[reassoc (attr := simp)]
theorem isoPullback_inv_fst (h : IsPullback fst snd f g) [HasPullback f g] :
    h.isoPullback.inv ‚â´ fst = pullback.fst _ _ := by simp [Iso.inv_comp_eq]

@[reassoc (attr := simp)]
theorem isoPullback_inv_snd (h : IsPullback fst snd f g) [HasPullback f g] :
    h.isoPullback.inv ‚â´ snd = pullback.snd _ _ := by simp [Iso.inv_comp_eq]

theorem of_iso_pullback (h : CommSq fst snd f g) [HasPullback f g] (i : P ‚âÖ pullback f g)
    (w‚ÇÅ : i.hom ‚â´ pullback.fst _ _ = fst) (w‚ÇÇ : i.hom ‚â´ pullback.snd _ _ = snd) :
      IsPullback fst snd f g :=
  of_isLimit' h
    (Limits.IsLimit.ofIsoLimit (limit.isLimit _)
      (@PullbackCone.ext _ _ _ _ _ _ _ (PullbackCone.mk _ _ _) _ i w‚ÇÅ.symm w‚ÇÇ.symm).symm)

theorem of_horiz_isIso_mono [IsIso fst] [Mono g] (sq : CommSq fst snd f g) :
    IsPullback fst snd f g :=
  of_isLimit' sq
    (by
      refine
        PullbackCone.IsLimit.mk _ (fun s => s.fst ‚â´ inv fst) (by simp)
          (fun s => ?_) (by cat_disch)
      simp only [‚Üê cancel_mono g, Category.assoc, ‚Üê sq.w, IsIso.inv_hom_id_assoc, s.condition])

theorem of_horiz_isIso [IsIso fst] [IsIso g] (sq : CommSq fst snd f g) :
    IsPullback fst snd f g :=
  of_horiz_isIso_mono sq

lemma of_iso (h : IsPullback fst snd f g)
    {P' X' Y' Z' : C} {fst' : P' ‚ü∂ X'} {snd' : P' ‚ü∂ Y'} {f' : X' ‚ü∂ Z'} {g' : Y' ‚ü∂ Z'}
    (e‚ÇÅ : P ‚âÖ P') (e‚ÇÇ : X ‚âÖ X') (e‚ÇÉ : Y ‚âÖ Y') (e‚ÇÑ : Z ‚âÖ Z')
    (commfst : fst ‚â´ e‚ÇÇ.hom = e‚ÇÅ.hom ‚â´ fst')
    (commsnd : snd ‚â´ e‚ÇÉ.hom = e‚ÇÅ.hom ‚â´ snd')
    (commf : f ‚â´ e‚ÇÑ.hom = e‚ÇÇ.hom ‚â´ f')
    (commg : g ‚â´ e‚ÇÑ.hom = e‚ÇÉ.hom ‚â´ g') :
    IsPullback fst' snd' f' g' where
  w := by
    rw [‚Üê cancel_epi e‚ÇÅ.hom, ‚Üê reassoc_of% commfst, ‚Üê commf,
      ‚Üê reassoc_of% commsnd, ‚Üê commg, h.w_assoc]
  isLimit' :=
    ‚ü®(IsLimit.postcomposeInvEquiv
        (cospanExt e‚ÇÇ e‚ÇÉ e‚ÇÑ commf.symm commg.symm) _).1
          (IsLimit.ofIsoLimit h.isLimit (by
            refine PullbackCone.ext e‚ÇÅ ?_ ?_
            ¬∑ change fst = e‚ÇÅ.hom ‚â´ fst' ‚â´ e‚ÇÇ.inv
              rw [‚Üê reassoc_of% commfst, e‚ÇÇ.hom_inv_id, Category.comp_id]
            ¬∑ change snd = e‚ÇÅ.hom ‚â´ snd' ‚â´ e‚ÇÉ.inv
              rw [‚Üê reassoc_of% commsnd, e‚ÇÉ.hom_inv_id, Category.comp_id]))‚ü©
section

variable {P X Y : C} {fst : P ‚ü∂ X} {snd : P ‚ü∂ X} {f : X ‚ü∂ Y} [Mono f]

lemma isIso_fst_of_mono (h : IsPullback fst snd f f) : IsIso fst :=
  h.cone.isIso_fst_of_mono_of_isLimit h.isLimit

lemma isIso_snd_iso_of_mono {P X Y : C} {fst : P ‚ü∂ X} {snd : P ‚ü∂ X} {f : X ‚ü∂ Y} [Mono f]
    (h : IsPullback fst snd f f) : IsIso snd :=
  h.cone.isIso_snd_of_mono_of_isLimit h.isLimit

end

section

lemma isIso_fst_of_isIso (h : IsPullback fst snd f g) [IsIso g] : IsIso fst := by
  have := h.hasPullback
  rw [‚Üê h.isoPullback_hom_fst]
  infer_instance

lemma isIso_snd_of_isIso (h : IsPullback fst snd f g) [IsIso f] : IsIso snd := by
  have := h.hasPullback
  rw [‚Üê h.isoPullback_hom_snd]
  infer_instance

end

end IsPullback

namespace IsPushout

variable {Z X Y P : C} {f : Z ‚ü∂ X} {g : Z ‚ü∂ Y} {inl : X ‚ü∂ P} {inr : Y ‚ü∂ P}

/-- The (colimiting) `PushoutCocone f g` implicit in the statement
that we have an `IsPushout f g inl inr`.
-/
def cocone (h : IsPushout f g inl inr) : PushoutCocone f g :=
  h.toCommSq.cocone

@[simp]
theorem cocone_inl (h : IsPushout f g inl inr) : h.cocone.inl = inl :=
  rfl

@[simp]
theorem cocone_inr (h : IsPushout f g inl inr) : h.cocone.inr = inr :=
  rfl

/-- The cocone obtained from `IsPushout f g inl inr` is a colimit cocone.
-/
noncomputable def isColimit (h : IsPushout f g inl inr) : IsColimit h.cocone :=
  h.isColimit'.some

/-- API for PushoutCocone.IsColimit.lift for `IsPushout` -/
noncomputable def desc (hP : IsPushout f g inl inr) {W : C} (h : X ‚ü∂ W) (k : Y ‚ü∂ W)
    (w : f ‚â´ h = g ‚â´ k) : P ‚ü∂ W :=
  PushoutCocone.IsColimit.desc hP.isColimit h k w

@[reassoc (attr := simp)]
lemma inl_desc (hP : IsPushout f g inl inr) {W : C} (h : X ‚ü∂ W) (k : Y ‚ü∂ W)
    (w : f ‚â´ h = g ‚â´ k) : inl ‚â´ hP.desc h k w = h :=
  PushoutCocone.IsColimit.inl_desc hP.isColimit h k w

@[reassoc (attr := simp)]
lemma inr_desc (hP : IsPushout f g inl inr) {W : C} (h : X ‚ü∂ W) (k : Y ‚ü∂ W)
    (w : f ‚â´ h = g ‚â´ k) : inr ‚â´ hP.desc h k w = k :=
  PushoutCocone.IsColimit.inr_desc hP.isColimit h k w

lemma hom_ext (hP : IsPushout f g inl inr) {W : C} {k l : P ‚ü∂ W}
    (h‚ÇÄ : inl ‚â´ k = inl ‚â´ l) (h‚ÇÅ : inr ‚â´ k = inr ‚â´ l) : k = l :=
  PushoutCocone.IsColimit.hom_ext hP.isColimit h‚ÇÄ h‚ÇÅ

/-- If `c` is a colimiting pushout cocone, then we have an `IsPushout f g c.inl c.inr`. -/
theorem of_isColimit {c : PushoutCocone f g} (h : Limits.IsColimit c) : IsPushout f g c.inl c.inr :=
  { w := c.condition
    isColimit' :=
      ‚ü®IsColimit.ofIsoColimit h (Limits.PushoutCocone.ext (Iso.refl _)
        (by simp) (by simp))‚ü© }

/-- A variant of `of_isColimit` that is more useful with `apply`. -/
theorem of_isColimit' (w : CommSq f g inl inr) (h : Limits.IsColimit w.cocone) :
    IsPushout f g inl inr :=
  of_isColimit h

/-- Variant of `of_isColimit` for an arbitrary cocone on a diagram `WalkingSpan ‚•§ C`. -/
lemma of_isColimit_cocone {D : WalkingSpan ‚•§ C} {c : Cocone D} (hc : IsColimit c) :
    IsPushout (D.map WalkingSpan.Hom.fst) (D.map WalkingSpan.Hom.snd)
      (c.Œπ.app .left) (c.Œπ.app .right) where
  w := by simp_rw [Cocone.w]
  isColimit' := ‚ü®IsColimit.equivOfNatIsoOfIso _ _ _ (PushoutCocone.isoMk c) hc‚ü©

lemma hasPushout (h : IsPushout f g inl inr) : HasPushout f g where
  exists_colimit := ‚ü®‚ü®h.cocone, h.isColimit‚ü©‚ü©

/-- The pushout provided by `HasPushout f g` fits into an `IsPushout`. -/
theorem of_hasPushout (f : Z ‚ü∂ X) (g : Z ‚ü∂ Y) [HasPushout f g] :
    IsPushout f g (pushout.inl f g) (pushout.inr f g) :=
  of_isColimit (colimit.isColimit (span f g))

/-- If `c` is a colimiting binary coproduct cocone, and we have an initial object,
then we have `IsPushout 0 0 c.inl c.inr`
(where each `0` is the unique morphism from the initial object). -/
theorem of_is_coproduct {c : BinaryCofan X Y} (h : Limits.IsColimit c) (t : IsInitial Z) :
    IsPushout (t.to _) (t.to _) c.inl c.inr :=
  of_isColimit
    (isPushoutOfIsInitialIsCoproduct _ _ _ _ t
      (IsColimit.ofIsoColimit h
        (Limits.Cocones.ext (Iso.refl c.pt)
          (by
            rintro ‚ü®‚ü®‚ü©‚ü© <;> simp))))

/-- A variant of `of_is_coproduct` that is more useful with `apply`. -/
theorem of_is_coproduct' (h : Limits.IsColimit (BinaryCofan.mk inl inr)) (t : IsInitial Z) :
    IsPushout (t.to _) (t.to _) inl inr :=
  of_is_coproduct h t

variable (X Y)

theorem of_hasBinaryCoproduct' [HasBinaryCoproduct X Y] [HasInitial C] :
    IsPushout (initial.to _) (initial.to _) (coprod.inl : X ‚ü∂ _) (coprod.inr : Y ‚ü∂ _) :=
  of_is_coproduct (colimit.isColimit _) initialIsInitial

open ZeroObject

theorem of_hasBinaryCoproduct [HasBinaryCoproduct X Y] [HasZeroObject C] [HasZeroMorphisms C] :
    IsPushout (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) coprod.inl coprod.inr := by
  convert @of_is_coproduct _ _ 0 X Y _ (colimit.isColimit _) HasZeroObject.zeroIsInitial
    <;> subsingleton

section

variable {P' : C} {inl' : X ‚ü∂ P'} {inr' : Y ‚ü∂ P'}

/-- Any object at the bottom right of a pushout square is isomorphic to the object at the bottom
right of any other pushout square with the same span. -/
noncomputable def isoIsPushout (h : IsPushout f g inl inr) (h' : IsPushout f g inl' inr') :
    P ‚âÖ P' :=
  IsColimit.coconePointUniqueUpToIso h.isColimit h'.isColimit

@[reassoc (attr := simp)]
theorem inl_isoIsPushout_hom (h : IsPushout f g inl inr) (h' : IsPushout f g inl' inr') :
    inl ‚â´ (h.isoIsPushout _ _ h').hom = inl' :=
  IsColimit.comp_coconePointUniqueUpToIso_hom h.isColimit h'.isColimit WalkingSpan.left

@[reassoc (attr := simp)]
theorem inr_isoIsPushout_hom (h : IsPushout f g inl inr) (h' : IsPushout f g inl' inr') :
    inr ‚â´ (h.isoIsPushout _ _ h').hom = inr' :=
  IsColimit.comp_coconePointUniqueUpToIso_hom h.isColimit h'.isColimit WalkingSpan.right

@[reassoc (attr := simp)]
theorem inl_isoIsPushout_inv (h : IsPushout f g inl inr) (h' : IsPushout f g inl' inr') :
    inl' ‚â´ (h.isoIsPushout _ _ h').inv = inl := by
  simp only [Iso.comp_inv_eq, inl_isoIsPushout_hom]

@[reassoc (attr := simp)]
theorem inr_isoIsPushout_inv (h : IsPushout f g inl inr) (h' : IsPushout f g inl' inr') :
    inr' ‚â´ (h.isoIsPushout _ _ h').inv = inr := by
  simp only [Iso.comp_inv_eq, inr_isoIsPushout_hom]

end


variable {X Y}

/-- Any object at the top left of a pullback square is
isomorphic to the pullback provided by the `HasLimit` API. -/
noncomputable def isoPushout (h : IsPushout f g inl inr) [HasPushout f g] : P ‚âÖ pushout f g :=
  (colimit.isoColimitCocone ‚ü®_, h.isColimit‚ü©).symm

@[reassoc (attr := simp)]
theorem inl_isoPushout_inv (h : IsPushout f g inl inr) [HasPushout f g] :
    pushout.inl _ _ ‚â´ h.isoPushout.inv = inl := by
  dsimp [isoPushout, cocone, CommSq.cocone]
  simp

@[reassoc (attr := simp)]
theorem inr_isoPushout_inv (h : IsPushout f g inl inr) [HasPushout f g] :
    pushout.inr _ _ ‚â´ h.isoPushout.inv = inr := by
  dsimp [isoPushout, cocone, CommSq.cocone]
  simp

@[reassoc (attr := simp)]
theorem inl_isoPushout_hom (h : IsPushout f g inl inr) [HasPushout f g] :
    inl ‚â´ h.isoPushout.hom = pushout.inl _ _ := by simp [‚Üê Iso.eq_comp_inv]

@[reassoc (attr := simp)]
theorem inr_isoPushout_hom (h : IsPushout f g inl inr) [HasPushout f g] :
    inr ‚â´ h.isoPushout.hom = pushout.inr _ _ := by simp [‚Üê Iso.eq_comp_inv]

theorem of_iso_pushout (h : CommSq f g inl inr) [HasPushout f g] (i : P ‚âÖ pushout f g)
    (w‚ÇÅ : inl ‚â´ i.hom = pushout.inl _ _) (w‚ÇÇ : inr ‚â´ i.hom = pushout.inr _ _) :
      IsPushout f g inl inr :=
  of_isColimit' h
    (Limits.IsColimit.ofIsoColimit (colimit.isColimit _)
      (PushoutCocone.ext (s := PushoutCocone.mk ..) i w‚ÇÅ w‚ÇÇ).symm)

lemma of_iso (h : IsPushout f g inl inr)
    {Z' X' Y' P' : C} {f' : Z' ‚ü∂ X'} {g' : Z' ‚ü∂ Y'} {inl' : X' ‚ü∂ P'} {inr' : Y' ‚ü∂ P'}
    (e‚ÇÅ : Z ‚âÖ Z') (e‚ÇÇ : X ‚âÖ X') (e‚ÇÉ : Y ‚âÖ Y') (e‚ÇÑ : P ‚âÖ P')
    (commf : f ‚â´ e‚ÇÇ.hom = e‚ÇÅ.hom ‚â´ f')
    (commg : g ‚â´ e‚ÇÉ.hom = e‚ÇÅ.hom ‚â´ g')
    (comminl : inl ‚â´ e‚ÇÑ.hom = e‚ÇÇ.hom ‚â´ inl')
    (comminr : inr ‚â´ e‚ÇÑ.hom = e‚ÇÉ.hom ‚â´ inr') :
    IsPushout f' g' inl' inr' where
  w := by
    rw [‚Üê cancel_epi e‚ÇÅ.hom, ‚Üê reassoc_of% commf, ‚Üê comminl,
      ‚Üê reassoc_of% commg, ‚Üê comminr, h.w_assoc]
  isColimit' :=
    ‚ü®(IsColimit.precomposeHomEquiv
        (spanExt e‚ÇÅ e‚ÇÇ e‚ÇÉ commf.symm commg.symm) _).1
          (IsColimit.ofIsoColimit h.isColimit
            (PushoutCocone.ext e‚ÇÑ comminl comminr))‚ü©

section

variable {P X Y : C} {inl : X ‚ü∂ P} {inr : X ‚ü∂ P} {f : Y ‚ü∂ X} [Epi f]

lemma isIso_inl_iso_of_epi (h : IsPushout f f inl inr) : IsIso inl :=
  h.cocone.isIso_inl_of_epi_of_isColimit h.isColimit

lemma isIso_inr_iso_of_epi (h : IsPushout f f inl inr) : IsIso inr :=
  h.cocone.isIso_inr_of_epi_of_isColimit h.isColimit

end

section

lemma isIso_inl_of_isIso (h : IsPushout f g inl inr) [IsIso g] : IsIso inl := by
  have := h.hasPushout
  rw [‚Üê h.inl_isoPushout_inv]
  infer_instance

lemma isIso_inr_of_isIso (h : IsPushout f g inl inr) [IsIso f] : IsIso inr := by
  have := h.hasPushout
  rw [‚Üê h.inr_isoPushout_inv]
  infer_instance

end

end IsPushout

namespace IsPullback

variable {P X Y Z : C} {fst : P ‚ü∂ X} {snd : P ‚ü∂ Y} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z}

theorem flip (h : IsPullback fst snd f g) : IsPullback snd fst g f :=
  of_isLimit (PullbackCone.flipIsLimit h.isLimit)

theorem flip_iff : IsPullback fst snd f g ‚Üî IsPullback snd fst g f :=
  ‚ü®flip, flip‚ü©

section

variable [HasZeroObject C] [HasZeroMorphisms C]

open ZeroObject

/-- The square with `0 : 0 ‚ü∂ 0` on the left and `ùüô X` on the right is a pullback square. -/
@[simp]
theorem zero_left (X : C) : IsPullback (0 : 0 ‚ü∂ X) (0 : (0 : C) ‚ü∂ 0) (ùüô X) (0 : 0 ‚ü∂ X) :=
  { w := by simp
    isLimit' :=
      ‚ü®{  lift := fun _ => 0
          fac := fun s => by
            simpa [eq_iff_true_of_subsingleton] using
              @PullbackCone.equalizer_ext _ _ _ _ _ _ _ s _ 0 (ùüô _)
                (by simpa using (PullbackCone.condition s).symm) }‚ü© }

/-- The square with `0 : 0 ‚ü∂ 0` on the top and `ùüô X` on the bottom is a pullback square. -/
@[simp]
theorem zero_top (X : C) : IsPullback (0 : (0 : C) ‚ü∂ 0) (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ X) (ùüô X) :=
  (zero_left X).flip

/-- The square with `0 : 0 ‚ü∂ 0` on the right and `ùüô X` on the left is a pullback square. -/
@[simp]
theorem zero_right (X : C) : IsPullback (0 : X ‚ü∂ 0) (ùüô X) (0 : (0 : C) ‚ü∂ 0) (0 : X ‚ü∂ 0) :=
  of_iso_pullback (by simp) ((zeroProdIso X).symm ‚â™‚â´ (pullbackZeroZeroIso _ _).symm)
    (by simp [eq_iff_true_of_subsingleton]) (by simp)

/-- The square with `0 : 0 ‚ü∂ 0` on the bottom and `ùüô X` on the top is a pullback square. -/
@[simp]
theorem zero_bot (X : C) : IsPullback (ùüô X) (0 : X ‚ü∂ 0) (0 : X ‚ü∂ 0) (0 : (0 : C) ‚ü∂ 0) :=
  (zero_right X).flip

end

-- Objects here are arranged in a 3x2 grid, and indexed by their xy coordinates.
-- Morphisms are named `h·µ¢‚±º` for a horizontal morphism starting at `(i,j)`,
-- and `v·µ¢‚±º` for a vertical morphism starting at `(i,j)`.
/-- Paste two pullback squares "vertically" to obtain another pullback square.

The objects in the statement fit into the following diagram:
```
X‚ÇÅ‚ÇÅ - h‚ÇÅ‚ÇÅ -> X‚ÇÅ‚ÇÇ
|            |
v‚ÇÅ‚ÇÅ          v‚ÇÅ‚ÇÇ
‚Üì            ‚Üì
X‚ÇÇ‚ÇÅ - h‚ÇÇ‚ÇÅ -> X‚ÇÇ‚ÇÇ
|            |
v‚ÇÇ‚ÇÅ          v‚ÇÇ‚ÇÇ
‚Üì            ‚Üì
X‚ÇÉ‚ÇÅ - h‚ÇÉ‚ÇÅ -> X‚ÇÉ‚ÇÇ
```
-/
theorem paste_vert {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÉ‚ÇÅ X‚ÇÉ‚ÇÇ : C} {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ}
    {h‚ÇÉ‚ÇÅ : X‚ÇÉ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÇ} {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÅ} {v‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÉ‚ÇÇ}
    (s : IsPullback h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) (t : IsPullback h‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÇ h‚ÇÉ‚ÇÅ) :
    IsPullback h‚ÇÅ‚ÇÅ (v‚ÇÅ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÅ) (v‚ÇÅ‚ÇÇ ‚â´ v‚ÇÇ‚ÇÇ) h‚ÇÉ‚ÇÅ :=
  of_isLimit (pasteHorizIsPullback rfl t.isLimit s.isLimit)

/-- Paste two pullback squares "horizontally" to obtain another pullback square.

The objects in the statement fit into the following diagram:
```
X‚ÇÅ‚ÇÅ - h‚ÇÅ‚ÇÅ -> X‚ÇÅ‚ÇÇ - h‚ÇÅ‚ÇÇ -> X‚ÇÅ‚ÇÉ
|            |            |
v‚ÇÅ‚ÇÅ          v‚ÇÅ‚ÇÇ          v‚ÇÅ‚ÇÉ
‚Üì            ‚Üì            ‚Üì
X‚ÇÇ‚ÇÅ - h‚ÇÇ‚ÇÅ -> X‚ÇÇ‚ÇÇ - h‚ÇÇ‚ÇÇ -> X‚ÇÇ‚ÇÉ
```
-/
theorem paste_horiz {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÅ‚ÇÉ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÇ‚ÇÉ : C} {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÅ‚ÇÉ}
    {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÉ} {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÉ ‚ü∂ X‚ÇÇ‚ÇÉ}
    (s : IsPullback h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) (t : IsPullback h‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÇ) :
    IsPullback (h‚ÇÅ‚ÇÅ ‚â´ h‚ÇÅ‚ÇÇ) v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÉ (h‚ÇÇ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÇ) :=
  (paste_vert s.flip t.flip).flip

/-- Given a pullback square assembled from a commuting square on the top and
a pullback square on the bottom, the top square is a pullback square.

The objects in the statement fit into the following diagram:
```
X‚ÇÅ‚ÇÅ - h‚ÇÅ‚ÇÅ -> X‚ÇÅ‚ÇÇ
|            |
v‚ÇÅ‚ÇÅ          v‚ÇÅ‚ÇÇ
‚Üì            ‚Üì
X‚ÇÇ‚ÇÅ - h‚ÇÇ‚ÇÅ -> X‚ÇÇ‚ÇÇ
|            |
v‚ÇÇ‚ÇÅ          v‚ÇÇ‚ÇÇ
‚Üì            ‚Üì
X‚ÇÉ‚ÇÅ - h‚ÇÉ‚ÇÅ -> X‚ÇÉ‚ÇÇ
```
-/
theorem of_bot {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÉ‚ÇÅ X‚ÇÉ‚ÇÇ : C} {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÉ‚ÇÅ : X‚ÇÉ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÇ}
    {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÅ} {v‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÉ‚ÇÇ}
    (s : IsPullback h‚ÇÅ‚ÇÅ (v‚ÇÅ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÅ) (v‚ÇÅ‚ÇÇ ‚â´ v‚ÇÇ‚ÇÇ) h‚ÇÉ‚ÇÅ) (p : h‚ÇÅ‚ÇÅ ‚â´ v‚ÇÅ‚ÇÇ = v‚ÇÅ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÅ)
    (t : IsPullback h‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÇ h‚ÇÉ‚ÇÅ) : IsPullback h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ :=
  of_isLimit (leftSquareIsPullback (PullbackCone.mk h‚ÇÅ‚ÇÅ _ p) rfl t.isLimit s.isLimit)

/-- Given a pullback square assembled from a commuting square on the left and
a pullback square on the right, the left square is a pullback square.

The objects in the statement fit into the following diagram:
```
X‚ÇÅ‚ÇÅ - h‚ÇÅ‚ÇÅ -> X‚ÇÅ‚ÇÇ - h‚ÇÅ‚ÇÇ -> X‚ÇÅ‚ÇÉ
|            |            |
v‚ÇÅ‚ÇÅ          v‚ÇÅ‚ÇÇ          v‚ÇÅ‚ÇÉ
‚Üì            ‚Üì            ‚Üì
X‚ÇÇ‚ÇÅ - h‚ÇÇ‚ÇÅ -> X‚ÇÇ‚ÇÇ - h‚ÇÇ‚ÇÇ -> X‚ÇÇ‚ÇÉ
```
-/
theorem of_right {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÅ‚ÇÉ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÇ‚ÇÉ : C} {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÅ‚ÇÉ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ}
    {h‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÉ} {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÉ ‚ü∂ X‚ÇÇ‚ÇÉ}
    (s : IsPullback (h‚ÇÅ‚ÇÅ ‚â´ h‚ÇÅ‚ÇÇ) v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÉ (h‚ÇÇ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÇ)) (p : h‚ÇÅ‚ÇÅ ‚â´ v‚ÇÅ‚ÇÇ = v‚ÇÅ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÅ)
    (t : IsPullback h‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÇ) : IsPullback h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ :=
  (of_bot s.flip p.symm t.flip).flip

theorem paste_vert_iff {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÉ‚ÇÅ X‚ÇÉ‚ÇÇ : C} {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ}
    {h‚ÇÉ‚ÇÅ : X‚ÇÉ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÇ} {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÅ} {v‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÉ‚ÇÇ}
    (s : IsPullback h‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÇ h‚ÇÉ‚ÇÅ) (e : h‚ÇÅ‚ÇÅ ‚â´ v‚ÇÅ‚ÇÇ = v‚ÇÅ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÅ) :
    IsPullback h‚ÇÅ‚ÇÅ (v‚ÇÅ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÅ) (v‚ÇÅ‚ÇÇ ‚â´ v‚ÇÇ‚ÇÇ) h‚ÇÉ‚ÇÅ ‚Üî IsPullback h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ :=
  ‚ü®fun h => h.of_bot e s, fun h => h.paste_vert s‚ü©

theorem paste_horiz_iff {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÅ‚ÇÉ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÇ‚ÇÉ : C} {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÅ‚ÇÉ}
    {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÉ} {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÉ ‚ü∂ X‚ÇÇ‚ÇÉ}
    (s : IsPullback h‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÇ) (e : h‚ÇÅ‚ÇÅ ‚â´ v‚ÇÅ‚ÇÇ = v‚ÇÅ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÅ) :
    IsPullback (h‚ÇÅ‚ÇÅ ‚â´ h‚ÇÅ‚ÇÇ) v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÉ (h‚ÇÇ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÇ) ‚Üî IsPullback h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ :=
  ‚ü®fun h => h.of_right e s, fun h => h.paste_horiz s‚ü©

/-- Variant of `IsPullback.of_right` where `h‚ÇÅ‚ÇÅ` is induced from a morphism `h‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÉ`, and
the universal property of the right square.

The objects fit in the following diagram:
```
X‚ÇÅ‚ÇÅ - h‚ÇÅ‚ÇÅ -> X‚ÇÅ‚ÇÇ - h‚ÇÅ‚ÇÇ -> X‚ÇÅ‚ÇÉ
|            |            |
v‚ÇÅ‚ÇÅ          v‚ÇÅ‚ÇÇ          v‚ÇÅ‚ÇÉ
‚Üì            ‚Üì            ‚Üì
X‚ÇÇ‚ÇÅ - h‚ÇÇ‚ÇÅ -> X‚ÇÇ‚ÇÇ - h‚ÇÇ‚ÇÇ -> X‚ÇÇ‚ÇÉ
```
-/
theorem of_right' {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÅ‚ÇÉ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÇ‚ÇÉ : C} {h‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÅ‚ÇÉ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ}
    {h‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÉ} {h‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÉ} {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÉ ‚ü∂ X‚ÇÇ‚ÇÉ}
    (s : IsPullback h‚ÇÅ‚ÇÉ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÉ (h‚ÇÇ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÇ)) (t : IsPullback h‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÇ) :
    IsPullback (t.lift h‚ÇÅ‚ÇÉ (v‚ÇÅ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÅ) (by rw [s.w, Category.assoc])) v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ :=
  of_right ((t.lift_fst _ _ _) ‚ñ∏ s) (t.lift_snd _ _ _) t

/-- Variant of `IsPullback.of_bot`, where `v‚ÇÅ‚ÇÅ` is induced from a morphism `v‚ÇÉ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÅ`, and
the universal property of the bottom square.

The objects in the statement fit into the following diagram:
```
X‚ÇÅ‚ÇÅ - h‚ÇÅ‚ÇÅ -> X‚ÇÅ‚ÇÇ
|            |
v‚ÇÅ‚ÇÅ          v‚ÇÅ‚ÇÇ
‚Üì            ‚Üì
X‚ÇÇ‚ÇÅ - h‚ÇÇ‚ÇÅ -> X‚ÇÇ‚ÇÇ
|            |
v‚ÇÇ‚ÇÅ          v‚ÇÇ‚ÇÇ
‚Üì            ‚Üì
X‚ÇÉ‚ÇÅ - h‚ÇÉ‚ÇÅ -> X‚ÇÉ‚ÇÇ
```
-/
theorem of_bot' {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÉ‚ÇÅ X‚ÇÉ‚ÇÇ : C} {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ}
    {h‚ÇÉ‚ÇÅ : X‚ÇÉ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÇ} {v‚ÇÉ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÅ} {v‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÉ‚ÇÇ}
    (s : IsPullback h‚ÇÅ‚ÇÅ v‚ÇÉ‚ÇÅ (v‚ÇÅ‚ÇÇ ‚â´ v‚ÇÇ‚ÇÇ) h‚ÇÉ‚ÇÅ) (t : IsPullback h‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÇ h‚ÇÉ‚ÇÅ) :
    IsPullback h‚ÇÅ‚ÇÅ (t.lift (h‚ÇÅ‚ÇÅ ‚â´ v‚ÇÅ‚ÇÇ) v‚ÇÉ‚ÇÅ (by rw [Category.assoc, s.w])) v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ :=
  of_bot ((t.lift_snd _ _ _) ‚ñ∏ s) (by simp only [lift_fst]) t

section

variable [HasZeroObject C] [HasZeroMorphisms C]

open ZeroObject

theorem of_isBilimit {b : BinaryBicone X Y} (h : b.IsBilimit) :
    IsPullback b.fst b.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) := by
  convert IsPullback.of_is_product' h.isLimit HasZeroObject.zeroIsTerminal
    <;> subsingleton

@[simp]
theorem of_has_biproduct (X Y : C) [HasBinaryBiproduct X Y] :
    IsPullback biprod.fst biprod.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) :=
  of_isBilimit (BinaryBiproduct.isBilimit X Y)

theorem inl_snd' {b : BinaryBicone X Y} (h : b.IsBilimit) :
    IsPullback b.inl (0 : X ‚ü∂ 0) b.snd (0 : 0 ‚ü∂ Y) := by
  refine of_right ?_ (by simp) (of_isBilimit h)
  simp

/-- The square
```
  X --inl--> X ‚äû Y
  |            |
  0           snd
  |            |
  v            v
  0 ---0-----> Y
```
is a pullback square.
-/
@[simp]
theorem inl_snd (X Y : C) [HasBinaryBiproduct X Y] :
    IsPullback biprod.inl (0 : X ‚ü∂ 0) biprod.snd (0 : 0 ‚ü∂ Y) :=
  inl_snd' (BinaryBiproduct.isBilimit X Y)

theorem inr_fst' {b : BinaryBicone X Y} (h : b.IsBilimit) :
    IsPullback b.inr (0 : Y ‚ü∂ 0) b.fst (0 : 0 ‚ü∂ X) := by
  apply flip
  refine of_bot ?_ (by simp) (of_isBilimit h)
  simp

/-- The square
```
  Y --inr--> X ‚äû Y
  |            |
  0           fst
  |            |
  v            v
  0 ---0-----> X
```
is a pullback square.
-/
@[simp]
theorem inr_fst (X Y : C) [HasBinaryBiproduct X Y] :
    IsPullback biprod.inr (0 : Y ‚ü∂ 0) biprod.fst (0 : 0 ‚ü∂ X) :=
  inr_fst' (BinaryBiproduct.isBilimit X Y)

theorem of_is_bilimit' {b : BinaryBicone X Y} (h : b.IsBilimit) :
    IsPullback (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) b.inl b.inr := by
  refine IsPullback.of_right ?_ (by simp) (IsPullback.inl_snd' h).flip
  simp

theorem of_hasBinaryBiproduct (X Y : C) [HasBinaryBiproduct X Y] :
    IsPullback (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) biprod.inl biprod.inr :=
  of_is_bilimit' (BinaryBiproduct.isBilimit X Y)

instance hasPullback_biprod_fst_biprod_snd [HasBinaryBiproduct X Y] :
    HasPullback (biprod.inl : X ‚ü∂ _) (biprod.inr : Y ‚ü∂ _) :=
  HasLimit.mk ‚ü®_, (of_hasBinaryBiproduct X Y).isLimit‚ü©

/-- The pullback of `biprod.inl` and `biprod.inr` is the zero object. -/
def pullbackBiprodInlBiprodInr [HasBinaryBiproduct X Y] :
    pullback (biprod.inl : X ‚ü∂ _) (biprod.inr : Y ‚ü∂ _) ‚âÖ 0 :=
  limit.isoLimitCone ‚ü®_, (of_hasBinaryBiproduct X Y).isLimit‚ü©

end

theorem op (h : IsPullback fst snd f g) : IsPushout g.op f.op snd.op fst.op :=
  IsPushout.of_isColimit
    (IsColimit.ofIsoColimit (Limits.PullbackCone.isLimitEquivIsColimitOp h.flip.cone h.flip.isLimit)
      h.toCommSq.flip.coneOp)

theorem unop {P X Y Z : C·µí·µñ} {fst : P ‚ü∂ X} {snd : P ‚ü∂ Y} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z}
    (h : IsPullback fst snd f g) : IsPushout g.unop f.unop snd.unop fst.unop :=
  IsPushout.of_isColimit
    (IsColimit.ofIsoColimit
      (Limits.PullbackCone.isLimitEquivIsColimitUnop h.flip.cone h.flip.isLimit)
      h.toCommSq.flip.coneUnop)

theorem of_vert_isIso_mono [IsIso snd] [Mono f] (sq : CommSq fst snd f g) :
    IsPullback fst snd f g :=
  IsPullback.flip (of_horiz_isIso_mono sq.flip)

theorem of_vert_isIso [IsIso snd] [IsIso f] (sq : CommSq fst snd f g) :
    IsPullback fst snd f g :=
  of_vert_isIso_mono sq

lemma of_id_fst : IsPullback (ùüô _) f f (ùüô _) := IsPullback.of_horiz_isIso ‚ü®by simp‚ü©

lemma of_id_snd : IsPullback f (ùüô _) (ùüô _) f := IsPullback.of_vert_isIso ‚ü®by simp‚ü©

/-- The following diagram is a pullback
```
X --f--> Z
|        |
id       id
v        v
X --f--> Z
```
-/
lemma id_vert (f : X ‚ü∂ Z) : IsPullback f (ùüô X) (ùüô Z) f :=
  of_vert_isIso ‚ü®by simp only [Category.id_comp, Category.comp_id]‚ü©

/-- The following diagram is a pullback
```
X --id--> X
|         |
f         f
v         v
Z --id--> Z
```
-/
lemma id_horiz (f : X ‚ü∂ Z) : IsPullback (ùüô X) f f (ùüô Z) :=
  of_horiz_isIso ‚ü®by simp only [Category.id_comp, Category.comp_id]‚ü©

/--
In a category, given a morphism `f : A ‚ü∂ B` and an object `X`,
this is the obvious pullback diagram:
```
A ‚®Ø X ‚ü∂ A
  |     |
  v     v
B ‚®Ø X ‚ü∂ B
```
-/
lemma of_prod_fst_with_id {A B : C} (f : A ‚ü∂ B) (X : C) [HasBinaryProduct A X]
    [HasBinaryProduct B X] :
    IsPullback prod.fst (prod.map f (ùüô X)) f prod.fst where
  isLimit' := ‚ü®PullbackCone.isLimitAux' _ (fun s ‚Ü¶ by
    refine ‚ü®prod.lift s.fst (s.snd ‚â´ prod.snd), ?_, ?_, ?_‚ü©
    ¬∑ simp
    ¬∑ ext
      ¬∑ simp [PullbackCone.condition]
      ¬∑ simp
    ¬∑ intro m h‚ÇÅ h‚ÇÇ
      dsimp at m h‚ÇÅ h‚ÇÇ ‚ä¢
      ext
      ¬∑ simpa using h‚ÇÅ
      ¬∑ simp [‚Üê h‚ÇÇ])‚ü©

lemma of_isLimit_binaryFan_of_isTerminal
    {X Y : C} {c : BinaryFan X Y} (hc : IsLimit c)
    {T : C} (hT : IsTerminal T) :
    IsPullback c.fst c.snd (hT.from _) (hT.from _) where
  isLimit' := ‚ü®PullbackCone.IsLimit.mk _
    (fun s ‚Ü¶ hc.lift (BinaryFan.mk s.fst s.snd))
    (fun s ‚Ü¶ hc.fac (BinaryFan.mk s.fst s.snd) ‚ü®.left‚ü©)
    (fun s ‚Ü¶ hc.fac (BinaryFan.mk s.fst s.snd) ‚ü®.right‚ü©)
    (fun s m h‚ÇÅ h‚ÇÇ ‚Ü¶ by
      apply BinaryFan.IsLimit.hom_ext hc
      ¬∑ rw [h‚ÇÅ, hc.fac (BinaryFan.mk s.fst s.snd) ‚ü®.left‚ü©]
        rfl
      ¬∑ rw [h‚ÇÇ, hc.fac (BinaryFan.mk s.fst s.snd) ‚ü®.right‚ü©]
        rfl)‚ü©

end IsPullback

namespace IsPushout

variable {Z X Y P : C} {f : Z ‚ü∂ X} {g : Z ‚ü∂ Y} {inl : X ‚ü∂ P} {inr : Y ‚ü∂ P}

theorem flip (h : IsPushout f g inl inr) : IsPushout g f inr inl :=
  of_isColimit (PushoutCocone.flipIsColimit h.isColimit)

theorem flip_iff : IsPushout f g inl inr ‚Üî IsPushout g f inr inl :=
  ‚ü®flip, flip‚ü©

section

variable [HasZeroObject C] [HasZeroMorphisms C]

open ZeroObject

/-- The square with `0 : 0 ‚ü∂ 0` on the right and `ùüô X` on the left is a pushout square. -/
@[simp]
theorem zero_right (X : C) : IsPushout (0 : X ‚ü∂ 0) (ùüô X) (0 : (0 : C) ‚ü∂ 0) (0 : X ‚ü∂ 0) :=
  { w := by simp
    isColimit' :=
      ‚ü®{  desc := fun _ => 0
          fac := fun s => by
            have c :=
              @PushoutCocone.coequalizer_ext _ _ _ _ _ _ _ s _ 0 (ùüô _)
                (by simp [eq_iff_true_of_subsingleton]) (by simpa using PushoutCocone.condition s)
            dsimp at c
            simpa using c }‚ü© }

/-- The square with `0 : 0 ‚ü∂ 0` on the bottom and `ùüô X` on the top is a pushout square. -/
@[simp]
theorem zero_bot (X : C) : IsPushout (ùüô X) (0 : X ‚ü∂ 0) (0 : X ‚ü∂ 0) (0 : (0 : C) ‚ü∂ 0) :=
  (zero_right X).flip

/-- The square with `0 : 0 ‚ü∂ 0` on the right left `ùüô X` on the right is a pushout square. -/
@[simp]
theorem zero_left (X : C) : IsPushout (0 : 0 ‚ü∂ X) (0 : (0 : C) ‚ü∂ 0) (ùüô X) (0 : 0 ‚ü∂ X) :=
  of_iso_pushout (by simp) ((coprodZeroIso X).symm ‚â™‚â´ (pushoutZeroZeroIso _ _).symm) (by simp)
    (by simp [eq_iff_true_of_subsingleton])

/-- The square with `0 : 0 ‚ü∂ 0` on the top and `ùüô X` on the bottom is a pushout square. -/
@[simp]
theorem zero_top (X : C) : IsPushout (0 : (0 : C) ‚ü∂ 0) (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ X) (ùüô X) :=
  (zero_left X).flip

end

-- Objects here are arranged in a 3x2 grid, and indexed by their xy coordinates.
-- Morphisms are named `h·µ¢‚±º` for a horizontal morphism starting at `(i,j)`,
-- and `v·µ¢‚±º` for a vertical morphism starting at `(i,j)`.
/-- Paste two pushout squares "vertically" to obtain another pushout square.

The objects in the statement fit into the following diagram:
```
X‚ÇÅ‚ÇÅ - h‚ÇÅ‚ÇÅ -> X‚ÇÅ‚ÇÇ
|            |
v‚ÇÅ‚ÇÅ          v‚ÇÅ‚ÇÇ
‚Üì            ‚Üì
X‚ÇÇ‚ÇÅ - h‚ÇÇ‚ÇÅ -> X‚ÇÇ‚ÇÇ
|            |
v‚ÇÇ‚ÇÅ          v‚ÇÇ‚ÇÇ
‚Üì            ‚Üì
X‚ÇÉ‚ÇÅ - h‚ÇÉ‚ÇÅ -> X‚ÇÉ‚ÇÇ
```
-/
theorem paste_vert {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÉ‚ÇÅ X‚ÇÉ‚ÇÇ : C} {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ}
    {h‚ÇÉ‚ÇÅ : X‚ÇÉ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÇ} {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÅ} {v‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÉ‚ÇÇ}
    (s : IsPushout h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) (t : IsPushout h‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÇ h‚ÇÉ‚ÇÅ) :
    IsPushout h‚ÇÅ‚ÇÅ (v‚ÇÅ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÅ) (v‚ÇÅ‚ÇÇ ‚â´ v‚ÇÇ‚ÇÇ) h‚ÇÉ‚ÇÅ :=
  of_isColimit (pasteHorizIsPushout rfl s.isColimit t.isColimit)

/-- Paste two pushout squares "horizontally" to obtain another pushout square.

The objects in the statement fit into the following diagram:
```
X‚ÇÅ‚ÇÅ - h‚ÇÅ‚ÇÅ -> X‚ÇÅ‚ÇÇ - h‚ÇÅ‚ÇÇ -> X‚ÇÅ‚ÇÉ
|            |            |
v‚ÇÅ‚ÇÅ          v‚ÇÅ‚ÇÇ          v‚ÇÅ‚ÇÉ
‚Üì            ‚Üì            ‚Üì
X‚ÇÇ‚ÇÅ - h‚ÇÇ‚ÇÅ -> X‚ÇÇ‚ÇÇ - h‚ÇÇ‚ÇÇ -> X‚ÇÇ‚ÇÉ
```
-/
theorem paste_horiz {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÅ‚ÇÉ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÇ‚ÇÉ : C} {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÅ‚ÇÉ}
    {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÉ} {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÉ ‚ü∂ X‚ÇÇ‚ÇÉ}
    (s : IsPushout h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) (t : IsPushout h‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÇ) :
    IsPushout (h‚ÇÅ‚ÇÅ ‚â´ h‚ÇÅ‚ÇÇ) v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÉ (h‚ÇÇ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÇ) :=
  (paste_vert s.flip t.flip).flip

/-- Given a pushout square assembled from a pushout square on the top and
a commuting square on the bottom, the bottom square is a pushout square.

The objects in the statement fit into the following diagram:
```
X‚ÇÅ‚ÇÅ - h‚ÇÅ‚ÇÅ -> X‚ÇÅ‚ÇÇ
|            |
v‚ÇÅ‚ÇÅ          v‚ÇÅ‚ÇÇ
‚Üì            ‚Üì
X‚ÇÇ‚ÇÅ - h‚ÇÇ‚ÇÅ -> X‚ÇÇ‚ÇÇ
|            |
v‚ÇÇ‚ÇÅ          v‚ÇÇ‚ÇÇ
‚Üì            ‚Üì
X‚ÇÉ‚ÇÅ - h‚ÇÉ‚ÇÅ -> X‚ÇÉ‚ÇÇ
```
-/
theorem of_top {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÉ‚ÇÅ X‚ÇÉ‚ÇÇ : C} {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÉ‚ÇÅ : X‚ÇÉ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÇ}
    {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÅ} {v‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÉ‚ÇÇ}
    (s : IsPushout h‚ÇÅ‚ÇÅ (v‚ÇÅ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÅ) (v‚ÇÅ‚ÇÇ ‚â´ v‚ÇÇ‚ÇÇ) h‚ÇÉ‚ÇÅ) (p : h‚ÇÇ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÇ = v‚ÇÇ‚ÇÅ ‚â´ h‚ÇÉ‚ÇÅ)
    (t : IsPushout h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) : IsPushout h‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÇ h‚ÇÉ‚ÇÅ :=
  of_isColimit <| rightSquareIsPushout
    (PushoutCocone.mk _ _ p) (cocone_inr _) t.isColimit s.isColimit

/-- Given a pushout square assembled from a pushout square on the left and
a commuting square on the right, the right square is a pushout square.

The objects in the statement fit into the following diagram:
```
X‚ÇÅ‚ÇÅ - h‚ÇÅ‚ÇÅ -> X‚ÇÅ‚ÇÇ - h‚ÇÅ‚ÇÇ -> X‚ÇÅ‚ÇÉ
|            |            |
v‚ÇÅ‚ÇÅ          v‚ÇÅ‚ÇÇ          v‚ÇÅ‚ÇÉ
‚Üì            ‚Üì            ‚Üì
X‚ÇÇ‚ÇÅ - h‚ÇÇ‚ÇÅ -> X‚ÇÇ‚ÇÇ - h‚ÇÇ‚ÇÇ -> X‚ÇÇ‚ÇÉ
```
-/
theorem of_left {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÅ‚ÇÉ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÇ‚ÇÉ : C} {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÅ‚ÇÉ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ}
    {h‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÉ} {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÉ ‚ü∂ X‚ÇÇ‚ÇÉ}
    (s : IsPushout (h‚ÇÅ‚ÇÅ ‚â´ h‚ÇÅ‚ÇÇ) v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÉ (h‚ÇÇ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÇ)) (p : h‚ÇÅ‚ÇÇ ‚â´ v‚ÇÅ‚ÇÉ = v‚ÇÅ‚ÇÇ ‚â´ h‚ÇÇ‚ÇÇ)
    (t : IsPushout h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) : IsPushout h‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÇ :=
  (of_top s.flip p.symm t.flip).flip

theorem paste_vert_iff {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÉ‚ÇÅ X‚ÇÉ‚ÇÇ : C} {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ}
    {h‚ÇÉ‚ÇÅ : X‚ÇÉ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÇ} {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÅ} {v‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÉ‚ÇÇ}
    (s : IsPushout h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) (e : h‚ÇÇ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÇ = v‚ÇÇ‚ÇÅ ‚â´ h‚ÇÉ‚ÇÅ) :
    IsPushout h‚ÇÅ‚ÇÅ (v‚ÇÅ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÅ) (v‚ÇÅ‚ÇÇ ‚â´ v‚ÇÇ‚ÇÇ) h‚ÇÉ‚ÇÅ ‚Üî IsPushout h‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÇ h‚ÇÉ‚ÇÅ :=
  ‚ü®fun h => h.of_top e s, s.paste_vert‚ü©

theorem paste_horiz_iff {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÅ‚ÇÉ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÇ‚ÇÉ : C} {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÅ‚ÇÉ}
    {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÉ} {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÉ ‚ü∂ X‚ÇÇ‚ÇÉ}
    (s : IsPushout h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) (e : h‚ÇÅ‚ÇÇ ‚â´ v‚ÇÅ‚ÇÉ = v‚ÇÅ‚ÇÇ ‚â´ h‚ÇÇ‚ÇÇ) :
    IsPushout (h‚ÇÅ‚ÇÅ ‚â´ h‚ÇÅ‚ÇÇ) v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÉ (h‚ÇÇ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÇ) ‚Üî IsPushout h‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÇ :=
  ‚ü®fun h => h.of_left e s, s.paste_horiz‚ü©

/-- Variant of `IsPushout.of_top` where `v‚ÇÇ‚ÇÇ` is induced from a morphism `v‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÉ‚ÇÇ`, and
the universal property of the top square.

The objects in the statement fit into the following diagram:
```
X‚ÇÅ‚ÇÅ - h‚ÇÅ‚ÇÅ -> X‚ÇÅ‚ÇÇ
|            |
v‚ÇÅ‚ÇÅ          v‚ÇÅ‚ÇÇ
‚Üì            ‚Üì
X‚ÇÇ‚ÇÅ - h‚ÇÇ‚ÇÅ -> X‚ÇÇ‚ÇÇ
|            |
v‚ÇÇ‚ÇÅ          v‚ÇÇ‚ÇÇ
‚Üì            ‚Üì
X‚ÇÉ‚ÇÅ - h‚ÇÉ‚ÇÅ -> X‚ÇÉ‚ÇÇ
```
-/
theorem of_top' {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÉ‚ÇÅ X‚ÇÉ‚ÇÇ : C} {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÉ‚ÇÅ : X‚ÇÉ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÇ}
    {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÉ‚ÇÇ} {v‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÅ}
    (s : IsPushout h‚ÇÅ‚ÇÅ (v‚ÇÅ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÅ) v‚ÇÅ‚ÇÉ h‚ÇÉ‚ÇÅ) (t : IsPushout h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) :
      IsPushout h‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÅ (t.desc v‚ÇÅ‚ÇÉ (v‚ÇÇ‚ÇÅ ‚â´ h‚ÇÉ‚ÇÅ) (by rw [s.w, Category.assoc])) h‚ÇÉ‚ÇÅ :=
  of_top ((t.inl_desc _ _ _).symm ‚ñ∏ s) (t.inr_desc _ _ _) t

/-- Variant of `IsPushout.of_right` where `h‚ÇÇ‚ÇÇ` is induced from a morphism `h‚ÇÇ‚ÇÉ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÉ`, and
the universal property of the left square.

The objects in the statement fit into the following diagram:
```
X‚ÇÅ‚ÇÅ - h‚ÇÅ‚ÇÅ -> X‚ÇÅ‚ÇÇ - h‚ÇÅ‚ÇÇ -> X‚ÇÅ‚ÇÉ
|            |            |
v‚ÇÅ‚ÇÅ          v‚ÇÅ‚ÇÇ          v‚ÇÅ‚ÇÉ
‚Üì            ‚Üì            ‚Üì
X‚ÇÇ‚ÇÅ - h‚ÇÇ‚ÇÅ -> X‚ÇÇ‚ÇÇ - h‚ÇÇ‚ÇÇ -> X‚ÇÇ‚ÇÉ
```
-/
theorem of_left' {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÅ‚ÇÉ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÇ‚ÇÉ : C} {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÅ‚ÇÉ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ}
    {h‚ÇÇ‚ÇÉ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÉ} {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÉ ‚ü∂ X‚ÇÇ‚ÇÉ}
    (s : IsPushout (h‚ÇÅ‚ÇÅ ‚â´ h‚ÇÅ‚ÇÇ) v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÉ) (t : IsPushout h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) :
    IsPushout h‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÉ (t.desc (h‚ÇÅ‚ÇÇ ‚â´ v‚ÇÅ‚ÇÉ) h‚ÇÇ‚ÇÉ (by rw [‚Üê Category.assoc, s.w])) :=
  of_left ((t.inr_desc _ _ _).symm ‚ñ∏ s) (by simp only [inl_desc]) t

section

variable [HasZeroObject C] [HasZeroMorphisms C]

open ZeroObject

theorem of_isBilimit {b : BinaryBicone X Y} (h : b.IsBilimit) :
    IsPushout (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) b.inl b.inr := by
  convert IsPushout.of_is_coproduct' h.isColimit HasZeroObject.zeroIsInitial
    <;> subsingleton

@[simp]
theorem of_has_biproduct (X Y : C) [HasBinaryBiproduct X Y] :
    IsPushout (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) biprod.inl biprod.inr :=
  of_isBilimit (BinaryBiproduct.isBilimit X Y)

theorem inl_snd' {b : BinaryBicone X Y} (h : b.IsBilimit) :
    IsPushout b.inl (0 : X ‚ü∂ 0) b.snd (0 : 0 ‚ü∂ Y) := by
  apply flip
  refine of_left ?_ (by simp) (of_isBilimit h)
  simp

/-- The square
```
  X --inl--> X ‚äû Y
  |            |
  0           snd
  |            |
  v            v
  0 ---0-----> Y
```
is a pushout square.
-/
theorem inl_snd (X Y : C) [HasBinaryBiproduct X Y] :
    IsPushout biprod.inl (0 : X ‚ü∂ 0) biprod.snd (0 : 0 ‚ü∂ Y) :=
  inl_snd' (BinaryBiproduct.isBilimit X Y)

theorem inr_fst' {b : BinaryBicone X Y} (h : b.IsBilimit) :
    IsPushout b.inr (0 : Y ‚ü∂ 0) b.fst (0 : 0 ‚ü∂ X) := by
  refine of_top ?_ (by simp) (of_isBilimit h)
  simp

/-- The square
```
  Y --inr--> X ‚äû Y
  |            |
  0           fst
  |            |
  v            v
  0 ---0-----> X
```
is a pushout square.
-/
theorem inr_fst (X Y : C) [HasBinaryBiproduct X Y] :
    IsPushout biprod.inr (0 : Y ‚ü∂ 0) biprod.fst (0 : 0 ‚ü∂ X) :=
  inr_fst' (BinaryBiproduct.isBilimit X Y)

theorem of_is_bilimit' {b : BinaryBicone X Y} (h : b.IsBilimit) :
    IsPushout b.fst b.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) := by
  refine IsPushout.of_left ?_ (by simp) (IsPushout.inl_snd' h)
  simp

theorem of_hasBinaryBiproduct (X Y : C) [HasBinaryBiproduct X Y] :
    IsPushout biprod.fst biprod.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) :=
  of_is_bilimit' (BinaryBiproduct.isBilimit X Y)

instance hasPushout_biprod_fst_biprod_snd [HasBinaryBiproduct X Y] :
    HasPushout (biprod.fst : _ ‚ü∂ X) (biprod.snd : _ ‚ü∂ Y) :=
  HasColimit.mk ‚ü®_, (of_hasBinaryBiproduct X Y).isColimit‚ü©

/-- The pushout of `biprod.fst` and `biprod.snd` is the zero object. -/
def pushoutBiprodFstBiprodSnd [HasBinaryBiproduct X Y] :
    pushout (biprod.fst : _ ‚ü∂ X) (biprod.snd : _ ‚ü∂ Y) ‚âÖ 0 :=
  colimit.isoColimitCocone ‚ü®_, (of_hasBinaryBiproduct X Y).isColimit‚ü©

end

theorem op (h : IsPushout f g inl inr) : IsPullback inr.op inl.op g.op f.op :=
  IsPullback.of_isLimit
    (IsLimit.ofIsoLimit
      (Limits.PushoutCocone.isColimitEquivIsLimitOp h.flip.cocone h.flip.isColimit)
      h.toCommSq.flip.coconeOp)

theorem unop {Z X Y P : C·µí·µñ} {f : Z ‚ü∂ X} {g : Z ‚ü∂ Y} {inl : X ‚ü∂ P} {inr : Y ‚ü∂ P}
    (h : IsPushout f g inl inr) : IsPullback inr.unop inl.unop g.unop f.unop :=
  IsPullback.of_isLimit
    (IsLimit.ofIsoLimit
      (Limits.PushoutCocone.isColimitEquivIsLimitUnop h.flip.cocone h.flip.isColimit)
      h.toCommSq.flip.coconeUnop)

theorem of_horiz_isIso_epi [Epi f] [IsIso inr] (sq : CommSq f g inl inr) : IsPushout f g inl inr :=
  of_isColimit' sq
    (by
      refine
        PushoutCocone.IsColimit.mk _ (fun s => inv inr ‚â´ s.inr) (fun s => ?_)
          (by simp) (by simp)
      simp only [‚Üê cancel_epi f, s.condition, sq.w_assoc, IsIso.hom_inv_id_assoc])

theorem of_horiz_isIso [IsIso f] [IsIso inr] (sq : CommSq f g inl inr) : IsPushout f g inl inr :=
  of_horiz_isIso_epi sq

theorem of_vert_isIso_epi [Epi g] [IsIso inl] (sq : CommSq f g inl inr) : IsPushout f g inl inr :=
  (of_horiz_isIso_epi sq.flip).flip

theorem of_vert_isIso [IsIso g] [IsIso inl] (sq : CommSq f g inl inr) : IsPushout f g inl inr :=
  of_vert_isIso_epi sq

lemma of_id_fst : IsPushout (ùüô _) f f (ùüô _) := IsPushout.of_horiz_isIso ‚ü®by simp‚ü©

lemma of_id_snd : IsPushout f (ùüô _) (ùüô _) f := IsPushout.of_vert_isIso ‚ü®by simp‚ü©

/-- The following diagram is a pullback
```
X --f--> Z
|        |
id       id
v        v
X --f--> Z
```
-/
lemma id_vert (f : X ‚ü∂ Z) : IsPushout f (ùüô X) (ùüô Z) f :=
  of_vert_isIso ‚ü®by simp only [Category.id_comp, Category.comp_id]‚ü©

/-- The following diagram is a pullback
```
X --id--> X
|         |
f         f
v         v
Z --id--> Z
```
-/
lemma id_horiz (f : X ‚ü∂ Z) : IsPushout (ùüô X) f f (ùüô Z) :=
  of_horiz_isIso ‚ü®by simp only [Category.id_comp, Category.comp_id]‚ü©

/--
In a category, given a morphism `f : A ‚ü∂ B` and an object `X`,
this is the obvious pushout diagram:
```
A ‚ü∂ A ‚®ø X
|     |
v     v
B ‚ü∂ B ‚®ø X
```
-/
lemma of_coprod_inl_with_id {A B : C} (f : A ‚ü∂ B) (X : C) [HasBinaryCoproduct A X]
    [HasBinaryCoproduct B X] :
    IsPushout coprod.inl f (coprod.map f (ùüô X)) coprod.inl where
  w := by simp
  isColimit' := ‚ü®PushoutCocone.isColimitAux' _ (fun s ‚Ü¶ by
    refine ‚ü®coprod.desc s.inr (coprod.inr ‚â´ s.inl), ?_, ?_, ?_‚ü©
    ¬∑ ext
      ¬∑ simp [PushoutCocone.condition]
      ¬∑ simp
    ¬∑ simp
    ¬∑ intro m h‚ÇÅ h‚ÇÇ
      dsimp at m h‚ÇÅ h‚ÇÇ ‚ä¢
      ext
      ¬∑ simpa using h‚ÇÇ
      ¬∑ simp [‚Üê h‚ÇÅ])‚ü©

lemma of_isColimit_binaryCofan_of_isInitial
    {X Y : C} {c : BinaryCofan X Y} (hc : IsColimit c)
    {I : C} (hI : IsInitial I) :
    IsPushout (hI.to _) (hI.to _) c.inr c.inl where
  w := hI.hom_ext _ _
  isColimit' := ‚ü®PushoutCocone.IsColimit.mk _
    (fun s ‚Ü¶ hc.desc (BinaryCofan.mk s.inr s.inl))
    (fun s ‚Ü¶ hc.fac (BinaryCofan.mk s.inr s.inl) ‚ü®.right‚ü©)
    (fun s ‚Ü¶ hc.fac (BinaryCofan.mk s.inr s.inl) ‚ü®.left‚ü©)
    (fun s m h‚ÇÅ h‚ÇÇ ‚Ü¶ by
      apply BinaryCofan.IsColimit.hom_ext hc
      ¬∑ rw [h‚ÇÇ, hc.fac (BinaryCofan.mk s.inr s.inl) ‚ü®.left‚ü©]
        rfl
      ¬∑ rw [h‚ÇÅ, hc.fac (BinaryCofan.mk s.inr s.inl) ‚ü®.right‚ü©]
        rfl)‚ü©

end IsPushout

section Equalizer

variable {X Y Z : C} {f f' : X ‚ü∂ Y} {g g' : Y ‚ü∂ Z}

/-- If `f : X ‚ü∂ Y`, `g g' : Y ‚ü∂ Z` forms a pullback square, then `f` is the equalizer of
`g` and `g'`. -/
noncomputable def IsPullback.isLimitFork (H : IsPullback f f g g') : IsLimit (Fork.ofŒπ f H.w) := by
  fapply Fork.IsLimit.mk
  ¬∑ exact fun s => H.isLimit.lift (PullbackCone.mk s.Œπ s.Œπ s.condition)
  ¬∑ exact fun s => H.isLimit.fac _ WalkingCospan.left
  ¬∑ intro s m e
    apply PullbackCone.IsLimit.hom_ext H.isLimit <;> refine e.trans ?_ <;> symm <;>
      exact H.isLimit.fac _ _

/-- If `f f' : X ‚ü∂ Y`, `g : Y ‚ü∂ Z` forms a pushout square, then `g` is the coequalizer of
`f` and `f'`. -/
noncomputable def IsPushout.isLimitFork (H : IsPushout f f' g g) :
    IsColimit (Cofork.ofœÄ g H.w) := by
  fapply Cofork.IsColimit.mk
  ¬∑ exact fun s => H.isColimit.desc (PushoutCocone.mk s.œÄ s.œÄ s.condition)
  ¬∑ exact fun s => H.isColimit.fac _ WalkingSpan.left
  ¬∑ intro s m e
    apply PushoutCocone.IsColimit.hom_ext H.isColimit <;> refine e.trans ?_ <;> symm <;>
      exact H.isColimit.fac _ _

end Equalizer

namespace BicartesianSq

variable {W X Y Z : C} {f : W ‚ü∂ X} {g : W ‚ü∂ Y} {h : X ‚ü∂ Z} {i : Y ‚ü∂ Z}

theorem of_isPullback_isPushout (p‚ÇÅ : IsPullback f g h i) (p‚ÇÇ : IsPushout f g h i) :
    BicartesianSq f g h i :=
  BicartesianSq.mk p‚ÇÅ p‚ÇÇ.isColimit'

theorem flip (p : BicartesianSq f g h i) : BicartesianSq g f i h :=
  of_isPullback_isPushout p.toIsPullback.flip p.toIsPushout.flip

variable [HasZeroObject C] [HasZeroMorphisms C]

open ZeroObject

/-- ```
 X ‚äû Y --fst--> X
   |            |
  snd           0
   |            |
   v            v
   Y -----0---> 0
```
is a bicartesian square.
-/
theorem of_is_biproduct‚ÇÅ {b : BinaryBicone X Y} (h : b.IsBilimit) :
    BicartesianSq b.fst b.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) :=
  of_isPullback_isPushout (IsPullback.of_isBilimit h) (IsPushout.of_is_bilimit' h)

/-- ```
   0 -----0---> X
   |            |
   0           inl
   |            |
   v            v
   Y --inr--> X ‚äû Y
```
is a bicartesian square.
-/
theorem of_is_biproduct‚ÇÇ {b : BinaryBicone X Y} (h : b.IsBilimit) :
    BicartesianSq (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) b.inl b.inr :=
  of_isPullback_isPushout (IsPullback.of_is_bilimit' h) (IsPushout.of_isBilimit h)

/-- ```
 X ‚äû Y --fst--> X
   |            |
  snd           0
   |            |
   v            v
   Y -----0---> 0
```
is a bicartesian square.
-/
@[simp]
theorem of_has_biproduct‚ÇÅ [HasBinaryBiproduct X Y] :
    BicartesianSq biprod.fst biprod.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) := by
  convert of_is_biproduct‚ÇÅ (BinaryBiproduct.isBilimit X Y)

/-- ```
   0 -----0---> X
   |            |
   0           inl
   |            |
   v            v
   Y --inr--> X ‚äû Y
```
is a bicartesian square.
-/
@[simp]
theorem of_has_biproduct‚ÇÇ [HasBinaryBiproduct X Y] :
    BicartesianSq (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) biprod.inl biprod.inr := by
  convert of_is_biproduct‚ÇÇ (BinaryBiproduct.isBilimit X Y)

end BicartesianSq

section Functor

variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
variable (F : C ‚•§ D) {W X Y Z : C} {f : W ‚ü∂ X} {g : W ‚ü∂ Y} {h : X ‚ü∂ Z} {i : Y ‚ü∂ Z}

theorem Functor.map_isPullback [PreservesLimit (cospan h i) F] (s : IsPullback f g h i) :
    IsPullback (F.map f) (F.map g) (F.map h) (F.map i) := by
  -- This is made slightly awkward because `C` and `D` have different universes,
  -- and so the relevant `WalkingCospan` diagrams live in different universes too!
  refine
    IsPullback.of_isLimit' (F.map_commSq s.toCommSq)
      (IsLimit.equivOfNatIsoOfIso (cospanCompIso F h i) _ _ (WalkingCospan.ext ?_ ?_ ?_)
        (isLimitOfPreserves F s.isLimit))
  ¬∑ rfl
  ¬∑ simp
  ¬∑ simp

theorem Functor.map_isPushout [PreservesColimit (span f g) F] (s : IsPushout f g h i) :
    IsPushout (F.map f) (F.map g) (F.map h) (F.map i) := by
  refine
    IsPushout.of_isColimit' (F.map_commSq s.toCommSq)
      (IsColimit.equivOfNatIsoOfIso (spanCompIso F f g) _ _ (WalkingSpan.ext ?_ ?_ ?_)
        (isColimitOfPreserves F s.isColimit))
  ¬∑ rfl
  ¬∑ simp
  ¬∑ simp

alias IsPullback.map := Functor.map_isPullback

alias IsPushout.map := Functor.map_isPushout

theorem IsPullback.of_map [ReflectsLimit (cospan h i) F] (e : f ‚â´ h = g ‚â´ i)
    (H : IsPullback (F.map f) (F.map g) (F.map h) (F.map i)) : IsPullback f g h i := by
  refine ‚ü®‚ü®e‚ü©, ‚ü®isLimitOfReflects F <| ?_‚ü©‚ü©
  refine
    (IsLimit.equivOfNatIsoOfIso (cospanCompIso F h i) _ _ (WalkingCospan.ext ?_ ?_ ?_)).symm
      H.isLimit
  exacts [Iso.refl _, (Category.comp_id _).trans (Category.id_comp _).symm,
    (Category.comp_id _).trans (Category.id_comp _).symm]

theorem IsPullback.of_map_of_faithful [ReflectsLimit (cospan h i) F] [F.Faithful]
    (H : IsPullback (F.map f) (F.map g) (F.map h) (F.map i)) : IsPullback f g h i :=
  H.of_map F (F.map_injective <| by simpa only [F.map_comp] using H.w)

theorem IsPullback.map_iff {D : Type*} [Category D] (F : C ‚•§ D) [PreservesLimit (cospan h i) F]
    [ReflectsLimit (cospan h i) F] (e : f ‚â´ h = g ‚â´ i) :
    IsPullback (F.map f) (F.map g) (F.map h) (F.map i) ‚Üî IsPullback f g h i :=
  ‚ü®fun h => h.of_map F e, fun h => h.map F‚ü©

theorem IsPushout.of_map [ReflectsColimit (span f g) F] (e : f ‚â´ h = g ‚â´ i)
    (H : IsPushout (F.map f) (F.map g) (F.map h) (F.map i)) : IsPushout f g h i := by
  refine ‚ü®‚ü®e‚ü©, ‚ü®isColimitOfReflects F <| ?_‚ü©‚ü©
  refine
    (IsColimit.equivOfNatIsoOfIso (spanCompIso F f g) _ _ (WalkingSpan.ext ?_ ?_ ?_)).symm
      H.isColimit
  exacts [Iso.refl _, (Category.comp_id _).trans (Category.id_comp _),
    (Category.comp_id _).trans (Category.id_comp _)]

theorem IsPushout.of_map_of_faithful [ReflectsColimit (span f g) F] [F.Faithful]
    (H : IsPushout (F.map f) (F.map g) (F.map h) (F.map i)) : IsPushout f g h i :=
  H.of_map F (F.map_injective <| by simpa only [F.map_comp] using H.w)

theorem IsPushout.map_iff {D : Type*} [Category D] (F : C ‚•§ D) [PreservesColimit (span f g) F]
    [ReflectsColimit (span f g) F] (e : f ‚â´ h = g ‚â´ i) :
    IsPushout (F.map f) (F.map g) (F.map h) (F.map i) ‚Üî IsPushout f g h i :=
  ‚ü®fun h => h.of_map F e, fun h => h.map F‚ü©

end Functor

end CategoryTheory
