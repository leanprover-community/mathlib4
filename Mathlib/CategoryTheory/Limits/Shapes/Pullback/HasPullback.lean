/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Markus Himmel, Bhavik Mehta, Andrew Yang, Emily Riehl, Calle S√∂nne
-/
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.PullbackCone

#align_import category_theory.limits.shapes.pullbacks from "leanprover-community/mathlib"@"7316286ff2942aa14e540add9058c6b0aa1c8070"

/-!
# HasPullback
`HasPullback f g` and `pullback f g` provides API for `HasLimit` and `limit` in the case of
pullacks.

# Main definitions

* `HasPullback f g`: this is an abbreviation for `HasLimit (cospan f g)`, and is a typeclass used to
  express the fact that a given pair of morphisms has a pullback.

* `HasPullbacks`: expresses the fact that `C` admits all pullbacks, it is implemented as an
  abbrevation for `HasLimitsOfShape WalkingCospan C`

* `pullback f g`: Given a `HasPullback f g` instance, this function returns the choice of a limit
  object corresponding to the pullback of `f` and `g`. It fits into the following diagram:
```
  pullback f g ---pullback.snd f g---> Y
      |                                |
      |                                |
pullback.snd f g                       g
      |                                |
      v                                v
      X --------------f--------------> Z
```

* `HasPushout f g`: this is an abbreviation for `HasColimit (span f g)`, and is a typeclass used to
  express the fact that a given pair of morphisms has a pushout.
* `HasPushouts`: expresses the fact that `C` admits all pushouts, it is implemented as an
abbrevation for `HasColimitsOfShape WalkingSpan C`
* `pushout f g`: Given a `HasPushout f g` instance, this function returns the choice of a colimit
  object corresponding to the pushout of `f` and `g`. It fits into the following diagram:
```
      X --------------f--------------> Y
      |                                |
      g                          pushout.inr f g
      |                                |
      v                                v
      Z ---pushout.inl f g---> pushout f g
```

# Main results & API
* The following API is available for using the universal property of `pullback f g`:
`lift`, `lift_fst`, `lift_snd`, `lift'`, `hom_ext` (for uniqueness).

* `pullback.map` is the induced map between pullbacks `W √ó‚Çõ X ‚ü∂ Y √ó‚Çú Z` given pointwise
(compatible) maps `W ‚ü∂ Y`, `X ‚ü∂ Z` and `S ‚ü∂ T`.

* `pullbackComparison`: Given a functor `G`, this is the natural morphism
`G.obj (pullback f g) ‚ü∂ pullback (G.map f) (G.map g)`

* `pullbackSymmetry` provides the natural isomorphism `pullback f g ‚âÖ pullback g f`

(The dual results for pushouts are also available)

NOTE: golfed some proofs also

## References
* [Stacks: Fibre products](https://stacks.math.columbia.edu/tag/001U)
* [Stacks: Pushouts](https://stacks.math.columbia.edu/tag/0025)
-/

noncomputable section

open CategoryTheory

universe w v‚ÇÅ v‚ÇÇ v u u‚ÇÇ

namespace CategoryTheory.Limits

open WalkingSpan.Hom WalkingCospan.Hom WidePullbackShape.Hom WidePushoutShape.Hom

variable {C : Type u} [Category.{v} C] {W X Y Z : C}

/-- `HasPullback f g` represents a particular choice of limiting cone
for the pair of morphisms `f : X ‚ü∂ Z` and `g : Y ‚ü∂ Z`.
-/
abbrev HasPullback {X Y Z : C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :=
  HasLimit (cospan f g)
#align category_theory.limits.has_pullback CategoryTheory.Limits.HasPullback

/-- `HasPushout f g` represents a particular choice of colimiting cocone
for the pair of morphisms `f : X ‚ü∂ Y` and `g : X ‚ü∂ Z`.
-/
abbrev HasPushout {X Y Z : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) :=
  HasColimit (span f g)
#align category_theory.limits.has_pushout CategoryTheory.Limits.HasPushout

/-- `pullback f g` computes the pullback of a pair of morphisms with the same target. -/
abbrev pullback {X Y Z : C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [HasPullback f g] :=
  limit (cospan f g)
#align category_theory.limits.pullback CategoryTheory.Limits.pullback

/-- `pushout f g` computes the pushout of a pair of morphisms with the same source. -/
abbrev pushout {X Y Z : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) [HasPushout f g] :=
  colimit (span f g)
#align category_theory.limits.pushout CategoryTheory.Limits.pushout

/-- The first projection of the pullback of `f` and `g`. -/
abbrev pullback.fst {X Y Z : C} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z} [HasPullback f g] : pullback f g ‚ü∂ X :=
  limit.œÄ (cospan f g) WalkingCospan.left
#align category_theory.limits.pullback.fst CategoryTheory.Limits.pullback.fst

/-- The second projection of the pullback of `f` and `g`. -/
abbrev pullback.snd {X Y Z : C} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z} [HasPullback f g] : pullback f g ‚ü∂ Y :=
  limit.œÄ (cospan f g) WalkingCospan.right
#align category_theory.limits.pullback.snd CategoryTheory.Limits.pullback.snd

/-- The first inclusion into the pushout of `f` and `g`. -/
abbrev pushout.inl {X Y Z : C} {f : X ‚ü∂ Y} {g : X ‚ü∂ Z} [HasPushout f g] : Y ‚ü∂ pushout f g :=
  colimit.Œπ (span f g) WalkingSpan.left
#align category_theory.limits.pushout.inl CategoryTheory.Limits.pushout.inl

/-- The second inclusion into the pushout of `f` and `g`. -/
abbrev pushout.inr {X Y Z : C} {f : X ‚ü∂ Y} {g : X ‚ü∂ Z} [HasPushout f g] : Z ‚ü∂ pushout f g :=
  colimit.Œπ (span f g) WalkingSpan.right
#align category_theory.limits.pushout.inr CategoryTheory.Limits.pushout.inr

/-- A pair of morphisms `h : W ‚ü∂ X` and `k : W ‚ü∂ Y` satisfying `h ‚â´ f = k ‚â´ g` induces a morphism
    `pullback.lift : W ‚ü∂ pullback f g`. -/
abbrev pullback.lift {W X Y Z : C} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z} [HasPullback f g] (h : W ‚ü∂ X) (k : W ‚ü∂ Y)
    (w : h ‚â´ f = k ‚â´ g) : W ‚ü∂ pullback f g :=
  limit.lift _ (PullbackCone.mk h k w)
#align category_theory.limits.pullback.lift CategoryTheory.Limits.pullback.lift

/-- A pair of morphisms `h : Y ‚ü∂ W` and `k : Z ‚ü∂ W` satisfying `f ‚â´ h = g ‚â´ k` induces a morphism
    `pushout.desc : pushout f g ‚ü∂ W`. -/
abbrev pushout.desc {W X Y Z : C} {f : X ‚ü∂ Y} {g : X ‚ü∂ Z} [HasPushout f g] (h : Y ‚ü∂ W) (k : Z ‚ü∂ W)
    (w : f ‚â´ h = g ‚â´ k) : pushout f g ‚ü∂ W :=
  colimit.desc _ (PushoutCocone.mk h k w)
#align category_theory.limits.pushout.desc CategoryTheory.Limits.pushout.desc

@[simp]
theorem PullbackCone.fst_limit_cone {X Y Z : C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [HasLimit (cospan f g)] :
    PullbackCone.fst (limit.cone (cospan f g)) = pullback.fst := rfl
#align category_theory.limits.pullback_cone.fst_colimit_cocone CategoryTheory.Limits.PullbackCone.fst_limit_cone

@[simp]
theorem PullbackCone.snd_limit_cone {X Y Z : C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [HasLimit (cospan f g)] :
    PullbackCone.snd (limit.cone (cospan f g)) = pullback.snd := rfl
#align category_theory.limits.pullback_cone.snd_colimit_cocone CategoryTheory.Limits.PullbackCone.snd_limit_cone

-- Porting note (#10618): simp can prove this; removed simp
theorem PushoutCocone.inl_colimit_cocone {X Y Z : C} (f : Z ‚ü∂ X) (g : Z ‚ü∂ Y)
    [HasColimit (span f g)] : PushoutCocone.inl (colimit.cocone (span f g)) = pushout.inl := rfl
#align category_theory.limits.pushout_cocone.inl_colimit_cocone CategoryTheory.Limits.PushoutCocone.inl_colimit_cocone

-- Porting note (#10618): simp can prove this; removed simp
theorem PushoutCocone.inr_colimit_cocone {X Y Z : C} (f : Z ‚ü∂ X) (g : Z ‚ü∂ Y)
    [HasColimit (span f g)] : PushoutCocone.inr (colimit.cocone (span f g)) = pushout.inr := rfl
#align category_theory.limits.pushout_cocone.inr_colimit_cocone CategoryTheory.Limits.PushoutCocone.inr_colimit_cocone

-- Porting note (#10618): simp can prove this and reassoced version; removed simp
@[reassoc]
theorem pullback.lift_fst {W X Y Z : C} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z} [HasPullback f g] (h : W ‚ü∂ X)
    (k : W ‚ü∂ Y) (w : h ‚â´ f = k ‚â´ g) : pullback.lift h k w ‚â´ pullback.fst = h :=
  limit.lift_œÄ _ _
#align category_theory.limits.pullback.lift_fst CategoryTheory.Limits.pullback.lift_fst

-- Porting note (#10618): simp can prove this and reassoced version; removed simp
@[reassoc]
theorem pullback.lift_snd {W X Y Z : C} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z} [HasPullback f g] (h : W ‚ü∂ X)
    (k : W ‚ü∂ Y) (w : h ‚â´ f = k ‚â´ g) : pullback.lift h k w ‚â´ pullback.snd = k :=
  limit.lift_œÄ _ _
#align category_theory.limits.pullback.lift_snd CategoryTheory.Limits.pullback.lift_snd

-- Porting note (#10618): simp can prove this and reassoced version; removed simp
@[reassoc]
theorem pushout.inl_desc {W X Y Z : C} {f : X ‚ü∂ Y} {g : X ‚ü∂ Z} [HasPushout f g] (h : Y ‚ü∂ W)
    (k : Z ‚ü∂ W) (w : f ‚â´ h = g ‚â´ k) : pushout.inl ‚â´ pushout.desc h k w = h :=
  colimit.Œπ_desc _ _
#align category_theory.limits.pushout.inl_desc CategoryTheory.Limits.pushout.inl_desc

-- Porting note (#10618): simp can prove this and reassoced version; removed simp
@[reassoc]
theorem pushout.inr_desc {W X Y Z : C} {f : X ‚ü∂ Y} {g : X ‚ü∂ Z} [HasPushout f g] (h : Y ‚ü∂ W)
    (k : Z ‚ü∂ W) (w : f ‚â´ h = g ‚â´ k) : pushout.inr ‚â´ pushout.desc h k w = k :=
  colimit.Œπ_desc _ _
#align category_theory.limits.pushout.inr_desc CategoryTheory.Limits.pushout.inr_desc

/-- A pair of morphisms `h : W ‚ü∂ X` and `k : W ‚ü∂ Y` satisfying `h ‚â´ f = k ‚â´ g` induces a morphism
    `l : W ‚ü∂ pullback f g` such that `l ‚â´ pullback.fst = h` and `l ‚â´ pullback.snd = k`. -/
def pullback.lift' {W X Y Z : C} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z} [HasPullback f g] (h : W ‚ü∂ X) (k : W ‚ü∂ Y)
    (w : h ‚â´ f = k ‚â´ g) : { l : W ‚ü∂ pullback f g // l ‚â´ pullback.fst = h ‚àß l ‚â´ pullback.snd = k } :=
  ‚ü®pullback.lift h k w, pullback.lift_fst _ _ _, pullback.lift_snd _ _ _‚ü©
#align category_theory.limits.pullback.lift' CategoryTheory.Limits.pullback.lift'

/-- A pair of morphisms `h : Y ‚ü∂ W` and `k : Z ‚ü∂ W` satisfying `f ‚â´ h = g ‚â´ k` induces a morphism
    `l : pushout f g ‚ü∂ W` such that `pushout.inl ‚â´ l = h` and `pushout.inr ‚â´ l = k`. -/
def pullback.desc' {W X Y Z : C} {f : X ‚ü∂ Y} {g : X ‚ü∂ Z} [HasPushout f g] (h : Y ‚ü∂ W) (k : Z ‚ü∂ W)
    (w : f ‚â´ h = g ‚â´ k) : { l : pushout f g ‚ü∂ W // pushout.inl ‚â´ l = h ‚àß pushout.inr ‚â´ l = k } :=
  ‚ü®pushout.desc h k w, pushout.inl_desc _ _ _, pushout.inr_desc _ _ _‚ü©
#align category_theory.limits.pullback.desc' CategoryTheory.Limits.pullback.desc'

@[reassoc]
theorem pullback.condition {X Y Z : C} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z} [HasPullback f g] :
    (pullback.fst : pullback f g ‚ü∂ X) ‚â´ f = pullback.snd ‚â´ g :=
  PullbackCone.condition _
#align category_theory.limits.pullback.condition CategoryTheory.Limits.pullback.condition

@[reassoc]
theorem pushout.condition {X Y Z : C} {f : X ‚ü∂ Y} {g : X ‚ü∂ Z} [HasPushout f g] :
    f ‚â´ (pushout.inl : Y ‚ü∂ pushout f g) = g ‚â´ pushout.inr :=
  PushoutCocone.condition _
#align category_theory.limits.pushout.condition CategoryTheory.Limits.pushout.condition

/-- Given such a diagram, then there is a natural morphism `W √ó‚Çõ X ‚ü∂ Y √ó‚Çú Z`.

    W ‚ü∂ Y
      ‚Üò      ‚Üò
        S ‚ü∂ T
      ‚Üó      ‚Üó
    X ‚ü∂ Z

-/
abbrev pullback.map {W X Y Z S T : C} (f‚ÇÅ : W ‚ü∂ S) (f‚ÇÇ : X ‚ü∂ S) [HasPullback f‚ÇÅ f‚ÇÇ] (g‚ÇÅ : Y ‚ü∂ T)
    (g‚ÇÇ : Z ‚ü∂ T) [HasPullback g‚ÇÅ g‚ÇÇ] (i‚ÇÅ : W ‚ü∂ Y) (i‚ÇÇ : X ‚ü∂ Z) (i‚ÇÉ : S ‚ü∂ T)
    (eq‚ÇÅ : f‚ÇÅ ‚â´ i‚ÇÉ = i‚ÇÅ ‚â´ g‚ÇÅ) (eq‚ÇÇ : f‚ÇÇ ‚â´ i‚ÇÉ = i‚ÇÇ ‚â´ g‚ÇÇ) : pullback f‚ÇÅ f‚ÇÇ ‚ü∂ pullback g‚ÇÅ g‚ÇÇ :=
  pullback.lift (pullback.fst ‚â´ i‚ÇÅ) (pullback.snd ‚â´ i‚ÇÇ)
    (by simp [‚Üê eq‚ÇÅ, ‚Üê eq‚ÇÇ, pullback.condition_assoc])
#align category_theory.limits.pullback.map CategoryTheory.Limits.pullback.map

/-- The canonical map `X √ó‚Çõ Y ‚ü∂ X √ó‚Çú Y` given `S ‚ü∂ T`. -/
abbrev pullback.mapDesc {X Y S T : C} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) (i : S ‚ü∂ T) [HasPullback f g]
    [HasPullback (f ‚â´ i) (g ‚â´ i)] : pullback f g ‚ü∂ pullback (f ‚â´ i) (g ‚â´ i) :=
  pullback.map f g (f ‚â´ i) (g ‚â´ i) (ùüô _) (ùüô _) i (Category.id_comp _).symm (Category.id_comp _).symm
#align category_theory.limits.pullback.map_desc CategoryTheory.Limits.pullback.mapDesc

/-- Given such a diagram, then there is a natural morphism `W ‚®ø‚Çõ X ‚ü∂ Y ‚®ø‚Çú Z`.

        W ‚ü∂ Y
      ‚Üó      ‚Üó
    S ‚ü∂ T
      ‚Üò      ‚Üò
        X ‚ü∂ Z

-/
abbrev pushout.map {W X Y Z S T : C} (f‚ÇÅ : S ‚ü∂ W) (f‚ÇÇ : S ‚ü∂ X) [HasPushout f‚ÇÅ f‚ÇÇ] (g‚ÇÅ : T ‚ü∂ Y)
    (g‚ÇÇ : T ‚ü∂ Z) [HasPushout g‚ÇÅ g‚ÇÇ] (i‚ÇÅ : W ‚ü∂ Y) (i‚ÇÇ : X ‚ü∂ Z) (i‚ÇÉ : S ‚ü∂ T) (eq‚ÇÅ : f‚ÇÅ ‚â´ i‚ÇÅ = i‚ÇÉ ‚â´ g‚ÇÅ)
    (eq‚ÇÇ : f‚ÇÇ ‚â´ i‚ÇÇ = i‚ÇÉ ‚â´ g‚ÇÇ) : pushout f‚ÇÅ f‚ÇÇ ‚ü∂ pushout g‚ÇÅ g‚ÇÇ :=
  pushout.desc (i‚ÇÅ ‚â´ pushout.inl) (i‚ÇÇ ‚â´ pushout.inr)
    (by
      simp only [‚Üê Category.assoc, eq‚ÇÅ, eq‚ÇÇ]
      simp [pushout.condition])
#align category_theory.limits.pushout.map CategoryTheory.Limits.pushout.map

/-- The canonical map `X ‚®ø‚Çõ Y ‚ü∂ X ‚®ø‚Çú Y` given `S ‚ü∂ T`. -/
abbrev pushout.mapLift {X Y S T : C} (f : T ‚ü∂ X) (g : T ‚ü∂ Y) (i : S ‚ü∂ T) [HasPushout f g]
    [HasPushout (i ‚â´ f) (i ‚â´ g)] : pushout (i ‚â´ f) (i ‚â´ g) ‚ü∂ pushout f g :=
  pushout.map (i ‚â´ f) (i ‚â´ g) f g (ùüô _) (ùüô _) i (Category.comp_id _) (Category.comp_id _)
#align category_theory.limits.pushout.map_lift CategoryTheory.Limits.pushout.mapLift

/-- Two morphisms into a pullback are equal if their compositions with the pullback morphisms are
    equal -/
@[ext 1100]
theorem pullback.hom_ext {X Y Z : C} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z} [HasPullback f g] {W : C}
    {k l : W ‚ü∂ pullback f g} (h‚ÇÄ : k ‚â´ pullback.fst = l ‚â´ pullback.fst)
    (h‚ÇÅ : k ‚â´ pullback.snd = l ‚â´ pullback.snd) : k = l :=
  limit.hom_ext <| PullbackCone.equalizer_ext _ h‚ÇÄ h‚ÇÅ
#align category_theory.limits.pullback.hom_ext CategoryTheory.Limits.pullback.hom_ext

/-- The pullback cone built from the pullback projections is a pullback. -/
def pullbackIsPullback {X Y Z : C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [HasPullback f g] :
    IsLimit (PullbackCone.mk (pullback.fst : pullback f g ‚ü∂ _) pullback.snd pullback.condition) :=
  PullbackCone.IsLimit.mk _ (fun s => pullback.lift s.fst s.snd s.condition) (by simp) (by simp)
    (by aesop_cat)
#align category_theory.limits.pullback_is_pullback CategoryTheory.Limits.pullbackIsPullback

/-- Two morphisms out of a pushout are equal if their compositions with the pushout morphisms are
    equal -/
@[ext 1100]
theorem pushout.hom_ext {X Y Z : C} {f : X ‚ü∂ Y} {g : X ‚ü∂ Z} [HasPushout f g] {W : C}
    {k l : pushout f g ‚ü∂ W} (h‚ÇÄ : pushout.inl ‚â´ k = pushout.inl ‚â´ l)
    (h‚ÇÅ : pushout.inr ‚â´ k = pushout.inr ‚â´ l) : k = l :=
  colimit.hom_ext <| PushoutCocone.coequalizer_ext _ h‚ÇÄ h‚ÇÅ
#align category_theory.limits.pushout.hom_ext CategoryTheory.Limits.pushout.hom_ext

/-- The pushout cocone built from the pushout coprojections is a pushout. -/
def pushoutIsPushout {X Y Z : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) [HasPushout f g] :
    IsColimit (PushoutCocone.mk (pushout.inl : _ ‚ü∂ pushout f g) pushout.inr pushout.condition) :=
  PushoutCocone.IsColimit.mk _ (fun s => pushout.desc s.inl s.inr s.condition) (by simp) (by simp)
    (by aesop_cat)
#align category_theory.limits.pushout_is_pushout CategoryTheory.Limits.pushoutIsPushout

instance pullback.map_isIso {W X Y Z S T : C} (f‚ÇÅ : W ‚ü∂ S) (f‚ÇÇ : X ‚ü∂ S) [HasPullback f‚ÇÅ f‚ÇÇ]
    (g‚ÇÅ : Y ‚ü∂ T) (g‚ÇÇ : Z ‚ü∂ T) [HasPullback g‚ÇÅ g‚ÇÇ] (i‚ÇÅ : W ‚ü∂ Y) (i‚ÇÇ : X ‚ü∂ Z) (i‚ÇÉ : S ‚ü∂ T)
    (eq‚ÇÅ : f‚ÇÅ ‚â´ i‚ÇÉ = i‚ÇÅ ‚â´ g‚ÇÅ) (eq‚ÇÇ : f‚ÇÇ ‚â´ i‚ÇÉ = i‚ÇÇ ‚â´ g‚ÇÇ) [IsIso i‚ÇÅ] [IsIso i‚ÇÇ] [IsIso i‚ÇÉ] :
    IsIso (pullback.map f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ i‚ÇÅ i‚ÇÇ i‚ÇÉ eq‚ÇÅ eq‚ÇÇ) := by
  refine ‚ü®‚ü®pullback.map _ _ _ _ (inv i‚ÇÅ) (inv i‚ÇÇ) (inv i‚ÇÉ) ?_ ?_, ?_, ?_‚ü©‚ü©
  ¬∑ rw [IsIso.comp_inv_eq, Category.assoc, eq‚ÇÅ, IsIso.inv_hom_id_assoc]
  ¬∑ rw [IsIso.comp_inv_eq, Category.assoc, eq‚ÇÇ, IsIso.inv_hom_id_assoc]
  ¬∑ aesop_cat
  ¬∑ aesop_cat
#align category_theory.limits.pullback.map_is_iso CategoryTheory.Limits.pullback.map_isIso

/-- If `f‚ÇÅ = f‚ÇÇ` and `g‚ÇÅ = g‚ÇÇ`, we may construct a canonical
isomorphism `pullback f‚ÇÅ g‚ÇÅ ‚âÖ pullback f‚ÇÇ g‚ÇÇ` -/
@[simps! hom]
def pullback.congrHom {X Y Z : C} {f‚ÇÅ f‚ÇÇ : X ‚ü∂ Z} {g‚ÇÅ g‚ÇÇ : Y ‚ü∂ Z} (h‚ÇÅ : f‚ÇÅ = f‚ÇÇ) (h‚ÇÇ : g‚ÇÅ = g‚ÇÇ)
    [HasPullback f‚ÇÅ g‚ÇÅ] [HasPullback f‚ÇÇ g‚ÇÇ] : pullback f‚ÇÅ g‚ÇÅ ‚âÖ pullback f‚ÇÇ g‚ÇÇ :=
  asIso <| pullback.map _ _ _ _ (ùüô _) (ùüô _) (ùüô _) (by simp [h‚ÇÅ]) (by simp [h‚ÇÇ])
#align category_theory.limits.pullback.congr_hom CategoryTheory.Limits.pullback.congrHom

@[simp]
theorem pullback.congrHom_inv {X Y Z : C} {f‚ÇÅ f‚ÇÇ : X ‚ü∂ Z} {g‚ÇÅ g‚ÇÇ : Y ‚ü∂ Z} (h‚ÇÅ : f‚ÇÅ = f‚ÇÇ)
    (h‚ÇÇ : g‚ÇÅ = g‚ÇÇ) [HasPullback f‚ÇÅ g‚ÇÅ] [HasPullback f‚ÇÇ g‚ÇÇ] :
    (pullback.congrHom h‚ÇÅ h‚ÇÇ).inv =
      pullback.map _ _ _ _ (ùüô _) (ùüô _) (ùüô _) (by simp [h‚ÇÅ]) (by simp [h‚ÇÇ]) := by
  ext
  ¬∑ erw [pullback.lift_fst]
    rw [Iso.inv_comp_eq]
    erw [pullback.lift_fst_assoc]
    rw [Category.comp_id, Category.comp_id]
  ¬∑ erw [pullback.lift_snd]
    rw [Iso.inv_comp_eq]
    erw [pullback.lift_snd_assoc]
    rw [Category.comp_id, Category.comp_id]
#align category_theory.limits.pullback.congr_hom_inv CategoryTheory.Limits.pullback.congrHom_inv

instance pushout.map_isIso {W X Y Z S T : C} (f‚ÇÅ : S ‚ü∂ W) (f‚ÇÇ : S ‚ü∂ X) [HasPushout f‚ÇÅ f‚ÇÇ]
    (g‚ÇÅ : T ‚ü∂ Y) (g‚ÇÇ : T ‚ü∂ Z) [HasPushout g‚ÇÅ g‚ÇÇ] (i‚ÇÅ : W ‚ü∂ Y) (i‚ÇÇ : X ‚ü∂ Z) (i‚ÇÉ : S ‚ü∂ T)
    (eq‚ÇÅ : f‚ÇÅ ‚â´ i‚ÇÅ = i‚ÇÉ ‚â´ g‚ÇÅ) (eq‚ÇÇ : f‚ÇÇ ‚â´ i‚ÇÇ = i‚ÇÉ ‚â´ g‚ÇÇ) [IsIso i‚ÇÅ] [IsIso i‚ÇÇ] [IsIso i‚ÇÉ] :
    IsIso (pushout.map f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ i‚ÇÅ i‚ÇÇ i‚ÇÉ eq‚ÇÅ eq‚ÇÇ) := by
  refine ‚ü®‚ü®pushout.map _ _ _ _ (inv i‚ÇÅ) (inv i‚ÇÇ) (inv i‚ÇÉ) ?_ ?_, ?_, ?_‚ü©‚ü©
  ¬∑ rw [IsIso.comp_inv_eq, Category.assoc, eq‚ÇÅ, IsIso.inv_hom_id_assoc]
  ¬∑ rw [IsIso.comp_inv_eq, Category.assoc, eq‚ÇÇ, IsIso.inv_hom_id_assoc]
  ¬∑ aesop_cat
  ¬∑ aesop_cat
#align category_theory.limits.pushout.map_is_iso CategoryTheory.Limits.pushout.map_isIso

theorem pullback.mapDesc_comp {X Y S T S' : C} (f : X ‚ü∂ T) (g : Y ‚ü∂ T) (i : T ‚ü∂ S) (i' : S ‚ü∂ S')
    [HasPullback f g] [HasPullback (f ‚â´ i) (g ‚â´ i)] [HasPullback (f ‚â´ i ‚â´ i') (g ‚â´ i ‚â´ i')]
    [HasPullback ((f ‚â´ i) ‚â´ i') ((g ‚â´ i) ‚â´ i')] :
    pullback.mapDesc f g (i ‚â´ i') = pullback.mapDesc f g i ‚â´ pullback.mapDesc _ _ i' ‚â´
    (pullback.congrHom (Category.assoc _ _ _) (Category.assoc _ _ _)).hom := by
  aesop_cat
#align category_theory.limits.pullback.map_desc_comp CategoryTheory.Limits.pullback.mapDesc_comp

/-- If `f‚ÇÅ = f‚ÇÇ` and `g‚ÇÅ = g‚ÇÇ`, we may construct a canonical
isomorphism `pushout f‚ÇÅ g‚ÇÅ ‚âÖ pullback f‚ÇÇ g‚ÇÇ` -/
@[simps! hom]
def pushout.congrHom {X Y Z : C} {f‚ÇÅ f‚ÇÇ : X ‚ü∂ Y} {g‚ÇÅ g‚ÇÇ : X ‚ü∂ Z} (h‚ÇÅ : f‚ÇÅ = f‚ÇÇ) (h‚ÇÇ : g‚ÇÅ = g‚ÇÇ)
    [HasPushout f‚ÇÅ g‚ÇÅ] [HasPushout f‚ÇÇ g‚ÇÇ] : pushout f‚ÇÅ g‚ÇÅ ‚âÖ pushout f‚ÇÇ g‚ÇÇ :=
  asIso <| pushout.map _ _ _ _ (ùüô _) (ùüô _) (ùüô _) (by simp [h‚ÇÅ]) (by simp [h‚ÇÇ])
#align category_theory.limits.pushout.congr_hom CategoryTheory.Limits.pushout.congrHom

@[simp]
theorem pushout.congrHom_inv {X Y Z : C} {f‚ÇÅ f‚ÇÇ : X ‚ü∂ Y} {g‚ÇÅ g‚ÇÇ : X ‚ü∂ Z} (h‚ÇÅ : f‚ÇÅ = f‚ÇÇ)
    (h‚ÇÇ : g‚ÇÅ = g‚ÇÇ) [HasPushout f‚ÇÅ g‚ÇÅ] [HasPushout f‚ÇÇ g‚ÇÇ] :
    (pushout.congrHom h‚ÇÅ h‚ÇÇ).inv =
      pushout.map _ _ _ _ (ùüô _) (ùüô _) (ùüô _) (by simp [h‚ÇÅ]) (by simp [h‚ÇÇ]) := by
  ext
  ¬∑ erw [pushout.inl_desc]
    rw [Iso.comp_inv_eq, Category.id_comp]
    erw [pushout.inl_desc]
    rw [Category.id_comp]
  ¬∑ erw [pushout.inr_desc]
    rw [Iso.comp_inv_eq, Category.id_comp]
    erw [pushout.inr_desc]
    rw [Category.id_comp]
#align category_theory.limits.pushout.congr_hom_inv CategoryTheory.Limits.pushout.congrHom_inv

theorem pushout.mapLift_comp {X Y S T S' : C} (f : T ‚ü∂ X) (g : T ‚ü∂ Y) (i : S ‚ü∂ T) (i' : S' ‚ü∂ S)
    [HasPushout f g] [HasPushout (i ‚â´ f) (i ‚â´ g)] [HasPushout (i' ‚â´ i ‚â´ f) (i' ‚â´ i ‚â´ g)]
    [HasPushout ((i' ‚â´ i) ‚â´ f) ((i' ‚â´ i) ‚â´ g)] :
    pushout.mapLift f g (i' ‚â´ i) =
      (pushout.congrHom (Category.assoc _ _ _) (Category.assoc _ _ _)).hom ‚â´
        pushout.mapLift _ _ i' ‚â´ pushout.mapLift f g i := by
  aesop_cat
#align category_theory.limits.pushout.map_lift_comp CategoryTheory.Limits.pushout.mapLift_comp

section

variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] (G : C ‚•§ D)

/-- The comparison morphism for the pullback of `f,g`.
This is an isomorphism iff `G` preserves the pullback of `f,g`; see
`Mathlib/CategoryTheory/Limits/Preserves/Shapes/Pullbacks.lean`
-/
def pullbackComparison (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [HasPullback f g] [HasPullback (G.map f) (G.map g)] :
    G.obj (pullback f g) ‚ü∂ pullback (G.map f) (G.map g) :=
  pullback.lift (G.map pullback.fst) (G.map pullback.snd)
    (by simp only [‚Üê G.map_comp, pullback.condition])
#align category_theory.limits.pullback_comparison CategoryTheory.Limits.pullbackComparison

@[reassoc (attr := simp)]
theorem pullbackComparison_comp_fst (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [HasPullback f g]
    [HasPullback (G.map f) (G.map g)] :
    pullbackComparison G f g ‚â´ pullback.fst = G.map pullback.fst :=
  pullback.lift_fst _ _ _
#align category_theory.limits.pullback_comparison_comp_fst CategoryTheory.Limits.pullbackComparison_comp_fst

@[reassoc (attr := simp)]
theorem pullbackComparison_comp_snd (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [HasPullback f g]
    [HasPullback (G.map f) (G.map g)] :
    pullbackComparison G f g ‚â´ pullback.snd = G.map pullback.snd :=
  pullback.lift_snd _ _ _
#align category_theory.limits.pullback_comparison_comp_snd CategoryTheory.Limits.pullbackComparison_comp_snd

@[reassoc (attr := simp)]
theorem map_lift_pullbackComparison (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [HasPullback f g]
    [HasPullback (G.map f) (G.map g)] {W : C} {h : W ‚ü∂ X} {k : W ‚ü∂ Y} (w : h ‚â´ f = k ‚â´ g) :
    G.map (pullback.lift _ _ w) ‚â´ pullbackComparison G f g =
      pullback.lift (G.map h) (G.map k) (by simp only [‚Üê G.map_comp, w]) := by
  ext <;> simp [‚Üê G.map_comp]
#align category_theory.limits.map_lift_pullback_comparison CategoryTheory.Limits.map_lift_pullbackComparison

/-- The comparison morphism for the pushout of `f,g`.
This is an isomorphism iff `G` preserves the pushout of `f,g`; see
`Mathlib/CategoryTheory/Limits/Preserves/Shapes/Pullbacks.lean`
-/
def pushoutComparison (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) [HasPushout f g] [HasPushout (G.map f) (G.map g)] :
    pushout (G.map f) (G.map g) ‚ü∂ G.obj (pushout f g) :=
  pushout.desc (G.map pushout.inl) (G.map pushout.inr)
    (by simp only [‚Üê G.map_comp, pushout.condition])
#align category_theory.limits.pushout_comparison CategoryTheory.Limits.pushoutComparison

@[reassoc (attr := simp)]
theorem inl_comp_pushoutComparison (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) [HasPushout f g]
    [HasPushout (G.map f) (G.map g)] : pushout.inl ‚â´ pushoutComparison G f g = G.map pushout.inl :=
  pushout.inl_desc _ _ _
#align category_theory.limits.inl_comp_pushout_comparison CategoryTheory.Limits.inl_comp_pushoutComparison

@[reassoc (attr := simp)]
theorem inr_comp_pushoutComparison (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) [HasPushout f g]
    [HasPushout (G.map f) (G.map g)] : pushout.inr ‚â´ pushoutComparison G f g = G.map pushout.inr :=
  pushout.inr_desc _ _ _
#align category_theory.limits.inr_comp_pushout_comparison CategoryTheory.Limits.inr_comp_pushoutComparison

@[reassoc (attr := simp)]
theorem pushoutComparison_map_desc (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) [HasPushout f g]
    [HasPushout (G.map f) (G.map g)] {W : C} {h : Y ‚ü∂ W} {k : Z ‚ü∂ W} (w : f ‚â´ h = g ‚â´ k) :
    pushoutComparison G f g ‚â´ G.map (pushout.desc _ _ w) =
      pushout.desc (G.map h) (G.map k) (by simp only [‚Üê G.map_comp, w]) := by
  ext <;> simp [‚Üê G.map_comp]
#align category_theory.limits.pushout_comparison_map_desc CategoryTheory.Limits.pushoutComparison_map_desc

end

section PullbackSymmetry

open WalkingCospan

variable (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z)

/-- Making this a global instance would make the typeclass search go in an infinite loop. -/
theorem hasPullback_symmetry [HasPullback f g] : HasPullback g f :=
  ‚ü®‚ü®‚ü®_, PullbackCone.flipIsLimit (pullbackIsPullback f g)‚ü©‚ü©‚ü©
#align category_theory.limits.has_pullback_symmetry CategoryTheory.Limits.hasPullback_symmetry

attribute [local instance] hasPullback_symmetry

/-- The isomorphism `X √ó[Z] Y ‚âÖ Y √ó[Z] X`. -/
def pullbackSymmetry [HasPullback f g] : pullback f g ‚âÖ pullback g f :=
  IsLimit.conePointUniqueUpToIso
    (PullbackCone.flipIsLimit (pullbackIsPullback f g)) (limit.isLimit _)
#align category_theory.limits.pullback_symmetry CategoryTheory.Limits.pullbackSymmetry

@[reassoc (attr := simp)]
theorem pullbackSymmetry_hom_comp_fst [HasPullback f g] :
    (pullbackSymmetry f g).hom ‚â´ pullback.fst = pullback.snd := by simp [pullbackSymmetry]
#align category_theory.limits.pullback_symmetry_hom_comp_fst CategoryTheory.Limits.pullbackSymmetry_hom_comp_fst

@[reassoc (attr := simp)]
theorem pullbackSymmetry_hom_comp_snd [HasPullback f g] :
    (pullbackSymmetry f g).hom ‚â´ pullback.snd = pullback.fst := by simp [pullbackSymmetry]
#align category_theory.limits.pullback_symmetry_hom_comp_snd CategoryTheory.Limits.pullbackSymmetry_hom_comp_snd

@[reassoc (attr := simp)]
theorem pullbackSymmetry_inv_comp_fst [HasPullback f g] :
    (pullbackSymmetry f g).inv ‚â´ pullback.fst = pullback.snd := by simp [Iso.inv_comp_eq]
#align category_theory.limits.pullback_symmetry_inv_comp_fst CategoryTheory.Limits.pullbackSymmetry_inv_comp_fst

@[reassoc (attr := simp)]
theorem pullbackSymmetry_inv_comp_snd [HasPullback f g] :
    (pullbackSymmetry f g).inv ‚â´ pullback.snd = pullback.fst := by simp [Iso.inv_comp_eq]
#align category_theory.limits.pullback_symmetry_inv_comp_snd CategoryTheory.Limits.pullbackSymmetry_inv_comp_snd

end PullbackSymmetry

section PushoutSymmetry

open WalkingCospan

variable (f : X ‚ü∂ Y) (g : X ‚ü∂ Z)

/-- Making this a global instance would make the typeclass search go in an infinite loop. -/
theorem hasPushout_symmetry [HasPushout f g] : HasPushout g f :=
  ‚ü®‚ü®‚ü®_, PushoutCocone.flipIsColimit (pushoutIsPushout f g)‚ü©‚ü©‚ü©
#align category_theory.limits.has_pushout_symmetry CategoryTheory.Limits.hasPushout_symmetry

attribute [local instance] hasPushout_symmetry

/-- The isomorphism `Y ‚®ø[X] Z ‚âÖ Z ‚®ø[X] Y`. -/
def pushoutSymmetry [HasPushout f g] : pushout f g ‚âÖ pushout g f :=
  IsColimit.coconePointUniqueUpToIso
    (PushoutCocone.flipIsColimit (pushoutIsPushout f g)) (colimit.isColimit _)
#align category_theory.limits.pushout_symmetry CategoryTheory.Limits.pushoutSymmetry

@[reassoc (attr := simp)]
theorem inl_comp_pushoutSymmetry_hom [HasPushout f g] :
    pushout.inl ‚â´ (pushoutSymmetry f g).hom = pushout.inr :=
  (colimit.isColimit (span f g)).comp_coconePointUniqueUpToIso_hom
    (PushoutCocone.flipIsColimit (pushoutIsPushout g f)) _
#align category_theory.limits.inl_comp_pushout_symmetry_hom CategoryTheory.Limits.inl_comp_pushoutSymmetry_hom

@[reassoc (attr := simp)]
theorem inr_comp_pushoutSymmetry_hom [HasPushout f g] :
    pushout.inr ‚â´ (pushoutSymmetry f g).hom = pushout.inl :=
  (colimit.isColimit (span f g)).comp_coconePointUniqueUpToIso_hom
    (PushoutCocone.flipIsColimit (pushoutIsPushout g f)) _
#align category_theory.limits.inr_comp_pushout_symmetry_hom CategoryTheory.Limits.inr_comp_pushoutSymmetry_hom

@[reassoc (attr := simp)]
theorem inl_comp_pushoutSymmetry_inv [HasPushout f g] :
    pushout.inl ‚â´ (pushoutSymmetry f g).inv = pushout.inr := by simp [Iso.comp_inv_eq]
#align category_theory.limits.inl_comp_pushout_symmetry_inv CategoryTheory.Limits.inl_comp_pushoutSymmetry_inv

@[reassoc (attr := simp)]
theorem inr_comp_pushoutSymmetry_inv [HasPushout f g] :
    pushout.inr ‚â´ (pushoutSymmetry f g).inv = pushout.inl := by simp [Iso.comp_inv_eq]
#align category_theory.limits.inr_comp_pushout_symmetry_inv CategoryTheory.Limits.inr_comp_pushoutSymmetry_inv

end PushoutSymmetry

variable (C)

/-- `HasPullbacks` represents a choice of pullback for every pair of morphisms

See <https://stacks.math.columbia.edu/tag/001W>
-/
abbrev HasPullbacks :=
  HasLimitsOfShape WalkingCospan C
#align category_theory.limits.has_pullbacks CategoryTheory.Limits.HasPullbacks

/-- `HasPushouts` represents a choice of pushout for every pair of morphisms -/
abbrev HasPushouts :=
  HasColimitsOfShape WalkingSpan C
#align category_theory.limits.has_pushouts CategoryTheory.Limits.HasPushouts

/-- If `C` has all limits of diagrams `cospan f g`, then it has all pullbacks -/
theorem hasPullbacks_of_hasLimit_cospan
    [‚àÄ {X Y Z : C} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z}, HasLimit (cospan f g)] : HasPullbacks C :=
  { has_limit := fun F => hasLimitOfIso (diagramIsoCospan F).symm }
#align category_theory.limits.has_pullbacks_of_has_limit_cospan CategoryTheory.Limits.hasPullbacks_of_hasLimit_cospan

/-- If `C` has all colimits of diagrams `span f g`, then it has all pushouts -/
theorem hasPushouts_of_hasColimit_span
    [‚àÄ {X Y Z : C} {f : X ‚ü∂ Y} {g : X ‚ü∂ Z}, HasColimit (span f g)] : HasPushouts C :=
  { has_colimit := fun F => hasColimitOfIso (diagramIsoSpan F) }
#align category_theory.limits.has_pushouts_of_has_colimit_span CategoryTheory.Limits.hasPushouts_of_hasColimit_span

/-- The duality equivalence `WalkingSpan·µí·µñ ‚âå WalkingCospan` -/
@[simps!]
def walkingSpanOpEquiv : WalkingSpan·µí·µñ ‚âå WalkingCospan :=
  widePushoutShapeOpEquiv _
#align category_theory.limits.walking_span_op_equiv CategoryTheory.Limits.walkingSpanOpEquiv

/-- The duality equivalence `WalkingCospan·µí·µñ ‚âå WalkingSpan` -/
@[simps!]
def walkingCospanOpEquiv : WalkingCospan·µí·µñ ‚âå WalkingSpan :=
  widePullbackShapeOpEquiv _
#align category_theory.limits.walking_cospan_op_equiv CategoryTheory.Limits.walkingCospanOpEquiv

-- see Note [lower instance priority]
/-- Having wide pullback at any universe level implies having binary pullbacks. -/
instance (priority := 100) hasPullbacks_of_hasWidePullbacks (D : Type u) [h : Category.{v} D]
    [h' : HasWidePullbacks.{w} D] : HasPullbacks.{v,u} D := by
  haveI I := @hasWidePullbacks_shrink.{0, w} D h h'
  infer_instance
#align category_theory.limits.has_pullbacks_of_has_wide_pullbacks CategoryTheory.Limits.hasPullbacks_of_hasWidePullbacks

-- see Note [lower instance priority]
/-- Having wide pushout at any universe level implies having binary pushouts. -/
instance (priority := 100) hasPushouts_of_hasWidePushouts (D : Type u) [h : Category.{v} D]
    [h' : HasWidePushouts.{w} D] : HasPushouts.{v,u} D := by
  haveI I := @hasWidePushouts_shrink.{0, w} D h h'
  infer_instance


end CategoryTheory.Limits
