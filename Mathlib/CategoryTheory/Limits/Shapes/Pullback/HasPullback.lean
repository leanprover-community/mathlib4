/-
Copyright (c) 2018 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Markus Himmel, Bhavik Mehta, Andrew Yang, Emily Riehl, Calle SÃ¶nne
-/
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.PullbackCone

/-!
# HasPullback
`HasPullback f g` and `pullback f g` provides API for `HasLimit` and `limit` in the case of
pullbacks.

# Main definitions

* `HasPullback f g`: this is an abbreviation for `HasLimit (cospan f g)`, and is a typeclass used to
  express the fact that a given pair of morphisms has a pullback.

* `HasPullbacks`: expresses the fact that `C` admits all pullbacks, it is implemented as an
  abbreviation for `HasLimitsOfShape WalkingCospan C`

* `pullback f g`: Given a `HasPullback f g` instance, this function returns the choice of a limit
  object corresponding to the pullback of `f` and `g`. It fits into the following diagram:
```
  pullback f g ---pullback.fst f g---> X
      |                                |
      |                                |
pullback.snd f g                       f
      |                                |
      v                                v
      Y --------------g--------------> Z
```

* `HasPushout f g`: this is an abbreviation for `HasColimit (span f g)`, and is a typeclass used to
  express the fact that a given pair of morphisms has a pushout.
* `HasPushouts`: expresses the fact that `C` admits all pushouts, it is implemented as an
  abbreviation for `HasColimitsOfShape WalkingSpan C`
* `pushout f g`: Given a `HasPushout f g` instance, this function returns the choice of a colimit
  object corresponding to the pushout of `f` and `g`. It fits into the following diagram:
```
      X --------------f--------------> Y
      |                                |
      g                          pushout.inl f g
      |                                |
      v                                v
      Z ---pushout.inr f g---> pushout f g
```

# Main results & API
* The following API is available for using the universal property of `pullback f g`:
  `lift`, `lift_fst`, `lift_snd`, `lift'`, `hom_ext` (for uniqueness).

* `pullback.map` is the induced map between pullbacks `W Ã—â‚› X âŸ¶ Y Ã—â‚œ Z` given pointwise
  (compatible) maps `W âŸ¶ Y`, `X âŸ¶ Z` and `S âŸ¶ T`.

* `pullbackComparison`: Given a functor `G`, this is the natural morphism
  `G.obj (pullback f g) âŸ¶ pullback (G.map f) (G.map g)`

* `pullbackSymmetry` provides the natural isomorphism `pullback f g â‰… pullback g f`

(The dual results for pushouts are also available)

## References
* [Stacks: Fibre products](https://stacks.math.columbia.edu/tag/001U)
* [Stacks: Pushouts](https://stacks.math.columbia.edu/tag/0025)
-/

noncomputable section

open CategoryTheory

universe w vâ‚ vâ‚‚ v u uâ‚‚

namespace CategoryTheory.Limits

open WalkingSpan.Hom WalkingCospan.Hom WidePullbackShape.Hom WidePushoutShape.Hom

variable {C : Type u} [Category.{v} C] {W X Y Z : C}

/-- Two morphisms `f : X âŸ¶ Z` and `g : Y âŸ¶ Z` have a pullback if the diagram `cospan f g` has a
limit. -/
abbrev HasPullback {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :=
  HasLimit (cospan f g)

/-- Two morphisms `f : X âŸ¶ Y` and `g : X âŸ¶ Z` have a pushout if the diagram `span f g` has a
colimit. -/
abbrev HasPushout {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) :=
  HasColimit (span f g)

/-- `pullback f g` computes the pullback of a pair of morphisms with the same target. -/
abbrev pullback {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasPullback f g] :=
  limit (cospan f g)

/-- The cone associated to the pullback of `f` and `g` -/
abbrev pullback.cone {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasPullback f g] : PullbackCone f g :=
  limit.cone (cospan f g)

/-- `pushout f g` computes the pushout of a pair of morphisms with the same source. -/
abbrev pushout {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) [HasPushout f g] :=
  colimit (span f g)

/-- The cocone associated to the pullback of `f` and `g` -/
abbrev pushout.cocone {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) [HasPushout f g] : PushoutCocone f g :=
  colimit.cocone (span f g)

/-- The first projection of the pullback of `f` and `g`. -/
abbrev pullback.fst {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasPullback f g] : pullback f g âŸ¶ X :=
  limit.Ï€ (cospan f g) WalkingCospan.left

/-- The second projection of the pullback of `f` and `g`. -/
abbrev pullback.snd {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasPullback f g] : pullback f g âŸ¶ Y :=
  limit.Ï€ (cospan f g) WalkingCospan.right

/-- The first inclusion into the pushout of `f` and `g`. -/
abbrev pushout.inl {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) [HasPushout f g] : Y âŸ¶ pushout f g :=
  colimit.Î¹ (span f g) WalkingSpan.left

/-- The second inclusion into the pushout of `f` and `g`. -/
abbrev pushout.inr {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) [HasPushout f g] : Z âŸ¶ pushout f g :=
  colimit.Î¹ (span f g) WalkingSpan.right

/-- A pair of morphisms `h : W âŸ¶ X` and `k : W âŸ¶ Y` satisfying `h â‰« f = k â‰« g` induces a morphism
`pullback.lift : W âŸ¶ pullback f g`. -/
abbrev pullback.lift {W X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [HasPullback f g] (h : W âŸ¶ X)
    (k : W âŸ¶ Y) (w : h â‰« f = k â‰« g := by cat_disch) : W âŸ¶ pullback f g :=
  limit.lift _ (PullbackCone.mk h k w)

/-- A pair of morphisms `h : Y âŸ¶ W` and `k : Z âŸ¶ W` satisfying `f â‰« h = g â‰« k` induces a morphism
`pushout.desc : pushout f g âŸ¶ W`. -/
abbrev pushout.desc {W X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [HasPushout f g] (h : Y âŸ¶ W) (k : Z âŸ¶ W)
    (w : f â‰« h = g â‰« k := by cat_disch) : pushout f g âŸ¶ W :=
  colimit.desc _ (PushoutCocone.mk h k w)

/-- The cone associated to a pullback is a limit cone. -/
abbrev pullback.isLimit {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasPullback f g] :
    IsLimit (pullback.cone f g) :=
  limit.isLimit (cospan f g)

/-- The cocone associated to a pushout is a colimit cone. -/
abbrev pushout.isColimit {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) [HasPushout f g] :
    IsColimit (pushout.cocone f g) :=
  colimit.isColimit (span f g)

@[simp]
theorem PullbackCone.fst_limit_cone {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasLimit (cospan f g)] :
    PullbackCone.fst (limit.cone (cospan f g)) = pullback.fst f g := rfl

@[simp]
theorem PullbackCone.snd_limit_cone {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasLimit (cospan f g)] :
    PullbackCone.snd (limit.cone (cospan f g)) = pullback.snd f g := rfl

theorem PushoutCocone.inl_colimit_cocone {X Y Z : C} (f : Z âŸ¶ X) (g : Z âŸ¶ Y)
    [HasColimit (span f g)] : PushoutCocone.inl (colimit.cocone (span f g)) = pushout.inl _ _ := rfl

theorem PushoutCocone.inr_colimit_cocone {X Y Z : C} (f : Z âŸ¶ X) (g : Z âŸ¶ Y)
    [HasColimit (span f g)] : PushoutCocone.inr (colimit.cocone (span f g)) = pushout.inr _ _ := rfl

@[reassoc]
theorem pullback.lift_fst {W X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [HasPullback f g] (h : W âŸ¶ X)
    (k : W âŸ¶ Y) (w : h â‰« f = k â‰« g) : pullback.lift h k w â‰« pullback.fst f g = h :=
  limit.lift_Ï€ _ _

@[reassoc]
theorem pullback.lift_snd {W X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [HasPullback f g] (h : W âŸ¶ X)
    (k : W âŸ¶ Y) (w : h â‰« f = k â‰« g) : pullback.lift h k w â‰« pullback.snd f g = k :=
  limit.lift_Ï€ _ _

@[reassoc]
theorem pushout.inl_desc {W X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [HasPushout f g] (h : Y âŸ¶ W)
    (k : Z âŸ¶ W) (w : f â‰« h = g â‰« k) : pushout.inl _ _ â‰« pushout.desc h k w = h :=
  colimit.Î¹_desc _ _

@[reassoc]
theorem pushout.inr_desc {W X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [HasPushout f g] (h : Y âŸ¶ W)
    (k : Z âŸ¶ W) (w : f â‰« h = g â‰« k) : pushout.inr _ _ â‰« pushout.desc h k w = k :=
  colimit.Î¹_desc _ _

/-- A pair of morphisms `h : W âŸ¶ X` and `k : W âŸ¶ Y` satisfying `h â‰« f = k â‰« g` induces a morphism
`l : W âŸ¶ pullback f g` such that `l â‰« pullback.fst = h` and `l â‰« pullback.snd = k`. -/
def pullback.lift' {W X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [HasPullback f g] (h : W âŸ¶ X) (k : W âŸ¶ Y)
    (w : h â‰« f = k â‰« g) :
      { l : W âŸ¶ pullback f g // l â‰« pullback.fst f g = h âˆ§ l â‰« pullback.snd f g = k } :=
  âŸ¨pullback.lift h k w, pullback.lift_fst _ _ _, pullback.lift_snd _ _ _âŸ©

/-- A pair of morphisms `h : Y âŸ¶ W` and `k : Z âŸ¶ W` satisfying `f â‰« h = g â‰« k` induces a morphism
`l : pushout f g âŸ¶ W` such that `pushout.inl _ _ â‰« l = h` and `pushout.inr _ _ â‰« l = k`. -/
def pullback.desc' {W X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [HasPushout f g] (h : Y âŸ¶ W) (k : Z âŸ¶ W)
    (w : f â‰« h = g â‰« k) :
      { l : pushout f g âŸ¶ W // pushout.inl _ _ â‰« l = h âˆ§ pushout.inr _ _ â‰« l = k } :=
  âŸ¨pushout.desc h k w, pushout.inl_desc _ _ _, pushout.inr_desc _ _ _âŸ©

@[reassoc]
theorem pullback.condition {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [HasPullback f g] :
    pullback.fst f g â‰« f = pullback.snd f g â‰« g :=
  PullbackCone.condition _

@[reassoc]
theorem pushout.condition {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [HasPushout f g] :
    f â‰« (pushout.inl f g) = g â‰« pushout.inr _ _ :=
  PushoutCocone.condition _

/-- Two morphisms into a pullback are equal if their compositions with the pullback morphisms are
equal -/
@[ext 1100]
theorem pullback.hom_ext {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [HasPullback f g] {W : C}
    {k l : W âŸ¶ pullback f g} (hâ‚€ : k â‰« pullback.fst f g = l â‰« pullback.fst f g)
    (hâ‚ : k â‰« pullback.snd f g = l â‰« pullback.snd f g) : k = l :=
  limit.hom_ext <| PullbackCone.equalizer_ext _ hâ‚€ hâ‚

/-- The pullback cone built from the pullback projections is a pullback. -/
def pullbackIsPullback {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasPullback f g] :
    IsLimit (PullbackCone.mk (pullback.fst f g) (pullback.snd f g) pullback.condition) :=
  PullbackCone.mkSelfIsLimit <| pullback.isLimit f g

/-- Two morphisms out of a pushout are equal if their compositions with the pushout morphisms are
equal -/
@[ext 1100]
theorem pushout.hom_ext {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [HasPushout f g] {W : C}
    {k l : pushout f g âŸ¶ W} (hâ‚€ : pushout.inl _ _ â‰« k = pushout.inl _ _ â‰« l)
    (hâ‚ : pushout.inr _ _ â‰« k = pushout.inr _ _ â‰« l) : k = l :=
  colimit.hom_ext <| PushoutCocone.coequalizer_ext _ hâ‚€ hâ‚

/-- The pushout cocone built from the pushout coprojections is a pushout. -/
def pushoutIsPushout {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) [HasPushout f g] :
    IsColimit (PushoutCocone.mk (pushout.inl f g) (pushout.inr _ _) pushout.condition) :=
  PushoutCocone.IsColimit.mk _ (fun s => pushout.desc s.inl s.inr s.condition) (by simp) (by simp)
    (by cat_disch)

@[simp]
lemma pullback.lift_fst_snd {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasPullback f g] :
    lift (fst f g) (snd f g) condition = ğŸ™ (pullback f g) := by
  apply hom_ext <;> simp

@[simp]
lemma pushout.desc_inl_inr {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) [HasPushout f g] :
    desc (inl f g) (inr f g) condition = ğŸ™ (pushout f g) := by
  apply hom_ext <;> simp

/-- Given such a diagram, then there is a natural morphism `W Ã—â‚› X âŸ¶ Y Ã—â‚œ Z`.

```
W âŸ¶ Y
  â†˜   â†˜
  S âŸ¶ T
  â†—   â†—
X âŸ¶ Z
```
-/
abbrev pullback.map {W X Y Z S T : C} (fâ‚ : W âŸ¶ S) (fâ‚‚ : X âŸ¶ S) [HasPullback fâ‚ fâ‚‚] (gâ‚ : Y âŸ¶ T)
    (gâ‚‚ : Z âŸ¶ T) [HasPullback gâ‚ gâ‚‚] (iâ‚ : W âŸ¶ Y) (iâ‚‚ : X âŸ¶ Z) (iâ‚ƒ : S âŸ¶ T)
    (eqâ‚ : fâ‚ â‰« iâ‚ƒ = iâ‚ â‰« gâ‚) (eqâ‚‚ : fâ‚‚ â‰« iâ‚ƒ = iâ‚‚ â‰« gâ‚‚) : pullback fâ‚ fâ‚‚ âŸ¶ pullback gâ‚ gâ‚‚ :=
  pullback.lift (pullback.fst fâ‚ fâ‚‚ â‰« iâ‚) (pullback.snd fâ‚ fâ‚‚ â‰« iâ‚‚)
    (by simp only [Category.assoc, â† eqâ‚, â† eqâ‚‚, pullback.condition_assoc])

/-- The canonical map `X Ã—â‚› Y âŸ¶ X Ã—â‚œ Y` given `S âŸ¶ T`. -/
abbrev pullback.mapDesc {X Y S T : C} (f : X âŸ¶ S) (g : Y âŸ¶ S) (i : S âŸ¶ T) [HasPullback f g]
    [HasPullback (f â‰« i) (g â‰« i)] : pullback f g âŸ¶ pullback (f â‰« i) (g â‰« i) :=
  pullback.map f g (f â‰« i) (g â‰« i) (ğŸ™ _) (ğŸ™ _) i (Category.id_comp _).symm (Category.id_comp _).symm

@[reassoc]
lemma pullback.map_comp {X Y Z X' Y' Z' X'' Y'' Z'' : C}
    {f : X âŸ¶ Z} {g : Y âŸ¶ Z} {f' : X' âŸ¶ Z'} {g' : Y' âŸ¶ Z'} {f'' : X'' âŸ¶ Z''} {g'' : Y'' âŸ¶ Z''}
    (iâ‚ : X âŸ¶ X') (jâ‚ : X' âŸ¶ X'') (iâ‚‚ : Y âŸ¶ Y') (jâ‚‚ : Y' âŸ¶ Y'') (iâ‚ƒ : Z âŸ¶ Z') (jâ‚ƒ : Z' âŸ¶ Z'')
    [HasPullback f g] [HasPullback f' g'] [HasPullback f'' g'']
    (eâ‚ eâ‚‚ eâ‚ƒ eâ‚„) :
    pullback.map f g f' g' iâ‚ iâ‚‚ iâ‚ƒ eâ‚ eâ‚‚ â‰« pullback.map f' g' f'' g'' jâ‚ jâ‚‚ jâ‚ƒ eâ‚ƒ eâ‚„ =
      pullback.map f g f'' g'' (iâ‚ â‰« jâ‚) (iâ‚‚ â‰« jâ‚‚) (iâ‚ƒ â‰« jâ‚ƒ)
        (by rw [reassoc_of% eâ‚, eâ‚ƒ, Category.assoc])
        (by rw [reassoc_of% eâ‚‚, eâ‚„, Category.assoc]) := by ext <;> simp

@[simp]
lemma pullback.map_id {X Y Z : C}
    {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [HasPullback f g] :
    pullback.map f g f g (ğŸ™ _) (ğŸ™ _) (ğŸ™ _) (by simp) (by simp) = ğŸ™ _ := by ext <;> simp

/-- Given such a diagram, then there is a natural morphism `W â¨¿â‚› X âŸ¶ Y â¨¿â‚œ Z`.

```
  W âŸ¶ Y
 â†—   â†—
S âŸ¶ T
 â†˜   â†˜
  X âŸ¶ Z
```
-/
abbrev pushout.map {W X Y Z S T : C} (fâ‚ : S âŸ¶ W) (fâ‚‚ : S âŸ¶ X) [HasPushout fâ‚ fâ‚‚] (gâ‚ : T âŸ¶ Y)
    (gâ‚‚ : T âŸ¶ Z) [HasPushout gâ‚ gâ‚‚] (iâ‚ : W âŸ¶ Y) (iâ‚‚ : X âŸ¶ Z) (iâ‚ƒ : S âŸ¶ T) (eqâ‚ : fâ‚ â‰« iâ‚ = iâ‚ƒ â‰« gâ‚)
    (eqâ‚‚ : fâ‚‚ â‰« iâ‚‚ = iâ‚ƒ â‰« gâ‚‚) : pushout fâ‚ fâ‚‚ âŸ¶ pushout gâ‚ gâ‚‚ :=
  pushout.desc (iâ‚ â‰« pushout.inl _ _) (iâ‚‚ â‰« pushout.inr _ _)
    (by simp only [reassoc_of% eqâ‚, reassoc_of% eqâ‚‚, condition])

/-- The canonical map `X â¨¿â‚› Y âŸ¶ X â¨¿â‚œ Y` given `S âŸ¶ T`. -/
abbrev pushout.mapLift {X Y S T : C} (f : T âŸ¶ X) (g : T âŸ¶ Y) (i : S âŸ¶ T) [HasPushout f g]
    [HasPushout (i â‰« f) (i â‰« g)] : pushout (i â‰« f) (i â‰« g) âŸ¶ pushout f g :=
  pushout.map (i â‰« f) (i â‰« g) f g (ğŸ™ _) (ğŸ™ _) i (Category.comp_id _) (Category.comp_id _)

@[reassoc]
lemma pushout.map_comp {X Y Z X' Y' Z' X'' Y'' Z'' : C}
    {f : X âŸ¶ Y} {g : X âŸ¶ Z} {f' : X' âŸ¶ Y'} {g' : X' âŸ¶ Z'} {f'' : X'' âŸ¶ Y''} {g'' : X'' âŸ¶ Z''}
    (iâ‚ : X âŸ¶ X') (jâ‚ : X' âŸ¶ X'') (iâ‚‚ : Y âŸ¶ Y') (jâ‚‚ : Y' âŸ¶ Y'') (iâ‚ƒ : Z âŸ¶ Z') (jâ‚ƒ : Z' âŸ¶ Z'')
    [HasPushout f g] [HasPushout f' g'] [HasPushout f'' g'']
    (eâ‚ eâ‚‚ eâ‚ƒ eâ‚„) :
    pushout.map f g f' g' iâ‚‚ iâ‚ƒ iâ‚ eâ‚ eâ‚‚ â‰« pushout.map f' g' f'' g'' jâ‚‚ jâ‚ƒ jâ‚ eâ‚ƒ eâ‚„ =
      pushout.map f g f'' g'' (iâ‚‚ â‰« jâ‚‚) (iâ‚ƒ â‰« jâ‚ƒ) (iâ‚ â‰« jâ‚)
        (by rw [reassoc_of% eâ‚, eâ‚ƒ, Category.assoc])
        (by rw [reassoc_of% eâ‚‚, eâ‚„, Category.assoc]) := by ext <;> simp

@[simp]
lemma pushout.map_id {X Y Z : C}
    {f : X âŸ¶ Y} {g : X âŸ¶ Z} [HasPushout f g] :
    pushout.map f g f g (ğŸ™ _) (ğŸ™ _) (ğŸ™ _) (by simp) (by simp) = ğŸ™ _ := by ext <;> simp

instance pullback.map_isIso {W X Y Z S T : C} (fâ‚ : W âŸ¶ S) (fâ‚‚ : X âŸ¶ S) [HasPullback fâ‚ fâ‚‚]
    (gâ‚ : Y âŸ¶ T) (gâ‚‚ : Z âŸ¶ T) [HasPullback gâ‚ gâ‚‚] (iâ‚ : W âŸ¶ Y) (iâ‚‚ : X âŸ¶ Z) (iâ‚ƒ : S âŸ¶ T)
    (eqâ‚ : fâ‚ â‰« iâ‚ƒ = iâ‚ â‰« gâ‚) (eqâ‚‚ : fâ‚‚ â‰« iâ‚ƒ = iâ‚‚ â‰« gâ‚‚) [IsIso iâ‚] [IsIso iâ‚‚] [IsIso iâ‚ƒ] :
    IsIso (pullback.map fâ‚ fâ‚‚ gâ‚ gâ‚‚ iâ‚ iâ‚‚ iâ‚ƒ eqâ‚ eqâ‚‚) := by
  refine âŸ¨âŸ¨pullback.map _ _ _ _ (inv iâ‚) (inv iâ‚‚) (inv iâ‚ƒ) ?_ ?_, ?_, ?_âŸ©âŸ©
  Â· rw [IsIso.comp_inv_eq, Category.assoc, eqâ‚, IsIso.inv_hom_id_assoc]
  Â· rw [IsIso.comp_inv_eq, Category.assoc, eqâ‚‚, IsIso.inv_hom_id_assoc]
  Â· cat_disch
  Â· cat_disch

/-- If `fâ‚ = fâ‚‚` and `gâ‚ = gâ‚‚`, we may construct a canonical
isomorphism `pullback fâ‚ gâ‚ â‰… pullback fâ‚‚ gâ‚‚` -/
@[simps! hom]
def pullback.congrHom {X Y Z : C} {fâ‚ fâ‚‚ : X âŸ¶ Z} {gâ‚ gâ‚‚ : Y âŸ¶ Z} (hâ‚ : fâ‚ = fâ‚‚) (hâ‚‚ : gâ‚ = gâ‚‚)
    [HasPullback fâ‚ gâ‚] [HasPullback fâ‚‚ gâ‚‚] : pullback fâ‚ gâ‚ â‰… pullback fâ‚‚ gâ‚‚ :=
  asIso <| pullback.map _ _ _ _ (ğŸ™ _) (ğŸ™ _) (ğŸ™ _) (by simp [hâ‚]) (by simp [hâ‚‚])

@[simp]
theorem pullback.congrHom_inv {X Y Z : C} {fâ‚ fâ‚‚ : X âŸ¶ Z} {gâ‚ gâ‚‚ : Y âŸ¶ Z} (hâ‚ : fâ‚ = fâ‚‚)
    (hâ‚‚ : gâ‚ = gâ‚‚) [HasPullback fâ‚ gâ‚] [HasPullback fâ‚‚ gâ‚‚] :
    (pullback.congrHom hâ‚ hâ‚‚).inv =
      pullback.map _ _ _ _ (ğŸ™ _) (ğŸ™ _) (ğŸ™ _) (by simp [hâ‚]) (by simp [hâ‚‚]) := by
  ext <;> simp [Iso.inv_comp_eq]

instance pushout.map_isIso {W X Y Z S T : C} (fâ‚ : S âŸ¶ W) (fâ‚‚ : S âŸ¶ X) [HasPushout fâ‚ fâ‚‚]
    (gâ‚ : T âŸ¶ Y) (gâ‚‚ : T âŸ¶ Z) [HasPushout gâ‚ gâ‚‚] (iâ‚ : W âŸ¶ Y) (iâ‚‚ : X âŸ¶ Z) (iâ‚ƒ : S âŸ¶ T)
    (eqâ‚ : fâ‚ â‰« iâ‚ = iâ‚ƒ â‰« gâ‚) (eqâ‚‚ : fâ‚‚ â‰« iâ‚‚ = iâ‚ƒ â‰« gâ‚‚) [IsIso iâ‚] [IsIso iâ‚‚] [IsIso iâ‚ƒ] :
    IsIso (pushout.map fâ‚ fâ‚‚ gâ‚ gâ‚‚ iâ‚ iâ‚‚ iâ‚ƒ eqâ‚ eqâ‚‚) := by
  refine âŸ¨âŸ¨pushout.map _ _ _ _ (inv iâ‚) (inv iâ‚‚) (inv iâ‚ƒ) ?_ ?_, ?_, ?_âŸ©âŸ©
  Â· rw [IsIso.comp_inv_eq, Category.assoc, eqâ‚, IsIso.inv_hom_id_assoc]
  Â· rw [IsIso.comp_inv_eq, Category.assoc, eqâ‚‚, IsIso.inv_hom_id_assoc]
  Â· cat_disch
  Â· cat_disch

theorem pullback.mapDesc_comp {X Y S T S' : C} (f : X âŸ¶ T) (g : Y âŸ¶ T) (i : T âŸ¶ S) (i' : S âŸ¶ S')
    [HasPullback f g] [HasPullback (f â‰« i) (g â‰« i)] [HasPullback (f â‰« i â‰« i') (g â‰« i â‰« i')]
    [HasPullback ((f â‰« i) â‰« i') ((g â‰« i) â‰« i')] :
    pullback.mapDesc f g (i â‰« i') = pullback.mapDesc f g i â‰« pullback.mapDesc _ _ i' â‰«
    (pullback.congrHom (Category.assoc _ _ _) (Category.assoc _ _ _)).hom := by
  cat_disch

/-- If `fâ‚ = fâ‚‚` and `gâ‚ = gâ‚‚`, we may construct a canonical
isomorphism `pushout fâ‚ gâ‚ â‰… pullback fâ‚‚ gâ‚‚` -/
@[simps! hom]
def pushout.congrHom {X Y Z : C} {fâ‚ fâ‚‚ : X âŸ¶ Y} {gâ‚ gâ‚‚ : X âŸ¶ Z} (hâ‚ : fâ‚ = fâ‚‚) (hâ‚‚ : gâ‚ = gâ‚‚)
    [HasPushout fâ‚ gâ‚] [HasPushout fâ‚‚ gâ‚‚] : pushout fâ‚ gâ‚ â‰… pushout fâ‚‚ gâ‚‚ :=
  asIso <| pushout.map _ _ _ _ (ğŸ™ _) (ğŸ™ _) (ğŸ™ _) (by simp [hâ‚]) (by simp [hâ‚‚])

@[simp]
theorem pushout.congrHom_inv {X Y Z : C} {fâ‚ fâ‚‚ : X âŸ¶ Y} {gâ‚ gâ‚‚ : X âŸ¶ Z} (hâ‚ : fâ‚ = fâ‚‚)
    (hâ‚‚ : gâ‚ = gâ‚‚) [HasPushout fâ‚ gâ‚] [HasPushout fâ‚‚ gâ‚‚] :
    (pushout.congrHom hâ‚ hâ‚‚).inv =
      pushout.map _ _ _ _ (ğŸ™ _) (ğŸ™ _) (ğŸ™ _) (by simp [hâ‚]) (by simp [hâ‚‚]) := by
  ext <;> simp [Iso.comp_inv_eq]

theorem pushout.mapLift_comp {X Y S T S' : C} (f : T âŸ¶ X) (g : T âŸ¶ Y) (i : S âŸ¶ T) (i' : S' âŸ¶ S)
    [HasPushout f g] [HasPushout (i â‰« f) (i â‰« g)] [HasPushout (i' â‰« i â‰« f) (i' â‰« i â‰« g)]
    [HasPushout ((i' â‰« i) â‰« f) ((i' â‰« i) â‰« g)] :
    pushout.mapLift f g (i' â‰« i) =
      (pushout.congrHom (Category.assoc _ _ _) (Category.assoc _ _ _)).hom â‰«
        pushout.mapLift _ _ i' â‰« pushout.mapLift f g i := by
  cat_disch

section

variable {D : Type uâ‚‚} [Category.{vâ‚‚} D] (G : C â¥¤ D)

/-- The comparison morphism for the pullback of `f,g`.
This is an isomorphism iff `G` preserves the pullback of `f,g`; see
`Mathlib/CategoryTheory/Limits/Preserves/Shapes/Pullbacks.lean`
-/
def pullbackComparison (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasPullback f g] [HasPullback (G.map f) (G.map g)] :
    G.obj (pullback f g) âŸ¶ pullback (G.map f) (G.map g) :=
  pullback.lift (G.map (pullback.fst f g)) (G.map (pullback.snd f g))
    (by simp only [â† G.map_comp, pullback.condition])

@[reassoc (attr := simp)]
theorem pullbackComparison_comp_fst (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasPullback f g]
    [HasPullback (G.map f) (G.map g)] :
    pullbackComparison G f g â‰« pullback.fst _ _ = G.map (pullback.fst f g) :=
  pullback.lift_fst _ _ _

@[reassoc (attr := simp)]
theorem pullbackComparison_comp_snd (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasPullback f g]
    [HasPullback (G.map f) (G.map g)] :
    pullbackComparison G f g â‰« pullback.snd _ _ = G.map (pullback.snd f g) :=
  pullback.lift_snd _ _ _

@[reassoc (attr := simp)]
theorem map_lift_pullbackComparison (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasPullback f g]
    [HasPullback (G.map f) (G.map g)] {W : C} {h : W âŸ¶ X} {k : W âŸ¶ Y} (w : h â‰« f = k â‰« g) :
    G.map (pullback.lift _ _ w) â‰« pullbackComparison G f g =
      pullback.lift (G.map h) (G.map k) (by simp only [â† G.map_comp, w]) := by
  ext <;> simp [â† G.map_comp]

/-- The comparison morphism for the pushout of `f,g`.
This is an isomorphism iff `G` preserves the pushout of `f,g`; see
`Mathlib/CategoryTheory/Limits/Preserves/Shapes/Pullbacks.lean`
-/
def pushoutComparison (f : X âŸ¶ Y) (g : X âŸ¶ Z) [HasPushout f g] [HasPushout (G.map f) (G.map g)] :
    pushout (G.map f) (G.map g) âŸ¶ G.obj (pushout f g) :=
  pushout.desc (G.map (pushout.inl _ _)) (G.map (pushout.inr _ _))
    (by simp only [â† G.map_comp, pushout.condition])

@[reassoc (attr := simp)]
theorem inl_comp_pushoutComparison (f : X âŸ¶ Y) (g : X âŸ¶ Z) [HasPushout f g]
    [HasPushout (G.map f) (G.map g)] : pushout.inl _ _ â‰« pushoutComparison G f g =
      G.map (pushout.inl _ _) :=
  pushout.inl_desc _ _ _

@[reassoc (attr := simp)]
theorem inr_comp_pushoutComparison (f : X âŸ¶ Y) (g : X âŸ¶ Z) [HasPushout f g]
    [HasPushout (G.map f) (G.map g)] : pushout.inr _ _ â‰« pushoutComparison G f g =
      G.map (pushout.inr _ _) :=
  pushout.inr_desc _ _ _

@[reassoc (attr := simp)]
theorem pushoutComparison_map_desc (f : X âŸ¶ Y) (g : X âŸ¶ Z) [HasPushout f g]
    [HasPushout (G.map f) (G.map g)] {W : C} {h : Y âŸ¶ W} {k : Z âŸ¶ W} (w : f â‰« h = g â‰« k) :
    pushoutComparison G f g â‰« G.map (pushout.desc _ _ w) =
      pushout.desc (G.map h) (G.map k) (by simp only [â† G.map_comp, w]) := by
  ext <;> simp [â† G.map_comp]

end

section PullbackSymmetry

open WalkingCospan

variable (f : X âŸ¶ Z) (g : Y âŸ¶ Z)

/-- Making this a global instance would make the typeclass search go in an infinite loop. -/
theorem hasPullback_symmetry [HasPullback f g] : HasPullback g f :=
  âŸ¨âŸ¨âŸ¨_, PullbackCone.flipIsLimit (pullbackIsPullback f g)âŸ©âŸ©âŸ©

attribute [local instance] hasPullback_symmetry

/-- The isomorphism `X Ã—[Z] Y â‰… Y Ã—[Z] X`. -/
def pullbackSymmetry [HasPullback f g] : pullback f g â‰… pullback g f :=
  IsLimit.conePointUniqueUpToIso
    (PullbackCone.flipIsLimit (pullbackIsPullback f g)) (limit.isLimit _)

@[reassoc (attr := simp)]
theorem pullbackSymmetry_hom_comp_fst [HasPullback f g] :
    (pullbackSymmetry f g).hom â‰« pullback.fst g f = pullback.snd f g := by simp [pullbackSymmetry]

@[reassoc (attr := simp)]
theorem pullbackSymmetry_hom_comp_snd [HasPullback f g] :
    (pullbackSymmetry f g).hom â‰« pullback.snd g f = pullback.fst f g := by simp [pullbackSymmetry]

@[reassoc (attr := simp)]
theorem pullbackSymmetry_inv_comp_fst [HasPullback f g] :
    (pullbackSymmetry f g).inv â‰« pullback.fst f g = pullback.snd g f := by simp [Iso.inv_comp_eq]

@[reassoc (attr := simp)]
theorem pullbackSymmetry_inv_comp_snd [HasPullback f g] :
    (pullbackSymmetry f g).inv â‰« pullback.snd f g = pullback.fst g f := by simp [Iso.inv_comp_eq]

end PullbackSymmetry

section PushoutSymmetry

open WalkingCospan

variable (f : X âŸ¶ Y) (g : X âŸ¶ Z)

/-- Making this a global instance would make the typeclass search go in an infinite loop. -/
theorem hasPushout_symmetry [HasPushout f g] : HasPushout g f :=
  âŸ¨âŸ¨âŸ¨_, PushoutCocone.flipIsColimit (pushoutIsPushout f g)âŸ©âŸ©âŸ©

attribute [local instance] hasPushout_symmetry

/-- The isomorphism `Y â¨¿[X] Z â‰… Z â¨¿[X] Y`. -/
def pushoutSymmetry [HasPushout f g] : pushout f g â‰… pushout g f :=
  IsColimit.coconePointUniqueUpToIso
    (PushoutCocone.flipIsColimit (pushoutIsPushout f g)) (colimit.isColimit _)

@[reassoc (attr := simp)]
theorem inl_comp_pushoutSymmetry_hom [HasPushout f g] :
    pushout.inl _ _ â‰« (pushoutSymmetry f g).hom = pushout.inr _ _ :=
  (colimit.isColimit (span f g)).comp_coconePointUniqueUpToIso_hom
    (PushoutCocone.flipIsColimit (pushoutIsPushout g f)) _

@[reassoc (attr := simp)]
theorem inr_comp_pushoutSymmetry_hom [HasPushout f g] :
    pushout.inr _ _ â‰« (pushoutSymmetry f g).hom = pushout.inl _ _ :=
  (colimit.isColimit (span f g)).comp_coconePointUniqueUpToIso_hom
    (PushoutCocone.flipIsColimit (pushoutIsPushout g f)) _

@[reassoc (attr := simp)]
theorem inl_comp_pushoutSymmetry_inv [HasPushout f g] :
    pushout.inl _ _ â‰« (pushoutSymmetry f g).inv = pushout.inr _ _ := by simp [Iso.comp_inv_eq]

@[reassoc (attr := simp)]
theorem inr_comp_pushoutSymmetry_inv [HasPushout f g] :
    pushout.inr _ _ â‰« (pushoutSymmetry f g).inv = pushout.inl _ _ := by simp [Iso.comp_inv_eq]

end PushoutSymmetry

/-- `HasPullbacksAlong f` states that pullbacks of all morphisms into `Y`
along `f : X âŸ¶ Y` exist. -/
abbrev HasPullbacksAlong (f : X âŸ¶ Y) : Prop := âˆ€ {W} (h : W âŸ¶ Y), HasPullback h f

/-- `HasPushoutsAlong f` states that pushouts of all morphisms out of `X`
along `f : X âŸ¶ Y` exist. -/
abbrev HasPushoutsAlong (f : X âŸ¶ Y) : Prop := âˆ€ {W} (h : X âŸ¶ W), HasPushout h f

variable (C)

/-- A category `HasPullbacks` if it has all limits of shape `WalkingCospan`, i.e. if it has a
pullback for every pair of morphisms with the same codomain. -/
@[stacks 001W]
abbrev HasPullbacks :=
  HasLimitsOfShape WalkingCospan C

/-- A category `HasPushouts` if it has all colimits of shape `WalkingSpan`, i.e. if it has a
pushout for every pair of morphisms with the same domain. -/
abbrev HasPushouts :=
  HasColimitsOfShape WalkingSpan C

/-- If `C` has all limits of diagrams `cospan f g`, then it has all pullbacks -/
theorem hasPullbacks_of_hasLimit_cospan
    [âˆ€ {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z}, HasLimit (cospan f g)] : HasPullbacks C :=
  { has_limit := fun F => hasLimit_of_iso (diagramIsoCospan F).symm }

/-- If `C` has all colimits of diagrams `span f g`, then it has all pushouts -/
theorem hasPushouts_of_hasColimit_span
    [âˆ€ {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z}, HasColimit (span f g)] : HasPushouts C :=
  { has_colimit := fun F => hasColimit_of_iso (diagramIsoSpan F) }

/-- The duality equivalence `WalkingSpanáµ’áµ– â‰Œ WalkingCospan` -/
@[simps!]
def walkingSpanOpEquiv : WalkingSpanáµ’áµ– â‰Œ WalkingCospan :=
  widePushoutShapeOpEquiv _

/-- The duality equivalence `WalkingCospanáµ’áµ– â‰Œ WalkingSpan` -/
@[simps!]
def walkingCospanOpEquiv : WalkingCospanáµ’áµ– â‰Œ WalkingSpan :=
  widePullbackShapeOpEquiv _

-- see Note [lower instance priority]
/-- Having wide pullback at any universe level implies having binary pullbacks. -/
instance (priority := 100) hasPullbacks_of_hasWidePullbacks (D : Type u) [Category.{v} D]
    [HasWidePullbacks.{w} D] : HasPullbacks.{v, u} D :=
  hasWidePullbacks_shrink WalkingPair

-- see Note [lower instance priority]
/-- Having wide pushout at any universe level implies having binary pushouts. -/
instance (priority := 100) hasPushouts_of_hasWidePushouts (D : Type u) [Category.{v} D]
    [HasWidePushouts.{w} D] : HasPushouts.{v, u} D :=
  hasWidePushouts_shrink WalkingPair

end CategoryTheory.Limits
