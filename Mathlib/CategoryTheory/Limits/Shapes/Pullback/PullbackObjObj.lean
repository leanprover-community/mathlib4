/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou, Jack McKoen
-/
module

public import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq
public import Mathlib.CategoryTheory.Adjunction.Parametrized

/-!
# Leibniz Constructions

Let `F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ`. Given morphisms `f‚ÇÅ : X‚ÇÅ ‚ü∂ Y‚ÇÅ` in `C‚ÇÅ`
and `f‚ÇÇ : X‚ÇÇ ‚ü∂ Y‚ÇÇ` in `C‚ÇÇ`, we introduce a structure
`F.PushoutObjObj f‚ÇÅ f‚ÇÇ` which contains the data of a
pushout of `(F.obj Y‚ÇÅ).obj X‚ÇÇ` and `(F.obj X‚ÇÅ).obj Y‚ÇÇ`
along `(F.obj X‚ÇÅ).obj X‚ÇÇ`. If `sq‚ÇÅ‚ÇÇ : F.PushoutObjObj f‚ÇÅ f‚ÇÇ`,
we have a canonical "inclusion" `sq‚ÇÅ‚ÇÇ.Œπ : sq‚ÇÅ‚ÇÇ.pt ‚ü∂ (F.obj Y‚ÇÅ).obj Y‚ÇÇ`.

If `C‚ÇÉ` has pushouts, then we define the Leibniz pushout (often called pushout-product) as the
canonical inclusion `(PushoutObjObj.ofHasPushout F f‚ÇÅ f‚ÇÇ).Œπ`. This defines a bifunctor
`F.leibnizPushout : Arrow C‚ÇÅ ‚•§ Arrow C‚ÇÇ ‚•§ Arrow C‚ÇÉ`.

Similarly, if we have a bifunctor `G : C‚ÇÅ·µí·µñ ‚•§ C‚ÇÉ ‚•§ C‚ÇÇ`, and
morphisms `f‚ÇÅ : X‚ÇÅ ‚ü∂ Y‚ÇÅ` in `C‚ÇÅ` and `f‚ÇÉ : X‚ÇÉ ‚ü∂ Y‚ÇÉ` in `C‚ÇÉ`,
we introduce a structure `F.PullbackObjObj f‚ÇÅ f‚ÇÉ` which
contains the data of a pullback of `(G.obj (op X‚ÇÅ)).obj X‚ÇÉ`
and `(G.obj (op Y‚ÇÅ)).obj Y‚ÇÉ` over `(G.obj (op X‚ÇÅ)).obj Y‚ÇÉ`.
If `sq‚ÇÅ‚ÇÉ : F.PullbackObjObj f‚ÇÅ f‚ÇÉ`, we have a canonical
projection `sq‚ÇÅ‚ÇÉ.œÄ : (G.obj Y‚ÇÅ).obj X‚ÇÉ ‚ü∂ sq‚ÇÅ‚ÇÉ.pt`.

If `C‚ÇÇ` has pullbacks, then we define the Leibniz pullback (often called pullback-hom) as the
canonical projection `(PullbackObjObj.ofHasPullback G f‚ÇÅ f‚ÇÉ).œÄ`. This defines a bifunctor
`G.leibnizPullback : (Arrow C‚ÇÅ)·µí·µñ ‚•§ Arrow C‚ÇÉ ‚•§ Arrow C‚ÇÇ`.

## References

* [Emily Riehl, Dominic Verity, *Elements of ‚àû-Category Theory*, Definition C.2.8][RV22]
* https://ncatlab.org/nlab/show/pushout-product
* https://ncatlab.org/nlab/show/pullback-power

## Tags

pushout-product, pullback-hom, pullback-power, Leibniz
-/

@[expose] public section

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

namespace CategoryTheory

open Opposite Limits

variable {C‚ÇÅ : Type u‚ÇÅ} {C‚ÇÇ : Type u‚ÇÇ} {C‚ÇÉ : Type u‚ÇÉ}
  [Category.{v‚ÇÅ} C‚ÇÅ] [Category.{v‚ÇÇ} C‚ÇÇ] [Category.{v‚ÇÉ} C‚ÇÉ]
  (F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ) (G : C‚ÇÅ·µí·µñ ‚•§ C‚ÇÉ ‚•§ C‚ÇÇ)

namespace Functor

section

variable {X‚ÇÅ Y‚ÇÅ : C‚ÇÅ} (f‚ÇÅ : X‚ÇÅ ‚ü∂ Y‚ÇÅ) {X‚ÇÇ Y‚ÇÇ : C‚ÇÇ} (f‚ÇÇ : X‚ÇÇ ‚ü∂ Y‚ÇÇ)

/-- Given a bifunctor `F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ`, and morphisms `f‚ÇÅ : X‚ÇÅ ‚ü∂ Y‚ÇÅ` in `C‚ÇÅ`
and `f‚ÇÇ : X‚ÇÇ ‚ü∂ Y‚ÇÇ` in `C‚ÇÇ`, this structure contains the data of
a pushout of `(F.obj Y‚ÇÅ).obj X‚ÇÇ` and `(F.obj X‚ÇÅ).obj Y‚ÇÇ`
along `(F.obj X‚ÇÅ).obj X‚ÇÇ`. -/
structure PushoutObjObj where
  /-- the pushout -/
  pt : C‚ÇÉ
  /-- the first inclusion -/
  inl : (F.obj Y‚ÇÅ).obj X‚ÇÇ ‚ü∂ pt
  /-- the second inclusion -/
  inr : (F.obj X‚ÇÅ).obj Y‚ÇÇ ‚ü∂ pt
  isPushout : IsPushout ((F.map f‚ÇÅ).app X‚ÇÇ) ((F.obj X‚ÇÅ).map f‚ÇÇ) inl inr

namespace PushoutObjObj

/-- The `PushoutObjObj` structure given by the pushout of the colimits API. -/
@[simps]
noncomputable def ofHasPushout
    [HasPushout ((F.map f‚ÇÅ).app X‚ÇÇ) ((F.obj X‚ÇÅ).map f‚ÇÇ)] :
    F.PushoutObjObj f‚ÇÅ f‚ÇÇ :=
  { isPushout := IsPushout.of_hasPushout _ _, .. }

variable {F f‚ÇÅ f‚ÇÇ} (sq : F.PushoutObjObj f‚ÇÅ f‚ÇÇ)

/-- The "inclusion" `sq.pt ‚ü∂ (F.obj Y‚ÇÅ).obj Y‚ÇÇ` when
`sq : F.PushoutObjObj f‚ÇÅ f‚ÇÇ`. -/
noncomputable def Œπ : sq.pt ‚ü∂ (F.obj Y‚ÇÅ).obj Y‚ÇÇ :=
  sq.isPushout.desc ((F.obj Y‚ÇÅ).map f‚ÇÇ) ((F.map f‚ÇÅ).app Y‚ÇÇ) (by simp)

@[reassoc (attr := simp)]
lemma inl_Œπ : sq.inl ‚â´ sq.Œπ = (F.obj Y‚ÇÅ).map f‚ÇÇ := by simp [Œπ]

@[reassoc (attr := simp)]
lemma inr_Œπ : sq.inr ‚â´ sq.Œπ = (F.map f‚ÇÅ).app Y‚ÇÇ := by simp [Œπ]

/-- Given `sq : F.PushoutObjObj f‚ÇÅ f‚ÇÇ`, flipping the pushout square gives
`sq.flip : F.flip.PushoutObjObj f‚ÇÇ f‚ÇÅ`. -/
@[simps]
def flip : F.flip.PushoutObjObj f‚ÇÇ f‚ÇÅ where
  pt := sq.pt
  inl := sq.inr
  inr := sq.inl
  isPushout := sq.isPushout.flip

@[simp]
lemma Œπ_flip : sq.flip.Œπ = sq.Œπ := by
  apply sq.flip.isPushout.hom_ext
  ¬∑ rw [inl_Œπ, flip_inl, inr_Œπ, flip_obj_map]
  ¬∑ rw [inr_Œπ, flip_inr, inl_Œπ, flip_map_app]

@[simp]
lemma ofHasPushout_Œπ [HasPushout ((F.map f‚ÇÅ).app X‚ÇÇ) ((F.obj X‚ÇÅ).map f‚ÇÇ)] :
    (ofHasPushout F f‚ÇÅ f‚ÇÇ).Œπ =
      pushout.desc ((F.obj Y‚ÇÅ).map f‚ÇÇ) ((F.map f‚ÇÅ).app Y‚ÇÇ) (by simp) := by
  apply (ofHasPushout F f‚ÇÅ f‚ÇÇ).isPushout.hom_ext
  all_goals simp [PushoutObjObj.Œπ]

noncomputable section Arrow

variable {f‚ÇÅ f‚ÇÅ' : Arrow C‚ÇÅ} {f‚ÇÇ : Arrow C‚ÇÇ}
  (sq‚ÇÅ‚ÇÇ : F.PushoutObjObj f‚ÇÅ.hom f‚ÇÇ.hom)
  (sq‚ÇÅ‚ÇÇ' : F.PushoutObjObj f‚ÇÅ'.hom f‚ÇÇ.hom)

/-- Given a `PushoutObjObj` of `f‚ÇÅ : Arrow C‚ÇÅ` and `f‚ÇÇ : Arrow C‚ÇÇ`, a `PushoutObjObj` of `f‚ÇÅ'` and
  `f‚ÇÇ : Arrow C‚ÇÇ`, and a morphism `f‚ÇÅ ‚ü∂ f‚ÇÅ'`, this defines a morphism between the induced
  pushout maps. -/
@[simps]
def mapArrowLeft (sq : f‚ÇÅ ‚ü∂ f‚ÇÅ') :
    Arrow.mk sq‚ÇÅ‚ÇÇ.Œπ ‚ü∂ Arrow.mk sq‚ÇÅ‚ÇÇ'.Œπ where
  left := sq‚ÇÅ‚ÇÇ.isPushout.desc
    ((F.map sq.right).app f‚ÇÇ.left ‚â´ sq‚ÇÅ‚ÇÇ'.inl)
    ((F.map sq.left).app f‚ÇÇ.right ‚â´ sq‚ÇÅ‚ÇÇ'.inr)
    (by grind [sq.w, sq‚ÇÅ‚ÇÇ'.isPushout.w])
  right := (F.map sq.right).app f‚ÇÇ.right
  w := by
    apply sq‚ÇÅ‚ÇÇ.isPushout.hom_ext
    all_goals simp [‚Üê NatTrans.comp_app, ‚Üê Functor.map_comp]

@[simp]
lemma mapArrowLeft_id :
    mapArrowLeft sq‚ÇÅ‚ÇÇ sq‚ÇÅ‚ÇÇ (ùüô _) = ùüô _ := by
  apply Arrow.hom_ext
  ¬∑ apply sq‚ÇÅ‚ÇÇ.isPushout.hom_ext
    all_goals simp
  ¬∑ simp

@[reassoc (attr := simp)]
lemma mapArrowLeft_comp {f‚ÇÅ'' : Arrow C‚ÇÅ} (sq‚ÇÅ‚ÇÇ'' : F.PushoutObjObj f‚ÇÅ''.hom f‚ÇÇ.hom)
    (sq : f‚ÇÅ ‚ü∂ f‚ÇÅ') (sq' : f‚ÇÅ' ‚ü∂ f‚ÇÅ'') :
    (mapArrowLeft sq‚ÇÅ‚ÇÇ sq‚ÇÅ‚ÇÇ' sq) ‚â´ (mapArrowLeft sq‚ÇÅ‚ÇÇ' sq‚ÇÅ‚ÇÇ'' sq') =
      mapArrowLeft sq‚ÇÅ‚ÇÇ sq‚ÇÅ‚ÇÇ'' (sq ‚â´ sq') := by
  apply Arrow.hom_ext
  ¬∑ apply sq‚ÇÅ‚ÇÇ.isPushout.hom_ext
    all_goals simp
  ¬∑ simp

/-- Given a `PushoutObjObj` of `f‚ÇÅ : Arrow C‚ÇÅ` and `f‚ÇÇ : Arrow C‚ÇÇ`, a `PushoutObjObj` of `f‚ÇÅ'` and
  `f‚ÇÇ : Arrow C‚ÇÇ`, and an isomorphism `f‚ÇÅ ‚âÖ f‚ÇÅ'`, this defines an isomorphism of the induced
  pushout maps. -/
@[simps]
def Œπ_iso_of_iso_left (iso : f‚ÇÅ ‚âÖ f‚ÇÅ') :
    Arrow.mk sq‚ÇÅ‚ÇÇ.Œπ ‚âÖ Arrow.mk sq‚ÇÅ‚ÇÇ'.Œπ where
  hom := mapArrowLeft sq‚ÇÅ‚ÇÇ sq‚ÇÅ‚ÇÇ' iso.hom
  inv := mapArrowLeft sq‚ÇÅ‚ÇÇ' sq‚ÇÅ‚ÇÇ iso.inv

variable {f‚ÇÅ : Arrow C‚ÇÅ} {f‚ÇÇ f‚ÇÇ' : Arrow C‚ÇÇ}
    (sq‚ÇÅ‚ÇÇ : F.PushoutObjObj f‚ÇÅ.hom f‚ÇÇ.hom)
    (sq‚ÇÅ‚ÇÇ' : F.PushoutObjObj f‚ÇÅ.hom f‚ÇÇ'.hom)

/-- Given a `PushoutObjObj` of `f‚ÇÅ : Arrow C‚ÇÅ` and `f‚ÇÇ : Arrow C‚ÇÇ`, a `PushoutObjObj` of `f‚ÇÅ` and
  `f‚ÇÇ' : Arrow C‚ÇÇ`, and a morphism `f‚ÇÇ ‚ü∂ f‚ÇÇ'`, this defines a morphism between the induced
  pushout maps. -/
@[simps]
def mapArrowRight (sq : f‚ÇÇ ‚ü∂ f‚ÇÇ') :
    Arrow.mk sq‚ÇÅ‚ÇÇ.Œπ ‚ü∂ Arrow.mk sq‚ÇÅ‚ÇÇ'.Œπ where
  left :=   sq‚ÇÅ‚ÇÇ.isPushout.desc
    (((F.obj f‚ÇÅ.right).map sq.left) ‚â´ sq‚ÇÅ‚ÇÇ'.inl)
    (((F.obj f‚ÇÅ.left).map sq.right) ‚â´ sq‚ÇÅ‚ÇÇ'.inr)
    (by grind [sq.w, sq‚ÇÅ‚ÇÇ'.isPushout.w])
  right := (F.obj f‚ÇÅ.right).map sq.right
  w := by
    apply sq‚ÇÅ‚ÇÇ.isPushout.hom_ext
    ¬∑ simp [‚Üê map_comp]
    ¬∑ cat_disch

@[simp]
lemma mapArrowRight_id :
    mapArrowRight sq‚ÇÅ‚ÇÇ sq‚ÇÅ‚ÇÇ (ùüô _) = ùüô _ := by
  apply Arrow.hom_ext
  ¬∑ apply sq‚ÇÅ‚ÇÇ.isPushout.hom_ext
    all_goals simp
  ¬∑ simp

@[reassoc (attr := simp)]
lemma mapArrowRight_comp {f‚ÇÇ'' : Arrow C‚ÇÇ} (sq‚ÇÅ‚ÇÇ'' : F.PushoutObjObj f‚ÇÅ.hom f‚ÇÇ''.hom)
    (sq : f‚ÇÇ ‚ü∂ f‚ÇÇ') (sq' : f‚ÇÇ' ‚ü∂ f‚ÇÇ'') :
    (mapArrowRight sq‚ÇÅ‚ÇÇ sq‚ÇÅ‚ÇÇ' sq) ‚â´ (mapArrowRight sq‚ÇÅ‚ÇÇ' sq‚ÇÅ‚ÇÇ'' sq') =
      mapArrowRight sq‚ÇÅ‚ÇÇ sq‚ÇÅ‚ÇÇ'' (sq ‚â´ sq') := by
  apply Arrow.hom_ext
  ¬∑ apply sq‚ÇÅ‚ÇÇ.isPushout.hom_ext
    all_goals simp
  ¬∑ simp

/-- Given a `PushoutObjObj` of `f‚ÇÅ : Arrow C‚ÇÅ` and `f‚ÇÇ : Arrow C‚ÇÇ`, a `PushoutObjObj` of `f‚ÇÅ` and
  `f‚ÇÇ' : Arrow C‚ÇÇ`, and an isomorphism `f‚ÇÇ ‚âÖ f‚ÇÇ'`, this defines an isomorphism of the induced
  pushout maps. -/
@[simps]
def Œπ_iso_of_iso_right (iso : f‚ÇÇ ‚âÖ f‚ÇÇ') :
    Arrow.mk sq‚ÇÅ‚ÇÇ.Œπ ‚âÖ Arrow.mk sq‚ÇÅ‚ÇÇ'.Œπ where
  hom := mapArrowRight sq‚ÇÅ‚ÇÇ sq‚ÇÅ‚ÇÇ' iso.hom
  inv := mapArrowRight sq‚ÇÅ‚ÇÇ' sq‚ÇÅ‚ÇÇ iso.inv

end Arrow

end PushoutObjObj

end

/-- Given a bifunctor `F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ` to a category `C‚ÇÉ` which has pushouts, the Leibniz pushout
  (pushout-product) of `f‚ÇÅ : X‚ÇÅ ‚ü∂ Y‚ÇÅ` in `C‚ÇÅ` and `f‚ÇÇ : X‚ÇÇ ‚ü∂ Y‚ÇÇ` in `C‚ÇÇ` is the map
  `pushout ((F.map f‚ÇÅ).app X‚ÇÇ) ((F.obj X‚ÇÅ).map f‚ÇÇ) ‚ü∂ (F.obj Y‚ÇÅ).obj Y‚ÇÇ` induced by the diagram
```
  `(F.obj X‚ÇÅ).obj X‚ÇÇ` ----> `(F.obj Y‚ÇÅ).obj X‚ÇÇ`
          |                            |
          |                            |
          v                            v
  `(F.obj X‚ÇÅ).obj Y‚ÇÇ` ----> `(F.obj Y‚ÇÅ).obj Y‚ÇÇ`
```
-/
@[simps]
noncomputable
def leibnizPushout [HasPushouts C‚ÇÉ] : Arrow C‚ÇÅ ‚•§ Arrow C‚ÇÇ ‚•§ Arrow C‚ÇÉ where
  obj f‚ÇÅ :=
    { obj f‚ÇÇ := Arrow.mk (PushoutObjObj.ofHasPushout F f‚ÇÅ.hom f‚ÇÇ.hom).Œπ
      map sq :=
        PushoutObjObj.mapArrowRight
          (PushoutObjObj.ofHasPushout F ..)
          (PushoutObjObj.ofHasPushout F ..) sq }
  map sq :=
    { app f‚ÇÇ :=
        PushoutObjObj.mapArrowLeft
          (PushoutObjObj.ofHasPushout F ..)
          (PushoutObjObj.ofHasPushout F ..) sq }

section

variable {X‚ÇÅ Y‚ÇÅ : C‚ÇÅ} (f‚ÇÅ : X‚ÇÅ ‚ü∂ Y‚ÇÅ) {X‚ÇÉ Y‚ÇÉ : C‚ÇÉ} (f‚ÇÉ : X‚ÇÉ ‚ü∂ Y‚ÇÉ)

/-- Given a bifunctor `G : C‚ÇÅ·µí·µñ ‚•§ C‚ÇÉ ‚•§ C‚ÇÇ`, and morphisms `f‚ÇÅ : X‚ÇÅ ‚ü∂ Y‚ÇÅ` in `C‚ÇÅ`
and `f‚ÇÉ : X‚ÇÉ ‚ü∂ Y‚ÇÉ` in `C‚ÇÉ`, this structure contains the data of
a pullback of `(G.obj (op X‚ÇÅ)).obj X‚ÇÉ`
and `(G.obj (op Y‚ÇÅ)).obj Y‚ÇÉ` over `(G.obj (op X‚ÇÅ)).obj Y‚ÇÉ`. -/
structure PullbackObjObj where
  /-- the pullback -/
  pt : C‚ÇÇ
  /-- the first projection -/
  fst : pt ‚ü∂ (G.obj (op X‚ÇÅ)).obj X‚ÇÉ
  /-- the second projection -/
  snd : pt ‚ü∂ (G.obj (op Y‚ÇÅ)).obj Y‚ÇÉ
  isPullback : IsPullback fst snd ((G.obj (op X‚ÇÅ)).map f‚ÇÉ)
    ((G.map f‚ÇÅ.op).app Y‚ÇÉ)

namespace PullbackObjObj

/-- The `PullbackObjObj` structure given by the pullback of the limits API. -/
@[simps]
noncomputable def ofHasPullback
    [HasPullback ((G.obj (op X‚ÇÅ)).map f‚ÇÉ) ((G.map f‚ÇÅ.op).app Y‚ÇÉ)] :
    G.PullbackObjObj f‚ÇÅ f‚ÇÉ :=
  { isPullback := IsPullback.of_hasPullback _ _, ..}

variable {G f‚ÇÅ f‚ÇÉ} (sq : G.PullbackObjObj f‚ÇÅ f‚ÇÉ)

/-- The projection `(G.obj (op Y‚ÇÅ)).obj X‚ÇÉ ‚ü∂ sq.pt` when
`sq : G.PullbackObjObj f‚ÇÅ f‚ÇÉ`. -/
noncomputable def œÄ : (G.obj (op Y‚ÇÅ)).obj X‚ÇÉ ‚ü∂ sq.pt :=
  sq.isPullback.lift ((G.map f‚ÇÅ.op).app X‚ÇÉ) ((G.obj (op Y‚ÇÅ)).map f‚ÇÉ) (by simp)

@[reassoc (attr := simp)]
lemma œÄ_fst : sq.œÄ ‚â´ sq.fst = (G.map f‚ÇÅ.op).app X‚ÇÉ := by simp [œÄ]

@[reassoc (attr := simp)]
lemma œÄ_snd : sq.œÄ ‚â´ sq.snd = (G.obj (op Y‚ÇÅ)).map f‚ÇÉ := by simp [œÄ]

@[simp]
lemma ofHasPullback_œÄ
    [HasPullback ((G.obj (op X‚ÇÅ)).map f‚ÇÉ) ((G.map f‚ÇÅ.op).app Y‚ÇÉ)] :
    (ofHasPullback G f‚ÇÅ f‚ÇÉ).œÄ =
    (IsPullback.of_hasPullback ((G.obj (op X‚ÇÅ)).map f‚ÇÉ) ((G.map f‚ÇÅ.op).app Y‚ÇÉ)).lift
      ((G.map f‚ÇÅ.op).app X‚ÇÉ) ((G.obj (op Y‚ÇÅ)).map f‚ÇÉ) ((G.map f‚ÇÅ.op).naturality f‚ÇÉ).symm := rfl

noncomputable section Arrow

variable {f‚ÇÅ f‚ÇÅ' : Arrow C‚ÇÅ} {f‚ÇÉ : Arrow C‚ÇÉ}
  (sq‚ÇÅ‚ÇÉ : G.PullbackObjObj f‚ÇÅ.hom f‚ÇÉ.hom)
  (sq‚ÇÅ‚ÇÉ' : G.PullbackObjObj f‚ÇÅ'.hom f‚ÇÉ.hom)

/-- Given a `PullbackObjObj` of `f‚ÇÅ : Arrow C‚ÇÅ` and `f‚ÇÉ : Arrow C‚ÇÉ`, a `PullbackObjObj` of `f‚ÇÅ'` and
  `f‚ÇÉ : Arrow C‚ÇÉ`, and a morphism `f‚ÇÅ' ‚ü∂ f‚ÇÅ`, this defines a morphism between the induced
  pullback maps. -/
@[simps]
def mapArrowLeft (sq : f‚ÇÅ' ‚ü∂ f‚ÇÅ) :
    Arrow.mk sq‚ÇÅ‚ÇÉ.œÄ ‚ü∂ Arrow.mk sq‚ÇÅ‚ÇÉ'.œÄ where
  left := (G.map sq.right.op).app f‚ÇÉ.left
  right :=   sq‚ÇÅ‚ÇÉ'.isPullback.lift
    (sq‚ÇÅ‚ÇÉ.fst ‚â´ (G.map sq.left.op).app f‚ÇÉ.left)
    (sq‚ÇÅ‚ÇÉ.snd ‚â´ (G.map sq.right.op).app f‚ÇÉ.right)
    (by simp only [id_obj, Category.assoc]; grind [sq.w, sq‚ÇÅ‚ÇÉ.isPullback.w])
  w := by
    apply sq‚ÇÅ‚ÇÉ'.isPullback.hom_ext
    ¬∑ simp [‚Üê NatTrans.comp_app, ‚Üê map_comp, ‚Üê op_comp]
    ¬∑ cat_disch

@[simp]
lemma mapArrowLeft_id :
    mapArrowLeft sq‚ÇÅ‚ÇÉ sq‚ÇÅ‚ÇÉ (ùüô _) = ùüô _ := by
  apply Arrow.hom_ext
  ¬∑ simp
  ¬∑ apply sq‚ÇÅ‚ÇÉ.isPullback.hom_ext
    all_goals simp

@[simp]
lemma mapArrowLeft_comp {f‚ÇÅ'' : Arrow C‚ÇÅ} (sq‚ÇÅ‚ÇÉ'' : G.PullbackObjObj f‚ÇÅ''.hom f‚ÇÉ.hom)
    (sq' : f‚ÇÅ'' ‚ü∂ f‚ÇÅ') (sq : f‚ÇÅ' ‚ü∂ f‚ÇÅ) :
    (mapArrowLeft sq‚ÇÅ‚ÇÉ sq‚ÇÅ‚ÇÉ' sq) ‚â´ (mapArrowLeft sq‚ÇÅ‚ÇÉ' sq‚ÇÅ‚ÇÉ'' sq') =
      mapArrowLeft sq‚ÇÅ‚ÇÉ sq‚ÇÅ‚ÇÉ'' (sq' ‚â´ sq) := by
  apply Arrow.hom_ext
  ¬∑ simp
  ¬∑ apply sq‚ÇÅ‚ÇÉ''.isPullback.hom_ext
    all_goals simp

/-- Given a `PullbackObjObj` of `f‚ÇÅ : Arrow C‚ÇÅ` and `f‚ÇÉ : Arrow C‚ÇÉ`, a `PullbackObjObj` of `f‚ÇÅ'` and
  `f‚ÇÉ : Arrow C‚ÇÉ`, and an isomorphism `f‚ÇÅ ‚âÖ f‚ÇÅ'`, this defines an isomorphism of the induced
  pullback maps. -/
@[simps]
def œÄ_iso_of_iso_left (iso : f‚ÇÅ ‚âÖ f‚ÇÅ') :
    Arrow.mk sq‚ÇÅ‚ÇÉ.œÄ ‚âÖ Arrow.mk sq‚ÇÅ‚ÇÉ'.œÄ where
  hom := mapArrowLeft sq‚ÇÅ‚ÇÉ sq‚ÇÅ‚ÇÉ' iso.inv
  inv := mapArrowLeft sq‚ÇÅ‚ÇÉ' sq‚ÇÅ‚ÇÉ iso.hom

variable {f‚ÇÅ : Arrow C‚ÇÅ} {f‚ÇÉ f‚ÇÉ' : Arrow C‚ÇÉ}
  (sq‚ÇÅ‚ÇÉ : G.PullbackObjObj f‚ÇÅ.hom f‚ÇÉ.hom)
  (sq‚ÇÅ‚ÇÉ' : G.PullbackObjObj f‚ÇÅ.hom f‚ÇÉ'.hom)

/-- Given a `PullbackObjObj` of `f‚ÇÅ : Arrow C‚ÇÅ` and `f‚ÇÉ : Arrow C‚ÇÉ`, a `PullbackObjObj` of `f‚ÇÅ` and
  `f‚ÇÉ' : Arrow C‚ÇÉ`, and a morphism `f‚ÇÉ ‚ü∂ f‚ÇÉ'`, this defines a morphism between the induced
  pullback maps. -/
@[simps]
def mapArrowRight (sq : f‚ÇÉ ‚ü∂ f‚ÇÉ') :
    Arrow.mk sq‚ÇÅ‚ÇÉ.œÄ ‚ü∂ Arrow.mk sq‚ÇÅ‚ÇÉ'.œÄ where
  left := (G.obj (.op f‚ÇÅ.right)).map sq.left
  right := sq‚ÇÅ‚ÇÉ'.isPullback.lift
    (sq‚ÇÅ‚ÇÉ.fst ‚â´ (G.obj (.op f‚ÇÅ.left)).map sq.left)
    (sq‚ÇÅ‚ÇÉ.snd ‚â´ (G.obj (.op f‚ÇÅ.right)).map sq.right)
    (by grind [sq.w, sq‚ÇÅ‚ÇÉ.isPullback.w])
  w := by
    apply sq‚ÇÅ‚ÇÉ'.isPullback.hom_ext
    all_goals simp [‚Üê Functor.map_comp]

@[simp]
lemma mapArrowRight_id :
    mapArrowRight sq‚ÇÅ‚ÇÉ sq‚ÇÅ‚ÇÉ (ùüô _) = ùüô _ := by
  apply Arrow.hom_ext
  ¬∑ simp
  ¬∑ apply sq‚ÇÅ‚ÇÉ.isPullback.hom_ext
    all_goals simp

@[simp]
lemma mapArrowRight_comp {f‚ÇÉ'' : Arrow C‚ÇÉ} (sq‚ÇÅ‚ÇÉ'' : G.PullbackObjObj f‚ÇÅ.hom f‚ÇÉ''.hom)
    (sq : f‚ÇÉ ‚ü∂ f‚ÇÉ') (sq' : f‚ÇÉ' ‚ü∂ f‚ÇÉ'') :
    (mapArrowRight sq‚ÇÅ‚ÇÉ sq‚ÇÅ‚ÇÉ' sq) ‚â´ (mapArrowRight sq‚ÇÅ‚ÇÉ' sq‚ÇÅ‚ÇÉ'' sq') =
      mapArrowRight sq‚ÇÅ‚ÇÉ sq‚ÇÅ‚ÇÉ'' (sq ‚â´ sq') := by
  apply Arrow.hom_ext
  ¬∑ simp
  ¬∑ apply sq‚ÇÅ‚ÇÉ''.isPullback.hom_ext
    all_goals simp

/-- Given a `PullbackObjObj` of `f‚ÇÅ : Arrow C‚ÇÅ` and `f‚ÇÉ : Arrow C‚ÇÉ`, a `PullbackObjObj` of `f‚ÇÅ` and
  `f‚ÇÉ' : Arrow C‚ÇÉ`, and an isomorphism `f‚ÇÉ ‚âÖ f‚ÇÉ'`, this defines an isomorphism of the induced
  pullback maps. -/
@[simps]
def œÄ_iso_of_iso_right (iso : f‚ÇÉ ‚âÖ f‚ÇÉ') :
    Arrow.mk sq‚ÇÅ‚ÇÉ.œÄ ‚âÖ Arrow.mk sq‚ÇÅ‚ÇÉ'.œÄ where
  hom := mapArrowRight sq‚ÇÅ‚ÇÉ sq‚ÇÅ‚ÇÉ' iso.hom
  inv := mapArrowRight sq‚ÇÅ‚ÇÉ' sq‚ÇÅ‚ÇÉ iso.inv

end Arrow

end PullbackObjObj

end

/-- Given a bifunctor `G : C‚ÇÅ·µí·µñ ‚•§ C‚ÇÉ ‚•§ C‚ÇÇ` to a category `C‚ÇÇ` which has pullbacks, the Leibniz
  pullback (pullback-power) of `f‚ÇÅ : X‚ÇÅ ‚ü∂ Y‚ÇÅ` in `C‚ÇÅ` and `f‚ÇÉ : X‚ÇÉ ‚ü∂ Y‚ÇÉ` in `C‚ÇÉ` is the map
  `(G.obj (op Y‚ÇÅ)).obj X‚ÇÉ ‚ü∂ pullback ((G.obj (op X‚ÇÅ)).map f‚ÇÉ) ((G.map f‚ÇÅ.op).app Y‚ÇÉ)` induced by
  the diagram
```
  `(G.obj (op Y‚ÇÅ)).obj X‚ÇÉ` ----> `(G.obj (op X‚ÇÅ)).obj X‚ÇÉ`
              |                              |
              |                              |
              v                              v
  `(G.obj (op Y‚ÇÅ)).obj Y‚ÇÉ` ----> `(G.obj (op X‚ÇÅ)).obj Y‚ÇÉ`
```
-/
@[simps]
noncomputable
def leibnizPullback [HasPullbacks C‚ÇÇ] : (Arrow C‚ÇÅ)·µí·µñ ‚•§ Arrow C‚ÇÉ ‚•§ Arrow C‚ÇÇ where
  obj f‚ÇÅ :=
    { obj f‚ÇÉ := Arrow.mk (PullbackObjObj.ofHasPullback G f‚ÇÅ.unop.hom f‚ÇÉ.hom).œÄ
      map sq :=
        PullbackObjObj.mapArrowRight
          (PullbackObjObj.ofHasPullback G ..)
          (PullbackObjObj.ofHasPullback G ..) sq }
  map sq :=
    { app f‚ÇÉ :=
        PullbackObjObj.mapArrowLeft
          (PullbackObjObj.ofHasPullback G ..)
          (PullbackObjObj.ofHasPullback G ..) sq.unop }

end Functor

end CategoryTheory
