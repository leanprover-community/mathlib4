/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Bhavik Mehta
-/
module

public import Mathlib.CategoryTheory.EffectiveEpi.Basic
public import Mathlib.CategoryTheory.Limits.Shapes.Equalizers
public import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq
public import Mathlib.CategoryTheory.MorphismProperty.Composition

/-!
# Definitions and basic properties of regular monomorphisms and epimorphisms.

A regular monomorphism is a morphism that is the equalizer of some parallel pair.

In this file, we give the following definitions.
* `RegularMono f`, which is a structure carrying the data that exhibits `f` as a regular
  monomorphism. That is, it carries a fork and data specifying `f` as the equalizer of that fork.
* `IsRegularMono f`, which is a `Prop`-valued class stating that `f` is a regular monomorphism. In
  particular, this doesn't carry any data.
and constructions
* `IsSplitMono f ‚Üí RegularMono f` and
* `RegularMono f ‚Üí Mono f`
as well as the dual definitions/constructions for regular epimorphisms.

Additionally, we give the constructions
* `RegularEpi f ‚Üí EffectiveEpi f`, from which it can be deduced that regular epimorphisms are
  strong.
* `regularEpiOfEffectiveEpi`: constructs a `RegularEpi f` instance from `EffectiveEpi f` and
  `HasPullback f f`.


We also define classes `IsRegularMonoCategory` and `IsRegularEpiCategory` for categories in which
every monomorphism or epimorphism is regular, and deduce that these categories are
`StrongMonoCategory`s resp. `StrongEpiCategory`s.

-/

@[expose] public section


noncomputable section

namespace CategoryTheory

open CategoryTheory.Limits

universe v‚ÇÅ u‚ÇÅ u‚ÇÇ

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]
variable {X Y : C}

/-- A regular monomorphism is a morphism which is the equalizer of some parallel pair. -/
structure RegularMono (f : X ‚ü∂ Y) where
  /-- An object in `C` -/
  Z : C
  /-- A map from the codomain of `f` to `Z` -/
  left : Y ‚ü∂ Z
  /-- Another map from the codomain of `f` to `Z` -/
  right : Y ‚ü∂ Z
  /-- `f` equalizes the two maps -/
  w : f ‚â´ left = f ‚â´ right := by cat_disch
  /-- `f` is the equalizer of the two maps -/
  isLimit : IsLimit (Fork.ofŒπ f w)

attribute [reassoc] RegularMono.w

/-- Every regular monomorphism is a monomorphism. -/
lemma RegularMono.mono {f : X ‚ü∂ Y} (h : RegularMono f) : Mono f :=
  mono_of_isLimit_fork h.isLimit

/-- Every isomorphism is a regular monomorphism. -/
def RegularMono.ofIso (e : X ‚âÖ Y) : RegularMono e.hom where
  Z := Y
  left := ùüô Y
  right := ùüô Y
  isLimit := Fork.IsLimit.mk _ (fun s ‚Ü¶ s.Œπ ‚â´ e.inv) (by simp) fun s m w ‚Ü¶ by simp [‚Üê w]

/-- Regular monomorphisms are preserved by isomorphisms in the arrow category. -/
def RegularMono.ofArrowIso {X'} {Y'} {f : X ‚ü∂ Y} {g : X' ‚ü∂ Y'}
    (e : Arrow.mk f ‚âÖ Arrow.mk g) (h : RegularMono f) :
    RegularMono g where
  Z := h.Z
  left := e.inv.right ‚â´ h.left
  right := e.inv.right ‚â´ h.right
  w := by
    have := Arrow.mk_hom g ‚ñ∏ Arrow.w_mk_right e.inv
    simp_rw [‚Üê reassoc_of% this, h.w]
  isLimit := Fork.isLimitOfIsos _ h.isLimit _
    (Arrow.rightFunc.mapIso e) (Iso.refl _) (Arrow.leftFunc.mapIso e)

/-- `IsRegularMono f` is the assertion that `f` is a regular monomorphism. -/
class IsRegularMono {X Y : C} (f : X ‚ü∂ Y) : Prop where
  regularMono : Nonempty (RegularMono f)

variable (C) in
/-- The `MorphismProperty C` satisfied by regular monomorphisms in `C`. -/
def MorphismProperty.regularMono : MorphismProperty C := fun _ _ f => IsRegularMono f

@[simp]
theorem MorphismProperty.regularMono_iff (f : X ‚ü∂ Y) :
    (MorphismProperty.regularMono C) f ‚Üî IsRegularMono f :=
  Iff.rfl

instance MorphismProperty.regularMono.containsIdentities :
    (MorphismProperty.regularMono C).ContainsIdentities where
  id_mem _ := ‚ü®‚ü®RegularMono.ofIso <| Iso.refl _‚ü©‚ü©

instance MorphismProperty.regularMono.respectsIso :
    (MorphismProperty.regularMono C).RespectsIso :=
  RespectsIso.of_respects_arrow_iso _ (fun _ _ e h ‚Ü¶ ‚ü®‚ü®.ofArrowIso e (h := h.regularMono.some)‚ü©‚ü©)

lemma isRegularMono_of_regularMono {f : X ‚ü∂ Y} (h : RegularMono f) : IsRegularMono f := ‚ü®‚ü®h‚ü©‚ü©

/-- Given `IsRegularMono f`, a choice of data for `RegularMono f`. -/
def IsRegularMono.getStruct (f : X ‚ü∂ Y) [IsRegularMono f] : RegularMono f :=
  IsRegularMono.regularMono.some

@[deprecated (since := "2025-12-01")] noncomputable alias regularMonoOfIsRegularMono :=
  IsRegularMono.getStruct

section IsRegularMono

/-!

Given a regular monomorphism `f : X ‚ü∂ Y` (i.e. a morphism satisfying the predicate `IsRegularMono`),
this section gives an equalizer diagram
```
     X
    f|
     v
     Y
left| |right
    v v
     Z
```
The names `Z`, `left`, and `right` all being in the `IsRegularMono` namespace.
-/

variable {X Y : C} (f : X ‚ü∂ Y) [IsRegularMono f]

/-- The target of the equalizer diagram for `f`. -/
def IsRegularMono.Z : C := (IsRegularMono.getStruct f).Z

/-- The "left" map `Y ‚ü∂ Z`. -/
def IsRegularMono.left : Y ‚ü∂ Z f := (IsRegularMono.getStruct f).left

/-- The "right" map `Y ‚ü∂ Z`. -/
def IsRegularMono.right : Y ‚ü∂ Z f := (IsRegularMono.getStruct f).right

/-- The equalizer condition. -/
lemma IsRegularMono.w : f ‚â´ left f = f ‚â´ right f := (IsRegularMono.getStruct f).w

/-- The fork is in fact an equalizer. -/
def IsRegularMono.isLimit : IsLimit <| Fork.ofŒπ _ (w f) := (IsRegularMono.getStruct f).isLimit

/-- Lift a morphism `k : W ‚ü∂ Y`, equalized by the two morphisms `left` and `right`, along `f`. -/
def IsRegularMono.lift {W : C} (f : X ‚ü∂ Y) [IsRegularMono f] (k : W ‚ü∂ Y)
    (h : k ‚â´ left f = k ‚â´ right f) : W ‚ü∂ X :=
  Fork.IsLimit.lift (isLimit f) k h

@[reassoc (attr := simp)]
lemma IsRegularMono.fac {W : C} (f : X ‚ü∂ Y) [IsRegularMono f] (k : W ‚ü∂ Y)
    (h : k ‚â´ left f = k ‚â´ right f) : lift f k h ‚â´ f = k :=
  Fork.IsLimit.lift_Œπ (isLimit f)

lemma IsRegularMono.uniq {W : C} (f : X ‚ü∂ Y) [IsRegularMono f] (k : W ‚ü∂ Y)
    (h : k ‚â´ left f = k ‚â´ right f) (m : W ‚ü∂ X) (hm : m ‚â´ f = k) : m = lift f k h :=
  Fork.IsLimit.existsUnique (isLimit f) k h |>.unique hm <| by simp

end IsRegularMono

/-- The chosen equalizer of a parallel pair is a regular monomorphism. -/
def RegularMono.equalizer (g h : X ‚ü∂ Y) [HasLimit (parallelPair g h)] :
    RegularMono (equalizer.Œπ g h) where
  Z := Y
  left := g
  right := h
  w := equalizer.condition g h
  isLimit :=
    Fork.IsLimit.mk _ (fun s => limit.lift _ s) (by simp) fun s m w => by
      apply equalizer.hom_ext
      simp [‚Üê w]

instance (g h : X ‚ü∂ Y) [HasLimit (parallelPair g h)] :
    IsRegularMono (equalizer.Œπ g h) :=
  isRegularMono_of_regularMono <| RegularMono.equalizer g h

/-- Every split monomorphism is a regular monomorphism. -/
def RegularMono.ofIsSplitMono (f : X ‚ü∂ Y) [IsSplitMono f] :
    RegularMono f where
  Z := Y
  left := ùüô Y
  right := retraction f ‚â´ f
  isLimit := isSplitMonoEqualizes f

instance (priority := 100) (f : X ‚ü∂ Y) [IsSplitMono f] :
    IsRegularMono f :=
  isRegularMono_of_regularMono <| .ofIsSplitMono f

/-- If `f` is a regular mono, then any map `k : W ‚ü∂ Y` equalizing `RegularMono.left` and
`RegularMono.right` induces a morphism `l : W ‚ü∂ X` such that `l ‚â´ f = k`. -/
def RegularMono.lift' {W : C} {f : X ‚ü∂ Y} (hf : RegularMono f) (k : W ‚ü∂ Y)
    (h : k ‚â´ hf.left = k ‚â´ hf.right) :
    { l : W ‚ü∂ X // l ‚â´ f = k } :=
  Fork.IsLimit.lift' hf.isLimit _ h

/-- The second leg of a pullback cone is a regular monomorphism if the right component is too.

See also `Pullback.sndOfMono` for the basic monomorphism version, and
`regularOfIsPullbackFstOfRegular` for the flipped version.
-/
def regularOfIsPullbackSndOfRegular {P Q R S : C} {f : P ‚ü∂ Q} {g : P ‚ü∂ R} {h : Q ‚ü∂ S} {k : R ‚ü∂ S}
    (hr : RegularMono h) (comm : f ‚â´ h = g ‚â´ k) (t : IsLimit (PullbackCone.mk _ _ comm)) :
    RegularMono g where
  Z := hr.Z
  left := k ‚â´ hr.left
  right := k ‚â´ hr.right
  w := by
    repeat (rw [‚Üê Category.assoc, ‚Üê eq_whisker comm])
    simp only [Category.assoc, hr.w]
  isLimit := by
    apply Fork.IsLimit.mk' _ _
    intro s
    have l‚ÇÅ : (Fork.Œπ s ‚â´ k) ‚â´ hr.left = (Fork.Œπ s ‚â´ k) ‚â´ hr.right := by
      rw [Category.assoc, s.condition, Category.assoc]
    obtain ‚ü®l, hl‚ü© := Fork.IsLimit.lift' hr.isLimit _ l‚ÇÅ
    obtain ‚ü®p, _, hp‚ÇÇ‚ü© := PullbackCone.IsLimit.lift' t _ _ hl
    refine ‚ü®p, hp‚ÇÇ, ?_‚ü©
    intro m w
    have z : m ‚â´ g = p ‚â´ g := w.trans hp‚ÇÇ.symm
    apply t.hom_ext
    have := hr.mono
    apply (PullbackCone.mk f g comm).equalizer_ext
    ¬∑ simp only [PullbackCone.mk_œÄ_app, ‚Üê cancel_mono h]
      grind [Fork.Œπ_ofŒπ]
    ¬∑ exact z

/-- The first leg of a pullback cone is a regular monomorphism if the left component is too.

See also `Pullback.fstOfMono` for the basic monomorphism version, and
`regularOfIsPullbackSndOfRegular` for the flipped version.
-/
def regularOfIsPullbackFstOfRegular {P Q R S : C} {f : P ‚ü∂ Q} {g : P ‚ü∂ R} {h : Q ‚ü∂ S} {k : R ‚ü∂ S}
    (hk : RegularMono k) (comm : f ‚â´ h = g ‚â´ k) (t : IsLimit (PullbackCone.mk _ _ comm)) :
    RegularMono f :=
  regularOfIsPullbackSndOfRegular hk comm.symm (PullbackCone.flipIsLimit t)

/-- Any regular monomorphism is a strong monomorphism. -/
lemma RegularMono.strongMono {f : X ‚ü∂ Y} (h : RegularMono f) : StrongMono f :=
  have := h.mono
  StrongMono.mk' (by
      intro A B z hz u v sq
      have : v ‚â´ h.left = v ‚â´ h.right := by
        apply (cancel_epi z).1
        repeat (rw [‚Üê Category.assoc, ‚Üê eq_whisker sq.w])
        simp only [Category.assoc, RegularMono.w]
      obtain ‚ü®t, ht‚ü© := RegularMono.lift' _ _ this
      refine CommSq.HasLift.mk' ‚ü®t, (cancel_mono f).1 ?_, ht‚ü©
      simp only [Category.assoc, ht, sq.w])

instance (priority := 100) (f : X ‚ü∂ Y) [IsRegularMono f] : StrongMono f :=
  IsRegularMono.getStruct f |>.strongMono

/-- A regular monomorphism is an isomorphism if it is an epimorphism. -/
theorem isIso_of_regularMono_of_epi (f : X ‚ü∂ Y) (h : RegularMono f) [Epi f] : IsIso f :=
  have := RegularMono.strongMono h
  isIso_of_epi_of_strongMono _

section

variable (C)

/-- A regular mono category is a category in which every monomorphism is regular. -/
class IsRegularMonoCategory : Prop where
  /-- Every monomorphism is a regular monomorphism -/
  regularMonoOfMono : ‚àÄ {X Y : C} (f : X ‚ü∂ Y) [Mono f], IsRegularMono f

end

/-- In a category in which every monomorphism is regular, we can express every monomorphism as
an equalizer. This is not an instance because it would create an instance loop. -/
def regularMonoOfMono [IsRegularMonoCategory C] (f : X ‚ü∂ Y) [Mono f] : RegularMono f :=
  have := IsRegularMonoCategory.regularMonoOfMono f
  IsRegularMono.getStruct f

instance (priority := 100) regularMonoCategoryOfSplitMonoCategory [SplitMonoCategory C] :
    IsRegularMonoCategory C where
  regularMonoOfMono f _ :=
    haveI := isSplitMono_of_mono f
    isRegularMono_of_regularMono <| RegularMono.ofIsSplitMono f

instance (priority := 100) strongMonoCategory_of_regularMonoCategory [IsRegularMonoCategory C] :
    StrongMonoCategory C where
  strongMono_of_mono f _ :=
    RegularMono.strongMono <| regularMonoOfMono f

/-- A regular epimorphism is a morphism which is the coequalizer of some parallel pair. -/
structure RegularEpi (f : X ‚ü∂ Y) where
  /-- An object from `C` -/
  W : C
  /-- Two maps to the domain of `f` -/
  (left right : W ‚ü∂ X)
  /-- `f` coequalizes the two maps -/
  w : left ‚â´ f = right ‚â´ f := by cat_disch
  /-- `f` is the coequalizer -/
  isColimit : IsColimit (Cofork.ofœÄ f w)

attribute [reassoc] RegularEpi.w

/-- Every regular epimorphism is an epimorphism. -/
lemma RegularEpi.epi (f : X ‚ü∂ Y) (h : RegularEpi f) : Epi f :=
  epi_of_isColimit_cofork h.isColimit

/-- Every isomorphism is a regular epimorphism. -/
def RegularEpi.ofIso (e : X ‚âÖ Y) : RegularEpi e.hom where
  W := X
  left := ùüô X
  right := ùüô X
  isColimit := Cofork.IsColimit.mk _ (fun s ‚Ü¶ e.inv ‚â´ s.œÄ) (by simp) fun s m w ‚Ü¶ by
    simp [‚Üê w]

/-- Regular epimorphisms are preserved by isomorphisms in the arrow category. -/
def RegularEpi.ofArrowIso {X'} {Y'} {f : X ‚ü∂ Y} {g : X' ‚ü∂ Y'}
    (e : Arrow.mk f ‚âÖ Arrow.mk g) (h : RegularEpi f) :
    RegularEpi g where
  W := h.W
  left := h.left ‚â´ e.hom.left
  right := h.right ‚â´ e.hom.left
  w := by
    simp only [Category.assoc, Arrow.w_mk_right, Arrow.mk_hom]
    rw [reassoc_of% h.w]
  isColimit := Cofork.isColimitOfIsos _ h.isColimit _
    (Iso.refl _) (Arrow.leftFunc.mapIso e) (Arrow.rightFunc.mapIso e)

/-- `IsRegularEpi f` is the assertion that `f` is a regular epimorphism. -/
class IsRegularEpi {X Y : C} (f : X ‚ü∂ Y) : Prop where
  regularEpi : Nonempty (RegularEpi f)

variable (C) in
/-- The `MorphismProperty C` satisfied by regular epimorphisms in `C`. -/
def MorphismProperty.regularEpi : MorphismProperty C := fun _ _ f => IsRegularEpi f

@[simp]
theorem MorphismProperty.regularEpi_iff (f : X ‚ü∂ Y) :
    (MorphismProperty.regularEpi C) f ‚Üî IsRegularEpi f :=
  Iff.rfl

instance MorphismProperty.regularEpi.containsIdentities :
    (MorphismProperty.regularEpi C).ContainsIdentities where
  id_mem _ := ‚ü®‚ü®RegularEpi.ofIso <| Iso.refl _‚ü©‚ü©

instance MorphismProperty.regularEpi.respectsIso :
    (MorphismProperty.regularEpi C).RespectsIso :=
  RespectsIso.of_respects_arrow_iso _ (fun _ _ e h ‚Ü¶ ‚ü®‚ü®.ofArrowIso e (h := h.regularEpi.some)‚ü©‚ü©)

lemma isRegularEpi_of_regularEpi {f : X ‚ü∂ Y} (h : RegularEpi f) : IsRegularEpi f := ‚ü®‚ü®h‚ü©‚ü©

/-- Given `IsRegularEpi f`, a choice of data for `RegularEpi f`. -/
def IsRegularEpi.getStruct (f : X ‚ü∂ Y) [h : IsRegularEpi f] : RegularEpi f :=
  h.regularEpi.some

@[deprecated (since := "2025-12-01")] noncomputable alias regularEpiOfIsRegularEpi :=
  IsRegularEpi.getStruct

section IsRegularEpi

/-!

Given a regular epimorphism `f : X ‚ü∂ Y` (i.e. a morphism satisfying the predicate `IsRegularEpi`),
this section gives a coequalizer diagram
```
     W
left| |right
    v v
     X
    f|
     v
     Y
```
The names `W`, `left`, and `right` all being in the `IsRegularEpi` namespace.
-/

variable {X Y : C} (f : X ‚ü∂ Y) [IsRegularEpi f]

/-- The source of the coequalizer diagram for `f`. -/
def IsRegularEpi.W : C := (IsRegularEpi.getStruct f).W

/-- The "left" map `W ‚ü∂ X`. -/
def IsRegularEpi.left : W f ‚ü∂ X := (IsRegularEpi.getStruct f).left

/-- The "right" map `W ‚ü∂ X`. -/
def IsRegularEpi.right : W f ‚ü∂ X := (IsRegularEpi.getStruct f).right

/-- The coequalizer condition. -/
lemma IsRegularEpi.w : left f ‚â´ f = right f ‚â´ f := (IsRegularEpi.getStruct f).w

/-- The cofork is in fact a coequalizer. -/
def IsRegularEpi.isColimit : IsColimit <| Cofork.ofœÄ _ (w f) := (IsRegularEpi.getStruct f).isColimit

/--
Descend a morphism `k : X ‚ü∂ Z`, coequalized by the two morphisms `left` and `right`, along `f`.
-/
def IsRegularEpi.desc {Z : C} (f : X ‚ü∂ Y) [IsRegularEpi f] (k : X ‚ü∂ Z)
    (h : left f ‚â´ k = right f ‚â´ k) : Y ‚ü∂ Z :=
  Cofork.IsColimit.desc (isColimit f) k h

@[reassoc (attr := simp)]
lemma IsRegularEpi.fac {Z : C} (f : X ‚ü∂ Y) [IsRegularEpi f] (k : X ‚ü∂ Z)
    (h : left f ‚â´ k = right f ‚â´ k) : f ‚â´ desc f k h = k :=
  Cofork.IsColimit.œÄ_desc (isColimit f)

lemma IsRegularEpi.uniq {Z : C} (f : X ‚ü∂ Y) [IsRegularEpi f] (k : X ‚ü∂ Z)
    (h : left f ‚â´ k = right f ‚â´ k) (m : Y ‚ü∂ Z) (hm : f ‚â´ m = k) : m = desc f k h :=
  Cofork.IsColimit.existsUnique (isColimit f) k h |>.unique hm <| by simp

end IsRegularEpi

/-- The chosen coequalizer of a parallel pair is a regular epimorphism. -/
def coequalizerRegular (g h : X ‚ü∂ Y) [HasColimit (parallelPair g h)] :
    RegularEpi (coequalizer.œÄ g h) where
  W := X
  left := g
  right := h
  w := coequalizer.condition g h
  isColimit :=
    Cofork.IsColimit.mk _ (fun s => colimit.desc _ s) (by simp) fun s m w => by
      apply coequalizer.hom_ext
      simp [‚Üê w]

instance (g h : X ‚ü∂ Y) [HasColimit (parallelPair g h)] :
    IsRegularEpi (coequalizer.œÄ g h) :=
  ‚ü®‚ü®coequalizerRegular g h‚ü©‚ü©

/-- A morphism which is a coequalizer for its kernel pair is a regular epi. -/
def regularEpiOfKernelPair {B X : C} (f : X ‚ü∂ B) [HasPullback f f]
    (hc : IsColimit (Cofork.ofœÄ f pullback.condition)) : RegularEpi f where
  W := pullback f f
  left := pullback.fst f f
  right := pullback.snd f f
  w := pullback.condition
  isColimit := hc

/-- The data of an `EffectiveEpi` structure on a `RegularEpi`. -/
def effectiveEpiStructOfRegularEpi {B X : C} {f : X ‚ü∂ B} (hf : RegularEpi f) :
    EffectiveEpiStruct f where
  desc _ h := Cofork.IsColimit.desc hf.isColimit _ (h _ _ hf.w)
  fac _ _ := Cofork.IsColimit.œÄ_desc' hf.isColimit _ _
  uniq _ _ _ hg := Cofork.IsColimit.hom_ext hf.isColimit (hg.trans
    (Cofork.IsColimit.œÄ_desc' _ _ _).symm)

lemma RegularEpi.effectiveEpi {B X : C} {f : X ‚ü∂ B} (h : RegularEpi f) : EffectiveEpi f :=
  ‚ü®‚ü®effectiveEpiStructOfRegularEpi h‚ü©‚ü©

instance (priority := 100) {B X : C} {f : X ‚ü∂ B} [h : IsRegularEpi f] : EffectiveEpi f :=
  IsRegularEpi.getStruct f |>.effectiveEpi

/-- A morphism which is a coequalizer for its kernel pair is an effective epi. -/
theorem effectiveEpi_of_kernelPair {B X : C} (f : X ‚ü∂ B) [HasPullback f f]
    (hc : IsColimit (Cofork.ofœÄ f pullback.condition)) : EffectiveEpi f :=
  RegularEpi.effectiveEpi <| regularEpiOfKernelPair f hc

@[deprecated (since := "2025-11-20")] alias effectiveEpiOfKernelPair := effectiveEpi_of_kernelPair

/--
Given a kernel pair of an effective epimorphism `f : X ‚ü∂ B`, the induced cofork is a coequalizer.
-/
def isColimitCoforkOfEffectiveEpi {B X : C} (f : X ‚ü∂ B) [EffectiveEpi f]
    (c : PullbackCone f f) (hc : IsLimit c) :
    IsColimit (Cofork.ofœÄ f c.condition) where
  desc s := EffectiveEpi.desc f (s.Œπ.app WalkingParallelPair.one) fun g‚ÇÅ g‚ÇÇ hg ‚Ü¶ (by
      simp only [Cofork.app_one_eq_œÄ]
      rw [‚Üê PullbackCone.IsLimit.lift_snd hc g‚ÇÅ g‚ÇÇ hg, Category.assoc,
        ‚Üê Cofork.app_zero_eq_comp_œÄ_right]
      simp)
  fac s := by
    have := EffectiveEpi.fac f (s.Œπ.app WalkingParallelPair.one) fun g‚ÇÅ g‚ÇÇ hg ‚Ü¶ (by
      simp only [Cofork.app_one_eq_œÄ]
      rw [‚Üê PullbackCone.IsLimit.lift_snd hc g‚ÇÅ g‚ÇÇ hg,
        Category.assoc, ‚Üê Cofork.app_zero_eq_comp_œÄ_right]
      simp)
    rintro (_ | _)
    all_goals simp_all
  uniq _ _ h := EffectiveEpi.uniq f _ _ _ (h WalkingParallelPair.one)

/-- An effective epi which has a kernel pair is a regular epi. -/
def regularEpiOfEffectiveEpi {B X : C} (f : X ‚ü∂ B) [HasPullback f f]
    [EffectiveEpi f] : RegularEpi f where
  W := pullback f f
  left := pullback.fst f f
  right := pullback.snd f f
  w := pullback.condition
  isColimit := isColimitCoforkOfEffectiveEpi f _ (pullback.isLimit _ _)

instance isRegularEpi_of_EffectiveEpi {B X : C} (f : X ‚ü∂ B) [HasPullback f f]
    [EffectiveEpi f] : IsRegularEpi f :=
  isRegularEpi_of_regularEpi <| regularEpiOfEffectiveEpi f

lemma isRegularEpi_iff_effectiveEpi {B X : C} (f : X ‚ü∂ B) [HasPullback f f] :
    IsRegularEpi f ‚Üî EffectiveEpi f :=
  ‚ü®fun ‚ü®_‚ü© ‚Ü¶ inferInstance, fun _ ‚Ü¶ inferInstance‚ü©

/-- Let `p : Y ‚ü∂ X` be an effective epimorphism, `p‚ÇÅ : Z ‚ü∂ Y` and `p‚ÇÇ : Z ‚ü∂ Y` two
morphisms which make `Z` the pullback of two copies of `Y` over `X`.
Then, `Y ‚ü∂ X` is the coequalizer of `p‚ÇÅ` and `p‚ÇÇ`. -/
noncomputable def EffectiveEpiStruct.isColimitCoforkOfIsPullback
    {X Y Z : C} {p : Y ‚ü∂ X} (hp : EffectiveEpiStruct p) {p‚ÇÅ p‚ÇÇ : Z ‚ü∂ Y}
    (sq : IsPullback p‚ÇÅ p‚ÇÇ p p) :
    IsColimit (Cofork.ofœÄ p sq.w) :=
  Cofork.IsColimit.mk _ (fun s ‚Ü¶ hp.desc s.œÄ (fun {T} g‚ÇÅ g‚ÇÇ h ‚Ü¶ by
      obtain ‚ü®l, rfl, rfl‚ü© := sq.exists_lift g‚ÇÅ g‚ÇÇ h
      simp [s.condition]))
    (fun s ‚Ü¶ hp.fac _ _)
    (fun s m hm ‚Ü¶ hp.uniq _ _ _ hm)

/-- Every split epimorphism is a regular epimorphism. -/
def RegularEpi.ofSplitEpi (f : X ‚ü∂ Y) [IsSplitEpi f] : RegularEpi f where
  W := X
  left := ùüô X
  right := f ‚â´ section_ f
  isColimit := isSplitEpiCoequalizes f

instance (priority := 100) (f : X ‚ü∂ Y) [IsSplitEpi f] : IsRegularEpi f :=
  isRegularEpi_of_regularEpi <| RegularEpi.ofSplitEpi f

/-- If `f` is a regular epi, then every morphism `k : X ‚ü∂ W` coequalizing `RegularEpi.left` and
`RegularEpi.right` induces `l : Y ‚ü∂ W` such that `f ‚â´ l = k`. -/
def RegularEpi.desc' {W : C} {f : X ‚ü∂ Y} (hf : RegularEpi f) (k : X ‚ü∂ W)
    (h : hf.left ‚â´ k = hf.right ‚â´ k) :
    { l : Y ‚ü∂ W // f ‚â´ l = k } :=
  Cofork.IsColimit.desc' hf.isColimit _ h

/-- The second leg of a pushout cocone is a regular epimorphism if the right component is too.

See also `Pushout.sndOfEpi` for the basic epimorphism version, and
`regularOfIsPushoutFstOfRegular` for the flipped version.
-/
def regularOfIsPushoutSndOfRegular {P Q R S : C} {f : P ‚ü∂ Q} {g : P ‚ü∂ R} {h : Q ‚ü∂ S} {k : R ‚ü∂ S}
    (gr : RegularEpi g) (comm : f ‚â´ h = g ‚â´ k) (t : IsColimit (PushoutCocone.mk _ _ comm)) :
    RegularEpi h where
  W := gr.W
  left := gr.left ‚â´ f
  right := gr.right ‚â´ f
  w := by rw [Category.assoc, Category.assoc, comm]; simp only [‚Üê Category.assoc, eq_whisker gr.w]
  isColimit := by
    apply Cofork.IsColimit.mk' _ _
    intro s
    have l‚ÇÅ : gr.left ‚â´ f ‚â´ s.œÄ = gr.right ‚â´ f ‚â´ s.œÄ := by
      rw [‚Üê Category.assoc, ‚Üê Category.assoc, s.condition]
    obtain ‚ü®l, hl‚ü© := Cofork.IsColimit.desc' gr.isColimit (f ‚â´ Cofork.œÄ s) l‚ÇÅ
    obtain ‚ü®p, hp‚ÇÅ, _‚ü© := PushoutCocone.IsColimit.desc' t _ _ hl.symm
    refine ‚ü®p, hp‚ÇÅ, ?_‚ü©
    intro m w
    have z := w.trans hp‚ÇÅ.symm
    apply t.hom_ext
    have := gr.epi
    apply (PushoutCocone.mk _ _ comm).coequalizer_ext
    ¬∑ exact z
    ¬∑ erw [‚Üê cancel_epi g, ‚Üê Category.assoc, ‚Üê eq_whisker comm]
      erw [‚Üê Category.assoc, ‚Üê eq_whisker comm]
      dsimp at z; simp only [Category.assoc, z]

/-- The first leg of a pushout cocone is a regular epimorphism if the left component is too.

See also `Pushout.fstOfEpi` for the basic epimorphism version, and
`regularOfIsPushoutSndOfRegular` for the flipped version.
-/
def regularOfIsPushoutFstOfRegular {P Q R S : C} {f : P ‚ü∂ Q} {g : P ‚ü∂ R} {h : Q ‚ü∂ S} {k : R ‚ü∂ S}
    (hf : RegularEpi f) (comm : f ‚â´ h = g ‚â´ k) (t : IsColimit (PushoutCocone.mk _ _ comm)) :
    RegularEpi k :=
  regularOfIsPushoutSndOfRegular hf comm.symm (PushoutCocone.flipIsColimit t)

@[deprecated "No replacement" (since := "2025-11-20")]
lemma strongEpi_of_regularEpi (f : X ‚ü∂ Y) (h : RegularEpi f) : StrongEpi f :=
  have := isRegularEpi_of_regularEpi h
  inferInstance

/-- A regular epimorphism is an isomorphism if it is a monomorphism. -/
theorem isIso_of_regularEpi_of_mono (f : X ‚ü∂ Y) (h : RegularEpi f) [Mono f] : IsIso f :=
  have := isRegularEpi_of_regularEpi h
  isIso_of_mono_of_strongEpi _

section

variable (C)

/-- A regular epi category is a category in which every epimorphism is regular. -/
class IsRegularEpiCategory : Prop where
  /-- Everyone epimorphism is a regular epimorphism -/
  regularEpiOfEpi : ‚àÄ {X Y : C} (f : X ‚ü∂ Y) [Epi f], IsRegularEpi f

end

/-- In a category in which every epimorphism is regular, we can express every epimorphism as
a coequalizer. This is not an instance because it would create an instance loop. -/
def regularEpiOfEpi [IsRegularEpiCategory C] (f : X ‚ü∂ Y) [Epi f] : RegularEpi f :=
  have := IsRegularEpiCategory.regularEpiOfEpi f
  IsRegularEpi.getStruct f

instance (priority := 100) regularEpiCategoryOfSplitEpiCategory [SplitEpiCategory C] :
    IsRegularEpiCategory C where
  regularEpiOfEpi f _ := by
    haveI := isSplitEpi_of_epi f
    infer_instance

instance (priority := 100) strongEpiCategory_of_regularEpiCategory [IsRegularEpiCategory C] :
    StrongEpiCategory C where
  strongEpi_of_epi f _ := by
    haveI := isRegularEpi_of_regularEpi <| regularEpiOfEpi f
    infer_instance

end CategoryTheory
