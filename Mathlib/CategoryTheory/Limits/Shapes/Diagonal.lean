/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.CategoryTheory.Limits.Shapes.KernelPair
import Mathlib.CategoryTheory.Limits.Shapes.CommSq
import Mathlib.CategoryTheory.Adjunction.Over

#align_import category_theory.limits.shapes.diagonal from "leanprover-community/mathlib"@"f6bab67886fb92c3e2f539cc90a83815f69a189d"

/-!
# The diagonal object of a morphism.

We provide various API and isomorphisms considering the diagonal object `Î”_{Y/X} := pullback f f`
of a morphism `f : X âŸ¶ Y`.

-/


open CategoryTheory

noncomputable section

namespace CategoryTheory.Limits

variable {C : Type*} [Category C] {X Y Z : C}

namespace pullback

section Diagonal

variable (f : X âŸ¶ Y) [HasPullback f f]

/-- The diagonal object of a morphism `f : X âŸ¶ Y` is `Î”_{X/Y} := pullback f f`. -/
abbrev diagonalObj : C :=
  pullback f f
#align category_theory.limits.pullback.diagonal_obj CategoryTheory.Limits.pullback.diagonalObj

/-- The diagonal morphism `X âŸ¶ Î”_{X/Y}` for a morphism `f : X âŸ¶ Y`. -/
def diagonal : X âŸ¶ diagonalObj f :=
  pullback.lift (ğŸ™ _) (ğŸ™ _) rfl
#align category_theory.limits.pullback.diagonal CategoryTheory.Limits.pullback.diagonal

@[reassoc (attr := simp)]
theorem diagonal_fst : diagonal f â‰« pullback.fst = ğŸ™ _ :=
  pullback.lift_fst _ _ _
#align category_theory.limits.pullback.diagonal_fst CategoryTheory.Limits.pullback.diagonal_fst

@[reassoc (attr := simp)]
theorem diagonal_snd : diagonal f â‰« pullback.snd = ğŸ™ _ :=
  pullback.lift_snd _ _ _
#align category_theory.limits.pullback.diagonal_snd CategoryTheory.Limits.pullback.diagonal_snd

instance : IsSplitMono (diagonal f) :=
  âŸ¨âŸ¨âŸ¨pullback.fst, diagonal_fst fâŸ©âŸ©âŸ©

instance : IsSplitEpi (pullback.fst : pullback f f âŸ¶ X) :=
  âŸ¨âŸ¨âŸ¨diagonal f, diagonal_fst fâŸ©âŸ©âŸ©

instance : IsSplitEpi (pullback.snd : pullback f f âŸ¶ X) :=
  âŸ¨âŸ¨âŸ¨diagonal f, diagonal_snd fâŸ©âŸ©âŸ©

instance [Mono f] : IsIso (diagonal f) := by
  rw [(IsIso.inv_eq_of_inv_hom_id (diagonal_fst f)).symm]
  infer_instance

/-- The two projections `Î”_{X/Y} âŸ¶ X` form a kernel pair for `f : X âŸ¶ Y`. -/
theorem diagonal_isKernelPair : IsKernelPair f (pullback.fst : diagonalObj f âŸ¶ _) pullback.snd :=
  IsPullback.of_hasPullback f f
#align category_theory.limits.pullback.diagonal_is_kernel_pair CategoryTheory.Limits.pullback.diagonal_isKernelPair

end Diagonal

end pullback

variable [HasPullbacks C]

open pullback

section

variable {U Vâ‚ Vâ‚‚ : C} (f : X âŸ¶ Y) (i : U âŸ¶ Y)
variable (iâ‚ : Vâ‚ âŸ¶ pullback f i) (iâ‚‚ : Vâ‚‚ âŸ¶ pullback f i)

@[reassoc (attr := simp)]
theorem pullback_diagonal_map_snd_fst_fst :
    (pullback.snd :
          pullback (diagonal f)
              (map (iâ‚ â‰« snd) (iâ‚‚ â‰« snd) f f (iâ‚ â‰« fst) (iâ‚‚ â‰« fst) i (by simp [condition])
                (by simp [condition])) âŸ¶
            _) â‰«
        fst â‰« iâ‚ â‰« fst =
      pullback.fst := by
  conv_rhs => rw [â† Category.comp_id pullback.fst]
  rw [â† diagonal_fst f, pullback.condition_assoc, pullback.lift_fst]
#align category_theory.limits.pullback_diagonal_map_snd_fst_fst CategoryTheory.Limits.pullback_diagonal_map_snd_fst_fst

@[reassoc (attr := simp)]
theorem pullback_diagonal_map_snd_snd_fst :
    (pullback.snd :
          pullback (diagonal f)
              (map (iâ‚ â‰« snd) (iâ‚‚ â‰« snd) f f (iâ‚ â‰« fst) (iâ‚‚ â‰« fst) i (by simp [condition])
                (by simp [condition])) âŸ¶
            _) â‰«
        snd â‰« iâ‚‚ â‰« fst =
      pullback.fst := by
  conv_rhs => rw [â† Category.comp_id pullback.fst]
  rw [â† diagonal_snd f, pullback.condition_assoc, pullback.lift_snd]
#align category_theory.limits.pullback_diagonal_map_snd_snd_fst CategoryTheory.Limits.pullback_diagonal_map_snd_snd_fst

variable [HasPullback iâ‚ iâ‚‚]

set_option maxHeartbeats 400000 in
/-- This iso witnesses the fact that
given `f : X âŸ¶ Y`, `i : U âŸ¶ Y`, and `iâ‚ : Vâ‚ âŸ¶ X Ã—[Y] U`, `iâ‚‚ : Vâ‚‚ âŸ¶ X Ã—[Y] U`, the diagram

Vâ‚ Ã—[X Ã—[Y] U] Vâ‚‚ âŸ¶ Vâ‚ Ã—[U] Vâ‚‚
        |                 |
        |                 |
        â†“                 â†“
        X        âŸ¶ X Ã—[Y] X

is a pullback square.
Also see `pullback_fst_map_snd_isPullback`.
-/
def pullbackDiagonalMapIso :
    pullback (diagonal f)
        (map (iâ‚ â‰« snd) (iâ‚‚ â‰« snd) f f (iâ‚ â‰« fst) (iâ‚‚ â‰« fst) i
          (by simp only [Category.assoc, condition])
          (by simp only [Category.assoc, condition])) â‰…
      pullback iâ‚ iâ‚‚ where
  hom :=
    pullback.lift (pullback.snd â‰« pullback.fst) (pullback.snd â‰« pullback.snd) (by
      ext
      Â· simp [Category.assoc, pullback_diagonal_map_snd_fst_fst, pullback_diagonal_map_snd_snd_fst]
      Â· simp [Category.assoc, pullback.condition, pullback.condition_assoc])
  inv :=
    pullback.lift (pullback.fst â‰« iâ‚ â‰« pullback.fst)
      (pullback.map _ _ _ _ (ğŸ™ _) (ğŸ™ _) pullback.snd (Category.id_comp _).symm
        (Category.id_comp _).symm) (by
        ext
        Â· simp only [Category.assoc, diagonal_fst, Category.comp_id, limit.lift_Ï€,
            PullbackCone.mk_pt, PullbackCone.mk_Ï€_app, limit.lift_Ï€_assoc, cospan_left]
        Â· simp only [condition_assoc, Category.assoc, diagonal_snd, Category.comp_id,
            limit.lift_Ï€, PullbackCone.mk_pt, PullbackCone.mk_Ï€_app,
            limit.lift_Ï€_assoc, cospan_right])
#align category_theory.limits.pullback_diagonal_map_iso CategoryTheory.Limits.pullbackDiagonalMapIso

@[reassoc (attr := simp)]
theorem pullbackDiagonalMapIso_hom_fst :
    (pullbackDiagonalMapIso f i iâ‚ iâ‚‚).hom â‰« pullback.fst = pullback.snd â‰« pullback.fst := by
  delta pullbackDiagonalMapIso
  simp
#align category_theory.limits.pullback_diagonal_map_iso_hom_fst CategoryTheory.Limits.pullbackDiagonalMapIso_hom_fst

@[reassoc (attr := simp)]
theorem pullbackDiagonalMapIso_hom_snd :
    (pullbackDiagonalMapIso f i iâ‚ iâ‚‚).hom â‰« pullback.snd = pullback.snd â‰« pullback.snd := by
  delta pullbackDiagonalMapIso
  simp
#align category_theory.limits.pullback_diagonal_map_iso_hom_snd CategoryTheory.Limits.pullbackDiagonalMapIso_hom_snd

@[reassoc (attr := simp)]
theorem pullbackDiagonalMapIso_inv_fst :
    (pullbackDiagonalMapIso f i iâ‚ iâ‚‚).inv â‰« pullback.fst = pullback.fst â‰« iâ‚ â‰« pullback.fst := by
  delta pullbackDiagonalMapIso
  simp
#align category_theory.limits.pullback_diagonal_map_iso_inv_fst CategoryTheory.Limits.pullbackDiagonalMapIso_inv_fst

@[reassoc (attr := simp)]
theorem pullbackDiagonalMapIso_inv_snd_fst :
    (pullbackDiagonalMapIso f i iâ‚ iâ‚‚).inv â‰« pullback.snd â‰« pullback.fst = pullback.fst := by
  delta pullbackDiagonalMapIso
  simp
#align category_theory.limits.pullback_diagonal_map_iso_inv_snd_fst CategoryTheory.Limits.pullbackDiagonalMapIso_inv_snd_fst

@[reassoc (attr := simp)]
theorem pullbackDiagonalMapIso_inv_snd_snd :
    (pullbackDiagonalMapIso f i iâ‚ iâ‚‚).inv â‰« pullback.snd â‰« pullback.snd = pullback.snd := by
  delta pullbackDiagonalMapIso
  simp
#align category_theory.limits.pullback_diagonal_map_iso_inv_snd_snd CategoryTheory.Limits.pullbackDiagonalMapIso_inv_snd_snd

theorem pullback_fst_map_snd_isPullback :
    IsPullback (fst â‰« iâ‚ â‰« fst)
      (map iâ‚ iâ‚‚ (iâ‚ â‰« snd) (iâ‚‚ â‰« snd) _ _ _ (Category.id_comp _).symm (Category.id_comp _).symm)
      (diagonal f)
      (map (iâ‚ â‰« snd) (iâ‚‚ â‰« snd) f f (iâ‚ â‰« fst) (iâ‚‚ â‰« fst) i (by simp [condition])
        (by simp [condition])) :=
  IsPullback.of_iso_pullback âŸ¨by ext <;> simp [condition_assoc]âŸ©
    (pullbackDiagonalMapIso f i iâ‚ iâ‚‚).symm (pullbackDiagonalMapIso_inv_fst f i iâ‚ iâ‚‚)
    (by aesop_cat)
#align category_theory.limits.pullback_fst_map_snd_is_pullback CategoryTheory.Limits.pullback_fst_map_snd_isPullback

end

section

variable {S T : C} (f : X âŸ¶ T) (g : Y âŸ¶ T) (i : T âŸ¶ S)
variable [HasPullback i i] [HasPullback f g] [HasPullback (f â‰« i) (g â‰« i)]
variable
  [HasPullback (diagonal i)
      (pullback.map (f â‰« i) (g â‰« i) i i f g (ğŸ™ _) (Category.comp_id _) (Category.comp_id _))]

/-- This iso witnesses the fact that
given `f : X âŸ¶ T`, `g : Y âŸ¶ T`, and `i : T âŸ¶ S`, the diagram

X Ã—â‚œ Y âŸ¶ X Ã—â‚› Y
  |         |
  |         |
  â†“         â†“
  T  âŸ¶  T Ã—â‚› T

is a pullback square.
Also see `pullback_map_diagonal_isPullback`.
-/
def pullbackDiagonalMapIdIso :
    pullback (diagonal i)
        (pullback.map (f â‰« i) (g â‰« i) i i f g (ğŸ™ _) (Category.comp_id _) (Category.comp_id _)) â‰…
      pullback f g := by
  refine ?_ â‰ªâ‰«
    pullbackDiagonalMapIso i (ğŸ™ _) (f â‰« inv pullback.fst) (g â‰« inv pullback.fst) â‰ªâ‰« ?_
  Â· refine @asIso _ _ _ _ (pullback.map _ _ _ _ (ğŸ™ T) ((pullback.congrHom ?_ ?_).hom) (ğŸ™ _) ?_ ?_)
      ?_
    Â· rw [â† Category.comp_id pullback.snd, â† condition, Category.assoc, IsIso.inv_hom_id_assoc]
    Â· rw [â† Category.comp_id pullback.snd, â† condition, Category.assoc, IsIso.inv_hom_id_assoc]
    Â· rw [Category.comp_id, Category.id_comp]
    Â· ext <;> simp
    Â· infer_instance
  Â· refine @asIso _ _ _ _ (pullback.map _ _ _ _ (ğŸ™ _) (ğŸ™ _) pullback.fst ?_ ?_) ?_
    Â· rw [Category.assoc, IsIso.inv_hom_id, Category.comp_id, Category.id_comp]
    Â· rw [Category.assoc, IsIso.inv_hom_id, Category.comp_id, Category.id_comp]
    Â· infer_instance
#align category_theory.limits.pullback_diagonal_map_id_iso CategoryTheory.Limits.pullbackDiagonalMapIdIso

@[reassoc (attr := simp)]
theorem pullbackDiagonalMapIdIso_hom_fst :
    (pullbackDiagonalMapIdIso f g i).hom â‰« pullback.fst = pullback.snd â‰« pullback.fst := by
  delta pullbackDiagonalMapIdIso
  simp
#align category_theory.limits.pullback_diagonal_map_id_iso_hom_fst CategoryTheory.Limits.pullbackDiagonalMapIdIso_hom_fst

@[reassoc (attr := simp)]
theorem pullbackDiagonalMapIdIso_hom_snd :
    (pullbackDiagonalMapIdIso f g i).hom â‰« pullback.snd = pullback.snd â‰« pullback.snd := by
  delta pullbackDiagonalMapIdIso
  simp
#align category_theory.limits.pullback_diagonal_map_id_iso_hom_snd CategoryTheory.Limits.pullbackDiagonalMapIdIso_hom_snd

@[reassoc (attr := simp)]
theorem pullbackDiagonalMapIdIso_inv_fst :
    (pullbackDiagonalMapIdIso f g i).inv â‰« pullback.fst = pullback.fst â‰« f := by
  rw [Iso.inv_comp_eq, â† Category.comp_id pullback.fst, â† diagonal_fst i, pullback.condition_assoc]
  simp
#align category_theory.limits.pullback_diagonal_map_id_iso_inv_fst CategoryTheory.Limits.pullbackDiagonalMapIdIso_inv_fst

@[reassoc (attr := simp)]
theorem pullbackDiagonalMapIdIso_inv_snd_fst :
    (pullbackDiagonalMapIdIso f g i).inv â‰« pullback.snd â‰« pullback.fst = pullback.fst := by
  rw [Iso.inv_comp_eq]
  simp
#align category_theory.limits.pullback_diagonal_map_id_iso_inv_snd_fst CategoryTheory.Limits.pullbackDiagonalMapIdIso_inv_snd_fst

@[reassoc (attr := simp)]
theorem pullbackDiagonalMapIdIso_inv_snd_snd :
    (pullbackDiagonalMapIdIso f g i).inv â‰« pullback.snd â‰« pullback.snd = pullback.snd := by
  rw [Iso.inv_comp_eq]
  simp
#align category_theory.limits.pullback_diagonal_map_id_iso_inv_snd_snd CategoryTheory.Limits.pullbackDiagonalMapIdIso_inv_snd_snd

theorem pullback.diagonal_comp (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [HasPullback f f] [HasPullback g g]
    [HasPullback (f â‰« g) (f â‰« g)] :
    diagonal (f â‰« g) = diagonal f â‰« (pullbackDiagonalMapIdIso f f g).inv â‰« pullback.snd := by
  ext <;> simp
#align category_theory.limits.pullback.diagonal_comp CategoryTheory.Limits.pullback.diagonal_comp

theorem pullback_map_diagonal_isPullback :
    IsPullback (pullback.fst â‰« f)
      (pullback.map f g (f â‰« i) (g â‰« i) _ _ i (Category.id_comp _).symm (Category.id_comp _).symm)
      (diagonal i)
      (pullback.map (f â‰« i) (g â‰« i) i i f g (ğŸ™ _) (Category.comp_id _) (Category.comp_id _)) := by
  apply IsPullback.of_iso_pullback _ (pullbackDiagonalMapIdIso f g i).symm
  Â· simp
  Â· ext <;> simp
  Â· constructor
    ext <;> simp [condition]
#align category_theory.limits.pullback_map_diagonal_is_pullback CategoryTheory.Limits.pullback_map_diagonal_isPullback

/-- The diagonal object of `X Ã—[Z] Y âŸ¶ X` is isomorphic to `Î”_{Y/Z} Ã—[Z] X`. -/
def diagonalObjPullbackFstIso {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
    diagonalObj (pullback.fst : pullback f g âŸ¶ X) â‰…
      pullback (pullback.snd â‰« g : diagonalObj g âŸ¶ Z) f :=
  pullbackRightPullbackFstIso _ _ _ â‰ªâ‰«
    pullback.congrHom pullback.condition rfl â‰ªâ‰«
      pullbackAssoc _ _ _ _ â‰ªâ‰« pullbackSymmetry _ _ â‰ªâ‰« pullback.congrHom pullback.condition rfl
#align category_theory.limits.diagonal_obj_pullback_fst_iso CategoryTheory.Limits.diagonalObjPullbackFstIso

@[reassoc (attr := simp)]
theorem diagonalObjPullbackFstIso_hom_fst_fst {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
    (diagonalObjPullbackFstIso f g).hom â‰« pullback.fst â‰« pullback.fst =
      pullback.fst â‰« pullback.snd := by
  delta diagonalObjPullbackFstIso
  simp
#align category_theory.limits.diagonal_obj_pullback_fst_iso_hom_fst_fst CategoryTheory.Limits.diagonalObjPullbackFstIso_hom_fst_fst

@[reassoc (attr := simp)]
theorem diagonalObjPullbackFstIso_hom_fst_snd {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
    (diagonalObjPullbackFstIso f g).hom â‰« pullback.fst â‰« pullback.snd =
      pullback.snd â‰« pullback.snd := by
  delta diagonalObjPullbackFstIso
  simp
#align category_theory.limits.diagonal_obj_pullback_fst_iso_hom_fst_snd CategoryTheory.Limits.diagonalObjPullbackFstIso_hom_fst_snd

@[reassoc (attr := simp)]
theorem diagonalObjPullbackFstIso_hom_snd {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
    (diagonalObjPullbackFstIso f g).hom â‰« pullback.snd = pullback.fst â‰« pullback.fst := by
  delta diagonalObjPullbackFstIso
  simp
#align category_theory.limits.diagonal_obj_pullback_fst_iso_hom_snd CategoryTheory.Limits.diagonalObjPullbackFstIso_hom_snd

@[reassoc (attr := simp)]
theorem diagonalObjPullbackFstIso_inv_fst_fst {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
    (diagonalObjPullbackFstIso f g).inv â‰« pullback.fst â‰« pullback.fst = pullback.snd := by
  delta diagonalObjPullbackFstIso
  simp
#align category_theory.limits.diagonal_obj_pullback_fst_iso_inv_fst_fst CategoryTheory.Limits.diagonalObjPullbackFstIso_inv_fst_fst

@[reassoc (attr := simp)]
theorem diagonalObjPullbackFstIso_inv_fst_snd {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
    (diagonalObjPullbackFstIso f g).inv â‰« pullback.fst â‰« pullback.snd =
      pullback.fst â‰« pullback.fst := by
  delta diagonalObjPullbackFstIso
  simp
#align category_theory.limits.diagonal_obj_pullback_fst_iso_inv_fst_snd CategoryTheory.Limits.diagonalObjPullbackFstIso_inv_fst_snd

@[reassoc (attr := simp)]
theorem diagonalObjPullbackFstIso_inv_snd_fst {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
    (diagonalObjPullbackFstIso f g).inv â‰« pullback.snd â‰« pullback.fst = pullback.snd := by
  delta diagonalObjPullbackFstIso
  simp
#align category_theory.limits.diagonal_obj_pullback_fst_iso_inv_snd_fst CategoryTheory.Limits.diagonalObjPullbackFstIso_inv_snd_fst

@[reassoc (attr := simp)]
theorem diagonalObjPullbackFstIso_inv_snd_snd {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
    (diagonalObjPullbackFstIso f g).inv â‰« pullback.snd â‰« pullback.snd =
      pullback.fst â‰« pullback.snd := by
  delta diagonalObjPullbackFstIso
  simp
#align category_theory.limits.diagonal_obj_pullback_fst_iso_inv_snd_snd CategoryTheory.Limits.diagonalObjPullbackFstIso_inv_snd_snd

theorem diagonal_pullback_fst {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
    diagonal (pullback.fst : pullback f g âŸ¶ _) =
      (pullbackSymmetry _ _).hom â‰«
        ((Over.baseChange f).map
              (Over.homMk (diagonal g) : Over.mk g âŸ¶ Over.mk (pullback.snd â‰« g))).left â‰«
          (diagonalObjPullbackFstIso f g).inv := by
  ext <;> dsimp <;> simp
#align category_theory.limits.diagonal_pullback_fst CategoryTheory.Limits.diagonal_pullback_fst

end

/-- Given the following diagram with `S âŸ¶ S'` a monomorphism,

    X âŸ¶ X'
      â†˜      â†˜
        S âŸ¶ S'
      â†—      â†—
    Y âŸ¶ Y'

This iso witnesses the fact that

      X Ã—[S] Y âŸ¶ (X' Ã—[S'] Y') Ã—[Y'] Y
          |                  |
          |                  |
          â†“                  â†“
(X' Ã—[S'] Y') Ã—[X'] X âŸ¶ X' Ã—[S'] Y'

is a pullback square. The diagonal map of this square is `pullback.map`.
Also see `pullback_lift_map_is_pullback`.
-/
@[simps]
def pullbackFstFstIso {X Y S X' Y' S' : C} (f : X âŸ¶ S) (g : Y âŸ¶ S) (f' : X' âŸ¶ S') (g' : Y' âŸ¶ S')
    (iâ‚ : X âŸ¶ X') (iâ‚‚ : Y âŸ¶ Y') (iâ‚ƒ : S âŸ¶ S') (eâ‚ : f â‰« iâ‚ƒ = iâ‚ â‰« f') (eâ‚‚ : g â‰« iâ‚ƒ = iâ‚‚ â‰« g')
    [Mono iâ‚ƒ] :
    pullback (pullback.fst : pullback (pullback.fst : pullback f' g' âŸ¶ _) iâ‚ âŸ¶ _)
        (pullback.fst : pullback (pullback.snd : pullback f' g' âŸ¶ _) iâ‚‚ âŸ¶ _) â‰…
      pullback f g where
  hom :=
    pullback.lift (pullback.fst â‰« pullback.snd) (pullback.snd â‰« pullback.snd)
      (by
        rw [â† cancel_mono iâ‚ƒ, Category.assoc, Category.assoc, Category.assoc, Category.assoc, eâ‚,
          eâ‚‚, â† pullback.condition_assoc, pullback.condition_assoc, pullback.condition,
          pullback.condition_assoc])
  inv :=
    pullback.lift
      (pullback.lift (pullback.map _ _ _ _ _ _ _ eâ‚ eâ‚‚) pullback.fst (pullback.lift_fst _ _ _))
      (pullback.lift (pullback.map _ _ _ _ _ _ _ eâ‚ eâ‚‚) pullback.snd (pullback.lift_snd _ _ _))
      (by rw [pullback.lift_fst, pullback.lift_fst])
  hom_inv_id := by
    -- We could use `ext` here to immediately descend to the leaf goals,
    -- but it only obscures the structure.
    apply pullback.hom_ext
    Â· apply pullback.hom_ext
      Â· apply pullback.hom_ext
        Â· simp only [Category.assoc, lift_fst, lift_fst_assoc, Category.id_comp]
          rw [condition]
        Â· simp [Category.assoc, lift_snd]
          rw [condition_assoc, condition]
      Â· simp only [Category.assoc, lift_fst_assoc, lift_snd, lift_fst, Category.id_comp]
    Â· apply pullback.hom_ext
      Â· apply pullback.hom_ext
        Â· simp only [Category.assoc, lift_snd_assoc, lift_fst_assoc, lift_fst, Category.id_comp]
          rw [â† condition_assoc, condition]
        Â· simp only [Category.assoc, lift_snd, lift_fst_assoc, lift_snd_assoc, Category.id_comp]
          rw [condition]
      Â· simp only [Category.assoc, lift_snd_assoc, lift_snd, Category.id_comp]
  inv_hom_id := by
    apply pullback.hom_ext
    Â· simp only [Category.assoc, lift_fst, lift_fst_assoc, lift_snd, Category.id_comp]
    Â· simp only [Category.assoc, lift_snd, lift_snd_assoc, Category.id_comp]
#align category_theory.limits.pullback_fst_fst_iso CategoryTheory.Limits.pullbackFstFstIso

theorem pullback_map_eq_pullbackFstFstIso_inv {X Y S X' Y' S' : C} (f : X âŸ¶ S) (g : Y âŸ¶ S)
    (f' : X' âŸ¶ S') (g' : Y' âŸ¶ S') (iâ‚ : X âŸ¶ X') (iâ‚‚ : Y âŸ¶ Y') (iâ‚ƒ : S âŸ¶ S') (eâ‚ : f â‰« iâ‚ƒ = iâ‚ â‰« f')
    (eâ‚‚ : g â‰« iâ‚ƒ = iâ‚‚ â‰« g') [Mono iâ‚ƒ] :
    pullback.map f g f' g' iâ‚ iâ‚‚ iâ‚ƒ eâ‚ eâ‚‚ =
      (pullbackFstFstIso f g f' g' iâ‚ iâ‚‚ iâ‚ƒ eâ‚ eâ‚‚).inv â‰« pullback.snd â‰« pullback.fst := by
  simp only [pullbackFstFstIso_inv, lift_snd_assoc, lift_fst]
#align category_theory.limits.pullback_map_eq_pullback_fst_fst_iso_inv CategoryTheory.Limits.pullback_map_eq_pullbackFstFstIso_inv

theorem pullback_lift_map_isPullback {X Y S X' Y' S' : C} (f : X âŸ¶ S) (g : Y âŸ¶ S) (f' : X' âŸ¶ S')
    (g' : Y' âŸ¶ S') (iâ‚ : X âŸ¶ X') (iâ‚‚ : Y âŸ¶ Y') (iâ‚ƒ : S âŸ¶ S') (eâ‚ : f â‰« iâ‚ƒ = iâ‚ â‰« f')
    (eâ‚‚ : g â‰« iâ‚ƒ = iâ‚‚ â‰« g') [Mono iâ‚ƒ] :
    IsPullback (pullback.lift (pullback.map f g f' g' iâ‚ iâ‚‚ iâ‚ƒ eâ‚ eâ‚‚) fst (lift_fst _ _ _))
      (pullback.lift (pullback.map f g f' g' iâ‚ iâ‚‚ iâ‚ƒ eâ‚ eâ‚‚) snd (lift_snd _ _ _)) pullback.fst
      pullback.fst :=
  IsPullback.of_iso_pullback âŸ¨by rw [lift_fst, lift_fst]âŸ©
    (pullbackFstFstIso f g f' g' iâ‚ iâ‚‚ iâ‚ƒ eâ‚ eâ‚‚).symm (by simp) (by simp)
#align category_theory.limits.pullback_lift_map_is_pullback CategoryTheory.Limits.pullback_lift_map_isPullback

end CategoryTheory.Limits
