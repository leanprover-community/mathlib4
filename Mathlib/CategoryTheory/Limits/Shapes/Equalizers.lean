/-
Copyright (c) 2018 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Markus Himmel
-/
import Mathlib.CategoryTheory.EpiMono
import Mathlib.CategoryTheory.Limits.HasLimits
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.HasPullback

/-!
# Equalizers and coequalizers

This file defines (co)equalizers as special cases of (co)limits.

An equalizer is the categorical generalization of the subobject {a âˆˆ A | f(a) = g(a)} known
from abelian groups or modules. It is a limit cone over the diagram formed by `f` and `g`.

A coequalizer is the dual concept.

## Main definitions

* `WalkingParallelPair` is the indexing category used for (co)equalizer_diagrams
* `parallelPair` is a functor from `WalkingParallelPair` to our category `C`.
* a `fork` is a cone over a parallel pair.
  * there is really only one interesting morphism in a fork: the arrow from the vertex of the fork
    to the domain of f and g. It is called `fork.Î¹`.
* an `equalizer` is now just a `limit (parallelPair f g)`

Each of these has a dual.

## Main statements

* `equalizer.Î¹_mono` states that every equalizer map is a monomorphism
* `isIso_limit_cone_parallelPair_of_self` states that the identity on the domain of `f` is an
  equalizer of `f` and `f`.

## Implementation notes
As with the other special shapes in the limits library, all the definitions here are given as
`abbreviation`s of the general statements for limits, so all the `simp` lemmas and theorems about
general limits can be used.

## References

* [F. Borceux, *Handbook of Categorical Algebra 1*][borceux-vol1]
-/

section

open CategoryTheory Opposite

namespace CategoryTheory.Limits

universe v vâ‚‚ u uâ‚‚

/-- The type of objects for the diagram indexing a (co)equalizer. -/
inductive WalkingParallelPair : Type
  | zero
  | one
  deriving DecidableEq, Inhabited

open WalkingParallelPair

-- Don't generate unnecessary `sizeOf_spec` lemma which the `simpNF` linter will complain about.
set_option genSizeOfSpec false in
/-- The type family of morphisms for the diagram indexing a (co)equalizer. -/
inductive WalkingParallelPairHom : WalkingParallelPair â†’ WalkingParallelPair â†’ Type
  | left : WalkingParallelPairHom zero one
  | right : WalkingParallelPairHom zero one
  | id (X : WalkingParallelPair) : WalkingParallelPairHom X X
  deriving DecidableEq

/-- Satisfying the inhabited linter -/
instance : Inhabited (WalkingParallelPairHom zero one) where default := WalkingParallelPairHom.left

open WalkingParallelPairHom

/-- Composition of morphisms in the indexing diagram for (co)equalizers. -/
def WalkingParallelPairHom.comp :
    âˆ€ {X Y Z : WalkingParallelPair} (_ : WalkingParallelPairHom X Y)
      (_ : WalkingParallelPairHom Y Z), WalkingParallelPairHom X Z
  | _, _, _, id _, h => h
  | _, _, _, left, id one => left
  | _, _, _, right, id one => right

theorem WalkingParallelPairHom.id_comp
    {X Y : WalkingParallelPair} (g : WalkingParallelPairHom X Y) : comp (id X) g = g :=
  rfl

theorem WalkingParallelPairHom.comp_id
    {X Y : WalkingParallelPair} (f : WalkingParallelPairHom X Y) : comp f (id Y) = f := by
  cases f <;> rfl

theorem WalkingParallelPairHom.assoc {X Y Z W : WalkingParallelPair}
    (f : WalkingParallelPairHom X Y) (g : WalkingParallelPairHom Y Z)
    (h : WalkingParallelPairHom Z W) : comp (comp f g) h = comp f (comp g h) := by
  cases f <;> cases g <;> cases h <;> rfl

instance walkingParallelPairHomCategory : SmallCategory WalkingParallelPair where
  Hom := WalkingParallelPairHom
  id := id
  comp := comp
  comp_id := comp_id
  id_comp := id_comp
  assoc := assoc

@[simp]
theorem walkingParallelPairHom_id (X : WalkingParallelPair) : WalkingParallelPairHom.id X = ğŸ™ X :=
  rfl

/-- The functor `WalkingParallelPair â¥¤ WalkingParallelPairáµ’áµ–` sending left to left and right to
right.
-/
def walkingParallelPairOp : WalkingParallelPair â¥¤ WalkingParallelPairáµ’áµ– where
  obj x := op <| by cases x; exacts [one, zero]
  map f := by
    cases f <;> apply Quiver.Hom.op
    exacts [left, right, WalkingParallelPairHom.id _]
  map_comp := by rintro _ _ _ (_ | _ | _) g <;> cases g <;> rfl

@[simp]
theorem walkingParallelPairOp_zero : walkingParallelPairOp.obj zero = op one := rfl

@[simp]
theorem walkingParallelPairOp_one : walkingParallelPairOp.obj one = op zero := rfl

@[simp]
theorem walkingParallelPairOp_left :
    walkingParallelPairOp.map left = @Quiver.Hom.op _ _ zero one left := rfl

@[simp]
theorem walkingParallelPairOp_right :
    walkingParallelPairOp.map right = @Quiver.Hom.op _ _ zero one right := rfl

/--
The equivalence `WalkingParallelPair â¥¤ WalkingParallelPairáµ’áµ–` sending left to left and right to
right.
-/
@[simps functor inverse]
def walkingParallelPairOpEquiv : WalkingParallelPair â‰Œ WalkingParallelPairáµ’áµ– where
  functor := walkingParallelPairOp
  inverse := walkingParallelPairOp.leftOp
  unitIso :=
    NatIso.ofComponents (fun j => eqToIso (by cases j <;> rfl))
      (by rintro _ _ (_ | _ | _) <;> simp)
  counitIso :=
    NatIso.ofComponents (fun j => eqToIso (by
            induction j with | _ X
            cases X <;> rfl))
      (fun {i} {j} f => by
      induction i with | _ i
      induction j with | _ j
      let g := f.unop
      have : f = g.op := rfl
      rw [this]
      cases i <;> cases j <;> cases g <;> rfl)
  functor_unitIso_comp := fun j => by cases j <;> rfl

@[simp]
theorem walkingParallelPairOpEquiv_unitIso_zero :
    walkingParallelPairOpEquiv.unitIso.app zero = Iso.refl zero := rfl

@[simp]
theorem walkingParallelPairOpEquiv_unitIso_one :
    walkingParallelPairOpEquiv.unitIso.app one = Iso.refl one := rfl

@[simp]
theorem walkingParallelPairOpEquiv_counitIso_zero :
    walkingParallelPairOpEquiv.counitIso.app (op zero) = Iso.refl (op zero) := rfl

@[simp]
theorem walkingParallelPairOpEquiv_counitIso_one :
    walkingParallelPairOpEquiv.counitIso.app (op one) = Iso.refl (op one) :=
  rfl

@[simp]
theorem walkingParallelPairOpEquiv_unitIso_hom_app_zero :
    walkingParallelPairOpEquiv.unitIso.hom.app zero = ğŸ™ zero := rfl

@[simp]
theorem walkingParallelPairOpEquiv_unitIso_hom_app_one :
    walkingParallelPairOpEquiv.unitIso.hom.app one = ğŸ™ one := rfl

@[simp]
theorem walkingParallelPairOpEquiv_unitIso_inv_app_zero :
    walkingParallelPairOpEquiv.unitIso.inv.app zero = ğŸ™ zero := rfl

@[simp]
theorem walkingParallelPairOpEquiv_unitIso_inv_app_one :
    walkingParallelPairOpEquiv.unitIso.inv.app one = ğŸ™ one := rfl

@[simp]
theorem walkingParallelPairOpEquiv_counitIso_hom_app_op_zero :
    walkingParallelPairOpEquiv.counitIso.hom.app (op zero) = ğŸ™ (op zero) := rfl

@[simp]
theorem walkingParallelPairOpEquiv_counitIso_hom_app_op_one :
    walkingParallelPairOpEquiv.counitIso.hom.app (op one) = ğŸ™ (op one) :=
  rfl

@[simp]
theorem walkingParallelPairOpEquiv_counitIso_inv_app_op_zero :
    walkingParallelPairOpEquiv.counitIso.inv.app (op zero) = ğŸ™ (op zero) := rfl

@[simp]
theorem walkingParallelPairOpEquiv_counitIso_inv_app_op_one :
    walkingParallelPairOpEquiv.counitIso.inv.app (op one) = ğŸ™ (op one) :=
  rfl

variable {C : Type u} [Category.{v} C]
variable {X Y : C}

/-- `parallelPair f g` is the diagram in `C` consisting of the two morphisms `f` and `g` with
common domain and codomain. -/
def parallelPair (f g : X âŸ¶ Y) : WalkingParallelPair â¥¤ C where
  obj x :=
    match x with
    | zero => X
    | one => Y
  map h :=
    match h with
    | WalkingParallelPairHom.id _ => ğŸ™ _
    | left => f
    | right => g
  -- `sorry` can cope with this, but it's too slow:
  map_comp := by
    rintro _ _ _ âŸ¨âŸ© g <;> cases g <;> simp

@[simp]
theorem parallelPair_obj_zero (f g : X âŸ¶ Y) : (parallelPair f g).obj zero = X := rfl

@[simp]
theorem parallelPair_obj_one (f g : X âŸ¶ Y) : (parallelPair f g).obj one = Y := rfl

@[simp]
theorem parallelPair_map_left (f g : X âŸ¶ Y) : (parallelPair f g).map left = f := rfl

@[simp]
theorem parallelPair_map_right (f g : X âŸ¶ Y) : (parallelPair f g).map right = g := rfl

@[simp]
theorem parallelPair_functor_obj {F : WalkingParallelPair â¥¤ C} (j : WalkingParallelPair) :
    (parallelPair (F.map left) (F.map right)).obj j = F.obj j := by cases j <;> rfl

/-- Every functor indexing a (co)equalizer is naturally isomorphic (actually, equal) to a
`parallelPair` -/
@[simps!]
def diagramIsoParallelPair (F : WalkingParallelPair â¥¤ C) :
    F â‰… parallelPair (F.map left) (F.map right) :=
  NatIso.ofComponents (fun j => eqToIso <| by cases j <;> rfl) (by rintro _ _ (_ | _ | _) <;> simp)

/-- Construct a morphism between parallel pairs. -/
def parallelPairHom {X' Y' : C} (f g : X âŸ¶ Y) (f' g' : X' âŸ¶ Y') (p : X âŸ¶ X') (q : Y âŸ¶ Y')
    (wf : f â‰« q = p â‰« f') (wg : g â‰« q = p â‰« g') : parallelPair f g âŸ¶ parallelPair f' g' where
  app j :=
    match j with
    | zero => p
    | one => q
  naturality := by
    rintro _ _ âŸ¨âŸ© <;> simp [wf,wg]

@[simp]
theorem parallelPairHom_app_zero {X' Y' : C} (f g : X âŸ¶ Y) (f' g' : X' âŸ¶ Y') (p : X âŸ¶ X')
    (q : Y âŸ¶ Y') (wf : f â‰« q = p â‰« f') (wg : g â‰« q = p â‰« g') :
    (parallelPairHom f g f' g' p q wf wg).app zero = p :=
  rfl

@[simp]
theorem parallelPairHom_app_one {X' Y' : C} (f g : X âŸ¶ Y) (f' g' : X' âŸ¶ Y') (p : X âŸ¶ X')
    (q : Y âŸ¶ Y') (wf : f â‰« q = p â‰« f') (wg : g â‰« q = p â‰« g') :
    (parallelPairHom f g f' g' p q wf wg).app one = q :=
  rfl

/-- Construct a natural isomorphism between functors out of the walking parallel pair from
its components. -/
@[simps!]
def parallelPair.ext {F G : WalkingParallelPair â¥¤ C} (zero : F.obj zero â‰… G.obj zero)
    (one : F.obj one â‰… G.obj one) (left : F.map left â‰« one.hom = zero.hom â‰« G.map left)
    (right : F.map right â‰« one.hom = zero.hom â‰« G.map right) : F â‰… G :=
  NatIso.ofComponents
    (by
      rintro âŸ¨jâŸ©
      exacts [zero, one])
    (by rintro _ _ âŸ¨_âŸ© <;> simp [left, right])

/-- Construct a natural isomorphism between `parallelPair f g` and `parallelPair f' g'` given
equalities `f = f'` and `g = g'`. -/
@[simps!]
def parallelPair.eqOfHomEq {f g f' g' : X âŸ¶ Y} (hf : f = f') (hg : g = g') :
    parallelPair f g â‰… parallelPair f' g' :=
  parallelPair.ext (Iso.refl _) (Iso.refl _) (by simp [hf]) (by simp [hg])

/-- A fork on `f` and `g` is just a `Cone (parallelPair f g)`. -/
abbrev Fork (f g : X âŸ¶ Y) :=
  Cone (parallelPair f g)

/-- A cofork on `f` and `g` is just a `Cocone (parallelPair f g)`. -/
abbrev Cofork (f g : X âŸ¶ Y) :=
  Cocone (parallelPair f g)

variable {f g : X âŸ¶ Y}

/-- A fork `t` on the parallel pair `f g : X âŸ¶ Y` consists of two morphisms
`t.Ï€.app zero : t.pt âŸ¶ X` and `t.Ï€.app one : t.pt âŸ¶ Y`. Of these,
only the first one is interesting, and we give it the shorter name `Fork.Î¹ t`. -/
def Fork.Î¹ (t : Fork f g) :=
  t.Ï€.app zero

@[simp]
theorem Fork.app_zero_eq_Î¹ (t : Fork f g) : t.Ï€.app zero = t.Î¹ :=
  rfl

/-- A cofork `t` on the parallelPair `f g : X âŸ¶ Y` consists of two morphisms
`t.Î¹.app zero : X âŸ¶ t.pt` and `t.Î¹.app one : Y âŸ¶ t.pt`. Of these, only the second one is
interesting, and we give it the shorter name `Cofork.Ï€ t`. -/
def Cofork.Ï€ (t : Cofork f g) :=
  t.Î¹.app one

@[simp]
theorem Cofork.app_one_eq_Ï€ (t : Cofork f g) : t.Î¹.app one = t.Ï€ :=
  rfl

@[simp]
theorem Fork.app_one_eq_Î¹_comp_left (s : Fork f g) : s.Ï€.app one = s.Î¹ â‰« f := by
  rw [â† s.app_zero_eq_Î¹, â† s.w left, parallelPair_map_left]

@[reassoc]
theorem Fork.app_one_eq_Î¹_comp_right (s : Fork f g) : s.Ï€.app one = s.Î¹ â‰« g := by
  rw [â† s.app_zero_eq_Î¹, â† s.w right, parallelPair_map_right]

@[simp]
theorem Cofork.app_zero_eq_comp_Ï€_left (s : Cofork f g) : s.Î¹.app zero = f â‰« s.Ï€ := by
  rw [â† s.app_one_eq_Ï€, â† s.w left, parallelPair_map_left]

@[reassoc]
theorem Cofork.app_zero_eq_comp_Ï€_right (s : Cofork f g) : s.Î¹.app zero = g â‰« s.Ï€ := by
  rw [â† s.app_one_eq_Ï€, â† s.w right, parallelPair_map_right]

/-- A fork on `f g : X âŸ¶ Y` is determined by the morphism `Î¹ : P âŸ¶ X` satisfying `Î¹ â‰« f = Î¹ â‰« g`.
-/
@[simps]
def Fork.ofÎ¹ {P : C} (Î¹ : P âŸ¶ X) (w : Î¹ â‰« f = Î¹ â‰« g) : Fork f g where
  pt := P
  Ï€ :=
    { app := fun X => by
        cases X
        Â· exact Î¹
        Â· exact Î¹ â‰« f
      naturality := fun {X} {Y} f =>
        by cases X <;> cases Y <;> cases f <;> simp; assumption }

/-- A cofork on `f g : X âŸ¶ Y` is determined by the morphism `Ï€ : Y âŸ¶ P` satisfying
`f â‰« Ï€ = g â‰« Ï€`. -/
@[simps]
def Cofork.ofÏ€ {P : C} (Ï€ : Y âŸ¶ P) (w : f â‰« Ï€ = g â‰« Ï€) : Cofork f g where
  pt := P
  Î¹ :=
    { app := fun X => WalkingParallelPair.casesOn X (f â‰« Ï€) Ï€
      naturality := fun i j f => by cases f <;> simp [w] }

@[simp]
theorem Fork.Î¹_ofÎ¹ {P : C} (Î¹ : P âŸ¶ X) (w : Î¹ â‰« f = Î¹ â‰« g) : (Fork.ofÎ¹ Î¹ w).Î¹ = Î¹ :=
  rfl

@[simp]
theorem Cofork.Ï€_ofÏ€ {P : C} (Ï€ : Y âŸ¶ P) (w : f â‰« Ï€ = g â‰« Ï€) : (Cofork.ofÏ€ Ï€ w).Ï€ = Ï€ :=
  rfl

@[reassoc (attr := simp)]
theorem Fork.condition (t : Fork f g) : t.Î¹ â‰« f = t.Î¹ â‰« g := by
  rw [â† t.app_one_eq_Î¹_comp_left, â† t.app_one_eq_Î¹_comp_right]

@[reassoc (attr := simp)]
theorem Cofork.condition (t : Cofork f g) : f â‰« t.Ï€ = g â‰« t.Ï€ := by
  rw [â† t.app_zero_eq_comp_Ï€_left, â† t.app_zero_eq_comp_Ï€_right]

/-- To check whether two maps are equalized by both maps of a fork, it suffices to check it for the
first map -/
theorem Fork.equalizer_ext (s : Fork f g) {W : C} {k l : W âŸ¶ s.pt} (h : k â‰« s.Î¹ = l â‰« s.Î¹) :
    âˆ€ j : WalkingParallelPair, k â‰« s.Ï€.app j = l â‰« s.Ï€.app j
  | zero => h
  | one => by
    have : k â‰« Î¹ s â‰« f = l â‰« Î¹ s â‰« f := by
      simp only [â† Category.assoc]; exact congrArg (Â· â‰« f) h
    rw [s.app_one_eq_Î¹_comp_left, this]

/-- To check whether two maps are coequalized by both maps of a cofork, it suffices to check it for
the second map -/
theorem Cofork.coequalizer_ext (s : Cofork f g) {W : C} {k l : s.pt âŸ¶ W}
    (h : Cofork.Ï€ s â‰« k = Cofork.Ï€ s â‰« l) : âˆ€ j : WalkingParallelPair, s.Î¹.app j â‰« k = s.Î¹.app j â‰« l
  | zero => by simp only [s.app_zero_eq_comp_Ï€_left, Category.assoc, h]
  | one => h

theorem Fork.IsLimit.hom_ext {s : Fork f g} (hs : IsLimit s) {W : C} {k l : W âŸ¶ s.pt}
    (h : k â‰« Fork.Î¹ s = l â‰« Fork.Î¹ s) : k = l :=
  hs.hom_ext <| Fork.equalizer_ext _ h

theorem Cofork.IsColimit.hom_ext {s : Cofork f g} (hs : IsColimit s) {W : C} {k l : s.pt âŸ¶ W}
    (h : Cofork.Ï€ s â‰« k = Cofork.Ï€ s â‰« l) : k = l :=
  hs.hom_ext <| Cofork.coequalizer_ext _ h

@[reassoc (attr := simp)]
theorem Fork.IsLimit.lift_Î¹ {s t : Fork f g} (hs : IsLimit s) : hs.lift t â‰« s.Î¹ = t.Î¹ :=
  hs.fac _ _

@[reassoc (attr := simp)]
theorem Cofork.IsColimit.Ï€_desc {s t : Cofork f g} (hs : IsColimit s) : s.Ï€ â‰« hs.desc t = t.Ï€ :=
  hs.fac _ _

/-- If `s` is a limit fork over `f` and `g`, then a morphism `k : W âŸ¶ X` satisfying
`k â‰« f = k â‰« g` induces a morphism `l : W âŸ¶ s.pt` such that `l â‰« fork.Î¹ s = k`. -/
def Fork.IsLimit.lift {s : Fork f g} (hs : IsLimit s) {W : C} (k : W âŸ¶ X) (h : k â‰« f = k â‰« g) :
    W âŸ¶ s.pt :=
  hs.lift (Fork.ofÎ¹ _ h)

@[reassoc (attr := simp)]
lemma Fork.IsLimit.lift_Î¹' {s : Fork f g} (hs : IsLimit s) {W : C} (k : W âŸ¶ X) (h : k â‰« f = k â‰« g) :
    Fork.IsLimit.lift hs k h â‰« Fork.Î¹ s = k :=
    hs.fac _ _

/-- If `s` is a limit fork over `f` and `g`, then a morphism `k : W âŸ¶ X` satisfying
`k â‰« f = k â‰« g` induces a morphism `l : W âŸ¶ s.pt` such that `l â‰« fork.Î¹ s = k`. -/
def Fork.IsLimit.lift' {s : Fork f g} (hs : IsLimit s) {W : C} (k : W âŸ¶ X) (h : k â‰« f = k â‰« g) :
    { l : W âŸ¶ s.pt // l â‰« Fork.Î¹ s = k } :=
  âŸ¨Fork.IsLimit.lift hs k h, by simpâŸ©

lemma Fork.IsLimit.mono {s : Fork f g} (hs : IsLimit s) : Mono s.Î¹ where
  right_cancellation _ _ h := hom_ext hs h

/-- If `s` is a colimit cofork over `f` and `g`, then a morphism `k : Y âŸ¶ W` satisfying
`f â‰« k = g â‰« k` induces a morphism `l : s.pt âŸ¶ W` such that `cofork.Ï€ s â‰« l = k`. -/
def Cofork.IsColimit.desc {s : Cofork f g} (hs : IsColimit s) {W : C} (k : Y âŸ¶ W)
    (h : f â‰« k = g â‰« k) : s.pt âŸ¶ W :=
  hs.desc (Cofork.ofÏ€ _ h)

@[reassoc (attr := simp)]
lemma Cofork.IsColimit.Ï€_desc' {s : Cofork f g} (hs : IsColimit s) {W : C} (k : Y âŸ¶ W)
    (h : f â‰« k = g â‰« k) : Cofork.Ï€ s â‰« Cofork.IsColimit.desc hs k h = k :=
  hs.fac _ _

/-- If `s` is a colimit cofork over `f` and `g`, then a morphism `k : Y âŸ¶ W` satisfying
`f â‰« k = g â‰« k` induces a morphism `l : s.pt âŸ¶ W` such that `cofork.Ï€ s â‰« l = k`. -/
def Cofork.IsColimit.desc' {s : Cofork f g} (hs : IsColimit s) {W : C} (k : Y âŸ¶ W)
    (h : f â‰« k = g â‰« k) : { l : s.pt âŸ¶ W // Cofork.Ï€ s â‰« l = k } :=
  âŸ¨Cofork.IsColimit.desc hs k h, by simpâŸ©

lemma Cofork.IsColimit.epi {s : Cofork f g} (hs : IsColimit s) : Epi s.Ï€ where
  left_cancellation _ _ h := hom_ext hs h

theorem Fork.IsLimit.existsUnique {s : Fork f g} (hs : IsLimit s) {W : C} (k : W âŸ¶ X)
    (h : k â‰« f = k â‰« g) : âˆƒ! l : W âŸ¶ s.pt, l â‰« Fork.Î¹ s = k :=
  âŸ¨hs.lift <| Fork.ofÎ¹ _ h, hs.fac _ _, fun _ hm =>
    Fork.IsLimit.hom_ext hs <| hm.symm â–¸ (hs.fac (Fork.ofÎ¹ _ h) WalkingParallelPair.zero).symmâŸ©

theorem Cofork.IsColimit.existsUnique {s : Cofork f g} (hs : IsColimit s) {W : C} (k : Y âŸ¶ W)
    (h : f â‰« k = g â‰« k) : âˆƒ! d : s.pt âŸ¶ W, Cofork.Ï€ s â‰« d = k :=
  âŸ¨hs.desc <| Cofork.ofÏ€ _ h, hs.fac _ _, fun _ hm =>
    Cofork.IsColimit.hom_ext hs <| hm.symm â–¸ (hs.fac (Cofork.ofÏ€ _ h) WalkingParallelPair.one).symmâŸ©

/-- This is a slightly more convenient method to verify that a fork is a limit cone. It
only asks for a proof of facts that carry any mathematical content -/
@[simps]
def Fork.IsLimit.mk (t : Fork f g) (lift : âˆ€ s : Fork f g, s.pt âŸ¶ t.pt)
    (fac : âˆ€ s : Fork f g, lift s â‰« Fork.Î¹ t = Fork.Î¹ s)
    (uniq : âˆ€ (s : Fork f g) (m : s.pt âŸ¶ t.pt) (_ : m â‰« t.Î¹ = s.Î¹), m = lift s) : IsLimit t :=
  { lift
    fac := fun s j =>
      WalkingParallelPair.casesOn j (fac s) <| by
        erw [â† s.w left, â† t.w left, â† Category.assoc, fac]; rfl
    uniq := fun s m j => by aesop}

/-- This is another convenient method to verify that a fork is a limit cone. It
only asks for a proof of facts that carry any mathematical content, and allows access to the
same `s` for all parts. -/
def Fork.IsLimit.mk' {X Y : C} {f g : X âŸ¶ Y} (t : Fork f g)
    (create : âˆ€ s : Fork f g, { l // l â‰« t.Î¹ = s.Î¹ âˆ§ âˆ€ {m}, m â‰« t.Î¹ = s.Î¹ â†’ m = l }) : IsLimit t :=
  Fork.IsLimit.mk t (fun s => (create s).1) (fun s => (create s).2.1) fun s _ w => (create s).2.2 w

/-- This is a slightly more convenient method to verify that a cofork is a colimit cocone. It
only asks for a proof of facts that carry any mathematical content -/
def Cofork.IsColimit.mk (t : Cofork f g) (desc : âˆ€ s : Cofork f g, t.pt âŸ¶ s.pt)
    (fac : âˆ€ s : Cofork f g, Cofork.Ï€ t â‰« desc s = Cofork.Ï€ s)
    (uniq : âˆ€ (s : Cofork f g) (m : t.pt âŸ¶ s.pt) (_ : t.Ï€ â‰« m = s.Ï€), m = desc s) : IsColimit t :=
  { desc
    fac := fun s j =>
      WalkingParallelPair.casesOn j (by simp_all) (fac s)
    uniq := by aesop }

/-- This is another convenient method to verify that a fork is a limit cone. It
only asks for a proof of facts that carry any mathematical content, and allows access to the
same `s` for all parts. -/
def Cofork.IsColimit.mk' {X Y : C} {f g : X âŸ¶ Y} (t : Cofork f g)
    (create : âˆ€ s : Cofork f g, { l : t.pt âŸ¶ s.pt // t.Ï€ â‰« l = s.Ï€
                                    âˆ§ âˆ€ {m}, t.Ï€ â‰« m = s.Ï€ â†’ m = l }) : IsColimit t :=
  Cofork.IsColimit.mk t (fun s => (create s).1) (fun s => (create s).2.1) fun s _ w =>
    (create s).2.2 w

/-- Noncomputably make a limit cone from the existence of unique factorizations. -/
noncomputable def Fork.IsLimit.ofExistsUnique {t : Fork f g}
    (hs : âˆ€ s : Fork f g, âˆƒ! l : s.pt âŸ¶ t.pt, l â‰« Fork.Î¹ t = Fork.Î¹ s) : IsLimit t := by
  choose d hd hd' using hs
  exact Fork.IsLimit.mk _ d hd fun s m hm => hd' _ _ hm

/-- Noncomputably make a colimit cocone from the existence of unique factorizations. -/
noncomputable def Cofork.IsColimit.ofExistsUnique {t : Cofork f g}
    (hs : âˆ€ s : Cofork f g, âˆƒ! d : t.pt âŸ¶ s.pt, Cofork.Ï€ t â‰« d = Cofork.Ï€ s) : IsColimit t := by
  choose d hd hd' using hs
  exact Cofork.IsColimit.mk _ d hd fun s m hm => hd' _ _ hm

/--
Given a limit cone for the pair `f g : X âŸ¶ Y`, for any `Z`, morphisms from `Z` to its point are in
bijection with morphisms `h : Z âŸ¶ X` such that `h â‰« f = h â‰« g`.
Further, this bijection is natural in `Z`: see `Fork.IsLimit.homIso_natural`.
This is a special case of `IsLimit.homIso'`, often useful to construct adjunctions.
-/
@[simps]
def Fork.IsLimit.homIso {X Y : C} {f g : X âŸ¶ Y} {t : Fork f g} (ht : IsLimit t) (Z : C) :
    (Z âŸ¶ t.pt) â‰ƒ { h : Z âŸ¶ X // h â‰« f = h â‰« g } where
  toFun k := âŸ¨k â‰« t.Î¹, by simp only [Category.assoc, t.condition]âŸ©
  invFun h := (Fork.IsLimit.lift' ht _ h.prop).1
  left_inv _ := Fork.IsLimit.hom_ext ht (Fork.IsLimit.lift' _ _ _).prop
  right_inv _ := Subtype.ext (Fork.IsLimit.lift' ht _ _).prop

/-- The bijection of `Fork.IsLimit.homIso` is natural in `Z`. -/
theorem Fork.IsLimit.homIso_natural {X Y : C} {f g : X âŸ¶ Y} {t : Fork f g} (ht : IsLimit t)
    {Z Z' : C} (q : Z' âŸ¶ Z) (k : Z âŸ¶ t.pt) :
    (Fork.IsLimit.homIso ht _ (q â‰« k) : Z' âŸ¶ X) = q â‰« (Fork.IsLimit.homIso ht _ k : Z âŸ¶ X) :=
  Category.assoc _ _ _

/-- Given a colimit cocone for the pair `f g : X âŸ¶ Y`, for any `Z`, morphisms from the cocone point
to `Z` are in bijection with morphisms `h : Y âŸ¶ Z` such that `f â‰« h = g â‰« h`.
Further, this bijection is natural in `Z`: see `Cofork.IsColimit.homIso_natural`.
This is a special case of `IsColimit.homIso'`, often useful to construct adjunctions.
-/
@[simps]
def Cofork.IsColimit.homIso {X Y : C} {f g : X âŸ¶ Y} {t : Cofork f g} (ht : IsColimit t) (Z : C) :
    (t.pt âŸ¶ Z) â‰ƒ { h : Y âŸ¶ Z // f â‰« h = g â‰« h } where
  toFun k := âŸ¨t.Ï€ â‰« k, by simp only [â† Category.assoc, t.condition]âŸ©
  invFun h := (Cofork.IsColimit.desc' ht _ h.prop).1
  left_inv _ := Cofork.IsColimit.hom_ext ht (Cofork.IsColimit.desc' _ _ _).prop
  right_inv _ := Subtype.ext (Cofork.IsColimit.desc' ht _ _).prop

/-- The bijection of `Cofork.IsColimit.homIso` is natural in `Z`. -/
theorem Cofork.IsColimit.homIso_natural {X Y : C} {f g : X âŸ¶ Y} {t : Cofork f g} {Z Z' : C}
    (q : Z âŸ¶ Z') (ht : IsColimit t) (k : t.pt âŸ¶ Z) :
    (Cofork.IsColimit.homIso ht _ (k â‰« q) : Y âŸ¶ Z') =
      (Cofork.IsColimit.homIso ht _ k : Y âŸ¶ Z) â‰« q :=
  (Category.assoc _ _ _).symm

/-- This is a helper construction that can be useful when verifying that a category has all
equalizers. Given `F : WalkingParallelPair â¥¤ C`, which is really the same as
`parallelPair (F.map left) (F.map right)`, and a fork on `F.map left` and `F.map right`,
we get a cone on `F`.

If you're thinking about using this, have a look at `hasEqualizers_of_hasLimit_parallelPair`,
which you may find to be an easier way of achieving your goal. -/
def Cone.ofFork {F : WalkingParallelPair â¥¤ C} (t : Fork (F.map left) (F.map right)) : Cone F where
  pt := t.pt
  Ï€ :=
    { app := fun X => t.Ï€.app X â‰« eqToHom (by simp)
      naturality := by rintro _ _ (_ | _ | _) <;> simp [t.condition]}

/-- This is a helper construction that can be useful when verifying that a category has all
coequalizers. Given `F : WalkingParallelPair â¥¤ C`, which is really the same as
`parallelPair (F.map left) (F.map right)`, and a cofork on `F.map left` and `F.map right`,
we get a cocone on `F`.

If you're thinking about using this, have a look at
`hasCoequalizers_of_hasColimit_parallelPair`, which you may find to be an easier way of
achieving your goal. -/
def Cocone.ofCofork {F : WalkingParallelPair â¥¤ C} (t : Cofork (F.map left) (F.map right)) :
    Cocone F where
  pt := t.pt
  Î¹ :=
    { app := fun X => eqToHom (by simp) â‰« t.Î¹.app X
      naturality := by rintro _ _ (_ | _ | _) <;> simp [t.condition]}

@[simp]
theorem Cone.ofFork_Ï€ {F : WalkingParallelPair â¥¤ C} (t : Fork (F.map left) (F.map right)) (j) :
    (Cone.ofFork t).Ï€.app j = t.Ï€.app j â‰« eqToHom (by simp) := rfl

@[simp]
theorem Cocone.ofCofork_Î¹ {F : WalkingParallelPair â¥¤ C} (t : Cofork (F.map left) (F.map right))
    (j) : (Cocone.ofCofork t).Î¹.app j = eqToHom (by simp) â‰« t.Î¹.app j := rfl

/-- Given `F : WalkingParallelPair â¥¤ C`, which is really the same as
`parallelPair (F.map left) (F.map right)` and a cone on `F`, we get a fork on
`F.map left` and `F.map right`. -/
def Fork.ofCone {F : WalkingParallelPair â¥¤ C} (t : Cone F) : Fork (F.map left) (F.map right) where
  pt := t.pt
  Ï€ := { app := fun X => t.Ï€.app X â‰« eqToHom (by simp)
         naturality := by rintro _ _ (_ | _ | _) <;> simp}

/-- Given `F : WalkingParallelPair â¥¤ C`, which is really the same as
`parallelPair (F.map left) (F.map right)` and a cocone on `F`, we get a cofork on
`F.map left` and `F.map right`. -/
def Cofork.ofCocone {F : WalkingParallelPair â¥¤ C} (t : Cocone F) :
    Cofork (F.map left) (F.map right) where
  pt := t.pt
  Î¹ := { app := fun X => eqToHom (by simp) â‰« t.Î¹.app X
         naturality := by rintro _ _ (_ | _ | _) <;> simp}

@[simp]
theorem Fork.ofCone_Ï€ {F : WalkingParallelPair â¥¤ C} (t : Cone F) (j) :
    (Fork.ofCone t).Ï€.app j = t.Ï€.app j â‰« eqToHom (by simp) := rfl

@[simp]
theorem Cofork.ofCocone_Î¹ {F : WalkingParallelPair â¥¤ C} (t : Cocone F) (j) :
    (Cofork.ofCocone t).Î¹.app j = eqToHom (by simp) â‰« t.Î¹.app j := rfl

@[simp]
theorem Fork.Î¹_postcompose {f' g' : X âŸ¶ Y} {Î± : parallelPair f g âŸ¶ parallelPair f' g'}
    {c : Fork f g} : Fork.Î¹ ((Cones.postcompose Î±).obj c) = c.Î¹ â‰« Î±.app _ :=
  rfl

@[simp]
theorem Cofork.Ï€_precompose {f' g' : X âŸ¶ Y} {Î± : parallelPair f g âŸ¶ parallelPair f' g'}
    {c : Cofork f' g'} : Cofork.Ï€ ((Cocones.precompose Î±).obj c) = Î±.app _ â‰« c.Ï€ :=
  rfl

/-- Helper function for constructing morphisms between equalizer forks.
-/
@[simps]
def Fork.mkHom {s t : Fork f g} (k : s.pt âŸ¶ t.pt) (w : k â‰« t.Î¹ = s.Î¹) : s âŸ¶ t where
  hom := k
  w := by
    rintro âŸ¨_ | _âŸ©
    Â· exact w
    Â· simp only [Fork.app_one_eq_Î¹_comp_left,â† Category.assoc]
      congr

/-- To construct an isomorphism between forks,
it suffices to give an isomorphism between the cone points
and check that it commutes with the `Î¹` morphisms.
-/
@[simps]
def Fork.ext {s t : Fork f g} (i : s.pt â‰… t.pt) (w : i.hom â‰« t.Î¹ = s.Î¹ := by cat_disch) :
    s â‰… t where
  hom := Fork.mkHom i.hom w
  inv := Fork.mkHom i.inv (by rw [â† w, Iso.inv_hom_id_assoc])

/-- Two forks of the form `ofÎ¹` are isomorphic whenever their `Î¹`'s are equal. -/
def ForkOfÎ¹.ext {P : C} {Î¹ Î¹' : P âŸ¶ X} (w : Î¹ â‰« f = Î¹ â‰« g) (w' : Î¹' â‰« f = Î¹' â‰« g) (h : Î¹ = Î¹') :
    Fork.ofÎ¹ Î¹ w â‰… Fork.ofÎ¹ Î¹' w' :=
  Fork.ext (Iso.refl _) (by simp [h])

/-- Every fork is isomorphic to one of the form `Fork.of_Î¹ _ _`. -/
def Fork.isoForkOfÎ¹ (c : Fork f g) : c â‰… Fork.ofÎ¹ c.Î¹ c.condition :=
  Fork.ext (by simp only [Fork.ofÎ¹_pt, Functor.const_obj_obj]; rfl) (by simp)

/--
Given two forks with isomorphic components in such a way that the natural diagrams commute, then
one is a limit if and only if the other one is.
-/
def Fork.isLimitEquivOfIsos {X Y : C} {f g : X âŸ¶ Y} {X' Y' : C}
    (c : Fork f g)
    {f' g' : X' âŸ¶ Y'} (c' : Fork f' g')
    (eâ‚€ : X â‰… X') (eâ‚ : Y â‰… Y') (e : c.pt â‰… c'.pt)
    (commâ‚ : eâ‚€.hom â‰« f' = f â‰« eâ‚.hom := by cat_disch)
    (commâ‚‚ : eâ‚€.hom â‰« g' = g â‰« eâ‚.hom := by cat_disch)
    (commâ‚ƒ : e.hom â‰« c'.Î¹ = c.Î¹ â‰« eâ‚€.hom := by cat_disch) :
    IsLimit c â‰ƒ IsLimit c' :=
  let i : parallelPair f g â‰… parallelPair f' g' := parallelPair.ext eâ‚€ eâ‚ commâ‚.symm commâ‚‚.symm
  IsLimit.equivOfNatIsoOfIso i c c' (Fork.ext e commâ‚ƒ)

/--
Given two forks with isomorphic components in such a way that the natural diagrams commute, then if
one is a limit, then the other one is as well.
-/
def Fork.isLimitOfIsos {X' Y' : C} (c : Fork f g) (hc : IsLimit c)
    {f' g' : X' âŸ¶ Y'} (c' : Fork f' g')
    (eâ‚€ : X â‰… X') (eâ‚ : Y â‰… Y') (e : c.pt â‰… c'.pt)
    (commâ‚ : eâ‚€.hom â‰« f' = f â‰« eâ‚.hom := by cat_disch)
    (commâ‚‚ : eâ‚€.hom â‰« g' = g â‰« eâ‚.hom := by cat_disch)
    (commâ‚ƒ : e.hom â‰« c'.Î¹ = c.Î¹ â‰« eâ‚€.hom := by cat_disch) : IsLimit c' :=
  (Fork.isLimitEquivOfIsos c c' eâ‚€ eâ‚ e) hc

/-- Helper function for constructing morphisms between coequalizer coforks.
-/
@[simps]
def Cofork.mkHom {s t : Cofork f g} (k : s.pt âŸ¶ t.pt) (w : s.Ï€ â‰« k = t.Ï€) : s âŸ¶ t where
  hom := k
  w := by
    rintro âŸ¨_ | _âŸ©
    Â· simp [Cofork.app_zero_eq_comp_Ï€_left, w]
    Â· exact w

@[reassoc (attr := simp)]
theorem Fork.hom_comp_Î¹ {s t : Fork f g} (f : s âŸ¶ t) : f.hom â‰« t.Î¹ = s.Î¹ := by
  cases s; cases t; cases f; aesop

@[reassoc (attr := simp)]
theorem Fork.Ï€_comp_hom {s t : Cofork f g} (f : s âŸ¶ t) : s.Ï€ â‰« f.hom = t.Ï€ := by
  cases s; cases t; cases f; aesop

/-- To construct an isomorphism between coforks,
it suffices to give an isomorphism between the cocone points
and check that it commutes with the `Ï€` morphisms.
-/
@[simps]
def Cofork.ext {s t : Cofork f g} (i : s.pt â‰… t.pt) (w : s.Ï€ â‰« i.hom = t.Ï€ := by cat_disch) :
    s â‰… t where
  hom := Cofork.mkHom i.hom w
  inv := Cofork.mkHom i.inv (by rw [Iso.comp_inv_eq, w])

/-- Two coforks of the form `ofÏ€` are isomorphic whenever their `Ï€`'s are equal. -/
def CoforkOfÏ€.ext {P : C} {Ï€ Ï€' : Y âŸ¶ P} (w : f â‰« Ï€ = g â‰« Ï€) (w' : f â‰« Ï€' = g â‰« Ï€') (h : Ï€ = Ï€') :
    Cofork.ofÏ€ Ï€ w â‰… Cofork.ofÏ€ Ï€' w' :=
  Cofork.ext (Iso.refl _) (by simp [h])

/-- Every cofork is isomorphic to one of the form `Cofork.ofÏ€ _ _`. -/
def Cofork.isoCoforkOfÏ€ (c : Cofork f g) : c â‰… Cofork.ofÏ€ c.Ï€ c.condition :=
  Cofork.ext (by simp only [Cofork.ofÏ€_pt, Functor.const_obj_obj]; rfl) (by simp)

/--
Given two coforks with isomorphic components in such a way that the natural diagrams commute, then
one is a colimit if and only if the other one is.
-/
def Cofork.isColimitEquivOfIsos {X Y : C} {f g : X âŸ¶ Y} {X' Y' : C}
    (c : Cofork f g)
    {f' g' : X' âŸ¶ Y'} (c' : Cofork f' g')
    (eâ‚€ : X â‰… X') (eâ‚ : Y â‰… Y') (e : c.pt â‰… c'.pt)
    (commâ‚ : eâ‚€.hom â‰« f' = f â‰« eâ‚.hom := by cat_disch)
    (commâ‚‚ : eâ‚€.hom â‰« g' = g â‰« eâ‚.hom := by cat_disch)
    (commâ‚ƒ : eâ‚.inv â‰« c.Ï€ â‰« e.hom = c'.Ï€ := by cat_disch) :
    IsColimit c â‰ƒ IsColimit c' :=
  let i : parallelPair f g â‰… parallelPair f' g' := parallelPair.ext eâ‚€ eâ‚ commâ‚.symm commâ‚‚.symm
  IsColimit.equivOfNatIsoOfIso i c c' (Cofork.ext e (by rw [â† commâ‚ƒ, â† Category.assoc]; rfl))

/--
Given two coforks with isomorphic components in such a way that the natural diagrams commute, then
if one is a colimit, then the other one is as well.
-/
def Cofork.isColimitOfIsos {X' Y' : C} (c : Cofork f g) (hc : IsColimit c)
    {f' g' : X' âŸ¶ Y'} (c' : Cofork f' g')
    (eâ‚€ : X â‰… X') (eâ‚ : Y â‰… Y') (e : c.pt â‰… c'.pt)
    (commâ‚ : eâ‚€.hom â‰« f' = f â‰« eâ‚.hom := by cat_disch)
    (commâ‚‚ : eâ‚€.hom â‰« g' = g â‰« eâ‚.hom := by cat_disch)
    (commâ‚ƒ : eâ‚.inv â‰« c.Ï€ â‰« e.hom = c'.Ï€ := by cat_disch) : IsColimit c' :=
  (Cofork.isColimitEquivOfIsos c c' eâ‚€ eâ‚ e) hc

variable (f g)

section

/-- Two parallel morphisms `f` and `g` have an equalizer if the diagram `parallelPair f g` has a
limit. -/
abbrev HasEqualizer :=
  HasLimit (parallelPair f g)

variable [HasEqualizer f g]

/-- If an equalizer of `f` and `g` exists, we can access an arbitrary choice of such by
saying `equalizer f g`. -/
noncomputable abbrev equalizer : C :=
  limit (parallelPair f g)

/-- If an equalizer of `f` and `g` exists, we can access the inclusion
equalizer f g âŸ¶ X` by saying `equalizer.Î¹ f g`. -/
noncomputable abbrev equalizer.Î¹ : equalizer f g âŸ¶ X :=
  limit.Ï€ (parallelPair f g) zero

/-- An equalizer cone for a parallel pair `f` and `g` -/
noncomputable abbrev equalizer.fork : Fork f g :=
  limit.cone (parallelPair f g)

@[simp]
theorem equalizer.fork_Î¹ : (equalizer.fork f g).Î¹ = equalizer.Î¹ f g :=
  rfl

@[simp]
theorem equalizer.fork_Ï€_app_zero : (equalizer.fork f g).Ï€.app zero = equalizer.Î¹ f g :=
  rfl

@[reassoc]
theorem equalizer.condition : equalizer.Î¹ f g â‰« f = equalizer.Î¹ f g â‰« g :=
  Fork.condition <| limit.cone <| parallelPair f g

/-- The equalizer built from `equalizer.Î¹ f g` is limiting. -/
noncomputable def equalizerIsEqualizer : IsLimit (Fork.ofÎ¹ (equalizer.Î¹ f g)
    (equalizer.condition f g)) :=
  IsLimit.ofIsoLimit (limit.isLimit _) (Fork.ext (Iso.refl _) (by simp))

variable {f g}

/-- A morphism `k : W âŸ¶ X` satisfying `k â‰« f = k â‰« g` factors through the equalizer of `f` and `g`
via `equalizer.lift : W âŸ¶ equalizer f g`. -/
noncomputable abbrev equalizer.lift {W : C} (k : W âŸ¶ X) (h : k â‰« f = k â‰« g) : W âŸ¶ equalizer f g :=
  limit.lift (parallelPair f g) (Fork.ofÎ¹ k h)

@[reassoc]
theorem equalizer.lift_Î¹ {W : C} (k : W âŸ¶ X) (h : k â‰« f = k â‰« g) :
    equalizer.lift k h â‰« equalizer.Î¹ f g = k :=
  limit.lift_Ï€ _ _

/-- A morphism `k : W âŸ¶ X` satisfying `k â‰« f = k â‰« g` induces a morphism `l : W âŸ¶ equalizer f g`
satisfying `l â‰« equalizer.Î¹ f g = k`. -/
noncomputable def equalizer.lift' {W : C} (k : W âŸ¶ X) (h : k â‰« f = k â‰« g) :
    { l : W âŸ¶ equalizer f g // l â‰« equalizer.Î¹ f g = k } :=
  âŸ¨equalizer.lift k h, equalizer.lift_Î¹ _ _âŸ©

/-- Two maps into an equalizer are equal if they are equal when composed with the equalizer map. -/
@[ext]
theorem equalizer.hom_ext {W : C} {k l : W âŸ¶ equalizer f g}
    (h : k â‰« equalizer.Î¹ f g = l â‰« equalizer.Î¹ f g) : k = l :=
  Fork.IsLimit.hom_ext (limit.isLimit _) h

theorem equalizer.existsUnique {W : C} (k : W âŸ¶ X) (h : k â‰« f = k â‰« g) :
    âˆƒ! l : W âŸ¶ equalizer f g, l â‰« equalizer.Î¹ f g = k :=
  Fork.IsLimit.existsUnique (limit.isLimit _) _ h

/-- An equalizer morphism is a monomorphism -/
instance equalizer.Î¹_mono : Mono (equalizer.Î¹ f g) where
  right_cancellation _ _ w := equalizer.hom_ext w

end

section

variable {f g}

/-- The equalizer morphism in any limit cone is a monomorphism. -/
theorem mono_of_isLimit_fork {c : Fork f g} (i : IsLimit c) : Mono (Fork.Î¹ c) :=
  { right_cancellation := fun _ _ w => Fork.IsLimit.hom_ext i w }

end

section

variable {f g}

/-- The identity determines a cone on the equalizer diagram of `f` and `g` if `f = g`. -/
def idFork (h : f = g) : Fork f g :=
  Fork.ofÎ¹ (ğŸ™ X) <| h â–¸ rfl

/-- The identity on `X` is an equalizer of `(f, g)`, if `f = g`. -/
def isLimitIdFork (h : f = g) : IsLimit (idFork h) :=
  Fork.IsLimit.mk _ (fun s => Fork.Î¹ s) (fun _ => Category.comp_id _) fun s m h => by
    convert h
    exact (Category.comp_id _).symm

/-- Every equalizer of `(f, g)`, where `f = g`, is an isomorphism. -/
theorem isIso_limit_cone_parallelPair_of_eq (hâ‚€ : f = g) {c : Fork f g} (h : IsLimit c) :
    IsIso c.Î¹ :=
  Iso.isIso_hom <| IsLimit.conePointUniqueUpToIso h <| isLimitIdFork hâ‚€

/-- The equalizer of `(f, g)`, where `f = g`, is an isomorphism. -/
theorem equalizer.Î¹_of_eq [HasEqualizer f g] (h : f = g) : IsIso (equalizer.Î¹ f g) :=
  isIso_limit_cone_parallelPair_of_eq h <| limit.isLimit _

/-- Every equalizer of `(f, f)` is an isomorphism. -/
theorem isIso_limit_cone_parallelPair_of_self {c : Fork f f} (h : IsLimit c) : IsIso c.Î¹ :=
  isIso_limit_cone_parallelPair_of_eq rfl h

/-- An equalizer that is an epimorphism is an isomorphism. -/
theorem isIso_limit_cone_parallelPair_of_epi {c : Fork f g} (h : IsLimit c) [Epi c.Î¹] : IsIso c.Î¹ :=
  isIso_limit_cone_parallelPair_of_eq ((cancel_epi _).1 (Fork.condition c)) h

/-- Two morphisms are equal if there is a fork whose inclusion is epi. -/
theorem eq_of_epi_fork_Î¹ (t : Fork f g) [Epi (Fork.Î¹ t)] : f = g :=
  (cancel_epi (Fork.Î¹ t)).1 <| Fork.condition t

/-- If the equalizer of two morphisms is an epimorphism, then the two morphisms are equal. -/
theorem eq_of_epi_equalizer [HasEqualizer f g] [Epi (equalizer.Î¹ f g)] : f = g :=
  (cancel_epi (equalizer.Î¹ f g)).1 <| equalizer.condition _ _

end

instance hasEqualizer_of_self : HasEqualizer f f :=
  HasLimit.mk
    { cone := idFork rfl
      isLimit := isLimitIdFork rfl }

/-- The equalizer inclusion for `(f, f)` is an isomorphism. -/
instance equalizer.Î¹_of_self : IsIso (equalizer.Î¹ f f) :=
  equalizer.Î¹_of_eq rfl

/-- The equalizer of a morphism with itself is isomorphic to the source. -/
noncomputable def equalizer.isoSourceOfSelf : equalizer f f â‰… X :=
  asIso (equalizer.Î¹ f f)

@[simp]
theorem equalizer.isoSourceOfSelf_hom : (equalizer.isoSourceOfSelf f).hom = equalizer.Î¹ f f :=
  rfl

@[simp]
theorem equalizer.isoSourceOfSelf_inv :
    (equalizer.isoSourceOfSelf f).inv = equalizer.lift (ğŸ™ X) (by simp) := by
  ext
  simp [equalizer.isoSourceOfSelf]


section

variable {f g : X âŸ¶ Y} {Z : C} (h : Z âŸ¶ X)

/--
Given a fork `s` on morphisms `f, g : X âŸ¶ Y` and a pullback cone `c` on `s.Î¹ : s.pt âŸ¶ X` and a
morphism `h : Z âŸ¶ X`, the projection `c.snd : c.pt âŸ¶ Z` induces a fork on `h â‰« f` and `h â‰« g`.
-/
def precompFork (s : Fork f g) (c : PullbackCone s.Î¹ h) :
    Fork (h â‰« f) (h â‰« g) :=
  Fork.ofÎ¹ c.snd <| by
    rw [â† reassoc_of% c.condition, â† reassoc_of% c.condition, s.condition]

/--
Any fork on `h â‰« f` and `h â‰« g` lifts to a pullback along `h` of an equalizer of `f` and `g`.
-/
def liftPrecomp {s : Fork f g} (hs : IsLimit s) {c : PullbackCone s.Î¹ h} (hc : IsLimit c)
    (s' : Fork (h â‰« f) (h â‰« g)) :
    s'.pt âŸ¶ (precompFork h s c).pt :=
  hc.lift <| PullbackCone.mk
    (hs.lift <| Fork.ofÎ¹ (s'.Î¹ â‰« h)
      (by
        simp only [Functor.const_obj_obj, parallelPair_obj_zero, Category.assoc]
        rw [s'.condition]))
    s'.Î¹

/-- The pullback of an equalizer is an equalizer. -/
def isLimitPrecompFork {s : Fork f g} (hs : IsLimit s) {c : PullbackCone s.Î¹ h} (hc : IsLimit c) :
    IsLimit (precompFork h s c) :=
  Fork.IsLimit.mk _
    (fun s' â†¦ liftPrecomp h hs hc s')
    (by simp [liftPrecomp, precompFork])
    (fun s' m h â†¦ hc.hom_ext <| by
      apply PullbackCone.equalizer_ext
      Â· simp only [liftPrecomp, Fork.ofÎ¹_pt, IsLimit.fac, PullbackCone.mk_Ï€_app]
        apply hs.hom_ext
        apply Fork.equalizer_ext
        simp only [Category.assoc, Fork.IsLimit.lift_Î¹, Fork.Î¹_ofÎ¹, precompFork] at âŠ¢ h
        rw [c.condition, reassoc_of% h]
      Â· simpa [liftPrecomp] using h)

lemma hasEqualizer_precomp_of_equalizer {s : Fork f g} (hs : IsLimit s)
    {c : PullbackCone s.Î¹ h} (hc : IsLimit c) :
    HasEqualizer (h â‰« f) (h â‰« g) :=
  HasLimit.mk
    { cone := precompFork h s c
      isLimit := isLimitPrecompFork h hs hc }

instance hasEqualizer_precomp_of_hasEqualizer [HasEqualizer f g] [HasPullback (equalizer.Î¹ f g) h] :
    HasEqualizer (h â‰« f) (h â‰« g) :=
  hasEqualizer_precomp_of_equalizer h
    (equalizerIsEqualizer f g) (pullback.isLimit (equalizer.Î¹ f g) h)

end

section

/-- Two parallel morphisms `f` and `g` have a coequalizer if the diagram `parallelPair f g` has a
colimit. -/
abbrev HasCoequalizer :=
  HasColimit (parallelPair f g)

variable [HasCoequalizer f g]

/-- If a coequalizer of `f` and `g` exists, we can access an arbitrary choice of such by
saying `coequalizer f g`. -/
noncomputable abbrev coequalizer : C :=
  colimit (parallelPair f g)

/-- If a coequalizer of `f` and `g` exists, we can access the corresponding projection by
saying `coequalizer.Ï€ f g`. -/
noncomputable abbrev coequalizer.Ï€ : Y âŸ¶ coequalizer f g :=
  colimit.Î¹ (parallelPair f g) one

/-- An arbitrary choice of coequalizer cocone for a parallel pair `f` and `g`.
-/
noncomputable abbrev coequalizer.cofork : Cofork f g :=
  colimit.cocone (parallelPair f g)

@[simp]
theorem coequalizer.cofork_Ï€ : (coequalizer.cofork f g).Ï€ = coequalizer.Ï€ f g :=
  rfl

theorem coequalizer.cofork_Î¹_app_one : (coequalizer.cofork f g).Î¹.app one = coequalizer.Ï€ f g :=
  rfl

@[reassoc]
theorem coequalizer.condition : f â‰« coequalizer.Ï€ f g = g â‰« coequalizer.Ï€ f g :=
  Cofork.condition <| colimit.cocone <| parallelPair f g

/-- The cofork built from `coequalizer.Ï€ f g` is colimiting. -/
noncomputable def coequalizerIsCoequalizer :
    IsColimit (Cofork.ofÏ€ (coequalizer.Ï€ f g) (coequalizer.condition f g)) :=
  IsColimit.ofIsoColimit (colimit.isColimit _) (Cofork.ext (Iso.refl _) (by simp))

variable {f g}

/-- Any morphism `k : Y âŸ¶ W` satisfying `f â‰« k = g â‰« k` factors through the coequalizer of `f`
and `g` via `coequalizer.desc : coequalizer f g âŸ¶ W`. -/
noncomputable abbrev coequalizer.desc {W : C} (k : Y âŸ¶ W) (h : f â‰« k = g â‰« k) :
    coequalizer f g âŸ¶ W :=
  colimit.desc (parallelPair f g) (Cofork.ofÏ€ k h)

@[reassoc]
theorem coequalizer.Ï€_desc {W : C} (k : Y âŸ¶ W) (h : f â‰« k = g â‰« k) :
    coequalizer.Ï€ f g â‰« coequalizer.desc k h = k :=
  colimit.Î¹_desc _ _

theorem coequalizer.Ï€_colimMap_desc {X' Y' Z : C} (f' g' : X' âŸ¶ Y') [HasCoequalizer f' g']
    (p : X âŸ¶ X') (q : Y âŸ¶ Y') (wf : f â‰« q = p â‰« f') (wg : g â‰« q = p â‰« g') (h : Y' âŸ¶ Z)
    (wh : f' â‰« h = g' â‰« h) :
    coequalizer.Ï€ f g â‰« colimMap (parallelPairHom f g f' g' p q wf wg) â‰« coequalizer.desc h wh =
      q â‰« h := by
  rw [Î¹_colimMap_assoc, parallelPairHom_app_one, coequalizer.Ï€_desc]

/-- Any morphism `k : Y âŸ¶ W` satisfying `f â‰« k = g â‰« k` induces a morphism
`l : coequalizer f g âŸ¶ W` satisfying `coequalizer.Ï€ â‰« g = l`. -/
noncomputable def coequalizer.desc' {W : C} (k : Y âŸ¶ W) (h : f â‰« k = g â‰« k) :
    { l : coequalizer f g âŸ¶ W // coequalizer.Ï€ f g â‰« l = k } :=
  âŸ¨coequalizer.desc k h, coequalizer.Ï€_desc _ _âŸ©

/-- Two maps from a coequalizer are equal if they are equal when composed with the coequalizer
map -/
@[ext]
theorem coequalizer.hom_ext {W : C} {k l : coequalizer f g âŸ¶ W}
    (h : coequalizer.Ï€ f g â‰« k = coequalizer.Ï€ f g â‰« l) : k = l :=
  Cofork.IsColimit.hom_ext (colimit.isColimit _) h

theorem coequalizer.existsUnique {W : C} (k : Y âŸ¶ W) (h : f â‰« k = g â‰« k) :
    âˆƒ! d : coequalizer f g âŸ¶ W, coequalizer.Ï€ f g â‰« d = k :=
  Cofork.IsColimit.existsUnique (colimit.isColimit _) _ h

/-- A coequalizer morphism is an epimorphism -/
instance coequalizer.Ï€_epi : Epi (coequalizer.Ï€ f g) where
  left_cancellation _ _ w := coequalizer.hom_ext w

end

section

variable {f g}

/-- The coequalizer morphism in any colimit cocone is an epimorphism. -/
theorem epi_of_isColimit_cofork {c : Cofork f g} (i : IsColimit c) : Epi c.Ï€ :=
  { left_cancellation := fun _ _ w => Cofork.IsColimit.hom_ext i w }

end

section

variable {f g}

/-- The identity determines a cocone on the coequalizer diagram of `f` and `g`, if `f = g`. -/
def idCofork (h : f = g) : Cofork f g :=
  Cofork.ofÏ€ (ğŸ™ Y) <| h â–¸ rfl

/-- The identity on `Y` is a coequalizer of `(f, g)`, where `f = g`. -/
def isColimitIdCofork (h : f = g) : IsColimit (idCofork h) :=
  Cofork.IsColimit.mk _ (fun s => Cofork.Ï€ s) (fun _ => Category.id_comp _) fun s m h => by
    convert h
    exact (Category.id_comp _).symm

/-- Every coequalizer of `(f, g)`, where `f = g`, is an isomorphism. -/
theorem isIso_colimit_cocone_parallelPair_of_eq (hâ‚€ : f = g) {c : Cofork f g} (h : IsColimit c) :
    IsIso c.Ï€ :=
  Iso.isIso_hom <| IsColimit.coconePointUniqueUpToIso (isColimitIdCofork hâ‚€) h

/-- The coequalizer of `(f, g)`, where `f = g`, is an isomorphism. -/
theorem coequalizer.Ï€_of_eq [HasCoequalizer f g] (h : f = g) : IsIso (coequalizer.Ï€ f g) :=
  isIso_colimit_cocone_parallelPair_of_eq h <| colimit.isColimit _

/-- Every coequalizer of `(f, f)` is an isomorphism. -/
theorem isIso_colimit_cocone_parallelPair_of_self {c : Cofork f f} (h : IsColimit c) : IsIso c.Ï€ :=
  isIso_colimit_cocone_parallelPair_of_eq rfl h

/-- A coequalizer that is a monomorphism is an isomorphism. -/
theorem isIso_limit_cocone_parallelPair_of_epi {c : Cofork f g} (h : IsColimit c) [Mono c.Ï€] :
    IsIso c.Ï€ :=
  isIso_colimit_cocone_parallelPair_of_eq ((cancel_mono _).1 (Cofork.condition c)) h

/-- Two morphisms are equal if there is a cofork whose projection is mono. -/
theorem eq_of_mono_cofork_Ï€ (t : Cofork f g) [Mono (Cofork.Ï€ t)] : f = g :=
  (cancel_mono (Cofork.Ï€ t)).1 <| Cofork.condition t

/-- If the coequalizer of two morphisms is a monomorphism, then the two morphisms are equal. -/
theorem eq_of_mono_coequalizer [HasCoequalizer f g] [Mono (coequalizer.Ï€ f g)] : f = g :=
  (cancel_mono (coequalizer.Ï€ f g)).1 <| coequalizer.condition _ _

end

instance hasCoequalizer_of_self : HasCoequalizer f f :=
  HasColimit.mk
    { cocone := idCofork rfl
      isColimit := isColimitIdCofork rfl }

/-- The coequalizer projection for `(f, f)` is an isomorphism. -/
instance coequalizer.Ï€_of_self : IsIso (coequalizer.Ï€ f f) :=
  coequalizer.Ï€_of_eq rfl

/-- The coequalizer of a morphism with itself is isomorphic to the target. -/
noncomputable def coequalizer.isoTargetOfSelf : coequalizer f f â‰… Y :=
  (asIso (coequalizer.Ï€ f f)).symm

@[simp]
theorem coequalizer.isoTargetOfSelf_hom :
    (coequalizer.isoTargetOfSelf f).hom = coequalizer.desc (ğŸ™ Y) (by simp) := by
  ext
  simp [coequalizer.isoTargetOfSelf]

@[simp]
theorem coequalizer.isoTargetOfSelf_inv : (coequalizer.isoTargetOfSelf f).inv = coequalizer.Ï€ f f :=
  rfl

section Comparison

variable {D : Type uâ‚‚} [Category.{vâ‚‚} D] (G : C â¥¤ D)

/-- The comparison morphism for the equalizer of `f,g`.
This is an isomorphism iff `G` preserves the equalizer of `f,g`; see
`CategoryTheory/Limits/Preserves/Shapes/Equalizers.lean`
-/
noncomputable def equalizerComparison [HasEqualizer f g] [HasEqualizer (G.map f) (G.map g)] :
    G.obj (equalizer f g) âŸ¶ equalizer (G.map f) (G.map g) :=
  equalizer.lift (G.map (equalizer.Î¹ _ _))
    (by simp only [â† G.map_comp]; rw [equalizer.condition])

@[reassoc (attr := simp)]
theorem equalizerComparison_comp_Ï€ [HasEqualizer f g] [HasEqualizer (G.map f) (G.map g)] :
    equalizerComparison f g G â‰« equalizer.Î¹ (G.map f) (G.map g) = G.map (equalizer.Î¹ f g) :=
  equalizer.lift_Î¹ _ _

@[reassoc (attr := simp)]
theorem map_lift_equalizerComparison [HasEqualizer f g] [HasEqualizer (G.map f) (G.map g)] {Z : C}
    {h : Z âŸ¶ X} (w : h â‰« f = h â‰« g) :
    G.map (equalizer.lift h w) â‰« equalizerComparison f g G =
      equalizer.lift (G.map h) (by simp only [â† G.map_comp, w]) := by
  apply equalizer.hom_ext
  simp [â† G.map_comp]

/-- The comparison morphism for the coequalizer of `f,g`. -/
noncomputable def coequalizerComparison [HasCoequalizer f g] [HasCoequalizer (G.map f) (G.map g)] :
    coequalizer (G.map f) (G.map g) âŸ¶ G.obj (coequalizer f g) :=
  coequalizer.desc (G.map (coequalizer.Ï€ _ _))
    (by simp only [â† G.map_comp]; rw [coequalizer.condition])

@[reassoc (attr := simp)]
theorem Î¹_comp_coequalizerComparison [HasCoequalizer f g] [HasCoequalizer (G.map f) (G.map g)] :
    coequalizer.Ï€ _ _ â‰« coequalizerComparison f g G = G.map (coequalizer.Ï€ _ _) :=
  coequalizer.Ï€_desc _ _

@[reassoc (attr := simp)]
theorem coequalizerComparison_map_desc [HasCoequalizer f g] [HasCoequalizer (G.map f) (G.map g)]
    {Z : C} {h : Y âŸ¶ Z} (w : f â‰« h = g â‰« h) :
    coequalizerComparison f g G â‰« G.map (coequalizer.desc h w) =
      coequalizer.desc (G.map h) (by simp only [â† G.map_comp, w]) := by
  apply coequalizer.hom_ext
  simp [â† G.map_comp]

end Comparison

variable (C)

/-- A category `HasEqualizers` if it has all limits of shape `WalkingParallelPair`, i.e. if it has
an equalizer for every parallel pair of morphisms. -/
abbrev HasEqualizers :=
  HasLimitsOfShape WalkingParallelPair C

/-- A category `HasCoequalizers` if it has all colimits of shape `WalkingParallelPair`, i.e. if it
has a coequalizer for every parallel pair of morphisms. -/
abbrev HasCoequalizers :=
  HasColimitsOfShape WalkingParallelPair C

/-- If `C` has all limits of diagrams `parallelPair f g`, then it has all equalizers -/
theorem hasEqualizers_of_hasLimit_parallelPair
    [âˆ€ {X Y : C} {f g : X âŸ¶ Y}, HasLimit (parallelPair f g)] : HasEqualizers C :=
  { has_limit := fun F => hasLimit_of_iso (diagramIsoParallelPair F).symm }

/-- If `C` has all colimits of diagrams `parallelPair f g`, then it has all coequalizers -/
theorem hasCoequalizers_of_hasColimit_parallelPair
    [âˆ€ {X Y : C} {f g : X âŸ¶ Y}, HasColimit (parallelPair f g)] : HasCoequalizers C :=
  { has_colimit := fun F => hasColimit_of_iso (diagramIsoParallelPair F) }

section

-- In this section we show that a split mono `f` equalizes `(retraction f â‰« f)` and `(ğŸ™ Y)`.
variable {C} [IsSplitMono f]

/-- A split mono `f` equalizes `(retraction f â‰« f)` and `(ğŸ™ Y)`.
Here we build the cone, and show in `isSplitMonoEqualizes` that it is a limit cone.
-/
@[simps (rhsMd := default)]
noncomputable def coneOfIsSplitMono : Fork (ğŸ™ Y) (retraction f â‰« f) :=
  Fork.ofÎ¹ f (by simp)

@[simp]
theorem coneOfIsSplitMono_Î¹ : (coneOfIsSplitMono f).Î¹ = f :=
  rfl

/-- A split mono `f` equalizes `(retraction f â‰« f)` and `(ğŸ™ Y)`.
-/
noncomputable def isSplitMonoEqualizes {X Y : C} (f : X âŸ¶ Y) [IsSplitMono f] :
    IsLimit (coneOfIsSplitMono f) :=
  Fork.IsLimit.mk' _ fun s =>
    âŸ¨s.Î¹ â‰« retraction f, by
      dsimp
      rw [Category.assoc, â† s.condition]
      apply Category.comp_id, fun hm => by simp [â† hm]âŸ©

end

/-- We show that the converse to `isSplitMonoEqualizes` is true:
Whenever `f` equalizes `(r â‰« f)` and `(ğŸ™ Y)`, then `r` is a retraction of `f`. -/
def splitMonoOfEqualizer {X Y : C} {f : X âŸ¶ Y} {r : Y âŸ¶ X} (hr : f â‰« r â‰« f = f)
    (h : IsLimit (Fork.ofÎ¹ f (hr.trans (Category.comp_id _).symm : f â‰« r â‰« f = f â‰« ğŸ™ Y))) :
    SplitMono f where
  retraction := r
  id := Fork.IsLimit.hom_ext h ((Category.assoc _ _ _).trans <| hr.trans (Category.id_comp _).symm)

variable {C f g}

/-- The fork obtained by postcomposing an equalizer fork with a monomorphism is an equalizer. -/
def isEqualizerCompMono {c : Fork f g} (i : IsLimit c) {Z : C} (h : Y âŸ¶ Z) [hm : Mono h] :
    have : Fork.Î¹ c â‰« f â‰« h = Fork.Î¹ c â‰« g â‰« h := by
      simp only [â† Category.assoc]
      exact congrArg (Â· â‰« h) c.condition
    IsLimit (Fork.ofÎ¹ c.Î¹ (by simp [this]) : Fork (f â‰« h) (g â‰« h)) :=
  Fork.IsLimit.mk' _ fun s =>
    let s' : Fork f g := Fork.ofÎ¹ s.Î¹ (by apply hm.right_cancellation; simp [s.condition])
    let l := Fork.IsLimit.lift' i s'.Î¹ s'.condition
    âŸ¨l.1, l.2, fun hm => by
      apply Fork.IsLimit.hom_ext i; rw [Fork.Î¹_ofÎ¹] at hm; rw [hm]; exact l.2.symmâŸ©

variable (C f g)

@[instance]
theorem hasEqualizer_comp_mono [HasEqualizer f g] {Z : C} (h : Y âŸ¶ Z) [Mono h] :
    HasEqualizer (f â‰« h) (g â‰« h) :=
  âŸ¨âŸ¨{   cone := _
        isLimit := isEqualizerCompMono (limit.isLimit _) h }âŸ©âŸ©

/-- An equalizer of an idempotent morphism and the identity is split mono. -/
@[simps]
def splitMonoOfIdempotentOfIsLimitFork {X : C} {f : X âŸ¶ X} (hf : f â‰« f = f) {c : Fork (ğŸ™ X) f}
    (i : IsLimit c) : SplitMono c.Î¹ where
  retraction := i.lift (Fork.ofÎ¹ f (by simp [hf]))
  id := by
    letI := mono_of_isLimit_fork i
    rw [â† cancel_mono_id c.Î¹, Category.assoc, Fork.IsLimit.lift_Î¹, Fork.Î¹_ofÎ¹, â† c.condition]
    exact Category.comp_id c.Î¹

/-- The equalizer of an idempotent morphism and the identity is split mono. -/
noncomputable def splitMonoOfIdempotentEqualizer {X : C} {f : X âŸ¶ X} (hf : f â‰« f = f)
    [HasEqualizer (ğŸ™ X) f] : SplitMono (equalizer.Î¹ (ğŸ™ X) f) :=
  splitMonoOfIdempotentOfIsLimitFork _ hf (limit.isLimit _)

section

-- In this section we show that a split epi `f` coequalizes `(f â‰« section_ f)` and `(ğŸ™ X)`.
variable {C} [IsSplitEpi f]

/-- A split epi `f` coequalizes `(f â‰« section_ f)` and `(ğŸ™ X)`.
Here we build the cocone, and show in `isSplitEpiCoequalizes` that it is a colimit cocone.
-/
@[simps (rhsMd := default)]
noncomputable def coconeOfIsSplitEpi : Cofork (ğŸ™ X) (f â‰« section_ f) :=
  Cofork.ofÏ€ f (by simp)

@[simp]
theorem coconeOfIsSplitEpi_Ï€ : (coconeOfIsSplitEpi f).Ï€ = f :=
  rfl

/-- A split epi `f` coequalizes `(f â‰« section_ f)` and `(ğŸ™ X)`.
-/
noncomputable def isSplitEpiCoequalizes {X Y : C} (f : X âŸ¶ Y) [IsSplitEpi f] :
    IsColimit (coconeOfIsSplitEpi f) :=
  Cofork.IsColimit.mk' _ fun s =>
    âŸ¨section_ f â‰« s.Ï€, by
      dsimp
      rw [â† Category.assoc, â† s.condition, Category.id_comp], fun hm => by simp [â† hm]âŸ©

end

/-- We show that the converse to `isSplitEpiEqualizes` is true:
Whenever `f` coequalizes `(f â‰« s)` and `(ğŸ™ X)`, then `s` is a section of `f`. -/
def splitEpiOfCoequalizer {X Y : C} {f : X âŸ¶ Y} {s : Y âŸ¶ X} (hs : f â‰« s â‰« f = f)
    (h :
      IsColimit
        (Cofork.ofÏ€ f
          ((Category.assoc _ _ _).trans <| hs.trans (Category.id_comp f).symm :
            (f â‰« s) â‰« f = ğŸ™ X â‰« f))) :
    SplitEpi f where
  section_ := s
  id := Cofork.IsColimit.hom_ext h (hs.trans (Category.comp_id _).symm)

variable {C f g}

/-- The cofork obtained by precomposing a coequalizer cofork with an epimorphism is
a coequalizer. -/
def isCoequalizerEpiComp {c : Cofork f g} (i : IsColimit c) {W : C} (h : W âŸ¶ X) [hm : Epi h] :
    have : (h â‰« f) â‰« Cofork.Ï€ c = (h â‰« g) â‰« Cofork.Ï€ c := by
      simp only [Category.assoc]
      exact congrArg (h â‰« Â·) c.condition
    IsColimit (Cofork.ofÏ€ c.Ï€ (this) : Cofork (h â‰« f) (h â‰« g)) :=
  Cofork.IsColimit.mk' _ fun s =>
    let s' : Cofork f g :=
      Cofork.ofÏ€ s.Ï€ (by apply hm.left_cancellation; simp_rw [â† Category.assoc, s.condition])
    let l := Cofork.IsColimit.desc' i s'.Ï€ s'.condition
    âŸ¨l.1, l.2, fun hm => by
      apply Cofork.IsColimit.hom_ext i; rw [Cofork.Ï€_ofÏ€] at hm; rw [hm]; exact l.2.symmâŸ©

theorem hasCoequalizer_epi_comp [HasCoequalizer f g] {W : C} (h : W âŸ¶ X) [Epi h] :
    HasCoequalizer (h â‰« f) (h â‰« g) :=
  âŸ¨âŸ¨{   cocone := _
        isColimit := isCoequalizerEpiComp (colimit.isColimit _) h }âŸ©âŸ©

variable (C f g)

/-- A coequalizer of an idempotent morphism and the identity is split epi. -/
@[simps]
def splitEpiOfIdempotentOfIsColimitCofork {X : C} {f : X âŸ¶ X} (hf : f â‰« f = f) {c : Cofork (ğŸ™ X) f}
    (i : IsColimit c) : SplitEpi c.Ï€ where
  section_ := i.desc (Cofork.ofÏ€ f (by simp [hf]))
  id := by
    letI := epi_of_isColimit_cofork i
    rw [â† cancel_epi_id c.Ï€, â† Category.assoc, Cofork.IsColimit.Ï€_desc, Cofork.Ï€_ofÏ€, â†
      c.condition]
    exact Category.id_comp _

/-- The coequalizer of an idempotent morphism and the identity is split epi. -/
noncomputable def splitEpiOfIdempotentCoequalizer {X : C} {f : X âŸ¶ X} (hf : f â‰« f = f)
    [HasCoequalizer (ğŸ™ X) f] : SplitEpi (coequalizer.Ï€ (ğŸ™ X) f) :=
  splitEpiOfIdempotentOfIsColimitCofork _ hf (colimit.isColimit _)

end CategoryTheory.Limits

end
