/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Markus Himmel
-/
import Mathlib.CategoryTheory.EpiMono
import Mathlib.CategoryTheory.Limits.HasLimits

#align_import category_theory.limits.shapes.equalizers from "leanprover-community/mathlib"@"4698e35ca56a0d4fa53aa5639c3364e0a77f4eba"

/-!
# Equalizers and coequalizers

This file defines (co)equalizers as special cases of (co)limits.

An equalizer is the categorical generalization of the subobject {a ‚àà A | f(a) = g(a)} known
from abelian groups or modules. It is a limit cone over the diagram formed by `f` and `g`.

A coequalizer is the dual concept.

## Main definitions

* `WalkingParallelPair` is the indexing category used for (co)equalizer_diagrams
* `parallelPair` is a functor from `WalkingParallelPair` to our category `C`.
* a `fork` is a cone over a parallel pair.
  * there is really only one interesting morphism in a fork: the arrow from the vertex of the fork
    to the domain of f and g. It is called `fork.Œπ`.
* an `equalizer` is now just a `limit (parallelPair f g)`

Each of these has a dual.

## Main statements

* `equalizer.Œπ_mono` states that every equalizer map is a monomorphism
* `isIso_limit_cone_parallelPair_of_self` states that the identity on the domain of `f` is an
  equalizer of `f` and `f`.

## Implementation notes
As with the other special shapes in the limits library, all the definitions here are given as
`abbreviation`s of the general statements for limits, so all the `simp` lemmas and theorems about
general limits can be used.

## References

* [F. Borceux, *Handbook of Categorical Algebra 1*][borceux-vol1]
-/

/- Porting note: removed global noncomputable since there are things that might be
computable value like WalkingPair -/
section

open CategoryTheory Opposite

namespace CategoryTheory.Limits

-- attribute [local tidy] tactic.case_bash -- Porting note: no tidy nor cases_bash

universe v v‚ÇÇ u u‚ÇÇ

/-- The type of objects for the diagram indexing a (co)equalizer. -/
inductive WalkingParallelPair : Type
  | zero
  | one
  deriving DecidableEq, Inhabited
#align category_theory.limits.walking_parallel_pair CategoryTheory.Limits.WalkingParallelPair

open WalkingParallelPair

/-- The type family of morphisms for the diagram indexing a (co)equalizer. -/
inductive WalkingParallelPairHom : WalkingParallelPair ‚Üí WalkingParallelPair ‚Üí Type
  | left : WalkingParallelPairHom zero one
  | right : WalkingParallelPairHom zero one
  | id (X : WalkingParallelPair) : WalkingParallelPairHom X X
  deriving DecidableEq
#align category_theory.limits.walking_parallel_pair_hom CategoryTheory.Limits.WalkingParallelPairHom

/- Porting note: this simplifies using walkingParallelPairHom_id; replacement is below;
simpNF still complains of striking this from the simp list -/
attribute [-simp, nolint simpNF] WalkingParallelPairHom.id.sizeOf_spec

/-- Satisfying the inhabited linter -/
instance : Inhabited (WalkingParallelPairHom zero one) where default := WalkingParallelPairHom.left

open WalkingParallelPairHom

/-- Composition of morphisms in the indexing diagram for (co)equalizers. -/
def WalkingParallelPairHom.comp :
    -- Porting note: changed X Y Z to implicit to match comp fields in precategory
    ‚àÄ { X Y Z : WalkingParallelPair } (_ : WalkingParallelPairHom X Y)
      (_ : WalkingParallelPairHom Y Z), WalkingParallelPairHom X Z
  | _, _, _, id _, h => h
  | _, _, _, left, id one => left
  | _, _, _, right, id one => right
#align category_theory.limits.walking_parallel_pair_hom.comp CategoryTheory.Limits.WalkingParallelPairHom.comp

-- Porting note: adding these since they are simple and aesop couldn't directly prove them
theorem WalkingParallelPairHom.id_comp
    {X Y : WalkingParallelPair} (g : WalkingParallelPairHom X Y) : comp (id X) g = g :=
  rfl

theorem WalkingParallelPairHom.comp_id
    {X Y : WalkingParallelPair} (f : WalkingParallelPairHom X Y) : comp f (id Y) = f := by
  cases f <;> rfl

theorem WalkingParallelPairHom.assoc {X Y Z W : WalkingParallelPair}
    (f : WalkingParallelPairHom X Y) (g: WalkingParallelPairHom Y Z)
    (h : WalkingParallelPairHom Z W) : comp (comp f g) h = comp f (comp g h) := by
  cases f <;> cases g <;> cases h <;> rfl

instance walkingParallelPairHomCategory : SmallCategory WalkingParallelPair where
  Hom := WalkingParallelPairHom
  id := id
  comp := comp
  comp_id := comp_id
  id_comp := id_comp
  assoc := assoc
#align category_theory.limits.walking_parallel_pair_hom_category CategoryTheory.Limits.walkingParallelPairHomCategory

@[simp]
theorem walkingParallelPairHom_id (X : WalkingParallelPair) : WalkingParallelPairHom.id X = ùüô X :=
  rfl
#align category_theory.limits.walking_parallel_pair_hom_id CategoryTheory.Limits.walkingParallelPairHom_id

-- Porting note: simpNF asked me to do this because the LHS of the non-primed version reduced
@[simp]
theorem WalkingParallelPairHom.id.sizeOf_spec' (X : WalkingParallelPair) :
    (WalkingParallelPairHom._sizeOf_inst X X).sizeOf (ùüô X) = 1 + sizeOf X := by cases X <;> rfl

/-- The functor `WalkingParallelPair ‚•§ WalkingParallelPair·µí·µñ` sending left to left and right to
right.
-/
def walkingParallelPairOp : WalkingParallelPair ‚•§ WalkingParallelPair·µí·µñ where
  obj x := op <| by cases x; exacts [one, zero]
  map f := by
    cases f <;> apply Quiver.Hom.op
    exacts [left, right, WalkingParallelPairHom.id _]
  map_comp := by rintro _ _ _ (_|_|_) g <;> cases g <;> rfl
#align category_theory.limits.walking_parallel_pair_op CategoryTheory.Limits.walkingParallelPairOp

@[simp]
theorem walkingParallelPairOp_zero : walkingParallelPairOp.obj zero = op one := rfl
#align category_theory.limits.walking_parallel_pair_op_zero CategoryTheory.Limits.walkingParallelPairOp_zero

@[simp]
theorem walkingParallelPairOp_one : walkingParallelPairOp.obj one = op zero := rfl
#align category_theory.limits.walking_parallel_pair_op_one CategoryTheory.Limits.walkingParallelPairOp_one

@[simp]
theorem walkingParallelPairOp_left :
    walkingParallelPairOp.map left = @Quiver.Hom.op _ _ zero one left := rfl
#align category_theory.limits.walking_parallel_pair_op_left CategoryTheory.Limits.walkingParallelPairOp_left

@[simp]
theorem walkingParallelPairOp_right :
    walkingParallelPairOp.map right = @Quiver.Hom.op _ _ zero one right := rfl
#align category_theory.limits.walking_parallel_pair_op_right CategoryTheory.Limits.walkingParallelPairOp_right

/--
The equivalence `WalkingParallelPair ‚•§ WalkingParallelPair·µí·µñ` sending left to left and right to
right.
-/
@[simps functor inverse]
def walkingParallelPairOpEquiv : WalkingParallelPair ‚âå WalkingParallelPair·µí·µñ where
  functor := walkingParallelPairOp
  inverse := walkingParallelPairOp.leftOp
  unitIso :=
    NatIso.ofComponents (fun j => eqToIso (by cases j <;> rfl))
      (by rintro _ _ (_ | _ | _) <;> simp)
  counitIso :=
    NatIso.ofComponents (fun j => eqToIso (by
            induction' j with X
            cases X <;> rfl))
      (fun {i} {j} f => by
      induction' i with i
      induction' j with j
      let g := f.unop
      have : f = g.op := rfl
      rw [this]
      cases i <;> cases j <;> cases g <;> rfl)
  functor_unitIso_comp := fun j => by cases j <;> rfl
#align category_theory.limits.walking_parallel_pair_op_equiv CategoryTheory.Limits.walkingParallelPairOpEquiv

@[simp]
theorem walkingParallelPairOpEquiv_unitIso_zero :
    walkingParallelPairOpEquiv.unitIso.app zero = Iso.refl zero := rfl
#align category_theory.limits.walking_parallel_pair_op_equiv_unit_iso_zero CategoryTheory.Limits.walkingParallelPairOpEquiv_unitIso_zero

@[simp]
theorem walkingParallelPairOpEquiv_unitIso_one :
    walkingParallelPairOpEquiv.unitIso.app one = Iso.refl one := rfl
#align category_theory.limits.walking_parallel_pair_op_equiv_unit_iso_one CategoryTheory.Limits.walkingParallelPairOpEquiv_unitIso_one

@[simp]
theorem walkingParallelPairOpEquiv_counitIso_zero :
    walkingParallelPairOpEquiv.counitIso.app (op zero) = Iso.refl (op zero) := rfl
#align category_theory.limits.walking_parallel_pair_op_equiv_counit_iso_zero CategoryTheory.Limits.walkingParallelPairOpEquiv_counitIso_zero

@[simp]
theorem walkingParallelPairOpEquiv_counitIso_one :
    walkingParallelPairOpEquiv.counitIso.app (op one) = Iso.refl (op one) :=
  rfl
#align category_theory.limits.walking_parallel_pair_op_equiv_counit_iso_one CategoryTheory.Limits.walkingParallelPairOpEquiv_counitIso_one

variable {C : Type u} [Category.{v} C]
variable {X Y : C}

/-- `parallelPair f g` is the diagram in `C` consisting of the two morphisms `f` and `g` with
    common domain and codomain. -/
def parallelPair (f g : X ‚ü∂ Y) : WalkingParallelPair ‚•§ C where
  obj x :=
    match x with
    | zero => X
    | one => Y
  map h :=
    match h with
    | WalkingParallelPairHom.id _ => ùüô _
    | left => f
    | right => g
  -- `sorry` can cope with this, but it's too slow:
  map_comp := by
    rintro _ _ _ ‚ü®‚ü© g <;> cases g <;> {dsimp; simp}
#align category_theory.limits.parallel_pair CategoryTheory.Limits.parallelPair

@[simp]
theorem parallelPair_obj_zero (f g : X ‚ü∂ Y) : (parallelPair f g).obj zero = X := rfl
#align category_theory.limits.parallel_pair_obj_zero CategoryTheory.Limits.parallelPair_obj_zero

@[simp]
theorem parallelPair_obj_one (f g : X ‚ü∂ Y) : (parallelPair f g).obj one = Y := rfl
#align category_theory.limits.parallel_pair_obj_one CategoryTheory.Limits.parallelPair_obj_one

@[simp]
theorem parallelPair_map_left (f g : X ‚ü∂ Y) : (parallelPair f g).map left = f := rfl
#align category_theory.limits.parallel_pair_map_left CategoryTheory.Limits.parallelPair_map_left

@[simp]
theorem parallelPair_map_right (f g : X ‚ü∂ Y) : (parallelPair f g).map right = g := rfl
#align category_theory.limits.parallel_pair_map_right CategoryTheory.Limits.parallelPair_map_right

@[simp]
theorem parallelPair_functor_obj {F : WalkingParallelPair ‚•§ C} (j : WalkingParallelPair) :
    (parallelPair (F.map left) (F.map right)).obj j = F.obj j := by cases j <;> rfl
#align category_theory.limits.parallel_pair_functor_obj CategoryTheory.Limits.parallelPair_functor_obj

/-- Every functor indexing a (co)equalizer is naturally isomorphic (actually, equal) to a
    `parallelPair` -/
@[simps!]
def diagramIsoParallelPair (F : WalkingParallelPair ‚•§ C) :
    F ‚âÖ parallelPair (F.map left) (F.map right) :=
  NatIso.ofComponents (fun j => eqToIso <| by cases j <;> rfl) (by rintro _ _ (_|_|_) <;> simp)
#align category_theory.limits.diagram_iso_parallel_pair CategoryTheory.Limits.diagramIsoParallelPair

/-- Construct a morphism between parallel pairs. -/
def parallelPairHom {X' Y' : C} (f g : X ‚ü∂ Y) (f' g' : X' ‚ü∂ Y') (p : X ‚ü∂ X') (q : Y ‚ü∂ Y')
    (wf : f ‚â´ q = p ‚â´ f') (wg : g ‚â´ q = p ‚â´ g') : parallelPair f g ‚ü∂ parallelPair f' g' where
  app j :=
    match j with
    | zero => p
    | one => q
  naturality := by
    rintro _ _ ‚ü®‚ü© <;> {dsimp; simp [wf,wg]}
#align category_theory.limits.parallel_pair_hom CategoryTheory.Limits.parallelPairHom

@[simp]
theorem parallelPairHom_app_zero {X' Y' : C} (f g : X ‚ü∂ Y) (f' g' : X' ‚ü∂ Y') (p : X ‚ü∂ X')
    (q : Y ‚ü∂ Y') (wf : f ‚â´ q = p ‚â´ f') (wg : g ‚â´ q = p ‚â´ g') :
    (parallelPairHom f g f' g' p q wf wg).app zero = p :=
  rfl
#align category_theory.limits.parallel_pair_hom_app_zero CategoryTheory.Limits.parallelPairHom_app_zero

@[simp]
theorem parallelPairHom_app_one {X' Y' : C} (f g : X ‚ü∂ Y) (f' g' : X' ‚ü∂ Y') (p : X ‚ü∂ X')
    (q : Y ‚ü∂ Y') (wf : f ‚â´ q = p ‚â´ f') (wg : g ‚â´ q = p ‚â´ g') :
    (parallelPairHom f g f' g' p q wf wg).app one = q :=
  rfl
#align category_theory.limits.parallel_pair_hom_app_one CategoryTheory.Limits.parallelPairHom_app_one

/-- Construct a natural isomorphism between functors out of the walking parallel pair from
its components. -/
@[simps!]
def parallelPair.ext {F G : WalkingParallelPair ‚•§ C} (zero : F.obj zero ‚âÖ G.obj zero)
    (one : F.obj one ‚âÖ G.obj one) (left : F.map left ‚â´ one.hom = zero.hom ‚â´ G.map left)
    (right : F.map right ‚â´ one.hom = zero.hom ‚â´ G.map right) : F ‚âÖ G :=
  NatIso.ofComponents
    (by
      rintro ‚ü®j‚ü©
      exacts [zero, one])
    (by rintro _ _ ‚ü®_‚ü© <;> simp [left, right])
#align category_theory.limits.parallel_pair.ext CategoryTheory.Limits.parallelPair.ext

/-- Construct a natural isomorphism between `parallelPair f g` and `parallelPair f' g'` given
equalities `f = f'` and `g = g'`. -/
@[simps!]
def parallelPair.eqOfHomEq {f g f' g' : X ‚ü∂ Y} (hf : f = f') (hg : g = g') :
    parallelPair f g ‚âÖ parallelPair f' g' :=
  parallelPair.ext (Iso.refl _) (Iso.refl _) (by simp [hf]) (by simp [hg])
#align category_theory.limits.parallel_pair.eq_of_hom_eq CategoryTheory.Limits.parallelPair.eqOfHomEq

/-- A fork on `f` and `g` is just a `Cone (parallelPair f g)`. -/
abbrev Fork (f g : X ‚ü∂ Y) :=
  Cone (parallelPair f g)
#align category_theory.limits.fork CategoryTheory.Limits.Fork

/-- A cofork on `f` and `g` is just a `Cocone (parallelPair f g)`. -/
abbrev Cofork (f g : X ‚ü∂ Y) :=
  Cocone (parallelPair f g)
#align category_theory.limits.cofork CategoryTheory.Limits.Cofork

variable {f g : X ‚ü∂ Y}

/-- A fork `t` on the parallel pair `f g : X ‚ü∂ Y` consists of two morphisms
    `t.œÄ.app zero : t.pt ‚ü∂ X`
    and `t.œÄ.app one : t.pt ‚ü∂ Y`. Of these, only the first one is interesting, and we give it the
    shorter name `Fork.Œπ t`. -/
def Fork.Œπ (t : Fork f g) :=
  t.œÄ.app zero
#align category_theory.limits.fork.Œπ CategoryTheory.Limits.Fork.Œπ

@[simp]
theorem Fork.app_zero_eq_Œπ (t : Fork f g) : t.œÄ.app zero = t.Œπ :=
  rfl
#align category_theory.limits.fork.app_zero_eq_Œπ CategoryTheory.Limits.Fork.app_zero_eq_Œπ

/-- A cofork `t` on the parallelPair `f g : X ‚ü∂ Y` consists of two morphisms
    `t.Œπ.app zero : X ‚ü∂ t.pt` and `t.Œπ.app one : Y ‚ü∂ t.pt`. Of these, only the second one is
    interesting, and we give it the shorter name `Cofork.œÄ t`. -/
def Cofork.œÄ (t : Cofork f g) :=
  t.Œπ.app one
#align category_theory.limits.cofork.œÄ CategoryTheory.Limits.Cofork.œÄ

@[simp]
theorem Cofork.app_one_eq_œÄ (t : Cofork f g) : t.Œπ.app one = t.œÄ :=
  rfl
#align category_theory.limits.cofork.app_one_eq_œÄ CategoryTheory.Limits.Cofork.app_one_eq_œÄ

@[simp]
theorem Fork.app_one_eq_Œπ_comp_left (s : Fork f g) : s.œÄ.app one = s.Œπ ‚â´ f := by
  rw [‚Üê s.app_zero_eq_Œπ, ‚Üê s.w left, parallelPair_map_left]
#align category_theory.limits.fork.app_one_eq_Œπ_comp_left CategoryTheory.Limits.Fork.app_one_eq_Œπ_comp_left

@[reassoc]
theorem Fork.app_one_eq_Œπ_comp_right (s : Fork f g) : s.œÄ.app one = s.Œπ ‚â´ g := by
  rw [‚Üê s.app_zero_eq_Œπ, ‚Üê s.w right, parallelPair_map_right]
#align category_theory.limits.fork.app_one_eq_Œπ_comp_right CategoryTheory.Limits.Fork.app_one_eq_Œπ_comp_right

@[simp]
theorem Cofork.app_zero_eq_comp_œÄ_left (s : Cofork f g) : s.Œπ.app zero = f ‚â´ s.œÄ := by
  rw [‚Üê s.app_one_eq_œÄ, ‚Üê s.w left, parallelPair_map_left]
#align category_theory.limits.cofork.app_zero_eq_comp_œÄ_left CategoryTheory.Limits.Cofork.app_zero_eq_comp_œÄ_left

@[reassoc]
theorem Cofork.app_zero_eq_comp_œÄ_right (s : Cofork f g) : s.Œπ.app zero = g ‚â´ s.œÄ := by
  rw [‚Üê s.app_one_eq_œÄ, ‚Üê s.w right, parallelPair_map_right]
#align category_theory.limits.cofork.app_zero_eq_comp_œÄ_right CategoryTheory.Limits.Cofork.app_zero_eq_comp_œÄ_right

/-- A fork on `f g : X ‚ü∂ Y` is determined by the morphism `Œπ : P ‚ü∂ X` satisfying `Œπ ‚â´ f = Œπ ‚â´ g`.
-/
@[simps]
def Fork.ofŒπ {P : C} (Œπ : P ‚ü∂ X) (w : Œπ ‚â´ f = Œπ ‚â´ g) : Fork f g where
  pt := P
  œÄ :=
    { app := fun X => by
        cases X
        ¬∑ exact Œπ
        ¬∑ exact Œπ ‚â´ f
      naturality := fun {X} {Y} f =>
        by cases X <;> cases Y <;> cases f <;> dsimp <;> simp; assumption }
#align category_theory.limits.fork.of_Œπ CategoryTheory.Limits.Fork.ofŒπ

/-- A cofork on `f g : X ‚ü∂ Y` is determined by the morphism `œÄ : Y ‚ü∂ P` satisfying
    `f ‚â´ œÄ = g ‚â´ œÄ`. -/
@[simps]
def Cofork.ofœÄ {P : C} (œÄ : Y ‚ü∂ P) (w : f ‚â´ œÄ = g ‚â´ œÄ) : Cofork f g where
  pt := P
  Œπ :=
    { app := fun X => WalkingParallelPair.casesOn X (f ‚â´ œÄ) œÄ
      naturality := fun i j f => by cases f <;> dsimp <;> simp [w] }
#align category_theory.limits.cofork.of_œÄ CategoryTheory.Limits.Cofork.ofœÄ

-- See note [dsimp, simp]
@[simp]
theorem Fork.Œπ_ofŒπ {P : C} (Œπ : P ‚ü∂ X) (w : Œπ ‚â´ f = Œπ ‚â´ g) : (Fork.ofŒπ Œπ w).Œπ = Œπ :=
  rfl
#align category_theory.limits.fork.Œπ_of_Œπ CategoryTheory.Limits.Fork.Œπ_ofŒπ

@[simp]
theorem Cofork.œÄ_ofœÄ {P : C} (œÄ : Y ‚ü∂ P) (w : f ‚â´ œÄ = g ‚â´ œÄ) : (Cofork.ofœÄ œÄ w).œÄ = œÄ :=
  rfl
#align category_theory.limits.cofork.œÄ_of_œÄ CategoryTheory.Limits.Cofork.œÄ_ofœÄ

@[reassoc (attr := simp)]
theorem Fork.condition (t : Fork f g) : t.Œπ ‚â´ f = t.Œπ ‚â´ g := by
  rw [‚Üê t.app_one_eq_Œπ_comp_left, ‚Üê t.app_one_eq_Œπ_comp_right]
#align category_theory.limits.fork.condition CategoryTheory.Limits.Fork.condition

@[reassoc (attr := simp)]
theorem Cofork.condition (t : Cofork f g) : f ‚â´ t.œÄ = g ‚â´ t.œÄ := by
  rw [‚Üê t.app_zero_eq_comp_œÄ_left, ‚Üê t.app_zero_eq_comp_œÄ_right]
#align category_theory.limits.cofork.condition CategoryTheory.Limits.Cofork.condition

/-- To check whether two maps are equalized by both maps of a fork, it suffices to check it for the
    first map -/
theorem Fork.equalizer_ext (s : Fork f g) {W : C} {k l : W ‚ü∂ s.pt} (h : k ‚â´ s.Œπ = l ‚â´ s.Œπ) :
    ‚àÄ j : WalkingParallelPair, k ‚â´ s.œÄ.app j = l ‚â´ s.œÄ.app j
  | zero => h
  | one => by
    have : k ‚â´ Œπ s ‚â´ f = l ‚â´ Œπ s ‚â´ f := by
      simp only [‚Üê Category.assoc]; exact congrArg (¬∑ ‚â´ f) h
    rw [s.app_one_eq_Œπ_comp_left, this]
#align category_theory.limits.fork.equalizer_ext CategoryTheory.Limits.Fork.equalizer_ext

/-- To check whether two maps are coequalized by both maps of a cofork, it suffices to check it for
    the second map -/
theorem Cofork.coequalizer_ext (s : Cofork f g) {W : C} {k l : s.pt ‚ü∂ W}
    (h : Cofork.œÄ s ‚â´ k = Cofork.œÄ s ‚â´ l) : ‚àÄ j : WalkingParallelPair, s.Œπ.app j ‚â´ k = s.Œπ.app j ‚â´ l
  | zero => by simp only [s.app_zero_eq_comp_œÄ_left, Category.assoc, h]
  | one => h
#align category_theory.limits.cofork.coequalizer_ext CategoryTheory.Limits.Cofork.coequalizer_ext

theorem Fork.IsLimit.hom_ext {s : Fork f g} (hs : IsLimit s) {W : C} {k l : W ‚ü∂ s.pt}
    (h : k ‚â´ Fork.Œπ s = l ‚â´ Fork.Œπ s) : k = l :=
  hs.hom_ext <| Fork.equalizer_ext _ h
#align category_theory.limits.fork.is_limit.hom_ext CategoryTheory.Limits.Fork.IsLimit.hom_ext

theorem Cofork.IsColimit.hom_ext {s : Cofork f g} (hs : IsColimit s) {W : C} {k l : s.pt ‚ü∂ W}
    (h : Cofork.œÄ s ‚â´ k = Cofork.œÄ s ‚â´ l) : k = l :=
  hs.hom_ext <| Cofork.coequalizer_ext _ h
#align category_theory.limits.cofork.is_colimit.hom_ext CategoryTheory.Limits.Cofork.IsColimit.hom_ext

@[reassoc (attr := simp)]
theorem Fork.IsLimit.lift_Œπ {s t : Fork f g} (hs : IsLimit s) : hs.lift t ‚â´ s.Œπ = t.Œπ :=
  hs.fac _ _
#align category_theory.limits.fork.is_limit.lift_Œπ CategoryTheory.Limits.Fork.IsLimit.lift_Œπ

@[reassoc (attr := simp)]
theorem Cofork.IsColimit.œÄ_desc {s t : Cofork f g} (hs : IsColimit s) : s.œÄ ‚â´ hs.desc t = t.œÄ :=
  hs.fac _ _
#align category_theory.limits.cofork.is_colimit.œÄ_desc CategoryTheory.Limits.Cofork.IsColimit.œÄ_desc

-- Porting note: `Fork.IsLimit.lift` was added in order to ease the port
/-- If `s` is a limit fork over `f` and `g`, then a morphism `k : W ‚ü∂ X` satisfying
    `k ‚â´ f = k ‚â´ g` induces a morphism `l : W ‚ü∂ s.pt` such that `l ‚â´ fork.Œπ s = k`. -/
def Fork.IsLimit.lift {s : Fork f g} (hs : IsLimit s) {W : C} (k : W ‚ü∂ X) (h : k ‚â´ f = k ‚â´ g) :
    W ‚ü∂ s.pt :=
  hs.lift (Fork.ofŒπ _ h)

@[reassoc (attr := simp)]
lemma Fork.IsLimit.lift_Œπ' {s : Fork f g} (hs : IsLimit s) {W : C} (k : W ‚ü∂ X) (h : k ‚â´ f = k ‚â´ g) :
    Fork.IsLimit.lift hs k h ‚â´ Fork.Œπ s = k :=
    hs.fac _ _

/-- If `s` is a limit fork over `f` and `g`, then a morphism `k : W ‚ü∂ X` satisfying
    `k ‚â´ f = k ‚â´ g` induces a morphism `l : W ‚ü∂ s.pt` such that `l ‚â´ fork.Œπ s = k`. -/
def Fork.IsLimit.lift' {s : Fork f g} (hs : IsLimit s) {W : C} (k : W ‚ü∂ X) (h : k ‚â´ f = k ‚â´ g) :
    { l : W ‚ü∂ s.pt // l ‚â´ Fork.Œπ s = k } :=
  ‚ü®Fork.IsLimit.lift hs k h, by simp‚ü©
#align category_theory.limits.fork.is_limit.lift' CategoryTheory.Limits.Fork.IsLimit.lift'

-- Porting note: `Cofork.IsColimit.desc` was added in order to ease the port
/-- If `s` is a colimit cofork over `f` and `g`, then a morphism `k : Y ‚ü∂ W` satisfying
    `f ‚â´ k = g ‚â´ k` induces a morphism `l : s.pt ‚ü∂ W` such that `cofork.œÄ s ‚â´ l = k`. -/
def Cofork.IsColimit.desc {s : Cofork f g} (hs : IsColimit s) {W : C} (k : Y ‚ü∂ W)
    (h : f ‚â´ k = g ‚â´ k) : s.pt ‚ü∂ W :=
  hs.desc (Cofork.ofœÄ _ h)

@[reassoc (attr := simp)]
lemma Cofork.IsColimit.œÄ_desc' {s : Cofork f g} (hs : IsColimit s) {W : C} (k : Y ‚ü∂ W)
    (h : f ‚â´ k = g ‚â´ k) : Cofork.œÄ s ‚â´ Cofork.IsColimit.desc hs k h = k :=
  hs.fac _ _

/-- If `s` is a colimit cofork over `f` and `g`, then a morphism `k : Y ‚ü∂ W` satisfying
    `f ‚â´ k = g ‚â´ k` induces a morphism `l : s.pt ‚ü∂ W` such that `cofork.œÄ s ‚â´ l = k`. -/
def Cofork.IsColimit.desc' {s : Cofork f g} (hs : IsColimit s) {W : C} (k : Y ‚ü∂ W)
    (h : f ‚â´ k = g ‚â´ k) : { l : s.pt ‚ü∂ W // Cofork.œÄ s ‚â´ l = k } :=
  ‚ü®Cofork.IsColimit.desc hs k h, by simp‚ü©
#align category_theory.limits.cofork.is_colimit.desc' CategoryTheory.Limits.Cofork.IsColimit.desc'

theorem Fork.IsLimit.existsUnique {s : Fork f g} (hs : IsLimit s) {W : C} (k : W ‚ü∂ X)
    (h : k ‚â´ f = k ‚â´ g) : ‚àÉ! l : W ‚ü∂ s.pt, l ‚â´ Fork.Œπ s = k :=
  ‚ü®hs.lift <| Fork.ofŒπ _ h, hs.fac _ _, fun _ hm =>
    Fork.IsLimit.hom_ext hs <| hm.symm ‚ñ∏ (hs.fac (Fork.ofŒπ _ h) WalkingParallelPair.zero).symm‚ü©
#align category_theory.limits.fork.is_limit.exists_unique CategoryTheory.Limits.Fork.IsLimit.existsUnique

theorem Cofork.IsColimit.existsUnique {s : Cofork f g} (hs : IsColimit s) {W : C} (k : Y ‚ü∂ W)
    (h : f ‚â´ k = g ‚â´ k) : ‚àÉ! d : s.pt ‚ü∂ W, Cofork.œÄ s ‚â´ d = k :=
  ‚ü®hs.desc <| Cofork.ofœÄ _ h, hs.fac _ _, fun _ hm =>
    Cofork.IsColimit.hom_ext hs <| hm.symm ‚ñ∏ (hs.fac (Cofork.ofœÄ _ h) WalkingParallelPair.one).symm‚ü©
#align category_theory.limits.cofork.is_colimit.exists_unique CategoryTheory.Limits.Cofork.IsColimit.existsUnique

/-- This is a slightly more convenient method to verify that a fork is a limit cone. It
    only asks for a proof of facts that carry any mathematical content -/
@[simps]
def Fork.IsLimit.mk (t : Fork f g) (lift : ‚àÄ s : Fork f g, s.pt ‚ü∂ t.pt)
    (fac : ‚àÄ s : Fork f g, lift s ‚â´ Fork.Œπ t = Fork.Œπ s)
    (uniq : ‚àÄ (s : Fork f g) (m : s.pt ‚ü∂ t.pt) (_ : m ‚â´ t.Œπ = s.Œπ), m = lift s) : IsLimit t :=
  { lift
    fac := fun s j =>
      WalkingParallelPair.casesOn j (fac s) <| by
        erw [‚Üê s.w left, ‚Üê t.w left, ‚Üê Category.assoc, fac]; rfl
    uniq := fun s m j => by aesop}
#align category_theory.limits.fork.is_limit.mk CategoryTheory.Limits.Fork.IsLimit.mk

/-- This is another convenient method to verify that a fork is a limit cone. It
    only asks for a proof of facts that carry any mathematical content, and allows access to the
    same `s` for all parts. -/
def Fork.IsLimit.mk' {X Y : C} {f g : X ‚ü∂ Y} (t : Fork f g)
    (create : ‚àÄ s : Fork f g, { l // l ‚â´ t.Œπ = s.Œπ ‚àß ‚àÄ {m}, m ‚â´ t.Œπ = s.Œπ ‚Üí m = l }) : IsLimit t :=
  Fork.IsLimit.mk t (fun s => (create s).1) (fun s => (create s).2.1) fun s _ w => (create s).2.2 w
#align category_theory.limits.fork.is_limit.mk' CategoryTheory.Limits.Fork.IsLimit.mk'

/-- This is a slightly more convenient method to verify that a cofork is a colimit cocone. It
    only asks for a proof of facts that carry any mathematical content -/
def Cofork.IsColimit.mk (t : Cofork f g) (desc : ‚àÄ s : Cofork f g, t.pt ‚ü∂ s.pt)
    (fac : ‚àÄ s : Cofork f g, Cofork.œÄ t ‚â´ desc s = Cofork.œÄ s)
    (uniq : ‚àÄ (s : Cofork f g) (m : t.pt ‚ü∂ s.pt) (_ : t.œÄ ‚â´ m = s.œÄ), m = desc s) : IsColimit t :=
  { desc
    fac := fun s j =>
      WalkingParallelPair.casesOn j (by erw [‚Üê s.w left, ‚Üê t.w left, Category.assoc, fac]; rfl)
        (fac s)
    uniq := by aesop }
#align category_theory.limits.cofork.is_colimit.mk CategoryTheory.Limits.Cofork.IsColimit.mk

/-- This is another convenient method to verify that a fork is a limit cone. It
    only asks for a proof of facts that carry any mathematical content, and allows access to the
    same `s` for all parts. -/
def Cofork.IsColimit.mk' {X Y : C} {f g : X ‚ü∂ Y} (t : Cofork f g)
    (create : ‚àÄ s : Cofork f g, { l : t.pt ‚ü∂ s.pt // t.œÄ ‚â´ l = s.œÄ
                                    ‚àß ‚àÄ {m}, t.œÄ ‚â´ m = s.œÄ ‚Üí m = l }) : IsColimit t :=
  Cofork.IsColimit.mk t (fun s => (create s).1) (fun s => (create s).2.1) fun s _ w =>
    (create s).2.2 w
#align category_theory.limits.cofork.is_colimit.mk' CategoryTheory.Limits.Cofork.IsColimit.mk'

/-- Noncomputably make a limit cone from the existence of unique factorizations. -/
noncomputable def Fork.IsLimit.ofExistsUnique {t : Fork f g}
    (hs : ‚àÄ s : Fork f g, ‚àÉ! l : s.pt ‚ü∂ t.pt, l ‚â´ Fork.Œπ t = Fork.Œπ s) : IsLimit t := by
  choose d hd hd' using hs
  exact Fork.IsLimit.mk _ d hd fun s m hm => hd' _ _ hm
#align category_theory.limits.fork.is_limit.of_exists_unique CategoryTheory.Limits.Fork.IsLimit.ofExistsUnique

/-- Noncomputably make a colimit cocone from the existence of unique factorizations. -/
noncomputable def Cofork.IsColimit.ofExistsUnique {t : Cofork f g}
    (hs : ‚àÄ s : Cofork f g, ‚àÉ! d : t.pt ‚ü∂ s.pt, Cofork.œÄ t ‚â´ d = Cofork.œÄ s) : IsColimit t := by
  choose d hd hd' using hs
  exact Cofork.IsColimit.mk _ d hd fun s m hm => hd' _ _ hm
#align category_theory.limits.cofork.is_colimit.of_exists_unique CategoryTheory.Limits.Cofork.IsColimit.ofExistsUnique

/--
Given a limit cone for the pair `f g : X ‚ü∂ Y`, for any `Z`, morphisms from `Z` to its point are in
bijection with morphisms `h : Z ‚ü∂ X` such that `h ‚â´ f = h ‚â´ g`.
Further, this bijection is natural in `Z`: see `Fork.IsLimit.homIso_natural`.
This is a special case of `IsLimit.homIso'`, often useful to construct adjunctions.
-/
@[simps]
def Fork.IsLimit.homIso {X Y : C} {f g : X ‚ü∂ Y} {t : Fork f g} (ht : IsLimit t) (Z : C) :
    (Z ‚ü∂ t.pt) ‚âÉ { h : Z ‚ü∂ X // h ‚â´ f = h ‚â´ g } where
  toFun k := ‚ü®k ‚â´ t.Œπ, by simp only [Category.assoc, t.condition]‚ü©
  invFun h := (Fork.IsLimit.lift' ht _ h.prop).1
  left_inv k := Fork.IsLimit.hom_ext ht (Fork.IsLimit.lift' _ _ _).prop
  right_inv h := Subtype.ext (Fork.IsLimit.lift' ht _ _).prop
#align category_theory.limits.fork.is_limit.hom_iso CategoryTheory.Limits.Fork.IsLimit.homIso

/-- The bijection of `Fork.IsLimit.homIso` is natural in `Z`. -/
theorem Fork.IsLimit.homIso_natural {X Y : C} {f g : X ‚ü∂ Y} {t : Fork f g} (ht : IsLimit t)
    {Z Z' : C} (q : Z' ‚ü∂ Z) (k : Z ‚ü∂ t.pt) :
    (Fork.IsLimit.homIso ht _ (q ‚â´ k) : Z' ‚ü∂ X) = q ‚â´ (Fork.IsLimit.homIso ht _ k : Z ‚ü∂ X) :=
  Category.assoc _ _ _
#align category_theory.limits.fork.is_limit.hom_iso_natural CategoryTheory.Limits.Fork.IsLimit.homIso_natural

/-- Given a colimit cocone for the pair `f g : X ‚ü∂ Y`, for any `Z`, morphisms from the cocone point
to `Z` are in bijection with morphisms `h : Y ‚ü∂ Z` such that `f ‚â´ h = g ‚â´ h`.
Further, this bijection is natural in `Z`: see `Cofork.IsColimit.homIso_natural`.
This is a special case of `IsColimit.homIso'`, often useful to construct adjunctions.
-/
@[simps]
def Cofork.IsColimit.homIso {X Y : C} {f g : X ‚ü∂ Y} {t : Cofork f g} (ht : IsColimit t) (Z : C) :
    (t.pt ‚ü∂ Z) ‚âÉ { h : Y ‚ü∂ Z // f ‚â´ h = g ‚â´ h } where
  toFun k := ‚ü®t.œÄ ‚â´ k, by simp only [‚Üê Category.assoc, t.condition]‚ü©
  invFun h := (Cofork.IsColimit.desc' ht _ h.prop).1
  left_inv k := Cofork.IsColimit.hom_ext ht (Cofork.IsColimit.desc' _ _ _).prop
  right_inv h := Subtype.ext (Cofork.IsColimit.desc' ht _ _).prop
#align category_theory.limits.cofork.is_colimit.hom_iso CategoryTheory.Limits.Cofork.IsColimit.homIso

/-- The bijection of `Cofork.IsColimit.homIso` is natural in `Z`. -/
theorem Cofork.IsColimit.homIso_natural {X Y : C} {f g : X ‚ü∂ Y} {t : Cofork f g} {Z Z' : C}
    (q : Z ‚ü∂ Z') (ht : IsColimit t) (k : t.pt ‚ü∂ Z) :
    (Cofork.IsColimit.homIso ht _ (k ‚â´ q) : Y ‚ü∂ Z') =
      (Cofork.IsColimit.homIso ht _ k : Y ‚ü∂ Z) ‚â´ q :=
  (Category.assoc _ _ _).symm
#align category_theory.limits.cofork.is_colimit.hom_iso_natural CategoryTheory.Limits.Cofork.IsColimit.homIso_natural

/-- This is a helper construction that can be useful when verifying that a category has all
    equalizers. Given `F : WalkingParallelPair ‚•§ C`, which is really the same as
    `parallelPair (F.map left) (F.map right)`, and a fork on `F.map left` and `F.map right`,
    we get a cone on `F`.

    If you're thinking about using this, have a look at `hasEqualizers_of_hasLimit_parallelPair`,
    which you may find to be an easier way of achieving your goal. -/
def Cone.ofFork {F : WalkingParallelPair ‚•§ C} (t : Fork (F.map left) (F.map right)) : Cone F where
  pt := t.pt
  œÄ :=
    { app := fun X => t.œÄ.app X ‚â´ eqToHom (by aesop)
      naturality := by rintro _ _ (_|_|_) <;> {dsimp; simp [t.condition]}}
#align category_theory.limits.cone.of_fork CategoryTheory.Limits.Cone.ofFork

/-- This is a helper construction that can be useful when verifying that a category has all
    coequalizers. Given `F : WalkingParallelPair ‚•§ C`, which is really the same as
    `parallelPair (F.map left) (F.map right)`, and a cofork on `F.map left` and `F.map right`,
    we get a cocone on `F`.

    If you're thinking about using this, have a look at
    `hasCoequalizers_of_hasColimit_parallelPair`, which you may find to be an easier way of
    achieving your goal. -/
def Cocone.ofCofork {F : WalkingParallelPair ‚•§ C} (t : Cofork (F.map left) (F.map right)) :
    Cocone F where
  pt := t.pt
  Œπ :=
    { app := fun X => eqToHom (by aesop) ‚â´ t.Œπ.app X
      naturality := by rintro _ _ (_|_|_) <;> {dsimp; simp [t.condition]}}
#align category_theory.limits.cocone.of_cofork CategoryTheory.Limits.Cocone.ofCofork

@[simp]
theorem Cone.ofFork_œÄ {F : WalkingParallelPair ‚•§ C} (t : Fork (F.map left) (F.map right)) (j) :
    (Cone.ofFork t).œÄ.app j = t.œÄ.app j ‚â´ eqToHom (by aesop) := rfl
#align category_theory.limits.cone.of_fork_œÄ CategoryTheory.Limits.Cone.ofFork_œÄ

@[simp]
theorem Cocone.ofCofork_Œπ {F : WalkingParallelPair ‚•§ C} (t : Cofork (F.map left) (F.map right))
    (j) : (Cocone.ofCofork t).Œπ.app j = eqToHom (by aesop) ‚â´ t.Œπ.app j := rfl
#align category_theory.limits.cocone.of_cofork_Œπ CategoryTheory.Limits.Cocone.ofCofork_Œπ

/-- Given `F : WalkingParallelPair ‚•§ C`, which is really the same as
    `parallelPair (F.map left) (F.map right)` and a cone on `F`, we get a fork on
    `F.map left` and `F.map right`. -/
def Fork.ofCone {F : WalkingParallelPair ‚•§ C} (t : Cone F) : Fork (F.map left) (F.map right) where
  pt := t.pt
  œÄ := { app := fun X => t.œÄ.app X ‚â´ eqToHom (by aesop)
         naturality := by rintro _ _ (_|_|_) <;> {dsimp; simp}}
#align category_theory.limits.fork.of_cone CategoryTheory.Limits.Fork.ofCone

/-- Given `F : WalkingParallelPair ‚•§ C`, which is really the same as
    `parallelPair (F.map left) (F.map right)` and a cocone on `F`, we get a cofork on
    `F.map left` and `F.map right`. -/
def Cofork.ofCocone {F : WalkingParallelPair ‚•§ C} (t : Cocone F) :
    Cofork (F.map left) (F.map right) where
  pt := t.pt
  Œπ := { app := fun X => eqToHom (by aesop) ‚â´ t.Œπ.app X
         naturality := by rintro _ _ (_|_|_) <;> {dsimp; simp}}
#align category_theory.limits.cofork.of_cocone CategoryTheory.Limits.Cofork.ofCocone

@[simp]
theorem Fork.ofCone_œÄ {F : WalkingParallelPair ‚•§ C} (t : Cone F) (j) :
    (Fork.ofCone t).œÄ.app j = t.œÄ.app j ‚â´ eqToHom (by aesop) := rfl
#align category_theory.limits.fork.of_cone_œÄ CategoryTheory.Limits.Fork.ofCone_œÄ

@[simp]
theorem Cofork.ofCocone_Œπ {F : WalkingParallelPair ‚•§ C} (t : Cocone F) (j) :
    (Cofork.ofCocone t).Œπ.app j = eqToHom (by aesop) ‚â´ t.Œπ.app j := rfl
#align category_theory.limits.cofork.of_cocone_Œπ CategoryTheory.Limits.Cofork.ofCocone_Œπ

@[simp]
theorem Fork.Œπ_postcompose {f' g' : X ‚ü∂ Y} {Œ± : parallelPair f g ‚ü∂ parallelPair f' g'}
    {c : Fork f g} : Fork.Œπ ((Cones.postcompose Œ±).obj c) = c.Œπ ‚â´ Œ±.app _ :=
  rfl
#align category_theory.limits.fork.Œπ_postcompose CategoryTheory.Limits.Fork.Œπ_postcompose

@[simp]
theorem Cofork.œÄ_precompose {f' g' : X ‚ü∂ Y} {Œ± : parallelPair f g ‚ü∂ parallelPair f' g'}
    {c : Cofork f' g'} : Cofork.œÄ ((Cocones.precompose Œ±).obj c) = Œ±.app _ ‚â´ c.œÄ :=
  rfl
#align category_theory.limits.cofork.œÄ_precompose CategoryTheory.Limits.Cofork.œÄ_precompose

/-- Helper function for constructing morphisms between equalizer forks.
-/
@[simps]
def Fork.mkHom {s t : Fork f g} (k : s.pt ‚ü∂ t.pt) (w : k ‚â´ t.Œπ = s.Œπ) : s ‚ü∂ t where
  hom := k
  w := by
    rintro ‚ü®_ | _‚ü©
    ¬∑ exact w
    ¬∑ simp only [Fork.app_one_eq_Œπ_comp_left,‚Üê Category.assoc]
      congr
#align category_theory.limits.fork.mk_hom CategoryTheory.Limits.Fork.mkHom

/-- To construct an isomorphism between forks,
it suffices to give an isomorphism between the cone points
and check that it commutes with the `Œπ` morphisms.
-/
@[simps]
def Fork.ext {s t : Fork f g} (i : s.pt ‚âÖ t.pt) (w : i.hom ‚â´ t.Œπ = s.Œπ := by aesop_cat) :
    s ‚âÖ t where
  hom := Fork.mkHom i.hom w
  inv := Fork.mkHom i.inv (by rw [‚Üê w, Iso.inv_hom_id_assoc])
#align category_theory.limits.fork.ext CategoryTheory.Limits.Fork.ext

/-- Two forks of the form `ofŒπ` are isomorphic whenever their `Œπ`'s are equal. -/
def ForkOfŒπ.ext {P : C} {Œπ Œπ' : P ‚ü∂ X} (w : Œπ ‚â´ f = Œπ ‚â´ g) (w' : Œπ' ‚â´ f = Œπ' ‚â´ g) (h : Œπ = Œπ') :
    Fork.ofŒπ Œπ w ‚âÖ Fork.ofŒπ Œπ' w' :=
  Fork.ext (Iso.refl _) (by simp [h])

/-- Every fork is isomorphic to one of the form `Fork.of_Œπ _ _`. -/
def Fork.isoForkOfŒπ (c : Fork f g) : c ‚âÖ Fork.ofŒπ c.Œπ c.condition :=
  Fork.ext (by simp only [Fork.ofŒπ_pt, Functor.const_obj_obj]; rfl) (by simp)
#align category_theory.limits.fork.iso_fork_of_Œπ CategoryTheory.Limits.Fork.isoForkOfŒπ

/--
Given two forks with isomorphic components in such a way that the natural diagrams commute, then if
one is a limit, then the other one is as well.
-/
def Fork.isLimitOfIsos {X' Y' : C} (c : Fork f g) (hc : IsLimit c)
    {f' g' : X' ‚ü∂ Y'} (c' : Fork f' g')
    (e‚ÇÄ : X ‚âÖ X') (e‚ÇÅ : Y ‚âÖ Y') (e : c.pt ‚âÖ c'.pt)
    (comm‚ÇÅ : e‚ÇÄ.hom ‚â´ f' = f ‚â´ e‚ÇÅ.hom := by aesop_cat)
    (comm‚ÇÇ : e‚ÇÄ.hom ‚â´ g' = g ‚â´ e‚ÇÅ.hom := by aesop_cat)
    (comm‚ÇÉ : e.hom ‚â´ c'.Œπ = c.Œπ ‚â´ e‚ÇÄ.hom := by aesop_cat) : IsLimit c' :=
  let i : parallelPair f g ‚âÖ parallelPair f' g' := parallelPair.ext e‚ÇÄ e‚ÇÅ comm‚ÇÅ.symm comm‚ÇÇ.symm
  (IsLimit.equivOfNatIsoOfIso i c c' (Fork.ext e comm‚ÇÉ)) hc

/-- Helper function for constructing morphisms between coequalizer coforks.
-/
@[simps]
def Cofork.mkHom {s t : Cofork f g} (k : s.pt ‚ü∂ t.pt) (w : s.œÄ ‚â´ k = t.œÄ) : s ‚ü∂ t where
  hom := k
  w := by
    rintro ‚ü®_ | _‚ü©
    ¬∑ simp [Cofork.app_zero_eq_comp_œÄ_left, w]
    ¬∑ exact w
#align category_theory.limits.cofork.mk_hom CategoryTheory.Limits.Cofork.mkHom

@[reassoc (attr := simp)]
theorem Fork.hom_comp_Œπ {s t : Fork f g} (f : s ‚ü∂ t) : f.hom ‚â´ t.Œπ = s.Œπ := by
  cases s; cases t; cases f; aesop
#align category_theory.limits.fork.hom_comp_Œπ CategoryTheory.Limits.Fork.hom_comp_Œπ

@[reassoc (attr := simp)]
theorem Fork.œÄ_comp_hom {s t : Cofork f g} (f : s ‚ü∂ t) : s.œÄ ‚â´ f.hom = t.œÄ := by
  cases s; cases t; cases f; aesop
#align category_theory.limits.fork.œÄ_comp_hom CategoryTheory.Limits.Fork.œÄ_comp_hom

/-- To construct an isomorphism between coforks,
it suffices to give an isomorphism between the cocone points
and check that it commutes with the `œÄ` morphisms.
-/
@[simps]
def Cofork.ext {s t : Cofork f g} (i : s.pt ‚âÖ t.pt) (w : s.œÄ ‚â´ i.hom = t.œÄ := by aesop_cat) :
    s ‚âÖ t where
  hom := Cofork.mkHom i.hom w
  inv := Cofork.mkHom i.inv (by rw [Iso.comp_inv_eq, w])
#align category_theory.limits.cofork.ext CategoryTheory.Limits.Cofork.ext

/-- Every cofork is isomorphic to one of the form `Cofork.ofœÄ _ _`. -/
def Cofork.isoCoforkOfœÄ (c : Cofork f g) : c ‚âÖ Cofork.ofœÄ c.œÄ c.condition :=
  Cofork.ext (by simp only [Cofork.ofœÄ_pt, Functor.const_obj_obj]; rfl) (by dsimp; simp)
#align category_theory.limits.cofork.iso_cofork_of_œÄ CategoryTheory.Limits.Cofork.isoCoforkOfœÄ

variable (f g)

section

/-- `HasEqualizer f g` represents a particular choice of limiting cone
for the parallel pair of morphisms `f` and `g`.
-/
abbrev HasEqualizer :=
  HasLimit (parallelPair f g)
#align category_theory.limits.has_equalizer CategoryTheory.Limits.HasEqualizer

variable [HasEqualizer f g]

/-- If an equalizer of `f` and `g` exists, we can access an arbitrary choice of such by
    saying `equalizer f g`. -/
noncomputable abbrev equalizer : C :=
  limit (parallelPair f g)
#align category_theory.limits.equalizer CategoryTheory.Limits.equalizer

/-- If an equalizer of `f` and `g` exists, we can access the inclusion
    `equalizer f g ‚ü∂ X` by saying `equalizer.Œπ f g`. -/
noncomputable abbrev equalizer.Œπ : equalizer f g ‚ü∂ X :=
  limit.œÄ (parallelPair f g) zero
#align category_theory.limits.equalizer.Œπ CategoryTheory.Limits.equalizer.Œπ

/-- An equalizer cone for a parallel pair `f` and `g` -/
noncomputable abbrev equalizer.fork : Fork f g :=
  limit.cone (parallelPair f g)
#align category_theory.limits.equalizer.fork CategoryTheory.Limits.equalizer.fork

@[simp]
theorem equalizer.fork_Œπ : (equalizer.fork f g).Œπ = equalizer.Œπ f g :=
  rfl
#align category_theory.limits.equalizer.fork_Œπ CategoryTheory.Limits.equalizer.fork_Œπ

@[simp]
theorem equalizer.fork_œÄ_app_zero : (equalizer.fork f g).œÄ.app zero = equalizer.Œπ f g :=
  rfl
#align category_theory.limits.equalizer.fork_œÄ_app_zero CategoryTheory.Limits.equalizer.fork_œÄ_app_zero

@[reassoc]
theorem equalizer.condition : equalizer.Œπ f g ‚â´ f = equalizer.Œπ f g ‚â´ g :=
  Fork.condition <| limit.cone <| parallelPair f g
#align category_theory.limits.equalizer.condition CategoryTheory.Limits.equalizer.condition

/-- The equalizer built from `equalizer.Œπ f g` is limiting. -/
noncomputable def equalizerIsEqualizer : IsLimit (Fork.ofŒπ (equalizer.Œπ f g)
    (equalizer.condition f g)) :=
  IsLimit.ofIsoLimit (limit.isLimit _) (Fork.ext (Iso.refl _) (by aesop))
#align category_theory.limits.equalizer_is_equalizer CategoryTheory.Limits.equalizerIsEqualizer

variable {f g}

/-- A morphism `k : W ‚ü∂ X` satisfying `k ‚â´ f = k ‚â´ g` factors through the equalizer of `f` and `g`
    via `equalizer.lift : W ‚ü∂ equalizer f g`. -/
noncomputable abbrev equalizer.lift {W : C} (k : W ‚ü∂ X) (h : k ‚â´ f = k ‚â´ g) : W ‚ü∂ equalizer f g :=
  limit.lift (parallelPair f g) (Fork.ofŒπ k h)
#align category_theory.limits.equalizer.lift CategoryTheory.Limits.equalizer.lift

-- Porting note (#10618): removed simp since simp can prove this and the reassoc version
@[reassoc]
theorem equalizer.lift_Œπ {W : C} (k : W ‚ü∂ X) (h : k ‚â´ f = k ‚â´ g) :
    equalizer.lift k h ‚â´ equalizer.Œπ f g = k :=
  limit.lift_œÄ _ _
#align category_theory.limits.equalizer.lift_Œπ CategoryTheory.Limits.equalizer.lift_Œπ

/-- A morphism `k : W ‚ü∂ X` satisfying `k ‚â´ f = k ‚â´ g` induces a morphism `l : W ‚ü∂ equalizer f g`
    satisfying `l ‚â´ equalizer.Œπ f g = k`. -/
noncomputable def equalizer.lift' {W : C} (k : W ‚ü∂ X) (h : k ‚â´ f = k ‚â´ g) :
    { l : W ‚ü∂ equalizer f g // l ‚â´ equalizer.Œπ f g = k } :=
  ‚ü®equalizer.lift k h, equalizer.lift_Œπ _ _‚ü©
#align category_theory.limits.equalizer.lift' CategoryTheory.Limits.equalizer.lift'

/-- Two maps into an equalizer are equal if they are equal when composed with the equalizer map. -/
@[ext]
theorem equalizer.hom_ext {W : C} {k l : W ‚ü∂ equalizer f g}
    (h : k ‚â´ equalizer.Œπ f g = l ‚â´ equalizer.Œπ f g) : k = l :=
  Fork.IsLimit.hom_ext (limit.isLimit _) h
#align category_theory.limits.equalizer.hom_ext CategoryTheory.Limits.equalizer.hom_ext

theorem equalizer.existsUnique {W : C} (k : W ‚ü∂ X) (h : k ‚â´ f = k ‚â´ g) :
    ‚àÉ! l : W ‚ü∂ equalizer f g, l ‚â´ equalizer.Œπ f g = k :=
  Fork.IsLimit.existsUnique (limit.isLimit _) _ h
#align category_theory.limits.equalizer.exists_unique CategoryTheory.Limits.equalizer.existsUnique

/-- An equalizer morphism is a monomorphism -/
instance equalizer.Œπ_mono : Mono (equalizer.Œπ f g) where
  right_cancellation _ _ w := equalizer.hom_ext w
#align category_theory.limits.equalizer.Œπ_mono CategoryTheory.Limits.equalizer.Œπ_mono

end

section

variable {f g}

/-- The equalizer morphism in any limit cone is a monomorphism. -/
theorem mono_of_isLimit_fork {c : Fork f g} (i : IsLimit c) : Mono (Fork.Œπ c) :=
  { right_cancellation := fun _ _ w => Fork.IsLimit.hom_ext i w }
#align category_theory.limits.mono_of_is_limit_fork CategoryTheory.Limits.mono_of_isLimit_fork

end

section

variable {f g}

/-- The identity determines a cone on the equalizer diagram of `f` and `g` if `f = g`. -/
def idFork (h : f = g) : Fork f g :=
  Fork.ofŒπ (ùüô X) <| h ‚ñ∏ rfl
#align category_theory.limits.id_fork CategoryTheory.Limits.idFork

/-- The identity on `X` is an equalizer of `(f, g)`, if `f = g`. -/
def isLimitIdFork (h : f = g) : IsLimit (idFork h) :=
  Fork.IsLimit.mk _ (fun s => Fork.Œπ s) (fun s => Category.comp_id _) fun s m h => by
    convert h
    exact (Category.comp_id _).symm
#align category_theory.limits.is_limit_id_fork CategoryTheory.Limits.isLimitIdFork

/-- Every equalizer of `(f, g)`, where `f = g`, is an isomorphism. -/
theorem isIso_limit_cone_parallelPair_of_eq (h‚ÇÄ : f = g) {c : Fork f g} (h : IsLimit c) :
    IsIso c.Œπ :=
  Iso.isIso_hom <| IsLimit.conePointUniqueUpToIso h <| isLimitIdFork h‚ÇÄ
#align category_theory.limits.is_iso_limit_cone_parallel_pair_of_eq CategoryTheory.Limits.isIso_limit_cone_parallelPair_of_eq

/-- The equalizer of `(f, g)`, where `f = g`, is an isomorphism. -/
theorem equalizer.Œπ_of_eq [HasEqualizer f g] (h : f = g) : IsIso (equalizer.Œπ f g) :=
  isIso_limit_cone_parallelPair_of_eq h <| limit.isLimit _
#align category_theory.limits.equalizer.Œπ_of_eq CategoryTheory.Limits.equalizer.Œπ_of_eq

/-- Every equalizer of `(f, f)` is an isomorphism. -/
theorem isIso_limit_cone_parallelPair_of_self {c : Fork f f} (h : IsLimit c) : IsIso c.Œπ :=
  isIso_limit_cone_parallelPair_of_eq rfl h
#align category_theory.limits.is_iso_limit_cone_parallel_pair_of_self CategoryTheory.Limits.isIso_limit_cone_parallelPair_of_self

/-- An equalizer that is an epimorphism is an isomorphism. -/
theorem isIso_limit_cone_parallelPair_of_epi {c : Fork f g} (h : IsLimit c) [Epi c.Œπ] : IsIso c.Œπ :=
  isIso_limit_cone_parallelPair_of_eq ((cancel_epi _).1 (Fork.condition c)) h
#align category_theory.limits.is_iso_limit_cone_parallel_pair_of_epi CategoryTheory.Limits.isIso_limit_cone_parallelPair_of_epi

/-- Two morphisms are equal if there is a fork whose inclusion is epi. -/
theorem eq_of_epi_fork_Œπ (t : Fork f g) [Epi (Fork.Œπ t)] : f = g :=
  (cancel_epi (Fork.Œπ t)).1 <| Fork.condition t
#align category_theory.limits.eq_of_epi_fork_Œπ CategoryTheory.Limits.eq_of_epi_fork_Œπ

/-- If the equalizer of two morphisms is an epimorphism, then the two morphisms are equal. -/
theorem eq_of_epi_equalizer [HasEqualizer f g] [Epi (equalizer.Œπ f g)] : f = g :=
  (cancel_epi (equalizer.Œπ f g)).1 <| equalizer.condition _ _
#align category_theory.limits.eq_of_epi_equalizer CategoryTheory.Limits.eq_of_epi_equalizer

end

instance hasEqualizer_of_self : HasEqualizer f f :=
  HasLimit.mk
    { cone := idFork rfl
      isLimit := isLimitIdFork rfl }
#align category_theory.limits.has_equalizer_of_self CategoryTheory.Limits.hasEqualizer_of_self

/-- The equalizer inclusion for `(f, f)` is an isomorphism. -/
instance equalizer.Œπ_of_self : IsIso (equalizer.Œπ f f) :=
  equalizer.Œπ_of_eq rfl
#align category_theory.limits.equalizer.Œπ_of_self CategoryTheory.Limits.equalizer.Œπ_of_self

/-- The equalizer of a morphism with itself is isomorphic to the source. -/
noncomputable def equalizer.isoSourceOfSelf : equalizer f f ‚âÖ X :=
  asIso (equalizer.Œπ f f)
#align category_theory.limits.equalizer.iso_source_of_self CategoryTheory.Limits.equalizer.isoSourceOfSelf

@[simp]
theorem equalizer.isoSourceOfSelf_hom : (equalizer.isoSourceOfSelf f).hom = equalizer.Œπ f f :=
  rfl
#align category_theory.limits.equalizer.iso_source_of_self_hom CategoryTheory.Limits.equalizer.isoSourceOfSelf_hom

@[simp]
theorem equalizer.isoSourceOfSelf_inv :
    (equalizer.isoSourceOfSelf f).inv = equalizer.lift (ùüô X) (by simp) := by
  ext
  simp [equalizer.isoSourceOfSelf]
#align category_theory.limits.equalizer.iso_source_of_self_inv CategoryTheory.Limits.equalizer.isoSourceOfSelf_inv

section

/-- `HasCoequalizer f g` represents a particular choice of colimiting cocone
for the parallel pair of morphisms `f` and `g`.
-/
abbrev HasCoequalizer :=
  HasColimit (parallelPair f g)
#align category_theory.limits.has_coequalizer CategoryTheory.Limits.HasCoequalizer

variable [HasCoequalizer f g]

/-- If a coequalizer of `f` and `g` exists, we can access an arbitrary choice of such by
    saying `coequalizer f g`. -/
noncomputable abbrev coequalizer : C :=
  colimit (parallelPair f g)
#align category_theory.limits.coequalizer CategoryTheory.Limits.coequalizer

/-- If a coequalizer of `f` and `g` exists, we can access the corresponding projection by
    saying `coequalizer.œÄ f g`. -/
noncomputable abbrev coequalizer.œÄ : Y ‚ü∂ coequalizer f g :=
  colimit.Œπ (parallelPair f g) one
#align category_theory.limits.coequalizer.œÄ CategoryTheory.Limits.coequalizer.œÄ

/-- An arbitrary choice of coequalizer cocone for a parallel pair `f` and `g`.
-/
noncomputable abbrev coequalizer.cofork : Cofork f g :=
  colimit.cocone (parallelPair f g)
#align category_theory.limits.coequalizer.cofork CategoryTheory.Limits.coequalizer.cofork

@[simp]
theorem coequalizer.cofork_œÄ : (coequalizer.cofork f g).œÄ = coequalizer.œÄ f g :=
  rfl
#align category_theory.limits.coequalizer.cofork_œÄ CategoryTheory.Limits.coequalizer.cofork_œÄ

-- Porting note (#10618): simp can prove this, simp removed
theorem coequalizer.cofork_Œπ_app_one : (coequalizer.cofork f g).Œπ.app one = coequalizer.œÄ f g :=
  rfl
#align category_theory.limits.coequalizer.cofork_Œπ_app_one CategoryTheory.Limits.coequalizer.cofork_Œπ_app_one

@[reassoc]
theorem coequalizer.condition : f ‚â´ coequalizer.œÄ f g = g ‚â´ coequalizer.œÄ f g :=
  Cofork.condition <| colimit.cocone <| parallelPair f g
#align category_theory.limits.coequalizer.condition CategoryTheory.Limits.coequalizer.condition

/-- The cofork built from `coequalizer.œÄ f g` is colimiting. -/
noncomputable def coequalizerIsCoequalizer :
    IsColimit (Cofork.ofœÄ (coequalizer.œÄ f g) (coequalizer.condition f g)) :=
  IsColimit.ofIsoColimit (colimit.isColimit _) (Cofork.ext (Iso.refl _) (by aesop))
#align category_theory.limits.coequalizer_is_coequalizer CategoryTheory.Limits.coequalizerIsCoequalizer

variable {f g}

/-- Any morphism `k : Y ‚ü∂ W` satisfying `f ‚â´ k = g ‚â´ k` factors through the coequalizer of `f`
    and `g` via `coequalizer.desc : coequalizer f g ‚ü∂ W`. -/
noncomputable abbrev coequalizer.desc {W : C} (k : Y ‚ü∂ W) (h : f ‚â´ k = g ‚â´ k) :
    coequalizer f g ‚ü∂ W :=
  colimit.desc (parallelPair f g) (Cofork.ofœÄ k h)
#align category_theory.limits.coequalizer.desc CategoryTheory.Limits.coequalizer.desc

-- Porting note (#10618): removing simp since simp can prove this and reassoc version
@[reassoc]
theorem coequalizer.œÄ_desc {W : C} (k : Y ‚ü∂ W) (h : f ‚â´ k = g ‚â´ k) :
    coequalizer.œÄ f g ‚â´ coequalizer.desc k h = k :=
  colimit.Œπ_desc _ _
#align category_theory.limits.coequalizer.œÄ_desc CategoryTheory.Limits.coequalizer.œÄ_desc

theorem coequalizer.œÄ_colimMap_desc {X' Y' Z : C} (f' g' : X' ‚ü∂ Y') [HasCoequalizer f' g']
    (p : X ‚ü∂ X') (q : Y ‚ü∂ Y') (wf : f ‚â´ q = p ‚â´ f') (wg : g ‚â´ q = p ‚â´ g') (h : Y' ‚ü∂ Z)
    (wh : f' ‚â´ h = g' ‚â´ h) :
    coequalizer.œÄ f g ‚â´ colimMap (parallelPairHom f g f' g' p q wf wg) ‚â´ coequalizer.desc h wh =
      q ‚â´ h := by
  rw [Œπ_colimMap_assoc, parallelPairHom_app_one, coequalizer.œÄ_desc]
#align category_theory.limits.coequalizer.œÄ_colim_map_desc CategoryTheory.Limits.coequalizer.œÄ_colimMap_desc

/-- Any morphism `k : Y ‚ü∂ W` satisfying `f ‚â´ k = g ‚â´ k` induces a morphism
    `l : coequalizer f g ‚ü∂ W` satisfying `coequalizer.œÄ ‚â´ g = l`. -/
noncomputable def coequalizer.desc' {W : C} (k : Y ‚ü∂ W) (h : f ‚â´ k = g ‚â´ k) :
    { l : coequalizer f g ‚ü∂ W // coequalizer.œÄ f g ‚â´ l = k } :=
  ‚ü®coequalizer.desc k h, coequalizer.œÄ_desc _ _‚ü©
#align category_theory.limits.coequalizer.desc' CategoryTheory.Limits.coequalizer.desc'

/-- Two maps from a coequalizer are equal if they are equal when composed with the coequalizer
    map -/
@[ext]
theorem coequalizer.hom_ext {W : C} {k l : coequalizer f g ‚ü∂ W}
    (h : coequalizer.œÄ f g ‚â´ k = coequalizer.œÄ f g ‚â´ l) : k = l :=
  Cofork.IsColimit.hom_ext (colimit.isColimit _) h
#align category_theory.limits.coequalizer.hom_ext CategoryTheory.Limits.coequalizer.hom_ext

theorem coequalizer.existsUnique {W : C} (k : Y ‚ü∂ W) (h : f ‚â´ k = g ‚â´ k) :
    ‚àÉ! d : coequalizer f g ‚ü∂ W, coequalizer.œÄ f g ‚â´ d = k :=
  Cofork.IsColimit.existsUnique (colimit.isColimit _) _ h
#align category_theory.limits.coequalizer.exists_unique CategoryTheory.Limits.coequalizer.existsUnique

/-- A coequalizer morphism is an epimorphism -/
instance coequalizer.œÄ_epi : Epi (coequalizer.œÄ f g) where
  left_cancellation _ _ w := coequalizer.hom_ext w
#align category_theory.limits.coequalizer.œÄ_epi CategoryTheory.Limits.coequalizer.œÄ_epi

end

section

variable {f g}

/-- The coequalizer morphism in any colimit cocone is an epimorphism. -/
theorem epi_of_isColimit_cofork {c : Cofork f g} (i : IsColimit c) : Epi c.œÄ :=
  { left_cancellation := fun _ _ w => Cofork.IsColimit.hom_ext i w }
#align category_theory.limits.epi_of_is_colimit_cofork CategoryTheory.Limits.epi_of_isColimit_cofork

end

section

variable {f g}

/-- The identity determines a cocone on the coequalizer diagram of `f` and `g`, if `f = g`. -/
def idCofork (h : f = g) : Cofork f g :=
  Cofork.ofœÄ (ùüô Y) <| h ‚ñ∏ rfl
#align category_theory.limits.id_cofork CategoryTheory.Limits.idCofork

/-- The identity on `Y` is a coequalizer of `(f, g)`, where `f = g`.  -/
def isColimitIdCofork (h : f = g) : IsColimit (idCofork h) :=
  Cofork.IsColimit.mk _ (fun s => Cofork.œÄ s) (fun s => Category.id_comp _) fun s m h => by
    convert h
    exact (Category.id_comp _).symm
#align category_theory.limits.is_colimit_id_cofork CategoryTheory.Limits.isColimitIdCofork

/-- Every coequalizer of `(f, g)`, where `f = g`, is an isomorphism. -/
theorem isIso_colimit_cocone_parallelPair_of_eq (h‚ÇÄ : f = g) {c : Cofork f g} (h : IsColimit c) :
    IsIso c.œÄ :=
  Iso.isIso_hom <| IsColimit.coconePointUniqueUpToIso (isColimitIdCofork h‚ÇÄ) h
#align category_theory.limits.is_iso_colimit_cocone_parallel_pair_of_eq CategoryTheory.Limits.isIso_colimit_cocone_parallelPair_of_eq

/-- The coequalizer of `(f, g)`, where `f = g`, is an isomorphism. -/
theorem coequalizer.œÄ_of_eq [HasCoequalizer f g] (h : f = g) : IsIso (coequalizer.œÄ f g) :=
  isIso_colimit_cocone_parallelPair_of_eq h <| colimit.isColimit _
#align category_theory.limits.coequalizer.œÄ_of_eq CategoryTheory.Limits.coequalizer.œÄ_of_eq

/-- Every coequalizer of `(f, f)` is an isomorphism. -/
theorem isIso_colimit_cocone_parallelPair_of_self {c : Cofork f f} (h : IsColimit c) : IsIso c.œÄ :=
  isIso_colimit_cocone_parallelPair_of_eq rfl h
#align category_theory.limits.is_iso_colimit_cocone_parallel_pair_of_self CategoryTheory.Limits.isIso_colimit_cocone_parallelPair_of_self

/-- A coequalizer that is a monomorphism is an isomorphism. -/
theorem isIso_limit_cocone_parallelPair_of_epi {c : Cofork f g} (h : IsColimit c) [Mono c.œÄ] :
    IsIso c.œÄ :=
  isIso_colimit_cocone_parallelPair_of_eq ((cancel_mono _).1 (Cofork.condition c)) h
#align category_theory.limits.is_iso_limit_cocone_parallel_pair_of_epi CategoryTheory.Limits.isIso_limit_cocone_parallelPair_of_epi

/-- Two morphisms are equal if there is a cofork whose projection is mono. -/
theorem eq_of_mono_cofork_œÄ (t : Cofork f g) [Mono (Cofork.œÄ t)] : f = g :=
  (cancel_mono (Cofork.œÄ t)).1 <| Cofork.condition t
#align category_theory.limits.eq_of_mono_cofork_œÄ CategoryTheory.Limits.eq_of_mono_cofork_œÄ

/-- If the coequalizer of two morphisms is a monomorphism, then the two morphisms are equal. -/
theorem eq_of_mono_coequalizer [HasCoequalizer f g] [Mono (coequalizer.œÄ f g)] : f = g :=
  (cancel_mono (coequalizer.œÄ f g)).1 <| coequalizer.condition _ _
#align category_theory.limits.eq_of_mono_coequalizer CategoryTheory.Limits.eq_of_mono_coequalizer

end

instance hasCoequalizer_of_self : HasCoequalizer f f :=
  HasColimit.mk
    { cocone := idCofork rfl
      isColimit := isColimitIdCofork rfl }
#align category_theory.limits.has_coequalizer_of_self CategoryTheory.Limits.hasCoequalizer_of_self

/-- The coequalizer projection for `(f, f)` is an isomorphism. -/
instance coequalizer.œÄ_of_self : IsIso (coequalizer.œÄ f f) :=
  coequalizer.œÄ_of_eq rfl
#align category_theory.limits.coequalizer.œÄ_of_self CategoryTheory.Limits.coequalizer.œÄ_of_self

/-- The coequalizer of a morphism with itself is isomorphic to the target. -/
noncomputable def coequalizer.isoTargetOfSelf : coequalizer f f ‚âÖ Y :=
  (asIso (coequalizer.œÄ f f)).symm
#align category_theory.limits.coequalizer.iso_target_of_self CategoryTheory.Limits.coequalizer.isoTargetOfSelf

@[simp]
theorem coequalizer.isoTargetOfSelf_hom :
    (coequalizer.isoTargetOfSelf f).hom = coequalizer.desc (ùüô Y) (by simp) := by
  ext
  simp [coequalizer.isoTargetOfSelf]
#align category_theory.limits.coequalizer.iso_target_of_self_hom CategoryTheory.Limits.coequalizer.isoTargetOfSelf_hom

@[simp]
theorem coequalizer.isoTargetOfSelf_inv : (coequalizer.isoTargetOfSelf f).inv = coequalizer.œÄ f f :=
  rfl
#align category_theory.limits.coequalizer.iso_target_of_self_inv CategoryTheory.Limits.coequalizer.isoTargetOfSelf_inv

section Comparison

variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] (G : C ‚•§ D)

/-- The comparison morphism for the equalizer of `f,g`.
This is an isomorphism iff `G` preserves the equalizer of `f,g`; see
`CategoryTheory/Limits/Preserves/Shapes/Equalizers.lean`
-/
noncomputable def equalizerComparison [HasEqualizer f g] [HasEqualizer (G.map f) (G.map g)] :
    G.obj (equalizer f g) ‚ü∂ equalizer (G.map f) (G.map g) :=
  equalizer.lift (G.map (equalizer.Œπ _ _))
    (by simp only [‚Üê G.map_comp]; rw [equalizer.condition])
#align category_theory.limits.equalizer_comparison CategoryTheory.Limits.equalizerComparison

@[reassoc (attr := simp)]
theorem equalizerComparison_comp_œÄ [HasEqualizer f g] [HasEqualizer (G.map f) (G.map g)] :
    equalizerComparison f g G ‚â´ equalizer.Œπ (G.map f) (G.map g) = G.map (equalizer.Œπ f g) :=
  equalizer.lift_Œπ _ _
#align category_theory.limits.equalizer_comparison_comp_œÄ CategoryTheory.Limits.equalizerComparison_comp_œÄ

@[reassoc (attr := simp)]
theorem map_lift_equalizerComparison [HasEqualizer f g] [HasEqualizer (G.map f) (G.map g)] {Z : C}
    {h : Z ‚ü∂ X} (w : h ‚â´ f = h ‚â´ g) :
    G.map (equalizer.lift h w) ‚â´ equalizerComparison f g G =
      equalizer.lift (G.map h) (by simp only [‚Üê G.map_comp, w]) := by
  apply equalizer.hom_ext
  simp [‚Üê G.map_comp]
#align category_theory.limits.map_lift_equalizer_comparison CategoryTheory.Limits.map_lift_equalizerComparison

/-- The comparison morphism for the coequalizer of `f,g`. -/
noncomputable def coequalizerComparison [HasCoequalizer f g] [HasCoequalizer (G.map f) (G.map g)] :
    coequalizer (G.map f) (G.map g) ‚ü∂ G.obj (coequalizer f g) :=
  coequalizer.desc (G.map (coequalizer.œÄ _ _))
    (by simp only [‚Üê G.map_comp]; rw [coequalizer.condition])
#align category_theory.limits.coequalizer_comparison CategoryTheory.Limits.coequalizerComparison

@[reassoc (attr := simp)]
theorem Œπ_comp_coequalizerComparison [HasCoequalizer f g] [HasCoequalizer (G.map f) (G.map g)] :
    coequalizer.œÄ _ _ ‚â´ coequalizerComparison f g G = G.map (coequalizer.œÄ _ _) :=
  coequalizer.œÄ_desc _ _
#align category_theory.limits.Œπ_comp_coequalizer_comparison CategoryTheory.Limits.Œπ_comp_coequalizerComparison

@[reassoc (attr := simp)]
theorem coequalizerComparison_map_desc [HasCoequalizer f g] [HasCoequalizer (G.map f) (G.map g)]
    {Z : C} {h : Y ‚ü∂ Z} (w : f ‚â´ h = g ‚â´ h) :
    coequalizerComparison f g G ‚â´ G.map (coequalizer.desc h w) =
      coequalizer.desc (G.map h) (by simp only [‚Üê G.map_comp, w]) := by
  apply coequalizer.hom_ext
  simp [‚Üê G.map_comp]
#align category_theory.limits.coequalizer_comparison_map_desc CategoryTheory.Limits.coequalizerComparison_map_desc

end Comparison

variable (C)

/-- `HasEqualizers` represents a choice of equalizer for every pair of morphisms -/
abbrev HasEqualizers :=
  HasLimitsOfShape WalkingParallelPair C
#align category_theory.limits.has_equalizers CategoryTheory.Limits.HasEqualizers

/-- `HasCoequalizers` represents a choice of coequalizer for every pair of morphisms -/
abbrev HasCoequalizers :=
  HasColimitsOfShape WalkingParallelPair C
#align category_theory.limits.has_coequalizers CategoryTheory.Limits.HasCoequalizers

/-- If `C` has all limits of diagrams `parallelPair f g`, then it has all equalizers -/
theorem hasEqualizers_of_hasLimit_parallelPair
    [‚àÄ {X Y : C} {f g : X ‚ü∂ Y}, HasLimit (parallelPair f g)] : HasEqualizers C :=
  { has_limit := fun F => hasLimitOfIso (diagramIsoParallelPair F).symm }
#align category_theory.limits.has_equalizers_of_has_limit_parallel_pair CategoryTheory.Limits.hasEqualizers_of_hasLimit_parallelPair

/-- If `C` has all colimits of diagrams `parallelPair f g`, then it has all coequalizers -/
theorem hasCoequalizers_of_hasColimit_parallelPair
    [‚àÄ {X Y : C} {f g : X ‚ü∂ Y}, HasColimit (parallelPair f g)] : HasCoequalizers C :=
  { has_colimit := fun F => hasColimitOfIso (diagramIsoParallelPair F) }
#align category_theory.limits.has_coequalizers_of_has_colimit_parallel_pair CategoryTheory.Limits.hasCoequalizers_of_hasColimit_parallelPair

section

-- In this section we show that a split mono `f` equalizes `(retraction f ‚â´ f)` and `(ùüô Y)`.
variable {C} [IsSplitMono f]

/-- A split mono `f` equalizes `(retraction f ‚â´ f)` and `(ùüô Y)`.
Here we build the cone, and show in `isSplitMonoEqualizes` that it is a limit cone.
-/
-- @[simps (config := { rhsMd := semireducible })] Porting note: no semireducible
@[simps!]
noncomputable def coneOfIsSplitMono : Fork (ùüô Y) (retraction f ‚â´ f) :=
  Fork.ofŒπ f (by simp)
#align category_theory.limits.cone_of_is_split_mono CategoryTheory.Limits.coneOfIsSplitMono

@[simp]
theorem coneOfIsSplitMono_Œπ : (coneOfIsSplitMono f).Œπ = f :=
  rfl
#align category_theory.limits.cone_of_is_split_mono_Œπ CategoryTheory.Limits.coneOfIsSplitMono_Œπ

/-- A split mono `f` equalizes `(retraction f ‚â´ f)` and `(ùüô Y)`.
-/
noncomputable def isSplitMonoEqualizes {X Y : C} (f : X ‚ü∂ Y) [IsSplitMono f] :
    IsLimit (coneOfIsSplitMono f) :=
  Fork.IsLimit.mk' _ fun s =>
    ‚ü®s.Œπ ‚â´ retraction f, by
      dsimp
      rw [Category.assoc, ‚Üê s.condition]
      apply Category.comp_id, fun hm => by simp [‚Üê hm]‚ü©
#align category_theory.limits.is_split_mono_equalizes CategoryTheory.Limits.isSplitMonoEqualizes

end

/-- We show that the converse to `isSplitMonoEqualizes` is true:
Whenever `f` equalizes `(r ‚â´ f)` and `(ùüô Y)`, then `r` is a retraction of `f`. -/
def splitMonoOfEqualizer {X Y : C} {f : X ‚ü∂ Y} {r : Y ‚ü∂ X} (hr : f ‚â´ r ‚â´ f = f)
    (h : IsLimit (Fork.ofŒπ f (hr.trans (Category.comp_id _).symm : f ‚â´ r ‚â´ f = f ‚â´ ùüô Y))) :
    SplitMono f where
  retraction := r
  id := Fork.IsLimit.hom_ext h ((Category.assoc _ _ _).trans <| hr.trans (Category.id_comp _).symm)
#align category_theory.limits.split_mono_of_equalizer CategoryTheory.Limits.splitMonoOfEqualizer

variable {C f g}

/-- The fork obtained by postcomposing an equalizer fork with a monomorphism is an equalizer. -/
def isEqualizerCompMono {c : Fork f g} (i : IsLimit c) {Z : C} (h : Y ‚ü∂ Z) [hm : Mono h] :
    have : Fork.Œπ c ‚â´ f ‚â´ h = Fork.Œπ c ‚â´ g ‚â´ h := by
      simp only [‚Üê Category.assoc]
      exact congrArg (¬∑ ‚â´ h) c.condition;
    IsLimit (Fork.ofŒπ c.Œπ (by simp [this]) : Fork (f ‚â´ h) (g ‚â´ h)) :=
  Fork.IsLimit.mk' _ fun s =>
    let s' : Fork f g := Fork.ofŒπ s.Œπ (by apply hm.right_cancellation; simp [s.condition])
    let l := Fork.IsLimit.lift' i s'.Œπ s'.condition
    ‚ü®l.1, l.2, fun hm => by
      apply Fork.IsLimit.hom_ext i; rw [Fork.Œπ_ofŒπ] at hm; rw [hm]; exact l.2.symm‚ü©
#align category_theory.limits.is_equalizer_comp_mono CategoryTheory.Limits.isEqualizerCompMono

variable (C f g)

@[instance]
theorem hasEqualizer_comp_mono [HasEqualizer f g] {Z : C} (h : Y ‚ü∂ Z) [Mono h] :
    HasEqualizer (f ‚â´ h) (g ‚â´ h) :=
  ‚ü®‚ü®{   cone := _
        isLimit := isEqualizerCompMono (limit.isLimit _) h }‚ü©‚ü©
#align category_theory.limits.has_equalizer_comp_mono CategoryTheory.Limits.hasEqualizer_comp_mono

/-- An equalizer of an idempotent morphism and the identity is split mono. -/
@[simps]
def splitMonoOfIdempotentOfIsLimitFork {X : C} {f : X ‚ü∂ X} (hf : f ‚â´ f = f) {c : Fork (ùüô X) f}
    (i : IsLimit c) : SplitMono c.Œπ where
  retraction := i.lift (Fork.ofŒπ f (by simp [hf]))
  id := by
    letI := mono_of_isLimit_fork i
    rw [‚Üê cancel_mono_id c.Œπ, Category.assoc, Fork.IsLimit.lift_Œπ, Fork.Œπ_ofŒπ, ‚Üê c.condition]
    exact Category.comp_id c.Œπ
#align category_theory.limits.split_mono_of_idempotent_of_is_limit_fork CategoryTheory.Limits.splitMonoOfIdempotentOfIsLimitFork

/-- The equalizer of an idempotent morphism and the identity is split mono. -/
noncomputable def splitMonoOfIdempotentEqualizer {X : C} {f : X ‚ü∂ X} (hf : f ‚â´ f = f)
    [HasEqualizer (ùüô X) f] : SplitMono (equalizer.Œπ (ùüô X) f) :=
  splitMonoOfIdempotentOfIsLimitFork _ hf (limit.isLimit _)
#align category_theory.limits.split_mono_of_idempotent_equalizer CategoryTheory.Limits.splitMonoOfIdempotentEqualizer

section

-- In this section we show that a split epi `f` coequalizes `(f ‚â´ section_ f)` and `(ùüô X)`.
variable {C} [IsSplitEpi f]

/-- A split epi `f` coequalizes `(f ‚â´ section_ f)` and `(ùüô X)`.
Here we build the cocone, and show in `isSplitEpiCoequalizes` that it is a colimit cocone.
-/
-- @[simps (config := { rhsMd := semireducible })] Porting note: no semireducible
@[simps!]
noncomputable def coconeOfIsSplitEpi : Cofork (ùüô X) (f ‚â´ section_ f) :=
  Cofork.ofœÄ f (by simp)
#align category_theory.limits.cocone_of_is_split_epi CategoryTheory.Limits.coconeOfIsSplitEpi

@[simp]
theorem coconeOfIsSplitEpi_œÄ : (coconeOfIsSplitEpi f).œÄ = f :=
  rfl
#align category_theory.limits.cocone_of_is_split_epi_œÄ CategoryTheory.Limits.coconeOfIsSplitEpi_œÄ

/-- A split epi `f` coequalizes `(f ‚â´ section_ f)` and `(ùüô X)`.
-/
noncomputable def isSplitEpiCoequalizes {X Y : C} (f : X ‚ü∂ Y) [IsSplitEpi f] :
    IsColimit (coconeOfIsSplitEpi f) :=
  Cofork.IsColimit.mk' _ fun s =>
    ‚ü®section_ f ‚â´ s.œÄ, by
      dsimp
      rw [‚Üê Category.assoc, ‚Üê s.condition, Category.id_comp], fun hm => by simp [‚Üê hm]‚ü©
#align category_theory.limits.is_split_epi_coequalizes CategoryTheory.Limits.isSplitEpiCoequalizes

end

/-- We show that the converse to `isSplitEpiEqualizes` is true:
Whenever `f` coequalizes `(f ‚â´ s)` and `(ùüô X)`, then `s` is a section of `f`. -/
def splitEpiOfCoequalizer {X Y : C} {f : X ‚ü∂ Y} {s : Y ‚ü∂ X} (hs : f ‚â´ s ‚â´ f = f)
    (h :
      IsColimit
        (Cofork.ofœÄ f
          ((Category.assoc _ _ _).trans <| hs.trans (Category.id_comp f).symm :
            (f ‚â´ s) ‚â´ f = ùüô X ‚â´ f))) :
    SplitEpi f where
  section_ := s
  id := Cofork.IsColimit.hom_ext h (hs.trans (Category.comp_id _).symm)
#align category_theory.limits.split_epi_of_coequalizer CategoryTheory.Limits.splitEpiOfCoequalizer

variable {C f g}

/-- The cofork obtained by precomposing a coequalizer cofork with an epimorphism is
a coequalizer. -/
def isCoequalizerEpiComp {c : Cofork f g} (i : IsColimit c) {W : C} (h : W ‚ü∂ X) [hm : Epi h] :
    have : (h ‚â´ f) ‚â´ Cofork.œÄ c = (h ‚â´ g) ‚â´ Cofork.œÄ c := by
      simp only [Category.assoc]
      exact congrArg (h ‚â´ ¬∑) c.condition
    IsColimit (Cofork.ofœÄ c.œÄ (this) : Cofork (h ‚â´ f) (h ‚â´ g)) :=
  Cofork.IsColimit.mk' _ fun s =>
    let s' : Cofork f g :=
      Cofork.ofœÄ s.œÄ (by apply hm.left_cancellation; simp_rw [‚Üê Category.assoc, s.condition])
    let l := Cofork.IsColimit.desc' i s'.œÄ s'.condition
    ‚ü®l.1, l.2, fun hm => by
      apply Cofork.IsColimit.hom_ext i; rw [Cofork.œÄ_ofœÄ] at hm; rw [hm]; exact l.2.symm‚ü©
#align category_theory.limits.is_coequalizer_epi_comp CategoryTheory.Limits.isCoequalizerEpiComp

theorem hasCoequalizer_epi_comp [HasCoequalizer f g] {W : C} (h : W ‚ü∂ X) [Epi h] :
    HasCoequalizer (h ‚â´ f) (h ‚â´ g) :=
  ‚ü®‚ü®{   cocone := _
        isColimit := isCoequalizerEpiComp (colimit.isColimit _) h }‚ü©‚ü©
#align category_theory.limits.has_coequalizer_epi_comp CategoryTheory.Limits.hasCoequalizer_epi_comp

variable (C f g)

/-- A coequalizer of an idempotent morphism and the identity is split epi. -/
@[simps]
def splitEpiOfIdempotentOfIsColimitCofork {X : C} {f : X ‚ü∂ X} (hf : f ‚â´ f = f) {c : Cofork (ùüô X) f}
    (i : IsColimit c) : SplitEpi c.œÄ where
  section_ := i.desc (Cofork.ofœÄ f (by simp [hf]))
  id := by
    letI := epi_of_isColimit_cofork i
    rw [‚Üê cancel_epi_id c.œÄ, ‚Üê Category.assoc, Cofork.IsColimit.œÄ_desc, Cofork.œÄ_ofœÄ, ‚Üê
      c.condition]
    exact Category.id_comp _
#align category_theory.limits.split_epi_of_idempotent_of_is_colimit_cofork CategoryTheory.Limits.splitEpiOfIdempotentOfIsColimitCofork

/-- The coequalizer of an idempotent morphism and the identity is split epi. -/
noncomputable def splitEpiOfIdempotentCoequalizer {X : C} {f : X ‚ü∂ X} (hf : f ‚â´ f = f)
    [HasCoequalizer (ùüô X) f] : SplitEpi (coequalizer.œÄ (ùüô X) f) :=
  splitEpiOfIdempotentOfIsColimitCofork _ hf (colimit.isColimit _)
#align category_theory.limits.split_epi_of_idempotent_coequalizer CategoryTheory.Limits.splitEpiOfIdempotentCoequalizer

end CategoryTheory.Limits
