/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Reid Barton
-/
import Mathlib.Data.TypeMax
import Mathlib.Logic.UnivLE
import Mathlib.CategoryTheory.Limits.Shapes.Images
import Mathlib.CategoryTheory.Filtered.Basic

#align_import category_theory.limits.types from "leanprover-community/mathlib"@"4aa2a2e17940311e47007f087c9df229e7f12942"

/-!
# Limits in the category of types.

We show that the category of types has all (co)limits, by providing the usual concrete models.

We also give a characterisation of filtered colimits in `Type`, via
`colimit.Î¹ F i xi = colimit.Î¹ F j xj â†” âˆƒ k (f : i âŸ¶ k) (g : j âŸ¶ k), F.map f xi = F.map g xj`.

Next, we prove the category of types has categorical images, and that these agree with the range of
a function.

Finally, we give the natural isomorphism between cones on `F` with cone point `X` and the type
`lim Hom(X, FÂ·)`, and similarly the natural isomorphism between cocones on `F` with cocone point `X`
and the type `lim Hom(FÂ·, X)`.

-/

set_option autoImplicit true


open CategoryTheory CategoryTheory.Limits

universe v u w

namespace CategoryTheory.Limits

namespace Types

section limit_characterization

variable {J : Type v} [Category.{w} J] {F : J â¥¤ Type u}

/-- Given a section of a functor F into `Type*`,
  construct a cone over F with `PUnit` as the cone point. -/
def coneOfSection {s} (hs : s âˆˆ F.sections) : Cone F where
  pt := PUnit
  Ï€ :=
  { app := fun j _ â†¦ s j,
    naturality := fun i j f â†¦ by ext; exact (hs f).symm }

/-- Given a cone over a functor F into `Type*` and an element in the cone point,
  construct a section of F. -/
def sectionOfCone (c : Cone F) (x : c.pt) : F.sections :=
  âŸ¨fun j â†¦ c.Ï€.app j x, fun f â†¦ congr_fun (c.Ï€.naturality f).symm xâŸ©

theorem isLimit_iff (c : Cone F) :
    Nonempty (IsLimit c) â†” âˆ€ s âˆˆ F.sections, âˆƒ! x : c.pt, âˆ€ j, c.Ï€.app j x = s j := by
  refine âŸ¨fun âŸ¨tâŸ© s hs â†¦ ?_, fun h â†¦ âŸ¨?_âŸ©âŸ©
  Â· let cs := coneOfSection hs
    exact âŸ¨t.lift cs âŸ¨âŸ©, fun j â†¦ congr_fun (t.fac cs j) âŸ¨âŸ©,
      fun x hx â†¦ congr_fun (t.uniq cs (fun _ â†¦ x) fun j â†¦ funext fun _ â†¦ hx j) âŸ¨âŸ©âŸ©
  Â· choose x hx using fun c y â†¦ h _ (sectionOfCone c y).2
    exact âŸ¨x, fun c j â†¦ funext fun y â†¦ (hx c y).1 j,
      fun c f hf â†¦ funext fun y â†¦ (hx c y).2 (f y) (fun j â†¦ congr_fun (hf j) y)âŸ©

/-- The equivalence between a limiting cone of `F` in `Type u` and the "concrete" definition as the
  sections of `F`. -/
noncomputable def isLimitEquivSections {c : Cone F} (t : IsLimit c) :
    c.pt â‰ƒ F.sections where
  toFun := sectionOfCone c
  invFun s := t.lift (coneOfSection s.2) âŸ¨âŸ©
  left_inv x := (congr_fun (t.uniq (coneOfSection _) (fun _ â†¦ x) fun _ â†¦ rfl) âŸ¨âŸ©).symm
  right_inv s := Subtype.ext (funext fun j â†¦ congr_fun (t.fac (coneOfSection s.2) j) âŸ¨âŸ©)
#align category_theory.limits.types.is_limit_equiv_sections CategoryTheory.Limits.Types.isLimitEquivSections

@[simp]
theorem isLimitEquivSections_apply {c : Cone F} (t : IsLimit c) (j : J)
    (x : c.pt) : (isLimitEquivSections t x : âˆ€ j, F.obj j) j = c.Ï€.app j x := rfl
#align category_theory.limits.types.is_limit_equiv_sections_apply CategoryTheory.Limits.Types.isLimitEquivSections_apply

@[simp]
theorem isLimitEquivSections_symm_apply {c : Cone F} (t : IsLimit c)
    (x : F.sections) (j : J) :
    c.Ï€.app j ((isLimitEquivSections t).symm x) = (x : âˆ€ j, F.obj j) j := by
  conv_rhs => rw [â† (isLimitEquivSections t).right_inv x]
#align category_theory.limits.types.is_limit_equiv_sections_symm_apply CategoryTheory.Limits.Types.isLimitEquivSections_symm_apply

end limit_characterization

variable {J : Type v} [Category.{w} J]

/-! We now provide two distinct implementations in the category of types.

The first, in the `CategoryTheory.Limits.Types.Small` namespace,
assumes `Small.{u} J` and constructs `J`-indexed limits in `Type u`.

The second, in the `CategoryTheory.Limits.Types.TypeMax` namespace
constructs limits for functors `F : J â¥¤ TypeMax.{v, u}`, for `J : Type v`.
This construction is slightly nicer, as the limit is definitionally just `F.sections`,
rather than `Shrink F.sections`, which makes an arbitrary choice of `u`-small representative.

Hopefully we might be able to entirely remove the `TypeMax` constructions,
but for now they are useful glue for the later parts of the library.
-/

namespace Small

variable [Small.{u} J]

/-- (internal implementation) the limit cone of a functor,
implemented as flat sections of a pi type
-/
@[simps]
noncomputable def limitCone (F : J â¥¤ Type u) : Cone F where
  pt := Shrink F.sections
  Ï€ :=
    { app := fun j u => ((equivShrink F.sections).symm u).val j
      naturality := fun j j' f => by
        funext x
        simp }

@[ext]
lemma limitCone_pt_ext (F : J â¥¤ Type u) {x y : (limitCone F).pt}
    (w : (equivShrink F.sections).symm x = (equivShrink F.sections).symm y) : x = y := by
  aesop

/-- (internal implementation) the fact that the proposed limit cone is the limit -/
@[simps]
noncomputable def limitConeIsLimit (F : J â¥¤ Type u) : IsLimit (limitCone.{v, u} F) where
  lift s v := equivShrink F.sections
    { val := fun j => s.Ï€.app j v
      property := fun f => congr_fun (Cone.w s f) _ }
  uniq := fun _ _ w => by
    ext x j
    simpa using congr_fun (w j) x

end Small

-- TODO: If `UnivLE` works out well, we will eventually want to deprecate these
-- definitions, and probably as a first step put them in namespace or otherwise rename them.
section TypeMax

/-- (internal implementation) the limit cone of a functor,
implemented as flat sections of a pi type
-/
@[simps]
noncomputable def limitCone (F : J â¥¤ TypeMax.{v, u}) : Cone F where
  pt := F.sections
  Ï€ :=
    { app := fun j u => u.val j
      naturality := fun j j' f => by
        funext x
        simp }
#align category_theory.limits.types.limit_cone CategoryTheory.Limits.Types.limitCone

/-- (internal implementation) the fact that the proposed limit cone is the limit -/
@[simps]
noncomputable def limitConeIsLimit (F : J â¥¤ TypeMax.{v, u}) : IsLimit (limitCone F) where
  lift s v :=
    { val := fun j => s.Ï€.app j v
      property := fun f => congr_fun (Cone.w s f) _ }
  uniq := fun _ _ w => by
    funext x
    apply Subtype.ext
    funext j
    exact congr_fun (w j) x
#align category_theory.limits.types.limit_cone_is_limit CategoryTheory.Limits.Types.limitConeIsLimit

end TypeMax


/-!
The results in this section have a `UnivLE.{v, u}` hypothesis,
but as they only use the constructions from the `CategoryTheory.Limits.Types.UnivLE` namespace
in their definitions (rather than their statements),
we leave them in the main `CategoryTheory.Limits.Types` namespace.
-/
section UnivLE

open UnivLE

instance hasLimit [Small.{u} J] (F : J â¥¤ Type u) : HasLimit F :=
  HasLimit.mk
    { cone := Small.limitCone.{v, u} F
      isLimit := Small.limitConeIsLimit F }

/--
The category of types has all limits.

More specifically, when `UnivLE.{v, u}`, the category `Type u` has all `v`-small limits.

See <https://stacks.math.columbia.edu/tag/002U>.
-/
instance (priority := 1300) hasLimitsOfSize [UnivLE.{v, u}] : HasLimitsOfSize.{w', v} (Type u) where
  has_limits_of_shape _ := { }
#align category_theory.limits.types.has_limits_of_size CategoryTheory.Limits.Types.hasLimitsOfSize

variable [Small.{u} J]
/-- The equivalence between the abstract limit of `F` in `TypeMax.{v, u}`
and the "concrete" definition as the sections of `F`.
-/
noncomputable def limitEquivSections (F : J â¥¤ Type u) :
    (@limit _ _ _ _ F (hasLimit F) : Type u) â‰ƒ F.sections :=
  isLimitEquivSections (limit.isLimit F)
#align category_theory.limits.types.limit_equiv_sections CategoryTheory.Limits.Types.limitEquivSections

@[simp]
theorem limitEquivSections_apply (F : J â¥¤ Type u) (x : limit F) (j : J) :
    ((limitEquivSections F) x : âˆ€ j, F.obj j) j = limit.Ï€ F j x :=
  isLimitEquivSections_apply _ _ _
#align category_theory.limits.types.limit_equiv_sections_apply CategoryTheory.Limits.Types.limitEquivSections_apply

@[simp]
theorem limitEquivSections_symm_apply (F : J â¥¤ Type u) (x : F.sections) (j : J) :
    limit.Ï€ F j ((limitEquivSections F).symm x) = (x : âˆ€ j, F.obj j) j :=
  isLimitEquivSections_symm_apply _ _ _
#align category_theory.limits.types.limit_equiv_sections_symm_apply CategoryTheory.Limits.Types.limitEquivSections_symm_apply

-- Porting note: `limitEquivSections_symm_apply'` was removed because the linter
--   complains it is unnecessary
--@[simp]
--theorem limitEquivSections_symm_apply' (F : J â¥¤ Type v) (x : F.sections) (j : J) :
--    limit.Ï€ F j ((limitEquivSections.{v, v} F).symm x) = (x : âˆ€ j, F.obj j) j :=
--  isLimitEquivSections_symm_apply _ _ _
--#align category_theory.limits.types.limit_equiv_sections_symm_apply' CategoryTheory.Limits.Types.limitEquivSections_symm_apply'

--porting note: removed @[ext]
/-- Construct a term of `limit F : Type u` from a family of terms `x : Î  j, F.obj j`
which are "coherent": `âˆ€ (j j') (f : j âŸ¶ j'), F.map f (x j) = x j'`.
-/
noncomputable def Limit.mk (F : J â¥¤ Type u) (x : âˆ€ j, F.obj j)
    (h : âˆ€ (j j') (f : j âŸ¶ j'), F.map f (x j) = x j') : (limit F : Type u) :=
  (limitEquivSections F).symm âŸ¨x, h _ _âŸ©
#align category_theory.limits.types.limit.mk CategoryTheory.Limits.Types.Limit.mk

@[simp]
theorem Limit.Ï€_mk (F : J â¥¤ Type u) (x : âˆ€ j, F.obj j)
    (h : âˆ€ (j j') (f : j âŸ¶ j'), F.map f (x j) = x j') (j) :
      limit.Ï€ F j (Limit.mk F x h) = x j := by
  dsimp [Limit.mk]
  simp
#align category_theory.limits.types.limit.Ï€_mk CategoryTheory.Limits.Types.Limit.Ï€_mk

-- Porting note: `Limit.Ï€_mk'` was removed because the linter complains it is unnecessary
--@[simp]
--theorem Limit.Ï€_mk' (F : J â¥¤ Type v) (x : âˆ€ j, F.obj j)
--    (h : âˆ€ (j j') (f : j âŸ¶ j'), F.map f (x j) = x j') (j) :
--    limit.Ï€ F j (Limit.mk.{v, v} F x h) = x j := by
--  dsimp [Limit.mk]
--  simp
--#align category_theory.limits.types.limit.Ï€_mk' CategoryTheory.Limits.Types.Limit.Ï€_mk'

-- PROJECT: prove this for concrete categories where the forgetful functor preserves limits
@[ext]
theorem limit_ext (F : J â¥¤ Type u) (x y : limit F)
    (w : âˆ€ j, limit.Ï€ F j x = limit.Ï€ F j y) : x = y := by
  apply (limitEquivSections F).injective
  ext j
  simp [w j]
#align category_theory.limits.types.limit_ext CategoryTheory.Limits.Types.limit_ext

@[ext]
theorem limit_ext' (F : J â¥¤ Type v) (x y : limit F) (w : âˆ€ j, limit.Ï€ F j x = limit.Ï€ F j y) :
    x = y :=
  limit_ext F x y w
#align category_theory.limits.types.limit_ext' CategoryTheory.Limits.Types.limit_ext'

theorem limit_ext_iff (F : J â¥¤ Type u) (x y : limit F) :
    x = y â†” âˆ€ j, limit.Ï€ F j x = limit.Ï€ F j y :=
  âŸ¨fun t _ => t â–¸ rfl, limit_ext _ _ _âŸ©
#align category_theory.limits.types.limit_ext_iff CategoryTheory.Limits.Types.limit_ext_iff

theorem limit_ext_iff' (F : J â¥¤ Type v) (x y : limit F) :
    x = y â†” âˆ€ j, limit.Ï€ F j x = limit.Ï€ F j y :=
  âŸ¨fun t _ => t â–¸ rfl, limit_ext' _ _ _âŸ©
#align category_theory.limits.types.limit_ext_iff' CategoryTheory.Limits.Types.limit_ext_iff'

-- TODO: are there other limits lemmas that should have `_apply` versions?
-- Can we generate these like with `@[reassoc]`?
-- PROJECT: prove these for any concrete category where the forgetful functor preserves limits?
--porting note: @[simp] was removed because the linter said it was useless
--@[simp]
theorem Limit.w_apply {F : J â¥¤ Type u} {j j' : J} {x : limit F} (f : j âŸ¶ j') :
    F.map f (limit.Ï€ F j x) = limit.Ï€ F j' x :=
  congr_fun (limit.w F f) x
#align category_theory.limits.types.limit.w_apply CategoryTheory.Limits.Types.Limit.w_apply

--porting note: @[simp] was removed because the linter said it was useless
theorem Limit.lift_Ï€_apply (F : J â¥¤ Type u) (s : Cone F) (j : J) (x : s.pt) :
    limit.Ï€ F j (limit.lift F s x) = s.Ï€.app j x :=
  congr_fun (limit.lift_Ï€ s j) x
#align category_theory.limits.types.limit.lift_Ï€_apply CategoryTheory.Limits.Types.Limit.lift_Ï€_apply

--porting note: @[simp] was removed because the linter said it was useless
theorem Limit.map_Ï€_apply {F G : J â¥¤ Type u} (Î± : F âŸ¶ G) (j : J) (x : limit F) :
    limit.Ï€ G j (limMap Î± x) = Î±.app j (limit.Ï€ F j x) :=
  congr_fun (limMap_Ï€ Î± j) x
#align category_theory.limits.types.limit.map_Ï€_apply CategoryTheory.Limits.Types.Limit.map_Ï€_apply

@[simp]
theorem Limit.w_apply' {F : J â¥¤ Type v} {j j' : J} {x : limit F} (f : j âŸ¶ j') :
    F.map f (limit.Ï€ F j x) = limit.Ï€ F j' x :=
  congr_fun (limit.w F f) x
#align category_theory.limits.types.limit.w_apply' CategoryTheory.Limits.Types.Limit.w_apply'

@[simp]
theorem Limit.lift_Ï€_apply' (F : J â¥¤ Type v) (s : Cone F) (j : J) (x : s.pt) :
    limit.Ï€ F j (limit.lift F s x) = s.Ï€.app j x :=
  congr_fun (limit.lift_Ï€ s j) x
#align category_theory.limits.types.limit.lift_Ï€_apply' CategoryTheory.Limits.Types.Limit.lift_Ï€_apply'

@[simp]
theorem Limit.map_Ï€_apply' {F G : J â¥¤ Type v} (Î± : F âŸ¶ G) (j : J) (x : limit F) :
    limit.Ï€ G j (limMap Î± x) = Î±.app j (limit.Ï€ F j x) :=
  congr_fun (limMap_Ï€ Î± j) x
#align category_theory.limits.types.limit.map_Ï€_apply' CategoryTheory.Limits.Types.Limit.map_Ï€_apply'

end UnivLE

/-!
In this section we verify that instances are available as expected.
-/
section instances

example : HasLimitsOfSize.{w, w, max v w, max (v + 1) (w + 1)} (TypeMax.{w, v}) := inferInstance
example : HasLimitsOfSize.{w, w, max v w, max (v + 1) (w + 1)} (Type max v w) := inferInstance

example : HasLimitsOfSize.{0, 0, v, v+1} (Type v) := inferInstance
example : HasLimitsOfSize.{v, v, v, v+1} (Type v) := inferInstance

example [UnivLE.{v, u}] : HasLimitsOfSize.{v, v, u, u+1} (Type u) := inferInstance

end instances

/--
The relation defining the quotient type which implements the colimit of a functor `F : J â¥¤ Type u`.
See `CategoryTheory.Limits.Types.Quot`.
-/
def Quot.Rel (F : J â¥¤ TypeMax.{v, u}) : (Î£ j, F.obj j) â†’ (Î£ j, F.obj j) â†’ Prop := fun p p' =>
  âˆƒ f : p.1 âŸ¶ p'.1, p'.2 = F.map f p.2
#align category_theory.limits.types.quot.rel CategoryTheory.Limits.Types.Quot.Rel

-- porting note (#10927): removed @[nolint has_nonempty_instance]
/-- A quotient type implementing the colimit of a functor `F : J â¥¤ Type u`,
as pairs `âŸ¨j, xâŸ©` where `x : F.obj j`, modulo the equivalence relation generated by
`âŸ¨j, xâŸ© ~ âŸ¨j', x'âŸ©` whenever there is a morphism `f : j âŸ¶ j'` so `F.map f x = x'`.
-/
def Quot (F : J â¥¤ TypeMax.{v, u}) : TypeMax.{v, u} :=
  _root_.Quot (Quot.Rel F)
#align category_theory.limits.types.quot CategoryTheory.Limits.Types.Quot

/-- (internal implementation) the colimit cocone of a functor,
implemented as a quotient of a sigma type
-/
def colimitCocone (F : J â¥¤ TypeMax.{v, u}) : Cocone F where
  pt := Quot F
  Î¹ :=
    { app := fun j x => Quot.mk _ âŸ¨j, xâŸ©
      naturality := fun _ _ f => funext fun _ => Eq.symm (Quot.sound âŸ¨f, rflâŸ©) }
#align category_theory.limits.types.colimit_cocone CategoryTheory.Limits.Types.colimitCocone

--attribute [local elab_with_expected_type] Quot.lift

/-- (internal implementation) the fact that the proposed colimit cocone is the colimit -/
def colimitCoconeIsColimit (F : J â¥¤ TypeMax.{v, u}) : IsColimit (colimitCocone F) where
  desc s :=
    Quot.lift (fun p : Î£j, F.obj j => s.Î¹.app p.1 p.2) fun âŸ¨j, xâŸ© âŸ¨j', x'âŸ© âŸ¨f, hfâŸ© => by
      dsimp at hf
      rw [hf]
      exact (congr_fun (Cocone.w s f) x).symm
  uniq s m hm := by
    funext x
    induction' x using Quot.ind with x
    exact congr_fun (hm x.1) x.2
#align category_theory.limits.types.colimit_cocone_is_colimit CategoryTheory.Limits.Types.colimitCoconeIsColimit

namespace Small

variable [Small.{u} J]

/--
The relation defining the quotient type which implements the colimit of a functor `F : J â¥¤ Type u`.
See `CategoryTheory.Limits.Types.Quot`.
-/
def Quot.Rel (F : J â¥¤ Type u) :
    (Î£ j, F.obj ((equivShrink.{u} J).symm j)) â†’ (Î£ j, F.obj ((equivShrink.{u} J).symm j)) â†’ Prop :=
  fun p p' => âˆƒ f, p'.2 = F.map f p.2

/-- A quotient type implementing the colimit of a functor `F : J â¥¤ Type u`,
as pairs `âŸ¨j, xâŸ©` where `x : F.obj j`, modulo the equivalence relation generated by
`âŸ¨j, xâŸ© ~ âŸ¨j', x'âŸ©` whenever there is a morphism `f : j âŸ¶ j'` so `F.map f x = x'`.
-/
def Quot (F : J â¥¤ Type u) : Type u :=
  _root_.Quot (Quot.Rel F)

/-- (internal implementation) the colimit cocone of a functor,
implemented as a quotient of a sigma type
-/
noncomputable def colimitCocone (F : J â¥¤ Type u) : Cocone F where
  pt := Quot F
  Î¹ :=
    { app := fun j x => Quot.mk _
        âŸ¨equivShrink J j, F.map (eqToHom (Equiv.symm_apply_apply _ _).symm) xâŸ©
      naturality := fun j j' f => funext fun x =>
        Eq.symm (Quot.sound âŸ¨eqToHom (by simp) â‰« f â‰« eqToHom (by simp), by simpâŸ©) }

/-- (internal implementation) the fact that the proposed colimit cocone is the colimit -/
noncomputable def colimitCoconeIsColimit (F : J â¥¤ Type u) : IsColimit (colimitCocone F) where
  desc s :=
    Quot.lift (fun p => s.Î¹.app _ p.2) <| by
      rintro âŸ¨j, xâŸ© âŸ¨j', x'âŸ© âŸ¨f, hfâŸ©
      dsimp at hf
      rw [hf]
      exact (congr_fun (Cocone.w s f) x).symm
  fac s j := by
    ext x
    let t : j âŸ¶ (equivShrink J).symm ((equivShrink J) j) := eqToHom (by simp)
    simpa using congrFun (s.Î¹.naturality t) x
  uniq s m hm := by
    ext x
    induction' x using Quot.ind with x
    dsimp only [id_eq, Functor.const_obj_obj, types_comp_apply, eq_mpr_eq_cast]
    rw [â† congrFun (hm ((equivShrink J).symm x.1)) x.2]
    exact congrArg m (Quot.sound âŸ¨_, rflâŸ©)

end Small

section UnivLE

instance hasColimit [Small.{u} J] (F : J â¥¤ Type u) : HasColimit F :=
  HasColimit.mk
    { cocone := Small.colimitCocone.{v, u} F
      isColimit := Small.colimitCoconeIsColimit F }

instance (priority := 1300) hasColimitsOfSize [UnivLE.{v, u}] : HasColimitsOfSize.{w', v} (Type u) where
  has_colimits_of_shape _ := { }

end UnivLE

section instances

example : HasColimitsOfSize.{w, w, max v w, max (v + 1) (w + 1)} (TypeMax.{w, v}) := inferInstance
example : HasColimitsOfSize.{w, w, max v w, max (v + 1) (w + 1)} (Type max v w) := inferInstance

example : HasColimitsOfSize.{0, 0, v, v+1} (Type v) := inferInstance
example : HasColimitsOfSize.{v, v, v, v+1} (Type v) := inferInstance

example [UnivLE.{v, u}] : HasColimitsOfSize.{v, v, u, u+1} (Type u) := inferInstance

end instances

/-- The equivalence between the abstract colimit of `F` in `Type u`
and the "concrete" definition as a quotient.
-/
noncomputable def colimitEquivQuot (F : J â¥¤ TypeMax.{v, u}) : colimit F â‰ƒ Quot F :=
  (IsColimit.coconePointUniqueUpToIso (colimit.isColimit F) (colimitCoconeIsColimit F)).toEquiv
#align category_theory.limits.types.colimit_equiv_quot CategoryTheory.Limits.Types.colimitEquivQuot

@[simp]
theorem colimitEquivQuot_symm_apply (F : J â¥¤ TypeMax.{v, u}) (j : J) (x : F.obj j) :
    (colimitEquivQuot F).symm (Quot.mk _ âŸ¨j, xâŸ©) = colimit.Î¹ F j x :=
  rfl
#align category_theory.limits.types.colimit_equiv_quot_symm_apply CategoryTheory.Limits.Types.colimitEquivQuot_symm_apply

@[simp]
theorem colimitEquivQuot_apply (F : J â¥¤ TypeMax.{v, u}) (j : J) (x : F.obj j) :
    (colimitEquivQuot F) (colimit.Î¹ F j x) = Quot.mk _ âŸ¨j, xâŸ© := by
  apply (colimitEquivQuot F).symm.injective
  simp
#align category_theory.limits.types.colimit_equiv_quot_apply CategoryTheory.Limits.Types.colimitEquivQuot_apply

--porting note: @[simp] was removed because the linter said it was useless
theorem Colimit.w_apply {F : J â¥¤ TypeMax.{v, u}} {j j' : J} {x : F.obj j} (f : j âŸ¶ j') :
    colimit.Î¹ F j' (F.map f x) = colimit.Î¹ F j x :=
  congr_fun (colimit.w F f) x
#align category_theory.limits.types.colimit.w_apply CategoryTheory.Limits.Types.Colimit.w_apply

--porting note: @[simp] was removed because the linter said it was useless
theorem Colimit.Î¹_desc_apply (F : J â¥¤ TypeMax.{v, u}) (s : Cocone F) (j : J) (x : F.obj j) :
    colimit.desc F s (colimit.Î¹ F j x) = s.Î¹.app j x :=
   congr_fun (colimit.Î¹_desc s j) x
#align category_theory.limits.types.colimit.Î¹_desc_apply CategoryTheory.Limits.Types.Colimit.Î¹_desc_apply

--porting note: @[simp] was removed because the linter said it was useless
theorem Colimit.Î¹_map_apply {F G : J â¥¤ TypeMax.{v, u}} (Î± : F âŸ¶ G) (j : J) (x : F.obj j) :
    colim.map Î± (colimit.Î¹ F j x) = colimit.Î¹ G j (Î±.app j x) :=
  congr_fun (colimit.Î¹_map Î± j) x
#align category_theory.limits.types.colimit.Î¹_map_apply CategoryTheory.Limits.Types.Colimit.Î¹_map_apply

@[simp]
theorem Colimit.w_apply' {F : J â¥¤ Type v} {j j' : J} {x : F.obj j} (f : j âŸ¶ j') :
    colimit.Î¹ F j' (F.map f x) = colimit.Î¹ F j x :=
  congr_fun (colimit.w F f) x
#align category_theory.limits.types.colimit.w_apply' CategoryTheory.Limits.Types.Colimit.w_apply'

@[simp]
theorem Colimit.Î¹_desc_apply' (F : J â¥¤ Type v) (s : Cocone F) (j : J) (x : F.obj j) :
    colimit.desc F s (colimit.Î¹ F j x) = s.Î¹.app j x :=
  congr_fun (colimit.Î¹_desc s j) x
#align category_theory.limits.types.colimit.Î¹_desc_apply' CategoryTheory.Limits.Types.Colimit.Î¹_desc_apply'

@[simp]
theorem Colimit.Î¹_map_apply' {F G : J â¥¤ Type v} (Î± : F âŸ¶ G) (j : J) (x) :
    colim.map Î± (colimit.Î¹ F j x) = colimit.Î¹ G j (Î±.app j x) :=
  congr_fun (colimit.Î¹_map Î± j) x
#align category_theory.limits.types.colimit.Î¹_map_apply' CategoryTheory.Limits.Types.Colimit.Î¹_map_apply'

theorem colimit_sound {F : J â¥¤ TypeMax.{v, u}} {j j' : J} {x : F.obj j} {x' : F.obj j'} (f : j âŸ¶ j')
    (w : F.map f x = x') : colimit.Î¹ F j x = colimit.Î¹ F j' x' := by
  rw [â† w, Colimit.w_apply]
#align category_theory.limits.types.colimit_sound CategoryTheory.Limits.Types.colimit_sound

theorem colimit_sound' {F : J â¥¤ TypeMax.{v, u}} {j j' : J} {x : F.obj j} {x' : F.obj j'} {j'' : J}
    (f : j âŸ¶ j'') (f' : j' âŸ¶ j'') (w : F.map f x = F.map f' x') :
    colimit.Î¹ F j x = colimit.Î¹ F j' x' := by
  rw [â† colimit.w _ f, â† colimit.w _ f']
  rw [types_comp_apply, types_comp_apply, w]
#align category_theory.limits.types.colimit_sound' CategoryTheory.Limits.Types.colimit_sound'

theorem colimit_eq {F : J â¥¤ TypeMax.{v, u}} {j j' : J} {x : F.obj j} {x' : F.obj j'}
    (w : colimit.Î¹ F j x = colimit.Î¹ F j' x') :
      EqvGen (Quot.Rel F) âŸ¨j, xâŸ© âŸ¨j', x'âŸ© := by
  apply Quot.eq.1
  simpa using congr_arg (colimitEquivQuot.{v, u} F) w
#align category_theory.limits.types.colimit_eq CategoryTheory.Limits.Types.colimit_eq

theorem jointly_surjective_of_isColimit {F : J â¥¤ Type u} {t : Cocone F} (h : IsColimit t)
    (x : t.pt) : âˆƒ j y, t.Î¹.app j y = x := by
  by_contra hx
  simp_rw [not_exists] at hx
  apply (_ : (fun _ â†¦ ULift.up True) â‰  (âŸ¨Â· â‰  xâŸ©))
  Â· refine h.hom_ext fun j â†¦ ?_
    ext y
    exact (true_iff _).mpr (hx j y)
  Â· exact fun he â†¦ of_eq_true (congr_arg ULift.down <| congr_fun he x).symm rfl

theorem jointly_surjective (F : J â¥¤ TypeMax.{v, u}) {t : Cocone F} (h : IsColimit t) (x : t.pt) :
    âˆƒ j y, t.Î¹.app j y = x := jointly_surjective_of_isColimit h x
#align category_theory.limits.types.jointly_surjective CategoryTheory.Limits.Types.jointly_surjective

/-- A variant of `jointly_surjective` for `x : colimit F`. -/
theorem jointly_surjective' {F : J â¥¤ TypeMax.{v, u}} (x : colimit F) :
    âˆƒ j y, colimit.Î¹ F j y = x :=
  jointly_surjective F (colimit.isColimit F) x
#align category_theory.limits.types.jointly_surjective' CategoryTheory.Limits.Types.jointly_surjective'


namespace FilteredColimit

/- For filtered colimits of types, we can give an explicit description
  of the equivalence relation generated by the relation used to form
  the colimit.  -/
variable (F : J â¥¤ TypeMax.{v, u})

/-- An alternative relation on `Î£ j, F.obj j`,
which generates the same equivalence relation as we use to define the colimit in `Type` above,
but that is more convenient when working with filtered colimits.

Elements in `F.obj j` and `F.obj j'` are equivalent if there is some `k : J` to the right
where their images are equal.
-/
protected def Rel (x y : Î£ j, F.obj j) : Prop :=
  âˆƒ (k : _) (f : x.1 âŸ¶ k) (g : y.1 âŸ¶ k), F.map f x.2 = F.map g y.2
#align category_theory.limits.types.filtered_colimit.rel CategoryTheory.Limits.Types.FilteredColimit.Rel

theorem rel_of_quot_rel (x y : Î£ j, F.obj j) :
    Quot.Rel F x y â†’ FilteredColimit.Rel.{v, u} F x y :=
  fun âŸ¨f, hâŸ© => âŸ¨y.1, f, ðŸ™ y.1, by rw [â† h, FunctorToTypes.map_id_apply]âŸ©
#align category_theory.limits.types.filtered_colimit.rel_of_quot_rel CategoryTheory.Limits.Types.FilteredColimit.rel_of_quot_rel

theorem eqvGen_quot_rel_of_rel (x y : Î£ j, F.obj j) :
    FilteredColimit.Rel.{v, u} F x y â†’ EqvGen (Quot.Rel F) x y := fun âŸ¨k, f, g, hâŸ© => by
  refine' EqvGen.trans _ âŸ¨k, F.map f x.2âŸ© _ _ _
  Â· exact (EqvGen.rel _ _ âŸ¨f, rflâŸ©)
  Â· exact (EqvGen.symm _ _ (EqvGen.rel _ _ âŸ¨g, hâŸ©))
#align category_theory.limits.types.filtered_colimit.eqv_gen_quot_rel_of_rel CategoryTheory.Limits.Types.FilteredColimit.eqvGen_quot_rel_of_rel

--attribute [local elab_without_expected_type] nat_trans.app

/-- Recognizing filtered colimits of types. -/
noncomputable def isColimitOf (t : Cocone F) (hsurj : âˆ€ x : t.pt, âˆƒ i xi, x = t.Î¹.app i xi)
    (hinj :
      âˆ€ i j xi xj,
        t.Î¹.app i xi = t.Î¹.app j xj â†’ âˆƒ (k : _) (f : i âŸ¶ k) (g : j âŸ¶ k), F.map f xi = F.map g xj) :
    IsColimit t := by
  -- Strategy: Prove that the map from "the" colimit of F (defined above) to t.X
  -- is a bijection.
  apply IsColimit.ofIsoColimit (colimit.isColimit F)
  refine' Cocones.ext (Equiv.toIso (Equiv.ofBijective _ _)) _
  Â· exact colimit.desc F t
  Â· constructor
    Â· show Function.Injective _
      intro a b h
      rcases jointly_surjective F (colimit.isColimit F) a with âŸ¨i, xi, rflâŸ©
      rcases jointly_surjective F (colimit.isColimit F) b with âŸ¨j, xj, rflâŸ©
      replace h : (colimit.Î¹ F i â‰« colimit.desc F t) xi = (colimit.Î¹ F j â‰« colimit.desc F t) xj := h
      rw [colimit.Î¹_desc, colimit.Î¹_desc] at h
      rcases hinj i j xi xj h with âŸ¨k, f, g, h'âŸ©
      change colimit.Î¹ F i xi = colimit.Î¹ F j xj
      rw [â† colimit.w F f, â† colimit.w F g]
      change colimit.Î¹ F k (F.map f xi) = colimit.Î¹ F k (F.map g xj)
      rw [h']
    Â· show Function.Surjective _
      intro x
      rcases hsurj x with âŸ¨i, xi, rflâŸ©
      use colimit.Î¹ F i xi
      apply Colimit.Î¹_desc_apply
  Â· intro j
    apply colimit.Î¹_desc
#align category_theory.limits.types.filtered_colimit.is_colimit_of CategoryTheory.Limits.Types.FilteredColimit.isColimitOf

variable [IsFilteredOrEmpty J]

protected theorem rel_equiv : _root_.Equivalence (FilteredColimit.Rel.{v, u} F) where
  refl x := âŸ¨x.1, ðŸ™ x.1, ðŸ™ x.1, rflâŸ©
  symm := fun âŸ¨k, f, g, hâŸ© => âŸ¨k, g, f, h.symmâŸ©
  trans {x y z} := fun âŸ¨k, f, g, hâŸ© âŸ¨k', f', g', h'âŸ© =>
    let âŸ¨l, fl, gl, _âŸ© := IsFilteredOrEmpty.cocone_objs k k'
    let âŸ¨m, n, hnâŸ© := IsFilteredOrEmpty.cocone_maps (g â‰« fl) (f' â‰« gl)
    âŸ¨m, f â‰« fl â‰« n, g' â‰« gl â‰« n,
      calc
        F.map (f â‰« fl â‰« n) x.2 = F.map (fl â‰« n) (F.map f x.2) := by simp
        _ = F.map (fl â‰« n) (F.map g y.2) := by rw [h]
        _ = F.map ((g â‰« fl) â‰« n) y.2 := by simp
        _ = F.map ((f' â‰« gl) â‰« n) y.2 := by rw [hn]
        _ = F.map (gl â‰« n) (F.map f' y.2) := by simp
        _ = F.map (gl â‰« n) (F.map g' z.2) := by rw [h']
        _ = F.map (g' â‰« gl â‰« n) z.2 := by simpâŸ©
#align category_theory.limits.types.filtered_colimit.rel_equiv CategoryTheory.Limits.Types.FilteredColimit.rel_equiv

protected theorem rel_eq_eqvGen_quot_rel :
    FilteredColimit.Rel.{v, u} F = EqvGen (Quot.Rel F) := by
  ext âŸ¨j, xâŸ© âŸ¨j', yâŸ©
  constructor
  Â· apply eqvGen_quot_rel_of_rel
  Â· rw [â† (FilteredColimit.rel_equiv F).eqvGen_iff]
    exact EqvGen.mono (rel_of_quot_rel F)
#align category_theory.limits.types.filtered_colimit.rel_eq_eqv_gen_quot_rel CategoryTheory.Limits.Types.FilteredColimit.rel_eq_eqvGen_quot_rel

theorem colimit_eq_iff_aux {i j : J} {xi : F.obj i} {xj : F.obj j} :
    (colimitCocone.{v, u} F).Î¹.app i xi = (colimitCocone.{v, u} F).Î¹.app j xj â†”
      FilteredColimit.Rel.{v, u} F âŸ¨i, xiâŸ© âŸ¨j, xjâŸ© := by
  change Quot.mk _ _ = Quot.mk _ _ â†” _
  rw [Quot.eq, FilteredColimit.rel_eq_eqvGen_quot_rel]
#align category_theory.limits.types.filtered_colimit.colimit_eq_iff_aux CategoryTheory.Limits.Types.FilteredColimit.colimit_eq_iff_aux

theorem isColimit_eq_iff {t : Cocone F} (ht : IsColimit t) {i j : J} {xi : F.obj i} {xj : F.obj j} :
    t.Î¹.app i xi = t.Î¹.app j xj â†” âˆƒ (k : _) (f : i âŸ¶ k) (g : j âŸ¶ k), F.map f xi = F.map g xj := by
  let t' := colimitCocone.{v, u} F
  let e : t' â‰… t := IsColimit.uniqueUpToIso (colimitCoconeIsColimit F) ht
  let e' : t'.pt â‰… t.pt := (Cocones.forget _).mapIso e
  refine' Iff.trans _ (colimit_eq_iff_aux F)
  exact @Equiv.apply_eq_iff_eq _ _ e'.toEquiv ((colimitCocone.{v, u} F).Î¹.app i xi)
    ((colimitCocone.{v, u} F).Î¹.app j xj)
#align category_theory.limits.types.filtered_colimit.is_colimit_eq_iff CategoryTheory.Limits.Types.FilteredColimit.isColimit_eq_iff

theorem colimit_eq_iff {i j : J} {xi : F.obj i} {xj : F.obj j} :
    colimit.Î¹ F i xi = colimit.Î¹ F j xj â†”
      âˆƒ (k : _) (f : i âŸ¶ k) (g : j âŸ¶ k), F.map f xi = F.map g xj :=
  isColimit_eq_iff _ (colimit.isColimit F)
#align category_theory.limits.types.filtered_colimit.colimit_eq_iff CategoryTheory.Limits.Types.FilteredColimit.colimit_eq_iff

end FilteredColimit

variable {Î± Î² : Type u} (f : Î± âŸ¶ Î²)

section

-- implementation of `HasImage`
/-- the image of a morphism in Type is just `Set.range f` -/
def Image : Type u :=
  Set.range f
#align category_theory.limits.types.image CategoryTheory.Limits.Types.Image

instance [Inhabited Î±] : Inhabited (Image f) where default := âŸ¨f default, âŸ¨_, rflâŸ©âŸ©

/-- the inclusion of `Image f` into the target -/
def Image.Î¹ : Image f âŸ¶ Î² :=
  Subtype.val
#align category_theory.limits.types.image.Î¹ CategoryTheory.Limits.Types.Image.Î¹

instance : Mono (Image.Î¹ f) :=
  (mono_iff_injective _).2 Subtype.val_injective

variable {f}

/-- the universal property for the image factorisation -/
noncomputable def Image.lift (F' : MonoFactorisation f) : Image f âŸ¶ F'.I :=
  (fun x => F'.e (Classical.indefiniteDescription _ x.2).1 : Image f â†’ F'.I)
#align category_theory.limits.types.image.lift CategoryTheory.Limits.Types.Image.lift

theorem Image.lift_fac (F' : MonoFactorisation f) : Image.lift F' â‰« F'.m = Image.Î¹ f := by
  funext x
  change (F'.e â‰« F'.m) _ = _
  rw [F'.fac, (Classical.indefiniteDescription _ x.2).2]
  rfl
#align category_theory.limits.types.image.lift_fac CategoryTheory.Limits.Types.Image.lift_fac

end

/-- the factorisation of any morphism in Type through a mono. -/
def monoFactorisation : MonoFactorisation f where
  I := Image f
  m := Image.Î¹ f
  e := Set.rangeFactorization f
#align category_theory.limits.types.mono_factorisation CategoryTheory.Limits.Types.monoFactorisation

/-- the factorisation through a mono has the universal property of the image. -/
noncomputable def isImage : IsImage (monoFactorisation f) where
  lift := Image.lift
  lift_fac := Image.lift_fac
#align category_theory.limits.types.is_image CategoryTheory.Limits.Types.isImage

instance : HasImage f :=
  HasImage.mk âŸ¨_, isImage fâŸ©

instance : HasImages (Type u) where
  has_image := by infer_instance

instance : HasImageMaps (Type u) where
  has_image_map {f g} st :=
    HasImageMap.transport st (monoFactorisation f.hom) (isImage g.hom)
      (fun x => âŸ¨st.right x.1, âŸ¨st.left (Classical.choose x.2), by
        have p := st.w
        replace p := congr_fun p (Classical.choose x.2)
        simp only [Functor.id_obj, Functor.id_map, types_comp_apply] at p
        erw [p, Classical.choose_spec x.2]âŸ©âŸ©) rfl

end Types

open Functor Opposite

variable {J : Type v} [SmallCategory J] {C : Type u} [Category.{v} C]

/-- A cone on `F` with cone point `X` is the same as an element of `lim Hom(X, FÂ·)`. -/
@[simps]
noncomputable def limitCompCoyonedaIsoCone (F : J â¥¤ C) (X : C) :
    limit (F â‹™ coyoneda.obj (op X)) â‰… ((const J).obj X âŸ¶ F) where
  hom := fun x => âŸ¨fun j => limit.Ï€ (F â‹™ coyoneda.obj (op X)) j x,
    fun j j' f => by simpa using (congrFun (limit.w (F â‹™ coyoneda.obj (op X)) f) x).symmâŸ©
  inv := fun Î¹ => Types.Limit.mk _ (fun j => Î¹.app j)
    fun j j' f => by simpa using (Î¹.naturality f).symm

/-- A cone on `F` with cone point `X` is the same as an element of `lim Hom(X, FÂ·)`,
    naturally in `X`. -/
@[simps!]
noncomputable def coyonedaCompLimIsoCones (F : J â¥¤ C) :
    coyoneda â‹™ (whiskeringLeft _ _ _).obj F â‹™ lim â‰… F.cones :=
  NatIso.ofComponents (fun X => limitCompCoyonedaIsoCone F X.unop)

variable (J) (C) in
/-- A cone on `F` with cone point `X` is the same as an element of `lim Hom(X, FÂ·)`,
    naturally in `F` and `X`. -/
@[simps!]
noncomputable def whiskeringLimYonedaIsoCones : whiskeringLeft _ _ _ â‹™
    (whiskeringRight _ _ _).obj lim â‹™ (whiskeringLeft _ _ _).obj coyoneda â‰… cones J C :=
  NatIso.ofComponents coyonedaCompLimIsoCones

/-- A cocone on `F` with cocone point `X` is the same as an element of `lim Hom(FÂ·, X)`. -/
@[simps]
noncomputable def limitCompYonedaIsoCocone (F : J â¥¤ C) (X : C) :
    limit (F.op â‹™ yoneda.obj X) â‰… (F âŸ¶ (const J).obj X) where
  hom := fun x => âŸ¨fun j => limit.Ï€ (F.op â‹™ yoneda.obj X) (Opposite.op j) x,
    fun j j' f => by simpa using congrFun (limit.w (F.op â‹™ yoneda.obj X) f.op) xâŸ©
  inv := fun Î¹ => Types.Limit.mk _ (fun j => Î¹.app j.unop) (by simp)

/-- A cocone on `F` with cocone point `X` is the same as an element of `lim Hom(FÂ·, X)`,
    naturally in `X`. -/
@[simps!]
noncomputable def yonedaCompLimIsoCocones (F : J â¥¤ C) :
    yoneda â‹™ (whiskeringLeft _ _ _).obj F.op â‹™ lim â‰… F.cocones :=
  NatIso.ofComponents (limitCompYonedaIsoCocone F)

variable (J) (C) in
/-- A cocone on `F` with cocone point `X` is the same as an element of `lim Hom(FÂ·, X)`,
    naturally in `F` and `X`. -/
@[simps!]
noncomputable def opHomCompWhiskeringLimYonedaIsoCocones : opHom _ _ â‹™ whiskeringLeft _ _ _ â‹™
      (whiskeringRight _ _ _).obj lim â‹™ (whiskeringLeft _ _ _).obj yoneda â‰… cocones J C :=
  NatIso.ofComponents (fun F => yonedaCompLimIsoCocones F.unop)

end CategoryTheory.Limits
