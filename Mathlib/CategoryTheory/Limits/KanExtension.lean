/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Adam Topaz
-/
import Mathlib.CategoryTheory.Comma.StructuredArrow
import Mathlib.CategoryTheory.Limits.Shapes.Terminal
import Mathlib.CategoryTheory.PUnit

#align_import category_theory.limits.kan_extension from "leanprover-community/mathlib"@"c9c9fa15fec7ca18e9ec97306fb8764bfe988a7e"

/-!

# Kan extensions

This file defines the right and left Kan extensions of a functor.
They exist under the assumption that the target category has enough limits
resp. colimits.

The main definitions are `Ran Î¹` and `Lan Î¹`, where `Î¹ : S â¥¤ L` is a functor.
Namely, `Ran Î¹` is the right Kan extension, while `Lan Î¹` is the left Kan extension,
both as functors `(S â¥¤ D) â¥¤ (L â¥¤ D)`.

To access the right resp. left adjunction associated to these, use `Ran.adjunction`
resp. `Lan.adjunction`.

# Projects

A lot of boilerplate could be generalized by defining and working with pseudofunctors.

-/


noncomputable section

namespace CategoryTheory

open Limits

universe v vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

variable {S : Type uâ‚} {L : Type uâ‚‚} {D : Type uâ‚ƒ}
variable [Category.{vâ‚} S] [Category.{vâ‚‚} L] [Category.{vâ‚ƒ} D]
variable (Î¹ : S â¥¤ L)

namespace Ran

attribute [local simp] StructuredArrow.proj

/-- The diagram indexed by `Ran.index Î¹ x` used to define `Ran`. -/
abbrev diagram (F : S â¥¤ D) (x : L) : StructuredArrow x Î¹ â¥¤ D :=
  StructuredArrow.proj x Î¹ â‹™ F
set_option linter.uppercaseLean3 false in
#align category_theory.Ran.diagram CategoryTheory.Ran.diagram

variable {Î¹}

/-- A cone over `Ran.diagram Î¹ F x` used to define `Ran`. -/
@[simp]
def cone {F : S â¥¤ D} {G : L â¥¤ D} (x : L) (f : Î¹ â‹™ G âŸ¶ F) : Cone (diagram Î¹ F x)
    where
  pt := G.obj x
  Ï€ :=
    { app := fun i => G.map i.hom â‰« f.app i.right
      naturality := by
        rintro âŸ¨âŸ¨ilâŸ©, ir, iâŸ© âŸ¨âŸ¨jlâŸ©, jr, jâŸ© âŸ¨âŸ¨âŸ¨flâŸ©âŸ©, fr, ffâŸ©
        dsimp at *
        dsimp at ff
        simp only [Category.id_comp, Category.assoc] at *
        rw [ff]
        have := f.naturality
        aesop_cat }
set_option linter.uppercaseLean3 false in
#align category_theory.Ran.cone CategoryTheory.Ran.cone

variable (Î¹)

/-- An auxiliary definition used to define `Ran`. -/
@[simps]
def loc (F : S â¥¤ D) [h : âˆ€ x, HasLimit (diagram Î¹ F x)] : L â¥¤ D
    where
  obj x := limit (diagram Î¹ F x)
  map {X Y} f :=
    haveI : HasLimit <| StructuredArrow.map f â‹™ diagram Î¹ F X := h Y
    limit.pre (diagram Î¹ F X) (StructuredArrow.map f)
  map_id := by
    intro l
    haveI : HasLimit (StructuredArrow.map (ðŸ™ _) â‹™ diagram Î¹ F l) := h _
    dsimp
    ext j
    simp only [Category.id_comp, limit.pre_Ï€]
    congr 1
    simp
  map_comp := by
    intro x y z f g
    apply limit.hom_ext
    intro j
    -- Porting note: The fact that we need to add these instances all over the place
    -- is certainly not ideal.
    haveI : HasLimit (StructuredArrow.map f â‹™ diagram Î¹ F _) := h _
    haveI : HasLimit (StructuredArrow.map g â‹™ diagram Î¹ F _) := h _
    haveI : HasLimit (StructuredArrow.map (f â‰« g) â‹™ diagram Î¹ F _) := h _
    haveI : HasLimit (StructuredArrow.map g â‹™ StructuredArrow.map f â‹™ diagram Î¹ F _) := h _
    haveI : HasLimit ((StructuredArrow.map g â‹™ StructuredArrow.map f) â‹™ diagram Î¹ F _) := h _
    erw [limit.pre_pre, limit.pre_Ï€, limit.pre_Ï€]
    congr 1
    aesop_cat
set_option linter.uppercaseLean3 false in
#align category_theory.Ran.loc CategoryTheory.Ran.loc

/-- An auxiliary definition used to define `Ran` and `Ran.adjunction`. -/
@[simps]
def equiv (F : S â¥¤ D) [h : âˆ€ x, HasLimit (diagram Î¹ F x)] (G : L â¥¤ D) :
    (G âŸ¶ loc Î¹ F) â‰ƒ (((whiskeringLeft _ _ _).obj Î¹).obj G âŸ¶ F)
    where
  toFun f :=
    { app := fun x => f.app _ â‰« limit.Ï€ (diagram Î¹ F (Î¹.obj x)) (StructuredArrow.mk (ðŸ™ _))
      naturality := by
        intro x y ff
        dsimp only [whiskeringLeft]
        simp only [Functor.comp_map, NatTrans.naturality_assoc, loc_map, Category.assoc]
        congr 1
        haveI : HasLimit (StructuredArrow.map (Î¹.map ff) â‹™ diagram Î¹ F (Î¹.obj x)) := h _
        erw [limit.pre_Ï€]
        let t : StructuredArrow.mk (ðŸ™ (Î¹.obj x)) âŸ¶
          (StructuredArrow.map (Î¹.map ff)).obj (StructuredArrow.mk (ðŸ™ (Î¹.obj y))) :=
          StructuredArrow.homMk ff ?_
        convert (limit.w (diagram Î¹ F (Î¹.obj x)) t).symm using 1
        simp }
  invFun f :=
    { app := fun x => limit.lift (diagram Î¹ F x) (cone _ f)
      naturality := by
        intro x y ff
        apply limit.hom_ext
        intros j
        haveI : HasLimit (StructuredArrow.map ff â‹™ diagram Î¹ F x) := h _
        erw [limit.lift_pre, limit.lift_Ï€, Category.assoc, limit.lift_Ï€ (cone _ f) j]
        simp }
  left_inv := by
    intro x
    ext k
    apply limit.hom_ext
    intros j
    dsimp only [cone]
    rw [limit.lift_Ï€]
    simp only [NatTrans.naturality_assoc, loc_map]
    haveI : HasLimit (StructuredArrow.map j.hom â‹™ diagram Î¹ F k) := h _
    erw [limit.pre_Ï€]
    congr
    rcases j with âŸ¨âŸ¨âŸ©, _, _âŸ©
    aesop_cat
  right_inv := by aesop_cat
set_option linter.uppercaseLean3 false in
#align category_theory.Ran.equiv CategoryTheory.Ran.equiv

end Ran

/-- The right Kan extension of a functor. -/
@[simps!]
def ran [âˆ€ X, HasLimitsOfShape (StructuredArrow X Î¹) D] : (S â¥¤ D) â¥¤ L â¥¤ D :=
  Adjunction.rightAdjointOfEquiv (fun F G => (Ran.equiv Î¹ G F).symm) (by {
    -- Porting note (#10936): was `tidy`
    intros X' X Y f g
    ext t
    apply limit.hom_ext
    intros j
    dsimp [Ran.equiv]
    simp })
set_option linter.uppercaseLean3 false in
#align category_theory.Ran CategoryTheory.ran

namespace Ran

variable (D)

/-- The adjunction associated to `Ran`. -/
def adjunction [âˆ€ X, HasLimitsOfShape (StructuredArrow X Î¹) D] :
    (whiskeringLeft _ _ D).obj Î¹ âŠ£ ran Î¹ :=
  Adjunction.adjunctionOfEquivRight _ _
set_option linter.uppercaseLean3 false in
#align category_theory.Ran.adjunction CategoryTheory.Ran.adjunction

theorem reflective [Full Î¹] [Faithful Î¹] [âˆ€ X, HasLimitsOfShape (StructuredArrow X Î¹) D] :
    IsIso (adjunction D Î¹).counit := by
  suffices âˆ€ (X : S â¥¤ D), IsIso (NatTrans.app (adjunction D Î¹).counit X) by
    apply NatIso.isIso_of_isIso_app
  intro F
  suffices âˆ€ (X : S), IsIso (NatTrans.app (NatTrans.app (adjunction D Î¹).counit F) X) by
    apply NatIso.isIso_of_isIso_app
  intro X
  dsimp [adjunction, equiv]
  simp only [Category.id_comp]
  exact
    IsIso.of_iso
      ((limit.isLimit _).conePointUniqueUpToIso
        (limitOfDiagramInitial StructuredArrow.mkIdInitial _))
set_option linter.uppercaseLean3 false in
#align category_theory.Ran.reflective CategoryTheory.Ran.reflective

variable {D}

abbrev self (F : L â¥¤ D) [âˆ€ x, HasLimit (Ran.diagram Î¹ (Î¹ â‹™ F) x)] : L â¥¤ D :=
  Ran.loc Î¹ (Î¹ â‹™ F)

end Ran

class IsRightKanExtendedAlong (F : L â¥¤ D) [âˆ€ x, HasLimit (Ran.diagram Î¹ (Î¹ â‹™ F) x)] : Prop where
  isoSelf : Nonempty (F â‰… Ran.self Î¹ F)

namespace Lan

attribute [local simp] CostructuredArrow.proj

/-- The diagram indexed by `Lan.index Î¹ x` used to define `Lan`. -/
abbrev diagram (F : S â¥¤ D) (x : L) : CostructuredArrow Î¹ x â¥¤ D :=
  CostructuredArrow.proj Î¹ x â‹™ F
set_option linter.uppercaseLean3 false in
#align category_theory.Lan.diagram CategoryTheory.Lan.diagram

variable {Î¹}

/-- A cocone over `Lan.diagram Î¹ F x` used to define `Lan`. -/
@[simp]
def cocone {F : S â¥¤ D} {G : L â¥¤ D} (x : L) (f : F âŸ¶ Î¹ â‹™ G) : Cocone (diagram Î¹ F x)
    where
  pt := G.obj x
  Î¹ :=
    { app := fun i => f.app i.left â‰« G.map i.hom
      naturality := by
        rintro âŸ¨ir, âŸ¨ilâŸ©, iâŸ© âŸ¨jl, âŸ¨jrâŸ©, jâŸ© âŸ¨fl, âŸ¨âŸ¨flâŸ©âŸ©, ffâŸ©
        dsimp at *
        simp only [Functor.comp_map, Category.comp_id, NatTrans.naturality_assoc]
        rw [â† G.map_comp, ff]
        aesop_cat }
set_option linter.uppercaseLean3 false in
#align category_theory.Lan.cocone CategoryTheory.Lan.cocone

variable (Î¹)

/-- An auxiliary definition used to define `Lan`. -/
@[simps]
def loc (F : S â¥¤ D) [I : âˆ€ x, HasColimit (diagram Î¹ F x)] : L â¥¤ D
    where
  obj x := colimit (diagram Î¹ F x)
  map {x y} f :=
    haveI : HasColimit (CostructuredArrow.map f â‹™ diagram Î¹ F y) := I _
    colimit.pre (diagram Î¹ F y) (CostructuredArrow.map f)
  map_id := by
    intro l
    dsimp
    haveI : HasColimit (CostructuredArrow.map (ðŸ™ l) â‹™ diagram Î¹ F l) := I _
    ext j
    erw [colimit.Î¹_pre, Category.comp_id]
    congr 1
    simp
  map_comp := by
    intro x y z f g
    dsimp
    haveI : HasColimit (CostructuredArrow.map (f â‰« g) â‹™ diagram Î¹ F z) := I _
    ext j
    let ff : CostructuredArrow Î¹ _ â¥¤ _ := CostructuredArrow.map f
    let gg : CostructuredArrow Î¹ _ â¥¤ _ := CostructuredArrow.map g
    let dd := diagram Î¹ F z
    -- Porting note: It seems that even Lean3 had some trouble with instances in this case.
    -- I don't know why lean can't deduce the following three instances...
    haveI : HasColimit (ff â‹™ gg â‹™ dd) := I _
    haveI : HasColimit ((ff â‹™ gg) â‹™ dd) := I _
    haveI : HasColimit (gg â‹™ dd) := I _
    change _ = colimit.Î¹ ((ff â‹™ gg) â‹™ dd) j â‰« _ â‰« _
    erw [colimit.pre_pre dd gg ff, colimit.Î¹_pre, colimit.Î¹_pre]
    congr 1
    simp
set_option linter.uppercaseLean3 false in
#align category_theory.Lan.loc CategoryTheory.Lan.loc

/-- An auxiliary definition used to define `Lan` and `Lan.adjunction`. -/
@[simps]
def equiv (F : S â¥¤ D) [I : âˆ€ x, HasColimit (diagram Î¹ F x)] (G : L â¥¤ D) :
    (loc Î¹ F âŸ¶ G) â‰ƒ (F âŸ¶ ((whiskeringLeft _ _ _).obj Î¹).obj G)
    where
  toFun f :=
    { app := fun x => colimit.Î¹ (diagram Î¹ F (Î¹.obj x)) (CostructuredArrow.mk (ðŸ™ _)) â‰« f.app _
      naturality := by
        intro x y ff
        dsimp only [whiskeringLeft]
        simp only [Functor.comp_map, Category.assoc]
        rw [â† f.naturality (Î¹.map ff), â† Category.assoc, â† Category.assoc]
        let fff : CostructuredArrow Î¹ _ â¥¤ _ := CostructuredArrow.map (Î¹.map ff)
        -- same issue :-(
        haveI : HasColimit (fff â‹™ diagram Î¹ F (Î¹.obj y)) := I _
        erw [colimit.Î¹_pre (diagram Î¹ F (Î¹.obj y)) fff (CostructuredArrow.mk (ðŸ™ _))]
        let xx : CostructuredArrow Î¹ (Î¹.obj y) := CostructuredArrow.mk (Î¹.map ff)
        let yy : CostructuredArrow Î¹ (Î¹.obj y) := CostructuredArrow.mk (ðŸ™ _)
        let fff' : xx âŸ¶ yy :=
          CostructuredArrow.homMk ff
            (by
              simp only [xx, CostructuredArrow.mk_hom_eq_self]
              erw [Category.comp_id])
        erw [colimit.w (diagram Î¹ F (Î¹.obj y)) fff']
        congr
        simp [fff] }
  invFun f :=
    { app := fun x => colimit.desc (diagram Î¹ F x) (cocone _ f)
      naturality := by
        intro x y ff
        apply colimit.hom_ext
        intros j
        haveI : HasColimit (CostructuredArrow.map ff â‹™ diagram Î¹ F y) := I _
        erw [colimit.pre_desc, â† Category.assoc, colimit.Î¹_desc, colimit.Î¹_desc]
        simp }
  left_inv := by
    intros x
    dsimp
    ext k
    dsimp
    apply colimit.hom_ext
    intros j
    rw [colimit.Î¹_desc]
    dsimp only [cocone]
    rw [Category.assoc, â† x.naturality j.hom, â† Category.assoc]
    congr 1
    dsimp [loc]
    haveI : HasColimit (CostructuredArrow.map j.hom â‹™ diagram Î¹ F k) := I _
    erw [colimit.Î¹_pre (diagram Î¹ F k) (CostructuredArrow.map j.hom)]
    congr
    rcases j with âŸ¨_, âŸ¨âŸ©, _âŸ©
    simp only [CostructuredArrow.map_mk, Category.id_comp]
    rfl
  right_inv := by aesop_cat
set_option linter.uppercaseLean3 false in
#align category_theory.Lan.equiv CategoryTheory.Lan.equiv

-- These lemmas have always been bad (#7657), but leanprover/lean4#2644 made `simp` start noticing
attribute [nolint simpNF] CategoryTheory.Ran.equiv_symm_apply_app
  CategoryTheory.Ran.equiv_apply_app
  CategoryTheory.Lan.equiv_symm_apply_app
  CategoryTheory.Lan.equiv_apply_app

end Lan

/-- The left Kan extension of a functor. -/
@[simps!]
def lan [âˆ€ F : S â¥¤ D, âˆ€ x, HasColimit (Lan.diagram Î¹ F x)] : (S â¥¤ D) â¥¤ L â¥¤ D :=
  Adjunction.leftAdjointOfEquiv (fun F G => Lan.equiv Î¹ F G) (by {
    intros X' X Y f g
    ext
    simp [Lan.equiv]
    -- This used to be the end of the proof before leanprover/lean4#2644
    erw [Equiv.coe_fn_mk, Equiv.coe_fn_mk]
    simp })
set_option linter.uppercaseLean3 false in
#align category_theory.Lan CategoryTheory.lan

namespace Lan

variable (D)

/-- The adjunction associated to `Lan`. -/
def adjunction [âˆ€ F : S â¥¤ D, âˆ€ x, HasColimit (Lan.diagram Î¹ F x)] :
    lan Î¹ âŠ£ (whiskeringLeft _ _ D).obj Î¹ :=
  Adjunction.adjunctionOfEquivLeft _ _
set_option linter.uppercaseLean3 false in
#align category_theory.Lan.adjunction CategoryTheory.Lan.adjunction

theorem coreflective [Full Î¹] [Faithful Î¹] [âˆ€ F : S â¥¤ D, âˆ€ x, HasColimit (Lan.diagram Î¹ F x)] :
    IsIso (adjunction D Î¹).unit := by
  suffices âˆ€ (X : S â¥¤ D), IsIso (NatTrans.app (adjunction D Î¹).unit X) by
    apply NatIso.isIso_of_isIso_app
  intro F
  suffices âˆ€ (X : S), IsIso (NatTrans.app (NatTrans.app (adjunction D Î¹).unit F) X) by
    apply NatIso.isIso_of_isIso_app
  intro X
  dsimp [adjunction, equiv]
  simp only [Category.comp_id]
  exact
    IsIso.of_iso
      ((colimit.isColimit _).coconePointUniqueUpToIso
          (colimitOfDiagramTerminal CostructuredArrow.mkIdTerminal _)).symm
set_option linter.uppercaseLean3 false in
#align category_theory.Lan.coreflective CategoryTheory.Lan.coreflective

variable {D}

abbrev self (F : L â¥¤ D) [âˆ€ x, HasColimit (Lan.diagram Î¹ (Î¹ â‹™ F) x)] : L â¥¤ D :=
  Lan.loc Î¹ (Î¹ â‹™ F)

end Lan

class IsLeftKanExtendedAlong (F : L â¥¤ D) [âˆ€ x, HasColimit (Lan.diagram Î¹ (Î¹ â‹™ F) x)] : Prop where
  isoSelf : Nonempty (F â‰… Lan.self Î¹ F)


end CategoryTheory
