/-
Copyright (c) 2017 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stephen Morgan, Kim Morrison, Floris van Doorn
-/
import Mathlib.CategoryTheory.Functor.Const
import Mathlib.CategoryTheory.Discrete.Basic
import Mathlib.CategoryTheory.Yoneda
import Mathlib.CategoryTheory.Functor.ReflectsIso.Basic

/-!
# Cones and cocones

We define `Cone F`, a cone over a functor `F`,
and `F.cones : Cáµ’áµ– â¥¤ Type`, the functor associating to `X` the cones over `F` with cone point `X`.

A cone `c` is defined by specifying its cone point `c.pt` and a natural transformation `c.Ï€`
from the constant `c.pt`-valued functor to `F`.

We provide `c.w f : c.Ï€.app j â‰« F.map f = c.Ï€.app j'` for any `f : j âŸ¶ j'`
as a wrapper for `c.Ï€.naturality f` avoiding unneeded identity morphisms.

We define `c.extend f`, where `c : cone F` and `f : Y âŸ¶ c.pt` for some other `Y`,
which replaces the cone point by `Y` and inserts `f` into each of the components of the cone.
Similarly we have `c.whisker F` producing a `Cone (E â‹™ F)`

We define morphisms of cones, and the category of cones.

We define `Cone.postcompose Î± : cone F â¥¤ cone G` for `Î±` a natural transformation `F âŸ¶ G`.

And, of course, we dualise all this to cocones as well.

For more results about the category of cones, see `cone_category.lean`.
-/


-- morphism levels before object levels. See note [category theory universes].
universe vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ vâ‚… uâ‚ uâ‚‚ uâ‚ƒ uâ‚„ uâ‚…

open CategoryTheory

variable {J : Type uâ‚} [Category.{vâ‚} J]
variable {K : Type uâ‚‚} [Category.{vâ‚‚} K]
variable {C : Type uâ‚ƒ} [Category.{vâ‚ƒ} C]
variable {D : Type uâ‚„} [Category.{vâ‚„} D]
variable {E : Type uâ‚…} [Category.{vâ‚…} E]

open CategoryTheory

open CategoryTheory.Category

open CategoryTheory.Functor

open Opposite

namespace CategoryTheory

namespace Functor

variable (F : J â¥¤ C)

/-- If `F : J â¥¤ C` then `F.cones` is the functor assigning to an object `X : C` the
type of natural transformations from the constant functor with value `X` to `F`.
An object representing this functor is a limit of `F`.
-/
@[simps!]
def cones : Cáµ’áµ– â¥¤ Type max uâ‚ vâ‚ƒ :=
  (const J).op â‹™ yoneda.obj F

/-- If `F : J â¥¤ C` then `F.cocones` is the functor assigning to an object `(X : C)`
the type of natural transformations from `F` to the constant functor with value `X`.
An object corepresenting this functor is a colimit of `F`.
-/
@[simps!]
def cocones : C â¥¤ Type max uâ‚ vâ‚ƒ :=
  const J â‹™ coyoneda.obj (op F)

end Functor

section

variable (J C)

/-- Functorially associated to each functor `J â¥¤ C`, we have the `C`-presheaf consisting of
cones with a given cone point.
-/
@[simps!]
def cones : (J â¥¤ C) â¥¤ Cáµ’áµ– â¥¤ Type max uâ‚ vâ‚ƒ where
  obj := Functor.cones
  map f := whiskerLeft (const J).op (yoneda.map f)

/-- Contravariantly associated to each functor `J â¥¤ C`, we have the `C`-copresheaf consisting of
cocones with a given cocone point.
-/
@[simps!]
def cocones : (J â¥¤ C)áµ’áµ– â¥¤ C â¥¤ Type max uâ‚ vâ‚ƒ where
  obj F := Functor.cocones (unop F)
  map f := whiskerLeft (const J) (coyoneda.map f)

end

namespace Limits

section

/-- A `c : Cone F` is:
* an object `c.pt` and
* a natural transformation `c.Ï€ : c.pt âŸ¶ F` from the constant `c.pt` functor to `F`.

Example: if `J` is a category coming from a poset then the data required to make
a term of type `Cone F` is morphisms `Ï€â±¼ : c.pt âŸ¶ F j` for all `j : J` and,
for all `i â‰¤ j` in `J`, morphisms `Ï€áµ¢â±¼ : F i âŸ¶ F j` such that `Ï€áµ¢ â‰« Ï€áµ¢â±¼ = Ï€â±¼`.

`Cone F` is equivalent, via `cone.equiv` below, to `Î£ X, F.cones.obj X`.
-/
structure Cone (F : J â¥¤ C) where
  /-- An object of `C` -/
  pt : C
  /-- A natural transformation from the constant functor at `X` to `F` -/
  Ï€ : (const J).obj pt âŸ¶ F

instance inhabitedCone (F : Discrete PUnit â¥¤ C) : Inhabited (Cone F) :=
  âŸ¨{  pt := F.obj âŸ¨âŸ¨âŸ©âŸ©
      Ï€ := { app := fun âŸ¨âŸ¨âŸ©âŸ© => ðŸ™ _
             naturality := by
              intro X Y f
              match X, Y, f with
              | .mk A, .mk B, .up g =>
                cat_disch
           }
  }âŸ©

@[reassoc (attr := simp)]
theorem Cone.w {F : J â¥¤ C} (c : Cone F) {j j' : J} (f : j âŸ¶ j') :
    c.Ï€.app j â‰« F.map f = c.Ï€.app j' := by
  rw [â† c.Ï€.naturality f]
  apply id_comp

/-- A `c : Cocone F` is
* an object `c.pt` and
* a natural transformation `c.Î¹ : F âŸ¶ c.pt` from `F` to the constant `c.pt` functor.

For example, if the source `J` of `F` is a partially ordered set, then to give
`c : Cocone F` is to give a collection of morphisms `Î¹â±¼ : F j âŸ¶ c.pt` and, for
all `j â‰¤ k` in `J`, morphisms `Î¹â±¼â‚– : F j âŸ¶ F k` such that `Fâ±¼â‚– â‰« Fâ‚– = Fâ±¼` for all `j â‰¤ k`.

`Cocone F` is equivalent, via `Cone.equiv` below, to `Î£ X, F.cocones.obj X`.
-/
structure Cocone (F : J â¥¤ C) where
  /-- An object of `C` -/
  pt : C
  /-- A natural transformation from `F` to the constant functor at `pt` -/
  Î¹ : F âŸ¶ (const J).obj pt

instance inhabitedCocone (F : Discrete PUnit â¥¤ C) : Inhabited (Cocone F) :=
  âŸ¨{  pt := F.obj âŸ¨âŸ¨âŸ©âŸ©
      Î¹ := { app := fun âŸ¨âŸ¨âŸ©âŸ© => ðŸ™ _
             naturality := by
              intro X Y f
              match X, Y, f with
              | .mk A, .mk B, .up g =>
                simp
           }
  }âŸ©

@[reassoc]
theorem Cocone.w {F : J â¥¤ C} (c : Cocone F) {j j' : J} (f : j âŸ¶ j') :
    F.map f â‰« c.Î¹.app j' = c.Î¹.app j := by
  rw [c.Î¹.naturality f]
  apply comp_id

end

variable {F : J â¥¤ C}

namespace Cone

/-- The isomorphism between a cone on `F` and an element of the functor `F.cones`. -/
@[simps!]
def equiv (F : J â¥¤ C) : Cone F â‰… Î£ X, F.cones.obj X where
  hom c := âŸ¨op c.pt, c.Ï€âŸ©
  inv c :=
    { pt := c.1.unop
      Ï€ := c.2 }
  hom_inv_id := by
    funext X
    cases X
    rfl
  inv_hom_id := by
    funext X
    cases X
    rfl

/-- A map to the vertex of a cone naturally induces a cone by composition. -/
@[simps]
def extensions (c : Cone F) : yoneda.obj c.pt â‹™ uliftFunctor.{uâ‚} âŸ¶ F.cones where
  app _ f := (const J).map f.down â‰« c.Ï€

/-- A map to the vertex of a cone induces a cone by composition. -/
@[simps]
def extend (c : Cone F) {X : C} (f : X âŸ¶ c.pt) : Cone F :=
  { pt := X
    Ï€ := c.extensions.app (op X) âŸ¨fâŸ© }

/-- Whisker a cone by precomposition of a functor. -/
@[simps]
def whisker (E : K â¥¤ J) (c : Cone F) : Cone (E â‹™ F) where
  pt := c.pt
  Ï€ := whiskerLeft E c.Ï€

end Cone

namespace Cocone

/-- The isomorphism between a cocone on `F` and an element of the functor `F.cocones`. -/
def equiv (F : J â¥¤ C) : Cocone F â‰… Î£ X, F.cocones.obj X where
  hom c := âŸ¨c.pt, c.Î¹âŸ©
  inv c :=
    { pt := c.1
      Î¹ := c.2 }
  hom_inv_id := by
    funext X
    cases X
    rfl
  inv_hom_id := by
    funext X
    cases X
    rfl

/-- A map from the vertex of a cocone naturally induces a cocone by composition. -/
@[simps]
def extensions (c : Cocone F) : coyoneda.obj (op c.pt) â‹™ uliftFunctor.{uâ‚} âŸ¶ F.cocones where
  app _ f := c.Î¹ â‰« (const J).map f.down

/-- A map from the vertex of a cocone induces a cocone by composition. -/
@[simps]
def extend (c : Cocone F) {Y : C} (f : c.pt âŸ¶ Y) : Cocone F where
  pt := Y
  Î¹ := c.extensions.app Y âŸ¨fâŸ©

/-- Whisker a cocone by precomposition of a functor. See `whiskering` for a functorial
version.
-/
@[simps]
def whisker (E : K â¥¤ J) (c : Cocone F) : Cocone (E â‹™ F) where
  pt := c.pt
  Î¹ := whiskerLeft E c.Î¹

end Cocone

/-- A cone morphism between two cones for the same diagram is a morphism of the cone points which
commutes with the cone legs. -/
structure ConeMorphism (A B : Cone F) where
  /-- A morphism between the two vertex objects of the cones -/
  hom : A.pt âŸ¶ B.pt
  /-- The triangle consisting of the two natural transformations and `hom` commutes -/
  w : âˆ€ j : J, hom â‰« B.Ï€.app j = A.Ï€.app j := by cat_disch

attribute [reassoc (attr := simp)] ConeMorphism.w

instance inhabitedConeMorphism (A : Cone F) : Inhabited (ConeMorphism A A) :=
  âŸ¨{ hom := ðŸ™ _ }âŸ©

/-- The category of cones on a given diagram. -/
@[simps]
instance Cone.category : Category (Cone F) where
  Hom A B := ConeMorphism A B
  comp f g := { hom := f.hom â‰« g.hom }
  id B := { hom := ðŸ™ B.pt }

/- We do not want `simps` automatically generate the lemma for simplifying the
hom field of a category. So we need to write the `ext` lemma in terms of the
categorical morphism, rather than the underlying structure. -/
@[ext]
theorem ConeMorphism.ext {c c' : Cone F} (f g : c âŸ¶ c') (w : f.hom = g.hom) : f = g := by
  cases f
  cases g
  congr

@[reassoc (attr := simp)]
lemma ConeMorphism.hom_inv_id {c d : Cone F} (f : c â‰… d) : f.hom.hom â‰« f.inv.hom = ðŸ™ _ := by
  simp [â† Cone.category_comp_hom]

@[reassoc (attr := simp)]
lemma ConeMorphism.inv_hom_id {c d : Cone F} (f : c â‰… d) : f.inv.hom â‰« f.hom.hom = ðŸ™ _ := by
  simp [â† Cone.category_comp_hom]

instance {c d : Cone F} (f : c â‰… d) : IsIso f.hom.hom := âŸ¨f.inv.hom, by simpâŸ©

instance {c d : Cone F} (f : c â‰… d) : IsIso f.inv.hom := âŸ¨f.hom.hom, by simpâŸ©

@[reassoc (attr := simp)]
lemma ConeMorphism.map_w {c c' : Cone F} (f : c âŸ¶ c') (G : C â¥¤ D) (j : J) :
    G.map f.hom â‰« G.map (c'.Ï€.app j) = G.map (c.Ï€.app j) := by
  simp only [â† map_comp, ConeMorphism.w]

namespace Cones

/-- To give an isomorphism between cones, it suffices to give an
  isomorphism between their vertices which commutes with the cone
  maps. -/
@[aesop apply safe (rule_sets := [CategoryTheory]), simps]
def ext {c c' : Cone F} (Ï† : c.pt â‰… c'.pt)
    (w : âˆ€ j, c.Ï€.app j = Ï†.hom â‰« c'.Ï€.app j := by cat_disch) : c â‰… c' where
  hom := { hom := Ï†.hom }
  inv :=
    { hom := Ï†.inv
      w := fun j => Ï†.inv_comp_eq.mpr (w j) }

/-- Eta rule for cones. -/
@[simps!]
def eta (c : Cone F) : c â‰… âŸ¨c.pt, c.Ï€âŸ© :=
  Cones.ext (Iso.refl _)

/-- Given a cone morphism whose object part is an isomorphism, produce an
isomorphism of cones.
-/
theorem cone_iso_of_hom_iso {K : J â¥¤ C} {c d : Cone K} (f : c âŸ¶ d) [i : IsIso f.hom] : IsIso f :=
  âŸ¨âŸ¨{   hom := inv f.hom
        w := fun j => (asIso f.hom).inv_comp_eq.2 (f.w j).symm }, by cat_dischâŸ©âŸ©

/-- There is a morphism from an extended cone to the original cone. -/
@[simps]
def extend (s : Cone F) {X : C} (f : X âŸ¶ s.pt) : s.extend f âŸ¶ s where
  hom := f

/-- Extending a cone by the identity does nothing. -/
@[simps!]
def extendId (s : Cone F) : s.extend (ðŸ™ s.pt) â‰… s :=
  Cones.ext (Iso.refl _)

/-- Extending a cone by a composition is the same as extending the cone twice. -/
@[simps!]
def extendComp (s : Cone F) {X Y : C} (f : X âŸ¶ Y) (g : Y âŸ¶ s.pt) :
    s.extend (f â‰« g) â‰… (s.extend g).extend f :=
  Cones.ext (Iso.refl _)

/-- A cone extended by an isomorphism is isomorphic to the original cone. -/
@[simps]
def extendIso (s : Cone F) {X : C} (f : X â‰… s.pt) : s.extend f.hom â‰… s where
  hom := { hom := f.hom }
  inv := { hom := f.inv }

instance {s : Cone F} {X : C} (f : X âŸ¶ s.pt) [IsIso f] : IsIso (Cones.extend s f) :=
  âŸ¨(extendIso s (asIso f)).inv, by cat_dischâŸ©

/--
Functorially postcompose a cone for `F` by a natural transformation `F âŸ¶ G` to give a cone for `G`.
-/
@[simps]
def postcompose {G : J â¥¤ C} (Î± : F âŸ¶ G) : Cone F â¥¤ Cone G where
  obj c :=
    { pt := c.pt
      Ï€ := c.Ï€ â‰« Î± }
  map f := { hom := f.hom }

/-- Postcomposing a cone by the composite natural transformation `Î± â‰« Î²` is the same as
postcomposing by `Î±` and then by `Î²`. -/
@[simps!]
def postcomposeComp {G H : J â¥¤ C} (Î± : F âŸ¶ G) (Î² : G âŸ¶ H) :
    postcompose (Î± â‰« Î²) â‰… postcompose Î± â‹™ postcompose Î² :=
  NatIso.ofComponents fun s => Cones.ext (Iso.refl _)

/-- Postcomposing by the identity does not change the cone up to isomorphism. -/
@[simps!]
def postcomposeId : postcompose (ðŸ™ F) â‰… ðŸ­ (Cone F) :=
  NatIso.ofComponents fun s => Cones.ext (Iso.refl _)

/-- If `F` and `G` are naturally isomorphic functors, then they have equivalent categories of
cones.
-/
@[simps]
def postcomposeEquivalence {G : J â¥¤ C} (Î± : F â‰… G) : Cone F â‰Œ Cone G where
  functor := postcompose Î±.hom
  inverse := postcompose Î±.inv
  unitIso := NatIso.ofComponents fun s => Cones.ext (Iso.refl _)
  counitIso := NatIso.ofComponents fun s => Cones.ext (Iso.refl _)

/-- Whiskering on the left by `E : K â¥¤ J` gives a functor from `Cone F` to `Cone (E â‹™ F)`.
-/
@[simps]
def whiskering (E : K â¥¤ J) : Cone F â¥¤ Cone (E â‹™ F) where
  obj c := c.whisker E
  map f := { hom := f.hom }

/-- Whiskering by an equivalence gives an equivalence between categories of cones.
-/
@[simps]
def whiskeringEquivalence (e : K â‰Œ J) : Cone F â‰Œ Cone (e.functor â‹™ F) where
  functor := whiskering e.functor
  inverse := whiskering e.inverse â‹™ postcompose (e.invFunIdAssoc F).hom
  unitIso := NatIso.ofComponents fun s => Cones.ext (Iso.refl _)
  counitIso :=
    NatIso.ofComponents
      fun s =>
        Cones.ext (Iso.refl _)
          (by
            intro k
            simpa [e.counit_app_functor] using s.w (e.unitInv.app k))

/-- The categories of cones over `F` and `G` are equivalent if `F` and `G` are naturally isomorphic
(possibly after changing the indexing category by an equivalence).
-/
@[simps! functor inverse unitIso counitIso]
def equivalenceOfReindexing {G : K â¥¤ C} (e : K â‰Œ J) (Î± : e.functor â‹™ F â‰… G) : Cone F â‰Œ Cone G :=
  (whiskeringEquivalence e).trans (postcomposeEquivalence Î±)

section

variable (F)

/-- Forget the cone structure and obtain just the cone point. -/
@[simps]
def forget : Cone F â¥¤ C where
  obj t := t.pt
  map f := f.hom

variable (G : C â¥¤ D)

/-- A functor `G : C â¥¤ D` sends cones over `F` to cones over `F â‹™ G` functorially. -/
@[simps]
def functoriality : Cone F â¥¤ Cone (F â‹™ G) where
  obj A :=
    { pt := G.obj A.pt
      Ï€ :=
        { app := fun j => G.map (A.Ï€.app j)
          naturality := by intros; erw [â† G.map_comp]; simp } }
  map f :=
    { hom := G.map f.hom
      w := ConeMorphism.map_w f G }

/-- Functoriality is functorial. -/
def functorialityCompFunctoriality (H : D â¥¤ E) :
    functoriality F G â‹™ functoriality (F â‹™ G) H â‰… functoriality F (G â‹™ H) :=
  NatIso.ofComponents (fun _ â†¦ Iso.refl _) (by simp [functoriality])

instance functoriality_full [G.Full] [G.Faithful] : (functoriality F G).Full where
  map_surjective t :=
    âŸ¨{ hom := G.preimage t.hom
       w := fun j => G.map_injective (by simpa using t.w j) }, by cat_dischâŸ©

instance functoriality_faithful [G.Faithful] : (Cones.functoriality F G).Faithful where
  map_injective {_X} {_Y} f g h :=
    ConeMorphism.ext f g <| G.map_injective <| congr_arg ConeMorphism.hom h

/-- If `e : C â‰Œ D` is an equivalence of categories, then `functoriality F e.functor` induces an
equivalence between cones over `F` and cones over `F â‹™ e.functor`.
-/
@[simps]
def functorialityEquivalence (e : C â‰Œ D) : Cone F â‰Œ Cone (F â‹™ e.functor) :=
  let f : (F â‹™ e.functor) â‹™ e.inverse â‰… F :=
    Functor.associator _ _ _ â‰ªâ‰« isoWhiskerLeft _ e.unitIso.symm â‰ªâ‰« Functor.rightUnitor _
  { functor := functoriality F e.functor
    inverse := functoriality (F â‹™ e.functor) e.inverse â‹™ (postcomposeEquivalence f).functor
    unitIso := NatIso.ofComponents fun c => Cones.ext (e.unitIso.app _)
    counitIso := NatIso.ofComponents fun c => Cones.ext (e.counitIso.app _) }

/-- If `F` reflects isomorphisms, then `Cones.functoriality F` reflects isomorphisms
as well.
-/
instance reflects_cone_isomorphism (F : C â¥¤ D) [F.ReflectsIsomorphisms] (K : J â¥¤ C) :
    (Cones.functoriality K F).ReflectsIsomorphisms := by
  constructor
  intro A B f _
  haveI : IsIso (F.map f.hom) :=
    (Cones.forget (K â‹™ F)).map_isIso ((Cones.functoriality K F).map f)
  haveI := ReflectsIsomorphisms.reflects F f.hom
  apply cone_iso_of_hom_iso

end

end Cones

/-- A cocone morphism between two cocones for the same diagram is a morphism of the cocone points
which commutes with the cocone legs. -/
structure CoconeMorphism (A B : Cocone F) where
  /-- A morphism between the (co)vertex objects in `C` -/
  hom : A.pt âŸ¶ B.pt
  /-- The triangle made from the two natural transformations and `hom` commutes -/
  w : âˆ€ j : J, A.Î¹.app j â‰« hom = B.Î¹.app j := by cat_disch

instance inhabitedCoconeMorphism (A : Cocone F) : Inhabited (CoconeMorphism A A) :=
  âŸ¨{ hom := ðŸ™ _ }âŸ©

attribute [reassoc (attr := simp)] CoconeMorphism.w

@[simps]
instance Cocone.category : Category (Cocone F) where
  Hom A B := CoconeMorphism A B
  comp f g := { hom := f.hom â‰« g.hom }
  id B := { hom := ðŸ™ B.pt }

/- We do not want `simps` automatically generate the lemma for simplifying the
hom field of a category. So we need to write the `ext` lemma in terms of the
categorical morphism, rather than the underlying structure. -/
@[ext]
theorem CoconeMorphism.ext {c c' : Cocone F} (f g : c âŸ¶ c') (w : f.hom = g.hom) : f = g := by
  cases f
  cases g
  congr

@[reassoc (attr := simp)]
lemma CoconeMorphism.hom_inv_id {c d : Cocone F} (f : c â‰… d) : f.hom.hom â‰« f.inv.hom = ðŸ™ _ := by
  simp [â† Cocone.category_comp_hom]

@[reassoc (attr := simp)]
lemma CoconeMorphism.inv_hom_id {c d : Cocone F} (f : c â‰… d) : f.inv.hom â‰« f.hom.hom = ðŸ™ _ := by
  simp [â† Cocone.category_comp_hom]

instance {c d : Cocone F} (f : c â‰… d) : IsIso f.hom.hom := âŸ¨f.inv.hom, by simpâŸ©

instance {c d : Cocone F} (f : c â‰… d) : IsIso f.inv.hom := âŸ¨f.hom.hom, by simpâŸ©

@[reassoc (attr := simp)]
lemma CoconeMorphism.map_w {c c' : Cocone F} (f : c âŸ¶ c') (G : C â¥¤ D) (j : J) :
    G.map (c.Î¹.app j) â‰« G.map f.hom = G.map (c'.Î¹.app j) := by
  simp only [â† map_comp, CoconeMorphism.w]

namespace Cocones

/-- To give an isomorphism between cocones, it suffices to give an
  isomorphism between their vertices which commutes with the cocone
  maps. -/
@[aesop apply safe (rule_sets := [CategoryTheory]), simps]
def ext {c c' : Cocone F} (Ï† : c.pt â‰… c'.pt)
    (w : âˆ€ j, c.Î¹.app j â‰« Ï†.hom = c'.Î¹.app j := by cat_disch) : c â‰… c' where
  hom := { hom := Ï†.hom }
  inv :=
    { hom := Ï†.inv
      w := fun j => Ï†.comp_inv_eq.mpr (w j).symm }

/-- Eta rule for cocones. -/
@[simps!]
def eta (c : Cocone F) : c â‰… âŸ¨c.pt, c.Î¹âŸ© :=
  Cocones.ext (Iso.refl _)

/-- Given a cocone morphism whose object part is an isomorphism, produce an
isomorphism of cocones.
-/
theorem cocone_iso_of_hom_iso {K : J â¥¤ C} {c d : Cocone K} (f : c âŸ¶ d) [i : IsIso f.hom] :
    IsIso f :=
  âŸ¨âŸ¨{ hom := inv f.hom
      w := fun j => (asIso f.hom).comp_inv_eq.2 (f.w j).symm }, by cat_dischâŸ©âŸ©

/-- There is a morphism from a cocone to its extension. -/
@[simps]
def extend (s : Cocone F) {X : C} (f : s.pt âŸ¶ X) : s âŸ¶ s.extend f where
  hom := f

/-- Extending a cocone by the identity does nothing. -/
@[simps!]
def extendId (s : Cocone F) : s â‰… s.extend (ðŸ™ s.pt) :=
  Cocones.ext (Iso.refl _)

/-- Extending a cocone by a composition is the same as extending the cone twice. -/
@[simps!]
def extendComp (s : Cocone F) {X Y : C} (f : s.pt âŸ¶ X) (g : X âŸ¶ Y) :
    s.extend (f â‰« g) â‰… (s.extend f).extend g :=
  Cocones.ext (Iso.refl _)

/-- A cocone extended by an isomorphism is isomorphic to the original cone. -/
@[simps]
def extendIso (s : Cocone F) {X : C} (f : s.pt â‰… X) : s â‰… s.extend f.hom where
  hom := { hom := f.hom }
  inv := { hom := f.inv }

instance {s : Cocone F} {X : C} (f : s.pt âŸ¶ X) [IsIso f] : IsIso (Cocones.extend s f) :=
  âŸ¨(extendIso s (asIso f)).inv, by cat_dischâŸ©

/-- Functorially precompose a cocone for `F` by a natural transformation `G âŸ¶ F` to give a cocone
for `G`. -/
@[simps]
def precompose {G : J â¥¤ C} (Î± : G âŸ¶ F) : Cocone F â¥¤ Cocone G where
  obj c :=
    { pt := c.pt
      Î¹ := Î± â‰« c.Î¹ }
  map f := { hom := f.hom }

/-- Precomposing a cocone by the composite natural transformation `Î± â‰« Î²` is the same as
precomposing by `Î²` and then by `Î±`. -/
def precomposeComp {G H : J â¥¤ C} (Î± : F âŸ¶ G) (Î² : G âŸ¶ H) :
    precompose (Î± â‰« Î²) â‰… precompose Î² â‹™ precompose Î± :=
  NatIso.ofComponents fun s => Cocones.ext (Iso.refl _)

/-- Precomposing by the identity does not change the cocone up to isomorphism. -/
def precomposeId : precompose (ðŸ™ F) â‰… ðŸ­ (Cocone F) :=
  NatIso.ofComponents fun s => Cocones.ext (Iso.refl _)

/-- If `F` and `G` are naturally isomorphic functors, then they have equivalent categories of
cocones.
-/
@[simps]
def precomposeEquivalence {G : J â¥¤ C} (Î± : G â‰… F) : Cocone F â‰Œ Cocone G where
  functor := precompose Î±.hom
  inverse := precompose Î±.inv
  unitIso := NatIso.ofComponents fun s => Cocones.ext (Iso.refl _)
  counitIso := NatIso.ofComponents fun s => Cocones.ext (Iso.refl _)

/-- Whiskering on the left by `E : K â¥¤ J` gives a functor from `Cocone F` to `Cocone (E â‹™ F)`.
-/
@[simps]
def whiskering (E : K â¥¤ J) : Cocone F â¥¤ Cocone (E â‹™ F) where
  obj c := c.whisker E
  map f := { hom := f.hom }

/-- Whiskering by an equivalence gives an equivalence between categories of cones.
-/
@[simps]
def whiskeringEquivalence (e : K â‰Œ J) : Cocone F â‰Œ Cocone (e.functor â‹™ F) where
  functor := whiskering e.functor
  inverse :=
    whiskering e.inverse â‹™
      precompose
        ((Functor.leftUnitor F).inv â‰«
          whiskerRight e.counitIso.inv F â‰« (Functor.associator _ _ _).inv)
  unitIso := NatIso.ofComponents fun s => Cocones.ext (Iso.refl _)
  counitIso := NatIso.ofComponents fun s =>
    Cocones.ext (Iso.refl _) fun k => by simpa [e.counitInv_app_functor k] using s.w (e.unit.app k)

/--
The categories of cocones over `F` and `G` are equivalent if `F` and `G` are naturally isomorphic
(possibly after changing the indexing category by an equivalence).
-/
@[simps! functor_obj]
def equivalenceOfReindexing {G : K â¥¤ C} (e : K â‰Œ J) (Î± : e.functor â‹™ F â‰… G) : Cocone F â‰Œ Cocone G :=
  (whiskeringEquivalence e).trans (precomposeEquivalence Î±.symm)

section

variable (F)

/-- Forget the cocone structure and obtain just the cocone point. -/
@[simps]
def forget : Cocone F â¥¤ C where
  obj t := t.pt
  map f := f.hom

variable (G : C â¥¤ D)

/-- A functor `G : C â¥¤ D` sends cocones over `F` to cocones over `F â‹™ G` functorially. -/
@[simps]
def functoriality : Cocone F â¥¤ Cocone (F â‹™ G) where
  obj A :=
    { pt := G.obj A.pt
      Î¹ :=
        { app := fun j => G.map (A.Î¹.app j)
          naturality := by intros; erw [â† G.map_comp]; simp } }
  map f :=
    { hom := G.map f.hom
      w := CoconeMorphism.map_w f G }

/-- Functoriality is functorial. -/
def functorialityCompFunctoriality (H : D â¥¤ E) :
    functoriality F G â‹™ functoriality (F â‹™ G) H â‰… functoriality F (G â‹™ H) :=
  NatIso.ofComponents (fun _ â†¦ Iso.refl _) (by simp [functoriality])

instance functoriality_full [G.Full] [G.Faithful] : (functoriality F G).Full where
  map_surjective t :=
    âŸ¨{ hom := G.preimage t.hom
       w := fun j => G.map_injective (by simpa using t.w j) }, by cat_dischâŸ©

instance functoriality_faithful [G.Faithful] : (functoriality F G).Faithful where
  map_injective {_X} {_Y} f g h :=
    CoconeMorphism.ext f g <| G.map_injective <| congr_arg CoconeMorphism.hom h

/-- If `e : C â‰Œ D` is an equivalence of categories, then `functoriality F e.functor` induces an
equivalence between cocones over `F` and cocones over `F â‹™ e.functor`.
-/
@[simps]
def functorialityEquivalence (e : C â‰Œ D) : Cocone F â‰Œ Cocone (F â‹™ e.functor) :=
  let f : (F â‹™ e.functor) â‹™ e.inverse â‰… F :=
    Functor.associator _ _ _ â‰ªâ‰« isoWhiskerLeft _ e.unitIso.symm â‰ªâ‰« Functor.rightUnitor _
  { functor := functoriality F e.functor
    inverse := functoriality (F â‹™ e.functor) e.inverse â‹™ (precomposeEquivalence f.symm).functor
    unitIso := NatIso.ofComponents fun c => Cocones.ext (e.unitIso.app _)
    counitIso := NatIso.ofComponents fun c => Cocones.ext (e.counitIso.app _) }

/-- If `F` reflects isomorphisms, then `Cocones.functoriality F` reflects isomorphisms
as well.
-/
instance reflects_cocone_isomorphism (F : C â¥¤ D) [F.ReflectsIsomorphisms] (K : J â¥¤ C) :
    (Cocones.functoriality K F).ReflectsIsomorphisms := by
  constructor
  intro A B f _
  haveI : IsIso (F.map f.hom) :=
    (Cocones.forget (K â‹™ F)).map_isIso ((Cocones.functoriality K F).map f)
  haveI := ReflectsIsomorphisms.reflects F f.hom
  apply cocone_iso_of_hom_iso

end

end Cocones

end Limits

namespace Functor

variable (H : C â¥¤ D) {F : J â¥¤ C} {G : J â¥¤ C}

open CategoryTheory.Limits

/-- The image of a cone in C under a functor G : C â¥¤ D is a cone in D. -/
@[simps!]
def mapCone (c : Cone F) : Cone (F â‹™ H) :=
  (Cones.functoriality F H).obj c

/-- The construction `mapCone` respects functor composition. -/
@[simps!]
noncomputable def mapConeMapCone {F : J â¥¤ C} {H : C â¥¤ D} {H' : D â¥¤ E} (c : Cone F) :
    H'.mapCone (H.mapCone c) â‰… (H â‹™ H').mapCone c := Cones.ext (Iso.refl _)

/-- The image of a cocone in C under a functor G : C â¥¤ D is a cocone in D. -/
@[simps!]
def mapCocone (c : Cocone F) : Cocone (F â‹™ H) :=
  (Cocones.functoriality F H).obj c

/-- The construction `mapCocone` respects functor composition. -/
@[simps!]
noncomputable def mapCoconeMapCocone {F : J â¥¤ C} {H : C â¥¤ D} {H' : D â¥¤ E} (c : Cocone F) :
    H'.mapCocone (H.mapCocone c) â‰… (H â‹™ H').mapCocone c := Cocones.ext (Iso.refl _)

/-- Given a cone morphism `c âŸ¶ c'`, construct a cone morphism on the mapped cones functorially. -/
def mapConeMorphism {c c' : Cone F} (f : c âŸ¶ c') : H.mapCone c âŸ¶ H.mapCone c' :=
  (Cones.functoriality F H).map f

/-- Given a cocone morphism `c âŸ¶ c'`, construct a cocone morphism on the mapped cocones
functorially. -/
def mapCoconeMorphism {c c' : Cocone F} (f : c âŸ¶ c') : H.mapCocone c âŸ¶ H.mapCocone c' :=
  (Cocones.functoriality F H).map f

/-- If `H` is an equivalence, we invert `H.mapCone` and get a cone for `F` from a cone
for `F â‹™ H`. -/
noncomputable def mapConeInv [IsEquivalence H] (c : Cone (F â‹™ H)) : Cone F :=
  (Limits.Cones.functorialityEquivalence F (asEquivalence H)).inverse.obj c

/-- `mapCone` is the left inverse to `mapConeInv`. -/
noncomputable def mapConeMapConeInv {F : J â¥¤ D} (H : D â¥¤ C) [IsEquivalence H]
    (c : Cone (F â‹™ H)) :
    mapCone H (mapConeInv H c) â‰… c :=
  (Limits.Cones.functorialityEquivalence F (asEquivalence H)).counitIso.app c

/-- `MapCone` is the right inverse to `mapConeInv`. -/
noncomputable def mapConeInvMapCone {F : J â¥¤ D} (H : D â¥¤ C) [IsEquivalence H] (c : Cone F) :
    mapConeInv H (mapCone H c) â‰… c :=
  (Limits.Cones.functorialityEquivalence F (asEquivalence H)).unitIso.symm.app c

/-- If `H` is an equivalence, we invert `H.mapCone` and get a cone for `F` from a cone
for `F â‹™ H`. -/
noncomputable def mapCoconeInv [IsEquivalence H] (c : Cocone (F â‹™ H)) : Cocone F :=
  (Limits.Cocones.functorialityEquivalence F (asEquivalence H)).inverse.obj c

/-- `mapCocone` is the left inverse to `mapCoconeInv`. -/
noncomputable def mapCoconeMapCoconeInv {F : J â¥¤ D} (H : D â¥¤ C) [IsEquivalence H]
    (c : Cocone (F â‹™ H)) :
    mapCocone H (mapCoconeInv H c) â‰… c :=
  (Limits.Cocones.functorialityEquivalence F (asEquivalence H)).counitIso.app c

/-- `mapCocone` is the right inverse to `mapCoconeInv`. -/
noncomputable def mapCoconeInvMapCocone {F : J â¥¤ D} (H : D â¥¤ C) [IsEquivalence H] (c : Cocone F) :
    mapCoconeInv H (mapCocone H c) â‰… c :=
  (Limits.Cocones.functorialityEquivalence F (asEquivalence H)).unitIso.symm.app c

/-- `functoriality F _ â‹™ postcompose (whisker_left F _)` simplifies to `functoriality F _`. -/
@[simps!]
def functorialityCompPostcompose {H H' : C â¥¤ D} (Î± : H â‰… H') :
    Cones.functoriality F H â‹™ Cones.postcompose (whiskerLeft F Î±.hom) â‰… Cones.functoriality F H' :=
  NatIso.ofComponents fun c => Cones.ext (Î±.app _)

/-- For `F : J â¥¤ C`, given a cone `c : Cone F`, and a natural isomorphism `Î± : H â‰… H'` for functors
`H H' : C â¥¤ D`, the postcomposition of the cone `H.mapCone` using the isomorphism `Î±` is
isomorphic to the cone `H'.mapCone`.
-/
@[simps!]
def postcomposeWhiskerLeftMapCone {H H' : C â¥¤ D} (Î± : H â‰… H') (c : Cone F) :
    (Cones.postcompose (whiskerLeft F Î±.hom :)).obj (mapCone H c) â‰… mapCone H' c :=
  (functorialityCompPostcompose Î±).app c

/--
`mapCone` commutes with `postcompose`. In particular, for `F : J â¥¤ C`, given a cone `c : Cone F`, a
natural transformation `Î± : F âŸ¶ G` and a functor `H : C â¥¤ D`, we have two obvious ways of producing
a cone over `G â‹™ H`, and they are both isomorphic.
-/
@[simps!]
def mapConePostcompose {Î± : F âŸ¶ G} {c} :
    mapCone H ((Cones.postcompose Î±).obj c) â‰…
      (Cones.postcompose (whiskerRight Î± H :)).obj (mapCone H c) :=
  Cones.ext (Iso.refl _)

/-- `mapCone` commutes with `postcomposeEquivalence`
-/
@[simps!]
def mapConePostcomposeEquivalenceFunctor {Î± : F â‰… G} {c} :
    mapCone H ((Cones.postcomposeEquivalence Î±).functor.obj c) â‰…
      (Cones.postcomposeEquivalence (isoWhiskerRight Î± H :)).functor.obj (mapCone H c) :=
  Cones.ext (Iso.refl _)

/-- `functoriality F _ â‹™ precompose (whiskerLeft F _)` simplifies to `functoriality F _`. -/
@[simps!]
def functorialityCompPrecompose {H H' : C â¥¤ D} (Î± : H â‰… H') :
    Cocones.functoriality F H â‹™ Cocones.precompose (whiskerLeft F Î±.inv) â‰…
      Cocones.functoriality F H' :=
  NatIso.ofComponents fun c => Cocones.ext (Î±.app _)

/--
For `F : J â¥¤ C`, given a cocone `c : Cocone F`, and a natural isomorphism `Î± : H â‰… H'` for functors
`H H' : C â¥¤ D`, the precomposition of the cocone `H.mapCocone` using the isomorphism `Î±` is
isomorphic to the cocone `H'.mapCocone`.
-/
@[simps!]
def precomposeWhiskerLeftMapCocone {H H' : C â¥¤ D} (Î± : H â‰… H') (c : Cocone F) :
    (Cocones.precompose (whiskerLeft F Î±.inv :)).obj (mapCocone H c) â‰… mapCocone H' c :=
  (functorialityCompPrecompose Î±).app c

/-- `map_cocone` commutes with `precompose`. In particular, for `F : J â¥¤ C`, given a cocone
`c : Cocone F`, a natural transformation `Î± : F âŸ¶ G` and a functor `H : C â¥¤ D`, we have two obvious
ways of producing a cocone over `G â‹™ H`, and they are both isomorphic.
-/
@[simps!]
def mapCoconePrecompose {Î± : F âŸ¶ G} {c} :
    mapCocone H ((Cocones.precompose Î±).obj c) â‰…
      (Cocones.precompose (whiskerRight Î± H :)).obj (mapCocone H c) :=
  Cocones.ext (Iso.refl _)

/-- `mapCocone` commutes with `precomposeEquivalence`
-/
@[simps!]
def mapCoconePrecomposeEquivalenceFunctor {Î± : F â‰… G} {c} :
    mapCocone H ((Cocones.precomposeEquivalence Î±).functor.obj c) â‰…
      (Cocones.precomposeEquivalence (isoWhiskerRight Î± H :)).functor.obj (mapCocone H c) :=
  Cocones.ext (Iso.refl _)

/-- `mapCone` commutes with `whisker`
-/
@[simps!]
def mapConeWhisker {E : K â¥¤ J} {c : Cone F} : mapCone H (c.whisker E) â‰… (mapCone H c).whisker E :=
  Cones.ext (Iso.refl _)

/-- `mapCocone` commutes with `whisker`
-/
@[simps!]
def mapCoconeWhisker {E : K â¥¤ J} {c : Cocone F} :
    mapCocone H (c.whisker E) â‰… (mapCocone H c).whisker E :=
  Cocones.ext (Iso.refl _)

end Functor

end CategoryTheory

namespace CategoryTheory.Limits

section

variable {F : J â¥¤ C}

/-- Change a `Cocone F` into a `Cone F.op`. -/
@[simps]
def Cocone.op (c : Cocone F) : Cone F.op where
  pt := Opposite.op c.pt
  Ï€ := NatTrans.op c.Î¹

/-- Change a `Cone F` into a `Cocone F.op`. -/
@[simps]
def Cone.op (c : Cone F) : Cocone F.op where
  pt := Opposite.op c.pt
  Î¹ := NatTrans.op c.Ï€

/-- Change a `Cocone F.op` into a `Cone F`. -/
@[simps]
def Cocone.unop (c : Cocone F.op) : Cone F where
  pt := Opposite.unop c.pt
  Ï€ := NatTrans.removeOp c.Î¹

/-- Change a `Cone F.op` into a `Cocone F`. -/
@[simps]
def Cone.unop (c : Cone F.op) : Cocone F where
  pt := Opposite.unop c.pt
  Î¹ := NatTrans.removeOp c.Ï€

variable (F)

/-- The category of cocones on `F`
is equivalent to the opposite category of
the category of cones on the opposite of `F`.
-/
def coconeEquivalenceOpConeOp : Cocone F â‰Œ (Cone F.op)áµ’áµ– where
  functor :=
    { obj := fun c => op (Cocone.op c)
      map := fun {X} {Y} f =>
        Quiver.Hom.op
          { hom := f.hom.op
            w := fun j => by
              apply Quiver.Hom.unop_inj
              dsimp
              apply CoconeMorphism.w } }
  inverse :=
    { obj := fun c => Cone.unop (unop c)
      map := fun {X} {Y} f =>
        { hom := f.unop.hom.unop
          w := fun j => by
            apply Quiver.Hom.op_inj
            dsimp
            apply ConeMorphism.w } }
  unitIso := NatIso.ofComponents (fun c => Cocones.ext (Iso.refl _))
  counitIso :=
    NatIso.ofComponents
      (fun c => (Cones.ext (Iso.refl c.unop.pt)).op)
      fun {X} {Y} f =>
      Quiver.Hom.unop_inj (ConeMorphism.ext _ _ (by simp))
  functor_unitIso_comp c := by
    apply Quiver.Hom.unop_inj
    apply ConeMorphism.ext
    dsimp
    apply comp_id

attribute [simps] coconeEquivalenceOpConeOp

end

section

variable {F : J â¥¤ Cáµ’áµ–}

/-- Change a cocone on `F.leftOp : Jáµ’áµ– â¥¤ C` to a cocone on `F : J â¥¤ Cáµ’áµ–`. -/
@[simps!]
def coneOfCoconeLeftOp (c : Cocone F.leftOp) : Cone F where
  pt := op c.pt
  Ï€ := NatTrans.removeLeftOp c.Î¹

/-- Change a cone on `F : J â¥¤ Cáµ’áµ–` to a cocone on `F.leftOp : Jáµ’áµ– â¥¤ C`. -/
@[simps!]
def coconeLeftOpOfCone (c : Cone F) : Cocone F.leftOp where
  pt := unop c.pt
  Î¹ := NatTrans.leftOp c.Ï€

/- When trying use `@[simps]` to generate the `Î¹_app` field of this definition, `@[simps]` tries to
  reduce the RHS using `expr.dsimp` and `expr.simp`, but for some reason the expression is not
  being simplified properly. -/
/-- Change a cone on `F.leftOp : Jáµ’áµ– â¥¤ C` to a cocone on `F : J â¥¤ Cáµ’áµ–`. -/
@[simps pt]
def coconeOfConeLeftOp (c : Cone F.leftOp) : Cocone F where
  pt := op c.pt
  Î¹ := NatTrans.removeLeftOp c.Ï€

@[simp]
theorem coconeOfConeLeftOp_Î¹_app (c : Cone F.leftOp) (j) :
    (coconeOfConeLeftOp c).Î¹.app j = (c.Ï€.app (op j)).op := by
  dsimp only [coconeOfConeLeftOp]
  simp

/-- Change a cocone on `F : J â¥¤ Cáµ’áµ–` to a cone on `F.leftOp : Jáµ’áµ– â¥¤ C`. -/
@[simps!]
def coneLeftOpOfCocone (c : Cocone F) : Cone F.leftOp where
  pt := unop c.pt
  Ï€ := NatTrans.leftOp c.Î¹

end

section

variable {F : Jáµ’áµ– â¥¤ C}

/-- Change a cocone on `F.rightOp : J â¥¤ Cáµ’áµ–` to a cone on `F : Jáµ’áµ– â¥¤ C`. -/
@[simps]
def coneOfCoconeRightOp (c : Cocone F.rightOp) : Cone F where
  pt := unop c.pt
  Ï€ := NatTrans.removeRightOp c.Î¹

/-- Change a cone on `F : Jáµ’áµ– â¥¤ C` to a cocone on `F.rightOp : Jáµ’áµ– â¥¤ C`. -/
@[simps]
def coconeRightOpOfCone (c : Cone F) : Cocone F.rightOp where
  pt := op c.pt
  Î¹ := NatTrans.rightOp c.Ï€

/-- Change a cone on `F.rightOp : J â¥¤ Cáµ’áµ–` to a cocone on `F : Jáµ’áµ– â¥¤ C`. -/
@[simps]
def coconeOfConeRightOp (c : Cone F.rightOp) : Cocone F where
  pt := unop c.pt
  Î¹ := NatTrans.removeRightOp c.Ï€

/-- Change a cocone on `F : Jáµ’áµ– â¥¤ C` to a cone on `F.rightOp : J â¥¤ Cáµ’áµ–`. -/
@[simps]
def coneRightOpOfCocone (c : Cocone F) : Cone F.rightOp where
  pt := op c.pt
  Ï€ := NatTrans.rightOp c.Î¹

end

section

variable {F : Jáµ’áµ– â¥¤ Cáµ’áµ–}

/-- Change a cocone on `F.unop : J â¥¤ C` into a cone on `F : Jáµ’áµ– â¥¤ Cáµ’áµ–`. -/
@[simps]
def coneOfCoconeUnop (c : Cocone F.unop) : Cone F where
  pt := op c.pt
  Ï€ := NatTrans.removeUnop c.Î¹

/-- Change a cone on `F : Jáµ’áµ– â¥¤ Cáµ’áµ–` into a cocone on `F.unop : J â¥¤ C`. -/
@[simps]
def coconeUnopOfCone (c : Cone F) : Cocone F.unop where
  pt := unop c.pt
  Î¹ := NatTrans.unop c.Ï€

/-- Change a cone on `F.unop : J â¥¤ C` into a cocone on `F : Jáµ’áµ– â¥¤ Cáµ’áµ–`. -/
@[simps]
def coconeOfConeUnop (c : Cone F.unop) : Cocone F where
  pt := op c.pt
  Î¹ := NatTrans.removeUnop c.Ï€

/-- Change a cocone on `F : Jáµ’áµ– â¥¤ Cáµ’áµ–` into a cone on `F.unop : J â¥¤ C`. -/
@[simps]
def coneUnopOfCocone (c : Cocone F) : Cone F.unop where
  pt := unop c.pt
  Ï€ := NatTrans.unop c.Î¹

end

end CategoryTheory.Limits

namespace CategoryTheory.Functor

open CategoryTheory.Limits

variable {F : J â¥¤ C}

section

variable (G : C â¥¤ D)

/-- The opposite cocone of the image of a cone is the image of the opposite cocone. -/
@[simps!]
def mapConeOp (t : Cone F) : (mapCone G t).op â‰… mapCocone G.op t.op :=
  Cocones.ext (Iso.refl _)

/-- The opposite cone of the image of a cocone is the image of the opposite cone. -/
@[simps!]
def mapCoconeOp {t : Cocone F} : (mapCocone G t).op â‰… mapCone G.op t.op :=
  Cones.ext (Iso.refl _)

end

end CategoryTheory.Functor
