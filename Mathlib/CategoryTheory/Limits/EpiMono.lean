/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Limits.Shapes.CommSq

/-!
# Relation between mono/epi and pullback/pushout squares

In this file, monomorphisms and epimorphisms are characterized in terms
of pullback and pushout squares. For example, we obtain `mono_iff_isPullback`
which asserts that a morphism `f : X âŸ¶ Y` is a monomorphism iff the obvious
square

```
X âŸ¶ X
|   |
v   v
X âŸ¶ Y
```

is a pullback square.


-/

namespace CategoryTheory

open Category Limits

variable {C : Type*} [Category C] {X Y : C} {f : X âŸ¶ Y}

section Mono

variable {c : PullbackCone f f} (hc : IsLimit c)

lemma mono_iff_fst_eq_snd : Mono f â†” c.fst = c.snd := by
  constructor
  Â· intro hf
    simpa only [â† cancel_mono f] using c.condition
  Â· intro hf
    constructor
    intro Z g g' h
    obtain âŸ¨Ï†, rfl, rflâŸ© := PullbackCone.IsLimit.lift' hc g g' h
    rw [hf]

lemma mono_iff_isIso_fst : Mono f â†” IsIso c.fst := by
  rw [mono_iff_fst_eq_snd hc]
  constructor
  Â· intro h
    obtain âŸ¨Ï†, hÏ†â‚, hÏ†â‚‚âŸ© := PullbackCone.IsLimit.lift' hc (ğŸ™ X) (ğŸ™ X) (by simp)
    refine âŸ¨Ï†, PullbackCone.IsLimit.hom_ext hc ?_ ?_, hÏ†â‚âŸ©
    Â· dsimp
      simp only [assoc, hÏ†â‚, id_comp, comp_id]
    Â· dsimp
      simp only [assoc, hÏ†â‚‚, id_comp, comp_id, h]
  Â· intro
    obtain âŸ¨Ï†, hÏ†â‚, hÏ†â‚‚âŸ© := PullbackCone.IsLimit.lift' hc (ğŸ™ X) (ğŸ™ X) (by simp)
    have : IsSplitEpi Ï† := IsSplitEpi.mk âŸ¨SplitEpi.mk c.fst (by
      rw [â† cancel_mono c.fst, assoc, id_comp, hÏ†â‚, comp_id])âŸ©
    rw [â† cancel_epi Ï†, hÏ†â‚, hÏ†â‚‚]

lemma mono_iff_isIso_snd : Mono f â†” IsIso c.snd :=
  mono_iff_isIso_fst (PullbackCone.flipIsLimit hc)

variable (f)

lemma mono_iff_isPullback : Mono f â†” IsPullback (ğŸ™ X) (ğŸ™ X) f f := by
  constructor
  Â· intro
    exact IsPullback.of_isLimit (PullbackCone.isLimitMkIdId f)
  Â· intro hf
    exact (mono_iff_fst_eq_snd hf.isLimit).2 rfl

end Mono

section Epi

variable {c : PushoutCocone f f} (hc : IsColimit c)

lemma epi_iff_inl_eq_inr : Epi f â†” c.inl = c.inr := by
  constructor
  Â· intro hf
    simpa only [â† cancel_epi f] using c.condition
  Â· intro hf
    constructor
    intro Z g g' h
    obtain âŸ¨Ï†, rfl, rflâŸ© := PushoutCocone.IsColimit.desc' hc g g' h
    rw [hf]

lemma epi_iff_isIso_inl : Epi f â†” IsIso c.inl := by
  rw [epi_iff_inl_eq_inr hc]
  constructor
  Â· intro h
    obtain âŸ¨Ï†, hÏ†â‚, hÏ†â‚‚âŸ© := PushoutCocone.IsColimit.desc' hc (ğŸ™ Y) (ğŸ™ Y) (by simp)
    refine âŸ¨Ï†, hÏ†â‚, PushoutCocone.IsColimit.hom_ext hc ?_ ?_âŸ©
    Â· dsimp
      simp only [comp_id, reassoc_of% hÏ†â‚]
    Â· dsimp
      simp only [comp_id, h, reassoc_of% hÏ†â‚‚]
  Â· intro
    obtain âŸ¨Ï†, hÏ†â‚, hÏ†â‚‚âŸ© := PushoutCocone.IsColimit.desc' hc (ğŸ™ Y) (ğŸ™ Y) (by simp)
    have : IsSplitMono Ï† := IsSplitMono.mk âŸ¨SplitMono.mk c.inl (by
      rw [â† cancel_epi c.inl, reassoc_of% hÏ†â‚, comp_id])âŸ©
    rw [â† cancel_mono Ï†, hÏ†â‚, hÏ†â‚‚]

lemma epi_iff_isIso_inr : Epi f â†” IsIso c.inr :=
  epi_iff_isIso_inl (PushoutCocone.flipIsColimit hc)

variable (f)

lemma epi_iff_isPushout : Epi f â†” IsPushout f f (ğŸ™ Y) (ğŸ™ Y) := by
  constructor
  Â· intro
    exact IsPushout.of_isColimit (PushoutCocone.isColimitMkIdId f)
  Â· intro hf
    exact (epi_iff_inl_eq_inr hf.isColimit).2 rfl

end Epi

end CategoryTheory
