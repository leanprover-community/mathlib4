/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Limits.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Limits.Shapes.Equalizers
import Mathlib.CategoryTheory.Limits.Shapes.WidePullbacks
import Mathlib.CategoryTheory.IsConnected
import Mathlib.CategoryTheory.Limits.Preserves.Basic

/-!
# Connected limits

A connected limit is a limit whose shape is a connected category.

We show that constant functors from a connected category have a limit
and a colimit. From this we deduce that a cocone `c` over a connected diagram
is a colimit cocone if and only if `colimMap c.Î¹` is an isomorphism (where
`c.Î¹ : F âŸ¶ const c.pt` is the natural transformation that defines the
cocone).

We give examples of connected categories, and prove
that the functor given by `(X Ã— -)` preserves any connected limit.
That is, any limit of shape `J` where `J` is a connected category is
preserved by the functor `(X Ã— -)`.
-/


noncomputable section

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

open CategoryTheory CategoryTheory.Category CategoryTheory.Limits

namespace CategoryTheory

section Const

namespace Limits

variable {J : Type uâ‚} [Category.{vâ‚} J] {C : Type uâ‚‚} [Category.{vâ‚‚} C] (X : C)

section

variable (J)

/-- The obvious cone of a constant functor. -/
@[simps]
def constCone : Cone ((Functor.const J).obj X) where
  pt := X
  Ï€ := ğŸ™ _

/-- The obvious cocone of a constant functor. -/
@[simps]
def constCocone : Cocone ((Functor.const J).obj X) where
  pt := X
  Î¹ := ğŸ™ _

variable [IsConnected J]

/-- When `J` is a connected category, the limit of a
constant functor `J â¥¤ C` with value `X : C` identifies to `X`. -/
def isLimitConstCone : IsLimit (constCone J X) where
  lift s := s.Ï€.app (Classical.arbitrary _)
  fac s j := by
    dsimp
    rw [comp_id]
    exact constant_of_preserves_morphisms _
      (fun _ _ f â†¦ by simpa using s.w f) _ _
  uniq s m hm := by simpa using hm (Classical.arbitrary _)

/-- When `J` is a connected category, the colimit of a
constant functor `J â¥¤ C` with value `X : C` identifies to `X`. -/
def isColimitConstCocone : IsColimit (constCocone J X) where
  desc s := s.Î¹.app (Classical.arbitrary _)
  fac s j := by
    dsimp
    rw [id_comp]
    exact constant_of_preserves_morphisms _
      (fun _ _ f â†¦ by simpa using (s.w f).symm) _ _
  uniq s m hm := by simpa using hm (Classical.arbitrary _)

instance hasLimit_const_of_isConnected : HasLimit ((Functor.const J).obj X) :=
  âŸ¨_, isLimitConstCone J XâŸ©

instance hasColimit_const_of_isConnected : HasColimit ((Functor.const J).obj X) :=
  âŸ¨_, isColimitConstCocone J XâŸ©

end

section

variable [IsConnected J]

/-- If `J` is connected, `F : J â¥¤ C` and `c` is a cone on `F`, then to check that `c` is a
limit it is sufficient to check that `limMap c.Ï€` is an isomorphism. The converse is also
true, see `Cone.isLimit_iff_isIso_limMap_Ï€`. -/
def Cone.isLimitOfIsIsoLimMapÏ€ {F : J â¥¤ C} [HasLimit F] (c : Cone F)
    [IsIso (limMap c.Ï€)] : IsLimit c := by
  refine IsLimit.ofIsoLimit (limit.isLimit _) (Cones.ext ((asIso (limMap c.Ï€)).symm â‰ªâ‰«
    (limit.isLimit _).conePointUniqueUpToIso (isLimitConstCone J c.pt)) ?_)
  intro j
  simp only [limit.cone_x, Functor.const_obj_obj, limit.cone_Ï€, Iso.trans_hom, Iso.symm_hom,
    asIso_inv, assoc, IsIso.eq_inv_comp, limMap_Ï€]
  congr 1
  simp [â† Iso.inv_comp_eq_id]

theorem IsLimit.isIso_limMap_Ï€ {F : J â¥¤ C} [HasLimit F] {c : Cone F} (hc : IsLimit c) :
    IsIso (limMap c.Ï€) := by
  suffices limMap c.Ï€ = ((limit.isLimit _).conePointUniqueUpToIso (isLimitConstCone J c.pt) â‰ªâ‰«
      hc.conePointUniqueUpToIso (limit.isLimit _)).hom by
    rw [this]; infer_instance
  ext j
  simp only [limMap_Ï€, Functor.const_obj_obj, limit.cone_x, constCone_pt, Iso.trans_hom, assoc,
    limit.conePointUniqueUpToIso_hom_comp]
  congr 1
  simp [â† Iso.inv_comp_eq_id]

theorem Cone.isLimit_iff_isIso_limMap_Ï€ {F : J â¥¤ C} [HasLimit F] (c : Cone F) :
    Nonempty (IsLimit c) â†” IsIso (limMap c.Ï€) :=
  âŸ¨fun âŸ¨hâŸ© => IsLimit.isIso_limMap_Ï€ h, fun _ => âŸ¨c.isLimitOfIsIsoLimMapÏ€âŸ©âŸ©

/-- If `J` is connected, `F : J â¥¤ C` and `C` is a cocone on `F`, then to check that `c` is a
colimit it is sufficient to check that `colimMap c.Î¹` is an isomorphism. The converse is also
true, see `Cocone.isColimit_iff_isIso_colimMap_Î¹`. -/
def Cocone.isColimitOfIsIsoColimMapÎ¹ {F : J â¥¤ C} [HasColimit F] (c : Cocone F)
    [IsIso (colimMap c.Î¹)] : IsColimit c :=
  IsColimit.ofIsoColimit (colimit.isColimit _) (Cocones.ext (asIso (colimMap c.Î¹) â‰ªâ‰«
    (colimit.isColimit _).coconePointUniqueUpToIso (isColimitConstCocone J c.pt)) (by simp))

theorem IsColimit.isIso_colimMap_Î¹ {F : J â¥¤ C} [HasColimit F] {c : Cocone F} (hc : IsColimit c) :
    IsIso (colimMap c.Î¹) := by
  suffices colimMap c.Î¹ = ((colimit.isColimit _).coconePointUniqueUpToIso hc â‰ªâ‰«
      (isColimitConstCocone J c.pt).coconePointUniqueUpToIso (colimit.isColimit _)).hom by
    rw [this]; infer_instance
  ext j
  simp only [Î¹_colimMap, Functor.const_obj_obj, colimit.cocone_x, Iso.trans_hom,
    colimit.comp_coconePointUniqueUpToIso_hom_assoc]
  congr 1
  simp [â† Iso.comp_inv_eq_id]

theorem Cocone.isColimit_iff_isIso_colimMap_Î¹ {F : J â¥¤ C} [HasColimit F] (c : Cocone F) :
    Nonempty (IsColimit c) â†” IsIso (colimMap c.Î¹) :=
  âŸ¨fun âŸ¨hâŸ© => IsColimit.isIso_colimMap_Î¹ h, fun _ => âŸ¨c.isColimitOfIsIsoColimMapÎ¹âŸ©âŸ©

end

end Limits

end Const

section Examples

instance widePullbackShape_connected (J : Type vâ‚) : IsConnected (WidePullbackShape J) := by
  apply IsConnected.of_induct
  Â· introv hp t
    cases j
    Â· exact hp
    Â· rwa [t (WidePullbackShape.Hom.term _)]

instance widePushoutShape_connected (J : Type vâ‚) : IsConnected (WidePushoutShape J) := by
  apply IsConnected.of_induct
  Â· introv hp t
    cases j
    Â· exact hp
    Â· rwa [â† t (WidePushoutShape.Hom.init _)]

instance parallelPairInhabited : Inhabited WalkingParallelPair :=
  âŸ¨WalkingParallelPair.oneâŸ©

instance parallel_pair_connected : IsConnected WalkingParallelPair := by
  apply IsConnected.of_induct
  Â· introv _ t
    cases j
    Â· rwa [t WalkingParallelPairHom.left]
    Â· assumption

end Examples

variable {C : Type uâ‚‚} [Category.{vâ‚‚} C]
variable [HasBinaryProducts C]
variable {J : Type vâ‚‚} [SmallCategory J]

namespace ProdPreservesConnectedLimits

/-- (Impl). The obvious natural transformation from (X Ã— K -) to K. -/
@[simps]
def Î³â‚‚ {K : J â¥¤ C} (X : C) : K â‹™ prod.functor.obj X âŸ¶ K where app _ := Limits.prod.snd

/-- (Impl). The obvious natural transformation from (X Ã— K -) to X -/
@[simps]
def Î³â‚ {K : J â¥¤ C} (X : C) : K â‹™ prod.functor.obj X âŸ¶ (Functor.const J).obj X where
  app _ := Limits.prod.fst

/-- (Impl).
Given a cone for (X Ã— K -), produce a cone for K using the natural transformation `Î³â‚‚` -/
@[simps]
def forgetCone {X : C} {K : J â¥¤ C} (s : Cone (K â‹™ prod.functor.obj X)) : Cone K where
  pt := s.pt
  Ï€ := s.Ï€ â‰« Î³â‚‚ X

end ProdPreservesConnectedLimits

open ProdPreservesConnectedLimits

/-- The functor `(X Ã— -)` preserves any connected limit.
Note that this functor does not preserve the two most obvious disconnected limits - that is,
`(X Ã— -)` does not preserve products or terminal object, e.g. `(X â¨¯ A) â¨¯ (X â¨¯ B)` is not isomorphic
to `X â¨¯ (A â¨¯ B)` and `X â¨¯ 1` is not isomorphic to `1`.
-/
lemma prod_preservesConnectedLimits [IsConnected J] (X : C) :
    PreservesLimitsOfShape J (prod.functor.obj X) where
  preservesLimit {K} :=
    { preserves := fun {c} l => âŸ¨{
          lift := fun s =>
            prod.lift (s.Ï€.app (Classical.arbitrary _) â‰« Limits.prod.fst) (l.lift (forgetCone s))
          fac := fun s j => by
            apply Limits.prod.hom_ext
            Â· erw [assoc, limMap_Ï€, comp_id, limit.lift_Ï€]
              exact (nat_trans_from_is_connected (s.Ï€ â‰« Î³â‚ X) j (Classical.arbitrary _)).symm
            Â· simp
          uniq := fun s m L => by
            apply Limits.prod.hom_ext
            Â· erw [limit.lift_Ï€, â† L (Classical.arbitrary J), assoc, limMap_Ï€, comp_id]
              rfl
            Â· rw [limit.lift_Ï€]
              apply l.uniq (forgetCone s)
              intro j
              simp [â† L j] }âŸ© }

end CategoryTheory
