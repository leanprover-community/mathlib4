/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Jo√´l Riou
-/
import Mathlib.CategoryTheory.Limits.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Limits.Shapes.Equalizers
import Mathlib.CategoryTheory.Limits.Shapes.WidePullbacks
import Mathlib.CategoryTheory.IsConnected
import Mathlib.CategoryTheory.Limits.Preserves.Basic

/-!
# Connected limits

A connected limit is a limit whose shape is a connected category.

We show that constant functors from a connected category have a limit
and a colimit. From this we deduce that a cocone `c` over a connected diagram
is a colimit cocone if and only if `colimMap c.Œπ` is an isomorphism (where
`c.Œπ : F ‚ü∂ const c.pt` is the natural transformation that defines the
cocone).

We give examples of connected categories, and prove
that the functor given by `(X √ó -)` preserves any connected limit.
That is, any limit of shape `J` where `J` is a connected category is
preserved by the functor `(X √ó -)`.
-/


noncomputable section

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

open CategoryTheory CategoryTheory.Category CategoryTheory.Limits

namespace CategoryTheory

section Const

namespace Limits

variable {J : Type u‚ÇÅ} [Category.{v‚ÇÅ} J] {C : Type u‚ÇÇ} [Category.{v‚ÇÇ} C] (X : C)

section

variable (J)

/-- The obvious cone of a constant functor. -/
@[simps]
def constCone : Cone ((Functor.const J).obj X) where
  pt := X
  œÄ := ùüô _

/-- The obvious cocone of a constant functor. -/
@[simps]
def constCocone : Cocone ((Functor.const J).obj X) where
  pt := X
  Œπ := ùüô _

variable [IsConnected J]

/-- When `J` is a connected category, the limit of a
constant functor `J ‚•§ C` with value `X : C` identifies to `X`. -/
def isLimitConstCone : IsLimit (constCone J X) where
  lift s := s.œÄ.app (Classical.arbitrary _)
  fac s j := by
    dsimp
    rw [comp_id]
    exact constant_of_preserves_morphisms _
      (fun _ _ f ‚Ü¶ by simpa using s.w f) _ _
  uniq s m hm := by simpa using hm (Classical.arbitrary _)

/-- When `J` is a connected category, the colimit of a
constant functor `J ‚•§ C` with value `X : C` identifies to `X`. -/
def isColimitConstCocone : IsColimit (constCocone J X) where
  desc s := s.Œπ.app (Classical.arbitrary _)
  fac s j := by
    dsimp
    rw [id_comp]
    exact constant_of_preserves_morphisms _
      (fun _ _ f ‚Ü¶ by simpa using (s.w f).symm) _ _
  uniq s m hm := by simpa using hm (Classical.arbitrary _)

instance hasLimit_const_of_isConnected : HasLimit ((Functor.const J).obj X) :=
  ‚ü®_, isLimitConstCone J X‚ü©

instance hasColimit_const_of_isConnected : HasColimit ((Functor.const J).obj X) :=
  ‚ü®_, isColimitConstCocone J X‚ü©

end

section

variable [IsConnected J]

/-- If `J` is connected, `F : J ‚•§ C` and `c` is a cone on `F`, then to check that `c` is a
limit it is sufficient to check that `limMap c.œÄ` is an isomorphism. The converse is also
true, see `Cone.isLimit_iff_isIso_limMap_œÄ`. -/
def Cone.isLimitOfIsIsoLimMapœÄ {F : J ‚•§ C} [HasLimit F] (c : Cone F)
    [IsIso (limMap c.œÄ)] : IsLimit c := by
  refine IsLimit.ofIsoLimit (limit.isLimit _) (Cones.ext ((asIso (limMap c.œÄ)).symm ‚â™‚â´
    (limit.isLimit _).conePointUniqueUpToIso (isLimitConstCone J c.pt)) ?_)
  intro j
  simp only [limit.cone_x, Functor.const_obj_obj, limit.cone_œÄ, Iso.trans_hom, Iso.symm_hom,
    asIso_inv, assoc, IsIso.eq_inv_comp, limMap_œÄ]
  congr 1
  simp [‚Üê Iso.inv_comp_eq_id]

theorem IsLimit.isIso_limMap_œÄ {F : J ‚•§ C} [HasLimit F] {c : Cone F} (hc : IsLimit c) :
    IsIso (limMap c.œÄ) := by
  suffices limMap c.œÄ = ((limit.isLimit _).conePointUniqueUpToIso (isLimitConstCone J c.pt) ‚â™‚â´
      hc.conePointUniqueUpToIso (limit.isLimit _)).hom by
    rw [this]; infer_instance
  ext j
  simp only [limMap_œÄ, Functor.const_obj_obj, limit.cone_x, constCone_pt, Iso.trans_hom, assoc,
    limit.conePointUniqueUpToIso_hom_comp]
  congr 1
  simp [‚Üê Iso.inv_comp_eq_id]

theorem Cone.isLimit_iff_isIso_limMap_œÄ {F : J ‚•§ C} [HasLimit F] (c : Cone F) :
    Nonempty (IsLimit c) ‚Üî IsIso (limMap c.œÄ) :=
  ‚ü®fun ‚ü®h‚ü© ‚Ü¶ IsLimit.isIso_limMap_œÄ h, fun _ ‚Ü¶ ‚ü®c.isLimitOfIsIsoLimMapœÄ‚ü©‚ü©

/-- If `J` is connected, `F : J ‚•§ C` and `C` is a cocone on `F`, then to check that `c` is a
colimit it is sufficient to check that `colimMap c.Œπ` is an isomorphism. The converse is also
true, see `Cocone.isColimit_iff_isIso_colimMap_Œπ`. -/
def Cocone.isColimitOfIsIsoColimMapŒπ {F : J ‚•§ C} [HasColimit F] (c : Cocone F)
    [IsIso (colimMap c.Œπ)] : IsColimit c :=
  IsColimit.ofIsoColimit (colimit.isColimit _) (Cocones.ext (asIso (colimMap c.Œπ) ‚â™‚â´
    (colimit.isColimit _).coconePointUniqueUpToIso (isColimitConstCocone J c.pt)) (by simp))

theorem IsColimit.isIso_colimMap_Œπ {F : J ‚•§ C} [HasColimit F] {c : Cocone F} (hc : IsColimit c) :
    IsIso (colimMap c.Œπ) := by
  suffices colimMap c.Œπ = ((colimit.isColimit _).coconePointUniqueUpToIso hc ‚â™‚â´
      (isColimitConstCocone J c.pt).coconePointUniqueUpToIso (colimit.isColimit _)).hom by
    rw [this]; infer_instance
  ext j
  simp only [Œπ_colimMap, Functor.const_obj_obj, colimit.cocone_x, Iso.trans_hom,
    colimit.comp_coconePointUniqueUpToIso_hom_assoc]
  congr 1
  simp [‚Üê Iso.comp_inv_eq_id]

theorem Cocone.isColimit_iff_isIso_colimMap_Œπ {F : J ‚•§ C} [HasColimit F] (c : Cocone F) :
    Nonempty (IsColimit c) ‚Üî IsIso (colimMap c.Œπ) :=
  ‚ü®fun ‚ü®h‚ü© ‚Ü¶ IsColimit.isIso_colimMap_Œπ h, fun _ ‚Ü¶ ‚ü®c.isColimitOfIsIsoColimMapŒπ‚ü©‚ü©

end

end Limits

end Const

section Examples

instance widePullbackShape_connected (J : Type v‚ÇÅ) : IsConnected (WidePullbackShape J) := by
  apply IsConnected.of_induct
  ¬∑ introv hp t
    cases j
    ¬∑ exact hp
    ¬∑ rwa [t (WidePullbackShape.Hom.term _)]

instance widePushoutShape_connected (J : Type v‚ÇÅ) : IsConnected (WidePushoutShape J) := by
  apply IsConnected.of_induct
  ¬∑ introv hp t
    cases j
    ¬∑ exact hp
    ¬∑ rwa [‚Üê t (WidePushoutShape.Hom.init _)]

instance parallelPairInhabited : Inhabited WalkingParallelPair :=
  ‚ü®WalkingParallelPair.one‚ü©

instance parallel_pair_connected : IsConnected WalkingParallelPair := by
  apply IsConnected.of_induct
  ¬∑ introv _ t
    cases j
    ¬∑ rwa [t WalkingParallelPairHom.left]
    ¬∑ assumption

end Examples

variable {C : Type u‚ÇÇ} [Category.{v‚ÇÇ} C]
variable [HasBinaryProducts C]
variable {J : Type v‚ÇÇ} [SmallCategory J]

namespace ProdPreservesConnectedLimits

/-- (Impl). The obvious natural transformation from (X √ó K -) to K. -/
@[simps]
def Œ≥‚ÇÇ {K : J ‚•§ C} (X : C) : K ‚ãô prod.functor.obj X ‚ü∂ K where app _ := Limits.prod.snd

/-- (Impl). The obvious natural transformation from (X √ó K -) to X -/
@[simps]
def Œ≥‚ÇÅ {K : J ‚•§ C} (X : C) : K ‚ãô prod.functor.obj X ‚ü∂ (Functor.const J).obj X where
  app _ := Limits.prod.fst

/-- (Impl).
Given a cone for (X √ó K -), produce a cone for K using the natural transformation `Œ≥‚ÇÇ` -/
@[simps]
def forgetCone {X : C} {K : J ‚•§ C} (s : Cone (K ‚ãô prod.functor.obj X)) : Cone K where
  pt := s.pt
  œÄ := s.œÄ ‚â´ Œ≥‚ÇÇ X

end ProdPreservesConnectedLimits

open ProdPreservesConnectedLimits

/-- The functor `(X √ó -)` preserves any connected limit.
Note that this functor does not preserve the two most obvious disconnected limits - that is,
`(X √ó -)` does not preserve products or terminal object, eg `(X ‚®Ø A) ‚®Ø (X ‚®Ø B)` is not isomorphic to
`X ‚®Ø (A ‚®Ø B)` and `X ‚®Ø 1` is not isomorphic to `1`.
-/
lemma prod_preservesConnectedLimits [IsConnected J] (X : C) :
    PreservesLimitsOfShape J (prod.functor.obj X) where
  preservesLimit {K} :=
    { preserves := fun {c} l ‚Ü¶ ‚ü®{
          lift := fun s ‚Ü¶
            prod.lift (s.œÄ.app (Classical.arbitrary _) ‚â´ Limits.prod.fst) (l.lift (forgetCone s))
          fac := fun s j ‚Ü¶ by
            apply Limits.prod.hom_ext
            ¬∑ erw [assoc, limMap_œÄ, comp_id, limit.lift_œÄ]
              exact (nat_trans_from_is_connected (s.œÄ ‚â´ Œ≥‚ÇÅ X) j (Classical.arbitrary _)).symm
            ¬∑ simp
          uniq := fun s m L ‚Ü¶ by
            apply Limits.prod.hom_ext
            ¬∑ erw [limit.lift_œÄ, ‚Üê L (Classical.arbitrary J), assoc, limMap_œÄ, comp_id]
              rfl
            ¬∑ rw [limit.lift_œÄ]
              apply l.uniq (forgetCone s)
              intro j
              simp [‚Üê L j] }‚ü© }

end CategoryTheory
