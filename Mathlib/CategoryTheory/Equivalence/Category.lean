/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.Equivalence

/-!
# The category of equivalences of categories

In this file, we put a `Category` structure on equivalences of categories. By definition,
a morphism between two equivalences is a natural transformation between their respective functors.

This file merely contains the definition of the category structure and provides basic properties and
constructions, such as `functorFunctor : (C â‰Œ D) â¥¤ (C â¥¤ D)`, and `congrRightFunctor`.

to keep transitive imports as minimal as possible, we do not provide in this file the
"inverseFunctor" functor, as its most natural construction makes use of the calculus of mates from
`CategoryTheory.Adjuction.Mates`.
-/

namespace CategoryTheory

open CategoryTheory.Functor NatIso Category

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D]

namespace Equivalence

/-- A morphism between equivalences of categories is a natural transformation between their
functors. -/
def Hom : (C â‰Œ D) â†’ (C â‰Œ D) â†’ Type (max uâ‚ vâ‚‚) :=
  fun f g â†¦ (f.functor âŸ¶ g.functor)

instance : Category.{max uâ‚ vâ‚‚} (C â‰Œ D) where
  Hom e f := Hom e f
  id e := ğŸ™ e.functor
  comp {a b c} f g := ((f :) â‰« (g :) : a.functor âŸ¶ _)

/-- Promote a natural transformation `e.functor âŸ¶ f.functor` to a morphism in `C â‰Œ D`. -/
def mkHom {e f : C â‰Œ D} (Î· : e.functor âŸ¶ f.functor) : e âŸ¶ f := Î·

/-- Recover a natural transformation between `e.functor` and `f.functor` from the data of
a morphism `e âŸ¶ f`. -/
def asNatTrans {e f : C â‰Œ D} (Î· : e âŸ¶ f) : e.functor âŸ¶ f.functor := Î·

@[simp]
lemma mkHom_asNatTrans {e f : C â‰Œ D} (Î· : e.functor âŸ¶ f.functor) :
    mkHom (asNatTrans Î·) = Î· :=
  rfl

@[simp]
lemma asNatTrans_mkHom {e f : C â‰Œ D} (Î· : e âŸ¶ f) :
    asNatTrans (mkHom Î·) = Î· :=
  rfl

@[simp]
lemma id_asNatTrans {e : C â‰Œ D} : asNatTrans (ğŸ™ e) = ğŸ™ _ := rfl

@[simp]
lemma id_asNatTrans' {e : C â‰Œ D} : asNatTrans (ğŸ™ e.functor) = ğŸ™ _ := rfl

@[simp]
lemma comp_asNatTrans {e f g: C â‰Œ D} (Î± : e âŸ¶ f) (Î² : f âŸ¶ g) :
    asNatTrans (Î± â‰« Î²) = asNatTrans Î± â‰« asNatTrans Î² :=
  rfl

@[simp]
lemma mkHom_id_functor {e : C â‰Œ D} : mkHom (ğŸ™ e.functor) = ğŸ™ e := rfl

@[simp]
lemma mkHom_id_inverse {e : C â‰Œ D} : mkHom (ğŸ™ e.inverse) = ğŸ™ e.symm := rfl

@[simp]
lemma mkHom_comp {e f g: C â‰Œ D} (Î± : e.functor âŸ¶ f.functor) (Î² : f.functor âŸ¶ g.functor) :
    mkHom (Î± â‰« Î²) = (mkHom Î±) â‰« (mkHom Î²) :=
  rfl

@[ext]
lemma homExt {e f : C â‰Œ D} {Î± Î² : e âŸ¶ f} (h : asNatTrans Î± = asNatTrans Î²) : Î± = Î² := by
  apply NatTrans.ext
  exact NatTrans.ext_iff.mp h

/-- Construct an isomorphism in `C â‰Œ D` from a natural isomorphism between the functors
of the equivalences. -/
@[simps]
def mkIso {e f : C â‰Œ D} (Î· : e.functor â‰… f.functor) : e â‰… f where
  hom := mkHom Î·.hom
  inv := mkHom Î·.inv

/-- Obtain a natural isomorphism between the functors of two equivalences from
  an isomorphism in `C â‰Œ D`. -/
@[simps]
def asNatIso {e f : C â‰Œ D} (Î· : e â‰… f) : e.functor â‰… f.functor where
  hom := asNatTrans Î·.hom
  inv := asNatTrans Î·.inv
  hom_inv_id := by simp [â† comp_asNatTrans]
  inv_hom_id := by simp [â† comp_asNatTrans]

variable (C D)

/-- The `functor` functor that sends an equivalence of categories to its functor. -/
@[simps!]
def functorFunctor : (C â‰Œ D) â¥¤ (C â¥¤ D) where
  obj f := f.functor
  map Î± := asNatTrans Î±

/-- Promoting `Equivalence.congrRight` to a functor. -/
@[simps]
def congrRightFunctor (E : Type*) [Category E] : (C â‰Œ D) â¥¤ ((E â¥¤ C) â‰Œ (E â¥¤ D)) where
  obj e := e.congrRight
  map {e f} Î± := mkHom <| (whiskeringRight _ _ _).map <| asNatTrans Î±

end Equivalence

end CategoryTheory
