/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Johan Commelin, Bhavik Mehta
-/
import Mathlib.CategoryTheory.Iso
import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.EqToHom

#align_import category_theory.comma from "leanprover-community/mathlib"@"8a318021995877a44630c898d0b2bc376fceef3b"

/-!
# Comma categories

A comma category is a construction in category theory, which builds a category out of two functors
with a common codomain. Specifically, for functors `L : A ‚•§ T` and `R : B ‚•§ T`, an object in
`Comma L R` is a morphism `hom : L.obj left ‚ü∂ R.obj right` for some objects `left : A` and
`right : B`, and a morphism in `Comma L R` between `hom : L.obj left ‚ü∂ R.obj right` and
`hom' : L.obj left' ‚ü∂ R.obj right'` is a commutative square

```
L.obj left  ‚ü∂  L.obj left'
      |               |
  hom |               | hom'
      ‚Üì               ‚Üì
R.obj right ‚ü∂  R.obj right',
```

where the top and bottom morphism come from morphisms `left ‚ü∂ left'` and `right ‚ü∂ right'`,
respectively.

## Main definitions

* `Comma L R`: the comma category of the functors `L` and `R`.
* `Over X`: the over category of the object `X` (developed in `Over.lean`).
* `Under X`: the under category of the object `X` (also developed in `Over.lean`).
* `Arrow T`: the arrow category of the category `T` (developed in `Arrow.lean`).

## References

* <https://ncatlab.org/nlab/show/comma+category>

## Tags

comma, slice, coslice, over, under, arrow
-/


namespace CategoryTheory

-- declare the `v`'s first; see `CategoryTheory.Category` for an explanation
universe v‚ÇÅ v‚ÇÇ v‚ÇÉ v‚ÇÑ v‚ÇÖ u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ u‚ÇÖ

variable {A : Type u‚ÇÅ} [Category.{v‚ÇÅ} A]
variable {B : Type u‚ÇÇ} [Category.{v‚ÇÇ} B]
variable {T : Type u‚ÇÉ} [Category.{v‚ÇÉ} T]

/-- The objects of the comma category are triples of an object `left : A`, an object
   `right : B` and a morphism `hom : L.obj left ‚ü∂ R.obj right`.  -/
structure Comma (L : A ‚•§ T) (R : B ‚•§ T) : Type max u‚ÇÅ u‚ÇÇ v‚ÇÉ where
  left : A
  right : B
  hom : L.obj left ‚ü∂ R.obj right
#align category_theory.comma CategoryTheory.Comma

-- Satisfying the inhabited linter
instance Comma.inhabited [Inhabited T] : Inhabited (Comma (ùü≠ T) (ùü≠ T)) where
  default :=
    { left := default
      right := default
      hom := ùüô default }
#align category_theory.comma.inhabited CategoryTheory.Comma.inhabited

variable {L : A ‚•§ T} {R : B ‚•§ T}

/-- A morphism between two objects in the comma category is a commutative square connecting the
    morphisms coming from the two objects using morphisms in the image of the functors `L` and `R`.
-/
@[ext]
structure CommaMorphism (X Y : Comma L R) where
  left : X.left ‚ü∂ Y.left
  right : X.right ‚ü∂ Y.right
  w : L.map left ‚â´ Y.hom = X.hom ‚â´ R.map right := by aesop_cat
#align category_theory.comma_morphism CategoryTheory.CommaMorphism

-- Satisfying the inhabited linter
instance CommaMorphism.inhabited [Inhabited (Comma L R)] :
    Inhabited (CommaMorphism (default : Comma L R) default) :=
    ‚ü®{ left := ùüô _, right := ùüô _}‚ü©
#align category_theory.comma_morphism.inhabited CategoryTheory.CommaMorphism.inhabited

attribute [reassoc (attr := simp)] CommaMorphism.w

instance commaCategory : Category (Comma L R) where
  Hom X Y := CommaMorphism X Y
  id X :=
    { left := ùüô X.left
      right := ùüô X.right }
  comp f g :=
    { left := f.left ‚â´ g.left
      right := f.right ‚â´ g.right }
#align category_theory.comma_category CategoryTheory.commaCategory

namespace Comma

section

variable {X Y Z : Comma L R} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z}

-- Porting note: this lemma was added because `CommaMorphism.ext`
-- was not triggered automatically
@[ext]
lemma hom_ext (f g : X ‚ü∂ Y) (h‚ÇÅ : f.left = g.left) (h‚ÇÇ : f.right = g.right) : f = g :=
  CommaMorphism.ext _ _ h‚ÇÅ h‚ÇÇ

@[simp]
theorem id_left : (ùüô X : CommaMorphism X X).left = ùüô X.left :=
  rfl
#align category_theory.comma.id_left CategoryTheory.Comma.id_left

@[simp]
theorem id_right : (ùüô X : CommaMorphism X X).right = ùüô X.right :=
  rfl
#align category_theory.comma.id_right CategoryTheory.Comma.id_right

@[simp]
theorem comp_left : (f ‚â´ g).left = f.left ‚â´ g.left :=
  rfl
#align category_theory.comma.comp_left CategoryTheory.Comma.comp_left

@[simp]
theorem comp_right : (f ‚â´ g).right = f.right ‚â´ g.right :=
  rfl
#align category_theory.comma.comp_right CategoryTheory.Comma.comp_right

end

variable (L) (R)

/-- The functor sending an object `X` in the comma category to `X.left`. -/
@[simps]
def fst : Comma L R ‚•§ A where
  obj X := X.left
  map f := f.left
#align category_theory.comma.fst CategoryTheory.Comma.fst

/-- The functor sending an object `X` in the comma category to `X.right`. -/
@[simps]
def snd : Comma L R ‚•§ B where
  obj X := X.right
  map f := f.right
#align category_theory.comma.snd CategoryTheory.Comma.snd

/-- We can interpret the commutative square constituting a morphism in the comma category as a
    natural transformation between the functors `fst ‚ãô L` and `snd ‚ãô R` from the comma category
    to `T`, where the components are given by the morphism that constitutes an object of the comma
    category. -/
@[simps]
def natTrans : fst L R ‚ãô L ‚ü∂ snd L R ‚ãô R where app X := X.hom
#align category_theory.comma.nat_trans CategoryTheory.Comma.natTrans

@[simp]
theorem eqToHom_left (X Y : Comma L R) (H : X = Y) :
    CommaMorphism.left (eqToHom H) = eqToHom (by cases H; rfl) := by
  cases H
  rfl
#align category_theory.comma.eq_to_hom_left CategoryTheory.Comma.eqToHom_left

@[simp]
theorem eqToHom_right (X Y : Comma L R) (H : X = Y) :
    CommaMorphism.right (eqToHom H) = eqToHom (by cases H; rfl) := by
  cases H
  rfl
#align category_theory.comma.eq_to_hom_right CategoryTheory.Comma.eqToHom_right

section

variable {L‚ÇÅ L‚ÇÇ L‚ÇÉ : A ‚•§ T} {R‚ÇÅ R‚ÇÇ R‚ÇÉ : B ‚•§ T}

/-- Construct an isomorphism in the comma category given isomorphisms of the objects whose forward
directions give a commutative square.
-/
@[simps]
def isoMk {X Y : Comma L‚ÇÅ R‚ÇÅ} (l : X.left ‚âÖ Y.left) (r : X.right ‚âÖ Y.right)
    (h : L‚ÇÅ.map l.hom ‚â´ Y.hom = X.hom ‚â´ R‚ÇÅ.map r.hom := by aesop_cat) : X ‚âÖ Y where
  hom :=
    { left := l.hom
      right := r.hom
      w := h }
  inv :=
    { left := l.inv
      right := r.inv
      w := by
        rw [‚Üê L‚ÇÅ.mapIso_inv l, Iso.inv_comp_eq, L‚ÇÅ.mapIso_hom, ‚Üê Category.assoc, h,
          Category.assoc, ‚Üê R‚ÇÅ.map_comp]
        simp }
#align category_theory.comma.iso_mk CategoryTheory.Comma.isoMk

/-- A natural transformation `L‚ÇÅ ‚ü∂ L‚ÇÇ` induces a functor `Comma L‚ÇÇ R ‚•§ Comma L‚ÇÅ R`. -/
@[simps]
def mapLeft (l : L‚ÇÅ ‚ü∂ L‚ÇÇ) : Comma L‚ÇÇ R ‚•§ Comma L‚ÇÅ R where
  obj X :=
    { left := X.left
      right := X.right
      hom := l.app X.left ‚â´ X.hom }
  map f :=
    { left := f.left
      right := f.right }
#align category_theory.comma.map_left CategoryTheory.Comma.mapLeft

/-- The functor `Comma L R ‚•§ Comma L R` induced by the identity natural transformation on `L` is
    naturally isomorphic to the identity functor. -/
@[simps]
def mapLeftId : mapLeft R (ùüô L) ‚âÖ ùü≠ _ where
  hom :=
    { app := fun X =>
        { left := ùüô _
          right := ùüô _ } }
  inv :=
    { app := fun X =>
        { left := ùüô _
          right := ùüô _ } }
#align category_theory.comma.map_left_id CategoryTheory.Comma.mapLeftId

/-- The functor `Comma L‚ÇÅ R ‚•§ Comma L‚ÇÉ R` induced by the composition of two natural transformations
    `l : L‚ÇÅ ‚ü∂ L‚ÇÇ` and `l' : L‚ÇÇ ‚ü∂ L‚ÇÉ` is naturally isomorphic to the composition of the two functors
    induced by these natural transformations. -/
@[simps]
def mapLeftComp (l : L‚ÇÅ ‚ü∂ L‚ÇÇ) (l' : L‚ÇÇ ‚ü∂ L‚ÇÉ) :
    mapLeft R (l ‚â´ l') ‚âÖ mapLeft R l' ‚ãô mapLeft R l where
  hom :=
    { app := fun X =>
        { left := ùüô _
          right := ùüô _ } }
  inv :=
    { app := fun X =>
        { left := ùüô _
          right := ùüô _ } }
#align category_theory.comma.map_left_comp CategoryTheory.Comma.mapLeftComp

/-- Two equal natural transformations `L‚ÇÅ ‚ü∂ L‚ÇÇ` yield naturally isomorphic functors
    `Comma L‚ÇÅ R ‚•§ Comma L‚ÇÇ R`. -/
@[simps!]
def mapLeftEq (l l' : L‚ÇÅ ‚ü∂ L‚ÇÇ) (h : l = l') : mapLeft R l ‚âÖ mapLeft R l' :=
  NatIso.ofComponents (fun X => isoMk (Iso.refl _) (Iso.refl _) (by aesop_cat)) (by aesop_cat)

/-- A natural isomorphism `L‚ÇÅ ‚âÖ L‚ÇÇ` induces an equivalence of categories
    `Comma L‚ÇÅ R ‚âå Comma L‚ÇÇ R`. -/
@[simps!]
def mapLeftIso (i : L‚ÇÅ ‚âÖ L‚ÇÇ) : Comma L‚ÇÅ R ‚âå Comma L‚ÇÇ R :=
  Equivalence.mk (mapLeft _ i.inv) (mapLeft _ i.hom)
    ((mapLeftId _ _).symm ‚â™‚â´ mapLeftEq _ _ _ i.hom_inv_id.symm ‚â™‚â´ mapLeftComp _ _ _)
    ((mapLeftComp _ _ _).symm ‚â™‚â´ mapLeftEq _ _ _ i.inv_hom_id ‚â™‚â´ mapLeftId _ _)

/-- A natural transformation `R‚ÇÅ ‚ü∂ R‚ÇÇ` induces a functor `Comma L R‚ÇÅ ‚•§ Comma L R‚ÇÇ`. -/
@[simps]
def mapRight (r : R‚ÇÅ ‚ü∂ R‚ÇÇ) : Comma L R‚ÇÅ ‚•§ Comma L R‚ÇÇ where
  obj X :=
    { left := X.left
      right := X.right
      hom := X.hom ‚â´ r.app X.right }
  map f :=
    { left := f.left
      right := f.right }
#align category_theory.comma.map_right CategoryTheory.Comma.mapRight

/-- The functor `Comma L R ‚•§ Comma L R` induced by the identity natural transformation on `R` is
    naturally isomorphic to the identity functor. -/
@[simps]
def mapRightId : mapRight L (ùüô R) ‚âÖ ùü≠ _ where
  hom :=
    { app := fun X =>
        { left := ùüô _
          right := ùüô _ } }
  inv :=
    { app := fun X =>
        { left := ùüô _
          right := ùüô _ } }
#align category_theory.comma.map_right_id CategoryTheory.Comma.mapRightId

/-- The functor `Comma L R‚ÇÅ ‚•§ Comma L R‚ÇÉ` induced by the composition of the natural transformations
    `r : R‚ÇÅ ‚ü∂ R‚ÇÇ` and `r' : R‚ÇÇ ‚ü∂ R‚ÇÉ` is naturally isomorphic to the composition of the functors
    induced by these natural transformations. -/
@[simps]
def mapRightComp (r : R‚ÇÅ ‚ü∂ R‚ÇÇ) (r' : R‚ÇÇ ‚ü∂ R‚ÇÉ) :
    mapRight L (r ‚â´ r') ‚âÖ mapRight L r ‚ãô mapRight L r' where
  hom :=
    { app := fun X =>
        { left := ùüô _
          right := ùüô _ } }
  inv :=
    { app := fun X =>
        { left := ùüô _
          right := ùüô _ } }
#align category_theory.comma.map_right_comp CategoryTheory.Comma.mapRightComp

/-- Two equal natural transformations `R‚ÇÅ ‚ü∂ R‚ÇÇ` yield naturally isomorphic functors
    `Comma L R‚ÇÅ ‚•§ Comma L R‚ÇÇ`. -/
@[simps!]
def mapRightEq (r r' : R‚ÇÅ ‚ü∂ R‚ÇÇ) (h : r = r') : mapRight L r ‚âÖ mapRight L r' :=
  NatIso.ofComponents (fun X => isoMk (Iso.refl _) (Iso.refl _) (by aesop_cat)) (by aesop_cat)

/-- A natural isomorphism `R‚ÇÅ ‚âÖ R‚ÇÇ` induces an equivalence of categories
    `Comma L R‚ÇÅ ‚âå Comma L R‚ÇÇ`. -/
@[simps!]
def mapRightIso (i : R‚ÇÅ ‚âÖ R‚ÇÇ) : Comma L R‚ÇÅ ‚âå Comma L R‚ÇÇ :=
  Equivalence.mk (mapRight _ i.hom) (mapRight _ i.inv)
    ((mapRightId _ _).symm ‚â™‚â´ mapRightEq _ _ _ i.hom_inv_id.symm ‚â™‚â´ mapRightComp _ _ _)
    ((mapRightComp _ _ _).symm ‚â™‚â´ mapRightEq _ _ _ i.inv_hom_id ‚â™‚â´ mapRightId _ _)

end

section

variable {C : Type u‚ÇÑ} [Category.{v‚ÇÑ} C] {D : Type u‚ÇÖ} [Category.{v‚ÇÖ} D]

/-- The functor `(F ‚ãô L, R) ‚•§ (L, R)` -/
@[simps]
def preLeft (F : C ‚•§ A) (L : A ‚•§ T) (R : B ‚•§ T) : Comma (F ‚ãô L) R ‚•§ Comma L R where
  obj X :=
    { left := F.obj X.left
      right := X.right
      hom := X.hom }
  map f :=
    { left := F.map f.left
      right := f.right
      w := by simpa using f.w }
#align category_theory.comma.pre_left CategoryTheory.Comma.preLeft

instance (F : C ‚•§ A) (L : A ‚•§ T) (R : B ‚•§ T) [Faithful F] : Faithful (preLeft F L R) where
  map_injective {X Y} f g h := hom_ext _ _ (F.map_injective (congrArg CommaMorphism.left h))
    (by apply congrArg CommaMorphism.right h)

instance (F : C ‚•§ A) (L : A ‚•§ T) (R : B ‚•§ T) [Full F] : Full (preLeft F L R) where
  preimage {X Y} f := CommaMorphism.mk (F.preimage f.left) f.right (by simpa using f.w)

instance (F : C ‚•§ A) (L : A ‚•§ T) (R : B ‚•§ T) [EssSurj F] : EssSurj (preLeft F L R) where
  mem_essImage Y :=
    ‚ü®Comma.mk (F.objPreimage Y.left) Y.right ((L.mapIso (F.objObjPreimageIso _)).hom ‚â´ Y.hom),
     ‚ü®isoMk (F.objObjPreimageIso _) (Iso.refl _) (by simp)‚ü©‚ü©

/-- If `F` is an equivalence, then so is `preLeft F L R`. -/
noncomputable def isEquivalencePreLeft (F : C ‚•§ A) (L : A ‚•§ T) (R : B ‚•§ T) [IsEquivalence F] :
    IsEquivalence (preLeft F L R) :=
  have := Equivalence.essSurj_of_equivalence F
  Equivalence.ofFullyFaithfullyEssSurj _

/-- The functor `(F ‚ãô L, R) ‚•§ (L, R)` -/
@[simps]
def preRight (L : A ‚•§ T) (F : C ‚•§ B) (R : B ‚•§ T) : Comma L (F ‚ãô R) ‚•§ Comma L R where
  obj X :=
    { left := X.left
      right := F.obj X.right
      hom := X.hom }
  map f :=
    { left := f.left
      right := F.map f.right }
#align category_theory.comma.pre_right CategoryTheory.Comma.preRight

instance (L : A ‚•§ T) (F : C ‚•§ B) (R : B ‚•§ T) [Faithful F] : Faithful (preRight L F R) where
  map_injective {X Y } f g h := hom_ext _ _ (by apply congrArg CommaMorphism.left h)
    (F.map_injective (congrArg CommaMorphism.right h))

instance (L : A ‚•§ T) (F : C ‚•§ B) (R : B ‚•§ T) [Full F] : Full (preRight L F R) where
  preimage {X Y} f := CommaMorphism.mk f.left (F.preimage f.right) (by simpa using f.w)

instance (L : A ‚•§ T) (F : C ‚•§ B) (R : B ‚•§ T) [EssSurj F] : EssSurj (preRight L F R) where
  mem_essImage Y :=
    ‚ü®Comma.mk Y.left (F.objPreimage Y.right) (Y.hom ‚â´ (R.mapIso (F.objObjPreimageIso _)).inv),
     ‚ü®isoMk (Iso.refl _) (F.objObjPreimageIso _) (by simp [‚Üê R.map_comp])‚ü©‚ü©

/-- If `F` is an equivalence, then so is `preRight L F R`. -/
noncomputable def isEquivalencePreRight (L : A ‚•§ T) (F : C ‚•§ B) (R : B ‚•§ T) [IsEquivalence F] :
    IsEquivalence (preRight L F R) :=
  have := Equivalence.essSurj_of_equivalence F
  Equivalence.ofFullyFaithfullyEssSurj _

/-- The functor `(L, R) ‚•§ (L ‚ãô F, R ‚ãô F)` -/
@[simps]
def post (L : A ‚•§ T) (R : B ‚•§ T) (F : T ‚•§ C) : Comma L R ‚•§ Comma (L ‚ãô F) (R ‚ãô F) where
  obj X :=
    { left := X.left
      right := X.right
      hom := F.map X.hom }
  map f :=
    { left := f.left
      right := f.right
      w := by simp only [Functor.comp_map, ‚Üê F.map_comp, f.w] }
#align category_theory.comma.post CategoryTheory.Comma.post

end

end Comma

end CategoryTheory
