/-
Copyright (c) 2024 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.Tactic.CategoryTheory.Reassoc
import Mathlib.CategoryTheory.Comma.Over.Basic

/-!
# Typeclasses for `S`-objects and `S`-morphisms

**Warning**: This is not usually how typeclasses should be used.
This is only a sensible approach when the morphism is considered as a structure on `X`,
typically in algebraic geometry.

This is analogous to to how we view ringhoms as structures via the `Algebra` typeclass.

For other applications use unbundled arrows or `CategoryTheory.Over`.

## Main definition
- `CategoryTheory.OverClass`: `OverClass X S` equips `X` with a morphism into `S`.
  `X ‚Üò S : X ‚ü∂ S` is the structure morphism.
- `CategoryTheory.HomIsOver`:
  `HomIsOver f S` asserts that `f` commutes with the structure morphisms.

-/

namespace CategoryTheory

universe v u

variable {C : Type u} [Category.{v} C]

variable {X Y Z : C} (f : X ‚ü∂ Y) (S S' : C)

/--
`OverClass X S` is the typeclass containing the data of a structure morphism `X ‚Üò S : X ‚ü∂ S`.
-/
class OverClass (X S : C) : Type v where
  ofHom ::
  /-- The structure morphism. Use `X ‚Üò S` instead. -/
  hom : X ‚ü∂ S

/--
The structure morphism `X ‚Üò S : X ‚ü∂ S` given `OverClass X S`.
The instance argument is an `optParam` instead so that it appears in the discrimination tree.
-/
def over (X S : C) (_ : OverClass X S := by infer_instance) : X ‚ü∂ S := OverClass.hom

/-- The structure morphism `X ‚Üò S : X ‚ü∂ S` given `OverClass X S`. -/
notation:90 X:90 " ‚Üò " S:90 => CategoryTheory.over X S inferInstance

/-- See Note [custom simps projection] -/
def OverClass.Simps.over (X S : C) [OverClass X S] : X ‚ü∂ S := X ‚Üò S

initialize_simps_projections OverClass (hom ‚Üí over)

/--
`X.CanonicallyOverClass S` is the typeclass containing the data of a
structure morphism `X ‚Üò S : X ‚ü∂ S`,
and that `S` is (uniquely) inferable from the structure of `X`.
-/
class CanonicallyOverClass (X : C) (S : semiOutParam C) extends OverClass X S where

/-- See Note [custom simps projection] -/
def CanonicallyOverClass.Simps.over (X S : C) [CanonicallyOverClass X S] : X ‚ü∂ S := X ‚Üò S

initialize_simps_projections CanonicallyOverClass (hom ‚Üí over)

@[simps]
instance : OverClass X X := ‚ü®ùüô _‚ü©

instance : IsIso (S ‚Üò S) := inferInstanceAs (IsIso (ùüô S))

namespace CanonicallyOverClass
-- This cannot be a simp lemma be cause it loops with `comp_over`.
@[simps -isSimp]
instance (priority := 900) [CanonicallyOverClass X Y] [OverClass Y S] : OverClass X S :=
  ‚ü®X ‚Üò Y ‚â´ Y ‚Üò S‚ü©
end CanonicallyOverClass

/-- Given `OverClass X S` and `OverClass Y S` and `f : X ‚ü∂ Y`,
`HomIsOver f S` is the typeclass asserting `f` commutes with the structure morphisms. -/
class HomIsOver (f : X ‚ü∂ Y) (S : C) [OverClass X S] [OverClass Y S] : Prop where
  comp_over : f ‚â´ Y ‚Üò S = X ‚Üò S := by aesop

@[reassoc (attr := simp)]
lemma comp_over [OverClass X S] [OverClass Y S] [HomIsOver f S] :
    f ‚â´ Y ‚Üò S = X ‚Üò S :=
  HomIsOver.comp_over

instance [OverClass X S] : HomIsOver (ùüô X) S where

instance [OverClass X S] [OverClass Y S] [OverClass Z S]
    (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [HomIsOver f S] [HomIsOver g S] :
    HomIsOver (f ‚â´ g) S where

/-- `Scheme.IsOverTower X Y S` is the typeclass asserting that the structure morphisms
`X ‚Üò Y`, `Y ‚Üò S`, and `X ‚Üò S` commute. -/
abbrev IsOverTower (X Y S : C) [OverClass X S] [OverClass Y S] [OverClass X Y] :=
  HomIsOver (X ‚Üò Y) S

instance [OverClass X S] : IsOverTower X X S where
instance [OverClass X S] : IsOverTower X S S where

instance [CanonicallyOverClass X Y] [OverClass Y S] : IsOverTower X Y S :=
  ‚ü®rfl‚ü©

lemma homIsOver_of_isOverTower [OverClass X S] [OverClass X S'] [OverClass Y S]
    [OverClass Y S'] [OverClass S S']
    [IsOverTower X S S'] [IsOverTower Y S S'] [HomIsOver f S] : HomIsOver f S' := by
  constructor
  rw [‚Üê comp_over (Y ‚Üò S), comp_over_assoc f, comp_over]

instance [CanonicallyOverClass X S]
    [OverClass X S'] [OverClass Y S] [OverClass Y S'] [OverClass S S']
    [IsOverTower X S S'] [IsOverTower Y S S'] [HomIsOver f S] : HomIsOver f S' :=
  homIsOver_of_isOverTower f S S'

instance [OverClass X S]
    [OverClass X S'] [CanonicallyOverClass Y S] [OverClass Y S'] [OverClass S S']
    [IsOverTower X S S'] [IsOverTower Y S S'] [HomIsOver f S] : HomIsOver f S' :=
  homIsOver_of_isOverTower f S S'

variable (X) in
/-- Bundle `X` with an `OverClass X S` instance into `Over S`. -/
@[simps! hom left]
def OverClass.asOver [OverClass X S] : Over S := Over.mk (X ‚Üò S)

/-- Bundle a morphism `f : X ‚ü∂ Y` with `HomIsOver f S` into a morphism in `Over S`. -/
@[simps! left]
def OverClass.asOverHom [OverClass X S] [OverClass Y S] (f : X ‚ü∂ Y) [HomIsOver f S] :
    OverClass.asOver X S ‚ü∂ OverClass.asOver Y S :=
  Over.homMk f (comp_over f S)

@[simps]
instance OverClass.fromOver {S : C} (X : Over S) : OverClass X.left S where
  hom := X.hom

instance {S : C} {X Y : Over S} (f : X ‚ü∂ Y) : HomIsOver f.left S where
  comp_over := Over.w f

end CategoryTheory
