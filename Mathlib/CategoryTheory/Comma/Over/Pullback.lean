/-
Copyright (c) 2021 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Andrew Yang
-/

import Mathlib.CategoryTheory.Adjunction.Mates
import Mathlib.CategoryTheory.Limits.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.HasPullback
import Mathlib.CategoryTheory.Monad.Products

/-!
# Adjunctions related to the over category

In a category with pullbacks, for any morphism `f : X ‚ü∂ Y`, the functor
`Over.map f : Over X ‚•§ Over Y` has a right adjoint `Over.pullback f`.

In a category with binary products, for any object `X` the functor
`Over.forget X : Over X ‚•§ C` has a right adjoint `Over.star X`.

## Main declarations

- `Over.pullback f : Over Y ‚•§ Over X` is the functor induced by a morphism `f : X ‚ü∂ Y`.
- `Over.mapPullbackAdj` is the adjunction `Over.map f ‚ä£ Over.pullback f`.
- `star : C ‚•§ Over X` is the functor induced by an object `X`.
- `forgetAdjStar` is the adjunction  `forget X ‚ä£ star X`.

## TODO
Show `star X` itself has a right adjoint provided `C` is cartesian closed and has pullbacks.
-/

noncomputable section

universe v u

namespace CategoryTheory

open Category Limits Comonad

variable {C : Type u} [Category.{v} C] (X : C)


namespace Over

open Limits

variable [HasPullbacks C]

/-- In a category with pullbacks, a morphism `f : X ‚ü∂ Y` induces a functor `Over Y ‚•§ Over X`,
by pulling back a morphism along `f`. -/
@[simps! (config := { simpRhs := true}) obj_left obj_hom map_left]
def pullback {X Y : C} (f : X ‚ü∂ Y) : Over Y ‚•§ Over X where
  obj g := Over.mk (pullback.snd g.hom f)
  map := fun g {h} {k} =>
    Over.homMk (pullback.lift (pullback.fst _ _ ‚â´ k.left) (pullback.snd _ _)
      (by simp [pullback.condition]))

/-- `Over.map f` is left adjoint to `Over.pullback f`. -/
@[simps! unit_app counit_app]
def mapPullbackAdj {X Y : C} (f : X ‚ü∂ Y) : Over.map f ‚ä£ pullback f :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun x y =>
        { toFun := fun u =>
            Over.homMk (pullback.lift u.left x.hom <| by simp)
          invFun := fun v => Over.homMk (v.left ‚â´ pullback.fst _ _) <| by
            simp [‚Üê Over.w v, pullback.condition]
          left_inv := by aesop_cat
          right_inv := fun v => by
            ext
            dsimp
            ext
            ¬∑ simp
            ¬∑ simpa using (Over.w v).symm } }

/-- pullback (ùüô X) : Over X ‚•§ Over X is the identity functor. -/
def pullbackId {X : C} : pullback (ùüô X) ‚âÖ ùü≠ _ :=
  conjugateIsoEquiv (mapPullbackAdj (ùüô _)) (Adjunction.id (C := Over _)) (Over.mapId _).symm

/-- pullback commutes with composition (up to natural isomorphism). -/
def pullbackComp {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    pullback (f ‚â´ g) ‚âÖ pullback g ‚ãô pullback f :=
  conjugateIsoEquiv (mapPullbackAdj _) ((mapPullbackAdj _).comp (mapPullbackAdj _))
    (Over.mapComp _ _).symm

instance pullbackIsRightAdjoint {X Y : C} (f : X ‚ü∂ Y) : (pullback f).IsRightAdjoint  :=
  ‚ü®_, ‚ü®mapPullbackAdj f‚ü©‚ü©

open Limits

/-- The category over any object `X` factors through the category over the terminal object `T`. -/
@[simps!]
noncomputable def forgetMapTerminal {T : C} (hT : IsTerminal T)  :
    forget X ‚âÖ map (hT.from X) ‚ãô (equivalenceOfIsTerminal hT).functor :=
  NatIso.ofComponents fun X ‚Ü¶ .refl _

section HasBinaryProducts
variable [HasBinaryProducts C]

/--
The functor from `C` to `Over X` which sends `Y : C` to `œÄ‚ÇÅ : X ‚®Ø Y ‚ü∂ X`, sometimes denoted `X*`.
-/
@[simps! obj_left obj_hom map_left]
def star : C ‚•§ Over X := cofree _ ‚ãô coalgebraToOver X

/-- The functor `Over.forget X : Over X ‚•§ C` has a right adjoint given by `star X`.

Note that the binary products assumption is necessary: the existence of a right adjoint to
`Over.forget X` is equivalent to the existence of each binary product `X ‚®Ø -`.
-/
def forgetAdjStar : forget X ‚ä£ star X := (coalgebraEquivOver X).symm.toAdjunction.comp (adj _)

instance : (star X).IsRightAdjoint := ‚ü®_, ‚ü®forgetAdjStar X‚ü©‚ü©

/-- Note that the binary products assumption is necessary: the existence of a right adjoint to
`Over.forget X` is equivalent to the existence of each binary product `X ‚®Ø -`. -/
instance : (forget X).IsLeftAdjoint := ‚ü®_, ‚ü®forgetAdjStar X‚ü©‚ü©

end HasBinaryProducts
end Over

namespace Under

variable [HasPushouts C]

/-- When `C` has pushouts, a morphism `f : X ‚ü∂ Y` induces a functor `Under X ‚•§ Under Y`,
by pushing a morphism forward along `f`. -/
@[simps]
def pushout {X Y : C} (f : X ‚ü∂ Y) : Under X ‚•§ Under Y where
  obj x := Under.mk (pushout.inr x.hom f)
  map := fun x {x'} {u} =>
    Under.homMk (pushout.desc (u.right ‚â´ pushout.inl _ _) (pushout.inr _ _)
      (by simp [‚Üê pushout.condition]))

/-- `Under.pushout f` is left adjoint to `Under.map f`. -/
@[simps! unit_app counit_app]
def mapPushoutAdj {X Y : C} (f : X ‚ü∂ Y) : pushout f ‚ä£ map f :=
  Adjunction.mkOfHomEquiv {
    homEquiv := fun x y => {
      toFun := fun u => Under.homMk (pushout.inl _ _ ‚â´ u.right) <| by
        simp only [map_obj_hom]
        rw [‚Üê Under.w u]
        simp only [Functor.const_obj_obj, map_obj_right, Functor.id_obj, pushout_obj, mk_right,
          mk_hom]
        rw [‚Üê assoc, ‚Üê assoc, pushout.condition]
      invFun := fun v => Under.homMk (pushout.desc v.right y.hom <| by simp)
      left_inv := fun u => by
        ext
        dsimp
        ext
        ¬∑ simp
        ¬∑ simpa using (Under.w u).symm
      right_inv := by aesop_cat
    }
  }

/-- pushout (ùüô X) : Under X ‚•§ Under X is the identity functor. -/
def pushoutId {X : C} : pushout (ùüô X) ‚âÖ ùü≠ _ :=
  (conjugateIsoEquiv (Adjunction.id (C := Under _)) (mapPushoutAdj (ùüô _)) ).symm
    (Under.mapId X).symm

/-- pushout commutes with composition (up to natural isomorphism). -/
def pushoutComp {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : pushout (f ‚â´ g) ‚âÖ pushout f ‚ãô pushout g :=
  (conjugateIsoEquiv ((mapPushoutAdj _).comp (mapPushoutAdj _)) (mapPushoutAdj _) ).symm
    (mapComp f g).symm

@[deprecated (since := "2025-04-15")]
noncomputable alias pullbackComp := pushoutComp

instance pushoutIsLeftAdjoint {X Y : C} (f : X ‚ü∂ Y) : (pushout f).IsLeftAdjoint  :=
  ‚ü®_, ‚ü®mapPushoutAdj f‚ü©‚ü©

/-- The category under any object `X` factors through the category under the initial object `I`. -/
@[simps!]
noncomputable def forgetMapInitial {I : C} (hI : IsInitial I) :
    forget X ‚âÖ map (hI.to X) ‚ãô (equivalenceOfIsInitial hI).functor :=
  NatIso.ofComponents fun X ‚Ü¶ .refl _

section HasBinaryCoproducts
variable [HasBinaryCoproducts C]

/-- The functor from `C` to `Under X` which sends `Y : C` to `in‚ÇÅ : X ‚ü∂ X ‚®ø Y`. -/
@[simps! obj_left obj_hom map_left]
def costar : C ‚•§ Under X := Monad.free _ ‚ãô algebraToUnder X

/-- The functor `Under.forget X : Under X ‚•§ C` has a left adjoint given by `costar X`.

Note that the binary coproducts assumption is necessary: the existence of a left adjoint to
`Under.forget X` is equivalent to the existence of each binary coproduct `X ‚®ø -`. -/
def costarAdjForget : costar X ‚ä£ forget X := (Monad.adj _).comp (algebraEquivUnder X).toAdjunction

instance : (costar X).IsLeftAdjoint := ‚ü®_, ‚ü®costarAdjForget X‚ü©‚ü©

/-- Note that the binary coproducts assumption is necessary: the existence of a left adjoint to
`Under.forget X` is equivalent to the existence of each binary coproduct `X ‚®ø -`. -/
instance : (forget X).IsRightAdjoint := ‚ü®_, ‚ü®costarAdjForget X‚ü©‚ü©

end HasBinaryCoproducts
end Under

end CategoryTheory
