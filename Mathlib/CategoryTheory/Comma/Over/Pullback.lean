/-
Copyright (c) 2021 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Andrew Yang
-/

import Mathlib.CategoryTheory.Adjunction.Mates
import Mathlib.CategoryTheory.Limits.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.HasPullback
import Mathlib.CategoryTheory.Monad.Products

/-!
# Adjunctions related to the over category

In a category with pullbacks, for any morphism `f : X âŸ¶ Y`, the functor
`Over.map f : Over X â¥¤ Over Y` has a right adjoint `Over.pullback f`.

In a category with binary products, for any object `X` the functor
`Over.forget X : Over X â¥¤ C` has a right adjoint `Over.star X`.

## Main declarations

- `Over.pullback f : Over Y â¥¤ Over X` is the functor induced by a morphism `f : X âŸ¶ Y`.
- `Over.mapPullbackAdj` is the adjunction `Over.map f âŠ£ Over.pullback f`.
- `star : C â¥¤ Over X` is the functor induced by an object `X`.
- `forgetAdjStar` is the adjunction  `forget X âŠ£ star X`.

## TODO
Show `star X` itself has a right adjoint provided `C` is cartesian closed and has pullbacks.
-/

noncomputable section

universe v u

namespace CategoryTheory

open Category Limits Comonad

variable {C : Type u} [Category.{v} C] (X : C)


namespace Over

open Limits

variable [HasPullbacks C]

/-- In a category with pullbacks, a morphism `f : X âŸ¶ Y` induces a functor `Over Y â¥¤ Over X`,
by pulling back a morphism along `f`. -/
@[simps! (config := { simpRhs := true}) obj_left obj_hom map_left]
def pullback {X Y : C} (f : X âŸ¶ Y) : Over Y â¥¤ Over X where
  obj g := Over.mk (pullback.snd g.hom f)
  map := fun g {h} {k} =>
    Over.homMk (pullback.lift (pullback.fst _ _ â‰« k.left) (pullback.snd _ _)
      (by simp [pullback.condition]))

/-- `Over.map f` is left adjoint to `Over.pullback f`. -/
@[simps! unit_app counit_app]
def mapPullbackAdj {X Y : C} (f : X âŸ¶ Y) : Over.map f âŠ£ pullback f :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun x y =>
        { toFun := fun u =>
            Over.homMk (pullback.lift u.left x.hom <| by simp)
          invFun := fun v => Over.homMk (v.left â‰« pullback.fst _ _) <| by
            simp [â† Over.w v, pullback.condition]
          left_inv := by aesop_cat
          right_inv := fun v => by
            ext
            dsimp
            ext
            Â· simp
            Â· simpa using (Over.w v).symm } }

/-- pullback (ğŸ™ X) : Over X â¥¤ Over X is the identity functor. -/
def pullbackId {X : C} : pullback (ğŸ™ X) â‰… ğŸ­ _ :=
  conjugateIsoEquiv (mapPullbackAdj (ğŸ™ _)) (Adjunction.id (C := Over _)) (Over.mapId _).symm

/-- pullback commutes with composition (up to natural isomorphism). -/
def pullbackComp {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    pullback (f â‰« g) â‰… pullback g â‹™ pullback f :=
  conjugateIsoEquiv (mapPullbackAdj _) ((mapPullbackAdj _).comp (mapPullbackAdj _))
    (Over.mapComp _ _).symm

instance pullbackIsRightAdjoint {X Y : C} (f : X âŸ¶ Y) : (pullback f).IsRightAdjoint  :=
  âŸ¨_, âŸ¨mapPullbackAdj fâŸ©âŸ©

open Limits

/-- If `T : C` is terminal, then the over category of `T` is equivalent to `C`. -/
@[simps!]
noncomputable def forgetMapTerminal {T : C} (hT : IsTerminal T)  :
    forget _ â‰… map (hT.from X) â‹™ (equivalenceOfIsTerminal hT).functor :=
  NatIso.ofComponents fun X â†¦ .refl _

section HasBinaryProducts
variable [HasBinaryProducts C]

/--
The functor from `C` to `Over X` which sends `Y : C` to `Ï€â‚ : X â¨¯ Y âŸ¶ X`, sometimes denoted `X*`.
-/
@[simps! obj_left obj_hom map_left]
def star : C â¥¤ Over X := cofree _ â‹™ coalgebraToOver X

/-- The functor `Over.forget X : Over X â¥¤ C` has a right adjoint given by `star X`.

Note that the binary products assumption is necessary: the existence of a right adjoint to
`Over.forget X` is equivalent to the existence of each binary product `X â¨¯ -`.
-/
def forgetAdjStar : forget X âŠ£ star X := (coalgebraEquivOver X).symm.toAdjunction.comp (adj _)

instance : (star X).IsRightAdjoint := âŸ¨_, âŸ¨forgetAdjStar XâŸ©âŸ©

/-- Note that the binary products assumption is necessary: the existence of a right adjoint to
`Over.forget X` is equivalent to the existence of each binary product `X â¨¯ -`. -/
instance : (forget X).IsLeftAdjoint := âŸ¨_, âŸ¨forgetAdjStar XâŸ©âŸ©

end HasBinaryProducts
end Over

namespace Under

variable [HasPushouts C]

/-- When `C` has pushouts, a morphism `f : X âŸ¶ Y` induces a functor `Under X â¥¤ Under Y`,
by pushing a morphism forward along `f`. -/
@[simps]
def pushout {X Y : C} (f : X âŸ¶ Y) : Under X â¥¤ Under Y where
  obj x := Under.mk (pushout.inr x.hom f)
  map := fun x {x'} {u} =>
    Under.homMk (pushout.desc (u.right â‰« pushout.inl _ _) (pushout.inr _ _)
      (by simp [â† pushout.condition]))

/-- `Under.pushout f` is left adjoint to `Under.map f`. -/
@[simps! unit_app counit_app]
def mapPushoutAdj {X Y : C} (f : X âŸ¶ Y) : pushout f âŠ£ map f :=
  Adjunction.mkOfHomEquiv {
    homEquiv := fun x y => {
      toFun := fun u => Under.homMk (pushout.inl _ _ â‰« u.right) <| by
        simp only [map_obj_hom]
        rw [â† Under.w u]
        simp only [Functor.const_obj_obj, map_obj_right, Functor.id_obj, pushout_obj, mk_right,
          mk_hom]
        rw [â† assoc, â† assoc, pushout.condition]
      invFun := fun v => Under.homMk (pushout.desc v.right y.hom <| by simp)
      left_inv := fun u => by
        ext
        dsimp
        ext
        Â· simp
        Â· simpa using (Under.w u).symm
      right_inv := by aesop_cat
    }
  }

/-- pushout (ğŸ™ X) : Under X â¥¤ Under X is the identity functor. -/
def pushoutId {X : C} : pushout (ğŸ™ X) â‰… ğŸ­ _ :=
  (conjugateIsoEquiv (Adjunction.id (C := Under _)) (mapPushoutAdj (ğŸ™ _)) ).symm
    (Under.mapId X).symm

/-- pushout commutes with composition (up to natural isomorphism). -/
def pullbackComp {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) : pushout (f â‰« g) â‰… pushout f â‹™ pushout g :=
  (conjugateIsoEquiv ((mapPushoutAdj _).comp (mapPushoutAdj _)) (mapPushoutAdj _) ).symm
    (mapComp f g).symm

instance pushoutIsLeftAdjoint {X Y : C} (f : X âŸ¶ Y) : (pushout f).IsLeftAdjoint  :=
  âŸ¨_, âŸ¨mapPushoutAdj fâŸ©âŸ©

/-- If `T : C` is initial, then the under category of `T` is equivalent to `C`. -/
@[simps!]
noncomputable def forgetMapInitial {T : C} (hT : IsInitial T) :
    forget _ â‰… map (hT.to X) â‹™ (equivalenceOfIsInitial hT).functor :=
  NatIso.ofComponents fun X â†¦ .refl _

section HasBinaryCoproducts
variable [HasBinaryCoproducts C]

/-- The functor from `C` to `Under X` which sends `Y : C` to `inâ‚ : X âŸ¶ X â¨¿ Y`. -/
@[simps! obj_left obj_hom map_left]
def costar : C â¥¤ Under X := Monad.free _ â‹™ algebraToUnder X

/-- The functor `Under.forget X : Under X â¥¤ C` has a left adjoint given by `costar X`.

Note that the binary coproducts assumption is necessary: the existence of a left adjoint to
`Under.forget X` is equivalent to the existence of each binary coproduct `X â¨¿ -`. -/
def costarAdjForget : costar X âŠ£ forget X := (Monad.adj _).comp (algebraEquivUnder X).toAdjunction

instance : (costar X).IsLeftAdjoint := âŸ¨_, âŸ¨costarAdjForget XâŸ©âŸ©

/-- Note that the binary coproducts assumption is necessary: the existence of a left adjoint to
`Under.forget X` is equivalent to the existence of each binary coproduct `X â¨¿ -`. -/
instance : (forget X).IsRightAdjoint := âŸ¨_, âŸ¨costarAdjForget XâŸ©âŸ©

end HasBinaryCoproducts
end Under

end CategoryTheory
