import Mathlib.CategoryTheory.Comma.StructuredArrow

namespace CategoryTheory

open Category

namespace Comma

variable {Câ‚ Câ‚‚ D Câ‚' Câ‚‚' D' : Type*} [Category Câ‚] [Category Câ‚‚] [Category D]
  [Category Câ‚'] [Category Câ‚‚'] [Category D']

variable {L : Câ‚ â¥¤ D} {R : Câ‚‚ â¥¤ D} {L' : Câ‚' â¥¤ D'} {R' : Câ‚‚' â¥¤ D'}
  {Fâ‚ : Câ‚ â¥¤ Câ‚'} {Fâ‚‚ : Câ‚‚ â¥¤ Câ‚‚'} {F : D â¥¤ D'}
  (Î± : Fâ‚ â‹™ L' âŸ¶ L â‹™ F)
  (Î² : R â‹™ F âŸ¶ Fâ‚‚ â‹™ R')

@[simps!]
def map : Comma L R â¥¤ Comma L' R' where
  obj X :=
    { left := Fâ‚.obj X.left
      right := Fâ‚‚.obj X.right
      hom := Î±.app X.left â‰« F.map X.hom â‰« Î².app X.right }
  map {X Y} Ï† :=
    { left := Fâ‚.map Ï†.left
      right := Fâ‚‚.map Ï†.right
      w := by
        dsimp
        rw [assoc, assoc]
        erw [Î±.naturality_assoc, â† Î².naturality]
        dsimp
        rw [â† F.map_comp_assoc, â† F.map_comp_assoc, Ï†.w] }

instance faithful_map [Fâ‚.Faithful] [Fâ‚‚.Faithful] : (map Î± Î²).Faithful where
  map_injective {X Y} f g h := by
    ext
    Â· exact Fâ‚.map_injective (congr_arg CommaMorphism.left h)
    Â· exact Fâ‚‚.map_injective (congr_arg CommaMorphism.right h)

instance fullMap [F.Faithful] [Fâ‚.Full] [Fâ‚‚.Full] [IsIso Î±] [IsIso Î²] : (map Î± Î²).Full where
  preimage {X Y} Ï† :=
    { left := Fâ‚.preimage Ï†.left
      right := Fâ‚‚.preimage Ï†.right
      w := F.map_injective (by
        rw [â† cancel_mono (Î².app _), â† cancel_epi (Î±.app _), F.map_comp, F.map_comp,
          assoc, assoc]
        erw [â† Î±.naturality_assoc, Î².naturality]
        dsimp
        rw [Fâ‚.image_preimage, Fâ‚‚.image_preimage]
        simpa using Ï†.w) }

instance essSurj_map [Fâ‚.EssSurj] [Fâ‚‚.EssSurj] [F.Full] [IsIso Î±] [IsIso Î²] :
    (map Î± Î²).EssSurj where
  mem_essImage X :=
    âŸ¨{  left := Fâ‚.objPreimage X.left
        right := Fâ‚‚.objPreimage X.right
        hom := F.preimage ((inv Î±).app _ â‰« L'.map (Fâ‚.objObjPreimageIso X.left).hom â‰«
          X.hom â‰« R'.map (Fâ‚‚.objObjPreimageIso X.right).inv â‰« (inv Î²).app _) },
            âŸ¨isoMk (Fâ‚.objObjPreimageIso X.left) (Fâ‚‚.objObjPreimageIso X.right) (by
              dsimp
              simp only [NatIso.isIso_inv_app, Functor.comp_obj, Functor.image_preimage, assoc,
                IsIso.inv_hom_id, comp_id, IsIso.hom_inv_id_assoc]
              rw [â† R'.map_comp, Iso.inv_hom_id, R'.map_id, comp_id])âŸ©âŸ©

noncomputable instance isEquivalenceMap
    [Fâ‚.Faithful] [Fâ‚‚.Faithful] [F.Faithful] [Fâ‚.Full] [Fâ‚‚.Full]
    [Fâ‚.EssSurj] [Fâ‚‚.EssSurj] [F.Full] [IsIso Î±] [IsIso Î²] :
    (map Î± Î²).IsEquivalence := by
  apply Functor.IsEquivalence.ofFullyFaithfullyEssSurj

end Comma

namespace StructuredArrow

variable {C D C' D' : Type*} [Category C] [Category D]
  [Category C'] [Category D']

variable {L : D} {R : C â¥¤ D} {L' : D'} {R' : C' â¥¤ D'}
  {F : C â¥¤ C'} {G : D â¥¤ D'}
  (Î± : L' âŸ¶ G.obj L)
  (Î² : R â‹™ G âŸ¶ F â‹™ R')

@[simps!]
def mapâ‚‚ : StructuredArrow L R â¥¤ StructuredArrow L' R' :=
  Comma.map (Fâ‚ := ðŸ­ (Discrete PUnit)) (Discrete.natTrans (fun _ => Î±)) Î²

instance faithful_mapâ‚‚ [F.Faithful] : (mapâ‚‚ Î± Î²).Faithful := by
  apply Comma.faithful_map

instance {I : Type*} {F G : Discrete I â¥¤ C} (f : âˆ€ i, F.obj i âŸ¶ G.obj i)
    [âˆ€ i, IsIso (f i)] :
    IsIso (Discrete.natTrans f) := by
  change IsIso (Discrete.natIso (fun i => asIso (f i))).hom
  infer_instance

instance fullMapâ‚‚ [G.Faithful] [F.Full] [IsIso Î±] [IsIso Î²] : (mapâ‚‚ Î± Î²).Full := by
  apply Comma.fullMap

instance essSurj_mapâ‚‚ [F.EssSurj] [G.Full] [IsIso Î±] [IsIso Î²] : (mapâ‚‚ Î± Î²).EssSurj := by
  apply Comma.essSurj_map

noncomputable instance isEquivalenceMapâ‚‚
    [F.Faithful] [G.Faithful] [F.EssSurj] [F.Full] [G.Full] [IsIso Î±] [IsIso Î²] :
    (mapâ‚‚ Î± Î²).IsEquivalence := by
  apply Comma.isEquivalenceMap

end StructuredArrow

end CategoryTheory
