/-
Copyright (c) 2025 Jakob von Raumer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jakob von Raumer
-/
import Mathlib.CategoryTheory.Functor.KanExtension.Adjunction
import Mathlib.CategoryTheory.Limits.Final

/-!
# Finality on Costructured Arrow categories

## References

* [M. Kashiwara, P. Schapira, *Categories and Sheaves*][Kashiwara2006], Proposition 3.1.8(i)
-/

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

namespace CategoryTheory

namespace Functor

open Limits Functor CostructuredArrow

section Small

variable {A : Type u‚ÇÅ} [SmallCategory A] {B : Type u‚ÇÅ} [SmallCategory B]
variable {T : Type u‚ÇÅ} [SmallCategory T]

attribute [local instance] Grothendieck.final_map

/-- The version of `final_of_final_costructuredArrowToOver` on small categories used to prove the
full statement. -/
private lemma final_of_final_costructuredArrowToOver_small (L : A ‚•§ T) (R : B ‚•§ T) [Final R]
    [‚àÄ b : B, Final (CostructuredArrow.toOver L (R.obj b))] : Final L := by
  rw [final_iff_isIso_colimit_pre]
  intro G
  have : ‚àÄ (b : B), Final ((whiskerLeft R (preFunctor L (ùü≠ T))).app b) := fun b ‚Ü¶
    inferInstanceAs (Final (CostructuredArrow.toOver L (R.obj b)))
  let i : colimit (L ‚ãô G) ‚âÖ colimit G :=
    calc colimit (L ‚ãô G) ‚âÖ colimit <| grothendieckProj L ‚ãô L ‚ãô G :=
            colimitIsoColimitGrothendieck L (L ‚ãô G)
      _ ‚âÖ colimit <| Grothendieck.pre (functor L) R ‚ãô grothendieckProj L ‚ãô L ‚ãô G :=
            (Final.colimitIso (Grothendieck.pre (functor L) R) (grothendieckProj L ‚ãô L ‚ãô G)).symm
      _ ‚âÖ colimit <| Grothendieck.map (whiskerLeft _ (preFunctor L (ùü≠ T))) ‚ãô
            grothendieckPrecompFunctorToComma (ùü≠ T) R ‚ãô Comma.fst (ùü≠ T) R ‚ãô G :=
              HasColimit.isoOfNatIso (NatIso.ofComponents (fun _ ‚Ü¶ Iso.refl _))
      _ ‚âÖ colimit <| grothendieckPrecompFunctorToComma (ùü≠ T) R ‚ãô Comma.fst (ùü≠ T) R ‚ãô G :=
            Final.colimitIso _ _
      _ ‚âÖ colimit <| Grothendieck.pre (functor (ùü≠ T)) R ‚ãô grothendieckProj (ùü≠ T) ‚ãô G :=
            HasColimit.isoOfNatIso (Iso.refl _)
      _ ‚âÖ colimit <| grothendieckProj (ùü≠ T) ‚ãô G :=
            Final.colimitIso _ _
      _ ‚âÖ colimit G := (colimitIsoColimitGrothendieck (ùü≠ T) G).symm
  convert Iso.isIso_hom i
  simp only [Iso.trans_def, comp_obj, grothendieckProj_obj, Grothendieck.pre_obj_base,
    Grothendieck.pre_obj_fiber, Iso.trans_assoc, Iso.trans_hom, Iso.symm_hom, i]
  rw [‚Üê Iso.inv_comp_eq, Iso.eq_inv_comp]
  apply colimit.hom_ext (fun _ ‚Ü¶ by simp)

end Small

variable {A : Type u‚ÇÅ} [Category.{v‚ÇÅ} A] {B : Type u‚ÇÇ} [Category.{v‚ÇÇ} B]
variable {T : Type u‚ÇÉ} [Category.{v‚ÇÉ} T]

/-- A functor `L : A ‚•§ T` is final if there is a final functor `R : B ‚•§ T` such that for all
`b : B`, the canonical functor `CostructuredArrow L (R.obj b) ‚•§ Over (R.obj b)` is final. -/
theorem final_of_final_costructuredArrowToOver (L : A ‚•§ T) (R : B ‚•§ T) [Final R]
    [hB : ‚àÄ b : B, Final (CostructuredArrow.toOver L (R.obj b))] : Final L := by
  let sA : A ‚âå AsSmall.{max u‚ÇÅ u‚ÇÇ u‚ÇÉ v‚ÇÅ v‚ÇÇ v‚ÇÉ} A := AsSmall.equiv
  let sB : B ‚âå AsSmall.{max u‚ÇÅ u‚ÇÇ u‚ÇÉ v‚ÇÅ v‚ÇÇ v‚ÇÉ} B := AsSmall.equiv
  let sT : T ‚âå AsSmall.{max u‚ÇÅ u‚ÇÇ u‚ÇÉ v‚ÇÅ v‚ÇÇ v‚ÇÉ} T := AsSmall.equiv
  let L' := sA.inverse ‚ãô L ‚ãô sT.functor
  let R' := sB.inverse ‚ãô R ‚ãô sT.functor
  have (b : _) : (CostructuredArrow.toOver L' (R'.obj b)).Final := by
    dsimp only [L', R', CostructuredArrow.toOver] at hB ‚ä¢
    let x := (sB.inverse ‚ãô R ‚ãô sT.functor).obj b
    let F'' : CostructuredArrow (sA.inverse ‚ãô L ‚ãô sT.functor) x ‚•§ CostructuredArrow (ùü≠ _) x :=
      map‚ÇÇ (F := sA.inverse) (G := sT.inverse) (whiskerLeft (sA.inverse ‚ãô L) sT.unit) (ùüô _) ‚ãô
      pre L (ùü≠ T) (R.obj _) ‚ãô map‚ÇÇ (F := sT.functor) (G := sT.functor) (ùüô _) (ùüô _)
    apply final_of_natIso (F := F'')
    have hsT (X) : sT.counitInv.app X = ùüô _ := rfl
    exact NatIso.ofComponents (fun X ‚Ü¶ CostructuredArrow.isoMk (Iso.refl _) (by simp [F'', hsT]))
  have := final_of_final_costructuredArrowToOver_small L' R'
  apply final_of_natIso (F := (sA.functor ‚ãô L' ‚ãô sT.inverse))
  exact (sA.functor.associator (sA.inverse ‚ãô L ‚ãô sT.functor) sT.inverse).symm ‚â™‚â´
    ((sA.functor.associator sA.inverse (L ‚ãô sT.functor)).symm ‚â™‚â´
      isoWhiskerRight sA.unitIso.symm _ ‚â™‚â´ (L ‚ãô sT.functor).leftUnitor).compInverseIso

end Functor

end CategoryTheory
