/-
Copyright (c) 2025 Jakob von Raumer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jakob von Raumer
-/
import Mathlib.CategoryTheory.Comma.Final
import Mathlib.CategoryTheory.Filtered.OfColimitCommutesFiniteLimit
import Mathlib.CategoryTheory.Functor.KanExtension.Adjunction
import Mathlib.CategoryTheory.Limits.FilteredColimitCommutesFiniteLimit
import Mathlib.CategoryTheory.Limits.Preserves.Grothendieck
import Mathlib.CategoryTheory.Limits.Final

/-!
# Finality on Costructured Arrow categories

## References

* [M. Kashiwara, P. Schapira, *Categories and Sheaves*][Kashiwara2006], Proposition 3.1.8(i)
-/

universe vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

namespace CategoryTheory

namespace Functor

open Limits Functor CostructuredArrow

section Small

variable {A : Type uâ‚} [SmallCategory A] {B : Type uâ‚} [SmallCategory B]
variable {T : Type uâ‚} [SmallCategory T]

attribute [local instance] Grothendieck.final_map

/-- The version of `final_of_final_CostructuredArrowtoOver` on small categories used to prove the
full statement. -/
private lemma final_of_final_CostructuredArrowtoOver_small (L : A â¥¤ T) (R : B â¥¤ T) [Final R]
    [âˆ€ b : B, Final (CostructuredArrow.toOver L (R.obj b))] : Final L := by
  rw [final_iff_isIso_colimit_pre]
  intro G
  have : âˆ€ (b : B), Final ((whiskerLeft R (functorPre L (ðŸ­ T))).app b) := fun b =>
    inferInstanceAs (Final (CostructuredArrow.toOver L (R.obj b)))
  let i : colimit (L â‹™ G) â‰… colimit G :=
  calc colimit (L â‹™ G) â‰… colimit <| grothendieckProj L â‹™ L â‹™ G :=
          colimitIsoColimitGrothendieck L (L â‹™ G)
    _ â‰… colimit <| Grothendieck.pre (functor L) R â‹™ grothendieckProj L â‹™ L â‹™ G :=
          (Final.colimitIso (Grothendieck.pre (functor L) R) (grothendieckProj L â‹™ L â‹™ G)).symm
    _ â‰… colimit <| Grothendieck.map (whiskerLeft _ (functorPre L (ðŸ­ T))) â‹™
          grothendieckPrecompFunctorToComma (ðŸ­ T) R â‹™ Comma.fst (ðŸ­ T) R â‹™ G :=
            HasColimit.isoOfNatIso (NatIso.ofComponents (fun _ => Iso.refl _))
    _ â‰… colimit <| grothendieckPrecompFunctorToComma (ðŸ­ T) R â‹™ Comma.fst (ðŸ­ T) R â‹™ G :=
          Final.colimitIso _ _
    _ â‰… colimit <| Grothendieck.pre (functor (ðŸ­ T)) R â‹™ grothendieckProj (ðŸ­ T) â‹™ G :=
          HasColimit.isoOfNatIso (Iso.refl _)
    _ â‰… colimit <| grothendieckProj (ðŸ­ T) â‹™ G :=
          Final.colimitIso _ _
    _ â‰… colimit G := (colimitIsoColimitGrothendieck (ðŸ­ T) G).symm
  convert (Iso.isIso_hom i)
  simp only [Iso.instTransIso_trans, comp_obj, grothendieckProj_obj, Grothendieck.pre_obj_base,
    Grothendieck.pre_obj_fiber, Iso.trans_assoc, Iso.trans_hom, Iso.symm_hom, i]
  rw [â† Iso.inv_comp_eq, Iso.eq_inv_comp]
  apply colimit.hom_ext (fun _ => by simp)

end Small

variable {A : Type uâ‚} [Category.{vâ‚} A] {B : Type uâ‚} [Category.{vâ‚‚} B]
variable {T : Type uâ‚} [Category.{vâ‚ƒ} T]

/-- A functor `L : A â¥¤ T` is final if there is a final functor `R : B â¥¤ T` such that for all
`b : B`, the canonical functor `CostructuredArrow L (R.obj b) â¥¤ Over (R.obj b)` is final. -/
private lemma final_of_final_CostructuredArrowtoOver (L : A â¥¤ T) (R : B â¥¤ T) [Final R]
    [hB : âˆ€ b : B, Final (CostructuredArrow.toOver L (R.obj b))] : Final L := by
  let sA : A â‰Œ AsSmall.{max uâ‚ uâ‚‚ uâ‚ƒ vâ‚ vâ‚‚ vâ‚ƒ} A := AsSmall.equiv
  let sB : B â‰Œ AsSmall.{max uâ‚ uâ‚‚ uâ‚ƒ vâ‚ vâ‚‚ vâ‚ƒ} B := AsSmall.equiv
  let sT : T â‰Œ AsSmall.{max uâ‚ uâ‚‚ uâ‚ƒ vâ‚ vâ‚‚ vâ‚ƒ} T := AsSmall.equiv
  let L' := sA.inverse â‹™ L â‹™ sT.functor
  let R' := sB.inverse â‹™ R â‹™ sT.functor
  have : âˆ€ (b : AsSmall B), (CostructuredArrow.toOver L' (R'.obj b)).Final := fun b => by
    dsimp only [L', R', CostructuredArrow.toOver] at hB âŠ¢
    let x := (sB.inverse â‹™ R â‹™ sT.functor).obj b
    let F := pre sA.inverse (L â‹™ sT.functor) x â‹™ pre L _ x â‹™ pre sT.functor (ðŸ­ _) x
    have : (pre sT.functor (ðŸ­ _) x).IsEquivalence := Comma.isEquivalence_preLeft _ _ _
    have : (pre L (ðŸ­ T) (AsSmall.down.obj x)).Final := hB _
    have : (pre L sT.functor x).Final := by
      apply final_of_natIso (F := mapâ‚‚ (S := L â‹™ sT.functor)  (F := ðŸ­ _) (G := sT.inverse)
        (whiskerLeft L sT.unitIso.hom) (ðŸ™ _) â‹™ pre L (ðŸ­ T) (AsSmall.down.obj x) â‹™
        mapâ‚‚ (F := ðŸ­ _) (G := sT.functor) (ðŸ™ _) (ðŸ™ _))
      apply NatIso.ofComponents (fun x => by {
        apply eqToIso
        rcases x
        simp [pre, Comma.preLeft, map, mapâ‚‚, Comma.map]
        congr
        simp
        change _ â‰« eqToHom (by { simp [sT, AsSmall.down, AsSmall.up]; congr }) = _
        apply eq_of_heq
        simp })
    apply final_of_natIso (F := F) (Iso.refl _)
  have := final_of_final_CostructuredArrowtoOver_small L' R'
  apply final_of_natIso (F := (sA.functor â‹™ L' â‹™ sT.inverse))
  exact (sA.functor.associator (sA.inverse â‹™ L â‹™ sT.functor) sT.inverse).symm â‰ªâ‰«
    ((sA.functor.associator sA.inverse (L â‹™ sT.functor)).symm â‰ªâ‰«
      isoWhiskerRight sA.unitIso.symm _ â‰ªâ‰« (L â‹™ sT.functor).leftUnitor).compInverseIso

end Functor

end CategoryTheory
