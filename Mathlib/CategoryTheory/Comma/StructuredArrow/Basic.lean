/-
Copyright (c) 2021 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz, Kim Morrison
-/
import Mathlib.CategoryTheory.Comma.Basic
import Mathlib.CategoryTheory.PUnit
import Mathlib.CategoryTheory.Limits.Shapes.IsTerminal
import Mathlib.CategoryTheory.Functor.EpiMono

/-!
# The category of "structured arrows"

For `T : C ‚•§ D`, a `T`-structured arrow with source `S : D`
is just a morphism `S ‚ü∂ T.obj Y`, for some `Y : C`.

These form a category with morphisms `g : Y ‚ü∂ Y'` making the obvious diagram commute.

We prove that `ùüô (T.obj Y)` is the initial object in `T`-structured objects with source `T.obj Y`.
-/


namespace CategoryTheory

-- morphism levels before object levels. See note [CategoryTheory universes].
universe v‚ÇÅ v‚ÇÇ v‚ÇÉ v‚ÇÑ v‚ÇÖ v‚ÇÜ u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ u‚ÇÖ u‚ÇÜ

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]

/-- The category of `T`-structured arrows with domain `S : D` (here `T : C ‚•§ D`),
has as its objects `D`-morphisms of the form `S ‚ü∂ T Y`, for some `Y : C`,
and morphisms `C`-morphisms `Y ‚ü∂ Y'` making the obvious triangle commute.
-/
-- We explicitly come from `PUnit.{1}` here to obtain the correct universe for morphisms of
-- structured arrows.
def StructuredArrow (S : D) (T : C ‚•§ D) :=
  Comma (Functor.fromPUnit.{0} S) T

/-- See through the type synonym `StructuredArrow S T = Comma _ _`. -/
instance (S : D) (T : C ‚•§ D) : Category (StructuredArrow S T) :=
  inferInstanceAs <| Category (Comma _ _)

namespace StructuredArrow

/-- The obvious projection functor from structured arrows. -/
@[simps!]
def proj (S : D) (T : C ‚•§ D) : StructuredArrow S T ‚•§ C :=
  Comma.snd _ _

variable {S S' S'' : D} {Y Y' Y'' : C} {T T' : C ‚•§ D}

@[ext]
lemma hom_ext {X Y : StructuredArrow S T} (f g : X ‚ü∂ Y) (h : f.right = g.right) : f = g :=
  CommaMorphism.ext (Subsingleton.elim _ _) h

@[simp]
theorem hom_eq_iff {X Y : StructuredArrow S T} (f g : X ‚ü∂ Y) : f = g ‚Üî f.right = g.right :=
  ‚ü®fun h ‚Ü¶ by rw [h], hom_ext _ _‚ü©

/-- Construct a structured arrow from a morphism. -/
def mk (f : S ‚ü∂ T.obj Y) : StructuredArrow S T :=
  ‚ü®‚ü®‚ü®‚ü©‚ü©, Y, f‚ü©

@[simp]
theorem mk_left (f : S ‚ü∂ T.obj Y) : (mk f).left = ‚ü®‚ü®‚ü©‚ü© :=
  rfl

@[simp]
theorem mk_right (f : S ‚ü∂ T.obj Y) : (mk f).right = Y :=
  rfl

@[simp]
theorem mk_hom_eq_self (f : S ‚ü∂ T.obj Y) : (mk f).hom = f :=
  rfl

@[reassoc (attr := simp)]
theorem w {A B : StructuredArrow S T} (f : A ‚ü∂ B) : A.hom ‚â´ T.map f.right = B.hom := by
  have := f.w; cat_disch

@[simp]
theorem comp_right {X Y Z : StructuredArrow S T} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).right = f.right ‚â´ g.right := rfl

@[simp]
theorem id_right (X : StructuredArrow S T) : (ùüô X : X ‚ü∂ X).right = ùüô X.right := rfl

@[simp]
theorem eqToHom_right {X Y : StructuredArrow S T} (h : X = Y) :
    (eqToHom h).right = eqToHom (by rw [h]) := by
  subst h
  simp only [eqToHom_refl, id_right]

@[simp]
theorem left_eq_id {X Y : StructuredArrow S T} (f : X ‚ü∂ Y) : f.left = ùüô X.left := rfl

/-- To construct a morphism of structured arrows,
we need a morphism of the objects underlying the target,
and to check that the triangle commutes.
-/
@[simps right]
def homMk {f f' : StructuredArrow S T} (g : f.right ‚ü∂ f'.right)
    (w : f.hom ‚â´ T.map g = f'.hom := by cat_disch) : f ‚ü∂ f' where
  left := ùüô f.left
  right := g
  w := by
    dsimp
    simpa using w.symm

theorem homMk_surjective {f f' : StructuredArrow S T} (œÜ : f ‚ü∂ f') :
    ‚àÉ (œà : f.right ‚ü∂ f'.right) (hœà : f.hom ‚â´ T.map œà = f'.hom),
      œÜ = StructuredArrow.homMk œà hœà :=
  ‚ü®œÜ.right, StructuredArrow.w œÜ, rfl‚ü©

/-- Given a structured arrow `X ‚ü∂ T(Y)`, and an arrow `Y ‚ü∂ Y'`, we can construct a morphism of
structured arrows given by `(X ‚ü∂ T(Y)) ‚ü∂ (X ‚ü∂ T(Y) ‚ü∂ T(Y'))`. -/
@[simps]
def homMk' (f : StructuredArrow S T) (g : f.right ‚ü∂ Y') : f ‚ü∂ mk (f.hom ‚â´ T.map g) where
  left := ùüô _
  right := g

lemma homMk'_id (f : StructuredArrow S T) : homMk' f (ùüô f.right) = eqToHom (by cat_disch) := by
  simp [eqToHom_right]

lemma homMk'_mk_id (f : S ‚ü∂ T.obj Y) : homMk' (mk f) (ùüô Y) = eqToHom (by simp) :=
  homMk'_id _

lemma homMk'_comp (f : StructuredArrow S T) (g : f.right ‚ü∂ Y') (g' : Y' ‚ü∂ Y'') :
    homMk' f (g ‚â´ g') = homMk' f g ‚â´ homMk' (mk (f.hom ‚â´ T.map g)) g' ‚â´ eqToHom (by simp) := by
  simp [eqToHom_right]

lemma homMk'_mk_comp (f : S ‚ü∂ T.obj Y) (g : Y ‚ü∂ Y') (g' : Y' ‚ü∂ Y'') :
    homMk' (mk f) (g ‚â´ g') = homMk' (mk f) g ‚â´ homMk' (mk (f ‚â´ T.map g)) g' ‚â´ eqToHom (by simp) :=
  homMk'_comp _ _ _

/-- Variant of `homMk'` where both objects are applications of `mk`. -/
@[simps]
def mkPostcomp (f : S ‚ü∂ T.obj Y) (g : Y ‚ü∂ Y') : mk f ‚ü∂ mk (f ‚â´ T.map g) where
  left := ùüô _
  right := g

lemma mkPostcomp_id (f : S ‚ü∂ T.obj Y) : mkPostcomp f (ùüô Y) = eqToHom (by simp) := by simp
lemma mkPostcomp_comp (f : S ‚ü∂ T.obj Y) (g : Y ‚ü∂ Y') (g' : Y' ‚ü∂ Y'') :
    mkPostcomp f (g ‚â´ g') = mkPostcomp f g ‚â´ mkPostcomp (f ‚â´ T.map g) g' ‚â´ eqToHom (by simp) := by
  simp

/-- To construct an isomorphism of structured arrows,
we need an isomorphism of the objects underlying the target,
and to check that the triangle commutes.
-/
@[simps! hom_right inv_right]
def isoMk {f f' : StructuredArrow S T} (g : f.right ‚âÖ f'.right)
    (w : f.hom ‚â´ T.map g.hom = f'.hom := by cat_disch) :
    f ‚âÖ f' :=
  Comma.isoMk (eqToIso (by ext)) g (by simpa using w.symm)

theorem obj_ext (x y : StructuredArrow S T) (hr : x.right = y.right)
    (hh : x.hom ‚â´ T.map (eqToHom hr) = y.hom) : x = y := by
  cases x
  cases y
  cases hr
  cat_disch

theorem ext {A B : StructuredArrow S T} (f g : A ‚ü∂ B) : f.right = g.right ‚Üí f = g :=
  CommaMorphism.ext (Subsingleton.elim _ _)

theorem ext_iff {A B : StructuredArrow S T} (f g : A ‚ü∂ B) : f = g ‚Üî f.right = g.right :=
  ‚ü®fun h => h ‚ñ∏ rfl, ext f g‚ü©

instance proj_faithful : (proj S T).Faithful where
  map_injective {_ _} := ext

/-- The converse of this is true with additional assumptions, see `mono_iff_mono_right`. -/
theorem mono_of_mono_right {A B : StructuredArrow S T} (f : A ‚ü∂ B) [h : Mono f.right] : Mono f :=
  (proj S T).mono_of_mono_map h

theorem epi_of_epi_right {A B : StructuredArrow S T} (f : A ‚ü∂ B) [h : Epi f.right] : Epi f :=
  (proj S T).epi_of_epi_map h

instance mono_homMk {A B : StructuredArrow S T} (f : A.right ‚ü∂ B.right) (w) [h : Mono f] :
    Mono (homMk f w) :=
  (proj S T).mono_of_mono_map h

instance epi_homMk {A B : StructuredArrow S T} (f : A.right ‚ü∂ B.right) (w) [h : Epi f] :
    Epi (homMk f w) :=
  (proj S T).epi_of_epi_map h

/-- Eta rule for structured arrows. Prefer `StructuredArrow.eta` for rewriting, since equality of
objects tends to cause problems. -/
theorem eq_mk (f : StructuredArrow S T) : f = mk f.hom :=
  rfl

/-- Eta rule for structured arrows. -/
@[simps! hom_right inv_right]
def eta (f : StructuredArrow S T) : f ‚âÖ mk f.hom :=
  isoMk (Iso.refl _)

lemma mk_surjective (f : StructuredArrow S T) :
    ‚àÉ (Y : C) (g : S ‚ü∂ T.obj Y), f = mk g :=
  ‚ü®_, _, eq_mk f‚ü©

/-- A morphism between source objects `S ‚ü∂ S'`
contravariantly induces a functor between structured arrows,
`StructuredArrow S' T ‚•§ StructuredArrow S T`.

Ideally this would be described as a 2-functor from `D`
(promoted to a 2-category with equations as 2-morphisms)
to `Cat`.
-/
@[simps!]
def map (f : S ‚ü∂ S') : StructuredArrow S' T ‚•§ StructuredArrow S T :=
  Comma.mapLeft _ ((Functor.const _).map f)

@[simp]
theorem map_mk {f : S' ‚ü∂ T.obj Y} (g : S ‚ü∂ S') : (map g).obj (mk f) = mk (g ‚â´ f) :=
  rfl

@[simp]
theorem map_id {f : StructuredArrow S T} : (map (ùüô S)).obj f = f := by
  rw [eq_mk f]
  simp

@[simp]
theorem map_comp {f : S ‚ü∂ S'} {f' : S' ‚ü∂ S''} {h : StructuredArrow S'' T} :
    (map (f ‚â´ f')).obj h = (map f).obj ((map f').obj h) := by
  rw [eq_mk h]
  simp

/-- An isomorphism `S ‚âÖ S'` induces an equivalence `StructuredArrow S T ‚âå StructuredArrow S' T`. -/
@[simps!]
def mapIso (i : S ‚âÖ S') : StructuredArrow S T ‚âå StructuredArrow S' T :=
  Comma.mapLeftIso _ ((Functor.const _).mapIso i)

/-- A natural isomorphism `T ‚âÖ T'` induces an equivalence
`StructuredArrow S T ‚âå StructuredArrow S T'`. -/
@[simps!]
def mapNatIso (i : T ‚âÖ T') : StructuredArrow S T ‚âå StructuredArrow S T' :=
  Comma.mapRightIso _ i

instance proj_reflectsIsomorphisms : (proj S T).ReflectsIsomorphisms where
  reflects {Y Z} f t :=
    ‚ü®‚ü®StructuredArrow.homMk
        (inv ((proj S T).map f))
        (by rw [Functor.map_inv, IsIso.comp_inv_eq]; simp),
      by constructor <;> apply CommaMorphism.ext <;> dsimp at t ‚ä¢ <;> simp‚ü©‚ü©

open CategoryTheory.Limits

/-- The identity structured arrow is initial. -/
noncomputable def mkIdInitial [T.Full] [T.Faithful] : IsInitial (mk (ùüô (T.obj Y))) where
  desc c := homMk (T.preimage c.pt.hom)
  uniq c m _ := by
    apply CommaMorphism.ext
    ¬∑ simp
    ¬∑ apply T.map_injective
      simpa only [homMk_right, T.map_preimage, ‚Üê w m] using (Category.id_comp _).symm

variable {A : Type u‚ÇÉ} [Category.{v‚ÇÉ} A] {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B]

/-- The functor `(S, F ‚ãô G) ‚•§ (S, G)`. -/
@[simps!]
def pre (S : D) (F : B ‚•§ C) (G : C ‚•§ D) : StructuredArrow S (F ‚ãô G) ‚•§ StructuredArrow S G :=
  Comma.preRight _ F G

instance (S : D) (F : B ‚•§ C) (G : C ‚•§ D) [F.Faithful] : (pre S F G).Faithful :=
  show (Comma.preRight _ _ _).Faithful from inferInstance

instance (S : D) (F : B ‚•§ C) (G : C ‚•§ D) [F.Full] : (pre S F G).Full :=
  show (Comma.preRight _ _ _).Full from inferInstance

instance (S : D) (F : B ‚•§ C) (G : C ‚•§ D) [F.EssSurj] : (pre S F G).EssSurj :=
  show (Comma.preRight _ _ _).EssSurj from inferInstance

/-- If `F` is an equivalence, then so is the functor `(S, F ‚ãô G) ‚•§ (S, G)`. -/
instance isEquivalence_pre (S : D) (F : B ‚•§ C) (G : C ‚•§ D) [F.IsEquivalence] :
    (pre S F G).IsEquivalence :=
  Comma.isEquivalence_preRight _ _ _

/-- The functor `(S, F) ‚•§ (G(S), F ‚ãô G)`. -/
@[simps]
def post (S : C) (F : B ‚•§ C) (G : C ‚•§ D) :
    StructuredArrow S F ‚•§ StructuredArrow (G.obj S) (F ‚ãô G) where
  obj X := StructuredArrow.mk (G.map X.hom)
  map f := StructuredArrow.homMk f.right (by simp [Functor.comp_map, ‚Üê G.map_comp, ‚Üê f.w])

instance (S : C) (F : B ‚•§ C) (G : C ‚•§ D) : (post S F G).Faithful where
  map_injective {_ _} _ _ h := by simpa [ext_iff] using h

instance (S : C) (F : B ‚•§ C) (G : C ‚•§ D) [G.Faithful] : (post S F G).Full where
  map_surjective f := ‚ü®homMk f.right (G.map_injective (by simpa using f.w.symm)), by simp‚ü©

instance (S : C) (F : B ‚•§ C) (G : C ‚•§ D) [G.Full] : (post S F G).EssSurj where
  mem_essImage h := ‚ü®mk (G.preimage h.hom), ‚ü®isoMk (Iso.refl _) (by simp)‚ü©‚ü©

/-- If `G` is fully faithful, then `post S F G : (S, F) ‚•§ (G(S), F ‚ãô G)` is an equivalence. -/
instance isEquivalence_post (S : C) (F : B ‚•§ C) (G : C ‚•§ D) [G.Full] [G.Faithful] :
    (post S F G).IsEquivalence where

section

variable {L : D} {R : C ‚•§ D} {L' : B} {R' : A ‚•§ B} {F : C ‚•§ A} {G : D ‚•§ B}
  (Œ± : L' ‚ü∂ G.obj L) (Œ≤ : R ‚ãô G ‚ü∂ F ‚ãô R')

/-- The functor `StructuredArrow L R ‚•§ StructuredArrow L' R'` that is deduced from
a natural transformation `R ‚ãô G ‚ü∂ F ‚ãô R'` and a morphism `L' ‚ü∂ G.obj L.` -/
@[simps!]
def map‚ÇÇ : StructuredArrow L R ‚•§ StructuredArrow L' R' :=
  Comma.map (F‚ÇÅ := ùü≠ (Discrete PUnit)) (Discrete.natTrans (fun _ => Œ±)) Œ≤

instance faithful_map‚ÇÇ [F.Faithful] : (map‚ÇÇ Œ± Œ≤).Faithful := by
  apply Comma.faithful_map

instance full_map‚ÇÇ [G.Faithful] [F.Full] [IsIso Œ±] [IsIso Œ≤] : (map‚ÇÇ Œ± Œ≤).Full := by
  apply Comma.full_map

instance essSurj_map‚ÇÇ [F.EssSurj] [G.Full] [IsIso Œ±] [IsIso Œ≤] : (map‚ÇÇ Œ± Œ≤).EssSurj := by
  apply Comma.essSurj_map

noncomputable instance isEquivalenceMap‚ÇÇ
    [F.IsEquivalence] [G.Faithful] [G.Full] [IsIso Œ±] [IsIso Œ≤] :
    (map‚ÇÇ Œ± Œ≤).IsEquivalence := by
  apply Comma.isEquivalenceMap

/-- The composition of two applications of `map‚ÇÇ` is naturally isomorphic to a single such one. -/
def map‚ÇÇCompMap‚ÇÇIso {C' : Type u‚ÇÜ} [Category.{v‚ÇÜ} C'] {D' : Type u‚ÇÖ} [Category.{v‚ÇÖ} D']
    {L'' : D'} {R'' : C' ‚•§ D'} {F' : C' ‚•§ C} {G' : D' ‚•§ D} (Œ±' : L ‚ü∂ G'.obj L'')
    (Œ≤' : R'' ‚ãô G' ‚ü∂ F' ‚ãô R) :
    map‚ÇÇ Œ±' Œ≤' ‚ãô map‚ÇÇ Œ± Œ≤ ‚âÖ
    map‚ÇÇ (Œ± ‚â´ G.map Œ±')
      ((Functor.associator _ _ _).inv ‚â´ Functor.whiskerRight Œ≤' _ ‚â´ (Functor.associator _ _ _).hom ‚â´
        Functor.whiskerLeft _ Œ≤ ‚â´ (Functor.associator _ _ _).inv) :=
  NatIso.ofComponents (fun X => isoMk (Iso.refl _))

end

/-- `StructuredArrow.post` is a special case of `StructuredArrow.map‚ÇÇ` up to natural isomorphism. -/
def postIsoMap‚ÇÇ (S : C) (F : B ‚•§ C) (G : C ‚•§ D) :
    post S F G ‚âÖ map‚ÇÇ (F := ùü≠ _) (ùüô _) (ùüô (F ‚ãô G)) :=
  NatIso.ofComponents fun _ => isoMk <| Iso.refl _

/-- `StructuredArrow.map` is a special case of `StructuredArrow.map‚ÇÇ` up to natural isomorphism. -/
def mapIsoMap‚ÇÇ {S S' : D} (f : S ‚ü∂ S') : map (T := T) f ‚âÖ map‚ÇÇ (F := ùü≠ _) (G := ùü≠ _) f (ùüô T) :=
  NatIso.ofComponents fun _ => isoMk <| Iso.refl _

/-- `StructuredArrow.pre` is a special case of `StructuredArrow.map‚ÇÇ` up to natural isomorphism. -/
def preIsoMap‚ÇÇ (S : D) (F : B ‚•§ C) (G : C ‚•§ D) :
    pre S F G ‚âÖ map‚ÇÇ (G := ùü≠ _) (ùüô _) (ùüô (F ‚ãô G)) :=
  NatIso.ofComponents fun _ => isoMk <| Iso.refl _

/-- A structured arrow is called universal if it is initial. -/
abbrev IsUniversal (f : StructuredArrow S T) := IsInitial f

namespace IsUniversal

variable {f g : StructuredArrow S T}

theorem uniq (h : IsUniversal f) (Œ∑ : f ‚ü∂ g) : Œ∑ = h.to g :=
  h.hom_ext Œ∑ (h.to g)

/-- The family of morphisms out of a universal arrow. -/
def desc (h : IsUniversal f) (g : StructuredArrow S T) : f.right ‚ü∂ g.right :=
  (h.to g).right

/-- Any structured arrow factors through a universal arrow. -/
@[reassoc (attr := simp)]
theorem fac (h : IsUniversal f) (g : StructuredArrow S T) :
    f.hom ‚â´ T.map (h.desc g) = g.hom :=
  Category.id_comp g.hom ‚ñ∏ (h.to g).w.symm

theorem hom_desc (h : IsUniversal f) {c : C} (Œ∑ : f.right ‚ü∂ c) :
    Œ∑ = h.desc (mk <| f.hom ‚â´ T.map Œ∑) :=
  let g := mk <| f.hom ‚â´ T.map Œ∑
  congrArg CommaMorphism.right (h.hom_ext (homMk Œ∑ rfl : f ‚ü∂ g) (h.to g))

/-- Two morphisms out of a universal `T`-structured arrow are equal if their image under `T` are
equal after precomposing the universal arrow. -/
theorem hom_ext (h : IsUniversal f) {c : C} {Œ∑ Œ∑' : f.right ‚ü∂ c}
    (w : f.hom ‚â´ T.map Œ∑ = f.hom ‚â´ T.map Œ∑') : Œ∑ = Œ∑' := by
  rw [h.hom_desc Œ∑, h.hom_desc Œ∑', w]

theorem existsUnique (h : IsUniversal f) (g : StructuredArrow S T) :
    ‚àÉ! Œ∑ : f.right ‚ü∂ g.right, f.hom ‚â´ T.map Œ∑ = g.hom :=
  ‚ü®h.desc g, h.fac g, fun f w ‚Ü¶ h.hom_ext <| by simp [w]‚ü©

end IsUniversal

end StructuredArrow

/-- The category of `S`-costructured arrows with target `T : D` (here `S : C ‚•§ D`),
has as its objects `D`-morphisms of the form `S Y ‚ü∂ T`, for some `Y : C`,
and morphisms `C`-morphisms `Y ‚ü∂ Y'` making the obvious triangle commute.
-/
-- We explicitly come from `PUnit.{1}` here to obtain the correct universe for morphisms of
-- costructured arrows.
def CostructuredArrow (S : C ‚•§ D) (T : D) :=
  Comma S (Functor.fromPUnit.{0} T)

instance (S : C ‚•§ D) (T : D) : Category (CostructuredArrow S T) := commaCategory

namespace CostructuredArrow

/-- The obvious projection functor from costructured arrows. -/
@[simps!]
def proj (S : C ‚•§ D) (T : D) : CostructuredArrow S T ‚•§ C :=
  Comma.fst _ _

variable {T T' T'' : D} {Y Y' Y'' : C} {S S' : C ‚•§ D}

@[ext]
lemma hom_ext {X Y : CostructuredArrow S T} (f g : X ‚ü∂ Y) (h : f.left = g.left) : f = g :=
  CommaMorphism.ext h (Subsingleton.elim _ _)

@[simp]
theorem hom_eq_iff {X Y : CostructuredArrow S T} (f g : X ‚ü∂ Y) : f = g ‚Üî f.left = g.left :=
  ‚ü®fun h ‚Ü¶ by rw [h], hom_ext _ _‚ü©

/-- Construct a costructured arrow from a morphism. -/
def mk (f : S.obj Y ‚ü∂ T) : CostructuredArrow S T :=
  ‚ü®Y, ‚ü®‚ü®‚ü©‚ü©, f‚ü©

@[simp]
theorem mk_left (f : S.obj Y ‚ü∂ T) : (mk f).left = Y :=
  rfl

@[simp]
theorem mk_right (f : S.obj Y ‚ü∂ T) : (mk f).right = ‚ü®‚ü®‚ü©‚ü© :=
  rfl

@[simp]
theorem mk_hom_eq_self (f : S.obj Y ‚ü∂ T) : (mk f).hom = f :=
  rfl

@[reassoc]
theorem w {A B : CostructuredArrow S T} (f : A ‚ü∂ B) : S.map f.left ‚â´ B.hom = A.hom := by simp

@[simp]
theorem comp_left {X Y Z : CostructuredArrow S T} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).left = f.left ‚â´ g.left := rfl

@[simp]
theorem id_left (X : CostructuredArrow S T) : (ùüô X : X ‚ü∂ X).left = ùüô X.left := rfl

@[simp]
theorem eqToHom_left {X Y : CostructuredArrow S T} (h : X = Y) :
    (eqToHom h).left = eqToHom (by rw [h]) := by
  subst h
  simp only [eqToHom_refl, id_left]

@[simp]
theorem right_eq_id {X Y : CostructuredArrow S T} (f : X ‚ü∂ Y) : f.right = ùüô X.right := rfl

/-- To construct a morphism of costructured arrows,
we need a morphism of the objects underlying the source,
and to check that the triangle commutes.
-/
@[simps! left]
def homMk {f f' : CostructuredArrow S T} (g : f.left ‚ü∂ f'.left)
    (w : S.map g ‚â´ f'.hom = f.hom := by cat_disch) : f ‚ü∂ f' where
  left := g
  right := ùüô f.right

theorem homMk_surjective {f f' : CostructuredArrow S T} (œÜ : f ‚ü∂ f') :
    ‚àÉ (œà : f.left ‚ü∂ f'.left) (hœà : S.map œà ‚â´ f'.hom = f.hom),
      œÜ = CostructuredArrow.homMk œà hœà :=
  ‚ü®œÜ.left, CostructuredArrow.w œÜ, rfl‚ü©

/-- Given a costructured arrow `S(Y) ‚ü∂ X`, and an arrow `Y' ‚ü∂ Y'`, we can construct a morphism of
costructured arrows given by `(S(Y) ‚ü∂ X) ‚ü∂ (S(Y') ‚ü∂ S(Y) ‚ü∂ X)`. -/
@[simps]
def homMk' (f : CostructuredArrow S T) (g : Y' ‚ü∂ f.left) : mk (S.map g ‚â´ f.hom) ‚ü∂ f where
  left := g
  right := ùüô _

lemma homMk'_id (f : CostructuredArrow S T) : homMk' f (ùüô f.left) = eqToHom (by cat_disch) := by
  simp [eqToHom_left]

lemma homMk'_mk_id (f : S.obj Y ‚ü∂ T) : homMk' (mk f) (ùüô Y) = eqToHom (by simp) :=
  homMk'_id _

lemma homMk'_comp (f : CostructuredArrow S T) (g : Y' ‚ü∂ f.left) (g' : Y'' ‚ü∂ Y') :
    homMk' f (g' ‚â´ g) = eqToHom (by simp) ‚â´ homMk' (mk (S.map g ‚â´ f.hom)) g' ‚â´ homMk' f g := by
  simp [eqToHom_left]

lemma homMk'_mk_comp (f : S.obj Y ‚ü∂ T) (g : Y' ‚ü∂ Y) (g' : Y'' ‚ü∂ Y') :
    homMk' (mk f) (g' ‚â´ g) = eqToHom (by simp) ‚â´ homMk' (mk (S.map g ‚â´ f)) g' ‚â´ homMk' (mk f) g :=
  homMk'_comp _ _ _

/-- Variant of `homMk'` where both objects are applications of `mk`. -/
@[simps]
def mkPrecomp (f : S.obj Y ‚ü∂ T) (g : Y' ‚ü∂ Y) : mk (S.map g ‚â´ f) ‚ü∂ mk f where
  left := g
  right := ùüô _

lemma mkPrecomp_id (f : S.obj Y ‚ü∂ T) : mkPrecomp f (ùüô Y) = eqToHom (by simp) := by simp
lemma mkPrecomp_comp (f : S.obj Y ‚ü∂ T) (g : Y' ‚ü∂ Y) (g' : Y'' ‚ü∂ Y') :
    mkPrecomp f (g' ‚â´ g) = eqToHom (by simp) ‚â´ mkPrecomp (S.map g ‚â´ f) g' ‚â´ mkPrecomp f g := by
  simp

/-- To construct an isomorphism of costructured arrows,
we need an isomorphism of the objects underlying the source,
and to check that the triangle commutes.
-/
@[simps! hom_left inv_left]
def isoMk {f f' : CostructuredArrow S T} (g : f.left ‚âÖ f'.left)
    (w : S.map g.hom ‚â´ f'.hom = f.hom := by cat_disch) : f ‚âÖ f' :=
  Comma.isoMk g (eqToIso (by ext)) (by simpa using w)

theorem obj_ext (x y : CostructuredArrow S T) (hl : x.left = y.left)
    (hh : S.map (eqToHom hl) ‚â´ y.hom = x.hom) : x = y := by
  cases x
  cases y
  cases hl
  cat_disch

theorem ext {A B : CostructuredArrow S T} (f g : A ‚ü∂ B) (h : f.left = g.left) : f = g :=
  CommaMorphism.ext h (Subsingleton.elim _ _)

theorem ext_iff {A B : CostructuredArrow S T} (f g : A ‚ü∂ B) : f = g ‚Üî f.left = g.left :=
  ‚ü®fun h => h ‚ñ∏ rfl, ext f g‚ü©

instance proj_faithful : (proj S T).Faithful where map_injective {_ _} := ext

theorem mono_of_mono_left {A B : CostructuredArrow S T} (f : A ‚ü∂ B) [h : Mono f.left] : Mono f :=
  (proj S T).mono_of_mono_map h

/-- The converse of this is true with additional assumptions, see `epi_iff_epi_left`. -/
theorem epi_of_epi_left {A B : CostructuredArrow S T} (f : A ‚ü∂ B) [h : Epi f.left] : Epi f :=
  (proj S T).epi_of_epi_map h

instance mono_homMk {A B : CostructuredArrow S T} (f : A.left ‚ü∂ B.left) (w) [h : Mono f] :
    Mono (homMk f w) :=
  (proj S T).mono_of_mono_map h

instance epi_homMk {A B : CostructuredArrow S T} (f : A.left ‚ü∂ B.left) (w) [h : Epi f] :
    Epi (homMk f w) :=
  (proj S T).epi_of_epi_map h

/-- Eta rule for costructured arrows. Prefer `CostructuredArrow.eta` for rewriting, as equality of
objects tends to cause problems. -/
theorem eq_mk (f : CostructuredArrow S T) : f = mk f.hom :=
  rfl

/-- Eta rule for costructured arrows. -/
@[simps! hom_left inv_left]
def eta (f : CostructuredArrow S T) : f ‚âÖ mk f.hom :=
  isoMk (Iso.refl _)

lemma mk_surjective (f : CostructuredArrow S T) :
    ‚àÉ (Y : C) (g : S.obj Y ‚ü∂ T), f = mk g :=
  ‚ü®_, _, eq_mk f‚ü©

/-- A morphism between target objects `T ‚ü∂ T'`
covariantly induces a functor between costructured arrows,
`CostructuredArrow S T ‚•§ CostructuredArrow S T'`.

Ideally this would be described as a 2-functor from `D`
(promoted to a 2-category with equations as 2-morphisms)
to `Cat`.
-/
@[simps!]
def map (f : T ‚ü∂ T') : CostructuredArrow S T ‚•§ CostructuredArrow S T' :=
  Comma.mapRight _ ((Functor.const _).map f)

@[simp]
theorem map_mk {f : S.obj Y ‚ü∂ T} (g : T ‚ü∂ T') : (map g).obj (mk f) = mk (f ‚â´ g) :=
  rfl

@[simp]
theorem map_id {f : CostructuredArrow S T} : (map (ùüô T)).obj f = f := by
  rw [eq_mk f]
  simp

@[simp]
theorem map_comp {f : T ‚ü∂ T'} {f' : T' ‚ü∂ T''} {h : CostructuredArrow S T} :
    (map (f ‚â´ f')).obj h = (map f').obj ((map f).obj h) := by
  rw [eq_mk h]
  simp

/-- An isomorphism `T ‚âÖ T'` induces an equivalence
`CostructuredArrow S T ‚âå CostructuredArrow S T'`. -/
@[simps!]
def mapIso (i : T ‚âÖ T') : CostructuredArrow S T ‚âå CostructuredArrow S T' :=
  Comma.mapRightIso _ ((Functor.const _).mapIso i)

/-- A natural isomorphism `S ‚âÖ S'` induces an equivalence
`CostrucutredArrow S T ‚âå CostructuredArrow S' T`. -/
@[simps!]
def mapNatIso (i : S ‚âÖ S') : CostructuredArrow S T ‚âå CostructuredArrow S' T :=
  Comma.mapLeftIso _ i

instance proj_reflectsIsomorphisms : (proj S T).ReflectsIsomorphisms where
  reflects {Y Z} f t :=
    ‚ü®‚ü®CostructuredArrow.homMk
        (inv ((proj S T).map f))
        (by rw [Functor.map_inv, IsIso.inv_comp_eq]; simp),
      by constructor <;> ext <;> dsimp at t ‚ä¢ <;> simp‚ü©‚ü©

open CategoryTheory.Limits

/-- The identity costructured arrow is terminal. -/
noncomputable def mkIdTerminal [S.Full] [S.Faithful] : IsTerminal (mk (ùüô (S.obj Y))) where
  lift c := homMk (S.preimage c.pt.hom)
  uniq := by
    rintro c m -
    ext
    apply S.map_injective
    simpa only [homMk_left, S.map_preimage, ‚Üê w m] using (Category.comp_id _).symm

variable {A : Type u‚ÇÉ} [Category.{v‚ÇÉ} A] {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B]

/-- The functor `(F ‚ãô G, S) ‚•§ (G, S)`. -/
@[simps!]
def pre (F : B ‚•§ C) (G : C ‚•§ D) (S : D) : CostructuredArrow (F ‚ãô G) S ‚•§ CostructuredArrow G S :=
  Comma.preLeft F G _

instance (F : B ‚•§ C) (G : C ‚•§ D) (S : D) [F.Faithful] : (pre F G S).Faithful :=
  show (Comma.preLeft _ _ _).Faithful from inferInstance

instance (F : B ‚•§ C) (G : C ‚•§ D) (S : D) [F.Full] : (pre F G S).Full :=
  show (Comma.preLeft _ _ _).Full from inferInstance

instance (F : B ‚•§ C) (G : C ‚•§ D) (S : D) [F.EssSurj] : (pre F G S).EssSurj :=
  show (Comma.preLeft _ _ _).EssSurj from inferInstance

/-- If `F` is an equivalence, then so is the functor `(F ‚ãô G, S) ‚•§ (G, S)`. -/
instance isEquivalence_pre (F : B ‚•§ C) (G : C ‚•§ D) (S : D) [F.IsEquivalence] :
    (pre F G S).IsEquivalence :=
  Comma.isEquivalence_preLeft _ _ _

/-- The functor `(F, S) ‚•§ (F ‚ãô G, G(S))`. -/
@[simps]
def post (F : B ‚•§ C) (G : C ‚•§ D) (S : C) :
    CostructuredArrow F S ‚•§ CostructuredArrow (F ‚ãô G) (G.obj S) where
  obj X := CostructuredArrow.mk (G.map X.hom)
  map f := CostructuredArrow.homMk f.left (by simp [Functor.comp_map, ‚Üê G.map_comp])

instance (F : B ‚•§ C) (G : C ‚•§ D) (S : C) : (post F G S).Faithful where
  map_injective {_ _} _ _ h := by simpa [ext_iff] using h

instance (F : B ‚•§ C) (G : C ‚•§ D) (S : C) [G.Faithful] : (post F G S).Full where
  map_surjective f := ‚ü®homMk f.left (G.map_injective (by simpa using f.w)), by simp‚ü©

instance (F : B ‚•§ C) (G : C ‚•§ D) (S : C) [G.Full] : (post F G S).EssSurj where
  mem_essImage h := ‚ü®mk (G.preimage h.hom), ‚ü®isoMk (Iso.refl _) (by simp)‚ü©‚ü©

/-- If `G` is fully faithful, then `post F G S : (F, S) ‚•§ (F ‚ãô G, G(S))` is an equivalence. -/
instance isEquivalence_post (S : C) (F : B ‚•§ C) (G : C ‚•§ D) [G.Full] [G.Faithful] :
    (post F G S).IsEquivalence where

section

variable {U : A ‚•§ B} {V : B} {F : C ‚•§ A} {G : D ‚•§ B}
  (Œ± : F ‚ãô U ‚ü∂ S ‚ãô G) (Œ≤ : G.obj T ‚ü∂ V)

/-- The functor `CostructuredArrow S T ‚•§ CostructuredArrow U V` that is deduced from
a natural transformation `F ‚ãô U ‚ü∂ S ‚ãô G` and a morphism `G.obj T ‚ü∂ V` -/
@[simps!]
def map‚ÇÇ : CostructuredArrow S T ‚•§ CostructuredArrow U V :=
  Comma.map (F‚ÇÇ := ùü≠ (Discrete PUnit)) Œ± (Discrete.natTrans (fun _ => Œ≤))

instance faithful_map‚ÇÇ [F.Faithful] : (map‚ÇÇ Œ± Œ≤).Faithful := by
  apply Comma.faithful_map

instance full_map‚ÇÇ [G.Faithful] [F.Full] [IsIso Œ±] [IsIso Œ≤] : (map‚ÇÇ Œ± Œ≤).Full := by
  apply Comma.full_map

instance essSurj_map‚ÇÇ [F.EssSurj] [G.Full] [IsIso Œ±] [IsIso Œ≤] : (map‚ÇÇ Œ± Œ≤).EssSurj := by
  apply Comma.essSurj_map

noncomputable instance isEquivalenceMap‚ÇÇ
    [F.IsEquivalence] [G.Faithful] [G.Full] [IsIso Œ±] [IsIso Œ≤] :
    (map‚ÇÇ Œ± Œ≤).IsEquivalence := by
  apply Comma.isEquivalenceMap

end

/-- `CostructuredArrow.post` is a special case of `CostructuredArrow.map‚ÇÇ` up to natural
isomorphism. -/
def postIsoMap‚ÇÇ (S : C) (F : B ‚•§ C) (G : C ‚•§ D) :
    post F G S ‚âÖ map‚ÇÇ (F := ùü≠ _) (ùüô (F ‚ãô G)) (ùüô _) :=
  NatIso.ofComponents fun _ => isoMk <| Iso.refl _

/-- A costructured arrow is called universal if it is terminal. -/
abbrev IsUniversal (f : CostructuredArrow S T) := IsTerminal f

namespace IsUniversal

variable {f g : CostructuredArrow S T}

theorem uniq (h : IsUniversal f) (Œ∑ : g ‚ü∂ f) : Œ∑ = h.from g :=
  h.hom_ext Œ∑ (h.from g)

/-- The family of morphisms into a universal arrow. -/
def lift (h : IsUniversal f) (g : CostructuredArrow S T) : g.left ‚ü∂ f.left :=
  (h.from g).left

/-- Any costructured arrow factors through a universal arrow. -/
@[reassoc (attr := simp)]
theorem fac (h : IsUniversal f) (g : CostructuredArrow S T) :
    S.map (h.lift g) ‚â´ f.hom = g.hom :=
  Category.comp_id g.hom ‚ñ∏ (h.from g).w

theorem hom_desc (h : IsUniversal f) {c : C} (Œ∑ : c ‚ü∂ f.left) :
    Œ∑ = h.lift (mk <| S.map Œ∑ ‚â´ f.hom) :=
  let g := mk <| S.map Œ∑ ‚â´ f.hom
  congrArg CommaMorphism.left (h.hom_ext (homMk Œ∑ rfl : g ‚ü∂ f) (h.from g))

/-- Two morphisms into a universal `S`-costructured arrow are equal if their image under `S` are
equal after postcomposing the universal arrow. -/
theorem hom_ext (h : IsUniversal f) {c : C} {Œ∑ Œ∑' : c ‚ü∂ f.left}
    (w : S.map Œ∑ ‚â´ f.hom = S.map Œ∑' ‚â´ f.hom) : Œ∑ = Œ∑' := by
  rw [h.hom_desc Œ∑, h.hom_desc Œ∑', w]

theorem existsUnique (h : IsUniversal f) (g : CostructuredArrow S T) :
    ‚àÉ! Œ∑ : g.left ‚ü∂ f.left, S.map Œ∑ ‚â´ f.hom = g.hom :=
  ‚ü®h.lift g, h.fac g, fun f w ‚Ü¶ h.hom_ext <| by simp [w]‚ü©

end IsUniversal

end CostructuredArrow

namespace Functor

variable {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E]

/-- Given `X : D` and `F : C ‚•§ D`, to upgrade a functor `G : E ‚•§ C` to a functor
`E ‚•§ StructuredArrow X F`, it suffices to provide maps `X ‚ü∂ F.obj (G.obj Y)` for all `Y` making
the obvious triangles involving all `F.map (G.map g)` commute.

This is of course the same as providing a cone over `F ‚ãô G` with cone point `X`, see
`Functor.toStructuredArrowIsoToStructuredArrow`. -/
@[simps]
def toStructuredArrow (G : E ‚•§ C) (X : D) (F : C ‚•§ D) (f : (Y : E) ‚Üí X ‚ü∂ F.obj (G.obj Y))
    (h : ‚àÄ {Y Z : E} (g : Y ‚ü∂ Z), f Y ‚â´ F.map (G.map g) = f Z) : E ‚•§ StructuredArrow X F where
  obj Y := StructuredArrow.mk (f Y)
  map g := StructuredArrow.homMk (G.map g) (h g)

/-- Upgrading a functor `E ‚•§ C` to a functor `E ‚•§ StructuredArrow X F` and composing with the
forgetful functor `StructuredArrow X F ‚•§ C` recovers the original functor. -/
def toStructuredArrowCompProj (G : E ‚•§ C) (X : D) (F : C ‚•§ D) (f : (Y : E) ‚Üí X ‚ü∂ F.obj (G.obj Y))
    (h : ‚àÄ {Y Z : E} (g : Y ‚ü∂ Z), f Y ‚â´ F.map (G.map g) = f Z) :
    G.toStructuredArrow X F f h ‚ãô StructuredArrow.proj _ _ ‚âÖ G :=
  Iso.refl _

@[simp]
lemma toStructuredArrow_comp_proj (G : E ‚•§ C) (X : D) (F : C ‚•§ D)
    (f : (Y : E) ‚Üí X ‚ü∂ F.obj (G.obj Y)) (h : ‚àÄ {Y Z : E} (g : Y ‚ü∂ Z), f Y ‚â´ F.map (G.map g) = f Z) :
    G.toStructuredArrow X F f h ‚ãô StructuredArrow.proj _ _ = G :=
  rfl

/-- Given `F : C ‚•§ D` and `X : D`, to upgrade a functor `G : E ‚•§ C` to a functor
`E ‚•§ CostructuredArrow F X`, it suffices to provide maps `F.obj (G.obj Y) ‚ü∂ X` for all `Y`
making the obvious triangles involving all `F.map (G.map g)` commute.

This is of course the same as providing a cocone over `F ‚ãô G` with cocone point `X`, see
`Functor.toCostructuredArrowIsoToCostructuredArrow`. -/
@[simps]
def toCostructuredArrow (G : E ‚•§ C) (F : C ‚•§ D) (X : D) (f : (Y : E) ‚Üí F.obj (G.obj Y) ‚ü∂ X)
    (h : ‚àÄ {Y Z : E} (g : Y ‚ü∂ Z), F.map (G.map g) ‚â´ f Z = f Y) : E ‚•§ CostructuredArrow F X where
  obj Y := CostructuredArrow.mk (f Y)
  map g := CostructuredArrow.homMk (G.map g) (h g)

/-- Upgrading a functor `E ‚•§ C` to a functor `E ‚•§ CostructuredArrow F X` and composing with the
forgetful functor `CostructuredArrow F X ‚•§ C` recovers the original functor. -/
def toCostructuredArrowCompProj (G : E ‚•§ C) (F : C ‚•§ D) (X : D)
    (f : (Y : E) ‚Üí F.obj (G.obj Y) ‚ü∂ X) (h : ‚àÄ {Y Z : E} (g : Y ‚ü∂ Z), F.map (G.map g) ‚â´ f Z = f Y) :
    G.toCostructuredArrow F X f h ‚ãô CostructuredArrow.proj _ _ ‚âÖ G :=
  Iso.refl _

@[simp]
lemma toCostructuredArrow_comp_proj (G : E ‚•§ C) (F : C ‚•§ D) (X : D)
    (f : (Y : E) ‚Üí F.obj (G.obj Y) ‚ü∂ X) (h : ‚àÄ {Y Z : E} (g : Y ‚ü∂ Z), F.map (G.map g) ‚â´ f Z = f Y) :
    G.toCostructuredArrow F X f h ‚ãô CostructuredArrow.proj _ _ = G :=
rfl

end Functor

open Opposite

namespace StructuredArrow

/-- For a functor `F : C ‚•§ D` and an object `d : D`, we obtain a contravariant functor from the
category of structured arrows `d ‚ü∂ F.obj c` to the category of costructured arrows
`F.op.obj c ‚ü∂ (op d)`.
-/
@[simps]
def toCostructuredArrow (F : C ‚•§ D) (d : D) :
    (StructuredArrow d F)·µí·µñ ‚•§ CostructuredArrow F.op (op d) where
  obj X := @CostructuredArrow.mk _ _ _ _ _ (op X.unop.right) F.op X.unop.hom.op
  map f := CostructuredArrow.homMk f.unop.right.op (by simp [‚Üê op_comp])

/-- For a functor `F : C ‚•§ D` and an object `d : D`, we obtain a contravariant functor from the
category of structured arrows `op d ‚ü∂ F.op.obj c` to the category of costructured arrows
`F.obj c ‚ü∂ d`.
-/
@[simps]
def toCostructuredArrow' (F : C ‚•§ D) (d : D) :
    (StructuredArrow (op d) F.op)·µí·µñ ‚•§ CostructuredArrow F d where
  obj X := @CostructuredArrow.mk _ _ _ _ _ (unop X.unop.right) F X.unop.hom.unop
  map f :=
    CostructuredArrow.homMk f.unop.right.unop
      (by
        dsimp
        rw [‚Üê Quiver.Hom.unop_op (F.map (Quiver.Hom.unop f.unop.right)), ‚Üê unop_comp, ‚Üê F.op_map, ‚Üê
          f.unop.w]
        simp)

end StructuredArrow

namespace CostructuredArrow

/-- For a functor `F : C ‚•§ D` and an object `d : D`, we obtain a contravariant functor from the
category of costructured arrows `F.obj c ‚ü∂ d` to the category of structured arrows
`op d ‚ü∂ F.op.obj c`.
-/
@[simps]
def toStructuredArrow (F : C ‚•§ D) (d : D) :
    (CostructuredArrow F d)·µí·µñ ‚•§ StructuredArrow (op d) F.op where
  obj X := @StructuredArrow.mk _ _ _ _ _ (op X.unop.left) F.op X.unop.hom.op
  map f := StructuredArrow.homMk f.unop.left.op (by simp [‚Üê op_comp])

/-- For a functor `F : C ‚•§ D` and an object `d : D`, we obtain a contravariant functor from the
category of costructured arrows `F.op.obj c ‚ü∂ op d` to the category of structured arrows
`d ‚ü∂ F.obj c`.
-/
@[simps]
def toStructuredArrow' (F : C ‚•§ D) (d : D) :
    (CostructuredArrow F.op (op d))·µí·µñ ‚•§ StructuredArrow d F where
  obj X := @StructuredArrow.mk _ _ _ _ _ (unop X.unop.left) F X.unop.hom.unop
  map f :=
    StructuredArrow.homMk f.unop.left.unop
      (by
        dsimp
        rw [‚Üê Quiver.Hom.unop_op (F.map f.unop.left.unop), ‚Üê unop_comp, ‚Üê F.op_map, f.unop.w,
          Functor.const_obj_map]
        simp)

end CostructuredArrow

/-- For a functor `F : C ‚•§ D` and an object `d : D`, the category of structured arrows `d ‚ü∂ F.obj c`
is contravariantly equivalent to the category of costructured arrows `F.op.obj c ‚ü∂ op d`.
-/
def structuredArrowOpEquivalence (F : C ‚•§ D) (d : D) :
    (StructuredArrow d F)·µí·µñ ‚âå CostructuredArrow F.op (op d) where
  functor := StructuredArrow.toCostructuredArrow F d
  inverse := (CostructuredArrow.toStructuredArrow' F d).rightOp
  unitIso := NatIso.ofComponents
      (fun X => (StructuredArrow.isoMk (Iso.refl _)).op)
      fun {X Y} f => Quiver.Hom.unop_inj <| by
        apply CommaMorphism.ext <;>
          dsimp [StructuredArrow.isoMk, Comma.isoMk,StructuredArrow.homMk]; simp
  counitIso := NatIso.ofComponents
      (fun X => CostructuredArrow.isoMk (Iso.refl _))
      fun {X Y} f => by
        apply CommaMorphism.ext <;>
          dsimp [CostructuredArrow.isoMk, Comma.isoMk, CostructuredArrow.homMk]; simp

/-- For a functor `F : C ‚•§ D` and an object `d : D`, the category of costructured arrows
`F.obj c ‚ü∂ d` is contravariantly equivalent to the category of structured arrows
`op d ‚ü∂ F.op.obj c`.
-/
def costructuredArrowOpEquivalence (F : C ‚•§ D) (d : D) :
    (CostructuredArrow F d)·µí·µñ ‚âå StructuredArrow (op d) F.op where
  functor := CostructuredArrow.toStructuredArrow F d
  inverse := (StructuredArrow.toCostructuredArrow' F d).rightOp
  unitIso := NatIso.ofComponents
      (fun X => (CostructuredArrow.isoMk (Iso.refl _)).op)
      fun {X Y} f => Quiver.Hom.unop_inj <| by
        apply CommaMorphism.ext <;>
          dsimp [CostructuredArrow.isoMk, CostructuredArrow.homMk, Comma.isoMk]; simp
  counitIso := NatIso.ofComponents
      (fun X => StructuredArrow.isoMk (Iso.refl _))
      fun {X Y} f => by
        apply CommaMorphism.ext <;>
          dsimp [StructuredArrow.isoMk, StructuredArrow.homMk, Comma.isoMk]; simp

section Pre

variable {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E] (F : C ‚•§ D) {G : D ‚•§ E} {e : E}

/-- The functor establishing the equivalence `StructuredArrow.preEquivalence`. -/
@[simps!]
def StructuredArrow.preEquivalenceFunctor (f : StructuredArrow e G) :
    StructuredArrow f (pre e F G) ‚•§ StructuredArrow f.right F where
  obj g := mk g.hom.right
  map œÜ := homMk œÜ.right.right <| by
    have := w œÜ
    simp only [Functor.const_obj_obj] at this ‚ä¢
    rw [‚Üê this, comp_right]
    simp

/-- The inverse functor establishing the equivalence `StructuredArrow.preEquivalence`. -/
@[simps!]
def StructuredArrow.preEquivalenceInverse (f : StructuredArrow e G) :
    StructuredArrow f.right F ‚•§ StructuredArrow f (pre e F G) where
  obj g := mk
            (Y := mk (Y := g.right)
              (f.hom ‚â´ (G.map g.hom : G.obj f.right ‚ü∂ (F ‚ãô G).obj g.right)))
            (homMk g.hom)
  map œÜ := homMk <| homMk œÜ.right <| by
    simp only [Functor.const_obj_obj, Functor.comp_obj, mk_right, mk_left, mk_hom_eq_self,
      Functor.comp_map, Category.assoc, ‚Üê w œÜ, Functor.map_comp]

/-- A structured arrow category on a `StructuredArrow.pre e F G` functor is equivalent to the
structured arrow category on F -/
@[simps]
def StructuredArrow.preEquivalence (f : StructuredArrow e G) :
    StructuredArrow f (pre e F G) ‚âå StructuredArrow f.right F where
  functor := preEquivalenceFunctor F f
  inverse := preEquivalenceInverse F f
  unitIso := NatIso.ofComponents (fun _ => isoMk (isoMk (Iso.refl _)))
  counitIso := NatIso.ofComponents (fun _ => isoMk (Iso.refl _))

/-- The functor `StructuredArrow d T ‚•§ StructuredArrow e (T ‚ãô S)` that `u : e ‚ü∂ S.obj d`
induces via `StructuredArrow.map‚ÇÇ` can be expressed up to isomorphism by
`StructuredArrow.preEquivalence` and `StructuredArrow.proj`. -/
def StructuredArrow.map‚ÇÇIsoPreEquivalenceInverseCompProj {T : C ‚•§ D} {S : D ‚•§ E} {T' : C ‚•§ E}
    (d : D) (e : E) (u : e ‚ü∂ S.obj d) (Œ± : T ‚ãô S ‚ü∂ T') :
    map‚ÇÇ (F := ùü≠ _) u Œ± ‚âÖ (preEquivalence T (mk u)).inverse ‚ãô proj (mk u) (pre _ T S) ‚ãô
      map‚ÇÇ (F := ùü≠ _) (G := ùü≠ _) (ùüô _) Œ± :=
  NatIso.ofComponents fun _ => isoMk (Iso.refl _)

/-- The functor establishing the equivalence `CostructuredArrow.preEquivalence`. -/
@[simps!]
def CostructuredArrow.preEquivalence.functor (f : CostructuredArrow G e) :
    CostructuredArrow (pre F G e) f ‚•§ CostructuredArrow F f.left where
  obj g := mk g.hom.left
  map œÜ := homMk œÜ.left.left <| by
    have := w œÜ
    simp only [Functor.const_obj_obj] at this ‚ä¢
    rw [‚Üê this, comp_left]
    simp

/-- The inverse functor establishing the equivalence `CostructuredArrow.preEquivalence`. -/
@[simps!]
def CostructuredArrow.preEquivalence.inverse (f : CostructuredArrow G e) :
    CostructuredArrow F f.left ‚•§ CostructuredArrow (pre F G e) f where
  obj g := mk (Y := mk (Y := g.left) (G.map g.hom ‚â´ f.hom)) (homMk g.hom)
  map œÜ := homMk <| homMk œÜ.left <| by
    simp only [Functor.const_obj_obj, Functor.comp_obj, mk_left, Functor.comp_map, mk_hom_eq_self,
      ‚Üê w œÜ, Functor.map_comp, Category.assoc]

/-- A costructured arrow category on a `CostructuredArrow.pre F G e` functor is equivalent to the
costructured arrow category on F -/
def CostructuredArrow.preEquivalence (f : CostructuredArrow G e) :
    CostructuredArrow (pre F G e) f ‚âå CostructuredArrow F f.left where
  functor := preEquivalence.functor F f
  inverse := preEquivalence.inverse F f
  unitIso := NatIso.ofComponents (fun _ => isoMk (isoMk (Iso.refl _)))
  counitIso := NatIso.ofComponents (fun _ => isoMk (Iso.refl _))

/-- The functor `CostructuredArrow T d ‚•§ CostructuredArrow (T ‚ãô S) e` that `u : S.obj d ‚ü∂ e`
induces via `CostructuredArrow.map‚ÇÇ` can be expressed up to isomorphism by
`CostructuredArrow.preEquivalence` and `CostructuredArrow.proj`. -/
def CostructuredArrow.map‚ÇÇIsoPreEquivalenceInverseCompProj (T : C ‚•§ D) (S : D ‚•§ E) (d : D) (e : E)
    (u : S.obj d ‚ü∂ e) :
    map‚ÇÇ (F := ùü≠ _) (U := T ‚ãô S) (ùüô (T ‚ãô S)) u ‚âÖ
      (preEquivalence T (mk u)).inverse ‚ãô proj (pre T S _) (mk u) :=
  NatIso.ofComponents fun _ => isoMk (Iso.refl _)

end Pre

section Prod

section

variable {C' : Type u‚ÇÉ} [Category.{v‚ÇÉ} C'] {D' : Type u‚ÇÑ} [Category.{v‚ÇÑ} D']
  (S : D) (S' : D') (T : C ‚•§ D) (T' : C' ‚•§ D')

@[reassoc (attr := simp)]
theorem StructuredArrow.w_prod_fst {X Y : StructuredArrow (S, S') (T.prod T')}
    (f : X ‚ü∂ Y) : X.hom.1 ‚â´ T.map f.right.1 = Y.hom.1 :=
  congr_arg _root_.Prod.fst (StructuredArrow.w f)

@[reassoc (attr := simp)]
theorem StructuredArrow.w_prod_snd {X Y : StructuredArrow (S, S') (T.prod T')}
    (f : X ‚ü∂ Y) : X.hom.2 ‚â´ T'.map f.right.2 = Y.hom.2 :=
  congr_arg _root_.Prod.snd (StructuredArrow.w f)

/-- Implementation; see `StructuredArrow.prodEquivalence`. -/
@[simps]
def StructuredArrow.prodFunctor :
    StructuredArrow (S, S') (T.prod T') ‚•§ StructuredArrow S T √ó StructuredArrow S' T' where
  obj f := ‚ü®.mk f.hom.1, .mk f.hom.2‚ü©
  map Œ∑ := ‚ü®StructuredArrow.homMk Œ∑.right.1 (by simp),
            StructuredArrow.homMk Œ∑.right.2 (by simp)‚ü©

/-- Implementation; see `StructuredArrow.prodEquivalence`. -/
@[simps]
def StructuredArrow.prodInverse :
    StructuredArrow S T √ó StructuredArrow S' T' ‚•§ StructuredArrow (S, S') (T.prod T') where
  obj f := .mk (Y := (f.1.right, f.2.right)) ‚ü®f.1.hom, f.2.hom‚ü©
  map Œ∑ := StructuredArrow.homMk ‚ü®Œ∑.1.right, Œ∑.2.right‚ü© (by simp)

/-- The natural equivalence
`StructuredArrow (S, S') (T.prod T') ‚âå StructuredArrow S T √ó StructuredArrow S' T'`. -/
@[simps]
def StructuredArrow.prodEquivalence :
    StructuredArrow (S, S') (T.prod T') ‚âå StructuredArrow S T √ó StructuredArrow S' T' where
  functor := StructuredArrow.prodFunctor S S' T T'
  inverse := StructuredArrow.prodInverse S S' T T'
  unitIso := NatIso.ofComponents (fun f => Iso.refl _) (by simp)
  counitIso := NatIso.ofComponents (fun f => Iso.refl _) (by simp)

end

section

variable {C' : Type u‚ÇÉ} [Category.{v‚ÇÉ} C'] {D' : Type u‚ÇÑ} [Category.{v‚ÇÑ} D']
  (S : C ‚•§ D) (S' : C' ‚•§ D') (T : D) (T' : D')

@[reassoc (attr := simp)]
theorem CostructuredArrow.w_prod_fst {A B : CostructuredArrow (S.prod S') (T, T')} (f : A ‚ü∂ B) :
    S.map f.left.1 ‚â´ B.hom.1 = A.hom.1 :=
  congr_arg _root_.Prod.fst (CostructuredArrow.w f)

@[reassoc (attr := simp)]
theorem CostructuredArrow.w_prod_snd {A B : CostructuredArrow (S.prod S') (T, T')} (f : A ‚ü∂ B) :
    S'.map f.left.2 ‚â´ B.hom.2 = A.hom.2 :=
  congr_arg _root_.Prod.snd (CostructuredArrow.w f)

/-- Implementation; see `CostructuredArrow.prodEquivalence`. -/
@[simps]
def CostructuredArrow.prodFunctor :
    CostructuredArrow (S.prod S') (T, T') ‚•§ CostructuredArrow S T √ó CostructuredArrow S' T' where
  obj f := ‚ü®.mk f.hom.1, .mk f.hom.2‚ü©
  map Œ∑ := ‚ü®CostructuredArrow.homMk Œ∑.left.1 (by simp),
            CostructuredArrow.homMk Œ∑.left.2 (by simp)‚ü©

/-- Implementation; see `CostructuredArrow.prodEquivalence`. -/
@[simps]
def CostructuredArrow.prodInverse :
    CostructuredArrow S T √ó CostructuredArrow S' T' ‚•§ CostructuredArrow (S.prod S') (T, T') where
  obj f := .mk (Y := (f.1.left, f.2.left)) ‚ü®f.1.hom, f.2.hom‚ü©
  map Œ∑ := CostructuredArrow.homMk ‚ü®Œ∑.1.left, Œ∑.2.left‚ü© (by simp)

/-- The natural equivalence
`CostructuredArrow (S.prod S') (T, T') ‚âå CostructuredArrow S T √ó CostructuredArrow S' T'`. -/
@[simps]
def CostructuredArrow.prodEquivalence :
    CostructuredArrow (S.prod S') (T, T') ‚âå CostructuredArrow S T √ó CostructuredArrow S' T' where
  functor := CostructuredArrow.prodFunctor S S' T T'
  inverse := CostructuredArrow.prodInverse S S' T T'
  unitIso := NatIso.ofComponents (fun f => Iso.refl _) (by simp)
  counitIso := NatIso.ofComponents (fun f => Iso.refl _) (by simp)

end

end Prod

end CategoryTheory
