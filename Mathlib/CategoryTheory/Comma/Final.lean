/-
Copyright (c) 2024 Jakob von Raumer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jakob von Raumer
-/
import Mathlib.CategoryTheory.Functor.KanExtension.Adjunction
import Mathlib.CategoryTheory.Limits.IsConnected
import Mathlib.CategoryTheory.Limits.Sifted
import Mathlib.CategoryTheory.Filtered.Final
import Mathlib.CategoryTheory.Filtered.Flat
import Mathlib.CategoryTheory.Grothendieck
import Mathlib.CategoryTheory.Comma.StructuredArrow.CommaMap

/-!
# Finality of Projections in Comma Categories

We show that `fst L R` is final if `R` is and that `snd L R` is initial if `L` is.
As a corollary, we show that `Comma L R` with `L : A â¥¤ T` and `R : B â¥¤ T` is connected if `R` is
final and `A` is connected.

We then use this in a proof that derives finality of `map` between two comma categories
on a quasi-commutative diagram of functors, some of which need to be final.

Finally we prove filteredness of a `Comma L R` and finality of `snd L R`, given that `R` is final
and `A` and `B` are filtered.

## References

* [M. Kashiwara, P. Schapira, *Categories and Sheaves*][Kashiwara2006], Lemma 3.4.3 -- 3.4.5
-/

universe vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ vâ‚… vâ‚† uâ‚ uâ‚‚ uâ‚ƒ uâ‚„ uâ‚… uâ‚†

namespace CategoryTheory

namespace Comma

open Limits Functor CostructuredArrow

section Small

variable {A : Type vâ‚} [Category.{vâ‚} A]
variable {B : Type vâ‚} [Category.{vâ‚} B]
variable {T : Type vâ‚} [Category.{vâ‚} T]
variable (L : A â¥¤ T) (R : B â¥¤ T)

private lemma final_fst_small [R.Final] : (fst L R).Final := by
  rw  [Functor.final_iff_isIso_colimit_pre]
  intro G
  let i : colimit G â‰… colimit (fst L R â‹™ G) :=
    colimitIsoColimitGrothendieck L G â‰ªâ‰«
    (Final.colimitIso (Grothendieck.pre (functor L) R) (grothendieckProj L â‹™ G)).symm â‰ªâ‰«
    HasColimit.isoOfNatIso (Iso.refl _) â‰ªâ‰«
    Final.colimitIso (grothendieckPrecompFunctorEquivalence L R).functor (fst L R â‹™ G)
  convert i.isIso_inv
  apply colimit.hom_ext
  intro âŸ¨a, b, fâŸ©
  simp only [colimit.Î¹_pre, comp_obj, fst_obj, grothendieckPrecompFunctorEquivalence_functor,
    Iso.trans_inv, Iso.symm_inv, Category.assoc, i]
  change _ = colimit.Î¹ (fst L R â‹™ G)
    ((grothendieckPrecompFunctorToComma L R).obj âŸ¨b, CostructuredArrow.mk fâŸ©) â‰« _
  simp

end Small

section NonSmall

variable {A : Type uâ‚} [Category.{vâ‚} A]
variable {B : Type uâ‚‚} [Category.{vâ‚‚} B]
variable {T : Type uâ‚ƒ} [Category.{vâ‚ƒ} T]
variable (L : A â¥¤ T) (R : B â¥¤ T)

instance final_fst [R.Final] : (fst L R).Final := by
  let sA : A â‰Œ AsSmall.{max uâ‚ uâ‚‚ uâ‚ƒ vâ‚ vâ‚‚ vâ‚ƒ} A := AsSmall.equiv
  let sB : B â‰Œ AsSmall.{max uâ‚ uâ‚‚ uâ‚ƒ vâ‚ vâ‚‚ vâ‚ƒ} B := AsSmall.equiv
  let sT : T â‰Œ AsSmall.{max uâ‚ uâ‚‚ uâ‚ƒ vâ‚ vâ‚‚ vâ‚ƒ} T := AsSmall.equiv
  let L' := sA.inverse â‹™ L â‹™ sT.functor
  let R' := sB.inverse â‹™ R â‹™ sT.functor
  let fC : Comma L R â¥¤ Comma L' R' :=
    map (Fâ‚ := sA.functor) (F := sT.functor) (Fâ‚‚ := sB.functor)
      (isoWhiskerRight sA.unitIso (L â‹™ sT.functor)).hom
      (isoWhiskerRight sB.unitIso (R â‹™ sT.functor)).hom
  have : Final (fst L' R') := final_fst_small _ _
  apply final_of_natIso (F := (fC â‹™ fst L' R' â‹™ sA.inverse))
  exact (Functor.associator _ _ _).symm.trans (Iso.compInverseIso (mapFst _ _))

instance initial_snd [L.Initial] : (snd L R).Initial := by
  have : ((opFunctor L R).leftOp â‹™ fst R.op L.op).Final :=
    final_equivalence_comp (opEquiv L R).functor.leftOp (fst R.op L.op)
  have : (snd L R).op.Final := final_of_natIso (opFunctorCompFst _ _)
  apply initial_of_final_op

/-- `Comma L R` with `L : A â¥¤ T` and `R : B â¥¤ T` is connected if `R` is final and `A` is
connected. -/
instance isConnected_comma_of_final [IsConnected A] [R.Final] : IsConnected (Comma L R) := by
  rwa [isConnected_iff_of_final (fst L R)]

/-- `Comma L R` with `L : A â¥¤ T` and `R : B â¥¤ T` is connected if `L` is initial and `B` is
connected. -/
instance isConnected_comma_of_initial [IsConnected B] [L.Initial] : IsConnected (Comma L R) := by
  rwa [isConnected_iff_of_initial (snd L R)]

end NonSmall

/-- Let the following diagram commute up to isomorphism:

```
      L       R
  A  ---â†’ T  â†--- B
  |       |       |
  | F     | H     | G
  â†“       â†“       â†“
  A' ---â†’ T' â†--- B'
      L'      R'
```

Let `F`, `G`, `R` and `R'` be final and `B` be filtered. Then, the induced functor between the comma
categories of the first and second row of the diagram is final. -/
lemma map_final {A : Type uâ‚} [Category.{vâ‚} A] {B : Type uâ‚‚} [Category.{vâ‚‚} B] {T : Type uâ‚ƒ}
    [Category.{vâ‚ƒ} T] {L : A â¥¤ T} {R : B â¥¤ T} {A' : Type uâ‚„} [Category.{vâ‚„} A'] {B' : Type uâ‚…}
    [Category.{vâ‚…} B'] {T' : Type uâ‚†} [Category.{vâ‚†} T'] {L' : A' â¥¤ T'} {R' : B' â¥¤ T'} {F : A â¥¤ A'}
    {G : B â¥¤ B'} {H : T â¥¤ T'} (iL : F â‹™ L' â‰… L â‹™ H) (iR : G â‹™ R' â‰… R â‹™ H) [IsFiltered B]
    [R.Final] [R'.Final] [F.Final] [G.Final] :
    (Comma.map iL.hom iR.inv).Final := âŸ¨fun âŸ¨iâ‚‚, jâ‚‚, uâ‚‚âŸ© => by
  haveI := final_of_natIso iR
  rw [isConnected_iff_of_equivalence (StructuredArrow.commaMapEquivalence iL.hom iR.inv _)]
  have : StructuredArrow.mapâ‚‚ uâ‚‚ iR.hom â‰… StructuredArrow.post jâ‚‚ G R' â‹™
      StructuredArrow.mapâ‚‚ (G := ğŸ­ _) (F := ğŸ­ _) (R' := R â‹™ H) uâ‚‚ iR.hom â‹™
      StructuredArrow.pre _ R H :=
    eqToIso (by
      congr
      Â· simp
      Â· ext; simp) â‰ªâ‰«
    (StructuredArrow.mapâ‚‚CompMapâ‚‚Iso _ _ _ _).symm â‰ªâ‰«
    isoWhiskerLeft _ ((StructuredArrow.mapâ‚‚CompMapâ‚‚Iso _ _ _ _).symm â‰ªâ‰«
      isoWhiskerLeft _ (StructuredArrow.preIsoMapâ‚‚ _ _ _).symm) â‰ªâ‰«
    isoWhiskerRight (StructuredArrow.postIsoMapâ‚‚ jâ‚‚ G R').symm _
  haveI := final_of_natIso this.symm
  rw [IsIso.Iso.inv_inv]
  infer_instanceâŸ©

section Filtered

variable {A : Type uâ‚} [Category.{vâ‚} A]
variable {B : Type uâ‚‚} [Category.{vâ‚‚} B]
variable {T : Type uâ‚ƒ} [Category.{vâ‚ƒ} T]
variable (L : A â¥¤ T) (R : B â¥¤ T)

attribute [local instance] map_final in
/-- Let `A` and `B` be filtered categories, `R : B â¥¤ T` be final and `L : A â¥¤ T`. Then, the
comma category `Comma L R` is filtered. -/
instance isFiltered_of_final [IsFiltered A] [IsFiltered B] [R.Final] : IsFiltered (Comma L R) := by
  haveI (a : A) : IsFiltered (Comma (fromPUnit (L.obj a)) R) :=
    R.final_iff_isFiltered_structuredArrow.mp inferInstance (L.obj a)
  have (a : A) : (fromPUnit (Over.mk (ğŸ™ a))).Final := final_const_of_isTerminal Over.mkIdTerminal
  let Î· (a : A) : fromPUnit (Over.mk (ğŸ™ a)) â‹™ Over.forget a â‹™ L â‰… fromPUnit (L.obj a) :=
    NatIso.ofComponents (fun _ => Iso.refl _)
  have (a : A) := IsFiltered.of_final (map (L := fromPUnit (L.obj a)) (F := ğŸ­ T) (Î· a).hom
    ((Iso.refl (ğŸ­ B â‹™ R)).inv))
  have : RepresentablyCoflat (fst L R) :=
    âŸ¨fun a => IsFiltered.of_equivalence (CostructuredArrow.ofCommaFstEquivalence L R a).symmâŸ©
  apply isFiltered_of_representablyCoflat (fst L R)

attribute [local instance] isFiltered_of_final in
/-- Let `A` and `B` be cofiltered categories, `L : A â¥¤ T` be initial and `R : B â¥¤ T`. Then, the
comma category `Comma L R` is cofiltered. -/
lemma isCofiltered_of_initial [IsCofiltered A] [IsCofiltered B] [L.Initial] :
    IsCofiltered (Comma L R) :=
  IsCofiltered.of_equivalence (Comma.opEquiv _ _).symm

attribute [local instance] final_of_isFiltered_of_pUnit in
/-- Let `A` and `B` be filtered categories, `R : B â¥¤ T` be final and `R : A â¥¤ T`. Then, the
projection `snd L R : Comma L R â¥¤ B` is final. -/
instance final_snd [IsFiltered A] [IsFiltered B] [R.Final] : (snd L R).Final := by
  let iL : star.{1} A â‹™ ğŸ­ _ â‰… L â‹™ star _ := Iso.refl _
  let iR : ğŸ­ B â‹™ star.{1} B â‰… R â‹™ star _ := Iso.refl _
  have := map_final iL iR
  let s := (equivProd (ğŸ­ _) (star B)).trans <| prod.leftUnitorEquivalence B
  let iS : map iL.hom iR.inv â‹™ s.functor â‰… snd L R :=
    NatIso.ofComponents (fun _ => Iso.refl _) (fun f => by simp [iL, iR, s])
  apply final_of_natIso iS

/-- Let `A` and `B` be cofiltered categories, `L : A â¥¤ T` be initial and `R : B â¥¤ T`. Then, the
projection `fst L R : Comma L R â¥¤ A` is initial. -/
instance initial_fst [IsCofiltered A] [IsCofiltered B] [L.Initial] : (fst L R).Initial := by
  have : ((opFunctor L R).leftOp â‹™ snd R.op L.op).Final :=
    final_equivalence_comp (opEquiv L R).functor.leftOp _
  have : (fst L R).op.Final := final_of_natIso <| opFunctorCompSnd _ _
  apply initial_of_final_op

end Filtered

end Comma

end CategoryTheory
