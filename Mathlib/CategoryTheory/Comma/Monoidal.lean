/-
Copyright (c) 2026 Jack McKoen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jack McKoen
-/
module

public import Mathlib.CategoryTheory.Limits.Shapes.Pullback.PullbackObjObj
public import Mathlib.CategoryTheory.Monoidal.Cartesian.Basic
public import Mathlib.CategoryTheory.Monoidal.Closed.Cartesian

/-!
# Monoidal structure on the arrow category

-/

universe v u

namespace CategoryTheory.Arrow

open Opposite Limits MonoidalCategory Functor PushoutProduct

variable {C : Type u} [Category.{v} C] [HasPushouts C] [CartesianMonoidalCategory C]
  (F : C ‚•§ C ‚•§ C) (G : C·µí·µñ ‚•§ C ‚•§ C)
  {A B X Y Z W : C} (f : A ‚ü∂ B) (g : X ‚ü∂ Y) (h : Z ‚ü∂ W)

notation3 f "‚ñ°" g:10 => Functor.pushoutProduct (curriedTensor _) f g

@[simp]
def IsPushout_ofWhiskerLeft {Z X Y W : C} {f : Z ‚ü∂ X} {g : Z ‚ü∂ Y}
    [PreservesColimit (span f g) (tensorLeft W)] :
    IsPushout (W ‚óÅ f) (W ‚óÅ g)
      (W ‚óÅ (pushout.inl f g)) (W ‚óÅ (pushout.inr f g)) where
  w := by simp only [‚Üê MonoidalCategory.whiskerLeft_comp, pushout.condition]
  isColimit' := ‚ü®Limits.isColimitOfHasPushoutOfPreservesColimit (tensorLeft W) _ _‚ü©

@[simp]
def IsPushout_ofWhiskerLeft' {Z X Y P W : C} {f : Z ‚ü∂ X} {g : Z ‚ü∂ Y}
    {inl : X ‚ü∂ P} {inr : Y ‚ü∂ P} (hP : IsPushout f g inl inr)
    [PreservesColimit (span f g) (tensorLeft W)] :
    IsPushout (W ‚óÅ f) (W ‚óÅ g)
      (W ‚óÅ inl) (W ‚óÅ inr) where
  w := by simp only [‚Üê MonoidalCategory.whiskerLeft_comp, hP.w]
  isColimit' := ‚ü®isColimitPushoutCoconeMapOfIsColimit (tensorLeft W) hP.w hP.isColimit‚ü©

@[simp]
def IsPushout_ofWhiskerRight {Z X Y W : C} {f : Z ‚ü∂ X} {g : Z ‚ü∂ Y}
    [PreservesColimit (span f g) (tensorRight W)] :
    IsPushout (f ‚ñ∑ W) (g ‚ñ∑ W)
      ((pushout.inl f g) ‚ñ∑ W) ((pushout.inr f g) ‚ñ∑ W) where
  w := by simp only [‚Üê MonoidalCategory.comp_whiskerRight, pushout.condition]
  isColimit' := ‚ü®Limits.isColimitOfHasPushoutOfPreservesColimit (tensorRight W) _ _‚ü©

@[simp]
def IsPushout_ofWhiskerRight' {Z X Y P W : C} {f : Z ‚ü∂ X} {g : Z ‚ü∂ Y}
    {inl : X ‚ü∂ P} {inr : Y ‚ü∂ P} (hP : IsPushout f g inl inr)
    [PreservesColimit (span f g) (tensorRight W)] :
    IsPushout (f ‚ñ∑ W) (g ‚ñ∑ W)
      (inl ‚ñ∑ W) (inr ‚ñ∑ W) where
  w := by simp only [‚Üê MonoidalCategory.comp_whiskerRight, hP.w]
  isColimit' := ‚ü®isColimitPushoutCoconeMapOfIsColimit (tensorRight W) hP.w hP.isColimit‚ü©

omit [HasPushouts C] in
@[reassoc (attr := simp)]
lemma whisker_inl_desc {Z X Y P Q : C} {f : Z ‚ü∂ X} {g : Z ‚ü∂ Y} {inl : X ‚ü∂ P} {inr : Y ‚ü∂ P}
    (hP : IsPushout f g inl inr) {W : C} (h : X ‚ü∂ W) (k : Y ‚ü∂ W)
    (w : f ‚â´ h = g ‚â´ k) :
    inl ‚ñ∑ Q ‚â´ hP.desc h k w ‚ñ∑ Q = h ‚ñ∑ Q := by cat_disch

omit [HasPushouts C] in
@[reassoc (attr := simp)]
lemma whisker_inr_desc {Z X Y P Q : C} {f : Z ‚ü∂ X} {g : Z ‚ü∂ Y} {inl : X ‚ü∂ P} {inr : Y ‚ü∂ P}
    (hP : IsPushout f g inl inr) {W : C} (h : X ‚ü∂ W) (k : Y ‚ü∂ W)
    (w : f ‚â´ h = g ‚â´ k) :
    inr ‚ñ∑ Q ‚â´ hP.desc h k w ‚ñ∑ Q = k ‚ñ∑ Q := by cat_disch

@[reassoc]
lemma whisker_pushout_condition {X Y Z Q : C} {f : X ‚ü∂ Y} {g : X ‚ü∂ Z} :
    Q ‚óÅ f ‚â´ Q ‚óÅ (pushout.inl f g) = Q ‚óÅ g ‚â´ Q ‚óÅ pushout.inr _ _ := by
  simp [‚Üê MonoidalCategory.whiskerLeft_comp, pushout.condition]

namespace PushoutProduct

-- need (F.obj A).obj ((F.obj B).obj C) ‚âÖ (F.obj ((F.obj A).obj B)).obj C for general F
@[simps!]
noncomputable
def tensorLeft_PushoutObjObj_iso
    [PreservesColimitsOfSize (tensorLeft W)] :
      W ‚äó (Arrow.mk (f ‚ñ° g)).left ‚âÖ
      (Arrow.mk ((W ‚óÅ f) ‚ñ° g)).left := by
  refine (IsPushout_ofWhiskerLeft' (IsPushout.of_hasPushout _ _)).isoPushout ‚â™‚â´ HasColimit.isoOfNatIso (spanExt ?_ ?_ ?_ ?_ ?_)
  ¬∑ exact (Œ±_ W A X).symm
  ¬∑ exact (Œ±_ W B X).symm
  ¬∑ exact (Œ±_ W A Y).symm
  ¬∑ exact (associator_inv_naturality_middle W f X).symm
  ¬∑ exact (associator_inv_naturality_right W A g).symm

@[simps!]
noncomputable
def PushoutObjObj_whiskerRight_iso [PreservesColimitsOfSize (tensorRight W)] :
    (Arrow.mk (f ‚ñ° g)).left ‚äó W ‚âÖ
    (Arrow.mk (f ‚ñ° (g ‚ñ∑ W))).left := by
  refine (IsPushout_ofWhiskerRight' (IsPushout.of_hasPushout _ _)).isoPushout ‚â™‚â´
    HasColimit.isoOfNatIso (spanExt ?_ ?_ ?_ ?_ ?_)
  ¬∑ exact Œ±_ A X W
  ¬∑ exact Œ±_ B X W
  ¬∑ exact Œ±_ A Y W
  ¬∑ exact (associator_naturality_left f X W).symm
  ¬∑ exact (associator_naturality_middle A g W).symm

@[simps!]
noncomputable
def PushoutProduct.whiskerRight_iso [PreservesColimitsOfSize (tensorRight W)] :
    Arrow.mk ((f ‚ñ° g) ‚ñ∑ W) ‚âÖ Arrow.mk (f ‚ñ° (g ‚ñ∑ W)) := by
  refine Arrow.isoMk (PushoutObjObj_whiskerRight_iso f g) (Œ±_ B Y W) ?_
  ¬∑ apply (IsPushout_ofWhiskerRight' (IsPushout.of_hasPushout _ _)).hom_ext
    all_goals simp

@[simps!]
noncomputable
def PushoutProduct.whiskerLeft_iso [PreservesColimitsOfSize (tensorLeft W)] :
    Arrow.mk (W ‚óÅ (f ‚ñ° g)) ‚âÖ Arrow.mk ((W ‚óÅ f) ‚ñ° g) := by
  refine Arrow.isoMk (tensorLeft_PushoutObjObj_iso _ _) (Œ±_ W B Y).symm ?_
  ¬∑ apply (IsPushout_ofWhiskerLeft' (IsPushout.of_hasPushout _ _)).hom_ext
    all_goals simp [‚Üê MonoidalCategory.whiskerLeft_comp_assoc]

@[simp]
noncomputable
def associator_hom_left_aux [PreservesColimitsOfSize (tensorRight W)] :
    ((Arrow.mk (f ‚ñ° g)).left) ‚äó W ‚ü∂
    (Arrow.mk (f ‚ñ° (g ‚ñ° h))).left := by
  refine (PushoutObjObj_whiskerRight_iso _ _).hom ‚â´ pushout.desc ?_ ?_ ?_
  ¬∑ exact (B ‚óÅ pushout.inr _ _) ‚â´ pushout.inl _ _
  ¬∑ exact pushout.inr _ _
  ¬∑ dsimp
    rw [‚Üê whisker_exchange_assoc, pushout.condition,
      ‚Üê MonoidalCategory.whiskerLeft_comp_assoc, IsPushout.inr_desc]

@[simp]
noncomputable
def associator_hom_left
    [PreservesColimitsOfSize (tensorRight Z)]
    [PreservesColimitsOfSize (tensorRight W)] :
    (Arrow.mk ((f ‚ñ° g) ‚ñ° h)).left ‚ü∂ (Arrow.mk (f ‚ñ° (g ‚ñ° h))).left := by
  refine pushout.desc ?_ ?_ ?_
  ¬∑ exact (Œ±_ B Y Z).hom ‚â´ (B ‚óÅ pushout.inl _ _) ‚â´ pushout.inl _ _
  ¬∑ exact associator_hom_left_aux ..
  ¬∑ apply (IsPushout_ofWhiskerRight' (IsPushout.of_hasPushout _ _)).hom_ext
    ¬∑ simp [whisker_pushout_condition_assoc, ‚Üê whisker_exchange_assoc]
    ¬∑ simp only [curriedTensor_obj_obj, PushoutObjObj.ofHasPushout_pt, curriedTensor_map_app,
        curriedTensor_obj_map, PushoutObjObj.ofHasPushout_Œπ, mk_left, pushoutProduct,
        whisker_inr_desc_assoc, associator_hom_left_aux, PushoutObjObj_whiskerRight_iso_hom,
        Category.assoc, HasColimit.isoOfNatIso_hom_desc, ‚Üê whisker_exchange_assoc,
        tensor_whiskerLeft, IsPushout.inr_isoPushout_hom_assoc, colimit.Œπ_desc,
        Cocones.precompose_obj_pt, PushoutCocone.mk_pt, Cocones.precompose_obj_Œπ, NatTrans.comp_app,
        span_right, const_obj_obj, spanExt_hom_app_right, PushoutCocone.mk_Œπ_app,
        Iso.inv_hom_id_assoc]
      rw [associator_naturality_left_assoc, ‚Üê whisker_exchange_assoc, pushout.condition,
        ‚Üê MonoidalCategory.whiskerLeft_comp_assoc, IsPushout.inl_desc]

@[simp]
noncomputable
def associator_inv_left
    [PreservesColimitsOfSize (tensorLeft A)]
    [PreservesColimitsOfSize (tensorLeft B)] :
     (Arrow.mk (f ‚ñ° (g ‚ñ° h))).left ‚ü∂ (Arrow.mk ((f ‚ñ° g) ‚ñ° h)).left := by
  apply pushout.desc ?_ ?_ ?_
  ¬∑ refine (tensorLeft_PushoutObjObj_iso g h).hom ‚â´ pushout.desc ?_ ?_ ?_
    ¬∑ exact ùüô _ ‚â´ pushout.inl _ _
    ¬∑ exact (pushout.inl _ _ ‚ñ∑ W) ‚â´ pushout.inr _ _
    ¬∑ dsimp [Functor.PushoutObjObj.Œπ]
      rw [Category.id_comp, whisker_exchange_assoc, ‚Üê pushout.condition,
        ‚Üê MonoidalCategory.comp_whiskerRight_assoc, IsPushout.inl_desc]
  ¬∑ exact (Œ±_ _ _ _).inv ‚â´ (pushout.inr _ _) ‚ñ∑ _ ‚â´ pushout.inr _ _
  ¬∑ dsimp
    apply (IsPushout_ofWhiskerLeft' (IsPushout.of_hasPushout _ _)).hom_ext
    ¬∑ simp only [Category.id_comp, Category.assoc, HasColimit.isoOfNatIso_hom_desc,
        whisker_exchange_assoc, whiskerRight_tensor, IsPushout.inl_isoPushout_hom_assoc,
        colimit.Œπ_desc, Cocones.precompose_obj_pt, PushoutCocone.mk_pt, Cocones.precompose_obj_Œπ,
        NatTrans.comp_app, span_left, const_obj_obj, spanExt_hom_app_left, Iso.symm_hom,
        PushoutCocone.mk_Œπ_app, Iso.hom_inv_id_assoc, ‚Üê MonoidalCategory.whiskerLeft_comp_assoc,
        IsPushout.inl_desc]
      rw [‚Üê associator_inv_naturality_left_assoc, associator_inv_naturality_right_assoc,
        whisker_exchange_assoc, ‚Üê pushout.condition, whiskerRight_tensor, whisker_inr_desc_assoc]
      simp only [Category.assoc, Iso.hom_inv_id_assoc]
    ¬∑ simp only [Category.id_comp, Category.assoc, HasColimit.isoOfNatIso_hom_desc,
        whisker_exchange_assoc, whiskerRight_tensor, IsPushout.inr_isoPushout_hom_assoc,
        colimit.Œπ_desc, Cocones.precompose_obj_pt, PushoutCocone.mk_pt, Cocones.precompose_obj_Œπ,
        NatTrans.comp_app, span_right, const_obj_obj, spanExt_hom_app_right, Iso.symm_hom,
        PushoutCocone.mk_Œπ_app, Iso.hom_inv_id_assoc, ‚Üê MonoidalCategory.whiskerLeft_comp_assoc,
        IsPushout.inr_desc]
      rw [‚Üê associator_inv_naturality_left_assoc, associator_inv_naturality_middle_assoc,
        ‚Üê comp_whiskerRight_assoc, ‚Üê pushout.condition, whiskerRight_tensor, comp_whiskerRight]
      simp only [Category.assoc, Iso.hom_inv_id_assoc]

@[simp]
noncomputable
def associator_iso_left
      [PreservesColimitsOfSize (tensorLeft A)]
      [PreservesColimitsOfSize (tensorLeft B)]
      [PreservesColimitsOfSize (tensorRight Z)]
      [PreservesColimitsOfSize (tensorRight W)] :
    (Arrow.mk ((f ‚ñ° g) ‚ñ° h)).left ‚âÖ (Arrow.mk (f ‚ñ° (g ‚ñ° h))).left where
  hom := associator_hom_left f g h
  inv := associator_inv_left f g h
  hom_inv_id := by
    apply pushout.hom_ext
    ¬∑ simp
    ¬∑ exact (IsPushout_ofWhiskerRight' (IsPushout.of_hasPushout _ _)).hom_ext (by simp) (by simp)
  inv_hom_id := by
    apply pushout.hom_ext
    ¬∑ exact (IsPushout_ofWhiskerLeft' (IsPushout.of_hasPushout _ _)).hom_ext (by simp) (by simp)
    ¬∑ simp

@[simp]
noncomputable
def braiding_left_iso [BraidedCategory C] : (Arrow.mk (f ‚ñ° g)).left ‚âÖ
    (Arrow.mk (g ‚ñ° f)).left :=
  pushoutSymmetry (f ‚ñ∑ X) (A ‚óÅ g) ‚â™‚â´
    (HasColimit.isoOfNatIso (spanExt (Œ≤_ _ _) (Œ≤_ _ _) (Œ≤_ _ _)
    (BraidedCategory.braiding_naturality_right A g).symm
    (BraidedCategory.braiding_naturality_left f X).symm))

noncomputable
def braiding [BraidedCategory C] : Arrow.mk (f ‚ñ° g) ‚âÖ Arrow.mk (g ‚ñ° f) :=
  Arrow.isoMk (braiding_left_iso f g) (Œ≤_ _ _) (by cat_disch)

@[simp]
noncomputable
def associator
    [PreservesColimitsOfSize (tensorLeft A)]
    [PreservesColimitsOfSize (tensorLeft B)]
    [PreservesColimitsOfSize (tensorRight Z)]
    [PreservesColimitsOfSize (tensorRight W)] :
    Arrow.mk ((f ‚ñ° g) ‚ñ° h) ‚âÖ Arrow.mk (f ‚ñ° g ‚ñ° h) := by
  refine Arrow.isoMk (associator_iso_left _ _ _) (Œ±_ _ _ _) ?_
  ¬∑ apply pushout.hom_ext
    ¬∑ simp [‚Üê MonoidalCategory.whiskerLeft_comp]
    ¬∑ apply (IsPushout_ofWhiskerRight' (IsPushout.of_hasPushout _ _)).hom_ext
      ¬∑ simp [‚Üê MonoidalCategory.whiskerLeft_comp, ‚Üê MonoidalCategory.comp_whiskerRight_assoc]
      ¬∑ simp [‚Üê MonoidalCategory.comp_whiskerRight_assoc]

@[simps!]
noncomputable
def leftUnitor [HasInitial C] [CartesianMonoidalCategory C] [MonoidalClosed C] [BraidedCategory C]
    (X : Arrow C) :
    ((leftBifunctor (curriedTensor C)).obj (mk (initial.to (ùüô_ C)))).obj X ‚âÖ X := by
  refine Arrow.isoMk ?_ (Œª_ X.right) ?_
  ¬∑ dsimp
    refine Iso.mk ?_ ?_ ?_ ?_
    ¬∑ refine pushout.desc (Œª_ X.left).hom ?_ ?_
      ¬∑ exact IsInitial.to (IsInitial.ofIso initialIsInitial (mulZero initialIsInitial).symm) _
      ¬∑ apply (IsInitial.ofIso initialIsInitial (mulZero initialIsInitial).symm).hom_ext
    ¬∑ exact (Œª_ X.left).inv ‚â´ pushout.inl _ _
    ¬∑ apply pushout.hom_ext
      ¬∑ simp
      ¬∑ apply (IsInitial.ofIso initialIsInitial (mulZero initialIsInitial).symm).hom_ext
    ¬∑ simp
  ¬∑ apply pushout.hom_ext
    ¬∑ simp
    ¬∑ apply (IsInitial.ofIso initialIsInitial (mulZero initialIsInitial).symm).hom_ext

@[simps!]
noncomputable
def rightUnitor [HasInitial C] [CartesianMonoidalCategory C] [MonoidalClosed C] [BraidedCategory C]
    (X : Arrow C) :
    ((leftBifunctor (curriedTensor C)).obj X).obj (mk (initial.to (ùüô_ C))) ‚âÖ X := by
  refine Arrow.isoMk ?_ (œÅ_ X.right) ?_
  ¬∑ dsimp
    refine Iso.mk ?_ ?_ ?_ ?_
    ¬∑ refine pushout.desc ?_ (œÅ_ X.left).hom ?_
      ¬∑ exact IsInitial.to (IsInitial.ofIso initialIsInitial (zeroMul initialIsInitial).symm) _
      ¬∑ apply (IsInitial.ofIso initialIsInitial (zeroMul initialIsInitial).symm).hom_ext
    ¬∑ exact (œÅ_ X.left).inv ‚â´ pushout.inr _ _
    ¬∑ apply pushout.hom_ext
      ¬∑ apply (IsInitial.ofIso initialIsInitial (zeroMul initialIsInitial).symm).hom_ext
      ¬∑ simp
    ¬∑ simp
  ¬∑ apply pushout.hom_ext
    ¬∑ apply (IsInitial.ofIso initialIsInitial (zeroMul initialIsInitial).symm).hom_ext
    ¬∑ simp

lemma associator_naturality
    [‚àÄ S : C, PreservesColimitsOfSize (tensorLeft S)]
    [‚àÄ S : C, PreservesColimitsOfSize (tensorRight S)]
    {X‚ÇÅ X‚ÇÇ X‚ÇÉ Y‚ÇÅ Y‚ÇÇ Y‚ÇÉ : Arrow C} (f‚ÇÅ : X‚ÇÅ ‚ü∂ Y‚ÇÅ) (f‚ÇÇ : X‚ÇÇ ‚ü∂ Y‚ÇÇ) (f‚ÇÉ : X‚ÇÉ ‚ü∂ Y‚ÇÉ) :
    (((leftBifunctor _).map (((leftBifunctor _).map f‚ÇÅ).app X‚ÇÇ ‚â´
    ((leftBifunctor _).obj Y‚ÇÅ).map f‚ÇÇ)).app X‚ÇÉ ‚â´
    ((leftBifunctor _).obj (((leftBifunctor _).obj Y‚ÇÅ).obj Y‚ÇÇ)).map f‚ÇÉ) ‚â´
    (associator Y‚ÇÅ.hom Y‚ÇÇ.hom Y‚ÇÉ.hom).hom =
    (associator X‚ÇÅ.hom X‚ÇÇ.hom X‚ÇÉ.hom).hom ‚â´
    ((leftBifunctor _).map f‚ÇÅ).app (((leftBifunctor _).obj X‚ÇÇ).obj X‚ÇÉ) ‚â´
    ((leftBifunctor _).obj Y‚ÇÅ).map (((leftBifunctor _).map f‚ÇÇ).app X‚ÇÉ ‚â´
    ((leftBifunctor _).obj Y‚ÇÇ).map f‚ÇÉ) := by
  apply Arrow.hom_ext
  ¬∑ apply pushout.hom_ext
    ¬∑ simp only [leftBifunctor_obj_obj_right, curriedTensor_obj_obj, id_obj,
        PushoutObjObj.ofHasPushout_pt, curriedTensor_map_app, curriedTensor_obj_map,
        PushoutObjObj.ofHasPushout_Œπ, mk_left, leftBifunctor_obj_obj_left,
        leftBifunctor_obj_obj_hom, map_comp, NatTrans.comp_app, Category.assoc, associator,
        associator_iso_left,
        associator_hom_left, associator_hom_left_aux, PushoutObjObj_whiskerRight_iso_hom,
        HasColimit.isoOfNatIso_hom_desc, associator_inv_left, tensorLeft_PushoutObjObj_iso_hom,
        Category.id_comp, comp_left, leftBifunctor_map_app_left, leftBifunctor_map_app_right,
        leftBifunctor_obj_map_right, whisker_assoc, leftBifunctor_obj_map_left, tensor_whiskerLeft,
        isoMk_hom_left, IsPushout.inl_desc_assoc, colimit.Œπ_desc, PushoutCocone.mk_pt,
        PushoutCocone.mk_Œπ_app, Iso.inv_hom_id_assoc, whiskerRight_tensor, colimit.Œπ_desc_assoc,
        span_left, IsPushout.inl_desc, whisker_exchange_assoc, Iso.hom_inv_id_assoc,
        ‚Üê MonoidalCategory.whiskerLeft_comp_assoc]
    ¬∑ apply (IsPushout_ofWhiskerRight' (IsPushout.of_hasPushout _ _)).hom_ext
      ¬∑ simp only [id_obj, curriedTensor_obj_obj, PushoutObjObj.ofHasPushout_pt,
        curriedTensor_map_app, curriedTensor_obj_map, PushoutObjObj.ofHasPushout_Œπ, mk_left,
        leftBifunctor_obj_obj_left, leftBifunctor_obj_obj_right, leftBifunctor_obj_obj_hom,
        map_comp, NatTrans.comp_app, Category.assoc, associator, associator_iso_left,
        associator_hom_left, associator_hom_left_aux, PushoutObjObj_whiskerRight_iso_hom,
        HasColimit.isoOfNatIso_hom_desc, associator_inv_left, tensorLeft_PushoutObjObj_iso_hom,
        Category.id_comp, comp_left, leftBifunctor_map_app_left, leftBifunctor_map_app_right,
        leftBifunctor_obj_map_right, whisker_assoc, leftBifunctor_obj_map_left, tensor_whiskerLeft,
        isoMk_hom_left, IsPushout.inr_desc_assoc, colimit.Œπ_desc, PushoutCocone.mk_pt,
        PushoutCocone.mk_Œπ_app, ‚Üê whisker_exchange_assoc, whisker_inl_desc_assoc, comp_whiskerRight,
        IsPushout.inl_isoPushout_hom_assoc, Cocones.precompose_obj_pt, Cocones.precompose_obj_Œπ,
        span_left, const_obj_obj, spanExt_hom_app_left, Iso.inv_hom_id_assoc, whiskerRight_tensor,
        colimit.Œπ_desc_assoc, span_right, IsPushout.inl_desc_assoc, IsPushout.inl_desc,
        Iso.cancel_iso_hom_left, ‚Üê MonoidalCategory.whiskerLeft_comp_assoc]
        rw [MonoidalCategory.whiskerLeft_comp, Category.assoc, IsPushout.inr_desc,
          associator_naturality_left_assoc, Iso.inv_hom_id_assoc]
        simp only [whisker_exchange_assoc, ‚Üê MonoidalCategory.whiskerLeft_comp_assoc]
      ¬∑ simp only [id_obj, curriedTensor_obj_obj, PushoutObjObj.ofHasPushout_pt,
        curriedTensor_map_app, curriedTensor_obj_map, PushoutObjObj.ofHasPushout_Œπ, mk_left,
        leftBifunctor_obj_obj_left, leftBifunctor_obj_obj_right, leftBifunctor_obj_obj_hom,
        map_comp, NatTrans.comp_app, Category.assoc, associator, associator_iso_left,
        associator_hom_left, associator_hom_left_aux, PushoutObjObj_whiskerRight_iso_hom,
        HasColimit.isoOfNatIso_hom_desc, associator_inv_left, tensorLeft_PushoutObjObj_iso_hom,
        Category.id_comp, comp_left, leftBifunctor_map_app_left, leftBifunctor_map_app_right,
        leftBifunctor_obj_map_right, whisker_assoc, leftBifunctor_obj_map_left, tensor_whiskerLeft,
        isoMk_hom_left, IsPushout.inr_desc_assoc, colimit.Œπ_desc, PushoutCocone.mk_pt,
        PushoutCocone.mk_Œπ_app, whisker_inr_desc_assoc, comp_whiskerRight, whiskerRight_tensor,
        colimit.Œπ_desc_assoc, span_right, IsPushout.inr_isoPushout_hom_assoc,
        Cocones.precompose_obj_pt, Cocones.precompose_obj_Œπ, const_obj_obj, spanExt_hom_app_right,
        IsPushout.inr_desc, Iso.hom_inv_id_assoc]
        rw [associator_naturality_left_assoc]
        simp only [whiskerRight_tensor, ‚Üê whisker_exchange_assoc, tensor_whiskerLeft,
          IsPushout.inr_isoPushout_hom_assoc, colimit.Œπ_desc, Cocones.precompose_obj_pt,
          PushoutCocone.mk_pt, Cocones.precompose_obj_Œπ, NatTrans.comp_app, span_right,
          const_obj_obj, spanExt_hom_app_right, PushoutCocone.mk_Œπ_app, Category.assoc,
          Iso.inv_hom_id_assoc, Iso.hom_inv_id_assoc]
  ¬∑ simp

lemma pentagon_aux
    [‚àÄ S : C, PreservesColimitsOfSize (tensorLeft S)]
    [‚àÄ S : C, PreservesColimitsOfSize (tensorRight S)]
    (W X Y Z : Arrow C) :
    pushout.inr (((W.hom ‚ñ° X.hom) ‚ñ° Y.hom) ‚ñ∑ Z.left)
      ((mk ((W.hom ‚ñ° X.hom) ‚ñ° Y.hom)).left ‚óÅ Z.hom) ‚â´
    leftBifunctor_map_left (curriedTensor C) Z (associator W.hom X.hom Y.hom).hom
        (.ofHasPushout _ ((W.hom ‚ñ° X.hom) ‚ñ° Y.hom) Z.hom)
        (.ofHasPushout _ (W.hom ‚ñ° X.hom ‚ñ° Y.hom) Z.hom) ‚â´
      (associator_hom_left W.hom (X.hom ‚ñ° Y.hom) Z.hom) ‚â´
    leftFunctor_map_left _ W (associator X.hom Y.hom Z.hom).hom
        (.ofHasPushout _ W.hom ((X.hom ‚ñ° Y.hom) ‚ñ° Z.hom))
        (.ofHasPushout _ W.hom (X.hom ‚ñ° Y.hom ‚ñ° Z.hom)) =
    pushout.inr (((W.hom ‚ñ° X.hom) ‚ñ° Y.hom) ‚ñ∑ Z.left)
      ((mk ((W.hom ‚ñ° X.hom) ‚ñ° Y.hom)).left ‚óÅ Z.hom) ‚â´
    ((associator_hom_left (W.hom ‚ñ° X.hom) Y.hom Z.hom) ‚â´
    (associator_hom_left W.hom X.hom (Y.hom ‚ñ° Z.hom))) := by
  apply (IsPushout_ofWhiskerRight' (IsPushout.of_hasPushout _ _)).hom_ext
  ¬∑ sorry
    /-
    simp [‚Üê comp_whiskerRight_assoc]
    rw [‚Üê MonoidalCategory.whiskerLeft_comp_assoc, ‚Üê MonoidalCategory.whiskerLeft_comp_assoc,
      ‚Üê MonoidalCategory.whiskerLeft_comp_assoc]
    simp
    -/
  ¬∑ apply (IsPushout_ofWhiskerRight' (IsPushout_ofWhiskerRight' (IsPushout.of_hasPushout _ _))).hom_ext
    ¬∑ simp only [id_obj, PushoutObjObj.ofHasPushout_pt, curriedTensor_obj_obj,
      curriedTensor_map_app, curriedTensor_obj_map, PushoutObjObj.ofHasPushout_Œπ, mk_left, mk_right,
      mk_hom, leftBifunctor_map_left, tensor_whiskerLeft, PushoutObjObj.ofHasPushout_inl,
      PushoutObjObj.ofHasPushout_inr, associator, associator_iso_left, associator_hom_left,
      associator_hom_left_aux, PushoutObjObj_whiskerRight_iso_hom, Category.assoc,
      HasColimit.isoOfNatIso_hom_desc, associator_inv_left, tensorLeft_PushoutObjObj_iso_hom,
      Category.id_comp, isoMk_hom_right, isoMk_hom_left, leftFunctor_map_left,
      IsPushout.inr_desc_assoc, colimit.Œπ_desc_assoc, span_right, PushoutCocone.mk_pt,
      PushoutCocone.mk_Œπ_app, whiskerRight_tensor, IsPushout.inr_isoPushout_hom_assoc,
      Cocones.precompose_obj_pt, Cocones.precompose_obj_Œπ, NatTrans.comp_app, const_obj_obj,
      spanExt_hom_app_right, colimit.Œπ_desc]
      rw [‚Üê comp_whiskerRight_assoc, ‚Üê comp_whiskerRight_assoc]
      simp only [Category.assoc, colimit.Œπ_desc, PushoutCocone.mk_pt, PushoutCocone.mk_Œπ_app,
        IsPushout.inl_isoPushout_hom_assoc, Cocones.precompose_obj_pt, Cocones.precompose_obj_Œπ,
        NatTrans.comp_app, span_left, const_obj_obj, spanExt_hom_app_left, comp_whiskerRight,
        whisker_assoc, colimit.Œπ_desc_assoc, IsPushout.inl_desc, Iso.inv_hom_id_assoc,
        ‚Üê MonoidalCategory.whiskerLeft_comp_assoc]
      simp only [IsPushout.inr_isoPushout_hom_assoc, colimit.Œπ_desc, Cocones.precompose_obj_pt,
        PushoutCocone.mk_pt, Cocones.precompose_obj_Œπ, NatTrans.comp_app, span_right, const_obj_obj,
        spanExt_hom_app_right, PushoutCocone.mk_Œπ_app, MonoidalCategory.whiskerLeft_comp,
        Category.assoc, pentagon_assoc, associator_naturality_left_assoc, whiskerRight_tensor,
        IsPushout.inl_isoPushout_hom_assoc, span_left, spanExt_hom_app_left]
    ¬∑ simp only [id_obj, curriedTensor_obj_obj, PushoutObjObj.ofHasPushout_pt,
      curriedTensor_map_app, curriedTensor_obj_map, PushoutObjObj.ofHasPushout_Œπ, mk_left, mk_right,
      mk_hom, leftBifunctor_map_left, tensor_whiskerLeft, PushoutObjObj.ofHasPushout_inl,
      PushoutObjObj.ofHasPushout_inr, associator, associator_iso_left, associator_hom_left,
      associator_hom_left_aux, PushoutObjObj_whiskerRight_iso_hom, Category.assoc,
      HasColimit.isoOfNatIso_hom_desc, associator_inv_left, tensorLeft_PushoutObjObj_iso_hom,
      Category.id_comp, isoMk_hom_right, isoMk_hom_left, leftFunctor_map_left,
      IsPushout.inr_desc_assoc, colimit.Œπ_desc_assoc, span_right, PushoutCocone.mk_pt,
      PushoutCocone.mk_Œπ_app, whiskerRight_tensor, IsPushout.inr_isoPushout_hom_assoc,
      Cocones.precompose_obj_pt, Cocones.precompose_obj_Œπ, NatTrans.comp_app, const_obj_obj,
      spanExt_hom_app_right, colimit.Œπ_desc]
      rw [‚Üê comp_whiskerRight_assoc, ‚Üê comp_whiskerRight_assoc]
      simp only [Category.assoc, colimit.Œπ_desc, PushoutCocone.mk_pt, PushoutCocone.mk_Œπ_app,
        IsPushout.inr_isoPushout_hom_assoc, Cocones.precompose_obj_pt, Cocones.precompose_obj_Œπ,
        NatTrans.comp_app, span_right, const_obj_obj, spanExt_hom_app_right, comp_whiskerRight,
        colimit.Œπ_desc_assoc, IsPushout.inr_desc, pentagon_assoc]
      simp only [whiskerRight_tensor, IsPushout.inr_isoPushout_hom_assoc, colimit.Œπ_desc,
        Cocones.precompose_obj_pt, PushoutCocone.mk_pt, Cocones.precompose_obj_Œπ, NatTrans.comp_app,
        span_right, const_obj_obj, spanExt_hom_app_right, PushoutCocone.mk_Œπ_app,
        associator_naturality_left_assoc]

lemma pentagon
    [‚àÄ S : C, PreservesColimitsOfSize (tensorLeft S)]
    [‚àÄ S : C, PreservesColimitsOfSize (tensorRight S)]
    (W X Y Z : Arrow C) :
    ((leftBifunctor _).map (associator W.hom X.hom Y.hom).hom).app Z ‚â´
    (associator W.hom (((leftBifunctor _).obj X).obj Y).hom Z.hom).hom ‚â´
    ((leftBifunctor _).obj W).map (associator X.hom Y.hom Z.hom).hom =
    (associator (((leftBifunctor _).obj W).obj X).hom Y.hom Z.hom).hom ‚â´
    (associator W.hom X.hom (((leftBifunctor _).obj Y).obj Z).hom).hom := by
  apply Arrow.hom_ext
  ¬∑ apply pushout.hom_ext
    ¬∑ simp only [id_obj, PushoutObjObj.ofHasPushout_pt, curriedTensor_obj_obj,
      curriedTensor_map_app, curriedTensor_obj_map, PushoutObjObj.ofHasPushout_Œπ, mk_right,
      leftBifunctor_obj_obj_left, mk_left, mk_hom, associator, associator_iso_left,
      associator_hom_left, associator_hom_left_aux, PushoutObjObj_whiskerRight_iso_hom,
      Category.assoc, HasColimit.isoOfNatIso_hom_desc, associator_inv_left,
      tensorLeft_PushoutObjObj_iso_hom, Category.id_comp, leftBifunctor_obj_obj_right,
      leftBifunctor_obj_obj_hom, comp_left, leftBifunctor_map_app_left, tensor_whiskerLeft,
      isoMk_hom_right, isoMk_hom_left, leftBifunctor_obj_map_left, IsPushout.inl_desc_assoc,
      colimit.Œπ_desc_assoc, span_left, PushoutCocone.mk_pt, PushoutCocone.mk_Œπ_app,
      IsPushout.inl_desc, whiskerRight_tensor, colimit.Œπ_desc, Iso.inv_hom_id_assoc,
      ‚Üê MonoidalCategory.whiskerLeft_comp_assoc]
      simp only [MonoidalCategory.whiskerLeft_comp, Category.assoc, pentagon_assoc]
    ¬∑ exact pentagon_aux ..
  ¬∑ exact MonoidalCategory.pentagon W.right X.right Y.right Z.right

end PushoutProduct

noncomputable
instance [HasPushouts C] [HasInitial C] [CartesianMonoidalCategory C]
    [MonoidalClosed C] [BraidedCategory C]
    [‚àÄ S : C, PreservesColimitsOfSize (tensorLeft S)]
    [‚àÄ S : C, PreservesColimitsOfSize (tensorRight S)] : MonoidalCategory (Arrow C) where
  tensorObj X Y := ((leftBifunctor (curriedTensor C)).obj X).obj Y
  whiskerLeft X _ _ f := ((leftBifunctor (curriedTensor C)).obj X).map f
  whiskerRight f X := ((leftBifunctor (curriedTensor C)).map f).app X
  tensorUnit := initial.to (ùüô_ C)
  associator X Y Z := PushoutProduct.associator X.hom Y.hom Z.hom
  associator_naturality := PushoutProduct.associator_naturality
  pentagon := PushoutProduct.pentagon
  leftUnitor := PushoutProduct.leftUnitor
  leftUnitor_naturality f := by
    apply Arrow.hom_ext
    ¬∑ apply pushout.hom_ext
      ¬∑ simp
      ¬∑ apply (IsInitial.ofIso initialIsInitial (mulZero initialIsInitial).symm).hom_ext
    ¬∑ exact leftUnitor_naturality f.right
  rightUnitor := PushoutProduct.rightUnitor
  rightUnitor_naturality f := by
    apply Arrow.hom_ext
    ¬∑ apply pushout.hom_ext
      ¬∑ apply (IsInitial.ofIso initialIsInitial (zeroMul initialIsInitial).symm).hom_ext
      ¬∑ simp
    ¬∑ exact rightUnitor_naturality f.right
  triangle X Y := by
    apply Arrow.hom_ext
    ¬∑ apply pushout.hom_ext
      ¬∑ simp [‚Üê MonoidalCategory.whiskerLeft_comp_assoc]
      ¬∑ apply (IsPushout_ofWhiskerRight' (IsPushout.of_hasPushout _ _)).hom_ext
        ¬∑ apply (IsInitial.ofIso initialIsInitial ((initialIsoIsInitial ?_) ‚â™‚â´
            (mulZero ?_).symm)).hom_ext
          all_goals exact IsInitial.ofIso initialIsInitial (zeroMul initialIsInitial).symm
        ¬∑ simp [‚Üê comp_whiskerRight_assoc]
    ¬∑ exact MonoidalCategory.triangle X.right Y.right

end CategoryTheory.Arrow
