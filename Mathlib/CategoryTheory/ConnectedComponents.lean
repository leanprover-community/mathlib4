/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.Data.List.Chain
import Mathlib.CategoryTheory.IsConnected
import Mathlib.CategoryTheory.Sigma.Basic
import Mathlib.CategoryTheory.FullSubcategory

#align_import category_theory.connected_components from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# Connected components of a category

Defines a type `ConnectedComponents J` indexing the connected components of a category, and the
full subcategories giving each connected component: `Component j : Type u‚ÇÅ`.
We show that each `Component j` is in fact connected.

We show every category can be expressed as a disjoint union of its connected components, in
particular `Decomposed J` is the category (definitionally) given by the sigma-type of the connected
components of `J`, and it is shown that this is equivalent to `J`.
-/

set_option autoImplicit true


universe v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ

noncomputable section

open CategoryTheory.Category

namespace CategoryTheory

attribute [instance 100] IsConnected.is_nonempty

variable {J : Type u‚ÇÅ} [Category.{v‚ÇÅ} J]

variable {C : Type u‚ÇÇ} [Category.{u‚ÇÅ} C]

/-- This type indexes the connected components of the category `J`. -/
def ConnectedComponents (J : Type u‚ÇÅ) [Category.{v‚ÇÅ} J] : Type u‚ÇÅ :=
  Quotient (Zigzag.setoid J)
#align category_theory.connected_components CategoryTheory.ConnectedComponents

instance [Inhabited J] : Inhabited (ConnectedComponents J) :=
  ‚ü®Quotient.mk'' default‚ü©

/-- Given an index for a connected component, produce the actual component as a full subcategory. -/
def Component (j : ConnectedComponents J) : Type u‚ÇÅ :=
  FullSubcategory fun k => Quotient.mk'' k = j
#align category_theory.component CategoryTheory.Component

instance : Category (Component (j : ConnectedComponents J)) :=
  FullSubcategory.category _

--porting note : it was originally @[simps (config := { rhsMd := semireducible })]
/-- The inclusion functor from a connected component to the whole category. -/
@[simps!]
def Component.Œπ (j : ConnectedComponents J) : Component j ‚•§ J :=
  fullSubcategoryInclusion _
#align category_theory.component.Œπ CategoryTheory.Component.Œπ

instance : Full (Component.Œπ (j : ConnectedComponents J)) :=
  FullSubcategory.full _

instance : Faithful (Component.Œπ (j : ConnectedComponents J)) :=
  FullSubcategory.faithful _

/-- Each connected component of the category is nonempty. -/
instance (j : ConnectedComponents J) : Nonempty (Component j) := by
  induction j using Quotient.inductionOn'
  -- ‚ä¢ Nonempty (Component (Quotient.mk'' a‚úù))
  exact ‚ü®‚ü®_, rfl‚ü©‚ü©
  -- üéâ no goals

instance (j : ConnectedComponents J) : Inhabited (Component j) :=
  Classical.inhabited_of_nonempty'

/-- Each connected component of the category is connected. -/
instance (j : ConnectedComponents J) : IsConnected (Component j) := by
  -- Show it's connected by constructing a zigzag (in `Component j`) between any two objects
  apply isConnected_of_zigzag
  -- ‚ä¢ ‚àÄ (j‚ÇÅ j‚ÇÇ : Component j), ‚àÉ l, List.Chain Zag j‚ÇÅ l ‚àß List.getLast (j‚ÇÅ :: l) ( ‚Ä¶
  rintro ‚ü®j‚ÇÅ, hj‚ÇÅ‚ü© ‚ü®j‚ÇÇ, rfl‚ü©
  -- ‚ä¢ ‚àÉ l, List.Chain Zag { obj := j‚ÇÅ, property := hj‚ÇÅ } l ‚àß List.getLast ({ obj : ‚Ä¶
  -- We know that the underlying objects j‚ÇÅ j‚ÇÇ have some zigzag between them in `J`
  have h‚ÇÅ‚ÇÇ : Zigzag j‚ÇÅ j‚ÇÇ := Quotient.exact' hj‚ÇÅ
  -- ‚ä¢ ‚àÉ l, List.Chain Zag { obj := j‚ÇÅ, property := hj‚ÇÅ } l ‚àß List.getLast ({ obj : ‚Ä¶
  -- Get an explicit zigzag as a list
  rcases List.exists_chain_of_relationReflTransGen h‚ÇÅ‚ÇÇ with ‚ü®l, hl‚ÇÅ, hl‚ÇÇ‚ü©
  -- ‚ä¢ ‚àÉ l, List.Chain Zag { obj := j‚ÇÅ, property := hj‚ÇÅ } l ‚àß List.getLast ({ obj : ‚Ä¶
  -- Everything which has a zigzag to j‚ÇÇ can be lifted to the same component as `j‚ÇÇ`.
  let f : ‚àÄ x, Zigzag x j‚ÇÇ ‚Üí Component (Quotient.mk'' j‚ÇÇ) := fun x h => ‚ü®x, Quotient.sound' h‚ü©
  -- ‚ä¢ ‚àÉ l, List.Chain Zag { obj := j‚ÇÅ, property := hj‚ÇÅ } l ‚àß List.getLast ({ obj : ‚Ä¶
  -- Everything in our chosen zigzag from `j‚ÇÅ` to `j‚ÇÇ` has a zigzag to `j‚ÇÇ`.
  have hf : ‚àÄ a : J, a ‚àà l ‚Üí Zigzag a j‚ÇÇ := by
    intro i hi
    apply List.Chain.induction (fun t => Zigzag t j‚ÇÇ) _ hl‚ÇÅ hl‚ÇÇ _ _ _ (List.mem_of_mem_tail hi)
    ¬∑ intro j k
      apply Relation.ReflTransGen.head
    ¬∑ apply Relation.ReflTransGen.refl
  -- Now lift the zigzag from `j‚ÇÅ` to `j‚ÇÇ` in `J` to the same thing in `component j`.
  refine' ‚ü®l.pmap f hf, _, _‚ü©
  -- ‚ä¢ List.Chain Zag { obj := j‚ÇÅ, property := hj‚ÇÅ } (List.pmap f l hf)
  ¬∑ refine' @List.chain_pmap_of_chain _ _ _ _ _ f (fun x y _ _ h => _) _ _ hl‚ÇÅ h‚ÇÅ‚ÇÇ _
    -- ‚ä¢ Zag (f x x‚úù¬π) (f y x‚úù)
    exact zag_of_zag_obj (Component.Œπ _) h
    -- üéâ no goals
  ¬∑ erw [List.getLast_pmap _ f (j‚ÇÅ :: l) (by simpa [h‚ÇÅ‚ÇÇ] using hf) (List.cons_ne_nil _ _)]
    -- ‚ä¢ f (List.getLast (j‚ÇÅ :: l) (_ : j‚ÇÅ :: l ‚â† [])) (_ : Zigzag (List.getLast (j‚ÇÅ  ‚Ä¶
    exact FullSubcategory.ext _ _ hl‚ÇÇ
    -- üéâ no goals

/-- The disjoint union of `J`s connected components, written explicitly as a sigma-type with the
category structure.
This category is equivalent to `J`.
-/
abbrev Decomposed (J : Type u‚ÇÅ) [Category.{v‚ÇÅ} J] :=
  Œ£j : ConnectedComponents J, Component j
#align category_theory.decomposed CategoryTheory.Decomposed

-- This name may cause clashes further down the road, and so might need to be changed.
/--
The inclusion of each component into the decomposed category. This is just `sigma.incl` but having
this abbreviation helps guide typeclass search to get the right category instance on `decomposed J`.
-/
abbrev inclusion (j : ConnectedComponents J) : Component j ‚•§ Decomposed J :=
  Sigma.incl _
#align category_theory.inclusion CategoryTheory.inclusion

--porting note : it was originally @[simps (config := { rhsMd := semireducible })]
/-- The forward direction of the equivalence between the decomposed category and the original. -/
@[simps!]
def decomposedTo (J : Type u‚ÇÅ) [Category.{v‚ÇÅ} J] : Decomposed J ‚•§ J :=
  Sigma.desc Component.Œπ
#align category_theory.decomposed_to CategoryTheory.decomposedTo

@[simp]
theorem inclusion_comp_decomposedTo (j : ConnectedComponents J) :
    inclusion j ‚ãô decomposedTo J = Component.Œπ j :=
  rfl
#align category_theory.inclusion_comp_decomposed_to CategoryTheory.inclusion_comp_decomposedTo

instance : Full (decomposedTo J)
    where
  preimage := by
    rintro ‚ü®j', X, hX‚ü© ‚ü®k', Y, hY‚ü© f
    -- ‚ä¢ { fst := j', snd := { obj := X, property := hX } } ‚ü∂ { fst := k', snd := { o ‚Ä¶
    dsimp at f
    -- ‚ä¢ { fst := j', snd := { obj := X, property := hX } } ‚ü∂ { fst := k', snd := { o ‚Ä¶
    have : j' = k'
    -- ‚ä¢ j' = k'
    rw [‚Üê hX, ‚Üê hY, Quotient.eq'']
    -- ‚ä¢ Setoid.r X Y
    exact Relation.ReflTransGen.single (Or.inl ‚ü®f‚ü©)
    -- ‚ä¢ { fst := j', snd := { obj := X, property := hX } } ‚ü∂ { fst := k', snd := { o ‚Ä¶
    subst this
    -- ‚ä¢ { fst := j', snd := { obj := X, property := hX } } ‚ü∂ { fst := j', snd := { o ‚Ä¶
    exact Sigma.SigmaHom.mk f
    -- üéâ no goals
  witness := by
    rintro ‚ü®j', X, hX‚ü© ‚ü®_, Y, rfl‚ü© f
    -- ‚ä¢ (decomposedTo J).map (Sigma.casesOn (motive := fun x => {Y : Decomposed J} ‚Üí ‚Ä¶
    have : Quotient.mk'' Y = j' := by
      rw [‚Üê hX, Quotient.eq'']
      exact Relation.ReflTransGen.single (Or.inr ‚ü®f‚ü©)
    subst this
    -- ‚ä¢ (decomposedTo J).map (Sigma.casesOn (motive := fun x => {Y : Decomposed J} ‚Üí ‚Ä¶
    rfl
    -- üéâ no goals

instance : Faithful (decomposedTo J) where
  map_injective := by
    rintro ‚ü®_, j, rfl‚ü© ‚ü®_, k, hY‚ü© ‚ü®f‚ü© ‚ü®_‚ü© rfl
    -- ‚ä¢ Sigma.SigmaHom.mk f = Sigma.SigmaHom.mk ((decomposedTo J).map (Sigma.SigmaHo ‚Ä¶
    rfl
    -- üéâ no goals

instance : EssSurj (decomposedTo J) where mem_essImage j := ‚ü®‚ü®_, j, rfl‚ü©, ‚ü®Iso.refl _‚ü©‚ü©

instance : IsEquivalence (decomposedTo J) :=
  Equivalence.ofFullyFaithfullyEssSurj _

-- porting note: it was originally @[simps (config := { rhsMd := semireducible }) Functor]
/-- This gives that any category is equivalent to a disjoint union of connected categories. -/
@[simps! functor]
def decomposedEquiv : Decomposed J ‚âå J :=
  (decomposedTo J).asEquivalence
#align category_theory.decomposed_equiv CategoryTheory.decomposedEquiv

end CategoryTheory
