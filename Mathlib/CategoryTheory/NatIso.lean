/-
Copyright (c) 2017 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Tim Baumann, Stephen Morgan, Kim Morrison, Floris van Doorn
-/
import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.Iso

/-!
# Natural isomorphisms

For the most part, natural isomorphisms are just another sort of isomorphism.

We provide some special support for extracting components:
* if `Î± : F â‰… G`, then `a.app X : F.obj X â‰… G.obj X`,
and building natural isomorphisms from components:
*
```
NatIso.ofComponents
  (app : âˆ€ X : C, F.obj X â‰… G.obj X)
  (naturality : âˆ€ {X Y : C} (f : X âŸ¶ Y), F.map f â‰« (app Y).hom = (app X).hom â‰« G.map f) :
F â‰… G
```
only needing to check naturality in one direction.

## Implementation

Note that `NatIso` is a namespace without a corresponding definition;
we put some declarations that are specifically about natural isomorphisms in the `Iso`
namespace so that they are available using dot notation.
-/

-- declare the `v`'s first; see `CategoryTheory.Category` for an explanation
universe vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ uâ‚ uâ‚‚ uâ‚ƒ uâ‚„

namespace CategoryTheory

open NatTrans

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D] {E : Type uâ‚ƒ}
  [Category.{vâ‚ƒ} E] {E' : Type uâ‚„} [Category.{vâ‚„} E']

namespace Iso

/-- The application of a natural isomorphism to an object. We put this definition in a different
namespace, so that we can use `Î±.app` -/
@[simps]
def app {F G : C â¥¤ D} (Î± : F â‰… G) (X : C) :
    F.obj X â‰… G.obj X where
  hom := Î±.hom.app X
  inv := Î±.inv.app X
  hom_inv_id := by rw [â† comp_app, Iso.hom_inv_id]; rfl
  inv_hom_id := by rw [â† comp_app, Iso.inv_hom_id]; rfl

@[reassoc (attr := simp)]
theorem hom_inv_id_app {F G : C â¥¤ D} (Î± : F â‰… G) (X : C) :
    Î±.hom.app X â‰« Î±.inv.app X = ğŸ™ (F.obj X) :=
  congr_fun (congr_arg NatTrans.app Î±.hom_inv_id) X

@[reassoc (attr := simp)]
theorem inv_hom_id_app {F G : C â¥¤ D} (Î± : F â‰… G) (X : C) :
    Î±.inv.app X â‰« Î±.hom.app X = ğŸ™ (G.obj X) :=
  congr_fun (congr_arg NatTrans.app Î±.inv_hom_id) X

@[reassoc (attr := simp)]
lemma hom_inv_id_app_app {F G : C â¥¤ D â¥¤ E} (e : F â‰… G) (Xâ‚ : C) (Xâ‚‚ : D) :
    (e.hom.app Xâ‚).app Xâ‚‚ â‰« (e.inv.app Xâ‚).app Xâ‚‚ = ğŸ™ _ := by
  rw [â† NatTrans.comp_app, Iso.hom_inv_id_app, NatTrans.id_app]

@[reassoc (attr := simp)]
lemma inv_hom_id_app_app {F G : C â¥¤ D â¥¤ E} (e : F â‰… G) (Xâ‚ : C) (Xâ‚‚ : D) :
    (e.inv.app Xâ‚).app Xâ‚‚ â‰« (e.hom.app Xâ‚).app Xâ‚‚ = ğŸ™ _ := by
  rw [â† NatTrans.comp_app, Iso.inv_hom_id_app, NatTrans.id_app]

@[reassoc (attr := simp)]
lemma hom_inv_id_app_app_app {F G : C â¥¤ D â¥¤ E â¥¤ E'} (e : F â‰… G)
    (Xâ‚ : C) (Xâ‚‚ : D) (Xâ‚ƒ : E) :
    ((e.hom.app Xâ‚).app Xâ‚‚).app Xâ‚ƒ â‰« ((e.inv.app Xâ‚).app Xâ‚‚).app Xâ‚ƒ = ğŸ™ _ := by
  rw [â† NatTrans.comp_app, â† NatTrans.comp_app, Iso.hom_inv_id_app,
    NatTrans.id_app, NatTrans.id_app]

@[reassoc (attr := simp)]
lemma inv_hom_id_app_app_app {F G : C â¥¤ D â¥¤ E â¥¤ E'} (e : F â‰… G)
    (Xâ‚ : C) (Xâ‚‚ : D) (Xâ‚ƒ : E) :
    ((e.inv.app Xâ‚).app Xâ‚‚).app Xâ‚ƒ â‰« ((e.hom.app Xâ‚).app Xâ‚‚).app Xâ‚ƒ = ğŸ™ _ := by
  rw [â† NatTrans.comp_app, â† NatTrans.comp_app, Iso.inv_hom_id_app,
    NatTrans.id_app, NatTrans.id_app]

end Iso

namespace NatIso

open CategoryTheory.Category CategoryTheory.Functor

@[simp]
theorem trans_app {F G H : C â¥¤ D} (Î± : F â‰… G) (Î² : G â‰… H) (X : C) :
    (Î± â‰ªâ‰« Î²).app X = Î±.app X â‰ªâ‰« Î².app X :=
  rfl

@[deprecated Iso.app_hom (since := "2025-03-11")]
theorem app_hom {F G : C â¥¤ D} (Î± : F â‰… G) (X : C) : (Î±.app X).hom = Î±.hom.app X :=
  rfl

@[deprecated Iso.app_hom (since := "2025-03-11")]
theorem app_inv {F G : C â¥¤ D} (Î± : F â‰… G) (X : C) : (Î±.app X).inv = Î±.inv.app X :=
  rfl

variable {F G : C â¥¤ D}

instance hom_app_isIso (Î± : F â‰… G) (X : C) : IsIso (Î±.hom.app X) :=
  âŸ¨âŸ¨Î±.inv.app X,
    âŸ¨by rw [â† comp_app, Iso.hom_inv_id, â† id_app], by rw [â† comp_app, Iso.inv_hom_id, â† id_app]âŸ©âŸ©âŸ©

instance inv_app_isIso (Î± : F â‰… G) (X : C) : IsIso (Î±.inv.app X) :=
  âŸ¨âŸ¨Î±.hom.app X,
    âŸ¨by rw [â† comp_app, Iso.inv_hom_id, â† id_app], by rw [â† comp_app, Iso.hom_inv_id, â† id_app]âŸ©âŸ©âŸ©

section

/-!
Unfortunately we need a separate set of cancellation lemmas for components of natural isomorphisms,
because the `simp` normal form is `Î±.hom.app X`, rather than `Î±.app.hom X`.

(With the latter, the morphism would be visibly part of an isomorphism, so general lemmas about
isomorphisms would apply.)

In the future, we should consider a redesign that changes this simp norm form,
but for now it breaks too many proofs.
-/


variable (Î± : F â‰… G)

@[simp]
theorem cancel_natIso_hom_left {X : C} {Z : D} (g g' : G.obj X âŸ¶ Z) :
    Î±.hom.app X â‰« g = Î±.hom.app X â‰« g' â†” g = g' := by simp only [cancel_epi, refl]

@[simp]
theorem cancel_natIso_inv_left {X : C} {Z : D} (g g' : F.obj X âŸ¶ Z) :
    Î±.inv.app X â‰« g = Î±.inv.app X â‰« g' â†” g = g' := by simp only [cancel_epi, refl]

@[simp]
theorem cancel_natIso_hom_right {X : D} {Y : C} (f f' : X âŸ¶ F.obj Y) :
    f â‰« Î±.hom.app Y = f' â‰« Î±.hom.app Y â†” f = f' := by simp only [cancel_mono, refl]

@[simp]
theorem cancel_natIso_inv_right {X : D} {Y : C} (f f' : X âŸ¶ G.obj Y) :
    f â‰« Î±.inv.app Y = f' â‰« Î±.inv.app Y â†” f = f' := by simp only [cancel_mono, refl]

@[simp]
theorem cancel_natIso_hom_right_assoc {W X X' : D} {Y : C} (f : W âŸ¶ X) (g : X âŸ¶ F.obj Y)
    (f' : W âŸ¶ X') (g' : X' âŸ¶ F.obj Y) :
    f â‰« g â‰« Î±.hom.app Y = f' â‰« g' â‰« Î±.hom.app Y â†” f â‰« g = f' â‰« g' := by
  simp only [â† Category.assoc, cancel_mono, refl]

@[simp]
theorem cancel_natIso_inv_right_assoc {W X X' : D} {Y : C} (f : W âŸ¶ X) (g : X âŸ¶ G.obj Y)
    (f' : W âŸ¶ X') (g' : X' âŸ¶ G.obj Y) :
    f â‰« g â‰« Î±.inv.app Y = f' â‰« g' â‰« Î±.inv.app Y â†” f â‰« g = f' â‰« g' := by
  simp only [â† Category.assoc, cancel_mono, refl]

@[simp]
theorem inv_inv_app {F G : C â¥¤ D} (e : F â‰… G) (X : C) : inv (e.inv.app X) = e.hom.app X := by
  aesop_cat

end

variable {X Y : C}

theorem naturality_1 (Î± : F â‰… G) (f : X âŸ¶ Y) : Î±.inv.app X â‰« F.map f â‰« Î±.hom.app Y = G.map f := by
  simp

theorem naturality_2 (Î± : F â‰… G) (f : X âŸ¶ Y) : Î±.hom.app X â‰« G.map f â‰« Î±.inv.app Y = F.map f := by
  simp

theorem naturality_1' (Î± : F âŸ¶ G) (f : X âŸ¶ Y) {_ : IsIso (Î±.app X)} :
    inv (Î±.app X) â‰« F.map f â‰« Î±.app Y = G.map f := by simp

@[reassoc (attr := simp)]
theorem naturality_2' (Î± : F âŸ¶ G) (f : X âŸ¶ Y) {_ : IsIso (Î±.app Y)} :
    Î±.app X â‰« G.map f â‰« inv (Î±.app Y) = F.map f := by
  rw [â† Category.assoc, â† naturality, Category.assoc, IsIso.hom_inv_id, Category.comp_id]

/-- The components of a natural isomorphism are isomorphisms.
-/
instance isIso_app_of_isIso (Î± : F âŸ¶ G) [IsIso Î±] (X) : IsIso (Î±.app X) :=
  âŸ¨âŸ¨(inv Î±).app X,
      âŸ¨congr_fun (congr_arg NatTrans.app (IsIso.hom_inv_id Î±)) X,
        congr_fun (congr_arg NatTrans.app (IsIso.inv_hom_id Î±)) XâŸ©âŸ©âŸ©

@[simp]
theorem isIso_inv_app (Î± : F âŸ¶ G) {_ : IsIso Î±} (X) : (inv Î±).app X = inv (Î±.app X) := by
  -- Porting note: the next lemma used to be in `ext`, but that is no longer allowed.
  -- We've added an aesop apply rule;
  -- it would be nice to have a hook to run those without aesop warning it didn't close the goal.
  apply IsIso.eq_inv_of_hom_inv_id
  rw [â† NatTrans.comp_app]
  simp

@[simp]
theorem inv_map_inv_app (F : C â¥¤ D â¥¤ E) {X Y : C} (e : X â‰… Y) (Z : D) :
    inv ((F.map e.inv).app Z) = (F.map e.hom).app Z := by
  aesop_cat

/-- Construct a natural isomorphism between functors by giving object level isomorphisms,
and checking naturality only in the forward direction.
-/
@[simps]
def ofComponents (app : âˆ€ X : C, F.obj X â‰… G.obj X)
    (naturality : âˆ€ {X Y : C} (f : X âŸ¶ Y),
      F.map f â‰« (app Y).hom = (app X).hom â‰« G.map f := by aesop_cat) :
    F â‰… G where
  hom := { app := fun X => (app X).hom }
  inv :=
    { app := fun X => (app X).inv,
      naturality := fun X Y f => by
        have h := congr_arg (fun f => (app X).inv â‰« f â‰« (app Y).inv) (naturality f).symm
        simp only [Iso.inv_hom_id_assoc, Iso.hom_inv_id, assoc, comp_id, cancel_mono] at h
        exact h }

@[simp]
theorem ofComponents.app (app' : âˆ€ X : C, F.obj X â‰… G.obj X) (naturality) (X) :
    (ofComponents app' naturality).app X = app' X := by aesop

-- Making this an instance would cause a typeclass inference loop with `isIso_app_of_isIso`.
/-- A natural transformation is an isomorphism if all its components are isomorphisms.
-/
theorem isIso_of_isIso_app (Î± : F âŸ¶ G) [âˆ€ X : C, IsIso (Î±.app X)] : IsIso Î± :=
  (ofComponents (fun X => asIso (Î±.app X)) (by simp)).isIso_hom

/-- Horizontal composition of natural isomorphisms. -/
@[simps]
def hcomp {F G : C â¥¤ D} {H I : D â¥¤ E} (Î± : F â‰… G) (Î² : H â‰… I) : F â‹™ H â‰… G â‹™ I := by
  refine âŸ¨Î±.hom â—« Î².hom, Î±.inv â—« Î².inv, ?_, ?_âŸ©
  Â· ext
    rw [â† NatTrans.exchange]
    simp
  ext; rw [â† NatTrans.exchange]; simp

theorem isIso_map_iff {Fâ‚ Fâ‚‚ : C â¥¤ D} (e : Fâ‚ â‰… Fâ‚‚) {X Y : C} (f : X âŸ¶ Y) :
    IsIso (Fâ‚.map f) â†” IsIso (Fâ‚‚.map f) := by
  revert Fâ‚ Fâ‚‚
  suffices âˆ€ {Fâ‚ Fâ‚‚ : C â¥¤ D} (_ : Fâ‚ â‰… Fâ‚‚) (_ : IsIso (Fâ‚.map f)), IsIso (Fâ‚‚.map f) by
    exact fun Fâ‚ Fâ‚‚ e => âŸ¨this e, this e.symmâŸ©
  intro Fâ‚ Fâ‚‚ e hf
  refine IsIso.mk âŸ¨e.inv.app Y â‰« inv (Fâ‚.map f) â‰« e.hom.app X, ?_, ?_âŸ©
  Â· simp only [NatTrans.naturality_assoc, IsIso.hom_inv_id_assoc, Iso.inv_hom_id_app]
  Â· simp only [assoc, â† e.hom.naturality, IsIso.inv_hom_id_assoc, Iso.inv_hom_id_app]

end NatIso

lemma NatTrans.isIso_iff_isIso_app {F G : C â¥¤ D} (Ï„ : F âŸ¶ G) :
    IsIso Ï„ â†” âˆ€ X, IsIso (Ï„.app X) :=
  âŸ¨fun _ â†¦ inferInstance, fun _ â†¦ NatIso.isIso_of_isIso_app _âŸ©

namespace Functor

variable (F : C â¥¤ D) (obj : C â†’ D) (e : âˆ€ X, F.obj X â‰… obj X)

/-- Constructor for a functor that is isomorphic to a given functor `F : C â¥¤ D`,
while being definitionally equal on objects to a given map `obj : C â†’ D`
such that for all `X : C`, we have an isomorphism `F.obj X â‰… obj X`. -/
@[simps obj]
def copyObj : C â¥¤ D where
  obj := obj
  map f := (e _).inv â‰« F.map f â‰« (e _).hom

/-- The functor constructed with `copyObj` is isomorphic to the given functor. -/
@[simps!]
def isoCopyObj : F â‰… F.copyObj obj e :=
  NatIso.ofComponents e (by simp [Functor.copyObj])

end Functor

end CategoryTheory
