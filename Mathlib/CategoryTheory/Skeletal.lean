/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Adjunction.Basic
import Mathlib.CategoryTheory.Category.Preorder
import Mathlib.CategoryTheory.IsomorphismClasses
import Mathlib.CategoryTheory.Thin

/-!
# Skeleton of a category

Define skeletal categories as categories in which any two isomorphic objects are equal.

Construct the skeleton of an arbitrary category by taking isomorphism classes, and show it is a
skeleton of the original category.

In addition, construct the skeleton of a thin category as a partial ordering, and (noncomputably)
show it is a skeleton of the original category. The advantage of this special case being handled
separately is that lemmas and definitions about orderings can be used directly, for example for the
subobject lattice. In addition, some of the commutative diagrams about the functors commute
definitionally on the nose which is convenient in practice.
-/


universe v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

namespace CategoryTheory

open Category

variable (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C]
variable (D : Type u‚ÇÇ) [Category.{v‚ÇÇ} D]
variable {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E]

/-- A category is skeletal if isomorphic objects are equal. -/
def Skeletal : Prop :=
  ‚àÄ ‚¶ÉX Y : C‚¶Ñ, IsIsomorphic X Y ‚Üí X = Y

/-- `IsSkeletonOf C D F` says that `F : D ‚•§ C` exhibits `D` as a skeletal full subcategory of `C`,
in particular `F` is a (strong) equivalence and `D` is skeletal.
-/
structure IsSkeletonOf (F : D ‚•§ C) : Prop where
  /-- The category `D` has isomorphic objects equal -/
  skel : Skeletal D
  /-- The functor `F` is an equivalence -/
  eqv : F.IsEquivalence := by infer_instance

attribute [local instance] isIsomorphicSetoid

variable {C D}

/-- If `C` is thin and skeletal, then any naturally isomorphic functors to `C` are equal. -/
theorem Functor.eq_of_iso {F‚ÇÅ F‚ÇÇ : D ‚•§ C} [Quiver.IsThin C] (hC : Skeletal C) (hF : F‚ÇÅ ‚âÖ F‚ÇÇ) :
    F‚ÇÅ = F‚ÇÇ :=
  Functor.ext (fun X => hC ‚ü®hF.app X‚ü©) fun _ _ _ => Subsingleton.elim _ _

/-- If `C` is thin and skeletal, `D ‚•§ C` is skeletal.
`CategoryTheory.functor_thin` shows it is thin also.
-/
theorem functor_skeletal [Quiver.IsThin C] (hC : Skeletal C) : Skeletal (D ‚•§ C) := fun _ _ h =>
  h.elim (Functor.eq_of_iso hC)

variable (C D)

noncomputable section

/-- Construct the skeleton category as the induced category on the isomorphism classes, and derive
its category structure.
-/
def Skeleton : Type u‚ÇÅ := InducedCategory (C := Quotient (isIsomorphicSetoid C)) C Quotient.out
deriving
  Category,
  [Inhabited C] ‚Üí Inhabited _,
  (Œ± : Sort _) ‚Üí [CoeSort C Œ±] ‚Üí CoeSort _ Œ±

end

/-- The functor from the skeleton of `C` to `C`. -/
@[simps!]
noncomputable def fromSkeleton : Skeleton C ‚•§ C :=
  inducedFunctor _
-- The `Full, Faithful` instances should be constructed by a deriving handler.
-- https://github.com/leanprover-community/mathlib4/issues/380
-- Note(kmill): `derive Functor.Full, Functor.Faithful` does not create instances
-- that are in terms of `Skeleton`, but rather `InducedCategory`, which can't be applied.
-- With `deriving @Functor.Full (Skeleton C)`, the instance can't be derived, for a similar reason.

noncomputable instance : (fromSkeleton C).Full := by
  apply InducedCategory.full
noncomputable instance : (fromSkeleton C).Faithful := by
  apply InducedCategory.faithful

instance : (fromSkeleton C).EssSurj where mem_essImage X := ‚ü®Quotient.mk' X, Quotient.mk_out X‚ü©

noncomputable instance fromSkeleton.isEquivalence : (fromSkeleton C).IsEquivalence where

variable {C}

/-- The class of an object in the skeleton. -/
abbrev toSkeleton (X : C) : Skeleton C := ‚ü¶X‚üß

/-- The isomorphism between `‚ü¶X‚üß.out` and `X`. -/
noncomputable def preCounitIso (X : C) : (fromSkeleton C).obj (toSkeleton X) ‚âÖ X :=
  Nonempty.some (Quotient.mk_out X)

alias fromSkeletonToSkeletonIso := preCounitIso

variable (C)

/-- An inverse to `fromSkeleton C` that forms an equivalence with it. -/
@[simps] noncomputable def toSkeletonFunctor : C ‚•§ Skeleton C where
  obj := toSkeleton
  map {X Y} f := by apply (preCounitIso X).hom ‚â´ f ‚â´ (preCounitIso Y).inv
  map_id _ := by aesop
  map_comp _ _ := by change _ = CategoryStruct.comp (obj := C) _ _; simp

/-- The equivalence between the skeleton and the category itself. -/
@[simps] noncomputable def skeletonEquivalence : Skeleton C ‚âå C where
  functor := fromSkeleton C
  inverse := toSkeletonFunctor C
  unitIso := NatIso.ofComponents
    (fun X ‚Ü¶ InducedCategory.isoMk (Nonempty.some <| Quotient.mk_out X.out).symm)
    fun _ ‚Ü¶ .symm <| Iso.inv_hom_id_assoc _ _
  counitIso := NatIso.ofComponents preCounitIso
  functor_unitIso_comp _ := Iso.inv_hom_id _

theorem skeleton_skeletal : Skeletal (Skeleton C) := by
  rintro X Y ‚ü®h‚ü©
  have : X.out ‚âà Y.out := ‚ü®(fromSkeleton C).mapIso h‚ü©
  simpa using Quotient.sound this

/-- The `skeleton` of `C` given by choice is a skeleton of `C`. -/
lemma skeleton_isSkeleton : IsSkeletonOf C (Skeleton C) (fromSkeleton C) where
  skel := skeleton_skeletal C
  eqv := fromSkeleton.isEquivalence C

variable {C D}

lemma toSkeleton_fromSkeleton_obj (X : Skeleton C) : toSkeleton ((fromSkeleton C).obj X) = X :=
  Quotient.out_eq _

lemma toSkeleton_eq_toSkeleton_iff {X Y : C} : toSkeleton X = toSkeleton Y ‚Üî Nonempty (X ‚âÖ Y) :=
  Quotient.eq

lemma congr_toSkeleton_of_iso {X Y : C} (e : X ‚âÖ Y) : toSkeleton X = toSkeleton Y :=
  Quotient.sound ‚ü®e‚ü©

/-- Provides a (noncomputable) isomorphism `X ‚âÖ Y` given that `toSkeleton X = toSkeleton Y`. -/
noncomputable def Skeleton.isoOfEq {X Y : C} (h : toSkeleton X = toSkeleton Y) :
    X ‚âÖ Y :=
  Quotient.exact h |>.some

lemma toSkeleton_eq_iff {X : C} {Y : Skeleton C} :
    toSkeleton X = Y ‚Üî Nonempty (X ‚âÖ (fromSkeleton C).obj Y) :=
  Quotient.mk_eq_iff_out

namespace Functor

/-- From a functor `C ‚•§ D`, construct a map of skeletons `Skeleton C ‚Üí Skeleton D`. -/
noncomputable def mapSkeleton (F : C ‚•§ D) : Skeleton C ‚•§ Skeleton D :=
  (skeletonEquivalence C).functor ‚ãô F ‚ãô (skeletonEquivalence D).inverse

variable (F : C ‚•§ D)

lemma mapSkeleton_obj_toSkeleton (X : C) :
    F.mapSkeleton.obj (toSkeleton X) = toSkeleton (F.obj X) :=
  congr_toSkeleton_of_iso <| F.mapIso <| preCounitIso X

instance [F.Full] : F.mapSkeleton.Full := by unfold mapSkeleton; infer_instance

instance [F.Faithful] : F.mapSkeleton.Faithful := by unfold mapSkeleton; infer_instance

instance [F.EssSurj] : F.mapSkeleton.EssSurj := by unfold mapSkeleton; infer_instance

/-- A natural isomorphism between `X ‚Ü¶ ‚ü¶X‚üß ‚Ü¶ ‚ü¶FX‚üß` and `X ‚Ü¶ FX ‚Ü¶ ‚ü¶FX‚üß`. On the level of
categories, these are `C ‚•§ Skeleton C ‚•§ Skeleton D` and `C ‚•§ D ‚•§ Skeleton D`. So this says that
the square formed by these 4 objects and 4 functors commutes. -/
noncomputable def toSkeletonFunctorCompMapSkeletonIso :
    toSkeletonFunctor C ‚ãô F.mapSkeleton ‚âÖ F ‚ãô toSkeletonFunctor D :=
  NatIso.ofComponents (fun X ‚Ü¶ (toSkeletonFunctor D).mapIso <| F.mapIso <| preCounitIso X)
    (fun {X Y} f ‚Ü¶ show (_ ‚â´ _) ‚â´ _ = _ ‚â´ _ by simp [assoc])

lemma mapSkeleton_injective [F.Full] [F.Faithful] : Function.Injective F.mapSkeleton.obj :=
  fun _ _ h ‚Ü¶ skeleton_skeletal C ‚ü®F.mapSkeleton.preimageIso <| eqToIso h‚ü©

lemma mapSkeleton_surjective [F.EssSurj] : Function.Surjective F.mapSkeleton.obj :=
  fun Y ‚Ü¶ let ‚ü®X, h‚ü© := EssSurj.mem_essImage Y; ‚ü®X, skeleton_skeletal D h‚ü©

end Functor

/-- Two categories which are categorically equivalent have skeletons with equivalent objects.
-/
noncomputable def Equivalence.skeletonEquiv (e : C ‚âå D) : Skeleton C ‚âÉ Skeleton D :=
  let f := ((skeletonEquivalence C).trans e).trans (skeletonEquivalence D).symm
  { toFun := f.functor.obj
    invFun := f.inverse.obj
    left_inv := fun X => skeleton_skeletal C ‚ü®(f.unitIso.app X).symm‚ü©
    right_inv := fun Y => skeleton_skeletal D ‚ü®f.counitIso.app Y‚ü© }

variable (C D)

/-- Construct the skeleton category by taking the quotient of objects. This construction gives a
preorder with nice definitional properties, but is only really appropriate for thin categories.
If your original category is not thin, you probably want to be using `Skeleton` instead of this.
-/
def ThinSkeleton : Type u‚ÇÅ :=
  Quotient (isIsomorphicSetoid C)

variable {C} in
/-- Convenience constructor for `ThinSkeleton`. -/
abbrev ThinSkeleton.mk (c : C) : ThinSkeleton C := Quotient.mk' c

instance inhabitedThinSkeleton [Inhabited C] : Inhabited (ThinSkeleton C) :=
  ‚ü®ThinSkeleton.mk default‚ü©

instance ThinSkeleton.preorder : Preorder (ThinSkeleton C) where
  le :=
    @Quotient.lift‚ÇÇ C C _ (isIsomorphicSetoid C) (isIsomorphicSetoid C)
      (fun X Y => Nonempty (X ‚ü∂ Y))
        (by
          rintro _ _ _ _ ‚ü®i‚ÇÅ‚ü© ‚ü®i‚ÇÇ‚ü©
          exact
            propext
              ‚ü®Nonempty.map fun f => i‚ÇÅ.inv ‚â´ f ‚â´ i‚ÇÇ.hom,
                Nonempty.map fun f => i‚ÇÅ.hom ‚â´ f ‚â´ i‚ÇÇ.inv‚ü©)
  le_refl := by
    refine Quotient.ind fun a => ?_
    exact ‚ü®ùüô _‚ü©
  le_trans a b c := Quotient.inductionOn‚ÇÉ a b c fun _ _ _ => Nonempty.map2 (¬∑ ‚â´ ¬∑)

/-- The functor from a category to its thin skeleton. -/
@[simps]
def toThinSkeleton : C ‚•§ ThinSkeleton C where
  obj := ThinSkeleton.mk
  map f := homOfLE (Nonempty.intro f)

/-!
The constructions here are intended to be used when the category `C` is thin, even though
some of the statements can be shown without this assumption.
-/


namespace ThinSkeleton

/-- The thin skeleton is thin. -/
instance thin : Quiver.IsThin (ThinSkeleton C) := fun _ _ =>
  ‚ü®by
    rintro ‚ü®‚ü®f‚ÇÅ‚ü©‚ü© ‚ü®‚ü®_‚ü©‚ü©
    rfl‚ü©

variable {C} {D}

/-- A functor `C ‚•§ D` computably lowers to a functor `ThinSkeleton C ‚•§ ThinSkeleton D`. -/
@[simps]
def map (F : C ‚•§ D) : ThinSkeleton C ‚•§ ThinSkeleton D where
  obj := Quotient.map F.obj fun _ _ ‚ü®hX‚ü© => ‚ü®F.mapIso hX‚ü©
  map {X} {Y} := Quotient.recOnSubsingleton‚ÇÇ X Y fun _ _ k => homOfLE (k.le.elim fun t => ‚ü®F.map t‚ü©)

theorem comp_toThinSkeleton (F : C ‚•§ D) : F ‚ãô toThinSkeleton D = toThinSkeleton C ‚ãô map F :=
  rfl

/-- Given a natural transformation `F‚ÇÅ ‚ü∂ F‚ÇÇ`, induce a natural transformation `map F‚ÇÅ ‚ü∂ map F‚ÇÇ`. -/
def mapNatTrans {F‚ÇÅ F‚ÇÇ : C ‚•§ D} (k : F‚ÇÅ ‚ü∂ F‚ÇÇ) : map F‚ÇÅ ‚ü∂ map F‚ÇÇ where
  app X := Quotient.recOnSubsingleton X fun x => ‚ü®‚ü®‚ü®k.app x‚ü©‚ü©‚ü©

/- Porting note: `map‚ÇÇObjMap`, `map‚ÇÇFunctor`, and `map‚ÇÇNatTrans` were all extracted
from the original `map‚ÇÇ` proof. Lean needed an extensive amount explicit type
annotations to figure things out. This also translated into repeated deterministic
timeouts. The extracted defs allow for explicit motives for the multiple
descents to the quotients.

It would be better to prove that
`ThinSkeleton (C √ó D) ‚âå ThinSkeleton C √ó ThinSkeleton D`
which is more immediate from comparing the preorders. Then one could get
`map‚ÇÇ` by currying.
-/
/-- Given a bifunctor, we descend to a function on objects of `ThinSkeleton` -/
def map‚ÇÇObjMap (F : C ‚•§ D ‚•§ E) : ThinSkeleton C ‚Üí ThinSkeleton D ‚Üí ThinSkeleton E :=
  fun x y =>
    @Quotient.map‚ÇÇ C D (isIsomorphicSetoid C) (isIsomorphicSetoid D) E (isIsomorphicSetoid E)
      (fun X Y => (F.obj X).obj Y)
          (fun X‚ÇÅ _ ‚ü®hX‚ü© _ Y‚ÇÇ ‚ü®hY‚ü© => ‚ü®(F.obj X‚ÇÅ).mapIso hY ‚â™‚â´ (F.mapIso hX).app Y‚ÇÇ‚ü©) x y

/-- For each `x : ThinSkeleton C`, we promote `map‚ÇÇObjMap F x` to a functor -/
def map‚ÇÇFunctor (F : C ‚•§ D ‚•§ E) : ThinSkeleton C ‚Üí ThinSkeleton D ‚•§ ThinSkeleton E :=
  fun x =>
    { obj := fun y => map‚ÇÇObjMap F x y
      map := fun {y‚ÇÅ} {y‚ÇÇ} => @Quotient.recOnSubsingleton C (isIsomorphicSetoid C)
        (fun x => (y‚ÇÅ ‚ü∂ y‚ÇÇ) ‚Üí (map‚ÇÇObjMap F x y‚ÇÅ ‚ü∂ map‚ÇÇObjMap F x y‚ÇÇ)) _ x fun X
          => Quotient.recOnSubsingleton‚ÇÇ y‚ÇÅ y‚ÇÇ fun _ _ hY =>
            homOfLE (hY.le.elim fun g => ‚ü®(F.obj X).map g‚ü©) }

/-- This provides natural transformations `map‚ÇÇFunctor F x‚ÇÅ ‚ü∂ map‚ÇÇFunctor F x‚ÇÇ` given
`x‚ÇÅ ‚ü∂ x‚ÇÇ` -/
def map‚ÇÇNatTrans (F : C ‚•§ D ‚•§ E) : {x‚ÇÅ x‚ÇÇ : ThinSkeleton C} ‚Üí (x‚ÇÅ ‚ü∂ x‚ÇÇ) ‚Üí
    (map‚ÇÇFunctor F x‚ÇÅ ‚ü∂ map‚ÇÇFunctor F x‚ÇÇ) := fun {x‚ÇÅ} {x‚ÇÇ} =>
  @Quotient.recOnSubsingleton‚ÇÇ C C (isIsomorphicSetoid C) (isIsomorphicSetoid C)
    (fun x x' : ThinSkeleton C => (x ‚ü∂ x') ‚Üí (map‚ÇÇFunctor F x ‚ü∂ map‚ÇÇFunctor F x')) _ x‚ÇÅ x‚ÇÇ
    (fun X‚ÇÅ X‚ÇÇ f => { app := fun y =>
      Quotient.recOnSubsingleton y fun Y => homOfLE (f.le.elim fun f' => ‚ü®(F.map f').app Y‚ü©) })

-- TODO: state the lemmas about what happens when you compose with `toThinSkeleton`
/-- A functor `C ‚•§ D ‚•§ E` computably lowers to a functor
`ThinSkeleton C ‚•§ ThinSkeleton D ‚•§ ThinSkeleton E` -/
@[simps]
def map‚ÇÇ (F : C ‚•§ D ‚•§ E) : ThinSkeleton C ‚•§ ThinSkeleton D ‚•§ ThinSkeleton E where
  obj := map‚ÇÇFunctor F
  map := map‚ÇÇNatTrans F

variable (C)

section

variable [Quiver.IsThin C]

instance toThinSkeleton_faithful : (toThinSkeleton C).Faithful where

/-- Use `Quotient.out` to create a functor out of the thin skeleton. -/
@[simps]
noncomputable def fromThinSkeleton : ThinSkeleton C ‚•§ C where
  obj := Quotient.out
  map {x} {y} :=
    Quotient.recOnSubsingleton‚ÇÇ x y fun X Y f =>
      (Nonempty.some (Quotient.mk_out X)).hom ‚â´ f.le.some ‚â´ (Nonempty.some (Quotient.mk_out Y)).inv

/-- The equivalence between the thin skeleton and the category itself. -/
noncomputable def equivalence : ThinSkeleton C ‚âå C where
  functor := fromThinSkeleton C
  inverse := toThinSkeleton C
  counitIso := NatIso.ofComponents fun X => Nonempty.some (Quotient.mk_out X)
  unitIso := NatIso.ofComponents fun x => Quotient.recOnSubsingleton x fun X =>
    eqToIso (Quotient.sound ‚ü®(Nonempty.some (Quotient.mk_out X)).symm‚ü©)

noncomputable instance fromThinSkeleton_isEquivalence : (fromThinSkeleton C).IsEquivalence :=
  (equivalence C).isEquivalence_functor

variable {C}

theorem equiv_of_both_ways {X Y : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ X) : X ‚âà Y :=
  ‚ü®iso_of_both_ways f g‚ü©

instance thinSkeletonPartialOrder : PartialOrder (ThinSkeleton C) :=
  { CategoryTheory.ThinSkeleton.preorder C with
    le_antisymm :=
      Quotient.ind‚ÇÇ
        (by
          rintro _ _ ‚ü®f‚ü© ‚ü®g‚ü©
          apply Quotient.sound (equiv_of_both_ways f g)) }

theorem skeletal : Skeletal (ThinSkeleton C) := fun X Y =>
  Quotient.inductionOn‚ÇÇ X Y fun _ _ h => h.elim fun i => i.1.le.antisymm i.2.le

theorem map_comp_eq (F : E ‚•§ D) (G : D ‚•§ C) : map (F ‚ãô G) = map F ‚ãô map G :=
  Functor.eq_of_iso skeletal <|
    NatIso.ofComponents fun X => Quotient.recOnSubsingleton X fun _ => Iso.refl _

theorem map_id_eq : map (ùü≠ C) = ùü≠ (ThinSkeleton C) :=
  Functor.eq_of_iso skeletal <|
    NatIso.ofComponents fun X => Quotient.recOnSubsingleton X fun _ => Iso.refl _

theorem map_iso_eq {F‚ÇÅ F‚ÇÇ : D ‚•§ C} (h : F‚ÇÅ ‚âÖ F‚ÇÇ) : map F‚ÇÅ = map F‚ÇÇ :=
  Functor.eq_of_iso skeletal
    { hom := mapNatTrans h.hom
      inv := mapNatTrans h.inv }

/--
Applying `fromThinSkeleton`, `F` and then `toThinSkeleton` is isomorphic to applying `map F`.
-/
noncomputable def fromThinSkeletonCompToThinSkeletonIso (F : C ‚•§ D) :
    fromThinSkeleton C ‚ãô F ‚ãô toThinSkeleton D ‚âÖ map F :=
  Functor.isoWhiskerLeft (fromThinSkeleton C) (Iso.refl _) ‚â™‚â´
    Functor.isoWhiskerRight (equivalence C).unitIso.symm (map F) ‚â™‚â´
    Functor.leftUnitor (map F)

/--
Applying `map F` and then `fromThinSkeleton` is isomorphic to first applying `fromThinSkeleton`
and then applying `F`.
-/
noncomputable def mapCompFromThinSkeletonIso [Quiver.IsThin D] (F : C ‚•§ D) :
    map F ‚ãô fromThinSkeleton D ‚âÖ fromThinSkeleton C ‚ãô F  :=
  Functor.isoWhiskerRight (fromThinSkeletonCompToThinSkeletonIso F).symm _ ‚â™‚â´
    Functor.isoWhiskerLeft (fromThinSkeleton C ‚ãô F) (equivalence D).counitIso ‚â™‚â´
    Functor.rightUnitor (fromThinSkeleton C ‚ãô F)

/-- `fromThinSkeleton C` exhibits the thin skeleton as a skeleton. -/
lemma thinSkeleton_isSkeleton : IsSkeletonOf C (ThinSkeleton C) (fromThinSkeleton C) where
  skel := skeletal

instance isSkeletonOfInhabited :
    Inhabited (IsSkeletonOf C (ThinSkeleton C) (fromThinSkeleton C)) :=
  ‚ü®thinSkeleton_isSkeleton‚ü©

end

variable {C}

/-- An adjunction between thin categories gives an adjunction between their thin skeletons. -/
def lowerAdjunction (R : D ‚•§ C) (L : C ‚•§ D) (h : L ‚ä£ R) :
    ThinSkeleton.map L ‚ä£ ThinSkeleton.map R where
  unit :=
    { app := fun X => by
        letI := isIsomorphicSetoid C
        exact Quotient.recOnSubsingleton X fun x => homOfLE ‚ü®h.unit.app x‚ü© }
      -- TODO: make quotient.rec_on_subsingleton' so the letI isn't needed
  counit :=
    { app := fun X => by
        letI := isIsomorphicSetoid D
        exact Quotient.recOnSubsingleton X fun x => homOfLE ‚ü®h.counit.app x‚ü© }

end ThinSkeleton

open ThinSkeleton

section

variable {C} {Œ± : Type*} [PartialOrder Œ±]

/--
When `e : C ‚âå Œ±` is a categorical equivalence from a thin category `C` to some partial order `Œ±`,
the `ThinSkeleton C` is order isomorphic to `Œ±`.
-/
noncomputable def Equivalence.thinSkeletonOrderIso [Quiver.IsThin C] (e : C ‚âå Œ±) :
    ThinSkeleton C ‚âÉo Œ± :=
  ((ThinSkeleton.equivalence C).trans e).toOrderIso

end

end CategoryTheory
