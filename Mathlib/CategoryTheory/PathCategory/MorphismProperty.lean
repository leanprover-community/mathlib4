/-
Copyright (c) 2024 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.PathCategory.Basic
import Mathlib.CategoryTheory.MorphismProperty.Basic

/-!
# Properties of morphisms in a path category.

We provide a formulation of induction principles for morphisms in a path category in terms of
`MorphismProperty`. This file is separate from `CategoryTheory.PathCategory.Basic` in order to
reduce transitive imports. -/


universe vâ‚ uâ‚

namespace CategoryTheory.Paths

section
variable (V : Type uâ‚) [Quiver.{vâ‚ + 1} V]

/-- A reformulation of `CategoryTheory.Paths.induction` in terms of `MorphismProperty`. -/
lemma morphismProperty_eq_top
    (P : MorphismProperty (Paths V))
    (id : âˆ€ {v : V}, P (ğŸ™ ((of V).obj v)))
    (comp : âˆ€ {u v w : V}
      (p : (of V).obj u âŸ¶ (of V).obj v) (q : v âŸ¶ w), P p â†’ P (p â‰« (of V).map q)) :
    P = âŠ¤ := by
  ext; constructor
  Â· simp
  Â· exact fun _ â†¦ induction (fun f â†¦ P f) id comp _

/-- A reformulation of `CategoryTheory.Paths.induction'` in terms of `MorphismProperty`. -/
lemma morphismProperty_eq_top'
    (P : MorphismProperty (Paths V))
    (id : âˆ€ {v : V}, P (ğŸ™ ((of V).obj v)))
    (comp : âˆ€ {u v w : V}
      (p : u âŸ¶ v) (q : (of V).obj v âŸ¶ (of V).obj w), P q â†’ P ((of V).map p â‰« q)) :
    P = âŠ¤ := by
  ext; constructor
  Â· simp
  Â· exact fun _ â†¦ induction' (fun f â†¦ P f) id comp _

end
section

variable {V : Type uâ‚} [Quiver.{vâ‚ + 1} V]

/-- A natural transformation between `F G : Paths V â¥¤ C` is defined by its components and
its unary naturality squares. -/
def liftNatTrans {C} [Category C] {F G : Paths V â¥¤ C} (Î±_app : (v : V) â†’ (F.obj v âŸ¶ G.obj v))
    (Î±_nat : {X Y : V} â†’ (f : X âŸ¶ Y) â†’
      F.map (Quiver.Hom.toPath f) â‰« Î±_app Y = Î±_app X â‰« G.map (Quiver.Hom.toPath f)) : F âŸ¶ G where
        app := Î±_app
        naturality := by
          apply MorphismProperty.of_eq_top
          apply morphismProperty_eq_top
          Â· simp only [Functor.map_id, Category.id_comp, Category.comp_id, implies_true]
          Â· simp only [of_obj, of_map, Functor.map_comp, Category.assoc]
            intro _ _ _ _ q hyp
            rw [Î±_nat q, â† Category.assoc, hyp, Category.assoc]

/-- A natural isomorphism between `F G : Paths V â¥¤ C` is defined by its components and
its unary naturality squares. -/
def liftNatIso {C} [Category C] {F G : Paths V â¥¤ C} (Î±_app : (v : V) â†’ (F.obj v â‰… G.obj v))
    (Î±_nat : {X Y : V} â†’ (f : X âŸ¶ Y) â†’
      F.map (Quiver.Hom.toPath f) â‰« (Î±_app Y).hom = (Î±_app X).hom â‰« G.map (Quiver.Hom.toPath f)) :
    F â‰… G :=
  NatIso.ofComponents Î±_app (fun f â†¦ (liftNatTrans (fun v â†¦ (Î±_app v).hom) Î±_nat).naturality f)

end

end CategoryTheory.Paths
