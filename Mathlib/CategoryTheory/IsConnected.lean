/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Jakob von Raumer
-/
import Mathlib.Data.List.Chain
import Mathlib.CategoryTheory.PUnit
import Mathlib.CategoryTheory.Groupoid
import Mathlib.CategoryTheory.Category.ULift

/-!
# Connected category

Define a connected category as a _nonempty_ category for which every functor
to a discrete category is isomorphic to the constant functor.

NB. Some authors include the empty category as connected, we do not.
We instead are interested in categories with exactly one 'connected
component'.

We give some equivalent definitions:
- A nonempty category for which every functor to a discrete category is
  constant on objects.
  See `any_functor_const_on_obj` and `Connected.of_any_functor_const_on_obj`.
- A nonempty category for which every function `F` for which the presence of a
  morphism `f : j‚ÇÅ ‚ü∂ j‚ÇÇ` implies `F j‚ÇÅ = F j‚ÇÇ` must be constant everywhere.
  See `constant_of_preserves_morphisms` and `Connected.of_constant_of_preserves_morphisms`.
- A nonempty category for which any subset of its elements containing the
  default and closed under morphisms is everything.
  See `induct_on_objects` and `Connected.of_induct`.
- A nonempty category for which every object is related under the reflexive
  transitive closure of the relation "there is a morphism in some direction
  from `j‚ÇÅ` to `j‚ÇÇ`".
  See `connected_zigzag` and `zigzag_connected`.
- A nonempty category for which for any two objects there is a sequence of
  morphisms (some reversed) from one to the other.
  See `exists_zigzag'` and `connected_of_zigzag`.

We also prove the result that the functor given by `(X √ó -)` preserves any
connected limit. That is, any limit of shape `J` where `J` is a connected
category is preserved by the functor `(X √ó -)`. This appears in `CategoryTheory.Limits.Connected`.
-/


universe w‚ÇÅ w‚ÇÇ v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

noncomputable section

open CategoryTheory.Category CategoryTheory.Functor

open Opposite

namespace CategoryTheory

/-- A possibly empty category for which every functor to a discrete category is constant.
-/
class IsPreconnected (J : Type u‚ÇÅ) [Category.{v‚ÇÅ} J] : Prop where
  iso_constant :
    ‚àÄ {Œ± : Type u‚ÇÅ} (F : J ‚•§ Discrete Œ±) (j : J), Nonempty (F ‚âÖ (Functor.const J).obj (F.obj j))

attribute [inherit_doc IsPreconnected] IsPreconnected.iso_constant

/-- We define a connected category as a _nonempty_ category for which every
functor to a discrete category is constant.

NB. Some authors include the empty category as connected, we do not.
We instead are interested in categories with exactly one 'connected
component'.

This allows us to show that the functor X ‚®Ø - preserves connected limits. -/
@[stacks 002S]
class IsConnected (J : Type u‚ÇÅ) [Category.{v‚ÇÅ} J] : Prop extends IsPreconnected J where
  [is_nonempty : Nonempty J]

attribute [instance 100] IsConnected.is_nonempty

variable {J : Type u‚ÇÅ} [Category.{v‚ÇÅ} J]
variable {K : Type u‚ÇÇ} [Category.{v‚ÇÇ} K]

namespace IsPreconnected.IsoConstantAux

/-- Implementation detail of `isoConstant`. -/
private def liftToDiscrete {Œ± : Type u‚ÇÇ} (F : J ‚•§ Discrete Œ±) : J ‚•§ Discrete J where
  obj j := have := Nonempty.intro j
    Discrete.mk (Function.invFun F.obj (F.obj j))
  map {j _} f := have := Nonempty.intro j
    ‚ü®‚ü®congr_arg (Function.invFun F.obj) (Discrete.ext (Discrete.eq_of_hom (F.map f)))‚ü©‚ü©

/-- Implementation detail of `isoConstant`. -/
private def factorThroughDiscrete {Œ± : Type u‚ÇÇ} (F : J ‚•§ Discrete Œ±) :
    liftToDiscrete F ‚ãô Discrete.functor F.obj ‚âÖ F :=
  NatIso.ofComponents (fun _ => eqToIso Function.apply_invFun_apply) (by cat_disch)

end IsPreconnected.IsoConstantAux

/-- If `J` is connected, any functor `F : J ‚•§ Discrete Œ±` is isomorphic to
the constant functor with value `F.obj j` (for any choice of `j`).
-/
def isoConstant [IsPreconnected J] {Œ± : Type u‚ÇÇ} (F : J ‚•§ Discrete Œ±) (j : J) :
    F ‚âÖ (Functor.const J).obj (F.obj j) :=
  (IsPreconnected.IsoConstantAux.factorThroughDiscrete F).symm
    ‚â™‚â´ isoWhiskerRight (IsPreconnected.iso_constant _ j).some _
    ‚â™‚â´ NatIso.ofComponents (fun _ => eqToIso Function.apply_invFun_apply) (by simp)

/-- If `J` is connected, any functor to a discrete category is constant on objects.
The converse is given in `IsConnected.of_any_functor_const_on_obj`.
-/
theorem any_functor_const_on_obj [IsPreconnected J] {Œ± : Type u‚ÇÇ} (F : J ‚•§ Discrete Œ±) (j j' : J) :
    F.obj j = F.obj j' := by
  ext; exact ((isoConstant F j').hom.app j).down.1

/-- If any functor to a discrete category is constant on objects, J is connected.
The converse of `any_functor_const_on_obj`.
-/
theorem IsPreconnected.of_any_functor_const_on_obj
    (h : ‚àÄ {Œ± : Type u‚ÇÅ} (F : J ‚•§ Discrete Œ±), ‚àÄ j j' : J, F.obj j = F.obj j') :
    IsPreconnected J where
  iso_constant := fun F j' => ‚ü®NatIso.ofComponents fun j => eqToIso (h F j j')‚ü©

instance IsPreconnected.prod [IsPreconnected J] [IsPreconnected K] : IsPreconnected (J √ó K) := by
  refine .of_any_functor_const_on_obj (fun {a} F ‚ü®j, k‚ü© ‚ü®j', k'‚ü© => ?_)
  exact (any_functor_const_on_obj (Prod.sectL J k ‚ãô F) j j').trans
    (any_functor_const_on_obj (Prod.sectR j' K ‚ãô F) k k')

instance IsConnected.prod [IsConnected J] [IsConnected K] : IsConnected (J √ó K) where

/-- If any functor to a discrete category is constant on objects, J is connected.
The converse of `any_functor_const_on_obj`.
-/
theorem IsConnected.of_any_functor_const_on_obj [Nonempty J]
    (h : ‚àÄ {Œ± : Type u‚ÇÅ} (F : J ‚•§ Discrete Œ±), ‚àÄ j j' : J, F.obj j = F.obj j') : IsConnected J :=
  { IsPreconnected.of_any_functor_const_on_obj h with }

/-- If `J` is connected, then given any function `F` such that the presence of a
morphism `j‚ÇÅ ‚ü∂ j‚ÇÇ` implies `F j‚ÇÅ = F j‚ÇÇ`, we have that `F` is constant.
This can be thought of as a local-to-global property.

The converse is shown in `IsConnected.of_constant_of_preserves_morphisms`
-/
theorem constant_of_preserves_morphisms [IsPreconnected J] {Œ± : Type u‚ÇÇ} (F : J ‚Üí Œ±)
    (h : ‚àÄ (j‚ÇÅ j‚ÇÇ : J) (_ : j‚ÇÅ ‚ü∂ j‚ÇÇ), F j‚ÇÅ = F j‚ÇÇ) (j j' : J) : F j = F j' := by
  simpa using
    any_functor_const_on_obj
      { obj := Discrete.mk ‚àò F
        map := fun f => eqToHom (by ext; exact h _ _ f) }
      j j'

/-- `J` is connected if: given any function `F : J ‚Üí Œ±` which is constant for any
`j‚ÇÅ, j‚ÇÇ` for which there is a morphism `j‚ÇÅ ‚ü∂ j‚ÇÇ`, then `F` is constant.
This can be thought of as a local-to-global property.

The converse of `constant_of_preserves_morphisms`.
-/
theorem IsPreconnected.of_constant_of_preserves_morphisms
    (h : ‚àÄ {Œ± : Type u‚ÇÅ} (F : J ‚Üí Œ±),
      (‚àÄ {j‚ÇÅ j‚ÇÇ : J} (_ : j‚ÇÅ ‚ü∂ j‚ÇÇ), F j‚ÇÅ = F j‚ÇÇ) ‚Üí ‚àÄ j j' : J, F j = F j') :
    IsPreconnected J :=
  IsPreconnected.of_any_functor_const_on_obj fun F =>
    h F.obj fun f => by ext; exact Discrete.eq_of_hom (F.map f)

/-- `J` is connected if: given any function `F : J ‚Üí Œ±` which is constant for any
`j‚ÇÅ, j‚ÇÇ` for which there is a morphism `j‚ÇÅ ‚ü∂ j‚ÇÇ`, then `F` is constant.
This can be thought of as a local-to-global property.

The converse of `constant_of_preserves_morphisms`.
-/
theorem IsConnected.of_constant_of_preserves_morphisms [Nonempty J]
    (h : ‚àÄ {Œ± : Type u‚ÇÅ} (F : J ‚Üí Œ±),
      (‚àÄ {j‚ÇÅ j‚ÇÇ : J} (_ : j‚ÇÅ ‚ü∂ j‚ÇÇ), F j‚ÇÅ = F j‚ÇÇ) ‚Üí ‚àÄ j j' : J, F j = F j') :
    IsConnected J :=
  { IsPreconnected.of_constant_of_preserves_morphisms h with }

/-- An inductive-like property for the objects of a connected category.
If the set `p` is nonempty, and `p` is closed under morphisms of `J`,
then `p` contains all of `J`.

The converse is given in `IsConnected.of_induct`.
-/
theorem induct_on_objects [IsPreconnected J] (p : Set J) {j‚ÇÄ : J} (h0 : j‚ÇÄ ‚àà p)
    (h1 : ‚àÄ {j‚ÇÅ j‚ÇÇ : J} (_ : j‚ÇÅ ‚ü∂ j‚ÇÇ), j‚ÇÅ ‚àà p ‚Üî j‚ÇÇ ‚àà p) (j : J) : j ‚àà p := by
  let aux (j‚ÇÅ j‚ÇÇ : J) (f : j‚ÇÅ ‚ü∂ j‚ÇÇ) := congrArg ULift.up <| (h1 f).eq
  injection constant_of_preserves_morphisms (fun k => ULift.up.{u‚ÇÅ} (k ‚àà p)) aux j j‚ÇÄ with i
  rwa [i]

/--
If any maximal connected component containing some element j‚ÇÄ of J is all of J, then J is connected.

The converse of `induct_on_objects`.
-/
theorem IsConnected.of_induct {j‚ÇÄ : J}
    (h : ‚àÄ p : Set J, j‚ÇÄ ‚àà p ‚Üí (‚àÄ {j‚ÇÅ j‚ÇÇ : J} (_ : j‚ÇÅ ‚ü∂ j‚ÇÇ), j‚ÇÅ ‚àà p ‚Üî j‚ÇÇ ‚àà p) ‚Üí ‚àÄ j : J, j ‚àà p) :
    IsConnected J :=
  have := Nonempty.intro j‚ÇÄ
  IsConnected.of_constant_of_preserves_morphisms fun {Œ±} F a => by
    have w := h { j | F j = F j‚ÇÄ } rfl (fun {j‚ÇÅ} {j‚ÇÇ} f => by
      change F j‚ÇÅ = F j‚ÇÄ ‚Üî F j‚ÇÇ = F j‚ÇÄ
      simp [a f])
    intro j j'
    rw [w j, w j']

attribute [local instance] uliftCategory in
/-- Lifting the universe level of morphisms and objects preserves connectedness. -/
instance [hc : IsConnected J] : IsConnected (ULiftHom.{v‚ÇÇ} (ULift.{u‚ÇÇ} J)) := by
  apply IsConnected.of_induct
  ¬∑ rintro p hj‚ÇÄ h ‚ü®j‚ü©
    let p' : Set J := {j : J | p ‚ü®j‚ü©}
    have hj‚ÇÄ' : Classical.choice hc.is_nonempty ‚àà p' := by
      simp only [p']
      exact hj‚ÇÄ
    apply induct_on_objects p' hj‚ÇÄ' fun f => h ((ULiftHomULiftCategory.equiv J).functor.map f)

/-- Another induction principle for `IsPreconnected J`:
given a type family `Z : J ‚Üí Sort*` and
a rule for transporting in *both* directions along a morphism in `J`,
we can transport an `x : Z j‚ÇÄ` to a point in `Z j` for any `j`.
-/
theorem isPreconnected_induction [IsPreconnected J] (Z : J ‚Üí Sort*)
    (h‚ÇÅ : ‚àÄ {j‚ÇÅ j‚ÇÇ : J} (_ : j‚ÇÅ ‚ü∂ j‚ÇÇ), Z j‚ÇÅ ‚Üí Z j‚ÇÇ) (h‚ÇÇ : ‚àÄ {j‚ÇÅ j‚ÇÇ : J} (_ : j‚ÇÅ ‚ü∂ j‚ÇÇ), Z j‚ÇÇ ‚Üí Z j‚ÇÅ)
    {j‚ÇÄ : J} (x : Z j‚ÇÄ) (j : J) : Nonempty (Z j) :=
  (induct_on_objects { j | Nonempty (Z j) } ‚ü®x‚ü©
      (fun f => ‚ü®by rintro ‚ü®y‚ü©; exact ‚ü®h‚ÇÅ f y‚ü©, by rintro ‚ü®y‚ü©; exact ‚ü®h‚ÇÇ f y‚ü©‚ü©)
      j :)

/-- If `J` and `K` are equivalent, then if `J` is preconnected then `K` is as well. -/
theorem isPreconnected_of_equivalent {K : Type u‚ÇÇ} [Category.{v‚ÇÇ} K] [IsPreconnected J]
    (e : J ‚âå K) : IsPreconnected K where
  iso_constant F k :=
    ‚ü®calc
        F ‚âÖ e.inverse ‚ãô e.functor ‚ãô F := (e.invFunIdAssoc F).symm
        _ ‚âÖ e.inverse ‚ãô (Functor.const J).obj ((e.functor ‚ãô F).obj (e.inverse.obj k)) :=
          isoWhiskerLeft e.inverse (isoConstant (e.functor ‚ãô F) (e.inverse.obj k))
        _ ‚âÖ e.inverse ‚ãô (Functor.const J).obj (F.obj k) :=
          isoWhiskerLeft _ ((F ‚ãô Functor.const J).mapIso (e.counitIso.app k))
        _ ‚âÖ (Functor.const K).obj (F.obj k) := NatIso.ofComponents fun _ => Iso.refl _‚ü©

lemma isPreconnected_iff_of_equivalence {K : Type u‚ÇÇ} [Category.{v‚ÇÇ} K] (e : J ‚âå K) :
    IsPreconnected J ‚Üî IsPreconnected K :=
  ‚ü®fun _ => isPreconnected_of_equivalent e, fun _ => isPreconnected_of_equivalent e.symm‚ü©

/-- If `J` and `K` are equivalent, then if `J` is connected then `K` is as well. -/
theorem isConnected_of_equivalent {K : Type u‚ÇÇ} [Category.{v‚ÇÇ} K] (e : J ‚âå K) [IsConnected J] :
    IsConnected K :=
  { is_nonempty := Nonempty.map e.functor.obj (by infer_instance)
    toIsPreconnected := isPreconnected_of_equivalent e }

lemma isConnected_iff_of_equivalence {K : Type u‚ÇÇ} [Category.{v‚ÇÇ} K] (e : J ‚âå K) :
    IsConnected J ‚Üî IsConnected K :=
  ‚ü®fun _ => isConnected_of_equivalent e, fun _ => isConnected_of_equivalent e.symm‚ü©

/-- If `J` is preconnected, then `J·µí·µñ` is preconnected as well. -/
instance isPreconnected_op [IsPreconnected J] : IsPreconnected J·µí·µñ where
  iso_constant := fun {Œ±} F X =>
    ‚ü®NatIso.ofComponents fun Y =>
      eqToIso (Discrete.ext (Discrete.eq_of_hom ((Nonempty.some
        (IsPreconnected.iso_constant (F.rightOp ‚ãô (Discrete.opposite Œ±).functor) (unop X))).app
          (unop Y)).hom))‚ü©

/-- If `J` is connected, then `J·µí·µñ` is connected as well. -/
instance isConnected_op [IsConnected J] : IsConnected J·µí·µñ where
  is_nonempty := Nonempty.intro (op (Classical.arbitrary J))

theorem isPreconnected_of_isPreconnected_op [IsPreconnected J·µí·µñ] : IsPreconnected J :=
  isPreconnected_of_equivalent (opOpEquivalence J)

theorem isConnected_of_isConnected_op [IsConnected J·µí·µñ] : IsConnected J :=
  isConnected_of_equivalent (opOpEquivalence J)

variable (J) in
@[simp]
theorem isConnected_op_iff_isConnected : IsConnected J·µí·µñ ‚Üî IsConnected J :=
  ‚ü®fun _ => isConnected_of_isConnected_op, fun _ => isConnected_op‚ü©

/-- j‚ÇÅ and j‚ÇÇ are related by `Zag` if there is a morphism between them. -/
def Zag (j‚ÇÅ j‚ÇÇ : J) : Prop :=
  Nonempty (j‚ÇÅ ‚ü∂ j‚ÇÇ) ‚à® Nonempty (j‚ÇÇ ‚ü∂ j‚ÇÅ)

@[refl] theorem Zag.refl (X : J) : Zag X X := Or.inl ‚ü®ùüô _‚ü©

theorem zag_symmetric : Symmetric (@Zag J _) := fun _ _ h => h.symm

@[symm] theorem Zag.symm {j‚ÇÅ j‚ÇÇ : J} (h : Zag j‚ÇÅ j‚ÇÇ) : Zag j‚ÇÇ j‚ÇÅ := zag_symmetric h

theorem Zag.of_hom {j‚ÇÅ j‚ÇÇ : J} (f : j‚ÇÅ ‚ü∂ j‚ÇÇ) : Zag j‚ÇÅ j‚ÇÇ := Or.inl ‚ü®f‚ü©

theorem Zag.of_inv {j‚ÇÅ j‚ÇÇ : J} (f : j‚ÇÇ ‚ü∂ j‚ÇÅ) : Zag j‚ÇÅ j‚ÇÇ := Or.inr ‚ü®f‚ü©

/-- `j‚ÇÅ` and `j‚ÇÇ` are related by `Zigzag` if there is a chain of
morphisms from `j‚ÇÅ` to `j‚ÇÇ`, with backward morphisms allowed.
-/
def Zigzag : J ‚Üí J ‚Üí Prop :=
  Relation.ReflTransGen Zag

theorem zigzag_symmetric : Symmetric (@Zigzag J _) :=
  Relation.ReflTransGen.symmetric zag_symmetric

theorem zigzag_equivalence : _root_.Equivalence (@Zigzag J _) :=
  _root_.Equivalence.mk Relation.reflexive_reflTransGen (fun h => zigzag_symmetric h)
  (fun h g => Relation.transitive_reflTransGen h g)

@[refl] theorem Zigzag.refl (X : J) : Zigzag X X := zigzag_equivalence.refl _

@[symm] theorem Zigzag.symm {j‚ÇÅ j‚ÇÇ : J} (h : Zigzag j‚ÇÅ j‚ÇÇ) : Zigzag j‚ÇÇ j‚ÇÅ := zigzag_symmetric h

@[trans] theorem Zigzag.trans {j‚ÇÅ j‚ÇÇ j‚ÇÉ : J} (h‚ÇÅ : Zigzag j‚ÇÅ j‚ÇÇ) (h‚ÇÇ : Zigzag j‚ÇÇ j‚ÇÉ) :
    Zigzag j‚ÇÅ j‚ÇÉ :=
  zigzag_equivalence.trans h‚ÇÅ h‚ÇÇ

instance : Trans (Œ± := J) (Zigzag ¬∑ ¬∑) (Zigzag ¬∑ ¬∑) (Zigzag ¬∑ ¬∑) where
  trans := Zigzag.trans

theorem Zigzag.of_zag {j‚ÇÅ j‚ÇÇ : J} (h : Zag j‚ÇÅ j‚ÇÇ) : Zigzag j‚ÇÅ j‚ÇÇ :=
  Relation.ReflTransGen.single h

theorem Zigzag.of_hom {j‚ÇÅ j‚ÇÇ : J} (f : j‚ÇÅ ‚ü∂ j‚ÇÇ) : Zigzag j‚ÇÅ j‚ÇÇ :=
  of_zag (Zag.of_hom f)

theorem Zigzag.of_inv {j‚ÇÅ j‚ÇÇ : J} (f : j‚ÇÇ ‚ü∂ j‚ÇÅ) : Zigzag j‚ÇÅ j‚ÇÇ :=
  of_zag (Zag.of_inv f)

theorem Zigzag.of_zag_trans {j‚ÇÅ j‚ÇÇ j‚ÇÉ : J} (h‚ÇÅ : Zag j‚ÇÅ j‚ÇÇ) (h‚ÇÇ : Zag j‚ÇÇ j‚ÇÉ) : Zigzag j‚ÇÅ j‚ÇÉ :=
  trans (of_zag h‚ÇÅ) (of_zag h‚ÇÇ)

instance : Trans (Œ± := J) (Zag ¬∑ ¬∑) (Zigzag ¬∑ ¬∑) (Zigzag ¬∑ ¬∑) where
  trans h h' := Zigzag.trans (.of_zag h) h'

instance : Trans (Œ± := J) (Zigzag ¬∑ ¬∑) (Zag ¬∑ ¬∑) (Zigzag ¬∑ ¬∑) where
  trans h h' := Zigzag.trans h (.of_zag h')

instance : Trans (Œ± := J) (Zag ¬∑ ¬∑) (Zag ¬∑ ¬∑) (Zigzag ¬∑ ¬∑) where
  trans := Zigzag.of_zag_trans

theorem Zigzag.of_hom_hom {j‚ÇÅ j‚ÇÇ j‚ÇÉ : J} (f‚ÇÅ‚ÇÇ : j‚ÇÅ ‚ü∂ j‚ÇÇ) (f‚ÇÇ‚ÇÉ : j‚ÇÇ ‚ü∂ j‚ÇÉ) : Zigzag j‚ÇÅ j‚ÇÉ :=
  (of_hom f‚ÇÅ‚ÇÇ).trans (of_hom f‚ÇÇ‚ÇÉ)

theorem Zigzag.of_hom_inv {j‚ÇÅ j‚ÇÇ j‚ÇÉ : J} (f‚ÇÅ‚ÇÇ : j‚ÇÅ ‚ü∂ j‚ÇÇ) (f‚ÇÉ‚ÇÇ : j‚ÇÉ ‚ü∂ j‚ÇÇ) : Zigzag j‚ÇÅ j‚ÇÉ :=
  (of_hom f‚ÇÅ‚ÇÇ).trans (of_inv f‚ÇÉ‚ÇÇ)

theorem Zigzag.of_inv_hom {j‚ÇÅ j‚ÇÇ j‚ÇÉ : J} (f‚ÇÇ‚ÇÅ : j‚ÇÇ ‚ü∂ j‚ÇÅ) (f‚ÇÇ‚ÇÉ : j‚ÇÇ ‚ü∂ j‚ÇÉ) : Zigzag j‚ÇÅ j‚ÇÉ :=
  (of_inv f‚ÇÇ‚ÇÅ).trans (of_hom f‚ÇÇ‚ÇÉ)

theorem Zigzag.of_inv_inv {j‚ÇÅ j‚ÇÇ j‚ÇÉ : J} (f‚ÇÇ‚ÇÅ : j‚ÇÇ ‚ü∂ j‚ÇÅ) (f‚ÇÉ‚ÇÇ : j‚ÇÉ ‚ü∂ j‚ÇÇ) : Zigzag j‚ÇÅ j‚ÇÉ :=
  (of_inv f‚ÇÇ‚ÇÅ).trans (of_inv f‚ÇÉ‚ÇÇ)

/-- The setoid given by the equivalence relation `Zigzag`. A quotient for this
setoid is a connected component of the category.
-/
def Zigzag.setoid (J : Type u‚ÇÇ) [Category.{v‚ÇÅ} J] : Setoid J where
  r := Zigzag
  iseqv := zigzag_equivalence

/-- If there is a zigzag from `j‚ÇÅ` to `j‚ÇÇ`, then there is a zigzag from `F j‚ÇÅ` to
`F j‚ÇÇ` as long as `F` is a prefunctor.
-/
theorem zigzag_prefunctor_obj_of_zigzag (F : J ‚•§q K) {j‚ÇÅ j‚ÇÇ : J} (h : Zigzag j‚ÇÅ j‚ÇÇ) :
    Zigzag (F.obj j‚ÇÅ) (F.obj j‚ÇÇ) :=
  h.lift _ fun _ _ => Or.imp (Nonempty.map fun f => F.map f) (Nonempty.map fun f => F.map f)

/-- If there is a zigzag from `j‚ÇÅ` to `j‚ÇÇ`, then there is a zigzag from `F j‚ÇÅ` to
`F j‚ÇÇ` as long as `F` is a functor.
-/
theorem zigzag_obj_of_zigzag (F : J ‚•§ K) {j‚ÇÅ j‚ÇÇ : J} (h : Zigzag j‚ÇÅ j‚ÇÇ) :
    Zigzag (F.obj j‚ÇÅ) (F.obj j‚ÇÇ) :=
  zigzag_prefunctor_obj_of_zigzag F.toPrefunctor h

/-- A Zag in a discrete category entails an equality of its extremities -/
lemma eq_of_zag (X) {a b : Discrete X} (h : Zag a b) : a.as = b.as :=
  h.elim (fun ‚ü®f‚ü© ‚Ü¶ Discrete.eq_of_hom f) (fun ‚ü®f‚ü© ‚Ü¶ (Discrete.eq_of_hom f).symm)

/-- A zigzag in a discrete category entails an equality of its extremities -/
lemma eq_of_zigzag (X) {a b : Discrete X} (h : Zigzag a b) : a.as = b.as := by
  induction h with
  | refl => rfl
  | tail _ h eq  => exact eq.trans (eq_of_zag _ h)

-- TODO: figure out the right way to generalise this to `Zigzag`.
theorem zag_of_zag_obj (F : J ‚•§ K) [F.Full] {j‚ÇÅ j‚ÇÇ : J} (h : Zag (F.obj j‚ÇÅ) (F.obj j‚ÇÇ)) :
    Zag j‚ÇÅ j‚ÇÇ :=
  Or.imp (Nonempty.map F.preimage) (Nonempty.map F.preimage) h

/-- Any equivalence relation containing (‚ü∂) holds for all pairs of a connected category. -/
theorem equiv_relation [IsPreconnected J] (r : J ‚Üí J ‚Üí Prop) (hr : _root_.Equivalence r)
    (h : ‚àÄ {j‚ÇÅ j‚ÇÇ : J} (_ : j‚ÇÅ ‚ü∂ j‚ÇÇ), r j‚ÇÅ j‚ÇÇ) : ‚àÄ j‚ÇÅ j‚ÇÇ : J, r j‚ÇÅ j‚ÇÇ := by
  intros j‚ÇÅ j‚ÇÇ
  have z : ‚àÄ j : J, r j‚ÇÅ j :=
    induct_on_objects {k | r j‚ÇÅ k} (hr.1 j‚ÇÅ)
      fun f => ‚ü®fun t => hr.3 t (h f), fun t => hr.3 t (hr.2 (h f))‚ü©
  exact z j‚ÇÇ

/-- In a connected category, any two objects are related by `Zigzag`. -/
theorem isPreconnected_zigzag [IsPreconnected J] (j‚ÇÅ j‚ÇÇ : J) : Zigzag j‚ÇÅ j‚ÇÇ :=
  equiv_relation _ zigzag_equivalence
    (fun f => Relation.ReflTransGen.single (Or.inl (Nonempty.intro f))) _ _


theorem zigzag_isPreconnected (h : ‚àÄ j‚ÇÅ j‚ÇÇ : J, Zigzag j‚ÇÅ j‚ÇÇ) : IsPreconnected J := by
  apply IsPreconnected.of_constant_of_preserves_morphisms
  intro Œ± F hF j j'
  specialize h j j'
  induction h with
  | refl => rfl
  | tail _ hj ih =>
    rw [ih]
    rcases hj with (‚ü®‚ü®hj‚ü©‚ü©|‚ü®‚ü®hj‚ü©‚ü©)
    exacts [hF hj, (hF hj).symm]

/-- If any two objects in a nonempty category are related by `Zigzag`, the category is connected.
-/
theorem zigzag_isConnected [Nonempty J] (h : ‚àÄ j‚ÇÅ j‚ÇÇ : J, Zigzag j‚ÇÅ j‚ÇÇ) : IsConnected J :=
  { zigzag_isPreconnected h with }

theorem exists_zigzag' [IsConnected J] (j‚ÇÅ j‚ÇÇ : J) :
    ‚àÉ l, List.Chain Zag j‚ÇÅ l ‚àß List.getLast (j‚ÇÅ :: l) (List.cons_ne_nil _ _) = j‚ÇÇ :=
  List.exists_chain_of_relationReflTransGen (isPreconnected_zigzag _ _)

/-- If any two objects in a nonempty category are linked by a sequence of (potentially reversed)
morphisms, then J is connected.

The converse of `exists_zigzag'`.
-/
theorem isPreconnected_of_zigzag (h : ‚àÄ j‚ÇÅ j‚ÇÇ : J, ‚àÉ l,
    List.Chain Zag j‚ÇÅ l ‚àß List.getLast (j‚ÇÅ :: l) (List.cons_ne_nil _ _) = j‚ÇÇ) :
    IsPreconnected J := by
  apply zigzag_isPreconnected
  intro j‚ÇÅ j‚ÇÇ
  rcases h j‚ÇÅ j‚ÇÇ with ‚ü®l, hl‚ÇÅ, hl‚ÇÇ‚ü©
  apply List.relationReflTransGen_of_exists_chain l hl‚ÇÅ hl‚ÇÇ

/-- If any two objects in a nonempty category are linked by a sequence of (potentially reversed)
morphisms, then J is connected.

The converse of `exists_zigzag'`.
-/
theorem isConnected_of_zigzag [Nonempty J] (h : ‚àÄ j‚ÇÅ j‚ÇÇ : J, ‚àÉ l,
    List.Chain Zag j‚ÇÅ l ‚àß List.getLast (j‚ÇÅ :: l) (List.cons_ne_nil _ _) = j‚ÇÇ) :
    IsConnected J :=
  { isPreconnected_of_zigzag h with }

/-- If `Discrete Œ±` is connected, then `Œ±` is (type-)equivalent to `PUnit`. -/
def discreteIsConnectedEquivPUnit {Œ± : Type u‚ÇÅ} [IsConnected (Discrete Œ±)] : Œ± ‚âÉ PUnit :=
  Discrete.equivOfEquivalence.{u‚ÇÅ, u‚ÇÅ}
    { functor := Functor.star (Discrete Œ±)
      inverse := Discrete.functor fun _ => Classical.arbitrary _
      unitIso := isoConstant _ (Classical.arbitrary _)
      counitIso := Functor.punitExt _ _ }

variable {C : Type w‚ÇÇ} [Category.{w‚ÇÅ} C]

/-- For objects `X Y : C`, any natural transformation `Œ± : const X ‚ü∂ const Y` from a connected
category must be constant.
This is the key property of connected categories which we use to establish properties about limits.
-/
theorem nat_trans_from_is_connected [IsPreconnected J] {X Y : C}
    (Œ± : (Functor.const J).obj X ‚ü∂ (Functor.const J).obj Y) :
    ‚àÄ j j' : J, Œ±.app j = (Œ±.app j' : X ‚ü∂ Y) :=
  @constant_of_preserves_morphisms _ _ _ (X ‚ü∂ Y) (fun j => Œ±.app j) fun _ _ f => by
    simpa using (Œ±.naturality f).symm

instance [IsConnected J] : (Functor.const J : C ‚•§ J ‚•§ C).Full where
  map_surjective f := ‚ü®f.app (Classical.arbitrary J), by
    ext j
    apply nat_trans_from_is_connected f (Classical.arbitrary J) j‚ü©

theorem nonempty_hom_of_preconnected_groupoid {G} [Groupoid G] [IsPreconnected G] :
    ‚àÄ x y : G, Nonempty (x ‚ü∂ y) := by
  refine equiv_relation _ ?_ fun {j‚ÇÅ j‚ÇÇ} => Nonempty.intro
  exact
    ‚ü®fun j => ‚ü®ùüô _‚ü©,
     fun {j‚ÇÅ j‚ÇÇ} => Nonempty.map fun f => inv f,
     fun {_ _ _} => Nonempty.map2 (¬∑ ‚â´ ¬∑)‚ü©

attribute [instance] nonempty_hom_of_preconnected_groupoid

instance isPreconnected_of_subsingleton [Subsingleton J] : IsPreconnected J where
  iso_constant {Œ±} F j := ‚ü®NatIso.ofComponents (fun x ‚Ü¶ eqToIso (by simp [Subsingleton.allEq x j]))‚ü©

instance isConnected_of_nonempty_and_subsingleton [Nonempty J] [Subsingleton J] :
    IsConnected J where

end CategoryTheory
