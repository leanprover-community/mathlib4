/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Functor.Basic

/-!
# Unbundled functors, as a typeclass decorating the object-level function.
-/


namespace CategoryTheory

-- declare the `v`'s first; see `CategoryTheory.Category` for an explanation
universe v vâ‚ vâ‚‚ vâ‚ƒ u uâ‚ uâ‚‚ uâ‚ƒ

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D]

-- Perhaps in the future we could redefine `Functor` in terms of this, but that isn't the
-- immediate plan.
/-- An unbundled functor. -/
class Functorial (F : C â†’ D) : Type max vâ‚ vâ‚‚ uâ‚ uâ‚‚ where
  /-- If `F : C â†’ D` (just a function) has `[Functorial F]`,
  we can write `map F f : F X âŸ¶ F Y` for the action of `F` on a morphism `f : X âŸ¶ Y`. -/
  map (F) : âˆ€ {X Y : C}, (X âŸ¶ Y) â†’ (F X âŸ¶ F Y)
  /-- A functorial map preserves identities. -/
  map_id : âˆ€ {X : C}, map (ðŸ™ X) = ðŸ™ (F X) := by cat_disch
  /-- A functorial map preserves composition of morphisms. -/
  map_comp : âˆ€ {X Y Z : C} {f : X âŸ¶ Y} {g : Y âŸ¶ Z}, map (f â‰« g) = map f â‰« map g := by
    cat_disch

attribute [simp, grind =] Functorial.map_id Functorial.map_comp
export Functorial (map)

namespace Functor

/-- Bundle a functorial function as a functor.
-/
def of (F : C â†’ D) [I : Functorial.{vâ‚, vâ‚‚} F] : C â¥¤ D :=
  { I with obj := F
           map := Functorial.map F }

end Functor

instance (F : C â¥¤ D) : Functorial.{vâ‚, vâ‚‚} F.obj :=
  { F with map := F.map }

@[simp, grind =]
theorem map_functorial_obj (F : C â¥¤ D) {X Y : C} (f : X âŸ¶ Y) : map F.obj f = F.map f :=
  rfl

instance functorial_id : Functorial.{vâ‚, vâ‚} (id : C â†’ C) where map f := f

section

variable {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E]

-- This is no longer viable as an instance in Lean 3.7,
-- #lint reports an instance loop
-- Will this be a problem?
/-- `G âˆ˜ F` is a functorial if both `F` and `G` are.
-/
def functorial_comp (F : C â†’ D) [Functorial.{vâ‚, vâ‚‚} F] (G : D â†’ E) [Functorial.{vâ‚‚, vâ‚ƒ} G] :
    Functorial.{vâ‚, vâ‚ƒ} (G âˆ˜ F) :=
  { Functor.of F â‹™ Functor.of G with map := fun f => map G (map F f) }

end

end CategoryTheory
