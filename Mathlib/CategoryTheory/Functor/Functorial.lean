/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Functor.Basic

/-!
# Unbundled functors, as a typeclass decorating the object-level function.
-/


namespace CategoryTheory

-- declare the `v`'s first; see `CategoryTheory.Category` for an explanation
universe v v‚ÇÅ v‚ÇÇ v‚ÇÉ u u‚ÇÅ u‚ÇÇ u‚ÇÉ

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]

-- Perhaps in the future we could redefine `Functor` in terms of this, but that isn't the
-- immediate plan.
/-- An unbundled functor. -/
class Functorial (F : C ‚Üí D) : Type max v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ where
  /-- If `F : C ‚Üí D` (just a function) has `[Functorial F]`,
  we can write `map F f : F X ‚ü∂ F Y` for the action of `F` on a morphism `f : X ‚ü∂ Y`. -/
  map (F) : ‚àÄ {X Y : C}, (X ‚ü∂ Y) ‚Üí (F X ‚ü∂ F Y)
  /-- A functorial map preserves identities. -/
  map_id : ‚àÄ {X : C}, map (ùüô X) = ùüô (F X) := by cat_disch
  /-- A functorial map preserves composition of morphisms. -/
  map_comp : ‚àÄ {X Y Z : C} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z}, map (f ‚â´ g) = map f ‚â´ map g := by
    cat_disch

attribute [simp, grind =] Functorial.map_id Functorial.map_comp
export Functorial (map)

namespace Functor

/-- Bundle a functorial function as a functor.
-/
def of (F : C ‚Üí D) [I : Functorial.{v‚ÇÅ, v‚ÇÇ} F] : C ‚•§ D :=
  { I with obj := F
           map := Functorial.map F }

end Functor

instance (F : C ‚•§ D) : Functorial.{v‚ÇÅ, v‚ÇÇ} F.obj :=
  { F with map := F.map }

@[simp, grind =]
theorem map_functorial_obj (F : C ‚•§ D) {X Y : C} (f : X ‚ü∂ Y) : map F.obj f = F.map f :=
  rfl

instance functorial_id : Functorial.{v‚ÇÅ, v‚ÇÅ} (id : C ‚Üí C) where map f := f

section

variable {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E]

-- This is no longer viable as an instance in Lean 3.7,
-- #lint reports an instance loop
-- Will this be a problem?
/-- `G ‚àò F` is a functorial if both `F` and `G` are.
-/
def functorial_comp (F : C ‚Üí D) [Functorial.{v‚ÇÅ, v‚ÇÇ} F] (G : D ‚Üí E) [Functorial.{v‚ÇÇ, v‚ÇÉ} G] :
    Functorial.{v‚ÇÅ, v‚ÇÉ} (G ‚àò F) :=
  { Functor.of F ‚ãô Functor.of G with map := fun f ‚Ü¶ map G (map F f) }

end

end CategoryTheory
