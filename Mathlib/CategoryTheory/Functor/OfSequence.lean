/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Order.Group.Nat
import Mathlib.CategoryTheory.Category.Preorder
import Mathlib.CategoryTheory.EqToHom

/-!
# Functors from the category of the ordered set `‚Ñï`

In this file, we provide a constructor `Functor.ofSequence`
for functors `‚Ñï ‚•§ C` which takes as an input a sequence
of morphisms `f : X n ‚ü∂ X (n + 1)` for all `n : ‚Ñï`.

We also provide a constructor `NatTrans.ofSequence` for natural
transformations between functors `‚Ñï ‚•§ C` which allows to check
the naturality condition only for morphisms `n ‚ü∂ n + 1`.

-/

namespace CategoryTheory

open Category

variable {C : Type*} [Category C]

namespace Functor

variable {X : ‚Ñï ‚Üí C} (f : ‚àÄ n, X n ‚ü∂ X (n + 1))

namespace OfSequence

lemma congr_f (i j : ‚Ñï) (h : i = j) :
    f i = eqToHom (by rw [h]) ‚â´ f j ‚â´ eqToHom (by rw [h]) := by
  subst h
  simp

/-- The morphism `X i ‚ü∂ X (i + k)` obtained by composing `k` morphisms of
the form `X n ‚ü∂ X (n + 1)`. -/
@[simp]
def map' : ‚àÄ (i k : ‚Ñï), X i ‚ü∂ X (i + k)
  | _, 0 => ùüô _
  | i, (k+1) => map' i k ‚â´ f (i + k)

lemma comp_map' (i k‚ÇÅ k‚ÇÇ : ‚Ñï) :
    map' f i k‚ÇÅ ‚â´ map' f (i + k‚ÇÅ) k‚ÇÇ =
      map' f i (k‚ÇÅ + k‚ÇÇ) ‚â´ eqToHom (by rw [add_assoc]) := by
  revert i k‚ÇÅ
  induction k‚ÇÇ with
  | zero => simp
  | succ k‚ÇÇ hk‚ÇÇ =>
      intro i k‚ÇÅ
      simp [reassoc_of% (hk‚ÇÇ i k‚ÇÅ), congr_f f _ _ (add_assoc i k‚ÇÅ k‚ÇÇ)]

/-- The morphism `X i ‚ü∂ X j` obtained by composing morphisms of
the form `X n ‚ü∂ X (n + 1)` when `i ‚â§ j`. -/
def map (i j : ‚Ñï) (hij : i ‚â§ j) : X i ‚ü∂ X j :=
  map' f i (j - i) ‚â´ eqToHom (by congr; omega)

lemma map_eq (i j k : ‚Ñï) (hk : i + k = j) :
    map f i j (by omega) = map' f i k ‚â´ eqToHom (by rw [hk]) := by
  obtain rfl := tsub_eq_of_eq_add_rev hk.symm
  rfl

lemma map_id (i : ‚Ñï) : map f i i (by rfl) = ùüô _ := by
  rw [map_eq f i i 0 (by omega), eqToHom_refl, comp_id]
  rfl

lemma map_comp (i j k : ‚Ñï) (hij : i ‚â§ j) (hjk : j ‚â§ k) :
    map f i k (hij.trans hjk) = map f i j hij ‚â´ map f j k hjk := by
  obtain ‚ü®k‚ÇÅ, rfl‚ü© := Nat.exists_eq_add_of_le hij
  obtain ‚ü®k‚ÇÇ, rfl‚ü© := Nat.exists_eq_add_of_le hjk
  rw [map_eq f i _ k‚ÇÅ rfl, eqToHom_refl, comp_id, map_eq f (i + k‚ÇÅ) _ k‚ÇÇ rfl, eqToHom_refl,
    comp_id, comp_map', map_eq f i (i + k‚ÇÅ + k‚ÇÇ) (k‚ÇÅ + k‚ÇÇ) (by rw [add_assoc])]

lemma map_of_le_succ (n : ‚Ñï) :
    map f n (n+1) (by omega) = f n := by
  simp [map_eq f n _ 1 rfl]

end OfSequence

/-- The functor `‚Ñï ‚•§ C` constructed from a sequence of
morphisms `f : X n ‚ü∂ X (n + 1)` for all `n : ‚Ñï`. -/
@[simps obj]
def ofSequence : ‚Ñï ‚•§ C where
  obj := X
  map {i j} œÜ := OfSequence.map f i j (leOfHom œÜ)
  map_id i := OfSequence.map_id f i
  map_comp {i j k} Œ± Œ≤ := OfSequence.map_comp f i j k (leOfHom Œ±) (leOfHom Œ≤)

@[simp]
lemma ofSequence_map_homOfLE_succ (n : ‚Ñï) :
    (ofSequence f).map (homOfLE (Nat.le_add_right n 1)) = f n :=
  OfSequence.map_of_le_succ f n

end Functor

namespace NatTrans

variable {F G : ‚Ñï ‚•§ C} (app : ‚àÄ (n : ‚Ñï), F.obj n ‚ü∂ G.obj n)
  (naturality : ‚àÄ (n : ‚Ñï), F.map (homOfLE (n.le_add_right 1)) ‚â´ app (n + 1) =
      app n ‚â´ G.map (homOfLE (n.le_add_right 1)))

/-- Constructor for natural transformations `F ‚ü∂ G` in `‚Ñï ‚•§ C` which takes as inputs
the morphisms `F.obj n ‚ü∂ G.obj n` for all `n : ‚Ñï` and the naturality condition only
for morphisms of the form `n ‚ü∂ n + 1`. -/
@[simps app]
def ofSequence : F ‚ü∂ G where
  app := app
  naturality := by
    intro i j œÜ
    obtain ‚ü®k, hk‚ü© := Nat.exists_eq_add_of_le (leOfHom œÜ)
    obtain rfl := Subsingleton.elim œÜ (homOfLE (by omega))
    revert i j
    induction k with
    | zero =>
        intro i j hk
        obtain rfl : j = i := by omega
        simp
    | succ k hk =>
        intro i j hk'
        obtain rfl : j = i + k + 1 := by omega
        simp only [‚Üê homOfLE_comp (show i ‚â§ i + k by omega) (show i + k ‚â§ i + k + 1 by omega),
          Functor.map_comp, assoc, naturality, reassoc_of% (hk rfl)]

end NatTrans

end CategoryTheory
