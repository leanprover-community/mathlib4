/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Order.Group.Nat
import Mathlib.CategoryTheory.Category.Preorder
import Mathlib.CategoryTheory.EqToHom

/-!
# Functors from the category of the ordered set `‚Ñï`

In this file, we provide a constructor `Functor.ofSequence`
for functors `‚Ñï ‚•§ C` which takes as an input a sequence
of morphisms `f : X n ‚ü∂ X (n + 1)` for all `n : ‚Ñï`.

We also provide a constructor `NatTrans.ofSequence` for natural
transformations between functors `‚Ñï ‚•§ C` which allows to check
the naturality condition only for morphisms `n ‚ü∂ n + 1`.

The duals of the above for functors `‚Ñï·µí·µñ ‚•§ C` are given by `Functor.ofOpSequence` and
`NatTrans.ofOpSequence`.

-/

namespace CategoryTheory

open Category

variable {C : Type*} [Category C]

namespace Functor

variable {X : ‚Ñï ‚Üí C} (f : ‚àÄ n, X n ‚ü∂ X (n + 1))

namespace OfSequence

lemma congr_f (i j : ‚Ñï) (h : i = j) :
    f i = eqToHom (by rw [h]) ‚â´ f j ‚â´ eqToHom (by rw [h]) := by
  subst h
  simp

/-- The morphism `X i ‚ü∂ X j` obtained by composing morphisms of
the form `X n ‚ü∂ X (n + 1)` when `i ‚â§ j`. -/
def map : ‚àÄ {X : ‚Ñï ‚Üí C} (_ : ‚àÄ n, X n ‚ü∂ X (n + 1)) (i j : ‚Ñï), i ‚â§ j ‚Üí (X i ‚ü∂ X j)
  | _, _, 0, 0 => fun _ ‚Ü¶ ùüô _
  | _, f, 0, 1 => fun _ ‚Ü¶ f 0
  | _, f, 0, l + 1 => fun _ ‚Ü¶ f 0 ‚â´ map (fun n ‚Ü¶ f (n + 1)) 0 l (by omega)
  | _, _, _ + 1, 0 => nofun
  | _, f, k + 1, l + 1 => fun _ ‚Ü¶ map (fun n ‚Ü¶ f (n + 1)) k l (by omega)

lemma map_id (i : ‚Ñï) : map f i i (by omega) = ùüô _ := by
  revert X f
  induction i with
  | zero => intros; rfl
  | succ _ hi =>
      intro X f
      apply hi

lemma map_le_succ (i : ‚Ñï) : map f i (i + 1) (by omega) = f i := by
  revert X f
  induction i with
  | zero => intros; rfl
  | succ _ hi =>
      intro X f
      apply hi

@[reassoc]
lemma map_comp (i j k : ‚Ñï) (hij : i ‚â§ j) (hjk : j ‚â§ k) :
    map f i k (hij.trans hjk) = map f i j hij ‚â´ map f j k hjk := by
  revert X f j k
  induction i with
  | zero =>
      intros X f j
      revert X f
      induction j with
      | zero =>
          intros X f k hij hjk
          rw [map_id, id_comp]
      | succ j hj =>
          rintro X f (_|_|k) hij hjk
          ¬∑ omega
          ¬∑ obtain rfl : j = 0 := by omega
            rw [map_id, comp_id]
          ¬∑ dsimp [map]
            rw [hj (fun n ‚Ü¶ f (n + 1)) (k + 1) (by omega) (by omega)]
            obtain _|j := j
            all_goals simp [map]
  | succ i hi =>
      rintro X f (_|j) (_|k)
      ¬∑ omega
      ¬∑ omega
      ¬∑ omega
      ¬∑ intros
        exact hi _ j k (by omega) (by omega)

-- `map` has good definitional properties when applied to explicit natural numbers
example : map f 5 5 (by omega) = ùüô _ := rfl
example : map f 0 3 (by omega) = f 0 ‚â´ f 1 ‚â´ f 2 := rfl
example : map f 3 7 (by omega) = f 3 ‚â´ f 4 ‚â´ f 5 ‚â´ f 6 := rfl

end OfSequence

/-- The functor `‚Ñï ‚•§ C` constructed from a sequence of
morphisms `f : X n ‚ü∂ X (n + 1)` for all `n : ‚Ñï`. -/
@[simps obj]
def ofSequence : ‚Ñï ‚•§ C where
  obj := X
  map {i j} œÜ := OfSequence.map f i j (leOfHom œÜ)
  map_id i := OfSequence.map_id f i
  map_comp {i j k} Œ± Œ≤ := OfSequence.map_comp f i j k (leOfHom Œ±) (leOfHom Œ≤)

@[simp]
lemma ofSequence_map_homOfLE_succ (n : ‚Ñï) :
    (ofSequence f).map (homOfLE (Nat.le_add_right n 1)) = f n :=
  OfSequence.map_le_succ f n

end Functor

namespace NatTrans

variable {F G : ‚Ñï ‚•§ C} (app : ‚àÄ (n : ‚Ñï), F.obj n ‚ü∂ G.obj n)
  (naturality : ‚àÄ (n : ‚Ñï), F.map (homOfLE (n.le_add_right 1)) ‚â´ app (n + 1) =
      app n ‚â´ G.map (homOfLE (n.le_add_right 1)))

/-- Constructor for natural transformations `F ‚ü∂ G` in `‚Ñï ‚•§ C` which takes as inputs
the morphisms `F.obj n ‚ü∂ G.obj n` for all `n : ‚Ñï` and the naturality condition only
for morphisms of the form `n ‚ü∂ n + 1`. -/
@[simps app]
def ofSequence : F ‚ü∂ G where
  app := app
  naturality := by
    intro i j œÜ
    obtain ‚ü®k, hk‚ü© := Nat.exists_eq_add_of_le (leOfHom œÜ)
    obtain rfl := Subsingleton.elim œÜ (homOfLE (by omega))
    revert i j
    induction k with
    | zero =>
        intro i j hk
        obtain rfl : j = i := by omega
        simp
    | succ k hk =>
        intro i j hk'
        obtain rfl : j = i + k + 1 := by omega
        simp only [‚Üê homOfLE_comp (show i ‚â§ i + k by omega) (show i + k ‚â§ i + k + 1 by omega),
          Functor.map_comp, assoc, naturality, reassoc_of% (hk rfl)]

end NatTrans

namespace Functor

variable {X : ‚Ñï ‚Üí C} (f : ‚àÄ n, X (n + 1) ‚ü∂ X n)

namespace OfOpSequence

lemma congr_f (i j : ‚Ñï) (h : i = j) :
    f i = eqToHom (by rw [h]) ‚â´ f j ‚â´ eqToHom (by rw [h]) := by
  subst h
  simp

/-- The morphism `X j ‚ü∂ X i` obtained by composing morphisms of
the form `X (n + 1) ‚ü∂ X n` when `i ‚â§ j`. -/
def map : ‚àÄ {X : ‚Ñï ‚Üí C} (_ : ‚àÄ n, X (n + 1) ‚ü∂ X n) (i j : ‚Ñï), i ‚â§ j ‚Üí (X j ‚ü∂ X i)
  | _, _, 0, 0 => fun _ ‚Ü¶ ùüô _
  | _, f, 0, 1 => fun _ ‚Ü¶ f 0
  | _, f, 0, l + 1 => fun _ ‚Ü¶ f l ‚â´ map f 0 l (by omega)
  | _, _, _ + 1, 0 => nofun
  | X, f, k + 1, l + 1 => fun _ ‚Ü¶ map (X := fun n ‚Ü¶ X (n + 1)) (fun n ‚Ü¶ f (n + 1)) k l (by omega)


lemma map_id (i : ‚Ñï) : map f i i (by omega) = ùüô _ := by
  revert X f
  induction i with
  | zero => intros; rfl
  | succ _ hi =>
      intro X f
      apply hi

lemma map_le_succ (i : ‚Ñï) : map f i (i + 1) (by omega) = f i := by
  revert X f
  induction i with
  | zero => intros; rfl
  | succ _ hi =>
      intro X f
      apply hi

lemma map_zero_succ (i : ‚Ñï) : map f 0 (i + 1) (by omega) = f i ‚â´ map f 0 i (by omega) := by
  revert X f
  induction i with
  | zero => intros; simp [map]
  | succ _ _ => intros; simp [map]

lemma map_succ (i j : ‚Ñï) (h : j ‚â§ i) :
    map f j (i + 1) (by omega) = map f (j + 1) (i + 1) (by omega) ‚â´ f j := by
  revert X f i
  induction j with
  | zero =>
      intro X f i
      induction i with
      | zero => simp [map]
      | succ _ hi => simp [map, map_zero_succ, hi]
  | succ j hj => rintro X f (_|i) h; exacts [by omega, hj _ _ (by omega)]

@[reassoc]
lemma map_comp (i j k : ‚Ñï) (hij : i ‚â§ j) (hjk : j ‚â§ k) :
    map f i k (hij.trans hjk) = map f j k hjk ‚â´ map f i j hij := by
  revert X f i j
  induction k with
  | zero =>
      intros X f j
      revert X f
      induction j with
      | zero =>
          intros X f k hij hjk
          obtain rfl : k = 0 := by omega
          rw [map_id, comp_id]
      | succ j hj =>
          rintro X f (_|_|k) hij hjk
          ¬∑ omega
          ¬∑ obtain rfl : j = 0 := by omega
            rw [map_id, comp_id]
          ¬∑ omega
  | succ k hk =>
      rintro X f (_|i)
      ¬∑ intro j
        induction j with
        | zero => intros; simp [map]
        | succ j hj =>
            intro _ h
            nth_rewrite 2 [map_zero_succ]
            rw [hj (by omega) (by omega), map_succ _ k j (by omega)]
            simp
      ¬∑ rintro (_|j)
        ¬∑ omega
        ¬∑ intros
          exact hk _ i j (by omega) (by omega)

-- `map` has good definitional properties when applied to explicit natural numbers
example : map f 5 5 (by omega) = ùüô _ := rfl
example : map f 0 3 (by omega) = f 2 ‚â´ f 1 ‚â´ f 0 := rfl
example : map f 3 7 (by omega) = f 6 ‚â´ f 5 ‚â´ f 4 ‚â´ f 3 := rfl

end OfOpSequence

/-- The functor `‚Ñï·µí·µñ ‚•§ C` constructed from a sequence of
morphisms `f : X (n + 1) ‚ü∂ X n` for all `n : ‚Ñï`. -/
@[simps obj]
def ofOpSequence : ‚Ñï·µí·µñ ‚•§ C where
  obj n := X n.unop
  map œÜ := OfOpSequence.map f _ _ (leOfHom œÜ.unop)
  map_id i := OfOpSequence.map_id f i.unop
  map_comp Œ± Œ≤ := OfOpSequence.map_comp f _ _ _ (leOfHom Œ≤.unop) (leOfHom Œ±.unop)

@[simp]
lemma ofOpSequence_map_homOfLE_succ (n : ‚Ñï) :
    (ofOpSequence f).map (homOfLE (Nat.le_add_right n 1)).op = f n :=
  OfOpSequence.map_le_succ f n

end Functor

namespace NatTrans

variable {F G : ‚Ñï·µí·µñ ‚•§ C} (app : ‚àÄ (n : ‚Ñï), F.obj ‚ü®n‚ü© ‚ü∂ G.obj ‚ü®n‚ü©)
  (naturality : ‚àÄ (n : ‚Ñï), F.map (homOfLE (n.le_add_right 1)).op ‚â´ app n =
      app (n + 1) ‚â´ G.map (homOfLE (n.le_add_right 1)).op)

/-- Constructor for natural transformations `F ‚ü∂ G` in `‚Ñï·µí·µñ ‚•§ C` which takes as inputs
the morphisms `F.obj ‚ü®n‚ü© ‚ü∂ G.obj ‚ü®n‚ü©` for all `n : ‚Ñï` and the naturality condition only
for morphisms of the form `n ‚ü∂ n + 1`. -/
@[simps app]
def ofOpSequence : F ‚ü∂ G where
  app n := app n.unop
  naturality := by
    intro ‚ü®i‚ü© ‚ü®j‚ü© ‚ü®(œÜ : j ‚ü∂ i)‚ü©
    change F.map œÜ.op ‚â´ _ = _ ‚â´ G.map œÜ.op
    obtain ‚ü®k, hk‚ü© := Nat.exists_eq_add_of_le (leOfHom œÜ)
    obtain rfl := Subsingleton.elim œÜ (homOfLE (by omega))
    revert i j
    induction k with
    | zero =>
        intro i j hk
        obtain rfl : j = i := by omega
        simp
    | succ k hk =>
        intro i j hk'
        obtain rfl : i = j + k + 1 := by omega
        simp only [‚Üê homOfLE_comp (show j ‚â§ j + k by omega) (show j + k ‚â§ j + k + 1 by omega),
          Functor.map_comp, assoc, naturality, op_comp, hk _ j rfl ]
        simp only [homOfLE_leOfHom, ‚Üê assoc, naturality]

end NatTrans

end CategoryTheory
