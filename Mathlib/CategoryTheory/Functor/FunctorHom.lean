/-
Copyright (c) 2024 Jack McKoen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jack McKoen, JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Monoidal.FunctorCategory
import Mathlib.CategoryTheory.Monoidal.Types.Basic
import Mathlib.CategoryTheory.Enriched.Basic

/-!
# Internal hom in functor categories

Given functors `F G : C â¥¤ D`, define a functor `functorHom F G` from `C` to `Type max v' v u`,
which is a proxy for the "internal hom" functor Hom(F âŠ— coyoneda(-), G). This is used to show
that the functor category `C â¥¤ D` is enriched over `C â¥¤ Type max v' v u`. This is also useful
for showing that `C â¥¤ Type max w v u` is monoidal closed.

See `Mathlib/CategoryTheory/Closed/FunctorToTypes.lean`.

-/


universe w v' v u u'

open CategoryTheory MonoidalCategory

variable {C : Type u} [Category.{v} C] {D : Type u'} [Category.{v'} D]

variable (F G : C â¥¤ D)

namespace CategoryTheory.Functor

/-- Given functors `F G : C â¥¤ D`, `HomObj F G A` is a proxy for the type
of "morphisms" `F âŠ— A âŸ¶ G`, where `A : C â¥¤ Type w` (`w` an arbitrary universe). -/
@[ext]
structure HomObj (A : C â¥¤ Type w) where
  /-- The morphism `F.obj c âŸ¶ G.obj c` associated with `a : A.obj c`. -/
  app (c : C) (a : A.obj c) : F.obj c âŸ¶ G.obj c
  naturality {c d : C} (f : c âŸ¶ d) (a : A.obj c) :
    F.map f â‰« app d (A.map f a) = app c a â‰« G.map f := by cat_disch

/-- When `F`, `G`, and `A` are all functors `C â¥¤ Type w`, then `HomObj F G A` is in
bijection with `F âŠ— A âŸ¶ G`. -/
@[simps]
def homObjEquiv (F G A : C â¥¤ Type w) : (HomObj F G A) â‰ƒ (F âŠ— A âŸ¶ G) where
  toFun a := âŸ¨fun X âŸ¨x, yâŸ© â†¦ a.app X y x, fun X Y f â†¦ by
    ext âŸ¨x, yâŸ©
    erw [congr_fun (a.naturality f y) x]
    rfl âŸ©
  invFun a := âŸ¨fun X y x â†¦ a.app X (x, y), fun Ï† y â†¦ by
    ext x
    erw [congr_fun (a.naturality Ï†) (x, y)]
    rfl âŸ©
  left_inv _ := by aesop
  right_inv _ := by aesop

namespace HomObj

attribute [reassoc (attr := simp)] naturality

variable {F G} {A : C â¥¤ Type w}

lemma congr_app {f g : HomObj F G A} (h : f = g) (X : C)
    (a : A.obj X) : f.app X a = g.app X a := by subst h; rfl

/-- Given a natural transformation `F âŸ¶ G`, get a term of `HomObj F G A` by "ignoring" `A`. -/
@[simps]
def ofNatTrans (f : F âŸ¶ G) : HomObj F G A where
  app X _ := f.app X

/-- The identity `HomObj F F A`. -/
@[simps!]
def id (A : C â¥¤ Type w) : HomObj F F A := ofNatTrans (ðŸ™ F)

/-- Composition of `f : HomObj F G A` with `g : HomObj G M A`. -/
@[simps]
def comp {M : C â¥¤ D} (f : HomObj F G A) (g : HomObj G M A) : HomObj F M A where
  app X a := f.app X a â‰« g.app X a

/-- Given a morphism `A' âŸ¶ A`, send a term of `HomObj F G A` to a term of `HomObj F G A'`. -/
@[simps]
def map {A' : C â¥¤ Type w} (f : A' âŸ¶ A) (x : HomObj F G A) : HomObj F G A' where
  app Î” a := x.app Î” (f.app Î” a)
  naturality {Î” Î”'} Ï† a := by
    rw [â† x.naturality Ï† (f.app Î” a), FunctorToTypes.naturality _ _ f Ï† a]

end HomObj

/-- The contravariant functor taking `A : C â¥¤ Type w` to `HomObj F G A`, i.e. Hom(F âŠ— -, G). -/
@[simps]
def homObjFunctor : (C â¥¤ Type w)áµ’áµ– â¥¤ Type max w v' u where
  obj A := HomObj F G A.unop
  map {A A'} f x :=
    { app := fun X a â†¦ x.app X (f.unop.app _ a)
      naturality := fun {X Y} Ï† a â†¦ by
        rw [â† HomObj.naturality]
        congr 2
        exact congr_fun (f.unop.naturality Ï†) a }

/-- Composition of `homObjFunctor` with the co-Yoneda embedding, i.e. Hom(F âŠ— coyoneda(-), G).
When `F G : C â¥¤ Type max v' v u`, this is the internal hom of `F` and `G`: see
`Mathlib/CategoryTheory/Closed/FunctorToTypes.lean`. -/
def functorHom (F G : C â¥¤ D) : C â¥¤ Type max v' v u := coyoneda.rightOp â‹™ homObjFunctor.{v} F G

variable {F G} in
@[ext]
lemma functorHom_ext {X : C} {x y : (F.functorHom G).obj X}
    (h : âˆ€ (Y : C) (f : X âŸ¶ Y), x.app Y f = y.app Y f) : x = y :=
  HomObj.ext (by ext; apply h)

/-- The equivalence `(A âŸ¶ F.functorHom G) â‰ƒ HomObj F G A`. -/
@[simps]
def functorHomEquiv (A : C â¥¤ Type max u v v') : (A âŸ¶ F.functorHom G) â‰ƒ HomObj F G A where
  toFun Ï† :=
    { app := fun X a â†¦ (Ï†.app X a).app X (ðŸ™ _)
      naturality := fun {X Y} f a => by
        rw [â† (Ï†.app X a).naturality f (ðŸ™ _)]
        have := HomObj.congr_app (congr_fun (Ï†.naturality f) a) Y (ðŸ™ _)
        dsimp [functorHom, homObjFunctor] at this
        aesop }
  invFun x :=
    { app := fun X a â†¦ { app := fun Y f => x.app Y (A.map f a) }
      naturality := fun X Y f => by
        ext
        dsimp only [types_comp_apply]
        rw [â† FunctorToTypes.map_comp_apply]
        rfl }
  left_inv Ï† := by
    ext X a Y f
    exact (HomObj.congr_app (congr_fun (Ï†.naturality f) a) Y (ðŸ™ _)).trans
      (congr_arg ((Ï†.app X a).app Y) (by simp))
  right_inv x := by simp

variable {F G} in
/-- Morphisms `(ðŸ™_ (C â¥¤ Type max v' v u) âŸ¶ F.functorHom G)` are in bijection with
morphisms `F âŸ¶ G`. -/
@[simps]
def natTransEquiv : (ðŸ™_ (C â¥¤ Type max v' v u) âŸ¶ F.functorHom G) â‰ƒ (F âŸ¶ G) where
  toFun f := âŸ¨fun X â†¦ (f.app X (PUnit.unit)).app X (ðŸ™ _), by
    intro X Y Ï†
    rw [â† (f.app X (PUnit.unit)).naturality Ï†]
    congr 1
    have := HomObj.congr_app (congr_fun (f.naturality Ï†) PUnit.unit) Y (ðŸ™ Y)
    dsimp [functorHom, homObjFunctor] at this
    aesop âŸ©
  invFun f := { app _ _ := HomObj.ofNatTrans f }
  left_inv f := by
    ext X a Y Ï†
    have := HomObj.congr_app (congr_fun (f.naturality Ï†) PUnit.unit) Y (ðŸ™ Y)
    dsimp [functorHom, homObjFunctor] at this
    aesop

end CategoryTheory.Functor

open Functor

namespace CategoryTheory.Enriched.Functor

@[simp]
lemma natTransEquiv_symm_app_app_apply (F G : C â¥¤ D) (f : F âŸ¶ G)
    {X : C} {a : (ðŸ™_ (C â¥¤ Type (max v' v u))).obj X} (Y : C) {Ï† : X âŸ¶ Y} :
    ((natTransEquiv.symm f).app X a).app Y Ï† = f.app Y := rfl

@[simp]
lemma natTransEquiv_symm_whiskerRight_functorHom_app (K L : C â¥¤ D) (X : C) (f : K âŸ¶ K)
    (x : ðŸ™_ _ âŠ— (K.functorHom L).obj X) :
    ((natTransEquiv.symm f â–· K.functorHom L).app X x) =
    (HomObj.ofNatTrans f, x.2) := rfl

@[simp]
lemma functorHom_whiskerLeft_natTransEquiv_symm_app (K L : C â¥¤ D) (X : C) (f : L âŸ¶ L)
    (x : (K.functorHom L).obj X âŠ— ðŸ™_ _) :
    ((K.functorHom L â— natTransEquiv.symm f).app X x) =
    (x.1, HomObj.ofNatTrans f) := rfl

@[simp]
lemma whiskerLeft_app_apply (K L M N : C â¥¤ D)
    (g : L.functorHom M âŠ— M.functorHom N âŸ¶ L.functorHom N)
    {X : C} (a : (K.functorHom L âŠ— L.functorHom M âŠ— M.functorHom N).obj X) :
    (K.functorHom L â— g).app X a = âŸ¨a.1, g.app X a.2âŸ© := rfl

@[simp]
lemma whiskerRight_app_apply (K L M N : C â¥¤ D)
    (f : K.functorHom L âŠ— L.functorHom M âŸ¶ K.functorHom M)
    {X : C} (a : ((K.functorHom L âŠ— L.functorHom M) âŠ— M.functorHom N).obj X) :
    (f â–·  M.functorHom N).app X a = âŸ¨f.app X a.1, a.2âŸ© := rfl

@[simp]
lemma associator_inv_apply (K L M N : C â¥¤ D) {X : C}
    (x : ((K.functorHom L) âŠ— (L.functorHom M) âŠ— (M.functorHom N)).obj X) :
    (Î±_ ((K.functorHom L).obj X) ((L.functorHom M).obj X) ((M.functorHom N).obj X)).inv x =
    âŸ¨âŸ¨x.1, x.2.1âŸ©, x.2.2âŸ© := rfl

@[simp]
lemma associator_hom_apply (K L M N : C â¥¤ D) {X : C}
    (x : (((K.functorHom L) âŠ— (L.functorHom M)) âŠ— (M.functorHom N)).obj X) :
    (Î±_ ((K.functorHom L).obj X) ((L.functorHom M).obj X) ((M.functorHom N).obj X)).hom x =
    âŸ¨x.1.1, x.1.2, x.2âŸ© := rfl

noncomputable instance : EnrichedCategory (C â¥¤ Type max v' v u) (C â¥¤ D) where
  Hom := functorHom
  id F := natTransEquiv.symm (ðŸ™ F)
  comp F G H := { app := fun _ âŸ¨f, gâŸ© => f.comp g }

end CategoryTheory.Enriched.Functor
