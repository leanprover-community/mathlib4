/-
Copyright (c) 2024 Jack McKoen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jack McKoen, Jo√´l Riou
-/
import Mathlib.CategoryTheory.Monoidal.FunctorCategory
import Mathlib.CategoryTheory.Monoidal.Types.Basic
import Mathlib.CategoryTheory.Enriched.Basic

/-!
# Internal hom in functor categories

Given functors `F G : C ‚•§ D`, define a functor `functorHom F G` from `C` to `Type max v' v u`,
which is a proxy for the "internal hom" functor Hom(F ‚äó coyoneda(-), G). This is used to show
that the functor category `C ‚•§ D` is enriched over `C ‚•§ Type max v' v u`. This is also useful
for showing that `C ‚•§ Type max w v u` is monoidal closed.

See `Mathlib/CategoryTheory/Closed/FunctorToTypes.lean`.

-/


universe w v' v u u'

open CategoryTheory MonoidalCategory

variable {C : Type u} [Category.{v} C] {D : Type u'} [Category.{v'} D]

variable (F G : C ‚•§ D)

namespace CategoryTheory.Functor

/-- Given functors `F G : C ‚•§ D`, `HomObj F G A` is a proxy for the type
of "morphisms" `F ‚äó A ‚ü∂ G`, where `A : C ‚•§ Type w` (`w` an arbitrary universe). -/
@[ext]
structure HomObj (A : C ‚•§ Type w) where
  /-- The morphism `F.obj c ‚ü∂ G.obj c` associated with `a : A.obj c`. -/
  app (c : C) (a : A.obj c) : F.obj c ‚ü∂ G.obj c
  naturality {c d : C} (f : c ‚ü∂ d) (a : A.obj c) :
    F.map f ‚â´ app d (A.map f a) = app c a ‚â´ G.map f := by cat_disch

/-- When `F`, `G`, and `A` are all functors `C ‚•§ Type w`, then `HomObj F G A` is in
bijection with `F ‚äó A ‚ü∂ G`. -/
@[simps]
def homObjEquiv (F G A : C ‚•§ Type w) : (HomObj F G A) ‚âÉ (F ‚äó A ‚ü∂ G) where
  toFun a := ‚ü®fun X ‚ü®x, y‚ü© ‚Ü¶ a.app X y x, fun X Y f ‚Ü¶ by
    ext ‚ü®x, y‚ü©
    erw [congr_fun (a.naturality f y) x]
    rfl ‚ü©
  invFun a := ‚ü®fun X y x ‚Ü¶ a.app X (x, y), fun œÜ y ‚Ü¶ by
    ext x
    erw [congr_fun (a.naturality œÜ) (x, y)]
    rfl ‚ü©
  left_inv _ := by aesop
  right_inv _ := by aesop

namespace HomObj

attribute [reassoc (attr := simp)] naturality

variable {F G} {A : C ‚•§ Type w}

lemma congr_app {f g : HomObj F G A} (h : f = g) (X : C)
    (a : A.obj X) : f.app X a = g.app X a := by subst h; rfl

/-- Given a natural transformation `F ‚ü∂ G`, get a term of `HomObj F G A` by "ignoring" `A`. -/
@[simps]
def ofNatTrans (f : F ‚ü∂ G) : HomObj F G A where
  app X _ := f.app X

/-- The identity `HomObj F F A`. -/
@[simps!]
def id (A : C ‚•§ Type w) : HomObj F F A := ofNatTrans (ùüô F)

/-- Composition of `f : HomObj F G A` with `g : HomObj G M A`. -/
@[simps]
def comp {M : C ‚•§ D} (f : HomObj F G A) (g : HomObj G M A) : HomObj F M A where
  app X a := f.app X a ‚â´ g.app X a

/-- Given a morphism `A' ‚ü∂ A`, send a term of `HomObj F G A` to a term of `HomObj F G A'`. -/
@[simps]
def map {A' : C ‚•§ Type w} (f : A' ‚ü∂ A) (x : HomObj F G A) : HomObj F G A' where
  app Œî a := x.app Œî (f.app Œî a)
  naturality {Œî Œî'} œÜ a := by
    rw [‚Üê x.naturality œÜ (f.app Œî a), FunctorToTypes.naturality _ _ f œÜ a]

end HomObj

/-- The contravariant functor taking `A : C ‚•§ Type w` to `HomObj F G A`, i.e. Hom(F ‚äó -, G). -/
@[simps]
def homObjFunctor : (C ‚•§ Type w)·µí·µñ ‚•§ Type max w v' u where
  obj A := HomObj F G A.unop
  map {A A'} f x :=
    { app := fun X a ‚Ü¶ x.app X (f.unop.app _ a)
      naturality := fun {X Y} œÜ a ‚Ü¶ by
        rw [‚Üê HomObj.naturality]
        congr 2
        exact congr_fun (f.unop.naturality œÜ) a }

/-- Composition of `homObjFunctor` with the co-Yoneda embedding, i.e. Hom(F ‚äó coyoneda(-), G).
When `F G : C ‚•§ Type max v' v u`, this is the internal hom of `F` and `G`: see
`Mathlib/CategoryTheory/Closed/FunctorToTypes.lean`. -/
def functorHom (F G : C ‚•§ D) : C ‚•§ Type max v' v u := coyoneda.rightOp ‚ãô homObjFunctor.{v} F G

variable {F G} in
@[ext]
lemma functorHom_ext {X : C} {x y : (F.functorHom G).obj X}
    (h : ‚àÄ (Y : C) (f : X ‚ü∂ Y), x.app Y f = y.app Y f) : x = y :=
  HomObj.ext (by ext; apply h)

/-- The equivalence `(A ‚ü∂ F.functorHom G) ‚âÉ HomObj F G A`. -/
@[simps]
def functorHomEquiv (A : C ‚•§ Type max u v v') : (A ‚ü∂ F.functorHom G) ‚âÉ HomObj F G A where
  toFun œÜ :=
    { app := fun X a ‚Ü¶ (œÜ.app X a).app X (ùüô _)
      naturality := fun {X Y} f a ‚Ü¶ by
        rw [‚Üê (œÜ.app X a).naturality f (ùüô _)]
        have := HomObj.congr_app (congr_fun (œÜ.naturality f) a) Y (ùüô _)
        dsimp [functorHom, homObjFunctor] at this
        aesop }
  invFun x :=
    { app := fun X a ‚Ü¶ { app := fun Y f ‚Ü¶ x.app Y (A.map f a) }
      naturality := fun X Y f ‚Ü¶ by
        ext
        dsimp only [types_comp_apply]
        rw [‚Üê FunctorToTypes.map_comp_apply]
        rfl }
  left_inv œÜ := by
    ext X a Y f
    exact (HomObj.congr_app (congr_fun (œÜ.naturality f) a) Y (ùüô _)).trans
      (congr_arg ((œÜ.app X a).app Y) (by simp))
  right_inv x := by simp

variable {F G} in
/-- Morphisms `(ùüô_ (C ‚•§ Type max v' v u) ‚ü∂ F.functorHom G)` are in bijection with
morphisms `F ‚ü∂ G`. -/
@[simps]
def natTransEquiv : (ùüô_ (C ‚•§ Type max v' v u) ‚ü∂ F.functorHom G) ‚âÉ (F ‚ü∂ G) where
  toFun f := ‚ü®fun X ‚Ü¶ (f.app X (PUnit.unit)).app X (ùüô _), by
    intro X Y œÜ
    rw [‚Üê (f.app X (PUnit.unit)).naturality œÜ]
    congr 1
    have := HomObj.congr_app (congr_fun (f.naturality œÜ) PUnit.unit) Y (ùüô Y)
    dsimp [functorHom, homObjFunctor] at this
    aesop ‚ü©
  invFun f := { app _ _ := HomObj.ofNatTrans f }
  left_inv f := by
    ext X a Y œÜ
    have := HomObj.congr_app (congr_fun (f.naturality œÜ) PUnit.unit) Y (ùüô Y)
    dsimp [functorHom, homObjFunctor] at this
    aesop

end CategoryTheory.Functor

open Functor

namespace CategoryTheory.Enriched.Functor

@[simp]
lemma natTransEquiv_symm_app_app_apply (F G : C ‚•§ D) (f : F ‚ü∂ G)
    {X : C} {a : (ùüô_ (C ‚•§ Type (max v' v u))).obj X} (Y : C) {œÜ : X ‚ü∂ Y} :
    ((natTransEquiv.symm f).app X a).app Y œÜ = f.app Y := rfl

@[simp]
lemma natTransEquiv_symm_whiskerRight_functorHom_app (K L : C ‚•§ D) (X : C) (f : K ‚ü∂ K)
    (x : ùüô_ _ ‚äó (K.functorHom L).obj X) :
    ((natTransEquiv.symm f ‚ñ∑ K.functorHom L).app X x) =
    (HomObj.ofNatTrans f, x.2) := rfl

@[simp]
lemma functorHom_whiskerLeft_natTransEquiv_symm_app (K L : C ‚•§ D) (X : C) (f : L ‚ü∂ L)
    (x : (K.functorHom L).obj X ‚äó ùüô_ _) :
    ((K.functorHom L ‚óÅ natTransEquiv.symm f).app X x) =
    (x.1, HomObj.ofNatTrans f) := rfl

@[simp]
lemma whiskerLeft_app_apply (K L M N : C ‚•§ D)
    (g : L.functorHom M ‚äó M.functorHom N ‚ü∂ L.functorHom N)
    {X : C} (a : (K.functorHom L ‚äó L.functorHom M ‚äó M.functorHom N).obj X) :
    (K.functorHom L ‚óÅ g).app X a = ‚ü®a.1, g.app X a.2‚ü© := rfl

@[simp]
lemma whiskerRight_app_apply (K L M N : C ‚•§ D)
    (f : K.functorHom L ‚äó L.functorHom M ‚ü∂ K.functorHom M)
    {X : C} (a : ((K.functorHom L ‚äó L.functorHom M) ‚äó M.functorHom N).obj X) :
    (f ‚ñ∑  M.functorHom N).app X a = ‚ü®f.app X a.1, a.2‚ü© := rfl

@[simp]
lemma associator_inv_apply (K L M N : C ‚•§ D) {X : C}
    (x : ((K.functorHom L) ‚äó (L.functorHom M) ‚äó (M.functorHom N)).obj X) :
    (Œ±_ ((K.functorHom L).obj X) ((L.functorHom M).obj X) ((M.functorHom N).obj X)).inv x =
    ‚ü®‚ü®x.1, x.2.1‚ü©, x.2.2‚ü© := rfl

@[simp]
lemma associator_hom_apply (K L M N : C ‚•§ D) {X : C}
    (x : (((K.functorHom L) ‚äó (L.functorHom M)) ‚äó (M.functorHom N)).obj X) :
    (Œ±_ ((K.functorHom L).obj X) ((L.functorHom M).obj X) ((M.functorHom N).obj X)).hom x =
    ‚ü®x.1.1, x.1.2, x.2‚ü© := rfl

noncomputable instance : EnrichedCategory (C ‚•§ Type max v' v u) (C ‚•§ D) where
  Hom := functorHom
  id F := natTransEquiv.symm (ùüô F)
  comp F G H := { app := fun _ ‚ü®f, g‚ü© ‚Ü¶ f.comp g }

end CategoryTheory.Enriched.Functor
