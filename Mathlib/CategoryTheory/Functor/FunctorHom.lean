/-
Copyright (c) 2024 Jack McKoen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jack McKoen, JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Monoidal.FunctorCategory
import Mathlib.CategoryTheory.Monoidal.Types.Basic

/-!
# Internal hom in functor categories

Given functors `F G : C â¥¤ D`, define a functor `functorHom F G` from `C` to `Type max v' v u`,
which is a proxy for the "internal hom" functor Hom(F âŠ— coyoneda(-), G). This is useful for
showing that the functor category `C â¥¤ D` is enriched over `C â¥¤ Type max v' v u`, and
that `C â¥¤ Type max w v u` is monoidal closed.

See `Mathlib.CategoryTheory.Closed.FunctorToTypes` and `Mathlib.CategoryTheory.Enriched.Functor`.

-/


universe w v' v u u'

open CategoryTheory MonoidalCategory

namespace CategoryTheory.Functor

variable {C : Type u} [Category.{v} C] {D : Type u'} [Category.{v'} D]

variable (F G : C â¥¤ D)

/-- Given functors `F G : C â¥¤ D`, `HomObj F G A` is a proxy for the type
of "morphisms" `F âŠ— A âŸ¶ G`, where `A : C â¥¤ Type w` (`w` an arbitrary universe). -/
@[ext]
structure HomObj (A : C â¥¤ Type w) where
  /-- The morphism `F.obj c âŸ¶ G.obj c` associated with `a : A.obj c`. -/
  app (c : C) (a : A.obj c) : F.obj c âŸ¶ G.obj c
  naturality {c d : C} (f : c âŸ¶ d) (a : A.obj c) :
    F.map f â‰« app d (A.map f a) = app c a â‰« G.map f := by aesop_cat

/-- When `F`, `G`, and `A` are all functors `C â¥¤ Type w`, then `HomObj F G A` is in
bijection with `F âŠ— A âŸ¶ G`. -/
def HomObjEquiv (F G A : C â¥¤ Type w) : (F âŠ— A âŸ¶ G) â‰ƒ (HomObj F G A) where
  toFun a := âŸ¨fun X y x â†¦ a.app X (x, y), fun Ï† y â†¦ by
    ext x
    erw [congr_fun (a.naturality Ï†) (x, y)]
    rfl âŸ©
  invFun a := âŸ¨fun X âŸ¨x, yâŸ© â†¦ a.app X y x, fun X Y f â†¦ by
    ext âŸ¨x, yâŸ©
    erw [congr_fun (a.naturality f y) x]
    rfl âŸ©
  left_inv _ := by aesop
  right_inv _ := by aesop

namespace HomObj

attribute [reassoc (attr := simp)] naturality

variable {F G} {A : C â¥¤ Type w}

lemma congr_app {f g : HomObj F G A} (h : f = g) (X : C)
    (a : A.obj X) : f.app X a = g.app X a := by subst h; rfl

/-- Given a natural transformation `F âŸ¶ G`, get a term of `HomObj F G A` by "ignoring" `A`. -/
@[simps]
def ofNatTrans (f : F âŸ¶ G) : HomObj F G A where
  app X _ := f.app X

/-- The identity `HomObj F F A`. -/
abbrev id (A : C â¥¤ Type w) : HomObj F F A := ofNatTrans (ğŸ™ F)

/-- Composition of `f : HomObj F G A` with `g : HomObj G M A`. -/
@[simps]
def comp {M : C â¥¤ D} (f : HomObj F G A) (g : HomObj G M A) : HomObj F M A where
  app X a := f.app X a â‰« g.app X a

/-- Given a morphism `A' âŸ¶ A`, send a term of `HomObj F G A` to a term of `HomObj F G A'`. -/
@[simps]
def map {A' : C â¥¤ Type w} (f : A' âŸ¶ A) (x : HomObj F G A) : HomObj F G A' where
  app Î” a := x.app Î” (f.app Î” a)
  naturality {Î” Î”'} Ï† a := by
    dsimp
    rw [â† x.naturality Ï† (f.app Î” a), FunctorToTypes.naturality _ _ f Ï† a]

end HomObj

/-- The contravariant functor taking `A : C â¥¤ Type w` to `HomObj F G A`, i.e. Hom(F âŠ— -, G). -/
def HomObjFunctor : (C â¥¤ Type w)áµ’áµ– â¥¤ Type max w v' u where
  obj A := HomObj F G A.unop
  map {A A'} f x :=
    { app := fun X a â†¦ x.app X (f.unop.app _ a)
      naturality := fun {X Y} Ï† a â†¦ by
        dsimp
        rw [â† HomObj.naturality]
        congr 2
        exact congr_fun (f.unop.naturality Ï†) a }

/-- Composition of `HomObjFunctor` with the co-Yoneda embedding, i.e. Hom(F âŠ— coyoneda(-), G).
When `F G : C â¥¤ Type max v' v u`, this is the internal hom of `F` and `G`: see
`Mathlib.CategoryTheory.Closed.FunctorToTypes`. -/
def functorHom (F G : C â¥¤ D) : C â¥¤ Type max v' v u := coyoneda.rightOp â‹™ HomObjFunctor.{v} F G

variable {F G} in
@[ext]
lemma functorHom_ext {X : C} {x y : (F.functorHom G).obj X}
    (h : âˆ€ (Y : C) (f : X âŸ¶ Y), x.app Y f = y.app Y f) : x = y :=
  HomObj.ext (by ext; apply h)

/-- The equivalence `HomObj F G A â‰ƒ (A âŸ¶ F.functorHom G)`. -/
def functorHomEquiv (A : C â¥¤ Type max u v v') : HomObj F G A â‰ƒ (A âŸ¶ F.functorHom G) where
  toFun x :=
    { app := fun X a â†¦ { app := fun Y f => x.app Y (A.map f a) }
      naturality := fun X Y f => by
        ext
        dsimp only [types_comp_apply]
        rw [â† FunctorToTypes.map_comp_apply]
        rfl }
  invFun Ï† :=
    { app := fun X a â†¦ (Ï†.app X a).app X (ğŸ™ _)
      naturality := fun {X Y} f a => by
        rw [â† (Ï†.app X a).naturality f (ğŸ™ _)]
        have := HomObj.congr_app (congr_fun (Ï†.naturality f) a) Y (ğŸ™ _)
        dsimp [functorHom, HomObjFunctor] at this
        aesop }
  left_inv x := by aesop
  right_inv Ï† := by
    ext X a Y f
    exact (HomObj.congr_app (congr_fun (Ï†.naturality f) a) Y (ğŸ™ _)).trans
      (congr_arg ((Ï†.app X a).app Y) (by simp))

variable {F G} in
/-- Morphisms `F âŸ¶ G` are in bijection with
morphisms `(ğŸ™_ (C â¥¤ Type max v' v u) âŸ¶ F.functorHom G)`-/
@[simps]
def natTransEquiv : (F âŸ¶ G) â‰ƒ (ğŸ™_ (C â¥¤ Type max v' v u) âŸ¶ F.functorHom G) where
  toFun f := âŸ¨fun _ _ â†¦ HomObj.ofNatTrans f, _âŸ©
  invFun f := âŸ¨fun X â†¦ (f.app X (PUnit.unit)).app X (ğŸ™ _), by
    intro X Y Ï†
    rw [â† (f.app X (PUnit.unit)).naturality Ï†]
    congr 1
    have := HomObj.congr_app (congr_fun (f.naturality Ï†) PUnit.unit) Y (ğŸ™ Y)
    dsimp [functorHom, HomObjFunctor] at this
    aesop âŸ©
  left_inv _ := rfl
  right_inv f := by
    ext X a Y Ï†
    have := HomObj.congr_app (congr_fun (f.naturality Ï†) PUnit.unit) Y (ğŸ™ Y)
    dsimp [functorHom, HomObjFunctor] at this
    aesop

end CategoryTheory.Functor
