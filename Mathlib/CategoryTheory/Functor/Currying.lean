/-
Copyright (c) 2017 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.EqToHom
import Mathlib.CategoryTheory.Products.Basic

/-!
# Curry and uncurry, as functors.

We define `curry : ((C Ã— D) â¥¤ E) â¥¤ (C â¥¤ (D â¥¤ E))` and `uncurry : (C â¥¤ (D â¥¤ E)) â¥¤ ((C Ã— D) â¥¤ E)`,
and verify that they provide an equivalence of categories
`currying : (C â¥¤ (D â¥¤ E)) â‰Œ ((C Ã— D) â¥¤ E)`.

-/


namespace CategoryTheory

universe vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ vâ‚… uâ‚ uâ‚‚ uâ‚ƒ uâ‚„ uâ‚…

variable {B : Type uâ‚} [Category.{vâ‚} B] {C : Type uâ‚‚} [Category.{vâ‚‚} C] {D : Type uâ‚ƒ}
  [Category.{vâ‚ƒ} D] {E : Type uâ‚„} [Category.{vâ‚„} E] {H : Type uâ‚…} [Category.{vâ‚…} H]

/-- The uncurrying functor, taking a functor `C â¥¤ (D â¥¤ E)` and producing a functor `(C Ã— D) â¥¤ E`.
-/
@[simps]
def uncurry : (C â¥¤ D â¥¤ E) â¥¤ C Ã— D â¥¤ E where
  obj F :=
    { obj := fun X => (F.obj X.1).obj X.2
      map := fun {X} {Y} f => (F.map f.1).app X.2 â‰« (F.obj Y.1).map f.2
      map_comp := fun f g => by
        simp only [prod_comp_fst, prod_comp_snd, Functor.map_comp, NatTrans.comp_app,
          Category.assoc]
        slice_lhs 2 3 => rw [â† NatTrans.naturality]
        rw [Category.assoc] }
  map T :=
    { app := fun X => (T.app X.1).app X.2
      naturality := fun X Y f => by
        simp only [prod_comp_fst, prod_comp_snd, Category.comp_id, Category.assoc, Functor.map_id,
          Functor.map_comp, NatTrans.id_app, NatTrans.comp_app]
        slice_lhs 2 3 => rw [NatTrans.naturality]
        slice_lhs 1 2 => rw [â† NatTrans.comp_app, NatTrans.naturality, NatTrans.comp_app]
        rw [Category.assoc] }

/-- The object level part of the currying functor. (See `curry` for the functorial version.)
-/
def curryObj (F : C Ã— D â¥¤ E) : C â¥¤ D â¥¤ E where
  obj X :=
    { obj := fun Y => F.obj (X, Y)
      map := fun g => F.map (ðŸ™ X, g)
      map_id := fun Y => by simp only [F.map_id]; rw [â† prod_id]; exact F.map_id âŸ¨X,YâŸ©
      map_comp := fun f g => by simp [â† F.map_comp]}
  map f :=
    { app := fun Y => F.map (f, ðŸ™ Y)
      naturality := fun {Y} {Y'} g => by simp [â† F.map_comp] }
  map_id := fun X => by ext Y; exact F.map_id _
  map_comp := fun f g => by ext Y; simp [â† F.map_comp]

/-- The currying functor, taking a functor `(C Ã— D) â¥¤ E` and producing a functor `C â¥¤ (D â¥¤ E)`.
-/
@[simps! obj_obj_obj obj_obj_map obj_map_app map_app_app]
def curry : (C Ã— D â¥¤ E) â¥¤ C â¥¤ D â¥¤ E where
  obj F := curryObj F
  map T :=
    { app := fun X =>
        { app := fun Y => T.app (X, Y)
          naturality := fun Y Y' g => by
            dsimp [curryObj]
            rw [NatTrans.naturality] }
      naturality := fun X X' f => by
        ext; dsimp [curryObj]
        rw [NatTrans.naturality] }

-- create projection simp lemmas even though this isn't a `{ .. }`.
/-- The equivalence of functor categories given by currying/uncurrying.
-/
@[simps!]
def currying : C â¥¤ D â¥¤ E â‰Œ C Ã— D â¥¤ E where
  functor := uncurry
  inverse := curry
  unitIso := NatIso.ofComponents (fun _ â†¦ NatIso.ofComponents
    (fun _ â†¦ NatIso.ofComponents (fun _ â†¦ Iso.refl _)))
  counitIso := NatIso.ofComponents
    (fun F â†¦ NatIso.ofComponents (fun _ â†¦ Iso.refl _) (by
      rintro âŸ¨Xâ‚, Xâ‚‚âŸ© âŸ¨Yâ‚, Yâ‚‚âŸ© âŸ¨fâ‚, fâ‚‚âŸ©
      dsimp at fâ‚ fâ‚‚ âŠ¢
      simp only [â† F.map_comp, prod_comp, Category.comp_id, Category.id_comp]))

/-- The functor `uncurry : (C â¥¤ D â¥¤ E) â¥¤ C Ã— D â¥¤ E` is fully faithful. -/
def fullyFaithfulUncurry : (uncurry : (C â¥¤ D â¥¤ E) â¥¤ C Ã— D â¥¤ E).FullyFaithful :=
  currying.fullyFaithfulFunctor

instance : (uncurry : (C â¥¤ D â¥¤ E) â¥¤ C Ã— D â¥¤ E).Full :=
  fullyFaithfulUncurry.full

instance : (uncurry : (C â¥¤ D â¥¤ E) â¥¤ C Ã— D â¥¤ E).Faithful :=
  fullyFaithfulUncurry.faithful

/-- The functor `curry : (C Ã— D â¥¤ E) â¥¤ (C â¥¤ D â¥¤ E)` is fully faithful. -/
def fullyFaithfulCurry : (curry : (C Ã— D â¥¤ E) â¥¤ (C â¥¤ D â¥¤ E)).FullyFaithful :=
  currying.fullyFaithfulInverse

/-- Given functors `Fâ‚ : C â¥¤ D`, `Fâ‚‚ : C' â¥¤ D'` and `G : D Ã— D' â¥¤ E`, this is the isomorphism
between `curry.obj ((Fâ‚.prod Fâ‚‚).comp G)` and
`Fâ‚ â‹™ curry.obj G â‹™ (whiskeringLeft C' D' E).obj Fâ‚‚` in the category `C â¥¤ C' â¥¤ E`. -/
@[simps!]
def curryObjProdComp {C' D' : Type*} [Category C'] [Category D']
    (Fâ‚ : C â¥¤ D) (Fâ‚‚ : C' â¥¤ D') (G : D Ã— D' â¥¤ E) :
    curry.obj ((Fâ‚.prod Fâ‚‚).comp G) â‰…
      Fâ‚ â‹™ curry.obj G â‹™ (whiskeringLeft C' D' E).obj Fâ‚‚ :=
  NatIso.ofComponents (fun Xâ‚ â†¦ NatIso.ofComponents (fun Xâ‚‚ â†¦ Iso.refl _))

/-- `F.flip` is isomorphic to uncurrying `F`, swapping the variables, and currying. -/
@[simps!]
def flipIsoCurrySwapUncurry (F : C â¥¤ D â¥¤ E) : F.flip â‰… curry.obj (Prod.swap _ _ â‹™ uncurry.obj F) :=
  NatIso.ofComponents fun d => NatIso.ofComponents fun _ => Iso.refl _

/-- The uncurrying of `F.flip` is isomorphic to
swapping the factors followed by the uncurrying of `F`. -/
@[simps!]
def uncurryObjFlip (F : C â¥¤ D â¥¤ E) : uncurry.obj F.flip â‰… Prod.swap _ _ â‹™ uncurry.obj F :=
  NatIso.ofComponents fun _ => Iso.refl _

variable (B C D E)

/-- A version of `CategoryTheory.whiskeringRight` for bifunctors, obtained by uncurrying,
applying `whiskeringRight` and currying back
-/
@[simps!]
def whiskeringRightâ‚‚ : (C â¥¤ D â¥¤ E) â¥¤ (B â¥¤ C) â¥¤ (B â¥¤ D) â¥¤ B â¥¤ E :=
  uncurry â‹™
    whiskeringRight _ _ _ â‹™ (whiskeringLeft _ _ _).obj (prodFunctorToFunctorProd _ _ _) â‹™ curry

namespace Functor

variable {B C D E}

lemma uncurry_obj_curry_obj (F : B Ã— C â¥¤ D) : uncurry.obj (curry.obj F) = F :=
  Functor.ext (by simp) (fun âŸ¨xâ‚, xâ‚‚âŸ© âŸ¨yâ‚, yâ‚‚âŸ© âŸ¨fâ‚, fâ‚‚âŸ© => by
    dsimp
    simp only [â† F.map_comp, Category.id_comp, Category.comp_id, prod_comp])

lemma curry_obj_injective {Fâ‚ Fâ‚‚ : C Ã— D â¥¤ E} (h : curry.obj Fâ‚ = curry.obj Fâ‚‚) :
    Fâ‚ = Fâ‚‚ := by
  rw [â† uncurry_obj_curry_obj Fâ‚, â† uncurry_obj_curry_obj Fâ‚‚, h]

lemma curry_obj_uncurry_obj (F : B â¥¤ C â¥¤ D) : curry.obj (uncurry.obj F) = F :=
  Functor.ext (fun _ => Functor.ext (by simp) (by simp)) (by aesop_cat)

lemma uncurry_obj_injective {Fâ‚ Fâ‚‚ : B â¥¤ C â¥¤ D} (h : uncurry.obj Fâ‚ = uncurry.obj Fâ‚‚) :
    Fâ‚ = Fâ‚‚ := by
  rw [â† curry_obj_uncurry_obj Fâ‚, â† curry_obj_uncurry_obj Fâ‚‚, h]

lemma flip_flip (F : B â¥¤ C â¥¤ D) : F.flip.flip = F := rfl

lemma flip_injective {Fâ‚ Fâ‚‚ : B â¥¤ C â¥¤ D} (h : Fâ‚.flip = Fâ‚‚.flip) :
    Fâ‚ = Fâ‚‚ := by
  rw [â† flip_flip Fâ‚, â† flip_flip Fâ‚‚, h]

lemma uncurry_obj_curry_obj_flip_flip (Fâ‚ : B â¥¤ C) (Fâ‚‚ : D â¥¤ E) (G : C Ã— E â¥¤ H) :
    uncurry.obj (Fâ‚‚ â‹™ (Fâ‚ â‹™ curry.obj G).flip).flip = (Fâ‚.prod Fâ‚‚) â‹™ G :=
  Functor.ext (by simp) (fun âŸ¨xâ‚, xâ‚‚âŸ© âŸ¨yâ‚, yâ‚‚âŸ© âŸ¨fâ‚, fâ‚‚âŸ© => by
    dsimp
    simp only [Category.id_comp, Category.comp_id, â† G.map_comp, prod_comp])

lemma uncurry_obj_curry_obj_flip_flip' (Fâ‚ : B â¥¤ C) (Fâ‚‚ : D â¥¤ E) (G : C Ã— E â¥¤ H) :
    uncurry.obj (Fâ‚ â‹™ (Fâ‚‚ â‹™ (curry.obj G).flip).flip) = (Fâ‚.prod Fâ‚‚) â‹™ G :=
  Functor.ext (by simp) (fun âŸ¨xâ‚, xâ‚‚âŸ© âŸ¨yâ‚, yâ‚‚âŸ© âŸ¨fâ‚, fâ‚‚âŸ© => by
    dsimp
    simp only [Category.id_comp, Category.comp_id, â† G.map_comp, prod_comp])

variable {Câ‚ : Type uâ‚} {Câ‚‚ : Type uâ‚‚} {D : Type uâ‚ƒ}
  [Category.{vâ‚} Câ‚] [Category.{vâ‚‚} Câ‚‚] [Category.{vâ‚ƒ} D]

@[simps!]
def curryObjUncurryObjIso (F : Câ‚ â¥¤ Câ‚‚ â¥¤ D) : curry.obj (uncurry.obj F) â‰… F :=
  NatIso.ofComponents (fun Xâ‚ => NatIso.ofComponents (fun Xâ‚‚ => Iso.refl _) (by aesop_cat))
    (by aesop_cat)

end Functor

end CategoryTheory
