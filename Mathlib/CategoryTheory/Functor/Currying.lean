/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Products.Associator

#align_import category_theory.functor.currying from "leanprover-community/mathlib"@"369525b73f229ccd76a6ec0e0e0bf2be57599768"

/-!
# Curry and uncurry, as functors.

We define `curry : ((C √ó D) ‚•§ E) ‚•§ (C ‚•§ (D ‚•§ E))` and `uncurry : (C ‚•§ (D ‚•§ E)) ‚•§ ((C √ó D) ‚•§ E)`,
and verify that they provide an equivalence of categories
`currying : (C ‚•§ (D ‚•§ E)) ‚âå ((C √ó D) ‚•§ E)`.

-/


namespace CategoryTheory

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ v‚ÇÑ v‚ÇÖ u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ u‚ÇÖ

variable {B : Type u‚ÇÅ} [Category.{v‚ÇÅ} B] {C : Type u‚ÇÇ} [Category.{v‚ÇÇ} C] {D : Type u‚ÇÉ}
  [Category.{v‚ÇÉ} D] {E : Type u‚ÇÑ} [Category.{v‚ÇÑ} E] {H : Type u‚ÇÖ} [Category.{v‚ÇÖ} H]

/-- The action on objects of a bifunctor. -/
def Functor.obj‚ÇÇ (F : B ‚•§ C ‚•§ D) (X : B) (Y : C) : D :=
  (F.obj X).obj Y

/-- The component of a morphism of bifunctors at two objects. -/
def NatTrans.app‚ÇÇ {F G : B ‚•§ C ‚•§ D} (Œ± : NatTrans F G) (X : B) (Y : C) :
    F.obj‚ÇÇ X Y ‚ü∂ G.obj‚ÇÇ X Y := (Œ±.app X).app Y

/-- The action on morphisms of a bifunctor. -/
def Functor.map‚ÇÇ (F : B ‚•§ C ‚•§ D) {X X' : B} {Y Y' : C} (f : X ‚ü∂ X') (g : Y ‚ü∂ Y') :
    F.obj‚ÇÇ X Y ‚ü∂ F.obj‚ÇÇ X' Y' :=
  (F.map f).app Y ‚â´ (F.obj X').map g

/-- The uncurrying functor, taking a functor `C ‚•§ (D ‚•§ E)` and producing a functor `(C √ó D) ‚•§ E`.
-/
@[simps]
def uncurry : (C ‚•§ D ‚•§ E) ‚•§ C √ó D ‚•§ E
    where
  obj F :=
    { obj := fun X => (F.obj X.1).obj X.2
      map := fun {X} {Y} f => (F.map f.1).app X.2 ‚â´ (F.obj Y.1).map f.2
      map_comp := fun f g => by
        simp only [prod_comp_fst, prod_comp_snd, Functor.map_comp, NatTrans.comp_app,
          Category.assoc]
        slice_lhs 2 3 => rw [‚Üê NatTrans.naturality]
        rw [Category.assoc] }
  map T :=
    { app := fun X => (T.app X.1).app X.2
      naturality := fun X Y f => by
        simp only [prod_comp_fst, prod_comp_snd, Category.comp_id, Category.assoc, Functor.map_id,
          Functor.map_comp, NatTrans.id_app, NatTrans.comp_app]
        slice_lhs 2 3 => rw [NatTrans.naturality]
        slice_lhs 1 2 => rw [‚Üê NatTrans.comp_app, NatTrans.naturality, NatTrans.comp_app]
        rw [Category.assoc] }
#align category_theory.uncurry CategoryTheory.uncurry

lemma uncurry_obj_obj_eq_uncurry_obj‚ÇÇ (F : C ‚•§ D ‚•§ E) :
    (uncurry.obj F).obj = F.obj‚ÇÇ.uncurry := rfl
lemma uncurry_obj_map_eq_uncurry_map‚ÇÇ (F : C ‚•§ D ‚•§ E) {X Y : C √ó D} :
    ((uncurry.obj F).map : (X ‚ü∂ Y) ‚Üí _) = F.map‚ÇÇ.uncurry := rfl

/-- The object level part of the currying functor. (See `curry` for the functorial version.)
-/
def curryObj (F : C √ó D ‚•§ E) : C ‚•§ D ‚•§ E
    where
  obj X :=
    { obj := fun Y => F.obj (X, Y)
      map := fun g => F.map (ùüô X, g)
      map_id := fun Y => by simp only [F.map_id]; rw [‚Üê prod_id]; exact F.map_id ‚ü®X,Y‚ü©
      map_comp := fun f g => by simp [‚Üê F.map_comp]}
  map f :=
    { app := fun Y => F.map (f, ùüô Y)
      naturality := fun {Y} {Y'} g => by simp [‚Üê F.map_comp] }
  map_id := fun X => by ext Y; exact F.map_id _
  map_comp := fun f g => by ext Y; dsimp; simp [‚Üê F.map_comp]
#align category_theory.curry_obj CategoryTheory.curryObj

/-- The currying functor, taking a functor `(C √ó D) ‚•§ E` and producing a functor `C ‚•§ (D ‚•§ E)`.
-/
@[simps! obj_obj_obj obj_obj_map obj_map_app map_app_app]
def curry : (C √ó D ‚•§ E) ‚•§ C ‚•§ D ‚•§ E where
  obj F := curryObj F
  map T :=
    { app := fun X =>
        { app := fun Y => T.app (X, Y)
          naturality := fun Y Y' g => by
            dsimp [curryObj]
            rw [NatTrans.naturality] }
      naturality := fun X X' f => by
        ext; dsimp [curryObj]
        rw [NatTrans.naturality] }
#align category_theory.curry CategoryTheory.curry

lemma curry_obj_obj‚ÇÇ_eq_curry_obj (F : C √ó D ‚•§ E) :
    (curry.obj F).obj‚ÇÇ = F.obj.curry := rfl

lemma curry_obj_map‚ÇÇ_eq_curry_map‚ÇÇ (F : C √ó D ‚•§ E) {X X' Y Y'} :
    (curry.obj F).map‚ÇÇ = ((F.map (X := (X, Y)) (Y := (X', Y'))).curry) :=
  funext‚ÇÇ $ fun f g => Eq.trans (F.map_comp _ _).symm $ congrArg _
  $ congrArg‚ÇÇ _ (Category.comp_id f) (Category.id_comp g)

-- create projection simp lemmas even though this isn't a `{ .. }`.
/-- The equivalence of functor categories given by currying/uncurrying.
-/
@[simps!]
def currying : C ‚•§ D ‚•§ E ‚âå C √ó D ‚•§ E :=
  let Œ∑ : ùü≠ (C ‚•§ D ‚•§ E) ‚âÖ uncurry ‚ãô curry :=
    NatIso.ofComponents fun F =>
      NatIso.ofComponents fun X => NatIso.ofComponents fun Y => Iso.refl _
  let Œµ : curry ‚ãô uncurry ‚âÖ ùü≠ (C √ó D ‚•§ E) :=
    NatIso.ofComponents fun F => NatIso.ofComponents (fun X => eqToIso (by simp))
      (by intros X Y f; cases X; cases Y; cases f; dsimp at *; rw [‚Üê F.map_comp]; simp)
  have H (F : C ‚•§ D ‚•§ E) :
      uncurry.map (Œ∑.hom.app F) ‚â´ Œµ.hom.app (uncurry.obj F) = ùüô (uncurry.obj F) :=
    by ext; exact Category.comp_id _
  Equivalence.mk' uncurry curry Œ∑ Œµ H
#align category_theory.currying CategoryTheory.currying

/-- `F.flip` is isomorphic to uncurrying `F`, swapping the variables, and currying. -/
@[simps!]
def flipIsoCurrySwapUncurry (F : C ‚•§ D ‚•§ E) : F.flip ‚âÖ curry.obj (Prod.swap _ _ ‚ãô uncurry.obj F) :=
  NatIso.ofComponents fun d => NatIso.ofComponents fun c => Iso.refl _
#align category_theory.flip_iso_curry_swap_uncurry CategoryTheory.flipIsoCurrySwapUncurry

/-- The uncurrying of `F.flip` is isomorphic to
swapping the factors followed by the uncurrying of `F`. -/
@[simps!]
def uncurryObjFlip (F : C ‚•§ D ‚•§ E) : uncurry.obj F.flip ‚âÖ Prod.swap _ _ ‚ãô uncurry.obj F :=
  NatIso.ofComponents fun p => Iso.refl _
#align category_theory.uncurry_obj_flip CategoryTheory.uncurryObjFlip

variable (B C D E)

/-- A version of `CategoryTheory.whiskeringRight` for bifunctors, obtained by uncurrying,
applying `whiskeringRight` and currying back
-/
@[simps!]
def whiskeringRight‚ÇÇ : (C ‚•§ D ‚•§ E) ‚•§ (B ‚•§ C) ‚•§ (B ‚•§ D) ‚•§ B ‚•§ E :=
  uncurry ‚ãô
    whiskeringRight _ _ _ ‚ãô (whiskeringLeft _ _ _).obj (prodFunctorToFunctorProd _ _ _) ‚ãô curry
#align category_theory.whiskering_right‚ÇÇ CategoryTheory.whiskeringRight‚ÇÇ

namespace Functor

variable {B C D E}

lemma uncurry_obj_curry_obj (F : B √ó C ‚•§ D) : uncurry.obj (curry.obj F) = F :=
  Functor.ext (by simp) (fun ‚ü®x‚ÇÅ, x‚ÇÇ‚ü© ‚ü®y‚ÇÅ, y‚ÇÇ‚ü© ‚ü®f‚ÇÅ, f‚ÇÇ‚ü© => by
    dsimp
    simp only [‚Üê F.map_comp, Category.id_comp, Category.comp_id, prod_comp])

lemma curry_obj_injective {F‚ÇÅ F‚ÇÇ : C √ó D ‚•§ E} (h : curry.obj F‚ÇÅ = curry.obj F‚ÇÇ) :
    F‚ÇÅ = F‚ÇÇ := by
  rw [‚Üê uncurry_obj_curry_obj F‚ÇÅ, ‚Üê uncurry_obj_curry_obj F‚ÇÇ, h]

lemma curry_obj_uncurry_obj (F : B ‚•§ C ‚•§ D) : curry.obj (uncurry.obj F) = F :=
  Functor.ext (fun _ => Functor.ext (by simp) (by simp)) (by aesop_cat)

lemma uncurry_obj_injective {F‚ÇÅ F‚ÇÇ : B ‚•§ C ‚•§ D} (h : uncurry.obj F‚ÇÅ = uncurry.obj F‚ÇÇ) :
    F‚ÇÅ = F‚ÇÇ := by
  rw [‚Üê curry_obj_uncurry_obj F‚ÇÅ, ‚Üê curry_obj_uncurry_obj F‚ÇÇ, h]

lemma flip_flip (F : B ‚•§ C ‚•§ D) : F.flip.flip = F := rfl

lemma flip_injective {F‚ÇÅ F‚ÇÇ : B ‚•§ C ‚•§ D} (h : F‚ÇÅ.flip = F‚ÇÇ.flip) :
    F‚ÇÅ = F‚ÇÇ := by
  rw [‚Üê flip_flip F‚ÇÅ, ‚Üê flip_flip F‚ÇÇ, h]

lemma uncurry_obj_curry_obj_flip_flip (F‚ÇÅ : B ‚•§ C) (F‚ÇÇ : D ‚•§ E) (G : C √ó E ‚•§ H) :
    uncurry.obj (F‚ÇÇ ‚ãô (F‚ÇÅ ‚ãô curry.obj G).flip).flip = (F‚ÇÅ.prod F‚ÇÇ) ‚ãô G :=
  Functor.ext (by simp) (fun ‚ü®x‚ÇÅ, x‚ÇÇ‚ü© ‚ü®y‚ÇÅ, y‚ÇÇ‚ü© ‚ü®f‚ÇÅ, f‚ÇÇ‚ü© => by
    dsimp
    simp only [Category.id_comp, Category.comp_id, ‚Üê G.map_comp, prod_comp])

lemma uncurry_obj_curry_obj_flip_flip' (F‚ÇÅ : B ‚•§ C) (F‚ÇÇ : D ‚•§ E) (G : C √ó E ‚•§ H) :
    uncurry.obj (F‚ÇÅ ‚ãô (F‚ÇÇ ‚ãô (curry.obj G).flip).flip) = (F‚ÇÅ.prod F‚ÇÇ) ‚ãô G :=
  Functor.ext (by simp) (fun ‚ü®x‚ÇÅ, x‚ÇÇ‚ü© ‚ü®y‚ÇÅ, y‚ÇÇ‚ü© ‚ü®f‚ÇÅ, f‚ÇÇ‚ü© => by
    dsimp
    simp only [Category.id_comp, Category.comp_id, ‚Üê G.map_comp, prod_comp])

end Functor

end CategoryTheory
