/-
Copyright (c) 2017 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Tim Baumann, Stephen Morgan, Kim Morrison
-/
module

public import Mathlib.CategoryTheory.Category.Basic
public import Mathlib.Combinatorics.Quiver.Prefunctor
public import Mathlib.Tactic.CategoryTheory.CheckCompositions

/-!
# Functors

Defines a functor between categories, extending a `Prefunctor` between quivers.

Introduces, in the `CategoryTheory` scope, notations `C ‚•§ D` for the type of all functors
from `C` to `D`, `ùü≠` for the identity functor and `‚ãô` for functor composition.

TODO: Switch to using the `‚áí` arrow.
-/

@[expose] public section

set_option mathlib.tactic.category.grind true

namespace CategoryTheory

-- declare the `v`'s first; see note [category theory universes].
universe v v‚ÇÅ v‚ÇÇ v‚ÇÉ u u‚ÇÅ u‚ÇÇ u‚ÇÉ

section

/-- `Functor C D` represents a functor between categories `C` and `D`.

To apply a functor `F` to an object use `F.obj X`, and to a morphism use `F.map f`.

The axiom `map_id` expresses preservation of identities, and
`map_comp` expresses functoriality. -/
@[stacks 001B]
structure Functor (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] (D : Type u‚ÇÇ) [Category.{v‚ÇÇ} D] :
    Type max v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ where
  /-- The action of a functor on objects. -/
  obj : C ‚Üí D
  /-- The action of a functor on morphisms. -/
  map : ‚àÄ {X Y : C}, (X ‚ü∂ Y) ‚Üí ((obj X) ‚ü∂ (obj Y))
  /-- A functor preserves identity morphisms. -/
  map_id : ‚àÄ X : C, map (ùüô X) = ùüô (obj X) := by cat_disch
  /-- A functor preserves composition. -/
  map_comp : ‚àÄ {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z), map (f ‚â´ g) = map f ‚â´ map g := by cat_disch

end

/-- Notation for a functor between categories. -/
-- A functor is basically a function, so give ‚•§ a similar precedence to ‚Üí (25).
-- For example, `C √ó D ‚•§ E` should parse as `(C √ó D) ‚•§ E` not `C √ó (D ‚•§ E)`.
scoped[CategoryTheory] infixr:26 " ‚•§ " => Functor -- type as \func

attribute [simp] Functor.map_id Functor.map_comp
attribute [grind =] Functor.map_id
attribute [grind _=_] Functor.map_comp

-- Note: We manually add this lemma which could be generated by `reassoc`,
-- since we will import this file into `Mathlib/Tactic/Reassoc.lean`.
lemma Functor.map_comp_assoc {C : Type u‚ÇÅ} [Category C] {D : Type u‚ÇÇ} [Category D] (F : C ‚•§ D)
    {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) {W : D} (h : F.obj Z ‚ü∂ W) :
    (F.map (f ‚â´ g)) ‚â´ h = F.map f ‚â´ F.map g ‚â´ h := by
  grind

namespace Functor

section

variable (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C]

initialize_simps_projections Functor

-- We don't use `@[simps]` here because we want `C` implicit for the simp lemmas.
/-- `ùü≠ C` is the identity functor on a category `C`. -/
protected def id : C ‚•§ C where
  obj X := X
  map f := f

/-- Notation for the identity functor on a category. -/
scoped[CategoryTheory] notation "ùü≠" => Functor.id -- Type this as `\sb1`

instance : Inhabited (C ‚•§ C) :=
  ‚ü®Functor.id C‚ü©

variable {C}

@[simp, grind =]
theorem id_obj (X : C) : (ùü≠ C).obj X = X := rfl

@[simp, grind =]
theorem id_map {X Y : C} (f : X ‚ü∂ Y) : (ùü≠ C).map f = f := rfl

end

section

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
  {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E]

/-- The prefunctor between the underlying quivers. -/
@[simps]
def toPrefunctor (F : C ‚•§ D) : Prefunctor C D := { F with }

theorem congr_map (F : C ‚•§ D) {X Y : C} {f g : X ‚ü∂ Y}
    (h : f = g) : F.map f = F.map g := by
  rw [h]

/-- `F ‚ãô G` is the composition of a functor `F` and a functor `G` (`F` first, then `G`).
-/
@[simps (attr := grind =) obj]
def comp (F : C ‚•§ D) (G : D ‚•§ E) : C ‚•§ E where
  obj X := G.obj (F.obj X)
  map f := G.map (F.map f)

/-- Notation for composition of functors. -/
scoped[CategoryTheory] infixr:80 " ‚ãô " => Functor.comp

@[simp, grind =]
theorem comp_map (F : C ‚•§ D) (G : D ‚•§ E) {X Y : C} (f : X ‚ü∂ Y) :
    (F ‚ãô G).map f = G.map (F.map f) := rfl

-- These are not simp lemmas because rewriting along equalities between functors
-- is not necessarily a good idea.
-- Natural isomorphisms are also provided in `Whiskering.lean`.
protected theorem comp_id (F : C ‚•§ D) : F ‚ãô ùü≠ D = F := by cases F; rfl

protected theorem id_comp (F : C ‚•§ D) : ùü≠ C ‚ãô F = F := by cases F; rfl

@[simp]
theorem map_dite (F : C ‚•§ D) {X Y : C} {P : Prop} [Decidable P]
    (f : P ‚Üí (X ‚ü∂ Y)) (g : ¬¨P ‚Üí (X ‚ü∂ Y)) :
    F.map (if h : P then f h else g h) = if h : P then F.map (f h) else F.map (g h) := by
  cat_disch

@[simp]
theorem toPrefunctor_comp (F : C ‚•§ D) (G : D ‚•§ E) :
    F.toPrefunctor.comp G.toPrefunctor = (F ‚ãô G).toPrefunctor := rfl

lemma toPrefunctor_injective {F G : C ‚•§ D} (h : F.toPrefunctor = G.toPrefunctor) :
    F = G := by
  obtain ‚ü®obj, map, _, _‚ü© := F
  obtain ‚ü®obj', map', _, _‚ü© := G
  obtain rfl : obj = obj' := congr_arg Prefunctor.obj h
  obtain rfl : @map = @map' := by simpa [Functor.toPrefunctor] using h
  rfl

end

end Functor

end CategoryTheory
