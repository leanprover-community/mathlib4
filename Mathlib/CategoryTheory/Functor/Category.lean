/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Tim Baumann, Stephen Morgan, Scott Morrison, Floris van Doorn
-/
import Mathlib.CategoryTheory.NatTrans
import Mathlib.CategoryTheory.Iso

/-!
# The category of functors and natural transformations between two fixed categories.

We provide the category instance on `C â¥¤ D`, with morphisms the natural transformations.

## Universes

If `C` and `D` are both small categories at the same universe level,
this is another small category at that level.
However if `C` and `D` are both large categories at the same universe level,
this is a small category at the next higher level.
-/

namespace CategoryTheory

-- declare the `v`'s first; see note [CategoryTheory universes].
universe vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

open NatTrans Category CategoryTheory.Functor

variable (C : Type uâ‚) [Category.{vâ‚} C] (D : Type uâ‚‚) [Category.{vâ‚‚} D]

attribute [local simp] vcomp_app

variable {C D} {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E]
variable {F G H I : C â¥¤ D}

/-- `Functor.category C D` gives the category structure on functors and natural transformations
between categories `C` and `D`.

Notice that if `C` and `D` are both small categories at the same universe level,
this is another small category at that level.
However if `C` and `D` are both large categories at the same universe level,
this is a small category at the next higher level.
-/
instance Functor.category : Category.{max uâ‚ vâ‚‚} (C â¥¤ D) where
  Hom F G := NatTrans F G
  id F := NatTrans.id F
  comp Î± Î² := vcomp Î± Î²

namespace NatTrans

-- Porting note: the behaviour of `ext` has changed here.
-- We need to provide a copy of the `NatTrans.ext` lemma,
-- written in terms of `F âŸ¶ G` rather than `NatTrans F G`,
-- or `ext` will not retrieve it from the cache.
@[ext]
theorem ext' {Î± Î² : F âŸ¶ G} (w : Î±.app = Î².app) : Î± = Î² := NatTrans.ext w

@[simp]
theorem vcomp_eq_comp (Î± : F âŸ¶ G) (Î² : G âŸ¶ H) : vcomp Î± Î² = Î± â‰« Î² := rfl

theorem vcomp_app' (Î± : F âŸ¶ G) (Î² : G âŸ¶ H) (X : C) : (Î± â‰« Î²).app X = Î±.app X â‰« Î².app X := rfl

theorem congr_app {Î± Î² : F âŸ¶ G} (h : Î± = Î²) (X : C) : Î±.app X = Î².app X := by rw [h]

@[simp]
theorem id_app (F : C â¥¤ D) (X : C) : (ğŸ™ F : F âŸ¶ F).app X = ğŸ™ (F.obj X) := rfl

@[simp]
theorem comp_app {F G H : C â¥¤ D} (Î± : F âŸ¶ G) (Î² : G âŸ¶ H) (X : C) :
    (Î± â‰« Î²).app X = Î±.app X â‰« Î².app X := rfl

attribute [reassoc] comp_app

@[reassoc]
theorem app_naturality {F G : C â¥¤ D â¥¤ E} (T : F âŸ¶ G) (X : C) {Y Z : D} (f : Y âŸ¶ Z) :
    (F.obj X).map f â‰« (T.app X).app Z = (T.app X).app Y â‰« (G.obj X).map f :=
  (T.app X).naturality f

@[reassoc]
theorem naturality_app {F G : C â¥¤ D â¥¤ E} (T : F âŸ¶ G) (Z : D) {X Y : C} (f : X âŸ¶ Y) :
    (F.map f).app Z â‰« (T.app Y).app Z = (T.app X).app Z â‰« (G.map f).app Z :=
  congr_fun (congr_arg app (T.naturality f)) Z

/-- A natural transformation is a monomorphism if each component is. -/
theorem mono_of_mono_app (Î± : F âŸ¶ G) [âˆ€ X : C, Mono (Î±.app X)] : Mono Î± :=
  âŸ¨fun g h eq => by
    ext X
    rw [â† cancel_mono (Î±.app X), â† comp_app, eq, comp_app]âŸ©

/-- A natural transformation is an epimorphism if each component is. -/
theorem epi_of_epi_app (Î± : F âŸ¶ G) [âˆ€ X : C, Epi (Î±.app X)] : Epi Î± :=
  âŸ¨fun g h eq => by
    ext X
    rw [â† cancel_epi (Î±.app X), â† comp_app, eq, comp_app]âŸ©

/-- The monoid of natural transformations of the identity is commutative.-/
lemma id_comm (Î± Î² : (ğŸ­ C) âŸ¶ (ğŸ­ C)) : Î± â‰« Î² = Î² â‰« Î± := by
  ext X
  exact (Î±.naturality (Î².app X)).symm

/-- `hcomp Î± Î²` is the horizontal composition of natural transformations. -/
@[simps]
def hcomp {H I : D â¥¤ E} (Î± : F âŸ¶ G) (Î² : H âŸ¶ I) : F â‹™ H âŸ¶ G â‹™ I where
  app := fun X : C => Î².app (F.obj X) â‰« I.map (Î±.app X)
  naturality X Y f := by
    rw [Functor.comp_map, Functor.comp_map, â† assoc, naturality, assoc, â† map_comp I, naturality,
      map_comp, assoc]

/-- Notation for horizontal composition of natural transformations. -/
infixl:80 " â—« " => hcomp

theorem hcomp_id_app {H : D â¥¤ E} (Î± : F âŸ¶ G) (X : C) : (Î± â—« ğŸ™ H).app X = H.map (Î±.app X) := by
  simp

theorem id_hcomp_app {H : E â¥¤ C} (Î± : F âŸ¶ G) (X : E) : (ğŸ™ H â—« Î±).app X = Î±.app _ := by simp

-- Note that we don't yet prove a `hcomp_assoc` lemma here: even stating it is painful, because we
-- need to use associativity of functor composition. (It's true without the explicit associator,
-- because functor composition is definitionally associative,
-- but relying on the definitional equality causes bad problems with elaboration later.)
theorem exchange {I J K : D â¥¤ E} (Î± : F âŸ¶ G) (Î² : G âŸ¶ H) (Î³ : I âŸ¶ J) (Î´ : J âŸ¶ K) :
    (Î± â‰« Î²) â—« (Î³ â‰« Î´) = (Î± â—« Î³) â‰« Î² â—« Î´ := by
  aesop_cat

end NatTrans

open NatTrans

namespace Functor

/-- Flip the arguments of a bifunctor. See also `Currying.lean`. -/
@[simps]
protected def flip (F : C â¥¤ D â¥¤ E) : D â¥¤ C â¥¤ E where
  obj k :=
    { obj := fun j => (F.obj j).obj k,
      map := fun f => (F.map f).app k, }
  map f := { app := fun j => (F.obj j).map f }

end Functor

namespace Iso

@[reassoc (attr := simp)]
theorem map_hom_inv_id_app {X Y : C} (e : X â‰… Y) (F : C â¥¤ D â¥¤ E) (Z : D) :
    (F.map e.hom).app Z â‰« (F.map e.inv).app Z = ğŸ™ _ := by
  simp [â† NatTrans.comp_app, â† Functor.map_comp]

@[reassoc (attr := simp)]
theorem map_inv_hom_id_app {X Y : C} (e : X â‰… Y) (F : C â¥¤ D â¥¤ E) (Z : D) :
    (F.map e.inv).app Z â‰« (F.map e.hom).app Z = ğŸ™ _ := by
  simp [â† NatTrans.comp_app, â† Functor.map_comp]

end Iso

@[deprecated (since := "2024-06-09")] alias map_hom_inv_app := Iso.map_hom_inv_id_app
@[deprecated (since := "2024-06-09")] alias map_inv_hom_app := Iso.map_inv_hom_id_app
@[deprecated (since := "2024-06-09")] alias map_hom_inv_app_assoc := Iso.map_hom_inv_id_app_assoc
@[deprecated (since := "2024-06-09")] alias map_inv_hom_app_assoc := Iso.map_inv_hom_id_app_assoc

end CategoryTheory
