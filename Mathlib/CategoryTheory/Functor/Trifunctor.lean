/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Functor.Bifunctor

/-!
# Trifunctors obtained by composition of bifunctors

Given two bifunctors `F‚ÇÅ‚ÇÇ : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÅ‚ÇÇ` and `G : C‚ÇÅ‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ`, we define
the trifunctor `bifunctorComp‚ÇÅ‚ÇÇ F‚ÇÅ‚ÇÇ G : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ` which sends three
objects `X‚ÇÅ : C‚ÇÅ`, `X‚ÇÇ : C‚ÇÇ` and `X‚ÇÉ : C‚ÇÉ` to `G.obj ((F‚ÇÅ‚ÇÇ.obj X‚ÇÅ).obj X‚ÇÇ)).obj X‚ÇÉ`.

Similarly, given two bifunctors `F : C‚ÇÅ ‚•§ C‚ÇÇ‚ÇÉ ‚•§ C‚ÇÑ` and `G‚ÇÇ‚ÇÉ : C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÇ‚ÇÉ`, we define
the trifunctor `bifunctorComp‚ÇÇ‚ÇÉ F G‚ÇÇ‚ÇÉ : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ` which sends three
objects `X‚ÇÅ : C‚ÇÅ`, `X‚ÇÇ : C‚ÇÇ` and `X‚ÇÉ : C‚ÇÉ` to `(F.obj X‚ÇÅ).obj ((G‚ÇÇ‚ÇÉ.obj X‚ÇÇ).obj X‚ÇÉ)`.

-/

namespace CategoryTheory

section

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ v‚ÇÑ u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ
variable {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} {E : Type u‚ÇÉ} {F : Type u‚ÇÑ}
variable [Category.{v‚ÇÅ} C] [Category.{v‚ÇÇ} D] [Category.{v‚ÇÉ} E] [Category.{v‚ÇÑ} F]

/-- Action of three-variable functors on objects. -/
abbrev Functor.obj‚ÇÉ (H : C ‚•§ D ‚•§ E ‚•§ F) (A : C) (B : D) (C : E) : F :=
  ((H.obj A).obj B).obj C

/-- Apply a natural transformation between bifunctors in three variables to three objects. -/
abbrev NatTrans.app‚ÇÉ {H G : C ‚•§ D ‚•§ E ‚•§ F} (Œ± : H ‚ü∂ G) (X : C) (Y : D) (Z : E) :
    H.obj‚ÇÉ X Y Z ‚ü∂ G.obj‚ÇÉ X Y Z :=
  ((Œ±.app X).app Y).app Z

@[reassoc, simp]
lemma comp_app‚ÇÉ {H G K : C ‚•§ D ‚•§ E ‚•§ F} (Œ± : H ‚ü∂ G) (Œ≤ : G ‚ü∂ K) (X : C) (Y : D)
    (Z : E) : (Œ± ‚â´ Œ≤).app‚ÇÉ X Y Z = Œ±.app‚ÇÉ X Y Z ‚â´ Œ≤.app‚ÇÉ X Y Z := rfl

end

variable {C‚ÇÅ C‚ÇÇ C‚ÇÉ C‚ÇÑ C‚ÇÅ‚ÇÇ C‚ÇÇ‚ÇÉ : Type*} [Category C‚ÇÅ] [Category C‚ÇÇ] [Category C‚ÇÉ]
  [Category C‚ÇÑ] [Category C‚ÇÅ‚ÇÇ] [Category C‚ÇÇ‚ÇÉ]

section bifunctorComp‚ÇÅ‚ÇÇFunctor

/-- Auxiliary definition for `bifunctorComp‚ÇÅ‚ÇÇ`. -/
@[simps]
def bifunctorComp‚ÇÅ‚ÇÇObj (F‚ÇÅ‚ÇÇ : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÅ‚ÇÇ) (G : C‚ÇÅ‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ) (X‚ÇÅ : C‚ÇÅ) :
    C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ where
  obj X‚ÇÇ :=
    { obj := fun X‚ÇÉ => G.obj‚ÇÇ (F‚ÇÅ‚ÇÇ.obj‚ÇÇ X‚ÇÅ X‚ÇÇ) X‚ÇÉ
      map := fun {_ _} œÜ => G.map‚ÇÇ (ùüô (F‚ÇÅ‚ÇÇ.obj‚ÇÇ X‚ÇÅ X‚ÇÇ)) œÜ }
  map {X‚ÇÇ Y‚ÇÇ} œÜ :=
    { app := fun X‚ÇÉ => (G.map ((F‚ÇÅ‚ÇÇ.obj X‚ÇÅ).map œÜ)).app X‚ÇÉ }

/-- Given two bifunctors `F‚ÇÅ‚ÇÇ : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÅ‚ÇÇ` and `G : C‚ÇÅ‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ`, this is
the trifunctor `C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ` obtained by composition. -/
@[simps]
def bifunctorComp‚ÇÅ‚ÇÇ (F‚ÇÅ‚ÇÇ : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÅ‚ÇÇ) (G : C‚ÇÅ‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ) :
    C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ where
  obj X‚ÇÅ := bifunctorComp‚ÇÅ‚ÇÇObj F‚ÇÅ‚ÇÇ G X‚ÇÅ
  map {X‚ÇÅ Y‚ÇÅ} œÜ :=
    { app := fun X‚ÇÇ =>
        { app := fun X‚ÇÉ => (G.map ((F‚ÇÅ‚ÇÇ.map œÜ).app X‚ÇÇ)).app X‚ÇÉ }
      naturality := fun {X‚ÇÇ Y‚ÇÇ} œà => by
        ext X‚ÇÉ
        dsimp
        simp only [‚Üê NatTrans.comp_app, ‚Üê G.map_comp, NatTrans.naturality] }

/-- Auxiliary definition for `bifunctorComp‚ÇÅ‚ÇÇFunctor`. -/
@[simps]
def bifunctorComp‚ÇÅ‚ÇÇFunctorObj (F‚ÇÅ‚ÇÇ : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÅ‚ÇÇ) :
    (C‚ÇÅ‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ) ‚•§ C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ where
  obj G := bifunctorComp‚ÇÅ‚ÇÇ F‚ÇÅ‚ÇÇ G
  map {G G'} œÜ :=
    { app X‚ÇÅ :=
        { app X‚ÇÇ :=
            { app X‚ÇÉ := (œÜ.app ((F‚ÇÅ‚ÇÇ.obj X‚ÇÅ).obj X‚ÇÇ)).app X‚ÇÉ }
          naturality := fun X‚ÇÇ Y‚ÇÇ f ‚Ü¶ by
            ext X‚ÇÉ
            dsimp
            simp only [‚Üê NatTrans.comp_app, NatTrans.naturality] }
      naturality X‚ÇÅ Y‚ÇÅ f := by
        ext X‚ÇÇ X‚ÇÉ
        dsimp
        simp only [‚Üê NatTrans.comp_app, NatTrans.naturality] }

/-- Auxiliary definition for `bifunctorComp‚ÇÅ‚ÇÇFunctor`. -/
@[simps]
def bifunctorComp‚ÇÅ‚ÇÇFunctorMap {F‚ÇÅ‚ÇÇ F‚ÇÅ‚ÇÇ' : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÅ‚ÇÇ} (œÜ : F‚ÇÅ‚ÇÇ ‚ü∂ F‚ÇÅ‚ÇÇ') :
    bifunctorComp‚ÇÅ‚ÇÇFunctorObj (C‚ÇÉ := C‚ÇÉ) (C‚ÇÑ := C‚ÇÑ) F‚ÇÅ‚ÇÇ ‚ü∂ bifunctorComp‚ÇÅ‚ÇÇFunctorObj F‚ÇÅ‚ÇÇ' where
  app G :=
    { app X‚ÇÅ :=
        { app X‚ÇÇ := { app X‚ÇÉ := (G.map ((œÜ.app X‚ÇÅ).app X‚ÇÇ)).app X‚ÇÉ }
          naturality := fun X‚ÇÇ Y‚ÇÇ f ‚Ü¶ by
            ext X‚ÇÉ
            dsimp
            simp only [‚Üê NatTrans.comp_app, NatTrans.naturality, ‚Üê G.map_comp] }
      naturality X‚ÇÅ Y‚ÇÅ f := by
        ext X‚ÇÇ X‚ÇÉ
        dsimp
        simp only [‚Üê NatTrans.comp_app, NatTrans.naturality, ‚Üê G.map_comp] }
  naturality G G' f := by
    ext X‚ÇÅ X‚ÇÇ X‚ÇÉ
    dsimp
    simp only [‚Üê NatTrans.comp_app, NatTrans.naturality]

/-- The functor `(C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÅ‚ÇÇ) ‚•§ (C‚ÇÅ‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ) ‚•§ C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ` which
sends `F‚ÇÅ‚ÇÇ : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÅ‚ÇÇ` and `G : C‚ÇÅ‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ` to the functor
`bifunctorComp‚ÇÅ‚ÇÇ F‚ÇÅ‚ÇÇ G : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ`. -/
@[simps]
def bifunctorComp‚ÇÅ‚ÇÇFunctor : (C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÅ‚ÇÇ) ‚•§ (C‚ÇÅ‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ) ‚•§ C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ where
  obj := bifunctorComp‚ÇÅ‚ÇÇFunctorObj
  map := bifunctorComp‚ÇÅ‚ÇÇFunctorMap

end bifunctorComp‚ÇÅ‚ÇÇFunctor

section bifunctorComp‚ÇÇ‚ÇÉFunctor

/-- Auxiliary definition for `bifunctorComp‚ÇÇ‚ÇÉ`. -/
@[simps]
def bifunctorComp‚ÇÇ‚ÇÉObj (F : C‚ÇÅ ‚•§ C‚ÇÇ‚ÇÉ ‚•§ C‚ÇÑ) (G‚ÇÇ‚ÇÉ : C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÇ‚ÇÉ) (X‚ÇÅ : C‚ÇÅ) :
    C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ where
  obj X‚ÇÇ :=
    { obj X‚ÇÉ := (F.obj X‚ÇÅ).obj ((G‚ÇÇ‚ÇÉ.obj X‚ÇÇ).obj X‚ÇÉ)
      map œÜ := (F.obj X‚ÇÅ).map ((G‚ÇÇ‚ÇÉ.obj X‚ÇÇ).map œÜ) }
  map {X‚ÇÇ Y‚ÇÇ} œÜ :=
    { app X‚ÇÉ := (F.obj X‚ÇÅ).map ((G‚ÇÇ‚ÇÉ.map œÜ).app X‚ÇÉ)
      naturality X‚ÇÉ Y‚ÇÉ œÜ := by
        dsimp
        simp only [‚Üê Functor.map_comp, NatTrans.naturality] }

/-- Given two bifunctors `F : C‚ÇÅ ‚•§ C‚ÇÇ‚ÇÉ ‚•§ C‚ÇÑ` and `G‚ÇÇ‚ÇÉ : C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ`, this is
the trifunctor `C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ` obtained by composition. -/
@[simps]
def bifunctorComp‚ÇÇ‚ÇÉ (F : C‚ÇÅ ‚•§ C‚ÇÇ‚ÇÉ ‚•§ C‚ÇÑ) (G‚ÇÇ‚ÇÉ : C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÇ‚ÇÉ) :
    C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ where
  obj X‚ÇÅ := bifunctorComp‚ÇÇ‚ÇÉObj F G‚ÇÇ‚ÇÉ X‚ÇÅ
  map {X‚ÇÅ Y‚ÇÅ} œÜ :=
    { app := fun X‚ÇÇ =>
        { app := fun X‚ÇÉ => (F.map œÜ).app ((G‚ÇÇ‚ÇÉ.obj X‚ÇÇ).obj X‚ÇÉ) } }

/-- Auxiliary definition for `bifunctorComp‚ÇÇ‚ÇÉFunctor`. -/
@[simps]
def bifunctorComp‚ÇÇ‚ÇÉFunctorObj (F : C‚ÇÅ ‚•§ C‚ÇÇ‚ÇÉ ‚•§ C‚ÇÑ) :
    (C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÇ‚ÇÉ) ‚•§ C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ where
  obj G‚ÇÇ‚ÇÉ := bifunctorComp‚ÇÇ‚ÇÉ F G‚ÇÇ‚ÇÉ
  map {G‚ÇÇ‚ÇÉ G‚ÇÇ‚ÇÉ'} œÜ :=
    { app X‚ÇÅ :=
        { app X‚ÇÇ :=
            { app X‚ÇÉ := (F.obj X‚ÇÅ).map ((œÜ.app X‚ÇÇ).app X‚ÇÉ)
              naturality X‚ÇÉ Y‚ÇÉ f := by
                dsimp
                simp only [‚Üê Functor.map_comp, NatTrans.naturality] }
          naturality X‚ÇÇ Y‚ÇÇ f := by
            ext X‚ÇÉ
            dsimp
            simp only [‚Üê NatTrans.comp_app, ‚Üê Functor.map_comp, NatTrans.naturality] } }

/-- Auxiliary definition for `bifunctorComp‚ÇÇ‚ÇÉFunctor`. -/
@[simps]
def bifunctorComp‚ÇÇ‚ÇÉFunctorMap {F F' : C‚ÇÅ ‚•§ C‚ÇÇ‚ÇÉ ‚•§ C‚ÇÑ} (œÜ : F ‚ü∂ F') :
    bifunctorComp‚ÇÇ‚ÇÉFunctorObj F (C‚ÇÇ := C‚ÇÇ) (C‚ÇÉ := C‚ÇÉ) ‚ü∂ bifunctorComp‚ÇÇ‚ÇÉFunctorObj F' where
  app G‚ÇÇ‚ÇÉ :=
    { app X‚ÇÅ := { app X‚ÇÇ := { app X‚ÇÉ := (œÜ.app X‚ÇÅ).app ((G‚ÇÇ‚ÇÉ.obj X‚ÇÇ).obj X‚ÇÉ) } }
      naturality X‚ÇÅ Y‚ÇÅ f := by
        ext X‚ÇÇ X‚ÇÉ
        dsimp
        simp only [‚Üê NatTrans.comp_app, NatTrans.naturality] }

/-- The functor `(C‚ÇÅ ‚•§ C‚ÇÇ‚ÇÉ ‚•§ C‚ÇÑ) ‚•§ (C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÇ‚ÇÉ) ‚•§ C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ` which
sends `F : C‚ÇÅ ‚•§ C‚ÇÇ‚ÇÉ ‚•§ C‚ÇÑ` and `G‚ÇÇ‚ÇÉ : C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÇ‚ÇÉ` to the
functor `bifunctorComp‚ÇÇ‚ÇÉ F G‚ÇÇ‚ÇÉ : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ`. -/
@[simps]
def bifunctorComp‚ÇÇ‚ÇÉFunctor :
    (C‚ÇÅ ‚•§ C‚ÇÇ‚ÇÉ ‚•§ C‚ÇÑ) ‚•§ (C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÇ‚ÇÉ) ‚•§ C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ ‚•§ C‚ÇÑ where
  obj := bifunctorComp‚ÇÇ‚ÇÉFunctorObj
  map := bifunctorComp‚ÇÇ‚ÇÉFunctorMap

end bifunctorComp‚ÇÇ‚ÇÉFunctor

end CategoryTheory
