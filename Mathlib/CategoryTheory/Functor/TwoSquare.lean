/-
Copyright (c) 2025 Jakob von Raumer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou, Jakob von Raumer
-/
import Mathlib.CategoryTheory.Equivalence
/-!
# 2-squares of functors

Given four functors `T`, `L`, `R` and `B`, a 2-square `TwoSquare T L R B` consists of
a natural transformation `w : T ‚ãô R ‚ü∂ L ‚ãô B`:
```
     T
  C‚ÇÅ ‚•§ C‚ÇÇ
L |     | R
  v     v
  C‚ÇÉ ‚•§ C‚ÇÑ
     B
```

We define operations to paste such squares horizontally and vertically and prove the interchange
law of those two operations.

## TODO

Generalize all of this to double categories.

-/

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ v‚ÇÑ v‚ÇÖ v‚ÇÜ v‚Çá v‚Çà v‚Çâ u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ u‚ÇÖ u‚ÇÜ u‚Çá u‚Çà u‚Çâ

namespace CategoryTheory

open Category

variable {C‚ÇÅ : Type u‚ÇÅ} {C‚ÇÇ : Type u‚ÇÇ} {C‚ÇÉ : Type u‚ÇÉ} {C‚ÇÑ : Type u‚ÇÑ}
  [Category.{v‚ÇÅ} C‚ÇÅ] [Category.{v‚ÇÇ} C‚ÇÇ] [Category.{v‚ÇÉ} C‚ÇÉ] [Category.{v‚ÇÑ} C‚ÇÑ]
  (T : C‚ÇÅ ‚•§ C‚ÇÇ) (L : C‚ÇÅ ‚•§ C‚ÇÉ) (R : C‚ÇÇ ‚•§ C‚ÇÑ) (B : C‚ÇÉ ‚•§ C‚ÇÑ)

/-- A `2`-square consists of a natural transformation `T ‚ãô R ‚ü∂ L ‚ãô B`
involving fours functors `T`, `L`, `R`, `B` that are on the
top/left/right/bottom sides of a square of categories. -/
def TwoSquare := T ‚ãô R ‚ü∂ L ‚ãô B

namespace TwoSquare

/-- Constructor for `TwoSquare`. -/
abbrev mk (Œ± : T ‚ãô R ‚ü∂ L ‚ãô B) : TwoSquare T L R B := Œ±

variable {T L R B}

@[ext]
lemma ext (w w' : TwoSquare T L R B) (h : ‚àÄ (X : C‚ÇÅ), w.app X = w'.app X) :
    w = w' :=
  NatTrans.ext (funext h)

/-- The hoizontal identity 2-square. -/
def hId (L : C‚ÇÅ ‚•§ C‚ÇÉ) : TwoSquare (ùü≠ _) L L (ùü≠ _) :=
  ùüô _

/-- Notation for the horizontal identity 2-square. -/
scoped notation "ùüô‚Çï" => hId  -- type as \b1\_h

/-- The vertical identity 2-square. -/
def vId (T : C‚ÇÅ ‚•§ C‚ÇÇ) : TwoSquare T (ùü≠ _) (ùü≠ _) T :=
  ùüô _

/-- Notation for the vertical identity 2-square. -/
scoped notation "ùüô·µ•" => vId  -- type as \b1\_v

variable {C‚ÇÖ : Type u‚ÇÖ} {C‚ÇÜ : Type u‚ÇÜ} {C‚Çá : Type u‚Çá} {C‚Çà : Type u‚Çà}
  [Category.{v‚ÇÖ} C‚ÇÖ] [Category.{v‚ÇÜ} C‚ÇÜ] [Category.{v‚Çá} C‚Çá] [Category.{v‚Çà} C‚Çà]
  {T' : C‚ÇÇ ‚•§ C‚ÇÖ} {R' : C‚ÇÖ ‚•§ C‚ÇÜ} {B' : C‚ÇÑ ‚•§ C‚ÇÜ} {L' : C‚ÇÉ ‚•§ C‚Çá} {R'' : C‚ÇÑ ‚•§ C‚Çà} {B'' : C‚Çá ‚•§ C‚Çà}

/-- The horizontal composition of 2-squares. -/
def hComp (w : TwoSquare T L R B) (w' : TwoSquare T' R R' B') :
    TwoSquare (T ‚ãô T') L R' (B ‚ãô B') :=
  (whiskerLeft T w') ‚â´ (whiskerRight w B')

/-- Notation for the horizontal composition of 2-squares. -/
scoped infixr:80 " ‚â´‚Çï " => hComp -- type as \gg\_h

/-- The vertical composition of 2-squares. -/
def vComp (w : TwoSquare T L R B) (w' : TwoSquare B L' R'' B'') :
    TwoSquare T (L ‚ãô L') (R ‚ãô R'') B'' :=
  (whiskerRight w R'') ‚â´ (whiskerLeft L w')

/-- Notation for the vertical composition of 2-squares. -/
scoped infixr:80 " ‚â´·µ• " => vComp -- type as \gg\_v

section Interchange

variable {C‚Çâ : Type u‚Çâ} [Category.{v‚Çâ} C‚Çâ] {R‚ÇÉ : C‚ÇÜ ‚•§ C‚Çâ} {B‚ÇÉ : C‚Çà ‚•§ C‚Çâ}

/-- When composing 2-squares which form a diagram of grid, compositing horionzall first yields the
same result as composing vertically first. -/
lemma hCompVComphComp (w‚ÇÅ : TwoSquare T L R B) (w‚ÇÇ : TwoSquare T' R R' B')
    (w‚ÇÉ : TwoSquare B L' R'' B'') (w‚ÇÑ : TwoSquare B' R'' R‚ÇÉ B‚ÇÉ) :
    (w‚ÇÅ ‚â´‚Çï w‚ÇÇ) ‚â´·µ• (w‚ÇÉ ‚â´‚Çï w‚ÇÑ) = (w‚ÇÅ ‚â´·µ• w‚ÇÉ) ‚â´‚Çï (w‚ÇÇ ‚â´·µ• w‚ÇÑ) := by
  unfold hComp vComp
  unfold whiskerLeft whiskerRight
  ext c
  simp only [Functor.comp_obj, NatTrans.comp_app, Functor.map_comp, assoc]
  slice_rhs 2 3 =>
    rw [‚Üê Functor.comp_map _ B‚ÇÉ, ‚Üê w‚ÇÑ.naturality]
  simp only [Functor.comp_obj, Functor.comp_map, assoc]

end Interchange

end TwoSquare

end CategoryTheory
