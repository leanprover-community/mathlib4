/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.NatIso
import Mathlib.Logic.Equiv.Defs
import Mathlib.Logic.Function.Basic

#align_import category_theory.functor.fully_faithful from "leanprover-community/mathlib"@"70d50ecfd4900dd6d328da39ab7ebd516abe4025"

/-!
# Full and faithful functors

We define typeclasses `Full` and `Faithful`, decorating functors.

## Main definitions and results
* Use `F.map_injective` to retrieve the fact that `F.map` is injective when `[Faithful F]`.
* Similarly, `F.map_surjective` states that `F.map` is surjective when `[Full F]`.
* Use `F.preimage` to obtain preimages of morphisms when `[Full F]`.
* We prove some basic "cancellation" lemmas for full and/or faithful functors, as well as a
  construction for "dividing" a functor by a faithful functor, see `Faithful.div`.
* `Full F` carries data, so definitional properties of the preimage can be used when using
  `F.preimage`. To obtain an instance of `Full F` non-constructively, you can use `fullOfExists`
  and `fullOfSurjective`.

See `CategoryTheory.Equivalence.of_fullyFaithful_ess_surj` for the fact that a functor is an
equivalence if and only if it is fully faithful and essentially surjective.

-/


-- declare the `v`'s first; see `CategoryTheory.Category` for an explanation
universe v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

namespace CategoryTheory

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]

/-- A functor `F : C ‚•§ D` is full if for each `X Y : C`, `F.map` is surjective.
In fact, we use a constructive definition, so the `Full F` typeclass contains data,
specifying a particular preimage of each `f : F.obj X ‚ü∂ F.obj Y`.

See <https://stacks.math.columbia.edu/tag/001C>.
-/
class Full (F : C ‚•§ D) where
  /-- The data of a preimage for every `f : F.obj X ‚ü∂ F.obj Y`. -/
  preimage : ‚àÄ {X Y : C} (_ : F.obj X ‚ü∂ F.obj Y), X ‚ü∂ Y
  /-- The property that `Full.preimage f` of maps to `f` via `F.map`. -/
  witness : ‚àÄ {X Y : C} (f : F.obj X ‚ü∂ F.obj Y), F.map (preimage f) = f := by aesop_cat
#align category_theory.full CategoryTheory.Full
#align category_theory.full.witness CategoryTheory.Full.witness

attribute [simp] Full.witness


/- ./././Mathport/Syntax/Translate/Command.lean:379:30: infer kinds are unsupported in Lean 4:
#[`map_injective'] [] -/
/-- A functor `F : C ‚•§ D` is faithful if for each `X Y : C`, `F.map` is injective.

See <https://stacks.math.columbia.edu/tag/001C>.
-/
class Faithful (F : C ‚•§ D) : Prop where
  /-- `F.map` is injective for each `X Y : C`. -/
  map_injective : ‚àÄ {X Y : C}, Function.Injective (F.map : (X ‚ü∂ Y) ‚Üí (F.obj X ‚ü∂ F.obj Y)) := by
    aesop_cat
#align category_theory.faithful CategoryTheory.Faithful
#align category_theory.faithful.map_injective CategoryTheory.Faithful.map_injective

namespace Functor

variable {X Y : C}

theorem map_injective (F : C ‚•§ D) [Faithful F] :
    Function.Injective <| (F.map : (X ‚ü∂ Y) ‚Üí (F.obj X ‚ü∂ F.obj Y)) :=
  Faithful.map_injective
#align category_theory.functor.map_injective CategoryTheory.Functor.map_injective

theorem mapIso_injective (F : C ‚•§ D) [Faithful F] :
    Function.Injective <| (F.mapIso : (X ‚âÖ Y) ‚Üí (F.obj X ‚âÖ F.obj Y))  := fun _ _ h =>
  Iso.ext (map_injective F (congr_arg Iso.hom h : _))
#align category_theory.functor.map_iso_injective CategoryTheory.Functor.mapIso_injective

/-- The specified preimage of a morphism under a full functor. -/
@[pp_dot]
def preimage (F : C ‚•§ D) [Full F] (f : F.obj X ‚ü∂ F.obj Y) : X ‚ü∂ Y :=
  Full.preimage.{v‚ÇÅ, v‚ÇÇ} f
#align category_theory.functor.preimage CategoryTheory.Functor.preimage

@[simp]
theorem image_preimage (F : C ‚•§ D) [Full F] {X Y : C} (f : F.obj X ‚ü∂ F.obj Y) :
    F.map (preimage F f) = f := by unfold preimage; aesop_cat
#align category_theory.functor.image_preimage CategoryTheory.Functor.image_preimage

theorem map_surjective (F : C ‚•§ D) [Full F] :
    Function.Surjective (F.map : (X ‚ü∂ Y) ‚Üí (F.obj X ‚ü∂ F.obj Y)) :=
  fun f => ‚ü®F.preimage f, F.image_preimage f‚ü©
#align category_theory.functor.map_surjective CategoryTheory.Functor.map_surjective

/-- Deduce that `F` is full from the existence of preimages, using choice. -/
noncomputable def fullOfExists (F : C ‚•§ D)
    (h : ‚àÄ (X Y : C) (f : F.obj X ‚ü∂ F.obj Y), ‚àÉ p, F.map p = f) : Full F := by
  choose p hp using h
  exact ‚ü®@p, @hp‚ü©
#align category_theory.functor.full_of_exists CategoryTheory.Functor.fullOfExists

/-- Deduce that `F` is full from surjectivity of `F.map`, using choice. -/
noncomputable def fullOfSurjective (F : C ‚•§ D)
    (h : ‚àÄ X Y : C, Function.Surjective (F.map : (X ‚ü∂ Y) ‚Üí (F.obj X ‚ü∂ F.obj Y))) : Full F :=
  fullOfExists _ h
#align category_theory.functor.full_of_surjective CategoryTheory.Functor.fullOfSurjective

end Functor

section

variable {F : C ‚•§ D} [Full F] [Faithful F] {X Y Z : C}

@[simp]
theorem preimage_id : F.preimage (ùüô (F.obj X)) = ùüô X :=
  F.map_injective (by simp)
#align category_theory.preimage_id CategoryTheory.preimage_id

@[simp]
theorem preimage_comp (f : F.obj X ‚ü∂ F.obj Y) (g : F.obj Y ‚ü∂ F.obj Z) :
    F.preimage (f ‚â´ g) = F.preimage f ‚â´ F.preimage g :=
  F.map_injective (by simp)
#align category_theory.preimage_comp CategoryTheory.preimage_comp

@[simp]
theorem preimage_map (f : X ‚ü∂ Y) : F.preimage (F.map f) = f :=
  F.map_injective (by simp)
#align category_theory.preimage_map CategoryTheory.preimage_map

variable (F)

namespace Functor

/-- If `F : C ‚•§ D` is fully faithful, every isomorphism `F.obj X ‚âÖ F.obj Y` has a preimage. -/
@[simps]
def preimageIso (f : F.obj X ‚âÖ F.obj Y) :
    X ‚âÖ Y where
  hom := F.preimage f.hom
  inv := F.preimage f.inv
  hom_inv_id := F.map_injective (by simp)
  inv_hom_id := F.map_injective (by simp)
#align category_theory.functor.preimage_iso CategoryTheory.Functor.preimageIso
#align category_theory.functor.preimage_iso_inv CategoryTheory.Functor.preimageIso_inv
#align category_theory.functor.preimage_iso_hom CategoryTheory.Functor.preimageIso_hom

@[simp]
theorem preimageIso_mapIso (f : X ‚âÖ Y) : F.preimageIso (F.mapIso f) = f := by
  ext
  simp
#align category_theory.functor.preimage_iso_map_iso CategoryTheory.Functor.preimageIso_mapIso

end Functor

/-- If the image of a morphism under a fully faithful functor in an isomorphism,
then the original morphisms is also an isomorphism.
-/
theorem isIso_of_fully_faithful (f : X ‚ü∂ Y) [IsIso (F.map f)] : IsIso f :=
  ‚ü®‚ü®F.preimage (inv (F.map f)), ‚ü®F.map_injective (by simp), F.map_injective (by simp)‚ü©‚ü©‚ü©
#align category_theory.is_iso_of_fully_faithful CategoryTheory.isIso_of_fully_faithful

/-- If `F` is fully faithful, we have an equivalence of hom-sets `X ‚ü∂ Y` and `F X ‚ü∂ F Y`. -/
@[simps]
def equivOfFullyFaithful {X Y} :
    (X ‚ü∂ Y) ‚âÉ (F.obj X ‚ü∂ F.obj Y) where
  toFun f := F.map f
  invFun f := F.preimage f
  left_inv f := by simp
  right_inv f := by simp
#align category_theory.equiv_of_fully_faithful CategoryTheory.equivOfFullyFaithful
#align category_theory.equiv_of_fully_faithful_apply CategoryTheory.equivOfFullyFaithful_apply
#align category_theory.equiv_of_fully_faithful_symm_apply CategoryTheory.equivOfFullyFaithful_symm_apply

/-- If `F` is fully faithful, we have an equivalence of iso-sets `X ‚âÖ Y` and `F X ‚âÖ F Y`. -/
@[simps]
def isoEquivOfFullyFaithful {X Y} :
    (X ‚âÖ Y) ‚âÉ (F.obj X ‚âÖ F.obj Y) where
  toFun f := F.mapIso f
  invFun f := F.preimageIso f
  left_inv f := by simp
  right_inv f := by
    ext
    simp
#align category_theory.iso_equiv_of_fully_faithful CategoryTheory.isoEquivOfFullyFaithful
#align category_theory.iso_equiv_of_fully_faithful_symm_apply CategoryTheory.isoEquivOfFullyFaithful_symm_apply
#align category_theory.iso_equiv_of_fully_faithful_apply CategoryTheory.isoEquivOfFullyFaithful_apply

end

section

variable {E : Type*} [Category E] {F G : C ‚•§ D} (H : D ‚•§ E) [Full H] [Faithful H]

/-- We can construct a natural transformation between functors by constructing a
natural transformation between those functors composed with a fully faithful functor. -/
@[simps]
def natTransOfCompFullyFaithful (Œ± : F ‚ãô H ‚ü∂ G ‚ãô H) :
    F ‚ü∂ G where
  app X := (equivOfFullyFaithful H).symm (Œ±.app X)
  naturality X Y f := by
    dsimp
    apply H.map_injective
    simpa using Œ±.naturality f
#align category_theory.nat_trans_of_comp_fully_faithful CategoryTheory.natTransOfCompFullyFaithful
#align category_theory.nat_trans_of_comp_fully_faithful_app CategoryTheory.natTransOfCompFullyFaithful_app

/-- We can construct a natural isomorphism between functors by constructing a natural isomorphism
between those functors composed with a fully faithful functor. -/
@[simps!]
def natIsoOfCompFullyFaithful (i : F ‚ãô H ‚âÖ G ‚ãô H) : F ‚âÖ G :=
  NatIso.ofComponents (fun X => (isoEquivOfFullyFaithful H).symm (i.app X)) fun f => by
    dsimp
    apply H.map_injective
    simpa using i.hom.naturality f
#align category_theory.nat_iso_of_comp_fully_faithful CategoryTheory.natIsoOfCompFullyFaithful
#align category_theory.nat_iso_of_comp_fully_faithful_hom_app CategoryTheory.natIsoOfCompFullyFaithful_hom_app
#align category_theory.nat_iso_of_comp_fully_faithful_inv_app CategoryTheory.natIsoOfCompFullyFaithful_inv_app

theorem natIsoOfCompFullyFaithful_hom (i : F ‚ãô H ‚âÖ G ‚ãô H) :
    (natIsoOfCompFullyFaithful H i).hom = natTransOfCompFullyFaithful H i.hom := by
  ext
  simp [natIsoOfCompFullyFaithful]
#align category_theory.nat_iso_of_comp_fully_faithful_hom CategoryTheory.natIsoOfCompFullyFaithful_hom

theorem natIsoOfCompFullyFaithful_inv (i : F ‚ãô H ‚âÖ G ‚ãô H) :
    (natIsoOfCompFullyFaithful H i).inv = natTransOfCompFullyFaithful H i.inv := by
  ext
  simp [‚Üê preimage_comp]
#align category_theory.nat_iso_of_comp_fully_faithful_inv CategoryTheory.natIsoOfCompFullyFaithful_inv

/-- Horizontal composition with a fully faithful functor induces a bijection on
natural transformations. -/
@[simps]
def NatTrans.equivOfCompFullyFaithful :
    (F ‚ü∂ G) ‚âÉ (F ‚ãô H ‚ü∂ G ‚ãô H) where
  toFun Œ± := Œ± ‚ó´ ùüô H
  invFun := natTransOfCompFullyFaithful H
  left_inv := by aesop_cat
  right_inv := by aesop_cat
#align category_theory.nat_trans.equiv_of_comp_fully_faithful CategoryTheory.NatTrans.equivOfCompFullyFaithful
#align category_theory.nat_trans.equiv_of_comp_fully_faithful_apply CategoryTheory.NatTrans.equivOfCompFullyFaithful_apply
#align category_theory.nat_trans.equiv_of_comp_fully_faithful_symm_apply CategoryTheory.NatTrans.equivOfCompFullyFaithful_symm_apply

/-- Horizontal composition with a fully faithful functor induces a bijection on
natural isomorphisms. -/
@[simps]
def NatIso.equivOfCompFullyFaithful :
    (F ‚âÖ G) ‚âÉ (F ‚ãô H ‚âÖ G ‚ãô H) where
  toFun e := NatIso.hcomp e (Iso.refl H)
  invFun := natIsoOfCompFullyFaithful H
  left_inv := by aesop_cat
  right_inv := by aesop_cat
#align category_theory.nat_iso.equiv_of_comp_fully_faithful CategoryTheory.NatIso.equivOfCompFullyFaithful
#align category_theory.nat_iso.equiv_of_comp_fully_faithful_symm_apply CategoryTheory.NatIso.equivOfCompFullyFaithful_symm_apply
#align category_theory.nat_iso.equiv_of_comp_fully_faithful_apply CategoryTheory.NatIso.equivOfCompFullyFaithful_apply

end

end CategoryTheory

namespace CategoryTheory

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]

instance Full.id : Full (ùü≠ C) where preimage f := f
#align category_theory.full.id CategoryTheory.Full.id

instance Faithful.id : Faithful (ùü≠ C) := { }
#align category_theory.faithful.id CategoryTheory.Faithful.id

variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E]

variable (F F' : C ‚•§ D) (G : D ‚•§ E)

instance Faithful.comp [Faithful F] [Faithful G] :
    Faithful (F ‚ãô G) where map_injective p := F.map_injective (G.map_injective p)
#align category_theory.faithful.comp CategoryTheory.Faithful.comp

theorem Faithful.of_comp [Faithful <| F ‚ãô G] : Faithful F :=
  -- Porting note: (F ‚ãô G).map_injective.of_comp has the incorrect type
  { map_injective := fun {_ _} => Function.Injective.of_comp (F ‚ãô G).map_injective }
#align category_theory.faithful.of_comp CategoryTheory.Faithful.of_comp

section

variable {F F'}

/-- If `F` is full, and naturally isomorphic to some `F'`, then `F'` is also full. -/
def Full.ofIso [Full F] (Œ± : F ‚âÖ F') :
    Full F' where
  preimage {X Y} f := F.preimage ((Œ±.app X).hom ‚â´ f ‚â´ (Œ±.app Y).inv)
  witness f := by simp [‚Üê NatIso.naturality_1 Œ±]
#align category_theory.full.of_iso CategoryTheory.Full.ofIso

theorem Faithful.of_iso [Faithful F] (Œ± : F ‚âÖ F') : Faithful F' :=
  { map_injective := fun h =>
      F.map_injective (by rw [‚Üê NatIso.naturality_1 Œ±.symm, h, NatIso.naturality_1 Œ±.symm]) }
#align category_theory.faithful.of_iso CategoryTheory.Faithful.of_iso

end

variable {F G}

theorem Faithful.of_comp_iso {H : C ‚•§ E} [Faithful H] (h : F ‚ãô G ‚âÖ H) : Faithful F :=
  @Faithful.of_comp _ _ _ _ _ _ F G (Faithful.of_iso h.symm)
#align category_theory.faithful.of_comp_iso CategoryTheory.Faithful.of_comp_iso

alias _root_.CategoryTheory.Iso.faithful_of_comp := Faithful.of_comp_iso
#align category_theory.iso.faithful_of_comp CategoryTheory.Iso.faithful_of_comp

-- We could prove this from `Faithful.of_comp_iso` using `eq_to_iso`,
-- but that would introduce a cyclic import.
theorem Faithful.of_comp_eq {H : C ‚•§ E} [‚Ñã : Faithful H] (h : F ‚ãô G = H) : Faithful F :=
  @Faithful.of_comp _ _ _ _ _ _ F G (h.symm ‚ñ∏ ‚Ñã)
#align category_theory.faithful.of_comp_eq CategoryTheory.Faithful.of_comp_eq

alias _root_.Eq.faithful_of_comp := Faithful.of_comp_eq
#align eq.faithful_of_comp Eq.faithful_of_comp

variable (F G)
/-- ‚ÄúDivide‚Äù a functor by a faithful functor. -/
protected def Faithful.div (F : C ‚•§ E) (G : D ‚•§ E) [Faithful G] (obj : C ‚Üí D)
    (h_obj : ‚àÄ X, G.obj (obj X) = F.obj X) (map : ‚àÄ {X Y}, (X ‚ü∂ Y) ‚Üí (obj X ‚ü∂ obj Y))
    (h_map : ‚àÄ {X Y} {f : X ‚ü∂ Y}, HEq (G.map (map f)) (F.map f)) : C ‚•§ D :=
  { obj, map := @map,
    map_id := by
      intros X
      apply G.map_injective
      apply eq_of_heq
      trans F.map (ùüô X)
      ¬∑ exact h_map
      ¬∑ rw [F.map_id, G.map_id, h_obj X]
    map_comp := by
      intros X Y Z f g
      refine G.map_injective <| eq_of_heq <| h_map.trans ?_
      simp only [Functor.map_comp]
      convert HEq.refl (F.map f ‚â´ F.map g)
      all_goals { first | apply h_obj | apply h_map } }
#align category_theory.faithful.div CategoryTheory.Faithful.div

-- This follows immediately from `Functor.hext` (`Functor.hext h_obj @h_map`),
-- but importing `CategoryTheory.EqToHom` causes an import loop:
-- CategoryTheory.EqToHom ‚Üí CategoryTheory.Opposites ‚Üí
-- CategoryTheory.Equivalence ‚Üí CategoryTheory.FullyFaithful
theorem Faithful.div_comp (F : C ‚•§ E) [Faithful F] (G : D ‚•§ E) [Faithful G] (obj : C ‚Üí D)
    (h_obj : ‚àÄ X, G.obj (obj X) = F.obj X) (map : ‚àÄ {X Y}, (X ‚ü∂ Y) ‚Üí (obj X ‚ü∂ obj Y))
    (h_map : ‚àÄ {X Y} {f : X ‚ü∂ Y}, HEq (G.map (map f)) (F.map f)) :
    Faithful.div F G obj @h_obj @map @h_map ‚ãô G = F := by
  -- Porting note: Have to unfold the structure twice because the first one recovers only the
  -- prefunctor `F_pre`
  cases' F with F_pre _ _; cases' G with G_pre _ _
  cases' F_pre with F_obj _; cases' G_pre with G_obj _
  unfold Faithful.div Functor.comp
  -- Porting note: unable to find the lean4 analogue to `unfold_projs`, works without it
  have : F_obj = G_obj ‚àò obj := (funext h_obj).symm
  subst this
  congr
  simp only [Function.comp_apply, heq_eq_eq] at h_map
  ext
  exact h_map
#align category_theory.faithful.div_comp CategoryTheory.Faithful.div_comp

theorem Faithful.div_faithful (F : C ‚•§ E) [Faithful F] (G : D ‚•§ E) [Faithful G] (obj : C ‚Üí D)
    (h_obj : ‚àÄ X, G.obj (obj X) = F.obj X) (map : ‚àÄ {X Y}, (X ‚ü∂ Y) ‚Üí (obj X ‚ü∂ obj Y))
    (h_map : ‚àÄ {X Y} {f : X ‚ü∂ Y}, HEq (G.map (map f)) (F.map f)) :
    Faithful (Faithful.div F G obj @h_obj @map @h_map) :=
  (Faithful.div_comp F G _ h_obj _ @h_map).faithful_of_comp
#align category_theory.faithful.div_faithful CategoryTheory.Faithful.div_faithful

instance Full.comp [Full F] [Full G] :
    Full (F ‚ãô G) where preimage f := F.preimage (G.preimage f)
#align category_theory.full.comp CategoryTheory.Full.comp

/-- If `F ‚ãô G` is full and `G` is faithful, then `F` is full. -/
def Full.ofCompFaithful [Full <| F ‚ãô G] [Faithful G] :
    Full F where
  preimage f := (F ‚ãô G).preimage (G.map f)
  witness _ := G.map_injective ((F ‚ãô G).image_preimage _)
#align category_theory.full.of_comp_faithful CategoryTheory.Full.ofCompFaithful

/-- If `F ‚ãô G` is full and `G` is faithful, then `F` is full. -/
def Full.ofCompFaithfulIso {F : C ‚•§ D} {G : D ‚•§ E} {H : C ‚•§ E} [Full H] [Faithful G]
    (h : F ‚ãô G ‚âÖ H) : Full F :=
  @Full.ofCompFaithful _ _ _ _ _ _ F G (Full.ofIso h.symm) _
#align category_theory.full.of_comp_faithful_iso CategoryTheory.Full.ofCompFaithfulIso

/-- Given a natural isomorphism between `F ‚ãô H` and `G ‚ãô H` for a fully faithful functor `H`, we
can 'cancel' it to give a natural iso between `F` and `G`.
-/
def fullyFaithfulCancelRight {F G : C ‚•§ D} (H : D ‚•§ E) [Full H] [Faithful H]
    (comp_iso : F ‚ãô H ‚âÖ G ‚ãô H) : F ‚âÖ G :=
  NatIso.ofComponents (fun X => H.preimageIso (comp_iso.app X)) fun f =>
    H.map_injective (by simpa using comp_iso.hom.naturality f)
#align category_theory.fully_faithful_cancel_right CategoryTheory.fullyFaithfulCancelRight

@[simp]
theorem fullyFaithfulCancelRight_hom_app {F G : C ‚•§ D} {H : D ‚•§ E} [Full H] [Faithful H]
    (comp_iso : F ‚ãô H ‚âÖ G ‚ãô H) (X : C) :
    (fullyFaithfulCancelRight H comp_iso).hom.app X = H.preimage (comp_iso.hom.app X) :=
  rfl
#align category_theory.fully_faithful_cancel_right_hom_app CategoryTheory.fullyFaithfulCancelRight_hom_app

@[simp]
theorem fullyFaithfulCancelRight_inv_app {F G : C ‚•§ D} {H : D ‚•§ E} [Full H] [Faithful H]
    (comp_iso : F ‚ãô H ‚âÖ G ‚ãô H) (X : C) :
    (fullyFaithfulCancelRight H comp_iso).inv.app X = H.preimage (comp_iso.inv.app X) :=
  rfl
#align category_theory.fully_faithful_cancel_right_inv_app CategoryTheory.fullyFaithfulCancelRight_inv_app

end CategoryTheory
