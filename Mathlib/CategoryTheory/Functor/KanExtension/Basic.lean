/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Comma.StructuredArrow.Basic
import Mathlib.CategoryTheory.Limits.Shapes.Equivalence
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Terminal

/-!
# Kan extensions

The basic definitions for Kan extensions of functors is introduced in this file. Part of API
is parallel to the definitions for bicategories (see `CategoryTheory.Bicategory.Kan.IsKan`).
(The bicategory API cannot be used directly here because it would not allow the universe
polymorphism which is necessary for some applications.)

Given a natural transformation `Œ± : L ‚ãô F' ‚ü∂ F`, we define the property
`F'.IsRightKanExtension Œ±` which expresses that `(F', Œ±)` is a right Kan
extension of `F` along `L`, i.e. that it is a terminal object in a
category `RightExtension L F` of costructured arrows. The condition
`F'.IsLeftKanExtension Œ±` for `Œ± : F ‚ü∂ L ‚ãô F'` is defined similarly.

We also introduce typeclasses `HasRightKanExtension L F` and `HasLeftKanExtension L F`
which assert the existence of a right or left Kan extension, and chosen Kan extensions
are obtained as `leftKanExtension L F` and `rightKanExtension L F`.

## References
* https://ncatlab.org/nlab/show/Kan+extension

-/

namespace CategoryTheory

open Category Limits

namespace Functor

variable {C C' H D D' : Type*} [Category C] [Category C'] [Category H] [Category D] [Category D']

/-- Given two functors `L : C ‚•§ D` and `F : C ‚•§ H`, this is the category of functors
`F' : H ‚•§ D` equipped with a natural transformation `L ‚ãô F' ‚ü∂ F`. -/
abbrev RightExtension (L : C ‚•§ D) (F : C ‚•§ H) :=
  CostructuredArrow ((whiskeringLeft C D H).obj L) F

/-- Given two functors `L : C ‚•§ D` and `F : C ‚•§ H`, this is the category of functors
`F' : H ‚•§ D` equipped with a natural transformation `F ‚ü∂ L ‚ãô F'`. -/
abbrev LeftExtension (L : C ‚•§ D) (F : C ‚•§ H) :=
  StructuredArrow F ((whiskeringLeft C D H).obj L)

/-- Constructor for objects of the category `Functor.RightExtension L F`. -/
@[simps!]
def RightExtension.mk (F' : D ‚•§ H) {L : C ‚•§ D} {F : C ‚•§ H} (Œ± : L ‚ãô F' ‚ü∂ F) :
    RightExtension L F :=
  CostructuredArrow.mk Œ±

/-- Constructor for objects of the category `Functor.LeftExtension L F`. -/
@[simps!]
def LeftExtension.mk (F' : D ‚•§ H) {L : C ‚•§ D} {F : C ‚•§ H} (Œ± : F ‚ü∂ L ‚ãô F') :
    LeftExtension L F :=
  StructuredArrow.mk Œ±

section

variable (F' : D ‚•§ H) {L : C ‚•§ D} {F : C ‚•§ H} (Œ± : L ‚ãô F' ‚ü∂ F)

/-- Given `Œ± : L ‚ãô F' ‚ü∂ F`, the property `F'.IsRightKanExtension Œ±` asserts that
`(F', Œ±)` is a terminal object in the category `RightExtension L F`, i.e. that `(F', Œ±)`
is a right Kan extension of `F` along `L`. -/
class IsRightKanExtension : Prop where
  nonempty_isUniversal : Nonempty (RightExtension.mk F' Œ±).IsUniversal

variable [F'.IsRightKanExtension Œ±]

/-- If `(F', Œ±)` is a right Kan extension of `F` along `L`, then `(F', Œ±)` is a terminal object
in the category `RightExtension L F`. -/
noncomputable def isUniversalOfIsRightKanExtension : (RightExtension.mk F' Œ±).IsUniversal :=
  IsRightKanExtension.nonempty_isUniversal.some

/-- If `(F', Œ±)` is a right Kan extension of `F` along `L` and `Œ≤ : L ‚ãô G ‚ü∂ F` is
a natural transformation, this is the induced morphism `G ‚ü∂ F'`. -/
noncomputable def liftOfIsRightKanExtension (G : D ‚•§ H) (Œ≤ : L ‚ãô G ‚ü∂ F) : G ‚ü∂ F' :=
  (F'.isUniversalOfIsRightKanExtension Œ±).lift (RightExtension.mk G Œ≤)

@[reassoc (attr := simp)]
lemma liftOfIsRightKanExtension_fac (G : D ‚•§ H) (Œ≤ : L ‚ãô G ‚ü∂ F) :
    whiskerLeft L (F'.liftOfIsRightKanExtension Œ± G Œ≤) ‚â´ Œ± = Œ≤ :=
  (F'.isUniversalOfIsRightKanExtension Œ±).fac (RightExtension.mk G Œ≤)

@[reassoc (attr := simp)]
lemma liftOfIsRightKanExtension_fac_app (G : D ‚•§ H) (Œ≤ : L ‚ãô G ‚ü∂ F) (X : C) :
    (F'.liftOfIsRightKanExtension Œ± G Œ≤).app (L.obj X) ‚â´ Œ±.app X = Œ≤.app X :=
  NatTrans.congr_app (F'.liftOfIsRightKanExtension_fac Œ± G Œ≤) X

lemma hom_ext_of_isRightKanExtension {G : D ‚•§ H} (Œ≥‚ÇÅ Œ≥‚ÇÇ : G ‚ü∂ F')
    (hŒ≥ : whiskerLeft L Œ≥‚ÇÅ ‚â´ Œ± = whiskerLeft L Œ≥‚ÇÇ ‚â´ Œ±) : Œ≥‚ÇÅ = Œ≥‚ÇÇ :=
  (F'.isUniversalOfIsRightKanExtension Œ±).hom_ext hŒ≥

/-- If `(F', Œ±)` is a right Kan extension of `F` along `L`, then this
is the induced bijection `(G ‚ü∂ F') ‚âÉ (L ‚ãô G ‚ü∂ F)` for all `G`. -/
noncomputable def homEquivOfIsRightKanExtension (G : D ‚•§ H) :
    (G ‚ü∂ F') ‚âÉ (L ‚ãô G ‚ü∂ F) where
  toFun Œ≤ := whiskerLeft _ Œ≤ ‚â´ Œ±
  invFun Œ≤ := liftOfIsRightKanExtension _ Œ± _ Œ≤
  left_inv Œ≤ := Functor.hom_ext_of_isRightKanExtension _ Œ± _ _ (by simp)
  right_inv := by aesop_cat

lemma isRightKanExtension_of_iso {F' F'' : D ‚•§ H} (e : F' ‚âÖ F'') {L : C ‚•§ D} {F : C ‚•§ H}
    (Œ± : L ‚ãô F' ‚ü∂ F) (Œ±' : L ‚ãô F'' ‚ü∂ F) (comm : whiskerLeft L e.hom ‚â´ Œ±' = Œ±)
    [F'.IsRightKanExtension Œ±] : F''.IsRightKanExtension Œ±' where
  nonempty_isUniversal := ‚ü®IsTerminal.ofIso (F'.isUniversalOfIsRightKanExtension Œ±)
    (CostructuredArrow.isoMk e comm)‚ü©

lemma isRightKanExtension_iff_of_iso {F' F'' : D ‚•§ H} (e : F' ‚âÖ F'') {L : C ‚•§ D} {F : C ‚•§ H}
    (Œ± : L ‚ãô F' ‚ü∂ F) (Œ±' : L ‚ãô F'' ‚ü∂ F) (comm : whiskerLeft L e.hom ‚â´ Œ±' = Œ±) :
    F'.IsRightKanExtension Œ± ‚Üî F''.IsRightKanExtension Œ±' := by
  constructor
  ¬∑ intro
    exact isRightKanExtension_of_iso e Œ± Œ±' comm
  ¬∑ intro
    refine isRightKanExtension_of_iso e.symm Œ±' Œ± ?_
    rw [‚Üê comm, ‚Üê whiskerLeft_comp_assoc, Iso.symm_hom, e.inv_hom_id, whiskerLeft_id', id_comp]

/-- Right Kan extensions of isomorphic functors are isomorphic. -/
@[simps]
noncomputable def rightKanExtensionUniqueOfIso {G : C ‚•§ H} (i : F ‚âÖ G) (G' : D ‚•§ H)
    (Œ≤ : L ‚ãô G' ‚ü∂ G) [G'.IsRightKanExtension Œ≤] : F' ‚âÖ G' where
  hom := liftOfIsRightKanExtension _ Œ≤ F' (Œ± ‚â´ i.hom)
  inv := liftOfIsRightKanExtension _ Œ± G' (Œ≤ ‚â´ i.inv)
  hom_inv_id := F'.hom_ext_of_isRightKanExtension Œ± _ _ (by simp)
  inv_hom_id := G'.hom_ext_of_isRightKanExtension Œ≤ _ _ (by simp)

/-- Two right Kan extensions are (canonically) isomorphic. -/
@[simps!]
noncomputable def rightKanExtensionUnique
    (F'' : D ‚•§ H) (Œ±' : L ‚ãô F'' ‚ü∂ F) [F''.IsRightKanExtension Œ±'] : F' ‚âÖ F'' :=
  rightKanExtensionUniqueOfIso F' Œ± (Iso.refl _) F'' Œ±'


lemma isRightKanExtension_iff_isIso {F' : D ‚•§ H} {F'' : D ‚•§ H} (œÜ : F'' ‚ü∂ F')
    {L : C ‚•§ D} {F : C ‚•§ H} (Œ± : L ‚ãô F' ‚ü∂ F) (Œ±' : L ‚ãô F'' ‚ü∂ F)
    (comm : whiskerLeft L œÜ ‚â´ Œ± = Œ±') [F'.IsRightKanExtension Œ±] :
    F''.IsRightKanExtension Œ±' ‚Üî IsIso œÜ := by
  constructor
  ¬∑ intro
    rw [F'.hom_ext_of_isRightKanExtension Œ± œÜ (rightKanExtensionUnique _ Œ±' _ Œ±).hom
      (by simp [comm])]
    infer_instance
  ¬∑ intro
    rw [isRightKanExtension_iff_of_iso (asIso œÜ) Œ±' Œ± comm]
    infer_instance
end

section

variable (F' : D ‚•§ H) {L : C ‚•§ D} {F : C ‚•§ H} (Œ± : F ‚ü∂ L ‚ãô F')

/-- Given `Œ± : F ‚ü∂ L ‚ãô F'`, the property `F'.IsLeftKanExtension Œ±` asserts that
`(F', Œ±)` is an initial object in the category `LeftExtension L F`, i.e. that `(F', Œ±)`
is a left Kan extension of `F` along `L`. -/
class IsLeftKanExtension : Prop where
  nonempty_isUniversal : Nonempty (LeftExtension.mk F' Œ±).IsUniversal

variable [F'.IsLeftKanExtension Œ±]

/-- If `(F', Œ±)` is a left Kan extension of `F` along `L`, then `(F', Œ±)` is an initial object
in the category `LeftExtension L F`. -/
noncomputable def isUniversalOfIsLeftKanExtension : (LeftExtension.mk F' Œ±).IsUniversal :=
  IsLeftKanExtension.nonempty_isUniversal.some

/-- If `(F', Œ±)` is a left Kan extension of `F` along `L` and `Œ≤ : F ‚ü∂ L ‚ãô G` is
a natural transformation, this is the induced morphism `F' ‚ü∂ G`. -/
noncomputable def descOfIsLeftKanExtension (G : D ‚•§ H) (Œ≤ : F ‚ü∂ L ‚ãô G) : F' ‚ü∂ G :=
  (F'.isUniversalOfIsLeftKanExtension Œ±).desc (LeftExtension.mk G Œ≤)

@[reassoc (attr := simp)]
lemma descOfIsLeftKanExtension_fac (G : D ‚•§ H) (Œ≤ : F ‚ü∂ L ‚ãô G) :
    Œ± ‚â´ whiskerLeft L (F'.descOfIsLeftKanExtension Œ± G Œ≤) = Œ≤ :=
  (F'.isUniversalOfIsLeftKanExtension Œ±).fac (LeftExtension.mk G Œ≤)

@[reassoc (attr := simp)]
lemma descOfIsLeftKanExtension_fac_app (G : D ‚•§ H) (Œ≤ : F ‚ü∂ L ‚ãô G) (X : C) :
    Œ±.app X ‚â´ (F'.descOfIsLeftKanExtension Œ± G Œ≤).app (L.obj X) = Œ≤.app X :=
  NatTrans.congr_app (F'.descOfIsLeftKanExtension_fac Œ± G Œ≤) X

lemma hom_ext_of_isLeftKanExtension {G : D ‚•§ H} (Œ≥‚ÇÅ Œ≥‚ÇÇ : F' ‚ü∂ G)
    (hŒ≥ : Œ± ‚â´ whiskerLeft L Œ≥‚ÇÅ = Œ± ‚â´ whiskerLeft L Œ≥‚ÇÇ) : Œ≥‚ÇÅ = Œ≥‚ÇÇ :=
  (F'.isUniversalOfIsLeftKanExtension Œ±).hom_ext hŒ≥

/-- If `(F', Œ±)` is a left Kan extension of `F` along `L`, then this
is the induced bijection `(F' ‚ü∂ G) ‚âÉ (F ‚ü∂ L ‚ãô G)` for all `G`. -/
@[simps!]
noncomputable def homEquivOfIsLeftKanExtension (G : D ‚•§ H) :
    (F' ‚ü∂ G) ‚âÉ (F ‚ü∂ L ‚ãô G) where
  toFun Œ≤ := Œ± ‚â´ whiskerLeft _ Œ≤
  invFun Œ≤ := descOfIsLeftKanExtension _ Œ± _ Œ≤
  left_inv Œ≤ := Functor.hom_ext_of_isLeftKanExtension _ Œ± _ _ (by simp)
  right_inv := by aesop_cat

lemma isLeftKanExtension_of_iso {F' : D ‚•§ H} {F'' : D ‚•§ H} (e : F' ‚âÖ F'')
    {L : C ‚•§ D} {F : C ‚•§ H} (Œ± : F ‚ü∂ L ‚ãô F') (Œ±' : F ‚ü∂ L ‚ãô F'')
    (comm : Œ± ‚â´ whiskerLeft L e.hom = Œ±') [F'.IsLeftKanExtension Œ±] :
    F''.IsLeftKanExtension Œ±' where
  nonempty_isUniversal := ‚ü®IsInitial.ofIso (F'.isUniversalOfIsLeftKanExtension Œ±)
    (StructuredArrow.isoMk e comm)‚ü©

lemma isLeftKanExtension_iff_of_iso {F' F'' : D ‚•§ H} (e : F' ‚âÖ F'')
    {L : C ‚•§ D} {F : C ‚•§ H} (Œ± : F ‚ü∂ L ‚ãô F') (Œ±' : F ‚ü∂ L ‚ãô F'')
    (comm : Œ± ‚â´ whiskerLeft L e.hom = Œ±') :
    F'.IsLeftKanExtension Œ± ‚Üî F''.IsLeftKanExtension Œ±' := by
  constructor
  ¬∑ intro
    exact isLeftKanExtension_of_iso e Œ± Œ±' comm
  ¬∑ intro
    refine isLeftKanExtension_of_iso e.symm Œ±' Œ± ?_
    rw [‚Üê comm, assoc, ‚Üê whiskerLeft_comp, Iso.symm_hom, e.hom_inv_id, whiskerLeft_id', comp_id]

/-- Left Kan extensions of isomorphic functors are isomorphic. -/
@[simps]
noncomputable def leftKanExtensionUniqueOfIso {G : C ‚•§ H} (i : F ‚âÖ G) (G' : D ‚•§ H)
    (Œ≤ : G ‚ü∂ L ‚ãô G') [G'.IsLeftKanExtension Œ≤] : F' ‚âÖ G' where
  hom := descOfIsLeftKanExtension _ Œ± G' (i.hom ‚â´ Œ≤)
  inv := descOfIsLeftKanExtension _ Œ≤ F' (i.inv ‚â´ Œ±)
  hom_inv_id := F'.hom_ext_of_isLeftKanExtension Œ± _ _ (by simp)
  inv_hom_id := G'.hom_ext_of_isLeftKanExtension Œ≤ _ _ (by simp)

/-- Two left Kan extensions are (canonically) isomorphic. -/
@[simps!]
noncomputable def leftKanExtensionUnique
    (F'' : D ‚•§ H) (Œ±' : F ‚ü∂ L ‚ãô F'') [F''.IsLeftKanExtension Œ±'] : F' ‚âÖ F'' :=
  leftKanExtensionUniqueOfIso F' Œ± (Iso.refl _) F'' Œ±'

lemma isLeftKanExtension_iff_isIso {F' : D ‚•§ H} {F'' : D ‚•§ H} (œÜ : F' ‚ü∂ F'')
    {L : C ‚•§ D} {F : C ‚•§ H} (Œ± : F ‚ü∂ L ‚ãô F') (Œ±' : F ‚ü∂ L ‚ãô F'')
    (comm : Œ± ‚â´ whiskerLeft L œÜ = Œ±') [F'.IsLeftKanExtension Œ±] :
    F''.IsLeftKanExtension Œ±' ‚Üî IsIso œÜ := by
  constructor
  ¬∑ intro
    rw [F'.hom_ext_of_isLeftKanExtension Œ± œÜ (leftKanExtensionUnique _ Œ± _ Œ±').hom
      (by simp [comm])]
    infer_instance
  ¬∑ intro
    exact isLeftKanExtension_of_iso (asIso œÜ) Œ± Œ±' comm

end

/-- This property `HasRightKanExtension L F` holds when the functor `F` has a right
Kan extension along `L`. -/
abbrev HasRightKanExtension (L : C ‚•§ D) (F : C ‚•§ H) := HasTerminal (RightExtension L F)

lemma HasRightKanExtension.mk (F' : D ‚•§ H) {L : C ‚•§ D} {F : C ‚•§ H} (Œ± : L ‚ãô F' ‚ü∂ F)
    [F'.IsRightKanExtension Œ±] : HasRightKanExtension L F :=
  (F'.isUniversalOfIsRightKanExtension Œ±).hasTerminal

/-- This property `HasLeftKanExtension L F` holds when the functor `F` has a left
Kan extension along `L`. -/
abbrev HasLeftKanExtension (L : C ‚•§ D) (F : C ‚•§ H) := HasInitial (LeftExtension L F)

lemma HasLeftKanExtension.mk (F' : D ‚•§ H) {L : C ‚•§ D} {F : C ‚•§ H} (Œ± : F ‚ü∂ L ‚ãô F')
    [F'.IsLeftKanExtension Œ±] : HasLeftKanExtension L F :=
  (F'.isUniversalOfIsLeftKanExtension Œ±).hasInitial

section

variable (L : C ‚•§ D) (F : C ‚•§ H) [HasRightKanExtension L F]

/-- A chosen right Kan extension when `[HasRightKanExtension L F]` holds. -/
noncomputable def rightKanExtension : D ‚•§ H := (‚ä§_ _ : RightExtension L F).left

/-- The counit of the chosen right Kan extension `rightKanExtension L F`. -/
noncomputable def rightKanExtensionCounit : L ‚ãô rightKanExtension L F ‚ü∂ F :=
  (‚ä§_ _ : RightExtension L F).hom

instance : (L.rightKanExtension F).IsRightKanExtension (L.rightKanExtensionCounit F) where
  nonempty_isUniversal := ‚ü®terminalIsTerminal‚ü©

@[ext]
lemma rightKanExtension_hom_ext {G : D ‚•§ H} (Œ≥‚ÇÅ Œ≥‚ÇÇ : G ‚ü∂ rightKanExtension L F)
    (hŒ≥ : whiskerLeft L Œ≥‚ÇÅ ‚â´ rightKanExtensionCounit L F =
      whiskerLeft L Œ≥‚ÇÇ ‚â´ rightKanExtensionCounit L F) :
    Œ≥‚ÇÅ = Œ≥‚ÇÇ :=
  hom_ext_of_isRightKanExtension _ _ _ _ hŒ≥

end

section

variable (L : C ‚•§ D) (F : C ‚•§ H) [HasLeftKanExtension L F]

/-- A chosen left Kan extension when `[HasLeftKanExtension L F]` holds. -/
noncomputable def leftKanExtension : D ‚•§ H := (‚ä•_ _ : LeftExtension L F).right

/-- The unit of the chosen left Kan extension `leftKanExtension L F`. -/
noncomputable def leftKanExtensionUnit : F ‚ü∂ L ‚ãô leftKanExtension L F :=
  (‚ä•_ _ : LeftExtension L F).hom

instance : (L.leftKanExtension F).IsLeftKanExtension (L.leftKanExtensionUnit F) where
  nonempty_isUniversal := ‚ü®initialIsInitial‚ü©

@[ext]
lemma leftKanExtension_hom_ext {G : D ‚•§ H} (Œ≥‚ÇÅ Œ≥‚ÇÇ : leftKanExtension L F ‚ü∂ G)
    (hŒ≥ : leftKanExtensionUnit L F ‚â´ whiskerLeft L Œ≥‚ÇÅ =
      leftKanExtensionUnit L F ‚â´ whiskerLeft L Œ≥‚ÇÇ) : Œ≥‚ÇÅ = Œ≥‚ÇÇ :=
  hom_ext_of_isLeftKanExtension _ _ _ _ hŒ≥

end

section

variable {L : C ‚•§ D} {L' : C ‚•§ D'} (G : D ‚•§ D')

/-- The functor `LeftExtension L' F ‚•§ LeftExtension L F`
induced by a natural transformation `L' ‚ü∂ L ‚ãô G'`. -/
@[simps!]
def LeftExtension.postcomp‚ÇÅ (f : L' ‚ü∂ L ‚ãô G) (F : C ‚•§ H) :
    LeftExtension L' F ‚•§ LeftExtension L F :=
  StructuredArrow.map‚ÇÇ (F := (whiskeringLeft D D' H).obj G) (G := ùü≠ _) (ùüô _)
    ((whiskeringLeft C D' H).map f)

/-- The functor `RightExtension L' F ‚•§ RightExtension L F`
induced by a natural transformation `L ‚ãô G ‚ü∂ L'`. -/
@[simps!]
def RightExtension.postcomp‚ÇÅ (f : L ‚ãô G ‚ü∂ L') (F : C ‚•§ H) :
    RightExtension L' F ‚•§ RightExtension L F :=
  CostructuredArrow.map‚ÇÇ (F := (whiskeringLeft D D' H).obj G) (G := ùü≠ _)
    ((whiskeringLeft C D' H).map f) (ùüô _)

variable [IsEquivalence G]

noncomputable instance (f : L' ‚ü∂ L ‚ãô G) [IsIso f] (F : C ‚•§ H) :
    IsEquivalence (LeftExtension.postcomp‚ÇÅ G f F) := by
  apply StructuredArrow.isEquivalenceMap‚ÇÇ

noncomputable instance (f : L ‚ãô G ‚ü∂ L') [IsIso f] (F : C ‚•§ H) :
    IsEquivalence (RightExtension.postcomp‚ÇÅ G f F) := by
  apply CostructuredArrow.isEquivalenceMap‚ÇÇ

variable {G} in
lemma hasLeftExtension_iff_postcomp‚ÇÅ (e : L ‚ãô G ‚âÖ L') (F : C ‚•§ H) :
    HasLeftKanExtension L' F ‚Üî HasLeftKanExtension L F :=
  (LeftExtension.postcomp‚ÇÅ G e.inv F).asEquivalence.hasInitial_iff

variable {G} in
lemma hasRightExtension_iff_postcomp‚ÇÅ (e : L ‚ãô G ‚âÖ L') (F : C ‚•§ H) :
    HasRightKanExtension L' F ‚Üî HasRightKanExtension L F :=
  (RightExtension.postcomp‚ÇÅ G e.hom F).asEquivalence.hasTerminal_iff

variable (e : L ‚ãô G ‚âÖ L') (F : C ‚•§ H)

/-- Given an isomorphism `e : L ‚ãô G ‚âÖ L'`, a left extension of `F` along `L'` is universal
iff the corresponding left extension of `L` along `L` is. -/
noncomputable def LeftExtension.isUniversalPostcomp‚ÇÅEquiv (ex : LeftExtension L' F) :
    ex.IsUniversal ‚âÉ ((LeftExtension.postcomp‚ÇÅ G e.inv F).obj ex).IsUniversal := by
  apply IsInitial.isInitialIffObj (LeftExtension.postcomp‚ÇÅ G e.inv F)

/-- Given an isomorphism `e : L ‚ãô G ‚âÖ L'`, a right extension of `F` along `L'` is universal
iff the corresponding right extension of `L` along `L` is. -/
noncomputable def RightExtension.isUniversalPostcomp‚ÇÅEquiv (ex : RightExtension L' F) :
    ex.IsUniversal ‚âÉ ((RightExtension.postcomp‚ÇÅ G e.hom F).obj ex).IsUniversal := by
  apply IsTerminal.isTerminalIffObj (RightExtension.postcomp‚ÇÅ G e.hom F)

variable {F F'}

lemma isLeftKanExtension_iff_postcomp‚ÇÅ (Œ± : F ‚ü∂ L' ‚ãô F') :
    F'.IsLeftKanExtension Œ± ‚Üî (G ‚ãô F').IsLeftKanExtension
      (Œ± ‚â´ whiskerRight e.inv _ ‚â´ (Functor.associator _ _ _).hom) := by
  let eq : (LeftExtension.mk _ Œ±).IsUniversal ‚âÉ
      (LeftExtension.mk _
        (Œ± ‚â´ whiskerRight e.inv _ ‚â´ (Functor.associator _ _ _).hom)).IsUniversal :=
    (LeftExtension.isUniversalPostcomp‚ÇÅEquiv G e F _).trans
    (IsInitial.equivOfIso (StructuredArrow.isoMk (Iso.refl _)))
  constructor
  ¬∑ exact fun _ => ‚ü®‚ü®eq (isUniversalOfIsLeftKanExtension _ _)‚ü©‚ü©
  ¬∑ exact fun _ => ‚ü®‚ü®eq.symm (isUniversalOfIsLeftKanExtension _ _)‚ü©‚ü©

lemma isRightKanExtension_iff_postcomp‚ÇÅ (Œ± : L' ‚ãô F' ‚ü∂ F) :
    F'.IsRightKanExtension Œ± ‚Üî (G ‚ãô F').IsRightKanExtension
      ((Functor.associator _ _ _).inv ‚â´ whiskerRight e.hom F' ‚â´ Œ±) := by
  let eq : (RightExtension.mk _ Œ±).IsUniversal ‚âÉ
    (RightExtension.mk _
      ((Functor.associator _ _ _).inv ‚â´ whiskerRight e.hom F' ‚â´ Œ±)).IsUniversal :=
  (RightExtension.isUniversalPostcomp‚ÇÅEquiv G e F _).trans
    (IsTerminal.equivOfIso (CostructuredArrow.isoMk (Iso.refl _)))
  constructor
  ¬∑ exact fun _ => ‚ü®‚ü®eq (isUniversalOfIsRightKanExtension _ _)‚ü©‚ü©
  ¬∑ exact fun _ => ‚ü®‚ü®eq.symm (isUniversalOfIsRightKanExtension _ _)‚ü©‚ü©

end

section

variable (L : C ‚•§ D) (F : C ‚•§ H) (F' : D ‚•§ H) (G : C' ‚•§ C)

/-- The functor `LeftExtension L F ‚•§ LeftExtension (G ‚ãô L) (G ‚ãô F)`
obtained by precomposition. -/
@[simps!]
def LeftExtension.precomp : LeftExtension L F ‚•§ LeftExtension (G ‚ãô L) (G ‚ãô F) :=
  StructuredArrow.map‚ÇÇ (F := ùü≠ _) (G := (whiskeringLeft C' C H).obj G) (ùüô _) (ùüô _)

/-- The functor `RightExtension L F ‚•§ RightExtension (G ‚ãô L) (G ‚ãô F)`
obtained by precomposition. -/
@[simps!]
def RightExtension.precomp : RightExtension L F ‚•§ RightExtension (G ‚ãô L) (G ‚ãô F) :=
  CostructuredArrow.map‚ÇÇ (F := ùü≠ _) (G := (whiskeringLeft C' C H).obj G) (ùüô _) (ùüô _)

variable [IsEquivalence G]

noncomputable instance : IsEquivalence (LeftExtension.precomp L F G) := by
  apply StructuredArrow.isEquivalenceMap‚ÇÇ

noncomputable instance : IsEquivalence (RightExtension.precomp L F G) := by
  apply CostructuredArrow.isEquivalenceMap‚ÇÇ

/-- If `G` is an equivalence, then a left extension of `F` along `L` is universal iff
the corresponding left extension of `G ‚ãô F` along `G ‚ãô L` is. -/
noncomputable def LeftExtension.isUniversalPrecompEquiv (e : LeftExtension L F) :
    e.IsUniversal ‚âÉ ((LeftExtension.precomp L F G).obj e).IsUniversal := by
  apply IsInitial.isInitialIffObj (LeftExtension.precomp L F G)

/-- If `G` is an equivalence, then a right extension of `F` along `L` is universal iff
the corresponding left extension of `G ‚ãô F` along `G ‚ãô L` is. -/
noncomputable def RightExtension.isUniversalPrecompEquiv (e : RightExtension L F) :
    e.IsUniversal ‚âÉ ((RightExtension.precomp L F G).obj e).IsUniversal := by
  apply IsTerminal.isTerminalIffObj (RightExtension.precomp L F G)

variable {F L}

lemma isLeftKanExtension_iff_precomp (Œ± : F ‚ü∂ L ‚ãô F') :
    F'.IsLeftKanExtension Œ± ‚Üî F'.IsLeftKanExtension
      (whiskerLeft G Œ± ‚â´ (Functor.associator _ _ _).inv) := by
  let eq : (LeftExtension.mk _ Œ±).IsUniversal ‚âÉ (LeftExtension.mk _
      (whiskerLeft G Œ± ‚â´ (Functor.associator _ _ _).inv)).IsUniversal :=
    (LeftExtension.isUniversalPrecompEquiv L F G _).trans
    (IsInitial.equivOfIso (StructuredArrow.isoMk (Iso.refl _)))
  constructor
  ¬∑ exact fun _ => ‚ü®‚ü®eq (isUniversalOfIsLeftKanExtension _ _)‚ü©‚ü©
  ¬∑ exact fun _ => ‚ü®‚ü®eq.symm (isUniversalOfIsLeftKanExtension _ _)‚ü©‚ü©

lemma isRightKanExtension_iff_precomp (Œ± : L ‚ãô F' ‚ü∂ F) :
    F'.IsRightKanExtension Œ± ‚Üî
      F'.IsRightKanExtension ((Functor.associator _ _ _).hom ‚â´ whiskerLeft G Œ±) := by
  let eq : (RightExtension.mk _ Œ±).IsUniversal ‚âÉ (RightExtension.mk _
      ((Functor.associator _ _ _).hom ‚â´ whiskerLeft G Œ±)).IsUniversal :=
    (RightExtension.isUniversalPrecompEquiv L F G _).trans
    (IsTerminal.equivOfIso (CostructuredArrow.isoMk (Iso.refl _)))
  constructor
  ¬∑ exact fun _ => ‚ü®‚ü®eq (isUniversalOfIsRightKanExtension _ _)‚ü©‚ü©
  ¬∑ exact fun _ => ‚ü®‚ü®eq.symm (isUniversalOfIsRightKanExtension _ _)‚ü©‚ü©

end

section

variable {L L' : C ‚•§ D} (iso‚ÇÅ : L ‚âÖ L') (F : C ‚•§ H)

/-- The equivalence `RightExtension L F ‚âå RightExtension L' F` induced by
a natural isomorphism `L ‚âÖ L'`. -/
def rightExtensionEquivalenceOfIso‚ÇÅ : RightExtension L F ‚âå RightExtension L' F :=
  CostructuredArrow.mapNatIso ((whiskeringLeft C D H).mapIso iso‚ÇÅ)

include iso‚ÇÅ in
lemma hasRightExtension_iff_of_iso‚ÇÅ : HasRightKanExtension L F ‚Üî HasRightKanExtension L' F :=
  (rightExtensionEquivalenceOfIso‚ÇÅ iso‚ÇÅ F).hasTerminal_iff

/-- The equivalence `LeftExtension L F ‚âå LeftExtension L' F` induced by
a natural isomorphism `L ‚âÖ L'`. -/
def leftExtensionEquivalenceOfIso‚ÇÅ : LeftExtension L F ‚âå LeftExtension L' F :=
  StructuredArrow.mapNatIso ((whiskeringLeft C D H).mapIso iso‚ÇÅ)

include iso‚ÇÅ in
lemma hasLeftExtension_iff_of_iso‚ÇÅ : HasLeftKanExtension L F ‚Üî HasLeftKanExtension L' F :=
  (leftExtensionEquivalenceOfIso‚ÇÅ iso‚ÇÅ F).hasInitial_iff

end

section

variable (L : C ‚•§ D) {F F' : C ‚•§ H} (iso‚ÇÇ : F ‚âÖ F')

/-- The equivalence `RightExtension L F ‚âå RightExtension L F'` induced by
a natural isomorphism `F ‚âÖ F'`. -/
def rightExtensionEquivalenceOfIso‚ÇÇ : RightExtension L F ‚âå RightExtension L F' :=
  CostructuredArrow.mapIso iso‚ÇÇ

include iso‚ÇÇ in
lemma hasRightExtension_iff_of_iso‚ÇÇ : HasRightKanExtension L F ‚Üî HasRightKanExtension L F' :=
  (rightExtensionEquivalenceOfIso‚ÇÇ L iso‚ÇÇ).hasTerminal_iff

/-- The equivalence `LeftExtension L F ‚âå LeftExtension L F'` induced by
a natural isomorphism `F ‚âÖ F'`. -/
def leftExtensionEquivalenceOfIso‚ÇÇ : LeftExtension L F ‚âå LeftExtension L F' :=
  StructuredArrow.mapIso iso‚ÇÇ

include iso‚ÇÇ in
lemma hasLeftExtension_iff_of_iso‚ÇÇ : HasLeftKanExtension L F ‚Üî HasLeftKanExtension L F' :=
  (leftExtensionEquivalenceOfIso‚ÇÇ L iso‚ÇÇ).hasInitial_iff

end

section

variable {L : C ‚•§ D} {F‚ÇÅ F‚ÇÇ : C ‚•§ H}

/-- When two left extensions `Œ±‚ÇÅ : LeftExtension L F‚ÇÅ` and `Œ±‚ÇÇ : LeftExtension L F‚ÇÇ`
are essentially the same via an isomorphism of functors `F‚ÇÅ ‚âÖ F‚ÇÇ`,
then `Œ±‚ÇÅ` is universal iff `Œ±‚ÇÇ` is. -/
noncomputable def LeftExtension.isUniversalEquivOfIso‚ÇÇ
    (Œ±‚ÇÅ : LeftExtension L F‚ÇÅ) (Œ±‚ÇÇ : LeftExtension L F‚ÇÇ) (e : F‚ÇÅ ‚âÖ F‚ÇÇ)
    (e' : Œ±‚ÇÅ.right ‚âÖ Œ±‚ÇÇ.right)
    (h : Œ±‚ÇÅ.hom ‚â´ whiskerLeft L e'.hom = e.hom ‚â´ Œ±‚ÇÇ.hom) :
    Œ±‚ÇÅ.IsUniversal ‚âÉ Œ±‚ÇÇ.IsUniversal :=
  (IsInitial.isInitialIffObj (leftExtensionEquivalenceOfIso‚ÇÇ L e).functor Œ±‚ÇÅ).trans
    (IsInitial.equivOfIso (StructuredArrow.isoMk e'
      (by simp [leftExtensionEquivalenceOfIso‚ÇÇ, h])))

lemma isLeftKanExtension_iff_of_iso‚ÇÇ {F‚ÇÅ' F‚ÇÇ' : D ‚•§ H} (Œ±‚ÇÅ : F‚ÇÅ ‚ü∂ L ‚ãô F‚ÇÅ') (Œ±‚ÇÇ : F‚ÇÇ ‚ü∂ L ‚ãô F‚ÇÇ')
    (e : F‚ÇÅ ‚âÖ F‚ÇÇ) (e' : F‚ÇÅ' ‚âÖ F‚ÇÇ') (h : Œ±‚ÇÅ ‚â´ whiskerLeft L e'.hom = e.hom ‚â´ Œ±‚ÇÇ) :
    F‚ÇÅ'.IsLeftKanExtension Œ±‚ÇÅ ‚Üî F‚ÇÇ'.IsLeftKanExtension Œ±‚ÇÇ := by
  let eq := LeftExtension.isUniversalEquivOfIso‚ÇÇ (LeftExtension.mk _ Œ±‚ÇÅ)
    (LeftExtension.mk _ Œ±‚ÇÇ) e e' h
  constructor
  ¬∑ exact fun _ => ‚ü®‚ü®eq.1 (isUniversalOfIsLeftKanExtension F‚ÇÅ' Œ±‚ÇÅ)‚ü©‚ü©
  ¬∑ exact fun _ => ‚ü®‚ü®eq.2 (isUniversalOfIsLeftKanExtension F‚ÇÇ' Œ±‚ÇÇ)‚ü©‚ü©

/-- When two right extensions `Œ±‚ÇÅ : RightExtension L F‚ÇÅ` and `Œ±‚ÇÇ : RightExtension L F‚ÇÇ`
are essentially the same via an isomorphism of functors `F‚ÇÅ ‚âÖ F‚ÇÇ`,
then `Œ±‚ÇÅ` is universal iff `Œ±‚ÇÇ` is. -/
noncomputable def RightExtension.isUniversalEquivOfIso‚ÇÇ
    (Œ±‚ÇÅ : RightExtension L F‚ÇÅ) (Œ±‚ÇÇ : RightExtension L F‚ÇÇ) (e : F‚ÇÅ ‚âÖ F‚ÇÇ)
    (e' : Œ±‚ÇÅ.left ‚âÖ Œ±‚ÇÇ.left)
    (h : whiskerLeft L e'.hom ‚â´ Œ±‚ÇÇ.hom = Œ±‚ÇÅ.hom ‚â´ e.hom) :
    Œ±‚ÇÅ.IsUniversal ‚âÉ Œ±‚ÇÇ.IsUniversal :=
  (IsTerminal.isTerminalIffObj (rightExtensionEquivalenceOfIso‚ÇÇ L e).functor Œ±‚ÇÅ).trans
    (IsTerminal.equivOfIso (CostructuredArrow.isoMk e'
      (by simp [rightExtensionEquivalenceOfIso‚ÇÇ, h])))

lemma isRightKanExtension_iff_of_iso‚ÇÇ {F‚ÇÅ' F‚ÇÇ' : D ‚•§ H} (Œ±‚ÇÅ : L ‚ãô F‚ÇÅ' ‚ü∂ F‚ÇÅ) (Œ±‚ÇÇ : L ‚ãô F‚ÇÇ' ‚ü∂ F‚ÇÇ)
    (e : F‚ÇÅ ‚âÖ F‚ÇÇ) (e' : F‚ÇÅ' ‚âÖ F‚ÇÇ') (h : whiskerLeft L e'.hom ‚â´ Œ±‚ÇÇ = Œ±‚ÇÅ ‚â´ e.hom) :
    F‚ÇÅ'.IsRightKanExtension Œ±‚ÇÅ ‚Üî F‚ÇÇ'.IsRightKanExtension Œ±‚ÇÇ := by
  let eq := RightExtension.isUniversalEquivOfIso‚ÇÇ (RightExtension.mk _ Œ±‚ÇÅ)
    (RightExtension.mk _ Œ±‚ÇÇ) e e' h
  constructor
  ¬∑ exact fun _ => ‚ü®‚ü®eq.1 (isUniversalOfIsRightKanExtension F‚ÇÅ' Œ±‚ÇÅ)‚ü©‚ü©
  ¬∑ exact fun _ => ‚ü®‚ü®eq.2 (isUniversalOfIsRightKanExtension F‚ÇÇ' Œ±‚ÇÇ)‚ü©‚ü©

end

section Colimit

variable (F' : D ‚•§ H) {L : C ‚•§ D} {F : C ‚•§ H} (Œ± : F ‚ü∂ L ‚ãô F') [F'.IsLeftKanExtension Œ±]

/-- Construct a cocone for a left Kan extension `F' : D ‚•§ H` of `F : C ‚•§ H` along a functor
`L : C ‚•§ D` given a cocone for `F`. -/
@[simps]
noncomputable def coconeOfIsLeftKanExtension (c : Cocone F) : Cocone F' where
  pt := c.pt
  Œπ := F'.descOfIsLeftKanExtension Œ± _ c.Œπ

/-- If `c` is a colimit cocone for a functor `F : C ‚•§ H` and `Œ± : F ‚ü∂ L ‚ãô F'` is the unit of any
left Kan extension `F' : D ‚•§ H` of `F` along `L : C ‚•§ D`, then `coconeOfIsLeftKanExtension Œ± c` is
a colimit cocone, too. -/
@[simps]
noncomputable def isColimitCoconeOfIsLeftKanExtension {c : Cocone F} (hc : IsColimit c) :
    IsColimit (F'.coconeOfIsLeftKanExtension Œ± c) where
  desc s := hc.desc (Cocone.mk _ (Œ± ‚â´ whiskerLeft L s.Œπ))
  fac s := by
    have : F'.descOfIsLeftKanExtension Œ± ((const D).obj c.pt) c.Œπ ‚â´
        (Functor.const _).map (hc.desc (Cocone.mk _ (Œ± ‚â´ whiskerLeft L s.Œπ))) = s.Œπ :=
      F'.hom_ext_of_isLeftKanExtension Œ± _ _ (by aesop_cat)
    exact congr_app this
  uniq s m hm := hc.hom_ext (fun j ‚Ü¶ by
    have := hm (L.obj j)
    nth_rw 1 [‚Üê F'.descOfIsLeftKanExtension_fac_app Œ± ((const D).obj c.pt)]
    dsimp at this ‚ä¢
    rw [assoc, this, IsColimit.fac, NatTrans.comp_app, whiskerLeft_app])

variable [HasColimit F] [HasColimit F']

/-- If `F' : D ‚•§ H` is a left Kan extension of `F : C ‚•§ H` along `L : C ‚•§ D`, the colimit over `F'`
is isomorphic to the colimit over `F`. -/
noncomputable def colimitIsoOfIsLeftKanExtension : colimit F' ‚âÖ colimit F :=
  IsColimit.coconePointUniqueUpToIso (colimit.isColimit F')
    (F'.isColimitCoconeOfIsLeftKanExtension Œ± (colimit.isColimit F))

@[reassoc (attr := simp)]
lemma Œπ_colimitIsoOfIsLeftKanExtension_hom (i : C) :
    Œ±.app i ‚â´ colimit.Œπ F' (L.obj i) ‚â´ (F'.colimitIsoOfIsLeftKanExtension Œ±).hom =
      colimit.Œπ F i := by
  simp [colimitIsoOfIsLeftKanExtension]

@[reassoc (attr := simp)]
lemma Œπ_colimitIsoOfIsLeftKanExtension_inv (i : C) :
    colimit.Œπ F i ‚â´ (F'.colimitIsoOfIsLeftKanExtension Œ±).inv =
    Œ±.app i ‚â´ colimit.Œπ F' (L.obj i) := by
  rw [Iso.comp_inv_eq, assoc, Œπ_colimitIsoOfIsLeftKanExtension_hom]

end Colimit

section Limit

variable (F' : D ‚•§ H) {L : C ‚•§ D} {F : C ‚•§ H} (Œ± : L ‚ãô F' ‚ü∂ F) [F'.IsRightKanExtension Œ±]

/-- Construct a cone for a right Kan extension `F' : D ‚•§ H` of `F : C ‚•§ H` along a functor
`L : C ‚•§ D` given a cone for `F`. -/
@[simps]
noncomputable def coneOfIsRightKanExtension (c : Cone F) : Cone F' where
  pt := c.pt
  œÄ := F'.liftOfIsRightKanExtension Œ± _ c.œÄ

/-- If `c` is a limit cone for a functor `F : C ‚•§ H` and `Œ± : L ‚ãô F' ‚ü∂ F` is the counit of any
right Kan extension `F' : D ‚•§ H` of `F` along `L : C ‚•§ D`, then `coneOfIsRightKanExtension Œ± c` is
a limit cone, too. -/
@[simps]
noncomputable def isLimitConeOfIsRightKanExtension {c : Cone F} (hc : IsLimit c) :
    IsLimit (F'.coneOfIsRightKanExtension Œ± c) where
  lift s := hc.lift (Cone.mk _ (whiskerLeft L s.œÄ ‚â´ Œ±))
  fac s := by
    have : (Functor.const _).map (hc.lift (Cone.mk _ (whiskerLeft L s.œÄ ‚â´ Œ±))) ‚â´
        F'.liftOfIsRightKanExtension Œ± ((const D).obj c.pt) c.œÄ = s.œÄ :=
      F'.hom_ext_of_isRightKanExtension Œ± _ _ (by aesop_cat)
    exact congr_app this
  uniq s m hm := hc.hom_ext (fun j ‚Ü¶ by
    have := hm (L.obj j)
    nth_rw 1 [‚Üê F'.liftOfIsRightKanExtension_fac_app Œ± ((const D).obj c.pt)]
    dsimp at this ‚ä¢
    rw [‚Üê assoc, this, IsLimit.fac, NatTrans.comp_app, whiskerLeft_app])

variable [HasLimit F] [HasLimit F']

/-- If `F' : D ‚•§ H` is a right Kan extension of `F : C ‚•§ H` along `L : C ‚•§ D`, the limit over `F'`
is isomorphic to the limit over `F`. -/
noncomputable def limitIsoOfIsRightKanExtension : limit F' ‚âÖ limit F :=
  IsLimit.conePointUniqueUpToIso (limit.isLimit F')
    (F'.isLimitConeOfIsRightKanExtension Œ± (limit.isLimit F))

@[reassoc (attr := simp)]
lemma limitIsoOfIsRightKanExtension_inv_œÄ (i : C) :
    (F'.limitIsoOfIsRightKanExtension Œ±).inv ‚â´ limit.œÄ F' (L.obj i) ‚â´ Œ±.app i = limit.œÄ F i := by
  simp [limitIsoOfIsRightKanExtension]

@[reassoc (attr := simp)]
lemma limitIsoOfIsRightKanExtension_hom_œÄ (i : C) :
    (F'.limitIsoOfIsRightKanExtension Œ±).hom ‚â´ limit.œÄ F i = limit.œÄ F' (L.obj i) ‚â´ Œ±.app i := by
  rw [‚Üê Iso.eq_inv_comp, limitIsoOfIsRightKanExtension_inv_œÄ]

end Limit

end Functor

end CategoryTheory
