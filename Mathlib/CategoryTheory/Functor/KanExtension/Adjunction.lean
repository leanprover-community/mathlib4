/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Functor.KanExtension.Pointwise
public import Mathlib.CategoryTheory.Limits.Shapes.Grothendieck
public import Mathlib.CategoryTheory.Comma.StructuredArrow.Functor

/-! # The Kan extension functor

Given a functor `L : C ‚•§ D`, we define the left Kan extension functor
`L.lan : (C ‚•§ H) ‚•§ (D ‚•§ H)` which sends a functor `F : C ‚•§ H` to its
left Kan extension along `L`. This is defined if all `F` have such
a left Kan extension. It is shown that `L.lan` is the left adjoint to
the functor `(D ‚•§ H) ‚•§ (C ‚•§ H)` given by the precomposition
with `L` (see `Functor.lanAdjunction`).

Similarly, we define the right Kan extension functor
`L.ran : (C ‚•§ H) ‚•§ (D ‚•§ H)` which sends a functor `F : C ‚•§ H` to its
right Kan extension along `L`.

-/

@[expose] public section

namespace CategoryTheory

open Category Limits

namespace Functor

variable {C D : Type*} [Category* C] [Category* D] (L : C ‚•§ D) {H : Type*} [Category* H]

section lan

section

variable [‚àÄ (F : C ‚•§ H), HasLeftKanExtension L F]

/-- The left Kan extension functor `(C ‚•§ H) ‚•§ (D ‚•§ H)` along a functor `C ‚•§ D`. -/
noncomputable def lan : (C ‚•§ H) ‚•§ (D ‚•§ H) where
  obj F := leftKanExtension L F
  map {F‚ÇÅ F‚ÇÇ} œÜ := descOfIsLeftKanExtension _ (leftKanExtensionUnit L F‚ÇÅ) _
    (œÜ ‚â´ leftKanExtensionUnit L F‚ÇÇ)

/-- The natural transformation `F ‚ü∂ L ‚ãô (L.lan).obj G`. -/
noncomputable def lanUnit : (ùü≠ (C ‚•§ H)) ‚ü∂ L.lan ‚ãô (whiskeringLeft C D H).obj L where
  app F := leftKanExtensionUnit L F
  naturality {F‚ÇÅ F‚ÇÇ} œÜ := by ext; simp [lan]

instance (F : C ‚•§ H) : (L.lan.obj F).IsLeftKanExtension (L.lanUnit.app F) := by
  dsimp [lan, lanUnit]
  infer_instance

end

/-- If there exists a pointwise left Kan extension of `F` along `L`,
then `L.lan.obj G` is a pointwise left Kan extension of `F`. -/
noncomputable def isPointwiseLeftKanExtensionLeftKanExtensionUnit
    (F : C ‚•§ H) [HasPointwiseLeftKanExtension L F] :
    (LeftExtension.mk _ (L.leftKanExtensionUnit F)).IsPointwiseLeftKanExtension :=
  isPointwiseLeftKanExtensionOfIsLeftKanExtension (F := F) _ (leftKanExtensionUnit L F)

section

open CostructuredArrow

variable (F : C ‚•§ H) [HasPointwiseLeftKanExtension L F]

/-- If a left Kan extension is pointwise, then evaluating it at an object is isomorphic to
taking a colimit. -/
noncomputable def leftKanExtensionObjIsoColimit [HasLeftKanExtension L F] (X : D) :
    (L.leftKanExtension F).obj X ‚âÖ colimit (proj L X ‚ãô F) :=
  LeftExtension.IsPointwiseLeftKanExtensionAt.isoColimit (F := F)
    (isPointwiseLeftKanExtensionLeftKanExtensionUnit L F X)

@[reassoc (attr := simp)]
lemma Œπ_leftKanExtensionObjIsoColimit_inv [HasLeftKanExtension L F] (X : D)
    (f : CostructuredArrow L X) :
    colimit.Œπ _ f ‚â´ (L.leftKanExtensionObjIsoColimit F X).inv =
    (L.leftKanExtensionUnit F).app f.left ‚â´ (L.leftKanExtension F).map f.hom := by
  simp [leftKanExtensionObjIsoColimit]

@[reassoc (attr := simp)]
lemma Œπ_leftKanExtensionObjIsoColimit_hom (X : D) (f : CostructuredArrow L X) :
    (L.leftKanExtensionUnit F).app f.left ‚â´ (L.leftKanExtension F).map f.hom ‚â´
      (L.leftKanExtensionObjIsoColimit F X).hom =
    colimit.Œπ (proj L X ‚ãô F) f :=
  LeftExtension.IsPointwiseLeftKanExtensionAt.Œπ_isoColimit_hom (F := F)
    (isPointwiseLeftKanExtensionLeftKanExtensionUnit L F X) f

lemma leftKanExtensionUnit_leftKanExtension_map_leftKanExtensionObjIsoColimit_hom (X : D)
    (f : CostructuredArrow L X) :
    (leftKanExtensionUnit L F).app f.left ‚â´ (leftKanExtension L F).map f.hom ‚â´
       (L.leftKanExtensionObjIsoColimit F X).hom =
    colimit.Œπ (proj L X ‚ãô F) f :=
  LeftExtension.IsPointwiseLeftKanExtensionAt.Œπ_isoColimit_hom (F := F)
    (isPointwiseLeftKanExtensionLeftKanExtensionUnit L F X) f

@[reassoc (attr := simp)]
lemma leftKanExtensionUnit_leftKanExtensionObjIsoColimit_hom (X : C) :
    (L.leftKanExtensionUnit F).app X ‚â´ (L.leftKanExtensionObjIsoColimit F (L.obj X)).hom =
    colimit.Œπ (proj L (L.obj X) ‚ãô F) (CostructuredArrow.mk (ùüô _)) := by
  simpa using leftKanExtensionUnit_leftKanExtension_map_leftKanExtensionObjIsoColimit_hom L F
    (L.obj X) (CostructuredArrow.mk (ùüô _))

@[instance]
theorem hasColimit_map_comp_Œπ_comp_grothendieckProj {X Y : D} (f : X ‚ü∂ Y) :
    HasColimit (((functor L).map f).toFunctor ‚ãô Grothendieck.Œπ (functor L) Y ‚ãô
      grothendieckProj L ‚ãô F) :=
  hasColimit_of_iso (isoWhiskerRight (mapCompŒπCompGrothendieckProj L f) F)

@[deprecated (since := "2025-07-27")]
alias hasColimit_map_comp_Œπ_comp_grotendieckProj := hasColimit_map_comp_Œπ_comp_grothendieckProj

/-- The left Kan extension of `F : C ‚•§ H` along a functor `L : C ‚•§ D` is isomorphic to the
fiberwise colimit of the projection functor on the Grothendieck construction of the costructured
arrow category composed with `F`. -/
@[simps!]
noncomputable def leftKanExtensionIsoFiberwiseColimit [HasLeftKanExtension L F] :
    leftKanExtension L F ‚âÖ fiberwiseColimit (grothendieckProj L ‚ãô F) :=
  letI : ‚àÄ X, HasColimit (Grothendieck.Œπ (functor L) X ‚ãô grothendieckProj L ‚ãô F) :=
      fun X => hasColimit_of_iso <| Iso.symm <|
        isoWhiskerRight (eqToIso congr($((functor L).map_id X).toFunctor)) _ ‚â™‚â´
        Functor.leftUnitor (Grothendieck.Œπ (functor L) X ‚ãô grothendieckProj L ‚ãô F)
  Iso.symm <| NatIso.ofComponents
    (fun X => HasColimit.isoOfNatIso (isoWhiskerRight (ŒπCompGrothendieckProj L X) F) ‚â™‚â´
      (leftKanExtensionObjIsoColimit L F X).symm)
    fun f => colimit.hom_ext (by simp)

end

section HasLeftKanExtension

variable [‚àÄ (F : C ‚•§ H), HasLeftKanExtension L F]

variable (H) in
/-- The left Kan extension functor `L.Lan` is left adjoint to the precomposition by `L`. -/
noncomputable def lanAdjunction : L.lan ‚ä£ (whiskeringLeft C D H).obj L :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun F G => homEquivOfIsLeftKanExtension _ (L.lanUnit.app F) G
      homEquiv_naturality_left_symm := fun {F‚ÇÅ F‚ÇÇ G} f Œ± =>
        hom_ext_of_isLeftKanExtension _ (L.lanUnit.app F‚ÇÅ) _ _ (by
          ext X
          dsimp [homEquivOfIsLeftKanExtension]
          rw [descOfIsLeftKanExtension_fac_app, NatTrans.comp_app, ‚Üê assoc]
          have h := congr_app (L.lanUnit.naturality f) X
          dsimp at h ‚ä¢
          rw [‚Üê h, assoc, descOfIsLeftKanExtension_fac_app])
      homEquiv_naturality_right := fun {F G‚ÇÅ G‚ÇÇ} Œ≤ f => by
        dsimp [homEquivOfIsLeftKanExtension]
        rw [assoc] }

variable (H) in
@[simp]
lemma lanAdjunction_unit : (L.lanAdjunction H).unit = L.lanUnit := by
  ext F : 2
  dsimp [lanAdjunction, homEquivOfIsLeftKanExtension]
  simp

lemma lanAdjunction_counit_app (G : D ‚•§ H) :
    (L.lanAdjunction H).counit.app G =
      descOfIsLeftKanExtension (L.lan.obj (L ‚ãô G)) (L.lanUnit.app (L ‚ãô G)) G (ùüô (L ‚ãô G)) :=
  rfl

@[reassoc (attr := simp)]
lemma lanUnit_app_whiskerLeft_lanAdjunction_counit_app (G : D ‚•§ H) :
    L.lanUnit.app (L ‚ãô G) ‚â´ whiskerLeft L ((L.lanAdjunction H).counit.app G) = ùüô (L ‚ãô G) := by
  simp [lanAdjunction_counit_app]

@[reassoc (attr := simp)]
lemma lanUnit_app_app_lanAdjunction_counit_app_app (G : D ‚•§ H) (X : C) :
    (L.lanUnit.app (L ‚ãô G)).app X ‚â´ ((L.lanAdjunction H).counit.app G).app (L.obj X) = ùüô _ :=
  congr_app (L.lanUnit_app_whiskerLeft_lanAdjunction_counit_app G) X

lemma isIso_lanAdjunction_counit_app_iff (G : D ‚•§ H) :
    IsIso ((L.lanAdjunction H).counit.app G) ‚Üî G.IsLeftKanExtension (ùüô (L ‚ãô G)) :=
  (isLeftKanExtension_iff_isIso _ (L.lanUnit.app (L ‚ãô G)) _ (by simp)).symm

lemma isIso_lanAdjunction_homEquiv_symm_iff {F : C ‚•§ H} {G : D ‚•§ H} (Œ± : F ‚ü∂ L ‚ãô G) :
    IsIso (((L.lanAdjunction H).homEquiv _ _).symm Œ±) ‚Üî G.IsLeftKanExtension Œ± :=
  (isLeftKanExtension_iff_isIso ((((L.lanAdjunction H).homEquiv _ _).symm Œ±))
    (L.lanUnit.app F) Œ± (by simp [lanAdjunction])).symm

/-- Composing the left Kan extension of `L : C ‚•§ D` with `colim` on shapes `D` is isomorphic
to `colim` on shapes `C`. -/
@[simps!]
noncomputable def lanCompColimIso [HasColimitsOfShape C H] [HasColimitsOfShape D H] :
    L.lan ‚ãô colim ‚âÖ colim (C := H) :=
  Iso.symm <| NatIso.ofComponents
    (fun G ‚Ü¶ (colimitIsoOfIsLeftKanExtension _ (L.lanUnit.app G)).symm)
    (fun f ‚Ü¶ colimit.hom_ext (fun i ‚Ü¶ by
      dsimp
      rw [Œπ_colimMap_assoc, Œπ_colimitIsoOfIsLeftKanExtension_inv,
        Œπ_colimitIsoOfIsLeftKanExtension_inv_assoc, Œπ_colimMap, ‚Üê assoc, ‚Üê assoc]
      congr 1
      exact congr_app (L.lanUnit.naturality f) i))

end HasLeftKanExtension

section HasPointwiseLeftKanExtension

variable (G : C ‚•§ H) [L.HasPointwiseLeftKanExtension G]

variable [HasColimitsOfShape D H]

instance : HasColimit (CostructuredArrow.grothendieckProj L ‚ãô G) :=
  hasColimit_of_hasColimit_fiberwiseColimit_of_hasColimit _

variable [HasColimitsOfShape C H]

/-- If `G : C ‚•§ H` admits a left Kan extension along a functor `L : C ‚•§ D` and `H` has colimits of
shape `C` and `D`, then the colimit of `G` is isomorphic to the colimit of a canonical functor
`Grothendieck (CostructuredArrow.functor L) ‚•§ H` induced by `L` and `G`. -/
noncomputable def colimitIsoColimitGrothendieck :
    colimit G ‚âÖ colimit (CostructuredArrow.grothendieckProj L ‚ãô G) := calc
  colimit G
    ‚âÖ colimit (leftKanExtension L G) :=
        (colimitIsoOfIsLeftKanExtension _ (L.leftKanExtensionUnit G)).symm
  _ ‚âÖ colimit (fiberwiseColimit (CostructuredArrow.grothendieckProj L ‚ãô G)) :=
        HasColimit.isoOfNatIso (leftKanExtensionIsoFiberwiseColimit L G)
  _ ‚âÖ colimit (CostructuredArrow.grothendieckProj L ‚ãô G) :=
        colimitFiberwiseColimitIso _

@[reassoc (attr := simp)]
lemma Œπ_colimitIsoColimitGrothendieck_inv (X : Grothendieck (CostructuredArrow.functor L)) :
    colimit.Œπ (CostructuredArrow.grothendieckProj L ‚ãô G) X ‚â´
      (colimitIsoColimitGrothendieck L G).inv =
    colimit.Œπ G ((CostructuredArrow.proj L X.base).obj X.fiber) := by
  simp [colimitIsoColimitGrothendieck]

@[reassoc (attr := simp)]
lemma Œπ_colimitIsoColimitGrothendieck_hom (X : C) :
    colimit.Œπ G X ‚â´ (colimitIsoColimitGrothendieck L G).hom =
    colimit.Œπ (CostructuredArrow.grothendieckProj L ‚ãô G) ‚ü®L.obj X, .mk (ùüô _)‚ü© := by
  rw [‚Üê Iso.eq_comp_inv]
  exact (Œπ_colimitIsoColimitGrothendieck_inv L G ‚ü®L.obj X, .mk (ùüô _)‚ü©).symm

end HasPointwiseLeftKanExtension


section

variable [Full L] [Faithful L]

instance (F : C ‚•§ H) (X : C) [HasPointwiseLeftKanExtension L F]
    [‚àÄ (F : C ‚•§ H), HasLeftKanExtension L F] :
    IsIso ((L.lanUnit.app F).app X) :=
  (isPointwiseLeftKanExtensionLeftKanExtensionUnit L F (L.obj X)).isIso_hom_app

instance (F : C ‚•§ H) [HasPointwiseLeftKanExtension L F]
    [‚àÄ (F : C ‚•§ H), HasLeftKanExtension L F] :
    IsIso (L.lanUnit.app F) :=
  NatIso.isIso_of_isIso_app _

instance coreflective [‚àÄ (F : C ‚•§ H), HasPointwiseLeftKanExtension L F] :
    IsIso (L.lanUnit (H := H)) := by
  apply NatIso.isIso_of_isIso_app _

instance (F : C ‚•§ H) [HasPointwiseLeftKanExtension L F]
    [‚àÄ (F : C ‚•§ H), HasLeftKanExtension L F] :
    IsIso ((L.lanAdjunction H).unit.app F) := by
  rw [lanAdjunction_unit]
  infer_instance

instance coreflective' [‚àÄ (F : C ‚•§ H), HasPointwiseLeftKanExtension L F] :
    IsIso (L.lanAdjunction H).unit := by
  apply NatIso.isIso_of_isIso_app _

end

end lan

section ran

section

variable [‚àÄ (F : C ‚•§ H), HasRightKanExtension L F]

/-- The right Kan extension functor `(C ‚•§ H) ‚•§ (D ‚•§ H)` along a functor `C ‚•§ D`. -/
noncomputable def ran : (C ‚•§ H) ‚•§ (D ‚•§ H) where
  obj F := rightKanExtension L F
  map {F‚ÇÅ F‚ÇÇ} œÜ := liftOfIsRightKanExtension _ (rightKanExtensionCounit L F‚ÇÇ) _
    (rightKanExtensionCounit L F‚ÇÅ ‚â´ œÜ)

/-- The natural transformation `L ‚ãô (L.lan).obj G ‚ü∂ L`. -/
noncomputable def ranCounit : L.ran ‚ãô (whiskeringLeft C D H).obj L ‚ü∂ (ùü≠ (C ‚•§ H)) where
  app F := rightKanExtensionCounit L F
  naturality {F‚ÇÅ F‚ÇÇ} œÜ := by ext; simp [ran]

instance (F : C ‚•§ H) : (L.ran.obj F).IsRightKanExtension (L.ranCounit.app F) := by
  dsimp [ran, ranCounit]
  infer_instance

/-- If there exists a pointwise right Kan extension of `F` along `L`,
then `L.ran.obj G` is a pointwise right Kan extension of `F`. -/
noncomputable def isPointwiseRightKanExtensionRanCounit
    (F : C ‚•§ H) [HasPointwiseRightKanExtension L F] :
    (RightExtension.mk _ (L.ranCounit.app F)).IsPointwiseRightKanExtension :=
  isPointwiseRightKanExtensionOfIsRightKanExtension (F := F) _ (L.ranCounit.app F)

/-- If a right Kan extension is pointwise, then evaluating it at an object is isomorphic to
taking a limit. -/
noncomputable def ranObjObjIsoLimit (F : C ‚•§ H) [HasPointwiseRightKanExtension L F] (X : D) :
    (L.ran.obj F).obj X ‚âÖ limit (StructuredArrow.proj X L ‚ãô F) :=
  RightExtension.IsPointwiseRightKanExtensionAt.isoLimit (F := F)
    (isPointwiseRightKanExtensionRanCounit L F X)

@[reassoc (attr := simp)]
lemma ranObjObjIsoLimit_hom_œÄ
    (F : C ‚•§ H) [HasPointwiseRightKanExtension L F] (X : D) (f : StructuredArrow X L) :
    (L.ranObjObjIsoLimit F X).hom ‚â´ limit.œÄ _ f =
    (L.ran.obj F).map f.hom ‚â´ (L.ranCounit.app F).app f.right := by
  simp [ranObjObjIsoLimit, ran, ranCounit]

@[reassoc (attr := simp)]
lemma ranObjObjIsoLimit_inv_œÄ
    (F : C ‚•§ H) [HasPointwiseRightKanExtension L F] (X : D) (f : StructuredArrow X L) :
    (L.ranObjObjIsoLimit F X).inv ‚â´ (L.ran.obj F).map f.hom ‚â´ (L.ranCounit.app F).app f.right =
    limit.œÄ _ f :=
  RightExtension.IsPointwiseRightKanExtensionAt.isoLimit_inv_œÄ (F := F)
    (isPointwiseRightKanExtensionRanCounit L F X) f

variable (H) in
/-- The right Kan extension functor `L.ran` is right adjoint to the
precomposition by `L`. -/
noncomputable def ranAdjunction : (whiskeringLeft C D H).obj L ‚ä£ L.ran :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun F G =>
        (homEquivOfIsRightKanExtension (Œ± := L.ranCounit.app G) _ F).symm
      homEquiv_naturality_right := fun {F G‚ÇÅ G‚ÇÇ} Œ≤ f ‚Ü¶
        hom_ext_of_isRightKanExtension _ (L.ranCounit.app G‚ÇÇ) _ _ (by
        ext X
        dsimp [homEquivOfIsRightKanExtension]
        rw [liftOfIsRightKanExtension_fac_app, NatTrans.comp_app, assoc]
        have h := congr_app (L.ranCounit.naturality f) X
        dsimp at h ‚ä¢
        rw [h, liftOfIsRightKanExtension_fac_app_assoc])
      homEquiv_naturality_left_symm := fun {F‚ÇÅ F‚ÇÇ G} Œ≤ f ‚Ü¶ by
        dsimp [homEquivOfIsRightKanExtension]
        rw [assoc] }

variable (H) in
@[simp]
lemma ranAdjunction_counit : (L.ranAdjunction H).counit = L.ranCounit := by
  ext F : 2
  dsimp [ranAdjunction, homEquivOfIsRightKanExtension]
  simp

lemma ranAdjunction_unit_app (G : D ‚•§ H) :
    (L.ranAdjunction H).unit.app G =
      liftOfIsRightKanExtension (L.ran.obj (L ‚ãô G)) (L.ranCounit.app (L ‚ãô G)) G (ùüô (L ‚ãô G)) :=
  rfl

@[reassoc (attr := simp)]
lemma ranCounit_app_whiskerLeft_ranAdjunction_unit_app (G : D ‚•§ H) :
    whiskerLeft L ((L.ranAdjunction H).unit.app G) ‚â´ L.ranCounit.app (L ‚ãô G) = ùüô (L ‚ãô G) := by
  simp [ranAdjunction_unit_app]

@[reassoc (attr := simp)]
lemma ranCounit_app_app_ranAdjunction_unit_app_app (G : D ‚•§ H) (X : C) :
    ((L.ranAdjunction H).unit.app G).app (L.obj X) ‚â´ (L.ranCounit.app (L ‚ãô G)).app X = ùüô _ :=
  congr_app (L.ranCounit_app_whiskerLeft_ranAdjunction_unit_app G) X

lemma isIso_ranAdjunction_unit_app_iff (G : D ‚•§ H) :
    IsIso ((L.ranAdjunction H).unit.app G) ‚Üî G.IsRightKanExtension (ùüô (L ‚ãô G)) :=
  (isRightKanExtension_iff_isIso _ (L.ranCounit.app (L ‚ãô G)) _ (by simp)).symm

lemma isIso_ranAdjunction_homEquiv_iff {F : C ‚•§ H} {G : D ‚•§ H} (Œ± : L ‚ãô G ‚ü∂ F) :
    IsIso (((L.ranAdjunction H).homEquiv _ _) Œ±) ‚Üî G.IsRightKanExtension Œ± :=
  (isRightKanExtension_iff_isIso ((((L.ranAdjunction H).homEquiv _ _) Œ±))
    (L.ranCounit.app F) Œ± (by simp [ranAdjunction])).symm

/-- Composing the right Kan extension of `L : C ‚•§ D` with `lim` on shapes `D` is isomorphic
to `lim` on shapes `C`. -/
@[simps!]
noncomputable def ranCompLimIso (L : C ‚•§ D) [‚àÄ (G : C ‚•§ H), L.HasRightKanExtension G]
    [HasLimitsOfShape C H] [HasLimitsOfShape D H] : L.ran ‚ãô lim ‚âÖ lim (C := H) :=
  NatIso.ofComponents
    (fun G ‚Ü¶ limitIsoOfIsRightKanExtension _ (L.ranCounit.app G))
    (fun f ‚Ü¶ limit.hom_ext (fun i ‚Ü¶ by
      dsimp
      rw [assoc, assoc, limMap_œÄ, limitIsoOfIsRightKanExtension_hom_œÄ_assoc,
        limitIsoOfIsRightKanExtension_hom_œÄ, limMap_œÄ_assoc]
      congr 1
      exact congr_app (L.ranCounit.naturality f) i))

end

section

variable [Full L] [Faithful L]

instance (F : C ‚•§ H) (X : C) [HasPointwiseRightKanExtension L F]
    [‚àÄ (F : C ‚•§ H), HasRightKanExtension L F] :
    IsIso ((L.ranCounit.app F).app X) :=
  (isPointwiseRightKanExtensionRanCounit L F (L.obj X)).isIso_hom_app

instance (F : C ‚•§ H) [HasPointwiseRightKanExtension L F]
    [‚àÄ (F : C ‚•§ H), HasRightKanExtension L F] :
    IsIso (L.ranCounit.app F) :=
  NatIso.isIso_of_isIso_app _

instance reflective [‚àÄ (F : C ‚•§ H), HasPointwiseRightKanExtension L F] :
    IsIso (L.ranCounit (H := H)) := by
  apply NatIso.isIso_of_isIso_app _

instance (F : C ‚•§ H) [HasPointwiseRightKanExtension L F]
    [‚àÄ (F : C ‚•§ H), HasRightKanExtension L F] :
    IsIso ((L.ranAdjunction H).counit.app F) := by
  rw [ranAdjunction_counit]
  infer_instance

instance reflective' [‚àÄ (F : C ‚•§ H), HasPointwiseRightKanExtension L F] :
    IsIso (L.ranAdjunction H).counit := by
  apply NatIso.isIso_of_isIso_app _

end

end ran

end Functor

end CategoryTheory
