/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Functor.KanExtension.Pointwise
import Mathlib.CategoryTheory.Limits.Shapes.Grothendieck
import Mathlib.CategoryTheory.Comma.StructuredArrow.Functor

/-! # The Kan extension functor

Given a functor `L : C â¥¤ D`, we define the left Kan extension functor
`L.lan : (C â¥¤ H) â¥¤ (D â¥¤ H)` which sends a functor `F : C â¥¤ H` to its
left Kan extension along `L`. This is defined if all `F` have such
a left Kan extension. It is shown that `L.lan` is the left adjoint to
the functor `(D â¥¤ H) â¥¤ (C â¥¤ H)` given by the precomposition
with `L` (see `Functor.lanAdjunction`).

Similarly, we define the right Kan extension functor
`L.ran : (C â¥¤ H) â¥¤ (D â¥¤ H)` which sends a functor `F : C â¥¤ H` to its
right Kan extension along `L`.

-/

namespace CategoryTheory

open Category Limits

namespace Functor

variable {C D : Type*} [Category C] [Category D] (L : C â¥¤ D) {H : Type*} [Category H]

section lan

section

variable [âˆ€ (F : C â¥¤ H), HasLeftKanExtension L F]

/-- The left Kan extension functor `(C â¥¤ H) â¥¤ (D â¥¤ H)` along a functor `C â¥¤ D`. -/
noncomputable def lan : (C â¥¤ H) â¥¤ (D â¥¤ H) where
  obj F := leftKanExtension L F
  map {Fâ‚ Fâ‚‚} Ï† := descOfIsLeftKanExtension _ (leftKanExtensionUnit L Fâ‚) _
    (Ï† â‰« leftKanExtensionUnit L Fâ‚‚)

/-- The natural transformation `F âŸ¶ L â‹™ (L.lan).obj G`. -/
noncomputable def lanUnit : (ğŸ­ (C â¥¤ H)) âŸ¶ L.lan â‹™ (whiskeringLeft C D H).obj L where
  app F := leftKanExtensionUnit L F
  naturality {Fâ‚ Fâ‚‚} Ï† := by ext; simp [lan]

instance (F : C â¥¤ H) : (L.lan.obj F).IsLeftKanExtension (L.lanUnit.app F) := by
  dsimp [lan, lanUnit]
  infer_instance

end

/-- If there exists a pointwise left Kan extension of `F` along `L`,
then `L.lan.obj G` is a pointwise left Kan extension of `F`. -/
noncomputable def isPointwiseLeftKanExtensionLeftKanExtensionUnit
    (F : C â¥¤ H) [HasPointwiseLeftKanExtension L F] :
    (LeftExtension.mk _ (L.leftKanExtensionUnit F)).IsPointwiseLeftKanExtension :=
  isPointwiseLeftKanExtensionOfIsLeftKanExtension (F := F) _ (leftKanExtensionUnit L F)

section

open CostructuredArrow

variable (F : C â¥¤ H) [HasPointwiseLeftKanExtension L F]

/-- If a left Kan extension is pointwise, then evaluating it at an object is isomorphic to
taking a colimit. -/
noncomputable def leftKanExtensionObjIsoColimit [HasLeftKanExtension L F] (X : D) :
    (L.leftKanExtension F).obj X â‰… colimit (proj L X â‹™ F) :=
  LeftExtension.IsPointwiseLeftKanExtensionAt.isoColimit (F := F)
    (isPointwiseLeftKanExtensionLeftKanExtensionUnit L F X)

@[reassoc (attr := simp)]
lemma Î¹_leftKanExtensionObjIsoColimit_inv [HasLeftKanExtension L F] (X : D)
    (f : CostructuredArrow L X) :
    colimit.Î¹ _ f â‰« (L.leftKanExtensionObjIsoColimit F X).inv =
    (L.leftKanExtensionUnit F).app f.left â‰« (L.leftKanExtension F).map f.hom := by
  simp [leftKanExtensionObjIsoColimit]

@[reassoc (attr := simp)]
lemma Î¹_leftKanExtensionObjIsoColimit_hom (X : D) (f : CostructuredArrow L X) :
    (L.leftKanExtensionUnit F).app f.left â‰« (L.leftKanExtension F).map f.hom â‰«
      (L.leftKanExtensionObjIsoColimit F X).hom =
    colimit.Î¹ (proj L X â‹™ F) f :=
  LeftExtension.IsPointwiseLeftKanExtensionAt.Î¹_isoColimit_hom (F := F)
    (isPointwiseLeftKanExtensionLeftKanExtensionUnit L F X) f

lemma leftKanExtensionUnit_leftKanExtension_map_leftKanExtensionObjIsoColimit_hom (X : D)
    (f : CostructuredArrow L X) :
    (leftKanExtensionUnit L F).app f.left â‰« (leftKanExtension L F).map f.hom â‰«
       (L.leftKanExtensionObjIsoColimit F X).hom =
    colimit.Î¹ (proj L X â‹™ F) f :=
  LeftExtension.IsPointwiseLeftKanExtensionAt.Î¹_isoColimit_hom (F := F)
    (isPointwiseLeftKanExtensionLeftKanExtensionUnit L F X) f

@[reassoc (attr := simp)]
lemma leftKanExtensionUnit_leftKanExtensionObjIsoColimit_hom (X : C) :
    (L.leftKanExtensionUnit F).app X â‰« (L.leftKanExtensionObjIsoColimit F (L.obj X)).hom =
    colimit.Î¹ (proj L (L.obj X) â‹™ F) (CostructuredArrow.mk (ğŸ™ _)) := by
  simpa using leftKanExtensionUnit_leftKanExtension_map_leftKanExtensionObjIsoColimit_hom L F
    (L.obj X) (CostructuredArrow.mk (ğŸ™ _))

@[instance]
theorem hasColimit_map_comp_Î¹_comp_grothendieckProj {X Y : D} (f : X âŸ¶ Y) :
    HasColimit ((functor L).map f â‹™ Grothendieck.Î¹ (functor L) Y â‹™ grothendieckProj L â‹™ F) :=
  hasColimit_of_iso (isoWhiskerRight (mapCompÎ¹CompGrothendieckProj L f) F)

@[deprecated (since := "2025-07-27")]
alias hasColimit_map_comp_Î¹_comp_grotendieckProj := hasColimit_map_comp_Î¹_comp_grothendieckProj

/-- The left Kan extension of `F : C â¥¤ H` along a functor `L : C â¥¤ D` is isomorphic to the
fiberwise colimit of the projection functor on the Grothendieck construction of the costructured
arrow category composed with `F`. -/
@[simps!]
noncomputable def leftKanExtensionIsoFiberwiseColimit [HasLeftKanExtension L F] :
    leftKanExtension L F â‰… fiberwiseColimit (grothendieckProj L â‹™ F) :=
  letI : âˆ€ X, HasColimit (Grothendieck.Î¹ (functor L) X â‹™ grothendieckProj L â‹™ F) :=
      fun X => hasColimit_of_iso <| Iso.symm <|
        isoWhiskerRight (eqToIso ((functor L).map_id X)) _ â‰ªâ‰«
        Functor.leftUnitor (Grothendieck.Î¹ (functor L) X â‹™ grothendieckProj L â‹™ F)
  Iso.symm <| NatIso.ofComponents
    (fun X => HasColimit.isoOfNatIso (isoWhiskerRight (Î¹CompGrothendieckProj L X) F) â‰ªâ‰«
      (leftKanExtensionObjIsoColimit L F X).symm)
    fun f => colimit.hom_ext (by simp)

end

section HasLeftKanExtension

variable [âˆ€ (F : C â¥¤ H), HasLeftKanExtension L F]

variable (H) in
/-- The left Kan extension functor `L.Lan` is left adjoint to the precomposition by `L`. -/
noncomputable def lanAdjunction : L.lan âŠ£ (whiskeringLeft C D H).obj L :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun F G => homEquivOfIsLeftKanExtension _ (L.lanUnit.app F) G
      homEquiv_naturality_left_symm := fun {Fâ‚ Fâ‚‚ G} f Î± =>
        hom_ext_of_isLeftKanExtension _ (L.lanUnit.app Fâ‚) _ _ (by
          ext X
          dsimp [homEquivOfIsLeftKanExtension]
          rw [descOfIsLeftKanExtension_fac_app, NatTrans.comp_app, â† assoc]
          have h := congr_app (L.lanUnit.naturality f) X
          dsimp at h âŠ¢
          rw [â† h, assoc, descOfIsLeftKanExtension_fac_app] )
      homEquiv_naturality_right := fun {F Gâ‚ Gâ‚‚} Î² f => by
        dsimp [homEquivOfIsLeftKanExtension]
        rw [assoc] }

variable (H) in
@[simp]
lemma lanAdjunction_unit : (L.lanAdjunction H).unit = L.lanUnit := by
  ext F : 2
  dsimp [lanAdjunction, homEquivOfIsLeftKanExtension]
  simp

lemma lanAdjunction_counit_app (G : D â¥¤ H) :
    (L.lanAdjunction H).counit.app G =
      descOfIsLeftKanExtension (L.lan.obj (L â‹™ G)) (L.lanUnit.app (L â‹™ G)) G (ğŸ™ (L â‹™ G)) :=
  rfl

@[reassoc (attr := simp)]
lemma lanUnit_app_whiskerLeft_lanAdjunction_counit_app (G : D â¥¤ H) :
    L.lanUnit.app (L â‹™ G) â‰« whiskerLeft L ((L.lanAdjunction H).counit.app G) = ğŸ™ (L â‹™ G) := by
  simp [lanAdjunction_counit_app]

@[reassoc (attr := simp)]
lemma lanUnit_app_app_lanAdjunction_counit_app_app (G : D â¥¤ H) (X : C) :
    (L.lanUnit.app (L â‹™ G)).app X â‰« ((L.lanAdjunction H).counit.app G).app (L.obj X) = ğŸ™ _ :=
  congr_app (L.lanUnit_app_whiskerLeft_lanAdjunction_counit_app G) X

lemma isIso_lanAdjunction_counit_app_iff (G : D â¥¤ H) :
    IsIso ((L.lanAdjunction H).counit.app G) â†” G.IsLeftKanExtension (ğŸ™ (L â‹™ G)) :=
  (isLeftKanExtension_iff_isIso _ (L.lanUnit.app (L â‹™ G)) _ (by simp)).symm

/-- Composing the left Kan extension of `L : C â¥¤ D` with `colim` on shapes `D` is isomorphic
to `colim` on shapes `C`. -/
@[simps!]
noncomputable def lanCompColimIso [HasColimitsOfShape C H] [HasColimitsOfShape D H] :
    L.lan â‹™ colim â‰… colim (C := H) :=
  Iso.symm <| NatIso.ofComponents
    (fun G â†¦ (colimitIsoOfIsLeftKanExtension _ (L.lanUnit.app G)).symm)
    (fun f â†¦ colimit.hom_ext (fun i â†¦ by
      dsimp
      rw [Î¹_colimMap_assoc, Î¹_colimitIsoOfIsLeftKanExtension_inv,
        Î¹_colimitIsoOfIsLeftKanExtension_inv_assoc, Î¹_colimMap, â† assoc, â† assoc]
      congr 1
      exact congr_app (L.lanUnit.naturality f) i))

end HasLeftKanExtension

section HasPointwiseLeftKanExtension

variable (G : C â¥¤ H) [L.HasPointwiseLeftKanExtension G]

variable [HasColimitsOfShape D H]

instance : HasColimit (CostructuredArrow.grothendieckProj L â‹™ G) :=
  hasColimit_of_hasColimit_fiberwiseColimit_of_hasColimit _

variable [HasColimitsOfShape C H]

/-- If `G : C â¥¤ H` admits a left Kan extension along a functor `L : C â¥¤ D` and `H` has colimits of
shape `C` and `D`, then the colimit of `G` is isomorphic to the colimit of a canonical functor
`Grothendieck (CostructuredArrow.functor L) â¥¤ H` induced by `L` and `G`. -/
noncomputable def colimitIsoColimitGrothendieck :
    colimit G â‰… colimit (CostructuredArrow.grothendieckProj L â‹™ G) := calc
  colimit G
    â‰… colimit (leftKanExtension L G) :=
        (colimitIsoOfIsLeftKanExtension _ (L.leftKanExtensionUnit G)).symm
  _ â‰… colimit (fiberwiseColimit (CostructuredArrow.grothendieckProj L â‹™ G)) :=
        HasColimit.isoOfNatIso (leftKanExtensionIsoFiberwiseColimit L G)
  _ â‰… colimit (CostructuredArrow.grothendieckProj L â‹™ G) :=
        colimitFiberwiseColimitIso _

@[reassoc (attr := simp)]
lemma Î¹_colimitIsoColimitGrothendieck_inv (X : Grothendieck (CostructuredArrow.functor L)) :
    colimit.Î¹ (CostructuredArrow.grothendieckProj L â‹™ G) X â‰«
      (colimitIsoColimitGrothendieck L G).inv =
    colimit.Î¹ G ((CostructuredArrow.proj L X.base).obj X.fiber) := by
  simp [colimitIsoColimitGrothendieck]

@[reassoc (attr := simp)]
lemma Î¹_colimitIsoColimitGrothendieck_hom (X : C) :
    colimit.Î¹ G X â‰« (colimitIsoColimitGrothendieck L G).hom =
    colimit.Î¹ (CostructuredArrow.grothendieckProj L â‹™ G) âŸ¨L.obj X, .mk (ğŸ™ _)âŸ© := by
  rw [â† Iso.eq_comp_inv]
  exact (Î¹_colimitIsoColimitGrothendieck_inv L G âŸ¨L.obj X, .mk (ğŸ™ _)âŸ©).symm

end HasPointwiseLeftKanExtension


section

variable [Full L] [Faithful L]

instance (F : C â¥¤ H) (X : C) [HasPointwiseLeftKanExtension L F]
    [âˆ€ (F : C â¥¤ H), HasLeftKanExtension L F] :
    IsIso ((L.lanUnit.app F).app X) :=
  (isPointwiseLeftKanExtensionLeftKanExtensionUnit L F (L.obj X)).isIso_hom_app

instance (F : C â¥¤ H) [HasPointwiseLeftKanExtension L F]
    [âˆ€ (F : C â¥¤ H), HasLeftKanExtension L F] :
    IsIso (L.lanUnit.app F) :=
  NatIso.isIso_of_isIso_app _

instance coreflective [âˆ€ (F : C â¥¤ H), HasPointwiseLeftKanExtension L F] :
    IsIso (L.lanUnit (H := H)) := by
  apply NatIso.isIso_of_isIso_app _

instance (F : C â¥¤ H) [HasPointwiseLeftKanExtension L F]
    [âˆ€ (F : C â¥¤ H), HasLeftKanExtension L F] :
    IsIso ((L.lanAdjunction H).unit.app F) := by
  rw [lanAdjunction_unit]
  infer_instance

instance coreflective' [âˆ€ (F : C â¥¤ H), HasPointwiseLeftKanExtension L F] :
    IsIso (L.lanAdjunction H).unit := by
  apply NatIso.isIso_of_isIso_app _

end

end lan

section ran

section

variable [âˆ€ (F : C â¥¤ H), HasRightKanExtension L F]

/-- The right Kan extension functor `(C â¥¤ H) â¥¤ (D â¥¤ H)` along a functor `C â¥¤ D`. -/
noncomputable def ran : (C â¥¤ H) â¥¤ (D â¥¤ H) where
  obj F := rightKanExtension L F
  map {Fâ‚ Fâ‚‚} Ï† := liftOfIsRightKanExtension _ (rightKanExtensionCounit L Fâ‚‚) _
    (rightKanExtensionCounit L Fâ‚ â‰« Ï†)

/-- The natural transformation `L â‹™ (L.lan).obj G âŸ¶ L`. -/
noncomputable def ranCounit : L.ran â‹™ (whiskeringLeft C D H).obj L âŸ¶ (ğŸ­ (C â¥¤ H)) where
  app F := rightKanExtensionCounit L F
  naturality {Fâ‚ Fâ‚‚} Ï† := by ext; simp [ran]

instance (F : C â¥¤ H) : (L.ran.obj F).IsRightKanExtension (L.ranCounit.app F) := by
  dsimp [ran, ranCounit]
  infer_instance

/-- If there exists a pointwise right Kan extension of `F` along `L`,
then `L.ran.obj G` is a pointwise right Kan extension of `F`. -/
noncomputable def isPointwiseRightKanExtensionRanCounit
    (F : C â¥¤ H) [HasPointwiseRightKanExtension L F] :
    (RightExtension.mk _ (L.ranCounit.app F)).IsPointwiseRightKanExtension :=
  isPointwiseRightKanExtensionOfIsRightKanExtension (F := F) _ (L.ranCounit.app F)

/-- If a right Kan extension is pointwise, then evaluating it at an object is isomorphic to
taking a limit. -/
noncomputable def ranObjObjIsoLimit (F : C â¥¤ H) [HasPointwiseRightKanExtension L F] (X : D) :
    (L.ran.obj F).obj X â‰… limit (StructuredArrow.proj X L â‹™ F) :=
  RightExtension.IsPointwiseRightKanExtensionAt.isoLimit (F := F)
    (isPointwiseRightKanExtensionRanCounit L F X)

@[reassoc (attr := simp)]
lemma ranObjObjIsoLimit_hom_Ï€
    (F : C â¥¤ H) [HasPointwiseRightKanExtension L F] (X : D) (f : StructuredArrow X L) :
    (L.ranObjObjIsoLimit F X).hom â‰« limit.Ï€ _ f =
    (L.ran.obj F).map f.hom â‰« (L.ranCounit.app F).app f.right := by
  simp [ranObjObjIsoLimit, ran, ranCounit]

@[reassoc (attr := simp)]
lemma ranObjObjIsoLimit_inv_Ï€
    (F : C â¥¤ H) [HasPointwiseRightKanExtension L F] (X : D) (f : StructuredArrow X L) :
    (L.ranObjObjIsoLimit F X).inv â‰« (L.ran.obj F).map f.hom â‰« (L.ranCounit.app F).app f.right =
    limit.Ï€ _ f :=
  RightExtension.IsPointwiseRightKanExtensionAt.isoLimit_inv_Ï€ (F := F)
    (isPointwiseRightKanExtensionRanCounit L F X) f

variable (H) in
/-- The right Kan extension functor `L.ran` is right adjoint to the
precomposition by `L`. -/
noncomputable def ranAdjunction : (whiskeringLeft C D H).obj L âŠ£ L.ran :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun F G =>
        (homEquivOfIsRightKanExtension (Î± := L.ranCounit.app G) _ F).symm
      homEquiv_naturality_right := fun {F Gâ‚ Gâ‚‚} Î² f â†¦
        hom_ext_of_isRightKanExtension _ (L.ranCounit.app Gâ‚‚) _ _ (by
        ext X
        dsimp [homEquivOfIsRightKanExtension]
        rw [liftOfIsRightKanExtension_fac_app, NatTrans.comp_app, assoc]
        have h := congr_app (L.ranCounit.naturality f) X
        dsimp at h âŠ¢
        rw [h, liftOfIsRightKanExtension_fac_app_assoc])
      homEquiv_naturality_left_symm := fun {Fâ‚ Fâ‚‚ G} Î² f â†¦ by
        dsimp [homEquivOfIsRightKanExtension]
        rw [assoc] }

variable (H) in
@[simp]
lemma ranAdjunction_counit : (L.ranAdjunction H).counit = L.ranCounit := by
  ext F : 2
  dsimp [ranAdjunction, homEquivOfIsRightKanExtension]
  simp

lemma ranAdjunction_unit_app (G : D â¥¤ H) :
    (L.ranAdjunction H).unit.app G =
      liftOfIsRightKanExtension (L.ran.obj (L â‹™ G)) (L.ranCounit.app (L â‹™ G)) G (ğŸ™ (L â‹™ G)) :=
  rfl

@[reassoc (attr := simp)]
lemma ranCounit_app_whiskerLeft_ranAdjunction_unit_app (G : D â¥¤ H) :
    whiskerLeft L ((L.ranAdjunction H).unit.app G) â‰« L.ranCounit.app (L â‹™ G) = ğŸ™ (L â‹™ G) := by
  simp [ranAdjunction_unit_app]

@[reassoc (attr := simp)]
lemma ranCounit_app_app_ranAdjunction_unit_app_app (G : D â¥¤ H) (X : C) :
    ((L.ranAdjunction H).unit.app G).app (L.obj X) â‰« (L.ranCounit.app (L â‹™ G)).app X = ğŸ™ _ :=
  congr_app (L.ranCounit_app_whiskerLeft_ranAdjunction_unit_app G) X

lemma isIso_ranAdjunction_unit_app_iff (G : D â¥¤ H) :
    IsIso ((L.ranAdjunction H).unit.app G) â†” G.IsRightKanExtension (ğŸ™ (L â‹™ G)) :=
  (isRightKanExtension_iff_isIso _ (L.ranCounit.app (L â‹™ G)) _ (by simp)).symm

/-- Composing the right Kan extension of `L : C â¥¤ D` with `lim` on shapes `D` is isomorphic
to `lim` on shapes `C`. -/
@[simps!]
noncomputable def ranCompLimIso (L : C â¥¤ D) [âˆ€ (G : C â¥¤ H), L.HasRightKanExtension G]
    [HasLimitsOfShape C H] [HasLimitsOfShape D H] : L.ran â‹™ lim â‰… lim (C := H) :=
  NatIso.ofComponents
    (fun G â†¦ limitIsoOfIsRightKanExtension _ (L.ranCounit.app G))
    (fun f â†¦ limit.hom_ext (fun i â†¦ by
      dsimp
      rw [assoc, assoc, limMap_Ï€, limitIsoOfIsRightKanExtension_hom_Ï€_assoc,
        limitIsoOfIsRightKanExtension_hom_Ï€, limMap_Ï€_assoc]
      congr 1
      exact congr_app (L.ranCounit.naturality f) i))

end

section

variable [Full L] [Faithful L]

instance (F : C â¥¤ H) (X : C) [HasPointwiseRightKanExtension L F]
    [âˆ€ (F : C â¥¤ H), HasRightKanExtension L F] :
    IsIso ((L.ranCounit.app F).app X) :=
  (isPointwiseRightKanExtensionRanCounit L F (L.obj X)).isIso_hom_app

instance (F : C â¥¤ H) [HasPointwiseRightKanExtension L F]
    [âˆ€ (F : C â¥¤ H), HasRightKanExtension L F] :
    IsIso (L.ranCounit.app F) :=
  NatIso.isIso_of_isIso_app _

instance reflective [âˆ€ (F : C â¥¤ H), HasPointwiseRightKanExtension L F] :
    IsIso (L.ranCounit (H := H)) := by
  apply NatIso.isIso_of_isIso_app _

instance (F : C â¥¤ H) [HasPointwiseRightKanExtension L F]
    [âˆ€ (F : C â¥¤ H), HasRightKanExtension L F] :
    IsIso ((L.ranAdjunction H).counit.app F) := by
  rw [ranAdjunction_counit]
  infer_instance

instance reflective' [âˆ€ (F : C â¥¤ H), HasPointwiseRightKanExtension L F] :
    IsIso (L.ranAdjunction H).counit := by
  apply NatIso.isIso_of_isIso_app _

end

end ran

end Functor

end CategoryTheory
