/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Functor.KanExtension.Pointwise

/-! # The Kan extension functor

Given a functor `F : C â¥¤ D`, we define the left Kan extension functor
`F.lan : (C â¥¤ E) â¥¤ (D â¥¤ E)` which sends a functor `G : C â¥¤ E` to its
left Kan extension along `F`. This is defined if all `G` have such
a left Kan extension. It is shown that `F.lan` is the left adjoint to
the functor `(D â¥¤ E) â¥¤ (C â¥¤ E)` given by the precomposition
with `F` (see `Functor.lanAdjunction`).

## TODO
- dualize the results for right Kan extensions
- refactor the file `CategoryTheory.Limits.KanExtension`

-/

namespace CategoryTheory

open Category

namespace Functor

variable {C D : Type*} [Category C] [Category D] (F : C â¥¤ D)
  {E : Type*} [Category E] [âˆ€ (G : C â¥¤ E), HasLeftKanExtension F G]

/-- The left Kan extension functor `(C â¥¤ E) â¥¤ (D â¥¤ E)` along a functor `C â¥¤ D`. -/
@[pp_dot]
noncomputable def lan : (C â¥¤ E) â¥¤ (D â¥¤ E) where
  obj G := leftKanExtension F G
  map {Gâ‚ Gâ‚‚} Ï† := descOfIsLeftKanExtension _ (leftKanExtensionUnit F Gâ‚) _
    (Ï† â‰« leftKanExtensionUnit F Gâ‚‚)

/-- The natural transformation `G âŸ¶ F â‹™ (F.lan).obj G`. -/
@[pp_dot]
noncomputable def lanUnit : (ðŸ­ (C â¥¤ E)) âŸ¶ F.lan â‹™ (whiskeringLeft C D E).obj F where
  app G := leftKanExtensionUnit F G
  naturality {Gâ‚ Gâ‚‚} Ï† := by ext; simp [lan]

instance (G : C â¥¤ E) : (F.lan.obj G).IsLeftKanExtension (F.lanUnit.app G) := by
  dsimp [lan, lanUnit]
  infer_instance

/-- If there exists a pointwise left Kan extension of `G` along `F`,
then `F.lan.obj G` is a pointwise left Kan extension of `G`. -/
noncomputable def isPointwiseLeftKanExtensionLanUnit
    (G : C â¥¤ E) [HasPointwiseLeftKanExtension F G] :
    (LeftExtension.mk _ (F.lanUnit.app G)).IsPointwiseLeftKanExtension :=
  isPointwiseLeftKanExtensionOfIsLeftKanExtension (F := G) _ (F.lanUnit.app G)

variable (E) in
/-- The left Kan extension functor `F.Lan` is left adjoint to the
precomposition by `F`. -/
@[pp_dot]
noncomputable def lanAdjunction : F.lan âŠ£ (whiskeringLeft C D E).obj F :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun G H => homEquivOfIsLeftKanExtension _ (F.lanUnit.app G) H
      homEquiv_naturality_left_symm := fun {Gâ‚ Gâ‚‚ H} f Î± =>
        hom_ext_of_isLeftKanExtension _  (F.lanUnit.app Gâ‚) _ _ (by
          ext X
          dsimp [homEquivOfIsLeftKanExtension]
          rw [descOfIsLeftKanExtension_fac_app, NatTrans.comp_app, â† assoc]
          have h := congr_app (F.lanUnit.naturality f) X
          dsimp at h âŠ¢
          rw [â† h, assoc, descOfIsLeftKanExtension_fac_app] )
      homEquiv_naturality_right := fun {G Hâ‚ Hâ‚‚} Î² f => by
        dsimp [homEquivOfIsLeftKanExtension]
        rw [assoc] }

variable (E) in
@[simp]
lemma lanAdjunction_unit :
    (F.lanAdjunction E).unit =
      lanUnit F := by
  ext G : 2
  dsimp [lanAdjunction, homEquivOfIsLeftKanExtension]
  simp

section

variable [Full F] [Faithful F]

instance (G : C â¥¤ E) (X : C) [HasPointwiseLeftKanExtension F G] :
    IsIso ((F.lanUnit.app G).app X) :=
  (isPointwiseLeftKanExtensionLanUnit F G (F.obj X)).isIso_hom_app

instance (G : C â¥¤ E) [HasPointwiseLeftKanExtension F G] :
    IsIso (F.lanUnit.app G) :=
  NatIso.isIso_of_isIso_app _

instance coreflective [âˆ€ (G : C â¥¤ E), HasPointwiseLeftKanExtension F G] :
    IsIso (F.lanUnit (E := E)) := by
  apply NatIso.isIso_of_isIso_app _

instance (G : C â¥¤ E) [HasPointwiseLeftKanExtension F G] :
    IsIso ((F.lanAdjunction E).unit.app G) := by
  rw [lanAdjunction_unit]
  infer_instance

instance coreflective' [âˆ€ (G : C â¥¤ E), HasPointwiseLeftKanExtension F G] :
    IsIso (F.lanAdjunction E).unit := by
  apply NatIso.isIso_of_isIso_app _

end

end Functor

end CategoryTheory
