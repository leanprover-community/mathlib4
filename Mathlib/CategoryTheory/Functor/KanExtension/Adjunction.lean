/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Functor.KanExtension.Pointwise

/-! # The Kan extension functor

Given a functor `L : C â¥¤ D`, we define the left Kan extension functor
`L.lan : (C â¥¤ H) â¥¤ (D â¥¤ H)` which sends a functor `F : C â¥¤ H` to its
left Kan extension along `L`. This is defined if all `F` have such
a left Kan extension. It is shown that `L.lan` is the left adjoint to
the functor `(D â¥¤ H) â¥¤ (C â¥¤ H)` given by the precomposition
with `L` (see `Functor.lanAdjunction`).

Similarly, we define the right Kan extension functor
`L.ran : (C â¥¤ H) â¥¤ (D â¥¤ H)` which sends a functor `F : C â¥¤ H` to its
right Kan extension along `L`.

-/

namespace CategoryTheory

open Category Limits

namespace Functor

variable {C D : Type*} [Category C] [Category D] (L : C â¥¤ D) {H : Type*} [Category H]

section lan

section

variable [âˆ€ (F : C â¥¤ H), HasLeftKanExtension L F]

/-- The left Kan extension functor `(C â¥¤ H) â¥¤ (D â¥¤ H)` along a functor `C â¥¤ D`. -/
noncomputable def lan : (C â¥¤ H) â¥¤ (D â¥¤ H) where
  obj F := leftKanExtension L F
  map {Fâ‚ Fâ‚‚} Ï† := descOfIsLeftKanExtension _ (leftKanExtensionUnit L Fâ‚) _
    (Ï† â‰« leftKanExtensionUnit L Fâ‚‚)

/-- The natural transformation `F âŸ¶ L â‹™ (L.lan).obj G`. -/
noncomputable def lanUnit : (ðŸ­ (C â¥¤ H)) âŸ¶ L.lan â‹™ (whiskeringLeft C D H).obj L where
  app F := leftKanExtensionUnit L F
  naturality {Fâ‚ Fâ‚‚} Ï† := by ext; simp [lan]

instance (F : C â¥¤ H) : (L.lan.obj F).IsLeftKanExtension (L.lanUnit.app F) := by
  dsimp [lan, lanUnit]
  infer_instance

/-- If there exists a pointwise left Kan extension of `F` along `L`,
then `L.lan.obj G` is a pointwise left Kan extension of `F`. -/
noncomputable def isPointwiseLeftKanExtensionLanUnit
    (F : C â¥¤ H) [HasPointwiseLeftKanExtension L F] :
    (LeftExtension.mk _ (L.lanUnit.app F)).IsPointwiseLeftKanExtension :=
  isPointwiseLeftKanExtensionOfIsLeftKanExtension (F := F) _ (L.lanUnit.app F)

variable (H) in
/-- The left Kan extension functor `L.Lan` is left adjoint to the
precomposition by `L`. -/
noncomputable def lanAdjunction : L.lan âŠ£ (whiskeringLeft C D H).obj L :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun F G => homEquivOfIsLeftKanExtension _ (L.lanUnit.app F) G
      homEquiv_naturality_left_symm := fun {Fâ‚ Fâ‚‚ G} f Î± =>
        hom_ext_of_isLeftKanExtension _ (L.lanUnit.app Fâ‚) _ _ (by
          ext X
          dsimp [homEquivOfIsLeftKanExtension]
          rw [descOfIsLeftKanExtension_fac_app, NatTrans.comp_app, â† assoc]
          have h := congr_app (L.lanUnit.naturality f) X
          dsimp at h âŠ¢
          rw [â† h, assoc, descOfIsLeftKanExtension_fac_app] )
      homEquiv_naturality_right := fun {F Gâ‚ Gâ‚‚} Î² f => by
        dsimp [homEquivOfIsLeftKanExtension]
        rw [assoc] }

variable (H) in
@[simp]
lemma lanAdjunction_unit : (L.lanAdjunction H).unit = L.lanUnit := by
  ext F : 2
  dsimp [lanAdjunction, homEquivOfIsLeftKanExtension]
  simp

lemma lanAdjunction_counit_app (G : D â¥¤ H) :
    (L.lanAdjunction H).counit.app G =
      descOfIsLeftKanExtension (L.lan.obj (L â‹™ G)) (L.lanUnit.app (L â‹™ G)) G (ðŸ™ (L â‹™ G)) :=
  rfl

@[reassoc (attr := simp)]
lemma lanUnit_app_whiskerLeft_lanAdjunction_counit_app (G : D â¥¤ H) :
    L.lanUnit.app (L â‹™ G) â‰« whiskerLeft L ((L.lanAdjunction H).counit.app G) = ðŸ™ (L â‹™ G) := by
  simp [lanAdjunction_counit_app]

@[reassoc (attr := simp)]
lemma lanUnit_app_app_lanAdjunction_counit_app_app (G : D â¥¤ H) (X : C) :
    (L.lanUnit.app (L â‹™ G)).app X â‰« ((L.lanAdjunction H).counit.app G).app (L.obj X) = ðŸ™ _ :=
  congr_app (L.lanUnit_app_whiskerLeft_lanAdjunction_counit_app G) X

lemma isIso_lanAdjunction_counit_app_iff (G : D â¥¤ H) :
    IsIso ((L.lanAdjunction H).counit.app G) â†” G.IsLeftKanExtension (ðŸ™ (L â‹™ G)) :=
  (isLeftKanExtension_iff_isIso _ (L.lanUnit.app (L â‹™ G)) _ (by simp)).symm

section Colim

variable (F' : D â¥¤ H) {L : C â¥¤ D} {F : C â¥¤ H} (Î± : F âŸ¶ L â‹™ F') [F'.IsLeftKanExtension Î±]

/-- Construct a cocone for a left Kan extension of `F` given a cocone for `F`. -/
@[simps]
noncomputable def coconeOfIsLeftKanExtension (c : Cocone F) : Cocone F' where
  pt := c.pt
  Î¹ := F'.descOfIsLeftKanExtension Î± _ c.Î¹

/-- If `c` is a colimit cocone, then `coconeOfIsLeftKanExtension Î± c` is a colimit cocone, too. -/
@[simps]
def isColimitCoconeOfIsLeftKanExtension {c : Cocone F} (hc : IsColimit c) :
    IsColimit (F'.coconeOfIsLeftKanExtension Î± c) where
  desc s := hc.desc (Cocone.mk _ (Î± â‰« whiskerLeft L s.Î¹))
  fac s := by
    have : F'.descOfIsLeftKanExtension Î± ((const D).obj c.pt) c.Î¹ â‰«
        (Functor.const _).map (hc.desc (Cocone.mk _ (Î± â‰« whiskerLeft L s.Î¹))) = s.Î¹ :=
      F'.hom_ext_of_isLeftKanExtension Î± _ _ (by aesop_cat)
    exact congr_app this
  uniq s m hm := hc.hom_ext (fun j â†¦ by
    have := hm (L.obj j)
    nth_rw 1 [â† F'.descOfIsLeftKanExtension_fac_app Î± ((const D).obj c.pt)]
    dsimp at this âŠ¢
    rw [assoc, this, IsColimit.fac, NatTrans.comp_app, whiskerLeft_app])

/-- Composing the left Kan extension of `L : C â¥¤ D` with `colim` on shapes `D` is isomorphic
to `colim` on shapes `C`. -/
@[simps!]
noncomputable def lanCompColimIso (L : C â¥¤ D) [âˆ€ (G : C â¥¤ H), L.HasLeftKanExtension G]
    [HasColimitsOfShape C H] [HasColimitsOfShape D H] : L.lan â‹™ colim â‰… colim (C := H) :=
  NatIso.ofComponents (fun F => IsColimit.coconePointUniqueUpToIso
    (colimit.isColimit (L.leftKanExtension F))
    (isColimitCoconeOfIsLeftKanExtension _ (leftKanExtensionUnit _ _) (colimit.isColimit F)))
    (fun _ => by
      simp only [colim_obj, colim_map, â† Iso.inv_comp_eq, â† assoc, â† Iso.eq_comp_inv]
      ext
      rw [Î¹_colimMap_assoc]
      simp only [comp_map, colim, colimMap, IsColimit.map, lan]
      rw [IsColimit.coconePointUniqueUpToIso_inv_desc]
      simp only [isColimitCoconeOfIsLeftKanExtension_desc, Cocones.precompose_obj_pt,
        colimit.cocone_x, Cocones.precompose_obj_Î¹, whiskerLeft_comp, colimit.isColimit_desc,
        colimit.Î¹_desc, NatTrans.comp_app, comp_obj, const_obj_obj, whiskerLeft_app,
        colimit.cocone_Î¹]
      conv_rhs => rw [â† assoc]
      rw [Iso.eq_comp_inv]
      simp only [colimit.Î¹, colimit.cocone, lan]
      rw [descOfIsLeftKanExtension_fac_app_assoc]
      simp)

end Colim

end

section

variable [Full L] [Faithful L]

instance (F : C â¥¤ H) (X : C) [HasPointwiseLeftKanExtension L F]
    [âˆ€ (F : C â¥¤ H), HasLeftKanExtension L F] :
    IsIso ((L.lanUnit.app F).app X) :=
  (isPointwiseLeftKanExtensionLanUnit L F (L.obj X)).isIso_hom_app

instance (F : C â¥¤ H) [HasPointwiseLeftKanExtension L F]
    [âˆ€ (F : C â¥¤ H), HasLeftKanExtension L F] :
    IsIso (L.lanUnit.app F) :=
  NatIso.isIso_of_isIso_app _

instance coreflective [âˆ€ (F : C â¥¤ H), HasPointwiseLeftKanExtension L F] :
    IsIso (L.lanUnit (H := H)) := by
  apply NatIso.isIso_of_isIso_app _

instance (F : C â¥¤ H) [HasPointwiseLeftKanExtension L F]
    [âˆ€ (F : C â¥¤ H), HasLeftKanExtension L F] :
    IsIso ((L.lanAdjunction H).unit.app F) := by
  rw [lanAdjunction_unit]
  infer_instance

instance coreflective' [âˆ€ (F : C â¥¤ H), HasPointwiseLeftKanExtension L F] :
    IsIso (L.lanAdjunction H).unit := by
  apply NatIso.isIso_of_isIso_app _

end

end lan

section ran

section

variable [âˆ€ (F : C â¥¤ H), HasRightKanExtension L F]

/-- The right Kan extension functor `(C â¥¤ H) â¥¤ (D â¥¤ H)` along a functor `C â¥¤ D`. -/
noncomputable def ran : (C â¥¤ H) â¥¤ (D â¥¤ H) where
  obj F := rightKanExtension L F
  map {Fâ‚ Fâ‚‚} Ï† := liftOfIsRightKanExtension _ (rightKanExtensionCounit L Fâ‚‚) _
    (rightKanExtensionCounit L Fâ‚ â‰« Ï†)

/-- The natural transformation `L â‹™ (L.lan).obj G âŸ¶ L`. -/
noncomputable def ranCounit : L.ran â‹™ (whiskeringLeft C D H).obj L âŸ¶ (ðŸ­ (C â¥¤ H)) where
  app F := rightKanExtensionCounit L F
  naturality {Fâ‚ Fâ‚‚} Ï† := by ext; simp [ran]

instance (F : C â¥¤ H) : (L.ran.obj F).IsRightKanExtension (L.ranCounit.app F) := by
  dsimp [ran, ranCounit]
  infer_instance

/-- If there exists a pointwise right Kan extension of `F` along `L`,
then `L.ran.obj G` is a pointwise right Kan extension of `F`. -/
noncomputable def isPointwiseRightKanExtensionRanCounit
    (F : C â¥¤ H) [HasPointwiseRightKanExtension L F] :
    (RightExtension.mk _ (L.ranCounit.app F)).IsPointwiseRightKanExtension :=
  isPointwiseRightKanExtensionOfIsRightKanExtension (F := F) _ (L.ranCounit.app F)

variable (H) in
/-- The right Kan extension functor `L.ran` is right adjoint to the
precomposition by `L`. -/
noncomputable def ranAdjunction : (whiskeringLeft C D H).obj L âŠ£ L.ran :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun F G =>
        (homEquivOfIsRightKanExtension (Î± := L.ranCounit.app G) F).symm
      homEquiv_naturality_right := fun {F Gâ‚ Gâ‚‚} Î² f â†¦
        hom_ext_of_isRightKanExtension _ (L.ranCounit.app Gâ‚‚) _ _ (by
        ext X
        dsimp [homEquivOfIsRightKanExtension]
        rw [liftOfIsRightKanExtension_fac_app, NatTrans.comp_app, assoc]
        have h := congr_app (L.ranCounit.naturality f) X
        dsimp at h âŠ¢
        rw [h, liftOfIsRightKanExtension_fac_app_assoc])
      homEquiv_naturality_left_symm := fun {Fâ‚ Fâ‚‚ G} Î² f â†¦ by
        dsimp [homEquivOfIsRightKanExtension]
        rw [assoc] }

variable (H) in
@[simp]
lemma ranAdjunction_counit : (L.ranAdjunction H).counit = L.ranCounit := by
  ext F : 2
  dsimp [ranAdjunction, homEquivOfIsRightKanExtension]
  simp

lemma ranAdjunction_unit_app (G : D â¥¤ H) :
    (L.ranAdjunction H).unit.app G =
      liftOfIsRightKanExtension (L.ran.obj (L â‹™ G)) (L.ranCounit.app (L â‹™ G)) G (ðŸ™ (L â‹™ G)) :=
  rfl

@[reassoc (attr := simp)]
lemma ranCounit_app_whiskerLeft_ranAdjunction_unit_app (G : D â¥¤ H) :
    whiskerLeft L ((L.ranAdjunction H).unit.app G) â‰« L.ranCounit.app (L â‹™ G) = ðŸ™ (L â‹™ G) := by
  simp [ranAdjunction_unit_app]

@[reassoc (attr := simp)]
lemma ranCounit_app_app_ranAdjunction_unit_app_app (G : D â¥¤ H) (X : C) :
    ((L.ranAdjunction H).unit.app G).app (L.obj X) â‰« (L.ranCounit.app (L â‹™ G)).app X = ðŸ™ _ :=
  congr_app (L.ranCounit_app_whiskerLeft_ranAdjunction_unit_app G) X

lemma isIso_ranAdjunction_unit_app_iff (G : D â¥¤ H) :
    IsIso ((L.ranAdjunction H).unit.app G) â†” G.IsRightKanExtension (ðŸ™ (L â‹™ G)) :=
  (isRightKanExtension_iff_isIso _ (L.ranCounit.app (L â‹™ G)) _ (by simp)).symm

section Lim

variable (F' : D â¥¤ H) {L : C â¥¤ D} {F : C â¥¤ H} (Î± : L â‹™ F' âŸ¶ F) [F'.IsRightKanExtension Î±]

/-- Construct a cone for a right Kan extension of `F` given a cone for `F`. -/
@[simps]
noncomputable def coneOfIsRightKanExtension (c : Cone F) : Cone F' where
  pt := c.pt
  Ï€ := F'.liftOfIsRightKanExtension Î± _ c.Ï€

/-- If `c` is a limit cone, then `coneOfIsRightKanExtension Î± c` is a limit cone, too. -/
@[simps]
def isLimitConeOfIsRightKanExtension {c : Cone F} (hc : IsLimit c) :
    IsLimit (F'.coneOfIsRightKanExtension Î± c) where
  lift s := hc.lift (Cone.mk _ (whiskerLeft L s.Ï€ â‰« Î±))
  fac s := by
    have : (Functor.const _).map (hc.lift (Cone.mk _ (whiskerLeft L s.Ï€ â‰« Î±))) â‰«
        F'.liftOfIsRightKanExtension Î± ((const D).obj c.pt) c.Ï€ = s.Ï€ :=
      F'.hom_ext_of_isRightKanExtension Î± _ _ (by aesop_cat)
    exact congr_app this
  uniq s m hm := hc.hom_ext (fun j â†¦ by
    have := hm (L.obj j)
    nth_rw 1 [â† F'.liftOfIsRightKanExtension_fac_app Î± ((const D).obj c.pt)]
    dsimp at this âŠ¢
    rw [â† assoc, this, IsLimit.fac, NatTrans.comp_app, whiskerLeft_app])

/-- Composing the right Kan extension of `L : C â¥¤ D` with `lim` on shapes `D` is isomorphic
to `lim` on shapes `C`. -/
@[simps!]
noncomputable def ranCompLimIso (L : C â¥¤ D) [âˆ€ (G : C â¥¤ H), L.HasRightKanExtension G]
    [HasLimitsOfShape C H] [HasLimitsOfShape D H] : L.ran â‹™ lim â‰… lim (C := H) :=
  NatIso.ofComponents (fun F => IsLimit.conePointUniqueUpToIso
    (limit.isLimit (L.rightKanExtension F))
    (isLimitConeOfIsRightKanExtension _ (rightKanExtensionCounit _ _) (limit.isLimit F)))
    (fun _ => by
      simp only [lim_obj, lim_map]
      ext c
      conv_rhs => rw [assoc, limMap_Ï€]
      simp only [comp_map, lim, limMap, IsLimit.map, ran]
      rw [IsLimit.lift_comp_conePointUniqueUpToIso_hom]
      simp only [limit.isLimit_lift, comp_obj, isLimitConeOfIsRightKanExtension_lift,
        Cones.postcompose_obj_pt, limit.cone_x, Cones.postcompose_obj_Ï€, whiskerLeft_comp,
        limit.lift_Ï€, assoc, liftOfIsRightKanExtension_fac, NatTrans.comp_app, const_obj_obj,
        whiskerLeft_app, limit.cone_Ï€]
      simp only [limit.Ï€, limit.cone, ran]
      erw [IsLimit.conePointUniqueUpToIso_hom_comp_assoc])

end Lim

end

section

variable [Full L] [Faithful L]

instance (F : C â¥¤ H) (X : C) [HasPointwiseRightKanExtension L F]
    [âˆ€ (F : C â¥¤ H), HasRightKanExtension L F] :
    IsIso ((L.ranCounit.app F).app X) :=
  (isPointwiseRightKanExtensionRanCounit L F (L.obj X)).isIso_hom_app

instance (F : C â¥¤ H) [HasPointwiseRightKanExtension L F]
    [âˆ€ (F : C â¥¤ H), HasRightKanExtension L F] :
    IsIso (L.ranCounit.app F) :=
  NatIso.isIso_of_isIso_app _

instance reflective [âˆ€ (F : C â¥¤ H), HasPointwiseRightKanExtension L F] :
    IsIso (L.ranCounit (H := H)) := by
  apply NatIso.isIso_of_isIso_app _

instance (F : C â¥¤ H) [HasPointwiseRightKanExtension L F]
    [âˆ€ (F : C â¥¤ H), HasRightKanExtension L F] :
    IsIso ((L.ranAdjunction H).counit.app F) := by
  rw [ranAdjunction_counit]
  infer_instance

instance reflective' [âˆ€ (F : C â¥¤ H), HasPointwiseRightKanExtension L F] :
    IsIso (L.ranAdjunction H).counit := by
  apply NatIso.isIso_of_isIso_app _

end

end ran

end Functor

end CategoryTheory
