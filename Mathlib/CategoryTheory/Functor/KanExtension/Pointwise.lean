/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Functor.KanExtension.Basic

/-!
# Pointwise Kan extensions

In this file, we define the notion of pointwise (left) Kan extension. Given two functors
`L : C ‚•§ D` and `F : C ‚•§ H`, and `E : LeftExtension L F`, we introduce a cocone
`E.coconeAt Y` for the functor `CostructuredArrow.proj L Y ‚ãô F : CostructuredArrow L Y ‚•§ H`
the point of which is `E.right.obj Y`, and the type `E.IsPointwiseLeftKanExtensionAt Y`
which expresses that `E.coconeAt Y` is colimit. When this holds for all `Y : D`,
we may say that `E` is a pointwise left Kan extension (`E.IsPointwiseLeftKanExtension`).

Conversely, when `CostructuredArrow.proj L Y ‚ãô F` has a colimit, we say that
`F` has a pointwise left Kan extension at `Y : D` (`HasPointwiseLeftKanExtensionAt L F Y`),
and if this holds for all `Y : D`, we construct a functor
`pointwiseLeftKanExtension L F : D ‚•§ H` and show it is a pointwise Kan extension.

A dual API for pointwise right Kan extension is also formalized.

## References
* https://ncatlab.org/nlab/show/Kan+extension

-/

namespace CategoryTheory

open Category Limits

namespace Functor

variable {C D D' H : Type*} [Category C] [Category D] [Category D'] [Category H]
  (L : C ‚•§ D) (L' : C ‚•§ D') (F : C ‚•§ H)

/-- The condition that a functor `F` has a pointwise left Kan extension along `L` at `Y`.
It means that the functor `CostructuredArrow.proj L Y ‚ãô F : CostructuredArrow L Y ‚•§ H`
has a colimit. -/
abbrev HasPointwiseLeftKanExtensionAt (Y : D) :=
  HasColimit (CostructuredArrow.proj L Y ‚ãô F)

/-- The condition that a functor `F` has a pointwise left Kan extension along `L`: it means
that it has a pointwise left Kan extension at any object. -/
abbrev HasPointwiseLeftKanExtension := ‚àÄ (Y : D), HasPointwiseLeftKanExtensionAt L F Y

/-- The condition that a functor `F` has a pointwise right Kan extension along `L` at `Y`.
It means that the functor `StructuredArrow.proj Y L ‚ãô F : StructuredArrow Y L ‚•§ H`
has a limit. -/
abbrev HasPointwiseRightKanExtensionAt (Y : D) :=
  HasLimit (StructuredArrow.proj Y L ‚ãô F)

/-- The condition that a functor `F` has a pointwise right Kan extension along `L`: it means
that it has a pointwise right Kan extension at any object. -/
abbrev HasPointwiseRightKanExtension := ‚àÄ (Y : D), HasPointwiseRightKanExtensionAt L F Y

lemma hasPointwiseLeftKanExtensionAt_iff_of_iso {Y‚ÇÅ Y‚ÇÇ : D} (e : Y‚ÇÅ ‚âÖ Y‚ÇÇ) :
    HasPointwiseLeftKanExtensionAt L F Y‚ÇÅ ‚Üî
      HasPointwiseLeftKanExtensionAt L F Y‚ÇÇ := by
  revert Y‚ÇÅ Y‚ÇÇ e
  suffices ‚àÄ ‚¶ÉY‚ÇÅ Y‚ÇÇ : D‚¶Ñ (_ : Y‚ÇÅ ‚âÖ Y‚ÇÇ) [HasPointwiseLeftKanExtensionAt L F Y‚ÇÅ],
      HasPointwiseLeftKanExtensionAt L F Y‚ÇÇ from
    fun Y‚ÇÅ Y‚ÇÇ e => ‚ü®fun _ => this e, fun _ => this e.symm‚ü©
  intro Y‚ÇÅ Y‚ÇÇ e _
  change HasColimit ((CostructuredArrow.mapIso e.symm).functor ‚ãô CostructuredArrow.proj L Y‚ÇÅ ‚ãô F)
  infer_instance

lemma hasPointwiseRightKanExtensionAt_iff_of_iso {Y‚ÇÅ Y‚ÇÇ : D} (e : Y‚ÇÅ ‚âÖ Y‚ÇÇ) :
    HasPointwiseRightKanExtensionAt L F Y‚ÇÅ ‚Üî
      HasPointwiseRightKanExtensionAt L F Y‚ÇÇ := by
  revert Y‚ÇÅ Y‚ÇÇ e
  suffices ‚àÄ ‚¶ÉY‚ÇÅ Y‚ÇÇ : D‚¶Ñ (_ : Y‚ÇÅ ‚âÖ Y‚ÇÇ) [HasPointwiseRightKanExtensionAt L F Y‚ÇÅ],
      HasPointwiseRightKanExtensionAt L F Y‚ÇÇ from
    fun Y‚ÇÅ Y‚ÇÇ e => ‚ü®fun _ => this e, fun _ => this e.symm‚ü©
  intro Y‚ÇÅ Y‚ÇÇ e _
  change HasLimit ((StructuredArrow.mapIso e.symm).functor ‚ãô StructuredArrow.proj Y‚ÇÅ L ‚ãô F)
  infer_instance

variable {L} in
/-- `HasPointwiseLeftKanExtensionAt` is invariant when we replace `L` by an equivalent functor. -/
lemma hasPointwiseLeftKanExtensionAt_iff_of_natIso {L' : C ‚•§ D} (e : L ‚âÖ L') (Y : D) :
    HasPointwiseLeftKanExtensionAt L F Y ‚Üî
      HasPointwiseLeftKanExtensionAt L' F Y := by
  revert L L' e
  suffices ‚àÄ ‚¶ÉL L' : C ‚•§ D‚¶Ñ (_ : L ‚âÖ L') [HasPointwiseLeftKanExtensionAt L F Y],
      HasPointwiseLeftKanExtensionAt L' F Y from
    fun L L' e => ‚ü®fun _ => this e, fun _ => this e.symm‚ü©
  intro L L' e _
  let Œ¶ : CostructuredArrow L' Y ‚âå CostructuredArrow L Y := Comma.mapLeftIso _ e.symm
  let e' : CostructuredArrow.proj L' Y ‚ãô F ‚âÖ
    Œ¶.functor ‚ãô CostructuredArrow.proj L Y ‚ãô F := Iso.refl _
  exact hasColimit_of_iso e'

variable {L} in
/-- `HasPointwiseRightKanExtensionAt` is invariant when we replace `L` by an equivalent functor. -/
lemma hasPointwiseRightKanExtensionAt_iff_of_natIso {L' : C ‚•§ D} (e : L ‚âÖ L') (Y : D) :
    HasPointwiseRightKanExtensionAt L F Y ‚Üî
      HasPointwiseRightKanExtensionAt L' F Y := by
  revert L L' e
  suffices ‚àÄ ‚¶ÉL L' : C ‚•§ D‚¶Ñ (_ : L ‚âÖ L') [HasPointwiseRightKanExtensionAt L F Y],
      HasPointwiseRightKanExtensionAt L' F Y from
    fun L L' e => ‚ü®fun _ => this e, fun _ => this e.symm‚ü©
  intro L L' e _
  let Œ¶ : StructuredArrow Y L' ‚âå StructuredArrow Y L := Comma.mapRightIso _ e.symm
  let e' : StructuredArrow.proj Y L' ‚ãô F ‚âÖ
    Œ¶.functor ‚ãô StructuredArrow.proj Y L ‚ãô F := Iso.refl _
  exact hasLimit_of_iso e'.symm

lemma hasPointwiseLeftKanExtensionAt_of_equivalence
    (E : D ‚âå D') (eL : L ‚ãô E.functor ‚âÖ L') (Y : D) (Y' : D') (e : E.functor.obj Y ‚âÖ Y')
    [HasPointwiseLeftKanExtensionAt L F Y] :
    HasPointwiseLeftKanExtensionAt L' F Y' := by
  rw [‚Üê hasPointwiseLeftKanExtensionAt_iff_of_natIso F eL,
    hasPointwiseLeftKanExtensionAt_iff_of_iso _ F e.symm]
  let Œ¶ := CostructuredArrow.post L E.functor Y
  have : HasColimit ((asEquivalence Œ¶).functor ‚ãô
    CostructuredArrow.proj (L ‚ãô E.functor) (E.functor.obj Y) ‚ãô F) :=
    (inferInstance : HasPointwiseLeftKanExtensionAt L F Y)
  exact hasColimit_of_equivalence_comp (asEquivalence Œ¶)

lemma hasPointwiseLeftKanExtensionAt_iff_of_equivalence
    (E : D ‚âå D') (eL : L ‚ãô E.functor ‚âÖ L') (Y : D) (Y' : D') (e : E.functor.obj Y ‚âÖ Y') :
    HasPointwiseLeftKanExtensionAt L F Y ‚Üî
      HasPointwiseLeftKanExtensionAt L' F Y' := by
  constructor
  ¬∑ intro
    exact hasPointwiseLeftKanExtensionAt_of_equivalence L L' F E eL Y Y' e
  ¬∑ intro
    exact hasPointwiseLeftKanExtensionAt_of_equivalence L' L F E.symm
      (isoWhiskerRight eL.symm _ ‚â™‚â´ Functor.associator _ _ _ ‚â™‚â´
        isoWhiskerLeft L E.unitIso.symm ‚â™‚â´ L.rightUnitor) Y' Y
      (E.inverse.mapIso e.symm ‚â™‚â´ E.unitIso.symm.app Y)

lemma hasPointwiseRightKanExtensionAt_of_equivalence
    (E : D ‚âå D') (eL : L ‚ãô E.functor ‚âÖ L') (Y : D) (Y' : D') (e : E.functor.obj Y ‚âÖ Y')
    [HasPointwiseRightKanExtensionAt L F Y] :
    HasPointwiseRightKanExtensionAt L' F Y' := by
  rw [‚Üê hasPointwiseRightKanExtensionAt_iff_of_natIso F eL,
    hasPointwiseRightKanExtensionAt_iff_of_iso _ F e.symm]
  let Œ¶ := StructuredArrow.post Y L E.functor
  have : HasLimit ((asEquivalence Œ¶).functor ‚ãô
    StructuredArrow.proj (E.functor.obj Y) (L ‚ãô E.functor) ‚ãô F) :=
    (inferInstance : HasPointwiseRightKanExtensionAt L F Y)
  exact hasLimit_of_equivalence_comp (asEquivalence Œ¶)

lemma hasPointwiseRightKanExtensionAt_iff_of_equivalence
    (E : D ‚âå D') (eL : L ‚ãô E.functor ‚âÖ L') (Y : D) (Y' : D') (e : E.functor.obj Y ‚âÖ Y') :
    HasPointwiseRightKanExtensionAt L F Y ‚Üî
      HasPointwiseRightKanExtensionAt L' F Y' := by
  constructor
  ¬∑ intro
    exact hasPointwiseRightKanExtensionAt_of_equivalence L L' F E eL Y Y' e
  ¬∑ intro
    exact hasPointwiseRightKanExtensionAt_of_equivalence L' L F E.symm
      (isoWhiskerRight eL.symm _ ‚â™‚â´ Functor.associator _ _ _ ‚â™‚â´
        isoWhiskerLeft L E.unitIso.symm ‚â™‚â´ L.rightUnitor) Y' Y
      (E.inverse.mapIso e.symm ‚â™‚â´ E.unitIso.symm.app Y)

namespace LeftExtension

variable {F L}
variable (E : LeftExtension L F)

/-- The cocone for `CostructuredArrow.proj L Y ‚ãô F` attached to `E : LeftExtension L F`.
The point of this cocone is `E.right.obj Y` -/
@[simps]
def coconeAt (Y : D) : Cocone (CostructuredArrow.proj L Y ‚ãô F) where
  pt := E.right.obj Y
  Œπ :=
    { app := fun g => E.hom.app g.left ‚â´ E.right.map g.hom
      naturality := fun g‚ÇÅ g‚ÇÇ œÜ => by
        dsimp
        rw [‚Üê CostructuredArrow.w œÜ]
        simp only [NatTrans.naturality_assoc, Functor.comp_map,
          Functor.map_comp, comp_id] }

variable (L F) in
/-- The cocones for `CostructuredArrow.proj L Y ‚ãô F`, as a functor from `LeftExtension L F`. -/
@[simps]
def coconeAtFunctor (Y : D) :
    LeftExtension L F ‚•§ Cocone (CostructuredArrow.proj L Y ‚ãô F) where
  obj E := E.coconeAt Y
  map {E E'} œÜ := CoconeMorphism.mk (œÜ.right.app Y) (fun G => by
    dsimp
    rw [‚Üê StructuredArrow.w œÜ]
    simp)

/-- A left extension `E : LeftExtension L F` is a pointwise left Kan extension at `Y` when
`E.coconeAt Y` is a colimit cocone. -/
def IsPointwiseLeftKanExtensionAt (Y : D) := IsColimit (E.coconeAt Y)

variable {E} in
lemma IsPointwiseLeftKanExtensionAt.hasPointwiseLeftKanExtensionAt
    {Y : D} (h : E.IsPointwiseLeftKanExtensionAt Y) :
    HasPointwiseLeftKanExtensionAt L F Y := ‚ü®_, h‚ü©

lemma IsPointwiseLeftKanExtensionAt.isIso_hom_app
    {X : C} (h : E.IsPointwiseLeftKanExtensionAt (L.obj X)) [L.Full] [L.Faithful] :
    IsIso (E.hom.app X) := by
  simpa using h.isIso_Œπ_app_of_isTerminal _ CostructuredArrow.mkIdTerminal

/-- The condition of being a pointwise left Kan extension at an object `Y` is
unchanged by replacing `Y` by an isomorphic object `Y'`. -/
def isPointwiseLeftKanExtensionAtOfIso'
    {Y : D} (hY : E.IsPointwiseLeftKanExtensionAt Y) {Y' : D} (e : Y ‚âÖ Y') :
    E.IsPointwiseLeftKanExtensionAt Y' :=
  IsColimit.ofIsoColimit (hY.whiskerEquivalence (CostructuredArrow.mapIso e.symm))
    (Cocones.ext (E.right.mapIso e))

/-- The condition of being a pointwise left Kan extension at an object `Y` is
unchanged by replacing `Y` by an isomorphic object `Y'`. -/
def isPointwiseLeftKanExtensionAtEquivOfIso' {Y Y' : D} (e : Y ‚âÖ Y') :
    E.IsPointwiseLeftKanExtensionAt Y ‚âÉ E.IsPointwiseLeftKanExtensionAt Y' where
  toFun h := E.isPointwiseLeftKanExtensionAtOfIso' h e
  invFun h := E.isPointwiseLeftKanExtensionAtOfIso' h e.symm
  left_inv h := by
    dsimp only [IsPointwiseLeftKanExtensionAt]
    apply Subsingleton.elim
  right_inv h := by
    dsimp only [IsPointwiseLeftKanExtensionAt]
    apply Subsingleton.elim

namespace IsPointwiseLeftKanExtensionAt

variable {E} {Y : D} (h : E.IsPointwiseLeftKanExtensionAt Y)
  [HasColimit (CostructuredArrow.proj L Y ‚ãô F)]

/-- A pointwise left Kan extension of `F` along `L` applied to an object `Y` is isomorphic to
`colimit (CostructuredArrow.proj L Y ‚ãô F)`. -/
noncomputable def isoColimit :
    E.right.obj Y ‚âÖ colimit (CostructuredArrow.proj L Y ‚ãô F) :=
  h.coconePointUniqueUpToIso (colimit.isColimit _)

@[reassoc (attr := simp)]
lemma Œπ_isoColimit_inv (g : CostructuredArrow L Y) :
    colimit.Œπ _ g ‚â´ h.isoColimit.inv = E.hom.app g.left ‚â´ E.right.map g.hom :=
  IsColimit.comp_coconePointUniqueUpToIso_inv _ _ _

@[reassoc (attr := simp)]
lemma Œπ_isoColimit_hom (g : CostructuredArrow L Y) :
    E.hom.app g.left ‚â´ E.right.map g.hom ‚â´ h.isoColimit.hom =
      colimit.Œπ (CostructuredArrow.proj L Y ‚ãô F) g := by
  simpa using h.comp_coconePointUniqueUpToIso_hom (colimit.isColimit _) g

end IsPointwiseLeftKanExtensionAt

/-- A left extension `E : LeftExtension L F` is a pointwise left Kan extension when
it is a pointwise left Kan extension at any object. -/
abbrev IsPointwiseLeftKanExtension := ‚àÄ (Y : D), E.IsPointwiseLeftKanExtensionAt Y

variable {E E'}

/-- If two left extensions `E` and `E'` are isomorphic, `E` is a pointwise
left Kan extension at `Y` iff `E'` is. -/
def isPointwiseLeftKanExtensionAtEquivOfIso (e : E ‚âÖ E') (Y : D) :
    E.IsPointwiseLeftKanExtensionAt Y ‚âÉ E'.IsPointwiseLeftKanExtensionAt Y :=
  IsColimit.equivIsoColimit ((coconeAtFunctor L F Y).mapIso e)

/-- If two left extensions `E` and `E'` are isomorphic, `E` is a pointwise
left Kan extension iff `E'` is. -/
def isPointwiseLeftKanExtensionEquivOfIso (e : E ‚âÖ E') :
    E.IsPointwiseLeftKanExtension ‚âÉ E'.IsPointwiseLeftKanExtension where
  toFun h := fun Y => (isPointwiseLeftKanExtensionAtEquivOfIso e Y) (h Y)
  invFun h := fun Y => (isPointwiseLeftKanExtensionAtEquivOfIso e Y).symm (h Y)
  left_inv h := by simp
  right_inv h := by simp

variable (h : E.IsPointwiseLeftKanExtension)
include h

lemma IsPointwiseLeftKanExtension.hasPointwiseLeftKanExtension :
    HasPointwiseLeftKanExtension L F :=
  fun Y => (h Y).hasPointwiseLeftKanExtensionAt

/-- The (unique) morphism from a pointwise left Kan extension. -/
def IsPointwiseLeftKanExtension.homFrom (G : LeftExtension L F) : E ‚ü∂ G :=
  StructuredArrow.homMk
    { app := fun Y => (h Y).desc (LeftExtension.coconeAt G Y)
      naturality := fun Y‚ÇÅ Y‚ÇÇ œÜ => (h Y‚ÇÅ).hom_ext (fun X => by
        rw [(h Y‚ÇÅ).fac_assoc (coconeAt G Y‚ÇÅ) X]
        simpa using (h Y‚ÇÇ).fac (coconeAt G Y‚ÇÇ) ((CostructuredArrow.map œÜ).obj X)) }
    (by
      ext X
      simpa using (h (L.obj X)).fac (LeftExtension.coconeAt G _) (CostructuredArrow.mk (ùüô _)))

lemma IsPointwiseLeftKanExtension.hom_ext
    {G : LeftExtension L F} {f‚ÇÅ f‚ÇÇ : E ‚ü∂ G} : f‚ÇÅ = f‚ÇÇ := by
  ext Y
  apply (h Y).hom_ext
  intro X
  have eq‚ÇÅ := congr_app (StructuredArrow.w f‚ÇÅ) X.left
  have eq‚ÇÇ := congr_app (StructuredArrow.w f‚ÇÇ) X.left
  dsimp at eq‚ÇÅ eq‚ÇÇ ‚ä¢
  simp only [assoc, NatTrans.naturality]
  rw [reassoc_of% eq‚ÇÅ, reassoc_of% eq‚ÇÇ]

/-- A pointwise left Kan extension is universal, i.e. it is a left Kan extension. -/
def IsPointwiseLeftKanExtension.isUniversal : E.IsUniversal :=
  IsInitial.ofUniqueHom h.homFrom (fun _ _ => h.hom_ext)

lemma IsPointwiseLeftKanExtension.isLeftKanExtension :
    E.right.IsLeftKanExtension E.hom where
  nonempty_isUniversal := ‚ü®h.isUniversal‚ü©

lemma IsPointwiseLeftKanExtension.hasLeftKanExtension :
    HasLeftKanExtension L F :=
  have := h.isLeftKanExtension
  HasLeftKanExtension.mk E.right E.hom

lemma IsPointwiseLeftKanExtension.isIso_hom [L.Full] [L.Faithful] :
    IsIso (E.hom) :=
  have := fun X => (h (L.obj X)).isIso_hom_app
  NatIso.isIso_of_isIso_app ..

end LeftExtension

namespace RightExtension

variable {F L}
variable (E E' : RightExtension L F)

/-- The cone for `StructuredArrow.proj Y L ‚ãô F` attached to `E : RightExtension L F`.
The point of this cone is `E.left.obj Y` -/
@[simps]
def coneAt (Y : D) : Cone (StructuredArrow.proj Y L ‚ãô F) where
  pt := E.left.obj Y
  œÄ :=
    { app := fun g ‚Ü¶ E.left.map g.hom ‚â´ E.hom.app g.right
      naturality := fun g‚ÇÅ g‚ÇÇ œÜ ‚Ü¶ by
        dsimp
        rw [assoc, id_comp, ‚Üê StructuredArrow.w œÜ, Functor.map_comp, assoc]
        congr 1
        apply E.hom.naturality }

variable (L F) in
/-- The cones for `StructuredArrow.proj Y L ‚ãô F`, as a functor from `RightExtension L F`. -/
@[simps]
def coneAtFunctor (Y : D) :
    RightExtension L F ‚•§ Cone (StructuredArrow.proj Y L ‚ãô F) where
  obj E := E.coneAt Y
  map {E E'} œÜ := ConeMorphism.mk (œÜ.left.app Y) (fun G ‚Ü¶ by
    dsimp
    rw [‚Üê CostructuredArrow.w œÜ]
    simp)

/-- A right extension `E : RightExtension L F` is a pointwise right Kan extension at `Y` when
`E.coneAt Y` is a limit cone. -/
def IsPointwiseRightKanExtensionAt (Y : D) := IsLimit (E.coneAt Y)

variable {E} in
lemma IsPointwiseRightKanExtensionAt.hasPointwiseRightKanExtensionAt
    {Y : D} (h : E.IsPointwiseRightKanExtensionAt Y) :
    HasPointwiseRightKanExtensionAt L F Y := ‚ü®_, h‚ü©

lemma IsPointwiseRightKanExtensionAt.isIso_hom_app
    {X : C} (h : E.IsPointwiseRightKanExtensionAt (L.obj X)) [L.Full] [L.Faithful] :
    IsIso (E.hom.app X) := by
  simpa using h.isIso_œÄ_app_of_isInitial _ StructuredArrow.mkIdInitial

/-- The condition of being a pointwise right Kan extension at an object `Y` is
unchanged by replacing `Y` by an isomorphic object `Y'`. -/
def isPointwiseRightKanExtensionAtOfIso'
    {Y : D} (hY : E.IsPointwiseRightKanExtensionAt Y) {Y' : D} (e : Y ‚âÖ Y') :
    E.IsPointwiseRightKanExtensionAt Y' :=
  IsLimit.ofIsoLimit (hY.whiskerEquivalence (StructuredArrow.mapIso e.symm))
    (Cones.ext (E.left.mapIso e))

/-- The condition of being a pointwise right Kan extension at an object `Y` is
unchanged by replacing `Y` by an isomorphic object `Y'`. -/
def isPointwiseRightKanExtensionAtEquivOfIso' {Y Y' : D} (e : Y ‚âÖ Y') :
    E.IsPointwiseRightKanExtensionAt Y ‚âÉ E.IsPointwiseRightKanExtensionAt Y' where
  toFun h := E.isPointwiseRightKanExtensionAtOfIso' h e
  invFun h := E.isPointwiseRightKanExtensionAtOfIso' h e.symm
  left_inv h := by
    dsimp only [IsPointwiseRightKanExtensionAt]
    apply Subsingleton.elim
  right_inv h := by
    dsimp only [IsPointwiseRightKanExtensionAt]
    apply Subsingleton.elim

namespace IsPointwiseRightKanExtensionAt

variable {E} {Y : D} (h : E.IsPointwiseRightKanExtensionAt Y)
  [HasLimit (StructuredArrow.proj Y L ‚ãô F)]

/-- A pointwise right Kan extension of `F` along `L` applied to an object `Y` is isomorphic to
`limit (StructuredArrow.proj Y L ‚ãô F)`. -/
noncomputable def isoLimit :
    E.left.obj Y ‚âÖ limit (StructuredArrow.proj Y L ‚ãô F) :=
  h.conePointUniqueUpToIso (limit.isLimit _)

@[reassoc (attr := simp)]
lemma isoLimit_hom_œÄ (g : StructuredArrow Y L) :
    h.isoLimit.hom ‚â´ limit.œÄ _ g = E.left.map g.hom ‚â´ E.hom.app g.right :=
  IsLimit.conePointUniqueUpToIso_hom_comp _ _ _

@[reassoc (attr := simp)]
lemma isoLimit_inv_œÄ (g : StructuredArrow Y L) :
    h.isoLimit.inv ‚â´ E.left.map g.hom ‚â´ E.hom.app g.right =
      limit.œÄ (StructuredArrow.proj Y L ‚ãô F) g := by
  simpa using h.conePointUniqueUpToIso_inv_comp (limit.isLimit _) g

end IsPointwiseRightKanExtensionAt

/-- A right extension `E : RightExtension L F` is a pointwise right Kan extension when
it is a pointwise right Kan extension at any object. -/
abbrev IsPointwiseRightKanExtension := ‚àÄ (Y : D), E.IsPointwiseRightKanExtensionAt Y

variable {E E'}

/-- If two right extensions `E` and `E'` are isomorphic, `E` is a pointwise
right Kan extension at `Y` iff `E'` is. -/
def isPointwiseRightKanExtensionAtEquivOfIso (e : E ‚âÖ E') (Y : D) :
    E.IsPointwiseRightKanExtensionAt Y ‚âÉ E'.IsPointwiseRightKanExtensionAt Y :=
  IsLimit.equivIsoLimit ((coneAtFunctor L F Y).mapIso e)

/-- If two right extensions `E` and `E'` are isomorphic, `E` is a pointwise
right Kan extension iff `E'` is. -/
def isPointwiseRightKanExtensionEquivOfIso (e : E ‚âÖ E') :
    E.IsPointwiseRightKanExtension ‚âÉ E'.IsPointwiseRightKanExtension where
  toFun h := fun Y => (isPointwiseRightKanExtensionAtEquivOfIso e Y) (h Y)
  invFun h := fun Y => (isPointwiseRightKanExtensionAtEquivOfIso e Y).symm (h Y)
  left_inv h := by simp
  right_inv h := by simp

variable (h : E.IsPointwiseRightKanExtension)
include h

lemma IsPointwiseRightKanExtension.hasPointwiseRightKanExtension :
    HasPointwiseRightKanExtension L F :=
  fun Y => (h Y).hasPointwiseRightKanExtensionAt

/-- The (unique) morphism to a pointwise right Kan extension. -/
def IsPointwiseRightKanExtension.homTo (G : RightExtension L F) : G ‚ü∂ E :=
  CostructuredArrow.homMk
    { app := fun Y ‚Ü¶ (h Y).lift (RightExtension.coneAt G Y)
      naturality := fun Y‚ÇÅ Y‚ÇÇ œÜ ‚Ü¶ (h Y‚ÇÇ).hom_ext (fun X ‚Ü¶ by
        rw [assoc, (h Y‚ÇÇ).fac (coneAt G Y‚ÇÇ) X]
        simpa using ((h Y‚ÇÅ).fac (coneAt G Y‚ÇÅ) ((StructuredArrow.map œÜ).obj X)).symm) }
    (by
      ext X
      simpa using (h (L.obj X)).fac (RightExtension.coneAt G _) (StructuredArrow.mk (ùüô _)) )

lemma IsPointwiseRightKanExtension.hom_ext
    {G : RightExtension L F} {f‚ÇÅ f‚ÇÇ : G ‚ü∂ E} : f‚ÇÅ = f‚ÇÇ := by
  ext Y
  apply (h Y).hom_ext
  intro X
  have eq‚ÇÅ := congr_app (CostructuredArrow.w f‚ÇÅ) X.right
  have eq‚ÇÇ := congr_app (CostructuredArrow.w f‚ÇÇ) X.right
  dsimp at eq‚ÇÅ eq‚ÇÇ ‚ä¢
  simp only [‚Üê NatTrans.naturality_assoc, eq‚ÇÅ, eq‚ÇÇ]

/-- A pointwise right Kan extension is universal, i.e. it is a right Kan extension. -/
def IsPointwiseRightKanExtension.isUniversal : E.IsUniversal :=
  IsTerminal.ofUniqueHom h.homTo (fun _ _ => h.hom_ext)

lemma IsPointwiseRightKanExtension.isRightKanExtension :
    E.left.IsRightKanExtension E.hom where
  nonempty_isUniversal := ‚ü®h.isUniversal‚ü©

lemma IsPointwiseRightKanExtension.hasRightKanExtension :
    HasRightKanExtension L F :=
  have := h.isRightKanExtension
  HasRightKanExtension.mk E.left E.hom

lemma IsPointwiseRightKanExtension.isIso_hom [L.Full] [L.Faithful] :
    IsIso (E.hom) :=
  have := fun X => (h (L.obj X)).isIso_hom_app
  NatIso.isIso_of_isIso_app ..

end RightExtension

section

variable [HasPointwiseLeftKanExtension L F]

/-- The constructed pointwise left Kan extension when `HasPointwiseLeftKanExtension L F` holds. -/
@[simps]
noncomputable def pointwiseLeftKanExtension : D ‚•§ H where
  obj Y := colimit (CostructuredArrow.proj L Y ‚ãô F)
  map {Y‚ÇÅ Y‚ÇÇ} f :=
    colimit.desc (CostructuredArrow.proj L Y‚ÇÅ ‚ãô F)
      (Cocone.mk (colimit (CostructuredArrow.proj L Y‚ÇÇ ‚ãô F))
        { app := fun g => colimit.Œπ (CostructuredArrow.proj L Y‚ÇÇ ‚ãô F)
            ((CostructuredArrow.map f).obj g)
          naturality := fun g‚ÇÅ g‚ÇÇ œÜ => by
            simpa using colimit.w (CostructuredArrow.proj L Y‚ÇÇ ‚ãô F)
              ((CostructuredArrow.map f).map œÜ) })
  map_id Y := colimit.hom_ext (fun j => by
    dsimp
    simp only [colimit.Œπ_desc, comp_id]
    congr
    apply CostructuredArrow.map_id)
  map_comp {Y‚ÇÅ Y‚ÇÇ Y‚ÇÉ} f f' := colimit.hom_ext (fun j => by
    dsimp
    simp only [colimit.Œπ_desc, colimit.Œπ_desc_assoc, comp_obj, CostructuredArrow.proj_obj]
    congr 1
    apply CostructuredArrow.map_comp)

/-- The unit of the constructed pointwise left Kan extension when
`HasPointwiseLeftKanExtension L F` holds. -/
@[simps]
noncomputable def pointwiseLeftKanExtensionUnit : F ‚ü∂ L ‚ãô pointwiseLeftKanExtension L F where
  app X := colimit.Œπ (CostructuredArrow.proj L (L.obj X) ‚ãô F)
    (CostructuredArrow.mk (ùüô (L.obj X)))
  naturality {X‚ÇÅ X‚ÇÇ} f := by
    simp only [comp_obj, pointwiseLeftKanExtension_obj, comp_map,
      pointwiseLeftKanExtension_map, colimit.Œπ_desc, CostructuredArrow.map_mk]
    rw [id_comp]
    let œÜ : CostructuredArrow.mk (L.map f) ‚ü∂ CostructuredArrow.mk (ùüô (L.obj X‚ÇÇ)) :=
      CostructuredArrow.homMk f
    exact colimit.w (CostructuredArrow.proj L (L.obj X‚ÇÇ) ‚ãô F) œÜ

/-- The functor `pointwiseLeftKanExtension L F` is a pointwise left Kan
extension of `F` along `L`. -/
noncomputable def pointwiseLeftKanExtensionIsPointwiseLeftKanExtension :
    (LeftExtension.mk _ (pointwiseLeftKanExtensionUnit L F)).IsPointwiseLeftKanExtension :=
  fun X => IsColimit.ofIsoColimit (colimit.isColimit _) (Cocones.ext (Iso.refl _) (fun j => by
    dsimp
    simp only [comp_id, colimit.Œπ_desc, CostructuredArrow.map_mk]
    congr 1
    rw [id_comp, ‚Üê CostructuredArrow.eq_mk]))

/-- The functor `pointwiseLeftKanExtension L F` is a left Kan extension of `F` along `L`. -/
noncomputable def pointwiseLeftKanExtensionIsUniversal :
    (LeftExtension.mk _ (pointwiseLeftKanExtensionUnit L F)).IsUniversal :=
  (pointwiseLeftKanExtensionIsPointwiseLeftKanExtension L F).isUniversal

instance : (pointwiseLeftKanExtension L F).IsLeftKanExtension
    (pointwiseLeftKanExtensionUnit L F) where
  nonempty_isUniversal := ‚ü®pointwiseLeftKanExtensionIsUniversal L F‚ü©

instance : HasLeftKanExtension L F :=
  HasLeftKanExtension.mk _ (pointwiseLeftKanExtensionUnit L F)

/-- An auxiliary cocone used in the lemma `pointwiseLeftKanExtension_desc_app` -/
@[simps]
def costructuredArrowMapCocone (G : D ‚•§ H) (Œ± : F ‚ü∂ L ‚ãô G) (Y : D) :
    Cocone (CostructuredArrow.proj L Y ‚ãô F) where
  pt := G.obj Y
  Œπ := {
    app := fun f ‚Ü¶ Œ±.app f.left ‚â´ G.map f.hom
    naturality := by simp [‚Üê G.map_comp] }

@[simp]
lemma pointwiseLeftKanExtension_desc_app (G : D ‚•§ H) (Œ± : F ‚ü∂ L ‚ãô G) (Y : D) :
    ((pointwiseLeftKanExtension L F).descOfIsLeftKanExtension (pointwiseLeftKanExtensionUnit L F)
      G Œ± |>.app Y) = colimit.desc _ (costructuredArrowMapCocone L F G Œ± Y) := by
  let Œ≤ : L.pointwiseLeftKanExtension F ‚ü∂ G :=
    { app := fun Y ‚Ü¶ colimit.desc _ (costructuredArrowMapCocone L F G Œ± Y) }
  have h : (pointwiseLeftKanExtension L F).descOfIsLeftKanExtension
      (pointwiseLeftKanExtensionUnit L F) G Œ± = Œ≤ := by
    apply hom_ext_of_isLeftKanExtension (Œ± := pointwiseLeftKanExtensionUnit L F)
    aesop
  exact NatTrans.congr_app h Y

variable {F L}

/-- If `F` admits a pointwise left Kan extension along `L`, then any left Kan extension of `F`
along `L` is a pointwise left Kan extension. -/
noncomputable def isPointwiseLeftKanExtensionOfIsLeftKanExtension (F' : D ‚•§ H) (Œ± : F ‚ü∂ L ‚ãô F')
    [F'.IsLeftKanExtension Œ±] :
    (LeftExtension.mk _ Œ±).IsPointwiseLeftKanExtension :=
  LeftExtension.isPointwiseLeftKanExtensionEquivOfIso
    (IsColimit.coconePointUniqueUpToIso (pointwiseLeftKanExtensionIsUniversal L F)
      (F'.isUniversalOfIsLeftKanExtension Œ±))
    (pointwiseLeftKanExtensionIsPointwiseLeftKanExtension L F)

end

section

variable [HasPointwiseRightKanExtension L F]

/-- The constructed pointwise right Kan extension
when `HasPointwiseRightKanExtension L F` holds. -/
@[simps]
noncomputable def pointwiseRightKanExtension : D ‚•§ H where
  obj Y := limit (StructuredArrow.proj Y L ‚ãô F)
  map {Y‚ÇÅ Y‚ÇÇ} f := limit.lift (StructuredArrow.proj Y‚ÇÇ L ‚ãô F)
      (Cone.mk (limit (StructuredArrow.proj Y‚ÇÅ L ‚ãô F))
        { app := fun g ‚Ü¶ limit.œÄ (StructuredArrow.proj Y‚ÇÅ L ‚ãô F)
            ((StructuredArrow.map f).obj g)
          naturality := fun g‚ÇÅ g‚ÇÇ œÜ ‚Ü¶ by
            simpa using (limit.w (StructuredArrow.proj Y‚ÇÅ L ‚ãô F)
              ((StructuredArrow.map f).map œÜ)).symm })
  map_id Y := limit.hom_ext (fun j => by
    dsimp
    simp only [limit.lift_œÄ, id_comp]
    congr
    apply StructuredArrow.map_id)
  map_comp {Y‚ÇÅ Y‚ÇÇ Y‚ÇÉ} f f' := limit.hom_ext (fun j => by
    dsimp
    simp only [limit.lift_œÄ, assoc]
    congr 1
    apply StructuredArrow.map_comp)

/-- The counit of the constructed pointwise right Kan extension when
`HasPointwiseRightKanExtension L F` holds. -/
@[simps]
noncomputable def pointwiseRightKanExtensionCounit :
    L ‚ãô pointwiseRightKanExtension L F ‚ü∂ F where
  app X := limit.œÄ (StructuredArrow.proj (L.obj X) L ‚ãô F)
    (StructuredArrow.mk (ùüô (L.obj X)))
  naturality {X‚ÇÅ X‚ÇÇ} f := by
    simp only [comp_obj, pointwiseRightKanExtension_obj, comp_map,
      pointwiseRightKanExtension_map, limit.lift_œÄ, StructuredArrow.map_mk]
    rw [comp_id]
    let œÜ : StructuredArrow.mk (ùüô (L.obj X‚ÇÅ)) ‚ü∂ StructuredArrow.mk (L.map f) :=
      StructuredArrow.homMk f
    exact (limit.w (StructuredArrow.proj (L.obj X‚ÇÅ) L ‚ãô F) œÜ).symm

/-- The functor `pointwiseRightKanExtension L F` is a pointwise right Kan
extension of `F` along `L`. -/
noncomputable def pointwiseRightKanExtensionIsPointwiseRightKanExtension :
    (RightExtension.mk _ (pointwiseRightKanExtensionCounit L F)).IsPointwiseRightKanExtension :=
  fun X => IsLimit.ofIsoLimit (limit.isLimit _) (Cones.ext (Iso.refl _) (fun j => by
    dsimp
    simp only [limit.lift_œÄ, StructuredArrow.map_mk, id_comp]
    congr
    rw [comp_id, ‚Üê StructuredArrow.eq_mk]))

/-- The functor `pointwiseRightKanExtension L F` is a right Kan extension of `F` along `L`. -/
noncomputable def pointwiseRightKanExtensionIsUniversal :
    (RightExtension.mk _ (pointwiseRightKanExtensionCounit L F)).IsUniversal :=
  (pointwiseRightKanExtensionIsPointwiseRightKanExtension L F).isUniversal

instance : (pointwiseRightKanExtension L F).IsRightKanExtension
    (pointwiseRightKanExtensionCounit L F) where
  nonempty_isUniversal := ‚ü®pointwiseRightKanExtensionIsUniversal L F‚ü©

instance : HasRightKanExtension L F :=
  HasRightKanExtension.mk _ (pointwiseRightKanExtensionCounit L F)

/-- An auxiliary cocone used in the lemma `pointwiseRightKanExtension_lift_app` -/
@[simps]
def structuredArrowMapCone (G : D ‚•§ H) (Œ± : L ‚ãô G ‚ü∂ F) (Y : D) :
    Cone (StructuredArrow.proj Y L ‚ãô F) where
  pt := G.obj Y
  œÄ := {
    app := fun f ‚Ü¶ G.map f.hom ‚â´ Œ±.app f.right
    naturality := by simp [‚Üê Œ±.naturality, ‚Üê G.map_comp_assoc] }

@[simp]
lemma pointwiseRightKanExtension_lift_app (G : D ‚•§ H) (Œ± : L ‚ãô G ‚ü∂ F) (Y : D) :
    ((pointwiseRightKanExtension L F).liftOfIsRightKanExtension
      (pointwiseRightKanExtensionCounit L F) G Œ± |>.app Y) =
        limit.lift _ (structuredArrowMapCone L F G Œ± Y) := by
  let Œ≤ : G ‚ü∂ L.pointwiseRightKanExtension F :=
    { app := fun Y ‚Ü¶ limit.lift _ (structuredArrowMapCone L F G Œ± Y) }
  have h : (pointwiseRightKanExtension L F).liftOfIsRightKanExtension
      (pointwiseRightKanExtensionCounit L F) G Œ± = Œ≤ := by
    apply hom_ext_of_isRightKanExtension (Œ± := pointwiseRightKanExtensionCounit L F)
    aesop
  exact NatTrans.congr_app h Y

variable {F L}

/-- If `F` admits a pointwise right Kan extension along `L`, then any right Kan extension of `F`
along `L` is a pointwise right Kan extension. -/
noncomputable def isPointwiseRightKanExtensionOfIsRightKanExtension (F' : D ‚•§ H) (Œ± : L ‚ãô F' ‚ü∂ F)
    [F'.IsRightKanExtension Œ±] :
    (RightExtension.mk _ Œ±).IsPointwiseRightKanExtension :=
  RightExtension.isPointwiseRightKanExtensionEquivOfIso
    (IsLimit.conePointUniqueUpToIso (pointwiseRightKanExtensionIsUniversal L F)
      (F'.isUniversalOfIsRightKanExtension Œ±))
    (pointwiseRightKanExtensionIsPointwiseRightKanExtension L F)

end

end Functor

end CategoryTheory
