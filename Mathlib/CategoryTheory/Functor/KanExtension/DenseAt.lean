/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Functor.KanExtension.Pointwise
public import Mathlib.CategoryTheory.Limits.Final

/-!
# Canonical colimits, or functors that are dense at an object

Given a functor `F : C ‚•§ D` and `Y : D`, we say that `F` is dense at `Y` (`F.DenseAt Y`),
if `Y` identifies to the colimit of all `F.obj X` for `X : C`
and `f : F.obj X ‚ü∂ Y`, i.e. `Y` identifies to the colimit of
the obvious functor `CostructuredArrow F Y ‚•§ D`. In some references,
it is also said that `Y` is a canonical colimit relatively to `F`.
While `F.DenseAt Y` contains data, we also introduce the
corresponding property `isDenseAt F` of objects of `D`.

## TODO

* formalize dense subcategories
* show the presheaves of types are canonical colimits relatively
to the Yoneda embedding

## References
* https://ncatlab.org/nlab/show/dense+functor

-/

@[expose] public section

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory

open Limits

variable {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} [Category.{v‚ÇÅ} C] [Category.{v‚ÇÇ} D]
  (F : C ‚•§ D)

namespace Functor

/-- A functor `F : C ‚•§ D` is dense at `Y : D` if the obvious natural transformation
`F ‚ü∂ F ‚ãô ùü≠ D` makes `ùü≠ D` a pointwise left Kan extension of `F` along itself at `Y`,
i.e. `Y` identifies to the colimit of the obvious functor `CostructuredArrow F Y ‚•§ D`. -/
abbrev DenseAt (Y : D) : Type max u‚ÇÅ u‚ÇÇ v‚ÇÇ :=
  (Functor.LeftExtension.mk (ùü≠ D) F.rightUnitor.inv).IsPointwiseLeftKanExtensionAt Y

/-- `F` is dense at `Y` if `Y` identifies to the colimit of the obvious functor
`CostructuredArrow F Y ‚•§ D`. -/
def denseAtEquiv (Y : D) :
    F.DenseAt Y ‚âÉ IsColimit ((LeftExtension.mk (ùü≠ D) F.rightUnitor.inv).coconeAt Y) :=
  .refl _

variable {F} {Y : D} (hY : F.DenseAt Y)

/-- If `F : C ‚•§ D` is dense at `Y : D`, then it is also at `Y'`
if `Y` and `Y'` are isomorphic. -/
def DenseAt.ofIso {Y' : D} (e : Y ‚âÖ Y') : F.DenseAt Y' :=
  LeftExtension.isPointwiseLeftKanExtensionAtOfIso' _ hY e

/-- If `F : C ‚•§ D` is dense at `Y : D`, and `G` is a functor that is isomorphic to `F`,
then `G` is also dense at `Y`. -/
def DenseAt.ofNatIso {G : C ‚•§ D} (e : F ‚âÖ G) : G.DenseAt Y :=
  (IsColimit.equivOfNatIsoOfIso
      ((Functor.associator _ _ _).symm ‚â™‚â´ Functor.isoWhiskerLeft _ e) _ _
      (by exact Cocones.ext (Iso.refl _)))
    (hY.whiskerEquivalence (CostructuredArrow.mapNatIso e.symm))

/-- If the canonical functor `CostructuredArrow (G ‚â´ F) Y ‚•§ CostructuredArrow F Y` is final, then
`G ‚ãô F` is dense at `Y` if and only if `F` is dense at `Y`. -/
noncomputable def DenseAt.precompEquivOfFinal
    {C' : Type*} [Category* C'] (G : C' ‚•§ C) [(CostructuredArrow.pre G F Y).Final] :
    (G ‚ãô F).DenseAt Y ‚âÉ F.DenseAt Y :=
  Functor.Final.isColimitWhiskerEquiv (CostructuredArrow.pre G F Y)
    ((LeftExtension.mk (ùü≠ D) F.rightUnitor.inv).coconeAt Y)

/-- If `F : C ‚•§ D` is dense at `Y : D`, then so is `G ‚ãô F` if
the canonical functor `CostructuredArrow (G ‚â´ F) Y ‚•§ CostructuredArrow F Y` is final.
This holds in particular if `G` is an equivalence. -/
noncomputable def DenseAt.precompOfFinal
    {C' : Type*} [Category* C'] (G : C' ‚•§ C) [(CostructuredArrow.pre G F Y).Final] :
    (G ‚ãô F).DenseAt Y :=
  (DenseAt.precompEquivOfFinal G).symm hY

@[deprecated (since := "2025-12-17")]
alias DenseAt.precompEquivalence := DenseAt.precompOfFinal

/-- If `F : C ‚•§ D` is dense at `Y : D` and `G : D ‚•§ D'` is an equivalence,
then `F ‚ãô G` is dense at `G.obj Y`. -/
noncomputable def DenseAt.postcompEquivalence
    {D' : Type*} [Category* D'] (G : D ‚•§ D') [G.IsEquivalence] :
    (F ‚ãô G).DenseAt (G.obj Y) :=
  IsColimit.ofWhiskerEquivalence (CostructuredArrow.post F G Y).asEquivalence
    (IsColimit.ofIsoColimit ((isColimitOfPreserves G hY)) (Cocones.ext (Iso.refl _)))

variable (F) in
/-- Given a functor `F : C ‚•§ D`, this is the property of objects `Y : D` such
that `F` is dense at `Y`. -/
def isDenseAt : ObjectProperty D :=
  fun Y ‚Ü¶ Nonempty (F.DenseAt Y)

lemma isDenseAt_eq_isPointwiseLeftKanExtensionAt :
    F.isDenseAt =
      (Functor.LeftExtension.mk (ùü≠ D) F.rightUnitor.inv).isPointwiseLeftKanExtensionAt :=
  rfl

lemma isDenseAt_iff {X : D} :
    F.isDenseAt X ‚Üî Nonempty (IsColimit <| (LeftExtension.mk (ùü≠ D) F.rightUnitor.inv).coconeAt X) :=
  .rfl

instance : F.isDenseAt.IsClosedUnderIsomorphisms := by
  rw [isDenseAt_eq_isPointwiseLeftKanExtensionAt]
  infer_instance

lemma congr_isDenseAt {G : C ‚•§ D} (e : F ‚âÖ G) :
    F.isDenseAt = G.isDenseAt := by
  ext X
  exact ‚ü®fun ‚ü®h‚ü© ‚Ü¶ ‚ü®h.ofNatIso e‚ü©, fun ‚ü®h‚ü© ‚Ü¶ ‚ü®h.ofNatIso e.symm‚ü©‚ü©

lemma IsDenseAt.iff_of_final {C' : Type*} [Category* C'] (G : C' ‚•§ C)
    [(CostructuredArrow.pre G F Y).Final] :
    (G ‚ãô F).isDenseAt Y ‚Üî F.isDenseAt Y :=
  (DenseAt.precompEquivOfFinal G).nonempty_congr

lemma IsDenseAt.of_final {C' : Type*} [Category* C'] (G : C' ‚•§ C)
    [(CostructuredArrow.pre G F Y).Final] (hY : F.isDenseAt Y) :
    (G ‚ãô F).isDenseAt Y :=
  (iff_of_final G).mpr hY

end Functor

end CategoryTheory
