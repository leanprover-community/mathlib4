/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Bhavik Mehta
-/
import Mathlib.CategoryTheory.Opposites

#align_import category_theory.functor.const from "leanprover-community/mathlib"@"dc6c365e751e34d100e80fe6e314c3c3e0fd2988"

/-!
# The constant functor

`const J : C â¥¤ (J â¥¤ C)` is the functor that sends an object `X : C` to the functor `J â¥¤ C` sending
every object in `J` to `X`, and every morphism to `ğŸ™ X`.

When `J` is nonempty, `const` is faithful.

We have `(const J).obj X â‹™ F â‰… (const J).obj (F.obj X)` for any `F : C â¥¤ D`.
-/


-- declare the `v`'s first; see `CategoryTheory.Category` for an explanation
universe vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

open CategoryTheory

namespace CategoryTheory.Functor

variable (J : Type uâ‚) [Category.{vâ‚} J]

variable {C : Type uâ‚‚} [Category.{vâ‚‚} C]

/-- The functor sending `X : C` to the constant functor `J â¥¤ C` sending everything to `X`.
-/
@[simps]
def const : C â¥¤ J â¥¤ C
    where
  obj X :=
    { obj := fun _ => X
      map := fun _ => ğŸ™ X }
  map f := { app := fun _ => f }

namespace const

open Opposite

variable {J}

/-- The constant functor `Jáµ’áµ– â¥¤ Cáµ’áµ–` sending everything to `op X`
is (naturally isomorphic to) the opposite of the constant functor `J â¥¤ C` sending everything to `X`.
-/
@[simps]
def opObjOp (X : C) : (const Jáµ’áµ–).obj (op X) â‰… ((const J).obj X).op
    where
  hom := { app := fun j => ğŸ™ _ }
  inv := { app := fun j => ğŸ™ _ }

/-- The constant functor `Jáµ’áµ– â¥¤ C` sending everything to `unop X`
is (naturally isomorphic to) the opposite of
the constant functor `J â¥¤ Cáµ’áµ–` sending everything to `X`.
-/
def opObjUnop (X : Cáµ’áµ–) : (const Jáµ’áµ–).obj (unop X) â‰… ((const J).obj X).leftOp
    where
  hom := { app := fun j => ğŸ™ _ }
  inv := { app := fun j => ğŸ™ _ }

-- Lean needs some help with universes here.
@[simp]
theorem opObjUnop_hom_app (X : Cáµ’áµ–) (j : Jáµ’áµ–) : (opObjUnop.{vâ‚, vâ‚‚} X).hom.app j = ğŸ™ _ :=
  rfl

@[simp]
theorem opObjUnop_inv_app (X : Cáµ’áµ–) (j : Jáµ’áµ–) : (opObjUnop.{vâ‚, vâ‚‚} X).inv.app j = ğŸ™ _ :=
  rfl

@[simp]
theorem unop_functor_op_obj_map (X : Cáµ’áµ–) {jâ‚ jâ‚‚ : J} (f : jâ‚ âŸ¶ jâ‚‚) :
    (unop ((Functor.op (const J)).obj X)).map f = ğŸ™ (unop X) :=
  rfl

end const

section

variable {D : Type uâ‚ƒ} [Category.{vâ‚ƒ} D]

/-- These are actually equal, of course, but not definitionally equal
  (the equality requires F.map (ğŸ™ _) = ğŸ™ _). A natural isomorphism is
  more convenient than an equality between functors (compare id_to_iso). -/
@[simps]
def constComp (X : C) (F : C â¥¤ D) : (const J).obj X â‹™ F â‰… (const J).obj (F.obj X)
    where
  hom := { app := fun _ => ğŸ™ _ }
  inv := { app := fun _ => ğŸ™ _ }

/-- If `J` is nonempty, then the constant functor over `J` is faithful. -/
instance [Nonempty J] : Faithful (const J : C â¥¤ J â¥¤ C) where
  map_injective e := NatTrans.congr_app e (Classical.arbitrary J)

end

end CategoryTheory.Functor
