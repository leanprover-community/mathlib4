/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Functor.Derived.LeftDerived
public import Mathlib.CategoryTheory.Functor.KanExtension.Pointwise
public import Mathlib.CategoryTheory.Localization.StructuredArrow

/-!
# Pointwise left derived functors

We define pointwise left derived functors using the notion
of pointwise right Kan extensions.

We show that if `F : C ‚•§ H` inverts `W : MorphismProperty C`,
then it has a pointwise left derived functor.

Note: this file was obtained by dualizing the definitions in the file
`Mathlib/CategoryTheory/Functor/Derived/PointwiseRightDerived.lean`. These two files should be
kept in sync.

-/

@[expose] public section

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

namespace CategoryTheory

open Category Limits

namespace Functor

variable {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} {H : Type u‚ÇÉ}
  [Category.{v‚ÇÅ} C] [Category.{v‚ÇÇ} D] [Category.{v‚ÇÉ} H]
  (F' : D ‚•§ H) (F : C ‚•§ H) (L : C ‚•§ D) (Œ± : L ‚ãô F' ‚ü∂ F) (W : MorphismProperty C)

/-- Given `F : C ‚•§ H`, `W : MorphismProperty C` and `X : C`, we say that `F` has a
pointwise left derived functor at `X` if `F` has a right Kan extension
at `L.obj X` for any localization functor `L : C ‚•§ D` for `W`. In the
definition, this is stated for `L := W.Q`, see `hasPointwiseLeftDerivedFunctorAt_iff`
for the more general equivalence. -/
class HasPointwiseLeftDerivedFunctorAt (X : C) : Prop where
  /-- Use the more general `hasLimit` lemma instead, see also
  `hasPointwiseLeftDerivedFunctorAt_iff` -/
  hasLimit' : HasPointwiseRightKanExtensionAt W.Q F (W.Q.obj X)

/-- A functor `F : C ‚•§ H` has a pointwise left derived functor with respect to
`W : MorphismProperty C` if it has a pointwise left derived functor at `X`
for any `X : C`. -/
abbrev HasPointwiseLeftDerivedFunctor := ‚àÄ (X : C), F.HasPointwiseLeftDerivedFunctorAt W X

lemma hasPointwiseLeftDerivedFunctorAt_iff [L.IsLocalization W] (X : C) :
    F.HasPointwiseLeftDerivedFunctorAt W X ‚Üî
      HasPointwiseRightKanExtensionAt L F (L.obj X) := by
  rw [‚Üê hasPointwiseRightKanExtensionAt_iff_of_equivalence W.Q L F
    (Localization.uniq W.Q L W) (Localization.compUniqFunctor W.Q L W) (W.Q.obj X) (L.obj X)
    ((Localization.compUniqFunctor W.Q L W).app X)]
  exact ‚ü®fun h ‚Ü¶ h.hasLimit', fun h ‚Ü¶ ‚ü®h‚ü©‚ü©

lemma HasPointwiseLeftDerivedFunctorAt.hasLimit
    [L.IsLocalization W] (X : C) [F.HasPointwiseLeftDerivedFunctorAt W X] :
    HasPointwiseRightKanExtensionAt L F (L.obj X) := by
  rwa [‚Üê hasPointwiseLeftDerivedFunctorAt_iff F L W]

lemma hasPointwiseLeftDerivedFunctorAt_iff_of_mem {X Y : C} (w : X ‚ü∂ Y) (hw : W w) :
    F.HasPointwiseLeftDerivedFunctorAt W X ‚Üî
      F.HasPointwiseLeftDerivedFunctorAt W Y := by
  simp only [F.hasPointwiseLeftDerivedFunctorAt_iff W.Q W]
  exact hasPointwiseRightKanExtensionAt_iff_of_iso W.Q F (Localization.isoOfHom W.Q W w hw)

section

variable [F.HasPointwiseLeftDerivedFunctor W]

lemma hasPointwiseRightKanExtension_of_hasPointwiseLeftDerivedFunctor [L.IsLocalization W] :
    HasPointwiseRightKanExtension L F := fun Y ‚Ü¶ by
  have := Localization.essSurj L W
  rw [‚Üê hasPointwiseRightKanExtensionAt_iff_of_iso _ F (L.objObjPreimageIso Y),
    ‚Üê F.hasPointwiseLeftDerivedFunctorAt_iff L W]
  infer_instance

lemma hasLeftDerivedFunctor_of_hasPointwiseLeftDerivedFunctor :
    F.HasLeftDerivedFunctor W where
  hasRightKanExtension' := by
    have := F.hasPointwiseRightKanExtension_of_hasPointwiseLeftDerivedFunctor W.Q W
    infer_instance

attribute [instance] hasLeftDerivedFunctor_of_hasPointwiseLeftDerivedFunctor

variable {F L}

/-- A left derived functor is a pointwise left derived functor when
there exists a pointwise left derived functor. -/
noncomputable def isPointwiseRightKanExtensionOfHasPointwiseLeftDerivedFunctor
     [L.IsLocalization W] [F'.IsLeftDerivedFunctor Œ± W] :
    (RightExtension.mk _ Œ±).IsPointwiseRightKanExtension :=
  have := hasPointwiseRightKanExtension_of_hasPointwiseLeftDerivedFunctor F L
  have := IsLeftDerivedFunctor.isRightKanExtension F' Œ± W
  isPointwiseRightKanExtensionOfIsRightKanExtension F' Œ±

end

section

variable {F L}

/-- If `L : C ‚•§ D` is a localization functor for `W` and `e : F ‚âÖ L ‚ãô G` is an isomorphism,
then `e.inv` makes `G` a pointwise right Kan extension of `F` along `L` at `L.obj Y`
for any `Y : C`. -/
def isPointwiseRightKanExtensionAtOfIsoOfIsLocalization
    {G : D ‚•§ H} (e : F ‚âÖ L ‚ãô G) [L.IsLocalization W] (Y : C) :
    (RightExtension.mk _ e.inv).IsPointwiseRightKanExtensionAt (L.obj Y) where
  lift s := s.œÄ.app (StructuredArrow.mk (ùüô (L.obj Y))) ‚â´ e.hom.app Y
  fac s j := by
    refine Localization.induction_structuredArrow L W _ (by simp)
      (fun X‚ÇÅ X‚ÇÇ f œÜ hœÜ ‚Ü¶ ?_) (fun X‚ÇÅ X‚ÇÇ w hw œÜ hœÜ ‚Ü¶ ?_) j
    ¬∑ have eq := s.œÄ.naturality
        (StructuredArrow.homMk f : StructuredArrow.mk œÜ ‚ü∂ StructuredArrow.mk (œÜ ‚â´ L.map f))
      dsimp at eq hœÜ ‚ä¢
      rw [id_comp] at eq
      rw [assoc] at hœÜ
      simp [eq, ‚Üê reassoc_of% hœÜ, ‚Üê e.inv.naturality f]
    ¬∑ have : IsIso (F.map w) := by
        have := Localization.inverts L W w hw
        rw [‚Üê NatIso.naturality_2 e w]
        dsimp
        infer_instance
      have eq := s.œÄ.naturality (StructuredArrow.homMk w :
          StructuredArrow.mk (œÜ ‚â´ (Localization.isoOfHom L W w hw).inv) ‚ü∂
            StructuredArrow.mk œÜ)
      dsimp at eq hœÜ ‚ä¢
      rw [id_comp] at eq
      rw [assoc] at hœÜ
      simp only [‚Üê cancel_mono (F.map w), ‚Üê eq, comp_obj, comp_map, assoc,
        ‚Üê hœÜ, ‚Üê NatTrans.naturality, ‚Üê G.map_comp_assoc,
        Localization.isoOfHom_inv_hom_id, comp_id]
  uniq s m hm := by
    have := hm (StructuredArrow.mk (ùüô (L.obj Y)))
    dsimp at this m hm ‚ä¢
    simp [‚Üê reassoc_of% this]

/-- If `L` is a localization functor for `W` and `e : F ‚âÖ L ‚ãô G` is an isomorphism,
then `e.inv` makes `G` a pointwise right Kan extension of `F` along `L`. -/
noncomputable def isPointwiseRightKanExtensionOfIsoOfIsLocalization
    {G : D ‚•§ H} (e : F ‚âÖ L ‚ãô G) [L.IsLocalization W] :
    (RightExtension.mk _ e.inv).IsPointwiseRightKanExtension := fun Y ‚Ü¶
  have := Localization.essSurj L W
  (RightExtension.mk _ e.inv).isPointwiseRightKanExtensionAtEquivOfIso'
    (L.objObjPreimageIso Y) (isPointwiseRightKanExtensionAtOfIsoOfIsLocalization W e _)

/-- Let `L : C ‚•§ D` be a localization functor for `W`, if an extension `E`
of `F : C ‚•§ H` along `L` is such that the natural transformation
`E.hom : L ‚ãô E.right ‚ü∂ F` is an isomorphism, then `E` is a pointwise
right Kan extension. -/
noncomputable def RightExtension.isPointwiseRightKanExtensionOfIsIsoOfIsLocalization
    (E : RightExtension L F) [IsIso E.hom] [L.IsLocalization W] :
    E.IsPointwiseRightKanExtension :=
  Functor.isPointwiseRightKanExtensionOfIsoOfIsLocalization W (asIso E.hom).symm

lemma hasPointwiseLeftDerivedFunctor_of_inverts
    (F : C ‚•§ H) {W : MorphismProperty C} (hF : W.IsInvertedBy F) :
    F.HasPointwiseLeftDerivedFunctor W := by
  intro X
  rw [hasPointwiseLeftDerivedFunctorAt_iff F W.Q W]
  exact (isPointwiseRightKanExtensionOfIsoOfIsLocalization W
    (Localization.fac F hF W.Q).symm).hasPointwiseRightKanExtension _

lemma isLeftDerivedFunctor_of_inverts
    [L.IsLocalization W] (F' : D ‚•§ H) (e : L ‚ãô F' ‚âÖ F) :
    F'.IsLeftDerivedFunctor e.hom W where
  isRightKanExtension :=
    (isPointwiseRightKanExtensionOfIsoOfIsLocalization W e.symm).isRightKanExtension

instance [L.IsLocalization W] (hF : W.IsInvertedBy F) :
    (Localization.lift F hF L).IsLeftDerivedFunctor (Localization.fac F hF L).hom W :=
  isLeftDerivedFunctor_of_inverts W _ _

variable {W} in
lemma isIso_of_isLeftDerivedFunctor_of_inverts [L.IsLocalization W]
    {F : C ‚•§ H} (LF : D ‚•§ H) (Œ± : L ‚ãô LF ‚ü∂ F)
    (hF : W.IsInvertedBy F) [LF.IsLeftDerivedFunctor Œ± W] :
    IsIso Œ± := by
  have : Œ± = whiskerLeft _ (leftDerivedUnique _ _ (Localization.fac F hF L).hom Œ± W).inv ‚â´
      (Localization.fac F hF L).hom := by simp
  rw [this]
  infer_instance

variable {W} in
lemma isLeftDerivedFunctor_iff_of_inverts [L.IsLocalization W]
    {F : C ‚•§ H} (LF : D ‚•§ H) (Œ± : L ‚ãô LF ‚ü∂ F)
    (hF : W.IsInvertedBy F) :
    LF.IsLeftDerivedFunctor Œ± W ‚Üî IsIso Œ± :=
  ‚ü®fun _ ‚Ü¶ isIso_of_isLeftDerivedFunctor_of_inverts LF Œ± hF, fun _ ‚Ü¶
    isLeftDerivedFunctor_of_inverts W LF (asIso Œ±)‚ü©

end

end Functor

end CategoryTheory
