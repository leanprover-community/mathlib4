/-
Copyright (c) 2025 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Functor.Derived.RightDerived
import Mathlib.CategoryTheory.Functor.KanExtension.Pointwise
import Mathlib.CategoryTheory.Localization.StructuredArrow

/-!
# Pointwise right derived functors

We define the pointwise right derived functors using the notion
of pointwise left Kan extensions.

We show that if `F : C â¥¤ H` inverts `W : MorphismProperty C`,
then it has a pointwise right derived functor.

-/

namespace CategoryTheory

open Category Limits

namespace Functor

variable {C D H : Type _} [Category C] [Category D] [Category H]
  (F' : D â¥¤ H) (F : C â¥¤ H) (L : C â¥¤ D) (Î± : F âŸ¶ L â‹™ F') (W : MorphismProperty C)

/-- Given `F : C â¥¤ H`, `W : MorphismProperty C` and `X : C`, we say that `F` has a
pointwise right derived functor at `X` if `F` has a left Kan extension
at `L.obj X` for any localization functor `L : C â¥¤ D` for `W`. In the
definition, this is stated for `L := W.Q`, see `hasPointwiseRightDerivedFunctorAt_iff`
for the more general equivalence. -/
class HasPointwiseRightDerivedFunctorAt (X : C) : Prop where
  hasColimit' : HasPointwiseLeftKanExtensionAt W.Q F (W.Q.obj X)

/-- A functor `F : C â¥¤ H` has a pointwise right derived functor with respect to
`W : MorphismProperty C` if it has a pointwise right derived functor at `X`
for any `X : C`. -/
abbrev HasPointwiseRightDerivedFunctor := âˆ€ (X : C), F.HasPointwiseRightDerivedFunctorAt W X

lemma hasPointwiseRightDerivedFunctorAt_iff [L.IsLocalization W] (X : C) :
    F.HasPointwiseRightDerivedFunctorAt W X â†”
      HasPointwiseLeftKanExtensionAt L F (L.obj X) := by
  rw [â† hasPointwiseLeftKanExtensionAt_iff_of_equivalence W.Q L F
    (Localization.uniq W.Q L W) (Localization.compUniqFunctor W.Q L W) (W.Q.obj X) (L.obj X)
    ((Localization.compUniqFunctor W.Q L W).app X)]
  exact âŸ¨fun h => h.hasColimit', fun h => âŸ¨hâŸ©âŸ©

lemma hasPointwiseRightDerivedFunctorAt_iff_of_mem {X Y : C} (w : X âŸ¶ Y) (hw : W w) :
    F.HasPointwiseRightDerivedFunctorAt W X â†”
      F.HasPointwiseRightDerivedFunctorAt W Y := by
  simp only [F.hasPointwiseRightDerivedFunctorAt_iff W.Q W]
  exact hasPointwiseLeftKanExtensionAt_iff_of_iso W.Q F (Localization.isoOfHom W.Q W w hw)

section

variable [F.HasPointwiseRightDerivedFunctor W]

lemma hasPointwiseLeftKanExtension_of_hasPointwiseRightDerivedFunctor [L.IsLocalization W] :
      HasPointwiseLeftKanExtension L F := fun Y => by
    have := Localization.essSurj L W
    rw [â† hasPointwiseLeftKanExtensionAt_iff_of_iso _ F (L.objObjPreimageIso Y),
      â† F.hasPointwiseRightDerivedFunctorAt_iff L W]
    infer_instance

lemma hasRightDerivedFunctor_of_hasPointwiseRightDerivedFunctor :
    F.HasRightDerivedFunctor W where
  hasLeftKanExtension' := by
    have := F.hasPointwiseLeftKanExtension_of_hasPointwiseRightDerivedFunctor W.Q W
    infer_instance

attribute [instance] hasRightDerivedFunctor_of_hasPointwiseRightDerivedFunctor

variable {F L}

/-- A right derived functor is a pointwise right derived functor when
there exists a pointwise right derived functor. -/
noncomputable def isPointwiseLeftKanExtensionOfHasPointwiseRightDerivedFunctor
     [L.IsLocalization W] [F'.IsRightDerivedFunctor Î± W] :
    (LeftExtension.mk _ Î±).IsPointwiseLeftKanExtension := by
  have := hasPointwiseLeftKanExtension_of_hasPointwiseRightDerivedFunctor F L
  have := IsRightDerivedFunctor.isLeftKanExtension F' Î± W
  exact isPointwiseLeftKanExtensionOfIsLeftKanExtension F' Î±

end

section

variable {F L}

/-- If `L : C â¥¤ D` is a localization functor for `W` and `e : F â‰… L â‹™ G` is an isomorphism,
then `e.hom` makes `G` a pointwise left Kan extension of `F` along `L` at `L.obj Y`
for any `Y : C`. -/
def isPointwiseLeftKanExtensionAtOfIso
    {G : D â¥¤ H} (e : F â‰… L â‹™ G) [L.IsLocalization W] (Y : C) :
    (LeftExtension.mk _ e.hom).IsPointwiseLeftKanExtensionAt (L.obj Y) where
  desc s := e.inv.app Y â‰« s.Î¹.app (CostructuredArrow.mk (ðŸ™ (L.obj Y)))
  fac s := by
    intro j
    refine Localization.induction_costructuredArrow L W _ (by simp)
      (fun Xâ‚ Xâ‚‚ f Ï† hÏ† â†¦ ?_) (fun Xâ‚ Xâ‚‚ w hw Ï† hÏ† â†¦ ?_) j
    Â· have eq := s.Î¹.naturality
        (CostructuredArrow.homMk f : CostructuredArrow.mk (L.map f â‰« Ï†) âŸ¶ CostructuredArrow.mk Ï†)
      dsimp at eq hÏ† âŠ¢
      rw [comp_id] at eq
      rw [assoc] at hÏ†
      rw [assoc, map_comp_assoc, â† eq, â† hÏ†, NatTrans.naturality_assoc, comp_map]
    Â· have : IsIso (F.map w) := by
        have := Localization.inverts L W w hw
        rw [â† NatIso.naturality_2 e w]
        dsimp
        infer_instance
      have eq := s.Î¹.naturality
        (CostructuredArrow.homMk w : CostructuredArrow.mk Ï† âŸ¶ CostructuredArrow.mk
          ((Localization.isoOfHom L W w hw).inv â‰« Ï†))
      dsimp at eq hÏ† âŠ¢
      rw [comp_id] at eq
      rw [assoc] at hÏ†
      rw [map_comp, assoc, assoc, â† cancel_epi (F.map w), eq, â† hÏ†,
        NatTrans.naturality_assoc, comp_map, â† G.map_comp_assoc]
      simp
  uniq s m hm := by
    have := hm (CostructuredArrow.mk (ðŸ™ (L.obj Y)))
    dsimp at this m hm âŠ¢
    simp only [â† this, map_id, comp_id, Iso.inv_hom_id_app_assoc]

/-- If `L` is a localization functor for `W` and `e : F â‰… L â‹™ G` is an isomorphism,
then `e.hom` makes `G` a poinwise left Kan extension of `F` along `L`. -/
noncomputable def isPointwiseLeftKanExtensionOfIso
    {G : D â¥¤ H} (e : F â‰… L â‹™ G) [L.IsLocalization W] :
    (LeftExtension.mk _ e.hom).IsPointwiseLeftKanExtension := fun Y => by
  have := Localization.essSurj L W
  exact (LeftExtension.mk _ e.hom).isPointwiseLeftKanExtensionAtEquivOfIso'
    (L.objObjPreimageIso Y) (isPointwiseLeftKanExtensionAtOfIso W e _)

/-- Let `L : C â¥¤ D` be a localization functor for `W`, if an extension `E`
of `F : C â¥¤ H` along `L` is such that the natural transformation
`E.hom : F âŸ¶ L â‹™ E.right` is an isomorphism, then `E` is a pointwise
left Ken extension. -/
noncomputable def LeftExtension.isPointwiseLeftKanExtensionOfIsIso
    (E : LeftExtension L F) [IsIso E.hom] [L.IsLocalization W] :
    E.IsPointwiseLeftKanExtension :=
  Functor.isPointwiseLeftKanExtensionOfIso W (asIso E.hom)

variable {W} in
lemma hasPointwiseRightDerivedFunctor_of_inverts
    (F : C â¥¤ H) {W : MorphismProperty C} (hF : W.IsInvertedBy F) :
    F.HasPointwiseRightDerivedFunctor W := by
  intro X
  rw [hasPointwiseRightDerivedFunctorAt_iff F W.Q W]
  exact (isPointwiseLeftKanExtensionOfIso W
    (Localization.fac F hF W.Q).symm).hasPointwiseLeftKanExtension  _

lemma isRightDerivedFunctor_of_inverts
    [L.IsLocalization W] (F' : D â¥¤ H) (e : L â‹™ F' â‰… F) :
    F'.IsRightDerivedFunctor e.inv W where
  isLeftKanExtension' :=
    (isPointwiseLeftKanExtensionOfIso W e.symm).isLeftKanExtension

variable {W} in
lemma isIso_of_isRightDerivedFunctor_of_inverts [L.IsLocalization W]
    {F : C â¥¤ H} (RF : D â¥¤ H) (Î± : F âŸ¶ L â‹™ RF)
    (hF : W.IsInvertedBy F) [RF.IsRightDerivedFunctor Î± W] :
    IsIso Î± := by
  let e := Localization.fac F hF L
  have := isRightDerivedFunctor_of_inverts W _ e
  have : Î± = e.inv â‰« whiskerLeft _ (rightDerivedUnique _ _ e.inv Î± W).hom := by simp
  rw [this]
  infer_instance

end

end Functor

end CategoryTheory
