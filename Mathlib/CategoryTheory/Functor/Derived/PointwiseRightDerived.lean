/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Functor.Derived.RightDerived
import Mathlib.CategoryTheory.Functor.KanExtension.Pointwise
import Mathlib.CategoryTheory.Localization.StructuredArrow

/-!
# Pointwise right derived functors

We define the pointwise right derived functors using the notion
of pointwise left Kan extensions.

We show that if `F : C ‚•§ H` inverts `W : MorphismProperty C`,
then it has a pointwise right derived functor.

-/

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

namespace CategoryTheory

open Category Limits

namespace Functor

variable {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} {H : Type u‚ÇÉ}
  [Category.{v‚ÇÅ} C] [Category.{v‚ÇÇ} D] [Category.{v‚ÇÉ} H]
  (F' : D ‚•§ H) (F : C ‚•§ H) (L : C ‚•§ D) (Œ± : F ‚ü∂ L ‚ãô F') (W : MorphismProperty C)

/-- Given `F : C ‚•§ H`, `W : MorphismProperty C` and `X : C`, we say that `F` has a
pointwise right derived functor at `X` if `F` has a left Kan extension
at `L.obj X` for any localization functor `L : C ‚•§ D` for `W`. In the
definition, this is stated for `L := W.Q`, see `hasPointwiseRightDerivedFunctorAt_iff`
for the more general equivalence. -/
class HasPointwiseRightDerivedFunctorAt (X : C) : Prop where
  /-- Use the more general `hasColimit` lemma instead, see also
  `hasPointwiseRightDerivedFunctorAt_iff` -/
  hasColimit' : HasPointwiseLeftKanExtensionAt W.Q F (W.Q.obj X)

/-- A functor `F : C ‚•§ H` has a pointwise right derived functor with respect to
`W : MorphismProperty C` if it has a pointwise right derived functor at `X`
for any `X : C`. -/
abbrev HasPointwiseRightDerivedFunctor := ‚àÄ (X : C), F.HasPointwiseRightDerivedFunctorAt W X

lemma hasPointwiseRightDerivedFunctorAt_iff [L.IsLocalization W] (X : C) :
    F.HasPointwiseRightDerivedFunctorAt W X ‚Üî
      HasPointwiseLeftKanExtensionAt L F (L.obj X) := by
  rw [‚Üê hasPointwiseLeftKanExtensionAt_iff_of_equivalence W.Q L F
    (Localization.uniq W.Q L W) (Localization.compUniqFunctor W.Q L W) (W.Q.obj X) (L.obj X)
    ((Localization.compUniqFunctor W.Q L W).app X)]
  exact ‚ü®fun h ‚Ü¶ h.hasColimit', fun h ‚Ü¶ ‚ü®h‚ü©‚ü©

lemma HasPointwiseRightDerivedFunctorAt.hasColimit
    [L.IsLocalization W] (X : C) [F.HasPointwiseRightDerivedFunctorAt W X] :
    HasPointwiseLeftKanExtensionAt L F (L.obj X) := by
  rwa [‚Üê hasPointwiseRightDerivedFunctorAt_iff F L W]

lemma hasPointwiseRightDerivedFunctorAt_iff_of_mem {X Y : C} (w : X ‚ü∂ Y) (hw : W w) :
    F.HasPointwiseRightDerivedFunctorAt W X ‚Üî
      F.HasPointwiseRightDerivedFunctorAt W Y := by
  simp only [F.hasPointwiseRightDerivedFunctorAt_iff W.Q W]
  exact hasPointwiseLeftKanExtensionAt_iff_of_iso W.Q F (Localization.isoOfHom W.Q W w hw)

section

variable [F.HasPointwiseRightDerivedFunctor W]

lemma hasPointwiseLeftKanExtension_of_hasPointwiseRightDerivedFunctor [L.IsLocalization W] :
    HasPointwiseLeftKanExtension L F := fun Y ‚Ü¶ by
  have := Localization.essSurj L W
  rw [‚Üê hasPointwiseLeftKanExtensionAt_iff_of_iso _ F (L.objObjPreimageIso Y),
    ‚Üê F.hasPointwiseRightDerivedFunctorAt_iff L W]
  infer_instance

lemma hasRightDerivedFunctor_of_hasPointwiseRightDerivedFunctor :
    F.HasRightDerivedFunctor W where
  hasLeftKanExtension' := by
    have := F.hasPointwiseLeftKanExtension_of_hasPointwiseRightDerivedFunctor W.Q W
    infer_instance

attribute [instance] hasRightDerivedFunctor_of_hasPointwiseRightDerivedFunctor

variable {F L}

/-- A right derived functor is a pointwise right derived functor when
there exists a pointwise right derived functor. -/
noncomputable def isPointwiseLeftKanExtensionOfHasPointwiseRightDerivedFunctor
     [L.IsLocalization W] [F'.IsRightDerivedFunctor Œ± W] :
    (LeftExtension.mk _ Œ±).IsPointwiseLeftKanExtension :=
  have := hasPointwiseLeftKanExtension_of_hasPointwiseRightDerivedFunctor F L
  have := IsRightDerivedFunctor.isLeftKanExtension F' Œ± W
  isPointwiseLeftKanExtensionOfIsLeftKanExtension F' Œ±

end

section

variable {F L}

/-- If `L : C ‚•§ D` is a localization functor for `W` and `e : F ‚âÖ L ‚ãô G` is an isomorphism,
then `e.hom` makes `G` a pointwise left Kan extension of `F` along `L` at `L.obj Y`
for any `Y : C`. -/
def isPointwiseLeftKanExtensionAtOfIsoOfIsLocalization
    {G : D ‚•§ H} (e : F ‚âÖ L ‚ãô G) [L.IsLocalization W] (Y : C) :
    (LeftExtension.mk _ e.hom).IsPointwiseLeftKanExtensionAt (L.obj Y) where
  desc s := e.inv.app Y ‚â´ s.Œπ.app (CostructuredArrow.mk (ùüô (L.obj Y)))
  fac s j := by
    refine Localization.induction_costructuredArrow L W _ (by simp)
      (fun X‚ÇÅ X‚ÇÇ f œÜ hœÜ ‚Ü¶ ?_) (fun X‚ÇÅ X‚ÇÇ w hw œÜ hœÜ ‚Ü¶ ?_) j
    ¬∑ have eq := s.Œπ.naturality
        (CostructuredArrow.homMk f : CostructuredArrow.mk (L.map f ‚â´ œÜ) ‚ü∂ CostructuredArrow.mk œÜ)
      dsimp at eq hœÜ ‚ä¢
      rw [comp_id] at eq
      rw [assoc] at hœÜ
      rw [assoc, map_comp_assoc, ‚Üê eq, ‚Üê hœÜ, NatTrans.naturality_assoc, comp_map]
    ¬∑ have : IsIso (F.map w) := by
        have := Localization.inverts L W w hw
        rw [‚Üê NatIso.naturality_2 e w]
        dsimp
        infer_instance
      have eq := s.Œπ.naturality
        (CostructuredArrow.homMk w : CostructuredArrow.mk œÜ ‚ü∂ CostructuredArrow.mk
          ((Localization.isoOfHom L W w hw).inv ‚â´ œÜ))
      dsimp at eq hœÜ ‚ä¢
      rw [comp_id] at eq
      rw [assoc] at hœÜ
      rw [map_comp, assoc, assoc, ‚Üê cancel_epi (F.map w), eq, ‚Üê hœÜ,
        NatTrans.naturality_assoc, comp_map, ‚Üê G.map_comp_assoc]
      simp
  uniq s m hm := by
    have := hm (CostructuredArrow.mk (ùüô (L.obj Y)))
    dsimp at this m hm ‚ä¢
    simp only [‚Üê this, map_id, comp_id, Iso.inv_hom_id_app_assoc]

/-- If `L` is a localization functor for `W` and `e : F ‚âÖ L ‚ãô G` is an isomorphism,
then `e.hom` makes `G` a poinwise left Kan extension of `F` along `L`. -/
noncomputable def isPointwiseLeftKanExtensionOfIsoOfIsLocalization
    {G : D ‚•§ H} (e : F ‚âÖ L ‚ãô G) [L.IsLocalization W] :
    (LeftExtension.mk _ e.hom).IsPointwiseLeftKanExtension := fun Y ‚Ü¶
  have := Localization.essSurj L W
  (LeftExtension.mk _ e.hom).isPointwiseLeftKanExtensionAtEquivOfIso'
    (L.objObjPreimageIso Y) (isPointwiseLeftKanExtensionAtOfIsoOfIsLocalization W e _)

/-- Let `L : C ‚•§ D` be a localization functor for `W`, if an extension `E`
of `F : C ‚•§ H` along `L` is such that the natural transformation
`E.hom : F ‚ü∂ L ‚ãô E.right` is an isomorphism, then `E` is a pointwise
left Kan extension. -/
noncomputable def LeftExtension.isPointwiseLeftKanExtensionOfIsIsoOfIsLocalization
    (E : LeftExtension L F) [IsIso E.hom] [L.IsLocalization W] :
    E.IsPointwiseLeftKanExtension :=
  Functor.isPointwiseLeftKanExtensionOfIsoOfIsLocalization W (asIso E.hom)

lemma hasPointwiseRightDerivedFunctor_of_inverts
    (F : C ‚•§ H) {W : MorphismProperty C} (hF : W.IsInvertedBy F) :
    F.HasPointwiseRightDerivedFunctor W := by
  intro X
  rw [hasPointwiseRightDerivedFunctorAt_iff F W.Q W]
  exact (isPointwiseLeftKanExtensionOfIsoOfIsLocalization W
    (Localization.fac F hF W.Q).symm).hasPointwiseLeftKanExtension  _

end

end Functor

end CategoryTheory
