/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.Control.EquivFunctor
import Mathlib.CategoryTheory.Groupoid
import Mathlib.CategoryTheory.Whiskering
import Mathlib.CategoryTheory.Types

/-!
# The core of a category

The core of a category `C` is the (non-full) subcategory of `C` consisting of all objects,
and all isomorphisms. We construct it as a `CategoryTheory.Groupoid`.

`CategoryTheory.Core.inclusion : Core C â¥¤ C` gives the faithful inclusion into the original
category.

Any functor `F` from a groupoid `G` into `C` factors through `CategoryTheory.Core C`,
but this is not functorial with respect to `F`.
-/

namespace CategoryTheory

universe vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ uâ‚ uâ‚‚ uâ‚ƒ uâ‚„

-- morphism levels before object levels. See note [CategoryTheory universes].
/-- The core of a category C is the groupoid whose morphisms are all the
isomorphisms of C. -/
def Core (C : Type uâ‚) := C

variable {C : Type uâ‚} [Category.{vâ‚} C]

instance coreCategory : Groupoid.{vâ‚} (Core C) where
  Hom (X Y : C) := X â‰… Y
  id (X : C) := Iso.refl X
  comp f g := Iso.trans f g
  inv {_ _} f := Iso.symm f

namespace Core

@[simp]
/- Porting note: abomination -/
theorem id_hom (X : C) : Iso.hom (coreCategory.id X) = @CategoryStruct.id C _ X := by
  rfl

@[simp]
theorem comp_hom {X Y Z : Core C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) : (f â‰« g).hom = f.hom â‰« g.hom :=
  rfl

@[ext]
theorem hom_ext {X Y : Core C} {f g : X âŸ¶ Y} (h : f.hom = g.hom) : f = g := Iso.ext h

variable (C)

/-- The core of a category is naturally included in the category. -/
@[simps!]
def inclusion : Core C â¥¤ C where
  obj := id
  map f := f.hom

-- Porting note: This worked without proof before.
instance : (inclusion C).Faithful where
  map_injective := by
    intro _ _
    apply Iso.ext

variable {C} {G : Type uâ‚‚} [Groupoid.{vâ‚‚} G]

-- Note that this function is not functorial
-- (consider the two functors from [0] to [1], and the natural transformation between them).
/-- A functor from a groupoid to a category C factors through the core of C. -/
def functorToCore (F : G â¥¤ C) : G â¥¤ Core C where
  obj X := F.obj X
  map f := { hom := F.map f, inv := F.map (Groupoid.inv f) }

/-- We can functorially associate to any functor from a groupoid to the core of a category `C`,
a functor from the groupoid to `C`, simply by composing with the embedding `Core C â¥¤ C`.
-/
def forgetFunctorToCore : (G â¥¤ Core C) â¥¤ G â¥¤ C :=
  (whiskeringRight _ _ _).obj (inclusion C)

end Core

section

namespace Functor

variable {D : Type uâ‚‚} [Category.{vâ‚‚} D]

/-- A functor `C â¥¤ D` induces a functor `Core C â¥¤ Core D`. -/
@[simps!]
def core (F : C â¥¤ D) : Core C â¥¤ Core D := Core.functorToCore (Core.inclusion _ â‹™ F)

variable (C) in
/-- The core of the identity functor is the identity functor on the cores. -/
@[simps! hom_app inv_app]
def coreId : (ðŸ­ C).core â‰… ðŸ­ (Core C) := Iso.refl _

/-- The core of the composition of F and G is the composition of the cores. -/
@[simps! hom_app inv_app]
def coreComp {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E] (F : C â¥¤ D) (G : D â¥¤ E) :
  (F â‹™ G).core â‰… F.core â‹™ G.core := Iso.refl _

end Functor

namespace Iso

variable {D : Type uâ‚‚} [Category.{vâ‚‚} D]

/-- A natural isomorphism of functors induces a natural isomorphism between their cores. -/
@[simps! hom_app inv_app]
def core {F G : C â¥¤ D} (Î± : F â‰… G) : F.core â‰… G.core :=
  NatIso.ofComponents
    (fun x â†¦ Groupoid.isoEquivHom _ _|>.symm <| Î±.app x)
    (fun {_ _} f â†¦ by aesop_cat)

@[simp]
lemma coreComp {F G H : C â¥¤ D} (Î± : F â‰… G) (Î² : G â‰… H) : (Î± â‰ªâ‰« Î²).core = Î±.core â‰ªâ‰« Î².core := rfl

@[simp]
lemma coreId {F : C â¥¤ D} : (Iso.refl F).core = Iso.refl F.core := rfl

-- Arguments order and simp normal forms here are chosen to mimic simp lemmas of pseudofunctors.

@[simp]
lemma coreWhiskerLeft {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E] (F : C â¥¤ D) {G H : D â¥¤ E} (Î· : G â‰… H) :
    (isoWhiskerLeft F Î·).core =
    F.coreComp G â‰ªâ‰« isoWhiskerLeft F.core Î·.core â‰ªâ‰« (F.coreComp H).symm := by
  aesop_cat

@[simp]
lemma coreWhiskerRight {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E] {F G : C â¥¤ D} (Î· : F â‰… G) (H : D â¥¤ E) :
    (isoWhiskerRight Î· H ).core =
    F.coreComp H â‰ªâ‰« isoWhiskerRight Î·.core H.core â‰ªâ‰« (G.coreComp H).symm := by
  aesop_cat

@[simp]
lemma coreLeftUnitor {F : C â¥¤ D} :
    F.leftUnitor.core =
    (ðŸ­ C).coreComp F â‰ªâ‰« isoWhiskerRight (Functor.coreId C) _ â‰ªâ‰« F.core.leftUnitor := by
  aesop_cat

@[simp]
lemma coreRightUnitor {F : C â¥¤ D} :
    F.rightUnitor.core =
    (F).coreComp (ðŸ­ D) â‰ªâ‰« isoWhiskerLeft _ (Functor.coreId D) â‰ªâ‰« F.core.rightUnitor := by
  aesop_cat

@[simp]
lemma coreAssociator {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E] {E' : Type uâ‚„} [Category.{vâ‚„} E']
    (F : C â¥¤ D) (G : D â¥¤ E) (H : E â¥¤ E') :
    (Functor.associator F G H).core =
    (F â‹™ G).coreComp H â‰ªâ‰« isoWhiskerRight (F.coreComp G) H.core â‰ªâ‰«
      Functor.associator F.core G.core H.core â‰ªâ‰« (isoWhiskerLeft F.core (G.coreComp H)).symm â‰ªâ‰«
      (F.coreComp (G â‹™ H)).symm
      := by
  aesop_cat

end Iso

namespace Functor

variable {D : Type uâ‚‚} [Category.{vâ‚‚} D]

/-- Taking the core of a functor is functorial if we discard non-invertible natural
transformations. -/
@[simps!]
def coreFunctor : Core (C â¥¤ D) â¥¤ Core C â¥¤ Core D where
  obj F := F.core
  map Î· := Î·.core.hom

end Functor

end

/-- `ofEquivFunctor m` lifts a type-level `EquivFunctor`
to a categorical functor `Core (Type uâ‚) â¥¤ Core (Type uâ‚‚)`.
-/
def ofEquivFunctor (m : Type uâ‚ â†’ Type uâ‚‚) [EquivFunctor m] : Core (Type uâ‚) â¥¤ Core (Type uâ‚‚) where
  obj := m
  map f := (EquivFunctor.mapEquiv m f.toEquiv).toIso
  map_id Î± := by apply Iso.ext; funext x; exact congr_fun (EquivFunctor.map_refl' _) x
  map_comp f g := by
    apply Iso.ext; funext x; dsimp
    erw [Iso.toEquiv_comp]
    rw [EquivFunctor.map_trans', Function.comp]

end CategoryTheory
