/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Limits.Final

/-!
# Guitart exact squares

Given four functors `T`, `L`, `R` and `B`, a 2-square `TwoSquare T L R B` consists of
a natural transformation `w : T ‚ãô R ‚ü∂ L ‚ãô B`:
```
     T
  C‚ÇÅ ‚•§ C‚ÇÇ
L |     | R
  v     v
  C‚ÇÉ ‚•§ C‚ÇÑ
     B
```

In this file, we define a typeclass `w.GuitartExact` which expresses
that this square is exact in the sense of Guitart. This means that
for any `X‚ÇÉ : C‚ÇÉ`, the induced functor
`CostructuredArrow L X‚ÇÉ ‚•§ CostructuredArrow R (B.obj X‚ÇÉ)` is final.
It is also equivalent to the fact that for any `X‚ÇÇ : C‚ÇÇ`, the
induced functor `StructuredArrow X‚ÇÇ T ‚•§ StructuredArrow (R.obj X‚ÇÇ) B`
is initial.

Various categorical notions (fully faithful functors, adjunctions, etc.) can
be characterized in terms of Guitart exact squares. Their particular role
in pointwise Kan extensions shall also be used in the construction of
derived functors.

## TODO

* Define the notion of derivability structure from
[the paper by Kahn and Maltsiniotis][KahnMaltsiniotis2008] using Guitart exact squares
and construct (pointwise) derived functors using this notion

## References
* https://ncatlab.org/nlab/show/exact+square
* [Ren√© Guitart, *Relations et carr√©s exacts*][Guitart1980]
* [Bruno Kahn and Georges Maltsiniotis, *Structures de d√©rivabilit√©*][KahnMaltsiniotis2008]

-/

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ v‚ÇÑ v‚ÇÅ' v‚ÇÇ' v‚ÇÉ' v‚ÇÑ' u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ u‚ÇÅ' u‚ÇÇ' u‚ÇÉ' u‚ÇÑ'

namespace CategoryTheory

open Limits

namespace IsConnected

variable {C D : Type*} [Category C] [Category D]

instance [IsConnected C] [IsConnected D] : IsConnected (C √ó D) := by
  apply zigzag_isConnected
  intro ‚ü®X‚ÇÅ, Y‚ÇÅ‚ü© ‚ü®X‚ÇÇ, Y‚ÇÇ‚ü©
  exact (zigzag_obj_of_zigzag (Functor.prod' (ùü≠ C) ((Functor.const C).obj Y‚ÇÅ))
      (isPreconnected_zigzag X‚ÇÅ X‚ÇÇ)).trans
    (zigzag_obj_of_zigzag (Functor.prod' ((Functor.const D).obj X‚ÇÇ) (ùü≠ D))
      (isPreconnected_zigzag Y‚ÇÅ Y‚ÇÇ))

end IsConnected

open Category

variable {C‚ÇÅ : Type u‚ÇÅ} {C‚ÇÇ : Type u‚ÇÇ} {C‚ÇÉ : Type u‚ÇÉ} {C‚ÇÑ : Type u‚ÇÑ}
  [Category.{v‚ÇÅ} C‚ÇÅ] [Category.{v‚ÇÇ} C‚ÇÇ] [Category.{v‚ÇÉ} C‚ÇÉ] [Category.{v‚ÇÑ} C‚ÇÑ]
  (T : C‚ÇÅ ‚•§ C‚ÇÇ) (L : C‚ÇÅ ‚•§ C‚ÇÉ) (R : C‚ÇÇ ‚•§ C‚ÇÑ) (B : C‚ÇÉ ‚•§ C‚ÇÑ)

section

variable {T}

theorem StructuredArrow.mk_surjective {X‚ÇÇ : C‚ÇÇ} (f : StructuredArrow X‚ÇÇ T) :
    ‚àÉ (X‚ÇÅ : C‚ÇÅ) (g : X‚ÇÇ ‚ü∂ T.obj X‚ÇÅ), f = mk g := ‚ü®_, _, eq_mk f‚ü©

theorem StructuredArrow.homMk_surjective {X‚ÇÇ : C‚ÇÇ} {f g : StructuredArrow X‚ÇÇ T} (œÜ : f ‚ü∂ g) :
    ‚àÉ (œà : f.right ‚ü∂ g.right) (hœà : f.hom ‚â´ T.map œà = g.hom),
      œÜ = StructuredArrow.homMk œà hœà := ‚ü®œÜ.right, StructuredArrow.w œÜ, rfl‚ü©

theorem CostructuredArrow.mk_surjective {X‚ÇÇ : C‚ÇÇ} (f : CostructuredArrow T X‚ÇÇ) :
    ‚àÉ (X‚ÇÅ : C‚ÇÅ) (g :T.obj X‚ÇÅ ‚ü∂ X‚ÇÇ), f = mk g := ‚ü®_, _, eq_mk f‚ü©

theorem CostructuredArrow.homMk_surjective {X‚ÇÇ : C‚ÇÇ} {f g : CostructuredArrow T X‚ÇÇ} (œÜ : f ‚ü∂ g) :
    ‚àÉ (œà : f.left ‚ü∂ g.left) (hœà : T.map œà ‚â´ g.hom = f.hom),
      œÜ = CostructuredArrow.homMk œà hœà := ‚ü®œÜ.left, CostructuredArrow.w œÜ, rfl‚ü©

end

/-- A `2`-square consists of a natural transformation `T ‚ãô R ‚ü∂ L ‚ãô B`
involving fours functors `T`, `L`, `R`, `B` that are on the
top/left/right/bottom sides of a square of categories. -/
def TwoSquare := T ‚ãô R ‚ü∂ L ‚ãô B

namespace TwoSquare

abbrev mk (Œ± : T ‚ãô R ‚ü∂ L ‚ãô B) : TwoSquare T L R B := Œ±

variable {T L R B}

@[ext]
lemma ext (w w' : TwoSquare T L R B) (h : ‚àÄ (X : C‚ÇÅ), w.app X = w'.app X) :
    w = w' :=
  NatTrans.ext _ _ (funext h)

variable (w : TwoSquare T L R B)

/-- Given `w : TwoSquare T L R B` and `X‚ÇÉ : C‚ÇÉ`, this is the obvious functor
`CostructuredArrow L X‚ÇÉ ‚•§ CostructuredArrow R (B.obj X‚ÇÉ)`. -/
@[simps! obj map]
def costructuredArrowRightwards (X‚ÇÉ : C‚ÇÉ) :
    CostructuredArrow L X‚ÇÉ ‚•§ CostructuredArrow R (B.obj X‚ÇÉ) :=
  CostructuredArrow.post L B X‚ÇÉ ‚ãô Comma.mapLeft _ w ‚ãô
    CostructuredArrow.pre T R (B.obj X‚ÇÉ)

/-- Given `w : TwoSquare T L R B` and `X‚ÇÇ : C‚ÇÇ`, this is the obvious functor
`StructuredArrow X‚ÇÇ T ‚•§ StructuredArrow (R.obj X‚ÇÇ) B`. -/
@[simps! obj map]
def structuredArrowDownwards (X‚ÇÇ : C‚ÇÇ) :
    StructuredArrow X‚ÇÇ T ‚•§ StructuredArrow (R.obj X‚ÇÇ) B :=
  StructuredArrow.post X‚ÇÇ T R ‚ãô Comma.mapRight _ w ‚ãô
    StructuredArrow.pre (R.obj X‚ÇÇ) L B

section

variable {X‚ÇÇ : C‚ÇÇ} {X‚ÇÉ : C‚ÇÉ} (g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ)

/- In [the paper by Kahn and Maltsiniotis, ¬ß4.3][KahnMaltsiniotis2008], given
`w : TwoSquare T L R B` and `g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ`, a category `J` is introduced
and it is observed that it is equivalent to the two categories
`w.StructuredArrowRightwards g` and `w.CostructuredArrowDownwards g`. We shall show below
that there is an equivalence
`w.equivalenceJ g : w.StructuredArrowRightwards g ‚âå w.CostructuredArrowDownwards g`. -/

/-- Given `w : TwoSquare T L R B` and a morphism `g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ`, this is the
category `StructuredArrow (CostructuredArrow.mk g) (w.costructuredArrowRightwards X‚ÇÉ)`,
see the constructor `StructuredArrowRightwards.mk` for the data that is involved. -/
abbrev StructuredArrowRightwards :=
  StructuredArrow (CostructuredArrow.mk g) (w.costructuredArrowRightwards X‚ÇÉ)

/-- Given `w : TwoSquare T L R B` and a morphism `g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ`, this is the
category `CostructuredArrow (w.structuredArrowDownwards X‚ÇÇ) (StructuredArrow.mk g)`,
see the constructor `CostructuredArrowDownwards.mk` for the data that is involved. -/
abbrev CostructuredArrowDownwards :=
  CostructuredArrow (w.structuredArrowDownwards X‚ÇÇ) (StructuredArrow.mk g)

section

variable (X‚ÇÅ : C‚ÇÅ) (a : X‚ÇÇ ‚ü∂ T.obj X‚ÇÅ) (b : L.obj X‚ÇÅ ‚ü∂ X‚ÇÉ)
  (comm : R.map a ‚â´ w.app X‚ÇÅ ‚â´ B.map b = g)

/-- Constructor for objects in `w.StructuredArrowRightwards g`. -/
abbrev StructuredArrowRightwards.mk : w.StructuredArrowRightwards g :=
  StructuredArrow.mk (Y := CostructuredArrow.mk b) (CostructuredArrow.homMk a comm)

/-- Constructor for objects in `w.CostructuredArrowDownwards g`. -/
abbrev CostructuredArrowDownwards.mk : w.CostructuredArrowDownwards g :=
  CostructuredArrow.mk (Y := StructuredArrow.mk a)
    (StructuredArrow.homMk b (by simpa using comm))

variable {w g}

lemma StructuredArrowRightwards.mk_surjective
    (f : w.StructuredArrowRightwards g) :
    ‚àÉ (X‚ÇÅ : C‚ÇÅ) (a : X‚ÇÇ ‚ü∂ T.obj X‚ÇÅ) (b : L.obj X‚ÇÅ ‚ü∂ X‚ÇÉ) (comm : R.map a ‚â´ w.app X‚ÇÅ ‚â´ B.map b = g),
      f = mk w g X‚ÇÅ a b comm := by
  obtain ‚ü®g, œÜ, rfl‚ü© := StructuredArrow.mk_surjective f
  obtain ‚ü®X‚ÇÅ, b, rfl‚ü© := g.mk_surjective
  obtain ‚ü®a, ha, rfl‚ü© := CostructuredArrow.homMk_surjective œÜ
  exact ‚ü®X‚ÇÅ, a, b, by simpa using ha, rfl‚ü©

lemma CostructuredArrowDownwards.mk_surjective
    (f : w.CostructuredArrowDownwards g) :
    ‚àÉ (X‚ÇÅ : C‚ÇÅ) (a : X‚ÇÇ ‚ü∂ T.obj X‚ÇÅ) (b : L.obj X‚ÇÅ ‚ü∂ X‚ÇÉ) (comm : R.map a ‚â´ w.app X‚ÇÅ ‚â´ B.map b = g),
      f = mk w g X‚ÇÅ a b comm := by
  obtain ‚ü®g, œÜ, rfl‚ü© := CostructuredArrow.mk_surjective f
  obtain ‚ü®X‚ÇÅ, a, rfl‚ü© := g.mk_surjective
  obtain ‚ü®b, hb, rfl‚ü© := StructuredArrow.homMk_surjective œÜ
  exact ‚ü®X‚ÇÅ, a, b, by simpa using hb, rfl‚ü©

end

namespace EquivalenceJ

/-- Given `w : TwoSquare T L R B` and a morphism `g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ`, this is
the obvious functor `w.StructuredArrowRightwards g ‚•§ w.CostructuredArrowDownwards g`. -/
@[simps]
def functor : w.StructuredArrowRightwards g ‚•§ w.CostructuredArrowDownwards g where
  obj f := CostructuredArrow.mk (Y := StructuredArrow.mk f.hom.left)
      (StructuredArrow.homMk f.right.hom (by simpa using CostructuredArrow.w f.hom))
  map {f‚ÇÅ f‚ÇÇ} œÜ :=
    CostructuredArrow.homMk (StructuredArrow.homMk œÜ.right.left
      (by dsimp; rw [‚Üê StructuredArrow.w œÜ]; rfl))
      (by ext; exact CostructuredArrow.w œÜ.right)
  map_id _ := rfl
  map_comp _ _ := rfl

/-- Given `w : TwoSquare T L R B` and a morphism `g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ`, this is
the obvious functor `w.CostructuredArrowDownwards g ‚•§ w.StructuredArrowRightwards g`. -/
@[simps]
def inverse : w.CostructuredArrowDownwards g ‚•§ w.StructuredArrowRightwards g where
  obj f := StructuredArrow.mk (Y := CostructuredArrow.mk f.hom.right)
      (CostructuredArrow.homMk f.left.hom (by simpa using StructuredArrow.w f.hom))
  map {f‚ÇÅ f‚ÇÇ} œÜ :=
    StructuredArrow.homMk (CostructuredArrow.homMk œÜ.left.right
      (by dsimp; rw [‚Üê CostructuredArrow.w œÜ]; rfl))
      (by ext; exact StructuredArrow.w œÜ.left)
  map_id _ := rfl
  map_comp _ _ := rfl

end EquivalenceJ

/-- Given `w : TwoSquare T L R B` and a morphism `g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ`, this is
the obvious equivalence of categories
`w.StructuredArrowRightwards g ‚âå w.CostructuredArrowDownwards g`. -/
@[simps functor inverse unitIso counitIso]
def equivalenceJ : w.StructuredArrowRightwards g ‚âå w.CostructuredArrowDownwards g where
  functor := EquivalenceJ.functor w g
  inverse := EquivalenceJ.inverse w g
  unitIso := Iso.refl _
  counitIso := Iso.refl _

lemma isConnected_rightwards_iff_downwards :
    IsConnected (w.StructuredArrowRightwards g) ‚Üî IsConnected (w.CostructuredArrowDownwards g) :=
  isConnected_iff_of_equivalence (w.equivalenceJ g)

end

/-- Condition on `w : TwoSquare T L R B` expressing that it is a Guitart exact square.
It is equivalent to saying that for any `X‚ÇÉ : C‚ÇÉ`, the induced functor
`CostructuredArrow L X‚ÇÉ ‚•§ CostructuredArrow R (B.obj X‚ÇÉ)` is final (see `guitartExact_iff_final`)
or equivalently that for any `X‚ÇÇ : C‚ÇÇ`, the induced functor
`StructuredArrow X‚ÇÇ T ‚•§ StructuredArrow (R.obj X‚ÇÇ) B` is initial (see `guitartExact_iff_initial`).
See also  `guitartExact_iff_isConnected_rightwards`, `guitartExact_iff_isConnected_downwards`
for characterizations in terms of the connectedness of auxiliary categories. -/
class GuitartExact : Prop where
  isConnected_rightwards {X‚ÇÇ : C‚ÇÇ} {X‚ÇÉ : C‚ÇÉ} (g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ) :
    IsConnected (w.StructuredArrowRightwards g)

lemma guitartExact_iff_isConnected_rightwards :
    w.GuitartExact ‚Üî ‚àÄ {X‚ÇÇ : C‚ÇÇ} {X‚ÇÉ : C‚ÇÉ} (g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ),
      IsConnected (w.StructuredArrowRightwards g) :=
  ‚ü®fun h => h.isConnected_rightwards, fun h => ‚ü®h‚ü©‚ü©

lemma guitartExact_iff_isConnected_downwards :
    w.GuitartExact ‚Üî ‚àÄ {X‚ÇÇ : C‚ÇÇ} {X‚ÇÉ : C‚ÇÉ} (g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ),
      IsConnected (w.CostructuredArrowDownwards g) := by
  simp only [guitartExact_iff_isConnected_rightwards,
    isConnected_rightwards_iff_downwards]

instance [hw : w.GuitartExact] {X‚ÇÉ : C‚ÇÉ} (g : CostructuredArrow R (B.obj X‚ÇÉ)) :
    IsConnected (StructuredArrow g (w.costructuredArrowRightwards X‚ÇÉ)) := by
  rw [guitartExact_iff_isConnected_rightwards] at hw
  apply hw

instance [hw : w.GuitartExact] {X‚ÇÇ : C‚ÇÇ} (g : StructuredArrow (R.obj X‚ÇÇ) B) :
    IsConnected (CostructuredArrow (w.structuredArrowDownwards X‚ÇÇ) g) := by
  rw [guitartExact_iff_isConnected_downwards] at hw
  apply hw

lemma guitartExact_iff_final :
    w.GuitartExact ‚Üî ‚àÄ (X‚ÇÉ : C‚ÇÉ), (w.costructuredArrowRightwards X‚ÇÉ).Final :=
  ‚ü®fun _ _ => ‚ü®fun _ => inferInstance‚ü©, fun _ => ‚ü®fun _ => inferInstance‚ü©‚ü©

instance [hw : w.GuitartExact] (X‚ÇÉ : C‚ÇÉ) :
    (w.costructuredArrowRightwards X‚ÇÉ).Final := by
  rw [guitartExact_iff_final] at hw
  apply hw

lemma guitartExact_iff_initial :
    w.GuitartExact ‚Üî ‚àÄ (X‚ÇÇ : C‚ÇÇ), (w.structuredArrowDownwards X‚ÇÇ).Initial :=
  ‚ü®fun _ _ => ‚ü®fun _ => inferInstance‚ü©, by
    rw [guitartExact_iff_isConnected_downwards]
    intros
    infer_instance‚ü©

instance [hw : w.GuitartExact] (X‚ÇÇ : C‚ÇÇ) :
    (w.structuredArrowDownwards X‚ÇÇ).Initial := by
  rw [guitartExact_iff_initial] at hw
  apply hw

instance [L.IsEquivalence] [R.IsEquivalence] [IsIso w] : GuitartExact w := by
  rw [guitartExact_iff_initial]
  intro X‚ÇÇ
  have := StructuredArrow.isEquivalencePost X‚ÇÇ T R
  have : (Comma.mapRight _ w : StructuredArrow (R.obj X‚ÇÇ) _ ‚•§ StructuredArrow (R.obj X‚ÇÇ) _).IsEquivalence :=
    Functor.IsEquivalence.ofEquivalence (Comma.mapRightIso _ (asIso w))
  have := StructuredArrow.isEquivalencePre (R.obj X‚ÇÇ) L B
  dsimp only [structuredArrowDownwards]
  infer_instance

@[simps!]
def whiskerVertical {L' : C‚ÇÅ ‚•§ C‚ÇÉ} {R' : C‚ÇÇ ‚•§ C‚ÇÑ} (Œ± : L ‚ü∂ L') (Œ≤ : R' ‚ü∂ R) :
    TwoSquare T L' R' B :=
  whiskerLeft _ Œ≤ ‚â´ w ‚â´ whiskerRight Œ± _

namespace GuitartExact

lemma whiskerVertical [w.GuitartExact] {L' : C‚ÇÅ ‚•§ C‚ÇÉ} {R' : C‚ÇÇ ‚•§ C‚ÇÑ}
    (Œ± : L ‚âÖ L') (Œ≤ : R ‚âÖ R') : (w.whiskerVertical Œ±.hom Œ≤.inv).GuitartExact := by
  rw [guitartExact_iff_initial]
  intro X‚ÇÇ
  let e : structuredArrowDownwards (w.whiskerVertical Œ±.hom Œ≤.inv) X‚ÇÇ ‚âÖ
      w.structuredArrowDownwards X‚ÇÇ ‚ãô (StructuredArrow.mapIso (Œ≤.app X‚ÇÇ) ).functor :=
    NatIso.ofComponents (fun f => StructuredArrow.isoMk (Œ±.symm.app f.right) (by
      dsimp
      simp only [NatTrans.naturality_assoc, assoc, NatIso.cancel_natIso_inv_left, ‚Üê B.map_comp,
        Iso.hom_inv_id_app, B.map_id, comp_id])) (by aesop_cat)
  rw [Functor.initial_natIso_iff e]
  infer_instance

@[simp]
lemma whiskerVertical_iff {L' : C‚ÇÅ ‚•§ C‚ÇÉ} {R' : C‚ÇÇ ‚•§ C‚ÇÑ}
    (Œ± : L ‚âÖ L') (Œ≤ : R ‚âÖ R') :
    (w.whiskerVertical Œ±.hom Œ≤.inv).GuitartExact ‚Üî w.GuitartExact := by
  constructor
  ¬∑ intro h
    have : w = TwoSquare.whiskerVertical
        (TwoSquare.whiskerVertical w Œ±.hom Œ≤.inv) Œ±.inv Œ≤.hom := by
      ext X‚ÇÅ
      simp only [Functor.comp_obj, whiskerVertical_app, assoc, Iso.hom_inv_id_app_assoc,
        ‚Üê B.map_comp, Iso.hom_inv_id_app, B.map_id, comp_id]
    rw [this]
    exact whiskerVertical (w.whiskerVertical Œ±.hom Œ≤.inv) Œ±.symm Œ≤.symm
  ¬∑ intro h
    exact whiskerVertical w Œ± Œ≤

instance [w.GuitartExact] {L' : C‚ÇÅ ‚•§ C‚ÇÉ} {R' : C‚ÇÇ ‚•§ C‚ÇÑ} (Œ± : L ‚ü∂ L') (Œ≤ : R' ‚ü∂ R)
    [IsIso Œ±] [IsIso Œ≤] : (w.whiskerVertical Œ± Œ≤).GuitartExact :=
  whiskerVertical w (asIso Œ±) (asIso Œ≤).symm

end GuitartExact

section prod

variable {C‚ÇÅ' : Type u‚ÇÅ'} {C‚ÇÇ' : Type u‚ÇÇ'} {C‚ÇÉ' : Type u‚ÇÉ'} {C‚ÇÑ' : Type u‚ÇÑ'}
  [Category.{v‚ÇÅ'} C‚ÇÅ'] [Category.{v‚ÇÇ'} C‚ÇÇ'] [Category.{v‚ÇÉ'} C‚ÇÉ'] [Category.{v‚ÇÑ'} C‚ÇÑ']
  {T' : C‚ÇÅ' ‚•§ C‚ÇÇ'} {L' : C‚ÇÅ' ‚•§ C‚ÇÉ'} {R' : C‚ÇÇ' ‚•§ C‚ÇÑ'} {B' : C‚ÇÉ' ‚•§ C‚ÇÑ'}
  (w' : TwoSquare T' L' R' B')

def prod : TwoSquare (T.prod T') (L.prod L') (R.prod R') (B.prod B') := NatTrans.prod w w'

section

variable {Y‚ÇÇ : C‚ÇÇ √ó C‚ÇÇ'} {Y‚ÇÉ : C‚ÇÉ √ó C‚ÇÉ'} (g : (R.prod R').obj Y‚ÇÇ ‚ü∂ (B.prod B').obj Y‚ÇÉ)

namespace JRightwardsProdEquivalence

@[simp]
def functorObj (X : StructuredArrowRightwards (w.prod w') g) : (StructuredArrowRightwards w g.1) √ó (StructuredArrowRightwards w' g.2) :=
  ‚ü®StructuredArrowRightwards.mk w g.1 _ X.hom.left.1 X.right.hom.1
      (by simpa using congr_arg _root_.Prod.fst X.hom.w),
    StructuredArrowRightwards.mk w' g.2 _ X.hom.left.2 X.right.hom.2
      (by simpa using congr_arg _root_.Prod.snd X.hom.w)‚ü©

@[simps]
def functor : StructuredArrowRightwards (w.prod w') g ‚•§ (StructuredArrowRightwards w g.1) √ó (StructuredArrowRightwards w' g.2) where
  obj X := functorObj w w' g X
  map {X Y} f :=
    ‚ü®StructuredArrow.homMk (CostructuredArrow.homMk f.right.left.1
        (by simpa using congr_arg _root_.Prod.fst f.right.w)) (by
          ext
          have eq := StructuredArrow.w f
          dsimp at eq ‚ä¢
          rw [‚Üê eq]
          rfl),
      StructuredArrow.homMk (CostructuredArrow.homMk f.right.left.2
        (by simpa using congr_arg _root_.Prod.snd f.right.w)) (by
          ext
          have eq := StructuredArrow.w f
          dsimp at eq ‚ä¢
          rw [‚Üê eq]
          rfl)‚ü©
  map_id _ := rfl
  map_comp f g := rfl

@[simp]
def inverseObj (X : (StructuredArrowRightwards w g.1) √ó (StructuredArrowRightwards w' g.2)) : StructuredArrowRightwards (w.prod w') g :=
  StructuredArrowRightwards.mk _ _ ‚ü®X.1.right.left, X.2.right.left‚ü©
    ‚ü®X.1.hom.left, X.2.hom.left‚ü© ‚ü®X.1.right.hom, X.2.right.hom‚ü© (by
      dsimp
      ext
      ¬∑ simpa using X.1.hom.w
      ¬∑ simpa using X.2.hom.w)

@[simps]
def inverse : (StructuredArrowRightwards w g.1) √ó (StructuredArrowRightwards w' g.2) ‚•§ StructuredArrowRightwards (w.prod w') g where
  obj X := inverseObj w w' g X
  map {X Y} f := StructuredArrow.homMk
    (CostructuredArrow.homMk ‚ü®f.1.right.left, f.2.right.left‚ü© (by
      dsimp
      ext
      ¬∑ exact CostructuredArrow.w f.1.right
      ¬∑ exact CostructuredArrow.w f.2.right)) (by
      dsimp
      ext
      dsimp
      ext
      ¬∑ have eq := StructuredArrow.w f.1
        dsimp at eq ‚ä¢
        rw [‚Üê eq]
        rfl
      ¬∑ have eq := StructuredArrow.w f.2
        dsimp at eq ‚ä¢
        rw [‚Üê eq]
        rfl)
  map_id _ := rfl
  map_comp _ _ := rfl

end JRightwardsProdEquivalence

set_option maxHeartbeats 400000 in
@[simps]
def StructuredArrowRightwardsProdEquivalence :
    StructuredArrowRightwards (w.prod w') g ‚âå (StructuredArrowRightwards w g.1) √ó (StructuredArrowRightwards w' g.2) where
  functor := JRightwardsProdEquivalence.functor w w' g
  inverse := JRightwardsProdEquivalence.inverse w w' g
  unitIso := Iso.refl _
  counitIso := Iso.refl _
  functor_unitIso_comp X := by
    dsimp
    erw [comp_id, comp_id]
    rfl

end

namespace GuitartExact

instance prod [w.GuitartExact] [w'.GuitartExact] :
    (w.prod w').GuitartExact := by
  rw [guitartExact_iff_isConnected_rightwards]
  rintro Y‚ÇÇ Y‚ÇÉ g
  exact isConnected_of_equivalent (StructuredArrowRightwardsProdEquivalence w w' g).symm

instance id (F : C‚ÇÅ ‚•§ C‚ÇÇ) : TwoSquare.GuitartExact (show TwoSquare (ùü≠ C‚ÇÅ) F F (ùü≠ C‚ÇÇ) from ùüô F) := by
  rw [guitartExact_iff_isConnected_rightwards]
  intro X‚ÇÇ X‚ÇÉ (g : F.obj X‚ÇÇ ‚ü∂ X‚ÇÉ)
  let Z := StructuredArrowRightwards (show TwoSquare (ùü≠ C‚ÇÅ) F F (ùü≠ C‚ÇÇ) from ùüô F) g
  let X‚ÇÄ : Z := StructuredArrow.mk (Y := CostructuredArrow.mk g) (CostructuredArrow.homMk (ùüô _))
  have œÜ : ‚àÄ (X : Z), X‚ÇÄ ‚ü∂ X := fun X =>
    StructuredArrow.homMk (CostructuredArrow.homMk X.hom.left
      (by simpa using CostructuredArrow.w X.hom))
  have : Nonempty Z := ‚ü®X‚ÇÄ‚ü©
  change IsConnected Z
  apply zigzag_isConnected
  intro X Y
  exact (zigzag_symmetric (Relation.ReflTransGen.single (Or.inl ‚ü®œÜ X‚ü©))).trans
    (Relation.ReflTransGen.single (Or.inl ‚ü®œÜ Y‚ü©))

end GuitartExact

end prod

end TwoSquare

end CategoryTheory
