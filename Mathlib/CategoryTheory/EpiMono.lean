/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Reid Barton, Kim Morrison
-/
import Mathlib.CategoryTheory.Opposites
import Mathlib.CategoryTheory.Groupoid

/-!
# Facts about epimorphisms and monomorphisms.

The definitions of `Epi` and `Mono` are in `CategoryTheory.Category`,
since they are used by some lemmas for `Iso`, which is used everywhere.
-/


universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

namespace CategoryTheory

variable {C : Type uâ‚} [Category.{vâ‚} C]

instance unop_mono_of_epi {A B : Cáµ’áµ–} (f : A âŸ¶ B) [Epi f] : Mono f.unop :=
  âŸ¨fun _ _ eq => Quiver.Hom.op_inj ((cancel_epi f).1 (Quiver.Hom.unop_inj eq))âŸ©

instance unop_epi_of_mono {A B : Cáµ’áµ–} (f : A âŸ¶ B) [Mono f] : Epi f.unop :=
  âŸ¨fun _ _ eq => Quiver.Hom.op_inj ((cancel_mono f).1 (Quiver.Hom.unop_inj eq))âŸ©

instance op_mono_of_epi {A B : C} (f : A âŸ¶ B) [Epi f] : Mono f.op :=
  âŸ¨fun _ _ eq => Quiver.Hom.unop_inj ((cancel_epi f).1 (Quiver.Hom.op_inj eq))âŸ©

instance op_epi_of_mono {A B : C} (f : A âŸ¶ B) [Mono f] : Epi f.op :=
  âŸ¨fun _ _ eq => Quiver.Hom.unop_inj ((cancel_mono f).1 (Quiver.Hom.op_inj eq))âŸ©

/-- A split monomorphism is a morphism `f : X âŸ¶ Y` with a given retraction `retraction f : Y âŸ¶ X`
such that `f â‰« retraction f = ðŸ™ X`.

Every split monomorphism is a monomorphism.
-/
@[ext, aesop apply safe (rule_sets := [CategoryTheory])]
structure SplitMono {X Y : C} (f : X âŸ¶ Y) where
  /-- The map splitting `f` -/
  retraction : Y âŸ¶ X
  /-- `f` composed with `retraction` is the identity -/
  id : f â‰« retraction = ðŸ™ X := by cat_disch

attribute [reassoc (attr := simp)] SplitMono.id

/-- `IsSplitMono f` is the assertion that `f` admits a retraction -/
class IsSplitMono {X Y : C} (f : X âŸ¶ Y) : Prop where
  /-- There is a splitting -/
  exists_splitMono : Nonempty (SplitMono f)

/-- A composition of `SplitMono` is a `SplitMono`. -/
@[simps]
def SplitMono.comp {X Y Z : C} {f : X âŸ¶ Y} {g : Y âŸ¶ Z} (smf : SplitMono f) (smg : SplitMono g) :
    SplitMono (f â‰« g) where
  retraction := smg.retraction â‰« smf.retraction

/-- A constructor for `IsSplitMono f` taking a `SplitMono f` as an argument -/
theorem IsSplitMono.mk' {X Y : C} {f : X âŸ¶ Y} (sm : SplitMono f) : IsSplitMono f :=
  âŸ¨Nonempty.intro smâŸ©

/-- A split epimorphism is a morphism `f : X âŸ¶ Y` with a given section `section_ f : Y âŸ¶ X`
such that `section_ f â‰« f = ðŸ™ Y`.
(Note that `section` is a reserved keyword, so we append an underscore.)

Every split epimorphism is an epimorphism.
-/
@[ext, aesop apply safe (rule_sets := [CategoryTheory])]
structure SplitEpi {X Y : C} (f : X âŸ¶ Y) where
  /-- The map splitting `f` -/
  section_ : Y âŸ¶ X
  /-- `section_` composed with `f` is the identity -/
  id : section_ â‰« f = ðŸ™ Y := by cat_disch

attribute [reassoc (attr := simp)] SplitEpi.id

/-- `IsSplitEpi f` is the assertion that `f` admits a section -/
class IsSplitEpi {X Y : C} (f : X âŸ¶ Y) : Prop where
  /-- There is a splitting -/
  exists_splitEpi : Nonempty (SplitEpi f)

/-- A composition of `SplitEpi` is a split `SplitEpi`. -/
@[simps]
def SplitEpi.comp {X Y Z : C} {f : X âŸ¶ Y} {g : Y âŸ¶ Z} (sef : SplitEpi f) (seg : SplitEpi g) :
    SplitEpi (f â‰« g) where
  section_ := seg.section_ â‰« sef.section_

/-- A constructor for `IsSplitEpi f` taking a `SplitEpi f` as an argument -/
theorem IsSplitEpi.mk' {X Y : C} {f : X âŸ¶ Y} (se : SplitEpi f) : IsSplitEpi f :=
  âŸ¨Nonempty.intro seâŸ©

/-- The chosen retraction of a split monomorphism. -/
noncomputable def retraction {X Y : C} (f : X âŸ¶ Y) [hf : IsSplitMono f] : Y âŸ¶ X :=
  hf.exists_splitMono.some.retraction

@[reassoc (attr := simp)]
theorem IsSplitMono.id {X Y : C} (f : X âŸ¶ Y) [hf : IsSplitMono f] : f â‰« retraction f = ðŸ™ X :=
  hf.exists_splitMono.some.id

/-- The retraction of a split monomorphism has an obvious section. -/
def SplitMono.splitEpi {X Y : C} {f : X âŸ¶ Y} (sm : SplitMono f) : SplitEpi sm.retraction where
  section_ := f

/-- The retraction of a split monomorphism is itself a split epimorphism. -/
instance retraction_isSplitEpi {X Y : C} (f : X âŸ¶ Y) [IsSplitMono f] :
    IsSplitEpi (retraction f) :=
  IsSplitEpi.mk' (SplitMono.splitEpi _)

/-- A split mono which is epi is an iso. -/
theorem isIso_of_epi_of_isSplitMono {X Y : C} (f : X âŸ¶ Y) [IsSplitMono f] [Epi f] : IsIso f :=
  âŸ¨âŸ¨retraction f, âŸ¨by simp, by simp [â† cancel_epi f]âŸ©âŸ©âŸ©

/-- The chosen section of a split epimorphism.
(Note that `section` is a reserved keyword, so we append an underscore.)
-/
noncomputable def section_ {X Y : C} (f : X âŸ¶ Y) [hf : IsSplitEpi f] : Y âŸ¶ X :=
  hf.exists_splitEpi.some.section_

@[reassoc (attr := simp)]
theorem IsSplitEpi.id {X Y : C} (f : X âŸ¶ Y) [hf : IsSplitEpi f] : section_ f â‰« f = ðŸ™ Y :=
  hf.exists_splitEpi.some.id

/-- The section of a split epimorphism has an obvious retraction. -/
def SplitEpi.splitMono {X Y : C} {f : X âŸ¶ Y} (se : SplitEpi f) : SplitMono se.section_ where
  retraction := f

/-- The section of a split epimorphism is itself a split monomorphism. -/
instance section_isSplitMono {X Y : C} (f : X âŸ¶ Y) [IsSplitEpi f] : IsSplitMono (section_ f) :=
  IsSplitMono.mk' (SplitEpi.splitMono _)

/-- A split epi which is mono is an iso. -/
theorem isIso_of_mono_of_isSplitEpi {X Y : C} (f : X âŸ¶ Y) [Mono f] [IsSplitEpi f] : IsIso f :=
  âŸ¨âŸ¨section_ f, âŸ¨by simp [â† cancel_mono f], by simpâŸ©âŸ©âŸ©

/-- Every iso is a split mono. -/
instance (priority := 100) IsSplitMono.of_iso {X Y : C} (f : X âŸ¶ Y) [IsIso f] : IsSplitMono f :=
  IsSplitMono.mk' { retraction := inv f }

/-- Every iso is a split epi. -/
instance (priority := 100) IsSplitEpi.of_iso {X Y : C} (f : X âŸ¶ Y) [IsIso f] : IsSplitEpi f :=
  IsSplitEpi.mk' { section_ := inv f }

theorem SplitMono.mono {X Y : C} {f : X âŸ¶ Y} (sm : SplitMono f) : Mono f :=
  { right_cancellation := fun g h w => by replace w := w =â‰« sm.retraction; simpa using w }

/-- Every split mono is a mono. -/
instance (priority := 100) IsSplitMono.mono {X Y : C} (f : X âŸ¶ Y) [hf : IsSplitMono f] : Mono f :=
  hf.exists_splitMono.some.mono

theorem SplitEpi.epi {X Y : C} {f : X âŸ¶ Y} (se : SplitEpi f) : Epi f :=
  { left_cancellation := fun g h w => by replace w := se.section_ â‰«= w; simpa using w }

/-- Every split epi is an epi. -/
instance (priority := 100) IsSplitEpi.epi {X Y : C} (f : X âŸ¶ Y) [hf : IsSplitEpi f] : Epi f :=
  hf.exists_splitEpi.some.epi

instance {X Y Z : C} {f : X âŸ¶ Y} {g : Y âŸ¶ Z} [hf : IsSplitMono f] [hg : IsSplitMono g] :
    IsSplitMono (f â‰« g) := IsSplitMono.mk' <| hf.exists_splitMono.some.comp hg.exists_splitMono.some

instance {X Y Z : C} {f : X âŸ¶ Y} {g : Y âŸ¶ Z} [hf : IsSplitEpi f] [hg : IsSplitEpi g] :
    IsSplitEpi (f â‰« g) := IsSplitEpi.mk' <| hf.exists_splitEpi.some.comp hg.exists_splitEpi.some

/-- Every split mono whose retraction is mono is an iso. -/
theorem IsIso.of_mono_retraction' {X Y : C} {f : X âŸ¶ Y} (hf : SplitMono f) [Mono <| hf.retraction] :
    IsIso f :=
  âŸ¨âŸ¨hf.retraction, âŸ¨by simp, (cancel_mono_id <| hf.retraction).mp (by simp)âŸ©âŸ©âŸ©

/-- Every split mono whose retraction is mono is an iso. -/
theorem IsIso.of_mono_retraction {X Y : C} (f : X âŸ¶ Y) [hf : IsSplitMono f]
    [hf' : Mono <| retraction f] : IsIso f :=
  @IsIso.of_mono_retraction' _ _ _ _ _ hf.exists_splitMono.some hf'

/-- Every split epi whose section is epi is an iso. -/
theorem IsIso.of_epi_section' {X Y : C} {f : X âŸ¶ Y} (hf : SplitEpi f) [Epi <| hf.section_] :
    IsIso f :=
  âŸ¨âŸ¨hf.section_, âŸ¨(cancel_epi_id <| hf.section_).mp (by simp), by simpâŸ©âŸ©âŸ©

/-- Every split epi whose section is epi is an iso. -/
theorem IsIso.of_epi_section {X Y : C} (f : X âŸ¶ Y) [hf : IsSplitEpi f] [hf' : Epi <| section_ f] :
    IsIso f :=
  @IsIso.of_epi_section' _ _ _ _ _ hf.exists_splitEpi.some hf'

-- FIXME this has unnecessarily become noncomputable!
/-- A category where every morphism has a `Trunc` retraction is computably a groupoid. -/
noncomputable def Groupoid.ofTruncSplitMono
    (all_split_mono : âˆ€ {X Y : C} (f : X âŸ¶ Y), Trunc (IsSplitMono f)) : Groupoid.{vâ‚} C := by
  apply Groupoid.ofIsIso
  intro X Y f
  have âŸ¨a,_âŸ© := Trunc.exists_rep <| all_split_mono f
  have âŸ¨b,_âŸ© := Trunc.exists_rep <| all_split_mono <| retraction f
  apply IsIso.of_mono_retraction

section

variable (C)

/-- A split mono category is a category in which every monomorphism is split. -/
class SplitMonoCategory : Prop where
  /-- All monos are split -/
  isSplitMono_of_mono : âˆ€ {X Y : C} (f : X âŸ¶ Y) [Mono f], IsSplitMono f

/-- A split epi category is a category in which every epimorphism is split. -/
class SplitEpiCategory : Prop where
  /-- All epis are split -/
  isSplitEpi_of_epi : âˆ€ {X Y : C} (f : X âŸ¶ Y) [Epi f], IsSplitEpi f

end

/-- In a category in which every monomorphism is split, every monomorphism splits. This is not an
    instance because it would create an instance loop. -/
theorem isSplitMono_of_mono [SplitMonoCategory C] {X Y : C} (f : X âŸ¶ Y) [Mono f] : IsSplitMono f :=
  SplitMonoCategory.isSplitMono_of_mono _

/-- In a category in which every epimorphism is split, every epimorphism splits. This is not an
    instance because it would create an instance loop. -/
theorem isSplitEpi_of_epi [SplitEpiCategory C] {X Y : C} (f : X âŸ¶ Y) [Epi f] : IsSplitEpi f :=
  SplitEpiCategory.isSplitEpi_of_epi _

section

variable {D : Type uâ‚‚} [Category.{vâ‚‚} D]

/-- Split monomorphisms are also absolute monomorphisms. -/
@[simps]
def SplitMono.map {X Y : C} {f : X âŸ¶ Y} (sm : SplitMono f) (F : C â¥¤ D) : SplitMono (F.map f) where
  retraction := F.map sm.retraction
  id := by rw [â† Functor.map_comp, SplitMono.id, Functor.map_id]

/-- Split epimorphisms are also absolute epimorphisms. -/
@[simps]
def SplitEpi.map {X Y : C} {f : X âŸ¶ Y} (se : SplitEpi f) (F : C â¥¤ D) : SplitEpi (F.map f) where
  section_ := F.map se.section_
  id := by rw [â† Functor.map_comp, SplitEpi.id, Functor.map_id]

instance {X Y : C} (f : X âŸ¶ Y) [hf : IsSplitMono f] (F : C â¥¤ D) : IsSplitMono (F.map f) :=
  IsSplitMono.mk' (hf.exists_splitMono.some.map F)

instance {X Y : C} (f : X âŸ¶ Y) [hf : IsSplitEpi f] (F : C â¥¤ D) : IsSplitEpi (F.map f) :=
  IsSplitEpi.mk' (hf.exists_splitEpi.some.map F)

end

section

/-- When `f` is an epimorphism, `f â‰« g` is epic iff `g` is. -/
@[simp]
lemma epi_comp_iff_of_epi {X Y Z : C} (f : X âŸ¶ Y) [Epi f] (g : Y âŸ¶ Z) :
    Epi (f â‰« g) â†” Epi g :=
  âŸ¨fun _ â†¦ epi_of_epi f _, fun _ â†¦ inferInstanceâŸ©

/-- When `g` is an isomorphism, `f â‰« g` is epic iff `f` is. -/
@[simp]
lemma epi_comp_iff_of_isIso {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [IsIso g] :
    Epi (f â‰« g) â†” Epi f := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ inferInstanceâŸ©
  simpa using (inferInstance : Epi ((f â‰« g) â‰« inv g ))

/-- When `f` is an isomorphism, `f â‰« g` is monic iff `g` is. -/
@[simp]
lemma mono_comp_iff_of_isIso {X Y Z : C} (f : X âŸ¶ Y) [IsIso f] (g : Y âŸ¶ Z) :
    Mono (f â‰« g) â†” Mono g := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ inferInstanceâŸ©
  simpa using (inferInstance : Mono (inv f â‰« f â‰« g))

/-- When `g` is a monomorphism, `f â‰« g` is monic iff `f` is. -/
@[simp]
lemma mono_comp_iff_of_mono {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [Mono g] :
    Mono (f â‰« g) â†” Mono f :=
  âŸ¨fun _ â†¦ mono_of_mono _ g, fun _ â†¦ inferInstanceâŸ©

end

end CategoryTheory
