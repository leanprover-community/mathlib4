/-
Copyright (c) 2019 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Algebra.Group.Units.Equiv
import Mathlib.CategoryTheory.Endomorphism

/-!
# Conjugate morphisms by isomorphisms

An isomorphism `Î± : X â‰… Y` defines
- a monoid isomorphism
  `CategoryTheory.Iso.conj : End X â‰ƒ* End Y` by `Î±.conj f = Î±.inv â‰« f â‰« Î±.hom`;
- a group isomorphism `CategoryTheory.Iso.conjAut : Aut X â‰ƒ* Aut Y` by
  `Î±.conjAut f = Î±.symm â‰ªâ‰« f â‰ªâ‰« Î±`.

For completeness, we also define
`CategoryTheory.Iso.homCongr : (X â‰… Xâ‚) â†’ (Y â‰… Yâ‚) â†’ (X âŸ¶ Y) â‰ƒ (Xâ‚ âŸ¶ Yâ‚)`,
cf. `Equiv.arrowCongr`,
and `CategoryTheory.Iso.isoCongr : (f : Xâ‚ â‰… Xâ‚‚) â†’ (g : Yâ‚ â‰… Yâ‚‚) â†’ (Xâ‚ â‰… Yâ‚) â‰ƒ (Xâ‚‚ â‰… Yâ‚‚)`.
-/


universe v u

namespace CategoryTheory

namespace Iso

variable {C : Type u} [Category.{v} C]

/-- If `X` is isomorphic to `Xâ‚` and `Y` is isomorphic to `Yâ‚`, then
there is a natural bijection between `X âŸ¶ Y` and `Xâ‚ âŸ¶ Yâ‚`. See also `Equiv.arrowCongr`. -/
def homCongr {X Y Xâ‚ Yâ‚ : C} (Î± : X â‰… Xâ‚) (Î² : Y â‰… Yâ‚) : (X âŸ¶ Y) â‰ƒ (Xâ‚ âŸ¶ Yâ‚) where
  toFun f := Î±.inv â‰« f â‰« Î².hom
  invFun f := Î±.hom â‰« f â‰« Î².inv
  left_inv f :=
    show Î±.hom â‰« (Î±.inv â‰« f â‰« Î².hom) â‰« Î².inv = f by
      rw [Category.assoc, Category.assoc, Î².hom_inv_id, Î±.hom_inv_id_assoc, Category.comp_id]
  right_inv f :=
    show Î±.inv â‰« (Î±.hom â‰« f â‰« Î².inv) â‰« Î².hom = f by
      rw [Category.assoc, Category.assoc, Î².inv_hom_id, Î±.inv_hom_id_assoc, Category.comp_id]

-- @[simp, nolint simpNF] Porting note (#10675): dsimp can not prove this
@[simp]
theorem homCongr_apply {X Y Xâ‚ Yâ‚ : C} (Î± : X â‰… Xâ‚) (Î² : Y â‰… Yâ‚) (f : X âŸ¶ Y) :
    Î±.homCongr Î² f = Î±.inv â‰« f â‰« Î².hom := by
  rfl

theorem homCongr_comp {X Y Z Xâ‚ Yâ‚ Zâ‚ : C} (Î± : X â‰… Xâ‚) (Î² : Y â‰… Yâ‚) (Î³ : Z â‰… Zâ‚) (f : X âŸ¶ Y)
    (g : Y âŸ¶ Z) : Î±.homCongr Î³ (f â‰« g) = Î±.homCongr Î² f â‰« Î².homCongr Î³ g := by simp

/- Porting note (#10618): removed `@[simp]`; simp can prove this -/
theorem homCongr_refl {X Y : C} (f : X âŸ¶ Y) : (Iso.refl X).homCongr (Iso.refl Y) f = f := by simp

/- Porting note (#10618): removed `@[simp]`; simp can prove this -/
theorem homCongr_trans {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ Xâ‚ƒ Yâ‚ƒ : C} (Î±â‚ : Xâ‚ â‰… Xâ‚‚) (Î²â‚ : Yâ‚ â‰… Yâ‚‚) (Î±â‚‚ : Xâ‚‚ â‰… Xâ‚ƒ)
    (Î²â‚‚ : Yâ‚‚ â‰… Yâ‚ƒ) (f : Xâ‚ âŸ¶ Yâ‚) :
    (Î±â‚ â‰ªâ‰« Î±â‚‚).homCongr (Î²â‚ â‰ªâ‰« Î²â‚‚) f = (Î±â‚.homCongr Î²â‚).trans (Î±â‚‚.homCongr Î²â‚‚) f := by simp

@[simp]
theorem homCongr_symm {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : C} (Î± : Xâ‚ â‰… Xâ‚‚) (Î² : Yâ‚ â‰… Yâ‚‚) :
    (Î±.homCongr Î²).symm = Î±.symm.homCongr Î².symm :=
  rfl

/-- If `X` is isomorphic to `Xâ‚` and `Y` is isomorphic to `Yâ‚`, then
there is a bijection between `X â‰… Y` and `Xâ‚ â‰… Yâ‚`. -/
def isoCongr {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : C} (f : Xâ‚ â‰… Xâ‚‚) (g : Yâ‚ â‰… Yâ‚‚) : (Xâ‚ â‰… Yâ‚) â‰ƒ (Xâ‚‚ â‰… Yâ‚‚) where
  toFun h := f.symm.trans <| h.trans <| g
  invFun h := f.trans <| h.trans <| g.symm
  left_inv := by aesop_cat
  right_inv := by aesop_cat

/-- If `Xâ‚` is isomorphic to `Xâ‚‚`, then there is a bijection between `Xâ‚ â‰… Y` and `Xâ‚‚ â‰… Y`. -/
def isoCongrLeft {Xâ‚ Xâ‚‚ Y : C} (f : Xâ‚ â‰… Xâ‚‚) : (Xâ‚ â‰… Y) â‰ƒ (Xâ‚‚ â‰… Y) :=
  isoCongr f (Iso.refl _)

/-- If `Yâ‚` is isomorphic to `Yâ‚‚`, then there is a bijection between `X â‰… Yâ‚` and `X â‰… Yâ‚‚`. -/
def isoCongrRight {X Yâ‚ Yâ‚‚ : C} (g : Yâ‚ â‰… Yâ‚‚) : (X â‰… Yâ‚) â‰ƒ (X â‰… Yâ‚‚) :=
  isoCongr (Iso.refl _) g

variable {X Y : C} (Î± : X â‰… Y)

/-- An isomorphism between two objects defines a monoid isomorphism between their
monoid of endomorphisms. -/
def conj : End X â‰ƒ* End Y :=
  { homCongr Î± Î± with map_mul' := fun f g => homCongr_comp Î± Î± Î± g f }

theorem conj_apply (f : End X) : Î±.conj f = Î±.inv â‰« f â‰« Î±.hom :=
  rfl

@[simp]
theorem conj_comp (f g : End X) : Î±.conj (f â‰« g) = Î±.conj f â‰« Î±.conj g :=
  Î±.conj.map_mul g f

@[simp]
theorem conj_id : Î±.conj (ğŸ™ X) = ğŸ™ Y :=
  Î±.conj.map_one

@[simp]
theorem refl_conj (f : End X) : (Iso.refl X).conj f = f := by
  rw [conj_apply, Iso.refl_inv, Iso.refl_hom, Category.id_comp, Category.comp_id]

@[simp]
theorem trans_conj {Z : C} (Î² : Y â‰… Z) (f : End X) : (Î± â‰ªâ‰« Î²).conj f = Î².conj (Î±.conj f) :=
  homCongr_trans Î± Î± Î² Î² f

@[simp]
theorem symm_self_conj (f : End X) : Î±.symm.conj (Î±.conj f) = f := by
  rw [â† trans_conj, Î±.self_symm_id, refl_conj]

@[simp]
theorem self_symm_conj (f : End Y) : Î±.conj (Î±.symm.conj f) = f :=
  Î±.symm.symm_self_conj f

/- Porting note (#10618): removed `@[simp]`; simp can prove this -/
theorem conj_pow (f : End X) (n : â„•) : Î±.conj (f ^ n) = Î±.conj f ^ n :=
  Î±.conj.toMonoidHom.map_pow f n

-- Porting note (#11215): TODO: change definition so that `conjAut_apply` becomes a `rfl`?
/-- `conj` defines a group isomorphisms between groups of automorphisms -/
def conjAut : Aut X â‰ƒ* Aut Y :=
  (Aut.unitsEndEquivAut X).symm.trans <| (Units.mapEquiv Î±.conj).trans <| Aut.unitsEndEquivAut Y

theorem conjAut_apply (f : Aut X) : Î±.conjAut f = Î±.symm â‰ªâ‰« f â‰ªâ‰« Î± := by aesop_cat

@[simp]
theorem conjAut_hom (f : Aut X) : (Î±.conjAut f).hom = Î±.conj f.hom :=
  rfl

@[simp]
theorem trans_conjAut {Z : C} (Î² : Y â‰… Z) (f : Aut X) :
    (Î± â‰ªâ‰« Î²).conjAut f = Î².conjAut (Î±.conjAut f) := by
  simp only [conjAut_apply, Iso.trans_symm, Iso.trans_assoc]

/- Porting note (#10618): removed `@[simp]`; simp can prove this -/
theorem conjAut_mul (f g : Aut X) : Î±.conjAut (f * g) = Î±.conjAut f * Î±.conjAut g :=
  Î±.conjAut.map_mul f g

@[simp]
theorem conjAut_trans (f g : Aut X) : Î±.conjAut (f â‰ªâ‰« g) = Î±.conjAut f â‰ªâ‰« Î±.conjAut g :=
  conjAut_mul Î± g f

/- Porting note (#10618): removed `@[simp]`; simp can prove this -/
theorem conjAut_pow (f : Aut X) (n : â„•) : Î±.conjAut (f ^ n) = Î±.conjAut f ^ n :=
  Î±.conjAut.toMonoidHom.map_pow f n

/- Porting note (#10618): removed `@[simp]`; simp can prove this -/
theorem conjAut_zpow (f : Aut X) (n : â„¤) : Î±.conjAut (f ^ n) = Î±.conjAut f ^ n :=
  Î±.conjAut.toMonoidHom.map_zpow f n

end Iso

namespace Functor

universe vâ‚ uâ‚

variable {C : Type u} [Category.{v} C] {D : Type uâ‚} [Category.{vâ‚} D] (F : C â¥¤ D)

theorem map_homCongr {X Y Xâ‚ Yâ‚ : C} (Î± : X â‰… Xâ‚) (Î² : Y â‰… Yâ‚) (f : X âŸ¶ Y) :
    F.map (Iso.homCongr Î± Î² f) = Iso.homCongr (F.mapIso Î±) (F.mapIso Î²) (F.map f) := by simp

theorem map_conj {X Y : C} (Î± : X â‰… Y) (f : End X) :
    F.map (Î±.conj f) = (F.mapIso Î±).conj (F.map f) :=
  map_homCongr F Î± Î± f

theorem map_conjAut (F : C â¥¤ D) {X Y : C} (Î± : X â‰… Y) (f : Aut X) :
    F.mapIso (Î±.conjAut f) = (F.mapIso Î±).conjAut (F.mapIso f) := by
  ext; simp only [mapIso_hom, Iso.conjAut_hom, F.map_conj]

-- alternative proof: by simp only [Iso.conjAut_apply, F.mapIso_trans, F.mapIso_symm]
end Functor

end CategoryTheory
