/-
Copyright (c) 2019 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Algebra.Group.Units.Equiv
import Mathlib.CategoryTheory.Endomorphism
import Mathlib.CategoryTheory.HomCongr

/-!
# Conjugate morphisms by isomorphisms

An isomorphism `Î± : X â‰… Y` defines
- a monoid isomorphism
  `CategoryTheory.Iso.conj : End X â‰ƒ* End Y` by `Î±.conj f = Î±.inv â‰« f â‰« Î±.hom`;
- a group isomorphism `CategoryTheory.Iso.conjAut : Aut X â‰ƒ* Aut Y` by
  `Î±.conjAut f = Î±.symm â‰ªâ‰« f â‰ªâ‰« Î±`
using
`CategoryTheory.Iso.homCongr : (X â‰… Xâ‚) â†’ (Y â‰… Yâ‚) â†’ (X âŸ¶ Y) â‰ƒ (Xâ‚ âŸ¶ Yâ‚)`
and `CategoryTheory.Iso.isoCongr : (f : Xâ‚ â‰… Xâ‚‚) â†’ (g : Yâ‚ â‰… Yâ‚‚) â†’ (Xâ‚ â‰… Yâ‚) â‰ƒ (Xâ‚‚ â‰… Yâ‚‚)`
which are defined in  `CategoryTheory.HomCongr`.
-/

universe v u

namespace CategoryTheory

namespace Iso

variable {C : Type u} [Category.{v} C]

variable {X Y : C} (Î± : X â‰… Y)

/-- An isomorphism between two objects defines a monoid isomorphism between their
monoid of endomorphisms. -/
def conj : End X â‰ƒ* End Y :=
  { homCongr Î± Î± with map_mul' := fun f g => homCongr_comp Î± Î± Î± g f }

theorem conj_apply (f : End X) : Î±.conj f = Î±.inv â‰« f â‰« Î±.hom :=
  rfl

@[simp]
theorem conj_comp (f g : End X) : Î±.conj (f â‰« g) = Î±.conj f â‰« Î±.conj g :=
  map_mul Î±.conj g f

@[simp]
theorem conj_id : Î±.conj (ğŸ™ X) = ğŸ™ Y :=
  map_one Î±.conj

@[simp]
theorem refl_conj (f : End X) : (Iso.refl X).conj f = f := by
  rw [conj_apply, Iso.refl_inv, Iso.refl_hom, Category.id_comp, Category.comp_id]

@[simp]
theorem trans_conj {Z : C} (Î² : Y â‰… Z) (f : End X) : (Î± â‰ªâ‰« Î²).conj f = Î².conj (Î±.conj f) :=
  homCongr_trans Î± Î± Î² Î² f

@[simp]
theorem symm_self_conj (f : End X) : Î±.symm.conj (Î±.conj f) = f := by
  rw [â† trans_conj, Î±.self_symm_id, refl_conj]

@[simp]
theorem self_symm_conj (f : End Y) : Î±.conj (Î±.symm.conj f) = f :=
  Î±.symm.symm_self_conj f

@[simp]
theorem conj_pow (f : End X) (n : â„•) : Î±.conj (f ^ n) = Î±.conj f ^ n :=
  Î±.conj.toMonoidHom.map_pow f n

-- TODO: change definition so that `conjAut_apply` becomes a `rfl`?
/-- `conj` defines a group isomorphisms between groups of automorphisms -/
def conjAut : Aut X â‰ƒ* Aut Y :=
  (Aut.unitsEndEquivAut X).symm.trans <| (Units.mapEquiv Î±.conj).trans <| Aut.unitsEndEquivAut Y

theorem conjAut_apply (f : Aut X) : Î±.conjAut f = Î±.symm â‰ªâ‰« f â‰ªâ‰« Î± := by cat_disch

@[simp]
theorem conjAut_hom (f : Aut X) : (Î±.conjAut f).hom = Î±.conj f.hom :=
  rfl

@[simp]
theorem trans_conjAut {Z : C} (Î² : Y â‰… Z) (f : Aut X) :
    (Î± â‰ªâ‰« Î²).conjAut f = Î².conjAut (Î±.conjAut f) := by
  simp only [conjAut_apply, Iso.trans_symm, Iso.trans_assoc]

@[simp]
theorem conjAut_mul (f g : Aut X) : Î±.conjAut (f * g) = Î±.conjAut f * Î±.conjAut g :=
  map_mul Î±.conjAut f g

@[simp]
theorem conjAut_trans (f g : Aut X) : Î±.conjAut (f â‰ªâ‰« g) = Î±.conjAut f â‰ªâ‰« Î±.conjAut g :=
  conjAut_mul Î± g f

@[simp]
theorem conjAut_pow (f : Aut X) (n : â„•) : Î±.conjAut (f ^ n) = Î±.conjAut f ^ n :=
  map_pow Î±.conjAut f n

@[simp]
theorem conjAut_zpow (f : Aut X) (n : â„¤) : Î±.conjAut (f ^ n) = Î±.conjAut f ^ n :=
  map_zpow Î±.conjAut f n

end Iso

namespace Functor

universe vâ‚ uâ‚

variable {C : Type u} [Category.{v} C] {D : Type uâ‚} [Category.{vâ‚} D] (F : C â¥¤ D)

theorem map_conj {X Y : C} (Î± : X â‰… Y) (f : End X) :
    F.map (Î±.conj f) = (F.mapIso Î±).conj (F.map f) :=
  map_homCongr F Î± Î± f

theorem map_conjAut (F : C â¥¤ D) {X Y : C} (Î± : X â‰… Y) (f : Aut X) :
    F.mapIso (Î±.conjAut f) = (F.mapIso Î±).conjAut (F.mapIso f) := by
  ext; simp only [mapIso_hom, Iso.conjAut_hom, F.map_conj]

-- alternative proof: by simp only [Iso.conjAut_apply, F.mapIso_trans, F.mapIso_symm]
end Functor

end CategoryTheory
