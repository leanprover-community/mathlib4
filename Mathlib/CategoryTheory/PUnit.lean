/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Bhavik Mehta
-/
import Mathlib.CategoryTheory.Functor.Const
import Mathlib.CategoryTheory.DiscreteCategory

#align_import category_theory.punit from "leanprover-community/mathlib"@"2738d2ca56cbc63be80c3bd48e9ed90ad94e947d"

/-!
# The category `Discrete PUnit`

We define `star : C ‚•§ Discrete PUnit` sending everything to `PUnit.star`,
show that any two functors to `Discrete PUnit` are naturally isomorphic,
and construct the equivalence `(Discrete PUnit ‚•§ C) ‚âå C`.
-/


universe w v u

-- morphism levels before object levels. See note [CategoryTheory universes].
namespace CategoryTheory

variable (C : Type u) [Category.{v} C]

namespace Functor

/-- The constant functor sending everything to `PUnit.star`. -/
@[simps!]
def star : C ‚•§ Discrete PUnit.{w + 1} :=
  (Functor.const _).obj ‚ü®‚ü®‚ü©‚ü©
#align category_theory.functor.star CategoryTheory.Functor.star
-- Porting note: simp can simplify this
attribute [nolint simpNF] star_map_down_down
variable {C}

/-- Any two functors to `Discrete PUnit` are isomorphic. -/
@[simps!]
def punitExt (F G : C ‚•§ Discrete PUnit.{w + 1}) : F ‚âÖ G :=
  NatIso.ofComponents fun X => eqToIso (by simp only [eq_iff_true_of_subsingleton])
                                           -- üéâ no goals
#align category_theory.functor.punit_ext CategoryTheory.Functor.punitExt
-- Porting note: simp does indeed fire for these despite the linter warning
attribute [nolint simpNF] punitExt_hom_app_down_down punitExt_inv_app_down_down

/-- Any two functors to `Discrete PUnit` are *equal*.
You probably want to use `punitExt` instead of this. -/
theorem punit_ext' (F G : C ‚•§ Discrete PUnit.{w + 1}) : F = G :=
  Functor.ext fun X => by simp only [eq_iff_true_of_subsingleton]
                          -- üéâ no goals
#align category_theory.functor.punit_ext' CategoryTheory.Functor.punit_ext'

/-- The functor from `Discrete PUnit` sending everything to the given object. -/
abbrev fromPUnit (X : C) : Discrete PUnit.{w + 1} ‚•§ C :=
  (Functor.const _).obj X
#align category_theory.functor.from_punit CategoryTheory.Functor.fromPUnit

/-- Functors from `Discrete PUnit` are equivalent to the category itself. -/
@[simps]
def equiv : Discrete PUnit.{w + 1} ‚•§ C ‚âå C where
  functor :=
    { obj := fun F => F.obj ‚ü®‚ü®‚ü©‚ü©
      map := fun Œ∏ => Œ∏.app ‚ü®‚ü®‚ü©‚ü© }
  inverse := Functor.const _
  unitIso := NatIso.ofComponents fun X => Discrete.natIso fun i => Iso.refl _
  counitIso := NatIso.ofComponents Iso.refl
#align category_theory.functor.equiv CategoryTheory.Functor.equiv

end Functor

/-- A category being equivalent to `PUnit` is equivalent to it having a unique morphism between
  any two objects. (In fact, such a category is also a groupoid;
  see `CategoryTheory.Groupoid.ofHomUnique`) -/
theorem equiv_punit_iff_unique :
    Nonempty (C ‚âå Discrete PUnit.{w + 1}) ‚Üî Nonempty C ‚àß ‚àÄ x y : C, Nonempty <| Unique (x ‚ü∂ y) := by
  constructor
  -- ‚ä¢ Nonempty (C ‚âå Discrete PUnit) ‚Üí Nonempty C ‚àß ‚àÄ (x y : C), Nonempty (Unique ( ‚Ä¶
  ¬∑ rintro ‚ü®h‚ü©
    -- ‚ä¢ Nonempty C ‚àß ‚àÄ (x y : C), Nonempty (Unique (x ‚ü∂ y))
    refine' ‚ü®‚ü®h.inverse.obj ‚ü®‚ü®‚ü©‚ü©‚ü©, fun x y => Nonempty.intro _‚ü©
    -- ‚ä¢ Unique (x ‚ü∂ y)
    let f : x ‚ü∂ y := by
      have hx : x ‚ü∂ h.inverse.obj ‚ü®‚ü®‚ü©‚ü© := by convert h.unit.app x
      have hy : h.inverse.obj ‚ü®‚ü®‚ü©‚ü© ‚ü∂ y := by convert h.unitInv.app y
      exact hx ‚â´ hy
    suffices sub : Subsingleton (x ‚ü∂ y) from uniqueOfSubsingleton f
    -- ‚ä¢ Subsingleton (x ‚ü∂ y)
    have : ‚àÄ z, z = h.unit.app x ‚â´ (h.functor ‚ãô h.inverse).map z ‚â´ h.unitInv.app y := by
      intro z
      simp [congrArg (¬∑ ‚â´ h.unitInv.app y) (h.unit.naturality z)]
    apply Subsingleton.intro
    -- ‚ä¢ ‚àÄ (a b : x ‚ü∂ y), a = b
    intro a b
    -- ‚ä¢ a = b
    rw [this a, this b]
    -- ‚ä¢ NatTrans.app (Equivalence.unit h) x ‚â´ (h.functor ‚ãô h.inverse).map a ‚â´ NatTra ‚Ä¶
    simp only [Functor.comp_map]
    -- ‚ä¢ NatTrans.app (Equivalence.unit h) x ‚â´ h.inverse.map (h.functor.map a) ‚â´ NatT ‚Ä¶
    congr 3
    -- ‚ä¢ h.functor.map a = h.functor.map b
    apply ULift.ext
    -- ‚ä¢ (h.functor.map a).down = (h.functor.map b).down
    simp
    -- üéâ no goals
  ¬∑ rintro ‚ü®‚ü®p‚ü©, h‚ü©
    -- ‚ä¢ Nonempty (C ‚âå Discrete PUnit)
    haveI := fun x y => (h x y).some
    -- ‚ä¢ Nonempty (C ‚âå Discrete PUnit)
    refine'
      Nonempty.intro
        (CategoryTheory.Equivalence.mk ((Functor.const _).obj ‚ü®‚ü®‚ü©‚ü©)
          ((@Functor.const <| Discrete PUnit).obj p) ?_ (by apply Functor.punitExt))
    exact
      NatIso.ofComponents fun _ =>
        { hom := default
          inv := default }
#align category_theory.equiv_punit_iff_unique CategoryTheory.equiv_punit_iff_unique

end CategoryTheory
