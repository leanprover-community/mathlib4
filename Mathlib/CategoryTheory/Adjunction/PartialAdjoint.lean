/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/

import Mathlib.CategoryTheory.Adjunction.Basic
import Mathlib.CategoryTheory.Limits.HasLimits
import Mathlib.CategoryTheory.Yoneda

/-!
# Domain of definition of the partial left adjoint

Given a functor `F : D ‚•§ C`, we define a functor
`F.partialLeftAdjoint : F.PartialLeftAdjointSource ‚•§ D` which is
defined on the full subcategory of `C` consisting of those objects `X : C`
such that `F ‚ãô coyoneda.obj (op X) : D ‚•§ Type _` is corepresentable.
We have a natural bijection
`(F.partialLeftAdjoint.obj X ‚ü∂ Y) ‚âÉ (X.obj ‚ü∂ F.obj Y)`
that is similar to what we would expect for the image of the object `X`
by the left adjoint of `F`, if such an adjoint existed.

Indeed, if the predicate `F.LeftAdjointObjIsDefined` which defines
the `F.PartialLeftAdjointSource` holds for all
objects `X : C`, then `F` has a left adjoint.

When colimits indexed by a category `J` exist in `D`, we show that
the predicate `F.LeftAdjointObjIsDefined` is stable under colimits indexed by `J`.

## TODO
* consider dualizing the results to right adjoints

-/

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory

namespace Functor

open Category Opposite Limits

section partialLeftAdjoint

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] (F : D ‚•§ C)

/-- Given a functor `F : D ‚•§ C`, this is a predicate on objects `X : C` corresponding
to the domain of definition of the (partial) left adjoint of `F`. -/
def leftAdjointObjIsDefined : ObjectProperty C :=
  fun X ‚Ü¶ IsCorepresentable (F ‚ãô coyoneda.obj (op X))

@[deprecated (since := "2025-03-05")] alias LeftAdjointObjIsDefined := leftAdjointObjIsDefined

lemma leftAdjointObjIsDefined_iff (X : C) :
    F.leftAdjointObjIsDefined X ‚Üî IsCorepresentable (F ‚ãô coyoneda.obj (op X)) := by rfl

variable {F} in
lemma leftAdjointObjIsDefined_of_adjunction {G : C ‚•§ D} (adj : G ‚ä£ F) (X : C) :
    F.leftAdjointObjIsDefined X :=
  (adj.corepresentableBy X).isCorepresentable

/-- The full subcategory where `F.partialLeftAdjoint` shall be defined. -/
abbrev PartialLeftAdjointSource := F.leftAdjointObjIsDefined.FullSubcategory

instance (X : F.PartialLeftAdjointSource) :
    IsCorepresentable (F ‚ãô coyoneda.obj (op X.obj)) := X.property

/-- Given `F : D ‚•§ C`, this is `F.partialLeftAdjoint` on objects: it sends
`X : C` such that `F.leftAdjointObjIsDefined X` holds to an object of `D`
which represents the functor `F ‚ãô coyoneda.obj (op X.obj)`. -/
noncomputable def partialLeftAdjointObj (X : F.PartialLeftAdjointSource) : D :=
  (F ‚ãô coyoneda.obj (op X.obj)).coreprX

/-- Given `F : D ‚•§ C`, this is the canonical bijection
`(F.partialLeftAdjointObj X ‚ü∂ Y) ‚âÉ (X.obj ‚ü∂ F.obj Y)`
for all `X : F.PartialLeftAdjointSource` and `Y : D`. -/
noncomputable def partialLeftAdjointHomEquiv {X : F.PartialLeftAdjointSource} {Y : D} :
    (F.partialLeftAdjointObj X ‚ü∂ Y) ‚âÉ (X.obj ‚ü∂ F.obj Y) :=
  (F ‚ãô coyoneda.obj (op X.obj)).corepresentableBy.homEquiv

lemma partialLeftAdjointHomEquiv_comp {X : F.PartialLeftAdjointSource} {Y Y' : D}
    (f : F.partialLeftAdjointObj X ‚ü∂ Y) (g : Y ‚ü∂ Y') :
    F.partialLeftAdjointHomEquiv (f ‚â´ g) =
      F.partialLeftAdjointHomEquiv f ‚â´ F.map g := by
  apply CorepresentableBy.homEquiv_comp

/-- Given `F : D ‚•§ C`, this is `F.partialLeftAdjoint` on morphisms. -/
noncomputable def partialLeftAdjointMap {X Y : F.PartialLeftAdjointSource}
    (f : X ‚ü∂ Y) : F.partialLeftAdjointObj X ‚ü∂ F.partialLeftAdjointObj Y :=
    F.partialLeftAdjointHomEquiv.symm (f.hom ‚â´ F.partialLeftAdjointHomEquiv (ùüô _))

@[simp]
lemma partialLeftAdjointHomEquiv_map {X Y : F.PartialLeftAdjointSource}
    (f : X ‚ü∂ Y) :
    F.partialLeftAdjointHomEquiv (F.partialLeftAdjointMap f) =
      f.hom ‚â´ F.partialLeftAdjointHomEquiv (ùüô _) := by
  simp [partialLeftAdjointMap]

lemma partialLeftAdjointHomEquiv_map_comp {X X' : F.PartialLeftAdjointSource} {Y : D}
    (f : X ‚ü∂ X') (g : F.partialLeftAdjointObj X' ‚ü∂ Y) :
    F.partialLeftAdjointHomEquiv (F.partialLeftAdjointMap f ‚â´ g) =
      f.hom ‚â´ F.partialLeftAdjointHomEquiv g := by
  rw [partialLeftAdjointHomEquiv_comp, partialLeftAdjointHomEquiv_map, assoc,
    ‚Üê partialLeftAdjointHomEquiv_comp, id_comp]

@[reassoc]
lemma partialLeftAdjointHomEquiv_symm_comp {X : F.PartialLeftAdjointSource} {Y Y' : D}
    (f : X.obj ‚ü∂ F.obj Y) (g : Y ‚ü∂ Y') :
    F.partialLeftAdjointHomEquiv.symm f ‚â´ g = F.partialLeftAdjointHomEquiv.symm (f ‚â´ F.map g) :=
  CorepresentableBy.homEquiv_symm_comp ..

@[reassoc]
lemma partialLeftAdjointHomEquiv_comp_symm {X X' : F.PartialLeftAdjointSource} {Y : D}
    (f : X'.obj ‚ü∂ F.obj Y) (g : X ‚ü∂ X') :
    F.partialLeftAdjointMap g ‚â´ F.partialLeftAdjointHomEquiv.symm f =
    F.partialLeftAdjointHomEquiv.symm (g.hom ‚â´ f) := by
  rw [Equiv.eq_symm_apply, partialLeftAdjointHomEquiv_comp, partialLeftAdjointHomEquiv_map,
    assoc, ‚Üê partialLeftAdjointHomEquiv_comp, id_comp, Equiv.apply_symm_apply]

/-- Given `F : D ‚•§ C`, this is the partial adjoint functor `F.PartialLeftAdjointSource ‚•§ D`. -/
@[simps]
noncomputable def partialLeftAdjoint : F.PartialLeftAdjointSource ‚•§ D where
  obj := F.partialLeftAdjointObj
  map := F.partialLeftAdjointMap
  map_id X := by
    apply F.partialLeftAdjointHomEquiv.injective
    dsimp
    rw [partialLeftAdjointHomEquiv_map]
    erw [id_comp]
  map_comp {X Y Z} f g := by
    apply F.partialLeftAdjointHomEquiv.injective
    dsimp
    rw [partialLeftAdjointHomEquiv_map, partialLeftAdjointHomEquiv_comp,
      partialLeftAdjointHomEquiv_map, assoc]
    erw [assoc]
    rw [‚Üê F.partialLeftAdjointHomEquiv_comp, id_comp,
      partialLeftAdjointHomEquiv_map]

variable {F}

lemma isRightAdjoint_of_leftAdjointObjIsDefined_eq_top
    (h : F.leftAdjointObjIsDefined = ‚ä§) : F.IsRightAdjoint := by
  replace h : ‚àÄ X, IsCorepresentable (F ‚ãô coyoneda.obj (op X)) := fun X ‚Ü¶ by
    simp only [‚Üê leftAdjointObjIsDefined_iff, h, Pi.top_apply, Prop.top_eq_true]
  exact (Adjunction.adjunctionOfEquivLeft
    (fun X Y ‚Ü¶ (F ‚ãô coyoneda.obj (op X)).corepresentableBy.homEquiv)
    (fun X Y Y' g f ‚Ü¶ by apply CorepresentableBy.homEquiv_comp)).isRightAdjoint

variable (F) in
lemma isRightAdjoint_iff_leftAdjointObjIsDefined_eq_top :
    F.IsRightAdjoint ‚Üî F.leftAdjointObjIsDefined = ‚ä§ := by
  refine ‚ü®fun h ‚Ü¶ ?_, isRightAdjoint_of_leftAdjointObjIsDefined_eq_top‚ü©
  ext X
  simpa only [Pi.top_apply, Prop.top_eq_true, iff_true]
    using leftAdjointObjIsDefined_of_adjunction (Adjunction.ofIsRightAdjoint F) X

/-- Auxiliary definition for `leftAdjointObjIsDefined_of_isColimit`. -/
noncomputable def corepresentableByCompCoyonedaObjOfIsColimit {J : Type*} [Category J]
    {R : J ‚•§ F.PartialLeftAdjointSource}
    {c : Cocone (R ‚ãô ObjectProperty.Œπ _)} (hc : IsColimit c)
    {c' : Cocone (R ‚ãô F.partialLeftAdjoint)} (hc' : IsColimit c') :
    (F ‚ãô coyoneda.obj (op c.pt)).CorepresentableBy c'.pt where
  homEquiv {Y} :=
    { toFun := fun f ‚Ü¶ hc.desc (Cocone.mk _
        { app := fun j ‚Ü¶ F.partialLeftAdjointHomEquiv (c'.Œπ.app j ‚â´ f)
          naturality := fun j j' œÜ ‚Ü¶ by
            dsimp
            rw [comp_id, ‚Üê c'.w œÜ, ‚Üê partialLeftAdjointHomEquiv_map_comp, assoc]
            dsimp })
      invFun := fun g ‚Ü¶ hc'.desc (Cocone.mk _
        { app := fun j ‚Ü¶ F.partialLeftAdjointHomEquiv.symm (c.Œπ.app j ‚â´ g)
          naturality := fun j j' œÜ ‚Ü¶ by
            apply F.partialLeftAdjointHomEquiv.injective
            have := c.w œÜ
            dsimp at this ‚ä¢
            rw [comp_id, Equiv.apply_symm_apply, partialLeftAdjointHomEquiv_map_comp,
              Equiv.apply_symm_apply, reassoc_of% this] })
      left_inv := fun f ‚Ü¶ hc'.hom_ext (fun j ‚Ü¶ by simp)
      right_inv := fun g ‚Ü¶ hc.hom_ext (fun j ‚Ü¶ by simp) }
  homEquiv_comp {Y Y'} g f := hc.hom_ext (fun j ‚Ü¶ by
    dsimp
    simp only [IsColimit.fac, IsColimit.fac_assoc, partialLeftAdjointHomEquiv_comp,
      F.map_comp, assoc] )

lemma leftAdjointObjIsDefined_of_isColimit {J : Type*} [Category J] {R : J ‚•§ C} {c : Cocone R}
    (hc : IsColimit c) [HasColimitsOfShape J D]
    (h : ‚àÄ (j : J), F.leftAdjointObjIsDefined (R.obj j)) :
    F.leftAdjointObjIsDefined c.pt :=
  (corepresentableByCompCoyonedaObjOfIsColimit
    (R := ObjectProperty.lift _ R h) hc (colimit.isColimit _)).isCorepresentable

lemma leftAdjointObjIsDefined_colimit {J : Type*} [Category J] (R : J ‚•§ C)
    [HasColimit R] [HasColimitsOfShape J D]
    (h : ‚àÄ (j : J), F.leftAdjointObjIsDefined (R.obj j)) :
    F.leftAdjointObjIsDefined (colimit R) :=
  leftAdjointObjIsDefined_of_isColimit (colimit.isColimit R) h

end partialLeftAdjoint

section partialRightAdjoint

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] (F : C ‚•§ D)

/-- Given a functor `F : C ‚•§ D`, this is a predicate on objects `X : D` corresponding
to the domain of definition of the (partial) right adjoint of `F`. -/
def rightAdjointObjIsDefined : ObjectProperty D :=
  fun Y ‚Ü¶ IsRepresentable (F.op ‚ãô yoneda.obj Y)

lemma rightAdjointObjIsDefined_iff (Y : D) :
    F.rightAdjointObjIsDefined Y ‚Üî IsRepresentable (F.op ‚ãô yoneda.obj Y) := by rfl

variable {F} in
lemma rightAdjointObjIsDefined_of_adjunction {G : D ‚•§ C} (adj : F ‚ä£ G) (Y : D) :
    F.rightAdjointObjIsDefined Y :=
  (adj.representableBy Y).isRepresentable

/-- The full subcategory where `F.partialRightAdjoint` shall be defined. -/
abbrev PartialRightAdjointSource := F.rightAdjointObjIsDefined.FullSubcategory

instance (Y : F.PartialRightAdjointSource) :
    IsRepresentable (F.op ‚ãô yoneda.obj Y.obj) := Y.property

/-- Given `F : C ‚•§ D`, this is `F.partialRightAdjoint` on objects: it sends
`X : D` such that `F.rightAdjointObjIsDefined X` holds to an object of `C`
which represents the functor `F ‚ãô coyoneda.obj (op X.obj)`. -/
noncomputable def partialRightAdjointObj (Y : F.PartialRightAdjointSource) : C :=
  (F.op ‚ãô yoneda.obj Y.obj).reprX

/-- Given `F : C ‚•§ D`, this is the canonical bijection
`(X ‚ü∂ F.partialRightAdjointObj Y) ‚âÉ (F.obj X ‚ü∂ Y.obj)`
for all `X : C` and `Y : F.PartialRightAdjointSource`. -/
noncomputable def partialRightAdjointHomEquiv {X : C} {Y : F.PartialRightAdjointSource} :
    (X ‚ü∂ F.partialRightAdjointObj Y) ‚âÉ (F.obj X ‚ü∂ Y.obj) :=
  (F.op ‚ãô yoneda.obj Y.obj).representableBy.homEquiv

lemma partialRightAdjointHomEquiv_comp {X X' : C} {Y : F.PartialRightAdjointSource}
    (f : X' ‚ü∂ F.partialRightAdjointObj Y) (g : X ‚ü∂ X') :
    F.partialRightAdjointHomEquiv (g ‚â´ f) =
      F.map g ‚â´ F.partialRightAdjointHomEquiv f :=
  RepresentableBy.homEquiv_comp ..

/-- Given `F : C ‚•§ D`, this is `F.partialRightAdjoint` on morphisms. -/
noncomputable def partialRightAdjointMap {X Y : F.PartialRightAdjointSource}
    (f : X ‚ü∂ Y) : F.partialRightAdjointObj X ‚ü∂ F.partialRightAdjointObj Y :=
    F.partialRightAdjointHomEquiv.symm (F.partialRightAdjointHomEquiv (ùüô _) ‚â´ f.hom)

@[simp]
lemma partialRightAdjointHomEquiv_map {X Y : F.PartialRightAdjointSource}
    (f : X ‚ü∂ Y) :
    F.partialRightAdjointHomEquiv (F.partialRightAdjointMap f) =
      F.partialRightAdjointHomEquiv (ùüô _) ‚â´ f.hom := by
  simp [partialRightAdjointMap]

lemma partialRightAdjointHomEquiv_map_comp {X : C} {Y Y' : F.PartialRightAdjointSource}
    (f : X ‚ü∂ F.partialRightAdjointObj Y) (g : Y ‚ü∂ Y') :
    F.partialRightAdjointHomEquiv (f ‚â´ F.partialRightAdjointMap g) =
      F.partialRightAdjointHomEquiv f ‚â´ g.hom := by
  rw [partialRightAdjointHomEquiv_comp, partialRightAdjointHomEquiv_map,
    ‚Üê assoc, ‚Üê partialRightAdjointHomEquiv_comp, comp_id]

@[reassoc]
lemma partialRightAdjointHomEquiv_comp_symm {X X' : C} {Y : F.PartialRightAdjointSource}
    (f : F.obj X' ‚ü∂ Y.obj) (g : X ‚ü∂ X') :
    g ‚â´ F.partialRightAdjointHomEquiv.symm f =
      F.partialRightAdjointHomEquiv.symm (F.map g ‚â´ f) :=
  RepresentableBy.comp_homEquiv_symm ..

@[reassoc]
lemma partialRightAdjointHomEquiv_symm_comp {X : C} {Y Y' : F.PartialRightAdjointSource}
    (f : F.obj X ‚ü∂ Y.obj) (g : Y ‚ü∂ Y') :
    F.partialRightAdjointHomEquiv.symm f ‚â´ F.partialRightAdjointMap g =
      F.partialRightAdjointHomEquiv.symm (f ‚â´ g.hom) := by
  simp [Equiv.eq_symm_apply, partialRightAdjointHomEquiv_map_comp]

/-- Given `F : C ‚•§ D`, this is the partial adjoint functor `F.PartialLeftAdjointSource ‚•§ C`. -/
@[simps]
noncomputable def partialRightAdjoint : F.PartialRightAdjointSource ‚•§ C where
  obj := F.partialRightAdjointObj
  map := F.partialRightAdjointMap
  map_id X := by
    apply F.partialRightAdjointHomEquiv.injective
    dsimp
    rw [partialRightAdjointHomEquiv_map]
    erw [comp_id]
  map_comp {X Y Z} f g := by
    apply F.partialRightAdjointHomEquiv.injective
    dsimp
    rw [partialRightAdjointHomEquiv_map, partialRightAdjointHomEquiv_comp,
      partialRightAdjointHomEquiv_map, ‚Üê assoc]
    erw [‚Üê assoc]
    rw [‚Üê F.partialRightAdjointHomEquiv_comp, comp_id,
      partialRightAdjointHomEquiv_map]

variable {F}

lemma isLeftAdjoint_of_rightAdjointObjIsDefined_eq_top
    (h : F.rightAdjointObjIsDefined = ‚ä§) : F.IsLeftAdjoint := by
  replace h : ‚àÄ X, IsRepresentable (F.op ‚ãô yoneda.obj X) := fun X ‚Ü¶ by
    simp only [‚Üê rightAdjointObjIsDefined_iff, h, Pi.top_apply, Prop.top_eq_true]
  exact (Adjunction.adjunctionOfEquivRight
    (fun X Y ‚Ü¶ (F.op ‚ãô yoneda.obj Y).representableBy.homEquiv.symm)
    (fun X Y Y' g f ‚Ü¶ (RepresentableBy.comp_homEquiv_symm ..).symm)).isLeftAdjoint

variable (F) in
lemma isLeftAdjoint_iff_rightAdjointObjIsDefined_eq_top :
    F.IsLeftAdjoint ‚Üî F.rightAdjointObjIsDefined = ‚ä§ := by
  refine ‚ü®fun h ‚Ü¶ ?_, isLeftAdjoint_of_rightAdjointObjIsDefined_eq_top‚ü©
  ext X
  simpa only [Pi.top_apply, Prop.top_eq_true, iff_true]
    using rightAdjointObjIsDefined_of_adjunction (Adjunction.ofIsLeftAdjoint F) X

/-- Auxiliary definition for `rightAdjointObjIsDefined_of_isLimit`. -/
noncomputable def representableByCompYonedaObjOfIsLimit {J : Type*} [Category J]
    {R : J ‚•§ F.PartialRightAdjointSource}
    {c : Cone (R ‚ãô ObjectProperty.Œπ _)} (hc : IsLimit c)
    {c' : Cone (R ‚ãô F.partialRightAdjoint)} (hc' : IsLimit c') :
    (F.op ‚ãô yoneda.obj c.pt).RepresentableBy c'.pt where
  homEquiv {Y} :=
    { toFun := fun f ‚Ü¶ hc.lift (Cone.mk _
        { app := fun j ‚Ü¶ F.partialRightAdjointHomEquiv (f ‚â´ c'.œÄ.app j)
          naturality := fun j j' œÜ ‚Ü¶ by
            dsimp
            rw [id_comp, ‚Üê c'.w œÜ, ‚Üê partialRightAdjointHomEquiv_map_comp,
              ‚Üê assoc]
            dsimp })
      invFun := fun g ‚Ü¶ hc'.lift (Cone.mk _
        { app := fun j ‚Ü¶ F.partialRightAdjointHomEquiv.symm (g ‚â´ c.œÄ.app j)
          naturality := fun j j' œÜ ‚Ü¶ by
            apply F.partialRightAdjointHomEquiv.injective
            have := c.w œÜ
            dsimp at this ‚ä¢
            rw [id_comp, Equiv.apply_symm_apply, partialRightAdjointHomEquiv_map_comp,
              Equiv.apply_symm_apply, assoc, this] })
      left_inv := fun f ‚Ü¶ hc'.hom_ext (fun j ‚Ü¶ by simp)
      right_inv := fun g ‚Ü¶ hc.hom_ext (fun j ‚Ü¶ by simp) }
  homEquiv_comp {Y Y'} g f := hc.hom_ext (fun j ‚Ü¶ by
    dsimp
    simp only [IsLimit.fac, partialRightAdjointHomEquiv_comp, assoc] )

lemma rightAdjointObjIsDefined_of_isLimit {J : Type*} [Category J] {R : J ‚•§ D} {c : Cone R}
    (hc : IsLimit c) [HasLimitsOfShape J C]
    (h : ‚àÄ (j : J), F.rightAdjointObjIsDefined (R.obj j)) :
    F.rightAdjointObjIsDefined c.pt :=
  (representableByCompYonedaObjOfIsLimit
    (R := ObjectProperty.lift _ R h) hc (limit.isLimit _)).isRepresentable

lemma rightAdjointObjIsDefined_limit {J : Type*} [Category J] (R : J ‚•§ D)
    [HasLimit R] [HasLimitsOfShape J C]
    (h : ‚àÄ (j : J), F.rightAdjointObjIsDefined (R.obj j)) :
    F.rightAdjointObjIsDefined (limit R) :=
  rightAdjointObjIsDefined_of_isLimit (limit.isLimit R) h

end partialRightAdjoint

end Functor

end CategoryTheory
