/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Adjunction.FullyFaithful
import Mathlib.CategoryTheory.Conj
import Mathlib.CategoryTheory.Functor.ReflectsIso

#align_import category_theory.adjunction.reflective from "leanprover-community/mathlib"@"239d882c4fb58361ee8b3b39fb2091320edef10a"

/-!
# Reflective functors

Basic properties of reflective functors, especially those relating to their essential image.

Note properties of reflective functors relating to limits and colimits are included in
`CategoryTheory.Monad.Limits`.
-/


universe vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

noncomputable section

namespace CategoryTheory

open Category Adjunction

variable {C : Type uâ‚} {D : Type uâ‚‚} {E : Type uâ‚ƒ}
variable [Category.{vâ‚} C] [Category.{vâ‚‚} D] [Category.{vâ‚ƒ} E]

/--
A functor is *reflective*, or *a reflective inclusion*, if it is fully faithful and right adjoint.
-/
class Reflective (R : D â¥¤ C) extends R.Full, R.Faithful where
  /-- a choice of a left adjoint to `R` -/
  L : C â¥¤ D
  /-- `R` is a right adjoint -/
  adj : L âŠ£ R
#align category_theory.reflective CategoryTheory.Reflective

variable (i : D â¥¤ C)

/-- The reflector `C â¥¤ D` when `R : D â¥¤ C` is reflective. -/
def reflector [Reflective i] : C â¥¤ D := Reflective.L (R := i)

/-- The adjunction `reflector i âŠ£ i` when `i` is reflective. -/
def reflectorAdjunction [Reflective i] : reflector i âŠ£ i := Reflective.adj

instance [Reflective i] : i.IsRightAdjoint := âŸ¨_, âŸ¨reflectorAdjunction iâŸ©âŸ©

instance [Reflective i] : (reflector i).IsLeftAdjoint := âŸ¨_, âŸ¨reflectorAdjunction iâŸ©âŸ©

/-- A reflective functor is fully faithful. -/
def Functor.fullyFaithfulOfReflective [Reflective i] : i.FullyFaithful :=
  (reflectorAdjunction i).fullyFaithfulROfIsIsoCounit

-- TODO: This holds more generally for idempotent adjunctions, not just reflective adjunctions.
/-- For a reflective functor `i` (with left adjoint `L`), with unit `Î·`, we have `Î·_iL = iL Î·`.
-/
theorem unit_obj_eq_map_unit [Reflective i] (X : C) :
    (reflectorAdjunction i).unit.app (i.obj ((reflector i).obj X)) =
      i.map ((reflector i).map ((reflectorAdjunction i).unit.app X)) := by
  rw [â† cancel_mono (i.map ((reflectorAdjunction i).counit.app ((reflector i).obj X))),
    â† i.map_comp]
  simp
#align category_theory.unit_obj_eq_map_unit CategoryTheory.unit_obj_eq_map_unit

/--
When restricted to objects in `D` given by `i : D â¥¤ C`, the unit is an isomorphism. In other words,
`Î·_iX` is an isomorphism for any `X` in `D`.
More generally this applies to objects essentially in the reflective subcategory, see
`Functor.essImage.unit_isIso`.
-/
example [Reflective i] {B : D} : IsIso ((reflectorAdjunction i).unit.app (i.obj B)) :=
  inferInstance

variable {i}

/-- If `A` is essentially in the image of a reflective functor `i`, then `Î·_A` is an isomorphism.
This gives that the "witness" for `A` being in the essential image can instead be given as the
reflection of `A`, with the isomorphism as `Î·_A`.

(For any `B` in the reflective subcategory, we automatically have that `Îµ_B` is an iso.)
-/
theorem Functor.essImage.unit_isIso [Reflective i] {A : C} (h : A âˆˆ i.essImage) :
    IsIso ((reflectorAdjunction i).unit.app A) := by
  rwa [isIso_unit_app_iff_mem_essImage]
#align category_theory.functor.ess_image.unit_is_iso CategoryTheory.Functor.essImage.unit_isIso

/-- If `Î·_A` is an isomorphism, then `A` is in the essential image of `i`. -/
theorem mem_essImage_of_unit_isIso {L : C â¥¤ D} (adj : L âŠ£ i) (A : C)
    [IsIso (adj.unit.app A)] : A âˆˆ i.essImage :=
  âŸ¨L.obj A, âŸ¨(asIso (adj.unit.app A)).symmâŸ©âŸ©
#align category_theory.mem_ess_image_of_unit_is_iso CategoryTheory.mem_essImage_of_unit_isIso

/-- If `Î·_A` is a split monomorphism, then `A` is in the reflective subcategory. -/
theorem mem_essImage_of_unit_isSplitMono [Reflective i] {A : C}
    [IsSplitMono ((reflectorAdjunction i).unit.app A)] : A âˆˆ i.essImage := by
  let Î· : ðŸ­ C âŸ¶ reflector i â‹™ i := (reflectorAdjunction i).unit
  haveI : IsIso (Î·.app (i.obj ((reflector i).obj A))) :=
    Functor.essImage.unit_isIso ((i.obj_mem_essImage _))
  have : Epi (Î·.app A) := by
    refine @epi_of_epi _ _ _ _ _ (retraction (Î·.app A)) (Î·.app A) ?_
    rw [show retraction _ â‰« Î·.app A = _ from Î·.naturality (retraction (Î·.app A))]
    apply epi_comp (Î·.app (i.obj ((reflector i).obj A)))
  haveI := isIso_of_epi_of_isSplitMono (Î·.app A)
  exact mem_essImage_of_unit_isIso (reflectorAdjunction i) A
#align category_theory.mem_ess_image_of_unit_is_split_mono CategoryTheory.mem_essImage_of_unit_isSplitMono

/-- Composition of reflective functors. -/
instance Reflective.comp (F : C â¥¤ D) (G : D â¥¤ E) [Reflective F] [Reflective G] :
    Reflective (F â‹™ G) where
  L := reflector G â‹™ reflector F
  adj := (reflectorAdjunction G).comp (reflectorAdjunction F)
#align category_theory.reflective.comp CategoryTheory.Reflective.comp

/-- (Implementation) Auxiliary definition for `unitCompPartialBijective`. -/
def unitCompPartialBijectiveAux [Reflective i] (A : C) (B : D) :
    (A âŸ¶ i.obj B) â‰ƒ (i.obj ((reflector i).obj A) âŸ¶ i.obj B) :=
  ((reflectorAdjunction i).homEquiv _ _).symm.trans
    (Functor.FullyFaithful.ofFullyFaithful i).homEquiv
#align category_theory.unit_comp_partial_bijective_aux CategoryTheory.unitCompPartialBijectiveAux

/-- The description of the inverse of the bijection `unitCompPartialBijectiveAux`. -/
theorem unitCompPartialBijectiveAux_symm_apply [Reflective i] {A : C} {B : D}
    (f : i.obj ((reflector i).obj A) âŸ¶ i.obj B) :
    (unitCompPartialBijectiveAux _ _).symm f = (reflectorAdjunction i).unit.app A â‰« f := by
  simp [unitCompPartialBijectiveAux]
#align category_theory.unit_comp_partial_bijective_aux_symm_apply CategoryTheory.unitCompPartialBijectiveAux_symm_apply

/-- If `i` has a reflector `L`, then the function `(i.obj (L.obj A) âŸ¶ B) â†’ (A âŸ¶ B)` given by
precomposing with `Î·.app A` is a bijection provided `B` is in the essential image of `i`.
That is, the function `fun (f : i.obj (L.obj A) âŸ¶ B) â†¦ Î·.app A â‰« f` is bijective,
as long as `B` is in the essential image of `i`.
This definition gives an equivalence: the key property that the inverse can be described
nicely is shown in `unitCompPartialBijective_symm_apply`.

This establishes there is a natural bijection `(A âŸ¶ B) â‰ƒ (i.obj (L.obj A) âŸ¶ B)`. In other words,
from the point of view of objects in `D`, `A` and `i.obj (L.obj A)` look the same: specifically
that `Î·.app A` is an isomorphism.
-/
def unitCompPartialBijective [Reflective i] (A : C) {B : C} (hB : B âˆˆ i.essImage) :
    (A âŸ¶ B) â‰ƒ (i.obj ((reflector i).obj A) âŸ¶ B) :=
  calc
    (A âŸ¶ B) â‰ƒ (A âŸ¶ i.obj (Functor.essImage.witness hB)) := Iso.homCongr (Iso.refl _) hB.getIso.symm
    _ â‰ƒ (i.obj _ âŸ¶ i.obj (Functor.essImage.witness hB)) := unitCompPartialBijectiveAux _ _
    _ â‰ƒ (i.obj ((reflector i).obj A) âŸ¶ B) :=
      Iso.homCongr (Iso.refl _) (Functor.essImage.getIso hB)
#align category_theory.unit_comp_partial_bijective CategoryTheory.unitCompPartialBijective

@[simp]
theorem unitCompPartialBijective_symm_apply [Reflective i] (A : C) {B : C} (hB : B âˆˆ i.essImage)
    (f) : (unitCompPartialBijective A hB).symm f = (reflectorAdjunction i).unit.app A â‰« f := by
  simp [unitCompPartialBijective, unitCompPartialBijectiveAux_symm_apply]
#align category_theory.unit_comp_partial_bijective_symm_apply CategoryTheory.unitCompPartialBijective_symm_apply

theorem unitCompPartialBijective_symm_natural [Reflective i] (A : C) {B B' : C} (h : B âŸ¶ B')
    (hB : B âˆˆ i.essImage) (hB' : B' âˆˆ i.essImage) (f : i.obj ((reflector i).obj A) âŸ¶ B) :
    (unitCompPartialBijective A hB').symm (f â‰« h) = (unitCompPartialBijective A hB).symm f â‰« h := by
  simp
#align category_theory.unit_comp_partial_bijective_symm_natural CategoryTheory.unitCompPartialBijective_symm_natural

theorem unitCompPartialBijective_natural [Reflective i] (A : C) {B B' : C} (h : B âŸ¶ B')
    (hB : B âˆˆ i.essImage) (hB' : B' âˆˆ i.essImage) (f : A âŸ¶ B) :
    (unitCompPartialBijective A hB') (f â‰« h) = unitCompPartialBijective A hB f â‰« h := by
  rw [â† Equiv.eq_symm_apply, unitCompPartialBijective_symm_natural A h, Equiv.symm_apply_apply]
#align category_theory.unit_comp_partial_bijective_natural CategoryTheory.unitCompPartialBijective_natural

instance [Reflective i] (X : Functor.EssImageSubcategory i) :
  IsIso (NatTrans.app (reflectorAdjunction i).unit X.obj) :=
Functor.essImage.unit_isIso X.property

-- Porting note: the following auxiliary definition and the next two lemmas were
-- introduced in order to ease the port
/-- The counit isomorphism of the equivalence `D â‰Œ i.EssImageSubcategory` given
by `equivEssImageOfReflective` when the functor `i` is reflective. -/
def equivEssImageOfReflective_counitIso_app [Reflective i] (X : Functor.EssImageSubcategory i) :
    ((Functor.essImageInclusion i â‹™ reflector i) â‹™ Functor.toEssImage i).obj X â‰… X := by
  refine Iso.symm (@asIso _ _ X _ ((reflectorAdjunction i).unit.app X.obj) ?_)
  refine @isIso_of_reflects_iso _ _ _ _ _ _ _ i.essImageInclusion ?_ _
  dsimp
  exact inferInstance

lemma equivEssImageOfReflective_map_counitIso_app_hom [Reflective i]
    (X : Functor.EssImageSubcategory i) :
  (Functor.essImageInclusion i).map (equivEssImageOfReflective_counitIso_app X).hom =
    inv (NatTrans.app (reflectorAdjunction i).unit X.obj) := by
    simp only [Functor.comp_obj, Functor.essImageInclusion_obj, Functor.toEssImage_obj_obj,
      equivEssImageOfReflective_counitIso_app, asIso, Iso.symm_mk, Functor.essImageInclusion_map,
      Functor.id_obj]
    rfl

lemma equivEssImageOfReflective_map_counitIso_app_inv [Reflective i]
    (X : Functor.EssImageSubcategory i) :
  (Functor.essImageInclusion i).map (equivEssImageOfReflective_counitIso_app X).inv =
    (NatTrans.app (reflectorAdjunction i).unit X.obj) := rfl

/-- If `i : D â¥¤ C` is reflective, the inverse functor of `i â‰Œ F.essImage` can be explicitly
defined by the reflector. -/
@[simps]
def equivEssImageOfReflective [Reflective i] : D â‰Œ i.EssImageSubcategory where
  functor := i.toEssImage
  inverse := i.essImageInclusion â‹™ reflector i
  unitIso :=
    NatIso.ofComponents (fun X => (asIso <| (reflectorAdjunction i).counit.app X).symm)
      (by
        intro X Y f
        dsimp
        rw [IsIso.comp_inv_eq, Category.assoc, IsIso.eq_inv_comp]
        exact ((reflectorAdjunction i).counit.naturality f).symm)
  counitIso :=
    NatIso.ofComponents equivEssImageOfReflective_counitIso_app
      (by
        intro X Y f
        apply (Functor.essImageInclusion i).map_injective
        have h := ((reflectorAdjunction i).unit.naturality f).symm
        rw [Functor.id_map] at h
        erw [Functor.map_comp, Functor.map_comp,
          equivEssImageOfReflective_map_counitIso_app_hom,
          equivEssImageOfReflective_map_counitIso_app_hom,
          IsIso.comp_inv_eq, assoc, â† h, IsIso.inv_hom_id_assoc, Functor.comp_map])
  functor_unitIso_comp := fun X => by
    -- Porting note: this proof was automatically handled by the automation in mathlib
    apply (Functor.essImageInclusion i).map_injective
    erw [Functor.map_comp, equivEssImageOfReflective_map_counitIso_app_hom]
    aesop_cat
#align category_theory.equiv_ess_image_of_reflective CategoryTheory.equivEssImageOfReflective

end CategoryTheory
