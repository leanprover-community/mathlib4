/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Limits.Shapes.Equalizers
import Mathlib.CategoryTheory.Limits.Shapes.Reflexive
import Mathlib.CategoryTheory.Monad.Adjunction
import Mathlib.CategoryTheory.Monad.Coequalizer

#align_import category_theory.adjunction.lifting from "leanprover-community/mathlib"@"9bc7dfa6e50f902fb0684c9670a680459ebaed68"

/-!
# Adjoint lifting

This file gives two constructions for building left adjoints: the adjoint triangle theorem and the
adjoint lifting theorem.
The adjoint triangle theorem concerns a functor `U : B ‚•§ C` with a left adjoint `F` such
that `Œµ_X : FUX ‚ü∂ X` is a regular epi. Then for any category `A` with coequalizers of reflexive
pairs, a functor `R : A ‚•§ B` has a left adjoint if (and only if) the composite `R ‚ãô U` does.
Note that the condition on `U` regarding `Œµ_X` is automatically satisfied in the case when `U` is
a monadic functor, giving the corollary: `monadicAdjointTriangleLift`, i.e. if `U` is monadic,
`A` has reflexive coequalizers then `R : A ‚•§ B` has a left adjoint provided `R ‚ãô U` does.

The adjoint lifting theorem says that given a commutative square of functors (up to isomorphism):

      Q
    A ‚Üí B
  U ‚Üì   ‚Üì V
    C ‚Üí D
      R

where `U` and `V` are monadic and `A` has reflexive coequalizers, then if `R` has a left adjoint
then `Q` has a left adjoint.

## Implementation

It is more convenient to prove this theorem by assuming we are given the explicit adjunction rather
than just a functor known to be a right adjoint. In docstrings, we write `(Œ∑, Œµ)` for the unit
and counit of the adjunction `adj‚ÇÅ : F ‚ä£ U` and `(Œπ, Œ¥)` for the unit and counit of the adjunction
`adj‚ÇÇ : F' ‚ä£ R ‚ãô U`.

## TODO

Dualise to lift right adjoints through comonads (by reversing 1-cells) and dualise to lift right
adjoints through monads (by reversing 2-cells), and the combination.

## References
* https://ncatlab.org/nlab/show/adjoint+triangle+theorem
* https://ncatlab.org/nlab/show/adjoint+lifting+theorem
* Adjoint Lifting Theorems for Categories of Algebras (PT Johnstone, 1975)
* A unified approach to the lifting of adjoints (AJ Power, 1988)
-/


namespace CategoryTheory

open Category Limits

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ v‚ÇÑ u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ

variable {A : Type u‚ÇÅ} {B : Type u‚ÇÇ} {C : Type u‚ÇÉ}

variable [Category.{v‚ÇÅ} A] [Category.{v‚ÇÇ} B] [Category.{v‚ÇÉ} C]

-- Hide implementation details in this namespace
namespace LiftAdjoint

variable {U : B ‚•§ C} {F : C ‚•§ B} (R : A ‚•§ B) (F' : C ‚•§ A)

variable (adj‚ÇÅ : F ‚ä£ U) (adj‚ÇÇ : F' ‚ä£ R ‚ãô U)

/-- To show that `Œµ_X` is a coequalizer for `(FUŒµ_X, Œµ_FUX)`, it suffices to assume it's always a
coequalizer of something (i.e. a regular epi).
-/
def counitCoequalises [‚àÄ X : B, RegularEpi (adj‚ÇÅ.counit.app X)] (X : B) :
    IsColimit (Cofork.ofœÄ (adj‚ÇÅ.counit.app X) (adj‚ÇÅ.counit_naturality _)) :=
  Cofork.IsColimit.mk' _ fun s => by
    refine' ‚ü®(RegularEpi.desc' (adj‚ÇÅ.counit.app X) s.œÄ _).1, _, _‚ü©
    ¬∑ rw [‚Üê cancel_epi (adj‚ÇÅ.counit.app (RegularEpi.W (adj‚ÇÅ.counit.app X)))]
      -- ‚ä¢ NatTrans.app adj‚ÇÅ.counit (RegularEpi.W (NatTrans.app adj‚ÇÅ.counit X)) ‚â´ Regul ‚Ä¶
      rw [‚Üê adj‚ÇÅ.counit_naturality_assoc RegularEpi.left]
      -- ‚ä¢ F.map (U.map RegularEpi.left) ‚â´ NatTrans.app adj‚ÇÅ.counit ((U ‚ãô F).obj X) ‚â´ C ‚Ä¶
      dsimp only [Functor.comp_obj]
      -- ‚ä¢ F.map (U.map RegularEpi.left) ‚â´ NatTrans.app adj‚ÇÅ.counit (F.obj (U.obj X)) ‚â´ ‚Ä¶
      rw [‚Üê s.condition, ‚Üê F.map_comp_assoc, ‚Üê U.map_comp, RegularEpi.w, U.map_comp,
        F.map_comp_assoc, s.condition, ‚Üê adj‚ÇÅ.counit_naturality_assoc RegularEpi.right]
    ¬∑ apply (RegularEpi.desc' (adj‚ÇÅ.counit.app X) s.œÄ _).2
      -- üéâ no goals
    ¬∑ intro m hm
      -- ‚ä¢ m = ‚Üë(RegularEpi.desc' (NatTrans.app adj‚ÇÅ.counit X) (Cofork.œÄ s) (_ : Regula ‚Ä¶
      rw [‚Üê cancel_epi (adj‚ÇÅ.counit.app X)]
      -- ‚ä¢ NatTrans.app adj‚ÇÅ.counit X ‚â´ m = NatTrans.app adj‚ÇÅ.counit X ‚â´ ‚Üë(RegularEpi.d ‚Ä¶
      apply hm.trans (RegularEpi.desc' (adj‚ÇÅ.counit.app X) s.œÄ _).2.symm
      -- üéâ no goals
#align category_theory.lift_adjoint.counit_coequalises CategoryTheory.LiftAdjoint.counitCoequalises

/-- (Implementation)
To construct the left adjoint, we use the coequalizer of `F' U Œµ_Y` with the composite

`F' U F U X ‚ü∂ F' U F U R F U' X ‚ü∂ F' U R F' U X ‚ü∂ F' U X`

where the first morphism is `F' U F Œπ_UX`, the second is `F' U Œµ_RF'UX`, and the third is `Œ¥_F'UX`.
We will show that this coequalizer exists and that it forms the object map for a left adjoint to
`R`.
-/
def otherMap (X) : F'.obj (U.obj (F.obj (U.obj X))) ‚ü∂ F'.obj (U.obj X) :=
  F'.map (U.map (F.map (adj‚ÇÇ.unit.app _) ‚â´ adj‚ÇÅ.counit.app _)) ‚â´ adj‚ÇÇ.counit.app _
#align category_theory.lift_adjoint.other_map CategoryTheory.LiftAdjoint.otherMap

/-- `(F'UŒµ_X, otherMap X)` is a reflexive pair: in particular if `A` has reflexive coequalizers then
it has a coequalizer.
-/
instance (X : B) :
    IsReflexivePair (F'.map (U.map (adj‚ÇÅ.counit.app X))) (otherMap _ _ adj‚ÇÅ adj‚ÇÇ X) :=
  IsReflexivePair.mk' (F'.map (adj‚ÇÅ.unit.app (U.obj X)))
    (by
      rw [‚Üê F'.map_comp, adj‚ÇÅ.right_triangle_components]
      -- ‚ä¢ F'.map (ùüô (U.obj X)) = ùüô (F'.obj (U.obj ((ùü≠ B).obj X)))
      apply F'.map_id)
      -- üéâ no goals
    (by
      dsimp [otherMap]
      -- ‚ä¢ F'.map (NatTrans.app adj‚ÇÅ.unit (U.obj X)) ‚â´ F'.map (U.map (F.map (NatTrans.a ‚Ä¶
      rw [‚Üê F'.map_comp_assoc, U.map_comp, adj‚ÇÅ.unit_naturality_assoc,
        adj‚ÇÅ.right_triangle_components, comp_id, adj‚ÇÇ.left_triangle_components])

variable [HasReflexiveCoequalizers A]

/-- Construct the object part of the desired left adjoint as the coequalizer of `F'UŒµ_Y` with
`otherMap`.
-/
noncomputable def constructLeftAdjointObj (Y : B) : A :=
  coequalizer (F'.map (U.map (adj‚ÇÅ.counit.app Y))) (otherMap _ _ adj‚ÇÅ adj‚ÇÇ Y)
#align category_theory.lift_adjoint.construct_left_adjoint_obj CategoryTheory.LiftAdjoint.constructLeftAdjointObj

/-- The homset equivalence which helps show that `R` is a right adjoint. -/
@[simps!] -- Porting note: Originally `@[simps (config := { rhsMd := semireducible })]`
noncomputable def constructLeftAdjointEquiv [‚àÄ X : B, RegularEpi (adj‚ÇÅ.counit.app X)] (Y : A)
    (X : B) : (constructLeftAdjointObj _ _ adj‚ÇÅ adj‚ÇÇ X ‚ü∂ Y) ‚âÉ (X ‚ü∂ R.obj Y) :=
  calc
    (constructLeftAdjointObj _ _ adj‚ÇÅ adj‚ÇÇ X ‚ü∂ Y) ‚âÉ
        { f : F'.obj (U.obj X) ‚ü∂ Y //
          F'.map (U.map (adj‚ÇÅ.counit.app X)) ‚â´ f = otherMap _ _ adj‚ÇÅ adj‚ÇÇ _ ‚â´ f } :=
      Cofork.IsColimit.homIso (colimit.isColimit _) _
    _ ‚âÉ { g : U.obj X ‚ü∂ U.obj (R.obj Y) //
          U.map (F.map g ‚â´ adj‚ÇÅ.counit.app _) = U.map (adj‚ÇÅ.counit.app _) ‚â´ g } := by
      apply (adj‚ÇÇ.homEquiv _ _).subtypeEquiv _
      -- ‚ä¢ ‚àÄ (a : F'.obj (U.obj X) ‚ü∂ Y), F'.map (U.map (NatTrans.app adj‚ÇÅ.counit X)) ‚â´  ‚Ä¶
      intro f
      -- ‚ä¢ F'.map (U.map (NatTrans.app adj‚ÇÅ.counit X)) ‚â´ f = otherMap R F' adj‚ÇÅ adj‚ÇÇ X  ‚Ä¶
      rw [‚Üê (adj‚ÇÇ.homEquiv _ _).injective.eq_iff, eq_comm, adj‚ÇÇ.homEquiv_naturality_left,
        otherMap, assoc, adj‚ÇÇ.homEquiv_naturality_left, ‚Üê adj‚ÇÇ.counit_naturality,
        adj‚ÇÇ.homEquiv_naturality_left, adj‚ÇÇ.homEquiv_unit, adj‚ÇÇ.right_triangle_components,
        comp_id, Functor.comp_map, ‚Üê U.map_comp, assoc, ‚Üê adj‚ÇÅ.counit_naturality,
        adj‚ÇÇ.homEquiv_unit, adj‚ÇÇ.homEquiv_unit, F.map_comp, assoc]
      rfl
      -- üéâ no goals
    _ ‚âÉ { z : F.obj (U.obj X) ‚ü∂ R.obj Y // _ } := by
      apply (adj‚ÇÅ.homEquiv _ _).symm.subtypeEquiv
      -- ‚ä¢ ‚àÄ (a : U.obj X ‚ü∂ U.obj (R.obj Y)), U.map (F.map a ‚â´ NatTrans.app adj‚ÇÅ.counit ‚Ä¶
      intro g
      -- ‚ä¢ U.map (F.map g ‚â´ NatTrans.app adj‚ÇÅ.counit (R.obj Y)) = U.map (NatTrans.app a ‚Ä¶
      rw [‚Üê (adj‚ÇÅ.homEquiv _ _).symm.injective.eq_iff, adj‚ÇÅ.homEquiv_counit,
        adj‚ÇÅ.homEquiv_counit, adj‚ÇÅ.homEquiv_counit, F.map_comp, assoc, U.map_comp, F.map_comp,
        assoc, adj‚ÇÅ.counit_naturality, adj‚ÇÅ.counit_naturality_assoc]
      apply eq_comm
      -- üéâ no goals
    _ ‚âÉ (X ‚ü∂ R.obj Y) := (Cofork.IsColimit.homIso (counitCoequalises adj‚ÇÅ X) _).symm
#align category_theory.lift_adjoint.construct_left_adjoint_equiv CategoryTheory.LiftAdjoint.constructLeftAdjointEquiv

/-- Construct the left adjoint to `R`, with object map `constructLeftAdjointObj`. -/
noncomputable def constructLeftAdjoint [‚àÄ X : B, RegularEpi (adj‚ÇÅ.counit.app X)] : B ‚•§ A := by
  refine' Adjunction.leftAdjointOfEquiv (fun X Y => constructLeftAdjointEquiv R _ adj‚ÇÅ adj‚ÇÇ Y X) _
  -- ‚ä¢ ‚àÄ (X : B) (Y Y' : A) (g : Y ‚ü∂ Y') (h : constructLeftAdjointObj R F' adj‚ÇÅ adj ‚Ä¶
  intro X Y Y' g h
  -- ‚ä¢ ‚Üë((fun X Y => constructLeftAdjointEquiv R F' adj‚ÇÅ adj‚ÇÇ Y X) X Y') (h ‚â´ g) =  ‚Ä¶
  rw [constructLeftAdjointEquiv_apply, constructLeftAdjointEquiv_apply,
    Equiv.symm_apply_eq, Subtype.ext_iff]
  dsimp
  -- ‚ä¢ F.map (‚Üë(Adjunction.homEquiv adj‚ÇÇ (U.obj X) Y') ‚Üë(‚Üë(Cofork.IsColimit.homIso  ‚Ä¶
  rw [Cofork.IsColimit.homIso_natural, Cofork.IsColimit.homIso_natural]
  -- ‚ä¢ F.map (‚Üë(Adjunction.homEquiv adj‚ÇÇ (U.obj X) Y') (‚Üë(‚Üë(Cofork.IsColimit.homIso ‚Ä¶
  erw [adj‚ÇÇ.homEquiv_naturality_right]
  -- ‚ä¢ F.map (‚Üë(Adjunction.homEquiv adj‚ÇÇ (U.obj X) Y) ‚Üë(‚Üë(Cofork.IsColimit.homIso ( ‚Ä¶
  simp_rw [Functor.comp_map]
  -- ‚ä¢ F.map (‚Üë(Adjunction.homEquiv adj‚ÇÇ (U.obj X) Y) ‚Üë(‚Üë(Cofork.IsColimit.homIso ( ‚Ä¶
  simp
  -- üéâ no goals
#align category_theory.lift_adjoint.construct_left_adjoint CategoryTheory.LiftAdjoint.constructLeftAdjoint

end LiftAdjoint

/-- The adjoint triangle theorem: Suppose `U : B ‚•§ C` has a left adjoint `F` such that each counit
`Œµ_X : FUX ‚ü∂ X` is a regular epimorphism. Then if a category `A` has coequalizers of reflexive
pairs, then a functor `R : A ‚•§ B` has a left adjoint if the composite `R ‚ãô U` does.

Note the converse is true (with weaker assumptions), by `Adjunction.comp`.
See https://ncatlab.org/nlab/show/adjoint+triangle+theorem
-/
noncomputable def adjointTriangleLift {U : B ‚•§ C} {F : C ‚•§ B} (R : A ‚•§ B) (adj‚ÇÅ : F ‚ä£ U)
    [‚àÄ X : B, RegularEpi (adj‚ÇÅ.counit.app X)] [HasReflexiveCoequalizers A]
    [IsRightAdjoint (R ‚ãô U)] : IsRightAdjoint R where
  left := LiftAdjoint.constructLeftAdjoint R _ adj‚ÇÅ (Adjunction.ofRightAdjoint _)
  adj := Adjunction.adjunctionOfEquivLeft _ _
#align category_theory.adjoint_triangle_lift CategoryTheory.adjointTriangleLift

/-- If `R ‚ãô U` has a left adjoint, the domain of `R` has reflexive coequalizers and `U` is a monadic
functor, then `R` has a left adjoint.
This is a special case of `adjointTriangleLift` which is often more useful in practice.
-/
noncomputable def monadicAdjointTriangleLift (U : B ‚•§ C) [MonadicRightAdjoint U] {R : A ‚•§ B}
    [HasReflexiveCoequalizers A] [IsRightAdjoint (R ‚ãô U)] : IsRightAdjoint R := by
  let R' : A ‚•§ _ := R ‚ãô Monad.comparison (Adjunction.ofRightAdjoint U)
  -- ‚ä¢ IsRightAdjoint R
  rsuffices : IsRightAdjoint R'
  -- ‚ä¢ IsRightAdjoint R
  ¬∑ let this : IsRightAdjoint (R' ‚ãô (Monad.comparison (Adjunction.ofRightAdjoint U)).inv) := by
      infer_instance
    ¬∑ let this : R' ‚ãô (Monad.comparison (Adjunction.ofRightAdjoint U)).inv ‚âÖ R :=
        (isoWhiskerLeft R (Monad.comparison _).asEquivalence.unitIso.symm : _) ‚â™‚â´ R.rightUnitor
      exact Adjunction.rightAdjointOfNatIso this
      -- üéâ no goals
  let this : IsRightAdjoint (R' ‚ãô Monad.forget (Adjunction.ofRightAdjoint U).toMonad) :=
    Adjunction.rightAdjointOfNatIso
      (isoWhiskerLeft R (Monad.comparisonForget (Adjunction.ofRightAdjoint U)).symm : _)
  let this : ‚àÄ X, RegularEpi ((Monad.adj (Adjunction.ofRightAdjoint U).toMonad).counit.app X) := by
    intro X
    simp only [Monad.adj_counit]
    exact ‚ü®_, _, _, _, Monad.beckAlgebraCoequalizer X‚ü©
  exact adjointTriangleLift R' (Monad.adj _)
  -- üéâ no goals
#align category_theory.monadic_adjoint_triangle_lift CategoryTheory.monadicAdjointTriangleLift

variable {D : Type u‚ÇÑ}

variable [Category.{v‚ÇÑ} D]

/-- Suppose we have a commutative square of functors

      Q
    A ‚Üí B
  U ‚Üì   ‚Üì V
    C ‚Üí D
      R

where `U` has a left adjoint, `A` has reflexive coequalizers and `V` has a left adjoint such that
each component of the counit is a regular epi.
Then `Q` has a left adjoint if `R` has a left adjoint.

See https://ncatlab.org/nlab/show/adjoint+lifting+theorem
-/
noncomputable def adjointSquareLift (Q : A ‚•§ B) (V : B ‚•§ D) (U : A ‚•§ C) (R : C ‚•§ D)
    (comm : U ‚ãô R ‚âÖ Q ‚ãô V) [IsRightAdjoint U] [IsRightAdjoint V] [IsRightAdjoint R]
    [‚àÄ X, RegularEpi ((Adjunction.ofRightAdjoint V).counit.app X)] [HasReflexiveCoequalizers A] :
    IsRightAdjoint Q :=
  letI := Adjunction.rightAdjointOfNatIso comm
  adjointTriangleLift Q (Adjunction.ofRightAdjoint V)
#align category_theory.adjoint_square_lift CategoryTheory.adjointSquareLift

/-- Suppose we have a commutative square of functors

      Q
    A ‚Üí B
  U ‚Üì   ‚Üì V
    C ‚Üí D
      R

where `U` has a left adjoint, `A` has reflexive coequalizers and `V` is monadic.
Then `Q` has a left adjoint if `R` has a left adjoint.

See https://ncatlab.org/nlab/show/adjoint+lifting+theorem
-/
noncomputable def monadicAdjointSquareLift (Q : A ‚•§ B) (V : B ‚•§ D) (U : A ‚•§ C) (R : C ‚•§ D)
    (comm : U ‚ãô R ‚âÖ Q ‚ãô V) [IsRightAdjoint U] [MonadicRightAdjoint V] [IsRightAdjoint R]
    [HasReflexiveCoequalizers A] : IsRightAdjoint Q :=
  letI := Adjunction.rightAdjointOfNatIso comm
  monadicAdjointTriangleLift V
#align category_theory.monadic_adjoint_square_lift CategoryTheory.monadicAdjointSquareLift

end CategoryTheory
