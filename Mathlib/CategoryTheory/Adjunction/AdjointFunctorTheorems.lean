/-
Copyright (c) 2021 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Comma.StructuredArrow.Small
import Mathlib.CategoryTheory.Generator
import Mathlib.CategoryTheory.Limits.ConeCategory
import Mathlib.CategoryTheory.Limits.Constructions.WeaklyInitial
import Mathlib.CategoryTheory.Limits.FunctorCategory.Basic
import Mathlib.CategoryTheory.Subobject.Comma

/-!
# Adjoint functor theorem

This file proves the (general) adjoint functor theorem, in the form:
* If `G : D ‚•§ C` preserves limits and `D` has limits, and satisfies the solution set condition,
  then it has a left adjoint: `isRightAdjointOfPreservesLimitsOfIsCoseparating`.

We show that the converse holds, i.e. that if `G` has a left adjoint then it satisfies the solution
set condition, see `solutionSetCondition_of_isRightAdjoint`
(the file `CategoryTheory/Adjunction/Limits` already shows it preserves limits).

We define the *solution set condition* for the functor `G : D ‚•§ C` to mean, for every object
`A : C`, there is a set-indexed family ${f_i : A ‚ü∂ G (B_i)}$ such that any morphism `A ‚ü∂ G X`
factors through one of the `f_i`.

This file also proves the special adjoint functor theorem, in the form:
* If `G : D ‚•§ C` preserves limits and `D` is complete, well-powered and has a small coseparating
  set, then `G` has a left adjoint: `isRightAdjointOfPreservesLimitsOfIsCoseparating`

Finally, we prove the following corollary of the special adjoint functor theorem:
* If `C` is complete, well-powered and has a small coseparating set, then it is cocomplete:
  `hasColimits_of_hasLimits_of_isCoseparating`

-/


universe v u u'

namespace CategoryTheory

open Limits

variable {J : Type v}
variable {C : Type u} [Category.{v} C]

/-- The functor `G : D ‚•§ C` satisfies the *solution set condition* if for every `A : C`, there is a
family of morphisms `{f_i : A ‚ü∂ G (B_i) // i ‚àà Œπ}` such that given any morphism `h : A ‚ü∂ G X`,
there is some `i ‚àà Œπ` such that `h` factors through `f_i`.

The key part of this definition is that the indexing set `Œπ` lives in `Type v`, where `v` is the
universe of morphisms of the category: this is the "smallness" condition which allows the general
adjoint functor theorem to go through.
-/
def SolutionSetCondition {D : Type u} [Category.{v} D] (G : D ‚•§ C) : Prop :=
  ‚àÄ A : C,
    ‚àÉ (Œπ : Type v) (B : Œπ ‚Üí D) (f : ‚àÄ i : Œπ, A ‚ü∂ G.obj (B i)),
      ‚àÄ (X) (h : A ‚ü∂ G.obj X), ‚àÉ (i : Œπ) (g : B i ‚ü∂ X), f i ‚â´ G.map g = h

section GeneralAdjointFunctorTheorem

variable {D : Type u} [Category.{v} D]
variable (G : D ‚•§ C)

/-- If `G : D ‚•§ C` is a right adjoint it satisfies the solution set condition. -/
theorem solutionSetCondition_of_isRightAdjoint [G.IsRightAdjoint] : SolutionSetCondition G := by
  intro A
  refine
    ‚ü®PUnit, fun _ => G.leftAdjoint.obj A, fun _ => (Adjunction.ofIsRightAdjoint G).unit.app A, ?_‚ü©
  intro B h
  refine ‚ü®PUnit.unit, ((Adjunction.ofIsRightAdjoint G).homEquiv _ _).symm h, ?_‚ü©
  rw [‚Üê Adjunction.homEquiv_unit, Equiv.apply_symm_apply]

/-- The general adjoint functor theorem says that if `G : D ‚•§ C` preserves limits and `D` has them,
if `G` satisfies the solution set condition then `G` is a right adjoint.
-/
lemma isRightAdjoint_of_preservesLimits_of_solutionSetCondition [HasLimits D]
    [PreservesLimits G] (hG : SolutionSetCondition G) : G.IsRightAdjoint := by
  refine @isRightAdjointOfStructuredArrowInitials _ _ _ _ G ?_
  intro A
  specialize hG A
  choose Œπ B f g using hG
  let B' : Œπ ‚Üí StructuredArrow A G := fun i => StructuredArrow.mk (f i)
  have hB' : ‚àÄ A' : StructuredArrow A G, ‚àÉ i, Nonempty (B' i ‚ü∂ A') := by
    intro A'
    obtain ‚ü®i, _, t‚ü© := g _ A'.hom
    exact ‚ü®i, ‚ü®StructuredArrow.homMk _ t‚ü©‚ü©
  obtain ‚ü®T, hT‚ü© := has_weakly_initial_of_weakly_initial_set_and_hasProducts hB'
  apply hasInitial_of_weakly_initial_and_hasWideEqualizers hT

end GeneralAdjointFunctorTheorem

section SpecialAdjointFunctorTheorem

variable {D : Type u'} [Category.{v} D]

/-- The special adjoint functor theorem: if `G : D ‚•§ C` preserves limits and `D` is complete,
well-powered and has a small coseparating set, then `G` has a left adjoint.
-/
lemma isRightAdjoint_of_preservesLimits_of_isCoseparating [HasLimits D] [WellPowered D]
    {ùí¢ : Set D} [Small.{v} ùí¢] (hùí¢ : IsCoseparating ùí¢) (G : D ‚•§ C) [PreservesLimits G] :
    G.IsRightAdjoint :=
  have : ‚àÄ A, HasInitial (StructuredArrow A G) := fun A =>
    hasInitial_of_isCoseparating (StructuredArrow.isCoseparating_proj_preimage A G hùí¢)
  isRightAdjointOfStructuredArrowInitials _

/-- The special adjoint functor theorem: if `F : C ‚•§ D` preserves colimits and `C` is cocomplete,
well-copowered and has a small separating set, then `F` has a right adjoint.
-/
lemma isLeftAdjoint_of_preservesColimits_of_isSeparating [HasColimits C] [WellPowered C·µí·µñ]
    {ùí¢ : Set C} [Small.{v} ùí¢] (hùí¢ : IsSeparating ùí¢) (F : C ‚•§ D) [PreservesColimits F] :
    F.IsLeftAdjoint :=
  have : ‚àÄ A, HasTerminal (CostructuredArrow F A) := fun A =>
    hasTerminal_of_isSeparating (CostructuredArrow.isSeparating_proj_preimage F A hùí¢)
  isLeftAdjoint_of_costructuredArrowTerminals _

end SpecialAdjointFunctorTheorem

namespace Limits

/-- A consequence of the special adjoint functor theorem: if `C` is complete, well-powered and
    has a small coseparating set, then it is cocomplete. -/
theorem hasColimits_of_hasLimits_of_isCoseparating [HasLimits C] [WellPowered C] {ùí¢ : Set C}
    [Small.{v} ùí¢] (hùí¢ : IsCoseparating ùí¢) : HasColimits C :=
  { has_colimits_of_shape := fun _ _ =>
      hasColimitsOfShape_iff_isRightAdjoint_const.2
        (isRightAdjoint_of_preservesLimits_of_isCoseparating hùí¢ _) }

/-- A consequence of the special adjoint functor theorem: if `C` is cocomplete, well-copowered and
    has a small separating set, then it is complete. -/
theorem hasLimits_of_hasColimits_of_isSeparating [HasColimits C] [WellPowered C·µí·µñ] {ùí¢ : Set C}
    [Small.{v} ùí¢] (hùí¢ : IsSeparating ùí¢) : HasLimits C :=
  { has_limits_of_shape := fun _ _ =>
      hasLimitsOfShape_iff_isLeftAdjoint_const.2
        (isLeftAdjoint_of_preservesColimits_of_isSeparating hùí¢ _) }

theorem hasLimits_of_hasColimits_of_hasSeparator [HasColimits C] [HasSeparator C]
    [WellPowered C·µí·µñ] : HasLimits C := by
  obtain ‚ü®G, hG‚ü© : HasSeparator C := inferInstance
  exact hasLimits_of_hasColimits_of_isSeparating hG

theorem hasColimits_of_hasLimits_of_hasCoseparator [HasLimits C] [HasCoseparator C]
    [WellPowered C] : HasColimits C := by
  suffices HasLimits C·µí·µñ from hasColimits_of_hasLimits_op
  have : WellPowered C·µí·µñ·µí·µñ := wellPowered_of_equiv (opOpEquivalence C).symm
  have : HasSeparator C·µí·µñ := HasCoseparator.hasSeparator_op
  exact hasLimits_of_hasColimits_of_hasSeparator

end Limits

end CategoryTheory
