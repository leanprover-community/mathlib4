/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Emily Riehl
-/
import Mathlib.CategoryTheory.Adjunction.Basic
import Mathlib.CategoryTheory.Conj
import Mathlib.CategoryTheory.Category.Basic
import Mathlib.CategoryTheory.Functor.Basic
import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.Whiskering

import Mathlib.Tactic.ApplyFun

#align_import category_theory.adjunction.mates from "leanprover-community/mathlib"@"cea27692b3fdeb328a2ddba6aabf181754543184"

/-!
# Mate of natural transformations

This file establishes the bijection between the 2-cells

         Lâ‚                  Râ‚
      C --â†’ D             C â†-- D
    G â†“  â†—  â†“ H         G â†“  â†˜  â†“ H
      E --â†’ F             E â†-- F
         Lâ‚‚                  Râ‚‚

where `Lâ‚ âŠ£ Râ‚` and `Lâ‚‚ âŠ£ Râ‚‚`. The corresponding natural transformations are called mates.

This bijection includes a number of interesting cases as specializations. For instance, in the
special case where `G,H` are identity functors then the bijection preserves and reflects
isomorphisms (i.e. we have bijections`(Lâ‚‚ âŸ¶ Lâ‚) â‰ƒ (Râ‚ âŸ¶ Râ‚‚)`, and if either side is an iso then the
other side is as well). This demonstrates that adjoints to a given functor are unique up to
isomorphism (since if `Lâ‚ â‰… Lâ‚‚` then we deduce `Râ‚ â‰… Râ‚‚`).

Another example arises from considering the square representing that a functor `H` preserves
products, in particular the morphism `HA â¨¯ H- âŸ¶ H(A â¨¯ -)`. Then provided `(A â¨¯ -)` and `HA â¨¯ -`
have left adjoints (for instance if the relevant categories are cartesian closed), the transferred
natural transformation is the exponential comparison morphism: `H(A ^ -) âŸ¶ HA ^ H-`.
Furthermore if `H` has a left adjoint `L`, this morphism is an isomorphism iff its mate
`L(HA â¨¯ -) âŸ¶ A â¨¯ L-` is an isomorphism, see
https://ncatlab.org/nlab/show/Frobenius+reciprocity#InCategoryTheory.
This also relates to Grothendieck's yoga of six operations, though this is not spelled out in
mathlib: https://ncatlab.org/nlab/show/six+operations.
-/

universe vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ vâ‚… vâ‚† vâ‚‡ vâ‚ˆ vâ‚‰ uâ‚ uâ‚‚ uâ‚ƒ uâ‚„ uâ‚… uâ‚† uâ‚‡ uâ‚ˆ uâ‚‰
namespace CategoryTheory

open Category Functor Adjunction NatTrans

section Mates

variable {C : Type uâ‚} {D : Type uâ‚‚}{E : Type uâ‚ƒ} {F : Type uâ‚„}
variable [Category.{vâ‚} C] [Category.{vâ‚‚} D][Category.{vâ‚ƒ} E] [Category.{vâ‚„} F]
variable {G : C â¥¤ E} {H : D â¥¤ F} {Lâ‚ : C â¥¤ D} {Râ‚ : D â¥¤ C} {Lâ‚‚ : E â¥¤ F} {Râ‚‚ : F â¥¤ E}
variable (adjâ‚ : Lâ‚ âŠ£ Râ‚) (adjâ‚‚ : Lâ‚‚ âŠ£ Râ‚‚)

/-- Suppose we have a square of functors (where the top and
bottom are adjunctions `Lâ‚ âŠ£ Râ‚` and `Lâ‚‚ âŠ£ Râ‚‚` respectively).

      C â†” D
    G â†“   â†“ H
      E â†” F

Then we have a bijection between natural transformations `G â‹™ Lâ‚‚ âŸ¶ Lâ‚ â‹™ H` and
`Râ‚ â‹™ G âŸ¶ H â‹™ Râ‚‚`. This can be seen as a bijection of the 2-cells:

         Lâ‚                  Râ‚
      C --â†’ D             C â†-- D
    G â†“  â†—  â†“ H         G â†“  â†˜  â†“ H
      E --â†’ F             E â†-- F
         Lâ‚‚                  Râ‚‚

Note that if one of the transformations is an iso, it does not imply the other is an iso.
-/

-- ER: The original definition of the mates equivalence.
def transferNatTrans : (G â‹™ Lâ‚‚ âŸ¶ Lâ‚ â‹™ H) â‰ƒ (Râ‚ â‹™ G âŸ¶ H â‹™ Râ‚‚) where
  toFun h :=
    { app := fun X => adjâ‚‚.unit.app _ â‰« Râ‚‚.map (h.app _ â‰« H.map (adjâ‚.counit.app _))
      naturality := fun X Y f => by
        dsimp
        rw [assoc, â† Râ‚‚.map_comp, assoc, â† H.map_comp, â† adjâ‚.counit_naturality, H.map_comp, â†
          Functor.comp_map Lâ‚, â† h.naturality_assoc]
        simp }
  invFun h :=
    { app := fun X => Lâ‚‚.map (G.map (adjâ‚.unit.app _) â‰« h.app _) â‰« adjâ‚‚.counit.app _
      naturality := fun X Y f => by
        dsimp
        rw [â† Lâ‚‚.map_comp_assoc, â† G.map_comp_assoc, â† adjâ‚.unit_naturality, G.map_comp_assoc, â†
          Functor.comp_map, h.naturality]
        simp }
  left_inv h := by
    ext X
    dsimp
    simp only [Lâ‚‚.map_comp, assoc, adjâ‚‚.counit_naturality, adjâ‚‚.left_triangle_components_assoc, â†
      Functor.comp_map G Lâ‚‚, h.naturality_assoc, Functor.comp_map Lâ‚, â† H.map_comp,
      adjâ‚.left_triangle_components]
    dsimp
    simp only [id_comp, â† Functor.comp_map, â† Functor.comp_obj, NatTrans.naturality_assoc]
    simp only [Functor.comp_obj, Functor.comp_map, â† Functor.map_comp]
    have : Prefunctor.map Lâ‚.toPrefunctor (NatTrans.app adjâ‚.unit X) â‰«
        NatTrans.app adjâ‚.counit (Prefunctor.obj Lâ‚.toPrefunctor X) = ğŸ™ _ := by simp
    simp [this]
  -- See library note [dsimp, simp].
  right_inv h := by
    ext X
    dsimp
    simp [-Functor.comp_map, â† Functor.comp_map H, Functor.comp_map Râ‚, -NatTrans.naturality, â†
      h.naturality, -Functor.map_comp, â† Functor.map_comp_assoc G, Râ‚‚.map_comp]
#align category_theory.transfer_nat_trans CategoryTheory.transferNatTrans

/-- Corresponding natural transformations under the equivalence below are called mates.-/
@[simps]
def Mates :
    (G â‹™ Lâ‚‚ âŸ¶ Lâ‚ â‹™ H) â‰ƒ (Râ‚ â‹™ G âŸ¶ H â‹™ Râ‚‚) where
      toFun := fun Î± â†¦
        whiskerLeft (Râ‚ â‹™ G) adjâ‚‚.unit â‰«
        whiskerRight (whiskerLeft Râ‚ Î±) Râ‚‚ â‰«
        whiskerRight adjâ‚.counit (H â‹™ Râ‚‚)
      invFun := fun Î² â†¦
        whiskerRight adjâ‚.unit (G â‹™ Lâ‚‚) â‰«
        whiskerRight (whiskerLeft Lâ‚ Î²) Lâ‚‚ â‰«
        whiskerLeft (Lâ‚ â‹™ H) adjâ‚‚.counit
      left_inv := by
        intro Î±
        ext
        unfold whiskerRight whiskerLeft
        simp only [comp_obj, id_obj, Functor.comp_map, comp_app, map_comp, assoc, counit_naturality,
          counit_naturality_assoc, left_triangle_components_assoc]
        rw [â† assoc, â† Functor.comp_map, Î±.naturality, Functor.comp_map, assoc, â† H.map_comp,
          left_triangle_components, map_id]
        simp only [comp_obj, comp_id]
      right_inv := by
        intro Î²
        ext
        unfold whiskerLeft whiskerRight
        simp only [comp_obj, id_obj, Functor.comp_map, comp_app, map_comp, assoc,
          unit_naturality_assoc, right_triangle_components_assoc]
        rw [â† assoc, â† Functor.comp_map, assoc, â† Î².naturality, â† assoc, Functor.comp_map,
          â† G.map_comp, right_triangle_components, map_id, id_comp]
#align category_theory.mates CategoryTheory.Mates

-- ER: Note these definitions agree.
theorem RightMateEqualsTransferNatTrans
    (Î± : G â‹™ Lâ‚‚ âŸ¶ Lâ‚ â‹™ H) :
    Mates adjâ‚ adjâ‚‚ Î± = (transferNatTrans adjâ‚ adjâ‚‚) Î± := by
  ext; unfold Mates transferNatTrans; simp

-- ER: Note these definitions agree.
theorem LeftMateEqualsTransferNatTrans.symm
    (Î± : Râ‚ â‹™ G âŸ¶ H â‹™ Râ‚‚) :
    (Mates adjâ‚ adjâ‚‚).symm Î± = (transferNatTrans adjâ‚ adjâ‚‚).symm Î± := by
  ext; unfold Mates transferNatTrans; simp

-- ER: Propose to cut and replace with the version below:
theorem transferNatTrans_counit (f : G â‹™ Lâ‚‚ âŸ¶ Lâ‚ â‹™ H) (Y : D) :
    Lâ‚‚.map ((transferNatTrans adjâ‚ adjâ‚‚ f).app _) â‰« adjâ‚‚.counit.app _ =
      f.app _ â‰« H.map (adjâ‚.counit.app Y) := by
  erw [Functor.map_comp]
  simp

/-- A component of a transposed version of the mates correspondence. -/
theorem Mates_counit (Î± : G â‹™ Lâ‚‚ âŸ¶ Lâ‚ â‹™ H) (d : D) :
    Lâ‚‚.map ((Mates adjâ‚ adjâ‚‚ Î±).app _) â‰« adjâ‚‚.counit.app _ =
      Î±.app _ â‰« H.map (adjâ‚.counit.app d) := by
  erw [Functor.map_comp]; simp

-- ER: Propose to cut and replace with the version below:
theorem unit_transferNatTrans (f : G â‹™ Lâ‚‚ âŸ¶ Lâ‚ â‹™ H) (X : C) :
    G.map (adjâ‚.unit.app X) â‰« (transferNatTrans adjâ‚ adjâ‚‚ f).app _ =
      adjâ‚‚.unit.app _ â‰« Râ‚‚.map (f.app _) := by
  dsimp [transferNatTrans]
  rw [â† adjâ‚‚.unit_naturality_assoc, â† Râ‚‚.map_comp, â† Functor.comp_map G Lâ‚‚, f.naturality_assoc,
    Functor.comp_map, â† H.map_comp]
  dsimp; simp
#align category_theory.unit_transfer_nat_trans CategoryTheory.unit_transferNatTrans

-- ER: I don't get why this is harder than the counit case.
/- A component of a transposed version of the mates correspondence. -/
theorem unit_Mates (Î± : G â‹™ Lâ‚‚ âŸ¶ Lâ‚ â‹™ H) (c : C) :
    G.map (adjâ‚.unit.app c) â‰« (Mates adjâ‚ adjâ‚‚ Î±).app _ =
      adjâ‚‚.unit.app _ â‰« Râ‚‚.map (Î±.app _) := by
  dsimp [Mates]
  rw [â† adjâ‚‚.unit_naturality_assoc]
  slice_lhs 2 3 =>
    {
      rw [â† Râ‚‚.map_comp, â† Functor.comp_map G Lâ‚‚]
      rw [Î±.naturality]
    }
  rw [Râ‚‚.map_comp]
  slice_lhs 3 4 =>
    {
      rw [â† Râ‚‚.map_comp, Functor.comp_map Lâ‚ H]
      rw [â† H.map_comp]
      rw [left_triangle_components]
    }
  simp only [comp_obj, map_id, comp_id]

-- See library note [dsimp, simp]
end Mates

section MatesVComp

variable {A : Type uâ‚} {B : Type uâ‚‚} {C : Type uâ‚ƒ}
variable {D : Type uâ‚„} {E : Type uâ‚…} {F : Type uâ‚†}
variable [Category.{vâ‚} A] [Category.{vâ‚‚} B][Category.{vâ‚ƒ} C]
variable [Category.{vâ‚„} D] [Category.{vâ‚…} E][Category.{vâ‚†} F]
variable {Gâ‚ : A â¥¤ C}{Gâ‚‚ : C â¥¤ E}{Hâ‚ : B â¥¤ D}{Hâ‚‚ : D â¥¤ F}
variable {Lâ‚ : A â¥¤ B}{Râ‚ : B â¥¤ A} {Lâ‚‚ : C â¥¤ D}{Râ‚‚ : D â¥¤ C}
variable {Lâ‚ƒ : E â¥¤ F}{Râ‚ƒ : F â¥¤ E}
variable (adjâ‚ : Lâ‚ âŠ£ Râ‚) (adjâ‚‚ : Lâ‚‚ âŠ£ Râ‚‚) (adjâ‚ƒ : Lâ‚ƒ âŠ£ Râ‚ƒ)

/-- Squares between left adjoints can be composed "vertically" by pasting. -/
def LeftAdjointSquare.vcomp :
    (Gâ‚ â‹™ Lâ‚‚ âŸ¶ Lâ‚ â‹™ Hâ‚) â†’ (Gâ‚‚ â‹™ Lâ‚ƒ âŸ¶ Lâ‚‚ â‹™ Hâ‚‚) â†’ ((Gâ‚ â‹™ Gâ‚‚) â‹™ Lâ‚ƒ âŸ¶ Lâ‚ â‹™ (Hâ‚ â‹™ Hâ‚‚)) :=
  fun Î± Î² â†¦ (whiskerLeft Gâ‚ Î²) â‰« (whiskerRight Î± Hâ‚‚)
#align category_theory.leftadjointsquare_vcomp CategoryTheory.LeftAdjointSquare.vcomp

/-- Squares between right adjoints can be composed "vertically" by pasting. -/
def RightAdjointSquare.vcomp :
    (Râ‚ â‹™ Gâ‚ âŸ¶ Hâ‚ â‹™ Râ‚‚) â†’ (Râ‚‚ â‹™ Gâ‚‚ âŸ¶ Hâ‚‚ â‹™ Râ‚ƒ) â†’ (Râ‚ â‹™ (Gâ‚ â‹™ Gâ‚‚) âŸ¶ (Hâ‚ â‹™ Hâ‚‚) â‹™ Râ‚ƒ) :=
  fun Î± Î² â†¦ (whiskerRight Î± Gâ‚‚) â‰« (whiskerLeft Hâ‚ Î²)
#align category_theory.rightadjointsquare_vcomp CategoryTheory.RightAdjointSquare.vcomp

/-- The mates equivalence commutes with vertical composition. -/
theorem Mates_vcomp
    (Î± : Gâ‚ â‹™ Lâ‚‚ âŸ¶ Lâ‚ â‹™ Hâ‚) (Î² : Gâ‚‚ â‹™ Lâ‚ƒ âŸ¶ Lâ‚‚ â‹™ Hâ‚‚) :
    (Mates (G := Gâ‚ â‹™ Gâ‚‚) (H := Hâ‚ â‹™ Hâ‚‚) adjâ‚ adjâ‚ƒ) (LeftAdjointSquare.vcomp Î± Î²) =
      RightAdjointSquare.vcomp (Mates adjâ‚ adjâ‚‚ Î±) (Mates adjâ‚‚ adjâ‚ƒ Î²) := by
  unfold LeftAdjointSquare.vcomp RightAdjointSquare.vcomp Mates
  ext b
  simp only [comp_obj, Equiv.coe_fn_mk, whiskerLeft_comp, whiskerLeft_twice, whiskerRight_comp,
    assoc, comp_app, whiskerLeft_app, whiskerRight_app, id_obj, Functor.comp_map,
    whiskerRight_twice]
  slice_rhs 1 4 =>
    {
      rw [â† assoc, â† assoc, â† unit_naturality (adjâ‚ƒ)]
    }
  rw [Lâ‚ƒ.map_comp, Râ‚ƒ.map_comp]
  slice_rhs 2 4 =>
    {
      rw [â† Râ‚ƒ.map_comp, â† Râ‚ƒ.map_comp, â† assoc, â† Lâ‚ƒ.map_comp, â† Gâ‚‚.map_comp, â† Gâ‚‚.map_comp]
      rw [â† Functor.comp_map Gâ‚‚ Lâ‚ƒ, Î².naturality]
    }
  rw [(Lâ‚‚ â‹™ Hâ‚‚).map_comp, Râ‚ƒ.map_comp, Râ‚ƒ.map_comp]
  slice_rhs 4 5 =>
    {
      rw [â† Râ‚ƒ.map_comp, Functor.comp_map Lâ‚‚ _, â† Functor.comp_map _ Lâ‚‚, â† Hâ‚‚.map_comp]
      rw [adjâ‚‚.counit.naturality]
    }
  simp only [comp_obj, Functor.comp_map, map_comp, id_obj, Functor.id_map, assoc]
  slice_rhs 4 5 =>
    {
      rw [â† Râ‚ƒ.map_comp, â† Hâ‚‚.map_comp, â† Functor.comp_map _ Lâ‚‚, adjâ‚‚.counit.naturality]
    }
  simp only [comp_obj, id_obj, Functor.id_map, map_comp, assoc]
  slice_rhs 3 4 =>
    {
      rw [â† Râ‚ƒ.map_comp, â† Hâ‚‚.map_comp, left_triangle_components]
    }
  simp only [map_id, id_comp]

end MatesVComp

section MatesHComp

variable {A : Type uâ‚} {B : Type uâ‚‚} {C : Type uâ‚ƒ} {D : Type uâ‚„} {E : Type uâ‚…} {F : Type uâ‚†}
variable [Category.{vâ‚} A] [Category.{vâ‚‚} B][Category.{vâ‚ƒ} C]
variable [Category.{vâ‚„} D] [Category.{vâ‚…} E][Category.{vâ‚†} F]
variable {G : A â¥¤ D}{H : B â¥¤ E}{K : C â¥¤ F}
variable {Lâ‚ : A â¥¤ B}{Râ‚ : B â¥¤ A} {Lâ‚‚ : D â¥¤ E}{Râ‚‚ : E â¥¤ D}
variable {Lâ‚ƒ : B â¥¤ C}{Râ‚ƒ : C â¥¤ B} {Lâ‚„ : E â¥¤ F}{Râ‚„ : F â¥¤ E}
variable (adjâ‚ : Lâ‚ âŠ£ Râ‚) (adjâ‚‚ : Lâ‚‚ âŠ£ Râ‚‚)
variable (adjâ‚ƒ : Lâ‚ƒ âŠ£ Râ‚ƒ) (adjâ‚„ : Lâ‚„ âŠ£ Râ‚„)

/-- Squares between left adjoints can be composed "horizontally" by pasting. -/
def LeftAdjointSquare.hcomp :
    (G â‹™ Lâ‚‚ âŸ¶ Lâ‚ â‹™ H) â†’ (H â‹™ Lâ‚„ âŸ¶ Lâ‚ƒ â‹™ K) â†’ (G â‹™ (Lâ‚‚ â‹™ Lâ‚„) âŸ¶ (Lâ‚ â‹™ Lâ‚ƒ) â‹™ K) := fun Î± Î² â†¦
  (whiskerRight Î± Lâ‚„) â‰« (whiskerLeft Lâ‚ Î²)

/-- Squares between right adjoints can be composed "horizontally" by pasting. -/
def RightAdjointSquare.hcomp :
    (Râ‚ â‹™ G âŸ¶ H â‹™ Râ‚‚) â†’ (Râ‚ƒ â‹™ H âŸ¶ K â‹™ Râ‚„) â†’ ((Râ‚ƒ â‹™ Râ‚) â‹™ G âŸ¶ K â‹™ (Râ‚„ â‹™ Râ‚‚)) := fun Î± Î² â†¦
  (whiskerLeft Râ‚ƒ Î±) â‰« (whiskerRight Î² Râ‚‚)

/-- The mates equivalence commutes with horizontal composition of squares. -/
theorem Mates_hcomp
    (Î± : G â‹™ Lâ‚‚ âŸ¶ Lâ‚ â‹™ H) (Î² : H â‹™ Lâ‚„ âŸ¶ Lâ‚ƒ â‹™ K) :
    (Mates (adjâ‚.comp adjâ‚ƒ) (adjâ‚‚.comp adjâ‚„)) (LeftAdjointSquare.hcomp Î± Î²) =
      RightAdjointSquare.hcomp (Mates adjâ‚ adjâ‚‚ Î±) (Mates adjâ‚ƒ adjâ‚„ Î²) := by
  unfold LeftAdjointSquare.hcomp RightAdjointSquare.hcomp Mates Adjunction.comp
  ext c
  simp only [comp_obj, whiskerLeft_comp, whiskerLeft_twice, whiskerRight_comp, assoc,
    Equiv.coe_fn_mk, comp_app, whiskerLeft_app, whiskerRight_app, id_obj, associator_inv_app,
    Functor.comp_map, associator_hom_app, map_id, id_comp, whiskerRight_twice]
  slice_rhs 2 4 =>
    {
      rw [â† Râ‚‚.map_comp, â† Râ‚‚.map_comp, â† assoc, â† unit_naturality (adjâ‚„)]
    }
  rw [Râ‚‚.map_comp, Lâ‚„.map_comp, Râ‚„.map_comp, Râ‚‚.map_comp]
  slice_rhs 4 5 =>
    {
      rw [â† Râ‚‚.map_comp, â† Râ‚„.map_comp, â† Functor.comp_map _ Lâ‚„ , Î².naturality]
    }
  simp only [comp_obj, Functor.comp_map, map_comp, assoc]

end MatesHComp

section MatesSquareComp

variable {A : Type uâ‚} {B : Type uâ‚‚} {C : Type uâ‚ƒ}
variable {D : Type uâ‚„} {E : Type uâ‚…} {F : Type uâ‚†}
variable {X : Type uâ‚‡} {Y : Type uâ‚ˆ} {Z : Type uâ‚‰}
variable [Category.{vâ‚} A] [Category.{vâ‚‚} B][Category.{vâ‚ƒ} C]
variable [Category.{vâ‚„} D] [Category.{vâ‚…} E][Category.{vâ‚†} F]
variable [Category.{vâ‚‡} X] [Category.{vâ‚ˆ} Y][Category.{vâ‚‰} Z]
variable {Gâ‚ : A â¥¤ D} {Hâ‚ : B â¥¤ E} {Kâ‚ : C â¥¤ F} {Gâ‚‚ : D â¥¤ X} {Hâ‚‚ : E â¥¤ Y} {Kâ‚‚ : F â¥¤ Z}
variable {Lâ‚ : A â¥¤ B} {Râ‚ : B â¥¤ A} {Lâ‚‚ : B â¥¤ C} {Râ‚‚ : C â¥¤ B} {Lâ‚ƒ : D â¥¤ E} {Râ‚ƒ : E â¥¤ D}
variable {Lâ‚„ : E â¥¤ F} {Râ‚„ : F â¥¤ E} {Lâ‚… : X â¥¤ Y} {Râ‚… : Y â¥¤ X} {Lâ‚† : Y â¥¤ Z} {Râ‚† : Z â¥¤ Y}
variable (adjâ‚ : Lâ‚ âŠ£ Râ‚) (adjâ‚‚ : Lâ‚‚ âŠ£ Râ‚‚) (adjâ‚ƒ : Lâ‚ƒ âŠ£ Râ‚ƒ)
variable (adjâ‚„ : Lâ‚„ âŠ£ Râ‚„) (adjâ‚… : Lâ‚… âŠ£ Râ‚…) (adjâ‚† : Lâ‚† âŠ£ Râ‚†)

/-- Squares of squares between left adjoints can be composed by iterating vertical and horizontal composition.-/
def LeftAdjointSquare.comp
    (Î± : Gâ‚ â‹™ Lâ‚ƒ âŸ¶ Lâ‚ â‹™ Hâ‚) (Î² : Hâ‚ â‹™ Lâ‚„ âŸ¶ Lâ‚‚ â‹™ Kâ‚)
    (Î³ : Gâ‚‚ â‹™ Lâ‚… âŸ¶ Lâ‚ƒ â‹™ Hâ‚‚) (Î´ : Hâ‚‚ â‹™ Lâ‚† âŸ¶ Lâ‚„ â‹™ Kâ‚‚) :
    ((Gâ‚ â‹™ Gâ‚‚) â‹™ (Lâ‚… â‹™ Lâ‚†)) âŸ¶ ((Lâ‚ â‹™ Lâ‚‚) â‹™ (Kâ‚ â‹™ Kâ‚‚)) :=
  LeftAdjointSquare.vcomp (LeftAdjointSquare.hcomp Î± Î²) (LeftAdjointSquare.hcomp Î³ Î´)
#align category_theory.leftadjointsquare_comp CategoryTheory.LeftAdjointSquare.comp

theorem LeftAdjointSquare.comp_vhcomp
    (Î± : Gâ‚ â‹™ Lâ‚ƒ âŸ¶ Lâ‚ â‹™ Hâ‚) (Î² : Hâ‚ â‹™ Lâ‚„ âŸ¶ Lâ‚‚ â‹™ Kâ‚)
    (Î³ : Gâ‚‚ â‹™ Lâ‚… âŸ¶ Lâ‚ƒ â‹™ Hâ‚‚) (Î´ : Hâ‚‚ â‹™ Lâ‚† âŸ¶ Lâ‚„ â‹™ Kâ‚‚) :
    LeftAdjointSquare.comp Î± Î² Î³ Î´ =
      LeftAdjointSquare.vcomp (LeftAdjointSquare.hcomp Î± Î²) (LeftAdjointSquare.hcomp Î³ Î´) := rfl

/-- Horizontal and vertical composition of squares commutes.-/
theorem LeftAdjointSquare.comp_hvcomp
    (Î± : Gâ‚ â‹™ Lâ‚ƒ âŸ¶ Lâ‚ â‹™ Hâ‚) (Î² : Hâ‚ â‹™ Lâ‚„ âŸ¶ Lâ‚‚ â‹™ Kâ‚)
    (Î³ : Gâ‚‚ â‹™ Lâ‚… âŸ¶ Lâ‚ƒ â‹™ Hâ‚‚) (Î´ : Hâ‚‚ â‹™ Lâ‚† âŸ¶ Lâ‚„ â‹™ Kâ‚‚) :
    LeftAdjointSquare.comp Î± Î² Î³ Î´ =
      LeftAdjointSquare.hcomp (LeftAdjointSquare.vcomp Î± Î³) (LeftAdjointSquare.vcomp Î² Î´) := by
  unfold LeftAdjointSquare.comp LeftAdjointSquare.hcomp LeftAdjointSquare.vcomp
  unfold whiskerLeft whiskerRight
  ext a
  simp only [comp_obj, comp_app, map_comp, assoc]
  slice_rhs 2 3 =>
    {
      rw [â† Functor.comp_map _ Lâ‚†, Î´.naturality]
    }
  simp only [comp_obj, Functor.comp_map, assoc]

/-- Squares of squares between right adjoints can be composed by iterating vertical and horizontal composition.-/
def RightAdjointSquare.comp
    (Î± : Râ‚ â‹™ Gâ‚ âŸ¶ Hâ‚ â‹™ Râ‚ƒ) (Î² : Râ‚‚ â‹™ Hâ‚ âŸ¶ Kâ‚ â‹™ Râ‚„)
    (Î³ : Râ‚ƒ â‹™ Gâ‚‚ âŸ¶ Hâ‚‚ â‹™ Râ‚…) (Î´ : Râ‚„ â‹™ Hâ‚‚ âŸ¶ Kâ‚‚ â‹™ Râ‚†) :
    ((Râ‚‚ â‹™ Râ‚) â‹™ (Gâ‚ â‹™ Gâ‚‚) âŸ¶ (Kâ‚ â‹™ Kâ‚‚) â‹™ (Râ‚† â‹™ Râ‚…)) :=
  RightAdjointSquare.vcomp (RightAdjointSquare.hcomp Î± Î²) (RightAdjointSquare.hcomp Î³ Î´)
#align category_theory.rightadjointsquare_comp CategoryTheory.RightAdjointSquare.comp

theorem RightAdjointSquare.comp_vhcomp
    (Î± : Râ‚ â‹™ Gâ‚ âŸ¶ Hâ‚ â‹™ Râ‚ƒ) (Î² : Râ‚‚ â‹™ Hâ‚ âŸ¶ Kâ‚ â‹™ Râ‚„)
    (Î³ : Râ‚ƒ â‹™ Gâ‚‚ âŸ¶ Hâ‚‚ â‹™ Râ‚…) (Î´ : Râ‚„ â‹™ Hâ‚‚ âŸ¶ Kâ‚‚ â‹™ Râ‚†) :
    RightAdjointSquare.comp Î± Î² Î³ Î´ =
    RightAdjointSquare.vcomp (RightAdjointSquare.hcomp Î± Î²) (RightAdjointSquare.hcomp Î³ Î´) := rfl

/-- Horizontal and vertical composition of squares commutes.-/
theorem RightAdjointSquare.comp_hvcomp
    (Î± : Râ‚ â‹™ Gâ‚ âŸ¶ Hâ‚ â‹™ Râ‚ƒ) (Î² : Râ‚‚ â‹™ Hâ‚ âŸ¶ Kâ‚ â‹™ Râ‚„)
    (Î³ : Râ‚ƒ â‹™ Gâ‚‚ âŸ¶ Hâ‚‚ â‹™ Râ‚…) (Î´ : Râ‚„ â‹™ Hâ‚‚ âŸ¶ Kâ‚‚ â‹™ Râ‚†) :
    RightAdjointSquare.comp Î± Î² Î³ Î´ =
    RightAdjointSquare.hcomp (RightAdjointSquare.vcomp Î± Î³) (RightAdjointSquare.vcomp Î² Î´) := by
  unfold RightAdjointSquare.comp RightAdjointSquare.hcomp RightAdjointSquare.vcomp
  unfold whiskerLeft whiskerRight
  ext c
  simp only [comp_obj, comp_app, map_comp, assoc]
  slice_rhs 2 3 =>
    {
      rw [â† Functor.comp_map _ Râ‚…, â† Î³.naturality]
    }
  simp only [comp_obj, Functor.comp_map, assoc]

/-- The mates equivalence commutes with composition of squares of squares. These results form the basis for an isomorphism of double categories to be proven later.
-/
theorem Mates_square
    (Î± : Gâ‚ â‹™ Lâ‚ƒ âŸ¶ Lâ‚ â‹™ Hâ‚) (Î² : Hâ‚ â‹™ Lâ‚„ âŸ¶ Lâ‚‚ â‹™ Kâ‚)
    (Î³ : Gâ‚‚ â‹™ Lâ‚… âŸ¶ Lâ‚ƒ â‹™ Hâ‚‚) (Î´ : Hâ‚‚ â‹™ Lâ‚† âŸ¶ Lâ‚„ â‹™ Kâ‚‚) :
    (Mates (G := Gâ‚ â‹™ Gâ‚‚) (H := Kâ‚ â‹™ Kâ‚‚) (adjâ‚.comp adjâ‚‚) (adjâ‚….comp adjâ‚†))
        (LeftAdjointSquare.comp Î± Î² Î³ Î´) =
      RightAdjointSquare.comp
        (Mates adjâ‚ adjâ‚ƒ Î±) (Mates adjâ‚‚ adjâ‚„ Î²) (Mates adjâ‚ƒ adjâ‚… Î³) (Mates adjâ‚„ adjâ‚† Î´) := by
  have vcomp :=
    Mates_vcomp (adjâ‚.comp adjâ‚‚) (adjâ‚ƒ.comp adjâ‚„) (adjâ‚….comp adjâ‚†)
      (LeftAdjointSquare.hcomp Î± Î²) (LeftAdjointSquare.hcomp Î³ Î´)
  have hcomp1 := Mates_hcomp adjâ‚ adjâ‚ƒ adjâ‚‚ adjâ‚„ Î± Î²
  have hcomp2 := Mates_hcomp adjâ‚ƒ adjâ‚… adjâ‚„ adjâ‚† Î³ Î´
  rw [hcomp1, hcomp2] at vcomp
  exact vcomp

end MatesSquareComp

section Conjugates

variable {C : Type uâ‚} {D : Type uâ‚‚}
variable [Category.{vâ‚} C] [Category.{vâ‚‚} D]
variable {Lâ‚ Lâ‚‚ Lâ‚ƒ : C â¥¤ D} {Râ‚ Râ‚‚ Râ‚ƒ : D â¥¤ C}
variable (adjâ‚ : Lâ‚ âŠ£ Râ‚) (adjâ‚‚ : Lâ‚‚ âŠ£ Râ‚‚) (adjâ‚ƒ : Lâ‚ƒ âŠ£ Râ‚ƒ)

/-- Given two adjunctions `Lâ‚ âŠ£ Râ‚` and `Lâ‚‚ âŠ£ Râ‚‚` both between categories `C`, `D`, there is a
bijection between natural transformations `Lâ‚‚ âŸ¶ Lâ‚` and natural transformations `Râ‚ âŸ¶ Râ‚‚`. This is defined as a special case of `Mates`, where the two "vertical" functors are identity, modulo composition with the unitors.
TODO: Generalise to when the two vertical functors are equivalences rather than being exactly `ğŸ­`.

Furthermore, this bijection preserves (and reflects) isomorphisms, i.e. a transformation is an iso
iff its image under the bijection is an iso, see eg `CategoryTheory.transferNatTransSelf_iso`.
This is in contrast to the general case `transferNatTrans` which
 does not in general have this property.-/

/-- Mates between parallel adjunctions are called conjugates. -/
def Conjugates : (Lâ‚‚ âŸ¶ Lâ‚) â‰ƒ (Râ‚ âŸ¶ Râ‚‚) :=
  calc
    (Lâ‚‚ âŸ¶ Lâ‚) â‰ƒ _ := (Iso.homCongr Lâ‚‚.leftUnitor Lâ‚.rightUnitor).symm
    _ â‰ƒ _ := Mates adjâ‚ adjâ‚‚
    _ â‰ƒ (Râ‚ âŸ¶ Râ‚‚) := Râ‚.rightUnitor.homCongr Râ‚‚.leftUnitor

/-- A component of a transposed form of the conjugation definition. -/
theorem Conjugates_counit (Î± : Lâ‚‚ âŸ¶ Lâ‚) (d : D) :
    Lâ‚‚.map ((Conjugates adjâ‚ adjâ‚‚ Î±).app _) â‰« adjâ‚‚.counit.app d =
      Î±.app _ â‰« adjâ‚.counit.app d := by
  dsimp [Conjugates]
  rw [id_comp, comp_id]
  have := Mates_counit adjâ‚ adjâ‚‚ (Lâ‚‚.leftUnitor.hom â‰« Î± â‰« Lâ‚.rightUnitor.inv) d
  dsimp at this
  rw [this]
  simp only [comp_id, id_comp]

/-- A component of a transposed form of the conjugation definition. -/
theorem unit_Conjugates (Î± : Lâ‚‚ âŸ¶ Lâ‚) (c : C) :
    adjâ‚.unit.app _ â‰« (Conjugates adjâ‚ adjâ‚‚ Î±).app _ =
      adjâ‚‚.unit.app c â‰« Râ‚‚.map (Î±.app _) := by
  dsimp [Conjugates]
  rw [id_comp, comp_id]
  have := unit_Mates adjâ‚ adjâ‚‚ (Lâ‚‚.leftUnitor.hom â‰« Î± â‰« Lâ‚.rightUnitor.inv) c
  dsimp at this
  rw [this]
  simp

@[simp]
theorem Conjugates_id : Conjugates adjâ‚ adjâ‚ (ğŸ™ _) = ğŸ™ _ := by
  ext
  dsimp [Conjugates, Mates]
  simp only [comp_id, map_id, id_comp, right_triangle_components]

@[simp]
theorem Conjugates_symm_id : (Conjugates adjâ‚ adjâ‚).symm (ğŸ™ _) = ğŸ™ _ := by
  rw [Equiv.symm_apply_eq]
  simp only [Conjugates_id]
#align category_theory.conjugates_symm_id CategoryTheory.Conjugates_symm_id

theorem Conjugates_adjunction_id {L R : C â¥¤ C} (adj : L âŠ£ R) (Î± : ğŸ­ C âŸ¶ L) (c : C) :
    (Conjugates adj Adjunction.id Î±).app c = Î±.app (R.obj c) â‰« adj.counit.app c := by
  dsimp [Conjugates, Mates, Adjunction.id]
  simp only [comp_id, id_comp]

theorem Conjugates_adjunction_id_symm {L R : C â¥¤ C} (adj : L âŠ£ R) (Î± : R âŸ¶ ğŸ­ C) (c : C) :
    ((Conjugates adj Adjunction.id).symm Î±).app c = adj.unit.app c â‰« Î±.app (L.obj c) := by
  dsimp [Conjugates, Mates, Adjunction.id]
  simp only [comp_id, id_comp]
end Conjugates


section ConjugateComposition
variable {C : Type uâ‚} {D : Type uâ‚‚}
variable [Category.{vâ‚} C] [Category.{vâ‚‚} D]
variable {Lâ‚ Lâ‚‚ Lâ‚ƒ : C â¥¤ D} {Râ‚ Râ‚‚ Râ‚ƒ : D â¥¤ C}
variable (adjâ‚ : Lâ‚ âŠ£ Râ‚) (adjâ‚‚ : Lâ‚‚ âŠ£ Râ‚‚) (adjâ‚ƒ : Lâ‚ƒ âŠ£ Râ‚ƒ)

theorem Conjugates_comp (Î± : Lâ‚‚ âŸ¶ Lâ‚) (Î² : Lâ‚ƒ âŸ¶ Lâ‚‚) :
    Conjugates adjâ‚ adjâ‚‚ Î± â‰« Conjugates adjâ‚‚ adjâ‚ƒ Î² =
      Conjugates adjâ‚ adjâ‚ƒ (Î² â‰« Î±) := by
  ext d
  dsimp [Conjugates, Mates]
  have vcomp := Mates_vcomp adjâ‚ adjâ‚‚ adjâ‚ƒ
    (Lâ‚‚.leftUnitor.hom â‰« Î± â‰« Lâ‚.rightUnitor.inv)
    (Lâ‚ƒ.leftUnitor.hom â‰« Î² â‰« Lâ‚‚.rightUnitor.inv)
  have vcompd := congr_app vcomp d
  dsimp [Mates, LeftAdjointSquare.vcomp, RightAdjointSquare.vcomp] at vcompd
  simp at vcompd
  simp only [comp_id, id_comp, assoc, map_comp]
  rw [vcompd]

-- ER: Warning: swapped order of the arguments.
theorem Conjugates_symm_comp (Î± : Râ‚ âŸ¶ Râ‚‚) (Î² : Râ‚‚ âŸ¶ Râ‚ƒ) :
    (Conjugates adjâ‚‚ adjâ‚ƒ).symm Î² â‰« (Conjugates adjâ‚ adjâ‚‚).symm Î± =
      (Conjugates adjâ‚ adjâ‚ƒ).symm (Î± â‰« Î²) := by
  rw [Equiv.eq_symm_apply, â† Conjugates_comp _ adjâ‚‚]
  simp only [Equiv.apply_symm_apply]

theorem Conjugates_comm {Î± : Lâ‚‚ âŸ¶ Lâ‚} {Î² : Lâ‚ âŸ¶ Lâ‚‚} (Î²Î± : Î² â‰« Î± = ğŸ™ _) :
    Conjugates adjâ‚ adjâ‚‚ Î± â‰« Conjugates adjâ‚‚ adjâ‚ Î² = ğŸ™ _ := by
  rw [Conjugates_comp, Î²Î±, Conjugates_id]

theorem Conjugates_symm_comm {Î± : Râ‚ âŸ¶ Râ‚‚}{Î² : Râ‚‚ âŸ¶ Râ‚} (Î±Î² : Î± â‰« Î² = ğŸ™ _) :
    (Conjugates adjâ‚‚ adjâ‚).symm Î² â‰« (Conjugates adjâ‚ adjâ‚‚).symm Î± = ğŸ™ _ := by
  rw [Conjugates_symm_comp, Î±Î², Conjugates_symm_id]

/-- If `Î±` is an isomorphism between left adjoints, then its conjugate transformation is an isomorphism.
The converse is given in `Conjugates_of_iso`.
-/
instance Conjugates_iso (Î± : Lâ‚‚ âŸ¶ Lâ‚) [IsIso Î±] :
    IsIso (Conjugates adjâ‚ adjâ‚‚ Î±) :=
  âŸ¨âŸ¨Conjugates adjâ‚‚ adjâ‚ (inv Î±),
      âŸ¨Conjugates_comm _ _ (by simp), Conjugates_comm _ _ (by simp)âŸ©âŸ©âŸ©

/-- If `Î±` is an isomorphism between right adjoints, then its conjugate transformation is an isomorphism.
The converse is given in `Conjugates_symm_of_iso`.
-/
instance Conjugates_symm_iso (Î± : Râ‚ âŸ¶ Râ‚‚) [IsIso Î±] :
    IsIso ((Conjugates adjâ‚ adjâ‚‚).symm Î±) :=
  âŸ¨âŸ¨(Conjugates adjâ‚‚ adjâ‚).symm (inv Î±),
      âŸ¨Conjugates_symm_comm _ _ (by simp), Conjugates_symm_comm _ _ (by simp)âŸ©âŸ©âŸ©

/-- If `Î±` is a natural transformation between left adjoints whose conjugate natural transformation is an isomorphism,
then `Î±` is an isomorphism.
The converse is given in `Conjugate_iso`.
-/
theorem Conjugate_of_iso (Î± : Lâ‚‚ âŸ¶ Lâ‚) [IsIso (Conjugates adjâ‚ adjâ‚‚ Î±)] :
    IsIso Î± := by
  suffices IsIso ((Conjugates adjâ‚ adjâ‚‚).symm (Conjugates adjâ‚ adjâ‚‚ Î±))
    by simpa using this
  infer_instance

/--
If `Î±` is a natural transformation between right adjoints whose conjugate natural transformation is an isomorphism,
then `Î±` is an isomorphism.
The converse is given in `Conjugates_symm_iso`.
-/
theorem Conjugates_symm_of_iso (Î± : Râ‚ âŸ¶ Râ‚‚)
    [IsIso ((Conjugates adjâ‚ adjâ‚‚).symm Î±)] : IsIso Î± := by
  suffices IsIso ((Conjugates adjâ‚ adjâ‚‚) ((Conjugates adjâ‚ adjâ‚‚).symm Î±))
    by simpa using this
  infer_instance
end ConjugateComposition

section Self
variable {C : Type uâ‚} {D : Type uâ‚‚}
variable [Category.{vâ‚} C] [Category.{vâ‚‚} D]
variable {Lâ‚ Lâ‚‚ Lâ‚ƒ : C â¥¤ D} {Râ‚ Râ‚‚ Râ‚ƒ : D â¥¤ C}
variable (adjâ‚ : Lâ‚ âŠ£ Râ‚) (adjâ‚‚ : Lâ‚‚ âŠ£ Râ‚‚) (adjâ‚ƒ : Lâ‚ƒ âŠ£ Râ‚ƒ)

/-- The old definition to be cut.-/
def transferNatTransSelf : (Lâ‚‚ âŸ¶ Lâ‚) â‰ƒ (Râ‚ âŸ¶ Râ‚‚) :=
  calc
    (Lâ‚‚ âŸ¶ Lâ‚) â‰ƒ _ := (Iso.homCongr Lâ‚‚.leftUnitor Lâ‚.rightUnitor).symm
    _ â‰ƒ _ := transferNatTrans adjâ‚ adjâ‚‚
    _ â‰ƒ (Râ‚ âŸ¶ Râ‚‚) := Râ‚.rightUnitor.homCongr Râ‚‚.leftUnitor
#align category_theory.transfer_nat_trans_self CategoryTheory.transferNatTransSelf

theorem transferNatTransSelf_counit (f : Lâ‚‚ âŸ¶ Lâ‚) (X) :
    Lâ‚‚.map ((transferNatTransSelf adjâ‚ adjâ‚‚ f).app _) â‰« adjâ‚‚.counit.app X =
      f.app _ â‰« adjâ‚.counit.app X := by
  dsimp [transferNatTransSelf]
  rw [id_comp, comp_id]
  have := transferNatTrans_counit adjâ‚ adjâ‚‚ (Lâ‚‚.leftUnitor.hom â‰« f â‰« Lâ‚.rightUnitor.inv) X
  dsimp at this
  rw [this]
  simp
#align category_theory.transfer_nat_trans_self_counit CategoryTheory.transferNatTransSelf_counit

theorem unit_transferNatTransSelf (f : Lâ‚‚ âŸ¶ Lâ‚) (X) :
    adjâ‚.unit.app _ â‰« (transferNatTransSelf adjâ‚ adjâ‚‚ f).app _ =
      adjâ‚‚.unit.app X â‰« Râ‚‚.map (f.app _) := by
  dsimp [transferNatTransSelf]
  rw [id_comp, comp_id]
  have := unit_transferNatTrans adjâ‚ adjâ‚‚ (Lâ‚‚.leftUnitor.hom â‰« f â‰« Lâ‚.rightUnitor.inv) X
  dsimp at this
  rw [this]
  simp
#align category_theory.unit_transfer_nat_trans_self CategoryTheory.unit_transferNatTransSelf

@[simp]
theorem transferNatTransSelf_id : transferNatTransSelf adjâ‚ adjâ‚ (ğŸ™ _) = ğŸ™ _ := by
  ext
  dsimp [transferNatTransSelf, transferNatTrans]
  simp
#align category_theory.transfer_nat_trans_self_id CategoryTheory.transferNatTransSelf_id

-- See library note [dsimp, simp]
@[simp]
theorem transferNatTransSelf_symm_id : (transferNatTransSelf adjâ‚ adjâ‚).symm (ğŸ™ _) = ğŸ™ _ := by
  rw [Equiv.symm_apply_eq]
  simp
#align category_theory.transfer_nat_trans_self_symm_id CategoryTheory.transferNatTransSelf_symm_id

theorem transferNatTransSelf_comp (f g) :
    transferNatTransSelf adjâ‚ adjâ‚‚ f â‰« transferNatTransSelf adjâ‚‚ adjâ‚ƒ g =
      transferNatTransSelf adjâ‚ adjâ‚ƒ (g â‰« f) := by
  ext
  dsimp [transferNatTransSelf, transferNatTrans]
  simp only [id_comp, comp_id]
  rw [â† adjâ‚ƒ.unit_naturality_assoc, â† Râ‚ƒ.map_comp, g.naturality_assoc, Lâ‚‚.map_comp, assoc,
    adjâ‚‚.counit_naturality, adjâ‚‚.left_triangle_components_assoc, assoc]
#align category_theory.transfer_nat_trans_self_comp CategoryTheory.transferNatTransSelf_comp

theorem transferNatTransSelf_adjunction_id {L R : C â¥¤ C} (adj : L âŠ£ R) (f : ğŸ­ C âŸ¶ L) (X : C) :
    (transferNatTransSelf adj Adjunction.id f).app X = f.app (R.obj X) â‰« adj.counit.app X := by
  dsimp [transferNatTransSelf, transferNatTrans, Adjunction.id]
  simp only [comp_id, id_comp]
#align category_theory.transfer_nat_trans_self_adjunction_id CategoryTheory.transferNatTransSelf_adjunction_id

theorem transferNatTransSelf_adjunction_id_symm {L R : C â¥¤ C} (adj : L âŠ£ R) (g : R âŸ¶ ğŸ­ C) (X : C) :
    ((transferNatTransSelf adj Adjunction.id).symm g).app X = adj.unit.app X â‰« g.app (L.obj X) := by
  dsimp [transferNatTransSelf, transferNatTrans, Adjunction.id]
  simp only [comp_id, id_comp]
#align category_theory.transfer_nat_trans_self_adjunction_id_symm CategoryTheory.transferNatTransSelf_adjunction_id_symm

theorem transferNatTransSelf_symm_comp (f g) :
    (transferNatTransSelf adjâ‚‚ adjâ‚).symm f â‰« (transferNatTransSelf adjâ‚ƒ adjâ‚‚).symm g =
      (transferNatTransSelf adjâ‚ƒ adjâ‚).symm (g â‰« f) := by
  rw [Equiv.eq_symm_apply, â† transferNatTransSelf_comp _ adjâ‚‚]
  simp
#align category_theory.transfer_nat_trans_self_symm_comp CategoryTheory.transferNatTransSelf_symm_comp

theorem transferNatTransSelf_comm {f g} (gf : g â‰« f = ğŸ™ _) :
    transferNatTransSelf adjâ‚ adjâ‚‚ f â‰« transferNatTransSelf adjâ‚‚ adjâ‚ g = ğŸ™ _ := by
  rw [transferNatTransSelf_comp, gf, transferNatTransSelf_id]
#align category_theory.transfer_nat_trans_self_comm CategoryTheory.transferNatTransSelf_comm

theorem transferNatTransSelf_symm_comm {f g} (gf : g â‰« f = ğŸ™ _) :
    (transferNatTransSelf adjâ‚ adjâ‚‚).symm f â‰« (transferNatTransSelf adjâ‚‚ adjâ‚).symm g = ğŸ™ _ := by
  rw [transferNatTransSelf_symm_comp, gf, transferNatTransSelf_symm_id]
#align category_theory.transfer_nat_trans_self_symm_comm CategoryTheory.transferNatTransSelf_symm_comm

/-- If `f` is an isomorphism, then the transferred natural transformation is an isomorphism.
The converse is given in `transferNatTransSelf_of_iso`.
-/
instance transferNatTransSelf_iso (f : Lâ‚‚ âŸ¶ Lâ‚) [IsIso f] :
    IsIso (transferNatTransSelf adjâ‚ adjâ‚‚ f) :=
  âŸ¨âŸ¨transferNatTransSelf adjâ‚‚ adjâ‚ (inv f),
      âŸ¨transferNatTransSelf_comm _ _ (by simp), transferNatTransSelf_comm _ _ (by simp)âŸ©âŸ©âŸ©
#align category_theory.transfer_nat_trans_self_iso CategoryTheory.transferNatTransSelf_iso

/-- If `f` is an isomorphism, then the un-transferred natural transformation is an isomorphism.
The converse is given in `transferNatTransSelf_symm_of_iso`.
-/
instance transferNatTransSelf_symm_iso (f : Râ‚ âŸ¶ Râ‚‚) [IsIso f] :
    IsIso ((transferNatTransSelf adjâ‚ adjâ‚‚).symm f) :=
  âŸ¨âŸ¨(transferNatTransSelf adjâ‚‚ adjâ‚).symm (inv f),
      âŸ¨transferNatTransSelf_symm_comm _ _ (by simp), transferNatTransSelf_symm_comm _ _ (by simp)âŸ©âŸ©âŸ©
#align category_theory.transfer_nat_trans_self_symm_iso CategoryTheory.transferNatTransSelf_symm_iso

/-- If `f` is a natural transformation whose transferred natural transformation is an isomorphism,
then `f` is an isomorphism.
The converse is given in `transferNatTransSelf_iso`.
-/
theorem transferNatTransSelf_of_iso (f : Lâ‚‚ âŸ¶ Lâ‚) [IsIso (transferNatTransSelf adjâ‚ adjâ‚‚ f)] :
    IsIso f := by
  suffices IsIso ((transferNatTransSelf adjâ‚ adjâ‚‚).symm (transferNatTransSelf adjâ‚ adjâ‚‚ f))
    by simpa using this
  infer_instance
#align category_theory.transfer_nat_trans_self_of_iso CategoryTheory.transferNatTransSelf_of_iso

/--
If `f` is a natural transformation whose un-transferred natural transformation is an isomorphism,
then `f` is an isomorphism.
The converse is given in `transferNatTransSelf_symm_iso`.
-/
theorem transferNatTransSelf_symm_of_iso (f : Râ‚ âŸ¶ Râ‚‚)
    [IsIso ((transferNatTransSelf adjâ‚ adjâ‚‚).symm f)] : IsIso f := by
  suffices IsIso ((transferNatTransSelf adjâ‚ adjâ‚‚) ((transferNatTransSelf adjâ‚ adjâ‚‚).symm f))
    by simpa using this
  infer_instance
#align category_theory.transfer_nat_trans_self_symm_of_iso CategoryTheory.transferNatTransSelf_symm_of_iso

end Self

end CategoryTheory
