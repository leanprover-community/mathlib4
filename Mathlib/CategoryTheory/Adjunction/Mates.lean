/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Emily Riehl, Jo√´l Riou
-/
import Mathlib.CategoryTheory.Adjunction.Basic
import Mathlib.CategoryTheory.Functor.TwoSquare
import Mathlib.CategoryTheory.HomCongr
import Mathlib.Tactic.ApplyFun

/-!
# Mate of natural transformations

This file establishes the bijection between the 2-cells

```
         L‚ÇÅ                  R‚ÇÅ
      C --‚Üí D             C ‚Üê-- D
    G ‚Üì  ‚Üó  ‚Üì H         G ‚Üì  ‚Üò  ‚Üì H
      E --‚Üí F             E ‚Üê-- F
         L‚ÇÇ                  R‚ÇÇ
```

where `L‚ÇÅ ‚ä£ R‚ÇÅ` and `L‚ÇÇ ‚ä£ R‚ÇÇ`. The corresponding natural transformations are called mates.

This bijection includes a number of interesting cases as specializations. For instance, in the
special case where `G,H` are identity functors then the bijection preserves and reflects
isomorphisms (i.e. we have bijections`(L‚ÇÇ ‚ü∂ L‚ÇÅ) ‚âÉ (R‚ÇÅ ‚ü∂ R‚ÇÇ)`, and if either side is an iso then the
other side is as well). This demonstrates that adjoints to a given functor are unique up to
isomorphism (since if `L‚ÇÅ ‚âÖ L‚ÇÇ` then we deduce `R‚ÇÅ ‚âÖ R‚ÇÇ`).

Another example arises from considering the square representing that a functor `H` preserves
products, in particular the morphism `HA ‚®Ø H- ‚ü∂ H(A ‚®Ø -)`. Then provided `(A ‚®Ø -)` and `HA ‚®Ø -`
have left adjoints (for instance if the relevant categories are Cartesian closed), the transferred
natural transformation is the exponential comparison morphism: `H(A ^ -) ‚ü∂ HA ^ H-`.
Furthermore if `H` has a left adjoint `L`, this morphism is an isomorphism iff its mate
`L(HA ‚®Ø -) ‚ü∂ A ‚®Ø L-` is an isomorphism, see
https://ncatlab.org/nlab/show/Frobenius+reciprocity#InCategoryTheory.
This also relates to Grothendieck's yoga of six operations, though this is not spelled out in
mathlib: https://ncatlab.org/nlab/show/six+operations.
-/

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ v‚ÇÑ v‚ÇÖ v‚ÇÜ v‚Çá v‚Çà v‚Çâ u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ u‚ÇÖ u‚ÇÜ u‚Çá u‚Çà u‚Çâ
namespace CategoryTheory

open Category Functor Adjunction NatTrans TwoSquare

section mateEquiv

variable {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} {E : Type u‚ÇÉ} {F : Type u‚ÇÑ}
variable [Category.{v‚ÇÅ} C] [Category.{v‚ÇÇ} D] [Category.{v‚ÇÉ} E] [Category.{v‚ÇÑ} F]
variable {G : C ‚•§ E} {H : D ‚•§ F} {L‚ÇÅ : C ‚•§ D} {R‚ÇÅ : D ‚•§ C} {L‚ÇÇ : E ‚•§ F} {R‚ÇÇ : F ‚•§ E}
variable (adj‚ÇÅ : L‚ÇÅ ‚ä£ R‚ÇÅ) (adj‚ÇÇ : L‚ÇÇ ‚ä£ R‚ÇÇ)

/-- Suppose we have a square of functors (where the top and bottom are adjunctions `L‚ÇÅ ‚ä£ R‚ÇÅ`
and `L‚ÇÇ ‚ä£ R‚ÇÇ` respectively).

```
      C ‚Üî D
    G ‚Üì   ‚Üì H
      E ‚Üî F
```

Then we have a bijection between natural transformations `G ‚ãô L‚ÇÇ ‚ü∂ L‚ÇÅ ‚ãô H` and
`R‚ÇÅ ‚ãô G ‚ü∂ H ‚ãô R‚ÇÇ`. This can be seen as a bijection of the 2-cells:

```
         L‚ÇÅ                  R‚ÇÅ
      C --‚Üí D             C ‚Üê-- D
    G ‚Üì  ‚Üó  ‚Üì H         G ‚Üì  ‚Üò  ‚Üì H
      E --‚Üí F             E ‚Üê-- F
         L‚ÇÇ                  R‚ÇÇ
```

Note that if one of the transformations is an iso, it does not imply the other is an iso.
-/
@[simps]
def mateEquiv : TwoSquare G L‚ÇÅ L‚ÇÇ H ‚âÉ TwoSquare R‚ÇÅ H G R‚ÇÇ where
  toFun Œ± := .mk _ _ _ _ <|
    (rightUnitor _).inv ‚â´
    whiskerLeft (R‚ÇÅ ‚ãô G) adj‚ÇÇ.unit ‚â´
    (associator _ _ _).hom ‚â´ whiskerLeft _ (associator _ _ _).inv ‚â´
    whiskerLeft R‚ÇÅ (whiskerRight Œ±.natTrans R‚ÇÇ) ‚â´
    whiskerLeft _ (associator _ _ _).hom ‚â´ (associator _ _ _).inv ‚â´
    whiskerRight adj‚ÇÅ.counit (H ‚ãô R‚ÇÇ) ‚â´
    (leftUnitor _).hom
  invFun Œ≤ := .mk _ _ _ _ <|
    (leftUnitor _).inv ‚â´
    whiskerRight adj‚ÇÅ.unit (G ‚ãô L‚ÇÇ) ‚â´
    (associator _ _ _).inv ‚â´ whiskerRight (associator _ _ _).hom _ ‚â´
    whiskerRight (whiskerLeft L‚ÇÅ Œ≤.natTrans) L‚ÇÇ ‚â´
    whiskerRight (associator _ _ _).inv _ ‚â´ (associator _ _ _).hom ‚â´
    whiskerLeft (L‚ÇÅ ‚ãô H) adj‚ÇÇ.counit ‚â´
    (rightUnitor _).hom
  left_inv Œ± := by
    ext
    simp only [comp_obj, whiskerLeft_comp, whiskerLeft_twice, assoc, Iso.hom_inv_id_assoc,
      whiskerRight_comp, comp_app, id_obj, leftUnitor_inv_app, Functor.whiskerRight_app,
      Functor.comp_map, associator_inv_app, associator_hom_app, map_id, Functor.whiskerLeft_app,
      rightUnitor_inv_app, leftUnitor_hom_app, rightUnitor_hom_app, comp_id, id_comp,
      counit_naturality, counit_naturality_assoc, left_triangle_components_assoc]
    rw [‚Üê assoc, ‚Üê Functor.comp_map, Œ±.natTrans.naturality, Functor.comp_map, assoc, ‚Üê H.map_comp,
      left_triangle_components, map_id]
    simp only [comp_obj, comp_id]
  right_inv Œ≤ := by
    ext
    simp only [comp_obj, whiskerRight_comp, whiskerRight_twice, assoc, Iso.inv_hom_id_assoc,
      whiskerLeft_comp, comp_app, id_obj, rightUnitor_inv_app, Functor.whiskerLeft_app,
      associator_hom_app, associator_inv_app, Functor.whiskerRight_app, leftUnitor_inv_app, map_id,
      Functor.comp_map, rightUnitor_hom_app, leftUnitor_hom_app, comp_id, id_comp,
      unit_naturality_assoc, right_triangle_components_assoc]
    rw [‚Üê assoc, ‚Üê Functor.comp_map, assoc, ‚Üê Œ≤.natTrans.naturality, ‚Üê assoc, Functor.comp_map,
      ‚Üê G.map_comp, right_triangle_components, map_id, id_comp]

/-- A component of a transposed version of the mates correspondence. -/
theorem mateEquiv_counit (Œ± : TwoSquare G L‚ÇÅ L‚ÇÇ H) (d : D) :
    L‚ÇÇ.map ((mateEquiv adj‚ÇÅ adj‚ÇÇ Œ±).app _) ‚â´ adj‚ÇÇ.counit.app _ =
      Œ±.app _ ‚â´ H.map (adj‚ÇÅ.counit.app d) := by simp

/-- A component of a transposed version of the inverse mates correspondence. -/
theorem mateEquiv_counit_symm (Œ± : TwoSquare R‚ÇÅ H G R‚ÇÇ) (d : D) :
    L‚ÇÇ.map (Œ±.app _) ‚â´ adj‚ÇÇ.counit.app _ =
      ((mateEquiv adj‚ÇÅ adj‚ÇÇ).symm Œ±).app _ ‚â´ H.map (adj‚ÇÅ.counit.app d) := by
  conv_lhs => rw [‚Üê (mateEquiv adj‚ÇÅ adj‚ÇÇ).right_inv Œ±]
  exact (mateEquiv_counit adj‚ÇÅ adj‚ÇÇ ((mateEquiv adj‚ÇÅ adj‚ÇÇ).symm Œ±) d)

/- A component of a transposed version of the mates correspondence. -/
theorem unit_mateEquiv (Œ± : TwoSquare G L‚ÇÅ L‚ÇÇ H) (c : C) :
    G.map (adj‚ÇÅ.unit.app c) ‚â´ (mateEquiv adj‚ÇÅ adj‚ÇÇ Œ±).app _ =
      adj‚ÇÇ.unit.app _ ‚â´ R‚ÇÇ.map (Œ±.app _) := by
  simp only [id_obj, comp_obj, mateEquiv, Equiv.coe_fn_mk, comp_app, rightUnitor_inv_app,
    Functor.whiskerLeft_app, associator_hom_app, associator_inv_app, Functor.whiskerRight_app,
    Functor.comp_map, leftUnitor_hom_app, comp_id, id_comp]
  rw [‚Üê adj‚ÇÇ.unit_naturality_assoc]
  slice_lhs 2 3 =>
    rw [‚Üê R‚ÇÇ.map_comp, ‚Üê Functor.comp_map G L‚ÇÇ, Œ±.naturality]
  rw [R‚ÇÇ.map_comp]
  slice_lhs 3 4 =>
    rw [‚Üê R‚ÇÇ.map_comp, Functor.comp_map L‚ÇÅ H, ‚Üê H.map_comp, left_triangle_components]
  simp only [comp_obj, map_id, comp_id]

/-- A component of a transposed version of the inverse mates correspondence. -/
theorem unit_mateEquiv_symm (Œ± : TwoSquare R‚ÇÅ H G R‚ÇÇ) (c : C) :
    G.map (adj‚ÇÅ.unit.app c) ‚â´ Œ±.app _ =
      adj‚ÇÇ.unit.app _ ‚â´ R‚ÇÇ.map (((mateEquiv adj‚ÇÅ adj‚ÇÇ).symm Œ±).app _) := by
  conv_lhs => rw [‚Üê (mateEquiv adj‚ÇÅ adj‚ÇÇ).right_inv Œ±]
  exact (unit_mateEquiv adj‚ÇÅ adj‚ÇÇ ((mateEquiv adj‚ÇÅ adj‚ÇÇ).symm Œ±) c)

end mateEquiv

section mateEquivVComp

variable {A : Type u‚ÇÅ} {B : Type u‚ÇÇ} {C : Type u‚ÇÉ} {D : Type u‚ÇÑ} {E : Type u‚ÇÖ} {F : Type u‚ÇÜ}
variable [Category.{v‚ÇÅ} A] [Category.{v‚ÇÇ} B] [Category.{v‚ÇÉ} C]
variable [Category.{v‚ÇÑ} D] [Category.{v‚ÇÖ} E] [Category.{v‚ÇÜ} F]
variable {G‚ÇÅ : A ‚•§ C} {G‚ÇÇ : C ‚•§ E} {H‚ÇÅ : B ‚•§ D} {H‚ÇÇ : D ‚•§ F}
variable {L‚ÇÅ : A ‚•§ B} {R‚ÇÅ : B ‚•§ A} {L‚ÇÇ : C ‚•§ D} {R‚ÇÇ : D ‚•§ C} {L‚ÇÉ : E ‚•§ F} {R‚ÇÉ : F ‚•§ E}
variable (adj‚ÇÅ : L‚ÇÅ ‚ä£ R‚ÇÅ) (adj‚ÇÇ : L‚ÇÇ ‚ä£ R‚ÇÇ) (adj‚ÇÉ : L‚ÇÉ ‚ä£ R‚ÇÉ)

/-- The mates equivalence commutes with vertical composition. -/
theorem mateEquiv_vcomp (Œ± : TwoSquare G‚ÇÅ L‚ÇÅ L‚ÇÇ H‚ÇÅ) (Œ≤ : TwoSquare G‚ÇÇ L‚ÇÇ L‚ÇÉ H‚ÇÇ) :
    (mateEquiv adj‚ÇÅ adj‚ÇÉ) (Œ± ‚â´‚Çï Œ≤) = (mateEquiv adj‚ÇÅ adj‚ÇÇ Œ±) ‚â´·µ• (mateEquiv adj‚ÇÇ adj‚ÇÉ Œ≤) := by
  unfold hComp vComp mateEquiv
  ext b
  simp only [comp_obj, Equiv.coe_fn_mk, whiskerRight_comp, whiskerRight_twice, assoc,
    whiskerLeft_comp, comp_app, id_obj, rightUnitor_inv_app, Functor.whiskerLeft_app,
    associator_hom_app, associator_inv_app, Functor.whiskerRight_app, map_id, Functor.comp_map,
    leftUnitor_hom_app, comp_id, id_comp, whiskerLeft_twice, Iso.hom_inv_id_assoc]
  slice_rhs 1 4 => rw [‚Üê assoc, ‚Üê assoc, ‚Üê unit_naturality (adj‚ÇÉ)]
  rw [L‚ÇÉ.map_comp, R‚ÇÉ.map_comp]
  slice_rhs 2 4 =>
    rw [‚Üê R‚ÇÉ.map_comp, ‚Üê R‚ÇÉ.map_comp, ‚Üê assoc, ‚Üê L‚ÇÉ.map_comp, ‚Üê G‚ÇÇ.map_comp, ‚Üê G‚ÇÇ.map_comp]
    rw [‚Üê Functor.comp_map G‚ÇÇ L‚ÇÉ, Œ≤.naturality]
  rw [(L‚ÇÇ ‚ãô H‚ÇÇ).map_comp, R‚ÇÉ.map_comp, R‚ÇÉ.map_comp]
  slice_rhs 4 5 =>
    rw [‚Üê R‚ÇÉ.map_comp, Functor.comp_map L‚ÇÇ _, ‚Üê Functor.comp_map _ L‚ÇÇ, ‚Üê H‚ÇÇ.map_comp]
    rw [adj‚ÇÇ.counit.naturality]
  simp only [comp_obj, Functor.comp_map, map_comp, id_obj, Functor.id_map, assoc]
  slice_rhs 4 5 =>
    rw [‚Üê R‚ÇÉ.map_comp, ‚Üê H‚ÇÇ.map_comp, ‚Üê Functor.comp_map _ L‚ÇÇ, adj‚ÇÇ.counit.naturality]
  simp only [comp_obj, id_obj, Functor.id_map, map_comp, assoc]
  slice_rhs 3 4 =>
    rw [‚Üê R‚ÇÉ.map_comp, ‚Üê H‚ÇÇ.map_comp, left_triangle_components]
  simp only [map_id, id_comp]

end mateEquivVComp

section mateEquivHComp

variable {A : Type u‚ÇÅ} {B : Type u‚ÇÇ} {C : Type u‚ÇÉ} {D : Type u‚ÇÑ} {E : Type u‚ÇÖ} {F : Type u‚ÇÜ}
variable [Category.{v‚ÇÅ} A] [Category.{v‚ÇÇ} B] [Category.{v‚ÇÉ} C]
variable [Category.{v‚ÇÑ} D] [Category.{v‚ÇÖ} E] [Category.{v‚ÇÜ} F]
variable {G : A ‚•§ D} {H : B ‚•§ E} {K : C ‚•§ F}
variable {L‚ÇÅ : A ‚•§ B} {R‚ÇÅ : B ‚•§ A} {L‚ÇÇ : D ‚•§ E} {R‚ÇÇ : E ‚•§ D}
variable {L‚ÇÉ : B ‚•§ C} {R‚ÇÉ : C ‚•§ B} {L‚ÇÑ : E ‚•§ F} {R‚ÇÑ : F ‚•§ E}
variable (adj‚ÇÅ : L‚ÇÅ ‚ä£ R‚ÇÅ) (adj‚ÇÇ : L‚ÇÇ ‚ä£ R‚ÇÇ) (adj‚ÇÉ : L‚ÇÉ ‚ä£ R‚ÇÉ) (adj‚ÇÑ : L‚ÇÑ ‚ä£ R‚ÇÑ)

/-- The mates equivalence commutes with horizontal composition of squares. -/
theorem mateEquiv_hcomp (Œ± : TwoSquare G L‚ÇÅ L‚ÇÇ H) (Œ≤ : TwoSquare H L‚ÇÉ L‚ÇÑ K) :
    (mateEquiv (adj‚ÇÅ.comp adj‚ÇÉ) (adj‚ÇÇ.comp adj‚ÇÑ)) (Œ± ‚â´·µ• Œ≤) =
      (mateEquiv adj‚ÇÉ adj‚ÇÑ Œ≤) ‚â´‚Çï (mateEquiv adj‚ÇÅ adj‚ÇÇ Œ±) := by
  unfold vComp hComp mateEquiv Adjunction.comp
  ext c
  simp only [comp_obj, whiskerRight_comp, assoc, mk'_unit, whiskerLeft_comp, mk'_counit,
    whiskerRight_twice, Iso.inv_hom_id_assoc, Equiv.coe_fn_mk, comp_app, id_obj,
    rightUnitor_inv_app, Functor.whiskerLeft_app, Functor.whiskerRight_app, map_id,
    associator_inv_app, associator_hom_app, Functor.comp_map, rightUnitor_hom_app,
    leftUnitor_hom_app, comp_id, id_comp, whiskerLeft_twice, Iso.hom_inv_id_assoc]
  slice_rhs 2 4 =>
    rw [‚Üê R‚ÇÇ.map_comp, ‚Üê R‚ÇÇ.map_comp, ‚Üê assoc, ‚Üê unit_naturality (adj‚ÇÑ)]
  rw [R‚ÇÇ.map_comp, L‚ÇÑ.map_comp, R‚ÇÑ.map_comp, R‚ÇÇ.map_comp]
  slice_rhs 4 5 =>
    rw [‚Üê R‚ÇÇ.map_comp, ‚Üê R‚ÇÑ.map_comp, ‚Üê Functor.comp_map _ L‚ÇÑ, Œ≤.naturality]
  simp only [comp_obj, Functor.comp_map, map_comp, assoc]

end mateEquivHComp

section mateEquivSquareComp

variable {A : Type u‚ÇÅ} {B : Type u‚ÇÇ} {C : Type u‚ÇÉ}
variable {D : Type u‚ÇÑ} {E : Type u‚ÇÖ} {F : Type u‚ÇÜ}
variable {X : Type u‚Çá} {Y : Type u‚Çà} {Z : Type u‚Çâ}
variable [Category.{v‚ÇÅ} A] [Category.{v‚ÇÇ} B] [Category.{v‚ÇÉ} C]
variable [Category.{v‚ÇÑ} D] [Category.{v‚ÇÖ} E] [Category.{v‚ÇÜ} F]
variable [Category.{v‚Çá} X] [Category.{v‚Çà} Y] [Category.{v‚Çâ} Z]
variable {G‚ÇÅ : A ‚•§ D} {H‚ÇÅ : B ‚•§ E} {K‚ÇÅ : C ‚•§ F} {G‚ÇÇ : D ‚•§ X} {H‚ÇÇ : E ‚•§ Y} {K‚ÇÇ : F ‚•§ Z}
variable {L‚ÇÅ : A ‚•§ B} {R‚ÇÅ : B ‚•§ A} {L‚ÇÇ : B ‚•§ C} {R‚ÇÇ : C ‚•§ B} {L‚ÇÉ : D ‚•§ E} {R‚ÇÉ : E ‚•§ D}
variable {L‚ÇÑ : E ‚•§ F} {R‚ÇÑ : F ‚•§ E} {L‚ÇÖ : X ‚•§ Y} {R‚ÇÖ : Y ‚•§ X} {L‚ÇÜ : Y ‚•§ Z} {R‚ÇÜ : Z ‚•§ Y}
variable (adj‚ÇÅ : L‚ÇÅ ‚ä£ R‚ÇÅ) (adj‚ÇÇ : L‚ÇÇ ‚ä£ R‚ÇÇ) (adj‚ÇÉ : L‚ÇÉ ‚ä£ R‚ÇÉ)
variable (adj‚ÇÑ : L‚ÇÑ ‚ä£ R‚ÇÑ) (adj‚ÇÖ : L‚ÇÖ ‚ä£ R‚ÇÖ) (adj‚ÇÜ : L‚ÇÜ ‚ä£ R‚ÇÜ)

/-- The mates equivalence commutes with composition of squares of squares. These results form the
basis for an isomorphism of double categories to be proven later.
-/
theorem mateEquiv_square (Œ± : TwoSquare G‚ÇÅ L‚ÇÅ L‚ÇÉ H‚ÇÅ) (Œ≤ : TwoSquare H‚ÇÅ L‚ÇÇ L‚ÇÑ K‚ÇÅ)
    (Œ≥ : TwoSquare G‚ÇÇ L‚ÇÉ L‚ÇÖ H‚ÇÇ) (Œ¥ : TwoSquare H‚ÇÇ L‚ÇÑ L‚ÇÜ K‚ÇÇ) :
    (mateEquiv (adj‚ÇÅ.comp adj‚ÇÇ) (adj‚ÇÖ.comp adj‚ÇÜ)) ((Œ± ‚â´·µ• Œ≤) ‚â´‚Çï (Œ≥ ‚â´·µ• Œ¥)) =
      ((mateEquiv adj‚ÇÇ adj‚ÇÑ Œ≤) ‚â´‚Çï (mateEquiv adj‚ÇÅ adj‚ÇÉ Œ±))
         ‚â´·µ• ((mateEquiv adj‚ÇÑ adj‚ÇÜ Œ¥) ‚â´‚Çï (mateEquiv adj‚ÇÉ adj‚ÇÖ Œ≥)) := by
  have vcomp :=
    mateEquiv_vcomp (adj‚ÇÅ.comp adj‚ÇÇ) (adj‚ÇÉ.comp adj‚ÇÑ) (adj‚ÇÖ.comp adj‚ÇÜ) (Œ± ‚â´·µ• Œ≤) (Œ≥ ‚â´·µ• Œ¥)
  simp only [mateEquiv_hcomp] at vcomp
  assumption

end mateEquivSquareComp

section conjugateEquiv

variable {C : Type u‚ÇÅ} {D : Type u‚ÇÇ}
variable [Category.{v‚ÇÅ} C] [Category.{v‚ÇÇ} D]
variable {L‚ÇÅ L‚ÇÇ : C ‚•§ D} {R‚ÇÅ R‚ÇÇ : D ‚•§ C}
variable (adj‚ÇÅ : L‚ÇÅ ‚ä£ R‚ÇÅ) (adj‚ÇÇ : L‚ÇÇ ‚ä£ R‚ÇÇ)

/-- Given two adjunctions `L‚ÇÅ ‚ä£ R‚ÇÅ` and `L‚ÇÇ ‚ä£ R‚ÇÇ` both between categories `C`, `D`, there is a
bijection between natural transformations `L‚ÇÇ ‚ü∂ L‚ÇÅ` and natural transformations `R‚ÇÅ ‚ü∂ R‚ÇÇ`. This is
defined as a special case of `mateEquiv`, where the two "vertical" functors are identity, modulo
composition with the unitors. Corresponding natural transformations are called `conjugateEquiv`.
TODO: Generalise to when the two vertical functors are equivalences rather than being exactly `ùü≠`.

Furthermore, this bijection preserves (and reflects) isomorphisms, i.e. a transformation is an iso
iff its image under the bijection is an iso, see e.g. `CategoryTheory.conjugateIsoEquiv`.
This is in contrast to the general case `mateEquiv` which does not in general have this property.
-/
@[simps!]
def conjugateEquiv : (L‚ÇÇ ‚ü∂ L‚ÇÅ) ‚âÉ (R‚ÇÅ ‚ü∂ R‚ÇÇ) :=
  calc
    (L‚ÇÇ ‚ü∂ L‚ÇÅ) ‚âÉ (ùü≠ C ‚ãô L‚ÇÇ ‚ü∂ L‚ÇÅ ‚ãô ùü≠ D) := (Iso.homCongr L‚ÇÇ.leftUnitor L‚ÇÅ.rightUnitor).symm
    _ ‚âÉ TwoSquare _ _ _ _ := (TwoSquare.equivNatTrans _ _ _ _).symm
    _ ‚âÉ _ := mateEquiv adj‚ÇÅ adj‚ÇÇ
    _ ‚âÉ (R‚ÇÅ ‚ãô ùü≠ C ‚ü∂ ùü≠ D ‚ãô R‚ÇÇ) := TwoSquare.equivNatTrans _ _ _ _
    _ ‚âÉ (R‚ÇÅ ‚ü∂ R‚ÇÇ) := R‚ÇÅ.rightUnitor.homCongr R‚ÇÇ.leftUnitor

/-- A component of a transposed form of the conjugation definition. -/
theorem conjugateEquiv_counit (Œ± : L‚ÇÇ ‚ü∂ L‚ÇÅ) (d : D) :
    L‚ÇÇ.map ((conjugateEquiv adj‚ÇÅ adj‚ÇÇ Œ±).app _) ‚â´ adj‚ÇÇ.counit.app d =
      Œ±.app _ ‚â´ adj‚ÇÅ.counit.app d := by
  simp

/-- A component of a transposed form of the inverse conjugation definition. -/
theorem conjugateEquiv_counit_symm (Œ± : R‚ÇÅ ‚ü∂ R‚ÇÇ) (d : D) :
    L‚ÇÇ.map (Œ±.app _) ‚â´ adj‚ÇÇ.counit.app d =
      ((conjugateEquiv adj‚ÇÅ adj‚ÇÇ).symm Œ±).app _ ‚â´ adj‚ÇÅ.counit.app d := by
    conv_lhs => rw [‚Üê (conjugateEquiv adj‚ÇÅ adj‚ÇÇ).right_inv Œ±]
    exact (conjugateEquiv_counit adj‚ÇÅ adj‚ÇÇ ((conjugateEquiv adj‚ÇÅ adj‚ÇÇ).symm Œ±) d)

/-- A component of a transposed form of the conjugation definition. -/
theorem unit_conjugateEquiv (Œ± : L‚ÇÇ ‚ü∂ L‚ÇÅ) (c : C) :
    adj‚ÇÅ.unit.app _ ‚â´ (conjugateEquiv adj‚ÇÅ adj‚ÇÇ Œ±).app _ =
      adj‚ÇÇ.unit.app c ‚â´ R‚ÇÇ.map (Œ±.app _) := by
  dsimp [conjugateEquiv]
  rw [id_comp, comp_id]
  have := unit_mateEquiv adj‚ÇÅ adj‚ÇÇ (L‚ÇÇ.leftUnitor.hom ‚â´ Œ± ‚â´ L‚ÇÅ.rightUnitor.inv) c
  dsimp at this
  rw [this]
  simp

/-- A component of a transposed form of the inverse conjugation definition. -/
theorem unit_conjugateEquiv_symm (Œ± : R‚ÇÅ ‚ü∂ R‚ÇÇ) (c : C) :
    adj‚ÇÅ.unit.app _ ‚â´ Œ±.app _ =
      adj‚ÇÇ.unit.app c ‚â´ R‚ÇÇ.map (((conjugateEquiv adj‚ÇÅ adj‚ÇÇ).symm Œ±).app _) := by
    conv_lhs => rw [‚Üê (conjugateEquiv adj‚ÇÅ adj‚ÇÇ).right_inv Œ±]
    exact (unit_conjugateEquiv adj‚ÇÅ adj‚ÇÇ ((conjugateEquiv adj‚ÇÅ adj‚ÇÇ).symm Œ±) c)

@[simp]
theorem conjugateEquiv_id : conjugateEquiv adj‚ÇÅ adj‚ÇÅ (ùüô _) = ùüô _ := by
  ext
  simp

@[simp]
theorem conjugateEquiv_symm_id : (conjugateEquiv adj‚ÇÅ adj‚ÇÅ).symm (ùüô _) = ùüô _ := by
  rw [Equiv.symm_apply_eq]
  simp only [conjugateEquiv_id]

theorem conjugateEquiv_adjunction_id {L R : C ‚•§ C} (adj : L ‚ä£ R) (Œ± : ùü≠ C ‚ü∂ L) (c : C) :
    (conjugateEquiv adj Adjunction.id Œ±).app c = Œ±.app (R.obj c) ‚â´ adj.counit.app c := by
  simp [conjugateEquiv, mateEquiv, Adjunction.id]

theorem conjugateEquiv_adjunction_id_symm {L R : C ‚•§ C} (adj : L ‚ä£ R) (Œ± : R ‚ü∂ ùü≠ C) (c : C) :
    ((conjugateEquiv adj Adjunction.id).symm Œ±).app c = adj.unit.app c ‚â´ Œ±.app (L.obj c) := by
  simp [conjugateEquiv, mateEquiv, Adjunction.id]

end conjugateEquiv

section ConjugateComposition
variable {C : Type u‚ÇÅ} {D : Type u‚ÇÇ}
variable [Category.{v‚ÇÅ} C] [Category.{v‚ÇÇ} D]
variable {L‚ÇÅ L‚ÇÇ L‚ÇÉ : C ‚•§ D} {R‚ÇÅ R‚ÇÇ R‚ÇÉ : D ‚•§ C}
variable (adj‚ÇÅ : L‚ÇÅ ‚ä£ R‚ÇÅ) (adj‚ÇÇ : L‚ÇÇ ‚ä£ R‚ÇÇ) (adj‚ÇÉ : L‚ÇÉ ‚ä£ R‚ÇÉ)

@[reassoc (attr := simp)]
theorem conjugateEquiv_comp (Œ± : L‚ÇÇ ‚ü∂ L‚ÇÅ) (Œ≤ : L‚ÇÉ ‚ü∂ L‚ÇÇ) :
    conjugateEquiv adj‚ÇÅ adj‚ÇÇ Œ± ‚â´ conjugateEquiv adj‚ÇÇ adj‚ÇÉ Œ≤ =
      conjugateEquiv adj‚ÇÅ adj‚ÇÉ (Œ≤ ‚â´ Œ±) := by
  ext d
  dsimp [conjugateEquiv, mateEquiv]
  have vcomp := mateEquiv_vcomp adj‚ÇÅ adj‚ÇÇ adj‚ÇÉ
    (L‚ÇÇ.leftUnitor.hom ‚â´ Œ± ‚â´ L‚ÇÅ.rightUnitor.inv)
    (L‚ÇÉ.leftUnitor.hom ‚â´ Œ≤ ‚â´ L‚ÇÇ.rightUnitor.inv)
  have vcompd := congr_app vcomp d
  simp only [comp_obj, id_obj, mateEquiv_apply, comp_app, rightUnitor_inv_app,
    Functor.whiskerLeft_app, associator_hom_app, associator_inv_app, Functor.whiskerRight_app,
    hComp_app, leftUnitor_hom_app, comp_id, id_comp, Functor.id_map, map_comp, Functor.comp_map,
    assoc, whiskerRight_comp, whiskerLeft_comp, vComp_app, map_id] at vcompd ‚ä¢
  rw [vcompd]

@[reassoc (attr := simp)]
theorem conjugateEquiv_symm_comp (Œ± : R‚ÇÅ ‚ü∂ R‚ÇÇ) (Œ≤ : R‚ÇÇ ‚ü∂ R‚ÇÉ) :
    (conjugateEquiv adj‚ÇÇ adj‚ÇÉ).symm Œ≤ ‚â´ (conjugateEquiv adj‚ÇÅ adj‚ÇÇ).symm Œ± =
      (conjugateEquiv adj‚ÇÅ adj‚ÇÉ).symm (Œ± ‚â´ Œ≤) := by
  rw [Equiv.eq_symm_apply, ‚Üê conjugateEquiv_comp _ adj‚ÇÇ]
  simp only [Equiv.apply_symm_apply]

theorem conjugateEquiv_comm {Œ± : L‚ÇÇ ‚ü∂ L‚ÇÅ} {Œ≤ : L‚ÇÅ ‚ü∂ L‚ÇÇ} (Œ≤Œ± : Œ≤ ‚â´ Œ± = ùüô _) :
    conjugateEquiv adj‚ÇÅ adj‚ÇÇ Œ± ‚â´ conjugateEquiv adj‚ÇÇ adj‚ÇÅ Œ≤ = ùüô _ := by
  rw [conjugateEquiv_comp, Œ≤Œ±, conjugateEquiv_id]

theorem conjugateEquiv_symm_comm {Œ± : R‚ÇÅ ‚ü∂ R‚ÇÇ} {Œ≤ : R‚ÇÇ ‚ü∂ R‚ÇÅ} (Œ±Œ≤ : Œ± ‚â´ Œ≤ = ùüô _) :
    (conjugateEquiv adj‚ÇÇ adj‚ÇÅ).symm Œ≤ ‚â´ (conjugateEquiv adj‚ÇÅ adj‚ÇÇ).symm Œ± = ùüô _ := by
  rw [conjugateEquiv_symm_comp, Œ±Œ≤, conjugateEquiv_symm_id]

end ConjugateComposition

section ConjugateIsomorphism

variable {C : Type u‚ÇÅ} {D : Type u‚ÇÇ}
variable [Category.{v‚ÇÅ} C] [Category.{v‚ÇÇ} D]
variable {L‚ÇÅ L‚ÇÇ : C ‚•§ D} {R‚ÇÅ R‚ÇÇ : D ‚•§ C}
variable (adj‚ÇÅ : L‚ÇÅ ‚ä£ R‚ÇÅ) (adj‚ÇÇ : L‚ÇÇ ‚ä£ R‚ÇÇ)

/-- If `Œ±` is an isomorphism between left adjoints, then its conjugate transformation is an
isomorphism. The converse is given in `conjugateEquiv_of_iso`.
-/
instance conjugateEquiv_iso (Œ± : L‚ÇÇ ‚ü∂ L‚ÇÅ) [IsIso Œ±] :
    IsIso (conjugateEquiv adj‚ÇÅ adj‚ÇÇ Œ±) :=
  ‚ü®‚ü®conjugateEquiv adj‚ÇÇ adj‚ÇÅ (inv Œ±),
      ‚ü®conjugateEquiv_comm _ _ (by simp), conjugateEquiv_comm _ _ (by simp)‚ü©‚ü©‚ü©

/-- If `Œ±` is an isomorphism between right adjoints, then its conjugate transformation is an
isomorphism. The converse is given in `conjugateEquiv_symm_of_iso`.
-/
instance conjugateEquiv_symm_iso (Œ± : R‚ÇÅ ‚ü∂ R‚ÇÇ) [IsIso Œ±] :
    IsIso ((conjugateEquiv adj‚ÇÅ adj‚ÇÇ).symm Œ±) :=
  ‚ü®‚ü®(conjugateEquiv adj‚ÇÇ adj‚ÇÅ).symm (inv Œ±),
      ‚ü®conjugateEquiv_symm_comm _ _ (by simp), conjugateEquiv_symm_comm _ _ (by simp)‚ü©‚ü©‚ü©

/-- If `Œ±` is a natural transformation between left adjoints whose conjugate natural transformation
is an isomorphism, then `Œ±` is an isomorphism. The converse is given in `Conjugate_iso`.
-/
theorem conjugateEquiv_of_iso (Œ± : L‚ÇÇ ‚ü∂ L‚ÇÅ) [IsIso (conjugateEquiv adj‚ÇÅ adj‚ÇÇ Œ±)] :
    IsIso Œ± := by
  suffices IsIso ((conjugateEquiv adj‚ÇÅ adj‚ÇÇ).symm (conjugateEquiv adj‚ÇÅ adj‚ÇÇ Œ±))
    by simpa using this
  infer_instance

/--
If `Œ±` is a natural transformation between right adjoints whose conjugate natural transformation is
an isomorphism, then `Œ±` is an isomorphism. The converse is given in `conjugateEquiv_symm_iso`.
-/
theorem conjugateEquiv_symm_of_iso (Œ± : R‚ÇÅ ‚ü∂ R‚ÇÇ)
    [IsIso ((conjugateEquiv adj‚ÇÅ adj‚ÇÇ).symm Œ±)] : IsIso Œ± := by
  suffices IsIso ((conjugateEquiv adj‚ÇÅ adj‚ÇÇ) ((conjugateEquiv adj‚ÇÅ adj‚ÇÇ).symm Œ±))
    by simpa using this
  infer_instance

/-- Thus conjugation defines an equivalence between natural isomorphisms. -/
@[simps]
def conjugateIsoEquiv : (L‚ÇÇ ‚âÖ L‚ÇÅ) ‚âÉ (R‚ÇÅ ‚âÖ R‚ÇÇ) where
  toFun Œ± := {
    hom := conjugateEquiv adj‚ÇÅ adj‚ÇÇ Œ±.hom
    inv := conjugateEquiv adj‚ÇÇ adj‚ÇÅ Œ±.inv
  }
  invFun Œ≤ := {
    hom := (conjugateEquiv adj‚ÇÅ adj‚ÇÇ).symm Œ≤.hom
    inv := (conjugateEquiv adj‚ÇÇ adj‚ÇÅ).symm Œ≤.inv
  }
  left_inv := by cat_disch
  right_inv := by cat_disch

end ConjugateIsomorphism

variable {A : Type u‚ÇÅ} {B : Type u‚ÇÇ} {C : Type u‚ÇÉ} {D : Type u‚ÇÑ}
variable [Category.{v‚ÇÅ} A] [Category.{v‚ÇÇ} B] [Category.{v‚ÇÉ} C] [Category.{v‚ÇÑ} D]

section IteratedmateEquiv

variable {F‚ÇÅ : A ‚•§ C} {U‚ÇÅ : C ‚•§ A} {F‚ÇÇ : B ‚•§ D} {U‚ÇÇ : D ‚•§ B}
variable {L‚ÇÅ : A ‚•§ B} {R‚ÇÅ : B ‚•§ A} {L‚ÇÇ : C ‚•§ D} {R‚ÇÇ : D ‚•§ C}
variable (adj‚ÇÅ : L‚ÇÅ ‚ä£ R‚ÇÅ) (adj‚ÇÇ : L‚ÇÇ ‚ä£ R‚ÇÇ) (adj‚ÇÉ : F‚ÇÅ ‚ä£ U‚ÇÅ) (adj‚ÇÑ : F‚ÇÇ ‚ä£ U‚ÇÇ)

/-- When all four functors in a square are left adjoints, the mates operation can be iterated:

```
         L‚ÇÅ                  R‚ÇÅ                  R‚ÇÅ
      C --‚Üí D             C ‚Üê-- D             C ‚Üê-- D
   F‚ÇÅ ‚Üì  ‚Üó  ‚Üì  F‚ÇÇ      F‚ÇÅ ‚Üì  ‚Üò  ‚Üì F‚ÇÇ       U‚ÇÅ ‚Üë  ‚Üô  ‚Üë U‚ÇÇ
      E --‚Üí F             E ‚Üê-- F             E ‚Üê-- F
         L‚ÇÇ                  R‚ÇÇ                  R‚ÇÇ
```

In this case the iterated mate equals the conjugate of the original transformation and is thus an
isomorphism if and only if the original transformation is. This explains why some Beck-Chevalley
natural transformations are natural isomorphisms.
-/
theorem iterated_mateEquiv_conjugateEquiv (Œ± : TwoSquare F‚ÇÅ L‚ÇÅ L‚ÇÇ F‚ÇÇ) :
    (mateEquiv adj‚ÇÑ adj‚ÇÉ (mateEquiv adj‚ÇÅ adj‚ÇÇ Œ±)).natTrans =
      conjugateEquiv (adj‚ÇÅ.comp adj‚ÇÑ) (adj‚ÇÉ.comp adj‚ÇÇ) Œ± := by
  ext d
  unfold conjugateEquiv mateEquiv Adjunction.comp
  simp

theorem iterated_mateEquiv_conjugateEquiv_symm (Œ± : TwoSquare U‚ÇÇ R‚ÇÇ R‚ÇÅ U‚ÇÅ) :
    (mateEquiv adj‚ÇÅ adj‚ÇÇ).symm ((mateEquiv adj‚ÇÑ adj‚ÇÉ).symm Œ±) =
      (conjugateEquiv (adj‚ÇÅ.comp adj‚ÇÑ) (adj‚ÇÉ.comp adj‚ÇÇ)).symm.trans
        (equivNatTrans _ _ _ _).symm Œ± := by
  ext
  simp

end IteratedmateEquiv

variable {G : A ‚•§ C} {H : B ‚•§ D}

/-- The mates equivalence commutes with this composition, essentially by `mateEquiv_vcomp`. -/
theorem mateEquiv_conjugateEquiv_vcomp {L‚ÇÅ : A ‚•§ B} {R‚ÇÅ : B ‚•§ A} {L‚ÇÇ : C ‚•§ D} {R‚ÇÇ : D ‚•§ C}
    {L‚ÇÉ : C ‚•§ D} {R‚ÇÉ : D ‚•§ C}
    (adj‚ÇÅ : L‚ÇÅ ‚ä£ R‚ÇÅ) (adj‚ÇÇ : L‚ÇÇ ‚ä£ R‚ÇÇ) (adj‚ÇÉ : L‚ÇÉ ‚ä£ R‚ÇÉ) (Œ± : TwoSquare G L‚ÇÅ L‚ÇÇ H) (Œ≤ : L‚ÇÉ ‚ü∂ L‚ÇÇ) :
    (mateEquiv adj‚ÇÅ adj‚ÇÉ) (Œ±.whiskerRight Œ≤) =
      (mateEquiv adj‚ÇÅ adj‚ÇÇ Œ±).whiskerBottom (conjugateEquiv adj‚ÇÇ adj‚ÇÉ Œ≤) := by
  ext b
  have vcomp := mateEquiv_vcomp adj‚ÇÅ adj‚ÇÇ adj‚ÇÉ Œ± (L‚ÇÉ.leftUnitor.hom ‚â´ Œ≤ ‚â´ L‚ÇÇ.rightUnitor.inv)
  unfold vComp hComp at vcomp
  unfold TwoSquare.whiskerRight TwoSquare.whiskerBottom conjugateEquiv
  have vcompb := congr_app vcomp b
  simp only [comp_obj, id_obj, whiskerLeft_comp, assoc, mateEquiv_apply, whiskerLeft_twice,
    Iso.hom_inv_id_assoc, whiskerRight_comp, comp_app, Functor.whiskerLeft_app,
    Functor.whiskerRight_app, associator_hom_app, map_id, associator_inv_app, leftUnitor_hom_app,
    rightUnitor_inv_app, Functor.id_map, Functor.comp_map, id_comp, whiskerRight_twice,
    comp_id] at vcompb
  simpa [mateEquiv]

/-- The mates equivalence commutes with this composition, essentially by `mateEquiv_vcomp`. -/
theorem conjugateEquiv_mateEquiv_vcomp {L‚ÇÅ : A ‚•§ B} {R‚ÇÅ : B ‚•§ A} {L‚ÇÇ : A ‚•§ B} {R‚ÇÇ : B ‚•§ A}
    {L‚ÇÉ : C ‚•§ D} {R‚ÇÉ : D ‚•§ C}
    (adj‚ÇÅ : L‚ÇÅ ‚ä£ R‚ÇÅ) (adj‚ÇÇ : L‚ÇÇ ‚ä£ R‚ÇÇ) (adj‚ÇÉ : L‚ÇÉ ‚ä£ R‚ÇÉ) (Œ± : L‚ÇÇ ‚ü∂ L‚ÇÅ) (Œ≤ : TwoSquare G L‚ÇÇ L‚ÇÉ H) :
    (mateEquiv adj‚ÇÅ adj‚ÇÉ) (Œ≤.whiskerLeft Œ±) =
      (mateEquiv adj‚ÇÇ adj‚ÇÉ Œ≤).whiskerTop (conjugateEquiv adj‚ÇÅ adj‚ÇÇ Œ±) := by
  ext b
  have vcomp := mateEquiv_vcomp adj‚ÇÅ adj‚ÇÇ adj‚ÇÉ (L‚ÇÇ.leftUnitor.hom ‚â´ Œ± ‚â´ L‚ÇÅ.rightUnitor.inv) Œ≤
  unfold vComp hComp at vcomp
  unfold TwoSquare.whiskerLeft TwoSquare.whiskerTop conjugateEquiv
  have vcompb := congr_app vcomp b
  simp only [comp_obj, id_obj, whiskerRight_comp, assoc, mateEquiv_apply, whiskerLeft_comp,
    whiskerLeft_twice, comp_app, Functor.whiskerLeft_app, Functor.whiskerRight_app,
    associator_hom_app, map_id, associator_inv_app, leftUnitor_hom_app, rightUnitor_inv_app,
    Functor.comp_map, Functor.id_map, id_comp, whiskerRight_twice, Iso.inv_hom_id_assoc,
    comp_id] at vcompb
  simpa [mateEquiv]

lemma conjugateEquiv_associator_hom
    {L‚ÇÄ‚ÇÅ : A ‚•§ B} {R‚ÇÅ‚ÇÄ : B ‚•§ A} {L‚ÇÅ‚ÇÇ : B ‚•§ C} {R‚ÇÇ‚ÇÅ : C ‚•§ B}
    {L‚ÇÇ‚ÇÉ : C ‚•§ D} {R‚ÇÉ‚ÇÇ : D ‚•§ C} (adj‚ÇÄ‚ÇÅ : L‚ÇÄ‚ÇÅ ‚ä£ R‚ÇÅ‚ÇÄ) (adj‚ÇÅ‚ÇÇ : L‚ÇÅ‚ÇÇ ‚ä£ R‚ÇÇ‚ÇÅ)
    (adj‚ÇÇ‚ÇÉ : L‚ÇÇ‚ÇÉ ‚ä£ R‚ÇÉ‚ÇÇ) :
    conjugateEquiv (adj‚ÇÄ‚ÇÅ.comp (adj‚ÇÅ‚ÇÇ.comp adj‚ÇÇ‚ÇÉ)) ((adj‚ÇÄ‚ÇÅ.comp adj‚ÇÅ‚ÇÇ).comp adj‚ÇÇ‚ÇÉ)
      (associator _ _ _).hom = (associator _ _ _).hom := by
  ext X
  simp only [comp_obj, conjugateEquiv_apply_app, Adjunction.comp_unit_app, id_obj,
    Functor.comp_map, Category.assoc, ‚Üê map_comp, associator_hom_app, map_id,
    Adjunction.comp_counit_app, Category.id_comp]
  simp

lemma conjugateEquiv_leftUnitor_hom
    {L : A ‚•§ B} {R : B ‚•§ A} (adj : L ‚ä£ R) :
    conjugateEquiv adj (id.comp adj) (leftUnitor L).hom =
      (rightUnitor R).inv := by
  cat_disch

lemma conjugateEquiv_rightUnitor_hom
    {L : A ‚•§ B} {R : B ‚•§ A} (adj : L ‚ä£ R) :
    conjugateEquiv adj (adj.comp id) (rightUnitor L).hom =
      (leftUnitor R).inv := by
  cat_disch

lemma conjugateEquiv_whiskerLeft
    {L‚ÇÅ L‚ÇÇ : B ‚•§ C} {R‚ÇÅ R‚ÇÇ : C ‚•§ B} {L : A ‚•§ B} {R : B ‚•§ A}
    (adj‚ÇÅ : L‚ÇÅ ‚ä£ R‚ÇÅ) (adj‚ÇÇ : L‚ÇÇ ‚ä£ R‚ÇÇ) (adj : L ‚ä£ R) (œÑ : L‚ÇÇ ‚ü∂ L‚ÇÅ) :
    conjugateEquiv (adj.comp adj‚ÇÅ) (adj.comp adj‚ÇÇ) (whiskerLeft L œÑ) =
      whiskerRight (conjugateEquiv adj‚ÇÅ adj‚ÇÇ œÑ) R := by
  ext X
  have h‚ÇÅ := congr_map (R‚ÇÇ ‚ãô R) (œÑ.naturality (adj.counit.app (R‚ÇÅ.obj X)))
  have h‚ÇÇ := congr_map R (adj‚ÇÇ.unit_naturality (adj.counit.app (R‚ÇÅ.obj X)))
  simp only [comp_obj, id_obj, Functor.map_comp] at h‚ÇÅ h‚ÇÇ
  simp [‚Üê reassoc_of% h‚ÇÅ, reassoc_of% h‚ÇÇ]

lemma conjugateEquiv_whiskerRight
    {L‚ÇÅ L‚ÇÇ : A ‚•§ B} {R‚ÇÅ R‚ÇÇ : B ‚•§ A} {L : B ‚•§ C} {R : C ‚•§ B}
    (adj‚ÇÅ : L‚ÇÅ ‚ä£ R‚ÇÅ) (adj‚ÇÇ : L‚ÇÇ ‚ä£ R‚ÇÇ) (adj : L ‚ä£ R) (œÑ : L‚ÇÇ ‚ü∂ L‚ÇÅ) :
    conjugateEquiv (adj‚ÇÅ.comp adj) (adj‚ÇÇ.comp adj) (whiskerRight œÑ L) =
      whiskerLeft R (conjugateEquiv adj‚ÇÅ adj‚ÇÇ œÑ) := by
  ext X
  simp only [comp_obj, conjugateEquiv_apply_app, comp_unit_app, id_obj, Functor.whiskerRight_app,
    Functor.comp_map, comp_counit_app, ‚Üê map_comp, assoc, Functor.whiskerLeft_app]
  simp

end CategoryTheory
