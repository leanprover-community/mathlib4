/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Reid Barton, Johan Commelin, Bhavik Mehta
-/
import Mathlib.CategoryTheory.Equivalence

#align_import category_theory.adjunction.basic from "leanprover-community/mathlib"@"d101e93197bb5f6ea89bd7ba386b7f7dff1f3903"

/-!
# Adjunctions between functors

`F ‚ä£ G` represents the data of an adjunction between two functors
`F : C ‚•§ D` and `G : D ‚•§ C`. `F` is the left adjoint and `G` is the right adjoint.

We provide various useful constructors:
* `mkOfHomEquiv`
* `mkOfUnitCounit`
* `leftAdjointOfEquiv` / `rightAdjointOfEquiv`
  construct a left/right adjoint of a given functor given the action on objects and
  the relevant equivalence of morphism spaces.
* `adjunctionOfEquivLeft` / `adjunctionOfEquivRight` witness that these constructions
  give adjunctions.

There are also typeclasses `IsLeftAdjoint` / `IsRightAdjoint`, which asserts the
existence of a adjoint functor. Given `[F.IsLeftAdjoint]`, a chosen right
adjoint can be obtained as `F.rightAdjoint`.

`Adjunction.comp` composes adjunctions.

`toEquivalence` upgrades an adjunction to an equivalence,
given witnesses that the unit and counit are pointwise isomorphisms.
Conversely `Equivalence.toAdjunction` recovers the underlying adjunction from an equivalence.
-/


namespace CategoryTheory

open Category

-- declare the `v`'s first; see `CategoryTheory.Category` for an explanation
universe v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

-- Porting Note: `elab_without_expected_type` cannot be a local attribute
-- attribute [local elab_without_expected_type] whiskerLeft whiskerRight

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]

/-- `F ‚ä£ G` represents the data of an adjunction between two functors
`F : C ‚•§ D` and `G : D ‚•§ C`. `F` is the left adjoint and `G` is the right adjoint.

To construct an `adjunction` between two functors, it's often easier to instead use the
constructors `mkOfHomEquiv` or `mkOfUnitCounit`. To construct a left adjoint,
there are also constructors `leftAdjointOfEquiv` and `adjunctionOfEquivLeft` (as
well as their duals) which can be simpler in practice.

Uniqueness of adjoints is shown in `CategoryTheory.Adjunction.Opposites`.

See <https://stacks.math.columbia.edu/tag/0037>.
-/
structure Adjunction (F : C ‚•§ D) (G : D ‚•§ C) where
  /-- The equivalence between `Hom (F X) Y` and `Hom X (G Y)` coming from an adjunction -/
  homEquiv : ‚àÄ X Y, (F.obj X ‚ü∂ Y) ‚âÉ (X ‚ü∂ G.obj Y)
  /-- The unit of an adjunction -/
  unit : ùü≠ C ‚ü∂ F.comp G
  /-- The counit of an adjunction -/
  counit : G.comp F ‚ü∂ ùü≠ D
  -- Porting note: It's strange that this `Prop` is being flagged by the `docBlame` linter
  /-- Naturality of the unit of an adjunction -/
  homEquiv_unit : ‚àÄ {X Y f}, (homEquiv X Y) f = (unit : _ ‚ü∂ _).app X ‚â´ G.map f := by aesop_cat
  -- Porting note: It's strange that this `Prop` is being flagged by the `docBlame` linter
  /-- Naturality of the counit of an adjunction -/
  homEquiv_counit : ‚àÄ {X Y g}, (homEquiv X Y).symm g = F.map g ‚â´ counit.app Y := by aesop_cat
#align category_theory.adjunction CategoryTheory.Adjunction
#align category_theory.adjunction.hom_equiv CategoryTheory.Adjunction.homEquiv
#align category_theory.adjunction.hom_equiv_unit CategoryTheory.Adjunction.homEquiv_unit
#align category_theory.adjunction.hom_equiv_unit' CategoryTheory.Adjunction.homEquiv_unit
#align category_theory.adjunction.hom_equiv_counit CategoryTheory.Adjunction.homEquiv_counit
#align category_theory.adjunction.hom_equiv_counit' CategoryTheory.Adjunction.homEquiv_counit

/-- The notation `F ‚ä£ G` stands for `Adjunction F G` representing that `F` is left adjoint to `G` -/
infixl:15 " ‚ä£ " => Adjunction

namespace Functor

/-- A class asserting the existence of a right adjoint. -/
class IsLeftAdjoint (left : C ‚•§ D) : Prop where
  exists_rightAdjoint : ‚àÉ (right : D ‚•§ C), Nonempty (left ‚ä£ right)
#align category_theory.is_left_adjoint CategoryTheory.Functor.IsLeftAdjoint

/-- A class asserting the existence of a left adjoint. -/
class IsRightAdjoint (right : D ‚•§ C) : Prop where
  exists_leftAdjoint : ‚àÉ (left : C ‚•§ D), Nonempty (left ‚ä£ right)
#align category_theory.is_right_adjoint CategoryTheory.Functor.IsRightAdjoint

/-- A chosen left adjoint to a functor that is a right adjoint. -/
noncomputable def leftAdjoint (R : D ‚•§ C) [IsRightAdjoint R] : C ‚•§ D :=
  (IsRightAdjoint.exists_leftAdjoint (right := R)).choose
#align category_theory.left_adjoint CategoryTheory.Functor.leftAdjoint

/-- A chosen right adjoint to a functor that is a left adjoint. -/
noncomputable def rightAdjoint (L : C ‚•§ D) [IsLeftAdjoint L] : D ‚•§ C :=
  (IsLeftAdjoint.exists_rightAdjoint (left := L)).choose
#align category_theory.right_adjoint CategoryTheory.Functor.rightAdjoint

end Functor

/-- The adjunction associated to a functor known to be a left adjoint. -/
noncomputable def Adjunction.ofIsLeftAdjoint (left : C ‚•§ D) [left.IsLeftAdjoint] :
    left ‚ä£ left.rightAdjoint :=
  Functor.IsLeftAdjoint.exists_rightAdjoint.choose_spec.some
#align category_theory.adjunction.of_left_adjoint CategoryTheory.Adjunction.ofIsLeftAdjoint

/-- The adjunction associated to a functor known to be a right adjoint. -/
noncomputable def Adjunction.ofIsRightAdjoint (right : C ‚•§ D) [right.IsRightAdjoint] :
    right.leftAdjoint ‚ä£ right :=
  Functor.IsRightAdjoint.exists_leftAdjoint.choose_spec.some
#align category_theory.adjunction.of_right_adjoint CategoryTheory.Adjunction.ofIsRightAdjoint

namespace Adjunction

-- Porting note: Workaround not needed in Lean 4
-- restate_axiom homEquiv_unit'

-- restate_axiom homEquiv_counit'

attribute [simp] homEquiv_unit homEquiv_counit

section

variable {F : C ‚•§ D} {G : D ‚•§ C} (adj : F ‚ä£ G)

lemma isLeftAdjoint : F.IsLeftAdjoint := ‚ü®_, ‚ü®adj‚ü©‚ü©

lemma isRightAdjoint : G.IsRightAdjoint := ‚ü®_, ‚ü®adj‚ü©‚ü©

instance (R : D ‚•§ C) [R.IsRightAdjoint] : R.leftAdjoint.IsLeftAdjoint :=
  (ofIsRightAdjoint R).isLeftAdjoint

instance (L : C ‚•§ D) [L.IsLeftAdjoint] : L.rightAdjoint.IsRightAdjoint :=
  (ofIsLeftAdjoint L).isRightAdjoint

variable {X' X : C} {Y Y' : D}

theorem homEquiv_id (X : C) : adj.homEquiv X _ (ùüô _) = adj.unit.app X := by simp
#align category_theory.adjunction.hom_equiv_id CategoryTheory.Adjunction.homEquiv_id

theorem homEquiv_symm_id (X : D) : (adj.homEquiv _ X).symm (ùüô _) = adj.counit.app X := by simp
#align category_theory.adjunction.hom_equiv_symm_id CategoryTheory.Adjunction.homEquiv_symm_id

/-
Porting note: `nolint simpNF` as the linter was complaining that this was provable using `simp`
but it is in fact not. Also the `docBlame` linter expects a docstring even though this is `Prop`
valued
-/
@[simp, nolint simpNF]
theorem homEquiv_naturality_left_symm (f : X' ‚ü∂ X) (g : X ‚ü∂ G.obj Y) :
    (adj.homEquiv X' Y).symm (f ‚â´ g) = F.map f ‚â´ (adj.homEquiv X Y).symm g := by
  rw [homEquiv_counit, F.map_comp, assoc, adj.homEquiv_counit.symm]
#align category_theory.adjunction.hom_equiv_naturality_left_symm CategoryTheory.Adjunction.homEquiv_naturality_left_symm

-- Porting note: Same as above
@[simp, nolint simpNF]
theorem homEquiv_naturality_left (f : X' ‚ü∂ X) (g : F.obj X ‚ü∂ Y) :
    (adj.homEquiv X' Y) (F.map f ‚â´ g) = f ‚â´ (adj.homEquiv X Y) g := by
  rw [‚Üê Equiv.eq_symm_apply]
  simp only [Equiv.symm_apply_apply,eq_self_iff_true,homEquiv_naturality_left_symm]
#align category_theory.adjunction.hom_equiv_naturality_left CategoryTheory.Adjunction.homEquiv_naturality_left

-- Porting note: Same as above
@[simp, nolint simpNF]
theorem homEquiv_naturality_right (f : F.obj X ‚ü∂ Y) (g : Y ‚ü∂ Y') :
    (adj.homEquiv X Y') (f ‚â´ g) = (adj.homEquiv X Y) f ‚â´ G.map g := by
  rw [homEquiv_unit, G.map_comp, ‚Üê assoc, ‚Üê homEquiv_unit]
#align category_theory.adjunction.hom_equiv_naturality_right CategoryTheory.Adjunction.homEquiv_naturality_right

-- Porting note: Same as above
@[simp, nolint simpNF]
theorem homEquiv_naturality_right_symm (f : X ‚ü∂ G.obj Y) (g : Y ‚ü∂ Y') :
    (adj.homEquiv X Y').symm (f ‚â´ G.map g) = (adj.homEquiv X Y).symm f ‚â´ g := by
  rw [Equiv.symm_apply_eq]
  simp only [homEquiv_naturality_right,eq_self_iff_true,Equiv.apply_symm_apply]
#align category_theory.adjunction.hom_equiv_naturality_right_symm CategoryTheory.Adjunction.homEquiv_naturality_right_symm

@[simp]
theorem left_triangle : whiskerRight adj.unit F ‚â´ whiskerLeft F adj.counit = ùüô _ := by
  ext; dsimp
  erw [‚Üê adj.homEquiv_counit, Equiv.symm_apply_eq, adj.homEquiv_unit]
  simp
#align category_theory.adjunction.left_triangle CategoryTheory.Adjunction.left_triangle

@[simp]
theorem right_triangle : whiskerLeft G adj.unit ‚â´ whiskerRight adj.counit G = ùüô _ := by
  ext; dsimp
  erw [‚Üê adj.homEquiv_unit, ‚Üê Equiv.eq_symm_apply, adj.homEquiv_counit]
  simp
#align category_theory.adjunction.right_triangle CategoryTheory.Adjunction.right_triangle

variable (X Y)

@[reassoc (attr := simp)]
theorem left_triangle_components :
    F.map (adj.unit.app X) ‚â´ adj.counit.app (F.obj X) = ùüô (F.obj X) :=
  congr_arg (fun t : NatTrans _ (ùü≠ C ‚ãô F) => t.app X) adj.left_triangle
#align category_theory.adjunction.left_triangle_components CategoryTheory.Adjunction.left_triangle_components

@[reassoc (attr := simp)]
theorem right_triangle_components :
    adj.unit.app (G.obj Y) ‚â´ G.map (adj.counit.app Y) = ùüô (G.obj Y) :=
  congr_arg (fun t : NatTrans _ (G ‚ãô ùü≠ C) => t.app Y) adj.right_triangle
#align category_theory.adjunction.right_triangle_components CategoryTheory.Adjunction.right_triangle_components

variable {X Y}

@[reassoc (attr := simp)]
theorem counit_naturality {X Y : D} (f : X ‚ü∂ Y) :
    F.map (G.map f) ‚â´ adj.counit.app Y = adj.counit.app X ‚â´ f :=
  adj.counit.naturality f
#align category_theory.adjunction.counit_naturality CategoryTheory.Adjunction.counit_naturality

@[reassoc (attr := simp)]
theorem unit_naturality {X Y : C} (f : X ‚ü∂ Y) :
    adj.unit.app X ‚â´ G.map (F.map f) = f ‚â´ adj.unit.app Y :=
  (adj.unit.naturality f).symm
#align category_theory.adjunction.unit_naturality CategoryTheory.Adjunction.unit_naturality

theorem homEquiv_apply_eq {A : C} {B : D} (f : F.obj A ‚ü∂ B) (g : A ‚ü∂ G.obj B) :
    adj.homEquiv A B f = g ‚Üî f = (adj.homEquiv A B).symm g :=
  ‚ü®fun h => by
    cases h
    simp, fun h => by
    cases h
    simp‚ü©
#align category_theory.adjunction.hom_equiv_apply_eq CategoryTheory.Adjunction.homEquiv_apply_eq

theorem eq_homEquiv_apply {A : C} {B : D} (f : F.obj A ‚ü∂ B) (g : A ‚ü∂ G.obj B) :
    g = adj.homEquiv A B f ‚Üî (adj.homEquiv A B).symm g = f :=
  ‚ü®fun h => by
    cases h
    simp, fun h => by
    cases h
    simp‚ü©
#align category_theory.adjunction.eq_hom_equiv_apply CategoryTheory.Adjunction.eq_homEquiv_apply

end

end Adjunction

namespace Adjunction

/-- This is an auxiliary data structure useful for constructing adjunctions.
See `Adjunction.mkOfHomEquiv`.
This structure won't typically be used anywhere else.
-/
-- Porting note(#5171): `has_nonempty_instance` linter not ported yet
-- @[nolint has_nonempty_instance]
structure CoreHomEquiv (F : C ‚•§ D) (G : D ‚•§ C) where
  /-- The equivalence between `Hom (F X) Y` and `Hom X (G Y)` -/
  homEquiv : ‚àÄ X Y, (F.obj X ‚ü∂ Y) ‚âÉ (X ‚ü∂ G.obj Y)
  /-- The property that describes how `homEquiv.symm` transforms compositions `X' ‚ü∂ X ‚ü∂ G Y` -/
  homEquiv_naturality_left_symm :
    ‚àÄ {X' X Y} (f : X' ‚ü∂ X) (g : X ‚ü∂ G.obj Y),
      (homEquiv X' Y).symm (f ‚â´ g) = F.map f ‚â´ (homEquiv X Y).symm g := by
    aesop_cat
  /-- The property that describes how `homEquiv` transforms compositions `F X ‚ü∂ Y ‚ü∂ Y'` -/
  homEquiv_naturality_right :
    ‚àÄ {X Y Y'} (f : F.obj X ‚ü∂ Y) (g : Y ‚ü∂ Y'),
      (homEquiv X Y') (f ‚â´ g) = (homEquiv X Y) f ‚â´ G.map g := by
    aesop_cat
#align category_theory.adjunction.core_hom_equiv CategoryTheory.Adjunction.CoreHomEquiv
#align category_theory.adjunction.core_hom_equiv.hom_equiv CategoryTheory.Adjunction.CoreHomEquiv.homEquiv
#align category_theory.adjunction.core_hom_equiv.hom_equiv' CategoryTheory.Adjunction.CoreHomEquiv.homEquiv
#align category_theory.adjunction.core_hom_equiv.hom_equiv_naturality_right CategoryTheory.Adjunction.CoreHomEquiv.homEquiv_naturality_right
#align category_theory.adjunction.core_hom_equiv.hom_equiv_naturality_right' CategoryTheory.Adjunction.CoreHomEquiv.homEquiv_naturality_right
#align category_theory.adjunction.core_hom_equiv.hom_equiv_naturality_left_symm CategoryTheory.Adjunction.CoreHomEquiv.homEquiv_naturality_left_symm
#align category_theory.adjunction.core_hom_equiv.hom_equiv_naturality_left_symm' CategoryTheory.Adjunction.CoreHomEquiv.homEquiv_naturality_left_symm

namespace CoreHomEquiv

-- Porting note: Workaround not needed in Lean 4.
-- restate_axiom homEquiv_naturality_left_symm'

-- restate_axiom homEquiv_naturality_right'

attribute [simp] homEquiv_naturality_left_symm homEquiv_naturality_right

variable {F : C ‚•§ D} {G : D ‚•§ C} (adj : CoreHomEquiv F G) {X' X : C} {Y Y' : D}

@[simp]
theorem homEquiv_naturality_left_aux (f : X' ‚ü∂ X) (g : F.obj X ‚ü∂ Y) :
    (adj.homEquiv X' (F.obj X)) (F.map f) ‚â´ G.map g = f ‚â´ (adj.homEquiv X Y) g := by
  rw [‚Üê homEquiv_naturality_right, ‚Üê Equiv.eq_symm_apply]; simp

-- @[simp] -- Porting note: LHS simplifies, added aux lemma above
theorem homEquiv_naturality_left (f : X' ‚ü∂ X) (g : F.obj X ‚ü∂ Y) :
    (adj.homEquiv X' Y) (F.map f ‚â´ g) = f ‚â´ (adj.homEquiv X Y) g := by
  rw [‚Üê Equiv.eq_symm_apply]; simp
#align category_theory.adjunction.core_hom_equiv.hom_equiv_naturality_left CategoryTheory.Adjunction.CoreHomEquiv.homEquiv_naturality_left

@[simp]
theorem homEquiv_naturality_right_symm_aux (f : X ‚ü∂ G.obj Y) (g : Y ‚ü∂ Y') :
    F.map f ‚â´ (adj.homEquiv (G.obj Y) Y').symm (G.map g) = (adj.homEquiv X Y).symm f ‚â´ g := by
  rw [‚Üê homEquiv_naturality_left_symm, Equiv.symm_apply_eq]; simp

-- @[simp] -- Porting note: LHS simplifies, added aux lemma above
theorem homEquiv_naturality_right_symm (f : X ‚ü∂ G.obj Y) (g : Y ‚ü∂ Y') :
    (adj.homEquiv X Y').symm (f ‚â´ G.map g) = (adj.homEquiv X Y).symm f ‚â´ g := by
  rw [Equiv.symm_apply_eq]; simp
#align category_theory.adjunction.core_hom_equiv.hom_equiv_naturality_right_symm CategoryTheory.Adjunction.CoreHomEquiv.homEquiv_naturality_right_symm

end CoreHomEquiv

/-- This is an auxiliary data structure useful for constructing adjunctions.
See `Adjunction.mkOfUnitCounit`.
This structure won't typically be used anywhere else.
-/
-- Porting note(#5171): `has_nonempty_instance` linter not ported yet
-- @[nolint has_nonempty_instance]
structure CoreUnitCounit (F : C ‚•§ D) (G : D ‚•§ C) where
  /-- The unit of an adjunction between `F` and `G` -/
  unit : ùü≠ C ‚ü∂ F.comp G
  /-- The counit of an adjunction between `F` and `G`s -/
  counit : G.comp F ‚ü∂ ùü≠ D
  /-- Equality of the composition of the unit, associator, and counit with the identity
  `F ‚ü∂ (F G) F ‚ü∂ F (G F) ‚ü∂ F = NatTrans.id F` -/
  left_triangle :
    whiskerRight unit F ‚â´ (Functor.associator F G F).hom ‚â´ whiskerLeft F counit =
      NatTrans.id (ùü≠ C ‚ãô F) := by
    aesop_cat
  /-- Equality of the composition of the unit, associator, and counit with the identity
  `G ‚ü∂ G (F G) ‚ü∂ (F G) F ‚ü∂ G = NatTrans.id G` -/
  right_triangle :
    whiskerLeft G unit ‚â´ (Functor.associator G F G).inv ‚â´ whiskerRight counit G =
      NatTrans.id (G ‚ãô ùü≠ C) := by
    aesop_cat
#align category_theory.adjunction.core_unit_counit CategoryTheory.Adjunction.CoreUnitCounit
#align category_theory.adjunction.core_unit_counit.left_triangle' CategoryTheory.Adjunction.CoreUnitCounit.left_triangle
#align category_theory.adjunction.core_unit_counit.left_triangle CategoryTheory.Adjunction.CoreUnitCounit.left_triangle
#align category_theory.adjunction.core_unit_counit.right_triangle' CategoryTheory.Adjunction.CoreUnitCounit.right_triangle
#align category_theory.adjunction.core_unit_counit.right_triangle CategoryTheory.Adjunction.CoreUnitCounit.right_triangle

namespace CoreUnitCounit

attribute [simp] left_triangle right_triangle

end CoreUnitCounit

variable {F : C ‚•§ D} {G : D ‚•§ C}

/-- Construct an adjunction between `F` and `G` out of a natural bijection between each
`F.obj X ‚ü∂ Y` and `X ‚ü∂ G.obj Y`. -/
@[simps]
def mkOfHomEquiv (adj : CoreHomEquiv F G) : F ‚ä£ G :=
  -- See note [dsimp, simp].
  { adj with
    unit :=
      { app := fun X => (adj.homEquiv X (F.obj X)) (ùüô (F.obj X))
        naturality := by
          intros
          erw [‚Üê adj.homEquiv_naturality_left, ‚Üê adj.homEquiv_naturality_right]
          dsimp; simp }
    counit :=
      { app := fun Y => (adj.homEquiv _ _).invFun (ùüô (G.obj Y))
        naturality := by
          intros
          erw [‚Üê adj.homEquiv_naturality_left_symm, ‚Üê adj.homEquiv_naturality_right_symm]
          dsimp; simp }
    homEquiv_unit := @fun X Y f => by erw [‚Üê adj.homEquiv_naturality_right]; simp
    homEquiv_counit := @fun X Y f => by erw [‚Üê adj.homEquiv_naturality_left_symm]; simp
  }
#align category_theory.adjunction.mk_of_hom_equiv CategoryTheory.Adjunction.mkOfHomEquiv

/-- Construct an adjunction between functors `F` and `G` given a unit and counit for the adjunction
satisfying the triangle identities. -/

@[simps!]
def mkOfUnitCounit (adj : CoreUnitCounit F G) : F ‚ä£ G :=
  { adj with
    homEquiv := fun X Y =>
      { toFun := fun f => adj.unit.app X ‚â´ G.map f
        invFun := fun g => F.map g ‚â´ adj.counit.app Y
        left_inv := fun f => by
          change F.map (_ ‚â´ _) ‚â´ _ = _
          rw [F.map_comp, assoc, ‚Üê Functor.comp_map, adj.counit.naturality, ‚Üê assoc]
          convert id_comp f
          have t := congrArg (fun (s : NatTrans (ùü≠ C ‚ãô F) (F ‚ãô ùü≠ D)) => s.app X) adj.left_triangle
          dsimp at t
          simp only [id_comp] at t
          exact t
        right_inv := fun g => by
          change _ ‚â´ G.map (_ ‚â´ _) = _
          rw [G.map_comp, ‚Üê assoc, ‚Üê Functor.comp_map, ‚Üê adj.unit.naturality, assoc]
          convert comp_id g
          have t := congrArg (fun t : NatTrans (G ‚ãô ùü≠ C) (ùü≠ D ‚ãô G) => t.app Y) adj.right_triangle
          dsimp at t
          simp only [id_comp] at t
          exact t } }
#align category_theory.adjunction.mk_of_unit_counit CategoryTheory.Adjunction.mkOfUnitCounit

/- Porting note: simpNF linter claims these are solved by simp but that
is not true -/
attribute [nolint simpNF] CategoryTheory.Adjunction.mkOfUnitCounit_homEquiv_symm_apply
attribute [nolint simpNF] CategoryTheory.Adjunction.mkOfUnitCounit_homEquiv_apply

/-- The adjunction between the identity functor on a category and itself. -/
def id : ùü≠ C ‚ä£ ùü≠ C where
  homEquiv X Y := Equiv.refl _
  unit := ùüô _
  counit := ùüô _
#align category_theory.adjunction.id CategoryTheory.Adjunction.id

-- Satisfy the inhabited linter.
instance : Inhabited (Adjunction (ùü≠ C) (ùü≠ C)) :=
  ‚ü®id‚ü©

/-- If F and G are naturally isomorphic functors, establish an equivalence of hom-sets. -/
@[simps]
def equivHomsetLeftOfNatIso {F F' : C ‚•§ D} (iso : F ‚âÖ F') {X : C} {Y : D} :
    (F.obj X ‚ü∂ Y) ‚âÉ (F'.obj X ‚ü∂ Y) where
  toFun f := iso.inv.app _ ‚â´ f
  invFun g := iso.hom.app _ ‚â´ g
  left_inv f := by simp
  right_inv g := by simp
#align category_theory.adjunction.equiv_homset_left_of_nat_iso CategoryTheory.Adjunction.equivHomsetLeftOfNatIso

/-- If G and H are naturally isomorphic functors, establish an equivalence of hom-sets. -/
@[simps]
def equivHomsetRightOfNatIso {G G' : D ‚•§ C} (iso : G ‚âÖ G') {X : C} {Y : D} :
    (X ‚ü∂ G.obj Y) ‚âÉ (X ‚ü∂ G'.obj Y) where
  toFun f := f ‚â´ iso.hom.app _
  invFun g := g ‚â´ iso.inv.app _
  left_inv f := by simp
  right_inv g := by simp
#align category_theory.adjunction.equiv_homset_right_of_nat_iso CategoryTheory.Adjunction.equivHomsetRightOfNatIso

/-- Transport an adjunction along a natural isomorphism on the left. -/
def ofNatIsoLeft {F G : C ‚•§ D} {H : D ‚•§ C} (adj : F ‚ä£ H) (iso : F ‚âÖ G) : G ‚ä£ H :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun X Y => (equivHomsetLeftOfNatIso iso.symm).trans (adj.homEquiv X Y) }
#align category_theory.adjunction.of_nat_iso_left CategoryTheory.Adjunction.ofNatIsoLeft

/-- Transport an adjunction along a natural isomorphism on the right. -/
def ofNatIsoRight {F : C ‚•§ D} {G H : D ‚•§ C} (adj : F ‚ä£ G) (iso : G ‚âÖ H) : F ‚ä£ H :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun X Y => (adj.homEquiv X Y).trans (equivHomsetRightOfNatIso iso) }
#align category_theory.adjunction.of_nat_iso_right CategoryTheory.Adjunction.ofNatIsoRight

section

variable {E : Type u‚ÇÉ} [‚Ñ∞ : Category.{v‚ÇÉ} E] {H : D ‚•§ E} {I : E ‚•§ D}

/-- Composition of adjunctions.

See <https://stacks.math.columbia.edu/tag/0DV0>.
-/
def comp (adj‚ÇÅ : F ‚ä£ G) (adj‚ÇÇ : H ‚ä£ I) : F ‚ãô H ‚ä£ I ‚ãô G where
  homEquiv X Z := Equiv.trans (adj‚ÇÇ.homEquiv _ _) (adj‚ÇÅ.homEquiv _ _)
  unit := adj‚ÇÅ.unit ‚â´ (whiskerLeft F <| whiskerRight adj‚ÇÇ.unit G) ‚â´ (Functor.associator _ _ _).inv
  counit :=
    (Functor.associator _ _ _).hom ‚â´ (whiskerLeft I <| whiskerRight adj‚ÇÅ.counit H) ‚â´ adj‚ÇÇ.counit
#align category_theory.adjunction.comp CategoryTheory.Adjunction.comp

end

section ConstructLeft

-- Construction of a left adjoint. In order to construct a left
-- adjoint to a functor G : D ‚Üí C, it suffices to give the object part
-- of a functor F : C ‚Üí D together with isomorphisms Hom(FX, Y) ‚âÉ
-- Hom(X, GY) natural in Y. The action of F on morphisms can be
-- constructed from this data.
variable {F_obj : C ‚Üí D}
variable (e : ‚àÄ X Y, (F_obj X ‚ü∂ Y) ‚âÉ (X ‚ü∂ G.obj Y))
variable (he : ‚àÄ X Y Y' g h, e X Y' (h ‚â´ g) = e X Y h ‚â´ G.map g)

private theorem he' {X Y Y'} (f g) : (e X Y').symm (f ‚â´ G.map g) = (e X Y).symm f ‚â´ g := by
  rw [Equiv.symm_apply_eq, he]; simp
-- #align category_theory.adjunction.he' category_theory.adjunction.he'

/-- Construct a left adjoint functor to `G`, given the functor's value on objects `F_obj` and
a bijection `e` between `F_obj X ‚ü∂ Y` and `X ‚ü∂ G.obj Y` satisfying a naturality law
`he : ‚àÄ X Y Y' g h, e X Y' (h ‚â´ g) = e X Y h ‚â´ G.map g`.
Dual to `rightAdjointOfEquiv`. -/
@[simps!]
def leftAdjointOfEquiv : C ‚•§ D where
  obj := F_obj
  map {X} {X'} f := (e X (F_obj X')).symm (f ‚â´ e X' (F_obj X') (ùüô _))
  map_comp := fun f f' => by
    rw [Equiv.symm_apply_eq, he, Equiv.apply_symm_apply]
    conv =>
      rhs
      rw [assoc, ‚Üê he, id_comp, Equiv.apply_symm_apply]
    simp
#align category_theory.adjunction.left_adjoint_of_equiv CategoryTheory.Adjunction.leftAdjointOfEquiv

/-- Show that the functor given by `leftAdjointOfEquiv` is indeed left adjoint to `G`. Dual
to `adjunctionOfRightEquiv`. -/
@[simps!]
def adjunctionOfEquivLeft : leftAdjointOfEquiv e he ‚ä£ G :=
  mkOfHomEquiv
    { homEquiv := e
      homEquiv_naturality_left_symm := fun {X'} {X} {Y} f g => by
        have := @he' C _ D _ G F_obj e he
        erw [‚Üê this, ‚Üê Equiv.apply_eq_iff_eq (e X' Y)]
        simp only [leftAdjointOfEquiv_obj, Equiv.apply_symm_apply, assoc]
        congr
        rw [‚Üê he]
        simp
    }
#align category_theory.adjunction.adjunction_of_equiv_left CategoryTheory.Adjunction.adjunctionOfEquivLeft

end ConstructLeft

section ConstructRight

-- Construction of a right adjoint, analogous to the above.
variable {G_obj : D ‚Üí C}
variable (e : ‚àÄ X Y, (F.obj X ‚ü∂ Y) ‚âÉ (X ‚ü∂ G_obj Y))
variable (he : ‚àÄ X' X Y f g, e X' Y (F.map f ‚â´ g) = f ‚â´ e X Y g)

private theorem he'' {X' X Y} (f g) : F.map f ‚â´ (e X Y).symm g = (e X' Y).symm (f ‚â´ g) := by
  rw [Equiv.eq_symm_apply, he]; simp
-- #align category_theory.adjunction.he' category_theory.adjunction.he'

/-- Construct a right adjoint functor to `F`, given the functor's value on objects `G_obj` and
a bijection `e` between `F.obj X ‚ü∂ Y` and `X ‚ü∂ G_obj Y` satisfying a naturality law
`he : ‚àÄ X Y Y' g h, e X' Y (F.map f ‚â´ g) = f ‚â´ e X Y g`.
Dual to `leftAdjointOfEquiv`. -/
@[simps!]
def rightAdjointOfEquiv : D ‚•§ C where
  obj := G_obj
  map {Y} {Y'} g := (e (G_obj Y) Y') ((e (G_obj Y) Y).symm (ùüô _) ‚â´ g)
  map_comp := fun {Y} {Y'} {Y''} g g' => by
    rw [‚Üê Equiv.eq_symm_apply, ‚Üê he'' e he, Equiv.symm_apply_apply]
    conv =>
      rhs
      rw [‚Üê assoc, he'' e he, comp_id, Equiv.symm_apply_apply]
    simp
#align category_theory.adjunction.right_adjoint_of_equiv CategoryTheory.Adjunction.rightAdjointOfEquiv

/-- Show that the functor given by `rightAdjointOfEquiv` is indeed right adjoint to `F`. Dual
to `adjunctionOfEquivRight`. -/
@[simps!]
def adjunctionOfEquivRight : F ‚ä£ (rightAdjointOfEquiv e he) :=
  mkOfHomEquiv
    { homEquiv := e
      homEquiv_naturality_left_symm := by
        intro X X' Y f g; rw [Equiv.symm_apply_eq]; dsimp; rw [he]; simp
      homEquiv_naturality_right := by
        intro X Y Y' g h
        erw [‚Üê he, Equiv.apply_eq_iff_eq, ‚Üê assoc, he'' e he, comp_id, Equiv.symm_apply_apply] }
#align category_theory.adjunction.adjunction_of_equiv_right CategoryTheory.Adjunction.adjunctionOfEquivRight

end ConstructRight

/--
If the unit and counit of a given adjunction are (pointwise) isomorphisms, then we can upgrade the
adjunction to an equivalence.
-/
@[simps!]
noncomputable def toEquivalence (adj : F ‚ä£ G) [‚àÄ X, IsIso (adj.unit.app X)]
    [‚àÄ Y, IsIso (adj.counit.app Y)] : C ‚âå D where
  functor := F
  inverse := G
  unitIso := NatIso.ofComponents fun X => asIso (adj.unit.app X)
  counitIso := NatIso.ofComponents fun Y => asIso (adj.counit.app Y)
#align category_theory.adjunction.to_equivalence CategoryTheory.Adjunction.toEquivalence

end Adjunction

open Adjunction

/--
If the unit and counit for the adjunction corresponding to a right adjoint functor are (pointwise)
isomorphisms, then the functor is an equivalence of categories.
-/
lemma Functor.isEquivalence_of_isRightAdjoint (G : C ‚•§ D) [IsRightAdjoint G]
    [‚àÄ X, IsIso ((Adjunction.ofIsRightAdjoint G).unit.app X)]
    [‚àÄ Y, IsIso ((Adjunction.ofIsRightAdjoint G).counit.app Y)] : G.IsEquivalence :=
  (Adjunction.ofIsRightAdjoint G).toEquivalence.isEquivalence_inverse
#align category_theory.adjunction.is_right_adjoint_to_is_equivalence CategoryTheory.Functor.isEquivalence_of_isRightAdjoint

namespace Equivalence

variable (e : C ‚âå D)

/-- The adjunction given by an equivalence of categories. (To obtain the opposite adjunction,
simply use `e.symm.toAdjunction`. -/
@[simps! unit counit]
def toAdjunction : e.functor ‚ä£ e.inverse :=
  mkOfUnitCounit
    ‚ü®e.unit, e.counit, by
      ext
      dsimp
      simp only [id_comp]
      exact e.functor_unit_comp _, by
      ext
      dsimp
      simp only [id_comp]
      exact e.unit_inverse_comp _‚ü©
#align category_theory.equivalence.to_adjunction CategoryTheory.Equivalence.toAdjunction
#align category_theory.equivalence.as_equivalence_to_adjunction_unit CategoryTheory.Equivalence.toAdjunction_unit‚Çì
#align category_theory.equivalence.as_equivalence_to_adjunction_counit CategoryTheory.Equivalence.toAdjunction_counit‚Çì

lemma isLeftAdjoint_functor : e.functor.IsLeftAdjoint where
  exists_rightAdjoint := ‚ü®_, ‚ü®e.toAdjunction‚ü©‚ü©

lemma isRightAdjoint_inverse : e.inverse.IsRightAdjoint where
  exists_leftAdjoint := ‚ü®_, ‚ü®e.toAdjunction‚ü©‚ü©

lemma isLeftAdjoint_inverse : e.inverse.IsLeftAdjoint :=
  e.symm.isLeftAdjoint_functor

lemma isRightAdjoint_functor : e.functor.IsRightAdjoint :=
  e.symm.isRightAdjoint_inverse

end Equivalence

namespace Functor

/-- If `F` and `G` are left adjoints then `F ‚ãô G` is a left adjoint too. -/
instance isLeftAdjoint_comp {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E] (F : C ‚•§ D) (G : D ‚•§ E)
    [F.IsLeftAdjoint] [G.IsLeftAdjoint] : (F ‚ãô G).IsLeftAdjoint where
  exists_rightAdjoint :=
    ‚ü®_, ‚ü®(Adjunction.ofIsLeftAdjoint F).comp (Adjunction.ofIsLeftAdjoint G)‚ü©‚ü©
#align category_theory.adjunction.left_adjoint_of_comp CategoryTheory.Functor.isLeftAdjoint_comp

/-- If `F` and `G` are right adjoints then `F ‚ãô G` is a right adjoint too. -/
instance isRightAdjoint_comp {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E] {F : C ‚•§ D} {G : D ‚•§ E}
    [IsRightAdjoint F] [IsRightAdjoint G] : IsRightAdjoint (F ‚ãô G) where
  exists_leftAdjoint :=
    ‚ü®_, ‚ü®(Adjunction.ofIsRightAdjoint G).comp (Adjunction.ofIsRightAdjoint F)‚ü©‚ü©
#align category_theory.adjunction.right_adjoint_of_comp CategoryTheory.Functor.isRightAdjoint_comp

/-- Transport being a right adjoint along a natural isomorphism. -/
lemma isRightAdjoint_of_iso {F G : C ‚•§ D} (h : F ‚âÖ G) [F.IsRightAdjoint] :
    IsRightAdjoint G where
  exists_leftAdjoint := ‚ü®_, ‚ü®(Adjunction.ofIsRightAdjoint F).ofNatIsoRight h‚ü©‚ü©
#align category_theory.adjunction.right_adjoint_of_nat_iso CategoryTheory.Functor.isRightAdjoint_of_iso

/-- Transport being a left adjoint along a natural isomorphism. -/
lemma isLeftAdjoint_of_iso {F G : C ‚•§ D} (h : F ‚âÖ G) [IsLeftAdjoint F] :
    IsLeftAdjoint G where
  exists_rightAdjoint := ‚ü®_, ‚ü®(Adjunction.ofIsLeftAdjoint F).ofNatIsoLeft h‚ü©‚ü©
#align category_theory.adjunction.left_adjoint_of_nat_iso CategoryTheory.Functor.isLeftAdjoint_of_iso


/-- An equivalence `E` is left adjoint to its inverse. -/
noncomputable def adjunction (E : C ‚•§ D) [IsEquivalence E] : E ‚ä£ E.inv :=
  E.asEquivalence.toAdjunction
#align category_theory.functor.adjunction CategoryTheory.Functor.adjunction

/-- If `F` is an equivalence, it's a left adjoint. -/
instance (priority := 10) isLeftAdjoint_of_isEquivalence {F : C ‚•§ D} [F.IsEquivalence] :
    IsLeftAdjoint F :=
  F.asEquivalence.isLeftAdjoint_functor
#align category_theory.functor.left_adjoint_of_equivalence CategoryTheory.Functor.isLeftAdjoint_of_isEquivalence

/-- If `F` is an equivalence, it's a right adjoint. -/
instance (priority := 10) isRightAdjoint_of_isEquivalence {F : C ‚•§ D} [F.IsEquivalence] :
    IsRightAdjoint F :=
  F.asEquivalence.isRightAdjoint_functor
#align category_theory.functor.right_adjoint_of_equivalence CategoryTheory.Functor.isRightAdjoint_of_isEquivalence

end Functor

end CategoryTheory
