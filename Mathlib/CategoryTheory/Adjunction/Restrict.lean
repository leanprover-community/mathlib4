/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Conj
import Mathlib.CategoryTheory.Adjunction.Basic
/-!

# Restricting adjunctions

`Adjunction.restrictFullyFaithful` shows that an adjunction can be restricted along fully faithful
inclusions.
-/

namespace CategoryTheory.Adjunction

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ v‚ÇÉ v‚ÇÑ u‚ÇÉ u‚ÇÑ

open Category Opposite

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]
variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
variable {C' : Type u‚ÇÉ} [Category.{v‚ÇÉ} C']
variable {D' : Type u‚ÇÑ} [Category.{v‚ÇÑ} D']
variable (iC : C ‚•§ C') (iD : D ‚•§ D') {L' : C' ‚•§ D'} {R' : D' ‚•§ C'} (adj : L' ‚ä£ R')
variable {L : C ‚•§ D} {R : D ‚•§ C} (comm1 : iC ‚ãô L' ‚âÖ L ‚ãô iD) (comm2 : iD ‚ãô R' ‚âÖ R ‚ãô iC)
variable [iC.Full] [iC.Faithful] [iD.Full] [iD.Faithful]

/-- If `C` is a full subcategory of `C'` and `D` is a full subcategory of `D'`, then we can restrict
an adjunction `L' ‚ä£ R'` where `L' : C' ‚•§ D'` and `R' : D' ‚•§ C'` to `C` and `D`.
The construction here is slightly more general, in that `C` is required only to have a full and
faithful "inclusion" functor `iC : C ‚•§ C'` (and similarly `iD : D ‚•§ D'`) which commute (up to
natural isomorphism) with the proposed restrictions.
-/
def restrictFullyFaithful : L ‚ä£ R :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun X Y =>
        calc
          (L.obj X ‚ü∂ Y) ‚âÉ (iD.obj (L.obj X) ‚ü∂ iD.obj Y) := equivOfFullyFaithful iD
          _ ‚âÉ (L'.obj (iC.obj X) ‚ü∂ iD.obj Y) := Iso.homCongr (comm1.symm.app X) (Iso.refl _)
          _ ‚âÉ (iC.obj X ‚ü∂ R'.obj (iD.obj Y)) := adj.homEquiv _ _
          _ ‚âÉ (iC.obj X ‚ü∂ iC.obj (R.obj Y)) := Iso.homCongr (Iso.refl _) (comm2.app Y)
          _ ‚âÉ (X ‚ü∂ R.obj Y) := (equivOfFullyFaithful iC).symm

      homEquiv_naturality_left_symm := fun {X' X Y} f g => by
        apply iD.map_injective
        simpa [Trans.trans] using (comm1.inv.naturality_assoc f _).symm
      homEquiv_naturality_right := fun {X Y' Y} f g => by
        apply iC.map_injective
        suffices R'.map (iD.map g) ‚â´ comm2.hom.app Y = comm2.hom.app Y' ‚â´ iC.map (R.map g) by
          simp [Trans.trans, this]
        apply comm2.hom.naturality g }
#align category_theory.adjunction.restrict_fully_faithful CategoryTheory.Adjunction.restrictFullyFaithful

@[simp, reassoc]
lemma map_restrictFullyFaithful_unit_app (X : C) :
    iC.map ((restrictFullyFaithful iC iD adj comm1 comm2).unit.app X) =
    adj.unit.app (iC.obj X) ‚â´ R'.map (comm1.hom.app X) ‚â´ comm2.hom.app (L.obj X) := by
  simp [restrictFullyFaithful]

@[simp, reassoc]
lemma map_restrictFullyFaithful_counit_app (X : D) :
    iD.map ((restrictFullyFaithful iC iD adj comm1 comm2).counit.app X) =
    comm1.inv.app (R.obj X) ‚â´ L'.map (comm2.inv.app X) ‚â´ adj.counit.app (iD.obj X) := by
  simp only [Functor.comp_obj, Functor.id_obj, restrictFullyFaithful, equivOfFullyFaithful,
    Equiv.instTransSortSortSortEquivEquivEquiv_trans, mkOfHomEquiv_counit_app, Equiv.invFun_as_coe,
    Equiv.symm_trans_apply, Equiv.symm_symm, Iso.homCongr_symm, Iso.refl_symm, Iso.homCongr_apply,
    Iso.refl_inv, Iso.symm_hom, Iso.app_inv, id_comp, homEquiv_counit, Functor.map_comp, assoc,
    Iso.symm_inv, Iso.app_hom, Iso.refl_hom, comp_id, Equiv.coe_fn_symm_mk, Functor.image_preimage,
    NatIso.cancel_natIso_inv_left]
  change L'.map (iC.map (ùüô _)) ‚â´ _ = _
  simp

end CategoryTheory.Adjunction
