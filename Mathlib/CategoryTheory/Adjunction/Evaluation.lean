/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import Mathlib.CategoryTheory.Limits.Shapes.Products
import Mathlib.CategoryTheory.Functor.EpiMono

/-!

# Adjunctions involving evaluation

We show that evaluation of functors have adjoints, given the existence of (co)products.

-/


namespace CategoryTheory

open CategoryTheory.Limits

universe vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

variable {C : Type uâ‚} [Category.{vâ‚} C] (D : Type uâ‚‚) [Category.{vâ‚‚} D]

noncomputable section

section

variable [âˆ€ a b : C, HasCoproductsOfShape (a âŸ¶ b) D]

/-- The left adjoint of evaluation. -/
@[simps]
def evaluationLeftAdjoint (c : C) : D â¥¤ C â¥¤ D where
  obj d :=
    { obj := fun t => âˆ fun _ : c âŸ¶ t => d
      map := fun f => Sigma.desc fun g => (Sigma.Î¹ fun _ => d) <| g â‰« f}
  map {_ dâ‚‚} f :=
    { app := fun _ => Sigma.desc fun h => f â‰« Sigma.Î¹ (fun _ => dâ‚‚) h
      naturality := by
        intros
        dsimp
        ext
        simp }

/-- The adjunction showing that evaluation is a right adjoint. -/
@[simps! unit_app counit_app_app]
def evaluationAdjunctionRight (c : C) : evaluationLeftAdjoint D c âŠ£ (evaluation _ _).obj c :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun d F =>
        { toFun := fun f => Sigma.Î¹ (fun _ => d) (ðŸ™ _) â‰« f.app c
          invFun := fun f => { app := fun _ => Sigma.desc fun h => f â‰« F.map h }
          left_inv := by
            intro f
            ext x
            dsimp
            ext g
            simp only [colimit.Î¹_desc, Cofan.mk_Î¹_app, Category.assoc, â† f.naturality,
              evaluationLeftAdjoint_obj_map, colimit.Î¹_desc_assoc,
              Discrete.functor_obj, Cofan.mk_pt, Category.id_comp]
          right_inv := fun f => by simp } }

instance evaluationIsRightAdjoint (c : C) : ((evaluation _ D).obj c).IsRightAdjoint  :=
  âŸ¨_, âŸ¨evaluationAdjunctionRight _ _âŸ©âŸ©

/-- See also the file `CategoryTheory.Limits.FunctorCategory.EpiMono`
for a similar result under a `HasPullbacks` assumption. -/
theorem NatTrans.mono_iff_mono_app' {F G : C â¥¤ D} (Î· : F âŸ¶ G) : Mono Î· â†” âˆ€ c, Mono (Î·.app c) := by
  constructor
  Â· intro h c
    exact (inferInstance : Mono (((evaluation _ _).obj c).map Î·))
  Â· intro _
    apply NatTrans.mono_of_mono_app

end

section

variable [âˆ€ a b : C, HasProductsOfShape (a âŸ¶ b) D]

/-- The right adjoint of evaluation. -/
@[simps]
def evaluationRightAdjoint (c : C) : D â¥¤ C â¥¤ D where
  obj d :=
    { obj := fun t => âˆá¶œ fun _ : t âŸ¶ c => d
      map := fun f => Pi.lift fun g => Pi.Ï€ _ <| f â‰« g }
  map f := { app := fun _ => Pi.lift fun g => Pi.Ï€ _ g â‰« f }

/-- The adjunction showing that evaluation is a left adjoint. -/
@[simps! unit_app_app counit_app]
def evaluationAdjunctionLeft (c : C) : (evaluation _ _).obj c âŠ£ evaluationRightAdjoint D c :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun F d =>
        { toFun := fun f => { app := fun _ => Pi.lift fun g => F.map g â‰« f }
          invFun := fun f => f.app _ â‰« Pi.Ï€ _ (ðŸ™ _)
          left_inv := fun f => by simp
          right_inv := by
            intro f
            ext x
            dsimp
            ext g
            simp only [NatTrans.naturality_assoc,
              evaluationRightAdjoint_obj_obj, evaluationRightAdjoint_obj_map, limit.lift_Ï€,
              Fan.mk_pt, Fan.mk_Ï€_app, Category.comp_id] } }

instance evaluationIsLeftAdjoint (c : C) : ((evaluation _ D).obj c).IsLeftAdjoint :=
  âŸ¨_, âŸ¨evaluationAdjunctionLeft _ _âŸ©âŸ©

/-- See also the file `Mathlib/CategoryTheory/Limits/FunctorCategory/EpiMono.lean`
for a similar result under a `HasPushouts` assumption. -/
theorem NatTrans.epi_iff_epi_app' {F G : C â¥¤ D} (Î· : F âŸ¶ G) : Epi Î· â†” âˆ€ c, Epi (Î·.app c) := by
  constructor
  Â· intro h c
    exact (inferInstance : Epi (((evaluation _ _).obj c).map Î·))
  Â· intros
    apply NatTrans.epi_of_epi_app

end

end

end CategoryTheory
