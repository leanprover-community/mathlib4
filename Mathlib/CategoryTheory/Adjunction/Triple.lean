/-
Copyright (c) 2024 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson, Ben Eltschig
-/
import Mathlib.CategoryTheory.Adjunction.Unique
import Mathlib.CategoryTheory.Monad.Adjunction
/-!

# Adjoint triples

This file concerns adjoint triples `F ‚ä£ G ‚ä£ H` of functors `F H : C ‚•§ D`, `G : D ‚•§ C`. We first
prove that `F` is fully faithful iff `H` is, and then prove results about the two special cases
where `G` is fully faithful or `F` and `H` are.

## Main results

All results are about an adjoint triple `F ‚ä£ G ‚ä£ H` where `adj‚ÇÅ : F ‚ä£ G` and `adj‚ÇÇ : G ‚ä£ H`. We
bundle the adjunctions in a structure `Triple F G H`.
* `fullyFaithfulEquiv`: `F` is fully faithful iff `H` is.
* `rightToLeft`: the canonical natural transformation `H ‚ü∂ F` that exists whenever `G` is fully
  faithful. This is defined as the preimage of `adj‚ÇÇ.counit ‚â´ adj‚ÇÅ.unit` under whiskering with `G`,
  but formulas in terms of the units resp. counits of the adjunctions are also given.
* `whiskerRight_rightToLeft`: whiskering `rightToLeft : H ‚ü∂ F` with `G` yields
  `adj‚ÇÇ.counit ‚â´ adj‚ÇÅ.unit : H ‚ãô G ‚ü∂ F ‚ãô G`.
* `epi_rightToLeft_app_iff_epi_map_adj‚ÇÅ_unit_app`: `rightToLeft : H ‚ü∂ F` is epic at `X` iff the
  image of `adj‚ÇÅ.unit.app X` under `H` is.
* `epi_rightToLeft_app_iff_epi_map_adj‚ÇÇ_counit_app`: `rightToLeft : H ‚ü∂ F` is epic at `X` iff the
  image of `adj‚ÇÇ.counit.app X` under `F` is.
* `epi_rightToLeft_app_iff`: when `H` preserves epimorphisms, `rightToLeft : H ‚ü∂ F` is epic at `X`
  iff `adj‚ÇÇ.counit ‚â´ adj‚ÇÅ.unit : H ‚ãô G ‚ü∂ F ‚ãô G` is.
* `leftToRight`: the canonical natural transformation `F ‚ü∂ H` that exists whenever `F` and `H` are
  fully faithful. This is defined in terms of the units of the adjunctions, but a formula in terms
  of the counits is also given.
* `whiskerLeft_leftToRight`: whiskering `G` with `leftToRight : F ‚ü∂ H` yields
  `adj‚ÇÅ.counit ‚â´ adj‚ÇÇ.unit : G ‚ãô F ‚ü∂ G ‚ãô H`.
* `mono_leftToRight_app_iff_mono_adj‚ÇÇ_unit_app`: `leftToRight : F ‚ü∂ H` is monic at `X` iff
  `adj‚ÇÇ.unit` is monic at `F.obj X`.
* `mono_leftToRight_app_iff_mono_adj‚ÇÅ_counit_app`: `leftToRight : F ‚ü∂ H` is monic at `X` iff
  `adj‚ÇÅ.unit` is monic at `H.obj X`.
* `mono_leftToRight_app_iff`: `leftToRight : H ‚ü∂ F` is componentwise monic iff
  `adj‚ÇÅ.counit ‚â´ adj‚ÇÇ.unit : G ‚ãô F ‚ü∂ G ‚ãô H` is.
-/

open CategoryTheory Functor

variable {C D : Type*} [Category C] [Category D]
variable (F : C ‚•§ D) (G : D ‚•§ C) (H : C ‚•§ D)

/-- Structure containing the two adjunctions of an adjoint triple `F ‚ä£ G ‚ä£ H`. -/
structure CategoryTheory.Adjunction.Triple where
  /-- Adjunction `F ‚ä£ G` of the adjoint triple `F ‚ä£ G ‚ä£ H`. -/
  adj‚ÇÅ : F ‚ä£ G
  /-- Adjunction `G ‚ä£ H` of the adjoint triple `F ‚ä£ G ‚ä£ H`. -/
  adj‚ÇÇ : G ‚ä£ H

namespace CategoryTheory.Adjunction.Triple

variable {F G H} (t : Triple F G H)

lemma isIso_unit_iff_isIso_counit : IsIso t.adj‚ÇÅ.unit ‚Üî IsIso t.adj‚ÇÇ.counit := by
  let adj : F ‚ãô G ‚ä£ H ‚ãô G := t.adj‚ÇÅ.comp t.adj‚ÇÇ
  constructor
  ¬∑ intro h
    let idAdj : ùü≠ C ‚ä£ H ‚ãô G := adj.ofNatIsoLeft (asIso t.adj‚ÇÅ.unit).symm
    exact t.adj‚ÇÇ.isIso_counit_of_iso (idAdj.rightAdjointUniq id)
  ¬∑ intro h
    let adjId : F ‚ãô G ‚ä£ ùü≠ C := adj.ofNatIsoRight (asIso t.adj‚ÇÇ.counit)
    exact t.adj‚ÇÅ.isIso_unit_of_iso (adjId.leftAdjointUniq id)

/--
Given an adjoint triple `F ‚ä£ G ‚ä£ H`, the left adjoint `F` is fully faithful if and only if the
right adjoint `H` is fully faithful.
-/
noncomputable def fullyFaithfulEquiv : F.FullyFaithful ‚âÉ H.FullyFaithful where
  toFun h :=
    haveI := h.full
    haveI := h.faithful
    haveI : IsIso t.adj‚ÇÇ.counit := by
      rw [‚Üê t.isIso_unit_iff_isIso_counit]
      infer_instance
    t.adj‚ÇÇ.fullyFaithfulROfIsIsoCounit
  invFun h :=
    haveI := h.full
    haveI := h.faithful
    haveI : IsIso t.adj‚ÇÅ.unit := by
      rw [t.isIso_unit_iff_isIso_counit]
      infer_instance
    t.adj‚ÇÅ.fullyFaithfulLOfIsIsoUnit
  left_inv _ := Subsingleton.elim _ _
  right_inv _ := Subsingleton.elim _ _

section InnerFullyFaithful

variable [G.Full] [G.Faithful]

/-- The natural transformation `H ‚ü∂ F` that exists for every adjoint triple `F ‚ä£ G ‚ä£ H` where `G`
is fully faithful, given here as the preimage of `adj‚ÇÇ.counit ‚â´ adj‚ÇÅ.unit : H ‚ãô G ‚ü∂ F ‚ãô G`
under whiskering with `G`. -/
noncomputable def rightToLeft : H ‚ü∂ F :=
  ((FullyFaithful.ofFullyFaithful G).whiskeringRight _).preimage (t.adj‚ÇÇ.counit ‚â´ t.adj‚ÇÅ.unit)

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `G` is fully faithful, whiskering the natural
transformation `H ‚ü∂ F` with `G` yields the composition of the counit of the second adjunction with
the unit of the first adjunction. -/
@[simp, reassoc]
lemma whiskerRight_rightToLeft : whiskerRight t.rightToLeft G = t.adj‚ÇÇ.counit ‚â´ t.adj‚ÇÅ.unit :=
  ((FullyFaithful.ofFullyFaithful G).whiskeringRight _).map_preimage _

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `G` is fully faithful, the images of the components of
the natural transformation `H ‚ü∂ F` under `G` are the components of the composition of counit of the
second adjunction with the unit of the first adjunction. -/
@[simp, reassoc]
lemma map_rightToLeft_app (X : C) :
    G.map (t.rightToLeft.app X) = t.adj‚ÇÇ.counit.app X ‚â´ t.adj‚ÇÅ.unit.app X :=
  congr_app t.whiskerRight_rightToLeft X

/-- The natural transformation `H ‚ü∂ F` for an adjoint triple `F ‚ä£ G ‚ä£ H` with `G` fully faithful
is also equal to the whiskered unit `H ‚ü∂ F ‚ãô G ‚ãô H` of the first adjunction followed by the
inverse of the whiskered unit `F ‚ü∂ F ‚ãô G ‚ãô H` of the second. -/
lemma rightToLeft_eq_units :
    t.rightToLeft = H.leftUnitor.inv ‚â´ whiskerRight t.adj‚ÇÅ.unit H ‚â´ (Functor.associator _ _ _).hom ‚â´
    inv (whiskerLeft F t.adj‚ÇÇ.unit) ‚â´ F.rightUnitor.hom := by
  ext X; apply G.map_injective; simp [rightToLeft]

/-- The natural transformation `H ‚ü∂ F` for an adjoint triple `F ‚ä£ G ‚ä£ H` with `G` fully faithful
is also equal to the inverse of the whiskered counit `H ‚ãô G ‚ãô F ‚ü∂ H` of the first adjunction
followed by the whiskered counit `H ‚ãô G ‚ãô F ‚ü∂ F` of the second. -/
lemma rightToLeft_eq_counits :
    t.rightToLeft = H.rightUnitor.inv ‚â´ inv (whiskerLeft H t.adj‚ÇÅ.counit) ‚â´
    (Functor.associator _ _ _).inv ‚â´ whiskerRight t.adj‚ÇÇ.counit F ‚â´ F.leftUnitor.hom := by
  ext X; apply G.map_injective; simp [rightToLeft]

@[reassoc (attr := simp)]
lemma adj‚ÇÅ_counit_app_rightToLeft_app (X : C) :
    t.adj‚ÇÅ.counit.app (H.obj X) ‚â´ t.rightToLeft.app X = F.map (t.adj‚ÇÇ.counit.app X) :=
  G.map_injective (by simp [‚Üê cancel_epi (t.adj‚ÇÅ.unit.app _)])

@[reassoc (attr := simp)]
lemma rightToLeft_app_adj‚ÇÇ_unit_app (X : C) :
    t.rightToLeft.app X ‚â´ t.adj‚ÇÇ.unit.app (F.obj X) = H.map (t.adj‚ÇÅ.unit.app X) :=
  G.map_injective (by simp [‚Üê cancel_mono (t.adj‚ÇÇ.counit.app _)])

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `G` is fully faithful, the natural transformation
`H ‚ü∂ F` is epic at `X` iff the image of the unit of the adjunction `F ‚ä£ G` under `H` is. -/
lemma epi_rightToLeft_app_iff_epi_map_adj‚ÇÅ_unit_app {X : C} :
    Epi (t.rightToLeft.app X) ‚Üî Epi (H.map (t.adj‚ÇÅ.unit.app X)) := by
  rw [‚Üê epi_comp_iff_of_isIso _ (t.adj‚ÇÇ.unit.app (F.obj X)), rightToLeft_app_adj‚ÇÇ_unit_app]

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `G` is fully faithful, the natural transformation
`H ‚ü∂ F` is epic at `X` iff the image of the counit of the adjunction `G ‚ä£ H` under `F` is. -/
lemma epi_rightToLeft_app_iff_epi_map_adj‚ÇÇ_counit_app {X : C} :
    Epi (t.rightToLeft.app X) ‚Üî Epi (F.map (t.adj‚ÇÇ.counit.app X)) := by
  rw [‚Üê epi_comp_iff_of_epi (t.adj‚ÇÅ.counit.app (H.obj X)), adj‚ÇÅ_counit_app_rightToLeft_app]

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `G` is fully faithful and `H` preserves epimorphisms
(which is for example the case if `H` has a further right adjoint), the components of the natural
transformation `H ‚ü∂ F` are epic iff the respective components of the natural transformation
`H ‚ãô G ‚ü∂ F ‚ãô G` obtained from the units and counits of the adjunctions are. -/
lemma epi_rightToLeft_app_iff [H.PreservesEpimorphisms] {X : C} :
    Epi (t.rightToLeft.app X) ‚Üî Epi (t.adj‚ÇÇ.counit.app X ‚â´ t.adj‚ÇÅ.unit.app X) := by
  have _ := t.adj‚ÇÇ.isLeftAdjoint
  refine ‚ü®fun h ‚Ü¶ by rw [‚Üê map_rightToLeft_app]; exact G.map_epi _, fun h ‚Ü¶ ?_‚ü©
  rw [epi_rightToLeft_app_iff_epi_map_adj‚ÇÅ_unit_app]
  simpa using epi_comp (t.adj‚ÇÇ.unit.app (H.obj X)) (H.map (t.adj‚ÇÇ.counit.app X ‚â´ t.adj‚ÇÅ.unit.app X))

end InnerFullyFaithful

section OuterFullyFaithful

variable [F.Full] [F.Faithful] [H.Full] [H.Faithful]

/-- The natural transformation `F ‚ü∂ H` that exists for every adjoint triple `F ‚ä£ G ‚ä£ H` where `F`
and `H` are fully faithful, given here as the whiskered unit `F ‚ü∂ F ‚ãô G ‚ãô H` of the second
adjunction followed by the inverse of the whiskered unit `F ‚ãô G ‚ãô H ‚ü∂ H` of the first. -/
noncomputable def leftToRight : F ‚ü∂ H :=
  F.rightUnitor.inv ‚â´ whiskerLeft F t.adj‚ÇÇ.unit ‚â´ (Functor.associator _ _ _).inv ‚â´
  inv (whiskerRight t.adj‚ÇÅ.unit H) ‚â´ H.leftUnitor.hom

omit [H.Full] [H.Faithful] in
lemma leftToRight_app {X : C} :
    t.leftToRight.app X = t.adj‚ÇÇ.unit.app (F.obj X) ‚â´ inv (H.map (t.adj‚ÇÅ.unit.app X)) := by
  simp [leftToRight]

/-- The natural transformation `F ‚ü∂ H` for an adjoint triple `F ‚ä£ G ‚ä£ H` with `F` and `H`
fully faithful is also equal to the inverse of the whiskered counit `H ‚ãô G ‚ãô F ‚ü∂ F` of the second
adjunction followed by the whiskered counit `H ‚ãô G ‚ãô F ‚ü∂ H` of the first. -/
lemma leftToRight_eq_counits :
    t.leftToRight = F.leftUnitor.inv ‚â´ inv (whiskerRight t.adj‚ÇÇ.counit F) ‚â´
    (Functor.associator _ _ _).hom ‚â´ whiskerLeft H t.adj‚ÇÅ.counit ‚â´ H.rightUnitor.hom := by
  ext X; dsimp [leftToRight]; simp only [Category.id_comp, Category.comp_id, NatIso.isIso_inv_app]
  rw [IsIso.comp_inv_eq, Category.assoc, IsIso.eq_inv_comp]
  refine Eq.trans ?_ (t.adj‚ÇÅ.counit_naturality <| (whiskerRight t.adj‚ÇÅ.unit H).app X)
  rw [whiskerRight_app _ H, (asIso (t.adj‚ÇÇ.counit.app (G.obj _))).eq_comp_inv.2
      (t.adj‚ÇÇ.counit_naturality (t.adj‚ÇÅ.unit.app X)),
    ‚Üê (asIso _).comp_hom_eq_id.1 <| t.adj‚ÇÇ.left_triangle_components (F.obj X)]
  simp

omit [H.Full] [H.Faithful] in
/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `F` and `H` are fully faithful, the components of the
natural transformation `F ‚ü∂ H` at `G` are precisely the components of the natural transformation
`G ‚ãô F ‚ü∂ G ‚ãô H` obtained from the units and counits of the adjunctions. -/
@[simp, reassoc]
lemma leftToRight_app_obj {X : D} :
    t.leftToRight.app (G.obj X) = t.adj‚ÇÅ.counit.app X ‚â´ t.adj‚ÇÇ.unit.app X := by
  refine (((t.adj‚ÇÇ.homEquiv _ _).apply_symm_apply _).symm.trans ?_).symm
  rw [homEquiv_symm_apply, map_comp, Category.assoc, left_triangle_components,
    homEquiv_apply, leftToRight_app, ‚Üê H.map_inv]
  congr
  simpa using IsIso.eq_inv_of_hom_inv_id (t.adj‚ÇÅ.right_triangle_components _)

omit [H.Full] [H.Faithful] in
/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `F` and `H` are fully faithful, whiskering `G` with the
natural transformation `F ‚ü∂ H` yields the composition of the counit of the first adjunction with
the unit of the second adjunction. -/
@[simp, reassoc]
lemma whiskerLeft_leftToRight : whiskerLeft G t.leftToRight = t.adj‚ÇÅ.counit ‚â´ t.adj‚ÇÇ.unit := by
  ext X; exact t.leftToRight_app_obj

omit [H.Full] [H.Faithful] in
lemma map_adj‚ÇÇ_counit_app_leftToRight_app (X : C) :
    F.map (t.adj‚ÇÇ.counit.app X) ‚â´ t.leftToRight.app X = t.adj‚ÇÅ.counit.app (H.obj X) := by
  simp

omit [H.Full] [H.Faithful] in
@[reassoc (attr := simp)]
lemma leftToRight_app_map_adj‚ÇÅ_unit_app (X : C) :
    t.leftToRight.app X ‚â´ H.map (t.adj‚ÇÅ.unit.app X) = t.adj‚ÇÇ.unit.app (F.obj X) := by
  simp [leftToRight_app]

omit [H.Full] [H.Faithful] in
/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `F` and `H` are fully faithful, the natural
transformation `F ‚ü∂ H` is monic at `X` iff the unit of the adjunction `G ‚ä£ H` is monic
at `F.obj X`. -/
lemma mono_leftToRight_app_iff_mono_adj‚ÇÇ_unit_app {X : C} :
    Mono (t.leftToRight.app X) ‚Üî Mono (t.adj‚ÇÇ.unit.app (F.obj X)) := by
  rw [‚Üê leftToRight_app_map_adj‚ÇÅ_unit_app, mono_comp_iff_of_mono]

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `F` and `H` are fully faithful, the natural
transformation `F ‚ü∂ H` is monic at `X` iff the counit of the adjunction `F ‚ä£ G` is monic
at `H.obj X`. -/
lemma mono_leftToRight_app_iff_mono_adj‚ÇÅ_counit_app {X : C} :
    Mono (t.leftToRight.app X) ‚Üî Mono (t.adj‚ÇÅ.counit.app (H.obj X)) := by
  rw [‚Üê map_adj‚ÇÇ_counit_app_leftToRight_app, mono_comp_iff_of_isIso]

omit [H.Full] [H.Faithful] in
/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `F` and `H` are fully faithful, the natural
transformation `F ‚ü∂ H` is componentwise monic iff the natural transformation `G ‚ãô F ‚ü∂ G ‚ãô H`
obtained from the units and counits of the adjunctions is.
Note that unlike `epi_rightToLeft_app_iff`, this equivalence does not make sense
on a per-object basis because the components of the two natural transformations are indexed by
different categories. -/
lemma mono_leftToRight_app_iff :
    (‚àÄ X, Mono (t.leftToRight.app X)) ‚Üî ‚àÄ X, Mono (t.adj‚ÇÅ.counit.app X ‚â´ t.adj‚ÇÇ.unit.app X) := by
  refine ‚ü®fun h X ‚Ü¶ by rw [‚Üê leftToRight_app_obj]; exact h _, fun h X ‚Ü¶ ?_‚ü©
  rw [mono_leftToRight_app_iff_mono_adj‚ÇÇ_unit_app]
  simpa using h (F.obj X)

end OuterFullyFaithful

end CategoryTheory.Adjunction.Triple
