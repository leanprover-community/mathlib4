/-
Copyright (c) 2024 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson, Ben Eltschig
-/
import Mathlib.CategoryTheory.Adjunction.Unique
import Mathlib.CategoryTheory.Limits.FunctorCategory.EpiMono
import Mathlib.CategoryTheory.Monad.Adjunction
/-!

# Adjoint triples

This file concerns adjoint triples `F ‚ä£ G ‚ä£ H` of functors `F H : C ‚•§ D`, `G : D ‚•§ C`.

Currently, the only result is that `F` is fully faithful if and only if `H` is fully faithful.
-/

namespace CategoryTheory.Adjunction

variable {C D : Type*} [Category C] [Category D]
variable {F H : C ‚•§ D} {G : D ‚•§ C}
variable (adj‚ÇÅ : F ‚ä£ G) (adj‚ÇÇ : G ‚ä£ H)

lemma isIso_unit_iff_isIso_counit : IsIso adj‚ÇÅ.unit ‚Üî IsIso adj‚ÇÇ.counit := by
  let adj : F ‚ãô G ‚ä£ H ‚ãô G := adj‚ÇÅ.comp adj‚ÇÇ
  constructor
  ¬∑ intro h
    let idAdj : ùü≠ C ‚ä£ H ‚ãô G := adj.ofNatIsoLeft (asIso adj‚ÇÅ.unit).symm
    exact adj‚ÇÇ.isIso_counit_of_iso (idAdj.rightAdjointUniq id)
  ¬∑ intro h
    let adjId : F ‚ãô G ‚ä£ ùü≠ C := adj.ofNatIsoRight (asIso adj‚ÇÇ.counit)
    exact adj‚ÇÅ.isIso_unit_of_iso (adjId.leftAdjointUniq id)

/--
Given an adjoint triple `F ‚ä£ G ‚ä£ H`, the left adjoint `F` is fully faithful if and only if the
right adjoint `H` is fully faithful.
-/
noncomputable def fullyFaithfulEquiv : F.FullyFaithful ‚âÉ H.FullyFaithful where
  toFun h :=
    haveI := h.full
    haveI := h.faithful
    haveI : IsIso adj‚ÇÇ.counit := by
      rw [‚Üê adj‚ÇÅ.isIso_unit_iff_isIso_counit adj‚ÇÇ]
      infer_instance
    adj‚ÇÇ.fullyFaithfulROfIsIsoCounit
  invFun h :=
    haveI := h.full
    haveI := h.faithful
    haveI : IsIso adj‚ÇÅ.unit := by
      rw [adj‚ÇÅ.isIso_unit_iff_isIso_counit adj‚ÇÇ]
      infer_instance
    adj‚ÇÅ.fullyFaithfulLOfIsIsoUnit
  left_inv _ := Subsingleton.elim _ _
  right_inv _ := Subsingleton.elim _ _

end CategoryTheory.Adjunction

open CategoryTheory Limits

-- TODO: move these somewhere else
section Misc

/-- When `f` is an isomorphism, `f ‚â´ g` is epic iff `g` is.
TODO: should this and the following lemmas be simp lemmas? might cause slowdowns because it triggers
instance searches for `IsIso` whenever `simp` is used on a goal containing `Mono (f ‚â´ g)`. -/
lemma CategoryTheory.epi_isIso_comp_iff {C : Type*} [Category C] {X Y Z : C} (f : X ‚ü∂ Y) [IsIso f]
    (g : Y ‚ü∂ Z) : Epi (f ‚â´ g) ‚Üî Epi g := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ inferInstance‚ü©
  simpa using (inferInstance : Epi (inv f ‚â´ f ‚â´ g))

/-- When `g` is an isomorphism, `f ‚â´ g` is epic iff `f` is. -/
lemma CategoryTheory.epi_comp_isIso_iff {C : Type*} [Category C] {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)
    [IsIso g] : Epi (f ‚â´ g) ‚Üî Epi f := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ inferInstance‚ü©
  simpa using (inferInstance : Epi ((f ‚â´ g) ‚â´ inv g ))

/-- When `f` is an isomorphism, `f ‚â´ g` is monic iff `g` is. -/
lemma CategoryTheory.mono_isIso_comp_iff {C : Type*} [Category C] {X Y Z : C} (f : X ‚ü∂ Y) [IsIso f]
    (g : Y ‚ü∂ Z) : Mono (f ‚â´ g) ‚Üî Mono g := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ inferInstance‚ü©
  simpa using (inferInstance : Mono (inv f ‚â´ f ‚â´ g))

/-- When `g` is an isomorphism, `f ‚â´ g` is monic iff `f` is. -/
lemma CategoryTheory.mono_comp_isIso_iff {C : Type*} [Category C] {X Y Z : C} (f : X ‚ü∂ Y)
    (g : Y ‚ü∂ Z) [IsIso g] : Mono (f ‚â´ g) ‚Üî Mono f := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ inferInstance‚ü©
  simpa using (inferInstance : Mono ((f ‚â´ g) ‚â´ inv g ))

/-- For any natural transformation `f : F ‚ü∂ G`, if `X` and `Y` are isomorphic, the component of
`f` at `X` is epic iff the component of `f` at `Y` is. -/
lemma CategoryTheory.NatTrans.epi_app_congr_iso {C D : Type*} [Category C] [Category D]
    {F G : C ‚•§ D} {f : F ‚ü∂ G} {X Y : C} (Œ± : X ‚âÖ Y) : Epi (f.app X) ‚Üî Epi (f.app Y) := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ rw [(IsIso.eq_inv_comp _).2 <| f.naturality Œ±.hom]; infer_instance
  ¬∑ rw [(IsIso.eq_inv_comp _).2 <| f.naturality Œ±.inv]; infer_instance

/-- For any natural transformation `f : F ‚ü∂ G`, if `X` and `Y` are isomorphic, the component of
`f` at `X` is monic iff the component of `f` at `Y` is. -/
lemma CategoryTheory.NatTrans.mono_app_congr_iso {C D : Type*} [Category C] [Category D]
    {F G : C ‚•§ D} {f : F ‚ü∂ G} {X Y : C} (Œ± : X ‚âÖ Y) : Mono (f.app X) ‚Üî Mono (f.app Y) := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ rw [(IsIso.eq_inv_comp _).2 <| f.naturality Œ±.hom]; infer_instance
  ¬∑ rw [(IsIso.eq_inv_comp _).2 <| f.naturality Œ±.inv]; infer_instance

/-- For any natural isomorphism `Œ± : F ‚âÖ G` and morphism `f : X ‚ü∂ Y`, `F.map f` is epic
iff `G.map f` is. -/
lemma CategoryTheory.Functor.epi_map_congr_iso {C D : Type*} [Category C] [Category D]
    {F G : C ‚•§ D} {X Y : C} (f : X ‚ü∂ Y) (Œ± : F ‚âÖ G) : Epi (F.map f) ‚Üî Epi (G.map f) := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ rw [‚Üê (IsIso.inv_comp_eq _).2 <| Œ±.hom.naturality f]; infer_instance
  ¬∑ rw [‚Üê (IsIso.inv_comp_eq _).2 <| Œ±.inv.naturality f]; infer_instance

/-- For any natural isomorphism `Œ± : F ‚âÖ G` and morphism `f : X ‚ü∂ Y`, `F.map f` is monic
iff `G.map f` is. -/
lemma CategoryTheory.Functor.mono_map_congr_iso {C D : Type*} [Category C] [Category D]
    {F G : C ‚•§ D} {X Y : C} (f : X ‚ü∂ Y) (Œ± : F ‚âÖ G) : Mono (F.map f) ‚Üî Mono (G.map f) := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ rw [‚Üê (IsIso.inv_comp_eq _).2 <| Œ±.hom.naturality f]; infer_instance
  ¬∑ rw [‚Üê (IsIso.inv_comp_eq _).2 <| Œ±.inv.naturality f]; infer_instance

end Misc

namespace CategoryTheory.Adjunction

variable {C D : Type*} [Category C] [Category D]
variable {F H : C ‚•§ D} {G : D ‚•§ C}
variable (adj‚ÇÅ : F ‚ä£ G) (adj‚ÇÇ : G ‚ä£ H)

section InnerFullyFaithful

variable [G.Full] [G.Faithful]

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `G` is fully faithful, the two natural transformations
`H ‚ãô G ‚ãô F ‚ü∂ F ‚ãô G ‚ãô H` obtained by following the whiskered counit and units of either
adjunction agree. Note that this is also true when `F` and `H` are fully faithful instead of `G`;
see `whiskered_counit_unit_eq_of_outer` for the corresponding variant of this lemma. -/
lemma whiskered_counit_unit_eq_of_inner :
    whiskerLeft H adj‚ÇÅ.counit ‚â´ H.rightUnitor.hom ‚â´ H.leftUnitor.inv ‚â´
    whiskerRight adj‚ÇÅ.unit H ‚â´ (Functor.associator _ _ _).hom =
    (Functor.associator _ _ _).inv ‚â´ whiskerRight adj‚ÇÇ.counit F ‚â´ F.leftUnitor.hom ‚â´
    F.rightUnitor.inv ‚â´ whiskerLeft F adj‚ÇÇ.unit := by
  ext X
  dsimp; simp only [Category.id_comp, Category.comp_id]
  refine (adj‚ÇÅ.counit_naturality <| (whiskerRight adj‚ÇÅ.unit H).app X).symm.trans ?_
  rw [whiskerRight_app, (asIso (adj‚ÇÇ.counit.app (G.obj _))).eq_comp_inv.2
      (adj‚ÇÇ.counit_naturality (adj‚ÇÅ.unit.app X)),
    ‚Üê (asIso _).comp_hom_eq_id.1 <| adj‚ÇÇ.left_triangle_components (F.obj X)]
  simp

/-- The natural transformation `H ‚ü∂ F` that exists for every adjoint triple `F ‚ä£ G ‚ä£ H` where `G`
is fully faithful, given here as the whiskered unit `H ‚ü∂ F ‚ãô G ‚ãô H` of the first adjunction
followed by the inverse of the whiskered unit `F ‚ü∂ F ‚ãô G ‚ãô H` of the second. -/
@[simps!]
noncomputable def HToF : H ‚ü∂ F :=
  H.leftUnitor.inv ‚â´ whiskerRight adj‚ÇÅ.unit H ‚â´ (Functor.associator _ _ _).hom ‚â´
  inv (whiskerLeft F adj‚ÇÇ.unit) ‚â´ F.rightUnitor.hom

/-- The natural transformation `H ‚ü∂ F` for an adjoint triple `F ‚ä£ G ‚ä£ H` with `G` fully faithful
is also equal to the inverse of the whiskered counit `H ‚ãô G ‚ãô F ‚ü∂ H` of the first adjunction
followed by the whiskered counit `H ‚ãô G ‚ãô F ‚ü∂ F` of the second. -/
lemma HToF_eq :
    HToF adj‚ÇÅ adj‚ÇÇ = H.rightUnitor.inv ‚â´ inv (whiskerLeft H adj‚ÇÅ.counit) ‚â´
    (Functor.associator _ _ _).inv ‚â´ whiskerRight adj‚ÇÇ.counit F ‚â´ F.leftUnitor.hom := by
  ext X; dsimp [HToF]
  simp only [NatIso.isIso_inv_app, Functor.comp_obj, Category.comp_id, Category.id_comp]
  rw [IsIso.comp_inv_eq]
  simpa using congr_app (whiskered_counit_unit_eq_of_inner adj‚ÇÅ adj‚ÇÇ) X

omit [G.Full] [G.Faithful] in
/-- For an adjoint triple `F ‚ä£ G ‚ä£ H`, the components of the natural transformation
`H ‚ãô G ‚ü∂ F ‚ãô G` obtained from the units and counits of the adjunctions are
under the second adjunction adjunct to the image of the unit of the first adjunction under `H`. -/
lemma homEquiv_counit_unit_app_eq_H_map_unit {X : C} :
    adj‚ÇÇ.homEquiv _ _ (adj‚ÇÇ.counit.app X ‚â´ adj‚ÇÅ.unit.app X) = H.map (adj‚ÇÅ.unit.app X) := by
  simp [Adjunction.homEquiv_apply]

omit [G.Full] [G.Faithful] in
/-- For an adjoint triple `F ‚ä£ G ‚ä£ H`, the components of the natural transformation
`H ‚ãô G ‚ü∂ F ‚ãô G` obtained from the units and counits of the adjunctions are
under the first adjunction adjunct to the image of the counit of the second adjunction under `F`. -/
lemma homEquiv_symm_counit_unit_app_eq_F_map_counit {X : C} :
    (adj‚ÇÅ.homEquiv _ _).symm (adj‚ÇÇ.counit.app X ‚â´ adj‚ÇÅ.unit.app X) = F.map (adj‚ÇÇ.counit.app X) := by
  simp [Adjunction.homEquiv_symm_apply]

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `G` is fully faithful, the components of the natural
transformation `H ‚ãô G ‚ü∂ F ‚ãô G` obtained from the units and counits of the adjunctions are simply
the images of the components of the natural transformation `H ‚ü∂ F` under `G`. -/
lemma counit_unit_app_eq_map_HToF {X : C} :
    adj‚ÇÇ.counit.app X ‚â´ adj‚ÇÅ.unit.app X = G.map ((HToF adj‚ÇÅ adj‚ÇÇ).app X) := by
  refine ((adj‚ÇÇ.homEquiv _ _).symm_apply_apply _).symm.trans ?_
  rw [homEquiv_counit_unit_app_eq_H_map_unit]; dsimp
  rw [Adjunction.homEquiv_symm_apply, ‚Üê Adjunction.inv_map_unit, ‚Üê G.map_inv,
    ‚Üê G.map_comp, HToF_app]

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `G` is fully faithful, the natural transformation
`H ‚ãô G ‚ü∂ F ‚ãô G` obtained from the units and counits of the adjunctions is simply the
natural transformation `H ‚ü∂ F` whiskered with `G`. -/
lemma counit_unit_eq_whiskerRight : adj‚ÇÇ.counit ‚â´ adj‚ÇÅ.unit = whiskerRight (HToF adj‚ÇÅ adj‚ÇÇ) G := by
  ext X; exact counit_unit_app_eq_map_HToF adj‚ÇÅ adj‚ÇÇ

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `G` is fully faithful, the natural transformation
`H ‚ü∂ F` is epic at `X` iff the image of the unit of the adjunction `F ‚ä£ G` under `H` is. -/
lemma HToF_app_epi_iff_map_unit_app_epi {X : C} :
    Epi ((HToF adj‚ÇÅ adj‚ÇÇ).app X) ‚Üî Epi (H.map (adj‚ÇÅ.unit.app X)) := by
  rw [‚Üê epi_isIso_comp_iff (H.map (adj‚ÇÇ.counit.app _)) (H.map (adj‚ÇÅ.unit.app _)),
    ‚Üê H.map_comp, counit_unit_app_eq_map_HToF]
  exact Functor.epi_map_congr_iso _ (asIso (adj‚ÇÇ.unit))

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `G` is fully faithful and its codomain has
all pushouts, the natural transformation `H ‚ü∂ F` is epic iff the unit of the adjunction `F ‚ä£ G`
whiskered with `H` is. -/
lemma HToF_epi_iff_whiskerRight_unit_epi [HasPushouts D] :
    Epi (HToF adj‚ÇÅ adj‚ÇÇ) ‚Üî Epi (whiskerRight adj‚ÇÅ.unit H) := by
  repeat rw [NatTrans.epi_iff_epi_app]
  exact forall_congr' fun _ ‚Ü¶ adj‚ÇÅ.HToF_app_epi_iff_map_unit_app_epi adj‚ÇÇ

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `G` is fully faithful and `H` preserves epimorphisms
(which is for example the case if `H` has a further right adjoint), the components of the natural
transformation `H ‚ü∂ F` are epic iff the respective components of the natural transformation
`H ‚ãô G ‚ü∂ F ‚ãô G` obtained from the units and counits of the adjunctions are. -/
lemma HToF_app_epi_iff_counit_unit_app_epi [H.PreservesEpimorphisms] {X : C} :
    Epi ((HToF adj‚ÇÅ adj‚ÇÇ).app X) ‚Üî Epi (adj‚ÇÇ.counit.app X ‚â´ adj‚ÇÅ.unit.app X) := by
  have _ := adj‚ÇÇ.isLeftAdjoint
  refine ‚ü®fun h ‚Ü¶ by rw [counit_unit_app_eq_map_HToF]; exact G.map_epi _, fun h ‚Ü¶ ?_‚ü©
  rw [HToF_app, ‚Üê homEquiv_counit_unit_app_eq_H_map_unit adj‚ÇÅ adj‚ÇÇ, adj‚ÇÇ.homEquiv_apply]
  infer_instance

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `G` is fully faithful, `H` preserves epimorphisms
(which is for example the case if `H` has a further right adjoint) and both categories have
all pushouts, the natural transformation `H ‚ü∂ F` is epic iff the natural transformation
`H ‚ãô G ‚ü∂ F ‚ãô G` obtained from the units and counits of the adjunctions is. -/
lemma HToF_epi_iff_counit_unit_epi [HasPushouts C] [HasPushouts D] [H.PreservesEpimorphisms] :
    Epi (HToF adj‚ÇÅ adj‚ÇÇ) ‚Üî Epi (adj‚ÇÇ.counit ‚â´ adj‚ÇÅ.unit) := by
  repeat rw [NatTrans.epi_iff_epi_app]
  exact forall_congr' fun _ ‚Ü¶ adj‚ÇÅ.HToF_app_epi_iff_counit_unit_app_epi adj‚ÇÇ

end InnerFullyFaithful

section OuterFullyFaithful

variable [F.Full] [F.Faithful] [H.Full] [H.Faithful]

omit [F.Full] [F.Faithful] in
/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `F` and `H` are fully faithful, the two natural
transformations `H ‚ãô G ‚ãô F ‚ü∂ F ‚ãô G ‚ãô H` obtained by following the whiskered counit and unit
of either adjunction agree. Note that this is also true when `G` is fully faithful instead of `F`
and `H`; see `whiskered_counit_unit_eq_of_inner` for the corresponding variant of this lemma. -/
lemma whiskered_counit_unit_eq_of_outer :
    whiskerLeft H adj‚ÇÅ.counit ‚â´ H.rightUnitor.hom ‚â´ H.leftUnitor.inv ‚â´
    whiskerRight adj‚ÇÅ.unit H ‚â´ (Functor.associator _ _ _).hom =
    (Functor.associator _ _ _).inv ‚â´ whiskerRight adj‚ÇÇ.counit F ‚â´ F.leftUnitor.hom ‚â´
    F.rightUnitor.inv ‚â´ whiskerLeft F adj‚ÇÇ.unit := by
  ext X
  dsimp; simp only [Category.id_comp, Category.comp_id]
  refine (adj‚ÇÅ.counit_naturality <| (whiskerRight adj‚ÇÅ.unit H).app X).symm.trans ?_
  rw [whiskerRight_app, (asIso (adj‚ÇÇ.counit.app (G.obj _))).eq_comp_inv.2
      (adj‚ÇÇ.counit_naturality (adj‚ÇÅ.unit.app X)),
    ‚Üê (asIso _).comp_hom_eq_id.1 <| adj‚ÇÇ.left_triangle_components (F.obj X)]
  simp

/-- The natural transformation `F ‚ü∂ H` that exists for every adjoint triple `F ‚ä£ G ‚ä£ H` where `F`
and `H` are fully faithful, given here as the whiskered unit `F ‚ü∂ F ‚ãô G ‚ãô H` of the second
adjunction followed by the inverse of the whiskered unit `F ‚ãô G ‚ãô H ‚ü∂ H` of the first. -/
@[simps!]
noncomputable def FToH : F ‚ü∂ H :=
  F.rightUnitor.inv ‚â´ whiskerLeft F adj‚ÇÇ.unit ‚â´ (Functor.associator _ _ _).inv ‚â´
  inv (whiskerRight adj‚ÇÅ.unit H) ‚â´ H.leftUnitor.hom

/-- The natural transformation `F ‚ü∂ H` for an adjoint triple `F ‚ä£ G ‚ä£ H` with `F` and `H`
fully faithful is also equal to the inverse of the whiskered counit `H ‚ãô G ‚ãô F ‚ü∂ F` of the second
adjunction followed by the whiskered counit `H ‚ãô G ‚ãô F ‚ü∂ H` of the first. -/
lemma FToH_eq :
    FToH adj‚ÇÅ adj‚ÇÇ = F.leftUnitor.inv ‚â´ inv (whiskerRight adj‚ÇÇ.counit F) ‚â´
    (Functor.associator _ _ _).hom ‚â´ whiskerLeft H adj‚ÇÅ.counit ‚â´ H.rightUnitor.hom := by
  ext X; dsimp [FToH]
  simp only [NatIso.isIso_inv_app, Functor.comp_obj, Category.comp_id, Category.id_comp]
  rw [IsIso.comp_inv_eq]
  simpa using congr_app (whiskered_counit_unit_eq_of_outer adj‚ÇÅ adj‚ÇÇ).symm X

omit [F.Full] [F.Faithful] [H.Full] [H.Faithful] in
/-- For an adjoint triple `F ‚ä£ G ‚ä£ H`, the components of the natural transformation
`G ‚ãô F ‚ü∂ G ‚ãô H` obtained from the units and counits of the adjunctions are
under the first adjunction adjunct to the image of the unit of the second adjunction under `G`. -/
lemma homEquiv_counit_unit_app_eq_G_map_unit {X : D} :
    adj‚ÇÅ.homEquiv _ _ (adj‚ÇÅ.counit.app X ‚â´ adj‚ÇÇ.unit.app X) = G.map (adj‚ÇÇ.unit.app X) := by
  simp [homEquiv_apply]

omit [F.Full] [F.Faithful] [H.Full] [H.Faithful] in
/-- For an adjoint triple `F ‚ä£ G ‚ä£ H`, the components of the natural transformation
`G ‚ãô F ‚ü∂ G ‚ãô H` obtained from the units and counits of the adjunctions are
under the second adjunction adjunct to the image of the counit of the first adjunction under `G`. -/
lemma homEquiv_symm_counit_unit_app_eq_G_map_counit {X : D} :
    (adj‚ÇÇ.homEquiv _ _).symm (adj‚ÇÅ.counit.app X ‚â´ adj‚ÇÇ.unit.app X) = G.map (adj‚ÇÅ.counit.app X) := by
  simp [homEquiv_symm_apply]

omit [H.Full] [H.Faithful] in
/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `F` and `H` are fully faithful, the components of the
natural transformation `G ‚ãô F ‚ü∂ G ‚ãô H` obtained from the units and counits of the adjunctions
are simply the components of the natural transformation `F ‚ü∂ H` at `G`. -/
lemma counit_unit_app_eq_FToH_app {X : D} :
    adj‚ÇÅ.counit.app X ‚â´ adj‚ÇÇ.unit.app X = (FToH adj‚ÇÅ adj‚ÇÇ).app (G.obj X) := by
  refine ((adj‚ÇÇ.homEquiv _ _).apply_symm_apply _).symm.trans ?_
  rw [homEquiv_symm_counit_unit_app_eq_G_map_counit, adj‚ÇÇ.homEquiv_apply, FToH_app, ‚Üê H.map_inv]
  congr
  exact IsIso.eq_inv_of_hom_inv_id (adj‚ÇÅ.right_triangle_components _)

omit [H.Full] [H.Faithful] in
/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `F` and `H` are fully faithful, the natural
transformation `G ‚ãô F ‚ü∂ G ‚ãô H` obtained from the units and counits of the adjunctions is simply
the natural transformation `F ‚ü∂ H` whiskered from the left with `G`. -/
lemma counit_unit_eq_whiskerLeft : adj‚ÇÅ.counit ‚â´ adj‚ÇÇ.unit = whiskerLeft G (FToH adj‚ÇÅ adj‚ÇÇ) := by
  ext X; exact counit_unit_app_eq_FToH_app adj‚ÇÅ adj‚ÇÇ

omit [H.Full] [H.Faithful] in
/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `F` and `H` are fully faithful, the natural
transformation `F ‚ü∂ H` is monic at `X` iff the unit of the adjunction `G ‚ä£ H` is monic
at `F.obj X`. -/
lemma FToH_app_mono_iff_unit_app_mono {X : C} :
    Mono ((FToH adj‚ÇÅ adj‚ÇÇ).app X) ‚Üî Mono (adj‚ÇÇ.unit.app (F.obj X)) := by
  rw [‚Üê mono_isIso_comp_iff (adj‚ÇÅ.counit.app _) (adj‚ÇÇ.unit.app _), counit_unit_app_eq_FToH_app]
  exact NatTrans.mono_app_congr_iso (asIso (adj‚ÇÅ.unit.app X))

omit [H.Full] [H.Faithful] in
/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `F` and `H` are fully faithful and their codomain has
all pullbacks, the natural transformation `F ‚ü∂ H` is monic iff `F` whiskered with the unit of the
adjunction `G ‚ä£ H` is. -/
lemma FToH_mono_iff_whiskerLeft_unit_mono [HasPullbacks D] :
    Mono (FToH adj‚ÇÅ adj‚ÇÇ) ‚Üî Mono (whiskerLeft F adj‚ÇÇ.unit) := by
  repeat rw [NatTrans.mono_iff_mono_app]
  exact forall_congr' fun _ ‚Ü¶ adj‚ÇÅ.FToH_app_mono_iff_unit_app_mono adj‚ÇÇ

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `F` and `H` are fully faithful, all components of the
natural transformation `F ‚ü∂ H` are monic iff all components of the natural transformation
`G ‚ãô F ‚ü∂ G ‚ãô H` obtained from the units and counits of the adjunctions are.
Note that unlike `HToF_app_epi_iff_counit_unit_app_epi`, this equivalence does not make sense on a
per-object basis because the components of the two natural transformations are indexed by different
categories. -/
lemma FToH_app_mono_iff_counit_unit_app_mono :
    (‚àÄ X, Mono ((FToH adj‚ÇÅ adj‚ÇÇ).app X)) ‚Üî ‚àÄ X, Mono (adj‚ÇÅ.counit.app X ‚â´ adj‚ÇÇ.unit.app X) := by
  refine ‚ü®fun h X ‚Ü¶ by rw [counit_unit_app_eq_FToH_app]; exact h _, fun h X ‚Ü¶ ?_‚ü©
  specialize h (H.obj X)
  rw [counit_unit_app_eq_FToH_app] at h
  exact (NatTrans.mono_app_congr_iso (asIso (adj‚ÇÇ.counit.app X))).1 h

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `F` and `H` are fully faithful and their codomain has
all pullbacks, the natural transformation `F ‚ü∂ H` is monic iff the natural transformation
`G ‚ãô F ‚ü∂ G ‚ãô H` obtained from the units and counits of the adjunctions is. -/
lemma FToH_mono_iff_counit_unit_mono [HasPullbacks D] :
    Mono (FToH adj‚ÇÅ adj‚ÇÇ) ‚Üî Mono (adj‚ÇÅ.counit ‚â´ adj‚ÇÇ.unit) := by
  repeat rw [NatTrans.mono_iff_mono_app]
  exact adj‚ÇÅ.FToH_app_mono_iff_counit_unit_app_mono adj‚ÇÇ

end OuterFullyFaithful

end CategoryTheory.Adjunction
