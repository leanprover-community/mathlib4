/-
Copyright (c) 2024 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson, Ben Eltschig
-/
import Mathlib.CategoryTheory.Adjunction.Unique
import Mathlib.CategoryTheory.Limits.FunctorCategory.EpiMono
import Mathlib.CategoryTheory.Monad.Adjunction
/-!

# Adjoint triples

This file concerns adjoint triples `F ‚ä£ G ‚ä£ H` of functors `F H : C ‚•§ D`, `G : D ‚•§ C`. We first
prove that `F` is fully faithful iff `H` is, and then prove results about the two special cases
where `G` is fully faithful or `F` and `H` are.

## Main results

All results are about an adjoint triple `F ‚ä£ G ‚ä£ H` where `adj‚ÇÅ : F ‚ä£ G` and `adj‚ÇÇ : G ‚ä£ H`.
* `fullyFaithfulEquiv`: `F` is fully faithful iff `H` is.
* `HToF`: the canonical natural transformation `H ‚ü∂ F` that exists whenever `G` is fully faithful.
  This is defined in terms of the units of the adjunctions, but a formula in terms of the counits
  is also given.
* `counit_unit_eq_whiskerRight`: when `G` is fully faithful, the natural transformation
  `H ‚ãô G ‚ü∂ F ‚ãô G` given by `adj‚ÇÇ.counit ‚â´ adj‚ÇÅ.unit` is just `HToF` whiskered with `G`.
* `HToF_epi_iff_whiskerRight_unit_epi`: assuming `D` has all pushouts, `HToF : H ‚ü∂ F` is epic iff
  the whiskering `H ‚ü∂ F ‚ãô G ‚ãô H` of `adj‚ÇÅ.unit` and `H` is. For the components this holds even
  without assumptions on `D`.
* `HToF_epi_iff_counit_unit_epi`: assuming `D` has all pushouts, `HToF : H ‚ü∂ F` is epic iff
  `adj‚ÇÇ.counit ‚â´ adj‚ÇÅ.unit : H ‚ãô G ‚ü∂ F ‚ãô G` is. For the components this holds even without
  assumptions on `D`.
* `FToH`: the canonical natural transformation `F ‚ü∂ H` that exists whenever `F` and `G` are fully
  faithful. This is defined in terms of the units of the adjunctions, but a formula in terms of the
  counits is also given.
* `counit_unit_eq_whiskerLeft`: when `F` and `H` are fully faithful, the natural transformation
  `G ‚ãô F ‚ü∂ G ‚ãô H` given by `adj‚ÇÅ.counit ‚â´ adj‚ÇÇ.unit` is just `G` whiskered with `FToH`.
* `FToH_mono_iff_whiskerLeft_unit_mono`: assuming `D` has all pullbacks, `FToH : F ‚ü∂ H` is monic iff
  the whiskering `F ‚ü∂ F ‚ãô G ‚ãô H` of `F` and `adj‚ÇÇ.unit` is. For the components this holds even
  without assumptions on `D`.
* `FToH_mono_iff_counit_unit_mono`: assuming `D` has all pullbacks, `FToH : H ‚ü∂ F` is monic iff
  `adj‚ÇÅ.counit ‚â´ adj‚ÇÇ.unit : G ‚ãô F ‚ü∂ G ‚ãô H` is. For the components this holds even without
  assumptions on `D`.
-/

open CategoryTheory Limits

variable {C D : Type*} [Category C] [Category D]
variable (F : C ‚•§ D) (G : D ‚•§ C) (H : C ‚•§ D)

/-- Structure containing the two adjunctions of an adjoint triple `F ‚ä£ G ‚ä£ H`. -/
structure CategoryTheory.Adjunction.Triple where
  /- Adjunction `F ‚ä£ G` of the adjoint triple `F ‚ä£ G ‚ä£ H`. -/
  adj‚ÇÅ : F ‚ä£ G
  /- Adjunction `G ‚ä£ H` of the adjoint triple `F ‚ä£ G ‚ä£ H`. -/
  adj‚ÇÇ : G ‚ä£ H

namespace CategoryTheory.Adjunction.Triple

variable {F G H} (t : Triple F G H)

lemma isIso_unit_iff_isIso_counit : IsIso t.adj‚ÇÅ.unit ‚Üî IsIso t.adj‚ÇÇ.counit := by
  let adj : F ‚ãô G ‚ä£ H ‚ãô G := t.adj‚ÇÅ.comp t.adj‚ÇÇ
  constructor
  ¬∑ intro h
    let idAdj : ùü≠ C ‚ä£ H ‚ãô G := adj.ofNatIsoLeft (asIso t.adj‚ÇÅ.unit).symm
    exact t.adj‚ÇÇ.isIso_counit_of_iso (idAdj.rightAdjointUniq id)
  ¬∑ intro h
    let adjId : F ‚ãô G ‚ä£ ùü≠ C := adj.ofNatIsoRight (asIso t.adj‚ÇÇ.counit)
    exact t.adj‚ÇÅ.isIso_unit_of_iso (adjId.leftAdjointUniq id)

/--
Given an adjoint triple `F ‚ä£ G ‚ä£ H`, the left adjoint `F` is fully faithful if and only if the
right adjoint `H` is fully faithful.
-/
noncomputable def fullyFaithfulEquiv : F.FullyFaithful ‚âÉ H.FullyFaithful where
  toFun h :=
    haveI := h.full
    haveI := h.faithful
    haveI : IsIso t.adj‚ÇÇ.counit := by
      rw [‚Üê t.isIso_unit_iff_isIso_counit]
      infer_instance
    t.adj‚ÇÇ.fullyFaithfulROfIsIsoCounit
  invFun h :=
    haveI := h.full
    haveI := h.faithful
    haveI : IsIso t.adj‚ÇÅ.unit := by
      rw [t.isIso_unit_iff_isIso_counit]
      infer_instance
    t.adj‚ÇÅ.fullyFaithfulLOfIsIsoUnit
  left_inv _ := Subsingleton.elim _ _
  right_inv _ := Subsingleton.elim _ _

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H`, the components of the natural transformation
`H ‚ãô G ‚ü∂ F ‚ãô G` obtained from the units and counits of the adjunctions are
under the second adjunction adjunct to the image of the unit of the first adjunction under `H`. -/
lemma homEquiv_counit_unit_app_eq_H_map_unit {X : C} :
    t.adj‚ÇÇ.homEquiv _ _ (t.adj‚ÇÇ.counit.app X ‚â´ t.adj‚ÇÅ.unit.app X) = H.map (t.adj‚ÇÅ.unit.app X) := by
  simp [Adjunction.homEquiv_apply]

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H`, the components of the natural transformation
`H ‚ãô G ‚ü∂ F ‚ãô G` obtained from the units and counits of the adjunctions are
under the first adjunction adjunct to the image of the counit of the second adjunction under `F`. -/
lemma homEquiv_symm_counit_unit_app_eq_F_map_counit {X : C} :
    (t.adj‚ÇÅ.homEquiv _ _).symm (t.adj‚ÇÇ.counit.app X ‚â´ t.adj‚ÇÅ.unit.app X) =
      F.map (t.adj‚ÇÇ.counit.app X) := by
  simp [Adjunction.homEquiv_symm_apply]

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H`, the components of the natural transformation
`G ‚ãô F ‚ü∂ G ‚ãô H` obtained from the units and counits of the adjunctions are
under the first adjunction adjunct to the image of the unit of the second adjunction under `G`. -/
lemma homEquiv_counit_unit_app_eq_G_map_unit {X : D} :
    t.adj‚ÇÅ.homEquiv _ _ (t.adj‚ÇÅ.counit.app X ‚â´ t.adj‚ÇÇ.unit.app X) = G.map (t.adj‚ÇÇ.unit.app X) := by
  simp [homEquiv_apply]

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H`, the components of the natural transformation
`G ‚ãô F ‚ü∂ G ‚ãô H` obtained from the units and counits of the adjunctions are
under the second adjunction adjunct to the image of the counit of the first adjunction under `G`. -/
lemma homEquiv_symm_counit_unit_app_eq_G_map_counit {X : D} :
    (t.adj‚ÇÇ.homEquiv _ _).symm (t.adj‚ÇÅ.counit.app X ‚â´ t.adj‚ÇÇ.unit.app X) =
      G.map (t.adj‚ÇÅ.counit.app X) := by
  simp [homEquiv_symm_apply]

section InnerFullyFaithful

variable [G.Full] [G.Faithful]

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `G` is fully faithful, the two natural transformations
`H ‚ãô G ‚ãô F ‚ü∂ F ‚ãô G ‚ãô H` obtained by following the whiskered counit and units of either
adjunction agree. Note that this is also true when `F` and `H` are fully faithful instead of `G`;
see `whiskered_counit_unit_eq_of_outer` for the corresponding variant of this lemma. -/
lemma whiskered_counit_unit_eq_of_inner :
    whiskerLeft H t.adj‚ÇÅ.counit ‚â´ H.rightUnitor.hom ‚â´ H.leftUnitor.inv ‚â´
    whiskerRight t.adj‚ÇÅ.unit H ‚â´ (Functor.associator _ _ _).hom =
    (Functor.associator _ _ _).inv ‚â´ whiskerRight t.adj‚ÇÇ.counit F ‚â´ F.leftUnitor.hom ‚â´
    F.rightUnitor.inv ‚â´ whiskerLeft F t.adj‚ÇÇ.unit := by
  ext X
  dsimp; simp only [Category.id_comp, Category.comp_id]
  refine (t.adj‚ÇÅ.counit_naturality <| (whiskerRight t.adj‚ÇÅ.unit H).app X).symm.trans ?_
  rw [whiskerRight_app, (asIso (t.adj‚ÇÇ.counit.app (G.obj _))).eq_comp_inv.2
      (t.adj‚ÇÇ.counit_naturality (t.adj‚ÇÅ.unit.app X)),
    ‚Üê (asIso _).comp_hom_eq_id.1 <| t.adj‚ÇÇ.left_triangle_components (F.obj X)]
  simp

/-- The natural transformation `H ‚ü∂ F` that exists for every adjoint triple `F ‚ä£ G ‚ä£ H` where `G`
is fully faithful, given here as the whiskered unit `H ‚ü∂ F ‚ãô G ‚ãô H` of the first adjunction
followed by the inverse of the whiskered unit `F ‚ü∂ F ‚ãô G ‚ãô H` of the second. -/
@[simps!]
noncomputable def HToF : H ‚ü∂ F :=
  H.leftUnitor.inv ‚â´ whiskerRight t.adj‚ÇÅ.unit H ‚â´ (Functor.associator _ _ _).hom ‚â´
  inv (whiskerLeft F t.adj‚ÇÇ.unit) ‚â´ F.rightUnitor.hom

/-- The natural transformation `H ‚ü∂ F` for an adjoint triple `F ‚ä£ G ‚ä£ H` with `G` fully faithful
is also equal to the inverse of the whiskered counit `H ‚ãô G ‚ãô F ‚ü∂ H` of the first adjunction
followed by the whiskered counit `H ‚ãô G ‚ãô F ‚ü∂ F` of the second. -/
lemma HToF_eq_counits :
    t.HToF = H.rightUnitor.inv ‚â´ inv (whiskerLeft H t.adj‚ÇÅ.counit) ‚â´
    (Functor.associator _ _ _).inv ‚â´ whiskerRight t.adj‚ÇÇ.counit F ‚â´ F.leftUnitor.hom := by
  ext X; dsimp [HToF]
  simp only [NatIso.isIso_inv_app, Functor.comp_obj, Category.comp_id, Category.id_comp]
  rw [IsIso.comp_inv_eq]
  simpa using congr_app t.whiskered_counit_unit_eq_of_inner X

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `G` is fully faithful, the components of the natural
transformation `H ‚ãô G ‚ü∂ F ‚ãô G` obtained from the units and counits of the adjunctions are simply
the images of the components of the natural transformation `H ‚ü∂ F` under `G`. -/
lemma counit_unit_app_eq_map_HToF {X : C} :
    t.adj‚ÇÇ.counit.app X ‚â´ t.adj‚ÇÅ.unit.app X = G.map (t.HToF.app X) := by
  refine ((t.adj‚ÇÇ.homEquiv _ _).symm_apply_apply _).symm.trans ?_
  rw [homEquiv_counit_unit_app_eq_H_map_unit]; dsimp
  rw [Adjunction.homEquiv_symm_apply, ‚Üê Adjunction.inv_map_unit, ‚Üê G.map_inv,
    ‚Üê G.map_comp, HToF_app]

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `G` is fully faithful, the natural transformation
`H ‚ãô G ‚ü∂ F ‚ãô G` obtained from the units and counits of the adjunctions is simply the
natural transformation `H ‚ü∂ F` whiskered with `G`. -/
lemma counit_unit_eq_whiskerRight : t.adj‚ÇÇ.counit ‚â´ t.adj‚ÇÅ.unit = whiskerRight t.HToF G := by
  ext X; exact t.counit_unit_app_eq_map_HToF

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `G` is fully faithful, the natural transformation
`H ‚ü∂ F` is epic at `X` iff the image of the unit of the adjunction `F ‚ä£ G` under `H` is. -/
lemma HToF_app_epi_iff_map_unit_app_epi {X : C} :
    Epi (t.HToF.app X) ‚Üî Epi (H.map (t.adj‚ÇÅ.unit.app X)) := by
  rw [‚Üê epi_isIso_comp_iff (H.map (t.adj‚ÇÇ.counit.app _)) (H.map (t.adj‚ÇÅ.unit.app _)),
    ‚Üê H.map_comp, counit_unit_app_eq_map_HToF]
  exact Functor.epi_map_congr_iso _ (asIso t.adj‚ÇÇ.unit)

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `G` is fully faithful and its codomain has
all pushouts, the natural transformation `H ‚ü∂ F` is epic iff the unit of the adjunction `F ‚ä£ G`
whiskered with `H` is. -/
lemma HToF_epi_iff_whiskerRight_unit_epi [HasPushouts D] :
    Epi t.HToF ‚Üî Epi (whiskerRight t.adj‚ÇÅ.unit H) := by
  repeat rw [NatTrans.epi_iff_epi_app]
  exact forall_congr' fun _ ‚Ü¶ t.HToF_app_epi_iff_map_unit_app_epi

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `G` is fully faithful and `H` preserves epimorphisms
(which is for example the case if `H` has a further right adjoint), the components of the natural
transformation `H ‚ü∂ F` are epic iff the respective components of the natural transformation
`H ‚ãô G ‚ü∂ F ‚ãô G` obtained from the units and counits of the adjunctions are. -/
lemma HToF_app_epi_iff_counit_unit_app_epi [H.PreservesEpimorphisms] {X : C} :
    Epi (t.HToF.app X) ‚Üî Epi (t.adj‚ÇÇ.counit.app X ‚â´ t.adj‚ÇÅ.unit.app X) := by
  have _ := t.adj‚ÇÇ.isLeftAdjoint
  refine ‚ü®fun h ‚Ü¶ by rw [counit_unit_app_eq_map_HToF]; exact G.map_epi _, fun h ‚Ü¶ ?_‚ü©
  rw [HToF_app, ‚Üê t.homEquiv_counit_unit_app_eq_H_map_unit, t.adj‚ÇÇ.homEquiv_apply]
  infer_instance

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `G` is fully faithful, `H` preserves epimorphisms
(which is for example the case if `H` has a further right adjoint) and both categories have
all pushouts, the natural transformation `H ‚ü∂ F` is epic iff the natural transformation
`H ‚ãô G ‚ü∂ F ‚ãô G` obtained from the units and counits of the adjunctions is. -/
lemma HToF_epi_iff_counit_unit_epi [HasPushouts C] [HasPushouts D] [H.PreservesEpimorphisms] :
    Epi t.HToF ‚Üî Epi (t.adj‚ÇÇ.counit ‚â´ t.adj‚ÇÅ.unit) := by
  repeat rw [NatTrans.epi_iff_epi_app]
  exact forall_congr' fun _ ‚Ü¶ t.HToF_app_epi_iff_counit_unit_app_epi

end InnerFullyFaithful

section OuterFullyFaithful

variable [F.Full] [F.Faithful] [H.Full] [H.Faithful]

omit [F.Full] [F.Faithful] in
/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `F` and `H` are fully faithful, the two natural
transformations `H ‚ãô G ‚ãô F ‚ü∂ F ‚ãô G ‚ãô H` obtained by following the whiskered counit and unit
of either adjunction agree. Note that this is also true when `G` is fully faithful instead of `F`
and `H`; see `whiskered_counit_unit_eq_of_inner` for the corresponding variant of this lemma. -/
lemma whiskered_counit_unit_eq_of_outer :
    whiskerLeft H t.adj‚ÇÅ.counit ‚â´ H.rightUnitor.hom ‚â´ H.leftUnitor.inv ‚â´
    whiskerRight t.adj‚ÇÅ.unit H ‚â´ (Functor.associator _ _ _).hom =
    (Functor.associator _ _ _).inv ‚â´ whiskerRight t.adj‚ÇÇ.counit F ‚â´ F.leftUnitor.hom ‚â´
    F.rightUnitor.inv ‚â´ whiskerLeft F t.adj‚ÇÇ.unit := by
  ext X
  dsimp; simp only [Category.id_comp, Category.comp_id]
  refine (t.adj‚ÇÅ.counit_naturality <| (whiskerRight t.adj‚ÇÅ.unit H).app X).symm.trans ?_
  rw [whiskerRight_app, (asIso (t.adj‚ÇÇ.counit.app (G.obj _))).eq_comp_inv.2
      (t.adj‚ÇÇ.counit_naturality (t.adj‚ÇÅ.unit.app X)),
    ‚Üê (asIso _).comp_hom_eq_id.1 <| t.adj‚ÇÇ.left_triangle_components (F.obj X)]
  simp

/-- The natural transformation `F ‚ü∂ H` that exists for every adjoint triple `F ‚ä£ G ‚ä£ H` where `F`
and `H` are fully faithful, given here as the whiskered unit `F ‚ü∂ F ‚ãô G ‚ãô H` of the second
adjunction followed by the inverse of the whiskered unit `F ‚ãô G ‚ãô H ‚ü∂ H` of the first. -/
@[simps!]
noncomputable def FToH : F ‚ü∂ H :=
  F.rightUnitor.inv ‚â´ whiskerLeft F t.adj‚ÇÇ.unit ‚â´ (Functor.associator _ _ _).inv ‚â´
  inv (whiskerRight t.adj‚ÇÅ.unit H) ‚â´ H.leftUnitor.hom

/-- The natural transformation `F ‚ü∂ H` for an adjoint triple `F ‚ä£ G ‚ä£ H` with `F` and `H`
fully faithful is also equal to the inverse of the whiskered counit `H ‚ãô G ‚ãô F ‚ü∂ F` of the second
adjunction followed by the whiskered counit `H ‚ãô G ‚ãô F ‚ü∂ H` of the first. -/
lemma FToH_eq_counits :
    t.FToH = F.leftUnitor.inv ‚â´ inv (whiskerRight t.adj‚ÇÇ.counit F) ‚â´
    (Functor.associator _ _ _).hom ‚â´ whiskerLeft H t.adj‚ÇÅ.counit ‚â´ H.rightUnitor.hom := by
  ext X; dsimp [FToH]
  simp only [NatIso.isIso_inv_app, Functor.comp_obj, Category.comp_id, Category.id_comp]
  rw [IsIso.comp_inv_eq]
  simpa using congr_app t.whiskered_counit_unit_eq_of_outer.symm X

omit [H.Full] [H.Faithful] in
/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `F` and `H` are fully faithful, the components of the
natural transformation `G ‚ãô F ‚ü∂ G ‚ãô H` obtained from the units and counits of the adjunctions
are simply the components of the natural transformation `F ‚ü∂ H` at `G`. -/
lemma counit_unit_app_eq_FToH_app {X : D} :
    t.adj‚ÇÅ.counit.app X ‚â´ t.adj‚ÇÇ.unit.app X = t.FToH.app (G.obj X) := by
  refine ((t.adj‚ÇÇ.homEquiv _ _).apply_symm_apply _).symm.trans ?_
  rw [homEquiv_symm_counit_unit_app_eq_G_map_counit, t.adj‚ÇÇ.homEquiv_apply, FToH_app, ‚Üê H.map_inv]
  congr
  exact IsIso.eq_inv_of_hom_inv_id (t.adj‚ÇÅ.right_triangle_components _)

omit [H.Full] [H.Faithful] in
/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `F` and `H` are fully faithful, the natural
transformation `G ‚ãô F ‚ü∂ G ‚ãô H` obtained from the units and counits of the adjunctions is simply
the natural transformation `F ‚ü∂ H` whiskered from the left with `G`. -/
lemma counit_unit_eq_whiskerLeft : t.adj‚ÇÅ.counit ‚â´ t.adj‚ÇÇ.unit = whiskerLeft G t.FToH := by
  ext X; exact t.counit_unit_app_eq_FToH_app

omit [H.Full] [H.Faithful] in
/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `F` and `H` are fully faithful, the natural
transformation `F ‚ü∂ H` is monic at `X` iff the unit of the adjunction `G ‚ä£ H` is monic
at `F.obj X`. -/
lemma FToH_app_mono_iff_unit_app_mono {X : C} :
    Mono (t.FToH.app X) ‚Üî Mono (t.adj‚ÇÇ.unit.app (F.obj X)) := by
  rw [‚Üê mono_isIso_comp_iff (t.adj‚ÇÅ.counit.app _) (t.adj‚ÇÇ.unit.app _), counit_unit_app_eq_FToH_app]
  exact NatTrans.mono_app_congr_iso (asIso (t.adj‚ÇÅ.unit.app X))

omit [H.Full] [H.Faithful] in
/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `F` and `H` are fully faithful and their codomain has
all pullbacks, the natural transformation `F ‚ü∂ H` is monic iff `F` whiskered with the unit of the
adjunction `G ‚ä£ H` is. -/
lemma FToH_mono_iff_whiskerLeft_unit_mono [HasPullbacks D] :
    Mono t.FToH ‚Üî Mono (whiskerLeft F t.adj‚ÇÇ.unit) := by
  repeat rw [NatTrans.mono_iff_mono_app]
  exact forall_congr' fun _ ‚Ü¶ t.FToH_app_mono_iff_unit_app_mono

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `F` and `H` are fully faithful, all components of the
natural transformation `F ‚ü∂ H` are monic iff all components of the natural transformation
`G ‚ãô F ‚ü∂ G ‚ãô H` obtained from the units and counits of the adjunctions are.
Note that unlike `HToF_app_epi_iff_counit_unit_app_epi`, this equivalence does not make sense on a
per-object basis because the components of the two natural transformations are indexed by different
categories. -/
lemma FToH_app_mono_iff_counit_unit_app_mono :
    (‚àÄ X, Mono (t.FToH.app X)) ‚Üî ‚àÄ X, Mono (t.adj‚ÇÅ.counit.app X ‚â´ t.adj‚ÇÇ.unit.app X) := by
  refine ‚ü®fun h X ‚Ü¶ by rw [counit_unit_app_eq_FToH_app]; exact h _, fun h X ‚Ü¶ ?_‚ü©
  specialize h (H.obj X)
  rw [counit_unit_app_eq_FToH_app] at h
  exact (NatTrans.mono_app_congr_iso (asIso (t.adj‚ÇÇ.counit.app X))).1 h

/-- For an adjoint triple `F ‚ä£ G ‚ä£ H` where `F` and `H` are fully faithful and their codomain has
all pullbacks, the natural transformation `F ‚ü∂ H` is monic iff the natural transformation
`G ‚ãô F ‚ü∂ G ‚ãô H` obtained from the units and counits of the adjunctions is. -/
lemma FToH_mono_iff_counit_unit_mono [HasPullbacks D] :
    Mono t.FToH ‚Üî Mono (t.adj‚ÇÅ.counit ‚â´ t.adj‚ÇÇ.unit) := by
  repeat rw [NatTrans.mono_iff_mono_app]
  exact t.FToH_app_mono_iff_counit_unit_app_mono

end OuterFullyFaithful

end CategoryTheory.Adjunction.Triple
