/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Reid Barton, Johan Commelin
-/
import Mathlib.CategoryTheory.Adjunction.Basic
import Mathlib.CategoryTheory.Limits.Creates

/-!
# Adjunctions and limits

A left adjoint preserves colimits (`CategoryTheory.Adjunction.leftAdjoint_preservesColimits`),
and a right adjoint preserves limits (`CategoryTheory.Adjunction.rightAdjoint_preservesLimits`).

Equivalences create and reflect (co)limits.
(`CategoryTheory.Functor.createsLimitsOfIsEquivalence`,
`CategoryTheory.Functor.createsColimitsOfIsEquivalence`,
`CategoryTheory.Functor.reflectsLimits_of_isEquivalence`,
`CategoryTheory.Functor.reflectsColimits_of_isEquivalence`.)

In `CategoryTheory.Adjunction.coconesIso` we show that
when `F âŠ£ G`,
the functor associating to each `Y` the cocones over `K â‹™ F` with cone point `Y`
is naturally isomorphic to
the functor associating to each `Y` the cocones over `K` with cone point `G.obj Y`.
-/


open Opposite

namespace CategoryTheory

open Functor Limits

universe v u vâ‚ vâ‚‚ vâ‚€ uâ‚ uâ‚‚

namespace Adjunction

section ArbitraryUniverse

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D]
variable {F : C â¥¤ D} {G : D â¥¤ C} (adj : F âŠ£ G)

section PreservationColimits

variable {J : Type u} [Category.{v} J] (K : J â¥¤ C)

/-- The right adjoint of `Cocones.functoriality K F : Cocone K â¥¤ Cocone (K â‹™ F)`.

Auxiliary definition for `functorialityIsLeftAdjoint`.
-/
def functorialityRightAdjoint : Cocone (K â‹™ F) â¥¤ Cocone K :=
  Cocones.functoriality _ G â‹™
    Cocones.precompose (K.rightUnitor.inv â‰« whiskerLeft K adj.unit â‰« (associator _ _ _).inv)

attribute [local simp] functorialityRightAdjoint

/-- The unit for the adjunction for `Cocones.functoriality K F : Cocone K â¥¤ Cocone (K â‹™ F)`.

Auxiliary definition for `functorialityIsLeftAdjoint`.
-/
@[simps]
def functorialityUnit :
    ðŸ­ (Cocone K) âŸ¶ Cocones.functoriality _ F â‹™ functorialityRightAdjoint adj K where
  app c := { hom := adj.unit.app c.pt }

/-- The counit for the adjunction for `Cocones.functoriality K F : Cocone K â¥¤ Cocone (K â‹™ F)`.

Auxiliary definition for `functorialityIsLeftAdjoint`.
-/
@[simps]
def functorialityCounit :
    functorialityRightAdjoint adj K â‹™ Cocones.functoriality _ F âŸ¶ ðŸ­ (Cocone (K â‹™ F)) where
  app c := { hom := adj.counit.app c.pt }

/-- The functor `Cocones.functoriality K F : Cocone K â¥¤ Cocone (K â‹™ F)` is a left adjoint. -/
def functorialityAdjunction : Cocones.functoriality K F âŠ£ functorialityRightAdjoint adj K where
  unit := functorialityUnit adj K
  counit := functorialityCounit adj K

include adj in
/-- A left adjoint preserves colimits. -/
@[stacks 0038]
lemma leftAdjoint_preservesColimits : PreservesColimitsOfSize.{v, u} F where
  preservesColimitsOfShape :=
    { preservesColimit :=
        { preserves := fun hc =>
            âŸ¨IsColimit.isoUniqueCoconeMorphism.inv fun _ =>
              @Equiv.unique _ _ (IsColimit.isoUniqueCoconeMorphism.hom hc _)
                ((adj.functorialityAdjunction _).homEquiv _ _)âŸ© } }

noncomputable
instance colim_preservesColimits [HasColimitsOfShape J C] :
    PreservesColimits (colim (J := J) (C := C)) :=
  colimConstAdj.leftAdjoint_preservesColimits

-- see Note [lower instance priority]
noncomputable instance (priority := 100) isEquivalence_preservesColimits
    (E : C â¥¤ D) [E.IsEquivalence] :
    PreservesColimitsOfSize.{v, u} E :=
  leftAdjoint_preservesColimits E.adjunction

-- see Note [lower instance priority]
noncomputable instance (priority := 100)
    _root_.CategoryTheory.Functor.reflectsColimits_of_isEquivalence
    (E : D â¥¤ C) [E.IsEquivalence] :
    ReflectsColimitsOfSize.{v, u} E where
  reflectsColimitsOfShape :=
    { reflectsColimit :=
        { reflects := fun t =>
          âŸ¨(isColimitOfPreserves E.inv t).mapCoconeEquiv E.asEquivalence.unitIso.symmâŸ© } }

-- see Note [lower instance priority]
noncomputable instance (priority := 100)
    _root_.CategoryTheory.Functor.createsColimitsOfIsEquivalence (H : D â¥¤ C)
    [H.IsEquivalence] :
    CreatesColimitsOfSize.{v, u} H where
  CreatesColimitsOfShape :=
    { CreatesColimit :=
        { lifts := fun c _ =>
            { liftedCocone := mapCoconeInv H c
              validLift := mapCoconeMapCoconeInv H c } } }


-- verify the preserve_colimits instance works as expected:
noncomputable example (E : C â¥¤ D) [E.IsEquivalence] (c : Cocone K) (h : IsColimit c) :
    IsColimit (E.mapCocone c) :=
  isColimitOfPreserves E h

theorem hasColimit_comp_equivalence (E : C â¥¤ D) [E.IsEquivalence] [HasColimit K] :
    HasColimit (K â‹™ E) :=
  HasColimit.mk
    { cocone := E.mapCocone (colimit.cocone K)
      isColimit := isColimitOfPreserves _ (colimit.isColimit K) }

theorem hasColimit_of_comp_equivalence (E : C â¥¤ D) [E.IsEquivalence] [HasColimit (K â‹™ E)] :
    HasColimit K := by
  rw [hasColimit_iff_of_iso
    ((Functor.rightUnitor _).symm â‰ªâ‰« isoWhiskerLeft K E.asEquivalence.unitIso)]
  exact hasColimit_comp_equivalence (K â‹™ E) E.inv

/-- Transport a `HasColimitsOfShape` instance across an equivalence. -/
theorem hasColimitsOfShape_of_equivalence (E : C â¥¤ D) [E.IsEquivalence] [HasColimitsOfShape J D] :
    HasColimitsOfShape J C :=
  âŸ¨fun F => hasColimit_of_comp_equivalence F EâŸ©

/-- Transport a `HasColimitsOfSize` instance across an equivalence. -/
theorem has_colimits_of_equivalence (E : C â¥¤ D) [E.IsEquivalence] [HasColimitsOfSize.{v, u} D] :
    HasColimitsOfSize.{v, u} C :=
  âŸ¨fun _ _ => hasColimitsOfShape_of_equivalence EâŸ©

end PreservationColimits

section PreservationLimits

variable {J : Type u} [Category.{v} J] (K : J â¥¤ D)

/-- The left adjoint of `Cones.functoriality K G : Cone K â¥¤ Cone (K â‹™ G)`.

Auxiliary definition for `functorialityIsRightAdjoint`.
-/
def functorialityLeftAdjoint : Cone (K â‹™ G) â¥¤ Cone K :=
  Cones.functoriality _ F â‹™
    Cones.postcompose ((associator _ _ _).hom â‰« whiskerLeft K adj.counit â‰« K.rightUnitor.hom)

attribute [local simp] functorialityLeftAdjoint

/-- The unit for the adjunction for `Cones.functoriality K G : Cone K â¥¤ Cone (K â‹™ G)`.

Auxiliary definition for `functorialityIsRightAdjoint`.
-/
@[simps]
def functorialityUnit' :
    ðŸ­ (Cone (K â‹™ G)) âŸ¶ functorialityLeftAdjoint adj K â‹™ Cones.functoriality _ G where
  app c := { hom := adj.unit.app c.pt }

/-- The counit for the adjunction for `Cones.functoriality K G : Cone K â¥¤ Cone (K â‹™ G)`.

Auxiliary definition for `functorialityIsRightAdjoint`.
-/
@[simps]
def functorialityCounit' :
    Cones.functoriality _ G â‹™ functorialityLeftAdjoint adj K âŸ¶ ðŸ­ (Cone K) where
  app c := { hom := adj.counit.app c.pt }

/-- The functor `Cones.functoriality K G : Cone K â¥¤ Cone (K â‹™ G)` is a right adjoint. -/
def functorialityAdjunction' : functorialityLeftAdjoint adj K âŠ£ Cones.functoriality K G where
  unit := functorialityUnit' adj K
  counit := functorialityCounit' adj K

include adj in
/-- A right adjoint preserves limits. -/
@[stacks 0038]
lemma rightAdjoint_preservesLimits : PreservesLimitsOfSize.{v, u} G where
  preservesLimitsOfShape :=
    { preservesLimit :=
        { preserves := fun hc =>
            âŸ¨IsLimit.isoUniqueConeMorphism.inv fun _ =>
              @Equiv.unique _ _ (IsLimit.isoUniqueConeMorphism.hom hc _)
                ((adj.functorialityAdjunction' _).homEquiv _ _).symmâŸ© } }

instance lim_preservesLimits [HasLimitsOfShape J C] :
    PreservesLimits (lim (J := J) (C := C)) :=
  constLimAdj.rightAdjoint_preservesLimits

-- see Note [lower instance priority]
instance (priority := 100) isEquivalencePreservesLimits
    (E : D â¥¤ C) [E.IsEquivalence] :
    PreservesLimitsOfSize.{v, u} E :=
  rightAdjoint_preservesLimits E.asEquivalence.symm.toAdjunction

-- see Note [lower instance priority]
noncomputable instance (priority := 100)
    _root_.CategoryTheory.Functor.reflectsLimits_of_isEquivalence
    (E : D â¥¤ C) [E.IsEquivalence] :
    ReflectsLimitsOfSize.{v, u} E where
  reflectsLimitsOfShape :=
    { reflectsLimit :=
        { reflects := fun t =>
            âŸ¨(isLimitOfPreserves E.inv t).mapConeEquiv E.asEquivalence.unitIso.symmâŸ© } }

-- see Note [lower instance priority]
noncomputable instance (priority := 100)
    _root_.CategoryTheory.Functor.createsLimitsOfIsEquivalence (H : D â¥¤ C) [H.IsEquivalence] :
    CreatesLimitsOfSize.{v, u} H where
  CreatesLimitsOfShape :=
    { CreatesLimit :=
        { lifts := fun c _ =>
            { liftedCone := mapConeInv H c
              validLift := mapConeMapConeInv H c } } }


-- verify the preserve_limits instance works as expected:
noncomputable example (E : D â¥¤ C) [E.IsEquivalence] (c : Cone K) (h : IsLimit c) :
    IsLimit (E.mapCone c) :=
  isLimitOfPreserves E h

theorem hasLimit_comp_equivalence (E : D â¥¤ C) [E.IsEquivalence] [HasLimit K] : HasLimit (K â‹™ E) :=
  HasLimit.mk
    { cone := E.mapCone (limit.cone K)
      isLimit := isLimitOfPreserves _ (limit.isLimit K) }

theorem hasLimit_of_comp_equivalence (E : D â¥¤ C) [E.IsEquivalence] [HasLimit (K â‹™ E)] :
    HasLimit K := by
  rw [â† hasLimit_iff_of_iso
    (isoWhiskerLeft K E.asEquivalence.unitIso.symm â‰ªâ‰« Functor.rightUnitor _)]
  exact hasLimit_comp_equivalence (K â‹™ E) E.inv

/-- Transport a `HasLimitsOfShape` instance across an equivalence. -/
theorem hasLimitsOfShape_of_equivalence (E : D â¥¤ C) [E.IsEquivalence] [HasLimitsOfShape J C] :
    HasLimitsOfShape J D :=
  âŸ¨fun F => hasLimit_of_comp_equivalence F EâŸ©

/-- Transport a `HasLimitsOfSize` instance across an equivalence. -/
theorem has_limits_of_equivalence (E : D â¥¤ C) [E.IsEquivalence] [HasLimitsOfSize.{v, u} C] :
    HasLimitsOfSize.{v, u} D :=
  âŸ¨fun _ _ => hasLimitsOfShape_of_equivalence EâŸ©

end PreservationLimits

/-- auxiliary construction for `coconesIso` -/
@[simp]
def coconesIsoComponentHom {J : Type u} [Category.{v} J] {K : J â¥¤ C} (Y : D)
    (t : ((cocones J D).obj (op (K â‹™ F))).obj Y) : (G â‹™ (cocones J C).obj (op K)).obj Y where
  app j := (adj.homEquiv (K.obj j) Y) (t.app j)
  naturality j j' f := by
    rw [â† adj.homEquiv_naturality_left, â† Functor.comp_map, t.naturality]
    simp

/-- auxiliary construction for `coconesIso` -/
@[simp]
def coconesIsoComponentInv {J : Type u} [Category.{v} J] {K : J â¥¤ C} (Y : D)
    (t : (G â‹™ (cocones J C).obj (op K)).obj Y) : ((cocones J D).obj (op (K â‹™ F))).obj Y where
  app j := (adj.homEquiv (K.obj j) Y).symm (t.app j)
  naturality j j' f := by
    erw [â† adj.homEquiv_naturality_left_symm, â† adj.homEquiv_naturality_right_symm, t.naturality]
    simp

/-- auxiliary construction for `conesIso` -/
@[simp]
def conesIsoComponentHom {J : Type u} [Category.{v} J] {K : J â¥¤ D} (X : Cáµ’áµ–)
    (t : (Functor.op F â‹™ (cones J D).obj K).obj X) : ((cones J C).obj (K â‹™ G)).obj X where
  app j := (adj.homEquiv (unop X) (K.obj j)) (t.app j)
  naturality j j' f := by
    erw [â† adj.homEquiv_naturality_right, â† t.naturality, Category.id_comp, Category.id_comp]
    rfl

/-- auxiliary construction for `conesIso` -/
@[simp]
def conesIsoComponentInv {J : Type u} [Category.{v} J] {K : J â¥¤ D} (X : Cáµ’áµ–)
    (t : ((cones J C).obj (K â‹™ G)).obj X) : (Functor.op F â‹™ (cones J D).obj K).obj X where
  app j := (adj.homEquiv (unop X) (K.obj j)).symm (t.app j)
  naturality j j' f := by
    erw [â† adj.homEquiv_naturality_right_symm, â† t.naturality, Category.id_comp, Category.id_comp]

end ArbitraryUniverse

variable {C : Type uâ‚} [Category.{vâ‚€} C] {D : Type uâ‚‚} [Category.{vâ‚€} D] {F : C â¥¤ D} {G : D â¥¤ C}
  (adj : F âŠ£ G)

attribute [local simp] homEquiv_unit homEquiv_counit

-- Note: this is natural in K, but we do not yet have the tools to formulate that.
/-- When `F âŠ£ G`,
the functor associating to each `Y` the cocones over `K â‹™ F` with cone point `Y`
is naturally isomorphic to
the functor associating to each `Y` the cocones over `K` with cone point `G.obj Y`.
-/
def coconesIso {J : Type u} [Category.{v} J] {K : J â¥¤ C} :
    (cocones J D).obj (op (K â‹™ F)) â‰… G â‹™ (cocones J C).obj (op K) :=
  NatIso.ofComponents fun Y =>
    { hom := coconesIsoComponentHom adj Y
      inv := coconesIsoComponentInv adj Y }

-- Note: this is natural in K, but we do not yet have the tools to formulate that.
/-- When `F âŠ£ G`,
the functor associating to each `X` the cones over `K` with cone point `F.op.obj X`
is naturally isomorphic to
the functor associating to each `X` the cones over `K â‹™ G` with cone point `X`.
-/
def conesIso {J : Type u} [Category.{v} J] {K : J â¥¤ D} :
    F.op â‹™ (cones J D).obj K â‰… (cones J C).obj (K â‹™ G) :=
  NatIso.ofComponents fun X =>
    { hom := conesIsoComponentHom adj X
      inv := conesIsoComponentInv adj X }

end Adjunction

namespace Functor

variable {J C D : Type*} [Category J] [Category C] [Category D]
  (F : C â¥¤ D)

noncomputable instance [IsLeftAdjoint F] : PreservesColimitsOfShape J F :=
  (Adjunction.ofIsLeftAdjoint F).leftAdjoint_preservesColimits.preservesColimitsOfShape

noncomputable instance [IsLeftAdjoint F] : PreservesColimitsOfSize.{v, u} F where

noncomputable instance [IsRightAdjoint F] : PreservesLimitsOfShape J F :=
  (Adjunction.ofIsRightAdjoint F).rightAdjoint_preservesLimits.preservesLimitsOfShape

noncomputable instance [IsRightAdjoint F] : PreservesLimitsOfSize.{v, u} F where

end Functor

end CategoryTheory
