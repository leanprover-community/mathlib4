/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Reid Barton, Johan Commelin
-/
import Mathlib.CategoryTheory.Adjunction.Basic
import Mathlib.CategoryTheory.Limits.Creates

#align_import category_theory.adjunction.limits from "leanprover-community/mathlib"@"9e7c80f638149bfb3504ba8ff48dfdbfc949fb1a"

/-!
# Adjunctions and limits

A left adjoint preserves colimits (`CategoryTheory.Adjunction.leftAdjointPreservesColimits`),
and a right adjoint preserves limits (`CategoryTheory.Adjunction.rightAdjointPreservesLimits`).

Equivalences create and reflect (co)limits.
(`CategoryTheory.Adjunction.isEquivalenceCreatesLimits`,
`CategoryTheory.Adjunction.isEquivalenceCreatesColimits`,
`CategoryTheory.Adjunction.isEquivalenceReflectsLimits`,
`CategoryTheory.Adjunction.isEquivalenceReflectsColimits`,)

In `CategoryTheory.Adjunction.coconesIso` we show that
when `F ‚ä£ G`,
the functor associating to each `Y` the cocones over `K ‚ãô F` with cone point `Y`
is naturally isomorphic to
the functor associating to each `Y` the cocones over `K` with cone point `G.obj Y`.
-/


open Opposite

namespace CategoryTheory

open Functor Limits

universe v u v‚ÇÅ v‚ÇÇ v‚ÇÄ u‚ÇÅ u‚ÇÇ

namespace Adjunction

section ArbitraryUniverse

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
variable {F : C ‚•§ D} {G : D ‚•§ C} (adj : F ‚ä£ G)

section PreservationColimits

variable {J : Type u} [Category.{v} J] (K : J ‚•§ C)

/-- The right adjoint of `Cocones.functoriality K F : Cocone K ‚•§ Cocone (K ‚ãô F)`.

Auxiliary definition for `functorialityIsLeftAdjoint`.
-/
def functorialityRightAdjoint : Cocone (K ‚ãô F) ‚•§ Cocone K :=
  Cocones.functoriality _ G ‚ãô
    Cocones.precompose (K.rightUnitor.inv ‚â´ whiskerLeft K adj.unit ‚â´ (associator _ _ _).inv)
#align category_theory.adjunction.functoriality_right_adjoint CategoryTheory.Adjunction.functorialityRightAdjoint

attribute [local simp] functorialityRightAdjoint

/-- The unit for the adjunction for `Cocones.functoriality K F : Cocone K ‚•§ Cocone (K ‚ãô F)`.

Auxiliary definition for `functorialityIsLeftAdjoint`.
-/
@[simps]
def functorialityUnit :
    ùü≠ (Cocone K) ‚ü∂ Cocones.functoriality _ F ‚ãô functorialityRightAdjoint adj K where
  app c := { hom := adj.unit.app c.pt }
#align category_theory.adjunction.functoriality_unit CategoryTheory.Adjunction.functorialityUnit

/-- The counit for the adjunction for `Cocones.functoriality K F : Cocone K ‚•§ Cocone (K ‚ãô F)`.

Auxiliary definition for `functorialityIsLeftAdjoint`.
-/
@[simps]
def functorialityCounit :
    functorialityRightAdjoint adj K ‚ãô Cocones.functoriality _ F ‚ü∂ ùü≠ (Cocone (K ‚ãô F)) where
  app c := { hom := adj.counit.app c.pt }
#align category_theory.adjunction.functoriality_counit CategoryTheory.Adjunction.functorialityCounit

/-- The functor `Cocones.functoriality K F : Cocone K ‚•§ Cocone (K ‚ãô F)` is a left adjoint. -/
def functorialityAdjunction : Cocones.functoriality K F ‚ä£ functorialityRightAdjoint adj K :=
  mkOfUnitCounit
    { unit := functorialityUnit adj K
      counit := functorialityCounit adj K}
#align category_theory.adjunction.functoriality_is_left_adjoint CategoryTheory.Adjunction.functorialityAdjunction

/-- A left adjoint preserves colimits.

See <https://stacks.math.columbia.edu/tag/0038>.
-/
def leftAdjointPreservesColimits : PreservesColimitsOfSize.{v, u} F where
  preservesColimitsOfShape :=
    { preservesColimit :=
        { preserves := fun hc =>
            IsColimit.isoUniqueCoconeMorphism.inv fun _ =>
              @Equiv.unique _ _ (IsColimit.isoUniqueCoconeMorphism.hom hc _)
                ((adj.functorialityAdjunction _).homEquiv _ _) } }
#align category_theory.adjunction.left_adjoint_preserves_colimits CategoryTheory.Adjunction.leftAdjointPreservesColimits

-- see Note [lower instance priority]
noncomputable instance (priority := 100) isEquivalencePreservesColimits
    (E : C ‚•§ D) [E.IsEquivalence] :
    PreservesColimitsOfSize.{v, u} E :=
  leftAdjointPreservesColimits E.adjunction
#align category_theory.adjunction.is_equivalence_preserves_colimits CategoryTheory.Adjunction.isEquivalencePreservesColimits

-- see Note [lower instance priority]
noncomputable instance (priority := 100) isEquivalenceReflectsColimits
    (E : D ‚•§ C) [E.IsEquivalence] :
    ReflectsColimitsOfSize.{v, u} E where
  reflectsColimitsOfShape :=
    { reflectsColimit :=
        { reflects := fun t =>
          (isColimitOfPreserves E.inv t).mapCoconeEquiv E.asEquivalence.unitIso.symm } }
#align category_theory.adjunction.is_equivalence_reflects_colimits CategoryTheory.Adjunction.isEquivalenceReflectsColimits

-- see Note [lower instance priority]
noncomputable instance (priority := 100) isEquivalenceCreatesColimits (H : D ‚•§ C)
    [H.IsEquivalence] :
    CreatesColimitsOfSize.{v, u} H where
  CreatesColimitsOfShape :=
    { CreatesColimit :=
        { lifts := fun c _ =>
            { liftedCocone := mapCoconeInv H c
              validLift := mapCoconeMapCoconeInv H c } } }
#align category_theory.adjunction.is_equivalence_creates_colimits CategoryTheory.Adjunction.isEquivalenceCreatesColimits

-- verify the preserve_colimits instance works as expected:
noncomputable example (E : C ‚•§ D) [E.IsEquivalence] (c : Cocone K) (h : IsColimit c) :
    IsColimit (E.mapCocone c) :=
  PreservesColimit.preserves h

theorem hasColimit_comp_equivalence (E : C ‚•§ D) [E.IsEquivalence] [HasColimit K] :
    HasColimit (K ‚ãô E) :=
  HasColimit.mk
    { cocone := E.mapCocone (colimit.cocone K)
      isColimit := PreservesColimit.preserves (colimit.isColimit K) }
#align category_theory.adjunction.has_colimit_comp_equivalence CategoryTheory.Adjunction.hasColimit_comp_equivalence

theorem hasColimit_of_comp_equivalence (E : C ‚•§ D) [E.IsEquivalence] [HasColimit (K ‚ãô E)] :
    HasColimit K :=
  @hasColimitOfIso _ _ _ _ (K ‚ãô E ‚ãô E.inv) K
    (@hasColimit_comp_equivalence _ _ _ _ _ _ (K ‚ãô E) E.inv _ _)
    ((Functor.rightUnitor _).symm ‚â™‚â´ isoWhiskerLeft K E.asEquivalence.unitIso)
#align category_theory.adjunction.has_colimit_of_comp_equivalence CategoryTheory.Adjunction.hasColimit_of_comp_equivalence

/-- Transport a `HasColimitsOfShape` instance across an equivalence. -/
theorem hasColimitsOfShape_of_equivalence (E : C ‚•§ D) [E.IsEquivalence] [HasColimitsOfShape J D] :
    HasColimitsOfShape J C :=
  ‚ü®fun F => hasColimit_of_comp_equivalence F E‚ü©
#align category_theory.adjunction.has_colimits_of_shape_of_equivalence CategoryTheory.Adjunction.hasColimitsOfShape_of_equivalence

/-- Transport a `HasColimitsOfSize` instance across an equivalence. -/
theorem has_colimits_of_equivalence (E : C ‚•§ D) [E.IsEquivalence] [HasColimitsOfSize.{v, u} D] :
    HasColimitsOfSize.{v, u} C :=
  ‚ü®fun _ _ => hasColimitsOfShape_of_equivalence E‚ü©
#align category_theory.adjunction.has_colimits_of_equivalence CategoryTheory.Adjunction.has_colimits_of_equivalence

end PreservationColimits

section PreservationLimits

variable {J : Type u} [Category.{v} J] (K : J ‚•§ D)

/-- The left adjoint of `Cones.functoriality K G : Cone K ‚•§ Cone (K ‚ãô G)`.

Auxiliary definition for `functorialityIsRightAdjoint`.
-/
def functorialityLeftAdjoint : Cone (K ‚ãô G) ‚•§ Cone K :=
  Cones.functoriality _ F ‚ãô
    Cones.postcompose ((associator _ _ _).hom ‚â´ whiskerLeft K adj.counit ‚â´ K.rightUnitor.hom)
#align category_theory.adjunction.functoriality_left_adjoint CategoryTheory.Adjunction.functorialityLeftAdjoint

attribute [local simp] functorialityLeftAdjoint

/-- The unit for the adjunction for `Cones.functoriality K G : Cone K ‚•§ Cone (K ‚ãô G)`.

Auxiliary definition for `functorialityIsRightAdjoint`.
-/
@[simps]
def functorialityUnit' :
    ùü≠ (Cone (K ‚ãô G)) ‚ü∂ functorialityLeftAdjoint adj K ‚ãô Cones.functoriality _ G where
  app c := { hom := adj.unit.app c.pt }
#align category_theory.adjunction.functoriality_unit' CategoryTheory.Adjunction.functorialityUnit'

/-- The counit for the adjunction for `Cones.functoriality K G : Cone K ‚•§ Cone (K ‚ãô G)`.

Auxiliary definition for `functorialityIsRightAdjoint`.
-/
@[simps]
def functorialityCounit' :
    Cones.functoriality _ G ‚ãô functorialityLeftAdjoint adj K ‚ü∂ ùü≠ (Cone K) where
  app c := { hom := adj.counit.app c.pt }
#align category_theory.adjunction.functoriality_counit' CategoryTheory.Adjunction.functorialityCounit'

/-- The functor `Cones.functoriality K G : Cone K ‚•§ Cone (K ‚ãô G)` is a right adjoint. -/
def functorialityAdjunction' : functorialityLeftAdjoint adj K ‚ä£ Cones.functoriality K G :=
  mkOfUnitCounit
    { unit := functorialityUnit' adj K
      counit := functorialityCounit' adj K }
#align category_theory.adjunction.functoriality_is_right_adjoint CategoryTheory.Adjunction.functorialityAdjunction'

/-- A right adjoint preserves limits.

See <https://stacks.math.columbia.edu/tag/0038>.
-/
def rightAdjointPreservesLimits : PreservesLimitsOfSize.{v, u} G where
  preservesLimitsOfShape :=
    { preservesLimit :=
        { preserves := fun hc =>
            IsLimit.isoUniqueConeMorphism.inv fun _ =>
              @Equiv.unique _ _ (IsLimit.isoUniqueConeMorphism.hom hc _)
                ((adj.functorialityAdjunction' _).homEquiv _ _).symm } }
#align category_theory.adjunction.right_adjoint_preserves_limits CategoryTheory.Adjunction.rightAdjointPreservesLimits

-- see Note [lower instance priority]
noncomputable instance (priority := 100) isEquivalencePreservesLimits
    (E : D ‚•§ C) [E.IsEquivalence] :
    PreservesLimitsOfSize.{v, u} E :=
  rightAdjointPreservesLimits E.asEquivalence.symm.toAdjunction
#align category_theory.adjunction.is_equivalence_preserves_limits CategoryTheory.Adjunction.isEquivalencePreservesLimits

-- see Note [lower instance priority]
noncomputable instance (priority := 100) isEquivalenceReflectsLimits
    (E : D ‚•§ C) [E.IsEquivalence] :
    ReflectsLimitsOfSize.{v, u} E where
  reflectsLimitsOfShape :=
    { reflectsLimit :=
        { reflects := fun t =>
            (isLimitOfPreserves E.inv t).mapConeEquiv E.asEquivalence.unitIso.symm } }
#align category_theory.adjunction.is_equivalence_reflects_limits CategoryTheory.Adjunction.isEquivalenceReflectsLimits

-- see Note [lower instance priority]
noncomputable instance (priority := 100) isEquivalenceCreatesLimits (H : D ‚•§ C) [H.IsEquivalence] :
    CreatesLimitsOfSize.{v, u} H where
  CreatesLimitsOfShape :=
    { CreatesLimit :=
        { lifts := fun c _ =>
            { liftedCone := mapConeInv H c
              validLift := mapConeMapConeInv H c } } }
#align category_theory.adjunction.is_equivalence_creates_limits CategoryTheory.Adjunction.isEquivalenceCreatesLimits

-- verify the preserve_limits instance works as expected:
noncomputable example (E : D ‚•§ C) [E.IsEquivalence] (c : Cone K) (h : IsLimit c) :
    IsLimit (E.mapCone c) :=
  PreservesLimit.preserves h

theorem hasLimit_comp_equivalence (E : D ‚•§ C) [E.IsEquivalence] [HasLimit K] : HasLimit (K ‚ãô E) :=
  HasLimit.mk
    { cone := E.mapCone (limit.cone K)
      isLimit := PreservesLimit.preserves (limit.isLimit K) }
#align category_theory.adjunction.has_limit_comp_equivalence CategoryTheory.Adjunction.hasLimit_comp_equivalence

theorem hasLimit_of_comp_equivalence (E : D ‚•§ C) [E.IsEquivalence] [HasLimit (K ‚ãô E)] :
    HasLimit K :=
  @hasLimitOfIso _ _ _ _ (K ‚ãô E ‚ãô E.inv) K
    (@hasLimit_comp_equivalence _ _ _ _ _ _ (K ‚ãô E) E.inv _ _)
    (isoWhiskerLeft K E.asEquivalence.unitIso.symm ‚â™‚â´ Functor.rightUnitor _)
#align category_theory.adjunction.has_limit_of_comp_equivalence CategoryTheory.Adjunction.hasLimit_of_comp_equivalence

/-- Transport a `HasLimitsOfShape` instance across an equivalence. -/
theorem hasLimitsOfShape_of_equivalence (E : D ‚•§ C) [E.IsEquivalence] [HasLimitsOfShape J C] :
    HasLimitsOfShape J D :=
  ‚ü®fun F => hasLimit_of_comp_equivalence F E‚ü©
#align category_theory.adjunction.has_limits_of_shape_of_equivalence CategoryTheory.Adjunction.hasLimitsOfShape_of_equivalence

/-- Transport a `HasLimitsOfSize` instance across an equivalence. -/
theorem has_limits_of_equivalence (E : D ‚•§ C) [E.IsEquivalence] [HasLimitsOfSize.{v, u} C] :
    HasLimitsOfSize.{v, u} D :=
  ‚ü®fun _ _ => hasLimitsOfShape_of_equivalence E‚ü©
#align category_theory.adjunction.has_limits_of_equivalence CategoryTheory.Adjunction.has_limits_of_equivalence

end PreservationLimits

/-- auxiliary construction for `coconesIso` -/
@[simp]
def coconesIsoComponentHom {J : Type u} [Category.{v} J] {K : J ‚•§ C} (Y : D)
    (t : ((cocones J D).obj (op (K ‚ãô F))).obj Y) : (G ‚ãô (cocones J C).obj (op K)).obj Y where
  app j := (adj.homEquiv (K.obj j) Y) (t.app j)
  naturality j j' f := by
    erw [‚Üê adj.homEquiv_naturality_left, t.naturality]
    dsimp
    simp
#align category_theory.adjunction.cocones_iso_component_hom CategoryTheory.Adjunction.coconesIsoComponentHom

/-- auxiliary construction for `coconesIso` -/
@[simp]
def coconesIsoComponentInv {J : Type u} [Category.{v} J] {K : J ‚•§ C} (Y : D)
    (t : (G ‚ãô (cocones J C).obj (op K)).obj Y) : ((cocones J D).obj (op (K ‚ãô F))).obj Y where
  app j := (adj.homEquiv (K.obj j) Y).symm (t.app j)
  naturality j j' f := by
    erw [‚Üê adj.homEquiv_naturality_left_symm, ‚Üê adj.homEquiv_naturality_right_symm, t.naturality]
    dsimp; simp
#align category_theory.adjunction.cocones_iso_component_inv CategoryTheory.Adjunction.coconesIsoComponentInv

/-- auxiliary construction for `conesIso` -/
@[simp]
def conesIsoComponentHom {J : Type u} [Category.{v} J] {K : J ‚•§ D} (X : C·µí·µñ)
    (t : (Functor.op F ‚ãô (cones J D).obj K).obj X) : ((cones J C).obj (K ‚ãô G)).obj X where
  app j := (adj.homEquiv (unop X) (K.obj j)) (t.app j)
  naturality j j' f := by
    erw [‚Üê adj.homEquiv_naturality_right, ‚Üê t.naturality, Category.id_comp, Category.id_comp]
    rfl
#align category_theory.adjunction.cones_iso_component_hom CategoryTheory.Adjunction.conesIsoComponentHom

/-- auxiliary construction for `conesIso` -/
@[simp]
def conesIsoComponentInv {J : Type u} [Category.{v} J] {K : J ‚•§ D} (X : C·µí·µñ)
    (t : ((cones J C).obj (K ‚ãô G)).obj X) : (Functor.op F ‚ãô (cones J D).obj K).obj X where
  app j := (adj.homEquiv (unop X) (K.obj j)).symm (t.app j)
  naturality j j' f := by
    erw [‚Üê adj.homEquiv_naturality_right_symm, ‚Üê t.naturality, Category.id_comp, Category.id_comp]
#align category_theory.adjunction.cones_iso_component_inv CategoryTheory.Adjunction.conesIsoComponentInv

end ArbitraryUniverse

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÄ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÄ} D] {F : C ‚•§ D} {G : D ‚•§ C}
  (adj : F ‚ä£ G)

-- Note: this is natural in K, but we do not yet have the tools to formulate that.
/-- When `F ‚ä£ G`,
the functor associating to each `Y` the cocones over `K ‚ãô F` with cone point `Y`
is naturally isomorphic to
the functor associating to each `Y` the cocones over `K` with cone point `G.obj Y`.
-/
def coconesIso {J : Type u} [Category.{v} J] {K : J ‚•§ C} :
    (cocones J D).obj (op (K ‚ãô F)) ‚âÖ G ‚ãô (cocones J C).obj (op K) :=
  NatIso.ofComponents fun Y =>
    { hom := coconesIsoComponentHom adj Y
      inv := coconesIsoComponentInv adj Y }
#align category_theory.adjunction.cocones_iso CategoryTheory.Adjunction.coconesIso

-- Note: this is natural in K, but we do not yet have the tools to formulate that.
/-- When `F ‚ä£ G`,
the functor associating to each `X` the cones over `K` with cone point `F.op.obj X`
is naturally isomorphic to
the functor associating to each `X` the cones over `K ‚ãô G` with cone point `X`.
-/
def conesIso {J : Type u} [Category.{v} J] {K : J ‚•§ D} :
    F.op ‚ãô (cones J D).obj K ‚âÖ (cones J C).obj (K ‚ãô G) :=
  NatIso.ofComponents fun X =>
    { hom := conesIsoComponentHom adj X
      inv := conesIsoComponentInv adj X }
#align category_theory.adjunction.cones_iso CategoryTheory.Adjunction.conesIso

end Adjunction

namespace Functor

variable {J C D : Type*} [Category J] [Category C] [Category D]
  (F : C ‚•§ D)

noncomputable instance [IsLeftAdjoint F] : PreservesColimitsOfShape J F :=
  (Adjunction.ofIsLeftAdjoint F).leftAdjointPreservesColimits.preservesColimitsOfShape

noncomputable instance [IsLeftAdjoint F] : PreservesColimitsOfSize.{v, u} F where

noncomputable instance [IsRightAdjoint F] : PreservesLimitsOfShape J F :=
  (Adjunction.ofIsRightAdjoint F).rightAdjointPreservesLimits.preservesLimitsOfShape

noncomputable instance [IsRightAdjoint F] : PreservesLimitsOfSize.{v, u} F where

end Functor

end CategoryTheory
