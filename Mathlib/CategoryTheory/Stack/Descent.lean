/-
Copyright (c) 2025 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor
import Mathlib.CategoryTheory.Bicategory.LocallyDiscrete
import Mathlib.CategoryTheory.Sites.Sieves

/-!
# Descent data

Let `C` be a category. Given a pseudofunctor `S` from  `C` into the 2-category `Cat` and a
presieve `P` on an object `a : C`, a descent datum on `P` consists of
* an object `X f` of `S b` for each morphism `f : b âŸ¶ a` in `P`, and
* an isomorphism `Ï† g : S g (X f) â‰… X (g â‰« f)` for each `f : b âŸ¶ a` and `g : c âŸ¶ b`,
satisfying compatibility conditions for the identities and compositions.

The set of descent data on `P` forms a category `DescentData S P`.

-/

universe v u vâ‚ uâ‚

open CategoryTheory Opposite Bicategory Limits LocallyDiscrete

variable {C : Type u} [Category.{v} C]

namespace CategoryTheory

namespace Pseudofunctor

/-- The category `S X` associated with an object `X : C` by the pseudofunctor `S`. -/
abbrev mkCat (S : Pseudofunctor (LocallyDiscrete Cáµ’áµ–) Cat.{vâ‚, uâ‚}) (X : C) : Cat :=
  S.obj (.mk (.op X))

/-- The functor `S X âŸ¶ S Y` associated with a morphism `f : Y âŸ¶ X` in `C` by
the pseudofunctor `S`. -/
abbrev mkFunctor (S : Pseudofunctor (LocallyDiscrete Cáµ’áµ–) Cat.{vâ‚, uâ‚}) {X Y : C} (f : Y âŸ¶ X) :
    S.mkCat X âŸ¶ S.mkCat Y :=
  S.map (mkHom f.op)

/-- The natural isomorphism `S (ğŸ™ X) â‰… ğŸ™ (S X)`. -/
abbrev mapIdCat (S : Pseudofunctor (LocallyDiscrete Cáµ’áµ–) Cat.{vâ‚, uâ‚}) (X : C) :
    S.mkFunctor (ğŸ™ X) â‰… ğŸ™ (S.mkCat X) :=
  S.mapId âŸ¨op XâŸ©

/-- The natural isomorphism `S (g â‰« f) â‰… S f â‰« S g`. -/
abbrev mapCompCat (S : Pseudofunctor (LocallyDiscrete Cáµ’áµ–) Cat.{vâ‚, uâ‚}) {X Y Z : C}
    (f : Y âŸ¶ X) (g : Z âŸ¶ Y) :
    (S.mkFunctor (g â‰« f)) â‰… S.mkFunctor f â‰« S.mkFunctor g :=
  S.mapComp (mkHom f.op) (mkHom g.op)

/-- An auxiliary structure for descent data: it contains only data, with no conditions. -/
structure PreDescentData (S : Pseudofunctor (LocallyDiscrete Cáµ’áµ–) Cat.{vâ‚, uâ‚})
    {a : C} (P : Presieve a) where
  /-- A family of objects in the category `S b` for each morphism `f : b âŸ¶ a` in
  the presieve `P`. -/
  obj : âˆ€ {b : C} (f : b âŸ¶ a) (_ : P f := by cat_disch), S.mkCat b
  /-- A family of isomorphisms `S g (obj f) â‰… obj (g â‰« f)` in `S c` for each composable pair
  `c -gâ†’ b -fâ†’ a` with `P f` and `P (g â‰« f)`. -/
  iso : âˆ€ {b c : C} {f : b âŸ¶ a} (g : c âŸ¶ b)
    (hf : P f := by cat_disch) (hg : P (g â‰« f) := by cat_disch),
    (S.mkFunctor g).obj (obj f) â‰… obj (g â‰« f)

/--
Given a pseudofunctor `S` and a presieve `P` on an object `a` in the category `C`, the descent datum
for `S` consists of:
- `obj`: A family of objects in the category `S b` for each morphism `f : b âŸ¶ a` in
  the presieve `P`.
- `iso`: A family of isomorphisms `S g (obj f) â‰… obj (g â‰« f)` in `S c` for each composable pair
  `c -gâ†’ b -fâ†’ a` with `P f` and `P (g â‰« f)`.
- `iso_id`: The condition that the isomorphism associated with the identity morphism is compatible
  with the isomorphism `S (ğŸ™ b) â‰… ğŸ™ (S b)`.
- `iso_comp`: The condition that the isomorphism associated with the composition `h â‰« g` is
  compatible with the isomorphism `S (h â‰« g) â‰… S h â‰« S g`.
-/
structure DescentData (S : Pseudofunctor (LocallyDiscrete Cáµ’áµ–) Cat.{vâ‚, uâ‚})
    {a : C} (P : Presieve a) extends PreDescentData S P where
  iso_id {b} (f : b âŸ¶ a) (hf : P f := by cat_disch) :
    iso (ğŸ™ b) = (S.mapIdCat b).app (obj f) â‰ªâ‰« eqToIso (by simp)
  iso_comp {b c d} (f : b âŸ¶ a) (g : c âŸ¶ b) (h : d âŸ¶ c)
      (hf : P f := by cat_disch) (hg : P (g â‰« f) := by cat_disch)
      (hh : P (h â‰« g â‰« f) := by cat_disch) :
    iso (h â‰« g) =
      (S.mapCompCat g h).app (obj f) â‰ªâ‰«
        (S.map (mkHom h.op)).mapIso (iso g) â‰ªâ‰« iso h â‰ªâ‰« eqToIso (by simp)

namespace DescentData

variable {S : Pseudofunctor (LocallyDiscrete Cáµ’áµ–) Cat.{vâ‚, uâ‚}} {a : C} {P : Presieve a}

/-- The homomorphism between two descent data. -/
@[ext]
structure Hom (ğ’Ÿâ‚ ğ’Ÿâ‚‚ : DescentData S P) where
  /-- For each morphism `f : b âŸ¶ a` in `P`, a morphism `ğ’Ÿâ‚.obj f âŸ¶ ğ’Ÿâ‚‚.obj f`. -/
  hom {b : C} (f : b âŸ¶ a) (_ : P f := by cat_disch) : ğ’Ÿâ‚.obj f âŸ¶ ğ’Ÿâ‚‚.obj f
  comm {b c : C} (f : b âŸ¶ a) (g : c âŸ¶ b)
    (hf : P f := by cat_disch) (hg : P (g â‰« f) := by cat_disch) :
    (S.mkFunctor g).map (hom f) â‰« (ğ’Ÿâ‚‚.iso g).hom = (ğ’Ÿâ‚.iso g).hom â‰« hom (g â‰« f) := by cat_disch

attribute [reassoc] Hom.comm

/-- The identity morphisms on a descent datum. -/
@[simps]
def Hom.id (ğ’Ÿ : DescentData S P) : Hom ğ’Ÿ ğ’Ÿ where
  hom f _ := ğŸ™ (ğ’Ÿ.obj f)

/-- The composition of morphisms between descent data. -/
@[simps]
def Hom.comp {ğ’Ÿâ‚ ğ’Ÿâ‚‚ ğ’Ÿâ‚ƒ : DescentData S P} (Î·â‚ : Hom ğ’Ÿâ‚ ğ’Ÿâ‚‚) (Î·â‚‚ : Hom ğ’Ÿâ‚‚ ğ’Ÿâ‚ƒ) : Hom ğ’Ÿâ‚ ğ’Ÿâ‚ƒ where
  hom f _ := Î·â‚.hom f â‰« Î·â‚‚.hom f
  comm f g hf hg := by
    simp only [Functor.map_comp, Category.assoc]
    rw [Î·â‚‚.comm f g, Î·â‚.comm_assoc f g]

instance : Category (DescentData S P) where
  Hom := Hom
  id := Hom.id
  comp := Hom.comp

/-- Given a presieve on `a : C`, we have a descent datum for each object `x : S a`,
called the canonical descent datum. The object for `f : b âŸ¶ a` is given by `(S f) x`,
and the isomorphism for `c -gâŸ¶ b -fâŸ¶ a` is given by `S g ((S f) x) â‰… (S (g â‰« f)) x`
which is the `mapComp` isomorphism of the pseudofunctor `S`. -/
@[simps]
def canonical (S : Pseudofunctor (LocallyDiscrete Cáµ’áµ–) Cat.{vâ‚, uâ‚}) (x : S.mkCat a) :
    DescentData S P where
  obj f _ := (S.mkFunctor f).obj x
  iso g _ _ := (S.mapCompCat _ g).symm.app x
  iso_id f _ := by
    ext
    dsimp only [Cat.comp_obj, Iso.app_hom, Iso.symm_hom, Cat.id_obj, Iso.trans_hom, eqToIso.hom]
    rw [â† eqToHom_app (by simp), â† Cat.whiskerLeft_app, â† NatTrans.comp_app]
    congr 1
    dsimp only [mapCompCat, mkFunctor]
    rw [S.mapComp_eq_right _ (show mkHom (ğŸ™ _).op = ğŸ™ _ from rfl)]
    dsimp only [op_id, op_comp, eqToIso_refl, Iso.trans_inv, Iso.refl_inv]
    rw [S.mapComp_id_right_inv]
    simp only [Category.assoc]
    rw [Cat.rightUnitor_eqToIso, LocallyDiscrete.rightUnitor_inv, S.mapâ‚‚_eqToHom]
    simp only [eqToIso_refl, Iso.refl_hom, Category.comp_id, eqToHom_naturality_assoc,
      Category.id_comp]
    /- We need to give an argument. `rw [Category.id_comp]` alone does not work. -/
    rw [Category.id_comp (X := S.map (mkHom f.op) â‰« S.map (ğŸ™ _))]
    rw [Category.comp_id (Y := S.map (mkHom f.op â‰« mkHom (ğŸ™ _)))]
  iso_comp f g h _ _ _ := by
    ext
    dsimp only [Iso.app_hom, Iso.symm_hom, Iso.trans_hom, Functor.mapIso_hom, eqToIso.hom]
    /- We manually write this to avoid a defeq abuse about the associator. Actually, I want to get
    the RHS by `rw` or `simp`. Related discussion:
    https://leanprover.zulipchat.com/#narrow/channel/287929-mathlib4/topic/Functor.20identity.20.60.F0.9D.9F.AD.20_.20.E2.8B.99.20F.20.3D.20F.60.20is.20definitional.20equality/with/527059148 -/
    have : (S.mapCompCat g h).hom.app ((S.mkFunctor f).obj x) â‰«
          (S.map (mkHom h.op)).map ((S.mapCompCat f g).inv.app x) =
          (S.mkFunctor f â— (S.mapCompCat g h).hom â‰« (Î±_ _ _ _).inv â‰«
          (S.mapCompCat f g).inv â–· S.map (mkHom h.op)).app x := by
      dsimp only [Cat.comp_obj, Cat.comp_app, Cat.whiskerLeft_app, Cat.whiskerRight_app]
      rw [Cat.associator_eqToIso]
      simp only [eqToIso_refl, Iso.refl_inv, Cat.id_app, Cat.comp_obj, Category.id_comp]
    rw [reassoc_of% this, â† eqToHom_app (by simp)]
    simp only [â† NatTrans.comp_app]
    congr 1
    dsimp only [mapCompCat, mkFunctor]
    rw [S.mapComp_eq_right _ (show (mkHom (h â‰« g).op) = mkHom g.op â‰« mkHom h.op from rfl)]
    rw [S.mapComp_eq_left (show (mkHom (g â‰« f).op) = mkHom f.op â‰« mkHom g.op from rfl)]
    dsimp only [op_comp, eqToIso_refl, Iso.trans_inv, Iso.refl_inv]
    simp only [Category.assoc]
    rw [Category.id_comp (X := S.map (mkHom f.op) â‰« S.map (mkHom g.op â‰« mkHom h.op))]
    rw [Category.id_comp (X := S.map (mkHom f.op â‰« mkHom g.op) â‰« S.map (mkHom h.op))]
    rw [Category.id_comp (X := S.map (mkHom (f.op â‰« g.op) â‰« mkHom h.op))]
    rw [Category.comp_id (Y := S.map (mkHom f.op â‰« mkHom (g.op â‰« h.op)))]
    rw [S.mapComp_comp_right_inv]
    rw [LocallyDiscrete.associator_hom]
    rw [S.mapâ‚‚_eqToHom]

end DescentData

end Pseudofunctor

end CategoryTheory
