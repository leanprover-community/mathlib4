/-
Copyright (c) 2025 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Limits.Shapes.Equalizers
import Mathlib.CategoryTheory.Subpresheaf.Image

/-!
# The equalizer of two morphisms of presheaves, as a subpresheaf

If `Fâ‚` and `Fâ‚‚` are presheaves of types, `A : Subpresheaf Fâ‚`, and
`f` and `g` are two morphisms `A.toPresheaf âŸ¶ Fâ‚‚`, we introduce
`Subcomplex.equalizer f g`, which is the subpresheaf of `Fâ‚` contained in `A`
where `f` and `g` coincide.

-/

universe w v u

namespace CategoryTheory

variable {C : Type u} [Category.{v} C] {Fâ‚ Fâ‚‚ : Cáµ’áµ– â¥¤ Type w} {A : Subpresheaf Fâ‚}
  (f g : A.toPresheaf âŸ¶ Fâ‚‚)

namespace Subpresheaf

/-- The equalizer of two morphisms of presheaves of types of the form
`A.toPresheaf âŸ¶ Fâ‚‚` with `A : Subpresheaf Fâ‚`, as a subcomplex of `Fâ‚`. -/
@[simps -isSimp]
protected def equalizer : Subpresheaf Fâ‚ where
  obj U := setOf (fun x â†¦ âˆƒ (hx : x âˆˆ A.obj _), f.app _ âŸ¨x, hxâŸ© = g.app _ âŸ¨x, hxâŸ©)
  map Ï† x := by
    rintro âŸ¨hx, hâŸ©
    exact âŸ¨A.map _ hx,
      (FunctorToTypes.naturality _ _ f Ï† âŸ¨x, hxâŸ©).trans (Eq.trans (by rw [h])
        (FunctorToTypes.naturality _ _ g Ï† âŸ¨x, hxâŸ©).symm)âŸ©

attribute [local simp] equalizer_obj

lemma equalizer_le : Subpresheaf.equalizer f g â‰¤ A :=
  fun _ _ h â†¦ h.1

@[simp]
lemma equalizer_self : Subpresheaf.equalizer f f = A := by aesop

lemma mem_equalizer_iff {i : Cáµ’áµ–} (x : A.toPresheaf.obj i) :
    x.1 âˆˆ (Subpresheaf.equalizer f g).obj i â†” f.app i x = g.app i x := by
  simp

lemma range_le_equalizer_iff {G : Cáµ’áµ– â¥¤ Type w} (Ï† : G âŸ¶ A.toPresheaf) :
    range (Ï† â‰« A.Î¹) â‰¤ Subpresheaf.equalizer f g â†” Ï† â‰« f = Ï† â‰« g := by
  rw [NatTrans.ext_iff]
  simp [le_def, Set.subset_def, funext_iff, CategoryTheory.types_ext_iff]

lemma equalizer_eq_iff :
    Subpresheaf.equalizer f g = A â†” f = g := by
  have := range_le_equalizer_iff f g (ğŸ™ _)
  simp only [Category.id_comp, range_Î¹] at this
  rw [â† this]
  constructor
  Â· intro h
    rw [h]
  Â· intro h
    exact le_antisymm (equalizer_le f g) h

/-- Given two morphisms `f` and `g` in `A.toPresheaf âŸ¶ Fâ‚‚`, this is the monomorphism
of presheaves corresponding to the inclusion `Subpresheaf.equalizer f g â‰¤ A`. -/
def equalizer.Î¹ : (Subpresheaf.equalizer f g).toPresheaf âŸ¶ A.toPresheaf :=
  homOfLe (equalizer_le f g)

instance : Mono (equalizer.Î¹ f g) := by
  dsimp [equalizer.Î¹]
  infer_instance

@[reassoc (attr := simp)]
lemma equalizer.Î¹_Î¹ : equalizer.Î¹ f g â‰« A.Î¹ = (Subpresheaf.equalizer f g).Î¹ := rfl

@[reassoc]
lemma equalizer.condition : equalizer.Î¹ f g â‰« f = equalizer.Î¹ f g â‰« g := by
  simp [â† range_le_equalizer_iff]

/-- Given two morphisms `f` and `g` in `A.toPresheaf âŸ¶ Fâ‚‚`, if `Ï† : G âŸ¶ A.toPresheaf`
is such that `Ï† â‰« f = Ï† â‰« g`, then this is the lifted morphism
`G âŸ¶ (Subpresheaf.equalizer f g).toPresheaf`. This is part of the universal
property of the equalizer that is satisfied by
the presheaf `(Subpresheaf.equalizer f g).toPresheaf`. -/
def equalizer.lift {G : Cáµ’áµ– â¥¤ Type w} (Ï† : G âŸ¶ A.toPresheaf)
    (w : Ï† â‰« f = Ï† â‰« g) :
    G âŸ¶ (Subpresheaf.equalizer f g).toPresheaf :=
  Subpresheaf.lift (Ï† â‰« A.Î¹) (by simpa only [range_le_equalizer_iff] using w)

@[reassoc (attr := simp)]
lemma equalizer.lift_Î¹' {G : Cáµ’áµ– â¥¤ Type w} (Ï† : G âŸ¶ A.toPresheaf)
    (w : Ï† â‰« f = Ï† â‰« g) :
    equalizer.lift f g Ï† w â‰« (Subpresheaf.equalizer f g).Î¹ = Ï† â‰« A.Î¹ :=
  rfl

@[reassoc (attr := simp)]
lemma equalizer.lift_Î¹ {G : Cáµ’áµ– â¥¤ Type w} (Ï† : G âŸ¶ A.toPresheaf)
    (w : Ï† â‰« f = Ï† â‰« g) :
    equalizer.lift f g Ï† w â‰« equalizer.Î¹ f g = Ï† :=
  rfl

/-- The (limit) fork which expresses `(Subpresheaf.equalizer f g).toPresheaf` as
the equalizer of `f` and `g`. -/
@[simps! pt]
def equalizer.fork : Limits.Fork f g :=
  Limits.Fork.ofÎ¹ (equalizer.Î¹ f g) (equalizer.condition f g)

@[simp]
lemma equalizer.fork_Î¹ :
    (equalizer.fork f g).Î¹ = equalizer.Î¹ f g := rfl

/-- `(Subpresheaf.equalizer f g).toPresheaf` is the equalizer of `f` and `g`. -/
def equalizer.forkIsLimit : Limits.IsLimit (equalizer.fork f g) :=
  Limits.Fork.IsLimit.mk _
    (fun s â†¦ equalizer.lift _ _ s.Î¹ s.condition)
    (fun s â†¦ by dsimp)
    (fun s m hm â†¦ by simp [â† cancel_mono (Subpresheaf.equalizer f g).Î¹, â† hm])

end Subpresheaf

end CategoryTheory
