/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang, Jo√´l Riou
-/
import Mathlib.CategoryTheory.Subpresheaf.Basic
import Mathlib.CategoryTheory.Limits.FunctorCategory.EpiMono
import Mathlib.CategoryTheory.Limits.Types.Colimits

/-!
# The image of a subpresheaf

Given a morphism of presheaves of types `p : F' ‚ü∂ F`, we define its range
`Subpresheaf.range p`. More generally, if `G' : Subpresheaf F'`, we
define `G'.image p : Subpresheaf F` as the image of `G'` by `f`, and
if `G : Subpresheaf F`, we define its preimage `G.preimage f : Subpresheaf F'`.

-/

universe w v u

namespace CategoryTheory

variable {C : Type u} [Category.{v} C] {F F' F'' : C·µí·µñ ‚•§ Type w}

namespace Subpresheaf

section range

/-- The range of a morphism of presheaves of types, as a subpresheaf of the target. -/
@[simps]
def range (p : F' ‚ü∂ F) : Subpresheaf F where
  obj U := Set.range (p.app U)
  map := by
    rintro U V i _ ‚ü®x, rfl‚ü©
    exact ‚ü®_, FunctorToTypes.naturality  _ _ p i x‚ü©

variable (F) in
lemma range_id : range (ùüô F) = ‚ä§ := by aesop

@[simp]
lemma range_Œπ (G : Subpresheaf F) : range G.Œπ = G := by aesop

end range

section lift

variable (f : F' ‚ü∂ F) {G : Subpresheaf F} (hf : range f ‚â§ G)

/-- If the image of a morphism falls in a subpresheaf, then the morphism factors through it. -/
@[simps!]
def lift : F' ‚ü∂ G.toPresheaf where
  app U x := ‚ü®f.app U x, hf U (by simp)‚ü©
  naturality _ _ g := by
    ext x
    simpa [Subtype.ext_iff] using FunctorToTypes.naturality _ _ f g x

@[reassoc (attr := simp)]
theorem lift_Œπ : lift f hf ‚â´ G.Œπ = f := rfl

end lift

section range

variable (p : F' ‚ü∂ F)

/-- Given a morphism `p : F' ‚ü∂ F` of presheaves of types, this is the morphism
from `F'` to its range. -/
def toRange :
    F' ‚ü∂ (range p).toPresheaf :=
  lift p (by rfl)

@[reassoc (attr := simp)]
lemma toRange_Œπ : toRange p ‚â´ (range p).Œπ = p := rfl

lemma toRange_app_val {i : C·µí·µñ} (x : F'.obj i) :
    ((toRange p).app i x).val = p.app i x := by
  simp [toRange]

@[simp]
lemma range_toRange : range (toRange p) = ‚ä§ := by
  ext i ‚ü®x, hx‚ü©
  dsimp at hx ‚ä¢
  simp only [Set.mem_range, Set.mem_univ, iff_true]
  simp only [Set.mem_range] at hx
  obtain ‚ü®y, rfl‚ü© := hx
  exact ‚ü®y, rfl‚ü©

lemma epi_iff_range_eq_top :
    Epi p ‚Üî range p = ‚ä§ := by
  simp [NatTrans.epi_iff_epi_app, epi_iff_surjective, Subpresheaf.ext_iff, funext_iff,
    Set.range_eq_univ]

lemma range_eq_top [Epi p] : range p = ‚ä§ := by rwa [‚Üê epi_iff_range_eq_top]

instance : Epi (toRange p) := by simp [epi_iff_range_eq_top]

instance [Mono p] : IsIso (toRange p) := by
  have := mono_of_mono_fac (toRange_Œπ p)
  rw [NatTrans.isIso_iff_isIso_app]
  intro i
  rw [isIso_iff_bijective]
  constructor
  ¬∑ rw [‚Üê mono_iff_injective]
    infer_instance
  ¬∑ rw [‚Üê epi_iff_surjective]
    infer_instance

lemma range_comp_le (f : F ‚ü∂ F') (g : F' ‚ü∂ F'') :
    range (f ‚â´ g) ‚â§ range g := fun _ _ _ ‚Ü¶ by aesop

end range

section image

variable (G : Subpresheaf F) (f : F ‚ü∂ F')

/-- The image of a subpresheaf by a morphism of presheaves of types. -/
@[simps]
def image : Subpresheaf F' where
  obj i := (f.app i) '' (G.obj i)
  map := by
    rintro Œî Œî' œÜ _ ‚ü®x, hx, rfl‚ü©
    exact ‚ü®F.map œÜ x, G.map œÜ hx, by apply FunctorToTypes.naturality‚ü©

lemma image_top : (‚ä§ : Subpresheaf F).image f = range f := by aesop

@[simp]
lemma image_iSup {Œπ : Type*} (G : Œπ ‚Üí Subpresheaf F) (f : F ‚ü∂ F') :
    (‚®Ü i, G i).image f = ‚®Ü i, (G i).image f := by aesop

lemma image_comp (g : F' ‚ü∂ F'') :
    G.image (f ‚â´ g) = (G.image f).image g := by aesop

lemma range_comp (g : F' ‚ü∂ F'') :
    range (f ‚â´ g) = (range f).image g := by aesop

end image

section preimage

/-- The preimage of a subpresheaf by a morphism of presheaves of types. -/
@[simps]
def preimage (G : Subpresheaf F) (p : F' ‚ü∂ F) : Subpresheaf F' where
  obj n := p.app n ‚Åª¬π' (G.obj n)
  map f := (Set.preimage_mono (G.map f)).trans (by
    simp only [Set.preimage_preimage, FunctorToTypes.naturality _ _ p f]
    rfl)

@[simp]
lemma preimage_id (G : Subpresheaf F) :
    G.preimage (ùüô F) = G := by aesop

lemma preimage_comp (G : Subpresheaf F) (f : F'' ‚ü∂ F') (g : F' ‚ü∂ F) :
    G.preimage (f ‚â´ g) = (G.preimage g).preimage f := by aesop

lemma image_le_iff (G : Subpresheaf F) (f : F ‚ü∂ F') (G' : Subpresheaf F') :
    G.image f ‚â§ G' ‚Üî G ‚â§ G'.preimage f := by
  simp [Subpresheaf.le_def]

/-- Given a morphism `p : F' ‚ü∂ F` of presheaves of types and `G : Subpresheaf F`,
this is the morphism from the preimage of `G` by `p` to `G`. -/
def fromPreimage (G : Subpresheaf F) (p : F' ‚ü∂ F) :
    (G.preimage p).toPresheaf ‚ü∂ G.toPresheaf :=
  lift ((G.preimage p).Œπ ‚â´ p) (by
    rw [range_comp, range_Œπ, image_le_iff])

@[reassoc]
lemma fromPreimage_Œπ (G : Subpresheaf F) (p : F' ‚ü∂ F) :
    G.fromPreimage p ‚â´ G.Œπ = (G.preimage p).Œπ ‚â´ p := rfl

lemma preimage_eq_top_iff (G : Subpresheaf F) (p : F' ‚ü∂ F) :
    G.preimage p = ‚ä§ ‚Üî range p ‚â§ G := by
  rw [‚Üê image_top, image_le_iff]
  aesop

@[simp]
lemma preimage_image_of_epi (G : Subpresheaf F) (p : F' ‚ü∂ F) [hp : Epi p] :
    (G.preimage p).image p = G := by
  apply le_antisymm
  ¬∑ rw [image_le_iff]
  ¬∑ intro i x hx
    simp only [NatTrans.epi_iff_epi_app, epi_iff_surjective] at hp
    obtain ‚ü®y, rfl‚ü© := hp _ x
    exact ‚ü®y, hx, rfl‚ü©

end preimage

end Subpresheaf

@[deprecated (since := "2025-01-25")] alias imagePresheaf := Subpresheaf.range
@[deprecated (since := "2025-01-25")] alias imagePresheaf_id := Subpresheaf.range_id
@[deprecated (since := "2025-01-25")] alias toImagePresheaf := Subpresheaf.toRange
@[deprecated (since := "2025-01-25")] alias toImagePresheaf_Œπ := Subpresheaf.toRange_Œπ
@[deprecated (since := "2025-01-25")] alias imagePresheaf_comp_le := Subpresheaf.range_comp_le

end CategoryTheory
