/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang, JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Subpresheaf.Basic
import Mathlib.CategoryTheory.Limits.FunctorCategory.EpiMono
import Mathlib.CategoryTheory.Limits.Types.Colimits

/-!
# The image of a subpresheaf

Given a morphism of presheaves of types `p : F' âŸ¶ F`, we define its range
`Subpresheaf.range p`. More generally, if `G' : Subpresheaf F'`, we
define `G'.image p : Subpresheaf F` as the image of `G'` by `f`, and
if `G : Subpresheaf F`, we define its preimage `G.preimage f : Subpresheaf F'`.

-/

universe w v u

namespace CategoryTheory

variable {C : Type u} [Category.{v} C] {F F' F'' : Cáµ’áµ– â¥¤ Type w}

namespace Subpresheaf

section range

/-- The range of a morphism of presheaves of types, as a subpresheaf of the target. -/
@[simps]
def range (p : F' âŸ¶ F) : Subpresheaf F where
  obj U := Set.range (p.app U)
  map := by
    rintro U V i _ âŸ¨x, rflâŸ©
    exact âŸ¨_, FunctorToTypes.naturality  _ _ p i xâŸ©

variable (F) in
lemma range_id : range (ğŸ™ F) = âŠ¤ := by aesop

@[simp]
lemma range_Î¹ (G : Subpresheaf F) : range G.Î¹ = G := by aesop

end range

section lift

variable (f : F' âŸ¶ F) {G : Subpresheaf F} (hf : range f â‰¤ G)

/-- If the image of a morphism falls in a subpresheaf, then the morphism factors through it. -/
@[simps!]
def lift : F' âŸ¶ G.toPresheaf where
  app U x := âŸ¨f.app U x, hf U (by simp)âŸ©
  naturality _ _ g := by
    ext x
    simpa [Subtype.ext_iff] using FunctorToTypes.naturality _ _ f g x

@[reassoc (attr := simp)]
theorem lift_Î¹ : lift f hf â‰« G.Î¹ = f := rfl

end lift

section range

variable (p : F' âŸ¶ F)

/-- Given a morphism `p : F' âŸ¶ F` of presheaves of types, this is the morphism
from `F'` to its range. -/
def toRange :
    F' âŸ¶ (range p).toPresheaf :=
  lift p (by rfl)

@[reassoc (attr := simp)]
lemma toRange_Î¹ : toRange p â‰« (range p).Î¹ = p := rfl

lemma toRange_app_val {i : Cáµ’áµ–} (x : F'.obj i) :
    ((toRange p).app i x).val = p.app i x := by
  simp [toRange]

@[simp]
lemma range_toRange : range (toRange p) = âŠ¤ := by
  ext i âŸ¨x, hxâŸ©
  dsimp at hx âŠ¢
  simp only [Set.mem_range, Set.mem_univ, iff_true]
  simp only [Set.mem_range] at hx
  obtain âŸ¨y, rflâŸ© := hx
  exact âŸ¨y, rflâŸ©

lemma epi_iff_range_eq_top :
    Epi p â†” range p = âŠ¤ := by
  simp [NatTrans.epi_iff_epi_app, epi_iff_surjective, Subpresheaf.ext_iff, funext_iff,
    Set.range_eq_univ]

lemma range_eq_top [Epi p] : range p = âŠ¤ := by rwa [â† epi_iff_range_eq_top]

instance : Epi (toRange p) := by simp [epi_iff_range_eq_top]

instance [Mono p] : IsIso (toRange p) := by
  have := mono_of_mono_fac (toRange_Î¹ p)
  rw [NatTrans.isIso_iff_isIso_app]
  intro i
  rw [isIso_iff_bijective]
  constructor
  Â· rw [â† mono_iff_injective]
    infer_instance
  Â· rw [â† epi_iff_surjective]
    infer_instance

lemma range_comp_le (f : F âŸ¶ F') (g : F' âŸ¶ F'') :
    range (f â‰« g) â‰¤ range g := fun _ _ _ â†¦ by aesop

end range

section image

variable (G : Subpresheaf F) (f : F âŸ¶ F')

/-- The image of a subpresheaf by a morphism of presheaves of types. -/
@[simps]
def image : Subpresheaf F' where
  obj i := (f.app i) '' (G.obj i)
  map := by
    rintro Î” Î”' Ï† _ âŸ¨x, hx, rflâŸ©
    exact âŸ¨F.map Ï† x, G.map Ï† hx, by apply FunctorToTypes.naturalityâŸ©

lemma image_top : (âŠ¤ : Subpresheaf F).image f = range f := by aesop

@[simp]
lemma image_iSup {Î¹ : Type*} (G : Î¹ â†’ Subpresheaf F) (f : F âŸ¶ F') :
    (â¨† i, G i).image f = â¨† i, (G i).image f := by aesop

lemma image_comp (g : F' âŸ¶ F'') :
    G.image (f â‰« g) = (G.image f).image g := by aesop

lemma range_comp (g : F' âŸ¶ F'') :
    range (f â‰« g) = (range f).image g := by aesop

end image

section preimage

/-- The preimage of a subpresheaf by a morphism of presheaves of types. -/
@[simps]
def preimage (G : Subpresheaf F) (p : F' âŸ¶ F) : Subpresheaf F' where
  obj n := p.app n â»Â¹' (G.obj n)
  map f := (Set.preimage_mono (G.map f)).trans (by
    simp only [Set.preimage_preimage, FunctorToTypes.naturality _ _ p f]
    rfl)

@[simp]
lemma preimage_id (G : Subpresheaf F) :
    G.preimage (ğŸ™ F) = G := by aesop

lemma preimage_comp (G : Subpresheaf F) (f : F'' âŸ¶ F') (g : F' âŸ¶ F) :
    G.preimage (f â‰« g) = (G.preimage g).preimage f := by aesop

lemma image_le_iff (G : Subpresheaf F) (f : F âŸ¶ F') (G' : Subpresheaf F') :
    G.image f â‰¤ G' â†” G â‰¤ G'.preimage f := by
  simp [Subpresheaf.le_def]

/-- Given a morphism `p : F' âŸ¶ F` of presheaves of types and `G : Subpresheaf F`,
this is the morphism from the preimage of `G` by `p` to `G`. -/
def fromPreimage (G : Subpresheaf F) (p : F' âŸ¶ F) :
    (G.preimage p).toPresheaf âŸ¶ G.toPresheaf :=
  lift ((G.preimage p).Î¹ â‰« p) (by
    rw [range_comp, range_Î¹, image_le_iff])

@[reassoc]
lemma fromPreimage_Î¹ (G : Subpresheaf F) (p : F' âŸ¶ F) :
    G.fromPreimage p â‰« G.Î¹ = (G.preimage p).Î¹ â‰« p := rfl

lemma preimage_eq_top_iff (G : Subpresheaf F) (p : F' âŸ¶ F) :
    G.preimage p = âŠ¤ â†” range p â‰¤ G := by
  rw [â† image_top, image_le_iff]
  simp

@[simp]
lemma preimage_image_of_epi (G : Subpresheaf F) (p : F' âŸ¶ F) [hp : Epi p] :
    (G.preimage p).image p = G := by
  apply le_antisymm
  Â· rw [image_le_iff]
  Â· intro i x hx
    simp only [NatTrans.epi_iff_epi_app, epi_iff_surjective] at hp
    obtain âŸ¨y, rflâŸ© := hp _ x
    exact âŸ¨y, hx, rflâŸ©

end preimage

end Subpresheaf

end CategoryTheory
