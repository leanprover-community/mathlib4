/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Category.Preorder
import Mathlib.CategoryTheory.Limits.IsLimit
import Mathlib.Order.IsWellOrderLimitElement

/-! # Transfinite iterations of a functor

In this file, given a functor `Œ¶ : C ‚•§ C` and a natural transformation
`Œµ : ùü≠ C ‚ü∂ Œ¶`, we shall define the transfinite iterations of `Œ¶` (TODO).

Given `j : J` where `J` is a well ordered set, we first introduce
a category `Iteration Œµ j`. An object in this category consists of
a functor `F : { i // i ‚â§ j } ‚•§ C ‚•§ C` equipped with the data
which makes it the `i`th-iteration of `Œ¶` for all `i` such that `i ‚â§ j`.
Under suitable assumptions on `C`, we shall show that this category
`Iteration Œµ j` is equivalent to the punctual category (TODO).
We shall study morphisms in this category, showing first that
there is at most one morphism between two morphisms (done), and secondly,
that there does always exist a unique morphism between
two objects (TODO). Then, we shall show the existence of
an object (TODO). In these proofs, which are all done using
transfinite induction, we have to treat three cases separately:
* the case `j = ‚ä•`;
* the case `j` is a successor;
* the case `j` is a limit element.

-/

universe u

namespace CategoryTheory

open Category Limits

variable {C : Type*} [Category C] {Œ¶ : C ‚•§ C} (Œµ : ùü≠ C ‚ü∂ Œ¶)
  {J : Type u} [LinearOrder J] [IsWellOrder J (¬∑ < ¬∑)] [OrderBot J]

namespace Functor

namespace Iteration

variable {j : J} (F : { i // i ‚â§ j } ‚•§ C)

/-- The map `F.obj ‚ü®i, _‚ü© ‚ü∂ F.obj ‚ü®wellOrderSucc i, _‚ü©` when `F : { i // i ‚â§ j } ‚•§ C`
and `i : J` is such that `i < j`. -/
noncomputable abbrev mapSucc' (i : J) (hi : i < j) :
    F.obj ‚ü®i, hi.le‚ü© ‚ü∂ F.obj ‚ü®wellOrderSucc i, wellOrderSucc_le hi‚ü© :=
  F.map (homOfLE (by simpa only [Subtype.mk_le_mk] using self_le_wellOrderSucc i))

variable {i : J} (hi : i ‚â§ j)

/-- The functor `{ k // k < i } ‚•§ C` obtained by "restriction" of `F : { i // i ‚â§ j } ‚•§ C`
when `i ‚â§ j`. -/
def restrictionLT : { k // k < i } ‚•§ C :=
  (monotone_inclusion_lt_le_of_le hi).functor ‚ãô F

@[simp]
lemma restrictionLT_obj (k : J) (hk : k < i) :
    (restrictionLT F hi).obj ‚ü®k, hk‚ü© = F.obj ‚ü®k, hk.le.trans hi‚ü© := rfl

@[simp]
lemma restrictionLT_map {k‚ÇÅ k‚ÇÇ : { k // k < i }} (œÜ : k‚ÇÅ ‚ü∂ k‚ÇÇ) :
    (restrictionLT F hi).map œÜ = F.map (homOfLE (by simpa using leOfHom œÜ)) := rfl

/-- Given `F : { i // i ‚â§ j } ‚•§ C`, `i : J` such that `hi : i ‚â§ j`, this is the
cocone consisting of all maps `F.obj ‚ü®k, hk‚ü© ‚ü∂ F.obj ‚ü®i, hi‚ü©` for `k : J` such that `k < i`. -/
@[simps]
def coconeOfLE : Cocone (restrictionLT F hi) where
  pt := F.obj ‚ü®i, hi‚ü©
  Œπ :=
    { app := fun ‚ü®k, hk‚ü© => F.map (homOfLE (by simpa using hk.le))
      naturality := fun ‚ü®k‚ÇÅ, hk‚ÇÅ‚ü© ‚ü®k‚ÇÇ, hk‚ÇÇ‚ü© _ => by
        simp [comp_id, ‚Üê Functor.map_comp, homOfLE_comp] }

end Iteration

/-- The category of `j`th iterations of a functor `Œ¶` equipped with a natural
transformation `Œµ : ùü≠ C ‚ü∂ Œ¶`. An object consists of the data of all iterations
of `Œ¶` for `i : J` such that `i ‚â§ j` (this is the field `F`). Such objects are
equipped with data and properties which characterizes the iterations up to a unique
isomorphism for the three types of elements: `‚ä•`, successors, limit elements. -/
structure Iteration (j : J) where
  /-- The data of all `i`th iterations for `i : J` such that `i ‚â§ j`. -/
  F : { i // i ‚â§ j } ‚•§ C ‚•§ C
  /-- The zeroth iteration is the identity functor. -/
  isoZero : F.obj ‚ü®‚ä•, bot_le‚ü© ‚âÖ ùü≠ C
  /-- The iteration on a successor element is obtained by composition of
  the previous iteration with `Œ¶`. -/
  isoSucc (i : J) (hi : i < j) :
    F.obj ‚ü®wellOrderSucc i, wellOrderSucc_le hi‚ü© ‚âÖ F.obj ‚ü®i, hi.le‚ü© ‚ãô Œ¶
  /-- The natural map from an iteration to its successor is induced by `Œµ`. -/
  mapSucc'_eq (i : J) (hi : i < j) :
    Iteration.mapSucc' F i hi = whiskerLeft _ Œµ ‚â´ (isoSucc i hi).inv
  /-- If `i` is a limit element, the `i`th iteration is the colimit
  of `k`th iterations for `k < i`. -/
  isColimit (i : J) [IsWellOrderLimitElement i] (hi : i ‚â§ j) :
    IsColimit (Iteration.coconeOfLE F hi)

namespace Iteration

variable {Œµ}
variable {j : J}

section

variable (iter : Œ¶.Iteration Œµ j)

/-- For `iter : Œ¶.Iteration.Œµ j`, this is the map
`iter.F.obj ‚ü®i, _‚ü© ‚ü∂ iter.F.obj ‚ü®wellOrderSucc i, _‚ü©` if `i : J` is such that `i < j`. -/
noncomputable abbrev mapSucc (i : J) (hi : i < j) :
    iter.F.obj ‚ü®i, hi.le‚ü© ‚ü∂ iter.F.obj ‚ü®wellOrderSucc i, wellOrderSucc_le hi‚ü© :=
  mapSucc' iter.F i hi

lemma mapSucc_eq (i : J) (hi : i < j) :
    iter.mapSucc i hi = whiskerLeft _ Œµ ‚â´ (iter.isoSucc i hi).inv :=
  iter.mapSucc'_eq _ hi

end

variable (iter‚ÇÅ iter‚ÇÇ iter‚ÇÉ : Œ¶.Iteration Œµ j)

/-- A morphism between two objects `iter‚ÇÅ` and `iter‚ÇÇ` in the
category `Œ¶.Iteration Œµ j` of `j`th iterations of a functor `Œ¶`
equipped with a natural transformation `Œµ : ùü≠ C ‚ü∂ Œ¶` consists of a natural
transformation `natTrans : iter‚ÇÅ.F ‚ü∂ iter‚ÇÇ.F` which is compatible with the
isomorphisms `isoZero` and `isoSucc`. -/
structure Hom where
  /-- A natural transformation `iter‚ÇÅ.F ‚ü∂ iter‚ÇÇ.F` -/
  natTrans : iter‚ÇÅ.F ‚ü∂ iter‚ÇÇ.F
  natTrans_app_zero :
    natTrans.app ‚ü®‚ä•, bot_le‚ü© = iter‚ÇÅ.isoZero.hom ‚â´ iter‚ÇÇ.isoZero.inv := by aesop_cat
  natTrans_app_succ (i : J) (hi : i < j) :
    natTrans.app ‚ü®wellOrderSucc i, wellOrderSucc_le hi‚ü© = (iter‚ÇÅ.isoSucc i hi).hom ‚â´
      whiskerRight (natTrans.app ‚ü®i, hi.le‚ü©) _ ‚â´ (iter‚ÇÇ.isoSucc i hi).inv := by aesop_cat

namespace Hom

attribute [simp, reassoc] natTrans_app_zero

/-- The identity morphism in the category `Œ¶.Iteration Œµ j`. -/
@[simps]
def id : Hom iter‚ÇÅ iter‚ÇÅ where
  natTrans := ùüô _

variable {iter‚ÇÅ iter‚ÇÇ iter‚ÇÉ}

-- Note: this is not made a global ext lemma because it is shown below
-- that the type of morphisms is a subsingleton.
lemma ext' {f g : Hom iter‚ÇÅ iter‚ÇÇ} (h : f.natTrans = g.natTrans) : f = g := by
  cases f
  cases g
  subst h
  rfl

attribute [local ext] ext'

/-- The composition of morphisms in the category `Œ¶.Iteration Œµ j`. -/
@[simps]
def comp {iter‚ÇÉ : Iteration Œµ j} (f : Hom iter‚ÇÅ iter‚ÇÇ) (g : Hom iter‚ÇÇ iter‚ÇÉ) :
    Hom iter‚ÇÅ iter‚ÇÉ where
  natTrans := f.natTrans ‚â´ g.natTrans
  natTrans_app_succ i hi := by simp [natTrans_app_succ _ _ hi]

instance : Category (Iteration Œµ j) where
  Hom := Hom
  id := id
  comp := comp

instance : Subsingleton (iter‚ÇÅ ‚ü∂ iter‚ÇÇ) where
  allEq f g := ext' (by
    let P := fun (i : J) => ‚àÄ (hi : i ‚â§ j), f.natTrans.app ‚ü®i, hi‚ü© = g.natTrans.app ‚ü®i, hi‚ü©
    suffices ‚àÄ (i : J), P i by
      ext ‚ü®i, hi‚ü© : 2
      apply this
    refine fun _ => WellFoundedLT.induction _ (fun i hi hi' => ?_)
    obtain rfl|‚ü®i, rfl, hi''‚ü©|_ := eq_bot_or_eq_succ_or_isWellOrderLimitElement i
    ¬∑ simp only [natTrans_app_zero]
    ¬∑ simp only [Hom.natTrans_app_succ _ i (lt_of_lt_of_le hi'' hi'), hi i hi'']
    ¬∑ exact (iter‚ÇÅ.isColimit i hi').hom_ext (fun ‚ü®k, hk‚ü© => by simp [hi k hk]))

end Hom

end Iteration

end Functor

end CategoryTheory
