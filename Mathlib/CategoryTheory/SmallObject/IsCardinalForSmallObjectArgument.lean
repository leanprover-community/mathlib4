/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.SmallObject.Construction
import Mathlib.CategoryTheory.SmallObject.TransfiniteCompositionLifting
import Mathlib.CategoryTheory.SmallObject.TransfiniteIteration
import Mathlib.CategoryTheory.MorphismProperty.IsSmall
import Mathlib.CategoryTheory.MorphismProperty.LiftingProperty
import Mathlib.SetTheory.Cardinal.Cofinality

/-!
# Cardinals that are suitable for the small object argument

## References
- https://ncatlab.org/nlab/show/small+object+argument

-/

universe w v v' u u'

lemma Cardinal.zero_lt_ord_iff (Îº : Cardinal.{w}) : 0 < Îº.ord â†” Îº â‰  0 := by
  constructor
  Â· intro h h'
    simp only [h', ord_zero, lt_self_iff_false] at h
  Â· intro h
    by_contra!
    exact h (ord_eq_zero.1 (le_antisymm this (Ordinal.zero_le _)))

noncomputable def Cardinal.IsRegular.orderBotOrdToType
    {Îº : Cardinal.{w}} (hÎº : Îº.IsRegular) : OrderBot Îº.ord.toType :=
  Ordinal.toTypeOrderBotOfPos (by
    rw [Cardinal.zero_lt_ord_iff]
    rintro rfl
    apply Cardinal.aleph0_ne_zero.{w}
    simpa using hÎº.aleph0_le)

namespace CategoryTheory

open Category

noncomputable instance (o : Ordinal.{w}) : SuccOrder o.toType :=
  SuccOrder.ofLinearWellFoundedLT o.toType

open Limits SmallObject

variable {C : Type u} [Category.{v} C]

namespace MorphismProperty

variable (I : MorphismProperty C)

section

variable (J : Type u') [LinearOrder J] [SuccOrder J] [OrderBot J] [WellFoundedLT J]

lemma transfiniteCompositionsOfShape_pushouts_coproducts_le_rlp_llp :
    (coproducts.{w} I).pushouts.transfiniteCompositionsOfShape J â‰¤ I.rlp.llp := by
  simpa using transfiniteCompositionsOfShape_le_rlp_llp (coproducts.{w} I).pushouts J

lemma retracts_transfiniteCompositionsOfShape_pushouts_coproducts_le_rlp_llp :
    ((coproducts.{w} I).pushouts.transfiniteCompositionsOfShape J).retracts â‰¤ I.rlp.llp := by
  rw [le_llp_iff_le_rlp, retracts_rlp, â† le_llp_iff_le_rlp]
  apply transfiniteCompositionsOfShape_pushouts_coproducts_le_rlp_llp

end

lemma transfiniteCompositions_pushouts_coproducts_le_rlp_llp :
    (transfiniteCompositions.{w} (coproducts.{w} I).pushouts) â‰¤ I.rlp.llp := by
  simpa using transfiniteCompositions_le_rlp_llp (coproducts.{w} I).pushouts

lemma retracts_transfiniteComposition_pushouts_coproducts_le_rlp_llp :
    (transfiniteCompositions.{w} (coproducts.{w} I).pushouts).retracts â‰¤ I.rlp.llp := by
  rw [le_llp_iff_le_rlp, retracts_rlp, â† le_llp_iff_le_rlp]
  apply transfiniteCompositions_pushouts_coproducts_le_rlp_llp

class IsCardinalForSmallObjectArgument (Îº : Cardinal.{w}) [Fact Îº.IsRegular]
    [OrderBot Îº.ord.toType] : Prop where
  isSmall : IsSmall.{w} I := by infer_instance
  locallySmall : LocallySmall.{w} C := by infer_instance
  hasPushouts : HasPushouts C := by infer_instance
  hasCoproducts : HasCoproducts.{w} C := by infer_instance
  hasIterationOfShape : HasIterationOfShape C Îº.ord.toType
  preservesColimit :
      âˆ€ {A B : C} (i : A âŸ¶ B) (_ : I i)
      (F : Îº.ord.toType â¥¤ C) [F.IsWellOrderContinuous]
      (_ : âˆ€ (j : _) (_ : Â¬IsMax j),
        (coproducts.{w} I).pushouts (F.map (homOfLE (Order.le_succ j)))),
      PreservesColimit F (coyoneda.obj (Opposite.op A))

end MorphismProperty

namespace SmallObject

open MorphismProperty

variable (I : MorphismProperty C)

section

variable (Îº : Cardinal.{w}) [Fact Îº.IsRegular] [OrderBot Îº.ord.toType]
  [I.IsCardinalForSmallObjectArgument Îº]

include I Îº

lemma isSmall : IsSmall.{w} I :=
  IsCardinalForSmallObjectArgument.isSmall Îº

lemma locallySmall : LocallySmall.{w} C :=
  IsCardinalForSmallObjectArgument.locallySmall I Îº

lemma hasIterationOfShape : HasIterationOfShape C Îº.ord.toType :=
  IsCardinalForSmallObjectArgument.hasIterationOfShape I

lemma hasPushouts : HasPushouts C :=
  IsCardinalForSmallObjectArgument.hasPushouts I Îº

lemma hasCoproducts : HasCoproducts.{w} C :=
  IsCardinalForSmallObjectArgument.hasCoproducts I Îº

lemma preservesColimit_coyoneda_obj
    {A B : C} (i : A âŸ¶ B) (hi : I i)
    (F : Îº.ord.toType â¥¤ C) [F.IsWellOrderContinuous]
    (hF : âˆ€ (j : Îº.ord.toType) (_ : Â¬IsMax j),
      (coproducts.{w} I).pushouts (F.map (homOfLE (Order.le_succ j)))) :
    PreservesColimit F (coyoneda.obj (Opposite.op A)) :=
  IsCardinalForSmallObjectArgument.preservesColimit i hi F hF

lemma small_functorObjIndex {X Y : C} (p : X âŸ¶ Y) :
    Small.{w} (FunctorObjIndex I.homFamily p) := by
  have := locallySmall I Îº
  have := isSmall I Îº
  let Ï† : FunctorObjIndex I.homFamily p â†’
    Î£ (i : Shrink.{w} I.toSet),
      Shrink.{w} ((((equivShrink _).symm i).1.left âŸ¶ X) Ã—
        (((equivShrink _).symm i).1.right âŸ¶ Y)) :=
        fun x â†¦ âŸ¨equivShrink _ x.i, equivShrink _
          (âŸ¨eqToHom (by simp) â‰« x.t, eqToHom (by simp) â‰« x.bâŸ©)âŸ©
  have hÏ† : Function.Injective Ï† := by
    rintro âŸ¨iâ‚, tâ‚, bâ‚, _âŸ© âŸ¨iâ‚‚, tâ‚‚, bâ‚‚, _âŸ© h
    obtain rfl : iâ‚ = iâ‚‚ := by simpa [Ï†] using congr_arg Sigma.fst h
    simpa [cancel_epi, Ï†] using h
  exact small_of_injective hÏ†

lemma hasColimitsOfShape_discrete (X Y : C) (p : X âŸ¶ Y) :
    HasColimitsOfShape
      (Discrete (FunctorObjIndex I.homFamily p)) C := by
  have := small_functorObjIndex I Îº p
  have := hasCoproducts I Îº
  exact hasColimitsOfShape_of_equivalence (Discrete.equivalence (equivShrink.{w} _)).symm

@[simps! (config := .lemmasOnly)]
noncomputable def succStruct : SuccStruct (Arrow C â¥¤ Arrow C) :=
  have := hasColimitsOfShape_discrete I Îº
  have := hasPushouts I Îº
  SuccStruct.ofNatTrans (Îµ I.homFamily)

noncomputable def iterationFunctor : Îº.ord.toType â¥¤ Arrow C â¥¤ Arrow C :=
  have := hasIterationOfShape I Îº
  (succStruct I Îº).iterationFunctor Îº.ord.toType

instance : (iterationFunctor I Îº).IsWellOrderContinuous := by
  dsimp [iterationFunctor]
  infer_instance

instance (f : Arrow C) :
    (iterationFunctor I Îº â‹™ (evaluation _ _).obj f).IsWellOrderContinuous := by
  have := hasIterationOfShape I Îº
  infer_instance

noncomputable def iteration : Arrow C â¥¤ Arrow C :=
  have := hasIterationOfShape I Îº
  (succStruct I Îº).iteration Îº.ord.toType

noncomputable def iterationCocone : Cocone (iterationFunctor I Îº) :=
  have := hasIterationOfShape I Îº
  (succStruct I Îº).iterationCocone Îº.ord.toType

@[simp]
lemma iterationCocone_pt : (iterationCocone I Îº).pt = iteration I Îº := rfl

@[reassoc (attr := simp)]
lemma iterationCocone_w_app_app_left
    (f : Arrow C) {jâ‚ jâ‚‚ : Îº.ord.toType} (g : jâ‚ âŸ¶ jâ‚‚) :
    (((iterationFunctor I Îº).map g).app f).left â‰« (((iterationCocone I Îº).Î¹.app jâ‚‚).app f).left =
      (((iterationCocone I Îº).Î¹.app jâ‚).app f).left := by
  rw [â† Arrow.comp_left, â† NatTrans.comp_app, Cocone.w]

@[reassoc (attr := simp)]
lemma iterationCocone_w_app_app_right
    (f : Arrow C) {jâ‚ jâ‚‚ : Îº.ord.toType} (g : jâ‚ âŸ¶ jâ‚‚) :
    (((iterationFunctor I Îº).map g).app f).right â‰«
      (((iterationCocone I Îº).Î¹.app jâ‚‚).app f).right =
      (((iterationCocone I Îº).Î¹.app jâ‚).app f).right := by
  rw [â† Arrow.comp_right, â† NatTrans.comp_app, Cocone.w]

@[nolint unusedHavesSuffices]
noncomputable def isColimitIterationCocone : IsColimit (iterationCocone I Îº) :=
  have := hasIterationOfShape I Îº
  colimit.isColimit _

noncomputable def Î¹Iteration : ðŸ­ _ âŸ¶ iteration I Îº :=
  have := hasIterationOfShape I Îº
  (succStruct I Îº).Î¹Iteration Îº.ord.toType

def propArrow : MorphismProperty (Arrow C) := fun _ _ f â†¦
  (coproducts.{w} I).pushouts f.left âˆ§ (isomorphisms C) f.right

lemma succStruct_prop_le_propArrow :
    (succStruct I Îº).prop â‰¤ (propArrow.{w} I).functorCategory (Arrow C) := by
  have := hasColimitsOfShape_discrete I Îº
  have := hasPushouts I Îº
  intro _ _ _ âŸ¨FâŸ© f
  constructor
  Â· have := small_functorObjIndex I Îº (F.obj f).hom
    nth_rw 1 [â† I.ofHoms_homFamily]
    apply pushouts_mk _ (functorObj_isPushout I.homFamily (F.obj f).hom)
    exact coproducts_of_small _ _
      (colimitsOfShape_colimMap _ _ (by rintro âŸ¨jâŸ©; constructor))
  Â· rw [MorphismProperty.isomorphisms.iff]
    dsimp [succStruct]
    infer_instance

@[nolint unusedHavesSuffices]
lemma transfiniteCompositionOfShape_succStruct_prop_Î¹Iteration :
    (succStruct I Îº).prop.transfiniteCompositionsOfShape Îº.ord.toType (Î¹Iteration I Îº) := by
  have := hasIterationOfShape I Îº
  apply SuccStruct.transfiniteCompositionOfShape_Î¹Iteration

@[nolint unusedHavesSuffices]
lemma transfiniteCompositionOfShape_succStruct_iterationFunctor_map_from_bot (j : Îº.ord.toType) :
    (succStruct I Îº).prop.transfiniteCompositionsOfShape (Set.Iic j)
      ((iterationFunctor I Îº).map (homOfLE bot_le : âŠ¥ âŸ¶ j)) := by
  have := hasIterationOfShape I Îº
  apply SuccStruct.transfiniteCompositionOfShape_iterationFunctor_map_from_bot

lemma transfiniteCompositionOfShape_propArrow_Î¹Iteration :
    ((propArrow.{w} I).functorCategory (Arrow C)).transfiniteCompositionsOfShape
      Îº.ord.toType (Î¹Iteration I Îº) :=
  monotone_transfiniteCompositionsOfShape _ (succStruct_prop_le_propArrow I Îº) _
    (transfiniteCompositionOfShape_succStruct_prop_Î¹Iteration I Îº)

lemma transfiniteCompositionOfShape_propArrow_iterationFunctor_map_from_bot (j : Îº.ord.toType) :
    ((propArrow.{w} I).functorCategory (Arrow C)).transfiniteCompositionsOfShape
      (Set.Iic j) (((iterationFunctor I Îº).map (homOfLE bot_le : âŠ¥ âŸ¶ j))) :=
  monotone_transfiniteCompositionsOfShape _ (succStruct_prop_le_propArrow I Îº) _
    (transfiniteCompositionOfShape_succStruct_iterationFunctor_map_from_bot I Îº j)

omit Îº in
lemma propArrow_functorCategory_arrow_le (f : Arrow C) :
    (propArrow I).functorCategory (Arrow C) â‰¤
      (isomorphisms C).inverseImage
        ((evaluation (Arrow C) (Arrow C)).obj f â‹™ Arrow.rightFunc) := by
  intro _ _ _ h
  exact (h f).2

lemma isEventuallyConstantFrom_bot_iterationFunctor_evaluation_right (f : Arrow C) :
    (iterationFunctor I Îº â‹™
      (evaluation _ (Arrow C)).obj f â‹™ Arrow.rightFunc).IsEventuallyConstantFrom âŠ¥ := by
  intro j Ï†
  have := hasIterationOfShape I Îº
  suffices (isomorphisms _).transfiniteCompositionsOfShape (Set.Iic j)
      (((evaluation _ (Arrow C)).obj f â‹™ Arrow.rightFunc).map
      ((iterationFunctor I Îº).map Ï†)) from
    (isomorphisms C).transfiniteCompositionsOfShape_le _ _ this
  apply transfiniteCompositionsOfShape_map_of_preserves
  exact monotone_transfiniteCompositionsOfShape _
    (propArrow_functorCategory_arrow_le I f) _
    (transfiniteCompositionOfShape_propArrow_iterationFunctor_map_from_bot _ _ _)

instance isIso_Î¹Iteration_app_right (f : Arrow C) :
    IsIso ((Î¹Iteration I Îº).app f).right := by
  have := hasIterationOfShape I Îº
  suffices (isomorphisms _).transfiniteCompositionsOfShape Îº.ord.toType
      (((evaluation _ (Arrow C)).obj f â‹™ Arrow.rightFunc).map (Î¹Iteration I Îº)) from
    (isomorphisms C).transfiniteCompositionsOfShape_le Îº.ord.toType _ this
  apply transfiniteCompositionsOfShape_map_of_preserves
  exact monotone_transfiniteCompositionsOfShape _
    (propArrow_functorCategory_arrow_le I f) _
    (transfiniteCompositionOfShape_propArrow_Î¹Iteration I Îº)

instance (f : Arrow C) (j : Îº.ord.toType) :
    IsIso (((iterationCocone I Îº).Î¹.app j).app f).right :=
  have := hasIterationOfShape I Îº
  (isEventuallyConstantFrom_bot_iterationFunctor_evaluation_right
    I Îº f).isIso_Î¹_of_isColimit'
      (isColimitOfPreserves ((evaluation _ _).obj f â‹™ Arrow.rightFunc)
        (isColimitIterationCocone I Îº)) j (homOfLE bot_le)

noncomputable def iterationFunctorObjSuccObjLeftIso
    (f : Arrow C) (j : Îº.ord.toType) (hj : Â¬ IsMax j) :
    letI := hasColimitsOfShape_discrete I Îº
    letI := hasPushouts I Îº
    (((iterationFunctor I Îº).obj (Order.succ j)).obj f).left â‰…
        functorObj I.homFamily (((iterationFunctor I Îº).obj j).obj f).hom :=
  have := hasIterationOfShape I Îº
  Arrow.leftFunc.mapIso (((succStruct I Îº).iterationFunctorObjSuccIso j hj).app f)

@[reassoc]
lemma Î¹FunctorObj_iterationFunctorObjSuccObjLeftIso_inv
    (f : Arrow C) (j : Îº.ord.toType) (hj : Â¬ IsMax j) :
    letI := hasColimitsOfShape_discrete I Îº
    letI := hasPushouts I Îº
    Î¹FunctorObj I.homFamily (((iterationFunctor I Îº).obj j).obj f).hom â‰«
      (iterationFunctorObjSuccObjLeftIso I Îº f j hj).inv =
        (((iterationFunctor I Îº).map (homOfLE (Order.le_succ j))).app f).left := by
  have := hasIterationOfShape I Îº
  exact ((evaluation _ _).obj f â‹™ Arrow.leftFunc).congr_map
    ((succStruct I Îº).iterationFunctor_map_succ j hj).symm

lemma Ï€FunctorObj_iterationCocone_Î¹_app_app_right
    (f : Arrow C) (j : Îº.ord.toType) (hj : Â¬ IsMax j) :
    letI := hasColimitsOfShape_discrete I Îº
    letI := hasPushouts I Îº
    Ï€FunctorObj I.homFamily (((iterationFunctor I Îº).obj j).obj f).hom â‰«
        (((iterationCocone I Îº).Î¹.app j).app f).right =
      (iterationFunctorObjSuccObjLeftIso I Îº f j hj).inv â‰«
        (((iterationFunctor I Îº).obj (Order.succ j)).obj f).hom â‰«
        ((((iterationCocone I Îº).Î¹.app (Order.succ j)).app f)).right := by
  have := hasIterationOfShape I Îº
  simp [iterationFunctorObjSuccObjLeftIso, iterationFunctor,
    succStruct_succ_obj_hom I Îº, â† (iterationCocone I Îº).w (homOfLE (Order.le_succ j)),
    (succStruct I Îº).iterationFunctor_map_succ j hj, succStruct_toSucc_app_right]

lemma hasRightLiftingProperty_iteration_obj_hom (f : Arrow C) {A B : C} (i : A âŸ¶ B) (hi : I i):
    HasLiftingProperty i ((iteration I Îº).obj f).hom := âŸ¨by
  have := Cardinal.noMaxOrder (Fact.elim inferInstance : Îº.IsRegular).aleph0_le
  have := hasIterationOfShape I Îº
  have := hasColimitsOfShape_discrete I Îº
  have := hasPushouts I Îº
  intro g b sq
  have : PreservesColimit (iterationFunctor I Îº â‹™
    ((evaluation (Arrow C) (Arrow C)).obj f â‹™ Arrow.leftFunc))
      (coyoneda.obj (Opposite.op A)) :=
    preservesColimit_coyoneda_obj I Îº i hi _
      (fun j hj â†¦ (succStruct_prop_le_propArrow I Îº _
        ((succStruct I Îº).prop_iterationFunctor_map_succ j hj) f).1)
  obtain âŸ¨j, t, htâŸ© := Types.jointly_surjective _
    (isColimitOfPreserves (((evaluation _ _).obj f â‹™ Arrow.leftFunc) â‹™
      coyoneda.obj (Opposite.op A)) (isColimitIterationCocone I Îº)) g
  dsimp at t ht
  obtain âŸ¨l, hlâ‚, hlâ‚‚âŸ© := Î¹FunctorObj_extension (f := I.homFamily) (i := âŸ¨i, hiâŸ©)
    (Ï€X := (((iterationFunctor I Îº).obj j).obj f).hom) t
    (b â‰« inv (((iterationCocone I Îº).Î¹.app j).app f).right) (âŸ¨by
      have := (((iterationCocone I Îº).Î¹.app j).app f).w
      dsimp at this
      rw [â† cancel_mono (((iterationCocone I Îº).Î¹.app j).app f).right, assoc, assoc, assoc,
        IsIso.inv_hom_id]
      dsimp
      rw [comp_id, â† sq.w, â† this, â† reassoc_of% ht] âŸ©)
  dsimp at l hlâ‚
  exact âŸ¨âŸ¨{
    l := l â‰« (iterationFunctorObjSuccObjLeftIso I Îº f j (not_isMax j)).inv â‰«
        (((iterationCocone I Îº).Î¹.app (Order.succ j)).app f).left
    fac_left := by
      rw [reassoc_of% hlâ‚, â† ht, Î¹FunctorObj_iterationFunctorObjSuccObjLeftIso_inv_assoc,
        iterationCocone_w_app_app_left]
    fac_right := by
      rw [â† cancel_mono (inv (((iterationCocone I Îº).Î¹.app j).app f).right),
        assoc, assoc, assoc, â† hlâ‚‚,
        â† cancel_mono ((((iterationCocone I Îº).Î¹.app j).app f).right),
        assoc, assoc, assoc, assoc, assoc, IsIso.inv_hom_id, comp_id,
        Ï€FunctorObj_iterationCocone_Î¹_app_app_right _ _ _ _ (not_isMax j)]
      dsimp
      rw [Arrow.w_mk_right] }âŸ©âŸ©âŸ©

section

variable {X Y : C} (f : X âŸ¶ Y)

noncomputable def obj : C := ((iteration I Îº).obj (Arrow.mk f)).left

noncomputable def Î¹Obj : X âŸ¶ obj I Îº f := ((Î¹Iteration I Îº).app (Arrow.mk f)).left

noncomputable def Ï€Obj : obj I Îº f âŸ¶ Y :=
  ((iteration I Îº).obj (Arrow.mk f)).hom â‰« inv ((Î¹Iteration I Îº).app f).right

@[reassoc (attr := simp)]
lemma Î¹Obj_Ï€Obj : Î¹Obj I Îº f â‰« Ï€Obj I Îº f = f := by
  simp [Î¹Obj, Ï€Obj]

lemma transfiniteCompositionsOfShape_Î¹Obj :
    (coproducts.{w} I).pushouts.transfiniteCompositionsOfShape Îº.ord.toType
      (Î¹Obj I Îº f) := by
  have := hasIterationOfShape I Îº
  change (coproducts.{w} I).pushouts.transfiniteCompositionsOfShape Îº.ord.toType
    (((evaluation _ (Arrow C)).obj (Arrow.mk f) â‹™ Arrow.leftFunc).map (Î¹Iteration I Îº))
  apply transfiniteCompositionsOfShape_map_of_preserves
  apply monotone_transfiniteCompositionsOfShape _ _ _
    (transfiniteCompositionOfShape_propArrow_Î¹Iteration I Îº)
  intro _ _ _ h
  exact (h f).1

lemma rlp_llp_Î¹Obj : I.rlp.llp (Î¹Obj I Îº f) := by
  apply I.transfiniteCompositionsOfShape_pushouts_coproducts_le_rlp_llp Îº.ord.toType
  apply transfiniteCompositionsOfShape_Î¹Obj

lemma hasRightLiftingProperty_Ï€Obj {A B : C} (i : A âŸ¶ B) (hi : I i) (f : X âŸ¶ Y) :
    HasLiftingProperty i (Ï€Obj I Îº f) := by
  dsimp [Ï€Obj]
  have := hasRightLiftingProperty_iteration_obj_hom I Îº (Arrow.mk f) i hi
  infer_instance

lemma rlp_Ï€Obj : I.rlp (Ï€Obj I Îº f) :=
  fun _ _ _ hi â†¦ hasRightLiftingProperty_Ï€Obj _ _ _ hi _

end

section

noncomputable def objMap {f g : Arrow C} (Ï† : f âŸ¶ g) : obj I Îº f.hom âŸ¶ obj I Îº g.hom :=
  ((iteration I Îº).map Ï†).left

@[simp]
lemma objMap_id (f : Arrow C) : objMap I Îº (ðŸ™ f) = ðŸ™ _ := by
  simp only [objMap, Functor.map_id]
  rfl

@[reassoc, simp]
lemma objMap_comp {f g h : Arrow C} (Ï† : f âŸ¶ g) (Ïˆ : g âŸ¶ h) :
    objMap I Îº (Ï† â‰« Ïˆ) = objMap I Îº Ï† â‰« objMap I Îº Ïˆ := by
  simp only [objMap, Functor.map_comp]
  rfl

@[reassoc (attr := simp)]
lemma Î¹Obj_naturality {f g : Arrow C} (Ï† : f âŸ¶ g) :
    Î¹Obj I Îº f.hom â‰« objMap I Îº Ï† = Ï†.left â‰« Î¹Obj I Îº g.hom :=
  Arrow.leftFunc.congr_map ((Î¹Iteration I Îº).naturality Ï†).symm

@[reassoc (attr := simp)]
lemma Ï€Obj_naturality {f g : Arrow C} (Ï† : f âŸ¶ g) :
    objMap I Îº Ï† â‰« Ï€Obj I Îº g.hom = Ï€Obj I Îº f.hom â‰« Ï†.right := by
  let eâ‚ := asIso ((Î¹Iteration I Îº).app (Arrow.mk f.hom)).right
  let eâ‚‚ := asIso ((Î¹Iteration I Îº).app (Arrow.mk g.hom)).right
  change _ â‰« _ â‰« eâ‚‚.inv = (_ â‰« eâ‚.inv) â‰« _
  have hâ‚ := ((iteration I Îº).map Ï†).w =â‰« eâ‚‚.inv
  have hâ‚‚ : Ï†.right â‰« eâ‚‚.hom = eâ‚.hom â‰« ((iteration I Îº).map Ï†).right :=
    ((whiskerRight (Î¹Iteration I Îº) Arrow.rightFunc).naturality Ï†)
  dsimp at hâ‚
  rw [assoc] at hâ‚
  apply hâ‚.trans
  simp only [â† cancel_mono eâ‚‚.hom, assoc, eâ‚‚.inv_hom_id, hâ‚‚, eâ‚.inv_hom_id_assoc]
  rw [â† assoc]
  apply comp_id

end

@[simps]
noncomputable def functorialFactorizationData :
    FunctorialFactorizationData I.rlp.llp I.rlp where
  Z :=
    { obj f := obj I Îº f.hom
      map Ï† := objMap I Îº Ï†
      map_id := by aesop_cat
      map_comp := by aesop_cat }
  i := { app f := Î¹Obj I Îº f.hom }
  p := { app f := Ï€Obj I Îº f.hom }
  hi f := rlp_llp_Î¹Obj I Îº f.hom
  hp f := rlp_Ï€Obj I Îº f.hom

lemma hasFunctorialFactorization :
    HasFunctorialFactorization I.rlp.llp I.rlp where
  nonempty_functorialFactorizationData :=
    âŸ¨functorialFactorizationData I ÎºâŸ©

/-- If `Îº` is a suitable cardinal for the small object argument for `I : MorphismProperty C`,
then the class `I.rlp.llp` is exactly the class of morphisms that are retracts
of transfinite compositions (of shape `Îº.ord.toType`) of pushouts of coproducts
of maps in `I`.  -/
lemma rlp_llp_of_isCardinalForSmallObjectArgument' :
    I.rlp.llp = (transfiniteCompositionsOfShape
      (coproducts.{w} I).pushouts Îº.ord.toType).retracts := by
  refine le_antisymm ?_
    (retracts_transfiniteCompositionsOfShape_pushouts_coproducts_le_rlp_llp I Îº.ord.toType)
  intro X Y f hf
  have sq : CommSq (Î¹Obj I Îº f) f (Ï€Obj I Îº f) (ðŸ™ _) := âŸ¨by simpâŸ©
  have := hf _ (rlp_Ï€Obj I Îº f)
  refine âŸ¨_, _, _, ?_, transfiniteCompositionsOfShape_Î¹Obj I Îº fâŸ©
  exact
    { i := Arrow.homMk (ðŸ™ _) sq.lift
      r := Arrow.homMk (ðŸ™ _) (Ï€Obj I Îº f) }

/-- If `Îº` is a suitable cardinal for the small object argument for `I : MorphismProperty C`,
then the class `I.rlp.llp` is exactly the class of morphisms that are retracts
of transfinite compositions of pushouts of coproducts of maps in `I`.  -/
lemma rlp_llp_of_isCardinalForSmallObjectArgument :
    I.rlp.llp =
      (transfiniteCompositions.{w} (coproducts.{w} I).pushouts).retracts := by
  refine le_antisymm ?_
    (retracts_transfiniteComposition_pushouts_coproducts_le_rlp_llp I)
  rw [rlp_llp_of_isCardinalForSmallObjectArgument' I Îº]
  apply monotone_retracts
  apply transfiniteCompositionsOfShape_le_transfiniteCompositions

end

end SmallObject

end CategoryTheory
