/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.SmallObject.Construction
import Mathlib.CategoryTheory.SmallObject.TransfiniteCompositionLifting
import Mathlib.CategoryTheory.SmallObject.TransfiniteIteration
import Mathlib.CategoryTheory.MorphismProperty.IsSmall
import Mathlib.CategoryTheory.MorphismProperty.LiftingProperty
import Mathlib.AlgebraicTopology.RelativeCellComplex.Basic
import Mathlib.SetTheory.Cardinal.Cofinality

/-!
# Cardinals that are suitable for the small object argument

In this file, given a class of morphisms `I : MorphismProperty C` and
a regular cardinal `Œ∫ : Cardinal.{w}`, we define a typeclass
`IsCardinalForSmallObjectArgument I Œ∫` which requires certain
smallness properties (`I` is `w`-small, `C` is locally `w`-small),
the existence of certain colimits (pushouts, coproducts of size `w`,
and the condition `HasIterationOfShape Œ∫.ord.toType C` about the
existence of colimits indexed by limit ordinal smaller than or equal
to `Œ∫.ord`), and the technical assumption that if `A` is the
a morphism in `I`, then the functor `Hom(A, _)` should commute
with the filtering colimits corresponding to relative
`I`-cell complexes. (This last condition shall hold when `Œ∫`
is the successor of an infinite cardinal `c` such that all
these objects `A` are `c`-presentable, see the file `Presentable.Basic`.)

## References
- https://ncatlab.org/nlab/show/small+object+argument

-/

universe w v v' u u'

namespace CategoryTheory

open Category HomotopicalAlgebra Limits SmallObject

variable {C : Type u} [Category.{v} C] (I : MorphismProperty C)

namespace MorphismProperty

/-- Given `I : MorphismProperty C` and a regular cardinal `Œ∫ : Cardinal.{w}`,
this property asserts the technical conditions which allow to proceed
to the small object argument by doing a construction by transfinite
induction indexed by the well ordered type `Œ∫.ord.toType`. -/
class IsCardinalForSmallObjectArgument (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular]
    [OrderBot Œ∫.ord.toType] : Prop where
  isSmall : IsSmall.{w} I := by infer_instance
  locallySmall : LocallySmall.{w} C := by infer_instance
  hasPushouts : HasPushouts C := by infer_instance
  hasCoproducts : HasCoproducts.{w} C := by infer_instance
  hasIterationOfShape : HasIterationOfShape Œ∫.ord.toType C
  preservesColimit {A B X Y : C} (i : A ‚ü∂ B) (_ : I i) (f : X ‚ü∂ Y)
    (hf : RelativeCellComplex.{w} (fun (_ : Œ∫.ord.toType) ‚Ü¶ I.homFamily) f) :
    PreservesColimit hf.F (coyoneda.obj (Opposite.op A)) := by infer_instance

end MorphismProperty

namespace SmallObject

open MorphismProperty

variable (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular] [OrderBot Œ∫.ord.toType]
  [I.IsCardinalForSmallObjectArgument Œ∫]

include I Œ∫

lemma isSmall : IsSmall.{w} I :=
  IsCardinalForSmallObjectArgument.isSmall Œ∫

lemma locallySmall : LocallySmall.{w} C :=
  IsCardinalForSmallObjectArgument.locallySmall I Œ∫

lemma hasIterationOfShape : HasIterationOfShape Œ∫.ord.toType C :=
  IsCardinalForSmallObjectArgument.hasIterationOfShape I

lemma hasPushouts : HasPushouts C :=
  IsCardinalForSmallObjectArgument.hasPushouts I Œ∫

lemma hasCoproducts : HasCoproducts.{w} C :=
  IsCardinalForSmallObjectArgument.hasCoproducts I Œ∫

lemma preservesColimit {A B X Y : C} (i : A ‚ü∂ B) (hi : I i) (f : X ‚ü∂ Y)
    (hf : RelativeCellComplex.{w} (fun (_ : Œ∫.ord.toType) ‚Ü¶ I.homFamily) f) :
    PreservesColimit hf.F (coyoneda.obj (Opposite.op A)) :=
  IsCardinalForSmallObjectArgument.preservesColimit i hi f hf

lemma hasColimitsOfShape_discrete (X Y : C) (p : X ‚ü∂ Y) :
    HasColimitsOfShape
      (Discrete (FunctorObjIndex I.homFamily p)) C := by
  have := locallySmall I Œ∫
  have := isSmall I Œ∫
  have := hasCoproducts I Œ∫
  exact hasColimitsOfShape_of_equivalence
    (Discrete.equivalence (equivShrink.{w} _)).symm

/-- The successor structure on `Arrow C ‚•§ Arrow C` corresponding
to the iterations of the natural transformation
`Œµ : ùü≠ (Arrow C) ‚ü∂ SmallObject.functor I.homFamily`
(see the file `SmallObject.Construction`). -/
noncomputable def succStruct : SuccStruct (Arrow C ‚•§ Arrow C) :=
  have := hasColimitsOfShape_discrete I Œ∫
  have := hasPushouts I Œ∫
  SuccStruct.ofNatTrans (Œµ I.homFamily)

/-- For the successor structure `succStruct I Œ∫` on `Arrow C ‚•§ Arrow C`,
the morphism from an object to its successor induces
morphisms in `C` which consists in attaching `I`-cells. -/
@[nolint unusedHavesSuffices]
noncomputable def attachCellsOfSuccStructProp
    {F G : Arrow C ‚•§ Arrow C} {œÜ : F ‚ü∂ G}
    (h : (succStruct I Œ∫).prop œÜ) (f : Arrow C) :
    AttachCells.{w} I.homFamily (œÜ.app f).left :=
  have := locallySmall I Œ∫
  have := isSmall I Œ∫
  have := hasColimitsOfShape_discrete I Œ∫
  have := hasPushouts I Œ∫
  AttachCells.ofArrowIso (attachCellsŒπFunctorObjOfSmall _ _)
    ((Functor.mapArrow ((evaluation _ _).obj f ‚ãô
      Arrow.leftFunc)).mapIso h.arrowIso.symm)

/-- The class of morphisms in `Arrow C` which on the left side are
pushouts of coproducts of morphisms in `I`, and which are
isomorphisms on the right side. -/
def propArrow : MorphismProperty (Arrow C) := fun _ _ f ‚Ü¶
  (coproducts.{w} I).pushouts f.left ‚àß (isomorphisms C) f.right

lemma succStruct_prop_le_propArrow :
    (succStruct I Œ∫).prop ‚â§ (propArrow.{w} I).functorCategory (Arrow C) := by
  have := locallySmall I Œ∫
  have := isSmall I Œ∫
  have := hasColimitsOfShape_discrete I Œ∫
  have := hasPushouts I Œ∫
  intro _ _ _ ‚ü®F‚ü© f
  constructor
  ¬∑ nth_rw 1 [‚Üê I.ofHoms_homFamily]
    apply pushouts_mk _ (functorObj_isPushout I.homFamily (F.obj f).hom)
    exact coproducts_of_small _ _
      (colimitsOfShape_colimMap _ _ (by rintro ‚ü®j‚ü©; constructor))
  ¬∑ rw [MorphismProperty.isomorphisms.iff]
    dsimp [succStruct]
    infer_instance

/-- The functor `Œ∫.ord.toType ‚•§ Arrow C ‚•§ Arrow C` corresponding to the
iterations of the successor structure `succStruct I Œ∫`. -/
noncomputable def iterationFunctor : Œ∫.ord.toType ‚•§ Arrow C ‚•§ Arrow C :=
  have := hasIterationOfShape I Œ∫
  (succStruct I Œ∫).iterationFunctor Œ∫.ord.toType

/-- The colimit of `iterationFunctor I Œ∫`. -/
noncomputable def iteration : Arrow C ‚•§ Arrow C :=
  have := hasIterationOfShape I Œ∫
  (succStruct I Œ∫).iteration Œ∫.ord.toType

/-- The natural "inclusion" `ùü≠ (Arrow C) ‚ü∂ iteration I Œ∫`. -/
noncomputable def ŒπIteration : ùü≠ _ ‚ü∂ iteration I Œ∫ :=
  have := hasIterationOfShape I Œ∫
  (succStruct I Œ∫).ŒπIteration Œ∫.ord.toType

/-- The morphism `ŒπIteration I Œ∫` is a transfinite composition of shape
`Œ∫.ord.toType` of morphisms satisfying `(succStruct I Œ∫).prop`. -/
noncomputable def transfiniteCompositionOfShapeSuccStructPropŒπIteration :
    (succStruct I Œ∫).prop.TransfiniteCompositionOfShape Œ∫.ord.toType (ŒπIteration I Œ∫) :=
  have := hasIterationOfShape I Œ∫
  (succStruct I Œ∫).transfiniteCompositionOfShapeŒπIteration Œ∫.ord.toType

@[simp]
lemma transfiniteCompositionOfShapeSuccStructPropŒπIteration_F :
    (transfiniteCompositionOfShapeSuccStructPropŒπIteration I Œ∫).F =
      iterationFunctor I Œ∫ :=
  rfl

/-- For any `f : Arrow C`, the map `((ŒπIteration I Œ∫).app f).right` is
a transfinite composition of isomorphisms. -/
noncomputable def transfiniteCompositionOfShapeŒπIterationAppRight (f : Arrow C) :
    (isomorphisms C).TransfiniteCompositionOfShape Œ∫.ord.toType
      ((ŒπIteration I Œ∫).app f).right :=
  have := hasIterationOfShape I Œ∫
  let h := transfiniteCompositionOfShapeSuccStructPropŒπIteration I Œ∫
  { toTransfiniteCompositionOfShape :=
      h.toTransfiniteCompositionOfShape.map ((evaluation _ _).obj f ‚ãô Arrow.rightFunc)
    map_mem j hj := ((succStruct_prop_le_propArrow I Œ∫ _ (h.map_mem j hj)) f).2 }

end SmallObject

end CategoryTheory
