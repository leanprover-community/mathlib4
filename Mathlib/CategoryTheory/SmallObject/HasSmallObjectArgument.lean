/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.SmallObject.Basic
import Mathlib.CategoryTheory.SmallObject.TransfiniteCompositionLifting
import Mathlib.CategoryTheory.MorphismProperty.IsSmall
import Mathlib.CategoryTheory.MorphismProperty.LiftingProperty
import Mathlib.SetTheory.Cardinal.HasCardinalLT

/-!
# Morphism properties which admits a small object argument

## References
- https://ncatlab.org/nlab/show/small+object+argument

-/

universe w v u

lemma Cardinal.zero_lt_ord_iff (Îº : Cardinal.{w}) : 0 < Îº.ord â†” Îº â‰  0 := by
  constructor
  Â· intro h h'
    simp only [h', ord_zero, lt_self_iff_false] at h
  Â· intro h
    by_contra!
    exact h (ord_eq_zero.1 (le_antisymm this (Ordinal.zero_le _)))

noncomputable def Cardinal.IsRegular.orderBotOrdToType
    {Îº : Cardinal.{w}} (hÎº : Îº.IsRegular) : OrderBot Îº.ord.toType :=
  Ordinal.toTypeOrderBotOfPos (by
    rw [Cardinal.zero_lt_ord_iff]
    rintro rfl
    apply Cardinal.aleph0_ne_zero.{w}
    simpa using hÎº.aleph0_le)

namespace CategoryTheory

noncomputable instance (o : Ordinal.{w}) : SuccOrder o.toType :=
  SuccOrder.ofLinearWellFoundedLT o.toType

open Limits

variable {C : Type u} [Category.{v} C]

namespace MorphismProperty

variable (I : MorphismProperty C)

class IsCardinalForSmallObjectArgument (Îº : Cardinal.{w}) [OrderBot Îº.ord.toType] : Prop where
  isSmall : IsSmall.{w} I
  isRegular : Îº.IsRegular
  locallySmall : LocallySmall.{w} C
  hasPushouts : HasPushouts C
  hasCoproducts : HasCoproducts.{w} C
  hasIterationOfShape : HasIterationOfShape C Îº.ord.toType
  preservesColimit :
      âˆ€ {A B : C} (i : A âŸ¶ B) (_ : I i)
      (F : Îº.ord.toType â¥¤ C) [F.IsWellOrderContinuous]
      (_ : âˆ€ (j : _) (_ : Â¬IsMax j),
        (coproducts.{w} I).pushouts (F.map (homOfLE (Order.le_succ j)))),
      PreservesColimit F (coyoneda.obj (Opposite.op A))

class HasSmallObjectArgument : Prop where
  exists_cardinal : âˆƒ (Îº : Cardinal.{w}) (_ : OrderBot Îº.ord.toType),
    IsCardinalForSmallObjectArgument I Îº

variable [HasSmallObjectArgument.{w} I]

noncomputable def smallObjectÎº : Cardinal.{w} :=
  (HasSmallObjectArgument.exists_cardinal (I := I)).choose

noncomputable def smallObjectÎº_isRegular : I.smallObjectÎº.IsRegular :=
  (HasSmallObjectArgument.exists_cardinal (I := I)).choose_spec.choose_spec.isRegular

noncomputable instance : OrderBot I.smallObjectÎº.ord.toType :=
  (HasSmallObjectArgument.exists_cardinal (I := I)).choose_spec.choose

instance isCardinalForSmallObjectArgument_smallObjectÎº :
    IsCardinalForSmallObjectArgument.{w} I I.smallObjectÎº :=
  (HasSmallObjectArgument.exists_cardinal (I := I)).choose_spec.choose_spec

end MorphismProperty

namespace SmallObject

open MorphismProperty

variable (I : MorphismProperty C)

section

variable (Îº : Cardinal.{w}) [OrderBot Îº.ord.toType] [I.IsCardinalForSmallObjectArgument Îº]

include I Îº

lemma isSmall : IsSmall.{w} I :=
  IsCardinalForSmallObjectArgument.isSmall Îº

lemma locallySmall : LocallySmall.{w} C :=
  IsCardinalForSmallObjectArgument.locallySmall I Îº

lemma isRegular : Îº.IsRegular :=
  IsCardinalForSmallObjectArgument.isRegular I

lemma hasIterationOfShape : HasIterationOfShape C Îº.ord.toType :=
  IsCardinalForSmallObjectArgument.hasIterationOfShape I

lemma hasPushouts : HasPushouts C :=
  IsCardinalForSmallObjectArgument.hasPushouts I Îº

lemma hasCoproducts : HasCoproducts.{w} C :=
  IsCardinalForSmallObjectArgument.hasCoproducts I Îº

lemma preservesColimit_coyoneda_obj
    {A B : C} (i : A âŸ¶ B) (hi : I i)
    (F : Îº.ord.toType â¥¤ C) [F.IsWellOrderContinuous]
    (hF : âˆ€ (j : Îº.ord.toType) (_ : Â¬IsMax j),
      (coproducts.{w} I).pushouts (F.map (homOfLE (Order.le_succ j)))) :
    PreservesColimit F (coyoneda.obj (Opposite.op A)) :=
  IsCardinalForSmallObjectArgument.preservesColimit i hi F hF

variable {X Y : C} (p : X âŸ¶ Y)

lemma small_functorObjIndex :
    Small.{w} (FunctorObjIndex I.homFamily p) := by
  have := locallySmall I Îº
  have := isSmall I Îº
  infer_instance

variable (X Y) in
lemma hasColimitsOfShape_discrete :
    HasColimitsOfShape
      (Discrete (FunctorObjIndex I.homFamily p)) C := by
  have := small_functorObjIndex I Îº p
  have := hasCoproducts I Îº
  exact hasColimitsOfShape_of_equivalence (Discrete.equivalence (equivShrink.{w} _)).symm

variable (Y) in
noncomputable def transfiniteIterationFunctor : Over Y â¥¤ Over Y :=
  have := hasIterationOfShape I Îº
  have := hasPushouts I Îº
  have := hasColimitsOfShape_discrete I Îº
  ((functor I.homFamily Y).transfiniteIteration (Îµ _ Y) Îº.ord.toType)

variable (Y) in
noncomputable def Î¹TransfiniteIterationFunctor : ðŸ­ _ âŸ¶ transfiniteIterationFunctor I Îº Y :=
  have := hasIterationOfShape I Îº
  have := hasPushouts I Îº
  have := hasColimitsOfShape_discrete I Îº
  ((functor I.homFamily Y).Î¹TransfiniteIteration (Îµ _ Y) Îº.ord.toType)

noncomputable def object : C := ((transfiniteIterationFunctor I Îº Y).obj (Over.mk p)).left

noncomputable def Î¹Object : X âŸ¶ object I Îº p :=
  ((Î¹TransfiniteIterationFunctor I Îº Y).app (Over.mk p)).left

noncomputable def Ï€Object : object I Îº p âŸ¶ Y :=
  ((transfiniteIterationFunctor I Îº Y).obj (Over.mk p)).hom

@[reassoc (attr := simp)]
lemma Î¹Object_Ï€Object : Î¹Object I Îº p â‰« Ï€Object I Îº p = p := by
  simp [Î¹Object, Ï€Object]

lemma transfiniteCompositionsOfShape_Î¹Object :
    (coproducts.{w} I).pushouts.transfiniteCompositionsOfShape Îº.ord.toType
      (Î¹Object I Îº p) := by
  have := hasIterationOfShape I Îº
  have := hasColimitsOfShape_discrete I Îº
  have := hasPushouts I Îº
  have := isSmall I Îº
  have := locallySmall I Îº
  simpa only [ofHoms_homFamily] using
    transfiniteCompositionsOfShape_Î¹Obj (f := homFamily I) (J := Îº.ord.toType) (p := p)

lemma rlp_Ï€Object : I.rlp (Ï€Object I Îº p) := by
  have := Cardinal.noMaxOrder (isRegular I Îº).aleph0_le
  have := hasIterationOfShape I Îº
  have := hasColimitsOfShape_discrete I Îº
  have := hasPushouts I Îº
  have := isSmall I Îº
  have := locallySmall I Îº
  have (i : I.toSet) : PreservesColimit (inductiveSystemForget I.homFamily Îº.ord.toType p)
      (coyoneda.obj (Opposite.op i.1.left)) :=
    preservesColimit_coyoneda_obj I Îº i.1.hom i.2 _ (fun j hj â†¦ by
      refine (arrow_mk_iso_iff _
        ((Over.forget _).mapArrow.mapIso
          (Functor.transfiniteIterationMapLeSuccAppArrowIso _ _ j hj _))).2 ?_
      simpa using coproducts_pushouts_Î¹FunctorObj (homFamily I) _)
  intro _ _ _ hi
  rw [â† ofHoms_homFamily I] at hi
  obtain âŸ¨iâŸ© := hi
  apply hasLiftingProperty_Ï€Obj

@[simps]
noncomputable def mapFactorizationData : MapFactorizationData I.rlp.llp I.rlp p where
  i := Î¹Object I Îº p
  p := Ï€Object I Îº p
  hi := by
    simpa using transfiniteCompositionsOfShape_le_rlp_llp _ _ _
      (transfiniteCompositionsOfShape_Î¹Object I Îº p)
  hp := rlp_Ï€Object I Îº p

/-- Variant of `rlp_llp` which shows it suffices to consider transfinite compositions
indexed by `Îº.ord.toType`. -/
lemma rlp_llp' :
    I.rlp.llp =
      ((coproducts.{w} I).pushouts.transfiniteCompositionsOfShape Îº.ord.toType).retracts := by
  apply le_antisymm
  Â· intro X Y f hf
    replace hf := hf _ (rlp_Ï€Object I Îº f)
    have sq : CommSq (Î¹Object I Îº f) f (Ï€Object I Îº f) (ðŸ™ _) := âŸ¨by simpâŸ©
    refine âŸ¨_, _, _, ?_, transfiniteCompositionsOfShape_Î¹Object I Îº fâŸ©
    -- this is a particular case of the retract argument
    exact
      { i := Arrow.homMk (u := ðŸ™ X) (v := sq.lift) (by simp)
        r := Arrow.homMk (u := ðŸ™ X) (v := Ï€Object I Îº f) (by simp) }
  Â· rw [le_llp_iff_le_rlp, retracts_rlp, â† le_llp_iff_le_rlp]
    simpa using transfiniteCompositionsOfShape_le_rlp_llp
      (coproducts.{w} I).pushouts Îº.ord.toType

end

section

variable [HasSmallObjectArgument I]

lemma hasFactorization : HasFactorization I.rlp.llp I.rlp where
  nonempty_mapFactorizationData p := âŸ¨mapFactorizationData I I.smallObjectÎº pâŸ©

lemma rlp_llp :
    I.rlp.llp =
      (transfiniteCompositions.{w} (coproducts.{w} I).pushouts).retracts := by
  apply le_antisymm
  Â· rw [rlp_llp' I I.smallObjectÎº]
    apply monotone_retracts
    apply transfiniteCompositionsOfShape_le_transfiniteCompositions
  Â· rw [le_llp_iff_le_rlp, retracts_rlp, â† le_llp_iff_le_rlp]
    simpa using transfiniteCompositions_le_rlp_llp.{w} (coproducts.{w} I).pushouts

end

end SmallObject

end CategoryTheory
