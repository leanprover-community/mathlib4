/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Limits.Shapes.Products
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.HasPullback
import Mathlib.AlgebraicTopology.RelativeCellComplex.AttachCells

/-!
# Construction for the small object argument

Given a family of morphisms `f i : A i âŸ¶ B i` in a category `C`,
we define a functor
`SmallObject.functor f : Arrow S â¥¤ Arrow S` which sends
an object given by arrow `Ï€X : X âŸ¶ S` to the pushout `functorObj f Ï€X`:
```
âˆ functorObjSrcFamily f Ï€X âŸ¶       X

            |                      |
            |                      |
            v                      v

âˆ functorObjTgtFamily f Ï€X âŸ¶ functorObj f Ï€X
```
where the morphism on the left is a coproduct (of copies of maps `f i`)
indexed by a type `FunctorObjIndex f Ï€X` which parametrizes the
diagrams of the form
```
A i âŸ¶ X
 |    |
 |    |
 v    v
B i âŸ¶ S
```

The morphism `Î¹FunctorObj f Ï€X : X âŸ¶ functorObj f Ï€X` is part of
a natural transformation `SmallObject.Îµ f : ğŸ­ (Arrow C) âŸ¶ functor f S`.
The main idea in this construction is that for any commutative square
as above, there may not exist a lifting `B i âŸ¶ X`, but the construction
provides a tautological morphism `B i âŸ¶ functorObj f Ï€X`
(see `SmallObject.Î¹FunctorObj_extension`).

## References
- https://ncatlab.org/nlab/show/small+object+argument

-/
universe t w v u

namespace CategoryTheory

open Category Limits HomotopicalAlgebra

namespace SmallObject

variable {C : Type u} [Category.{v} C] {I : Type w} {A B : I â†’ C} (f : âˆ€ i, A i âŸ¶ B i)

section

variable {S X : C} (Ï€X : X âŸ¶ S)

/-- Given a family of morphisms `f i : A i âŸ¶ B i` and a morphism `Ï€X : X âŸ¶ S`,
this type parametrizes the commutative squares with a morphism `f i` on the left
and `Ï€X` in the right. -/
structure FunctorObjIndex where
  /-- an element in the index type -/
  i : I
  /-- the top morphism in the square -/
  t : A i âŸ¶ X
  /-- the bottom morphism in the square -/
  b : B i âŸ¶ S
  w : t â‰« Ï€X = f i â‰« b

attribute [reassoc (attr := simp)] FunctorObjIndex.w

variable [HasColimitsOfShape (Discrete (FunctorObjIndex f Ï€X)) C]

/-- The family of objects `A x.i` parametrized by `x : FunctorObjIndex f Ï€X`. -/
abbrev functorObjSrcFamily (x : FunctorObjIndex f Ï€X) : C := A x.i

/-- The family of objects `B x.i` parametrized by `x : FunctorObjIndex f Ï€X`. -/
abbrev functorObjTgtFamily (x : FunctorObjIndex f Ï€X) : C := B x.i

/-- The family of the morphisms `f x.i : A x.i âŸ¶ B x.i`
parametrized by `x : FunctorObjIndex f Ï€X`. -/
abbrev functorObjLeftFamily (x : FunctorObjIndex f Ï€X) :
    functorObjSrcFamily f Ï€X x âŸ¶ functorObjTgtFamily f Ï€X x := f x.i

/-- The top morphism in the pushout square in the definition of `pushoutObj f Ï€X`. -/
noncomputable abbrev functorObjTop : âˆ functorObjSrcFamily f Ï€X âŸ¶ X :=
  Limits.Sigma.desc (fun x => x.t)

/-- The left morphism in the pushout square in the definition of `pushoutObj f Ï€X`. -/
noncomputable abbrev functorObjLeft :
    âˆ functorObjSrcFamily f Ï€X âŸ¶ âˆ functorObjTgtFamily f Ï€X :=
  Limits.Sigma.map (functorObjLeftFamily f Ï€X)

variable [HasPushout (functorObjTop f Ï€X) (functorObjLeft f Ï€X)]

/-- The functor `SmallObject.functor f : Arrow C â¥¤ Arrow C` that is part of
the small object argument for a family of morphisms `f`, on an object given
as a morphism `Ï€X : X âŸ¶ S`. -/
noncomputable abbrev functorObj : C :=
  pushout (functorObjTop f Ï€X) (functorObjLeft f Ï€X)

/-- The canonical morphism `X âŸ¶ functorObj f Ï€X`. -/
noncomputable abbrev Î¹FunctorObj : X âŸ¶ functorObj f Ï€X := pushout.inl _ _

/-- The canonical morphism `âˆ (functorObjTgtFamily f Ï€X) âŸ¶ functorObj f Ï€X`. -/
noncomputable abbrev ÏFunctorObj : âˆ functorObjTgtFamily f Ï€X âŸ¶ functorObj f Ï€X := pushout.inr _ _

@[reassoc]
lemma functorObj_comm :
    functorObjTop f Ï€X â‰« Î¹FunctorObj f Ï€X = functorObjLeft f Ï€X â‰« ÏFunctorObj f Ï€X :=
  pushout.condition

lemma functorObj_isPushout :
    IsPushout (functorObjTop f Ï€X) (functorObjLeft f Ï€X) (Î¹FunctorObj f Ï€X) (ÏFunctorObj f Ï€X) :=
  IsPushout.of_hasPushout _ _

@[reassoc]
lemma FunctorObjIndex.comm (x : FunctorObjIndex f Ï€X) :
    f x.i â‰« Sigma.Î¹ (functorObjTgtFamily f Ï€X) x â‰« ÏFunctorObj f Ï€X = x.t â‰« Î¹FunctorObj f Ï€X := by
  simpa using (Sigma.Î¹ (functorObjSrcFamily f Ï€X) x â‰«= functorObj_comm f Ï€X).symm

/-- The canonical projection on the base object. -/
noncomputable abbrev Ï€'FunctorObj : âˆ functorObjTgtFamily f Ï€X âŸ¶ S := Sigma.desc (fun x => x.b)

/-- The canonical projection on the base object. -/
noncomputable def Ï€FunctorObj : functorObj f Ï€X âŸ¶ S :=
  pushout.desc Ï€X (Ï€'FunctorObj f Ï€X) (by ext; simp [Ï€'FunctorObj])

@[reassoc (attr := simp)]
lemma ÏFunctorObj_Ï€ : ÏFunctorObj f Ï€X â‰« Ï€FunctorObj f Ï€X = Ï€'FunctorObj f Ï€X := by
  simp [Ï€FunctorObj]

@[reassoc (attr := simp)]
lemma Î¹FunctorObj_Ï€FunctorObj : Î¹FunctorObj f Ï€X â‰« Ï€FunctorObj f Ï€X = Ï€X := by
  simp [Î¹FunctorObj, Ï€FunctorObj]

/-- The morphism `Î¹FunctorObj f Ï€X : X âŸ¶ functorObj f Ï€X` is obtained by
attaching `f`-cells. -/
@[simps]
noncomputable def attachCellsÎ¹FunctorObj :
    AttachCells.{max v w} f (Î¹FunctorObj f Ï€X) where
  Î¹ := FunctorObjIndex f Ï€X
  Ï€ x := x.i
  isColimitâ‚ := coproductIsCoproduct _
  isColimitâ‚‚ := coproductIsCoproduct _
  m := functorObjLeft f Ï€X
  gâ‚ := functorObjTop f Ï€X
  gâ‚‚ := ÏFunctorObj f Ï€X
  isPushout := IsPushout.of_hasPushout (functorObjTop f Ï€X) (functorObjLeft f Ï€X)
  cofanâ‚ := _
  cofanâ‚‚ := _

section Small

variable [LocallySmall.{t} C] [Small.{t} I]

instance : Small.{t} (FunctorObjIndex f Ï€X) := by
  let Ï† (x : FunctorObjIndex f Ï€X) :
    Î£ (i : Shrink.{t} I),
      Shrink.{t} ((A ((equivShrink _).symm i) âŸ¶ X) Ã—
        (B ((equivShrink _).symm i) âŸ¶ S)) :=
        âŸ¨equivShrink _ x.i, equivShrink _
          âŸ¨eqToHom (by simp) â‰« x.t, eqToHom (by simp) â‰« x.bâŸ©âŸ©
  have hÏ† : Function.Injective Ï† := by
    rintro âŸ¨iâ‚, tâ‚, bâ‚, _âŸ© âŸ¨iâ‚‚, tâ‚‚, bâ‚‚, _âŸ© h
    obtain rfl : iâ‚ = iâ‚‚ := by simpa [Ï†] using congr_arg Sigma.fst h
    simpa [cancel_epi, Ï†] using h
  exact small_of_injective hÏ†

instance : Small.{t} (attachCellsÎ¹FunctorObj f Ï€X).Î¹ := by
  dsimp
  infer_instance

/-- The morphism `Î¹FunctorObj f Ï€X : X âŸ¶ functorObj f Ï€X` is obtained by
attaching `f`-cells, and the index type can be chosen to be in `Type t`
if the category is `t`-locally small and the index type for `f`
is `t`-small. -/
noncomputable def attachCellsÎ¹FunctorObjOfSmall :
    AttachCells.{t} f (Î¹FunctorObj f Ï€X) :=
  (attachCellsÎ¹FunctorObj f Ï€X).reindex (equivShrink.{t} _).symm

end Small

section

variable {S T X Y : C} {Ï€X : X âŸ¶ S} {Ï€Y : Y âŸ¶ T} (Ï„ : Arrow.mk Ï€X âŸ¶ Arrow.mk Ï€Y)
  [HasColimitsOfShape (Discrete (FunctorObjIndex f Ï€X)) C]
  [HasColimitsOfShape (Discrete (FunctorObjIndex f Ï€Y)) C]

/-- The canonical morphism `âˆ (functorObjSrcFamily f Ï€X) âŸ¶ âˆ (functorObjSrcFamily f Ï€Y)`
induced by a morphism `Arrow.mk Ï€X âŸ¶ Arrow.mk Ï€Y`. -/
noncomputable def functorMapSrc :
    âˆ (functorObjSrcFamily f Ï€X) âŸ¶ âˆ functorObjSrcFamily f Ï€Y :=
  Sigma.map' (fun x => FunctorObjIndex.mk x.i (x.t â‰« Ï„.left) (x.b â‰« Ï„.right) (by simp))
    (fun _ => ğŸ™ _)

@[reassoc]
lemma Î¹_functorMapSrc (i : I) (t : A i âŸ¶ X) (b : B i âŸ¶ S) (w : t â‰« Ï€X = f i â‰« b)
    (b' : B i âŸ¶ T) (hb' : b â‰« Ï„.right = b')
    (t' : A i âŸ¶ Y) (ht' : t â‰« Ï„.left = t') :
    Sigma.Î¹ _ (FunctorObjIndex.mk i t b w) â‰« functorMapSrc f Ï„ =
      Sigma.Î¹ (functorObjSrcFamily f Ï€Y)
        (FunctorObjIndex.mk i t' b' (by
          have := Ï„.w
          dsimp at this
          rw [â† hb', â† reassoc_of% w, â† ht', assoc, this])) := by
  subst hb' ht'
  simp [functorMapSrc]

@[reassoc (attr := simp)]
lemma functorMapSrc_functorObjTop :
    functorMapSrc f Ï„ â‰« functorObjTop f Ï€Y = functorObjTop f Ï€X â‰« Ï„.left := by
  ext âŸ¨i, t, b, wâŸ©
  simp [Î¹_functorMapSrc_assoc f Ï„ i t b w _ rfl]

/-- The canonical morphism `âˆ functorObjTgtFamily f Ï€X âŸ¶ âˆ functorObjTgtFamily f Ï€Y`
induced by a morphism `Arrow.mk Ï€X âŸ¶ Arrow.mk Ï€Y`. -/
noncomputable def functorMapTgt :
    âˆ functorObjTgtFamily f Ï€X âŸ¶ âˆ functorObjTgtFamily f Ï€Y :=
  Sigma.map' (fun x => FunctorObjIndex.mk x.i (x.t â‰« Ï„.left) (x.b â‰« Ï„.right) (by simp))
    (fun _ => ğŸ™ _)

@[reassoc]
lemma Î¹_functorMapTgt (i : I) (t : A i âŸ¶ X) (b : B i âŸ¶ S) (w : t â‰« Ï€X = f i â‰« b)
    (b' : B i âŸ¶ T) (hb' : b â‰« Ï„.right = b')
    (t' : A i âŸ¶ Y) (ht' : t â‰« Ï„.left = t') :
    Sigma.Î¹ _ (FunctorObjIndex.mk i t b w) â‰« functorMapTgt f Ï„ =
      Sigma.Î¹ (functorObjTgtFamily f Ï€Y)
        (FunctorObjIndex.mk i t' b' (by
          have := Ï„.w
          dsimp at this
          rw [â† hb', â† reassoc_of% w, â† ht', assoc, this])) := by
  subst hb' ht'
  simp [functorMapTgt]

lemma functorMap_comm :
    functorObjLeft f Ï€X â‰« functorMapTgt f Ï„ =
      functorMapSrc f Ï„ â‰« functorObjLeft f Ï€Y := by
  ext âŸ¨i, t, b, wâŸ©
  simp only [Î¹_colimMap_assoc, Discrete.natTrans_app, Î¹_colimMap,
    Î¹_functorMapTgt f Ï„ i t b w _ rfl,
    Î¹_functorMapSrc_assoc f Ï„ i t b w _ rfl]

variable [HasPushout (functorObjTop f Ï€X) (functorObjLeft f Ï€X)]
  [HasPushout (functorObjTop f Ï€Y) (functorObjLeft f Ï€Y)]

/-- The functor `SmallObject.functor f S : Arrow S â¥¤ Arrow S` that is part of
the small object argument for a family of morphisms `f`, on morphisms. -/
noncomputable def functorMap : functorObj f Ï€X âŸ¶ functorObj f Ï€Y :=
  pushout.map _ _ _ _ Ï„.left (functorMapTgt f Ï„) (functorMapSrc f Ï„) (by simp)
    (functorMap_comm f Ï„)

@[reassoc (attr := simp)]
lemma functorMap_Ï€ : functorMap f Ï„ â‰« Ï€FunctorObj f Ï€Y = Ï€FunctorObj f Ï€X â‰« Ï„.right := by
  ext âŸ¨i, t, b, wâŸ©
  Â· simp [functorMap]
  Â· simp [functorMap, Î¹_functorMapTgt_assoc f Ï„ i t b w _ rfl]

variable (X) in
@[simp]
lemma functorMap_id : functorMap f (ğŸ™ (Arrow.mk Ï€X)) = ğŸ™ _ := by
  ext âŸ¨i, t, b, wâŸ©
  Â· simp [functorMap]
  Â· simp [functorMap,
      Î¹_functorMapTgt_assoc f (ğŸ™ (Arrow.mk Ï€X)) i t b w b (by simp) t (by simp)]

@[reassoc (attr := simp)]
lemma Î¹FunctorObj_naturality :
    Î¹FunctorObj f Ï€X â‰« functorMap f Ï„ = Ï„.left â‰« Î¹FunctorObj f Ï€Y := by
  simp [Î¹FunctorObj, functorMap]

lemma Î¹FunctorObj_extension {i : I} (t : A i âŸ¶ X) (b : B i âŸ¶ S)
    (sq : CommSq t (f i) Ï€X b) :
    âˆƒ (l : B i âŸ¶ functorObj f Ï€X), f i â‰« l = t â‰« Î¹FunctorObj f Ï€X âˆ§
      l â‰« Ï€FunctorObj f Ï€X = b :=
  âŸ¨Sigma.Î¹ (functorObjTgtFamily f Ï€X) (FunctorObjIndex.mk i t b sq.w) â‰«
    ÏFunctorObj f Ï€X, (FunctorObjIndex.mk i t b _).comm, by simpâŸ©

/-- Variant of `Î¹FunctorObj_extension` where the diagram involving `functorObj f Ï€X`
is replaced by an isomorphic diagram. -/
lemma Î¹FunctorObj_extension' {X' S' Z' : C} (Ï€X' : X' âŸ¶ S') (Î¹' : X' âŸ¶ Z') (Ï€Z' : Z' âŸ¶ S')
    (fac' : Î¹' â‰« Ï€Z' = Ï€X') (eX : X' â‰… X) (eS : S' â‰… S) (eZ : Z' â‰… functorObj f Ï€X)
    (commÎ¹ : Î¹' â‰« eZ.hom = eX.hom â‰« Î¹FunctorObj f Ï€X)
    (commÏ€ : Ï€Z' â‰« eS.hom = eZ.hom â‰« Ï€FunctorObj f Ï€X)
    {i : I} (t : A i âŸ¶ X') (b : B i âŸ¶ S') (fac : t â‰« Ï€X' = f i â‰« b) :
    âˆƒ (l : B i âŸ¶ Z'), f i â‰« l = t â‰« Î¹' âˆ§ l â‰« Ï€Z' = b := by
  obtain âŸ¨l, hlâ‚, hlâ‚‚âŸ© :=
    Î¹FunctorObj_extension f (Ï€X := Ï€X) (i := i) (t â‰« eX.hom) (b â‰« eS.hom) âŸ¨by
      rw [assoc, â† Î¹FunctorObj_Ï€FunctorObj f Ï€X, â† reassoc_of% commÎ¹, â† commÏ€,
        reassoc_of% fac', reassoc_of% fac]âŸ©
  refine âŸ¨l â‰« eZ.inv, ?_, ?_âŸ©
  Â· rw [reassoc_of% hlâ‚, â† reassoc_of% commÎ¹, eZ.hom_inv_id, comp_id]
  Â· rw [â† cancel_mono eS.hom, assoc, assoc, commÏ€, eZ.inv_hom_id_assoc, hlâ‚‚]

end

variable [HasPushouts C]
  [âˆ€ {X S : C} (Ï€X : X âŸ¶ S), HasColimitsOfShape (Discrete (FunctorObjIndex f Ï€X)) C]

/-- The functor `Arrow C â¥¤ Arrow C` that is constructed in order to apply the small
object argument to a family of morphisms `f i : A i âŸ¶ B i`, see the introduction
of the file `Mathlib/CategoryTheory/SmallObject/Construction.lean` -/
@[simps! obj map]
noncomputable def functor : Arrow C â¥¤ Arrow C where
  obj Ï€ := Arrow.mk (Ï€FunctorObj f Ï€.hom)
  map {Ï€â‚ Ï€â‚‚} Ï„ := Arrow.homMk (functorMap f Ï„) Ï„.right
  map_id g := by
    ext
    Â· apply functorMap_id
    Â· dsimp
  map_comp {Ï€â‚ Ï€â‚‚ Ï€â‚ƒ} Ï„ Ï„' := by
    ext
    Â· dsimp
      simp only [functorMap, Arrow.comp_left, Arrow.mk_left]
      ext âŸ¨i, t, b, wâŸ©
      Â· simp
      Â· simp [Î¹_functorMapTgt_assoc f Ï„ i t b w _ rfl _ rfl,
          Î¹_functorMapTgt_assoc f (Ï„ â‰« Ï„') i t b w _ rfl _ rfl,
          Î¹_functorMapTgt_assoc f Ï„' i (t â‰« Ï„.left) (b â‰« Ï„.right)
            (by simp [reassoc_of% w]) (b â‰« Ï„.right â‰« Ï„'.right) (by simp)
            (t â‰« (Ï„ â‰« Ï„').left) (by simp)]
    Â· dsimp

/-- The canonical natural transformation `ğŸ­ (Arrow C) âŸ¶ functor f`. -/
@[simps app]
noncomputable def Îµ : ğŸ­ (Arrow C) âŸ¶ functor f where
  app Ï€ := Arrow.homMk (Î¹FunctorObj f Ï€.hom) (ğŸ™ _)

end

end SmallObject

end CategoryTheory
