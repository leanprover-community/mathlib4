/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.SmallObject.Iteration.Basic

/-!
# Uniqueness of morphisms in the category of iterations of functors

Given a functor `Î¦ : C â¥¤ C` and a natural transformation `Îµ : ğŸ­ C âŸ¶ Î¦`,
we shall show in this file that there exists a unique morphism (TODO)
between two arbitrary objects in the category `Functor.Iteration Îµ j`
when `j : J` and `J` is a well orderered set.

-/

universe u

namespace CategoryTheory

open Category Limits

variable {C : Type*} [Category C] {Î¦ : C â¥¤ C} {Îµ : ğŸ­ C âŸ¶ Î¦}
  {J : Type u} [LinearOrder J] [OrderBot J] [SuccOrder J]

namespace Functor

namespace Iteration

namespace Hom

variable (j : J)

/-- The (unique) morphism between two objects in `Iteration Îµ âŠ¥` -/
def mkOfBot (iterâ‚ iterâ‚‚ : Iteration Îµ (âŠ¥ : J)) : iterâ‚ âŸ¶ iterâ‚‚ where
  natTrans :=
    { app := fun âŸ¨i, hiâŸ© => eqToHom (by congr; simpa using hi) â‰« iterâ‚.isoZero.hom â‰«
        iterâ‚‚.isoZero.inv â‰« eqToHom (by congr; symm; simpa using hi)
      naturality := fun âŸ¨i, hiâŸ© âŸ¨k, hkâŸ© Ï† => by
        obtain rfl : i = âŠ¥ := by simpa using hi
        obtain rfl : k = âŠ¥ := by simpa using hk
        obtain rfl : Ï† = ğŸ™ _ := rfl
        simp }
  natTrans_app_succ i hi := by simp at hi

section

variable {i : J} {iterâ‚ iterâ‚‚ : Iteration Îµ (Order.succ i)}
  (hi : Â¬IsMax i) (Ï† : iterâ‚.trunc (SuccOrder.le_succ i) âŸ¶ iterâ‚‚.trunc (SuccOrder.le_succ i))

/-- Auxiliary definition for `mkOfSucc`. -/
noncomputable def mkOfSuccNatTransApp (k : J) (hk : k â‰¤ Order.succ i) :
    iterâ‚.F.obj âŸ¨k, hkâŸ© âŸ¶ iterâ‚‚.F.obj âŸ¨k, hkâŸ© := by
  classical
  exact if hk' : k = Order.succ i then
    eqToHom (by subst hk'; rfl) â‰« (iterâ‚.isoSucc i (Order.lt_succ_of_not_isMax hi)).hom â‰«
      whiskerRight (Ï†.natTrans.app âŸ¨i, by simpâŸ©) _ â‰«
      (iterâ‚‚.isoSucc i (Order.lt_succ_of_not_isMax hi)).inv â‰«
      eqToHom (by subst hk'; rfl)
  else
    Ï†.natTrans.app âŸ¨k, Order.le_of_lt_succ (by
      obtain hk | rfl := hk.lt_or_eq
      Â· exact hk
      Â· tauto)âŸ©

lemma mkOfSuccNatTransApp_eq_of_le (k : J) (hk : k â‰¤ i) :
    mkOfSuccNatTransApp hi Ï† k (hk.trans (Order.le_succ i)) =
      Ï†.natTrans.app âŸ¨k, hkâŸ© :=
  dif_neg (by rintro rfl; simpa using lt_of_le_of_lt hk (Order.lt_succ_of_not_isMax hi))

@[simp]
lemma mkOfSuccNatTransApp_succ_eq :
    mkOfSuccNatTransApp hi Ï† (Order.succ i) (by rfl) =
      (iterâ‚.isoSucc i (Order.lt_succ_of_not_isMax hi)).hom â‰«
        whiskerRight (Ï†.natTrans.app âŸ¨i, by simpâŸ©) _ â‰«
        (iterâ‚‚.isoSucc i (Order.lt_succ_of_not_isMax hi)).inv := by
  dsimp [mkOfSuccNatTransApp]
  rw [dif_pos rfl, comp_id, id_comp]

/-- Auxiliary definition for `mkOfSucc`. -/
@[simps]
noncomputable def mkOfSuccNatTrans :
    iterâ‚.F âŸ¶ iterâ‚‚.F where
  app := fun âŸ¨k, hkâŸ© => mkOfSuccNatTransApp hi Ï† k hk
  naturality := fun âŸ¨kâ‚, hkâ‚âŸ© âŸ¨kâ‚‚, hkâ‚‚âŸ© f => by
    dsimp
    have hk : kâ‚ â‰¤ kâ‚‚ := leOfHom f
    obtain hâ‚‚ | rfl := hkâ‚‚.lt_or_eq
    Â· replace hâ‚‚ : kâ‚‚ â‰¤ i := Order.le_of_lt_succ hâ‚‚
      rw [mkOfSuccNatTransApp_eq_of_le hi Ï† kâ‚‚ hâ‚‚,
        mkOfSuccNatTransApp_eq_of_le hi Ï† kâ‚ (hk.trans hâ‚‚)]
      exact natTrans_naturality Ï† kâ‚ kâ‚‚ hk hâ‚‚
    Â· obtain hâ‚ | rfl := hk.lt_or_eq
      Â· have hâ‚‚ : kâ‚ â‰¤ i := Order.le_of_lt_succ hâ‚
        let fâ‚ : (âŸ¨kâ‚, hkâŸ© : { l | l â‰¤ Order.succ i}) âŸ¶
          âŸ¨i, Order.le_succ iâŸ© := homOfLE hâ‚‚
        let fâ‚‚ : (âŸ¨i, Order.le_succ iâŸ© : Set.Iic (Order.succ i)) âŸ¶
          âŸ¨Order.succ i, by simpâŸ© := homOfLE (Order.le_succ i)
        obtain rfl : f = fâ‚ â‰« fâ‚‚ := rfl
        rw [Functor.map_comp, Functor.map_comp, assoc,
          mkOfSuccNatTransApp_eq_of_le hi Ï† kâ‚ hâ‚‚]
        erw [â† natTrans_naturality_assoc Ï† kâ‚ i hâ‚‚ (by rfl)]
        rw [mkOfSuccNatTransApp_succ_eq]
        dsimp
        change _ â‰« iterâ‚.mapSucc i (Order.lt_succ_of_not_isMax hi) â‰« _ =
          _ â‰« _ â‰« iterâ‚‚.mapSucc i (Order.lt_succ_of_not_isMax hi)
        rw [iterâ‚.mapSucc_eq i, iterâ‚‚.mapSucc_eq i, assoc,
          Iso.inv_hom_id_assoc]
        ext X
        dsimp
        rw [â† Îµ.naturality_assoc]
        rfl
      Â· obtain rfl : f = ğŸ™ _ := rfl
        rw [Functor.map_id, Functor.map_id, id_comp, comp_id]

end

/-- The (unique) morphism between two objects in `Iteration Îµ (Order.succ i)`,
assuming we have a morphism between the truncations to `Iteration Îµ i`. -/
noncomputable def mkOfSucc
    {i : J} (iterâ‚ iterâ‚‚ : Iteration Îµ (Order.succ i)) (hi : Â¬IsMax i)
    (Ï† : iterâ‚.trunc (SuccOrder.le_succ i) âŸ¶ iterâ‚‚.trunc (SuccOrder.le_succ i)) :
    iterâ‚ âŸ¶ iterâ‚‚ where
  natTrans := mkOfSuccNatTrans hi Ï†
  natTrans_app_zero := by
    dsimp
    rw [mkOfSuccNatTransApp_eq_of_le _ _ _ bot_le, Ï†.natTrans_app_zero]
    rfl
  natTrans_app_succ k hk := by
    obtain hk' | rfl := (Order.le_of_lt_succ hk).lt_or_eq
    Â· dsimp
      rw [mkOfSuccNatTransApp_eq_of_le hi Ï† k hk'.le,
        mkOfSuccNatTransApp_eq_of_le hi Ï† (Order.succ k) (Order.succ_le_of_lt hk'),
        Ï†.natTrans_app_succ _ hk']
      rfl
    Â· simp [mkOfSuccNatTransApp_eq_of_le hi Ï† k (by rfl)]

end Hom

end Iteration

end Functor

end CategoryTheory
