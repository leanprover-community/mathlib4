/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import Mathlib.CategoryTheory.SmallObject.Iteration.Basic

/-!
# The functor from `Set.Iic j` deduced from a cocone

Given a functor `F : Set.Iio j â¥¤ C` and `c : Cocone F`, we define
an extension of `F` as a functor `Set.Iic j â¥¤ C` for which
the top element is mapped to `c.pt`.

-/

universe u

namespace CategoryTheory

open Category Limits

namespace SmallObject

namespace SuccStruct

variable {C : Type*} [Category C]
  {J : Type u} [LinearOrder J]
  {j : J} {F : Set.Iio j â¥¤ C} (c : Cocone F)

namespace ofCocone

/-- Auxiliary definition for `ofCocone`. -/
def obj (i : J) : C :=
  if hi : i < j then
    F.obj âŸ¨i, hiâŸ©
  else c.pt

/-- Auxiliary definition for `ofCocone`. -/
def objIso (i : J) (hi : i < j) :
    obj c i â‰… F.obj âŸ¨i, hiâŸ© :=
  eqToIso (dif_pos hi)

/-- Auxiliary definition for `ofCocone`. -/
def objIsoPt :
    obj c j  â‰… c.pt :=
  eqToIso (dif_neg (by simp))

/-- Auxiliary definition for `ofCocone`. -/
def map (iâ‚ iâ‚‚ : J) (hi : iâ‚ â‰¤ iâ‚‚) (hiâ‚‚ : iâ‚‚ â‰¤ j) :
    obj c iâ‚ âŸ¶ obj c iâ‚‚ :=
  if hâ‚‚ : iâ‚‚ < j then
    (objIso c iâ‚ (lt_of_le_of_lt hi hâ‚‚)).hom â‰« F.map (homOfLE hi) â‰« (objIso c iâ‚‚ hâ‚‚).inv
  else
    have hâ‚‚' : iâ‚‚ = j := le_antisymm hiâ‚‚ (by simpa using hâ‚‚)
    if hâ‚ : iâ‚ < j then
      (objIso c iâ‚ hâ‚).hom â‰« c.Î¹.app âŸ¨iâ‚, hâ‚âŸ© â‰« (objIsoPt c).inv â‰« eqToHom (by subst hâ‚‚'; rfl)
    else
      have hâ‚' : iâ‚ = j := le_antisymm (hi.trans hiâ‚‚) (by simpa using hâ‚)
      eqToHom (by subst hâ‚' hâ‚‚'; rfl)

lemma map_id (i : J) (hi : i â‰¤ j) :
    map c i i (by rfl) hi = ðŸ™ _:= by
  dsimp [map]
  grind

lemma map_comp (iâ‚ iâ‚‚ iâ‚ƒ : J) (hi : iâ‚ â‰¤ iâ‚‚) (hi' : iâ‚‚ â‰¤ iâ‚ƒ) (hiâ‚ƒ : iâ‚ƒ â‰¤ j) :
    map c iâ‚ iâ‚ƒ (hi.trans hi') hiâ‚ƒ =
      map c iâ‚ iâ‚‚ hi (hi'.trans hiâ‚ƒ) â‰«
        map c iâ‚‚ iâ‚ƒ hi' hiâ‚ƒ := by
  obtain hiâ‚â‚‚ | rfl := hi.lt_or_eq
  Â· obtain hiâ‚‚â‚ƒ | rfl := hi'.lt_or_eq
    Â· dsimp [map]
      obtain hiâ‚ƒ' | rfl := hiâ‚ƒ.lt_or_eq
      Â· rw [dif_pos hiâ‚ƒ', dif_pos (hiâ‚‚â‚ƒ.trans hiâ‚ƒ'), dif_pos hiâ‚ƒ', assoc, assoc,
          Iso.inv_hom_id_assoc, â† Functor.map_comp_assoc, homOfLE_comp]
      Â· rw [dif_neg (by simp), dif_pos (hiâ‚â‚‚.trans hiâ‚‚â‚ƒ), dif_pos hiâ‚‚â‚ƒ, dif_neg (by simp),
          dif_pos hiâ‚‚â‚ƒ, eqToHom_refl, comp_id, assoc, assoc, Iso.inv_hom_id_assoc,
          Cocone.w_assoc]
    Â· rw [map_id, comp_id]
  Â· rw [map_id, id_comp]

end ofCocone

/-- Given a functor `F : Set.Iio j â¥¤ C` and a cocone `c : Cocone F`,
where `j : J` and `J` is linearly ordered, this is the functor
`Set.Iic j â¥¤ C` which extends `F` and sends the top element to `c.pt`. -/
def ofCocone : Set.Iic j â¥¤ C where
  obj i := ofCocone.obj c i.1
  map {_ j} f := ofCocone.map c _ _ (leOfHom f) j.2
  map_id i := ofCocone.map_id _ _ i.2
  map_comp {_ _ iâ‚ƒ} _ _ := ofCocone.map_comp _ _ _ _ _ _ iâ‚ƒ.2

lemma ofCocone_obj_eq (i : J) (hi : i < j) :
    (ofCocone c).obj âŸ¨i, hi.leâŸ© = F.obj âŸ¨i, hiâŸ© :=
  dif_pos hi

/-- The isomorphism `(ofCocone c).obj âŸ¨i, _âŸ© â‰… F.obj âŸ¨i, _âŸ©` when `i < j`. -/
def ofCoconeObjIso (i : J) (hi : i < j) :
    (ofCocone c).obj âŸ¨i, hi.leâŸ© â‰… F.obj âŸ¨i, hiâŸ© :=
  ofCocone.objIso c _ _

lemma ofCocone_obj_eq_pt :
    (ofCocone c).obj âŸ¨j, by simpâŸ© = c.pt :=
  dif_neg (by simp)

/-- The isomorphism `(ofCocone c).obj âŸ¨j, _âŸ© â‰… c.pt`. -/
def ofCoconeObjIsoPt :
    (ofCocone c).obj âŸ¨j, by simpâŸ© â‰… c.pt :=
  ofCocone.objIsoPt c

lemma ofCocone_map_to_top (i : J) (hi : i < j) :
    (ofCocone c).map (homOfLE hi.le) =
      (ofCoconeObjIso c i hi).hom â‰« c.Î¹.app âŸ¨i, hiâŸ© â‰« (ofCoconeObjIsoPt c).inv := by
  dsimp [ofCocone, ofCocone.map, ofCoconeObjIso, ofCoconeObjIsoPt]
  rw [dif_neg (by simp), dif_pos hi, comp_id]

@[reassoc]
lemma ofCocone_map (iâ‚ iâ‚‚ : J) (hi : iâ‚ â‰¤ iâ‚‚) (hiâ‚‚ : iâ‚‚ < j) :
    (ofCocone c).map (homOfLE hi : âŸ¨iâ‚, hi.trans hiâ‚‚.leâŸ© âŸ¶ âŸ¨iâ‚‚, hiâ‚‚.leâŸ©) =
      (ofCoconeObjIso c iâ‚ (lt_of_le_of_lt hi hiâ‚‚)).hom â‰« F.map (homOfLE hi) â‰«
        (ofCoconeObjIso c iâ‚‚ hiâ‚‚).inv := by
  dsimp [ofCocone, ofCoconeObjIso, ofCocone.map]
  rw [dif_pos hiâ‚‚]

@[reassoc]
lemma ofCoconeObjIso_hom_naturality (iâ‚ iâ‚‚ : J) (hi : iâ‚ â‰¤ iâ‚‚) (hiâ‚‚ : iâ‚‚ < j) :
    (ofCocone c).map (homOfLE hi : âŸ¨iâ‚, hi.trans hiâ‚‚.leâŸ© âŸ¶ âŸ¨iâ‚‚, hiâ‚‚.leâŸ©) â‰«
      (ofCoconeObjIso c iâ‚‚ hiâ‚‚).hom =
      (ofCoconeObjIso c iâ‚ (lt_of_le_of_lt hi hiâ‚‚)).hom â‰« F.map (homOfLE hi) := by
  rw [ofCocone_map c iâ‚ iâ‚‚ hi hiâ‚‚, assoc, assoc, Iso.inv_hom_id, comp_id]

/-- The isomorphism expressing that `ofCocone c` extends the functor `F`
when `c : Cocone F`. -/
@[simps!]
def restrictionLTOfCoconeIso :
    SmallObject.restrictionLT (ofCocone c) (Preorder.le_refl j) â‰… F :=
  NatIso.ofComponents (fun âŸ¨i, hiâŸ© â†¦ ofCoconeObjIso c i hi)
    (by intros; apply ofCoconeObjIso_hom_naturality)

variable {c} in
/-- If `c` is a colimit cocone, then so is `coconeOfLE (ofCocone c) (Preorder.le_refl j)`. -/
def isColimitCoconeOfLEOfCocone (hc : IsColimit c) :
    IsColimit (coconeOfLE (ofCocone c) (Preorder.le_refl j)) :=
  (IsColimit.precomposeInvEquiv (restrictionLTOfCoconeIso c) _).1
    (IsColimit.ofIsoColimit hc
      (Cocones.ext (ofCoconeObjIsoPt c).symm (fun âŸ¨i, hiâŸ© â†¦ by
        dsimp
        rw [ofCocone_map_to_top _ _ hi, Iso.inv_hom_id_assoc])))

lemma arrowMap_ofCocone (iâ‚ iâ‚‚ : J) (hâ‚â‚‚ : iâ‚ â‰¤ iâ‚‚) (hâ‚‚ : iâ‚‚ < j) :
    arrowMap (ofCocone c) iâ‚ iâ‚‚ hâ‚â‚‚ hâ‚‚.le =
      Arrow.mk (F.map (homOfLE hâ‚â‚‚ : âŸ¨iâ‚, lt_of_le_of_lt hâ‚â‚‚ hâ‚‚âŸ© âŸ¶ âŸ¨iâ‚‚, hâ‚‚âŸ©)) :=
  Arrow.ext (ofCocone_obj_eq _ _ _) (ofCocone_obj_eq _ _ _) (ofCocone_map _ _ _ _ _)

lemma arrowMap_ofCocone_to_top (i : J) (hi : i < j) :
    arrowMap (ofCocone c) i j hi.le (by simp) = Arrow.mk (c.Î¹.app âŸ¨i, hiâŸ©) := by
  rw [arrowMap, ofCocone_map_to_top _ _ hi]
  exact Arrow.ext (ofCocone_obj_eq _ _ _) (ofCocone_obj_eq_pt _) rfl

end SuccStruct

end SmallObject

end CategoryTheory
