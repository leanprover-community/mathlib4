/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Category.Preorder
import Mathlib.CategoryTheory.Limits.HasLimits
import Mathlib.CategoryTheory.MorphismProperty.IsSmall
import Mathlib.CategoryTheory.MorphismProperty.TransfiniteComposition
import Mathlib.CategoryTheory.Limits.FunctorCategory.Basic
import Mathlib.CategoryTheory.Limits.Over
import Mathlib.Order.ConditionallyCompleteLattice.Basic
import Mathlib.Order.SuccPred.Limit

/-! # Transfinite iterations of a construction

In this file, we introduce the structure `SuccStruct` on a category `C`.
It consists of the data of an object `X‚ÇÄ : C`, a successor map `succ : C ‚Üí C`
and a morphism `toSucc : X ‚ü∂ succ X` for any `X : C`. The map `toSucc`
does not have to be natural in `X`. For any element `j : J` in
well-ordered type `J`, we would like to define
the iteration of `Œ¶ : SuccStruct C`, as a functor `F : J ‚•§ C`
such that `F.obj ‚ä• = X‚ÇÄ`, `F.obj j ‚ü∂ F.obj (Order.succ j)` is `toSucc (F.obj j)`
when `j` is not maximal, and when `j` is limit, `F.obj j` should be the
colimit of the `F.obj k` for `k < j`.

In the small object argument, we shall apply this to the iteration of
a functor `F : C ‚•§ C` equipped with a natural transformation `Œµ : ùü≠ C ‚ü∂ F`:
this will correspond to the case of
`SuccStruct.ofTrans Œµ : SuccStruct (C ‚•§ C)`, for which `X‚ÇÄ := ùü≠ C`,
`succ G := G ‚ãô F` and `toSucc G : G ‚ü∂ G ‚ãô F` is given by `whiskerLeft G Œµ`.

The construction of the iteration of `Œ¶ : SuccStruct C` is done
by transfinite induction, under an assumption `HasIterationOfShape C J`.
However, for a limit element `j : J`, the definition of `F.obj j`
does not involve only the objects `F.obj i` for `i < j`, but it also
involves the maps `F.obj i‚ÇÅ ‚ü∂ F.obj i‚ÇÇ` for `i‚ÇÅ ‚â§ i‚ÇÇ < j`.
Then, this is a straightforward application of definitions by
transfinite induction. In order to solve this technical difficulty,
we introduce a structure `Œ¶.Iteration j` for any `j : J`. This
structure contains all the expected data and properties for
all the indices that are `‚â§ j`. In this file, we show that
`Œ¶.Iteration j` is a subsingleton. The uniqueness shall ne
obtained in the file `SmallObject.Iteration.Nonempty`, and
the construction of the functor `Œ¶.iterationFunctor J : J ‚•§ C`
and of its colimit `Œ¶.iteration J : C` is done in the
file `SmallObject.Iteration.Iteration`.

The map `Œ¶.toSucc X : X ‚ü∂ Œ¶.succ X` does not have to be natural
(and it is not in certain applications). Then, two isomorphic
objects `X` and `Y` may have non isomorphic successors. This is
the reason why we make an extensive use of equalities of objects
in the definitions.

In the file `SmallObject.TransfiniteIteration`, we consider
the colimit over all `j : J` of the `j`th iteration of `Œ¶`.

-/

universe w v v' u u'

namespace CategoryTheory

open Category Limits

variable {C : Type u} [Category.{v} C] {J : Type w}

section

variable {D : Type u'} [Category.{v'} D]

namespace Arrow

lemma functor_ext {F G : C ‚•§ D} (h : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y),
    F.mapArrow.obj (Arrow.mk f) = G.mapArrow.obj (Arrow.mk f)) :
    F = G :=
  Functor.ext (fun X ‚Ü¶ congr_arg Comma.left (h (ùüô X))) (fun X Y f ‚Ü¶ by
    have := (eqToIso (h f)).hom.w
    dsimp at this
    rw [Comma.eqToHom_left, Comma.eqToHom_right] at this
    rw [reassoc_of% this, eqToHom_trans, eqToHom_refl, comp_id])

lemma congr_mk_id {X Y : C} (h : X = Y) :
    Arrow.mk (ùüô X) = Arrow.mk (ùüô Y) := by rw [h]

lemma mk_eq_mk_iff {X Y X' Y' : C} (f : X ‚ü∂ Y) (f' : X' ‚ü∂ Y') :
    Arrow.mk f = Arrow.mk f' ‚Üî
      ‚àÉ (hX : X = X') (hY : Y = Y'), f = eqToHom hX ‚â´ f' ‚â´ eqToHom hY.symm := by
  constructor
  ¬∑ intro h
    refine ‚ü®congr_arg Comma.left h, congr_arg Comma.right h, ?_‚ü©
    have := (eqToIso h).hom.w
    dsimp at this
    rw [Comma.eqToHom_left, Comma.eqToHom_right] at this
    rw [reassoc_of% this, eqToHom_trans, eqToHom_refl, comp_id]
  ¬∑ rintro ‚ü®rfl, rfl, h‚ü©
    simp only [eqToHom_refl, comp_id, id_comp] at h
    rw [h]

end Arrow

namespace Functor

--@[simp]
--lemma mapArrow_obj_mk (F : C ‚•§ D) {X Y : C} (f : X ‚ü∂ Y) :
--    F.mapArrow.obj (Arrow.mk f) = Arrow.mk (F.map f) := rfl

end Functor

end
namespace SmallObject

section

variable [Preorder J] {j : J} (F : Set.Iic j ‚•§ C) {i : J} (hi : i ‚â§ j)

/-- The functor `Set.Iio i ‚•§ C` obtained by "restriction" of `F : Set.Iic j ‚•§ C`
when `i ‚â§ j`. -/
def restrictionLT : Set.Iio i ‚•§ C :=
  (monotone_inclusion_lt_le_of_le hi).functor ‚ãô F

@[simp]
lemma restrictionLT_obj (k : J) (hk : k < i) :
    (restrictionLT F hi).obj ‚ü®k, hk‚ü© = F.obj ‚ü®k, hk.le.trans hi‚ü© := rfl

@[simp]
lemma restrictionLT_map {k‚ÇÅ k‚ÇÇ : Set.Iio i} (œÜ : k‚ÇÅ ‚ü∂ k‚ÇÇ) :
    (restrictionLT F hi).map œÜ = F.map (homOfLE (by simpa using leOfHom œÜ)) := rfl

/-- Given `F : Set.Iic j ‚•§ C`, `i : J` such that `hi : i ‚â§ j`, this is the
cocone consisting of all maps `F.obj ‚ü®k, hk‚ü© ‚ü∂ F.obj ‚ü®i, hi‚ü©` for `k : J` such that `k < i`. -/
@[simps]
def coconeOfLE : Cocone (restrictionLT F hi) where
  pt := F.obj ‚ü®i, hi‚ü©
  Œπ :=
    { app := fun ‚ü®k, hk‚ü© => F.map (homOfLE (by simpa using hk.le))
      naturality := fun ‚ü®k‚ÇÅ, hk‚ÇÅ‚ü© ‚ü®k‚ÇÇ, hk‚ÇÇ‚ü© _ => by
        simp [comp_id, ‚Üê Functor.map_comp, homOfLE_comp] }

/-- The functor `Set.Iic i ‚•§ C` obtained by "restriction" of `F : Set.Iic j ‚•§ C`
when `i ‚â§ j`. -/
def restrictionLE : Set.Iic i ‚•§ C :=
  (monotone_inclusion_le_le_of_le hi).functor ‚ãô F

@[simp]
lemma restrictionLE_obj (k : J) (hk : k ‚â§ i) :
    (restrictionLE F hi).obj ‚ü®k, hk‚ü© = F.obj ‚ü®k, hk.trans hi‚ü© := rfl

@[simp]
lemma restrictionLE_map {k‚ÇÅ k‚ÇÇ : Set.Iic i} (œÜ : k‚ÇÅ ‚ü∂ k‚ÇÇ) :
    (restrictionLE F hi).map œÜ = F.map (homOfLE (by simpa using leOfHom œÜ)) := rfl

end

section

variable [Preorder J]

variable (C J) in
/-- A category `C` has iterations of shape `J` when certain shapes
of colimits exists. When `J` is well ordered, this assumption is used in
order to show that the category `Iteration Œµ j` is nonempty for any `j : J`,
see the file `CategoryTheory.SmallObject.Iteration.Nonempty`. The API is developed
further in `CategoryTheory.SmallObject.TransfiniteIteration`. -/
class HasIterationOfShape : Prop where
  hasColimitsOfShape_of_isSuccLimit (j : J) (hj : Order.IsSuccLimit j) :
    HasColimitsOfShape (Set.Iio j) C := by infer_instance
  hasColimitsOfShape : HasColimitsOfShape J C := by infer_instance

attribute [instance] HasIterationOfShape.hasColimitsOfShape

variable (C) in
lemma hasColimitsOfShape_of_isSuccLimit [HasIterationOfShape C J] (j : J)
    (hj : Order.IsSuccLimit j) :
    HasColimitsOfShape (Set.Iio j) C :=
  HasIterationOfShape.hasColimitsOfShape_of_isSuccLimit j hj

instance [HasIterationOfShape C J] :
    HasIterationOfShape (Arrow C) J where
  hasColimitsOfShape_of_isSuccLimit j hj := by
    have := hasColimitsOfShape_of_isSuccLimit C j hj
    infer_instance

instance [HasIterationOfShape C J] {K : Type u'} [Category.{v'} K]:
    HasIterationOfShape (K ‚•§ C) J where
  hasColimitsOfShape_of_isSuccLimit j hj := by
    have := hasColimitsOfShape_of_isSuccLimit C j hj
    infer_instance

instance [HasIterationOfShape C J] (K : Type*) [Category K] (X : K) :
    PreservesWellOrderContinuousOfShape J ((evaluation K C).obj X) where
  preservesColimitsOfShape j hj := by
    have := hasColimitsOfShape_of_isSuccLimit C j hj
    infer_instance

instance [HasIterationOfShape C J] :
    PreservesWellOrderContinuousOfShape J (Arrow.leftFunc : _ ‚•§ C) where
  preservesColimitsOfShape j hj := by
    have := hasColimitsOfShape_of_isSuccLimit C j hj
    infer_instance

instance [HasIterationOfShape C J] :
    PreservesWellOrderContinuousOfShape J (Arrow.rightFunc : _ ‚•§ C) where
  preservesColimitsOfShape j hj := by
    have := hasColimitsOfShape_of_isSuccLimit C j hj
    infer_instance

end

variable (C) in
/-- A successor structure () on a category consists of the
data of an object `succ X` for any `X : C`, a map `toSucc X : X ‚ü∂ toSucc X`
(which does not need to be natural), and a zeroth object `X‚ÇÄ`.
-/
structure SuccStruct where
  /-- the zeroth object -/
  X‚ÇÄ : C
  /-- the successor of an object -/
  succ (X : C) : C
  /-- the map to the successor -/
  toSucc (X : C) : X ‚ü∂ succ X

namespace SuccStruct

/-- Given a functor `Œ¶ : C ‚•§ C`, a natural transformation of the form `ùü≠ C ‚ü∂ Œ¶`
induces a successor structure. -/
@[simps]
def ofNatTrans {F : C ‚•§ C} (Œµ : ùü≠ C ‚ü∂ F) : SuccStruct (C ‚•§ C) where
  succ G := G ‚ãô F
  toSucc G := whiskerLeft G Œµ
  X‚ÇÄ := ùü≠ C

--lemma congr_toSucc (Œ¶ : SuccStruct C) {X Y : C} (h : X = Y) :
--    Œ¶.toSucc X = eqToHom (by rw [h]) ‚â´ Œ¶.toSucc Y ‚â´ eqToHom (by rw [h]) := by
--  subst h
--  simp

variable (Œ¶ : SuccStruct C)

/-- The map `Œ¶.toSucc X : X ‚ü∂ Œ¶.Succ X`, as an element in `Arrow C`. -/
def toSuccArrow (X : C) : Arrow C := Arrow.mk (Œ¶.toSucc X)

/-- The class of morphisms that are of the morphism `toSucc X : X ‚ü∂ succ X`. -/
def prop : MorphismProperty C := .ofHoms (fun (X : C) ‚Ü¶ Œ¶.toSucc X)

lemma prop_toSucc (X : C) : Œ¶.prop (Œ¶.toSucc X) := ‚ü®_‚ü©

lemma prop_iff {X Y : C} (f : X ‚ü∂ Y) :
    Œ¶.prop f ‚Üî Arrow.mk f = Œ¶.toSuccArrow X := by
  constructor
  ¬∑ rintro ‚ü®_‚ü©
    rfl
  ¬∑ intro h
    rw [‚Üê Œ¶.prop.arrow_mk_mem_toSet_iff, h]
    apply prop_toSucc

variable [LinearOrder J] [OrderBot J] [SuccOrder J]
    [HasIterationOfShape C J]

/-- Given a functor `F : Set.Iic ‚•§ C`, this is the morphism in `C`, as an element
in `Arrow C`, that is obtained by applying `F.map` to an inequality, -/
def arrowMap {j : J} (F : Set.Iic j ‚•§ C) (i‚ÇÅ i‚ÇÇ : J) (h‚ÇÅ‚ÇÇ : i‚ÇÅ ‚â§ i‚ÇÇ)
    (h‚ÇÇ : i‚ÇÇ ‚â§ j) : Arrow C :=
  Arrow.mk (F.map (homOfLE h‚ÇÅ‚ÇÇ : ‚ü®i‚ÇÅ, h‚ÇÅ‚ÇÇ.trans h‚ÇÇ‚ü© ‚ü∂ ‚ü®i‚ÇÇ, h‚ÇÇ‚ü©))

/-- Given a functor `F : Set.Iic j ‚•§ C` and `i : J` such that `i < j`,
this is the arrow `F.obj ‚ü®i, _‚ü© ‚ü∂ F.obj ‚ü®Order.succ i, _‚ü©`. -/
def arrowSucc {j : J} (F : Set.Iic j ‚•§ C) (i : J) (hi : i < j) : Arrow C :=
    arrowMap F i (Order.succ i) (Order.le_succ i) (Order.succ_le_of_lt hi)

omit [OrderBot J] [HasIterationOfShape C J] in
lemma arrowSucc_def {j : J} (F : Set.Iic j ‚•§ C) (i : J) (hi : i < j) :
    arrowSucc F i hi = arrowMap F i (Order.succ i) (Order.le_succ i) (Order.succ_le_of_lt hi) :=
  rfl

omit [OrderBot J] [SuccOrder J] [HasIterationOfShape C J] in
@[simp]
lemma arrowMap_refl {j : J} (F : Set.Iic j ‚•§ C) (i : J) (hi : i ‚â§ j) :
    arrowMap F i i (by simp) hi = Arrow.mk (ùüô (F.obj ‚ü®i, hi‚ü©)) := by
  simp [arrowMap]

omit [OrderBot J] [SuccOrder J] [HasIterationOfShape C J] in
lemma arrowMap_restrictionLE {j : J} (F : Set.Iic j ‚•§ C) {j' : J} (hj' : j' ‚â§ j)
    (i‚ÇÅ i‚ÇÇ : J) (h‚ÇÅ‚ÇÇ : i‚ÇÅ ‚â§ i‚ÇÇ) (h‚ÇÇ : i‚ÇÇ ‚â§ j') :
    arrowMap (restrictionLE F hj') i‚ÇÅ i‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ =
      arrowMap F i‚ÇÅ i‚ÇÇ h‚ÇÅ‚ÇÇ (h‚ÇÇ.trans hj') := rfl

/-- Given `F : Set.Iio i ‚•§ C`, with `i` a limit element, and `k` such that `hk : k < i`,
this is the map `colimit.Œπ F ‚ü®k, hk‚ü©`, as an element in `Arrow C`. -/
noncomputable def arrowŒπ {i : J} (F : Set.Iio i ‚•§ C) (hi : Order.IsSuccLimit i)
    (k : J) (hk : k < i) : Arrow C :=
  letI := hasColimitsOfShape_of_isSuccLimit C i hi
  Arrow.mk (colimit.Œπ F ‚ü®k, hk‚ü©)

/-- The category of `j`th iterations of a succesor structure `Œ¶ : SuccStruct C`.
An object consists of the data of all iterations of `Œ¶` for `i : J` such
that `i ‚â§ j` (this is the field `F`). Such objects are
equipped with data and properties which characterizes uniquely the iterations
on three types of elements: `‚ä•`, successors, limit elements. -/
@[ext]
structure Iteration [WellFoundedLT J] (j : J) where
  /-- The data of all `i`th iterations for `i : J` such that `i ‚â§ j`. -/
  F : Set.Iic j ‚•§ C
  /-- The zeroth iteration is the zeroth object . -/
  obj_bot : F.obj ‚ü®‚ä•, bot_le‚ü© = Œ¶.X‚ÇÄ
  /-- The iteration on a successor element identifies to the successor. -/
  arrowSucc_eq (i : J) (hi : i < j) : arrowSucc F i hi = Œ¶.toSuccArrow (F.obj ‚ü®i, hi.le‚ü©)
  arrowMap_limit (i : J) (hi : Order.IsSuccLimit i) (hij : i ‚â§ j) (k : J) (hk : k < i) :
    arrowMap F k i hk.le hij = arrowŒπ (restrictionLT F hij) hi k hk
  --/-- The natural map from an iteration to its successor is induced by `toSucc`. -/
  --map_succ (i : J) (hi : i < j) :
  --  F.map (homOfLE (Order.le_succ i) : ‚ü®i, hi.le‚ü© ‚ü∂ ‚ü®Order.succ i, Order.succ_le_of_lt hi‚ü©) =
  --    Œ¶.toSucc _ ‚â´ eqToHom (by rw [obj_succ _ hi])
  --/-- If `i` is a limit element, the `i`th iteration is the colimit
  --of `k`th iterations for `k < i`. -/
  --map_eq_Œπ (i : J) (hi : Order.IsSuccLimit i) (hij : i ‚â§ j)
  --    (k : J) (hk : k < i) :
  --  letI := hasColimitsOfShape_of_isSuccLimit C i hi
  --  F.map (homOfLE hk.le : ‚ü®k, hk.le.trans hij‚ü© ‚ü∂ ‚ü®i, hij‚ü©) =
  --    colimit.Œπ (restrictionLT F hij) ‚ü®k, hk‚ü© ‚â´
  --      eqToHom (by rw [obj_limit i hi])

variable [WellFoundedLT J]

namespace Iteration

variable {Œ¶}
variable {j : J}

attribute [simp] obj_bot

section

variable (iter : Œ¶.Iteration j)

/-- The isomorphism `iter.F.obj ‚ü®‚ä•, bot_le‚ü© ‚âÖ Œ¶.X‚ÇÄ`. -/
def isoBot : iter.F.obj ‚ü®‚ä•, bot_le‚ü© ‚âÖ Œ¶.X‚ÇÄ :=
  eqToIso (by rw [obj_bot])

lemma obj_succ (i : J) (hi : i < j) :
    iter.F.obj ‚ü®Order.succ i, Order.succ_le_of_lt hi‚ü© = Œ¶.succ (iter.F.obj ‚ü®i, hi.le‚ü©) :=
  congr_arg Comma.right (iter.arrowSucc_eq i hi)

lemma obj_limit (i : J) (hi : Order.IsSuccLimit i) (hij : i ‚â§ j) :
    letI := hasColimitsOfShape_of_isSuccLimit C i hi
    iter.F.obj ‚ü®i, hij‚ü© = colimit (restrictionLT iter.F hij) :=
  congr_arg Comma.right (iter.arrowMap_limit i hi hij ‚ä• (Order.IsSuccLimit.bot_lt hi))

/-
/-- The object `iter.F.obj ‚ü®Order.succ i, _‚ü©` identifies to
the successor of `iter.F.obj ‚ü®i, _‚ü©` when `i < j`. -/
def isoSucc (i : J) (hi : i < j) :
    iter.F.obj ‚ü®Order.succ i, Order.succ_le_of_lt hi‚ü© ‚âÖ
      Œ¶.succ (iter.F.obj ‚ü®i, hi.le‚ü©) :=
  eqToIso (by rw [obj_succ _ i hi])

/-- Variant of `map_succ'` involving the isomorphism `isoSucc`. -/
lemma map_succ' (i : J) (hi : i < j) :
    iter.F.map (homOfLE (Order.le_succ i) :
        ‚ü®i, hi.le‚ü© ‚ü∂ ‚ü®Order.succ i, Order.succ_le_of_lt hi‚ü©) =
      Œ¶.toSucc _ ‚â´ (iter.isoSucc i hi).inv :=
  iter.map_succ i hi

lemma arrow_mk_map_succ (i : J) (hi : i < j) :
    Arrow.mk (iter.F.map (homOfLE (Order.le_succ i) :
      ‚ü®i, hi.le‚ü© ‚ü∂ ‚ü®Order.succ i, Order.succ_le_of_lt hi‚ü©)) =
        Œ¶.toSuccArrow (iter.F.obj ‚ü®i, hi.le‚ü©) :=
  Arrow.ext rfl (iter.obj_succ i hi)
    (by simp [iter.map_succ' i hi, toSuccArrow, isoSucc])

lemma prop_map_succ (i : J) (hi : i < j) :
    Œ¶.prop (iter.F.map (homOfLE (Order.le_succ i) :
      ‚ü®i, hi.le‚ü© ‚ü∂ ‚ü®Order.succ i, Order.succ_le_of_lt hi‚ü©)) := by
  rw [prop_iff, iter.arrow_mk_map_succ _ hi]

/-- When `i : J` is limit, `iter.F.obj ‚ü®i, _‚ü©` identifies
to the colimit of the restriction of `iter.F` to `Set.Iio i`. -/
noncomputable def isColimit (i : J)
    (hi : Order.IsSuccLimit i) (hij : i ‚â§ j) :
    IsColimit (coconeOfLE iter.F hij) := by
  have := hasColimitsOfShape_of_isSuccLimit C i hi
  exact IsColimit.ofIsoColimit (colimit.isColimit _)
    (Cocones.ext (eqToIso (iter.obj_limit i hi hij).symm)
    (fun ‚ü®k, hk‚ü© ‚Ü¶ (iter.map_eq_Œπ i hi hij k hk).symm))
    -/

/-- The element in `Œ¶.Iteration i` that is deduced from an element
in `Œ¶.Iteration j` when `i ‚â§ j`. -/
@[simps F]
def trunc (iter : Œ¶.Iteration j) {j' : J} (hj' : j' ‚â§ j) : Œ¶.Iteration j' where
  F := restrictionLE iter.F hj'
  obj_bot := iter.obj_bot
  arrowSucc_eq i hi := iter.arrowSucc_eq i (lt_of_lt_of_le hi hj')
  arrowMap_limit i hi hij k hk := iter.arrowMap_limit i hi (hij.trans hj') k hk

end

--omit [OrderBot J] [SuccOrder J] [WellFoundedLT J] in
--lemma congr_colimit_Œπ {F G : Set.Iio j ‚•§ C} (h : F = G) (hj : Order.IsSuccLimit j)
--    (i : Set.Iio j) :
--    letI := hasColimitsOfShape_of_isSuccLimit C j hj
--    colimit.Œπ F i = by
--      refine eqToHom (by rw [h]) ‚â´ colimit.Œπ G i ‚â´ eqToHom (by rw [h]) := by
--  subst h
--  simp

namespace subsingleton

end subsingleton

namespace subsingleton

omit [OrderBot J] [SuccOrder J] [HasIterationOfShape C J] [WellFoundedLT J]

variable (F G : Set.Iic j ‚•§ C)

section

variable (k‚ÇÅ k‚ÇÇ : J) (h‚ÇÅ‚ÇÇ : k‚ÇÅ ‚â§ k‚ÇÇ) (h‚ÇÇ : k‚ÇÇ ‚â§ j)

/-- Auxiliary definition for the proof of `Subsingleton (Œ¶.Iteration j)`. -/
def MapEq : Prop := arrowMap F k‚ÇÅ k‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ = arrowMap G k‚ÇÅ k‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ

namespace MapEq

variable {k‚ÇÅ k‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ} (h : MapEq F G k‚ÇÅ k‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ)

include h

lemma src : F.obj ‚ü®k‚ÇÅ, h‚ÇÅ‚ÇÇ.trans h‚ÇÇ‚ü© = G.obj ‚ü®k‚ÇÅ, h‚ÇÅ‚ÇÇ.trans h‚ÇÇ‚ü© :=
  congr_arg Comma.left h

lemma tgt : F.obj ‚ü®k‚ÇÇ, h‚ÇÇ‚ü© = G.obj ‚ü®k‚ÇÇ, h‚ÇÇ‚ü© :=
  congr_arg Comma.right h

lemma w :
    F.map (homOfLE h‚ÇÅ‚ÇÇ : ‚ü®k‚ÇÅ, h‚ÇÅ‚ÇÇ.trans h‚ÇÇ‚ü© ‚ü∂ ‚ü®k‚ÇÇ, h‚ÇÇ‚ü©) =
      eqToHom (by rw [h.src]) ‚â´ G.map (homOfLE h‚ÇÅ‚ÇÇ : ‚ü®k‚ÇÅ, h‚ÇÅ‚ÇÇ.trans h‚ÇÇ‚ü© ‚ü∂ ‚ü®k‚ÇÇ, h‚ÇÇ‚ü©) ‚â´
        eqToHom (by rw [h.tgt]) := by
  have := (eqToIso h).hom.w
  dsimp [arrowMap] at this
  rw [Comma.eqToHom_left, Comma.eqToHom_right] at this
  simp [reassoc_of% this]

end MapEq

end

variable {F G}


lemma mapEq_refl (k : J) (hk : k ‚â§ j) (h : F.obj ‚ü®k, hk‚ü© = G.obj ‚ü®k, hk‚ü©) :
    MapEq F G k k (by simp) hk := by
  rw [MapEq, arrowMap_refl, arrowMap_refl, h]

-- there is a more general lemma in `Arrow C`
lemma mapEq_trans {i‚ÇÅ i‚ÇÇ i‚ÇÉ : J} (h‚ÇÅ‚ÇÇ : i‚ÇÅ ‚â§ i‚ÇÇ) (h‚ÇÇ‚ÇÉ : i‚ÇÇ ‚â§ i‚ÇÉ) {h‚ÇÉ : i‚ÇÉ ‚â§ j}
    (m‚ÇÅ‚ÇÇ : MapEq F G i‚ÇÅ i‚ÇÇ h‚ÇÅ‚ÇÇ (h‚ÇÇ‚ÇÉ.trans h‚ÇÉ)) (m‚ÇÇ‚ÇÉ : MapEq F G i‚ÇÇ i‚ÇÉ h‚ÇÇ‚ÇÉ h‚ÇÉ) :
    MapEq F G i‚ÇÅ i‚ÇÉ (h‚ÇÅ‚ÇÇ.trans h‚ÇÇ‚ÇÉ) h‚ÇÉ := by
  simp only [MapEq, arrowMap, Arrow.mk_eq_mk_iff]
  refine ‚ü®m‚ÇÅ‚ÇÇ.src, m‚ÇÇ‚ÇÉ.tgt, ?_‚ü©
  rw [‚Üê homOfLE_comp (y := ‚ü®i‚ÇÇ, h‚ÇÇ‚ÇÉ.trans h‚ÇÉ‚ü©) h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ]
  simp [-homOfLE_comp, m‚ÇÅ‚ÇÇ.w, m‚ÇÇ‚ÇÉ.w]

lemma ext (h : ‚àÄ (k‚ÇÅ k‚ÇÇ : J) (h‚ÇÅ‚ÇÇ : k‚ÇÅ ‚â§ k‚ÇÇ) (h‚ÇÇ : k‚ÇÇ ‚â§ j),
    MapEq F G k‚ÇÅ k‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ) :
    F = G := by
  apply Arrow.functor_ext
  rintro ‚ü®k‚ÇÅ, _‚ü© ‚ü®k‚ÇÇ, h‚ÇÇ‚ü© f
  apply h

end subsingleton

open subsingleton in
instance subsingleton : Subsingleton (Œ¶.Iteration j) where
  allEq iter‚ÇÅ iter‚ÇÇ := by
    suffices iter‚ÇÅ.F = iter‚ÇÇ.F by aesop
    revert iter‚ÇÅ iter‚ÇÇ
    induction j using SuccOrder.limitRecOn with
    | hm j h =>
        obtain rfl := h.eq_bot
        intro iter‚ÇÅ iter‚ÇÇ
        refine ext (fun k‚ÇÅ k‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ ‚Ü¶ ?_)
        obtain rfl : k‚ÇÇ = ‚ä• := by simpa using h‚ÇÇ
        obtain rfl : k‚ÇÅ = ‚ä• := by simpa using h‚ÇÅ‚ÇÇ
        apply mapEq_refl _ _ (by simp)
    | hs j hj‚ÇÅ hj‚ÇÇ =>
        intro iter‚ÇÅ iter‚ÇÇ
        refine ext (fun k‚ÇÅ k‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ ‚Ü¶ ?_)
        have h‚ÇÄ := Order.le_succ j
        replace hj‚ÇÇ := hj‚ÇÇ (iter‚ÇÅ.trunc h‚ÇÄ) (iter‚ÇÇ.trunc h‚ÇÄ)
        have hsucc := Functor.congr_obj hj‚ÇÇ ‚ü®j, by simp‚ü©
        dsimp at hj‚ÇÇ hsucc
        wlog h : k‚ÇÇ ‚â§ j generalizing k‚ÇÅ k‚ÇÇ
        ¬∑ obtain h‚ÇÇ | rfl := h‚ÇÇ.lt_or_eq
          ¬∑ exact this _ _ _ _ ((Order.lt_succ_iff_of_not_isMax hj‚ÇÅ).1 h‚ÇÇ)
          ¬∑ by_cases h' : k‚ÇÅ ‚â§ j
            ¬∑ apply mapEq_trans _ h‚ÇÄ (this k‚ÇÅ j h' h‚ÇÄ (by simp))
              simp only [MapEq, ‚Üê arrowSucc_def _ _ (Order.lt_succ_of_not_isMax hj‚ÇÅ),
                arrowSucc_eq, hsucc]
            ¬∑ simp only [not_le] at h'
              obtain rfl : k‚ÇÅ = Order.succ j := le_antisymm h‚ÇÅ‚ÇÇ
                ((Order.succ_le_iff_of_not_isMax hj‚ÇÅ).2 h')
              rw [MapEq, arrowMap_refl, arrowMap_refl,
                obj_succ _ _ h', obj_succ _ _ h', hsucc]
        simp only [MapEq, ‚Üê arrowMap_restrictionLE _ (Order.le_succ j) _ _ _ h, hj‚ÇÇ]
    | hl j h‚ÇÅ h‚ÇÇ =>
        intro iter‚ÇÅ iter‚ÇÇ
        refine ext (fun k‚ÇÅ k‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÉ ‚Ü¶ ?_)
        wlog h‚ÇÑ : k‚ÇÇ < j generalizing k‚ÇÅ k‚ÇÇ; swap
        ¬∑ have := h‚ÇÇ k‚ÇÇ h‚ÇÑ (iter‚ÇÅ.trunc h‚ÇÑ.le) (iter‚ÇÇ.trunc h‚ÇÑ.le)
          simp at this
          simp only [MapEq, ‚Üê arrowMap_restrictionLE _ h‚ÇÑ.le _ _ _ (by rfl), this]
        ¬∑ obtain rfl : j = k‚ÇÇ := le_antisymm (by simpa using h‚ÇÑ) h‚ÇÉ
          have : restrictionLT iter‚ÇÅ.F le_rfl = restrictionLT iter‚ÇÇ.F le_rfl :=
            Arrow.functor_ext (fun _ l _ ‚Ü¶ this _ _ _ _ l.2)
          by_cases h‚ÇÖ : k‚ÇÅ < j
          ¬∑ dsimp [MapEq]
            simp_rw [arrowMap_limit _ _ h‚ÇÅ _ _ h‚ÇÖ, this]
          ¬∑ obtain rfl : k‚ÇÅ = j := le_antisymm h‚ÇÅ‚ÇÇ (by simpa using h‚ÇÖ)
            apply mapEq_refl
            simp only [obj_limit _ _ h‚ÇÅ, this]


end Iteration

end SuccStruct

end SmallObject

end CategoryTheory
