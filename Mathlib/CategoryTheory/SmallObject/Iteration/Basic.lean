/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Category.Preorder
import Mathlib.CategoryTheory.Limits.IsLimit
import Mathlib.Order.ConditionallyCompleteLattice.Basic
import Mathlib.Order.SuccPred.Limit

/-! # Transfinite iterations of a functor

In this file, given a functor `Œ¶ : C ‚•§ C` and a natural transformation
`Œµ : ùü≠ C ‚ü∂ Œ¶`, we shall define the transfinite iterations of `Œ¶` (TODO).

Given `j : J` where `J` is a well ordered set, we first introduce
a category `Iteration Œµ j`. An object in this category consists of
a functor `F : Set.Iic j ‚•§ C ‚•§ C` equipped with the data
which makes it the `i`th-iteration of `Œ¶` for all `i` such that `i ‚â§ j`.
Under suitable assumptions on `C`, we shall show that this category
`Iteration Œµ j` is equivalent to the punctual category (TODO).
We shall study morphisms in this category, showing first that
there is at most one morphism between two morphisms (done), and secondly,
that there does always exist a unique morphism between
two objects (TODO). Then, we shall show the existence of
an object (TODO). In these proofs, which are all done using
transfinite induction, we have to treat three cases separately:
* the case `j = ‚ä•`;
* the case `j` is a successor;
* the case `j` is a limit element.

-/

universe u

namespace CategoryTheory

open Category Limits

variable {C : Type*} [Category C] {Œ¶ : C ‚•§ C} (Œµ : ùü≠ C ‚ü∂ Œ¶) {J : Type u}

namespace Functor

namespace Iteration

variable [Preorder J] {j : J} (F : Set.Iic j ‚•§ C)

/-- The map `F.obj ‚ü®i, _‚ü© ‚ü∂ F.obj ‚ü®Order.succ i, _‚ü©` when `F : Set.Iic j ‚•§ C`
and `i : J` is such that `i < j`. -/
noncomputable abbrev mapSucc' [SuccOrder J] (i : J) (hi : i < j) :
    F.obj ‚ü®i, hi.le‚ü© ‚ü∂ F.obj ‚ü®Order.succ i, Order.succ_le_of_lt hi‚ü© :=
  F.map <| homOfLE <| Subtype.mk_le_mk.2 <| Order.le_succ i

variable {i : J} (hi : i ‚â§ j)

/-- The functor `Set.Iio i ‚•§ C` obtained by "restriction" of `F : Set.Iic j ‚•§ C`
when `i ‚â§ j`. -/
def restrictionLT : Set.Iio i ‚•§ C :=
  (monotone_inclusion_lt_le_of_le hi).functor ‚ãô F

@[simp]
lemma restrictionLT_obj (k : J) (hk : k < i) :
    (restrictionLT F hi).obj ‚ü®k, hk‚ü© = F.obj ‚ü®k, hk.le.trans hi‚ü© := rfl

@[simp]
lemma restrictionLT_map {k‚ÇÅ k‚ÇÇ : Set.Iio i} (œÜ : k‚ÇÅ ‚ü∂ k‚ÇÇ) :
    (restrictionLT F hi).map œÜ = F.map (homOfLE (by simpa using leOfHom œÜ)) := rfl

/-- Given `F : Set.Iic j ‚•§ C`, `i : J` such that `hi : i ‚â§ j`, this is the
cocone consisting of all maps `F.obj ‚ü®k, hk‚ü© ‚ü∂ F.obj ‚ü®i, hi‚ü©` for `k : J` such that `k < i`. -/
@[simps]
def coconeOfLE : Cocone (restrictionLT F hi) where
  pt := F.obj ‚ü®i, hi‚ü©
  Œπ :=
    { app := fun ‚ü®k, hk‚ü© => F.map (homOfLE (by simpa using hk.le))
      naturality := fun ‚ü®k‚ÇÅ, hk‚ÇÅ‚ü© ‚ü®k‚ÇÇ, hk‚ÇÇ‚ü© _ => by
        simp [comp_id, ‚Üê Functor.map_comp, homOfLE_comp] }

/-- The functor `Set.Iic i ‚•§ C` obtained by "restriction" of `F : Set.Iic j ‚•§ C`
when `i ‚â§ j`. -/
def restrictionLE : Set.Iic i ‚•§ C :=
  (monotone_inclusion_le_le_of_le hi).functor ‚ãô F

@[simp]
lemma restrictionLE_obj (k : J) (hk : k ‚â§ i) :
    (restrictionLE F hi).obj ‚ü®k, hk‚ü© = F.obj ‚ü®k, hk.trans hi‚ü© := rfl

@[simp]
lemma restrictionLE_map {k‚ÇÅ k‚ÇÇ : Set.Iic i} (œÜ : k‚ÇÅ ‚ü∂ k‚ÇÇ) :
    (restrictionLE F hi).map œÜ = F.map (homOfLE (by simpa using leOfHom œÜ)) := rfl

end Iteration

/-- The category of `j`th iterations of a functor `Œ¶` equipped with a natural
transformation `Œµ : ùü≠ C ‚ü∂ Œ¶`. An object consists of the data of all iterations
of `Œ¶` for `i : J` such that `i ‚â§ j` (this is the field `F`). Such objects are
equipped with data and properties which characterizes the iterations up to a unique
isomorphism for the three types of elements: `‚ä•`, successors, limit elements. -/
structure Iteration [Preorder J] [OrderBot J] [SuccOrder J] (j : J) where
  /-- The data of all `i`th iterations for `i : J` such that `i ‚â§ j`. -/
  F : Set.Iic j ‚•§ C ‚•§ C
  /-- The zeroth iteration is the identity functor. -/
  isoZero : F.obj ‚ü®‚ä•, bot_le‚ü© ‚âÖ ùü≠ C
  /-- The iteration on a successor element is obtained by composition of
  the previous iteration with `Œ¶`. -/
  isoSucc (i : J) (hi : i < j) : F.obj ‚ü®Order.succ i, Order.succ_le_of_lt hi‚ü© ‚âÖ F.obj ‚ü®i, hi.le‚ü© ‚ãô Œ¶
  /-- The natural map from an iteration to its successor is induced by `Œµ`. -/
  mapSucc'_eq (i : J) (hi : i < j) :
    Iteration.mapSucc' F i hi = whiskerLeft _ Œµ ‚â´ (isoSucc i hi).inv
  /-- If `i` is a limit element, the `i`th iteration is the colimit
  of `k`th iterations for `k < i`. -/
  isColimit (i : J) (hi : Order.IsSuccLimit i) (hij : i ‚â§ j) :
    IsColimit (Iteration.coconeOfLE F hij)

namespace Iteration

variable {Œµ}
variable {j : J}

section

variable [Preorder J] [OrderBot J] [SuccOrder J] (iter : Œ¶.Iteration Œµ j)

/-- For `iter : Œ¶.Iteration.Œµ j`, this is the map
`iter.F.obj ‚ü®i, _‚ü© ‚ü∂ iter.F.obj ‚ü®Order.succ i, _‚ü©` if `i : J` is such that `i < j`. -/
noncomputable abbrev mapSucc (i : J) (hi : i < j) :
    iter.F.obj ‚ü®i, hi.le‚ü© ‚ü∂ iter.F.obj ‚ü®Order.succ i, Order.succ_le_of_lt hi‚ü© :=
  mapSucc' iter.F i hi

lemma mapSucc_eq (i : J) (hi : i < j) :
    iter.mapSucc i hi = whiskerLeft _ Œµ ‚â´ (iter.isoSucc i hi).inv :=
  iter.mapSucc'_eq _ hi

end

section

variable [Preorder J] [OrderBot J] [SuccOrder J] (iter‚ÇÅ iter‚ÇÇ : Œ¶.Iteration Œµ j)

/-- A morphism between two objects `iter‚ÇÅ` and `iter‚ÇÇ` in the
category `Œ¶.Iteration Œµ j` of `j`th iterations of a functor `Œ¶`
equipped with a natural transformation `Œµ : ùü≠ C ‚ü∂ Œ¶` consists of a natural
transformation `natTrans : iter‚ÇÅ.F ‚ü∂ iter‚ÇÇ.F` which is compatible with the
isomorphisms `isoZero` and `isoSucc`. -/
structure Hom where
  /-- A natural transformation `iter‚ÇÅ.F ‚ü∂ iter‚ÇÇ.F` -/
  natTrans : iter‚ÇÅ.F ‚ü∂ iter‚ÇÇ.F
  natTrans_app_zero :
    natTrans.app ‚ü®‚ä•, bot_le‚ü© = iter‚ÇÅ.isoZero.hom ‚â´ iter‚ÇÇ.isoZero.inv := by aesop_cat
  natTrans_app_succ (i : J) (hi : i < j) :
    natTrans.app ‚ü®Order.succ i, Order.succ_le_of_lt hi‚ü© = (iter‚ÇÅ.isoSucc i hi).hom ‚â´
      whiskerRight (natTrans.app ‚ü®i, hi.le‚ü©) _ ‚â´ (iter‚ÇÇ.isoSucc i hi).inv := by aesop_cat

namespace Hom

attribute [simp, reassoc] natTrans_app_zero

/-- The identity morphism in the category `Œ¶.Iteration Œµ j`. -/
@[simps]
def id : Hom iter‚ÇÅ iter‚ÇÅ where
  natTrans := ùüô _

variable {iter‚ÇÅ iter‚ÇÇ}

-- Note: this is not made a global ext lemma because it is shown below
-- that the type of morphisms is a subsingleton.
lemma ext' {f g : Hom iter‚ÇÅ iter‚ÇÇ} (h : f.natTrans = g.natTrans) : f = g := by
  cases f
  cases g
  subst h
  rfl

attribute [local ext] ext'

/-- The composition of morphisms in the category `Œ¶.Iteration Œµ j`. -/
@[simps]
def comp {iter‚ÇÉ : Iteration Œµ j} (f : Hom iter‚ÇÅ iter‚ÇÇ) (g : Hom iter‚ÇÇ iter‚ÇÉ) :
    Hom iter‚ÇÅ iter‚ÇÉ where
  natTrans := f.natTrans ‚â´ g.natTrans
  natTrans_app_succ i hi := by simp [natTrans_app_succ _ _ hi]

instance : Category (Iteration Œµ j) where
  Hom := Hom
  id := id
  comp := comp

instance {J} {j : J} [PartialOrder J] [OrderBot J] [WellFoundedLT J] [SuccOrder J]
    {iter‚ÇÅ iter‚ÇÇ : Iteration Œµ j} :
    Subsingleton (iter‚ÇÅ ‚ü∂ iter‚ÇÇ) where
  allEq f g := by
    apply ext'
    suffices ‚àÄ i hi, f.natTrans.app ‚ü®i, hi‚ü© = g.natTrans.app ‚ü®i, hi‚ü© by
      ext ‚ü®i, hi‚ü© : 2
      apply this
    intro i
    induction i using SuccOrder.limitRecOn with
    | hm j H =>
      obtain rfl := H.eq_bot
      simp [natTrans_app_zero]
    | hs j H IH =>
      intro hj
      simp [Hom.natTrans_app_succ, IH, (Order.lt_succ_of_not_isMax H).trans_le hj]
    | hl j H IH =>
      refine fun hj ‚Ü¶ (iter‚ÇÅ.isColimit j H hj).hom_ext ?_
      rintro ‚ü®k, hk‚ü©
      simp [IH k hk]

end Hom

@[simp]
lemma natTrans_id : Hom.natTrans (ùüô iter‚ÇÅ) = ùüô _ := rfl

variable {iter‚ÇÅ iter‚ÇÇ}

@[simp, reassoc]
lemma natTrans_comp {iter‚ÇÉ : Iteration Œµ j} (œÜ : iter‚ÇÅ ‚ü∂ iter‚ÇÇ) (œà : iter‚ÇÇ ‚ü∂ iter‚ÇÉ) :
    (œÜ ‚â´ œà).natTrans = œÜ.natTrans ‚â´ œà.natTrans := rfl

@[reassoc]
lemma natTrans_naturality (œÜ : iter‚ÇÅ ‚ü∂ iter‚ÇÇ) (i‚ÇÅ i‚ÇÇ : J) (h : i‚ÇÅ ‚â§ i‚ÇÇ) (h' : i‚ÇÇ ‚â§ j) :
    iter‚ÇÅ.F.map (by exact homOfLE h) ‚â´ œÜ.natTrans.app ‚ü®i‚ÇÇ, h'‚ü© =
      œÜ.natTrans.app ‚ü®i‚ÇÅ, h.trans h'‚ü© ‚â´ iter‚ÇÇ.F.map (by exact homOfLE h) := by
  apply œÜ.natTrans.naturality

variable (Œµ) in
/-- The evaluation functor `Iteration Œµ j ‚•§ C ‚•§ C` at `i : J` when `i ‚â§ j`. -/
@[simps]
def eval {i : J} (hi : i ‚â§ j) : Iteration Œµ j ‚•§ C ‚•§ C where
  obj iter := iter.F.obj ‚ü®i, hi‚ü©
  map œÜ := œÜ.natTrans.app _

/-- Given `iter : Iteration Œµ j` and `i : J` such that `i ‚â§ j`, this is the
induced element in `Iteration Œµ i`. -/
@[simps F isoZero isoSucc]
def trunc (iter : Iteration Œµ j) {i : J} (hi : i ‚â§ j) : Iteration Œµ i where
  F := restrictionLE (iter.F) hi
  isoZero := iter.isoZero
  isoSucc k hk := iter.isoSucc k (lt_of_lt_of_le hk hi)
  mapSucc'_eq k hk := iter.mapSucc'_eq k (lt_of_lt_of_le hk hi)
  isColimit k hk' hk := iter.isColimit k hk' (hk.trans hi)

variable (Œµ) in
/-- The truncation functor `Iteration Œµ j ‚•§ Iteration Œµ i` when `i ‚â§ j`. -/
@[simps obj]
def truncFunctor {i : J} (hi : i ‚â§ j) : Iteration Œµ j ‚•§ Iteration Œµ i where
  obj iter := iter.trunc hi
  map {iter‚ÇÅ iter‚ÇÇ} œÜ :=
    { natTrans := whiskerLeft _ œÜ.natTrans
      natTrans_app_succ := fun k hk => œÜ.natTrans_app_succ k (lt_of_lt_of_le hk hi) }

@[simp]
lemma truncFunctor_map_natTrans_app
    (œÜ : iter‚ÇÅ ‚ü∂ iter‚ÇÇ) {i : J} (hi : i ‚â§ j) (k : J) (hk : k ‚â§ i) :
    ((truncFunctor Œµ hi).map œÜ).natTrans.app ‚ü®k, hk‚ü© =
      œÜ.natTrans.app ‚ü®k, hk.trans hi‚ü© := rfl

end

namespace Hom

variable [PartialOrder J] [OrderBot J] [SuccOrder J] [WellFoundedLT J]
  (iter‚ÇÅ iter‚ÇÇ : Œ¶.Iteration Œµ j)

lemma congr_app (œÜ œÜ' : iter‚ÇÅ ‚ü∂ iter‚ÇÇ) (i : J) (hi : i ‚â§ j) :
    œÜ.natTrans.app ‚ü®i, hi‚ü© = œÜ'.natTrans.app ‚ü®i, hi‚ü© := by
  obtain rfl := Subsingleton.elim œÜ œÜ'
  rfl

end Hom

end Iteration

end Functor

end CategoryTheory
