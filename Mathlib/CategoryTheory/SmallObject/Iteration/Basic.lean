/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Category.Preorder
import Mathlib.CategoryTheory.Limits.FunctorCategory.Basic
import Mathlib.CategoryTheory.Limits.Shapes.Preorder.HasIterationOfShape
import Mathlib.CategoryTheory.Limits.Shapes.Preorder.PrincipalSeg
import Mathlib.CategoryTheory.Limits.Comma
import Mathlib.Order.ConditionallyCompleteLattice.Basic
import Mathlib.Order.SuccPred.Limit
import Mathlib.Order.Interval.Set.InitialSeg

/-! # Transfinite iterations of a successor structure

In this file, we introduce the structure `SuccStruct` on a category `C`.
It consists of the data of an object `X‚ÇÄ : C`, a successor map `succ : C ‚Üí C`
and a morphism `toSucc : X ‚ü∂ succ X` for any `X : C`. The map `toSucc`
does not have to be natural in `X`. For any element `j : J` in a
well-ordered type `J`, we would like to define
the iteration of `Œ¶ : SuccStruct C`, as a functor `F : J ‚•§ C`
such that `F.obj ‚ä• = X‚ÇÄ`, `F.obj j ‚ü∂ F.obj (Order.succ j)` is `toSucc (F.obj j)`
when `j` is not maximal, and when `j` is limit, `F.obj j` should be the
colimit of the `F.obj k` for `k < j`.

In the small object argument, we shall apply this to the iteration of
a functor `F : C ‚•§ C` equipped with a natural transformation `Œµ : ùü≠ C ‚ü∂ F`:
this will correspond to the case of
`SuccStruct.ofNatTrans Œµ : SuccStruct (C ‚•§ C)`, for which `X‚ÇÄ := ùü≠ C`,
`succ G := G ‚ãô F` and `toSucc G : G ‚ü∂ G ‚ãô F` is given by `whiskerLeft G Œµ`.

The construction of the iteration of `Œ¶ : SuccStruct C` is done
by transfinite induction, under an assumption `HasIterationOfShape C J`.
However, for a limit element `j : J`, the definition of `F.obj j`
does not involve only the objects `F.obj i` for `i < j`, but it also
involves the maps `F.obj i‚ÇÅ ‚ü∂ F.obj i‚ÇÇ` for `i‚ÇÅ ‚â§ i‚ÇÇ < j`.
Then, this is not a straightforward application of definitions by
transfinite induction. In order to solve this technical difficulty,
we introduce a structure `Œ¶.Iteration j` for any `j : J`. This
structure contains all the expected data and properties for
all the indices that are `‚â§ j`. In this file, we show that
`Œ¶.Iteration j` is a subsingleton. The existence shall be
obtained in the file `SmallObject.Iteration.Nonempty`, and
the construction of the functor `Œ¶.iterationFunctor J : J ‚•§ C`
and of its colimit `Œ¶.iteration J : C` will done in the
file `SmallObject.TransfiniteIteration`.

The map `Œ¶.toSucc X : X ‚ü∂ Œ¶.succ X` does not have to be natural
(and it is not in certain applications). Then, two isomorphic
objects `X` and `Y` may have non isomorphic successors. This is
the reason why we make an extensive use of equalities in
`C` and in `Arrow C` in the definitions.

## Note

The iteration was first introduced in mathlib by Jo√´l Riou, using
a different approach as the one described above. After refactoring
his code, he found that the approach described above had already
been used in the pioneering formalization work in Lean 3 by
Reid Barton in 2018 towards the model category structure on
topological spaces.

-/

universe w v v' u u'

namespace CategoryTheory

open Category Limits

namespace SmallObject

variable {C : Type u} [Category.{v} C] {J : Type w}

section

variable [PartialOrder J] {j : J} (F : Set.Iic j ‚•§ C) {i : J} (hi : i ‚â§ j)

/-- The functor `Set.Iio i ‚•§ C` obtained by "restriction" of `F : Set.Iic j ‚•§ C`
when `i ‚â§ j`. -/
def restrictionLT : Set.Iio i ‚•§ C :=
  (Set.principalSegIioIicOfLE hi).monotone.functor ‚ãô F


@[simp]
lemma restrictionLT_obj (k : J) (hk : k < i) :
    (restrictionLT F hi).obj ‚ü®k, hk‚ü© = F.obj ‚ü®k, hk.le.trans hi‚ü© := rfl

@[simp]
lemma restrictionLT_map {k‚ÇÅ k‚ÇÇ : Set.Iio i} (œÜ : k‚ÇÅ ‚ü∂ k‚ÇÇ) :
    (restrictionLT F hi).map œÜ = F.map (homOfLE (by simpa using leOfHom œÜ)) := rfl

/-- Given `F : Set.Iic j ‚•§ C`, `i : J` such that `hi : i ‚â§ j`, this is the
cocone consisting of all maps `F.obj ‚ü®k, hk‚ü© ‚ü∂ F.obj ‚ü®i, hi‚ü©` for `k : J` such that `k < i`. -/
@[simps!]
def coconeOfLE : Cocone (restrictionLT F hi) :=
  (Set.principalSegIioIicOfLE hi).cocone F

/-- The functor `Set.Iic i ‚•§ C` obtained by "restriction" of `F : Set.Iic j ‚•§ C`
when `i ‚â§ j`. -/
def restrictionLE : Set.Iic i ‚•§ C :=
  (Set.initialSegIicIicOfLE hi).monotone.functor ‚ãô F

@[simp]
lemma restrictionLE_obj (k : J) (hk : k ‚â§ i) :
    (restrictionLE F hi).obj ‚ü®k, hk‚ü© = F.obj ‚ü®k, hk.trans hi‚ü© := rfl

@[simp]
lemma restrictionLE_map {k‚ÇÅ k‚ÇÇ : Set.Iic i} (œÜ : k‚ÇÅ ‚ü∂ k‚ÇÇ) :
    (restrictionLE F hi).map œÜ = F.map (homOfLE (by simpa using leOfHom œÜ)) := rfl

end

variable (C) in
/-- A successor structure on a category consists of the
data of an object `succ X` for any `X : C`, a map `toSucc X : X ‚ü∂ toSucc X`
(which does not need to be natural), and a zeroth object `X‚ÇÄ`.
-/
structure SuccStruct where
  /-- the zeroth object -/
  X‚ÇÄ : C
  /-- the successor of an object -/
  succ (X : C) : C
  /-- the map to the successor -/
  toSucc (X : C) : X ‚ü∂ succ X

namespace SuccStruct

/-- Given a functor `Œ¶ : C ‚•§ C`, a natural transformation of the form `ùü≠ C ‚ü∂ Œ¶`
induces a successor structure on `C ‚•§ C`. -/
@[simps]
def ofNatTrans {F : C ‚•§ C} (Œµ : ùü≠ C ‚ü∂ F) : SuccStruct (C ‚•§ C) where
  succ G := G ‚ãô F
  toSucc G := whiskerLeft G Œµ
  X‚ÇÄ := ùü≠ C

variable (Œ¶ : SuccStruct C)

/-- The class of morphisms that are of the form `toSucc X : X ‚ü∂ succ X`. -/
def prop : MorphismProperty C := .ofHoms (fun (X : C) ‚Ü¶ Œ¶.toSucc X)

lemma prop_toSucc (X : C) : Œ¶.prop (Œ¶.toSucc X) := ‚ü®_‚ü©

/-- The map `Œ¶.toSucc X : X ‚ü∂ Œ¶.Succ X`, as an element in `Arrow C`. -/
@[simps!]
def toSuccArrow (X : C) : Arrow C := Arrow.mk (Œ¶.toSucc X)

lemma prop_iff {X Y : C} (f : X ‚ü∂ Y) :
    Œ¶.prop f ‚Üî Arrow.mk f = Œ¶.toSuccArrow X := by
  constructor
  ¬∑ rintro ‚ü®_‚ü©
    rfl
  ¬∑ intro h
    rw [‚Üê Œ¶.prop.arrow_mk_mem_toSet_iff, h]
    apply prop_toSucc

variable {Œ¶}

lemma prop.succ_eq {X Y : C} {f : X ‚ü∂ Y} (hf : Œ¶.prop f) :
    Œ¶.succ X = Y := by
  cases hf
  rfl

@[reassoc]
lemma prop.fac {X Y : C} {f : X ‚ü∂ Y} (hf : Œ¶.prop f) :
    f = Œ¶.toSucc X ‚â´ eqToHom hf.succ_eq := by
  cases hf
  simp

/-- If `Œ¶ : SuccStruct C` and `f` is a morphism in `C` which
satisfies `Œ¶.prop f`, then this is the isomorphism of arrows
between `f` and `Œ¶.toSuccArrow X`. -/
@[simps!]
def prop.arrowIso {X Y : C} {f : X ‚ü∂ Y} (hf : Œ¶.prop f) :
    Arrow.mk f ‚âÖ Œ¶.toSuccArrow X :=
  Arrow.isoMk (Iso.refl _) (eqToIso hf.succ_eq.symm) (by simp [hf.fac])

variable (Œ¶)
variable [LinearOrder J]

/-- Given a functor `F : Set.Iic ‚•§ C`, this is the morphism in `C`, as an element
in `Arrow C`, that is obtained by applying `F.map` to an inequality. -/
def arrowMap {j : J} (F : Set.Iic j ‚•§ C) (i‚ÇÅ i‚ÇÇ : J) (h‚ÇÅ‚ÇÇ : i‚ÇÅ ‚â§ i‚ÇÇ) (h‚ÇÇ : i‚ÇÇ ‚â§ j) :
    Arrow C :=
  Arrow.mk (F.map (homOfLE h‚ÇÅ‚ÇÇ : ‚ü®i‚ÇÅ, h‚ÇÅ‚ÇÇ.trans h‚ÇÇ‚ü© ‚ü∂ ‚ü®i‚ÇÇ, h‚ÇÇ‚ü©))

@[simp]
lemma arrowMap_refl {j : J} (F : Set.Iic j ‚•§ C) (i : J) (hi : i ‚â§ j) :
    arrowMap F i i (by simp) hi = Arrow.mk (ùüô (F.obj ‚ü®i, hi‚ü©)) := by
  simp [arrowMap]

lemma arrowMap_restrictionLE {j : J} (F : Set.Iic j ‚•§ C) {j' : J} (hj' : j' ‚â§ j)
    (i‚ÇÅ i‚ÇÇ : J) (h‚ÇÅ‚ÇÇ : i‚ÇÅ ‚â§ i‚ÇÇ) (h‚ÇÇ : i‚ÇÇ ‚â§ j') :
    arrowMap (restrictionLE F hj') i‚ÇÅ i‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ =
      arrowMap F i‚ÇÅ i‚ÇÇ h‚ÇÅ‚ÇÇ (h‚ÇÇ.trans hj') := rfl

section

variable [SuccOrder J] {j : J} (F : Set.Iic j ‚•§ C) (i : J) (hi : i < j)

/-- Given a functor `F : Set.Iic j ‚•§ C` and `i : J` such that `i < j`,
this is the arrow `F.obj ‚ü®i, _‚ü© ‚ü∂ F.obj ‚ü®Order.succ i, _‚ü©`. -/
def arrowSucc : Arrow C :=
    arrowMap F i (Order.succ i) (Order.le_succ i) (Order.succ_le_of_lt hi)

lemma arrowSucc_def :
    arrowSucc F i hi = arrowMap F i (Order.succ i) (Order.le_succ i) (Order.succ_le_of_lt hi) :=
  rfl

end

section

variable [HasIterationOfShape J C]
  {i : J} (F : Set.Iio i ‚•§ C) (hi : Order.IsSuccLimit i) (k : J) (hk : k < i)

/-- Given `F : Set.Iio i ‚•§ C`, with `i` a limit element, and `k` such that `hk : k < i`,
this is the map `colimit.Œπ F ‚ü®k, hk‚ü©`, as an element in `Arrow C`. -/
noncomputable def arrowŒπ : Arrow C :=
  letI := hasColimitsOfShape_of_isSuccLimit C i hi
  Arrow.mk (colimit.Œπ F ‚ü®k, hk‚ü©)

lemma arrowŒπ_def :
    letI := hasColimitsOfShape_of_isSuccLimit C i hi
    arrowŒπ F hi k hk = Arrow.mk (colimit.Œπ F ‚ü®k, hk‚ü©) := rfl

end

variable [SuccOrder J] [OrderBot J] [HasIterationOfShape J C]

/-- The category of `j`th iterations of a successor structure `Œ¶ : SuccStruct C`.
An object consists of the data of all iterations of `Œ¶` for `i : J` such
that `i ‚â§ j` (this is the field `F`). Such objects are
equipped with data and properties which characterizes uniquely the iterations
on three types of elements: `‚ä•`, successors, limit elements. -/
@[ext]
structure Iteration [WellFoundedLT J] (j : J) where
  /-- The data of all `i`th iterations for `i : J` such that `i ‚â§ j`. -/
  F : Set.Iic j ‚•§ C
  /-- The zeroth iteration is the zeroth object . -/
  obj_bot : F.obj ‚ü®‚ä•, bot_le‚ü© = Œ¶.X‚ÇÄ
  /-- The iteration on a successor element is the successor. -/
  arrowSucc_eq (i : J) (hi : i < j) : arrowSucc F i hi = Œ¶.toSuccArrow (F.obj ‚ü®i, hi.le‚ü©)
  /-- The iteration on a limit element identifies to the colimit of the
  value on smaller elements, see `Iteration.isColimit`. -/
  arrowMap_limit (i : J) (hi : Order.IsSuccLimit i) (hij : i ‚â§ j) (k : J) (hk : k < i) :
    arrowMap F k i hk.le hij = arrowŒπ (restrictionLT F hij) hi k hk

variable [WellFoundedLT J]

namespace Iteration

variable {Œ¶}
variable {j : J}

section

variable (iter : Œ¶.Iteration j)

lemma obj_succ (i : J) (hi : i < j) :
    iter.F.obj ‚ü®Order.succ i, Order.succ_le_of_lt hi‚ü© = Œ¶.succ (iter.F.obj ‚ü®i, hi.le‚ü©) :=
  congr_arg Comma.right (iter.arrowSucc_eq i hi)

lemma prop_map_succ (i : J) (hi : i < j) :
    Œ¶.prop (iter.F.map (homOfLE (Order.le_succ i) :
      ‚ü®i, hi.le‚ü© ‚ü∂ ‚ü®Order.succ i, Order.succ_le_of_lt hi‚ü©)) := by
  rw [prop_iff, ‚Üê arrowMap, ‚Üê arrowSucc_def _ _ hi, iter.arrowSucc_eq]

lemma obj_limit (i : J) (hi : Order.IsSuccLimit i) (hij : i ‚â§ j) :
    letI := hasColimitsOfShape_of_isSuccLimit C i hi
    iter.F.obj ‚ü®i, hij‚ü© = colimit (restrictionLT iter.F hij) :=
  congr_arg Comma.right (iter.arrowMap_limit i hi hij ‚ä• (Order.IsSuccLimit.bot_lt hi))

/-- The iteration on a limit element identifies to the colimit of the
value on smaller elements. -/
noncomputable def isColimit (i : J) (hi : Order.IsSuccLimit i) (hij : i ‚â§ j) :
    IsColimit (coconeOfLE iter.F hij) := by
  letI := hasColimitsOfShape_of_isSuccLimit C i hi
  refine IsColimit.ofIsoColimit (colimit.isColimit (restrictionLT iter.F hij))
    (Cocones.ext (eqToIso (iter.obj_limit i hi hij).symm) ?_)
  rintro ‚ü®k, hk‚ü©
  apply Arrow.mk_injective
  dsimp
  rw [‚Üê arrowMap]
  simp [iter.arrowMap_limit i hi hij k hk, arrowŒπ_def]

/-- The element in `Œ¶.Iteration i` that is deduced from an element
in `Œ¶.Iteration j` when `i ‚â§ j`. -/
@[simps F]
def trunc (iter : Œ¶.Iteration j) {j' : J} (hj' : j' ‚â§ j) : Œ¶.Iteration j' where
  F := restrictionLE iter.F hj'
  obj_bot := iter.obj_bot
  arrowSucc_eq i hi := iter.arrowSucc_eq i (lt_of_lt_of_le hi hj')
  arrowMap_limit i hi hij k hk := iter.arrowMap_limit i hi (hij.trans hj') k hk

end

namespace subsingleton

variable {K : Type w} [LinearOrder K] {x : K} (F G : Set.Iic x ‚•§ C)

section

variable (k‚ÇÅ k‚ÇÇ : K) (h‚ÇÅ‚ÇÇ : k‚ÇÅ ‚â§ k‚ÇÇ) (h‚ÇÇ : k‚ÇÇ ‚â§ x)

/-- Auxiliary definition for the proof of `Subsingleton (Œ¶.Iteration j)`. -/
def MapEq : Prop := arrowMap F k‚ÇÅ k‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ = arrowMap G k‚ÇÅ k‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ

namespace MapEq

variable {k‚ÇÅ k‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ} (h : MapEq F G k‚ÇÅ k‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ)

include h

lemma src : F.obj ‚ü®k‚ÇÅ, h‚ÇÅ‚ÇÇ.trans h‚ÇÇ‚ü© = G.obj ‚ü®k‚ÇÅ, h‚ÇÅ‚ÇÇ.trans h‚ÇÇ‚ü© :=
  congr_arg Comma.left h

lemma tgt : F.obj ‚ü®k‚ÇÇ, h‚ÇÇ‚ü© = G.obj ‚ü®k‚ÇÇ, h‚ÇÇ‚ü© :=
  congr_arg Comma.right h

lemma w :
    F.map (homOfLE h‚ÇÅ‚ÇÇ : ‚ü®k‚ÇÅ, h‚ÇÅ‚ÇÇ.trans h‚ÇÇ‚ü© ‚ü∂ ‚ü®k‚ÇÇ, h‚ÇÇ‚ü©) =
      eqToHom (by rw [h.src]) ‚â´ G.map (homOfLE h‚ÇÅ‚ÇÇ : ‚ü®k‚ÇÅ, h‚ÇÅ‚ÇÇ.trans h‚ÇÇ‚ü© ‚ü∂ ‚ü®k‚ÇÇ, h‚ÇÇ‚ü©) ‚â´
        eqToHom (by rw [h.tgt]) := by
  have := (Arrow.mk_eq_mk_iff _ _).1 h
  tauto

end MapEq

end

variable {F G}

lemma mapEq_refl (k : K) (hk : k ‚â§ x) (h : F.obj ‚ü®k, hk‚ü© = G.obj ‚ü®k, hk‚ü©) :
    MapEq F G k k (by simp) hk := by
  rw [MapEq, arrowMap_refl, arrowMap_refl, h]

lemma mapEq_trans {i‚ÇÅ i‚ÇÇ i‚ÇÉ : K} (h‚ÇÅ‚ÇÇ : i‚ÇÅ ‚â§ i‚ÇÇ) (h‚ÇÇ‚ÇÉ : i‚ÇÇ ‚â§ i‚ÇÉ) {h‚ÇÉ : i‚ÇÉ ‚â§ x}
    (m‚ÇÅ‚ÇÇ : MapEq F G i‚ÇÅ i‚ÇÇ h‚ÇÅ‚ÇÇ (h‚ÇÇ‚ÇÉ.trans h‚ÇÉ)) (m‚ÇÇ‚ÇÉ : MapEq F G i‚ÇÇ i‚ÇÉ h‚ÇÇ‚ÇÉ h‚ÇÉ) :
    MapEq F G i‚ÇÅ i‚ÇÉ (h‚ÇÅ‚ÇÇ.trans h‚ÇÇ‚ÇÉ) h‚ÇÉ := by
  simp only [MapEq, arrowMap, Arrow.mk_eq_mk_iff]
  refine ‚ü®m‚ÇÅ‚ÇÇ.src, m‚ÇÇ‚ÇÉ.tgt, ?_‚ü©
  rw [‚Üê homOfLE_comp (y := ‚ü®i‚ÇÇ, h‚ÇÇ‚ÇÉ.trans h‚ÇÉ‚ü©) h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ]
  simp [-homOfLE_comp, m‚ÇÅ‚ÇÇ.w, m‚ÇÇ‚ÇÉ.w]

lemma ext (h : ‚àÄ (k‚ÇÅ k‚ÇÇ : K) (h‚ÇÅ‚ÇÇ : k‚ÇÅ ‚â§ k‚ÇÇ) (h‚ÇÇ : k‚ÇÇ ‚â§ x),
    MapEq F G k‚ÇÅ k‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ) :
    F = G := by
  apply Arrow.functor_ext
  rintro ‚ü®k‚ÇÅ, _‚ü© ‚ü®k‚ÇÇ, h‚ÇÇ‚ü© f
  apply h

end subsingleton

open subsingleton in
instance subsingleton : Subsingleton (Œ¶.Iteration j) where
  allEq iter‚ÇÅ iter‚ÇÇ := by
    suffices iter‚ÇÅ.F = iter‚ÇÇ.F by aesop
    revert iter‚ÇÅ iter‚ÇÇ
    induction j using SuccOrder.limitRecOn with
    | isMin j h =>
      obtain rfl := h.eq_bot
      intro iter‚ÇÅ iter‚ÇÇ
      refine ext (fun k‚ÇÅ k‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ ‚Ü¶ ?_)
      obtain rfl : k‚ÇÇ = ‚ä• := by simpa using h‚ÇÇ
      obtain rfl : k‚ÇÅ = ‚ä• := by simpa using h‚ÇÅ‚ÇÇ
      apply mapEq_refl _ _ (by simp only [obj_bot])
    | succ j hj‚ÇÅ hj‚ÇÇ =>
      intro iter‚ÇÅ iter‚ÇÇ
      refine ext (fun k‚ÇÅ k‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ ‚Ü¶ ?_)
      have h‚ÇÄ := Order.le_succ j
      replace hj‚ÇÇ := hj‚ÇÇ (iter‚ÇÅ.trunc h‚ÇÄ) (iter‚ÇÇ.trunc h‚ÇÄ)
      have hsucc := Functor.congr_obj hj‚ÇÇ ‚ü®j, by simp‚ü©
      dsimp at hj‚ÇÇ hsucc
      wlog h : k‚ÇÇ ‚â§ j generalizing k‚ÇÅ k‚ÇÇ
      ¬∑ obtain h‚ÇÇ | rfl := h‚ÇÇ.lt_or_eq
        ¬∑ exact this _ _ _ _ ((Order.lt_succ_iff_of_not_isMax hj‚ÇÅ).1 h‚ÇÇ)
        ¬∑ by_cases h' : k‚ÇÅ ‚â§ j
          ¬∑ apply mapEq_trans _ h‚ÇÄ (this k‚ÇÅ j h' h‚ÇÄ (by simp))
            simp only [MapEq, ‚Üê arrowSucc_def _ _ (Order.lt_succ_of_not_isMax hj‚ÇÅ),
              arrowSucc_eq, hsucc]
          ¬∑ simp only [not_le] at h'
            obtain rfl : k‚ÇÅ = Order.succ j := le_antisymm h‚ÇÅ‚ÇÇ
              ((Order.succ_le_iff_of_not_isMax hj‚ÇÅ).2 h')
            rw [MapEq, arrowMap_refl, arrowMap_refl,
              obj_succ _ _ h', obj_succ _ _ h', hsucc]
      simp only [MapEq, ‚Üê arrowMap_restrictionLE _ (Order.le_succ j) _ _ _ h, hj‚ÇÇ]
    | isSuccLimit j h‚ÇÅ h‚ÇÇ =>
      intro iter‚ÇÅ iter‚ÇÇ
      refine ext (fun k‚ÇÅ k‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÉ ‚Ü¶ ?_)
      wlog h‚ÇÑ : k‚ÇÇ < j generalizing k‚ÇÅ k‚ÇÇ; swap
      ¬∑ have := h‚ÇÇ k‚ÇÇ h‚ÇÑ (iter‚ÇÅ.trunc h‚ÇÑ.le) (iter‚ÇÇ.trunc h‚ÇÑ.le)
        simp at this
        simp only [MapEq, ‚Üê arrowMap_restrictionLE _ h‚ÇÑ.le _ _ _ (by rfl), this]
      ¬∑ obtain rfl : j = k‚ÇÇ := le_antisymm (by simpa using h‚ÇÑ) h‚ÇÉ
        have : restrictionLT iter‚ÇÅ.F le_rfl = restrictionLT iter‚ÇÇ.F le_rfl :=
          Arrow.functor_ext (fun _ l _ ‚Ü¶ this _ _ _ _ l.2)
        by_cases h‚ÇÖ : k‚ÇÅ < j
        ¬∑ dsimp [MapEq]
          simp_rw [arrowMap_limit _ _ h‚ÇÅ _ _ h‚ÇÖ, this]
        ¬∑ obtain rfl : k‚ÇÅ = j := le_antisymm h‚ÇÅ‚ÇÇ (by simpa using h‚ÇÖ)
          apply mapEq_refl
          simp only [obj_limit _ _ h‚ÇÅ, this]

lemma congr_obj {j‚ÇÅ j‚ÇÇ : J} (iter‚ÇÅ : Œ¶.Iteration j‚ÇÅ) (iter‚ÇÇ : Œ¶.Iteration j‚ÇÇ)
    (k : J) (h‚ÇÅ : k ‚â§ j‚ÇÅ) (h‚ÇÇ : k ‚â§ j‚ÇÇ) :
    iter‚ÇÅ.F.obj ‚ü®k, h‚ÇÅ‚ü© = iter‚ÇÇ.F.obj ‚ü®k, h‚ÇÇ‚ü© := by
  wlog h : j‚ÇÅ ‚â§ j‚ÇÇ generalizing j‚ÇÅ j‚ÇÇ
  ¬∑ exact (this iter‚ÇÇ iter‚ÇÅ h‚ÇÇ h‚ÇÅ (le_of_lt (by simpa using h))).symm
  rw [Subsingleton.elim iter‚ÇÅ (iter‚ÇÇ.trunc h)]
  dsimp

lemma congr_arrowMap {j‚ÇÅ j‚ÇÇ : J} (iter‚ÇÅ : Œ¶.Iteration j‚ÇÅ) (iter‚ÇÇ : Œ¶.Iteration j‚ÇÇ)
    {k‚ÇÅ k‚ÇÇ : J} (h : k‚ÇÅ ‚â§ k‚ÇÇ) (h‚ÇÅ : k‚ÇÇ ‚â§ j‚ÇÅ) (h‚ÇÇ : k‚ÇÇ ‚â§ j‚ÇÇ) :
    arrowMap iter‚ÇÅ.F k‚ÇÅ k‚ÇÇ h h‚ÇÅ = arrowMap iter‚ÇÇ.F k‚ÇÅ k‚ÇÇ h h‚ÇÇ := by
  wlog hj : j‚ÇÅ ‚â§ j‚ÇÇ generalizing j‚ÇÅ j‚ÇÇ
  ¬∑ simp [this iter‚ÇÇ iter‚ÇÅ h‚ÇÇ h‚ÇÅ ((not_le.1 hj).le)]
  rw [Subsingleton.elim iter‚ÇÅ (iter‚ÇÇ.trunc hj)]
  rfl

lemma congr_map {j‚ÇÅ j‚ÇÇ : J} (iter‚ÇÅ : Œ¶.Iteration j‚ÇÅ) (iter‚ÇÇ : Œ¶.Iteration j‚ÇÇ)
    {k‚ÇÅ k‚ÇÇ : J} (h : k‚ÇÅ ‚â§ k‚ÇÇ) (h‚ÇÅ : k‚ÇÇ ‚â§ j‚ÇÅ) (h‚ÇÇ : k‚ÇÇ ‚â§ j‚ÇÇ) :
    iter‚ÇÅ.F.map (homOfLE h : ‚ü®k‚ÇÅ, h.trans h‚ÇÅ‚ü© ‚ü∂ ‚ü®k‚ÇÇ, h‚ÇÅ‚ü©) =
      eqToHom (congr_obj iter‚ÇÅ iter‚ÇÇ k‚ÇÅ (h.trans h‚ÇÅ) (h.trans h‚ÇÇ)) ‚â´
        iter‚ÇÇ.F.map (homOfLE h) ‚â´
        eqToHom (congr_obj iter‚ÇÅ iter‚ÇÇ k‚ÇÇ h‚ÇÅ h‚ÇÇ).symm := by
  have := (Arrow.mk_eq_mk_iff _ _).1 (congr_arrowMap iter‚ÇÅ iter‚ÇÇ h h‚ÇÅ h‚ÇÇ)
  tauto

/-- Given `iter‚ÇÅ : Œ¶.Iteration j‚ÇÅ` and `iter‚ÇÇ : Œ¶.Iteration j‚ÇÇ`, with `j‚ÇÅ ‚â§ j‚ÇÇ`,
if `k‚ÇÅ ‚â§ k‚ÇÇ` are elements such that `k‚ÇÅ ‚â§ j‚ÇÅ` and `k‚ÇÇ ‚â§ k‚ÇÇ`, then this
is the canonical map `iter‚ÇÅ.F.obj ‚ü®k‚ÇÅ, h‚ÇÅ‚ü© ‚ü∂ iter‚ÇÇ.F.obj ‚ü®k‚ÇÇ, h‚ÇÇ‚ü©`. -/
def mapObj {j‚ÇÅ j‚ÇÇ : J} (iter‚ÇÅ : Œ¶.Iteration j‚ÇÅ) (iter‚ÇÇ : Œ¶.Iteration j‚ÇÇ)
    {k‚ÇÅ k‚ÇÇ : J} (h‚ÇÅ‚ÇÇ : k‚ÇÅ ‚â§ k‚ÇÇ) (h‚ÇÅ : k‚ÇÅ ‚â§ j‚ÇÅ) (h‚ÇÇ : k‚ÇÇ ‚â§ j‚ÇÇ) (hj : j‚ÇÅ ‚â§ j‚ÇÇ) :
    iter‚ÇÅ.F.obj ‚ü®k‚ÇÅ, h‚ÇÅ‚ü© ‚ü∂ iter‚ÇÇ.F.obj ‚ü®k‚ÇÇ, h‚ÇÇ‚ü© :=
  eqToHom (congr_obj iter‚ÇÅ iter‚ÇÇ k‚ÇÅ h‚ÇÅ (h‚ÇÅ.trans hj)) ‚â´
    iter‚ÇÇ.F.map (homOfLE h‚ÇÅ‚ÇÇ)

lemma arrow_mk_mapObj {j‚ÇÅ j‚ÇÇ : J} (iter‚ÇÅ : Œ¶.Iteration j‚ÇÅ) (iter‚ÇÇ : Œ¶.Iteration j‚ÇÇ)
    {k‚ÇÅ k‚ÇÇ : J} (h‚ÇÅ‚ÇÇ : k‚ÇÅ ‚â§ k‚ÇÇ) (h‚ÇÅ : k‚ÇÅ ‚â§ j‚ÇÅ) (h‚ÇÇ : k‚ÇÇ ‚â§ j‚ÇÇ) (hj : j‚ÇÅ ‚â§ j‚ÇÇ) :
    Arrow.mk (mapObj iter‚ÇÅ iter‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÅ h‚ÇÇ hj) =
      arrowMap iter‚ÇÇ.F k‚ÇÅ k‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ := by
  simp [mapObj, arrowMap]

@[simp]
lemma mapObj_refl {j : J} (iter : Œ¶.Iteration j)
    {k l : J} (h : k ‚â§ l) (h' : l ‚â§ j) :
    mapObj iter iter h (h.trans h') h' (by rfl) = iter.F.map (homOfLE h) := by
  simp [mapObj]

@[reassoc (attr := simp)]
lemma mapObj_trans {j‚ÇÅ j‚ÇÇ j‚ÇÉ : J} (iter‚ÇÅ : Œ¶.Iteration j‚ÇÅ) (iter‚ÇÇ : Œ¶.Iteration j‚ÇÇ)
    (iter‚ÇÉ : Œ¶.Iteration j‚ÇÉ) {k‚ÇÅ k‚ÇÇ k‚ÇÉ : J} (h‚ÇÅ‚ÇÇ : k‚ÇÅ ‚â§ k‚ÇÇ) (h‚ÇÇ‚ÇÉ : k‚ÇÇ ‚â§ k‚ÇÉ)
    (h‚ÇÅ : k‚ÇÅ ‚â§ j‚ÇÅ) (h‚ÇÇ : k‚ÇÇ ‚â§ j‚ÇÇ) (h‚ÇÉ : k‚ÇÉ ‚â§ j‚ÇÉ) (h‚ÇÅ‚ÇÇ' : j‚ÇÅ ‚â§ j‚ÇÇ) (h‚ÇÇ‚ÇÉ' : j‚ÇÇ ‚â§ j‚ÇÉ) :
    mapObj iter‚ÇÅ iter‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÅ h‚ÇÇ h‚ÇÅ‚ÇÇ' ‚â´ mapObj iter‚ÇÇ iter‚ÇÉ h‚ÇÇ‚ÇÉ h‚ÇÇ h‚ÇÉ h‚ÇÇ‚ÇÉ' =
      mapObj iter‚ÇÅ iter‚ÇÉ (h‚ÇÅ‚ÇÇ.trans h‚ÇÇ‚ÇÉ) h‚ÇÅ h‚ÇÉ (h‚ÇÅ‚ÇÇ'.trans h‚ÇÇ‚ÇÉ') := by
  simp [mapObj, congr_map iter‚ÇÇ iter‚ÇÉ h‚ÇÅ‚ÇÇ h‚ÇÇ (h‚ÇÇ.trans h‚ÇÇ‚ÇÉ'), ‚Üê Functor.map_comp]

end Iteration

end SuccStruct

end SmallObject

end CategoryTheory
