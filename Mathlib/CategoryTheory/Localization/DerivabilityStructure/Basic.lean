/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Localization.Resolution
import Mathlib.CategoryTheory.Localization.Opposite
import Mathlib.CategoryTheory.GuitartExact.Opposite

/-!
# Derivability structures

Let `Î¦ : LocalizerMorphism Wâ‚ Wâ‚‚` be a localizer morphism, i.e. `Wâ‚ : MorphismProperty Câ‚`,
`Wâ‚‚ : MorphismProperty Câ‚‚`, and `Î¦.functor : Câ‚ â¥¤ Câ‚‚` is a functors which maps `Wâ‚` to `Wâ‚‚`.
Following the definition introduced by Bruno Kahn and Georges Maltsiniotis in
[Bruno Kahn and Georges Maltsiniotis, *Structures de dÃ©rivabilitÃ©*][KahnMaltsiniotis2008],
we say that `Î¦` is a right derivability structure if `Î¦` has right resolutions and
the following 2-square is Guitart exact, where `Lâ‚ : Câ‚ â¥¤ Dâ‚` and `Lâ‚‚ : Câ‚‚ â¥¤ Dâ‚‚` are
localization functors for `Wâ‚` and `Wâ‚‚`, and `F : Dâ‚ â¥¤ Dâ‚‚` is the induced functor
on the localized categories:

```
    Î¦.functor
  Câ‚   â¥¤   Câ‚‚
  |         |
Lâ‚|         | Lâ‚‚
  v         v
  Dâ‚   â¥¤   Dâ‚‚
       F
```

## Implementation details

In the field `guitartExact'` of the structure `LocalizerMorphism.IsRightDerivabilityStructure`,
The condition that the square is Guitart exact is stated for the localization functors
of the constructed categories (`Wâ‚.Q` and `Wâ‚‚.Q`).
The lemma `LocalizerMorphism.isRightDerivabilityStructure_iff` show that it does
not depend of the choice of the localization functors.

## TODO

* Construct the injective derivability structure in order to derive functor from
  the bounded below homotopy category in an abelian category with enough injectives
* Construct the projective derivability structure in order to derive functor from
  the bounded above homotopy category in an abelian category with enough projectives
* Construct the flat derivability structure on the bounded above homotopy category
  of categories of modules (and categories of sheaves of modules)
* Define the product derivability structure and formalize derived functors of
  functors in several variables

## References
* [Bruno Kahn and Georges Maltsiniotis, *Structures de dÃ©rivabilitÃ©*][KahnMaltsiniotis2008]

-/
universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

namespace CategoryTheory

open Category Localization Functor

variable {Câ‚ : Type uâ‚} {Câ‚‚ : Type uâ‚‚} [Category.{vâ‚} Câ‚] [Category.{vâ‚‚} Câ‚‚]
  {Wâ‚ : MorphismProperty Câ‚} {Wâ‚‚ : MorphismProperty Câ‚‚}

namespace LocalizerMorphism

variable (Î¦ : LocalizerMorphism Wâ‚ Wâ‚‚)

/-- A localizer morphism `Î¦ : LocalizerMorphism Wâ‚ Wâ‚‚` is a right derivability
structure if it has right resolutions and the 2-square where the left and right functors
are localizations functors for `Wâ‚` and `Wâ‚‚` are Guitart exact. -/
class IsRightDerivabilityStructure : Prop where
  hasRightResolutions : Î¦.HasRightResolutions := by infer_instance
  /-- Do not use this field directly: use the more general
  `guitartExact_of_isRightDerivabilityStructure` instead,
  see also the lemma `isRightDerivabilityStructure_iff`. -/
  guitartExact' : TwoSquare.GuitartExact ((Î¦.catCommSq Wâ‚.Q Wâ‚‚.Q).iso).hom

attribute [instance] IsRightDerivabilityStructure.hasRightResolutions
  IsRightDerivabilityStructure.guitartExact'

variable {Dâ‚ Dâ‚‚ : Type*} [Category Dâ‚] [Category Dâ‚‚] (Lâ‚ : Câ‚ â¥¤ Dâ‚) (Lâ‚‚ : Câ‚‚ â¥¤ Dâ‚‚)
  [Lâ‚.IsLocalization Wâ‚] [Lâ‚‚.IsLocalization Wâ‚‚] (F : Dâ‚ â¥¤ Dâ‚‚)

lemma isRightDerivabilityStructure_iff [Î¦.HasRightResolutions] (e : Î¦.functor â‹™ Lâ‚‚ â‰… Lâ‚ â‹™ F) :
    Î¦.IsRightDerivabilityStructure â†” TwoSquare.GuitartExact e.hom := by
  have : Î¦.IsRightDerivabilityStructure â†”
      TwoSquare.GuitartExact ((Î¦.catCommSq Wâ‚.Q Wâ‚‚.Q).iso).hom :=
    âŸ¨fun h => h.guitartExact', fun h => âŸ¨inferInstance, hâŸ©âŸ©
  rw [this]
  let e' := (Î¦.catCommSq Wâ‚.Q Wâ‚‚.Q).iso
  let Eâ‚ := Localization.uniq Wâ‚.Q Lâ‚ Wâ‚
  let Eâ‚‚ := Localization.uniq Wâ‚‚.Q Lâ‚‚ Wâ‚‚
  let eâ‚ : Wâ‚.Q â‹™ Eâ‚.functor â‰… Lâ‚ := compUniqFunctor Wâ‚.Q Lâ‚ Wâ‚
  let eâ‚‚ : Wâ‚‚.Q â‹™ Eâ‚‚.functor â‰… Lâ‚‚ := compUniqFunctor Wâ‚‚.Q Lâ‚‚ Wâ‚‚
  let e'' : (Î¦.functor â‹™ Wâ‚‚.Q) â‹™ Eâ‚‚.functor â‰… (Wâ‚.Q â‹™ Eâ‚.functor) â‹™ F :=
    associator _ _ _ â‰ªâ‰« isoWhiskerLeft _ eâ‚‚ â‰ªâ‰« e â‰ªâ‰« isoWhiskerRight eâ‚.symm F
  let e''' : Î¦.localizedFunctor Wâ‚.Q Wâ‚‚.Q â‹™ Eâ‚‚.functor â‰… Eâ‚.functor â‹™ F :=
    liftNatIso Wâ‚.Q Wâ‚ _ _ _ _ e''
  have : TwoSquare.vComp' e'.hom e'''.hom eâ‚ eâ‚‚ = e.hom := by
    ext Xâ‚
    rw [TwoSquare.vComp'_app, liftNatIso_hom, liftNatTrans_app]
    simp only [Functor.comp_obj, Iso.trans_hom, isoWhiskerLeft_hom, isoWhiskerRight_hom,
      Iso.symm_hom, NatTrans.comp_app, Functor.associator_hom_app, whiskerLeft_app,
      whiskerRight_app, id_comp, assoc, e'']
    dsimp [Lifting.iso]
    rw [F.map_id, id_comp, â† F.map_comp, Iso.inv_hom_id_app, F.map_id, comp_id,
      â† Functor.map_comp_assoc]
    erw [show (CatCommSq.iso Î¦.functor Wâ‚.Q Wâ‚‚.Q (localizedFunctor Î¦ Wâ‚.Q Wâ‚‚.Q)).hom =
      (Lifting.iso Wâ‚.Q Wâ‚ _ _).inv by rfl, Iso.inv_hom_id_app]
    simp
  rw [â† TwoSquare.GuitartExact.vComp'_iff_of_equivalences e'.hom Eâ‚ Eâ‚‚ e''' eâ‚ eâ‚‚, this]

instance guitartExact_of_isRightDerivabilityStructure' [h : Î¦.IsRightDerivabilityStructure]
    (e : Î¦.functor â‹™ Lâ‚‚ â‰… Lâ‚ â‹™ F) : TwoSquare.GuitartExact e.hom := by
  simpa only [Î¦.isRightDerivabilityStructure_iff Lâ‚ Lâ‚‚ F e] using h

instance guitartExact_of_isRightDerivabilityStructure [Î¦.IsRightDerivabilityStructure] :
    TwoSquare.GuitartExact ((Î¦.catCommSq Lâ‚ Lâ‚‚).iso).hom :=
  guitartExact_of_isRightDerivabilityStructure' _ _ _ _ _

instance [Wâ‚.ContainsIdentities] : (LocalizerMorphism.id Wâ‚).HasRightResolutions :=
  fun Xâ‚‚ => âŸ¨RightResolution.mk (ğŸ™ Xâ‚‚) (Wâ‚.id_mem Xâ‚‚)âŸ©

instance [Wâ‚.ContainsIdentities] : (LocalizerMorphism.id Wâ‚).IsRightDerivabilityStructure := by
  rw [(LocalizerMorphism.id Wâ‚).isRightDerivabilityStructure_iff Wâ‚.Q Wâ‚.Q (ğŸ­ Wâ‚.Localization)
    (Iso.refl _)]
  dsimp
  exact TwoSquare.guitartExact_id Wâ‚.Q

/-- A localizer morphism `Î¦ : LocalizerMorphism Wâ‚ Wâ‚‚` is a left derivability
structure if it has left resolutions and the 2-square where the top and bottom functors
are localizations functors for `Wâ‚` and `Wâ‚‚` is Guitart exact. -/
class IsLeftDerivabilityStructure : Prop where
  hasLeftResolutions : Î¦.HasLeftResolutions := by infer_instance
  /-- Do not use this field directly: use the more general
  `guitartExact_of_isLeftDerivabilityStructure` instead,
  see also the lemma `isLeftDerivabilityStructure_iff`. -/
  guitartExact' : TwoSquare.GuitartExact ((Î¦.catCommSq Wâ‚.Q Wâ‚‚.Q).iso).inv

attribute [instance] IsLeftDerivabilityStructure.hasLeftResolutions
  IsLeftDerivabilityStructure.guitartExact'

lemma isLeftDerivabilityStructure_iff_op :
    Î¦.IsLeftDerivabilityStructure â†”
      Î¦.op.IsRightDerivabilityStructure := by
  let F := Î¦.localizedFunctor Wâ‚.Q Wâ‚‚.Q
  let e : Î¦.functor â‹™ Wâ‚‚.Q â‰… Wâ‚.Q â‹™ F := (Î¦.catCommSq Wâ‚.Q Wâ‚‚.Q).iso
  let e' : Î¦.functor.op â‹™ Wâ‚‚.Q.op â‰… Wâ‚.Q.op â‹™ F.op := NatIso.op e.symm
  have eq : TwoSquare.GuitartExact e'.hom â†” TwoSquare.GuitartExact e.inv :=
    TwoSquare.guitartExact_op_iff _
  constructor
  Â· rintro âŸ¨_, _âŸ©
    rwa [Î¦.op.isRightDerivabilityStructure_iff _ _ _ e', eq]
  Â· intro
    have : Î¦.HasLeftResolutions := by
      rw [hasLeftResolutions_iff_op]
      infer_instance
    refine âŸ¨inferInstance, ?_âŸ©
    rw [â† eq]
    exact Î¦.op.guitartExact_of_isRightDerivabilityStructure' _ _ _ e'

lemma isLeftDerivabilityStructure_iff [Î¦.HasLeftResolutions] (e : Î¦.functor â‹™ Lâ‚‚ â‰… Lâ‚ â‹™ F) :
    Î¦.IsLeftDerivabilityStructure â†” TwoSquare.GuitartExact e.inv := by
  rw [isLeftDerivabilityStructure_iff_op,
    Î¦.op.isRightDerivabilityStructure_iff Lâ‚.op Lâ‚‚.op F.op (NatIso.op e.symm),
    â† TwoSquare.guitartExact_op_iff e.inv]
  rfl

instance guitartExact_of_isLeftDerivabilityStructure' [h : Î¦.IsLeftDerivabilityStructure]
    (e : Î¦.functor â‹™ Lâ‚‚ â‰… Lâ‚ â‹™ F) : TwoSquare.GuitartExact e.inv := by
  simpa only [Î¦.isLeftDerivabilityStructure_iff Lâ‚ Lâ‚‚ F e] using h

instance guitartExact_of_isLeftDerivabilityStructure [Î¦.IsLeftDerivabilityStructure] :
    TwoSquare.GuitartExact ((Î¦.catCommSq Lâ‚ Lâ‚‚).iso).inv :=
  guitartExact_of_isLeftDerivabilityStructure' _ _ _ _ _

instance [Wâ‚.ContainsIdentities] : (LocalizerMorphism.id Wâ‚).HasLeftResolutions :=
  fun Xâ‚‚ => âŸ¨LeftResolution.mk (ğŸ™ Xâ‚‚) (Wâ‚.id_mem Xâ‚‚)âŸ©

instance [Wâ‚.ContainsIdentities] : (LocalizerMorphism.id Wâ‚).IsLeftDerivabilityStructure := by
  rw [(LocalizerMorphism.id Wâ‚).isLeftDerivabilityStructure_iff Wâ‚.Q Wâ‚.Q (ğŸ­ Wâ‚.Localization)
    (Iso.refl _)]
  dsimp
  exact TwoSquare.guitartExact_id' Wâ‚.Q

end LocalizerMorphism

end CategoryTheory
