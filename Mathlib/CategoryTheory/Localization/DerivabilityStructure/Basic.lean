/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Localization.Resolution
import Mathlib.CategoryTheory.Localization.Opposite
import Mathlib.CategoryTheory.GuitartExact.Opposite

/-!
# Derivability structures

Let `Œ¶ : LocalizerMorphism W‚ÇÅ W‚ÇÇ` be a localizer morphism, i.e. `W‚ÇÅ : MorphismProperty C‚ÇÅ`,
`W‚ÇÇ : MorphismProperty C‚ÇÇ`, and `Œ¶.functor : C‚ÇÅ ‚•§ C‚ÇÇ` is a functors which maps `W‚ÇÅ` to `W‚ÇÇ`.
Following the definition introduced by Bruno Kahn and Georges Maltsiniotis in
[Bruno Kahn and Georges Maltsiniotis, *Structures de d√©rivabilit√©*][KahnMaltsiniotis2008],
we say that `Œ¶` is a right derivability structure if `Œ¶` has right resolutions and
the following 2-square is Guitart exact, where `L‚ÇÅ : C‚ÇÅ ‚•§ D‚ÇÅ` and `L‚ÇÇ : C‚ÇÇ ‚•§ D‚ÇÇ` are
localization functors for `W‚ÇÅ` and `W‚ÇÇ`, and `F : D‚ÇÅ ‚•§ D‚ÇÇ` is the induced functor
on the localized categories:

```
    Œ¶.functor
  C‚ÇÅ   ‚•§   C‚ÇÇ
  |         |
L‚ÇÅ|         | L‚ÇÇ
  v         v
  D‚ÇÅ   ‚•§   D‚ÇÇ
       F
```

## Implementation details

In the field `guitartExact'` of the structure `LocalizerMorphism.IsRightDerivabilityStructure`,
The condition that the square is Guitart exact is stated for the localization functors
of the constructed categories (`W‚ÇÅ.Q` and `W‚ÇÇ.Q`).
The lemma `LocalizerMorphism.isRightDerivabilityStructure_iff` show that it does
not depend of the choice of the localization functors.

## TODO

* Construct derived functors using derivability structures
* Construct the injective derivability structure in order to derive functor from
  the bounded below homotopy category in an abelian category with enough injectives
* Construct the projective derivability structure in order to derive functor from
  the bounded above homotopy category in an abelian category with enough projectives
* Construct the flat derivability structure on the bounded above homotopy category
  of categories of modules (and categories of sheaves of modules)
* Define the product derivability structure and formalize derived functors of
  functors in several variables


## References
* [Bruno Kahn and Georges Maltsiniotis, *Structures de d√©rivabilit√©*][KahnMaltsiniotis2008]

-/
universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory

open Category Localization Functor

variable {C‚ÇÅ : Type u‚ÇÅ} {C‚ÇÇ : Type u‚ÇÇ} [Category.{v‚ÇÅ} C‚ÇÅ] [Category.{v‚ÇÇ} C‚ÇÇ]
  {W‚ÇÅ : MorphismProperty C‚ÇÅ} {W‚ÇÇ : MorphismProperty C‚ÇÇ}

namespace LocalizerMorphism

variable (Œ¶ : LocalizerMorphism W‚ÇÅ W‚ÇÇ)

/-- A localizer morphism `Œ¶ : LocalizerMorphism W‚ÇÅ W‚ÇÇ` is a right derivability
structure if it has right resolutions and the 2-square where the left and right functors
are localizations functors for `W‚ÇÅ` and `W‚ÇÇ` are Guitart exact. -/
class IsRightDerivabilityStructure : Prop where
  hasRightResolutions : Œ¶.HasRightResolutions := by infer_instance
  /-- Do not use this field directly: use the more general
  `guitartExact_of_isRightDerivabilityStructure` instead,
  see also the lemma `isRightDerivabilityStructure_iff`. -/
  guitartExact' : TwoSquare.GuitartExact ((Œ¶.catCommSq W‚ÇÅ.Q W‚ÇÇ.Q).iso).hom

attribute [instance] IsRightDerivabilityStructure.hasRightResolutions
  IsRightDerivabilityStructure.guitartExact'

variable {D‚ÇÅ D‚ÇÇ : Type*} [Category D‚ÇÅ] [Category D‚ÇÇ] (L‚ÇÅ : C‚ÇÅ ‚•§ D‚ÇÅ) (L‚ÇÇ : C‚ÇÇ ‚•§ D‚ÇÇ)
  [L‚ÇÅ.IsLocalization W‚ÇÅ] [L‚ÇÇ.IsLocalization W‚ÇÇ] (F : D‚ÇÅ ‚•§ D‚ÇÇ)

lemma isRightDerivabilityStructure_iff [Œ¶.HasRightResolutions] (e : Œ¶.functor ‚ãô L‚ÇÇ ‚âÖ L‚ÇÅ ‚ãô F) :
    Œ¶.IsRightDerivabilityStructure ‚Üî TwoSquare.GuitartExact e.hom := by
  have : Œ¶.IsRightDerivabilityStructure ‚Üî
      TwoSquare.GuitartExact ((Œ¶.catCommSq W‚ÇÅ.Q W‚ÇÇ.Q).iso).hom :=
    ‚ü®fun h ‚Ü¶ h.guitartExact', fun h ‚Ü¶ ‚ü®inferInstance, h‚ü©‚ü©
  rw [this]
  let e' := (Œ¶.catCommSq W‚ÇÅ.Q W‚ÇÇ.Q).iso
  let E‚ÇÅ := Localization.uniq W‚ÇÅ.Q L‚ÇÅ W‚ÇÅ
  let E‚ÇÇ := Localization.uniq W‚ÇÇ.Q L‚ÇÇ W‚ÇÇ
  let e‚ÇÅ : W‚ÇÅ.Q ‚ãô E‚ÇÅ.functor ‚âÖ L‚ÇÅ := compUniqFunctor W‚ÇÅ.Q L‚ÇÅ W‚ÇÅ
  let e‚ÇÇ : W‚ÇÇ.Q ‚ãô E‚ÇÇ.functor ‚âÖ L‚ÇÇ := compUniqFunctor W‚ÇÇ.Q L‚ÇÇ W‚ÇÇ
  let e'' : (Œ¶.functor ‚ãô W‚ÇÇ.Q) ‚ãô E‚ÇÇ.functor ‚âÖ (W‚ÇÅ.Q ‚ãô E‚ÇÅ.functor) ‚ãô F :=
    associator _ _ _ ‚â™‚â´ isoWhiskerLeft _ e‚ÇÇ ‚â™‚â´ e ‚â™‚â´ isoWhiskerRight e‚ÇÅ.symm F
  let e''' : Œ¶.localizedFunctor W‚ÇÅ.Q W‚ÇÇ.Q ‚ãô E‚ÇÇ.functor ‚âÖ E‚ÇÅ.functor ‚ãô F :=
    liftNatIso W‚ÇÅ.Q W‚ÇÅ _ _ _ _ e''
  have : TwoSquare.vComp' e'.hom e'''.hom e‚ÇÅ e‚ÇÇ = e.hom := by
    ext X‚ÇÅ
    rw [TwoSquare.vComp'_app, liftNatIso_hom, liftNatTrans_app]
    simp only [Functor.comp_obj, Iso.trans_hom, isoWhiskerLeft_hom, isoWhiskerRight_hom,
      Iso.symm_hom, NatTrans.comp_app, Functor.associator_hom_app, whiskerLeft_app,
      whiskerRight_app, id_comp, assoc, e'']
    dsimp [Lifting.iso]
    rw [F.map_id, id_comp, ‚Üê F.map_comp, Iso.inv_hom_id_app, F.map_id, comp_id,
      ‚Üê Functor.map_comp_assoc]
    erw [show (CatCommSq.iso Œ¶.functor W‚ÇÅ.Q W‚ÇÇ.Q (localizedFunctor Œ¶ W‚ÇÅ.Q W‚ÇÇ.Q)).hom =
      (Lifting.iso W‚ÇÅ.Q W‚ÇÅ _ _).inv by rfl, Iso.inv_hom_id_app]
    simp
  rw [‚Üê TwoSquare.GuitartExact.vComp'_iff_of_equivalences e'.hom E‚ÇÅ E‚ÇÇ e''' e‚ÇÅ e‚ÇÇ, this]

lemma guitartExact_of_isRightDerivabilityStructure' [h : Œ¶.IsRightDerivabilityStructure]
    (e : Œ¶.functor ‚ãô L‚ÇÇ ‚âÖ L‚ÇÅ ‚ãô F) : TwoSquare.GuitartExact e.hom := by
  simpa only [Œ¶.isRightDerivabilityStructure_iff L‚ÇÅ L‚ÇÇ F e] using h

lemma guitartExact_of_isRightDerivabilityStructure [Œ¶.IsRightDerivabilityStructure] :
    TwoSquare.GuitartExact ((Œ¶.catCommSq L‚ÇÅ L‚ÇÇ).iso).hom :=
  guitartExact_of_isRightDerivabilityStructure' _ _ _ _ _

instance [W‚ÇÅ.ContainsIdentities] : (LocalizerMorphism.id W‚ÇÅ).HasRightResolutions :=
  fun X‚ÇÇ ‚Ü¶ ‚ü®RightResolution.mk (ùüô X‚ÇÇ) (W‚ÇÅ.id_mem X‚ÇÇ)‚ü©

instance [W‚ÇÅ.ContainsIdentities] : (LocalizerMorphism.id W‚ÇÅ).IsRightDerivabilityStructure := by
  rw [(LocalizerMorphism.id W‚ÇÅ).isRightDerivabilityStructure_iff W‚ÇÅ.Q W‚ÇÅ.Q (ùü≠ W‚ÇÅ.Localization)
    (Iso.refl _)]
  dsimp
  exact TwoSquare.guitartExact_id W‚ÇÅ.Q

/-- A localizer morphism `Œ¶ : LocalizerMorphism W‚ÇÅ W‚ÇÇ` is a left derivability
structure if it has left resolutions and the 2-square where the top and bottom functors
are localizations functors for `W‚ÇÅ` and `W‚ÇÇ` is Guitart exact. -/
class IsLeftDerivabilityStructure : Prop where
  hasLeftResolutions : Œ¶.HasLeftResolutions := by infer_instance
  /-- Do not use this field directly: use the more general
  `guitartExact_of_isLeftDerivabilityStructure` instead,
  see also the lemma `isLeftDerivabilityStructure_iff`. -/
  guitartExact' : TwoSquare.GuitartExact ((Œ¶.catCommSq W‚ÇÅ.Q W‚ÇÇ.Q).iso).inv

attribute [instance] IsLeftDerivabilityStructure.hasLeftResolutions
  IsLeftDerivabilityStructure.guitartExact'

lemma isLeftDerivabilityStructure_iff_op :
    Œ¶.IsLeftDerivabilityStructure ‚Üî
      Œ¶.op.IsRightDerivabilityStructure := by
  let F := Œ¶.localizedFunctor W‚ÇÅ.Q W‚ÇÇ.Q
  let e : Œ¶.functor ‚ãô W‚ÇÇ.Q ‚âÖ W‚ÇÅ.Q ‚ãô F := (Œ¶.catCommSq W‚ÇÅ.Q W‚ÇÇ.Q).iso
  let e' : Œ¶.functor.op ‚ãô W‚ÇÇ.Q.op ‚âÖ W‚ÇÅ.Q.op ‚ãô F.op := NatIso.op e.symm
  have eq : TwoSquare.GuitartExact e'.hom ‚Üî TwoSquare.GuitartExact e.inv :=
    TwoSquare.guitartExact_op_iff _
  constructor
  ¬∑ rintro ‚ü®_, _‚ü©
    rwa [Œ¶.op.isRightDerivabilityStructure_iff _ _ _ e', eq]
  ¬∑ intro
    have : Œ¶.HasLeftResolutions := by
      rw [hasLeftResolutions_iff_op]
      infer_instance
    refine ‚ü®inferInstance, ?_‚ü©
    rw [‚Üê eq]
    exact Œ¶.op.guitartExact_of_isRightDerivabilityStructure' _ _ _ e'

lemma isLeftDerivabilityStructure_iff [Œ¶.HasLeftResolutions] (e : Œ¶.functor ‚ãô L‚ÇÇ ‚âÖ L‚ÇÅ ‚ãô F) :
    Œ¶.IsLeftDerivabilityStructure ‚Üî TwoSquare.GuitartExact e.inv := by
  rw [isLeftDerivabilityStructure_iff_op,
    Œ¶.op.isRightDerivabilityStructure_iff L‚ÇÅ.op L‚ÇÇ.op F.op (NatIso.op e.symm),
    ‚Üê TwoSquare.guitartExact_op_iff e.inv]
  rfl

lemma guitartExact_of_isLeftDerivabilityStructure' [h : Œ¶.IsLeftDerivabilityStructure]
    (e : Œ¶.functor ‚ãô L‚ÇÇ ‚âÖ L‚ÇÅ ‚ãô F) : TwoSquare.GuitartExact e.inv := by
  simpa only [Œ¶.isLeftDerivabilityStructure_iff L‚ÇÅ L‚ÇÇ F e] using h

lemma guitartExact_of_isLeftDerivabilityStructure [Œ¶.IsLeftDerivabilityStructure] :
    TwoSquare.GuitartExact ((Œ¶.catCommSq L‚ÇÅ L‚ÇÇ).iso).inv :=
  guitartExact_of_isLeftDerivabilityStructure' _ _ _ _ _

instance [W‚ÇÅ.ContainsIdentities] : (LocalizerMorphism.id W‚ÇÅ).HasLeftResolutions :=
  fun X‚ÇÇ ‚Ü¶ ‚ü®LeftResolution.mk (ùüô X‚ÇÇ) (W‚ÇÅ.id_mem X‚ÇÇ)‚ü©

instance [W‚ÇÅ.ContainsIdentities] : (LocalizerMorphism.id W‚ÇÅ).IsLeftDerivabilityStructure := by
  rw [(LocalizerMorphism.id W‚ÇÅ).isLeftDerivabilityStructure_iff W‚ÇÅ.Q W‚ÇÅ.Q (ùü≠ W‚ÇÅ.Localization)
    (Iso.refl _)]
  dsimp
  exact TwoSquare.guitartExact_id' W‚ÇÅ.Q

end LocalizerMorphism

end CategoryTheory
