/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou, Dagur Asgeirsson
-/
import Mathlib.CategoryTheory.Localization.Trifunctor
import Mathlib.CategoryTheory.Monoidal.Functor

/-!
# Localization of monoidal categories

Let `C` be a monoidal category equipped with a class of morphisms `W` which
is compatible with the monoidal category structure: this means `W`
is multiplicative and stable by left and right whiskerings (this is
the type class `W.IsMonoidal`). Let `L : C â¥¤ D` be a localization functor
for `W`. In the file, we construct a monoidal category structure
on `D` such that the localization functor is monoidal. The structure
is actually defined on a type synonym `LocalizedMonoidal L W Îµ`.
Here, the data `Îµ : L.obj (ğŸ™_ C) â‰… unit` is an isomorphism to some
object `unit : D` which allows the user to provide a preferred choice
of a unit object.

-/

namespace CategoryTheory

open Category MonoidalCategory

variable {C D : Type*} [Category C] [Category D] (L : C â¥¤ D) (W : MorphismProperty C)
  [MonoidalCategory C]

namespace MorphismProperty

/-- A class of morphisms `W` in a monoidal category is monoidal if it is multiplicative
and stable under left and right whiskering. Under this condition, the localized
category can be equipped with a monoidal category structure, see `LocalizedMonoidal`. -/
class IsMonoidal : Prop extends W.IsMultiplicative where
  whiskerLeft (X : C) {Yâ‚ Yâ‚‚ : C} (g : Yâ‚ âŸ¶ Yâ‚‚) (hg : W g) : W (X â— g)
  whiskerRight {Xâ‚ Xâ‚‚ : C} (f : Xâ‚ âŸ¶ Xâ‚‚) (hf : W f) (Y : C) : W (f â–· Y)

variable [W.IsMonoidal]

lemma whiskerLeft_mem (X : C) {Yâ‚ Yâ‚‚ : C} (g : Yâ‚ âŸ¶ Yâ‚‚) (hg : W g) : W (X â— g) :=
  IsMonoidal.whiskerLeft _ _ hg

lemma whiskerRight_mem {Xâ‚ Xâ‚‚ : C} (f : Xâ‚ âŸ¶ Xâ‚‚) (hf : W f) (Y : C) : W (f â–· Y) :=
  IsMonoidal.whiskerRight _ hf Y

lemma tensorHom_mem {Xâ‚ Xâ‚‚ : C} (f : Xâ‚ âŸ¶ Xâ‚‚) {Yâ‚ Yâ‚‚ : C} (g : Yâ‚ âŸ¶ Yâ‚‚)
    (hf : W f) (hg : W g) : W (f âŠ—â‚˜ g) := by
  rw [tensorHom_def]
  exact comp_mem _ _ _ (whiskerRight_mem _ _ hf _) (whiskerLeft_mem _ _ _ hg)

end MorphismProperty

/-- Given a monoidal category `C`, a localization functor `L : C â¥¤ D` with respect
to `W : MorphismProperty C` which satisfies `W.IsMonoidal`, and a choice
of object `unit : D` with an isomorphism `L.obj (ğŸ™_ C) â‰… unit`, this is a
type synonym for `D` on which we define the localized monoidal category structure. -/
@[nolint unusedArguments]
def LocalizedMonoidal (L : C â¥¤ D) (W : MorphismProperty C)
    [W.IsMonoidal] [L.IsLocalization W] {unit : D} (_ : L.obj (ğŸ™_ C) â‰… unit) :=
  D

variable [W.IsMonoidal] [L.IsLocalization W] {unit : D} (Îµ : L.obj (ğŸ™_ C) â‰… unit)

namespace Localization

instance : Category (LocalizedMonoidal L W Îµ) :=
  inferInstanceAs (Category D)

namespace Monoidal

/-- The monoidal functor from a monoidal category `C` to
its localization `LocalizedMonoidal L W Îµ`. -/
def toMonoidalCategory : C â¥¤ LocalizedMonoidal L W Îµ := L

/-- The isomorphism `Îµ : L.obj (ğŸ™_ C) â‰… unit`,
as `(toMonoidalCategory L W Îµ).obj (ğŸ™_ C) â‰… unit`. -/
abbrev Îµ' : (toMonoidalCategory L W Îµ).obj (ğŸ™_ C) â‰… unit := Îµ

local notation "L'" => toMonoidalCategory L W Îµ

instance : (L').IsLocalization W := inferInstanceAs (L.IsLocalization W)

lemma isInvertedByâ‚‚ :
    MorphismProperty.IsInvertedByâ‚‚ W W
      (curriedTensor C â‹™ (Functor.whiskeringRight C C D).obj L') := by
  rintro âŸ¨Xâ‚, Yâ‚âŸ© âŸ¨Xâ‚‚, Yâ‚‚âŸ© âŸ¨fâ‚, fâ‚‚âŸ© âŸ¨hfâ‚, hfâ‚‚âŸ©
  have := Localization.inverts L' W _ (W.whiskerRight_mem fâ‚ hfâ‚ Yâ‚)
  have := Localization.inverts L' W _ (W.whiskerLeft_mem Xâ‚‚ fâ‚‚ hfâ‚‚)
  dsimp
  infer_instance

/-- The localized tensor product, as a bifunctor. -/
noncomputable def tensorBifunctor :
    LocalizedMonoidal L W Îµ â¥¤ LocalizedMonoidal L W Îµ â¥¤ LocalizedMonoidal L W Îµ :=
  Localization.liftâ‚‚ _ (isInvertedByâ‚‚ L W Îµ) L L

noncomputable instance : Liftingâ‚‚ L' L' W W (curriedTensor C â‹™ (Functor.whiskeringRight C C
    (LocalizedMonoidal L W Îµ)).obj L') (tensorBifunctor L W Îµ) :=
  inferInstanceAs (Liftingâ‚‚ L L W W (curriedTensor C â‹™ (Functor.whiskeringRight C C D).obj L')
    (Localization.liftâ‚‚ _ (isInvertedByâ‚‚ L W Îµ) L L))

/-- The bifunctor `tensorBifunctor` on `LocalizedMonoidal L W Îµ` is induced by
`curriedTensor C`. -/
noncomputable abbrev tensorBifunctorIso :
    (((Functor.whiskeringLeftâ‚‚ D).obj L').obj L').obj (tensorBifunctor L W Îµ) â‰…
      (Functor.postcomposeâ‚‚.obj L').obj (curriedTensor C) :=
  Liftingâ‚‚.iso L' L' W W (curriedTensor C â‹™ (Functor.whiskeringRight C C
    (LocalizedMonoidal L W Îµ)).obj L') (tensorBifunctor L W Îµ)

noncomputable instance (X : C) :
    Lifting L' W (tensorLeft X â‹™ L') ((tensorBifunctor L W Îµ).obj ((L').obj X)) := by
  apply Liftingâ‚‚.liftingLiftâ‚‚ (hF := isInvertedByâ‚‚ L W Îµ)

noncomputable instance (Y : C) :
    Lifting L' W (tensorRight Y â‹™ L') ((tensorBifunctor L W Îµ).flip.obj ((L').obj Y)) := by
  apply Liftingâ‚‚.liftingLiftâ‚‚Flip (hF := isInvertedByâ‚‚ L W Îµ)

/-- The left unitor in the localized monoidal category `LocalizedMonoidal L W Îµ`. -/
noncomputable def leftUnitor : (tensorBifunctor L W Îµ).obj unit â‰… ğŸ­ _ :=
  (tensorBifunctor L W Îµ).mapIso Îµ.symm â‰ªâ‰«
    Localization.liftNatIso L' W (tensorLeft (ğŸ™_ C) â‹™ L') L'
      ((tensorBifunctor L W Îµ).obj ((L').obj (ğŸ™_ _))) _
        (Functor.isoWhiskerRight (leftUnitorNatIso C) _ â‰ªâ‰« L.leftUnitor)

/-- The right unitor in the localized monoidal category `LocalizedMonoidal L W Îµ`. -/
noncomputable def rightUnitor : (tensorBifunctor L W Îµ).flip.obj unit â‰… ğŸ­ _ :=
  (tensorBifunctor L W Îµ).flip.mapIso Îµ.symm â‰ªâ‰«
    Localization.liftNatIso L' W (tensorRight (ğŸ™_ C) â‹™ L') L'
      ((tensorBifunctor L W Îµ).flip.obj ((L').obj (ğŸ™_ _))) _
        (Functor.isoWhiskerRight (rightUnitorNatIso C) _ â‰ªâ‰« L.leftUnitor)

/-- The associator in the localized monoidal category `LocalizedMonoidal L W Îµ`. -/
noncomputable def associator :
    bifunctorCompâ‚â‚‚ (tensorBifunctor L W Îµ) (tensorBifunctor L W Îµ) â‰…
      bifunctorCompâ‚‚â‚ƒ (tensorBifunctor L W Îµ) (tensorBifunctor L W Îµ) :=
  Localization.associator L' L' L' L' L' L' W W W W W
    (curriedAssociatorNatIso C) (tensorBifunctor L W Îµ) (tensorBifunctor L W Îµ)
    (tensorBifunctor L W Îµ) (tensorBifunctor L W Îµ)

noncomputable instance monoidalCategoryStruct :
    MonoidalCategoryStruct (LocalizedMonoidal L W Îµ) where
  tensorObj X Y := ((tensorBifunctor L W Îµ).obj X).obj Y
  whiskerLeft X _ _ g := ((tensorBifunctor L W Îµ).obj X).map g
  whiskerRight f Y := ((tensorBifunctor L W Îµ).map f).app Y
  tensorUnit := unit
  associator X Y Z := (((associator L W Îµ).app X).app Y).app Z
  leftUnitor Y := (leftUnitor L W Îµ).app Y
  rightUnitor X := (rightUnitor L W Îµ).app X

/-- The compatibility isomorphism of the monoidal functor `toMonoidalCategory L W Îµ`
with respect to the tensor product. -/
noncomputable def Î¼ (X Y : C) : (L').obj X âŠ— (L').obj Y â‰… (L').obj (X âŠ— Y) :=
  ((tensorBifunctorIso L W Îµ).app X).app Y

@[reassoc (attr := simp)]
lemma Î¼_natural_left {Xâ‚ Xâ‚‚ : C} (f : Xâ‚ âŸ¶ Xâ‚‚) (Y : C) :
    (L').map f â–· (L').obj Y â‰« (Î¼ L W Îµ Xâ‚‚ Y).hom =
      (Î¼ L W Îµ Xâ‚ Y).hom â‰« (L').map (f â–· Y) :=
  NatTrans.naturality_app (tensorBifunctorIso L W Îµ).hom Y f

@[reassoc (attr := simp)]
lemma Î¼_inv_natural_left {Xâ‚ Xâ‚‚ : C} (f : Xâ‚ âŸ¶ Xâ‚‚) (Y : C) :
    (Î¼ L W Îµ Xâ‚ Y).inv â‰« (L').map f â–· (L').obj Y =
      (L').map (f â–· Y) â‰« (Î¼ L W Îµ Xâ‚‚ Y).inv := by
  simp [Iso.eq_comp_inv]

@[reassoc (attr := simp)]
lemma Î¼_natural_right (X : C) {Yâ‚ Yâ‚‚ : C} (g : Yâ‚ âŸ¶ Yâ‚‚) :
    (L').obj X â— (L').map g â‰« (Î¼ L W Îµ X Yâ‚‚).hom =
      (Î¼ L W Îµ X Yâ‚).hom â‰« (L').map (X â— g) :=
  ((tensorBifunctorIso L W Îµ).hom.app X).naturality g

@[reassoc (attr := simp)]
lemma Î¼_inv_natural_right (X : C) {Yâ‚ Yâ‚‚ : C} (g : Yâ‚ âŸ¶ Yâ‚‚) :
    (Î¼ L W Îµ X Yâ‚).inv â‰« (L').obj X â— (L').map g =
      (L').map (X â— g) â‰« (Î¼ L W Îµ X Yâ‚‚).inv := by
  simp [Iso.eq_comp_inv]

lemma leftUnitor_hom_app (Y : C) :
    (Î»_ ((L').obj Y)).hom =
      (Îµ' L W Îµ).inv â–· (L').obj Y â‰« (Î¼ _ _ _ _ _).hom â‰« (L').map (Î»_ Y).hom := by
  dsimp [monoidalCategoryStruct, leftUnitor]
  rw [liftNatTrans_app]
  dsimp
  rw [assoc]
  change _ â‰« (Î¼ L W Îµ  _ _).hom â‰« _ â‰« ğŸ™ _ â‰« ğŸ™ _ = _
  simp only [comp_id]

lemma rightUnitor_hom_app (X : C) :
    (Ï_ ((L').obj X)).hom =
      (L').obj X â— (Îµ' L W Îµ).inv â‰« (Î¼ _ _ _ _ _).hom â‰«
        (L').map (Ï_ X).hom := by
  dsimp [monoidalCategoryStruct, rightUnitor]
  rw [liftNatTrans_app]
  dsimp
  rw [assoc]
  change _ â‰« (Î¼ L W Îµ  _ _).hom â‰« _ â‰« ğŸ™ _ â‰« ğŸ™ _ = _
  simp only [comp_id]

lemma associator_hom_app (Xâ‚ Xâ‚‚ Xâ‚ƒ : C) :
    (Î±_ ((L').obj Xâ‚) ((L').obj Xâ‚‚) ((L').obj Xâ‚ƒ)).hom =
      ((Î¼ L W Îµ _ _).hom âŠ—â‚˜ ğŸ™ _) â‰« (Î¼ L W Îµ _ _).hom â‰« (L').map (Î±_ Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰«
        (Î¼ L W Îµ  _ _).inv â‰« (ğŸ™ _ âŠ—â‚˜ (Î¼ L W Îµ  _ _).inv) := by
  dsimp [monoidalCategoryStruct, associator]
  simp only [Functor.map_id, comp_id, NatTrans.id_app, id_comp]
  rw [Localization.associator_hom_app_app_app]
  rfl

lemma id_tensorHom (X : LocalizedMonoidal L W Îµ) {Yâ‚ Yâ‚‚ : LocalizedMonoidal L W Îµ} (f : Yâ‚ âŸ¶ Yâ‚‚) :
    ğŸ™ X âŠ—â‚˜ f = X â— f := by
  simp [monoidalCategoryStruct]

lemma tensorHom_id {Xâ‚ Xâ‚‚ : LocalizedMonoidal L W Îµ} (f : Xâ‚ âŸ¶ Xâ‚‚) (Y : LocalizedMonoidal L W Îµ) :
    f âŠ—â‚˜ ğŸ™ Y = f â–· Y := by
  simp [monoidalCategoryStruct]

@[reassoc]
lemma tensor_comp {Xâ‚ Yâ‚ Zâ‚ Xâ‚‚ Yâ‚‚ Zâ‚‚ : LocalizedMonoidal L W Îµ}
    (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (gâ‚ : Yâ‚ âŸ¶ Zâ‚) (gâ‚‚ : Yâ‚‚ âŸ¶ Zâ‚‚) :
    (fâ‚ â‰« gâ‚) âŠ—â‚˜ (fâ‚‚ â‰« gâ‚‚) = (fâ‚ âŠ—â‚˜ fâ‚‚) â‰« (gâ‚ âŠ—â‚˜ gâ‚‚) := by
  simp [monoidalCategoryStruct]

lemma id_tensorHom_id (Xâ‚ Xâ‚‚ : LocalizedMonoidal L W Îµ) : ğŸ™ Xâ‚ âŠ—â‚˜ ğŸ™ Xâ‚‚ = ğŸ™ (Xâ‚ âŠ— Xâ‚‚) := by
  simp [monoidalCategoryStruct]

@[deprecated (since := "2025-07-14")] alias tensor_id := id_tensorHom_id

@[reassoc]
theorem whiskerLeft_comp (Q : LocalizedMonoidal L W Îµ) {X Y Z : LocalizedMonoidal L W Îµ}
    (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    Q â— (f â‰« g) = Q â— f â‰« Q â— g := by
  simp only [â† id_tensorHom, â† tensor_comp, comp_id]

@[reassoc]
theorem whiskerRight_comp (Q : LocalizedMonoidal L W Îµ) {X Y Z : LocalizedMonoidal L W Îµ}
    (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    (f â‰« g) â–· Q = f â–· Q â‰« g â–· Q := by
  simp only [â† tensorHom_id, â† tensor_comp, comp_id]

lemma whiskerLeft_id (X Y : LocalizedMonoidal L W Îµ) :
    X â— (ğŸ™ Y) = ğŸ™ _ := by simp [monoidalCategoryStruct]

lemma whiskerRight_id (X Y : LocalizedMonoidal L W Îµ) :
    (ğŸ™ X) â–· Y = ğŸ™ _ := by simp [monoidalCategoryStruct]

@[reassoc]
lemma whisker_exchange {Q X Y Z : LocalizedMonoidal L W Îµ} (f : Q âŸ¶ X) (g : Y âŸ¶ Z) :
    Q â— g â‰« f â–· Z = f â–· Y â‰« X â— g := by
  simp only [â† id_tensorHom, â† tensorHom_id, â† tensor_comp, id_comp, comp_id]

@[reassoc]
lemma associator_naturality {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ : LocalizedMonoidal L W Îµ}
    (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ) :
    ((fâ‚ âŠ—â‚˜ fâ‚‚) âŠ—â‚˜ fâ‚ƒ) â‰« (Î±_ Yâ‚ Yâ‚‚ Yâ‚ƒ).hom = (Î±_ Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« (fâ‚ âŠ—â‚˜ fâ‚‚ âŠ—â‚˜ fâ‚ƒ) := by
  have hâ‚ := (((associator L W Îµ).hom.app Yâ‚).app Yâ‚‚).naturality fâ‚ƒ
  have hâ‚‚ := NatTrans.congr_app (((associator L W Îµ).hom.app Yâ‚).naturality fâ‚‚) Xâ‚ƒ
  have hâ‚ƒ := NatTrans.congr_app (NatTrans.congr_app ((associator L W Îµ).hom.naturality fâ‚) Xâ‚‚) Xâ‚ƒ
  simp only [monoidalCategoryStruct, Functor.map_comp, assoc]
  dsimp at hâ‚ hâ‚‚ hâ‚ƒ âŠ¢
  rw [hâ‚, assoc, reassoc_of% hâ‚‚, reassoc_of% hâ‚ƒ]

@[reassoc]
lemma associator_naturalityâ‚ {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ : LocalizedMonoidal L W Îµ} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) :
    ((fâ‚ â–· Xâ‚‚) â–· Xâ‚ƒ) â‰« (Î±_ Yâ‚ Xâ‚‚ Xâ‚ƒ).hom = (Î±_ Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« (fâ‚ â–· (Xâ‚‚ âŠ— Xâ‚ƒ)) := by
  simp only [â† tensorHom_id, associator_naturality, id_tensorHom_id]

@[reassoc]
lemma associator_naturalityâ‚‚ {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚‚ : LocalizedMonoidal L W Îµ} (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) :
    ((Xâ‚ â— fâ‚‚) â–· Xâ‚ƒ) â‰« (Î±_ Xâ‚ Yâ‚‚ Xâ‚ƒ).hom = (Î±_ Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« (Xâ‚ â— (fâ‚‚ â–· Xâ‚ƒ)) := by
  simp only [â† tensorHom_id, â† id_tensorHom, associator_naturality]

@[reassoc]
lemma associator_naturalityâ‚ƒ {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ƒ : LocalizedMonoidal L W Îµ} (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ) :
    ((Xâ‚ âŠ— Xâ‚‚) â— fâ‚ƒ) â‰« (Î±_ Xâ‚ Xâ‚‚ Yâ‚ƒ).hom = (Î±_ Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« (Xâ‚ â— (Xâ‚‚ â— fâ‚ƒ)) := by
  simp only [â† id_tensorHom, â† id_tensorHom_id, associator_naturality]

lemma pentagon_auxâ‚ {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ : LocalizedMonoidal L W Îµ} (i : Xâ‚ â‰… Yâ‚) :
    ((i.hom â–· Xâ‚‚) â–· Xâ‚ƒ) â‰« (Î±_ Yâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« (i.inv â–· (Xâ‚‚ âŠ— Xâ‚ƒ)) = (Î±_ Xâ‚ Xâ‚‚ Xâ‚ƒ).hom := by
  simp only [associator_naturalityâ‚_assoc, â† whiskerRight_comp,
    Iso.hom_inv_id, whiskerRight_id, comp_id]

lemma pentagon_auxâ‚‚ {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚‚ : LocalizedMonoidal L W Îµ} (i : Xâ‚‚ â‰… Yâ‚‚) :
    ((Xâ‚ â— i.hom) â–· Xâ‚ƒ) â‰« (Î±_ Xâ‚ Yâ‚‚ Xâ‚ƒ).hom â‰« (Xâ‚ â— (i.inv â–· Xâ‚ƒ)) = (Î±_ Xâ‚ Xâ‚‚ Xâ‚ƒ).hom := by
  simp only [associator_naturalityâ‚‚_assoc, â† whiskerLeft_comp, â† whiskerRight_comp,
    Iso.hom_inv_id, whiskerRight_id, whiskerLeft_id, comp_id]

lemma pentagon_auxâ‚ƒ {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ƒ : LocalizedMonoidal L W Îµ} (i : Xâ‚ƒ â‰… Yâ‚ƒ) :
    ((Xâ‚ âŠ— Xâ‚‚) â— i.hom) â‰« (Î±_ Xâ‚ Xâ‚‚ Yâ‚ƒ).hom â‰« (Xâ‚ â— (Xâ‚‚ â— i.inv)) = (Î±_ Xâ‚ Xâ‚‚ Xâ‚ƒ).hom := by
  simp only [associator_naturalityâ‚ƒ_assoc, â† whiskerLeft_comp,
    Iso.hom_inv_id, whiskerLeft_id, comp_id]

instance : (L').EssSurj := Localization.essSurj L' W

variable {L W Îµ} in
lemma pentagon (Yâ‚ Yâ‚‚ Yâ‚ƒ Yâ‚„ : LocalizedMonoidal L W Îµ) :
    Pentagon Yâ‚ Yâ‚‚ Yâ‚ƒ Yâ‚„ := by
  obtain âŸ¨Xâ‚, âŸ¨eâ‚âŸ©âŸ© : âˆƒ Xâ‚, Nonempty ((L').obj Xâ‚ â‰… Yâ‚) := âŸ¨_, âŸ¨(L').objObjPreimageIso Yâ‚âŸ©âŸ©
  obtain âŸ¨Xâ‚‚, âŸ¨eâ‚‚âŸ©âŸ© : âˆƒ Xâ‚‚, Nonempty ((L').obj Xâ‚‚ â‰… Yâ‚‚) := âŸ¨_, âŸ¨(L').objObjPreimageIso Yâ‚‚âŸ©âŸ©
  obtain âŸ¨Xâ‚ƒ, âŸ¨eâ‚ƒâŸ©âŸ© : âˆƒ Xâ‚ƒ, Nonempty ((L').obj Xâ‚ƒ â‰… Yâ‚ƒ) := âŸ¨_, âŸ¨(L').objObjPreimageIso Yâ‚ƒâŸ©âŸ©
  obtain âŸ¨Xâ‚„, âŸ¨eâ‚„âŸ©âŸ© : âˆƒ Xâ‚„, Nonempty ((L').obj Xâ‚„ â‰… Yâ‚„) := âŸ¨_, âŸ¨(L').objObjPreimageIso Yâ‚„âŸ©âŸ©
  suffices Pentagon ((L').obj Xâ‚) ((L').obj Xâ‚‚) ((L').obj Xâ‚ƒ) ((L').obj Xâ‚„) by
    dsimp [Pentagon]
    refine Eq.trans ?_ (((((eâ‚.inv âŠ—â‚˜ eâ‚‚.inv) âŠ—â‚˜ eâ‚ƒ.inv) âŠ—â‚˜ eâ‚„.inv) â‰«= this =â‰«
      (eâ‚.hom âŠ—â‚˜ eâ‚‚.hom âŠ—â‚˜ eâ‚ƒ.hom âŠ—â‚˜ eâ‚„.hom)).trans ?_)
    Â· rw [â† id_tensorHom, â† id_tensorHom, â† tensorHom_id, â† tensorHom_id, assoc, assoc,
        â† tensor_comp, â† associator_naturality, id_comp, â† comp_id eâ‚.hom,
        tensor_comp, â† associator_naturality_assoc, â† comp_id (ğŸ™ ((L').obj Xâ‚„)),
        â† tensor_comp_assoc, associator_naturality, comp_id, comp_id,
        â† tensor_comp_assoc, assoc, eâ‚„.inv_hom_id, â† tensor_comp, eâ‚.inv_hom_id,
        â† tensor_comp, eâ‚‚.inv_hom_id, eâ‚ƒ.inv_hom_id, id_tensorHom_id, id_tensorHom_id, comp_id]
    Â· rw [assoc, associator_naturality_assoc, associator_naturality_assoc,
        â† tensor_comp, eâ‚.inv_hom_id, â† tensor_comp, eâ‚‚.inv_hom_id, â† tensor_comp,
        eâ‚ƒ.inv_hom_id, eâ‚„.inv_hom_id, id_tensorHom_id, id_tensorHom_id, id_tensorHom_id, comp_id]
  dsimp [Pentagon]
  have : ((L').obj Xâ‚ â— (Î¼ L W Îµ Xâ‚‚ Xâ‚ƒ).inv) â–· (L').obj Xâ‚„ â‰«
      (Î±_ ((L').obj Xâ‚) ((L').obj Xâ‚‚ âŠ— (L').obj Xâ‚ƒ) ((L').obj Xâ‚„)).hom â‰«
        (L').obj Xâ‚ â— (Î¼ L W Îµ Xâ‚‚ Xâ‚ƒ).hom â–· (L').obj Xâ‚„ =
          (Î±_ ((L').obj Xâ‚) ((L').obj (Xâ‚‚ âŠ— Xâ‚ƒ)) ((L').obj Xâ‚„)).hom :=
    pentagon_auxâ‚‚ _ _ _ (Î¼ L W Îµ Xâ‚‚ Xâ‚ƒ).symm
  rw [associator_hom_app, tensorHom_id, id_tensorHom, associator_hom_app, tensorHom_id,
    whiskerLeft_comp, whiskerRight_comp, whiskerRight_comp, whiskerRight_comp, assoc, assoc,
    assoc, whiskerRight_comp, assoc,
    reassoc_of% this, associator_hom_app, tensorHom_id,
    â† pentagon_auxâ‚ (Xâ‚‚ := (L').obj Xâ‚ƒ) (Xâ‚ƒ := (L').obj Xâ‚„) (i := Î¼ L W Îµ Xâ‚ Xâ‚‚),
    â† pentagon_auxâ‚ƒ (Xâ‚ := (L').obj Xâ‚) (Xâ‚‚ := (L').obj Xâ‚‚) (i := Î¼ L W Îµ Xâ‚ƒ Xâ‚„),
    associator_hom_app, associator_hom_app]
  simp only [assoc, â† whiskerRight_comp_assoc, Iso.inv_hom_id, comp_id, Î¼_natural_left_assoc,
    id_tensorHom, â† whiskerLeft_comp, Iso.inv_hom_id_assoc]
  rw [â† (L').map_comp_assoc, whiskerLeft_comp, Î¼_inv_natural_right_assoc, â† (L').map_comp_assoc]
  simp only [assoc, MonoidalCategory.pentagon, Functor.map_comp, tensorHom_id,
    whiskerRight_comp_assoc]
  congr 3; simp only [â† assoc]; congr
  simp only [â† cancel_mono (Î¼ L W Îµ (Xâ‚ âŠ— Xâ‚‚) (Xâ‚ƒ âŠ— Xâ‚„)).inv, assoc, id_comp,
    whisker_exchange_assoc, â† whiskerRight_comp_assoc,
    Iso.inv_hom_id, whiskerRight_id, â† whiskerLeft_comp,
    whiskerLeft_id]

lemma leftUnitor_naturality {X Y : LocalizedMonoidal L W Îµ} (f : X âŸ¶ Y) :
    ğŸ™_ (LocalizedMonoidal L W Îµ) â— f â‰« (Î»_ Y).hom = (Î»_ X).hom â‰« f := by
  simp [monoidalCategoryStruct]

lemma rightUnitor_naturality {X Y : LocalizedMonoidal L W Îµ} (f : X âŸ¶ Y) :
    f â–· ğŸ™_ (LocalizedMonoidal L W Îµ) â‰« (Ï_ Y).hom = (Ï_ X).hom â‰« f :=
  (rightUnitor L W Îµ).hom.naturality f

@[reassoc]
lemma triangle_auxâ‚ {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ : LocalizedMonoidal L W Îµ}
    (iâ‚ : Xâ‚ â‰… Yâ‚) (iâ‚‚ : Xâ‚‚ â‰… Yâ‚‚) (iâ‚ƒ : Xâ‚ƒ â‰… Yâ‚ƒ) :
    ((iâ‚.hom âŠ—â‚˜ iâ‚‚.hom) âŠ—â‚˜ iâ‚ƒ.hom) â‰« (Î±_ Yâ‚ Yâ‚‚ Yâ‚ƒ).hom â‰« (iâ‚.inv âŠ—â‚˜ iâ‚‚.inv âŠ—â‚˜ iâ‚ƒ.inv) =
      (Î±_ Xâ‚ Xâ‚‚ Xâ‚ƒ).hom := by
  simp only [associator_naturality_assoc, â† tensor_comp, Iso.hom_inv_id, id_tensorHom,
    whiskerLeft_id, comp_id]

lemma triangle_auxâ‚‚ {X Y : LocalizedMonoidal L W Îµ} {X' Y' : C}
    (eâ‚ : (L').obj X' â‰… X) (eâ‚‚ : (L').obj Y' â‰… Y) :
      eâ‚.hom âŠ—â‚˜ (Îµ.hom âŠ—â‚˜ eâ‚‚.hom) â‰« (Î»_ Y).hom =
        (L').obj X' â— ((Îµ' L W Îµ).hom â–· (L').obj Y' â‰«
          ğŸ™_ _ â— eâ‚‚.hom â‰« (Î»_ Y).hom) â‰« eâ‚.hom â–· Y := by
  simp only [â† tensorHom_id, â† id_tensorHom, â† tensor_comp, comp_id, id_comp,
    â† tensor_comp_assoc, id_comp]
  congr 3
  exact (comp_id _).symm

lemma triangle_auxâ‚ƒ {X Y : LocalizedMonoidal L W Îµ} {X' Y' : C}
    (eâ‚ : (L').obj X' â‰… X) (eâ‚‚ : (L').obj Y' â‰… Y) : (Ï_ X).hom â–· _ =
      ((eâ‚.inv âŠ—â‚˜ Îµ.inv) âŠ—â‚˜ eâ‚‚.inv) â‰« _ â— eâ‚‚.hom â‰« ((Î¼ L W Îµ X' (ğŸ™_ C)).hom â‰«
        (L').map (Ï_ X').hom) â–· Y â‰« eâ‚.hom â–· Y := by
  simp only [â† tensorHom_id, â† id_tensorHom, â† tensor_comp, assoc, comp_id,
    id_comp, Iso.inv_hom_id]
  congr
  rw [â† cancel_mono eâ‚.inv, assoc, assoc, assoc, Iso.hom_inv_id, comp_id,
    â† rightUnitor_naturality, rightUnitor_hom_app,
    â† tensorHom_id, â† id_tensorHom, â† tensor_comp_assoc, comp_id, id_comp]

variable {L W Îµ} in
lemma triangle (X Y : LocalizedMonoidal L W Îµ) :
    (Î±_ X (ğŸ™_ _) Y).hom â‰« X â— (Î»_ Y).hom = (Ï_ X).hom â–· Y := by
  obtain âŸ¨X', âŸ¨eâ‚âŸ©âŸ© : âˆƒ Xâ‚, Nonempty ((L').obj Xâ‚ â‰… X) := âŸ¨_, âŸ¨(L').objObjPreimageIso XâŸ©âŸ©
  obtain âŸ¨Y', âŸ¨eâ‚‚âŸ©âŸ© : âˆƒ Xâ‚‚, Nonempty ((L').obj Xâ‚‚ â‰… Y) := âŸ¨_, âŸ¨(L').objObjPreimageIso YâŸ©âŸ©
  have hâ‚ := (associator_hom_app L W Îµ X' (ğŸ™_ _) Y' =â‰«
    (ğŸ™ ((L').obj X') âŠ—â‚˜ (Î¼ L W Îµ (ğŸ™_ C) Y').hom))
  simp only [assoc, id_tensorHom, â† whiskerLeft_comp,
    Iso.inv_hom_id, whiskerLeft_id, comp_id, Iso.inv_hom_id,
    â† cancel_mono (Î¼ L W Îµ X' (ğŸ™_ C âŠ— Y')).hom] at hâ‚
  have hâ‚‚ := (Îµ' L W Îµ).hom â–· (L').obj Y' â‰«= leftUnitor_hom_app L W Îµ Y'
  simp only [â† whiskerRight_comp_assoc, Iso.hom_inv_id, whiskerRight_id, id_comp] at hâ‚‚
  have hâ‚ƒ := (((Î¼ L W Îµ _ _).hom âŠ—â‚˜ ğŸ™ _) â‰« (Î¼ L W Îµ _ _).hom) â‰«=
    ((L').congr_map (MonoidalCategory.triangle X' Y'))
  simp only [assoc, Functor.map_comp, â† reassoc_of% hâ‚] at hâ‚ƒ
  rw [â† Î¼_natural_left, tensorHom_id, â† whiskerRight_comp_assoc,
    â† Î¼_natural_right, â† Iso.comp_inv_eq, assoc, assoc, assoc,
    Iso.hom_inv_id, comp_id, â† whiskerLeft_comp, â† hâ‚‚] at hâ‚ƒ
  replace hâ‚ƒ := ((eâ‚.inv âŠ—â‚˜ Îµ.inv) âŠ—â‚˜ eâ‚‚.inv) â‰«= (hâ‚ƒ =â‰« (_ â— eâ‚‚.hom)) =â‰« (eâ‚.hom â–· _)
  simp only [â† whiskerLeft_comp, assoc, â† leftUnitor_naturality, â† whisker_exchange] at hâ‚ƒ
  have : _ = (Î±_ X (ğŸ™_ (LocalizedMonoidal L W Îµ)) Y).hom :=
    triangle_auxâ‚ _ _ _ eâ‚.symm Îµ.symm eâ‚‚.symm
  simp only [â† this, Iso.symm_hom, Iso.symm_inv, assoc,
    â† id_tensorHom, â† tensor_comp, comp_id]
  convert hâ‚ƒ
  Â· exact triangle_auxâ‚‚ _ _ _ eâ‚ eâ‚‚
  Â· exact triangle_auxâ‚ƒ _ _ _ eâ‚ eâ‚‚

noncomputable instance :
    MonoidalCategory (LocalizedMonoidal L W Îµ) where
  tensorHom_def := by intros; simp [monoidalCategoryStruct]
  id_tensorHom_id := by intros; simp [monoidalCategoryStruct]
  tensorHom_comp_tensorHom := by intros; simp [monoidalCategoryStruct]
  whiskerLeft_id := by intros; simp [monoidalCategoryStruct]
  id_whiskerRight := by intros; simp [monoidalCategoryStruct]
  associator_naturality {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ} fâ‚ fâ‚‚ fâ‚ƒ := by apply associator_naturality
  leftUnitor_naturality := by intros; simp [monoidalCategoryStruct]
  rightUnitor_naturality := fun f â†¦ (rightUnitor L W Îµ).hom.naturality f
  pentagon := pentagon
  triangle := triangle

end Monoidal

end Localization

open Localization.Monoidal

noncomputable instance : (toMonoidalCategory L W Îµ).Monoidal :=
  Functor.CoreMonoidal.toMonoidal
    { ÎµIso := Îµ.symm
      Î¼Iso X Y := Î¼ L W Îµ X Y
      associativity X Y Z := by simp [associator_hom_app L W Îµ X Y Z]
      left_unitality Y := leftUnitor_hom_app L W Îµ Y
      right_unitality X := rightUnitor_hom_app L W Îµ X }

end CategoryTheory
