/-
Copyright (c) 2025 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Localization.Predicate
import Mathlib.CategoryTheory.Quotient

/-!
# Certain quotient categories are localizations

-/

namespace CategoryTheory

variable {C D : Type*} [Category C] [Category D]

namespace Quotient

variable (r : HomRel C) (W : MorphismProperty C)
  (hW : W.IsInvertedBy (functor r))
  (hr : âˆ€ â¦ƒX Y : Câ¦„ (fâ‚€ fâ‚ : X âŸ¶ Y) (_ : r fâ‚€ fâ‚),
    âˆƒ (Cyl : C) (iâ‚€ iâ‚ : X âŸ¶ Cyl) (Ï€ : Cyl âŸ¶ X) (_hÏ€ : W Ï€)
      (_hiâ‚€ : iâ‚€ â‰« Ï€ = ğŸ™ X) (_hiâ‚ : iâ‚ â‰« Ï€ = ğŸ™ X) (Ï† : Cyl âŸ¶ Y), iâ‚€ â‰« Ï† = fâ‚€ âˆ§ iâ‚ â‰« Ï† = fâ‚)

namespace isLocalization_functor

variable {r W} (E : Type*) [Category E]

include hr
def strictUniversalPropertyFixedTarget :
    Localization.StrictUniversalPropertyFixedTarget (functor r) W E where
  inverts := hW
  lift F hF := Quotient.lift r F (fun X Y fâ‚€ fâ‚ hf â†¦ by
    obtain âŸ¨Cyl, iâ‚€, iâ‚, Ï€, hÏ€, hiâ‚€, hiâ‚, Ï†, hÏ†â‚€, hÏ†â‚âŸ©  := hr fâ‚€ fâ‚ hf
    rw [â† hÏ†â‚€, â† hÏ†â‚, Functor.map_comp, Functor.map_comp]
    congr 1
    have := hF _ hÏ€
    rw [â† cancel_mono (F.map Ï€), â† Functor.map_comp, â† Functor.map_comp, hiâ‚€, hiâ‚])
  fac F hF := rfl
  uniq Fâ‚ Fâ‚‚ h := by
    fapply Functor.ext
    Â· rintro âŸ¨XâŸ©
      exact Functor.congr_obj h X
    Â· rintro âŸ¨XâŸ© âŸ¨YâŸ© âŸ¨fâŸ©
      exact Functor.congr_hom h f

end isLocalization_functor

include hW hr in
lemma isLocalization_functor : (functor r).IsLocalization W := by
  apply Functor.IsLocalization.mk'
  all_goals apply isLocalization_functor.strictUniversalPropertyFixedTarget hW hr

end Quotient

namespace Functor

lemma isLocalization_of_essSurj_of_full_of_exists_cylinders
    (L : C â¥¤ D) [L.EssSurj] [L.Full] (W : MorphismProperty C) (hW : W.IsInvertedBy L)
    (hr : âˆ€ â¦ƒX Y : Câ¦„ (fâ‚€ fâ‚ : X âŸ¶ Y) (_ : L.map fâ‚€ = L.map fâ‚),
      âˆƒ (Cyl : C) (iâ‚€ iâ‚ : X âŸ¶ Cyl) (Ï€ : Cyl âŸ¶ X) (_hÏ€ : W Ï€)
        (_hiâ‚€ : iâ‚€ â‰« Ï€ = ğŸ™ X) (_hiâ‚ : iâ‚ â‰« Ï€ = ğŸ™ X) (Ï† : Cyl âŸ¶ Y), iâ‚€ â‰« Ï† = fâ‚€ âˆ§ iâ‚ â‰« Ï† = fâ‚) :
    L.IsLocalization W := by
  let r := L.homRel
  let F : Quotient L.homRel â¥¤ D := Quotient.lift _ L (by simp)
  let iso : Quotient.functor L.homRel â‹™ F â‰… L := Iso.refl _
  have : F.EssSurj := âŸ¨fun Y â†¦ by
    have := essSurj_of_iso iso.symm
    obtain âŸ¨X, âŸ¨eâŸ©âŸ© := (Quotient.functor L.homRel â‹™ F).exists_of_essSurj Y
    exact âŸ¨_, âŸ¨eâŸ©âŸ©âŸ©
  have : F.Full := âŸ¨by
    rintro âŸ¨XâŸ© âŸ¨YâŸ© (f : L.obj X âŸ¶ L.obj Y)
    obtain âŸ¨f, rflâŸ© := L.map_surjective f
    exact âŸ¨(Quotient.functor L.homRel).map f, rflâŸ©âŸ©
  have : F.Faithful := âŸ¨by
    rintro âŸ¨XâŸ© âŸ¨YâŸ© fâ‚ fâ‚‚ hf
    obtain âŸ¨fâ‚, rflâŸ© := (Quotient.functor L.homRel).map_surjective fâ‚
    obtain âŸ¨fâ‚‚, rflâŸ© := (Quotient.functor L.homRel).map_surjective fâ‚‚
    exact Quotient.sound  _ hfâŸ©
  have : F.IsEquivalence := { }
  have hW' : W.IsInvertedBy (Quotient.functor L.homRel) := fun _ _ f hf â†¦ by
    have : IsIso (F.map ((Quotient.functor L.homRel).map f)) := hW _ hf
    apply isIso_of_reflects_iso _ F
  have := Quotient.isLocalization_functor L.homRel W hW' hr
  exact IsLocalization.of_equivalence_target (Quotient.functor L.homRel) W L
    F.asEquivalence iso

end Functor

end CategoryTheory
