/-
Copyright (c) 2025 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.CategoryTheory.Localization.Trifunctor
import Mathlib.CategoryTheory.Monoidal.Multifunctor
import Mathlib.CategoryTheory.Monoidal.NaturalTransformation
import Mathlib.Tactic.CategoryTheory.Coherence

/-!

# Universal property of localized monoidal categories

This file proves that, given a monoidal localization functor `L : C ‚•§ D`, and a functor
`F : D ‚•§ E` to a monoidal category, such that `L ‚ãô F` is monoidal, then `F` is monoidal. See
`CategoryTheory.Localization.Monoidal.functorMonoidalOfComp`.
-/

universe u

namespace CategoryTheory

open CategoryTheory MonoidalCategory Functor Monoidal LaxMonoidal OplaxMonoidal

namespace Localization.Monoidal

variable {C D E : Type*} [Category C] [Category D] [Category E]
  [MonoidalCategory C] [MonoidalCategory D] [MonoidalCategory E]
  (L : C ‚•§ D) (W : MorphismProperty C) [L.IsLocalization W] [L.Monoidal]
  (F : D ‚•§ E) (G : C ‚•§ E) [G.Monoidal] [W.ContainsIdentities] [Lifting L W G F]

@[simps]
instance lifting‚ÇÇCurriedTensorPre :
    Lifting‚ÇÇ L L W W (curriedTensorPre G) (curriedTensorPre F) where
  iso := curriedTensorPreFunctor.mapIso (Lifting.iso L W G F)

@[simps]
instance lifting‚ÇÇCurriedTensorPost :
    Lifting‚ÇÇ L L W W (curriedTensorPost G) (curriedTensorPost F) where
  iso := (postcompose‚ÇÇ.obj F).mapIso (curriedTensorPreIsoPost L) ‚â™‚â´
    curriedTensorPostFunctor.mapIso (Lifting.iso L W G F)

/--
The natural isomorphism of bifunctors `F - ‚äó F - ‚âÖ F (- ‚äó -)`, given that `L ‚ãô F` is monoidal.
-/
noncomputable def curriedTensorPreIsoPost : curriedTensorPre F ‚âÖ curriedTensorPost F :=
  lift‚ÇÇNatIso L L W W (curriedTensorPre G) (curriedTensorPost G) _ _
    (Functor.curriedTensorPreIsoPost G)

@[reassoc]
noncomputable def curriedTensorPreIsoPost_hom_app_app (X‚ÇÅ X‚ÇÇ : C) :
    letI e := Lifting.iso L W G F
    ((curriedTensorPreIsoPost L W F G).hom.app (L.obj X‚ÇÅ)).app (L.obj X‚ÇÇ) =
      (e.hom.app _ ‚äó‚Çò e.hom.app _) ‚â´ LaxMonoidal.Œº G X‚ÇÅ X‚ÇÇ ‚â´ e.inv.app _ ‚â´
        F.map (OplaxMonoidal.Œ¥ L _ _) := by
  simp [curriedTensorPreIsoPost]


lemma curriedTensorPreIsoPost_hom_app_app' {X‚ÇÅ X‚ÇÇ : C} {Y‚ÇÅ Y‚ÇÇ : D}
    (e‚ÇÅ : Y‚ÇÅ ‚âÖ L.obj X‚ÇÅ) (e‚ÇÇ : Y‚ÇÇ ‚âÖ L.obj X‚ÇÇ) :
    letI e := Lifting.iso L W G F
    ((curriedTensorPreIsoPost L W F G).hom.app Y‚ÇÅ).app Y‚ÇÇ =
      ((F.map e‚ÇÅ.hom ‚â´ e.hom.app _) ‚äó‚Çò (F.map e‚ÇÇ.hom ‚â´ e.hom.app _)) ‚â´
        LaxMonoidal.Œº G X‚ÇÅ X‚ÇÇ ‚â´ e.inv.app _ ‚â´
        F.map (OplaxMonoidal.Œ¥ L _ _‚â´ (e‚ÇÅ.inv ‚äó‚Çò e‚ÇÇ.inv)) := by
  have h‚ÇÅ := ((curriedTensorPreIsoPost L W F G).hom.app Y‚ÇÅ).naturality e‚ÇÇ.hom
  have h‚ÇÇ := congr_app ((curriedTensorPreIsoPost L W F G).hom.naturality e‚ÇÅ.hom)
  dsimp at h‚ÇÅ h‚ÇÇ ‚ä¢
  rw [‚Üê cancel_mono (F.map (Y‚ÇÅ ‚óÅ e‚ÇÇ.hom)), ‚Üê h‚ÇÅ, ‚Üê cancel_mono (F.map (e‚ÇÅ.hom ‚ñ∑ L.obj X‚ÇÇ)),
    Category.assoc, ‚Üê h‚ÇÇ, curriedTensorPreIsoPost_hom_app_app, Category.assoc, Category.assoc,
    Category.assoc, Category.assoc, ‚Üê tensorHom_def'_assoc, tensorHom_comp_tensorHom_assoc,
    ‚Üê Functor.map_comp, ‚Üê tensorHom_def', ‚Üê Functor.map_comp, Category.assoc,
    tensorHom_comp_tensorHom, Iso.inv_hom_id, Iso.inv_hom_id, tensorHom_id, id_whiskerRight,
    Category.comp_id]

/--
Monoidal structure on `F`, given that `L ‚ãô F` is monoidal, where `L` is a localization functor.
-/
@[simps!]
noncomputable def functorCoreMonoidalOfComp : F.CoreMonoidal := by
  letI e := Lifting.iso L W G F
  refine Functor.CoreMonoidal.ofBifunctor
    (ŒµIso G ‚â™‚â´ e.symm.app _ ‚â™‚â´ F.mapIso (ŒµIso L).symm) (curriedTensorPreIsoPost L W F G) ?_ ?_ ?_
  ¬∑ refine natTrans‚ÇÉ_ext L L L W W W (fun X‚ÇÅ X‚ÇÇ X‚ÇÉ ‚Ü¶ ?_)
    dsimp
    rw [curriedTensorPreIsoPost_hom_app_app, curriedTensorPreIsoPost_hom_app_app,
      curriedTensorPreIsoPost_hom_app_app' L W F G (ŒºIso L _ _) (Iso.refl _),
      curriedTensorPreIsoPost_hom_app_app' L W F G (Iso.refl _) (ŒºIso L _ _)]
    simp only [comp_whiskerRight, map_comp, Category.assoc,
      MonoidalCategory.whiskerLeft_comp]
    rw [tensorHom_def', tensorHom_def (e.hom.app X‚ÇÇ)]
    have e‚ÇÅ :
        F.map (Œ¥ L X‚ÇÅ X‚ÇÇ) ‚ñ∑ F.obj (L.obj X‚ÇÉ) ‚â´ F.map (Œº L X‚ÇÅ X‚ÇÇ) ‚ñ∑ F.obj (L.obj X‚ÇÉ) = ùüô _ := by
      grind [_=_ MonoidalCategory.comp_whiskerRight, MonoidalCategory.id_whiskerRight, Monoidal.Œ¥_Œº]
    have e‚ÇÇ : G.obj X‚ÇÅ ‚óÅ F.map (Œ¥ L X‚ÇÇ X‚ÇÉ) ‚â´ G.obj X‚ÇÅ ‚óÅ F.map (Œº L X‚ÇÇ X‚ÇÉ) = ùüô _ := by
      grind [_=_ MonoidalCategory.whiskerLeft_comp, MonoidalCategory.whiskerLeft_id, Monoidal.Œ¥_Œº]
    monoidal_simps
    congr 2
    dsimp [e]
    simp only [comp_whiskerRight, map_id, Category.id_comp, Category.assoc,
      ‚Üê whisker_exchange_assoc, tensor_whiskerLeft, MonoidalCategory.whiskerLeft_comp]
    simp only [‚Üê associator_inv_naturality_left_assoc, Iso.inv_hom_id_assoc, whisker_exchange_assoc,
      associator_inv_naturality_right_assoc, reassoc_of% e‚ÇÅ, reassoc_of% e‚ÇÇ]
    congr 1
    simp [‚Üê comp_whiskerRight_assoc, ‚Üê MonoidalCategory.whiskerLeft_comp_assoc,
      - MonoidalCategory.whiskerLeft_comp, ‚Üê whisker_exchange_assoc,
      tensor_whiskerLeft_symm, -tensor_whiskerLeft,
      ‚Üê LaxMonoidal.associativity_assoc G, ‚Üê Functor.map_comp]
  ¬∑ refine natTrans_ext L W (fun X‚ÇÇ ‚Ü¶ ?_)
    have := NatTrans.congr_app ((curriedTensorPreIsoPost L W F G).hom.naturality (ŒµIso L).inv)
      (L.obj X‚ÇÇ)
    dsimp [e] at this ‚ä¢
    monoidal_simps
    simp [reassoc_of% this, curriedTensorPreIsoPost_hom_app_app, ‚Üê comp_whiskerRight_assoc,
      -comp_whiskerRight, tensorHom_def, ‚Üê whisker_exchange_assoc, ‚Üê map_comp]
  ¬∑ refine natTrans_ext L W (fun X‚ÇÅ ‚Ü¶ ?_)
    have := ((curriedTensorPreIsoPost L W F G).hom.app (L.obj X‚ÇÅ)).naturality (ŒµIso L).inv
    dsimp [e] at this ‚ä¢
    monoidal_simps
    simp [reassoc_of% this, curriedTensorPreIsoPost_hom_app_app, tensorHom_def,
      whisker_exchange_assoc, ‚Üê MonoidalCategory.whiskerLeft_comp_assoc, ‚Üê map_comp]

/--
Monoidal structure on `F`, given that `L ‚ãô F` is monoidal, where `L` is a localization functor.
-/
noncomputable def functorMonoidalOfComp : F.Monoidal :=
  (functorCoreMonoidalOfComp L W F G).toMonoidal

@[reassoc]
lemma functorMonoidalOfComp_Œµ : letI := functorMonoidalOfComp L W F G
    letI e := Lifting.iso L W G F
    Œµ F = Œµ G ‚â´ e.inv.app _ ‚â´ F.map (Œ∑ L) := by
  simp [Functor.CoreMonoidal.toLaxMonoidal_Œµ]

@[reassoc]
lemma functorMonoidalOfComp_Œº (X Y : C) : letI := functorMonoidalOfComp L W F G
    letI e := Lifting.iso L W G F
    Œº F (L.obj X) (L.obj Y) = (e.hom.app _ ‚äó‚Çò e.hom.app _) ‚â´ Œº G X Y ‚â´ e.inv.app _ ‚â´
        F.map (Œ¥ L _ _) := by
  simp [Functor.CoreMonoidal.toLaxMonoidal_Œº, curriedTensorPreIsoPost_hom_app_app]

instance natTrans_isMonoidal :
    letI : F.Monoidal := functorMonoidalOfComp L W F G
    (Lifting.iso L W G F).hom.IsMonoidal := by
  letI : F.Monoidal := functorMonoidalOfComp L W F G
  refine ‚ü®?_, fun _ _ ‚Ü¶ ?_‚ü©
  ¬∑ simp [functorMonoidalOfComp_Œµ]
  ¬∑ simp [functorMonoidalOfComp_Œº]

end CategoryTheory.Localization.Monoidal
