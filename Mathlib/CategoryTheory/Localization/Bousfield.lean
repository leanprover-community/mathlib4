/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.ObjectProperty.Local
public import Mathlib.CategoryTheory.MorphismProperty.Composition
public import Mathlib.CategoryTheory.Localization.Adjunction

/-!
# Bousfield localization

Given a predicate `P : ObjectProperty C` on the objects of a category `C`,
we define `W.isLocal : MorphismProperty C` as the class of morphisms `f : X ‚ü∂ Y`
such that for any `Z : C` such that `P Z`, the precomposition with `f`
induces a bijection `(Y ‚ü∂ Z) ‚âÉ (X ‚ü∂ Z)`.

(This construction is part of the left Bousfield localization
in the context of model categories.)

When `G ‚ä£ F` is an adjunction with `F : C ‚•§ D` fully faithful, then
`G : D ‚•§ C` is a localization functor for the class `isLocal (¬∑ ‚àà Set.range F.obj)`,
which then identifies to the inverse image by `G` of the class of
isomorphisms in `C`.

The dual results are also obtained.

## References

* https://ncatlab.org/nlab/show/left+Bousfield+localization+of+model+categories

-/

@[expose] public section

namespace CategoryTheory

open Category

variable {C D : Type*} [Category* C] [Category* D]

namespace ObjectProperty

/-! ### Left Bousfield localization -/

section

variable (P : ObjectProperty C)

/-- Given `P : ObjectProperty C`, this is the class of morphisms `f : X ‚ü∂ Y`
such that for all `Z : C` such that `P Z`, the precomposition with `f` induces
a bijection `(Y ‚ü∂ Z) ‚âÉ (X ‚ü∂ Z)`. (One of the applications of this notion
is the left Bousfield localization of model categories.) -/
def isLocal : MorphismProperty C := fun _ _ f =>
  ‚àÄ Z, P Z ‚Üí Function.Bijective (fun (g : _ ‚ü∂ Z) => f ‚â´ g)

variable {P} in
/-- The bijection `(Y ‚ü∂ Z) ‚âÉ (X ‚ü∂ Z)` induced by `f : X ‚ü∂ Y` when `P.isLocal f`
and `P Z`. -/
@[simps! apply]
noncomputable def isLocal.homEquiv {X Y : C} {f : X ‚ü∂ Y} (hf : P.isLocal f) (Z : C) (hZ : P Z) :
    (Y ‚ü∂ Z) ‚âÉ (X ‚ü∂ Z) :=
  Equiv.ofBijective _ (hf Z hZ)

lemma isoClosure_isLocal : P.isoClosure.isLocal = P.isLocal := by
  ext X Y f
  constructor
  ¬∑ intro hf Z hZ
    exact hf _ (P.le_isoClosure _ hZ)
  ¬∑ rintro hf Z ‚ü®Z', hZ', ‚ü®e‚ü©‚ü©
    constructor
    ¬∑ intro g‚ÇÅ g‚ÇÇ eq
      rw [‚Üê cancel_mono e.hom]
      apply (hf _ hZ').1
      simp only [reassoc_of% eq]
    ¬∑ intro g
      obtain ‚ü®a, h‚ü© := (hf _ hZ').2 (g ‚â´ e.hom)
      exact ‚ü®a ‚â´ e.inv, by simp only [reassoc_of% h, e.hom_inv_id, comp_id]‚ü©

instance : P.isLocal.IsMultiplicative where
  id_mem X Z _ := by simpa [id_comp] using Function.bijective_id
  comp_mem f g hf hg Z hZ := by
    simpa using Function.Bijective.comp (hf Z hZ) (hg Z hZ)

instance : P.isLocal.HasTwoOutOfThreeProperty where
  of_postcomp f g hg hfg Z hZ := by
    rw [‚Üê Function.Bijective.of_comp_iff _ (hg Z hZ)]
    simpa using hfg Z hZ
  of_precomp f g hf hfg Z hZ := by
    rw [‚Üê Function.Bijective.of_comp_iff' (hf Z hZ)]
    simpa using hfg Z hZ

lemma isLocal_of_isIso {X Y : C} (f : X ‚ü∂ Y) [IsIso f] : P.isLocal f := fun Z _ => by
  constructor
  ¬∑ intro g‚ÇÅ g‚ÇÇ _
    simpa only [‚Üê cancel_epi f]
  ¬∑ intro g
    exact ‚ü®inv f ‚â´ g, by simp‚ü©

lemma isLocal_iff_isIso {X Y : C} (f : X ‚ü∂ Y) (hX : P X) (hY : P Y) :
    P.isLocal f ‚Üî IsIso f := by
  constructor
  ¬∑ intro hf
    obtain ‚ü®g, hg‚ü© := (hf _ hX).2 (ùüô X)
    exact ‚ü®g, hg, (hf _ hY).1 (by simp only [reassoc_of% hg, comp_id])‚ü©
  ¬∑ apply isLocal_of_isIso

instance : P.isLocal.RespectsIso where
  precomp f (_ : IsIso f) g hg := P.isLocal.comp_mem f g (isLocal_of_isIso _ f) hg
  postcomp f (_ : IsIso f) g hg := P.isLocal.comp_mem g f hg (isLocal_of_isIso _ f)

lemma le_isLocal_iff (P : ObjectProperty C) (W : MorphismProperty C) :
    W ‚â§ P.isLocal ‚Üî P ‚â§ W.isLocal :=
  ‚ü®fun h _ hZ _ _ _ hf ‚Ü¶ h _ hf _ hZ,
    fun h _ _ _ hf _ hZ ‚Ü¶ h _ hZ _ hf‚ü©

lemma galoisConnection_isLocal :
    GaloisConnection (OrderDual.toDual ‚àò isLocal (C := C))
      (MorphismProperty.isLocal ‚àò OrderDual.ofDual) :=
  le_isLocal_iff

end

/-! ### Right Bousfield localization -/

section

variable (P : ObjectProperty C)

/-- Given `P : ObjectProperty C`, this is the class of morphisms `g : Y ‚ü∂ Z`
such that for all `X : C` such that `P X`, the postcomposition with `g` induces
a bijection `(X ‚ü∂ Y) ‚âÉ (X ‚ü∂ Z)`. (One of the applications of this notion
is the right Bousfield localization of model categories.) -/
def isColocal : MorphismProperty C := fun _ _ g =>
  ‚àÄ X, P X ‚Üí Function.Bijective (fun (f : X ‚ü∂ _) => f ‚â´ g)

variable {P} in
/-- The bijection `(X ‚ü∂ Y) ‚âÉ (X ‚ü∂ Z)` induced by `g : Y ‚ü∂ Z` when `P.isColocal g`
and `P X`. -/
@[simps! apply]
noncomputable def isColocal.homEquiv {Y Z : C} {g : Y ‚ü∂ Z} (hg : P.isColocal g) (X : C) (hX : P X) :
    (X ‚ü∂ Y) ‚âÉ (X ‚ü∂ Z) :=
  Equiv.ofBijective _ (hg X hX)

lemma isoClosure_isColocal : P.isoClosure.isColocal = P.isColocal := by
  ext Y Z g
  constructor
  ¬∑ intro hg X hX
    exact hg _ (P.le_isoClosure _ hX)
  ¬∑ rintro hg X ‚ü®X', hX', ‚ü®e‚ü©‚ü©
    constructor
    ¬∑ intro f‚ÇÅ f‚ÇÇ eq
      rw [‚Üê cancel_epi e.inv]
      apply (hg _ hX').1
      simp [eq]
    ¬∑ intro f
      obtain ‚ü®a, h‚ü© := (hg _ hX').2 (e.inv ‚â´ f)
      exact ‚ü®e.hom ‚â´ a, by simp [h]‚ü©

instance : P.isColocal.IsMultiplicative where
  id_mem _ _ _ := by simpa [id_comp] using Function.bijective_id
  comp_mem f g hf hg X hX := by
    convert Function.Bijective.comp (hg X hX) (hf X hX)
    cat_disch

instance : P.isColocal.HasTwoOutOfThreeProperty where
  of_postcomp f g hg hfg X hX := by
    rw [‚Üê Function.Bijective.of_comp_iff' (hg X hX)]
    convert hfg X hX
    cat_disch
  of_precomp f g hf hfg X hX := by
    rw [‚Üê Function.Bijective.of_comp_iff _ (hf X hX)]
    convert hfg X hX
    cat_disch

lemma isColocal_of_isIso {X Y : C} (f : X ‚ü∂ Y) [IsIso f] : P.isColocal f := fun Z _ => by
  constructor
  ¬∑ intro g‚ÇÅ g‚ÇÇ _
    simpa only [‚Üê cancel_mono f]
  ¬∑ intro g
    exact ‚ü®g ‚â´ inv f, by simp‚ü©

lemma isColocal_iff_isIso {X Y : C} (f : X ‚ü∂ Y) (hX : P X) (hY : P Y) :
    P.isColocal f ‚Üî IsIso f := by
  constructor
  ¬∑ intro hf
    obtain ‚ü®g, hg‚ü© := (hf _ hY).2 (ùüô Y)
    exact ‚ü®g, (hf _ hX).1 (by cat_disch), hg‚ü©
  ¬∑ apply isColocal_of_isIso

instance : P.isColocal.RespectsIso where
  precomp f (_ : IsIso f) g hg := P.isColocal.comp_mem f g (isColocal_of_isIso _ f) hg
  postcomp f (_ : IsIso f) g hg := P.isColocal.comp_mem g f hg (isColocal_of_isIso _ f)

lemma le_isColocal_iff (P : ObjectProperty C) (W : MorphismProperty C) :
    W ‚â§ P.isColocal ‚Üî P ‚â§ W.isColocal :=
  ‚ü®fun h _ hZ _ _ _ hf ‚Ü¶ h _ hf _ hZ,
    fun h _ _ _ hf _ hZ ‚Ü¶ h _ hZ _ hf‚ü©

lemma galoisConnection_isColocal :
    GaloisConnection (OrderDual.toDual ‚àò isColocal (C := C))
      (MorphismProperty.isColocal ‚àò OrderDual.ofDual) :=
  le_isColocal_iff

end

/-! ### Bousfield localization and adjunctions -/

section

variable {F : C ‚•§ D} {G : D ‚•§ C} (adj : G ‚ä£ F) [F.Full] [F.Faithful]
include adj

lemma isLocal_adj_unit_app (X : D) : isLocal (¬∑ ‚àà Set.range F.obj) (adj.unit.app X) := by
  rintro _ ‚ü®Y, rfl‚ü©
  convert ((Functor.FullyFaithful.ofFullyFaithful F).homEquiv.symm.trans
    (adj.homEquiv X Y)).bijective using 1
  dsimp [Adjunction.homEquiv]
  aesop

lemma isLocal_iff_isIso_map {X Y : D} (f : X ‚ü∂ Y) :
    isLocal (¬∑ ‚àà Set.range F.obj) f ‚Üî IsIso (G.map f) := by
  have := adj.unit.naturality f
  dsimp at this
  rw [‚Üê (isLocal (¬∑ ‚àà Set.range F.obj)).postcomp_iff _ _ (isLocal_adj_unit_app adj Y),
    this, (isLocal (¬∑ ‚àà Set.range F.obj)).precomp_iff _ _ (isLocal_adj_unit_app adj X),
    isLocal_iff_isIso _ _ ‚ü®_, rfl‚ü© ‚ü®_, rfl‚ü©]
  exact ‚ü®fun _ ‚Ü¶ isIso_of_fully_faithful F (G.map f), fun _ ‚Ü¶ inferInstance‚ü©

lemma isLocal_eq_inverseImage_isomorphisms :
    isLocal (¬∑ ‚àà Set.range F.obj) = (MorphismProperty.isomorphisms _).inverseImage G := by
  ext P‚ÇÅ P‚ÇÇ f
  rw [isLocal_iff_isIso_map adj]
  rfl

lemma isLocalization_isLocal : G.IsLocalization (isLocal (¬∑ ‚àà Set.range F.obj)) := by
  rw [isLocal_eq_inverseImage_isomorphisms adj]
  exact adj.isLocalization

end

section

variable {F : C ‚•§ D} {G : D ‚•§ C} (adj : G ‚ä£ F) [G.Full] [G.Faithful]
include adj

lemma isColocal_adj_counit_app (X : C) : isColocal (¬∑ ‚àà Set.range G.obj) (adj.counit.app X) := by
  rintro _ ‚ü®Y, rfl‚ü©
  convert ((Functor.FullyFaithful.ofFullyFaithful G).homEquiv.symm.trans
    (adj.homEquiv Y X).symm).bijective using 1
  dsimp [Adjunction.homEquiv]
  cat_disch

lemma isColocal_iff_isIso_map {X Y : C} (f : X ‚ü∂ Y) :
    isColocal (¬∑ ‚àà Set.range G.obj) f ‚Üî IsIso (F.map f) := by
  have := adj.counit.naturality f
  dsimp at this
  rw [‚Üê (isColocal _).precomp_iff _ _ (isColocal_adj_counit_app adj X),
    ‚Üê this, (isColocal _).postcomp_iff _ _ (isColocal_adj_counit_app adj Y),
    isColocal_iff_isIso _ _ ‚ü®_, rfl‚ü© ‚ü®_, rfl‚ü©]
  exact ‚ü®fun _ ‚Ü¶ isIso_of_fully_faithful G (F.map f), fun _ ‚Ü¶ inferInstance‚ü©

lemma isColocal_eq_inverseImage_isomorphisms :
    isColocal (¬∑ ‚àà Set.range G.obj) = (MorphismProperty.isomorphisms _).inverseImage F := by
  ext P‚ÇÅ P‚ÇÇ f
  rw [isColocal_iff_isIso_map adj]
  rfl

lemma isLocalization_isColocal : F.IsLocalization (isColocal (¬∑ ‚àà Set.range G.obj)) := by
  rw [isColocal_eq_inverseImage_isomorphisms adj]
  exact adj.isLocalization'

end

end ObjectProperty

open Localization

lemma ObjectProperty.le_isLocal_isLocal (P : ObjectProperty C) :
    P ‚â§ P.isLocal.isLocal := by
  rw [‚Üê le_isLocal_iff]

lemma MorphismProperty.le_isLocal_isLocal (W : MorphismProperty C) :
    W ‚â§ W.isLocal.isLocal := by
  rw [ObjectProperty.le_isLocal_iff]

lemma ObjectProperty.le_isColocal_isColocal (P : ObjectProperty C) :
    P ‚â§ P.isColocal.isColocal := by
  rw [‚Üê le_isColocal_iff]

lemma MorphismProperty.le_isColocal_isColocal (W : MorphismProperty C) :
    W ‚â§ W.isColocal.isColocal := by
  rw [ObjectProperty.le_isColocal_iff]

@[deprecated (since := "2025-11-20")] alias ObjectProperty.le_isLocal_W :=
  ObjectProperty.le_isLocal_isLocal
@[deprecated (since := "2025-11-20")] alias MorphismProperty.le_leftBousfieldW_isLocal :=
  MorphismProperty.le_isLocal_isLocal

namespace Localization.LeftBousfield

open ObjectProperty

@[deprecated (since := "2025-11-20")] alias W := isLocal
@[deprecated (since := "2025-11-20")] alias W.homEquiv := isLocal.homEquiv
@[deprecated (since := "2025-11-20")] alias W_isoClosure := isoClosure_isLocal
@[deprecated (since := "2025-11-20")] alias W_of_isIso := isLocal_of_isIso
@[deprecated (since := "2025-11-20")] alias W_iff_isIso := isLocal_iff_isIso
@[deprecated (since := "2025-11-20")] alias le_W_iff := le_isLocal_iff
@[deprecated (since := "2025-11-20")] alias galoisConnection := galoisConnection_isLocal
@[deprecated (since := "2025-11-20")] alias W_adj_unit_app := isLocal_adj_unit_app
@[deprecated (since := "2025-11-20")] alias W_iff_isIso_map := isLocal_iff_isIso_map
@[deprecated (since := "2025-11-20")] alias W_eq_inverseImage_isomorphisms :=
  isLocal_eq_inverseImage_isomorphisms
@[deprecated (since := "2025-11-20")] alias isLocalization := isLocalization_isLocal

end Localization.LeftBousfield

end CategoryTheory
