/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.ClosedUnderIsomorphisms
import Mathlib.CategoryTheory.MorphismProperty.Composition
import Mathlib.CategoryTheory.Localization.Adjunction

/-!
# Bousfield localization

Given a predicate `P : C â†’ Prop` on the objects of a category `C`,
we define `Localization.LeftBousfield.W P : MorphismProperty C`
as the class of morphisms `f : X âŸ¶ Y` such that for any `Z : C`
such that `P Z`, the precomposition with `f` induces a bijection
`(Y âŸ¶ Z) â‰ƒ (X âŸ¶ Z)`.

(This construction is part of left Bousfield localization
in the context of model categories.)

When `G âŠ£ F` is an adjunction with `F : C â¥¤ D` fully faithful, then
`G : D â¥¤ C` is a localization functor for the class `W (Â· âˆˆ Set.range F.obj)`,
which then identifies to the inverse image by `G` of the class of
isomorphisms in `C`.

## References

* https://ncatlab.org/nlab/show/left+Bousfield+localization+of+model+categories

-/

namespace CategoryTheory

open Category

namespace Localization

variable {C D : Type*} [Category C] [Category D]

namespace LeftBousfield

section

variable (P : C â†’ Prop)

/-- Given a predicate `P : C â†’ Prop`, this is the class of morphisms `f : X âŸ¶ Y`
such that for all `Z : C` such that `P Z`, the precomposition with `f` induces
a bijection `(Y âŸ¶ Z) â‰ƒ (X âŸ¶ Z)`. -/
def W : MorphismProperty C := fun _ _ f =>
  âˆ€ Z, P Z â†’ Function.Bijective (fun (g : _ âŸ¶ Z) => f â‰« g)

lemma W_isoClosure : W (isoClosure P) = W P := by
  ext X Y f
  constructor
  Â· intro hf Z hZ
    exact hf _ (le_isoClosure _ _ hZ)
  Â· rintro hf Z âŸ¨Z', hZ', âŸ¨eâŸ©âŸ©
    constructor
    Â· intro gâ‚ gâ‚‚ eq
      rw [â† cancel_mono e.hom]
      apply (hf _ hZ').1
      simp only [reassoc_of% eq]
    Â· intro g
      obtain âŸ¨a, hâŸ© := (hf _ hZ').2 (g â‰« e.hom)
      exact âŸ¨a â‰« e.inv, by simp only [reassoc_of% h, e.hom_inv_id, comp_id]âŸ©

instance : (W P).IsMultiplicative where
  id_mem X Z _ := by
    simp [id_comp]
    exact Function.bijective_id
  comp_mem f g hf hg Z hZ := by
    simpa using Function.Bijective.comp (hf Z hZ) (hg Z hZ)

instance : (W P).HasTwoOutOfThreeProperty where
  of_postcomp f g hg hfg Z hZ := by
    rw [â† Function.Bijective.of_comp_iff _ (hg Z hZ)]
    simpa using hfg Z hZ
  of_precomp f g hf hfg Z hZ := by
    rw [â† Function.Bijective.of_comp_iff' (hf Z hZ)]
    simpa using hfg Z hZ

lemma W_of_isIso {X Y : C} (f : X âŸ¶ Y) [IsIso f] : W P f := fun Z _ => by
  constructor
  Â· intro gâ‚ gâ‚‚ _
    simpa only [â† cancel_epi f]
  Â· intro g
    exact âŸ¨inv f â‰« g, by simpâŸ©

lemma W_iff_isIso {X Y : C} (f : X âŸ¶ Y) (hX : P X) (hY : P Y) :
    W P f â†” IsIso f := by
  constructor
  Â· intro hf
    obtain âŸ¨g, hgâŸ© := (hf _ hX).2 (ğŸ™ X)
    exact âŸ¨g, hg, (hf _ hY).1 (by simp only [reassoc_of% hg, comp_id])âŸ©
  Â· apply W_of_isIso

end

section

variable {F : C â¥¤ D} {G : D â¥¤ C} (adj : G âŠ£ F) [F.Full] [F.Faithful]

lemma W_adj_unit_app (X : D) : W (Â· âˆˆ Set.range F.obj) (adj.unit.app X) := by
  rintro _ âŸ¨Y, rflâŸ©
  convert ((Functor.FullyFaithful.ofFullyFaithful F).homEquiv.symm.trans
    (adj.homEquiv X Y)).bijective using 1
  aesop

lemma W_iff_isIso_map {X Y : D} (f : X âŸ¶ Y) :
    W (Â· âˆˆ Set.range F.obj) f â†” IsIso (G.map f) := by
  rw [â† MorphismProperty.postcomp_iff _ _ _ (W_adj_unit_app adj Y)]
  erw [adj.unit.naturality f]
  rw [MorphismProperty.precomp_iff _ _ _ (W_adj_unit_app adj X),
    W_iff_isIso _ _ âŸ¨_, rflâŸ© âŸ¨_, rflâŸ©]
  constructor
  Â· intro h
    dsimp at h
    exact isIso_of_fully_faithful F (G.map f)
  Â· intro
    rw [Functor.comp_map]
    infer_instance

lemma W_eq_inverseImage_isomorphisms :
    W (Â· âˆˆ Set.range F.obj) = (MorphismProperty.isomorphisms _).inverseImage G := by
  ext Pâ‚ Pâ‚‚ f
  rw [W_iff_isIso_map adj]
  rfl

lemma isLocalization : G.IsLocalization (W (Â· âˆˆ Set.range F.obj)) := by
  rw [W_eq_inverseImage_isomorphisms adj]
  exact adj.isLocalization

end

end LeftBousfield

end Localization

end CategoryTheory
