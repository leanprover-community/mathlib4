/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Localization.LocalizerMorphism

/-!
# Resolutions for a morphism of localizers

Given a morphism of localizers `Î¦ : LocalizerMorphism Wâ‚ Wâ‚‚` (i.e. `Wâ‚` and `Wâ‚‚` are
morphism properties on categories `Câ‚` and `Câ‚‚`, and we have a functor
`Î¦.functor : Câ‚ â¥¤ Câ‚‚` which sends morphisms in `Wâ‚` to morphisms in `Wâ‚‚`), we introduce
the notion of right resolutions of objects in `Câ‚‚`: if `Xâ‚‚ : Câ‚‚`.
A right resolution consists of an object `Xâ‚ : Câ‚` and a morphism
`w : Xâ‚‚ âŸ¶ Î¦.functor.obj Xâ‚` that is in `Wâ‚‚`. Then, the typeclass
`Î¦.HasRightResolutions` holds when any `Xâ‚‚ : Câ‚‚` has a right resolution.

The type of right resolutions `Î¦.RightResolution Xâ‚‚` is endowed with a category
structure when the morphism property `Wâ‚` is multiplicative.

## Future works

* formalize right derivability structures as localizer morphisms admitting right resolutions
and forming a Guitart exact square, as it is defined in
[the paper by Kahn and Maltsiniotis][KahnMaltsiniotis2008] (TODO @joelriou)
* show that if `C` is an abelian category with enough injectives, there is a derivability
structure associated to the inclusion of the full subcategory of complexes of injective
objects into the bounded below homotopy category of `C` (TODO @joelriou)
* formalize dual results

## References
* [Bruno Kahn and Georges Maltsiniotis, *Structures de dÃ©rivabilitÃ©*][KahnMaltsiniotis2008]

-/

universe vâ‚ vâ‚‚ vâ‚‚' uâ‚ uâ‚‚ uâ‚‚'

namespace CategoryTheory

open Category Localization

variable {Câ‚ Câ‚‚ Dâ‚‚ H : Type*} [Category Câ‚] [Category Câ‚‚] [Category Dâ‚‚] [Category H]
  {Wâ‚ : MorphismProperty Câ‚} {Wâ‚‚ : MorphismProperty Câ‚‚}

namespace LocalizerMorphism

variable (Î¦ : LocalizerMorphism Wâ‚ Wâ‚‚)

/-- The category of resolutions of an object in the target category of a localizer morphism. -/
structure RightResolution (Xâ‚‚ : Câ‚‚) where
  /-- an object in the source category -/
  {Xâ‚ : Câ‚}
  /-- a morphism to an object of the form `Î¦.functor.obj Xâ‚` -/
  w : Xâ‚‚ âŸ¶ Î¦.functor.obj Xâ‚
  hw : Wâ‚‚ w

variable {Î¦ Xâ‚‚} in
lemma RightResolution.mk_surjective (R : Î¦.RightResolution Xâ‚‚) :
    âˆƒ (Xâ‚ : Câ‚) (w : Xâ‚‚ âŸ¶ Î¦.functor.obj Xâ‚) (hw : Wâ‚‚ w), R = RightResolution.mk w hw :=
  âŸ¨_, R.w, R.hw, rflâŸ©

/-- A localizer morphism has right resolutions when any object has a right resolution. -/
abbrev HasRightResolutions := âˆ€ (Xâ‚‚ : Câ‚‚), Nonempty (Î¦.RightResolution Xâ‚‚)

namespace RightResolution

variable {Î¦} {Xâ‚‚ : Câ‚‚}

/-- The type of morphisms in the category `Î¦.RightResolution Xâ‚‚` (when `Wâ‚` is multiplicative). -/
@[ext]
structure Hom (R R' : Î¦.RightResolution Xâ‚‚) where
  /-- a morphism in the source category -/
  f : R.Xâ‚ âŸ¶ R'.Xâ‚
  hf : Wâ‚ f
  comm : R.w â‰« Î¦.functor.map f = R'.w := by aesop_cat

attribute [reassoc (attr := simp)] Hom.comm

/-- The identity of a object in `Î¦.RightResolution Xâ‚‚`. -/
@[simps]
def Hom.id [Wâ‚.ContainsIdentities] (R : Î¦.RightResolution Xâ‚‚) : Hom R R where
  f := ğŸ™ _
  hf := Wâ‚.id_mem _

variable [Wâ‚.IsMultiplicative]

/-- The composition of morphisms in `Î¦.RightResolution Xâ‚‚`. -/
@[simps]
def Hom.comp {R R' R'' : Î¦.RightResolution Xâ‚‚}
    (Ï† : Hom R R') (Ïˆ : Hom R' R'') :
    Hom R R'' where
  f := Ï†.f â‰« Ïˆ.f
  hf := Wâ‚.comp_mem _ _ Ï†.hf Ïˆ.hf

instance : Category (Î¦.RightResolution Xâ‚‚) where
  Hom := Hom
  id := Hom.id
  comp := Hom.comp

@[simp]
lemma id_f (R : Î¦.RightResolution Xâ‚‚) : Hom.f (ğŸ™ R) = ğŸ™ R.Xâ‚ := rfl

@[simp, reassoc]
lemma comp_f {R R' R'' : Î¦.RightResolution Xâ‚‚} (Ï† : R âŸ¶ R') (Ïˆ : R' âŸ¶ R'') :
    (Ï† â‰« Ïˆ).f = Ï†.f â‰« Ïˆ.f := rfl

@[ext]
lemma hom_ext {R R' : Î¦.RightResolution Xâ‚‚} {Ï†â‚ Ï†â‚‚ : R âŸ¶ R'} (h : Ï†â‚.f = Ï†â‚‚.f) :
    Ï†â‚ = Ï†â‚‚ :=
  Hom.ext _ _ h

end RightResolution

section

variable [Î¦.HasRightResolutions] (Lâ‚‚ : Câ‚‚ â¥¤ Dâ‚‚) [Lâ‚‚.IsLocalization Wâ‚‚]

lemma essSurj_of_hasRightResolutions : (Î¦.functor â‹™ Lâ‚‚).EssSurj where
  mem_essImage Xâ‚‚ := by
    have := Localization.essSurj Lâ‚‚ Wâ‚‚
    have R : Î¦.RightResolution (Lâ‚‚.objPreimage Xâ‚‚) := Classical.arbitrary _
    exact âŸ¨R.Xâ‚, âŸ¨(Localization.isoOfHom Lâ‚‚ Wâ‚‚ _ R.hw).symm â‰ªâ‰« Lâ‚‚.objObjPreimageIso Xâ‚‚âŸ©âŸ©

lemma isIso_iff_of_hasRightResolutions {F G : Dâ‚‚ â¥¤ H} (Î± : F âŸ¶ G) :
    IsIso Î± â†” âˆ€ (Xâ‚ : Câ‚), IsIso (Î±.app (Lâ‚‚.obj (Î¦.functor.obj Xâ‚))) := by
  constructor
  Â· intros
    infer_instance
  Â· intro hÎ±
    have : âˆ€ (Xâ‚‚ : Dâ‚‚), IsIso (Î±.app Xâ‚‚) := fun Xâ‚‚ => by
      have := Î¦.essSurj_of_hasRightResolutions Lâ‚‚
      rw [â† NatTrans.isIso_app_iff_of_iso Î± ((Î¦.functor â‹™ Lâ‚‚).objObjPreimageIso Xâ‚‚)]
      apply hÎ±
    exact NatIso.isIso_of_isIso_app Î±

end

lemma hasRightResolutions_of_arrow [Î¦.arrow.HasRightResolutions] :
    Î¦.HasRightResolutions := fun Xâ‚‚ => by
  let R : Î¦.arrow.RightResolution (Arrow.mk (ğŸ™ Xâ‚‚)) := Classical.arbitrary _
  exact
   âŸ¨{ w := R.w.left
      hw := R.hw.1 } âŸ©

end LocalizerMorphism

end CategoryTheory
