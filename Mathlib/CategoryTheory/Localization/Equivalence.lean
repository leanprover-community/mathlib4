/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Localization.Predicate
import Mathlib.CategoryTheory.CatCommSq

/-!

# Localization functors are preserved through equivalences

-/

namespace CategoryTheory

open Category Localization

variable {Câ‚ Câ‚‚ D Dâ‚ Dâ‚‚ : Type _} [Category Câ‚] [Category Câ‚‚] [Category D]
  [Category Dâ‚] [Category Dâ‚‚]

namespace Localization

variable
  (Lâ‚ : Câ‚ â¥¤ Dâ‚) (Wâ‚ : MorphismProperty Câ‚) [Lâ‚.IsLocalization Wâ‚]
  (Lâ‚‚ : Câ‚‚ â¥¤ Dâ‚‚) (Wâ‚‚ : MorphismProperty Câ‚‚) [Lâ‚‚.IsLocalization Wâ‚‚]
  (G : Câ‚ â¥¤ Dâ‚‚) (G' : Dâ‚ â¥¤ Dâ‚‚) [Lifting Lâ‚ Wâ‚ G G']
  (F : Câ‚‚ â¥¤ Dâ‚) (F' : Dâ‚‚ â¥¤ Dâ‚) [Lifting Lâ‚‚ Wâ‚‚ F F']
  (Î± : G â‹™ F' â‰… Lâ‚) (Î² : F â‹™ G' â‰… Lâ‚‚)

/-- basic constructor of an equivalence between localized categories -/
noncomputable def equivalence : Dâ‚ â‰Œ Dâ‚‚ :=
  Equivalence.mk G' F' (liftNatIso Lâ‚ Wâ‚ Lâ‚ (G â‹™ F') (ğŸ­ Dâ‚) (G' â‹™ F') Î±.symm)
    (liftNatIso Lâ‚‚ Wâ‚‚ (F â‹™ G') Lâ‚‚ (F' â‹™ G') (ğŸ­ Dâ‚‚) Î²)

@[simp]
lemma equivalence_counit_app (X : Câ‚‚) :
    (equivalence Lâ‚ Wâ‚ Lâ‚‚ Wâ‚‚ G G' F F' Î± Î²).counitIso.app (Lâ‚‚.obj X) =
      (Lifting.iso Lâ‚‚ Wâ‚‚ (F â‹™ G') (F' â‹™ G')).app X â‰ªâ‰« Î².app X := by
  ext
  dsimp [equivalence, Equivalence.mk]
  rw [liftNatTrans_app]
  dsimp [Lifting.iso]
  rw [comp_id]

/-- basic construction of an equivalence between localized categories -/
noncomputable def isEquivalence : IsEquivalence G' :=
  IsEquivalence.ofEquivalence (equivalence Lâ‚ Wâ‚ Lâ‚‚ Wâ‚‚ G G' F F' Î± Î²)

end Localization

namespace Functor

namespace IsLocalization

lemma of_equivalence_source (Lâ‚ : Câ‚ â¥¤ D) (Wâ‚ : MorphismProperty Câ‚) (Lâ‚‚ : Câ‚‚ â¥¤ D) (Wâ‚‚ : MorphismProperty Câ‚‚)
  (E : Câ‚ â‰Œ Câ‚‚) (hWâ‚ : Wâ‚ âŠ† Wâ‚‚.isoClosure.inverseImage E.functor) (hWâ‚‚ : Wâ‚‚.IsInvertedBy Lâ‚‚)
  [Lâ‚.IsLocalization Wâ‚] (iso : E.functor â‹™ Lâ‚‚ â‰… Lâ‚) : Lâ‚‚.IsLocalization Wâ‚‚ := by
  have h : Wâ‚.IsInvertedBy (E.functor â‹™ Wâ‚‚.Q) := fun _ _ f hf => by
    obtain âŸ¨_, _, f', hf', âŸ¨eâŸ©âŸ© := hWâ‚ f hf
    exact ((MorphismProperty.RespectsIso.isomorphisms _).arrow_mk_iso_iff
      (Wâ‚‚.Q.mapArrow.mapIso e)).1 (Localization.inverts Wâ‚‚.Q Wâ‚‚ _ hf')
  exact
  { inverts := hWâ‚‚
    nonempty_isEquivalence :=
      âŸ¨Localization.isEquivalence Wâ‚‚.Q Wâ‚‚ Lâ‚ Wâ‚ Lâ‚‚ (Construction.lift Lâ‚‚ hWâ‚‚)
        (E.functor â‹™ Wâ‚‚.Q) (Localization.lift (E.functor â‹™ Wâ‚‚.Q) h Lâ‚)
        ((leftUnitor _).symm â‰ªâ‰« isoWhiskerRight E.counitIso.symm _ â‰ªâ‰«
          Functor.associator _ _ _ â‰ªâ‰«
          isoWhiskerLeft E.inverse ((Functor.associator _ _ _).symm â‰ªâ‰« isoWhiskerRight iso _) â‰ªâ‰«
          isoWhiskerLeft _ (Localization.fac (E.functor â‹™ Wâ‚‚.Q) h Lâ‚) â‰ªâ‰«
          (Functor.associator _ _ _).symm â‰ªâ‰« isoWhiskerRight E.counitIso _ â‰ªâ‰« leftUnitor _ )
        (Functor.associator _ _ _ â‰ªâ‰« isoWhiskerLeft _ (Lifting.iso Wâ‚‚.Q Wâ‚‚ _ _)  â‰ªâ‰« iso) âŸ© }

lemma of_equivalences (Lâ‚ : Câ‚ â¥¤ Dâ‚) (Wâ‚ : MorphismProperty Câ‚) [Lâ‚.IsLocalization Wâ‚]
  (Lâ‚‚ : Câ‚‚ â¥¤ Dâ‚‚) (Wâ‚‚ : MorphismProperty Câ‚‚)
  (E : Câ‚ â‰Œ Câ‚‚) (E' : Dâ‚ â‰Œ Dâ‚‚) [CatCommSq E.functor Lâ‚ Lâ‚‚ E'.functor]
  (hWâ‚ : Wâ‚ âŠ† Wâ‚‚.isoClosure.inverseImage E.functor) (hWâ‚‚ : Wâ‚‚.IsInvertedBy Lâ‚‚): Lâ‚‚.IsLocalization Wâ‚‚ := by
  haveI : (E.functor â‹™ Lâ‚‚).IsLocalization Wâ‚ :=
    of_equivalence_target Lâ‚ Wâ‚ _ E' ((CatCommSq.iso _ _ _ _).symm)
  exact of_equivalence_source (E.functor â‹™ Lâ‚‚) Wâ‚ Lâ‚‚ Wâ‚‚ E hWâ‚ hWâ‚‚ (Iso.refl _)

end IsLocalization

end Functor

end CategoryTheory
