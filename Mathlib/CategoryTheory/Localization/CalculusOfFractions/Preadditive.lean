/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Localization.CalculusOfFractions.Fractions
import Mathlib.CategoryTheory.Localization.HasLocalization
import Mathlib.CategoryTheory.Preadditive.AdditiveFunctor
import Mathlib.Logic.Equiv.TransferInstance

/-!
# The preadditive category structure on the localized category

In this file, it is shown that if `W : MorphismProperty C` has a left calculus
of fractions, and `C` is preadditive, then the localized category is preadditive,
and the localization functor is additive.

Let `L : C ‚•§ D` be a localization functor for `W`. We first construct an abelian
group structure on `L.obj X ‚ü∂ L.obj Y` for `X` and `Y` in `C`. The addition
is defined using representatives of two morphisms in `L` as left fractions with
the same denominator thanks to the lemmas in
`CategoryTheory.Localization.CalculusOfFractions.Fractions`.
As `L` is essentially surjective, we finally transport these abelian group structures
to `X' ‚ü∂ Y'` for all `X'` and `Y'` in `D`.

Preadditive category instances are defined on the categories `W.Localization`
(and `W.Localization'`) under the assumption the `W` has a left calculus of fractions.
(It would be easy to deduce from the results in this file that if `W` has a right calculus
of fractions, then the localized category can also be equipped with
a preadditive structure, but only one of these two constructions can be made an instance!)

-/

namespace CategoryTheory

open MorphismProperty Preadditive

variable {C D : Type*} [Category C] [Category D] [Preadditive C] (L : C ‚•§ D)
  {W : MorphismProperty C} [L.IsLocalization W] [W.HasLeftCalculusOfFractions]

namespace MorphismProperty

/-- The opposite of a left fraction. -/
abbrev LeftFraction.neg {X Y : C} (œÜ : W.LeftFraction X Y) :
    W.LeftFraction X Y where
  Y' := œÜ.Y'
  f := -œÜ.f
  s := œÜ.s
  hs := œÜ.hs

namespace LeftFraction‚ÇÇ

variable {X Y : C} (œÜ : W.LeftFraction‚ÇÇ X Y)

/-- The sum of two left fractions with the same denominator. -/
abbrev add : W.LeftFraction X Y where
  Y' := œÜ.Y'
  f := œÜ.f + œÜ.f'
  s := œÜ.s
  hs := œÜ.hs

@[simp]
lemma symm_add : œÜ.symm.add = œÜ.add := by
  dsimp [add, symm]
  congr 1
  apply add_comm

end LeftFraction‚ÇÇ

end MorphismProperty

variable (W)

namespace Localization

namespace Preadditive

section ImplementationDetails

/-! The definitions in this section (like `neg'` and `add'`) should never be used
directly. These are auxiliary definitions in order to construct the preadditive
structure `Localization.preadditive` (which is made irreducible). The user
should only rely on the fact that the localization functor is additive, as this
completely determines the preadditive structure on the localized category when
there is a calculus of left fractions. -/

variable {X Y Z : C}
variable {L}

/-- The opposite of a map `L.obj X ‚ü∂ L.obj Y` when `L : C ‚•§ D` is a localization
functor, `C` is preadditive and there is a left calculus of fractions. -/
noncomputable def neg' (f : L.obj X ‚ü∂ L.obj Y) : L.obj X ‚ü∂ L.obj Y :=
  (exists_leftFraction L W f).choose.neg.map L (inverts L W)

lemma neg'_eq (f : L.obj X ‚ü∂ L.obj Y) (œÜ : W.LeftFraction X Y)
    (hœÜ : f = œÜ.map L (inverts L W)) :
    neg' W f = œÜ.neg.map L (inverts L W) := by
  obtain ‚ü®œÜ‚ÇÄ, rfl, hœÜ‚ÇÄ‚ü© : ‚àÉ (œÜ‚ÇÄ : W.LeftFraction X Y)
    (_ : f = œÜ‚ÇÄ.map L (inverts L W)),
      neg' W f = œÜ‚ÇÄ.neg.map L (inverts L W) :=
    ‚ü®_, (exists_leftFraction L W f).choose_spec, rfl‚ü©
  rw [MorphismProperty.LeftFraction.map_eq_iff] at hœÜ
  obtain ‚ü®Y', t‚ÇÅ, t‚ÇÇ, hst, hft, ht‚ü© := hœÜ
  have := inverts L W _ ht
  rw [‚Üê cancel_mono (L.map (œÜ‚ÇÄ.s ‚â´ t‚ÇÅ))]
  nth_rw 1 [L.map_comp]
  rw [hœÜ‚ÇÄ, hst, LeftFraction.map_comp_map_s_assoc, L.map_comp,
    LeftFraction.map_comp_map_s_assoc, ‚Üê L.map_comp, ‚Üê L.map_comp,
    neg_comp, neg_comp, hft]

/-- The addition of two maps `L.obj X ‚ü∂ L.obj Y` when `L : C ‚•§ D` is a localization
functor, `C` is preadditive and there is a left calculus of fractions. -/
noncomputable def add' (f‚ÇÅ f‚ÇÇ : L.obj X ‚ü∂ L.obj Y) : L.obj X ‚ü∂ L.obj Y :=
  (exists_leftFraction‚ÇÇ L W f‚ÇÅ f‚ÇÇ).choose.add.map L (inverts L W)

lemma add'_eq (f‚ÇÅ f‚ÇÇ : L.obj X ‚ü∂ L.obj Y) (œÜ : W.LeftFraction‚ÇÇ X Y)
    (hœÜ‚ÇÅ : f‚ÇÅ = œÜ.fst.map L (inverts L W))
    (hœÜ‚ÇÇ : f‚ÇÇ = œÜ.snd.map L (inverts L W)) :
    add' W f‚ÇÅ f‚ÇÇ = œÜ.add.map L (inverts L W) := by
  obtain ‚ü®œÜ‚ÇÄ, rfl, rfl, hœÜ‚ÇÄ‚ü© : ‚àÉ (œÜ‚ÇÄ : W.LeftFraction‚ÇÇ X Y)
    (_ : f‚ÇÅ = œÜ‚ÇÄ.fst.map L (inverts L W))
    (_ : f‚ÇÇ = œÜ‚ÇÄ.snd.map L (inverts L W)),
    add' W f‚ÇÅ f‚ÇÇ = œÜ‚ÇÄ.add.map L (inverts L W) :=
    ‚ü®(exists_leftFraction‚ÇÇ L W f‚ÇÅ f‚ÇÇ).choose,
      (exists_leftFraction‚ÇÇ L W f‚ÇÅ f‚ÇÇ).choose_spec.1,
      (exists_leftFraction‚ÇÇ L W f‚ÇÅ f‚ÇÇ).choose_spec.2, rfl‚ü©
  obtain ‚ü®Z, t‚ÇÅ, t‚ÇÇ, hst, hft, hft', ht‚ü© := (LeftFraction‚ÇÇ.map_eq_iff L W œÜ‚ÇÄ œÜ).1 ‚ü®hœÜ‚ÇÅ, hœÜ‚ÇÇ‚ü©
  have := inverts L W _ ht
  rw [hœÜ‚ÇÄ, ‚Üê cancel_mono (L.map (œÜ‚ÇÄ.s ‚â´ t‚ÇÅ))]
  nth_rw 2 [hst]
  rw [L.map_comp, L.map_comp, LeftFraction.map_comp_map_s_assoc,
    LeftFraction.map_comp_map_s_assoc, ‚Üê L.map_comp, ‚Üê L.map_comp,
    add_comp, add_comp, hft, hft']

lemma add'_comm (f‚ÇÅ f‚ÇÇ : L.obj X ‚ü∂ L.obj Y) :
    add' W f‚ÇÅ f‚ÇÇ = add' W f‚ÇÇ f‚ÇÅ := by
  obtain ‚ü®Œ±, h‚ÇÅ, h‚ÇÇ‚ü© := exists_leftFraction‚ÇÇ L W f‚ÇÅ f‚ÇÇ
  rw [add'_eq W f‚ÇÅ f‚ÇÇ Œ± h‚ÇÅ h‚ÇÇ, add'_eq W f‚ÇÇ f‚ÇÅ Œ±.symm h‚ÇÇ h‚ÇÅ, Œ±.symm_add]

lemma add'_zero (f : L.obj X ‚ü∂ L.obj Y) :
    add' W f (L.map 0) = f := by
  obtain ‚ü®Œ±, hŒ±‚ü© := exists_leftFraction L W f
  rw [add'_eq W f (L.map 0) (LeftFraction‚ÇÇ.mk Œ±.f 0 Œ±.s Œ±.hs) hŒ±, hŒ±]; swap
  ¬∑ have := inverts L W _ Œ±.hs
    rw [‚Üê cancel_mono (L.map Œ±.s), ‚Üê L.map_comp, Limits.zero_comp,
      LeftFraction.map_comp_map_s]
  dsimp [LeftFraction‚ÇÇ.add]
  rw [add_zero]

lemma zero_add' (f : L.obj X ‚ü∂ L.obj Y) :
    add' W (L.map 0) f = f := by
  rw [add'_comm, add'_zero]

lemma add'_left_neg' (f : L.obj X ‚ü∂ L.obj Y) :
    add' W (neg' W f) f = L.map 0 := by
  obtain ‚ü®Œ±, rfl‚ü© := exists_leftFraction L W f
  have := inverts L W _ Œ±.hs
  rw [add'_eq W _ _ (LeftFraction‚ÇÇ.mk (-Œ±.f) Œ±.f Œ±.s Œ±.hs) (neg'_eq W _ _ rfl) rfl]
  simp only [‚Üê cancel_mono (L.map Œ±.s), LeftFraction.map_comp_map_s, ‚Üê L.map_comp,
    Limits.zero_comp, add_left_neg]

lemma add'_assoc (f‚ÇÅ f‚ÇÇ f‚ÇÉ : L.obj X ‚ü∂ L.obj Y) :
    add' W (add' W f‚ÇÅ f‚ÇÇ) f‚ÇÉ = add' W f‚ÇÅ (add' W f‚ÇÇ f‚ÇÉ) := by
  obtain ‚ü®Œ±, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ‚ü© := exists_leftFraction‚ÇÉ L W f‚ÇÅ f‚ÇÇ f‚ÇÉ
  rw [add'_eq W f‚ÇÅ f‚ÇÇ Œ±.forgetThd h‚ÇÅ h‚ÇÇ, add'_eq W f‚ÇÇ f‚ÇÉ Œ±.forgetFst h‚ÇÇ h‚ÇÉ,
    add'_eq W _ _ (LeftFraction‚ÇÇ.mk (Œ±.f + Œ±.f') Œ±.f'' Œ±.s Œ±.hs) rfl h‚ÇÉ,
    add'_eq W _ _ (LeftFraction‚ÇÇ.mk Œ±.f (Œ±.f' + Œ±.f'') Œ±.s Œ±.hs) h‚ÇÅ rfl]
  dsimp [LeftFraction‚ÇÇ.add]
  rw [add_assoc]

@[reassoc (attr := simp)]
lemma add'_comp (f‚ÇÅ f‚ÇÇ : L.obj X ‚ü∂ L.obj Y) (g : L.obj Y ‚ü∂ L.obj Z) :
    add' W f‚ÇÅ f‚ÇÇ ‚â´ g = add' W (f‚ÇÅ ‚â´ g) (f‚ÇÇ ‚â´ g) := by
  obtain ‚ü®Œ±, h‚ÇÅ, h‚ÇÇ‚ü© := exists_leftFraction‚ÇÇ L W f‚ÇÅ f‚ÇÇ
  obtain ‚ü®Œ≤, hŒ≤‚ü© := exists_leftFraction L W g
  obtain ‚ü®Œ≥, hŒ≥‚ü© := (RightFraction.mk _ Œ±.hs Œ≤.f).exists_leftFraction
  dsimp at hŒ≥
  rw [add'_eq W f‚ÇÅ f‚ÇÇ Œ± h‚ÇÅ h‚ÇÇ, add'_eq W (f‚ÇÅ ‚â´ g) (f‚ÇÇ ‚â´ g)
    (LeftFraction‚ÇÇ.mk (Œ±.f ‚â´ Œ≥.f) (Œ±.f' ‚â´ Œ≥.f) (Œ≤.s ‚â´ Œ≥.s)
    (W.comp_mem _ _ Œ≤.hs Œ≥.hs))]; rotate_left
  ¬∑ rw [h‚ÇÅ, hŒ≤]
    exact LeftFraction.map_comp_map_eq_map _ _ _ hŒ≥ _
  ¬∑ rw [h‚ÇÇ, hŒ≤]
    exact LeftFraction.map_comp_map_eq_map _ _ _ hŒ≥ _
  rw [hŒ≤, LeftFraction.map_comp_map_eq_map _ _ Œ≥ hŒ≥]
  dsimp [LeftFraction‚ÇÇ.add]
  rw [add_comp]

@[reassoc (attr := simp)]
lemma comp_add' (f : L.obj X ‚ü∂ L.obj Y) (g‚ÇÅ g‚ÇÇ : L.obj Y ‚ü∂ L.obj Z) :
    f ‚â´ add' W g‚ÇÅ g‚ÇÇ = add' W (f ‚â´ g‚ÇÅ) (f ‚â´ g‚ÇÇ) := by
  obtain ‚ü®Œ±, hŒ±‚ü© := exists_leftFraction L W f
  obtain ‚ü®Œ≤, hŒ≤‚ÇÅ, hŒ≤‚ÇÇ‚ü© := exists_leftFraction‚ÇÇ L W g‚ÇÅ g‚ÇÇ
  obtain ‚ü®Œ≥, hŒ≥‚ÇÅ, hŒ≥‚ÇÇ‚ü© := (RightFraction‚ÇÇ.mk _ Œ±.hs Œ≤.f Œ≤.f').exists_leftFraction‚ÇÇ
  dsimp at hŒ≥‚ÇÅ hŒ≥‚ÇÇ
  rw [add'_eq W g‚ÇÅ g‚ÇÇ Œ≤ hŒ≤‚ÇÅ hŒ≤‚ÇÇ, add'_eq W (f ‚â´ g‚ÇÅ) (f ‚â´ g‚ÇÇ)
    (LeftFraction‚ÇÇ.mk (Œ±.f ‚â´ Œ≥.f) (Œ±.f ‚â´ Œ≥.f') (Œ≤.s ‚â´ Œ≥.s) (W.comp_mem _ _ Œ≤.hs Œ≥.hs))
    (by simpa only [hŒ±, hŒ≤‚ÇÅ] using LeftFraction.map_comp_map_eq_map Œ± Œ≤.fst Œ≥.fst hŒ≥‚ÇÅ L)
    (by simpa only [hŒ±, hŒ≤‚ÇÇ] using LeftFraction.map_comp_map_eq_map Œ± Œ≤.snd Œ≥.snd hŒ≥‚ÇÇ L),
    hŒ±, LeftFraction.map_comp_map_eq_map Œ± Œ≤.add Œ≥.add
      (by simp only [add_comp, hŒ≥‚ÇÅ, hŒ≥‚ÇÇ, comp_add])]
  dsimp [LeftFraction‚ÇÇ.add]
  rw [comp_add]

@[simp]
lemma add'_map (f‚ÇÅ f‚ÇÇ : X ‚ü∂ Y) :
    add' W (L.map f‚ÇÅ) (L.map f‚ÇÇ) = L.map (f‚ÇÅ + f‚ÇÇ) :=
  (add'_eq W (L.map f‚ÇÅ) (L.map f‚ÇÇ) (LeftFraction‚ÇÇ.mk f‚ÇÅ f‚ÇÇ (ùüô _) (W.id_mem _))
    (LeftFraction.map_ofHom _ _ _ _).symm (LeftFraction.map_ofHom _ _ _ _).symm).trans
    (LeftFraction.map_ofHom _ _ _ _)

variable (L X Y)

/-- The abelian group structure on `L.obj X ‚ü∂ L.obj Y` when `L : C ‚•§ D` is a localization
functor, `C` is preadditive and there is a left calculus of fractions. -/
noncomputable def addCommGroup' : AddCommGroup (L.obj X ‚ü∂ L.obj Y) := by
  letI : Zero (L.obj X ‚ü∂ L.obj Y) := ‚ü®L.map 0‚ü©
  letI : Add (L.obj X ‚ü∂ L.obj Y) := ‚ü®add' W‚ü©
  letI : Neg (L.obj X ‚ü∂ L.obj Y) := ‚ü®neg' W‚ü©
  exact
    { add_assoc := add'_assoc _
      add_zero := add'_zero _
      add_comm := add'_comm _
      zero_add := zero_add' _
      add_left_neg := add'_left_neg' _
      nsmul := nsmulRec
      zsmul := zsmulRec }

variable {X Y}

variable {L}
variable {X' Y' Z' : D} (eX : L.obj X ‚âÖ X') (eY : L.obj Y ‚âÖ Y') (eZ : L.obj Z ‚âÖ Z')

/-- The bijection `(X' ‚ü∂ Y') ‚âÉ (L.obj X ‚ü∂ L.obj Y)` induced by isomorphisms
`eX : L.obj X ‚âÖ X'` and `eY : L.obj Y ‚âÖ Y'`. -/
@[simps]
def homEquiv : (X' ‚ü∂ Y') ‚âÉ (L.obj X ‚ü∂ L.obj Y) where
  toFun f := eX.hom ‚â´ f ‚â´ eY.inv
  invFun g := eX.inv ‚â´ g ‚â´ eY.hom
  left_inv _ := by simp
  right_inv _ := by simp

/-- The addition of morphisms in `D`, when `L : C ‚•§ D` is a localization
functor, `C` is preadditive and there is a left calculus of fractions. -/
noncomputable def add (f‚ÇÅ f‚ÇÇ : X' ‚ü∂ Y') : X' ‚ü∂ Y' :=
  (homEquiv eX eY).symm (add' W (homEquiv eX eY f‚ÇÅ) (homEquiv eX eY f‚ÇÇ))

@[reassoc]
lemma add_comp (f‚ÇÅ f‚ÇÇ : X' ‚ü∂ Y') (g : Y' ‚ü∂ Z') :
    add W eX eY f‚ÇÅ f‚ÇÇ ‚â´ g = add W eX eZ (f‚ÇÅ ‚â´ g) (f‚ÇÇ ‚â´ g) := by
  obtain ‚ü®f‚ÇÅ, rfl‚ü© := (homEquiv eX eY).symm.surjective f‚ÇÅ
  obtain ‚ü®f‚ÇÇ, rfl‚ü© := (homEquiv eX eY).symm.surjective f‚ÇÇ
  obtain ‚ü®g, rfl‚ü© := (homEquiv eY eZ).symm.surjective g
  simp [add]

@[reassoc]
lemma comp_add (f : X' ‚ü∂ Y') (g‚ÇÅ g‚ÇÇ : Y' ‚ü∂ Z') :
    f ‚â´ add W eY eZ g‚ÇÅ g‚ÇÇ = add W eX eZ (f ‚â´ g‚ÇÅ) (f ‚â´ g‚ÇÇ) := by
  obtain ‚ü®f, rfl‚ü© := (homEquiv eX eY).symm.surjective f
  obtain ‚ü®g‚ÇÅ, rfl‚ü© := (homEquiv eY eZ).symm.surjective g‚ÇÅ
  obtain ‚ü®g‚ÇÇ, rfl‚ü© := (homEquiv eY eZ).symm.surjective g‚ÇÇ
  simp [add]

lemma add_eq_add {X'' Y'' : C} (eX' : L.obj X'' ‚âÖ X') (eY' : L.obj Y'' ‚âÖ Y')
    (f‚ÇÅ f‚ÇÇ : X' ‚ü∂ Y') :
    add W eX eY f‚ÇÅ f‚ÇÇ = add W eX' eY' f‚ÇÅ f‚ÇÇ := by
  have h‚ÇÅ := comp_add W eX' eX eY (ùüô _) f‚ÇÅ f‚ÇÇ
  have h‚ÇÇ := add_comp W eX' eY eY' f‚ÇÅ f‚ÇÇ (ùüô _)
  simp only [Category.id_comp] at h‚ÇÅ
  simp only [Category.comp_id] at h‚ÇÇ
  rw [h‚ÇÅ, h‚ÇÇ]

variable (L X' Y') in
/-- The abelian group structure on morphisms in `D`, when `L : C ‚•§ D` is a localization
functor, `C` is preadditive and there is a left calculus of fractions. -/
noncomputable def addCommGroup : AddCommGroup (X' ‚ü∂ Y') := by
  have := Localization.essSurj L W
  letI := addCommGroup' L W (L.objPreimage X') (L.objPreimage Y')
  exact Equiv.addCommGroup (homEquiv (L.objObjPreimageIso X') (L.objObjPreimageIso Y'))

lemma add_eq (f‚ÇÅ f‚ÇÇ : X' ‚ü∂ Y') :
    letI := addCommGroup L W X' Y'
    f‚ÇÅ + f‚ÇÇ = add W eX eY f‚ÇÅ f‚ÇÇ := by
  apply add_eq_add

variable (L)

lemma map_add (f‚ÇÅ f‚ÇÇ : X ‚ü∂ Y) :
    letI := addCommGroup L W (L.obj X) (L.obj Y)
    L.map (f‚ÇÅ + f‚ÇÇ) = L.map f‚ÇÅ + L.map f‚ÇÇ := by
  rw [add_eq W (Iso.refl _) (Iso.refl _) (L.map f‚ÇÅ) (L.map f‚ÇÇ)]
  simp [add]

end ImplementationDetails

end Preadditive

/-- The preadditive structure on `D`, when `L : C ‚•§ D` is a localization
functor, `C` is preadditive and there is a left calculus of fractions. -/
noncomputable def preadditive : Preadditive D where
  homGroup := Preadditive.addCommGroup L W
  add_comp _ _ _ _ _ _ := by apply Preadditive.add_comp
  comp_add _ _ _ _ _ _ := by apply Preadditive.comp_add

lemma functor_additive :
    letI := preadditive L W
    L.Additive :=
  letI := preadditive L W
  ‚ü®by apply Preadditive.map_add‚ü©

attribute [irreducible] preadditive

noncomputable instance : Preadditive W.Localization := preadditive W.Q W

instance : W.Q.Additive := functor_additive W.Q W

variable [W.HasLocalization]

noncomputable instance : Preadditive W.Localization' := preadditive W.Q' W

instance : W.Q'.Additive := functor_additive W.Q' W

end Localization

end CategoryTheory
