/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Group.TransferInstance
import Mathlib.CategoryTheory.Localization.CalculusOfFractions.Fractions
import Mathlib.CategoryTheory.Localization.HasLocalization
import Mathlib.CategoryTheory.Preadditive.AdditiveFunctor

/-!
# The preadditive category structure on the localized category

In this file, it is shown that if `W : MorphismProperty C` has a left calculus
of fractions, and `C` is preadditive, then the localized category is preadditive,
and the localization functor is additive.

Let `L : C â¥¤ D` be a localization functor for `W`. We first construct an abelian
group structure on `L.obj X âŸ¶ L.obj Y` for `X` and `Y` in `C`. The addition
is defined using representatives of two morphisms in `L` as left fractions with
the same denominator thanks to the lemmas in
`CategoryTheory.Localization.CalculusOfFractions.Fractions`.
As `L` is essentially surjective, we finally transport these abelian group structures
to `X' âŸ¶ Y'` for all `X'` and `Y'` in `D`.

Preadditive category instances are defined on the categories `W.Localization`
(and `W.Localization'`) under the assumption the `W` has a left calculus of fractions.
(It would be easy to deduce from the results in this file that if `W` has a right calculus
of fractions, then the localized category can also be equipped with
a preadditive structure, but only one of these two constructions can be made an instance!)

-/

namespace CategoryTheory

open MorphismProperty Preadditive Limits Category

variable {C D : Type*} [Category C] [Category D] [Preadditive C] (L : C â¥¤ D)
  {W : MorphismProperty C} [L.IsLocalization W]

namespace MorphismProperty

/-- The opposite of a left fraction. -/
abbrev LeftFraction.neg {X Y : C} (Ï† : W.LeftFraction X Y) :
    W.LeftFraction X Y where
  Y' := Ï†.Y'
  f := -Ï†.f
  s := Ï†.s
  hs := Ï†.hs

namespace LeftFractionâ‚‚

variable {X Y : C} (Ï† : W.LeftFractionâ‚‚ X Y)

/-- The sum of two left fractions with the same denominator. -/
abbrev add : W.LeftFraction X Y where
  Y' := Ï†.Y'
  f := Ï†.f + Ï†.f'
  s := Ï†.s
  hs := Ï†.hs

@[simp]
lemma symm_add : Ï†.symm.add = Ï†.add := by
  dsimp [add, symm]
  congr 1
  apply add_comm

@[simp]
lemma map_add (F : C â¥¤ D) (hF : W.IsInvertedBy F) [Preadditive D] [F.Additive] :
    Ï†.add.map F hF = Ï†.fst.map F hF + Ï†.snd.map F hF := by
  have := hF Ï†.s Ï†.hs
  rw [â† cancel_mono (F.map Ï†.s), add_comp, LeftFraction.map_comp_map_s,
    LeftFraction.map_comp_map_s, LeftFraction.map_comp_map_s, F.map_add]

end LeftFractionâ‚‚

end MorphismProperty

variable (W)

namespace Localization

namespace Preadditive

section ImplementationDetails

/-! The definitions in this section (like `neg'` and `add'`) should never be used
directly. These are auxiliary definitions in order to construct the preadditive
structure `Localization.preadditive` (which is made irreducible). The user
should only rely on the fact that the localization functor is additive, as this
completely determines the preadditive structure on the localized category when
there is a calculus of left fractions. -/

variable [W.HasLeftCalculusOfFractions] {X Y Z : C}
variable {L}

/-- The opposite of a map `L.obj X âŸ¶ L.obj Y` when `L : C â¥¤ D` is a localization
functor, `C` is preadditive and there is a left calculus of fractions. -/
noncomputable def neg' (f : L.obj X âŸ¶ L.obj Y) : L.obj X âŸ¶ L.obj Y :=
  (exists_leftFraction L W f).choose.neg.map L (inverts L W)

lemma neg'_eq (f : L.obj X âŸ¶ L.obj Y) (Ï† : W.LeftFraction X Y)
    (hÏ† : f = Ï†.map L (inverts L W)) :
    neg' W f = Ï†.neg.map L (inverts L W) := by
  obtain âŸ¨Ï†â‚€, rfl, hÏ†â‚€âŸ© : âˆƒ (Ï†â‚€ : W.LeftFraction X Y)
    (_ : f = Ï†â‚€.map L (inverts L W)),
      neg' W f = Ï†â‚€.neg.map L (inverts L W) :=
    âŸ¨_, (exists_leftFraction L W f).choose_spec, rflâŸ©
  rw [MorphismProperty.LeftFraction.map_eq_iff] at hÏ†
  obtain âŸ¨Y', tâ‚, tâ‚‚, hst, hft, htâŸ© := hÏ†
  have := inverts L W _ ht
  rw [â† cancel_mono (L.map (Ï†â‚€.s â‰« tâ‚))]
  nth_rw 1 [L.map_comp]
  rw [hÏ†â‚€, hst, LeftFraction.map_comp_map_s_assoc, L.map_comp,
    LeftFraction.map_comp_map_s_assoc, â† L.map_comp, â† L.map_comp,
    neg_comp, neg_comp, hft]

/-- The addition of two maps `L.obj X âŸ¶ L.obj Y` when `L : C â¥¤ D` is a localization
functor, `C` is preadditive and there is a left calculus of fractions. -/
noncomputable def add' (fâ‚ fâ‚‚ : L.obj X âŸ¶ L.obj Y) : L.obj X âŸ¶ L.obj Y :=
  (exists_leftFractionâ‚‚ L W fâ‚ fâ‚‚).choose.add.map L (inverts L W)

lemma add'_eq (fâ‚ fâ‚‚ : L.obj X âŸ¶ L.obj Y) (Ï† : W.LeftFractionâ‚‚ X Y)
    (hÏ†â‚ : fâ‚ = Ï†.fst.map L (inverts L W))
    (hÏ†â‚‚ : fâ‚‚ = Ï†.snd.map L (inverts L W)) :
    add' W fâ‚ fâ‚‚ = Ï†.add.map L (inverts L W) := by
  obtain âŸ¨Ï†â‚€, rfl, rfl, hÏ†â‚€âŸ© : âˆƒ (Ï†â‚€ : W.LeftFractionâ‚‚ X Y)
    (_ : fâ‚ = Ï†â‚€.fst.map L (inverts L W))
    (_ : fâ‚‚ = Ï†â‚€.snd.map L (inverts L W)),
    add' W fâ‚ fâ‚‚ = Ï†â‚€.add.map L (inverts L W) :=
    âŸ¨(exists_leftFractionâ‚‚ L W fâ‚ fâ‚‚).choose,
      (exists_leftFractionâ‚‚ L W fâ‚ fâ‚‚).choose_spec.1,
      (exists_leftFractionâ‚‚ L W fâ‚ fâ‚‚).choose_spec.2, rflâŸ©
  obtain âŸ¨Z, tâ‚, tâ‚‚, hst, hft, hft', htâŸ© := (LeftFractionâ‚‚.map_eq_iff L W Ï†â‚€ Ï†).1 âŸ¨hÏ†â‚, hÏ†â‚‚âŸ©
  have := inverts L W _ ht
  rw [hÏ†â‚€, â† cancel_mono (L.map (Ï†â‚€.s â‰« tâ‚))]
  nth_rw 2 [hst]
  rw [L.map_comp, L.map_comp, LeftFraction.map_comp_map_s_assoc,
    LeftFraction.map_comp_map_s_assoc, â† L.map_comp, â† L.map_comp,
    add_comp, add_comp, hft, hft']

lemma add'_comm (fâ‚ fâ‚‚ : L.obj X âŸ¶ L.obj Y) :
    add' W fâ‚ fâ‚‚ = add' W fâ‚‚ fâ‚ := by
  obtain âŸ¨Î±, hâ‚, hâ‚‚âŸ© := exists_leftFractionâ‚‚ L W fâ‚ fâ‚‚
  rw [add'_eq W fâ‚ fâ‚‚ Î± hâ‚ hâ‚‚, add'_eq W fâ‚‚ fâ‚ Î±.symm hâ‚‚ hâ‚, Î±.symm_add]

lemma add'_zero (f : L.obj X âŸ¶ L.obj Y) :
    add' W f (L.map 0) = f := by
  obtain âŸ¨Î±, hÎ±âŸ© := exists_leftFraction L W f
  rw [add'_eq W f (L.map 0) (LeftFractionâ‚‚.mk Î±.f 0 Î±.s Î±.hs) hÎ±, hÎ±]; swap
  Â· have := inverts L W _ Î±.hs
    rw [â† cancel_mono (L.map Î±.s), â† L.map_comp, Limits.zero_comp,
      LeftFraction.map_comp_map_s]
  dsimp [LeftFractionâ‚‚.add]
  rw [add_zero]

lemma zero_add' (f : L.obj X âŸ¶ L.obj Y) :
    add' W (L.map 0) f = f := by
  rw [add'_comm, add'_zero]

lemma neg'_add'_self (f : L.obj X âŸ¶ L.obj Y) :
    add' W (neg' W f) f = L.map 0 := by
  obtain âŸ¨Î±, rflâŸ© := exists_leftFraction L W f
  have := inverts L W _ Î±.hs
  rw [add'_eq W _ _ (LeftFractionâ‚‚.mk (-Î±.f) Î±.f Î±.s Î±.hs) (neg'_eq W _ _ rfl) rfl]
  simp only [â† cancel_mono (L.map Î±.s), LeftFraction.map_comp_map_s, â† L.map_comp,
    Limits.zero_comp, neg_add_cancel]

lemma add'_assoc (fâ‚ fâ‚‚ fâ‚ƒ : L.obj X âŸ¶ L.obj Y) :
    add' W (add' W fâ‚ fâ‚‚) fâ‚ƒ = add' W fâ‚ (add' W fâ‚‚ fâ‚ƒ) := by
  obtain âŸ¨Î±, hâ‚, hâ‚‚, hâ‚ƒâŸ© := exists_leftFractionâ‚ƒ L W fâ‚ fâ‚‚ fâ‚ƒ
  rw [add'_eq W fâ‚ fâ‚‚ Î±.forgetThd hâ‚ hâ‚‚, add'_eq W fâ‚‚ fâ‚ƒ Î±.forgetFst hâ‚‚ hâ‚ƒ,
    add'_eq W _ _ (LeftFractionâ‚‚.mk (Î±.f + Î±.f') Î±.f'' Î±.s Î±.hs) rfl hâ‚ƒ,
    add'_eq W _ _ (LeftFractionâ‚‚.mk Î±.f (Î±.f' + Î±.f'') Î±.s Î±.hs) hâ‚ rfl]
  dsimp [LeftFractionâ‚‚.add]
  rw [add_assoc]

@[reassoc (attr := simp)]
lemma add'_comp (fâ‚ fâ‚‚ : L.obj X âŸ¶ L.obj Y) (g : L.obj Y âŸ¶ L.obj Z) :
    add' W fâ‚ fâ‚‚ â‰« g = add' W (fâ‚ â‰« g) (fâ‚‚ â‰« g) := by
  obtain âŸ¨Î±, hâ‚, hâ‚‚âŸ© := exists_leftFractionâ‚‚ L W fâ‚ fâ‚‚
  obtain âŸ¨Î², hÎ²âŸ© := exists_leftFraction L W g
  obtain âŸ¨Î³, hÎ³âŸ© := (RightFraction.mk _ Î±.hs Î².f).exists_leftFraction
  dsimp at hÎ³
  rw [add'_eq W fâ‚ fâ‚‚ Î± hâ‚ hâ‚‚, add'_eq W (fâ‚ â‰« g) (fâ‚‚ â‰« g)
    (LeftFractionâ‚‚.mk (Î±.f â‰« Î³.f) (Î±.f' â‰« Î³.f) (Î².s â‰« Î³.s)
    (W.comp_mem _ _ Î².hs Î³.hs))]; rotate_left
  Â· rw [hâ‚, hÎ²]
    exact LeftFraction.map_comp_map_eq_map _ _ _ hÎ³ _
  Â· rw [hâ‚‚, hÎ²]
    exact LeftFraction.map_comp_map_eq_map _ _ _ hÎ³ _
  rw [hÎ², LeftFraction.map_comp_map_eq_map _ _ Î³ hÎ³]
  dsimp [LeftFractionâ‚‚.add]
  rw [add_comp]

@[reassoc (attr := simp)]
lemma comp_add' (f : L.obj X âŸ¶ L.obj Y) (gâ‚ gâ‚‚ : L.obj Y âŸ¶ L.obj Z) :
    f â‰« add' W gâ‚ gâ‚‚ = add' W (f â‰« gâ‚) (f â‰« gâ‚‚) := by
  obtain âŸ¨Î±, hÎ±âŸ© := exists_leftFraction L W f
  obtain âŸ¨Î², hÎ²â‚, hÎ²â‚‚âŸ© := exists_leftFractionâ‚‚ L W gâ‚ gâ‚‚
  obtain âŸ¨Î³, hÎ³â‚, hÎ³â‚‚âŸ© := (RightFractionâ‚‚.mk _ Î±.hs Î².f Î².f').exists_leftFractionâ‚‚
  dsimp at hÎ³â‚ hÎ³â‚‚
  rw [add'_eq W gâ‚ gâ‚‚ Î² hÎ²â‚ hÎ²â‚‚, add'_eq W (f â‰« gâ‚) (f â‰« gâ‚‚)
    (LeftFractionâ‚‚.mk (Î±.f â‰« Î³.f) (Î±.f â‰« Î³.f') (Î².s â‰« Î³.s) (W.comp_mem _ _ Î².hs Î³.hs))
    (by simpa only [hÎ±, hÎ²â‚] using LeftFraction.map_comp_map_eq_map Î± Î².fst Î³.fst hÎ³â‚ L)
    (by simpa only [hÎ±, hÎ²â‚‚] using LeftFraction.map_comp_map_eq_map Î± Î².snd Î³.snd hÎ³â‚‚ L),
    hÎ±, LeftFraction.map_comp_map_eq_map Î± Î².add Î³.add
      (by simp only [add_comp, hÎ³â‚, hÎ³â‚‚, comp_add])]
  dsimp [LeftFractionâ‚‚.add]
  rw [comp_add]

@[simp]
lemma add'_map (fâ‚ fâ‚‚ : X âŸ¶ Y) :
    add' W (L.map fâ‚) (L.map fâ‚‚) = L.map (fâ‚ + fâ‚‚) :=
  (add'_eq W (L.map fâ‚) (L.map fâ‚‚) (LeftFractionâ‚‚.mk fâ‚ fâ‚‚ (ğŸ™ _) (W.id_mem _))
    (LeftFraction.map_ofHom _ _ _ _).symm (LeftFraction.map_ofHom _ _ _ _).symm).trans
    (LeftFraction.map_ofHom _ _ _ _)

variable (L X Y)

/-- The abelian group structure on `L.obj X âŸ¶ L.obj Y` when `L : C â¥¤ D` is a localization
functor, `C` is preadditive and there is a left calculus of fractions. -/
noncomputable def addCommGroup' : AddCommGroup (L.obj X âŸ¶ L.obj Y) := by
  letI : Zero (L.obj X âŸ¶ L.obj Y) := âŸ¨L.map 0âŸ©
  letI : Add (L.obj X âŸ¶ L.obj Y) := âŸ¨add' WâŸ©
  letI : Neg (L.obj X âŸ¶ L.obj Y) := âŸ¨neg' WâŸ©
  exact
    { add_assoc := add'_assoc _
      add_zero := add'_zero _
      add_comm := add'_comm _
      zero_add := zero_add' _
      neg_add_cancel := neg'_add'_self _
      nsmul := nsmulRec
      zsmul := zsmulRec }

variable {X Y}

variable {L}
variable {X' Y' Z' : D} (eX : L.obj X â‰… X') (eY : L.obj Y â‰… Y') (eZ : L.obj Z â‰… Z')

/-- The bijection `(X' âŸ¶ Y') â‰ƒ (L.obj X âŸ¶ L.obj Y)` induced by isomorphisms
`eX : L.obj X â‰… X'` and `eY : L.obj Y â‰… Y'`. -/
@[simps]
def homEquiv : (X' âŸ¶ Y') â‰ƒ (L.obj X âŸ¶ L.obj Y) where
  toFun f := eX.hom â‰« f â‰« eY.inv
  invFun g := eX.inv â‰« g â‰« eY.hom
  left_inv _ := by simp
  right_inv _ := by simp

/-- The addition of morphisms in `D`, when `L : C â¥¤ D` is a localization
functor, `C` is preadditive and there is a left calculus of fractions. -/
noncomputable def add (fâ‚ fâ‚‚ : X' âŸ¶ Y') : X' âŸ¶ Y' :=
  (homEquiv eX eY).symm (add' W (homEquiv eX eY fâ‚) (homEquiv eX eY fâ‚‚))

@[reassoc]
lemma add_comp (fâ‚ fâ‚‚ : X' âŸ¶ Y') (g : Y' âŸ¶ Z') :
    add W eX eY fâ‚ fâ‚‚ â‰« g = add W eX eZ (fâ‚ â‰« g) (fâ‚‚ â‰« g) := by
  obtain âŸ¨fâ‚, rflâŸ© := (homEquiv eX eY).symm.surjective fâ‚
  obtain âŸ¨fâ‚‚, rflâŸ© := (homEquiv eX eY).symm.surjective fâ‚‚
  obtain âŸ¨g, rflâŸ© := (homEquiv eY eZ).symm.surjective g
  simp [add]

@[reassoc]
lemma comp_add (f : X' âŸ¶ Y') (gâ‚ gâ‚‚ : Y' âŸ¶ Z') :
    f â‰« add W eY eZ gâ‚ gâ‚‚ = add W eX eZ (f â‰« gâ‚) (f â‰« gâ‚‚) := by
  obtain âŸ¨f, rflâŸ© := (homEquiv eX eY).symm.surjective f
  obtain âŸ¨gâ‚, rflâŸ© := (homEquiv eY eZ).symm.surjective gâ‚
  obtain âŸ¨gâ‚‚, rflâŸ© := (homEquiv eY eZ).symm.surjective gâ‚‚
  simp [add]

lemma add_eq_add {X'' Y'' : C} (eX' : L.obj X'' â‰… X') (eY' : L.obj Y'' â‰… Y')
    (fâ‚ fâ‚‚ : X' âŸ¶ Y') :
    add W eX eY fâ‚ fâ‚‚ = add W eX' eY' fâ‚ fâ‚‚ := by
  have hâ‚ := comp_add W eX' eX eY (ğŸ™ _) fâ‚ fâ‚‚
  have hâ‚‚ := add_comp W eX' eY eY' fâ‚ fâ‚‚ (ğŸ™ _)
  simp only [id_comp] at hâ‚
  simp only [comp_id] at hâ‚‚
  rw [hâ‚, hâ‚‚]

variable (L X' Y') in
/-- The abelian group structure on morphisms in `D`, when `L : C â¥¤ D` is a localization
functor, `C` is preadditive and there is a left calculus of fractions. -/
noncomputable def addCommGroup : AddCommGroup (X' âŸ¶ Y') := by
  have := Localization.essSurj L W
  letI := addCommGroup' L W (L.objPreimage X') (L.objPreimage Y')
  exact Equiv.addCommGroup (homEquiv (L.objObjPreimageIso X') (L.objObjPreimageIso Y'))

lemma add_eq (fâ‚ fâ‚‚ : X' âŸ¶ Y') :
    letI := addCommGroup L W X' Y'
    fâ‚ + fâ‚‚ = add W eX eY fâ‚ fâ‚‚ := by
  apply add_eq_add

variable (L)

lemma map_add (fâ‚ fâ‚‚ : X âŸ¶ Y) :
    letI := addCommGroup L W (L.obj X) (L.obj Y)
    L.map (fâ‚ + fâ‚‚) = L.map fâ‚ + L.map fâ‚‚ := by
  rw [add_eq W (Iso.refl _) (Iso.refl _) (L.map fâ‚) (L.map fâ‚‚)]
  simp [add]

end ImplementationDetails

end Preadditive

variable [W.HasLeftCalculusOfFractions]

/-- The preadditive structure on `D`, when `L : C â¥¤ D` is a localization
functor, `C` is preadditive and there is a left calculus of fractions. -/
noncomputable def preadditive : Preadditive D where
  homGroup := Preadditive.addCommGroup L W
  add_comp _ _ _ _ _ _ := by apply Preadditive.add_comp
  comp_add _ _ _ _ _ _ := by apply Preadditive.comp_add

lemma functor_additive :
    letI := preadditive L W
    L.Additive :=
  letI := preadditive L W
  âŸ¨by apply Preadditive.map_addâŸ©

attribute [irreducible] preadditive
include W in
lemma functor_additive_iff {E : Type*} [Category E] [Preadditive E] [Preadditive D] [L.Additive]
    (G : D â¥¤ E) :
    G.Additive â†” (L â‹™ G).Additive := by
  constructor
  Â· intro
    infer_instance
  Â· intro h
    suffices âˆ€ â¦ƒX Y : Câ¦„ (f g : L.obj X âŸ¶ L.obj Y), G.map (f + g) = G.map f + G.map g by
      refine âŸ¨fun {X Y f g} => ?_âŸ©
      have hL := essSurj L W
      have eq := this ((L.objObjPreimageIso X).hom â‰« f â‰« (L.objObjPreimageIso Y).inv)
        ((L.objObjPreimageIso X).hom â‰« g â‰« (L.objObjPreimageIso Y).inv)
      rw [Functor.map_comp, Functor.map_comp, Functor.map_comp, Functor.map_comp,
        â† comp_add, â† comp_add, â† add_comp, â† add_comp, Functor.map_comp, Functor.map_comp] at eq
      rw [â† cancel_mono (G.map (L.objObjPreimageIso Y).inv),
        â† cancel_epi (G.map (L.objObjPreimageIso X).hom), eq]
    intros X Y f g
    obtain âŸ¨Ï†, rfl, rflâŸ© := exists_leftFractionâ‚‚ L W f g
    have := Localization.inverts L W Ï†.s Ï†.hs
    rw [â† Ï†.map_add L (inverts L W), â† cancel_mono (G.map (L.map Ï†.s)), â† G.map_comp,
      add_comp, â† G.map_comp, â† G.map_comp, LeftFraction.map_comp_map_s,
      LeftFraction.map_comp_map_s, LeftFraction.map_comp_map_s, â† Functor.comp_map,
      Functor.map_add, Functor.comp_map, Functor.comp_map]

noncomputable instance : Preadditive W.Localization := preadditive W.Q W
instance : W.Q.Additive := functor_additive W.Q W
instance [HasZeroObject C] : HasZeroObject W.Localization := W.Q.hasZeroObject_of_additive

variable [W.HasLocalization]

noncomputable instance : Preadditive W.Localization' := preadditive W.Q' W
instance : W.Q'.Additive := functor_additive W.Q' W
instance [HasZeroObject C] : HasZeroObject W.Localization' := W.Q'.hasZeroObject_of_additive

end Localization

end CategoryTheory
