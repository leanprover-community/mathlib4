/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Localization.Opposite

/-!
# Calculus of fractions

Following the definitions by [Gabriel and Zisman][gabriel-zisman-1967],
given a morphism property `W : MorphismProperty C` on a category `C`,
we introduce the class `W.HasLeftCalculusOfFractions`. The main
result (TODO) is that if `L : C â¥¤ D` is a localization functor for `W`,
then for any morphism `L.obj X âŸ¶ L.obj Y` in `D`, there exists an auxiliary
object `Y' : C` and morphisms `g : X âŸ¶ Y'` and `s : Y âŸ¶ Y'`, with `W s`, such
that the given morphism is a sort of fraction `g / s`, or more precisely of
the form `L.map g â‰« (Localization.isoOfHom L W s hs).inv`.

## References

* [P. Gabriel, M. Zisman, *Calculus of fractions and homotopy theory*][gabriel-zisman-1967]

-/

namespace CategoryTheory

open Category

namespace MorphismProperty

variable {C D : Type _} [Category C] [Category D]

/-- A left fraction from `X : C` to `Y : C` for `W : MorphismProperty C` consists of the
datum of an object `Y' : C` and maps `f : X âŸ¶ Y'` and `s : Y âŸ¶ Y'` such that `W s`. -/
structure LeftFraction (W : MorphismProperty C) (X Y : C) where
  /-- the auxiliary object of a left fraction -/
  {Y' : C}
  /-- the numerator of a left fraction -/
  f : X âŸ¶ Y'
  /-- the denominator of a left fraction -/
  s : Y âŸ¶ Y'
  /-- the condition that the denominator belongs to the given morphism property -/
  hs : W s

namespace LeftFraction

variable (W : MorphismProperty C) {X Y : C}

/-- The left fraction from `X` to `Y` given by a morphism `f : X âŸ¶ Y`. -/
@[simps]
def ofHom (f : X âŸ¶ Y) [W.ContainsIdentities] :
    W.LeftFraction X Y := mk f (ğŸ™ Y) (W.id_mem Y)

variable {W}

/-- The left fraction from `X` to `Y` given by a morphism `s : Y âŸ¶ X` such that `W s`. -/
@[simps]
def ofInv (s : Y âŸ¶ X) (hs : W s) :
    W.LeftFraction X Y := mk (ğŸ™ X) s hs

/-- If `Ï† : W.LeftFraction X Y` and `L` is a functor which inverts `W`, this is the
induced morphism `L.obj X âŸ¶ L.obj Y`  -/
noncomputable def map (Ï† : W.LeftFraction X Y) (L : C â¥¤ D) (hL : W.IsInvertedBy L) :
    L.obj X âŸ¶ L.obj Y :=
  have := hL _ Ï†.hs
  L.map Ï†.f â‰« inv (L.map Ï†.s)

@[reassoc (attr := simp)]
lemma map_comp_map_s (Ï† : W.LeftFraction X Y) (L : C â¥¤ D) (hL : W.IsInvertedBy L) :
    Ï†.map L hL â‰« L.map Ï†.s = L.map Ï†.f := by
  letI := hL _ Ï†.hs
  simp [map]

variable (W)

lemma map_ofHom (f : X âŸ¶ Y) (L : C â¥¤ D) (hL : W.IsInvertedBy L) [W.ContainsIdentities] :
    (ofHom W f).map L hL = L.map f := by
  simp [map]

@[reassoc (attr := simp)]
lemma map_ofInv_hom_id (s : Y âŸ¶ X) (hs : W s) (L : C â¥¤ D) (hL : W.IsInvertedBy L) :
    (ofInv s hs).map L hL â‰« L.map s = ğŸ™ _ := by
  letI := hL _ hs
  simp [map]

@[reassoc (attr := simp)]
lemma map_hom_ofInv_id (s : Y âŸ¶ X) (hs : W s) (L : C â¥¤ D) (hL : W.IsInvertedBy L) :
    L.map s â‰« (ofInv s hs).map L hL = ğŸ™ _ := by
  letI := hL _ hs
  simp [map]

variable {W}

lemma cases (Î± : W.LeftFraction X Y) :
    âˆƒ (Y' : C) (f : X âŸ¶ Y') (s : Y âŸ¶ Y') (hs : W s), Î± = LeftFraction.mk f s hs :=
  âŸ¨_, _, _, _, rflâŸ©

end LeftFraction

/-- A right fraction from `X : C` to `Y : C` for `W : MorphismProperty C` consists of the
datum of an object `X' : C` and maps `s : X' âŸ¶ X` and `f : X' âŸ¶ Y` such that `W s`. -/
structure RightFraction (W : MorphismProperty C) (X Y : C) where
  /-- the auxiliary object of a right fraction -/
  {X' : C}
  /-- the denominator of a right fraction -/
  s : X' âŸ¶ X
  /-- the condition that the denominator belongs to the given morphism property -/
  hs : W s
  /-- the numerator of a right fraction -/
  f : X' âŸ¶ Y

namespace RightFraction

variable (W : MorphismProperty C)

variable {X Y : C}

/-- The right fraction from `X` to `Y` given by a morphism `f : X âŸ¶ Y`. -/
@[simps]
def ofHom (f : X âŸ¶ Y) [W.ContainsIdentities] :
    W.RightFraction X Y := mk (ğŸ™ X) (W.id_mem X) f

variable {W}

/-- The right fraction from `X` to `Y` given by a morphism `s : Y âŸ¶ X` such that `W s`. -/
@[simps]
def ofInv (s : Y âŸ¶ X) (hs : W s) :
    W.RightFraction X Y := mk s hs (ğŸ™ Y)

/-- If `Ï† : W.RightFraction X Y` and `L` is a functor which inverts `W`, this is the
induced morphism `L.obj X âŸ¶ L.obj Y`  -/
noncomputable def map (Ï† : W.RightFraction X Y) (L : C â¥¤ D) (hL : W.IsInvertedBy L) :
    L.obj X âŸ¶ L.obj Y :=
  have := hL _ Ï†.hs
  inv (L.map Ï†.s) â‰« L.map Ï†.f

@[reassoc (attr := simp)]
lemma map_s_comp_map (Ï† : W.RightFraction X Y) (L : C â¥¤ D) (hL : W.IsInvertedBy L) :
    L.map Ï†.s â‰« Ï†.map L hL = L.map Ï†.f := by
  letI := hL _ Ï†.hs
  simp [map]

variable (W)

@[simp]
lemma map_ofHom (f : X âŸ¶ Y) (L : C â¥¤ D) (hL : W.IsInvertedBy L) [W.ContainsIdentities] :
    (ofHom W f).map L hL = L.map f := by
  simp [map]

@[reassoc (attr := simp)]
lemma map_ofInv_hom_id (s : Y âŸ¶ X) (hs : W s) (L : C â¥¤ D) (hL : W.IsInvertedBy L) :
    (ofInv s hs).map L hL â‰« L.map s = ğŸ™ _ := by
  letI := hL _ hs
  simp [map]

@[reassoc (attr := simp)]
lemma map_hom_ofInv_id (s : Y âŸ¶ X) (hs : W s) (L : C â¥¤ D) (hL : W.IsInvertedBy L) :
    L.map s â‰« (ofInv s hs).map L hL = ğŸ™ _ := by
  letI := hL _ hs
  simp [map]

variable {W}

lemma cases (Î± : W.RightFraction X Y) :
    âˆƒ (X' : C) (s : X' âŸ¶ X) (hs : W s) (f : X' âŸ¶ Y) , Î± = RightFraction.mk s hs f :=
  âŸ¨_, _, _, _, rflâŸ©

end RightFraction

variable (W : MorphismProperty C)

/-- A multiplicative morphism property `W` has left calculus of fractions if
any right fraction can be turned into a left fraction and that two morphisms
that can be equalized by precomposition with a morphism in `W` can also
be equalized by postcomposition with a morphism in `W`. -/
class HasLeftCalculusOfFractions extends W.IsMultiplicative : Prop where
  exists_leftFraction â¦ƒX Y : Câ¦„ (Ï† : W.RightFraction X Y) :
    âˆƒ (Ïˆ : W.LeftFraction X Y), Ï†.f â‰« Ïˆ.s = Ï†.s â‰« Ïˆ.f
  ext : âˆ€ â¦ƒX' X Y : Câ¦„ (fâ‚ fâ‚‚ : X âŸ¶ Y) (s : X' âŸ¶ X) (_ : W s)
    (_ : s â‰« fâ‚ = s â‰« fâ‚‚), âˆƒ (Y' : C) (t : Y âŸ¶ Y') (_ : W t), fâ‚ â‰« t = fâ‚‚ â‰« t

/-- A multiplicative morphism property `W` has right calculus of fractions if
any left fraction can be turned into a right fraction and that two morphisms
that can be equalized by postcomposition with a morphism in `W` can also
be equalized by precomposition with a morphism in `W`. -/
class HasRightCalculusOfFractions extends W.IsMultiplicative : Prop where
  exists_rightFraction â¦ƒX Y : Câ¦„ (Ï† : W.LeftFraction X Y) :
    âˆƒ (Ïˆ : W.RightFraction X Y), Ïˆ.s â‰« Ï†.f = Ïˆ.f â‰« Ï†.s
  ext : âˆ€ â¦ƒX Y Y' : Câ¦„ (fâ‚ fâ‚‚ : X âŸ¶ Y) (s : Y âŸ¶ Y') (_ : W s)
    (_ : fâ‚ â‰« s = fâ‚‚ â‰« s), âˆƒ (X' : C) (t : X' âŸ¶ X) (_ : W t), t â‰« fâ‚ = t â‰« fâ‚‚

variable {W}

lemma RightFraction.exists_leftFraction [W.HasLeftCalculusOfFractions] {X Y : C}
    (Ï† : W.RightFraction X Y) : âˆƒ (Ïˆ : W.LeftFraction X Y), Ï†.f â‰« Ïˆ.s = Ï†.s â‰« Ïˆ.f :=
  HasLeftCalculusOfFractions.exists_leftFraction Ï†

/-- A choice of a left fraction deduced from a right fraction for a morphism property `W`
when `W` has left calculus of fractions. -/
noncomputable def RightFraction.leftFraction [W.HasLeftCalculusOfFractions] {X Y : C}
    (Ï† : W.RightFraction X Y) : W.LeftFraction X Y :=
  Ï†.exists_leftFraction.choose

@[reassoc]
lemma RightFraction.leftFraction_fac [W.HasLeftCalculusOfFractions] {X Y : C}
    (Ï† : W.RightFraction X Y) : Ï†.f â‰« Ï†.leftFraction.s = Ï†.s â‰« Ï†.leftFraction.f :=
  Ï†.exists_leftFraction.choose_spec

lemma LeftFraction.exists_rightFraction [W.HasRightCalculusOfFractions] {X Y : C}
    (Ï† : W.LeftFraction X Y) : âˆƒ (Ïˆ : W.RightFraction X Y), Ïˆ.s â‰« Ï†.f = Ïˆ.f â‰« Ï†.s :=
  HasRightCalculusOfFractions.exists_rightFraction Ï†

/-- A choice of a right fraction deduced from a left fraction for a morphism property `W`
when `W` has right calculus of fractions. -/
noncomputable def LeftFraction.rightFraction [W.HasRightCalculusOfFractions] {X Y : C}
    (Ï† : W.LeftFraction X Y) : W.RightFraction X Y :=
  Ï†.exists_rightFraction.choose

@[reassoc]
lemma LeftFraction.rightFraction_fac [W.HasRightCalculusOfFractions] {X Y : C}
    (Ï† : W.LeftFraction X Y) : Ï†.rightFraction.s â‰« Ï†.f = Ï†.rightFraction.f â‰« Ï†.s :=
  Ï†.exists_rightFraction.choose_spec

/-- The equivalence relation on left fractions for a morphism property `W`. -/
def LeftFractionRel {X Y : C} (zâ‚ zâ‚‚ : W.LeftFraction X Y) : Prop :=
  âˆƒ (Z : C)  (tâ‚ : zâ‚.Y' âŸ¶ Z) (tâ‚‚ : zâ‚‚.Y' âŸ¶ Z) (_ : zâ‚.s â‰« tâ‚ = zâ‚‚.s â‰« tâ‚‚)
    (_ : zâ‚.f â‰« tâ‚ = zâ‚‚.f â‰« tâ‚‚), W (zâ‚.s â‰« tâ‚)

namespace LeftFractionRel

lemma refl {X Y : C} (z : W.LeftFraction X Y) : LeftFractionRel z z :=
  âŸ¨z.Y', ğŸ™ _, ğŸ™ _, rfl, rfl, by simpa only [Category.comp_id] using z.hsâŸ©

lemma symm {X Y : C} {zâ‚ zâ‚‚ : W.LeftFraction X Y} (h : LeftFractionRel zâ‚ zâ‚‚) :
    LeftFractionRel zâ‚‚ zâ‚ := by
  obtain âŸ¨Z, tâ‚, tâ‚‚, hst, hft, htâŸ© := h
  exact âŸ¨Z, tâ‚‚, tâ‚, hst.symm, hft.symm, by simpa only [â† hst] using htâŸ©

lemma trans {X Y : C} {zâ‚ zâ‚‚ zâ‚ƒ : W.LeftFraction X Y}
    [HasLeftCalculusOfFractions W]
    (hâ‚â‚‚ : LeftFractionRel zâ‚ zâ‚‚) (hâ‚‚â‚ƒ : LeftFractionRel zâ‚‚ zâ‚ƒ) :
    LeftFractionRel zâ‚ zâ‚ƒ := by
  obtain âŸ¨Zâ‚„, tâ‚, tâ‚‚, hst, hft, htâŸ© := hâ‚â‚‚
  obtain âŸ¨Zâ‚…, uâ‚‚, uâ‚ƒ, hsu, hfu, huâŸ© := hâ‚‚â‚ƒ
  obtain âŸ¨âŸ¨vâ‚„, vâ‚…, hvâ‚…âŸ©, facâŸ© := HasLeftCalculusOfFractions.exists_leftFraction
    (RightFraction.mk (zâ‚.s â‰« tâ‚) ht (zâ‚ƒ.s â‰« uâ‚ƒ))
  simp only [Category.assoc] at fac
  have eq : zâ‚‚.s â‰« uâ‚‚ â‰« vâ‚…  = zâ‚‚.s â‰« tâ‚‚ â‰« vâ‚„ := by
    simpa only [â† reassoc_of% hsu, reassoc_of% hst] using fac
  obtain âŸ¨Zâ‚‡, w, hw, fac'âŸ© := HasLeftCalculusOfFractions.ext _ _ _ zâ‚‚.hs eq
  simp only [Category.assoc] at fac'
  refine' âŸ¨Zâ‚‡, tâ‚ â‰« vâ‚„ â‰« w, uâ‚ƒ â‰« vâ‚… â‰« w, _, _, _âŸ©
  Â· rw [reassoc_of% fac]
  Â· rw [reassoc_of% hft, â† fac', reassoc_of% hfu]
  Â· rw [â† reassoc_of% fac, â† reassoc_of% hsu, â† Category.assoc]
    exact W.comp_mem _ _ hu (W.comp_mem _ _ hvâ‚… hw)

end LeftFractionRel

section

variable [W.HasLeftCalculusOfFractions] (W)

lemma equivalenceLeftFractionRel (X Y : C) :
    @_root_.Equivalence (W.LeftFraction X Y) LeftFractionRel where
  refl := LeftFractionRel.refl
  symm := LeftFractionRel.symm
  trans := LeftFractionRel.trans

variable {W}

namespace LeftFraction

open HasLeftCalculusOfFractions

/-- Auxiliary definition for the composition of left fractions. -/
@[simp]
def compâ‚€ {X Y Z : C} (zâ‚ : W.LeftFraction X Y) (zâ‚‚ : W.LeftFraction Y Z)
    (zâ‚ƒ : W.LeftFraction zâ‚.Y' zâ‚‚.Y') :
    W.LeftFraction X Z :=
  mk (zâ‚.f â‰« zâ‚ƒ.f) (zâ‚‚.s â‰« zâ‚ƒ.s) (W.comp_mem _ _ zâ‚‚.hs zâ‚ƒ.hs)

/-- The equivalence class of `zâ‚.compâ‚€ zâ‚‚ zâ‚ƒ` does not depend on the choice of `zâ‚ƒ` provided
they satisfy the compatibility `zâ‚‚.f â‰« zâ‚ƒ.s = zâ‚.s â‰« zâ‚ƒ.f`. -/
lemma compâ‚€_rel {X Y Z : C} (zâ‚ : W.LeftFraction X Y) (zâ‚‚ : W.LeftFraction Y Z)
    (zâ‚ƒ zâ‚ƒ' : W.LeftFraction zâ‚.Y' zâ‚‚.Y') (hâ‚ƒ : zâ‚‚.f â‰« zâ‚ƒ.s = zâ‚.s â‰« zâ‚ƒ.f)
    (hâ‚ƒ' : zâ‚‚.f â‰« zâ‚ƒ'.s = zâ‚.s â‰« zâ‚ƒ'.f) :
    LeftFractionRel (zâ‚.compâ‚€ zâ‚‚ zâ‚ƒ) (zâ‚.compâ‚€ zâ‚‚ zâ‚ƒ') := by
  obtain âŸ¨zâ‚„, facâŸ© := exists_leftFraction (RightFraction.mk zâ‚ƒ.s zâ‚ƒ.hs zâ‚ƒ'.s)
  dsimp at fac
  have eq : zâ‚.s â‰« zâ‚ƒ.f â‰« zâ‚„.f = zâ‚.s â‰« zâ‚ƒ'.f â‰« zâ‚„.s := by
    rw [â† reassoc_of% hâ‚ƒ, â† reassoc_of% hâ‚ƒ', fac]
  obtain âŸ¨Y, t, ht, fac'âŸ© := ext _ _ _ zâ‚.hs eq
  simp only [assoc] at fac'
  refine' âŸ¨Y, zâ‚„.f â‰« t, zâ‚„.s â‰« t, _, _, _âŸ©
  Â· simp only [compâ‚€, assoc, reassoc_of% fac]
  Â· simp only [compâ‚€, assoc, fac']
  Â· simp only [compâ‚€, assoc, â† reassoc_of% fac]
    exact W.comp_mem _ _ zâ‚‚.hs (W.comp_mem _ _ zâ‚ƒ'.hs (W.comp_mem _ _ zâ‚„.hs ht))

variable (W)

/-- The morphisms in the constructed localized category for a morphism property `W`
that has left calculus of fractions are equivalence classes of left fractions. -/
def Localization.Hom (X Y : C) :=
  Quot (LeftFractionRel : W.LeftFraction X Y â†’ W.LeftFraction X Y â†’ Prop)

variable {W}

/-- The morphism in the constructed localized category that is induced by a left fraction. -/
def Localization.Hom.mk {X Y : C} (z : W.LeftFraction X Y) : Localization.Hom W X Y :=
  Quot.mk _ z

lemma Localization.Hom.mk_surjective {X Y : C} (f : Localization.Hom W X Y) :
    âˆƒ (z : W.LeftFraction X Y), f = mk z := by
  obtain âŸ¨zâŸ© := f
  exact âŸ¨z, rflâŸ©

/-- Auxiliary definition towards the definition of the composition of morphisms
in the constructed localized category for a morphism property that has
left calculus of fractions. -/
noncomputable def comp {X Y Z : C} (zâ‚ : W.LeftFraction X Y) (zâ‚‚ : W.LeftFraction Y Z) :
    Localization.Hom W X Z :=
  Localization.Hom.mk (zâ‚.compâ‚€ zâ‚‚ (RightFraction.mk zâ‚.s zâ‚.hs zâ‚‚.f).leftFraction)

lemma comp_eq {X Y Z : C} (zâ‚ : W.LeftFraction X Y) (zâ‚‚ : W.LeftFraction Y Z)
    (zâ‚ƒ : W.LeftFraction zâ‚.Y' zâ‚‚.Y') (hâ‚ƒ : zâ‚‚.f â‰« zâ‚ƒ.s = zâ‚.s â‰« zâ‚ƒ.f) :
    zâ‚.comp zâ‚‚ = Localization.Hom.mk (zâ‚.compâ‚€ zâ‚‚ zâ‚ƒ) :=
  Quot.sound (LeftFraction.compâ‚€_rel _ _ _ _
    (RightFraction.leftFraction_fac (RightFraction.mk zâ‚.s zâ‚.hs zâ‚‚.f)) hâ‚ƒ)

namespace Localization

/-- Composition of morphisms in the constructed localized category
for a morphism property that has left calculus of fractions. -/
noncomputable def Hom.comp {X Y Z : C} (zâ‚ : Hom W X Y) (zâ‚‚ : Hom W Y Z) : Hom W X Z := by
  refine' Quot.liftâ‚‚ (fun a b => a.comp b) _ _ zâ‚ zâ‚‚
  Â· rintro a bâ‚ bâ‚‚ âŸ¨U, tâ‚, tâ‚‚, hst, hft, htâŸ©
    obtain âŸ¨zâ‚, facâ‚âŸ© := exists_leftFraction (RightFraction.mk a.s a.hs bâ‚.f)
    obtain âŸ¨zâ‚‚, facâ‚‚âŸ© := exists_leftFraction (RightFraction.mk a.s a.hs bâ‚‚.f)
    obtain âŸ¨wâ‚, facâ‚'âŸ© := exists_leftFraction (RightFraction.mk zâ‚.s zâ‚.hs tâ‚)
    obtain âŸ¨wâ‚‚, facâ‚‚'âŸ© := exists_leftFraction (RightFraction.mk zâ‚‚.s zâ‚‚.hs tâ‚‚)
    obtain âŸ¨u, facâ‚ƒâŸ© := exists_leftFraction (RightFraction.mk wâ‚.s wâ‚.hs wâ‚‚.s)
    dsimp at facâ‚ facâ‚‚ facâ‚' facâ‚‚' facâ‚ƒ âŠ¢
    have eq : a.s â‰« zâ‚.f â‰« wâ‚.f â‰« u.f = a.s â‰« zâ‚‚.f â‰« wâ‚‚.f â‰« u.s := by
      rw [â† reassoc_of% facâ‚, â† reassoc_of% facâ‚‚, â† reassoc_of% facâ‚', â† reassoc_of% facâ‚‚',
        reassoc_of% hft, facâ‚ƒ]
    obtain âŸ¨Z, p, hp, facâ‚„âŸ© := ext _ _ _ a.hs eq
    simp only [assoc] at facâ‚„
    rw [comp_eq _ _ zâ‚ facâ‚, comp_eq _ _ zâ‚‚ facâ‚‚]
    apply Quot.sound
    refine' âŸ¨Z, wâ‚.f â‰« u.f â‰« p, wâ‚‚.f â‰« u.s â‰« p, _, _, _âŸ©
    Â· dsimp
      simp only [assoc, â† reassoc_of% facâ‚', â† reassoc_of% facâ‚‚',
        reassoc_of% hst, reassoc_of% facâ‚ƒ]
    Â· dsimp
      simp only [assoc, facâ‚„]
    Â· dsimp
      simp only [assoc]
      rw [â† reassoc_of% facâ‚', â† reassoc_of% facâ‚ƒ, â† assoc]
      exact W.comp_mem _ _ ht (W.comp_mem _ _ wâ‚‚.hs (W.comp_mem _ _ u.hs hp))
  Â· rintro aâ‚ aâ‚‚ b âŸ¨U, tâ‚, tâ‚‚, hst, hft, htâŸ©
    obtain âŸ¨zâ‚, facâ‚âŸ© := exists_leftFraction (RightFraction.mk aâ‚.s aâ‚.hs b.f)
    obtain âŸ¨zâ‚‚, facâ‚‚âŸ© := exists_leftFraction (RightFraction.mk aâ‚‚.s aâ‚‚.hs b.f)
    obtain âŸ¨wâ‚, facâ‚'âŸ© := exists_leftFraction (RightFraction.mk (aâ‚.s â‰« tâ‚) ht (b.f â‰« zâ‚.s))
    obtain âŸ¨wâ‚‚, facâ‚‚'âŸ© := exists_leftFraction (RightFraction.mk (aâ‚‚.s â‰« tâ‚‚)
      (show W _ by rw [â† hst]; exact ht) (b.f â‰« zâ‚‚.s))
    let pâ‚ : W.LeftFraction X Z := LeftFraction.mk (aâ‚.f â‰« tâ‚ â‰« wâ‚.f) (b.s â‰« zâ‚.s â‰« wâ‚.s)
      (W.comp_mem _ _ b.hs (W.comp_mem _ _ zâ‚.hs wâ‚.hs))
    let pâ‚‚ : W.LeftFraction X Z := LeftFraction.mk (aâ‚‚.f â‰« tâ‚‚ â‰« wâ‚‚.f) (b.s â‰« zâ‚‚.s â‰« wâ‚‚.s)
      (W.comp_mem _ _ b.hs (W.comp_mem _ _ zâ‚‚.hs wâ‚‚.hs))
    dsimp at facâ‚ facâ‚‚ facâ‚' facâ‚‚' âŠ¢
    simp only [assoc] at facâ‚' facâ‚‚'
    rw [comp_eq _ _ zâ‚ facâ‚, comp_eq _ _ zâ‚‚ facâ‚‚]
    apply Quot.sound
    refine' LeftFractionRel.trans _ ((_ : LeftFractionRel pâ‚ pâ‚‚).trans _)
    Â· have eq : aâ‚.s â‰« zâ‚.f â‰« wâ‚.s = aâ‚.s â‰« tâ‚ â‰« wâ‚.f := by rw [â† facâ‚', reassoc_of% facâ‚]
      obtain âŸ¨Z, u, hu, facâ‚ƒâŸ© := ext _ _ _ aâ‚.hs eq
      simp only [assoc] at facâ‚ƒ
      refine' âŸ¨Z, wâ‚.s â‰« u, u, _, _, _âŸ©
      Â· dsimp
        simp only [assoc]
      Â· dsimp
        simp only [assoc, facâ‚ƒ]
      Â· dsimp
        simp only [assoc]
        exact W.comp_mem _ _ b.hs (W.comp_mem _ _ zâ‚.hs (W.comp_mem _ _ wâ‚.hs hu))
    Â· obtain âŸ¨q, facâ‚ƒâŸ© := exists_leftFraction (RightFraction.mk (zâ‚.s â‰« wâ‚.s)
        (W.comp_mem _ _ zâ‚.hs wâ‚.hs) (zâ‚‚.s â‰« wâ‚‚.s))
      dsimp at facâ‚ƒ
      simp only [assoc] at facâ‚ƒ
      have eq : aâ‚.s â‰« tâ‚ â‰« wâ‚.f â‰« q.f = aâ‚.s â‰« tâ‚ â‰« wâ‚‚.f â‰« q.s := by
        rw [â† reassoc_of% facâ‚', â† facâ‚ƒ, reassoc_of% hst, reassoc_of% facâ‚‚']
      obtain âŸ¨Z, u, hu, facâ‚„âŸ© := ext _ _ _ aâ‚.hs eq
      simp only [assoc] at facâ‚„
      refine' âŸ¨Z, q.f â‰« u, q.s â‰« u, _, _, _âŸ©
      Â· simp only [assoc, reassoc_of% facâ‚ƒ]
      Â· rw [assoc, assoc, assoc, assoc, facâ‚„, reassoc_of% hft]
      Â· simp only [assoc, â† reassoc_of% facâ‚ƒ]
        exact W.comp_mem _ _ b.hs (W.comp_mem _ _ zâ‚‚.hs
          (W.comp_mem _ _ wâ‚‚.hs (W.comp_mem _ _ q.hs hu)))
    Â· have eq : aâ‚‚.s â‰« zâ‚‚.f â‰« wâ‚‚.s = aâ‚‚.s â‰« tâ‚‚ â‰« wâ‚‚.f := by
        rw [â† facâ‚‚', reassoc_of% facâ‚‚]
      obtain âŸ¨Z, u, hu, facâ‚„âŸ© := ext _ _ _ aâ‚‚.hs eq
      simp only [assoc] at facâ‚„
      refine' âŸ¨Z, u, wâ‚‚.s â‰« u, _, _, _âŸ©
      Â· dsimp
        simp only [assoc]
      Â· dsimp
        simp only [assoc, facâ‚„]
      Â· dsimp
        simp only [assoc]
        exact W.comp_mem _ _ b.hs (W.comp_mem _ _ zâ‚‚.hs (W.comp_mem _ _ wâ‚‚.hs hu))

lemma Hom.comp_eq {X Y Z : C} (zâ‚ : W.LeftFraction X Y) (zâ‚‚ : W.LeftFraction Y Z) :
    Hom.comp (mk zâ‚) (mk zâ‚‚) = zâ‚.comp zâ‚‚ := rfl

end Localization

/-- The constructed localized category for a morphism property
that has left calculus of fractions. -/
@[nolint unusedArguments]
def Localization (_ : MorphismProperty C) := C

namespace Localization

noncomputable instance : Category (Localization W) where
  Hom X Y := Localization.Hom W X Y
  id X := Localization.Hom.mk (ofHom W (ğŸ™ _))
  comp f g := f.comp g
  comp_id := by
    rintro (X Y : C) f
    obtain âŸ¨z, rflâŸ© := Hom.mk_surjective f
    change (Hom.mk z).comp (Hom.mk (ofHom W (ğŸ™ Y))) = Hom.mk z
    rw [Hom.comp_eq, comp_eq z (ofHom W (ğŸ™ Y)) (ofInv z.s z.hs) (by simp)]
    dsimp [compâ‚€]
    simp only [comp_id, id_comp]
  id_comp := by
    rintro (X Y : C) f
    obtain âŸ¨z, rflâŸ© := Hom.mk_surjective f
    change (Hom.mk (ofHom W (ğŸ™ X))).comp (Hom.mk z) = Hom.mk z
    rw [Hom.comp_eq, comp_eq (ofHom W (ğŸ™ X)) z (ofHom W z.f) (by simp)]
    dsimp
    simp only [compâ‚€, id_comp, comp_id]
  assoc := by
    rintro (Xâ‚ Xâ‚‚ Xâ‚ƒ Xâ‚„ : C) fâ‚ fâ‚‚ fâ‚ƒ
    obtain âŸ¨zâ‚, rflâŸ© := Hom.mk_surjective fâ‚
    obtain âŸ¨zâ‚‚, rflâŸ© := Hom.mk_surjective fâ‚‚
    obtain âŸ¨zâ‚ƒ, rflâŸ© := Hom.mk_surjective fâ‚ƒ
    change ((Hom.mk zâ‚).comp (Hom.mk zâ‚‚)).comp (Hom.mk zâ‚ƒ) =
      (Hom.mk zâ‚).comp ((Hom.mk zâ‚‚).comp (Hom.mk zâ‚ƒ))
    rw [Hom.comp_eq zâ‚ zâ‚‚, Hom.comp_eq zâ‚‚ zâ‚ƒ]
    obtain âŸ¨zâ‚â‚‚, facâ‚â‚‚âŸ© := exists_leftFraction (RightFraction.mk zâ‚.s zâ‚.hs zâ‚‚.f)
    obtain âŸ¨zâ‚‚â‚ƒ, facâ‚‚â‚ƒâŸ© := exists_leftFraction (RightFraction.mk zâ‚‚.s zâ‚‚.hs zâ‚ƒ.f)
    obtain âŸ¨z', facâŸ© := exists_leftFraction (RightFraction.mk zâ‚â‚‚.s zâ‚â‚‚.hs zâ‚‚â‚ƒ.f)
    dsimp at facâ‚â‚‚ facâ‚‚â‚ƒ fac
    rw [comp_eq zâ‚ zâ‚‚ zâ‚â‚‚ facâ‚â‚‚, comp_eq zâ‚‚ zâ‚ƒ zâ‚‚â‚ƒ facâ‚‚â‚ƒ, compâ‚€, compâ‚€,
      Hom.comp_eq, Hom.comp_eq,
      comp_eq _ zâ‚ƒ (mk z'.f (zâ‚‚â‚ƒ.s â‰« z'.s) (W.comp_mem _ _ zâ‚‚â‚ƒ.hs z'.hs))
        (by dsimp; rw [assoc, reassoc_of% facâ‚‚â‚ƒ, fac]),
      comp_eq zâ‚ _ (mk (zâ‚â‚‚.f â‰« z'.f) z'.s z'.hs)
        (by dsimp; rw [assoc, â† reassoc_of% facâ‚â‚‚, fac])]
    simp

variable (W)

/-- The localization functor to the constructed localized category for a morphism property
that has left calculus of fractions. -/
@[simps obj]
def Q : C â¥¤ Localization W where
  obj X := X
  map f := Hom.mk (ofHom W f)
  map_id _ := rfl
  map_comp {X Y Z} f g := by
    change _ = Hom.comp _ _
    rw [Hom.comp_eq, comp_eq (ofHom W f) (ofHom W g) (ofHom W g) (by simp)]
    simp only [ofHom, compâ‚€, comp_id]

end Localization

end LeftFraction

end

end MorphismProperty

end CategoryTheory
