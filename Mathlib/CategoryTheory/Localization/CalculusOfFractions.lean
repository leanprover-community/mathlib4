/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Localization.Opposite

/-!
# Calculus of fractions

Following the definitions by [Gabriel and Zisman][gabriel-zisman-1967],
given a morphism property `W : MorphismProperty C` on a category `C`,
we introduce the class `W.HasLeftCalculusOfFractions`. The main
result `Localization.exists_leftFraction` is that if `L : C ‚•§ D`
is a localization functor for `W`, then for any morphism `L.obj X ‚ü∂ L.obj Y` in `D`,
there exists an auxiliary object `Y' : C` and morphisms `g : X ‚ü∂ Y'` and `s : Y ‚ü∂ Y'`,
with `W s`, such that the given morphism is a sort of fraction `g / s`,
or more precisely of the form `L.map g ‚â´ (Localization.isoOfHom L W s hs).inv`.
We also show that the functor `L.mapArrow : Arrow C ‚•§ Arrow D` is essentially surjective.

Similar results are obtained when `W` has a right calculus of fractions.

## References

* [P. Gabriel, M. Zisman, *Calculus of fractions and homotopy theory*][gabriel-zisman-1967]

-/

namespace CategoryTheory

variable {C D : Type*} [Category C] [Category D]

open Category

namespace MorphismProperty

/-- A left fraction from `X : C` to `Y : C` for `W : MorphismProperty C` consists of the
datum of an object `Y' : C` and maps `f : X ‚ü∂ Y'` and `s : Y ‚ü∂ Y'` such that `W s`. -/
structure LeftFraction (W : MorphismProperty C) (X Y : C) where
  /-- the auxiliary object of a left fraction -/
  {Y' : C}
  /-- the numerator of a left fraction -/
  f : X ‚ü∂ Y'
  /-- the denominator of a left fraction -/
  s : Y ‚ü∂ Y'
  /-- the condition that the denominator belongs to the given morphism property -/
  hs : W s

namespace LeftFraction

variable (W : MorphismProperty C) {X Y : C}

/-- The left fraction from `X` to `Y` given by a morphism `f : X ‚ü∂ Y`. -/
@[simps]
def ofHom (f : X ‚ü∂ Y) [W.ContainsIdentities] :
    W.LeftFraction X Y := mk f (ùüô Y) (W.id_mem Y)

variable {W}

/-- The left fraction from `X` to `Y` given by a morphism `s : Y ‚ü∂ X` such that `W s`. -/
@[simps]
def ofInv (s : Y ‚ü∂ X) (hs : W s) :
    W.LeftFraction X Y := mk (ùüô X) s hs

/-- If `œÜ : W.LeftFraction X Y` and `L` is a functor which inverts `W`, this is the
induced morphism `L.obj X ‚ü∂ L.obj Y` -/
noncomputable def map (œÜ : W.LeftFraction X Y) (L : C ‚•§ D) (hL : W.IsInvertedBy L) :
    L.obj X ‚ü∂ L.obj Y :=
  have := hL _ œÜ.hs
  L.map œÜ.f ‚â´ inv (L.map œÜ.s)

@[reassoc (attr := simp)]
lemma map_comp_map_s (œÜ : W.LeftFraction X Y) (L : C ‚•§ D) (hL : W.IsInvertedBy L) :
    œÜ.map L hL ‚â´ L.map œÜ.s = L.map œÜ.f := by
  letI := hL _ œÜ.hs
  simp [map]

variable (W)

lemma map_ofHom (f : X ‚ü∂ Y) (L : C ‚•§ D) (hL : W.IsInvertedBy L) [W.ContainsIdentities] :
    (ofHom W f).map L hL = L.map f := by
  simp [map]

@[reassoc (attr := simp)]
lemma map_ofInv_hom_id (s : Y ‚ü∂ X) (hs : W s) (L : C ‚•§ D) (hL : W.IsInvertedBy L) :
    (ofInv s hs).map L hL ‚â´ L.map s = ùüô _ := by
  letI := hL _ hs
  simp [map]

@[reassoc (attr := simp)]
lemma map_hom_ofInv_id (s : Y ‚ü∂ X) (hs : W s) (L : C ‚•§ D) (hL : W.IsInvertedBy L) :
    L.map s ‚â´ (ofInv s hs).map L hL = ùüô _ := by
  letI := hL _ hs
  simp [map]

variable {W}

lemma cases (Œ± : W.LeftFraction X Y) :
    ‚àÉ (Y' : C) (f : X ‚ü∂ Y') (s : Y ‚ü∂ Y') (hs : W s), Œ± = LeftFraction.mk f s hs :=
  ‚ü®_, _, _, _, rfl‚ü©

end LeftFraction

/-- A right fraction from `X : C` to `Y : C` for `W : MorphismProperty C` consists of the
datum of an object `X' : C` and maps `s : X' ‚ü∂ X` and `f : X' ‚ü∂ Y` such that `W s`. -/
structure RightFraction (W : MorphismProperty C) (X Y : C) where
  /-- the auxiliary object of a right fraction -/
  {X' : C}
  /-- the denominator of a right fraction -/
  s : X' ‚ü∂ X
  /-- the condition that the denominator belongs to the given morphism property -/
  hs : W s
  /-- the numerator of a right fraction -/
  f : X' ‚ü∂ Y

namespace RightFraction

variable (W : MorphismProperty C)
variable {X Y : C}

/-- The right fraction from `X` to `Y` given by a morphism `f : X ‚ü∂ Y`. -/
@[simps]
def ofHom (f : X ‚ü∂ Y) [W.ContainsIdentities] :
    W.RightFraction X Y := mk (ùüô X) (W.id_mem X) f

variable {W}

/-- The right fraction from `X` to `Y` given by a morphism `s : Y ‚ü∂ X` such that `W s`. -/
@[simps]
def ofInv (s : Y ‚ü∂ X) (hs : W s) :
    W.RightFraction X Y := mk s hs (ùüô Y)

/-- If `œÜ : W.RightFraction X Y` and `L` is a functor which inverts `W`, this is the
induced morphism `L.obj X ‚ü∂ L.obj Y` -/
noncomputable def map (œÜ : W.RightFraction X Y) (L : C ‚•§ D) (hL : W.IsInvertedBy L) :
    L.obj X ‚ü∂ L.obj Y :=
  have := hL _ œÜ.hs
  inv (L.map œÜ.s) ‚â´ L.map œÜ.f

@[reassoc (attr := simp)]
lemma map_s_comp_map (œÜ : W.RightFraction X Y) (L : C ‚•§ D) (hL : W.IsInvertedBy L) :
    L.map œÜ.s ‚â´ œÜ.map L hL = L.map œÜ.f := by
  letI := hL _ œÜ.hs
  simp [map]

variable (W)

@[simp]
lemma map_ofHom (f : X ‚ü∂ Y) (L : C ‚•§ D) (hL : W.IsInvertedBy L) [W.ContainsIdentities] :
    (ofHom W f).map L hL = L.map f := by
  simp [map]

@[reassoc (attr := simp)]
lemma map_ofInv_hom_id (s : Y ‚ü∂ X) (hs : W s) (L : C ‚•§ D) (hL : W.IsInvertedBy L) :
    (ofInv s hs).map L hL ‚â´ L.map s = ùüô _ := by
  letI := hL _ hs
  simp [map]

@[reassoc (attr := simp)]
lemma map_hom_ofInv_id (s : Y ‚ü∂ X) (hs : W s) (L : C ‚•§ D) (hL : W.IsInvertedBy L) :
    L.map s ‚â´ (ofInv s hs).map L hL = ùüô _ := by
  letI := hL _ hs
  simp [map]

variable {W}

lemma cases (Œ± : W.RightFraction X Y) :
    ‚àÉ (X' : C) (s : X' ‚ü∂ X) (hs : W s) (f : X' ‚ü∂ Y), Œ± = RightFraction.mk s hs f :=
  ‚ü®_, _, _, _, rfl‚ü©

end RightFraction

variable (W : MorphismProperty C)

/-- A multiplicative morphism property `W` has left calculus of fractions if
any right fraction can be turned into a left fraction and that two morphisms
that can be equalized by precomposition with a morphism in `W` can also
be equalized by postcomposition with a morphism in `W`. -/
class HasLeftCalculusOfFractions : Prop extends W.IsMultiplicative where
  exists_leftFraction ‚¶ÉX Y : C‚¶Ñ (œÜ : W.RightFraction X Y) :
    ‚àÉ (œà : W.LeftFraction X Y), œÜ.f ‚â´ œà.s = œÜ.s ‚â´ œà.f
  ext : ‚àÄ ‚¶ÉX' X Y : C‚¶Ñ (f‚ÇÅ f‚ÇÇ : X ‚ü∂ Y) (s : X' ‚ü∂ X) (_ : W s)
    (_ : s ‚â´ f‚ÇÅ = s ‚â´ f‚ÇÇ), ‚àÉ (Y' : C) (t : Y ‚ü∂ Y') (_ : W t), f‚ÇÅ ‚â´ t = f‚ÇÇ ‚â´ t

/-- A multiplicative morphism property `W` has right calculus of fractions if
any left fraction can be turned into a right fraction and that two morphisms
that can be equalized by postcomposition with a morphism in `W` can also
be equalized by precomposition with a morphism in `W`. -/
class HasRightCalculusOfFractions : Prop extends W.IsMultiplicative where
  exists_rightFraction ‚¶ÉX Y : C‚¶Ñ (œÜ : W.LeftFraction X Y) :
    ‚àÉ (œà : W.RightFraction X Y), œà.s ‚â´ œÜ.f = œà.f ‚â´ œÜ.s
  ext : ‚àÄ ‚¶ÉX Y Y' : C‚¶Ñ (f‚ÇÅ f‚ÇÇ : X ‚ü∂ Y) (s : Y ‚ü∂ Y') (_ : W s)
    (_ : f‚ÇÅ ‚â´ s = f‚ÇÇ ‚â´ s), ‚àÉ (X' : C) (t : X' ‚ü∂ X) (_ : W t), t ‚â´ f‚ÇÅ = t ‚â´ f‚ÇÇ

variable {W}

lemma RightFraction.exists_leftFraction [W.HasLeftCalculusOfFractions] {X Y : C}
    (œÜ : W.RightFraction X Y) : ‚àÉ (œà : W.LeftFraction X Y), œÜ.f ‚â´ œà.s = œÜ.s ‚â´ œà.f :=
  HasLeftCalculusOfFractions.exists_leftFraction œÜ

/-- A choice of a left fraction deduced from a right fraction for a morphism property `W`
when `W` has left calculus of fractions. -/
noncomputable def RightFraction.leftFraction [W.HasLeftCalculusOfFractions] {X Y : C}
    (œÜ : W.RightFraction X Y) : W.LeftFraction X Y :=
  œÜ.exists_leftFraction.choose

@[reassoc]
lemma RightFraction.leftFraction_fac [W.HasLeftCalculusOfFractions] {X Y : C}
    (œÜ : W.RightFraction X Y) : œÜ.f ‚â´ œÜ.leftFraction.s = œÜ.s ‚â´ œÜ.leftFraction.f :=
  œÜ.exists_leftFraction.choose_spec

lemma LeftFraction.exists_rightFraction [W.HasRightCalculusOfFractions] {X Y : C}
    (œÜ : W.LeftFraction X Y) : ‚àÉ (œà : W.RightFraction X Y), œà.s ‚â´ œÜ.f = œà.f ‚â´ œÜ.s :=
  HasRightCalculusOfFractions.exists_rightFraction œÜ

/-- A choice of a right fraction deduced from a left fraction for a morphism property `W`
when `W` has right calculus of fractions. -/
noncomputable def LeftFraction.rightFraction [W.HasRightCalculusOfFractions] {X Y : C}
    (œÜ : W.LeftFraction X Y) : W.RightFraction X Y :=
  œÜ.exists_rightFraction.choose

@[reassoc]
lemma LeftFraction.rightFraction_fac [W.HasRightCalculusOfFractions] {X Y : C}
    (œÜ : W.LeftFraction X Y) : œÜ.rightFraction.s ‚â´ œÜ.f = œÜ.rightFraction.f ‚â´ œÜ.s :=
  œÜ.exists_rightFraction.choose_spec

/-- The equivalence relation on left fractions for a morphism property `W`. -/
def LeftFractionRel {X Y : C} (z‚ÇÅ z‚ÇÇ : W.LeftFraction X Y) : Prop :=
  ‚àÉ (Z : C) (t‚ÇÅ : z‚ÇÅ.Y' ‚ü∂ Z) (t‚ÇÇ : z‚ÇÇ.Y' ‚ü∂ Z) (_ : z‚ÇÅ.s ‚â´ t‚ÇÅ = z‚ÇÇ.s ‚â´ t‚ÇÇ)
    (_ : z‚ÇÅ.f ‚â´ t‚ÇÅ = z‚ÇÇ.f ‚â´ t‚ÇÇ), W (z‚ÇÅ.s ‚â´ t‚ÇÅ)

namespace LeftFractionRel

lemma refl {X Y : C} (z : W.LeftFraction X Y) : LeftFractionRel z z :=
  ‚ü®z.Y', ùüô _, ùüô _, rfl, rfl, by simpa only [Category.comp_id] using z.hs‚ü©

lemma symm {X Y : C} {z‚ÇÅ z‚ÇÇ : W.LeftFraction X Y} (h : LeftFractionRel z‚ÇÅ z‚ÇÇ) :
    LeftFractionRel z‚ÇÇ z‚ÇÅ := by
  obtain ‚ü®Z, t‚ÇÅ, t‚ÇÇ, hst, hft, ht‚ü© := h
  exact ‚ü®Z, t‚ÇÇ, t‚ÇÅ, hst.symm, hft.symm, by simpa only [‚Üê hst] using ht‚ü©

lemma trans {X Y : C} {z‚ÇÅ z‚ÇÇ z‚ÇÉ : W.LeftFraction X Y}
    [HasLeftCalculusOfFractions W]
    (h‚ÇÅ‚ÇÇ : LeftFractionRel z‚ÇÅ z‚ÇÇ) (h‚ÇÇ‚ÇÉ : LeftFractionRel z‚ÇÇ z‚ÇÉ) :
    LeftFractionRel z‚ÇÅ z‚ÇÉ := by
  obtain ‚ü®Z‚ÇÑ, t‚ÇÅ, t‚ÇÇ, hst, hft, ht‚ü© := h‚ÇÅ‚ÇÇ
  obtain ‚ü®Z‚ÇÖ, u‚ÇÇ, u‚ÇÉ, hsu, hfu, hu‚ü© := h‚ÇÇ‚ÇÉ
  obtain ‚ü®‚ü®v‚ÇÑ, v‚ÇÖ, hv‚ÇÖ‚ü©, fac‚ü© := HasLeftCalculusOfFractions.exists_leftFraction
    (RightFraction.mk (z‚ÇÅ.s ‚â´ t‚ÇÅ) ht (z‚ÇÉ.s ‚â´ u‚ÇÉ))
  simp only [Category.assoc] at fac
  have eq : z‚ÇÇ.s ‚â´ u‚ÇÇ ‚â´ v‚ÇÖ  = z‚ÇÇ.s ‚â´ t‚ÇÇ ‚â´ v‚ÇÑ := by
    simpa only [‚Üê reassoc_of% hsu, reassoc_of% hst] using fac
  obtain ‚ü®Z‚Çá, w, hw, fac'‚ü© := HasLeftCalculusOfFractions.ext _ _ _ z‚ÇÇ.hs eq
  simp only [Category.assoc] at fac'
  refine ‚ü®Z‚Çá, t‚ÇÅ ‚â´ v‚ÇÑ ‚â´ w, u‚ÇÉ ‚â´ v‚ÇÖ ‚â´ w, ?_, ?_, ?_‚ü©
  ¬∑ rw [reassoc_of% fac]
  ¬∑ rw [reassoc_of% hft, ‚Üê fac', reassoc_of% hfu]
  ¬∑ rw [‚Üê reassoc_of% fac, ‚Üê reassoc_of% hsu, ‚Üê Category.assoc]
    exact W.comp_mem _ _ hu (W.comp_mem _ _ hv‚ÇÖ hw)

end LeftFractionRel

section

variable (W)

lemma equivalenceLeftFractionRel [W.HasLeftCalculusOfFractions] (X Y : C) :
    @_root_.Equivalence (W.LeftFraction X Y) LeftFractionRel where
  refl := LeftFractionRel.refl
  symm := LeftFractionRel.symm
  trans := LeftFractionRel.trans

variable {W}

namespace LeftFraction

open HasLeftCalculusOfFractions

/-- Auxiliary definition for the composition of left fractions. -/
@[simp]
def comp‚ÇÄ [W.HasLeftCalculusOfFractions] {X Y Z : C}
    (z‚ÇÅ : W.LeftFraction X Y) (z‚ÇÇ : W.LeftFraction Y Z) (z‚ÇÉ : W.LeftFraction z‚ÇÅ.Y' z‚ÇÇ.Y') :
    W.LeftFraction X Z :=
  mk (z‚ÇÅ.f ‚â´ z‚ÇÉ.f) (z‚ÇÇ.s ‚â´ z‚ÇÉ.s) (W.comp_mem _ _ z‚ÇÇ.hs z‚ÇÉ.hs)

/-- The equivalence class of `z‚ÇÅ.comp‚ÇÄ z‚ÇÇ z‚ÇÉ` does not depend on the choice of `z‚ÇÉ` provided
they satisfy the compatibility `z‚ÇÇ.f ‚â´ z‚ÇÉ.s = z‚ÇÅ.s ‚â´ z‚ÇÉ.f`. -/
lemma comp‚ÇÄ_rel [W.HasLeftCalculusOfFractions]
    {X Y Z : C} (z‚ÇÅ : W.LeftFraction X Y) (z‚ÇÇ : W.LeftFraction Y Z)
    (z‚ÇÉ z‚ÇÉ' : W.LeftFraction z‚ÇÅ.Y' z‚ÇÇ.Y') (h‚ÇÉ : z‚ÇÇ.f ‚â´ z‚ÇÉ.s = z‚ÇÅ.s ‚â´ z‚ÇÉ.f)
    (h‚ÇÉ' : z‚ÇÇ.f ‚â´ z‚ÇÉ'.s = z‚ÇÅ.s ‚â´ z‚ÇÉ'.f) :
    LeftFractionRel (z‚ÇÅ.comp‚ÇÄ z‚ÇÇ z‚ÇÉ) (z‚ÇÅ.comp‚ÇÄ z‚ÇÇ z‚ÇÉ') := by
  obtain ‚ü®z‚ÇÑ, fac‚ü© := exists_leftFraction (RightFraction.mk z‚ÇÉ.s z‚ÇÉ.hs z‚ÇÉ'.s)
  dsimp at fac
  have eq : z‚ÇÅ.s ‚â´ z‚ÇÉ.f ‚â´ z‚ÇÑ.f = z‚ÇÅ.s ‚â´ z‚ÇÉ'.f ‚â´ z‚ÇÑ.s := by
    rw [‚Üê reassoc_of% h‚ÇÉ, ‚Üê reassoc_of% h‚ÇÉ', fac]
  obtain ‚ü®Y, t, ht, fac'‚ü© := HasLeftCalculusOfFractions.ext _ _ _ z‚ÇÅ.hs eq
  simp only [assoc] at fac'
  refine ‚ü®Y, z‚ÇÑ.f ‚â´ t, z‚ÇÑ.s ‚â´ t, ?_, ?_, ?_‚ü©
  ¬∑ simp only [comp‚ÇÄ, assoc, reassoc_of% fac]
  ¬∑ simp only [comp‚ÇÄ, assoc, fac']
  ¬∑ simp only [comp‚ÇÄ, assoc, ‚Üê reassoc_of% fac]
    exact W.comp_mem _ _ z‚ÇÇ.hs (W.comp_mem _ _ z‚ÇÉ'.hs (W.comp_mem _ _ z‚ÇÑ.hs ht))

variable (W) in
/-- The morphisms in the constructed localized category for a morphism property `W`
that has left calculus of fractions are equivalence classes of left fractions. -/
def Localization.Hom (X Y : C) :=
  Quot (LeftFractionRel : W.LeftFraction X Y ‚Üí W.LeftFraction X Y ‚Üí Prop)

/-- The morphism in the constructed localized category that is induced by a left fraction. -/
def Localization.Hom.mk {X Y : C} (z : W.LeftFraction X Y) : Localization.Hom W X Y :=
  Quot.mk _ z

lemma Localization.Hom.mk_surjective {X Y : C} (f : Localization.Hom W X Y) :
    ‚àÉ (z : W.LeftFraction X Y), f = mk z := by
  obtain ‚ü®z‚ü© := f
  exact ‚ü®z, rfl‚ü©

variable [W.HasLeftCalculusOfFractions]

/-- Auxiliary definition towards the definition of the composition of morphisms
in the constructed localized category for a morphism property that has
left calculus of fractions. -/
noncomputable def comp
    {X Y Z : C} (z‚ÇÅ : W.LeftFraction X Y) (z‚ÇÇ : W.LeftFraction Y Z) :
    Localization.Hom W X Z :=
  Localization.Hom.mk (z‚ÇÅ.comp‚ÇÄ z‚ÇÇ (RightFraction.mk z‚ÇÅ.s z‚ÇÅ.hs z‚ÇÇ.f).leftFraction)

lemma comp_eq {X Y Z : C} (z‚ÇÅ : W.LeftFraction X Y) (z‚ÇÇ : W.LeftFraction Y Z)
    (z‚ÇÉ : W.LeftFraction z‚ÇÅ.Y' z‚ÇÇ.Y') (h‚ÇÉ : z‚ÇÇ.f ‚â´ z‚ÇÉ.s = z‚ÇÅ.s ‚â´ z‚ÇÉ.f) :
    z‚ÇÅ.comp z‚ÇÇ = Localization.Hom.mk (z‚ÇÅ.comp‚ÇÄ z‚ÇÇ z‚ÇÉ) :=
  Quot.sound (LeftFraction.comp‚ÇÄ_rel _ _ _ _
    (RightFraction.leftFraction_fac (RightFraction.mk z‚ÇÅ.s z‚ÇÅ.hs z‚ÇÇ.f)) h‚ÇÉ)

namespace Localization

/-- Composition of morphisms in the constructed localized category
for a morphism property that has left calculus of fractions. -/
noncomputable def Hom.comp {X Y Z : C} (z‚ÇÅ : Hom W X Y) (z‚ÇÇ : Hom W Y Z) : Hom W X Z := by
  refine Quot.lift‚ÇÇ (fun a b => a.comp b) ?_ ?_ z‚ÇÅ z‚ÇÇ
  ¬∑ rintro a b‚ÇÅ b‚ÇÇ ‚ü®U, t‚ÇÅ, t‚ÇÇ, hst, hft, ht‚ü©
    obtain ‚ü®z‚ÇÅ, fac‚ÇÅ‚ü© := exists_leftFraction (RightFraction.mk a.s a.hs b‚ÇÅ.f)
    obtain ‚ü®z‚ÇÇ, fac‚ÇÇ‚ü© := exists_leftFraction (RightFraction.mk a.s a.hs b‚ÇÇ.f)
    obtain ‚ü®w‚ÇÅ, fac‚ÇÅ'‚ü© := exists_leftFraction (RightFraction.mk z‚ÇÅ.s z‚ÇÅ.hs t‚ÇÅ)
    obtain ‚ü®w‚ÇÇ, fac‚ÇÇ'‚ü© := exists_leftFraction (RightFraction.mk z‚ÇÇ.s z‚ÇÇ.hs t‚ÇÇ)
    obtain ‚ü®u, fac‚ÇÉ‚ü© := exists_leftFraction (RightFraction.mk w‚ÇÅ.s w‚ÇÅ.hs w‚ÇÇ.s)
    dsimp at fac‚ÇÅ fac‚ÇÇ fac‚ÇÅ' fac‚ÇÇ' fac‚ÇÉ ‚ä¢
    have eq : a.s ‚â´ z‚ÇÅ.f ‚â´ w‚ÇÅ.f ‚â´ u.f = a.s ‚â´ z‚ÇÇ.f ‚â´ w‚ÇÇ.f ‚â´ u.s := by
      rw [‚Üê reassoc_of% fac‚ÇÅ, ‚Üê reassoc_of% fac‚ÇÇ, ‚Üê reassoc_of% fac‚ÇÅ', ‚Üê reassoc_of% fac‚ÇÇ',
        reassoc_of% hft, fac‚ÇÉ]
    obtain ‚ü®Z, p, hp, fac‚ÇÑ‚ü© := HasLeftCalculusOfFractions.ext _ _ _ a.hs eq
    simp only [assoc] at fac‚ÇÑ
    rw [comp_eq _ _ z‚ÇÅ fac‚ÇÅ, comp_eq _ _ z‚ÇÇ fac‚ÇÇ]
    apply Quot.sound
    refine ‚ü®Z, w‚ÇÅ.f ‚â´ u.f ‚â´ p, w‚ÇÇ.f ‚â´ u.s ‚â´ p, ?_, ?_, ?_‚ü©
    ¬∑ dsimp
      simp only [assoc, ‚Üê reassoc_of% fac‚ÇÅ', ‚Üê reassoc_of% fac‚ÇÇ',
        reassoc_of% hst, reassoc_of% fac‚ÇÉ]
    ¬∑ dsimp
      simp only [assoc, fac‚ÇÑ]
    ¬∑ dsimp
      simp only [assoc]
      rw [‚Üê reassoc_of% fac‚ÇÅ', ‚Üê reassoc_of% fac‚ÇÉ, ‚Üê assoc]
      exact W.comp_mem _ _ ht (W.comp_mem _ _ w‚ÇÇ.hs (W.comp_mem _ _ u.hs hp))
  ¬∑ rintro a‚ÇÅ a‚ÇÇ b ‚ü®U, t‚ÇÅ, t‚ÇÇ, hst, hft, ht‚ü©
    obtain ‚ü®z‚ÇÅ, fac‚ÇÅ‚ü© := exists_leftFraction (RightFraction.mk a‚ÇÅ.s a‚ÇÅ.hs b.f)
    obtain ‚ü®z‚ÇÇ, fac‚ÇÇ‚ü© := exists_leftFraction (RightFraction.mk a‚ÇÇ.s a‚ÇÇ.hs b.f)
    obtain ‚ü®w‚ÇÅ, fac‚ÇÅ'‚ü© := exists_leftFraction (RightFraction.mk (a‚ÇÅ.s ‚â´ t‚ÇÅ) ht (b.f ‚â´ z‚ÇÅ.s))
    obtain ‚ü®w‚ÇÇ, fac‚ÇÇ'‚ü© := exists_leftFraction (RightFraction.mk (a‚ÇÇ.s ‚â´ t‚ÇÇ)
      (show W _ by rw [‚Üê hst]; exact ht) (b.f ‚â´ z‚ÇÇ.s))
    let p‚ÇÅ : W.LeftFraction X Z := LeftFraction.mk (a‚ÇÅ.f ‚â´ t‚ÇÅ ‚â´ w‚ÇÅ.f) (b.s ‚â´ z‚ÇÅ.s ‚â´ w‚ÇÅ.s)
      (W.comp_mem _ _ b.hs (W.comp_mem _ _ z‚ÇÅ.hs w‚ÇÅ.hs))
    let p‚ÇÇ : W.LeftFraction X Z := LeftFraction.mk (a‚ÇÇ.f ‚â´ t‚ÇÇ ‚â´ w‚ÇÇ.f) (b.s ‚â´ z‚ÇÇ.s ‚â´ w‚ÇÇ.s)
      (W.comp_mem _ _ b.hs (W.comp_mem _ _ z‚ÇÇ.hs w‚ÇÇ.hs))
    dsimp at fac‚ÇÅ fac‚ÇÇ fac‚ÇÅ' fac‚ÇÇ' ‚ä¢
    simp only [assoc] at fac‚ÇÅ' fac‚ÇÇ'
    rw [comp_eq _ _ z‚ÇÅ fac‚ÇÅ, comp_eq _ _ z‚ÇÇ fac‚ÇÇ]
    apply Quot.sound
    refine LeftFractionRel.trans ?_ ((?_ : LeftFractionRel p‚ÇÅ p‚ÇÇ).trans ?_)
    ¬∑ have eq : a‚ÇÅ.s ‚â´ z‚ÇÅ.f ‚â´ w‚ÇÅ.s = a‚ÇÅ.s ‚â´ t‚ÇÅ ‚â´ w‚ÇÅ.f := by rw [‚Üê fac‚ÇÅ', reassoc_of% fac‚ÇÅ]
      obtain ‚ü®Z, u, hu, fac‚ÇÉ‚ü© := HasLeftCalculusOfFractions.ext _ _ _ a‚ÇÅ.hs eq
      simp only [assoc] at fac‚ÇÉ
      refine ‚ü®Z, w‚ÇÅ.s ‚â´ u, u, ?_, ?_, ?_‚ü©
      ¬∑ dsimp [p‚ÇÅ]
        simp only [assoc]
      ¬∑ dsimp [p‚ÇÅ]
        simp only [assoc, fac‚ÇÉ]
      ¬∑ dsimp
        simp only [assoc]
        exact W.comp_mem _ _ b.hs (W.comp_mem _ _ z‚ÇÅ.hs (W.comp_mem _ _ w‚ÇÅ.hs hu))
    ¬∑ obtain ‚ü®q, fac‚ÇÉ‚ü© := exists_leftFraction (RightFraction.mk (z‚ÇÅ.s ‚â´ w‚ÇÅ.s)
        (W.comp_mem _ _ z‚ÇÅ.hs w‚ÇÅ.hs) (z‚ÇÇ.s ‚â´ w‚ÇÇ.s))
      dsimp at fac‚ÇÉ
      simp only [assoc] at fac‚ÇÉ
      have eq : a‚ÇÅ.s ‚â´ t‚ÇÅ ‚â´ w‚ÇÅ.f ‚â´ q.f = a‚ÇÅ.s ‚â´ t‚ÇÅ ‚â´ w‚ÇÇ.f ‚â´ q.s := by
        rw [‚Üê reassoc_of% fac‚ÇÅ', ‚Üê fac‚ÇÉ, reassoc_of% hst, reassoc_of% fac‚ÇÇ']
      obtain ‚ü®Z, u, hu, fac‚ÇÑ‚ü© := HasLeftCalculusOfFractions.ext _ _ _ a‚ÇÅ.hs eq
      simp only [assoc] at fac‚ÇÑ
      refine ‚ü®Z, q.f ‚â´ u, q.s ‚â´ u, ?_, ?_, ?_‚ü©
      ¬∑ simp only [p‚ÇÅ, p‚ÇÇ, assoc, reassoc_of% fac‚ÇÉ]
      ¬∑ rw [assoc, assoc, assoc, assoc, fac‚ÇÑ, reassoc_of% hft]
      ¬∑ simp only [p‚ÇÅ, assoc, ‚Üê reassoc_of% fac‚ÇÉ]
        exact W.comp_mem _ _ b.hs (W.comp_mem _ _ z‚ÇÇ.hs
          (W.comp_mem _ _ w‚ÇÇ.hs (W.comp_mem _ _ q.hs hu)))
    ¬∑ have eq : a‚ÇÇ.s ‚â´ z‚ÇÇ.f ‚â´ w‚ÇÇ.s = a‚ÇÇ.s ‚â´ t‚ÇÇ ‚â´ w‚ÇÇ.f := by
        rw [‚Üê fac‚ÇÇ', reassoc_of% fac‚ÇÇ]
      obtain ‚ü®Z, u, hu, fac‚ÇÑ‚ü© := HasLeftCalculusOfFractions.ext _ _ _ a‚ÇÇ.hs eq
      simp only [assoc] at fac‚ÇÑ
      refine ‚ü®Z, u, w‚ÇÇ.s ‚â´ u, ?_, ?_, ?_‚ü©
      ¬∑ dsimp [p‚ÇÅ, p‚ÇÇ]
        simp only [assoc]
      ¬∑ dsimp [p‚ÇÅ, p‚ÇÇ]
        simp only [assoc, fac‚ÇÑ]
      ¬∑ dsimp [p‚ÇÅ, p‚ÇÇ]
        simp only [assoc]
        exact W.comp_mem _ _ b.hs (W.comp_mem _ _ z‚ÇÇ.hs (W.comp_mem _ _ w‚ÇÇ.hs hu))

lemma Hom.comp_eq {X Y Z : C} (z‚ÇÅ : W.LeftFraction X Y) (z‚ÇÇ : W.LeftFraction Y Z) :
    Hom.comp (mk z‚ÇÅ) (mk z‚ÇÇ) = z‚ÇÅ.comp z‚ÇÇ := rfl

end Localization

/-- The constructed localized category for a morphism property
that has left calculus of fractions. -/
@[nolint unusedArguments]
def Localization (_ : MorphismProperty C) := C

namespace Localization

noncomputable instance : Category (Localization W) where
  Hom X Y := Localization.Hom W X Y
  id _ := Localization.Hom.mk (ofHom W (ùüô _))
  comp f g := f.comp g
  comp_id := by
    rintro (X Y : C) f
    obtain ‚ü®z, rfl‚ü© := Hom.mk_surjective f
    change (Hom.mk z).comp (Hom.mk (ofHom W (ùüô Y))) = Hom.mk z
    rw [Hom.comp_eq, comp_eq z (ofHom W (ùüô Y)) (ofInv z.s z.hs) (by simp)]
    dsimp [comp‚ÇÄ]
    simp only [comp_id, id_comp]
  id_comp := by
    rintro (X Y : C) f
    obtain ‚ü®z, rfl‚ü© := Hom.mk_surjective f
    change (Hom.mk (ofHom W (ùüô X))).comp (Hom.mk z) = Hom.mk z
    rw [Hom.comp_eq, comp_eq (ofHom W (ùüô X)) z (ofHom W z.f) (by simp)]
    dsimp
    simp only [id_comp, comp_id]
  assoc := by
    rintro (X‚ÇÅ X‚ÇÇ X‚ÇÉ X‚ÇÑ : C) f‚ÇÅ f‚ÇÇ f‚ÇÉ
    obtain ‚ü®z‚ÇÅ, rfl‚ü© := Hom.mk_surjective f‚ÇÅ
    obtain ‚ü®z‚ÇÇ, rfl‚ü© := Hom.mk_surjective f‚ÇÇ
    obtain ‚ü®z‚ÇÉ, rfl‚ü© := Hom.mk_surjective f‚ÇÉ
    change ((Hom.mk z‚ÇÅ).comp (Hom.mk z‚ÇÇ)).comp (Hom.mk z‚ÇÉ) =
      (Hom.mk z‚ÇÅ).comp ((Hom.mk z‚ÇÇ).comp (Hom.mk z‚ÇÉ))
    rw [Hom.comp_eq z‚ÇÅ z‚ÇÇ, Hom.comp_eq z‚ÇÇ z‚ÇÉ]
    obtain ‚ü®z‚ÇÅ‚ÇÇ, fac‚ÇÅ‚ÇÇ‚ü© := exists_leftFraction (RightFraction.mk z‚ÇÅ.s z‚ÇÅ.hs z‚ÇÇ.f)
    obtain ‚ü®z‚ÇÇ‚ÇÉ, fac‚ÇÇ‚ÇÉ‚ü© := exists_leftFraction (RightFraction.mk z‚ÇÇ.s z‚ÇÇ.hs z‚ÇÉ.f)
    obtain ‚ü®z', fac‚ü© := exists_leftFraction (RightFraction.mk z‚ÇÅ‚ÇÇ.s z‚ÇÅ‚ÇÇ.hs z‚ÇÇ‚ÇÉ.f)
    dsimp at fac‚ÇÅ‚ÇÇ fac‚ÇÇ‚ÇÉ fac
    rw [comp_eq z‚ÇÅ z‚ÇÇ z‚ÇÅ‚ÇÇ fac‚ÇÅ‚ÇÇ, comp_eq z‚ÇÇ z‚ÇÉ z‚ÇÇ‚ÇÉ fac‚ÇÇ‚ÇÉ, comp‚ÇÄ, comp‚ÇÄ,
      Hom.comp_eq, Hom.comp_eq,
      comp_eq _ z‚ÇÉ (mk z'.f (z‚ÇÇ‚ÇÉ.s ‚â´ z'.s) (W.comp_mem _ _ z‚ÇÇ‚ÇÉ.hs z'.hs))
        (by dsimp; rw [assoc, reassoc_of% fac‚ÇÇ‚ÇÉ, fac]),
      comp_eq z‚ÇÅ _ (mk (z‚ÇÅ‚ÇÇ.f ‚â´ z'.f) z'.s z'.hs)
        (by dsimp; rw [assoc, ‚Üê reassoc_of% fac‚ÇÅ‚ÇÇ, fac])]
    simp

variable (W) in
/-- The localization functor to the constructed localized category for a morphism property
that has left calculus of fractions. -/
@[simps obj]
noncomputable def Q : C ‚•§ Localization W where
  obj X := X
  map f := Hom.mk (ofHom W f)
  map_id _ := rfl
  map_comp {X Y Z} f g := by
    change _ = Hom.comp _ _
    rw [Hom.comp_eq, comp_eq (ofHom W f) (ofHom W g) (ofHom W g) (by simp)]
    simp only [ofHom, comp‚ÇÄ, comp_id]

/-- The morphism on `Localization W` that is induced by a left fraction. -/
noncomputable abbrev homMk {X Y : C} (f : W.LeftFraction X Y) : (Q W).obj X ‚ü∂ (Q W).obj Y :=
  Hom.mk f

lemma homMk_eq_hom_mk {X Y : C} (f : W.LeftFraction X Y) : homMk f = Hom.mk f := rfl

variable (W)

lemma Q_map {X Y : C} (f : X ‚ü∂ Y) : (Q W).map f = homMk (ofHom W f) := rfl

variable {W}

lemma homMk_comp_homMk {X Y Z : C} (z‚ÇÅ : W.LeftFraction X Y) (z‚ÇÇ : W.LeftFraction Y Z)
    (z‚ÇÉ : W.LeftFraction z‚ÇÅ.Y' z‚ÇÇ.Y') (h‚ÇÉ : z‚ÇÇ.f ‚â´ z‚ÇÉ.s = z‚ÇÅ.s ‚â´ z‚ÇÉ.f) :
    homMk z‚ÇÅ ‚â´ homMk z‚ÇÇ = homMk (z‚ÇÅ.comp‚ÇÄ z‚ÇÇ z‚ÇÉ) := by
  change Hom.comp _ _ = _
  rw [Hom.comp_eq, comp_eq z‚ÇÅ z‚ÇÇ z‚ÇÉ h‚ÇÉ]

lemma homMk_eq_of_leftFractionRel {X Y : C} (z‚ÇÅ z‚ÇÇ : W.LeftFraction X Y)
    (h : LeftFractionRel z‚ÇÅ z‚ÇÇ) :
    homMk z‚ÇÅ = homMk z‚ÇÇ :=
  Quot.sound h

lemma homMk_eq_iff_leftFractionRel {X Y : C} (z‚ÇÅ z‚ÇÇ : W.LeftFraction X Y) :
    homMk z‚ÇÅ = homMk z‚ÇÇ ‚Üî LeftFractionRel z‚ÇÅ z‚ÇÇ :=
  @Equivalence.quot_mk_eq_iff _ _ (equivalenceLeftFractionRel W X Y) _ _

/-- The morphism in `Localization W` that is the formal inverse of a morphism
which belongs to `W`. -/
noncomputable def Qinv {X Y : C} (s : X ‚ü∂ Y) (hs : W s) : (Q W).obj Y ‚ü∂ (Q W).obj X :=
  homMk (ofInv s hs)

lemma Q_map_comp_Qinv {X Y Y' : C} (f : X ‚ü∂ Y') (s : Y ‚ü∂ Y') (hs : W s) :
    (Q W).map f ‚â´ Qinv s hs = homMk (mk f s hs) := by
  dsimp only [Q_map, Qinv]
  rw [homMk_comp_homMk (ofHom W f) (ofInv s hs) (ofHom W (ùüô _)) (by simp)]
  simp

/-- The isomorphism in `Localization W` that is induced by a morphism in `W`. -/
@[simps]
noncomputable def Qiso {X Y : C} (s : X ‚ü∂ Y) (hs : W s) : (Q W).obj X ‚âÖ (Q W).obj Y where
  hom := (Q W).map s
  inv := Qinv s hs
  hom_inv_id := by
    rw [Q_map_comp_Qinv]
    apply homMk_eq_of_leftFractionRel
    exact ‚ü®_, ùüô Y, s, by simp, by simp, by simpa using hs‚ü©
  inv_hom_id := by
    dsimp only [Qinv, Q_map]
    rw [homMk_comp_homMk (ofInv s hs) (ofHom W s) (ofHom W (ùüô Y)) (by simp)]
    apply homMk_eq_of_leftFractionRel
    exact ‚ü®_, ùüô Y, ùüô Y, by simp, by simp, by simpa using W.id_mem Y‚ü©

@[reassoc (attr := simp)]
lemma Qiso_hom_inv_id {X Y : C} (s : X ‚ü∂ Y) (hs : W s) :
    (Q W).map s ‚â´ Qinv s hs = ùüô _ := (Qiso s hs).hom_inv_id

@[reassoc (attr := simp)]
lemma Qiso_inv_hom_id {X Y : C} (s : X ‚ü∂ Y) (hs : W s) :
    Qinv s hs  ‚â´ (Q W).map s = ùüô _ := (Qiso s hs).inv_hom_id

instance {X Y : C} (s : X ‚ü∂ Y) (hs : W s) : IsIso (Qinv s hs) :=
  (inferInstance : IsIso (Qiso s hs).inv)

section

variable {E : Type*} [Category E]

/-- The image by a functor which inverts `W` of an equivalence class of left fractions. -/
noncomputable def Hom.map {X Y : C} (f : Hom W X Y) (F : C ‚•§ E) (hF : W.IsInvertedBy F) :
    F.obj X ‚ü∂ F.obj Y :=
  Quot.lift (fun f => f.map F hF) (by
    intro a‚ÇÅ a‚ÇÇ ‚ü®Z, t‚ÇÅ, t‚ÇÇ, hst, hft, h‚ü©
    dsimp
    have := hF _ h
    rw [‚Üê cancel_mono (F.map (a‚ÇÅ.s ‚â´ t‚ÇÅ)), F.map_comp, map_comp_map_s_assoc,
      ‚Üê F.map_comp, ‚Üê F.map_comp, hst, hft, F.map_comp,
      F.map_comp, map_comp_map_s_assoc]) f

@[simp]
lemma Hom.map_mk {W} {X Y : C} (f : LeftFraction W X Y) (F : C ‚•§ E) (hF : W.IsInvertedBy F) :
    Hom.map (Hom.mk f) F hF = f.map F hF := rfl

namespace StrictUniversalPropertyFixedTarget

variable (W)

lemma inverts : W.IsInvertedBy (Q W) := fun _ _ s hs =>
  (inferInstance : IsIso (Qiso s hs).hom)

variable {W}

/-- The functor `Localization W ‚•§ E` that is induced by a functor `C ‚•§ E` which inverts `W`,
when `W` has a left calculus of fractions. -/
noncomputable def lift (F : C ‚•§ E) (hF : W.IsInvertedBy F) :
    Localization W ‚•§ E where
  obj X := F.obj X
  map {_ _ : C} f := f.map F hF
  map_id := by
    intro (X : C)
    change (Hom.mk (ofHom W (ùüô X))).map F hF = _
    rw [Hom.map_mk, map_ofHom, F.map_id]
  map_comp := by
    rintro (X Y Z : C) f g
    obtain ‚ü®f, rfl‚ü© := Hom.mk_surjective f
    obtain ‚ü®g, rfl‚ü© := Hom.mk_surjective g
    dsimp
    obtain ‚ü®z, fac‚ü© := HasLeftCalculusOfFractions.exists_leftFraction
      (RightFraction.mk f.s f.hs g.f)
    rw [homMk_comp_homMk f g z fac, Hom.map_mk]
    dsimp at fac ‚ä¢
    have := hF _ g.hs
    have := hF _ z.hs
    rw [‚Üê cancel_mono (F.map g.s), assoc, map_comp_map_s,
      ‚Üê cancel_mono (F.map z.s), assoc, assoc, ‚Üê F.map_comp,
      ‚Üê F.map_comp, map_comp_map_s, fac]
    dsimp
    rw [F.map_comp, F.map_comp, map_comp_map_s_assoc]

lemma fac (F : C ‚•§ E) (hF : W.IsInvertedBy F) : Q W ‚ãô lift F hF = F :=
  Functor.ext (fun _ => rfl) (fun X Y f => by
    dsimp [lift]
    rw [Q_map, Hom.map_mk, id_comp, comp_id, map_ofHom])

lemma uniq (F‚ÇÅ F‚ÇÇ : Localization W ‚•§ E) (h : Q W ‚ãô F‚ÇÅ = Q W ‚ãô F‚ÇÇ) : F‚ÇÅ = F‚ÇÇ :=
  Functor.ext (fun X => Functor.congr_obj h X) (by
    rintro (X Y : C) f
    obtain ‚ü®f, rfl‚ü© := Hom.mk_surjective f
    rw [show Hom.mk f = homMk (mk f.f f.s f.hs) by rfl,
      ‚Üê Q_map_comp_Qinv f.f f.s f.hs, F‚ÇÅ.map_comp, F‚ÇÇ.map_comp, assoc]
    erw [Functor.congr_hom h f.f]
    rw [assoc, assoc]
    congr 2
    have := inverts W _ f.hs
    rw [‚Üê cancel_epi (F‚ÇÇ.map ((Q W).map f.s)), ‚Üê F‚ÇÇ.map_comp_assoc,
      Qiso_hom_inv_id, Functor.map_id, id_comp]
    erw [Functor.congr_hom h.symm f.s]
    dsimp
    rw [assoc, assoc, eqToHom_trans_assoc, eqToHom_refl, id_comp, ‚Üê F‚ÇÅ.map_comp,
      Qiso_hom_inv_id]
    dsimp
    rw [F‚ÇÅ.map_id, comp_id])

end StrictUniversalPropertyFixedTarget

variable (W)

open StrictUniversalPropertyFixedTarget in
/-- The universal property of the localization for the constructed localized category
when there is a left calculus of fractions. -/
noncomputable def strictUniversalPropertyFixedTarget (E : Type*) [Category E] :
    Localization.StrictUniversalPropertyFixedTarget (Q W) W E where
  inverts := inverts W
  lift := lift
  fac := fac
  uniq := uniq

instance : (Q W).IsLocalization W :=
  Functor.IsLocalization.mk' _ _
    (strictUniversalPropertyFixedTarget W _)
    (strictUniversalPropertyFixedTarget W _)

end

lemma homMk_eq {X Y : C} (f : LeftFraction W X Y) :
    homMk f = f.map (Q W) (Localization.inverts _ W) := by
  have := Localization.inverts (Q W) W f.s f.hs
  rw [‚Üê Q_map_comp_Qinv f.f f.s f.hs, ‚Üê cancel_mono ((Q W).map f.s),
    assoc, Qiso_inv_hom_id, comp_id, map_comp_map_s]

lemma map_eq_iff {X Y : C} (f g : LeftFraction W X Y) :
    f.map (LeftFraction.Localization.Q W) (Localization.inverts _ _) =
        g.map (LeftFraction.Localization.Q W) (Localization.inverts _ _) ‚Üî
      LeftFractionRel f g := by
  simp only [‚Üê Hom.map_mk _ (Q W)]
  constructor
  ¬∑ intro h
    rw [‚Üê homMk_eq_iff_leftFractionRel, homMk_eq, homMk_eq]
    exact h
  ¬∑ intro h
    congr 1
    exact Quot.sound h

end Localization

section

lemma map_eq {W} {X Y : C} (œÜ : W.LeftFraction X Y) (L : C ‚•§ D) [L.IsLocalization W] :
    œÜ.map L (Localization.inverts L W) =
      L.map œÜ.f ‚â´ (Localization.isoOfHom L W œÜ.s œÜ.hs).inv := rfl

lemma map_compatibility {W} {X Y : C}
    (œÜ : W.LeftFraction X Y) {E : Type*} [Category E]
    (L‚ÇÅ : C ‚•§ D) (L‚ÇÇ : C ‚•§ E) [L‚ÇÅ.IsLocalization W] [L‚ÇÇ.IsLocalization W] :
    (Localization.uniq L‚ÇÅ L‚ÇÇ W).functor.map (œÜ.map L‚ÇÅ (Localization.inverts L‚ÇÅ W)) =
      (Localization.compUniqFunctor L‚ÇÅ L‚ÇÇ W).hom.app X ‚â´
        œÜ.map L‚ÇÇ (Localization.inverts L‚ÇÇ W) ‚â´
        (Localization.compUniqFunctor L‚ÇÅ L‚ÇÇ W).inv.app Y := by
  let e := Localization.compUniqFunctor L‚ÇÅ L‚ÇÇ W
  have := Localization.inverts L‚ÇÇ W œÜ.s œÜ.hs
  rw [‚Üê cancel_mono (e.hom.app Y), assoc, assoc, e.inv_hom_id_app, comp_id,
    ‚Üê cancel_mono (L‚ÇÇ.map œÜ.s), assoc, assoc, map_comp_map_s, ‚Üê e.hom.naturality]
  simpa [‚Üê Functor.map_comp_assoc, map_comp_map_s] using e.hom.naturality œÜ.f

lemma map_eq_of_map_eq {W} {X Y : C}
    (œÜ‚ÇÅ œÜ‚ÇÇ : W.LeftFraction X Y) {E : Type*} [Category E]
    (L‚ÇÅ : C ‚•§ D) (L‚ÇÇ : C ‚•§ E) [L‚ÇÅ.IsLocalization W] [L‚ÇÇ.IsLocalization W]
    (h : œÜ‚ÇÅ.map L‚ÇÅ (Localization.inverts L‚ÇÅ W) = œÜ‚ÇÇ.map L‚ÇÅ (Localization.inverts L‚ÇÅ W)) :
    œÜ‚ÇÅ.map L‚ÇÇ (Localization.inverts L‚ÇÇ W) = œÜ‚ÇÇ.map L‚ÇÇ (Localization.inverts L‚ÇÇ W) := by
  apply (Localization.uniq L‚ÇÇ L‚ÇÅ W).functor.map_injective
  rw [map_compatibility œÜ‚ÇÅ L‚ÇÇ L‚ÇÅ, map_compatibility œÜ‚ÇÇ L‚ÇÇ L‚ÇÅ, h]

lemma map_comp_map_eq_map {X Y Z : C} (z‚ÇÅ : W.LeftFraction X Y) (z‚ÇÇ : W.LeftFraction Y Z)
    (z‚ÇÉ : W.LeftFraction z‚ÇÅ.Y' z‚ÇÇ.Y') (h‚ÇÉ : z‚ÇÇ.f ‚â´ z‚ÇÉ.s = z‚ÇÅ.s ‚â´ z‚ÇÉ.f)
    (L : C ‚•§ D) [L.IsLocalization W] :
    z‚ÇÅ.map L (Localization.inverts L W) ‚â´ z‚ÇÇ.map L (Localization.inverts L W) =
      (z‚ÇÅ.comp‚ÇÄ z‚ÇÇ z‚ÇÉ).map L (Localization.inverts L W) := by
  have := Localization.inverts L W _ z‚ÇÇ.hs
  have := Localization.inverts L W _ z‚ÇÉ.hs
  have : IsIso (L.map (z‚ÇÇ.s ‚â´ z‚ÇÉ.s)) := by
    rw [L.map_comp]
    infer_instance
  dsimp [LeftFraction.comp‚ÇÄ]
  rw [‚Üê cancel_mono (L.map (z‚ÇÇ.s ‚â´ z‚ÇÉ.s)), map_comp_map_s,
    L.map_comp, assoc, map_comp_map_s_assoc, ‚Üê L.map_comp, h‚ÇÉ,
    L.map_comp, map_comp_map_s_assoc, L.map_comp]

end

end LeftFraction

end

end MorphismProperty

variable (L : C ‚•§ D) (W : MorphismProperty C) [L.IsLocalization W]

section

variable [W.HasLeftCalculusOfFractions]

lemma Localization.exists_leftFraction {X Y : C} (f : L.obj X ‚ü∂ L.obj Y) :
    ‚àÉ (œÜ : W.LeftFraction X Y), f = œÜ.map L (Localization.inverts L W) := by
  let E := Localization.uniq (MorphismProperty.LeftFraction.Localization.Q W) L W
  let e : _ ‚ãô E.functor ‚âÖ L := Localization.compUniqFunctor _ _ _
  obtain ‚ü®f', rfl‚ü© : ‚àÉ (f' : E.functor.obj X ‚ü∂ E.functor.obj Y),
      f = e.inv.app _ ‚â´ f' ‚â´ e.hom.app _ := ‚ü®e.hom.app _ ‚â´ f ‚â´ e.inv.app _, by simp‚ü©
  obtain ‚ü®g, rfl‚ü© := E.functor.map_surjective f'
  obtain ‚ü®g, rfl‚ü© := MorphismProperty.LeftFraction.Localization.Hom.mk_surjective g
  refine ‚ü®g, ?_‚ü©
  rw [‚Üê MorphismProperty.LeftFraction.Localization.homMk_eq_hom_mk,
    MorphismProperty.LeftFraction.Localization.homMk_eq g,
    g.map_compatibility (MorphismProperty.LeftFraction.Localization.Q W) L,
    assoc, assoc, Iso.inv_hom_id_app, comp_id, Iso.inv_hom_id_app_assoc]

lemma MorphismProperty.LeftFraction.map_eq_iff
    {X Y : C} (œÜ œà : W.LeftFraction X Y) :
    œÜ.map L (Localization.inverts _ _) = œà.map L (Localization.inverts _ _) ‚Üî
      LeftFractionRel œÜ œà := by
  constructor
  ¬∑ intro h
    rw [‚Üê MorphismProperty.LeftFraction.Localization.map_eq_iff]
    apply map_eq_of_map_eq _ _ _ _ h
  ¬∑ intro h
    simp only [‚Üê Localization.Hom.map_mk _ L (Localization.inverts _ _)]
    congr 1
    exact Quot.sound h

lemma MorphismProperty.map_eq_iff_postcomp {X Y : C} (f‚ÇÅ f‚ÇÇ : X ‚ü∂ Y) :
    L.map f‚ÇÅ = L.map f‚ÇÇ ‚Üî ‚àÉ (Z : C) (s : Y ‚ü∂ Z) (_ : W s), f‚ÇÅ ‚â´ s = f‚ÇÇ ‚â´ s := by
  constructor
  ¬∑ intro h
    rw [‚Üê LeftFraction.map_ofHom W _ L (Localization.inverts _ _),
      ‚Üê LeftFraction.map_ofHom W _ L (Localization.inverts _ _),
      LeftFraction.map_eq_iff] at h
    obtain ‚ü®Z, t‚ÇÅ, t‚ÇÇ, hst, hft, ht‚ü© := h
    dsimp at t‚ÇÅ t‚ÇÇ hst hft ht
    grind
  ¬∑ rintro ‚ü®Z, s, hs, fac‚ü©
    simp only [‚Üê cancel_mono (Localization.isoOfHom L W s hs).hom,
      Localization.isoOfHom_hom, ‚Üê L.map_comp, fac]

include W in
lemma Localization.essSurj_mapArrow :
    L.mapArrow.EssSurj where
  mem_essImage f := by
    have := Localization.essSurj L W
    obtain ‚ü®X, ‚ü®eX‚ü©‚ü© : ‚àÉ (X : C), Nonempty (L.obj X ‚âÖ f.left) :=
      ‚ü®_, ‚ü®L.objObjPreimageIso f.left‚ü©‚ü©
    obtain ‚ü®Y, ‚ü®eY‚ü©‚ü© : ‚àÉ (Y : C), Nonempty (L.obj Y ‚âÖ f.right) :=
      ‚ü®_, ‚ü®L.objObjPreimageIso f.right‚ü©‚ü©
    obtain ‚ü®œÜ, hœÜ‚ü© := Localization.exists_leftFraction L W (eX.hom ‚â´ f.hom ‚â´ eY.inv)
    refine ‚ü®Arrow.mk œÜ.f, ‚ü®Iso.symm ?_‚ü©‚ü©
    refine Arrow.isoMk eX.symm (eY.symm ‚â™‚â´ Localization.isoOfHom L W œÜ.s œÜ.hs) ?_
    dsimp
    simp only [‚Üê cancel_epi eX.hom, Iso.hom_inv_id_assoc, reassoc_of% hœÜ,
      MorphismProperty.LeftFraction.map_comp_map_s]

end


namespace MorphismProperty

variable {W}

/-- The right fraction in the opposite category corresponding to a left fraction. -/
@[simps]
def LeftFraction.op {X Y : C} (œÜ : W.LeftFraction X Y) :
    W.op.RightFraction (Opposite.op Y) (Opposite.op X) where
  X' := Opposite.op œÜ.Y'
  s := œÜ.s.op
  hs := œÜ.hs
  f := œÜ.f.op

/-- The left fraction in the opposite category corresponding to a right fraction. -/
@[simps]
def RightFraction.op {X Y : C} (œÜ : W.RightFraction X Y) :
    W.op.LeftFraction (Opposite.op Y) (Opposite.op X) where
  Y' := Opposite.op œÜ.X'
  s := œÜ.s.op
  hs := œÜ.hs
  f := œÜ.f.op

/-- The right fraction corresponding to a left fraction in the opposite category. -/
@[simps]
def LeftFraction.unop {W : MorphismProperty C·µí·µñ}
    {X Y : C·µí·µñ} (œÜ : W.LeftFraction X Y) :
    W.unop.RightFraction (Opposite.unop Y) (Opposite.unop X) where
  X' := Opposite.unop œÜ.Y'
  s := œÜ.s.unop
  hs := œÜ.hs
  f := œÜ.f.unop

/-- The left fraction corresponding to a right fraction in the opposite category. -/
@[simps]
def RightFraction.unop {W : MorphismProperty C·µí·µñ}
    {X Y : C·µí·µñ} (œÜ : W.RightFraction X Y) :
    W.unop.LeftFraction (Opposite.unop Y) (Opposite.unop X) where
  Y' := Opposite.unop œÜ.X'
  s := œÜ.s.unop
  hs := œÜ.hs
  f := œÜ.f.unop

lemma RightFraction.op_map
    {X Y : C} (œÜ : W.RightFraction X Y) (L : C ‚•§ D) (hL : W.IsInvertedBy L) :
    (œÜ.map L hL).op = œÜ.op.map L.op hL.op := by
  dsimp [map, LeftFraction.map]
  rw [op_inv]

lemma LeftFraction.op_map
    {X Y : C} (œÜ : W.LeftFraction X Y) (L : C ‚•§ D) (hL : W.IsInvertedBy L) :
    (œÜ.map L hL).op = œÜ.op.map L.op hL.op := by
  dsimp [map, RightFraction.map]
  rw [op_inv]

instance [h : W.HasLeftCalculusOfFractions] : W.op.HasRightCalculusOfFractions where
  exists_rightFraction X Y œÜ := by
    obtain ‚ü®œà, eq‚ü© := h.exists_leftFraction œÜ.unop
    exact ‚ü®œà.op, Quiver.Hom.unop_inj eq‚ü©
  ext X Y Y' f‚ÇÅ f‚ÇÇ s hs eq := by
    obtain ‚ü®X', t, ht, fac‚ü© := h.ext f‚ÇÅ.unop f‚ÇÇ.unop s.unop hs (Quiver.Hom.op_inj eq)
    exact ‚ü®Opposite.op X', t.op, ht, Quiver.Hom.unop_inj fac‚ü©

instance [h : W.HasRightCalculusOfFractions] : W.op.HasLeftCalculusOfFractions where
  exists_leftFraction X Y œÜ := by
    obtain ‚ü®œà, eq‚ü© := h.exists_rightFraction œÜ.unop
    exact ‚ü®œà.op, Quiver.Hom.unop_inj eq‚ü©
  ext X' X Y f‚ÇÅ f‚ÇÇ s hs eq := by
    obtain ‚ü®Y', t, ht, fac‚ü© := h.ext f‚ÇÅ.unop f‚ÇÇ.unop s.unop hs (Quiver.Hom.op_inj eq)
    exact ‚ü®Opposite.op Y', t.op, ht, Quiver.Hom.unop_inj fac‚ü©

instance (W : MorphismProperty C·µí·µñ) [h : W.HasLeftCalculusOfFractions] :
    W.unop.HasRightCalculusOfFractions where
  exists_rightFraction X Y œÜ := by
    obtain ‚ü®œà, eq‚ü© := h.exists_leftFraction œÜ.op
    exact ‚ü®œà.unop, Quiver.Hom.op_inj eq‚ü©
  ext X Y Y' f‚ÇÅ f‚ÇÇ s hs eq := by
    obtain ‚ü®X', t, ht, fac‚ü© := h.ext f‚ÇÅ.op f‚ÇÇ.op s.op hs (Quiver.Hom.unop_inj eq)
    exact ‚ü®Opposite.unop X', t.unop, ht, Quiver.Hom.op_inj fac‚ü©

instance (W : MorphismProperty C·µí·µñ) [h : W.HasRightCalculusOfFractions] :
    W.unop.HasLeftCalculusOfFractions where
  exists_leftFraction X Y œÜ := by
    obtain ‚ü®œà, eq‚ü© := h.exists_rightFraction œÜ.op
    exact ‚ü®œà.unop, Quiver.Hom.op_inj eq‚ü©
  ext X' X Y f‚ÇÅ f‚ÇÇ s hs eq := by
    obtain ‚ü®Y', t, ht, fac‚ü© := h.ext f‚ÇÅ.op f‚ÇÇ.op s.op hs (Quiver.Hom.unop_inj eq)
    exact ‚ü®Opposite.unop Y', t.unop, ht, Quiver.Hom.op_inj fac‚ü©

/-- The equivalence relation on right fractions for a morphism property `W`. -/
def RightFractionRel {X Y : C} (z‚ÇÅ z‚ÇÇ : W.RightFraction X Y) : Prop :=
  ‚àÉ (Z : C) (t‚ÇÅ : Z ‚ü∂ z‚ÇÅ.X') (t‚ÇÇ : Z ‚ü∂ z‚ÇÇ.X') (_ : t‚ÇÅ ‚â´ z‚ÇÅ.s = t‚ÇÇ ‚â´ z‚ÇÇ.s)
    (_ : t‚ÇÅ ‚â´ z‚ÇÅ.f = t‚ÇÇ ‚â´ z‚ÇÇ.f), W (t‚ÇÅ ‚â´ z‚ÇÅ.s)

lemma RightFractionRel.op {X Y : C} {z‚ÇÅ z‚ÇÇ : W.RightFraction X Y}
    (h : RightFractionRel z‚ÇÅ z‚ÇÇ) : LeftFractionRel z‚ÇÅ.op z‚ÇÇ.op := by
  obtain ‚ü®Z, t‚ÇÅ, t‚ÇÇ, hs, hf, ht‚ü© := h
  exact ‚ü®Opposite.op Z, t‚ÇÅ.op, t‚ÇÇ.op, Quiver.Hom.unop_inj hs,
    Quiver.Hom.unop_inj hf, ht‚ü©

lemma RightFractionRel.unop {W : MorphismProperty C·µí·µñ} {X Y : C·µí·µñ}
    {z‚ÇÅ z‚ÇÇ : W.RightFraction X Y}
    (h : RightFractionRel z‚ÇÅ z‚ÇÇ) : LeftFractionRel z‚ÇÅ.unop z‚ÇÇ.unop := by
  obtain ‚ü®Z, t‚ÇÅ, t‚ÇÇ, hs, hf, ht‚ü© := h
  exact ‚ü®Opposite.unop Z, t‚ÇÅ.unop, t‚ÇÇ.unop, Quiver.Hom.op_inj hs,
    Quiver.Hom.op_inj hf, ht‚ü©

lemma LeftFractionRel.op {X Y : C} {z‚ÇÅ z‚ÇÇ : W.LeftFraction X Y}
    (h : LeftFractionRel z‚ÇÅ z‚ÇÇ) : RightFractionRel z‚ÇÅ.op z‚ÇÇ.op := by
  obtain ‚ü®Z, t‚ÇÅ, t‚ÇÇ, hs, hf, ht‚ü© := h
  exact ‚ü®Opposite.op Z, t‚ÇÅ.op, t‚ÇÇ.op, Quiver.Hom.unop_inj hs,
    Quiver.Hom.unop_inj hf, ht‚ü©

lemma LeftFractionRel.unop {W : MorphismProperty C·µí·µñ} {X Y : C·µí·µñ}
    {z‚ÇÅ z‚ÇÇ : W.LeftFraction X Y}
    (h : LeftFractionRel z‚ÇÅ z‚ÇÇ) : RightFractionRel z‚ÇÅ.unop z‚ÇÇ.unop := by
  obtain ‚ü®Z, t‚ÇÅ, t‚ÇÇ, hs, hf, ht‚ü© := h
  exact ‚ü®Opposite.unop Z, t‚ÇÅ.unop, t‚ÇÇ.unop, Quiver.Hom.op_inj hs,
    Quiver.Hom.op_inj hf, ht‚ü©

lemma leftFractionRel_op_iff
    {X Y : C} (z‚ÇÅ z‚ÇÇ : W.RightFraction X Y) :
    LeftFractionRel z‚ÇÅ.op z‚ÇÇ.op ‚Üî RightFractionRel z‚ÇÅ z‚ÇÇ :=
  ‚ü®fun h => h.unop, fun h => h.op‚ü©

lemma rightFractionRel_op_iff
    {X Y : C} (z‚ÇÅ z‚ÇÇ : W.LeftFraction X Y) :
    RightFractionRel z‚ÇÅ.op z‚ÇÇ.op ‚Üî LeftFractionRel z‚ÇÅ z‚ÇÇ :=
  ‚ü®fun h => h.unop, fun h => h.op‚ü©

namespace RightFractionRel

lemma refl {X Y : C} (z : W.RightFraction X Y) : RightFractionRel z z :=
  (LeftFractionRel.refl z.op).unop

lemma symm {X Y : C} {z‚ÇÅ z‚ÇÇ : W.RightFraction X Y} (h : RightFractionRel z‚ÇÅ z‚ÇÇ) :
    RightFractionRel z‚ÇÇ z‚ÇÅ :=
  h.op.symm.unop

lemma trans {X Y : C} {z‚ÇÅ z‚ÇÇ z‚ÇÉ : W.RightFraction X Y}
    [HasRightCalculusOfFractions W]
    (h‚ÇÅ‚ÇÇ : RightFractionRel z‚ÇÅ z‚ÇÇ) (h‚ÇÇ‚ÇÉ : RightFractionRel z‚ÇÇ z‚ÇÉ) :
    RightFractionRel z‚ÇÅ z‚ÇÉ :=
  (h‚ÇÅ‚ÇÇ.op.trans h‚ÇÇ‚ÇÉ.op).unop

end RightFractionRel

lemma equivalenceRightFractionRel (X Y : C) [HasRightCalculusOfFractions W] :
    @_root_.Equivalence (W.RightFraction X Y) RightFractionRel where
  refl := RightFractionRel.refl
  symm := RightFractionRel.symm
  trans := RightFractionRel.trans

end MorphismProperty

section

variable [W.HasRightCalculusOfFractions]

lemma Localization.exists_rightFraction {X Y : C} (f : L.obj X ‚ü∂ L.obj Y) :
    ‚àÉ (œÜ : W.RightFraction X Y), f = œÜ.map L (Localization.inverts L W) := by
  obtain ‚ü®œÜ, eq‚ü© := Localization.exists_leftFraction L.op W.op f.op
  refine ‚ü®œÜ.unop, Quiver.Hom.op_inj ?_‚ü©
  rw [eq, MorphismProperty.RightFraction.op_map]
  rfl

lemma MorphismProperty.RightFraction.map_eq_iff
    {X Y : C} (œÜ œà : W.RightFraction X Y) :
    œÜ.map L (Localization.inverts _ _) = œà.map L (Localization.inverts _ _) ‚Üî
      RightFractionRel œÜ œà := by
  rw [‚Üê leftFractionRel_op_iff, ‚Üê LeftFraction.map_eq_iff L.op W.op œÜ.op œà.op,
    ‚Üê œÜ.op_map L (Localization.inverts _ _), ‚Üê œà.op_map L (Localization.inverts _ _)]
  constructor
  ¬∑ apply Quiver.Hom.unop_inj
  ¬∑ apply Quiver.Hom.op_inj

lemma MorphismProperty.map_eq_iff_precomp {Y Z : C} (f‚ÇÅ f‚ÇÇ : Y ‚ü∂ Z) :
    L.map f‚ÇÅ = L.map f‚ÇÇ ‚Üî ‚àÉ (X : C) (s : X ‚ü∂ Y) (_ : W s), s ‚â´ f‚ÇÅ = s ‚â´ f‚ÇÇ := by
  constructor
  ¬∑ intro h
    rw [‚Üê RightFraction.map_ofHom W _ L (Localization.inverts _ _),
      ‚Üê RightFraction.map_ofHom W _ L (Localization.inverts _ _),
      RightFraction.map_eq_iff] at h
    obtain ‚ü®Z, t‚ÇÅ, t‚ÇÇ, hst, hft, ht‚ü© := h
    dsimp at t‚ÇÅ t‚ÇÇ hst hft ht
    grind
  ¬∑ rintro ‚ü®Z, s, hs, fac‚ü©
    simp only [‚Üê cancel_epi (Localization.isoOfHom L W s hs).hom,
      Localization.isoOfHom_hom, ‚Üê L.map_comp, fac]

include W in
lemma Localization.essSurj_mapArrow_of_hasRightCalculusOfFractions :
    L.mapArrow.EssSurj where
  mem_essImage f := by
    have := Localization.essSurj_mapArrow L.op W.op
    obtain ‚ü®g, ‚ü®e‚ü©‚ü© : ‚àÉ (g : _), Nonempty (L.op.mapArrow.obj g ‚âÖ Arrow.mk f.hom.op) :=
      ‚ü®_, ‚ü®Functor.objObjPreimageIso _ _‚ü©‚ü©
    exact ‚ü®Arrow.mk g.hom.unop, ‚ü®Arrow.isoMk (Arrow.rightFunc.mapIso e.symm).unop
      (Arrow.leftFunc.mapIso e.symm).unop (Quiver.Hom.op_inj e.inv.w.symm)‚ü©‚ü©

end

end CategoryTheory
