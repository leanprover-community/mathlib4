/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Localization.SmallHom
public import Mathlib.CategoryTheory.Shift.ShiftedHom
public import Mathlib.CategoryTheory.Shift.Localization

/-!
# Shrinking morphisms in localized categories equipped with shifts

If `C` is a category equipped with a shift by an additive monoid `M`,
and `W : MorphismProperty C` is compatible with the shift,
we define a type-class `HasSmallLocalizedShiftedHom.{w} W X Y` which
says that all the types of morphisms from `X‚ü¶a‚üß` to `Y‚ü¶b‚üß` in the
localized category are `w`-small for a certain universe. Then,
we define types `SmallShiftedHom.{w} W X Y m : Type w` for all `m : M`,
and endow these with a composition which transports the composition
on the types `ShiftedHom (L.obj X) (L.obj Y) m` when `L : C ‚•§ D` is
any localization functor for `W`.

-/

@[expose] public section

universe w'' w w' v‚ÇÅ v‚ÇÇ v‚ÇÅ' v‚ÇÇ' u‚ÇÅ u‚ÇÇ u‚ÇÅ' u‚ÇÇ'

namespace CategoryTheory

open Category

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
  (W : MorphismProperty C) {M : Type w'} [AddMonoid M] [HasShift C M] [HasShift D M]

namespace Localization

section

variable (X Y : C)
variable (M)

/-- Given objects `X` and `Y` in a category `C`, this is the property that
all the types of morphisms from `X‚ü¶a‚üß` to `Y‚ü¶b‚üß` are `w`-small
in the localized category with respect to a class of morphisms `W`. -/
abbrev HasSmallLocalizedShiftedHom : Prop :=
  ‚àÄ (a b : M), HasSmallLocalizedHom.{w} W (X‚ü¶a‚üß) (Y‚ü¶b‚üß)

lemma hasSmallLocalizedShiftedHom_iff
    (L : C ‚•§ D) [L.IsLocalization W] [L.CommShift M] (X Y : C) :
    HasSmallLocalizedShiftedHom.{w} W M X Y ‚Üî
      ‚àÄ (a b : M), Small.{w} ((L.obj X)‚ü¶a‚üß ‚ü∂ (L.obj Y)‚ü¶b‚üß) := by
  dsimp [HasSmallLocalizedShiftedHom]
  have eq := fun (a b : M) ‚Ü¶ small_congr.{w}
    (Iso.homCongr ((L.commShiftIso a).app X) ((L.commShiftIso b).app Y))
  dsimp at eq
  simp only [hasSmallLocalizedHom_iff _ L, eq]

variable {Y} in
lemma hasSmallLocalizedShiftedHom_iff_target [W.IsCompatibleWithShift M]
    {Y' : C} (f : Y ‚ü∂ Y') (hf : W f) :
    HasSmallLocalizedShiftedHom.{w} W M X Y ‚Üî HasSmallLocalizedShiftedHom.{w} W M X Y' :=
  forall_congr' (fun a ‚Ü¶ forall_congr' (fun b ‚Ü¶
    hasSmallLocalizedHom_iff_target W (X‚ü¶a‚üß) (f‚ü¶b‚üß') (W.shift hf b)))

variable {X} in
lemma hasSmallLocalizedShiftedHom_iff_source [W.IsCompatibleWithShift M]
    {X' : C} (f : X ‚ü∂ X') (hf : W f) (Y : C) :
    HasSmallLocalizedShiftedHom.{w} W M X Y ‚Üî HasSmallLocalizedShiftedHom.{w} W M X' Y :=
  forall_congr' (fun a ‚Ü¶ forall_congr' (fun b ‚Ü¶
    hasSmallLocalizedHom_iff_source W (f‚ü¶a‚üß') (W.shift hf a) (Y‚ü¶b‚üß)))

variable [HasSmallLocalizedShiftedHom.{w} W M X Y]

include M in
lemma hasSmallLocalizedHom_of_hasSmallLocalizedShiftedHom‚ÇÄ :
    HasSmallLocalizedHom.{w} W X Y :=
  (hasSmallLocalizedHom_iff_of_isos W
    ((shiftFunctorZero C M).app X) ((shiftFunctorZero C M).app Y)).1 inferInstance

variable {M}

instance (m : M) : HasSmallLocalizedHom.{w} W X (Y‚ü¶m‚üß) :=
  (hasSmallLocalizedHom_iff_of_isos W
    ((shiftFunctorZero C M).app X) (Iso.refl (Y‚ü¶m‚üß))).1 inferInstance

instance (m : M) : HasSmallLocalizedHom.{w} W (X‚ü¶m‚üß) Y :=
  (hasSmallLocalizedHom_iff_of_isos W
    (Iso.refl (X‚ü¶m‚üß)) ((shiftFunctorZero C M).app Y)).1 inferInstance

instance (m m' n : M) : HasSmallLocalizedHom.{w} W (X‚ü¶m‚üß‚ü¶m'‚üß) (Y‚ü¶n‚üß) :=
  (hasSmallLocalizedHom_iff_of_isos W
    ((shiftFunctorAdd C m m').app X) (Iso.refl (Y‚ü¶n‚üß))).1 inferInstance

instance (m n n' : M) : HasSmallLocalizedHom.{w} W (X‚ü¶m‚üß) (Y‚ü¶n‚üß‚ü¶n'‚üß) :=
  (hasSmallLocalizedHom_iff_of_isos W
    (Iso.refl (X‚ü¶m‚üß)) ((shiftFunctorAdd C n n').app Y)).1 inferInstance

end

namespace SmallHom

variable {W}
variable [W.IsCompatibleWithShift M] (L : C ‚•§ D) [L.IsLocalization W] [L.CommShift M]
  {X Y : C} [HasSmallLocalizedHom.{w} W X Y]
  (f : SmallHom.{w} W X Y) (a : M) [HasSmallLocalizedHom.{w} W (X‚ü¶a‚üß) (Y‚ü¶a‚üß)]

/-- Given `f : SmallHom W X Y` and `a : M`, this is the element
in `SmallHom W (X‚ü¶a‚üß) (Y‚ü¶a‚üß)` obtained by shifting by `a`. -/
noncomputable def shift : SmallHom.{w} W (X‚ü¶a‚üß) (Y‚ü¶a‚üß) :=
  (W.shiftLocalizerMorphism a).smallHomMap f

lemma equiv_shift : equiv W L (f.shift a) =
    (L.commShiftIso a).hom.app X ‚â´ (equiv W L f)‚ü¶a‚üß' ‚â´ (L.commShiftIso a).inv.app Y :=
  (W.shiftLocalizerMorphism a).equiv_smallHomMap _ _ _ (L.commShiftIso a) f

end SmallHom

/-- The type of morphisms from `X` to `Y‚ü¶m‚üß` in the localized category
with respect to `W : MorphismProperty C` that is shrunk to `Type w`
when `HasSmallLocalizedShiftedHom.{w} W X Y` holds. -/
def SmallShiftedHom (X Y : C) [HasSmallLocalizedShiftedHom.{w} W M X Y] (m : M) : Type w :=
  SmallHom W X (Y‚ü¶m‚üß)

namespace SmallShiftedHom

section

variable [W.IsCompatibleWithShift M] {X Y Z : C}

/-- Constructor for `SmallShiftedHom`. -/
noncomputable def mk {m : M} [HasSmallLocalizedShiftedHom.{w} W M X Y] (f : ShiftedHom X Y m) :
    SmallShiftedHom.{w} W X Y m :=
  SmallHom.mk _ f

variable {W}

/-- Given `f : SmallShiftedHom.{w} W X Y a`, this is the element in
`SmallHom.{w} W (X‚ü¶n‚üß) (Y‚ü¶a'‚üß)` that is obtained by shifting by `n`
when `a + n = a'`. -/
noncomputable def shift {a : M} [HasSmallLocalizedShiftedHom.{w} W M X Y]
    [HasSmallLocalizedShiftedHom.{w} W M Y Y]
    (f : SmallShiftedHom.{w} W X Y a) (n a' : M) (h : a + n = a') :
    SmallHom.{w} W (X‚ü¶n‚üß) (Y‚ü¶a'‚üß) :=
  (SmallHom.shift f n).comp (SmallHom.mk W ((shiftFunctorAdd' C a n a' h).inv.app Y))

/-- The composition on `SmallShiftedHom W`. -/
noncomputable def comp {a b c : M} [HasSmallLocalizedShiftedHom.{w} W M X Y]
    [HasSmallLocalizedShiftedHom.{w} W M Y Z] [HasSmallLocalizedShiftedHom.{w} W M X Z]
    [HasSmallLocalizedShiftedHom.{w} W M Z Z]
    (f : SmallShiftedHom.{w} W X Y a) (g : SmallShiftedHom.{w} W Y Z b) (h : b + a = c) :
    SmallShiftedHom.{w} W X Z c :=
  SmallHom.comp f (g.shift a c h)

variable (W) in
/-- The canonical map `(X ‚ü∂ Y) ‚Üí SmallShiftedHom.{w} W X Y m‚ÇÄ` when `m‚ÇÄ = 0` and
`[HasSmallLocalizedShiftedHom.{w} W M X Y]` holds. -/
noncomputable def mk‚ÇÄ [HasSmallLocalizedShiftedHom.{w} W M X Y]
    (m‚ÇÄ : M) (hm‚ÇÄ : m‚ÇÄ = 0) (f : X ‚ü∂ Y) :
    SmallShiftedHom.{w} W X Y m‚ÇÄ :=
  SmallShiftedHom.mk _ (ShiftedHom.mk‚ÇÄ _ hm‚ÇÄ f)

/-- The formal inverse in `SmallShiftedHom.{w} W Y X m‚ÇÄ` of a morphism `f : Y ‚ü∂ X`
such that `W f`. -/
noncomputable def mk‚ÇÄInv [HasSmallLocalizedShiftedHom.{w} W M Y X] [W.RespectsIso]
    (m‚ÇÄ : M) (hm‚ÇÄ : m‚ÇÄ = 0) (f : X ‚ü∂ Y) (hf : W f) :
    SmallShiftedHom.{w} W Y X m‚ÇÄ :=
  SmallHom.mkInv ((shiftFunctorZero' C m‚ÇÄ hm‚ÇÄ).hom.app X ‚â´ f)
    (MorphismProperty.RespectsIso.precomp _ _ _ hf)

end

section

variable (L : C ‚•§ D) [L.IsLocalization W] [L.CommShift M]
  {X Y Z T : C}

/-- The bijection `SmallShiftedHom.{w} W X Y m ‚âÉ ShiftedHom (L.obj X) (L.obj Y) m`
for all `m : M`, and `X` and `Y` in `C` when `L : C ‚•§ D` is a localization functor for
`W : MorphismProperty C` such that the category `D` is equipped with a shift by `M`
and `L` commutes with the shifts. -/
noncomputable def equiv [HasSmallLocalizedShiftedHom.{w} W M X Y] {m : M} :
    SmallShiftedHom.{w} W X Y m ‚âÉ ShiftedHom (L.obj X) (L.obj Y) m :=
  (SmallHom.equiv W L).trans ((L.commShiftIso m).app Y).homToEquiv

lemma equiv_apply [HasSmallLocalizedShiftedHom.{w} W M X Y] {m : M}
    (f : SmallShiftedHom.{w} W X Y m) :
    equiv W L f = (SmallHom.equiv W L) f ‚â´ ((L.commShiftIso m).app Y).hom :=
  rfl

section
variable [W.IsCompatibleWithShift M]

lemma equiv_shift' {a : M} [HasSmallLocalizedShiftedHom.{w} W M X Y]
    [HasSmallLocalizedShiftedHom.{w} W M Y Y]
    (f : SmallShiftedHom.{w} W X Y a) (n a' : M) (h : a + n = a') :
    SmallHom.equiv W L (f.shift n a' h) = (L.commShiftIso n).hom.app X ‚â´
      (SmallHom.equiv W L f)‚ü¶n‚üß' ‚â´ ((L.commShiftIso a).hom.app Y)‚ü¶n‚üß' ‚â´
        (shiftFunctorAdd' D a n a' h).inv.app (L.obj Y) ‚â´ (L.commShiftIso a').inv.app Y := by
  simp only [shift, SmallHom.equiv_comp, SmallHom.equiv_shift, SmallHom.equiv_mk, assoc,
    L.commShiftIso_add' h, Functor.CommShift.isoAdd'_inv_app, Iso.inv_hom_id_app_assoc,
    ‚Üê Functor.map_comp_assoc, Iso.hom_inv_id_app, Functor.comp_obj, comp_id]

lemma equiv_shift {a : M} [HasSmallLocalizedShiftedHom.{w} W M X Y]
    [HasSmallLocalizedShiftedHom.{w} W M Y Y]
    (f : SmallShiftedHom.{w} W X Y a) (n a' : M) (h : a + n = a') :
    equiv W L (f.shift n a' h) = (L.commShiftIso n).hom.app X ‚â´ (equiv W L f)‚ü¶n‚üß' ‚â´
      (shiftFunctorAdd' D a n a' h).inv.app (L.obj Y) := by
  dsimp [equiv]
  erw [Iso.homToEquiv_apply, Iso.homToEquiv_apply, equiv_shift']
  simp only [Functor.comp_obj, Iso.app_hom, assoc, Iso.inv_hom_id_app, comp_id, Functor.map_comp]
  rfl

lemma equiv_comp [HasSmallLocalizedShiftedHom.{w} W M X Y]
    [HasSmallLocalizedShiftedHom.{w} W M Y Z] [HasSmallLocalizedShiftedHom.{w} W M X Z]
    [HasSmallLocalizedShiftedHom.{w} W M Z Z] {a b c : M}
    (f : SmallShiftedHom.{w} W X Y a) (g : SmallShiftedHom.{w} W Y Z b) (h : b + a = c) :
    equiv W L (f.comp g h) = (equiv W L f).comp (equiv W L g) h := by
  dsimp [comp, equiv, ShiftedHom.comp]
  erw [Iso.homToEquiv_apply, Iso.homToEquiv_apply, Iso.homToEquiv_apply, SmallHom.equiv_comp]
  simp only [equiv_shift', Functor.comp_obj, Iso.app_hom, assoc, Iso.inv_hom_id_app,
    comp_id, Functor.map_comp]
  rfl

end

@[simp]
lemma equiv_mk [HasSmallLocalizedShiftedHom.{w} W M X Y] {m : M} (f : ShiftedHom X Y m) :
    equiv W L (.mk _ f) = f.map L :=
  ((L.commShiftIso m).app Y).homToEquiv.symm.injective
    ((Equiv.symm_apply_apply ..).trans (by simp [ShiftedHom.map, mk]))

@[simp]
lemma equiv_mk‚ÇÄ [HasSmallLocalizedShiftedHom.{w} W M X Y]
    (m‚ÇÄ : M) (hm‚ÇÄ : m‚ÇÄ = 0) (f : X ‚ü∂ Y) :
    equiv W L (SmallShiftedHom.mk‚ÇÄ W m‚ÇÄ hm‚ÇÄ f) =
      ShiftedHom.mk‚ÇÄ m‚ÇÄ hm‚ÇÄ (L.map f) := by
  subst hm‚ÇÄ
  dsimp [equiv, mk‚ÇÄ]
  erw [SmallHom.equiv_mk, Iso.homToEquiv_apply, Functor.map_comp]
  dsimp [equiv, mk‚ÇÄ, ShiftedHom.mk‚ÇÄ, shiftFunctorZero']
  simp only [comp_id, L.commShiftIso_zero, Functor.CommShift.isoZero_hom_app, assoc,
    ‚Üê Functor.map_comp_assoc, Iso.inv_hom_id_app, Functor.id_obj, Functor.map_id, id_comp]

@[simp]
lemma equiv_mk‚ÇÄInv [HasSmallLocalizedShiftedHom.{w} W M Y X] [W.RespectsIso]
    (m‚ÇÄ : M) (hm‚ÇÄ : m‚ÇÄ = 0) (f : X ‚ü∂ Y) (hf : W f) :
    equiv W L (mk‚ÇÄInv m‚ÇÄ hm‚ÇÄ f hf) =
      ShiftedHom.mk‚ÇÄ m‚ÇÄ hm‚ÇÄ ((isoOfHom L W f hf).inv) := by
  have hf' : W ((shiftFunctorZero' C m‚ÇÄ hm‚ÇÄ).hom.app X ‚â´ f) :=
    MorphismProperty.RespectsIso.precomp _ _ _ hf
  refine (SmallHom.equiv_mkInv L _ hf' =‚â´ _).trans ?_
  rw [‚Üê cancel_epi (isoOfHom L W _ hf').hom, Iso.hom_inv_id_assoc]
  simp [ShiftedHom.mk‚ÇÄ, Functor.commShiftIso_zero' _ _ m‚ÇÄ hm‚ÇÄ]

end

section

variable [W.IsCompatibleWithShift M]

lemma comp_assoc {X Y Z T : C} {a‚ÇÅ a‚ÇÇ a‚ÇÉ a‚ÇÅ‚ÇÇ a‚ÇÇ‚ÇÉ a : M}
    [HasSmallLocalizedShiftedHom.{w} W M X Y] [HasSmallLocalizedShiftedHom.{w} W M X Z]
    [HasSmallLocalizedShiftedHom.{w} W M X T] [HasSmallLocalizedShiftedHom.{w} W M Y Z]
    [HasSmallLocalizedShiftedHom.{w} W M Y T] [HasSmallLocalizedShiftedHom.{w} W M Z T]
    [HasSmallLocalizedShiftedHom.{w} W M Z Z] [HasSmallLocalizedShiftedHom.{w} W M T T]
    (Œ± : SmallShiftedHom.{w} W X Y a‚ÇÅ) (Œ≤ : SmallShiftedHom.{w} W Y Z a‚ÇÇ)
    (Œ≥ : SmallShiftedHom.{w} W Z T a‚ÇÉ)
    (h‚ÇÅ‚ÇÇ : a‚ÇÇ + a‚ÇÅ = a‚ÇÅ‚ÇÇ) (h‚ÇÇ‚ÇÉ : a‚ÇÉ + a‚ÇÇ = a‚ÇÇ‚ÇÉ) (h : a‚ÇÉ + a‚ÇÇ + a‚ÇÅ = a) :
    (Œ±.comp Œ≤ h‚ÇÅ‚ÇÇ).comp Œ≥ (show a‚ÇÉ + a‚ÇÅ‚ÇÇ = a by rw [‚Üê h‚ÇÅ‚ÇÇ, ‚Üê add_assoc, h]) =
      Œ±.comp (Œ≤.comp Œ≥ h‚ÇÇ‚ÇÉ) (by rw [‚Üê h‚ÇÇ‚ÇÉ, h]) := by
  apply (equiv W W.Q).injective
  simp only [equiv_comp, ShiftedHom.comp_assoc _ _ _ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ h]

end

variable {W} in
@[simp]
lemma mk‚ÇÄ_comp_mk‚ÇÄInv {X Y : C} [HasSmallLocalizedShiftedHom.{w} W M X Y]
    [HasSmallLocalizedShiftedHom.{w} W M Y Y]
    [HasSmallLocalizedShiftedHom.{w} W M Y X] [W.IsCompatibleWithShift M] [W.RespectsIso]
    (m‚ÇÄ : M) (hm‚ÇÄ : m‚ÇÄ = 0) (f : Y ‚ü∂ X) (hf : W f) :
    (mk‚ÇÄ W m‚ÇÄ hm‚ÇÄ f).comp (mk‚ÇÄInv m‚ÇÄ hm‚ÇÄ f hf) (by subst hm‚ÇÄ; simp) =
      mk‚ÇÄ W m‚ÇÄ hm‚ÇÄ (ùüô Y) :=
  (equiv W W.Q).injective (by simp [equiv_comp])

variable {W} in
@[simp]
lemma mk‚ÇÄInv_comp_mk‚ÇÄ {X Y : C} [HasSmallLocalizedShiftedHom.{w} W M X Y]
    [HasSmallLocalizedShiftedHom.{w} W M X X]
    [HasSmallLocalizedShiftedHom.{w} W M Y X] [W.IsCompatibleWithShift M] [W.RespectsIso]
    (m‚ÇÄ : M) (hm‚ÇÄ : m‚ÇÄ = 0) (f : Y ‚ü∂ X) (hf : W f) :
    (mk‚ÇÄInv m‚ÇÄ hm‚ÇÄ f hf).comp (mk‚ÇÄ W m‚ÇÄ hm‚ÇÄ f) (by subst hm‚ÇÄ; simp) =
      mk‚ÇÄ W m‚ÇÄ hm‚ÇÄ (ùüô X) :=
  (equiv W W.Q).injective (by simp [equiv_comp])

variable {W} in
@[simp]
lemma comp_mk‚ÇÄ_id {X Y : C} [HasSmallLocalizedShiftedHom.{w} W M X Y]
    [HasSmallLocalizedShiftedHom.{w} W M Y Y]
    [W.IsCompatibleWithShift M] {m : M}
    (Œ± : SmallShiftedHom.{w} W X Y m) (m‚ÇÄ : M) (hm‚ÇÄ : m‚ÇÄ = 0) :
    Œ±.comp (mk‚ÇÄ W m‚ÇÄ hm‚ÇÄ (ùüô Y)) (by aesop) = Œ± :=
  (equiv W W.Q).injective (by simp [equiv_comp])

variable {W} in
@[simp]
lemma mk‚ÇÄ_id_comp {X Y : C} [HasSmallLocalizedShiftedHom.{w} W M X Y]
    [HasSmallLocalizedShiftedHom.{w} W M X X]
    [HasSmallLocalizedShiftedHom.{w} W M Y Y]
    [W.IsCompatibleWithShift M] {m : M}
    (Œ± : SmallShiftedHom.{w} W X Y m) (m‚ÇÄ : M) (hm‚ÇÄ : m‚ÇÄ = 0) :
    (mk‚ÇÄ W m‚ÇÄ hm‚ÇÄ (ùüô X)).comp Œ± (by aesop) = Œ± :=
  (equiv W W.Q).injective (by simp [equiv_comp])

variable {W} in
/-- The postcomposition on the types `SmallShiftedHom W` with a morphism
which satisfies `W` is a bijection. -/
@[simps!]
noncomputable def postcompEquiv {X Y Z : C}
    [W.RespectsIso] [W.IsCompatibleWithShift M]
    [HasSmallLocalizedShiftedHom.{w} W M X Y]
    [HasSmallLocalizedShiftedHom.{w} W M Y Z]
    [HasSmallLocalizedShiftedHom.{w} W M X Z]
    [HasSmallLocalizedShiftedHom.{w} W M Z Y]
    [HasSmallLocalizedShiftedHom.{w} W M Y Y]
    [HasSmallLocalizedShiftedHom.{w} W M Z Z]
    (f : Y ‚ü∂ Z) (hf : W f) {a : M} :
    SmallShiftedHom.{w} W X Y a ‚âÉ SmallShiftedHom.{w} W X Z a where
  toFun Œ± := Œ±.comp (mk‚ÇÄ _ _ rfl f) (zero_add _)
  invFun Œ≤ := Œ≤.comp (mk‚ÇÄInv _ rfl _ hf) (zero_add _)
  left_inv Œ± := by simp [comp_assoc]
  right_inv Œ≤ := by simp [comp_assoc]

variable {W} in
/-- The precomposition on the types `SmallShiftedHom W` with a morphism
which satisfies `W` is a bijection. -/
@[simps!]
noncomputable def precompEquiv {X Y Z : C}
    [W.RespectsIso] [W.IsCompatibleWithShift M]
    [HasSmallLocalizedShiftedHom.{w} W M X X]
    [HasSmallLocalizedShiftedHom.{w} W M Y Y]
    [HasSmallLocalizedShiftedHom.{w} W M X Y]
    [HasSmallLocalizedShiftedHom.{w} W M Y X]
    [HasSmallLocalizedShiftedHom.{w} W M Y Z]
    [HasSmallLocalizedShiftedHom.{w} W M X Z]
    [HasSmallLocalizedShiftedHom.{w} W M Z Z]
    (f : X ‚ü∂ Y) (hf : W f) {a : M} :
    SmallShiftedHom.{w} W Y Z a ‚âÉ SmallShiftedHom.{w} W X Z a where
  toFun Œ± := (mk‚ÇÄ _ _ rfl f).comp Œ± (add_zero _)
  invFun Œ≤ := (mk‚ÇÄInv _ rfl _ hf).comp Œ≤ (add_zero _)
  left_inv Œ± := by simp [‚Üê comp_assoc]
  right_inv Œ≤ := by simp [‚Üê comp_assoc]

section ChangeOfUniverse

variable {W}

/-- Up to an equivalence, the type `SmallShiftedHom.{w} W X Y m` does
not depend on the universe `w`. -/
noncomputable def chgUniv {X Y : C} {m : M}
    [HasSmallLocalizedShiftedHom.{w} W M X Y]
    [HasSmallLocalizedShiftedHom.{w''} W M X Y] :
    SmallShiftedHom.{w} W X Y m ‚âÉ SmallShiftedHom.{w''} W X Y m :=
  SmallHom.chgUniv

lemma equiv_chgUniv (L : C ‚•§ D) [L.IsLocalization W] [L.CommShift M] {X Y : C} {m : M}
    [HasSmallLocalizedShiftedHom.{w} W M X Y]
    [HasSmallLocalizedShiftedHom.{w''} W M X Y]
    (e : SmallShiftedHom.{w} W X Y m) :
    equiv W L (chgUniv.{w''} e) = equiv W L e := by
  dsimp [equiv]
  congr
  apply SmallHom.equiv_chgUniv

end ChangeOfUniverse

end SmallShiftedHom

end Localization

namespace LocalizerMorphism

open Localization

variable {C‚ÇÅ : Type u‚ÇÅ} [Category.{v‚ÇÅ} C‚ÇÅ] {C‚ÇÇ : Type u‚ÇÇ} [Category.{v‚ÇÇ} C‚ÇÇ]
  {D‚ÇÅ : Type u‚ÇÅ'} [Category.{v‚ÇÅ'} D‚ÇÅ] {D‚ÇÇ : Type u‚ÇÇ'} [Category.{v‚ÇÇ'} D‚ÇÇ]
  {W‚ÇÅ : MorphismProperty C‚ÇÅ} {W‚ÇÇ : MorphismProperty C‚ÇÇ}
  (Œ¶ : LocalizerMorphism W‚ÇÅ W‚ÇÇ)
  (L‚ÇÅ : C‚ÇÅ ‚•§ D‚ÇÅ) (L‚ÇÇ : C‚ÇÇ ‚•§ D‚ÇÇ) [L‚ÇÅ.IsLocalization W‚ÇÅ] [L‚ÇÇ.IsLocalization W‚ÇÇ]
  {M : Type w'} [AddMonoid M] [HasShift C‚ÇÅ M] [HasShift C‚ÇÇ M]
  [HasShift D‚ÇÅ M] [HasShift D‚ÇÇ M] [L‚ÇÅ.CommShift M] [L‚ÇÇ.CommShift M]
  [Œ¶.functor.CommShift M]
  {X‚ÇÅ Y‚ÇÅ Z‚ÇÅ : C‚ÇÅ} {X‚ÇÇ Y‚ÇÇ Z‚ÇÇ : C‚ÇÇ}
  [HasSmallLocalizedShiftedHom.{w} W‚ÇÅ M X‚ÇÅ Y‚ÇÅ] [HasSmallLocalizedShiftedHom.{w''} W‚ÇÇ M X‚ÇÇ X‚ÇÇ]
  [HasSmallLocalizedShiftedHom.{w''} W‚ÇÇ M X‚ÇÇ Y‚ÇÇ] [HasSmallLocalizedShiftedHom.{w''} W‚ÇÇ M Y‚ÇÇ Y‚ÇÇ]
  (eX : Œ¶.functor.obj X‚ÇÅ ‚âÖ X‚ÇÇ) (eY : Œ¶.functor.obj Y‚ÇÅ ‚âÖ Y‚ÇÇ)
  (eZ : Œ¶.functor.obj Z‚ÇÅ ‚âÖ Z‚ÇÇ)

/-- The action of a localizer morphism `Œ¶` on `SmallShiftedHom`. -/
noncomputable def smallShiftedHomMap {m : M} (f : SmallShiftedHom.{w} W‚ÇÅ X‚ÇÅ Y‚ÇÅ m) :
      SmallShiftedHom.{w''} W‚ÇÇ X‚ÇÇ Y‚ÇÇ m :=
  have := hasSmallLocalizedHom_of_hasSmallLocalizedShiftedHom‚ÇÄ.{w''} W‚ÇÇ M X‚ÇÇ X‚ÇÇ
  Œ¶.smallHomMap' eX ((Œ¶.functor.commShiftIso m).app Y‚ÇÅ ‚â™‚â´ (shiftFunctor _ _).mapIso eY) f

lemma equiv_smallShiftedHomMap (G : D‚ÇÅ ‚•§ D‚ÇÇ) [G.CommShift M]
    (e : Œ¶.functor ‚ãô L‚ÇÇ ‚âÖ L‚ÇÅ ‚ãô G) [NatTrans.CommShift e.hom M]
    {m : M} (f : SmallShiftedHom.{w} W‚ÇÅ X‚ÇÅ Y‚ÇÅ m) :
    SmallShiftedHom.equiv W‚ÇÇ L‚ÇÇ (Œ¶.smallShiftedHomMap eX eY f) =
      (ShiftedHom.mk‚ÇÄ 0 rfl (L‚ÇÇ.map eX.inv ‚â´ e.hom.app _)).comp
        (((SmallShiftedHom.equiv W‚ÇÅ L‚ÇÅ f).map G).comp
          ((ShiftedHom.mk‚ÇÄ 0 rfl (e.inv.app _ ‚â´ L‚ÇÇ.map eY.hom)))
          (zero_add m)) (add_zero m) := by
  have := hasSmallLocalizedHom_of_hasSmallLocalizedShiftedHom‚ÇÄ.{w''} W‚ÇÇ M X‚ÇÇ X‚ÇÇ
  apply ((L‚ÇÇ.commShiftIso m).app Y‚ÇÇ).homToEquiv.symm.injective
  simp only [Functor.comp_obj, SmallShiftedHom.equiv_apply, Iso.app_hom,
    Iso.homToEquiv_symm_apply, Iso.app_inv, assoc, Iso.hom_inv_id_app, comp_id]
  refine (Œ¶.equiv_smallHomMap' L‚ÇÅ L‚ÇÇ _ _ G e f).trans ?_
  simp only [Functor.comp_obj, NatTrans.app_shift,
    Functor.commShiftIso_comp_hom_app, Functor.commShiftIso_comp_inv_app, assoc,
    Iso.trans_hom, Iso.app_hom, Functor.mapIso_hom, Functor.map_comp, ShiftedHom.map,
    ShiftedHom.comp_mk‚ÇÄ, ShiftedHom.mk‚ÇÄ_comp,
    Functor.commShiftIso_inv_naturality]
  nth_rw 2 [‚Üê Functor.map_comp_assoc]
  simp

variable [W‚ÇÅ.IsCompatibleWithShift M] [W‚ÇÇ.IsCompatibleWithShift M]

@[simp]
lemma smallShiftedHomMap_mk {m : M} (f : ShiftedHom X‚ÇÅ Y‚ÇÅ m) :
    Œ¶.smallShiftedHomMap eX eY (.mk _ f) =
      .mk _ ((ShiftedHom.mk‚ÇÄ _ rfl eX.inv).comp
        ((f.map Œ¶.functor).comp (.mk‚ÇÄ _ rfl eY.hom) (zero_add m)) (add_zero _)) := by
  apply (SmallShiftedHom.equiv W‚ÇÇ W‚ÇÇ.Q).injective
  let e := CatCommSq.iso Œ¶.functor W‚ÇÅ.Q W‚ÇÇ.Q (Œ¶.localizedFunctor W‚ÇÅ.Q W‚ÇÇ.Q)
  simp only [Œ¶.equiv_smallShiftedHomMap W‚ÇÅ.Q W‚ÇÇ.Q _ _ (Œ¶.localizedFunctor W‚ÇÅ.Q W‚ÇÇ.Q) e,
    Functor.comp_obj, ShiftedHom.map, SmallShiftedHom.equiv_mk, Functor.map_comp, assoc,
    ShiftedHom.comp_mk‚ÇÄ, NatTrans.shift_app, Functor.commShiftIso_comp_inv_app,
    Functor.commShiftIso_comp_hom_app, Iso.hom_inv_id_app_assoc, ShiftedHom.mk‚ÇÄ_comp,
    Functor.commShiftIso_hom_naturality]
  nth_rw 2 [‚Üê Functor.map_comp_assoc]
  simp [reassoc_of% (NatIso.naturality_2 e f)]

lemma smallShiftedHomMap_mk‚ÇÄ (m‚ÇÄ : M) (hm‚ÇÄ : m‚ÇÄ = 0) (f : X‚ÇÅ ‚ü∂ Y‚ÇÅ) :
    Œ¶.smallShiftedHomMap eX eY (.mk‚ÇÄ _ _ hm‚ÇÄ f) =
      .mk‚ÇÄ _ _ hm‚ÇÄ (eX.inv ‚â´ Œ¶.functor.map f ‚â´ eY.hom) := by
  simp [SmallShiftedHom.mk‚ÇÄ]

lemma smallShiftedHomMap_comp
    [HasSmallLocalizedShiftedHom.{w} W‚ÇÅ M Y‚ÇÅ Z‚ÇÅ] [HasSmallLocalizedShiftedHom.{w''} W‚ÇÇ M Z‚ÇÇ Z‚ÇÇ]
    [HasSmallLocalizedShiftedHom.{w''} W‚ÇÇ M Y‚ÇÇ Z‚ÇÇ] [HasSmallLocalizedShiftedHom.{w} W‚ÇÅ M X‚ÇÅ Z‚ÇÅ]
    [HasSmallLocalizedShiftedHom.{w} W‚ÇÅ M Z‚ÇÅ Z‚ÇÅ] [HasSmallLocalizedShiftedHom.{w''} W‚ÇÇ M X‚ÇÇ Z‚ÇÇ]
    {a b c : M} (f : SmallShiftedHom.{w} W‚ÇÅ X‚ÇÅ Y‚ÇÅ a) (g : SmallShiftedHom.{w} W‚ÇÅ Y‚ÇÅ Z‚ÇÅ b)
    (h : b + a = c) :
    Œ¶.smallShiftedHomMap eX eZ (f.comp g h) =
      (Œ¶.smallShiftedHomMap eX eY f).comp (Œ¶.smallShiftedHomMap eY eZ g) h := by
  apply (SmallShiftedHom.equiv W‚ÇÇ W‚ÇÇ.Q).injective
  let e := CatCommSq.iso Œ¶.functor W‚ÇÅ.Q W‚ÇÇ.Q (Œ¶.localizedFunctor W‚ÇÅ.Q W‚ÇÇ.Q)
  simp only [Œ¶.equiv_smallShiftedHomMap W‚ÇÅ.Q W‚ÇÇ.Q _ _ (Œ¶.localizedFunctor W‚ÇÅ.Q W‚ÇÇ.Q) e,
    SmallShiftedHom.equiv_comp, ShiftedHom.map_comp]
  rw [ShiftedHom.comp_assoc _ _ _ _ (zero_add b) (by simpa),
    ShiftedHom.comp_assoc _ _ _ _ h (by simpa),
    ShiftedHom.comp_assoc _ _ _ _ (add_zero b) (by simpa),
    ‚Üê ShiftedHom.comp_assoc _ _ _ (add_zero 0) (add_zero b) (by simp),
    ShiftedHom.mk‚ÇÄ_comp_mk‚ÇÄ]
  simp

end LocalizerMorphism

end CategoryTheory
