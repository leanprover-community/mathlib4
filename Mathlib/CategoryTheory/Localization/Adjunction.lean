/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.CatCommSq
import Mathlib.CategoryTheory.Localization.Predicate
import Mathlib.CategoryTheory.Adjunction.FullyFaithful

/-!
# Localization of adjunctions

In this file, we show that if we have an adjunction `adj : G âŠ£ F` such that both
functors `G : Câ‚ â¥¤ Câ‚‚` and `F : Câ‚‚ â¥¤ Câ‚` induce functors
`G' : Dâ‚ â¥¤ Dâ‚‚` and `F' : Dâ‚‚ â¥¤ Dâ‚` on localized categories, i.e. that we
have localization functors `Lâ‚ : Câ‚ â¥¤ Dâ‚` and `Lâ‚‚ : Câ‚‚ â¥¤ Dâ‚‚` with respect
to morphism properties `Wâ‚` and `Wâ‚‚` respectively, and 2-commutative diagrams
`[CatCommSq G Lâ‚ Lâ‚‚ G']` and `[CatCommSq F Lâ‚‚ Lâ‚ F']`, then we have an
induced adjunction `Adjunction.localization Lâ‚ Wâ‚ Lâ‚‚ Wâ‚‚ G' F' : G' âŠ£ F'`.

-/

namespace CategoryTheory

open Localization Category Functor

namespace Adjunction

variable {Câ‚ Câ‚‚ Dâ‚ Dâ‚‚ : Type*} [Category Câ‚] [Category Câ‚‚] [Category Dâ‚] [Category Dâ‚‚]
  {G : Câ‚ â¥¤ Câ‚‚} {F : Câ‚‚ â¥¤ Câ‚} (adj : G âŠ£ F)

section

variable (Lâ‚ : Câ‚ â¥¤ Dâ‚) (Wâ‚ : MorphismProperty Câ‚) [Lâ‚.IsLocalization Wâ‚]
  (Lâ‚‚ : Câ‚‚ â¥¤ Dâ‚‚) (Wâ‚‚ : MorphismProperty Câ‚‚) [Lâ‚‚.IsLocalization Wâ‚‚]
  (G' : Dâ‚ â¥¤ Dâ‚‚) (F' : Dâ‚‚ â¥¤ Dâ‚)
  [CatCommSq G Lâ‚ Lâ‚‚ G'] [CatCommSq F Lâ‚‚ Lâ‚ F']


namespace Localization

/-- Auxiliary definition of the unit morphism for the adjunction `Adjunction.localization` -/
noncomputable def Îµ : ðŸ­ Dâ‚ âŸ¶ G' â‹™ F' := by
  letI : Lifting Lâ‚ Wâ‚ ((G â‹™ F) â‹™ Lâ‚) (G' â‹™ F') :=
    Lifting.mk (CatCommSq.hComp G F Lâ‚ Lâ‚‚ Lâ‚ G' F').iso.symm
  exact Localization.liftNatTrans Lâ‚ Wâ‚ Lâ‚ ((G â‹™ F) â‹™ Lâ‚) (ðŸ­ Dâ‚) (G' â‹™ F')
    (whiskerRight adj.unit Lâ‚)

lemma Îµ_app (Xâ‚ : Câ‚) :
    (Îµ adj Lâ‚ Wâ‚ Lâ‚‚ G' F').app (Lâ‚.obj Xâ‚) =
      Lâ‚.map (adj.unit.app Xâ‚) â‰« (CatCommSq.iso F Lâ‚‚ Lâ‚ F').hom.app (G.obj Xâ‚) â‰«
        F'.map ((CatCommSq.iso G Lâ‚ Lâ‚‚ G').hom.app Xâ‚) := by
  letI : Lifting Lâ‚ Wâ‚ ((G â‹™ F) â‹™ Lâ‚) (G' â‹™ F') :=
    Lifting.mk (CatCommSq.hComp G F Lâ‚ Lâ‚‚ Lâ‚ G' F').iso.symm
  simp only [Îµ, liftNatTrans_app, Lifting.iso, Iso.symm,
    Functor.id_obj, Functor.comp_obj, Functor.rightUnitor_hom_app,
      whiskerRight_app, CatCommSq.hComp_iso_hom_app, id_comp]

/-- Auxiliary definition of the counit morphism for the adjunction `Adjunction.localization` -/
noncomputable def Î· : F' â‹™ G' âŸ¶ ðŸ­ Dâ‚‚ := by
  letI : Lifting Lâ‚‚ Wâ‚‚ ((F â‹™ G) â‹™ Lâ‚‚) (F' â‹™ G') :=
    Lifting.mk (CatCommSq.hComp F G Lâ‚‚ Lâ‚ Lâ‚‚ F' G').iso.symm
  exact liftNatTrans Lâ‚‚ Wâ‚‚ ((F â‹™ G) â‹™ Lâ‚‚) Lâ‚‚ (F' â‹™ G') (ðŸ­ Dâ‚‚) (whiskerRight adj.counit Lâ‚‚)

lemma Î·_app (Xâ‚‚ : Câ‚‚) :
    (Î· adj Lâ‚ Lâ‚‚ Wâ‚‚ G' F').app (Lâ‚‚.obj Xâ‚‚) =
      G'.map ((CatCommSq.iso F Lâ‚‚ Lâ‚ F').inv.app Xâ‚‚) â‰«
        (CatCommSq.iso G Lâ‚ Lâ‚‚ G').inv.app (F.obj Xâ‚‚) â‰«
        Lâ‚‚.map (adj.counit.app Xâ‚‚) := by
  letI : Lifting Lâ‚‚ Wâ‚‚ ((F â‹™ G) â‹™ Lâ‚‚) (F' â‹™ G') :=
    Lifting.mk (CatCommSq.hComp F G Lâ‚‚ Lâ‚ Lâ‚‚ F' G').iso.symm
  simp only [Î·, liftNatTrans_app, Lifting.iso, Iso.symm, CatCommSq.hComp_iso_inv_app,
    whiskerRight_app, Functor.rightUnitor_inv_app, comp_id, assoc]

end Localization

/-- If `adj : G âŠ£ F` is an adjunction between two categories `Câ‚` and `Câ‚‚` that
are equipped with localization functors `Lâ‚ : Câ‚ â¥¤ Dâ‚` and `Lâ‚‚ : Câ‚‚ â¥¤ Dâ‚‚` with
respect to `Wâ‚ : MorphismProperty Câ‚` and `Wâ‚‚ : MorphismProperty Câ‚‚`, and that
the functors `F : Câ‚‚ â¥¤ Câ‚` and `G : Câ‚ â¥¤ Câ‚‚` induce functors `F' : Dâ‚‚ â¥¤ Dâ‚`
and `G' : Dâ‚ â¥¤ Dâ‚‚` on the localized categories, then the adjunction `adj`
induces an adjunction `G' âŠ£ F'`. -/
noncomputable def localization : G' âŠ£ F' :=
  Adjunction.mkOfUnitCounit
    { unit := Localization.Îµ adj Lâ‚ Wâ‚ Lâ‚‚ G' F'
      counit := Localization.Î· adj Lâ‚ Lâ‚‚ Wâ‚‚ G' F'
      left_triangle := by
        apply natTrans_ext Lâ‚ Wâ‚
        intro Xâ‚
        have eq := congr_app adj.left_triangle Xâ‚
        dsimp at eq
        rw [NatTrans.comp_app, NatTrans.comp_app, whiskerRight_app, Localization.Îµ_app,
          Functor.associator_hom_app, id_comp, whiskerLeft_app, G'.map_comp, G'.map_comp,
          assoc, assoc]
        erw [(Localization.Î· adj Lâ‚ Lâ‚‚ Wâ‚‚ G' F').naturality, Localization.Î·_app,
          assoc, assoc, â† G'.map_comp_assoc, â† G'.map_comp_assoc, assoc, Iso.hom_inv_id_app,
          comp_id, (CatCommSq.iso G Lâ‚ Lâ‚‚ G').inv.naturality_assoc, â† Lâ‚‚.map_comp_assoc, eq,
          Lâ‚‚.map_id, id_comp, Iso.inv_hom_id_app]
        rfl
      right_triangle := by
        apply natTrans_ext Lâ‚‚ Wâ‚‚
        intro Xâ‚‚
        have eq := congr_app adj.right_triangle Xâ‚‚
        dsimp at eq
        rw [NatTrans.comp_app, NatTrans.comp_app, whiskerLeft_app, whiskerRight_app,
          Localization.Î·_app, Functor.associator_inv_app, id_comp, F'.map_comp, F'.map_comp]
        erw [â† (Localization.Îµ _ _ _ _ _ _).naturality_assoc, Localization.Îµ_app,
          assoc, assoc, â† F'.map_comp_assoc, Iso.hom_inv_id_app, F'.map_id, id_comp,
          â† NatTrans.naturality, â† Lâ‚.map_comp_assoc, eq, Lâ‚.map_id, id_comp,
          Iso.inv_hom_id_app]
        rfl }

@[simp]
lemma localization_unit_app (Xâ‚ : Câ‚) :
    (adj.localization Lâ‚ Wâ‚ Lâ‚‚ Wâ‚‚ G' F').unit.app (Lâ‚.obj Xâ‚) =
    Lâ‚.map (adj.unit.app Xâ‚) â‰« (CatCommSq.iso F Lâ‚‚ Lâ‚ F').hom.app (G.obj Xâ‚) â‰«
      F'.map ((CatCommSq.iso G Lâ‚ Lâ‚‚ G').hom.app Xâ‚) := by
  apply Localization.Îµ_app

@[simp]
lemma localization_counit_app (Xâ‚‚ : Câ‚‚) :
    (adj.localization Lâ‚ Wâ‚ Lâ‚‚ Wâ‚‚ G' F').counit.app (Lâ‚‚.obj Xâ‚‚) =
    G'.map ((CatCommSq.iso F Lâ‚‚ Lâ‚ F').inv.app Xâ‚‚) â‰«
      (CatCommSq.iso G Lâ‚ Lâ‚‚ G').inv.app (F.obj Xâ‚‚) â‰«
      Lâ‚‚.map (adj.counit.app Xâ‚‚) := by
  apply Localization.Î·_app

end

include adj in
lemma isLocalization [F.Full] [F.Faithful] :
    G.IsLocalization ((MorphismProperty.isomorphisms Câ‚‚).inverseImage G) := by
  let W := ((MorphismProperty.isomorphisms Câ‚‚).inverseImage G)
  have hG : W.IsInvertedBy G := fun _ _ _ hf => hf
  have : âˆ€ (X : Câ‚), IsIso ((whiskerRight adj.unit W.Q).app X) := fun X =>
    Localization.inverts W.Q W _ (by
      change IsIso _
      infer_instance)
  have : IsIso (whiskerRight adj.unit W.Q) := NatIso.isIso_of_isIso_app _
  let e : W.Localization â‰Œ Câ‚‚ := Equivalence.mk (Localization.lift G hG W.Q) (F â‹™ W.Q)
    (liftNatIso W.Q W W.Q (G â‹™ F â‹™ W.Q) _ _
    (W.Q.leftUnitor.symm â‰ªâ‰« asIso (whiskerRight adj.unit W.Q)))
    (Functor.associator _ _ _ â‰ªâ‰« isoWhiskerLeft _ (Localization.fac G hG W.Q) â‰ªâ‰«
      asIso adj.counit)
  apply Functor.IsLocalization.of_equivalence_target W.Q W G e
    (Localization.fac G hG W.Q)

end Adjunction

end CategoryTheory
