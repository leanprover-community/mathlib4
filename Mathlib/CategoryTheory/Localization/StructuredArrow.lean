/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Localization.HomEquiv
import Mathlib.CategoryTheory.Localization.Opposite
import Mathlib.CategoryTheory.Comma.StructuredArrow.Basic

/-!
# Induction principles for structured and costructured arrows

Assume that `L : C â¥¤ D` is a localization functor for `W : MorphismProperty C`.
Given `X : C` and a predicate `P` on `StructuredArrow (L.obj X) L`, we obtain
the lemma `Localization.induction_structuredArrow` which shows that `P` holds for
all structured arrows if `P` holds for the identity map `ðŸ™ (L.obj X)`,
if `P` is stable by post-composition with `L.map f` for any `f`
and if `P` is stable by post-composition with the inverse of `L.map w` when `W w`.

We obtain a similar lemma `Localization.induction_costructuredArrow` for
costructured arrows.

-/

namespace CategoryTheory

open Opposite

variable {C D D' : Type*} [Category C] [Category D] [Category D']

namespace Localization

section

variable (W : MorphismProperty C) (L : C â¥¤ D) (L' : C â¥¤ D')
  [L.IsLocalization W] [L'.IsLocalization W] {X : C}

/-- The bijection `StructuredArrow (L.obj X) L â‰ƒ StructuredArrow (L'.obj X) L'`
when `L` and `L'` are two localization functors for the same class of morphisms. -/
@[simps]
noncomputable def structuredArrowEquiv :
    StructuredArrow (L.obj X) L â‰ƒ StructuredArrow (L'.obj X) L' where
  toFun f := StructuredArrow.mk (homEquiv W L L' f.hom)
  invFun f := StructuredArrow.mk (homEquiv W L' L f.hom)
  left_inv f := by
    obtain âŸ¨Y, f, rflâŸ© := f.mk_surjective
    dsimp
    rw [â† homEquiv_symm_apply, Equiv.symm_apply_apply]
  right_inv f := by
    obtain âŸ¨Y, f, rflâŸ© := f.mk_surjective
    dsimp
    rw [â† homEquiv_symm_apply, Equiv.symm_apply_apply]

end

section

variable (W : MorphismProperty C) {X : C}
  (P : StructuredArrow (W.Q.obj X) W.Q â†’ Prop)

open Construction in
private lemma induction_structuredArrow'
    (hPâ‚€ : P (StructuredArrow.mk (ðŸ™ (W.Q.obj X))))
    (hPâ‚ : âˆ€ â¦ƒYâ‚ Yâ‚‚ : Câ¦„ (f : Yâ‚ âŸ¶ Yâ‚‚) (Ï† : W.Q.obj X âŸ¶ W.Q.obj Yâ‚),
      P (StructuredArrow.mk Ï†) â†’ P (StructuredArrow.mk (Ï† â‰« W.Q.map f)))
    (hPâ‚‚ : âˆ€ â¦ƒYâ‚ Yâ‚‚ : Câ¦„ (w : Yâ‚ âŸ¶ Yâ‚‚) (hw : W w) (Ï† : W.Q.obj X âŸ¶ W.Q.obj Yâ‚‚),
      P (StructuredArrow.mk Ï†) â†’ P (StructuredArrow.mk (Ï† â‰« (isoOfHom W.Q W w hw).inv)))
    (g : StructuredArrow (W.Q.obj X) W.Q) : P g := by
  let Xâ‚€ : Paths (LocQuiver W) := âŸ¨XâŸ©
  suffices âˆ€ â¦ƒYâ‚€ : Paths (LocQuiver W)â¦„ (f : Xâ‚€ âŸ¶ Yâ‚€),
      P (StructuredArrow.mk ((Quotient.functor (relations W)).map f)) by
    obtain âŸ¨Y, g, rflâŸ© := g.mk_surjective
    obtain âŸ¨g, rflâŸ© := (Quotient.functor (relations W)).map_surjective g
    exact this g
  intro Yâ‚€ f
  induction f with
  | nil => exact hPâ‚€
  | cons f g hf =>
      obtain (g|âŸ¨w, hwâŸ©) := g
      Â· exact hPâ‚ g _ hf
      Â· simpa only [â† Construction.wInv_eq_isoOfHom_inv w hw] using hPâ‚‚ w hw _ hf

end

section

variable (L : C â¥¤ D) (W : MorphismProperty C) [L.IsLocalization W] {X : C}
  (P : StructuredArrow (L.obj X) L â†’ Prop)


@[elab_as_elim]
lemma induction_structuredArrow
    (hPâ‚€ : P (StructuredArrow.mk (ðŸ™ (L.obj X))))
    (hPâ‚ : âˆ€ â¦ƒYâ‚ Yâ‚‚ : Câ¦„ (f : Yâ‚ âŸ¶ Yâ‚‚) (Ï† : L.obj X âŸ¶ L.obj Yâ‚),
      P (StructuredArrow.mk Ï†) â†’ P (StructuredArrow.mk (Ï† â‰« L.map f)))
    (hPâ‚‚ : âˆ€ â¦ƒYâ‚ Yâ‚‚ : Câ¦„ (w : Yâ‚ âŸ¶ Yâ‚‚) (hw : W w) (Ï† : L.obj X âŸ¶ L.obj Yâ‚‚),
      P (StructuredArrow.mk Ï†) â†’ P (StructuredArrow.mk (Ï† â‰« (isoOfHom L W w hw).inv)))
    (g : StructuredArrow (L.obj X) L) : P g := by
  let P' : StructuredArrow (W.Q.obj X) W.Q â†’ Prop :=
    fun g â†¦ P (structuredArrowEquiv W W.Q L g)
  rw [â† (structuredArrowEquiv W W.Q L).apply_symm_apply g]
  apply induction_structuredArrow' W P'
  Â· convert hPâ‚€
    simp
  Â· intros Yâ‚ Yâ‚‚ f Ï† hÏ†
    convert hPâ‚ f (homEquiv W W.Q L Ï†) hÏ†
    simp [homEquiv_comp]
  Â· intros Yâ‚ Yâ‚‚ w hw Ï† hÏ†
    convert hPâ‚‚ w hw (homEquiv W W.Q L Ï†) hÏ†
    simp [homEquiv_comp, homEquiv_isoOfHom_inv]

end

section

variable (L : C â¥¤ D) (W : MorphismProperty C) [L.IsLocalization W] {Y : C}
  (P : CostructuredArrow L (L.obj Y) â†’ Prop)

@[elab_as_elim]
lemma induction_costructuredArrow
    (hPâ‚€ : P (CostructuredArrow.mk (ðŸ™ (L.obj Y))))
    (hPâ‚ : âˆ€ â¦ƒXâ‚ Xâ‚‚ : Câ¦„ (f : Xâ‚ âŸ¶ Xâ‚‚) (Ï† : L.obj Xâ‚‚ âŸ¶ L.obj Y),
      P (CostructuredArrow.mk Ï†) â†’ P (CostructuredArrow.mk (L.map f â‰« Ï†)))
    (hPâ‚‚ : âˆ€ â¦ƒXâ‚ Xâ‚‚ : Câ¦„ (w : Xâ‚ âŸ¶ Xâ‚‚) (hw : W w) (Ï† : L.obj Xâ‚ âŸ¶ L.obj Y),
      P (CostructuredArrow.mk Ï†) â†’ P (CostructuredArrow.mk ((isoOfHom L W w hw).inv â‰« Ï†)))
    (g : CostructuredArrow L (L.obj Y)) : P g := by
  let g' := StructuredArrow.mk (T := L.op) (Y := op g.left) g.hom.op
  change P (CostructuredArrow.mk g'.hom.unop)
  induction g' using induction_structuredArrow L.op W.op with
  | hPâ‚€ => exact hPâ‚€
  | hPâ‚ f Ï† hÏ† => exact hPâ‚ f.unop Ï†.unop hÏ†
  | hPâ‚‚ w hw Ï† hÏ† => simpa [isoOfHom_op_inv L W w hw] using hPâ‚‚ w.unop hw Ï†.unop hÏ†

end

end Localization

end CategoryTheory
