/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Localization.Equivalence

/-!
# Morphisms of localizers

A morphism of localizers consists of a functor `F : C‚ÇÅ ‚•§ C‚ÇÇ` between
two categories equipped with morphism properties `W‚ÇÅ` and `W‚ÇÇ` such
that `F` sends morphisms in `W‚ÇÅ` to morphisms in `W‚ÇÇ`.

If `Œ¶ : LocalizerMorphism W‚ÇÅ W‚ÇÇ`, and that `L‚ÇÅ : C‚ÇÅ ‚•§ D‚ÇÅ` and `L‚ÇÇ : C‚ÇÇ ‚•§ D‚ÇÇ`
are localization functors for `W‚ÇÅ` and `W‚ÇÇ`, the induced functor `D‚ÇÅ ‚•§ D‚ÇÇ`
is denoted `Œ¶.localizedFunctor L‚ÇÅ L‚ÇÇ`; we introduce the condition
`Œ¶.IsLocalizedEquivalence` which expresses that this functor is an equivalence
of categories. This condition is independent of the choice of the
localized categories.

## References
* [Bruno Kahn and Georges Maltsiniotis, *Structures de d√©rivabilit√©*][KahnMaltsiniotis2008]

-/

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ v‚ÇÑ v‚ÇÑ' v‚ÇÖ v‚ÇÖ' v‚ÇÜ u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ u‚ÇÑ' u‚ÇÖ u‚ÇÖ' u‚ÇÜ

namespace CategoryTheory

open Localization

variable {C‚ÇÅ : Type u‚ÇÅ} {C‚ÇÇ : Type u‚ÇÇ} {C‚ÇÉ : Type u‚ÇÉ}
  {D‚ÇÅ : Type u‚ÇÑ} {D‚ÇÇ : Type u‚ÇÖ} {D‚ÇÉ : Type u‚ÇÜ}
  [Category.{v‚ÇÅ} C‚ÇÅ] [Category.{v‚ÇÇ} C‚ÇÇ] [Category.{v‚ÇÉ} C‚ÇÉ]
  [Category.{v‚ÇÑ} D‚ÇÅ] [Category.{v‚ÇÖ} D‚ÇÇ] [Category.{v‚ÇÜ} D‚ÇÇ]
  (W‚ÇÅ : MorphismProperty C‚ÇÅ) (W‚ÇÇ : MorphismProperty C‚ÇÇ) (W‚ÇÉ : MorphismProperty C‚ÇÉ)

/-- If `W‚ÇÅ : MorphismProperty C‚ÇÅ` and `W‚ÇÇ : MorphismProperty C‚ÇÇ`, a `LocalizerMorphism W‚ÇÅ W‚ÇÇ`
is the datum of a functor `C‚ÇÅ ‚•§ C‚ÇÇ` which sends morphisms in `W‚ÇÅ` to morphisms in `W‚ÇÇ` -/
structure LocalizerMorphism where
  /-- a functor between the two categories -/
  functor : C‚ÇÅ ‚•§ C‚ÇÇ
  /-- the functor is compatible with the `MorphismProperty` -/
  map : W‚ÇÅ ‚â§ W‚ÇÇ.inverseImage functor

namespace LocalizerMorphism

/-- The identity functor as a morphism of localizers. -/
@[simps]
def id : LocalizerMorphism W‚ÇÅ W‚ÇÅ where
  functor := ùü≠ C‚ÇÅ
  map _ _ _ hf := hf

variable {W‚ÇÅ W‚ÇÇ W‚ÇÉ}

/-- The composition of two localizers morphisms. -/
@[simps]
def comp (Œ¶ : LocalizerMorphism W‚ÇÅ W‚ÇÇ) (Œ® : LocalizerMorphism W‚ÇÇ W‚ÇÉ) :
    LocalizerMorphism W‚ÇÅ W‚ÇÉ where
  functor := Œ¶.functor ‚ãô Œ®.functor
  map _ _ _ hf := Œ®.map _ (Œ¶.map _ hf)

variable (Œ¶ : LocalizerMorphism W‚ÇÅ W‚ÇÇ)

/-- The opposite localizer morphism `LocalizerMorphism W‚ÇÅ.op W‚ÇÇ.op` deduced
from `Œ¶ : LocalizerMorphism W‚ÇÅ W‚ÇÇ`. -/
@[simps]
def op : LocalizerMorphism W‚ÇÅ.op W‚ÇÇ.op where
  functor := Œ¶.functor.op
  map _ _ _ hf := Œ¶.map _ hf

variable (L‚ÇÅ : C‚ÇÅ ‚•§ D‚ÇÅ) [L‚ÇÅ.IsLocalization W‚ÇÅ] (L‚ÇÇ : C‚ÇÇ ‚•§ D‚ÇÇ) [L‚ÇÇ.IsLocalization W‚ÇÇ]

lemma inverts : W‚ÇÅ.IsInvertedBy (Œ¶.functor ‚ãô L‚ÇÇ) :=
  fun _ _ _ hf => Localization.inverts L‚ÇÇ W‚ÇÇ _ (Œ¶.map _ hf)

/-- When `Œ¶ : LocalizerMorphism W‚ÇÅ W‚ÇÇ` and that `L‚ÇÅ` and `L‚ÇÇ` are localization functors
for `W‚ÇÅ` and `W‚ÇÇ`, then `Œ¶.localizedFunctor L‚ÇÅ L‚ÇÇ` is the induced functor on the
localized categories. --/
noncomputable def localizedFunctor : D‚ÇÅ ‚•§ D‚ÇÇ :=
  lift (Œ¶.functor ‚ãô L‚ÇÇ) (Œ¶.inverts _) L‚ÇÅ

noncomputable instance : Lifting L‚ÇÅ W‚ÇÅ (Œ¶.functor ‚ãô L‚ÇÇ) (Œ¶.localizedFunctor L‚ÇÅ L‚ÇÇ) := by
  dsimp [localizedFunctor]
  infer_instance

/-- The 2-commutative square expressing that `Œ¶.localizedFunctor L‚ÇÅ L‚ÇÇ` lifts the
functor `Œ¶.functor`  -/
noncomputable instance catCommSq : CatCommSq Œ¶.functor L‚ÇÅ L‚ÇÇ (Œ¶.localizedFunctor L‚ÇÅ L‚ÇÇ) :=
  CatCommSq.mk (Lifting.iso _ W‚ÇÅ _ _).symm

variable (G : D‚ÇÅ ‚•§ D‚ÇÇ)

section

variable [CatCommSq Œ¶.functor L‚ÇÅ L‚ÇÇ G]
  {D‚ÇÅ' : Type u‚ÇÑ'} {D‚ÇÇ' : Type u‚ÇÖ'}
  [Category.{v‚ÇÑ'} D‚ÇÅ'] [Category.{v‚ÇÖ'} D‚ÇÇ']
  (L‚ÇÅ' : C‚ÇÅ ‚•§ D‚ÇÅ') (L‚ÇÇ' : C‚ÇÇ ‚•§ D‚ÇÇ') [L‚ÇÅ'.IsLocalization W‚ÇÅ] [L‚ÇÇ'.IsLocalization W‚ÇÇ]
  (G' : D‚ÇÅ' ‚•§ D‚ÇÇ') [CatCommSq Œ¶.functor L‚ÇÅ' L‚ÇÇ' G']

/-- If a localizer morphism induces an equivalence on some choice of localized categories,
it will be so for any choice of localized categoriees. -/
lemma isEquivalence_imp [G.IsEquivalence] : G'.IsEquivalence := by
  let E‚ÇÅ := Localization.uniq L‚ÇÅ L‚ÇÅ' W‚ÇÅ
  let E‚ÇÇ := Localization.uniq L‚ÇÇ L‚ÇÇ' W‚ÇÇ
  let e : L‚ÇÅ ‚ãô G ‚ãô E‚ÇÇ.functor ‚âÖ L‚ÇÅ ‚ãô E‚ÇÅ.functor ‚ãô G' :=
    calc
      L‚ÇÅ ‚ãô G ‚ãô E‚ÇÇ.functor ‚âÖ Œ¶.functor ‚ãô L‚ÇÇ ‚ãô E‚ÇÇ.functor :=
          (Functor.associator _ _ _).symm ‚â™‚â´
            isoWhiskerRight (CatCommSq.iso Œ¶.functor L‚ÇÅ L‚ÇÇ G).symm E‚ÇÇ.functor ‚â™‚â´
            Functor.associator _ _ _
      _ ‚âÖ Œ¶.functor ‚ãô L‚ÇÇ' := isoWhiskerLeft Œ¶.functor (compUniqFunctor L‚ÇÇ L‚ÇÇ' W‚ÇÇ)
      _ ‚âÖ L‚ÇÅ' ‚ãô G' := CatCommSq.iso Œ¶.functor L‚ÇÅ' L‚ÇÇ' G'
      _ ‚âÖ L‚ÇÅ ‚ãô E‚ÇÅ.functor ‚ãô G' :=
            isoWhiskerRight (compUniqFunctor L‚ÇÅ L‚ÇÅ' W‚ÇÅ).symm G' ‚â™‚â´ Functor.associator _ _ _
  have := Functor.isEquivalence_of_iso
    (liftNatIso L‚ÇÅ W‚ÇÅ _ _ (G ‚ãô E‚ÇÇ.functor) (E‚ÇÅ.functor ‚ãô G') e)
  exact Functor.isEquivalence_of_comp_left E‚ÇÅ.functor G'

lemma isEquivalence_iff : G.IsEquivalence ‚Üî G'.IsEquivalence :=
  ‚ü®fun _ => Œ¶.isEquivalence_imp L‚ÇÅ L‚ÇÇ G L‚ÇÅ' L‚ÇÇ' G',
    fun _ => Œ¶.isEquivalence_imp L‚ÇÅ' L‚ÇÇ' G' L‚ÇÅ L‚ÇÇ G‚ü©

end

/-- Condition that a `LocalizerMorphism` induces an equivalence on the localized categories -/
class IsLocalizedEquivalence : Prop :=
  /-- the induced functor on the constructed localized categories is an equivalence -/
  isEquivalence : (Œ¶.localizedFunctor W‚ÇÅ.Q W‚ÇÇ.Q).IsEquivalence

lemma IsLocalizedEquivalence.mk' [CatCommSq Œ¶.functor L‚ÇÅ L‚ÇÇ G] [G.IsEquivalence] :
    Œ¶.IsLocalizedEquivalence where
  isEquivalence := by
    rw [Œ¶.isEquivalence_iff W‚ÇÅ.Q W‚ÇÇ.Q (Œ¶.localizedFunctor W‚ÇÅ.Q W‚ÇÇ.Q) L‚ÇÅ L‚ÇÇ G]
    exact inferInstance

/-- If a `LocalizerMorphism` is a localized equivalence, then any compatible functor
between the localized categories is an equivalence. -/
lemma isEquivalence [h : Œ¶.IsLocalizedEquivalence] [CatCommSq Œ¶.functor L‚ÇÅ L‚ÇÇ G] :
    G.IsEquivalence := (by
  rw [Œ¶.isEquivalence_iff L‚ÇÅ L‚ÇÇ G W‚ÇÅ.Q W‚ÇÇ.Q (Œ¶.localizedFunctor W‚ÇÅ.Q W‚ÇÇ.Q)]
  exact h.isEquivalence)

/-- If a `LocalizerMorphism` is a localized equivalence, then the induced functor on
the localized categories is an equivalence -/
instance localizedFunctor_isEquivalence [Œ¶.IsLocalizedEquivalence] :
    (Œ¶.localizedFunctor L‚ÇÅ L‚ÇÇ).IsEquivalence :=
  Œ¶.isEquivalence L‚ÇÅ L‚ÇÇ _

/-- When `Œ¶ : LocalizerMorphism W‚ÇÅ W‚ÇÇ`, if the composition `Œ¶.functor ‚ãô L‚ÇÇ` is a
localization functor for `W‚ÇÅ`, then `Œ¶` is a localized equivalence. -/
lemma IsLocalizedEquivalence.of_isLocalization_of_isLocalization
    [(Œ¶.functor ‚ãô L‚ÇÇ).IsLocalization W‚ÇÅ] :
    IsLocalizedEquivalence Œ¶ := by
  have : CatCommSq Œ¶.functor (Œ¶.functor ‚ãô L‚ÇÇ) L‚ÇÇ (ùü≠ D‚ÇÇ) :=
    CatCommSq.mk (Functor.rightUnitor _).symm
  exact IsLocalizedEquivalence.mk' Œ¶ (Œ¶.functor ‚ãô L‚ÇÇ) L‚ÇÇ (ùü≠ D‚ÇÇ)

/-- When the underlying functor `Œ¶.functor` of `Œ¶ : LocalizerMorphism W‚ÇÅ W‚ÇÇ` is
an equivalence of categories and that `W‚ÇÅ` and `W‚ÇÇ` essentially correspond to each
other via this equivalence, then `Œ¶` is a localized equivalence. -/
lemma IsLocalizedEquivalence.of_equivalence [Œ¶.functor.IsEquivalence]
    (h : W‚ÇÇ ‚â§ W‚ÇÅ.map Œ¶.functor) : IsLocalizedEquivalence Œ¶ := by
  haveI : Functor.IsLocalization (Œ¶.functor ‚ãô MorphismProperty.Q W‚ÇÇ) W‚ÇÅ := by
    refine Functor.IsLocalization.of_equivalence_source W‚ÇÇ.Q W‚ÇÇ (Œ¶.functor ‚ãô W‚ÇÇ.Q) W‚ÇÅ
      (Functor.asEquivalence Œ¶.functor).symm ?_ (Œ¶.inverts W‚ÇÇ.Q)
      ((Functor.associator _ _ _).symm ‚â™‚â´ isoWhiskerRight ((Equivalence.unitIso _).symm) _ ‚â™‚â´
        Functor.leftUnitor _)
    erw [W‚ÇÅ.isoClosure.inverseImage_equivalence_functor_eq_map_inverse
      W‚ÇÅ.isoClosure_respectsIso Œ¶.functor.asEquivalence]
    rw [MorphismProperty.map_isoClosure]
    exact h
  exact IsLocalizedEquivalence.of_isLocalization_of_isLocalization Œ¶ W‚ÇÇ.Q

end LocalizerMorphism

end CategoryTheory
