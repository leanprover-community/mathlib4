/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/

import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.Functor.FullyFaithful
import Mathlib.CategoryTheory.Comma.Basic
import Mathlib.CategoryTheory.Whiskering
import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor

/-!
# Joins of category

Given categories `C, D`, this file constructs a category `C ‚ãÜ D`. Its objects are either
objects of `C` or objects of `D`, morphisms between objects of `C` are morphisms in `C`,
morphisms between object of `D` are morphisms in `D`, and finally, given `c : C` and `d : D`,
there is a unique morphism `c ‚ü∂ d` in `C ‚ãÜ D`.

## Main constructions
- `Join.edge c d`: the unique map from `c` to `d`.
- `Join.inclLeft : C ‚•§ C ‚ãÜ D`, the left inclusion. Its action on morphism is the main entry point
to constructs maps in `C ‚ãÜ D` between objects coming from `C`.
- `Join.inclRight : D ‚•§ C ‚ãÜ D`, the left inclusion. Its action on morphism is the main entry point
to constructs maps in `C ‚ãÜ D` between object coming from `D`.
- `Join.mkFunctor`, A constructor for functors out of a join of categories.
- `Join.mkNatTrans`, A constructor for natural transformations between functors out of a join
  of categories.

# TODOs
- Cofinality of the right inclusion, finality of the left inclusion.
-/

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ v‚ÇÑ v‚ÇÖ v‚ÇÜ u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ u‚ÇÖ u‚ÇÜ

namespace CategoryTheory

/-- Elements of `Join C D` are either elements of `C` or elements of `D`. -/
-- Impl. : We are not defining it as a type alias for `C ‚äï D` so that we can have
-- aesop to call cases on `Join C D`
inductive Join (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] (D : Type u‚ÇÇ) [Category.{v‚ÇÇ} D] : Type (max u‚ÇÅ u‚ÇÇ)
  | left : C ‚Üí Join C D
  | right : D ‚Üí Join C D

attribute [local aesop safe cases (rule_sets := [CategoryTheory])] Join

@[inherit_doc] infixr:30 " ‚ãÜ " => Join

namespace Join

variable (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] (D : Type u‚ÇÇ) [Category.{v‚ÇÇ} D]

section CategoryStructure

variable {C D}

/-- Morphisms in `C ‚ãÜ D` are those of `C` and `D`, plus an unique
morphism `(left c ‚ü∂ right d)` for every `c : C` and `d : D`. -/
def Hom : C ‚ãÜ D ‚Üí C ‚ãÜ D ‚Üí Type (max v‚ÇÅ v‚ÇÇ)
  | .left x, .left y => ULift (x ‚ü∂ y)
  | .right x, .right y => ULift (x ‚ü∂ y)
  | .left _, .right _ => PUnit
  | .right _, .left _ => PEmpty

/-- Identity morphisms in `C ‚ãÜ D` are inherited from those in `C` and `D`. -/
def id : ‚àÄ (X : C ‚ãÜ D), Hom X X
  | .left x => ULift.up (ùüô x)
  | .right x => ULift.up (ùüô x)

/-- Composition in `C ‚ãÜ D` is inherited from the compositions in `C` and `D`. -/
def comp : ‚àÄ {x y z : C ‚ãÜ D}, Hom x y ‚Üí Hom y z ‚Üí Hom x z
  | .left _x, .left _y, .left _z => fun f g ‚Ü¶ ULift.up (ULift.down f ‚â´ ULift.down g)
  | .left _x, .left _y, .right _z => fun _ _ ‚Ü¶ PUnit.unit
  | .left _x, .right _y, .left _z => fun _ g ‚Ü¶ PEmpty.elim g
  | .left _x, .right _y, .right _z => fun _ _ ‚Ü¶ PUnit.unit
  | .right _x, .left _y, .left _z => fun f _ ‚Ü¶ PEmpty.elim f
  | .right _x, .left _y, .right _z => fun f _ ‚Ü¶ PEmpty.elim f
  | .right _x, .right _y, .left _z => fun _ g ‚Ü¶ PEmpty.elim g
  | .right _x, .right _y, .right _z => fun f g ‚Ü¶ ULift.up (ULift.down f ‚â´ ULift.down g)

instance : Category.{max v‚ÇÅ v‚ÇÇ} (C ‚ãÜ D) where
  Hom X Y := Hom X Y
  id _ := id _
  comp := comp
  assoc {a b c d} f g h := by
    cases a <;>
    cases b <;>
    cases c <;>
    cases d <;>
    simp only [Hom, id, comp, Category.assoc] <;>
    tauto
  id_comp {x y} f := by
    cases x <;> cases y <;> simp only [Hom, id, comp, Category.id_comp] <;> tauto
  comp_id {x y} f := by
    cases x <;> cases y <;> simp only [Hom, id, comp, Category.comp_id] <;> tauto

@[aesop safe destruct (rule_sets := [CategoryTheory])]
lemma false_of_right_to_left {X : D} {Y : C} (f : right X ‚ü∂ left Y) : False := (f : PEmpty).elim

instance {X : C} {Y : D} : Unique (left X ‚ü∂ right Y) := inferInstanceAs (Unique PUnit)

/-- Join.edge c d is the unique morphism from c to d. -/
def edge (c : C) (d : D) : left c ‚ü∂ right d := default

@[simp]
lemma eq_edge {c : C} {d : D} (f : left c ‚ü∂ right d) : f = edge c d := rfl

end CategoryStructure

section Inclusions

/-- The canonical inclusion from C to `C ‚ãÜ D`. -/
@[simps! obj]
def inclLeft : C ‚•§ C ‚ãÜ D where
  obj := left
  map := ULift.up

/-- The canonical inclusion from D to `C ‚ãÜ D`. -/
@[simps! obj]
def inclRight : D ‚•§ C ‚ãÜ D where
  obj := right
  map := ULift.up

variable {C D}

/-- An induction principle for morphisms in a join of category: a morphism is either of the form
`(inclLeft _ _).map _`, `(inclRight _ _).map _)`, or is `edge _ _`. -/
@[elab_as_elim, cases_eliminator, induction_eliminator]
def homInduction {P : {x y : C ‚ãÜ D} ‚Üí (x ‚ü∂ y) ‚Üí Sort*}
    (left : ‚àÄ x y : C, (f : x ‚ü∂ y) ‚Üí P ((inclLeft C D).map f))
    (right : ‚àÄ x y : D, (f : x ‚ü∂ y) ‚Üí P ((inclRight C D).map f))
    (edge : ‚àÄ (c : C) (d : D), P (edge c d))
    {x y : C ‚ãÜ D} (f : x ‚ü∂ y) : P f :=
  match x, y, f with
  | .left x, .left y, f => left x y f.down
  | .right x, .right y, f => right x y f.down
  | .left x, .right y, _ => edge x y

@[simp]
lemma homInduction_left {P : {x y : C ‚ãÜ D} ‚Üí (x ‚ü∂ y) ‚Üí Sort*}
    (left : ‚àÄ x y : C, (f : x ‚ü∂ y) ‚Üí P ((inclLeft C D).map f))
    (right : ‚àÄ x y : D, (f : x ‚ü∂ y) ‚Üí P ((inclRight C D).map f))
    (edge : ‚àÄ (c : C) (d : D), P (edge c d))
    {x y : C} (f : x ‚ü∂ y) : homInduction left right edge ((inclLeft C D).map f) = left x y f :=
  rfl

@[simp]
lemma homInduction_right {P : {x y : C ‚ãÜ D} ‚Üí (x ‚ü∂ y) ‚Üí Sort*}
    (left : ‚àÄ x y : C, (f : x ‚ü∂ y) ‚Üí P ((inclLeft C D).map f))
    (right : ‚àÄ x y : D, (f : x ‚ü∂ y) ‚Üí P ((inclRight C D).map f))
    (edge : ‚àÄ (c : C) (d : D), P (edge c d))
    {x y : D} (f : x ‚ü∂ y) : homInduction left right edge ((inclRight C D).map f) = right x y f :=
  rfl

@[simp]
lemma homInduction_edge {P : {x y : C ‚ãÜ D} ‚Üí (x ‚ü∂ y) ‚Üí Sort*}
    (left : ‚àÄ x y : C, (f : x ‚ü∂ y) ‚Üí P ((inclLeft C D).map f))
    (right : ‚àÄ x y : D, (f : x ‚ü∂ y) ‚Üí P ((inclRight C D).map f))
    (edge : ‚àÄ (c : C) (d : D), P (edge c d))
    {c : C} {d : D} : homInduction left right edge (Join.edge c d) = edge c d :=
  rfl

variable (C D)

instance inclLeftFull: (inclLeft C D).Full where
  map_surjective f := by
    cases f
    use (by assumption)

instance inclRightFull: (inclRight C D).Full where
  map_surjective f := by
    cases f
    use (by assumption)

instance inclLeftFaithFull: (inclLeft C D).Faithful where
  map_injective {_ _} _ _ h := by injection h

instance inclRightFaithfull: (inclRight C D).Faithful where
  map_injective {_ _} _ _ h := by injection h

variable {C} in
/-- A situational lemma to help putting identities in the form `(inclLeft _ _).map _` when using
`homInduction`. -/
lemma id_left (c : C) : ùüô (left c) = (inclLeft C D).map (ùüô c) := rfl

variable {D} in
/-- A situational lemma to help putting identities in the form `(inclRight _ _).map _` when using
`homInduction`. -/
lemma id_right (d : D) : ùüô (right d) = (inclRight C D).map (ùüô d) := rfl

/-- The "canonical" natural transformation from `(Prod.fst C D) ‚ãô inclLeft C D` to
`(Prod.snd C D) ‚ãô inclRight C D`. This is bundling together all the edge morphisms
into the data of a natural transformation. -/
@[simps]
def edgeTransform :
    (Prod.fst C D) ‚ãô inclLeft C D ‚ü∂ (Prod.snd C D) ‚ãô inclRight C D where
  app := fun (c, d) ‚Ü¶ edge c d

end Inclusions

section Functoriality

variable {C D} {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E] {E' : Type u‚ÇÑ} [Category.{v‚ÇÑ} E']

/-- A pair of functor `F : C ‚•§ E, G : D ‚•§ E` as well as a natural transformation
`Œ± : (Prod.fst C D) ‚ãô F ‚ü∂ (Prod.snd C D) ‚ãô G`. defines a functor out of `C ‚ãÜ D`.
This is the main entry point to define functors out of a join of categories. -/
def mkFunctor (F : C ‚•§ E) (G : D ‚•§ E) (Œ± : (Prod.fst C D) ‚ãô F ‚ü∂ (Prod.snd C D) ‚ãô G) :
    C ‚ãÜ D ‚•§ E where
  obj X :=
    match X with
    | .left x => (F.obj x)
    | .right x => (G.obj x)
  map f :=
    homInduction
      (left := fun _ _ f ‚Ü¶ F.map f)
      (right := fun _ _ g ‚Ü¶ G.map g)
      (edge := fun c d ‚Ü¶ Œ±.app (c,d))
      f
  map_id x := by
    cases x
    ¬∑ dsimp only [id_left, homInduction_left]
      simp
    ¬∑ dsimp only [id_right, homInduction_right]
      simp
  map_comp {x y z} f g := by
    cases f <;> cases g
    ¬∑ simp [‚Üê Functor.map_comp]
    ¬∑ rename_i f d
      simpa using (Œ±.naturality <| (Prod.sectL _ d).map f).symm
    ¬∑ simp [‚Üê Functor.map_comp]
    ¬∑ rename_i c c' d f
      simpa using Œ±.naturality <| (Prod.sectR c _).map f

section

variable (F : C ‚•§ E) (G : D ‚•§ E) (Œ± : (Prod.fst C D) ‚ãô F ‚ü∂ (Prod.snd C D) ‚ãô G)

/-- Precomposing `mkFunctor F G Œ±` with the left inclusion gives back `F`. -/
def mkFunctorLeft : inclLeft C D ‚ãô (mkFunctor F G Œ±) ‚âÖ F := Iso.refl _

@[simp]
lemma mkFunctor_map_inclLeft {c c' : C} (f : c ‚ü∂ c') :
    (mkFunctor F G Œ±).map ((inclLeft C D).map f) = F.map f :=
  rfl

/-- Precomposing `mkFunctor F G Œ±` with the right inclusion gives back `G`. -/
def mkFunctorRight : inclRight C D ‚ãô (mkFunctor F G Œ±) ‚âÖ G := Iso.refl _

@[simp]
lemma mkFunctor_map_inclRight {d d' : D} (f : d ‚ü∂ d') :
    (mkFunctor F G Œ±).map ((inclRight C D).map f) = G.map f :=
  rfl

/-- Whiskering `mkFunctor F G Œ±` with the universal transformation gives back `Œ±`. -/
@[simp]
lemma mkFunctor_edgeTransform :
    whiskerRight (edgeTransform C D) (mkFunctor F G Œ±) = Œ± := by
  ext x
  simp [mkFunctor]

@[simp]
lemma mkFunctor_map_edge (c : C) (d : D) :
    (mkFunctor F G Œ±).map (edge c d) = Œ±.app (c, d) :=
  rfl

end
/-- Two functors out of a join of category are naturally isomorphic if their
compositions with the inclusions are isomorphic and the whiskering with the canonical
transformation is respected through these isomorphisms. -/
def functorIsoExt {F : C ‚ãÜ D ‚•§ E} {G : C ‚ãÜ D ‚•§ E}
    (e‚Çó : inclLeft C D ‚ãô F ‚âÖ inclLeft C D ‚ãô G)
    (e·µ£ : inclRight C D ‚ãô F ‚âÖ inclRight C D ‚ãô G)
    (h : (isoWhiskerLeft (Prod.fst C D) e‚Çó).hom ‚â´ whiskerRight (edgeTransform C D) G =
      whiskerRight (edgeTransform C D) F ‚â´ (isoWhiskerLeft (Prod.snd C D) e·µ£).hom :=
      by aesop_cat) :
    F ‚âÖ G :=
  NatIso.ofComponents
    (fun x ‚Ü¶ match x with
      | left x => e‚Çó.app x
      | right x => e·µ£.app x)
    (fun f ‚Ü¶ by
      cases f with
      | @left x y f => simpa using e‚Çó.hom.naturality f
      | @right x y f => simpa using e·µ£.hom.naturality f
      | edge c d => simpa using (congrArg (fun Œ± ‚Ü¶ Œ±.app (c,d)) h).symm)

/-- A version of `functorIsoExt` in which the hypothesis on the universal transform is supplied
extensionnaly, rather than as an equality of natural transformations. -/
def functorIsoExt' {F : C ‚ãÜ D ‚•§ E} {G : C ‚ãÜ D ‚•§ E}
    (e‚Çó : inclLeft C D ‚ãô F ‚âÖ inclLeft C D ‚ãô G)
    (e·µ£ : inclRight C D ‚ãô F ‚âÖ inclRight C D ‚ãô G)
    (h : ‚àÄ (c : C) (d : D), e‚Çó.hom.app c ‚â´ G.map (edge c d) = F.map (edge c d) ‚â´ e·µ£.hom.app d :=
      by aesop_cat) :
    F ‚âÖ G := functorIsoExt e‚Çó e·µ£

/-- A pair of functors ((C ‚•§ E), (D ‚•§ E')) induces a functor (C ‚ãÜ D ‚•§ E ‚ãÜ E'). -/
def mapPair (F‚Çó : C ‚•§ E) (F·µ£ : D ‚•§ E') : (C ‚ãÜ D) ‚•§ (E ‚ãÜ E') :=
  mkFunctor (F‚Çó ‚ãô inclLeft _ _) (F·µ£ ‚ãô inclRight _ _) { app := fun _ ‚Ü¶ edge _ _ }

/-- Any functor out of a join is naturally isomorphic to a functor of the form `mkFunctor F G Œ±`. -/
@[simps!]
def isoMkFunctor (F : C ‚ãÜ D ‚•§ E) :
    F ‚âÖ mkFunctor (inclLeft C D ‚ãô F) (inclRight C D ‚ãô F) (whiskerRight (edgeTransform C D) F) :=
  functorIsoExt (Iso.refl _) (Iso.refl _)

section

variable (F‚Çó : C ‚•§ E) (F·µ£ : D ‚•§ E')

/-- Characterizing `mapPair` on left morphisms. -/
@[simps!]
def mapPairLeft : inclLeft _ _ ‚ãô (mapPair F‚Çó F·µ£) ‚âÖ (F‚Çó ‚ãô inclLeft _ _) := mkFunctorLeft _ _ _

/-- Characterizing `mapPair` on right morphisms. -/
@[simps!]
def mapPairRight : inclRight _ _ ‚ãô (mapPair F‚Çó F·µ£) ‚âÖ (F·µ£ ‚ãô inclRight _ _) := mkFunctorRight _ _ _

/-- Characterizing the action of map_pair on edges. -/
@[simp]
def mapPairEdge (c : C) (d : D):
    (mapPair F‚Çó F·µ£).map (edge c d) = edge (F‚Çó.obj c) (F·µ£.obj d) :=
  rfl

end

/-- `mapPair` respects identities -/
@[simps!]
def mapPairId : mapPair (ùü≠ C) (ùü≠ D) ‚âÖ ùü≠ (C ‚ãÜ D) := functorIsoExt (Iso.refl _) (Iso.refl _)

variable {J : Type u‚ÇÖ} [Category.{v‚ÇÖ} J]
  {K : Type u‚ÇÜ} [Category.{v‚ÇÜ} K]

/-- `mapPair` respects composition -/
@[simps!]
def mapPairComp (F‚Çó : C ‚•§ E) (F·µ£ : D ‚•§ E') (G‚Çó : E ‚•§ J) (G·µ£ : E' ‚•§ K) :
    mapPair (F‚Çó ‚ãô G‚Çó) (F·µ£ ‚ãô G·µ£) ‚âÖ mapPair F‚Çó F·µ£ ‚ãô mapPair G‚Çó G·µ£ :=
  functorIsoExt (Iso.refl _) (Iso.refl _)

end Functoriality

section NaturalTransforms

variable {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E]
  {E' : Type u‚ÇÑ} [Category.{v‚ÇÑ} E']

variable {C D}

/-- Construct a natural transformation between functors from a join from
the data of natural transformations between each side that are compatible with the
action on edge maps. -/
@[simps!]
def mkNatTrans (F : C ‚ãÜ D ‚•§ E) (F' : C ‚ãÜ D ‚•§ E)
    (Œ±‚Çó : inclLeft C D ‚ãô F ‚ü∂ inclLeft C D ‚ãô F') (Œ±·µ£ : inclRight C D ‚ãô F ‚ü∂ inclRight C D ‚ãô F')
    (h : whiskerRight (edgeTransform C D) F ‚â´ whiskerLeft (Prod.snd C D) Œ±·µ£ =
      whiskerLeft (Prod.fst C D) Œ±‚Çó ‚â´ whiskerRight (edgeTransform C D) F' :=
      by aesop_cat) :
    F ‚ü∂ F' where
  app x := match x with
    | left x => Œ±‚Çó.app x
    | right x => Œ±·µ£.app x
  naturality {x y} f := by
    cases f with
    | @left x y f => simpa using Œ±‚Çó.naturality f
    | @right x y f => simpa using Œ±·µ£.naturality f
    | @edge c d => exact funext_iff.mp (NatTrans.ext_iff.mp h) (c, d)

/-- A natural transformation `F‚Çó ‚ü∂ G‚Çó` induces a natural transformation
  `mapPair F‚Çó H ‚ü∂ mapPair G‚Çó H` for every `H : D ‚•§ E'`. -/
@[simps!]
def mapWhiskerRight {F‚Çó : C ‚•§ E} {G‚Çó : C ‚•§ E} (Œ± : F‚Çó ‚ü∂ G‚Çó) (H : D ‚•§ E') :
    mapPair F‚Çó H ‚ü∂ mapPair G‚Çó H :=
  mkNatTrans _ _
    ((mapPairLeft F‚Çó H).inv ‚â´ (whiskerRight Œ± (inclLeft E E')) ‚â´ (mapPairLeft G‚Çó H).hom)
    (ùüô _)

/-- A natural transformation `F·µ£ ‚ü∂ G·µ£` induces a natural transformation
  `mapPair H F·µ£ ‚ü∂ mapPair H G·µ£` for every `H : C ‚•§ E`. -/
@[simps!]
def mapWhiskerLeft (H : C ‚•§ E) {F·µ£ : D ‚•§ E'} {G·µ£ : D ‚•§ E'} (Œ± : F·µ£ ‚ü∂ G·µ£) :
    mapPair H F·µ£ ‚ü∂ mapPair H G·µ£ :=
  mkNatTrans _ _
    (ùüô _)
    ((mapPairRight H F·µ£).inv ‚â´ (whiskerRight Œ± (inclRight E E')) ‚â´ (mapPairRight H G·µ£).hom)

/-- One can exchange `mapWhiskerLeft` and `mapWhiskerRight`. -/
lemma mapWhisker_exchange (F‚Çó : C ‚•§ E) (G‚Çó : C ‚•§ E) (F·µ£ : D ‚•§ E') (G·µ£ : D ‚•§ E')
    (Œ±‚Çó : F‚Çó ‚ü∂ G‚Çó) (Œ±·µ£ : F·µ£ ‚ü∂ G·µ£) :
    mapWhiskerLeft F‚Çó Œ±·µ£ ‚â´ mapWhiskerRight Œ±‚Çó G·µ£ =
      mapWhiskerRight Œ±‚Çó F·µ£ ‚â´ mapWhiskerLeft G‚Çó Œ±·µ£ := by
  aesop_cat

end NaturalTransforms

end Join

end CategoryTheory
