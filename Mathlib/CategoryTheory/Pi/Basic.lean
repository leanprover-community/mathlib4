/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Simon Hudon, Kim Morrison
-/
import Mathlib.CategoryTheory.EqToHom
import Mathlib.CategoryTheory.NatIso
import Mathlib.CategoryTheory.Products.Basic

/-!
# Categories of indexed families of objects.

We define the pointwise category structure on indexed families of objects in a category
(and also the dependent generalization).

-/

namespace CategoryTheory

universe wâ‚€ wâ‚ wâ‚‚ vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

variable {I : Type wâ‚€} {J : Type wâ‚} (C : I â†’ Type uâ‚) [âˆ€ i, Category.{vâ‚} (C i)]


/-- `pi C` gives the cartesian product of an indexed family of categories.
-/
instance pi : Category.{max wâ‚€ vâ‚} (âˆ€ i, C i) where
  Hom X Y := âˆ€ i, X i âŸ¶ Y i
  id X i := ðŸ™ (X i)
  comp f g i := f i â‰« g i

namespace Pi

@[simp]
theorem id_apply (X : âˆ€ i, C i) (i) : (ðŸ™ X : âˆ€ i, X i âŸ¶ X i) i = ðŸ™ (X i) :=
  rfl

@[simp]
theorem comp_apply {X Y Z : âˆ€ i, C i} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (i) :
    (f â‰« g : âˆ€ i, X i âŸ¶ Z i) i = f i â‰« g i :=
  rfl

@[ext]
lemma ext {X Y : âˆ€ i, C i} {f g : X âŸ¶ Y} (w : âˆ€ i, f i = g i) : f = g :=
  funext (w Â·)

/--
The evaluation functor at `i : I`, sending an `I`-indexed family of objects to the object over `i`.
-/
@[simps]
def eval (i : I) : (âˆ€ i, C i) â¥¤ C i where
  obj f := f i
  map Î± := Î± i

section

variable {J : Type wâ‚}

/- Porting note: add this because Lean cannot see directly through the `âˆ˜` for
`Function.comp` -/

instance (f : J â†’ I) : (j : J) â†’ Category ((C âˆ˜ f) j) := by
  dsimp
  infer_instance

/-- Pull back an `I`-indexed family of objects to a `J`-indexed family, along a function `J â†’ I`.
-/
@[simps]
def comap (h : J â†’ I) : (âˆ€ i, C i) â¥¤ (âˆ€ j, C (h j)) where
  obj f i := f (h i)
  map Î± i := Î± (h i)

variable (I)

/-- The natural isomorphism between
pulling back a grading along the identity function,
and the identity functor. -/
@[simps]
def comapId : comap C (id : I â†’ I) â‰… ðŸ­ (âˆ€ i, C i) where
  hom := { app := fun X => ðŸ™ X }
  inv := { app := fun X => ðŸ™ X }

example (g : J â†’ I) : (j : J) â†’ Category (C (g j)) := by infer_instance

variable {I}
variable {K : Type wâ‚‚}

/-- The natural isomorphism comparing between
pulling back along two successive functions, and
pulling back along their composition
-/
@[simps!]
def comapComp (f : K â†’ J) (g : J â†’ I) : comap C g â‹™ comap (C âˆ˜ g) f â‰… comap C (g âˆ˜ f) where
  hom :=
  { app := fun X b => ðŸ™ (X (g (f b)))
    naturality := fun X Y f' => by simp only [comap, Function.comp]; funext; simp }
  inv :=
  { app := fun X b => ðŸ™ (X (g (f b)))
    naturality := fun X Y f' => by simp only [comap, Function.comp]; funext; simp }

/-- The natural isomorphism between pulling back then evaluating, and just evaluating. -/
@[simps!]
def comapEvalIsoEval (h : J â†’ I) (j : J) : comap C h â‹™ eval (C âˆ˜ h) j â‰… eval C (h j) :=
  NatIso.ofComponents (fun _ => Iso.refl _) (by simp only [Iso.refl]; simp)

end

section

variable {J : Type wâ‚€} {D : J â†’ Type uâ‚} [âˆ€ j, Category.{vâ‚} (D j)]

/- Porting note: maybe mixing up universes -/
instance sumElimCategory : âˆ€ s : I âŠ• J, Category.{vâ‚} (Sum.elim C D s)
  | Sum.inl i => by
    dsimp
    infer_instance
  | Sum.inr j => by
    dsimp
    infer_instance

/- Porting note: replaced `Sum.rec` with `match`'s per the error about
current state of code generation -/

/-- The bifunctor combining an `I`-indexed family of objects with a `J`-indexed family of objects
to obtain an `I âŠ• J`-indexed family of objects.
-/
@[simps]
def sum : (âˆ€ i, C i) â¥¤ (âˆ€ j, D j) â¥¤ âˆ€ s : I âŠ• J, Sum.elim C D s where
  obj X :=
    { obj := fun Y s =>
        match s with
        | .inl i => X i
        | .inr j => Y j
      map := fun {_} {_} f s =>
        match s with
        | .inl i => ðŸ™ (X i)
        | .inr j => f j }
  map {X} {X'} f :=
    { app := fun Y s =>
        match s with
        | .inl i => f i
        | .inr j => ðŸ™ (Y j) }

end

variable {C}

/-- An isomorphism between `I`-indexed objects gives an isomorphism between each
pair of corresponding components. -/
@[simps]
def isoApp {X Y : âˆ€ i, C i} (f : X â‰… Y) (i : I) : X i â‰… Y i :=
  âŸ¨f.hom i, f.inv i,
    by rw [â† comp_apply, Iso.hom_inv_id, id_apply], by rw [â† comp_apply, Iso.inv_hom_id, id_apply]âŸ©

@[simp]
theorem isoApp_refl (X : âˆ€ i, C i) (i : I) : isoApp (Iso.refl X) i = Iso.refl (X i) :=
  rfl

@[simp]
theorem isoApp_symm {X Y : âˆ€ i, C i} (f : X â‰… Y) (i : I) : isoApp f.symm i = (isoApp f i).symm :=
  rfl

@[simp]
theorem isoApp_trans {X Y Z : âˆ€ i, C i} (f : X â‰… Y) (g : Y â‰… Z) (i : I) :
    isoApp (f â‰ªâ‰« g) i = isoApp f i â‰ªâ‰« isoApp g i :=
  rfl

end Pi

namespace Functor

variable {C}
variable {D : I â†’ Type uâ‚‚} [âˆ€ i, Category.{vâ‚‚} (D i)] {A : Type uâ‚ƒ} [Category.{vâ‚ƒ} A]

/-- Assemble an `I`-indexed family of functors into a functor between the pi types.
-/
@[simps]
def pi (F : âˆ€ i, C i â¥¤ D i) : (âˆ€ i, C i) â¥¤ âˆ€ i, D i where
  obj f i := (F i).obj (f i)
  map Î± i := (F i).map (Î± i)

/-- Similar to `pi`, but all functors come from the same category `A`
-/
@[simps]
def pi' (f : âˆ€ i, A â¥¤ C i) : A â¥¤ âˆ€ i, C i where
  obj a i := (f i).obj a
  map h i := (f i).map h

/-- The projections of `Functor.pi' F` are isomorphic to the functors of the family `F` -/
@[simps!]
def pi'CompEval {A : Type*} [Category A] (F : âˆ€ i, A â¥¤ C i) (i : I) :
    pi' F â‹™ Pi.eval C i â‰… F i :=
  Iso.refl _

section EqToHom

@[simp]
theorem eqToHom_proj {x x' : âˆ€ i, C i} (h : x = x') (i : I) :
    (eqToHom h : x âŸ¶ x') i = eqToHom (funext_iff.mp h i) := by
  subst h
  rfl

end EqToHom

-- One could add some natural isomorphisms showing
-- how `Functor.pi` commutes with `Pi.eval` and `Pi.comap`.
@[simp]
theorem pi'_eval (f : âˆ€ i, A â¥¤ C i) (i : I) : pi' f â‹™ Pi.eval C i = f i := by
  apply Functor.ext
  Â· intro _ _ _
    simp
  Â· intro _
    rfl

/-- Two functors to a product category are equal iff they agree on every coordinate. -/
theorem pi_ext (f f' : A â¥¤ âˆ€ i, C i) (h : âˆ€ i, f â‹™ (Pi.eval C i) = f' â‹™ (Pi.eval C i)) :
    f = f' := by
  apply Functor.ext; rotate_left
  Â· intro X
    ext i
    specialize h i
    have := congr_obj h X
    simpa
  Â· intro X Y g
    funext i
    specialize h i
    have := congr_hom h g
    simpa

end Functor

namespace NatTrans

variable {C}
variable {D : I â†’ Type uâ‚‚} [âˆ€ i, Category.{vâ‚‚} (D i)]
variable {F G : âˆ€ i, C i â¥¤ D i}

/-- Assemble an `I`-indexed family of natural transformations into a single natural transformation.
-/
@[simps!]
def pi (Î± : âˆ€ i, F i âŸ¶ G i) : Functor.pi F âŸ¶ Functor.pi G where
  app f i := (Î± i).app (f i)

/-- Assemble an `I`-indexed family of natural transformations into a single natural transformation.
-/
@[simps]
def pi' {E : Type*} [Category E] {F G : E â¥¤ âˆ€ i, C i}
    (Ï„ : âˆ€ i, F â‹™ Pi.eval C i âŸ¶ G â‹™ Pi.eval C i) : F âŸ¶ G where
  app := fun X i => (Ï„ i).app X
  naturality _ _ f := by
    ext i
    exact (Ï„ i).naturality f

end NatTrans

namespace NatIso

variable {C}
variable {D : I â†’ Type uâ‚‚} [âˆ€ i, Category.{vâ‚‚} (D i)]
variable {F G : âˆ€ i, C i â¥¤ D i}

/-- Assemble an `I`-indexed family of natural isomorphisms into a single natural isomorphism.
-/
@[simps]
def pi (e : âˆ€ i, F i â‰… G i) : Functor.pi F â‰… Functor.pi G where
  hom := NatTrans.pi (fun i => (e i).hom)
  inv := NatTrans.pi (fun i => (e i).inv)

/-- Assemble an `I`-indexed family of natural isomorphisms into a single natural isomorphism.
-/
@[simps]
def pi' {E : Type*} [Category E] {F G : E â¥¤ âˆ€ i, C i}
    (e : âˆ€ i, F â‹™ Pi.eval C i â‰… G â‹™ Pi.eval C i) : F â‰… G where
  hom := NatTrans.pi' (fun i => (e i).hom)
  inv := NatTrans.pi' (fun i => (e i).inv)

end NatIso

variable {C}

lemma isIso_pi_iff {X Y : âˆ€ i, C i} (f : X âŸ¶ Y) :
    IsIso f â†” âˆ€ i, IsIso (f i) := by
  constructor
  Â· intro _ i
    exact (Pi.isoApp (asIso f) i).isIso_hom
  Â· intro
    exact âŸ¨fun i => inv (f i), by aesop_cat, by aesop_catâŸ©

variable (C)

/-- For a family of categories `C i` indexed by `I`, an equality `i = j` in `I` induces
an equivalence `C i â‰Œ C j`. -/
def Pi.eqToEquivalence {i j : I} (h : i = j) : C i â‰Œ C j := by subst h; rfl

/-- When `i = j`, projections `Pi.eval C i` and `Pi.eval C j` are related by the equivalence
`Pi.eqToEquivalence C h : C i â‰Œ C j`. -/
@[simps!]
def Pi.evalCompEqToEquivalenceFunctor {i j : I} (h : i = j) :
    Pi.eval C i â‹™ (Pi.eqToEquivalence C h).functor â‰…
      Pi.eval C j :=
  eqToIso (by subst h; rfl)

/-- The equivalences given by `Pi.eqToEquivalence` are compatible with reindexing. -/
@[simps!]
def Pi.eqToEquivalenceFunctorIso (f : J â†’ I) {i' j' : J} (h : i' = j') :
    (Pi.eqToEquivalence C (congr_arg f h)).functor â‰…
      (Pi.eqToEquivalence (fun i' => C (f i')) h).functor :=
  eqToIso (by subst h; rfl)

attribute [local simp] eqToHom_map

/-- Reindexing a family of categories gives equivalent `Pi` categories. -/
@[simps]
noncomputable def Pi.equivalenceOfEquiv (e : J â‰ƒ I) :
    (âˆ€ j, C (e j)) â‰Œ (âˆ€ i, C i) where
  functor := Functor.pi' (fun i => Pi.eval _ (e.symm i) â‹™
    (Pi.eqToEquivalence C (by simp)).functor)
  inverse := Functor.pi' (fun i' => Pi.eval _ (e i'))
  unitIso := NatIso.pi' (fun i' => Functor.leftUnitor _ â‰ªâ‰«
    (Pi.evalCompEqToEquivalenceFunctor (fun j => C (e j)) (e.symm_apply_apply i')).symm â‰ªâ‰«
    isoWhiskerLeft _ ((Pi.eqToEquivalenceFunctorIso C e (e.symm_apply_apply i')).symm) â‰ªâ‰«
    (Functor.pi'CompEval _ _).symm â‰ªâ‰« isoWhiskerLeft _ (Functor.pi'CompEval _ _).symm â‰ªâ‰«
    (Functor.associator _ _ _).symm)
  counitIso := NatIso.pi' (fun i => (Functor.associator _ _ _).symm â‰ªâ‰«
    isoWhiskerRight (Functor.pi'CompEval _ _) _ â‰ªâ‰«
    Pi.evalCompEqToEquivalenceFunctor C (e.apply_symm_apply i) â‰ªâ‰«
    (Functor.leftUnitor _).symm)

/-- A product of categories indexed by `Option J` identifies to a binary product. -/
@[simps]
def Pi.optionEquivalence (C' : Option J â†’ Type uâ‚) [âˆ€ i, Category.{vâ‚} (C' i)] :
    (âˆ€ i, C' i) â‰Œ C' none Ã— (âˆ€ (j : J), C' (some j)) where
  functor := Functor.prod' (Pi.eval C' none)
    (Functor.pi' (fun i => (Pi.eval _ (some i))))
  inverse := Functor.pi' (fun i => match i with
    | none => Prod.fst _ _
    | some i => Prod.snd _ _ â‹™ (Pi.eval _ i))
  unitIso := NatIso.pi' (fun i => match i with
    | none => Iso.refl _
    | some _ => Iso.refl _)
  counitIso := by exact Iso.refl _

namespace Equivalence

variable {C}
variable {D : I â†’ Type uâ‚‚} [âˆ€ i, Category.{vâ‚‚} (D i)]

/-- Assemble an `I`-indexed family of equivalences of categories
into a single equivalence. -/
@[simps]
def pi (E : âˆ€ i, C i â‰Œ D i) : (âˆ€ i, C i) â‰Œ (âˆ€ i, D i) where
  functor := Functor.pi (fun i => (E i).functor)
  inverse := Functor.pi (fun i => (E i).inverse)
  unitIso := NatIso.pi (fun i => (E i).unitIso)
  counitIso := NatIso.pi (fun i => (E i).counitIso)

instance (F : âˆ€ i, C i â¥¤ D i) [âˆ€ i, (F i).IsEquivalence] :
    (Functor.pi F).IsEquivalence :=
  (pi (fun i => (F i).asEquivalence)).isEquivalence_functor

end Equivalence

end CategoryTheory
