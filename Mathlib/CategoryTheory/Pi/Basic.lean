/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Simon Hudon, Scott Morrison
-/
import Mathlib.CategoryTheory.NatIso
import Mathlib.CategoryTheory.EqToHom
import Mathlib.Data.Sum.Basic

#align_import category_theory.pi.basic from "leanprover-community/mathlib"@"dc6c365e751e34d100e80fe6e314c3c3e0fd2988"

/-!
# Categories of indexed families of objects.

We define the pointwise category structure on indexed families of objects in a category
(and also the dependent generalization).

-/

namespace CategoryTheory

universe w‚ÇÄ w‚ÇÅ w‚ÇÇ v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

variable {I : Type w‚ÇÄ} {J : Type w‚ÇÅ} (C : I ‚Üí Type u‚ÇÅ) [‚àÄ i, Category.{v‚ÇÅ} (C i)]


/-- `pi C` gives the cartesian product of an indexed family of categories.
-/
instance pi : Category.{max w‚ÇÄ v‚ÇÅ} (‚àÄ i, C i) where
  Hom X Y := ‚àÄ i, X i ‚ü∂ Y i
  id X i := ùüô (X i)
  comp f g i := f i ‚â´ g i
#align category_theory.pi CategoryTheory.pi

/-- This provides some assistance to typeclass search in a common situation,
which otherwise fails. (Without this `CategoryTheory.Pi.has_limit_of_has_limit_comp_eval` fails.)
-/
abbrev pi' {I : Type v‚ÇÅ} (C : I ‚Üí Type u‚ÇÅ) [‚àÄ i, Category.{v‚ÇÅ} (C i)] : Category.{v‚ÇÅ} (‚àÄ i, C i) :=
  CategoryTheory.pi C
#align category_theory.pi' CategoryTheory.pi'

attribute [instance] pi'

namespace Pi

@[simp]
theorem id_apply (X : ‚àÄ i, C i) (i) : (ùüô X : ‚àÄ i, X i ‚ü∂ X i) i = ùüô (X i) :=
  rfl
#align category_theory.pi.id_apply CategoryTheory.Pi.id_apply

@[simp]
theorem comp_apply {X Y Z : ‚àÄ i, C i} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (i) :
    (f ‚â´ g : ‚àÄ i, X i ‚ü∂ Z i) i = f i ‚â´ g i :=
  rfl
#align category_theory.pi.comp_apply CategoryTheory.Pi.comp_apply

-- Porting note: need to add an additional `ext` lemma.
@[ext]
lemma ext {X Y : ‚àÄ i, C i} {f g : X ‚ü∂ Y} (w : ‚àÄ i, f i = g i) : f = g :=
  funext (w ¬∑)

/--
The evaluation functor at `i : I`, sending an `I`-indexed family of objects to the object over `i`.
-/
@[simps]
def eval (i : I) : (‚àÄ i, C i) ‚•§ C i where
  obj f := f i
  map Œ± := Œ± i
#align category_theory.pi.eval CategoryTheory.Pi.eval

section

variable {J : Type w‚ÇÅ}

/- Porting note: add this because Lean cannot see directly through the `‚àò` for
`Function.comp` -/

instance (f : J ‚Üí I) : (j : J) ‚Üí Category ((C ‚àò f) j) := by
  dsimp
  infer_instance

/-- Pull back an `I`-indexed family of objects to a `J`-indexed family, along a function `J ‚Üí I`.
-/
@[simps]
def comap (h : J ‚Üí I) : (‚àÄ i, C i) ‚•§ (‚àÄ j, C (h j)) where
  obj f i := f (h i)
  map Œ± i := Œ± (h i)
#align category_theory.pi.comap CategoryTheory.Pi.comap

variable (I)

/-- The natural isomorphism between
pulling back a grading along the identity function,
and the identity functor. -/
@[simps]
def comapId : comap C (id : I ‚Üí I) ‚âÖ ùü≠ (‚àÄ i, C i) where
  hom := { app := fun X => ùüô X }
  inv := { app := fun X => ùüô X }
#align category_theory.pi.comap_id CategoryTheory.Pi.comapId

example (g : J ‚Üí I) : (j : J) ‚Üí Category (C (g j)) := by infer_instance

variable {I}

variable {K : Type w‚ÇÇ}

/-- The natural isomorphism comparing between
pulling back along two successive functions, and
pulling back along their composition
-/
@[simps!]
def comapComp (f : K ‚Üí J) (g : J ‚Üí I) : comap C g ‚ãô comap (C ‚àò g) f ‚âÖ comap C (g ‚àò f) where
  hom :=
  { app := fun X b => ùüô (X (g (f b)))
    naturality := fun X Y f' => by simp only [comap, Function.comp]; funext; simp }
  inv :=
  { app := fun X b => ùüô (X (g (f b)))
    naturality := fun X Y f' => by simp only [comap, Function.comp]; funext; simp }
#align category_theory.pi.comap_comp CategoryTheory.Pi.comapComp

/-- The natural isomorphism between pulling back then evaluating, and just evaluating. -/
@[simps!]
def comapEvalIsoEval (h : J ‚Üí I) (j : J) : comap C h ‚ãô eval (C ‚àò h) j ‚âÖ eval C (h j) :=
  NatIso.ofComponents (fun f => Iso.refl _) (by simp only [Iso.refl]; aesop_cat)
#align category_theory.pi.comap_eval_iso_eval CategoryTheory.Pi.comapEvalIsoEval

end

section

variable {J : Type w‚ÇÄ} {D : J ‚Üí Type u‚ÇÅ} [‚àÄ j, Category.{v‚ÇÅ} (D j)]

/- Porting note: maybe mixing up universes -/
instance sumElimCategory : ‚àÄ s : Sum I J, Category.{v‚ÇÅ} (Sum.elim C D s)
  | Sum.inl i => by
    dsimp
    infer_instance
  | Sum.inr j => by
    dsimp
    infer_instance
#align category_theory.pi.sum_elim_category CategoryTheory.Pi.sumElimCategory‚Çì

/- Porting note: replaced `Sum.rec` with `match`'s per the error about
current state of code generation -/

/-- The bifunctor combining an `I`-indexed family of objects with a `J`-indexed family of objects
to obtain an `I ‚äï J`-indexed family of objects.
-/
@[simps]
def sum : (‚àÄ i, C i) ‚•§ (‚àÄ j, D j) ‚•§ ‚àÄ s : Sum I J, Sum.elim C D s where
  obj X :=
    { obj := fun Y s =>
        match s with
        | .inl i => X i
        | .inr j => Y j
      map := fun {Y} {Y'} f s =>
        match s with
        | .inl i => ùüô (X i)
        | .inr j => f j }
  map {X} {X'} f :=
    { app := fun Y s =>
        match s with
        | .inl i => f i
        | .inr j => ùüô (Y j) }
#align category_theory.pi.sum CategoryTheory.Pi.sum

end

variable {C}

/-- An isomorphism between `I`-indexed objects gives an isomorphism between each
pair of corresponding components. -/
@[simps]
def isoApp {X Y : ‚àÄ i, C i} (f : X ‚âÖ Y) (i : I) : X i ‚âÖ Y i :=
  ‚ü®f.hom i, f.inv i,
    by rw [‚Üê comp_apply, Iso.hom_inv_id, id_apply], by rw [‚Üê comp_apply, Iso.inv_hom_id, id_apply]‚ü©
#align category_theory.pi.iso_app CategoryTheory.Pi.isoApp

@[simp]
theorem isoApp_refl (X : ‚àÄ i, C i) (i : I) : isoApp (Iso.refl X) i = Iso.refl (X i) :=
  rfl
#align category_theory.pi.iso_app_refl CategoryTheory.Pi.isoApp_refl

@[simp]
theorem isoApp_symm {X Y : ‚àÄ i, C i} (f : X ‚âÖ Y) (i : I) : isoApp f.symm i = (isoApp f i).symm :=
  rfl
#align category_theory.pi.iso_app_symm CategoryTheory.Pi.isoApp_symm

@[simp]
theorem isoApp_trans {X Y Z : ‚àÄ i, C i} (f : X ‚âÖ Y) (g : Y ‚âÖ Z) (i : I) :
    isoApp (f ‚â™‚â´ g) i = isoApp f i ‚â™‚â´ isoApp g i :=
  rfl
#align category_theory.pi.iso_app_trans CategoryTheory.Pi.isoApp_trans

end Pi

namespace Functor

variable {C}

variable {D : I ‚Üí Type u‚ÇÅ} [‚àÄ i, Category.{v‚ÇÅ} (D i)] {A : Type u‚ÇÅ} [Category.{u‚ÇÅ} A]

/-- Assemble an `I`-indexed family of functors into a functor between the pi types.
-/
@[simps]
def pi (F : ‚àÄ i, C i ‚•§ D i) : (‚àÄ i, C i) ‚•§ ‚àÄ i, D i where
  obj f i := (F i).obj (f i)
  map Œ± i := (F i).map (Œ± i)
#align category_theory.functor.pi CategoryTheory.Functor.pi

/-- Similar to `pi`, but all functors come from the same category `A`
-/
@[simps]
def pi' (f : ‚àÄ i, A ‚•§ C i) : A ‚•§ ‚àÄ i, C i where
  obj a i := (f i).obj a
  map h i := (f i).map h
#align category_theory.functor.pi' CategoryTheory.Functor.pi'

section EqToHom

@[simp]
theorem eqToHom_proj {x x' : ‚àÄ i, C i} (h : x = x') (i : I) :
    (eqToHom h : x ‚ü∂ x') i = eqToHom (Function.funext_iff.mp h i) := by
  subst h
  rfl
#align category_theory.functor.eq_to_hom_proj CategoryTheory.Functor.eqToHom_proj

end EqToHom

-- One could add some natural isomorphisms showing
-- how `Functor.pi` commutes with `Pi.eval` and `Pi.comap`.
@[simp]
theorem pi'_eval (f : ‚àÄ i, A ‚•§ C i) (i : I) : pi' f ‚ãô Pi.eval C i = f i := by
  apply Functor.ext
  intro _ _ _
  ¬∑ simp
  ¬∑ intro _
    rfl
#align category_theory.functor.pi'_eval CategoryTheory.Functor.pi'_eval

/-- Two functors to a product category are equal iff they agree on every coordinate. -/
theorem pi_ext (f f' : A ‚•§ ‚àÄ i, C i) (h : ‚àÄ i, f ‚ãô (Pi.eval C i) = f' ‚ãô (Pi.eval C i)) :
    f = f' := by
  apply Functor.ext; rotate_left
  ¬∑ intro X
    ext i
    specialize h i
    have := congr_obj h X
    simpa
  ¬∑ intro X Y g
    dsimp
    funext i
    specialize h i
    have := congr_hom h g
    simpa
#align category_theory.functor.pi_ext CategoryTheory.Functor.pi_ext

end Functor

namespace NatTrans

variable {C}

variable {D : I ‚Üí Type u‚ÇÅ} [‚àÄ i, Category.{v‚ÇÅ} (D i)]

variable {F G : ‚àÄ i, C i ‚•§ D i}

/-- Assemble an `I`-indexed family of natural transformations into a single natural transformation.
-/
@[simps!]
def pi (Œ± : ‚àÄ i, F i ‚ü∂ G i) : Functor.pi F ‚ü∂ Functor.pi G where
  app f i := (Œ± i).app (f i)
#align category_theory.nat_trans.pi CategoryTheory.NatTrans.pi

end NatTrans

end CategoryTheory
