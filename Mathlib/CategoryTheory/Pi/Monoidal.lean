/-
Copyright (c) 2025 Sina Hazratpour. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sina Hazratpour
-/
module

public import Mathlib.CategoryTheory.Pi.Basic
public import Mathlib.CategoryTheory.Monoidal.Category
public import Mathlib.CategoryTheory.Monoidal.Braided.Basic
public import Mathlib.CategoryTheory.Monoidal.Closed.Basic

/-!
# The pointwise monoidal structure on the product of families of monoidal categories

Given a family of monoidal categories `C i`, we define a monoidal structure on
`Î  i, C i` where the tensor product is defined pointwise.

-/

@[expose] public section

universe wâ‚ vâ‚ vâ‚‚ uâ‚ uâ‚‚

namespace CategoryTheory

namespace Pi

open Category MonoidalCategory

variable {I : Type wâ‚} {C : I â†’ Type uâ‚} [âˆ€ i, Category.{vâ‚} (C i)]
  [âˆ€ i, MonoidalCategory (C i)]

@[simps tensorObj tensorHom whiskerLeft whiskerRight tensorUnit]
instance monoidalCategoryStruct : MonoidalCategoryStruct (âˆ€ i, C i) where
  tensorObj X Y i := X i âŠ— Y i
  tensorHom f g i := f i âŠ—â‚˜ g i
  whiskerLeft X _ _ f i := X i â— f i
  whiskerRight f Y i := f i â–· Y i
  tensorUnit i := ğŸ™_ (C i)
  leftUnitor X := isoMk (fun i â†¦ Î»_ (X i))
  rightUnitor X := isoMk (fun i â†¦ Ï_ (X i))
  associator X Y Z := isoMk (fun i â†¦ Î±_ (X i) (Y i) (Z i))

@[simp]
theorem associator_hom_apply {X Y Z : âˆ€ i, C i} {i : I} :
    (Î±_ X Y Z).hom i = (Î±_ (X i) (Y i) (Z i)).hom := rfl

@[simp]
theorem associator_inv_apply {X Y Z : âˆ€ i, C i} {i : I} :
    (Î±_ X Y Z).inv i = (Î±_ (X i) (Y i) (Z i)).inv := rfl

@[simp]
theorem isoApp_associator {X Y Z : âˆ€ i, C i} {i : I} :
    isoApp (Î±_ X Y Z) i = Î±_ (X i) (Y i) (Z i) := rfl

@[simp]
theorem left_unitor_hom_apply {X : âˆ€ i, C i} {i : I} :
    (Î»_ X).hom i = (Î»_ (X i)).hom := rfl

@[simp]
theorem left_unitor_inv_apply {X : âˆ€ i, C i} {i : I} :
    (Î»_ X).inv i = (Î»_ (X i)).inv := rfl

@[simp]
theorem isoApp_left_unitor {X : âˆ€ i, C i} {i : I} :
    isoApp (Î»_ X) i = Î»_ (X i) := rfl

@[simp]
theorem right_unitor_hom_apply {X : âˆ€ i, C i} {i : I} :
    (Ï_ X).hom i = (Ï_ (X i)).hom := rfl

@[simp]
theorem right_unitor_inv_apply {X : âˆ€ i, C i} {i : I} :
    (Ï_ X).inv i = (Ï_ (X i)).inv := rfl

@[simp]
theorem isoApp_right_unitor {X : âˆ€ i, C i} {i : I} :
    isoApp (Ï_ X) i = Ï_ (X i) := rfl

/-- `Pi.monoidalCategory C` equips the product of an indexed family of categories with
the pointwise monoidal structure. -/
instance monoidalCategory : MonoidalCategory.{max wâ‚ vâ‚} (âˆ€ i, C i) where
  tensorHom_def {A B X Y} f g := by ext i; simp [tensorHom_def, whiskerLeft]

section BraidedCategory

open CategoryTheory.BraidedCategory

variable [âˆ€ i, BraidedCategory (C i)]

/-- When each `C i` is a braided monoidal category,
the natural pointwise monoidal structure on `âˆ€ i, C i`
is also braided.
-/
instance braidedCategory : BraidedCategory (âˆ€ i, C i) where
  braiding X Y := isoMk fun i => Î²_ (X i) (Y i)
  hexagon_forward X Y Z := by ext i; apply hexagon_forward
  hexagon_reverse X Y Z := by ext i; apply hexagon_reverse

@[simp]
theorem braiding_hom_apply {X Y : âˆ€ i, C i} {i : I} :
    (Î²_ X Y).hom i = (Î²_ (X i) (Y i)).hom := rfl

@[simp]
theorem braiding_inv_apply {X Y : âˆ€ i, C i} {i : I} :
    (Î²_ X Y).inv i = (Î²_ (X i) (Y i)).inv := rfl

@[simp]
theorem isoApp_braiding {X Y : âˆ€ i, C i} {i : I} :
    isoApp (Î²_ X Y) i = Î²_ (X i) (Y i) := rfl

end BraidedCategory

section SymmetricCategory

open CategoryTheory.SymmetricCategory

variable [âˆ€ i, SymmetricCategory (C i)]

/-- When each `C i` is a symmetric monoidal category,
the natural pointwise monoidal structure on `âˆ€ i, C i`
is also symmetric.
-/
instance symmetricCategory : SymmetricCategory (âˆ€ i, C i) where
  symmetry X Y := by ext i; apply symmetry

end SymmetricCategory

section Closed

open ihom

variable {I : Type wâ‚} {C : I â†’ Type uâ‚} [âˆ€ i, Category.{vâ‚} (C i)]
  [âˆ€ i, MonoidalCategory (C i)] [âˆ€ i, MonoidalClosed (C i)]

/-- The internal hom functor `X âŸ¶[âˆ€ i, C i] -` -/
@[simps!]
def ihom (X : âˆ€ i, C i) : (âˆ€ i, C i) â¥¤ (âˆ€ i, C i) where
  obj Y := fun i â†¦ (X i âŸ¶[C i] Y i)
  map {Y Z} f := fun i â†¦ (CategoryTheory.ihom (X i)).map (f i)

/-- The unit for the adjunction `tensorLeft X âŠ£ ihom X`. -/
@[simps]
def closedUnit (X : âˆ€ i, C i) : ğŸ­ (âˆ€ i, C i) âŸ¶ tensorLeft X â‹™ ihom X where
  app Y := fun i â†¦ (ihom.coev (X i)).app (Y i)

/-- The counit for the adjunction `tensorLeft X âŠ£ ihom X`. -/
@[simps]
def closedCounit (X : âˆ€ i, C i) : ihom X â‹™ tensorLeft X âŸ¶ ğŸ­ (âˆ€ i, C i) where
  app Y := fun i â†¦ (ihom.ev (X i)).app (Y i)

/-- Equips the product of a family of closed monoidal categories with
a pointwise closed monoidal structure. -/
@[simps]
instance monoidalClosed : MonoidalClosed (âˆ€ i, C i) where
  closed X := {
    rightAdj := ihom X
    adj.unit := closedUnit X
    adj.counit := closedCounit X }

end Closed

@[simps!]
instance (i : I) : (Pi.eval C i).Monoidal where
  Îµ := ğŸ™ _
  Î¼ X Y := ğŸ™ _
  Î· := ğŸ™ _
  Î´ X Y := ğŸ™ _

instance [âˆ€ i, BraidedCategory (C i)] (i : I) : (Pi.eval C i).Braided where

@[simps]
instance laxMonoidalPi' {D : Type*} [Category* D] [MonoidalCategory D] (F : âˆ€ i : I, D â¥¤ C i)
    [âˆ€ i, (F i).LaxMonoidal] :
    (Functor.pi' F).LaxMonoidal where
  Îµ := fun i â†¦ Functor.LaxMonoidal.Îµ (F i)
  Î¼ X Y := fun i â†¦ Functor.LaxMonoidal.Î¼ (F i) X Y

@[simps]
instance opLaxMonoidalPi' {D : Type*} [Category* D] [MonoidalCategory D]
    (F : âˆ€ i : I, D â¥¤ C i)
    [âˆ€ i, (F i).OplaxMonoidal] :
    (Functor.pi' F).OplaxMonoidal where
  Î· := fun i â†¦ Functor.OplaxMonoidal.Î· (F i)
  Î´ X Y := fun i â†¦ Functor.OplaxMonoidal.Î´ (F i) X Y
  oplax_left_unitality X := by ext; simp
  oplax_right_unitality X := by ext; simp

@[simps!]
instance monoidalPi' {D : Type*} [Category* D] [MonoidalCategory D]
    (F : âˆ€ i : I, D â¥¤ C i) [âˆ€ i, (F i).Monoidal] :
    (Functor.pi' F).Monoidal where

instance [âˆ€ i, BraidedCategory (C i)]
    {D : Type*} [Category* D] [MonoidalCategory D] [BraidedCategory D]
    (F : âˆ€ i : I, D â¥¤ C i) [âˆ€ i, (F i).LaxBraided] :
    (Functor.pi' F).LaxBraided where
  braided := by intros; ext i; exact Functor.LaxBraided.braided _ _

instance [âˆ€ i, BraidedCategory (C i)]
    {D : Type*} [Category* D] [MonoidalCategory D] [BraidedCategory D]
    (F : âˆ€ i : I, D â¥¤ C i) [âˆ€ i, (F i).Braided] :
    (Functor.pi' F).Braided where

@[simps]
instance laxMonoidalPi {D : I â†’ Type uâ‚‚} [âˆ€ i, Category.{vâ‚‚} (D i)]
    [âˆ€ i, MonoidalCategory (D i)] (F : âˆ€ i : I, D i â¥¤ C i)
    [âˆ€ i, (F i).LaxMonoidal] :
    (Functor.pi F).LaxMonoidal where
  Îµ := fun i â†¦ Functor.LaxMonoidal.Îµ (F i)
  Î¼ X Y := fun i â†¦ Functor.LaxMonoidal.Î¼ (F i) (X i) (Y i)

@[simps]
instance opLaxMonoidalPi {D : I â†’ Type uâ‚‚} [âˆ€ i, Category.{vâ‚‚} (D i)]
    [âˆ€ i, MonoidalCategory (D i)] (F : âˆ€ i : I, D i â¥¤ C i)
    [âˆ€ i, (F i).OplaxMonoidal] :
    (Functor.pi F).OplaxMonoidal where
  Î· := fun i â†¦ Functor.OplaxMonoidal.Î· (F i)
  Î´ X Y := fun i â†¦ Functor.OplaxMonoidal.Î´ (F i) (X i) (Y i)
  oplax_left_unitality X := by ext; simp
  oplax_right_unitality X := by ext; simp

@[simps!]
instance monoidalPi {D : I â†’ Type uâ‚‚} [âˆ€ i, Category.{vâ‚‚} (D i)]
    [âˆ€ i, MonoidalCategory (D i)] (F : âˆ€ i : I, D i â¥¤ C i)
    [âˆ€ i, (F i).Monoidal] :
    (Functor.pi F).Monoidal where

instance [âˆ€ i, BraidedCategory (C i)]
    {D : I â†’ Type uâ‚‚} [âˆ€ i, Category.{vâ‚‚} (D i)]
    [âˆ€ i, MonoidalCategory (D i)] [âˆ€ i, BraidedCategory (D i)]
    (F : âˆ€ i : I, D i â¥¤ C i) [âˆ€ i, (F i).LaxBraided] :
    (Functor.pi F).LaxBraided where
  braided := by intros; ext i; exact Functor.LaxBraided.braided _ _

instance [âˆ€ i, BraidedCategory (C i)]
    {D : I â†’ Type uâ‚‚} [âˆ€ i, Category.{vâ‚‚} (D i)]
    [âˆ€ i, MonoidalCategory (D i)] [âˆ€ i, BraidedCategory (D i)]
    (F : âˆ€ i : I, D i â¥¤ C i) [âˆ€ i, (F i).Braided] :
    (Functor.pi F).Braided where

instance {D : Type*} [Category* D] [MonoidalCategory D]
    {F G : D â¥¤ (âˆ€ i, C i)} [F.LaxMonoidal] [G.LaxMonoidal]
    (Ï„ : âˆ€ i, F â‹™ Pi.eval C i âŸ¶ G â‹™ Pi.eval C i)
    [âˆ€ i, (Ï„ i).IsMonoidal] :
    (NatTrans.pi' Ï„).IsMonoidal where
  unit := by ext i; simpa using NatTrans.IsMonoidal.unit (Ï„ := Ï„ i)
  tensor X Y := by ext i; simpa using NatTrans.IsMonoidal.tensor _ _ (Ï„ := Ï„ i)

instance {D : I â†’ Type uâ‚‚} [âˆ€ i, Category.{vâ‚‚} (D i)]
    [âˆ€ i, MonoidalCategory (D i)]
    {F G : âˆ€ i : I, (D i â¥¤ C i)} [âˆ€ i, (F i).LaxMonoidal]
    [âˆ€ i, (G i).LaxMonoidal] (Ï„ : âˆ€ i : I, (F i) âŸ¶ (G i))
    [âˆ€ i, (Ï„ i).IsMonoidal] :
    (NatTrans.pi Ï„).IsMonoidal where

end Pi

end CategoryTheory
