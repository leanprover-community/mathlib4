/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Shift.Adjunction
import Mathlib.CategoryTheory.Preadditive.AdditiveFunctor

/-!
# The pullback of a shift by a monoid morphism

Given a shift by a monoid `B` on a category `C` and a monoid morphism  `Ï† : A â†’+ B`,
we define a shift by `A` on a category `PullbackShift C Ï†` which is a type synonym for `C`.

If `F : C â¥¤ D` is a functor between categories equipped with shifts by `B`, we define
a type synonym `PullbackShift.functor F Ï†` for `F`. When `F` has a `CommShift` structure
by `B`, we define a pulled back `CommShift` structure by `A` on `PullbackShift.functor F Ï†`.

Similarly, if `Ï„` is a natural transformation between functors `F,G : C â¥¤ D`, we define
a type synonym
`PullbackShift.natTrans Ï„ Ï† : PullbackShift.functor F Ï† âŸ¶ PullbackShift.functor G Ï†`.
When `Ï„` has a `CommShift` structure by `B` (i.e. is compatible with `CommShift` structures
on `F` and `G`), we define a pulled back `CommShift` structure by `A` on
`PullbackShift.natTrans Ï„ Ï†`.

Finally, if we have an adjunction `F âŠ£ G` (with `G : D â¥¤ C`), we define a type synonym
`PullbackShift.adjunction adj Ï† : PullbackShift.functor F Ï† âŠ£ PullbackShift.functor G Ï†`
and we show that, if `adj` compatible with `CommShift` structures
on `F` and `G`, then `PullbackShift.adjunction adj Ï†` iis also compatible with the pulled back
`CommShift` structures.
-/

namespace CategoryTheory

open Limits Category

variable (C : Type*) [Category C] {A B : Type*} [AddMonoid A] [AddMonoid B]
  (Ï† : A â†’+ B) [HasShift C B]

/-- The category `PullbackShift C Ï†` is equipped with a shift such that for all `a`,
the shift functor by `a` is `shiftFunctor C (Ï† a)`. -/
@[nolint unusedArguments]
def PullbackShift (_ : A â†’+ B) [HasShift C B] := C

instance : Category (PullbackShift C Ï†) := by
  dsimp only [PullbackShift]
  infer_instance

attribute [local instance] endofunctorMonoidalCategory

/-- The shift on `PullbackShift C Ï†` is obtained by precomposing the shift on `C` with
the monoidal functor `Discrete.addMonoidalFunctor Ï† : Discrete A â¥¤ Discrete B`. -/
noncomputable instance : HasShift (PullbackShift C Ï†) A where
  shift := Discrete.addMonoidalFunctor Ï† â‹™ shiftMonoidalFunctor C B

instance [HasZeroObject C] : HasZeroObject (PullbackShift C Ï†) := by
  dsimp [PullbackShift]
  infer_instance

instance [Preadditive C] : Preadditive (PullbackShift C Ï†) := by
  dsimp [PullbackShift]
  infer_instance

instance [Preadditive C] (a : A) [(shiftFunctor C (Ï† a)).Additive] :
    (shiftFunctor (PullbackShift C Ï†) a).Additive := by
  change (shiftFunctor C (Ï† a)).Additive
  infer_instance

/-- When `b = Ï† a`, this is the canonical
isomorphism `shiftFunctor (PullbackShift C Ï†) a â‰… shiftFunctor C b`. -/
noncomputable def pullbackShiftIso (a : A) (b : B) (h : b = Ï† a) :
    shiftFunctor (PullbackShift C Ï†) a â‰… shiftFunctor C b := eqToIso (by subst h; rfl)

variable {C}
variable (X : PullbackShift C Ï†) (aâ‚ aâ‚‚ aâ‚ƒ : A) (h : aâ‚ + aâ‚‚ = aâ‚ƒ) (bâ‚ bâ‚‚ bâ‚ƒ : B)
  (hâ‚ : bâ‚ = Ï† aâ‚) (hâ‚‚ : bâ‚‚ = Ï† aâ‚‚) (hâ‚ƒ : bâ‚ƒ = Ï† aâ‚ƒ)

lemma pullbackShiftFunctorZero_inv_app :
    (shiftFunctorZero _ A).inv.app X =
      (shiftFunctorZero C B).inv.app X â‰« (pullbackShiftIso C Ï† 0 0 (by simp)).inv.app X := by
  change (shiftFunctorZero C B).inv.app X â‰« _ = _
  dsimp [Discrete.eqToHom, Discrete.addMonoidalFunctor_Îµ]
  congr 2
  apply eqToHom_map

lemma pullbackShiftFunctorZero_hom_app :
    (shiftFunctorZero _ A).hom.app X =
      (pullbackShiftIso C Ï† 0 0 (by simp)).hom.app X â‰« (shiftFunctorZero C B).hom.app X := by
  rw [â† cancel_epi ((shiftFunctorZero _ A).inv.app X), Iso.inv_hom_id_app,
    pullbackShiftFunctorZero_inv_app, assoc, Iso.inv_hom_id_app_assoc, Iso.inv_hom_id_app]
  rfl

lemma pullbackShiftFunctorZero'_inv_app :
    (shiftFunctorZero _ A).inv.app X = (shiftFunctorZero' C (Ï† 0) (by rw [map_zero])).inv.app X â‰«
      (pullbackShiftIso C Ï† 0 (Ï† 0) rfl).inv.app X := by
  rw [pullbackShiftFunctorZero_inv_app]
  simp only [Functor.id_obj, pullbackShiftIso, eqToIso.inv, eqToHom_app, shiftFunctorZero',
    Iso.trans_inv, NatTrans.comp_app, eqToIso_refl, Iso.refl_inv, NatTrans.id_app, assoc]
  erw [comp_id]

lemma pullbackShiftFunctorZero'_hom_app :
    (shiftFunctorZero _ A).hom.app X = (pullbackShiftIso C Ï† 0 (Ï† 0) rfl).hom.app X â‰«
      (shiftFunctorZero' C (Ï† 0) (by rw [map_zero])).hom.app X := by
  rw [â† cancel_epi ((shiftFunctorZero _ A).inv.app X), Iso.inv_hom_id_app,
    pullbackShiftFunctorZero'_inv_app, assoc, Iso.inv_hom_id_app_assoc, Iso.inv_hom_id_app]
  rfl

lemma pullbackShiftFunctorAdd'_inv_app :
    (shiftFunctorAdd' _ aâ‚ aâ‚‚ aâ‚ƒ h).inv.app X =
      (shiftFunctor (PullbackShift C Ï†) aâ‚‚).map ((pullbackShiftIso C Ï† aâ‚ bâ‚ hâ‚).hom.app X) â‰«
        (pullbackShiftIso C Ï† aâ‚‚ bâ‚‚ hâ‚‚).hom.app _ â‰«
        (shiftFunctorAdd' C bâ‚ bâ‚‚ bâ‚ƒ (by rw [hâ‚, hâ‚‚, hâ‚ƒ, â† h, Ï†.map_add])).inv.app X â‰«
        (pullbackShiftIso C Ï† aâ‚ƒ bâ‚ƒ hâ‚ƒ).inv.app X := by
  subst hâ‚ hâ‚‚ h
  obtain rfl : bâ‚ƒ = Ï† aâ‚ + Ï† aâ‚‚ := by rw [hâ‚ƒ, Ï†.map_add]
  simp only [Functor.comp_obj, NatTrans.naturality_assoc]
  erw [Functor.map_id, id_comp, id_comp, shiftFunctorAdd'_eq_shiftFunctorAdd,
    shiftFunctorAdd'_eq_shiftFunctorAdd]
  change _ â‰« _ = _
  congr 1
  rw [Discrete.addMonoidalFunctor_Î¼]
  dsimp [Discrete.eqToHom]
  congr 2
  apply eqToHom_map

lemma pullbackShiftFunctorAdd'_hom_app :
    (shiftFunctorAdd' _ aâ‚ aâ‚‚ aâ‚ƒ h).hom.app X =
      (pullbackShiftIso C Ï† aâ‚ƒ bâ‚ƒ hâ‚ƒ).hom.app X â‰«
      (shiftFunctorAdd' C bâ‚ bâ‚‚ bâ‚ƒ (by rw [hâ‚, hâ‚‚, hâ‚ƒ, â† h, Ï†.map_add])).hom.app X â‰«
      (pullbackShiftIso C Ï† aâ‚‚ bâ‚‚ hâ‚‚).inv.app _ â‰«
      (shiftFunctor (PullbackShift C Ï†) aâ‚‚).map ((pullbackShiftIso C Ï† aâ‚ bâ‚ hâ‚).inv.app X) := by
  rw [â† cancel_epi ((shiftFunctorAdd' _ aâ‚ aâ‚‚ aâ‚ƒ h).inv.app X), Iso.inv_hom_id_app,
    pullbackShiftFunctorAdd'_inv_app Ï† X aâ‚ aâ‚‚ aâ‚ƒ h bâ‚ bâ‚‚ bâ‚ƒ hâ‚ hâ‚‚ hâ‚ƒ, assoc, assoc, assoc,
    Iso.inv_hom_id_app_assoc, Iso.inv_hom_id_app_assoc, Iso.hom_inv_id_app_assoc,
    â† Functor.map_comp, Iso.hom_inv_id_app, Functor.map_id]
  rfl

variable {D : Type*} [Category D] [HasShift D B] (F : C â¥¤ D) [F.CommShift B]

/--
The functor `F`, seen as a functor from `PullbackShift C Ï†` to `PullbackShift D Ï†`.
Then a `CommShift B` instance on `F` will define a `CommShift A` instance on
`PullbackShift.functor F Ï†`, and we won't have to juggle with two `CommShift` instances
on `F`.
-/
def PullbackShift.functor : PullbackShift C Ï† â¥¤ PullbackShift D Ï† := F

variable {F} in
/--
The natural transformation `Ï„`, seen as a natural transformation from `PullbackShift.functor F Ï†`
to `PullbackShift.functor G Ï†`. Then a `CommShift B` instance on `Ï„` will define a `CommShift A`
instance on `PullbackShift.natTrans Ï„ Ï†`, and we won't have to juggle with two `CommShift`
instances on `Ï„`.
-/
def PullbackShift.natTrans {G : C â¥¤ D} (Ï„ : F âŸ¶ G) :
    PullbackShift.functor Ï† F âŸ¶ PullbackShift.functor Ï† G := Ï„

namespace Functor

/-- If `F : C â¥¤ D` commutes with the shifts on `C` and `D`, then `PullbackShift.functor F Ï†`
commutes with their pullbacks by an additive map `Ï†`.
-/
noncomputable instance commShiftPullback : (PullbackShift.functor Ï† F).CommShift A where
  iso a := isoWhiskerRight (pullbackShiftIso C Ï† a (Ï† a) rfl) F â‰ªâ‰«
    F.commShiftIso (Ï† a) â‰ªâ‰« isoWhiskerLeft _  (pullbackShiftIso D Ï† a (Ï† a) rfl).symm
  zero := by
    ext
    dsimp
    simp only [F.commShiftIso_zero' (A := B) (Ï† 0) (by rw [map_zero]), CommShift.isoZero'_hom_app,
      assoc, CommShift.isoZero_hom_app, pullbackShiftFunctorZero'_hom_app, map_comp,
      pullbackShiftFunctorZero'_inv_app]
    rfl
  add _ _ := by
    ext
    simp only [PullbackShift.functor, comp_obj, Iso.trans_hom, isoWhiskerRight_hom,
      isoWhiskerLeft_hom, Iso.symm_hom, NatTrans.comp_app, whiskerRight_app, whiskerLeft_app,
      CommShift.isoAdd_hom_app, map_comp, assoc]
    rw [F.commShiftIso_add' (Ï†.map_add _ _).symm,
      â† shiftFunctorAdd'_eq_shiftFunctorAdd, â† shiftFunctorAdd'_eq_shiftFunctorAdd,
      pullbackShiftFunctorAdd'_hom_app Ï† _ _ _ _ rfl _ _ _ rfl rfl rfl,
      pullbackShiftFunctorAdd'_inv_app Ï† _ _ _ _ rfl _ _ _ rfl rfl rfl]
    simp only [CommShift.isoAdd'_hom_app, assoc, map_comp, NatTrans.naturality_assoc,
      Iso.inv_hom_id_app_assoc]
    slice_rhs 9 10 => rw [â† map_comp, Iso.inv_hom_id_app, map_id]
    simp only [comp_obj, id_comp]
    rw [â† Functor.comp_map F (shiftFunctor D _), â† (F.commShiftIso _).hom.naturality_assoc]
    slice_rhs 4 5 => rw [â† map_comp, (pullbackShiftIso C Ï† _ _ rfl).hom.naturality, map_comp]
    slice_rhs 3 4 => rw [â† map_comp, Iso.inv_hom_id_app, map_id]
    simp only [comp_obj, id_comp, comp_map, assoc]
    slice_rhs 3 4 => rw [â† map_comp, â† map_comp, Iso.inv_hom_id_app, map_id, map_id]
    rw [id_comp, assoc, assoc]
    rfl

lemma commShiftPullback_iso_eq (a : A) (b : B) (h : b = Ï† a) :
    (PullbackShift.functor Ï† F).commShiftIso a (C := PullbackShift C Ï†) (D := PullbackShift D Ï†) =
      isoWhiskerRight (pullbackShiftIso C Ï† a b h) F â‰ªâ‰« (F.commShiftIso b) â‰ªâ‰«
        isoWhiskerLeft F (pullbackShiftIso D Ï† a b h).symm := by
  obtain rfl : b = Ï† a := h
  rfl

end Functor

namespace NatTrans

variable {F} {G : C â¥¤ D} [G.CommShift B]

open Functor in
instance commShiftPullback (Ï„ : F âŸ¶ G) [NatTrans.CommShift Ï„ B] :
    NatTrans.CommShift (PullbackShift.natTrans Ï† Ï„) A where
  shift_comm _ := by
    ext
    dsimp [PullbackShift.natTrans]
    simp only [commShiftPullback_iso_eq Ï† _ _ _ rfl, Iso.trans_hom, isoWhiskerRight_hom,
      isoWhiskerLeft_hom, Iso.symm_hom, comp_app, comp_obj, whiskerRight_app, whiskerLeft_app,
      assoc]
    rw [â† Ï„.naturality_assoc]
    simp  [â† NatTrans.shift_app_comm_assoc]

variable (C) in
/-- The natural isomorphism between the identity of `PullbackShift C Ï†` and the
pullback of the identity of `C`.
-/
def PullbackShift.natIsoId : ğŸ­ (PullbackShift C Ï†) â‰… PullbackShift.functor Ï† (ğŸ­ C) := Iso.refl _

/--
This expresses the compatibility between two `CommShift` structures by `A` on (synonyms of)
`ğŸ­ C`: the canonical `CommShift` structure on `ğŸ­ (PullbackShift C Ï†)`, and the `CommShift`
structure on `PullbackShift.functor (ğŸ­ C) Ï†` (i.e the pullback of the canonical `CommShift`
structure on `ğŸ­ C`).
-/
instance : NatTrans.CommShift (PullbackShift.natIsoId C Ï†).hom A where
  shift_comm _ := by
    ext
    simp [PullbackShift.natIsoId, Functor.commShiftPullback_iso_eq]

variable (F) {E : Type*} [Category E] [HasShift E B] (G : D â¥¤ E) [G.CommShift B]

/-- The natural isomorphism between the pullback of `F â‹™ G` and the
composition of the pullbacks of `F` and `G`.
-/
def PullbackShift.natIsoComp : PullbackShift.functor Ï† (F â‹™ G) â‰…
    PullbackShift.functor Ï† F â‹™ PullbackShift.functor Ï† G := Iso.refl _

/-
Suppose that `F` and `G` have `CommShift` structure by `B`. This expresses the
compatibility between two `CommShift` structures by `A` on (synonyms of) `F â‹™ G`:
the `CommShift` structure on `PullbackShift.functor (F â‹™ G) Ï†` (i.e the pullback of the
composition of `CommShift` structures by `B` on `F` and `G`), and that on
`PullbackShift.functor F Ï† â‹™ PullbackShift.functor G Ï†` (i.e. the one coming from
the composition of the pulled back `CommShift` structures on `F` and `G`).
-/
open Functor in
instance : NatTrans.CommShift (PullbackShift.natIsoComp Ï† F G).hom A where
  shift_comm _ := by
    ext
    dsimp [PullbackShift.natIsoComp]
    simp only [commShiftPullback_iso_eq Ï† _ _ _ rfl, Iso.trans_hom, isoWhiskerRight_hom,
      isoWhiskerLeft_hom, Iso.symm_hom, comp_app, comp_obj, whiskerRight_app, Functor.comp_map,
      commShiftIso_comp_hom_app, whiskerLeft_app, assoc, map_id, comp_id, map_comp, id_comp]
    dsimp [PullbackShift.functor]
    slice_rhs 3 4 => rw [â† G.map_comp, Iso.inv_hom_id_app]
    simp

end NatTrans

/--
The adjunction `adj`, seen as an adjunction between `PullbackShift.functor F Ï†`
and `PullbackShift.functor G Ï†`.
-/
@[simps -isSimp]
def PullbackShift.adjunction {F} {G : D â¥¤ C} (adj : F âŠ£ G) :
    PullbackShift.functor Ï† F âŠ£ PullbackShift.functor Ï† G where
  unit := (NatTrans.PullbackShift.natIsoId C Ï†).hom â‰«
    PullbackShift.natTrans Ï† adj.unit â‰« (NatTrans.PullbackShift.natIsoComp Ï† F G).hom
  counit := (NatTrans.PullbackShift.natIsoComp Ï† G F).inv â‰«
    PullbackShift.natTrans Ï† adj.counit â‰« (NatTrans.PullbackShift.natIsoId D Ï†).inv
  left_triangle_components _ := by
    simp [PullbackShift.natTrans, NatTrans.PullbackShift.natIsoComp,
      NatTrans.PullbackShift.natIsoId, PullbackShift.functor]
  right_triangle_components _ := by
    simp [PullbackShift.natTrans, NatTrans.PullbackShift.natIsoComp,
      NatTrans.PullbackShift.natIsoId, PullbackShift.functor]

namespace Adjunction

variable {F} {G : D â¥¤ C} (adj : F âŠ£ G) [G.CommShift B]

/--
If an adjunction `F âŠ£ G` is compatible with `CommShift` structures on `F` and `G`, then
it is also compatible with the pulled back `CommShift` structures by an additive map
`Ï† : B â†’+ A`.
-/
instance commShiftPullback [adj.CommShift B] : (PullbackShift.adjunction Ï† adj).CommShift A where
  commShift_unit := by
    dsimp [PullbackShift.adjunction]
    infer_instance
  commShift_counit := by
    dsimp [PullbackShift.adjunction]
    infer_instance

end Adjunction

end CategoryTheory
