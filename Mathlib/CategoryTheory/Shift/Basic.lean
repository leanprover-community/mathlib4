/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Johan Commelin, Andrew Yang, Jo√´l Riou
-/
import Mathlib.Algebra.Group.Basic
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Zero
import Mathlib.CategoryTheory.Monoidal.End
import Mathlib.CategoryTheory.Monoidal.Discrete

/-!
# Shift

A `Shift` on a category `C` indexed by a monoid `A` is nothing more than a monoidal functor
from `A` to `C ‚•§ C`. A typical example to keep in mind might be the category of
complexes `‚ãØ ‚Üí C_{n-1} ‚Üí C_n ‚Üí C_{n+1} ‚Üí ‚ãØ`. It has a shift indexed by `‚Ñ§`, where we assign to
each `n : ‚Ñ§` the functor `C ‚•§ C` that re-indexes the terms, so the degree `i` term of `Shift n C`
would be the degree `i+n`-th term of `C`.

## Main definitions
* `HasShift`: A typeclass asserting the existence of a shift functor.
* `shiftEquiv`: When the indexing monoid is a group, then the functor indexed by `n` and `-n` forms
  a self-equivalence of `C`.
* `shiftComm`: When the indexing monoid is commutative, then shifts commute as well.

## Implementation Notes

`[HasShift C A]` is implemented using monoidal functors from `Discrete A` to `C ‚•§ C`.
However, the API of monoidal functors is used only internally: one should use the API of
shifts functors which includes `shiftFunctor C a : C ‚•§ C` for `a : A`,
`shiftFunctorZero C A : shiftFunctor C (0 : A) ‚âÖ ùü≠ C` and
`shiftFunctorAdd C i j : shiftFunctor C (i + j) ‚âÖ shiftFunctor C i ‚ãô shiftFunctor C j`
(and its variant `shiftFunctorAdd'`). These isomorphisms satisfy some coherence properties
which are stated in lemmas like `shiftFunctorAdd'_assoc`, `shiftFunctorAdd'_zero_add` and
`shiftFunctorAdd'_add_zero`.

-/


namespace CategoryTheory

open Functor

noncomputable section

universe v u

variable (C : Type u) (A : Type*) [Category.{v} C]

attribute [local instance] endofunctorMonoidalCategory

variable {A C}

section Defs

variable (A C) [AddMonoid A]

/-- A category has a shift indexed by an additive monoid `A`
if there is a monoidal functor from `A` to `C ‚•§ C`. -/
class HasShift (C : Type u) (A : Type*) [Category.{v} C] [AddMonoid A] where
  /-- a shift is a monoidal functor from `A` to `C ‚•§ C` -/
  shift : Discrete A ‚•§ C ‚•§ C
  /-- `shift` is monoidal -/
  shiftMonoidal : shift.Monoidal := by infer_instance

/-- A helper structure to construct the shift functor `(Discrete A) ‚•§ (C ‚•§ C)`. -/
structure ShiftMkCore where
  /-- the family of shift functors -/
  F : A ‚Üí C ‚•§ C
  /-- the shift by 0 identifies to the identity functor -/
  zero : F 0 ‚âÖ ùü≠ C
  /-- the composition of shift functors identifies to the shift by the sum -/
  add : ‚àÄ n m : A, F (n + m) ‚âÖ F n ‚ãô F m
  /-- compatibility with the associativity -/
  assoc_hom_app : ‚àÄ (m‚ÇÅ m‚ÇÇ m‚ÇÉ : A) (X : C),
    (add (m‚ÇÅ + m‚ÇÇ) m‚ÇÉ).hom.app X ‚â´ (F m‚ÇÉ).map ((add m‚ÇÅ m‚ÇÇ).hom.app X) =
      eqToHom (by rw [add_assoc]) ‚â´ (add m‚ÇÅ (m‚ÇÇ + m‚ÇÉ)).hom.app X ‚â´
        (add m‚ÇÇ m‚ÇÉ).hom.app ((F m‚ÇÅ).obj X) := by cat_disch
  /-- compatibility with the left addition with 0 -/
  zero_add_hom_app : ‚àÄ (n : A) (X : C), (add 0 n).hom.app X =
    eqToHom (by dsimp; rw [zero_add]) ‚â´ (F n).map (zero.inv.app X) := by cat_disch
  /-- compatibility with the right addition with 0 -/
  add_zero_hom_app : ‚àÄ (n : A) (X : C), (add n 0).hom.app X =
    eqToHom (by dsimp; rw [add_zero]) ‚â´ zero.inv.app ((F n).obj X) := by cat_disch

namespace ShiftMkCore

variable {C A}

attribute [reassoc] assoc_hom_app

@[reassoc]
lemma assoc_inv_app (h : ShiftMkCore C A) (m‚ÇÅ m‚ÇÇ m‚ÇÉ : A) (X : C) :
    (h.F m‚ÇÉ).map ((h.add m‚ÇÅ m‚ÇÇ).inv.app X) ‚â´ (h.add (m‚ÇÅ + m‚ÇÇ) m‚ÇÉ).inv.app X =
    (h.add m‚ÇÇ m‚ÇÉ).inv.app ((h.F m‚ÇÅ).obj X) ‚â´ (h.add m‚ÇÅ (m‚ÇÇ + m‚ÇÉ)).inv.app X ‚â´
      eqToHom (by rw [add_assoc]) := by
  rw [‚Üê cancel_mono ((h.add (m‚ÇÅ + m‚ÇÇ) m‚ÇÉ).hom.app X ‚â´ (h.F m‚ÇÉ).map ((h.add m‚ÇÅ m‚ÇÇ).hom.app X)),
    Category.assoc, Category.assoc, Category.assoc, Iso.inv_hom_id_app_assoc, ‚Üê Functor.map_comp,
    Iso.inv_hom_id_app, Functor.map_id, h.assoc_hom_app, eqToHom_trans_assoc, eqToHom_refl,
    Category.id_comp, Iso.inv_hom_id_app_assoc, Iso.inv_hom_id_app]
  rfl

lemma zero_add_inv_app (h : ShiftMkCore C A) (n : A) (X : C) :
    (h.add 0 n).inv.app X = (h.F n).map (h.zero.hom.app X) ‚â´
      eqToHom (by dsimp; rw [zero_add]) := by
  rw [‚Üê cancel_epi ((h.add 0 n).hom.app X), Iso.hom_inv_id_app, h.zero_add_hom_app,
    Category.assoc, ‚Üê Functor.map_comp_assoc, Iso.inv_hom_id_app, Functor.map_id,
    Category.id_comp, eqToHom_trans, eqToHom_refl]

lemma add_zero_inv_app (h : ShiftMkCore C A) (n : A) (X : C) :
    (h.add n 0).inv.app X = h.zero.hom.app ((h.F n).obj X) ‚â´
      eqToHom (by dsimp; rw [add_zero]) := by
  rw [‚Üê cancel_epi ((h.add n 0).hom.app X), Iso.hom_inv_id_app, h.add_zero_hom_app,
    Category.assoc, Iso.inv_hom_id_app_assoc, eqToHom_trans, eqToHom_refl]

end ShiftMkCore

section

attribute [local simp] eqToHom_map

instance (h : ShiftMkCore C A) : (Discrete.functor h.F).Monoidal :=
  Functor.CoreMonoidal.toMonoidal
    { ŒµIso := h.zero.symm
      ŒºIso := fun m n ‚Ü¶ (h.add m.as n.as).symm
      ŒºIso_hom_natural_left := by
        rintro ‚ü®X‚ü© ‚ü®Y‚ü© ‚ü®‚ü®‚ü®rfl‚ü©‚ü©‚ü© ‚ü®X'‚ü©
        ext
        simp
      ŒºIso_hom_natural_right := by
        rintro ‚ü®X‚ü© ‚ü®Y‚ü© ‚ü®X'‚ü© ‚ü®‚ü®‚ü®rfl‚ü©‚ü©‚ü©
        ext
        simp
      associativity := by
        rintro ‚ü®m‚ÇÅ‚ü© ‚ü®m‚ÇÇ‚ü© ‚ü®m‚ÇÉ‚ü©
        ext X
        simp [endofunctorMonoidalCategory, h.assoc_inv_app_assoc]
      left_unitality := by
        rintro ‚ü®n‚ü©
        ext X
        simp [endofunctorMonoidalCategory, h.zero_add_inv_app, ‚Üê Functor.map_comp]
      right_unitality := by
        rintro ‚ü®n‚ü©
        ext X
        simp [endofunctorMonoidalCategory, h.add_zero_inv_app] }

/-- Constructs a `HasShift C A` instance from `ShiftMkCore`. -/
def hasShiftMk (h : ShiftMkCore C A) : HasShift C A where
  shift := Discrete.functor h.F

end

section
variable [HasShift C A]

/-- The monoidal functor from `A` to `C ‚•§ C` given a `HasShift` instance. -/
def shiftMonoidalFunctor : Discrete A ‚•§ C ‚•§ C :=
  HasShift.shift

instance : (shiftMonoidalFunctor C A).Monoidal := HasShift.shiftMonoidal

variable {A}

open Functor.Monoidal

/-- The shift autoequivalence, moving objects and morphisms 'up'. -/
def shiftFunctor (i : A) : C ‚•§ C :=
  (shiftMonoidalFunctor C A).obj ‚ü®i‚ü©

/-- Shifting by `i + j` is the same as shifting by `i` and then shifting by `j`. -/
def shiftFunctorAdd (i j : A) : shiftFunctor C (i + j) ‚âÖ shiftFunctor C i ‚ãô shiftFunctor C j :=
  (ŒºIso (shiftMonoidalFunctor C A) ‚ü®i‚ü© ‚ü®j‚ü©).symm

/-- When `k = i + j`, shifting by `k` is the same as shifting by `i` and then shifting by `j`. -/
def shiftFunctorAdd' (i j k : A) (h : i + j = k) :
    shiftFunctor C k ‚âÖ shiftFunctor C i ‚ãô shiftFunctor C j :=
  eqToIso (by rw [h]) ‚â™‚â´ shiftFunctorAdd C i j

lemma shiftFunctorAdd'_eq_shiftFunctorAdd (i j : A) :
    shiftFunctorAdd' C i j (i+j) rfl = shiftFunctorAdd C i j := by
  ext1
  apply Category.id_comp

variable (A) in
/-- Shifting by zero is the identity functor. -/
def shiftFunctorZero : shiftFunctor C (0 : A) ‚âÖ ùü≠ C :=
  (ŒµIso (shiftMonoidalFunctor C A)).symm

/-- Shifting by `a` such that `a = 0` identifies to the identity functor. -/
def shiftFunctorZero' (a : A) (ha : a = 0) : shiftFunctor C a ‚âÖ ùü≠ C :=
  eqToIso (by rw [ha]) ‚â™‚â´ shiftFunctorZero C A

end

variable {C A}

lemma ShiftMkCore.shiftFunctor_eq (h : ShiftMkCore C A) (a : A) :
    letI := hasShiftMk C A h
    shiftFunctor C a = h.F a := rfl

lemma ShiftMkCore.shiftFunctorZero_eq (h : ShiftMkCore C A) :
    letI := hasShiftMk C A h
    shiftFunctorZero C A = h.zero := rfl

lemma ShiftMkCore.shiftFunctorAdd_eq (h : ShiftMkCore C A) (a b : A) :
    letI := hasShiftMk C A h
    shiftFunctorAdd C a b = h.add a b := rfl

set_option quotPrecheck false in
/-- shifting an object `X` by `n` is obtained by the notation `X‚ü¶n‚üß` -/
notation -- Any better notational suggestions?
X "‚ü¶" n "‚üß" => (shiftFunctor _ n).obj X

set_option quotPrecheck false in
/-- shifting a morphism `f` by `n` is obtained by the notation `f‚ü¶n‚üß'` -/
notation f "‚ü¶" n "‚üß'" => (shiftFunctor _ n).map f

variable (C)
variable [HasShift C A]

lemma shiftFunctorAdd'_zero_add (a : A) :
    shiftFunctorAdd' C 0 a a (zero_add a) = (leftUnitor _).symm ‚â™‚â´
    isoWhiskerRight (shiftFunctorZero C A).symm (shiftFunctor C a) := by
  ext X
  dsimp [shiftFunctorAdd', shiftFunctorZero, shiftFunctor]
  simp only [eqToHom_app, obj_Œµ_app, Discrete.addMonoidal_leftUnitor, eqToIso.inv,
    eqToHom_map, Category.id_comp]
  rfl

lemma shiftFunctorAdd'_add_zero (a : A) :
    shiftFunctorAdd' C a 0 a (add_zero a) = (rightUnitor _).symm ‚â™‚â´
    isoWhiskerLeft (shiftFunctor C a) (shiftFunctorZero C A).symm := by
  ext
  dsimp [shiftFunctorAdd', shiftFunctorZero, shiftFunctor]
  simp only [eqToHom_app, Œµ_app_obj, Discrete.addMonoidal_rightUnitor, eqToIso.inv,
    eqToHom_map, Category.id_comp]
  rfl

lemma shiftFunctorAdd'_assoc (a‚ÇÅ a‚ÇÇ a‚ÇÉ a‚ÇÅ‚ÇÇ a‚ÇÇ‚ÇÉ a‚ÇÅ‚ÇÇ‚ÇÉ : A)
    (h‚ÇÅ‚ÇÇ : a‚ÇÅ + a‚ÇÇ = a‚ÇÅ‚ÇÇ) (h‚ÇÇ‚ÇÉ : a‚ÇÇ + a‚ÇÉ = a‚ÇÇ‚ÇÉ) (h‚ÇÅ‚ÇÇ‚ÇÉ : a‚ÇÅ + a‚ÇÇ + a‚ÇÉ = a‚ÇÅ‚ÇÇ‚ÇÉ) :
    shiftFunctorAdd' C a‚ÇÅ‚ÇÇ a‚ÇÉ a‚ÇÅ‚ÇÇ‚ÇÉ (by rw [‚Üê h‚ÇÅ‚ÇÇ, h‚ÇÅ‚ÇÇ‚ÇÉ]) ‚â™‚â´
      isoWhiskerRight (shiftFunctorAdd' C a‚ÇÅ a‚ÇÇ a‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ) _ ‚â™‚â´ associator _ _ _ =
    shiftFunctorAdd' C a‚ÇÅ a‚ÇÇ‚ÇÉ a‚ÇÅ‚ÇÇ‚ÇÉ (by rw [‚Üê h‚ÇÇ‚ÇÉ, ‚Üê add_assoc, h‚ÇÅ‚ÇÇ‚ÇÉ]) ‚â™‚â´
      isoWhiskerLeft _ (shiftFunctorAdd' C a‚ÇÇ a‚ÇÉ a‚ÇÇ‚ÇÉ h‚ÇÇ‚ÇÉ) := by
  subst h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÇ‚ÇÉ
  ext X
  dsimp
  simp only [shiftFunctorAdd'_eq_shiftFunctorAdd, Category.comp_id]
  dsimp [shiftFunctorAdd']
  simp only [eqToHom_app]
  dsimp [shiftFunctorAdd, shiftFunctor]
  simp only [obj_Œº_inv_app, Discrete.addMonoidal_associator, eqToIso.hom, eqToHom_map,
    eqToHom_app]
  erw [Œ¥_Œº_app_assoc, Category.assoc]
  rfl

lemma shiftFunctorAdd_assoc (a‚ÇÅ a‚ÇÇ a‚ÇÉ : A) :
    shiftFunctorAdd C (a‚ÇÅ + a‚ÇÇ) a‚ÇÉ ‚â™‚â´
      isoWhiskerRight (shiftFunctorAdd C a‚ÇÅ a‚ÇÇ) _ ‚â™‚â´ associator _ _ _ =
    shiftFunctorAdd' C a‚ÇÅ (a‚ÇÇ + a‚ÇÉ) _ (add_assoc a‚ÇÅ a‚ÇÇ a‚ÇÉ).symm ‚â™‚â´
      isoWhiskerLeft _ (shiftFunctorAdd C a‚ÇÇ a‚ÇÉ) := by
  ext X
  simpa [shiftFunctorAdd'_eq_shiftFunctorAdd]
    using NatTrans.congr_app (congr_arg Iso.hom
      (shiftFunctorAdd'_assoc C a‚ÇÅ a‚ÇÇ a‚ÇÉ _ _ _ rfl rfl rfl)) X

variable {C}

lemma shiftFunctorAdd'_zero_add_hom_app (a : A) (X : C) :
    (shiftFunctorAdd' C 0 a a (zero_add a)).hom.app X =
    ((shiftFunctorZero C A).inv.app X)‚ü¶a‚üß' := by
  simpa using NatTrans.congr_app (congr_arg Iso.hom (shiftFunctorAdd'_zero_add C a)) X

lemma shiftFunctorAdd_zero_add_hom_app (a : A) (X : C) :
    (shiftFunctorAdd C 0 a).hom.app X =
    eqToHom (by dsimp; rw [zero_add]) ‚â´ ((shiftFunctorZero C A).inv.app X)‚ü¶a‚üß' := by
  simp [‚Üê shiftFunctorAdd'_zero_add_hom_app, shiftFunctorAdd']

lemma shiftFunctorAdd'_zero_add_inv_app (a : A) (X : C) :
    (shiftFunctorAdd' C 0 a a (zero_add a)).inv.app X =
    ((shiftFunctorZero C A).hom.app X)‚ü¶a‚üß' := by
  simpa using NatTrans.congr_app (congr_arg Iso.inv (shiftFunctorAdd'_zero_add C a)) X

lemma shiftFunctorAdd_zero_add_inv_app (a : A) (X : C) : (shiftFunctorAdd C 0 a).inv.app X =
    ((shiftFunctorZero C A).hom.app X)‚ü¶a‚üß' ‚â´ eqToHom (by dsimp; rw [zero_add]) := by
  simp [‚Üê shiftFunctorAdd'_zero_add_inv_app, shiftFunctorAdd']

lemma shiftFunctorAdd'_add_zero_hom_app (a : A) (X : C) :
    (shiftFunctorAdd' C a 0 a (add_zero a)).hom.app X =
    (shiftFunctorZero C A).inv.app (X‚ü¶a‚üß) := by
  simpa using NatTrans.congr_app (congr_arg Iso.hom (shiftFunctorAdd'_add_zero C a)) X

lemma shiftFunctorAdd_add_zero_hom_app (a : A) (X : C) : (shiftFunctorAdd C a 0).hom.app X =
    eqToHom (by dsimp; rw [add_zero]) ‚â´ (shiftFunctorZero C A).inv.app (X‚ü¶a‚üß) := by
  simp [‚Üê shiftFunctorAdd'_add_zero_hom_app, shiftFunctorAdd']

lemma shiftFunctorAdd'_add_zero_inv_app (a : A) (X : C) :
    (shiftFunctorAdd' C a 0 a (add_zero a)).inv.app X =
    (shiftFunctorZero C A).hom.app (X‚ü¶a‚üß) := by
  simpa using NatTrans.congr_app (congr_arg Iso.inv (shiftFunctorAdd'_add_zero C a)) X

lemma shiftFunctorAdd_add_zero_inv_app (a : A) (X : C) : (shiftFunctorAdd C a 0).inv.app X =
    (shiftFunctorZero C A).hom.app (X‚ü¶a‚üß) ‚â´ eqToHom (by dsimp; rw [add_zero]) := by
  simp [‚Üê shiftFunctorAdd'_add_zero_inv_app, shiftFunctorAdd']

@[reassoc]
lemma shiftFunctorAdd'_assoc_hom_app (a‚ÇÅ a‚ÇÇ a‚ÇÉ a‚ÇÅ‚ÇÇ a‚ÇÇ‚ÇÉ a‚ÇÅ‚ÇÇ‚ÇÉ : A)
    (h‚ÇÅ‚ÇÇ : a‚ÇÅ + a‚ÇÇ = a‚ÇÅ‚ÇÇ) (h‚ÇÇ‚ÇÉ : a‚ÇÇ + a‚ÇÉ = a‚ÇÇ‚ÇÉ) (h‚ÇÅ‚ÇÇ‚ÇÉ : a‚ÇÅ + a‚ÇÇ + a‚ÇÉ = a‚ÇÅ‚ÇÇ‚ÇÉ) (X : C) :
    (shiftFunctorAdd' C a‚ÇÅ‚ÇÇ a‚ÇÉ a‚ÇÅ‚ÇÇ‚ÇÉ (by rw [‚Üê h‚ÇÅ‚ÇÇ, h‚ÇÅ‚ÇÇ‚ÇÉ])).hom.app X ‚â´
      ((shiftFunctorAdd' C a‚ÇÅ a‚ÇÇ a‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ).hom.app X)‚ü¶a‚ÇÉ‚üß' =
    (shiftFunctorAdd' C a‚ÇÅ a‚ÇÇ‚ÇÉ a‚ÇÅ‚ÇÇ‚ÇÉ (by rw [‚Üê h‚ÇÇ‚ÇÉ, ‚Üê add_assoc, h‚ÇÅ‚ÇÇ‚ÇÉ])).hom.app X ‚â´
      (shiftFunctorAdd' C a‚ÇÇ a‚ÇÉ a‚ÇÇ‚ÇÉ h‚ÇÇ‚ÇÉ).hom.app (X‚ü¶a‚ÇÅ‚üß) := by
  simpa using NatTrans.congr_app (congr_arg Iso.hom
    (shiftFunctorAdd'_assoc C _ _ _ _ _ _ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÇ‚ÇÉ)) X

@[reassoc]
lemma shiftFunctorAdd'_assoc_inv_app (a‚ÇÅ a‚ÇÇ a‚ÇÉ a‚ÇÅ‚ÇÇ a‚ÇÇ‚ÇÉ a‚ÇÅ‚ÇÇ‚ÇÉ : A)
    (h‚ÇÅ‚ÇÇ : a‚ÇÅ + a‚ÇÇ = a‚ÇÅ‚ÇÇ) (h‚ÇÇ‚ÇÉ : a‚ÇÇ + a‚ÇÉ = a‚ÇÇ‚ÇÉ) (h‚ÇÅ‚ÇÇ‚ÇÉ : a‚ÇÅ + a‚ÇÇ + a‚ÇÉ = a‚ÇÅ‚ÇÇ‚ÇÉ) (X : C) :
    ((shiftFunctorAdd' C a‚ÇÅ a‚ÇÇ a‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ).inv.app X)‚ü¶a‚ÇÉ‚üß' ‚â´
      (shiftFunctorAdd' C a‚ÇÅ‚ÇÇ a‚ÇÉ a‚ÇÅ‚ÇÇ‚ÇÉ (by rw [‚Üê h‚ÇÅ‚ÇÇ, h‚ÇÅ‚ÇÇ‚ÇÉ])).inv.app X =
    (shiftFunctorAdd' C a‚ÇÇ a‚ÇÉ a‚ÇÇ‚ÇÉ h‚ÇÇ‚ÇÉ).inv.app (X‚ü¶a‚ÇÅ‚üß) ‚â´
      (shiftFunctorAdd' C a‚ÇÅ a‚ÇÇ‚ÇÉ a‚ÇÅ‚ÇÇ‚ÇÉ (by rw [‚Üê h‚ÇÇ‚ÇÉ, ‚Üê add_assoc, h‚ÇÅ‚ÇÇ‚ÇÉ])).inv.app X := by
  simpa using NatTrans.congr_app (congr_arg Iso.inv
    (shiftFunctorAdd'_assoc C _ _ _ _ _ _ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÇ‚ÇÉ)) X

@[reassoc]
lemma shiftFunctorAdd_assoc_hom_app (a‚ÇÅ a‚ÇÇ a‚ÇÉ : A) (X : C) :
    (shiftFunctorAdd C (a‚ÇÅ + a‚ÇÇ) a‚ÇÉ).hom.app X ‚â´
      ((shiftFunctorAdd C a‚ÇÅ a‚ÇÇ).hom.app X)‚ü¶a‚ÇÉ‚üß' =
    (shiftFunctorAdd' C a‚ÇÅ (a‚ÇÇ + a‚ÇÉ) (a‚ÇÅ + a‚ÇÇ + a‚ÇÉ) (add_assoc _ _ _).symm).hom.app X ‚â´
      (shiftFunctorAdd C a‚ÇÇ a‚ÇÉ).hom.app (X‚ü¶a‚ÇÅ‚üß) := by
  simpa using NatTrans.congr_app (congr_arg Iso.hom (shiftFunctorAdd_assoc C a‚ÇÅ a‚ÇÇ a‚ÇÉ)) X

@[reassoc]
lemma shiftFunctorAdd_assoc_inv_app (a‚ÇÅ a‚ÇÇ a‚ÇÉ : A) (X : C) :
    ((shiftFunctorAdd C a‚ÇÅ a‚ÇÇ).inv.app X)‚ü¶a‚ÇÉ‚üß' ‚â´
      (shiftFunctorAdd C (a‚ÇÅ + a‚ÇÇ) a‚ÇÉ).inv.app X =
    (shiftFunctorAdd C a‚ÇÇ a‚ÇÉ).inv.app (X‚ü¶a‚ÇÅ‚üß) ‚â´
      (shiftFunctorAdd' C a‚ÇÅ (a‚ÇÇ + a‚ÇÉ) (a‚ÇÅ + a‚ÇÇ + a‚ÇÉ) (add_assoc _ _ _).symm).inv.app X := by
  simpa using NatTrans.congr_app (congr_arg Iso.inv (shiftFunctorAdd_assoc C a‚ÇÅ a‚ÇÇ a‚ÇÉ)) X

end Defs

section AddMonoid

variable [AddMonoid A] [HasShift C A] (X Y : C) (f : X ‚ü∂ Y)

--@[simp]
--theorem HasShift.shift_obj_obj (n : A) (X : C) : (HasShift.shift.obj ‚ü®n‚ü©).obj X = X‚ü¶n‚üß :=
--  rfl

/-- Shifting by `i + j` is the same as shifting by `i` and then shifting by `j`. -/
abbrev shiftAdd (i j : A) : X‚ü¶i + j‚üß ‚âÖ X‚ü¶i‚üß‚ü¶j‚üß :=
  (shiftFunctorAdd C i j).app _

theorem shift_shift' (i j : A) :
    f‚ü¶i‚üß'‚ü¶j‚üß' = (shiftAdd X i j).inv ‚â´ f‚ü¶i + j‚üß' ‚â´ (shiftAdd Y i j).hom := by
  simp

variable (A)

/-- Shifting by zero is the identity functor. -/
abbrev shiftZero : X‚ü¶(0 : A)‚üß ‚âÖ X :=
  (shiftFunctorZero C A).app _

theorem shiftZero' : f‚ü¶(0 : A)‚üß' = (shiftZero A X).hom ‚â´ f ‚â´ (shiftZero A Y).inv := by
  symm
  rw [Iso.app_inv, Iso.app_hom]
  apply NatIso.naturality_2

variable (C) {A}

/-- When `i + j = 0`, shifting by `i` and by `j` gives the identity functor -/
def shiftFunctorCompIsoId (i j : A) (h : i + j = 0) :
    shiftFunctor C i ‚ãô shiftFunctor C j ‚âÖ ùü≠ C :=
  (shiftFunctorAdd' C i j 0 h).symm ‚â™‚â´ shiftFunctorZero C A

end AddMonoid

section AddGroup

variable (C)
variable [AddGroup A] [HasShift C A]

/-- Shifting by `i` and shifting by `j` forms an equivalence when `i + j = 0`. -/
@[simps]
def shiftEquiv' (i j : A) (h : i + j = 0) : C ‚âå C where
  functor := shiftFunctor C i
  inverse := shiftFunctor C j
  unitIso := (shiftFunctorCompIsoId C i j h).symm
  counitIso := shiftFunctorCompIsoId C j i
    (by rw [‚Üê add_left_inj j, add_assoc, h, zero_add, add_zero])
  functor_unitIso_comp X := by
    convert (equivOfTensorIsoUnit (shiftMonoidalFunctor C A) ‚ü®i‚ü© ‚ü®j‚ü© (Discrete.eqToIso h)
      (Discrete.eqToIso (by dsimp; rw [‚Üê add_left_inj j, add_assoc, h, zero_add, add_zero]))
      (Subsingleton.elim _ _)).functor_unitIso_comp X
    all_goals
      ext X
      dsimp [shiftFunctorCompIsoId, unitOfTensorIsoUnit,
        shiftFunctorAdd']
      simp only [Category.assoc, eqToHom_map]
      rfl

/-- Shifting by `n` and shifting by `-n` forms an equivalence. -/
abbrev shiftEquiv (n : A) : C ‚âå C := shiftEquiv' C n (-n) (add_neg_cancel n)

variable (X Y : C) (f : X ‚ü∂ Y)

/-- Shifting by `i` is an equivalence. -/
instance (i : A) : (shiftFunctor C i).IsEquivalence := by
  change (shiftEquiv C i).functor.IsEquivalence
  infer_instance

variable {C}

/-- Shifting by `i` and then shifting by `-i` is the identity. -/
abbrev shiftShiftNeg (i : A) : X‚ü¶i‚üß‚ü¶-i‚üß ‚âÖ X :=
  (shiftEquiv C i).unitIso.symm.app X

/-- Shifting by `-i` and then shifting by `i` is the identity. -/
abbrev shiftNegShift (i : A) : X‚ü¶-i‚üß‚ü¶i‚üß ‚âÖ X :=
  (shiftEquiv C i).counitIso.app X

variable {X Y}

theorem shift_shift_neg' (i : A) :
    f‚ü¶i‚üß'‚ü¶-i‚üß' = (shiftFunctorCompIsoId C i (-i) (add_neg_cancel i)).hom.app X ‚â´
      f ‚â´ (shiftFunctorCompIsoId C i (-i) (add_neg_cancel i)).inv.app Y :=
  (NatIso.naturality_2 (shiftFunctorCompIsoId C i (-i) (add_neg_cancel i)) f).symm

theorem shift_neg_shift' (i : A) :
    f‚ü¶-i‚üß'‚ü¶i‚üß' = (shiftFunctorCompIsoId C (-i) i (neg_add_cancel i)).hom.app X ‚â´ f ‚â´
      (shiftFunctorCompIsoId C (-i) i (neg_add_cancel i)).inv.app Y :=
  (NatIso.naturality_2 (shiftFunctorCompIsoId C (-i) i (neg_add_cancel i)) f).symm

theorem shift_equiv_triangle (n : A) (X : C) :
    (shiftShiftNeg X n).inv‚ü¶n‚üß' ‚â´ (shiftNegShift (X‚ü¶n‚üß) n).hom = ùüô (X‚ü¶n‚üß) :=
  (shiftEquiv C n).functor_unitIso_comp X

section

theorem shift_shiftFunctorCompIsoId_hom_app (n m : A) (h : n + m = 0) (X : C) :
    ((shiftFunctorCompIsoId C n m h).hom.app X)‚ü¶n‚üß' =
    (shiftFunctorCompIsoId C m n
      (by rw [‚Üê neg_eq_of_add_eq_zero_left h, add_neg_cancel])).hom.app (X‚ü¶n‚üß) := by
  dsimp [shiftFunctorCompIsoId]
  simpa only [Functor.map_comp, ‚Üê shiftFunctorAdd'_zero_add_inv_app n X,
    ‚Üê shiftFunctorAdd'_add_zero_inv_app n X]
    using shiftFunctorAdd'_assoc_inv_app n m n 0 0 n h
      (by rw [‚Üê neg_eq_of_add_eq_zero_left h, add_neg_cancel]) (by rw [h, zero_add]) X

theorem shift_shiftFunctorCompIsoId_inv_app (n m : A) (h : n + m = 0) (X : C) :
    ((shiftFunctorCompIsoId C n m h).inv.app X)‚ü¶n‚üß' =
    ((shiftFunctorCompIsoId C m n
      (by rw [‚Üê neg_eq_of_add_eq_zero_left h, add_neg_cancel])).inv.app (X‚ü¶n‚üß)) := by
  rw [‚Üê cancel_mono (((shiftFunctorCompIsoId C n m h).hom.app X)‚ü¶n‚üß'),
    ‚Üê Functor.map_comp, Iso.inv_hom_id_app, Functor.map_id,
    shift_shiftFunctorCompIsoId_hom_app, Iso.inv_hom_id_app]
  rfl

theorem shift_shiftFunctorCompIsoId_add_neg_cancel_hom_app (n : A) (X : C) :
    ((shiftFunctorCompIsoId C n (-n) (add_neg_cancel n)).hom.app X)‚ü¶n‚üß' =
    (shiftFunctorCompIsoId C (-n) n (neg_add_cancel n)).hom.app (X‚ü¶n‚üß) := by
  apply shift_shiftFunctorCompIsoId_hom_app

theorem shift_shiftFunctorCompIsoId_add_neg_cancel_inv_app (n : A) (X : C) :
    ((shiftFunctorCompIsoId C n (-n) (add_neg_cancel n)).inv.app X)‚ü¶n‚üß' =
    (shiftFunctorCompIsoId C (-n) n (neg_add_cancel n)).inv.app (X‚ü¶n‚üß) := by
  apply shift_shiftFunctorCompIsoId_inv_app

theorem shift_shiftFunctorCompIsoId_neg_add_cancel_hom_app (n : A) (X : C) :
    ((shiftFunctorCompIsoId C (-n) n (neg_add_cancel n)).hom.app X)‚ü¶-n‚üß' =
    (shiftFunctorCompIsoId C n (-n) (add_neg_cancel n)).hom.app (X‚ü¶-n‚üß) := by
  apply shift_shiftFunctorCompIsoId_hom_app

theorem shift_shiftFunctorCompIsoId_neg_add_cancel_inv_app (n : A) (X : C) :
    ((shiftFunctorCompIsoId C (-n) n (neg_add_cancel n)).inv.app X)‚ü¶-n‚üß' =
    (shiftFunctorCompIsoId C n (-n) (add_neg_cancel n)).inv.app (X‚ü¶-n‚üß) := by
  apply shift_shiftFunctorCompIsoId_inv_app

end

section

variable (A)

lemma shiftFunctorCompIsoId_zero_zero_hom_app (X : C) :
    (shiftFunctorCompIsoId C 0 0 (add_zero 0)).hom.app X =
      ((shiftFunctorZero C A).hom.app X)‚ü¶0‚üß' ‚â´ (shiftFunctorZero C A).hom.app X := by
  simp [shiftFunctorCompIsoId, shiftFunctorAdd'_zero_add_inv_app]

lemma shiftFunctorCompIsoId_zero_zero_inv_app (X : C) :
    (shiftFunctorCompIsoId C 0 0 (add_zero 0)).inv.app X =
      (shiftFunctorZero C A).inv.app X ‚â´ ((shiftFunctorZero C A).inv.app X)‚ü¶0‚üß' := by
  simp [shiftFunctorCompIsoId, shiftFunctorAdd'_zero_add_hom_app]

end

section

variable (m n p m' n' p' : A) (hm : m' + m = 0) (hn : n' + n = 0) (hp : p' + p = 0)
  (h : m + n = p)

lemma shiftFunctorCompIsoId_add'_inv_app :
    (shiftFunctorCompIsoId C p' p hp).inv.app X =
      (shiftFunctorCompIsoId C n' n hn).inv.app X ‚â´
      (shiftFunctorCompIsoId C m' m hm).inv.app (X‚ü¶n'‚üß)‚ü¶n‚üß' ‚â´
      (shiftFunctorAdd' C m n p h).inv.app (X‚ü¶n'‚üß‚ü¶m'‚üß) ‚â´
      ((shiftFunctorAdd' C n' m' p'
        (by rw [‚Üê add_left_inj p, hp, ‚Üê h, add_assoc,
          ‚Üê add_assoc m', hm, zero_add, hn])).inv.app X)‚ü¶p‚üß' := by
  dsimp [shiftFunctorCompIsoId]
  simp only [Functor.map_comp, Category.assoc]
  congr 1
  rw [‚Üê NatTrans.naturality]
  dsimp
  rw [‚Üê cancel_mono ((shiftFunctorAdd' C p' p 0 hp).inv.app X), Iso.hom_inv_id_app,
    Category.assoc, Category.assoc, Category.assoc, Category.assoc,
    ‚Üê shiftFunctorAdd'_assoc_inv_app p' m n n' p 0
      (by rw [‚Üê add_left_inj n, hn, add_assoc, h, hp]) h (by rw [add_assoc, h, hp]),
    ‚Üê Functor.map_comp_assoc, ‚Üê Functor.map_comp_assoc, ‚Üê Functor.map_comp_assoc,
    Category.assoc, Category.assoc,
    shiftFunctorAdd'_assoc_inv_app n' m' m p' 0 n' _ hm
      (by rw [add_assoc, hm, add_zero]), Iso.hom_inv_id_app_assoc,
    ‚Üê shiftFunctorAdd'_add_zero_hom_app, Iso.hom_inv_id_app,
    Functor.map_id, Category.id_comp, Iso.hom_inv_id_app]

lemma shiftFunctorCompIsoId_add'_hom_app :
    (shiftFunctorCompIsoId C p' p hp).hom.app X =
      ((shiftFunctorAdd' C n' m' p'
          (by rw [‚Üê add_left_inj p, hp, ‚Üê h, add_assoc,
            ‚Üê add_assoc m', hm, zero_add, hn])).hom.app X)‚ü¶p‚üß' ‚â´
      (shiftFunctorAdd' C m n p h).hom.app (X‚ü¶n'‚üß‚ü¶m'‚üß) ‚â´
      (shiftFunctorCompIsoId C m' m hm).hom.app (X‚ü¶n'‚üß)‚ü¶n‚üß' ‚â´
      (shiftFunctorCompIsoId C n' n hn).hom.app X := by
  rw [‚Üê cancel_mono ((shiftFunctorCompIsoId C p' p hp).inv.app X), Iso.hom_inv_id_app,
    shiftFunctorCompIsoId_add'_inv_app m n p m' n' p' hm hn hp h,
    Category.assoc, Category.assoc, Category.assoc, Iso.hom_inv_id_app_assoc,
    ‚Üê Functor.map_comp_assoc, Iso.hom_inv_id_app]
  dsimp
  rw [Functor.map_id, Category.id_comp, Iso.hom_inv_id_app_assoc,
    ‚Üê Functor.map_comp, Iso.hom_inv_id_app, Functor.map_id]

end

open CategoryTheory.Limits

variable [HasZeroMorphisms C]

theorem shift_zero_eq_zero (X Y : C) (n : A) : (0 : X ‚ü∂ Y)‚ü¶n‚üß' = (0 : X‚ü¶n‚üß ‚ü∂ Y‚ü¶n‚üß) :=
  CategoryTheory.Functor.map_zero _ _ _

end AddGroup

section AddCommMonoid

variable [AddCommMonoid A] [HasShift C A]
variable (C)

/-- When shifts are indexed by an additive commutative monoid, then shifts commute. -/
def shiftFunctorComm (i j : A) :
    shiftFunctor C i ‚ãô shiftFunctor C j ‚âÖ
      shiftFunctor C j ‚ãô shiftFunctor C i :=
  (shiftFunctorAdd C i j).symm ‚â™‚â´ shiftFunctorAdd' C j i (i + j) (add_comm j i)

lemma shiftFunctorComm_eq (i j k : A) (h : i + j = k) :
    shiftFunctorComm C i j = (shiftFunctorAdd' C i j k h).symm ‚â™‚â´
      shiftFunctorAdd' C j i k (by rw [add_comm j i, h]) := by
  subst h
  rw [shiftFunctorAdd'_eq_shiftFunctorAdd]
  rfl

@[simp]
lemma shiftFunctorComm_eq_refl (i : A) :
    shiftFunctorComm C i i = Iso.refl _ := by
  rw [shiftFunctorComm_eq C i i (i + i) rfl, Iso.symm_self_id]

lemma shiftFunctorComm_symm (i j : A) :
    (shiftFunctorComm C i j).symm = shiftFunctorComm C j i := by
  ext1
  dsimp
  rw [shiftFunctorComm_eq C i j (i+j) rfl, shiftFunctorComm_eq C j i (i+j) (add_comm j i)]
  rfl

variable {C}
variable (X Y : C) (f : X ‚ü∂ Y)

/-- When shifts are indexed by an additive commutative monoid, then shifts commute. -/
abbrev shiftComm (i j : A) : X‚ü¶i‚üß‚ü¶j‚üß ‚âÖ X‚ü¶j‚üß‚ü¶i‚üß :=
  (shiftFunctorComm C i j).app X

@[simp]
theorem shiftComm_symm (i j : A) : (shiftComm X i j).symm = shiftComm X j i := by
  ext
  exact NatTrans.congr_app (congr_arg Iso.hom (shiftFunctorComm_symm C i j)) X

variable {X Y}

/-- When shifts are indexed by an additive commutative monoid, then shifts commute. -/
theorem shiftComm' (i j : A) :
    f‚ü¶i‚üß'‚ü¶j‚üß' = (shiftComm _ _ _).hom ‚â´ f‚ü¶j‚üß'‚ü¶i‚üß' ‚â´ (shiftComm _ _ _).hom := by
  erw [‚Üê shiftComm_symm Y i j, ‚Üê ((shiftFunctorComm C i j).hom.naturality_assoc f)]
  dsimp
  simp only [Iso.hom_inv_id_app, Functor.comp_obj, Category.comp_id]

@[reassoc]
theorem shiftComm_hom_comp (i j : A) :
    (shiftComm X i j).hom ‚â´ f‚ü¶j‚üß'‚ü¶i‚üß' = f‚ü¶i‚üß'‚ü¶j‚üß' ‚â´ (shiftComm Y i j).hom := by
  rw [shiftComm', ‚Üê shiftComm_symm, Iso.symm_hom, Iso.inv_hom_id_assoc]

lemma shiftFunctorZero_hom_app_shift (n : A) :
    (shiftFunctorZero C A).hom.app (X‚ü¶n‚üß) =
    (shiftFunctorComm C n 0).hom.app X ‚â´ ((shiftFunctorZero C A).hom.app X)‚ü¶n‚üß' := by
  rw [‚Üê shiftFunctorAdd'_zero_add_inv_app n X, shiftFunctorComm_eq C n 0 n (add_zero n)]
  dsimp
  rw [Category.assoc, Iso.hom_inv_id_app, Category.comp_id, shiftFunctorAdd'_add_zero_inv_app]

lemma shiftFunctorZero_inv_app_shift (n : A) :
    (shiftFunctorZero C A).inv.app (X‚ü¶n‚üß) =
      ((shiftFunctorZero C A).inv.app X)‚ü¶n‚üß' ‚â´ (shiftFunctorComm C n 0).inv.app X := by
  rw [‚Üê cancel_mono ((shiftFunctorZero C A).hom.app (X‚ü¶n‚üß)), Category.assoc, Iso.inv_hom_id_app,
    shiftFunctorZero_hom_app_shift, Iso.inv_hom_id_app_assoc, ‚Üê Functor.map_comp,
    Iso.inv_hom_id_app]
  dsimp
  rw [Functor.map_id]

lemma shiftFunctorComm_zero_hom_app (a : A) :
    (shiftFunctorComm C a 0).hom.app X =
      (shiftFunctorZero C A).hom.app (X‚ü¶a‚üß) ‚â´ ((shiftFunctorZero C A).inv.app X)‚ü¶a‚üß' := by
  simp only [shiftFunctorZero_hom_app_shift, Category.assoc, ‚Üê Functor.map_comp,
    Iso.hom_inv_id_app, Functor.map_id, Functor.comp_obj, Category.comp_id]

@[reassoc]
lemma shiftFunctorComm_hom_app_comp_shift_shiftFunctorAdd_hom_app (m‚ÇÅ m‚ÇÇ m‚ÇÉ : A) (X : C) :
    (shiftFunctorComm C m‚ÇÅ (m‚ÇÇ + m‚ÇÉ)).hom.app X ‚â´
    ((shiftFunctorAdd C m‚ÇÇ m‚ÇÉ).hom.app X)‚ü¶m‚ÇÅ‚üß' =
      (shiftFunctorAdd C m‚ÇÇ m‚ÇÉ).hom.app (X‚ü¶m‚ÇÅ‚üß) ‚â´
        ((shiftFunctorComm C m‚ÇÅ m‚ÇÇ).hom.app X)‚ü¶m‚ÇÉ‚üß' ‚â´
        (shiftFunctorComm C m‚ÇÅ m‚ÇÉ).hom.app (X‚ü¶m‚ÇÇ‚üß) := by
  rw [‚Üê cancel_mono ((shiftFunctorComm C m‚ÇÅ m‚ÇÉ).inv.app (X‚ü¶m‚ÇÇ‚üß)),
    ‚Üê cancel_mono (((shiftFunctorComm C m‚ÇÅ m‚ÇÇ).inv.app X)‚ü¶m‚ÇÉ‚üß')]
  simp only [Category.assoc, Iso.hom_inv_id_app]
  dsimp
  simp only [Category.id_comp, ‚Üê Functor.map_comp, Iso.hom_inv_id_app]
  dsimp
  simp only [Functor.map_id, Category.comp_id,
    shiftFunctorComm_eq C _ _ _ rfl, ‚Üê shiftFunctorAdd'_eq_shiftFunctorAdd]
  dsimp
  simp only [Category.assoc, Iso.hom_inv_id_app_assoc, Iso.inv_hom_id_app_assoc,
    ‚Üê Functor.map_comp,
    shiftFunctorAdd'_assoc_hom_app_assoc m‚ÇÇ m‚ÇÉ m‚ÇÅ (m‚ÇÇ + m‚ÇÉ) (m‚ÇÅ + m‚ÇÉ) (m‚ÇÅ + (m‚ÇÇ + m‚ÇÉ)) rfl
      (add_comm m‚ÇÉ m‚ÇÅ) (add_comm _ m‚ÇÅ) X,
    ‚Üê shiftFunctorAdd'_assoc_hom_app_assoc m‚ÇÇ m‚ÇÅ m‚ÇÉ (m‚ÇÅ + m‚ÇÇ) (m‚ÇÅ + m‚ÇÉ)
      (m‚ÇÅ + (m‚ÇÇ + m‚ÇÉ)) (add_comm _ _) rfl (by rw [add_comm m‚ÇÇ m‚ÇÅ, add_assoc]) X,
    shiftFunctorAdd'_assoc_hom_app m‚ÇÅ m‚ÇÇ m‚ÇÉ
      (m‚ÇÅ + m‚ÇÇ) (m‚ÇÇ + m‚ÇÉ) (m‚ÇÅ + (m‚ÇÇ + m‚ÇÉ)) rfl rfl (add_assoc _ _ _) X]

end AddCommMonoid

namespace Functor.FullyFaithful

variable {D : Type*} [Category D] [AddMonoid A] [HasShift D A]
variable {F : C ‚•§ D} (hF : F.FullyFaithful)
variable (s : A ‚Üí C ‚•§ C) (i : ‚àÄ i, s i ‚ãô F ‚âÖ F ‚ãô shiftFunctor D i)

namespace hasShift

/-- auxiliary definition for `FullyFaithful.hasShift` -/
def zero : s 0 ‚âÖ ùü≠ C :=
  (hF.whiskeringRight C).preimageIso ((i 0) ‚â™‚â´ isoWhiskerLeft F (shiftFunctorZero D A) ‚â™‚â´
    rightUnitor _ ‚â™‚â´ (leftUnitor _).symm)

@[simp]
lemma map_zero_hom_app (X : C) :
    F.map ((zero hF s i).hom.app X) =
      (i 0).hom.app X ‚â´ (shiftFunctorZero D A).hom.app (F.obj X) := by
  simp [zero]

@[simp]
lemma map_zero_inv_app (X : C) :
    F.map ((zero hF s i).inv.app X) =
      (shiftFunctorZero D A).inv.app (F.obj X) ‚â´ (i 0).inv.app X := by
  simp [zero]

/-- auxiliary definition for `FullyFaithful.hasShift` -/
def add (a b : A) : s (a + b) ‚âÖ s a ‚ãô s b :=
  (hF.whiskeringRight C).preimageIso (i (a + b) ‚â™‚â´ isoWhiskerLeft _ (shiftFunctorAdd D a b) ‚â™‚â´
      (associator _ _ _).symm ‚â™‚â´ (isoWhiskerRight (i a).symm _) ‚â™‚â´
      associator _ _ _ ‚â™‚â´ (isoWhiskerLeft _ (i b).symm) ‚â™‚â´
      (associator _ _ _).symm)

@[simp]
lemma map_add_hom_app (a b : A) (X : C) :
    F.map ((add hF s i a b).hom.app X) =
      (i (a + b)).hom.app X ‚â´ (shiftFunctorAdd D a b).hom.app (F.obj X) ‚â´
        ((i a).inv.app X)‚ü¶b‚üß' ‚â´ (i b).inv.app ((s a).obj X) := by
  dsimp [add]
  simp

@[simp]
lemma map_add_inv_app (a b : A) (X : C) :
    F.map ((add hF s i a b).inv.app X) =
      (i b).hom.app ((s a).obj X) ‚â´ ((i a).hom.app X)‚ü¶b‚üß' ‚â´
        (shiftFunctorAdd D a b).inv.app (F.obj X) ‚â´ (i (a + b)).inv.app X := by
  dsimp [add]
  simp

end hasShift

open hasShift in
/-- Given a family of endomorphisms of `C` which are intertwined by a fully faithful `F : C ‚•§ D`
with shift functors on `D`, we can promote that family to shift functors on `C`. -/
def hasShift :
    HasShift C A :=
  hasShiftMk C A
    { F := s
      zero := zero hF s i
      add := add hF s i
      assoc_hom_app := fun m‚ÇÅ m‚ÇÇ m‚ÇÉ X => hF.map_injective (by
        have h := shiftFunctorAdd'_assoc_hom_app m‚ÇÅ m‚ÇÇ m‚ÇÉ _ _ (m‚ÇÅ+m‚ÇÇ+m‚ÇÉ) rfl rfl rfl (F.obj X)
        simp only [shiftFunctorAdd'_eq_shiftFunctorAdd] at h
        rw [‚Üê cancel_mono ((i m‚ÇÉ).hom.app ((s m‚ÇÇ).obj ((s m‚ÇÅ).obj X)))]
        simp only [Functor.comp_obj, Functor.map_comp, map_add_hom_app,
          Category.assoc, Iso.inv_hom_id_app_assoc, NatTrans.naturality_assoc, Functor.comp_map,
          Iso.inv_hom_id_app, Category.comp_id]
        erw [(i m‚ÇÉ).hom.naturality]
        rw [Functor.comp_map, map_add_hom_app,
          Functor.map_comp, Functor.map_comp, Iso.inv_hom_id_app_assoc,
          ‚Üê Functor.map_comp_assoc _ ((i (m‚ÇÅ + m‚ÇÇ)).inv.app X), Iso.inv_hom_id_app,
          Functor.map_id, Category.id_comp, reassoc_of% h,
          dcongr_arg (fun a => (i a).hom.app X) (add_assoc m‚ÇÅ m‚ÇÇ m‚ÇÉ)]
        simp [shiftFunctorAdd', eqToHom_map])
      zero_add_hom_app := fun n X => hF.map_injective (by
        have this := dcongr_arg (fun a => (i a).hom.app X) (zero_add n)
        rw [‚Üê cancel_mono ((i n).hom.app ((s 0).obj X)) ]
        simp only [comp_obj, map_add_hom_app, this, shiftFunctorAdd_zero_add_hom_app, id_obj,
          Category.assoc, eqToHom_trans_assoc, eqToHom_refl, Category.id_comp, Iso.inv_hom_id_app,
          Category.comp_id, map_comp, eqToHom_map]
        congr 1
        erw [(i n).hom.naturality]
        simp)
      add_zero_hom_app := fun n X => hF.map_injective (by
        have := dcongr_arg (fun a => (i a).hom.app X) (add_zero n)
        simp [this, ‚Üê NatTrans.naturality_assoc, eqToHom_map,
          shiftFunctorAdd_add_zero_hom_app]) }

end Functor.FullyFaithful

end

end CategoryTheory
