/-
Copyright (c) 2024 Sophie Morel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sophie Morel, JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Shift.CommShift
import Mathlib.CategoryTheory.Adjunction.Mates

/-!
# Adjoints commute with shifts

Given categories `C` and `D` that have shifts by an additive group `A`, functors `F : C â¥¤ D`
and `G : C â¥¤ D`, an adjunction `F âŠ£ G` and a `CommShift` structure on `F`, this file constructs
a `CommShift` structure on `G`. As an easy application, if `E : C â‰Œ D` is an equivalence and
`E.functor` has a `CommShift` structure, we get a `CommShift` structure on `E.inverse`.

The `CommShift` structure on `G` must be compatible with the one on `F` in the following sense
(cf. `Adjunction.CommShift`):
for every `a` in `A`, the natural transformation `adj.unit : ðŸ­ C âŸ¶ G â‹™ F` commutes with
the isomorphism `shiftFunctor C A â‹™ G â‹™ F â‰… G â‹™ F â‹™ shiftFunctor C A` induces by
`F.commShiftIso a` and `G.commShiftIso a`. We actually require a similar condition for
`adj.counit`, but it follows from the one for `adj.unit`.

In order to simplify the construction of the `CommShift` structure on `G`, we first introduce
the compatibility condition on `adj.unit` for a fixed `a` in `A` and for isomorphisms
`eâ‚ : shiftFunctor C a â‹™ F â‰… F â‹™ shiftFunctor D a` and
`eâ‚‚ : shiftFunctor D a â‹™ G â‰… G â‹™ shiftFunctor C a`. We then prove that:
- If `eâ‚` and `eâ‚‚` satusfy this condition, then `eâ‚` uniquely determines `eâ‚‚` and vice versa.
- If `a = 0`, the isomorphisms `Functor.CommShift.isoZero F` and `Functor.CommShift.isoZero G`
satisfy the condition.
- The condition is stable by addition on `A`, if we use `Functor.CommShift.isoAdd` to deduce
commutation isomorphism for `a + b` from such isomorphism from `a` and `b`.
- Given commutation isomorphisms for `F`, our candidate commutation isomorphisms for `G`,
constructed in `Adjunction.RightAdjointCommShift.iso`, satisfy the compatibility condition.

Once we have established all this, the compatibility of the commutation isomorphism for
`F` expressed in `CommShift.zero` and `CommShift.add` immediately implies the similar
statements for the commutation isomorphisms for `G`.

TODO: Construct a `CommShift` structure on `F` from a `CommShift` structure on `G`, using
opposite categories.

-/

namespace CategoryTheory

open Category

namespace Adjunction

variable {C D : Type*} [Category C] [Category D]
  {F : C â¥¤ D} {G : D â¥¤ C} (adj : F âŠ£ G) {A : Type*} [AddMonoid A] [HasShift C A] [HasShift D A]

namespace CommShift

variable {a b : A} (eâ‚ : shiftFunctor C a â‹™ F â‰… F â‹™ shiftFunctor D a)
    (eâ‚' : shiftFunctor C a â‹™ F â‰… F â‹™ shiftFunctor D a)
    (fâ‚ : shiftFunctor C b â‹™ F â‰… F â‹™ shiftFunctor D b)
    (eâ‚‚ : shiftFunctor D a â‹™ G â‰… G â‹™ shiftFunctor C a)
    (eâ‚‚' : shiftFunctor D a â‹™ G â‰… G â‹™ shiftFunctor C a)
    (fâ‚‚ : shiftFunctor D b â‹™ G â‰… G â‹™ shiftFunctor C b)

/-- Given an adjunction `adj : F âŠ£ G`, `a` in `A` and commutation isomorphisms
`eâ‚ : shiftFunctor C a â‹™ F â‰… F â‹™ shiftFunctor D a` and
`eâ‚‚ : shiftFunctor D a â‹™ G â‰… G â‹™ shiftFunctor C a`, this expresses the compatibility of
`eâ‚` and `eâ‚‚` with the unit of the adjunction `adj`.
-/
abbrev CompatibilityUnit :=
  âˆ€ (X : C), (adj.unit.app X)âŸ¦aâŸ§' = adj.unit.app (XâŸ¦aâŸ§) â‰« G.map (eâ‚.hom.app X) â‰« eâ‚‚.hom.app _

/-- Given an adjunction `adj : F âŠ£ G`, `a` in `A` and commutation isomorphisms
`eâ‚ : shiftFunctor C a â‹™ F â‰… F â‹™ shiftFunctor D a` and
`eâ‚‚ : shiftFunctor D a â‹™ G â‰… G â‹™ shiftFunctor C a`, this expresses the compatibility of
`eâ‚` and `eâ‚‚` with the counit of the adjunction `adj`.
-/
abbrev CompatibilityCounit :=
  âˆ€ (Y : D), adj.counit.app (YâŸ¦aâŸ§) = F.map (eâ‚‚.hom.app Y) â‰« eâ‚.hom.app _ â‰« (adj.counit.app Y)âŸ¦aâŸ§'

/-- Given an adjunction `adj : F âŠ£ G`, `a` in `A` and commutation isomorphisms
`eâ‚ : shiftFunctor C a â‹™ F â‰… F â‹™ shiftFunctor D a` and
`eâ‚‚ : shiftFunctor D a â‹™ G â‰… G â‹™ shiftFunctor C a`, compatibility of `eâ‚` and `eâ‚‚` with the
unit of the adjunction `adj` implies compatibility with the counit of `adj`.
-/
lemma compatibilityCounit_of_compatibilityUnit (h : CompatibilityUnit adj eâ‚ eâ‚‚) :
    CompatibilityCounit adj eâ‚ eâ‚‚ := by
  intro Y
  have eq := h (G.obj Y)
  rw [â† cancel_mono (eâ‚‚.inv.app _ â‰« G.map (eâ‚.inv.app _))] at eq
  dsimp at eq âŠ¢
  conv_rhs at eq => slice 3 4; rw [Iso.hom_inv_id_app]
  conv_rhs at eq => erw [id_comp]; rw [â† Functor.map_comp, Iso.hom_inv_id_app];
                    erw [Functor.map_id]; rw [comp_id]
  apply (adj.homEquiv _ _).injective
  rw [adj.homEquiv_unit, adj.homEquiv_unit, G.map_comp, adj.unit_naturality_assoc, â† eq]
  slice_rhs 4 5 => rw [â† Functor.map_comp, â† assoc, Iso.inv_hom_id_app]; erw [id_comp]
  erw [â† eâ‚‚.inv.naturality]; rw [â† assoc _ _ (eâ‚‚.inv.app Y), Functor.comp_map, â† Functor.map_comp]
  simp only [Functor.id_obj, Functor.comp_obj, right_triangle_components, Functor.map_id, id_comp,
    Iso.hom_inv_id_app]

/-- Given an adjunction `adj : F âŠ£ G`, `a` in `A` and commutation isomorphisms
`eâ‚ : shiftFunctor C a â‹™ F â‰… F â‹™ shiftFunctor D a` and
`eâ‚‚ : shiftFunctor D a â‹™ G â‰… G â‹™ shiftFunctor C a`, if `eâ‚` and `eâ‚‚` are compatible with the
unit of the adjunction `adj`, then we get a formula for `eâ‚‚.inv` in terms of `eâ‚`.
-/
lemma CompatibilityUnit_right (h : CompatibilityUnit adj eâ‚ eâ‚‚) (Y : D) :
    eâ‚‚.inv.app Y = adj.unit.app _ â‰« G.map (eâ‚.hom.app _) â‰« G.map ((adj.counit.app _)âŸ¦aâŸ§') := by
  have := h (G.obj Y)
  rw [â† cancel_mono (eâ‚‚.inv.app _), assoc, assoc, Iso.hom_inv_id_app] at this
  erw [comp_id] at this
  rw [â† assoc, â† this, assoc]; erw [â† eâ‚‚.inv.naturality]
  rw [â† cancel_mono (eâ‚‚.hom.app _)]
  simp only [Functor.comp_obj, Iso.inv_hom_id_app, Functor.id_obj, Functor.comp_map, assoc, comp_id,
    â† (shiftFunctor C a).map_comp, right_triangle_components, Functor.map_id]

/-- Given an adjunction `adj : F âŠ£ G`, `a` in `A` and commutation isomorphisms
`eâ‚ : shiftFunctor C a â‹™ F â‰… F â‹™ shiftFunctor D a` and
`eâ‚‚ : shiftFunctor D a â‹™ G â‰… G â‹™ shiftFunctor C a`, if `eâ‚` and `eâ‚‚` are compatible with the
counit of the adjunction `adj`, then we get a formula for `eâ‚.hom` in terms of `eâ‚‚`.
-/
lemma CompatibilityCounit_left (h : CompatibilityCounit adj eâ‚ eâ‚‚) (X : C) :
    eâ‚.hom.app X = F.map ((adj.unit.app X)âŸ¦aâŸ§') â‰« F.map (eâ‚‚.inv.app _) â‰« adj.counit.app _ := by
  have := h (F.obj X)
  rw [â† cancel_epi (F.map (eâ‚‚.inv.app _)), â† assoc, â† F.map_comp, Iso.inv_hom_id_app, F.map_id,
    id_comp] at this
  rw [this]
  erw [eâ‚.hom.naturality_assoc]
  rw [Functor.comp_map, â† Functor.map_comp, left_triangle_components]
  simp only [Functor.comp_obj, Functor.id_obj, Functor.map_id, comp_id]

/-- Given an adjunction `adj : F âŠ£ G`, `a` in `A` and commutation isomorphisms
`eâ‚ : shiftFunctor C a â‹™ F â‰… F â‹™ shiftFunctor D a` and
`eâ‚‚ : shiftFunctor D a â‹™ G â‰… G â‹™ shiftFunctor C a`, if `eâ‚` and `eâ‚‚` are compatible with the
unit of the adjunction `adj`, then `eâ‚` uniquely determines `eâ‚‚`.
-/
lemma CompatibilityUnit_unique_right (h : CompatibilityUnit adj eâ‚ eâ‚‚)
    (h' : CompatibilityUnit adj eâ‚ eâ‚‚') : eâ‚‚ = eâ‚‚' := by
  rw [â† Iso.symm_eq_iff]
  ext _
  rw [Iso.symm_hom, Iso.symm_hom, CompatibilityUnit_right adj eâ‚ eâ‚‚ h,
    CompatibilityUnit_right adj eâ‚ eâ‚‚' h']

/-- Given an adjunction `adj : F âŠ£ G`, `a` in `A` and commutation isomorphisms
`eâ‚ : shiftFunctor C a â‹™ F â‰… F â‹™ shiftFunctor D a` and
`eâ‚‚ : shiftFunctor D a â‹™ G â‰… G â‹™ shiftFunctor C a`, if `eâ‚` and `eâ‚‚` are compatible with the
counit of the adjunction `adj`, then `eâ‚‚` uniquely determines `eâ‚`.
-/
lemma CompatibilityCounit_unique_left (h : CompatibilityCounit adj eâ‚ eâ‚‚)
    (h' : CompatibilityCounit adj eâ‚' eâ‚‚) : eâ‚ = eâ‚' := by
  ext _
  rw [CompatibilityCounit_left adj eâ‚ eâ‚‚ h, CompatibilityCounit_left adj eâ‚' eâ‚‚ h']

/--
The isomorphisms `Functor.CommShift.isoZero F` and `Functor.CommShift.isoZero G` are
compatible with the unit of an adjunction `F âŠ£ G`.
-/
lemma CompatibilityUnit_isoZero : CompatibilityUnit adj (Functor.CommShift.isoZero F A)
    (Functor.CommShift.isoZero G A) := by
  intro _
  simp only [Functor.id_obj, Functor.comp_obj, Functor.CommShift.isoZero_hom_app, Functor.map_comp,
    assoc, unit_naturality_assoc]
  slice_rhs 3 4 => rw [â† G.map_comp, Iso.inv_hom_id_app]
  rw [â† cancel_mono ((shiftFunctorZero C A).hom.app _)]
  simp only [Functor.id_obj, NatTrans.naturality, Functor.id_map, Functor.map_id, id_comp, assoc,
    Iso.inv_hom_id_app, comp_id]

/-- Given an adjunction `adj : F âŠ£ G`, `a, b` in `A` and commutation isomorphisms
between shifts by `a` (resp. `b`) and `F` and `G`, if these commutation isomorphisms are
compatible with the unit of `adj`, then so are the commutation isomorphisms between shifts
by `a + b` and `F` and `G` constructed by `Functor.CommShift.isoAdd`.
-/
lemma CompatibilityUnit_isoAdd (h : CompatibilityUnit adj eâ‚ eâ‚‚)
    (h' : CompatibilityUnit adj fâ‚ fâ‚‚) :
    CompatibilityUnit adj (Functor.CommShift.isoAdd eâ‚ fâ‚) (Functor.CommShift.isoAdd eâ‚‚ fâ‚‚) := by
  intro X
  simp only [Functor.id_obj, Functor.comp_obj, Functor.CommShift.isoAdd_hom_app, Functor.map_comp,
    assoc, unit_naturality_assoc]
  slice_rhs 5 6 => rw [â† G.map_comp, Iso.inv_hom_id_app]
  simp only [Functor.comp_obj, Functor.map_id, id_comp, assoc]
  slice_rhs 4 5 => erw [fâ‚‚.hom.naturality]
  have := h' (XâŸ¦aâŸ§)
  rw [â† cancel_mono (fâ‚‚.inv.app _), assoc, assoc, Iso.hom_inv_id_app] at this
  simp only [Functor.id_obj, Functor.comp_obj, comp_id] at this
  slice_rhs 2 3 => rw [â† this]
  simp only [Functor.comp_obj, assoc, Iso.inv_hom_id_app, comp_id, Functor.comp_map]
  rw [â† cancel_mono ((shiftFunctorAdd C a b).hom.app _), assoc, assoc, assoc, assoc,
    Iso.inv_hom_id_app]; erw [comp_id]
  rw [â† (shiftFunctor C b).map_comp, â† (shiftFunctor C b).map_comp, â† h X]
  simp only [Functor.comp_obj, NatTrans.naturality, Functor.comp_map, Functor.id_obj]

end CommShift

variable (A) [F.CommShift A] [G.CommShift A]

/--
The property for `CommShift` structures on `F` and `G` to be compatible with an
adjunction `F âŠ£ G`.
-/
class CommShift : Prop where
  commShift_unit : NatTrans.CommShift adj.unit A := by infer_instance
  commShift_counit : NatTrans.CommShift adj.counit A := by infer_instance

namespace CommShift

attribute [instance] commShift_unit commShift_counit

/-- Constructor for `Adjunction.CommShift`. -/
lemma mk' (h : NatTrans.CommShift adj.unit A) :
    adj.CommShift A where
  commShift_counit := âŸ¨by
    intro a
    ext _
    simp [Functor.commShiftIso_comp_hom_app]
    refine (compatibilityCounit_of_compatibilityUnit adj (F.commShiftIso a) (G.commShiftIso a)
      (fun X â†¦ ?_) _).symm
    have := h.comm' a
    apply_fun (fun x â†¦ x.app X) at this
    simp only [Functor.comp_obj, Functor.id_obj, NatTrans.comp_app,
      Functor.commShiftIso_id_hom_app, whiskerRight_app, id_comp, whiskerLeft_app,
      Functor.commShiftIso_comp_hom_app] at this
    exact this
  âŸ©

end CommShift

variable {A}

@[reassoc]
lemma shift_unit_app [adj.CommShift A] (a : A) (X : C) :
    (adj.unit.app X)âŸ¦aâŸ§' =
      adj.unit.app (XâŸ¦aâŸ§) â‰«
        G.map ((F.commShiftIso a).hom.app X) â‰«
          (G.commShiftIso a).hom.app (F.obj X) := by
  simpa [Functor.commShiftIso_comp_hom_app] using NatTrans.CommShift.comm_app adj.unit a X

@[reassoc]
lemma shift_counit_app [adj.CommShift A] (a : A) (Y : D) :
    (adj.counit.app Y)âŸ¦aâŸ§' =
      (F.commShiftIso a).inv.app (G.obj Y) â‰« F.map ((G.commShiftIso a).inv.app Y) â‰«
        adj.counit.app (YâŸ¦aâŸ§) := by
  have eq := NatTrans.CommShift.comm_app adj.counit a Y
  simp only [Functor.comp_obj, Functor.id_obj, Functor.commShiftIso_comp_hom_app, assoc,
    Functor.commShiftIso_id_hom_app, comp_id] at eq
  rw [â† eq, â† assoc (F.map ((G.commShiftIso a).inv.app _)), â† F.map_comp, Iso.inv_hom_id_app,
    F.map_id, id_comp, Iso.inv_hom_id_app_assoc]

end Adjunction

namespace Adjunction

variable {C D : Type*} [Category C] [Category D]
  {F : C â¥¤ D} {G : D â¥¤ C} (adj : F âŠ£ G) {A : Type*} [AddGroup A] [HasShift C A] [HasShift D A]

namespace RightAdjointCommShift

variable (a b : A) (h : b + a = 0) [F.CommShift A]

/-- Auxiliary definition for `iso`. -/
noncomputable def iso' : shiftFunctor D a â‹™ G â‰… G â‹™ shiftFunctor C a :=
  (conjugateIsoEquiv (Adjunction.comp adj (shiftEquiv' D b a h).toAdjunction)
  (Adjunction.comp (shiftEquiv' C b a h).toAdjunction adj)).toFun (F.commShiftIso b)

/--
Given an adjunction `F âŠ£ G` and a `CommShift` structure on `F`, these are the candidate
`CommShift.iso a` isomorphisms for a compatible `CommShift` structure on `G`.
-/
noncomputable def iso : shiftFunctor D a â‹™ G â‰… G â‹™ shiftFunctor C a :=
  iso' adj _ _ (neg_add_cancel a)

lemma iso_hom_app (X : D) :
    (iso adj a).hom.app X =
      (shiftFunctorCompIsoId C b a h).inv.app (G.obj ((shiftFunctor D a).obj X)) â‰«
        (adj.unit.app ((shiftFunctor C b).obj (G.obj ((shiftFunctor D a).obj X))))âŸ¦aâŸ§' â‰«
          (G.map ((F.commShiftIso b).hom.app (G.obj ((shiftFunctor D a).obj X))))âŸ¦aâŸ§' â‰«
            (G.map ((shiftFunctor D b).map (adj.counit.app ((shiftFunctor D a).obj X))))âŸ¦aâŸ§' â‰«
              (G.map ((shiftFunctorCompIsoId D a b
                (by rw [â† add_left_inj a, add_assoc, h, zero_add, add_zero])).hom.app X))âŸ¦aâŸ§' := by
  obtain rfl : b = -a := by rw [â† add_left_inj a, h, neg_add_cancel]
  simp only [Functor.comp_obj, iso, iso', shiftEquiv'_functor, shiftEquiv'_inverse,
    Equiv.toFun_as_coe, conjugateIsoEquiv_apply_hom, conjugateEquiv_apply_app, comp_unit_app,
    Functor.id_obj, Equivalence.toAdjunction_unit, Functor.comp_map, comp_counit_app,
    Equivalence.toAdjunction_counit, Functor.map_comp, assoc]
  rfl

lemma iso_inv_app (Y : D) :
    (iso adj a).inv.app Y =
      adj.unit.app ((shiftFunctor C a).obj (G.obj Y)) â‰«
          G.map ((shiftFunctorCompIsoId D b a h).inv.app
              (F.obj ((shiftFunctor C a).obj (G.obj Y)))) â‰«
            G.map ((shiftFunctor D a).map ((shiftFunctor D b).map
                ((F.commShiftIso a).hom.app (G.obj Y)))) â‰«
              G.map ((shiftFunctor D a).map ((shiftFunctorCompIsoId D a b
                  (by rw [eq_neg_of_add_eq_zero_left h, add_neg_cancel])).hom.app
                    (F.obj (G.obj Y)))) â‰«
                G.map ((shiftFunctor D a).map (adj.counit.app Y)) := by
  obtain rfl : b = -a := by rw [â† add_left_inj a, h, neg_add_cancel]
  simp only [Functor.comp_obj, iso, iso', shiftEquiv', Equiv.toFun_as_coe,
    conjugateIsoEquiv_apply_inv, conjugateEquiv_apply_app, comp_unit_app, Functor.id_obj,
    Equivalence.toAdjunction_unit, Equivalence.Equivalence_mk'_unit, Iso.symm_hom, Functor.comp_map,
    comp_counit_app, Equivalence.toAdjunction_counit, Equivalence.Equivalence_mk'_counit,
    Functor.map_shiftFunctorCompIsoId_hom_app, assoc, Functor.map_comp]
  slice_lhs 3 4 => rw [â† Functor.map_comp, â† Functor.map_comp, Iso.inv_hom_id_app]
  simp only [Functor.comp_obj, Functor.map_id, id_comp, assoc]

/--
The commutation isomorphisms of `Adjunction.RightAdjointCommShift.iso` are compatible with
the unit of the adjunction.
-/
lemma iso_CompatibilityUnit (a : A) : CommShift.CompatibilityUnit adj (F.commShiftIso a)
    (iso adj a) := by
  intro _
  rw [â† cancel_mono ((RightAdjointCommShift.iso adj a).inv.app _), assoc, assoc, Iso.hom_inv_id_app]
  dsimp
  rw [comp_id]
  simp only [RightAdjointCommShift.iso_inv_app adj _ _ (neg_add_cancel a), Functor.id_obj,
    Functor.comp_obj]
  apply (adj.homEquiv _ _).symm.injective
  dsimp
  simp only [homEquiv_counit, Functor.map_comp, assoc, counit_naturality, Functor.comp_obj,
    Functor.id_obj, counit_naturality_assoc, left_triangle_components_assoc]
  erw [â† NatTrans.naturality_assoc]
  dsimp
  rw [shift_shiftFunctorCompIsoId_hom_app, Iso.inv_hom_id_app_assoc,
    Functor.commShiftIso_hom_naturality_assoc, â† Functor.map_comp,
    left_triangle_components, Functor.map_id, comp_id]

end RightAdjointCommShift

variable (A)

/--
Given an adjunction `F âŠ£ G` and a `CommShift` structure on `F`, this constructs
the unique compatible `CommShift` structure on `G`.
-/
@[simps]
noncomputable def rightAdjointCommShift [F.CommShift A] : G.CommShift A where
  iso a := RightAdjointCommShift.iso adj a
  zero := by
    refine CommShift.CompatibilityUnit_unique_right adj (F.commShiftIso 0)  _ _
      (RightAdjointCommShift.iso_CompatibilityUnit adj 0) ?_
    rw [F.commShiftIso_zero]
    exact CommShift.CompatibilityUnit_isoZero adj
  add a b := by
    refine CommShift.CompatibilityUnit_unique_right adj (F.commShiftIso (a + b))  _ _
      (RightAdjointCommShift.iso_CompatibilityUnit adj (a + b)) ?_
    rw [F.commShiftIso_add]
    exact CommShift.CompatibilityUnit_isoAdd adj _ _ _ _
      (RightAdjointCommShift.iso_CompatibilityUnit adj a)
      (RightAdjointCommShift.iso_CompatibilityUnit adj b)

lemma commShift_of_leftAdjoint [F.CommShift A] :
    letI := adj.rightAdjointCommShift A
    adj.CommShift A := by
  letI := adj.rightAdjointCommShift A
  apply CommShift.mk'
  refine âŸ¨fun a => ?_âŸ©
  ext X
  dsimp
  simp only [Functor.commShiftIso_id_hom_app, Functor.comp_obj,
    Functor.id_obj, id_comp, Functor.commShiftIso_comp_hom_app]
  exact RightAdjointCommShift.iso_CompatibilityUnit adj a X

end Adjunction

namespace Equivalence

variable {C D : Type*} [Category C] [Category D] (E : C â‰Œ D)

section

variable (A : Type*) [AddMonoid A] [HasShift C A] [HasShift D A]

/--
If `E : C â‰Œ D` is an equivalence, this expresses the compatibility of `CommShift`
structures on `E.functor` and `E.inverse`.
-/
abbrev CommShift [E.functor.CommShift A] [E.inverse.CommShift A] : Prop :=
  E.toAdjunction.CommShift A

namespace CommShift

variable [E.functor.CommShift A] [E.inverse.CommShift A]

instance [E.CommShift A] : NatTrans.CommShift E.unitIso.hom A :=
  inferInstanceAs (NatTrans.CommShift E.toAdjunction.unit A)

instance [E.CommShift A]: NatTrans.CommShift E.counitIso.hom A :=
  inferInstanceAs (NatTrans.CommShift E.toAdjunction.counit A)

instance [h : E.functor.CommShift A] : E.symm.inverse.CommShift A := h
instance [h : E.inverse.CommShift A] : E.symm.functor.CommShift A := h

/-- Constructor for `Equivalence.CommShift`. -/
lemma mk' (h : NatTrans.CommShift E.unitIso.hom A) :
    E.CommShift A where
  commShift_unit := h
  commShift_counit := (Adjunction.CommShift.mk' E.toAdjunction A h).commShift_counit

/-- Constructor for `Equivalence.CommShift`. -/
lemma mk'' (h : NatTrans.CommShift E.counitIso.hom A) :
    E.CommShift A where
  commShift_unit := by
    have h' := NatTrans.CommShift.of_iso_inv E.counitIso A
    have : NatTrans.CommShift E.unitIso.symm.hom A :=
      (Adjunction.CommShift.mk' E.symm.toAdjunction A h').commShift_counit
    exact NatTrans.CommShift.of_iso_inv E.unitIso.symm A
  commShift_counit := h

/--
If `E : C â‰Œ D` is an equivalence and we have compatible `CommShift` structures on `E.functor`
and `E.inverse`, then we also have compatible `CommShift` structures on `E.symm.functor`
and `E.symm.inverse`.
-/
instance [E.functor.CommShift A] [E.inverse.CommShift A] [E.CommShift A] :
    E.symm.CommShift A := mk' _ _
      (by dsimp only [Equivalence.symm, Iso.symm]; exact NatTrans.CommShift.of_iso_inv _ _)

end CommShift

end

variable (A : Type*) [AddGroup A] [HasShift C A] [HasShift D A]

/--
If `E : C â‰Œ D` is an equivalence and we have a `CommShift` structure on `E.functor`,
this constructs the unique compatible `CommShift` structure on `E.inverse`.
-/
noncomputable def commShiftInverse [E.functor.CommShift A] : E.inverse.CommShift A :=
  E.toAdjunction.rightAdjointCommShift A

lemma commShift_of_functor [E.functor.CommShift A] :
    letI := E.commShiftInverse A
    E.CommShift A := by
  letI := E.commShiftInverse A
  exact CommShift.mk' _ _ (E.toAdjunction.commShift_of_leftAdjoint A).commShift_unit

/--
If `E : C â‰Œ D` is an equivalence and we have a `CommShift` structure on `E.inverse`,
this constructs the unique compatible `CommShift` structure on `E.functor`.
-/
noncomputable def commShiftFunctor [E.inverse.CommShift A] : E.functor.CommShift A :=
  E.symm.toAdjunction.rightAdjointCommShift A

lemma commShift_of_inverse [E.inverse.CommShift A] :
    letI := E.commShiftFunctor A
    E.CommShift A := by
  letI := E.commShiftFunctor A
  apply CommShift.mk''
  have : NatTrans.CommShift E.counitIso.symm.hom A :=
    (E.symm.toAdjunction.commShift_of_leftAdjoint A).commShift_unit
  exact NatTrans.CommShift.of_iso_inv E.counitIso.symm A

end Equivalence

end CategoryTheory
