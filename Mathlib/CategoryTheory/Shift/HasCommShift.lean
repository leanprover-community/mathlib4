import Mathlib.CategoryTheory.Shift.Basic

namespace CategoryTheory

open Category

namespace Functor

variable {C D E : Type _} [Category C] [Category D] [Category E]
  (F : C ‚•§ D) (G : D ‚•§ E) (A : Type _) [AddMonoid A]
  [HasShift C A] [HasShift D A] [HasShift E A]

namespace HasCommShift

@[simps!]
noncomputable def iso_zero :
  shiftFunctor C (0 : A) ‚ãô F ‚âÖ F ‚ãô shiftFunctor D (0 : A) :=
  isoWhiskerRight (shiftFunctorZero C A) F ‚â™‚â´ F.leftUnitor ‚â™‚â´
     F.rightUnitor.symm ‚â™‚â´ isoWhiskerLeft F (shiftFunctorZero D A).symm

variable {F A}

@[simps!]
noncomputable def iso_add' {a b c : A} (h : a + b = c)
    (e‚ÇÅ : shiftFunctor C a ‚ãô F ‚âÖ F ‚ãô shiftFunctor D a)
    (e‚ÇÇ : shiftFunctor C b ‚ãô F ‚âÖ F ‚ãô shiftFunctor D b) :
    shiftFunctor C c ‚ãô F ‚âÖ F ‚ãô shiftFunctor D c :=
  isoWhiskerRight (shiftFunctorAdd' C _ _ _ h) F ‚â™‚â´ Functor.associator _ _ _ ‚â™‚â´
    isoWhiskerLeft _ e‚ÇÇ ‚â™‚â´ (Functor.associator _ _ _).symm ‚â™‚â´ isoWhiskerRight e‚ÇÅ _ ‚â™‚â´
      Functor.associator _ _ _ ‚â™‚â´ isoWhiskerLeft _ (shiftFunctorAdd' D _ _ _ h).symm

noncomputable def iso_add {a b : A}
    (e‚ÇÅ : shiftFunctor C a ‚ãô F ‚âÖ F ‚ãô shiftFunctor D a)
    (e‚ÇÇ : shiftFunctor C b ‚ãô F ‚âÖ F ‚ãô shiftFunctor D b) :
    shiftFunctor C (a + b) ‚ãô F ‚âÖ F ‚ãô shiftFunctor D (a + b) :=
  HasCommShift.iso_add' rfl e‚ÇÅ e‚ÇÇ

@[simp]
lemma iso_add_hom_app  {a b : A}
    (e‚ÇÅ : shiftFunctor C a ‚ãô F ‚âÖ F ‚ãô shiftFunctor D a)
    (e‚ÇÇ : shiftFunctor C b ‚ãô F ‚âÖ F ‚ãô shiftFunctor D b) (X : C) :
      (HasCommShift.iso_add e‚ÇÅ e‚ÇÇ).hom.app X =
        F.map ((shiftFunctorAdd C a b).hom.app X) ‚â´ e‚ÇÇ.hom.app ((shiftFunctor C a).obj X) ‚â´
          (shiftFunctor D b).map (e‚ÇÅ.hom.app X) ‚â´ (shiftFunctorAdd D a b).inv.app (F.obj X) := by
  simp only [iso_add, iso_add'_hom_app, shiftFunctorAdd'_eq_shiftFunctorAdd]

@[simp]
lemma iso_add_inv_app  {a b : A}
    (e‚ÇÅ : shiftFunctor C a ‚ãô F ‚âÖ F ‚ãô shiftFunctor D a)
    (e‚ÇÇ : shiftFunctor C b ‚ãô F ‚âÖ F ‚ãô shiftFunctor D b) (X : C) :
      (HasCommShift.iso_add e‚ÇÅ e‚ÇÇ).inv.app X = (shiftFunctorAdd D a b).hom.app (F.obj X) ‚â´
        (shiftFunctor D b).map (e‚ÇÅ.inv.app X) ‚â´ e‚ÇÇ.inv.app ((shiftFunctor C a).obj X) ‚â´
        F.map ((shiftFunctorAdd C a b).inv.app X) := by
  simp only [iso_add, iso_add'_inv_app, shiftFunctorAdd'_eq_shiftFunctorAdd]

end HasCommShift

class HasCommShift where
  iso : ‚àÄ (a : A), shiftFunctor C a ‚ãô F ‚âÖ F ‚ãô shiftFunctor D a
  zero : iso 0 = HasCommShift.iso_zero F A := by aesop_cat
  add : ‚àÄ (a b : A), iso (a + b) = HasCommShift.iso_add (iso a) (iso b) := by aesop_cat

variable {A}

def commShiftIso [F.HasCommShift A] (a : A) :
    shiftFunctor C a ‚ãô F ‚âÖ F ‚ãô shiftFunctor D a :=
  HasCommShift.iso a

@[reassoc (attr := simp)]
lemma commShiftIso_hom_naturality [F.HasCommShift A] {X Y : C} (f : X ‚ü∂ Y) (a : A) :
    F.map (f‚ü¶a‚üß') ‚â´ (F.commShiftIso a).hom.app Y =
      (F.commShiftIso a).hom.app X ‚â´ (F.map f)‚ü¶a‚üß' :=
  (F.commShiftIso a).hom.naturality f

@[reassoc (attr := simp)]
lemma commShiftIso_inv_naturality [F.HasCommShift A] {X Y : C} (f : X ‚ü∂ Y) (a : A) :
    (F.map f)‚ü¶a‚üß' ‚â´ (F.commShiftIso a).inv.app Y =
      (F.commShiftIso a).inv.app X ‚â´ F.map (f‚ü¶a‚üß') :=
  (F.commShiftIso a).inv.naturality f

variable (A)

lemma commShiftIso_zero [F.HasCommShift A] :
  F.commShiftIso (0 : A) = HasCommShift.iso_zero F A :=
  HasCommShift.zero

variable {A}

lemma commShiftIso_add [F.HasCommShift A] (a b : A):
  F.commShiftIso (a + b) = HasCommShift.iso_add (F.commShiftIso a) (F.commShiftIso b) :=
  HasCommShift.add a b

lemma commShiftIso_add' [F.HasCommShift A] {a b c : A} (h : a + b = c) :
  F.commShiftIso c = HasCommShift.iso_add' h (F.commShiftIso a) (F.commShiftIso b) := by
  subst h
  simp only [commShiftIso_add, HasCommShift.iso_add]

namespace HasCommShift

variable (C)

instance id : HasCommShift (ùü≠ C) A where
  iso := fun a => rightUnitor _ ‚â™‚â´ (leftUnitor _).symm

variable {C F G}

variable [F.HasCommShift A] [G.HasCommShift A]

instance comp : (F ‚ãô G).HasCommShift A where
  iso a := (Functor.associator _ _ _).symm ‚â™‚â´ isoWhiskerRight (F.commShiftIso a) _ ‚â™‚â´
    Functor.associator _ _ _ ‚â™‚â´ isoWhiskerLeft _ (G.commShiftIso a) ‚â™‚â´
    (Functor.associator _ _ _).symm
  zero := by
    ext X
    dsimp
    simp only [id_comp, comp_id, commShiftIso_zero, iso_zero_hom_app, ‚Üê Functor.map_comp_assoc,
      assoc, Iso.inv_hom_id_app, id_obj, comp_map, comp_obj]
  add := fun a b => by
    ext X
    dsimp
    simp only [commShiftIso_add, iso_add_hom_app]
    dsimp
    simp only [comp_id, id_comp, assoc, ‚Üê Functor.map_comp_assoc, Iso.inv_hom_id_app, comp_obj]
    simp only [map_comp, assoc, commShiftIso_hom_naturality_assoc]

end HasCommShift

lemma commShiftIso_comp_hom_app [F.HasCommShift A] [G.HasCommShift A] (a : A) (X : C) :
    (commShiftIso (F ‚ãô G) a).hom.app X =
      G.map ((commShiftIso F a).hom.app X) ‚â´ (commShiftIso G a).hom.app (F.obj X) := by
  simp [commShiftIso, HasCommShift.iso]

lemma commShiftIso_comp_inv_app [F.HasCommShift A] [G.HasCommShift A] (a : A) (X : C) :
    (commShiftIso (F ‚ãô G) a).inv.app X =
      (commShiftIso G a).inv.app (F.obj X) ‚â´ G.map ((commShiftIso F a).inv.app X) := by
  simp [commShiftIso, HasCommShift.iso]

end Functor

namespace NatTrans

variable {C D E : Type _} [Category C] [Category D] [Category E]
  {F‚ÇÅ F‚ÇÇ F‚ÇÉ : C ‚•§ D} (œÑ : F‚ÇÅ ‚ü∂ F‚ÇÇ) (œÑ' : F‚ÇÇ ‚ü∂ F‚ÇÉ) (e : F‚ÇÅ ‚âÖ F‚ÇÇ)
    (G G' : D ‚•§ E) (œÑ'' : G ‚ü∂ G')
  (A : Type _) [AddMonoid A] [HasShift C A] [HasShift D A] [HasShift E A]
  [F‚ÇÅ.HasCommShift A] [F‚ÇÇ.HasCommShift A] [F‚ÇÉ.HasCommShift A]
    [G.HasCommShift A] [G'.HasCommShift A]

class CommShift : Prop :=
  comm' : ‚àÄ (a : A), (F‚ÇÅ.commShiftIso a).hom ‚â´ whiskerRight œÑ _ =
    whiskerLeft _ œÑ ‚â´ (F‚ÇÇ.commShiftIso a).hom

namespace CommShift

section

variable {A}
variable [NatTrans.CommShift œÑ A]

lemma comm (a : A) : (F‚ÇÅ.commShiftIso a).hom ‚â´ whiskerRight œÑ _ =
    whiskerLeft _ œÑ ‚â´ (F‚ÇÇ.commShiftIso a).hom := by
  apply comm'

@[reassoc]
lemma comm_app (a : A) (X : C) :
    (F‚ÇÅ.commShiftIso a).hom.app X ‚â´ (œÑ.app X)‚ü¶a‚üß' =
      œÑ.app (X‚ü¶a‚üß) ‚â´ (F‚ÇÇ.commShiftIso a).hom.app X :=
  NatTrans.congr_app (comm œÑ a) X

lemma shift_app (a : A) (X : C) :
    (œÑ.app X)‚ü¶a‚üß' = (F‚ÇÅ.commShiftIso a).inv.app X ‚â´
      œÑ.app (X‚ü¶a‚üß) ‚â´ (F‚ÇÇ.commShiftIso a).hom.app X := by
  rw [‚Üê comm_app, Iso.inv_hom_id_app_assoc]

lemma app_shift (a : A) (X : C) :
    œÑ.app (X‚ü¶a‚üß) = (F‚ÇÅ.commShiftIso a).hom.app X ‚â´ (œÑ.app X)‚ü¶a‚üß' ‚â´
      (F‚ÇÇ.commShiftIso a).inv.app X := by
  erw [comm_app_assoc, Iso.hom_inv_id_app, Category.comp_id]

end

instance of_iso_inv [NatTrans.CommShift e.hom A] :
  NatTrans.CommShift e.inv A := ‚ü®fun a => by
  ext X
  dsimp
  rw [‚Üê cancel_epi (e.hom.app (X‚ü¶a‚üß)), e.hom_inv_id_app_assoc, ‚Üê comm_app_assoc,
    ‚Üê Functor.map_comp, e.hom_inv_id_app, Functor.map_id]
  dsimp
  rw [Category.comp_id]‚ü©

lemma of_isIso [IsIso œÑ] [NatTrans.CommShift œÑ A] :
    NatTrans.CommShift (inv œÑ) A := by
  haveI : NatTrans.CommShift (asIso œÑ).hom A := by
    dsimp
    infer_instance
  change NatTrans.CommShift (asIso œÑ).inv A
  infer_instance

variable (F‚ÇÅ)

instance id : NatTrans.CommShift (ùüô F‚ÇÅ) A := ‚ü®by aesop_cat‚ü©

instance comp [NatTrans.CommShift œÑ A] [NatTrans.CommShift œÑ' A] :
    NatTrans.CommShift (œÑ ‚â´ œÑ') A := ‚ü®fun a => by
  ext X
  simp [comm_app_assoc, comm_app]‚ü©

variable {F‚ÇÅ}

instance whiskerRight [NatTrans.CommShift œÑ A] :
    NatTrans.CommShift (whiskerRight œÑ G) A := ‚ü®fun a => by
  ext X
  simp only [Functor.comp_obj, whiskerRight_twice, comp_app,
    whiskerRight_app, Functor.comp_map, whiskerLeft_app,
    Functor.commShiftIso_comp_hom_app, Category.assoc]
  erw [‚Üê NatTrans.naturality]
  dsimp
  simp only [‚Üê G.map_comp_assoc, comm_app]‚ü©

variable {G G'} (F‚ÇÅ)

instance whiskerLeft [NatTrans.CommShift œÑ'' A] :
    NatTrans.CommShift (whiskerLeft F‚ÇÅ œÑ'') A := ‚ü®fun a => by
  ext X
  simp only [Functor.comp_obj, comp_app, whiskerRight_app, whiskerLeft_app, whiskerLeft_twice,
    Functor.commShiftIso_comp_hom_app, Category.assoc, ‚Üê NatTrans.naturality_assoc, comm_app]‚ü©

end CommShift

end NatTrans

namespace Functor

section hasShiftOfFullyFaithful

variable {C D : Type _} [Category C] [Category D] [AddMonoid A] [HasShift D A]
  (F : C ‚•§ D) [Full F] [Faithful F]
  (s : A ‚Üí C ‚•§ C) (i : ‚àÄ i, s i ‚ãô F ‚âÖ F ‚ãô shiftFunctor D i)

namespace HasCommShift

def of_hasShiftOfFullyFaithful :
    letI := hasShiftOfFullyFaithful F s i; F.HasCommShift A := by
  letI := hasShiftOfFullyFaithful F s i
  exact
  { iso := i
    zero := by
      ext X
      simp only [comp_obj, iso_zero_hom_app, ShiftMkCore.shiftFunctorZero_eq, Iso.symm_hom,
        map_hasShiftOfFullyFaithful_zero_hom_app, id_obj, Category.assoc,
        Iso.hom_inv_id_app, Category.comp_id]
    add := fun a b => by
      ext X
      simp only [comp_obj, iso_add_hom_app, ShiftMkCore.shiftFunctorAdd_eq, Iso.symm_hom,
        map_hasShiftOfFullyFaithful_add_hom_app, Category.assoc, ShiftMkCore.shiftFunctor_eq,
        Iso.inv_hom_id_app_assoc, ‚Üê (shiftFunctor D b).map_comp_assoc, Iso.inv_hom_id_app,
        Functor.map_id, Category.id_comp, Iso.hom_inv_id_app, Category.comp_id] }

end HasCommShift

lemma shiftFunctorIso_of_hasShiftOfFullyFaithful (a : A) :
    letI := hasShiftOfFullyFaithful F s i;
    letI := HasCommShift.of_hasShiftOfFullyFaithful F s i;
    F.commShiftIso a = i a := by
  rfl

end hasShiftOfFullyFaithful

lemma map_shiftFunctorComm {C D : Type _} [Category C] [Category D] {A : Type _} [AddCommMonoid A]
  [HasShift C A] [HasShift D A] (F : C ‚•§ D) [F.HasCommShift A] (X : C) (a b : A) :
    F.map ((shiftFunctorComm C a b).hom.app X) = (F.commShiftIso b).hom.app (X‚ü¶a‚üß) ‚â´
      ((F.commShiftIso a).hom.app X)‚ü¶b‚üß' ‚â´ (shiftFunctorComm D a b).hom.app (F.obj X) ‚â´
      ((F.commShiftIso b).inv.app X)‚ü¶a‚üß' ‚â´ (F.commShiftIso a).inv.app (X‚ü¶b‚üß) := by
  have eq := NatTrans.congr_app (congr_arg Iso.hom (F.commShiftIso_add a b)) X
  simp only [comp_obj, HasCommShift.iso_add_hom_app,
    ‚Üê cancel_epi (F.map ((shiftFunctorAdd C a b).inv.app X)), Category.assoc,
    ‚Üê F.map_comp_assoc, Iso.inv_hom_id_app, F.map_id, Category.id_comp, F.map_comp] at eq
  simp only [shiftFunctorComm_eq D a b _ rfl]
  dsimp
  simp only [Functor.map_comp, shiftFunctorAdd'_eq_shiftFunctorAdd, Category.assoc,
    ‚Üê reassoc_of% eq,
    shiftFunctorComm_eq C a b _ rfl]
  dsimp
  rw [Functor.map_comp]
  congr 1
  simp only [NatTrans.congr_app (congr_arg Iso.hom (F.commShiftIso_add' (add_comm b a))) X,
    HasCommShift.iso_add'_hom_app, Category.assoc, Iso.inv_hom_id_app_assoc,
    ‚Üê Functor.map_comp_assoc, Iso.hom_inv_id_app]
  dsimp
  simp only [Functor.map_id, Category.id_comp, Iso.hom_inv_id_app, comp_obj, Category.comp_id]

@[simp]
lemma map_shiftFunctorCompIsoId_hom_app
    {C D : Type _} [Category C] [Category D] {A : Type _} [AddMonoid A]
    [HasShift C A] [HasShift D A] (F : C ‚•§ D) [F.HasCommShift A] (X : C) (a b : A) (h : a + b = 0) :
    F.map ((shiftFunctorCompIsoId C a b h).hom.app X) =
      (F.commShiftIso b).hom.app (X‚ü¶a‚üß) ‚â´ ((F.commShiftIso a).hom.app X)‚ü¶b‚üß' ‚â´
        (shiftFunctorCompIsoId D a b h).hom.app (F.obj X) := by
  dsimp [shiftFunctorCompIsoId]
  have eq := NatTrans.congr_app (congr_arg Iso.hom (F.commShiftIso_add' h)) X
  simp only [commShiftIso_zero, comp_obj, HasCommShift.iso_zero_hom_app,
    HasCommShift.iso_add'_hom_app] at eq
  rw [‚Üê cancel_epi (F.map ((shiftFunctorAdd' C a b 0 h).hom.app X)), ‚Üê reassoc_of% eq, F.map_comp]
  simp only [Iso.inv_hom_id_app, id_obj, Category.comp_id, ‚Üê F.map_comp_assoc, Iso.hom_inv_id_app,
    F.map_id, Category.id_comp]

@[simp]
lemma map_shiftFunctorCompIsoId_inv_app
    {C D : Type _} [Category C] [Category D] {A : Type _} [AddMonoid A]
    [HasShift C A] [HasShift D A] (F : C ‚•§ D) [F.HasCommShift A] (X : C) (a b : A) (h : a + b = 0) :
    F.map ((shiftFunctorCompIsoId C a b h).inv.app X) =
        (shiftFunctorCompIsoId D a b h).inv.app (F.obj X) ‚â´
      ((F.commShiftIso a).inv.app X)‚ü¶b‚üß' ‚â´
      (F.commShiftIso b).inv.app (X‚ü¶a‚üß) := by
  rw [‚Üê cancel_epi (F.map ((shiftFunctorCompIsoId C a b h).hom.app X)), ‚Üê F.map_comp,
    Iso.hom_inv_id_app, F.map_id, map_shiftFunctorCompIsoId_hom_app]
  simp only [comp_obj, id_obj, Category.assoc, Iso.hom_inv_id_app_assoc,
    ‚Üê Functor.map_comp_assoc, Iso.hom_inv_id_app, Functor.map_id, Category.id_comp]

end Functor

end CategoryTheory
