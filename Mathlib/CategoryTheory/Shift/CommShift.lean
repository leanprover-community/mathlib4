/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/

import Mathlib.CategoryTheory.Shift.Basic

/-!
# Functors which commute with shifts

Let `C` and `D` be two categories equipped with shifts by an additive monoid `A`. In this file,
we define the notion of functor `F : C ‚•§ D` which "commutes" with these shifts. The associated
type class is `[F.CommShift A]`. The data consists of commutation isomorphisms
`F.commShiftIso a : shiftFunctor C a ‚ãô F ‚âÖ F ‚ãô shiftFunctor D a` for all `a : A`
which satisfy a compatibility with the addition and the zero. After this was formalised in Lean,
it was found that this definition is exactly the definition which appears in Jean-Louis
Verdier's thesis (I 1.2.3/1.2.4), although the language is different. (In Verdier's thesis,
the shift is not given by a monoidal functor `Discrete A ‚•§ C ‚•§ C`, but by a fibred
category `C ‚•§ BA`, where `BA` is the category with one object, the endomorphisms of which
identify to `A`. The choice of a cleavage for this fibered category gives the individual
shift functors.)

## References
* [Jean-Louis Verdier, *Des cat√©gories d√©riv√©es des cat√©gories ab√©liennes*][verdier1996]

-/

namespace CategoryTheory

open Category

namespace Functor

variable {C D E : Type*} [Category C] [Category D] [Category E]
  (F : C ‚•§ D) (G : D ‚•§ E) (A B : Type*) [AddMonoid A] [AddCommMonoid B]
  [HasShift C A] [HasShift D A] [HasShift E A]
  [HasShift C B] [HasShift D B]

namespace CommShift

/-- For any functor `F : C ‚•§ D`, this is the obvious isomorphism
`shiftFunctor C (0 : A) ‚ãô F ‚âÖ F ‚ãô shiftFunctor D (0 : A)` deduced from the
isomorphisms `shiftFunctorZero` on both categories `C` and `D`. -/
@[simps!]
noncomputable def isoZero : shiftFunctor C (0 : A) ‚ãô F ‚âÖ F ‚ãô shiftFunctor D (0 : A) :=
  isoWhiskerRight (shiftFunctorZero C A) F ‚â™‚â´ F.leftUnitor ‚â™‚â´
     F.rightUnitor.symm ‚â™‚â´ isoWhiskerLeft F (shiftFunctorZero D A).symm

/-- For any functor `F : C ‚•§ D` and any `a` in `A` such that `a = 0`,
this is the obvious isomorphism `shiftFunctor C a ‚ãô F ‚âÖ F ‚ãô shiftFunctor D a` deduced from the
isomorphisms `shiftFunctorZero'` on both categories `C` and `D`. -/
@[simps!]
noncomputable def isoZero' (a : A) (ha : a = 0) : shiftFunctor C a ‚ãô F ‚âÖ F ‚ãô shiftFunctor D a :=
  isoWhiskerRight (shiftFunctorZero' C a ha) F ‚â™‚â´ F.leftUnitor ‚â™‚â´
     F.rightUnitor.symm ‚â™‚â´ isoWhiskerLeft F (shiftFunctorZero' D a ha).symm

@[simp]
lemma isoZero'_eq_isoZero : isoZero' F A 0 rfl = isoZero F A := by
  ext; simp [isoZero', shiftFunctorZero']

variable {F A}

/-- If a functor `F : C ‚•§ D` is equipped with "commutation isomorphisms" with the
shifts by `a` and `b`, then there is a commutation isomorphism with the shift by `c` when
`a + b = c`. -/
@[simps!]
noncomputable def isoAdd' {a b c : A} (h : a + b = c)
    (e‚ÇÅ : shiftFunctor C a ‚ãô F ‚âÖ F ‚ãô shiftFunctor D a)
    (e‚ÇÇ : shiftFunctor C b ‚ãô F ‚âÖ F ‚ãô shiftFunctor D b) :
    shiftFunctor C c ‚ãô F ‚âÖ F ‚ãô shiftFunctor D c :=
  isoWhiskerRight (shiftFunctorAdd' C _ _ _ h) F ‚â™‚â´ Functor.associator _ _ _ ‚â™‚â´
    isoWhiskerLeft _ e‚ÇÇ ‚â™‚â´ (Functor.associator _ _ _).symm ‚â™‚â´ isoWhiskerRight e‚ÇÅ _ ‚â™‚â´
      Functor.associator _ _ _ ‚â™‚â´ isoWhiskerLeft _ (shiftFunctorAdd' D _ _ _ h).symm

/-- If a functor `F : C ‚•§ D` is equipped with "commutation isomorphisms" with the
shifts by `a` and `b`, then there is a commutation isomorphism with the shift by `a + b`. -/
noncomputable def isoAdd {a b : A}
    (e‚ÇÅ : shiftFunctor C a ‚ãô F ‚âÖ F ‚ãô shiftFunctor D a)
    (e‚ÇÇ : shiftFunctor C b ‚ãô F ‚âÖ F ‚ãô shiftFunctor D b) :
    shiftFunctor C (a + b) ‚ãô F ‚âÖ F ‚ãô shiftFunctor D (a + b) :=
  CommShift.isoAdd' rfl e‚ÇÅ e‚ÇÇ

@[simp]
lemma isoAdd_hom_app {a b : A}
    (e‚ÇÅ : shiftFunctor C a ‚ãô F ‚âÖ F ‚ãô shiftFunctor D a)
    (e‚ÇÇ : shiftFunctor C b ‚ãô F ‚âÖ F ‚ãô shiftFunctor D b) (X : C) :
      (CommShift.isoAdd e‚ÇÅ e‚ÇÇ).hom.app X =
        F.map ((shiftFunctorAdd C a b).hom.app X) ‚â´ e‚ÇÇ.hom.app ((shiftFunctor C a).obj X) ‚â´
          (shiftFunctor D b).map (e‚ÇÅ.hom.app X) ‚â´ (shiftFunctorAdd D a b).inv.app (F.obj X) := by
  simp only [isoAdd, isoAdd'_hom_app, shiftFunctorAdd'_eq_shiftFunctorAdd]

@[simp]
lemma isoAdd_inv_app {a b : A}
    (e‚ÇÅ : shiftFunctor C a ‚ãô F ‚âÖ F ‚ãô shiftFunctor D a)
    (e‚ÇÇ : shiftFunctor C b ‚ãô F ‚âÖ F ‚ãô shiftFunctor D b) (X : C) :
      (CommShift.isoAdd e‚ÇÅ e‚ÇÇ).inv.app X = (shiftFunctorAdd D a b).hom.app (F.obj X) ‚â´
        (shiftFunctor D b).map (e‚ÇÅ.inv.app X) ‚â´ e‚ÇÇ.inv.app ((shiftFunctor C a).obj X) ‚â´
        F.map ((shiftFunctorAdd C a b).inv.app X) := by
  simp only [isoAdd, isoAdd'_inv_app, shiftFunctorAdd'_eq_shiftFunctorAdd]

end CommShift

/-- A functor `F` commutes with the shift by a monoid `A` if it is equipped with
commutation isomorphisms with the shifts by all `a : A`, and these isomorphisms
satisfy coherence properties with respect to `0 : A` and the addition in `A`. -/
class CommShift where
  /-- The commutation isomorphisms for all `a`-shifts this functor is equipped with -/
  iso (a : A) : shiftFunctor C a ‚ãô F ‚âÖ F ‚ãô shiftFunctor D a
  zero : iso 0 = CommShift.isoZero F A := by aesop_cat
  add (a b : A) : iso (a + b) = CommShift.isoAdd (iso a) (iso b) := by aesop_cat

variable {A}

section

variable [F.CommShift A]

/-- If a functor `F` commutes with the shift by `A` (i.e. `[F.CommShift A]`), then
`F.commShiftIso a` is the given isomorphism `shiftFunctor C a ‚ãô F ‚âÖ F ‚ãô shiftFunctor D a`. -/
def commShiftIso (a : A) :
    shiftFunctor C a ‚ãô F ‚âÖ F ‚ãô shiftFunctor D a :=
  CommShift.iso a

-- Note: The following two lemmas are introduced in order to have more proofs work `by simp`.
-- Indeed, `simp only [(F.commShiftIso a).hom.naturality f]` would almost never work because
-- of the compositions of functors which appear in both the source and target of
-- `F.commShiftIso a`. Otherwise, we would be forced to use `erw [NatTrans.naturality]`.

@[reassoc (attr := simp)]
lemma commShiftIso_hom_naturality {X Y : C} (f : X ‚ü∂ Y) (a : A) :
    F.map (f‚ü¶a‚üß') ‚â´ (F.commShiftIso a).hom.app Y =
      (F.commShiftIso a).hom.app X ‚â´ (F.map f)‚ü¶a‚üß' :=
  (F.commShiftIso a).hom.naturality f

@[reassoc (attr := simp)]
lemma commShiftIso_inv_naturality {X Y : C} (f : X ‚ü∂ Y) (a : A) :
    (F.map f)‚ü¶a‚üß' ‚â´ (F.commShiftIso a).inv.app Y =
      (F.commShiftIso a).inv.app X ‚â´ F.map (f‚ü¶a‚üß') :=
  (F.commShiftIso a).inv.naturality f

variable (A)

lemma commShiftIso_zero :
    F.commShiftIso (0 : A) = CommShift.isoZero F A :=
  CommShift.zero

set_option linter.docPrime false in
lemma commShiftIso_zero' (a : A) (h : a = 0) :
    F.commShiftIso a = CommShift.isoZero' F A a h := by
  subst h; rw [CommShift.isoZero'_eq_isoZero, commShiftIso_zero]

variable {A}

lemma commShiftIso_add (a b : A) :
    F.commShiftIso (a + b) = CommShift.isoAdd (F.commShiftIso a) (F.commShiftIso b) :=
  CommShift.add a b

lemma commShiftIso_add' {a b c : A} (h : a + b = c) :
    F.commShiftIso c = CommShift.isoAdd' h (F.commShiftIso a) (F.commShiftIso b) := by
  subst h
  simp only [commShiftIso_add, CommShift.isoAdd]

end

namespace CommShift

variable (C) in
instance id : CommShift (ùü≠ C) A where
  iso := fun _ => rightUnitor _ ‚â™‚â´ (leftUnitor _).symm

instance comp [F.CommShift A] [G.CommShift A] : (F ‚ãô G).CommShift A where
  iso a := (Functor.associator _ _ _).symm ‚â™‚â´ isoWhiskerRight (F.commShiftIso a) _ ‚â™‚â´
    Functor.associator _ _ _ ‚â™‚â´ isoWhiskerLeft _ (G.commShiftIso a) ‚â™‚â´
    (Functor.associator _ _ _).symm
  zero := by
    ext X
    dsimp
    simp only [id_comp, comp_id, commShiftIso_zero, isoZero_hom_app, ‚Üê Functor.map_comp_assoc,
      assoc, Iso.inv_hom_id_app, id_obj, comp_map, comp_obj]
  add := fun a b => by
    ext X
    dsimp
    simp only [commShiftIso_add, isoAdd_hom_app]
    dsimp
    simp only [comp_id, id_comp, assoc, ‚Üê Functor.map_comp_assoc, Iso.inv_hom_id_app, comp_obj]
    simp only [map_comp, assoc, commShiftIso_hom_naturality_assoc]

end CommShift

@[simp]
lemma commShiftIso_id_hom_app (a : A) (X : C) :
    (commShiftIso (ùü≠ C) a).hom.app X = ùüô _ := comp_id _

@[simp]
lemma commShiftIso_id_inv_app (a : A) (X : C) :
    (commShiftIso (ùü≠ C) a).inv.app X = ùüô _ := comp_id _

lemma commShiftIso_comp_hom_app [F.CommShift A] [G.CommShift A] (a : A) (X : C) :
    (commShiftIso (F ‚ãô G) a).hom.app X =
      G.map ((commShiftIso F a).hom.app X) ‚â´ (commShiftIso G a).hom.app (F.obj X) := by
  simp [commShiftIso, CommShift.iso]

lemma commShiftIso_comp_inv_app [F.CommShift A] [G.CommShift A] (a : A) (X : C) :
    (commShiftIso (F ‚ãô G) a).inv.app X =
      (commShiftIso G a).inv.app (F.obj X) ‚â´ G.map ((commShiftIso F a).inv.app X) := by
  simp [commShiftIso, CommShift.iso]

variable {B}

lemma map_shiftFunctorComm_hom_app [F.CommShift B] (X : C) (a b : B) :
    F.map ((shiftFunctorComm C a b).hom.app X) = (F.commShiftIso b).hom.app (X‚ü¶a‚üß) ‚â´
      ((F.commShiftIso a).hom.app X)‚ü¶b‚üß' ‚â´ (shiftFunctorComm D a b).hom.app (F.obj X) ‚â´
      ((F.commShiftIso b).inv.app X)‚ü¶a‚üß' ‚â´ (F.commShiftIso a).inv.app (X‚ü¶b‚üß) := by
  have eq := NatTrans.congr_app (congr_arg Iso.hom (F.commShiftIso_add a b)) X
  simp only [comp_obj, CommShift.isoAdd_hom_app,
    ‚Üê cancel_epi (F.map ((shiftFunctorAdd C a b).inv.app X)),
    ‚Üê F.map_comp_assoc, Iso.inv_hom_id_app, F.map_id, Category.id_comp] at eq
  simp only [shiftFunctorComm_eq D a b _ rfl]
  dsimp
  simp only [shiftFunctorAdd'_eq_shiftFunctorAdd, Category.assoc,
    ‚Üê reassoc_of% eq, shiftFunctorComm_eq C a b _ rfl]
  dsimp
  rw [Functor.map_comp]
  simp only [NatTrans.congr_app (congr_arg Iso.hom (F.commShiftIso_add' (add_comm b a))) X,
    CommShift.isoAdd'_hom_app, Category.assoc, Iso.inv_hom_id_app_assoc,
    ‚Üê Functor.map_comp_assoc, Iso.hom_inv_id_app,
    Functor.map_id, Category.id_comp, comp_obj, Category.comp_id]

@[simp, reassoc]
lemma map_shiftFunctorCompIsoId_hom_app [F.CommShift A] (X : C) (a b : A) (h : a + b = 0) :
    F.map ((shiftFunctorCompIsoId C a b h).hom.app X) =
      (F.commShiftIso b).hom.app (X‚ü¶a‚üß) ‚â´ ((F.commShiftIso a).hom.app X)‚ü¶b‚üß' ‚â´
        (shiftFunctorCompIsoId D a b h).hom.app (F.obj X) := by
  dsimp [shiftFunctorCompIsoId]
  have eq := NatTrans.congr_app (congr_arg Iso.hom (F.commShiftIso_add' h)) X
  simp only [commShiftIso_zero, comp_obj, CommShift.isoZero_hom_app,
    CommShift.isoAdd'_hom_app] at eq
  rw [‚Üê cancel_epi (F.map ((shiftFunctorAdd' C a b 0 h).hom.app X)), ‚Üê reassoc_of% eq, F.map_comp]
  simp only [Iso.inv_hom_id_app, id_obj, Category.comp_id, ‚Üê F.map_comp_assoc, Iso.hom_inv_id_app,
    F.map_id, Category.id_comp]

@[simp, reassoc]
lemma map_shiftFunctorCompIsoId_inv_app [F.CommShift A] (X : C) (a b : A) (h : a + b = 0) :
    F.map ((shiftFunctorCompIsoId C a b h).inv.app X) =
      (shiftFunctorCompIsoId D a b h).inv.app (F.obj X) ‚â´
        ((F.commShiftIso a).inv.app X)‚ü¶b‚üß' ‚â´ (F.commShiftIso b).inv.app (X‚ü¶a‚üß) := by
  rw [‚Üê cancel_epi (F.map ((shiftFunctorCompIsoId C a b h).hom.app X)), ‚Üê F.map_comp,
    Iso.hom_inv_id_app, F.map_id, map_shiftFunctorCompIsoId_hom_app]
  simp only [comp_obj, id_obj, Category.assoc, Iso.hom_inv_id_app_assoc,
    ‚Üê Functor.map_comp_assoc, Iso.hom_inv_id_app, Functor.map_id, Category.id_comp]

end Functor

namespace NatTrans

variable {C D E J : Type*} [Category C] [Category D] [Category E] [Category J]
  {F‚ÇÅ F‚ÇÇ F‚ÇÉ : C ‚•§ D} (œÑ : F‚ÇÅ ‚ü∂ F‚ÇÇ) (œÑ' : F‚ÇÇ ‚ü∂ F‚ÇÉ) (e : F‚ÇÅ ‚âÖ F‚ÇÇ)
    (G G' : D ‚•§ E) (œÑ'' : G ‚ü∂ G') (H : E ‚•§ J)
  (A : Type*) [AddMonoid A] [HasShift C A] [HasShift D A] [HasShift E A] [HasShift J A]
  [F‚ÇÅ.CommShift A] [F‚ÇÇ.CommShift A] [F‚ÇÉ.CommShift A]
    [G.CommShift A] [G'.CommShift A] [H.CommShift A]

/-- If `œÑ : F‚ÇÅ ‚ü∂ F‚ÇÇ` is a natural transformation between two functors
which commute with a shift by an additive monoid `A`, this typeclass
asserts a compatibility of `œÑ` with these shifts. -/
class CommShift : Prop where
  shift_comm (a : A) : (F‚ÇÅ.commShiftIso a).hom ‚â´ Functor.whiskerRight œÑ _ =
    Functor.whiskerLeft _ œÑ ‚â´ (F‚ÇÇ.commShiftIso a).hom := by aesop_cat

section

variable {A} [NatTrans.CommShift œÑ A]

@[reassoc]
lemma shift_comm (a : A) :
    (F‚ÇÅ.commShiftIso a).hom ‚â´ Functor.whiskerRight œÑ _ =
      Functor.whiskerLeft _ œÑ ‚â´ (F‚ÇÇ.commShiftIso a).hom := by
  apply CommShift.shift_comm

@[reassoc]
lemma shift_app_comm (a : A) (X : C) :
    (F‚ÇÅ.commShiftIso a).hom.app X ‚â´ (œÑ.app X)‚ü¶a‚üß' =
      œÑ.app (X‚ü¶a‚üß) ‚â´ (F‚ÇÇ.commShiftIso a).hom.app X :=
  congr_app (shift_comm œÑ a) X

@[reassoc]
lemma shift_app (a : A) (X : C) :
    (œÑ.app X)‚ü¶a‚üß' = (F‚ÇÅ.commShiftIso a).inv.app X ‚â´
      œÑ.app (X‚ü¶a‚üß) ‚â´ (F‚ÇÇ.commShiftIso a).hom.app X := by
  rw [‚Üê shift_app_comm, Iso.inv_hom_id_app_assoc]

@[reassoc]
lemma app_shift (a : A) (X : C) :
    œÑ.app (X‚ü¶a‚üß) = (F‚ÇÅ.commShiftIso a).hom.app X ‚â´ (œÑ.app X)‚ü¶a‚üß' ‚â´
      (F‚ÇÇ.commShiftIso a).inv.app X := by
  simp [shift_app_comm_assoc œÑ a X]

@[deprecated (since := "2024-12-31")] alias CommShift.comm' := shift_comm
@[deprecated (since := "2024-12-31")] alias CommShift.comm := shift_comm
@[deprecated (since := "2024-12-31")] alias CommShift.comm_app := shift_app_comm
@[deprecated (since := "2024-12-31")] alias CommShift.shift_app := shift_app
@[deprecated (since := "2024-12-31")] alias CommShift.app_shift := app_shift

end

namespace CommShift

instance of_iso_inv [NatTrans.CommShift e.hom A] :
    NatTrans.CommShift e.inv A := ‚ü®fun a => by
  ext X
  dsimp
  rw [‚Üê cancel_epi (e.hom.app (X‚ü¶a‚üß)), e.hom_inv_id_app_assoc, ‚Üê shift_app_comm_assoc,
    ‚Üê Functor.map_comp, e.hom_inv_id_app, Functor.map_id, Category.comp_id]‚ü©

lemma of_isIso [IsIso œÑ] [NatTrans.CommShift œÑ A] :
    NatTrans.CommShift (inv œÑ) A := by
  haveI : NatTrans.CommShift (asIso œÑ).hom A := by assumption
  change NatTrans.CommShift (asIso œÑ).inv A
  infer_instance

variable (F‚ÇÅ) in
instance id : NatTrans.CommShift (ùüô F‚ÇÅ) A where

attribute [local simp] Functor.commShiftIso_comp_hom_app
  shift_app_comm shift_app_comm_assoc

instance comp [NatTrans.CommShift œÑ A] [NatTrans.CommShift œÑ' A] :
    NatTrans.CommShift (œÑ ‚â´ œÑ') A where

instance whiskerRight [NatTrans.CommShift œÑ A] :
    NatTrans.CommShift (Functor.whiskerRight œÑ G) A := ‚ü®fun a => by
  ext X
  simp only [Functor.whiskerRight_twice, comp_app, Functor.commShiftIso_comp_hom_app,
    Functor.associator_hom_app, Functor.whiskerRight_app, Functor.comp_map,
    Functor.associator_inv_app, comp_id, id_comp, assoc, ‚Üê Functor.commShiftIso_hom_naturality, ‚Üê
    G.map_comp_assoc, shift_app_comm, Functor.whiskerLeft_app]‚ü©

instance whiskerLeft [NatTrans.CommShift œÑ'' A] :
    NatTrans.CommShift (Functor.whiskerLeft F‚ÇÅ œÑ'') A where

instance associator : CommShift (Functor.associator F‚ÇÅ G H).hom A where

instance leftUnitor : CommShift F‚ÇÅ.leftUnitor.hom A where

instance rightUnitor : CommShift F‚ÇÅ.rightUnitor.hom A where

end CommShift

end NatTrans

namespace Functor

namespace CommShift

variable {C D E : Type*} [Category C] [Category D]
  {F : C ‚•§ D} {G : C ‚•§ D} (e : F ‚âÖ G)
  (A : Type*) [AddMonoid A] [HasShift C A] [HasShift D A]
  [F.CommShift A]

/-- If `e : F ‚âÖ G` is an isomorphism of functors and if `F` commutes with the
shift, then `G` also commutes with the shift. -/
def ofIso : G.CommShift A where
  iso a := isoWhiskerLeft _ e.symm ‚â™‚â´ F.commShiftIso a ‚â™‚â´ isoWhiskerRight e _
  zero := by
    ext X
    simp only [comp_obj, F.commShiftIso_zero A, Iso.trans_hom, isoWhiskerLeft_hom,
      Iso.symm_hom, isoWhiskerRight_hom, NatTrans.comp_app, whiskerLeft_app,
      isoZero_hom_app, whiskerRight_app, assoc]
    erw [‚Üê e.inv.naturality_assoc, ‚Üê NatTrans.naturality,
      e.inv_hom_id_app_assoc]
  add a b := by
    ext X
    simp only [comp_obj, F.commShiftIso_add, Iso.trans_hom, isoWhiskerLeft_hom,
      Iso.symm_hom, isoWhiskerRight_hom, NatTrans.comp_app, whiskerLeft_app,
      isoAdd_hom_app, whiskerRight_app, assoc, map_comp, NatTrans.naturality_assoc,
      NatIso.cancel_natIso_inv_left]
    simp only [‚Üê Functor.map_comp_assoc, e.hom_inv_id_app_assoc]
    simp only [‚Üê NatTrans.naturality, comp_obj, comp_map, map_comp, assoc]

lemma ofIso_compatibility :
    letI := ofIso e A
    NatTrans.CommShift e.hom A := by
  letI := ofIso e A
  refine ‚ü®fun a => ?_‚ü©
  dsimp [commShiftIso, ofIso]
  rw [‚Üê whiskerLeft_comp_assoc, e.hom_inv_id, whiskerLeft_id', id_comp]

end CommShift

end Functor

/--
Assume that we have a diagram of categories
```
C‚ÇÅ ‚•§ D‚ÇÅ
‚Äñ     ‚Äñ
v     v
C‚ÇÇ ‚•§ D‚ÇÇ
‚Äñ     ‚Äñ
v     v
C‚ÇÉ ‚•§ D‚ÇÉ
```
with functors `F‚ÇÅ‚ÇÇ : C‚ÇÅ ‚•§ C‚ÇÇ`, `F‚ÇÇ‚ÇÉ : C‚ÇÇ ‚•§ C‚ÇÉ` and `F‚ÇÅ‚ÇÉ : C‚ÇÅ ‚•§ C‚ÇÉ` on the first
column that are related by a natural transformation `Œ± : F‚ÇÅ‚ÇÉ ‚ü∂ F‚ÇÅ‚ÇÇ ‚ãô F‚ÇÇ‚ÇÉ`
and similarly `Œ≤ : G‚ÇÅ‚ÇÇ ‚ãô G‚ÇÇ‚ÇÉ ‚ü∂ G‚ÇÅ‚ÇÉ` on the second column. Assume that we have
natural transformations
`e‚ÇÅ‚ÇÇ : F‚ÇÅ‚ÇÇ ‚ãô L‚ÇÇ ‚ü∂ L‚ÇÅ ‚ãô G‚ÇÅ‚ÇÇ` (top square), `e‚ÇÇ‚ÇÉ : F‚ÇÇ‚ÇÉ ‚ãô L‚ÇÉ ‚ü∂ L‚ÇÇ ‚ãô G‚ÇÇ‚ÇÉ` (bottom square),
and `e‚ÇÅ‚ÇÉ : F‚ÇÅ‚ÇÉ ‚ãô L‚ÇÉ ‚ü∂ L‚ÇÅ ‚ãô G‚ÇÅ‚ÇÉ` (outer square), where the horizontal functors
are denoted `L‚ÇÅ`, `L‚ÇÇ` and `L‚ÇÉ`. Assume that `e‚ÇÅ‚ÇÉ` is determined by the other
natural transformations `Œ±`, `e‚ÇÇ‚ÇÉ`, `e‚ÇÅ‚ÇÇ` and `Œ≤`. Then, if all these categories
are equipped with a shift by an additive monoid `A`, and all these functors commute with
these shifts, then the natural transformation `e‚ÇÅ‚ÇÉ` of the outer square commutes with the
shift if all `Œ±`, `e‚ÇÇ‚ÇÉ`, `e‚ÇÅ‚ÇÇ` and `Œ≤` do. -/
lemma NatTrans.CommShift.verticalComposition {C‚ÇÅ C‚ÇÇ C‚ÇÉ D‚ÇÅ D‚ÇÇ D‚ÇÉ : Type*}
    [Category C‚ÇÅ] [Category C‚ÇÇ] [Category C‚ÇÉ] [Category D‚ÇÅ] [Category D‚ÇÇ] [Category D‚ÇÉ]
    {F‚ÇÅ‚ÇÇ : C‚ÇÅ ‚•§ C‚ÇÇ} {F‚ÇÇ‚ÇÉ : C‚ÇÇ ‚•§ C‚ÇÉ} {F‚ÇÅ‚ÇÉ : C‚ÇÅ ‚•§ C‚ÇÉ} (Œ± : F‚ÇÅ‚ÇÉ ‚ü∂ F‚ÇÅ‚ÇÇ ‚ãô F‚ÇÇ‚ÇÉ)
    {G‚ÇÅ‚ÇÇ : D‚ÇÅ ‚•§ D‚ÇÇ} {G‚ÇÇ‚ÇÉ : D‚ÇÇ ‚•§ D‚ÇÉ} {G‚ÇÅ‚ÇÉ : D‚ÇÅ ‚•§ D‚ÇÉ} (Œ≤ : G‚ÇÅ‚ÇÇ ‚ãô G‚ÇÇ‚ÇÉ ‚ü∂ G‚ÇÅ‚ÇÉ)
    {L‚ÇÅ : C‚ÇÅ ‚•§ D‚ÇÅ} {L‚ÇÇ : C‚ÇÇ ‚•§ D‚ÇÇ} {L‚ÇÉ : C‚ÇÉ ‚•§ D‚ÇÉ}
    (e‚ÇÅ‚ÇÇ : F‚ÇÅ‚ÇÇ ‚ãô L‚ÇÇ ‚ü∂ L‚ÇÅ ‚ãô G‚ÇÅ‚ÇÇ) (e‚ÇÇ‚ÇÉ : F‚ÇÇ‚ÇÉ ‚ãô L‚ÇÉ ‚ü∂ L‚ÇÇ ‚ãô G‚ÇÇ‚ÇÉ) (e‚ÇÅ‚ÇÉ : F‚ÇÅ‚ÇÉ ‚ãô L‚ÇÉ ‚ü∂ L‚ÇÅ ‚ãô G‚ÇÅ‚ÇÉ)
    (A : Type*) [AddMonoid A] [HasShift C‚ÇÅ A] [HasShift C‚ÇÇ A] [HasShift C‚ÇÉ A]
    [HasShift D‚ÇÅ A] [HasShift D‚ÇÇ A] [HasShift D‚ÇÉ A]
    [F‚ÇÅ‚ÇÇ.CommShift A] [F‚ÇÇ‚ÇÉ.CommShift A] [F‚ÇÅ‚ÇÉ.CommShift A] [CommShift Œ± A]
    [G‚ÇÅ‚ÇÇ.CommShift A] [G‚ÇÇ‚ÇÉ.CommShift A] [G‚ÇÅ‚ÇÉ.CommShift A] [CommShift Œ≤ A]
    [L‚ÇÅ.CommShift A] [L‚ÇÇ.CommShift A] [L‚ÇÉ.CommShift A]
    [CommShift e‚ÇÅ‚ÇÇ A] [CommShift e‚ÇÇ‚ÇÉ A]
    (h‚ÇÅ‚ÇÉ : e‚ÇÅ‚ÇÉ = Functor.whiskerRight Œ± L‚ÇÉ ‚â´ (Functor.associator _ _ _).hom ‚â´
      Functor.whiskerLeft F‚ÇÅ‚ÇÇ e‚ÇÇ‚ÇÉ ‚â´ (Functor.associator _ _ _).inv ‚â´
        Functor.whiskerRight e‚ÇÅ‚ÇÇ G‚ÇÇ‚ÇÉ ‚â´ (Functor.associator _ _ _).hom ‚â´
          Functor.whiskerLeft L‚ÇÅ Œ≤) : CommShift e‚ÇÅ‚ÇÉ A := by
  subst h‚ÇÅ‚ÇÉ
  infer_instance

end CategoryTheory
