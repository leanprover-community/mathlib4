/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import Mathlib.CategoryTheory.Shift.Basic

/-!
# Functors which commute with shifts

Let `C` and `D` be two categories equipped with shifts by an additive monoid `A`. In this file,
we define the notion of functor `F : C â¥¤ D` which "commutes" with these shifts. The associated
type class is `[F.CommShift A]`. The data consists of commutation isomorphisms
`F.commShiftIso a : shiftFunctor C a â‹™ F â‰… F â‹™ shiftFunctor D a` for all `a : A`
which satisfy a compatibility with the addition and the zero. After this was formalised in Lean,
it was found that this definition is exactly the definition which appears in Jean-Louis
Verdier's thesis (I 1.2.3/1.2.4), although the language is different. (In Verdier's thesis,
the shift is not given by a monoidal functor `Discrete A â¥¤ C â¥¤ C`, but by a fibred
category `C â¥¤ BA`, where `BA` is the category with one object, the endomorphisms of which
identify to `A`. The choice of a cleavage for this fibered category gives the individual
shift functors.)

## References
* [Jean-Louis Verdier, *Des catÃ©gories dÃ©rivÃ©es des catÃ©gories abÃ©liennes*][verdier1996]

-/

namespace CategoryTheory

open Category

namespace Functor

variable {C D E : Type*} [Category C] [Category D] [Category E]
  (F : C â¥¤ D) (G : D â¥¤ E) (A : Type*) [AddMonoid A]
  [HasShift C A] [HasShift D A] [HasShift E A]

namespace CommShift

/-- For any functor `F : C â¥¤ D`, this is the obvious isomorphism
`shiftFunctor C (0 : A) â‹™ F â‰… F â‹™ shiftFunctor D (0 : A)` deduced from the
isomorphisms `shiftFunctorZero` on both categories `C` and `D`. -/
@[simps!]
noncomputable def isoZero : shiftFunctor C (0 : A) â‹™ F â‰… F â‹™ shiftFunctor D (0 : A) :=
  isoWhiskerRight (shiftFunctorZero C A) F â‰ªâ‰« F.leftUnitor â‰ªâ‰«
     F.rightUnitor.symm â‰ªâ‰« isoWhiskerLeft F (shiftFunctorZero D A).symm

variable {F A}

/-- If a functor `F : C â¥¤ D` is equipped with "commutation isomorphisms" with the
shifts by `a` and `b`, then there is a commutation isomorphism with the shift by `c` when
`a + b = c`. -/
@[simps!]
noncomputable def isoAdd' {a b c : A} (h : a + b = c)
    (eâ‚ : shiftFunctor C a â‹™ F â‰… F â‹™ shiftFunctor D a)
    (eâ‚‚ : shiftFunctor C b â‹™ F â‰… F â‹™ shiftFunctor D b) :
    shiftFunctor C c â‹™ F â‰… F â‹™ shiftFunctor D c :=
  isoWhiskerRight (shiftFunctorAdd' C _ _ _ h) F â‰ªâ‰« Functor.associator _ _ _ â‰ªâ‰«
    isoWhiskerLeft _ eâ‚‚ â‰ªâ‰« (Functor.associator _ _ _).symm â‰ªâ‰« isoWhiskerRight eâ‚ _ â‰ªâ‰«
      Functor.associator _ _ _ â‰ªâ‰« isoWhiskerLeft _ (shiftFunctorAdd' D _ _ _ h).symm

/-- If a functor `F : C â¥¤ D` is equipped with "commutation isomorphisms" with the
shifts by `a` and `b`, then there is a commutation isomorphism with the shift by `a + b`. -/
noncomputable def isoAdd {a b : A}
    (eâ‚ : shiftFunctor C a â‹™ F â‰… F â‹™ shiftFunctor D a)
    (eâ‚‚ : shiftFunctor C b â‹™ F â‰… F â‹™ shiftFunctor D b) :
    shiftFunctor C (a + b) â‹™ F â‰… F â‹™ shiftFunctor D (a + b) :=
  CommShift.isoAdd' rfl eâ‚ eâ‚‚

@[simp]
lemma isoAdd_hom_app {a b : A}
    (eâ‚ : shiftFunctor C a â‹™ F â‰… F â‹™ shiftFunctor D a)
    (eâ‚‚ : shiftFunctor C b â‹™ F â‰… F â‹™ shiftFunctor D b) (X : C) :
      (CommShift.isoAdd eâ‚ eâ‚‚).hom.app X =
        F.map ((shiftFunctorAdd C a b).hom.app X) â‰« eâ‚‚.hom.app ((shiftFunctor C a).obj X) â‰«
          (shiftFunctor D b).map (eâ‚.hom.app X) â‰« (shiftFunctorAdd D a b).inv.app (F.obj X) := by
  simp only [isoAdd, isoAdd'_hom_app, shiftFunctorAdd'_eq_shiftFunctorAdd]

@[simp]
lemma isoAdd_inv_app {a b : A}
    (eâ‚ : shiftFunctor C a â‹™ F â‰… F â‹™ shiftFunctor D a)
    (eâ‚‚ : shiftFunctor C b â‹™ F â‰… F â‹™ shiftFunctor D b) (X : C) :
      (CommShift.isoAdd eâ‚ eâ‚‚).inv.app X = (shiftFunctorAdd D a b).hom.app (F.obj X) â‰«
        (shiftFunctor D b).map (eâ‚.inv.app X) â‰« eâ‚‚.inv.app ((shiftFunctor C a).obj X) â‰«
        F.map ((shiftFunctorAdd C a b).inv.app X) := by
  simp only [isoAdd, isoAdd'_inv_app, shiftFunctorAdd'_eq_shiftFunctorAdd]

end CommShift

/-- A functor `F` commutes with the shift by a monoid `A` if it is equipped with
commutation isomorphisms with the shifts by all `a : A`, and these isomorphisms
satisfy coherence properties with respect to `0 : A` and the addition in `A`. -/
class CommShift where
  iso (a : A) : shiftFunctor C a â‹™ F â‰… F â‹™ shiftFunctor D a
  zero : iso 0 = CommShift.isoZero F A := by aesop_cat
  add (a b : A) : iso (a + b) = CommShift.isoAdd (iso a) (iso b) := by aesop_cat

variable {A}

section

variable [F.CommShift A]

/-- If a functor `F` commutes with the shift by `A` (i.e. `[F.CommShift A]`), then
`F.commShiftIso a` is the given isomorphism `shiftFunctor C a â‹™ F â‰… F â‹™ shiftFunctor D a`. -/
def commShiftIso (a : A) :
    shiftFunctor C a â‹™ F â‰… F â‹™ shiftFunctor D a :=
  CommShift.iso a

-- Note: The following two lemmas are introduced in order to have more proofs work `by simp`.
-- Indeed, `simp only [(F.commShiftIso a).hom.naturality f]` would almost never work because
-- of the compositions of functors which appear in both the source and target of
-- `F.commShiftIso a`. Otherwise, we would be forced to use `erw [NatTrans.naturality]`.

@[reassoc (attr := simp)]
lemma commShiftIso_hom_naturality {X Y : C} (f : X âŸ¶ Y) (a : A) :
    F.map (fâŸ¦aâŸ§') â‰« (F.commShiftIso a).hom.app Y =
      (F.commShiftIso a).hom.app X â‰« (F.map f)âŸ¦aâŸ§' :=
  (F.commShiftIso a).hom.naturality f

@[reassoc (attr := simp)]
lemma commShiftIso_inv_naturality {X Y : C} (f : X âŸ¶ Y) (a : A) :
    (F.map f)âŸ¦aâŸ§' â‰« (F.commShiftIso a).inv.app Y =
      (F.commShiftIso a).inv.app X â‰« F.map (fâŸ¦aâŸ§') :=
  (F.commShiftIso a).inv.naturality f

variable (A)

lemma commShiftIso_zero :
    F.commShiftIso (0 : A) = CommShift.isoZero F A :=
  CommShift.zero

variable {A}

lemma commShiftIso_add (a b : A) :
    F.commShiftIso (a + b) = CommShift.isoAdd (F.commShiftIso a) (F.commShiftIso b) :=
  CommShift.add a b

lemma commShiftIso_add' {a b c : A} (h : a + b = c) :
    F.commShiftIso c = CommShift.isoAdd' h (F.commShiftIso a) (F.commShiftIso b) := by
  subst h
  simp only [commShiftIso_add, CommShift.isoAdd]

end

namespace CommShift

variable (C)

instance id : CommShift (ðŸ­ C) A where
  iso := fun a => rightUnitor _ â‰ªâ‰« (leftUnitor _).symm

end CommShift

end Functor

end CategoryTheory
