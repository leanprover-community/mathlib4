/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Functor.Currying
public import Mathlib.CategoryTheory.Center.NegOnePow
public import Mathlib.CategoryTheory.Linear.LinearFunctor
public import Mathlib.CategoryTheory.Shift.Twist
public import Mathlib.CategoryTheory.Shift.Pullback
public import Mathlib.CategoryTheory.Shift.CommShiftProd
public import Mathlib.CategoryTheory.Shift.Prod

/-!
# Commutation with shifts of functors in two variables

We introduce a typeclass `Functor.CommShift‚ÇÇInt` for a bifunctor `G : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D`
(with `D` a preadditive category) as the two variable analogue of `Functor.CommShift`.
We require that `G` commutes with the shifts in both variables and that the two
ways to identify `(G.obj (X‚ÇÅ‚ü¶p‚üß)).obj (X‚ÇÇ‚ü¶q‚üß)` to `((G.obj X‚ÇÅ).obj X‚ÇÇ)‚ü¶p + q‚üß`
differ by the sign `(-1) ^ (p + q)`.

This is implemented using a structure `Functor.CommShift‚ÇÇ` which does not depend
on the preadditive structure on `D`: instead of signs, elements in `(CatCenter D)À£`
are used. These elements are part of a `CommShift‚ÇÇSetup` structure which extends
a `TwistShiftData` structure (see the file `Mathlib.CategoryTheory.Shift.Twist`).

## TODO (@joelriou)
* Show that `G : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D` satisfies `Functor.CommShift‚ÇÇInt` iff the uncurried
functor `C‚ÇÅ √ó C‚ÇÇ ‚•§ D` commutes with the shift by `‚Ñ§ √ó ‚Ñ§`, where `C‚ÇÅ √ó C‚ÇÇ` is
equipped with the obvious product shift, and `D` is equipped with
the twisted shift.

-/

@[expose] public section

namespace CategoryTheory

variable {C‚ÇÅ C‚ÇÅ' C‚ÇÇ C‚ÇÇ' D : Type*} [Category* C‚ÇÅ] [Category* C‚ÇÅ']
  [Category* C‚ÇÇ] [Category* C‚ÇÇ'] [Category* D]

variable (D) in
/-- Given a category `D` equipped with a shift by an additive monoid `M`, this
structure `CommShift‚ÇÇSetup D M` allows to define what it means for a bifunctor
`C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D` to commute with shifts by `M` with respect to both variables.
This structure consists of a `TwistShiftData` for the shift by `M √ó M` on `D`
obtained by pulling back the addition map `M √ó M ‚Üí+ M`, with two axioms `z_zero‚ÇÅ`
and `z_zero‚ÇÇ`. We also require an additional data of `Œµ m n : (CatCenter D)À£`
for `m` and `n`: even though this is determined by the `z` field of `TwistShiftData`,
we make it a separate field so as to have control on its definitional properties. -/
structure CommShift‚ÇÇSetup (M : Type*) [AddCommMonoid M] [HasShift D M] extends
    TwistShiftData (PullbackShift D (AddMonoidHom.fst M M + AddMonoidHom.snd _ _)) (M √ó M) where
  z_zero‚ÇÅ (m‚ÇÅ m‚ÇÇ : M) : z (0, m‚ÇÅ) (0, m‚ÇÇ) = 1 := by aesop
  z_zero‚ÇÇ (m‚ÇÅ m‚ÇÇ : M) : z (m‚ÇÅ, 0) (m‚ÇÇ, 0) = 1 := by aesop
  /-- The invertible elements in the center of `D` that are equal
  to `(z (0, n) (m, 0))‚Åª¬π * z (m, 0) (0, n)`. -/
  Œµ (m n : M) : (CatCenter D)À£
  hŒµ (m n : M) : Œµ m n = (z (0, n) (m, 0))‚Åª¬π * z (m, 0) (0, n) := by aesop

/-- The standard setup for the commutation of bifunctors with shifts by `‚Ñ§`. -/
@[simps]
noncomputable def CommShift‚ÇÇSetup.int [Preadditive D] [HasShift D ‚Ñ§]
    [‚àÄ (n : ‚Ñ§), (shiftFunctor D n).Additive] :
    CommShift‚ÇÇSetup D ‚Ñ§ where
  z m n := (-1) ^ (m.1 * n.2)
  assoc _ _ _ := by
    dsimp
    rw [‚Üê zpow_add, ‚Üê zpow_add]
    lia
  commShift _ _ := ‚ü®by cat_disch‚ü©
  Œµ p q := (-1) ^ (p * q)

namespace Functor

/-- A bifunctor `G : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D` commutes with the shifts by `M` if all functors
`G.obj X‚ÇÅ` and `G.flip X‚ÇÇ` are equipped with `Functor.CommShift` structures, in a way
that is natural in `X‚ÇÅ` and `X‚ÇÇ`, and that these isomorphisms commute up to
the multiplication with an element in `(CatCenter D)À£` which is determined by
a `CommShift‚ÇÇSetup D M` structure. (In most cases, one should use the
abbreviation `CommShift‚ÇÇInt`.) -/
class CommShift‚ÇÇ {M : Type*} [AddCommMonoid M] [HasShift C‚ÇÅ M] [HasShift C‚ÇÇ M] [HasShift D M]
    (G : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D) (h : CommShift‚ÇÇSetup D M) where
  commShiftObj (X‚ÇÅ : C‚ÇÅ) : (G.obj X‚ÇÅ).CommShift M := by infer_instance
  commShift_map {X‚ÇÅ Y‚ÇÅ : C‚ÇÅ} (f : X‚ÇÅ ‚ü∂ Y‚ÇÅ) : NatTrans.CommShift (G.map f) M := by infer_instance
  commShiftFlipObj (X‚ÇÇ : C‚ÇÇ) : (G.flip.obj X‚ÇÇ).CommShift M := by infer_instance
  commShift_flip_map {X‚ÇÇ Y‚ÇÇ : C‚ÇÇ} (g : X‚ÇÇ ‚ü∂ Y‚ÇÇ) : NatTrans.CommShift (G.flip.map g) M := by
    infer_instance
  comm (G h) (X‚ÇÅ : C‚ÇÅ) (X‚ÇÇ : C‚ÇÇ) (m n : M) :
    ((G.obj (X‚ÇÅ‚ü¶m‚üß)).commShiftIso n).hom.app X‚ÇÇ ‚â´
      (((G.flip.obj X‚ÇÇ).commShiftIso m).hom.app X‚ÇÅ)‚ü¶n‚üß' =
        ((G.flip.obj (X‚ÇÇ‚ü¶n‚üß)).commShiftIso m).hom.app X‚ÇÅ ‚â´
          (((G.obj X‚ÇÅ).commShiftIso n).hom.app X‚ÇÇ)‚ü¶m‚üß' ‚â´
            (shiftComm ((G.obj X‚ÇÅ).obj X‚ÇÇ) m n).inv ‚â´ (h.Œµ m n).val.app _

/-- This alias for `Functor.CommShift‚ÇÇ.comm` allows to use the dot notation. -/
alias commShift‚ÇÇ_comm := CommShift‚ÇÇ.comm

attribute [reassoc] commShift‚ÇÇ_comm

/-- A bifunctor `G : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D` commutes with the shifts by `‚Ñ§` if all functors
`G.obj X‚ÇÅ` and `G.flip X‚ÇÇ` are equipped with `Functor.CommShift` structures, in a way
that is natural in `X‚ÇÅ` and `X‚ÇÇ`, and that these isomorphisms for the shift by `p`
on the first variable and the shift by `q` on the second variable commute up
to the sign `(-1) ^ (p * q)`. -/
abbrev CommShift‚ÇÇInt [HasShift C‚ÇÅ ‚Ñ§] [HasShift C‚ÇÇ ‚Ñ§] [HasShift D ‚Ñ§] [Preadditive D]
    [‚àÄ (n : ‚Ñ§), (shiftFunctor D n).Additive] (G : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D) : Type _ :=
  G.CommShift‚ÇÇ .int

namespace CommShift‚ÇÇ

attribute [instance] commShiftObj commShiftFlipObj commShift_map commShift_flip_map

instance precomp‚ÇÅ {M : Type*} [AddCommMonoid M] [HasShift C‚ÇÅ M] [HasShift C‚ÇÅ' M]
    [HasShift C‚ÇÇ M] [HasShift D M] (F : C‚ÇÅ' ‚•§ C‚ÇÅ) [F.CommShift M]
    (G : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D) (h : CommShift‚ÇÇSetup D M) [G.CommShift‚ÇÇ h] :
    (F ‚ãô G).CommShift‚ÇÇ h where
  commShiftObj (X‚ÇÅ' : C‚ÇÅ') := inferInstanceAs ((G.obj (F.obj X‚ÇÅ')).CommShift M)
  commShift_map {X‚ÇÅ' Y‚ÇÅ' : C‚ÇÅ'} (f : X‚ÇÅ' ‚ü∂ Y‚ÇÅ') := by dsimp; infer_instance
  commShiftFlipObj (X‚ÇÇ : C‚ÇÇ) := inferInstanceAs ((F ‚ãô G.flip.obj X‚ÇÇ).CommShift M)
  commShift_flip_map {X‚ÇÇ Y‚ÇÇ : C‚ÇÇ} (g : X‚ÇÇ ‚ü∂ Y‚ÇÇ) :=
    inferInstanceAs (NatTrans.CommShift (whiskerLeft F (G.flip.map g)) M)
  comm X‚ÇÅ' X‚ÇÇ m n := by
    have := G.commShift‚ÇÇ_comm h (F.obj X‚ÇÅ') X‚ÇÇ m n
    dsimp [commShiftIso] at this ‚ä¢
    simp only [Category.comp_id, Category.id_comp, map_comp, Category.assoc]
    rw [NatTrans.shift_app (G.map ((F.commShiftIso m).hom.app X‚ÇÅ')) n X‚ÇÇ]
    simp [this]

instance precomp‚ÇÇ {M : Type*} [AddCommMonoid M] [HasShift C‚ÇÅ M] [HasShift C‚ÇÇ' M]
    [HasShift C‚ÇÇ M] [HasShift D M] (F : C‚ÇÇ' ‚•§ C‚ÇÇ) [F.CommShift M]
    (G : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D) (h : CommShift‚ÇÇSetup D M) [G.CommShift‚ÇÇ h] :
    (G ‚ãô (whiskeringLeft C‚ÇÇ' C‚ÇÇ D).obj F).CommShift‚ÇÇ h where
  commShiftObj (X‚ÇÅ : C‚ÇÅ) := inferInstanceAs ((F ‚ãô G.obj X‚ÇÅ).CommShift M)
  commShift_map {X‚ÇÅ Y‚ÇÅ : C‚ÇÅ} (f : X‚ÇÅ ‚ü∂ Y‚ÇÅ) := by dsimp; infer_instance
  commShiftFlipObj (X‚ÇÇ' : C‚ÇÇ') := inferInstanceAs ((G.flip.obj (F.obj X‚ÇÇ')).CommShift M)
  commShift_flip_map {X‚ÇÇ' Y‚ÇÇ' : C‚ÇÇ'} (g : X‚ÇÇ' ‚ü∂ Y‚ÇÇ') :=
    inferInstanceAs (NatTrans.CommShift (G.flip.map (F.map g)) M)
  comm X‚ÇÅ X‚ÇÇ' m n := by
    have := G.commShift‚ÇÇ_comm h X‚ÇÅ (F.obj X‚ÇÇ') m n
    dsimp [commShiftIso] at this ‚ä¢
    simp only [Category.comp_id, Category.id_comp, Category.assoc, map_comp]
    refine ((G.obj _).map _ ‚â´= this).trans ?_
    simp only [‚Üê Category.assoc]; congr 3
    exact (NatTrans.shift_app_comm (G.flip.map ((F.commShiftIso n).hom.app X‚ÇÇ')) m X‚ÇÅ).symm

/- TODO : If `G : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D` and `H : D ‚•§ D'` and commute with shifts,
and we have compatible "setups" on `D` and `D'`, show that `G ‚ãô H` also commutes
with shifts. -/

end CommShift‚ÇÇ

end Functor

namespace NatTrans

section

variable {M : Type*} [AddCommMonoid M] [HasShift C‚ÇÅ M] [HasShift C‚ÇÇ M] [HasShift D M]
  {G‚ÇÅ G‚ÇÇ G‚ÇÉ : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D} (œÑ : G‚ÇÅ ‚ü∂ G‚ÇÇ) (œÑ' : G‚ÇÇ ‚ü∂ G‚ÇÉ) (h : CommShift‚ÇÇSetup D M)
  [G‚ÇÅ.CommShift‚ÇÇ h] [G‚ÇÇ.CommShift‚ÇÇ h] [G‚ÇÉ.CommShift‚ÇÇ h]

/-- If `œÑ : G‚ÇÅ ‚ü∂ G‚ÇÇ` is a natural transformation between two bifunctors
which commute shifts on both variables, this typeclass asserts a compatibility of `œÑ`
with these shifts. -/
class CommShift‚ÇÇ : Prop where
  commShift_app (X‚ÇÅ : C‚ÇÅ) : NatTrans.CommShift (œÑ.app X‚ÇÅ) M := by infer_instance
  commShift_flipApp (X‚ÇÇ : C‚ÇÇ) : NatTrans.CommShift (œÑ.flipApp X‚ÇÇ) M := by infer_instance

namespace CommShift‚ÇÇ

attribute [instance] commShift_app commShift_flipApp

instance : CommShift‚ÇÇ (ùüô G‚ÇÅ) h where
  commShift_app _ := by dsimp; infer_instance
  commShift_flipApp _ := by
    simp only [flipApp, flipFunctor_obj, Functor.map_id, id_app]
    infer_instance

instance [CommShift‚ÇÇ œÑ h] [CommShift‚ÇÇ œÑ' h] : CommShift‚ÇÇ (œÑ ‚â´ œÑ') h where
  commShift_app _ := by dsimp; infer_instance
  commShift_flipApp _ := by
    simp only [flipApp, flipFunctor_obj, Functor.map_comp, comp_app]
    infer_instance

end CommShift‚ÇÇ

end

/-- If `œÑ : G‚ÇÅ ‚ü∂ G‚ÇÇ` is a natural transformation between two bifunctors
which commute shifts on both variables, this typeclass asserts a compatibility of `œÑ`
with these shifts. -/
abbrev CommShift‚ÇÇInt [HasShift C‚ÇÅ ‚Ñ§] [HasShift C‚ÇÇ ‚Ñ§] [HasShift D ‚Ñ§] [Preadditive D]
    [‚àÄ (n : ‚Ñ§), (shiftFunctor D n).Additive]
    {G‚ÇÅ G‚ÇÇ : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D} [G‚ÇÅ.CommShift‚ÇÇInt] [G‚ÇÇ.CommShift‚ÇÇInt] (œÑ : G‚ÇÅ ‚ü∂ G‚ÇÇ) : Prop :=
  NatTrans.CommShift‚ÇÇ œÑ .int

end NatTrans

variable {M : Type*} [AddCommMonoid M] [HasShift C‚ÇÅ M] [HasShift C‚ÇÇ M] [HasShift D M]
variable (h : CommShift‚ÇÇSetup D M)

namespace CommShift‚ÇÇSetup

protected def Category (h : CommShift‚ÇÇSetup D M) := h.toTwistShiftData.Category

instance category : Category h.Category := inferInstanceAs (Category (h.toTwistShiftData.Category))

noncomputable instance hasShift : HasShift h.Category (M √ó M) :=
  inferInstanceAs (HasShift h.toTwistShiftData.Category (M √ó M))

-- variable (G : C‚ÇÅ √ó C‚ÇÇ ‚•§ h.Category) [G.CommShift (M √ó M)]
-- should be essentially equivalent to
-- variable (F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D) [F.CommShift‚ÇÇ h]

noncomputable def shiftIso (m n p : M) (hp : m + n = p) :
    shiftFunctor h.Category (m, n) ‚âÖ shiftFunctor D p :=
  h.toTwistShiftData.shiftIso (m, n) ‚â™‚â´ pullbackShiftIso _ _ _ _ hp.symm

@[reassoc]
lemma shiftFunctor_map (m n p : M) (hp : m + n = p) {X Y : D} (f : X ‚ü∂ Y) :
    (shiftFunctor h.Category (m, n)).map f =
    (h.shiftIso m n p hp).hom.app X ‚â´ (shiftFunctor D p).map f ‚â´
      (h.shiftIso m n p hp).inv.app Y := by
  simp

lemma shiftFunctorZero_inv_app (X : h.Category) :
    (shiftFunctorZero _ (M √ó M)).inv.app X =
      (shiftFunctorZero D M).inv.app X ‚â´ (h.shiftIso 0 0 0 (add_zero 0)).inv.app X :=
  (h.toTwistShiftData.shiftFunctorZero_inv_app X).trans (by
    dsimp [shiftIso]
    rw [pullbackShiftFunctorZero_inv_app, Category.assoc]
    rfl)

attribute [local simp] CatCenter.smul_eq in
lemma shiftFunctorAdd'_inv_app (m‚ÇÅ m‚ÇÇ m‚ÇÉ : M) (hm : m‚ÇÅ + m‚ÇÇ = m‚ÇÉ)
    (n‚ÇÅ n‚ÇÇ n‚ÇÉ : M) (hn : n‚ÇÅ + n‚ÇÇ = n‚ÇÉ)
    (p‚ÇÅ p‚ÇÇ p‚ÇÉ : M) (hp‚ÇÅ : m‚ÇÅ + n‚ÇÅ = p‚ÇÅ) (hp‚ÇÇ : m‚ÇÇ + n‚ÇÇ = p‚ÇÇ) (hp‚ÇÉ : m‚ÇÉ + n‚ÇÉ = p‚ÇÉ)
    (X : h.Category) :
    (shiftFunctorAdd' h.Category (m‚ÇÅ, n‚ÇÅ) (m‚ÇÇ, n‚ÇÇ) (m‚ÇÉ, n‚ÇÉ) (by aesop)).inv.app X =
      (h.shiftIso m‚ÇÇ n‚ÇÇ p‚ÇÇ hp‚ÇÇ).hom.app _ ‚â´
        (shiftFunctor D p‚ÇÇ).map ((h.shiftIso m‚ÇÅ n‚ÇÅ p‚ÇÅ hp‚ÇÅ).hom.app X) ‚â´
        (shiftFunctorAdd' D p‚ÇÅ p‚ÇÇ p‚ÇÉ (by rw [‚Üê hp‚ÇÉ, ‚Üê hp‚ÇÇ, ‚Üê hp‚ÇÅ, ‚Üê hm, ‚Üê hn]; abel)).inv.app X ‚â´
        (h.shiftIso m‚ÇÉ n‚ÇÉ p‚ÇÉ hp‚ÇÉ).inv.app X ‚â´
          (((h.z (m‚ÇÅ, n‚ÇÅ) (m‚ÇÇ, n‚ÇÇ))‚Åª¬π).1).app _ :=
  (h.toTwistShiftData.shiftFunctorAdd'_inv_app (m‚ÇÅ, n‚ÇÅ) (m‚ÇÇ, n‚ÇÇ) (m‚ÇÉ, n‚ÇÉ) (by aesop) X).trans (by
    dsimp [shiftIso]
    rw [pullbackShiftFunctorAdd'_inv_app _ _ _ _ _ _ p‚ÇÅ p‚ÇÇ p‚ÇÉ
      (by aesop) (by aesop) (by aesop)]
    aesop)

section

open Functor

variable (F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D) [F.CommShift‚ÇÇ h]

protected abbrev uncurry : C‚ÇÅ √ó C‚ÇÇ ‚•§ h.Category := uncurry.obj F

namespace commShiftUncurry

noncomputable def iso‚ÇÅ (m‚ÇÅ : M) :
    shiftFunctor (C‚ÇÅ √ó C‚ÇÇ) (m‚ÇÅ, (0 : M)) ‚ãô h.uncurry F ‚âÖ
      h.uncurry F ‚ãô shiftFunctor h.Category (m‚ÇÅ, (0 : M)) :=
  fullyFaithfulCurry.preimageIso (NatIso.ofComponents
    (fun X‚ÇÅ ‚Ü¶ NatIso.ofComponents (fun X‚ÇÇ ‚Ü¶
      (F.obj (X‚ÇÅ‚ü¶m‚ÇÅ‚üß)).mapIso ((shiftFunctorZero C‚ÇÇ M).app X‚ÇÇ) ‚â™‚â´
        ((F.flip.obj X‚ÇÇ).commShiftIso m‚ÇÅ).app X‚ÇÅ ‚â™‚â´
        (h.shiftIso m‚ÇÅ 0 m‚ÇÅ (add_zero m‚ÇÅ)).symm.app _)
      (fun {X‚ÇÇ Y‚ÇÇ} f ‚Ü¶ by
        have := NatTrans.shift_app_comm (F.flip.map f) m‚ÇÅ X‚ÇÅ
        dsimp at this ‚ä¢
        simp only [Functor.map_id, NatTrans.id_app, Category.id_comp,
          Category.assoc, ‚Üê NatTrans.naturality, reassoc_of% this]
        simp [-Functor.map_comp, ‚Üê Functor.map_comp_assoc]))
    (fun {X‚ÇÅ Y‚ÇÅ} f ‚Ü¶ by
      ext X‚ÇÇ
      have := (F.flip.obj X‚ÇÇ).commShiftIso_hom_naturality f m‚ÇÅ
      dsimp at this ‚ä¢
      simp only [Functor.map_id, Category.comp_id, Category.assoc,
        ‚Üê NatTrans.naturality, ‚Üê NatTrans.naturality_assoc, ‚Üê reassoc_of% this]))

@[reassoc]
lemma iso‚ÇÅ_hom_app (X‚ÇÅ : C‚ÇÅ) (X‚ÇÇ : C‚ÇÇ) (m‚ÇÅ : M) :
    (iso‚ÇÅ h F m‚ÇÅ).hom.app (X‚ÇÅ, X‚ÇÇ) =
    (F.obj ((shiftFunctor C‚ÇÅ m‚ÇÅ).obj X‚ÇÅ)).map ((shiftFunctorZero C‚ÇÇ M).hom.app X‚ÇÇ) ‚â´
    ((F.flip.obj X‚ÇÇ).commShiftIso m‚ÇÅ).hom.app X‚ÇÅ ‚â´
      (h.shiftIso m‚ÇÅ 0 m‚ÇÅ (add_zero m‚ÇÅ)).inv.app ((F.obj X‚ÇÅ).obj X‚ÇÇ) := by
  simp [iso‚ÇÅ, fullyFaithfulCurry, Equivalence.fullyFaithfulInverse]

lemma iso‚ÇÅ_zero : iso‚ÇÅ h F 0 = Functor.CommShift.isoZero _ _ := by
  ext ‚ü®X‚ÇÅ, X‚ÇÇ‚ü©
  simp [iso‚ÇÅ_hom_app, shiftFunctorZero_inv_app, Functor.commShiftIso_zero]

lemma iso‚ÇÅ_add (m m' : M) :
    iso‚ÇÅ h F (m + m') =
      Functor.CommShift.isoAdd' (by aesop) (iso‚ÇÅ h F m) (iso‚ÇÅ h F m') := by
  ext ‚ü®X‚ÇÅ, X‚ÇÇ‚ü©
  have := NatTrans.shift_app_comm (F.flip.map ((shiftFunctorZero C‚ÇÇ M).hom.app X‚ÇÇ)) m' (X‚ÇÅ‚ü¶m‚üß)
  dsimp at this
  simp only [shiftFunctor_prod, Functor.comp_obj, Functor.prod_obj, uncurry_obj_obj, iso‚ÇÅ_hom_app,
    Functor.id_obj, Functor.flip_obj_obj, Functor.CommShift.isoAdd'_hom_app, shiftFunctorAdd'_prod,
    NatIso.prod_hom, uncurry_obj_map, NatTrans.prod_app_fst, NatTrans.prod_app_snd,
    Functor.map_comp, Category.assoc]
  rw [‚Üê Functor.map_comp_assoc, ‚Üê Functor.map_comp_assoc, ‚Üê Functor.map_comp_assoc,
    h.shiftFunctor_map_assoc _ _ _ (add_zero m'), Category.assoc, Iso.inv_hom_id_app_assoc,
    h.shiftFunctorAdd'_inv_app _ _ _ rfl _ _ _ (add_zero 0) _ _ (m + m')
    (add_zero m) (add_zero m') (by simp), Iso.inv_hom_id_app_assoc, h.z_zero‚ÇÇ,
    inv_one, Units.val_one, End.one_def,
    Functor.commShiftIso_add' _ rfl, Functor.CommShift.isoAdd'_hom_app]
  simp only [Functor.flip_obj_obj, Functor.flip_obj_map,
    Category.assoc, NatTrans.naturality_assoc, Functor.map_comp_assoc,
    shiftFunctorAdd'_add_zero_hom_app]
  nth_rw 1 [‚Üê Functor.map_comp_assoc]
  nth_rw 3 [‚Üê Functor.map_comp_assoc]
  simp [‚Üê reassoc_of% this]

noncomputable def iso‚ÇÇ (m‚ÇÇ : M) :
    shiftFunctor (C‚ÇÅ √ó C‚ÇÇ) ((0 : M), m‚ÇÇ) ‚ãô h.uncurry F ‚âÖ
      h.uncurry F ‚ãô shiftFunctor h.Category ((0 : M), m‚ÇÇ) :=
  fullyFaithfulCurry.preimageIso (NatIso.ofComponents
    (fun X‚ÇÅ ‚Ü¶ NatIso.ofComponents (fun X‚ÇÇ ‚Ü¶
      (F.mapIso ((shiftFunctorZero C‚ÇÅ M).app X‚ÇÅ)).app (X‚ÇÇ‚ü¶m‚ÇÇ‚üß) ‚â™‚â´
        ((F.obj X‚ÇÅ).commShiftIso m‚ÇÇ).app X‚ÇÇ ‚â™‚â´
        (h.shiftIso 0 m‚ÇÇ m‚ÇÇ (zero_add m‚ÇÇ)).symm.app ((F.obj X‚ÇÅ).obj X‚ÇÇ))
      (fun {X‚ÇÇ Y‚ÇÇ} f ‚Ü¶ by simp))
    (fun {X‚ÇÅ Y‚ÇÅ} f ‚Ü¶ by
      ext X‚ÇÇ
      have := congr_app (F.congr_map ((shiftFunctorZero C‚ÇÅ M).hom.naturality f)) (X‚ÇÇ‚ü¶m‚ÇÇ‚üß)
      simp only [Functor.map_comp] at this
      dsimp at this ‚ä¢
      simp only [Functor.map_id, Category.comp_id, Category.assoc, ‚Üê NatTrans.naturality]
      rw [NatTrans.shift_app_comm_assoc (F.map f) m‚ÇÇ X‚ÇÇ, reassoc_of% this]))

@[reassoc]
lemma iso‚ÇÇ_hom_app (X‚ÇÅ : C‚ÇÅ) (X‚ÇÇ : C‚ÇÇ) (m‚ÇÇ : M) :
    (iso‚ÇÇ h F m‚ÇÇ).hom.app (X‚ÇÅ, X‚ÇÇ) =
      (F.map ((shiftFunctorZero C‚ÇÅ M).hom.app X‚ÇÅ)).app (X‚ÇÇ‚ü¶m‚ÇÇ‚üß) ‚â´
        ((F.obj X‚ÇÅ).commShiftIso m‚ÇÇ).hom.app X‚ÇÇ ‚â´
        (h.shiftIso 0 m‚ÇÇ m‚ÇÇ (zero_add m‚ÇÇ)).inv.app ((F.obj X‚ÇÅ).obj X‚ÇÇ) := by
  simp [iso‚ÇÇ, fullyFaithfulCurry, Equivalence.fullyFaithfulInverse]

lemma iso‚ÇÇ_zero : iso‚ÇÇ h F 0 = Functor.CommShift.isoZero _ _ := by
  ext ‚ü®X‚ÇÅ, X‚ÇÇ‚ü©
  simp [iso‚ÇÇ_hom_app, shiftFunctorZero_inv_app, Functor.commShiftIso_zero]

lemma iso‚ÇÇ_add (m m' : M) :
    iso‚ÇÇ h F (m + m') =
      Functor.CommShift.isoAdd' (by aesop) (iso‚ÇÇ h F m) (iso‚ÇÇ h F m') := by
  ext ‚ü®X‚ÇÅ, X‚ÇÇ‚ü©
  have this := NatTrans.shift_app_comm (F.map ((shiftFunctorZero C‚ÇÅ M).hom.app X‚ÇÅ)) m' (X‚ÇÇ‚ü¶m‚üß)
  dsimp at this
  simp only [shiftFunctor_prod, Functor.comp_obj, Functor.prod_obj, uncurry_obj_obj,
    iso‚ÇÇ_hom_app, Functor.id_obj, Functor.CommShift.isoAdd'_hom_app, shiftFunctorAdd'_prod,
    NatIso.prod_hom, uncurry_obj_map, NatTrans.prod_app_fst, NatTrans.prod_app_snd,
    Functor.map_comp, Category.assoc, NatTrans.naturality_assoc]
  rw [h.shiftFunctor_map_assoc _ _ _ (zero_add m'), Iso.inv_hom_id_app_assoc,
    NatTrans.naturality_assoc, ‚Üê Functor.map_comp_assoc,
    h.shiftFunctorAdd'_inv_app _ _ _ (add_zero 0) _ _ _ rfl _ _ _
    (zero_add m) (zero_add m') (zero_add (m + m')), h.z_zero‚ÇÅ,
    inv_one, Units.val_one, End.one_def,
    Functor.commShiftIso_add' _ rfl, Functor.CommShift.isoAdd'_hom_app,
    shiftFunctorAdd'_add_zero_hom_app, ‚Üê NatTrans.comp_app_assoc, ‚Üê Functor.map_comp,
    Iso.inv_hom_id_app, NatTrans.naturality_assoc, NatTrans.naturality_assoc]
  nth_rw 2 [‚Üê Functor.map_comp_assoc]
  simp [reassoc_of% this]

lemma isoAdd'_iso‚ÇÅ_iso‚ÇÇ (m‚ÇÅ m‚ÇÇ : M) :
    Functor.CommShift.isoAdd' (show _ = (m‚ÇÅ, m‚ÇÇ) by aesop)
        (iso‚ÇÅ h F m‚ÇÅ) (iso‚ÇÇ h F m‚ÇÇ) =
      Functor.CommShift.isoAdd' (by aesop) (iso‚ÇÇ h F m‚ÇÇ) (iso‚ÇÅ h F m‚ÇÅ) := by
  ext ‚ü®X‚ÇÅ, X‚ÇÇ‚ü©
  simp only [shiftFunctor_prod, comp_obj, prod_obj, uncurry_obj_obj, CommShift.isoAdd'_hom_app,
    shiftFunctorAdd'_prod, NatIso.prod_hom, uncurry_obj_map, NatTrans.prod_app_fst,
    shiftFunctorAdd'_add_zero_hom_app, NatTrans.prod_app_snd, shiftFunctorAdd'_zero_add_hom_app,
    id_obj, iso‚ÇÇ_hom_app, iso‚ÇÅ_hom_app, flip_obj_obj, map_comp, Category.assoc,
    NatTrans.naturality_assoc, commShiftIso_hom_naturality_assoc]
  have h‚ÇÅ := Functor.CommShift‚ÇÇ.comm F h X‚ÇÅ X‚ÇÇ m‚ÇÅ m‚ÇÇ
  have h‚ÇÇ := NatTrans.naturality_1 ((F.flip.obj (X‚ÇÇ‚ü¶m‚ÇÇ‚üß)).commShiftIso m‚ÇÅ).hom
    ((shiftFunctorZero C‚ÇÅ M).app X‚ÇÅ)
  dsimp at h‚ÇÅ h‚ÇÇ
  conv_lhs =>
    rw [‚Üê Functor.map_comp_assoc, ‚Üê Functor.map_comp_assoc, ‚Üê Functor.map_comp_assoc,
      Category.assoc, Category.assoc, ‚Üê Functor.map_comp_assoc, Iso.inv_hom_id_app,
      shiftFunctor_map_assoc _ _ _ _ (zero_add _), Iso.inv_hom_id_app_assoc,
      shiftFunctorAdd'_inv_app _ _ _ _ (add_zero m‚ÇÅ) _ _ _ (zero_add m‚ÇÇ) _ _ _
        (add_zero m‚ÇÅ) (zero_add m‚ÇÇ) rfl, Iso.inv_hom_id_app_assoc,
      ‚Üê NatTrans.comp_app_assoc, ‚Üê Functor.map_comp, Iso.inv_hom_id_app]
    simp only [NatTrans.id_app, Functor.id_obj, Functor.map_id, Category.id_comp]
    rw [Functor.map_comp_assoc, reassoc_of% h‚ÇÅ, ‚Üê Functor.map_comp_assoc,
      Iso.inv_hom_id_app, Functor.map_id, Category.id_comp,
      ‚Üê CatCenter.naturality_assoc, ‚Üê CatCenter.naturality_assoc,
      ‚Üê CatCenter.mul_app, h.hŒµ, Units.val_mul, mul_assoc, Units.mul_inv, mul_one,
      shiftFunctorComm_eq _ _ _ _ rfl, Iso.trans_inv, Iso.symm_inv, NatTrans.comp_app,
      Category.assoc, Iso.hom_inv_id_app_assoc, ‚Üê reassoc_of% h‚ÇÇ]
  conv_rhs =>
    rw [‚Üê Functor.map_comp_assoc, Iso.inv_hom_id_app,
      shiftFunctorAdd'_inv_app _ _ _ _ (zero_add m‚ÇÅ) _ _ _ (add_zero m‚ÇÇ) _ _ _
        (zero_add m‚ÇÇ) (add_zero m‚ÇÅ) rfl, NatTrans.naturality_assoc,
      ‚Üê (shiftFunctor D m‚ÇÅ).map_comp_assoc, Iso.inv_hom_id_app]
    simp only [Functor.id_obj, Functor.map_id, Category.id_comp,
      NatTrans.naturality, NatTrans.naturality_assoc, Iso.inv_hom_id_app_assoc]

end commShiftUncurry

open commShiftUncurry in
noncomputable instance commShiftUncurry : (h.uncurry F).CommShift (M √ó M) :=
  Functor.CommShift.mkProd (iso‚ÇÅ h F) (iso‚ÇÇ h F) (iso‚ÇÅ_zero h F) (iso‚ÇÇ_zero h F)
    (iso‚ÇÅ_add h F) (iso‚ÇÇ_add h F) (isoAdd'_iso‚ÇÅ_iso‚ÇÇ h F)

end

section

open Functor

variable (G : C‚ÇÅ √ó C‚ÇÇ ‚•§ h.Category) [G.CommShift (M √ó M)]

protected abbrev curry : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D := curry.obj G

namespace commShift‚ÇÇCurry

noncomputable def iso‚ÇÅ (m : M) :
    curry.obj (Functor.prod (ùü≠ _) (shiftFunctor C‚ÇÇ m) ‚ãô G) ‚âÖ
    curry.obj (G ‚ãô shiftFunctor D m) :=
  curry.mapIso (isoWhiskerRight (NatIso.prod (shiftFunctorZero C‚ÇÅ M).symm (Iso.refl _)) _ ‚â™‚â´
    G.commShiftIso ((0 : M), m) ‚â™‚â´
    isoWhiskerLeft G (h.shiftIso 0 m m (zero_add m)))

@[reassoc]
lemma iso‚ÇÅ_hom_app_app (X‚ÇÅ : C‚ÇÅ) (X‚ÇÇ : C‚ÇÇ) (m : M) :
    ((iso‚ÇÅ h G m).hom.app X‚ÇÅ).app X‚ÇÇ =
      G.map (Prod.mkHom ((shiftFunctorZero C‚ÇÅ M).inv.app X‚ÇÅ) (ùüô _)) ‚â´
        (G.commShiftIso ((0 : M), m)).hom.app (X‚ÇÅ, X‚ÇÇ) ‚â´
          (h.shiftIso 0 m m (zero_add m)).hom.app _ := by
  simp [iso‚ÇÅ, NatTrans.prod]

noncomputable instance (X‚ÇÅ : C‚ÇÅ) :
    ((h.curry G).obj X‚ÇÅ).CommShift M where
  commShiftIso m := (iso‚ÇÅ h G m).app X‚ÇÅ
  commShiftIso_zero := by
    ext X‚ÇÇ
    simp only [curry_obj_obj_obj, Functor.comp_obj, Functor.prod_obj, Functor.id_obj, Iso.app_hom,
      iso‚ÇÅ_hom_app_app, shiftFunctor_prod, Functor.CommShift.isoZero_hom_app, curry_obj_obj_map]
    change _ ‚â´ (G.commShiftIso (0 : M √ó M)).hom.app (X‚ÇÅ, X‚ÇÇ) ‚â´ _ = _
    rw [G.commShiftIso_zero, Functor.CommShift.isoZero_hom_app,
      Category.assoc, shiftFunctorZero_inv_app, Category.assoc, Iso.inv_hom_id_app,
      Category.comp_id, ‚Üê G.map_comp_assoc]
    congr 2
    aesop
  commShiftIso_add m n := by
    ext X‚ÇÇ
    dsimp
    simp only [iso‚ÇÅ_hom_app_app, Functor.CommShift.isoAdd_hom_app,
      G.commShiftIso_add' (show ((0 : M), m) + (0, n) = (0, m + n) by aesop),
      shiftFunctorAdd'_inv_app _ _ _ _ (add_zero 0) _ _ _ rfl _ _ _ (zero_add m)
      (zero_add n) (zero_add (m + n)), Functor.id_obj, Functor.comp_obj, shiftFunctor_prod,
      Functor.CommShift.isoAdd'_hom_app, Functor.prod_obj, shiftFunctorAdd'_prod, NatIso.prod_hom,
      h.z_zero‚ÇÅ, inv_one, Units.val_one, End.one_def, NatTrans.id_app, Category.comp_id,
      NatTrans.naturality_assoc, Category.assoc, Iso.inv_hom_id_app, curry_obj_obj_obj, ‚Üê
      shiftFunctorAdd'_eq_shiftFunctorAdd, curry_obj_obj_map, Iso.app_hom,
      Functor.map_comp]
    rw [‚Üê NatTrans.naturality_1_assoc (G.commShiftIso ((0 : M), n)).hom
      (Iso.prod ((shiftFunctorZero C‚ÇÅ M).symm.app X‚ÇÅ) (Iso.refl (X‚ÇÇ‚ü¶m‚üß)))]
    dsimp
    simp only [‚Üê G.map_comp_assoc, NatTrans.naturality_assoc]
    congr 2
    ext
    ¬∑ simp [shiftFunctorAdd'_zero_add_hom_app, ‚Üê Functor.map_comp]
    ¬∑ simp

end commShift‚ÇÇCurry

open commShift‚ÇÇCurry in
lemma commShift_curry_obj_hom_app (X‚ÇÅ : C‚ÇÅ) (X‚ÇÇ : C‚ÇÇ) (m : M) :
    (((h.curry G).obj X‚ÇÅ).commShiftIso m).hom.app X‚ÇÇ = ((iso‚ÇÅ h G m).app X‚ÇÅ).hom.app X‚ÇÇ := rfl

namespace commShift‚ÇÇCurry

attribute [local simp] commShift_curry_obj_hom_app

instance {X‚ÇÅ Y‚ÇÅ : C‚ÇÅ} (f : X‚ÇÅ ‚ü∂ Y‚ÇÅ) :
    NatTrans.CommShift ((h.curry G).map f) M where
  shift_comm m := by
    ext X‚ÇÇ
    have h‚ÇÅ := (G.commShiftIso ((0 : M), m)).hom.naturality (Prod.mkHom f (ùüô X‚ÇÇ))
    have h‚ÇÇ := (h.shiftIso 0 m m (zero_add m)).hom.naturality (G.map (Prod.mkHom f (ùüô X‚ÇÇ)))
    dsimp at h‚ÇÅ h‚ÇÇ ‚ä¢
    simp only [iso‚ÇÅ_hom_app_app, Functor.id_obj, Functor.comp_obj, shiftFunctor_prod,
      Category.assoc]
    rw [‚Üê h‚ÇÇ, ‚Üê reassoc_of% h‚ÇÅ, ‚Üê G.map_comp_assoc, ‚Üê G.map_comp_assoc]
    congr 2
    ext
    ¬∑ simp [‚Üê NatTrans.naturality]
    ¬∑ simp

noncomputable def iso‚ÇÇ (m : M) :
    curry.obj (Functor.prod (shiftFunctor C‚ÇÅ m) (ùü≠ _)  ‚ãô G) ‚âÖ
    curry.obj (G ‚ãô shiftFunctor D m) :=
  curry.mapIso (isoWhiskerRight (NatIso.prod  (Iso.refl _) (shiftFunctorZero C‚ÇÇ M).symm) _ ‚â™‚â´
    G.commShiftIso (m, (0 : M)) ‚â™‚â´
    isoWhiskerLeft G (h.shiftIso m 0 m (add_zero m)))

@[reassoc]
lemma iso‚ÇÇ_hom_app_app (X‚ÇÅ : C‚ÇÅ) (X‚ÇÇ : C‚ÇÇ) (m : M) :
    ((iso‚ÇÇ h G m).hom.app X‚ÇÅ).app X‚ÇÇ =
      G.map (Prod.mkHom (ùüô _) ((shiftFunctorZero C‚ÇÇ M).inv.app X‚ÇÇ)) ‚â´
        (G.commShiftIso (m, (0 : M))).hom.app (X‚ÇÅ, X‚ÇÇ) ‚â´
          (h.shiftIso m 0 m (add_zero m)).hom.app _ := by
  simp [iso‚ÇÇ, NatTrans.prod]

noncomputable instance (X‚ÇÇ : C‚ÇÇ) : ((h.curry G).flip.obj X‚ÇÇ).CommShift M where
  commShiftIso m := ((flipFunctor _ _ _).mapIso (iso‚ÇÇ h G m)).app X‚ÇÇ
  commShiftIso_zero := by
    ext X‚ÇÅ
    simp only [flipFunctor_obj, Functor.flip_obj_obj, curry_obj_obj_obj, Functor.comp_obj,
      Functor.prod_obj, Functor.id_obj, Iso.app_hom, Functor.mapIso_hom, flipFunctor_map_app_app,
      iso‚ÇÇ_hom_app_app, shiftFunctor_prod, Functor.CommShift.isoZero_hom_app, Functor.flip_obj_map,
      curry_obj_map_app]
    change _ ‚â´ (G.commShiftIso (0 : M √ó M)).hom.app (X‚ÇÅ, X‚ÇÇ) ‚â´ _ = _
    rw [G.commShiftIso_zero, Functor.CommShift.isoZero_hom_app,
      Category.assoc, shiftFunctorZero_inv_app, Category.assoc, Iso.inv_hom_id_app,
      Category.comp_id, ‚Üê G.map_comp_assoc]
    congr 2
    aesop
  commShiftIso_add m n := by
    ext X‚ÇÅ
    simp only [flipFunctor_obj, flip_obj_obj, curry_obj_obj_obj, comp_obj, prod_obj, id_obj,
      Iso.app_hom, mapIso_hom, flipFunctor_map_app_app, iso‚ÇÇ_hom_app_app, shiftFunctor_prod,
      G.commShiftIso_add' (show (m, (0 : M)) + (n, 0) = (m + n, 0) by aesop),
      CommShift.isoAdd'_hom_app, shiftFunctorAdd'_prod, NatIso.prod_hom,
      shiftFunctorAdd'_inv_app _ _ _ _ rfl _ _ _ (zero_add 0) _ _ _ (add_zero m) (add_zero n)
          (add_zero (m + n)),
      h.z_zero‚ÇÇ, inv_one, Units.val_one, End.one_def, NatTrans.id_app, Category.comp_id,
      NatTrans.naturality_assoc, Category.assoc, Iso.inv_hom_id_app, CommShift.isoAdd_hom_app,
      ‚Üê shiftFunctorAdd'_eq_shiftFunctorAdd, flip_obj_map, curry_obj_map_app, map_comp]
    rw [‚Üê NatTrans.naturality_1_assoc (G.commShiftIso (n, (0 : M))).hom
      (Iso.prod (Iso.refl (X‚ÇÅ‚ü¶m‚üß)) ((shiftFunctorZero C‚ÇÇ M).symm.app X‚ÇÇ))]
    dsimp
    simp only [‚Üê G.map_comp_assoc, NatTrans.naturality_assoc]
    congr 2
    ext
    ¬∑ simp
    ¬∑ simp [shiftFunctorAdd'_zero_add_hom_app, ‚Üê Functor.map_comp]

end commShift‚ÇÇCurry

open commShift‚ÇÇCurry in
lemma commShift_curry_flip_obj_hom_app (X‚ÇÅ : C‚ÇÅ) (X‚ÇÇ : C‚ÇÇ) (m : M) :
    (((h.curry G).flip.obj X‚ÇÇ).commShiftIso m).hom.app X‚ÇÅ =
      ((iso‚ÇÇ h G m).hom.app X‚ÇÅ).app X‚ÇÇ := rfl

namespace commShift‚ÇÇCurry

attribute [local simp] commShift_curry_flip_obj_hom_app

instance {X‚ÇÇ Y‚ÇÇ : C‚ÇÇ} (f : X‚ÇÇ ‚ü∂ Y‚ÇÇ) :
    NatTrans.CommShift ((h.curry G).flip.map f) M where
  shift_comm m := by
    ext X‚ÇÅ
    dsimp
    have h‚ÇÅ := (G.commShiftIso (m, (0 : M))).hom.naturality (Prod.mkHom (ùüô X‚ÇÅ) f)
    have h‚ÇÇ := (h.shiftIso m 0 m (add_zero m)).hom.naturality (G.map (Prod.mkHom (ùüô X‚ÇÅ) f))
    simp only [iso‚ÇÇ_hom_app_app, Functor.id_obj, Functor.comp_obj, shiftFunctor_prod,
      Category.assoc]
    rw [‚Üê h‚ÇÇ, ‚Üê reassoc_of% h‚ÇÅ, ‚Üê G.map_comp_assoc, ‚Üê G.map_comp_assoc]
    congr 2
    ext
    ¬∑ simp
    ¬∑ simp [‚Üê NatTrans.naturality]

end commShift‚ÇÇCurry

open commShift‚ÇÇCurry in
noncomputable instance commShift‚ÇÇCurry : (h.curry G).CommShift‚ÇÇ h where
  comm X‚ÇÅ X‚ÇÇ m n := by
    simp only [Functor.comp_obj, curry_obj_obj_obj, Functor.flip_obj_obj,
      commShift_curry_obj_hom_app, Iso.app_hom, iso‚ÇÅ_hom_app_app, Functor.id_obj,
      shiftFunctor_prod, commShift_curry_flip_obj_hom_app, iso‚ÇÇ_hom_app_app,
      Functor.map_comp, Category.assoc, Iso.app_inv]
    trans (G.commShiftIso (m, n)).hom.app (X‚ÇÅ, X‚ÇÇ) ‚â´
      (h.shiftIso m n _ rfl).hom.app _ ‚â´ (shiftFunctorAdd' D m n (m + n) rfl).hom.app _ ‚â´
        (h.z (m, 0) (0, n)).1.app _
    ¬∑ simp only [G.commShiftIso_add' (show (m, 0) + (0, n) = (m, n) by aesop),
        shiftFunctorAdd'_inv_app _ _ _ _ (add_zero m) _ _ _ (zero_add n) _ _ _ (add_zero m)
          (zero_add n) rfl,
        Functor.comp_obj, shiftFunctor_prod, Functor.CommShift.isoAdd'_hom_app,
        Functor.prod_obj, shiftFunctorAdd'_prod, NatIso.prod_hom,
        NatTrans.naturality_assoc, Category.assoc]
      have := NatTrans.naturality_1 (G.commShiftIso ((0 : M), n)).hom
        (Iso.prod (Iso.refl (X‚ÇÅ‚ü¶m‚üß)) ((shiftFunctorZero C‚ÇÇ M).symm.app X‚ÇÇ) )
      dsimp at this
      rw [‚Üê reassoc_of% this, ‚Üê G.map_comp_assoc,
        ‚Üê CatCenter.naturality_assoc, ‚Üê CatCenter.naturality, Category.assoc,
        ‚Üê CatCenter.mul_app, Units.mul_inv, End.one_def,
        Iso.inv_hom_id_app_assoc, Iso.inv_hom_id_app_assoc,
        NatTrans.naturality_assoc]
      dsimp
      rw [Category.comp_id]
      congr 2
      ¬∑ ext
        ¬∑ simp [shiftFunctorAdd'_add_zero_hom_app]
        ¬∑ simp [shiftFunctorAdd'_zero_add_hom_app, ‚Üê Functor.map_comp]
    ¬∑ rw [G.commShiftIso_add' (show (0, n) + (m, 0) = (m, n) by aesop)]
      simp only [Functor.comp_obj, shiftFunctor_prod, Functor.CommShift.isoAdd'_hom_app,
        Functor.prod_obj, shiftFunctorAdd'_prod, NatIso.prod_hom,
        shiftFunctorAdd'_inv_app _ _ _ _ (zero_add m) _ _ _ (add_zero n) _ _ _ (zero_add n)
          (add_zero m) rfl,
        NatTrans.naturality_assoc, Category.assoc, shiftFunctorComm_eq _ _ _ _ rfl, Iso.trans_inv,
        Iso.symm_inv, NatTrans.comp_app]
      have := NatTrans.naturality_1 (G.commShiftIso (m, (0 : M))).hom
        (Iso.prod ((shiftFunctorZero C‚ÇÅ M).symm.app X‚ÇÅ) (Iso.refl (X‚ÇÇ‚ü¶n‚üß)))
      dsimp [Prod.mkHom] at this
      rw [‚Üê reassoc_of% this, ‚Üê G.map_comp_assoc,
        ‚Üê CatCenter.naturality_assoc, ‚Üê CatCenter.naturality_assoc, ‚Üê CatCenter.mul_app,
        ‚Üê Units.val_mul, ‚Üê h.hŒµ, Iso.inv_hom_id_app_assoc,
        NatTrans.naturality_assoc]
      dsimp
      congr 2
      ¬∑ ext
        ¬∑ simp [shiftFunctorAdd'_zero_add_hom_app, ‚Üê Functor.map_comp]
        ¬∑ simp [shiftFunctorAdd'_add_zero_hom_app]

end

section

open Functor

variable (G : C‚ÇÅ √ó C‚ÇÇ ‚•§ h.Category) [G.CommShift (M √ó M)]

abbrev uncurryCurryIso : h.uncurry (h.curry G) ‚âÖ G := currying.counitIso.app G

open commShiftUncurry in
instance : NatTrans.CommShift (h.uncurryCurryIso G).hom (M √ó M) where
  shift_comm := by
    rintro ‚ü®m, n‚ü©
    ext ‚ü®X‚ÇÅ, X‚ÇÇ‚ü©
    conv_lhs => dsimp [Functor.commShiftIso, Functor.CommShift.commShiftIso, commShiftUncurry,
      Functor.CommShift.mkProd]
    simp [iso‚ÇÅ_hom_app, iso‚ÇÇ_hom_app, commShift_curry_obj_hom_app,
      commShift_curry_flip_obj_hom_app, commShift‚ÇÇCurry.iso‚ÇÅ_hom_app_app,
      commShift‚ÇÇCurry.iso‚ÇÇ_hom_app_app,
      G.commShiftIso_add' (show (m, 0) + (0, n) = (m, n) by aesop)]
    simp only [‚Üê Functor.map_comp_assoc]
    congr 4
    ¬∑ aesop
    ¬∑ simp [prod_comp, ‚Üê prod_id]

end

end CommShift‚ÇÇSetup

end CategoryTheory
