/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/

import Mathlib.CategoryTheory.MorphismProperty.Limits
import Mathlib.CategoryTheory.Presentable.Basic
import Mathlib.CategoryTheory.Abelian.GrothendieckCategory.Basic
import Mathlib.CategoryTheory.Abelian.GrothendieckAxioms.Colim
import Mathlib.CategoryTheory.Limits.TypesFiltered
import Mathlib.CategoryTheory.Limits.FunctorCategory.EpiMono
import Mathlib.CategoryTheory.Limits.Connected
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq
import Mathlib.CategoryTheory.Subobject.Lattice
import Mathlib.CategoryTheory.Filtered.Final

/-!
# Morphisms to a colimit in a Grothendieck abelian category

Let `C : Type u` be an abelian category `[Category.{v} C]` which
satisfies `IsGrothendieckAbelian.{w} C`. Then, we may expect
that all objects `X : C` are `Œ∫`-presentable for some regular
cardinal `Œ∫`. However, we prove only a weaker result (which
should be enough in order to obtain the existence of enough
injectives): there is a regular cardinal `Œ∫` such that
if `Y : J ‚•§ C` is a functor from a `Œ∫`-filtered
category, and `c : Cocone Y` is a colimit cocone,
then the map from the colimit of `X ‚ü∂ Y j` to `X ‚ü∂ c.pt`
is injective, and bijective under the additional
assumption that for any map `f : j ‚ü∂ j'` in `J`, `Y.map f`
is a monomorphism.

-/

universe w v' v u' u

namespace CategoryTheory

open Limits Opposite

attribute [local instance] IsFiltered.isConnected

namespace HasExactColimitsOfShape

variable {C : Type u} [Category.{v} C] [Abelian C]
    {J : Type u'} [Category.{v'} J]

section

variable [HasColimitsOfShape J C] [HasExactColimitsOfShape J C]
    {S : ShortComplex (J ‚•§ C)} (hS : S.Exact)
    {c‚ÇÅ : Cocone S.X‚ÇÅ} (hc‚ÇÅ : IsColimit c‚ÇÅ) {c‚ÇÇ : Cocone S.X‚ÇÇ} (hc‚ÇÇ : IsColimit c‚ÇÇ)
    {c‚ÇÉ : Cocone S.X‚ÇÉ} (hc‚ÇÉ : IsColimit c‚ÇÉ)
    (f : c‚ÇÅ.pt ‚ü∂ c‚ÇÇ.pt) (g : c‚ÇÇ.pt ‚ü∂ c‚ÇÉ.pt)
    (hf : ‚àÄ j, c‚ÇÅ.Œπ.app j ‚â´ f = S.f.app j ‚â´ c‚ÇÇ.Œπ.app j)
    (hg : ‚àÄ j, c‚ÇÇ.Œπ.app j ‚â´ g = S.g.app j ‚â´ c‚ÇÉ.Œπ.app j)

variable (S c‚ÇÅ c‚ÇÇ c‚ÇÉ) in
@[simps]
def mapShortComplex : ShortComplex C :=
  ShortComplex.mk f g (hc‚ÇÅ.hom_ext (fun j ‚Ü¶ by
    dsimp
    rw [reassoc_of% (hf j), hg j, comp_zero, ‚Üê NatTrans.comp_app_assoc, S.zero,
      zero_app, zero_comp]))

include hc‚ÇÇ hc‚ÇÉ hS in
lemma mapShortComplex_exact : (mapShortComplex S c‚ÇÅ hc‚ÇÅ c‚ÇÇ c‚ÇÉ f g hf hg).Exact := by
  refine (ShortComplex.exact_iff_of_iso ?_).2 (hS.map colim)
  refine ShortComplex.isoMk
    (IsColimit.coconePointUniqueUpToIso hc‚ÇÅ (colimit.isColimit _))
    (IsColimit.coconePointUniqueUpToIso hc‚ÇÇ (colimit.isColimit _))
    (IsColimit.coconePointUniqueUpToIso hc‚ÇÉ (colimit.isColimit _))
    (hc‚ÇÅ.hom_ext (fun j ‚Ü¶ ?_)) (hc‚ÇÇ.hom_ext (fun j ‚Ü¶ ?_))
  ¬∑ dsimp
    rw [IsColimit.comp_coconePointUniqueUpToIso_hom_assoc,
      colimit.cocone_Œπ, Œπ_colimMap, reassoc_of% (hf j),
      IsColimit.comp_coconePointUniqueUpToIso_hom, colimit.cocone_Œπ]
  ¬∑ dsimp
    rw [IsColimit.comp_coconePointUniqueUpToIso_hom_assoc,
      colimit.cocone_Œπ, Œπ_colimMap, reassoc_of% (hg j),
      IsColimit.comp_coconePointUniqueUpToIso_hom, colimit.cocone_Œπ]

end

end HasExactColimitsOfShape

namespace MonoOver

variable {C : Type u} [Category.{v} C] {X : C}

instance mono_obj_hom (S : MonoOver X) :
    Mono S.obj.hom := S.2

lemma subobject_mk_le_mk_of_hom {S T : MonoOver X} (f : S ‚ü∂ T) :
    Subobject.mk S.obj.hom ‚â§ Subobject.mk T.obj.hom :=
  Subobject.mk_le_mk_of_comm f.left (by simp)

end MonoOver

namespace Subobject

variable {C : Type u} [Category.{v} C] {X Y : C} (f : X ‚ü∂ Y) [Mono f]

lemma epi_iff_mk_eq_top [Balanced C] :
    Epi f ‚Üî Subobject.mk f = ‚ä§ := by
  rw [‚Üê isIso_iff_mk_eq_top]
  exact ‚ü®fun _ ‚Ü¶ isIso_of_mono_of_epi f, fun _ ‚Ü¶ inferInstance‚ü©

end Subobject

namespace IsGrothendieckAbelian

variable {C : Type u} [Category.{v} C] [Abelian C] [IsGrothendieckAbelian.{w} C]
  {X : C} {J : Type w} [SmallCategory J] (F : J ‚•§ MonoOver X)

section

variable [IsFiltered J] {c : Cocone (F ‚ãô MonoOver.forget _ ‚ãô Over.forget _)}
  (hc : IsColimit c) (f : c.pt ‚ü∂ X) (hf : ‚àÄ (j : J), c.Œπ.app j ‚â´ f = (F.obj j).obj.hom)

include hc hf
lemma mono_of_isColimit_monoOver : Mono f := by
  let Œ± : F ‚ãô MonoOver.forget _ ‚ãô Over.forget _ ‚ü∂ (Functor.const _).obj X :=
    { app j := (F.obj j).obj.hom
      naturality _ _ f := (F.map f).w }
  have := NatTrans.mono_of_mono_app Œ±
  exact colim.map_mono' (œÜ := Œ±) (hc‚ÇÅ := hc)
    (hc‚ÇÇ := isColimitConstCocone J X) f (by simpa using hf)

lemma subobject_mk_of_isColimit_eq_iSup :
    haveI := mono_of_isColimit_monoOver F hc f hf
    Subobject.mk f = ‚®Ü j, Subobject.mk (F.obj j).obj.hom := by
  haveI := mono_of_isColimit_monoOver F hc f hf
  apply le_antisymm
  ¬∑ rw [le_iSup_iff]
    intro s H
    induction' s using Subobject.ind with Z g _
    let c' : Cocone (F ‚ãô MonoOver.forget _ ‚ãô Over.forget _) := Cocone.mk Z
      { app j := Subobject.ofMkLEMk _ _ (H j)
        naturality j j' f := by
          dsimp
          simpa only [‚Üê cancel_mono g, Category.assoc, Subobject.ofMkLEMk_comp,
            Category.comp_id] using MonoOver.w (F.map f) }
    exact Subobject.mk_le_mk_of_comm (hc.desc c')
      (hc.hom_ext (fun j ‚Ü¶ by rw [hc.fac_assoc c' j, hf, Subobject.ofMkLEMk_comp]))
  ¬∑ rw [iSup_le_iff]
    intro j
    exact Subobject.mk_le_mk_of_comm (c.Œπ.app j) (hf j)

end

section

variable
  {Œ∫ : Cardinal.{w}} [hŒ∫ : Fact Œ∫.IsRegular] [IsCardinalFiltered J Œ∫]
  (hXŒ∫ : HasCardinalLT (Subobject X) Œ∫)
  (c : Cocone (F ‚ãô MonoOver.forget _ ‚ãô Over.forget _)) (hc : IsColimit c)
  (f : c.pt ‚ü∂ X) (hf : ‚àÄ (j : J), c.Œπ.app j ‚â´ f = (F.obj j).obj.hom)

include hf hc hXŒ∫ in
lemma exists_isIso_of_functor_from_monoOver (h : Epi f) :
    ‚àÉ (j : J), IsIso (F.obj j).obj.hom := by
  have := isFiltered_of_isCardinalDirected J Œ∫
  simp only [Subobject.isIso_iff_mk_eq_top]
  have := mono_of_isColimit_monoOver F hc f hf
  rw [Subobject.epi_iff_mk_eq_top f,
    subobject_mk_of_isColimit_eq_iSup F hc f hf] at h
  let s (j : J) : Subobject X := Subobject.mk (F.obj j).obj.hom
  let S := Set.range s
  have h' : Function.Surjective (fun (j : J) ‚Ü¶ (‚ü®s j, _, rfl‚ü© : S)) := by
    rintro ‚ü®_, j, rfl‚ü©
    exact ‚ü®j, rfl‚ü©
  obtain ‚ü®œÉ, hœÉ‚ü© := h'.hasRightInverse
  have hS : HasCardinalLT S Œ∫ :=
    hXŒ∫.of_injective (f := Subtype.val) Subtype.val_injective
  refine ‚ü®IsCardinalFiltered.max œÉ hS, ?_‚ü©
  rw [‚Üê top_le_iff, ‚Üê h, iSup_le_iff]
  intro j
  let t : S := ‚ü®_, j, rfl‚ü©
  trans Subobject.mk (F.obj (œÉ t)).obj.hom
  ¬∑ have := le_of_eq (hœÉ t).symm
    exact this
  ¬∑ exact MonoOver.subobject_mk_le_mk_of_hom
      (F.map (IsCardinalFiltered.toMax œÉ hS t))

end

end IsGrothendieckAbelian

namespace IsCardinalFiltered

instance under (J : Type u) [Category.{v} J] (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular]
    [IsCardinalFiltered J Œ∫] (j‚ÇÄ : J) : IsCardinalFiltered (Under j‚ÇÄ) Œ∫ where
  nonempty_cocone {A _} F hA := ‚ü®by
    have := isFiltered_of_isCardinalDirected J Œ∫
    let c := cocone (F ‚ãô Under.forget j‚ÇÄ) hA
    let x (a : A) : j‚ÇÄ ‚ü∂ IsFiltered.max j‚ÇÄ c.pt := (F.obj a).hom ‚â´ c.Œπ.app a ‚â´
      IsFiltered.rightToMax j‚ÇÄ c.pt
    have hŒ∫' : HasCardinalLT A Œ∫ := hasCardinalLT_of_hasCardinalLT_arrow hA
    exact
      { pt := Under.mk (toCoeq x hŒ∫')
        Œπ :=
          { app a := Under.homMk (c.Œπ.app a ‚â´ IsFiltered.rightToMax j‚ÇÄ c.pt ‚â´ coeqHom x hŒ∫')
              (by simpa [x] using coeq_condition x hŒ∫' a)
            naturality a b f := by
              ext
              have := c.w f
              dsimp at this ‚ä¢
              simp only [reassoc_of% this, Category.assoc, Category.comp_id]} }‚ü©

end IsCardinalFiltered

variable {C : Type u} [Category.{v} C] [Abelian C]

variable [IsGrothendieckAbelian.{w} C]

namespace IsGrothendieckAbelian

namespace IsPresentable

variable {X : C} {J : Type w} [SmallCategory J] (Y : J ‚•§ C)

section injectivity

variable {Y} {c : Cocone Y} (hc : IsColimit c) {j‚ÇÄ : J}
  {y : X ‚ü∂ Y.obj j‚ÇÄ} (hy : y ‚â´ c.Œπ.app j‚ÇÄ = 0)

namespace injectivity‚ÇÄ

variable (y)

@[simps]
def Œ≥ : (Functor.const _).obj X ‚ü∂ Under.forget j‚ÇÄ ‚ãô Y where
  app t := y ‚â´ Y.map t.hom
  naturality t‚ÇÅ t‚ÇÇ f := by
    dsimp
    simp only [Category.id_comp, Category.assoc, ‚Üê Functor.map_comp, Under.w]

@[simps]
noncomputable def S : ShortComplex (Under j‚ÇÄ ‚•§ C) :=
  ShortComplex.mk _ _ (kernel.condition (Œ≥ y))

instance : Mono (S y).f := by dsimp; infer_instance

omit [IsGrothendieckAbelian C] in
lemma S_exact : (S y).Exact :=
  (S y).exact_of_f_is_kernel (kernelIsKernel _)

variable (c j‚ÇÄ) in
abbrev c‚ÇÉ : Cocone (Under.forget j‚ÇÄ ‚ãô Y) := c.whisker _

@[simps]
noncomputable def F : Under j‚ÇÄ ‚•§ MonoOver X where
  obj j := MonoOver.mk' ((kernel.Œπ (Œ≥ y)).app j)
  map {j j'} f := MonoOver.homMk ((kernel (Œ≥ y)).map f)

noncomputable def f : colimit (kernel (Œ≥ y)) ‚ü∂ X :=
  IsColimit.map (colimit.isColimit _) (constCocone _ X) (kernel.Œπ _)

lemma hf (j : Under j‚ÇÄ) :
    colimit.Œπ (kernel (Œ≥ y)) j ‚â´ f y = (kernel.Œπ (Œ≥ y)).app j :=
  (IsColimit.Œπ_map _ _ _ _).trans (by simp)

variable {y}

variable (Œ∫ : Cardinal.{w}) [hŒ∫ : Fact Œ∫.IsRegular] [IsCardinalFiltered J Œ∫]

variable (j‚ÇÄ) in
noncomputable def hc‚ÇÉ : IsColimit (c‚ÇÉ c j‚ÇÄ) :=
  have := isFiltered_of_isCardinalDirected J Œ∫
  (Functor.Final.isColimitWhiskerEquiv _ _).symm hc

include Œ∫ hŒ∫ hc hy in
lemma epi_f : Epi (f y) := by
  have := isFiltered_of_isCardinalDirected J Œ∫
  exact (HasExactColimitsOfShape.mapShortComplex_exact (S_exact y)
    (colimit.isColimit _) (isColimitConstCocone _ _) (hc‚ÇÉ hc j‚ÇÄ Œ∫) (f y) 0
    (fun j ‚Ü¶ by simpa using hf y j) (fun _ ‚Ü¶ by simpa using hy.symm)).epi_f rfl

end injectivity‚ÇÄ

variable {Œ∫ : Cardinal.{w}} [hŒ∫ : Fact Œ∫.IsRegular] [IsCardinalFiltered J Œ∫]
  (hXŒ∫ : HasCardinalLT (Subobject X) Œ∫)

include hXŒ∫ hc hy in
open injectivity‚ÇÄ in
lemma injectivity‚ÇÄ : ‚àÉ (j : J) (œÜ : j‚ÇÄ ‚ü∂ j), y ‚â´ Y.map œÜ = 0 := by
  obtain ‚ü®j, h‚ü© := exists_isIso_of_functor_from_monoOver (F y) hXŒ∫ _
      (colimit.isColimit (kernel (Œ≥ y))) (f y) (fun j ‚Ü¶ by simpa using hf y j)
      (epi_f hc hy Œ∫)
  dsimp at h
  refine ‚ü®j.right, j.hom, ?_‚ü©
  simpa only [‚Üê cancel_epi ((kernel.Œπ (Œ≥ y)).app j), comp_zero]
    using NatTrans.congr_app (kernel.condition (Œ≥ y)) j

end injectivity

section surjectivity

variable {Y} {c : Cocone Y} (hc : IsColimit c)

namespace surjectivity

variable (z : X ‚ü∂ c.pt)

@[simps]
noncomputable def F [Mono c.Œπ] : J ‚•§ MonoOver X where
  obj j := MonoOver.mk' ((pullback.snd c.Œπ ((Functor.const _).map z)).app j)
  map {j j'} f := MonoOver.homMk ((pullback c.Œπ ((Functor.const _).map z)).map f)

noncomputable def f : colimit (pullback c.Œπ ((Functor.const J).map z)) ‚ü∂ X :=
  colimit.desc _ (Cocone.mk X
    { app j := (pullback.snd c.Œπ ((Functor.const _).map z)).app j })

lemma hf (j : J) :
    colimit.Œπ (pullback c.Œπ ((Functor.const J).map z)) j ‚â´ f z =
      (pullback.snd c.Œπ ((Functor.const J).map z)).app j :=
  colimit.Œπ_desc _ _

variable (Œ∫ : Cardinal.{w}) [hŒ∫ : Fact Œ∫.IsRegular] [IsCardinalFiltered J Œ∫]

include hc Œ∫ in
lemma epi_f : Epi (f z) := by
  have := isFiltered_of_isCardinalDirected J Œ∫
  have isPullback := (IsPullback.of_hasPullback c.Œπ ((Functor.const _).map z)).map colim
  have : IsIso (f z) := by
    refine ((MorphismProperty.isomorphisms C).arrow_mk_iso_iff ?_).1
      (MorphismProperty.of_isPullback isPullback ?_)
    ¬∑ refine Arrow.isoMk (Iso.refl _)
        (IsColimit.coconePointUniqueUpToIso (colimit.isColimit _) (isColimitConstCocone J X)) ?_
      dsimp
      ext j
      dsimp
      rw [Category.id_comp, Œπ_colimMap_assoc, colimit.comp_coconePointUniqueUpToIso_hom,
        constCocone_Œπ, NatTrans.id_app, Category.comp_id]
      apply hf
    ¬∑ refine ((MorphismProperty.isomorphisms C).arrow_mk_iso_iff ?_).2
        (inferInstanceAs (IsIso (ùüô c.pt)))
      exact Arrow.isoMk (IsColimit.coconePointUniqueUpToIso (colimit.isColimit Y) hc)
        (IsColimit.coconePointUniqueUpToIso (colimit.isColimit _)
          (isColimitConstCocone J c.pt))
  infer_instance

end surjectivity

variable [‚àÄ (j j' : J) (œÜ : j ‚ü∂ j'), Mono (Y.map œÜ)]
  {Œ∫ : Cardinal.{w}} [hŒ∫ : Fact Œ∫.IsRegular] [IsCardinalFiltered J Œ∫]
  (hXŒ∫ : HasCardinalLT (Subobject X) Œ∫) (z : X ‚ü∂ c.pt)

include Œ∫ hXŒ∫ hc in
open surjectivity in
lemma surjectivity : ‚àÉ (j‚ÇÄ : J) (y : X ‚ü∂ Y.obj j‚ÇÄ), z = y ‚â´ c.Œπ.app j‚ÇÄ := by
  have := isFiltered_of_isCardinalDirected J Œ∫
  have := hc.mono_Œπ_app_of_isFiltered
  have := NatTrans.mono_of_mono_app c.Œπ
  obtain ‚ü®j, _‚ü© := exists_isIso_of_functor_from_monoOver (F z) hXŒ∫ _
    (colimit.isColimit _) (f z) (hf z) (epi_f hc z Œ∫)
  refine ‚ü®j, inv ((F z).obj j).obj.hom ‚â´ (pullback.fst c.Œπ _).app j, ?_‚ü©
  dsimp
  rw [Category.assoc, IsIso.eq_inv_comp, ‚Üê NatTrans.comp_app, pullback.condition,
    NatTrans.comp_app, Functor.const_map_app]

end surjectivity

variable {Œ∫ : Cardinal.{w}} [hŒ∫ : Fact Œ∫.IsRegular] [IsCardinalFiltered J Œ∫]
  (hXŒ∫ : HasCardinalLT (Subobject X) Œ∫)

include hXŒ∫ in
lemma preservesColimit_of_mono [‚àÄ (j j' : J) (œÜ : j ‚ü∂ j'), Mono (Y.map œÜ)] :
    PreservesColimit Y ((coyoneda.obj (op X))) where
  preserves {c} hc := ‚ü®by
    have := isFiltered_of_isCardinalDirected J Œ∫
    exact Types.FilteredColimit.isColimitOf' _ _
      (surjectivity hc hXŒ∫) (fun j‚ÇÄ y‚ÇÅ y‚ÇÇ hy ‚Ü¶ by
        dsimp at y‚ÇÅ y‚ÇÇ hy ‚ä¢
        rw [‚Üê sub_eq_zero, ‚Üê Preadditive.sub_comp] at hy
        obtain ‚ü®j, f, hf‚ü© := injectivity‚ÇÄ hc hy hXŒ∫
        exact ‚ü®j, f, by simpa only [Preadditive.sub_comp, sub_eq_zero] using hf‚ü©)‚ü©

end IsPresentable

end IsGrothendieckAbelian

end CategoryTheory
