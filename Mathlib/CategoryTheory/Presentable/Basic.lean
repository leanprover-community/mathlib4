/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Adjunction.Limits
public import Mathlib.CategoryTheory.Limits.Preserves.Ulift
public import Mathlib.CategoryTheory.Limits.Types.Filtered
public import Mathlib.CategoryTheory.Presentable.IsCardinalFiltered
public import Mathlib.SetTheory.Cardinal.HasCardinalLT

/-! # Presentable objects

A functor `F : C ‚•§ D` is `Œ∫`-accessible (`Functor.IsCardinalAccessible`)
if it commutes with colimits of shape `J` where `J` is any `Œ∫`-filtered category
(that is essentially small relative to the universe `w` such that `Œ∫ : Cardinal.{w}`.).
We also introduce another typeclass `Functor.IsAccessible` saying that there exists
a regular cardinal `Œ∫` such that `Functor.IsCardinalAccessible`.

An object `X` of a category is `Œ∫`-presentable (`IsCardinalPresentable`)
if the functor `Hom(X, _)` (i.e. `coyoneda.obj (op X)`) is `Œ∫`-accessible.
Similar as for accessible functors, we define a type class `IsAccessible`.

## References
* [Ad√°mek, J. and Rosick√Ω, J., *Locally presentable and accessible categories*][Adamek_Rosicky_1994]

-/

@[expose] public section

universe t w w' v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

namespace CategoryTheory

open Limits Opposite

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]

namespace Functor

section

variable (F G : C ‚•§ D) (e : F ‚âÖ G) (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular]

/-- A functor `F : C ‚•§ D` is `Œ∫`-accessible (with `Œ∫` a regular cardinal)
if it preserves colimits of shape `J` where `J` is any `Œ∫`-filtered category.
In the mathematical literature, some assumptions are often made on the
categories `C` or `D` (e.g. the existence of `Œ∫`-filtered colimits,
see `HasCardinalFilteredColimits` below), but here we do not
make such assumptions. -/
class IsCardinalAccessible : Prop where
  preservesColimitOfShape (J : Type w) [SmallCategory J] [IsCardinalFiltered J Œ∫] :
    PreservesColimitsOfShape J F := by intros; infer_instance

lemma preservesColimitsOfShape_of_isCardinalAccessible [F.IsCardinalAccessible Œ∫]
    (J : Type w) [SmallCategory J] [IsCardinalFiltered J Œ∫] :
    PreservesColimitsOfShape J F :=
  IsCardinalAccessible.preservesColimitOfShape Œ∫ _

lemma preservesColimitsOfShape_of_isCardinalAccessible_of_essentiallySmall
    [F.IsCardinalAccessible Œ∫]
    (J : Type u‚ÇÉ) [Category.{v‚ÇÉ} J] [EssentiallySmall.{w} J] [IsCardinalFiltered J Œ∫] :
    PreservesColimitsOfShape J F := by
  have := IsCardinalFiltered.of_equivalence Œ∫ (equivSmallModel.{w} J)
  have := F.preservesColimitsOfShape_of_isCardinalAccessible Œ∫ (SmallModel.{w} J)
  exact preservesColimitsOfShape_of_equiv (equivSmallModel.{w} J).symm F

variable {Œ∫} in
lemma isCardinalAccessible_of_le
    [F.IsCardinalAccessible Œ∫] {Œ∫' : Cardinal.{w}} [Fact Œ∫'.IsRegular] (h : Œ∫ ‚â§ Œ∫') :
    F.IsCardinalAccessible Œ∫' where
  preservesColimitOfShape {J _ _} := by
    have := IsCardinalFiltered.of_le J h
    exact F.preservesColimitsOfShape_of_isCardinalAccessible Œ∫ J

include e in
variable {F G} in
lemma isCardinalAccessible_of_natIso [F.IsCardinalAccessible Œ∫] : G.IsCardinalAccessible Œ∫ where
  preservesColimitOfShape J _ hŒ∫ := by
    have := F.preservesColimitsOfShape_of_isCardinalAccessible Œ∫ J
    exact preservesColimitsOfShape_of_natIso e

instance : IsCardinalAccessible (ùü≠ C) Œ∫ where

instance {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E] (F : C ‚•§ D) (G : D ‚•§ E)
    [F.IsCardinalAccessible Œ∫] [G.IsCardinalAccessible Œ∫] :
    (F ‚ãô G).IsCardinalAccessible Œ∫ := by
  have := F.preservesColimitsOfShape_of_isCardinalAccessible Œ∫
  have := G.preservesColimitsOfShape_of_isCardinalAccessible Œ∫
  exact { }

instance [PreservesColimitsOfSize.{w, w} F] : F.IsCardinalAccessible Œ∫ where

end

section

variable (F : C ‚•§ D)

/-- A functor is accessible relative to a universe `w` if
it is `Œ∫`-accessible for some regular `Œ∫ : Cardinal.{w}`. -/
@[pp_with_univ]
class IsAccessible : Prop where
  exists_cardinal : ‚àÉ (Œ∫ : Cardinal.{w}) (_ : Fact Œ∫.IsRegular), IsCardinalAccessible F Œ∫

lemma isAccessible_of_isCardinalAccessible (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular]
    [IsCardinalAccessible F Œ∫] : IsAccessible.{w} F where
  exists_cardinal := ‚ü®Œ∫, inferInstance, inferInstance‚ü©

end

end Functor

section

variable (X : C) (Y : C) (e : X ‚âÖ Y) (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular]

/-- An object `X` in a category is `Œ∫`-presentable (for `Œ∫` a regular cardinal)
when the functor `Hom(X, _)` preserves colimits indexed by
`Œ∫`-filtered categories. -/
abbrev IsCardinalPresentable : Prop := (coyoneda.obj (op X)).IsCardinalAccessible Œ∫

variable (C) in
/-- The property of objects that are `Œ∫`-presentable. -/
def isCardinalPresentable : ObjectProperty C := fun X ‚Ü¶ IsCardinalPresentable X Œ∫

instance (X : (isCardinalPresentable C Œ∫).FullSubcategory) :
    IsCardinalPresentable X.obj Œ∫ :=
  X.property

instance (X : (isCardinalPresentable C Œ∫).FullSubcategory) :
    IsCardinalPresentable ((isCardinalPresentable C Œ∫).Œπ.obj X) Œ∫ := by
  dsimp
  infer_instance

lemma isCardinalPresentable_iff_isCardinalAccessible_coyoneda_obj :
    IsCardinalPresentable X Œ∫ ‚Üî (coyoneda.obj (op X)).IsCardinalAccessible Œ∫ := Iff.rfl

lemma isCardinalPresentable_iff (X : C) :
    isCardinalPresentable C Œ∫ X ‚Üî IsCardinalPresentable X Œ∫ := Iff.rfl

lemma preservesColimitsOfShape_of_isCardinalPresentable [IsCardinalPresentable X Œ∫]
    (J : Type w) [SmallCategory.{w} J] [IsCardinalFiltered J Œ∫] :
    PreservesColimitsOfShape J (coyoneda.obj (op X)) :=
  (coyoneda.obj (op X)).preservesColimitsOfShape_of_isCardinalAccessible Œ∫ J

lemma preservesColimitsOfShape_of_isCardinalPresentable_of_essentiallySmall
    [IsCardinalPresentable X Œ∫]
    (J : Type u‚ÇÉ) [Category.{v‚ÇÉ} J] [EssentiallySmall.{w} J] [IsCardinalFiltered J Œ∫] :
    PreservesColimitsOfShape J (coyoneda.obj (op X)) :=
  (coyoneda.obj (op X)).preservesColimitsOfShape_of_isCardinalAccessible_of_essentiallySmall Œ∫ J

variable {Œ∫} in
lemma isCardinalPresentable_of_le [IsCardinalPresentable X Œ∫]
    {Œ∫' : Cardinal.{w}} [Fact Œ∫'.IsRegular] (h : Œ∫ ‚â§ Œ∫') :
    IsCardinalPresentable X Œ∫' :=
  (coyoneda.obj (op X)).isCardinalAccessible_of_le h

variable (C) {Œ∫} in
lemma isCardinalPresentable_monotone {Œ∫' : Cardinal.{w}} [Fact Œ∫'.IsRegular] (h : Œ∫ ‚â§ Œ∫') :
    isCardinalPresentable C Œ∫ ‚â§ isCardinalPresentable C Œ∫' := by
  intro X hX
  rw [isCardinalPresentable_iff] at hX ‚ä¢
  exact isCardinalPresentable_of_le _ h

include e in
variable {X Y} in
lemma isCardinalPresentable_of_iso [IsCardinalPresentable X Œ∫] : IsCardinalPresentable Y Œ∫ :=
  Functor.isCardinalAccessible_of_natIso (coyoneda.mapIso e.symm.op) Œ∫

instance : (isCardinalPresentable C Œ∫).IsClosedUnderIsomorphisms where
  of_iso e hX := by
    rw [isCardinalPresentable_iff] at hX ‚ä¢
    exact isCardinalPresentable_of_iso e _

lemma isCardinalPresentable_of_equivalence
    {C' : Type u‚ÇÉ} [Category.{v‚ÇÉ} C'] [IsCardinalPresentable X Œ∫] (e : C ‚âå C') :
    IsCardinalPresentable (e.functor.obj X) Œ∫ := by
  refine ‚ü®fun J _ _ ‚Ü¶ ‚ü®fun {Y} ‚Ü¶ ?_‚ü©‚ü©
  have := preservesColimitsOfShape_of_isCardinalPresentable X Œ∫ J
  suffices PreservesColimit Y (coyoneda.obj (op (e.functor.obj X)) ‚ãô uliftFunctor.{v‚ÇÅ}) from
    ‚ü®fun {c} hc ‚Ü¶ ‚ü®isColimitOfReflects uliftFunctor.{v‚ÇÅ}
        (isColimitOfPreserves (coyoneda.obj (op (e.functor.obj X)) ‚ãô uliftFunctor.{v‚ÇÅ}) hc)‚ü©‚ü©
  have iso : coyoneda.obj (op (e.functor.obj X)) ‚ãô uliftFunctor.{v‚ÇÅ} ‚âÖ
    e.inverse ‚ãô coyoneda.obj (op X) ‚ãô uliftFunctor.{v‚ÇÉ} :=
    NatIso.ofComponents (fun Z ‚Ü¶
      (Equiv.ulift.trans ((e.toAdjunction.homEquiv X Z).trans Equiv.ulift.symm)).toIso) (by
        intro _ _ f
        ext ‚ü®g‚ü©
        apply Equiv.ulift.injective
        simp [Adjunction.homEquiv_unit])
  exact preservesColimit_of_natIso Y iso.symm

instance isCardinalPresentable_of_isEquivalence
    {C' : Type u‚ÇÉ} [Category.{v‚ÇÉ} C'] [IsCardinalPresentable X Œ∫] (F : C ‚•§ C')
    [F.IsEquivalence] :
    IsCardinalPresentable (F.obj X) Œ∫ :=
  isCardinalPresentable_of_equivalence X Œ∫ F.asEquivalence

@[simp]
lemma isCardinalPresentable_iff_of_isEquivalence
    {C' : Type u‚ÇÉ} [Category.{v‚ÇÉ} C'] (F : C ‚•§ C')
    [F.IsEquivalence] :
    IsCardinalPresentable (F.obj X) Œ∫ ‚Üî IsCardinalPresentable X Œ∫ := by
  constructor
  ¬∑ intro
    exact isCardinalPresentable_of_iso
      (show F.inv.obj (F.obj X) ‚âÖ X from F.asEquivalence.unitIso.symm.app X :) Œ∫
  ¬∑ intro
    infer_instance

variable {X} in
lemma IsCardinalPresentable.exists_hom_of_isColimit [IsCardinalPresentable X Œ∫]
    {J : Type u‚ÇÇ} [Category.{v‚ÇÇ} J] [EssentiallySmall.{w} J] [IsCardinalFiltered J Œ∫]
    {F : J ‚•§ C} {c : Cocone F} (hc : IsColimit c) (f : X ‚ü∂ c.pt) :
    ‚àÉ (j : J) (f' : X ‚ü∂ F.obj j), f' ‚â´ c.Œπ.app j = f := by
  have := preservesColimitsOfShape_of_isCardinalPresentable_of_essentiallySmall X Œ∫ J
  exact Types.jointly_surjective_of_isColimit (isColimitOfPreserves (coyoneda.obj (op X)) hc) f

variable {X} in
lemma IsCardinalPresentable.exists_eq_of_isColimit [IsCardinalPresentable X Œ∫]
    {J : Type u‚ÇÇ} [Category.{v‚ÇÇ} J] [EssentiallySmall.{w} J] [IsCardinalFiltered J Œ∫]
    {F : J ‚•§ C} {c : Cocone F} (hc : IsColimit c) {i‚ÇÅ i‚ÇÇ : J} (f‚ÇÅ : X ‚ü∂ F.obj i‚ÇÅ)
    (f‚ÇÇ : X ‚ü∂ F.obj i‚ÇÇ) (hf : f‚ÇÅ ‚â´ c.Œπ.app i‚ÇÅ = f‚ÇÇ ‚â´ c.Œπ.app i‚ÇÇ) :
    ‚àÉ (j : J) (u : i‚ÇÅ ‚ü∂ j) (v : i‚ÇÇ ‚ü∂ j), f‚ÇÅ ‚â´ F.map u = f‚ÇÇ ‚â´ F.map v := by
  have := preservesColimitsOfShape_of_isCardinalPresentable_of_essentiallySmall X Œ∫ J
  have := isFiltered_of_isCardinalFiltered J Œ∫
  exact (Types.FilteredColimit.isColimit_eq_iff _
    (isColimitOfPreserves (coyoneda.obj (op X)) hc)).1 hf

variable {X} in
lemma IsCardinalPresentable.exists_eq_of_isColimit' [IsCardinalPresentable X Œ∫]
    {J : Type u‚ÇÇ} [Category.{v‚ÇÇ} J] [EssentiallySmall.{w} J] [IsCardinalFiltered J Œ∫]
    {F : J ‚•§ C} {c : Cocone F} (hc : IsColimit c) {i : J} (f‚ÇÅ f‚ÇÇ : X ‚ü∂ F.obj i)
    (hf : f‚ÇÅ ‚â´ c.Œπ.app i = f‚ÇÇ ‚â´ c.Œπ.app i) :
    ‚àÉ (j : J) (u : i ‚ü∂ j), f‚ÇÅ ‚â´ F.map u = f‚ÇÇ ‚â´ F.map u := by
  have := preservesColimitsOfShape_of_isCardinalPresentable_of_essentiallySmall X Œ∫ J
  have := isFiltered_of_isCardinalFiltered J Œ∫
  exact (Types.FilteredColimit.isColimit_eq_iff'
    (isColimitOfPreserves (coyoneda.obj (op X)) hc) f‚ÇÅ f‚ÇÇ).1 hf

lemma isCardinalPresentable_iff_isCardinalAccessible_uliftCoyoneda_obj :
    IsCardinalPresentable X Œ∫ ‚Üî (uliftCoyoneda.{t}.obj (op X)).IsCardinalAccessible Œ∫ := by
  change _ ‚Üî (coyoneda.obj (op X) ‚ãô uliftFunctor.{t}).IsCardinalAccessible Œ∫
  refine ‚ü®fun _ ‚Ü¶ inferInstance, fun _ ‚Ü¶ ‚ü®fun J _ _ ‚Ü¶ ?_‚ü©‚ü©
  have := Functor.preservesColimitsOfShape_of_isCardinalAccessible
    (coyoneda.obj (op X) ‚ãô uliftFunctor.{t}) Œ∫ J
  exact preservesColimitsOfShape_of_reflects_of_preserves _ uliftFunctor.{t, v‚ÇÅ}

instance [IsCardinalPresentable X Œ∫] :
    (uliftCoyoneda.{t}.obj (op X)).IsCardinalAccessible Œ∫ :=
  (isCardinalPresentable_iff_isCardinalAccessible_uliftCoyoneda_obj.{t} X Œ∫).1 inferInstance

end

section

variable (X : C)

/-- An object of a category is presentable relative to a universe `w`
if it is `Œ∫`-presentable for some regular `Œ∫ : Cardinal.{w}`. -/
@[pp_with_univ]
abbrev IsPresentable (X : C) : Prop :=
  Functor.IsAccessible.{w} (coyoneda.obj (op X))

lemma isPresentable_of_isCardinalPresentable (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular]
    [IsCardinalPresentable X Œ∫] : IsPresentable.{w} X where
  exists_cardinal := ‚ü®Œ∫, inferInstance, inferInstance‚ü©

end

section

variable (C) (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular]

/-- A category has `Œ∫`-filtered colimits if it has colimits of shape `J`
for any `Œ∫`-filtered category `J`. -/
class HasCardinalFilteredColimits : Prop where
  hasColimitsOfShape (J : Type w) [SmallCategory J] [IsCardinalFiltered J Œ∫] :
    HasColimitsOfShape J C := by intros; infer_instance

attribute [instance] HasCardinalFilteredColimits.hasColimitsOfShape

instance [HasColimitsOfSize.{w, w} C] : HasCardinalFilteredColimits.{w} C Œ∫ where

end

end CategoryTheory
