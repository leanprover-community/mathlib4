/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Presentable.Adjunction
import Mathlib.CategoryTheory.Limits.Shapes.Multiequalizer
import Mathlib.CategoryTheory.Limits.Shapes.RegularMono
import Mathlib.CategoryTheory.Localization.BousfieldTransfiniteComposition
import Mathlib.CategoryTheory.ObjectProperty.ColimitsOfShape
import Mathlib.CategoryTheory.SmallObject.TransfiniteIteration
import Mathlib.CategoryTheory.Adjunction.PartialAdjoint
import Mathlib.CategoryTheory.MorphismProperty.IsSmall

/-!
# The Orthogonal-reflection construction

Given `W : MorphismProperty C` (which should be small) and assuming the existence
of certain colimits in `C`, we construct a morphism `toSucc W Z : Z ‚ü∂ succ W Z` for
any `Z : C`. This morphism belongs to `LeftBousfield.W W.isLocal` and
is an isomorphism iff `Z` belongs to `W.isLocal` (see the lemma `isIso_toSucc_iff`).
The morphism `toSucc W Z : Z ‚ü∂ succ W Z` is defined as a composition
of two morphisms that are roughly described as follows:
* `toStep W Z : Z ‚ü∂ step W Z`: for any morphism `f : X ‚ü∂ Y` satisfying `W`
and any morphism `X ‚ü∂ Z`, we "attach" a morphism `Y ‚ü∂ step W Z` (using
coproducts and a pushout in essentially the same way as it is done in
the file `CategoryTheory.SmallObject.Construction` for the small object
argument);
* `fromStep W Z : step W Z ‚ü∂ succ W Z`: this morphism coequalizes all pairs
of morphisms `g‚ÇÅ g‚ÇÇ : Y ‚ü∂ step W Z` such that there is a `f : X ‚ü∂ Y`
satisfying `W` such that `f ‚â´ g‚ÇÅ = f ‚â´ g‚ÇÇ`.

The morphism `toSucc W Z : Z ‚ü∂ succ W Z` is a variant of the (wrong) definition
p. 32 in the book by Ad√°mek and Rosick√Ω. In this book, a slightly different object
as `succ W Z` is defined directly as a colimit of an intricate diagram, but
contrary to what is stated on p. 33, it does not satisfy `isIso_toSucc_iff`.
The author of this file was unable to not understand the attempt of the authors
to fix this mistake in the errata to this book. This led to the definition
in two steps outlined above.

## Main results

We do a transfinite iteration of the construction `toSucc` described above
in order to obtain the following results under the assumption that
`W : MorphismProperty C` is a `w`-small property of morphisms in a
locally `Œ∫`-presentable category `C` (with `Œ∫ : Cardinal.{w}` a regular cardinal)
such that the domains and domains of the morphisms satisfying `W` are `Œ∫`-presentable :
* `MorphismProperty.isRightAdjoint_Œπ_isLocal`: existence of the left adjoint
of the inclusion `W.isLocal ‚•§ C`;
* `MorphismProperty.isLocallyPresentable_isLocal`: the fullsubcategory
`W.isLocal` is locally presentable.

This is essentially the implication (i) ‚Üí (ii) in Theorem 1.39 (and the corollary 1.40)
in the book by Ad√°mek and Rosick√Ω (note that according to the
errata to this book, the implication (ii) ‚Üí (i) is wrong when `Œ∫ = ‚Ñµ‚ÇÄ`).

## References
* [Ad√°mek, J. and Rosick√Ω, J., *Locally presentable and accessible categories*][Adamek_Rosicky_1994]

-/

universe w v' u' v u

namespace CategoryTheory

open Limits Localization Opposite

variable {C : Type u} [Category.{v} C] (W : MorphismProperty C)

-- why is it not synthetized automatically???
instance (D : Type w) [SmallCategory.{w} D] : EssentiallySmall.{w} D :=
  essentiallySmallSelf D

lemma MorphismProperty.isClosedUnderColimitsOfShape_isLocal
    (J : Type u') [Category.{v'} J] [EssentiallySmall.{w} J]
    (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular] [IsCardinalFiltered J Œ∫]
    (hW : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), W f ‚Üí IsCardinalPresentable X Œ∫ ‚àß IsCardinalPresentable Y Œ∫) :
    W.isLocal.IsClosedUnderColimitsOfShape J where
  colimitsOfShape_le := fun Z ‚ü®p‚ü© X Y f hf ‚Ü¶ by
    obtain ‚ü®_, _‚ü© := hW f hf
    refine ‚ü®fun g‚ÇÅ g‚ÇÇ h ‚Ü¶ ?_, fun g ‚Ü¶ ?_‚ü©
    ¬∑ obtain ‚ü®j‚ÇÅ, g‚ÇÅ, rfl‚ü© := IsCardinalPresentable.exists_hom_of_isColimit Œ∫ p.isColimit g‚ÇÅ
      obtain ‚ü®j‚ÇÇ, g‚ÇÇ, rfl‚ü© := IsCardinalPresentable.exists_hom_of_isColimit Œ∫ p.isColimit g‚ÇÇ
      dsimp at h ‚ä¢
      obtain ‚ü®j‚ÇÉ, u, v, huv‚ü© :=
        IsCardinalPresentable.exists_eq_of_isColimit Œ∫ p.isColimit (f ‚â´ g‚ÇÅ) (f ‚â´ g‚ÇÇ)
          (by simpa)
      simp only [Category.assoc] at huv
      rw [‚Üê p.w u, ‚Üê p.w v, reassoc_of% ((p.prop_diag_obj j‚ÇÉ _ hf).1 huv)]
    ¬∑ obtain ‚ü®j, g, rfl‚ü© := IsCardinalPresentable.exists_hom_of_isColimit Œ∫ p.isColimit g
      obtain ‚ü®g, rfl‚ü© := (p.prop_diag_obj j _ hf).2 g
      exact ‚ü®g ‚â´ p.Œπ.app j, by simp‚ü©

lemma MorphismProperty.isCardinalAccessible_Œπ_isLocal
    (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular]
    [HasCardinalFilteredColimits C Œ∫]
    (hW : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), W f ‚Üí IsCardinalPresentable X Œ∫ ‚àß IsCardinalPresentable Y Œ∫) :
    W.isLocal.Œπ.IsCardinalAccessible Œ∫ where
  preservesColimitOfShape J _ _ := by
    have := W.isClosedUnderColimitsOfShape_isLocal J Œ∫ hW
    infer_instance

namespace OrthogonalReflection

variable (Z : C)

/-- Given `W : MorphismProperty C` and `Z : C`, this is the index type
parametrising the data of a morphism `f : X ‚ü∂ Y` satisfying `W`
and a morphism `X ‚ü∂ Z`. -/
def D‚ÇÅ : Type _ := Œ£ (f : W.toSet), f.1.left ‚ü∂ Z

instance [MorphismProperty.IsSmall.{w} W] [LocallySmall.{w} C] :
    Small.{w} (D‚ÇÅ (W := W) (Z := Z)) := by
  dsimp [D‚ÇÅ]
  infer_instance

lemma D‚ÇÅ.hasCoproductsOfShape [MorphismProperty.IsSmall.{w} W]
    [LocallySmall.{w} C] [HasCoproducts.{w} C] :
    HasCoproductsOfShape (D‚ÇÅ (W := W) (Z := Z)) C :=
  hasColimitsOfShape_of_equivalence
    (Discrete.equivalence (equivShrink.{w} _).symm)

variable {W Z} in
/-- If `d : D‚ÇÅ W Z` corresponds to the data of `f : X ‚ü∂ Y` satisfying `W` and
of a morphism `X ‚ü∂ Z`, this is the object `X`. -/
def D‚ÇÅ.obj‚ÇÅ (d : D‚ÇÅ W Z) : C := d.1.1.left

variable {W Z} in
/-- If `d : D‚ÇÅ W Z` corresponds to the data of `f : X ‚ü∂ Y` satisfying `W` and
of a morphism `X ‚ü∂ Z`, this is the object `Y`. -/
def D‚ÇÅ.obj‚ÇÇ (d : D‚ÇÅ W Z) : C := d.1.1.right

section

variable [HasCoproduct (D‚ÇÅ.obj‚ÇÅ (W := W) (Z := Z))]

/-- Considering all diagrams consisting of a morphism `f : X ‚ü∂ Y` satisfying `W`
and of a morphism `d : X ‚ü∂ Z`, this is the morphism from the coproduct of
all these `X` objects to `Z` given by these morphisms `d`. -/
noncomputable abbrev D‚ÇÅ.l : ‚àê (obj‚ÇÅ (W := W) (Z := Z)) ‚ü∂ Z :=
  Sigma.desc (fun d ‚Ü¶ d.2)

variable {W Z} in
/-- The inclusion of a summand in `‚àê obj‚ÇÅ`. -/
noncomputable abbrev D‚ÇÅ.ŒπLeft {X Y : C} (f : X ‚ü∂ Y) (hf : W f) (g : X ‚ü∂ Z) :
    X ‚ü∂ ‚àê obj‚ÇÅ (W := W) (Z := Z) :=
  Sigma.Œπ (obj‚ÇÅ (W := W) (Z := Z)) ‚ü®‚ü®Arrow.mk f, hf‚ü©, g‚ü©

variable {W Z} in
@[reassoc]
lemma D‚ÇÅ.ŒπLeft_comp_l {X Y : C} (f : X ‚ü∂ Y) (hf : W f) (g : X ‚ü∂ Z) :
    D‚ÇÅ.ŒπLeft f hf g ‚â´ D‚ÇÅ.l W Z = g :=
  Sigma.Œπ_desc _ _

variable [HasCoproduct (D‚ÇÅ.obj‚ÇÇ (W := W) (Z := Z))]

/-- The coproduct of all the morphisms `f` indexed by all diagrams
consisting of a morphism `f : X ‚ü∂ Y` satisfying `W` and of a morphism `d : X ‚ü∂ Z`. -/
noncomputable abbrev D‚ÇÅ.t : ‚àê (obj‚ÇÅ (W := W) (Z := Z)) ‚ü∂ ‚àê (obj‚ÇÇ (W := W) (Z := Z)) :=
  Limits.Sigma.map (fun d ‚Ü¶ d.1.1.hom)

variable {W Z} in
/-- The inclusion of a summand in `‚àê obj‚ÇÇ`. -/
noncomputable abbrev D‚ÇÅ.ŒπRight {X Y : C} (f : X ‚ü∂ Y) (hf : W f) (g : X ‚ü∂ Z) :
    Y ‚ü∂ ‚àê (obj‚ÇÇ (W := W) (Z := Z)) :=
  Sigma.Œπ (obj‚ÇÇ (W := W) (Z := Z)) ‚ü®‚ü®Arrow.mk f, hf‚ü©, g‚ü©

variable {W Z} in
@[reassoc]
lemma D‚ÇÅ.Œπ_comp_t (d : D‚ÇÅ W Z) :
    Sigma.Œπ _ d ‚â´ D‚ÇÅ.t W Z = d.1.1.hom ‚â´ Sigma.Œπ obj‚ÇÇ d := by
  apply Œπ_colimMap

variable {W Z} in
@[reassoc]
lemma D‚ÇÅ.ŒπLeft_comp_t {X Y : C} (f : X ‚ü∂ Y) (hf : W f) (g : X ‚ü∂ Z) :
    D‚ÇÅ.ŒπLeft f hf g ‚â´ D‚ÇÅ.t W Z = f ‚â´ D‚ÇÅ.ŒπRight f hf g := by
  apply Œπ_colimMap

variable [HasPushouts C]

/-- The intermediate object in the definition of the morphism `toSucc W Z : Z ‚ü∂ succ W Z`.
It is the pushout of the following square:
```lean
‚àê D‚ÇÅ.obj‚ÇÅ ‚ü∂ ‚àê D‚ÇÅ.obj‚ÇÇ
   |           |
   v           v
   Z      ‚ü∂   step W Z
```
where the coproduct is taken over all the diagram consisting of a morphism `f : X ‚ü∂ Y`
satisfying `W` and a morphism `X ‚ü∂ Z`. The top map is the coproduct of all of these `f`.
-/
noncomputable abbrev step := pushout (D‚ÇÅ.t W Z) (D‚ÇÅ.l W Z)

/-- The canonical map from `Z` to the pushout of `D‚ÇÅ.t W Z` and `D‚ÇÅ.l W Z`. -/
noncomputable abbrev toStep : Z ‚ü∂ step W Z := pushout.inr _ _

/-- The index type parametrising the data of two morphisms `g‚ÇÅ g‚ÇÇ : Y ‚ü∂ step W Z`, and
a map `f : X ‚ü∂ Y` satisfying `W` such that `f ‚â´ g‚ÇÅ = f ‚â´ g‚ÇÇ`. -/
def D‚ÇÇ : Type _ :=
  Œ£ (f : W.toSet),
    { pq : (f.1.right ‚ü∂ step W Z) √ó (f.1.right ‚ü∂ step W Z) // f.1.hom ‚â´ pq.1 = f.1.hom ‚â´ pq.2 }

/-- The shape of the multicoequalizer of all pairs of morphisms `g‚ÇÅ g‚ÇÇ : Y ‚ü∂ step W Z` with
a `f : X ‚ü∂ Y` satisfying `W` such that `f ‚â´ g‚ÇÅ = f ‚â´ g‚ÇÇ`. -/
@[simps]
def D‚ÇÇ.multispanShape : MultispanShape where
  L := D‚ÇÇ W Z
  R := Unit
  fst _ := .unit
  snd _ := .unit

section

variable [MorphismProperty.IsSmall.{w} W] [LocallySmall.{w} C]

instance : Small.{w} (D‚ÇÇ (W := W) (Z := Z)) := by
  dsimp [D‚ÇÇ]
  infer_instance

instance : Small.{w} (D‚ÇÇ.multispanShape W Z).L := by dsimp; infer_instance

attribute [local instance] essentiallySmall_of_small_of_locallySmall in
lemma D‚ÇÇ.hasColimitsOfShape [HasColimitsOfSize.{w, w} C] :
    HasColimitsOfShape (WalkingMultispan (multispanShape W Z)) C :=
  hasColimitsOfShape_of_equivalence (equivSmallModel.{w} _).symm

end

/-- The diagram of the multicoequalizer of all pair of morphisms `g‚ÇÅ g‚ÇÇ : Y ‚ü∂ step W Z` with
a `f : X ‚ü∂ Y` satisfying `W` such that `f ‚â´ g‚ÇÅ = f ‚â´ g‚ÇÇ`. -/
@[simps]
noncomputable def D‚ÇÇ.multispanIndex : MultispanIndex (multispanShape W Z) C where
  left d := d.1.1.right
  right _ := step W Z
  fst d := d.2.1.1
  snd d := d.2.1.2


variable [HasMulticoequalizer (D‚ÇÇ.multispanIndex W Z)]

/-- The object `succ W Z` is the multicoequalizer of all pairs of morphisms
 `g‚ÇÅ g‚ÇÇ : Y ‚ü∂ step W Z` with a `f : X ‚ü∂ Y` satisfying `W` such that `f ‚â´ g‚ÇÅ = f ‚â´ g‚ÇÇ`. -/
noncomputable abbrev succ := multicoequalizer (D‚ÇÇ.multispanIndex W Z)

/-- The projection from `Z` to the multicoequalizer of all morphisms `g‚ÇÅ g‚ÇÇ : Y ‚ü∂ step W Z` with
a `f : X ‚ü∂ Y` satisfying `W` such that `f ‚â´ g‚ÇÅ = f ‚â´ g‚ÇÇ`. -/
noncomputable abbrev fromStep : step W Z ‚ü∂ succ W Z :=
  Multicoequalizer.œÄ (D‚ÇÇ.multispanIndex W Z) .unit

variable {W Z} in
@[reassoc]
lemma D‚ÇÇ.condition {X Y : C} (f : X ‚ü∂ Y) (hf : W f)
    {g‚ÇÅ g‚ÇÇ : Y ‚ü∂ step W Z} (h : f ‚â´ g‚ÇÅ = f ‚â´ g‚ÇÇ) :
      g‚ÇÅ ‚â´ fromStep W Z = g‚ÇÇ ‚â´ fromStep W Z :=
  Multicoequalizer.condition (D‚ÇÇ.multispanIndex W Z)
    ‚ü®‚ü®Arrow.mk f, hf‚ü©, ‚ü®g‚ÇÅ, g‚ÇÇ‚ü©, h‚ü©

/-- The morphism `Z ‚ü∂ succ W Z`. -/
noncomputable abbrev toSucc : Z ‚ü∂ succ W Z := toStep W Z ‚â´ fromStep W Z

variable {W Z} in
lemma toSucc_injectivity {X Y : C} (f : X ‚ü∂ Y) (hf : W f)
    (g‚ÇÅ g‚ÇÇ : Y ‚ü∂ Z) (hg : f ‚â´ g‚ÇÅ = f ‚â´ g‚ÇÇ) :
    g‚ÇÅ ‚â´ toSucc W Z = g‚ÇÇ ‚â´ toSucc W Z := by
  simpa using D‚ÇÇ.condition f hf (g‚ÇÅ := g‚ÇÅ ‚â´ toStep W Z) (g‚ÇÇ := g‚ÇÇ ‚â´ toStep W Z)
    (by simp [reassoc_of% hg])

variable {W Z} in
lemma toSucc_surjectivity {X Y : C} (f : X ‚ü∂ Y) (hf : W f) (g : X ‚ü∂ Z) :
    ‚àÉ (g' : Y ‚ü∂ succ W Z), f ‚â´ g' = g ‚â´ toSucc W Z :=
  ‚ü®D‚ÇÅ.ŒπRight f hf g ‚â´ pushout.inl _ _ ‚â´ fromStep W Z, by
    simp [‚Üê D‚ÇÅ.ŒπLeft_comp_t_assoc, pushout.condition_assoc]‚ü©

lemma leftBousfieldW_isLocal_toSucc :
    LeftBousfield.W W.isLocal (toSucc W Z) := by
  refine fun T hT ‚Ü¶ ‚ü®fun œÜ‚ÇÅ œÜ‚ÇÇ h ‚Ü¶ ?_, fun g ‚Ü¶ ?_‚ü©
  ¬∑ ext ‚ü®‚ü©
    simp only [Category.assoc] at h
    dsimp
    ext d
    ¬∑ apply (hT d.1.1.hom d.1.2).1
      simp only [‚Üê D‚ÇÅ.Œπ_comp_t_assoc, pushout.condition_assoc, h]
    ¬∑ exact h
  ¬∑ choose f hf using fun (d : D‚ÇÅ W Z) ‚Ü¶ (hT d.1.1.hom d.1.2).2 (d.2 ‚â´ g)
    exact ‚ü®Multicoequalizer.desc _ _ (fun ‚ü®‚ü© ‚Ü¶ pushout.desc (Sigma.desc f) g)
      (fun d ‚Ü¶ (hT d.1.1.hom d.1.2).1 (by simp [reassoc_of% d.2.2])), by simp‚ü©

lemma isIso_toSucc_iff :
    IsIso (toSucc W Z) ‚Üî W.isLocal Z := by
  refine ‚ü®fun _ X Y f hf ‚Ü¶ ?_, fun hZ ‚Ü¶ ?_‚ü©
  ¬∑ refine ‚ü®fun g‚ÇÅ g‚ÇÇ h ‚Ü¶ ?_, fun g ‚Ü¶ ?_‚ü©
    ¬∑ simpa [‚Üê cancel_mono (toSucc W Z)] using
        D‚ÇÇ.condition f hf (g‚ÇÅ := g‚ÇÅ ‚â´ toStep W Z) (g‚ÇÇ := g‚ÇÇ ‚â´ toStep W Z)
          (by simp [reassoc_of% h])
    ¬∑ have hZ := IsIso.hom_inv_id (toSucc W Z)
      simp only [Category.assoc] at hZ
      exact ‚ü®D‚ÇÅ.ŒπRight f hf g ‚â´ pushout.inl _ _ ‚â´ fromStep W Z ‚â´ inv (toSucc W Z),
        by simp [‚Üê D‚ÇÅ.ŒπLeft_comp_t_assoc, pushout.condition_assoc, hZ]‚ü©
  ¬∑ obtain ‚ü®f, hf‚ü© := (leftBousfieldW_isLocal_toSucc W Z _ hZ).2 (ùüô _)
    dsimp at hf
    refine ‚ü®f, hf, ?_‚ü©
    ext ‚ü®‚ü©
    dsimp
    ext d
    ¬∑ simp only [Category.assoc] at hf
      simp only [Category.comp_id, ‚Üê Category.assoc]
      refine D‚ÇÇ.condition _ d.1.2 ?_
      rw [Category.assoc, Category.assoc, Category.assoc,
        ‚Üê D‚ÇÅ.Œπ_comp_t_assoc, pushout.condition_assoc, reassoc_of% hf,
        ‚Üê D‚ÇÅ.Œπ_comp_t_assoc, pushout.condition]
    ¬∑ simp [reassoc_of% hf]

end

open SmallObject

variable [HasPushouts C]
  [‚àÄ Z, HasCoproduct (D‚ÇÅ.obj‚ÇÅ (W := W) (Z := Z))]
  [‚àÄ Z, HasCoproduct (D‚ÇÅ.obj‚ÇÇ (W := W) (Z := Z))]
  [‚àÄ Z, HasMulticoequalizer (D‚ÇÇ.multispanIndex W Z)]

/-- The successor structure of the orthogonal-reflection construction. -/
noncomputable def succStruct (Z‚ÇÄ : C) : SuccStruct C where
  X‚ÇÄ := Z‚ÇÄ
  succ Z := succ W Z
  toSucc Z := toSucc W Z

variable (Œ∫ : Cardinal.{w}) [OrderBot Œ∫.ord.toType]
  [HasIterationOfShape Œ∫.ord.toType C]

/-- The transfinite iteration of `succStruct W Z` to the power `Œ∫.ord.toType`. -/
noncomputable def reflectionObj : C := (succStruct W Z).iteration Œ∫.ord.toType

/-- The map which shall exhibit `reflectionObj W Z Œ∫` as the image of `Z` by
the left adjoint of the inclusion of `W.isLocal`, see `corepresentableBy`. -/
noncomputable def reflection : Z ‚ü∂ reflectionObj W Z Œ∫ :=
  (succStruct W Z).ŒπIteration Œ∫.ord.toType

/-- The morphism `reflection W Z Œ∫ : Z ‚ü∂ reflectionObj W Z Œ∫` is a transfinite
compositions of morphisms in `LeftBousfield.W W.isLocal`. -/
noncomputable def transfiniteCompositionOfShapeReflection :
    (LeftBousfield.W W.isLocal).TransfiniteCompositionOfShape Œ∫.ord.toType
      (reflection W Z Œ∫) :=
  ((succStruct W Z).transfiniteCompositionOfShapeŒπIteration Œ∫.ord.toType).ofLE (by
    rintro Z‚ÇÄ _ _ ‚ü®_‚ü©
    exact leftBousfieldW_isLocal_toSucc W Z‚ÇÄ)

/-- The functor `Œ∫.ord.toType ‚•§ C` that is the diagram of the
transfinite composition `transfiniteCompositionOfShapeReflection`. -/
noncomputable abbrev iteration : Œ∫.ord.toType ‚•§ C :=
  (transfiniteCompositionOfShapeReflection W Z Œ∫).F

section

variable [Fact Œ∫.IsRegular]

/-- `(iteration W Z Œ∫).obj (Order.succ j)` identifies to the image of
`(iteration W Z Œ∫).obj j` by `succ`. -/
noncomputable def iterationObjSuccIso (j : Œ∫.ord.toType) :
  (iteration W Z Œ∫).obj (Order.succ j) ‚âÖ succ W ((iteration W Z Œ∫).obj j) :=
    (succStruct W Z).iterationFunctorObjSuccIso j (by
      have := Cardinal.noMaxOrder (Fact.elim inferInstance : Œ∫.IsRegular).aleph0_le
      exact not_isMax j)

@[reassoc]
lemma iteration_map_succ (j : Œ∫.ord.toType) :
    (iteration W Z Œ∫).map (homOfLE (Order.le_succ j)) =
      toSucc W _ ‚â´ (iterationObjSuccIso W Z Œ∫ j).inv :=
  (succStruct W Z).iterationFunctor_map_succ _ _

variable {Œ∫ W Z} in
lemma iteration_map_succ_injectivity {X Y : C} (f : X ‚ü∂ Y) (hf : W f) {j : Œ∫.ord.toType}
    (g‚ÇÅ g‚ÇÇ : Y ‚ü∂ (iteration W Z Œ∫).obj j) (hg : f ‚â´ g‚ÇÅ = f ‚â´ g‚ÇÇ) :
    g‚ÇÅ ‚â´ (iteration W Z Œ∫).map (homOfLE (Order.le_succ j)) =
      g‚ÇÇ ‚â´ (iteration W Z Œ∫).map (homOfLE (Order.le_succ j)) := by
  simp [iteration_map_succ, reassoc_of% (toSucc_injectivity f hf _ _ hg)]

variable {Œ∫ W Z} in
lemma iteration_map_succ_surjectivity {X Y : C} (f : X ‚ü∂ Y) (hf : W f) {j : Œ∫.ord.toType}
    (g : X ‚ü∂ (iteration W Z Œ∫).obj j) :
    ‚àÉ (g' : Y ‚ü∂ (iteration W Z Œ∫).obj (Order.succ j)),
      f ‚â´ g' = g ‚â´ (iteration W Z Œ∫).map (homOfLE (Order.le_succ j)) := by
  simp only [iteration_map_succ]
  obtain ‚ü®g', hg'‚ü© := toSucc_surjectivity f hf g
  exact ‚ü®g' ‚â´ (iterationObjSuccIso W Z Œ∫ j).inv, by simp [reassoc_of% hg']‚ü©

end

lemma leftBousfieldW_isLocal_reflection :
     LeftBousfield.W W.isLocal (reflection W Z Œ∫) :=
  (LeftBousfield.W W.isLocal).transfiniteCompositionsOfShape_le Œ∫.ord.toType _
    ‚ü®transfiniteCompositionOfShapeReflection W Z Œ∫‚ü©

variable {W} {Œ∫} [Fact Œ∫.IsRegular]
  (hW : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), W f ‚Üí IsCardinalPresentable X Œ∫ ‚àß IsCardinalPresentable Y Œ∫)

include hW

lemma isLocal_reflectionObj :
    W.isLocal (reflectionObj W Z Œ∫) := by
  let H := transfiniteCompositionOfShapeReflection W Z Œ∫
  intro X Y f hf
  obtain ‚ü®_, _‚ü© := hW f hf
  refine ‚ü®fun g‚ÇÅ g‚ÇÇ h ‚Ü¶ ?_, fun g ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®j, g‚ÇÅ, g‚ÇÇ, rfl, rfl‚ü© :
      ‚àÉ (j : Œ∫.ord.toType) (g‚ÇÅ' g‚ÇÇ' : Y ‚ü∂ H.F.obj j), g‚ÇÅ' ‚â´ H.incl.app j = g‚ÇÅ ‚àß
        g‚ÇÇ' ‚â´ H.incl.app j = g‚ÇÇ := by
      obtain ‚ü®j‚ÇÅ, g‚ÇÅ, rfl‚ü© := IsCardinalPresentable.exists_hom_of_isColimit Œ∫ H.isColimit g‚ÇÅ
      obtain ‚ü®j‚ÇÇ, g‚ÇÇ, rfl‚ü© := IsCardinalPresentable.exists_hom_of_isColimit Œ∫ H.isColimit g‚ÇÇ
      exact ‚ü®max j‚ÇÅ j‚ÇÇ, g‚ÇÅ ‚â´ H.F.map (homOfLE (le_max_left _ _)),
        g‚ÇÇ ‚â´ H.F.map (homOfLE (le_max_right _ _)), by simp‚ü©
    dsimp at h
    obtain ‚ü®k, u, hk‚ü© := IsCardinalPresentable.exists_eq_of_isColimit' Œ∫ H.isColimit
      (f ‚â´ g‚ÇÅ) (f ‚â´ g‚ÇÇ) (by simpa)
    have hg := iteration_map_succ_injectivity f hf
      (g‚ÇÅ ‚â´ H.F.map u) (g‚ÇÇ ‚â´ H.F.map u) (by simpa using hk)
    simp only [homOfLE_leOfHom, Category.assoc] at hg
    have := H.incl.naturality (u ‚â´ homOfLE (Order.le_succ k))
    simp only [Functor.const_obj_obj, Functor.const_obj_map, Category.comp_id] at this
    simp only [‚Üê this, Functor.map_comp, Category.assoc]
    rw [reassoc_of% hg]
  ¬∑ obtain ‚ü®j, g, rfl‚ü© := IsCardinalPresentable.exists_hom_of_isColimit Œ∫ H.isColimit g
    obtain ‚ü®g', hg'‚ü© := iteration_map_succ_surjectivity f hf g
    exact ‚ü®g' ‚â´ H.incl.app (Order.succ j), by simp [reassoc_of% hg']‚ü©

/-- The morphism `reflection W Z Œ∫ : Z ‚ü∂ reflectionObj W Z Œ∫` exhibits `reflectionObj W Z Œ∫`
as the image of `Z` by the left adjoint of the inclusion `W.isLocal.Œπ`. -/
noncomputable def corepresentableBy :
  (W.isLocal.Œπ ‚ãô coyoneda.obj (op Z)).CorepresentableBy
    ‚ü®_, isLocal_reflectionObj Z hW‚ü© where
  homEquiv {A} := Equiv.ofBijective _ (leftBousfieldW_isLocal_reflection W Z Œ∫ _ A.2)

variable (W Œ∫)

lemma isRightAdjoint_Œπ :
    W.isLocal.Œπ.IsRightAdjoint := by
  rw [Functor.isRightAdjoint_iff_leftAdjointObjIsDefined_eq_top]
  ext Z
  simpa using (corepresentableBy Z hW).isCorepresentable

end OrthogonalReflection

namespace MorphismProperty

open OrthogonalReflection in
lemma isRightAdjoint_Œπ_isLocal
    (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular]
    [MorphismProperty.IsSmall.{w} W] [LocallySmall.{w} C]
    (hW : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), W f ‚Üí IsCardinalPresentable X Œ∫ ‚àß IsCardinalPresentable Y Œ∫)
    [HasColimitsOfSize.{w, w} C] :
    W.isLocal.Œπ.IsRightAdjoint := by
  have : OrderBot Œ∫.ord.toType :=
    Cardinal.toTypeOrderBot (Cardinal.IsRegular.ne_zero Fact.out)
  have := D‚ÇÅ.hasCoproductsOfShape.{w} W
  have := D‚ÇÇ.hasColimitsOfShape.{w} W
  exact isRightAdjoint_Œπ W Œ∫ hW

lemma isLocallyPresentable_isLocal
    (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular] [IsCardinalLocallyPresentable C Œ∫]
    [MorphismProperty.IsSmall.{w} W]
    (hW : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), W f ‚Üí IsCardinalPresentable X Œ∫ ‚àß IsCardinalPresentable Y Œ∫) :
  IsCardinalLocallyPresentable W.isLocal.FullSubcategory Œ∫ := by
    have := isRightAdjoint_Œπ_isLocal W Œ∫ hW
    have := MorphismProperty.isCardinalAccessible_Œπ_isLocal W Œ∫ hW
    exact (Adjunction.ofIsRightAdjoint W.isLocal.Œπ).isCardinalLocallyPresentable Œ∫

end MorphismProperty

end CategoryTheory
