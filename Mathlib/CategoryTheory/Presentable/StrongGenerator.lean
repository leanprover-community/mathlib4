/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Presentable.LocallyPresentable
import Mathlib.CategoryTheory.ObjectProperty.ColimitsCardinalClosure
import Mathlib.CategoryTheory.ObjectProperty.Equivalence
import Mathlib.CategoryTheory.Functor.Dense

/-!
# Locally presentable categories and strong generators

In this file, we show that a category is locally `Œ∫`-presentable iff
it is cocomplete and has a strong generator consisting of `Œ∫`-presentable objects.
This is theorem 1.20 in the book by Ad√°mek and Rosick√Ω.

In particular, if a category if locally `Œ∫`-presentable, if it also
locally `Œ∫'`-presentable for any regular cardinal `Œ∫'` such that `Œ∫ ‚â§ Œ∫'`.

We also show that in a locally presentable category,
the `Œ∫`-presentable objects form an essentially small category
for any regular cardinal `Œ∫`.

## References
* [Ad√°mek, J. and Rosick√Ω, J., *Locally presentable and accessible categories*][Adamek_Rosicky_1994]

-/

universe w v' v u' u

namespace CategoryTheory

-- to be moved
instance CostructuredArrow.locallySmall {C : Type u} {D : Type u'} [Category.{u'} C]
    [Category.{v'} D] (F : C ‚•§ D) (Y : D) [LocallySmall.{w} C] :
    LocallySmall.{w} (CostructuredArrow F Y) where
  hom_small T‚ÇÅ T‚ÇÇ := small_of_injective (f := CommaMorphism.left)
    (fun _ _ _ ‚Ü¶ by aesop)

-- to be moved
instance CostructuredArrow.small {C : Type u} [Category.{v} C] {D : Type u'} [Category.{v'} D]
    [Small.{w} C] [LocallySmall.{w} D]
    {F : C ‚•§ D} (X : D) : Small.{w} (CostructuredArrow F X) := by
  let œÜ (f : CostructuredArrow F X) : Œ£ (Y : C), F.obj Y ‚ü∂ X := ‚ü®_, f.hom‚ü©
  have hœÜ : Function.Injective œÜ := by
    intro f g h
    obtain ‚ü®Y, f, rfl‚ü© := CostructuredArrow.mk_surjective f
    obtain ‚ü®Z, g, rfl‚ü© := CostructuredArrow.mk_surjective g
    obtain rfl : Y = Z := congr_arg Sigma.fst h
    obtain rfl : f = g := by simpa [œÜ] using h
    rfl
  exact small_of_injective hœÜ

-- to be moved
instance CostructuredArrow.essentiallySmall {C : Type u} {D : Type u'} [Category.{v} C]
    [Category.{v'} D] (F : C ‚•§ D) (Y : D) [EssentiallySmall.{w} C] [LocallySmall.{w} D] :
    EssentiallySmall.{w} (CostructuredArrow F Y) := by
  rw [‚Üê essentiallySmall_congr
    (CostructuredArrow.pre (equivSmallModel.{w} C).inverse F Y).asEquivalence]
  exact essentiallySmall_of_small_of_locallySmall _

open Limits

variable {C : Type u} [Category.{v} C] {Œ∫ : Cardinal.{w}} [Fact Œ∫.IsRegular]

variable (Œ∫) in
lemma HasCardinalFilteredGenerators.of_dense
    [LocallySmall.{w} C] {J : Type w} [SmallCategory.{w} J]
    (F : J ‚•§ C) [F.IsDense] [‚àÄ j, IsCardinalPresentable (F.obj j) Œ∫]
    [‚àÄ (X : C), IsCardinalFiltered (CostructuredArrow F X) Œ∫] :
    HasCardinalFilteredGenerators C Œ∫ where
  exists_generators := by
    refine ‚ü®_, F.obj, inferInstance, fun X ‚Ü¶ ?_‚ü©
    refine ‚ü®Shrink.{w} (CostructuredArrow F X), inferInstance,
      IsCardinalFiltered.of_equivalence _ (Shrink.equivalence _),
      { diag := _
        Œπ := _
        isColimit := (F.canonicalColimitOfIsDense X).whiskerEquivalence
          (Shrink.equivalence _).symm }, fun j ‚Ü¶ ?_‚ü©
    obtain ‚ü®f, rfl‚ü© := (equivShrink _).surjective j
    exact ‚ü®_, ‚ü®(CostructuredArrow.proj _ _ ‚ãô F).mapIso
      (((Shrink.equivalence _).unitIso.app f).symm)‚ü©‚ü©

lemma HasCardinalFilteredGenerators.of_small_of_dense [LocallySmall.{w} C]
    (P : ObjectProperty C) [ObjectProperty.Small.{w} P] [P.Œπ.IsDense]
    (hP : P ‚â§ isCardinalPresentable C Œ∫)
    [‚àÄ X, IsCardinalFiltered (CostructuredArrow P.Œπ X) Œ∫] :
    HasCardinalFilteredGenerators C Œ∫ := by
  let J := ShrinkHoms (Shrink.{w} P.FullSubcategory)
  let e : P.FullSubcategory ‚âå J :=
    (Shrink.equivalence P.FullSubcategory).trans (ShrinkHoms.equivalence _)
  let F := e.inverse ‚ãô P.Œπ
  have (j : J) : IsCardinalPresentable (F.obj j) Œ∫ := hP _ (e.inverse.obj j).2
  have (X : C) : IsCardinalFiltered (CostructuredArrow F X) Œ∫ :=
    IsCardinalFiltered.of_equivalence _
      (CostructuredArrow.pre e.inverse P.Œπ X).asEquivalence.symm
  exact HasCardinalFilteredGenerators.of_dense Œ∫ F

lemma HasCardinalFilteredGenerators.of_essentiallySmall_of_dense [LocallySmall.{w} C]
    (P : ObjectProperty C) [ObjectProperty.EssentiallySmall.{w} P]
    [P.IsClosedUnderIsomorphisms] [P.Œπ.IsDense]
    [‚àÄ X, IsCardinalFiltered (CostructuredArrow P.Œπ X) Œ∫]
    (hP : P ‚â§ isCardinalPresentable C Œ∫) :
    HasCardinalFilteredGenerators C Œ∫ := by
    have := ObjectProperty.EssentiallySmall.exists_small.{w} P
    obtain ‚ü®P‚ÇÄ, _, rfl‚ü© := ObjectProperty.EssentiallySmall.exists_small.{w} P
    let e : P‚ÇÄ.FullSubcategory ‚âå P‚ÇÄ.isoClosure.FullSubcategory :=
      (ObjectProperty.ŒπOfLE P‚ÇÄ.le_isoClosure).asEquivalence
    have : P‚ÇÄ.Œπ.IsDense := inferInstanceAs (e.functor ‚ãô P‚ÇÄ.isoClosure.Œπ).IsDense
    have (X : C) : IsCardinalFiltered (CostructuredArrow P‚ÇÄ.Œπ X) Œ∫ :=
      IsCardinalFiltered.of_equivalence _
        ((CostructuredArrow.pre e.functor P‚ÇÄ.isoClosure.Œπ X).asEquivalence).symm
    exact HasCardinalFilteredGenerators.of_small_of_dense P‚ÇÄ
      (by rwa [‚Üê ObjectProperty.isoClosure_le_iff])

instance ObjectProperty.isCardinalFiltered_costructuredArrow_colimitsCardinalClosure_Œπ
    (P : ObjectProperty C) (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular]
    [HasColimitsOfSize.{w, w} C] (X : C) :
    IsCardinalFiltered (CostructuredArrow (P.colimitsCardinalClosure Œ∫).Œπ X) Œ∫ where
  nonempty_cocone {J} _ K hJ := ‚ü®by
    have := ObjectProperty.isClosedUnderColimitsOfShape_colimitsCardinalClosure P Œ∫ J hJ
    exact colimit.cocone K‚ü©

instance ObjectProperty.isFiltered_costructuredArrow_colimitsCardinalClosure_Œπ
    (P : ObjectProperty C) (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular]
    [HasColimitsOfSize.{w, w} C] (X : C) :
    IsFiltered (CostructuredArrow (P.colimitsCardinalClosure Œ∫).Œπ X) :=
  isFiltered_of_isCardinalDirected _ Œ∫

lemma IsStrongGenerator.isDense_colimitsCardinalClosure_Œπ
    [HasColimitsOfSize.{w, w} C] [LocallySmall.{w} C]
    {P : ObjectProperty C} [ObjectProperty.Small.{w} P] (hS‚ÇÅ : IsStrongGenerator P)
    (hS‚ÇÇ : P ‚â§ isCardinalPresentable C Œ∫) :
    (P.colimitsCardinalClosure Œ∫).Œπ.IsDense where
  isCanonicalColimit_eq_top := by
    ext X
    simp only [Pi.top_apply, ¬´Prop¬ª.top_eq_true, iff_true]
    have : HasColimitsOfShape (CostructuredArrow (P.colimitsCardinalClosure Œ∫).Œπ X) C := by
      obtain ‚ü®P‚ÇÄ, _, hP‚ÇÄ‚ü© :=
        ObjectProperty.EssentiallySmall.exists_small.{w} (P.colimitsCardinalClosure Œ∫)
      have h : P‚ÇÄ ‚â§ P.colimitsCardinalClosure Œ∫ := by simp only [hP‚ÇÄ, P‚ÇÄ.le_isoClosure]
      have : (ObjectProperty.ŒπOfLE h).IsEquivalence :=
        (ObjectProperty.isEquivalence_ŒπOfLE_iff h).2 (by rw [hP‚ÇÄ])
      let e : P‚ÇÄ.FullSubcategory ‚âå (P.colimitsCardinalClosure Œ∫).FullSubcategory :=
        (ObjectProperty.ŒπOfLE h).asEquivalence
      have : HasColimitsOfShape
          (CostructuredArrow (e.functor ‚ãô (P.colimitsCardinalClosure Œ∫).Œπ) X) C :=
        HasColimitsOfShape.of_essentiallySmall.{w} _ _
      apply hasColimitsOfShape_of_equivalence
        (CostructuredArrow.pre e.functor (ObjectProperty.Œπ _) X).asEquivalence
    let c := canonicalCocone (P.colimitsCardinalClosure Œ∫).Œπ X
    have : Mono (colimit.desc _ c) := by
      rw [hS‚ÇÅ.isSeparating.mono_iff]
      let Œ¶ := CostructuredArrow.proj
        (P.colimitsCardinalClosure Œ∫).Œπ X ‚ãô (P.colimitsCardinalClosure Œ∫).Œπ
      intro G hG (g‚ÇÅ : G ‚ü∂ colimit Œ¶) (g‚ÇÇ : G ‚ü∂ colimit Œ¶)
        (h : g‚ÇÅ ‚â´ colimit.desc Œ¶ c = g‚ÇÇ ‚â´ colimit.desc Œ¶ c)
      have : IsCardinalPresentable G Œ∫ := hS‚ÇÇ _ hG
      obtain ‚ü®j, œÜ‚ÇÅ, œÜ‚ÇÇ, rfl, rfl‚ü© :
          ‚àÉ (j :  CostructuredArrow (P.colimitsCardinalClosure Œ∫).Œπ X)
            (œÜ‚ÇÅ œÜ‚ÇÇ : G ‚ü∂ Œ¶.obj j), œÜ‚ÇÅ ‚â´ colimit.Œπ _ _ = g‚ÇÅ ‚àß œÜ‚ÇÇ ‚â´ colimit.Œπ _ _ = g‚ÇÇ := by
        obtain ‚ü®j‚ÇÅ, f‚ÇÅ, hf‚ÇÅ‚ü© :=
          IsCardinalPresentable.exists_hom_of_isColimit Œ∫ (colimit.isColimit _) g‚ÇÅ
        obtain ‚ü®j‚ÇÇ, f‚ÇÇ, hf‚ÇÇ‚ü© :=
          IsCardinalPresentable.exists_hom_of_isColimit Œ∫ (colimit.isColimit _) g‚ÇÇ
        exact ‚ü®IsFiltered.max j‚ÇÅ j‚ÇÇ, f‚ÇÅ ‚â´ Œ¶.map (IsFiltered.leftToMax j‚ÇÅ j‚ÇÇ),
          f‚ÇÇ ‚â´ Œ¶.map (IsFiltered.rightToMax j‚ÇÅ j‚ÇÇ), by simpa, by simpa‚ü©
      simp only [Category.assoc, colimit.Œπ_desc] at h
      have : (P.colimitsCardinalClosure Œ∫).IsClosedUnderColimitsOfShape WalkingParallelPair := by
        apply ObjectProperty.isClosedUnderColimitsOfShape_colimitsCardinalClosure
        refine .of_le ?_ (Cardinal.IsRegular.aleph0_le Fact.out)
        simp only [hasCardinalLT_aleph0_iff]
        infer_instance
      let E : (P.colimitsCardinalClosure Œ∫).FullSubcategory :=
        ‚ü®coequalizer œÜ‚ÇÅ œÜ‚ÇÇ, by
          apply ObjectProperty.prop_colimit
          rintro (_ | _)
          ¬∑ exact P.le_colimitsCardinalClosure _ _ hG
          ¬∑ exact j.left.2‚ü©
      let a : (P.colimitsCardinalClosure Œ∫).Œπ.obj E ‚ü∂ X := coequalizer.desc (c.Œπ.app j) h
      let œà : j ‚ü∂ CostructuredArrow.mk a :=
        CostructuredArrow.homMk (coequalizer.œÄ _ _) (coequalizer.œÄ_desc _ _)
      rw [‚Üê colimit.w Œ¶ œà]
      apply coequalizer.condition_assoc
    have : IsIso (colimit.desc _ c) := hS‚ÇÅ.isIso_of_mono _ (fun g œÜ ‚Ü¶ by
      let Œ≥ : CostructuredArrow (P.colimitsCardinalClosure Œ∫).Œπ X :=
        CostructuredArrow.mk (Y := ‚ü®g.1, P.le_colimitsCardinalClosure _ _ g.2‚ü©) (by exact œÜ)
      refine ‚ü®colimit.Œπ (CostructuredArrow.proj _ _ ‚ãô (P.colimitsCardinalClosure Œ∫).Œπ) Œ≥, ?_‚ü©
      dsimp
      rw [colimit.Œπ_desc]
      rfl)
    exact ‚ü®CanonicalColimis.ofIsIso _ _‚ü©

lemma ObjectProperty.colimitsCardinalClosure_le_isCardinalPresentable
    [LocallySmall.{w} C] (P : ObjectProperty C) (hP : P ‚â§ isCardinalPresentable C Œ∫) :
    P.colimitsCardinalClosure Œ∫ ‚â§ isCardinalPresentable C Œ∫ :=
  P.colimitsCardinalClosure_le Œ∫
    (fun _ _ hJ ‚Ü¶ isClosedUnderColimitsOfShape_isCardinalPresentable C hJ) hP

lemma IsStrongGenerator.colimitsCardinalClosure_eq_isCardinalPresentable
    [HasColimitsOfSize.{w, w} C] [LocallySmall.{w} C]
    {P : ObjectProperty C} [ObjectProperty.Small.{w} P] (hS‚ÇÅ : IsStrongGenerator P)
    (hS‚ÇÇ : P ‚â§ isCardinalPresentable C Œ∫) :
    isCardinalPresentable C Œ∫ = P.colimitsCardinalClosure Œ∫ := by
  refine le_antisymm ?_ (P.colimitsCardinalClosure_le_isCardinalPresentable hS‚ÇÇ)
  have := hS‚ÇÅ.isDense_colimitsCardinalClosure_Œπ hS‚ÇÇ
  intro X hX
  rw [isCardinalPresentable_iff] at hX
  rw [‚Üê (P.colimitsCardinalClosure Œ∫).retractClosure_eq_self ]
  obtain ‚ü®j, œÜ, hœÜ‚ü© := IsCardinalPresentable.exists_hom_of_isColimit Œ∫
    ((P.colimitsCardinalClosure Œ∫).Œπ.canonicalColimitOfIsDense X) (ùüô X)
  exact ‚ü®_, j.left.2, ‚ü®{ i := _, r := _, retract := hœÜ }‚ü©‚ü©

namespace IsCardinalLocallyPresentable

variable (C Œ∫) in
lemma iff_exists_isStrongGenerator [HasColimitsOfSize.{w, w} C] [LocallySmall.{w} C] :
    IsCardinalLocallyPresentable C Œ∫ ‚Üî
      ‚àÉ (P : ObjectProperty C) (_ : ObjectProperty.Small.{w} P), IsStrongGenerator P ‚àß
        P ‚â§ isCardinalPresentable C Œ∫ := by
  refine ‚ü®fun _ ‚Ü¶ ?_, fun ‚ü®P, _, hS‚ÇÅ, hS‚ÇÇ‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®Œπ, G, hG‚ü© := HasCardinalFilteredGenerators.exists_generators C Œ∫
    refine ‚ü®Set.range G, inferInstanceAs (Small.{w} (Set.range G)), hG.isStrongGenerator, ?_‚ü©
    rintro _ ‚ü®i, rfl‚ü©
    exact hG.isCardinalPresentable i
  ¬∑ have := hS‚ÇÅ.isDense_colimitsCardinalClosure_Œπ hS‚ÇÇ
    have := HasCardinalFilteredGenerators.of_essentiallySmall_of_dense
      (P.colimitsCardinalClosure Œ∫) (P.colimitsCardinalClosure_le_isCardinalPresentable hS‚ÇÇ)
    constructor

variable [IsCardinalLocallyPresentable C Œ∫]

variable (C) in
lemma of_le {Œ∫' : Cardinal.{w}} [Fact Œ∫'.IsRegular]
    (h : Œ∫ ‚â§ Œ∫') :
    IsCardinalLocallyPresentable C Œ∫' := by
  rw [iff_exists_isStrongGenerator]
  obtain ‚ü®S, _, h‚ÇÅ, h‚ÇÇ‚ü© := (iff_exists_isStrongGenerator C Œ∫).1 inferInstance
  exact ‚ü®S, inferInstance, h‚ÇÅ, by
    refine h‚ÇÇ.trans (isCardinalPresentable_monotone _ h)‚ü©

instance : ObjectProperty.EssentiallySmall.{w} (isCardinalPresentable C Œ∫) := by
  obtain ‚ü®Œπ, G, hG‚ü© := HasCardinalFilteredGenerators.exists_generators C Œ∫
  have : ObjectProperty.Small.{w} (Set.range G) :=
    inferInstanceAs (Small.{w} (Set.range G))
  rw [hG.isStrongGenerator.colimitsCardinalClosure_eq_isCardinalPresentable (Œ∫ := Œ∫)
    (by rintro _ ‚ü®i, rfl‚ü©; exact hG.isCardinalPresentable i)]
  infer_instance

instance : (isCardinalPresentable C Œ∫).Œπ.IsDense := by
  obtain ‚ü®Œπ, G, hG‚ü© := HasCardinalFilteredGenerators.exists_generators C Œ∫
  have : ObjectProperty.Small.{w} (Set.range G) :=
    inferInstanceAs (Small.{w} (Set.range G))
  rw [hG.isStrongGenerator.colimitsCardinalClosure_eq_isCardinalPresentable (Œ∫ := Œ∫)
    (by rintro _ ‚ü®i, rfl‚ü©; exact hG.isCardinalPresentable i)]
  refine IsStrongGenerator.isDense_colimitsCardinalClosure_Œπ
    (hG.isStrongGenerator) (by rintro _ ‚ü®_, rfl‚ü©; apply hG.isCardinalPresentable)

end IsCardinalLocallyPresentable

namespace IsLocallyPresentable

instance essentiallySmall_isCardinalPresentable
    [IsLocallyPresentable.{w} C] :
    ObjectProperty.EssentiallySmall.{w} (isCardinalPresentable C Œ∫) := by
  obtain ‚ü®Œ∫‚ÇÄ, _, _‚ü©  := IsLocallyPresentable.exists_cardinal.{w} C
  by_cases h : Œ∫‚ÇÄ ‚â§ Œ∫
  ¬∑ have := IsCardinalLocallyPresentable.of_le C h
    infer_instance
  ¬∑ simp only [not_le] at h
    exact ObjectProperty.EssentiallySmall.of_le (isCardinalPresentable_monotone C h.le)

end IsLocallyPresentable

end CategoryTheory
