/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Presentable.Basic
public import Mathlib.CategoryTheory.Limits.Types.Filtered
public import Mathlib.CategoryTheory.Types.Set

/-!
# Presentable objects in Type

In this file, we show that if `Œ∫ : Cardinal.{u}` is a regular cardinal,
then `X : Type u` is `Œ∫`-presentable in the category of types iff
`HasCardinalLT X Œ∫` holds, i.e. the cardinal number of `X` is less than `Œ∫`.

-/

@[expose] public section

universe u

open CategoryTheory Limits Opposite

namespace HasCardinalLT

variable (X : Type u) (Œ∫ : Cardinal.{u})

variable {X Œ∫} in
lemma isCardinalPresentable (hX : HasCardinalLT X Œ∫) [Fact Œ∫.IsRegular] :
    IsCardinalPresentable X Œ∫ where
  preservesColimitOfShape J _ _ :=
    ‚ü®fun {F} ‚Ü¶ ‚ü®fun {c} hc ‚Ü¶ ‚ü®by
      have := isFiltered_of_isCardinalFiltered J Œ∫
      refine Types.FilteredColimit.isColimitOf' _ _ (fun f ‚Ü¶ ?_) (fun j f g h ‚Ü¶ ?_)
      ¬∑ choose j g hg using fun x ‚Ü¶ Types.jointly_surjective_of_isColimit hc (f x)
        refine ‚ü®IsCardinalFiltered.max j hX,
          fun x ‚Ü¶ F.map (IsCardinalFiltered.toMax j hX x) (g x), ?_‚ü©
        dsimp
        ext x
        dsimp at j g hg x ‚ä¢
        rw [‚Üê hg]
        exact congr_fun (c.w (IsCardinalFiltered.toMax j hX x)).symm (g x)
      ¬∑ choose k a hk using fun x ‚Ü¶
          (Types.FilteredColimit.isColimit_eq_iff' hc _ _).1 (congr_fun h x)
        dsimp at f g h k a hk ‚ä¢
        obtain ‚ü®l, b, c, hl‚ü© : ‚àÉ (l : J) (c : j ‚ü∂ l) (b : ‚àÄ x, k x ‚ü∂ l),
            ‚àÄ x, a x ‚â´ b x = c := by
          let œÜ (x : X) : j ‚ü∂ IsCardinalFiltered.max k hX :=
            a x ‚â´ IsCardinalFiltered.toMax k hX x
          exact ‚ü®IsCardinalFiltered.coeq œÜ hX,
            IsCardinalFiltered.toCoeq œÜ hX,
            fun x ‚Ü¶ IsCardinalFiltered.toMax k hX x ‚â´ IsCardinalFiltered.coeqHom œÜ hX,
            fun x ‚Ü¶ by simpa [œÜ] using IsCardinalFiltered.coeq_condition œÜ hX x‚ü©
        exact ‚ü®l, b, by ext x; simp [‚Üê hl x, hk]‚ü©‚ü©‚ü©‚ü©

/-- Given `X : Type u` and `Œ∫ : Cardinal.{u} X`, this is the preordered type
of subsets of `X` of cardinality `< Œ∫`. -/
protected abbrev Set := { A : Set X // HasCardinalLT A Œ∫ }

namespace Set

instance [Fact Œ∫.IsRegular] :
    IsCardinalFiltered (HasCardinalLT.Set X Œ∫) Œ∫ :=
  isCardinalFiltered_preorder _ _
    (fun Œπ A hŒπ ‚Ü¶ ‚ü®‚ü®‚ãÉ (i : Œπ), (A i).val,
      hasCardinalLT_iUnion _
        (by rwa [hasCardinalLT_iff_cardinal_mk_lt]) (fun i ‚Ü¶ (A i).prop)‚ü©,
      le_iSup (fun i ‚Ü¶ (A i).1)‚ü©)

instance [Fact Œ∫.IsRegular] :
    IsFiltered (HasCardinalLT.Set X Œ∫) :=
  isFiltered_of_isCardinalFiltered _ Œ∫

lemma isFiltered_of_aleph0_le (hŒ∫ : Cardinal.aleph0 ‚â§ Œ∫) :
    IsFiltered (HasCardinalLT.Set X Œ∫) where
  nonempty := ‚ü®‚ü®‚àÖ, hasCardinalLT_of_finite _ _ hŒ∫‚ü©‚ü©
  toIsFilteredOrEmpty := by
    have : IsDirectedOrder (HasCardinalLT.Set X Œ∫) :=
      ‚ü®fun A B ‚Ü¶ ‚ü®‚ü®A.val ‚à™ B.val, hasCardinalLT_union hŒ∫ A.prop B.prop‚ü©,
        Set.subset_union_left, Set.subset_union_right‚ü©‚ü©
    exact isFilteredOrEmpty_of_directed_le _

/-- The functor `HasCardinalLT.Set X Œ∫ ‚•§ Type u` which sends a subset of `X`
of cardinality `Œ∫` to the corresponding subtype. -/
@[simps!]
def functor : HasCardinalLT.Set X Œ∫ ‚•§ Type u :=
  Monotone.functor (f := Subtype.val) (by tauto) ‚ãô Set.functorToTypes (X := X)

/-- The cocone for `Set.functor X Œ∫ : HasCardinalLT.Set X Œ∫ ‚•§ Type u` with point `X`. -/
@[simps]
def cocone : Cocone (Set.functor X Œ∫) where
  pt := X
  Œπ.app _ := Subtype.val

/-- Any type `X` is the (filtered) colimit of its subsets of cardinality `< Œ∫`
when `Œ∫` is an infinite cardinal. (This colimit is `Œ∫`-filtered when `Œ∫` is
a regular cardinal.) -/
noncomputable def isColimitCocone
    (hŒ∫ : Cardinal.aleph0 ‚â§ Œ∫) : IsColimit (cocone X Œ∫) := by
  have := isFiltered_of_aleph0_le X Œ∫ hŒ∫
  refine Types.FilteredColimit.isColimitOf' _ _ (fun x ‚Ü¶ ?_) ?_
  ¬∑ exact ‚ü®‚ü®{x}, hasCardinalLT_of_finite _ _ hŒ∫‚ü©, ‚ü®x, by simp‚ü©, rfl‚ü©
  ¬∑ rintro A ‚ü®x, hx‚ü© ‚ü®y, hy‚ü© rfl
    exact ‚ü®A, ùüô _, rfl‚ü©

end Set

end HasCardinalLT

namespace CategoryTheory

namespace Types

variable {X : Type u}

lemma isCardinalPresentable_iff (Œ∫ : Cardinal.{u}) [Fact Œ∫.IsRegular] :
    IsCardinalPresentable X Œ∫ ‚Üî HasCardinalLT X Œ∫ := by
  refine ‚ü®fun _ ‚Ü¶ ?_, fun hX ‚Ü¶ hX.isCardinalPresentable‚ü©
  have := preservesColimitsOfShape_of_isCardinalPresentable X Œ∫
  obtain ‚ü®‚ü®A, hA‚ü©, f, hf‚ü© := Types.jointly_surjective_of_isColimit
    (isColimitOfPreserves (coyoneda.obj (op X))
      (HasCardinalLT.Set.isColimitCocone X Œ∫
        (Cardinal.IsRegular.aleph0_le Fact.out))) (ùüô X)
  obtain rfl : A = .univ := by
    ext x
    have := congr_fun hf x
    dsimp at this
    rw [‚Üê this]
    simp
  exact (hasCardinalLT_iff_of_equiv (Equiv.Set.univ X) _).1 hA

instance (X : Type u) : IsPresentable.{u} X := by
  obtain ‚ü®Œ∫, hŒ∫, hX‚ü© := HasCardinalLT.exists_regular_cardinal.{u} X
  have : Fact Œ∫.IsRegular := ‚ü®hŒ∫‚ü©
  have := hX.isCardinalPresentable
  exact isPresentable_of_isCardinalPresentable X Œ∫

end Types

end CategoryTheory
