/-
Copyright (c) 2025 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Presentable.Basic
import Mathlib.CategoryTheory.Limits.Types.Filtered
import Mathlib.CategoryTheory.Types.Set

/-!
# Presentable objects in Type

In this file, we show that if `Îº : Cardinal.{u}` is a regular cardinal,
then `X : Type u` is `Îº`-presentable in the category of types iff
`HasCardinalLT X Îº` holds.

-/

universe u

open CategoryTheory Limits Opposite

namespace HasCardinalLT

variable (X : Type u) (Îº : Cardinal.{u})

variable {X Îº} in
lemma isCardinalPresentable (hX : HasCardinalLT X Îº) [Fact Îº.IsRegular] :
    IsCardinalPresentable X Îº where
  preservesColimitOfShape J _ _ :=
    âŸ¨fun {F} â†¦ âŸ¨fun {c} hc â†¦ âŸ¨by
      have := isFiltered_of_isCardinalFiltered J Îº
      refine Types.FilteredColimit.isColimitOf' _ _ (fun f â†¦ ?_) (fun j f g h â†¦ ?_)
      Â· choose j g hg using fun x â†¦ Types.jointly_surjective_of_isColimit hc (f x)
        refine âŸ¨IsCardinalFiltered.max j hX,
          fun x â†¦ F.map (IsCardinalFiltered.toMax j hX x) (g x), ?_âŸ©
        dsimp
        ext x
        dsimp at j g hg x âŠ¢
        rw [â† hg]
        exact congr_fun (c.w (IsCardinalFiltered.toMax j hX x)).symm (g x)
      Â· choose k a hk using fun x â†¦
          (Types.FilteredColimit.isColimit_eq_iff' hc _ _).1 (congr_fun h x)
        dsimp at f g h k a hk âŠ¢
        obtain âŸ¨l, b, c, hlâŸ© : âˆƒ (l : J) (c : j âŸ¶ l) (b : âˆ€ x, k x âŸ¶ l),
            âˆ€ x, a x â‰« b x = c := by
          let Ï† (x : X) : j âŸ¶ IsCardinalFiltered.max k hX :=
            a x â‰« IsCardinalFiltered.toMax k hX x
          exact âŸ¨IsCardinalFiltered.coeq Ï† hX,
            IsCardinalFiltered.toCoeq Ï† hX,
            fun x â†¦ IsCardinalFiltered.toMax k hX x â‰« IsCardinalFiltered.coeqHom Ï† hX,
            fun x â†¦ by simpa [Ï†] using IsCardinalFiltered.coeq_condition Ï† hX xâŸ©
        exact âŸ¨l, b, by ext x; simp [â† hl x, hk]âŸ©âŸ©âŸ©âŸ©

/-- Given `X : Type u` and `Îº : Cardinal.{u} X`, this is the preordered type
of subsets of `X` of cardinality `< Îº`. -/
protected abbrev Set := { A : Set X // HasCardinalLT A Îº }

namespace Set

instance [Fact Îº.IsRegular] :
    IsCardinalFiltered (HasCardinalLT.Set X Îº) Îº :=
  isCardinalFiltered_preorder _ _
    (fun Î¹ A hÎ¹ â†¦ âŸ¨âŸ¨â‹ƒ (i : Î¹), (A i).val,
      hasCardinalLT_iUnion _
        (by rwa [hasCardinalLT_iff_cardinal_mk_lt]) (fun i â†¦ (A i).prop)âŸ©,
      le_iSup (fun i â†¦ (A i).1)âŸ©)

instance [Fact Îº.IsRegular] :
    IsFiltered (HasCardinalLT.Set X Îº) :=
  isFiltered_of_isCardinalFiltered _ Îº

lemma isFiltered_of_aleph0_le (hÎº : Cardinal.aleph0 â‰¤ Îº) :
    IsFiltered (HasCardinalLT.Set X Îº) where
  nonempty := âŸ¨âŸ¨âˆ…, hasCardinalLT_of_finite _ _ hÎºâŸ©âŸ©
  toIsFilteredOrEmpty := by
    have : IsDirected (HasCardinalLT.Set X Îº) (Â· â‰¤ Â·) :=
      âŸ¨fun A B â†¦ âŸ¨âŸ¨A.val âˆª B.val, hasCardinalLT_union hÎº A.prop B.propâŸ©,
        Set.subset_union_left, Set.subset_union_rightâŸ©âŸ©
    exact isFilteredOrEmpty_of_directed_le _

/-- The functor `HasCardinalLT.Set X Îº â¥¤ Type u` which sends a subset of `X`
of cardinality `Îº` to the corresponding subtype. -/
@[simps!]
def functor : HasCardinalLT.Set X Îº â¥¤ Type u :=
  Monotone.functor (f := Subtype.val) (by tauto) â‹™ Set.functorToTypes (X := X)

/-- The cocone for `Set.functor X Îº : HasCardinalLT.Set X Îº â¥¤ Type u` with point `X`. -/
@[simps]
def cocone : Cocone (Set.functor X Îº) where
  pt := X
  Î¹.app _ := Subtype.val

/-- Any type `X` is the (filtered) colimit of its subsets of cardinality `< Îº`
when `Îº` is an infinite cardinal. (This colimit is `Îº`-filtered when `Îº` is
a regular cardinal.) -/
noncomputable def isColimitCocone
    (hÎº : Cardinal.aleph0 â‰¤ Îº) : IsColimit (cocone X Îº) := by
  have := isFiltered_of_aleph0_le X Îº hÎº
  refine Types.FilteredColimit.isColimitOf' _ _ (fun x â†¦ ?_) ?_
  Â· exact âŸ¨âŸ¨{x}, hasCardinalLT_of_finite _ _ hÎºâŸ©, âŸ¨x, by simpâŸ©, rflâŸ©
  Â· rintro A âŸ¨x, hxâŸ© âŸ¨y, hyâŸ© rfl
    exact âŸ¨A, ğŸ™ _, rflâŸ©

end Set

end HasCardinalLT

namespace CategoryTheory

namespace Types

variable {X : Type u} (Îº : Cardinal.{u}) [Fact Îº.IsRegular]

lemma isCardinalPresentable_iff :
    IsCardinalPresentable X Îº â†” HasCardinalLT X Îº := by
  refine âŸ¨fun _ â†¦ ?_, fun hX â†¦ hX.isCardinalPresentableâŸ©
  have := preservesColimitsOfShape_of_isCardinalPresentable X Îº
  obtain âŸ¨âŸ¨A, hAâŸ©, f, hfâŸ© := Types.jointly_surjective_of_isColimit
    (isColimitOfPreserves (coyoneda.obj (op X))
      (HasCardinalLT.Set.isColimitCocone X Îº
        (Cardinal.IsRegular.aleph0_le Fact.out))) (ğŸ™ X)
  obtain rfl : A = .univ := by
    ext x
    have := congr_fun hf x
    dsimp at f hf this
    simp only [Set.mem_univ, iff_true]
    rw [â† this]
    exact (f x).2
  exact (hasCardinalLT_iff_of_equiv (Equiv.Set.univ X) _).1 hA

instance (X : Type u) : IsPresentable.{u} X := by
  obtain âŸ¨Îº, hÎº, hXâŸ© := HasCardinalLT.exists_regular_cardinal.{u} X
  have : Fact Îº.IsRegular := âŸ¨hÎºâŸ©
  have := hX.isCardinalPresentable
  exact isPresentable_of_isCardinalPresentable X Îº

end Types

end CategoryTheory
