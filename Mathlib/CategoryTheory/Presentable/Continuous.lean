/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.ObjectProperty.FunctorCategory.Presheaf
import Mathlib.CategoryTheory.Limits.FullSubcategory
import Mathlib.CategoryTheory.Presentable.OrthogonalReflection
import Mathlib.CategoryTheory.Presentable.Presheaf
import Mathlib.CategoryTheory.Presentable.Type

/-!
# `Œ∫`-continuous functors

Given categories `C`, `D` and a regular cardinal `Œ∫ : Cardinal.{w}`, we define
`isCardinalContinuous C D Œ∫ : ObjectProperty (C ‚•§ D)` as the property
of functors which preserves limits indexed by categories `J`
such that `HasCardinalLT (Arrow J) Œ∫`.

We show that if `C` is an essentially `w`-small category, then
the category of `Œ∫`-continuous functors `C·µí·µñ ‚•§ Type w` is locally
`Œ∫`-presentable. This is done by showing that `Œ∫`-continuous
functors `C·µí·µñ ‚•§ Type w` are exactly the objects that are local with
respect to a suitable `w`-small family of morphisms.

## TODO (@joelriou)
* Show that any locally `Œ∫`-presentable category is equivalent to
a category of `Œ∫`-continuous presheaves.

-/

universe w v v' v'' u u' u''

namespace CategoryTheory

open Limits Opposite

namespace Functor

section

variable {C : Type u} [Category.{v} C] {D : Type u'} [Category.{v'} D]

variable (C D) in
def isCardinalContinuous (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular] : ObjectProperty (C ‚•§ D) :=
  ‚®Ö (J : Type w) (_ : Category.{w} J) (_ : HasCardinalLT (Arrow J) Œ∫),
    preservesLimitsOfShape C D J

lemma isCardinalContinuous_iff (F : C ‚•§ D) (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular] :
    isCardinalContinuous C D Œ∫ F ‚Üî
      ‚àÄ (J : Type w) [SmallCategory J] (_ : HasCardinalLT (Arrow J) Œ∫),
        PreservesLimitsOfShape J F := by
  simp [isCardinalContinuous]

lemma isCardinalContinuous.preservesColimitsOfShape {F : C ‚•§ D}
    {Œ∫ : Cardinal.{w}} [Fact Œ∫.IsRegular] (hF : isCardinalContinuous C D Œ∫ F)
    (J : Type w) [SmallCategory.{w} J] (hŒ∫ : HasCardinalLT (Arrow J) Œ∫) :
    PreservesLimitsOfShape J F :=
  (isCardinalContinuous_iff F Œ∫).1 hF J hŒ∫

instance (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular] :
    (isCardinalContinuous C D Œ∫).IsClosedUnderIsomorphisms := by
  dsimp only [isCardinalContinuous]
  infer_instance

@[simp]
lemma isCardinalContinuous_precomp_iff {C' : Type u''} [Category.{v''} C']
    (G : C' ‚•§ C) [G.IsEquivalence] (F : C ‚•§ D) (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular] :
    isCardinalContinuous C' D Œ∫ (G ‚ãô F) ‚Üî isCardinalContinuous C D Œ∫ F := by
  simp only [isCardinalContinuous_iff]
  refine forall_congr' (fun J ‚Ü¶ forall_congr' (fun _ ‚Ü¶ forall_congr' (fun hŒ∫ ‚Ü¶ ?_)))
  refine ‚ü®fun _ ‚Ü¶ ?_, fun _ ‚Ü¶ inferInstance‚ü©
  let e : G.inv ‚ãô G ‚ãô F ‚âÖ F := (Functor.associator _ _ _).symm ‚â™‚â´
      Functor.isoWhiskerRight G.asEquivalence.counitIso _ ‚â™‚â´ F.leftUnitor
  exact preservesLimitsOfShape_of_natIso e

def isCardinalContinuousCongrLeft {C' : Type u'} [Category.{v'} C']
    (e : C ‚âå C') (D : Type u'') [Category.{v''} D]
    (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular] :
    (isCardinalContinuous C D Œ∫).FullSubcategory ‚âå
      (isCardinalContinuous C' D Œ∫).FullSubcategory :=
  e.congrLeft.congrFullSubcategory (by aesop)

end

end Functor

namespace Presheaf

open Functor

section Small

variable (C : Type w) [SmallCategory C] (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular]

abbrev isCardinalContinuousMorphismProperty : MorphismProperty (C·µí·µñ ‚•§ Type w) :=
  ‚®Ü (J) (F : SmallCategoryCardinalLT.categoryFamily Œ∫ J ‚•§ C·µí·µñ),
    MorphismProperty.ofHoms (Presheaf.preservesLimitHomFamily F)

example : MorphismProperty.IsSmall.{w}
    (isCardinalContinuousMorphismProperty C Œ∫) := by
  infer_instance

lemma isCardinalContinuous_eq_isLocal :
    isCardinalContinuous C·µí·µñ (Type w) Œ∫ =
      (isCardinalContinuousMorphismProperty.{w} C Œ∫).isLocal := by
  trans ‚®Ö (J : SmallCategoryCardinalLT Œ∫),
    preservesLimitsOfShape C·µí·µñ (Type w) (SmallCategoryCardinalLT.categoryFamily Œ∫ J)
  ¬∑ refine le_antisymm ?_ ?_
    ¬∑ simp only [le_iInf_iff]
      intro J P hP
      simpa using hP.preservesColimitsOfShape _ (SmallCategoryCardinalLT.hasCardinalLT Œ∫ J)
    ¬∑ dsimp [isCardinalContinuous]
      simp only [le_iInf_iff]
      intro J _ hJ
      obtain ‚ü®J', ‚ü®e‚ü©‚ü© := SmallCategoryCardinalLT.exists_equivalence Œ∫ J hJ
      rw [‚Üê congr_preservesLimitsOfShape _ _ e]
      apply iInf_le
  ¬∑ simp [preservesLimitsOfShape_eq_isLocal]

instance (X : C) : IsCardinalPresentable (shrinkYoneda.{w}.obj X) Œ∫ where
  preservesColimitOfShape J _ := ‚ü®fun {F} ‚Ü¶ ‚ü®fun {c} hc ‚Ü¶ ‚ü®by
    have := isFiltered_of_isCardinalFiltered J Œ∫
    refine Types.FilteredColimit.isColimitOf' _ _ (fun f ‚Ü¶ ?_) (fun j f‚ÇÅ f‚ÇÇ hf ‚Ü¶ ?_)
    ¬∑ obtain ‚ü®x, rfl‚ü© := shrinkYonedaEquiv.symm.surjective f
      obtain ‚ü®j, y, rfl‚ü© := Types.jointly_surjective_of_isColimit
        (isColimitOfPreserves ((evaluation _ _).obj (op X)) hc) x
      exact ‚ü®j, shrinkYonedaEquiv.symm y,
        shrinkYonedaEquiv.injective (by simp [shrinkYonedaEquiv_comp])‚ü©
    ¬∑ obtain ‚ü®x‚ÇÅ, rfl‚ü© := shrinkYonedaEquiv.symm.surjective f‚ÇÅ
      obtain ‚ü®x‚ÇÇ, rfl‚ü© := shrinkYonedaEquiv.symm.surjective f‚ÇÇ
      dsimp at hf ‚ä¢
      rw [shrinkYonedaEquiv_symm_comp, shrinkYonedaEquiv_symm_comp] at hf
      simp only [EmbeddingLike.apply_eq_iff_eq] at hf
      obtain ‚ü®l, a, hl‚ü© := (Types.FilteredColimit.isColimit_eq_iff'
          (isColimitOfPreserves ((evaluation _ _).obj (op X)) hc) x‚ÇÅ x‚ÇÇ).1 hf
      dsimp at hl
      refine ‚ü®l, a, ?_‚ü©
      rw [shrinkYonedaEquiv_symm_comp, shrinkYonedaEquiv_symm_comp, hl]‚ü©‚ü©‚ü©

instance (J : SmallCategoryCardinalLT Œ∫)
    (F : SmallCategoryCardinalLT.categoryFamily Œ∫ J ‚•§ C·µí·µñ) :
    IsCardinalPresentable (preservesLimitHomFamilySrc F) Œ∫ := by
  apply (config := { allowSynthFailures := true }) isCardinalPresentable_of_isColimit
    _ (colimit.isColimit (F.leftOp ‚ãô shrinkYoneda.{w}))
  ¬∑ simpa using J.prop
  ¬∑ intro
    dsimp
    infer_instance

instance (J : SmallCategoryCardinalLT Œ∫)
    (F : SmallCategoryCardinalLT.categoryFamily Œ∫ J ‚•§ C·µí·µñ) (hF) :
    IsCardinalPresentable (preservesLimitHomFamilyTgt F hF) Œ∫ := by
  dsimp [preservesLimitHomFamilyTgt]
  infer_instance

lemma isCardinalPresentable_isCardinalContinuousMorphismProperty_src_tgt
    ‚¶ÉX Y : C·µí·µñ ‚•§ Type w‚¶Ñ (f : X ‚ü∂ Y) (hf : isCardinalContinuousMorphismProperty C Œ∫ f) :
    IsCardinalPresentable X Œ∫ ‚àß IsCardinalPresentable Y Œ∫ := by
  simp only [isCardinalContinuousMorphismProperty, MorphismProperty.iSup_iff] at hf
  obtain ‚ü®J, F, ‚ü®hF‚ü©‚ü© := hf
  constructor <;> infer_instance

instance : IsCardinalLocallyPresentable
    (isCardinalContinuous C·µí·µñ (Type w) Œ∫).FullSubcategory Œ∫ := by
  rw [isCardinalContinuous_eq_isLocal]
  apply MorphismProperty.isLocallyPresentable_isLocal
  apply isCardinalPresentable_isCardinalContinuousMorphismProperty_src_tgt

instance : (isCardinalContinuous C·µí·µñ (Type w) Œ∫).Œπ.IsRightAdjoint := by
  rw [isCardinalContinuous_eq_isLocal]
  apply MorphismProperty.isRightAdjoint_Œπ_isLocal _ Œ∫
  apply isCardinalPresentable_isCardinalContinuousMorphismProperty_src_tgt

instance : HasLimitsOfSize.{w, w} (isCardinalContinuous C·µí·µñ (Type w) Œ∫).FullSubcategory := by
  rw [isCardinalContinuous_eq_isLocal]
  exact ‚ü®inferInstance‚ü©

variable {C Œ∫} in
lemma isCardinalFiltered_costructuredArrow_yoneda
    {P : C·µí·µñ ‚•§ Type w} (hP : isCardinalContinuous C·µí·µñ (Type w) Œ∫ P)
    (hC : ‚àÄ (J : Type w) [SmallCategory J] (_ : HasCardinalLT (Arrow J) Œ∫),
      HasColimitsOfShape J C) :
    IsCardinalFiltered (CostructuredArrow yoneda P) Œ∫ where
  nonempty_cocone {J _} F hJ := by
    have := hC J hJ
    have := hP.preservesColimitsOfShape J·µí·µñ (by simpa)
    let s : ((F ‚ãô CostructuredArrow.proj _ _).op ‚ãô P).sections :=
      { val j := yonedaEquiv (F := P) (F.obj j.unop).hom
        property {j j'} g := by
          dsimp
          rw [‚Üê CostructuredArrow.w (F.map g.unop), yonedaEquiv_naturality] }
    obtain ‚ü®f, hf‚ü© := ((Types.isLimit_iff_bijective_sectionOfCone _).1
      ‚ü®isLimitOfPreserves P (colimit.isColimit _).op‚ü©).2 s
    replace hf (j : J) := congr_fun (congr_arg Subtype.val hf) (op j)
    dsimp [s] at hf
    exact ‚ü®CostructuredArrow.mk (yonedaEquiv.symm f),
      { app j := CostructuredArrow.homMk (colimit.Œπ (F ‚ãô CostructuredArrow.proj _ _) j)
            (yonedaEquiv.injective (by simp [‚Üê hf j, yonedaEquiv_apply]))
        naturality j j' g := by
          ext
          simpa [-colimit.w] using colimit.w (F ‚ãô CostructuredArrow.proj _ _) g }‚ü©

end Small

section EssentiallySmall

variable (C : Type u) [Category.{v} C] [EssentiallySmall.{w} C]
  (Œ∫ : Cardinal.{w}) [Fact Œ∫.IsRegular]

instance : IsCardinalLocallyPresentable
      (isCardinalContinuous C (Type w) Œ∫).FullSubcategory Œ∫ :=
  (isCardinalContinuousCongrLeft ((equivSmallModel.{w} C·µí·µñ).op.symm.trans
    (opOpEquivalence C)) (Type w) Œ∫).isCardinalLocallyPresentable Œ∫

instance : IsLocallyPresentable.{w} (isCardinalContinuous C (Type w) Œ∫).FullSubcategory where
  exists_cardinal := ‚ü®Œ∫, inferInstance, inferInstance‚ü©

instance : (isCardinalContinuous C (Type w) Œ∫).Œπ.IsRightAdjoint := by
  let e := ((equivSmallModel.{w} C·µí·µñ).op.symm.trans (opOpEquivalence C))
  let e' := isCardinalContinuousCongrLeft e (Type w) Œ∫
  let iso : (isCardinalContinuous C (Type w) Œ∫).Œπ ‚âÖ
    e'.inverse ‚ãô
      (isCardinalContinuous (SmallModel.{w} C·µí·µñ)·µí·µñ (Type w) Œ∫).Œπ ‚ãô e.congrLeft.functor := by
    refine (leftUnitor _).symm ‚â™‚â´ isoWhiskerRight e'.counitIso.symm _ ‚â™‚â´ associator _ _ _ ‚â™‚â´
      isoWhiskerLeft e'.inverse (Iso.refl _)
  exact Functor.isRightAdjoint_of_iso iso.symm

instance :
    HasLimitsOfSize.{w, w} (isCardinalContinuous C (Type w) Œ∫).FullSubcategory :=
  Adjunction.has_limits_of_equivalence
    ((isCardinalContinuousCongrLeft ((equivSmallModel.{w} C·µí·µñ).op.symm.trans
      (opOpEquivalence C)) (Type w) Œ∫).inverse)

attribute [local simp] shrinkYoneda in
@[simps!]
noncomputable def _root_.CategoryTheory.Equivalence.shrinkYonedaIsoConjugateYoneda
    {D : Type*} [Category.{w} D] (e : C ‚âå D) :
    shrinkYoneda.{w} ‚âÖ e.functor ‚ãô yoneda ‚ãô
      (whiskeringLeft C·µí·µñ D·µí·µñ (Type w)).obj e.functor.op :=
  NatIso.ofComponents (fun X ‚Ü¶ NatIso.ofComponents (fun Y ‚Ü¶
    ((equivShrink _).symm.trans e.fullyFaithfulFunctor.homEquiv).toIso))

variable {C Œ∫} in
lemma exists_presentation_of_isCardinalContinuous
    (hC : ‚àÄ (J : Type w) [SmallCategory J] (_ : HasCardinalLT (Arrow J) Œ∫),
      HasColimitsOfShape J C)
    {F : C·µí·µñ ‚•§ Type w}
    (hF : isCardinalContinuous _ _ Œ∫ F) :
    ‚àÉ (J : Type w) (_ : SmallCategory J) (_ : IsCardinalFiltered J Œ∫)
      (G : J ‚•§ CostructuredArrow shrinkYoneda F)
      (Œπ : G ‚ãô CostructuredArrow.proj _ _ ‚ãô shrinkYoneda.{w} ‚ü∂ (Functor.const _).obj F),
        Nonempty (IsColimit (Cocone.mk _ Œπ)) := by
  let C' := SmallModel.{w} C
  let e : C ‚âå C' := equivSmallModel.{w} C
  replace hF : isCardinalContinuous _ _ Œ∫ (e.inverse.op ‚ãô F) := by simpa
  replace hF := isCardinalFiltered_costructuredArrow_yoneda hF (fun J _ hJ ‚Ü¶ by
    have := hC J hJ
    exact Adjunction.hasColimitsOfShape_of_equivalence e.inverse)
  let iso' : e.inverse ‚ãô shrinkYoneda.{w} ‚âÖ yoneda ‚ãô
    (whiskeringLeft C·µí·µñ C'·µí·µñ (Type w)).obj e.functor.op :=
      isoWhiskerLeft e.inverse e.shrinkYonedaIsoConjugateYoneda ‚â™‚â´ (associator _ _ _).symm ‚â™‚â´
        isoWhiskerRight e.counitIso _ ‚â™‚â´ leftUnitor _
  let isoF := ((associator _ _ _).symm ‚â™‚â´ isoWhiskerRight e.op.unitIso.symm F
    ‚â™‚â´ leftUnitor _)
  let G : CostructuredArrow yoneda (e.inverse.op ‚ãô F) ‚•§
      CostructuredArrow shrinkYoneda.{w} F :=
    CostructuredArrow.map‚ÇÇ (F := e.inverse) (G := (whiskeringLeft _ _ _).obj e.functor.op)
      iso'.hom isoF.hom
  let projIso : G ‚ãô CostructuredArrow.proj _ _ ‚âÖ  CostructuredArrow.proj _ _ ‚ãô e.inverse :=
    Iso.refl _
  refine ‚ü®CostructuredArrow yoneda (e.inverse.op ‚ãô F), inferInstance, hF, G, ?_, ‚ü®?_‚ü©‚ü©
  ¬∑ refine
      { app X := shrinkYonedaEquiv.symm (yonedaEquiv X.hom)
        naturality X Y f := ?_ }
    apply shrinkYonedaEquiv.injective
    have := congr_fun (Y.hom.naturality f.left.op) (ùüô _)
    dsimp at this
    simp only [Category.comp_id] at this
    simp [‚Üê CostructuredArrow.w f,
      -CommaMorphism.w, shrinkYoneda, yonedaEquiv, shrinkYonedaEquiv, G, this]
  ¬∑ have H := isColimitOfPreserves (e.op.congrLeft (E := Type w)).inverse
      ((isColimitTautologicalCocone (e.inverse.op ‚ãô F)))
    refine (IsColimit.equivOfNatIsoOfIso
      (associator _ _ _ ‚â™‚â´ isoWhiskerLeft _ iso'.symm ‚â™‚â´ (associator _ _ _).symm ‚â™‚â´
        isoWhiskerRight projIso.symm _ ‚â™‚â´ associator _ _ _) _ _ ?_).1
        (isColimitOfPreserves (e.op.congrLeft).inverse
          ((isColimitTautologicalCocone (e.inverse.op ‚ãô F))))
    refine Cocones.ext isoF ?_
    intro j
    dsimp
    obtain ‚ü®Y, f, rfl‚ü© := j.mk_surjective
    obtain ‚ü®y, rfl‚ü© := yonedaEquiv.symm.surjective f
    ext X x
    dsimp at x
    obtain ‚ü®x, rfl‚ü© := (equivShrink _).surjective x
    simp [iso', isoF, projIso, shrinkYonedaEquiv]
    erw [Equiv.apply_symm_apply]
    simp [yonedaEquiv, ‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]

end EssentiallySmall

end Presheaf

end CategoryTheory
