/-
Copyright (c) 2018 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Johannes H√∂lzl, Reid Barton, Sean Leather, Yury Kudryashov
-/
import Mathlib.CategoryTheory.Types

/-!
# Concrete categories

A concrete category is a category `C` where the objects and morphisms correspond with types and
(bundled) functions between these types. We define concrete categories using
`class ConcreteCategory`. To convert an object to a type, write `ToHom`. To convert a morphism
to a (bundled) function, write `hom`.

Each concrete category `C` comes with a canonical faithful functor `forget C : C ‚•§ Type*`,
see `class HasForget`. In particular, we impose no restrictions on the category `C`, so `Type`
has the identity forgetful functor.

We say that a concrete category `C` admits a *forgetful functor* to a concrete category `D`, if it
has a functor `forget‚ÇÇ C D : C ‚•§ D` such that `(forget‚ÇÇ C D) ‚ãô (forget D) = forget C`, see
`class HasForget‚ÇÇ`.  Due to `Faithful.div_comp`, it suffices to verify that `forget‚ÇÇ.obj` and
`forget‚ÇÇ.map` agree with the equality above; then `forget‚ÇÇ` will satisfy the functor laws
automatically, see `HasForget‚ÇÇ.mk'`.

Two classes helping construct concrete categories in the two most
common cases are provided in the files `BundledHom` and
`UnbundledHom`, see their documentation for details.

## Implementation notes

We are currently switching over from `HasForget` to a new class `ConcreteCategory`,
see Zulip thread: https://leanprover.zulipchat.com/#narrow/channel/287929-mathlib4/topic/Concrete.20category.20class.20redesign

Previously, `ConcreteCategory` had the same definition as now `HasForget`; the coercion of
objects/morphisms to types/functions was defined as `(forget C).obj` and `(forget C).map`
respectively. This leads to defeq issues since existing `CoeFun` and `FunLike` instances provide
their own casts. We replace this with a less bundled `ConcreteCategory` that does not directly
use these coercions.

We do not use `CoeSort` to convert objects in a concrete category to types, since this would lead
to elaboration mismatches between results taking a `[ConcreteCategory C]` instance and specific
types `C` that hold a `ConcreteCategory C` instance: the first gets a literal `CoeSort.coe` and
the second gets unfolded to the actual `coe` field.

`ToType` and `ToHom` are `abbrev`s so that we do not need to copy over instances such as `Ring`
or `RingHomClass` respectively.

Since `X ‚Üí Y` is not a `FunLike`, the category of types is not a `ConcreteCategory`, but it does
have a `HasForget` instance.

## References

See [Ahrens and Lumsdaine, *Displayed Categories*][ahrens2017] for
related work.
-/


assert_not_exists CategoryTheory.CommSq CategoryTheory.Adjunction

universe w w' v v' v'' u u' u''

namespace CategoryTheory

/-- A concrete category is a category `C` with a fixed faithful functor `Forget : C ‚•§ Type`.

Note that `HasForget` potentially depends on three independent universe levels,
* the universe level `w` appearing in `Forget : C ‚•§ Type w`
* the universe level `v` of the morphisms (i.e. we have a `Category.{v} C`)
* the universe level `u` of the objects (i.e `C : Type u`)
They are specified that order, to avoid unnecessary universe annotations.
-/
class HasForget (C : Type u) [Category.{v} C] where
  /-- We have a functor to Type -/
  protected forget : C ‚•§ Type w
  /-- That functor is faithful -/
  [forget_faithful : forget.Faithful]

attribute [inline, reducible] HasForget.forget
attribute [instance] HasForget.forget_faithful

/-- The forgetful functor from a concrete category to `Type u`. -/
abbrev forget (C : Type u) [Category.{v} C] [HasForget.{w} C] : C ‚•§ Type w :=
  HasForget.forget

-- this is reducible because we want `forget (Type u)` to unfold to `ùü≠ _`
@[instance] abbrev HasForget.types : HasForget.{u, u, u+1} (Type u) where
  forget := ùü≠ _

/-- Provide a coercion to `Type u` for a concrete category. This is not marked as an instance
as it could potentially apply to every type, and so is too expensive in typeclass search.

You can use it on particular examples as:
```
instance : HasCoeToSort X := HasForget.hasCoeToSort X
```
-/
def HasForget.hasCoeToSort (C : Type u) [Category.{v} C] [HasForget.{w} C] :
    CoeSort C (Type w) where
  coe X := (forget C).obj X

section

attribute [local instance] HasForget.hasCoeToSort

variable {C : Type u} [Category.{v} C] [HasForget.{w} C]

/-- In any concrete category, `(forget C).map` is injective. -/
abbrev HasForget.instFunLike {X Y : C} : FunLike (X ‚ü∂ Y) X Y where
  coe f := (forget C).map f
  coe_injective' _ _ h := (forget C).map_injective h
attribute [local instance] HasForget.instFunLike

/-- In any concrete category, we can test equality of morphisms by pointwise evaluations. -/
@[ext low] -- Porting note: lowered priority
theorem ConcreteCategory.hom_ext {X Y : C} (f g : X ‚ü∂ Y) (w : ‚àÄ x : X, f x = g x) : f = g := by
  apply (forget C).map_injective
  dsimp [forget]
  funext x
  exact w x

theorem forget_map_eq_coe {X Y : C} (f : X ‚ü∂ Y) : (forget C).map f = f := rfl

/-- Analogue of `congr_fun h x`,
when `h : f = g` is an equality between morphisms in a concrete category.
-/
theorem congr_hom {X Y : C} {f g : X ‚ü∂ Y} (h : f = g) (x : X) : f x = g x :=
  congrFun (congrArg (fun k : X ‚ü∂ Y => (k : X ‚Üí Y)) h) x

theorem coe_id {X : C} : (ùüô X : X ‚Üí X) = id :=
  (forget _).map_id X

theorem coe_comp {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : (f ‚â´ g : X ‚Üí Z) = g ‚àò f :=
  (forget _).map_comp f g

@[simp] theorem id_apply {X : C} (x : X) : (ùüô X : X ‚Üí X) x = x :=
  congr_fun ((forget _).map_id X) x

@[simp] theorem comp_apply {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : X) : (f ‚â´ g) x = g (f x) :=
  congr_fun ((forget _).map_comp _ _) x

/-- Variation of `ConcreteCategory.comp_apply` that uses `forget` instead. -/
theorem comp_apply' {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : X) :
    (forget C).map (f ‚â´ g) x = (forget C).map g ((forget C).map f x) := comp_apply f g x

theorem ConcreteCategory.congr_hom {X Y : C} {f g : X ‚ü∂ Y} (h : f = g) (x : X) : f x = g x :=
  congr_fun (congr_arg (fun f : X ‚ü∂ Y => (f : X ‚Üí Y)) h) x

theorem ConcreteCategory.congr_arg {X Y : C} (f : X ‚ü∂ Y) {x x' : X} (h : x = x') : f x = f x' :=
  congrArg (f : X ‚Üí Y) h

@[simp]
theorem ConcreteCategory.hasCoeToFun_Type {X Y : Type u} (f : X ‚ü∂ Y) : CoeFun.coe f = f := rfl

end

/-- `HasForget‚ÇÇ C D`, where `C` and `D` are both concrete categories, provides a functor
`forget‚ÇÇ C D : C ‚•§ D` and a proof that `forget‚ÇÇ ‚ãô (forget D) = forget C`.
-/
class HasForget‚ÇÇ (C : Type u) (D : Type u') [Category.{v} C] [HasForget.{w} C]
  [Category.{v'} D] [HasForget.{w} D] where
  /-- A functor from `C` to `D` -/
  forget‚ÇÇ : C ‚•§ D
  /-- It covers the `HasForget.forget` for `C` and `D` -/
  forget_comp : forget‚ÇÇ ‚ãô forget D = forget C := by aesop

/-- The forgetful functor `C ‚•§ D` between concrete categories for which we have an instance
`HasForget‚ÇÇ C`. -/
abbrev forget‚ÇÇ (C : Type u) (D : Type u') [Category.{v} C] [HasForget.{w} C]
    [Category.{v'} D] [HasForget.{w} D] [HasForget‚ÇÇ C D] : C ‚•§ D :=
  HasForget‚ÇÇ.forget‚ÇÇ

attribute [local instance] HasForget.instFunLike HasForget.hasCoeToSort

lemma forget‚ÇÇ_comp_apply {C : Type u} {D : Type u'} [Category.{v} C] [HasForget.{w} C]
    [Category.{v'} D] [HasForget.{w} D] [HasForget‚ÇÇ C D] {X Y Z : C}
    (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : (forget‚ÇÇ C D).obj X) :
    ((forget‚ÇÇ C D).map (f ‚â´ g) x) =
      (forget‚ÇÇ C D).map g ((forget‚ÇÇ C D).map f x) := by
  rw [Functor.map_comp, comp_apply]

instance forget‚ÇÇ_faithful (C : Type u) (D : Type u') [Category.{v} C] [HasForget.{w} C]
    [Category.{v'} D] [HasForget.{w} D] [HasForget‚ÇÇ C D] : (forget‚ÇÇ C D).Faithful :=
  HasForget‚ÇÇ.forget_comp.faithful_of_comp

instance InducedCategory.hasForget {C : Type u} {D : Type u'}
    [Category.{v'} D] [HasForget.{w} D] (f : C ‚Üí D) :
    HasForget (InducedCategory D f) where
  forget := inducedFunctor f ‚ãô forget D

instance InducedCategory.hasForget‚ÇÇ {C : Type u} {D : Type u'} [Category.{v} D]
    [HasForget.{w} D] (f : C ‚Üí D) : HasForget‚ÇÇ (InducedCategory D f) D where
  forget‚ÇÇ := inducedFunctor f
  forget_comp := rfl

instance FullSubcategory.hasForget {C : Type u} [Category.{v} C] [HasForget.{w} C]
    (P : ObjectProperty C) : HasForget P.FullSubcategory where
  forget := P.Œπ ‚ãô forget C

instance FullSubcategory.hasForget‚ÇÇ {C : Type u} [Category.{v} C] [HasForget.{w} C]
    (P : ObjectProperty C) : HasForget‚ÇÇ P.FullSubcategory C where
  forget‚ÇÇ := P.Œπ
  forget_comp := rfl

/-- In order to construct a ‚Äúpartially forgetting‚Äù functor, we do not need to verify functor laws;
it suffices to ensure that compositions agree with `forget‚ÇÇ C D ‚ãô forget D = forget C`.
-/
def HasForget‚ÇÇ.mk' {C : Type u} {D : Type u'} [Category.{v} C] [HasForget.{w} C]
    [Category.{v'} D] [HasForget.{w} D]
    (obj : C ‚Üí D) (h_obj : ‚àÄ X, (forget D).obj (obj X) = (forget C).obj X)
    (map : ‚àÄ {X Y}, (X ‚ü∂ Y) ‚Üí (obj X ‚ü∂ obj Y))
    (h_map : ‚àÄ {X Y} {f : X ‚ü∂ Y}, HEq ((forget D).map (map f)) ((forget C).map f)) :
    HasForget‚ÇÇ C D where
  forget‚ÇÇ := Functor.Faithful.div _ _ _ @h_obj _ @h_map
  forget_comp := by apply Functor.Faithful.div_comp

/-- Composition of `HasForget‚ÇÇ` instances. -/
@[reducible]
def HasForget‚ÇÇ.trans (C : Type u) [Category.{v} C] [HasForget.{w} C]
    (D : Type u') [Category.{v'} D] [HasForget.{w} D]
    (E : Type u'') [Category.{v''} E] [HasForget.{w} E]
    [HasForget‚ÇÇ C D] [HasForget‚ÇÇ D E] : HasForget‚ÇÇ C E where
  forget‚ÇÇ := CategoryTheory.forget‚ÇÇ C D ‚ãô CategoryTheory.forget‚ÇÇ D E
  forget_comp := by
    show (CategoryTheory.forget‚ÇÇ _ D) ‚ãô (CategoryTheory.forget‚ÇÇ D E ‚ãô CategoryTheory.forget E) = _
    simp only [HasForget‚ÇÇ.forget_comp]

/-- Every forgetful functor factors through the identity functor. This is not a global instance as
    it is prone to creating type class resolution loops. -/
def hasForgetToType (C : Type u) [Category.{v} C] [HasForget.{w} C] :
    HasForget‚ÇÇ C (Type w) where
  forget‚ÇÇ := forget C
  forget_comp := Functor.comp_id _

section ConcreteCategory

/-- A concrete category is a category `C` where objects correspond to types and morphisms to
(bundled) functions between those types.

In other words, it has a fixed faithful functor `forget : C ‚•§ Type`.

Note that `ConcreteCategory` potentially depends on three independent universe levels,
* the universe level `w` appearing in `forget : C ‚•§ Type w`
* the universe level `v` of the morphisms (i.e. we have a `Category.{v} C`)
* the universe level `u` of the objects (i.e `C : Type u`)
They are specified that order, to avoid unnecessary universe annotations.
-/
class ConcreteCategory (C : Type u) [Category.{v} C]
    (FC : outParam <| C ‚Üí C ‚Üí Type*) {CC : outParam <| C ‚Üí Type w}
    [outParam <| ‚àÄ X Y, FunLike (FC X Y) (CC X) (CC Y)] where
  /-- Convert a morphism of `C` to a bundled function. -/
  (hom : ‚àÄ {X Y}, (X ‚ü∂ Y) ‚Üí FC X Y)
  /-- Convert a bundled function to a morphism of `C`. -/
  (ofHom : ‚àÄ {X Y}, FC X Y ‚Üí (X ‚ü∂ Y))
  (hom_ofHom : ‚àÄ {X Y} (f : FC X Y), hom (ofHom f) = f := by aesop_cat)
  (ofHom_hom : ‚àÄ {X Y} (f : X ‚ü∂ Y), ofHom (hom f) = f := by aesop_cat)
  (id_apply : ‚àÄ {X} (x : CC X), hom (ùüô X) x = x := by aesop_cat)
  (comp_apply : ‚àÄ {X Y Z} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : CC X),
    hom (f ‚â´ g) x = hom g (hom f x) := by aesop_cat)

export ConcreteCategory (id_apply comp_apply)

variable {C : Type u} [Category.{v} C] {FC : C ‚Üí C ‚Üí Type*} {CC : C ‚Üí Type w}
variable [‚àÄ X Y, FunLike (FC X Y) (CC X) (CC Y)]
variable [ConcreteCategory C FC]

/-- `ToType X` converts the object `X` of the concrete category `C` to a type.

This is an `abbrev` so that instances on `X` (e.g. `Ring`) do not need to be redeclared.
-/
@[nolint unusedArguments] -- Need the instance to trigger unification that finds `CC`.
abbrev ToType [ConcreteCategory C FC] := CC

/-- `ToHom X Y` is the type of (bundled) functions between objects `X Y : C`.

This is an `abbrev` so that instances (e.g. `RingHomClass`) do not need to be redeclared.
-/
@[nolint unusedArguments] -- Need the instance to trigger unification that finds `FC`.
abbrev ToHom [ConcreteCategory C FC] := FC

namespace ConcreteCategory

attribute [simp] id_apply comp_apply

/-- We can apply morphisms of concrete categories by first casting them down
to the base functions.
-/
instance {X Y : C} : CoeFun (X ‚ü∂ Y) (fun _ ‚Ü¶ ToType X ‚Üí ToType Y) where
  coe f := hom f

/--
`ConcreteCategory.hom` bundled as an `Equiv`.
-/
def homEquiv {X Y : C} : (X ‚ü∂ Y) ‚âÉ ToHom X Y where
  toFun := hom
  invFun := ofHom
  left_inv := ofHom_hom
  right_inv := hom_ofHom

lemma hom_bijective {X Y : C} : Function.Bijective (hom : (X ‚ü∂ Y) ‚Üí ToHom X Y) :=
  homEquiv.bijective

lemma hom_injective {X Y : C} : Function.Injective (hom : (X ‚ü∂ Y) ‚Üí ToHom X Y) :=
  hom_bijective.injective

/-- In any concrete category, we can test equality of morphisms by pointwise evaluations. -/
@[ext] lemma ext {X Y : C} {f g : X ‚ü∂ Y} (h : hom f = hom g) : f = g :=
  hom_injective h

lemma coe_ext {X Y : C} {f g : X ‚ü∂ Y} (h : ‚áë(hom f) = ‚áë(hom g)) : f = g :=
  ext (DFunLike.coe_injective h)

lemma ext_apply {X Y : C} {f g : X ‚ü∂ Y} (h : ‚àÄ x, f x = g x) : f = g :=
  ext (DFunLike.ext _ _ h)

/-- A concrete category comes with a forgetful functor to `Type`.

Warning: because of the way that `ConcreteCategory` and `HasForget` are set up, we can't make
`forget Type` reducibly defeq to the identity functor. -/
instance toHasForget : HasForget C where
  forget.obj := ToType
  forget.map f := ‚áë(hom f)
  forget_faithful.map_injective h := coe_ext h

end ConcreteCategory

theorem forget_obj (X : C) : (forget C).obj X = ToType X := by
  with_reducible_and_instances rfl

@[simp]
theorem ConcreteCategory.forget_map_eq_coe {X Y : C} (f : X ‚ü∂ Y) : (forget C).map f = f := by
  with_reducible_and_instances rfl

/-- Analogue of `congr_fun h x`,
when `h : f = g` is an equality between morphisms in a concrete category.
-/
protected theorem congr_fun {X Y : C} {f g : X ‚ü∂ Y} (h : f = g) (x : ToType X) : f x = g x :=
  congrFun (congrArg (fun k : X ‚ü∂ Y => (k : ToType X ‚Üí ToType Y)) h) x

/-- Analogue of `congr_arg f h`,
when `h : x = x'` is an equality between elements of objects in a concrete category.
-/
protected theorem congr_arg {X Y : C} (f : X ‚ü∂ Y) {x x' : ToType X} (h : x = x') : f x = f x' :=
  congrArg (f : ToType X ‚Üí ToType Y) h

theorem hom_id {X : C} : (ùüô X : ToType X ‚Üí ToType X) = id :=
  (forget _).map_id X

theorem hom_comp {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : (f ‚â´ g : ToType X ‚Üí ToType Z) = g ‚àò f :=
  (forget _).map_comp f g

/-- Using the `FunLike` coercion of `HasForget` does the same as the original coercion.
-/
theorem coe_toHasForget_instFunLike {C : Type*} [Category C] {FC : C ‚Üí C ‚Üí Type*} {CC : C ‚Üí Type*}
    [inst : ‚àÄ X Y : C, FunLike (FC X Y) (CC X) (CC Y)] [ConcreteCategory C FC] {X Y : C}
    (f : X ‚ü∂ Y) :
    @DFunLike.coe (X ‚ü∂ Y) (ToType X) (fun _ => ToType Y) HasForget.instFunLike f = f := rfl

lemma ConcreteCategory.forget‚ÇÇ_comp_apply {C : Type u} {D : Type u'} [Category.{v} C]
    {FC : C ‚Üí C ‚Üí Type*} {CC : C ‚Üí Type w} [‚àÄ X Y, FunLike (FC X Y) (CC X) (CC Y)]
    [ConcreteCategory.{w} C FC] [Category.{v'} D] {FD : D ‚Üí D ‚Üí Type*} {CD : D ‚Üí Type w}
    [‚àÄ X Y, FunLike (FD X Y) (CD X) (CD Y)] [ConcreteCategory.{w} D FD] [HasForget‚ÇÇ C D] {X Y Z : C}
    (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : ToType ((forget‚ÇÇ C D).obj X)) :
    ((forget‚ÇÇ C D).map (f ‚â´ g) x) =
      (forget‚ÇÇ C D).map g ((forget‚ÇÇ C D).map f x) := by
  rw [Functor.map_comp, ConcreteCategory.comp_apply]

instance hom_isIso {X Y : C} (f : X ‚ü∂ Y) [IsIso f] :
    IsIso (C := Type _) ‚áë(ConcreteCategory.hom f) :=
  ((forget C).mapIso (asIso f)).isIso_hom

@[simp]
lemma NatTrans.naturality_apply {C D : Type*} [Category C] [Category D] {FD : D ‚Üí D ‚Üí Type*}
    {CD : D ‚Üí Type*} [‚àÄ X Y, FunLike (FD X Y) (CD X) (CD Y)] [ConcreteCategory D FD]
    {F G : C ‚•§ D} (œÜ : F ‚ü∂ G) {X Y : C} (f : X ‚ü∂ Y) (x : ToType (F.obj X)) :
    œÜ.app Y (F.map f x) = G.map f (œÜ.app X x) := by
  simpa only [Functor.map_comp] using congr_fun ((forget D).congr_map (œÜ.naturality f)) x

section

variable (C)

/-- Build a coercion to functions out of `HasForget`.

The intended usecase is to provide a `FunLike` instance in `HasForget.toConcreteCategory`.
See that definition for the considerations in making this an instance.

See note [reducible non-instances].
-/
abbrev HasForget.toFunLike [HasForget C] (X Y : C) :
    FunLike (X ‚ü∂ Y) ((forget C).obj X) ((forget C).obj Y) where
  coe := (forget C).map
  coe_injective' _ _ h := Functor.Faithful.map_injective h

/-- Build a concrete category out of `HasForget`.

The intended usecase is to prove theorems referencing only `(forget C)`
and not `(forget C).obj X` nor `(forget C).map f`: those should be written
as `ToType X` and `ConcreteCategory.hom f` respectively.

See note [reducible non-instances].
-/
abbrev HasForget.toConcreteCategory [HasForget C] :
    ConcreteCategory C (¬∑ ‚ü∂ ¬∑) where
  hom f := f
  ofHom f := f
  id_apply := congr_fun ((forget C).map_id _)
  comp_apply _ _ := congr_fun ((forget C).map_comp _ _)

/-- Check that the new `ConcreteCategory` has the same forgetful functor as we started with. -/
example [inst : HasForget C] :
    @forget C _ ((HasForget.toConcreteCategory _).toHasForget) = @forget C _ inst := by
  with_reducible_and_instances rfl

/--
Note that the `ConcreteCategory` and `HasForget` instances here differ from `forget_map_eq_coe`.
-/
theorem forget_eq_ConcreteCategory_hom [HasForget C] {X Y : C} (f : X ‚ü∂ Y) :
    (forget C).map f = @ConcreteCategory.hom _ _ _ _ _ (HasForget.toConcreteCategory C) _ _ f := by
  with_reducible_and_instances rfl

/-- A `FunLike` instance on plain functions, in order to instantiate a `ConcreteCategory` structure
on the category of types.

This is not an instance (yet) because that would require a lot of downstream fixes.

See note [reducible non-instances].
-/
abbrev Types.instFunLike : ‚àÄ X Y : Type u, FunLike (X ‚ü∂ Y) X Y := HasForget.toFunLike _

/-- The category of types is concrete, using the identity functor.

This is not an instance (yet) because that would require a lot of downstream fixes.

See note [reducible non-instances].
-/
abbrev Types.instConcreteCategory : ConcreteCategory (Type u) (fun X Y => X ‚ü∂ Y) where
  hom f := f
  ofHom f := f

end

open ConcreteCategory

instance InducedCategory.concreteCategory {C : Type u} {D : Type u'} [Category.{v'} D]
    {FD : D ‚Üí D ‚Üí Type*} {CD : D ‚Üí Type w} [‚àÄ X Y, FunLike (FD X Y) (CD X) (CD Y)]
    [ConcreteCategory.{w} D FD] (f : C ‚Üí D) :
    ConcreteCategory (InducedCategory D f) (fun X Y => FD (f X) (f Y)) where
  hom := hom (C := D)
  ofHom := ofHom (C := D)
  hom_ofHom := hom_ofHom (C := D)
  ofHom_hom := ofHom_hom (C := D)
  comp_apply := ConcreteCategory.comp_apply (C := D)
  id_apply := ConcreteCategory.id_apply (C := D)

instance FullSubcategory.concreteCategory {C : Type u} [Category.{v} C]
    {FC : C ‚Üí C ‚Üí Type*} {CC : C ‚Üí Type w} [‚àÄ X Y, FunLike (FC X Y) (CC X) (CC Y)]
    [ConcreteCategory.{w} C FC]
    (P : ObjectProperty C) : ConcreteCategory P.FullSubcategory (fun X Y => FC X.1 Y.1) where
  hom := hom (C := C)
  ofHom := ofHom (C := C)
  hom_ofHom := hom_ofHom (C := C)
  ofHom_hom := ofHom_hom (C := C)
  comp_apply := ConcreteCategory.comp_apply (C := C)
  id_apply := ConcreteCategory.id_apply (C := C)

end ConcreteCategory

end CategoryTheory
