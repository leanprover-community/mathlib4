/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Johannes H√∂lzl, Reid Barton, Sean Leather, Yury Kudryashov
-/
import Mathlib.CategoryTheory.Types
import Mathlib.CategoryTheory.Functor.EpiMono
import Mathlib.CategoryTheory.Limits.Constructions.EpiMono

#align_import category_theory.concrete_category.basic from "leanprover-community/mathlib"@"311ef8c4b4ae2804ea76b8a611bc5ea1d9c16872"

/-!
# Concrete categories

A concrete category is a category `C` with a fixed faithful functor
`forget : C ‚•§ Type*`.  We define concrete categories using `class ConcreteCategory`.
In particular, we impose no restrictions on the
carrier type `C`, so `Type` is a concrete category with the identity
forgetful functor.

Each concrete category `C` comes with a canonical faithful functor
`forget C : C ‚•§ Type*`.  We say that a concrete category `C` admits a
*forgetful functor* to a concrete category `D`, if it has a functor
`forget‚ÇÇ C D : C ‚•§ D` such that `(forget‚ÇÇ C D) ‚ãô (forget D) = forget C`,
see `class HasForget‚ÇÇ`.  Due to `Faithful.div_comp`, it suffices
to verify that `forget‚ÇÇ.obj` and `forget‚ÇÇ.map` agree with the equality
above; then `forget‚ÇÇ` will satisfy the functor laws automatically, see
`HasForget‚ÇÇ.mk'`.

Two classes helping construct concrete categories in the two most
common cases are provided in the files `BundledHom` and
`UnbundledHom`, see their documentation for details.

## References

See [Ahrens and Lumsdaine, *Displayed Categories*][ahrens2017] for
related work.
-/


universe w w' v v' v'' u u' u''

namespace CategoryTheory

open CategoryTheory.Limits

/-- A concrete category is a category `C` with a fixed faithful functor `Forget : C ‚•§ Type`.

Note that `ConcreteCategory` potentially depends on three independent universe levels,
* the universe level `w` appearing in `Forget : C ‚•§ Type w`
* the universe level `v` of the morphisms (i.e. we have a `Category.{v} C`)
* the universe level `u` of the objects (i.e `C : Type u`)
They are specified that order, to avoid unnecessary universe annotations.
-/
class ConcreteCategory (C : Type u) [Category.{v} C] where
  /-- We have a functor to Type -/
  protected forget : C ‚•§ Type w
  /-- That functor is faithful -/
  [forget_faithful : forget.Faithful]
#align category_theory.concrete_category CategoryTheory.ConcreteCategory
#align category_theory.concrete_category.forget CategoryTheory.ConcreteCategory.forget

attribute [reducible] ConcreteCategory.forget
attribute [instance] ConcreteCategory.forget_faithful

/-- The forgetful functor from a concrete category to `Type u`. -/
abbrev forget (C : Type u) [Category.{v} C] [ConcreteCategory.{w} C] : C ‚•§ Type w :=
  ConcreteCategory.forget
#align category_theory.forget CategoryTheory.forget

-- this is reducible because we want `forget (Type u)` to unfold to `ùü≠ _`
@[instance] abbrev ConcreteCategory.types : ConcreteCategory.{u, u, u+1} (Type u) where
  forget := ùü≠ _
#align category_theory.concrete_category.types CategoryTheory.ConcreteCategory.types

/-- Provide a coercion to `Type u` for a concrete category. This is not marked as an instance
as it could potentially apply to every type, and so is too expensive in typeclass search.

You can use it on particular examples as:
```
instance : HasCoeToSort X := ConcreteCategory.hasCoeToSort X
```
-/
def ConcreteCategory.hasCoeToSort (C : Type u) [Category.{v} C] [ConcreteCategory.{w} C] :
    CoeSort C (Type w) where
  coe := fun X => (forget C).obj X
#align category_theory.concrete_category.has_coe_to_sort CategoryTheory.ConcreteCategory.hasCoeToSort

section

attribute [local instance] ConcreteCategory.hasCoeToSort

variable {C : Type u} [Category.{v} C] [ConcreteCategory.{w} C]

-- Porting note: forget_obj_eq_coe has become a syntactic tautology.
#noalign category_theory.forget_obj_eq_coe

/-- In any concrete category, `(forget C).map` is injective. -/
abbrev ConcreteCategory.instFunLike {X Y : C} : FunLike (X ‚ü∂ Y) X Y where
  coe f := (forget C).map f
  coe_injective' _ _ h := (forget C).map_injective h
attribute [local instance] ConcreteCategory.instFunLike

/-- In any concrete category, we can test equality of morphisms by pointwise evaluations. -/
@[ext low] -- Porting note: lowered priority
theorem ConcreteCategory.hom_ext {X Y : C} (f g : X ‚ü∂ Y) (w : ‚àÄ x : X, f x = g x) : f = g := by
  apply (forget C).map_injective
  dsimp [forget]
  funext x
  exact w x
#align category_theory.concrete_category.hom_ext CategoryTheory.ConcreteCategory.hom_ext

theorem forget_map_eq_coe {X Y : C} (f : X ‚ü∂ Y) : (forget C).map f = f := rfl
#align category_theory.forget_map_eq_coe CategoryTheory.forget_map_eq_coe

/-- Analogue of `congr_fun h x`,
when `h : f = g` is an equality between morphisms in a concrete category.
-/
theorem congr_hom {X Y : C} {f g : X ‚ü∂ Y} (h : f = g) (x : X) : f x = g x :=
  congrFun (congrArg (fun k : X ‚ü∂ Y => (k : X ‚Üí Y)) h) x
#align category_theory.congr_hom CategoryTheory.congr_hom

theorem coe_id {X : C} : (ùüô X : X ‚Üí X) = id :=
  (forget _).map_id X
#align category_theory.coe_id CategoryTheory.coe_id

theorem coe_comp {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : (f ‚â´ g : X ‚Üí Z) = g ‚àò f :=
  (forget _).map_comp f g
#align category_theory.coe_comp CategoryTheory.coe_comp

@[simp] theorem id_apply {X : C} (x : X) : (ùüô X : X ‚Üí X) x = x :=
  congr_fun ((forget _).map_id X) x
#align category_theory.id_apply CategoryTheory.id_apply

@[simp] theorem comp_apply {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : X) : (f ‚â´ g) x = g (f x) :=
  congr_fun ((forget _).map_comp _ _) x
#align category_theory.comp_apply CategoryTheory.comp_apply

theorem comp_apply' {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : X) :
    (forget C).map (f ‚â´ g) x = (forget C).map g ((forget C).map f x) := comp_apply f g x

theorem ConcreteCategory.congr_hom {X Y : C} {f g : X ‚ü∂ Y} (h : f = g) (x : X) : f x = g x :=
  congr_fun (congr_arg (fun f : X ‚ü∂ Y => (f : X ‚Üí Y)) h) x
#align category_theory.concrete_category.congr_hom CategoryTheory.ConcreteCategory.congr_hom

theorem ConcreteCategory.congr_arg {X Y : C} (f : X ‚ü∂ Y) {x x' : X} (h : x = x') : f x = f x' :=
  congrArg (f : X ‚Üí Y) h
#align category_theory.concrete_category.congr_arg CategoryTheory.ConcreteCategory.congr_arg

/-- In any concrete category, injective morphisms are monomorphisms. -/
theorem ConcreteCategory.mono_of_injective {X Y : C} (f : X ‚ü∂ Y) (i : Function.Injective f) :
    Mono f :=
  (forget C).mono_of_mono_map ((mono_iff_injective f).2 i)
#align category_theory.concrete_category.mono_of_injective CategoryTheory.ConcreteCategory.mono_of_injective

theorem ConcreteCategory.injective_of_mono_of_preservesPullback {X Y : C} (f : X ‚ü∂ Y) [Mono f]
    [PreservesLimitsOfShape WalkingCospan (forget C)] : Function.Injective f :=
  (mono_iff_injective ((forget C).map f)).mp inferInstance
#align category_theory.concrete_category.injective_of_mono_of_preserves_pullback CategoryTheory.ConcreteCategory.injective_of_mono_of_preservesPullback

theorem ConcreteCategory.mono_iff_injective_of_preservesPullback {X Y : C} (f : X ‚ü∂ Y)
    [PreservesLimitsOfShape WalkingCospan (forget C)] : Mono f ‚Üî Function.Injective f :=
  ((forget C).mono_map_iff_mono _).symm.trans (mono_iff_injective _)
#align category_theory.concrete_category.mono_iff_injective_of_preserves_pullback CategoryTheory.ConcreteCategory.mono_iff_injective_of_preservesPullback

/-- In any concrete category, surjective morphisms are epimorphisms. -/
theorem ConcreteCategory.epi_of_surjective {X Y : C} (f : X ‚ü∂ Y) (s : Function.Surjective f) :
    Epi f :=
  (forget C).epi_of_epi_map ((epi_iff_surjective f).2 s)
#align category_theory.concrete_category.epi_of_surjective CategoryTheory.ConcreteCategory.epi_of_surjective

theorem ConcreteCategory.surjective_of_epi_of_preservesPushout {X Y : C} (f : X ‚ü∂ Y) [Epi f]
    [PreservesColimitsOfShape WalkingSpan (forget C)] : Function.Surjective f :=
  (epi_iff_surjective ((forget C).map f)).mp inferInstance
#align category_theory.concrete_category.surjective_of_epi_of_preserves_pushout CategoryTheory.ConcreteCategory.surjective_of_epi_of_preservesPushout

theorem ConcreteCategory.epi_iff_surjective_of_preservesPushout {X Y : C} (f : X ‚ü∂ Y)
    [PreservesColimitsOfShape WalkingSpan (forget C)] : Epi f ‚Üî Function.Surjective f :=
  ((forget C).epi_map_iff_epi _).symm.trans (epi_iff_surjective _)
#align category_theory.concrete_category.epi_iff_surjective_of_preserves_pushout CategoryTheory.ConcreteCategory.epi_iff_surjective_of_preservesPushout

theorem ConcreteCategory.bijective_of_isIso {X Y : C} (f : X ‚ü∂ Y) [IsIso f] :
    Function.Bijective ((forget C).map f) := by
  rw [‚Üê isIso_iff_bijective]
  infer_instance
#align category_theory.concrete_category.bijective_of_is_iso CategoryTheory.ConcreteCategory.bijective_of_isIso

/-- If the forgetful functor of a concrete category reflects isomorphisms, being an isomorphism
is equivalent to being bijective. -/
theorem ConcreteCategory.isIso_iff_bijective [(forget C).ReflectsIsomorphisms]
    {X Y : C} (f : X ‚ü∂ Y) : IsIso f ‚Üî Function.Bijective ((forget C).map f) := by
  rw [‚Üê CategoryTheory.isIso_iff_bijective]
  exact ‚ü®fun _ ‚Ü¶ inferInstance, fun _ ‚Ü¶ isIso_of_reflects_iso f (forget C)‚ü©

@[simp]
theorem ConcreteCategory.hasCoeToFun_Type {X Y : Type u} (f : X ‚ü∂ Y) : CoeFun.coe f = f := rfl
#align category_theory.concrete_category.has_coe_to_fun_Type CategoryTheory.ConcreteCategory.hasCoeToFun_Type

end

/-- `HasForget‚ÇÇ C D`, where `C` and `D` are both concrete categories, provides a functor
`forget‚ÇÇ C D : C ‚•§ D` and a proof that `forget‚ÇÇ ‚ãô (forget D) = forget C`.
-/
class HasForget‚ÇÇ (C : Type u) (D : Type u') [Category.{v} C] [ConcreteCategory.{w} C]
  [Category.{v'} D] [ConcreteCategory.{w} D] where
  /-- A functor from `C` to `D` -/
  forget‚ÇÇ : C ‚•§ D
  /-- It covers the `ConcreteCategory.forget` for `C` and `D` -/
  forget_comp : forget‚ÇÇ ‚ãô forget D = forget C := by aesop
#align category_theory.has_forget‚ÇÇ CategoryTheory.HasForget‚ÇÇ

/-- The forgetful functor `C ‚•§ D` between concrete categories for which we have an instance
`HasForget‚ÇÇ C`. -/
abbrev forget‚ÇÇ (C : Type u) (D : Type u') [Category.{v} C] [ConcreteCategory.{w} C]
    [Category.{v'} D] [ConcreteCategory.{w} D] [HasForget‚ÇÇ C D] : C ‚•§ D :=
  HasForget‚ÇÇ.forget‚ÇÇ
#align category_theory.forget‚ÇÇ CategoryTheory.forget‚ÇÇ

attribute [local instance] ConcreteCategory.instFunLike ConcreteCategory.hasCoeToSort

lemma forget‚ÇÇ_comp_apply {C : Type u} {D : Type u'} [Category.{v} C] [ConcreteCategory.{w} C]
    [Category.{v'} D] [ConcreteCategory.{w} D] [HasForget‚ÇÇ C D] {X Y Z : C}
    (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : (forget‚ÇÇ C D).obj X) :
    ((forget‚ÇÇ C D).map (f ‚â´ g) x) =
      (forget‚ÇÇ C D).map g ((forget‚ÇÇ C D).map f x) := by
  rw [Functor.map_comp, comp_apply]

instance forget‚ÇÇ_faithful (C : Type u) (D : Type u') [Category.{v} C] [ConcreteCategory.{w} C]
    [Category.{v'} D] [ConcreteCategory.{w} D] [HasForget‚ÇÇ C D] : (forget‚ÇÇ C D).Faithful :=
  HasForget‚ÇÇ.forget_comp.faithful_of_comp
#align category_theory.forget‚ÇÇ_faithful CategoryTheory.forget‚ÇÇ_faithful

instance forget‚ÇÇ_preservesMonomorphisms (C : Type u) (D : Type u')
    [Category.{v} C] [ConcreteCategory.{w} C] [Category.{v'} D] [ConcreteCategory.{w} D]
    [HasForget‚ÇÇ C D] [(forget C).PreservesMonomorphisms] :
    (forget‚ÇÇ C D).PreservesMonomorphisms :=
  have : (forget‚ÇÇ C D ‚ãô forget D).PreservesMonomorphisms := by
    simp only [HasForget‚ÇÇ.forget_comp]
    infer_instance
  Functor.preservesMonomorphisms_of_preserves_of_reflects _ (forget D)
#align category_theory.forget‚ÇÇ_preserves_monomorphisms CategoryTheory.forget‚ÇÇ_preservesMonomorphisms

instance forget‚ÇÇ_preservesEpimorphisms (C : Type u) (D : Type u')
    [Category.{v} C] [ConcreteCategory.{w} C] [Category.{v'} D] [ConcreteCategory.{w} D]
    [HasForget‚ÇÇ C D] [(forget C).PreservesEpimorphisms] :
    (forget‚ÇÇ C D).PreservesEpimorphisms :=
  have : (forget‚ÇÇ C D ‚ãô forget D).PreservesEpimorphisms := by
    simp only [HasForget‚ÇÇ.forget_comp]
    infer_instance
  Functor.preservesEpimorphisms_of_preserves_of_reflects _ (forget D)
#align category_theory.forget‚ÇÇ_preserves_epimorphisms CategoryTheory.forget‚ÇÇ_preservesEpimorphisms

instance InducedCategory.concreteCategory {C : Type u} {D : Type u'}
    [Category.{v'} D] [ConcreteCategory.{w} D] (f : C ‚Üí D) :
      ConcreteCategory (InducedCategory D f) where
  forget := inducedFunctor f ‚ãô forget D
#align category_theory.induced_category.concrete_category CategoryTheory.InducedCategory.concreteCategory

instance InducedCategory.hasForget‚ÇÇ {C : Type u} {D : Type u'} [Category.{v} D]
    [ConcreteCategory.{w} D] (f : C ‚Üí D) : HasForget‚ÇÇ (InducedCategory D f) D where
  forget‚ÇÇ := inducedFunctor f
  forget_comp := rfl
#align category_theory.induced_category.has_forget‚ÇÇ CategoryTheory.InducedCategory.hasForget‚ÇÇ

instance FullSubcategory.concreteCategory {C : Type u} [Category.{v} C] [ConcreteCategory.{w} C]
    (Z : C ‚Üí Prop) : ConcreteCategory (FullSubcategory Z) where
  forget := fullSubcategoryInclusion Z ‚ãô forget C
#align category_theory.full_subcategory.concrete_category CategoryTheory.FullSubcategory‚Çì.concreteCategory

instance FullSubcategory.hasForget‚ÇÇ {C : Type u} [Category.{v} C] [ConcreteCategory.{w} C]
    (Z : C ‚Üí Prop) : HasForget‚ÇÇ (FullSubcategory Z) C where
  forget‚ÇÇ := fullSubcategoryInclusion Z
  forget_comp := rfl
#align category_theory.full_subcategory.has_forget‚ÇÇ CategoryTheory.FullSubcategory‚Çì.hasForget‚ÇÇ

/-- In order to construct a ‚Äúpartially forgetting‚Äù functor, we do not need to verify functor laws;
it suffices to ensure that compositions agree with `forget‚ÇÇ C D ‚ãô forget D = forget C`.
-/
def HasForget‚ÇÇ.mk' {C : Type u} {D : Type u'} [Category.{v} C] [ConcreteCategory.{w} C]
    [Category.{v'} D] [ConcreteCategory.{w} D]
    (obj : C ‚Üí D) (h_obj : ‚àÄ X, (forget D).obj (obj X) = (forget C).obj X)
    (map : ‚àÄ {X Y}, (X ‚ü∂ Y) ‚Üí (obj X ‚ü∂ obj Y))
    (h_map : ‚àÄ {X Y} {f : X ‚ü∂ Y}, HEq ((forget D).map (map f)) ((forget C).map f)) :
    HasForget‚ÇÇ C D where
  forget‚ÇÇ := Functor.Faithful.div _ _ _ @h_obj _ @h_map
  forget_comp := by apply Functor.Faithful.div_comp
#align category_theory.has_forget‚ÇÇ.mk' CategoryTheory.HasForget‚ÇÇ.mk'

/-- Composition of `HasForget‚ÇÇ` instances. -/
@[reducible]
def HasForget‚ÇÇ.trans (C : Type u) [Category.{v} C] [ConcreteCategory.{w} C]
    (D : Type u') [Category.{v'} D] [ConcreteCategory.{w} D]
    (E : Type u'') [Category.{v''} E] [ConcreteCategory.{w} E]
    [HasForget‚ÇÇ C D] [HasForget‚ÇÇ D E] : HasForget‚ÇÇ C E where
  forget‚ÇÇ := CategoryTheory.forget‚ÇÇ C D ‚ãô CategoryTheory.forget‚ÇÇ D E
  forget_comp := by
    show (CategoryTheory.forget‚ÇÇ _ D) ‚ãô (CategoryTheory.forget‚ÇÇ D E ‚ãô CategoryTheory.forget E) = _
    simp only [HasForget‚ÇÇ.forget_comp]

/-- Every forgetful functor factors through the identity functor. This is not a global instance as
    it is prone to creating type class resolution loops. -/
def hasForgetToType (C : Type u) [Category.{v} C] [ConcreteCategory.{w} C] :
    HasForget‚ÇÇ C (Type w) where
  forget‚ÇÇ := forget C
  forget_comp := Functor.comp_id _
#align category_theory.has_forget_to_Type CategoryTheory.hasForgetToType

@[simp]
lemma NatTrans.naturality_apply {C D : Type*} [Category C] [Category D] [ConcreteCategory D]
    {F G : C ‚•§ D} (œÜ : F ‚ü∂ G) {X Y : C} (f : X ‚ü∂ Y) (x : F.obj X) :
    œÜ.app Y (F.map f x) = G.map f (œÜ.app X x) := by
  simpa only [Functor.map_comp] using congr_fun ((forget D).congr_map (œÜ.naturality f)) x

end CategoryTheory
