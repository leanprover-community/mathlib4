/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Yury Kudryashov
-/
import Mathlib.CategoryTheory.ConcreteCategory.Basic
import Mathlib.CategoryTheory.ConcreteCategory.Bundled

#align_import category_theory.concrete_category.bundled_hom from "leanprover-community/mathlib"@"77ca1ed347337ecbafa9d9f4a55e330e44e9f9f8"

/-!
# Category instances for algebraic structures that use bundled homs.

Many algebraic structures in Lean initially used unbundled homs (e.g. a bare function between types,
along with an `IsMonoidHom` typeclass), but the general trend is towards using bundled homs.

This file provides a basic infrastructure to define concrete categories using bundled homs, and
define forgetful functors between them.
-/


universe u

namespace CategoryTheory

variable {c : Type u â†’ Type u} (hom : âˆ€ â¦ƒÎ± Î² : Type uâ¦„ (_ : c Î±) (_ : c Î²), Type u)

/-- Class for bundled homs. Note that the arguments order follows that of lemmas for `MonoidHom`.
This way we can use `âŸ¨@MonoidHom.toFun, @MonoidHom.id ...âŸ©` in an instance. -/
structure BundledHom where
  /-- the underlying map of a bundled morphism -/
  toFun : âˆ€ {Î± Î² : Type u} (IÎ± : c Î±) (IÎ² : c Î²), hom IÎ± IÎ² â†’ Î± â†’ Î²
  /-- the identity as a bundled morphism -/
  id : âˆ€ {Î± : Type u} (I : c Î±), hom I I
  /-- composition of bundled morphisms -/
  comp : âˆ€ {Î± Î² Î³ : Type u} (IÎ± : c Î±) (IÎ² : c Î²) (IÎ³ : c Î³), hom IÎ² IÎ³ â†’ hom IÎ± IÎ² â†’ hom IÎ± IÎ³
  /-- a bundled morphism is determined by the underlying map -/
  hom_ext : âˆ€ {Î± Î² : Type u} (IÎ± : c Î±) (IÎ² : c Î²), Function.Injective (toFun IÎ± IÎ²) := by
   aesop_cat
  /-- compatibility with identities -/
  id_toFun : âˆ€ {Î± : Type u} (I : c Î±), toFun I I (id I) = _root_.id := by aesop_cat
  /-- compatibility with the composition -/
  comp_toFun :
    âˆ€ {Î± Î² Î³ : Type u} (IÎ± : c Î±) (IÎ² : c Î²) (IÎ³ : c Î³) (f : hom IÎ± IÎ²) (g : hom IÎ² IÎ³),
      toFun IÎ± IÎ³ (comp IÎ± IÎ² IÎ³ g f) = toFun IÎ² IÎ³ g âˆ˜ toFun IÎ± IÎ² f := by
   aesop_cat
#align category_theory.bundled_hom CategoryTheory.BundledHom

attribute [class] BundledHom

attribute [simp] BundledHom.id_toFun BundledHom.comp_toFun

namespace BundledHom

variable [ð’ž : BundledHom hom]

set_option synthInstance.checkSynthOrder false in
/-- Every `@BundledHom c _` defines a category with objects in `Bundled c`.

This instance generates the type-class problem `BundledHom ?m`.
Currently that is not a problem, as there are almost no instances of `BundledHom`.
-/
instance category : Category (Bundled c) where
  Hom := fun X Y => hom X.str Y.str
  id := fun X => BundledHom.id ð’ž (Î± := X) X.str
  comp := fun {X Y Z} f g => BundledHom.comp ð’ž (Î± := X) (Î² := Y) (Î³ := Z) X.str Y.str Z.str g f
  comp_id _ := by apply ð’ž.hom_ext; simp
  assoc _ _ _ := by apply ð’ž.hom_ext; aesop_cat
  id_comp _ := by apply ð’ž.hom_ext; simp
#align category_theory.bundled_hom.category CategoryTheory.BundledHom.category

/-- A category given by `BundledHom` is a concrete category. -/
instance concreteCategory : ConcreteCategory.{u} (Bundled c) where
  forget :=
    { obj := fun X => X
      map := @fun X Y f => ð’ž.toFun X.str Y.str f
      map_id := fun X => ð’ž.id_toFun X.str
      map_comp := fun f g => by dsimp; erw [ð’ž.comp_toFun];rfl }
  forget_faithful := { map_injective := by (intros; apply ð’ž.hom_ext) }
#align category_theory.bundled_hom.concrete_category CategoryTheory.BundledHom.concreteCategory

variable {hom}

attribute [local instance] ConcreteCategory.instFunLike

/-- A version of `HasForgetâ‚‚.mk'` for categories defined using `@BundledHom`. -/
def mkHasForgetâ‚‚ {d : Type u â†’ Type u} {hom_d : âˆ€ â¦ƒÎ± Î² : Type uâ¦„ (_ : d Î±) (_ : d Î²), Type u}
    [BundledHom hom_d] (obj : âˆ€ â¦ƒÎ±â¦„, c Î± â†’ d Î±)
    (map : âˆ€ {X Y : Bundled c}, (X âŸ¶ Y) â†’ (Bundled.map @obj X âŸ¶ (Bundled.map @obj Y)))
    (h_map : âˆ€ {X Y : Bundled c} (f : X âŸ¶ Y), â‡‘(map f) = â‡‘f) :
    HasForgetâ‚‚ (Bundled c) (Bundled d) :=
  HasForgetâ‚‚.mk' (Bundled.map @obj) (fun _ => rfl) map (by
    intros X Y f
    rw [heq_eq_eq, forget_map_eq_coe, forget_map_eq_coe, h_map f])
#align category_theory.bundled_hom.mk_has_forgetâ‚‚ CategoryTheory.BundledHom.mkHasForgetâ‚‚

variable {d : Type u â†’ Type u}
variable (hom)

section

/-- The `hom` corresponding to first forgetting along `F`, then taking the `hom` associated to `c`.

For typical usage, see the construction of `CommMonCat` from `MonCat`.
-/
abbrev MapHom (F : âˆ€ {Î±}, d Î± â†’ c Î±) : âˆ€ â¦ƒÎ± Î² : Type uâ¦„ (_ : d Î±) (_ : d Î²), Type u :=
  fun _ _ iÎ± iÎ² => hom (F iÎ±) (F iÎ²)
#align category_theory.bundled_hom.map_hom CategoryTheory.BundledHom.MapHom

end

/-- Construct the `CategoryTheory.BundledHom` induced by a map between type classes.
This is useful for building categories such as `CommMonCat` from `MonCat`.
-/
def map (F : âˆ€ {Î±}, d Î± â†’ c Î±) : BundledHom (MapHom hom @F) where
  toFun Î± Î² {iÎ±} {iÎ²} f := ð’ž.toFun (F iÎ±) (F iÎ²) f
  id Î± {iÎ±} := ð’ž.id (F iÎ±)
  comp := @fun Î± Î² Î³ iÎ± iÎ² iÎ³ f g => ð’ž.comp (F iÎ±) (F iÎ²) (F iÎ³) f g
  hom_ext := @fun Î± Î² iÎ± iÎ² f g h => ð’ž.hom_ext (F iÎ±) (F iÎ²) h
#align category_theory.bundled_hom.map CategoryTheory.BundledHom.map

section

/-- We use the empty `ParentProjection` class to label functions like `CommMonoid.toMonoid`,
which we would like to use to automatically construct `BundledHom` instances from.

Once we've set up `MonCat` as the category of bundled monoids,
this allows us to set up `CommMonCat` by defining an instance
```instance : ParentProjection (CommMonoid.toMonoid) := âŸ¨âŸ©```
-/
class ParentProjection (F : âˆ€ {Î±}, d Î± â†’ c Î±) : Prop
#align category_theory.bundled_hom.parent_projection CategoryTheory.BundledHom.ParentProjection

end

-- The `ParentProjection` typeclass is just a marker, so won't be used.
@[nolint unusedArguments]
instance bundledHomOfParentProjection (F : âˆ€ {Î±}, d Î± â†’ c Î±) [ParentProjection @F] :
    BundledHom (MapHom hom @F) :=
  map hom @F
#align category_theory.bundled_hom.bundled_hom_of_parent_projection CategoryTheory.BundledHom.bundledHomOfParentProjection

instance forgetâ‚‚ (F : âˆ€ {Î±}, d Î± â†’ c Î±) [ParentProjection @F] :
    HasForgetâ‚‚ (Bundled d) (Bundled c) where
  forgetâ‚‚ :=
    { obj := fun X => âŸ¨X, F X.2âŸ©
      map := @fun X Y f => f }
#align category_theory.bundled_hom.forgetâ‚‚ CategoryTheory.BundledHom.forgetâ‚‚

instance forgetâ‚‚_full (F : âˆ€ {Î±}, d Î± â†’ c Î±) [ParentProjection @F] :
    Functor.Full (CategoryTheory.forgetâ‚‚ (Bundled d) (Bundled c)) where
  map_surjective f := âŸ¨f, rflâŸ©
#align category_theory.bundled_hom.forgetâ‚‚_full CategoryTheory.BundledHom.forgetâ‚‚_full

end BundledHom

end CategoryTheory
