/-
Copyright (c) 2024 Sina Hazratpour. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sina Hazratpour
-/
import Mathlib.CategoryTheory.Elements
import Mathlib.CategoryTheory.ConcreteCategory.Basic

/-!
# The category of pointed objects of a concrete category

This file defines the category of pointed objects of a concrete category. In a concrete category
`C`, a pointed object is an object `X` together with a distinguished element of the underlying type
of `X`, often called the basepoint.

## Main definitions

* `Pointed C` is the type of pointed objects in a concrete category `C`. We equip this type
  with a category structure where the morphisms are the morphisms of the original category that
  preserve the basepoint.

* `Pointed.map`, for a map of concrete categories, takes the pointed objects of `C` to
  the pointed objects of `D`.

* `Pointed.functor` gives, for a map of concrete categories, a functor between the correponding
  categories of pointed objects.

### Implementation notes

This construction is equivalent to a special case of the category of elements construction and
also to a particular comma construction. This files provides a more convenient API for directly
working with the category of pointed objects. We prove the equivalences in
`CategoryTheory.ConcreteCategory.Pointed.elementsEquivalence` and
`CategoryTheory.ConcreteCategory.structuredArrowEquivalence`.
-/

namespace CategoryTheory

universe w v u

variable {C : Type u} [Category.{v} C] [ConcreteCategory.{w,v,u} C]

namespace ConcreteCategory

attribute [local instance] ConcreteCategory.hasCoeToSort
attribute [local instance] ConcreteCategory.instFunLike

variable (C) in

/-- The pointed objects of a category `C`. A pointed object in `C` is an object `obj : C`
together with a distinguished element `pt` of the underlying type of `obj`, often called
the basepoint. -/
class Pointed where
  /-- the underlying object -/
  obj : C
  /-- the basepoint -/
  pt : obj

namespace Pointed

@[ext]
lemma ext {A B : Pointed C} (h‚ÇÅ : A.obj = B.obj)
    (h‚ÇÇ : ConcreteCategory.forget.map (eqToHom h‚ÇÅ) A.pt = B.pt) :
    A = B := by
  obtain ‚ü®X, x‚ü© := A
  obtain ‚ü®Y, y‚ü© := B
  cases h‚ÇÅ
  congr 1
  aesop

/-- The type of morphisms between pointed objects. A morphism of pointed objects is a morphism
in the original category between the objects which preserve the basepoints. -/
structure Hom (A B : Pointed C) where
  /-- the object morphism -/
  obj : A.obj ‚ü∂ B.obj
  /-- compatibility with the basedpoint -/
  pt : obj A.pt = B.pt := by aesop_cat

/-- The category structure on the category of pointed objects. -/
@[simps!]
instance category : Category (Pointed C) where
  Hom := Pointed.Hom
  id A := ‚ü®ùüô A.obj, by aesop_cat‚ü©
  comp {X Y Z} f g := ‚ü®f.obj ‚â´ g.obj, by
    simp [f.pt, g.pt]‚ü©

/-- Constructor for morphisms of the category pointed objects. -/
@[simps!]
def homMk (A B : Pointed C) (f : A.obj ‚ü∂ B.obj) (hf : f A.pt = B.pt) :
    A ‚ü∂ B :=
  ‚ü®f, hf‚ü©

@[ext]
theorem Hom.ext {A B : Pointed C} (f g : A ‚ü∂ B) (w : f.obj = g.obj) : f = g := by
  cases f
  cases g
  cases w
  rfl

@[simp]
theorem comp_obj {A B C : Pointed C} {f : A ‚ü∂ B} {g : B ‚ü∂ C} : (f ‚â´ g).obj = f.obj ‚â´ g.obj := rfl

@[simp]
theorem id_obj {A : Pointed C} : (ùüô A : A ‚ü∂ A).obj = ùüô A.obj := rfl

@[simp]
theorem id_pt {A B : Pointed C} (f : A ‚ü∂ B) : f.obj A.pt = B.pt := f.pt

instance concreteCategory : ConcreteCategory (Pointed C) where
  forget :=
    { obj := fun A ‚Ü¶ A.obj
      map := fun f ‚Ü¶ f.obj }
  forget_faithful := by
    refine ‚ü®@fun X Y ‚Ü¶ ?_‚ü©
    dsimp
    fapply Function.Injective.comp
    ¬∑ apply forget_faithful.map_injective
    ¬∑ apply Hom.ext

instance : (forget (Pointed C)).Faithful := by
  exact forget_faithful

/-- The forward direction of the equivalence `Pointed C ‚âå (forget C).Elements`. -/
@[simps obj]
def toCategoryOfElement :
    Pointed C ‚•§ (forget C).Elements where
  obj X := ‚ü®X.obj, X.pt‚ü©
  map {X Y} f := ‚ü®f.obj, f.pt‚ü©

/-- The reverse direction of the equivalence `Pointed C ‚âå (forget C).Elements`. -/
@[simps map]
def fromCategoryOfElements :
    (forget C).Elements ‚•§ Pointed C where
  obj X := ‚ü®X.1, X.2‚ü©
  map {X Y} f := ‚ü®f.1, f.2‚ü©

/-- The equivalence between `Pointed C` and the category of elements of the forgetful
functor of `C`. -/
@[simps! functor_obj functor_map inverse_obj inverse_map unitIso_hom
  unitIso_inv counitIso_hom counitIso_inv]
def elementsEquivalence : Pointed C ‚âå (forget C).Elements :=
  Equivalence.mk (toCategoryOfElement) (fromCategoryOfElements)
    (NatIso.ofComponents fun X => eqToIso (by aesop_cat))
    (NatIso.ofComponents fun X => eqToIso (by aesop_cat))

/-- The equivalence between `Pointed C` and the comma category `(*, ConcreteCategory.forget)`. -/
@[simps! functor_obj functor_map inverse_obj inverse_map unitIso_hom
  unitIso_inv counitIso_hom counitIso_inv]
def structuredArrowEquivalence :
    Pointed C ‚âå StructuredArrow PUnit (forget C) :=
  (elementsEquivalence).trans (CategoryOfElements.structuredArrowEquivalence _)

variable {D : Type u} [Category.{v} D] [ConcreteCategory.{w,v,u} D]

/-- For a map of concrete categories, `Pointed.map` takes the pointed objects of `C` to
the pointed objects of `D`. -/
@[simps!]
def map [HasForget‚ÇÇ C D] (A : Pointed C) : Pointed D where
  obj := (forget‚ÇÇ C D).obj A.obj
  pt := by
    rw [‚Üê Functor.comp_obj, HasForget‚ÇÇ.forget_comp]
    exact A.pt

/-- A map of concrete categories induces a functor between the correponding category of pointed
objects. -/
@[simps!]
def functor [HasForget‚ÇÇ C D] : Pointed C ‚•§ Pointed D :=
  toCategoryOfElement ‚ãô
    CategoryOfElements.map (eqToHom HasForget‚ÇÇ.forget_comp.symm) ‚ãô
      CategoryOfElements.pullback (ConcreteCategory.forget (C:= D)) (forget‚ÇÇ C D) ‚ãô
        fromCategoryOfElements

end Pointed

end ConcreteCategory

end CategoryTheory
