/-
Copyright (c) 2023 David Spivak. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Spivak, Shaowei Lin
-/
import Init.Prelude
import Mathlib.CategoryTheory.Category.Basic
import Mathlib.CategoryTheory.Monoidal.Category
import Mathlib.CategoryTheory.Closed.Monoidal

/-!
# Polynomial Functors

Defines the category of polynomial functors, as a type class parametrised by the type of objects.

## Notations

For polynomial functos, we use the same notation as that for categories.
* `ðŸ™ p` for the identity lens from `p` to itself (type as `\b1`)
* `p âŸ¶ q` for the space of lenses from `p` to `q` (type as `\-->`)
* `p â‰« q` for composition in the diagrammatic order (type as `\gg`)

We introduce some new notations in the `Poly` scope
* `A y^B` for monomials

Users may like to add `f âŠš g` for composition in the classical order, using
```lean
local notation:80 f " âŠš " g => composemap g f    -- type as \oo
```

Users preferring `;` to `â‰«` for composition in the diagrammatic order may add
```lean
local infixr:80 " ; " => composemap
```
-/

library_note "Poly universes"
/--
The category `Poly.{u, v}` of polynomial functors and lenses
between them contains polynomial functors
whose positions live in `Type u` and
whose directions have codomains in `Type v`.

These polynomial functors can be applied to types
in any `Type w` independent of `Type u` and `Type v`.
-/

universe u v u' v' w

namespace CategoryTheory

namespace Poly





/-!
## Category of polynommial functors
-/

/-- Poly as a type where the objects are pairs (pos, dir). -/
structure Poly where
  pos : Type u
  dir : pos -> Type v

/-- The type of lenses/maps from one polynomial functor to another. -/
@[ext]
structure polymap (p q : Poly.{u, v}) : Type max u v where
  onPos : p.pos -> q.pos
  onDir : (x : p.pos) -> q.dir (onPos x) -> p.dir x

/-- The identity lens/map from a polynomial functor to itself. -/
def polyid (p : Poly) : polymap p p where
  onPos := id
  onDir := Î» _ â†¦ id

/-- Composition of lenses/maps. -/
def composemap {p q r : Poly} (f : polymap p q) (g : polymap q r) :
    polymap p r where
  onPos := g.onPos âˆ˜ f.onPos
  onDir := Î» px rd â†¦ f.onDir px (g.onDir (f.onPos px) rd)

/-- Poly as a type with some categorical structure. -/
instance Poly.categoryStruct : CategoryStruct Poly where
  Hom  := polymap
  id   := polyid
  comp := composemap

/-- Poly as a category. -/
instance Poly.category : Category Poly where
  id_comp := by intros; rfl
  comp_id := by intros; rfl
  assoc   := by intros; rfl

/-- Applying a polynomial functor to get a type. -/
def applyFun (p : Poly.{u, v}) (T : Type w) :
    Type max u v w :=
  Î£ (x : p.pos), (p.dir x) -> T

/-- Applying a lens/map to get a function. -/
def applyMap {p q : Poly.{u, v}} (f : p âŸ¶ q) (T : Type) :
    (applyFun p T) -> (applyFun q T) :=
  Î» x â†¦ Sigma.mk (f.onPos x.fst) (x.snd âˆ˜ (f.onDir x.fst))





/-!
## Special polynommial functors
-/

/-- A monomial functor. -/
def monomial (P : Type u) (D: Type v) : Poly.{u, v} where
  pos := P
  dir := (Î» _ â†¦ D)

/-- Notation for a monomial functor. -/
scoped notation:80 A:80 " y^" B:80 => monomial A B

/-- A representable functor. -/
def representable (D : Type v) : Poly.{u, v} := PUnit.{u+1} y^D

/-- Notation for a representable functor. -/
scoped notation:80 "y^" B:80 => representable B

/-- A constant polynomial functor. -/
def const (P : Type u) : Poly.{u, v} := P y^(PEmpty.{v+1})

/-- Notation for a constant polynomial functor. -/
scoped notation:80 A:80 " y^0" => const A

/-- A linear polynomial functor. -/
def linear (P : Type u) : Poly.{u, v} := P y^(PUnit.{v+1})

/-- Notation for a linear polynomial functor. -/
scoped notation:80 A:80 " y^1" => linear A

/-- The identity functor in Poly. -/
def y : Poly.{u, v} := linear PUnit.{u+1}

/-- Additional notation for a linear polynomial functor. -/
scoped notation "y^1" => y

/-- The initial object in Poly. -/
def poly0 : Poly.{u, v} := const PEmpty.{u+1}

/-- Notation for the initial object. -/
scoped notation "ðŸ¬" => poly0  -- type as `\sb0`

/-- Notation for unique map from empty type. -/
scoped notation "!ðŸ¬" => PEmpty.rec  -- type as `!\sb0`

/-- The terminal object in Poly. -/
def poly1 : Poly.{u, v} := const PUnit.{u+1}

/-- Notation for the terminal object. -/
scoped notation "ðŸ­" => poly1  -- type as `\sb1`

/-- Notation for unique map to unit type. -/
scoped notation "!ðŸ­" => Function.const _ PUnit.unit  -- type as `!\sb1`





/-!
## Special lenses/maps
-/

/-- A lens/map between constant polynomial functors. -/
def constantMap {T T' : Type u} (f : T -> T') : T y^0 âŸ¶ T' y^0 where
  onPos := f
  onDir := (Î» _ â†¦ !ðŸ¬)

/-- A lens/map between linear polynomial functors. -/
def linearMap {T T' : Type u} (f : T -> T') : T y^1 âŸ¶ T' y^1 where
  onPos := f
  onDir := (Î» _ â†¦ !ðŸ­)

/-- A lens/map between representable functors. -/
def representableMap {T T' : Type u} (f : T -> T') : y^T' âŸ¶ y^T where
  onPos := !ðŸ­
  onDir := (Î» _ â†¦ f)

/-- The unique lens/map from the initial object in Poly. -/
def bang0poly {p : Poly.{u, v}} : ðŸ¬ âŸ¶ p where
  onPos := !ðŸ¬
  onDir := !ðŸ¬

/-- The unique lens/map to the terminal object in Poly. -/
def bang1poly {P : Poly.{u, v}} : P âŸ¶ ðŸ­ where
  onPos := !ðŸ­
  onDir := (Î» _ â†¦ !ðŸ¬)

/-- A second representation for the type of lenses/maps. -/
def polymap2 (p q : Poly.{u, v}) : Type max u v :=
  (px : p.pos) -> Î£ (qx : q.pos), q.dir qx -> p.dir px

/-- Casting from the default representation for the type
    of lenses/maps to the second representation. -/
def cast12 {p q : Poly.{u, v}} (f : p âŸ¶ q) : polymap2 p q :=
  Î» px â†¦ (Sigma.mk (f.onPos px) (f.onDir px))

/-- Casting from the second representation for the type
    of lenses/maps to the default representation. -/
def cast21 {p q : Poly.{u, v}} (f : polymap2 p q) : p âŸ¶ q where
  onPos := (Î» px â†¦ (f px).fst)
  onDir := (Î» px â†¦ (f px).snd)





/-!
## Substitution product
-/

/--
Substitution product of polynomial functors.
Require polynomial functors from Poly.{u, u}
for the product to remain in Poly.{u, u}.
-/
def subst (p q : Poly.{u, u}) : Poly.{u, u} where
  pos := applyFun p q.pos
  dir := Î» x â†¦ Î£ (d : p.dir x.fst), (q.dir (x.snd d))

/-- Notation for substitution product of polynomial functors. -/
scoped infixr:80 "â—" => subst -- type as `\lhd`

def subst.whiskerLeft (p q q': Poly) (f : q âŸ¶ q') :
    (p â— q) âŸ¶ (p â— q') where
  onPos := Î» x â†¦ Sigma.mk x.fst (f.onPos âˆ˜ x.snd)
  onDir := Î» x d â†¦ Sigma.mk d.fst (f.onDir (x.snd d.fst) d.snd)

def subst.whiskerRight (f : p âŸ¶ p') (q : Poly) :
    (p â— q) âŸ¶ (p' â— q) where
  onPos := applyMap f q.pos
  onDir := Î» x d â†¦ Sigma.mk (f.onDir x.fst d.fst) d.snd

def subst.leftUnitor.hom (p : Poly) : (y â— p) âŸ¶ p where
  onPos := Î» x â†¦ x.snd x.fst
  onDir := Î» _ d â†¦ Sigma.mk PUnit.unit d

def subst.leftUnitor.inv (p : Poly) : p âŸ¶ (y â— p) where
  onPos := Î» x â†¦ Sigma.mk PUnit.unit (Î» _ â†¦ x)
  onDir := Î» _ d â†¦ d.snd

def subst.leftUnitor (p : Poly) : (y â— p) â‰… p where
  hom := subst.leftUnitor.hom p
  inv := subst.leftUnitor.inv p

def subst.rightUnitor.hom (p : Poly) : (p â— y) âŸ¶ p where
  onPos := Î» x â†¦ x.fst
  onDir := Î» _ d â†¦ Sigma.mk d PUnit.unit

def subst.rightUnitor.inv (p : Poly) : p âŸ¶ (p â— y) where
  onPos := Î» x â†¦ Sigma.mk x (Î» _ â†¦ PUnit.unit)
  onDir := Î» _ d â†¦ d.fst

def subst.rightUnitor (p : Poly) : (p â— y) â‰… p where
  hom := subst.rightUnitor.hom p
  inv := subst.rightUnitor.inv p

def subst.associator.hom (p q r : Poly) :
    (p â— q) â— r âŸ¶ p â— (q â— r) := by
  constructor
  case onPos =>
    intro pq_r
    let pq_r1 := pq_r.fst
    let pq_r2 := pq_r.snd
    let pq_r11 := pq_r1.fst
    let pq_r12 := pq_r1.snd
    constructor
    case fst =>
      exact pq_r11
    case snd =>
      intro pd
      constructor
      case fst =>
        exact pq_r12 pd
      case snd =>
        intro qd
        exact pq_r2 (Sigma.mk pd qd)
  case onDir =>
    intro _ p_qr
    let p_qr1  := p_qr.fst
    let p_qr2  := p_qr.snd
    let p_qr21 := p_qr2.fst
    let p_qr22 := p_qr2.snd
    exact Sigma.mk (Sigma.mk p_qr1 p_qr21) p_qr22

def subst.associator.inv (p q r : Poly) :
    p â— (q â— r) âŸ¶ (p â— q) â— r := by
  constructor
  case onPos =>
    intro p_qr
    let p_qr1 := p_qr.fst
    let p_qr2 := p_qr.snd
    constructor
    case fst =>
      constructor
      case fst =>
        exact p_qr1
      case snd =>
        intros pd
        exact (p_qr2 pd).fst
    case snd =>
      intro pqd
      exact (p_qr2 pqd.fst).snd pqd.snd
  case onDir =>
    intro p_qr1 pq_rd
    let pq_rd1 := pq_rd.fst
    let pq_rd2 := pq_rd.snd
    constructor
    case fst =>
      exact pq_rd1.fst
    case snd =>
      constructor
      case fst =>
        exact pq_rd1.snd
      case snd =>
        exact pq_rd2

def subst.associator (p q r : Poly) : (p â— q) â— r â‰… p â— (q â— r) where
  hom := subst.associator.hom p q r
  inv := subst.associator.inv p q r

instance Poly.subst.monoidalStruct : MonoidalCategoryStruct Poly where
  tensorObj    := subst
  whiskerLeft  := subst.whiskerLeft
  whiskerRight := subst.whiskerRight
  tensorUnit   := y
  leftUnitor   := subst.leftUnitor
  rightUnitor  := subst.rightUnitor
  associator   := subst.associator

/-- All hyptheses proven automatically so none provided. -/
instance Poly.subst.monoidal : MonoidalCategory Poly where

-- structure Comonad where
--   carrier : Poly
--   counit  : carrier âŸ¶ y
--   comult  : carrier âŸ¶ (carrier â— carrier)

/-!
## Co-Product
-/

def coproduct (p q : Poly.{u, u}) : Poly.{u, u} where
  pos := p.pos âŠ• q.pos
  dir := Î» x â†¦
    match x with
      | .inl ppos => p.dir ppos
      | .inr qpos => q.dir qpos

infixr:75 " + " => coproduct

def coproduct.map (p q r z : Poly.{u, u}) (f : p âŸ¶ q) (g : r âŸ¶ z) : (p + r) âŸ¶ (q + z) :=
    { onPos := Î» pos â†¦
      match pos with
        | .inl ppos => .inl (f.onPos ppos)
        | .inr qpos => .inr (g.onPos qpos)
    , onDir := Î» pos â†¦
      match pos with
        | .inl ppos => f.onDir ppos
        | .inr rpos => g.onDir rpos
    }

def coproduct.whiskerLeft (p : Poly) {q q' : Poly} (f : q âŸ¶ q') : p + q âŸ¶ p + q' :=
  (coproduct.map p p q q' ) (polyid p) f

def coproduct.whiskerRight {p p' : Poly} (f : p âŸ¶ p') (q : Poly) : p + q âŸ¶ p' + q :=
  (coproduct.map p p' q q) f (polyid q)

def coproduct.split.l {p : Poly.{u, u}} : p âŸ¶ p + p :=
  { onPos := Î» ppos â†¦ .inl ppos
  , onDir := Î» _ppos â†¦ id
  }

def coproduct.split.r {p : Poly.{u, u}} : p âŸ¶ p + p :=
  { onPos := Î» ppos â†¦ .inr ppos
  , onDir := Î» _ppos pdir â†¦ pdir
  }

def coproduct.leftUnitor.hom (p : Poly) : (ðŸ¬ + p) âŸ¶ p where
  onPos := Î» pos â†¦
  match pos with
  | .inr ppos => ppos
  onDir := Î» pos dir â†¦
  match pos with
  | .inr _ppos => dir

def coproduct.leftUnitor.inv (p : Poly) : p âŸ¶ (ðŸ¬ + p) where
  onPos := Î» ppos â†¦ .inr ppos
  onDir := Î» _ppos pdir â†¦ pdir


def coproduct.leftUnitor.inv_hom_id : composemap (leftUnitor.inv p) (leftUnitor.hom p) = polyid p :=
  by
  unfold composemap
  unfold polyid
  simp
  exact (And.intro rfl rfl)

def coproduct.leftUnitor.hom_inv_id :
    composemap (leftUnitor.hom p) (leftUnitor.inv p) = polyid (ðŸ¬ + p) := by
  ext d
  . cases d
    . contradiction
    . rfl
  . cases p
    simp only [hom, inv, composemap, polyid, Function.comp_apply, id_eq]
    congr!
    Â· split
      assumption
    Â· split
      assumption


def coproduct.leftUnitor (p : Poly) : (ðŸ¬ + p) â‰… p where
  hom := coproduct.leftUnitor.hom p
  inv := coproduct.leftUnitor.inv p
  hom_inv_id := coproduct.leftUnitor.hom_inv_id
  inv_hom_id := coproduct.leftUnitor.inv_hom_id

-- TODO:
-- instance Poly.coproduct.monoidalStruct : MonoidalCategoryStruct Poly where
--   tensorObj    := coproduct
--   whiskerLeft  := coproduct.whiskerLeft
--   whiskerRight := coproduct.whiskerRight
--   tensorUnit   := ðŸ¬
--   leftUnitor   := _
--   rightUnitor  := _
--   associator   := _

/-!
## Cartesian product
-/

def product (p q : Poly.{u, u}) : Poly.{u, u} where
  pos := p.pos Ã— q.pos
  dir := Î» (ppos , qpos) =>  Sum (p.dir ppos) (q.dir qpos)

infixr:85 " Ã— " => product

def product.map (p q r z : Poly.{u, u}) (f : p âŸ¶ q) (g : r âŸ¶ z) : (p Ã— r) âŸ¶ (q Ã— z) :=
    { onPos := Î» (ppos , rpos) => (f.onPos ppos , g.onPos rpos)
    , onDir := Î» (ppos , rpos) dir =>
      match dir with
        | .inl qdir => .inl (f.onDir ppos qdir)
        | .inr zdir => .inr (g.onDir rpos zdir)
    }

def product.whiskerLeft (p : Poly) {q q' : Poly} (f : q âŸ¶ q') : p Ã— q âŸ¶ p Ã— q' :=
  (product.map p p q q' ) (polyid p) f

def product.whiskerRight {p p' : Poly} (f : p âŸ¶ p') (q : Poly) : p Ã— q âŸ¶ p' Ã— q :=
  (product.map p p' q q) f (polyid q)

def product.fst {p q : Poly} : (p Ã— q) âŸ¶ p :=
  { onPos := Î» (ppos , _qpos) => ppos
  , onDir := Î» (_ppos , _qpos) pdir => .inl pdir
  }

def product.snd {p q : Poly} : (p Ã— q) âŸ¶ q :=
  { onPos := Î» (_ppos , qpos) => qpos
  , onDir := Î» (_ppos , _qpos) qdir => .inr qdir
  }

def product.swap {p q : Poly} : (p Ã— q) âŸ¶ (q Ã— p) :=
  { onPos := Î» (ppos , qpos) => (qpos , ppos)
  , onDir := Î» (_ppos , _qpos) dir =>
        match dir with
          | .inl qdir => .inr qdir
          | .inr pdir => .inl pdir
  }

def product.dupe {p : Poly} : p âŸ¶ p Ã— p :=
  { onPos := Î» ppos => (ppos , ppos)
  , onDir := Î» _pos dir =>
        match dir with
          | .inl pdir => pdir
          | .inr pdir => pdir
  }

def product.fanout {p q r : Poly} (f : r âŸ¶ p) (g : r âŸ¶ q) : r âŸ¶ p Ã— q :=
  { onPos := Î» rpos => (f.onPos rpos , g.onPos rpos)
  , onDir := Î» rpos dir =>
        match dir with
          | .inl pdir => f.onDir rpos pdir
          | .inr qdir => g.onDir rpos qdir
  }

def product.leftUnitor.hom (p : Poly) : (ðŸ­ Ã— p) âŸ¶ p where
  onPos := Î» (() , ppos) â†¦ ppos
  onDir := Î» (() , _ppos) pdir â†¦ .inr pdir

def product.leftUnitor.inv (p : Poly) : p âŸ¶ (ðŸ­ Ã— p) where
  onPos := Î» ppos â†¦ (.unit , ppos)
  onDir := Î» _ppos dir â†¦
  match dir with
  | .inr pfib => pfib

def product.leftUnitor.hom_inv_id : composemap (leftUnitor.hom p) (leftUnitor.inv p) = ðŸ™ (ðŸ­ Ã— p)
  := by
      unfold composemap
      ext
      . rfl
      . simp
        funext _ dir
        cases dir
        . contradiction
        . rfl

def product.leftUnitor (p : Poly) : (ðŸ­ Ã— p) â‰… p :=
  { hom := product.leftUnitor.hom p
  , inv := product.leftUnitor.inv p
  , hom_inv_id := product.leftUnitor.hom_inv_id -- extracted so that we may unfold composemap
  , inv_hom_id := by
      unfold product.leftUnitor.hom
      simp
      rfl
  }

/-!
## Parallel product
-/

def tensor (p q : Poly.{u, u}) : Poly.{u, u} where
  pos := p.pos Ã— q.pos
  dir := Î» (ppos , qpos) =>  (p.dir ppos) Ã— (q.dir qpos)

infixr:90 " âŠ— " => tensor

def tensor.map (p q r z : Poly.{u, u}) (f : p âŸ¶ q) (g : r âŸ¶ z) : p âŠ— r âŸ¶ q âŠ— z :=
    { onPos := Î» (ppos , rpos) => (f.onPos ppos , g.onPos rpos)
    , onDir := Î» (ppos , rpos) (qdir , zdir) => (f.onDir ppos qdir , g.onDir rpos zdir)
    }

def tensor.whiskerLeft (p : Poly) {q q' : Poly} (f : q âŸ¶ q') : p âŠ— q âŸ¶ p âŠ— q' :=
  (tensor.map p p q q' ) (polyid p) f

def tensor.whiskerRight {p p' : Poly} (f : p âŸ¶ p') (q : Poly) : p âŠ— q âŸ¶ p' âŠ— q :=
  (tensor.map p p' q q) f (polyid q)

def tensor.first {p q r : Poly.{u, u}} (f : p âŸ¶ r) : p âŠ— q âŸ¶ r âŠ— q :=
  (tensor.map p r q q) f (polyid q)

def tensor.second {p q r : Poly.{u, u}} (g : q âŸ¶ r) : p âŠ— q âŸ¶ p âŠ— r :=
  (tensor.map p p q r) (polyid p) g

def tensor.swap {p q : Poly} : p âŠ— q âŸ¶ q âŠ— p :=
  { onPos := Î» (ppos , qpos) => (qpos , ppos)
  , onDir := Î» _ (qdir , pdir) => (pdir , qdir)
  }

def tensor.assoc.fwd {p q r : Poly} : p âŠ— (q âŠ— r) âŸ¶ (p âŠ— q) âŠ— r :=
  { onPos := Î» (ppos , qpos , rpos) => ((ppos , qpos) , rpos)
  , onDir := Î» _ ((pdir, qdir) , rdir) => (pdir , qdir , rdir)
  }

def tensor.assoc.bwd {p q r : Poly} : (p âŠ— q) âŠ— r âŸ¶ p âŠ— (q âŠ— r) :=
  { onPos := Î» ((ppos , qpos) , rpos) => (ppos , qpos , rpos)
  , onDir := Î» _ (pdir , qdir , rdir) => ((pdir , qdir) , rdir)
  }

def tensor.split.l {p : Poly} : p âŸ¶ p âŠ— p :=
  { onPos := Î» ppos => (ppos , ppos)
  , onDir := Î» _ (f , _) => f
  }

def tensor.split.r {p : Poly} : p âŸ¶ p âŠ— p :=
  { onPos := Î» ppos => (ppos , ppos)
  , onDir := Î» _ (_ , g) => g
  }

def tensor.unit.l.fwd {P : Poly} : y âŠ— P âŸ¶ P :=
  { onPos := Î» (_ , ppos) => ppos
  , onDir := Î» (Unit, _) pdir => (Unit , pdir)
  }

def tensor.unit.l.bwd {P : Poly} : P âŸ¶ y âŠ— P :=
  { onPos := Î» ppos => (Unit.unit , ppos)
  , onDir := Î» _ (_ , pdir) => pdir
  }

def tensor.unit.r.fwd {P : Poly} : P âŠ— y âŸ¶ P :=
  { onPos := Î» (ppos , _) => ppos
  , onDir := Î» (_ , Unit) pdir => (pdir , Unit)
  }

def tensor.unit.r.bwd {P : Poly} : P âŸ¶ P âŠ— y :=
  { onPos := Î» ppos => (ppos , Unit.unit)
  , onDir := Î» _ (pdir , _) => pdir
  }

def tensor.leftUnitor (p : Poly) : (y âŠ— p) â‰… p :=
  { hom := tensor.unit.l.fwd
  , inv := tensor.unit.l.bwd
  }

def tensor.rightUnitor (p : Poly) : (p âŠ— y) â‰… p :=
  { hom := tensor.unit.r.fwd
  , inv := tensor.unit.r.bwd
  }

def tensor.associator (p q r : Poly) : (p âŠ— q) âŠ— r â‰… p âŠ— (q âŠ— r) :=
  { hom := tensor.assoc.bwd
  , inv := tensor.assoc.fwd
  }

instance Poly.tensor.monoidalStruct : MonoidalCategoryStruct Poly where
  tensorObj    := tensor
  whiskerLeft  := tensor.whiskerLeft
  whiskerRight := tensor.whiskerRight
  tensorUnit   := y
  leftUnitor   := tensor.leftUnitor
  rightUnitor  := tensor.rightUnitor
  associator   := tensor.associator

/-- All hypotheses proven automatically so none provided. -/
instance Poly.tensor.monoidal : MonoidalCategory Poly where


-- /-!
-- ## âŠ—-closure
-- -/

-- /--
-- The internal hom-object under âŠ—.
-- I don't know enough about universes but I suppose
-- they should remain constant (the Us in {u, u} below).
-- -/

def homTensor (q r : Poly.{u, u}) : Poly.{u, u} where
  pos := q âŸ¶ r
  dir := Î» Ï† â†¦ Î£ (j : q.pos), (r.dir (Ï†.onPos j))

scoped notation:95 "âŸ¦" A:80 "," B:80 "âŸ§"  => homTensor A B

def homTensor.closed.right.fmap {p q r : Poly} (f : q âŸ¶ r) :
    (âŸ¦p, qâŸ§ âŸ¶ âŸ¦p, râŸ§) where
  onPos := (Â· â‰« f)
  onDir | _, (Sigma.mk pPos toDirR) => Sigma.mk pPos (f.onDir _ toDirR)

-- The functor âŸ¦r, -âŸ§
def homTensor.closed.right (r : Poly) : Poly â¥¤ Poly where
  obj := Î» x â†¦ âŸ¦r, xâŸ§
  map := Î» f â†¦ homTensor.closed.right.fmap f

-- TODO: Shouldn't this be used somewhere to prove something about
-- the internal hom?
def homTensor.eval (p r : Poly) : âŸ¦ p, r âŸ§ âŠ— p âŸ¶ r where
  onPos := Î» (Ï†, pPos) â†¦ Ï†.onPos pPos
  onDir := Î» (Ï†, pPos) dirR â†¦ (âŸ¨pPos, dirRâŸ©, Ï†.onDir pPos dirR)

def homTensor.closed.adjunction.homEquiv.toFun {p : Poly} (Ï† : (p âŠ— X âŸ¶ Y)) : (X âŸ¶ âŸ¦p, YâŸ§ ) :=
    let curriedOnPos (xPos : X.pos) : p âŸ¶ Y :=
        { onPos := Î» pPos â†¦ Ï†.onPos (pPos, xPos)
        -- We have to bee explicit about Ï†.onPos here; if we pattern match on Ï†
        -- to extract onPos, we get a type mismatch error.
        , onDir := Î» (pPos : p.pos) (yDir : Poly.dir Y (Ï†.onPos (pPos, xPos)))  â†¦
            let âŸ¨dirp, _âŸ©  := Ï†.onDir (pPos, xPos) yDir
            dirp }
    let curriedOnDir (xPos : X.pos) (homDir : (âŸ¦p, YâŸ§).dir (curriedOnPos xPos)) : X.dir xPos := match homDir with
        | âŸ¨pPos, ydirâŸ© =>
            let âŸ¨_, dirxâŸ©  := Ï†.onDir (pPos, xPos) ydir
            dirx
      { onPos := curriedOnPos
        onDir := curriedOnDir }

def homTensor.closed.adjunction.homEquiv.invFun {p : Poly} (Ïˆ : X âŸ¶ âŸ¦p, YâŸ§ ) : (p âŠ— X âŸ¶ Y) :=
  let uncurriedOnPos (pxPos : (p âŠ— X).pos) : Y.pos :=
    let âŸ¨pPos, xPosâŸ© := pxPos
    let intermediate := Ïˆ.onPos xPos
    intermediate.onPos pPos
  let uncurriedOnDir (pxPos : (p âŠ— X).pos) (pyDir : Y.dir (uncurriedOnPos pxPos)) : (p âŠ— X).dir pxPos :=
    let âŸ¨pPos, xPosâŸ© := pxPos
    let intermediate := Ïˆ.onPos xPos
    âŸ¨intermediate.onDir pPos pyDir, Ïˆ.onDir xPos âŸ¨pPos, pyDirâŸ©âŸ©
  { onPos := uncurriedOnPos,
    onDir := uncurriedOnDir }


def homTensor.closed.adjunction.homEquiv (p X Y : Poly) :
  (p âŠ— X âŸ¶ Y)  -- Hom(p âŠ— X, Y)  (same as X âŠ— p because âŠ— is symmetric)
  â‰ƒ
  (X âŸ¶ âŸ¦p, YâŸ§ ) -- Hom (X, âŸ¦p, YâŸ§)
  where
   toFun := homTensor.closed.adjunction.homEquiv.toFun
   invFun := homTensor.closed.adjunction.homEquiv.invFun
   left_inv := by
    intro Ïˆ
    unfold homTensor.closed.adjunction.homEquiv.toFun
    unfold homTensor.closed.adjunction.homEquiv.invFun
    simp
    rfl
   right_inv := by
    intro Ïˆ
    unfold homTensor.closed.adjunction.homEquiv.toFun
    unfold homTensor.closed.adjunction.homEquiv.invFun
    simp
    rfl

def homTensor.closed.adjunction (p : Poly) : MonoidalCategory.tensorLeft p âŠ£ homTensor.closed.right p :=
  Adjunction.mkOfHomEquiv {homEquiv := homTensor.closed.adjunction.homEquiv p}

instance : Closed (p : Poly) where
  isAdj := {right := homTensor.closed.right p, adj := homTensor.closed.adjunction p}

/-!
## Or product
-/

def or (p q : Poly.{u, u}) : Poly.{u, u} := p + (p Ã— q) + q

infixr:75 " âˆ¨ " => or

def or.map (p q r z : Poly.{u, u}) (f : p âŸ¶ q) (g : r âŸ¶ z) : (p âˆ¨ r) âŸ¶ (q âˆ¨ z) :=
    { onPos := Î» pos =>
      match pos with
      | .inl ppos => .inl (f.onPos ppos)
      | .inr (.inl (ppos , rpos)) => .inr (.inl (f.onPos ppos , g.onPos rpos))
      | .inr (.inr rpos) => .inr (.inr (g.onPos rpos))
    , onDir := Î» pos fib =>
      match pos with
      | .inl ppos => f.onDir ppos fib
      | .inr (.inl (ppos , rpos)) =>
        match fib with
        | .inl qfib => .inl (f.onDir ppos qfib)
        | .inr zfib => .inr (g.onDir rpos zfib)
      | .inr (.inr rpos) => g.onDir rpos fib
    }

def or.whiskerLeft (p : Poly) {q q' : Poly} (f : q âŸ¶ q') : p âˆ¨ q âŸ¶ p âˆ¨ q' :=
  (or.map p p q q' ) (polyid p) f

def or.whiskerRight {p p' : Poly} (f : p âŸ¶ p') (q : Poly) : p âˆ¨ q âŸ¶ p' âˆ¨ q :=
  (or.map p p' q q) f (polyid q)


-- | _âˆ¨_ This Inclusion
def This {p q : Poly} : p âŸ¶ p âˆ¨ q :=
  { onPos := .inl
  , onDir := Î» _ => id
  }

-- | _âˆ¨_ That Inclusion
def That {p q : Poly} : q âŸ¶ p âˆ¨ q :=
  { onPos := .inr âˆ˜ .inr
  , onDir := Î» _ => id
  }

-- | _âˆ¨_ These Inclusion
def These {p q : Poly} : (p Ã— q) âŸ¶ p âˆ¨ q :=
  { onPos := .inr âˆ˜ .inl
  , onDir := Î» _ => id
  }

-- | _âˆ¨_ Eliminator
def these {p q r : Poly} (f : p âŸ¶ r) (g : q âŸ¶ r) (h : (p Ã— q) âŸ¶ r) : ((p âˆ¨ q) âŸ¶ r) :=
  { onPos := Î» pos =>
    match pos with
    | .inl ppos => f.onPos ppos
    | .inr (.inl (ppos , qpos)) => h.onPos (ppos , qpos)
    | .inr (.inr qpos) => g.onPos qpos
  , onDir := Î» pos fib =>
    match pos with
    | .inl ppos => f.onDir ppos fib
    | .inr (.inl (ppos , qpos)) => h.onDir (ppos , qpos) fib
    | .inr (.inr qpos) => g.onDir qpos fib
  }


end Poly

end CategoryTheory
