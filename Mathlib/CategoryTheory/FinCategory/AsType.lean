/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.FinCategory.Basic
import Mathlib.Data.Fintype.EquivFin

/-!
# Finite categories are equivalent to category in `Type 0`.
-/


universe w v u

noncomputable section

namespace CategoryTheory

namespace FinCategory

variable (Î± : Type*) [Fintype Î±] [SmallCategory Î±] [FinCategory Î±]

/-- A FinCategory `Î±` is equivalent to a category with objects in `Type`. -/
--@[nolint unused_arguments]
abbrev ObjAsType : Type :=
  InducedCategory Î± (Fintype.equivFin Î±).symm

instance {i j : ObjAsType Î±} : Fintype (i âŸ¶ j) :=
  FinCategory.fintypeHom ((Fintype.equivFin Î±).symm i) _

/-- The constructed category is indeed equivalent to `Î±`. -/
noncomputable def objAsTypeEquiv : ObjAsType Î± â‰Œ Î± :=
  (inducedFunctor (Fintype.equivFin Î±).symm).asEquivalence

/-- A FinCategory `Î±` is equivalent to a fin_category with in `Type`. -/
--@[nolint unused_arguments]
abbrev AsType : Type :=
  Fin (Fintype.card Î±)

@[simps -isSimp id comp]
noncomputable instance categoryAsType : SmallCategory (AsType Î±) where
  Hom i j := Fin (Fintype.card (@Quiver.Hom (ObjAsType Î±) _ i j))
  id _ := Fintype.equivFin _ (ðŸ™ _)
  comp f g := Fintype.equivFin _ ((Fintype.equivFin _).symm f â‰« (Fintype.equivFin _).symm g)

attribute [local simp] categoryAsType_id categoryAsType_comp

/-- The "identity" functor from `AsType Î±` to `ObjAsType Î±`. -/
@[simps]
noncomputable def asTypeToObjAsType : AsType Î± â¥¤ ObjAsType Î± where
  obj := id
  map {_ _} := (Fintype.equivFin _).symm

/-- The "identity" functor from `ObjAsType Î±` to `AsType Î±`. -/
@[simps]
noncomputable def objAsTypeToAsType : ObjAsType Î± â¥¤ AsType Î± where
  obj := id
  map {_ _} := Fintype.equivFin _

/-- The constructed category (`AsType Î±`) is equivalent to `ObjAsType Î±`. -/
noncomputable def asTypeEquivObjAsType : AsType Î± â‰Œ ObjAsType Î± where
  functor := asTypeToObjAsType Î±
  inverse := objAsTypeToAsType Î±
  unitIso := NatIso.ofComponents Iso.refl
  counitIso := NatIso.ofComponents Iso.refl

noncomputable instance asTypeFinCategory : FinCategory (AsType Î±) where
  fintypeHom := fun _ _ => show Fintype (Fin _) from inferInstance

/-- The constructed category (`ObjAsType Î±`) is indeed equivalent to `Î±`. -/
noncomputable def equivAsType : AsType Î± â‰Œ Î± :=
  (asTypeEquivObjAsType Î±).trans (objAsTypeEquiv Î±)

end FinCategory

end CategoryTheory
