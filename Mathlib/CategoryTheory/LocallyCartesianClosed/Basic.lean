/-
Copyright (c) 2025 Sina Hazratpour. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sina Hazratpour, Emily Riehl
-/

import Mathlib.CategoryTheory.Adjunction.Over
import Mathlib.CategoryTheory.Closed.Cartesian

/-!
# Locally cartesian closed categories

There are several equivalent definitions of locally cartesian closed categories. For instance
the following two definitions are equivalent:

1. A locally cartesian closed category is a category `C` such that for every object `I`
  the slice category `Over I` is cartesian closed.

2. Equivalently, a locally cartesian closed category is a category with pullbacks such that
  the base change functor `Over.pullback f`, for every morphisms `f : I ‚ü∂ J`, has a right adjoint.
  This condition is equivalent to exponentiability of `f` in `Over J`. The right adjoint of
  `Over.pullback f` is called the pushforward functor.

In this file we prove the equivalence of these conditions.

## Implementation notes

The type class `LocallyCartesianClosed` extends `HasPushforwards` with the extra data carrying the
witness of cartesian closedness of the slice categories. `HasPushforwards.cartesianClosedOver` shows
that the cartesian closed structure of the slices follows from existence of pushforwards
along all morphisms. As such when instantiating a `LocallyCartesianClosed` structure,
providing the the cartesian closed structure of the slices is not necessary and it will be filled
in automatically. See `LocallyCartesianClosed.mkOfHasPushforwards` and
`LocallyCartesianClosed.mkOfCartesianClosedOver`.

The advanatge we obtain from this implementation is that when using a
`LocallyCartesianClosed` structure, both the pushforward functor and the cartesian closed structure
of slices are automatically available.

## Main results

- `pushforwardFunctor` constructs, in a category with cartesian closed slices, the pushforward
  functor along a morphism `f : I ‚ü∂ J`. On objects, this functors is defined
  as a certain pullback in the slice category `Over J`.
- `CartesianClosedOver.hasPushforwards` shows that `pushforwardFunctor` is right adjoint to the
  pullback functor.
- `HasPushforwards.cartesianClosedOver` shows that, in a category with pushforwards along all
  morphisms, the slice categories are cartesian closed.
- `LocallyCartesianClosed.cartesianClosed` proves that a locally cartesian closed category with a
  terminal object is cartesian closed.
- `LocallyCartesianClosed.overLocallyCartesianClosed` shows that the slices of a locally cartesian
  closed category are locally cartesian closed.

## Notations

- `Œ† X Y` is a notation for the pushforward of `Y` along `X` in the slice category `Over I`. It is
  defined as `(pushforward X.hom).obj Y`.

-/

noncomputable section

namespace CategoryTheory

open CategoryTheory Category MonoidalCategory Limits Functor Adjunction Over

universe v u

variable {C : Type u} [Category.{v} C]

/-- A morphism `f : I ‚ü∂ J` is exponentiable if the pullback functor `Over J ‚•§ Over I`
has a right adjoint. -/
class ExponentiableMorphism [HasPullbacks C] {I J : C} (f : I ‚ü∂ J) where
  /-- The pushforward functor -/
  pushforward : Over I ‚•§ Over J
  /-- The pushforward functor is right adjoint to the pullback functor -/
  adj : pullback f ‚ä£ pushforward := by infer_instance

namespace ExponentiableMorphism

variable [HasPullbacks C]

instance OverMkHom {I J : C} {f : I ‚ü∂ J} [ExponentiableMorphism f] :
    ExponentiableMorphism (Over.mk f).hom := by
  dsimp
  infer_instance

/-- The identity morphisms `ùüô` are exponentiable. -/
@[simps]
instance id {I : C} : ExponentiableMorphism (ùüô I) where
  pushforward := ùü≠ (Over I)
  adj := ofNatIsoLeft (F:= ùü≠ _) Adjunction.id (pullbackId).symm

/-- The conjugate iso between the pushforward of the identity and the identity of the
pushforward. -/
def pushfowardIdIso {I : C} : (id : ExponentiableMorphism (ùüô I)).pushforward ‚âÖ ùü≠ (Over I) :=
  conjugateIsoEquiv Adjunction.id id.adj pullbackId

/-- The composition of exponentiable morphisms is exponentiable. -/
def comp {I J K : C} (f : I ‚ü∂ J) (g : J ‚ü∂ K)
  [fexp : ExponentiableMorphism f] [gexp : ExponentiableMorphism g] :
  ExponentiableMorphism (f ‚â´ g) where
  pushforward := (pushforward f) ‚ãô (pushforward g)
  adj := ofNatIsoLeft (gexp.adj.comp fexp.adj) (pullbackComp f g).symm

/-- The conjugate isomorphism between pushforward of the composition and the composition of
pushforward functors. -/
def pushforwardCompIso {I J K : C} (f : I ‚ü∂ J) (g : J ‚ü∂ K)
  [fexp : ExponentiableMorphism f] [gexp : ExponentiableMorphism g] :
  (comp f g).pushforward ‚âÖ fexp.pushforward ‚ãô gexp.pushforward  :=
  conjugateIsoEquiv (gexp.adj.comp fexp.adj) ((comp f g).adj) (pullbackComp f g)

attribute [local instance] ChosenFiniteProducts.ofFiniteProducts

/-- A morphism with a pushforward is exponentiable in the slice category. -/
instance exponentiableOverMk [HasFiniteWidePullbacks C] {X I : C} (f : X ‚ü∂ I)
    [ExponentiableMorphism f] :
    Exponentiable (Over.mk f) where
  rightAdj := pullback f ‚ãô pushforward f
  adj := by
    apply ofNatIsoLeft _ _
    ¬∑ exact Over.pullback f ‚ãô Over.map f
    ¬∑ exact Adjunction.comp ExponentiableMorphism.adj (Over.mapPullbackAdj _)
    ¬∑ exact sigmaReindexNatIsoTensorLeft (Over.mk f)

end ExponentiableMorphism

variable (C)

/-- A category `HasPushforwards` if every morphism is exponentiable. -/
class HasPushforwards [HasFiniteWidePullbacks C] where
  exponentiable {I J : C} (f : I ‚ü∂ J) : ExponentiableMorphism f := by infer_instance

namespace HasPushforwards

open Over

variable {C} [HasFiniteWidePullbacks C] [HasPushforwards C]

def pushforward {I J : C} (f : I ‚ü∂ J) :
    Over I ‚•§ Over J :=
  (exponentiable f).pushforward

attribute [scoped instance] ChosenFiniteProducts.ofFiniteProducts

/-- In a category with pushforwards along all morphisms, every slice category `Over I` is
cartesian closed. -/
instance cartesianClosedOver
    [HasFiniteWidePullbacks C] [HasPushforwards C] (I : C) :
    CartesianClosed (Over I) where
  closed X := {
    rightAdj := (Over.pullback X.hom ‚ãô (HasPushforwards.exponentiable X.hom).pushforward)
    adj := ofNatIsoLeft (F := (Over.pullback X.hom ‚ãô Over.map X.hom))
      (((HasPushforwards.exponentiable X.hom).adj).comp (Over.mapPullbackAdj X.hom))
      (Over.sigmaReindexNatIsoTensorLeft _)
  }

section Notation

def pushforwardObj {I : C} (X : Over I) (Y : Over (X.left)) : Over I :=
  (pushforward X.hom).obj Y

set_option quotPrecheck false in
scoped notation " Œ†_ " => pushforwardObj

end Notation

end HasPushforwards

namespace CartesianClosedOver

open Over Reindex IsIso ChosenFiniteProducts CartesianClosed HasPushforwards

variable {C} [HasFiniteWidePullbacks C] {I J : C} [CartesianClosed (Over J)] (f : I ‚ü∂ J)

/-- The first leg of a cospan constructing a pullback diagram in `Over J` used to define
the pushforward along `f`. -/
def curryId : Over.mk (ùüô J) ‚ü∂ (Over.mk f ‚üπ Over.mk f) :=
  CartesianClosed.curry (fst _ _)

/-- The second leg of a cospan constructing a pullback diagram in `Over J` used to define
the pushforward along `f`. -/
def expMapFstProj (X : Over I) :
    (Over.mk f ‚üπ ((Over.map f).obj X)) ‚ü∂ (Over.mk f ‚üπ Over.mk f) :=
  (exp _).map (Over.homMk X.hom)

/-- Pushforward along `f : I ‚ü∂ J` as a pullback in `Over J`. -/
def pushforwardObj (X : Over I) : Over J :=
  pullback (curryId f) (expMapFstProj f X)

/-- The functoriality of the pushforward derived form the functoriality of the exponentiation and
the functoriality of the pullback construction in the slice category `Over J`. -/
def pushforwardMap {X X' : Over I} (u : X ‚ü∂ X') :
    (pushforwardObj f X) ‚ü∂ (pushforwardObj f X') := by
  refine pullback.map _ _ _ _ (ùüô (Over.mk (ùüô J)))
      ((exp _).map ((Over.map f).map u)) (ùüô (Over.mk f ‚üπ Over.mk f))
    ?_ ?_
  ¬∑ simp
  ¬∑ unfold expMapFstProj
    simp only [comp_id, ‚Üê (exp (Over.mk f)).map_comp]
    congr
    simp [map_obj_left, mk_left, map_map_left, homMk_left, w]

/-- The pushforward functor `(Over I) ‚•§ (Over J)` of a morphism `f : I ‚ü∂ J` in a category `C`
using the cartesian closedness of `Over J`. -/
@[simps]
def pushforwardFunctor :
    (Over I) ‚•§ (Over J) where
  obj X := pullback (curryId f) (expMapFstProj f X)
  map u := pushforwardMap f u
  map_id X := by
    apply pullback.hom_ext
    ¬∑ simp [pushforwardMap]
    ¬∑ simp [pushforwardMap, expMapFstProj]
  map_comp := by
    apply fun X Y Z u v ‚Ü¶ pullback.hom_ext _ _
    ¬∑ unfold pushforwardMap
      simp
    ¬∑ simp [pushforwardMap, expMapFstProj]

variable {f}

/-- An auxiliary morphism used to define the currying of a morphism in `Over I` to a morphism
in `Over J`. See `pushforwardCurry`. -/
def pushforwardCurryAux {X : Over I} {A : Over J} (u : (Over.pullback f).obj A ‚ü∂ X) :
    A ‚ü∂ (Over.mk f ‚üπ (Over.map f).obj X) :=
  CartesianClosed.curry ((sigmaReindexIsoProd _ _).inv ‚â´ (Over.map f).map u)

/-- The currying of `(Over.pullback f).obj A ‚ü∂ X` in `Over I` to a morphism
`A ‚ü∂ (pushforward f).obj X` in `Over J`. -/
def pushforwardCurry {X : Over I} {A : Over J} (u : (Over.pullback f).obj A ‚ü∂ X) :
    A ‚ü∂ (pushforwardFunctor f).obj X := by
  apply pullback.lift ((mkIdTerminal (X := J)).from A) (pushforwardCurryAux u)
    ((uncurry_injective (A := Over.mk f)) _)
  rw [uncurry_natural_left, curryId, pushforwardCurryAux]
  simp [uncurry_curry, expMapFstProj, uncurry_natural_right]
  have sigmaReindexIsoProd_inv_comp_pi :
      (sigmaReindexIsoProd (Over.mk f) A).inv ‚â´ (œÄ_ (Over.mk f) A) = fst (Over.mk f) A := by
    rw [Iso.inv_comp_eq]
    simp [sigmaReindexIsoProd_hom_comp_fst]
  have : ((Over.map f).map u ‚â´ (homMk X.hom rfl : (Over.map f).obj X ‚ü∂ Over.mk f)) =
      œÄ_ (Over.mk f) A  := OverMorphism.ext (by aesop_cat)
  simp_rw [this, sigmaReindexIsoProd_inv_comp_pi]

/-- The uncurrying of `A ‚ü∂ (pushforward f).obj X` in `Over J` to a morphism
`(Over.pullback f).obj A ‚ü∂ X` in `Over I`. -/
def pushforwardUncurry {X : Over I} {A : Over J} (v : A ‚ü∂ (pushforwardFunctor f).obj X) :
    (Over.pullback f).obj A ‚ü∂ X := by
  let v‚ÇÅ : A ‚ü∂ Over.mk (ùüô J) := v ‚â´ (pullback.fst ..)
  let v‚ÇÇ : A ‚ü∂ ((Over.mk f) ‚üπ ((Over.map f).obj X)) := v ‚â´ pullback.snd ..
  have w : (mkIdTerminal (X := J)).from A ‚â´ (curryId f) =
      v‚ÇÇ ‚â´ (expMapFstProj f X) := by
    rw [IsTerminal.hom_ext mkIdTerminal ( (mkIdTerminal (X := J)).from A) v‚ÇÅ]
    simp [v‚ÇÅ, v‚ÇÇ, pullback.condition]
  dsimp [curryId, expMapFstProj] at w
  have w' := homEquiv_naturality_right_square (F := MonoidalCategory.tensorLeft (Over.mk f))
    (adj := exp.adjunction (Over.mk f)) _ _ _ _ w
  simp [CartesianClosed.curry] at w'
  refine Sigma.overHomMk ((sigmaReindexIsoProd _ _).hom ‚â´ (CartesianClosed.uncurry v‚ÇÇ)) ?_
  ¬∑ dsimp [CartesianClosed.uncurry] at *
    simp [Over.Sigma.fst, Over.Sigma]
    rw [‚Üê w']
    simp [sigmaReindexIsoProd_hom_comp_fst]

@[simp]
theorem pushforward_curry_uncurry {X : Over I} {A : Over J} (v : A ‚ü∂ (pushforwardFunctor f).obj X) :
    pushforwardCurry (pushforwardUncurry v) = v := by
  dsimp [pushforwardCurry, pushforwardUncurry, pushforwardCurryAux]
  let v‚ÇÅ : A ‚ü∂ Over.mk (ùüô J) := v ‚â´ (pullback.fst ..)
  let v‚ÇÇ : A ‚ü∂ ((Over.mk f) ‚üπ ((Over.map f).obj X)) := v ‚â´ pullback.snd _ _
  apply pullback.hom_ext
  ¬∑ simp
    rw [IsTerminal.hom_ext mkIdTerminal ((mkIdTerminal (X := J)).from A) v‚ÇÅ]
  ¬∑ simp
    apply (CartesianClosed.curry_eq_iff _ _).mpr
    ext
    simp [Sigma.overHomMk]
    rw [‚Üê assoc]
    have inv_hom_id := (sigmaReindexIsoProd (Over.mk f) A).inv_hom_id
    apply_fun (Over.forget _).map at inv_hom_id
    rw [(Over.forget _).map_id, (Over.forget _).map_comp] at inv_hom_id
    simp at inv_hom_id
    exact inv_hom_id ‚ñ∏ id_comp (CartesianClosed.uncurry v‚ÇÇ).left

theorem pushforward_uncurry_curry {X : Over I} {A : Over J} (u : (Over.pullback f).obj A ‚ü∂ X) :
    pushforwardUncurry (pushforwardCurry u) = u := by
  unfold pushforwardCurry pushforwardUncurry pushforwardCurryAux
  ext
  simp [Sigma.overHomMk]

end CartesianClosedOver

attribute [local instance] ChosenFiniteProducts.ofFiniteProducts

/-- A category with cartesian closed slices has pushforwards along all morphisms. -/
instance CartesianClosedOver.hasPushforwards [HasFiniteWidePullbacks C]
    [Œ† (I : C), CartesianClosed (Over I)] : HasPushforwards C where
  exponentiable {I J} f := {
    pushforward := pushforwardFunctor f
    adj := mkOfHomEquiv {
      homEquiv A X := {
        toFun := pushforwardCurry
        invFun := pushforwardUncurry
        left_inv := pushforward_uncurry_curry
        right_inv := pushforward_curry_uncurry
      }
      homEquiv_naturality_left_symm := by
        intro A' A X g v
        unfold pushforwardUncurry
        dsimp
        have natiso := (Over.sigmaReindexNatIsoTensorLeft (Over.mk f)).hom.naturality g
        simp only [Over.sigmaReindexNatIsoTensorLeft_hom_app, tensorLeft_map] at natiso
        simp_rw [CartesianClosed.uncurry_natural_left, MonoidalCategory.whiskerLeft_comp]
        simp_rw [‚Üê assoc, ‚Üê natiso]
        simp [Sigma.overHomMk]
        rfl
      homEquiv_naturality_right := by
        intro A X' X u g
        unfold pushforwardCurry
        dsimp
        apply pullback.hom_ext (IsTerminal.hom_ext mkIdTerminal _ _)
        unfold pushforwardCurryAux pushforwardMap
        simp [pullback.lift_snd]
        rw [‚Üê CartesianClosed.curry_natural_right, assoc]
    }
  }

/-- A category with `FiniteWidePullbacks` is locally cartesian closed if every morphisms in it
is exponentiable and all the slices are cartesian closed. -/
class LocallyCartesianClosed [HasFiniteWidePullbacks C] extends
    HasPushforwards C where
  cartesianClosedOver : Œ† (I : C), CartesianClosed (Over I) := HasPushforwards.cartesianClosedOver

namespace LocallyCartesianClosed

open Over Sigma Reindex HasPushforwards

variable {C} [HasFiniteWidePullbacks C]

attribute [scoped instance] hasFiniteLimits_of_hasTerminal_and_pullbacks

instance mkOfHasPushforwards [HasPushforwards C] : LocallyCartesianClosed C where

instance mkOfCartesianClosedOver [Œ† (I : C), CartesianClosed (Over I)] :
  LocallyCartesianClosed C where

variable [LocallyCartesianClosed C]

/-- The exponential `X^^A` in the slice category `Over I` is isomorphic to the pushforward of the
pullback of `X` along `A`. -/
def expIso {I : C} (A X : Over I) :  Œ†_ A (Œî_ A X) ‚âÖ X^^A := Iso.refl _

/-- The dependent evaluation morphisms. -/
abbrev ev {I : C} (X : Over I) (Y : Over X.left) : Œî_ X (Œ†_ X Y) ‚ü∂ Y :=
  (exponentiable X.hom).adj.counit.app Y

/-- A locally cartesian closed category with a terminal object is cartesian closed. -/
def cartesianClosed [HasTerminal C] :
    CartesianClosed C := cartesianClosedOfEquiv <| equivOverTerminal C

/-- The slices of a locally cartesian closed category are locally cartesian closed. -/
def overLocallyCartesianClosed (I : C) : LocallyCartesianClosed (Over I) := by
  apply (config := { allowSynthFailures:= true}) mkOfCartesianClosedOver
  intro X
  exact cartesianClosedOfEquiv (C := Over (X.left)) X.iteratedSliceEquiv.symm

end LocallyCartesianClosed

end CategoryTheory
