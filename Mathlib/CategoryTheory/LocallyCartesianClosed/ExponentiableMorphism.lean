/-
Copyright (c) 2025 Sina Hazratpour. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sina Hazratpour
-/
module

public import Mathlib.CategoryTheory.LocallyCartesianClosed.ChosenPullbacksAlong

/-!
# Exponentiable morphisms

We define an exponentiable morphism `f : I âŸ¶ J` to be a morphism with a functorial choice of
pullbacks, given by `ChosenPullbacksAlong f`, together with a right adjoint to
the pullback functor `ChosenPullbacksAlong.pulback f : Over J â¥¤ Over I`. We call this right adjoint
the pushforward functor along `f`.

## Main results

- The identity morphisms are exponentiable.
- The composition of exponentiable morphisms is exponentiable.

### TODO

- Any pullback of an exponentiable morphism is exponentiable.

-/

@[expose] public section

universe v u

namespace CategoryTheory

open Category MonoidalCategory Functor Adjunction

open ChosenPullbacksAlong

variable {C : Type u} [Category.{v} C]

/-- A morphism `f : I âŸ¶ J` is exponentiable if the pullback functor `Over J â¥¤ Over I`
has a right adjoint. -/
class ExponentiableMorphism {I J : C} (f : I âŸ¶ J) [ChosenPullbacksAlong f] where
  /-- The pushforward functor -/
  pushforward : Over I â¥¤ Over J
  /-- The pushforward functor is right adjoint to the pullback functor -/
  pullbackPushforwardAdj (f) : pullback f âŠ£ pushforward

/-- A morphism `f : I âŸ¶ J` is exponentiable if the pullback functor `Over J â¥¤ Over I`
has a right adjoint. -/
abbrev IsExponentiable [ChosenPullbacks C] : MorphismProperty C :=
  fun _ _ f â†¦ IsLeftAdjoint (pullback f)

namespace ExponentiableMorphism

instance isExponentiable [ChosenPullbacks C] {I J : C} (f : I âŸ¶ J) [ExponentiableMorphism f] :
  IsExponentiable f := âŸ¨pushforward f, âŸ¨pullbackPushforwardAdj fâŸ©âŸ©

section

variable {I J : C} (f : I âŸ¶ J) [ChosenPullbacksAlong f] [ExponentiableMorphism f]

/-- The dependent evaluation natural transformation as the counit of the adjunction. -/
def ev : pushforward f â‹™ pullback f âŸ¶ ðŸ­ _ :=
  pullbackPushforwardAdj f |>.counit

/-- The dependent coevaluation natural transformation as the unit of the adjunction. -/
def coev : ðŸ­ _ âŸ¶ pullback f â‹™ pushforward f :=
  pullbackPushforwardAdj f |>.unit

@[simp]
theorem ev_def : ev f = (pullbackPushforwardAdj f).counit :=
  rfl

@[simp]
theorem coev_def : coev f = (pullbackPushforwardAdj f).unit :=
  rfl

@[reassoc]
theorem ev_naturality {X Y : Over I} (g : X âŸ¶ Y) :
    (pullback f).map ((pushforward f).map g) â‰« (ev f).app Y = (ev f).app X â‰« g :=
  ev f |>.naturality g

@[reassoc]
theorem coev_naturality {X Y : Over J} (g : X âŸ¶ Y) :
    g â‰« (coev f).app Y = (coev f).app X â‰« (pushforward f).map ((pullback f).map g) :=
  coev f |>.naturality g

/-- The first triangle identity for the counit and unit of the adjunction. -/
@[reassoc]
theorem ev_coev (X : Over J) :
    (pullback f).map (coev f |>.app X) â‰« (ev f |>.app (pullback f |>.obj X)) =
    ðŸ™ (pullback f |>.obj X) :=
  pullbackPushforwardAdj f |>.left_triangle_components X

/-- The second triangle identity for the counit and unit of the adjunction. -/
@[reassoc]
theorem coev_ev (Y : Over I) :
    (coev f |>.app (pushforward f |>.obj Y)) â‰«
    (pushforward f |>.map (ev f |>.app Y)) =
    ðŸ™ (pushforward f |>.obj Y) :=
  pullbackPushforwardAdj f |>.right_triangle_components Y

variable {f}

/-- The currying of `(pullback f).obj A âŸ¶ X` in `Over I` to a morphism `A âŸ¶ (pushforward f).obj X`
in `Over J`. -/
def pushforwardCurry {X : Over I} {A : Over J}
    (u : (pullback f).obj A âŸ¶ X) :
    A âŸ¶ (pushforward f).obj X :=
  pullbackPushforwardAdj f |>.homEquiv A X u

/-- The uncurrying of `A âŸ¶ (pushforward f).obj X` in `Over J` to a morphism
`(Over.pullback f).obj A âŸ¶ X` in `Over I`. -/
def pushforwardUncurry {X : Over I} {A : Over J}
    (v : A âŸ¶ (pushforward f).obj X) :
    (pullback f).obj A âŸ¶ X :=
  pullbackPushforwardAdj f |>.homEquiv A X |>.invFun v

theorem homEquiv_apply_eq {X : Over I} {A : Over J} (u : (pullback f).obj A âŸ¶ X) :
    (pullbackPushforwardAdj f |>.homEquiv _ _) u = pushforwardCurry u :=
  rfl

theorem homEquiv_symm_apply_eq {X : Over I} {A : Over J} (v : A âŸ¶ (pushforward f).obj X) :
    (pullbackPushforwardAdj f |>.homEquiv _ _).symm v = pushforwardUncurry v :=
  rfl

theorem pushforward_uncurry_curry {X : Over I} {A : Over J}
    (u : (pullback f).obj A âŸ¶ X) :
    pushforwardUncurry (pushforwardCurry u) = u :=
  pullbackPushforwardAdj f |>.homEquiv A X |>.left_inv u

theorem pushforward_curry_uncurry {X : Over I} {A : Over J} (v : A âŸ¶ (pushforward f).obj X) :
    pushforwardCurry (pushforwardUncurry v) = v :=
  pullbackPushforwardAdj f |>.homEquiv A X |>.right_inv v

instance : ChosenPullbacksAlong (Over.mk f).hom := by
  dsimp only [Over.mk_hom]
  infer_instance

instance OverMkHom : ExponentiableMorphism (Over.mk f).hom := by
  dsimp only [Over.mk_hom]
  infer_instance

end

section

/-- The identity morphisms `ðŸ™ _` are exponentiable. -/
def id (I : C) [ChosenPullbacksAlong (ðŸ™ I)] : ExponentiableMorphism (ðŸ™ I) :=
  âŸ¨ðŸ­ _, ofNatIsoLeft (F := ðŸ­ _) Adjunction.id (pullbackId I).symmâŸ©

theorem id_pushforward (I : C) [ChosenPullbacksAlong (ðŸ™ I)] :
    (id I).pushforward = ðŸ­ (Over I) := by
  dsimp only [id]

/-- Any pushforward of the identity morphism is naturally isomorphic to the identity functor. -/
def pushforwardId (I : C) [ChosenPullbacksAlong (ðŸ™ I)] [ExponentiableMorphism (ðŸ™ I)] :
    pushforward (ðŸ™ I) â‰… ðŸ­ (Over I) :=
  Adjunction.rightAdjointUniq (pullbackPushforwardAdj (ðŸ™ I)) (id I).pullbackPushforwardAdj

@[reassoc (attr := simp)]
theorem unit_pushforwardId_hom (I : C) [ChosenPullbacksAlong (ðŸ™ I)] [ExponentiableMorphism (ðŸ™ I)] :
    (pullbackPushforwardAdj (ðŸ™ I)).unit â‰«
      (pullback (ðŸ™ I)).whiskerLeft (pushforwardId I).hom =
      (id I).pullbackPushforwardAdj.unit := by
  rw [pushforwardId, Adjunction.unit_rightAdjointUniq_hom]

@[reassoc (attr := simp)]
theorem pushforwardId_hom_counit (I : C) [ChosenPullbacksAlong (ðŸ™ I)]
    [ExponentiableMorphism (ðŸ™ I)] :
    Functor.whiskerRight (pushforwardId I).hom (pullback (ðŸ™ I)) â‰«
      (id I).pullbackPushforwardAdj.counit =
      (pullbackPushforwardAdj (ðŸ™ I)).counit := by
  rw [pushforwardId, Adjunction.rightAdjointUniq_hom_counit]

/-- The composition of exponentiable morphisms is exponentiable. -/
def comp {I J K : C} (f : I âŸ¶ J) (g : J âŸ¶ K)
    [ChosenPullbacksAlong f] [ChosenPullbacksAlong g] [ChosenPullbacksAlong (f â‰« g)]
    [ExponentiableMorphism f] [ExponentiableMorphism g] :
    ExponentiableMorphism (f â‰« g) :=
  âŸ¨pushforward f â‹™ pushforward g,
    ofNatIsoLeft (pullbackPushforwardAdj g |>.comp <| pullbackPushforwardAdj f)
    (pullbackComp f g).symmâŸ©

theorem comp_pushforward {I J K : C} (f : I âŸ¶ J) (g : J âŸ¶ K)
    [ChosenPullbacksAlong f] [ChosenPullbacksAlong g] [ChosenPullbacksAlong (f â‰« g)]
    [ExponentiableMorphism f] [ExponentiableMorphism g] :
    (comp f g).pushforward = pushforward f â‹™ pushforward g := by
  dsimp only [comp]

/-- The natural isomorphism between pushforward of the composition and the composition of
pushforward functors. -/
def pushforwardComp {I J K : C} (f : I âŸ¶ J) (g : J âŸ¶ K)
    [ChosenPullbacksAlong f] [ChosenPullbacksAlong g] [ChosenPullbacksAlong (f â‰« g)]
    [ExponentiableMorphism f] [ExponentiableMorphism g] [ExponentiableMorphism (f â‰« g)] :
    pushforward (C := C) (f â‰« g) â‰… pushforward f â‹™ pushforward g :=
  Adjunction.rightAdjointUniq (pullbackPushforwardAdj (f â‰« g)) ((comp f g).pullbackPushforwardAdj)

@[reassoc (attr := simp)]
theorem unit_pushforwardComp_hom {I J K : C} (f : I âŸ¶ J) (g : J âŸ¶ K)
    [ChosenPullbacksAlong f] [ChosenPullbacksAlong g] [ChosenPullbacksAlong (f â‰« g)]
    [ExponentiableMorphism f] [ExponentiableMorphism g] [ExponentiableMorphism (f â‰« g)] :
    (pullbackPushforwardAdj (f â‰« g)).unit â‰«
      (pullback (f â‰« g)).whiskerLeft (pushforwardComp f g).hom =
      (comp f g).pullbackPushforwardAdj.unit := by
  rw [pushforwardComp, Adjunction.unit_rightAdjointUniq_hom]

@[reassoc (attr := simp)]
theorem pushforwardComp_hom_counit {I J K : C} (f : I âŸ¶ J) (g : J âŸ¶ K)
    [ChosenPullbacksAlong f] [ChosenPullbacksAlong g] [ChosenPullbacksAlong (f â‰« g)]
    [ExponentiableMorphism f] [ExponentiableMorphism g] [ExponentiableMorphism (f â‰« g)] :
    Functor.whiskerRight (pushforwardComp f g).hom (pullback (f â‰« g)) â‰«
      (comp f g).pullbackPushforwardAdj.counit =
      (pullbackPushforwardAdj (f â‰« g)).counit := by
  rw [pushforwardComp, Adjunction.rightAdjointUniq_hom_counit]

end

end ExponentiableMorphism

end CategoryTheory
