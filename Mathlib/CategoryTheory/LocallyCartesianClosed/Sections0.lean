/-
Copyright (c) 2025 Sina Hazratpour. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sina Hazratpour
-/
import Mathlib.CategoryTheory.LocallyCartesianClosed.Prelim

/-!
# The section functor as a right adjoint to the star functor

We show that if `C` is cartesian closed then `star I : C â¥¤ Over I`
has a right adjoint `sectionsFunctor` whose object part is the object of sections
of `X` over `I`.

-/

noncomputable section

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

namespace CategoryTheory

open Category Limits MonoidalCategory CartesianClosed Adjunction Over

variable {C : Type uâ‚} [Category.{vâ‚} C]

attribute [local instance] hasBinaryProducts_of_hasTerminal_and_pullbacks
attribute [local instance] hasFiniteProducts_of_has_binary_and_terminal
attribute [local instance] CartesianMonoidalCategory.ofFiniteProducts

section

variable [HasFiniteProducts C]

/-- The isomorphism between `X â¨¯ Y` and `X âŠ— Y` for objects `X` and `Y` in `C`.
This is tautological by the definition of the cartesian monoidal structure on `C`.
This isomorphism provides an interface between `prod.fst` and `ChosenFiniteProducts.fst`
as well as between `prod.map` and `tensorHom`. -/
def prodIsoTensorObj (X Y : C) : X â¨¯ Y â‰… X âŠ— Y := Iso.refl _

@[reassoc (attr := simp)]
theorem prodIsoTensorObj_inv_fst {X Y : C} :
    (prodIsoTensorObj X Y).inv â‰« prod.fst = CartesianMonoidalCategory.fst X Y :=
  Category.id_comp _

@[reassoc (attr := simp)]
theorem prodIsoTensorObj_inv_snd {X Y : C} :
    (prodIsoTensorObj X Y).inv â‰« prod.snd = CartesianMonoidalCategory.snd X Y :=
  Category.id_comp _

@[reassoc (attr := simp)]
theorem prodIsoTensorObj_hom_fst {X Y : C} :
    (prodIsoTensorObj X Y).hom â‰« CartesianMonoidalCategory.fst X Y = prod.fst :=
  Category.id_comp _

@[reassoc (attr := simp)]
theorem prodIsoTensorObj_hom_snd {X Y : C} :
    (prodIsoTensorObj X Y).hom â‰« CartesianMonoidalCategory.snd X Y = prod.snd :=
  Category.id_comp _

@[reassoc (attr := simp)]
theorem prodMap_comp_prodIsoTensorObj_hom {X Y Z W : C} (f : X âŸ¶ Y) (g : Z âŸ¶ W) :
    prod.map f g â‰« (prodIsoTensorObj _ _).hom = (prodIsoTensorObj _ _).hom â‰« (f âŠ—â‚˜ g) := by
  apply CartesianMonoidalCategory.hom_ext <;> simp

end

variable [HasTerminal C] [HasPullbacks C]

variable (I : C) [Exponentiable I]

/-- The first leg of a cospan constructing a pullback diagram in `C` used to define `sections` . -/
def curryId : âŠ¤_ C âŸ¶ (I âŸ¹ I) :=
  CartesianClosed.curry (CartesianMonoidalCategory.fst I (âŠ¤_ C))

variable {I}

namespace Over

/-- Given `X : Over I`, `sectionsObj X` is the object of sections of `X` defined by the following
pullback diagram:

```
 sections X -->  I âŸ¹ X
   |               |
   |               |
   v               v
  âŠ¤_ C    ---->  I âŸ¹ I
```
-/
abbrev sectionsObj (X : Over I) : C :=
  Limits.pullback (curryId I) ((exp I).map X.hom)

/-- The functoriality of `sectionsObj`. -/
def sectionsMap {X X' : Over I} (u : X âŸ¶ X') :
    sectionsObj X âŸ¶ sectionsObj X' := by
  fapply pullback.map
  Â· exact ğŸ™ _
  Â· exact (exp I).map u.left
  Â· exact ğŸ™ _
  Â· simp only [comp_id, id_comp]
  Â· simp only [comp_id, â† Functor.map_comp, w]

@[simp]
lemma sectionsMap_id {X : Over I} : sectionsMap (ğŸ™ X) = ğŸ™ _ := by
  apply pullback.hom_ext
  Â· aesop
  Â· simp [sectionsMap]

@[simp]
lemma sectionsMap_comp {X X' X'' : Over I} (u : X âŸ¶ X') (v : X' âŸ¶ X'') :
    sectionsMap (u â‰« v) = sectionsMap u â‰« sectionsMap v := by
  apply pullback.hom_ext
  Â· aesop
  Â· simp [sectionsMap]

variable (I)

/-- The functor mapping an object `X` in `C` to the object of sections of `X` over `I`. -/
@[simps]
def sections :
    Over I â¥¤ C where
  obj X := sectionsObj X
  map u := sectionsMap u

variable {I}

/-- An auxiliary morphism used to define the currying of a morphism in `Over I` to a morphism
in `C`. See `sectionsCurry`. -/
def sectionsCurryAux {X : Over I} {A : C} (u : (star I).obj A âŸ¶ X) :
    A âŸ¶ (I âŸ¹ X.left) :=
  CartesianClosed.curry (u.left)

/-- The currying operation `Hom ((star I).obj A) X â†’ Hom A (I âŸ¹ X.left)`. -/
def sectionsCurry {X : Over I} {A : C} (u : (star I).obj A âŸ¶ X) :
    A âŸ¶ (sections I).obj X := by
  apply pullback.lift (terminal.from A)
    (CartesianClosed.curry ((prodIsoTensorObj _ _).inv â‰« u.left)) (uncurry_injective _)
  rw [uncurry_natural_left]
  simp [curryId, uncurry_natural_right, uncurry_curry]

/-- The uncurrying operation `Hom A (section X) â†’ Hom ((star I).obj A) X`. -/
def sectionsUncurry {X : Over I} {A : C} (v : A âŸ¶ (sections I).obj X) :
    (star I).obj A âŸ¶ X := by
  let vâ‚‚ : A âŸ¶ (I âŸ¹ X.left) := v â‰« pullback.snd ..
  have w : terminal.from A â‰« (curryId I) = vâ‚‚ â‰« (exp I).map X.hom := by
    rw [IsTerminal.hom_ext terminalIsTerminal (terminal.from A ) (v â‰« (pullback.fst ..))]
    simp [vâ‚‚, pullback.condition]
  dsimp [curryId] at w
  have w' := homEquiv_naturality_right_square (F := MonoidalCategory.tensorLeft I)
    (adj := exp.adjunction I) _ _ _ _ w
  simp [CartesianClosed.curry] at w'
  refine Over.homMk ((prodIsoTensorObj I A).hom â‰« CartesianClosed.uncurry vâ‚‚) ?_
  Â· dsimp [CartesianClosed.uncurry] at *
    rw [Category.assoc, â† w']
    simp [star_obj_hom]

@[simp]
theorem sections_curry_uncurry {X : Over I} {A : C} {v : A âŸ¶ (sections I).obj X} :
    sectionsCurry (sectionsUncurry v) = v := by
  dsimp [sectionsCurry, sectionsUncurry]
  let vâ‚‚ : A âŸ¶ (I âŸ¹ X.left) := v â‰« pullback.snd _ _
  apply pullback.hom_ext
  Â· simp
    rw [IsTerminal.hom_ext terminalIsTerminal (terminal.from A ) (v â‰« (pullback.fst ..))]
  Â· simp

@[simp]
theorem sections_uncurry_curry {X : Over I} {A : C} {u : (star I).obj A âŸ¶ X} :
    sectionsUncurry (sectionsCurry u) = u := by
  dsimp [sectionsCurry, sectionsUncurry]
  ext
  simp

/-- An auxiliary definition which is used to define the adjunction between the star functor
and the sections functor. See starSectionsAdjunction`. -/
def coreHomEquiv : CoreHomEquiv (star I) (sections I) where
  homEquiv A X := {
    toFun := sectionsCurry
    invFun := sectionsUncurry
    left_inv {u} := sections_uncurry_curry
    right_inv {v} := sections_curry_uncurry
  }
  homEquiv_naturality_left_symm := by
    intro A' A X g v
    dsimp [sectionsCurry, sectionsUncurry, curryId]
    simp only [star_map]
    rw [â† Over.homMk_comp]
    congr 1
    simp [CartesianClosed.uncurry_natural_left]
  homEquiv_naturality_right := by
    intro A X' X u g
    dsimp [sectionsCurry, sectionsUncurry, curryId]
    apply pullback.hom_ext (IsTerminal.hom_ext terminalIsTerminal _ _)
    simp [sectionsMap, curryId]
    rw [â† CartesianClosed.curry_natural_right, Category.assoc]

variable (I)

/-- The adjunction between the star functor and the sections functor. -/
@[simps! unit_app counit_app]
def starSectionsAdj : star I âŠ£ sections I :=
  .mkOfHomEquiv coreHomEquiv

end Over

end CategoryTheory
