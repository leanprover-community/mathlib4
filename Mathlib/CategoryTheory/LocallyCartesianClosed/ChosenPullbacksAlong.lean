/-
Copyright (c) 2025 Sina Hazratpour. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sina Hazratpour
-/
module

public import Mathlib.CategoryTheory.Comma.Over.Pullback
public import Mathlib.CategoryTheory.Limits.Shapes.Pullback.IsPullback.BicartesianSq
public import Mathlib.CategoryTheory.Monoidal.Cartesian.Basic
public import Mathlib.CategoryTheory.Adjunction.Unique

/-!
# Chosen pullbacks along a morphism

## Main declarations

- `ChosenPullbacksAlong` : For a morphism `f : Y ‚ü∂ X` in `C`, the type class
  `ChosenPullbacksAlong f` provides the data of a pullback functor `Over X ‚•§ Over Y`
  as a right adjoint to `Over.map f`.

## Main results

- We prove that `ChosenPullbacksAlong` has good closure properties: isos have chosen pullbacks,
  and composition of morphisms with chosen pullbacks have chosen pullbacks.

-  We prove that chosen pullbacks yields usual pullbacks: `ChosenPullbacksAlong.isPullback`
  proves that for morphisms `f` and `g` with the same codomain, the object
  `ChosenPullbacksAlong.pullbackObj f g` together with morphisms
  `ChosenPullbacksAlong.fst f g` and `ChosenPullbacksAlong.snd f g` form a pullback square
  over `f` and `g`.

- We prove that in cartesian monoidal categories, morphisms to the terminal tensor unit and
  the product projections have chosen pullbacks.

-/

@[expose] public section

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory

open Category Limits CartesianMonoidalCategory MonoidalCategory Over

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]

/-- A functorial choice of pullbacks along a morphism `f : Y ‚ü∂ X` in `C` given by a functor
`Over X ‚•§ Over Y` which is a right adjoint to the functor `Over.map f`. -/
class ChosenPullbacksAlong {Y X : C} (f : Y ‚ü∂ X) where
  /-- The pullback functor along `f`. -/
  pullback : Over X ‚•§ Over Y
  /-- The adjunction between `Over.map f` and `pullback f`. -/
  mapPullbackAdj (f) : Over.map f ‚ä£ pullback

variable (C) in
/-- A category has chosen pullbacks if every morphism has a chosen pullback. -/
abbrev ChosenPullbacks := Œ† {X Y : C} (f : Y ‚ü∂ X), ChosenPullbacksAlong f

namespace ChosenPullbacksAlong

/-- Relating the existing noncomputable `HasPullbacksAlong` typeclass to `ChosenPullbacksAlong`. -/
@[simps]
noncomputable def ofHasPullbacksAlong {Y X : C} (f : Y ‚ü∂ X) [HasPullbacksAlong f] :
    ChosenPullbacksAlong f where
  pullback := Over.pullback f
  mapPullbackAdj := Over.mapPullbackAdj f

/-- The identity morphism has a functorial choice of pullbacks. -/
def id (X : C) : ChosenPullbacksAlong (ùüô X) where
  pullback := ùü≠ _
  mapPullbackAdj := (Adjunction.id).ofNatIsoLeft (Over.mapId _).symm

/-- Any chosen pullback functor of the identity morphism is naturally isomorphic to the identity
functor. -/
def pullbackId (X : C) [ChosenPullbacksAlong (ùüô X)] :
    pullback (ùüô X) ‚âÖ ùü≠ (Over X) :=
  (mapPullbackAdj (ùüô X)).rightAdjointUniq (id X).mapPullbackAdj

@[reassoc (attr := simp)]
theorem unit_pullbackId_hom_app (X : C) [ChosenPullbacksAlong (ùüô X)] (Y : Over X) :
  (mapPullbackAdj (ùüô X)).unit.app Y ‚â´ (pullbackId X).hom.app ((Over.map (ùüô X)).obj Y) =
    (id X).mapPullbackAdj.unit.app Y := by
  rw [pullbackId, Adjunction.unit_rightAdjointUniq_hom_app]

@[reassoc (attr := simp)]
theorem unit_pullbackId_hom (X : C) [ChosenPullbacksAlong (ùüô X)] :
  (mapPullbackAdj (ùüô X)).unit ‚â´ (Over.map (ùüô X)).whiskerLeft (pullbackId X).hom =
    (id X).mapPullbackAdj.unit := by
  rw [pullbackId, Adjunction.unit_rightAdjointUniq_hom]

@[reassoc (attr := simp)]
theorem pullbackId_hom_counit (X : C) [ChosenPullbacksAlong (ùüô X)] :
  Functor.whiskerRight (pullbackId X).hom (Over.map (ùüô X)) ‚â´ (id X).mapPullbackAdj.counit =
    (mapPullbackAdj (ùüô X)).counit := by
  have := Adjunction.rightAdjointUniq_hom_counit (mapPullbackAdj (ùüô X)) (id X).mapPullbackAdj
  rw [pullbackId, Adjunction.rightAdjointUniq_hom_counit]

/-- Every isomorphism has a functorial choice of pullbacks. -/
@[simps]
def iso {Y X : C} (f : Y ‚âÖ X) : ChosenPullbacksAlong f.hom where
  pullback.obj Z := Over.mk (Z.hom ‚â´ f.inv)
  pullback.map {Y Z} g := Over.homMk (g.left)
  mapPullbackAdj.unit.app T := Over.homMk (ùüô T.left)
  mapPullbackAdj.counit.app U := Over.homMk (ùüô _)

/-- The inverse of an isomorphism has a functorial choice of pullbacks. -/
@[simps!]
def isoInv {Y X : C} (f : Y ‚âÖ X) : ChosenPullbacksAlong f.inv := iso f.symm

/-- The composition of morphisms with chosen pullbacks has a chosen pullback. -/
def comp {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)
    [ChosenPullbacksAlong f] [ChosenPullbacksAlong g] : ChosenPullbacksAlong (f ‚â´ g) where
  pullback := pullback g ‚ãô pullback f
  mapPullbackAdj := ((mapPullbackAdj f).comp (mapPullbackAdj g)).ofNatIsoLeft
    (Over.mapComp f g).symm

/-- Any chosen pullback of a composite of morphisms is naturally isomorphic to the composition of
chosen pullback functors. -/
def pullbackComp {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)
    [ChosenPullbacksAlong f] [ChosenPullbacksAlong g] [ChosenPullbacksAlong (f ‚â´ g)] :
    pullback (f ‚â´ g) ‚âÖ pullback g ‚ãô pullback f :=
  Adjunction.rightAdjointUniq (mapPullbackAdj (f ‚â´ g)) ((comp f g).mapPullbackAdj)

@[reassoc (attr := simp)]
theorem unit_pullbackComp_hom {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)
    [ChosenPullbacksAlong f] [ChosenPullbacksAlong g] [ChosenPullbacksAlong (f ‚â´ g)] :
    (mapPullbackAdj (f ‚â´ g)).unit ‚â´ (Over.map (f ‚â´ g)).whiskerLeft (pullbackComp f g).hom =
      (comp f g).mapPullbackAdj.unit := by
  rw [pullbackComp, Adjunction.unit_rightAdjointUniq_hom]

@[reassoc (attr := simp)]
theorem pullbackComp_hom_counit {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)
    [ChosenPullbacksAlong f] [ChosenPullbacksAlong g] [ChosenPullbacksAlong (f ‚â´ g)] :
    Functor.whiskerRight (pullbackComp f g).hom (Over.map (f ‚â´ g)) ‚â´
      (comp f g).mapPullbackAdj.counit =
      (mapPullbackAdj (f ‚â´ g)).counit := by
  rw [pullbackComp, Adjunction.rightAdjointUniq_hom_counit]

/-- In cartesian monoidal categories, any morphism to the terminal tensor unit has a functorial
choice of pullbacks. -/
@[simps]
def cartesianMonoidalCategoryToUnit [CartesianMonoidalCategory C] {X : C} (f : X ‚ü∂ ùüô_ C) :
    ChosenPullbacksAlong f where
  pullback.obj Y := Over.mk (snd Y.left X)
  pullback.map {Y Z} g := Over.homMk (g.left ‚ñ∑ X)
  mapPullbackAdj.unit.app T := Over.homMk (lift (ùüô _) (T.hom))
  mapPullbackAdj.counit.app U := Over.homMk (fst _ _)

/-- In cartesian monoidal categories, the first product projections `fst` have a functorial choice
of pullbacks. -/
@[simps]
def cartesianMonoidalCategoryFst [CartesianMonoidalCategory C] (X Y : C) :
    ChosenPullbacksAlong (fst X Y : X ‚äó Y ‚ü∂ X) where
  pullback.obj Z := Over.mk (Z.hom ‚ñ∑ Y)
  pullback.map g := Over.homMk (g.left ‚ñ∑ Y)
  mapPullbackAdj.unit.app T := Over.homMk (lift (ùüô _) (T.hom ‚â´ snd _ _))
  mapPullbackAdj.counit.app U := Over.homMk (fst _ _)

/-- In cartesian monoidal categories, the second product projections `snd` have a functorial choice
of pullbacks. -/
@[simps]
def cartesianMonoidalCategorySnd [CartesianMonoidalCategory C] (X Y : C) :
    ChosenPullbacksAlong (snd X Y : X ‚äó Y ‚ü∂ Y) where
  pullback.obj Z := Over.mk (X ‚óÅ Z.hom)
  pullback.map g := Over.homMk (X ‚óÅ g.left)
  mapPullbackAdj.unit.app T := Over.homMk (lift (T.hom ‚â´ fst _ _) (ùüô _))
  mapPullbackAdj.counit.app U := Over.homMk (snd _ _)

section PullbackFromChosenPullbacksAlongs

variable {Y Z X : C} (f : Y ‚ü∂ X) (g : Z ‚ü∂ X) [ChosenPullbacksAlong g]

/-- The underlying object of the chosen pullback along `g` of `f`. -/
abbrev pullbackObj : C := ((pullback g).obj (Over.mk f)).left

/-- A morphism in `Over X` from the chosen pullback along `g` of `f` to `Over.mk f`. -/
abbrev fst' : (Over.map g).obj ((pullback g).obj (Over.mk f)) ‚ü∂ Over.mk f :=
  (mapPullbackAdj g).counit.app <| Over.mk f

/-- The first projection from the chosen pullback along `g` of `f` to the domain of `f`. -/
abbrev fst : pullbackObj f g ‚ü∂ Y := fst' f g |>.left

theorem fst'_left : (fst' f g).left = fst f g := rfl

/-- The second projection from the chosen pullback along `g` of `f` to the domain of `g`. -/
abbrev snd : pullbackObj f g ‚ü∂ Z := (pullback g).obj (Over.mk f) |>.hom

/-- A morphism in `Over X` from the chosen pullback along `g` of `f` to `Over.mk g`. -/
abbrev snd' : (Over.map g).obj ((pullback g).obj (Over.mk f)) ‚ü∂ (Over.mk g) :=
  Over.homMk (snd f g)

theorem snd'_left : (snd' f g).left = snd f g := rfl

variable {f g}

@[reassoc]
theorem condition : fst f g ‚â´ f = snd f g ‚â´ g :=
  Over.w (fst' f g)

variable (f g) in
@[ext]
theorem hom_ext {W : C} {œÜ‚ÇÅ œÜ‚ÇÇ : W ‚ü∂ pullbackObj f g} (h‚ÇÅ : œÜ‚ÇÅ ‚â´ fst _ _ = œÜ‚ÇÇ ‚â´ fst _ _)
    (h‚ÇÇ : œÜ‚ÇÅ ‚â´ snd _ _ = œÜ‚ÇÇ ‚â´ snd _ _) :
    œÜ‚ÇÅ = œÜ‚ÇÇ := by
  let adj := mapPullbackAdj g
  let U : Over Z := Over.mk (œÜ‚ÇÅ ‚â´ snd f g)
  let œÜ‚ÇÅ' : U ‚ü∂ (pullback g).obj (Over.mk f) := Over.homMk œÜ‚ÇÅ
  let œÜ‚ÇÇ' : U ‚ü∂ (pullback g).obj (Over.mk f) := Over.homMk œÜ‚ÇÇ (by simpa using h‚ÇÇ.symm)
  have : œÜ‚ÇÅ' = œÜ‚ÇÇ' := by
    apply (adj.homEquiv U _).symm.injective
    apply (Over.forget X).map_injective
    simpa using h‚ÇÅ
  exact congr_arg CommaMorphism.left this

section Lift

variable {W : C} (a : W ‚ü∂ Y) (b : W ‚ü∂ Z) (h : a ‚â´ f = b ‚â´ g := by cat_disch)

set_option backward.privateInPublic true in
/-- Given morphisms `a : W ‚ü∂ Y` and `b : W ‚ü∂ Z` satisfying `a ‚â´ f = b ‚â´ g`,
constructs the unique morphism `W ‚ü∂ pullbackObj f g` which lifts `a` and `b`. -/
def lift : W ‚ü∂ pullbackObj f g :=
  (((mapPullbackAdj g).homEquiv (Over.mk b) (Over.mk f)) (Over.homMk a)).left

set_option backward.privateInPublic true in
@[reassoc (attr := simp)]
theorem lift_fst : lift a b h ‚â´ fst f g = a := by
  let adj := mapPullbackAdj g
  let a' : (Over.map g).obj (Over.mk b) ‚ü∂ Over.mk f := Over.homMk a h
  have : (Over.map g).map (adj.homEquiv (.mk b) (.mk f) (Over.homMk a)) ‚â´ fst' f g = a' := by
    simp only [‚Üê Adjunction.homEquiv_counit, Equiv.symm_apply_apply, adj, a']
  exact congr_arg CommaMorphism.left this

set_option backward.privateInPublic true in
@[reassoc (attr := simp)]
theorem lift_snd : lift a b h ‚â´ snd f g = b := by
  simp [lift]

end Lift

section PullbackMap

variable (f g)

/-- The functoriality of `pullbackObj f g` in both arguments: Given a map from the pullback cospans
of `f' : Y' ‚ü∂ X'` and `g' : Z' ‚ü∂ X'` to the pullback cospan of `f : Y ‚ü∂ X` and `g : Z ‚ü∂ X`
as in the diagram below
```
Y' ‚ü∂ Y
  ‚Üò   ‚Üò
  X' ‚ü∂ X
  ‚Üó   ‚Üó
Z' ‚ü∂ Z
```
if the morphisms `g'` and `g` both have chosen pullbacks, then we get an induced morphism
`pullbackMap f g f' g' comm‚ÇÅ comm‚ÇÇ` from the chosen pullback of
`f' : Y' ‚ü∂ X'` along `g'` to the chosen pullback of `f : Y ‚ü∂ X` along `g`.
Here `comm‚ÇÅ` and `comm‚ÇÇ` are the commutativity conditions of the squares in the diagram above.
-/
def pullbackMap {Y' Z' X' : C} (f' : Y' ‚ü∂ X') (g' : Z' ‚ü∂ X') [ChosenPullbacksAlong g']
    (Œ≥‚ÇÅ : Y' ‚ü∂ Y) (Œ≥‚ÇÇ : Z' ‚ü∂ Z) (Œ≥‚ÇÉ : X' ‚ü∂ X)
    (comm‚ÇÅ : f' ‚â´ Œ≥‚ÇÉ = Œ≥‚ÇÅ ‚â´ f := by cat_disch) (comm‚ÇÇ : g' ‚â´ Œ≥‚ÇÉ = Œ≥‚ÇÇ ‚â´ g := by cat_disch) :
    pullbackObj f' g' ‚ü∂ pullbackObj f g :=
  lift (fst f' g' ‚â´ Œ≥‚ÇÅ) (snd f' g' ‚â´ Œ≥‚ÇÇ)
    (by rw [assoc, ‚Üê comm‚ÇÅ, ‚Üê assoc, condition, assoc, comm‚ÇÇ, assoc])

variable {f g}

@[reassoc (attr := simp)]
theorem pullbackMap_fst {Y' Z' X' : C} {f' : Y' ‚ü∂ X'} {g' : Z' ‚ü∂ X'} [ChosenPullbacksAlong g']
    {Œ≥‚ÇÅ : Y' ‚ü∂ Y} {Œ≥‚ÇÇ : Z' ‚ü∂ Z} {Œ≥‚ÇÉ : X' ‚ü∂ X} (comm‚ÇÅ comm‚ÇÇ := by cat_disch) :
    pullbackMap f g f' g' Œ≥‚ÇÅ Œ≥‚ÇÇ Œ≥‚ÇÉ comm‚ÇÅ comm‚ÇÇ ‚â´ fst f g = fst f' g' ‚â´ Œ≥‚ÇÅ := by
  simp only [pullbackMap, lift_fst]

@[reassoc (attr := simp)]
theorem pullbackMap_snd {Y' Z' X' : C} {f' : Y' ‚ü∂ X'} {g' : Z' ‚ü∂ X'} [ChosenPullbacksAlong g']
    {Œ≥‚ÇÅ : Y' ‚ü∂ Y} {Œ≥‚ÇÇ : Z' ‚ü∂ Z} {Œ≥‚ÇÉ : X' ‚ü∂ X} (comm‚ÇÅ comm‚ÇÇ := by cat_disch) :
    pullbackMap f g f' g' Œ≥‚ÇÅ Œ≥‚ÇÇ Œ≥‚ÇÉ comm‚ÇÅ comm‚ÇÇ ‚â´ snd f g = snd f' g' ‚â´ Œ≥‚ÇÇ := by
  simp only [pullbackMap, lift_snd]

@[simp]
theorem pullbackMap_id : pullbackMap f g f g (ùüô Y) (ùüô Z) (ùüô X) = ùüô _ := by
  cat_disch

@[reassoc (attr := simp)]
theorem pullbackMap_comp {Y' Z' X' Y'' Z'' X'' : C}
    {f' : Y' ‚ü∂ X'} {g' : Z' ‚ü∂ X'} {f'' : Y'' ‚ü∂ X''} {g'' : Z'' ‚ü∂ X''}
    [ChosenPullbacksAlong g'] [ChosenPullbacksAlong g'']
    {Œ≥‚ÇÅ : Y' ‚ü∂ Y} {Œ≥‚ÇÇ : Z' ‚ü∂ Z} {Œ≥‚ÇÉ : X' ‚ü∂ X}
    {Œ¥‚ÇÅ : Y'' ‚ü∂ Y'} {Œ¥‚ÇÇ : Z'' ‚ü∂ Z'} {Œ¥‚ÇÉ : X'' ‚ü∂ X'}
    (comm‚ÇÅ comm‚ÇÇ comm‚ÇÅ' comm‚ÇÇ' := by cat_disch) :
    pullbackMap f' g' f'' g'' Œ¥‚ÇÅ Œ¥‚ÇÇ Œ¥‚ÇÉ comm‚ÇÅ' comm‚ÇÇ' ‚â´
      pullbackMap f g f' g' Œ≥‚ÇÅ Œ≥‚ÇÇ Œ≥‚ÇÉ comm‚ÇÅ comm‚ÇÇ =
    pullbackMap f g f'' g'' (Œ¥‚ÇÅ ‚â´ Œ≥‚ÇÅ) (Œ¥‚ÇÇ ‚â´ Œ≥‚ÇÇ) (Œ¥‚ÇÉ ‚â´ Œ≥‚ÇÉ)
      (by rw [reassoc_of% comm‚ÇÅ', comm‚ÇÅ, assoc]) (by rw [reassoc_of% comm‚ÇÇ', comm‚ÇÇ, assoc]) := by
  cat_disch

end PullbackMap

variable (f g)

/-- The canonical pullback cone from the data of a chosen pullback of `f` along `g`. -/
def pullbackCone : PullbackCone f g :=
  PullbackCone.mk (fst f g) (snd f g) (by rw [condition])

@[simp] lemma pullbackCone_fst : (pullbackCone f g).fst = fst f g := rfl

@[simp] lemma pullbackCone_snd : (pullbackCone f g).snd = snd f g := rfl

/-- The canonical pullback cone is a limit cone.
Note: this limit cone is computable as lifts are constructed from the data contained in the
`ChosenPullbackAlong` instance, contrary to `IsPullback.isLimit`, which constructs lifting data from
`CategoryTheory.Square.IsPullback` (a `Prop`). -/
def isLimitPullbackCone :
    IsLimit (pullbackCone f g) :=
  PullbackCone.IsLimit.mk condition (fun s ‚Ü¶ lift s.fst s.snd s.condition)
    (by cat_disch) (by cat_disch) (by cat_disch)

theorem isPullback : IsPullback (fst f g) (snd f g) f g where
  w := condition
  isLimit' := ‚ü®isLimitPullbackCone f g‚ü©

attribute [local simp] condition in
/-- If `g` has a chosen pullback, then `Over.ChosenPullbacksAlong.fst f g` has a chosen pullback. -/
def chosenPullbacksAlongFst : ChosenPullbacksAlong (fst f g) where
  pullback.obj W := Over.mk (pullbackMap _ _ _ _ W.hom (ùüô _) (ùüô _))
  pullback.map {W' W} k := Over.homMk (lift (fst _ g ‚â´ k.left) (snd _ g)) _
  mapPullbackAdj.unit.app Q := Over.homMk (lift (ùüô _) (Q.hom ‚â´ snd _ _))
  mapPullbackAdj.counit.app W := Over.homMk (fst _ g)

instance hasPullbackAlong : HasPullbacksAlong g := fun f => (isPullback f g).hasPullback

instance hasPullbacks [ChosenPullbacks C] : HasPullbacks C :=
  hasPullbacks_of_hasLimit_cospan _

/-- The computable `ChosenPullbacksAlong.pullback g` is naturally isomorphic to the noncomputable
`Over.pullback g`. -/
noncomputable def pullbackIsoOverPullback : ChosenPullbacksAlong.pullback g ‚âÖ Over.pullback g :=
  (ChosenPullbacksAlong.mapPullbackAdj g).rightAdjointUniq (Over.mapPullbackAdj g)

@[reassoc (attr := simp)]
theorem pullbackIsoOverPullback_hom_app_comp_fst (T : Over X) :
    ((pullbackIsoOverPullback g).hom.app T).left ‚â´ pullback.fst _ _ = fst _ _ := by
  simpa using (Over.forget _).congr_map
    ((ChosenPullbacksAlong.mapPullbackAdj g).rightAdjointUniq_hom_app_counit
      (Over.mapPullbackAdj g) T)

@[reassoc (attr := simp)]
theorem pullbackIsoOverPullback_hom_app_comp_snd (T : Over X) :
    ((pullbackIsoOverPullback g).hom.app T).left ‚â´ pullback.snd _ _ = snd _ _ :=
  Over.w ((pullbackIsoOverPullback g).hom.app T)

@[reassoc (attr := simp)]
theorem pullbackIsoOverPullback_inv_app_comp_fst (T : Over X) :
    ((pullbackIsoOverPullback g).inv.app T).left ‚â´ fst _ _ = pullback.fst _ _ := by
  simp [‚Üê pullbackIsoOverPullback_hom_app_comp_fst, ‚Üê Over.comp_left_assoc]

@[reassoc (attr := simp)]
theorem pullbackIsoOverPullback_inv_app_comp_snd (T : Over X) :
    ((pullbackIsoOverPullback g).inv.app T).left ‚â´ snd _ _ = pullback.snd _ _ :=
  Over.w ((pullbackIsoOverPullback g).inv.app T)

end PullbackFromChosenPullbacksAlongs

end ChosenPullbacksAlong

end CategoryTheory
