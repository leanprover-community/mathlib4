/-
Copyright (c) 2025 Sina Hazratpour. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sina Hazratpour
-/
import Mathlib.CategoryTheory.Comma.Over.Pullback
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq
import Mathlib.CategoryTheory.Monoidal.Cartesian.Basic

/-!
# Chosen pullbacks

## Main declarations

- `ChosenPullback` : For a morphism `f : Y âŸ¶ X` in `C`, the type class `ChosenPullback f`
provides the data of a pullback functor `Over X â¥¤ Over Y` as a right adjoint to `Over.map f`.

## Main results

- We prove that `ChosenPullback` has good closure properties, e.g., isos have chosen pullbacks, and
  composition of morphisms with chosen pullbacks have chosen pullbacks.

-  `Over.ChosenPullback.isPullback` proves that for morphisms `f` and `g` with the same codomain,
  the object `Over.ChosenPullback.pullbackObj f g` together with morphisms
  `Over.ChosenPullback.fst f g` and `Over.ChosenPullback.snd f g` form a pullback square
  over `f` and `g`.

- We prove that in cartesian monoidal categories, morphisms to the terminal object and the product
  projections have chosen pullbacks.

-/

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

namespace CategoryTheory

open Category Limits CartesianMonoidalCategory MonoidalCategory

variable {C : Type uâ‚} [Category.{vâ‚} C]

namespace Over

/-- A choice of pullback functor `Over X â¥¤ Over Y` along a morphism `f : Y âŸ¶ X` in `C`
as a right adjoint to the functor `Over.map f`. -/
class ChosenPullback {Y X : C} (f : Y âŸ¶ X) where
  /-- The pullback functor along `f`. -/
  pullback : Over X â¥¤ Over Y
  /-- The adjunction between `Over.map f` and `pullback f`. -/
  mapPullbackAdj (f) : Over.map f âŠ£ pullback

namespace ChosenPullback

/-- `ChosenPullback (Over.mk f).hom` from  `ChosenPullback f`. -/
def overMkHom {Y X : C} (f : Y âŸ¶ X) [ChosenPullback f] : ChosenPullback (Over.mk f).hom :=
  inferInstanceAs (ChosenPullback f)

/-- Relating the existing noncomputable `HasPullbacksAlong` typeclass to `ChosenPullback`. -/
@[simps]
noncomputable def ofHasPullbacksAlong {Y X : C} (f : Y âŸ¶ X) [HasPullbacksAlong f] :
    ChosenPullback f where
  pullback := Over.pullback f
  mapPullbackAdj := Over.mapPullbackAdj f

/-- The identity morphism has a chosen pullback. -/
@[simps]
def id (X : C) : ChosenPullback (ğŸ™ X) where
  pullback := ğŸ­ _
  mapPullbackAdj := (Adjunction.id).ofNatIsoLeft (Over.mapId _).symm

/-- Every isomorphism has a chosen pullback. -/
@[simps]
def iso {Y X : C} (f : Y â‰… X) : ChosenPullback f.hom where
  pullback.obj Z := Over.mk (Z.hom â‰« f.inv)
  pullback.map {Y Z} g := Over.homMk (g.left)
  mapPullbackAdj.unit.app T := Over.homMk (ğŸ™ T.left)
  mapPullbackAdj.counit.app U := Over.homMk (ğŸ™ _)

/-- The composition of morphisms with chosen pullbacks has a chosen pullback. -/
@[simps]
def comp {Z Y X : C} (f : Y âŸ¶ X) (g : Z âŸ¶ Y)
    [ChosenPullback f] [ChosenPullback g] : ChosenPullback (g â‰« f) where
  pullback := pullback f â‹™ pullback g
  mapPullbackAdj := ((mapPullbackAdj g).comp (mapPullbackAdj f)).ofNatIsoLeft
    (Over.mapComp g f).symm

/-- In cartesian monoidal categories, any morphism to the terminal object has a chosen pullback. -/
@[simps]
def cartesianMonoidalCategoryToTerminal [CartesianMonoidalCategory C] {X : C} (f : X âŸ¶ ğŸ™_ C) :
    ChosenPullback f where
  pullback.obj Y := Over.mk (fst X Y.left)
  pullback.map {Y Z} g := Over.homMk (X â— g.left)
  mapPullbackAdj := Adjunction.mkOfHomEquiv
    { homEquiv U Z :=
      { toFun z := Over.homMk (lift U.hom z.left)
        invFun u := Over.homMk (u.left â‰« snd X Z.left)
        left_inv k := by simp
        right_inv k := by
          ext
          dsimp
          ext
          Â· simpa using k.w.symm
          Â· aesop } }

/-- In cartesian monoidal categories, the first product projections `fst` have chosen pullbacks. -/
@[simps]
def cartesianMonoidalCategoryFst [CartesianMonoidalCategory C] {X Y : C} :
    ChosenPullback (fst X Y : X âŠ— Y âŸ¶ X) where
  pullback.obj Z := Over.mk (Z.hom â–· Y)
  pullback.map g := Over.homMk (g.left â–· Y)
  mapPullbackAdj.unit.app T := Over.homMk (lift (ğŸ™ _) (T.hom â‰« snd _ _))
  mapPullbackAdj.counit.app U := Over.homMk (fst _ _)

/-- In cartesian monoidal categories, the second product projections `snd` have chosen pullbacks. -/
@[simps]
def cartesianMonoidalCategorySnd [CartesianMonoidalCategory C] {X Y : C} :
    ChosenPullback (snd X Y : X âŠ— Y âŸ¶ Y) where
  pullback.obj Z := Over.mk (X â— Z.hom)
  pullback.map g := Over.homMk (X â— g.left)
  mapPullbackAdj.unit.app T := Over.homMk (lift (T.hom â‰« fst _ _) (ğŸ™ _))
  mapPullbackAdj.counit.app U := Over.homMk (snd _ _)

section PullbackFromChosenPullbacks

variable {Y Z X : C} (f : Y âŸ¶ X) (g : Z âŸ¶ X) [ChosenPullback g]

/-- The underlying object of the chosen pullback along `g` of `f`. -/
abbrev pullbackObj := ((pullback g).obj (Over.mk f)).left

/-- A morphism in `Over X` from the chosen pullback along `g` of `f` to `Over.mk f`. -/
abbrev fst' := (mapPullbackAdj g).counit.app (Over.mk f)

/-- The first projection from the chosen pullback along `g` of `f` to the domain of `f`. -/
abbrev fst : pullbackObj f g âŸ¶ Y := fst' f g |>.left

theorem fst'_left : (fst' f g).left = fst f g := rfl

/-- The second projection from the chosen pullback along `g` of `f` to the domain of `g`. -/
abbrev snd : pullbackObj f g âŸ¶ Z := (pullback g).obj (Over.mk f) |>.hom

/-- A morphism in `Over X` from the chosen pullback along `g` of `f` to `Over.mk g`. -/
abbrev snd' : (Over.map g).obj ((pullback g).obj (Over.mk f)) âŸ¶ (Over.mk g) :=
  Over.homMk (snd f g)

theorem snd'_left : (snd' f g).left = snd f g := by
  rfl

variable {f g}

@[reassoc]
theorem condition : fst f g â‰« f = snd f g â‰« g :=
  Over.w (fst' f g)

variable (f g) in

@[ext]
theorem hom_ext {W : C} {Ï†â‚ Ï†â‚‚ : W âŸ¶ pullbackObj f g} (hâ‚ : Ï†â‚ â‰« fst _ _ = Ï†â‚‚ â‰« fst _ _)
    (hâ‚‚ : Ï†â‚ â‰« snd _ _ = Ï†â‚‚ â‰« snd _ _) :
    Ï†â‚ = Ï†â‚‚ := by
  let adj := mapPullbackAdj g
  let U : Over Z := Over.mk (Ï†â‚ â‰« snd f g)
  let Ï†â‚' : U âŸ¶ (pullback g).obj (Over.mk f) := Over.homMk Ï†â‚
  let Ï†â‚‚' : U âŸ¶ (pullback g).obj (Over.mk f) := Over.homMk Ï†â‚‚ (by simpa using hâ‚‚.symm)
  have : (adj.homEquiv U _).symm Ï†â‚' = (adj.homEquiv U _).symm Ï†â‚‚' := by
    simp [adj.homEquiv_symm_apply]
    apply (forget X).map_injective
    simpa using hâ‚
  have : Ï†â‚' = Ï†â‚‚' := by
    apply (adj.homEquiv U _).symm.injective
    exact this
  apply congr_arg CommaMorphism.left this

section Lift

variable {W : C} (a : W âŸ¶ Y) (b : W âŸ¶ Z) (h : a â‰« f = b â‰« g)

/-- Given morphisms `a : W âŸ¶ Y` and `b : W âŸ¶ Z` satisfying `a â‰« f = b â‰« g`,
constructs the unique morphism `W âŸ¶ pullbackObj f g`. -/
def lift : W âŸ¶ pullbackObj f g :=
  (((mapPullbackAdj g).homEquiv (Over.mk b) (Over.mk f)) (Over.homMk a)).left

@[reassoc (attr := simp)]
theorem lift_fst : lift a b h â‰« fst f g = a := by
  let adj := mapPullbackAdj g
  let a' : (Over.map g).obj (Over.mk b) âŸ¶ Over.mk f := Over.homMk a h
  let l' := adj.homEquiv (Over.mk b) (Over.mk f) (Over.homMk a)
  have : (Over.map g).map l' â‰« fst' f g = a' := by
    simp [â† Adjunction.homEquiv_counit]
    aesop
  apply congr_arg CommaMorphism.left this

@[reassoc (attr := simp)]
theorem lift_snd : lift a b h â‰« snd f g = b := by
  simp [lift]

theorem isPullback {Y Z X : C} (f : Y âŸ¶ X) (g : Z âŸ¶ X) [ChosenPullback g] :
    IsPullback (fst f g) (snd f g) f g where
  w := condition
  isLimit' :=
    âŸ¨PullbackCone.IsLimit.mk _ (fun s â†¦ lift s.fst s.snd s.condition)
      (by simp) (by simp) (by aesop)âŸ©

end Lift

end PullbackFromChosenPullbacks

end ChosenPullback

end Over

end CategoryTheory
