/-
Copyright (c) 2025 Sina Hazratpour. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sina Hazratpour
-/
module

public import Mathlib.CategoryTheory.LocallyCartesianClosed.ChosenPullbacksAlong

/-!
# Cartesian monoidal structure on slices induced by chosen pullbacks

## Main declarations

- `cartesianMonoidalCategoryOver` provides a cartesian monoidal structure on the slice categories
  `Over X` for all objects `X : C`, induced by chosen pullbacks in the base category `C`.
  This is the computable analogue of the noncomputable instance
  `CategoryTheory.Over.cartesianMonoidalCategory`.

- For a cartesian monoidal category `C`, and for any object `X` of `C`,
  `toOver X` is a functor from `C` to `Over X` which maps an object `A : C` to the projection
  `A âŠ— X âŸ¶ X` in `Over X`. This is the computable analogue of the functor `Over.star`.

## Main results

- `cartesianMonoidalCategoryOver` proves that the slices of a category with chosen pullbacks are
  cartesian monoidal.

- `toOverPullbackIsoToOver` shows that in a category with chosen pullbacks, for any morphism
  `f : Y âŸ¶ X`, the functors `toOver X â‹™ pullback f` and `toOver Y` are naturally isomorphic.

- `toOverIteratedSliceForwardIsoPullback` shows that in a category with chosen pullbacks the functor
  `pullback f : Over X â¥¤ Over Y` is naturally isomorphic to
  `toOver (Over.mk f) : Over X â¥¤ Over (Over.mk f)` post-composed with the iterated slice equivalence
  `Over (Over.mk f) â¥¤ Over Y`. Note that the functor `toOver (Over.mk f)` exists by the result
  `cartesianMonoidalCategoryOver`.

### TODO

- Show that the functors `pullback f` are monoidal with respect to
  the cartesian monoidal structures on slices.

-/

@[expose] public section

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

namespace CategoryTheory

open Category

namespace ChosenPullbacksAlong

open CartesianMonoidalCategory MonoidalCategory

variable {C : Type uâ‚} [Category.{vâ‚} C]

section

open Limits

variable {X : C} (Y Z : Over X)

/-- The binary fan provided by `fst'` and `snd'`. -/
abbrev binaryFan [ChosenPullbacksAlong Z.hom] : BinaryFan Y Z :=
  BinaryFan.mk (P := (pullback Z.hom â‹™ Over.map Z.hom).obj (Over.mk Y.hom))
    (fst' Y.hom Z.hom) (snd' Y.hom Z.hom)

/-- The binary fan provided by `fst'` and `snd'` is a binary product in `Over X`. -/
def binaryFanIsBinaryProduct [ChosenPullbacksAlong Z.hom] :
    IsLimit (binaryFan Y Z) :=
  BinaryFan.IsLimit.mk (binaryFan Y Z)
    (fun u v => Over.homMk (lift (u.left) (v.left) (by rw [Over.w u, Over.w v])) (by simp))
    (by cat_disch) (by cat_disch)
    (fun a b m hâ‚ hâ‚‚ => by
      ext
      dsimp [Over.map, Comma.mapRight]
      cat_disch)

end

/-- A computable instance of `CartesianMonoidalCategory` for `Over X` when `C` has
chosen pullbacks. Contrast this with the noncomputable instance provided by
`CategoryTheory.Over.cartesianMonoidalCategory`.
-/
def cartesianMonoidalCategoryOver [ChosenPullbacks C] (X : C) :
    CartesianMonoidalCategory (Over X) :=
  ofChosenFiniteProducts (C := Over X)
    âŸ¨Limits.asEmptyCone (Over.mk (ğŸ™ X)), Limits.IsTerminal.ofUniqueHom (fun Y â†¦ Over.homMk Y.hom)
      fun Y m â†¦ Over.OverMorphism.ext (by simpa using m.w)âŸ©
    (fun Y Z â†¦ âŸ¨ _ , binaryFanIsBinaryProduct Y ZâŸ©)

namespace Over

open MonoidalCategory

variable [ChosenPullbacks C] {X : C}

attribute [local instance] cartesianMonoidalCategoryOver

@[ext]
lemma tensorObj_ext {A : C} {Y Z : Over X} (fâ‚ fâ‚‚ : A âŸ¶ (Y âŠ— Z).left)
    (eâ‚ : fâ‚ â‰« fst Y.hom Z.hom = fâ‚‚ â‰« fst Y.hom Z.hom)
    (eâ‚‚ : fâ‚ â‰« snd Y.hom Z.hom = fâ‚‚ â‰« snd Y.hom Z.hom) : fâ‚ = fâ‚‚ :=
  hom_ext Y.hom Z.hom eâ‚ eâ‚‚

@[simp]
lemma tensorObj_left (Y Z : Over X) : (Y âŠ— Z).left = pullbackObj Y.hom Z.hom := rfl

@[simp]
lemma tensorObj_hom (Y Z : Over X) : (Y âŠ— Z).hom = snd Y.hom Z.hom â‰« Z.hom := rfl

@[simp]
lemma tensorUnit_left : (ğŸ™_ (Over X)).left = X := rfl

@[simp]
lemma tensorUnit_hom : (ğŸ™_ (Over X)).hom = ğŸ™ X := rfl

lemma fst_eq_fst' (Y Z : Over X) :
    CartesianMonoidalCategory.fst Y Z = fst' Y.hom Z.hom :=
  rfl

lemma snd_eq_snd' (Y Z : Over X) :
    CartesianMonoidalCategory.snd Y Z = snd' Y.hom Z.hom :=
  rfl

@[simp]
lemma lift_left {W Y Z : Over X} (f : W âŸ¶ Y) (g : W âŸ¶ Z) :
    (CartesianMonoidalCategory.lift f g).left = lift f.left g.left := rfl

@[simp]
lemma toUnit_left {Z : Over X} : (toUnit Z).left = Z.hom := rfl

@[reassoc (attr := simp)]
lemma associator_hom_left_fst (R S T : Over X) :
    (Î±_ R S T).hom.left â‰« fst R.hom (snd S.hom T.hom â‰« T.hom) =
      fst (R âŠ— S).hom T.hom â‰« fst R.hom S.hom :=
  congr_arg CommaMorphism.left (associator_hom_fst R S T)

@[reassoc (attr := simp)]
lemma associator_hom_left_snd_fst (R S T : Over X) :
    (Î±_ R S T).hom.left â‰« snd R.hom (snd S.hom T.hom â‰« T.hom) â‰« fst S.hom T.hom =
      fst (R âŠ— S).hom T.hom â‰« snd R.hom S.hom :=
  congr_arg CommaMorphism.left (associator_hom_snd_fst R S T)

@[reassoc (attr := simp)]
lemma associator_hom_left_snd_snd (R S T : Over X) :
    (Î±_ R S T).hom.left â‰« snd R.hom (snd S.hom T.hom â‰« T.hom) â‰« snd S.hom T.hom =
      snd (R âŠ— S).hom T.hom :=
  congr_arg CommaMorphism.left (associator_hom_snd_snd R S T)

@[reassoc (attr := simp)]
lemma associator_inv_left_fst_fst (R S T : Over X) :
    (Î±_ R S T).inv.left â‰« fst (snd R.hom S.hom â‰« S.hom) T.hom â‰« fst R.hom S.hom =
      fst R.hom (S âŠ— T).hom :=
  congr_arg CommaMorphism.left (associator_inv_fst_fst R S T)

@[reassoc (attr := simp)]
lemma associator_inv_left_fst_snd (R S T : Over X) :
    (Î±_ R S T).inv.left â‰« fst (snd R.hom S.hom â‰« S.hom) T.hom â‰« snd R.hom S.hom =
      snd R.hom (S âŠ— T).hom â‰« fst S.hom T.hom :=
  congr_arg CommaMorphism.left (associator_inv_fst_snd R S T)

@[reassoc (attr := simp)]
lemma associator_inv_left_snd (R S T : Over X) :
    (Î±_ R S T).inv.left â‰« snd (snd R.hom S.hom â‰« S.hom) T.hom =
      snd R.hom (S âŠ— T).hom â‰« snd S.hom T.hom :=
  congr_arg CommaMorphism.left (associator_inv_snd R S T)

@[simp]
lemma leftUnitor_hom_left (Z : Over X) :
    (Î»_ Z).hom.left = snd _ Z.hom := rfl

@[reassoc (attr := simp)]
lemma leftUnitor_inv_left_fst (Z : Over X) :
    (Î»_ Z).inv.left â‰« fst (ğŸ™ X) Z.hom = Z.hom :=
  congr_arg CommaMorphism.left (leftUnitor_inv_fst Z)

@[reassoc (attr := simp)]
lemma leftUnitor_inv_left_snd (Y : Over X) :
    (Î»_ Y).inv.left â‰« snd (ğŸ™ X) Y.hom = ğŸ™ Y.left :=
  congr_arg CommaMorphism.left (leftUnitor_inv_snd Y)

@[simp]
lemma rightUnitor_hom_left (Y : Over X) :
    (Ï_ Y).hom.left = fst _ (ğŸ™ X) := rfl

@[reassoc (attr := simp)]
lemma rightUnitor_inv_left_fst (Y : Over X) :
    (Ï_ Y).inv.left â‰« fst Y.hom (ğŸ™ X) = ğŸ™ Y.left :=
  congr_arg CommaMorphism.left (rightUnitor_inv_fst Y)

@[reassoc (attr := simp)]
lemma rightUnitor_inv_left_snd (Y : Over X) :
    (Ï_ Y).inv.left â‰« snd Y.hom (ğŸ™ X) = Y.hom :=
  congr_arg CommaMorphism.left (rightUnitor_inv_snd Y)

lemma whiskerLeft_left {R S T : Over X} (f : S âŸ¶ T) :
    (R â— f).left = pullbackMap R.hom T.hom R.hom S.hom (ğŸ™ _) f.left (ğŸ™ _) :=
  rfl

@[reassoc (attr := simp)]
lemma whiskerLeft_left_fst {R S T : Over X} (f : S âŸ¶ T) :
    (R â— f).left â‰« fst R.hom T.hom = fst R.hom S.hom :=
  congr_arg CommaMorphism.left (whiskerLeft_fst R f)

@[reassoc (attr := simp)]
lemma whiskerLeft_left_snd {R S T : Over X} (f : S âŸ¶ T) :
    (R â— f).left â‰« snd R.hom T.hom = snd R.hom S.hom â‰« f.left :=
  congr_arg CommaMorphism.left (whiskerLeft_snd R f)

lemma whiskerRight_left {R S T : Over X} (f : S âŸ¶ T) :
    (f â–· R).left = pullbackMap T.hom R.hom S.hom R.hom f.left (ğŸ™ _) (ğŸ™ _) :=
  rfl

@[reassoc (attr := simp)]
lemma whiskerRight_left_fst {R S T : Over X} (f : S âŸ¶ T) :
    (f â–· R).left â‰« fst T.hom R.hom = fst S.hom R.hom â‰« f.left :=
  congr_arg CommaMorphism.left (whiskerRight_fst f R)

@[reassoc (attr := simp)]
lemma whiskerRight_left_snd {R S T : Over X} (f : S âŸ¶ T) :
    (f â–· R).left â‰« snd T.hom R.hom = snd S.hom R.hom :=
  congr_arg CommaMorphism.left (whiskerRight_snd f R)

lemma tensorHom_left {R S T U : Over X} (f : R âŸ¶ S) (g : T âŸ¶ U) :
    (f âŠ—â‚˜ g).left = pullbackMap S.hom U.hom R.hom T.hom f.left g.left (ğŸ™ _) :=
  rfl

@[reassoc (attr := simp)]
lemma tensorHom_left_fst {R S T U : Over X} (f : R âŸ¶ S) (g : T âŸ¶ U) :
    (f âŠ—â‚˜ g).left â‰« fst S.hom U.hom = fst R.hom T.hom â‰« f.left :=
  congr_arg CommaMorphism.left (tensorHom_fst f g)

@[reassoc (attr := simp)]
lemma tensorHom_left_snd {R S T U : Over X} (f : R âŸ¶ S) (g : T âŸ¶ U) :
    (f âŠ—â‚˜ g).left â‰« snd S.hom U.hom = snd R.hom T.hom â‰« g.left :=
  congr_arg CommaMorphism.left (tensorHom_snd f g)

end Over

end ChosenPullbacksAlong

section ToOver

open ChosenPullbacksAlong CartesianMonoidalCategory MonoidalCategory

variable {C : Type uâ‚} [Category.{vâ‚} C] [CartesianMonoidalCategory C]

/-- The functor which maps an object `A` in `C` to the projection `A âŠ— X âŸ¶ X` in `Over X`.
This is the computable analogue of the functor `Over.star`. -/
@[simps! obj_left obj_hom map_left]
def toOver (X : C) : C â¥¤ Over X where
  obj A := Over.mk <| CartesianMonoidalCategory.snd A X
  map f := Over.homMk (f â–· X)

@[simp]
lemma toOver_map {X : C} {A A' : C} (f : A âŸ¶ A') :
    (toOver X).map f = Over.homMk (f â–· X) := by
  simp [toOver]

variable (C)

/-- The functor from `C` to `Over (ğŸ™_ C)` which sends `X : C` to `Over.mk <| toUnit X`. -/
@[simps! obj_left obj_hom map_left]
def toOverUnit : C â¥¤ Over (ğŸ™_ C) where
  obj X := Over.mk <| toUnit X
  map f := Over.homMk f

/-- The slice category over the terminal unit object is equivalent to the original category. -/
@[simps]
def equivToOverUnit : Over (ğŸ™_ C) â‰Œ C where
  functor := Over.forget _
  inverse := toOverUnit _
  unitIso := NatIso.ofComponents fun X => Over.isoMk (Iso.refl _)
  counitIso := NatIso.ofComponents fun X => Iso.refl _

variable {C}

attribute [local instance] ChosenPullbacksAlong.cartesianMonoidalCategoryToUnit

/-- The isomorphism of functors `toOverUnit C â‹™ ChosenPullbacksAlong.pullback (toUnit X)` and
`toOver X`. -/
@[simps!]
def toOverUnitPullback (X : C) :
    toOverUnit C â‹™ pullback (toUnit X) â‰… toOver X :=
  NatIso.ofComponents fun X => Iso.refl _

/-- The functor `toOver X` is the right adjoint to the functor `Over.forget X`. -/
@[simps! unit_app counit_app]
def forgetAdjToOver (X : C) : Over.forget X âŠ£ toOver X where
  unit.app Z := Over.homMk (lift (ğŸ™ Z.left) (Z.hom))
  counit.app Z := fst Z X

theorem forgetAdjToOver.homEquiv_symm {X : C} (Z : Over X) (A : C) (f : Z âŸ¶ (toOver X).obj A) :
     ((forgetAdjToOver X).homEquiv Z A).symm f = f.left â‰« (fst _ _) := by
   rw [Adjunction.homEquiv_counit, forgetAdjToOver_counit_app]
   simp

/-- The isomorphism of functors `toOver (ğŸ™_ C)` and `toOverUnit C`. -/
@[simps!]
def toOverIsoToOverUnit : toOver (ğŸ™_ C) â‰… toOverUnit C :=
  (forgetAdjToOver (ğŸ™_ C)).rightAdjointUniq (equivToOverUnit C |>.toAdjunction)

/-- A natural isomorphism between the functors `toOver Y` and `toOver X â‹™ pullback f`
for any morphism `f : X âŸ¶ Y`. -/
@[simps!]
def toOverPullbackIsoToOver {X Y : C} (f : Y âŸ¶ X) [ChosenPullbacksAlong f] :
    toOver X â‹™ pullback f â‰… toOver Y :=
  conjugateIsoEquiv ((mapPullbackAdj f).comp (forgetAdjToOver X))
    (forgetAdjToOver Y) (Over.mapForget f)

attribute [local instance] cartesianMonoidalCategoryOver

omit [CartesianMonoidalCategory C] in
/-- The functor `pullback f : Over X â¥¤ Over Y` is naturally isomorphic to
`toOver : Over X â¥¤ Over (Over.mk f)` post-composed with the
iterated slice equivalence `Over (Over.mk f) â¥¤ Over Y`. -/
@[simps!]
def toOverIteratedSliceForwardIsoPullback [ChosenPullbacks C] {X Y : C} (f : Y âŸ¶ X) :
    toOver (Over.mk f) â‹™ (Over.mk f).iteratedSliceForward â‰… pullback f :=
  conjugateIsoEquiv ((Over.mk f).iteratedSliceEquiv.symm.toAdjunction.comp (forgetAdjToOver _))
  (mapPullbackAdj f) (eqToIso (Over.iteratedSliceBackward_forget (Over.mk f)))

end ToOver

end CategoryTheory
