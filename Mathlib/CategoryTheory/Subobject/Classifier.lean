/-
Copyright (c) 2025 Grothendieck Institute. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Pablo Donato
-/

import Mathlib.CategoryTheory.Subobject.Basic
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq

/-!
# Subobject classifier

Following Section I.3 of [Sheaves in Geometry and Logic][MM92], we introduce the notion
`CategoryTheory.Subobject.Classifier C` of subobject classifier in a category `C`.

## Main definitions

Let `C` refer to a category with a terminal object, denoted by `âŠ¤_ C`.

* `CategoryTheory.Subobject.IsClassifier t` describes what it means for a morphism `t : âŠ¤_ C âŸ¶ Î©`
  (called "true" in [MM92]) to be a subobject classifier for `C`.

* `CategoryTheory.Subobject.Classifier C` is the data of such an `Î©` and `t` together with a proof
  that `t` is a subobject classifier for `C`.

* `CategoryTheory.Subobject.HasClassifier C` is the mere existence of a subobject classifier for
  `C`.

* `CategoryTheory.Subobject.cmap` uses the `IsClassifier` property to send every subobject `x` of
  `X` to its characteristic map `Ï‡_ x : X âŸ¶ Î©`.

* `CategoryTheory.Subobject.compr` sends every map `Ï† : X âŸ¶ Î©` to the subobject of `X` whose
  characteristic map is `Ï†` by pulling back `t` along `Ï†`. This generalizes the construction of a
  subset "by comprehension" from its characteristic function in set theory.

* `CategoryTheory.Subobject.sub C` is the presheaf that sends every object `X : C` to its category
  of subobjects `Subobject X`, and every morphism `f : X âŸ¶ Y` to the function
  `Subobject Y â†’ Subobject X` that maps every subobject of `Y` to its pullback along `f`.

## Main statements

* `CategoryTheory.Subobject.hasClassifier_isRepresentable_iff` : a category `C` has a subobject
  classifier `Î©` if and only if the subobjects presheaf `CategoryTheory.Subobject.sub C` is
  representable by `Î©` (Proposition 1 in Section I.3 of [MM92]).

## Notation

* If `x` is a subobject, `Ï‡_ x` denotes the characteristic map of `x` given by the subobject
  classifier.

## Implementation notes

* **TODO**: add a uniqueness theorem for subobject classifiers (up to isomorphism)

## References

* [S. MacLane and I. Moerdijk, *Sheaves in geometry and logic: A first introduction to topos
  theory*][MM92]

## Tags

subobject, subobject classifier, representable functor, presheaf, topos theory
-/

universe u v

/-! ### Some general lemmas -/

lemma unique_eq {Î± : Type u} (h : Unique Î±) (x y : Î±) : x = y := by
  simp [Unique.uniq h x, Unique.uniq h y]

namespace CategoryTheory

open CategoryTheory
open Limits

variable {C : Type u} [Category.{v} C]

namespace IsPullback

lemma of_iso1 {P P' X Y Z : C}
    {f : X âŸ¶ Z} {g : Y âŸ¶ Z} {fst : P âŸ¶ X} {snd : P âŸ¶ Y}
    {fst' : P' âŸ¶ X} {snd' : P' âŸ¶ Y}
    (h : IsPullback fst snd f g) (i : P â‰… P')
    (commfst : fst = i.hom â‰« fst')
    (commsnd : snd = i.hom â‰« snd') :
    IsPullback fst' snd' f g := by
  apply IsPullback.of_iso h i (Iso.refl _) (Iso.refl _) (Iso.refl _) <;> aesop_cat

lemma of_iso3 {P X X' Y Z : C}
    {f : X âŸ¶ Z} {g : Y âŸ¶ Z} {fst : P âŸ¶ X} {snd : P âŸ¶ Y}
    {f' : X' âŸ¶ Z} {fst' : P âŸ¶ X'}
    (h : IsPullback fst snd f g) (i : X â‰… X')
    (commfst : fst â‰« i.hom = fst')
    (commf : f = i.hom â‰« f') :
    IsPullback fst' snd f' g := by
  apply IsPullback.of_iso h (Iso.refl _) i (Iso.refl _) (Iso.refl _) <;> aesop_cat

end IsPullback

namespace Subobject

/-! ### Additional lemmas about pullbacks and subobjects -/

open Subobject

section Pullback

lemma isPullback_eq {X Y Z : C} {x x' : Subobject X}
    {f : X âŸ¶ Z} {g : Y âŸ¶ Z} {k : (x : C) âŸ¶ Y} {k' : (x' : C) âŸ¶ Y}
    (h : IsPullback k x.arrow g f) (h' : IsPullback k' x'.arrow g f) :
    x = x' := by
  let i := @IsPullback.isoIsPullback _ _ _ _ _ _ _ _ _ _ _ _ _ h h'
  apply eq_of_comm i
  simp [i]

lemma isPullback_mk {X Y Z : C}
    (f : Y âŸ¶ Z) (g : X âŸ¶ Z) [HasPullback f g] [Mono f] :
    let Ï€â‚ := pullback.fst f g;
    let Ï€â‚‚ := pullback.snd f g;
    IsPullback ((underlyingIso Ï€â‚‚).hom â‰« Ï€â‚) (mk Ï€â‚‚).arrow f g := by
  intro Ï€â‚ Ï€â‚‚
  apply IsPullback.of_iso1 (IsPullback.of_hasPullback f g) (underlyingIso Ï€â‚‚).symm <;> simp [Ï€â‚, Ï€â‚‚]

lemma isPullback_eq_mk {X Y Z : C} {x : Subobject X}
    {f : Y âŸ¶ Z} {g : X âŸ¶ Z} [HasPullback f g] [Mono f]
    {fst : (x : C) âŸ¶ Y}
    (h : IsPullback fst x.arrow f g) :
    x = mk (pullback.snd f g) := by
  have h' := isPullback_mk f g
  apply isPullback_eq h h'

end Pullback

/-! ### The notion of subobject classifier -/

section SubobjectClassifier

/-- A monomorphism `f` from the terminal object `âŠ¤_ C` is a subobject classifier when it satisfies
    the universal property that every subobject is uniquely a pullback of `f`.
-/
def IsClassifier [HasTerminal C] {Î© : C} (f : âŠ¤_ C âŸ¶ Î©) :=
  Î  {X : C} (x : Subobject X),
  Unique {Ï† : X âŸ¶ Î© // IsPullback (terminal.from (x : C)) x.arrow f Ï†}

variable (C : Type u) [Category.{v} C] [HasTerminal C]

/-- The data for a subobject classifier consists of an object `Î©` of "truth values", together with a
    morphism `t : âŠ¤_ C âŸ¶ Î©` and a proof that `t` is a subobject classifier.
-/
class Classifier where
  /-- The object of "truth values". -/
  Î© : C
  /-- The subobject classifier, which is a generalized element of `Î©` denoting the truth value that
      is "always true". -/
  t : âŠ¤_ C âŸ¶ Î©
  /-- A proof that `t` satisfies the property of being a subobject classifier. -/
  is_classifier : IsClassifier t

/-- The mere existence of a subobject classifier. -/
class HasClassifier : Prop where
  has_classifier : Nonempty (Classifier C)

open Classifier

variable {C : Type u} [Category.{v} C] [HasTerminal C] [Classifier C]

/-- `truth` is the subobject associated to the subobject classifier `t`. -/
noncomputable def truth : Subobject (C := C) Î© := Subobject.mk t

/-- `x.cmap` is the unique characteristic map of subobject `x` given by the `IsClassifier` property.
-/
def cmap {X : C} (x : Subobject X) : X âŸ¶ Î© :=
  (is_classifier x).default.val

/-- `Ï‡_ x` is short for `x.cmap`. -/
abbrev Ï‡_ {X : C} (x : Subobject X) := x.cmap

variable [HasPullbacks C]

/-- `compr Ï‡` builds the subobject whose characteristic map is `Ï‡` by pulling back `truth` along
    `Ï‡`. This generalizes the construction of a subset "by comprehension" from its characteristic
    function in set theory. -/
noncomputable def compr {X : C} (Ï‡ : X âŸ¶ Î©) : Subobject X :=
  (pullback Ï‡).obj truth

lemma compr_isPullback {X : C} (Ï‡ : X âŸ¶ Î©) :
    IsPullback (terminal.from (compr Ï‡ : C)) (compr Ï‡).arrow t Ï‡ := by
  have h := IsPullback.of_hasPullback t Ï‡
  let i : (compr Ï‡ : C) â‰… Limits.pullback t Ï‡ := underlyingIso _
  apply IsPullback.of_iso1 h i.symm _ _ <;> try aesop_cat
  have heq : (compr Ï‡).arrow = (mk (pullback.snd t Ï‡)).arrow := by rfl
  simp [heq, i]

lemma compr_cmap {X : C} (x : Subobject X) :
    compr (Ï‡_ x) = x := by
  have h : IsPullback (terminal.from (x : C)) x.arrow t (Ï‡_ x) :=
    (is_classifier x).default.property
  have h' : IsPullback (terminal.from (compr (Ï‡_ x) : C)) (compr (Ï‡_ x)).arrow t (Ï‡_ x) := by
    apply compr_isPullback
  apply isPullback_eq h' h

lemma cmap_compr {X : C} (Ï† : X âŸ¶ Î©) :
    Ï‡_ (compr Ï†) = Ï† := by
  have h := compr_isPullback Ï†
  have h' := compr_isPullback (Ï‡_ (compr Ï†))
  rw [compr_cmap] at h'
  have heq := unique_eq (is_classifier (compr Ï†)) âŸ¨Ï†, hâŸ© âŸ¨Ï‡_ (compr Ï†), h'âŸ©
  simp [â† Subtype.mk_eq_mk.1 heq]

end SubobjectClassifier

/-! ### The subobjects presheaf `sub` -/

section SubPresheaf

variable [HasPullbacks C]

/-- `sub` is the presheaf that sends every object `X : C` to its category of subobjects
    `Subobject X`, and every morphism `f : X âŸ¶ Y` to the function `Subobject Y â†’ Subobject X`
    that maps every subobject of `Y` to its pullback along `f`. -/
noncomputable def sub : Cáµ’áµ– â¥¤ Type (max u v) where
  obj X := (@Subobject C _ X.unop)

  map f := (pullback f.unop).obj

  map_id := by
    simp only
    intro X
    funext
    rw [unop_id, pullback_id]
    simp

  map_comp := by
    simp only
    intro X Y Z f g
    funext
    rw [unop_comp, pullback_comp]
    simp

end SubPresheaf

/-! ### The representability theorem of subobject classifiers -/

open Classifier

variable [HasTerminal C] [HasPullbacks C]

/-- A category has a subobject classifier if and only if the subobjects functor is representable. -/
theorem isRepresentable_hasClassifier_iff : HasClassifier C â†” (@sub C).IsRepresentable := by
  constructor <;> intro h

  /- The `sub` presheaf is representable by the truth values object `Classifier.Î©`.  -/
  Â· obtain âŸ¨âŸ¨ğ’âŸ©âŸ© := h
    exists Î©
    constructor
    exact {
      /- The correspondence `compr` sending each map `Ï† : X âŸ¶ Î©` to the corresponding subobject is a
         bijection with `cmap` as inverse. -/
      homEquiv := {
        toFun := compr
        invFun := cmap
        left_inv := cmap_compr
        right_inv := compr_cmap
      }
      /- Furthermore, this bijection is natural by the fact that two pullback squares placed side by
         side yield a pullback rectangle (lemma `Subobject.pullback_comp`). -/
      homEquiv_comp := by
        intro X X' f g
        simp only [sub, Equiv.coe_fn_mk, compr, Quiver.Hom.unop_op, pullback_comp]
    }

  /- Any representation `âŸ¨Î©,Î¸âŸ©` of `sub` gives a subobject classifier with truth values
     object `Î©`. -/
  Â· obtain âŸ¨Î©, âŸ¨âŸ¨Î¸, hÎ¸âŸ©âŸ©âŸ© := h

    /- Each subobject `x` of `X` corresponds to a morphism `Ï†â‚“ : X âŸ¶ Î©` through `Î¸`. -/
    let Ï† := fun {X} (x : Subobject X) â†¦ Î¸.symm x
    have hÏ† : âˆ€ {X} (Ï‡ : X âŸ¶ Î©), Ï‡ = Ï† (Î¸ Ï‡) := by simp [Ï†]

    /- Some subobject `Î©â‚€` of `Î©` corresponds to the identity `ğŸ™ Î©` through `Î¸`. -/
    let Î©â‚€ : Subobject Î© := Î¸ (ğŸ™ Î©)
    /- Let `tâ‚€` be the underlying monomorphism of `Î©â‚€` (this requires the axiom of choice). -/
    let tâ‚€ := Î©â‚€.arrow
    have tâ‚€_mono : Mono tâ‚€ := inferInstance

    /- The naturality of `Î¸` (hypothesis `hÎ¸`) states that `x = sub Ï†â‚“ Î©â‚€` for any `x`. -/
    have hx_pullback : âˆ€ {X} (x : Subobject X), x = (pullback (Ï† x)).obj Î©â‚€ := by
      intro X x
      have := hÎ¸ (Î¸.symm x) (ğŸ™ _)
      simp only [Category.comp_id, Equiv.apply_symm_apply] at this
      rw (occs := .pos [1]) [this]
      simp [sub, Ï†, Î©â‚€]

    /- More explicitly, `x` is the canonical representative of the pullback of `tâ‚€` along `Ï†â‚“`. -/
    have hx_mk : âˆ€ {X} (x : Subobject X), x = Subobject.mk (pullback.snd tâ‚€ (Ï† x)) := by
      intro X x
      rw (occs := .pos [1]) [hx_pullback x, pullback_obj]

    /- Even more explicitly, we have an isomorphism `Î¹â‚“` between the underlying object `(x : C)` of
       `x` in `C` (obtained through the axiom of choice) and the pullback of `tâ‚€` and `Ï†â‚“`. -/
    let Î¹ : âˆ€ {X} (x : Subobject X), (x : C) â‰… Limits.pullback tâ‚€ (Ï† x) := by
      intro X x
      rw (occs := .pos [1]) [hx_mk x]
      exact (underlyingIso (pullback.snd tâ‚€ (Ï† x)))

    /- Let `Ï€â‚“ : x âŸ¶ Î©â‚€` be the first projection of the pullback of `tâ‚€` and `Ï†â‚“` modulo `Î¹â‚“`. -/
    let Ï€ := fun {X} (x : Subobject X) â†¦ (Î¹ x).hom â‰« pullback.fst tâ‚€ (Ï† x)

    /- We can finally state that the corresponding pullback square commutes (diagram (5) in [MM92]).

       Here we need to deal with the usual "transport hell" of dependent types, which materializes
       in Lean under the guise of the heterogenous equality type `HEq`. This is because the types of
       morphisms are *propositionally* equal rather than *definitionally* equal, which in turns is
       caused by the need to explicitly manipulate isomorphisms. Univalence would probably make
       things much easier.
    -/
    have isPullback_Ï† : âˆ€ {X} (x : Subobject X), IsPullback (Ï€ x) x.arrow tâ‚€ (Ï† x) := by
      intro X x
      have h := isPullback_mk tâ‚€ (Ï† x)
      have hx := hx_mk x
      dsimp at h
      rw (occs := .pos [1,2,3]) [hx]
      have h1 : ((underlyingIso (pullback.snd tâ‚€ (Ï† x))).hom â‰« pullback.fst tâ‚€ (Ï† x)) =
                (Ï€ (Subobject.mk (pullback.snd tâ‚€ (Ï† x)))) := by
        congr; try exact hx
        dsimp [Î¹]
        set hc := Eq.symm (congrArg (fun _a â†¦ underlying.obj _a â‰… Limits.pullback tâ‚€
                                              (Ï† (Subobject.mk (pullback.snd tâ‚€ (Ï† x)))))
                                    (hx_mk (Subobject.mk (pullback.snd tâ‚€ (Ï† x)))))
        have := cast_heq hc ((underlyingIso (pullback.snd tâ‚€
                                             (Ï† (Subobject.mk (pullback.snd tâ‚€ (Ï† x)))))))
        symm
        apply HEq.trans this
        symm
        congr
      rw [â† h1]
      exact h

    /- Furthermore, `Ï†â‚“` is the unique morphism that makes this pullback square commute by
       bijectivity and naturality of `Î¸`.

       Note that we actually generalize `Ï€â‚“` to any morphism `Ïˆ : x âŸ¶ Î©â‚€`, which will be necessary
       many times later on in the proof.
    -/
    have isPullback_uniq : âˆ€ {X} (x : Subobject X) Ïˆ Ï‡, IsPullback Ïˆ x.arrow tâ‚€ Ï‡ â†’ Ï‡ = Ï† x := by
      intro X x Ïˆ Ï‡ hÏ‡
      rw [hÏ† Ï‡]
      congr
      specialize hÎ¸ Ï‡ (ğŸ™ _)
      rw [Category.comp_id] at hÎ¸
      rw [hÎ¸]
      dsimp [sub]
      rw [pullback_obj, isPullback_eq_mk hÏ‡]
      rfl

    /- It remains to show that `Î©â‚€` is actually a terminal object in `C`. -/
    have isTerminal_Î©â‚€ : IsTerminal (Î©â‚€ : C) := by
      have : (X : C) â†’ Unique (X âŸ¶ Î©â‚€) := by
        intro X
        /- Taking `x` to be the (canonical representative of) the identity `ğŸ™ X`... -/
        let x := Subobject.mk (ğŸ™ X)
        /- ... gives a map `Ï†' : X âŸ¶ Î©â‚€` (modulo the canonical isomorphism `i : X â‰… x`). -/
        let i : X â‰… x := by dsimp [x]; exact (underlyingIso (ğŸ™ X)).symm
        let Ï†' := (i.hom â‰« Ï€ x)

        /- We show that every `Ï†'' : X âŸ¶ Î©â‚€` is equal to `Ï†'`. -/
        refine { default := Ï†', uniq := ?_ }
        intro Ï†''
        dsimp [default]

        /- Since `tâ‚€` is a monomorphism, every `Ïˆ : X âŸ¶ Î©â‚€` forms a "trivial" pullback square. -/
        have hÏˆ : âˆ€ Ïˆ, IsPullback Ïˆ (ğŸ™ X) tâ‚€ (Ïˆ â‰« tâ‚€) := by
          intro Ïˆ
          constructor
          Â· constructor
            apply PullbackCone.IsLimit.mk (lift := fun c â†¦ c.snd) <;> intro c
            Â· apply Mono.right_cancellation (f := tâ‚€)
              rw [c.condition]
              simp
            Â· simp
            Â· intro m hm1 hm2
              rw [â† hm2]
              simp
          Â· simp

        /- This applies in particular to `Ï†` and `Ï†'`. -/
        have h' := hÏˆ Ï†'
        have h'' := hÏˆ Ï†''

        /- This square has the same shape as (5) (modulo the iso `i`), hence by the uniqueness of
           `Ï†â‚“` in (5) we get `tâ‚€ âˆ˜ Ï†' = tâ‚€ âˆ˜ Ï†''`. -/
        have hÏ†' := isPullback_uniq x (Ï€ x) (Ï†' â‰« tâ‚€)
        have hÏ†'' := isPullback_uniq x (i.inv â‰« Ï†'') (Ï†'' â‰« tâ‚€)
        have h : Ï†' â‰« tâ‚€ = Ï†'' â‰« tâ‚€ := by
          rw [hÏ†', hÏ†'']
          Â· apply IsPullback.of_iso1 h'' i
            Â· simp
            Â· simp [i, x]
          Â· apply IsPullback.of_iso1 h' i
            Â· simp only [Ï†']
            Â· simp [i, x]
        /- As `tâ‚€` is monic, this gives `Ï†' = Ï†''`. -/
        exact Mono.right_cancellation _ _ h.symm
      apply IsTerminal.ofUnique

    /- We need to give explicitly the iso `i` with the "canonical" terminal object `âŠ¤_ C`. -/
    have i : âŠ¤_ C â‰… Î©â‚€ := by
      apply IsTerminal.uniqueUpToIso
      exact terminalIsTerminal
      exact isTerminal_Î©â‚€

    /- Finally, we can give `tâ‚€` as the subobject classifier (modulo `i`).  -/
    constructor; constructor
    exact {
      Î© := Î©
      t := i.hom â‰« tâ‚€
      is_classifier := by
        intro X x
        /- The characteristic map is given by `Ï†â‚“`. -/
        refine { default := âŸ¨Ï† x, ?_âŸ©, uniq := ?_ }
        /- `x` is the pullback of `tâ‚€` along `Ï†â‚“` (modulo `i`)... -/
        Â· apply IsPullback.of_iso3 (isPullback_Ï† x) i.symm
          Â· apply unique_eq (uniqueToTerminal _)
          Â· simp
        /- ...and it is so uniquely. -/
        Â· simp only [Subtype.forall, Subtype.mk.injEq]
          intro Ï‡ hÏ‡
          apply isPullback_uniq x (terminal.from (x : C) â‰« i.hom) Ï‡
          apply IsPullback.of_iso3 hÏ‡ i <;> rfl
    }

end Subobject
end CategoryTheory
