/-
Copyright (c) 2022 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Subobject.WellPowered
import Mathlib.CategoryTheory.Limits.Preserves.Finite
import Mathlib.CategoryTheory.Limits.Shapes.FiniteLimits

#align_import category_theory.subobject.comma from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# Subobjects in the category of structured arrows

We compute the subobjects of an object `A` in the category `StructuredArrow S T` for `T : C ‚•§ D`
and `S : D` as a subtype of the subobjects of `A.right`. We deduce that `StructuredArrow S T` is
well-powered if `C` is.

## Main declarations
* `StructuredArrow.subobjectEquiv `: the order-equivalence between `Subobject A` and a subtype of
  `Subobject A.right`.

## Implementation notes
Our computation requires that `C` has all limits and `T` preserves all limits. Furthermore, we
require that the morphisms of `C` and `D` are in the same universe. It is possible that both of
these requirements can be relaxed by refining the results about limits in comma categories.

We also provide the dual results. As usual, we use `Subobject (op A)` for the quotient objects of
`A`.

-/

noncomputable section

open CategoryTheory.Limits Opposite

universe v u‚ÇÅ u‚ÇÇ

namespace CategoryTheory

variable {C : Type u‚ÇÅ} [Category.{v} C] {D : Type u‚ÇÇ} [Category.{v} D]

namespace StructuredArrow

variable {S : D} {T : C ‚•§ D}

/-- Every subobject of a structured arrow can be projected to a subobject of the underlying
    object. -/
def projectSubobject [HasLimits C] [PreservesLimits T] {A : StructuredArrow S T} :
    Subobject A ‚Üí Subobject A.right := by
  refine' Subobject.lift (fun P f hf => Subobject.mk f.right) _
  -- ‚ä¢ ‚àÄ ‚¶ÉA_1 B : StructuredArrow S T‚¶Ñ (f : A_1 ‚ü∂ A) (g : B ‚ü∂ A) [inst : Mono f] [i ‚Ä¶
  intro P Q f g hf hg i hi
  -- ‚ä¢ (fun P f hf => Subobject.mk f.right) P f hf = (fun P f hf => Subobject.mk f. ‚Ä¶
  refine' Subobject.mk_eq_mk_of_comm _ _ ((proj S T).mapIso i) _
  -- ‚ä¢ ((proj S T).mapIso i).hom ‚â´ g.right = f.right
  exact congr_arg CommaMorphism.right hi
  -- üéâ no goals
#align category_theory.structured_arrow.project_subobject CategoryTheory.StructuredArrow.projectSubobject

@[simp]
theorem projectSubobject_mk [HasLimits C] [PreservesLimits T] {A P : StructuredArrow S T}
    (f : P ‚ü∂ A) [Mono f] : projectSubobject (Subobject.mk f) = Subobject.mk f.right :=
  rfl
#align category_theory.structured_arrow.project_subobject_mk CategoryTheory.StructuredArrow.projectSubobject_mk

theorem projectSubobject_factors [HasLimits C] [PreservesLimits T] {A : StructuredArrow S T} :
    ‚àÄ P : Subobject A, ‚àÉ q, q ‚â´ T.map (projectSubobject P).arrow = A.hom :=
  Subobject.ind _ fun P f hf =>
    ‚ü®P.hom ‚â´ T.map (Subobject.underlyingIso _).inv, by
      dsimp
      -- ‚ä¢ (P.hom ‚â´ T.map (Subobject.underlyingIso f.right).inv) ‚â´ T.map (Subobject.arr ‚Ä¶
      simp [‚Üê T.map_comp]‚ü©
      -- üéâ no goals
#align category_theory.structured_arrow.project_subobject_factors CategoryTheory.StructuredArrow.projectSubobject_factors

/-- A subobject of the underlying object of a structured arrow can be lifted to a subobject of
    the structured arrow, provided that there is a morphism making the subobject into a structured
    arrow. -/
@[simp]
def liftSubobject {A : StructuredArrow S T} (P : Subobject A.right) {q}
    (hq : q ‚â´ T.map P.arrow = A.hom) : Subobject A :=
  Subobject.mk (homMk P.arrow hq : mk q ‚ü∂ A)
#align category_theory.structured_arrow.lift_subobject CategoryTheory.StructuredArrow.liftSubobject

/-- Projecting and then lifting a subobject recovers the original subobject, because there is at
    most one morphism making the projected subobject into a structured arrow. -/
theorem lift_projectSubobject [HasLimits C] [PreservesLimits T] {A : StructuredArrow S T} :
    ‚àÄ (P : Subobject A) {q} (hq : q ‚â´ T.map (projectSubobject P).arrow = A.hom),
      liftSubobject (projectSubobject P) hq = P :=
  Subobject.ind _
    (by
      intro P f hf q hq
      -- ‚ä¢ liftSubobject (projectSubobject (Subobject.mk f)) hq = Subobject.mk f
      fapply Subobject.mk_eq_mk_of_comm
      -- ‚ä¢ mk q ‚âÖ P
      ¬∑ fapply isoMk
        -- ‚ä¢ (mk q).right ‚âÖ P.right
        ¬∑ exact Subobject.underlyingIso _
          -- üéâ no goals
        ¬∑ exact (cancel_mono (T.map f.right)).1 (by dsimp; simpa [‚Üê T.map_comp] using hq)
          -- üéâ no goals
      ¬∑ exact ext _ _ (by dsimp; simp))
        -- üéâ no goals
#align category_theory.structured_arrow.lift_project_subobject CategoryTheory.StructuredArrow.lift_projectSubobject

/-- If `A : S ‚Üí T.obj B` is a structured arrow for `S : D` and `T : C ‚•§ D`, then we can explicitly
    describe the subobjects of `A` as the subobjects `P` of `B` in `C` for which `A.hom` factors
    through the image of `P` under `T`. -/
@[simps!]
def subobjectEquiv [HasLimits C] [PreservesLimits T] (A : StructuredArrow S T) :
    Subobject A ‚âÉo { P : Subobject A.right // ‚àÉ q, q ‚â´ T.map P.arrow = A.hom } where
  toFun P := ‚ü®projectSubobject P, projectSubobject_factors P‚ü©
  invFun P := liftSubobject P.val P.prop.choose_spec
  left_inv P := lift_projectSubobject _ _
  right_inv P := Subtype.ext (by simp only [liftSubobject, homMk_right, projectSubobject_mk,
      Subobject.mk_arrow, Subtype.coe_eta])
  map_rel_iff' := by
    apply Subobject.ind‚ÇÇ
    -- ‚ä¢ ‚àÄ ‚¶ÉA_1 B : StructuredArrow S T‚¶Ñ (f : A_1 ‚ü∂ A) (g : B ‚ü∂ A) [inst : Mono f] [i ‚Ä¶
    intro P Q f g hf hg
    -- ‚ä¢ ‚Üë{ toFun := fun P => { val := projectSubobject P, property := (_ : ‚àÉ q, q ‚â´  ‚Ä¶
    refine' ‚ü®fun h => Subobject.mk_le_mk_of_comm _ _, fun h => _‚ü©
    ¬∑ exact homMk (Subobject.ofMkLEMk _ _ h)
        ((cancel_mono (T.map g.right)).1 (by simp [‚Üê T.map_comp]))
    ¬∑ aesop_cat
      -- üéâ no goals
    ¬∑ refine' Subobject.mk_le_mk_of_comm (Subobject.ofMkLEMk _ _ h).right _
      -- ‚ä¢ (Subobject.ofMkLEMk f g h).right ‚â´ (MonoOver.arrow (MonoOver.mk' g)).right = ‚Ä¶
      exact congr_arg CommaMorphism.right (Subobject.ofMkLEMk_comp h)
      -- üéâ no goals
#align category_theory.structured_arrow.subobject_equiv CategoryTheory.StructuredArrow.subobjectEquiv

/-- If `C` is well-powered and complete and `T` preserves limits, then `StructuredArrow S T` is
    well-powered. -/
instance wellPowered_structuredArrow [WellPowered C] [HasLimits C] [PreservesLimits T] :
    WellPowered (StructuredArrow S T)
    where subobject_small X := small_map (subobjectEquiv X).toEquiv
#align category_theory.structured_arrow.well_powered_structured_arrow CategoryTheory.StructuredArrow.wellPowered_structuredArrow

end StructuredArrow

namespace CostructuredArrow

variable {S : C ‚•§ D} {T : D}

/-- Every quotient of a costructured arrow can be projected to a quotient of the underlying
    object. -/
def projectQuotient [HasColimits C] [PreservesColimits S] {A : CostructuredArrow S T} :
    Subobject (op A) ‚Üí Subobject (op A.left) := by
  refine' Subobject.lift (fun P f hf => Subobject.mk f.unop.left.op) _
  -- ‚ä¢ ‚àÄ ‚¶ÉA_1 B : (CostructuredArrow S T)·µí·µñ‚¶Ñ (f : A_1 ‚ü∂ op A) (g : B ‚ü∂ op A) [inst  ‚Ä¶
  intro P Q f g hf hg i hi
  -- ‚ä¢ (fun P f hf => Subobject.mk f.unop.left.op) P f hf = (fun P f hf => Subobjec ‚Ä¶
  refine' Subobject.mk_eq_mk_of_comm _ _ ((proj S T).mapIso i.unop).op (Quiver.Hom.unop_inj _)
  -- ‚ä¢ ((Iso.op ((proj S T).mapIso (Iso.unop i))).hom ‚â´ g.unop.left.op).unop = f.un ‚Ä¶
  have := congr_arg Quiver.Hom.unop hi
  -- ‚ä¢ ((Iso.op ((proj S T).mapIso (Iso.unop i))).hom ‚â´ g.unop.left.op).unop = f.un ‚Ä¶
  simpa using congr_arg CommaMorphism.left this
  -- üéâ no goals
#align category_theory.costructured_arrow.project_quotient CategoryTheory.CostructuredArrow.projectQuotient

@[simp]
theorem projectQuotient_mk [HasColimits C] [PreservesColimits S] {A : CostructuredArrow S T}
    {P : (CostructuredArrow S T)·µí·µñ} (f : P ‚ü∂ op A) [Mono f] :
    projectQuotient (Subobject.mk f) = Subobject.mk f.unop.left.op :=
  rfl
#align category_theory.costructured_arrow.project_quotient_mk CategoryTheory.CostructuredArrow.projectQuotient_mk

theorem projectQuotient_factors [HasColimits C] [PreservesColimits S] {A : CostructuredArrow S T} :
    ‚àÄ P : Subobject (op A), ‚àÉ q, S.map (projectQuotient P).arrow.unop ‚â´ q = A.hom :=
  Subobject.ind _ fun P f hf =>
    ‚ü®S.map (Subobject.underlyingIso _).unop.inv ‚â´ P.unop.hom, by
      dsimp
      -- ‚ä¢ S.map (Subobject.arrow (Subobject.mk f.unop.left.op)).unop ‚â´ S.map (Subobjec ‚Ä¶
      rw [‚Üê Category.assoc, ‚Üê S.map_comp, ‚Üê unop_comp]
      -- ‚ä¢ S.map ((Subobject.underlyingIso f.unop.left.op).inv ‚â´ Subobject.arrow (Subob ‚Ä¶
      simp‚ü©
      -- üéâ no goals
#align category_theory.costructured_arrow.project_quotient_factors CategoryTheory.CostructuredArrow.projectQuotient_factors

/-- A quotient of the underlying object of a costructured arrow can be lifted to a quotient of
    the costructured arrow, provided that there is a morphism making the quotient into a
    costructured arrow. -/
@[simp]
def liftQuotient {A : CostructuredArrow S T} (P : Subobject (op A.left)) {q}
    (hq : S.map P.arrow.unop ‚â´ q = A.hom) : Subobject (op A) :=
  Subobject.mk (homMk P.arrow.unop hq : A ‚ü∂ mk q).op
#align category_theory.costructured_arrow.lift_quotient CategoryTheory.CostructuredArrow.liftQuotient

/-- Technical lemma for `lift_projectQuotient`. -/
@[simp]
theorem unop_left_comp_underlyingIso_hom_unop {A : CostructuredArrow S T}
    {P : (CostructuredArrow S T)·µí·µñ} (f : P ‚ü∂ op A) [Mono f.unop.left.op] :
    f.unop.left ‚â´ (Subobject.underlyingIso f.unop.left.op).hom.unop =
      (Subobject.mk f.unop.left.op).arrow.unop := by
  conv_lhs =>
    congr
    rw [‚Üê Quiver.Hom.unop_op f.unop.left]
  rw [‚Üê unop_comp, Subobject.underlyingIso_hom_comp_eq_mk]
  -- üéâ no goals
#align category_theory.costructured_arrow.unop_left_comp_underlying_iso_hom_unop CategoryTheory.CostructuredArrow.unop_left_comp_underlyingIso_hom_unop

/-- Projecting and then lifting a quotient recovers the original quotient, because there is at most
    one morphism making the projected quotient into a costructured arrow. -/
theorem lift_projectQuotient [HasColimits C] [PreservesColimits S] {A : CostructuredArrow S T} :
    ‚àÄ (P : Subobject (op A)) {q} (hq : S.map (projectQuotient P).arrow.unop ‚â´ q = A.hom),
      liftQuotient (projectQuotient P) hq = P :=
  Subobject.ind _
    (by
      intro P f hf q hq
      -- ‚ä¢ liftQuotient (projectQuotient (Subobject.mk f)) hq = Subobject.mk f
      fapply Subobject.mk_eq_mk_of_comm
      -- ‚ä¢ op (mk q) ‚âÖ P
      ¬∑ refine' (Iso.op (isoMk _ _) : _ ‚âÖ op (unop P))
        -- ‚ä¢ P.unop.left ‚âÖ (mk q).left
        ¬∑ exact (Subobject.underlyingIso f.unop.left.op).unop
          -- üéâ no goals
        ¬∑ refine' (cancel_epi (S.map f.unop.left)).1 _
          -- ‚ä¢ S.map f.unop.left ‚â´ S.map (Iso.unop (Subobject.underlyingIso f.unop.left.op) ‚Ä¶
          simpa [‚Üê Category.assoc, ‚Üê S.map_comp] using hq
          -- üéâ no goals
      ¬∑ exact Quiver.Hom.unop_inj (by aesop_cat))
        -- üéâ no goals
#align category_theory.costructured_arrow.lift_project_quotient CategoryTheory.CostructuredArrow.lift_projectQuotient

/-- Technical lemma for `quotientEquiv`. -/
theorem unop_left_comp_ofMkLEMk_unop {A : CostructuredArrow S T} {P Q : (CostructuredArrow S T)·µí·µñ}
    {f : P ‚ü∂ op A} {g : Q ‚ü∂ op A} [Mono f.unop.left.op] [Mono g.unop.left.op]
    (h : Subobject.mk f.unop.left.op ‚â§ Subobject.mk g.unop.left.op) :
    g.unop.left ‚â´ (Subobject.ofMkLEMk f.unop.left.op g.unop.left.op h).unop = f.unop.left := by
  conv_lhs =>
    congr
    rw [‚Üê Quiver.Hom.unop_op g.unop.left]
  rw [‚Üê unop_comp]
  -- ‚ä¢ (Subobject.ofMkLEMk f.unop.left.op g.unop.left.op h ‚â´ g.unop.left.op).unop = ‚Ä¶
  simp only [Subobject.ofMkLEMk_comp, Quiver.Hom.unop_op]
  -- üéâ no goals
#align category_theory.costructured_arrow.unop_left_comp_of_mk_le_mk_unop CategoryTheory.CostructuredArrow.unop_left_comp_ofMkLEMk_unop

/-- If `A : S.obj B ‚ü∂ T` is a costructured arrow for `S : C ‚•§ D` and `T : D`, then we can
    explicitly describe the quotients of `A` as the quotients `P` of `B` in `C` for which `A.hom`
    factors through the image of `P` under `S`. -/
def quotientEquiv [HasColimits C] [PreservesColimits S] (A : CostructuredArrow S T) :
    Subobject (op A) ‚âÉo { P : Subobject (op A.left) // ‚àÉ q, S.map P.arrow.unop ‚â´ q = A.hom } where
  toFun P := ‚ü®projectQuotient P, projectQuotient_factors P‚ü©
  invFun P := liftQuotient P.val P.prop.choose_spec
  left_inv P := lift_projectQuotient _ _
  right_inv P := Subtype.ext (by simp only [liftQuotient, Quiver.Hom.unop_op, homMk_left,
      Quiver.Hom.op_unop, projectQuotient_mk, Subobject.mk_arrow])
  map_rel_iff' := by
    apply Subobject.ind‚ÇÇ
    -- ‚ä¢ ‚àÄ ‚¶ÉA_1 B : (CostructuredArrow S T)·µí·µñ‚¶Ñ (f : A_1 ‚ü∂ op A) (g : B ‚ü∂ op A) [inst  ‚Ä¶
    intro P Q f g hf hg
    -- ‚ä¢ ‚Üë{ toFun := fun P => { val := projectQuotient P, property := (_ : ‚àÉ q, S.map ‚Ä¶
    refine' ‚ü®fun h => Subobject.mk_le_mk_of_comm _ _, fun h => _‚ü©
    ¬∑ refine' (homMk (Subobject.ofMkLEMk _ _ h).unop ((cancel_epi (S.map g.unop.left)).1 _)).op
      -- ‚ä¢ S.map g.unop.left ‚â´ S.map (Subobject.ofMkLEMk (MonoOver.arrow (MonoOver.mk'  ‚Ä¶
      dsimp
      -- ‚ä¢ S.map g.unop.left ‚â´ S.map (Subobject.ofMkLEMk f.unop.left.op g.unop.left.op  ‚Ä¶
      simp only [‚Üê S.map_comp_assoc, unop_left_comp_ofMkLEMk_unop, unop_op, CommaMorphism.w,
        Functor.const_obj_obj, right_eq_id, Functor.const_obj_map, Category.comp_id]
    ¬∑ apply Quiver.Hom.unop_inj
      -- ‚ä¢ ((homMk (Subobject.ofMkLEMk (MonoOver.arrow (MonoOver.mk' f)).unop.left.op ( ‚Ä¶
      ext
      -- ‚ä¢ ((homMk (Subobject.ofMkLEMk (MonoOver.arrow (MonoOver.mk' f)).unop.left.op ( ‚Ä¶
      exact unop_left_comp_ofMkLEMk_unop _
      -- üéâ no goals
    ¬∑ refine' Subobject.mk_le_mk_of_comm (Subobject.ofMkLEMk _ _ h).unop.left.op _
      -- ‚ä¢ (Subobject.ofMkLEMk f g h).unop.left.op ‚â´ (MonoOver.arrow (MonoOver.mk' g)). ‚Ä¶
      refine' Quiver.Hom.unop_inj _
      -- ‚ä¢ ((Subobject.ofMkLEMk f g h).unop.left.op ‚â´ (MonoOver.arrow (MonoOver.mk' g)) ‚Ä¶
      have := congr_arg Quiver.Hom.unop (Subobject.ofMkLEMk_comp h)
      -- ‚ä¢ ((Subobject.ofMkLEMk f g h).unop.left.op ‚â´ (MonoOver.arrow (MonoOver.mk' g)) ‚Ä¶
      simpa only [unop_op, Functor.id_obj, Functor.const_obj_obj, MonoOver.mk'_obj, Over.mk_left,
        MonoOver.mk'_arrow, unop_comp, Quiver.Hom.unop_op, comp_left]
          using congr_arg CommaMorphism.left this
#align category_theory.costructured_arrow.quotient_equiv CategoryTheory.CostructuredArrow.quotientEquiv

/-- If `C` is well-copowered and cocomplete and `S` preserves colimits, then
    `CostructuredArrow S T` is well-copowered. -/
instance well_copowered_costructuredArrow [WellPowered C·µí·µñ] [HasColimits C] [PreservesColimits S] :
    WellPowered (CostructuredArrow S T)·µí·µñ
    where subobject_small X := small_map (quotientEquiv (unop X)).toEquiv
#align category_theory.costructured_arrow.well_copowered_costructured_arrow CategoryTheory.CostructuredArrow.well_copowered_costructuredArrow

end CostructuredArrow

end CategoryTheory
