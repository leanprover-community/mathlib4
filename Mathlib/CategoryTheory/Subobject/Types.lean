/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Subobject.WellPowered
import Mathlib.CategoryTheory.Types

#align_import category_theory.subobject.types from "leanprover-community/mathlib"@"610955826b3be3caaab5170fef04ecd5458521bf"

/-!
# `Type u` is well-powered

By building a categorical equivalence `MonoOver Î± â‰Œ Set Î±` for any `Î± : Type u`,
we deduce that `Subobject Î± â‰ƒo Set Î±` and that `Type u` is well-powered.

One would hope that for a particular concrete category `C` (`AddCommGroup`, etc)
it's viable to prove `[WellPowered C]` without explicitly aligning `Subobject X`
with the "hand-rolled" definition of subobjects.

This may be possible using Lawvere theories,
but it remains to be seen whether this just pushes lumps around in the carpet.
-/


universe u

open CategoryTheory

open CategoryTheory.Subobject

theorem subtype_val_mono {Î± : Type u} (s : Set Î±) : Mono (â†¾(Subtype.val : s â†’ Î±)) :=
  (mono_iff_injective _).mpr Subtype.val_injective
#align subtype_val_mono subtype_val_mono

attribute [local instance] subtype_val_mono

/-- The category of `MonoOver Î±`, for `Î± : Type u`, is equivalent to the partial order `Set Î±`.
-/
@[simps]
noncomputable def Types.monoOverEquivalenceSet (Î± : Type u) : MonoOver Î± â‰Œ Set Î± where
  functor :=
    { obj := fun f => Set.range f.1.hom
      map := fun {f g} t =>
        homOfLE
          (by
            rintro a âŸ¨x, rflâŸ©
            -- âŠ¢ Comma.hom f.obj x âˆˆ (fun f => Set.range f.obj.hom) g
            exact âŸ¨t.1 x, congr_fun t.w xâŸ©) }
            -- ðŸŽ‰ no goals
  inverse :=
    { obj := fun s => MonoOver.mk' (Subtype.val : s â†’ Î±)
      map := fun {s t} b => MonoOver.homMk (fun w => âŸ¨w.1, Set.mem_of_mem_of_subset w.2 b.leâŸ©) }
  unitIso :=
    NatIso.ofComponents fun f =>
      MonoOver.isoMk (Equiv.ofInjective f.1.hom ((mono_iff_injective _).mp f.2)).toIso
  counitIso := NatIso.ofComponents fun s => eqToIso Subtype.range_val
#align types.mono_over_equivalence_set Types.monoOverEquivalenceSet

instance : WellPowered (Type u) :=
  wellPowered_of_essentiallySmall_monoOver fun Î± =>
    EssentiallySmall.mk' (Types.monoOverEquivalenceSet Î±)

/-- For `Î± : Type u`, `Subobject Î±` is order isomorphic to `Set Î±`.
-/
noncomputable def Types.subobjectEquivSet (Î± : Type u) : Subobject Î± â‰ƒo Set Î± :=
  (Types.monoOverEquivalenceSet Î±).thinSkeletonOrderIso
#align types.subobject_equiv_set Types.subobjectEquivSet
