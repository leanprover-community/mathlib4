/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Kim Morrison
-/
import Mathlib.CategoryTheory.Subobject.Lattice

/-!
# Specific subobjects

We define `equalizerSubobject`, `kernelSubobject` and `imageSubobject`, which are the subobjects
represented by the equalizer, kernel and image of (a pair of) morphism(s) and provide conditions
for `P.factors f`, where `P` is one of these special subobjects.

TODO: Add conditions for when `P` is a pullback subobject.
TODO: an iff characterisation of `(imageSubobject f).Factors h`

-/

universe v u

noncomputable section

open CategoryTheory CategoryTheory.Category CategoryTheory.Limits CategoryTheory.Subobject Opposite

variable {C : Type u} [Category.{v} C] {X Y Z : C}

namespace CategoryTheory

namespace Limits

section Equalizer

variable (f g : X ‚ü∂ Y) [HasEqualizer f g]

/-- The equalizer of morphisms `f g : X ‚ü∂ Y` as a `Subobject X`. -/
abbrev equalizerSubobject : Subobject X :=
  Subobject.mk (equalizer.Œπ f g)

/-- The underlying object of `equalizerSubobject f g` is (up to isomorphism!)
the same as the chosen object `equalizer f g`. -/
def equalizerSubobjectIso : (equalizerSubobject f g : C) ‚âÖ equalizer f g :=
  Subobject.underlyingIso (equalizer.Œπ f g)

@[reassoc (attr := simp)]
theorem equalizerSubobject_arrow :
    (equalizerSubobjectIso f g).hom ‚â´ equalizer.Œπ f g = (equalizerSubobject f g).arrow := by
  simp [equalizerSubobjectIso]

@[reassoc (attr := simp)]
theorem equalizerSubobject_arrow' :
    (equalizerSubobjectIso f g).inv ‚â´ (equalizerSubobject f g).arrow = equalizer.Œπ f g := by
  simp [equalizerSubobjectIso]

@[reassoc]
theorem equalizerSubobject_arrow_comp :
    (equalizerSubobject f g).arrow ‚â´ f = (equalizerSubobject f g).arrow ‚â´ g := by
  rw [‚Üê equalizerSubobject_arrow, Category.assoc, Category.assoc, equalizer.condition]

theorem equalizerSubobject_factors {W : C} (h : W ‚ü∂ X) (w : h ‚â´ f = h ‚â´ g) :
    (equalizerSubobject f g).Factors h :=
  ‚ü®equalizer.lift h w, by simp‚ü©

theorem equalizerSubobject_factors_iff {W : C} (h : W ‚ü∂ X) :
    (equalizerSubobject f g).Factors h ‚Üî h ‚â´ f = h ‚â´ g :=
  ‚ü®fun w => by
    rw [‚Üê Subobject.factorThru_arrow _ _ w, Category.assoc, equalizerSubobject_arrow_comp,
      Category.assoc],
    equalizerSubobject_factors f g h‚ü©

end Equalizer

section Kernel

variable [HasZeroMorphisms C] (f : X ‚ü∂ Y) [HasKernel f]

/-- The kernel of a morphism `f : X ‚ü∂ Y` as a `Subobject X`. -/
abbrev kernelSubobject : Subobject X :=
  Subobject.mk (kernel.Œπ f)

/-- The underlying object of `kernelSubobject f` is (up to isomorphism!)
the same as the chosen object `kernel f`. -/
def kernelSubobjectIso : (kernelSubobject f : C) ‚âÖ kernel f :=
  Subobject.underlyingIso (kernel.Œπ f)

@[reassoc (attr := simp), elementwise (attr := simp)]
theorem kernelSubobject_arrow :
    (kernelSubobjectIso f).hom ‚â´ kernel.Œπ f = (kernelSubobject f).arrow := by
  simp [kernelSubobjectIso]

@[reassoc (attr := simp), elementwise (attr := simp)]
theorem kernelSubobject_arrow' :
    (kernelSubobjectIso f).inv ‚â´ (kernelSubobject f).arrow = kernel.Œπ f := by
  simp [kernelSubobjectIso]

@[reassoc (attr := simp), elementwise (attr := simp)]
theorem kernelSubobject_arrow_comp : (kernelSubobject f).arrow ‚â´ f = 0 := by
  rw [‚Üê kernelSubobject_arrow]
  simp only [Category.assoc, kernel.condition, comp_zero]

theorem kernelSubobject_factors {W : C} (h : W ‚ü∂ X) (w : h ‚â´ f = 0) :
    (kernelSubobject f).Factors h :=
  ‚ü®kernel.lift _ h w, by simp‚ü©

theorem kernelSubobject_factors_iff {W : C} (h : W ‚ü∂ X) :
    (kernelSubobject f).Factors h ‚Üî h ‚â´ f = 0 :=
  ‚ü®fun w => by
    rw [‚Üê Subobject.factorThru_arrow _ _ w, Category.assoc, kernelSubobject_arrow_comp,
      comp_zero],
    kernelSubobject_factors f h‚ü©

/-- A factorisation of `h : W ‚ü∂ X` through `kernelSubobject f`, assuming `h ‚â´ f = 0`. -/
def factorThruKernelSubobject {W : C} (h : W ‚ü∂ X) (w : h ‚â´ f = 0) : W ‚ü∂ kernelSubobject f :=
  (kernelSubobject f).factorThru h (kernelSubobject_factors f h w)

@[simp]
theorem factorThruKernelSubobject_comp_arrow {W : C} (h : W ‚ü∂ X) (w : h ‚â´ f = 0) :
    factorThruKernelSubobject f h w ‚â´ (kernelSubobject f).arrow = h := by
  dsimp [factorThruKernelSubobject]
  simp

@[simp]
theorem factorThruKernelSubobject_comp_kernelSubobjectIso {W : C} (h : W ‚ü∂ X) (w : h ‚â´ f = 0) :
    factorThruKernelSubobject f h w ‚â´ (kernelSubobjectIso f).hom = kernel.lift f h w :=
  (cancel_mono (kernel.Œπ f)).1 <| by simp

section

variable {f} {X' Y' : C} {f' : X' ‚ü∂ Y'} [HasKernel f']

/-- A commuting square induces a morphism between the kernel subobjects. -/
def kernelSubobjectMap (sq : Arrow.mk f ‚ü∂ Arrow.mk f') :
    (kernelSubobject f : C) ‚ü∂ (kernelSubobject f' : C) :=
  Subobject.factorThru _ ((kernelSubobject f).arrow ‚â´ sq.left)
    (kernelSubobject_factors _ _ (by simp [sq.w]))

@[reassoc (attr := simp), elementwise (attr := simp)]
theorem kernelSubobjectMap_arrow (sq : Arrow.mk f ‚ü∂ Arrow.mk f') :
    kernelSubobjectMap sq ‚â´ (kernelSubobject f').arrow = (kernelSubobject f).arrow ‚â´ sq.left := by
  simp [kernelSubobjectMap]

@[simp]
theorem kernelSubobjectMap_id : kernelSubobjectMap (ùüô (Arrow.mk f)) = ùüô _ := by aesop_cat

@[simp]
theorem kernelSubobjectMap_comp {X'' Y'' : C} {f'' : X'' ‚ü∂ Y''} [HasKernel f'']
    (sq : Arrow.mk f ‚ü∂ Arrow.mk f') (sq' : Arrow.mk f' ‚ü∂ Arrow.mk f'') :
    kernelSubobjectMap (sq ‚â´ sq') = kernelSubobjectMap sq ‚â´ kernelSubobjectMap sq' := by
  aesop_cat

@[reassoc]
theorem kernel_map_comp_kernelSubobjectIso_inv (sq : Arrow.mk f ‚ü∂ Arrow.mk f') :
    kernel.map f f' sq.1 sq.2 sq.3.symm ‚â´ (kernelSubobjectIso _).inv =
      (kernelSubobjectIso _).inv ‚â´ kernelSubobjectMap sq := by aesop_cat

@[reassoc]
theorem kernelSubobjectIso_comp_kernel_map (sq : Arrow.mk f ‚ü∂ Arrow.mk f') :
    (kernelSubobjectIso _).hom ‚â´ kernel.map f f' sq.1 sq.2 sq.3.symm =
      kernelSubobjectMap sq ‚â´ (kernelSubobjectIso _).hom := by
  simp [‚Üê Iso.comp_inv_eq, kernel_map_comp_kernelSubobjectIso_inv]

end

@[simp]
theorem kernelSubobject_zero {A B : C} : kernelSubobject (0 : A ‚ü∂ B) = ‚ä§ :=
  (isIso_iff_mk_eq_top _).mp (by infer_instance)

instance isIso_kernelSubobject_zero_arrow : IsIso (kernelSubobject (0 : X ‚ü∂ Y)).arrow :=
  (isIso_arrow_iff_eq_top _).mpr kernelSubobject_zero

theorem le_kernelSubobject (A : Subobject X) (h : A.arrow ‚â´ f = 0) : A ‚â§ kernelSubobject f :=
  Subobject.le_mk_of_comm (kernel.lift f A.arrow h) (by simp)

/-- The isomorphism between the kernel of `f ‚â´ g` and the kernel of `g`,
when `f` is an isomorphism.
-/
def kernelSubobjectIsoComp {X' : C} (f : X' ‚ü∂ X) [IsIso f] (g : X ‚ü∂ Y) [HasKernel g] :
    (kernelSubobject (f ‚â´ g) : C) ‚âÖ (kernelSubobject g : C) :=
  kernelSubobjectIso _ ‚â™‚â´ kernelIsIsoComp f g ‚â™‚â´ (kernelSubobjectIso _).symm

@[simp]
theorem kernelSubobjectIsoComp_hom_arrow {X' : C} (f : X' ‚ü∂ X) [IsIso f] (g : X ‚ü∂ Y) [HasKernel g] :
    (kernelSubobjectIsoComp f g).hom ‚â´ (kernelSubobject g).arrow =
      (kernelSubobject (f ‚â´ g)).arrow ‚â´ f := by
  simp [kernelSubobjectIsoComp]

@[simp]
theorem kernelSubobjectIsoComp_inv_arrow {X' : C} (f : X' ‚ü∂ X) [IsIso f] (g : X ‚ü∂ Y) [HasKernel g] :
    (kernelSubobjectIsoComp f g).inv ‚â´ (kernelSubobject (f ‚â´ g)).arrow =
      (kernelSubobject g).arrow ‚â´ inv f := by
  simp [kernelSubobjectIsoComp]

/-- The kernel of `f` is always a smaller subobject than the kernel of `f ‚â´ h`. -/
theorem kernelSubobject_comp_le (f : X ‚ü∂ Y) [HasKernel f] {Z : C} (h : Y ‚ü∂ Z) [HasKernel (f ‚â´ h)] :
    kernelSubobject f ‚â§ kernelSubobject (f ‚â´ h) :=
  le_kernelSubobject _ _ (by simp)

/-- Postcomposing by a monomorphism does not change the kernel subobject. -/
@[simp]
theorem kernelSubobject_comp_mono (f : X ‚ü∂ Y) [HasKernel f] {Z : C} (h : Y ‚ü∂ Z) [Mono h] :
    kernelSubobject (f ‚â´ h) = kernelSubobject f :=
  le_antisymm (le_kernelSubobject _ _ ((cancel_mono h).mp (by simp))) (kernelSubobject_comp_le f h)

instance kernelSubobject_comp_mono_isIso (f : X ‚ü∂ Y) [HasKernel f] {Z : C} (h : Y ‚ü∂ Z) [Mono h] :
    IsIso (Subobject.ofLE _ _ (kernelSubobject_comp_le f h)) := by
  rw [ofLE_mk_le_mk_of_comm (kernelCompMono f h).inv]
  ¬∑ infer_instance
  ¬∑ simp

/-- Taking cokernels is an order-reversing map from the subobjects of `X` to the quotient objects
    of `X`. -/
@[simps]
def cokernelOrderHom [HasCokernels C] (X : C) : Subobject X ‚Üío (Subobject (op X))·µí·µà where
  toFun :=
    Subobject.lift (fun _ f _ => Subobject.mk (cokernel.œÄ f).op)
      (by
        rintro A B f g hf hg i rfl
        refine Subobject.mk_eq_mk_of_comm _ _ (Iso.op ?_) (Quiver.Hom.unop_inj ?_)
        ¬∑ exact (IsColimit.coconePointUniqueUpToIso (colimit.isColimit _)
            (isCokernelEpiComp (colimit.isColimit _) i.hom rfl)).symm
        ¬∑ simp only [Iso.comp_inv_eq, Iso.op_hom, Iso.symm_hom, unop_comp, Quiver.Hom.unop_op,
            colimit.comp_coconePointUniqueUpToIso_hom, Cofork.ofœÄ_Œπ_app,
            coequalizer.cofork_œÄ])
  monotone' :=
    Subobject.ind‚ÇÇ _ <| by
      intro A B f g hf hg h
      dsimp only [Subobject.lift_mk]
      refine Subobject.mk_le_mk_of_comm (cokernel.desc f (cokernel.œÄ g) ?_).op ?_
      ¬∑ rw [‚Üê Subobject.ofMkLEMk_comp h, Category.assoc, cokernel.condition, comp_zero]
      ¬∑ exact Quiver.Hom.unop_inj (cokernel.œÄ_desc _ _ _)

/-- Taking kernels is an order-reversing map from the quotient objects of `X` to the subobjects of
    `X`. -/
@[simps]
def kernelOrderHom [HasKernels C] (X : C) : (Subobject (op X))·µí·µà ‚Üío Subobject X where
  toFun :=
    Subobject.lift (fun _ f _ => Subobject.mk (kernel.Œπ f.unop))
      (by
        rintro A B f g hf hg i rfl
        refine Subobject.mk_eq_mk_of_comm _ _ ?_ ?_
        ¬∑ exact
            IsLimit.conePointUniqueUpToIso (limit.isLimit _)
              (isKernelCompMono (limit.isLimit (parallelPair g.unop 0)) i.unop.hom rfl)
        ¬∑ dsimp
          simp only [‚Üê Iso.eq_inv_comp, limit.conePointUniqueUpToIso_inv_comp,
            Fork.ofŒπ_œÄ_app])
  monotone' :=
    Subobject.ind‚ÇÇ _ <| by
      intro A B f g hf hg h
      dsimp only [Subobject.lift_mk]
      refine Subobject.mk_le_mk_of_comm (kernel.lift g.unop (kernel.Œπ f.unop) ?_) ?_
      ¬∑ rw [‚Üê Subobject.ofMkLEMk_comp h, unop_comp, kernel.condition_assoc, zero_comp]
      ¬∑ exact Quiver.Hom.op_inj (by simp)

end Kernel

section Image

variable (f : X ‚ü∂ Y) [HasImage f]

/-- The image of a morphism `f g : X ‚ü∂ Y` as a `Subobject Y`. -/
abbrev imageSubobject : Subobject Y :=
  Subobject.mk (image.Œπ f)

/-- The underlying object of `imageSubobject f` is (up to isomorphism!)
the same as the chosen object `image f`. -/
def imageSubobjectIso : (imageSubobject f : C) ‚âÖ image f :=
  Subobject.underlyingIso (image.Œπ f)

@[reassoc (attr := simp)]
theorem imageSubobject_arrow :
    (imageSubobjectIso f).hom ‚â´ image.Œπ f = (imageSubobject f).arrow := by simp [imageSubobjectIso]

@[reassoc (attr := simp)]
theorem imageSubobject_arrow' :
    (imageSubobjectIso f).inv ‚â´ (imageSubobject f).arrow = image.Œπ f := by simp [imageSubobjectIso]

/-- A factorisation of `f : X ‚ü∂ Y` through `imageSubobject f`. -/
def factorThruImageSubobject : X ‚ü∂ imageSubobject f :=
  factorThruImage f ‚â´ (imageSubobjectIso f).inv

instance [HasEqualizers C] : Epi (factorThruImageSubobject f) := by
  dsimp [factorThruImageSubobject]
  apply epi_comp

@[reassoc (attr := simp), elementwise (attr := simp)]
theorem imageSubobject_arrow_comp : factorThruImageSubobject f ‚â´ (imageSubobject f).arrow = f := by
  simp [factorThruImageSubobject, imageSubobject_arrow]

theorem imageSubobject_arrow_comp_eq_zero [HasZeroMorphisms C] {X Y Z : C} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z}
    [HasImage f] [Epi (factorThruImageSubobject f)] (h : f ‚â´ g = 0) :
    (imageSubobject f).arrow ‚â´ g = 0 :=
  zero_of_epi_comp (factorThruImageSubobject f) <| by simp [h]

theorem imageSubobject_factors_comp_self {W : C} (k : W ‚ü∂ X) : (imageSubobject f).Factors (k ‚â´ f) :=
  ‚ü®k ‚â´ factorThruImage f, by simp‚ü©

@[simp]
theorem factorThruImageSubobject_comp_self {W : C} (k : W ‚ü∂ X) (h) :
    (imageSubobject f).factorThru (k ‚â´ f) h = k ‚â´ factorThruImageSubobject f := by
  ext
  simp

@[simp]
theorem factorThruImageSubobject_comp_self_assoc {W W' : C} (k : W ‚ü∂ W') (k' : W' ‚ü∂ X) (h) :
    (imageSubobject f).factorThru (k ‚â´ k' ‚â´ f) h = k ‚â´ k' ‚â´ factorThruImageSubobject f := by
  ext
  simp

/-- The image of `h ‚â´ f` is always a smaller subobject than the image of `f`. -/
theorem imageSubobject_comp_le {X' : C} (h : X' ‚ü∂ X) (f : X ‚ü∂ Y) [HasImage f] [HasImage (h ‚â´ f)] :
    imageSubobject (h ‚â´ f) ‚â§ imageSubobject f :=
  Subobject.mk_le_mk_of_comm (image.preComp h f) (by simp)

section

open ZeroObject

variable [HasZeroMorphisms C] [HasZeroObject C]

@[simp]
theorem imageSubobject_zero_arrow : (imageSubobject (0 : X ‚ü∂ Y)).arrow = 0 := by
  rw [‚Üê imageSubobject_arrow]
  simp

@[simp]
theorem imageSubobject_zero {A B : C} : imageSubobject (0 : A ‚ü∂ B) = ‚ä• :=
  Subobject.eq_of_comm (imageSubobjectIso _ ‚â™‚â´ imageZero ‚â™‚â´ Subobject.botCoeIsoZero.symm) (by simp)

end

section

variable [HasEqualizers C]

/-- The morphism `imageSubobject (h ‚â´ f) ‚ü∂ imageSubobject f`
is an epimorphism when `h` is an epimorphism.
In general this does not imply that `imageSubobject (h ‚â´ f) = imageSubobject f`,
although it will when the ambient category is abelian.
-/
instance imageSubobject_comp_le_epi_of_epi {X' : C} (h : X' ‚ü∂ X) [Epi h] (f : X ‚ü∂ Y) [HasImage f]
    [HasImage (h ‚â´ f)] : Epi (Subobject.ofLE _ _ (imageSubobject_comp_le h f)) := by
  rw [ofLE_mk_le_mk_of_comm (image.preComp h f)]
  ¬∑ infer_instance
  ¬∑ simp

end

section

variable [HasEqualizers C]

/-- Postcomposing by an isomorphism gives an isomorphism between image subobjects. -/
def imageSubobjectCompIso (f : X ‚ü∂ Y) [HasImage f] {Y' : C} (h : Y ‚ü∂ Y') [IsIso h] :
    (imageSubobject (f ‚â´ h) : C) ‚âÖ (imageSubobject f : C) :=
  imageSubobjectIso _ ‚â™‚â´ (image.compIso _ _).symm ‚â™‚â´ (imageSubobjectIso _).symm

@[reassoc (attr := simp)]
theorem imageSubobjectCompIso_hom_arrow (f : X ‚ü∂ Y) [HasImage f] {Y' : C} (h : Y ‚ü∂ Y') [IsIso h] :
    (imageSubobjectCompIso f h).hom ‚â´ (imageSubobject f).arrow =
      (imageSubobject (f ‚â´ h)).arrow ‚â´ inv h := by
  simp [imageSubobjectCompIso]

@[reassoc (attr := simp)]
theorem imageSubobjectCompIso_inv_arrow (f : X ‚ü∂ Y) [HasImage f] {Y' : C} (h : Y ‚ü∂ Y') [IsIso h] :
    (imageSubobjectCompIso f h).inv ‚â´ (imageSubobject (f ‚â´ h)).arrow =
      (imageSubobject f).arrow ‚â´ h := by
  simp [imageSubobjectCompIso]

end

theorem imageSubobject_mono (f : X ‚ü∂ Y) [Mono f] : imageSubobject f = Subobject.mk f :=
  eq_of_comm (imageSubobjectIso f ‚â™‚â´ imageMonoIsoSource f ‚â™‚â´ (underlyingIso f).symm) (by simp)

/-- Precomposing by an isomorphism does not change the image subobject. -/
theorem imageSubobject_iso_comp [HasEqualizers C] {X' : C} (h : X' ‚ü∂ X) [IsIso h] (f : X ‚ü∂ Y)
    [HasImage f] : imageSubobject (h ‚â´ f) = imageSubobject f :=
  le_antisymm (imageSubobject_comp_le h f)
    (Subobject.mk_le_mk_of_comm (inv (image.preComp h f)) (by simp))

theorem imageSubobject_le {A B : C} {X : Subobject B} (f : A ‚ü∂ B) [HasImage f] (h : A ‚ü∂ X)
    (w : h ‚â´ X.arrow = f) : imageSubobject f ‚â§ X :=
  Subobject.le_of_comm
    ((imageSubobjectIso f).hom ‚â´
      image.lift
        { I := (X : C)
          e := h
          m := X.arrow })
    (by rw [assoc, image.lift_fac, imageSubobject_arrow])

theorem imageSubobject_le_mk {A B : C} {X : C} (g : X ‚ü∂ B) [Mono g] (f : A ‚ü∂ B) [HasImage f]
    (h : A ‚ü∂ X) (w : h ‚â´ g = f) : imageSubobject f ‚â§ Subobject.mk g :=
  imageSubobject_le f (h ‚â´ (Subobject.underlyingIso g).inv) (by simp [w])

/-- Given a commutative square between morphisms `f` and `g`,
we have a morphism in the category from `imageSubobject f` to `imageSubobject g`. -/
def imageSubobjectMap {W X Y Z : C} {f : W ‚ü∂ X} [HasImage f] {g : Y ‚ü∂ Z} [HasImage g]
    (sq : Arrow.mk f ‚ü∂ Arrow.mk g) [HasImageMap sq] :
    (imageSubobject f : C) ‚ü∂ (imageSubobject g : C) :=
  (imageSubobjectIso f).hom ‚â´ image.map sq ‚â´ (imageSubobjectIso g).inv

@[reassoc (attr := simp)]
theorem imageSubobjectMap_arrow {W X Y Z : C} {f : W ‚ü∂ X} [HasImage f] {g : Y ‚ü∂ Z} [HasImage g]
    (sq : Arrow.mk f ‚ü∂ Arrow.mk g) [HasImageMap sq] :
    imageSubobjectMap sq ‚â´ (imageSubobject g).arrow = (imageSubobject f).arrow ‚â´ sq.right := by
  simp only [imageSubobjectMap, Category.assoc, imageSubobject_arrow']
  erw [image.map_Œπ, ‚Üê Category.assoc, imageSubobject_arrow]

theorem image_map_comp_imageSubobjectIso_inv {W X Y Z : C} {f : W ‚ü∂ X} [HasImage f] {g : Y ‚ü∂ Z}
    [HasImage g] (sq : Arrow.mk f ‚ü∂ Arrow.mk g) [HasImageMap sq] :
    image.map sq ‚â´ (imageSubobjectIso _).inv =
      (imageSubobjectIso _).inv ‚â´ imageSubobjectMap sq := by
  ext
  simpa using image.map_Œπ sq

theorem imageSubobjectIso_comp_image_map {W X Y Z : C} {f : W ‚ü∂ X} [HasImage f] {g : Y ‚ü∂ Z}
    [HasImage g] (sq : Arrow.mk f ‚ü∂ Arrow.mk g) [HasImageMap sq] :
    (imageSubobjectIso _).hom ‚â´ image.map sq =
      imageSubobjectMap sq ‚â´ (imageSubobjectIso _).hom := by
  erw [‚Üê Iso.comp_inv_eq, Category.assoc, ‚Üê (imageSubobjectIso f).eq_inv_comp,
    image_map_comp_imageSubobjectIso_inv sq]

end Image

end Limits

end CategoryTheory
