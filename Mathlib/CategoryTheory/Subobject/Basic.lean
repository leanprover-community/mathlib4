/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Scott Morrison
-/
import Mathlib.CategoryTheory.Subobject.MonoOver
import Mathlib.CategoryTheory.Skeletal
import Mathlib.CategoryTheory.ConcreteCategory.Basic
import Mathlib.Tactic.ApplyFun
import Mathlib.Tactic.CategoryTheory.Elementwise

#align_import category_theory.subobject.basic from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# Subobjects

We define `Subobject X` as the quotient (by isomorphisms) of
`MonoOver X := {f : Over X // Mono f.hom}`.

Here `MonoOver X` is a thin category (a pair of objects has at most one morphism between them),
so we can think of it as a preorder. However as it is not skeletal, it is not a partial order.

There is a coercion from `Subobject X` back to the ambient category `C`
(using choice to pick a representative), and for `P : Subobject X`,
`P.arrow : (P : C) âŸ¶ X` is the inclusion morphism.

We provide
* `def pullback [HasPullbacks C] (f : X âŸ¶ Y) : Subobject Y â¥¤ Subobject X`
* `def map (f : X âŸ¶ Y) [Mono f] : Subobject X â¥¤ Subobject Y`
* `def Â«exists_Â» [HasImages C] (f : X âŸ¶ Y) : Subobject X â¥¤ Subobject Y`
and prove their basic properties and relationships.
These are all easy consequences of the earlier development
of the corresponding functors for `MonoOver`.

The subobjects of `X` form a preorder making them into a category. We have `X â‰¤ Y` if and only if
`X.arrow` factors through `Y.arrow`: see `ofLE`/`ofLEMk`/`ofMkLE`/`ofMkLEMk` and
`le_of_comm`. Similarly, to show that two subobjects are equal, we can supply an isomorphism between
the underlying objects that commutes with the arrows (`eq_of_comm`).

See also

* `CategoryTheory.Subobject.factorThru` :
  an API describing factorization of morphisms through subobjects.
* `CategoryTheory.Subobject.lattice` :
  the lattice structures on subobjects.

## Notes

This development originally appeared in Bhavik Mehta's "Topos theory for Lean" repository,
and was ported to mathlib by Scott Morrison.

### Implementation note

Currently we describe `pullback`, `map`, etc., as functors.
It may be better to just say that they are monotone functions,
and even avoid using categorical language entirely when describing `Subobject X`.
(It's worth keeping this in mind in future use; it should be a relatively easy change here
if it looks preferable.)

### Relation to pseudoelements

There is a separate development of pseudoelements in `CategoryTheory.Abelian.Pseudoelements`,
as a quotient (but not by isomorphism) of `Over X`.

When a morphism `f` has an image, the image represents the same pseudoelement.
In a category with images `Pseudoelements X` could be constructed as a quotient of `MonoOver X`.
In fact, in an abelian category (I'm not sure in what generality beyond that),
`Pseudoelements X` agrees with `Subobject X`, but we haven't developed this in mathlib yet.

-/


universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

noncomputable section

namespace CategoryTheory

open CategoryTheory CategoryTheory.Category CategoryTheory.Limits

variable {C : Type uâ‚} [Category.{vâ‚} C] {X Y Z : C}
variable {D : Type uâ‚‚} [Category.{vâ‚‚} D]

/-!
We now construct the subobject lattice for `X : C`,
as the quotient by isomorphisms of `MonoOver X`.

Since `MonoOver X` is a thin category, we use `ThinSkeleton` to take the quotient.

Essentially all the structure defined above on `MonoOver X` descends to `Subobject X`,
with morphisms becoming inequalities, and isomorphisms becoming equations.
-/


/-- The category of subobjects of `X : C`, defined as isomorphism classes of monomorphisms into `X`.
-/
def Subobject (X : C) :=
  ThinSkeleton (MonoOver X)
#align category_theory.subobject CategoryTheory.Subobject

instance (X : C) : PartialOrder (Subobject X) := by
  dsimp only [Subobject]
  infer_instance

namespace Subobject

-- Porting note: made it a def rather than an abbreviation
-- because Lean would make it too transparent
/-- Convenience constructor for a subobject. -/
def mk {X A : C} (f : A âŸ¶ X) [Mono f] : Subobject X :=
  (toThinSkeleton _).obj (MonoOver.mk' f)
#align category_theory.subobject.mk CategoryTheory.Subobject.mk

section

attribute [local ext] CategoryTheory.Comma

protected theorem ind {X : C} (p : Subobject X â†’ Prop)
    (h : âˆ€ â¦ƒA : Câ¦„ (f : A âŸ¶ X) [Mono f], p (Subobject.mk f)) (P : Subobject X) : p P := by
  apply Quotient.inductionOn'
  intro a
  exact h a.arrow
#align category_theory.subobject.ind CategoryTheory.Subobject.ind

protected theorem indâ‚‚ {X : C} (p : Subobject X â†’ Subobject X â†’ Prop)
    (h : âˆ€ â¦ƒA B : Câ¦„ (f : A âŸ¶ X) (g : B âŸ¶ X) [Mono f] [Mono g],
      p (Subobject.mk f) (Subobject.mk g))
    (P Q : Subobject X) : p P Q := by
  apply Quotient.inductionOnâ‚‚'
  intro a b
  exact h a.arrow b.arrow
#align category_theory.subobject.indâ‚‚ CategoryTheory.Subobject.indâ‚‚

end

/-- Declare a function on subobjects of `X` by specifying a function on monomorphisms with
    codomain `X`. -/
protected def lift {Î± : Sort*} {X : C} (F : âˆ€ â¦ƒA : Câ¦„ (f : A âŸ¶ X) [Mono f], Î±)
    (h :
      âˆ€ â¦ƒA B : Câ¦„ (f : A âŸ¶ X) (g : B âŸ¶ X) [Mono f] [Mono g] (i : A â‰… B),
        i.hom â‰« g = f â†’ F f = F g) :
    Subobject X â†’ Î± := fun P =>
  Quotient.liftOn' P (fun m => F m.arrow) fun m n âŸ¨iâŸ© =>
    h m.arrow n.arrow ((MonoOver.forget X â‹™ Over.forget X).mapIso i) (Over.w i.hom)
#align category_theory.subobject.lift CategoryTheory.Subobject.lift

@[simp]
protected theorem lift_mk {Î± : Sort*} {X : C} (F : âˆ€ â¦ƒA : Câ¦„ (f : A âŸ¶ X) [Mono f], Î±) {h A}
    (f : A âŸ¶ X) [Mono f] : Subobject.lift F h (Subobject.mk f) = F f :=
  rfl
#align category_theory.subobject.lift_mk CategoryTheory.Subobject.lift_mk

/-- The category of subobjects is equivalent to the `MonoOver` category. It is more convenient to
use the former due to the partial order instance, but oftentimes it is easier to define structures
on the latter. -/
noncomputable def equivMonoOver (X : C) : Subobject X â‰Œ MonoOver X :=
  ThinSkeleton.equivalence _
#align category_theory.subobject.equiv_mono_over CategoryTheory.Subobject.equivMonoOver

/-- Use choice to pick a representative `MonoOver X` for each `Subobject X`.
-/
noncomputable def representative {X : C} : Subobject X â¥¤ MonoOver X :=
  (equivMonoOver X).functor
#align category_theory.subobject.representative CategoryTheory.Subobject.representative

/-- Starting with `A : MonoOver X`, we can take its equivalence class in `Subobject X`
then pick an arbitrary representative using `representative.obj`.
This is isomorphic (in `MonoOver X`) to the original `A`.
-/
noncomputable def representativeIso {X : C} (A : MonoOver X) :
    representative.obj ((toThinSkeleton _).obj A) â‰… A :=
  (equivMonoOver X).counitIso.app A
#align category_theory.subobject.representative_iso CategoryTheory.Subobject.representativeIso

/-- Use choice to pick a representative underlying object in `C` for any `Subobject X`.

Prefer to use the coercion `P : C` rather than explicitly writing `underlying.obj P`.
-/
noncomputable def underlying {X : C} : Subobject X â¥¤ C :=
  representative â‹™ MonoOver.forget _ â‹™ Over.forget _
#align category_theory.subobject.underlying CategoryTheory.Subobject.underlying

instance : CoeOut (Subobject X) C where coe Y := underlying.obj Y

-- Porting note: removed as it has become a syntactic tautology
-- @[simp]
-- theorem underlying_as_coe {X : C} (P : Subobject X) : underlying.obj P = P :=
--   rfl
-- #align category_theory.subobject.underlying_as_coe CategoryTheory.Subobject.underlying_as_coe

/-- If we construct a `Subobject Y` from an explicit `f : X âŸ¶ Y` with `[Mono f]`,
then pick an arbitrary choice of underlying object `(Subobject.mk f : C)` back in `C`,
it is isomorphic (in `C`) to the original `X`.
-/
noncomputable def underlyingIso {X Y : C} (f : X âŸ¶ Y) [Mono f] : (Subobject.mk f : C) â‰… X :=
  (MonoOver.forget _ â‹™ Over.forget _).mapIso (representativeIso (MonoOver.mk' f))
#align category_theory.subobject.underlying_iso CategoryTheory.Subobject.underlyingIso

/-- The morphism in `C` from the arbitrarily chosen underlying object to the ambient object.
-/
noncomputable def arrow {X : C} (Y : Subobject X) : (Y : C) âŸ¶ X :=
  (representative.obj Y).obj.hom
#align category_theory.subobject.arrow CategoryTheory.Subobject.arrow

instance arrow_mono {X : C} (Y : Subobject X) : Mono Y.arrow :=
  (representative.obj Y).property
#align category_theory.subobject.arrow_mono CategoryTheory.Subobject.arrow_mono

@[simp]
theorem arrow_congr {A : C} (X Y : Subobject A) (h : X = Y) :
    eqToHom (congr_arg (fun X : Subobject A => (X : C)) h) â‰« Y.arrow = X.arrow := by
  induction h
  simp
#align category_theory.subobject.arrow_congr CategoryTheory.Subobject.arrow_congr

@[simp]
theorem representative_coe (Y : Subobject X) : (representative.obj Y : C) = (Y : C) :=
  rfl
#align category_theory.subobject.representative_coe CategoryTheory.Subobject.representative_coe

@[simp]
theorem representative_arrow (Y : Subobject X) : (representative.obj Y).arrow = Y.arrow :=
  rfl
#align category_theory.subobject.representative_arrow CategoryTheory.Subobject.representative_arrow

@[reassoc (attr := simp)]
theorem underlying_arrow {X : C} {Y Z : Subobject X} (f : Y âŸ¶ Z) :
    underlying.map f â‰« arrow Z = arrow Y :=
  Over.w (representative.map f)
#align category_theory.subobject.underlying_arrow CategoryTheory.Subobject.underlying_arrow

@[reassoc (attr := simp), elementwise (attr := simp)]
theorem underlyingIso_arrow {X Y : C} (f : X âŸ¶ Y) [Mono f] :
    (underlyingIso f).inv â‰« (Subobject.mk f).arrow = f :=
  Over.w _
#align category_theory.subobject.underlying_iso_arrow CategoryTheory.Subobject.underlyingIso_arrow

@[reassoc (attr := simp)]
theorem underlyingIso_hom_comp_eq_mk {X Y : C} (f : X âŸ¶ Y) [Mono f] :
    (underlyingIso f).hom â‰« f = (mk f).arrow :=
  (Iso.eq_inv_comp _).1 (underlyingIso_arrow f).symm
#align category_theory.subobject.underlying_iso_hom_comp_eq_mk CategoryTheory.Subobject.underlyingIso_hom_comp_eq_mk

/-- Two morphisms into a subobject are equal exactly if
the morphisms into the ambient object are equal -/
@[ext]
theorem eq_of_comp_arrow_eq {X Y : C} {P : Subobject Y} {f g : X âŸ¶ P}
    (h : f â‰« P.arrow = g â‰« P.arrow) : f = g :=
  (cancel_mono P.arrow).mp h
#align category_theory.subobject.eq_of_comp_arrow_eq CategoryTheory.Subobject.eq_of_comp_arrow_eq

theorem mk_le_mk_of_comm {B Aâ‚ Aâ‚‚ : C} {fâ‚ : Aâ‚ âŸ¶ B} {fâ‚‚ : Aâ‚‚ âŸ¶ B} [Mono fâ‚] [Mono fâ‚‚] (g : Aâ‚ âŸ¶ Aâ‚‚)
    (w : g â‰« fâ‚‚ = fâ‚) : mk fâ‚ â‰¤ mk fâ‚‚ :=
  âŸ¨MonoOver.homMk _ wâŸ©
#align category_theory.subobject.mk_le_mk_of_comm CategoryTheory.Subobject.mk_le_mk_of_comm

@[simp]
theorem mk_arrow (P : Subobject X) : mk P.arrow = P :=
  Quotient.inductionOn' P fun Q => by
    obtain âŸ¨eâŸ© := @Quotient.mk_out' _ (isIsomorphicSetoid _) Q
    exact Quotient.sound' âŸ¨MonoOver.isoMk (Iso.refl _) â‰ªâ‰« eâŸ©
#align category_theory.subobject.mk_arrow CategoryTheory.Subobject.mk_arrow

theorem le_of_comm {B : C} {X Y : Subobject B} (f : (X : C) âŸ¶ (Y : C)) (w : f â‰« Y.arrow = X.arrow) :
    X â‰¤ Y := by
  convert mk_le_mk_of_comm _ w <;> simp
#align category_theory.subobject.le_of_comm CategoryTheory.Subobject.le_of_comm

theorem le_mk_of_comm {B A : C} {X : Subobject B} {f : A âŸ¶ B} [Mono f] (g : (X : C) âŸ¶ A)
    (w : g â‰« f = X.arrow) : X â‰¤ mk f :=
  le_of_comm (g â‰« (underlyingIso f).inv) <| by simp [w]
#align category_theory.subobject.le_mk_of_comm CategoryTheory.Subobject.le_mk_of_comm

theorem mk_le_of_comm {B A : C} {X : Subobject B} {f : A âŸ¶ B} [Mono f] (g : A âŸ¶ (X : C))
    (w : g â‰« X.arrow = f) : mk f â‰¤ X :=
  le_of_comm ((underlyingIso f).hom â‰« g) <| by simp [w]
#align category_theory.subobject.mk_le_of_comm CategoryTheory.Subobject.mk_le_of_comm

/-- To show that two subobjects are equal, it suffices to exhibit an isomorphism commuting with
    the arrows. -/
@[ext]
theorem eq_of_comm {B : C} {X Y : Subobject B} (f : (X : C) â‰… (Y : C))
    (w : f.hom â‰« Y.arrow = X.arrow) : X = Y :=
  le_antisymm (le_of_comm f.hom w) <| le_of_comm f.inv <| f.inv_comp_eq.2 w.symm
#align category_theory.subobject.eq_of_comm CategoryTheory.Subobject.eq_of_comm

-- Porting note (#11182): removed @[ext]
/-- To show that two subobjects are equal, it suffices to exhibit an isomorphism commuting with
    the arrows. -/
theorem eq_mk_of_comm {B A : C} {X : Subobject B} (f : A âŸ¶ B) [Mono f] (i : (X : C) â‰… A)
    (w : i.hom â‰« f = X.arrow) : X = mk f :=
  eq_of_comm (i.trans (underlyingIso f).symm) <| by simp [w]
#align category_theory.subobject.eq_mk_of_comm CategoryTheory.Subobject.eq_mk_of_comm

-- Porting note (#11182): removed @[ext]
/-- To show that two subobjects are equal, it suffices to exhibit an isomorphism commuting with
    the arrows. -/
theorem mk_eq_of_comm {B A : C} {X : Subobject B} (f : A âŸ¶ B) [Mono f] (i : A â‰… (X : C))
    (w : i.hom â‰« X.arrow = f) : mk f = X :=
  Eq.symm <| eq_mk_of_comm _ i.symm <| by rw [Iso.symm_hom, Iso.inv_comp_eq, w]
#align category_theory.subobject.mk_eq_of_comm CategoryTheory.Subobject.mk_eq_of_comm

-- Porting note (#11182): removed @[ext]
/-- To show that two subobjects are equal, it suffices to exhibit an isomorphism commuting with
    the arrows. -/
theorem mk_eq_mk_of_comm {B Aâ‚ Aâ‚‚ : C} (f : Aâ‚ âŸ¶ B) (g : Aâ‚‚ âŸ¶ B) [Mono f] [Mono g] (i : Aâ‚ â‰… Aâ‚‚)
    (w : i.hom â‰« g = f) : mk f = mk g :=
  eq_mk_of_comm _ ((underlyingIso f).trans i) <| by simp [w]
#align category_theory.subobject.mk_eq_mk_of_comm CategoryTheory.Subobject.mk_eq_mk_of_comm

-- We make `X` and `Y` explicit arguments here so that when `ofLE` appears in goal statements
-- it is possible to see its source and target
-- (`h` will just display as `_`, because it is in `Prop`).
/-- An inequality of subobjects is witnessed by some morphism between the corresponding objects. -/
def ofLE {B : C} (X Y : Subobject B) (h : X â‰¤ Y) : (X : C) âŸ¶ (Y : C) :=
  underlying.map <| h.hom
#align category_theory.subobject.of_le CategoryTheory.Subobject.ofLE

@[reassoc (attr := simp)]
theorem ofLE_arrow {B : C} {X Y : Subobject B} (h : X â‰¤ Y) : ofLE X Y h â‰« Y.arrow = X.arrow :=
  underlying_arrow _
#align category_theory.subobject.of_le_arrow CategoryTheory.Subobject.ofLE_arrow

instance {B : C} (X Y : Subobject B) (h : X â‰¤ Y) : Mono (ofLE X Y h) := by
  fconstructor
  intro Z f g w
  replace w := w =â‰« Y.arrow
  ext
  simpa using w

theorem ofLE_mk_le_mk_of_comm {B Aâ‚ Aâ‚‚ : C} {fâ‚ : Aâ‚ âŸ¶ B} {fâ‚‚ : Aâ‚‚ âŸ¶ B} [Mono fâ‚] [Mono fâ‚‚]
    (g : Aâ‚ âŸ¶ Aâ‚‚) (w : g â‰« fâ‚‚ = fâ‚) :
    ofLE _ _ (mk_le_mk_of_comm g w) = (underlyingIso _).hom â‰« g â‰« (underlyingIso _).inv := by
  ext
  simp [w]
#align category_theory.subobject.of_le_mk_le_mk_of_comm CategoryTheory.Subobject.ofLE_mk_le_mk_of_comm

/-- An inequality of subobjects is witnessed by some morphism between the corresponding objects. -/
def ofLEMk {B A : C} (X : Subobject B) (f : A âŸ¶ B) [Mono f] (h : X â‰¤ mk f) : (X : C) âŸ¶ A :=
  ofLE X (mk f) h â‰« (underlyingIso f).hom
#align category_theory.subobject.of_le_mk CategoryTheory.Subobject.ofLEMk

instance {B A : C} (X : Subobject B) (f : A âŸ¶ B) [Mono f] (h : X â‰¤ mk f) :
    Mono (ofLEMk X f h) := by
  dsimp only [ofLEMk]
  infer_instance

@[simp]
theorem ofLEMk_comp {B A : C} {X : Subobject B} {f : A âŸ¶ B} [Mono f] (h : X â‰¤ mk f) :
    ofLEMk X f h â‰« f = X.arrow := by simp [ofLEMk]
#align category_theory.subobject.of_le_mk_comp CategoryTheory.Subobject.ofLEMk_comp

/-- An inequality of subobjects is witnessed by some morphism between the corresponding objects. -/
def ofMkLE {B A : C} (f : A âŸ¶ B) [Mono f] (X : Subobject B) (h : mk f â‰¤ X) : A âŸ¶ (X : C) :=
  (underlyingIso f).inv â‰« ofLE (mk f) X h
#align category_theory.subobject.of_mk_le CategoryTheory.Subobject.ofMkLE

instance {B A : C} (f : A âŸ¶ B) [Mono f] (X : Subobject B) (h : mk f â‰¤ X) :
    Mono (ofMkLE f X h) := by
  dsimp only [ofMkLE]
  infer_instance

@[simp]
theorem ofMkLE_arrow {B A : C} {f : A âŸ¶ B} [Mono f] {X : Subobject B} (h : mk f â‰¤ X) :
    ofMkLE f X h â‰« X.arrow = f := by simp [ofMkLE]
#align category_theory.subobject.of_mk_le_arrow CategoryTheory.Subobject.ofMkLE_arrow

/-- An inequality of subobjects is witnessed by some morphism between the corresponding objects. -/
def ofMkLEMk {B Aâ‚ Aâ‚‚ : C} (f : Aâ‚ âŸ¶ B) (g : Aâ‚‚ âŸ¶ B) [Mono f] [Mono g] (h : mk f â‰¤ mk g) :
    Aâ‚ âŸ¶ Aâ‚‚ :=
  (underlyingIso f).inv â‰« ofLE (mk f) (mk g) h â‰« (underlyingIso g).hom
#align category_theory.subobject.of_mk_le_mk CategoryTheory.Subobject.ofMkLEMk

instance {B Aâ‚ Aâ‚‚ : C} (f : Aâ‚ âŸ¶ B) (g : Aâ‚‚ âŸ¶ B) [Mono f] [Mono g] (h : mk f â‰¤ mk g) :
    Mono (ofMkLEMk f g h) := by
  dsimp only [ofMkLEMk]
  infer_instance

@[simp]
theorem ofMkLEMk_comp {B Aâ‚ Aâ‚‚ : C} {f : Aâ‚ âŸ¶ B} {g : Aâ‚‚ âŸ¶ B} [Mono f] [Mono g] (h : mk f â‰¤ mk g) :
    ofMkLEMk f g h â‰« g = f := by simp [ofMkLEMk]
#align category_theory.subobject.of_mk_le_mk_comp CategoryTheory.Subobject.ofMkLEMk_comp

@[reassoc (attr := simp)]
theorem ofLE_comp_ofLE {B : C} (X Y Z : Subobject B) (hâ‚ : X â‰¤ Y) (hâ‚‚ : Y â‰¤ Z) :
    ofLE X Y hâ‚ â‰« ofLE Y Z hâ‚‚ = ofLE X Z (hâ‚.trans hâ‚‚) := by
  simp only [ofLE, â† Functor.map_comp underlying]
  congr 1
#align category_theory.subobject.of_le_comp_of_le CategoryTheory.Subobject.ofLE_comp_ofLE

@[reassoc (attr := simp)]
theorem ofLE_comp_ofLEMk {B A : C} (X Y : Subobject B) (f : A âŸ¶ B) [Mono f] (hâ‚ : X â‰¤ Y)
    (hâ‚‚ : Y â‰¤ mk f) : ofLE X Y hâ‚ â‰« ofLEMk Y f hâ‚‚ = ofLEMk X f (hâ‚.trans hâ‚‚) := by
  simp only [ofMkLE, ofLEMk, ofLE, â† Functor.map_comp_assoc underlying]
  congr 1
#align category_theory.subobject.of_le_comp_of_le_mk CategoryTheory.Subobject.ofLE_comp_ofLEMk

@[reassoc (attr := simp)]
theorem ofLEMk_comp_ofMkLE {B A : C} (X : Subobject B) (f : A âŸ¶ B) [Mono f] (Y : Subobject B)
    (hâ‚ : X â‰¤ mk f) (hâ‚‚ : mk f â‰¤ Y) : ofLEMk X f hâ‚ â‰« ofMkLE f Y hâ‚‚ = ofLE X Y (hâ‚.trans hâ‚‚) := by
  simp only [ofMkLE, ofLEMk, ofLE, â† Functor.map_comp underlying, assoc, Iso.hom_inv_id_assoc]
  congr 1
#align category_theory.subobject.of_le_mk_comp_of_mk_le CategoryTheory.Subobject.ofLEMk_comp_ofMkLE

@[reassoc (attr := simp)]
theorem ofLEMk_comp_ofMkLEMk {B Aâ‚ Aâ‚‚ : C} (X : Subobject B) (f : Aâ‚ âŸ¶ B) [Mono f] (g : Aâ‚‚ âŸ¶ B)
    [Mono g] (hâ‚ : X â‰¤ mk f) (hâ‚‚ : mk f â‰¤ mk g) :
    ofLEMk X f hâ‚ â‰« ofMkLEMk f g hâ‚‚ = ofLEMk X g (hâ‚.trans hâ‚‚) := by
  simp only [ofMkLE, ofLEMk, ofLE, ofMkLEMk, â† Functor.map_comp_assoc underlying,
    assoc, Iso.hom_inv_id_assoc]
  congr 1
#align category_theory.subobject.of_le_mk_comp_of_mk_le_mk CategoryTheory.Subobject.ofLEMk_comp_ofMkLEMk

@[reassoc (attr := simp)]
theorem ofMkLE_comp_ofLE {B Aâ‚ : C} (f : Aâ‚ âŸ¶ B) [Mono f] (X Y : Subobject B) (hâ‚ : mk f â‰¤ X)
    (hâ‚‚ : X â‰¤ Y) : ofMkLE f X hâ‚ â‰« ofLE X Y hâ‚‚ = ofMkLE f Y (hâ‚.trans hâ‚‚) := by
  simp only [ofMkLE, ofLEMk, ofLE, ofMkLEMk, â† Functor.map_comp underlying,
    assoc]
  congr 1
#align category_theory.subobject.of_mk_le_comp_of_le CategoryTheory.Subobject.ofMkLE_comp_ofLE

@[reassoc (attr := simp)]
theorem ofMkLE_comp_ofLEMk {B Aâ‚ Aâ‚‚ : C} (f : Aâ‚ âŸ¶ B) [Mono f] (X : Subobject B) (g : Aâ‚‚ âŸ¶ B)
    [Mono g] (hâ‚ : mk f â‰¤ X) (hâ‚‚ : X â‰¤ mk g) :
    ofMkLE f X hâ‚ â‰« ofLEMk X g hâ‚‚ = ofMkLEMk f g (hâ‚.trans hâ‚‚) := by
  simp only [ofMkLE, ofLEMk, ofLE, ofMkLEMk, â† Functor.map_comp_assoc underlying, assoc]
  congr 1
#align category_theory.subobject.of_mk_le_comp_of_le_mk CategoryTheory.Subobject.ofMkLE_comp_ofLEMk

@[reassoc (attr := simp)]
theorem ofMkLEMk_comp_ofMkLE {B Aâ‚ Aâ‚‚ : C} (f : Aâ‚ âŸ¶ B) [Mono f] (g : Aâ‚‚ âŸ¶ B) [Mono g]
    (X : Subobject B) (hâ‚ : mk f â‰¤ mk g) (hâ‚‚ : mk g â‰¤ X) :
    ofMkLEMk f g hâ‚ â‰« ofMkLE g X hâ‚‚ = ofMkLE f X (hâ‚.trans hâ‚‚) := by
  simp only [ofMkLE, ofLEMk, ofLE, ofMkLEMk, â† Functor.map_comp underlying,
    assoc, Iso.hom_inv_id_assoc]
  congr 1
#align category_theory.subobject.of_mk_le_mk_comp_of_mk_le CategoryTheory.Subobject.ofMkLEMk_comp_ofMkLE

@[reassoc (attr := simp)]
theorem ofMkLEMk_comp_ofMkLEMk {B Aâ‚ Aâ‚‚ Aâ‚ƒ : C} (f : Aâ‚ âŸ¶ B) [Mono f] (g : Aâ‚‚ âŸ¶ B) [Mono g]
    (h : Aâ‚ƒ âŸ¶ B) [Mono h] (hâ‚ : mk f â‰¤ mk g) (hâ‚‚ : mk g â‰¤ mk h) :
    ofMkLEMk f g hâ‚ â‰« ofMkLEMk g h hâ‚‚ = ofMkLEMk f h (hâ‚.trans hâ‚‚) := by
  simp only [ofMkLE, ofLEMk, ofLE, ofMkLEMk, â† Functor.map_comp_assoc underlying, assoc,
    Iso.hom_inv_id_assoc]
  congr 1
#align category_theory.subobject.of_mk_le_mk_comp_of_mk_le_mk CategoryTheory.Subobject.ofMkLEMk_comp_ofMkLEMk

@[simp]
theorem ofLE_refl {B : C} (X : Subobject B) : ofLE X X le_rfl = ğŸ™ _ := by
  apply (cancel_mono X.arrow).mp
  simp
#align category_theory.subobject.of_le_refl CategoryTheory.Subobject.ofLE_refl

@[simp]
theorem ofMkLEMk_refl {B Aâ‚ : C} (f : Aâ‚ âŸ¶ B) [Mono f] : ofMkLEMk f f le_rfl = ğŸ™ _ := by
  apply (cancel_mono f).mp
  simp
#align category_theory.subobject.of_mk_le_mk_refl CategoryTheory.Subobject.ofMkLEMk_refl

-- As with `ofLE`, we have `X` and `Y` as explicit arguments for readability.
/-- An equality of subobjects gives an isomorphism of the corresponding objects.
(One could use `underlying.mapIso (eqToIso h))` here, but this is more readable.) -/
@[simps]
def isoOfEq {B : C} (X Y : Subobject B) (h : X = Y) : (X : C) â‰… (Y : C) where
  hom := ofLE _ _ h.le
  inv := ofLE _ _ h.ge
#align category_theory.subobject.iso_of_eq CategoryTheory.Subobject.isoOfEq

/-- An equality of subobjects gives an isomorphism of the corresponding objects. -/
@[simps]
def isoOfEqMk {B A : C} (X : Subobject B) (f : A âŸ¶ B) [Mono f] (h : X = mk f) : (X : C) â‰… A where
  hom := ofLEMk X f h.le
  inv := ofMkLE f X h.ge
#align category_theory.subobject.iso_of_eq_mk CategoryTheory.Subobject.isoOfEqMk

/-- An equality of subobjects gives an isomorphism of the corresponding objects. -/
@[simps]
def isoOfMkEq {B A : C} (f : A âŸ¶ B) [Mono f] (X : Subobject B) (h : mk f = X) : A â‰… (X : C) where
  hom := ofMkLE f X h.le
  inv := ofLEMk X f h.ge
#align category_theory.subobject.iso_of_mk_eq CategoryTheory.Subobject.isoOfMkEq

/-- An equality of subobjects gives an isomorphism of the corresponding objects. -/
@[simps]
def isoOfMkEqMk {B Aâ‚ Aâ‚‚ : C} (f : Aâ‚ âŸ¶ B) (g : Aâ‚‚ âŸ¶ B) [Mono f] [Mono g] (h : mk f = mk g) :
    Aâ‚ â‰… Aâ‚‚ where
  hom := ofMkLEMk f g h.le
  inv := ofMkLEMk g f h.ge
#align category_theory.subobject.iso_of_mk_eq_mk CategoryTheory.Subobject.isoOfMkEqMk

end Subobject

open CategoryTheory.Limits

namespace Subobject

/-- Any functor `MonoOver X â¥¤ MonoOver Y` descends to a functor
`Subobject X â¥¤ Subobject Y`, because `MonoOver Y` is thin. -/
def lower {Y : D} (F : MonoOver X â¥¤ MonoOver Y) : Subobject X â¥¤ Subobject Y :=
  ThinSkeleton.map F
#align category_theory.subobject.lower CategoryTheory.Subobject.lower

/-- Isomorphic functors become equal when lowered to `Subobject`.
(It's not as evil as usual to talk about equality between functors
because the categories are thin and skeletal.) -/
theorem lower_iso (Fâ‚ Fâ‚‚ : MonoOver X â¥¤ MonoOver Y) (h : Fâ‚ â‰… Fâ‚‚) : lower Fâ‚ = lower Fâ‚‚ :=
  ThinSkeleton.map_iso_eq h
#align category_theory.subobject.lower_iso CategoryTheory.Subobject.lower_iso

/-- A ternary version of `Subobject.lower`. -/
def lowerâ‚‚ (F : MonoOver X â¥¤ MonoOver Y â¥¤ MonoOver Z) : Subobject X â¥¤ Subobject Y â¥¤ Subobject Z :=
  ThinSkeleton.mapâ‚‚ F
#align category_theory.subobject.lowerâ‚‚ CategoryTheory.Subobject.lowerâ‚‚

@[simp]
theorem lower_comm (F : MonoOver Y â¥¤ MonoOver X) :
    toThinSkeleton _ â‹™ lower F = F â‹™ toThinSkeleton _ :=
  rfl
#align category_theory.subobject.lower_comm CategoryTheory.Subobject.lower_comm

/-- An adjunction between `MonoOver A` and `MonoOver B` gives an adjunction
between `Subobject A` and `Subobject B`. -/
def lowerAdjunction {A : C} {B : D} {L : MonoOver A â¥¤ MonoOver B} {R : MonoOver B â¥¤ MonoOver A}
    (h : L âŠ£ R) : lower L âŠ£ lower R :=
  ThinSkeleton.lowerAdjunction _ _ h
#align category_theory.subobject.lower_adjunction CategoryTheory.Subobject.lowerAdjunction

/-- An equivalence between `MonoOver A` and `MonoOver B` gives an equivalence
between `Subobject A` and `Subobject B`. -/
@[simps]
def lowerEquivalence {A : C} {B : D} (e : MonoOver A â‰Œ MonoOver B) : Subobject A â‰Œ Subobject B where
  functor := lower e.functor
  inverse := lower e.inverse
  unitIso := by
    apply eqToIso
    convert ThinSkeleton.map_iso_eq e.unitIso
    Â· exact ThinSkeleton.map_id_eq.symm
    Â· exact (ThinSkeleton.map_comp_eq _ _).symm
  counitIso := by
    apply eqToIso
    convert ThinSkeleton.map_iso_eq e.counitIso
    Â· exact (ThinSkeleton.map_comp_eq _ _).symm
    Â· exact ThinSkeleton.map_id_eq.symm
#align category_theory.subobject.lower_equivalence CategoryTheory.Subobject.lowerEquivalence

section Pullback

variable [HasPullbacks C]

/-- When `C` has pullbacks, a morphism `f : X âŸ¶ Y` induces a functor `Subobject Y â¥¤ Subobject X`,
by pulling back a monomorphism along `f`. -/
def pullback (f : X âŸ¶ Y) : Subobject Y â¥¤ Subobject X :=
  lower (MonoOver.pullback f)
#align category_theory.subobject.pullback CategoryTheory.Subobject.pullback

theorem pullback_id (x : Subobject X) : (pullback (ğŸ™ X)).obj x = x := by
  induction' x using Quotient.inductionOn' with f
  exact Quotient.sound âŸ¨MonoOver.pullbackId.app fâŸ©
#align category_theory.subobject.pullback_id CategoryTheory.Subobject.pullback_id

theorem pullback_comp (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (x : Subobject Z) :
    (pullback (f â‰« g)).obj x = (pullback f).obj ((pullback g).obj x) := by
  induction' x using Quotient.inductionOn' with t
  exact Quotient.sound âŸ¨(MonoOver.pullbackComp _ _).app tâŸ©
#align category_theory.subobject.pullback_comp CategoryTheory.Subobject.pullback_comp

instance (f : X âŸ¶ Y) : (pullback f).Faithful where

end Pullback

section Map

/-- We can map subobjects of `X` to subobjects of `Y`
by post-composition with a monomorphism `f : X âŸ¶ Y`.
-/
def map (f : X âŸ¶ Y) [Mono f] : Subobject X â¥¤ Subobject Y :=
  lower (MonoOver.map f)
#align category_theory.subobject.map CategoryTheory.Subobject.map

theorem map_id (x : Subobject X) : (map (ğŸ™ X)).obj x = x := by
  induction' x using Quotient.inductionOn' with f
  exact Quotient.sound âŸ¨(MonoOver.mapId _).app fâŸ©
#align category_theory.subobject.map_id CategoryTheory.Subobject.map_id

theorem map_comp (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [Mono f] [Mono g] (x : Subobject X) :
    (map (f â‰« g)).obj x = (map g).obj ((map f).obj x) := by
  induction' x using Quotient.inductionOn' with t
  exact Quotient.sound âŸ¨(MonoOver.mapComp _ _).app tâŸ©
#align category_theory.subobject.map_comp CategoryTheory.Subobject.map_comp

/-- Isomorphic objects have equivalent subobject lattices. -/
def mapIso {A B : C} (e : A â‰… B) : Subobject A â‰Œ Subobject B :=
  lowerEquivalence (MonoOver.mapIso e)
#align category_theory.subobject.map_iso CategoryTheory.Subobject.mapIso

-- Porting note: the note below doesn't seem true anymore
-- @[simps] here generates a lemma `map_iso_to_order_iso_to_equiv_symm_apply`
-- whose left hand side is not in simp normal form.
/-- In fact, there's a type level bijection between the subobjects of isomorphic objects,
which preserves the order. -/
def mapIsoToOrderIso (e : X â‰… Y) : Subobject X â‰ƒo Subobject Y where
  toFun := (map e.hom).obj
  invFun := (map e.inv).obj
  left_inv g := by simp_rw [â† map_comp, e.hom_inv_id, map_id]
  right_inv g := by simp_rw [â† map_comp, e.inv_hom_id, map_id]
  map_rel_iff' {A B} := by
    dsimp
    constructor
    Â· intro h
      apply_fun (map e.inv).obj at h
      Â· simpa only [â† map_comp, e.hom_inv_id, map_id] using h
      Â· apply Functor.monotone
    Â· intro h
      apply_fun (map e.hom).obj at h
      Â· exact h
      Â· apply Functor.monotone
#align category_theory.subobject.map_iso_to_order_iso CategoryTheory.Subobject.mapIsoToOrderIso

@[simp]
theorem mapIsoToOrderIso_apply (e : X â‰… Y) (P : Subobject X) :
    mapIsoToOrderIso e P = (map e.hom).obj P :=
  rfl
#align category_theory.subobject.map_iso_to_order_iso_apply CategoryTheory.Subobject.mapIsoToOrderIso_apply

@[simp]
theorem mapIsoToOrderIso_symm_apply (e : X â‰… Y) (Q : Subobject Y) :
    (mapIsoToOrderIso e).symm Q = (map e.inv).obj Q :=
  rfl
#align category_theory.subobject.map_iso_to_order_iso_symm_apply CategoryTheory.Subobject.mapIsoToOrderIso_symm_apply

/-- `map f : Subobject X â¥¤ Subobject Y` is
the left adjoint of `pullback f : Subobject Y â¥¤ Subobject X`. -/
def mapPullbackAdj [HasPullbacks C] (f : X âŸ¶ Y) [Mono f] : map f âŠ£ pullback f :=
  lowerAdjunction (MonoOver.mapPullbackAdj f)
#align category_theory.subobject.map_pullback_adj CategoryTheory.Subobject.mapPullbackAdj

@[simp]
theorem pullback_map_self [HasPullbacks C] (f : X âŸ¶ Y) [Mono f] (g : Subobject X) :
    (pullback f).obj ((map f).obj g) = g := by
  revert g
  exact Quotient.ind (fun g' => Quotient.sound âŸ¨(MonoOver.pullbackMapSelf f).app _âŸ©)
#align category_theory.subobject.pullback_map_self CategoryTheory.Subobject.pullback_map_self

theorem map_pullback [HasPullbacks C] {X Y Z W : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} {h : Y âŸ¶ W} {k : Z âŸ¶ W}
    [Mono h] [Mono g] (comm : f â‰« h = g â‰« k) (t : IsLimit (PullbackCone.mk f g comm))
    (p : Subobject Y) : (map g).obj ((pullback f).obj p) = (pullback k).obj ((map h).obj p) := by
  revert p
  apply Quotient.ind'
  intro a
  apply Quotient.sound
  apply ThinSkeleton.equiv_of_both_ways
  Â· refine MonoOver.homMk (pullback.lift pullback.fst _ ?_) (pullback.lift_snd _ _ _)
    change _ â‰« a.arrow â‰« h = (pullback.snd â‰« g) â‰« _
    rw [assoc, â† comm, pullback.condition_assoc]
  Â· refine MonoOver.homMk (pullback.lift pullback.fst
      (PullbackCone.IsLimit.lift t (pullback.fst â‰« a.arrow) pullback.snd _)
      (PullbackCone.IsLimit.lift_fst _ _ _ ?_).symm) ?_
    Â· rw [â† pullback.condition, assoc]
      rfl
    Â· dsimp
      rw [pullback.lift_snd_assoc]
      apply PullbackCone.IsLimit.lift_snd
#align category_theory.subobject.map_pullback CategoryTheory.Subobject.map_pullback

end Map

section Exists

variable [HasImages C]

/-- The functor from subobjects of `X` to subobjects of `Y` given by
sending the subobject `S` to its "image" under `f`, usually denoted $\exists_f$.
For instance, when `C` is the category of types,
viewing `Subobject X` as `Set X` this is just `Set.image f`.

This functor is left adjoint to the `pullback f` functor (shown in `existsPullbackAdj`)
provided both are defined, and generalises the `map f` functor, again provided it is defined.
-/
def Â«existsÂ» (f : X âŸ¶ Y) : Subobject X â¥¤ Subobject Y :=
  lower (MonoOver.exists f)
#align category_theory.subobject.exists CategoryTheory.Subobject.exists

/-- When `f : X âŸ¶ Y` is a monomorphism, `exists f` agrees with `map f`.
-/
theorem exists_iso_map (f : X âŸ¶ Y) [Mono f] : Â«existsÂ» f = map f :=
  lower_iso _ _ (MonoOver.existsIsoMap f)
#align category_theory.subobject.exists_iso_map CategoryTheory.Subobject.exists_iso_map

/-- `exists f : Subobject X â¥¤ Subobject Y` is
left adjoint to `pullback f : Subobject Y â¥¤ Subobject X`.
-/
def existsPullbackAdj (f : X âŸ¶ Y) [HasPullbacks C] : Â«existsÂ» f âŠ£ pullback f :=
  lowerAdjunction (MonoOver.existsPullbackAdj f)
#align category_theory.subobject.exists_pullback_adj CategoryTheory.Subobject.existsPullbackAdj

end Exists

end Subobject

end CategoryTheory
