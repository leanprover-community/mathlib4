/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Kim Morrison
-/

import Mathlib.CategoryTheory.Limits.Skeleton
import Mathlib.CategoryTheory.Subobject.MonoOver
import Mathlib.CategoryTheory.Skeletal
import Mathlib.CategoryTheory.ConcreteCategory.Basic
import Mathlib.Tactic.ApplyFun
import Mathlib.Tactic.CategoryTheory.Elementwise

/-!
# Subobjects

We define `Subobject X` as the quotient (by isomorphisms) of
`MonoOver X := {f : Over X // Mono f.hom}`.

Here `MonoOver X` is a thin category (a pair of objects has at most one morphism between them),
so we can think of it as a preorder. However as it is not skeletal, it is not a partial order.

There is a coercion from `Subobject X` back to the ambient category `C`
(using choice to pick a representative), and for `P : Subobject X`,
`P.arrow : (P : C) ‚ü∂ X` is the inclusion morphism.

We provide
* `def pullback [HasPullbacks C] (f : X ‚ü∂ Y) : Subobject Y ‚•§ Subobject X`
* `def map (f : X ‚ü∂ Y) [Mono f] : Subobject X ‚•§ Subobject Y`
* `def ¬´exists_¬ª [HasImages C] (f : X ‚ü∂ Y) : Subobject X ‚•§ Subobject Y`

and prove their basic properties and relationships.
These are all easy consequences of the earlier development
of the corresponding functors for `MonoOver`.

The subobjects of `X` form a preorder making them into a category. We have `X ‚â§ Y` if and only if
`X.arrow` factors through `Y.arrow`: see `ofLE`/`ofLEMk`/`ofMkLE`/`ofMkLEMk` and
`le_of_comm`. Similarly, to show that two subobjects are equal, we can supply an isomorphism between
the underlying objects that commutes with the arrows (`eq_of_comm`).

See also

* `CategoryTheory.Subobject.factorThru` :
  an API describing factorization of morphisms through subobjects.
* `CategoryTheory.Subobject.lattice` :
  the lattice structures on subobjects.

## Notes

This development originally appeared in Bhavik Mehta's "Topos theory for Lean" repository,
and was ported to mathlib by Kim Morrison.

### Implementation note

Currently we describe `pullback`, `map`, etc., as functors.
It may be better to just say that they are monotone functions,
and even avoid using categorical language entirely when describing `Subobject X`.
(It's worth keeping this in mind in future use; it should be a relatively easy change here
if it looks preferable.)

### Relation to pseudoelements

There is a separate development of pseudoelements in `CategoryTheory.Abelian.Pseudoelements`,
as a quotient (but not by isomorphism) of `Over X`.

When a morphism `f` has an image, the image represents the same pseudoelement.
In a category with images `Pseudoelements X` could be constructed as a quotient of `MonoOver X`.
In fact, in an abelian category (I'm not sure in what generality beyond that),
`Pseudoelements X` agrees with `Subobject X`, but we haven't developed this in mathlib yet.

-/


universe w' w v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

noncomputable section

namespace CategoryTheory

open CategoryTheory CategoryTheory.Category CategoryTheory.Limits

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {X Y Z : C}
variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]

/-!
We now construct the subobject lattice for `X : C`,
as the quotient by isomorphisms of `MonoOver X`.

Since `MonoOver X` is a thin category, we use `ThinSkeleton` to take the quotient.

Essentially all the structure defined above on `MonoOver X` descends to `Subobject X`,
with morphisms becoming inequalities, and isomorphisms becoming equations.
-/


/-- The category of subobjects of `X : C`, defined as isomorphism classes of monomorphisms into `X`.
-/
def Subobject (X : C) :=
  ThinSkeleton (MonoOver X)

instance (X : C) : PartialOrder (Subobject X) :=
  inferInstanceAs <| PartialOrder (ThinSkeleton (MonoOver X))

namespace Subobject

/-- Convenience constructor for a subobject. -/
def mk {X A : C} (f : A ‚ü∂ X) [Mono f] : Subobject X :=
  (toThinSkeleton _).obj (MonoOver.mk' f)

section

attribute [local ext] CategoryTheory.Comma

protected theorem ind {X : C} (p : Subobject X ‚Üí Prop)
    (h : ‚àÄ ‚¶ÉA : C‚¶Ñ (f : A ‚ü∂ X) [Mono f], p (Subobject.mk f)) (P : Subobject X) : p P := by
  apply Quotient.inductionOn'
  intro a
  exact h a.arrow

protected theorem ind‚ÇÇ {X : C} (p : Subobject X ‚Üí Subobject X ‚Üí Prop)
    (h : ‚àÄ ‚¶ÉA B : C‚¶Ñ (f : A ‚ü∂ X) (g : B ‚ü∂ X) [Mono f] [Mono g],
      p (Subobject.mk f) (Subobject.mk g))
    (P Q : Subobject X) : p P Q := by
  apply Quotient.inductionOn‚ÇÇ'
  intro a b
  exact h a.arrow b.arrow

end

/-- Declare a function on subobjects of `X` by specifying a function on monomorphisms with
codomain `X`. -/
protected def lift {Œ± : Sort*} {X : C} (F : ‚àÄ ‚¶ÉA : C‚¶Ñ (f : A ‚ü∂ X) [Mono f], Œ±)
    (h :
      ‚àÄ ‚¶ÉA B : C‚¶Ñ (f : A ‚ü∂ X) (g : B ‚ü∂ X) [Mono f] [Mono g] (i : A ‚âÖ B),
        i.hom ‚â´ g = f ‚Üí F f = F g) :
    Subobject X ‚Üí Œ± := fun P =>
  Quotient.liftOn' P (fun m => F m.arrow) fun m n ‚ü®i‚ü© =>
    h m.arrow n.arrow ((MonoOver.forget X ‚ãô Over.forget X).mapIso i) (Over.w i.hom)

@[simp]
protected theorem lift_mk {Œ± : Sort*} {X : C} (F : ‚àÄ ‚¶ÉA : C‚¶Ñ (f : A ‚ü∂ X) [Mono f], Œ±) {h A}
    (f : A ‚ü∂ X) [Mono f] : Subobject.lift F h (Subobject.mk f) = F f :=
  rfl

/-- The category of subobjects is equivalent to the `MonoOver` category. It is more convenient to
use the former due to the partial order instance, but oftentimes it is easier to define structures
on the latter. -/
noncomputable def equivMonoOver (X : C) : Subobject X ‚âå MonoOver X :=
  ThinSkeleton.equivalence _

/-- Use choice to pick a representative `MonoOver X` for each `Subobject X`.
-/
noncomputable def representative {X : C} : Subobject X ‚•§ MonoOver X :=
  (equivMonoOver X).functor

instance : (representative (X := X)).IsEquivalence :=
  (equivMonoOver X).isEquivalence_functor

/-- Starting with `A : MonoOver X`, we can take its equivalence class in `Subobject X`
then pick an arbitrary representative using `representative.obj`.
This is isomorphic (in `MonoOver X`) to the original `A`.
-/
noncomputable def representativeIso {X : C} (A : MonoOver X) :
    representative.obj ((toThinSkeleton _).obj A) ‚âÖ A :=
  (equivMonoOver X).counitIso.app A

/-- Use choice to pick a representative underlying object in `C` for any `Subobject X`.

Prefer to use the coercion `P : C` rather than explicitly writing `underlying.obj P`.
-/
noncomputable def underlying {X : C} : Subobject X ‚•§ C :=
  representative ‚ãô MonoOver.forget _ ‚ãô Over.forget _

instance : CoeOut (Subobject X) C where coe Y := underlying.obj Y

/-- If we construct a `Subobject Y` from an explicit `f : X ‚ü∂ Y` with `[Mono f]`,
then pick an arbitrary choice of underlying object `(Subobject.mk f : C)` back in `C`,
it is isomorphic (in `C`) to the original `X`.
-/
noncomputable def underlyingIso {X Y : C} (f : X ‚ü∂ Y) [Mono f] : (Subobject.mk f : C) ‚âÖ X :=
  (MonoOver.forget _ ‚ãô Over.forget _).mapIso (representativeIso (MonoOver.mk' f))

/-- The morphism in `C` from the arbitrarily chosen underlying object to the ambient object.
-/
noncomputable def arrow {X : C} (Y : Subobject X) : (Y : C) ‚ü∂ X :=
  (representative.obj Y).obj.hom

instance arrow_mono {X : C} (Y : Subobject X) : Mono Y.arrow :=
  (representative.obj Y).property

@[simp]
theorem arrow_congr {A : C} (X Y : Subobject A) (h : X = Y) :
    eqToHom (congr_arg (fun X : Subobject A => (X : C)) h) ‚â´ Y.arrow = X.arrow := by
  induction h
  simp

@[simp]
theorem representative_coe (Y : Subobject X) : (representative.obj Y : C) = (Y : C) :=
  rfl

@[simp]
theorem representative_arrow (Y : Subobject X) : (representative.obj Y).arrow = Y.arrow :=
  rfl

@[reassoc (attr := simp)]
theorem underlying_arrow {X : C} {Y Z : Subobject X} (f : Y ‚ü∂ Z) :
    underlying.map f ‚â´ arrow Z = arrow Y :=
  Over.w (representative.map f)

@[reassoc (attr := simp), elementwise (attr := simp)]
theorem underlyingIso_arrow {X Y : C} (f : X ‚ü∂ Y) [Mono f] :
    (underlyingIso f).inv ‚â´ (Subobject.mk f).arrow = f :=
  Over.w _

@[reassoc (attr := simp)]
theorem underlyingIso_hom_comp_eq_mk {X Y : C} (f : X ‚ü∂ Y) [Mono f] :
    (underlyingIso f).hom ‚â´ f = (mk f).arrow :=
  (Iso.eq_inv_comp _).1 (underlyingIso_arrow f).symm

/-- Two morphisms into a subobject are equal exactly if
the morphisms into the ambient object are equal -/
@[ext]
theorem eq_of_comp_arrow_eq {X Y : C} {P : Subobject Y} {f g : X ‚ü∂ P}
    (h : f ‚â´ P.arrow = g ‚â´ P.arrow) : f = g :=
  (cancel_mono P.arrow).mp h

theorem mk_le_mk_of_comm {B A‚ÇÅ A‚ÇÇ : C} {f‚ÇÅ : A‚ÇÅ ‚ü∂ B} {f‚ÇÇ : A‚ÇÇ ‚ü∂ B} [Mono f‚ÇÅ] [Mono f‚ÇÇ] (g : A‚ÇÅ ‚ü∂ A‚ÇÇ)
    (w : g ‚â´ f‚ÇÇ = f‚ÇÅ) : mk f‚ÇÅ ‚â§ mk f‚ÇÇ :=
  ‚ü®MonoOver.homMk _ w‚ü©

@[simp]
theorem mk_arrow (P : Subobject X) : mk P.arrow = P :=
  Quotient.inductionOn' P fun Q => by
    obtain ‚ü®e‚ü© := @Quotient.mk_out' _ (isIsomorphicSetoid _) Q
    exact Quotient.sound' ‚ü®MonoOver.isoMk (Iso.refl _) ‚â™‚â´ e‚ü©

theorem le_of_comm {B : C} {X Y : Subobject B} (f : (X : C) ‚ü∂ (Y : C)) (w : f ‚â´ Y.arrow = X.arrow) :
    X ‚â§ Y := by
  convert mk_le_mk_of_comm _ w <;> simp

theorem le_mk_of_comm {B A : C} {X : Subobject B} {f : A ‚ü∂ B} [Mono f] (g : (X : C) ‚ü∂ A)
    (w : g ‚â´ f = X.arrow) : X ‚â§ mk f :=
  le_of_comm (g ‚â´ (underlyingIso f).inv) <| by simp [w]

theorem mk_le_of_comm {B A : C} {X : Subobject B} {f : A ‚ü∂ B} [Mono f] (g : A ‚ü∂ (X : C))
    (w : g ‚â´ X.arrow = f) : mk f ‚â§ X :=
  le_of_comm ((underlyingIso f).hom ‚â´ g) <| by simp [w]

/-- To show that two subobjects are equal, it suffices to exhibit an isomorphism commuting with
the arrows. -/
@[ext (iff := false)]
theorem eq_of_comm {B : C} {X Y : Subobject B} (f : (X : C) ‚âÖ (Y : C))
    (w : f.hom ‚â´ Y.arrow = X.arrow) : X = Y :=
  le_antisymm (le_of_comm f.hom w) <| le_of_comm f.inv <| f.inv_comp_eq.2 w.symm

/-- To show that two subobjects are equal, it suffices to exhibit an isomorphism commuting with
the arrows. -/
theorem eq_mk_of_comm {B A : C} {X : Subobject B} (f : A ‚ü∂ B) [Mono f] (i : (X : C) ‚âÖ A)
    (w : i.hom ‚â´ f = X.arrow) : X = mk f :=
  eq_of_comm (i.trans (underlyingIso f).symm) <| by simp [w]

/-- To show that two subobjects are equal, it suffices to exhibit an isomorphism commuting with
the arrows. -/
theorem mk_eq_of_comm {B A : C} {X : Subobject B} (f : A ‚ü∂ B) [Mono f] (i : A ‚âÖ (X : C))
    (w : i.hom ‚â´ X.arrow = f) : mk f = X :=
  Eq.symm <| eq_mk_of_comm _ i.symm <| by rw [Iso.symm_hom, Iso.inv_comp_eq, w]

/-- To show that two subobjects are equal, it suffices to exhibit an isomorphism commuting with
the arrows. -/
theorem mk_eq_mk_of_comm {B A‚ÇÅ A‚ÇÇ : C} (f : A‚ÇÅ ‚ü∂ B) (g : A‚ÇÇ ‚ü∂ B) [Mono f] [Mono g] (i : A‚ÇÅ ‚âÖ A‚ÇÇ)
    (w : i.hom ‚â´ g = f) : mk f = mk g :=
  eq_mk_of_comm _ ((underlyingIso f).trans i) <| by simp [w]

lemma mk_surjective {X : C} (S : Subobject X) :
    ‚àÉ (A : C) (i : A ‚ü∂ X) (_ : Mono i), S = Subobject.mk i :=
  ‚ü®_, S.arrow, inferInstance, by simp‚ü©

-- We make `X` and `Y` explicit arguments here so that when `ofLE` appears in goal statements
-- it is possible to see its source and target
-- (`h` will just display as `_`, because it is in `Prop`).
/-- An inequality of subobjects is witnessed by some morphism between the corresponding objects. -/
def ofLE {B : C} (X Y : Subobject B) (h : X ‚â§ Y) : (X : C) ‚ü∂ (Y : C) :=
  underlying.map <| h.hom

@[reassoc (attr := simp)]
theorem ofLE_arrow {B : C} {X Y : Subobject B} (h : X ‚â§ Y) : ofLE X Y h ‚â´ Y.arrow = X.arrow :=
  underlying_arrow _

instance {B : C} (X Y : Subobject B) (h : X ‚â§ Y) : Mono (ofLE X Y h) := by
  fconstructor
  intro Z f g w
  replace w := w =‚â´ Y.arrow
  ext
  simpa using w

theorem ofLE_mk_le_mk_of_comm {B A‚ÇÅ A‚ÇÇ : C} {f‚ÇÅ : A‚ÇÅ ‚ü∂ B} {f‚ÇÇ : A‚ÇÇ ‚ü∂ B} [Mono f‚ÇÅ] [Mono f‚ÇÇ]
    (g : A‚ÇÅ ‚ü∂ A‚ÇÇ) (w : g ‚â´ f‚ÇÇ = f‚ÇÅ) :
    ofLE _ _ (mk_le_mk_of_comm g w) = (underlyingIso _).hom ‚â´ g ‚â´ (underlyingIso _).inv := by
  ext
  simp [w]

/-- An inequality of subobjects is witnessed by some morphism between the corresponding objects. -/
def ofLEMk {B A : C} (X : Subobject B) (f : A ‚ü∂ B) [Mono f] (h : X ‚â§ mk f) : (X : C) ‚ü∂ A :=
  ofLE X (mk f) h ‚â´ (underlyingIso f).hom

instance {B A : C} (X : Subobject B) (f : A ‚ü∂ B) [Mono f] (h : X ‚â§ mk f) :
    Mono (ofLEMk X f h) := by
  dsimp only [ofLEMk]
  infer_instance

@[simp]
theorem ofLEMk_comp {B A : C} {X : Subobject B} {f : A ‚ü∂ B} [Mono f] (h : X ‚â§ mk f) :
    ofLEMk X f h ‚â´ f = X.arrow := by simp [ofLEMk]

/-- An inequality of subobjects is witnessed by some morphism between the corresponding objects. -/
def ofMkLE {B A : C} (f : A ‚ü∂ B) [Mono f] (X : Subobject B) (h : mk f ‚â§ X) : A ‚ü∂ (X : C) :=
  (underlyingIso f).inv ‚â´ ofLE (mk f) X h

instance {B A : C} (f : A ‚ü∂ B) [Mono f] (X : Subobject B) (h : mk f ‚â§ X) :
    Mono (ofMkLE f X h) := by
  dsimp only [ofMkLE]
  infer_instance

@[simp]
theorem ofMkLE_arrow {B A : C} {f : A ‚ü∂ B} [Mono f] {X : Subobject B} (h : mk f ‚â§ X) :
    ofMkLE f X h ‚â´ X.arrow = f := by simp [ofMkLE]

/-- An inequality of subobjects is witnessed by some morphism between the corresponding objects. -/
def ofMkLEMk {B A‚ÇÅ A‚ÇÇ : C} (f : A‚ÇÅ ‚ü∂ B) (g : A‚ÇÇ ‚ü∂ B) [Mono f] [Mono g] (h : mk f ‚â§ mk g) :
    A‚ÇÅ ‚ü∂ A‚ÇÇ :=
  (underlyingIso f).inv ‚â´ ofLE (mk f) (mk g) h ‚â´ (underlyingIso g).hom

instance {B A‚ÇÅ A‚ÇÇ : C} (f : A‚ÇÅ ‚ü∂ B) (g : A‚ÇÇ ‚ü∂ B) [Mono f] [Mono g] (h : mk f ‚â§ mk g) :
    Mono (ofMkLEMk f g h) := by
  dsimp only [ofMkLEMk]
  infer_instance

@[simp]
theorem ofMkLEMk_comp {B A‚ÇÅ A‚ÇÇ : C} {f : A‚ÇÅ ‚ü∂ B} {g : A‚ÇÇ ‚ü∂ B} [Mono f] [Mono g] (h : mk f ‚â§ mk g) :
    ofMkLEMk f g h ‚â´ g = f := by simp [ofMkLEMk]

@[reassoc (attr := simp)]
theorem ofLE_comp_ofLE {B : C} (X Y Z : Subobject B) (h‚ÇÅ : X ‚â§ Y) (h‚ÇÇ : Y ‚â§ Z) :
    ofLE X Y h‚ÇÅ ‚â´ ofLE Y Z h‚ÇÇ = ofLE X Z (h‚ÇÅ.trans h‚ÇÇ) := by
  simp only [ofLE, ‚Üê Functor.map_comp underlying]
  congr 1

@[reassoc (attr := simp)]
theorem ofLE_comp_ofLEMk {B A : C} (X Y : Subobject B) (f : A ‚ü∂ B) [Mono f] (h‚ÇÅ : X ‚â§ Y)
    (h‚ÇÇ : Y ‚â§ mk f) : ofLE X Y h‚ÇÅ ‚â´ ofLEMk Y f h‚ÇÇ = ofLEMk X f (h‚ÇÅ.trans h‚ÇÇ) := by
  simp only [ofLEMk, ofLE, ‚Üê Functor.map_comp_assoc underlying]
  congr 1

@[reassoc (attr := simp)]
theorem ofLEMk_comp_ofMkLE {B A : C} (X : Subobject B) (f : A ‚ü∂ B) [Mono f] (Y : Subobject B)
    (h‚ÇÅ : X ‚â§ mk f) (h‚ÇÇ : mk f ‚â§ Y) : ofLEMk X f h‚ÇÅ ‚â´ ofMkLE f Y h‚ÇÇ = ofLE X Y (h‚ÇÅ.trans h‚ÇÇ) := by
  simp only [ofMkLE, ofLEMk, ofLE, ‚Üê Functor.map_comp underlying, assoc, Iso.hom_inv_id_assoc]
  congr 1

@[reassoc (attr := simp)]
theorem ofLEMk_comp_ofMkLEMk {B A‚ÇÅ A‚ÇÇ : C} (X : Subobject B) (f : A‚ÇÅ ‚ü∂ B) [Mono f] (g : A‚ÇÇ ‚ü∂ B)
    [Mono g] (h‚ÇÅ : X ‚â§ mk f) (h‚ÇÇ : mk f ‚â§ mk g) :
    ofLEMk X f h‚ÇÅ ‚â´ ofMkLEMk f g h‚ÇÇ = ofLEMk X g (h‚ÇÅ.trans h‚ÇÇ) := by
  simp only [ofLEMk, ofLE, ofMkLEMk, ‚Üê Functor.map_comp_assoc underlying,
    assoc, Iso.hom_inv_id_assoc]
  congr 1

@[reassoc (attr := simp)]
theorem ofMkLE_comp_ofLE {B A‚ÇÅ : C} (f : A‚ÇÅ ‚ü∂ B) [Mono f] (X Y : Subobject B) (h‚ÇÅ : mk f ‚â§ X)
    (h‚ÇÇ : X ‚â§ Y) : ofMkLE f X h‚ÇÅ ‚â´ ofLE X Y h‚ÇÇ = ofMkLE f Y (h‚ÇÅ.trans h‚ÇÇ) := by
  simp only [ofMkLE, ofLE, ‚Üê Functor.map_comp underlying,
    assoc]
  congr 1

@[reassoc (attr := simp)]
theorem ofMkLE_comp_ofLEMk {B A‚ÇÅ A‚ÇÇ : C} (f : A‚ÇÅ ‚ü∂ B) [Mono f] (X : Subobject B) (g : A‚ÇÇ ‚ü∂ B)
    [Mono g] (h‚ÇÅ : mk f ‚â§ X) (h‚ÇÇ : X ‚â§ mk g) :
    ofMkLE f X h‚ÇÅ ‚â´ ofLEMk X g h‚ÇÇ = ofMkLEMk f g (h‚ÇÅ.trans h‚ÇÇ) := by
  simp only [ofMkLE, ofLEMk, ofLE, ofMkLEMk, ‚Üê Functor.map_comp_assoc underlying, assoc]
  congr 1

@[reassoc (attr := simp)]
theorem ofMkLEMk_comp_ofMkLE {B A‚ÇÅ A‚ÇÇ : C} (f : A‚ÇÅ ‚ü∂ B) [Mono f] (g : A‚ÇÇ ‚ü∂ B) [Mono g]
    (X : Subobject B) (h‚ÇÅ : mk f ‚â§ mk g) (h‚ÇÇ : mk g ‚â§ X) :
    ofMkLEMk f g h‚ÇÅ ‚â´ ofMkLE g X h‚ÇÇ = ofMkLE f X (h‚ÇÅ.trans h‚ÇÇ) := by
  simp only [ofMkLE, ofLE, ofMkLEMk, ‚Üê Functor.map_comp underlying,
    assoc, Iso.hom_inv_id_assoc]
  congr 1

@[reassoc (attr := simp)]
theorem ofMkLEMk_comp_ofMkLEMk {B A‚ÇÅ A‚ÇÇ A‚ÇÉ : C} (f : A‚ÇÅ ‚ü∂ B) [Mono f] (g : A‚ÇÇ ‚ü∂ B) [Mono g]
    (h : A‚ÇÉ ‚ü∂ B) [Mono h] (h‚ÇÅ : mk f ‚â§ mk g) (h‚ÇÇ : mk g ‚â§ mk h) :
    ofMkLEMk f g h‚ÇÅ ‚â´ ofMkLEMk g h h‚ÇÇ = ofMkLEMk f h (h‚ÇÅ.trans h‚ÇÇ) := by
  simp only [ofLE, ofMkLEMk, ‚Üê Functor.map_comp_assoc underlying, assoc,
    Iso.hom_inv_id_assoc]
  congr 1

@[simp]
theorem ofLE_refl {B : C} (X : Subobject B) : ofLE X X le_rfl = ùüô _ := by
  apply (cancel_mono X.arrow).mp
  simp

@[simp]
theorem ofMkLEMk_refl {B A‚ÇÅ : C} (f : A‚ÇÅ ‚ü∂ B) [Mono f] : ofMkLEMk f f le_rfl = ùüô _ := by
  apply (cancel_mono f).mp
  simp

-- As with `ofLE`, we have `X` and `Y` as explicit arguments for readability.
/-- An equality of subobjects gives an isomorphism of the corresponding objects.
(One could use `underlying.mapIso (eqToIso h))` here, but this is more readable.) -/
@[simps]
def isoOfEq {B : C} (X Y : Subobject B) (h : X = Y) : (X : C) ‚âÖ (Y : C) where
  hom := ofLE _ _ h.le
  inv := ofLE _ _ h.ge

/-- An equality of subobjects gives an isomorphism of the corresponding objects. -/
@[simps]
def isoOfEqMk {B A : C} (X : Subobject B) (f : A ‚ü∂ B) [Mono f] (h : X = mk f) : (X : C) ‚âÖ A where
  hom := ofLEMk X f h.le
  inv := ofMkLE f X h.ge

/-- An equality of subobjects gives an isomorphism of the corresponding objects. -/
@[simps]
def isoOfMkEq {B A : C} (f : A ‚ü∂ B) [Mono f] (X : Subobject B) (h : mk f = X) : A ‚âÖ (X : C) where
  hom := ofMkLE f X h.le
  inv := ofLEMk X f h.ge

/-- An equality of subobjects gives an isomorphism of the corresponding objects. -/
@[simps]
def isoOfMkEqMk {B A‚ÇÅ A‚ÇÇ : C} (f : A‚ÇÅ ‚ü∂ B) (g : A‚ÇÇ ‚ü∂ B) [Mono f] [Mono g] (h : mk f = mk g) :
    A‚ÇÅ ‚âÖ A‚ÇÇ where
  hom := ofMkLEMk f g h.le
  inv := ofMkLEMk g f h.ge

lemma mk_lt_mk_of_comm {X A‚ÇÅ A‚ÇÇ : C} {i‚ÇÅ : A‚ÇÅ ‚ü∂ X} {i‚ÇÇ : A‚ÇÇ ‚ü∂ X} [Mono i‚ÇÅ] [Mono i‚ÇÇ]
    (f : A‚ÇÅ ‚ü∂ A‚ÇÇ) (fac : f ‚â´ i‚ÇÇ = i‚ÇÅ) (hf : ¬¨ IsIso f) :
    Subobject.mk i‚ÇÅ < Subobject.mk i‚ÇÇ := by
  obtain _ | h := (mk_le_mk_of_comm _ fac).lt_or_eq
  ¬∑ assumption
  ¬∑ exfalso
    apply hf
    convert (isoOfMkEqMk i‚ÇÅ i‚ÇÇ h).isIso_hom
    rw [‚Üê cancel_mono i‚ÇÇ, isoOfMkEqMk_hom, ofMkLEMk_comp, fac]

lemma mk_lt_mk_iff_of_comm {X A‚ÇÅ A‚ÇÇ : C} {i‚ÇÅ : A‚ÇÅ ‚ü∂ X} {i‚ÇÇ : A‚ÇÇ ‚ü∂ X} [Mono i‚ÇÅ] [Mono i‚ÇÇ]
    (f : A‚ÇÅ ‚ü∂ A‚ÇÇ) (fac : f ‚â´ i‚ÇÇ = i‚ÇÅ) :
    Subobject.mk i‚ÇÅ < Subobject.mk i‚ÇÇ ‚Üî ¬¨ IsIso f :=
  ‚ü®fun h hf ‚Ü¶ by simp only [mk_eq_mk_of_comm i‚ÇÅ i‚ÇÇ (asIso f) fac, lt_self_iff_false] at h,
    mk_lt_mk_of_comm f fac‚ü©

end Subobject

namespace MonoOver

variable {P Q : MonoOver X} (f : P ‚ü∂ Q)

include f in
lemma subobjectMk_le_mk_of_hom :
    Subobject.mk P.obj.hom ‚â§ Subobject.mk Q.obj.hom :=
  Subobject.mk_le_mk_of_comm f.left (by simp)

lemma isIso_left_iff_subobjectMk_eq :
    IsIso f.left ‚Üî Subobject.mk P.1.hom = Subobject.mk Q.1.hom :=
  ‚ü®fun _ ‚Ü¶ Subobject.mk_eq_mk_of_comm _ _ (asIso f.left) (by simp),
    fun h ‚Ü¶ ‚ü®Subobject.ofMkLEMk _ _ h.symm.le, by simp [‚Üê cancel_mono P.1.hom],
      by simp [‚Üê cancel_mono Q.1.hom]‚ü©‚ü©

lemma isIso_iff_subobjectMk_eq :
    IsIso f ‚Üî Subobject.mk P.1.hom = Subobject.mk Q.1.hom := by
  rw [isIso_iff_isIso_left, isIso_left_iff_subobjectMk_eq]

end MonoOver

open CategoryTheory.Limits

namespace Subobject

/-- Any functor `MonoOver X ‚•§ MonoOver Y` descends to a functor
`Subobject X ‚•§ Subobject Y`, because `MonoOver Y` is thin. -/
def lower {Y : D} (F : MonoOver X ‚•§ MonoOver Y) : Subobject X ‚•§ Subobject Y :=
  ThinSkeleton.map F

/-- Isomorphic functors become equal when lowered to `Subobject`.
(It's not as evil as usual to talk about equality between functors
because the categories are thin and skeletal.) -/
theorem lower_iso (F‚ÇÅ F‚ÇÇ : MonoOver X ‚•§ MonoOver Y) (h : F‚ÇÅ ‚âÖ F‚ÇÇ) : lower F‚ÇÅ = lower F‚ÇÇ :=
  ThinSkeleton.map_iso_eq h

/-- A ternary version of `Subobject.lower`. -/
def lower‚ÇÇ (F : MonoOver X ‚•§ MonoOver Y ‚•§ MonoOver Z) : Subobject X ‚•§ Subobject Y ‚•§ Subobject Z :=
  ThinSkeleton.map‚ÇÇ F

@[simp]
theorem lower_comm (F : MonoOver Y ‚•§ MonoOver X) :
    toThinSkeleton _ ‚ãô lower F = F ‚ãô toThinSkeleton _ :=
  rfl

/--
Applying `lower F` and then `representative` is isomorphic to first applying `representative`
and then applying `F`.
-/
def lowerCompRepresentativeIso (F : MonoOver Y ‚•§ MonoOver X) :
    lower F ‚ãô representative ‚âÖ representative ‚ãô F :=
  ThinSkeleton.mapCompFromThinSkeletonIso _

/-- An adjunction between `MonoOver A` and `MonoOver B` gives an adjunction
between `Subobject A` and `Subobject B`. -/
def lowerAdjunction {A : C} {B : D} {L : MonoOver A ‚•§ MonoOver B} {R : MonoOver B ‚•§ MonoOver A}
    (h : L ‚ä£ R) : lower L ‚ä£ lower R :=
  ThinSkeleton.lowerAdjunction _ _ h

/-- An equivalence between `MonoOver A` and `MonoOver B` gives an equivalence
between `Subobject A` and `Subobject B`. -/
@[simps]
def lowerEquivalence {A : C} {B : D} (e : MonoOver A ‚âå MonoOver B) : Subobject A ‚âå Subobject B where
  functor := lower e.functor
  inverse := lower e.inverse
  unitIso := by
    apply eqToIso
    convert ThinSkeleton.map_iso_eq e.unitIso
    ¬∑ exact ThinSkeleton.map_id_eq.symm
    ¬∑ exact (ThinSkeleton.map_comp_eq _ _).symm
  counitIso := by
    apply eqToIso
    convert ThinSkeleton.map_iso_eq e.counitIso
    ¬∑ exact (ThinSkeleton.map_comp_eq _ _).symm
    ¬∑ exact ThinSkeleton.map_id_eq.symm

section Limits

variable {J : Type u‚ÇÉ} [Category.{v‚ÇÉ} J]

instance hasLimitsOfShape [HasLimitsOfShape J (Over X)] :
    HasLimitsOfShape J (Subobject X) := by
  apply hasLimitsOfShape_thinSkeleton

instance hasFiniteLimits [HasFiniteLimits (Over X)] : HasFiniteLimits (Subobject X) where
  out _ _ _ := by infer_instance

instance hasLimitsOfSize [HasLimitsOfSize.{w, w'} (Over X)] :
    HasLimitsOfSize.{w, w'} (Subobject X) where
  has_limits_of_shape _ _ := by infer_instance

end Limits

section Colimits

variable [HasCoproducts C] [HasStrongEpiMonoFactorisations C]

instance hasColimitsOfSize : HasColimitsOfSize.{w, w'} (Subobject X) := by
  apply hasColimitsOfSize_thinSkeleton

end Colimits

section Pullback

variable [HasPullbacks C]

/-- When `C` has pullbacks, a morphism `f : X ‚ü∂ Y` induces a functor `Subobject Y ‚•§ Subobject X`,
by pulling back a monomorphism along `f`. -/
def pullback (f : X ‚ü∂ Y) : Subobject Y ‚•§ Subobject X :=
  lower (MonoOver.pullback f)

theorem pullback_id (x : Subobject X) : (pullback (ùüô X)).obj x = x := by
  induction x using Quotient.inductionOn' with | _ f
  exact Quotient.sound ‚ü®MonoOver.pullbackId.app f‚ü©

theorem pullback_comp (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : Subobject Z) :
    (pullback (f ‚â´ g)).obj x = (pullback f).obj ((pullback g).obj x) := by
  induction x using Quotient.inductionOn' with | _ t
  exact Quotient.sound ‚ü®(MonoOver.pullbackComp _ _).app t‚ü©

theorem pullback_obj_mk {A B X Y : C} {f : Y ‚ü∂ X} {i : A ‚ü∂ X} [Mono i]
    {j : B ‚ü∂ Y} [Mono j] {f' : B ‚ü∂ A}
    (h : IsPullback f' j i f) :
    (pullback f).obj (mk i) = mk j :=
  ((equivMonoOver Y).inverse.mapIso
    (MonoOver.pullbackObjIsoOfIsPullback _ _ _ _ h)).to_eq

theorem pullback_obj {X Y : C} (f : Y ‚ü∂ X) (x : Subobject X) :
    (pullback f).obj x = mk (pullback.snd x.arrow f) := by
  obtain ‚ü®Z, i, _, rfl‚ü© := mk_surjective x
  rw [pullback_obj_mk (IsPullback.of_hasPullback i f)]
  exact mk_eq_mk_of_comm _ _ (asIso (pullback.map i f (mk i).arrow f
    (underlyingIso i).inv (ùüô _) (ùüô _) (by simp) (by simp))) (by simp)

instance (f : X ‚ü∂ Y) : (pullback f).Faithful where

lemma isPullback_aux (f : X ‚ü∂ Y) (y : Subobject Y) :
    ‚àÉ œÜ, IsPullback œÜ ((pullback f).obj y).arrow y.arrow f := by
  obtain ‚ü®A, i, ‚ü®_, rfl‚ü©‚ü© := mk_surjective y
  rw [pullback_obj]
  exists (underlyingIso (pullback.snd (mk i).arrow f)).hom ‚â´ pullback.fst (mk i).arrow f
  exact IsPullback.of_iso (IsPullback.of_hasPullback (mk i).arrow f)
        (underlyingIso (pullback.snd (mk i).arrow f)).symm (Iso.refl _) (Iso.refl _) (Iso.refl _)
        (by simp) (by simp) (by simp) (by simp)

/-- For any morphism `f : X ‚ü∂ Y` and subobject `y` of `Y`, `Subobject.pullbackœÄ f y` is the first
    projection in the following pullback square:

    ```
    (Subobject.pullback f).obj y ----pullbackœÄ f y---> (y : C)
             |                                            |
    ((Subobject.pullback f).obj y).arrow               y.arrow
             |                                            |
             v                                            v
             X ---------------------f-------------------> Y
    ```

    For instance in the category of sets, `Subobject.pullbackœÄ f y` is the restriction of `f` to
    elements of `X` that are in the preimage of `y ‚äÜ Y`.
-/
noncomputable def pullbackœÄ (f : X ‚ü∂ Y) (y : Subobject Y) :
    ((Subobject.pullback f).obj y : C) ‚ü∂ (y : C) :=
  (isPullback_aux f y).choose

/-- This states that `pullbackœÄ f y` indeed forms a pullback square (see `Subobject.pullbackœÄ`). -/
theorem isPullback (f : X ‚ü∂ Y) (y : Subobject Y) :
    IsPullback (pullbackœÄ f y) ((pullback f).obj y).arrow y.arrow f :=
  (isPullback_aux f y).choose_spec

end Pullback

section Map

/-- We can map subobjects of `X` to subobjects of `Y`
by post-composition with a monomorphism `f : X ‚ü∂ Y`.
-/
def map (f : X ‚ü∂ Y) [Mono f] : Subobject X ‚•§ Subobject Y :=
  lower (MonoOver.map f)

lemma map_mk {A X Y : C} (i : A ‚ü∂ X) [Mono i] (f : X ‚ü∂ Y) [Mono f] :
    (map f).obj (mk i) = mk (i ‚â´ f) :=
  rfl

theorem map_id (x : Subobject X) : (map (ùüô X)).obj x = x := by
  induction x using Quotient.inductionOn' with | _ f
  exact Quotient.sound ‚ü®(MonoOver.mapId _).app f‚ü©

theorem map_comp (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [Mono f] [Mono g] (x : Subobject X) :
    (map (f ‚â´ g)).obj x = (map g).obj ((map f).obj x) := by
  induction x using Quotient.inductionOn' with | _ t
  exact Quotient.sound ‚ü®(MonoOver.mapComp _ _).app t‚ü©

lemma map_obj_injective {X Y : C} (f : X ‚ü∂ Y) [Mono f] :
    Function.Injective (Subobject.map f).obj := fun X‚ÇÅ X‚ÇÇ h ‚Ü¶ by
  induction X‚ÇÅ using Subobject.ind
  induction X‚ÇÇ using Subobject.ind
  simp only [map_mk] at h
  exact mk_eq_mk_of_comm _ _ (isoOfMkEqMk _ _ h) (by simp [‚Üê cancel_mono f])

/-- Isomorphic objects have equivalent subobject lattices. -/
def mapIso {A B : C} (e : A ‚âÖ B) : Subobject A ‚âå Subobject B :=
  lowerEquivalence (MonoOver.mapIso e)

/-- In fact, there's a type level bijection between the subobjects of isomorphic objects,
which preserves the order. -/
@[simps]
def mapIsoToOrderIso (e : X ‚âÖ Y) : Subobject X ‚âÉo Subobject Y where
  toFun := (map e.hom).obj
  invFun := (map e.inv).obj
  left_inv g := by simp_rw [‚Üê map_comp, e.hom_inv_id, map_id]
  right_inv g := by simp_rw [‚Üê map_comp, e.inv_hom_id, map_id]
  map_rel_iff' {A B} := by
    dsimp
    constructor
    ¬∑ intro h
      apply_fun (map e.inv).obj at h
      ¬∑ simpa only [‚Üê map_comp, e.hom_inv_id, map_id] using h
      ¬∑ apply Functor.monotone
    ¬∑ intro h
      apply_fun (map e.hom).obj at h
      ¬∑ exact h
      ¬∑ apply Functor.monotone

/-- `map f : Subobject X ‚•§ Subobject Y` is
the left adjoint of `pullback f : Subobject Y ‚•§ Subobject X`. -/
def mapPullbackAdj [HasPullbacks C] (f : X ‚ü∂ Y) [Mono f] : map f ‚ä£ pullback f :=
  lowerAdjunction (MonoOver.mapPullbackAdj f)

@[simp]
theorem pullback_map_self [HasPullbacks C] (f : X ‚ü∂ Y) [Mono f] (g : Subobject X) :
    (pullback f).obj ((map f).obj g) = g := by
  revert g
  exact Quotient.ind (fun g' => Quotient.sound ‚ü®(MonoOver.pullbackMapSelf f).app _‚ü©)

theorem map_pullback [HasPullbacks C] {X Y Z W : C} {f : X ‚ü∂ Y} {g : X ‚ü∂ Z} {h : Y ‚ü∂ W} {k : Z ‚ü∂ W}
    [Mono h] [Mono g] (comm : f ‚â´ h = g ‚â´ k) (t : IsLimit (PullbackCone.mk f g comm))
    (p : Subobject Y) : (map g).obj ((pullback f).obj p) = (pullback k).obj ((map h).obj p) := by
  revert p
  apply Quotient.ind'
  intro a
  apply Quotient.sound
  apply ThinSkeleton.equiv_of_both_ways
  ¬∑ refine MonoOver.homMk (pullback.lift (pullback.fst _ _) _ ?_) (pullback.lift_snd _ _ _)
    change _ ‚â´ a.arrow ‚â´ h = (pullback.snd _ _ ‚â´ g) ‚â´ _
    rw [assoc, ‚Üê comm, pullback.condition_assoc]
  ¬∑ refine MonoOver.homMk (pullback.lift (pullback.fst _ _)
      (PullbackCone.IsLimit.lift t (pullback.fst _ _ ‚â´ a.arrow) (pullback.snd _ _) _)
      (PullbackCone.IsLimit.lift_fst _ _ _ ?_).symm) ?_
    ¬∑ rw [‚Üê pullback.condition, assoc]
      rfl
    ¬∑ dsimp
      rw [pullback.lift_snd_assoc]
      apply PullbackCone.IsLimit.lift_snd

end Map

section Exists

variable [HasImages C]

/-- The functor from subobjects of `X` to subobjects of `Y` given by
sending the subobject `S` to its "image" under `f`, usually denoted $\exists_f$.
For instance, when `C` is the category of types,
viewing `Subobject X` as `Set X` this is just `Set.image f`.

This functor is left adjoint to the `pullback f` functor (shown in `existsPullbackAdj`)
provided both are defined, and generalises the `map f` functor, again provided it is defined.
-/
def ¬´exists¬ª (f : X ‚ü∂ Y) : Subobject X ‚•§ Subobject Y :=
  lower (MonoOver.exists f)

/-- When `f : X ‚ü∂ Y` is a monomorphism, `exists f` agrees with `map f`.
-/
theorem exists_iso_map (f : X ‚ü∂ Y) [Mono f] : ¬´exists¬ª f = map f :=
  lower_iso _ _ (MonoOver.existsIsoMap f)

/-- `exists f : Subobject X ‚•§ Subobject Y` is
left adjoint to `pullback f : Subobject Y ‚•§ Subobject X`.
-/
def existsPullbackAdj (f : X ‚ü∂ Y) [HasPullbacks C] : ¬´exists¬ª f ‚ä£ pullback f :=
  lowerAdjunction (MonoOver.existsPullbackAdj f)

/--
Taking representatives and then `MonoOver.exists` is isomorphic to taking `Subobject.exists`
and then taking representatives.
-/
def existsCompRepresentativeIso (f : X ‚ü∂ Y) :
    ¬´exists¬ª f ‚ãô representative ‚âÖ representative ‚ãô MonoOver.exists f :=
  lowerCompRepresentativeIso _

/-- `exists f` applied to a subobject `x` is isomorphic to the image of `x.arrow ‚â´ f`. -/
def existsIsoImage (f : X ‚ü∂ Y) (x : Subobject X) :
    ((¬´exists¬ª f).obj x : C) ‚âÖ Limits.image (x.arrow ‚â´ f) :=
  (MonoOver.forget Y ‚ãô Over.forget Y).mapIso <| (existsCompRepresentativeIso f).app x

/-- Given a subobject `x`, the `ImageFactorisation` of `x.arrow ‚â´ f` through `(exists f).obj x`. -/
@[simps! F_I F_m]
def imageFactorisation (f : X ‚ü∂ Y) (x : Subobject X) :
    ImageFactorisation (x.arrow ‚â´ f) :=
  let :=
    ImageFactorisation.ofIsoI
      (Image.imageFactorisation (x.arrow ‚â´ f))
      (existsIsoImage f x).symm
  ImageFactorisation.copy this
    ((¬´exists¬ª f).obj x).arrow
    this.F.e
    (by simpa [this, -Over.w] using (Over.w ((existsCompRepresentativeIso f).app x).hom).symm)

end Exists

end Subobject

end CategoryTheory
