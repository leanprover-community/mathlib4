/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
module

public import Mathlib.CategoryTheory.ObjectProperty.CompleteLattice
public import Mathlib.CategoryTheory.Sites.Hypercover.Zero

/-!
# Locality conditions on object properties

In this file we define locality conditions on object properties in a category. Let `K` be a
precoverage in a category `C` and `P` be an object property that is closed under isomorphisms.

We say that

- `P` is local if for every `X : C`, `P` holds for `X` if and only if it holds for `U·µ¢` for a
  `K`-cover `{U·µ¢}` of `X`.

## Implementation details

The covers appearing in the definitions have index type in the morphism universe of `C`.
-/

@[expose] public section

universe v u

namespace CategoryTheory.ObjectProperty

variable {C : Type u} [Category.{v} C]

/-- An object property is local if it holds for `X` if and only if it holds for all `U·µ¢` where
`{U·µ¢}` is a `K`-cover of `X`. -/
class IsLocal (P : ObjectProperty C) (K : Precoverage C) extends IsClosedUnderIsomorphisms P where
  component {X : C} {R : Presieve X} (hR : R ‚àà K X) {Y : C} (f : Y ‚ü∂ X) (hf : R f) : P X ‚Üí P Y
  of_presieve {X : C} {R : Presieve X} (hR : R ‚àà K X) (H : ‚àÄ ‚¶ÉY : C‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ, R f ‚Üí P Y) : P X

export IsLocal (of_presieve)

variable {P : ObjectProperty C} {K L : Precoverage C}

lemma iff_of_presieve [P.IsLocal K] {X : C} {R : Presieve X} (hR : R ‚àà K X) :
    P X ‚Üî ‚àÄ ‚¶ÉY : C‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ, R f ‚Üí P Y :=
  ‚ü®fun h _ _ hf ‚Ü¶ IsLocal.component hR _ hf h, fun h ‚Ü¶ of_presieve hR h‚ü©

namespace IsLocal

lemma mk_of_zeroHypercover [P.IsClosedUnderIsomorphisms]
    (H : ‚àÄ ‚¶ÉX : C‚¶Ñ (ùí∞ : Precoverage.ZeroHypercover.{max u v} K X),
      P X ‚Üî ‚àÄ i, P (ùí∞.X i)) :
    P.IsLocal K where
  component {X R} hR Y f hf hX := by
    rw [CategoryTheory.Precoverage.mem_iff_exists_zeroHypercover] at hR
    obtain ‚ü®ùí∞, rfl‚ü© := hR
    rw [H ùí∞] at hX
    obtain ‚ü®i‚ü© := hf
    exact hX i
  of_presieve {X R} hR h := by
    rw [CategoryTheory.Precoverage.mem_iff_exists_zeroHypercover] at hR
    obtain ‚ü®ùí∞, rfl‚ü© := hR
    rw [H ùí∞]
    intro i
    exact h ‚ü®i‚ü©

lemma of_le [IsLocal P L] (hle : K ‚â§ L) : IsLocal P K where
  component hR _ f hf hX := component (hle _ hR) f hf hX
  of_presieve hR H := of_presieve (hle _ hR) H

instance top : IsLocal (‚ä§ : ObjectProperty C) K where
  component := by simp
  of_presieve := by simp

instance inf (P Q : ObjectProperty C) [IsLocal P K] [IsLocal Q K] :
    IsLocal (P ‚äì Q) K where
  component hR _ _ hf h := ‚ü®component hR _ hf h.1, component hR _ hf h.2‚ü©
  of_presieve hR h := ‚ü®of_presieve hR fun _ _ hf ‚Ü¶ (h hf).1, of_presieve hR fun _ _ hf ‚Ü¶ (h hf).2‚ü©

end IsLocal

lemma of_zeroHypercover [P.IsLocal K] {X : C} (ùí∞ : K.ZeroHypercover X) (h : ‚àÄ i, P (ùí∞.X i)) : P X :=
  P.of_presieve ùí∞.mem‚ÇÄ fun _ f ‚ü®i‚ü© ‚Ü¶ h i

lemma iff_of_zeroHypercover [P.IsLocal K] {X : C} (ùí∞ : K.ZeroHypercover X) :
    P X ‚Üî ‚àÄ i, P (ùí∞.X i) :=
  ‚ü®fun h i ‚Ü¶ IsLocal.component ùí∞.mem‚ÇÄ _ ‚ü®i‚ü© h, fun h ‚Ü¶ of_zeroHypercover ùí∞ h‚ü©

end CategoryTheory.ObjectProperty
