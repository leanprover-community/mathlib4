/-
Copyright (c) 2017 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Reid Barton, Jo√´l Riou
-/
import Mathlib.CategoryTheory.InducedCategory
import Mathlib.CategoryTheory.ObjectProperty.Basic

/-!
# The full subcategory associated to a property of objects

Given a category `C` and `P : ObjectProperty C`, we define
a category structure on the type `P.FullSubcategory`
of objects in `C` satisfying `P`.

-/

universe v v' u u'

namespace CategoryTheory

namespace ObjectProperty

variable {C : Type u} [Category.{v} C]

section

variable (P : ObjectProperty C)

/--
A subtype-like structure for full subcategories. Morphisms just ignore the property. We don't use
actual subtypes since the simp-normal form `‚ÜëX` of `X.val` does not work well for full
subcategories. -/
@[ext, stacks 001D "We do not define 'strictly full' subcategories."]
structure FullSubcategory where
  /-- The category of which this is a full subcategory -/
  obj : C
  /-- The predicate satisfied by all objects in this subcategory -/
  property : P obj

instance FullSubcategory.category : Category.{v} P.FullSubcategory :=
  inferInstanceAs (Category (InducedCategory _ FullSubcategory.obj))

@[ext]
lemma hom_ext {X Y : P.FullSubcategory} {f g : X ‚ü∂ Y} (h : f.hom = g.hom) : f = g :=
  InducedCategory.hom_ext h

/-- The forgetful functor from a full subcategory into the original category
("forgetting" the condition).
-/
def Œπ : P.FullSubcategory ‚•§ C :=
  inducedFunctor FullSubcategory.obj

@[simp]
theorem Œπ_obj {X} : P.Œπ.obj X = X.obj :=
  rfl

@[simp]
theorem Œπ_map {X Y} {f : X ‚ü∂ Y} : P.Œπ.map f = f.hom :=
  rfl

@[simp]
lemma FullSubcategory.id_hom (X : P.FullSubcategory) :
    InducedCategory.Hom.hom (ùüô X) = ùüô X.obj := rfl

@[simp, reassoc]
lemma FullSubcategory.comp_hom {X Y Z : P.FullSubcategory} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).hom = f.hom ‚â´ g.hom := rfl

@[deprecated (since := "2025-06-26")] alias FullSubcategory.id_def := FullSubcategory.id_hom
@[deprecated (since := "2025-06-26")] alias FullSubcategory.comp_def := FullSubcategory.comp_hom

variable {P} in
/-- Constructor for morphisms in a full subcategory. -/
@[simps]
def homMk {X Y : P.FullSubcategory} (f : X.obj ‚ü∂ Y.obj) : X ‚ü∂ Y where
  hom := f

/-- The inclusion of a full subcategory is fully faithful. -/
abbrev fullyFaithfulŒπ :
    P.Œπ.FullyFaithful where
  preimage f := homMk _

instance full_Œπ : P.Œπ.Full := P.fullyFaithfulŒπ.full
instance faithful_Œπ : P.Œπ.Faithful := P.fullyFaithfulŒπ.faithful

/-- Constructor for isomorphisms in `P.FullSubcategory` when
`P : ObjectProperty C`. -/
@[simps]
def isoMk {X Y : P.FullSubcategory} (e : X.obj ‚âÖ Y.obj) : X ‚âÖ Y where
  hom := homMk e.hom
  inv := homMk e.inv

variable {P}

@[reassoc (attr := simp)]
lemma isoHom_inv_id_hom {X Y : P.FullSubcategory} (e : X ‚âÖ Y) :
    e.hom.hom ‚â´ e.inv.hom = ùüô _ :=
  P.Œπ.congr_map e.hom_inv_id

@[reassoc (attr := simp)]
lemma isoInv_hom_id_hom {X Y : P.FullSubcategory} (e : X ‚âÖ Y) :
    e.inv.hom ‚â´ e.hom.hom = ùüô _ :=
  P.Œπ.congr_map e.inv_hom_id

variable {P' : ObjectProperty C}

/-- If `P` and `P'` are properties of objects such that `P ‚â§ P'`, there is
an induced functor `P.FullSubcategory ‚•§ P'.FullSubcategory`. -/
@[simps]
def ŒπOfLE (h : P ‚â§ P') : P.FullSubcategory ‚•§ P'.FullSubcategory where
  obj X := ‚ü®X.1, h _ X.2‚ü©
  map f := homMk f.hom

/-- If `h : P ‚â§ P'`, then `ŒπOfLE h` is fully faithful. -/
def fullyFaithfulŒπOfLE (h : P ‚â§ P') :
    (ŒπOfLE h).FullyFaithful where
  preimage f := homMk f.hom

instance full_ŒπOfLE (h : P ‚â§ P') : (ŒπOfLE h).Full := (fullyFaithfulŒπOfLE h).full
instance faithful_ŒπOfLE (h : P ‚â§ P') : (ŒπOfLE h).Faithful := (fullyFaithfulŒπOfLE h).faithful

@[deprecated "use ŒπOfLECompŒπIso" (since := "2025-03-04")]
theorem FullSubcategory.map_inclusion (h : P ‚â§ P') : ŒπOfLE h ‚ãô P'.Œπ = P.Œπ := rfl

/-- If `h : P ‚â§ P'` is an inequality of properties of objects,
this is the obvious isomorphism `ŒπOfLE h ‚ãô P'.Œπ ‚âÖ P.Œπ`. -/
def ŒπOfLECompŒπIso (h : P ‚â§ P') : ŒπOfLE h ‚ãô P'.Œπ ‚âÖ P.Œπ := Iso.refl _

end

section lift

variable {D : Type u'} [Category.{v'} D] (P Q : ObjectProperty D)
  (F : C ‚•§ D) (hF : ‚àÄ X, P (F.obj X))

/-- A functor which maps objects to objects satisfying a certain property induces a lift through
    the full subcategory of objects satisfying that property. -/
@[simps]
def lift : C ‚•§ FullSubcategory P where
  obj X := ‚ü®F.obj X, hF X‚ü©
  map f := homMk (F.map f)

@[deprecated "use liftCompŒπIso" (since := "2025-03-04")]
theorem FullSubcategory.lift_comp_inclusion_eq :
    P.lift F hF ‚ãô P.Œπ = F :=
  rfl

/-- Composing the lift of a functor through a full subcategory with the inclusion yields the
    original functor. This is actually true definitionally. -/
def liftCompŒπIso : P.lift F hF ‚ãô P.Œπ ‚âÖ F := Iso.refl _

@[simp]
lemma Œπ_obj_lift_obj (X : C) :
    P.Œπ.obj ((P.lift F hF).obj X) = F.obj X := rfl

@[simp]
lemma Œπ_obj_lift_map {X Y : C} (f : X ‚ü∂ Y) :
    P.Œπ.map ((P.lift F hF).map f) = F.map f := rfl

instance [F.Faithful] : (P.lift F hF).Faithful :=
  Functor.Faithful.of_comp_iso (P.liftCompŒπIso F hF)

instance [F.Full] : (P.lift F hF).Full :=
  Functor.Full.of_comp_faithful_iso (P.liftCompŒπIso F hF)

variable {Q}

/-- When `h : P ‚â§ Q`, this is the canonical isomorphism
`P.lift F hF ‚ãô ŒπOfLE h ‚âÖ Q.lift F _`. -/
def liftCompŒπOfLEIso (h : P ‚â§ Q) :
    P.lift F hF ‚ãô ŒπOfLE h ‚âÖ Q.lift F (fun X ‚Ü¶ h _ (hF X)) := Iso.refl _

@[deprecated "Use liftCompŒπOfLEIso" (since := "2025-03-04")]
theorem FullSubcategory.lift_comp_map (h : P ‚â§ Q) :
    P.lift F hF ‚ãô ŒπOfLE h =
      Q.lift F (fun X ‚Ü¶ h _ (hF X)) :=
  rfl

end lift

end ObjectProperty

@[deprecated (since := "2025-03-04")] alias FullSubcategory := ObjectProperty.FullSubcategory
@[deprecated (since := "2025-03-04")] alias fullSubcategoryInclusion := ObjectProperty.Œπ
@[deprecated (since := "2025-03-04")] alias fullSubcategoryInclusion.obj := ObjectProperty.Œπ_obj
@[deprecated (since := "2025-03-04")] alias fullSubcategoryInclusion.map := ObjectProperty.Œπ_map
@[deprecated (since := "2025-03-04")] alias fullyFaithfulFullSubcategoryInclusion :=
  ObjectProperty.fullyFaithfulŒπ
@[deprecated (since := "2025-03-04")] alias FullSubcategory.map := ObjectProperty.ŒπOfLE
@[deprecated (since := "2025-03-04")] alias FullSubcategory.lift := ObjectProperty.lift
@[deprecated (since := "2025-03-04")] alias FullSubcategory.lift_comp_inclusion :=
  ObjectProperty.liftCompŒπIso
@[deprecated (since := "2025-03-04")] alias fullSubcategoryInclusion_obj_lift_obj :=
  ObjectProperty.Œπ_obj_lift_obj
@[deprecated (since := "2025-03-04")] alias fullSubcategoryInclusion_map_lift_map :=
  ObjectProperty.Œπ_obj_lift_map

end CategoryTheory
