/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.ObjectProperty.FunctorCategory.Limits
import Mathlib.CategoryTheory.ObjectProperty.Local
import Mathlib.CategoryTheory.Limits.FunctorCategory.Basic
import Mathlib.CategoryTheory.Limits.Types.Colimits
import Mathlib.CategoryTheory.Limits.Types.Limits

/-!
# Presheaves of types which preserves a limit

Let `F : J ‚•§ C·µí·µñ` be a functor. We show that a presheaf `P : C·µí·µñ ‚•§ Type w`
preserves the limit of `F` iff `P` is a local object with respect to a suitable
family of morphisms in `C·µí·µñ ‚•§ Type w` (this family contains `1` or `0` morphism
depending on whether the limit of `F` exists or not).

-/

universe w w' v v' u u'

namespace CategoryTheory

open Limits Opposite

section

variable {C : Type u} [Category.{v} C]

namespace FunctorToTypes

protected abbrev Small (F : C ‚•§ Type w') := ‚àÄ (X : C), _root_.Small.{w} (F.obj X)

@[simps]
noncomputable def shrink (F : C ‚•§ Type w') [FunctorToTypes.Small.{w} F] :
    C ‚•§ Type w where
  obj X := Shrink.{w} (F.obj X)
  map {X Y} f := equivShrink.{w} _ ‚àò F.map f ‚àò (equivShrink.{w} _).symm

attribute [local simp] FunctorToTypes.naturality in
@[simps]
noncomputable def shrinkMap {F G : C ‚•§ Type w'} (œÑ : F ‚ü∂ G) [FunctorToTypes.Small.{w} F]
    [FunctorToTypes.Small.{w} G] :
    shrink.{w} F ‚ü∂ shrink.{w} G where
  app X := equivShrink.{w} _ ‚àò œÑ.app X ‚àò (equivShrink.{w} _).symm

end FunctorToTypes

section

variable [LocallySmall.{w} C]

instance (X : C) : FunctorToTypes.Small.{w} (yoneda.obj X) :=
  fun _ ‚Ü¶ by dsimp; infer_instance

-- to be moved
@[pp_with_univ, simps -isSimp obj map]
noncomputable def shrinkYoneda :
    C ‚•§ C·µí·µñ ‚•§ Type w where
  obj X := FunctorToTypes.shrink (yoneda.obj X)
  map f := FunctorToTypes.shrinkMap (yoneda.map f)

noncomputable def shrinkYonedaObjObjEquiv {X : C} {Y : C·µí·µñ} :
    ((shrinkYoneda.{w}.obj X).obj Y) ‚âÉ (Y.unop ‚ü∂ X) :=
  (equivShrink _).symm

noncomputable def shrinkYonedaEquiv {X : C} {P : C·µí·µñ ‚•§ Type w} :
    (shrinkYoneda.{w}.obj X ‚ü∂ P) ‚âÉ P.obj (op X) where
  toFun œÑ := œÑ.app _ (equivShrink.{w} _ (ùüô X))
  invFun x :=
    { app Y f := P.map ((equivShrink.{w} _).symm f).op x
      naturality Y Z g := by ext; simp [shrinkYoneda] }
  left_inv œÑ := by
    ext Y f
    obtain ‚ü®f, rfl‚ü© := (equivShrink _).surjective f
    simpa [shrinkYoneda] using congr_fun (œÑ.naturality f.op).symm (equivShrink _ (ùüô X))
  right_inv x := by simp

lemma map_shrinkYonedaEquiv {X Y : C} {P : C·µí·µñ ‚•§ Type w} (f : shrinkYoneda.obj X ‚ü∂ P)
    (g : Y ‚ü∂ X) : P.map g.op (shrinkYonedaEquiv f) =
      f.app (op Y) (shrinkYonedaObjObjEquiv.symm g) := by
  simp [shrinkYonedaObjObjEquiv, shrinkYonedaEquiv, shrinkYoneda,
    ‚Üê FunctorToTypes.naturality]

lemma shrinkYonedaEquiv_shrinkYoneda_map {X Y : C} (f : X ‚ü∂ Y) :
    shrinkYonedaEquiv (shrinkYoneda.{w}.map f) = shrinkYonedaObjObjEquiv.symm f := by
  simp [shrinkYonedaEquiv, shrinkYoneda, shrinkYonedaObjObjEquiv]

lemma shrinkYonedaEquiv_comp {X : C} {P Q : C·µí·µñ ‚•§ Type w} (Œ± : shrinkYoneda.obj X ‚ü∂ P)
    (Œ≤ : P ‚ü∂ Q) :
    shrinkYonedaEquiv (Œ± ‚â´ Œ≤) = Œ≤.app _ (shrinkYonedaEquiv Œ±) := by
  simp [shrinkYonedaEquiv]

lemma shrinkYonedaEquiv_naturality {X Y : C} {P : C·µí·µñ ‚•§ Type w}
    (f : shrinkYoneda.obj X ‚ü∂ P) (g : Y ‚ü∂ X) :
    P.map g.op (shrinkYonedaEquiv f) = shrinkYonedaEquiv (shrinkYoneda.map g ‚â´ f) := by
  simpa [shrinkYonedaEquiv, shrinkYoneda]
    using congr_fun (f.naturality g.op).symm ((equivShrink _) (ùüô _))

@[reassoc]
lemma shrinkYonedaEquiv_symm_map {X Y : C·µí·µñ} (f : X ‚ü∂ Y) {P : C·µí·µñ ‚•§ Type w} (t : P.obj X) :
    shrinkYonedaEquiv.symm (P.map f t) =
      shrinkYoneda.map f.unop ‚â´ shrinkYonedaEquiv.symm t :=
  shrinkYonedaEquiv.injective (by
    obtain ‚ü®t, rfl‚ü© := shrinkYonedaEquiv.surjective t
    rw [‚Üê shrinkYonedaEquiv_naturality]
    simp)

@[reassoc]
lemma shrinkYonedaEquiv_symm_comp {X : C·µí·µñ} {P Q : C·µí·µñ ‚•§ Type w} (x : P.obj X) (Œ± : P ‚ü∂ Q) :
    shrinkYonedaEquiv.symm x ‚â´ Œ± = shrinkYonedaEquiv.symm (Œ±.app _ x) :=
  shrinkYonedaEquiv.injective (by simp [shrinkYonedaEquiv])

end

end

namespace Presheaf

section

variable {C : Type u} [Category.{v} C]
  {J : Type u'} [Category.{v'} J] [LocallySmall.{w} C]
  {F : J ‚•§ C·µí·µñ} (c : Cone F) {c' : Cocone (F.leftOp ‚ãô shrinkYoneda.{w})}
  (hc : IsLimit c) (hc' : IsColimit c') (P : C·µí·µñ ‚•§ Type w)

variable {P} in
@[simps -isSimp symm_apply apply_coe]
noncomputable def coconeCompShrinkYonedaHomEquiv :
    (c'.pt ‚ü∂ P) ‚âÉ (F ‚ãô P).sections where
  toFun f :=
    { val j := shrinkYonedaEquiv (c'.Œπ.app (op j) ‚â´ f)
      property {X X'} g := by
        have h‚ÇÅ := c'.w g.op
        dsimp at h‚ÇÅ ‚ä¢
        rw [‚Üê h‚ÇÅ, Category.assoc]
        conv_rhs => rw [shrinkYonedaEquiv_comp]
        erw [map_shrinkYonedaEquiv]
        rw [shrinkYonedaEquiv_shrinkYoneda_map]
        rfl }
  invFun s := hc'.desc (Cocone.mk _
    { app j := shrinkYonedaEquiv.symm (s.val j.unop)
      naturality j‚ÇÅ j‚ÇÇ f := by
        rw [‚Üê s.property f.unop]
        dsimp
        rw [shrinkYonedaEquiv_symm_map, Category.comp_id] })
  left_inv f := hc'.hom_ext (by simp)
  right_inv u := by ext; simp

noncomputable def coconePtToShrinkYoneda :
    c'.pt ‚ü∂ shrinkYoneda.{w}.obj c.pt.unop :=
  hc'.desc (shrinkYoneda.{w}.mapCocone (coconeLeftOpOfCone c))

variable {P} in
@[reassoc]
lemma coconePtToShrinkYoneda_comp (x : P.obj c.pt) :
    coconePtToShrinkYoneda c hc' ‚â´ shrinkYonedaEquiv.symm x =
      (coconeCompShrinkYonedaHomEquiv hc').symm
        (Types.sectionOfCone (P.mapCone c) x) := by
  refine hc'.hom_ext (fun j ‚Ü¶ ?_)
  dsimp [coconePtToShrinkYoneda, coconeCompShrinkYonedaHomEquiv_symm_apply]
  rw [hc'.fac_assoc, hc'.fac]
  dsimp
  rw [shrinkYonedaEquiv_symm_map]

lemma nonempty_isLimit_mapCone_iff :
    Nonempty (IsLimit (P.mapCone c)) ‚Üî
      (MorphismProperty.single (coconePtToShrinkYoneda c hc')).isLocal P := by
  -- this should be a separate lemma
  have h : (MorphismProperty.single (coconePtToShrinkYoneda c hc')).isLocal P ‚Üî
      (Function.Bijective (fun (f : _ ‚ü∂ P) ‚Ü¶ coconePtToShrinkYoneda c hc' ‚â´ f)) :=
    ‚ü®fun h ‚Ü¶ h _ ‚ü®‚ü®‚ü©‚ü©, fun h ‚Ü¶ by rintro _ _ _ ‚ü®_‚ü©; exact h‚ü©
  rw [Types.isLimit_iff_bijective_sectionOfCone, h, ‚Üê Function.Bijective.of_comp_iff'
    (coconeCompShrinkYonedaHomEquiv hc').symm.bijective,
    ‚Üê Function.Bijective.of_comp_iff _ shrinkYonedaEquiv.bijective]
  convert Iff.rfl using 2
  ext : 1
  simp [‚Üê coconePtToShrinkYoneda_comp]

variable {c}

include hc in
lemma preservesLimit_eq_isLocal_single :
    Functor.preservesLimit (Type w) F =
      (MorphismProperty.single (coconePtToShrinkYoneda c hc')).isLocal := by
  ext P
  rw [‚Üê nonempty_isLimit_mapCone_iff c hc' P]
  exact ‚ü®fun _ ‚Ü¶ ‚ü®isLimitOfPreserves P hc‚ü©,
    fun ‚ü®h‚ü© ‚Ü¶ preservesLimit_of_preserves_limit_cone hc h‚ü©

variable (F)

variable [Small.{w} J]
noncomputable def preservesLimitHomFamilySrc :=
  colimit (F.leftOp ‚ãô shrinkYoneda)

noncomputable def preservesLimitHomFamilyTgt (h : PLift (HasLimit F)) :=
  letI := h.down
  shrinkYoneda.obj (limit F).unop

--coconePtToShrinkYoneda (limit.cone F) (colimit.isColimit _)
noncomputable def preservesLimitHomFamily (h : PLift (HasLimit F)) :
    preservesLimitHomFamilySrc F ‚ü∂ preservesLimitHomFamilyTgt F h :=
  letI := h.down
  coconePtToShrinkYoneda (limit.cone F) (colimit.isColimit _)

lemma preservesLimit_eq_isLocal :
    Functor.preservesLimit (Type w) F =
      (MorphismProperty.ofHoms (preservesLimitHomFamily F)).isLocal := by
  ext G
  by_cases hF : HasLimit F
  ¬∑ rw [preservesLimit_eq_isLocal_single (limit.isLimit F) (colimit.isColimit _)]
    convert Iff.rfl
    ext _ _ f
    exact ‚ü®fun ‚ü®_‚ü© ‚Ü¶ ‚ü®‚ü®‚ü©‚ü©, fun ‚ü®_‚ü© ‚Ü¶ ‚ü®‚ü®hF‚ü©‚ü©‚ü©
  ¬∑ exact ‚ü®fun _ _ _ _ ‚ü®h‚ü© ‚Ü¶ (hF h.down).elim,
      fun _ ‚Ü¶ ‚ü®fun hc ‚Ü¶ (hF ‚ü®_, hc‚ü©).elim‚ü©‚ü©

lemma preservesLimitsOfShape_eq_isLocal :
    Functor.preservesLimitsOfShape C·µí·µñ (Type w) J =
      (‚®Ü (F : J ‚•§ C·µí·µñ), MorphismProperty.ofHoms (preservesLimitHomFamily F)).isLocal := by
  simp only [Functor.preservesLimitsOfShape,
    MorphismProperty.isLocal_iSup, preservesLimit_eq_isLocal]

end

end Presheaf

end CategoryTheory
