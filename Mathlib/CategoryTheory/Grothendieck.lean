/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Sina Hazratpour
-/
import Mathlib.CategoryTheory.Category.Cat.AsSmall
import Mathlib.CategoryTheory.Elements
import Mathlib.CategoryTheory.Comma.Over

/-!
# The Grothendieck construction

Given a functor `F : C â¥¤ Cat`, the objects of `Grothendieck F`
consist of dependent pairs `(b, f)`, where `b : C` and `f : F.obj c`,
and a morphism `(b, f) âŸ¶ (b', f')` is a pair `Î² : b âŸ¶ b'` in `C`, and
`Ï† : (F.map Î²).obj f âŸ¶ f'`

`Grothendieck.functor` makes the Grothendieck construction into a functor from the functor category
`C â¥¤ Cat` to the over category `Over C` in the category of categories.

Categories such as `PresheafedSpace` are in fact examples of this construction,
and it may be interesting to try to generalize some of the development there.

## Implementation notes

Really we should treat `Cat` as a 2-category, and allow `F` to be a 2-functor.

There is also a closely related construction starting with `G : Cáµ’áµ– â¥¤ Cat`,
where morphisms consists again of `Î² : b âŸ¶ b'` and `Ï† : f âŸ¶ (F.map (op Î²)).obj f'`.

## Notable constructions

- `Grothendieck F` is the Grothendieck construction.
- Elements of `Grothendieck F` whose base is `c : C` can be transported along `f : c âŸ¶ d` using
`transport`.
- A natural transformation `Î± : F âŸ¶ G` induces `map Î± : Grothendieck F â¥¤ Grothendieck G`.
- The Grothendieck construction and `map` together form a functor (`functor`) from the functor
category `E â¥¤ Cat` to the over category `Over E`.
- A functor `G : D â¥¤ C` induces `pre F G : Grothendieck (G â‹™ F) â¥¤ Grothendieck F`.

## References

See also `CategoryTheory.Functor.Elements` for the category of elements of functor `F : C â¥¤ Type`.

* https://stacks.math.columbia.edu/tag/02XV
* https://ncatlab.org/nlab/show/Grothendieck+construction

-/


universe w u v uâ‚ vâ‚ uâ‚‚ vâ‚‚

namespace CategoryTheory

variable {C : Type u} [Category.{v} C]
variable {D : Type uâ‚} [Category.{vâ‚} D]
variable (F : C â¥¤ Cat.{vâ‚‚, uâ‚‚})

/--
The Grothendieck construction (often written as `âˆ« F` in mathematics) for a functor `F : C â¥¤ Cat`
gives a category whose
* objects `X` consist of `X.base : C` and `X.fiber : F.obj base`
* morphisms `f : X âŸ¶ Y` consist of
  `base : X.base âŸ¶ Y.base` and
  `f.fiber : (F.map base).obj X.fiber âŸ¶ Y.fiber`
-/
-- Porting note (https://github.com/leanprover-community/mathlib4/issues/5171): no such linter yet
-- @[nolint has_nonempty_instance]
structure Grothendieck where
  /-- The underlying object in `C` -/
  base : C
  /-- The object in the fiber of the base object. -/
  fiber : F.obj base

namespace Grothendieck

variable {F}

/-- A morphism in the Grothendieck category `F : C â¥¤ Cat` consists of
`base : X.base âŸ¶ Y.base` and `f.fiber : (F.map base).obj X.fiber âŸ¶ Y.fiber`.
-/
structure Hom (X Y : Grothendieck F) where
  /-- The morphism between base objects. -/
  base : X.base âŸ¶ Y.base
  /-- The morphism from the pushforward to the source fiber object to the target fiber object. -/
  fiber : (F.map base).obj X.fiber âŸ¶ Y.fiber

@[ext (iff := false)]
theorem ext {X Y : Grothendieck F} (f g : Hom X Y) (w_base : f.base = g.base)
    (w_fiber : eqToHom (by rw [w_base]) â‰« f.fiber = g.fiber) : f = g := by
  cases f; cases g
  congr
  dsimp at w_base
  aesop_cat

/-- The identity morphism in the Grothendieck category.
-/
def id (X : Grothendieck F) : Hom X X where
  base := ğŸ™ X.base
  fiber := eqToHom (by erw [CategoryTheory.Functor.map_id, Functor.id_obj X.fiber])

instance (X : Grothendieck F) : Inhabited (Hom X X) :=
  âŸ¨id XâŸ©

/-- Composition of morphisms in the Grothendieck category.
-/
def comp {X Y Z : Grothendieck F} (f : Hom X Y) (g : Hom Y Z) : Hom X Z where
  base := f.base â‰« g.base
  fiber :=
    eqToHom (by erw [Functor.map_comp, Functor.comp_obj]) â‰« (F.map g.base).map f.fiber â‰« g.fiber

attribute [local simp] eqToHom_map

instance : Category (Grothendieck F) where
  Hom X Y := Grothendieck.Hom X Y
  id X := Grothendieck.id X
  comp := @fun _ _ _ f g => Grothendieck.comp f g
  comp_id := @fun X Y f => by
    dsimp; ext
    Â· simp [comp, id]
    Â· dsimp [comp, id]
      rw [â† NatIso.naturality_2 (eqToIso (F.map_id Y.base)) f.fiber]
      simp
  id_comp := @fun X Y f => by dsimp; ext <;> simp [comp, id]
  assoc := @fun W X Y Z f g h => by
    dsimp; ext
    Â· simp [comp, id]
    Â· dsimp [comp, id]
      rw [â† NatIso.naturality_2 (eqToIso (F.map_comp _ _)) f.fiber]
      simp

@[simp]
theorem id_base (X : Grothendieck F) :
    Hom.base (ğŸ™ X) = ğŸ™ X.base := by
  rfl

@[simp]
theorem id_fiber (X : Grothendieck F) :
    Hom.fiber (ğŸ™ X) = eqToHom (by erw [CategoryTheory.Functor.map_id, Functor.id_obj X.fiber]) :=
  rfl

@[simp]
theorem comp_base {X Y Z : Grothendieck F} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    (f â‰« g).base = f.base â‰« g.base :=
  rfl

@[simp]
theorem comp_fiber {X Y Z : Grothendieck F} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    Hom.fiber (f â‰« g) =
    eqToHom (by erw [Functor.map_comp, Functor.comp_obj]) â‰«
    (F.map g.base).map f.fiber â‰« g.fiber :=
  rfl


theorem congr {X Y : Grothendieck F} {f g : X âŸ¶ Y} (h : f = g) :
    f.fiber = eqToHom (by subst h; rfl) â‰« g.fiber := by
  subst h
  dsimp
  simp

lemma eqToHom_eq {X Y : Grothendieck F} (hF : X = Y) :
    eqToHom hF = { base := eqToHom (by subst hF; rfl), fiber := eqToHom (by subst hF; simp) } := by
  subst hF
  rfl

section Transport

/--
If `F : C â¥¤ Cat` is a functor and `t : c âŸ¶ d` is a morphism in `C`, then `transport` maps each
`c`-based element of `Grothendieck F` to a `d`-based element.
-/
def transport (x : Grothendieck F) {c : C} (t : x.base âŸ¶ c) : Grothendieck F :=
  âŸ¨c, (F.map t).obj x.fiberâŸ©

/--
If `F : C â¥¤ Cat` is a functor and `t : c âŸ¶ d` is a morphism in `C`, then `transport` maps each
`c`-based element `x` of `Grothendieck F` to a `d`-based element `x.transport t`.

`transport_hom` is the morphism `x âŸ¶ x.transport t` induced by `t` and the identity on fibers.
-/
def transport_hom (x : Grothendieck F) {c : C} (t : x.base âŸ¶ c) : x âŸ¶ x.transport t :=
  âŸ¨t, CategoryStruct.id _âŸ©

/--
If `F : C â¥¤ Cat` and `x : Grothendieck F`, then every `C`-isomorphism `Î± : x.base â‰… c` induces
an isomorphism between `x` and its transport along `Î±`
-/
@[simps]
def transportIso (x : Grothendieck F) {c : C} (Î± : x.base â‰… c) :
    x.transport Î±.hom â‰… x := by
  refine âŸ¨?_, x.transport_hom Î±.hom, ?_, ?_âŸ©
  Â· refine âŸ¨Î±.inv, eqToHom ?_âŸ©
    simp only [transport]
    rw [â† Functor.comp_obj, â† Cat.comp_eq_comp]
    simp
  Â· apply Grothendieck.ext <;> simp [transport_hom]
  Â· apply Grothendieck.ext <;> simp [transport_hom]

end Transport
section

variable (F)

/-- The forgetful functor from `Grothendieck F` to the source category. -/
@[simps!]
def forget : Grothendieck F â¥¤ C where
  obj X := X.1
  map := @fun _ _ f => f.1

end

section

variable {G : C â¥¤ Cat}

/-- The Grothendieck construction is functorial: a natural transformation `Î± : F âŸ¶ G` induces
a functor `Grothendieck.map : Grothendieck F â¥¤ Grothendieck G`.
-/
@[simps!]
def map (Î± : F âŸ¶ G) : Grothendieck F â¥¤ Grothendieck G where
  obj X :=
  { base := X.base
    fiber := (Î±.app X.base).obj X.fiber }
  map {X Y} f :=
  { base := f.base
    fiber := (eqToHom (Î±.naturality f.base).symm).app X.fiber â‰« (Î±.app Y.base).map f.fiber }
  map_id X := by simp only [Cat.eqToHom_app, id_fiber, eqToHom_map, eqToHom_trans]; rfl
  map_comp {X Y Z} f g := by
    dsimp
    congr 1
    simp only [comp_fiber f g, â† Category.assoc, Functor.map_comp, eqToHom_map]
    congr 1
    simp only [Cat.eqToHom_app, Cat.comp_obj, eqToHom_trans, eqToHom_map, Category.assoc]
    erw [Functor.congr_hom (Î±.naturality g.base).symm f.fiber]
    simp

theorem map_obj {Î± : F âŸ¶ G} (X : Grothendieck F) :
    (Grothendieck.map Î±).obj X = âŸ¨X.base, (Î±.app X.base).obj X.fiberâŸ© := rfl

theorem map_map {Î± : F âŸ¶ G} {X Y : Grothendieck F} {f : X âŸ¶ Y} :
    (Grothendieck.map Î±).map f =
    âŸ¨f.base, (eqToHom (Î±.naturality f.base).symm).app X.fiber â‰« (Î±.app Y.base).map f.fiberâŸ© := rfl

/-- The functor `Grothendieck.map Î± : Grothendieck F â¥¤ Grothendieck G` lies over `C`.-/
theorem functor_comp_forget {Î± : F âŸ¶ G} :
    Grothendieck.map Î± â‹™ Grothendieck.forget G = Grothendieck.forget F := rfl

theorem map_id_eq : map (ğŸ™ F) = ğŸ™ (Cat.of <| Grothendieck <| F) := by
  fapply Functor.ext
  Â· intro X
    rfl
  Â· intro X Y f
    simp [map_map]
    rfl

/-- Making the equality of functors into an isomorphism. Note: we should avoid equality of functors
if possible, and we should prefer `map_id_iso` to `map_id_eq` whenever we can. -/
def mapIdIso : map (ğŸ™ F) â‰… ğŸ™ (Cat.of <| Grothendieck <| F) := eqToIso map_id_eq

variable {H : C â¥¤ Cat}

theorem map_comp_eq (Î± : F âŸ¶ G) (Î² : G âŸ¶ H) :
    map (Î± â‰« Î²) = map Î± â‹™ map Î² := by
  fapply Functor.ext
  Â· intro X
    rfl
  Â· intro X Y f
    simp only [map_map, map_obj_base, NatTrans.comp_app, Cat.comp_obj, Cat.comp_map,
      eqToHom_refl, Functor.comp_map, Functor.map_comp, Category.comp_id, Category.id_comp]
    fapply Grothendieck.ext
    Â· rfl
    Â· simp

/-- Making the equality of functors into an isomorphism. Note: we should avoid equality of functors
if possible, and we should prefer `map_comp_iso` to `map_comp_eq` whenever we can. -/
def mapCompIso (Î± : F âŸ¶ G) (Î² : G âŸ¶ H) : map (Î± â‰« Î²) â‰… map Î± â‹™ map Î² := eqToIso (map_comp_eq Î± Î²)

variable (F)

/-- The inverse functor to build the equivalence `compAsSmallFunctorEquivalence`. -/
@[simps]
def compAsSmallFunctorEquivalenceInverse :
    Grothendieck F â¥¤ Grothendieck (F â‹™ Cat.asSmallFunctor.{w}) where
  obj X := âŸ¨X.base, AsSmall.up.obj X.fiberâŸ©
  map f := âŸ¨f.base, AsSmall.up.map f.fiberâŸ©

/-- The functor to build the equivalence `compAsSmallFunctorEquivalence`. -/
@[simps]
def compAsSmallFunctorEquivalenceFunctor :
    Grothendieck (F â‹™ Cat.asSmallFunctor.{w}) â¥¤ Grothendieck F where
  obj X := âŸ¨X.base, AsSmall.down.obj X.fiberâŸ©
  map f := âŸ¨f.base, AsSmall.down.map f.fiberâŸ©
  map_id _ := by apply Grothendieck.ext <;> simp
  map_comp _ _ := by apply Grothendieck.ext <;> simp [down_comp]

/-- Taking the Grothendieck construction on `F â‹™ asSmallFunctor`, where
`asSmallFunctor : Cat â¥¤ Cat` is the functor which turns each category into a small category of a
(potentiall) larger universe, is equivalent to the Grothendieck construction on `F` itself. -/
@[simps]
def compAsSmallFunctorEquivalence :
    Grothendieck (F â‹™ Cat.asSmallFunctor.{w}) â‰Œ Grothendieck F where
  functor := compAsSmallFunctorEquivalenceFunctor F
  inverse := compAsSmallFunctorEquivalenceInverse F
  counitIso := Iso.refl _
  unitIso := Iso.refl _

/-- Mapping a Grothendieck construction along the whiskering of any natural transformation
`Î± : F âŸ¶ G` with the functor `asSmallFunctor : Cat â¥¤ Cat` is naturally isomorphic to conjugating
`map Î±` with the equivalence between `Grothendieck (F â‹™ asSmallFunctor)` and `Grothendieck F`. -/
def mapWhiskerRightAsSmallFunctor (Î± : F âŸ¶ G) :
    map (whiskerRight Î± Cat.asSmallFunctor.{w}) â‰…
    (compAsSmallFunctorEquivalence F).functor â‹™ map Î± â‹™
      (compAsSmallFunctorEquivalence G).inverse :=
  NatIso.ofComponents
    (fun X => Iso.refl _)
    (fun f => by
      fapply Grothendieck.ext
      Â· simp [compAsSmallFunctorEquivalenceInverse]
      Â· simp only [compAsSmallFunctorEquivalence_functor, compAsSmallFunctorEquivalence_inverse,
          Functor.comp_obj, compAsSmallFunctorEquivalenceInverse_obj_base, map_obj_base,
          compAsSmallFunctorEquivalenceFunctor_obj_base, Cat.asSmallFunctor_obj, Cat.of_Î±,
          Iso.refl_hom, Functor.comp_map, comp_base, id_base,
          compAsSmallFunctorEquivalenceInverse_map_base, map_map_base,
          compAsSmallFunctorEquivalenceFunctor_map_base, Cat.asSmallFunctor_map, map_obj_fiber,
          whiskerRight_app, AsSmall.down_obj, AsSmall.up_obj_down,
          compAsSmallFunctorEquivalenceInverse_obj_fiber,
          compAsSmallFunctorEquivalenceFunctor_obj_fiber, comp_fiber, map_map_fiber,
          AsSmall.down_map, down_comp, eqToHom_down, AsSmall.up_map_down, Functor.map_comp,
          eqToHom_map, id_fiber, Category.assoc, eqToHom_trans_assoc,
          compAsSmallFunctorEquivalenceInverse_map_fiber,
          compAsSmallFunctorEquivalenceFunctor_map_fiber, eqToHom_comp_iff, comp_eqToHom_iff]
        simp only [eqToHom_trans_assoc, Category.assoc, conj_eqToHom_iff_heq']
        rw [G.map_id]
        simp )

end

/-- The Grothendieck construction as a functor from the functor category `E â¥¤ Cat` to the
over category `Over E`. -/
def functor {E : Cat.{v,u}} : (E â¥¤ Cat.{v,u}) â¥¤ Over (T := Cat.{v,u}) E where
  obj F := Over.mk (X := E) (Y := Cat.of (Grothendieck F)) (Grothendieck.forget F)
  map {_ _} Î± := Over.homMk (X:= E) (Grothendieck.map Î±) Grothendieck.functor_comp_forget
  map_id F := by
    ext
    exact Grothendieck.map_id_eq (F := F)
  map_comp Î± Î² := by
    simp [Grothendieck.map_comp_eq Î± Î²]
    rfl

variable (G : C â¥¤ Type w)

/-- Auxiliary definition for `grothendieckTypeToCat`, to speed up elaboration. -/
@[simps!]
def grothendieckTypeToCatFunctor : Grothendieck (G â‹™ typeToCat) â¥¤ G.Elements where
  obj X := âŸ¨X.1, X.2.asâŸ©
  map f := âŸ¨f.1, f.2.1.1âŸ©

/-- Auxiliary definition for `grothendieckTypeToCat`, to speed up elaboration. -/
-- Porting note:
-- `simps` is incorrectly producing Prop-valued projections here,
-- so we manually specify which ones to produce.
-- See https://leanprover.zulipchat.com/#narrow/stream/144837-PR-reviews/topic/!4.233204.20simps.20bug.20.28Grothendieck.20construction.29
@[simps! obj_base obj_fiber_as map_base]
def grothendieckTypeToCatInverse : G.Elements â¥¤ Grothendieck (G â‹™ typeToCat) where
  obj X := âŸ¨X.1, âŸ¨X.2âŸ©âŸ©
  map f := âŸ¨f.1, âŸ¨âŸ¨f.2âŸ©âŸ©âŸ©

/-- The Grothendieck construction applied to a functor to `Type`
(thought of as a functor to `Cat` by realising a type as a discrete category)
is the same as the 'category of elements' construction.
-/
-- See porting note on grothendieckTypeToCatInverse.
-- We just want to turn off grothendieckTypeToCat_inverse_map_fiber_down_down,
-- so have to list the complement here for `@[simps]`.
@[simps! functor_obj_fst functor_obj_snd functor_map_coe inverse_obj_base inverse_obj_fiber_as
  inverse_map_base unitIso_hom_app_base unitIso_hom_app_fiber unitIso_inv_app_base
  unitIso_inv_app_fiber counitIso_hom_app_coe counitIso_inv_app_coe]
def grothendieckTypeToCat : Grothendieck (G â‹™ typeToCat) â‰Œ G.Elements where
  functor := grothendieckTypeToCatFunctor G
  inverse := grothendieckTypeToCatInverse G
  unitIso :=
    NatIso.ofComponents
      (fun X => by
        rcases X with âŸ¨_, âŸ¨âŸ©âŸ©
        exact Iso.refl _)
      (by
        rintro âŸ¨_, âŸ¨âŸ©âŸ© âŸ¨_, âŸ¨âŸ©âŸ© âŸ¨base, âŸ¨âŸ¨fâŸ©âŸ©âŸ©
        dsimp at *
        simp
        rfl)
  counitIso :=
    NatIso.ofComponents
      (fun X => by
        cases X
        exact Iso.refl _)
      (by
        rintro âŸ¨âŸ© âŸ¨âŸ© âŸ¨f, eâŸ©
        dsimp at *
        simp
        rfl)
  functor_unitIso_comp := by
    rintro âŸ¨_, âŸ¨âŸ©âŸ©
    dsimp
    simp
    rfl

section Pre

variable (F)

/-- Applying a functor `G : D â¥¤ C` to the base of the Grothendieck construction induces a functor
`Grothendieck (G â‹™ F) â¥¤ Grothendieck F`. -/
@[simps]
def pre (G : D â¥¤ C) : Grothendieck (G â‹™ F) â¥¤ Grothendieck F where
  obj X := âŸ¨G.obj X.base, X.fiberâŸ©
  map f := âŸ¨G.map f.base, f.fiberâŸ©
  map_id X := Grothendieck.ext _ _ (G.map_id _) (by simp)
  map_comp f g := Grothendieck.ext _ _ (G.map_comp _ _) (by simp)

@[simp]
theorem pre_id : pre F (ğŸ­ C) = ğŸ­ _ := rfl

/--
An natural isomorphism between functors `G â‰… H` induces a natural isomorphism between the canonical
morphism `pre F G` and `pre F H`, up to composition with
`Grothendieck (G â‹™ F) â¥¤ Grothendieck (H â‹™ F)`.
-/
def preNatIso {G H : D â¥¤ C} (Î± : G â‰… H) :
    pre F G â‰… (map (whiskerRight Î±.hom F)) â‹™ (pre F H) :=
  NatIso.ofComponents
    (fun X => (transportIso âŸ¨G.obj X.base, X.fiberâŸ© (Î±.app X.base)).symm)
    (fun f => by fapply Grothendieck.ext <;> simp [transport_hom])

/--
Given an equivalence of categories `G`, `preInv _ G` is the (weak) inverse of the `pre _ G.functor`.
-/
def preInv (G : D â‰Œ C) : Grothendieck F â¥¤ Grothendieck (G.functor â‹™ F) := by
  refine map ?_ â‹™ Grothendieck.pre (G.functor â‹™ F) G.inverse
  rw [â† Functor.assoc]
  exact eqToHom (Functor.id_comp F) â‰« (whiskerRight G.counitInv F)

variable {F} in
lemma pre_comp_map (G: D â¥¤ C) {H : C â¥¤ Cat} (Î± : F âŸ¶ H) :
    pre F G â‹™ map Î± = map (whiskerLeft G Î±) â‹™ pre H G := rfl

variable {E : Type*} [Category E] in
@[simp]
lemma pre_comp (G : D â¥¤ C) (H : E â¥¤ D) : pre F (H â‹™ G) = pre (G â‹™ F) H â‹™ pre F G := rfl

/--
Let `G` be an equivalence of categories. The functor induced via `pre` by `G.functor â‹™ G.inverse`
is naturally isomorphic to the functor induced via `map` by a whiskered version of `G`'s inverse
unit.
-/
protected def preUnitIso (G : D â‰Œ C) :
    map (whiskerRight G.unitInv _) â‰… pre (G.functor â‹™ F) (G.functor â‹™ G.inverse) :=
  preNatIso _ G.unitIso.symm |>.symm

/--
Given a functor `F : C â¥¤ Cat` and an equivalence of categories `G : D â‰Œ C`, the functor
`pre F G.functor` is an equivalence between `Grothendieck (G.functor â‹™ F)` and `Grothendieck F`.
-/
def preEquivalence (G : D â‰Œ C) : Grothendieck (G.functor â‹™ F) â‰Œ Grothendieck F := by
  refine Equivalence.mk (pre F G.functor) (preInv F G) ?_ ?_
  Â· simp only [preInv, eqToHom_refl, Category.id_comp, eq_mpr_eq_cast, cast_eq]
    erw [â† Functor.assoc, pre_comp_map, Functor.assoc]
    simp only [Functor.assoc, â† pre_comp]
    calc
      _ = map (ğŸ™ _) := map_id_eq.symm
      _ = map _ := ?_
      _ = map _ â‹™ map _ := map_comp_eq _ _
      _ â‰… _ := Grothendieck.preUnitIso F G |> isoWhiskerLeft _
    congr
    ext X
    simp only [Functor.comp_obj, Functor.comp_map, â† Functor.map_comp, Functor.id_obj,
      Functor.map_id, NatTrans.comp_app, NatTrans.id_app, whiskerLeft_app, whiskerRight_app,
      Equivalence.counitInv_functor_comp]
  Â· simp only [preInv, eqToHom_refl, Category.id_comp, eq_mpr_eq_cast, cast_eq, Functor.assoc,
    â† pre_comp]
    exact preNatIso F G.counitIso.symm |>.symm

variable {F} in
/--
Let `F : C â¥¤ Cat` be a functor, `G : D â‰Œ C` an equivalence and `Î± : F âŸ¶ F` a natural transformation.

Left-whiskering `Î±` by `G` and then taking the Grothendieck construction is, up to isomorphism,
the same as taking the Grothendieck construction of `Î±` and then conjugating with `pre F G`.
-/
def mapWhiskerLeftIsoConjPreMap (G : D â‰Œ C) (Î± : F âŸ¶ F) :
    map (whiskerLeft G.functor Î±) â‰…
      (preEquivalence F G).functor â‹™ map Î± â‹™ (preEquivalence F G).inverse := by
  apply Iso.symm
  apply preEquivalence F G |>.congrRight.fullyFaithfulFunctor.preimageIso
  exact isoWhiskerLeft ((preEquivalence F G).functor â‹™ map Î±) (preEquivalence F G).counitIso

end Pre

section FunctorFrom

variable {E : Type*} [Category E]

variable (F) in
/-- The inclusion of a fiber `F.obj c` of a functor `F : C â¥¤ Cat` into its Grothendieck
construction.-/
@[simps obj map]
def Î¹ (c : C) : F.obj c â¥¤ Grothendieck F where
  obj d := âŸ¨c, dâŸ©
  map f := âŸ¨ğŸ™ _, eqToHom (by simp) â‰« fâŸ©
  map_id d := by
    dsimp
    congr
    simp only [Category.comp_id]
  map_comp f g := by
    apply Grothendieck.ext _ _ (by simp)
    simp only [comp_base, â† Category.assoc, eqToHom_trans, comp_fiber, Functor.map_comp,
      eqToHom_map]
    congr 1
    simp only [eqToHom_comp_iff, Category.assoc, eqToHom_trans_assoc]
    apply Functor.congr_hom (F.map_id _).symm

instance faithful_Î¹ (c : C) : (Î¹ F c).Faithful where
  map_injective f := by
    injection f with _ f
    rwa [cancel_epi] at f

/-- Every morphism `f : X âŸ¶ Y` in the base category induces a natural transformation from the fiber
inclusion `Î¹ F X` to the composition `F.map f â‹™ Î¹ F Y`. -/
@[simps]
def Î¹NatTrans {X Y : C} (f : X âŸ¶ Y) : Î¹ F X âŸ¶ F.map f â‹™ Î¹ F Y where
  app d := âŸ¨f, ğŸ™ _âŸ©
  naturality _ _ _ := by
    simp only [Î¹, Functor.comp_obj, Functor.comp_map]
    exact Grothendieck.ext _ _ (by simp) (by simp [eqToHom_map])

variable (fib : âˆ€ c, F.obj c â¥¤ E) (hom : âˆ€ {c c' : C} (f : c âŸ¶ c'), fib c âŸ¶ F.map f â‹™ fib c')
variable (hom_id : âˆ€ c, hom (ğŸ™ c) = eqToHom (by simp only [Functor.map_id]; rfl))
variable (hom_comp : âˆ€ câ‚ câ‚‚ câ‚ƒ (f : câ‚ âŸ¶ câ‚‚) (g : câ‚‚ âŸ¶ câ‚ƒ), hom (f â‰« g) =
  hom f â‰« whiskerLeft (F.map f) (hom g) â‰« eqToHom (by simp only [Functor.map_comp]; rfl))

/-- Construct a functor from `Grothendieck F` to another category `E` by providing a family of
functors on the fibers of `Grothendieck F`, a family of natural transformations on morphisms in the
base of `Grothendieck F` and coherence data for this family of natural transformations. -/
@[simps]
def functorFrom : Grothendieck F â¥¤ E where
  obj X := (fib X.base).obj X.fiber
  map {X Y} f := (hom f.base).app X.fiber â‰« (fib Y.base).map f.fiber
  map_id X := by simp [hom_id]
  map_comp f g := by simp [hom_comp]

/-- `Grothendieck.Î¹ F c` composed with `Grothendieck.functorFrom` is isomorphic a functor on a fiber
on `F` supplied as the first argument to `Grothendieck.functorFrom`. -/
def Î¹CompFunctorFrom (c : C) : Î¹ F c â‹™ (functorFrom fib hom hom_id hom_comp) â‰… fib c :=
  NatIso.ofComponents (fun _ => Iso.refl _) (fun f => by simp [hom_id])

end FunctorFrom

end Grothendieck

end CategoryTheory
