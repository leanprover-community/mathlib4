/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Sina Hazratpour
-/
import Mathlib.CategoryTheory.Category.Cat.AsSmall
import Mathlib.CategoryTheory.Elements
import Mathlib.CategoryTheory.Comma.Over.Basic

/-!
# The Grothendieck construction

Given a functor `F : C ‚•§ Cat`, the objects of `Grothendieck F`
consist of dependent pairs `(b, f)`, where `b : C` and `f : F.obj c`,
and a morphism `(b, f) ‚ü∂ (b', f')` is a pair `Œ≤ : b ‚ü∂ b'` in `C`, and
`œÜ : (F.map Œ≤).obj f ‚ü∂ f'`

`Grothendieck.functor` makes the Grothendieck construction into a functor from the functor category
`C ‚•§ Cat` to the over category `Over C` in the category of categories.

Categories such as `PresheafedSpace` are in fact examples of this construction,
and it may be interesting to try to generalize some of the development there.

## Implementation notes

Really we should treat `Cat` as a 2-category, and allow `F` to be a 2-functor.

There is also a closely related construction starting with `G : C·µí·µñ ‚•§ Cat`,
where morphisms consists again of `Œ≤ : b ‚ü∂ b'` and `œÜ : f ‚ü∂ (F.map (op Œ≤)).obj f'`.

## Notable constructions

- `Grothendieck F` is the Grothendieck construction.
- Elements of `Grothendieck F` whose base is `c : C` can be transported along `f : c ‚ü∂ d` using
`transport`.
- A natural transformation `Œ± : F ‚ü∂ G` induces `map Œ± : Grothendieck F ‚•§ Grothendieck G`.
- The Grothendieck construction and `map` together form a functor (`functor`) from the functor
category `E ‚•§ Cat` to the over category `Over E`.
- A functor `G : D ‚•§ C` induces `pre F G : Grothendieck (G ‚ãô F) ‚•§ Grothendieck F`.

## References

See also `CategoryTheory.Functor.Elements` for the category of elements of functor `F : C ‚•§ Type`.

* https://stacks.math.columbia.edu/tag/02XV
* https://ncatlab.org/nlab/show/Grothendieck+construction

-/


universe w u v u‚ÇÅ v‚ÇÅ u‚ÇÇ v‚ÇÇ

namespace CategoryTheory

open Functor

variable {C : Type u} [Category.{v} C]
variable {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D]
variable (F : C ‚•§ Cat.{v‚ÇÇ, u‚ÇÇ})

/--
The Grothendieck construction (often written as `‚à´ F` in mathematics) for a functor `F : C ‚•§ Cat`
gives a category whose
* objects `X` consist of `X.base : C` and `X.fiber : F.obj base`
* morphisms `f : X ‚ü∂ Y` consist of
  `base : X.base ‚ü∂ Y.base` and
  `f.fiber : (F.map base).obj X.fiber ‚ü∂ Y.fiber`
-/
structure Grothendieck where
  /-- The underlying object in `C` -/
  base : C
  /-- The object in the fiber of the base object. -/
  fiber : F.obj base

namespace Grothendieck

variable {F}

/-- A morphism in the Grothendieck category `F : C ‚•§ Cat` consists of
`base : X.base ‚ü∂ Y.base` and `f.fiber : (F.map base).obj X.fiber ‚ü∂ Y.fiber`.
-/
structure Hom (X Y : Grothendieck F) where
  /-- The morphism between base objects. -/
  base : X.base ‚ü∂ Y.base
  /-- The morphism from the pushforward to the source fiber object to the target fiber object. -/
  fiber : (F.map base).obj X.fiber ‚ü∂ Y.fiber

@[ext (iff := false)]
theorem ext {X Y : Grothendieck F} (f g : Hom X Y) (w_base : f.base = g.base)
    (w_fiber : eqToHom (by rw [w_base]) ‚â´ f.fiber = g.fiber) : f = g := by
  cases f; cases g
  congr
  dsimp at w_base
  cat_disch

/-- The identity morphism in the Grothendieck category.
-/
def id (X : Grothendieck F) : Hom X X where
  base := ùüô X.base
  fiber := eqToHom (by simp)

instance (X : Grothendieck F) : Inhabited (Hom X X) :=
  ‚ü®id X‚ü©

/-- Composition of morphisms in the Grothendieck category.
-/
def comp {X Y Z : Grothendieck F} (f : Hom X Y) (g : Hom Y Z) : Hom X Z where
  base := f.base ‚â´ g.base
  fiber :=
    eqToHom (by simp) ‚â´ (F.map g.base).map f.fiber ‚â´ g.fiber

attribute [local simp] eqToHom_map

instance : Category (Grothendieck F) where
  Hom X Y := Grothendieck.Hom X Y
  id X := Grothendieck.id X
  comp f g := Grothendieck.comp f g
  comp_id {X Y} f := by
    ext
    ¬∑ simp [comp, id]
    ¬∑ dsimp [comp, id]
      rw [‚Üê NatIso.naturality_2 (eqToIso (F.map_id Y.base)) f.fiber]
      simp
  id_comp f := by ext <;> simp [comp, id]
  assoc f g h := by
    ext
    ¬∑ simp [comp]
    ¬∑ dsimp [comp, id]
      rw [‚Üê NatIso.naturality_2 (eqToIso (F.map_comp _ _)) f.fiber]
      simp

@[simp]
theorem id_base (X : Grothendieck F) :
    Hom.base (ùüô X) = ùüô X.base :=
  rfl

@[simp]
theorem id_fiber (X : Grothendieck F) :
    Hom.fiber (ùüô X) = eqToHom (by simp) :=
  rfl

@[simp]
theorem comp_base {X Y Z : Grothendieck F} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).base = f.base ‚â´ g.base :=
  rfl

@[simp]
theorem comp_fiber {X Y Z : Grothendieck F} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    Hom.fiber (f ‚â´ g) =
      eqToHom (by simp) ‚â´ (F.map g.base).map f.fiber ‚â´ g.fiber :=
  rfl

theorem congr {X Y : Grothendieck F} {f g : X ‚ü∂ Y} (h : f = g) :
    f.fiber = eqToHom (by subst h; rfl) ‚â´ g.fiber := by
  subst h
  simp

@[simp]
theorem base_eqToHom {X Y : Grothendieck F} (h : X = Y) :
    (eqToHom h).base = eqToHom (congrArg Grothendieck.base h) := by subst h; rfl

@[simp]
theorem fiber_eqToHom {X Y : Grothendieck F} (h : X = Y) :
    (eqToHom h).fiber = eqToHom (by subst h; simp) := by subst h; rfl

lemma eqToHom_eq {X Y : Grothendieck F} (hF : X = Y) :
    eqToHom hF = { base := eqToHom (by subst hF; rfl), fiber := eqToHom (by subst hF; simp) } := by
  subst hF
  rfl

section Transport

/--
If `F : C ‚•§ Cat` is a functor and `t : c ‚ü∂ d` is a morphism in `C`, then `transport` maps each
`c`-based element of `Grothendieck F` to a `d`-based element.
-/
@[simps]
def transport (x : Grothendieck F) {c : C} (t : x.base ‚ü∂ c) : Grothendieck F :=
  ‚ü®c, (F.map t).obj x.fiber‚ü©

/--
If `F : C ‚•§ Cat` is a functor and `t : c ‚ü∂ d` is a morphism in `C`, then `transport` maps each
`c`-based element `x` of `Grothendieck F` to a `d`-based element `x.transport t`.

`toTransport` is the morphism `x ‚ü∂ x.transport t` induced by `t` and the identity on fibers.
-/
@[simps]
def toTransport (x : Grothendieck F) {c : C} (t : x.base ‚ü∂ c) : x ‚ü∂ x.transport t :=
  ‚ü®t, ùüô _‚ü©

/--
Construct an isomorphism in a Grothendieck construction from isomorphisms in its base and fiber.
-/
@[simps]
def isoMk {X Y : Grothendieck F} (e‚ÇÅ : X.base ‚âÖ Y.base)
    (e‚ÇÇ : (F.map e‚ÇÅ.hom).obj X.fiber ‚âÖ Y.fiber) :
    X ‚âÖ Y where
  hom := ‚ü®e‚ÇÅ.hom, e‚ÇÇ.hom‚ü©
  inv := ‚ü®e‚ÇÅ.inv, (F.map e‚ÇÅ.inv).map e‚ÇÇ.inv ‚â´
    eqToHom (Functor.congr_obj (F.mapIso e‚ÇÅ).hom_inv_id X.fiber)‚ü©
  hom_inv_id := Grothendieck.ext _ _ (by simp) (by simp)
  inv_hom_id := Grothendieck.ext _ _ (by simp) (by
    have := Functor.congr_hom (F.mapIso e‚ÇÅ).inv_hom_id e‚ÇÇ.inv
    dsimp at this
    simp [this])

/--
If `F : C ‚•§ Cat` and `x : Grothendieck F`, then every `C`-isomorphism `Œ± : x.base ‚âÖ c` induces
an isomorphism between `x` and its transport along `Œ±`
-/
@[simps!]
def transportIso (x : Grothendieck F) {c : C} (Œ± : x.base ‚âÖ c) :
    x.transport Œ±.hom ‚âÖ x := (isoMk Œ± (Iso.refl _)).symm

end Transport
section

variable (F)

/-- The forgetful functor from `Grothendieck F` to the source category. -/
@[simps!]
def forget : Grothendieck F ‚•§ C where
  obj X := X.1
  map f := f.1

end

section

variable {G : C ‚•§ Cat}

/-- The Grothendieck construction is functorial: a natural transformation `Œ± : F ‚ü∂ G` induces
a functor `Grothendieck.map : Grothendieck F ‚•§ Grothendieck G`.
-/
@[simps!]
def map (Œ± : F ‚ü∂ G) : Grothendieck F ‚•§ Grothendieck G where
  obj X :=
  { base := X.base
    fiber := (Œ±.app X.base).obj X.fiber }
  map {X Y} f :=
  { base := f.base
    fiber := (eqToHom (Œ±.naturality f.base).symm).app X.fiber ‚â´ (Œ±.app Y.base).map f.fiber }
  map_id X := by simp only [Cat.eqToHom_app, id_fiber, eqToHom_map, eqToHom_trans]; rfl
  map_comp {X Y Z} f g := by
    dsimp
    congr 1
    simp only [‚Üê Category.assoc, Functor.map_comp, eqToHom_map]
    congr 1
    simp only [Cat.eqToHom_app, Cat.comp_obj, eqToHom_trans, eqToHom_map, Category.assoc,
      ‚Üê Cat.comp_map]
    rw [Functor.congr_hom (Œ±.naturality g.base).symm f.fiber]
    simp

theorem map_obj {Œ± : F ‚ü∂ G} (X : Grothendieck F) :
    (Grothendieck.map Œ±).obj X = ‚ü®X.base, (Œ±.app X.base).obj X.fiber‚ü© := rfl

theorem map_map {Œ± : F ‚ü∂ G} {X Y : Grothendieck F} {f : X ‚ü∂ Y} :
    (Grothendieck.map Œ±).map f =
    ‚ü®f.base, (eqToHom (Œ±.naturality f.base).symm).app X.fiber ‚â´ (Œ±.app Y.base).map f.fiber‚ü© := rfl

/-- The functor `Grothendieck.map Œ± : Grothendieck F ‚•§ Grothendieck G` lies over `C`. -/
theorem functor_comp_forget {Œ± : F ‚ü∂ G} :
    Grothendieck.map Œ± ‚ãô Grothendieck.forget G = Grothendieck.forget F := rfl

theorem map_id_eq : map (ùüô F) = ùüô (Cat.of <| Grothendieck <| F) := by
  fapply Functor.ext
  ¬∑ intro X
    rfl
  ¬∑ intro X Y f
    simp [map_map]
    rfl

/-- Making the equality of functors into an isomorphism. Note: we should avoid equality of functors
if possible, and we should prefer `mapIdIso` to `map_id_eq` whenever we can. -/
def mapIdIso : map (ùüô F) ‚âÖ ùüô (Cat.of <| Grothendieck <| F) := eqToIso map_id_eq

variable {H : C ‚•§ Cat}

theorem map_comp_eq (Œ± : F ‚ü∂ G) (Œ≤ : G ‚ü∂ H) :
    map (Œ± ‚â´ Œ≤) = map Œ± ‚ãô map Œ≤ := by
  fapply Functor.ext
  ¬∑ intro X
    rfl
  ¬∑ intro X Y f
    simp only [map_map, map_obj_base, NatTrans.comp_app, Cat.comp_obj, Cat.comp_map,
      eqToHom_refl, Functor.comp_map, Functor.map_comp, Category.comp_id, Category.id_comp]
    fapply Grothendieck.ext
    ¬∑ rfl
    ¬∑ simp

/-- Making the equality of functors into an isomorphism. Note: we should avoid equality of functors
if possible, and we should prefer `map_comp_iso` to `map_comp_eq` whenever we can. -/
def mapCompIso (Œ± : F ‚ü∂ G) (Œ≤ : G ‚ü∂ H) : map (Œ± ‚â´ Œ≤) ‚âÖ map Œ± ‚ãô map Œ≤ := eqToIso (map_comp_eq Œ± Œ≤)

variable (F)

/-- The inverse functor to build the equivalence `compAsSmallFunctorEquivalence`. -/
@[simps]
def compAsSmallFunctorEquivalenceInverse :
    Grothendieck F ‚•§ Grothendieck (F ‚ãô Cat.asSmallFunctor.{w}) where
  obj X := ‚ü®X.base, AsSmall.up.obj X.fiber‚ü©
  map f := ‚ü®f.base, AsSmall.up.map f.fiber‚ü©

/-- The functor to build the equivalence `compAsSmallFunctorEquivalence`. -/
@[simps]
def compAsSmallFunctorEquivalenceFunctor :
    Grothendieck (F ‚ãô Cat.asSmallFunctor.{w}) ‚•§ Grothendieck F where
  obj X := ‚ü®X.base, AsSmall.down.obj X.fiber‚ü©
  map f := ‚ü®f.base, AsSmall.down.map f.fiber‚ü©
  map_id _ := by apply Grothendieck.ext <;> simp
  map_comp _ _ := by apply Grothendieck.ext <;> simp [down_comp]

/-- Taking the Grothendieck construction on `F ‚ãô asSmallFunctor`, where
`asSmallFunctor : Cat ‚•§ Cat` is the functor which turns each category into a small category of a
(potentially) larger universe, is equivalent to the Grothendieck construction on `F` itself. -/
@[simps]
def compAsSmallFunctorEquivalence :
    Grothendieck (F ‚ãô Cat.asSmallFunctor.{w}) ‚âå Grothendieck F where
  functor := compAsSmallFunctorEquivalenceFunctor F
  inverse := compAsSmallFunctorEquivalenceInverse F
  counitIso := Iso.refl _
  unitIso := Iso.refl _

variable {F} in
/-- Mapping a Grothendieck construction along the whiskering of any natural transformation
`Œ± : F ‚ü∂ G` with the functor `asSmallFunctor : Cat ‚•§ Cat` is naturally isomorphic to conjugating
`map Œ±` with the equivalence between `Grothendieck (F ‚ãô asSmallFunctor)` and `Grothendieck F`. -/
def mapWhiskerRightAsSmallFunctor (Œ± : F ‚ü∂ G) :
    map (whiskerRight Œ± Cat.asSmallFunctor.{w}) ‚âÖ
    (compAsSmallFunctorEquivalence F).functor ‚ãô map Œ± ‚ãô
      (compAsSmallFunctorEquivalence G).inverse :=
  NatIso.ofComponents
    (fun X => Iso.refl _)
    (fun f => by
      fapply Grothendieck.ext
      ¬∑ simp [compAsSmallFunctorEquivalenceInverse]
      ¬∑ simp only [compAsSmallFunctorEquivalence_functor, compAsSmallFunctorEquivalence_inverse,
          Functor.comp_obj, compAsSmallFunctorEquivalenceInverse_obj_base, map_obj_base,
          compAsSmallFunctorEquivalenceFunctor_obj_base, Cat.asSmallFunctor_obj, Cat.of_Œ±,
          Iso.refl_hom, Functor.comp_map, comp_base, id_base,
          compAsSmallFunctorEquivalenceInverse_map_base, map_map_base,
          compAsSmallFunctorEquivalenceFunctor_map_base, Cat.asSmallFunctor_map, map_obj_fiber,
          whiskerRight_app, AsSmall.down_obj, AsSmall.up_obj_down,
          compAsSmallFunctorEquivalenceInverse_obj_fiber,
          compAsSmallFunctorEquivalenceFunctor_obj_fiber, comp_fiber, map_map_fiber,
          AsSmall.down_map, down_comp, eqToHom_down, AsSmall.up_map_down, Functor.map_comp,
          eqToHom_map, id_fiber, Category.assoc, eqToHom_trans_assoc,
          compAsSmallFunctorEquivalenceInverse_map_fiber,
          compAsSmallFunctorEquivalenceFunctor_map_fiber, eqToHom_comp_iff, comp_eqToHom_iff]
        simp only [conj_eqToHom_iff_heq']
        rw [G.map_id]
        simp)

end

/-- The Grothendieck construction as a functor from the functor category `E ‚•§ Cat` to the
over category `Over E`. -/
def functor {E : Cat.{v, u}} : (E ‚•§ Cat.{v,u}) ‚•§ Over (T := Cat.{v,u}) E where
  obj F := Over.mk (X := E) (Y := Cat.of (Grothendieck F)) (Grothendieck.forget F)
  map {_ _} Œ± := Over.homMk (X:= E) (Grothendieck.map Œ±) Grothendieck.functor_comp_forget
  map_id F := by
    ext
    exact Grothendieck.map_id_eq (F := F)
  map_comp Œ± Œ≤ := by
    simp [Grothendieck.map_comp_eq Œ± Œ≤]
    rfl

variable (G : C ‚•§ Type w)

/-- Auxiliary definition for `grothendieckTypeToCat`, to speed up elaboration. -/
@[simps!]
def grothendieckTypeToCatFunctor : Grothendieck (G ‚ãô typeToCat) ‚•§ G.Elements where
  obj X := ‚ü®X.1, X.2.as‚ü©
  map f := ‚ü®f.1, f.2.1.1‚ü©

/-- Auxiliary definition for `grothendieckTypeToCat`, to speed up elaboration. -/
@[simps!]
def grothendieckTypeToCatInverse : G.Elements ‚•§ Grothendieck (G ‚ãô typeToCat) where
  obj X := ‚ü®X.1, ‚ü®X.2‚ü©‚ü©
  map f := ‚ü®f.1, ‚ü®‚ü®f.2‚ü©‚ü©‚ü©

/-- The Grothendieck construction applied to a functor to `Type`
(thought of as a functor to `Cat` by realising a type as a discrete category)
is the same as the 'category of elements' construction.
-/
@[simps!]
def grothendieckTypeToCat : Grothendieck (G ‚ãô typeToCat) ‚âå G.Elements where
  functor := grothendieckTypeToCatFunctor G
  inverse := grothendieckTypeToCatInverse G
  unitIso :=
    NatIso.ofComponents
      (fun X => by
        rcases X with ‚ü®_, ‚ü®‚ü©‚ü©
        exact Iso.refl _)
      (by
        rintro ‚ü®_, ‚ü®‚ü©‚ü© ‚ü®_, ‚ü®‚ü©‚ü© ‚ü®base, ‚ü®‚ü®f‚ü©‚ü©‚ü©
        dsimp at *
        simp
        rfl)
  counitIso :=
    NatIso.ofComponents
      (fun X => by
        cases X
        exact Iso.refl _)
      (by
        rintro ‚ü®‚ü© ‚ü®‚ü© ‚ü®f, e‚ü©
        dsimp at *
        simp
        rfl)
  functor_unitIso_comp := by
    rintro ‚ü®_, ‚ü®‚ü©‚ü©
    simp
    rfl

section Pre

variable (F)

/-- Applying a functor `G : D ‚•§ C` to the base of the Grothendieck construction induces a functor
`Grothendieck (G ‚ãô F) ‚•§ Grothendieck F`. -/
@[simps]
def pre (G : D ‚•§ C) : Grothendieck (G ‚ãô F) ‚•§ Grothendieck F where
  obj X := ‚ü®G.obj X.base, X.fiber‚ü©
  map f := ‚ü®G.map f.base, f.fiber‚ü©
  map_id X := Grothendieck.ext _ _ (G.map_id _) (by simp)
  map_comp f g := Grothendieck.ext _ _ (G.map_comp _ _) (by simp)

@[simp]
theorem pre_id : pre F (ùü≠ C) = ùü≠ _ := rfl

/--
An natural isomorphism between functors `G ‚âÖ H` induces a natural isomorphism between the canonical
morphism `pre F G` and `pre F H`, up to composition with
`Grothendieck (G ‚ãô F) ‚•§ Grothendieck (H ‚ãô F)`.
-/
def preNatIso {G H : D ‚•§ C} (Œ± : G ‚âÖ H) :
    pre F G ‚âÖ map (whiskerRight Œ±.hom F) ‚ãô (pre F H) :=
  NatIso.ofComponents
    (fun X => (transportIso ‚ü®G.obj X.base, X.fiber‚ü© (Œ±.app X.base)).symm)
    (fun f => by fapply Grothendieck.ext <;> simp)

/--
Given an equivalence of categories `G`, `preInv _ G` is the (weak) inverse of the `pre _ G.functor`.
-/
def preInv (G : D ‚âå C) : Grothendieck F ‚•§ Grothendieck (G.functor ‚ãô F) :=
  map (whiskerRight G.counitInv F) ‚ãô Grothendieck.pre (G.functor ‚ãô F) G.inverse

variable {F} in
lemma pre_comp_map (G : D ‚•§ C) {H : C ‚•§ Cat} (Œ± : F ‚ü∂ H) :
    pre F G ‚ãô map Œ± = map (whiskerLeft G Œ±) ‚ãô pre H G := rfl

variable {F} in
lemma pre_comp_map_assoc (G : D ‚•§ C) {H : C ‚•§ Cat} (Œ± : F ‚ü∂ H) {E : Type*} [Category E]
    (K : Grothendieck H ‚•§ E) : pre F G ‚ãô map Œ± ‚ãô K= map (whiskerLeft G Œ±) ‚ãô pre H G ‚ãô K := rfl

variable {E : Type*} [Category E] in
@[simp]
lemma pre_comp (G : D ‚•§ C) (H : E ‚•§ D) : pre F (H ‚ãô G) = pre (G ‚ãô F) H ‚ãô pre F G := rfl

/--
Let `G` be an equivalence of categories. The functor induced via `pre` by `G.functor ‚ãô G.inverse`
is naturally isomorphic to the functor induced via `map` by a whiskered version of `G`'s inverse
unit.
-/
protected def preUnitIso (G : D ‚âå C) :
    map (whiskerRight G.unitInv _) ‚âÖ pre (G.functor ‚ãô F) (G.functor ‚ãô G.inverse) :=
  preNatIso _ G.unitIso.symm |>.symm

/--
Given a functor `F : C ‚•§ Cat` and an equivalence of categories `G : D ‚âå C`, the functor
`pre F G.functor` is an equivalence between `Grothendieck (G.functor ‚ãô F)` and `Grothendieck F`.
-/
def preEquivalence (G : D ‚âå C) : Grothendieck (G.functor ‚ãô F) ‚âå Grothendieck F where
  functor := pre F G.functor
  inverse := preInv F G
  unitIso := by
    refine (eqToIso ?_)
      ‚â™‚â´ (Grothendieck.preUnitIso F G |> isoWhiskerLeft (map _))
      ‚â™‚â´ (pre_comp_map_assoc G.functor _ _ |> Eq.symm |> eqToIso)
    calc
      _ = map (ùüô _) := map_id_eq.symm
      _ = map _ := ?_
      _ = map _ ‚ãô map _ := map_comp_eq _ _
    congr; ext X
    simp only [Functor.comp_obj, Functor.comp_map, ‚Üê Functor.map_comp, Functor.id_obj,
      Functor.map_id, NatTrans.comp_app, NatTrans.id_app, whiskerLeft_app, whiskerRight_app,
      Equivalence.counitInv_functor_comp]
  counitIso := preNatIso F G.counitIso.symm |>.symm
  functor_unitIso_comp := by
    intro X
    simp only [preInv, Grothendieck.preUnitIso, pre_id,
      Iso.trans_hom, eqToIso.hom, eqToHom_app, eqToHom_refl, isoWhiskerLeft_hom, NatTrans.comp_app]
    fapply Grothendieck.ext <;> simp [preNatIso, transportIso]

variable {F} in
/--
Let `F, F' : C ‚•§ Cat` be functor, `G : D ‚âå C` an equivalence and `Œ± : F ‚ü∂ F'` a natural
transformation.

Left-whiskering `Œ±` by `G` and then taking the Grothendieck construction is, up to isomorphism,
the same as taking the Grothendieck construction of `Œ±` and using the equivalences `pre F G`
and `pre F' G` to match the expected type:

```
Grothendieck (G.functor ‚ãô F) ‚âå Grothendieck F ‚•§ Grothendieck F' ‚âå Grothendieck (G.functor ‚ãô F')
```
-/
def mapWhiskerLeftIsoConjPreMap {F' : C ‚•§ Cat} (G : D ‚âå C) (Œ± : F ‚ü∂ F') :
    map (whiskerLeft G.functor Œ±) ‚âÖ
      (preEquivalence F G).functor ‚ãô map Œ± ‚ãô (preEquivalence F' G).inverse :=
  (Functor.rightUnitor _).symm ‚â™‚â´ isoWhiskerLeft _ (preEquivalence F' G).unitIso

end Pre

section FunctorFrom

variable {E : Type*} [Category E]

variable (F) in
/-- The inclusion of a fiber `F.obj c` of a functor `F : C ‚•§ Cat` into its Grothendieck
construction. -/
@[simps obj map]
def Œπ (c : C) : F.obj c ‚•§ Grothendieck F where
  obj d := ‚ü®c, d‚ü©
  map f := ‚ü®ùüô _, eqToHom (by simp) ‚â´ f‚ü©
  map_id d := by
    dsimp
    congr
    simp only [Category.comp_id]
  map_comp f g := by
    apply Grothendieck.ext _ _ (by simp)
    simp only [comp_base, ‚Üê Category.assoc, eqToHom_trans, comp_fiber, Functor.map_comp,
      eqToHom_map]
    congr 1
    simp only [eqToHom_comp_iff, Category.assoc, eqToHom_trans_assoc]
    apply Functor.congr_hom (F.map_id _).symm

instance faithful_Œπ (c : C) : (Œπ F c).Faithful where
  map_injective f := by
    injection f with _ f
    rwa [cancel_epi] at f

/-- Every morphism `f : X ‚ü∂ Y` in the base category induces a natural transformation from the fiber
inclusion `Œπ F X` to the composition `F.map f ‚ãô Œπ F Y`. -/
@[simps]
def ŒπNatTrans {X Y : C} (f : X ‚ü∂ Y) : Œπ F X ‚ü∂ F.map f ‚ãô Œπ F Y where
  app d := ‚ü®f, ùüô _‚ü©
  naturality _ _ _ := by
    simp only [Œπ, Functor.comp_obj, Functor.comp_map]
    exact Grothendieck.ext _ _ (by simp) (by simp [eqToHom_map])

variable (fib : ‚àÄ c, F.obj c ‚•§ E) (hom : ‚àÄ {c c' : C} (f : c ‚ü∂ c'), fib c ‚ü∂ F.map f ‚ãô fib c')
variable (hom_id : ‚àÄ c, hom (ùüô c) = eqToHom (by simp only [Functor.map_id]; rfl))
variable (hom_comp : ‚àÄ c‚ÇÅ c‚ÇÇ c‚ÇÉ (f : c‚ÇÅ ‚ü∂ c‚ÇÇ) (g : c‚ÇÇ ‚ü∂ c‚ÇÉ), hom (f ‚â´ g) =
  hom f ‚â´ whiskerLeft (F.map f) (hom g) ‚â´ eqToHom (by simp only [Functor.map_comp]; rfl))

/-- Construct a functor from `Grothendieck F` to another category `E` by providing a family of
functors on the fibers of `Grothendieck F`, a family of natural transformations on morphisms in the
base of `Grothendieck F` and coherence data for this family of natural transformations. -/
@[simps]
def functorFrom : Grothendieck F ‚•§ E where
  obj X := (fib X.base).obj X.fiber
  map {X Y} f := (hom f.base).app X.fiber ‚â´ (fib Y.base).map f.fiber
  map_id X := by simp [hom_id]
  map_comp f g := by simp [hom_comp]

/-- `Grothendieck.Œπ F c` composed with `Grothendieck.functorFrom` is isomorphic a functor on a fiber
on `F` supplied as the first argument to `Grothendieck.functorFrom`. -/
def ŒπCompFunctorFrom (c : C) : Œπ F c ‚ãô (functorFrom fib hom hom_id hom_comp) ‚âÖ fib c :=
  NatIso.ofComponents (fun _ => Iso.refl _) (fun f => by simp [hom_id])

end FunctorFrom

/-- The fiber inclusion `Œπ F c` composed with `map Œ±` is isomorphic to `Œ±.app c ‚ãô Œπ F' c`. -/
@[simps!]
def ŒπCompMap {F' : C ‚•§ Cat} (Œ± : F ‚ü∂ F') (c : C) : Œπ F c ‚ãô map Œ± ‚âÖ Œ±.app c ‚ãô Œπ F' c :=
  NatIso.ofComponents (fun X => Iso.refl _) (fun f => by simp [map])

end Grothendieck

end CategoryTheory
