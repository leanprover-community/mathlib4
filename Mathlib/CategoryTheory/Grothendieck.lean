/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Sina Hazratpour
-/
import Mathlib.CategoryTheory.Category.Cat
import Mathlib.CategoryTheory.Elements
import Mathlib.CategoryTheory.Comma.Over

/-!
# The Grothendieck construction

Given a functor `F : C â¥¤ Cat`, the objects of `Grothendieck F`
consist of dependent pairs `(b, f)`, where `b : C` and `f : F.obj c`,
and a morphism `(b, f) âŸ¶ (b', f')` is a pair `Î² : b âŸ¶ b'` in `C`, and
`Ï† : (F.map Î²).obj f âŸ¶ f'`

`Grothendieck.functor` makes the Grothendieck construction into a functor from the functor category
`C â¥¤ Cat` to the over category `Over C` in the category of categories.

Categories such as `PresheafedSpace` are in fact examples of this construction,
and it may be interesting to try to generalize some of the development there.

## Implementation notes

Really we should treat `Cat` as a 2-category, and allow `F` to be a 2-functor.

There is also a closely related construction starting with `G : Cáµ’áµ– â¥¤ Cat`,
where morphisms consists again of `Î² : b âŸ¶ b'` and `Ï† : f âŸ¶ (F.map (op Î²)).obj f'`.

## References

See also `CategoryTheory.Functor.Elements` for the category of elements of functor `F : C â¥¤ Type`.

* https://stacks.math.columbia.edu/tag/02XV
* https://ncatlab.org/nlab/show/Grothendieck+construction

-/


universe u

namespace CategoryTheory

variable {C D : Type*} [Category C] [Category D]
variable (F : C â¥¤ Cat)

/--
The Grothendieck construction (often written as `âˆ« F` in mathematics) for a functor `F : C â¥¤ Cat`
gives a category whose
* objects `X` consist of `X.base : C` and `X.fiber : F.obj base`
* morphisms `f : X âŸ¶ Y` consist of
  `base : X.base âŸ¶ Y.base` and
  `f.fiber : (F.map base).obj X.fiber âŸ¶ Y.fiber`
-/
-- Porting note (https://github.com/leanprover-community/mathlib4/issues/5171): no such linter yet
-- @[nolint has_nonempty_instance]
structure Grothendieck where
  /-- The underlying object in `C` -/
  base : C
  /-- The object in the fiber of the base object. -/
  fiber : F.obj base

namespace Grothendieck

variable {F}

/-- A morphism in the Grothendieck category `F : C â¥¤ Cat` consists of
`base : X.base âŸ¶ Y.base` and `f.fiber : (F.map base).obj X.fiber âŸ¶ Y.fiber`.
-/
structure Hom (X Y : Grothendieck F) where
  /-- The morphism between base objects. -/
  base : X.base âŸ¶ Y.base
  /-- The morphism from the pushforward to the source fiber object to the target fiber object. -/
  fiber : (F.map base).obj X.fiber âŸ¶ Y.fiber

@[ext (iff := false)]
theorem ext {X Y : Grothendieck F} (f g : Hom X Y) (w_base : f.base = g.base)
    (w_fiber : eqToHom (by rw [w_base]) â‰« f.fiber = g.fiber) : f = g := by
  cases f; cases g
  congr
  dsimp at w_base
  aesop_cat

/-- The identity morphism in the Grothendieck category.
-/
def id (X : Grothendieck F) : Hom X X where
  base := ğŸ™ X.base
  fiber := eqToHom (by erw [CategoryTheory.Functor.map_id, Functor.id_obj X.fiber])

instance (X : Grothendieck F) : Inhabited (Hom X X) :=
  âŸ¨id XâŸ©

/-- Composition of morphisms in the Grothendieck category.
-/
def comp {X Y Z : Grothendieck F} (f : Hom X Y) (g : Hom Y Z) : Hom X Z where
  base := f.base â‰« g.base
  fiber :=
    eqToHom (by erw [Functor.map_comp, Functor.comp_obj]) â‰« (F.map g.base).map f.fiber â‰« g.fiber

attribute [local simp] eqToHom_map

instance : Category (Grothendieck F) where
  Hom X Y := Grothendieck.Hom X Y
  id X := Grothendieck.id X
  comp := @fun _ _ _ f g => Grothendieck.comp f g
  comp_id := @fun X Y f => by
    dsimp; ext
    Â· simp [comp, id]
    Â· dsimp [comp, id]
      rw [â† NatIso.naturality_2 (eqToIso (F.map_id Y.base)) f.fiber]
      simp
  id_comp := @fun X Y f => by dsimp; ext <;> simp [comp, id]
  assoc := @fun W X Y Z f g h => by
    dsimp; ext
    Â· simp [comp, id]
    Â· dsimp [comp, id]
      rw [â† NatIso.naturality_2 (eqToIso (F.map_comp _ _)) f.fiber]
      simp

@[simp]
theorem id_base (X : Grothendieck F) :
    Hom.base (ğŸ™ X) = ğŸ™ X.base := by
  rfl

@[simp]
theorem id_fiber (X : Grothendieck F) :
    Hom.fiber (ğŸ™ X) = eqToHom (by erw [CategoryTheory.Functor.map_id, Functor.id_obj X.fiber]) :=
  rfl

@[simp]
theorem comp_base {X Y Z : Grothendieck F} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    (f â‰« g).base = f.base â‰« g.base :=
  rfl

@[simp]
theorem comp_fiber {X Y Z : Grothendieck F} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    Hom.fiber (f â‰« g) =
    eqToHom (by erw [Functor.map_comp, Functor.comp_obj]) â‰«
    (F.map g.base).map f.fiber â‰« g.fiber :=
  rfl


theorem congr {X Y : Grothendieck F} {f g : X âŸ¶ Y} (h : f = g) :
    f.fiber = eqToHom (by subst h; rfl) â‰« g.fiber := by
  subst h
  dsimp
  simp

lemma eqToHom_eq {X Y : Grothendieck F} (hF : X = Y) :
    eqToHom hF = { base := eqToHom (by subst hF; rfl), fiber := eqToHom (by subst hF; simp) } := by
  subst hF
  rfl
section

variable (F)

/-- The forgetful functor from `Grothendieck F` to the source category. -/
@[simps!]
def forget : Grothendieck F â¥¤ C where
  obj X := X.1
  map := @fun _ _ f => f.1

end

section

variable {G : C â¥¤ Cat}

/-- The Grothendieck construction is functorial: a natural transformation `Î± : F âŸ¶ G` induces
a functor `Grothendieck.map : Grothendieck F â¥¤ Grothendieck G`.
-/
@[simps!]
def map (Î± : F âŸ¶ G) : Grothendieck F â¥¤ Grothendieck G where
  obj X :=
  { base := X.base
    fiber := (Î±.app X.base).obj X.fiber }
  map {X Y} f :=
  { base := f.base
    fiber := (eqToHom (Î±.naturality f.base).symm).app X.fiber â‰« (Î±.app Y.base).map f.fiber }
  map_id X := by simp only [Cat.eqToHom_app, id_fiber, eqToHom_map, eqToHom_trans]; rfl
  map_comp {X Y Z} f g := by
    dsimp
    congr 1
    simp only [comp_fiber f g, â† Category.assoc, Functor.map_comp, eqToHom_map]
    congr 1
    simp only [Cat.eqToHom_app, Cat.comp_obj, eqToHom_trans, eqToHom_map, Category.assoc]
    erw [Functor.congr_hom (Î±.naturality g.base).symm f.fiber]
    simp

theorem map_obj {Î± : F âŸ¶ G} (X : Grothendieck F) :
    (Grothendieck.map Î±).obj X = âŸ¨X.base, (Î±.app X.base).obj X.fiberâŸ© := rfl

theorem map_map {Î± : F âŸ¶ G} {X Y : Grothendieck F} {f : X âŸ¶ Y} :
    (Grothendieck.map Î±).map f =
    âŸ¨f.base, (eqToHom (Î±.naturality f.base).symm).app X.fiber â‰« (Î±.app Y.base).map f.fiberâŸ© := rfl

/-- The functor `Grothendieck.map Î± : Grothendieck F â¥¤ Grothendieck G` lies over `C`.-/
theorem functor_comp_forget {Î± : F âŸ¶ G} :
    Grothendieck.map Î± â‹™ Grothendieck.forget G = Grothendieck.forget F := rfl

theorem map_id_eq : map (ğŸ™ F) = ğŸ™ (Cat.of <| Grothendieck <| F) := by
  fapply Functor.ext
  Â· intro X
    rfl
  Â· intro X Y f
    simp [map_map]
    rfl

/-- Making the equality of functors into an isomorphism. Note: we should avoid equality of functors
if possible, and we should prefer `map_id_iso` to `map_id_eq` whenever we can. -/
def mapIdIso : map (ğŸ™ F) â‰… ğŸ™ (Cat.of <| Grothendieck <| F) := eqToIso map_id_eq

variable {H : C â¥¤ Cat}
theorem map_comp_eq (Î± : F âŸ¶ G) (Î² : G âŸ¶ H) :
    map (Î± â‰« Î²) = map Î± â‹™ map Î² := by
  fapply Functor.ext
  Â· intro X
    rfl
  Â· intro X Y f
    simp only [map_map, map_obj_base, NatTrans.comp_app, Cat.comp_obj, Cat.comp_map,
      eqToHom_refl, Functor.comp_map, Functor.map_comp, Category.comp_id, Category.id_comp]
    fapply Grothendieck.ext
    Â· rfl
    Â· simp

theorem map_comp_eq_assoc (Î± : F âŸ¶ G) (Î² : G âŸ¶ H) (I : Grothendieck H â¥¤ D) :
    map (Î± â‰« Î²) â‹™ I = map Î± â‹™ map Î² â‹™ I := by rw [map_comp_eq, Functor.assoc]

/-- Making the equality of functors into an isomorphism. Note: we should avoid equality of functors
if possible, and we should prefer `map_comp_iso` to `map_comp_eq` whenever we can. -/
def mapCompIso (Î± : F âŸ¶ G) (Î² : G âŸ¶ H) : map (Î± â‰« Î²) â‰… map Î± â‹™ map Î² := eqToIso (map_comp_eq Î± Î²)

def map_iso (Î± : F â‰… G) : Grothendieck F â‰Œ Grothendieck G where
  functor := map Î±.hom
  inverse := map Î±.inv
  unitIso := by
    apply eqToIso
    rw [â† map_comp_eq, Iso.hom_inv_id, map_id_eq]
    rfl
  counitIso := by
    apply eqToIso
    rw [â† map_comp_eq, Iso.inv_hom_id, map_id_eq]
    rfl

instance IsEquivalence_map (Î± : F âŸ¶ G) [IsIso Î±] : (map Î±).IsEquivalence := by
  suffices map_iso (asIso Î±) |>.functor |>.IsEquivalence by simpa
  infer_instance

end

universe v

/-- The Grothendieck construction as a functor from the functor category `E â¥¤ Cat` to the
over category `Over E`. -/
def functor {E : Cat.{v,u}} : (E â¥¤ Cat.{v,u}) â¥¤ Over (T := Cat.{v,u}) E where
  obj F := Over.mk (X := E) (Y := Cat.of (Grothendieck F)) (Grothendieck.forget F)
  map {_ _} Î± := Over.homMk (X:= E) (Grothendieck.map Î±) Grothendieck.functor_comp_forget
  map_id F := by
    ext
    exact Grothendieck.map_id_eq (F := F)
  map_comp Î± Î² := by
    simp [Grothendieck.map_comp_eq Î± Î²]
    rfl

universe w

variable (G : C â¥¤ Type w)

/-- Auxiliary definition for `grothendieckTypeToCat`, to speed up elaboration. -/
@[simps!]
def grothendieckTypeToCatFunctor : Grothendieck (G â‹™ typeToCat) â¥¤ G.Elements where
  obj X := âŸ¨X.1, X.2.asâŸ©
  map f := âŸ¨f.1, f.2.1.1âŸ©

/-- Auxiliary definition for `grothendieckTypeToCat`, to speed up elaboration. -/
-- Porting note:
-- `simps` is incorrectly producing Prop-valued projections here,
-- so we manually specify which ones to produce.
-- See https://leanprover.zulipchat.com/#narrow/stream/144837-PR-reviews/topic/!4.233204.20simps.20bug.20.28Grothendieck.20construction.29
@[simps! obj_base obj_fiber_as map_base]
def grothendieckTypeToCatInverse : G.Elements â¥¤ Grothendieck (G â‹™ typeToCat) where
  obj X := âŸ¨X.1, âŸ¨X.2âŸ©âŸ©
  map f := âŸ¨f.1, âŸ¨âŸ¨f.2âŸ©âŸ©âŸ©

/-- The Grothendieck construction applied to a functor to `Type`
(thought of as a functor to `Cat` by realising a type as a discrete category)
is the same as the 'category of elements' construction.
-/
-- See porting note on grothendieckTypeToCatInverse.
-- We just want to turn off grothendieckTypeToCat_inverse_map_fiber_down_down,
-- so have to list the complement here for `@[simps]`.
@[simps! functor_obj_fst functor_obj_snd functor_map_coe inverse_obj_base inverse_obj_fiber_as
  inverse_map_base unitIso_hom_app_base unitIso_hom_app_fiber unitIso_inv_app_base
  unitIso_inv_app_fiber counitIso_hom_app_coe counitIso_inv_app_coe]
def grothendieckTypeToCat : Grothendieck (G â‹™ typeToCat) â‰Œ G.Elements where
  functor := grothendieckTypeToCatFunctor G
  inverse := grothendieckTypeToCatInverse G
  unitIso :=
    NatIso.ofComponents
      (fun X => by
        rcases X with âŸ¨_, âŸ¨âŸ©âŸ©
        exact Iso.refl _)
      (by
        rintro âŸ¨_, âŸ¨âŸ©âŸ© âŸ¨_, âŸ¨âŸ©âŸ© âŸ¨base, âŸ¨âŸ¨fâŸ©âŸ©âŸ©
        dsimp at *
        simp
        rfl)
  counitIso :=
    NatIso.ofComponents
      (fun X => by
        cases X
        exact Iso.refl _)
      (by
        rintro âŸ¨âŸ© âŸ¨âŸ© âŸ¨f, eâŸ©
        dsimp at *
        simp
        rfl)
  functor_unitIso_comp := by
    rintro âŸ¨_, âŸ¨âŸ©âŸ©
    dsimp
    simp
    rfl

-- TODO: Grothendieck F = pre identity F
variable (F) in
/-- Applying a functor `G : D â¥¤ C` to the base of the Grothendieck construction induces a functor
`Grothendieck (G â‹™ F) â¥¤ Grothendieck F`. -/
@[simps]
def pre (G : D â¥¤ C) : Grothendieck (G â‹™ F) â¥¤ Grothendieck F where
  obj X := âŸ¨G.obj X.base, X.fiberâŸ©
  map f := âŸ¨G.map f.base, f.fiberâŸ©
  map_id X := Grothendieck.ext _ _ (G.map_id _) (by simp)
  map_comp f g := Grothendieck.ext _ _ (G.map_comp _ _) (by simp)

variable (F) in
@[simp]
-- TODO: Why does this type check?
theorem pre_id : pre F (ğŸ­ C) = ğŸ­ _ := by
  simp only [pre, Functor.id_obj, Functor.id_map, map, Functor.comp_obj, NatTrans.id_app,
    Cat.id_obj, Functor.comp_map, Cat.comp_obj, eqToHom_refl, Cat.id_app, Cat.id_map,
    Category.id_comp]
  rfl

variable (F) in
def preNatTrans {G H : D â¥¤ C} (Î± : G âŸ¶ H) :
    pre F G âŸ¶ (map (whiskerRight Î± F)) â‹™ (pre F H) := by
  refine âŸ¨fun X => âŸ¨Î±.app X.base, eqToHom rflâŸ©, ?_âŸ©
  intros
  apply Grothendieck.ext <;> simp

/-!
This is a variant of `preNatTrans` for which the type and the implementation depend on
transformations `Î±` and `Î²` that may not be definitionally equal, even though `Î± = Î²`.
This can be helpful to avoid dependent type theory hell when rewriting Î±.
-/
variable (F) in
def preNatTrans' {G H : D â¥¤ C} (Î± : G âŸ¶ H) {Î² : G âŸ¶ H} (h : Î± = Î²) :
    pre F G âŸ¶ (map (whiskerRight Î² F)) â‹™ (pre F H) := by
  refine âŸ¨fun X => âŸ¨Î±.app X.base, eqToHom (by rw [h]; rfl)âŸ©, ?_âŸ©
  cases h
  intros
  apply Grothendieck.ext <;> simp

lemma preNatTrans'_rfl_eq {G H : D â¥¤ C} (Î± : G âŸ¶ H) :
    preNatTrans' F Î± rfl = preNatTrans F Î± := rfl

lemma preNatTrans_app {G H : D â¥¤ C} (Î± : G âŸ¶ H) (x : Grothendieck (G â‹™ F)) :
    (preNatTrans F Î±).app x = âŸ¨Î±.app x.base, eqToHom rflâŸ© := rfl

@[simp]
lemma base_eqToHom {x y : Grothendieck F} (h : x = y) : (eqToHom h).base = eqToHom (by congr) := by
  cases h ; rfl

@[simp]
lemma fiber_eqToHom {x y : Grothendieck F} (h : x = y) :
    (eqToHom h).fiber = (eqToHom (by cases h ; simp)) := by cases h ; rfl

lemma preNatTrans'_id {G : D â¥¤ C} {Î² : G âŸ¶ G} (h : ğŸ™ G = Î²) :
    preNatTrans' F (ğŸ™ G) h = eqToHom (by
      cases h
      simp only [whiskerRight_id', map_id_eq]
      simp only [CategoryStruct.id]
      simp only [Cat.of_Î±]
      rw [Functor.id_comp]) := by
  cases h
  simp only [preNatTrans', Functor.comp_obj, NatTrans.id_app, pre_obj_base, map_obj_base,
    pre_obj_fiber, map_obj_fiber, whiskerRight_app, eqToHom_refl]
  ext X
  simp only [Functor.comp_obj, eqToHom_app]
  fapply Grothendieck.ext
  Â· simp only [base_eqToHom] ; rfl
  Â· simp only [fiber_eqToHom]
    simp only [pre_obj_base, map_obj_base, id_eq, Cat.of_Î±, eq_mpr_eq_cast, cast_eq, pre_obj_fiber,
      map_obj_fiber, Functor.comp_obj, whiskerRight_app, NatTrans.id_app, Category.comp_id]

variable (F) in
lemma preNatTrans_comp {G H I : D â¥¤ C} (Î± : G âŸ¶ H) (Î² : H âŸ¶ I) :
    preNatTrans F (Î± â‰« Î²) = preNatTrans F Î± â‰« whiskerLeft (map (whiskerRight Î± F)) (preNatTrans F Î²)
      â‰« eqToHom (by simp [map_comp_eq_assoc]) := by
  ext x
  simp only [NatTrans.comp_app, eqToHom_app, whiskerLeft_app, preNatTrans_app]
  fapply Grothendieck.ext
  Â· simp only [pre_obj_base, Functor.comp_obj, map_obj_base, pre_obj_fiber, map_obj_fiber,
    whiskerRight_app, eqToHom_refl, comp_base, base_eqToHom, Category.comp_id]
  Â· simp only [Functor.comp_obj, pre_obj_base, map_obj_base, pre_obj_fiber, map_obj_fiber,
    whiskerRight_app, eqToHom_refl, comp_base, NatTrans.comp_app, Category.comp_id, comp_fiber,
    Functor.map_id, fiber_eqToHom, base_eqToHom, Cat.id_obj, Functor.map_comp, Cat.comp_obj,
    eqToHom_naturality, eqToHom_trans]

variable (F) in
@[reassoc]
lemma preNatTrans_comp2 {G H I : D â¥¤ C} (Î± : G âŸ¶ H) (Î² : H âŸ¶ I) :
    preNatTrans F (Î± â‰« Î²) â‰« eqToHom (by simp [map_comp_eq_assoc]) = preNatTrans F Î± â‰«
      whiskerLeft (map (whiskerRight Î± F)) (preNatTrans F Î²) := by
  simp only [preNatTrans_comp, Category.assoc, eqToHom_trans, eqToHom_refl, Category.comp_id]

variable (F) in
def invPreNatTrans {G H : D â¥¤ C} (Î± : G â‰… H) :
    map (whiskerRight Î±.hom F) â‹™ pre F H âŸ¶ pre F G := by
  refine whiskerLeft (map (whiskerRight Î±.hom F)) (preNatTrans F Î±.inv) â‰« eqToHom ?_
  rw [â† map_comp_eq_assoc, â† whiskerRight_comp, Î±.hom_inv_id, whiskerRight_id', map_id_eq]
  apply Functor.id_comp

@[simp]
def preNatTrans_comp_invPreNatTrans_eq {G H : D â¥¤ C} (Î± : G â‰… H) :
    preNatTrans F Î±.hom â‰« invPreNatTrans F Î± = ğŸ™ (pre F G) := by
  rw [invPreNatTrans, â† preNatTrans_comp2_assoc, â† preNatTrans'_rfl_eq, eqToHom_trans]
  -- Iso.hom_inv_id is a nice example where simp works but rw doesn't
  simp only [Iso.hom_inv_id, preNatTrans'_id, eqToHom_trans, eqToHom_refl]

@[simp]
def invPreNatTrans_comp_preNatTrans_eq {G H : D â¥¤ C} (Î± : G â‰… H) :
    invPreNatTrans F Î± â‰« preNatTrans F Î±.hom = ğŸ™ _ := by
  simp only [invPreNatTrans]

  ext X
  simp only [Functor.comp_obj, NatTrans.comp_app, whiskerLeft_app, preNatTrans_app, eqToHom_app,
    map_obj_base]
  fapply Grothendieck.ext
  Â· simp only [pre_obj_base, map_obj_base, pre_obj_fiber, map_obj_fiber, Functor.comp_obj,
    whiskerRight_app, eqToHom_refl, Category.assoc, comp_base, base_eqToHom, Category.id_comp,
    Iso.inv_hom_id_app, NatTrans.id_app, id_base]
  Â· simp only [comp_fiber, fiber_eqToHom, eqToHom_trans, eqToHom_map]
    simp only [NatTrans.id_app, id_fiber]

-- TODO: implement calcSymm tactic?
-- TODO: move to Bicategory
def isoCancelRight'' {C : Type*} [Bicategory C] {c d e : C} {f : d âŸ¶ e} {finv : e âŸ¶ d}
    {gâ‚ gâ‚‚ : c âŸ¶ d} (Î· : ğŸ™ _ â‰… f â‰« finv) (Î± : gâ‚ â‰« f â‰… gâ‚‚ â‰« f) : gâ‚ â‰… gâ‚‚ := by
  calc
    gâ‚ â‰… gâ‚ â‰« ğŸ™ _ := Bicategory.rightUnitor _ |>.symm
    _ â‰… gâ‚ â‰« f â‰« finv := Bicategory.whiskerLeftIso _ Î·
    _ â‰… (gâ‚ â‰« f) â‰« finv := Bicategory.associator _ _ _ |>.symm
    _ â‰… (gâ‚‚ â‰« f) â‰« finv := Bicategory.whiskerRightIso Î± _
    _ â‰… gâ‚‚ â‰« f â‰« finv := Bicategory.associator _ _ _
    _ â‰… _ := Bicategory.whiskerLeftIso _ Î·.symm
    _ â‰… gâ‚‚ := Bicategory.rightUnitor _

-- TODO: Can we reduce this *nicely* to bicategories?
def isoCancelRight' {E : Type*} [Category E]
    {F : D â¥¤ E} {Finv : E â¥¤ D} {Gâ‚ Gâ‚‚ : C â¥¤ D} (Î· : ğŸ­ _ â‰… F â‹™ Finv)
    (Î± : Gâ‚ â‹™ F â‰… Gâ‚‚ â‹™ F) : (Gâ‚ â‰… Gâ‚‚) := by
  calc
    Gâ‚ â‹™ ğŸ­ _ â‰… Gâ‚ â‹™ F â‹™ Finv := isoWhiskerLeft _ Î·
    _ â‰… Gâ‚‚ â‹™ F â‹™ Finv := isoWhiskerRight Î± _
    _ â‰… Gâ‚‚ â‹™ ğŸ­ _ := isoWhiskerLeft _ Î·.symm

def isoCancelRight {E : Type*} [Category E] {Gâ‚ Gâ‚‚ : E â¥¤ D} (F : D â‰Œ C)
    (Î± : Gâ‚ â‹™ F.functor â‰… Gâ‚‚ â‹™ F.functor) : (Gâ‚ â‰… Gâ‚‚) := isoCancelRight' F.unitIso Î±

variable (F) in
def preNatIso {G H : D â¥¤ C} (Î± : G â‰… H) :
    pre F G â‰… (map (whiskerRight Î±.hom F)) â‹™ (pre F H) :=
  âŸ¨preNatTrans F Î±.hom, invPreNatTrans F Î±, by simp, by simpâŸ©

instance isEquivalence_pre_id : Functor.IsEquivalence <| pre F <| ğŸ­ C := by
  simp only [pre_id]
  infer_instance

variable (F) in
def preInv (G : D â‰Œ C) : Grothendieck F â¥¤ Grothendieck (G.functor â‹™ F) := by
  refine map ?_ â‹™ Grothendieck.pre (G.functor â‹™ F) G.inverse
  rw [â† Functor.assoc]
  exact eqToHom (Functor.id_comp F) â‰« (whiskerRight G.counitInv F)

lemma pre_comp_map (G: D â¥¤ C) {H : C â¥¤ Cat} (Î± : F âŸ¶ H) :
    pre F G â‹™ map Î± = map (whiskerLeft G Î±) â‹™ pre H G := rfl

variable (F) {E : Type*} [Category E] in
@[simp]
lemma pre_comp (G : D â¥¤ C) (H : E â¥¤ D) : pre F (H â‹™ G) = pre (G â‹™ F) H â‹™ pre F G := rfl

variable (F) in
protected def preUnitIso (G : D â‰Œ C) :
    map (whiskerRight G.unitInv _) â‰… pre (G.functor â‹™ F) (G.functor â‹™ G.inverse) :=
  preNatIso _ G.unitIso.symm |>.symm

variable (F) in
def preEquivalence (G : D â‰Œ C) : Grothendieck (G.functor â‹™ F) â‰Œ Grothendieck F := by
  refine Equivalence.mk (pre F G.functor) (preInv F G) ?_ ?_
  Â· simp only [preInv, eqToHom_refl, Category.id_comp, eq_mpr_eq_cast, cast_eq]
    erw [â† Functor.assoc, pre_comp_map, Functor.assoc]
    simp only [Functor.assoc, â† pre_comp]
    calc
      _ = map (ğŸ™ _) := map_id_eq.symm
      _ = map _ := ?_
      _ = map _ â‹™ map _ := map_comp_eq _ _
      _ â‰… _ := (Grothendieck.preUnitIso F G |> isoWhiskerLeft _)
    congr
    ext X
    simp only [Functor.comp_obj, Functor.comp_map, â† Functor.map_comp, Functor.id_obj,
      Functor.map_id, NatTrans.comp_app, NatTrans.id_app, whiskerLeft_app, whiskerRight_app,
      Equivalence.counitInv_functor_comp]
  Â· simp only [preInv, eqToHom_refl, Category.id_comp, eq_mpr_eq_cast, cast_eq, Functor.assoc,
    â† pre_comp]
    exact preNatIso F G.counitIso.symm |>.symm

def mapWhiskerLeftIsoConjPreMap (F : C â¥¤ Cat) (G : D â‰Œ C) (Î± : F âŸ¶ F) :
    map (whiskerLeft G.functor Î±) â‰…
      (preEquivalence F G).functor â‹™ map Î± â‹™ (preEquivalence F G).inverse := by
  apply Iso.symm
  apply isoCancelRight (preEquivalence F G)
  exact isoWhiskerLeft ((preEquivalence F G).functor â‹™ map Î±) (preEquivalence F G).counitIso

-- TODO: currently unused, where to put?
section Transport

def transport (x : Grothendieck F) {c : C} (t : x.base âŸ¶ c) :
    Grothendieck F := by
  exact âŸ¨c, (F.map t).obj x.fiberâŸ©

def transport_hom (x : Grothendieck F) {c : C} (t : x.base âŸ¶ c) :
    x âŸ¶ x.transport t := âŸ¨_, CategoryStruct.id _âŸ©

-- theorem transport_hom_comp (x : Grothendieck F) {c c' : C} (t : x.base âŸ¶ c) (t' : c âŸ¶ c') :
--     x.transport_hom (t â‰« t') = (x.transport_hom t) â‰« (x.transport t).transport_hom t' := sorry

noncomputable def transport.iso (x : Grothendieck F) {c : C} (t : x.base âŸ¶ c) [IsIso t] :
    x.transport t â‰… x := by
  refine âŸ¨?_, x.transport_hom t, ?_, ?_âŸ©
  Â· refine âŸ¨inv t, eqToHom ?_âŸ©
    simp only [transport]
    rw [â† Functor.comp_obj, Functor.map_inv]
    show (F.map t â‰« inv (F.map t)).obj x.fiber = x.fiber
    rw [comp_inv_eq_id _ |>.mpr rfl]
    simp only [Cat.id_obj]
  Â· apply Grothendieck.ext <;> simp [transport_hom]
  Â· apply Grothendieck.ext <;> simp [transport_hom]

end Transport

section FunctorFrom

variable {E : Type*} [Category E]

variable (F) in
/-- The inclusion of a fiber `F.obj c` of a functor `F : C â¥¤ Cat` into its Grothendieck
construction.-/
@[simps obj map]
def Î¹ (c : C) : F.obj c â¥¤ Grothendieck F where
  obj d := âŸ¨c, dâŸ©
  map f := âŸ¨ğŸ™ _, eqToHom (by simp) â‰« fâŸ©
  map_id d := by
    dsimp
    congr
    simp only [Category.comp_id]
  map_comp f g := by
    apply Grothendieck.ext _ _ (by simp)
    simp only [comp_base, â† Category.assoc, eqToHom_trans, comp_fiber, Functor.map_comp,
      eqToHom_map]
    congr 1
    simp only [eqToHom_comp_iff, Category.assoc, eqToHom_trans_assoc]
    apply Functor.congr_hom (F.map_id _).symm

instance faithful_Î¹ (c : C) : (Î¹ F c).Faithful where
  map_injective f := by
    injection f with _ f
    rwa [cancel_epi] at f

/-- Every morphism `f : X âŸ¶ Y` in the base category induces a natural transformation from the fiber
inclusion `Î¹ F X` to the composition `F.map f â‹™ Î¹ F Y`. -/
@[simps]
def Î¹NatTrans {X Y : C} (f : X âŸ¶ Y) : Î¹ F X âŸ¶ F.map f â‹™ Î¹ F Y where
  app d := âŸ¨f, ğŸ™ _âŸ©
  naturality _ _ _ := by
    simp only [Î¹, Functor.comp_obj, Functor.comp_map]
    exact Grothendieck.ext _ _ (by simp) (by simp [eqToHom_map])

variable (fib : âˆ€ c, F.obj c â¥¤ E) (hom : âˆ€ {c c' : C} (f : c âŸ¶ c'), fib c âŸ¶ F.map f â‹™ fib c')
variable (hom_id : âˆ€ c, hom (ğŸ™ c) = eqToHom (by simp only [Functor.map_id]; rfl))
variable (hom_comp : âˆ€ câ‚ câ‚‚ câ‚ƒ (f : câ‚ âŸ¶ câ‚‚) (g : câ‚‚ âŸ¶ câ‚ƒ), hom (f â‰« g) =
  hom f â‰« whiskerLeft (F.map f) (hom g) â‰« eqToHom (by simp only [Functor.map_comp]; rfl))

/-- Construct a functor from `Grothendieck F` to another category `E` by providing a family of
functors on the fibers of `Grothendieck F`, a family of natural transformations on morphisms in the
base of `Grothendieck F` and coherence data for this family of natural transformations. -/
@[simps]
def functorFrom : Grothendieck F â¥¤ E where
  obj X := (fib X.base).obj X.fiber
  map {X Y} f := (hom f.base).app X.fiber â‰« (fib Y.base).map f.fiber
  map_id X := by simp [hom_id]
  map_comp f g := by simp [hom_comp]

/-- `Grothendieck.Î¹ F c` composed with `Grothendieck.functorFrom` is isomorphic a functor on a fiber
on `F` supplied as the first argument to `Grothendieck.functorFrom`. -/
def Î¹CompFunctorFrom (c : C) : Î¹ F c â‹™ (functorFrom fib hom hom_id hom_comp) â‰… fib c :=
  NatIso.ofComponents (fun _ => Iso.refl _) (fun f => by simp [hom_id])

end FunctorFrom

end Grothendieck

end CategoryTheory
