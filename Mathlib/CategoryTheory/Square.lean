/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Comma.Arrow
import Mathlib.CategoryTheory.CommSq

/-!
# The category of commutative squares

In this file, we define a bundled version of `CommSq`
which allows to consider commutative squares as
objects in a category `Square C`.

The four objects in a commutative square are
numbered as follows:
```
X‚ÇÅ --> X‚ÇÇ
|      |
v      v
X‚ÇÉ --> X‚ÇÑ
```

We define the flip functor, and two equivalences with
the category `Arrow (Arrow C)`, depending on whether
we consider a commutative square as a horizontal
morphism between two vertical maps (`arrowArrowEquivalence`)
or a vertical morphism between two horizontal
maps (`arrowArrowEquivalence'`).

-/

universe v v' u u'

namespace CategoryTheory

open Category

variable (C : Type u) [Category.{v} C] {D : Type u'} [Category.{v'} D]

/-- The category of commutative squares in a category. -/
structure Square where
  /-- the top-left object -/
  {X‚ÇÅ : C}
  /-- the top-right object -/
  {X‚ÇÇ : C}
  /-- the bottom-left object -/
  {X‚ÇÉ : C}
  /-- the bottom-right object -/
  {X‚ÇÑ : C}
  /-- the top morphism -/
  f‚ÇÅ‚ÇÇ : X‚ÇÅ ‚ü∂ X‚ÇÇ
  /-- the left morphism -/
  f‚ÇÅ‚ÇÉ : X‚ÇÅ ‚ü∂ X‚ÇÉ
  /-- the right morphism -/
  f‚ÇÇ‚ÇÑ : X‚ÇÇ ‚ü∂ X‚ÇÑ
  /-- the bottom morphism -/
  f‚ÇÉ‚ÇÑ : X‚ÇÉ ‚ü∂ X‚ÇÑ
  fac : f‚ÇÅ‚ÇÇ ‚â´ f‚ÇÇ‚ÇÑ = f‚ÇÅ‚ÇÉ ‚â´ f‚ÇÉ‚ÇÑ

namespace Square

variable {C}

lemma commSq (sq : Square C) : CommSq sq.f‚ÇÅ‚ÇÇ sq.f‚ÇÅ‚ÇÉ sq.f‚ÇÇ‚ÇÑ sq.f‚ÇÉ‚ÇÑ where
  w := sq.fac

/-- A morphism between two commutative squares consists of 4 morphisms
which extend these two squares into a commuting cube. -/
@[ext]
structure Hom (sq‚ÇÅ sq‚ÇÇ : Square C) where
  /-- the top-left morphism -/
  œÑ‚ÇÅ : sq‚ÇÅ.X‚ÇÅ ‚ü∂ sq‚ÇÇ.X‚ÇÅ
  /-- the top-right morphism -/
  œÑ‚ÇÇ : sq‚ÇÅ.X‚ÇÇ ‚ü∂ sq‚ÇÇ.X‚ÇÇ
  /-- the bottom-left morphism -/
  œÑ‚ÇÉ : sq‚ÇÅ.X‚ÇÉ ‚ü∂ sq‚ÇÇ.X‚ÇÉ
  /-- the bottom-right morphism -/
  œÑ‚ÇÑ : sq‚ÇÅ.X‚ÇÑ ‚ü∂ sq‚ÇÇ.X‚ÇÑ
  comm‚ÇÅ‚ÇÇ : sq‚ÇÅ.f‚ÇÅ‚ÇÇ ‚â´ œÑ‚ÇÇ = œÑ‚ÇÅ ‚â´ sq‚ÇÇ.f‚ÇÅ‚ÇÇ := by cat_disch
  comm‚ÇÅ‚ÇÉ : sq‚ÇÅ.f‚ÇÅ‚ÇÉ ‚â´ œÑ‚ÇÉ = œÑ‚ÇÅ ‚â´ sq‚ÇÇ.f‚ÇÅ‚ÇÉ := by cat_disch
  comm‚ÇÇ‚ÇÑ : sq‚ÇÅ.f‚ÇÇ‚ÇÑ ‚â´ œÑ‚ÇÑ = œÑ‚ÇÇ ‚â´ sq‚ÇÇ.f‚ÇÇ‚ÇÑ := by cat_disch
  comm‚ÇÉ‚ÇÑ : sq‚ÇÅ.f‚ÇÉ‚ÇÑ ‚â´ œÑ‚ÇÑ = œÑ‚ÇÉ ‚â´ sq‚ÇÇ.f‚ÇÉ‚ÇÑ := by cat_disch

namespace Hom

attribute [reassoc (attr := simp)] comm‚ÇÅ‚ÇÇ comm‚ÇÅ‚ÇÉ comm‚ÇÇ‚ÇÑ comm‚ÇÉ‚ÇÑ

/-- The identity of a commutative square. -/
@[simps]
def id (sq : Square C) : Hom sq sq where
  œÑ‚ÇÅ := ùüô _
  œÑ‚ÇÇ := ùüô _
  œÑ‚ÇÉ := ùüô _
  œÑ‚ÇÑ := ùüô _

/-- The composition of morphisms of squares. -/
@[simps]
def comp {sq‚ÇÅ sq‚ÇÇ sq‚ÇÉ : Square C} (f : Hom sq‚ÇÅ sq‚ÇÇ) (g : Hom sq‚ÇÇ sq‚ÇÉ) : Hom sq‚ÇÅ sq‚ÇÉ where
  œÑ‚ÇÅ := f.œÑ‚ÇÅ ‚â´ g.œÑ‚ÇÅ
  œÑ‚ÇÇ := f.œÑ‚ÇÇ ‚â´ g.œÑ‚ÇÇ
  œÑ‚ÇÉ := f.œÑ‚ÇÉ ‚â´ g.œÑ‚ÇÉ
  œÑ‚ÇÑ := f.œÑ‚ÇÑ ‚â´ g.œÑ‚ÇÑ

end Hom

@[simps!]
instance category : Category (Square C) where
  Hom := Hom
  id := Hom.id
  comp := Hom.comp

@[ext]
lemma hom_ext {sq‚ÇÅ sq‚ÇÇ : Square C} {f g : sq‚ÇÅ ‚ü∂ sq‚ÇÇ}
    (h‚ÇÅ : f.œÑ‚ÇÅ = g.œÑ‚ÇÅ) (h‚ÇÇ : f.œÑ‚ÇÇ = g.œÑ‚ÇÇ)
    (h‚ÇÉ : f.œÑ‚ÇÉ = g.œÑ‚ÇÉ) (h‚ÇÑ : f.œÑ‚ÇÑ = g.œÑ‚ÇÑ) : f = g :=
  Hom.ext h‚ÇÅ h‚ÇÇ h‚ÇÉ h‚ÇÑ

/-- Constructor for isomorphisms in `Square c` -/
def isoMk {sq‚ÇÅ sq‚ÇÇ : Square C} (e‚ÇÅ : sq‚ÇÅ.X‚ÇÅ ‚âÖ sq‚ÇÇ.X‚ÇÅ) (e‚ÇÇ : sq‚ÇÅ.X‚ÇÇ ‚âÖ sq‚ÇÇ.X‚ÇÇ)
    (e‚ÇÉ : sq‚ÇÅ.X‚ÇÉ ‚âÖ sq‚ÇÇ.X‚ÇÉ) (e‚ÇÑ : sq‚ÇÅ.X‚ÇÑ ‚âÖ sq‚ÇÇ.X‚ÇÑ)
    (comm‚ÇÅ‚ÇÇ : sq‚ÇÅ.f‚ÇÅ‚ÇÇ ‚â´ e‚ÇÇ.hom = e‚ÇÅ.hom ‚â´ sq‚ÇÇ.f‚ÇÅ‚ÇÇ)
    (comm‚ÇÅ‚ÇÉ : sq‚ÇÅ.f‚ÇÅ‚ÇÉ ‚â´ e‚ÇÉ.hom = e‚ÇÅ.hom ‚â´ sq‚ÇÇ.f‚ÇÅ‚ÇÉ)
    (comm‚ÇÇ‚ÇÑ : sq‚ÇÅ.f‚ÇÇ‚ÇÑ ‚â´ e‚ÇÑ.hom = e‚ÇÇ.hom ‚â´ sq‚ÇÇ.f‚ÇÇ‚ÇÑ)
    (comm‚ÇÉ‚ÇÑ : sq‚ÇÅ.f‚ÇÉ‚ÇÑ ‚â´ e‚ÇÑ.hom = e‚ÇÉ.hom ‚â´ sq‚ÇÇ.f‚ÇÉ‚ÇÑ) :
    sq‚ÇÅ ‚âÖ sq‚ÇÇ where
  hom :=
    { œÑ‚ÇÅ := e‚ÇÅ.hom
      œÑ‚ÇÇ := e‚ÇÇ.hom
      œÑ‚ÇÉ := e‚ÇÉ.hom
      œÑ‚ÇÑ := e‚ÇÑ.hom }
  inv :=
    { œÑ‚ÇÅ := e‚ÇÅ.inv
      œÑ‚ÇÇ := e‚ÇÇ.inv
      œÑ‚ÇÉ := e‚ÇÉ.inv
      œÑ‚ÇÑ := e‚ÇÑ.inv
      comm‚ÇÅ‚ÇÇ := by simp only [‚Üê cancel_mono e‚ÇÇ.hom, assoc, Iso.inv_hom_id,
                      comp_id, comm‚ÇÅ‚ÇÇ, Iso.inv_hom_id_assoc]
      comm‚ÇÅ‚ÇÉ := by simp only [‚Üê cancel_mono e‚ÇÉ.hom, assoc, Iso.inv_hom_id,
                      comp_id, comm‚ÇÅ‚ÇÉ, Iso.inv_hom_id_assoc]
      comm‚ÇÇ‚ÇÑ := by simp only [‚Üê cancel_mono e‚ÇÑ.hom, assoc, Iso.inv_hom_id,
                      comp_id, comm‚ÇÇ‚ÇÑ, Iso.inv_hom_id_assoc]
      comm‚ÇÉ‚ÇÑ := by simp only [‚Üê cancel_mono e‚ÇÑ.hom, assoc, Iso.inv_hom_id,
                      comp_id, comm‚ÇÉ‚ÇÑ, Iso.inv_hom_id_assoc] }

/-- Flipping a square by switching the top-right and the bottom-left objects. -/
@[simps]
def flip (sq : Square C) : Square C where
  f‚ÇÅ‚ÇÇ := sq.f‚ÇÅ‚ÇÉ
  f‚ÇÅ‚ÇÉ := sq.f‚ÇÅ‚ÇÇ
  f‚ÇÇ‚ÇÑ := sq.f‚ÇÉ‚ÇÑ
  f‚ÇÉ‚ÇÑ := sq.f‚ÇÇ‚ÇÑ
  fac := sq.fac.symm

/-- The functor which flips commutative squares. -/
@[simps]
def flipFunctor : Square C ‚•§ Square C where
  obj := flip
  map œÜ :=
    { œÑ‚ÇÅ := œÜ.œÑ‚ÇÅ
      œÑ‚ÇÇ := œÜ.œÑ‚ÇÉ
      œÑ‚ÇÉ := œÜ.œÑ‚ÇÇ
      œÑ‚ÇÑ := œÜ.œÑ‚ÇÑ }

/-- Flipping commutative squares is an auto-equivalence. -/
@[simps]
def flipEquivalence : Square C ‚âå Square C where
  functor := flipFunctor
  inverse := flipFunctor
  unitIso := Iso.refl _
  counitIso := Iso.refl _

/-- The functor `Square C ‚•§ Arrow (Arrow C)` which sends a
commutative square `sq` to the obvious arrow from the left morphism of `sq`
to the right morphism of `sq`. -/
@[simps!]
def toArrowArrowFunctor : Square C ‚•§ Arrow (Arrow C) where
  obj sq := Arrow.mk (Arrow.homMk _ _ sq.fac : Arrow.mk sq.f‚ÇÅ‚ÇÉ ‚ü∂ Arrow.mk sq.f‚ÇÇ‚ÇÑ)
  map œÜ := Arrow.homMk (Arrow.homMk _ _ œÜ.comm‚ÇÅ‚ÇÉ.symm)
    (Arrow.homMk _ _ œÜ.comm‚ÇÇ‚ÇÑ.symm)

/-- The functor `Arrow (Arrow C) ‚•§ Square C` which sends
a morphism `Arrow.mk f ‚ü∂ Arrow.mk g` to the commutative square
with `f` on the left side and `g` on the right side. -/
@[simps!]
def fromArrowArrowFunctor : Arrow (Arrow C) ‚•§ Square C where
  obj f := { fac := f.hom.w, .. }
  map œÜ :=
    { œÑ‚ÇÅ := œÜ.left.left
      œÑ‚ÇÇ := œÜ.right.left
      œÑ‚ÇÉ := œÜ.left.right
      œÑ‚ÇÑ := œÜ.right.right
      comm‚ÇÅ‚ÇÇ := Arrow.leftFunc.congr_map œÜ.w.symm
      comm‚ÇÅ‚ÇÉ := œÜ.left.w.symm
      comm‚ÇÇ‚ÇÑ := œÜ.right.w.symm
      comm‚ÇÉ‚ÇÑ := Arrow.rightFunc.congr_map œÜ.w.symm }

/-- The equivalence `Square C ‚âå Arrow (Arrow C)` which sends a
commutative square `sq` to the obvious arrow from the left morphism of `sq`
to the right morphism of `sq`. -/
@[simps]
def arrowArrowEquivalence : Square C ‚âå Arrow (Arrow C) where
  functor := toArrowArrowFunctor
  inverse := fromArrowArrowFunctor
  unitIso := Iso.refl _
  counitIso := Iso.refl _

/-- The functor `Square C ‚•§ Arrow (Arrow C)` which sends a
commutative square `sq` to the obvious arrow from the top morphism of `sq`
to the bottom morphism of `sq`. -/
@[simps!]
def toArrowArrowFunctor' : Square C ‚•§ Arrow (Arrow C) where
  obj sq := Arrow.mk (Arrow.homMk _ _ sq.fac.symm : Arrow.mk sq.f‚ÇÅ‚ÇÇ ‚ü∂ Arrow.mk sq.f‚ÇÉ‚ÇÑ)
  map œÜ := Arrow.homMk (Arrow.homMk _ _ œÜ.comm‚ÇÅ‚ÇÇ.symm)
    (Arrow.homMk _ _ œÜ.comm‚ÇÉ‚ÇÑ.symm)

/-- The functor `Arrow (Arrow C) ‚•§ Square C` which sends
a morphism `Arrow.mk f ‚ü∂ Arrow.mk g` to the commutative square
with `f` on the top side and `g` on the bottom side. -/
@[simps!]
def fromArrowArrowFunctor' : Arrow (Arrow C) ‚•§ Square C where
  obj f := { fac := f.hom.w.symm, .. }
  map œÜ :=
    { œÑ‚ÇÅ := œÜ.left.left
      œÑ‚ÇÇ := œÜ.left.right
      œÑ‚ÇÉ := œÜ.right.left
      œÑ‚ÇÑ := œÜ.right.right
      comm‚ÇÅ‚ÇÇ := œÜ.left.w.symm
      comm‚ÇÅ‚ÇÉ := Arrow.leftFunc.congr_map œÜ.w.symm
      comm‚ÇÇ‚ÇÑ := Arrow.rightFunc.congr_map œÜ.w.symm
      comm‚ÇÉ‚ÇÑ := œÜ.right.w.symm }

/-- The equivalence `Square C ‚âå Arrow (Arrow C)` which sends a
commutative square `sq` to the obvious arrow from the top morphism of `sq`
to the bottom morphism of `sq`. -/
@[simps]
def arrowArrowEquivalence' : Square C ‚âå Arrow (Arrow C) where
  functor := toArrowArrowFunctor'
  inverse := fromArrowArrowFunctor'
  unitIso := Iso.refl _
  counitIso := Iso.refl _

/-- The top-left evaluation `Square C ‚•§ C`. -/
@[simps]
def evaluation‚ÇÅ : Square C ‚•§ C where
  obj sq := sq.X‚ÇÅ
  map œÜ := œÜ.œÑ‚ÇÅ

/-- The top-right evaluation `Square C ‚•§ C`. -/
@[simps]
def evaluation‚ÇÇ : Square C ‚•§ C where
  obj sq := sq.X‚ÇÇ
  map œÜ := œÜ.œÑ‚ÇÇ

/-- The bottom-left evaluation `Square C ‚•§ C`. -/
@[simps]
def evaluation‚ÇÉ : Square C ‚•§ C where
  obj sq := sq.X‚ÇÉ
  map œÜ := œÜ.œÑ‚ÇÉ

/-- The bottom-right evaluation `Square C ‚•§ C`. -/
@[simps]
def evaluation‚ÇÑ : Square C ‚•§ C where
  obj sq := sq.X‚ÇÑ
  map œÜ := œÜ.œÑ‚ÇÑ

/-- The map `Square C ‚Üí Square C·µí·µñ` which switches `X‚ÇÅ` and `X‚ÇÉ`, but
does not move `X‚ÇÇ` and `X‚ÇÉ`. -/
@[simps]
protected def op (sq : Square C) : Square C·µí·µñ where
  f‚ÇÅ‚ÇÇ := sq.f‚ÇÇ‚ÇÑ.op
  f‚ÇÅ‚ÇÉ := sq.f‚ÇÉ‚ÇÑ.op
  f‚ÇÇ‚ÇÑ := sq.f‚ÇÅ‚ÇÇ.op
  f‚ÇÉ‚ÇÑ := sq.f‚ÇÅ‚ÇÉ.op
  fac := Quiver.Hom.unop_inj sq.fac

/-- The map `Square C·µí·µñ ‚Üí Square C` which switches `X‚ÇÅ` and `X‚ÇÉ`, but
does not move `X‚ÇÇ` and `X‚ÇÉ`. -/
@[simps]
protected def unop (sq : Square C·µí·µñ) : Square C where
  f‚ÇÅ‚ÇÇ := sq.f‚ÇÇ‚ÇÑ.unop
  f‚ÇÅ‚ÇÉ := sq.f‚ÇÉ‚ÇÑ.unop
  f‚ÇÇ‚ÇÑ := sq.f‚ÇÅ‚ÇÇ.unop
  f‚ÇÉ‚ÇÑ := sq.f‚ÇÅ‚ÇÉ.unop
  fac := Quiver.Hom.op_inj sq.fac

/-- The functor `(Square C)·µí·µñ ‚•§ Square C·µí·µñ`. -/
@[simps]
def opFunctor : (Square C)·µí·µñ ‚•§ Square C·µí·µñ where
  obj sq := sq.unop.op
  map œÜ :=
    { œÑ‚ÇÅ := œÜ.unop.œÑ‚ÇÑ.op
      œÑ‚ÇÇ := œÜ.unop.œÑ‚ÇÇ.op
      œÑ‚ÇÉ := œÜ.unop.œÑ‚ÇÉ.op
      œÑ‚ÇÑ := œÜ.unop.œÑ‚ÇÅ.op
      comm‚ÇÅ‚ÇÇ := Quiver.Hom.unop_inj (by simp)
      comm‚ÇÅ‚ÇÉ := Quiver.Hom.unop_inj (by simp)
      comm‚ÇÇ‚ÇÑ := Quiver.Hom.unop_inj (by simp)
      comm‚ÇÉ‚ÇÑ := Quiver.Hom.unop_inj (by simp) }

/-- The functor `(Square C·µí·µñ)·µí·µñ ‚•§ Square C·µí·µñ`. -/
def unopFunctor : (Square C·µí·µñ)·µí·µñ ‚•§ Square C where
  obj sq := sq.unop.unop
  map œÜ :=
    { œÑ‚ÇÅ := œÜ.unop.œÑ‚ÇÑ.unop
      œÑ‚ÇÇ := œÜ.unop.œÑ‚ÇÇ.unop
      œÑ‚ÇÉ := œÜ.unop.œÑ‚ÇÉ.unop
      œÑ‚ÇÑ := œÜ.unop.œÑ‚ÇÅ.unop
      comm‚ÇÅ‚ÇÇ := Quiver.Hom.op_inj (by simp)
      comm‚ÇÅ‚ÇÉ := Quiver.Hom.op_inj (by simp)
      comm‚ÇÇ‚ÇÑ := Quiver.Hom.op_inj (by simp)
      comm‚ÇÉ‚ÇÑ := Quiver.Hom.op_inj (by simp) }

/-- The equivalence `(Square C)·µí·µñ ‚âå Square C·µí·µñ`. -/
def opEquivalence : (Square C)·µí·µñ ‚âå Square C·µí·µñ where
  functor := opFunctor
  inverse := unopFunctor.rightOp
  unitIso := Iso.refl _
  counitIso := Iso.refl _

/-- The image of a commutative square by a functor. -/
@[simps]
def map (sq : Square C) (F : C ‚•§ D) : Square D where
  f‚ÇÅ‚ÇÇ := F.map sq.f‚ÇÅ‚ÇÇ
  f‚ÇÅ‚ÇÉ := F.map sq.f‚ÇÅ‚ÇÉ
  f‚ÇÇ‚ÇÑ := F.map sq.f‚ÇÇ‚ÇÑ
  f‚ÇÉ‚ÇÑ := F.map sq.f‚ÇÉ‚ÇÑ
  fac := by simpa using F.congr_map sq.fac

end Square

variable {C}

namespace Functor

/-- The functor `Square C ‚•§ Square D` induced by a functor `C ‚•§ D`. -/
@[simps]
def mapSquare (F : C ‚•§ D) : Square C ‚•§ Square D where
  obj sq := sq.map F
  map œÜ :=
    { œÑ‚ÇÅ := F.map œÜ.œÑ‚ÇÅ
      œÑ‚ÇÇ := F.map œÜ.œÑ‚ÇÇ
      œÑ‚ÇÉ := F.map œÜ.œÑ‚ÇÉ
      œÑ‚ÇÑ := F.map œÜ.œÑ‚ÇÑ
      comm‚ÇÅ‚ÇÇ := by simpa only [Functor.map_comp] using F.congr_map œÜ.comm‚ÇÅ‚ÇÇ
      comm‚ÇÅ‚ÇÉ := by simpa only [Functor.map_comp] using F.congr_map œÜ.comm‚ÇÅ‚ÇÉ
      comm‚ÇÇ‚ÇÑ := by simpa only [Functor.map_comp] using F.congr_map œÜ.comm‚ÇÇ‚ÇÑ
      comm‚ÇÉ‚ÇÑ := by simpa only [Functor.map_comp] using F.congr_map œÜ.comm‚ÇÉ‚ÇÑ }

end Functor

/-- The natural transformation `F.mapSquare ‚ü∂ G.mapSquare` induces
by a natural transformation `F ‚ü∂ G`. -/
@[simps]
def NatTrans.mapSquare {F G : C ‚•§ D} (œÑ : F ‚ü∂ G) :
    F.mapSquare ‚ü∂ G.mapSquare where
  app sq :=
    { œÑ‚ÇÅ := œÑ.app _
      œÑ‚ÇÇ := œÑ.app _
      œÑ‚ÇÉ := œÑ.app _
      œÑ‚ÇÑ := œÑ.app _ }

/-- The functor `(C ‚•§ D) ‚•§ Square C ‚•§ Square D`. -/
@[simps]
def Square.mapFunctor : (C ‚•§ D) ‚•§ Square C ‚•§ Square D where
  obj F := F.mapSquare
  map œÑ := NatTrans.mapSquare œÑ

end CategoryTheory
