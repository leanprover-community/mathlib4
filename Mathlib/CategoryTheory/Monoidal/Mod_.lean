/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Paul Lezeau, Robin Carlier
-/
import Mathlib.CategoryTheory.Monoidal.Mon_
import Mathlib.CategoryTheory.Monoidal.Action.Basic

/-!
# The category of module objects over a monoid object.
-/

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

open CategoryTheory MonoidalCategory Mon_Class

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] [MonoidalCategory.{v‚ÇÅ} C]
  {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] [MonoidalLeftAction C D]

section Mod_Class

open Mon_Class

variable (M : C) [Mon_Class M]

open scoped MonoidalLeftAction
/-- Given an action of a monoidal category `C` on a category `D`,
an action of a monoid object `M` in `C` on an object `X` in `D` is the data of a
map `smul : M ‚äô‚Çó X ‚ü∂ X` that satisfies unitality and associativity with
multiplication.

See `MulAction` for the non-categorical version. -/
class Mod_Class (X : D) where
  /-- The action map -/
  smul : M ‚äô‚Çó X ‚ü∂ X
  /-- The identity acts trivially. -/
  one_smul' (X) : Œ∑ ‚äµ‚Çó X ‚â´ smul = (Œª‚Çó X).hom := by aesop_cat
  /-- The action map is compatible with multiplication. -/
  mul_smul' (X) : Œº ‚äµ‚Çó X ‚â´ smul = (Œ±‚Çó M M X).hom ‚â´ M ‚ä¥‚Çó smul ‚â´ smul := by aesop_cat

attribute [reassoc] Mod_Class.mul_smul' Mod_Class.one_smul'

@[inherit_doc] scoped[Mon_Class] notation "Œ≥" => Mod_Class.smul
@[inherit_doc] scoped[Mon_Class] notation "Œ≥["Y"]" => Mod_Class.smul (X := Y)
@[inherit_doc] scoped[Mon_Class] notation "Œ≥["N","Y"]" =>
  Mod_Class.smul (M := N) (X := Y)

variable {M}

namespace Mod_Class

@[reassoc (attr := simp)]
theorem one_smul (X : D) [Mod_Class M X] :
    Œ∑ ‚äµ‚Çó X ‚â´ Œ≥[M,X] = (Œª‚Çó[C] X).hom :=
  Mod_Class.one_smul' X

@[reassoc (attr := simp)]
theorem mul_smul (X : D) [Mod_Class M X] :
    Œº ‚äµ‚Çó X ‚â´ Œ≥ = (Œ±‚Çó M M X).hom ‚â´ M ‚ä¥‚Çó Œ≥ ‚â´ Œ≥ := Mod_Class.mul_smul' X

theorem assoc_flip (X : D) [Mod_Class M X] : M ‚ä¥‚Çó Œ≥ ‚â´ Œ≥ =
    (Œ±‚Çó M M X).inv ‚â´ Œº[M] ‚äµ‚Çó X ‚â´ Œ≥ := by
  simp

variable (M) in
/-- The action of a monoid object on itself. -/
-- See note [reducible non instances]
abbrev regular : Mod_Class M M where
  smul := Œº

/-- If `C` acts monoidally on `D`, then every object of `D` is canonically a
module over the trivial monoid. -/
@[simps]
instance (X : D) : Mod_Class (ùüô_ C) X where
  smul := (Œª‚Çó _).hom

@[ext]
theorem ext {X : C} (h‚ÇÅ h‚ÇÇ : Mod_Class M X) (H : h‚ÇÅ.smul = h‚ÇÇ.smul) :
    h‚ÇÅ = h‚ÇÇ := by
  cases h‚ÇÅ
  cases h‚ÇÇ
  subst H
  rfl

end Mod_Class

end Mod_Class

open scoped Mod_Class MonoidalLeftAction

variable (A : C) [Mon_Class A]
/-- A morphism in `D` is a morphism of `A`-module objects if it commutes with
the action maps -/
class IsMod_Hom {M N : D} [Mod_Class A M] [Mod_Class A N] (f : M ‚ü∂ N) where
  smul_hom : Œ≥[M] ‚â´ f = A ‚ä¥‚Çó f ‚â´ Œ≥[N] := by aesop_cat

attribute [reassoc (attr := simp)] IsMod_Hom.smul_hom

variable {M N O : D} [Mod_Class A M] [Mod_Class A N] [Mod_Class A O]

instance : IsMod_Hom A (ùüô M) where

instance (f : M ‚ü∂ N) (g : N ‚ü∂ O) [IsMod_Hom A f] [IsMod_Hom A g] :
    IsMod_Hom A (f ‚â´ g) where

instance (f : M ‚âÖ N) [IsMod_Hom A f.hom] :
    IsMod_Hom A f.inv where
  smul_hom := by simp [Iso.comp_inv_eq]

variable (D) in
/-- A module object for a monoid object in a monoidal category acting on the
ambient category. -/
structure Mod_ (A : C) [Mon_Class A] where
  /-- The underlying object in the ambient category -/
  X : D
  [mod : Mod_Class A X]

attribute [instance] Mod_.mod

namespace Mod_

variable {A : C} [Mon_Class A] (M : Mod_ D A)

theorem assoc_flip : A ‚ä¥‚Çó Œ≥ ‚â´ Œ≥ = (Œ±‚Çó A A M.X).inv ‚â´ Œº ‚äµ‚Çó M.X ‚â´ Œ≥ := by simp

/-- A morphism of module objects. -/
@[ext]
structure Hom (M N : Mod_ D A) where
  /-- The underlying morphism -/
  hom : M.X ‚ü∂ N.X
  [isMod_Hom : IsMod_Hom A hom]

attribute [instance] Hom.isMod_Hom

/-- An alternative constructor for `Hom`,
taking a morphism without a [isMod_Hom] instance, as well as the relevant
equality to put such an instance. -/
@[simps!]
def Hom.mk' {M N : Mod_ D A} (f : M.X ‚ü∂ N.X)
    (smul_hom : Œ≥[M.X] ‚â´ f = A ‚ä¥‚Çó f ‚â´ Œ≥[N.X] := by aesop_cat) : Hom M N :=
  letI : IsMod_Hom A f := ‚ü®smul_hom‚ü©
  ‚ü®f‚ü©

/-- An alternative constructor for `Hom`,
taking a morphism without a [isMod_Hom] instance, between objects with
a `Mod_Class` instance (rather than bundled as `Mod_`),
as well as the relevant equality to put such an instance. -/
@[simps!]
def Hom.mk'' {M N : D} [Mod_Class A M] [Mod_Class A N] (f : M ‚ü∂ N)
    (smul_hom : Œ≥[M] ‚â´ f = A ‚ä¥‚Çó f ‚â´ Œ≥[N] := by aesop_cat) :
    Hom (.mk (A := A) M) (.mk (A := A) N) :=
  letI : IsMod_Hom A f := ‚ü®smul_hom‚ü©
  ‚ü®f‚ü©

/-- The identity morphism on a module object. -/
@[simps]
def id (M : Mod_ D A) : Hom M M where hom := ùüô M.X

instance homInhabited (M : Mod_ D A) : Inhabited (Hom M M) :=
  ‚ü®id M‚ü©

/-- Composition of module object morphisms. -/
@[simps]
def comp {M N O : Mod_ D A} (f : Hom M N) (g : Hom N O) :
    Hom M O where
  hom := f.hom ‚â´ g.hom

instance : Category (Mod_ D A) where
  Hom M N := Hom M N
  id := id
  comp f g := comp f g

@[ext]
lemma hom_ext {M N : Mod_ D A} (f‚ÇÅ f‚ÇÇ : M ‚ü∂ N) (h : f‚ÇÅ.hom = f‚ÇÇ.hom) :
    f‚ÇÅ = f‚ÇÇ :=
  Hom.ext h

@[simp]
theorem id_hom' (M : Mod_ D A) : (ùüô M : M ‚ü∂ M).hom = ùüô M.X := by
  rfl

@[simp]
theorem comp_hom' {M N K : Mod_ D A} (f : M ‚ü∂ N) (g : N ‚ü∂ K) :
    (f ‚â´ g).hom = f.hom ‚â´ g.hom :=
  rfl

variable (A)

/-- A monoid object as a module over itself. -/
@[simps]
def regular : Mod_ C A :=
  letI : Mod_Class A A := .regular A
  ‚ü®A‚ü©

instance : Inhabited (Mod_ C A) :=
  ‚ü®regular A‚ü©

/-- The forgetful functor from module objects to the ambient category. -/
@[simps]
def forget : Mod_ D A ‚•§ D where
  obj A := A.X
  map f := f.hom

section comap

variable {A B : C} [Mon_Class A] [Mon_Class B] (f : A ‚ü∂ B) [IsMon_Hom f]

open MonoidalLeftAction in
/-- When `M` is a `B`-module in `D` and `f : A ‚ü∂ B` is a morphism of internal
monoid objects, `M` inherits an `A`-module structure via
"restriction of scalars", i.e `Œ≥[A, M] = f.hom ‚äµ‚Çó M ‚â´ Œ≥[B, M]`. -/
@[simps!]
def scalarRestriction (M : D) [Mod_Class B M] : Mod_Class A M where
  smul := f ‚äµ‚Çó M ‚â´ Œ≥[B, M]
  one_smul' := by
    rw [‚Üê comp_actionHomLeft_assoc]
    rw [IsMon_Hom.one_hom, Mod_Class.one_smul]
  mul_smul' := by
    -- oh, for homotopy.io in a widget!
    slice_rhs 2 3 => rw [action_exchange]
    simp only [actionHomLeft_action_assoc, Category.assoc, Iso.hom_inv_id_assoc,
      actionHomRight_comp]
    slice_rhs 4 6 => rw [Mod_Class.assoc_flip]
    slice_rhs 2 4 => rw [‚Üê whiskerLeft_actionHomLeft]
    slice_rhs 1 2 => rw [‚Üê comp_actionHomLeft]
    rw [‚Üê comp_actionHomLeft, Category.assoc, ‚Üê comp_actionHomLeft_assoc,
      IsMon_Hom.mul_hom, tensorHom_def, Category.assoc]

open MonoidalLeftAction in
/-- If `g : M ‚ü∂ N` is a `B`-linear morphisms of `B`-modules, then it induces an
`A`-linear morphism when `M` and `N` have an `A`-module structure obtained
by restricting scalars along a monoid morphism `A ‚ü∂ B`. -/
lemma scalarRestriction_hom
    (M N : D) [Mod_Class B M] [Mod_Class B N] (g : M ‚ü∂ N) [IsMod_Hom B g] :
    letI := scalarRestriction f M
    letI := scalarRestriction f N
    IsMod_Hom A g :=
  letI := scalarRestriction f M
  letI := scalarRestriction f N
  { smul_hom := by
      dsimp
      slice_rhs 1 2 => rw [action_exchange]
      slice_rhs 2 3 => rw [‚Üê IsMod_Hom.smul_hom]
      rw [Category.assoc] }

/-- A morphism of monoid objects induces a "restriction" or "comap" functor
between the categories of module objects.
-/
@[simps]
def comap {A B : C} [Mon_Class A] [Mon_Class B] (f : A ‚ü∂ B) [IsMon_Hom f] :
    Mod_ D B ‚•§ Mod_ D A where
  obj M :=
    letI := scalarRestriction f M.X
    ‚ü®M.X‚ü©
  map {M N} g :=
    letI := scalarRestriction_hom f M.X N.X g.hom
    ‚ü®g.hom‚ü©

-- Lots more could be said about `comap`, e.g. how it interacts with
-- identities, compositions, and equalities of monoid object morphisms.

end comap

end Mod_
