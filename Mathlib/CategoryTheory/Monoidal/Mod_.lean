/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Monoidal.Mon_

/-!
# The category of module objects over a monoid object.
-/


universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

open CategoryTheory MonoidalCategory

variable (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] [MonoidalCategory.{v‚ÇÅ} C]
variable {C}

/-- A module object for a monoid object, all internal to some monoidal category. -/
structure Mod_ (A : Mon_ C) where
  X : C
  act : A.X ‚äó X ‚ü∂ X
  one_act : (A.one ‚ñ∑ X) ‚â´ act = (Œª_ X).hom := by aesop_cat
  assoc : (A.mul ‚ñ∑ X) ‚â´ act = (Œ±_ A.X A.X X).hom ‚â´ (A.X ‚óÅ act) ‚â´ act := by aesop_cat

attribute [reassoc (attr := simp)] Mod_.one_act Mod_.assoc

namespace Mod_

variable {A : Mon_ C} (M : Mod_ A)

theorem assoc_flip :
    (A.X ‚óÅ M.act) ‚â´ M.act = (Œ±_ A.X A.X M.X).inv ‚â´ (A.mul ‚ñ∑ M.X) ‚â´ M.act := by simp

/-- A morphism of module objects. -/
@[ext]
structure Hom (M N : Mod_ A) where
  hom : M.X ‚ü∂ N.X
  act_hom : M.act ‚â´ hom = (A.X ‚óÅ hom) ‚â´ N.act := by aesop_cat

attribute [reassoc (attr := simp)] Hom.act_hom

/-- The identity morphism on a module object. -/
@[simps]
def id (M : Mod_ A) : Hom M M where hom := ùüô M.X

instance homInhabited (M : Mod_ A) : Inhabited (Hom M M) :=
  ‚ü®id M‚ü©

/-- Composition of module object morphisms. -/
@[simps]
def comp {M N O : Mod_ A} (f : Hom M N) (g : Hom N O) : Hom M O where hom := f.hom ‚â´ g.hom

instance : Category (Mod_ A) where
  Hom M N := Hom M N
  id := id
  comp f g := comp f g

@[ext]
lemma hom_ext {M N : Mod_ A} (f‚ÇÅ f‚ÇÇ : M ‚ü∂ N) (h : f‚ÇÅ.hom = f‚ÇÇ.hom) : f‚ÇÅ = f‚ÇÇ :=
  Hom.ext h

@[simp]
theorem id_hom' (M : Mod_ A) : (ùüô M : M ‚ü∂ M).hom = ùüô M.X := by
  rfl

@[simp]
theorem comp_hom' {M N K : Mod_ A} (f : M ‚ü∂ N) (g : N ‚ü∂ K) :
    (f ‚â´ g).hom = f.hom ‚â´ g.hom :=
  rfl

variable (A)

/-- A monoid object as a module over itself. -/
@[simps]
def regular : Mod_ A where
  X := A.X
  act := A.mul

instance : Inhabited (Mod_ A) :=
  ‚ü®regular A‚ü©

/-- The forgetful functor from module objects to the ambient category. -/
def forget : Mod_ A ‚•§ C where
  obj A := A.X
  map f := f.hom

open CategoryTheory.MonoidalCategory

#adaptation_note
/--
After https://github.com/leanprover/lean4/pull/6053
we needed to increase the `maxHeartbeats` limit.

This may indicate a configuration problem in Aesop.
-/
set_option maxHeartbeats 400000 in
/-- A morphism of monoid objects induces a "restriction" or "comap" functor
between the categories of module objects.
-/
@[simps]
def comap {A B : Mon_ C} (f : A ‚ü∂ B) : Mod_ B ‚•§ Mod_ A where
  obj M :=
    { X := M.X
      act := (f.hom ‚ñ∑ M.X) ‚â´ M.act
      one_act := by
        slice_lhs 1 2 => rw [‚Üê comp_whiskerRight]
        rw [f.one_hom, one_act]
      assoc := by
        -- oh, for homotopy.io in a widget!
        slice_rhs 2 3 => rw [whisker_exchange]
        simp only [whiskerRight_tensor, MonoidalCategory.whiskerLeft_comp, Category.assoc,
          Iso.hom_inv_id_assoc]
        slice_rhs 4 5 => rw [Mod_.assoc_flip]
        slice_rhs 3 4 => rw [associator_inv_naturality_middle]
        slice_rhs 2 4 => rw [Iso.hom_inv_id_assoc]
        slice_rhs 1 2 => rw [‚Üê MonoidalCategory.comp_whiskerRight, ‚Üê whisker_exchange]
        slice_rhs 1 2 => rw [‚Üê MonoidalCategory.comp_whiskerRight, ‚Üê tensorHom_def', ‚Üê f.mul_hom]
        rw [comp_whiskerRight, Category.assoc] }
  map g :=
    { hom := g.hom
      act_hom := by
        dsimp
        slice_rhs 1 2 => rw [whisker_exchange]
        slice_rhs 2 3 => rw [‚Üê g.act_hom]
        rw [Category.assoc] }

-- Lots more could be said about `comap`, e.g. how it interacts with
-- identities, compositions, and equalities of monoid object morphisms.
end Mod_
