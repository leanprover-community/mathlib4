/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Monoidal.Mon_

#align_import category_theory.monoidal.Mod_ from "leanprover-community/mathlib"@"33085c9739c41428651ac461a323fde9a2688d9b"

/-!
# The category of module objects over a monoid object.
-/


universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

open CategoryTheory MonoidalCategory

variable (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] [MonoidalCategory.{v‚ÇÅ} C]

variable {C}

/-- A module object for a monoid object, all internal to some monoidal category. -/
structure Mod_ (A : Mon_ C) where
  X : C
  act : A.X ‚äó X ‚ü∂ X
  one_act : (A.one ‚äó ùüô X) ‚â´ act = (Œª_ X).hom := by aesop_cat
  assoc : (A.mul ‚äó ùüô X) ‚â´ act = (Œ±_ A.X A.X X).hom ‚â´ (ùüô A.X ‚äó act) ‚â´ act := by aesop_cat
set_option linter.uppercaseLean3 false in
#align Mod_ Mod_

attribute [reassoc (attr := simp)] Mod_.one_act Mod_.assoc

namespace Mod_

variable {A : Mon_ C} (M : Mod_ A)

theorem assoc_flip :
    (ùüô A.X ‚äó M.act) ‚â´ M.act = (Œ±_ A.X A.X M.X).inv ‚â´ (A.mul ‚äó ùüô M.X) ‚â´ M.act := by simp
set_option linter.uppercaseLean3 false in
#align Mod_.assoc_flip Mod_.assoc_flip

/- ./././Mathport/Syntax/Translate/Expr.lean:177:8: unsupported: ambiguous notation -/
/-- A morphism of module objects. -/
@[ext]
structure Hom (M N : Mod_ A) where
  hom : M.X ‚ü∂ N.X
  act_hom : M.act ‚â´ hom = (ùüô A.X ‚äó hom) ‚â´ N.act := by aesop_cat
set_option linter.uppercaseLean3 false in
#align Mod_.hom Mod_.Hom

attribute [reassoc (attr := simp)] Hom.act_hom

/-- The identity morphism on a module object. -/
@[simps]
def id (M : Mod_ A) : Hom M M where hom := ùüô M.X
set_option linter.uppercaseLean3 false in
#align Mod_.id Mod_.id

instance homInhabited (M : Mod_ A) : Inhabited (Hom M M) :=
  ‚ü®id M‚ü©
set_option linter.uppercaseLean3 false in
#align Mod_.hom_inhabited Mod_.homInhabited

/-- Composition of module object morphisms. -/
@[simps]
def comp {M N O : Mod_ A} (f : Hom M N) (g : Hom N O) : Hom M O where hom := f.hom ‚â´ g.hom
set_option linter.uppercaseLean3 false in
#align Mod_.comp Mod_.comp

instance : Category (Mod_ A) where
  Hom M N := Hom M N
  id := id
  comp f g := comp f g

-- porting note: added because `Hom.ext` is not triggered automatically
-- See https://github.com/leanprover-community/mathlib4/issues/5229
@[ext]
lemma hom_ext {M N : Mod_ A} (f‚ÇÅ f‚ÇÇ : M ‚ü∂ N) (h : f‚ÇÅ.hom = f‚ÇÇ.hom) : f‚ÇÅ = f‚ÇÇ :=
  Hom.ext _ _ h

@[simp]
theorem id_hom' (M : Mod_ A) : (ùüô M : M ‚ü∂ M).hom = ùüô M.X := by
  rfl
set_option linter.uppercaseLean3 false in
#align Mod_.id_hom' Mod_.id_hom'

@[simp]
theorem comp_hom' {M N K : Mod_ A} (f : M ‚ü∂ N) (g : N ‚ü∂ K) :
    (f ‚â´ g).hom = f.hom ‚â´ g.hom :=
  rfl
set_option linter.uppercaseLean3 false in
#align Mod_.comp_hom' Mod_.comp_hom'

variable (A)

/-- A monoid object as a module over itself. -/
@[simps]
def regular : Mod_ A where
  X := A.X
  act := A.mul
set_option linter.uppercaseLean3 false in
#align Mod_.regular Mod_.regular

instance : Inhabited (Mod_ A) :=
  ‚ü®regular A‚ü©

/-- The forgetful functor from module objects to the ambient category. -/
def forget : Mod_ A ‚•§ C where
  obj A := A.X
  map f := f.hom
set_option linter.uppercaseLean3 false in
#align Mod_.forget Mod_.forget

open CategoryTheory.MonoidalCategory

/-- A morphism of monoid objects induces a "restriction" or "comap" functor
between the categories of module objects.
-/
@[simps]
def comap {A B : Mon_ C} (f : A ‚ü∂ B) : Mod_ B ‚•§ Mod_ A where
  obj M :=
    { X := M.X
      act := (f.hom ‚äó ùüô M.X) ‚â´ M.act
      one_act := by
        slice_lhs 1 2 => rw [‚Üê comp_tensor_id]
        rw [f.one_hom, one_act]
      assoc := by
        -- oh, for homotopy.io in a widget!
        slice_rhs 2 3 => rw [id_tensor_comp_tensor_id, ‚Üê tensor_id_comp_id_tensor]
        rw [id_tensor_comp]
        slice_rhs 4 5 => rw [Mod_.assoc_flip]
        slice_rhs 3 4 => rw [associator_inv_naturality]
        slice_rhs 2 3 => rw [‚Üê tensor_id, associator_inv_naturality]
        slice_rhs 1 3 => rw [Iso.hom_inv_id_assoc]
        slice_rhs 1 2 => rw [‚Üê comp_tensor_id, tensor_id_comp_id_tensor]
        slice_rhs 1 2 => rw [‚Üê comp_tensor_id, ‚Üê f.mul_hom]
        rw [comp_tensor_id, Category.assoc] }
  map g :=
    { hom := g.hom
      act_hom := by
        dsimp
        slice_rhs 1 2 => rw [id_tensor_comp_tensor_id, ‚Üê tensor_id_comp_id_tensor]
        slice_rhs 2 3 => rw [‚Üê g.act_hom]
        rw [Category.assoc] }
set_option linter.uppercaseLean3 false in
#align Mod_.comap Mod_.comap

-- Lots more could be said about `comap`, e.g. how it interacts with
-- identities, compositions, and equalities of monoid object morphisms.
end Mod_
