/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Monoidal.Free.Coherence
import Mathlib.Tactic.CategoryTheory.Coherence

#align_import category_theory.monoidal.opposite from "leanprover-community/mathlib"@"14b69e9f3c16630440a2cbd46f1ddad0d561dee7"

/-!
# Monoidal opposites

We write `C·µê·µí·µñ` for the monoidal opposite of a monoidal category `C`.
-/


universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

variable {C : Type u‚ÇÅ}

namespace CategoryTheory

open CategoryTheory.MonoidalCategory

/-- A type synonym for the monoidal opposite. Use the notation `C·¥π·µí·µñ`. -/
-- @[nolint has_nonempty_instance] -- Porting note: This linter does not exist yet.
def MonoidalOpposite (C : Type u‚ÇÅ) :=
  C
#align category_theory.monoidal_opposite CategoryTheory.MonoidalOpposite

namespace MonoidalOpposite

@[inherit_doc]
notation:max C "·¥π·µí·µñ" => MonoidalOpposite C

/-- Think of an object of `C` as an object of `C·¥π·µí·µñ`. -/
-- @[pp_nodot] -- Porting note: removed
def mop (X : C) : C·¥π·µí·µñ :=
  X
#align category_theory.monoidal_opposite.mop CategoryTheory.MonoidalOpposite.mop

/-- Think of an object of `C·¥π·µí·µñ` as an object of `C`. -/
-- @[pp_nodot] -- Porting note: removed
def unmop (X : C·¥π·µí·µñ) : C :=
  X
#align category_theory.monoidal_opposite.unmop CategoryTheory.MonoidalOpposite.unmop

theorem op_injective : Function.Injective (mop : C ‚Üí C·¥π·µí·µñ) :=
  fun _ _ => id
#align category_theory.monoidal_opposite.op_injective CategoryTheory.MonoidalOpposite.op_injective

theorem unop_injective : Function.Injective (unmop : C·¥π·µí·µñ ‚Üí C) :=
  fun _ _ => id
#align category_theory.monoidal_opposite.unop_injective CategoryTheory.MonoidalOpposite.unop_injective

@[simp]
theorem op_inj_iff (x y : C) : mop x = mop y ‚Üî x = y :=
  Iff.rfl
#align category_theory.monoidal_opposite.op_inj_iff CategoryTheory.MonoidalOpposite.op_inj_iff

@[simp]
theorem unop_inj_iff (x y : C·¥π·µí·µñ) : unmop x = unmop y ‚Üî x = y :=
  Iff.rfl
#align category_theory.monoidal_opposite.unop_inj_iff CategoryTheory.MonoidalOpposite.unop_inj_iff

@[simp]
theorem mop_unmop (X : C·¥π·µí·µñ) : mop (unmop X) = X :=
  rfl
#align category_theory.monoidal_opposite.mop_unmop CategoryTheory.MonoidalOpposite.mop_unmop

@[simp]
theorem unmop_mop (X : C) : unmop (mop X) = X :=
  rfl
#align category_theory.monoidal_opposite.unmop_mop CategoryTheory.MonoidalOpposite.unmop_mop

instance monoidalOppositeCategory [Category.{v‚ÇÅ} C] : Category C·¥π·µí·µñ :=
  InducedCategory.category unmop
#align category_theory.monoidal_opposite.monoidal_opposite_category CategoryTheory.MonoidalOpposite.monoidalOppositeCategory

end MonoidalOpposite

end CategoryTheory

open CategoryTheory

open CategoryTheory.MonoidalOpposite

variable [Category.{v‚ÇÅ} C]

/-- The monoidal opposite of a morphism `f : X ‚ü∂ Y` is just `f`, thought of as `mop X ‚ü∂ mop Y`. -/
def Quiver.Hom.mop {X Y : C} (f : X ‚ü∂ Y) : @Quiver.Hom C·¥π·µí·µñ _ (mop X) (mop Y) :=
  f
#align quiver.hom.mop Quiver.Hom.mop

/-- We can think of a morphism `f : mop X ‚ü∂ mop Y` as a morphism `X ‚ü∂ Y`. -/
def Quiver.Hom.unmop {X Y : C·¥π·µí·µñ} (f : X ‚ü∂ Y) : unmop X ‚ü∂ unmop Y :=
  f
#align quiver.hom.unmop Quiver.Hom.unmop

namespace CategoryTheory

theorem mop_inj {X Y : C} : Function.Injective (Quiver.Hom.mop : (X ‚ü∂ Y) ‚Üí (mop X ‚ü∂ mop Y)) :=
  fun _ _ H => congr_arg Quiver.Hom.unmop H
#align category_theory.mop_inj CategoryTheory.mop_inj

theorem unmop_inj {X Y : C·¥π·µí·µñ} :
    Function.Injective (Quiver.Hom.unmop : (X ‚ü∂ Y) ‚Üí (unmop X ‚ü∂ unmop Y)) :=
  fun _ _ H => congr_arg Quiver.Hom.mop H
#align category_theory.unmop_inj CategoryTheory.unmop_inj

@[simp]
theorem unmop_mop {X Y : C} {f : X ‚ü∂ Y} : f.mop.unmop = f :=
  rfl
#align category_theory.unmop_mop CategoryTheory.unmop_mop

@[simp]
theorem mop_unmop {X Y : C·¥π·µí·µñ} {f : X ‚ü∂ Y} : f.unmop.mop = f :=
  rfl
#align category_theory.mop_unmop CategoryTheory.mop_unmop

@[simp]
theorem mop_comp {X Y Z : C} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z} : (f ‚â´ g).mop = f.mop ‚â´ g.mop :=
  rfl
#align category_theory.mop_comp CategoryTheory.mop_comp

@[simp]
theorem mop_id {X : C} : (ùüô X).mop = ùüô (mop X) :=
  rfl
#align category_theory.mop_id CategoryTheory.mop_id

@[simp]
theorem unmop_comp {X Y Z : C·¥π·µí·µñ} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z} : (f ‚â´ g).unmop = f.unmop ‚â´ g.unmop :=
  rfl
#align category_theory.unmop_comp CategoryTheory.unmop_comp

@[simp]
theorem unmop_id {X : C·¥π·µí·µñ} : (ùüô X).unmop = ùüô (unmop X) :=
  rfl
#align category_theory.unmop_id CategoryTheory.unmop_id

@[simp]
theorem unmop_id_mop {X : C} : (ùüô (mop X)).unmop = ùüô X :=
  rfl
#align category_theory.unmop_id_mop CategoryTheory.unmop_id_mop

@[simp]
theorem mop_id_unmop {X : C·¥π·µí·µñ} : (ùüô (unmop X)).mop = ùüô X :=
  rfl
#align category_theory.mop_id_unmop CategoryTheory.mop_id_unmop

namespace Iso

variable {X Y : C}

/-- An isomorphism in `C` gives an isomorphism in `C·¥π·µí·µñ`. -/
@[simps]
def mop (f : X ‚âÖ Y) : mop X ‚âÖ mop Y where
  hom := f.hom.mop
  inv := f.inv.mop
  -- Porting note: it's a pity `attribute [aesop safe apply (rule_sets [CategoryTheory])] unmop_inj`
  -- doesn't automate these proofs.
  hom_inv_id := unmop_inj (by simp)
  inv_hom_id := unmop_inj (by simp)
#align category_theory.iso.mop CategoryTheory.Iso.mop

end Iso

namespace IsIso

instance {X Y : C}    (f : X ‚ü∂ Y) [i : IsIso f] : IsIso f.mop := i
instance {X Y : C·¥π·µí·µñ} (f : X ‚ü∂ Y) [i : IsIso f] : IsIso f.unmop := i

end IsIso

variable [MonoidalCategory.{v‚ÇÅ} C]

open Opposite MonoidalCategory

attribute [local simp] id_tensorHom tensorHom_id

instance monoidalCategoryOp : MonoidalCategory C·µí·µñ where
  tensorObj X Y := op (unop X ‚äó unop Y)
  whiskerLeft X _ _ f := (X.unop ‚óÅ f.unop).op
  whiskerRight f X := (f.unop ‚ñ∑ X.unop).op
  tensorHom f g := (f.unop ‚äó g.unop).op
  tensorHom_def f g := Quiver.Hom.unop_inj (tensorHom_def' _ _)
  tensorUnit := op (ùüô_ C)
  associator X Y Z := (Œ±_ (unop X) (unop Y) (unop Z)).symm.op
  leftUnitor X := (Œª_ (unop X)).symm.op
  rightUnitor X := (œÅ_ (unop X)).symm.op
  associator_naturality f g h := Quiver.Hom.unop_inj (by simp)
  leftUnitor_naturality f := Quiver.Hom.unop_inj (by simp)
  rightUnitor_naturality f := Quiver.Hom.unop_inj (by simp)
  triangle X Y := Quiver.Hom.unop_inj (by dsimp; coherence)
  pentagon W X Y Z := Quiver.Hom.unop_inj (by dsimp; coherence)
#align category_theory.monoidal_category_op CategoryTheory.monoidalCategoryOp

theorem op_tensorObj (X Y : C·µí·µñ) : X ‚äó Y = op (unop X ‚äó unop Y) :=
  rfl
#align category_theory.op_tensor_obj CategoryTheory.op_tensorObj

theorem op_tensorUnit : ùüô_ C·µí·µñ = op (ùüô_ C) :=
  rfl
#align category_theory.op_tensor_unit CategoryTheory.op_tensorUnit

instance monoidalCategoryMop : MonoidalCategory C·¥π·µí·µñ where
  tensorObj X Y := mop (unmop Y ‚äó unmop X)
  whiskerLeft X _ _ f := (f.unmop ‚ñ∑ X.unmop).mop
  whiskerRight f X := (X.unmop ‚óÅ f.unmop).mop
  tensorHom f g := (g.unmop ‚äó f.unmop).mop
  tensorHom_def f g := unmop_inj (tensorHom_def' _ _)
  tensorUnit := mop (ùüô_ C)
  associator X Y Z := (Œ±_ (unmop Z) (unmop Y) (unmop X)).symm.mop
  leftUnitor X := (œÅ_ (unmop X)).mop
  rightUnitor X := (Œª_ (unmop X)).mop
  associator_naturality f g h := unmop_inj (by simp)
  leftUnitor_naturality f := unmop_inj (by simp)
  rightUnitor_naturality f := unmop_inj (by simp)
  triangle X Y := unmop_inj (by simp) -- Porting note: Changed `by coherence` to `by simp`
  pentagon W X Y Z := unmop_inj (by dsimp; coherence)
#align category_theory.monoidal_category_mop CategoryTheory.monoidalCategoryMop

theorem mop_tensorObj (X Y : C·¥π·µí·µñ) : X ‚äó Y = mop (unmop Y ‚äó unmop X) :=
  rfl
#align category_theory.mop_tensor_obj CategoryTheory.mop_tensorObj

theorem mop_tensorUnit : ùüô_ C·¥π·µí·µñ = mop (ùüô_ C) :=
  rfl
#align category_theory.mop_tensor_unit CategoryTheory.mop_tensorUnit

variable (C)

@[simps!] def mopFunctor : C ‚•§ C·¥π·µí·µñ := Functor.mk ‚ü®mop, mop‚ü©
@[simps!] def unmopFunctor : C·¥π·µí·µñ ‚•§ C := Functor.mk ‚ü®unmop, unmop‚ü©

@[simps!]
def MonoidalOpposite.underlyingEquiv : C ‚âå C·¥π·µí·µñ := Equivalence.refl

-- todo: upgrade to monoidal equivalence
@[simps!] def MonoidalOpposite.double_dual_equiv : C·¥π·µí·µñ·¥π·µí·µñ ‚âå C := Equivalence.refl

@[simps!]
def MonoidalOpposite.tensor_iso :
    tensor C·¥π·µí·µñ ‚âÖ (unmopFunctor C).prod (unmopFunctor C)
                  ‚ãô Prod.swap C C ‚ãô tensor C ‚ãô mopFunctor C :=
  Iso.refl _

variable {C}

@[simps!]
def MonoidalOpposite.tensorLeft_iso (X : C·¥π·µí·µñ) :
    tensorLeft X ‚âÖ unmopFunctor C ‚ãô tensorRight (unmop X) ‚ãô mopFunctor C :=
  Iso.refl _

@[simps!]
def MonoidalOpposite.tensorLeft_mop_iso (X : C) :
    tensorLeft (mop X) ‚âÖ unmopFunctor C ‚ãô tensorRight X ‚ãô mopFunctor C :=
  Iso.refl _

@[simps!]
def MonoidalOpposite.tensorLeft_unmop_iso (X : C·¥π·µí·µñ) :
    tensorLeft (unmop X) ‚âÖ mopFunctor C ‚ãô tensorRight X ‚ãô mopFunctor C :=
  Iso.refl _

@[simps!]
def MonoidalOpposite.tensorRight_iso (X : C·¥π·µí·µñ) :
    tensorRight X ‚âÖ unmopFunctor C ‚ãô tensorLeft (unmop X) ‚ãô mopFunctor C :=
  Iso.refl _

@[simps!]
def MonoidalOpposite.tensorRight_mop_iso (X : C) :
    tensorRight (mop X) ‚âÖ unmopFunctor C ‚ãô tensorLeft X ‚ãô mopFunctor C :=
  Iso.refl _

@[simps!]
def MonoidalOpposite.tensorRight_unmop_iso (X : C·¥π·µí·µñ) :
    tensorRight (unmop X) ‚âÖ mopFunctor C ‚ãô tensorLeft X ‚ãô mopFunctor C :=
  Iso.refl _

end CategoryTheory
