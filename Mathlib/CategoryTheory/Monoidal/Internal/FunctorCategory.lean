/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Monoidal.CommMon_
import Mathlib.CategoryTheory.Monoidal.Comon_
import Mathlib.CategoryTheory.Monoidal.FunctorCategory

/-!
# `Mon_ (C ‚•§ D) ‚âå C ‚•§ Mon_ D`

When `D` is a monoidal category,
monoid objects in `C ‚•§ D` are the same thing as functors from `C` into the monoid objects of `D`.

This is formalised as:
* `monFunctorCategoryEquivalence : Mon_ (C ‚•§ D) ‚âå C ‚•§ Mon_ D`

The intended application is that as `Ring ‚âå Mon_ Ab` (not yet constructed!),
we have `presheaf Ring X ‚âå presheaf (Mon_ Ab) X ‚âå Mon_ (presheaf Ab X)`,
and we can model a module over a presheaf of rings as a module object in `presheaf Ab X`.

## Future work
Presumably this statement is not specific to monoids,
and could be generalised to any internal algebraic objects,
if the appropriate framework was available.
-/


universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

open CategoryTheory MonoidalCategory Mon_Class Comon_Class

namespace CategoryTheory.Monoidal

variable (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C]
variable (D : Type u‚ÇÇ) [Category.{v‚ÇÇ} D] [MonoidalCategory.{v‚ÇÇ} D]

namespace MonFunctorCategoryEquivalence

variable {C D}

/-- A monoid object in a functor category sends any object to a monoid object. -/
@[simps]
def functorObjObj (A : C ‚•§ D) [Mon_Class A] (X : C) : Mon_ D where
  X := A.obj X
  mon :=
  { one := Œ∑[A].app X
    mul := Œº[A].app X
    one_mul := congr_app (one_mul A) X
    mul_one := congr_app (mul_one A) X
    mul_assoc := congr_app (mul_assoc A) X }

/-- A monoid object in a functor category induces a functor to the category of monoid objects. -/
@[simps]
def functorObj (A : C ‚•§ D) [Mon_Class A] : C ‚•§ Mon_ D where
  obj := functorObjObj A
  map f :=
    { hom := A.map f
      is_mon_hom :=
        { one_hom := by dsimp; rw [‚Üê Œ∑[A].naturality, tensorUnit_map]; dsimp; rw [Category.id_comp]
          mul_hom := by dsimp; rw [‚Üê Œº[A].naturality, tensorObj_map] } }
  map_id X := by ext; dsimp; rw [CategoryTheory.Functor.map_id]
  map_comp f g := by ext; dsimp; rw [Functor.map_comp]

/-- Functor translating a monoid object in a functor category
to a functor into the category of monoid objects.
-/
@[simps]
def functor : Mon_ (C ‚•§ D) ‚•§ C ‚•§ Mon_ D where
  obj A := functorObj A.X
  map f :=
  { app := fun X =>
    { hom := f.hom.app X
      is_mon_hom :=
        { one_hom := congr_app (IsMon_Hom.one_hom f.hom) X
          mul_hom := congr_app (IsMon_Hom.mul_hom f.hom) X } } }

/-- A functor to the category of monoid objects can be translated as a monoid object
in the functor category. -/
@[simps]
def inverseObj (F : C ‚•§ Mon_ D) : Mon_ (C ‚•§ D) where
  X := F ‚ãô Mon_.forget D
  mon :=
  { one := { app X := Œ∑[(F.obj X).X] }
    mul := { app X := Œº[(F.obj X).X] } }

/-- Functor translating a functor into the category of monoid objects
to a monoid object in the functor category
-/
@[simps]
def inverse : (C ‚•§ Mon_ D) ‚•§ Mon_ (C ‚•§ D) where
  obj := inverseObj
  map Œ± := .mk'
    { app := fun X => (Œ±.app X).hom
      naturality := fun _ _ f => congr_arg Mon_.Hom.hom (Œ±.naturality f) }

/-- The unit for the equivalence `Mon_ (C ‚•§ D) ‚âå C ‚•§ Mon_ D`.
-/
@[simps!]
def unitIso : ùü≠ (Mon_ (C ‚•§ D)) ‚âÖ functor ‚ãô inverse :=
  NatIso.ofComponents (fun A =>
  { hom := .mk' { app := fun _ => ùüô _ }
    inv := .mk' { app := fun _ => ùüô _ } })

/-- The counit for the equivalence `Mon_ (C ‚•§ D) ‚âå C ‚•§ Mon_ D`.
-/
@[simps!]
def counitIso : inverse ‚ãô functor ‚âÖ ùü≠ (C ‚•§ Mon_ D) :=
  NatIso.ofComponents (fun A =>
    NatIso.ofComponents (fun X => { hom := { hom := ùüô _ }, inv := { hom := ùüô _ } }))

end MonFunctorCategoryEquivalence

open MonFunctorCategoryEquivalence

/-- When `D` is a monoidal category,
monoid objects in `C ‚•§ D` are the same thing
as functors from `C` into the monoid objects of `D`.
-/
@[simps]
def monFunctorCategoryEquivalence : Mon_ (C ‚•§ D) ‚âå C ‚•§ Mon_ D where
  functor := functor
  inverse := inverse
  unitIso := unitIso
  counitIso := counitIso

namespace ComonFunctorCategoryEquivalence

variable {C D}

/-- A comonoid object in a functor category sends any object to a comonoid object. -/
@[simps]
def functorObjObj (A : C ‚•§ D) [Comon_Class A] (X : C) : Comon_ D where
  X := A.obj X
  comon :=
  { counit := Œµ[A].app X
    comul := Œî[A].app X
    counit_comul := congr_app (counit_comul A) X
    comul_counit := congr_app (comul_counit A) X
    comul_assoc := congr_app (comul_assoc A) X }

/--
A comonoid object in a functor category induces a functor to the category of comonoid objects.
-/
@[simps]
def functorObj (A : (C ‚•§ D)) [Comon_Class A] : C ‚•§ Comon_ D where
  obj := functorObjObj A
  map f :=
    { hom := A.map f
      is_comon_hom :=
        { hom_counit := by dsimp; rw [Œµ[A].naturality, tensorUnit_map]; dsimp; rw [Category.comp_id]
          hom_comul := by dsimp; rw [Œî[A].naturality, tensorObj_map] } }
  map_id X := by ext; dsimp; rw [CategoryTheory.Functor.map_id]
  map_comp f g := by ext; dsimp; rw [Functor.map_comp]

/-- Functor translating a comonoid object in a functor category
to a functor into the category of comonoid objects.
-/
@[simps]
def functor : Comon_ (C ‚•§ D) ‚•§ C ‚•§ Comon_ D where
  obj A := functorObj A.X
  map f :=
  { app := fun X =>
    { hom := f.hom.app X
      is_comon_hom :=
        { hom_counit := congr_app (IsComon_Hom.hom_counit f.hom) X
          hom_comul := congr_app (IsComon_Hom.hom_comul f.hom) X } } }

/-- A functor to the category of comonoid objects can be translated as a comonoid object
in the functor category. -/
@[simps]
def inverseObj (F : C ‚•§ Comon_ D) : Comon_ (C ‚•§ D) where
  X := F ‚ãô Comon_.forget D
  comon :=
  { counit := { app X := Œµ[(F.obj X).X] }
    comul := { app X := Œî[(F.obj X).X] } }

/-- Functor translating a functor into the category of comonoid objects
to a comonoid object in the functor category
-/
@[simps]
private def inverse : (C ‚•§ Comon_ D) ‚•§ Comon_ (C ‚•§ D) where
  obj := inverseObj
  map Œ± :=
    { hom :=
      { app := fun X => (Œ±.app X).hom
        naturality := fun _ _ f => congr_arg Comon_.Hom.hom (Œ±.naturality f) }
      is_comon_hom :=
        { hom_counit := by ext x; dsimp; rw [IsComon_Hom.hom_counit (Œ±.app x).hom]
          hom_comul := by ext x; dsimp; rw [IsComon_Hom.hom_comul (Œ±.app x).hom] } }

/-- The unit for the equivalence `Comon_ (C ‚•§ D) ‚âå C ‚•§ Comon_ D`.
-/
@[simps!]
private def unitIso : ùü≠ (Comon_ (C ‚•§ D)) ‚âÖ functor ‚ãô inverse :=
  NatIso.ofComponents (fun A =>
    { hom := .mk' { app := fun _ => ùüô _ }
      inv := .mk' { app := fun _ => ùüô _ } })

/-- The counit for the equivalence `Mon_ (C ‚•§ D) ‚âå C ‚•§ Mon_ D`.
-/
@[simps!]
def counitIso : inverse ‚ãô functor ‚âÖ ùü≠ (C ‚•§ Comon_ D) :=
  NatIso.ofComponents (fun A =>
    NatIso.ofComponents (fun X => { hom := { hom := ùüô _ }, inv := { hom := ùüô _ } }) )

end ComonFunctorCategoryEquivalence

open ComonFunctorCategoryEquivalence

/-- When `D` is a monoidal category,
comonoid objects in `C ‚•§ D` are the same thing
as functors from `C` into the comonoid objects of `D`.
-/
@[simps]
def comonFunctorCategoryEquivalence : Comon_ (C ‚•§ D) ‚âå C ‚•§ Comon_ D where
  functor := functor
  inverse := inverse
  unitIso := unitIso
  counitIso := counitIso

variable [BraidedCategory.{v‚ÇÇ} D]

namespace CommMonFunctorCategoryEquivalence

variable {C D}

/-- Functor translating a commutative monoid object in a functor category
to a functor into the category of commutative monoid objects.
-/
@[simps!]
def functor : CommMon_ (C ‚•§ D) ‚•§ C ‚•§ CommMon_ D where
  obj A :=
    { obj X :=
        { ((monFunctorCategoryEquivalence C D).functor.obj A.toMon_).obj X with
          comm := { mul_comm := congr_app (IsCommMon.mul_comm A.X) X } }
      map f :=
        CommMon_.homMk (((monFunctorCategoryEquivalence C D).functor.obj A.toMon_).map f) }
  map f :=
    { app X :=
        CommMon_.homMk (((monFunctorCategoryEquivalence C D).functor.map f.hom).app X) }

/-- Functor translating a functor into the category of commutative monoid objects
to a commutative monoid object in the functor category
-/
@[simps!]
def inverse : (C ‚•§ CommMon_ D) ‚•§ CommMon_ (C ‚•§ D) where
  obj F :=
    { (monFunctorCategoryEquivalence C D).inverse.obj (F ‚ãô CommMon_.forget‚ÇÇMon_ D) with
      comm := { mul_comm := by ext X; exact IsCommMon.mul_comm (F.obj X).X } }
  map Œ± := CommMon_.homMk ((monFunctorCategoryEquivalence C D).inverse.map (whiskerRight Œ± _))

/-- The unit for the equivalence `CommMon_ (C ‚•§ D) ‚âå C ‚•§ CommMon_ D`.
-/
@[simps!]
def unitIso : ùü≠ (CommMon_ (C ‚•§ D)) ‚âÖ functor ‚ãô inverse :=
  NatIso.ofComponents (fun A => CommMon_.mkIso (Iso.refl _))

/-- The counit for the equivalence `CommMon_ (C ‚•§ D) ‚âå C ‚•§ CommMon_ D`.
-/
@[simps!]
def counitIso : inverse ‚ãô functor ‚âÖ ùü≠ (C ‚•§ CommMon_ D) :=
  NatIso.ofComponents (fun A ‚Ü¶ NatIso.ofComponents (fun X ‚Ü¶ Iso.refl _))

end CommMonFunctorCategoryEquivalence

open CommMonFunctorCategoryEquivalence

/-- When `D` is a braided monoidal category,
commutative monoid objects in `C ‚•§ D` are the same thing
as functors from `C` into the commutative monoid objects of `D`.
-/
@[simps]
def commMonFunctorCategoryEquivalence : CommMon_ (C ‚•§ D) ‚âå C ‚•§ CommMon_ D where
  functor := functor
  inverse := inverse
  unitIso := unitIso
  counitIso := counitIso

end CategoryTheory.Monoidal
