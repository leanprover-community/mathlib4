/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.Monoidal.ExternalProduct.Basic
import Mathlib.CategoryTheory.Functor.KanExtension.Pointwise
import Mathlib.CategoryTheory.Limits.Final

/-!
# Preservation of pointwise left Kan extensions by external products

We prove that if a functor `H' : D' ⥤ V` is pointwise left Kan extended from
`H : D ⥤ V` along `L : D ⥤ D'`, and if `K : E ⥤ V` is any functor such that
for any `e : E`, the functor `tensorRight (K.obj e)` commutes with colimits of
shape `CostructuredArrow L d`, then the functor `H' ⊠ K` is pointwise left kan extended
from `H ⊠ K` along `L.prod (𝟭 E)`.

We also prove a similar criterion to establish that `K ⊠ H'` is pointwise left Kan
extended from `K ⊠ H` along `(𝟭 E).prod L`.
-/
universe v₁ v₂ v₃ v₄ u₁ u₂ u₃ u₄

namespace CategoryTheory.MonoidalCategory.ExternalProduct

noncomputable section

variable {V : Type u₁} [Category.{v₁} V] [MonoidalCategory V]
  {D : Type u₂} {D' : Type u₃} {E : Type u₄}
  [Category.{v₂} D] [Category.{v₃} D'] [Category.{v₄} E]
  {H : D ⥤ V} {L : D ⥤ D'} (H' : D' ⥤ V) (α : H ⟶ L ⋙ H') (K : E ⥤ V)

/-- Given an extension `α : H ⟶ L ⋙ H'`, this is the canonical extension
`H ⊠ K ⟶ L.prod (𝟭 E) ⋙ H' ⊠ K` it induces through bifunctoriality of the external product. -/
abbrev extensionUnitLeft : H ⊠ K ⟶ L.prod (𝟭 E) ⋙ H' ⊠ K :=
    (externalProductBifunctor D E V).map ((α, K.leftUnitor.inv) : (H, K) ⟶ (L ⋙ H', 𝟭 E ⋙ K))

/-- Given an extension `α : H ⟶ L ⋙ H'`, this is the canonical extension
`K ⊠ H ⟶ (𝟭 E).prod L ⋙ K ⊠ H'` it induces through bifunctoriality of the external product. -/
abbrev extensionUnitRight : K ⊠ H ⟶ (𝟭 E).prod L ⋙ K ⊠ H' :=
    (externalProductBifunctor E D V).map ((K.leftUnitor.inv, α) : (K, H) ⟶ (𝟭 E ⋙ K, L ⋙ H'))

/-- If `H' : D' ⥤ V` is pointwise left Kan extended along `L : D ⥤ D'` at `(d : D')`,
and if tensoring right with an object preserves colimis in `V`
then `H' ⊠ K : D' × E ⥤ V` is pointwise left Kan extended along `L × (𝟭 E)` at `(d, e)`
for every `e : E`. -/
def pointwiseLeftKanExtensionAtLeft
    (d : D') (P : (Functor.LeftExtension.mk H' α).IsPointwiseLeftKanExtensionAt d) (e : E)
    [Limits.PreservesColimitsOfShape (CostructuredArrow L d) (tensorRight <| K.obj e)] :
    Functor.LeftExtension.mk (H' ⊠ K) (extensionUnitLeft H' α K)|>.IsPointwiseLeftKanExtensionAt
      (d, e) := by
  dsimp [Functor.LeftExtension.IsPointwiseLeftKanExtensionAt]
  set cone := Functor.LeftExtension.mk (H' ⊠ K) (extensionUnitLeft H' α K)|>.coconeAt (d, e)
  let equiv := CostructuredArrow.prodEquivalence L (𝟭 E) d e|>.symm
  apply Limits.IsColimit.ofWhiskerEquivalence equiv
  let I : CostructuredArrow L d ⥤ (CostructuredArrow L d) × CostructuredArrow (𝟭 E) e :=
    -- this definition makes it easier to prove finality of I
    (prod.rightUnitorEquivalence (CostructuredArrow L d)).inverse ⋙
      (𝟭 _).prod (Functor.fromPUnit.{0} <| .mk <| 𝟙 _)
  letI : I.Final := by
    letI : Functor.fromPUnit.{0} (.mk (𝟙 e) : CostructuredArrow (𝟭 E) e)|>.Final :=
      Functor.final_fromPUnit_of_isTerminal <| CostructuredArrow.mkIdTerminal (S := 𝟭 E) (Y := e)
    haveI := Functor.final_iff_final_comp
      (F := (prod.rightUnitorEquivalence <| CostructuredArrow L d).inverse)
      (G := (𝟭 _).prod <| Functor.fromPUnit.{0} (.mk (𝟙 e) : CostructuredArrow (𝟭 E) e))
    dsimp [I] at this ⊢
    rw [← this]
    infer_instance
  apply Functor.Final.isColimitWhiskerEquiv I (Limits.Cocone.whisker equiv.functor cone)|>.toFun
  -- through all the equivalences above, the new cocone we consider is in fact
  -- `(tensorRight (H.obj y)).mapCocone (dayConvolutionExtension G H).coconeAt y`
  let diag_iso :
      (CostructuredArrow.proj L d ⋙ H) ⋙ tensorRight (K.obj e) ≅
      I ⋙ equiv.functor ⋙ CostructuredArrow.proj (L.prod <| 𝟭 E) (d, e) ⋙ H ⊠ K :=
    NatIso.ofComponents (fun _ ↦ Iso.refl _)
  apply Limits.IsColimit.equivOfNatIsoOfIso diag_iso
    (d := Limits.Cocone.whisker I (Limits.Cocone.whisker equiv.functor cone))
    (c := tensorRight (K.obj e)|>.mapCocone <| (Functor.LeftExtension.mk H' α).coconeAt d)
    (Limits.Cocones.ext <| .refl _)|>.toFun
  exact Limits.PreservesColimit.preserves (F := tensorRight <| K.obj e) P|>.some

/-- If `H' : D' ⥤ V` is pointwise left Kan extended along `L : D ⥤ D'`,
and if tensoring right with an object preserves colimis in `V`
then `H' ⊠ K : D' × E ⥤ V` is pointwise left Kan extended along `L × (𝟭 E)`. -/
def pointwiseLeftKanExtensionLeft
    [∀ d : D', ∀ e : E,
      Limits.PreservesColimitsOfShape (CostructuredArrow L d) (tensorRight <| K.obj e)]
    (P : (Functor.LeftExtension.mk H' α).IsPointwiseLeftKanExtension) :
    Functor.LeftExtension.mk (H' ⊠ K) (extensionUnitLeft H' α K)|>.IsPointwiseLeftKanExtension :=
  fun ⟨d, e⟩ ↦ pointwiseLeftKanExtensionAtLeft H' α K d (P d) e

/-- If `H' : D' ⥤ V` is pointwise left Kan extended along `L : D ⥤ D'` at `d : D'` and
if tensoring left with an object preserves colimis in `V`,
then `K ⊠ H' : D' × E ⥤ V` is pointwise left Kan extended along `(𝟭 E) × L` at `(e, d)` for
every `e`. -/
def pointwiseLeftKanExtensionAtRight
    (d : D') (P : (Functor.LeftExtension.mk H' α).IsPointwiseLeftKanExtensionAt d) (e : E)
    [Limits.PreservesColimitsOfShape (CostructuredArrow L d) (tensorLeft <| K.obj e)] :
    (Functor.LeftExtension.mk (K ⊠ H')
      (extensionUnitRight H' α K)).IsPointwiseLeftKanExtensionAt (e, d) := by
  dsimp [Functor.LeftExtension.IsPointwiseLeftKanExtensionAt]
  set cone := Functor.LeftExtension.mk (K ⊠ H')
    (extensionUnitRight H' α K)|>.coconeAt (e, d)
  let equiv := CostructuredArrow.prodEquivalence (𝟭 E) L e d|>.symm
  apply Limits.IsColimit.ofWhiskerEquivalence equiv
  let I : CostructuredArrow L d ⥤ CostructuredArrow (𝟭 E) e × CostructuredArrow L d :=
    -- this definition makes it easier to prove finality of I
    (prod.leftUnitorEquivalence <| CostructuredArrow L d).inverse ⋙
      (Functor.fromPUnit.{0} <| .mk <| 𝟙 _).prod (𝟭 _)
  letI : I.Final := by
    letI : Functor.fromPUnit.{0} (.mk (𝟙 e) : (CostructuredArrow (𝟭 E) e))|>.Final :=
      Functor.final_fromPUnit_of_isTerminal <| CostructuredArrow.mkIdTerminal (S := 𝟭 E) (Y := e)
    haveI := Functor.final_iff_final_comp
      (F := (prod.leftUnitorEquivalence <| CostructuredArrow L d).inverse)
      (G := Functor.fromPUnit.{0} (.mk (𝟙 e) : CostructuredArrow (𝟭 E) e)|>.prod <| 𝟭 _)
    dsimp [I] at this ⊢
    rw [← this]
    infer_instance
  apply Functor.Final.isColimitWhiskerEquiv I (Limits.Cocone.whisker equiv.functor cone)|>.toFun
  -- through all the equivalences above, the new cocone we consider is in fact
  -- `(tensorRight (H.obj y)).mapCocone (dayConvolutionExtension G H).coconeAt y`
  let diag_iso :
      (CostructuredArrow.proj L d ⋙ H) ⋙ tensorLeft (K.obj e) ≅
      I ⋙ equiv.functor ⋙ CostructuredArrow.proj (𝟭 E|>.prod L) (e, d) ⋙ K ⊠ H :=
    NatIso.ofComponents (fun _ ↦ Iso.refl _)
  apply Limits.IsColimit.equivOfNatIsoOfIso diag_iso
    (d := Limits.Cocone.whisker I <| Limits.Cocone.whisker equiv.functor cone)
    (c := (tensorLeft <| K.obj e).mapCocone <| (Functor.LeftExtension.mk H' α).coconeAt d)
    (Limits.Cocones.ext <| .refl _)|>.toFun
  exact Limits.PreservesColimit.preserves (F := tensorLeft <| K.obj e) P|>.some

/-- If `H' : D' ⥤ V` is pointwise left Kan extended along `L : D ⥤ D'` and
if tensoring left with an object preserves colimis in `V`,
then `K ⊠ H' : D' × E ⥤ V` is pointwise left Kan extended along `(𝟭 E) × L`. -/
def pointwiseLeftKanExtensionRight
    [∀ d : D', ∀ e : E,
      Limits.PreservesColimitsOfShape (CostructuredArrow L d) (tensorLeft <| K.obj e)]
    (P : Functor.LeftExtension.mk H' α|>.IsPointwiseLeftKanExtension) :
    Functor.LeftExtension.mk (K ⊠ H') (extensionUnitRight H' α K)|>.IsPointwiseLeftKanExtension :=
  fun ⟨e, d⟩ ↦ pointwiseLeftKanExtensionAtRight H' α K d (P d) e

end

end CategoryTheory.MonoidalCategory.ExternalProduct
