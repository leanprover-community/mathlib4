/-
Copyright (c) 2025 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.CategoryTheory.Monoidal.Functor
/-!

# Constructing monoidal functors from natural transformations between multifunctors

This file provides alternative constructors for (op/lax) monoidal functors, given tensorators
`Î¼ : F - âŠ— F - âŸ¶  F (- âŠ— -)` / `Î´ : F (- âŠ— -) âŸ¶ F - âŠ— F -` as natural transformations between
bifunctors. The associativity conditions are phrased as equalities of natural transformations
between trifunctors `(F - âŠ— F -) âŠ— F - âŸ¶ F (- âŠ— (- âŠ— -))` / `F ((- âŠ— -) âŠ— -) âŸ¶ F - âŠ— (F - âŠ— F -)`,
and the unitality conditions are phrased as equalities of natural transformation between functors.

Once we have more API for quadrifunctors, we can add constructors for monoidal category structures
by phrasing the pentagon axiom as an equality of natural transformations between quadrifunctors.
-/

namespace CategoryTheory

variable {C : Type*} [Category C] [MonoidalCategory C]
  {D : Type*} [Category D] [MonoidalCategory D]

namespace MonoidalCategory

open Functor

/-- The bifunctor `(F -) âŠ— -`. -/
abbrev curriedTensorInsertFunctorâ‚ (F : C â¥¤ D) : C â¥¤ D â¥¤ D :=
  (((whiskeringLeftâ‚‚ _).obj F).obj (ğŸ­ D)).obj (curriedTensor D)

/-- The bifunctor `- âŠ— (F -)`. -/
abbrev curriedTensorInsertFunctorâ‚‚ (F : C â¥¤ D) : D â¥¤ C â¥¤ D :=
  (((whiskeringLeftâ‚‚ _).obj (ğŸ­ D)).obj F).obj (curriedTensor D)

/-- The bifunctor `F - âŠ— F -`. -/
abbrev curriedTensorPre (F : C â¥¤ D) : C â¥¤ C â¥¤ D :=
  (whiskeringLeftâ‚‚ _).obj F |>.obj F |>.obj (curriedTensor D)

/-- The bifunctor `F (- âŠ— -)`. -/
abbrev curriedTensorPost (F : C â¥¤ D) : C â¥¤ C â¥¤ D :=
  (Functor.postcomposeâ‚‚.obj F).obj (curriedTensor C)

/-- The trifunctor `(F - âŠ— F -) âŠ— F -`. -/
abbrev curriedTensorPrePre (F : C â¥¤ D) : C â¥¤ C â¥¤ C â¥¤ D :=
  bifunctorCompâ‚â‚‚ (curriedTensorPre F) (curriedTensorInsertFunctorâ‚‚ F)

/-- The trifunctor `F - âŠ— (F - âŠ— F -)`. -/
abbrev curriedTensorPrePre' (F : C â¥¤ D) : C â¥¤ C â¥¤ C â¥¤ D :=
  bifunctorCompâ‚‚â‚ƒ (curriedTensorInsertFunctorâ‚ F) (curriedTensorPre F)

/-- The trifunctor `F (- âŠ— -) âŠ— F -`. -/
abbrev curriedTensorPostPre (F : C â¥¤ D) : C â¥¤ C â¥¤ C â¥¤ D :=
  bifunctorCompâ‚â‚‚ (curriedTensor C) (curriedTensorPre F)

/-- The trifunctor `F - âŠ— F (- âŠ— -)`. -/
abbrev curriedTensorPrePost (F : C â¥¤ D) : C â¥¤ C â¥¤ C â¥¤ D :=
  bifunctorCompâ‚‚â‚ƒ (curriedTensorPre F) (curriedTensor C)

/-- The trifunctor `F ((- âŠ— -) âŠ— -)` -/
abbrev curriedTensorPostPost (F : C â¥¤ D) : C â¥¤ C â¥¤ C â¥¤ D :=
  bifunctorCompâ‚â‚‚ (curriedTensor C) (curriedTensorPost F)

/-- The trifunctor `F (- âŠ— (- âŠ— -))` -/
abbrev curriedTensorPostPost' (F : C â¥¤ D) : C â¥¤ C â¥¤ C â¥¤ D :=
  bifunctorCompâ‚‚â‚ƒ (curriedTensorPost F) (curriedTensor C)

end MonoidalCategory

open MonoidalCategory

namespace Functor.LaxMonoidal

/-!

# Lax monoidal functors

Given a unit morphism `Îµ : ğŸ™_ D âŸ¶ F.obj (ğŸ™_ C))` and a tensorator `Î¼ : F - âŠ— F - âŸ¶ F (- âŠ— -)`
such that the diagrams below commute, we define
`CategoryTheory.Functor.LaxMonoidal.ofBifunctor : F.LaxMonoidal`.

## Associativity hexagon

```
      (F - âŠ— F -) âŠ— F -
        /           \
       v             v
F (- âŠ— -) âŠ— F -    F - âŠ— (F - âŠ— F -)
       |             |
       v             v
F ((- âŠ— -) âŠ— -)    F - âŠ— F (- âŠ— -)
        \            /
         v          v
       F (- âŠ— (- âŠ— -))
```

## Left unitality square

```
ğŸ™ âŠ— F - âŸ¶ F ğŸ™ âŠ— F -
  |           |
  v           v
  F    â†   F (ğŸ™ âŠ— -)
```

## Right unitality square

```
F - âŠ— ğŸ™ âŸ¶ F - âŠ— F ğŸ™
  |           |
  v           v
  F   â†   F (- âŠ— ğŸ™)
```
-/

namespace ofBifunctor

/--
The top left map in the associativity hexagon.
-/
@[simps!]
def firstMapâ‚ {F : C â¥¤ D} (Î¼ : curriedTensorPre F âŸ¶ curriedTensorPost F) :
    curriedTensorPrePre F âŸ¶ curriedTensorPostPre F :=
  (bifunctorCompâ‚â‚‚Functor.map Î¼).app (curriedTensorInsertFunctorâ‚‚ F)

/--
The middle left map in the associativity hexagon.
-/
@[simps!]
def firstMapâ‚‚ {F : C â¥¤ D} (Î¼ : curriedTensorPre F âŸ¶ curriedTensorPost F) :
    (curriedTensorPostPre F) âŸ¶ curriedTensorPostPost F :=
  (bifunctorCompâ‚â‚‚Functor.obj _).map Î¼

/--
The bottom left map in the associativity hexagon.
-/
@[simps!]
def firstMapâ‚ƒ (F : C â¥¤ D) : curriedTensorPostPost F âŸ¶ curriedTensorPostPost' F :=
  (postcomposeâ‚ƒ.obj _).map (curriedAssociatorNatIso _).hom

/--
The composition of the left maps in the associativity hexagon.
-/
@[simps!]
def firstMap {F : C â¥¤ D} (Î¼ : curriedTensorPre F âŸ¶ curriedTensorPost F) :
    curriedTensorPrePre F âŸ¶ curriedTensorPostPost' F :=
  firstMapâ‚ Î¼ â‰« firstMapâ‚‚ Î¼ â‰« firstMapâ‚ƒ F

/--
The top right map in the associativity hexagon.
-/
@[simps!]
def secondMapâ‚ (F : C â¥¤ D) : curriedTensorPrePre F âŸ¶ curriedTensorPrePre' F :=
  ((((whiskeringLeftâ‚ƒ D).obj F).obj F).obj F).map (curriedAssociatorNatIso D).hom

/--
The middle right map in the associativity hexagon.
-/
@[simps!]
def secondMapâ‚‚ {F : C â¥¤ D} (Î¼ : curriedTensorPre F âŸ¶ curriedTensorPost F) :
    curriedTensorPrePre' F âŸ¶ curriedTensorPrePost F :=
  (bifunctorCompâ‚‚â‚ƒFunctor.obj _).map Î¼

/--
The bottom right map in the associativity hexagon.
-/
@[simps!]
def secondMapâ‚ƒ {F : C â¥¤ D} (Î¼ : curriedTensorPre F âŸ¶ curriedTensorPost F) :
    curriedTensorPrePost F âŸ¶ curriedTensorPostPost' F :=
  (bifunctorCompâ‚‚â‚ƒFunctor.map Î¼).app _

/--
The composition of the right maps in the associativity hexagon.
-/
@[simps!]
def secondMap {F : C â¥¤ D} (Î¼ : curriedTensorPre F âŸ¶ curriedTensorPost F) :
    curriedTensorPrePre F âŸ¶ curriedTensorPostPost' F :=
  secondMapâ‚ F â‰« secondMapâ‚‚ Î¼ â‰« secondMapâ‚ƒ Î¼

/--
The left map in the left unitality square.
-/
@[simps!]
def leftMapâ‚— (F : C â¥¤ D) : F â‹™ tensorUnitLeft D âŸ¶ F :=
  whiskerLeft F (leftUnitorNatIso D).hom

/--
The top map in the left unitality square.
-/
@[simps!]
def topMapâ‚— {F : C â¥¤ D} (Îµ : ğŸ™_ D âŸ¶ F.obj (ğŸ™_ C)) :
    F â‹™ tensorUnitLeft D âŸ¶ (curriedTensorPre F).obj (ğŸ™_ C) :=
  whiskerLeft F ((curriedTensor _).map Îµ )

/--
The bottom map in the left unitality square.
-/
@[simps!]
def bottomMapâ‚— (F : C â¥¤ D) : (curriedTensor C).obj (ğŸ™_ C) â‹™ F âŸ¶ F :=
  whiskerRight (leftUnitorNatIso C).hom F

/--
The left map in the right unitality square.
-/
@[simps!]
def leftMapáµ£ (F : C â¥¤ D) : F â‹™ tensorUnitRight D âŸ¶ F :=
  whiskerLeft F (rightUnitorNatIso D).hom

/--
The top map in the right unitality square.
-/
@[simps!]
def topMapáµ£ {F : C â¥¤ D} (Îµ : ğŸ™_ D âŸ¶ F.obj (ğŸ™_ C)) :
    F â‹™ tensorUnitRight D âŸ¶ (curriedTensorPre F).flip.obj (ğŸ™_ C) :=
  whiskerLeft F ((curriedTensor _).flip.map Îµ)

/--
The bottom map in the right unitality square.
-/
@[simps!]
def bottomMapáµ£ (F : C â¥¤ D) : (curriedTensor C).flip.obj (ğŸ™_ C) â‹™ F âŸ¶ F :=
  whiskerRight (rightUnitorNatIso C).hom F

end ofBifunctor

open ofBifunctor

variable {F : C â¥¤ D}
    /- unit morphism -/
    (Îµ : ğŸ™_ D âŸ¶ F.obj (ğŸ™_ C))
    /- tensorator as a morphism of bifunctors -/
    (Î¼ : curriedTensorPre F âŸ¶ curriedTensorPost F)
    /- the associativity hexagon commutes -/
    (associativity : firstMap Î¼ = secondMap Î¼)
    /- the left unitality square commutes -/
    (left_unitality : leftMapâ‚— F = topMapâ‚— Îµ â‰« Î¼.app (ğŸ™_ C) â‰« bottomMapâ‚— F)
    /- the right unitality square commutes -/
    (right_unitality : leftMapáµ£ F =
      topMapáµ£ Îµ â‰« ((flipFunctor _ _ _).map Î¼).app (ğŸ™_ C) â‰« bottomMapáµ£ F)

/--
`F` is lax monoidal given a unit morphism `Îµ : ğŸ™_ D âŸ¶ F.obj (ğŸ™_ C))` and a tensorator
`Î¼ : F - âŠ— F - âŸ¶ F (- âŠ— -)` as a natural transformation between bifunctors, satisfying the
relevant compatibilities.
-/
def ofBifunctor : F.LaxMonoidal where
  Îµ := Îµ
  Î¼ X Y := (Î¼.app X).app Y
  Î¼_natural_left f X := NatTrans.congr_app (Î¼.naturality f) X
  Î¼_natural_right X f := (Î¼.app X).naturality f
  associativity X Y Z :=
    NatTrans.congr_app (NatTrans.congr_app (NatTrans.congr_app associativity X) Y) Z
  left_unitality X := NatTrans.congr_app left_unitality X
  right_unitality X := NatTrans.congr_app right_unitality X

end LaxMonoidal

namespace OplaxMonoidal

/-!

# Oplax monoidal functors

Given a counit morphism `Î· : F.obj (ğŸ™_ C)) âŸ¶ ğŸ™_ D` and a tensorator `Î´ : F (- âŠ— -) âŸ¶ F - âŠ— F -`
such that the diagrams below commute, we define
`CategoryTheory.Functor.OplaxMonoidal.ofBifunctor : F.OplaxMonoidal`.

## Oplax associativity hexagon

```
      F ((- âŠ— -) âŠ— -)
        /           \
       v             v
F (- âŠ— -) âŠ— F -      F (- âŠ— (- âŠ— -))
       |                |
       v                v
(F - âŠ— F -) âŠ— F -    F - âŠ— F (- âŠ— -)
        \            /
         v          v
       F - âŠ— (F - âŠ— F -)
```

## Oplax left unitality square

```
  F   âŸ¶  F (ğŸ™ âŠ— -)
  |           |
  v           v
ğŸ™ âŠ— F - â† F ğŸ™ âŠ— F -
```

## Oplax right unitality square

```
  F  âŸ¶   F (- âŠ— ğŸ™)
  |           |
  v           v
F - âŠ— ğŸ™ â† F - âŠ— F ğŸ™
```
-/

namespace ofBifunctor

/--
The top left map in the oplax associativity hexagon.
-/
@[simps!]
def firstMapâ‚ {F : C â¥¤ D} (Î´ : curriedTensorPost F âŸ¶ curriedTensorPre F) :
    curriedTensorPostPost F âŸ¶ curriedTensorPostPre F :=
  (bifunctorCompâ‚â‚‚Functor.obj (curriedTensor C)).map Î´

/--
The middle left map in the oplax associativity hexagon.
-/
@[simps!]
def firstMapâ‚‚ {F : C â¥¤ D} (Î´ : curriedTensorPost F âŸ¶ curriedTensorPre F) :
    (curriedTensorPostPre F) âŸ¶ curriedTensorPrePre F :=
  (bifunctorCompâ‚â‚‚Functor.map Î´).app (curriedTensorInsertFunctorâ‚‚ F)

/--
The bottom left map in the oplax associativity hexagon.
-/
@[simps!]
def firstMapâ‚ƒ (F : C â¥¤ D) : curriedTensorPrePre F âŸ¶ curriedTensorPrePre' F :=
  ((((whiskeringLeftâ‚ƒ D).obj F).obj F).obj F).map (curriedAssociatorNatIso D).hom

/--
The composition of the three left maps in the oplax associativity hexagon.
-/
@[simps!]
def firstMap {F : C â¥¤ D} (Î´ : curriedTensorPost F âŸ¶ curriedTensorPre F) :
    curriedTensorPostPost F âŸ¶ curriedTensorPrePre' F :=
  firstMapâ‚ Î´ â‰« firstMapâ‚‚ Î´ â‰« firstMapâ‚ƒ F

/--
The top right map in the oplax associativity hexagon.
-/
@[simps!]
def secondMapâ‚ (F : C â¥¤ D) : curriedTensorPostPost F âŸ¶ curriedTensorPostPost' F :=
  (postcomposeâ‚ƒ.obj _).map (curriedAssociatorNatIso _).hom

/--
The middle right map in the oplax associativity hexagon.
-/
@[simps!]
def secondMapâ‚‚ {F : C â¥¤ D} (Î´ : curriedTensorPost F âŸ¶ curriedTensorPre F) :
    curriedTensorPostPost' F âŸ¶ curriedTensorPrePost F :=
  (bifunctorCompâ‚‚â‚ƒFunctor.map Î´).app _

/--
The bottom right map in the oplax associativity hexagon.
-/
@[simps!]
def secondMapâ‚ƒ {F : C â¥¤ D} (Î´ : curriedTensorPost F âŸ¶ curriedTensorPre F) :
    curriedTensorPrePost F âŸ¶ curriedTensorPrePre' F :=
  (bifunctorCompâ‚‚â‚ƒFunctor.obj (curriedTensorInsertFunctorâ‚ F)).map Î´

/--
The composition of the three right maps in the oplax associativity hexagon.
-/
@[simps!]
def secondMap {F : C â¥¤ D} (Î´ : curriedTensorPost F âŸ¶ curriedTensorPre F) :
    curriedTensorPostPost F âŸ¶ curriedTensorPrePre' F :=
  secondMapâ‚ F â‰« secondMapâ‚‚ Î´ â‰« secondMapâ‚ƒ Î´

/--
The left map in the oplax left unitality square.
-/
@[simps!]
def leftMapâ‚— (F : C â¥¤ D) : F âŸ¶ F â‹™ tensorUnitLeft D :=
  whiskerLeft F (leftUnitorNatIso D).inv

/--
The top map in the oplax left unitality square.
-/
@[simps!]
def topMapâ‚— (F : C â¥¤ D) : F âŸ¶ (curriedTensor C).obj (ğŸ™_ C) â‹™ F :=
  whiskerRight (leftUnitorNatIso C).inv F

/--
The bottom map in the oplax left unitality square.
-/
@[simps!]
def bottomMapâ‚— {F : C â¥¤ D} (Î· : F.obj (ğŸ™_ C) âŸ¶ ğŸ™_ D) :
    (curriedTensorPre F).obj (ğŸ™_ C) âŸ¶ F â‹™ tensorUnitLeft D :=
  whiskerLeft F ((curriedTensor _).map Î·)

/--
The left map in the oplax right unitality square.
-/
@[simps!]
def leftMapáµ£ (F : C â¥¤ D) : F âŸ¶ F â‹™ tensorUnitRight D :=
  whiskerLeft F (rightUnitorNatIso D).inv

/--
The top map in the oplax right unitality square.
-/
@[simps!]
def topMapáµ£ (F : C â¥¤ D) : F âŸ¶ (curriedTensor C).flip.obj (ğŸ™_ C) â‹™ F :=
  whiskerRight (rightUnitorNatIso C).inv F

/--
The bottom map in the oplax right unitality square.
-/
@[simps!]
def bottomMapáµ£ {F : C â¥¤ D} (Î· : F.obj (ğŸ™_ C) âŸ¶ ğŸ™_ D) :
    (curriedTensorPre F).flip.obj (ğŸ™_ C) âŸ¶ F â‹™ tensorUnitRight D :=
  whiskerLeft F ((curriedTensor _).flip.map Î·)

end ofBifunctor

open ofBifunctor

variable {F : C â¥¤ D}
    /- counit morphism -/
    (Î· : F.obj (ğŸ™_ C) âŸ¶ ğŸ™_ D)
    /- tensorator as a morphism of bifunctors -/
    (Î´ : curriedTensorPost F âŸ¶ curriedTensorPre F)
    /- the oplax associativity hexagon commutes -/
    (oplax_associativity : firstMap Î´ = secondMap Î´)
    /- the oplax left unitality square commutes -/
    (oplax_left_unitality : leftMapâ‚— F = topMapâ‚— F â‰« Î´.app (ğŸ™_ C) â‰« bottomMapâ‚— Î·)
    /- the oplax right unitality square commutes -/
    (oplax_right_unitality : leftMapáµ£ F =
      topMapáµ£ F â‰« ((flipFunctor _ _ _).map Î´).app (ğŸ™_ C) â‰« bottomMapáµ£ Î·)

/--
`F` is oplax monoidal given a counit morphism `Î· : F.obj (ğŸ™_ C) âŸ¶ ğŸ™_ D` and a tensorator
`Î´ : F (- âŠ— -) âŸ¶ F - âŠ— F -` as a natural transformation between bifunctors, satisfying the
relevant compatibilities.
-/
def ofBifunctor : F.OplaxMonoidal where
  Î· := Î·
  Î´ X Y := (Î´.app X).app Y
  Î´_natural_left f X := (NatTrans.congr_app (Î´.naturality f) X).symm
  Î´_natural_right X f := ((Î´.app X).naturality f).symm
  oplax_associativity X Y Z :=
    NatTrans.congr_app (NatTrans.congr_app (NatTrans.congr_app oplax_associativity X) Y) Z
  oplax_left_unitality X := NatTrans.congr_app oplax_left_unitality X
  oplax_right_unitality X := NatTrans.congr_app oplax_right_unitality X

end OplaxMonoidal

open LaxMonoidal.ofBifunctor OplaxMonoidal.ofBifunctor

namespace Monoidal

variable {F : C â¥¤ D}
    /- unit morphism -/
    (Îµ : ğŸ™_ D âŸ¶ F.obj (ğŸ™_ C))
    /- lax tensorator as a morphism of bifunctors -/
    (Î¼ : curriedTensorPre F âŸ¶ curriedTensorPost F)
    /- the lax associativity hexagon commutes -/
    (associativity : firstMap Î¼ = secondMap Î¼)
    /- the lax left unitality square commutes -/
    (left_unitality : LaxMonoidal.ofBifunctor.leftMapâ‚— F = topMapâ‚— Îµ â‰« Î¼.app (ğŸ™_ C) â‰« bottomMapâ‚— F)
    /- the lax right unitality square commutes -/
    (right_unitality : LaxMonoidal.ofBifunctor.leftMapáµ£ F =
      topMapáµ£ Îµ â‰« ((flipFunctor _ _ _).map Î¼).app (ğŸ™_ C) â‰« bottomMapáµ£ F)
    /- counit morphism -/
    (Î· : F.obj (ğŸ™_ C) âŸ¶ ğŸ™_ D)
    /- oplax tensorator as a morphism of bifunctors -/
    (Î´ : curriedTensorPost F âŸ¶ curriedTensorPre F)
    /- the oplax associativity hexagon commutes -/
    (oplax_associativity : firstMap Î´ = secondMap Î´)
    /- the left unitality square commutes -/
    (oplax_left_unitality : OplaxMonoidal.ofBifunctor.leftMapâ‚— F =
      topMapâ‚— F â‰« Î´.app (ğŸ™_ C) â‰« bottomMapâ‚— Î·)
    /- the right unitality square commutes -/
    (oplax_right_unitality : OplaxMonoidal.ofBifunctor.leftMapáµ£ F =
      topMapáµ£ F â‰« ((flipFunctor _ _ _).map Î´).app (ğŸ™_ C) â‰« bottomMapáµ£ Î·)

/--
`F` is monoidal given a co/unit morphisms `Îµ/Î· : ğŸ™_ D â†” F.obj (ğŸ™_ C)` and tensorators
`Î¼ / Î´ : F - âŠ— F - â†” F (- âŠ— -)` as natural transformations between bifunctors, satisfying the
relevant compatibilities.
-/
def ofBifunctor (Îµ_Î· : Îµ â‰« Î· = ğŸ™ _) (Î·_Îµ : Î· â‰« Îµ = ğŸ™ _) (Î¼_Î´ : Î¼ â‰« Î´ = ğŸ™ _)
    (Î´_Î¼ : Î´ â‰« Î¼ = ğŸ™ _) : F.Monoidal where
  toLaxMonoidal := .ofBifunctor Îµ Î¼ associativity left_unitality right_unitality
  toOplaxMonoidal := .ofBifunctor Î· Î´ oplax_associativity oplax_left_unitality oplax_right_unitality
  Îµ_Î· := Îµ_Î·
  Î·_Îµ := Î·_Îµ
  Î¼_Î´ X Y := NatTrans.congr_app ((NatTrans.congr_app Î¼_Î´) X) Y
  Î´_Î¼ X Y := NatTrans.congr_app ((NatTrans.congr_app Î´_Î¼) X) Y

end Monoidal

namespace CoreMonoidal

variable {F : C â¥¤ D}
    /- unit isomorphism -/
    (Îµ : ğŸ™_ D â‰… F.obj (ğŸ™_ C))
    /- tensorator as an isomorphism of bifunctors -/
    (Î¼ : curriedTensorPre F â‰… curriedTensorPost F)
    /- the associativity hexagon commutes -/
    (associativity : firstMap Î¼.hom = secondMap Î¼.hom)
    /- the left unitality square commutes -/
    (left_unitality : LaxMonoidal.ofBifunctor.leftMapâ‚— F =
      topMapâ‚— Îµ.hom â‰« Î¼.hom.app (ğŸ™_ C) â‰« bottomMapâ‚— F)
    /- the right unitality square commutes -/
    (right_unitality : LaxMonoidal.ofBifunctor.leftMapáµ£ F =
      topMapáµ£ Îµ.hom â‰« ((flipFunctor _ _ _).map Î¼.hom).app (ğŸ™_ C) â‰« bottomMapáµ£ F)

/--
`F` is monoidal given a unit isomorphism `Îµ : ğŸ™_ D â‰… F.obj (ğŸ™_ C)` and a tensorator isomorphism
`Î¼ : F - âŠ— F - â‰… F (- âŠ— -)` as a natural isomorphism between bifunctors, satisfying the
relevant compatibilities.
-/
def ofBifunctor : F.CoreMonoidal where
  ÎµIso := Îµ
  Î¼Iso X Y := (Î¼.app X).app Y
  Î¼Iso_hom_natural_left f X := NatTrans.congr_app (Î¼.hom.naturality f) X
  Î¼Iso_hom_natural_right X f := (Î¼.hom.app X).naturality f
  associativity X Y Z :=
    NatTrans.congr_app (NatTrans.congr_app (NatTrans.congr_app associativity X) Y) Z
  left_unitality X := NatTrans.congr_app left_unitality X
  right_unitality X := NatTrans.congr_app right_unitality X

end CategoryTheory.Functor.CoreMonoidal
