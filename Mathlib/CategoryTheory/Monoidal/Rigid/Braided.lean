/-
Copyright (c) 2024 Gareth Ma. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gareth Ma
-/
import Mathlib.CategoryTheory.Monoidal.Rigid.Basic
import Mathlib.CategoryTheory.Monoidal.Braided.Basic

/-!
# Deriving `RigidCategory` instance for braided and left/right rigid categories.
-/

open CategoryTheory Category BraidedCategory MonoidalCategory

variable {C : Type*} [Category C] [MonoidalCategory C] [BraidedCategory C] {X Y : C}

namespace CategoryTheory.BraidedCategory

/-- coevaluation_evaluation' field of `ExactPairing Y X` in a braided category -/
private theorem coevaluation_evaluation_braided' [inst : ExactPairing X Y] :
    X â— (Î·_ X Y â‰« (Î²_ Y X).inv) â‰« (Î±_ X Y X).inv â‰« ((Î²_ X Y).hom â‰« Îµ_ X Y) â–· X
      = (Ï_ X).hom â‰« (Î»_ X).inv := by
  /- Rearrange into _ = ğŸ™ _ -/
  rw [Iso.eq_comp_inv, â† Iso.inv_comp_eq_id]
  /- Whitney trick transcribed: https://mathoverflow.net/a/162729/493261 -/
  calc
    _ = ğŸ™ X âŠ—â‰« X â— Î·_ X Y âŠ—â‰« (X â— (Î²_ Y X).inv âŠ—â‰« (Î²_ X Y).hom â–· X) âŠ—â‰« Îµ_ X Y â–· X âŠ—â‰« ğŸ™ X := by
      monoidal
    _ = ğŸ™ X âŠ—â‰« X â— Î·_ X Y âŠ—â‰« (ğŸ™ (X âŠ— X âŠ— Y) âŠ—â‰« (Î²_ X X).hom â–· Y âŠ—â‰« X â— (Î²_ X Y).hom
          âŠ—â‰« (Î²_ Y X).inv â–· X âŠ—â‰« Y â— (Î²_ X X).inv âŠ—â‰« ğŸ™ ((Y âŠ— X) âŠ— X)) âŠ—â‰« Îµ_ X Y â–· X âŠ—â‰« ğŸ™ X := by
      congr 3
      simp only [monoidalComp, MonoidalCoherence.assoc'_iso, MonoidalCoherence.whiskerRight_iso,
        MonoidalCoherence.refl_iso, whiskerRightIso_refl, Iso.refl_trans, Iso.symm_hom,
        MonoidalCoherence.assoc_iso, Iso.trans_refl, comp_id, id_comp]
      rw [â† IsIso.eq_inv_comp]
      repeat rw [â† assoc]
      iterate 5 rw [â† IsIso.comp_inv_eq]
      simpa using yang_baxter X Y X
    _ = ğŸ™ X âŠ—â‰« (X â— Î·_ X Y â‰« (Î²_ X (X âŠ— Y)).hom) âŠ—â‰« ((Î²_ (Y âŠ— X) X).inv â‰« Îµ_ X Y â–· X) âŠ—â‰« ğŸ™ X := by
      simp [monoidalComp, braiding_tensor_right_hom, braiding_tensor_left_inv]
    _ = _ := by
      rw [braiding_naturality_right, â† braiding_inv_naturality_right]
      simp [monoidalComp]

/-- evaluation_coevaluation' field of `ExactPairing Y X` in a braided category -/
private theorem evaluation_coevaluation_braided' [inst : ExactPairing X Y] :
    (Î·_ X Y â‰« (Î²_ Y X).inv) â–· Y â‰« (Î±_ Y X Y).hom â‰« Y â— ((Î²_ X Y).hom â‰« Îµ_ X Y) =
      (Î»_ Y).hom â‰« (Ï_ Y).inv := by
  rw [Iso.eq_comp_inv, â† Iso.inv_comp_eq_id]
  calc
    _ = ğŸ™ Y âŠ—â‰« Î·_ X Y â–· Y âŠ—â‰« ((Î²_ Y X).inv â–· Y âŠ—â‰« Y â— (Î²_ X Y).hom) â‰« Y â— Îµ_ X Y âŠ—â‰« ğŸ™ Y := by
      monoidal
    _ = ğŸ™ Y âŠ—â‰« Î·_ X Y â–· Y âŠ—â‰« (ğŸ™ ((X âŠ— Y) âŠ— Y) âŠ—â‰« X â— (Î²_ Y Y).hom âŠ—â‰« (Î²_ X Y).hom â–· Y
        âŠ—â‰« Y â— (Î²_ Y X).inv âŠ—â‰« (Î²_ Y Y).inv â–· X âŠ—â‰« ğŸ™ (Y âŠ— Y âŠ— X)) âŠ—â‰« Y â— Îµ_ X Y âŠ—â‰« ğŸ™ Y := by
      congr 3
      all_goals simp [monoidalComp]
      iterate 2 rw [â† IsIso.eq_inv_comp]
      repeat rw [â† assoc]
      iterate 4 rw [â† IsIso.comp_inv_eq]
      simpa using (yang_baxter Y X Y).symm
    _ = ğŸ™ Y âŠ—â‰« (Î·_ X Y â–· Y â‰« (Î²_ (X âŠ— Y) Y).hom) âŠ—â‰« ((Î²_ Y (Y âŠ— X)).inv â‰« Y â— Îµ_ X Y) âŠ—â‰« ğŸ™ Y := by
      simp [monoidalComp, braiding_tensor_left_hom, braiding_tensor_right_inv]
    _ = _ := by
      rw [braiding_naturality_left, â† braiding_inv_naturality_left]
      simp [monoidalComp]

/-- If `X` and `Y` forms an exact pairing in a braided category, then so does `Y` and `X`
by composing the coevaluation and evaluation morphisms with associators. -/
def exactPairing_swap (X Y : C) [ExactPairing X Y] : ExactPairing Y X where
  coevaluation' := Î·_ X Y â‰« (Î²_ Y X).inv
  evaluation' := (Î²_ X Y).hom â‰« Îµ_ X Y
  coevaluation_evaluation' := coevaluation_evaluation_braided'
  evaluation_coevaluation' := evaluation_coevaluation_braided'

/-- If `X` has a right dual in a braided category, then it has a left dual. -/
def hasLeftDualOfHasRightDual [HasRightDual X] : HasLeftDual X where
  leftDual := Xá˜
  exact := exactPairing_swap X Xá˜

/-- If `X` has a left dual in a braided category, then it has a right dual. -/
def hasRightDualOfHasLeftDual [HasLeftDual X] : HasRightDual X where
  rightDual := á˜X
  exact := exactPairing_swap á˜X X

instance leftRigidCategoryOfRightRigidCategory [RightRigidCategory C] : LeftRigidCategory C where
  leftDual X := hasLeftDualOfHasRightDual (X := X)

instance rightRigidCategoryOfLeftRigidCategory [LeftRigidCategory C] : RightRigidCategory C where
  rightDual X := hasRightDualOfHasLeftDual (X := X)

/-- If `C` is a braided and right rigid category, then it is a rigid category. -/
instance rigidCategoryOfRightRigidCategory [RightRigidCategory C] : RigidCategory C where
  rightDual := inferInstance
  leftDual := inferInstance

/-- If `C` is a braided and left rigid category, then it is a rigid category. -/
instance rigidCategoryOfLeftRigidCategory [LeftRigidCategory C] : RigidCategory C where
  rightDual := inferInstance
  leftDual := inferInstance

end CategoryTheory.BraidedCategory
