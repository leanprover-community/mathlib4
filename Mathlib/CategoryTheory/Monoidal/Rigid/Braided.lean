/-
Copyright (c) 2024 Gareth Ma. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gareth Ma
-/
import Mathlib.CategoryTheory.Monoidal.Rigid.Basic
import Mathlib.CategoryTheory.Monoidal.Braided.Basic

/-!
# Deriving `RigidCategory` instance for braided and left/right rigid categories.
-/

open CategoryTheory Category BraidedCategory MonoidalCategory

variable {C : Type*} [Category C] [MonoidalCategory C] [BraidedCategory C] {X Y : C}

namespace CategoryTheory.BraidedCategory

/-- coevaluation_evaluation' field of `ExactPairing Y X` in a braided category -/
private theorem coevaluation_evaluation_braided' [inst : ExactPairing X Y] :
    X ‚óÅ (Œ∑_ X Y ‚â´ (Œ≤_ Y X).inv) ‚â´ (Œ±_ X Y X).inv ‚â´ ((Œ≤_ X Y).hom ‚â´ Œµ_ X Y) ‚ñ∑ X
      = (œÅ_ X).hom ‚â´ (Œª_ X).inv := by
  /- Rearrange into _ = ùüô _ -/
  rw [Iso.eq_comp_inv, ‚Üê Iso.inv_comp_eq_id]
  /- Whitney trick transcribed: https://mathoverflow.net/a/162729/493261 -/
  calc
    _ = ùüô X ‚äó‚â´ X ‚óÅ Œ∑_ X Y ‚äó‚â´ (X ‚óÅ (Œ≤_ Y X).inv ‚äó‚â´ (Œ≤_ X Y).hom ‚ñ∑ X) ‚äó‚â´ Œµ_ X Y ‚ñ∑ X ‚äó‚â´ ùüô X := by
      monoidal
    _ = ùüô X ‚äó‚â´ X ‚óÅ Œ∑_ X Y ‚äó‚â´ (ùüô (X ‚äó X ‚äó Y) ‚äó‚â´ (Œ≤_ X X).hom ‚ñ∑ Y ‚äó‚â´ X ‚óÅ (Œ≤_ X Y).hom
          ‚äó‚â´ (Œ≤_ Y X).inv ‚ñ∑ X ‚äó‚â´ Y ‚óÅ (Œ≤_ X X).inv ‚äó‚â´ ùüô ((Y ‚äó X) ‚äó X)) ‚äó‚â´ Œµ_ X Y ‚ñ∑ X ‚äó‚â´ ùüô X := by
      congr 3
      simp only [monoidalComp, MonoidalCoherence.assoc'_iso, MonoidalCoherence.whiskerRight_iso,
        MonoidalCoherence.refl_iso, whiskerRightIso_refl, Iso.refl_trans, Iso.symm_hom,
        MonoidalCoherence.assoc_iso, Iso.trans_refl, comp_id, id_comp]
      rw [‚Üê IsIso.eq_inv_comp]
      repeat rw [‚Üê assoc]
      iterate 5 rw [‚Üê IsIso.comp_inv_eq]
      simpa using yang_baxter X Y X
    _ = ùüô X ‚äó‚â´ (X ‚óÅ Œ∑_ X Y ‚â´ (Œ≤_ X (X ‚äó Y)).hom) ‚äó‚â´ ((Œ≤_ (Y ‚äó X) X).inv ‚â´ Œµ_ X Y ‚ñ∑ X) ‚äó‚â´ ùüô X := by
      simp [monoidalComp, braiding_tensor_right_hom, braiding_tensor_left_inv]
    _ = _ := by
      rw [braiding_naturality_right, ‚Üê braiding_inv_naturality_right]
      simp [monoidalComp]

/-- evaluation_coevaluation' field of `ExactPairing Y X` in a braided category -/
private theorem evaluation_coevaluation_braided' [inst : ExactPairing X Y] :
    (Œ∑_ X Y ‚â´ (Œ≤_ Y X).inv) ‚ñ∑ Y ‚â´ (Œ±_ Y X Y).hom ‚â´ Y ‚óÅ ((Œ≤_ X Y).hom ‚â´ Œµ_ X Y) =
      (Œª_ Y).hom ‚â´ (œÅ_ Y).inv := by
  rw [Iso.eq_comp_inv, ‚Üê Iso.inv_comp_eq_id]
  calc
    _ = ùüô Y ‚äó‚â´ Œ∑_ X Y ‚ñ∑ Y ‚äó‚â´ ((Œ≤_ Y X).inv ‚ñ∑ Y ‚äó‚â´ Y ‚óÅ (Œ≤_ X Y).hom) ‚â´ Y ‚óÅ Œµ_ X Y ‚äó‚â´ ùüô Y := by
      monoidal
    _ = ùüô Y ‚äó‚â´ Œ∑_ X Y ‚ñ∑ Y ‚äó‚â´ (ùüô ((X ‚äó Y) ‚äó Y) ‚äó‚â´ X ‚óÅ (Œ≤_ Y Y).hom ‚äó‚â´ (Œ≤_ X Y).hom ‚ñ∑ Y
        ‚äó‚â´ Y ‚óÅ (Œ≤_ Y X).inv ‚äó‚â´ (Œ≤_ Y Y).inv ‚ñ∑ X ‚äó‚â´ ùüô (Y ‚äó Y ‚äó X)) ‚äó‚â´ Y ‚óÅ Œµ_ X Y ‚äó‚â´ ùüô Y := by
      congr 3
      on_goal 2 => simp [monoidalComp]
      simp only [monoidalComp, MonoidalCoherence.assoc_iso, MonoidalCoherence.whiskerRight_iso,
        MonoidalCoherence.refl_iso, whiskerRightIso_refl, Iso.trans_refl,
        MonoidalCoherence.assoc'_iso, Iso.refl_trans, Iso.symm_hom, comp_id, id_comp]
      iterate 2 rw [‚Üê IsIso.eq_inv_comp]
      repeat rw [‚Üê assoc]
      iterate 4 rw [‚Üê IsIso.comp_inv_eq]
      simpa using (yang_baxter Y X Y).symm
    _ = ùüô Y ‚äó‚â´ (Œ∑_ X Y ‚ñ∑ Y ‚â´ (Œ≤_ (X ‚äó Y) Y).hom) ‚äó‚â´ ((Œ≤_ Y (Y ‚äó X)).inv ‚â´ Y ‚óÅ Œµ_ X Y) ‚äó‚â´ ùüô Y := by
      simp [monoidalComp, braiding_tensor_left_hom, braiding_tensor_right_inv]
    _ = _ := by
      rw [braiding_naturality_left, ‚Üê braiding_inv_naturality_left]
      simp [monoidalComp]

/-- If `X` and `Y` forms an exact pairing in a braided category, then so does `Y` and `X`
by composing the coevaluation and evaluation morphisms with associators. -/
def exactPairing_swap (X Y : C) [ExactPairing X Y] : ExactPairing Y X where
  coevaluation' := Œ∑_ X Y ‚â´ (Œ≤_ Y X).inv
  evaluation' := (Œ≤_ X Y).hom ‚â´ Œµ_ X Y
  coevaluation_evaluation' := coevaluation_evaluation_braided'
  evaluation_coevaluation' := evaluation_coevaluation_braided'

/-- If `X` has a right dual in a braided category, then it has a left dual. -/
def hasLeftDualOfHasRightDual [HasRightDual X] : HasLeftDual X where
  leftDual := X·òÅ
  exact := exactPairing_swap X X·òÅ

/-- If `X` has a left dual in a braided category, then it has a right dual. -/
def hasRightDualOfHasLeftDual [HasLeftDual X] : HasRightDual X where
  rightDual := ·òÅX
  exact := exactPairing_swap ·òÅX X

instance leftRigidCategoryOfRightRigidCategory [RightRigidCategory C] : LeftRigidCategory C where
  leftDual X := hasLeftDualOfHasRightDual (X := X)

instance rightRigidCategoryOfLeftRigidCategory [LeftRigidCategory C] : RightRigidCategory C where
  rightDual X := hasRightDualOfHasLeftDual (X := X)

/-- If `C` is a braided and right rigid category, then it is a rigid category. -/
instance rigidCategoryOfRightRigidCategory [RightRigidCategory C] : RigidCategory C where
  rightDual := inferInstance
  leftDual := inferInstance

/-- If `C` is a braided and left rigid category, then it is a rigid category. -/
instance rigidCategoryOfLeftRigidCategory [LeftRigidCategory C] : RigidCategory C where
  rightDual := inferInstance
  leftDual := inferInstance

end CategoryTheory.BraidedCategory
