/-
Copyright (c) 2022 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Monoidal.Rigid.Basic

/-!
# Transport rigid structures over a monoidal equivalence.
-/


namespace CategoryTheory

open MonoidalCategory Functor.LaxMonoidal Functor.OplaxMonoidal

variable {C D : Type*} [Category C] [Category D] [MonoidalCategory C] [MonoidalCategory D]
  (F : C â¥¤ D) [F.Monoidal]

/-- Given candidate data for an exact pairing,
which is sent by a faithful monoidal functor to an exact pairing,
the equations holds automatically. -/
def ExactPairing.ofFaithful [F.Faithful] {X Y : C} (eval : Y âŠ— X âŸ¶ ğŸ™_ C)
    (coeval : ğŸ™_ C âŸ¶ X âŠ— Y) [ExactPairing (F.obj X) (F.obj Y)]
    (map_eval : F.map eval = (Î´ F _ _) â‰« Îµ_ _ _ â‰« Îµ F)
    (map_coeval : F.map coeval = (Î· F) â‰« Î·_ _ _ â‰« Î¼ F _ _) : ExactPairing X Y where
  evaluation' := eval
  coevaluation' := coeval
  evaluation_coevaluation' :=
    F.map_injective <| by
      simp [map_eval, map_coeval, Functor.Monoidal.map_whiskerLeft,
        Functor.Monoidal.map_whiskerRight]
  coevaluation_evaluation' :=
    F.map_injective <| by
      simp [map_eval, map_coeval, Functor.Monoidal.map_whiskerLeft,
        Functor.Monoidal.map_whiskerRight]

/-- Given a pair of objects which are sent by a fully faithful functor to a pair of objects
with an exact pairing, we get an exact pairing.
-/
noncomputable def ExactPairing.ofFullyFaithful [F.Full] [F.Faithful] (X Y : C)
    [ExactPairing (F.obj X) (F.obj Y)] : ExactPairing X Y :=
  .ofFaithful F (F.preimage (Î´ F _ _ â‰« Îµ_ _ _ â‰« (Îµ F)))
    (F.preimage (Î· F â‰« Î·_ _ _ â‰« Î¼ F _ _)) (by simp) (by simp)

@[deprecated (since := "2025-10-17")] alias exactPairingOfFaithful := ExactPairing.ofFaithful

@[deprecated (since := "2025-10-17")]
alias exactPairingOfFullyFaithful := ExactPairing.ofFullyFaithful

variable {F}
variable {G : D â¥¤ C} (adj : F âŠ£ G) [F.IsEquivalence]

noncomputable section

/-- Pull back a left dual along an equivalence. -/
def hasLeftDualOfEquivalence (X : C) [HasLeftDual (F.obj X)] :
    HasLeftDual X where
  leftDual := G.obj (á˜(F.obj X))
  exact := by
    letI := exactPairingCongrLeft (X := F.obj (G.obj á˜(F.obj X)))
      (X' := á˜(F.obj X)) (Y := F.obj X) (adj.toEquivalence.counitIso.app á˜(F.obj X))
    apply ExactPairing.ofFullyFaithful F

/-- Pull back a right dual along an equivalence. -/
def hasRightDualOfEquivalence (X : C) [HasRightDual (F.obj X)] :
    HasRightDual X where
  rightDual := G.obj ((F.obj X)á˜)
  exact := by
    letI := exactPairingCongrRight (X := F.obj X) (Y := F.obj (G.obj (F.obj X)á˜))
      (Y' := (F.obj X)á˜) (adj.toEquivalence.counitIso.app (F.obj X)á˜)
    apply ExactPairing.ofFullyFaithful F

/-- Pull back a left rigid structure along an equivalence. -/
def leftRigidCategoryOfEquivalence [LeftRigidCategory D] :
    LeftRigidCategory C where leftDual X := hasLeftDualOfEquivalence adj X

/-- Pull back a right rigid structure along an equivalence. -/
def rightRigidCategoryOfEquivalence [RightRigidCategory D] :
    RightRigidCategory C where rightDual X := hasRightDualOfEquivalence adj X

/-- Pull back a rigid structure along an equivalence. -/
def rigidCategoryOfEquivalence [RigidCategory D] : RigidCategory C where
  leftDual X := hasLeftDualOfEquivalence adj X
  rightDual X := hasRightDualOfEquivalence adj X

end

end CategoryTheory
