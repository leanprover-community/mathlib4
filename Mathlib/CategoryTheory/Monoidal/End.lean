/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Andrew Yang
-/
import Mathlib.CategoryTheory.Monoidal.Functor

/-!
# Endofunctors as a monoidal category.

We give the monoidal category structure on `C â¥¤ C`,
and show that when `C` itself is monoidal, it embeds via a monoidal functor into `C â¥¤ C`.

## TODO

Can we use this to show coherence results, e.g. a cheap proof that `Î»_ (ğŸ™_ C) = Ï_ (ğŸ™_ C)`?
I suspect this is harder than is usually made out.
-/


universe v u

namespace CategoryTheory

open Functor.LaxMonoidal Functor.OplaxMonoidal Functor.Monoidal

variable (C : Type u) [Category.{v} C]

/-- The category of endofunctors of any category is a monoidal category,
with tensor product given by composition of functors
(and horizontal composition of natural transformations).

Note: due to the fact that composition of functors in mathlib is reversed compared to the
one usually found in the literature, this monoidal structure is in fact the monoidal
opposite of the one usually considered in the literature.
-/
def endofunctorMonoidalCategory : MonoidalCategory (C â¥¤ C) where
  tensorObj F G := F â‹™ G
  whiskerLeft X _ _ F := whiskerLeft X F
  whiskerRight F X := whiskerRight F X
  tensorHom Î± Î² := Î± â—« Î²
  tensorUnit := ğŸ­ C
  associator F G H := Functor.associator F G H
  leftUnitor F := Functor.leftUnitor F
  rightUnitor F := Functor.rightUnitor F

open CategoryTheory.MonoidalCategory

attribute [local instance] endofunctorMonoidalCategory

@[simp] theorem endofunctorMonoidalCategory_tensorUnit_obj (X : C) :
    (ğŸ™_ (C â¥¤ C)).obj X = X := rfl

@[simp] theorem endofunctorMonoidalCategory_tensorUnit_map {X Y : C} (f : X âŸ¶ Y) :
    (ğŸ™_ (C â¥¤ C)).map f = f := rfl

@[simp] theorem endofunctorMonoidalCategory_tensorObj_obj (F G : C â¥¤ C) (X : C) :
    (F âŠ— G).obj X = G.obj (F.obj X) := rfl

@[simp] theorem endofunctorMonoidalCategory_tensorObj_map (F G : C â¥¤ C) {X Y : C} (f : X âŸ¶ Y) :
    (F âŠ— G).map f = G.map (F.map f) := rfl

@[simp] theorem endofunctorMonoidalCategory_tensorMap_app
    {F G H K : C â¥¤ C} {Î± : F âŸ¶ G} {Î² : H âŸ¶ K} (X : C) :
    (Î± âŠ—â‚˜ Î²).app X = Î².app (F.obj X) â‰« K.map (Î±.app X) := rfl

@[simp] theorem endofunctorMonoidalCategory_whiskerLeft_app
    {F H K : C â¥¤ C} {Î² : H âŸ¶ K} (X : C) :
    (F â— Î²).app X = Î².app (F.obj X) := rfl

@[simp] theorem endofunctorMonoidalCategory_whiskerRight_app
    {F G H : C â¥¤ C} {Î± : F âŸ¶ G} (X : C) :
    (Î± â–· H).app X = H.map (Î±.app X) := rfl

@[simp] theorem endofunctorMonoidalCategory_associator_hom_app (F G H : C â¥¤ C) (X : C) :
  (Î±_ F G H).hom.app X = ğŸ™ _ := rfl

@[simp] theorem endofunctorMonoidalCategory_associator_inv_app (F G H : C â¥¤ C) (X : C) :
  (Î±_ F G H).inv.app X = ğŸ™ _ := rfl

@[simp] theorem endofunctorMonoidalCategory_leftUnitor_hom_app (F : C â¥¤ C) (X : C) :
  (Î»_ F).hom.app X = ğŸ™ _ := rfl

@[simp] theorem endofunctorMonoidalCategory_leftUnitor_inv_app (F : C â¥¤ C) (X : C) :
  (Î»_ F).inv.app X = ğŸ™ _ := rfl

@[simp] theorem endofunctorMonoidalCategory_rightUnitor_hom_app (F : C â¥¤ C) (X : C) :
  (Ï_ F).hom.app X = ğŸ™ _ := rfl

@[simp] theorem endofunctorMonoidalCategory_rightUnitor_inv_app (F : C â¥¤ C) (X : C) :
  (Ï_ F).inv.app X = ğŸ™ _ := rfl

namespace MonoidalCategory

variable [MonoidalCategory C]

/-- Tensoring on the right gives a monoidal functor from `C` into endofunctors of `C`.
-/
instance : (tensoringRight C).Monoidal :=
  Functor.CoreMonoidal.toMonoidal
    { ÎµIso := (rightUnitorNatIso C).symm
      Î¼Iso := fun X Y => (isoWhiskerRight (curriedAssociatorNatIso C)
      ((evaluation C (C â¥¤ C)).obj X â‹™ (evaluation C C).obj Y)) }

@[simp] lemma tensoringRight_Îµ :
    Îµ (tensoringRight C) = (rightUnitorNatIso C).inv := rfl

@[simp] lemma tensoringRight_Î· :
    Î· (tensoringRight C) = (rightUnitorNatIso C).hom := rfl

@[simp] lemma tensoringRight_Î¼ (X Y : C) (Z : C) :
    (Î¼ (tensoringRight C) X Y).app Z = (Î±_ Z X Y).hom := rfl

@[simp] lemma tensoringRight_Î´ (X Y : C) (Z : C) :
    (Î´ (tensoringRight C) X Y).app Z = (Î±_ Z X Y).inv := rfl

end MonoidalCategory

variable {C}
variable {M : Type*} [Category M] [MonoidalCategory M] (F : M â¥¤ (C â¥¤ C))

@[reassoc (attr := simp)]
theorem Î¼_Î´_app (i j : M) (X : C) [F.Monoidal] :
    (Î¼ F i j).app X â‰« (Î´ F i j).app X = ğŸ™ _ :=
  (Î¼Iso F i j).hom_inv_id_app X

@[reassoc (attr := simp)]
theorem Î´_Î¼_app (i j : M) (X : C) [F.Monoidal] :
    (Î´ F i j).app X â‰« (Î¼ F i j).app X = ğŸ™ _ :=
  (Î¼Iso F i j).inv_hom_id_app X

@[reassoc (attr := simp)]
theorem Îµ_Î·_app (X : C) [F.Monoidal] : (Îµ F).app X â‰« (Î· F).app X = ğŸ™ _ :=
  (ÎµIso F).hom_inv_id_app X

@[reassoc (attr := simp)]
theorem Î·_Îµ_app (X : C) [F.Monoidal] : (Î· F).app X â‰« (Îµ F).app X = ğŸ™ _ :=
  (ÎµIso F).inv_hom_id_app X

@[reassoc (attr := simp)]
theorem Îµ_naturality {X Y : C} (f : X âŸ¶ Y) [F.LaxMonoidal] :
    (Îµ F).app X â‰« (F.obj (ğŸ™_ M)).map f = f â‰« (Îµ F).app Y :=
  ((Îµ F).naturality f).symm

@[reassoc (attr := simp)]
theorem Î·_naturality {X Y : C} (f : X âŸ¶ Y) [F.OplaxMonoidal]:
    (Î· F).app X â‰« (ğŸ™_ (C â¥¤ C)).map f = (Î· F).app X â‰« f := by
  simp

@[reassoc (attr := simp)]
theorem Î¼_naturality {m n : M} {X Y : C} (f : X âŸ¶ Y) [F.LaxMonoidal] :
    (F.obj n).map ((F.obj m).map f) â‰« (Î¼ F m n).app Y = (Î¼ F m n).app X â‰« (F.obj _).map f :=
  (Î¼ F m n).naturality f

-- This is a simp lemma in the reverse direction via `NatTrans.naturality`.
@[reassoc]
theorem Î´_naturality {m n : M} {X Y : C} (f : X âŸ¶ Y) [F.OplaxMonoidal]:
    (Î´ F m n).app X â‰« (F.obj n).map ((F.obj m).map f) =
      (F.obj _).map f â‰« (Î´ F m n).app Y := by simp

-- This is not a simp lemma since it could be proved by the lemmas later.
@[reassoc]
theorem Î¼_naturalityâ‚‚ {m n m' n' : M} (f : m âŸ¶ m') (g : n âŸ¶ n') (X : C) [F.LaxMonoidal] :
    (F.map g).app ((F.obj m).obj X) â‰« (F.obj n').map ((F.map f).app X) â‰« (Î¼ F m' n').app X =
      (Î¼ F m n).app X â‰« (F.map (f âŠ—â‚˜ g)).app X := by
  have := congr_app (Î¼_natural F f g) X
  dsimp at this
  simpa using this

@[reassoc (attr := simp)]
theorem Î¼_naturalityâ‚— {m n m' : M} (f : m âŸ¶ m') (X : C) [F.LaxMonoidal]:
    (F.obj n).map ((F.map f).app X) â‰« (Î¼ F m' n).app X =
      (Î¼ F m n).app X â‰« (F.map (f â–· n)).app X := by
  rw [â† tensorHom_id, â† Î¼_naturalityâ‚‚ F f (ğŸ™ n) X]
  simp

@[reassoc (attr := simp)]
theorem Î¼_naturalityáµ£ {m n n' : M} (g : n âŸ¶ n') (X : C) [F.LaxMonoidal] :
    (F.map g).app ((F.obj m).obj X) â‰« (Î¼ F m n').app X =
      (Î¼ F m n).app X â‰« (F.map (m â— g)).app X := by
  rw [â† id_tensorHom, â† Î¼_naturalityâ‚‚ F (ğŸ™ m) g X]
  simp

@[reassoc (attr := simp)]
theorem Î´_naturalityâ‚— {m n m' : M} (f : m âŸ¶ m') (X : C) [F.OplaxMonoidal] :
    (Î´ F m n).app X â‰« (F.obj n).map ((F.map f).app X) =
      (F.map (f â–· n)).app X â‰« (Î´ F m' n).app X :=
  congr_app (Î´_natural_left F f n) X

@[reassoc (attr := simp)]
theorem Î´_naturalityáµ£ {m n n' : M} (g : n âŸ¶ n') (X : C) [F.OplaxMonoidal]:
    (Î´ F m n).app X â‰« (F.map g).app ((F.obj m).obj X) =
      (F.map (m â— g)).app X â‰« (Î´ F m n').app X :=
  congr_app (Î´_natural_right F m g) X

@[reassoc]
theorem left_unitality_app (n : M) (X : C) [F.LaxMonoidal]:
    (F.obj n).map ((Îµ F).app X) â‰« (Î¼ F (ğŸ™_ M) n).app X â‰« (F.map (Î»_ n).hom).app X = ğŸ™ _ :=
  congr_app (left_unitality F n).symm X

@[simp, reassoc]
theorem obj_Îµ_app (n : M) (X : C) [F.Monoidal]:
    (F.obj n).map ((Îµ F).app X) = (F.map (Î»_ n).inv).app X â‰« (Î´ F (ğŸ™_ M) n).app X := by
  rw [map_leftUnitor_inv]
  dsimp
  simp only [Category.id_comp, Category.assoc, Î¼_Î´_app, endofunctorMonoidalCategory_tensorObj_obj,
    Category.comp_id]

@[simp, reassoc]
theorem obj_Î·_app (n : M) (X : C) [F.Monoidal] :
    (F.obj n).map ((Î· F).app X) = (Î¼ F (ğŸ™_ M) n).app X â‰« (F.map (Î»_ n).hom).app X := by
  rw [â† cancel_mono ((F.obj n).map ((Îµ F).app X)), â† Functor.map_comp]
  simp

@[reassoc]
theorem right_unitality_app (n : M) (X : C) [F.Monoidal] :
    (Îµ F).app ((F.obj n).obj X) â‰« (Î¼ F n (ğŸ™_ M)).app X â‰« (F.map (Ï_ n).hom).app X = ğŸ™ _ :=
  congr_app (Functor.LaxMonoidal.right_unitality F n).symm X

@[simp]
theorem Îµ_app_obj (n : M) (X : C) [F.Monoidal] :
    (Îµ F).app ((F.obj n).obj X) = (F.map (Ï_ n).inv).app X â‰« (Î´ F n (ğŸ™_ M)).app X := by
  rw [map_rightUnitor_inv]
  dsimp
  simp only [Category.id_comp, Category.assoc, Î¼_Î´_app,
    endofunctorMonoidalCategory_tensorObj_obj, Category.comp_id]

@[simp]
theorem Î·_app_obj (n : M) (X : C) [F.Monoidal] :
    (Î· F).app ((F.obj n).obj X) = (Î¼ F n (ğŸ™_ M)).app X â‰« (F.map (Ï_ n).hom).app X := by
  rw [map_rightUnitor]
  dsimp
  simp only [Category.comp_id, Î¼_Î´_app_assoc]

@[reassoc]
theorem associativity_app (mâ‚ mâ‚‚ mâ‚ƒ : M) (X : C) [F.LaxMonoidal] :
    (F.obj mâ‚ƒ).map ((Î¼ F mâ‚ mâ‚‚).app X) â‰«
        (Î¼ F (mâ‚ âŠ— mâ‚‚) mâ‚ƒ).app X â‰« (F.map (Î±_ mâ‚ mâ‚‚ mâ‚ƒ).hom).app X =
      (Î¼ F mâ‚‚ mâ‚ƒ).app ((F.obj mâ‚).obj X) â‰« (Î¼ F mâ‚ (mâ‚‚ âŠ— mâ‚ƒ)).app X := by
  have := congr_app (associativity F mâ‚ mâ‚‚ mâ‚ƒ) X
  dsimp at this
  simpa using this

@[simp, reassoc]
theorem obj_Î¼_app (mâ‚ mâ‚‚ mâ‚ƒ : M) (X : C) [F.Monoidal] :
    (F.obj mâ‚ƒ).map ((Î¼ F mâ‚ mâ‚‚).app X) =
      (Î¼ F mâ‚‚ mâ‚ƒ).app ((F.obj mâ‚).obj X) â‰«
        (Î¼ F mâ‚ (mâ‚‚ âŠ— mâ‚ƒ)).app X â‰«
          (F.map (Î±_ mâ‚ mâ‚‚ mâ‚ƒ).inv).app X â‰« (Î´ F (mâ‚ âŠ— mâ‚‚) mâ‚ƒ).app X := by
  rw [â† associativity_app_assoc]
  simp

@[simp, reassoc]
theorem obj_Î¼_inv_app (mâ‚ mâ‚‚ mâ‚ƒ : M) (X : C) [F.Monoidal] :
    (F.obj mâ‚ƒ).map ((Î´ F mâ‚ mâ‚‚).app X) =
      (Î¼ F (mâ‚ âŠ— mâ‚‚) mâ‚ƒ).app X â‰«
        (F.map (Î±_ mâ‚ mâ‚‚ mâ‚ƒ).hom).app X â‰«
          (Î´ F mâ‚ (mâ‚‚ âŠ— mâ‚ƒ)).app X â‰« (Î´ F mâ‚‚ mâ‚ƒ).app ((F.obj mâ‚).obj X) := by
  rw [map_associator]
  dsimp
  simp only [Category.id_comp, Category.assoc, Î¼_Î´_app_assoc, Î¼_Î´_app,
    endofunctorMonoidalCategory_tensorObj_obj, Category.comp_id]

@[reassoc (attr := simp)]
theorem obj_zero_map_Î¼_app {m : M} {X Y : C} (f : X âŸ¶ (F.obj m).obj Y) [F.Monoidal] :
    (F.obj (ğŸ™_ M)).map f â‰« (Î¼ F m (ğŸ™_ M)).app _ =
    (Î· F).app _ â‰« f â‰« (F.map (Ï_ m).inv).app _ := by
  rw [â† cancel_epi ((Îµ F).app _), â† cancel_mono ((Î´ F _ _).app _)]
  simp

@[simp]
theorem obj_Î¼_zero_app (mâ‚ mâ‚‚ : M) (X : C) [F.Monoidal]:
    (Î¼ F (ğŸ™_ M) mâ‚‚).app ((F.obj mâ‚).obj X) â‰« (Î¼ F mâ‚ (ğŸ™_ M âŠ— mâ‚‚)).app X â‰«
    (F.map (Î±_ mâ‚ (ğŸ™_ M) mâ‚‚).inv).app X â‰« (Î´ F (mâ‚ âŠ— ğŸ™_ M) mâ‚‚).app X =
    (Î¼ F (ğŸ™_ M) mâ‚‚).app ((F.obj mâ‚).obj X) â‰«
    (F.map (Î»_ mâ‚‚).hom).app ((F.obj mâ‚).obj X) â‰« (F.obj mâ‚‚).map ((F.map (Ï_ mâ‚).inv).app X) := by
  rw [â† obj_Î·_app_assoc, â† Functor.map_comp]
  simp

/-- If `m âŠ— n â‰… ğŸ™_M`, then `F.obj m` is a left inverse of `F.obj n`. -/
@[simps!]
noncomputable def unitOfTensorIsoUnit (m n : M) (h : m âŠ— n â‰… ğŸ™_ M) [F.Monoidal] :
    F.obj m â‹™ F.obj n â‰… ğŸ­ C :=
  Î¼Iso F m n â‰ªâ‰« F.mapIso h â‰ªâ‰« (ÎµIso F).symm

/-- If `m âŠ— n â‰… ğŸ™_M` and `n âŠ— m â‰… ğŸ™_M` (subject to some commuting constraints),
  then `F.obj m` and `F.obj n` forms a self-equivalence of `C`. -/
@[simps]
noncomputable def equivOfTensorIsoUnit (m n : M) (hâ‚ : m âŠ— n â‰… ğŸ™_ M) (hâ‚‚ : n âŠ— m â‰… ğŸ™_ M)
    (H : hâ‚.hom â–· m â‰« (Î»_ m).hom = (Î±_ m n m).hom â‰« m â— hâ‚‚.hom â‰« (Ï_ m).hom) [F.Monoidal] :
    C â‰Œ C where
  functor := F.obj m
  inverse := F.obj n
  unitIso := (unitOfTensorIsoUnit F m n hâ‚).symm
  counitIso := unitOfTensorIsoUnit F n m hâ‚‚
  functor_unitIso_comp X := by
    dsimp
    simp only [Î¼_naturalityáµ£_assoc, Î¼_naturalityâ‚—_assoc, Î·_app_obj, Category.assoc,
      obj_Î¼_inv_app, Functor.map_comp, Î´_Î¼_app_assoc, obj_Îµ_app,
      unitOfTensorIsoUnit_inv_app]
    simp only [â† NatTrans.comp_app, â† F.map_comp, â† H, inv_hom_whiskerRight_assoc,
      Iso.inv_hom_id, Functor.map_id, NatTrans.id_app]

end CategoryTheory
