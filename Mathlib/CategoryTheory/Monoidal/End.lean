/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Andrew Yang
-/
import Mathlib.CategoryTheory.Monoidal.Functor

#align_import category_theory.monoidal.End from "leanprover-community/mathlib"@"85075bccb68ab7fa49fb05db816233fb790e4fe9"

/-!
# Endofunctors as a monoidal category.

We give the monoidal category structure on `C â¥¤ C`,
and show that when `C` itself is monoidal, it embeds via a monoidal functor into `C â¥¤ C`.

## TODO

Can we use this to show coherence results, e.g. a cheap proof that `Î»_ (ğŸ™_ C) = Ï_ (ğŸ™_ C)`?
I suspect this is harder than is usually made out.
-/


universe v u

namespace CategoryTheory

variable (C : Type u) [Category.{v} C]

/-- The category of endofunctors of any category is a monoidal category,
with tensor product given by composition of functors
(and horizontal composition of natural transformations).
-/
def endofunctorMonoidalCategory : MonoidalCategory (C â¥¤ C) where
  tensorObj F G := F â‹™ G
  whiskerLeft X _ _ F := whiskerLeft X F
  whiskerRight F X := whiskerRight F X
  tensorHom Î± Î² := Î± â—« Î²
  tensorUnit := ğŸ­ C
  associator F G H := Functor.associator F G H
  leftUnitor F := Functor.leftUnitor F
  rightUnitor F := Functor.rightUnitor F
#align category_theory.endofunctor_monoidal_category CategoryTheory.endofunctorMonoidalCategory

open CategoryTheory.MonoidalCategory

attribute [local instance] endofunctorMonoidalCategory

@[simp] theorem endofunctorMonoidalCategory_tensorUnit_obj (X : C) :
    (ğŸ™_ (C â¥¤ C)).obj X = X := rfl

@[simp] theorem endofunctorMonoidalCategory_tensorUnit_map {X Y : C} (f : X âŸ¶ Y) :
    (ğŸ™_ (C â¥¤ C)).map f = f := rfl

@[simp] theorem endofunctorMonoidalCategory_tensorObj_obj (F G : C â¥¤ C) (X : C) :
    (F âŠ— G).obj X = G.obj (F.obj X) := rfl

@[simp] theorem endofunctorMonoidalCategory_tensorObj_map (F G : C â¥¤ C) {X Y : C} (f : X âŸ¶ Y) :
    (F âŠ— G).map f = G.map (F.map f) := rfl

@[simp] theorem endofunctorMonoidalCategory_tensorMap_app
    {F G H K : C â¥¤ C} {Î± : F âŸ¶ G} {Î² : H âŸ¶ K} (X : C) :
    (Î± âŠ— Î²).app X = Î².app (F.obj X) â‰« K.map (Î±.app X) := rfl

@[simp] theorem endofunctorMonoidalCategory_whiskerLeft_app
    {F H K : C â¥¤ C} {Î² : H âŸ¶ K} (X : C) :
    (F â— Î²).app X = Î².app (F.obj X) := rfl

@[simp] theorem endofunctorMonoidalCategory_whiskerRight_app
    {F G H : C â¥¤ C} {Î± : F âŸ¶ G} (X : C) :
    (Î± â–· H).app X = H.map (Î±.app X) := rfl

@[simp] theorem endofunctorMonoidalCategory_associator_hom_app (F G H : C â¥¤ C) (X : C) :
  (Î±_ F G H).hom.app X = ğŸ™ _ := rfl

@[simp] theorem endofunctorMonoidalCategory_associator_inv_app (F G H : C â¥¤ C) (X : C) :
  (Î±_ F G H).inv.app X = ğŸ™ _ := rfl

@[simp] theorem endofunctorMonoidalCategory_leftUnitor_hom_app (F : C â¥¤ C) (X : C) :
  (Î»_ F).hom.app X = ğŸ™ _ := rfl

@[simp] theorem endofunctorMonoidalCategory_leftUnitor_inv_app (F : C â¥¤ C) (X : C) :
  (Î»_ F).inv.app X = ğŸ™ _ := rfl

@[simp] theorem endofunctorMonoidalCategory_rightUnitor_hom_app (F : C â¥¤ C) (X : C) :
  (Ï_ F).hom.app X = ğŸ™ _ := rfl

@[simp] theorem endofunctorMonoidalCategory_rightUnitor_inv_app (F : C â¥¤ C) (X : C) :
  (Ï_ F).inv.app X = ğŸ™ _ := rfl

/-- Tensoring on the right gives a monoidal functor from `C` into endofunctors of `C`.
-/
@[simps!]
def tensoringRightMonoidal [MonoidalCategory.{v} C] : MonoidalFunctor C (C â¥¤ C) :=
  { tensoringRight C with
    Îµ := (rightUnitorNatIso C).inv
    Î¼ := fun X Y => (isoWhiskerRight (curriedAssociatorNatIso C)
      ((evaluation C (C â¥¤ C)).obj X â‹™ (evaluation C C).obj Y)).hom }
#align category_theory.tensoring_right_monoidal CategoryTheory.tensoringRightMonoidal

variable {C}
variable {M : Type*} [Category M] [MonoidalCategory M] (F : MonoidalFunctor M (C â¥¤ C))

@[reassoc (attr := simp)]
theorem Î¼_hom_inv_app (i j : M) (X : C) : (F.Î¼ i j).app X â‰« (F.Î¼Iso i j).inv.app X = ğŸ™ _ :=
  (F.Î¼Iso i j).hom_inv_id_app X
#align category_theory.Î¼_hom_inv_app CategoryTheory.Î¼_hom_inv_app

@[reassoc (attr := simp)]
theorem Î¼_inv_hom_app (i j : M) (X : C) : (F.Î¼Iso i j).inv.app X â‰« (F.Î¼ i j).app X = ğŸ™ _ :=
  (F.Î¼Iso i j).inv_hom_id_app X
#align category_theory.Î¼_inv_hom_app CategoryTheory.Î¼_inv_hom_app

@[reassoc (attr := simp)]
theorem Îµ_hom_inv_app (X : C) : F.Îµ.app X â‰« F.ÎµIso.inv.app X = ğŸ™ _ :=
  F.ÎµIso.hom_inv_id_app X
#align category_theory.Îµ_hom_inv_app CategoryTheory.Îµ_hom_inv_app

@[reassoc (attr := simp)]
theorem Îµ_inv_hom_app (X : C) : F.ÎµIso.inv.app X â‰« F.Îµ.app X = ğŸ™ _ :=
  F.ÎµIso.inv_hom_id_app X
#align category_theory.Îµ_inv_hom_app CategoryTheory.Îµ_inv_hom_app

@[reassoc (attr := simp)]
theorem Îµ_naturality {X Y : C} (f : X âŸ¶ Y) : F.Îµ.app X â‰« (F.obj (ğŸ™_ M)).map f = f â‰« F.Îµ.app Y :=
  (F.Îµ.naturality f).symm
#align category_theory.Îµ_naturality CategoryTheory.Îµ_naturality

@[reassoc (attr := simp)]
theorem Îµ_inv_naturality {X Y : C} (f : X âŸ¶ Y) :
    (MonoidalFunctor.ÎµIso F).inv.app X â‰« (ğŸ™_ (C â¥¤ C)).map f = F.ÎµIso.inv.app X â‰« f := by
  aesop_cat
#align category_theory.Îµ_inv_naturality CategoryTheory.Îµ_inv_naturality

@[reassoc (attr := simp)]
theorem Î¼_naturality {m n : M} {X Y : C} (f : X âŸ¶ Y) :
    (F.obj n).map ((F.obj m).map f) â‰« (F.Î¼ m n).app Y = (F.Î¼ m n).app X â‰« (F.obj _).map f :=
  (F.toLaxMonoidalFunctor.Î¼ m n).naturality f
#align category_theory.Î¼_naturality CategoryTheory.Î¼_naturality

-- This is a simp lemma in the reverse direction via `NatTrans.naturality`.
@[reassoc]
theorem Î¼_inv_naturality {m n : M} {X Y : C} (f : X âŸ¶ Y) :
    (F.Î¼Iso m n).inv.app X â‰« (F.obj n).map ((F.obj m).map f) =
      (F.obj _).map f â‰« (F.Î¼Iso m n).inv.app Y :=
  ((F.Î¼Iso m n).inv.naturality f).symm
#align category_theory.Î¼_inv_naturality CategoryTheory.Î¼_inv_naturality

-- This is not a simp lemma since it could be proved by the lemmas later.
@[reassoc]
theorem Î¼_naturalityâ‚‚ {m n m' n' : M} (f : m âŸ¶ m') (g : n âŸ¶ n') (X : C) :
    (F.map g).app ((F.obj m).obj X) â‰« (F.obj n').map ((F.map f).app X) â‰« (F.Î¼ m' n').app X =
      (F.Î¼ m n).app X â‰« (F.map (f âŠ— g)).app X := by
  have := congr_app (F.toLaxMonoidalFunctor.Î¼_natural f g) X
  dsimp at this
  simpa using this
#align category_theory.Î¼_naturalityâ‚‚ CategoryTheory.Î¼_naturalityâ‚‚

@[reassoc (attr := simp)]
theorem Î¼_naturalityâ‚— {m n m' : M} (f : m âŸ¶ m') (X : C) :
    (F.obj n).map ((F.map f).app X) â‰« (F.Î¼ m' n).app X =
      (F.Î¼ m n).app X â‰« (F.map (f â–· n)).app X := by
  rw [â† tensorHom_id, â† Î¼_naturalityâ‚‚ F f (ğŸ™ n) X]
  simp
#align category_theory.Î¼_naturalityâ‚— CategoryTheory.Î¼_naturalityâ‚—

@[reassoc (attr := simp)]
theorem Î¼_naturalityáµ£ {m n n' : M} (g : n âŸ¶ n') (X : C) :
    (F.map g).app ((F.obj m).obj X) â‰« (F.Î¼ m n').app X =
      (F.Î¼ m n).app X â‰« (F.map (m â— g)).app X := by
  rw [â† id_tensorHom, â† Î¼_naturalityâ‚‚ F (ğŸ™ m) g X]
  simp
#align category_theory.Î¼_naturalityáµ£ CategoryTheory.Î¼_naturalityáµ£

@[reassoc (attr := simp)]
theorem Î¼_inv_naturalityâ‚— {m n m' : M} (f : m âŸ¶ m') (X : C) :
    (F.Î¼Iso m n).inv.app X â‰« (F.obj n).map ((F.map f).app X) =
      (F.map (f â–· n)).app X â‰« (F.Î¼Iso m' n).inv.app X := by
  rw [â† IsIso.comp_inv_eq, Category.assoc, â† IsIso.eq_inv_comp]
  simp
#align category_theory.Î¼_inv_naturalityâ‚— CategoryTheory.Î¼_inv_naturalityâ‚—

@[reassoc (attr := simp)]
theorem Î¼_inv_naturalityáµ£ {m n n' : M} (g : n âŸ¶ n') (X : C) :
    (F.Î¼Iso m n).inv.app X â‰« (F.map g).app ((F.obj m).obj X) =
      (F.map (m â— g)).app X â‰« (F.Î¼Iso m n').inv.app X := by
  rw [â† IsIso.comp_inv_eq, Category.assoc, â† IsIso.eq_inv_comp]
  simp
#align category_theory.Î¼_inv_naturalityáµ£ CategoryTheory.Î¼_inv_naturalityáµ£

@[reassoc]
theorem left_unitality_app (n : M) (X : C) :
    (F.obj n).map (F.Îµ.app X) â‰« (F.Î¼ (ğŸ™_ M) n).app X â‰« (F.map (Î»_ n).hom).app X = ğŸ™ _ := by
  have := congr_app (F.toLaxMonoidalFunctor.left_unitality n) X
  dsimp at this
  simpa using this.symm
#align category_theory.left_unitality_app CategoryTheory.left_unitality_app

-- Porting note: linter claims `simp can prove it`, but cnot
@[reassoc (attr := simp, nolint simpNF)]
theorem obj_Îµ_app (n : M) (X : C) :
    (F.obj n).map (F.Îµ.app X) = (F.map (Î»_ n).inv).app X â‰« (F.Î¼Iso (ğŸ™_ M) n).inv.app X := by
  refine Eq.trans ?_ (Category.id_comp _)
  rw [â† Category.assoc, â† IsIso.comp_inv_eq, â† IsIso.comp_inv_eq, Category.assoc]
  convert left_unitality_app F n X
  Â· simp
  Â· simp
#align category_theory.obj_Îµ_app CategoryTheory.obj_Îµ_app

-- Porting note: linter claims `simp can prove it`, but cnot
@[reassoc (attr := simp, nolint simpNF)]
theorem obj_Îµ_inv_app (n : M) (X : C) :
    (F.obj n).map (F.ÎµIso.inv.app X) = (F.Î¼ (ğŸ™_ M) n).app X â‰« (F.map (Î»_ n).hom).app X := by
  rw [â† cancel_mono ((F.obj n).map (F.Îµ.app X)), â† Functor.map_comp]
  simp
#align category_theory.obj_Îµ_inv_app CategoryTheory.obj_Îµ_inv_app

@[reassoc]
theorem right_unitality_app (n : M) (X : C) :
    F.Îµ.app ((F.obj n).obj X) â‰« (F.Î¼ n (ğŸ™_ M)).app X â‰« (F.map (Ï_ n).hom).app X = ğŸ™ _ := by
  have := congr_app (F.toLaxMonoidalFunctor.right_unitality n) X
  dsimp at this
  simpa using this.symm
#align category_theory.right_unitality_app CategoryTheory.right_unitality_app

@[simp]
theorem Îµ_app_obj (n : M) (X : C) :
    F.Îµ.app ((F.obj n).obj X) = (F.map (Ï_ n).inv).app X â‰« (F.Î¼Iso n (ğŸ™_ M)).inv.app X := by
  refine Eq.trans ?_ (Category.id_comp _)
  rw [â† Category.assoc, â† IsIso.comp_inv_eq, â† IsIso.comp_inv_eq, Category.assoc]
  convert right_unitality_app F n X using 1
  simp
#align category_theory.Îµ_app_obj CategoryTheory.Îµ_app_obj

@[simp]
theorem Îµ_inv_app_obj (n : M) (X : C) :
    F.ÎµIso.inv.app ((F.obj n).obj X) = (F.Î¼ n (ğŸ™_ M)).app X â‰« (F.map (Ï_ n).hom).app X := by
  rw [â† cancel_mono (F.Îµ.app ((F.obj n).obj X)), Îµ_inv_hom_app]
  simp
#align category_theory.Îµ_inv_app_obj CategoryTheory.Îµ_inv_app_obj

@[reassoc]
theorem associativity_app (mâ‚ mâ‚‚ mâ‚ƒ : M) (X : C) :
    (F.obj mâ‚ƒ).map ((F.Î¼ mâ‚ mâ‚‚).app X) â‰«
        (F.Î¼ (mâ‚ âŠ— mâ‚‚) mâ‚ƒ).app X â‰« (F.map (Î±_ mâ‚ mâ‚‚ mâ‚ƒ).hom).app X =
      (F.Î¼ mâ‚‚ mâ‚ƒ).app ((F.obj mâ‚).obj X) â‰« (F.Î¼ mâ‚ (mâ‚‚ âŠ— mâ‚ƒ)).app X := by
  have := congr_app (F.toLaxMonoidalFunctor.associativity mâ‚ mâ‚‚ mâ‚ƒ) X
  dsimp at this
  simpa using this
#align category_theory.associativity_app CategoryTheory.associativity_app

-- Porting note: linter claims `simp can prove it`, but cnot
@[reassoc (attr := simp, nolint simpNF)]
theorem obj_Î¼_app (mâ‚ mâ‚‚ mâ‚ƒ : M) (X : C) :
    (F.obj mâ‚ƒ).map ((F.Î¼ mâ‚ mâ‚‚).app X) =
      (F.Î¼ mâ‚‚ mâ‚ƒ).app ((F.obj mâ‚).obj X) â‰«
        (F.Î¼ mâ‚ (mâ‚‚ âŠ— mâ‚ƒ)).app X â‰«
          (F.map (Î±_ mâ‚ mâ‚‚ mâ‚ƒ).inv).app X â‰« (F.Î¼Iso (mâ‚ âŠ— mâ‚‚) mâ‚ƒ).inv.app X := by
  rw [â† associativity_app_assoc]
  simp
#align category_theory.obj_Î¼_app CategoryTheory.obj_Î¼_app

-- Porting note: linter claims `simp can prove it`, but cnot
@[reassoc (attr := simp, nolint simpNF)]
theorem obj_Î¼_inv_app (mâ‚ mâ‚‚ mâ‚ƒ : M) (X : C) :
    (F.obj mâ‚ƒ).map ((F.Î¼Iso mâ‚ mâ‚‚).inv.app X) =
      (F.Î¼ (mâ‚ âŠ— mâ‚‚) mâ‚ƒ).app X â‰«
        (F.map (Î±_ mâ‚ mâ‚‚ mâ‚ƒ).hom).app X â‰«
          (F.Î¼Iso mâ‚ (mâ‚‚ âŠ— mâ‚ƒ)).inv.app X â‰« (F.Î¼Iso mâ‚‚ mâ‚ƒ).inv.app ((F.obj mâ‚).obj X) := by
  rw [â† IsIso.inv_eq_inv]
  convert obj_Î¼_app F mâ‚ mâ‚‚ mâ‚ƒ X using 1
  Â· refine IsIso.inv_eq_of_hom_inv_id ?_
    rw [â† Functor.map_comp]
    simp
  Â· simp only [MonoidalFunctor.Î¼Iso_hom, Category.assoc, NatIso.inv_inv_app, IsIso.inv_comp]
    congr
    Â· refine IsIso.inv_eq_of_hom_inv_id ?_
      simp
    Â· refine IsIso.inv_eq_of_hom_inv_id ?_
      simp
#align category_theory.obj_Î¼_inv_app CategoryTheory.obj_Î¼_inv_app

@[reassoc (attr := simp)]
theorem obj_zero_map_Î¼_app {m : M} {X Y : C} (f : X âŸ¶ (F.obj m).obj Y) :
    (F.obj (ğŸ™_ M)).map f â‰« (F.Î¼ m (ğŸ™_ M)).app _ =
    F.ÎµIso.inv.app _ â‰« f â‰« (F.map (Ï_ m).inv).app _ := by
  rw [â† IsIso.inv_comp_eq, â† IsIso.comp_inv_eq]
  simp
#align category_theory.obj_zero_map_Î¼_app CategoryTheory.obj_zero_map_Î¼_app

@[simp]
theorem obj_Î¼_zero_app (mâ‚ mâ‚‚ : M) (X : C) :
    (F.Î¼ (ğŸ™_ M) mâ‚‚).app ((F.obj mâ‚).obj X) â‰« (F.Î¼ mâ‚ (ğŸ™_ M âŠ— mâ‚‚)).app X â‰«
    (F.map (Î±_ mâ‚ (ğŸ™_ M) mâ‚‚).inv).app X â‰« (F.Î¼Iso (mâ‚ âŠ— ğŸ™_ M) mâ‚‚).inv.app X =
    (F.Î¼ (ğŸ™_ M) mâ‚‚).app ((F.obj mâ‚).obj X) â‰«
    (F.map (Î»_ mâ‚‚).hom).app ((F.obj mâ‚).obj X) â‰« (F.obj mâ‚‚).map ((F.map (Ï_ mâ‚).inv).app X) := by
  rw [â† obj_Îµ_inv_app_assoc, â† Functor.map_comp]
  simp
#align category_theory.obj_Î¼_zero_app CategoryTheory.obj_Î¼_zero_app

/-- If `m âŠ— n â‰… ğŸ™_M`, then `F.obj m` is a left inverse of `F.obj n`. -/
@[simps!]
noncomputable def unitOfTensorIsoUnit (m n : M) (h : m âŠ— n â‰… ğŸ™_ M) : F.obj m â‹™ F.obj n â‰… ğŸ­ C :=
  F.Î¼Iso m n â‰ªâ‰« F.toFunctor.mapIso h â‰ªâ‰« F.ÎµIso.symm
#align category_theory.unit_of_tensor_iso_unit CategoryTheory.unitOfTensorIsoUnit

/-- If `m âŠ— n â‰… ğŸ™_M` and `n âŠ— m â‰… ğŸ™_M` (subject to some commuting constraints),
  then `F.obj m` and `F.obj n` forms a self-equivalence of `C`. -/
@[simps]
noncomputable def equivOfTensorIsoUnit (m n : M) (hâ‚ : m âŠ— n â‰… ğŸ™_ M) (hâ‚‚ : n âŠ— m â‰… ğŸ™_ M)
    (H : hâ‚.hom â–· m â‰« (Î»_ m).hom = (Î±_ m n m).hom â‰« m â— hâ‚‚.hom â‰« (Ï_ m).hom) : C â‰Œ C where
  functor := F.obj m
  inverse := F.obj n
  unitIso := (unitOfTensorIsoUnit F m n hâ‚).symm
  counitIso := unitOfTensorIsoUnit F n m hâ‚‚
  functor_unitIso_comp := by
    intro X
    dsimp
    simp only [Î¼_naturalityáµ£_assoc, Î¼_naturalityâ‚—_assoc, Îµ_inv_app_obj, Category.assoc,
      obj_Î¼_inv_app, Functor.map_comp, Î¼_inv_hom_app_assoc, obj_Îµ_app,
      unitOfTensorIsoUnit_inv_app]
    simp [â† NatTrans.comp_app, â† F.toFunctor.map_comp, â† H, -Functor.map_comp]
#align category_theory.equiv_of_tensor_iso_unit CategoryTheory.equivOfTensorIsoUnit

end CategoryTheory
