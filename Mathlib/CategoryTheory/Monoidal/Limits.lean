/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Monoidal.FunctorCategory
import Mathlib.CategoryTheory.Limits.HasLimits

/-!
# `lim : (J â¥¤ C) â¥¤ C` is lax monoidal when `C` is a monoidal category.

When `C` is a monoidal category, the limit functor `lim : (J â¥¤ C) â¥¤ C` is lax monoidal,
i.e. there are morphisms
* `(ğŸ™_ C) â†’ limit (ğŸ™_ (J â¥¤ C))`
* `limit F âŠ— limit G âŸ¶ limit (F âŠ— G)`
satisfying the laws of a lax monoidal functor.

## TODO
Now that we have oplax monoidal functors, assemble `Limits.colim` into an oplax monoidal functor.
-/

namespace CategoryTheory.Limits

open MonoidalCategory

universe v u w

noncomputable section

variable {J : Type w} [SmallCategory J] {C : Type u} [Category.{v} C] [HasLimitsOfShape J C]
  [MonoidalCategory.{v} C]

open Functor.LaxMonoidal

instance : (lim (J := J) (C := C)).LaxMonoidal :=
  Functor.LaxMonoidal.ofTensorHom
    (Îµ :=
      limit.lift _
        { pt := _
          Ï€ := { app := fun _ => ğŸ™ _ } })
    (Î¼ := fun F G â†¦
      limit.lift (F âŠ— G)
        { pt := limit F âŠ— limit G
          Ï€ :=
            { app := fun j => limit.Ï€ F j âŠ—â‚˜ limit.Ï€ G j
              naturality := fun j j' f => by
                dsimp
                simp only [Category.id_comp, â† tensor_comp, limit.w] } })
    (Î¼_natural := fun f g â†¦ limit.hom_ext (fun j â†¦ by
      dsimp
      simp only [limit.lift_Ï€, Cones.postcompose_obj_Ï€, Monoidal.tensorHom_app, limit.lift_map,
        NatTrans.comp_app, Category.assoc, â† tensor_comp, limMap_Ï€]))
    (associativity := fun F G H â†¦ limit.hom_ext (fun j â†¦ by
      dsimp
      simp only [tensorHom_id, limit.lift_map, Category.assoc, limit.lift_Ï€,
        id_tensorHom]
      dsimp
      conv_lhs => rw [tensorHom_def, Category.assoc, â† comp_whiskerRight_assoc,
        limit.lift_Ï€, tensor_whiskerLeft, Category.assoc, Category.assoc,
        Iso.inv_hom_id, Category.comp_id,
        â† associator_naturality_right, â† tensorHom_def_assoc]
      dsimp
      conv_rhs => rw [tensorHom_def, â† whisker_exchange,
        â† MonoidalCategory.whiskerLeft_comp_assoc, limit.lift_Ï€,
        whisker_exchange, â† associator_naturality_left_assoc]
      dsimp only
      conv_rhs => rw [tensorHom_def, MonoidalCategory.whiskerLeft_comp,
        â† associator_naturality_middle_assoc,
        â† associator_naturality_right, â† comp_whiskerRight_assoc,
        â† tensorHom_def, â† tensorHom_def_assoc]))
    (left_unitality := fun F â†¦ limit.hom_ext (fun j â†¦ by
      dsimp
      simp only [tensorHom_id, limit.lift_map, Category.assoc, limit.lift_Ï€]
      dsimp
      simp only [tensorHom_def, id_whiskerLeft, Category.assoc,
        Iso.inv_hom_id, Category.comp_id, â† comp_whiskerRight_assoc]
      erw [limit.lift_Ï€]
      rw [id_whiskerRight, Category.id_comp]))
    (right_unitality := fun F â†¦ limit.hom_ext (fun j â†¦ by
      dsimp
      simp only [id_tensorHom, limit.lift_map, Category.assoc, limit.lift_Ï€]
      dsimp
      simp only [tensorHom_def, â† whisker_exchange,
        MonoidalCategory.whiskerRight_id, Category.assoc, Iso.inv_hom_id,
        Category.comp_id, â† MonoidalCategory.whiskerLeft_comp_assoc]
      erw [limit.lift_Ï€]
      rw [MonoidalCategory.whiskerLeft_id, Category.id_comp]))

@[reassoc (attr := simp)]
lemma lim_Îµ_Ï€ (j : J) : Îµ (lim (J := J) (C := C)) â‰« limit.Ï€ _ j = ğŸ™ _ :=
  limit.lift_Ï€ _ _

@[reassoc (attr := simp)]
lemma lim_Î¼_Ï€ (F G : J â¥¤ C) (j : J) : Î¼ lim F G â‰« limit.Ï€ _ j = limit.Ï€ F j âŠ—â‚˜ limit.Ï€ G j :=
  limit.lift_Ï€ _ _

end

end CategoryTheory.Limits
