/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Monoidal.Pentagon
import Mathlib.CategoryTheory.Functor.Derived.LeftDerivedBifunctorComp
import Mathlib.CategoryTheory.Functor.Derived.LeftDerivedTrifunctorComp
import Mathlib.CategoryTheory.Functor.Derived.LeftDerivedFour
import Mathlib.CategoryTheory.Monoidal.Functor

/-!
# The derived monoidal category structure

-/

namespace CategoryTheory

open MonoidalCategory

variable {C D : Type*} [Category C] [Category D] [MonoidalCategory C]
    (L : C ‚•§ D) (W : MorphismProperty C) [L.IsLocalization W]
    [W.ContainsIdentities]

@[nolint unusedArguments]
def DerivedMonoidal (_ : C ‚•§ D) (_ : MorphismProperty C) := D

instance : Category (DerivedMonoidal L W) := inferInstanceAs (Category D)

def toDerivedMonoidal : C ‚•§ DerivedMonoidal L W := L

instance : (toDerivedMonoidal L W).IsLocalization W := by assumption

local notation "L'" => toDerivedMonoidal L W

section

variable [(curriedTensor C ‚ãô (whiskeringRight C C
  (DerivedMonoidal L W)).obj (toDerivedMonoidal L W)).HasLeftDerivedFunctor‚ÇÇ W W]

namespace DerivedMonoidal

noncomputable def bifunctor :
    DerivedMonoidal L W ‚•§ DerivedMonoidal L W ‚•§ DerivedMonoidal L W :=
  (curriedTensor C ‚ãô (whiskeringRight _ _ _).obj (L')).leftDerived‚ÇÇ (L') (L') W W

noncomputable def counit :
    (((whiskeringLeft‚ÇÇ D).obj (L')).obj (L')).obj (bifunctor L W) ‚ü∂
    curriedTensor C ‚ãô (whiskeringRight _ _ _).obj (L') :=
  (curriedTensor C ‚ãô (whiskeringRight _ _ _).obj (L')).leftDerivedCounit‚ÇÇ L L W W

@[simps!]
noncomputable def trifunctor‚ÇÅ‚ÇÇ : DerivedMonoidal L W ‚•§ DerivedMonoidal L W ‚•§
    DerivedMonoidal L W ‚•§ DerivedMonoidal L W :=
  bifunctorComp‚ÇÅ‚ÇÇ (bifunctor L W) (bifunctor L W)

@[simps!]
noncomputable def trifunctor‚ÇÇ‚ÇÉ : DerivedMonoidal L W ‚•§ DerivedMonoidal L W ‚•§
    DerivedMonoidal L W ‚•§ DerivedMonoidal L W :=
  bifunctorComp‚ÇÇ‚ÇÉ (bifunctor L W) (bifunctor L W)

noncomputable def counit‚ÇÅ‚ÇÇ :
  ((((whiskeringLeft‚ÇÉ D).obj (L')).obj (L')).obj (L')).obj (trifunctor‚ÇÅ‚ÇÇ L W) ‚ü∂
    (Functor.postcompose‚ÇÉ.obj (L')).obj (bifunctorComp‚ÇÅ‚ÇÇ (curriedTensor C) (curriedTensor C)) :=
  bifunctorComp‚ÇÅ‚ÇÇCounit (counit L W) (counit L W)

@[simp]
lemma counit‚ÇÅ‚ÇÇ_app (X‚ÇÅ X‚ÇÇ X‚ÇÉ : C) :
    (((counit‚ÇÅ‚ÇÇ L W).app X‚ÇÅ).app X‚ÇÇ).app X‚ÇÉ =
      ((bifunctor L W).map (((counit L W).app X‚ÇÅ).app X‚ÇÇ)).app ((L').obj X‚ÇÉ) ‚â´
        ((counit L W).app (X‚ÇÅ ‚äó X‚ÇÇ)).app X‚ÇÉ := by
  rfl

noncomputable def counit‚ÇÇ‚ÇÉ :
  ((((whiskeringLeft‚ÇÉ D).obj (L')).obj (L')).obj (L')).obj (trifunctor‚ÇÇ‚ÇÉ L W) ‚ü∂
    (Functor.postcompose‚ÇÉ.obj (L')).obj (bifunctorComp‚ÇÇ‚ÇÉ (curriedTensor C) (curriedTensor C)) :=
  bifunctorComp‚ÇÇ‚ÇÉCounit (counit L W) (counit L W)

@[simp]
lemma counit‚ÇÇ‚ÇÉ_app (X‚ÇÅ X‚ÇÇ X‚ÇÉ : C) :
    (((counit‚ÇÇ‚ÇÉ L W).app X‚ÇÅ).app X‚ÇÇ).app X‚ÇÉ =
      ((bifunctor L W).obj ((L').obj X‚ÇÅ)).map (((counit L W).app X‚ÇÇ).app X‚ÇÉ) ‚â´
        ((counit L W).app X‚ÇÅ).app (X‚ÇÇ ‚äó X‚ÇÉ) := by
  rfl

noncomputable def tensorUnitLeftCounit :
    L' ‚ãô (bifunctor L W).obj ((L').obj (ùüô_ C)) ‚ü∂ tensorLeft (ùüô_ C) ‚ãô L' :=
  Functor.bifunctorCounit‚ÇÅ (counit L W) (ùüô_ C)

@[simp]
lemma tensorUnitLeftCounit_app (X : C) :
    (tensorUnitLeftCounit L W).app X = ((counit L W).app (ùüô_ C)).app X := rfl

noncomputable def tensorUnitRightCounit :
    L' ‚ãô (bifunctor L W).flip.obj ((L').obj (ùüô_ C)) ‚ü∂ tensorRight (ùüô_ C) ‚ãô L' :=
  Functor.bifunctorCounit‚ÇÇ (counit L W) (ùüô_ C)

@[simp]
lemma tensorUnitRightCounit_app (X : C) :
    (tensorUnitRightCounit L W).app X = ((counit L W).app X).app (ùüô_ C) := rfl

instance : (bifunctor L W).IsLeftDerivedFunctor‚ÇÇ (counit L W) W W :=
  inferInstanceAs (Functor.IsLeftDerivedFunctor‚ÇÇ _ (Functor.leftDerivedCounit‚ÇÇ _ _ _ _ _) _ _)

noncomputable def quadrifunctorLeft : DerivedMonoidal L W ‚•§ DerivedMonoidal L W ‚•§
    DerivedMonoidal L W ‚•§ DerivedMonoidal L W ‚•§ DerivedMonoidal L W :=
  trifunctorComp‚ÇÅ‚ÇÇ‚ÇÉ (trifunctor‚ÇÅ‚ÇÇ L W) (bifunctor L W)

noncomputable def quadrifunctorRight : DerivedMonoidal L W ‚•§ DerivedMonoidal L W ‚•§
    DerivedMonoidal L W ‚•§ DerivedMonoidal L W ‚•§ DerivedMonoidal L W :=
  trifunctorComp‚ÇÇ‚ÇÉ‚ÇÑ (bifunctor L W) (trifunctor‚ÇÇ‚ÇÉ L W)

noncomputable def quadrifunctorRightCounit :
    (((((whiskeringLeft‚ÇÑ D).obj (L')).obj (L')).obj (L')).obj (L')).obj
      (quadrifunctorRight L W) ‚ü∂
        (Functor.postcompose‚ÇÑ.obj (L')).obj
          (trifunctorComp‚ÇÇ‚ÇÉ‚ÇÑ (curriedTensor C)
          (bifunctorComp‚ÇÇ‚ÇÉ (curriedTensor C) (curriedTensor C))) :=
  trifunctorComp‚ÇÇ‚ÇÉ‚ÇÑCounit (counit‚ÇÇ‚ÇÉ L W) (counit L W)

@[simp]
lemma quadrifunctorRightCounit_app_app_app_app (X‚ÇÅ X‚ÇÇ X‚ÇÉ X‚ÇÑ : C) :
    ((((quadrifunctorRightCounit L W).app X‚ÇÅ).app X‚ÇÇ).app X‚ÇÉ).app X‚ÇÑ =
  ((bifunctor L W).obj ((L').obj X‚ÇÅ)).map
      (((bifunctor L W).obj ((L').obj X‚ÇÇ)).map (((counit L W).app X‚ÇÉ).app X‚ÇÑ) ‚â´
        ((counit L W).app X‚ÇÇ).app (X‚ÇÉ ‚äó X‚ÇÑ)) ‚â´ ((counit L W).app X‚ÇÅ).app (X‚ÇÇ ‚äó X‚ÇÉ ‚äó X‚ÇÑ) :=
  rfl

end DerivedMonoidal

end

open DerivedMonoidal in
class Functor.HasDerivedMonoidalCategory : Prop where
  curriedTensor_hasLeftDerivedFunctor‚ÇÇ :
    (curriedTensor C ‚ãô (whiskeringRight _ _ _).obj (L')).HasLeftDerivedFunctor‚ÇÇ W W
  trifunctor‚ÇÅ‚ÇÇ_isLeftDerivedFunctor‚ÇÉ :
    (trifunctor‚ÇÅ‚ÇÇ L W).IsLeftDerivedFunctor‚ÇÉ (counit‚ÇÅ‚ÇÇ L W) W W W
  trifunctor‚ÇÇ‚ÇÉ_isLeftDerivedFunctor‚ÇÉ :
    (trifunctor‚ÇÇ‚ÇÉ L W).IsLeftDerivedFunctor‚ÇÉ (counit‚ÇÇ‚ÇÉ L W) W W W
  bifunctorObjUnit_isLeftDerivedFunctor :
    ((bifunctor L W).obj ((L').obj (ùüô_ C))).IsLeftDerivedFunctor (tensorUnitLeftCounit L W) W
  bifunctorFlipObjUnit_isLeftDerivedFunctor :
    ((bifunctor L W).flip.obj ((L').obj (ùüô_ C))).IsLeftDerivedFunctor (tensorUnitRightCounit L W) W
  quadrifunctorRight_isLeftDerivedFunctor‚ÇÑ :
    (quadrifunctorRight L W).IsLeftDerivedFunctor‚ÇÑ (quadrifunctorRightCounit L W) W W W W

namespace Functor.HasDerivedMonoidalCategory

attribute [instance] curriedTensor_hasLeftDerivedFunctor‚ÇÇ
  trifunctor‚ÇÅ‚ÇÇ_isLeftDerivedFunctor‚ÇÉ trifunctor‚ÇÇ‚ÇÉ_isLeftDerivedFunctor‚ÇÉ
  bifunctorObjUnit_isLeftDerivedFunctor bifunctorFlipObjUnit_isLeftDerivedFunctor
  quadrifunctorRight_isLeftDerivedFunctor‚ÇÑ

end Functor.HasDerivedMonoidalCategory

namespace DerivedMonoidal

variable [L.HasDerivedMonoidalCategory W]

noncomputable def associator :
    trifunctor‚ÇÅ‚ÇÇ L W ‚âÖ trifunctor‚ÇÇ‚ÇÉ L W :=
  Functor.leftDerived‚ÇÉNatIso _ _ (counit‚ÇÅ‚ÇÇ L W) (counit‚ÇÇ‚ÇÉ L W) W W W
    ((Functor.postcompose‚ÇÉ.obj L).mapIso (curriedAssociatorNatIso C))

@[reassoc (attr := simp)]
lemma associator_hom_fac_app_app_app (X‚ÇÅ X‚ÇÇ X‚ÇÉ : C) :
    (((associator L W).hom.app ((L').obj X‚ÇÅ)).app ((L').obj X‚ÇÇ)).app ((L').obj X‚ÇÉ) ‚â´
      ((bifunctor L W).obj ((L').obj X‚ÇÅ)).map (((counit L W).app X‚ÇÇ).app X‚ÇÉ) ‚â´
        ((counit L W).app X‚ÇÅ).app (X‚ÇÇ ‚äó X‚ÇÉ) =
      ((bifunctor L W).map (((counit L W).app X‚ÇÅ).app X‚ÇÇ)).app ((L').obj X‚ÇÉ) ‚â´
        ((counit L W).app (X‚ÇÅ ‚äó X‚ÇÇ)).app X‚ÇÉ ‚â´ (L').map (Œ±_ X‚ÇÅ X‚ÇÇ X‚ÇÉ).hom := by
  dsimp
  conv_rhs => rw [‚Üê Category.assoc]
  apply Functor.leftDerived‚ÇÉNatTrans_fac_app_app_app

noncomputable def leftUnitor : (bifunctor L W).obj ((L').obj (ùüô_ C)) ‚âÖ ùü≠ _ :=
  Functor.leftDerivedNatIso _ _ (tensorUnitLeftCounit L W) (L').rightUnitor.hom W
    (isoWhiskerRight (leftUnitorNatIso C) (L') ‚â™‚â´ (L').leftUnitor)

@[reassoc]
lemma leftUnitor_hom_app (X : C) :
    (leftUnitor L W).hom.app ((L').obj X) =
      ((counit L W).app (ùüô_ C)).app X ‚â´ (L').map (Œª_ X).hom := by
  simpa using Functor.leftDerivedNatTrans_fac_app _ _
      (tensorUnitLeftCounit L W) (L').rightUnitor.hom W
      (whiskerRight (leftUnitorNatIso C).hom (L') ‚â´ (L').leftUnitor.hom) X

noncomputable def rightUnitor : (bifunctor L W).flip.obj ((L').obj (ùüô_ C)) ‚âÖ ùü≠ _ :=
  Functor.leftDerivedNatIso _ _ (tensorUnitRightCounit L W) (L').rightUnitor.hom W
    (isoWhiskerRight (rightUnitorNatIso C) (L') ‚â™‚â´ (L').leftUnitor)

lemma rightUnitor_hom_app (X : C) :
    (rightUnitor L W).hom.app ((L').obj X) =
      ((counit L W).app X).app (ùüô_ C) ‚â´ (L').map (œÅ_ X).hom := by
  simpa using Functor.leftDerivedNatTrans_fac_app
    ((bifunctor L W).flip.obj ((L').obj (ùüô_ C))) (ùü≠ _)
    (tensorUnitRightCounit L W) (L').rightUnitor.hom W
      (whiskerRight (rightUnitorNatIso C).hom (L') ‚â´ (L').leftUnitor.hom) X

lemma triangle :
    NatTrans.Triangle (associator L W).hom ((L').obj (ùüô_ C))
      (leftUnitor L W) (rightUnitor L W) where
  triangle := by
    rw [‚Üê cancel_mono (Functor.leftUnitor (bifunctor L W)).hom]
    apply (bifunctor L W).leftDerived‚ÇÇ_ext (counit L W) W W
    ext X‚ÇÅ X‚ÇÉ
    have h‚ÇÅ := ((counit L W).app X‚ÇÅ).naturality (Œª_ X‚ÇÉ).hom
    have h‚ÇÇ := congr_app ((counit L W).naturality (œÅ_ X‚ÇÅ).hom) X‚ÇÉ
    dsimp at h‚ÇÅ h‚ÇÇ ‚ä¢
    rw [Category.comp_id, Category.comp_id, Category.assoc,
      rightUnitor_hom_app, leftUnitor_hom_app, Functor.map_comp_assoc,
      Functor.map_comp, NatTrans.comp_app_assoc, h‚ÇÅ,
      associator_hom_fac_app_app_app_assoc, ‚Üê (L').map_comp,
      MonoidalCategory.triangle, h‚ÇÇ]

lemma pentagon :
    NatTrans.Pentagon (associator L W).hom where
  natTrans‚ÇÅ‚ÇÇ_comp_natTrans‚ÇÇ‚ÇÉ_comp_natTrans‚ÇÉ‚ÇÑ := by
    apply (quadrifunctorRight L W).leftDerived‚ÇÑ_ext (quadrifunctorRightCounit L W) W W W W
    ext X‚ÇÅ X‚ÇÇ X‚ÇÉ X‚ÇÑ
    have h‚ÇÅ := ((counit L W).app X‚ÇÅ).naturality (Œ±_ X‚ÇÇ X‚ÇÉ X‚ÇÑ).hom
    have h‚ÇÇ := congr_app (((associator L W).hom.app ((L').obj X‚ÇÅ)).naturality
      (((counit L W).app X‚ÇÇ).app X‚ÇÉ)) ((L').obj X‚ÇÑ)
    have h‚ÇÉ := associator_hom_fac_app_app_app L W X‚ÇÅ (X‚ÇÇ ‚äó X‚ÇÉ) X‚ÇÑ
    have h‚ÇÑ := congr_app ((counit L W).naturality (Œ±_ X‚ÇÅ X‚ÇÇ X‚ÇÉ).hom) X‚ÇÑ
    have h‚ÇÖ := (((associator L W).hom.app ((L').obj X‚ÇÅ)).app ((L').obj X‚ÇÇ)).naturality
      (((counit L W).app X‚ÇÉ).app X‚ÇÑ)
    have h‚ÇÜ := ((bifunctor L W).map (((counit L W).app X‚ÇÅ).app X‚ÇÇ)).naturality
      (((counit L W).app X‚ÇÉ).app X‚ÇÑ)
    have h‚Çá := congr_app (congr_app ((associator L W).hom.naturality
      (((counit L W).app X‚ÇÅ).app X‚ÇÇ)) ((L').obj X‚ÇÉ)) ((L').obj X‚ÇÑ)
    dsimp at h‚ÇÅ h‚ÇÇ h‚ÇÉ h‚ÇÑ h‚ÇÖ h‚ÇÜ h‚Çá ‚ä¢
    conv_lhs =>
      rw [Category.assoc, Category.assoc,
        ‚Üê Functor.map_comp_assoc, associator_hom_fac_app_app_app,
        Functor.map_comp_assoc, Functor.map_comp_assoc, h‚ÇÅ, ‚Üê reassoc_of% h‚ÇÇ,
        reassoc_of% h‚ÇÉ, ‚Üê NatTrans.comp_app_assoc,
        ‚Üê NatTrans.comp_app_assoc, Category.assoc, ‚Üê Functor.map_comp,
        ‚Üê Functor.map_comp, associator_hom_fac_app_app_app, Functor.map_comp,
        NatTrans.comp_app_assoc, Functor.map_comp, NatTrans.comp_app_assoc,
        reassoc_of% h‚ÇÑ, ‚Üê Functor.map_comp, ‚Üê Functor.map_comp, MonoidalCategory.pentagon,
        Functor.map_comp]
    conv_rhs =>
      rw [Category.assoc, Functor.map_comp_assoc,
        ‚Üê reassoc_of% h‚ÇÖ, associator_hom_fac_app_app_app, reassoc_of% h‚ÇÜ, ‚Üê reassoc_of% h‚Çá,
        associator_hom_fac_app_app_app_assoc]

noncomputable instance : MonoidalCategory (DerivedMonoidal L W) :=
  .ofBifunctor _ _ _ _ _ (pentagon L W) (triangle L W)

lemma tensorObj_eq (X‚ÇÅ X‚ÇÇ : DerivedMonoidal L W) :
    X‚ÇÅ ‚äó X‚ÇÇ = ((bifunctor L W).obj X‚ÇÅ).obj X‚ÇÇ :=
  rfl

lemma whiskerLeft_eq (X‚ÇÅ : DerivedMonoidal L W) {X‚ÇÇ Y‚ÇÇ : DerivedMonoidal L W} (f‚ÇÇ : X‚ÇÇ ‚ü∂ Y‚ÇÇ) :
    X‚ÇÅ ‚óÅ f‚ÇÇ = ((bifunctor L W).obj X‚ÇÅ).map f‚ÇÇ := rfl

lemma whiskerRight_eq {X‚ÇÅ Y‚ÇÅ : DerivedMonoidal L W} (f‚ÇÅ : X‚ÇÅ ‚ü∂ Y‚ÇÅ) (X‚ÇÇ : DerivedMonoidal L W) :
    f‚ÇÅ ‚ñ∑ X‚ÇÇ = ((bifunctor L W).map f‚ÇÅ).app X‚ÇÇ := rfl

lemma associator_eq (X‚ÇÅ X‚ÇÇ X‚ÇÉ : DerivedMonoidal L W) :
    Œ±_ X‚ÇÅ X‚ÇÇ X‚ÇÉ = (((associator L W).app X‚ÇÅ).app X‚ÇÇ).app X‚ÇÉ :=
  rfl

lemma leftUnitor_eq (X : DerivedMonoidal L W) :
    Œª_ X = (leftUnitor L W).app X := rfl

lemma rightUnitor_eq (X : DerivedMonoidal L W) :
    œÅ_ X = (rightUnitor L W).app X := rfl

attribute [local simp] tensorObj_eq whiskerLeft_eq whiskerRight_eq associator_eq
  leftUnitor_eq rightUnitor_eq leftUnitor_hom_app rightUnitor_hom_app

noncomputable instance : (toDerivedMonoidal L W).LaxMonoidal where
  Œµ' := ùüô _
  Œº' X‚ÇÅ X‚ÇÇ := ((counit L W).app X‚ÇÅ).app X‚ÇÇ
  Œº'_natural_right X‚ÇÅ f‚ÇÇ := ((counit L W).app X‚ÇÅ).naturality f‚ÇÇ
  Œº'_natural_left f‚ÇÅ X‚ÇÇ := congr_app ((counit L W).naturality f‚ÇÅ) X‚ÇÇ

instance : IsIso (Functor.LaxMonoidal.Œµ (toDerivedMonoidal L W)) :=
  inferInstanceAs (IsIso (ùüô _))

instance : (curriedTensor (DerivedMonoidal L W)).IsLeftDerivedFunctor‚ÇÇ
    (Functor.LaxMonoidal.ŒºNatTrans (toDerivedMonoidal L W)) W W :=
  inferInstanceAs ((bifunctor L W).IsLeftDerivedFunctor‚ÇÇ (counit L W) W W)

end DerivedMonoidal

end CategoryTheory
