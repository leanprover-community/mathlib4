/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
module

public import Mathlib.CategoryTheory.Adjunction.Limits
public import Mathlib.CategoryTheory.Comma.Over.Pullback
public import Mathlib.CategoryTheory.Limits.Constructions.Over.Products
public import Mathlib.CategoryTheory.Monoidal.CommMon_
public import Mathlib.CategoryTheory.Monoidal.Grp_

/-!

# `CartesianMonoidalCategory` for `Over X`

We provide a `CartesianMonoidalCategory (Over X)` instance via pullbacks, and provide simp lemmas
for the induced `MonoidalCategory (Over X)` instance.

-/

public noncomputable section

namespace CategoryTheory.Over

open Functor Limits CartesianMonoidalCategory

variable {C : Type*} [Category* C] [HasPullbacks C]

/-- A choice of finite products of `Over X` given by `Limits.pullback`. -/
abbrev cartesianMonoidalCategory (X : C) : CartesianMonoidalCategory (Over X) :=
  .ofChosenFiniteProducts
    âŸ¨asEmptyCone (Over.mk (ğŸ™ X)), IsTerminal.ofUniqueHom (fun Y â†¦ Over.homMk Y.hom)
      fun Y m â†¦ Over.OverMorphism.ext (by simpa using m.w)âŸ©
    fun Y Z â†¦ âŸ¨pullbackConeEquivBinaryFan.functor.obj (pullback.cone Y.hom Z.hom),
    (pullback.isLimit _ _).pullbackConeEquivBinaryFanFunctorâŸ©

@[deprecated (since := "2025-05-15")]
noncomputable alias chosenFiniteProducts := cartesianMonoidalCategory

attribute [local instance] cartesianMonoidalCategory

/-- `Over X` is braided w.r.t. the Cartesian monoidal structure given by `Limits.pullback`. -/
abbrev braidedCategory (X : C) : BraidedCategory (Over X) :=
  .ofCartesianMonoidalCategory

attribute [local instance] braidedCategory

open MonoidalCategory

variable {X : C}

@[ext]
lemma tensorObj_ext {R : C} {S T : Over X} (fâ‚ fâ‚‚ : R âŸ¶ (S âŠ— T).left)
    (eâ‚ : fâ‚ â‰« pullback.fst _ _ = fâ‚‚ â‰« pullback.fst _ _)
    (eâ‚‚ : fâ‚ â‰« pullback.snd _ _ = fâ‚‚ â‰« pullback.snd _ _) : fâ‚ = fâ‚‚ :=
  pullback.hom_ext eâ‚ eâ‚‚

@[simp]
lemma tensorObj_left (R S : Over X) : (R âŠ— S).left = Limits.pullback R.hom S.hom := rfl

@[simp]
lemma tensorObj_hom (R S : Over X) : (R âŠ— S).hom = pullback.fst R.hom S.hom â‰« R.hom := rfl

@[simp]
lemma tensorUnit_left : (ğŸ™_ (Over X)).left = X := rfl

@[simp]
lemma tensorUnit_hom : (ğŸ™_ (Over X)).hom = ğŸ™ X := rfl

@[simp]
lemma lift_left {R S T : Over X} (f : R âŸ¶ S) (g : R âŸ¶ T) :
    (lift f g).left = pullback.lift f.left g.left (f.w.trans g.w.symm) := rfl

@[simp]
lemma toUnit_left {R : Over X} : (toUnit R).left = R.hom := rfl

@[reassoc (attr := simp)]
lemma associator_hom_left_fst (R S T : Over X) :
    (Î±_ R S T).hom.left â‰« pullback.fst _ (pullback.fst _ _ â‰« _) =
      pullback.fst _ _ â‰« pullback.fst _ _ :=
  limit.lift_Ï€ _ _

@[reassoc (attr := simp)]
lemma associator_hom_left_snd_fst (R S T : Over X) :
    (Î±_ R S T).hom.left â‰« pullback.snd _ (pullback.fst _ _ â‰« _) â‰« pullback.fst _ _ =
      pullback.fst _ _ â‰« pullback.snd _ _ :=
  (limit.lift_Ï€_assoc _ _ _).trans (limit.lift_Ï€ _ _)

@[reassoc (attr := simp)]
lemma associator_hom_left_snd_snd (R S T : Over X) :
    (Î±_ R S T).hom.left â‰« pullback.snd _ (pullback.fst _ _ â‰« _) â‰« pullback.snd _ _ =
      pullback.snd _ _ :=
  (limit.lift_Ï€_assoc _ _ _).trans (limit.lift_Ï€ _ _)

@[reassoc (attr := simp)]
lemma associator_inv_left_fst_fst (R S T : Over X) :
    (Î±_ R S T).inv.left â‰« pullback.fst (pullback.fst _ _ â‰« _) _ â‰« pullback.fst _ _ =
      pullback.fst _ _ :=
  (limit.lift_Ï€_assoc _ _ _).trans (limit.lift_Ï€ _ _)

@[reassoc (attr := simp)]
lemma associator_inv_left_fst_snd (R S T : Over X) :
    (Î±_ R S T).inv.left â‰« pullback.fst (pullback.fst _ _ â‰« _) _ â‰« pullback.snd _ _ =
      pullback.snd _ _ â‰« pullback.fst _ _ :=
  (limit.lift_Ï€_assoc _ _ _).trans (limit.lift_Ï€ _ _)

@[reassoc (attr := simp)]
lemma associator_inv_left_snd (R S T : Over X) :
    (Î±_ R S T).inv.left â‰« pullback.snd (pullback.fst _ _ â‰« _) _ =
      pullback.snd _ _ â‰« pullback.snd _ _ :=
  limit.lift_Ï€ _ _

@[simp]
lemma leftUnitor_hom_left (Y : Over X) :
    (Î»_ Y).hom.left = pullback.snd _ _ := rfl

@[reassoc (attr := simp)]
lemma leftUnitor_inv_left_fst (Y : Over X) :
    (Î»_ Y).inv.left â‰« pullback.fst (ğŸ™ X) _ = Y.hom :=
  limit.lift_Ï€ _ _

@[reassoc (attr := simp)]
lemma leftUnitor_inv_left_snd (Y : Over X) :
    (Î»_ Y).inv.left â‰« pullback.snd (ğŸ™ X) _ = ğŸ™ Y.left :=
  limit.lift_Ï€ _ _

@[simp]
lemma rightUnitor_hom_left (Y : Over X) :
    (Ï_ Y).hom.left = pullback.fst _ (ğŸ™ X) := rfl

@[reassoc (attr := simp)]
lemma rightUnitor_inv_left_fst (Y : Over X) :
    (Ï_ Y).inv.left â‰« pullback.fst _ (ğŸ™ X) = ğŸ™ _ :=
  limit.lift_Ï€ _ _

@[reassoc (attr := simp)]
lemma rightUnitor_inv_left_snd (Y : Over X) :
    (Ï_ Y).inv.left â‰« pullback.snd _ (ğŸ™ X) = Y.hom :=
  limit.lift_Ï€ _ _

lemma whiskerLeft_left {R S T : Over X} (f : S âŸ¶ T) :
    (R â— f).left = pullback.map _ _ _ _ (ğŸ™ _) f.left (ğŸ™ _) (by simp) (by simp) := rfl

@[reassoc (attr := simp)]
lemma whiskerLeft_left_fst {R S T : Over X} (f : S âŸ¶ T) :
    (R â— f).left â‰« pullback.fst _ _ = pullback.fst _ _ :=
  (limit.lift_Ï€ _ _).trans (Category.comp_id _)

@[reassoc (attr := simp)]
lemma whiskerLeft_left_snd {R S T : Over X} (f : S âŸ¶ T) :
    (R â— f).left â‰« pullback.snd _ _ = pullback.snd _ _ â‰« f.left :=
  limit.lift_Ï€ _ _

lemma whiskerRight_left {R S T : Over X} (f : S âŸ¶ T) :
    (f â–· R).left = pullback.map _ _ _ _ f.left (ğŸ™ _) (ğŸ™ _) (by simp) (by simp) := rfl

@[reassoc (attr := simp)]
lemma whiskerRight_left_fst {R S T : Over X} (f : S âŸ¶ T) :
    (f â–· R).left â‰« pullback.fst _ _ = pullback.fst _ _ â‰« f.left :=
  limit.lift_Ï€ _ _

@[reassoc (attr := simp)]
lemma whiskerRight_left_snd {R S T : Over X} (f : S âŸ¶ T) :
    (f â–· R).left â‰« pullback.snd _ _ = pullback.snd _ _ :=
  (limit.lift_Ï€ _ _).trans (Category.comp_id _)

lemma tensorHom_left {R S T U : Over X} (f : R âŸ¶ S) (g : T âŸ¶ U) :
    (f âŠ—â‚˜ g).left = pullback.map _ _ _ _ f.left g.left (ğŸ™ _) (by simp) (by simp) := rfl

@[reassoc (attr := simp)]
lemma tensorHom_left_fst {S U : C} {R T : Over X} (fS : S âŸ¶ X) (fU : U âŸ¶ X)
    (f : R âŸ¶ mk fS) (g : T âŸ¶ mk fU) :
    (f âŠ—â‚˜ g).left â‰« pullback.fst fS fU = pullback.fst R.hom T.hom â‰« f.left :=
  limit.lift_Ï€ _ _

@[reassoc (attr := simp)]
lemma tensorHom_left_snd {S U : C} {R T : Over X} (fS : S âŸ¶ X) (fU : U âŸ¶ X)
    (f : R âŸ¶ mk fS) (g : T âŸ¶ mk fU) :
    (f âŠ—â‚˜ g).left â‰« pullback.snd fS fU = pullback.snd R.hom T.hom â‰« g.left :=
  limit.lift_Ï€ _ _

@[simp]
lemma braiding_hom_left {R S : Over X} :
    (Î²_ R S).hom.left = (pullbackSymmetry _ _).hom := rfl

@[simp]
lemma braiding_inv_left {R S : Over X} :
    (Î²_ R S).inv.left = (pullbackSymmetry _ _).hom := rfl

variable {A B R S Y Z : C} {f : R âŸ¶ X} {g : S âŸ¶ X}

instance : (Over.pullback f).Braided := .ofChosenFiniteProducts _

@[simp]
lemma Î·_pullback_left : (OplaxMonoidal.Î· (Over.pullback f)).left = (pullback.snd (ğŸ™ _) f) := rfl

@[simp]
lemma Îµ_pullback_left : (LaxMonoidal.Îµ (Over.pullback f)).left = inv (pullback.snd (ğŸ™ _) f) := by
  apply IsIso.eq_inv_of_hom_inv_id
  rw [â† Î·_pullback_left, â† Over.comp_left, Monoidal.Î·_Îµ, Over.id_left]

lemma Î¼_pullback_left_fst_fst (R S : Over X) :
    (LaxMonoidal.Î¼ (Over.pullback f) R S).left â‰«
      pullback.fst _ _ â‰« pullback.fst _ _ = pullback.fst _ _ â‰« pullback.fst _ _ := by
  rw [Monoidal.Î¼_of_cartesianMonoidalCategory,
    â† cancel_epi (prodComparisonIso (Over.pullback f) R S).hom.left, â† Over.comp_left_assoc,
    Iso.hom_inv_id]
  simp [CartesianMonoidalCategory.prodComparison, fst]

lemma Î¼_pullback_left_fst_snd (R S : Over X) :
    (LaxMonoidal.Î¼ (Over.pullback f) R S).left â‰«
      pullback.fst _ _ â‰« pullback.snd _ _ = pullback.snd _ _ â‰« pullback.fst _ _ := by
  rw [Monoidal.Î¼_of_cartesianMonoidalCategory,
    â† cancel_epi (prodComparisonIso (Over.pullback f) R S).hom.left,
    â† Over.comp_left_assoc, Iso.hom_inv_id]
  simp [CartesianMonoidalCategory.prodComparison, snd]

lemma Î¼_pullback_left_snd (R S : Over X) :
    (LaxMonoidal.Î¼ (Over.pullback f) R S).left â‰« pullback.snd _ _ =
      pullback.snd _ _ â‰« pullback.snd _ _ := by
  rw [Monoidal.Î¼_of_cartesianMonoidalCategory,
    â† cancel_epi (prodComparisonIso (Over.pullback f) R S).hom.left,
    â† Over.comp_left_assoc, Iso.hom_inv_id]
  simp [CartesianMonoidalCategory.prodComparison]

@[simp]
lemma Î¼_pullback_left_fst_fst' (gâ‚ : Y âŸ¶ X) (gâ‚‚ : Z âŸ¶ X) :
    (LaxMonoidal.Î¼ (Over.pullback f) (.mk gâ‚) (.mk gâ‚‚)).left â‰«
      pullback.fst (pullback.fst gâ‚ gâ‚‚ â‰« gâ‚) f â‰« pullback.fst gâ‚ gâ‚‚ =
        pullback.fst _ _ â‰« pullback.fst _ _ :=
  Î¼_pullback_left_fst_fst ..

@[simp]
lemma Î¼_pullback_left_fst_snd' (gâ‚ : Y âŸ¶ X) (gâ‚‚ : Z âŸ¶ X) :
    (LaxMonoidal.Î¼ (Over.pullback f) (.mk gâ‚) (.mk gâ‚‚)).left â‰«
      pullback.fst (pullback.fst gâ‚ gâ‚‚ â‰« gâ‚) f â‰« pullback.snd gâ‚ gâ‚‚ =
        pullback.snd _ _ â‰« pullback.fst _ _ :=
  Î¼_pullback_left_fst_snd ..

@[simp]
lemma Î¼_pullback_left_snd' (gâ‚ : Y âŸ¶ X) (gâ‚‚ : Z âŸ¶ X) :
    (LaxMonoidal.Î¼ (Over.pullback f) (.mk gâ‚) (.mk gâ‚‚)).left â‰«
      pullback.snd (pullback.fst gâ‚ gâ‚‚ â‰« gâ‚) f =
        pullback.snd _ _ â‰« pullback.snd _ _ := Î¼_pullback_left_snd ..

@[simp]
lemma preservesTerminalIso_pullback (f : R âŸ¶ S) :
    preservesTerminalIso (Over.pullback f) =
      Over.isoMk (asIso (pullback.snd (ğŸ™ _) f)) (by simp) := by
  ext1; exact toUnit_unique _ _

@[simp]
lemma prodComparisonIso_pullback_inv_left_fst_fst (f : X âŸ¶ Y) (A B : Over Y) :
    (prodComparisonIso (Over.pullback f) A B).inv.left â‰«
      pullback.fst (pullback.fst A.hom B.hom â‰« A.hom) f â‰« pullback.fst _ _ =
        pullback.fst (pullback.snd A.hom f) (pullback.snd B.hom f) â‰« pullback.fst _ _ := by
  rw [â† cancel_epi (prodComparisonIso (Over.pullback f) A B).hom.left,
    Over.hom_left_inv_left_assoc]
  simp [CartesianMonoidalCategory.prodComparison, fst]

@[simp]
lemma prodComparisonIso_pullback_Spec_inv_left_fst_fst' (f : X âŸ¶ Y) (gA : A âŸ¶ Y) (gB : B âŸ¶ Y) :
    (prodComparisonIso (Over.pullback f) (.mk gA) (.mk gB)).inv.left â‰«
      pullback.fst (pullback.fst gA gB â‰« gA) f â‰« pullback.fst _ _ =
        pullback.fst (pullback.snd gA f) (pullback.snd gB f) â‰« pullback.fst _ _ :=
  prodComparisonIso_pullback_inv_left_fst_fst ..

@[simp]
lemma prodComparisonIso_pullback_inv_left_fst_snd' (f : X âŸ¶ Y) (gA : A âŸ¶ Y) (gB : B âŸ¶ Y) :
    (prodComparisonIso (Over.pullback f) (.mk gA) (.mk gB)).inv.left â‰«
      pullback.fst (pullback.fst gA gB â‰« gA) f â‰« pullback.snd _ _ =
        pullback.snd _ _ â‰« pullback.fst _ _ := by
  rw [â† cancel_epi (prodComparisonIso (Over.pullback f) _ _).hom.left,
    Over.hom_left_inv_left_assoc]
  simp [CartesianMonoidalCategory.prodComparison, snd]

@[simp]
lemma prodComparisonIso_pullback_inv_left_snd' (f : X âŸ¶ Y) (gA : A âŸ¶ Y) (gB : B âŸ¶ Y) :
    (prodComparisonIso (Over.pullback f) (.mk gA) (.mk gB)).inv.left â‰«
      pullback.snd (pullback.fst gA gB â‰« gA) f = pullback.snd _ _ â‰« pullback.snd _ _ := by
  rw [â† cancel_epi (prodComparisonIso (Over.pullback f) _ _).hom.left,
    Over.hom_left_inv_left_assoc]
  simp [CartesianMonoidalCategory.prodComparison]

/-- The pullback of a monoid object is a monoid object. -/
@[simps! -isSimp mul one]
abbrev monObjMkPullbackSnd [MonObj (Over.mk f)] : MonObj (Over.mk <| pullback.snd f g) :=
  ((Over.pullback g).mapMon.obj <| .mk <| .mk f).mon

attribute [local instance] monObjMkPullbackSnd

instance isCommMonObj_mk_pullbackSnd [MonObj (Over.mk f)] [IsCommMonObj (Over.mk f)] :
    IsCommMonObj (Over.mk <| pullback.snd f g) :=
  ((Over.pullback g).mapCommMon.obj <| .mk <| .mk f).comm

/-- The pullback of a monoid object is a monoid object. -/
@[simps! -isSimp mul one]
abbrev grpObjMkPullbackSnd [GrpObj (Over.mk f)] : GrpObj (Over.mk (pullback.snd f g)) :=
  ((Over.pullback g).mapGrp.obj <| .mk <| .mk f).grp

attribute [local simp] monObjMkPullbackSnd_one in
instance isMonHom_pullbackFst_id_right [MonObj (Over.mk f)] :
    IsMonHom <| Over.homMk (U := Over.mk <| pullback.snd f (ğŸ™ X)) (V := Over.mk f)
      (pullback.fst f (ğŸ™ X)) (pullback.condition.trans <| by simp) where
  mul_hom := by
    ext
    dsimp [monObjMkPullbackSnd_mul]
    simp only [Category.assoc, limit.lift_Ï€, PullbackCone.mk_pt, PullbackCone.mk_Ï€_app]
    simp only [â† Category.assoc]
    congr 1
    ext <;> simp

end CategoryTheory.Over
