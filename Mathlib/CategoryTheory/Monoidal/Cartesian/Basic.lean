/-
Copyright (c) 2019 Kim Morrison, Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Simon Hudon, Adam Topaz, Robin Carlier
-/
import Mathlib.CategoryTheory.Limits.Constructions.FiniteProductsOfBinaryProducts
import Mathlib.CategoryTheory.Limits.FullSubcategory
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Terminal
import Mathlib.CategoryTheory.Monoidal.Braided.Basic

/-!
# Categories with chosen finite products

We introduce a class, `CartesianMonoidalCategory`, which bundles explicit choices
for a terminal object and binary products in a category `C`.
This is primarily useful for categories which have finite products with good
definitional properties, such as the category of types.

For better defeqs, we also extend `MonoidalCategory`.

## Implementation notes

For Cartesian monoidal categories, the oplax-monoidal/monoidal/braided structure of a functor `F`
preserving finite products is uniquely determined. See the `ofChosenFiniteProducts` declarations.

We however develop the theory for any `F.OplaxMonoidal`/`F.Monoidal`/`F.Braided` instance instead of
requiring it to be the `ofChosenFiniteProducts` one. This is to avoid diamonds: Consider
e.g. `ğŸ­ C` and `F â‹™ G`.

In applications requiring a finite-product-preserving functor to be
oplax-monoidal/monoidal/braided, avoid `attribute [local instance] ofChosenFiniteProducts` but
instead turn on the corresponding `ofChosenFiniteProducts` declaration for that functor only.

# Projects

- Construct an instance of chosen finite products in the category of affine scheme, using
  the tensor product.
- Construct chosen finite products in other categories appearing "in nature".

-/

namespace CategoryTheory

universe v vâ‚ vâ‚‚ vâ‚ƒ u uâ‚ uâ‚‚ uâ‚ƒ

open MonoidalCategory Limits

variable (C) in
/--
An instance of `CartesianMonoidalCategory C` bundles an explicit choice of a binary
product of two objects of `C`, and a terminal object in `C`.

Users should use the monoidal notation: `X âŠ— Y` for the product and `ğŸ™_ C` for
the terminal object.
-/
class CartesianMonoidalCategory (C : Type u) [Category.{v} C] extends MonoidalCategory C where
  /-- The tensor unit is a terminal object. -/
  isTerminalTensorUnit : IsTerminal (ğŸ™_ C)
  /-- The first projection from the product. -/
  fst (X Y : C) : X âŠ— Y âŸ¶ X
  /-- The second projection from the product. -/
  snd (X Y : C) : X âŠ— Y âŸ¶ Y
  /-- The monoidal product is the categorical product. -/
  tensorProductIsBinaryProduct (X Y : C) : IsLimit <| BinaryFan.mk (fst X Y) (snd X Y)
  fst_def (X Y : C) : fst X Y = X â— isTerminalTensorUnit.from Y â‰« (Ï_ X).hom := by cat_disch
  snd_def (X Y : C) : snd X Y = isTerminalTensorUnit.from X â–· Y â‰« (Î»_ Y).hom := by cat_disch

@[deprecated (since := "2025-05-15")] alias ChosenFiniteProducts := CartesianMonoidalCategory

namespace CartesianMonoidalCategory

variable {C : Type u} [Category.{v} C]

section OfChosenFiniteProducts
variable (ğ’¯ : LimitCone (Functor.empty.{0} C)) (â„¬ : âˆ€ X Y : C, LimitCone (pair X Y))
  {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ Zâ‚ Zâ‚‚ : C}

namespace ofChosenFiniteProducts

/-- Implementation of the tensor product for `CartesianMonoidalCategory.ofChosenFiniteProducts`. -/
abbrev tensorObj (X Y : C) : C := (â„¬ X Y).cone.pt

/-- Implementation of the tensor product of morphisms for
`CartesianMonoidalCategory.ofChosenFiniteProducts`. -/
abbrev tensorHom (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚) : tensorObj â„¬ Xâ‚ Xâ‚‚ âŸ¶ tensorObj â„¬ Yâ‚ Yâ‚‚ :=
  (BinaryFan.IsLimit.lift' (â„¬ Yâ‚ Yâ‚‚).isLimit ((â„¬ Xâ‚ Xâ‚‚).cone.Ï€.app âŸ¨.leftâŸ© â‰« f)
      (((â„¬ Xâ‚ Xâ‚‚).cone.Ï€.app âŸ¨.rightâŸ© : (â„¬ Xâ‚ Xâ‚‚).cone.pt âŸ¶ Xâ‚‚) â‰« g)).val

lemma id_tensorHom_id (X Y : C) : tensorHom â„¬ (ğŸ™ X) (ğŸ™ Y) = ğŸ™ (tensorObj â„¬ X Y) :=
  (â„¬ _ _).isLimit.hom_ext <| by rintro âŸ¨_ | _âŸ© <;> simp [tensorHom]

@[deprecated (since := "2025-07-14")] alias tensor_id := id_tensorHom_id

lemma tensorHom_comp_tensorHom (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (gâ‚ : Yâ‚ âŸ¶ Zâ‚) (gâ‚‚ : Yâ‚‚ âŸ¶ Zâ‚‚) :
    tensorHom â„¬ fâ‚ fâ‚‚ â‰« tensorHom â„¬ gâ‚ gâ‚‚ = tensorHom â„¬ (fâ‚ â‰« gâ‚) (fâ‚‚ â‰« gâ‚‚) :=
  (â„¬ _ _).isLimit.hom_ext <| by rintro âŸ¨_ | _âŸ© <;> simp [tensorHom]

lemma pentagon (W X Y Z : C) :
    tensorHom â„¬ (BinaryFan.associatorOfLimitCone â„¬ W X Y).hom (ğŸ™ Z) â‰«
        (BinaryFan.associatorOfLimitCone â„¬ W (tensorObj â„¬ X Y) Z).hom â‰«
          tensorHom â„¬ (ğŸ™ W) (BinaryFan.associatorOfLimitCone â„¬ X Y Z).hom =
      (BinaryFan.associatorOfLimitCone â„¬ (tensorObj â„¬ W X) Y Z).hom â‰«
        (BinaryFan.associatorOfLimitCone â„¬ W X (tensorObj â„¬ Y Z)).hom := by
  dsimp [tensorHom]
  apply (â„¬ _ _).isLimit.hom_ext
  rintro âŸ¨_ | _âŸ©
  Â· simp
  apply (â„¬ _ _).isLimit.hom_ext
  rintro âŸ¨_ | _âŸ©
  Â· simp
  apply (â„¬ _ _).isLimit.hom_ext
  rintro âŸ¨_ | _âŸ© <;> simp

lemma triangle (X Y : C) :
    (BinaryFan.associatorOfLimitCone â„¬ X ğ’¯.cone.pt Y).hom â‰«
        tensorHom â„¬ (ğŸ™ X) (BinaryFan.leftUnitor ğ’¯.isLimit (â„¬ ğ’¯.cone.pt Y).isLimit).hom =
      tensorHom â„¬ (BinaryFan.rightUnitor ğ’¯.isLimit (â„¬ X ğ’¯.cone.pt).isLimit).hom (ğŸ™ Y) :=
  (â„¬ _ _).isLimit.hom_ext <| by rintro âŸ¨_ | _âŸ© <;> simp

lemma leftUnitor_naturality (f : Xâ‚ âŸ¶ Xâ‚‚) :
    tensorHom â„¬ (ğŸ™ ğ’¯.cone.pt) f â‰« (BinaryFan.leftUnitor ğ’¯.isLimit (â„¬ ğ’¯.cone.pt Xâ‚‚).isLimit).hom =
      (BinaryFan.leftUnitor ğ’¯.isLimit (â„¬ ğ’¯.cone.pt Xâ‚).isLimit).hom â‰« f := by
  simp [tensorHom]

lemma rightUnitor_naturality (f : Xâ‚ âŸ¶ Xâ‚‚) :
    tensorHom â„¬ f (ğŸ™ ğ’¯.cone.pt) â‰« (BinaryFan.rightUnitor ğ’¯.isLimit (â„¬ Xâ‚‚ ğ’¯.cone.pt).isLimit).hom =
      (BinaryFan.rightUnitor ğ’¯.isLimit (â„¬ Xâ‚ ğ’¯.cone.pt).isLimit).hom â‰« f := by
  simp [tensorHom]

lemma associator_naturality (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ) :
    tensorHom â„¬ (tensorHom â„¬ fâ‚ fâ‚‚) fâ‚ƒ â‰« (BinaryFan.associatorOfLimitCone â„¬ Yâ‚ Yâ‚‚ Yâ‚ƒ).hom =
      (BinaryFan.associatorOfLimitCone â„¬ Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« tensorHom â„¬ fâ‚ (tensorHom â„¬ fâ‚‚ fâ‚ƒ) := by
  dsimp [tensorHom]
  apply (â„¬ _ _).isLimit.hom_ext
  rintro âŸ¨_ | _âŸ©
  Â· simp
  apply (â„¬ _ _).isLimit.hom_ext
  rintro âŸ¨_ | _âŸ© <;> simp

end ofChosenFiniteProducts

open ofChosenFiniteProducts

/-- Construct an instance of `CartesianMonoidalCategory C` given a terminal object and limit cones
over arbitrary pairs of objects. -/
abbrev ofChosenFiniteProducts : CartesianMonoidalCategory C :=
  letI : MonoidalCategoryStruct C := {
    tensorUnit := ğ’¯.cone.pt
    tensorObj := tensorObj â„¬
    tensorHom := tensorHom â„¬
    whiskerLeft X {_ _} g := tensorHom â„¬ (ğŸ™ X) g
    whiskerRight {_ _} f Y := tensorHom â„¬ f (ğŸ™ Y)
    associator := BinaryFan.associatorOfLimitCone â„¬
    leftUnitor X := BinaryFan.leftUnitor ğ’¯.isLimit (â„¬ ğ’¯.cone.pt X).isLimit
    rightUnitor X := BinaryFan.rightUnitor ğ’¯.isLimit (â„¬ X ğ’¯.cone.pt).isLimit
  }
  {
  toMonoidalCategory := .ofTensorHom
    (id_tensorHom_id := id_tensorHom_id â„¬)
    (tensorHom_comp_tensorHom := tensorHom_comp_tensorHom â„¬)
    (pentagon := pentagon â„¬)
    (triangle := triangle ğ’¯ â„¬)
    (leftUnitor_naturality := leftUnitor_naturality ğ’¯ â„¬)
    (rightUnitor_naturality := rightUnitor_naturality ğ’¯ â„¬)
    (associator_naturality := associator_naturality â„¬)
  isTerminalTensorUnit :=
    .ofUniqueHom (ğ’¯.isLimit.lift <| asEmptyCone Â·) fun _ _ â†¦ ğ’¯.isLimit.hom_ext (by simp)
  fst X Y := BinaryFan.fst (â„¬ X Y).cone
  snd X Y := BinaryFan.snd (â„¬ X Y).cone
  tensorProductIsBinaryProduct X Y := BinaryFan.IsLimit.mk _
    (fun f g â†¦ (BinaryFan.IsLimit.lift' (â„¬ X Y).isLimit f g).1)
    (fun f g â†¦ (BinaryFan.IsLimit.lift' (â„¬ X Y).isLimit f g).2.1)
    (fun f g â†¦ (BinaryFan.IsLimit.lift' (â„¬ X Y).isLimit f g).2.2)
    (fun f g m hf hg â†¦
      BinaryFan.IsLimit.hom_ext (â„¬ X Y).isLimit (by simpa using hf) (by simpa using hg))
  fst_def X Y := (((â„¬ X ğ’¯.cone.pt).isLimit.fac
    (BinaryFan.mk _ _) âŸ¨.leftâŸ©).trans (Category.comp_id _)).symm
  snd_def X Y := (((â„¬ ğ’¯.cone.pt Y).isLimit.fac
    (BinaryFan.mk _ _) âŸ¨.rightâŸ©).trans (Category.comp_id _)).symm
  }

omit ğ’¯ in
/-- Construct an instance of `CartesianMonoidalCategory C` given the existence of finite products
in `C`. -/
noncomputable abbrev ofHasFiniteProducts [HasFiniteProducts C] : CartesianMonoidalCategory C :=
  .ofChosenFiniteProducts (getLimitCone (.empty C)) (getLimitCone <| pair Â· Â·)

@[deprecated (since := "2025-05-08")] alias ofFiniteProducts := ofHasFiniteProducts

end OfChosenFiniteProducts

variable {C : Type u} [Category.{v} C] [CartesianMonoidalCategory C]

open MonoidalCategory

/--
The unique map to the terminal object.
-/
def toUnit (X : C) : X âŸ¶ ğŸ™_ C := isTerminalTensorUnit.from _

instance (X : C) : Unique (X âŸ¶ ğŸ™_ C) := isTerminalEquivUnique _ _ isTerminalTensorUnit _

lemma default_eq_toUnit (X : C) : default = toUnit X := rfl

/--
This lemma follows from the preexisting `Unique` instance, but
it is often convenient to use it directly as `apply toUnit_unique` forcing
lean to do the necessary elaboration.
-/
@[ext]
lemma toUnit_unique {X : C} (f g : X âŸ¶ ğŸ™_ _) : f = g :=
  Subsingleton.elim _ _

@[simp] lemma toUnit_unit : toUnit (ğŸ™_ C) = ğŸ™ (ğŸ™_ C) := toUnit_unique ..

@[reassoc (attr := simp)]
theorem comp_toUnit {X Y : C} (f : X âŸ¶ Y) : f â‰« toUnit Y = toUnit X :=
  toUnit_unique _ _

/--
Construct a morphism to the product given its two components.
-/
def lift {T X Y : C} (f : T âŸ¶ X) (g : T âŸ¶ Y) : T âŸ¶ X âŠ— Y :=
  (BinaryFan.IsLimit.lift' (tensorProductIsBinaryProduct X Y) f g).1

@[reassoc (attr := simp)]
lemma lift_fst {T X Y : C} (f : T âŸ¶ X) (g : T âŸ¶ Y) : lift f g â‰« fst _ _ = f :=
  (BinaryFan.IsLimit.lift' (tensorProductIsBinaryProduct X Y) f g).2.1

@[reassoc (attr := simp)]
lemma lift_snd {T X Y : C} (f : T âŸ¶ X) (g : T âŸ¶ Y) : lift f g â‰« snd _ _ = g :=
  (BinaryFan.IsLimit.lift' (tensorProductIsBinaryProduct X Y) f g).2.2

instance mono_lift_of_mono_left {W X Y : C} (f : W âŸ¶ X) (g : W âŸ¶ Y)
    [Mono f] : Mono (lift f g) :=
  mono_of_mono_fac <| lift_fst _ _

instance mono_lift_of_mono_right {W X Y : C} (f : W âŸ¶ X) (g : W âŸ¶ Y)
    [Mono g] : Mono (lift f g) :=
  mono_of_mono_fac <| lift_snd _ _

@[ext 1050]
lemma hom_ext {T X Y : C} (f g : T âŸ¶ X âŠ— Y)
    (h_fst : f â‰« fst _ _ = g â‰« fst _ _)
    (h_snd : f â‰« snd _ _ = g â‰« snd _ _) :
    f = g :=
  BinaryFan.IsLimit.hom_ext (tensorProductIsBinaryProduct X Y) h_fst h_snd

-- Similarly to `CategoryTheory.Limits.prod.comp_lift`, we do not make the `assoc` version a simp
-- lemma
@[reassoc, simp]
lemma comp_lift {V W X Y : C} (f : V âŸ¶ W) (g : W âŸ¶ X) (h : W âŸ¶ Y) :
    f â‰« lift g h = lift (f â‰« g) (f â‰« h) := by ext <;> simp

@[simp]
lemma lift_fst_snd {X Y : C} : lift (fst X Y) (snd X Y) = ğŸ™ (X âŠ— Y) := by ext <;> simp

@[simp]
lemma lift_comp_fst_snd {X Y Z : C} (f : X âŸ¶ Y âŠ— Z) :
    lift (f â‰« fst _ _) (f â‰« snd _ _) = f := by
  cat_disch

@[reassoc (attr := simp)]
lemma whiskerLeft_fst (X : C) {Y Z : C} (f : Y âŸ¶ Z) : X â— f â‰« fst _ _ = fst _ _ := by
  simp [fst_def, â† whiskerLeft_comp_assoc]

@[reassoc (attr := simp)]
lemma whiskerLeft_snd (X : C) {Y Z : C} (f : Y âŸ¶ Z) : X â— f â‰« snd _ _ = snd _ _ â‰« f := by
  simp [snd_def, whisker_exchange_assoc]

@[reassoc (attr := simp)]
lemma whiskerRight_fst {X Y : C} (f : X âŸ¶ Y) (Z : C) : f â–· Z â‰« fst _ _ = fst _ _ â‰« f := by
  simp [fst_def, â† whisker_exchange_assoc]

@[reassoc (attr := simp)]
lemma whiskerRight_snd {X Y : C} (f : X âŸ¶ Y) (Z : C) : f â–· Z â‰« snd _ _ = snd _ _ := by
  simp [snd_def, â† comp_whiskerRight_assoc]

@[reassoc (attr := simp)]
lemma tensorHom_fst {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : C} (f : Xâ‚ âŸ¶ Xâ‚‚) (g : Yâ‚ âŸ¶ Yâ‚‚) :
    (f âŠ—â‚˜ g) â‰« fst _ _ = fst _ _ â‰« f := by simp [tensorHom_def]

@[reassoc (attr := simp)]
lemma tensorHom_snd {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : C} (f : Xâ‚ âŸ¶ Xâ‚‚) (g : Yâ‚ âŸ¶ Yâ‚‚) :
    (f âŠ—â‚˜ g) â‰« snd _ _ = snd _ _ â‰« g := by simp [tensorHom_def]

@[reassoc (attr := simp)]
lemma lift_map {V W X Y Z : C} (f : V âŸ¶ W) (g : V âŸ¶ X) (h : W âŸ¶ Y) (k : X âŸ¶ Z) :
    lift f g â‰« (h âŠ—â‚˜ k) = lift (f â‰« h) (g â‰« k) := by ext <;> simp

@[simp]
lemma lift_fst_comp_snd_comp {W X Y Z : C} (g : W âŸ¶ X) (g' : Y âŸ¶ Z) :
    lift (fst _ _ â‰« g) (snd _ _ â‰« g') = g âŠ—â‚˜ g' := by ext <;> simp

@[reassoc (attr := simp)]
lemma lift_whiskerRight {X Y Z W : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) (h : Y âŸ¶ W) :
    lift f g â‰« (h â–· Z) = lift (f â‰« h) g := by
  cat_disch

@[reassoc (attr := simp)]
lemma lift_whiskerLeft {X Y Z W : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) (h : Z âŸ¶ W) :
    lift f g â‰« (Y â— h) = lift f (g â‰« h) := by
  cat_disch

@[reassoc (attr := simp)]
lemma associator_hom_fst (X Y Z : C) :
    (Î±_ X Y Z).hom â‰« fst _ _ = fst _ _ â‰« fst _ _ := by
  simp [fst_def, â† whiskerLeft_rightUnitor_assoc, -whiskerLeft_rightUnitor,
    â† whiskerLeft_comp_assoc]

@[reassoc (attr := simp)]
lemma associator_hom_snd_fst (X Y Z : C) :
    (Î±_ X Y Z).hom â‰« snd _ _ â‰« fst _ _ = fst _ _ â‰« snd _ _ := by
  simp [fst_def, â† whiskerLeft_rightUnitor_assoc, -whiskerLeft_rightUnitor]

@[reassoc (attr := simp)]
lemma associator_hom_snd_snd (X Y Z : C) :
    (Î±_ X Y Z).hom â‰« snd _ _ â‰« snd _ _ = snd _ _ := by
  simp [snd_def, â† leftUnitor_whiskerRight_assoc, -leftUnitor_whiskerRight,
    â† comp_whiskerRight_assoc]

@[reassoc (attr := simp)]
lemma associator_inv_fst_fst (X Y Z : C) :
    (Î±_ X Y Z).inv â‰« fst _ _ â‰« fst _ _ = fst _ _ := by
  simp [fst_def, â† whiskerLeft_rightUnitor_assoc, -whiskerLeft_rightUnitor,
    â† whiskerLeft_comp_assoc]

@[reassoc (attr := simp)]
lemma associator_inv_fst_snd (X Y Z : C) :
    (Î±_ X Y Z).inv â‰« fst _ _ â‰« snd _ _ = snd _ _ â‰« fst _ _ := by
  simp [fst_def, â† whiskerLeft_rightUnitor_assoc, -whiskerLeft_rightUnitor]

@[reassoc (attr := simp)]
lemma associator_inv_snd (X Y Z : C) :
    (Î±_ X Y Z).inv â‰« snd _ _ = snd _ _ â‰« snd _ _ := by
  simp [snd_def, â† leftUnitor_whiskerRight_assoc, -leftUnitor_whiskerRight,
    â† comp_whiskerRight_assoc]

@[reassoc (attr := simp)]
lemma lift_lift_associator_hom {X Y Z W : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) (h : X âŸ¶ W) :
    lift (lift f g) h â‰« (Î±_ Y Z W).hom = lift f (lift g h) := by
  cat_disch

@[reassoc (attr := simp)]
lemma lift_lift_associator_inv {X Y Z W : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) (h : X âŸ¶ W) :
    lift f (lift g h) â‰« (Î±_ Y Z W).inv = lift (lift f g) h := by
  cat_disch

lemma leftUnitor_hom (X : C) : (Î»_ X).hom = snd _ _ := by simp [snd_def]
lemma rightUnitor_hom (X : C) : (Ï_ X).hom = fst _ _ := by simp [fst_def]

@[reassoc (attr := simp)]
lemma leftUnitor_inv_fst (X : C) :
    (Î»_ X).inv â‰« fst _ _ = toUnit _ := toUnit_unique _ _

@[reassoc (attr := simp)]
lemma leftUnitor_inv_snd (X : C) :
    (Î»_ X).inv â‰« snd _ _ = ğŸ™ X := by simp [snd_def]

@[reassoc (attr := simp)]
lemma rightUnitor_inv_fst (X : C) :
    (Ï_ X).inv â‰« fst _ _ = ğŸ™ X := by simp [fst_def]

@[reassoc (attr := simp)]
lemma rightUnitor_inv_snd (X : C) :
    (Ï_ X).inv â‰« snd _ _ = toUnit _ := toUnit_unique _ _

@[reassoc]
lemma whiskerLeft_toUnit_comp_rightUnitor_hom (X Y : C) : X â— toUnit Y â‰« (Ï_ X).hom = fst X Y := by
  rw [â† cancel_mono (Ï_ X).inv]; aesop

@[reassoc]
lemma whiskerRight_toUnit_comp_leftUnitor_hom (X Y : C) : toUnit X â–· Y â‰« (Î»_ Y).hom = snd X Y := by
  rw [â† cancel_mono (Î»_ Y).inv]; aesop

@[reassoc (attr := simp)]
lemma lift_leftUnitor_hom {X Y : C} (f : X âŸ¶ ğŸ™_ C) (g : X âŸ¶ Y) :
    lift f g â‰« (Î»_ Y).hom = g := by
  rw [â† Iso.eq_comp_inv]
  cat_disch

@[reassoc (attr := simp)]
lemma lift_rightUnitor_hom {X Y : C} (f : X âŸ¶ Y) (g : X âŸ¶ ğŸ™_ C) :
    lift f g â‰« (Ï_ Y).hom = f := by
  rw [â† Iso.eq_comp_inv]
  cat_disch

/-- Universal property of the Cartesian product: Maps to `X âŠ— Y` correspond to pairs of maps to `X`
and to `Y`. -/
@[simps]
def homEquivToProd {X Y Z : C} : (Z âŸ¶ X âŠ— Y) â‰ƒ (Z âŸ¶ X) Ã— (Z âŸ¶ Y) where
  toFun f := âŸ¨f â‰« fst _ _, f â‰« snd _ _âŸ©
  invFun f := lift f.1 f.2
  left_inv _ := by simp
  right_inv _ := by simp

section BraidedCategory

variable [BraidedCategory C]

@[reassoc (attr := simp)]
theorem braiding_hom_fst (X Y : C) : (Î²_ X Y).hom â‰« fst _ _ = snd _ _ := by
  simp [fst_def, snd_def, â† BraidedCategory.braiding_naturality_left_assoc]

@[reassoc (attr := simp)]
theorem braiding_hom_snd (X Y : C) : (Î²_ X Y).hom â‰« snd _ _ = fst _ _ := by
  simp [fst_def, snd_def, â† BraidedCategory.braiding_naturality_right_assoc]

@[reassoc (attr := simp)]
theorem braiding_inv_fst (X Y : C) : (Î²_ X Y).inv â‰« fst _ _ = snd _ _ := by
  simp [fst_def, snd_def, â† BraidedCategory.braiding_inv_naturality_left_assoc]

@[reassoc (attr := simp)]
theorem braiding_inv_snd (X Y : C) : (Î²_ X Y).inv â‰« snd _ _ = fst _ _ := by
  simp [fst_def, snd_def, â† BraidedCategory.braiding_inv_naturality_right_assoc]

@[reassoc (attr := simp)]
lemma tensorÎ¼_fst (W X Y Z : C) : tensorÎ¼ W X Y Z â‰« fst (W âŠ— Y) (X âŠ— Z) = fst W X âŠ—â‚˜ fst Y Z := by
  ext <;> simp [tensorÎ¼]

@[reassoc (attr := simp)]
lemma tensorÎ¼_snd (W X Y Z : C) : tensorÎ¼ W X Y Z â‰« snd (W âŠ— Y) (X âŠ— Z) = snd W X âŠ—â‚˜ snd Y Z := by
  ext <;> simp [tensorÎ¼]

@[reassoc (attr := simp)]
lemma tensorÎ´_fst (W X Y Z : C) : tensorÎ´ W X Y Z â‰« fst (W âŠ— X) (Y âŠ— Z) = fst W Y âŠ—â‚˜ fst X Z := by
  ext <;> simp [tensorÎ´]

@[reassoc (attr := simp)]
lemma tensorÎ´_snd (W X Y Z : C) : tensorÎ´ W X Y Z â‰« snd (W âŠ— X) (Y âŠ— Z) = snd W Y âŠ—â‚˜ snd X Z := by
  ext <;> simp [tensorÎ´]

theorem lift_snd_fst {X Y : C} : lift (snd X Y) (fst X Y) = (Î²_ X Y).hom := by cat_disch

@[simp, reassoc]
lemma lift_snd_comp_fst_comp {W X Y Z : C} (g : W âŸ¶ X) (g' : Y âŸ¶ Z) :
    lift (snd _ _ â‰« g') (fst _ _ â‰« g) = (Î²_ _ _).hom â‰« (g' âŠ—â‚˜ g) := by cat_disch

@[reassoc (attr := simp)]
lemma lift_braiding_hom {T X Y : C} (f : T âŸ¶ X) (g : T âŸ¶ Y) :
    lift f g â‰« (Î²_ X Y).hom = lift g f := by aesop

@[reassoc (attr := simp)]
lemma lift_braiding_inv {T X Y : C} (f : T âŸ¶ X) (g : T âŸ¶ Y) :
    lift f g â‰« (Î²_ Y X).inv = lift g f := by aesop

-- See note [lower instance priority]
instance (priority := low) toSymmetricCategory [BraidedCategory C] : SymmetricCategory C where

/-- `CartesianMonoidalCategory` implies `BraidedCategory`.
This is not an instance to prevent diamonds. -/
def _root_.CategoryTheory.BraidedCategory.ofCartesianMonoidalCategory : BraidedCategory C where
  braiding X Y := { hom := lift (snd _ _) (fst _ _), inv := lift (snd _ _) (fst _ _) }

@[deprecated (since := "2025-05-15")]
alias _root_.CategoryTheory.BraidedCategory.ofChosenFiniteProducts :=
  BraidedCategory.ofCartesianMonoidalCategory

instance : Nonempty (BraidedCategory C) := âŸ¨.ofCartesianMonoidalCategoryâŸ©

instance : Subsingleton (BraidedCategory C) where
  allEq
  | âŸ¨eâ‚, aâ‚, bâ‚, câ‚, dâ‚âŸ©, âŸ¨eâ‚‚, aâ‚‚, bâ‚‚, câ‚‚, dâ‚‚âŸ© => by
      congr
      ext
      Â· exact (@braiding_hom_fst C _ â€¹_â€º âŸ¨eâ‚, aâ‚, bâ‚, câ‚, dâ‚âŸ© ..).trans
          (@braiding_hom_fst C _ â€¹_â€º âŸ¨eâ‚‚, aâ‚‚, bâ‚‚, câ‚‚, dâ‚‚âŸ© ..).symm
      Â· exact (@braiding_hom_snd C _ â€¹_â€º âŸ¨eâ‚, aâ‚, bâ‚, câ‚, dâ‚âŸ© ..).trans
          (@braiding_hom_snd C _ â€¹_â€º âŸ¨eâ‚‚, aâ‚‚, bâ‚‚, câ‚‚, dâ‚‚âŸ© ..).symm

instance : Subsingleton (SymmetricCategory C) where
  allEq := by rintro âŸ¨_âŸ© âŸ¨_âŸ©; congr; exact Subsingleton.elim _ _

end BraidedCategory

instance (priority := 100) : Limits.HasFiniteProducts C :=
  letI : âˆ€ (X Y : C), Limits.HasLimit (Limits.pair X Y) := fun _ _ =>
    .mk âŸ¨_, tensorProductIsBinaryProduct _ _âŸ©
  letI : Limits.HasBinaryProducts C := Limits.hasBinaryProducts_of_hasLimit_pair _
  letI : Limits.HasTerminal C := Limits.hasTerminal_of_unique (ğŸ™_ C)
  hasFiniteProducts_of_has_binary_and_terminal

section CartesianMonoidalCategoryComparison

variable {D : Type uâ‚} [Category.{vâ‚} D] [CartesianMonoidalCategory D] (F : C â¥¤ D)
variable {E : Type uâ‚‚} [Category.{vâ‚‚} E] [CartesianMonoidalCategory E] (G : D â¥¤ E)

section terminalComparison

/-- When `C` and `D` have chosen finite products and `F : C â¥¤ D` is any functor,
`terminalComparison F` is the unique map `F (ğŸ™_ C) âŸ¶ ğŸ™_ D`. -/
abbrev terminalComparison : F.obj (ğŸ™_ C) âŸ¶ ğŸ™_ D := toUnit _

@[reassoc]
lemma map_toUnit_comp_terminalComparison (A : C) :
    F.map (toUnit A) â‰« terminalComparison F = toUnit _ := toUnit_unique _ _

open Limits

/-- If `terminalComparison F` is an Iso, then `F` preserves terminal objects. -/
lemma preservesLimit_empty_of_isIso_terminalComparison [IsIso (terminalComparison F)] :
    PreservesLimit (Functor.empty.{0} C) F := by
  apply preservesLimit_of_preserves_limit_cone isTerminalTensorUnit
  apply isLimitChangeEmptyCone D isTerminalTensorUnit
  exact asIso (terminalComparison F)|>.symm

/-- If `F` preserves terminal objects, then `terminalComparison F` is an isomorphism. -/
noncomputable def preservesTerminalIso [h : PreservesLimit (Functor.empty.{0} C) F] :
    F.obj (ğŸ™_ C) â‰… ğŸ™_ D :=
  (isLimitChangeEmptyCone D (isLimitOfPreserves _ isTerminalTensorUnit) (asEmptyCone (F.obj (ğŸ™_ C)))
    (Iso.refl _)).conePointUniqueUpToIso isTerminalTensorUnit

@[simp]
lemma preservesTerminalIso_hom [PreservesLimit (Functor.empty.{0} C) F] :
    (preservesTerminalIso F).hom = terminalComparison F := toUnit_unique _ _

instance terminalComparison_isIso_of_preservesLimits [PreservesLimit (Functor.empty.{0} C) F] :
    IsIso (terminalComparison F) := by
  rw [â† preservesTerminalIso_hom]
  infer_instance

@[simp]
lemma preservesTerminalIso_id : preservesTerminalIso (ğŸ­ C) = .refl _ := by
  cat_disch

@[simp]
lemma preservesTerminalIso_comp [PreservesLimit (Functor.empty.{0} C) F]
    [PreservesLimit (Functor.empty.{0} D) G] [PreservesLimit (Functor.empty.{0} C) (F â‹™ G)] :
    preservesTerminalIso (F â‹™ G) =
      G.mapIso (preservesTerminalIso F) â‰ªâ‰« preservesTerminalIso G := by
  cat_disch

end terminalComparison

section prodComparison

variable (A B : C)

/-- When `C` and `D` have chosen finite products and `F : C â¥¤ D` is any functor,
`prodComparison F A B` is the canonical comparison morphism from `F (A âŠ— B)` to `F(A) âŠ— F(B)`. -/
def prodComparison (A B : C) : F.obj (A âŠ— B) âŸ¶ F.obj A âŠ— F.obj B :=
  lift (F.map (fst A B)) (F.map (snd A B))

@[reassoc (attr := simp)]
theorem prodComparison_fst : prodComparison F A B â‰« fst _ _ = F.map (fst A B) :=
  lift_fst _ _

@[reassoc (attr := simp)]
theorem prodComparison_snd : prodComparison F A B â‰« snd _ _ = F.map (snd A B) :=
  lift_snd _ _

@[reassoc (attr := simp)]
theorem inv_prodComparison_map_fst [IsIso (prodComparison F A B)] :
    inv (prodComparison F A B) â‰« F.map (fst _ _) = fst _ _ := by simp [IsIso.inv_comp_eq]

@[reassoc (attr := simp)]
theorem inv_prodComparison_map_snd [IsIso (prodComparison F A B)] :
    inv (prodComparison F A B) â‰« F.map (snd _ _) = snd _ _ := by simp [IsIso.inv_comp_eq]

variable {A B} {A' B' : C}

/-- Naturality of the `prodComparison` morphism in both arguments. -/
@[reassoc]
theorem prodComparison_natural (f : A âŸ¶ A') (g : B âŸ¶ B') :
    F.map (f âŠ—â‚˜ g) â‰« prodComparison F A' B' =
      prodComparison F A B â‰« (F.map f âŠ—â‚˜ F.map g) := by
  apply hom_ext <;>
  simp only [Category.assoc, prodComparison_fst, tensorHom_fst, prodComparison_fst_assoc,
    prodComparison_snd, tensorHom_snd, prodComparison_snd_assoc, â† F.map_comp]

/-- Naturality of the `prodComparison` morphism in the right argument. -/
@[reassoc]
theorem prodComparison_natural_whiskerLeft (g : B âŸ¶ B') :
    F.map (A â— g) â‰« prodComparison F A B' =
      prodComparison F A B â‰« (F.obj A â— F.map g) := by
  ext <;> simp [â† Functor.map_comp]

/-- Naturality of the `prodComparison` morphism in the left argument. -/
@[reassoc]
theorem prodComparison_natural_whiskerRight (f : A âŸ¶ A') :
    F.map (f â–· B) â‰« prodComparison F A' B =
      prodComparison F A B â‰« (F.map f â–· F.obj B) := by
  ext <;> simp [â† Functor.map_comp]

section
variable [IsIso (prodComparison F A B)]

/-- If the product comparison morphism is an iso, its inverse is natural in both argument. -/
@[reassoc]
theorem prodComparison_inv_natural (f : A âŸ¶ A') (g : B âŸ¶ B') [IsIso (prodComparison F A' B')] :
    inv (prodComparison F A B) â‰« F.map (f âŠ—â‚˜ g) =
      (F.map f âŠ—â‚˜ F.map g) â‰« inv (prodComparison F A' B') := by
  rw [IsIso.eq_comp_inv, Category.assoc, IsIso.inv_comp_eq, prodComparison_natural]

/-- If the product comparison morphism is an iso, its inverse is natural in the right argument. -/
@[reassoc]
theorem prodComparison_inv_natural_whiskerLeft (g : B âŸ¶ B') [IsIso (prodComparison F A B')] :
    inv (prodComparison F A B) â‰« F.map (A â— g) =
      (F.obj A â— F.map g) â‰« inv (prodComparison F A B') := by
  rw [IsIso.eq_comp_inv, Category.assoc, IsIso.inv_comp_eq, prodComparison_natural_whiskerLeft]

/-- If the product comparison morphism is an iso, its inverse is natural in the left argument. -/
@[reassoc]
theorem prodComparison_inv_natural_whiskerRight (f : A âŸ¶ A') [IsIso (prodComparison F A' B)] :
    inv (prodComparison F A B) â‰« F.map (f â–· B) =
      (F.map f â–· F.obj B) â‰« inv (prodComparison F A' B) := by
  rw [IsIso.eq_comp_inv, Category.assoc, IsIso.inv_comp_eq, prodComparison_natural_whiskerRight]

end

lemma prodComparison_comp :
    prodComparison (F â‹™ G) A B =
      G.map (prodComparison F A B) â‰« prodComparison G (F.obj A) (F.obj B) := by
  unfold prodComparison
  ext <;> simp [â† G.map_comp]

@[simp]
lemma prodComparison_id :
    prodComparison (ğŸ­ C) A B = ğŸ™ (A âŠ— B) := lift_fst_snd

/-- The product comparison morphism from `F(A âŠ— -)` to `FA âŠ— F-`, whose components are given by
`prodComparison`. -/
@[simps]
def prodComparisonNatTrans (A : C) :
    (curriedTensor C).obj A â‹™ F âŸ¶ F â‹™ (curriedTensor D).obj (F.obj A) where
  app B := prodComparison F A B
  naturality x y f := by
    apply hom_ext <;>
    simp only [Functor.comp_obj, curriedTensor_obj_obj,
      Functor.comp_map, curriedTensor_obj_map, Category.assoc, prodComparison_fst, whiskerLeft_fst,
      prodComparison_snd, prodComparison_snd_assoc, whiskerLeft_snd, â† F.map_comp]

theorem prodComparisonNatTrans_comp :
    prodComparisonNatTrans (F â‹™ G) A = Functor.whiskerRight (prodComparisonNatTrans F A) G â‰«
      Functor.whiskerLeft F (prodComparisonNatTrans G (F.obj A)) := by
  ext; simp [prodComparison_comp]

@[simp]
lemma prodComparisonNatTrans_id :
    prodComparisonNatTrans (ğŸ­ C) A = ğŸ™ _ := by ext; simp

/-- The product comparison morphism from `F(- âŠ— -)` to `F- âŠ— F-`, whose components are given by
`prodComparison`. -/
@[simps]
def prodComparisonBifunctorNatTrans :
    curriedTensor C â‹™ (Functor.whiskeringRight _ _ _).obj F âŸ¶
      F â‹™ curriedTensor D â‹™ (Functor.whiskeringLeft _ _ _).obj F where
  app A := prodComparisonNatTrans F A
  naturality x y f := by
    ext z
    apply hom_ext <;> simp [â† Functor.map_comp]

variable {E : Type uâ‚‚} [Category.{vâ‚‚} E] [CartesianMonoidalCategory E] (G : D â¥¤ E)

theorem prodComparisonBifunctorNatTrans_comp : prodComparisonBifunctorNatTrans (F â‹™ G) =
    Functor.whiskerRight
      (prodComparisonBifunctorNatTrans F) ((Functor.whiskeringRight _ _ _).obj G) â‰«
        Functor.whiskerLeft F (Functor.whiskerRight (prodComparisonBifunctorNatTrans G)
          ((Functor.whiskeringLeft _ _ _).obj F)) := by
  ext; simp [prodComparison_comp]

instance (A : C) [âˆ€ B, IsIso (prodComparison F A B)] : IsIso (prodComparisonNatTrans F A) := by
  letI : âˆ€ X, IsIso ((prodComparisonNatTrans F A).app X) := by assumption
  apply NatIso.isIso_of_isIso_app

instance [âˆ€ A B, IsIso (prodComparison F A B)] : IsIso (prodComparisonBifunctorNatTrans F) := by
  letI : âˆ€ X, IsIso ((prodComparisonBifunctorNatTrans F).app X) :=
    fun _ â†¦ by dsimp; apply NatIso.isIso_of_isIso_app
  apply NatIso.isIso_of_isIso_app

open Limits
section PreservesLimitPairs

section
variable (A B)
variable [PreservesLimit (pair A B) F]

/-- If `F` preserves the limit of the pair `(A, B)`, then the binary fan given by
`(F.map fst A B, F.map (snd A B))` is a limit cone. -/
noncomputable def isLimitCartesianMonoidalCategoryOfPreservesLimits :
    IsLimit <| BinaryFan.mk (F.map (fst A B)) (F.map (snd A B)) :=
  mapIsLimitOfPreservesOfIsLimit F (fst _ _) (snd _ _) <|
    (tensorProductIsBinaryProduct A B).ofIsoLimit <|
      isoBinaryFanMk (BinaryFan.mk (fst A B) (snd A B))

@[deprecated (since := "2025-05-15")]
alias isLimitChosenFiniteProductsOfPreservesLimits :=
  isLimitCartesianMonoidalCategoryOfPreservesLimits

/-- If `F` preserves the limit of the pair `(A, B)`, then `prodComparison F A B` is an isomorphism.
-/
noncomputable def prodComparisonIso : F.obj (A âŠ— B) â‰… F.obj A âŠ— F.obj B :=
  IsLimit.conePointUniqueUpToIso (isLimitCartesianMonoidalCategoryOfPreservesLimits F A B)
    (tensorProductIsBinaryProduct _ _)

@[simp]
lemma prodComparisonIso_hom : (prodComparisonIso F A B).hom = prodComparison F A B :=
  rfl

instance isIso_prodComparison_of_preservesLimit_pair : IsIso (prodComparison F A B) := by
  rw [â† prodComparisonIso_hom]
  infer_instance

@[simp] lemma prodComparisonIso_id : prodComparisonIso (ğŸ­ C) A B = .refl _ := by ext <;> simp

@[simp]
lemma prodComparisonIso_comp [PreservesLimit (pair A B) (F â‹™ G)]
    [PreservesLimit (pair (F.obj A) (F.obj B)) G] :
    prodComparisonIso (F â‹™ G) A B =
      G.mapIso (prodComparisonIso F A B) â‰ªâ‰« prodComparisonIso G (F.obj A) (F.obj B) := by
  ext <;> simp [CartesianMonoidalCategory.prodComparison, â† G.map_comp]

end

/-- The natural isomorphism `F(A âŠ— -) â‰… FA âŠ— F-`, provided each `prodComparison F A B` is an
isomorphism (as `B` changes). -/
@[simps! hom inv]
noncomputable def prodComparisonNatIso (A : C) [âˆ€ B, PreservesLimit (pair A B) F] :
    (curriedTensor C).obj A â‹™ F â‰… F â‹™ (curriedTensor D).obj (F.obj A) :=
  asIso (prodComparisonNatTrans F A)

/-- The natural isomorphism of bifunctors `F(- âŠ— -) â‰… F- âŠ— F-`, provided each
`prodComparison F A B` is an isomorphism. -/
@[simps! hom inv]
noncomputable def prodComparisonBifunctorNatIso [âˆ€ A B, PreservesLimit (pair A B) F] :
    curriedTensor C â‹™ (Functor.whiskeringRight _ _ _).obj F â‰…
      F â‹™ curriedTensor D â‹™ (Functor.whiskeringLeft _ _ _).obj F :=
  asIso (prodComparisonBifunctorNatTrans F)

end PreservesLimitPairs

section ProdComparisonIso

/-- If `prodComparison F A B` is an isomorphism, then `F` preserves the limit of `pair A B`. -/
lemma preservesLimit_pair_of_isIso_prodComparison (A B : C)
    [IsIso (prodComparison F A B)] :
    PreservesLimit (pair A B) F := by
  apply preservesLimit_of_preserves_limit_cone (tensorProductIsBinaryProduct A B)
  refine IsLimit.equivOfNatIsoOfIso (pairComp A B F) _
    ((BinaryFan.mk (fst (F.obj A) (F.obj B)) (snd _ _)).extend (prodComparison F A B))
      (BinaryFan.ext (by exact Iso.refl _) ?_ ?_) |>.invFun
      (IsLimit.extendIso _ (tensorProductIsBinaryProduct (F.obj A) (F.obj B)))
  Â· dsimp only [BinaryFan.fst]
    simp [pairComp]
  Â· dsimp only [BinaryFan.snd]
    simp [pairComp]

/-- If `prodComparison F A B` is an isomorphism for all `A B` then `F` preserves limits of shape
`Discrete (WalkingPair)`. -/
lemma preservesLimitsOfShape_discrete_walkingPair_of_isIso_prodComparison
    [âˆ€ A B, IsIso (prodComparison F A B)] : PreservesLimitsOfShape (Discrete WalkingPair) F := by
  constructor
  intro K
  refine @preservesLimit_of_iso_diagram _ _ _ _ _ _ _ _ _ (diagramIsoPair K).symm ?_
  apply preservesLimit_pair_of_isIso_prodComparison

end ProdComparisonIso

end prodComparison

end CartesianMonoidalCategoryComparison

open Limits

variable {P : ObjectProperty C}

-- TODO: Introduce `ClosedUnderFiniteProducts`?
/-- The restriction of a Cartesian-monoidal category along an object property that's closed under
finite products is Cartesian-monoidal. -/
noncomputable def fullSubcategory (hPâ‚€ : ClosedUnderLimitsOfShape (Discrete PEmpty) P)
    (hPâ‚‚ : ClosedUnderLimitsOfShape (Discrete WalkingPair) P) :
    CartesianMonoidalCategory P.FullSubcategory where
  __ := MonoidalCategory.fullSubcategory P (hPâ‚€ isTerminalTensorUnit <| by simp)
    fun X Y hX hY â†¦ hPâ‚‚ (tensorProductIsBinaryProduct X Y) (by rintro âŸ¨_ | _âŸ© <;> simp [hX, hY])
  isTerminalTensorUnit := .ofUniqueHom (fun X â†¦ toUnit X.1) fun _ _ â†¦ by ext
  fst X Y := fst X.1 Y.1
  snd X Y := snd X.1 Y.1
  tensorProductIsBinaryProduct X Y :=
    BinaryFan.IsLimit.mk _ (lift (C := C)) (lift_fst (C := C)) (lift_snd (C := C))
      (by rintro T f g m rfl rfl; symm; exact lift_comp_fst_snd _)
  fst_def X Y := fst_def X.1 Y.1
  snd_def X Y := snd_def X.1 Y.1

end CartesianMonoidalCategory

open MonoidalCategory CartesianMonoidalCategory

variable
  {C : Type uâ‚} [Category.{vâ‚} C] [CartesianMonoidalCategory C]
  {D : Type uâ‚‚} [Category.{vâ‚‚} D] [CartesianMonoidalCategory D]
  {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E] [CartesianMonoidalCategory E]
  (F : C â¥¤ D) (G : D â¥¤ E) {X Y Z : C}

open Functor.LaxMonoidal Functor.OplaxMonoidal
open Limits (PreservesFiniteProducts)

namespace Functor.OplaxMonoidal
variable [F.OplaxMonoidal]

lemma Î·_of_cartesianMonoidalCategory :
    Î· F = CartesianMonoidalCategory.terminalComparison F := toUnit_unique ..

@[reassoc (attr := simp)]
lemma Î´_fst (X Y : C) :
    Î´ F X Y â‰« fst _ _ = F.map (fst _ _) := by
  trans F.map (X â— toUnit Y) â‰« F.map (Ï_ X).hom
  Â· rw [â† whiskerLeft_fst _ (F.map (toUnit Y)), Î´_natural_right_assoc]
    simp [â† OplaxMonoidal.right_unitality_hom, rightUnitor_hom (F.obj X)]
  Â· simp [â† Functor.map_comp, rightUnitor_hom]

@[reassoc (attr := simp)]
lemma Î´_snd (X Y : C) :
    Î´ F X Y â‰« snd _ _ = F.map (snd _ _) := by
  trans F.map (toUnit X â–· Y) â‰« F.map (Î»_ Y).hom
  Â· rw [â† whiskerRight_snd (F.map (toUnit X)), Î´_natural_left_assoc]
    simp [â† OplaxMonoidal.left_unitality_hom, leftUnitor_hom (F.obj Y)]
  Â· simp [â† Functor.map_comp, leftUnitor_hom]

@[reassoc (attr := simp)]
lemma lift_Î´ (f : X âŸ¶ Y) (g : X âŸ¶ Z) : F.map (lift f g) â‰« Î´ F _ _ = lift (F.map f) (F.map g) := by
  ext <;> simp [â† map_comp]

lemma Î´_of_cartesianMonoidalCategory (X Y : C) :
    Î´ F X Y = CartesianMonoidalCategory.prodComparison F X Y := by cat_disch

variable [PreservesFiniteProducts F]

instance : IsIso (Î· F) :=
  Î·_of_cartesianMonoidalCategory F â–¸ terminalComparison_isIso_of_preservesLimits F

instance (X Y : C) : IsIso (Î´ F X Y) :=
  Î´_of_cartesianMonoidalCategory F X Y â–¸ isIso_prodComparison_of_preservesLimit_pair F X Y

omit [F.OplaxMonoidal] in
/-- Any functor between Cartesian-monoidal categories is oplax monoidal.

This is not made an instance because it would create a diamond for the oplax monoidal structure on
the identity and composition of functors. -/
def ofChosenFiniteProducts (F : C â¥¤ D) : F.OplaxMonoidal where
  Î· := terminalComparison F
  Î´ X Y := prodComparison F X Y
  Î´_natural_left f X := by ext <;> simp [â† Functor.map_comp]
  Î´_natural_right X g := by ext <;> simp [â† Functor.map_comp]
  oplax_associativity _ _ _ := by ext <;> simp [â† Functor.map_comp]
  oplax_left_unitality _ := by ext; simp [â† Functor.map_comp]
  oplax_right_unitality _ := by ext; simp [â† Functor.map_comp]

omit [F.OplaxMonoidal] in
/-- Any functor between Cartesian-monoidal categories is oplax monoidal in a unique way. -/
instance : Subsingleton F.OplaxMonoidal where
  allEq a b := by
    ext1
    Â· exact toUnit_unique _ _
    Â· ext1; ext1; rw [Î´_of_cartesianMonoidalCategory, Î´_of_cartesianMonoidalCategory]

end OplaxMonoidal

namespace Monoidal
variable [F.Monoidal] [G.Monoidal]

@[reassoc (attr := simp)]
lemma toUnit_Îµ (X : C) : toUnit (F.obj X) â‰« Îµ F = F.map (toUnit X) := by
  rw [â† cancel_mono (ÎµIso F).inv]; exact toUnit_unique ..

@[reassoc (attr := simp)]
lemma lift_Î¼ (f : X âŸ¶ Y) (g : X âŸ¶ Z) : lift (F.map f) (F.map g) â‰« Î¼ F _ _ = F.map (lift f g) :=
  (cancel_mono (Î¼Iso _ _ _).inv).1 (by simp)

@[reassoc (attr := simp)]
lemma Î¼_fst (X Y : C) : Î¼ F X Y â‰« F.map (fst X Y) = fst (F.obj X) (F.obj Y) :=
  (cancel_epi (Î¼Iso _ _ _).inv).1 (by simp)

@[reassoc (attr := simp)]
lemma Î¼_snd (X Y : C) : Î¼ F X Y â‰« F.map (snd X Y) = snd (F.obj X) (F.obj Y) :=
  (cancel_epi (Î¼Iso _ _ _).inv).1 (by simp)

attribute [-instance] Functor.LaxMonoidal.comp Functor.Monoidal.instComp in
@[reassoc]
lemma Î¼_comp [(F â‹™ G).Monoidal] (X Y : C) : Î¼ (F â‹™ G) X Y = Î¼ G _ _ â‰« G.map (Î¼ F X Y) := by
  rw [â† cancel_mono (Î¼Iso _ _ _).inv]; ext <;> simp [â† Functor.comp_obj, â† Functor.map_comp]

variable [PreservesFiniteProducts F]

lemma Îµ_of_cartesianMonoidalCategory : Îµ F = (preservesTerminalIso F).inv := by
  change (ÎµIso F).symm.inv = _; congr; ext

lemma Î¼_of_cartesianMonoidalCategory (X Y : C) : Î¼ F X Y = (prodComparisonIso F X Y).inv := by
  change (Î¼Iso F X Y).symm.inv = _; congr; ext : 1; simpa using Î´_of_cartesianMonoidalCategory F X Y

attribute [local instance] Functor.OplaxMonoidal.ofChosenFiniteProducts in
omit [F.Monoidal] in
/-- A finite-product-preserving functor between Cartesian monoidal categories is monoidal.

This is not made an instance because it would create a diamond for the monoidal structure on
the identity and composition of functors. -/
noncomputable def ofChosenFiniteProducts (F : C â¥¤ D) [PreservesFiniteProducts F] : F.Monoidal :=
  .ofOplaxMonoidal F

instance : Subsingleton F.Monoidal := (toOplaxMonoidal_injective F).subsingleton

end Monoidal

namespace Monoidal

instance [F.Monoidal] : PreservesFiniteProducts F :=
  have (A B : _) : IsIso (CartesianMonoidalCategory.prodComparison F A B) :=
    Î´_of_cartesianMonoidalCategory F A B â–¸ inferInstance
  have : IsIso (CartesianMonoidalCategory.terminalComparison F) :=
    Î·_of_cartesianMonoidalCategory F â–¸ inferInstance
  have := preservesLimitsOfShape_discrete_walkingPair_of_isIso_prodComparison F
  have := preservesLimit_empty_of_isIso_terminalComparison F
  have := Limits.preservesLimitsOfShape_pempty_of_preservesTerminal F
  .of_preserves_binary_and_terminal _

attribute [local instance] OplaxMonoidal.ofChosenFiniteProducts in
/--
A functor between Cartesian monoidal categories is monoidal iff it preserves finite products.
-/
lemma nonempty_monoidal_iff_preservesFiniteProducts :
    Nonempty F.Monoidal â†” PreservesFiniteProducts F :=
  âŸ¨fun âŸ¨_âŸ© â†¦ inferInstance, fun _ â†¦ âŸ¨ofChosenFiniteProducts FâŸ©âŸ©

end Monoidal

namespace Braided
variable [BraidedCategory C] [BraidedCategory D]

attribute [local instance] Functor.Monoidal.ofChosenFiniteProducts in
/-- A finite-product-preserving functor between Cartesian monoidal categories is braided.

This is not made an instance because it would create a diamond for the monoidal structure on
the identity and composition of functors. -/
noncomputable def ofChosenFiniteProducts (F : C â¥¤ D) [PreservesFiniteProducts F] : F.Braided where
  braided X Y := by rw [â† cancel_mono (Monoidal.Î¼Iso _ _ _).inv]; ext <;> simp [â† F.map_comp]

instance : Subsingleton F.Braided := (Braided.toMonoidal_injective F).subsingleton

end Braided

@[deprecated (since := "2025-04-24")]
alias oplaxMonoidalOfChosenFiniteProducts := OplaxMonoidal.ofChosenFiniteProducts

@[deprecated (since := "2025-04-24")]
alias monoidalOfChosenFiniteProducts := Monoidal.ofChosenFiniteProducts

@[deprecated (since := "2025-04-24")]
alias braidedOfChosenFiniteProducts := Braided.ofChosenFiniteProducts

namespace EssImageSubcategory
variable [F.Full] [F.Faithful] [PreservesFiniteProducts F] {T X Y Z : F.EssImageSubcategory}

@[simps!]
noncomputable instance instCartesianMonoidalCategory :
     CartesianMonoidalCategory F.EssImageSubcategory :=
  .fullSubcategory (.essImage _) (.essImage _)

lemma tensor_obj (X Y : F.EssImageSubcategory) : (X âŠ— Y).obj = X.obj âŠ— Y.obj := rfl

lemma lift_def (f : T âŸ¶ X) (g : T âŸ¶ Y) : lift f g = lift (T := T.1) f g := rfl

lemma associator_hom_def (X Y Z : F.EssImageSubcategory) :
    (Î±_ X Y Z).hom = (Î±_ X.obj Y.obj Z.obj).hom := rfl

lemma associator_inv_def (X Y Z : F.EssImageSubcategory) :
    (Î±_ X Y Z).inv = (Î±_ X.obj Y.obj Z.obj).inv := rfl

lemma toUnit_def (X : F.EssImageSubcategory) : toUnit X = toUnit X.obj := toUnit_unique ..

end Functor.EssImageSubcategory

namespace NatTrans
variable (F G : C â¥¤ D) [F.Monoidal] [G.Monoidal]

instance IsMonoidal.of_cartesianMonoidalCategory (Î± : F âŸ¶ G) : IsMonoidal Î± where
  unit := (cancel_mono (Functor.Monoidal.ÎµIso _).inv).1 (toUnit_unique _ _)
  tensor {X Y} := by
    rw [â† cancel_mono (Functor.Monoidal.Î¼Iso _ _ _).inv]
    rw [â† cancel_epi (Functor.Monoidal.Î¼Iso _ _ _).inv]
    apply CartesianMonoidalCategory.hom_ext <;> simp

end NatTrans

end CategoryTheory
