/-
Copyright (c) 2025 Markus Himmel, Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel, Andrew Yang
-/
import Mathlib.Algebra.Category.MonCat.Limits
import Mathlib.CategoryTheory.Monoidal.Cartesian.Basic
import Mathlib.CategoryTheory.Monoidal.Mon_

/-!
# Yoneda embedding of `Mon C`

We show that monoid objects in Cartesian monoidal categories are exactly those whose yoneda presheaf
is a presheaf of monoids, by constructing the yoneda embedding `Mon C â¥¤ Cáµ’áµ– â¥¤ MonCat.{v}` and
showing that it is fully faithful and its (essential) image is the representable functors.
-/

open CategoryTheory MonoidalCategory Limits Opposite CartesianMonoidalCategory MonObj

universe w v u
variable {C D : Type*} [Category.{v} C] [CartesianMonoidalCategory C]
  [Category.{w} D] [CartesianMonoidalCategory D]
  {M N O X Y : C} [MonObj M] [MonObj N] [MonObj O]

namespace MonObj

instance : IsMonHom (toUnit M) where

instance : IsMonHom Î·[M] where
  mul_hom := by simp [toUnit_unique (Ï_ (ğŸ™_ C)).hom (Î»_ (ğŸ™_ C)).hom]

theorem lift_lift_assoc {A : C} {B : C} [MonObj B] (f g h : A âŸ¶ B) :
    lift (lift f g â‰« Î¼) h â‰« Î¼ = lift f (lift g h â‰« Î¼) â‰« Î¼ := by
  have := lift (lift f g) h â‰«= mul_assoc B
  rwa [lift_whiskerRight_assoc, lift_lift_associator_hom_assoc, lift_whiskerLeft_assoc] at this

@[reassoc (attr := simp)]
theorem lift_comp_one_left {A : C} {B : C} [MonObj B] (f : A âŸ¶ ğŸ™_ C) (g : A âŸ¶ B) :
    lift (f â‰« Î·) g â‰« Î¼ = g := by
  have := lift f g â‰«= one_mul B
  rwa [lift_whiskerRight_assoc, lift_leftUnitor_hom] at this

@[reassoc (attr := simp)]
theorem lift_comp_one_right {A : C} {B : C} [MonObj B] (f : A âŸ¶ B) (g : A âŸ¶ ğŸ™_ C) :
    lift f (g â‰« Î·) â‰« Î¼ = f := by
  have := lift f g â‰«= mul_one B
  rwa [lift_whiskerLeft_assoc, lift_rightUnitor_hom] at this

variable [BraidedCategory C]

attribute [local simp] tensorObj.one_def tensorObj.mul_def

instance : IsMonHom (fst M N) where
instance : IsMonHom (snd M N) where

instance {f : M âŸ¶ N} {g : M âŸ¶ O} [IsMonHom f] [IsMonHom g] : IsMonHom (lift f g) where

instance [IsCommMonObj M] : IsMonHom Î¼[M] where
  one_hom := by simp [toUnit_unique (Ï_ (ğŸ™_ C)).hom (Î»_ (ğŸ™_ C)).hom]

end MonObj

namespace Mon
variable [BraidedCategory C]

attribute [local simp] tensorObj.one_def tensorObj.mul_def

instance : CartesianMonoidalCategory (Mon C) where
  isTerminalTensorUnit := .ofUniqueHom (fun M â†¦ âŸ¨toUnit _âŸ©) fun M f â†¦ by ext; exact toUnit_unique ..
  fst M N := .mk (fst M.X N.X)
  snd M N := .mk (snd M.X N.X)
  tensorProductIsBinaryProduct M N :=
    BinaryFan.IsLimit.mk _ (fun {T} f g â†¦ âŸ¨lift f.hom g.homâŸ©)
      (by aesop_cat) (by aesop_cat) (by aesop_cat)
  fst_def M N := by ext; simp [fst_def]; congr
  snd_def M N := by ext; simp [snd_def]; congr

variable {M Nâ‚ Nâ‚‚ : Mon C}

@[simp] lemma lift_hom (f : M âŸ¶ Nâ‚) (g : M âŸ¶ Nâ‚‚) : (lift f g).hom = lift f.hom g.hom := rfl
@[simp] lemma fst_hom (M N : Mon C) : (fst M N).hom = fst M.X N.X := rfl
@[simp] lemma snd_hom (M N : Mon C) : (snd M N).hom = snd M.X N.X := rfl

end Mon

variable (X) in
/-- If `X` represents a presheaf of monoids, then `X` is a monoid object. -/
@[simps]
def MonObj.ofRepresentableBy (F : Cáµ’áµ– â¥¤ MonCat.{w}) (Î± : (F â‹™ forget _).RepresentableBy X) :
    MonObj X where
  one := Î±.homEquiv.symm 1
  mul := Î±.homEquiv.symm (Î±.homEquiv (fst X X) * Î±.homEquiv (snd X X))
  one_mul := by
    apply Î±.homEquiv.injective
    simp only [Î±.homEquiv_comp, Equiv.apply_symm_apply]
    simp only [Functor.comp_map, ConcreteCategory.forget_map_eq_coe, map_mul]
    simp only [â† ConcreteCategory.forget_map_eq_coe, â† Functor.comp_map, â† Î±.homEquiv_comp]
    simp only [whiskerRight_fst, whiskerRight_snd]
    simp only [Î±.homEquiv_comp, Equiv.apply_symm_apply]
    simp [leftUnitor_hom]
  mul_one := by
    apply Î±.homEquiv.injective
    simp only [Î±.homEquiv_comp, Equiv.apply_symm_apply]
    simp only [Functor.comp_map, ConcreteCategory.forget_map_eq_coe, map_mul]
    simp only [â† ConcreteCategory.forget_map_eq_coe, â† Functor.comp_map, â† Î±.homEquiv_comp]
    simp only [whiskerLeft_fst, whiskerLeft_snd]
    simp only [Î±.homEquiv_comp, Equiv.apply_symm_apply]
    simp [rightUnitor_hom]
  mul_assoc := by
    apply Î±.homEquiv.injective
    simp only [Î±.homEquiv_comp, Equiv.apply_symm_apply]
    simp only [Functor.comp_map, ConcreteCategory.forget_map_eq_coe, map_mul]
    simp only [â† ConcreteCategory.forget_map_eq_coe, â† Functor.comp_map, â† Î±.homEquiv_comp]
    simp only [whiskerRight_fst, whiskerRight_snd, whiskerLeft_fst,
      associator_hom_fst, whiskerLeft_snd]
    simp only [Î±.homEquiv_comp, Equiv.apply_symm_apply]
    simp only [Functor.comp_map, ConcreteCategory.forget_map_eq_coe, map_mul, _root_.mul_assoc]
    simp only [â† ConcreteCategory.forget_map_eq_coe, â† Functor.comp_map, â† Î±.homEquiv_comp]
    simp

@[deprecated (since := "2025-09-09")] alias Mon_Class.ofRepresentableBy := MonObj.ofRepresentableBy

@[deprecated (since := "2025-03-07")]
alias MonObjOfRepresentableBy := MonObj.ofRepresentableBy

@[deprecated (since := "2025-09-09")] alias Mon_ClassOfRepresentableBy := MonObjOfRepresentableBy

/-- If `M` is a monoid object, then `Hom(X, M)` has a monoid structure. -/
abbrev Hom.monoid : Monoid (X âŸ¶ M) where
  mul fâ‚ fâ‚‚ := lift fâ‚ fâ‚‚ â‰« Î¼
  mul_assoc fâ‚ fâ‚‚ fâ‚ƒ := by
    change lift (lift fâ‚ fâ‚‚ â‰« Î¼) fâ‚ƒ â‰« Î¼ = lift fâ‚ (lift fâ‚‚ fâ‚ƒ â‰« Î¼) â‰« Î¼
    trans lift (lift fâ‚ fâ‚‚) fâ‚ƒ â‰« Î¼ â–· M â‰« Î¼
    Â· rw [â† tensorHom_id, lift_map_assoc, Category.comp_id]
    trans lift fâ‚ (lift fâ‚‚ fâ‚ƒ) â‰« M â— Î¼ â‰« Î¼
    Â· rw [MonObj.mul_assoc]
      simp_rw [â† Category.assoc]
      congr 2
      ext <;> simp
    Â· rw [â† id_tensorHom, lift_map_assoc, Category.comp_id]
  one := toUnit X â‰« Î·
  one_mul f := by
    change lift (toUnit _ â‰« Î·) f â‰« Î¼ = f
    rw [â† Category.comp_id f, â† lift_map_assoc, tensorHom_id, MonObj.one_mul,
      Category.comp_id, leftUnitor_hom]
    exact lift_snd _ _
  mul_one f := by
    change lift f (toUnit _ â‰« Î·) â‰« Î¼ = f
    rw [â† Category.comp_id f, â† lift_map_assoc, id_tensorHom, MonObj.mul_one,
      Category.comp_id, rightUnitor_hom]
    exact lift_fst _ _

scoped[MonObj] attribute [instance] Hom.monoid

lemma Hom.one_def : (1 : X âŸ¶ M) = toUnit X â‰« Î· := rfl
lemma Hom.mul_def (fâ‚ fâ‚‚ : X âŸ¶ M) : fâ‚ * fâ‚‚ = lift fâ‚ fâ‚‚ â‰« Î¼ := rfl

namespace CategoryTheory.Functor
variable (F : C â¥¤ D) [F.Monoidal]

open scoped Obj

protected lemma map_mul (f g : X âŸ¶ M) : F.map (f * g) = F.map f * F.map g := by
  simp only [Hom.mul_def, map_comp, obj.Î¼_def, â† Category.assoc]
  congr 1
  rw [â† IsIso.comp_inv_eq]
  ext <;> simp

@[simp] protected lemma map_one : F.map (1 : X âŸ¶ M) = 1 := by simp [Hom.one_def]

/-- `Functor.map` of a monoidal functor as a `MonoidHom`. -/
@[simps]
def homMonoidHom : (X âŸ¶ M) â†’* (F.obj X âŸ¶ F.obj M) where
  toFun := F.map
  map_one' := F.map_one
  map_mul' := F.map_mul

/-- `Functor.map` of a fully faithful monoidal functor as a `MulEquiv`. -/
@[simps!]
def FullyFaithful.homMulEquiv (hF : F.FullyFaithful) : (X âŸ¶ M) â‰ƒ* (F.obj X âŸ¶ F.obj M) where
  __ := hF.homEquiv
  __ := F.homMonoidHom

end CategoryTheory.Functor

section BraidedCategory
variable [BraidedCategory C]

/-- If `M` is a commutative monoid object, then `Hom(X, M)` has a commutative monoid structure. -/
abbrev Hom.commMonoid [IsCommMonObj M] : CommMonoid (X âŸ¶ M) where
  mul_comm f g := by simpa [-IsCommMonObj.mul_comm] using lift g f â‰«= IsCommMonObj.mul_comm M

scoped[MonObj] attribute [instance] Hom.commMonoid

end BraidedCategory

variable (M) in
/-- If `M` is a monoid object, then `Hom(-, M)` is a presheaf of monoids. -/
@[simps]
def yonedaMonObj : Cáµ’áµ– â¥¤ MonCat.{v} where
  obj X := MonCat.of (unop X âŸ¶ M)
  map {X Yâ‚‚} Ï† := MonCat.ofHom
    { toFun := (Ï†.unop â‰« Â·)
      map_one' := by
        change Ï†.unop â‰« toUnit _ â‰« Î· = toUnit _ â‰« Î·
        rw [â† Category.assoc, toUnit_unique (Ï†.unop â‰« toUnit _)]
      map_mul' fâ‚ fâ‚‚ := by
        change Ï†.unop â‰« lift fâ‚ fâ‚‚ â‰« Î¼ = lift (Ï†.unop â‰« fâ‚) (Ï†.unop â‰« fâ‚‚) â‰« Î¼
        rw [â† Category.assoc]
        cat_disch }
  map_id _ := MonCat.hom_ext (MonoidHom.ext Category.id_comp)
  map_comp _ _ := MonCat.hom_ext (MonoidHom.ext (Category.assoc _ _))

variable (X) in
/-- If `X` represents a presheaf of monoids `F`, then `Hom(-, X)` is isomorphic to `F` as
a presheaf of monoids. -/
@[simps!]
def yonedaMonObjIsoOfRepresentableBy
    (F : Cáµ’áµ– â¥¤ MonCat.{v}) (Î± : (F â‹™ forget _).RepresentableBy X) :
    letI := MonObj.ofRepresentableBy X F Î±
    yonedaMonObj X â‰… F :=
  letI := MonObj.ofRepresentableBy X F Î±
  NatIso.ofComponents (fun Y â†¦ MulEquiv.toMonCatIso
    { toEquiv := Î±.homEquiv
      map_mul' fâ‚ fâ‚‚ := by
        change Î±.homEquiv (lift fâ‚ fâ‚‚ â‰« Î±.homEquiv.symm (Î±.homEquiv (fst X X) *
          Î±.homEquiv (snd X X))) = Î±.homEquiv fâ‚ * Î±.homEquiv fâ‚‚
        simp only [Î±.homEquiv_comp, Equiv.apply_symm_apply,
          Functor.comp_map, ConcreteCategory.forget_map_eq_coe, map_mul]
        simp only [â† Functor.comp_map, â† ConcreteCategory.forget_map_eq_coe, â† Î±.homEquiv_comp]
        simp }) (fun Ï† â†¦ MonCat.hom_ext (MonoidHom.ext (Î±.homEquiv_comp Ï†.unop)))

/-- The yoneda embedding of `Mon_C` into presheaves of monoids. -/
@[simps]
def yonedaMon : Mon C â¥¤ Cáµ’áµ– â¥¤ MonCat.{v} where
  obj M := yonedaMonObj M.X
  map {M N} Ïˆ :=
  { app Y := MonCat.ofHom
      { toFun := (Â· â‰« Ïˆ.hom)
        map_one' := by simp [Hom.one_def, Hom.one_def]
        map_mul' Ï†â‚ Ï†â‚‚ := by simp [Hom.mul_def] }
    naturality {M N} Ï† := MonCat.hom_ext <| MonoidHom.ext fun f â†¦ Category.assoc Ï†.unop f Ïˆ.hom }
  map_id M := NatTrans.ext <| funext fun _ â†¦ MonCat.hom_ext <| MonoidHom.ext Category.comp_id
  map_comp _ _ :=
    NatTrans.ext <| funext fun _ â†¦ MonCat.hom_ext <| MonoidHom.ext (.symm <| Category.assoc Â· _ _)

@[reassoc]
lemma yonedaMon_naturality (Î± : yonedaMonObj M âŸ¶ yonedaMonObj N) (f : X âŸ¶ Y) (g : Y âŸ¶ M) :
      Î±.app _ (f â‰« g) = f â‰« Î±.app _ g := congr($(Î±.naturality f.op) g)

variable (M) in
/-- If `M` is a monoid object, then `Hom(-, M)` as a presheaf of monoids is represented by `M`. -/
def yonedaMonObjRepresentableBy : (yonedaMonObj M â‹™ forget _).RepresentableBy M :=
  Functor.representableByEquiv.symm (.refl _)

variable (M) in
lemma MonObj.ofRepresentableBy_yonedaMonObjRepresentableBy :
    ofRepresentableBy M _ (yonedaMonObjRepresentableBy M) = â€¹_â€º := by
  ext; change lift (fst M M) (snd M M) â‰« Î¼ = Î¼; rw [lift_fst_snd, Category.id_comp]

@[deprecated (since := "2025-09-09")]
alias Mon_Class.ofRepresentableBy_yonedaMonObjRepresentableBy :=
  MonObj.ofRepresentableBy_yonedaMonObjRepresentableBy

@[deprecated (since := "2025-03-07")]
alias MonObjOfRepresentableBy_yonedaMonObjRepresentableBy :=
  MonObj.ofRepresentableBy_yonedaMonObjRepresentableBy

@[deprecated (since := "2025-09-09")]
alias Mon_ClassOfRepresentableBy_yonedaMonObjRepresentableBy :=
  MonObjOfRepresentableBy_yonedaMonObjRepresentableBy

/-- The yoneda embedding for `Mon_C` is fully faithful. -/
def yonedaMonFullyFaithful : yonedaMon (C := C).FullyFaithful where
  preimage {M N} Î± :=
    { hom := Î±.app (op M.X) (ğŸ™ M.X)
      isMonHom_hom.one_hom := by
          dsimp only [yonedaMon_obj] at Î± âŠ¢
          rw [â† yonedaMon_naturality, Category.comp_id,
            â† Category.id_comp Î·[M.X], toUnit_unique (ğŸ™ _) (toUnit _),
            â† Category.id_comp Î·[N.X], toUnit_unique (ğŸ™ _) (toUnit _)]
          exact (Î±.app _).hom.map_one
      isMonHom_hom.mul_hom := by
        dsimp only [yonedaMon_obj] at Î± âŠ¢
        rw [â† yonedaMon_naturality, Category.comp_id, â† Category.id_comp Î¼[M.X], â† lift_fst_snd]
        refine ((Î±.app _).hom.map_mul _ _).trans ?_
        change lift _ _ â‰« Î¼[N.X] = _
        congr 1
        ext <;> simp only [lift_fst, tensorHom_fst, lift_snd, tensorHom_snd,
          â† yonedaMon_naturality, Category.comp_id] }
  map_preimage {M N} Î± := by
    ext Y f
    dsimp only [yonedaMon_obj, yonedaMon_map_app, MonCat.hom_ofHom]
    simp_rw [â† yonedaMon_naturality]
    simp
  preimage_map Ï† := Mon.Hom.ext (Category.id_comp Ï†.hom)

instance : yonedaMon (C := C).Full := yonedaMonFullyFaithful.full
instance : yonedaMon (C := C).Faithful := yonedaMonFullyFaithful.faithful

lemma essImage_yonedaMon :
    yonedaMon (C := C).essImage = (Â· â‹™ forget _) â»Â¹' setOf Functor.IsRepresentable := by
  ext F
  constructor
  Â· rintro âŸ¨M, âŸ¨Î±âŸ©âŸ©
    exact âŸ¨M.X, âŸ¨Functor.representableByEquiv.symm (Functor.isoWhiskerRight Î± (forget _))âŸ©âŸ©
  Â· rintro âŸ¨X, âŸ¨eâŸ©âŸ©
    letI := MonObj.ofRepresentableBy X F e
    exact âŸ¨Mon.mk X, âŸ¨yonedaMonObjIsoOfRepresentableBy X F eâŸ©âŸ©

@[reassoc (attr := simp)]
lemma MonObj.one_comp (f : M âŸ¶ N) [IsMonHom f] : (1 : X âŸ¶ M) â‰« f = 1 := by simp [Hom.one_def]

@[deprecated (since := "2025-09-09")] alias Mon_Class.one_comp := MonObj.one_comp

@[reassoc]
lemma MonObj.mul_comp (fâ‚ fâ‚‚ : X âŸ¶ M) (g : M âŸ¶ N) [IsMonHom g] :
    (fâ‚ * fâ‚‚) â‰« g = fâ‚ â‰« g * fâ‚‚ â‰« g := by simp [Hom.mul_def]

@[deprecated (since := "2025-09-09")] alias Mon_Class.mul_comp := MonObj.mul_comp

@[reassoc]
lemma MonObj.pow_comp (f : X âŸ¶ M) (n : â„•) (g : M âŸ¶ N) [IsMonHom g] :
    (f ^ n) â‰« g = (f â‰« g) ^ n := by
  induction n <;> simp [pow_succ, MonObj.mul_comp, *]

@[deprecated (since := "2025-09-09")] alias Mon_Class.pow_comp := MonObj.pow_comp

@[reassoc (attr := simp)]
lemma MonObj.comp_one (f : X âŸ¶ Y) : f â‰« (1 : Y âŸ¶ M) = 1 :=
  ((yonedaMon.obj <| .mk M).map f.op).hom.map_one

@[deprecated (since := "2025-09-09")] alias Mon_Class.comp_one := MonObj.comp_one

@[reassoc]
lemma MonObj.comp_mul (f : X âŸ¶ Y) (gâ‚ gâ‚‚ : Y âŸ¶ M) : f â‰« (gâ‚ * gâ‚‚) = f â‰« gâ‚ * f â‰« gâ‚‚ :=
  ((yonedaMon.obj <| .mk M).map f.op).hom.map_mul _ _

@[deprecated (since := "2025-09-09")] alias Mon_Class.comp_mul := MonObj.comp_mul

@[reassoc]
lemma MonObj.comp_pow (f : X âŸ¶ M) (n : â„•) (h : Y âŸ¶ X) : h â‰« f ^ n = (h â‰« f) ^ n := by
  induction n <;> simp [pow_succ, MonObj.comp_mul, *]

@[deprecated (since := "2025-09-09")] alias Mon_Class.comp_pow := MonObj.comp_pow

variable (M) in
lemma MonObj.one_eq_one : Î· = (1 : _ âŸ¶ M) :=
  show _ = _ â‰« _ by rw [toUnit_unique (toUnit _) (ğŸ™ _), Category.id_comp]

@[deprecated (since := "2025-09-09")] alias Mon_Class.one_eq_one := MonObj.one_eq_one

variable (M) in
lemma MonObj.mul_eq_mul : Î¼ = fst M M * snd _ _ :=
  show _ = _ â‰« _ by rw [lift_fst_snd, Category.id_comp]

@[deprecated (since := "2025-09-09")] alias Mon_Class.mul_eq_mul := MonObj.mul_eq_mul
