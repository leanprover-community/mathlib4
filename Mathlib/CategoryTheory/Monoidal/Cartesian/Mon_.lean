/-
Copyright (c) 2025 Markus Himmel, Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel, Andrew Yang
-/
import Mathlib.Algebra.Category.MonCat.Limits
import Mathlib.CategoryTheory.Monoidal.Cartesian.Basic
import Mathlib.CategoryTheory.Monoidal.Mon_

/-!
# Yoneda embedding of `Mon C`

We show that monoid objects in Cartesian monoidal categories are exactly those whose yoneda presheaf
is a presheaf of monoids, by constructing the yoneda embedding `Mon C ‚•§ C·µí·µñ ‚•§ MonCat.{v}` and
showing that it is fully faithful and its (essential) image is the representable functors.
-/

open CategoryTheory MonoidalCategory Limits Opposite CartesianMonoidalCategory MonObj

namespace CategoryTheory
universe w v u
variable {C D : Type*} [Category.{v} C] [CartesianMonoidalCategory C]
  [Category.{w} D] [CartesianMonoidalCategory D]
  {M N O X Y : C} [MonObj M] [MonObj N] [MonObj O]

namespace MonObj

instance : IsMonHom (toUnit M) where

instance : IsMonHom Œ∑[M] where
  mul_hom := by simp [toUnit_unique (œÅ_ (ùüô_ C)).hom (Œª_ (ùüô_ C)).hom]

theorem lift_lift_assoc {A : C} {B : C} [MonObj B] (f g h : A ‚ü∂ B) :
    lift (lift f g ‚â´ Œº) h ‚â´ Œº = lift f (lift g h ‚â´ Œº) ‚â´ Œº := by
  have := lift (lift f g) h ‚â´= mul_assoc B
  rwa [lift_whiskerRight_assoc, lift_lift_associator_hom_assoc, lift_whiskerLeft_assoc] at this

@[reassoc (attr := simp)]
theorem lift_comp_one_left {A : C} {B : C} [MonObj B] (f : A ‚ü∂ ùüô_ C) (g : A ‚ü∂ B) :
    lift (f ‚â´ Œ∑) g ‚â´ Œº = g := by
  have := lift f g ‚â´= one_mul B
  rwa [lift_whiskerRight_assoc, lift_leftUnitor_hom] at this

@[reassoc (attr := simp)]
theorem lift_comp_one_right {A : C} {B : C} [MonObj B] (f : A ‚ü∂ B) (g : A ‚ü∂ ùüô_ C) :
    lift f (g ‚â´ Œ∑) ‚â´ Œº = f := by
  have := lift f g ‚â´= mul_one B
  rwa [lift_whiskerLeft_assoc, lift_rightUnitor_hom] at this

variable [BraidedCategory C]

attribute [local simp] tensorObj.one_def tensorObj.mul_def

instance : IsMonHom (fst M N) where
instance : IsMonHom (snd M N) where

instance {f : M ‚ü∂ N} {g : M ‚ü∂ O} [IsMonHom f] [IsMonHom g] : IsMonHom (lift f g) where

instance [IsCommMonObj M] : IsMonHom Œº[M] where
  one_hom := by simp [toUnit_unique (œÅ_ (ùüô_ C)).hom (Œª_ (ùüô_ C)).hom]

end MonObj

namespace Mon
variable [BraidedCategory C]

attribute [local simp] tensorObj.one_def tensorObj.mul_def in
instance : CartesianMonoidalCategory (Mon C) where
  isTerminalTensorUnit := .ofUniqueHom (fun M ‚Ü¶ ‚ü®toUnit _‚ü©) fun M f ‚Ü¶ by ext; exact toUnit_unique ..
  fst M N := .mk (fst M.X N.X)
  snd M N := .mk (snd M.X N.X)
  tensorProductIsBinaryProduct M N :=
    BinaryFan.IsLimit.mk _ (fun {T} f g ‚Ü¶ ‚ü®lift f.hom g.hom‚ü©)
      (by aesop_cat) (by aesop_cat) (by aesop_cat)
  fst_def M N := by ext; simp [fst_def]; congr
  snd_def M N := by ext; simp [snd_def]; congr

variable {M N N‚ÇÅ N‚ÇÇ : Mon C}

@[simp] lemma lift_hom (f : M ‚ü∂ N‚ÇÅ) (g : M ‚ü∂ N‚ÇÇ) : (lift f g).hom = lift f.hom g.hom := rfl
@[simp] lemma fst_hom (M N : Mon C) : (fst M N).hom = fst M.X N.X := rfl
@[simp] lemma snd_hom (M N : Mon C) : (snd M N).hom = snd M.X N.X := rfl

/-! ### Comm monoid objects are internal monoid objects -/

/-- A commutative monoid object is a monoid object in the category of monoid objects. -/
instance [IsCommMonObj M.X] : MonObj M where
  one := .mk Œ∑[M.X]
  mul := .mk Œº[M.X]

@[simp] lemma hom_Œ∑ (M : Mon C) [IsCommMonObj M.X] : Œ∑[M].hom = Œ∑[M.X] := rfl
@[simp] lemma hom_Œº (M : Mon C) [IsCommMonObj M.X] : Œº[M].hom = Œº[M.X] := rfl

/-- A commutative monoid object is a commutative monoid object in the category of monoid objects. -/
instance [IsCommMonObj M.X] : IsCommMonObj M where

end Mon

variable (X) in
/-- If `X` represents a presheaf of monoids, then `X` is a monoid object. -/
@[simps]
def MonObj.ofRepresentableBy (F : C·µí·µñ ‚•§ MonCat.{w}) (Œ± : (F ‚ãô forget _).RepresentableBy X) :
    MonObj X where
  one := Œ±.homEquiv.symm 1
  mul := Œ±.homEquiv.symm (Œ±.homEquiv (fst X X) * Œ±.homEquiv (snd X X))
  one_mul := by
    apply Œ±.homEquiv.injective
    simp only [Œ±.homEquiv_comp, Equiv.apply_symm_apply]
    simp only [Functor.comp_map, ConcreteCategory.forget_map_eq_coe, map_mul]
    simp only [‚Üê ConcreteCategory.forget_map_eq_coe, ‚Üê Functor.comp_map, ‚Üê Œ±.homEquiv_comp]
    simp only [whiskerRight_fst, whiskerRight_snd]
    simp only [Œ±.homEquiv_comp, Equiv.apply_symm_apply]
    simp [leftUnitor_hom]
  mul_one := by
    apply Œ±.homEquiv.injective
    simp only [Œ±.homEquiv_comp, Equiv.apply_symm_apply]
    simp only [Functor.comp_map, ConcreteCategory.forget_map_eq_coe, map_mul]
    simp only [‚Üê ConcreteCategory.forget_map_eq_coe, ‚Üê Functor.comp_map, ‚Üê Œ±.homEquiv_comp]
    simp only [whiskerLeft_fst, whiskerLeft_snd]
    simp only [Œ±.homEquiv_comp, Equiv.apply_symm_apply]
    simp [rightUnitor_hom]
  mul_assoc := by
    apply Œ±.homEquiv.injective
    simp only [Œ±.homEquiv_comp, Equiv.apply_symm_apply]
    simp only [Functor.comp_map, ConcreteCategory.forget_map_eq_coe, map_mul]
    simp only [‚Üê ConcreteCategory.forget_map_eq_coe, ‚Üê Functor.comp_map, ‚Üê Œ±.homEquiv_comp]
    simp only [whiskerRight_fst, whiskerRight_snd, whiskerLeft_fst,
      associator_hom_fst, whiskerLeft_snd]
    simp only [Œ±.homEquiv_comp, Equiv.apply_symm_apply]
    simp only [Functor.comp_map, ConcreteCategory.forget_map_eq_coe, map_mul, _root_.mul_assoc]
    simp only [‚Üê ConcreteCategory.forget_map_eq_coe, ‚Üê Functor.comp_map, ‚Üê Œ±.homEquiv_comp]
    simp

@[deprecated (since := "2025-09-09")] alias Mon_Class.ofRepresentableBy := MonObj.ofRepresentableBy

@[deprecated (since := "2025-03-07")]
alias MonObjOfRepresentableBy := MonObj.ofRepresentableBy

@[deprecated (since := "2025-09-09")] alias Mon_ClassOfRepresentableBy := MonObj.ofRepresentableBy

/-- If `M` is a monoid object, then `Hom(X, M)` has a monoid structure. -/
abbrev Hom.monoid : Monoid (X ‚ü∂ M) where
  mul f‚ÇÅ f‚ÇÇ := lift f‚ÇÅ f‚ÇÇ ‚â´ Œº
  mul_assoc f‚ÇÅ f‚ÇÇ f‚ÇÉ := by
    change lift (lift f‚ÇÅ f‚ÇÇ ‚â´ Œº) f‚ÇÉ ‚â´ Œº = lift f‚ÇÅ (lift f‚ÇÇ f‚ÇÉ ‚â´ Œº) ‚â´ Œº
    trans lift (lift f‚ÇÅ f‚ÇÇ) f‚ÇÉ ‚â´ Œº ‚ñ∑ M ‚â´ Œº
    ¬∑ rw [‚Üê tensorHom_id, lift_map_assoc, Category.comp_id]
    trans lift f‚ÇÅ (lift f‚ÇÇ f‚ÇÉ) ‚â´ M ‚óÅ Œº ‚â´ Œº
    ¬∑ rw [MonObj.mul_assoc]
      simp_rw [‚Üê Category.assoc]
      congr 2
      ext <;> simp
    ¬∑ rw [‚Üê id_tensorHom, lift_map_assoc, Category.comp_id]
  one := toUnit X ‚â´ Œ∑
  one_mul f := by
    change lift (toUnit _ ‚â´ Œ∑) f ‚â´ Œº = f
    rw [‚Üê Category.comp_id f, ‚Üê lift_map_assoc, tensorHom_id, MonObj.one_mul,
      Category.comp_id, leftUnitor_hom]
    exact lift_snd _ _
  mul_one f := by
    change lift f (toUnit _ ‚â´ Œ∑) ‚â´ Œº = f
    rw [‚Üê Category.comp_id f, ‚Üê lift_map_assoc, id_tensorHom, MonObj.mul_one,
      Category.comp_id, rightUnitor_hom]
    exact lift_fst _ _

scoped[CategoryTheory.MonObj] attribute [instance] Hom.monoid

lemma Hom.one_def : (1 : X ‚ü∂ M) = toUnit X ‚â´ Œ∑ := rfl
lemma Hom.mul_def (f‚ÇÅ f‚ÇÇ : X ‚ü∂ M) : f‚ÇÅ * f‚ÇÇ = lift f‚ÇÅ f‚ÇÇ ‚â´ Œº := rfl

namespace Functor
variable (F : C ‚•§ D) [F.Monoidal]

open scoped Obj

protected lemma map_mul (f g : X ‚ü∂ M) : F.map (f * g) = F.map f * F.map g := by
  simp only [Hom.mul_def, map_comp, obj.Œº_def, ‚Üê Category.assoc]
  congr 1
  rw [‚Üê IsIso.comp_inv_eq]
  ext <;> simp

@[simp] protected lemma map_one : F.map (1 : X ‚ü∂ M) = 1 := by simp [Hom.one_def]

/-- `Functor.map` of a monoidal functor as a `MonoidHom`. -/
@[simps]
def homMonoidHom : (X ‚ü∂ M) ‚Üí* (F.obj X ‚ü∂ F.obj M) where
  toFun := F.map
  map_one' := F.map_one
  map_mul' := F.map_mul

/-- `Functor.map` of a fully faithful monoidal functor as a `MulEquiv`. -/
@[simps!]
def FullyFaithful.homMulEquiv (hF : F.FullyFaithful) : (X ‚ü∂ M) ‚âÉ* (F.obj X ‚ü∂ F.obj M) where
  __ := hF.homEquiv
  __ := F.homMonoidHom

end Functor

section BraidedCategory
variable [BraidedCategory C]

/-- If `M` is a commutative monoid object, then `Hom(X, M)` has a commutative monoid structure. -/
abbrev Hom.commMonoid [IsCommMonObj M] : CommMonoid (X ‚ü∂ M) where
  mul_comm f g := by simpa [-IsCommMonObj.mul_comm] using lift g f ‚â´= IsCommMonObj.mul_comm M

namespace Mon.Hom
variable {M N : Mon C} [IsCommMonObj N.X]

@[simp] lemma hom_one : (1 : M ‚ü∂ N).hom = 1 := rfl
@[simp] lemma hom_mul (f g : M ‚ü∂ N) : (f * g).hom = f.hom * g.hom := rfl
@[simp] lemma hom_pow (f : M ‚ü∂ N) (n : ‚Ñï) : (f ^ n).hom = f.hom ^ n := by
  induction n <;> simp [pow_succ, *]

end Mon.Hom

scoped[CategoryTheory.MonObj] attribute [instance] Hom.commMonoid

end BraidedCategory

variable (M) in
/-- If `M` is a monoid object, then `Hom(-, M)` is a presheaf of monoids. -/
@[simps]
def yonedaMonObj : C·µí·µñ ‚•§ MonCat.{v} where
  obj X := MonCat.of (unop X ‚ü∂ M)
  map {X Y‚ÇÇ} œÜ := MonCat.ofHom
    { toFun := (œÜ.unop ‚â´ ¬∑)
      map_one' := by
        change œÜ.unop ‚â´ toUnit _ ‚â´ Œ∑ = toUnit _ ‚â´ Œ∑
        rw [‚Üê Category.assoc, toUnit_unique (œÜ.unop ‚â´ toUnit _)]
      map_mul' f‚ÇÅ f‚ÇÇ := by
        change œÜ.unop ‚â´ lift f‚ÇÅ f‚ÇÇ ‚â´ Œº = lift (œÜ.unop ‚â´ f‚ÇÅ) (œÜ.unop ‚â´ f‚ÇÇ) ‚â´ Œº
        rw [‚Üê Category.assoc]
        cat_disch }
  map_id _ := MonCat.hom_ext (MonoidHom.ext Category.id_comp)
  map_comp _ _ := MonCat.hom_ext (MonoidHom.ext (Category.assoc _ _))

variable (X) in
/-- If `X` represents a presheaf of monoids `F`, then `Hom(-, X)` is isomorphic to `F` as
a presheaf of monoids. -/
@[simps!]
def yonedaMonObjIsoOfRepresentableBy
    (F : C·µí·µñ ‚•§ MonCat.{v}) (Œ± : (F ‚ãô forget _).RepresentableBy X) :
    letI := MonObj.ofRepresentableBy X F Œ±
    yonedaMonObj X ‚âÖ F :=
  letI := MonObj.ofRepresentableBy X F Œ±
  NatIso.ofComponents (fun Y ‚Ü¶ MulEquiv.toMonCatIso
    { toEquiv := Œ±.homEquiv
      map_mul' f‚ÇÅ f‚ÇÇ := by
        change Œ±.homEquiv (lift f‚ÇÅ f‚ÇÇ ‚â´ Œ±.homEquiv.symm (Œ±.homEquiv (fst X X) *
          Œ±.homEquiv (snd X X))) = Œ±.homEquiv f‚ÇÅ * Œ±.homEquiv f‚ÇÇ
        simp only [Œ±.homEquiv_comp, Equiv.apply_symm_apply,
          Functor.comp_map, ConcreteCategory.forget_map_eq_coe, map_mul]
        simp only [‚Üê Functor.comp_map, ‚Üê ConcreteCategory.forget_map_eq_coe, ‚Üê Œ±.homEquiv_comp]
        simp }) (fun œÜ ‚Ü¶ MonCat.hom_ext (MonoidHom.ext (Œ±.homEquiv_comp œÜ.unop)))

/-- The yoneda embedding of `Mon_C` into presheaves of monoids. -/
@[simps]
def yonedaMon : Mon C ‚•§ C·µí·µñ ‚•§ MonCat.{v} where
  obj M := yonedaMonObj M.X
  map {M N} œà :=
  { app Y := MonCat.ofHom
      { toFun := (¬∑ ‚â´ œà.hom)
        map_one' := by simp [Hom.one_def, Hom.one_def]
        map_mul' œÜ‚ÇÅ œÜ‚ÇÇ := by simp [Hom.mul_def] }
    naturality {M N} œÜ := MonCat.hom_ext <| MonoidHom.ext fun f ‚Ü¶ Category.assoc œÜ.unop f œà.hom }
  map_id M := NatTrans.ext <| funext fun _ ‚Ü¶ MonCat.hom_ext <| MonoidHom.ext Category.comp_id
  map_comp _ _ :=
    NatTrans.ext <| funext fun _ ‚Ü¶ MonCat.hom_ext <| MonoidHom.ext (.symm <| Category.assoc ¬∑ _ _)

@[reassoc]
lemma yonedaMon_naturality (Œ± : yonedaMonObj M ‚ü∂ yonedaMonObj N) (f : X ‚ü∂ Y) (g : Y ‚ü∂ M) :
      Œ±.app _ (f ‚â´ g) = f ‚â´ Œ±.app _ g := congr($(Œ±.naturality f.op) g)

variable (M) in
/-- If `M` is a monoid object, then `Hom(-, M)` as a presheaf of monoids is represented by `M`. -/
def yonedaMonObjRepresentableBy : (yonedaMonObj M ‚ãô forget _).RepresentableBy M :=
  Functor.representableByEquiv.symm (.refl _)

variable (M) in
lemma MonObj.ofRepresentableBy_yonedaMonObjRepresentableBy :
    ofRepresentableBy M _ (yonedaMonObjRepresentableBy M) = ‚Äπ_‚Ä∫ := by
  ext; change lift (fst M M) (snd M M) ‚â´ Œº = Œº; rw [lift_fst_snd, Category.id_comp]

@[deprecated (since := "2025-09-09")]
alias Mon_Class.ofRepresentableBy_yonedaMonObjRepresentableBy :=
  MonObj.ofRepresentableBy_yonedaMonObjRepresentableBy

@[deprecated (since := "2025-03-07")]
alias MonObjOfRepresentableBy_yonedaMonObjRepresentableBy :=
  MonObj.ofRepresentableBy_yonedaMonObjRepresentableBy

@[deprecated (since := "2025-09-09")]
alias Mon_ClassOfRepresentableBy_yonedaMonObjRepresentableBy :=
  MonObj.ofRepresentableBy_yonedaMonObjRepresentableBy

/-- The yoneda embedding for `Mon_C` is fully faithful. -/
def yonedaMonFullyFaithful : yonedaMon (C := C).FullyFaithful where
  preimage {M N} Œ± :=
    { hom := Œ±.app (op M.X) (ùüô M.X)
      isMonHom_hom.one_hom := by
          dsimp only [yonedaMon_obj] at Œ± ‚ä¢
          rw [‚Üê yonedaMon_naturality, Category.comp_id,
            ‚Üê Category.id_comp Œ∑[M.X], toUnit_unique (ùüô _) (toUnit _),
            ‚Üê Category.id_comp Œ∑[N.X], toUnit_unique (ùüô _) (toUnit _)]
          exact (Œ±.app _).hom.map_one
      isMonHom_hom.mul_hom := by
        dsimp only [yonedaMon_obj] at Œ± ‚ä¢
        rw [‚Üê yonedaMon_naturality, Category.comp_id, ‚Üê Category.id_comp Œº[M.X], ‚Üê lift_fst_snd]
        refine ((Œ±.app _).hom.map_mul _ _).trans ?_
        change lift _ _ ‚â´ Œº[N.X] = _
        congr 1
        ext <;> simp only [lift_fst, tensorHom_fst, lift_snd, tensorHom_snd,
          ‚Üê yonedaMon_naturality, Category.comp_id] }
  map_preimage {M N} Œ± := by
    ext Y f
    dsimp only [yonedaMon_obj, yonedaMon_map_app, MonCat.hom_ofHom]
    simp_rw [‚Üê yonedaMon_naturality]
    simp
  preimage_map œÜ := Mon.Hom.ext (Category.id_comp œÜ.hom)

instance : yonedaMon (C := C).Full := yonedaMonFullyFaithful.full
instance : yonedaMon (C := C).Faithful := yonedaMonFullyFaithful.faithful

lemma essImage_yonedaMon :
    yonedaMon (C := C).essImage = (¬∑ ‚ãô forget _) ‚Åª¬π' setOf Functor.IsRepresentable := by
  ext F
  constructor
  ¬∑ rintro ‚ü®M, ‚ü®Œ±‚ü©‚ü©
    exact ‚ü®M.X, ‚ü®Functor.representableByEquiv.symm (Functor.isoWhiskerRight Œ± (forget _))‚ü©‚ü©
  ¬∑ rintro ‚ü®X, ‚ü®e‚ü©‚ü©
    letI := MonObj.ofRepresentableBy X F e
    exact ‚ü®Mon.mk X, ‚ü®yonedaMonObjIsoOfRepresentableBy X F e‚ü©‚ü©

@[reassoc (attr := simp)]
lemma MonObj.one_comp (f : M ‚ü∂ N) [IsMonHom f] : (1 : X ‚ü∂ M) ‚â´ f = 1 := by simp [Hom.one_def]

@[deprecated (since := "2025-09-09")] alias Mon_Class.one_comp := MonObj.one_comp

@[reassoc]
lemma MonObj.mul_comp (f‚ÇÅ f‚ÇÇ : X ‚ü∂ M) (g : M ‚ü∂ N) [IsMonHom g] :
    (f‚ÇÅ * f‚ÇÇ) ‚â´ g = f‚ÇÅ ‚â´ g * f‚ÇÇ ‚â´ g := by simp [Hom.mul_def]

@[deprecated (since := "2025-09-09")] alias Mon_Class.mul_comp := MonObj.mul_comp

@[reassoc]
lemma MonObj.pow_comp (f : X ‚ü∂ M) (n : ‚Ñï) (g : M ‚ü∂ N) [IsMonHom g] :
    (f ^ n) ‚â´ g = (f ‚â´ g) ^ n := by
  induction n <;> simp [pow_succ, MonObj.mul_comp, *]

@[deprecated (since := "2025-09-09")] alias Mon_Class.pow_comp := MonObj.pow_comp

@[reassoc (attr := simp)]
lemma MonObj.comp_one (f : X ‚ü∂ Y) : f ‚â´ (1 : Y ‚ü∂ M) = 1 :=
  ((yonedaMon.obj <| .mk M).map f.op).hom.map_one

@[deprecated (since := "2025-09-09")] alias Mon_Class.comp_one := MonObj.comp_one

@[reassoc]
lemma MonObj.comp_mul (f : X ‚ü∂ Y) (g‚ÇÅ g‚ÇÇ : Y ‚ü∂ M) : f ‚â´ (g‚ÇÅ * g‚ÇÇ) = f ‚â´ g‚ÇÅ * f ‚â´ g‚ÇÇ :=
  ((yonedaMon.obj <| .mk M).map f.op).hom.map_mul _ _

@[deprecated (since := "2025-09-09")] alias Mon_Class.comp_mul := MonObj.comp_mul

@[reassoc]
lemma MonObj.comp_pow (f : X ‚ü∂ M) (n : ‚Ñï) (h : Y ‚ü∂ X) : h ‚â´ f ^ n = (h ‚â´ f) ^ n := by
  induction n <;> simp [pow_succ, MonObj.comp_mul, *]

@[deprecated (since := "2025-09-09")] alias Mon_Class.comp_pow := MonObj.comp_pow

variable (M) in
lemma MonObj.one_eq_one : Œ∑ = (1 : _ ‚ü∂ M) :=
  show _ = _ ‚â´ _ by rw [toUnit_unique (toUnit _) (ùüô _), Category.id_comp]

@[deprecated (since := "2025-09-09")] alias Mon_Class.one_eq_one := MonObj.one_eq_one

variable (M) in
lemma MonObj.mul_eq_mul : Œº = fst M M * snd _ _ :=
  show _ = _ ‚â´ _ by rw [lift_fst_snd, Category.id_comp]

@[deprecated (since := "2025-09-09")] alias Mon_Class.mul_eq_mul := MonObj.mul_eq_mul

namespace Hom

/-- If `M` and `N` are isomorphic as monoid objects, then `X ‚ü∂ M` and `X ‚ü∂ N` are isomorphic
monoids. -/
@[simps!]
def mulEquivCongrRight (e : M ‚âÖ N) [IsMonHom e.hom] (X : C) : (X ‚ü∂ M) ‚âÉ* (X ‚ü∂ N) :=
  ((yonedaMon.mapIso <| Mon.mkIso' e).app <| .op X).monCatIsoToMulEquiv

end Hom
end CategoryTheory
