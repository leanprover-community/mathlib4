/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.Algebra.Category.Grp.Limits
import Mathlib.CategoryTheory.Monoidal.Grp_

/-!
# Yoneda embedding of `Grp_ C`

We show that group objects are exactly those whose yoneda presheaf is a presheaf of groups,
by constructing the yoneda embedding `Grp_ C â¥¤ Cáµ’áµ– â¥¤ Grp.{v}` and
showing that it is fully faithful and its (essential) image is the representable functors.
-/

open CategoryTheory MonoidalCategory Limits Opposite CartesianMonoidalCategory Mon_Class

universe w v u
variable {C : Type u} [Category.{v} C] [CartesianMonoidalCategory C]
  {G H X Y : C} [Grp_Class G] [Grp_Class H]

variable (X) in
/-- If `X` represents a presheaf of monoids, then `X` is a monoid object. -/
def Grp_Class.ofRepresentableBy (F : Cáµ’áµ– â¥¤ Grp.{w}) (Î± : (F â‹™ forget _).RepresentableBy X) :
    Grp_Class X where
  __ := Mon_Class.ofRepresentableBy X (F â‹™ forgetâ‚‚ Grp MonCat) Î±
  inv := Î±.homEquiv.symm (Î±.homEquiv (ğŸ™ _))â»Â¹
  left_inv' := by
    change lift (Î±.homEquiv.symm (Î±.homEquiv (ğŸ™ X))â»Â¹) (ğŸ™ X) â‰«
      Î±.homEquiv.symm (Î±.homEquiv (fst X X) * Î±.homEquiv (snd X X)) =
        toUnit X â‰« Î±.homEquiv.symm 1
    apply Î±.homEquiv.injective
    simp only [Î±.homEquiv_comp, Equiv.apply_symm_apply]
    simp only [Functor.comp_map, ConcreteCategory.forget_map_eq_coe, map_one, map_mul]
    simp only [â† ConcreteCategory.forget_map_eq_coe, â† Functor.comp_map, â† Î±.homEquiv_comp]
    simp [â† Functor.comp_obj]
  right_inv' := by
    change lift (ğŸ™ X) (Î±.homEquiv.symm (Î±.homEquiv (ğŸ™ X))â»Â¹) â‰«
      Î±.homEquiv.symm (Î±.homEquiv (fst X X) * Î±.homEquiv (snd X X)) =
        toUnit X â‰« Î±.homEquiv.symm 1
    apply Î±.homEquiv.injective
    simp only [Î±.homEquiv_comp, Equiv.apply_symm_apply]
    simp only [Functor.comp_map, ConcreteCategory.forget_map_eq_coe, map_one, map_mul]
    simp only [â† ConcreteCategory.forget_map_eq_coe, â† Functor.comp_map, â† Î±.homEquiv_comp]
    simp [â† Functor.comp_obj]

/-- If `G` is a group object, then `Hom(X, G)` has a group structure. -/
abbrev Hom.group : Group (X âŸ¶ G) where
  __ := monoid
  inv f := f â‰« Î¹
  inv_mul_cancel f := calc
    lift (f â‰« Î¹) f â‰« Î¼
    _ = (f â‰« lift Î¹ (ğŸ™ G)) â‰« Î¼ := by simp
    _ = toUnit X â‰« Î· := by rw [Category.assoc]; simp

attribute [local instance] Hom.group

lemma Hom.inv_def (f : X âŸ¶ G) : fâ»Â¹ = f â‰« Î¹ := rfl

variable (G) in
/-- If `G` is a group object, then `Hom(-, G)` is a presheaf of groups. -/
@[simps]
def yonedaGrpObj : Cáµ’áµ– â¥¤ Grp.{v} where
  obj X := Grp.of (unop X âŸ¶ G)
  map Ï† := Grp.ofHom ((yonedaMonObj G).map Ï†).hom

variable (G) in
/-- If `G` is a monoid object, then `Hom(-, G)` as a presheaf of monoids is represented by `G`. -/
def yonedaGrpObjRepresentableBy : (yonedaGrpObj G â‹™ forget _).RepresentableBy G :=
  Functor.representableByEquiv.symm (.refl _)

variable (G) in
lemma Grp_Class.ofRepresentableBy_yonedaGrpObjRepresentableBy :
    ofRepresentableBy G _ (yonedaGrpObjRepresentableBy G) = â€¹Grp_Class Gâ€º := by
  ext; show lift (fst G G) (snd G G) â‰« Î¼ = Î¼; rw [lift_fst_snd, Category.id_comp]

variable (X) in
/-- If `X` represents a presheaf of groups `F`, then `Hom(-, X)` is isomorphic to `F` as
a presheaf of groups. -/
@[simps! hom inv]
def yonedaGrpObjIsoOfRepresentableBy (F : Cáµ’áµ– â¥¤ Grp.{v}) (Î± : (F â‹™ forget _).RepresentableBy X) :
    letI := Grp_Class.ofRepresentableBy X F Î±
    yonedaGrpObj X â‰… F :=
  letI := Grp_Class.ofRepresentableBy X F Î±
  NatIso.ofComponents (fun Y â†¦ MulEquiv.toGrpIso
    { toEquiv := Î±.homEquiv
      map_mul' :=
  ((yonedaMonObjIsoOfRepresentableBy X (F â‹™ forgetâ‚‚ Grp MonCat) Î±).hom.app Y).hom.map_mul})
      fun Ï† â†¦ Grp.hom_ext <| MonoidHom.ext <| Î±.homEquiv_comp Ï†.unop

/-- The yoneda embedding of `Grp_C` into presheaves of groups. -/
@[simps]
def yonedaGrp : Grp_ C â¥¤ Cáµ’áµ– â¥¤ Grp.{v} where
  obj G := yonedaGrpObj G.X
  map {G H} Ïˆ := { app Y := Grp.ofHom ((yonedaMon.map Ïˆ).app Y).hom }

@[reassoc]
lemma yonedaGrp_naturality (Î± : yonedaGrpObj G âŸ¶ yonedaGrpObj H) (f : X âŸ¶ Y) (g : Y âŸ¶ G) :
    Î±.app _ (f â‰« g) = f â‰« Î±.app _ g := congr($(Î±.naturality f.op) g)

/-- The yoneda embedding for `Grp_C` is fully faithful. -/
def yonedaGrpFullyFaithful : yonedaGrp (C := C).FullyFaithful where
  preimage {G H} Î± := yonedaMonFullyFaithful.preimage (whiskerRight Î± (forgetâ‚‚ Grp MonCat))
  map_preimage {G H} Î± := by
    ext X : 3
    exact congr(($(yonedaMonFullyFaithful.map_preimage
      (whiskerRight Î± (forgetâ‚‚ Grp MonCat))).app X).hom)
  preimage_map := yonedaMonFullyFaithful.preimage_map

instance : yonedaGrp (C := C).Full := yonedaGrpFullyFaithful.full
instance : yonedaGrp (C := C).Faithful := yonedaGrpFullyFaithful.faithful

lemma essImage_yonedaGrp :
    yonedaGrp (C := C).essImage = (Â· â‹™ forget _) â»Â¹' setOf Functor.IsRepresentable := by
  ext F
  constructor
  Â· rintro âŸ¨G, âŸ¨Î±âŸ©âŸ©
    exact âŸ¨G.X, âŸ¨Functor.representableByEquiv.symm (isoWhiskerRight Î± (forget _))âŸ©âŸ©
  Â· rintro âŸ¨X, âŸ¨eâŸ©âŸ©
    letI := Grp_Class.ofRepresentableBy X F e
    exact âŸ¨.mk' X, âŸ¨yonedaGrpObjIsoOfRepresentableBy X F eâŸ©âŸ©

@[reassoc]
lemma Grp_Class.comp_inv (f : X âŸ¶ Y) (g : Y âŸ¶ G) : f â‰« gâ»Â¹ = (f â‰« g)â»Â¹ :=
  ((yonedaGrp.obj <| .mk' G).map f.op).hom.map_inv g

@[reassoc]
lemma Grp_Class.inv_comp (f : X âŸ¶ G) (g : G âŸ¶ H) [IsMon_Hom g] : fâ»Â¹ â‰« g = (f â‰« g)â»Â¹ := by
  simp [Hom.inv_def,IsMon_Hom.inv_hom]

lemma Grp_Class.inv_eq_inv : Î¹ = (ğŸ™ G)â»Â¹ := by simp [Hom.inv_def]
