/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.Algebra.Category.Grp.Limits
import Mathlib.CategoryTheory.Monoidal.Grp_

/-!
# Yoneda embedding of `Grp_ C`

We show that group objects are exactly those whose yoneda presheaf is a presheaf of groups,
by constructing the yoneda embedding `Grp_ C â¥¤ Cáµ’áµ– â¥¤ Grp.{v}` and
showing that it is fully faithful and its (essential) image is the representable functors.
-/

assert_not_exists Field

open CategoryTheory MonoidalCategory Limits Opposite CartesianMonoidalCategory MonObj

universe w v u
variable {C : Type u} [Category.{v} C] [CartesianMonoidalCategory C]
  {M G H X Y : C} [MonObj M] [GrpObj G] [GrpObj H]

/-- Construct a morphism `G âŸ¶ H` of `Grp_ C` C from a map `f : G âŸ¶ H` and a `IsMon_Hom f`
instance. -/
@[simps]
def Grp_.homMk (f : G âŸ¶ H) [IsMon_Hom f] : .mk G âŸ¶ Grp_.mk H := âŸ¨fâŸ©

variable (X) in
/-- If `X` represents a presheaf of monoids, then `X` is a monoid object. -/
def GrpObj.ofRepresentableBy (F : Cáµ’áµ– â¥¤ Grp.{w}) (Î± : (F â‹™ forget _).RepresentableBy X) :
    GrpObj X where
  __ := MonObj.ofRepresentableBy X (F â‹™ forgetâ‚‚ Grp MonCat) Î±
  inv := Î±.homEquiv.symm (Î±.homEquiv (ğŸ™ _))â»Â¹
  left_inv := by
    change lift (Î±.homEquiv.symm (Î±.homEquiv (ğŸ™ X))â»Â¹) (ğŸ™ X) â‰«
      Î±.homEquiv.symm (Î±.homEquiv (fst X X) * Î±.homEquiv (snd X X)) =
        toUnit X â‰« Î±.homEquiv.symm 1
    apply Î±.homEquiv.injective
    simp only [Î±.homEquiv_comp, Equiv.apply_symm_apply]
    simp only [Functor.comp_map, ConcreteCategory.forget_map_eq_coe, map_one, map_mul]
    simp only [â† ConcreteCategory.forget_map_eq_coe, â† Functor.comp_map, â† Î±.homEquiv_comp]
    simp [- Functor.comp_obj]
  right_inv := by
    change lift (ğŸ™ X) (Î±.homEquiv.symm (Î±.homEquiv (ğŸ™ X))â»Â¹) â‰«
      Î±.homEquiv.symm (Î±.homEquiv (fst X X) * Î±.homEquiv (snd X X)) =
        toUnit X â‰« Î±.homEquiv.symm 1
    apply Î±.homEquiv.injective
    simp only [Î±.homEquiv_comp, Equiv.apply_symm_apply]
    simp only [Functor.comp_map, ConcreteCategory.forget_map_eq_coe, map_one, map_mul]
    simp only [â† ConcreteCategory.forget_map_eq_coe, â† Functor.comp_map, â† Î±.homEquiv_comp]
    simp [- Functor.comp_obj]

@[deprecated (since := "2025-09-13")] alias Grp_Class.ofRepresentableBy := GrpObj.ofRepresentableBy

/-- If `G` is a group object, then `Hom(X, G)` has a group structure. -/
abbrev Hom.group : Group (X âŸ¶ G) where
  inv f := f â‰« Î¹
  inv_mul_cancel f := calc
    lift (f â‰« Î¹) f â‰« Î¼
    _ = (f â‰« lift Î¹ (ğŸ™ G)) â‰« Î¼ := by simp
    _ = toUnit X â‰« Î· := by rw [Category.assoc]; simp

scoped[MonObj] attribute [instance] Hom.group

lemma Hom.inv_def (f : X âŸ¶ G) : fâ»Â¹ = f â‰« Î¹ := rfl

variable (G) in
/-- If `G` is a group object, then `Hom(-, G)` is a presheaf of groups. -/
@[simps]
def yonedaGrpObj : Cáµ’áµ– â¥¤ Grp.{v} where
  obj X := Grp.of (unop X âŸ¶ G)
  map Ï† := Grp.ofHom ((yonedaMonObj G).map Ï†).hom

variable (G) in
/-- If `G` is a monoid object, then `Hom(-, G)` as a presheaf of monoids is represented by `G`. -/
def yonedaGrpObjRepresentableBy : (yonedaGrpObj G â‹™ forget _).RepresentableBy G :=
  Functor.representableByEquiv.symm (.refl _)

variable (G) in
lemma GrpObj.ofRepresentableBy_yonedaGrpObjRepresentableBy :
    ofRepresentableBy G _ (yonedaGrpObjRepresentableBy G) = â€¹GrpObj Gâ€º := by
  ext; change lift (fst G G) (snd G G) â‰« Î¼ = Î¼; rw [lift_fst_snd, Category.id_comp]

@[deprecated (since := "2025-09-13")]
alias Grp_Class.ofRepresentableBy_yonedaGrpObjRepresentableBy :=
  GrpObj.ofRepresentableBy_yonedaGrpObjRepresentableBy

variable (X) in
/-- If `X` represents a presheaf of groups `F`, then `Hom(-, X)` is isomorphic to `F` as
a presheaf of groups. -/
@[simps! hom inv]
def yonedaGrpObjIsoOfRepresentableBy (F : Cáµ’áµ– â¥¤ Grp.{v}) (Î± : (F â‹™ forget _).RepresentableBy X) :
    letI := GrpObj.ofRepresentableBy X F Î±
    yonedaGrpObj X â‰… F :=
  letI := GrpObj.ofRepresentableBy X F Î±
  NatIso.ofComponents (fun Y â†¦ MulEquiv.toGrpIso
    { toEquiv := Î±.homEquiv
      map_mul' :=
  ((yonedaMonObjIsoOfRepresentableBy X (F â‹™ forgetâ‚‚ Grp MonCat) Î±).hom.app Y).hom.map_mul})
      fun Ï† â†¦ Grp.hom_ext <| MonoidHom.ext <| Î±.homEquiv_comp Ï†.unop

/-- The yoneda embedding of `Grp_C` into presheaves of groups. -/
@[simps]
def yonedaGrp : Grp_ C â¥¤ Cáµ’áµ– â¥¤ Grp.{v} where
  obj G := yonedaGrpObj G.X
  map {G H} Ïˆ := { app Y := Grp.ofHom ((yonedaMon.map Ïˆ).app Y).hom }

@[reassoc]
lemma yonedaGrp_naturality (Î± : yonedaGrpObj G âŸ¶ yonedaGrpObj H) (f : X âŸ¶ Y) (g : Y âŸ¶ G) :
    Î±.app _ (f â‰« g) = f â‰« Î±.app _ g := congr($(Î±.naturality f.op) g)

/-- The yoneda embedding for `Grp_C` is fully faithful. -/
def yonedaGrpFullyFaithful : yonedaGrp (C := C).FullyFaithful where
  preimage {G H} Î± := yonedaMonFullyFaithful.preimage (Functor.whiskerRight Î± (forgetâ‚‚ Grp MonCat))
  map_preimage {G H} Î± := by
    ext X : 3
    exact congr(($(yonedaMonFullyFaithful.map_preimage (X := G.toMon_) (Y := H.toMon_)
      (Functor.whiskerRight Î± (forgetâ‚‚ Grp MonCat))).app X).hom)
  preimage_map := yonedaMonFullyFaithful.preimage_map

instance : yonedaGrp (C := C).Full := yonedaGrpFullyFaithful.full
instance : yonedaGrp (C := C).Faithful := yonedaGrpFullyFaithful.faithful

lemma essImage_yonedaGrp :
    yonedaGrp (C := C).essImage = (Â· â‹™ forget _) â»Â¹' setOf Functor.IsRepresentable := by
  ext F
  constructor
  Â· rintro âŸ¨G, âŸ¨Î±âŸ©âŸ©
    exact âŸ¨G.X, âŸ¨Functor.representableByEquiv.symm (Functor.isoWhiskerRight Î± (forget _))âŸ©âŸ©
  Â· rintro âŸ¨X, âŸ¨eâŸ©âŸ©
    letI := GrpObj.ofRepresentableBy X F e
    exact âŸ¨âŸ¨XâŸ©, âŸ¨yonedaGrpObjIsoOfRepresentableBy X F eâŸ©âŸ©

@[reassoc]
lemma GrpObj.inv_comp (f : X âŸ¶ G) (g : G âŸ¶ H) [IsMon_Hom g] : fâ»Â¹ â‰« g = (f â‰« g)â»Â¹ := by
  simp [Hom.inv_def]

@[deprecated (since := "2025-09-13")] alias Grp_Class.inv_comp := GrpObj.inv_comp

@[reassoc]
lemma GrpObj.div_comp (f g : X âŸ¶ G) (h : G âŸ¶ H) [IsMon_Hom h] :
    (f / g) â‰« h = (f â‰« h) / (g â‰« h) :=
  ((yonedaGrp.map <| Grp_.homMk h).app <| op X).hom.map_div f g

@[deprecated (since := "2025-09-13")] alias Grp_Class.div_comp := GrpObj.div_comp

@[reassoc]
lemma GrpObj.zpow_comp (f : X âŸ¶ G) (n : â„¤) (g : G âŸ¶ H) [IsMon_Hom g] :
    (f ^ n) â‰« g = (f â‰« g) ^ n :=
  ((yonedaGrp.map <| Grp_.homMk g).app <| op X).hom.map_zpow f n

@[deprecated (since := "2025-09-13")] alias Grp_Class.zpow_comp := GrpObj.zpow_comp

@[reassoc]
lemma GrpObj.comp_inv (f : X âŸ¶ Y) (g : Y âŸ¶ G) : f â‰« gâ»Â¹ = (f â‰« g)â»Â¹ :=
  ((yonedaGrp.obj âŸ¨GâŸ©).map f.op).hom.map_inv g

@[deprecated (since := "2025-09-13")] alias Grp_Class.comp_inv := GrpObj.comp_inv

@[reassoc]
lemma GrpObj.comp_div (f : X âŸ¶ Y) (g h : Y âŸ¶ G) : f â‰« (g / h) = f â‰« g / f â‰« h :=
  ((yonedaGrp.obj âŸ¨GâŸ©).map f.op).hom.map_div g h

@[deprecated (since := "2025-09-13")] alias Grp_Class.comp_div := GrpObj.comp_div

@[reassoc]
lemma GrpObj.comp_zpow (f : X âŸ¶ Y) (g : Y âŸ¶ G) : âˆ€ n : â„¤, f â‰« g ^ n = (f â‰« g) ^ n
  | (n : â„•) => by simp [comp_pow]
  | .negSucc n => by simp [comp_pow, comp_inv]

@[deprecated (since := "2025-09-13")] alias Grp_Class.comp_zpow := GrpObj.comp_zpow

lemma GrpObj.inv_eq_inv : Î¹ = (ğŸ™ G)â»Â¹ := by simp [Hom.inv_def]

@[deprecated (since := "2025-09-13")] alias Grp_Class.inv_eq_inv := GrpObj.inv_eq_inv

instance [BraidedCategory C] [IsCommMon G] : IsMon_Hom Î¹[G] where
  one_hom := by simp [one_eq_one, â† Hom.inv_def]
  mul_hom := by simp [GrpObj.mul_inv_rev]

attribute [local simp] Hom.inv_def in
instance [BraidedCategory C] [IsCommMon G] {f : M âŸ¶ G} [IsMon_Hom f] : IsMon_Hom fâ»Â¹ where

/-- If `G` is a commutative group object, then `Hom(X, G)` has a commutative group structure. -/
abbrev Hom.commGroup [BraidedCategory C] [IsCommMon G] : CommGroup (X âŸ¶ G) where

scoped[MonObj] attribute [instance] Hom.commGroup
