/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.Algebra.Category.Grp.Limits
import Mathlib.CategoryTheory.Monoidal.Cartesian.Mon_
import Mathlib.CategoryTheory.Monoidal.Grp_

/-!
# Yoneda embedding of `Grp C`

We show that group objects are exactly those whose yoneda presheaf is a presheaf of groups,
by constructing the yoneda embedding `Grp C ‚•§ C·µí·µñ ‚•§ GrpCat.{v}` and
showing that it is fully faithful and its (essential) image is the representable functors.
-/

assert_not_exists Field

open CategoryTheory MonoidalCategory Limits Opposite CartesianMonoidalCategory MonObj

namespace CategoryTheory
universe w v u
variable {C : Type u} [Category.{v} C] [CartesianMonoidalCategory C]
  {M G H X Y : C} [MonObj M] [GrpObj G] [GrpObj H]

/-- Construct a morphism `G ‚ü∂ H` of `Grp C` C from a map `f : G ‚ü∂ H` and a `IsMonHom f`
instance. -/
@[simps]
def Grp.homMk (f : G ‚ü∂ H) [IsMonHom f] : .mk G ‚ü∂ Grp.mk H := ‚ü®f‚ü©

@[deprecated (since := "2025-10-13")] alias Grp_.homMk := Grp.homMk

@[simp]
lemma Grp.homMk_hom' {G H : Grp C} (f : G ‚ü∂ H) : homMk (G := G.X) (H := H.X) f.hom = f := rfl

@[deprecated (since := "2025-10-13")] alias Grp_.homMk_hom' := Grp.homMk_hom'

variable (X) in
/-- If `X` represents a presheaf of monoids, then `X` is a monoid object. -/
def GrpObj.ofRepresentableBy (F : C·µí·µñ ‚•§ GrpCat.{w}) (Œ± : (F ‚ãô forget _).RepresentableBy X) :
    GrpObj X where
  __ := MonObj.ofRepresentableBy X (F ‚ãô forget‚ÇÇ GrpCat MonCat) Œ±
  inv := Œ±.homEquiv.symm (Œ±.homEquiv (ùüô _))‚Åª¬π
  left_inv := by
    change lift (Œ±.homEquiv.symm (Œ±.homEquiv (ùüô X))‚Åª¬π) (ùüô X) ‚â´
      Œ±.homEquiv.symm (Œ±.homEquiv (fst X X) * Œ±.homEquiv (snd X X)) =
        toUnit X ‚â´ Œ±.homEquiv.symm 1
    apply Œ±.homEquiv.injective
    simp only [Œ±.homEquiv_comp, Equiv.apply_symm_apply]
    simp only [Functor.comp_map, ConcreteCategory.forget_map_eq_coe, map_one, map_mul]
    simp only [‚Üê ConcreteCategory.forget_map_eq_coe, ‚Üê Functor.comp_map, ‚Üê Œ±.homEquiv_comp]
    simp [- Functor.comp_obj]
  right_inv := by
    change lift (ùüô X) (Œ±.homEquiv.symm (Œ±.homEquiv (ùüô X))‚Åª¬π) ‚â´
      Œ±.homEquiv.symm (Œ±.homEquiv (fst X X) * Œ±.homEquiv (snd X X)) =
        toUnit X ‚â´ Œ±.homEquiv.symm 1
    apply Œ±.homEquiv.injective
    simp only [Œ±.homEquiv_comp, Equiv.apply_symm_apply]
    simp only [Functor.comp_map, ConcreteCategory.forget_map_eq_coe, map_one, map_mul]
    simp only [‚Üê ConcreteCategory.forget_map_eq_coe, ‚Üê Functor.comp_map, ‚Üê Œ±.homEquiv_comp]
    simp [- Functor.comp_obj]

@[deprecated (since := "2025-09-13")] alias Grp_Class.ofRepresentableBy := GrpObj.ofRepresentableBy

/-- If `G` is a group object, then `Hom(X, G)` has a group structure. -/
abbrev Hom.group : Group (X ‚ü∂ G) where
  inv f := f ‚â´ Œπ
  inv_mul_cancel f := calc
    lift (f ‚â´ Œπ) f ‚â´ Œº
    _ = (f ‚â´ lift Œπ (ùüô G)) ‚â´ Œº := by simp
    _ = toUnit X ‚â´ Œ∑ := by rw [Category.assoc]; simp

scoped[CategoryTheory.MonObj] attribute [instance] Hom.group

lemma Hom.inv_def (f : X ‚ü∂ G) : f‚Åª¬π = f ‚â´ Œπ := rfl

variable (G) in
/-- If `G` is a group object, then `Hom(-, G)` is a presheaf of groups. -/
@[simps]
def yonedaGrpObj : C·µí·µñ ‚•§ GrpCat.{v} where
  obj X := GrpCat.of (unop X ‚ü∂ G)
  map œÜ := GrpCat.ofHom ((yonedaMonObj G).map œÜ).hom

variable (G) in
/-- If `G` is a monoid object, then `Hom(-, G)` as a presheaf of monoids is represented by `G`. -/
def yonedaGrpObjRepresentableBy : (yonedaGrpObj G ‚ãô forget _).RepresentableBy G :=
  Functor.representableByEquiv.symm (.refl _)

variable (G) in
lemma GrpObj.ofRepresentableBy_yonedaGrpObjRepresentableBy :
    ofRepresentableBy G _ (yonedaGrpObjRepresentableBy G) = ‚ÄπGrpObj G‚Ä∫ := by
  ext; change lift (fst G G) (snd G G) ‚â´ Œº = Œº; rw [lift_fst_snd, Category.id_comp]

@[deprecated (since := "2025-09-13")]
alias Grp_Class.ofRepresentableBy_yonedaGrpObjRepresentableBy :=
  GrpObj.ofRepresentableBy_yonedaGrpObjRepresentableBy

variable (X) in
/-- If `X` represents a presheaf of groups `F`, then `Hom(-, X)` is isomorphic to `F` as
a presheaf of groups. -/
@[simps! hom inv]
def yonedaGrpObjIsoOfRepresentableBy (F : C·µí·µñ ‚•§ GrpCat.{v}) (Œ± : (F ‚ãô forget _).RepresentableBy X) :
    letI := GrpObj.ofRepresentableBy X F Œ±
    yonedaGrpObj X ‚âÖ F :=
  letI := GrpObj.ofRepresentableBy X F Œ±
  NatIso.ofComponents (fun Y ‚Ü¶ MulEquiv.toGrpIso
    { toEquiv := Œ±.homEquiv
      map_mul' :=
  ((yonedaMonObjIsoOfRepresentableBy X (F ‚ãô forget‚ÇÇ GrpCat MonCat) Œ±).hom.app Y).hom.map_mul})
      fun œÜ ‚Ü¶ GrpCat.hom_ext <| MonoidHom.ext <| Œ±.homEquiv_comp œÜ.unop

/-- The yoneda embedding of `Grp_C` into presheaves of groups. -/
@[simps]
def yonedaGrp : Grp C ‚•§ C·µí·µñ ‚•§ GrpCat.{v} where
  obj G := yonedaGrpObj G.X
  map {G H} œà := { app Y := GrpCat.ofHom ((yonedaMon.map œà).app Y).hom }

@[reassoc]
lemma yonedaGrp_naturality (Œ± : yonedaGrpObj G ‚ü∂ yonedaGrpObj H) (f : X ‚ü∂ Y) (g : Y ‚ü∂ G) :
    Œ±.app _ (f ‚â´ g) = f ‚â´ Œ±.app _ g := congr($(Œ±.naturality f.op) g)

/-- The yoneda embedding for `Grp_C` is fully faithful. -/
def yonedaGrpFullyFaithful : yonedaGrp (C := C).FullyFaithful where
  preimage {G H} Œ± :=
    yonedaMonFullyFaithful.preimage (Functor.whiskerRight Œ± (forget‚ÇÇ GrpCat MonCat))
  map_preimage {G H} Œ± := by
    ext X : 3
    exact congr(($(yonedaMonFullyFaithful.map_preimage (X := G.toMon) (Y := H.toMon)
      (Functor.whiskerRight Œ± (forget‚ÇÇ GrpCat MonCat))).app X).hom)
  preimage_map := yonedaMonFullyFaithful.preimage_map

instance : yonedaGrp (C := C).Full := yonedaGrpFullyFaithful.full
instance : yonedaGrp (C := C).Faithful := yonedaGrpFullyFaithful.faithful

lemma essImage_yonedaGrp :
    yonedaGrp (C := C).essImage = (¬∑ ‚ãô forget _) ‚Åª¬π' setOf Functor.IsRepresentable := by
  ext F
  constructor
  ¬∑ rintro ‚ü®G, ‚ü®Œ±‚ü©‚ü©
    exact ‚ü®G.X, ‚ü®Functor.representableByEquiv.symm (Functor.isoWhiskerRight Œ± (forget _))‚ü©‚ü©
  ¬∑ rintro ‚ü®X, ‚ü®e‚ü©‚ü©
    letI := GrpObj.ofRepresentableBy X F e
    exact ‚ü®‚ü®X‚ü©, ‚ü®yonedaGrpObjIsoOfRepresentableBy X F e‚ü©‚ü©

@[reassoc]
lemma GrpObj.inv_comp (f : X ‚ü∂ G) (g : G ‚ü∂ H) [IsMonHom g] : f‚Åª¬π ‚â´ g = (f ‚â´ g)‚Åª¬π := by
  simp [Hom.inv_def]

@[deprecated (since := "2025-09-13")] alias Grp_Class.inv_comp := GrpObj.inv_comp

@[reassoc]
lemma GrpObj.div_comp (f g : X ‚ü∂ G) (h : G ‚ü∂ H) [IsMonHom h] :
    (f / g) ‚â´ h = (f ‚â´ h) / (g ‚â´ h) :=
  ((yonedaGrp.map <| Grp.homMk h).app <| op X).hom.map_div f g

@[deprecated (since := "2025-09-13")] alias Grp_Class.div_comp := GrpObj.div_comp

@[reassoc]
lemma GrpObj.zpow_comp (f : X ‚ü∂ G) (n : ‚Ñ§) (g : G ‚ü∂ H) [IsMonHom g] :
    (f ^ n) ‚â´ g = (f ‚â´ g) ^ n :=
  ((yonedaGrp.map <| Grp.homMk g).app <| op X).hom.map_zpow f n

@[deprecated (since := "2025-09-13")] alias Grp_Class.zpow_comp := GrpObj.zpow_comp

@[reassoc]
lemma GrpObj.comp_inv (f : X ‚ü∂ Y) (g : Y ‚ü∂ G) : f ‚â´ g‚Åª¬π = (f ‚â´ g)‚Åª¬π :=
  ((yonedaGrp.obj ‚ü®G‚ü©).map f.op).hom.map_inv g

@[deprecated (since := "2025-09-13")] alias Grp_Class.comp_inv := GrpObj.comp_inv

@[reassoc]
lemma GrpObj.comp_div (f : X ‚ü∂ Y) (g h : Y ‚ü∂ G) : f ‚â´ (g / h) = f ‚â´ g / f ‚â´ h :=
  ((yonedaGrp.obj ‚ü®G‚ü©).map f.op).hom.map_div g h

@[deprecated (since := "2025-09-13")] alias Grp_Class.comp_div := GrpObj.comp_div

@[reassoc]
lemma GrpObj.comp_zpow (f : X ‚ü∂ Y) (g : Y ‚ü∂ G) : ‚àÄ n : ‚Ñ§, f ‚â´ g ^ n = (f ‚â´ g) ^ n
  | (n : ‚Ñï) => by simp [comp_pow]
  | .negSucc n => by simp [comp_pow, comp_inv]

@[deprecated (since := "2025-09-13")] alias Grp_Class.comp_zpow := GrpObj.comp_zpow

lemma GrpObj.inv_eq_inv : Œπ = (ùüô G)‚Åª¬π := by simp [Hom.inv_def]

@[reassoc (attr := simp)]
lemma GrpObj.one_inv : Œ∑[G] ‚â´ Œπ = Œ∑ := by simp [GrpObj.inv_eq_inv, GrpObj.comp_inv, one_eq_one]

@[deprecated (since := "2025-09-13")] alias Grp_Class.inv_eq_inv := GrpObj.inv_eq_inv

variable [BraidedCategory C]

instance [IsCommMonObj G] : IsMonHom Œπ[G] where
  one_hom := by simp [one_eq_one, ‚Üê Hom.inv_def]
  mul_hom := by simp [GrpObj.mul_inv_rev]

attribute [local simp] Hom.inv_def in
instance [IsCommMonObj G] {f : M ‚ü∂ G} [IsMonHom f] : IsMonHom f‚Åª¬π where

namespace Grp_
variable {G H : Grp_ C} [IsCommMonObj H.X]

-- TODO: Make `Grp_.toMon` an abbrev in mathlib.
set_option allowUnsafeReducibility true in
attribute [reducible] Grp_.toMon

instance : MonObj H where
  one := Œ∑[H.toMon]
  mul := Œº[H.toMon]
  one_mul := MonObj.one_mul H.toMon
  mul_one := MonObj.mul_one H.toMon
  mul_assoc := MonObj.mul_assoc H.toMon


namespace Hom

@[simp] lemma hom_one : (1 : G ‚ü∂ H).hom = 1 := rfl
@[simp] lemma hom_mul (f g : G ‚ü∂ H) : (f * g).hom = f.hom * g.hom := rfl
@[simp] lemma hom_pow (f : G ‚ü∂ H) (n : ‚Ñï) : (f ^ n).hom = f.hom ^ n := Mon.Hom.hom_pow ..

end Hom

attribute [local simp] mul_eq_mul GrpObj.inv_eq_inv comp_mul in
/-- A commutative group object is a group object in the category of group objects. -/
instance : GrpObj H where inv := .mk Œπ[H.X]

namespace Hom

@[simp] lemma hom_inv (f : G ‚ü∂ H) : f‚Åª¬π.hom = f.hom‚Åª¬π := rfl
@[simp] lemma hom_div (f g : G ‚ü∂ H) : (f / g).hom = f.hom / g.hom := rfl
@[simp] lemma hom_zpow (f : G ‚ü∂ H) (n : ‚Ñ§) : (f ^ n).hom = f.hom ^ n := by cases n <;> simp

end Hom

attribute [local simp] mul_eq_mul comp_mul mul_comm mul_div_mul_comm in
/-- A commutative group object is a commutative group object in the category of group objects. -/
instance : IsCommMonObj H where

instance [IsCommMonObj G.X] (f : G ‚ü∂ H) : IsMonHom f where
  one_hom := by ext; simp [Grp_.instMonObj]
  mul_hom := by ext; simp [Grp_.instMonObj]

end Grp_

/-- If `G` is a commutative group object, then `Hom(X, G)` has a commutative group structure. -/
abbrev Hom.commGroup [IsCommMonObj G] : CommGroup (X ‚ü∂ G) where

scoped[CategoryTheory.MonObj] attribute [instance] Hom.commGroup

end CategoryTheory
