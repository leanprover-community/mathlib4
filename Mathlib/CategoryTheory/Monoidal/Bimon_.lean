/-
Copyright (c) 2024 Lean FRO LLC. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Monoidal.Comon_

/-!
# The category of bimonoids in a braided monoidal category.

We define bimonoids in a braided monoidal category `C`
as comonoid objects in the category of monoid objects in `C`.

We verify that this is equivalent to the monoid objects in the category of comonoid objects.

## TODO
* Construct the category of modules, and show that it is monoidal with a monoidal forgetful functor
  to `C`.
* Some form of Tannaka reconstruction:
  given a monoidal functor `F : C â¥¤ D` into a braided category `D`,
  the internal endomorphisms of `F` form a bimonoid in presheaves on `D`,
  in good circumstances this is representable by a bimonoid in `D`, and then
  `C` is monoidally equivalent to the modules over that bimonoid.
-/

noncomputable section

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚ u

open CategoryTheory MonoidalCategory

variable {C : Type uâ‚} [Category.{vâ‚} C] [MonoidalCategory.{vâ‚} C] [BraidedCategory C]

open scoped Mon_Class Comon_Class

/--
A bimonoid object in a braided category `C` is a object that is simultaneously monoid and comonoid
objects, and structure morphisms of them satisfy appropriate consistency conditions.
-/
class Bimon_Class (M : C) extends Mon_Class M, Comon_Class M where
  /- For the names of the conditions below, the unprimed names are reserved for the version where
  the argument `M` is explicit. -/
  mul_comul' : Î¼[M] â‰« Î”[M] = (Î”[M] âŠ— Î”[M]) â‰« tensorÎ¼ M M M M â‰« (Î¼[M] âŠ— Î¼[M]) := by aesop_cat
  one_comul' : Î·[M] â‰« Î”[M] = Î·[M âŠ— M] := by aesop_cat
  mul_counit' : Î¼[M] â‰« Îµ[M] = Îµ[M âŠ— M] := by aesop_cat
  one_counit' : Î·[M] â‰« Îµ[M] = ğŸ™ (ğŸ™_ C) := by aesop_cat

namespace Bimon_Class

/- The simp attribute is reserved for the unprimed versions. -/
attribute [reassoc] mul_comul' one_comul' mul_counit' one_counit'

variable (M : C) [Bimon_Class M]

@[reassoc (attr := simp)]
theorem mul_comul (M : C) [Bimon_Class M] :
    Î¼[M] â‰« Î”[M] = (Î”[M] âŠ— Î”[M]) â‰« tensorÎ¼ M M M M â‰« (Î¼[M] âŠ— Î¼[M]) :=
  mul_comul'

@[reassoc (attr := simp)]
theorem one_comul (M : C) [Bimon_Class M] : Î·[M] â‰« Î”[M] = Î·[M âŠ— M] := one_comul'

@[reassoc (attr := simp)]
theorem mul_counit (M : C) [Bimon_Class M] : Î¼[M] â‰« Îµ[M] = Îµ[M âŠ— M] := mul_counit'

@[reassoc (attr := simp)]
theorem one_counit (M : C) [Bimon_Class M] : Î·[M] â‰« Îµ[M] = ğŸ™ (ğŸ™_ C) := one_counit'

end Bimon_Class

/-- The property that a morphism between bimonoid objects is a bimonoid morphism. -/
class IsBimon_Hom {M N : C} [Bimon_Class M] [Bimon_Class N] (f : M âŸ¶ N) extends
    IsMon_Hom f, IsComon_Hom f : Prop

variable (C)

/--
A bimonoid object in a braided category `C` is a comonoid object in the (monoidal)
category of monoid objects in `C`.
-/
def Bimon_ := Comon_ (Mon_ C)

namespace Bimon_

instance : Category (Bimon_ C) := inferInstanceAs (Category (Comon_ (Mon_ C)))

@[ext] lemma ext {X Y : Bimon_ C} {f g : X âŸ¶ Y} (w : f.hom.hom = g.hom.hom) : f = g :=
  Comon_.Hom.ext (Mon_.Hom.ext w)

@[simp] theorem id_hom' (M : Bimon_ C) : Comon_.Hom.hom (ğŸ™ M) = ğŸ™ M.X := rfl

@[simp]
theorem comp_hom' {M N K : Bimon_ C} (f : M âŸ¶ N) (g : N âŸ¶ K) : (f â‰« g).hom = f.hom â‰« g.hom :=
  rfl

/-- The forgetful functor from bimonoid objects to monoid objects. -/
abbrev toMon_ : Bimon_ C â¥¤ Mon_ C := Comon_.forget (Mon_ C)

/-- The forgetful functor from bimonoid objects to the underlying category. -/
def forget : Bimon_ C â¥¤ C := toMon_ C â‹™ Mon_.forget C

@[simp]
theorem toMon_forget : toMon_ C â‹™ Mon_.forget C = forget C := rfl

/-- The forgetful functor from bimonoid objects to comonoid objects. -/
@[simps!]
def toComon_ : Bimon_ C â¥¤ Comon_ C := (Mon_.forget C).mapComon

@[simp]
theorem toComon_forget : toComon_ C â‹™ Comon_.forget C = forget C := rfl

/-- The object level part of the forward direction of `Comon_ (Mon_ C) â‰Œ Mon_ (Comon_ C)` -/
def toMon_Comon_obj (M : Bimon_ C) : Mon_ (Comon_ C) where
  X := (toComon_ C).obj M
  one := { hom := M.X.one }
  mul :=
  { hom := M.X.mul,
    hom_comul := by dsimp; simp [tensor_Î¼] }

attribute [simps] toMon_Comon_obj -- We add this after the fact to avoid a timeout.

/-- The forward direction of `Comon_ (Mon_ C) â‰Œ Mon_ (Comon_ C)` -/
@[simps]
def toMon_Comon_ : Bimon_ C â¥¤ Mon_ (Comon_ C) where
  obj := toMon_Comon_obj C
  map f :=
  { hom := (toComon_ C).map f }

/-- The object level part of the backward direction of `Comon_ (Mon_ C) â‰Œ Mon_ (Comon_ C)` -/
@[simps]
def ofMon_Comon_obj (M : Mon_ (Comon_ C)) : Bimon_ C where
  X := (Comon_.forget C).mapMon.obj M
  counit := { hom := M.X.counit }
  comul :=
  { hom := M.X.comul,
    mul_hom := by dsimp; simp [tensor_Î¼] }

/-- The backward direction of `Comon_ (Mon_ C) â‰Œ Mon_ (Comon_ C)` -/
@[simps]
def ofMon_Comon_ : Mon_ (Comon_ C) â¥¤ Bimon_ C where
  obj := ofMon_Comon_obj C
  map f :=
  { hom := (Comon_.forget C).mapMon.map f }

/-- The equivalence `Comon_ (Mon_ C) â‰Œ Mon_ (Comon_ C)` -/
def equivMon_Comon_ : Bimon_ C â‰Œ Mon_ (Comon_ C) where
  functor := toMon_Comon_ C
  inverse := ofMon_Comon_ C
  unitIso := NatIso.ofComponents (fun _ => Comon_.mkIso (Mon_.mkIso (Iso.refl _)))
  counitIso := NatIso.ofComponents (fun _ => Mon_.mkIso (Comon_.mkIso (Iso.refl _)))

/-! # The trivial bimonoid -/

/-- The trivial bimonoid object. -/
@[simps!]
def trivial : Bimon_ C := Comon_.trivial (Mon_ C)

/-- The bimonoid morphism from the trivial bimonoid to any bimonoid. -/
@[simps]
def trivialTo (A : Bimon_ C) : trivial C âŸ¶ A :=
  { hom := (default : Mon_.trivial C âŸ¶ A.X), }

@[deprecated (since := "2024-12-07")] alias trivial_to := trivialTo
@[deprecated (since := "2024-12-07")] alias trivial_to_hom := trivialTo_hom

/-- The bimonoid morphism from any bimonoid to the trivial bimonoid. -/
@[simps!]
def toTrivial (A : Bimon_ C) : A âŸ¶ trivial C :=
  (default : @Quiver.Hom (Comon_ (Mon_ C)) _ A (Comon_.trivial (Mon_ C)))

@[deprecated (since := "2024-12-07")] alias to_trivial := toTrivial
@[deprecated (since := "2024-12-07")] alias to_trivial_hom := toTrivial_hom

/-! # Additional lemmas -/

variable {C}

@[reassoc]
theorem one_comul (M : Bimon_ C) :
    M.X.one â‰« M.comul.hom = (Î»_ _).inv â‰« (M.X.one âŠ— M.X.one) := by
  simp

@[reassoc]
theorem mul_counit (M : Bimon_ C) :
    M.X.mul â‰« M.counit.hom = (M.counit.hom âŠ— M.counit.hom) â‰« (Î»_ _).hom := by
  simp

/-- Compatibility of the monoid and comonoid structures, in terms of morphisms in `C`. -/
@[reassoc (attr := simp)] theorem compatibility (M : Bimon_ C) :
    (M.comul.hom âŠ— M.comul.hom) â‰«
      (Î±_ _ _ (M.X.X âŠ— M.X.X)).hom â‰« M.X.X â— (Î±_ _ _ _).inv â‰«
      M.X.X â— (Î²_ M.X.X M.X.X).hom â–· M.X.X â‰«
      M.X.X â— (Î±_ _ _ _).hom â‰« (Î±_ _ _ _).inv â‰«
      (M.X.mul âŠ— M.X.mul) =
    M.X.mul â‰« M.comul.hom := by
  have := (Mon_.Hom.mul_hom M.comul).symm
  simpa [-Mon_.Hom.mul_hom, tensorÎ¼] using this

@[reassoc (attr := simp)] theorem comul_counit_hom (M : Bimon_ C) :
    M.comul.hom â‰« (_ â— M.counit.hom) = (Ï_ _).inv := by
  simpa [- Comon_.comul_counit] using congr_arg Mon_.Hom.hom M.comul_counit

@[reassoc (attr := simp)] theorem counit_comul_hom (M : Bimon_ C) :
    M.comul.hom â‰« (M.counit.hom â–· _) = (Î»_ _).inv := by
  simpa [- Comon_.counit_comul] using congr_arg Mon_.Hom.hom M.counit_comul

@[reassoc (attr := simp)] theorem comul_assoc_hom (M : Bimon_ C) :
    M.comul.hom â‰« (M.X.X â— M.comul.hom) =
      M.comul.hom â‰« (M.comul.hom â–· M.X.X) â‰« (Î±_ M.X.X M.X.X M.X.X).hom := by
  simpa [- Comon_.comul_assoc] using congr_arg Mon_.Hom.hom M.comul_assoc

@[reassoc] theorem comul_assoc_flip_hom (M : Bimon_ C) :
    M.comul.hom â‰« (M.comul.hom â–· M.X.X) =
      M.comul.hom â‰« (M.X.X â— M.comul.hom) â‰« (Î±_ M.X.X M.X.X M.X.X).inv := by
  simp

@[reassoc] theorem hom_comul_hom {M N : Bimon_ C} (f : M âŸ¶ N) :
    f.hom.hom â‰« N.comul.hom = M.comul.hom â‰« (f.hom.hom âŠ— f.hom.hom) := by
  simpa [- Comon_.Hom.hom_comul] using congr_arg Mon_.Hom.hom f.hom_comul

@[reassoc] theorem hom_counit_hom {M N : Bimon_ C} (f : M âŸ¶ N) :
    f.hom.hom â‰« N.counit.hom = M.counit.hom := by
  simpa [- Comon_.Hom.hom_counit] using congr_arg Mon_.Hom.hom f.hom_counit

end Bimon_
