/-
Copyright (c) 2025 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Monoidal.Cartesian.Mon_
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq
import Mathlib.CategoryTheory.Limits.ExactFunctor

/-!
# The category of groups in a Cartesian monoidal category

We define group objects in Cartesian monoidal categories.

We show that the associativity diagram of a group object is always Cartesian and deduce that
morphisms of group objects commute with taking inverses.

We show that a finite-product-preserving functor takes group objects to group objects.
-/

universe vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ u

open CategoryTheory Category Limits MonoidalCategory CartesianMonoidalCategory Mon MonObj

variable {C : Type uâ‚} [Category.{vâ‚} C] [CartesianMonoidalCategory.{vâ‚} C]

section

/-- A group object internal to a cartesian monoidal category. Also see the bundled `Grp_`. -/
class GrpObj (X : C) extends MonObj X where
  /-- The inverse in a group object -/
  inv : X âŸ¶ X
  left_inv (X) : lift inv (ğŸ™ X) â‰« mul = toUnit _ â‰« one := by cat_disch
  right_inv (X) : lift (ğŸ™ X) inv â‰« mul = toUnit _ â‰« one := by cat_disch

@[deprecated (since := "2025-09-13")] alias Grp_Class := GrpObj

namespace MonObj

@[inherit_doc] scoped notation "Î¹" => GrpObj.inv
@[inherit_doc] scoped notation "Î¹["G"]" => GrpObj.inv (X := G)

end MonObj

namespace GrpObj

attribute [reassoc (attr := simp)] left_inv right_inv

@[simps inv]
instance : GrpObj (ğŸ™_ C) where
  inv := ğŸ™ (ğŸ™_ C)

end GrpObj

end

variable (C) in
/-- A group object in a Cartesian monoidal category. -/
structure Grp_ where
  /-- The underlying object in the ambient monoidal category -/
  X : C
  [grp : GrpObj X]

attribute [instance] Grp_.grp

namespace Grp_

/-- A group object is a monoid object. -/
@[simps X]
def toMon (A : Grp_ C) : Mon C := âŸ¨A.XâŸ©

@[deprecated (since := "2025-09-15")] alias toMon_ := toMon

variable (C) in
/-- The trivial group object. -/
@[simps!]
def trivial : Grp_ C :=
  { Mon.trivial C with grp := inferInstanceAs (GrpObj (ğŸ™_ C)) }

instance : Inhabited (Grp_ C) where
  default := trivial C

@[deprecated (since := "2025-06-15")] alias mk' := mk

instance : Category (Grp_ C) :=
  InducedCategory.category Grp_.toMon

@[simp]
theorem id_hom (A : Grp_ C) : Mon.Hom.hom (ğŸ™ A) = ğŸ™ A.X :=
  rfl

@[simp]
theorem comp_hom {R S T : Grp_ C} (f : R âŸ¶ S) (g : S âŸ¶ T) :
    Mon.Hom.hom (f â‰« g) = f.hom â‰« g.hom :=
  rfl

@[ext]
theorem hom_ext {A B : Grp_ C} (f g : A âŸ¶ B) (h : f.hom = g.hom) : f = g :=
  Mon.Hom.ext h

@[simp]
lemma id' (A : Grp_ C) : (ğŸ™ A : A.toMon âŸ¶ A.toMon) = ğŸ™ (A.toMon) := rfl

@[simp]
lemma comp' {Aâ‚ Aâ‚‚ Aâ‚ƒ : Grp_ C} (f : Aâ‚ âŸ¶ Aâ‚‚) (g : Aâ‚‚ âŸ¶ Aâ‚ƒ) :
    ((f â‰« g : Aâ‚ âŸ¶ Aâ‚ƒ) : Aâ‚.toMon âŸ¶ Aâ‚ƒ.toMon) = @CategoryStruct.comp (Mon C) _ _ _ _ f g := rfl

end Grp_

namespace GrpObj
variable {G X : C} [GrpObj G]

variable {A : C} {B : C}

@[reassoc (attr := simp)]
theorem lift_comp_inv_right [GrpObj B] (f : A âŸ¶ B) :
    lift f (f â‰« Î¹) â‰« Î¼ = toUnit _ â‰« Î· := by
  have := f â‰«= right_inv B
  rwa [comp_lift_assoc, comp_id, reassoc_of% toUnit_unique (f â‰« toUnit B) (toUnit A)] at this

@[reassoc]
theorem lift_inv_comp_right [GrpObj A] [GrpObj B] (f : A âŸ¶ B) [IsMonHom f] :
    lift f (Î¹ â‰« f) â‰« Î¼ = toUnit _ â‰« Î· := by
  have := right_inv A =â‰« f
  rwa [assoc, IsMonHom.mul_hom, assoc, IsMonHom.one_hom, lift_map_assoc, id_comp] at this

@[reassoc (attr := simp)]
theorem lift_comp_inv_left [GrpObj B] (f : A âŸ¶ B) :
    lift (f â‰« Î¹) f â‰« Î¼ = toUnit _ â‰« Î· := by
  have := f â‰«= left_inv B
  rwa [comp_lift_assoc, comp_id, reassoc_of% toUnit_unique (f â‰« toUnit B) (toUnit A)] at this

@[reassoc]
theorem lift_inv_comp_left [GrpObj A] [GrpObj B] (f : A âŸ¶ B) [IsMonHom f] :
    lift (Î¹ â‰« f) f â‰« Î¼ = toUnit _ â‰« Î· := by
  have := left_inv A =â‰« f
  rwa [assoc, IsMonHom.mul_hom, assoc, IsMonHom.one_hom, lift_map_assoc, id_comp] at this

theorem eq_lift_inv_left [GrpObj B] (f g h : A âŸ¶ B) :
    f = lift (g â‰« Î¹) h â‰« Î¼ â†” lift g f â‰« Î¼ = h := by
  refine âŸ¨?_, ?_âŸ© <;> (rintro rfl; simp [â† lift_lift_assoc])

theorem lift_inv_left_eq [GrpObj B] (f g h : A âŸ¶ B) :
    lift (f â‰« Î¹) g â‰« Î¼ = h â†” g = lift f h â‰« Î¼ := by
  rw [eq_comm, eq_lift_inv_left, eq_comm]

theorem eq_lift_inv_right [GrpObj B] (f g h : A âŸ¶ B) :
    f = lift g (h â‰« Î¹) â‰« Î¼ â†” lift f h â‰« Î¼ = g := by
  refine âŸ¨?_, ?_âŸ© <;> (rintro rfl; simp [lift_lift_assoc])

theorem lift_inv_right_eq [GrpObj B] (f g h : A âŸ¶ B) :
    lift f (g â‰« Î¹) â‰« Î¼ = h â†” f = lift h g â‰« Î¼ := by
  rw [eq_comm, eq_lift_inv_right, eq_comm]

theorem lift_left_mul_ext [GrpObj B] {f g : A âŸ¶ B} (i : A âŸ¶ B)
    (h : lift f i â‰« Î¼ = lift g i â‰« Î¼) : f = g := by
  rwa [â† eq_lift_inv_right, lift_lift_assoc, lift_comp_inv_right, lift_comp_one_right] at h

@[reassoc (attr := simp)]
theorem inv_comp_inv (A : C) [GrpObj A] : Î¹ â‰« Î¹ = ğŸ™ A := by
  apply lift_left_mul_ext Î¹[A]
  rw [right_inv, â† comp_toUnit_assoc Î¹, â† left_inv, comp_lift_assoc, Category.comp_id]

/-- Transfer `GrpObj` along an isomorphism. -/
-- Note: The simps lemmas are not tagged simp because their `#discr_tree_simp_key` are too generic.
@[simps! -isSimp]
abbrev ofIso (e : G â‰… X) : GrpObj X where
  toMonObj := .ofIso e
  inv := e.inv â‰« Î¹[G] â‰« e.hom
  left_inv := by simp [MonObj.ofIso]
  right_inv := by simp [MonObj.ofIso]

instance (A : C) [GrpObj A] : IsIso Î¹[A] := âŸ¨Î¹, by simp, by simpâŸ©

/-- For `inv â‰« inv = ğŸ™` see `inv_comp_inv`. -/
@[simp]
theorem inv_inv (A : C) [GrpObj A] : CategoryTheory.inv Î¹ = Î¹[A] := by
  rw [eq_comm, â† CategoryTheory.inv_comp_eq_id, IsIso.inv_inv, inv_comp_inv]

@[reassoc]
theorem mul_inv [BraidedCategory C] (A : C) [GrpObj A] :
    Î¼ â‰« Î¹ = (Î²_ A A).hom â‰« (Î¹ âŠ—â‚˜ Î¹) â‰« Î¼ := by
  apply lift_left_mul_ext Î¼
  nth_rw 2 [â† Category.comp_id Î¼]
  rw [â† comp_lift, Category.assoc, left_inv, â† Category.assoc (Î²_ A A).hom,
    â† lift_snd_fst, lift_map, lift_lift_assoc]
  nth_rw 2 [â† Category.id_comp Î¼]
  rw [â† lift_fst_snd, â† lift_lift_assoc (fst A A â‰« _), lift_comp_inv_left, lift_comp_one_left,
    lift_comp_inv_left, comp_toUnit_assoc]

@[reassoc]
theorem tensorHom_inv_inv_mul [BraidedCategory C] (A : C) [GrpObj A] :
    (Î¹[A] âŠ—â‚˜ Î¹[A]) â‰« Î¼ = (Î²_ A A).hom â‰« Î¼ â‰« Î¹ := by
  rw [mul_inv A, SymmetricCategory.symmetry_assoc]

@[reassoc]
lemma mul_inv_rev [BraidedCategory C] (G : C) [GrpObj G] :
    Î¼ â‰« Î¹ = (Î¹[G] âŠ—â‚˜ Î¹) â‰« (Î²_ _ _).hom â‰« Î¼ := by simp [tensorHom_inv_inv_mul]

/-- The map `(Â· * f)`. -/
@[simps]
def mulRight {A : C} [GrpObj A] (f : ğŸ™_ C âŸ¶ A) : A â‰… A where
  hom := lift (ğŸ™ _) (toUnit _ â‰« f) â‰« Î¼
  inv := lift (ğŸ™ _) (toUnit _ â‰« f â‰« Î¹) â‰« Î¼
  hom_inv_id := by simp [comp_lift_assoc, lift_lift_assoc, â† comp_lift]
  inv_hom_id := by simp [comp_lift_assoc, lift_lift_assoc, â† comp_lift]

@[simp]
lemma mulRight_one (A : C) [GrpObj A] : mulRight Î·[A] = Iso.refl A := by
  ext; simp

/-- The associativity diagram of a group object is Cartesian.

In fact, any monoid object whose associativity diagram is Cartesian can be made into a group object
(we do not prove this in this file), so we should expect that many properties of group objects
follow from this result. -/
theorem isPullback (A : C) [GrpObj A] :
    IsPullback (Î¼ â–· A) ((Î±_ A A A).hom â‰« (A â— Î¼)) Î¼ Î¼ where
  w := by simp
  isLimit' := Nonempty.intro <| PullbackCone.IsLimit.mk _
    (fun s => lift
      (lift
        (s.snd â‰« fst _ _)
        (lift (s.snd â‰« fst _ _ â‰« Î¹) (s.fst â‰« fst _ _) â‰« Î¼))
      (s.fst â‰« snd _ _))
    (by
      refine fun s => CartesianMonoidalCategory.hom_ext _ _ ?_ (by simp)
      simp only [lift_whiskerRight, lift_fst]
      rw [â† lift_lift_assoc, â† assoc, lift_comp_inv_right, lift_comp_one_left])
    (by
      refine fun s => CartesianMonoidalCategory.hom_ext _ _ (by simp) ?_
      simp only [lift_lift_associator_hom_assoc, lift_whiskerLeft, lift_snd]
      have : lift (s.snd â‰« fst _ _ â‰« Î¹) (s.fst â‰« fst _ _) â‰« Î¼ =
          lift (s.snd â‰« snd _ _) (s.fst â‰« snd _ _ â‰« Î¹) â‰« Î¼ := by
        rw [â† assoc s.fst, eq_lift_inv_right, lift_lift_assoc, â† assoc s.snd, lift_inv_left_eq,
          lift_comp_fst_snd, lift_comp_fst_snd, s.condition]
      rw [this, lift_lift_assoc, â† assoc, lift_comp_inv_left, lift_comp_one_right])
    (by
      intro s m hmâ‚ hmâ‚‚
      refine CartesianMonoidalCategory.hom_ext _ _ (CartesianMonoidalCategory.hom_ext _ _ ?_ ?_) ?_
      Â· simpa using hmâ‚‚ =â‰« fst _ _
      Â· have h : m â‰« fst _ _ â‰« fst _ _ = s.snd â‰« fst _ _ := by simpa using hmâ‚‚ =â‰« fst _ _
        have := hmâ‚ =â‰« fst _ _
        simp only [assoc, whiskerRight_fst, lift_fst, lift_snd] at this âŠ¢
        rw [â† assoc, â† lift_comp_fst_snd (m â‰« _), assoc, assoc, h] at this
        rwa [â† assoc s.snd, eq_lift_inv_left]
      Â· simpa using hmâ‚ =â‰« snd _ _)

/-- Morphisms of group objects preserve inverses. -/
@[reassoc (attr := simp)]
theorem inv_hom [GrpObj A] [GrpObj B] (f : A âŸ¶ B) [IsMonHom f] : Î¹ â‰« f = f â‰« Î¹ := by
  suffices lift (lift f (Î¹ â‰« f)) f =
      lift (lift f (f â‰« Î¹)) f by simpa using (this =â‰« fst _ _) =â‰« snd _ _
  apply (isPullback B).hom_ext <;> apply CartesianMonoidalCategory.hom_ext <;>
    simp [lift_inv_comp_right, lift_inv_comp_left]

lemma toMonObj_injective {X : C} :
    Function.Injective (@GrpObj.toMonObj C â€¹_â€º â€¹_â€º X) := by
  intro hâ‚ hâ‚‚ e
  suffices hâ‚.inv = hâ‚‚.inv by cases hâ‚; congr!
  apply lift_left_mul_ext (ğŸ™ _)
  rw [left_inv]
  convert @left_inv _ _ _ _ hâ‚ using 2
  exacts [congr(($e.symm).mul), congr(($e.symm).one)]

@[deprecated (since := "2025-09-09")] alias toMon_Class_injective := toMonObj_injective

@[ext]
lemma ext {X : C} (hâ‚ hâ‚‚ : GrpObj X) (H : hâ‚.toMonObj = hâ‚‚.toMonObj) : hâ‚ = hâ‚‚ :=
  GrpObj.toMonObj_injective H

namespace tensorObj
variable [BraidedCategory C] {G H : C} [GrpObj G] [GrpObj H]

@[simps inv]
instance : GrpObj (G âŠ— H) where
  inv := Î¹ âŠ—â‚˜ Î¹

end GrpObj.tensorObj

namespace Grp_

section

variable (C)

/-- The forgetful functor from group objects to monoid objects. -/
@[simps! obj_X]
def forgetâ‚‚Mon : Grp_ C â¥¤ Mon C :=
  inducedFunctor Grp_.toMon

@[deprecated (since := "2025-09-15")] alias forgetâ‚‚Mon_ := forgetâ‚‚Mon

/-- The forgetful functor from group objects to monoid objects is fully faithful. -/
def fullyFaithfulForgetâ‚‚Mon : (forgetâ‚‚Mon C).FullyFaithful :=
  fullyFaithfulInducedFunctor _

@[deprecated (since := "2025-09-15")] alias fullyFaithfulForgetâ‚‚Mon_ := fullyFaithfulForgetâ‚‚Mon

instance : (forgetâ‚‚Mon C).Full := InducedCategory.full _
instance : (forgetâ‚‚Mon C).Faithful := InducedCategory.faithful _

variable {C}

@[simp]
theorem forgetâ‚‚Mon_obj_one (A : Grp_ C) : Î·[((forgetâ‚‚Mon C).obj A).X] = Î·[A.X] :=
  rfl

@[simp]
theorem forgetâ‚‚Mon_obj_mul (A : Grp_ C) : Î¼[((forgetâ‚‚Mon C).obj A).X] = Î¼[A.X] :=
  rfl

@[simp]
theorem forgetâ‚‚Mon_map_hom {A B : Grp_ C} (f : A âŸ¶ B) : ((forgetâ‚‚Mon C).map f).hom = f.hom :=
  rfl

variable (C)

/-- The forgetful functor from group objects to the ambient category. -/
@[simps!]
def forget : Grp_ C â¥¤ C :=
  forgetâ‚‚Mon C â‹™ Mon.forget C

instance : (forget C).Faithful where

@[simp]
theorem forgetâ‚‚Mon_comp_forget : forgetâ‚‚Mon C â‹™ Mon.forget C = forget C := rfl

instance {G H : Grp_ C} {f : G âŸ¶ H} [IsIso f] : IsIso f.hom :=
  inferInstanceAs <| IsIso <| (forget C).map f

end

/-- Construct an isomorphism of group objects by giving a monoid isomorphism between the underlying
objects. -/
@[simps!]
def mkIso' {G H : C} (e : G â‰… H) [GrpObj G] [GrpObj H] [IsMonHom e.hom] : mk G â‰… mk H :=
  (fullyFaithfulForgetâ‚‚Mon C).preimageIso (Mon.mkIso' e)

/-- Construct an isomorphism of group objects by giving an isomorphism between the underlying
objects and checking compatibility with unit and multiplication only in the forward direction. -/
@[simps!]
abbrev mkIso {G H : Grp_ C} (e : G.X â‰… H.X) (one_f : Î·[G.X] â‰« e.hom = Î·[H.X] := by cat_disch)
    (mul_f : Î¼[G.X] â‰« e.hom = (e.hom âŠ—â‚˜ e.hom) â‰« Î¼[H.X] := by cat_disch) : G â‰… H :=
  have : IsMonHom e.hom := âŸ¨one_f, mul_fâŸ©
  mkIso' e

instance uniqueHomFromTrivial (A : Grp_ C) : Unique (trivial C âŸ¶ A) :=
  Mon.uniqueHomFromTrivial A.toMon

instance : HasInitial (Grp_ C) :=
  hasInitial_of_unique (trivial C)

/-! ### `Grp_ C` is cartesian-monoidal -/

variable [BraidedCategory C] {G H Hâ‚ Hâ‚‚ : Grp_ C}

@[simps! tensorObj_X tensorHom_hom]
instance instMonoidalCategoryStruct : MonoidalCategoryStruct (Grp_ C) where
  tensorObj G H := âŸ¨G.X âŠ— H.XâŸ©
  tensorHom := tensorHom (C := Mon C)
  whiskerRight f G := whiskerRight (C := Mon C) f G.toMon
  whiskerLeft G _ _ f := MonoidalCategoryStruct.whiskerLeft (C := Mon C) G.toMon f
  tensorUnit := âŸ¨ğŸ™_ CâŸ©
  associator X Y Z :=
    (Grp_.fullyFaithfulForgetâ‚‚Mon C).preimageIso (associator X.toMon Y.toMon Z.toMon)
  leftUnitor G := (Grp_.fullyFaithfulForgetâ‚‚Mon C).preimageIso (leftUnitor G.toMon)
  rightUnitor G := (Grp_.fullyFaithfulForgetâ‚‚Mon C).preimageIso (rightUnitor G.toMon)

@[simp] lemma tensorUnit_X : (ğŸ™_ (Grp_ C)).X = ğŸ™_ C := rfl

@[simp] lemma tensorUnit_one : Î·[(ğŸ™_ (Grp_ C)).X] = Î·[ğŸ™_ C] := rfl
@[simp] lemma tensorUnit_mul : Î¼[(ğŸ™_ (Grp_ C)).X] = Î¼[ğŸ™_ C] := rfl

@[simp] lemma tensorObj_one (G H : Grp_ C) : Î·[(G âŠ— H).X] = Î·[G.X âŠ— H.X] := rfl
@[simp] lemma tensorObj_mul (G H : Grp_ C) : Î¼[(G âŠ— H).X] = Î¼[G.X âŠ— H.X] := rfl

@[simp] lemma whiskerLeft_hom {G H : Grp_ C} (f : G âŸ¶ H) (I : Grp_ C) :
    (f â–· I).hom = f.hom â–· I.X := rfl

@[simp] lemma whiskerRight_hom (G : Grp_ C) {H I : Grp_ C} (f : H âŸ¶ I) :
    (G â— f).hom = G.X â— f.hom := rfl

@[simp] lemma leftUnitor_hom_hom (G : Grp_ C) : (Î»_ G).hom.hom = (Î»_ G.X).hom := rfl
@[simp] lemma leftUnitor_inv_hom (G : Grp_ C) : (Î»_ G).inv.hom = (Î»_ G.X).inv := rfl
@[simp] lemma rightUnitor_hom_hom (G : Grp_ C) : (Ï_ G).hom.hom = (Ï_ G.X).hom := rfl
@[simp] lemma rightUnitor_inv_hom (G : Grp_ C) : (Ï_ G).inv.hom = (Ï_ G.X).inv := rfl

@[simp] lemma associator_hom_hom (G H I : Grp_ C) : (Î±_ G H I).hom.hom = (Î±_ G.X H.X I.X).hom := rfl
@[simp] lemma associator_inv_hom (G H I : Grp_ C) : (Î±_ G H I).inv.hom = (Î±_ G.X H.X I.X).inv := rfl

instance instMonoidalCategory : MonoidalCategory (Grp_ C) where
  tensorHom_def := by intros; ext; simp [tensorHom_def]
  triangle _ _ := by ext; exact triangle _ _

instance instCartesianMonoidalCategory : CartesianMonoidalCategory (Grp_ C) where
  isTerminalTensorUnit :=
    .ofUniqueHom (fun G â†¦ toUnit G.toMon) fun G f â†¦ by ext; exact toUnit_unique ..
  fst G H := fst G.toMon H.toMon
  snd G H := snd G.toMon H.toMon
  tensorProductIsBinaryProduct G H :=
    BinaryFan.IsLimit.mk _ (fun {T} f g â†¦ .mk (lift f.hom g.hom))
      (by aesop_cat) (by aesop_cat) (by aesop_cat)
  fst_def G H := Mon.Hom.ext <| fst_def _ _
  snd_def G H := Mon.Hom.ext <| snd_def _ _

@[simp] lemma lift_hom (f : G âŸ¶ Hâ‚) (g : G âŸ¶ Hâ‚‚) : (lift f g).hom = lift f.hom g.hom := rfl
@[simp] lemma fst_hom (G H : Grp_ C) : (fst G H).hom = fst G.X H.X := rfl
@[simp] lemma snd_hom (G H : Grp_ C) : (snd G H).hom = snd G.X H.X := rfl

@[simps]
instance : (forgetâ‚‚Mon C).Monoidal where
  Îµ := ğŸ™ _
  Â«Î¼Â» G H := ğŸ™ _
  Â«Î·Â» := ğŸ™ _
  Î´ G H := ğŸ™ _

attribute [local simp] MonObj.tensorObj.mul_def mul_eq_mul comp_mul in
instance instBraidedCategory : BraidedCategory (Grp_ C) :=
  .ofFaithful (forgetâ‚‚Mon C) fun G H â†¦ Grp_.mkIso (Î²_ G.X H.X)

@[simp] lemma braiding_hom_hom (G H : Grp_ C) : (Î²_ G H).hom.hom = (Î²_ G.X H.X).hom := rfl
@[simp] lemma braiding_inv_hom (G H : Grp_ C) : (Î²_ G H).inv.hom = (Î²_ G.X H.X).inv := rfl

end Grp_

namespace CategoryTheory
variable
  {D : Type uâ‚‚} [Category.{vâ‚‚} D] [CartesianMonoidalCategory D]
  {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E] [CartesianMonoidalCategory E]

namespace Functor
variable {F F' : C â¥¤ D} [F.Monoidal] [F'.Monoidal] {G : D â¥¤ E} [G.Monoidal]

open scoped Obj

/-- The image of a group object under a monoidal functor is a group object. -/
abbrev grpObjObj {G : C} [GrpObj G] : GrpObj (F.obj G) where
  inv := F.map Î¹
  left_inv := by
    simp [â† Functor.map_id, Functor.Monoidal.lift_Î¼_assoc,
      Functor.Monoidal.toUnit_Îµ_assoc, â† Functor.map_comp]
  right_inv := by
    simp [â† Functor.map_id, Functor.Monoidal.lift_Î¼_assoc,
      Functor.Monoidal.toUnit_Îµ_assoc, â† Functor.map_comp]

scoped[Obj] attribute [instance] CategoryTheory.Functor.grpObjObj

@[reassoc, simp] lemma obj.Î¹_def {G : C} [GrpObj G] : Î¹[F.obj G] =  F.map Î¹ := rfl

open Monoidal

variable (F) in
/-- A finite-product-preserving functor takes group objects to group objects. -/
@[simps!]
def mapGrp : Grp_ C â¥¤ Grp_ D where
  obj A := .mk (F.obj A.X)
  map f := F.mapMon.map f

protected instance Faithful.mapGrp [F.Faithful] : F.mapGrp.Faithful where
  map_injective hfg := F.mapMon.map_injective hfg

protected instance Full.mapGrp [F.Full] [F.Faithful] : F.mapGrp.Full where
  map_surjective := F.mapMon.map_surjective

/-- If `F : C â¥¤ D` is a fully faithful monoidal functor, then `Grp(F) : Grp C â¥¤ Grp D` is fully
faithful too. -/
@[simps]
protected def FullyFaithful.mapGrp (hF : F.FullyFaithful) : F.mapGrp.FullyFaithful where
  preimage f := .mk <| hF.preimage f.hom

@[simp]
theorem mapGrp_id_one (A : Grp_ C) :
    Î·[((ğŸ­ C).mapGrp.obj A).X] = ğŸ™ _ â‰« Î·[A.X] :=
  rfl

@[simp]
theorem mapGrp_id_mul (A : Grp_ C) :
    Î¼[((ğŸ­ C).mapGrp.obj A).X] = ğŸ™ _ â‰« Î¼[A.X] :=
  rfl

@[simp]
theorem comp_mapGrp_one (A : Grp_ C) :
    Î·[((F â‹™ G).mapGrp.obj A).X] = LaxMonoidal.Îµ (F â‹™ G) â‰« (F â‹™ G).map Î·[A.X] :=
  rfl

@[simp]
theorem comp_mapGrp_mul (A : Grp_ C) :
    Î¼[((F â‹™ G).mapGrp.obj A).X] = LaxMonoidal.Î¼ (F â‹™ G) _ _ â‰« (F â‹™ G).map Î¼[A.X] :=
  rfl

/-- The identity functor is also the identity on group objects. -/
@[simps!]
def mapGrpIdIso : mapGrp (ğŸ­ C) â‰… ğŸ­ (Grp_ C) :=
  NatIso.ofComponents fun X â†¦ Grp_.mkIso (.refl _)

/-- The composition functor is also the composition on group objects. -/
@[simps!]
def mapGrpCompIso : (F â‹™ G).mapGrp â‰… F.mapGrp â‹™ G.mapGrp :=
  NatIso.ofComponents fun X â†¦ Grp_.mkIso (.refl _)

/-- Natural transformations between functors lift to group objects. -/
@[simps!]
def mapGrpNatTrans (f : F âŸ¶ F') : F.mapGrp âŸ¶ F'.mapGrp where
  app X := .mk' (f.app _)

/-- Natural isomorphisms between functors lift to group objects. -/
@[simps!]
def mapGrpNatIso (e : F â‰… F') : F.mapGrp â‰… F'.mapGrp :=
  NatIso.ofComponents fun X â†¦ Grp_.mkIso (e.app _)

attribute [local instance] Monoidal.ofChosenFiniteProducts in
/-- `mapGrp` is functorial in the left-exact functor. -/
@[simps]
noncomputable def mapGrpFunctor : (C â¥¤â‚— D) â¥¤ Grp_ C â¥¤ Grp_ D where
  obj F := F.1.mapGrp
  map {F G} Î± := { app A := .mk' (Î±.app A.X) }

/-- Pullback a group object along a fully faithful monoidal functor. -/
@[simps]
abbrev FullyFaithful.grpObj (hF : F.FullyFaithful) (X : C) [GrpObj (F.obj X)] :
    GrpObj X where
  __ := hF.monObj X
  inv := hF.preimage Î¹[F.obj X]
  left_inv := hF.map_injective <| by
    simp [FullyFaithful.monObj, OplaxMonoidal.Î·_of_cartesianMonoidalCategory]
  right_inv := hF.map_injective <| by
    simp [FullyFaithful.monObj, OplaxMonoidal.Î·_of_cartesianMonoidalCategory]

@[deprecated (since := "2025-09-13")] alias FullyFaithful.grp_Class := FullyFaithful.grpObj

attribute [local simp] MonObj.ofIso_one MonObj.ofIso_mul in
/-- The essential image of a full and faithful functor between cartesian-monoidal categories is the
same on group objects as on objects. -/
@[simp] lemma essImage_mapGrp [F.Full] [F.Faithful] {G : Grp_ D} :
    F.mapGrp.essImage G â†” F.essImage G.X where
  mp := by rintro âŸ¨H, âŸ¨eâŸ©âŸ©; exact âŸ¨H.X, âŸ¨(Grp_.forget _).mapIso eâŸ©âŸ©
  mpr := by
    rintro âŸ¨H, âŸ¨eâŸ©âŸ©
    let : GrpObj (F.obj H) := .ofIso e.symm
    let : GrpObj H := (FullyFaithful.ofFullyFaithful F).grpObj H
    refine âŸ¨âŸ¨HâŸ©, âŸ¨Grp_.mkIso e ?_ ?_âŸ©âŸ© <;> simp

end Functor

open Functor

namespace Adjunction
variable {F : C â¥¤ D} {G : D â¥¤ C} (a : F âŠ£ G) [F.Monoidal] [G.Monoidal]

/-- An adjunction of monoidal functors lifts to an adjunction of their lifts to group objects. -/
@[simps] def mapGrp : F.mapGrp âŠ£ G.mapGrp where
  unit := mapGrpIdIso.inv â‰« mapGrpNatTrans a.unit â‰« mapGrpCompIso.hom
  counit := mapGrpCompIso.inv â‰« mapGrpNatTrans a.counit â‰« mapGrpIdIso.hom

end Adjunction

namespace Equivalence
variable (e : C â‰Œ D) [e.functor.Monoidal] [e.inverse.Monoidal]

/-- An equivalence of categories lifts to an equivalence of their group objects. -/
@[simps] def mapGrp : Grp_ C â‰Œ Grp_ D where
  functor := e.functor.mapGrp
  inverse := e.inverse.mapGrp
  unitIso := mapGrpIdIso.symm â‰ªâ‰« mapGrpNatIso e.unitIso â‰ªâ‰« mapGrpCompIso
  counitIso := mapGrpCompIso.symm â‰ªâ‰« mapGrpNatIso e.counitIso â‰ªâ‰« mapGrpIdIso

end CategoryTheory.Equivalence
