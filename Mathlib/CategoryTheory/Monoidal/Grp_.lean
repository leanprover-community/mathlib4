/-
Copyright (c) 2025 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Monoidal.Cartesian.Mon_
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq
import Mathlib.CategoryTheory.Limits.ExactFunctor

/-!
# The category of groups in a cartesian monoidal category

We define group objects in cartesian monoidal categories.

We show that the associativity diagram of a group object is always cartesian and deduce that
morphisms of group objects commute with taking inverses.

We show that a finite-product-preserving functor takes group objects to group objects.
-/

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚ u

open CategoryTheory Category Limits MonoidalCategory ChosenFiniteProducts Mon_

variable (C : Type uâ‚) [Category.{vâ‚} C] [ChosenFiniteProducts.{vâ‚} C]

section

variable {C}

/-- A group object internal to a cartesian monoidal category. Also see the bundled `Grp_`. -/
class Grp_Class (X : C) extends Mon_Class X where
  /-- The inverse in a group object -/
  inv : X âŸ¶ X
  left_inv' : lift inv (ğŸ™ X) â‰« mul = toUnit _ â‰« one := by aesop_cat
  right_inv' : lift (ğŸ™ X) inv â‰« mul = toUnit _ â‰« one := by aesop_cat

namespace Mon_Class

@[inherit_doc] scoped notation "Î¹" => Grp_Class.inv
@[inherit_doc] scoped notation "Î¹["M"]" => Grp_Class.inv (X := M)

end Mon_Class

open scoped Mon_Class

namespace Grp_Class

@[reassoc (attr := simp)]
theorem left_inv (X : C) [Grp_Class X] : lift Î¹ (ğŸ™ X) â‰« Î¼ = toUnit _ â‰« Î· := left_inv'

@[reassoc (attr := simp)]
theorem right_inv (X : C) [Grp_Class X] : lift (ğŸ™ X) Î¹ â‰« Î¼ = toUnit _ â‰« Î· := right_inv'

end Grp_Class

end

/-- A group object in a cartesian monoidal category. -/
structure Grp_ extends Mon_ C where
  /-- The inversion operation -/
  inv : X âŸ¶ X
  left_inv : lift inv (ğŸ™ X) â‰« mul = toUnit _ â‰« one := by aesop_cat
  right_inv : lift (ğŸ™ X) inv â‰« mul = toUnit _ â‰« one := by aesop_cat

attribute [reassoc (attr := simp)] Grp_.left_inv
attribute [reassoc (attr := simp)] Grp_.right_inv

namespace Grp_

/-- The trivial group object. -/
@[simps!]
def trivial : Grp_ C :=
  { Mon_.trivial C with inv := ğŸ™ _ }

instance : Inhabited (Grp_ C) where
  default := trivial C

variable {C}

/-- Make a group object from `Grp_Class`. -/
def mk' (X : C) [Grp_Class X] : Grp_ C where
  __ := Mon_.mk' X
  inv := Grp_Class.inv (X := X)

instance (X : Grp_ C) : Grp_Class X.X where
  inv := X.inv
  left_inv' := X.left_inv
  right_inv' := X.right_inv

instance : Category (Grp_ C) :=
  InducedCategory.category Grp_.toMon_

@[simp]
theorem id_hom (A : Grp_ C) : Mon_.Hom.hom (ğŸ™ A) = ğŸ™ A.X :=
  rfl

@[simp]
theorem comp_hom {R S T : Grp_ C} (f : R âŸ¶ S) (g : S âŸ¶ T) :
    Mon_.Hom.hom (f â‰« g) = f.hom â‰« g.hom :=
  rfl

@[ext]
theorem hom_ext {A B : Grp_ C} (f g : A âŸ¶ B) (h : f.hom = g.hom) : f = g :=
  Mon_.Hom.ext h

@[simp]
lemma id' (A : Grp_ C) : (ğŸ™ A : A.toMon_ âŸ¶ A.toMon_) = ğŸ™ (A.toMon_) := rfl

@[simp]
lemma comp' {Aâ‚ Aâ‚‚ Aâ‚ƒ : Grp_ C} (f : Aâ‚ âŸ¶ Aâ‚‚) (g : Aâ‚‚ âŸ¶ Aâ‚ƒ) :
    ((f â‰« g : Aâ‚ âŸ¶ Aâ‚ƒ) : Aâ‚.toMon_ âŸ¶ Aâ‚ƒ.toMon_) = @CategoryStruct.comp (Mon_ C) _ _ _ _ f g := rfl

@[reassoc (attr := simp)]
theorem lift_comp_inv_right {A : C} {B : Grp_ C} (f : A âŸ¶ B.X) :
    lift f (f â‰« B.inv) â‰« B.mul = toUnit _ â‰« B.one := by
  have := f â‰«= B.right_inv
  rwa [comp_lift_assoc, comp_id, reassoc_of% toUnit_unique (f â‰« toUnit B.X) (toUnit A)] at this

@[reassoc]
theorem lift_inv_comp_right {A B : Grp_ C} (f : A âŸ¶ B) :
    lift f.hom (A.inv â‰« f.hom) â‰« B.mul = toUnit _ â‰« B.one := by
  have := A.right_inv =â‰« f.hom
  rwa [assoc, f.mul_hom, assoc, f.one_hom, lift_map_assoc, id_comp] at this

@[reassoc (attr := simp)]
theorem lift_comp_inv_left {A : C} {B : Grp_ C} (f : A âŸ¶ B.X) :
    lift (f â‰« B.inv) f â‰« B.mul = toUnit _ â‰« B.one := by
  have := f â‰«= B.left_inv
  rwa [comp_lift_assoc, comp_id, reassoc_of% toUnit_unique (f â‰« toUnit B.X) (toUnit A)] at this

@[reassoc]
theorem lift_inv_comp_left {A B : Grp_ C} (f : A âŸ¶ B) :
    lift (A.inv â‰« f.hom) f.hom â‰« B.mul = toUnit _ â‰« B.one := by
  have := A.left_inv =â‰« f.hom
  rwa [assoc, f.mul_hom, assoc, f.one_hom, lift_map_assoc, id_comp] at this

theorem eq_lift_inv_left {A : C} {B : Grp_ C} (f g h : A âŸ¶ B.X) :
    f = lift (g â‰« B.inv) h â‰« B.mul â†” lift g f â‰« B.mul = h := by
  refine âŸ¨?_, ?_âŸ© <;> (rintro rfl; simp [â† lift_lift_assoc])

theorem lift_inv_left_eq {A : C} {B : Grp_ C} (f g h : A âŸ¶ B.X) :
    lift (f â‰« B.inv) g â‰« B.mul = h â†” g = lift f h â‰« B.mul := by
  rw [eq_comm, eq_lift_inv_left, eq_comm]

theorem eq_lift_inv_right {A : C} {B : Grp_ C} (f g h : A âŸ¶ B.X) :
    f = lift g (h â‰« B.inv) â‰« B.mul â†” lift f h â‰« B.mul = g := by
  refine âŸ¨?_, ?_âŸ© <;> (rintro rfl; simp [lift_lift_assoc])

theorem lift_inv_right_eq {A : C} {B : Grp_ C} (f g h : A âŸ¶ B.X) :
    lift f (g â‰« B.inv) â‰« B.mul = h â†” f = lift h g â‰« B.mul := by
  rw [eq_comm, eq_lift_inv_right, eq_comm]

theorem lift_left_mul_ext {A : C} {B : Grp_ C} {f g : A âŸ¶ B.X} (i : A âŸ¶ B.X)
    (h : lift f i â‰« B.mul = lift g i â‰« B.mul) : f = g := by
  rwa [â† eq_lift_inv_right, lift_lift_assoc, lift_comp_inv_right, lift_comp_one_right] at h

@[reassoc (attr := simp)]
theorem inv_comp_inv (A : Grp_ C) : A.inv â‰« A.inv = ğŸ™ A.X := by
  apply lift_left_mul_ext A.inv
  rw [right_inv, â† comp_toUnit_assoc A.inv, â† left_inv, comp_lift_assoc, Category.comp_id]

instance (A : Grp_ C) : IsIso A.inv := âŸ¨A.inv, by simp, by simpâŸ©

/-- For `inv â‰« inv = ğŸ™` see `inv_comp_inv`. -/
@[simp]
theorem inv_inv (A : Grp_ C) : CategoryTheory.inv A.inv = A.inv := by
  rw [eq_comm, â† CategoryTheory.inv_comp_eq_id, IsIso.inv_inv, inv_comp_inv]

@[reassoc]
theorem mul_inv (A : Grp_ C) :
    A.mul â‰« A.inv = (Î²_ A.X A.X).hom â‰« (A.inv âŠ— A.inv) â‰« A.mul := by
  apply lift_left_mul_ext A.mul
  nth_rw 2 [â† Category.comp_id A.mul]
  rw [â† comp_lift, Category.assoc, A.left_inv, â† Category.assoc (Î²_ A.X A.X).hom,
    â† lift_snd_fst, lift_map, lift_lift_assoc]
  nth_rw 2 [â† Category.id_comp A.mul]
  rw [â† lift_fst_snd, â† lift_lift_assoc (fst A.X A.X â‰« _), lift_comp_inv_left, lift_comp_one_left,
    lift_comp_inv_left, comp_toUnit_assoc]

@[reassoc (attr := simp)]
theorem tensorHom_inv_inv_mul (A : Grp_ C) :
    (A.inv âŠ— A.inv) â‰« A.mul = (Î²_ A.X A.X).hom â‰« A.mul â‰« A.inv := by
  rw [mul_inv A, SymmetricCategory.symmetry_assoc]

/-- The map `(Â· * f)`. -/
@[simps]
def mulRight (A : Grp_ C) (f : ğŸ™_ C âŸ¶ A.X) : A.X â‰… A.X where
  hom := lift (ğŸ™ _) (toUnit _ â‰« f) â‰« A.mul
  inv := lift (ğŸ™ _) (toUnit _ â‰« f â‰« A.inv) â‰« A.mul
  hom_inv_id := by simp [comp_lift_assoc, lift_lift_assoc, â† comp_lift]
  inv_hom_id := by simp [comp_lift_assoc, lift_lift_assoc, â† comp_lift]

@[simp]
lemma mulRight_one (A : Grp_ C) : A.mulRight A.one = Iso.refl A.X := by
  ext; simp

/-- The associativity diagram of a group object is cartesian.

In fact, any monoid object whose associativity diagram is cartesian can be made into a group object
(we do not prove this in this file), so we should expect that many properties of group objects
follow from this result. -/
theorem isPullback (A : Grp_ C) :
    IsPullback (A.mul â–· A.X) ((Î±_ A.X A.X A.X).hom â‰« (A.X â— A.mul)) A.mul A.mul where
  w := by simp
  isLimit' := Nonempty.intro <| PullbackCone.IsLimit.mk _
    (fun s => lift
      (lift
        (s.snd â‰« fst _ _)
        (lift (s.snd â‰« fst _ _ â‰« A.inv) (s.fst â‰« fst _ _) â‰« A.mul))
      (s.fst â‰« snd _ _))
    (by
      refine fun s => ChosenFiniteProducts.hom_ext _ _ ?_ (by simp)
      simp only [lift_whiskerRight, lift_fst]
      rw [â† lift_lift_assoc, â† assoc, lift_comp_inv_right, lift_comp_one_left])
    (by
      refine fun s => ChosenFiniteProducts.hom_ext _ _ (by simp) ?_
      simp only [lift_lift_associator_hom_assoc, lift_whiskerLeft, lift_snd]
      have : lift (s.snd â‰« fst _ _ â‰« A.inv) (s.fst â‰« fst _ _) â‰« A.mul =
          lift (s.snd â‰« snd _ _) (s.fst â‰« snd _ _ â‰« A.inv) â‰« A.mul := by
        rw [â† assoc s.fst, eq_lift_inv_right, lift_lift_assoc, â† assoc s.snd, lift_inv_left_eq,
          lift_comp_fst_snd, lift_comp_fst_snd, s.condition]
      rw [this, lift_lift_assoc, â† assoc, lift_comp_inv_left, lift_comp_one_right])
    (by
      intro s m hmâ‚ hmâ‚‚
      refine ChosenFiniteProducts.hom_ext _ _ (ChosenFiniteProducts.hom_ext _ _ ?_ ?_) ?_
      Â· simpa using hmâ‚‚ =â‰« fst _ _
      Â· have h : m â‰« fst _ _ â‰« fst _ _ = s.snd â‰« fst _ _ := by simpa using hmâ‚‚ =â‰« fst _ _
        have := hmâ‚ =â‰« fst _ _
        simp only [assoc, whiskerRight_fst, lift_fst, lift_snd] at this âŠ¢
        rw [â† assoc, â† lift_comp_fst_snd (m â‰« _), assoc, assoc, h] at this
        rwa [â† assoc s.snd, eq_lift_inv_left]
      Â· simpa using hmâ‚ =â‰« snd _ _)

/-- Morphisms of group objects preserve inverses. -/
@[reassoc (attr := simp)]
theorem inv_hom {A B : Grp_ C} (f : A âŸ¶ B) : A.inv â‰« f.hom = f.hom â‰« B.inv := by
  suffices lift (lift f.hom (A.inv â‰« f.hom)) f.hom =
      lift (lift f.hom (f.hom â‰« B.inv)) f.hom by simpa using (this =â‰« fst _ _) =â‰« snd _ _
  apply B.isPullback.hom_ext <;> apply ChosenFiniteProducts.hom_ext <;>
    simp [lift_inv_comp_right, lift_inv_comp_left]

open Mon_Class in
@[reassoc]
lemma _root_.IsMon_Hom.inv_hom {X Y : C} [Grp_Class X] [Grp_Class Y] (f : X âŸ¶ Y) [IsMon_Hom f] :
    Î¹ â‰« f = f â‰« Î¹ :=
  Grp_.inv_hom (A := .mk' X) (B := .mk' Y) (f := âŸ¨f, IsMon_Hom.one_hom, IsMon_Hom.mul_homâŸ©)

lemma _root_.Grp_Class.toMon_Class_injective {X : C} :
    Function.Injective (@Grp_Class.toMon_Class C â€¹_â€º â€¹_â€º X) := by
  intro hâ‚ hâ‚‚ e
  let Xâ‚ : Grp_ C := @Grp_.mk' _ _ _ X hâ‚
  let Xâ‚‚ : Grp_ C := @Grp_.mk' _ _ _ X hâ‚‚
  suffices Xâ‚.inv = Xâ‚‚.inv by cases hâ‚; cases hâ‚‚; subst e this; rfl
  apply lift_left_mul_ext (ğŸ™ _)
  rw [left_inv, show Xâ‚.mul = Xâ‚‚.mul from congr(($e).mul),
    show Xâ‚.one = Xâ‚‚.one from congr(($e).one)]
  exact Xâ‚‚.left_inv.symm

@[ext]
lemma _root_.Grp_Class.ext {X : C} (hâ‚ hâ‚‚ : Grp_Class X)
    (H : hâ‚.toMon_Class = hâ‚‚.toMon_Class) : hâ‚ = hâ‚‚ :=
  Grp_Class.toMon_Class_injective H

section

variable (C)

/-- The forgetful functor from group objects to monoid objects. -/
def forgetâ‚‚Mon_ : Grp_ C â¥¤ Mon_ C :=
  inducedFunctor Grp_.toMon_

/-- The forgetful functor from group objects to monoid objects is fully faithful. -/
def fullyFaithfulForgetâ‚‚Mon_ : (forgetâ‚‚Mon_ C).FullyFaithful :=
  fullyFaithfulInducedFunctor _

instance : (forgetâ‚‚Mon_ C).Full := InducedCategory.full _
instance : (forgetâ‚‚Mon_ C).Faithful := InducedCategory.faithful _

@[simp]
theorem forgetâ‚‚Mon_obj_one (A : Grp_ C) : ((forgetâ‚‚Mon_ C).obj A).one = A.one :=
  rfl

@[simp]
theorem forgetâ‚‚Mon_obj_mul (A : Grp_ C) : ((forgetâ‚‚Mon_ C).obj A).mul = A.mul :=
  rfl

@[simp]
theorem forgetâ‚‚Mon_map_hom {A B : Grp_ C} (f : A âŸ¶ B) : ((forgetâ‚‚Mon_ C).map f).hom = f.hom :=
  rfl

/-- The forgetful functor from group objects to the ambient category. -/
@[simps!]
def forget : Grp_ C â¥¤ C :=
  forgetâ‚‚Mon_ C â‹™ Mon_.forget C

instance : (forget C).Faithful where

@[simp]
theorem forgetâ‚‚Mon_comp_forget : forgetâ‚‚Mon_ C â‹™ Mon_.forget C = forget C := rfl

end

section

variable {M N : Grp_ C} (f : M.X â‰… N.X) (one_f : M.one â‰« f.hom = N.one := by aesop_cat)
  (mul_f : M.mul â‰« f.hom = (f.hom âŠ— f.hom) â‰« N.mul := by aesop_cat)

/-- Constructor for isomorphisms in the category `Grp_ C`. -/
def mkIso : M â‰… N :=
  (fullyFaithfulForgetâ‚‚Mon_ C).preimageIso (Mon_.mkIso f one_f mul_f)

@[simp] lemma mkIso_hom_hom : (mkIso f one_f mul_f).hom.hom = f.hom := rfl
@[simp] lemma mkIso_inv_hom : (mkIso f one_f mul_f).inv.hom = f.inv := rfl

end

instance uniqueHomFromTrivial (A : Grp_ C) : Unique (trivial C âŸ¶ A) :=
  Mon_.uniqueHomFromTrivial A.toMon_

instance : HasInitial (Grp_ C) :=
  hasInitial_of_unique (trivial C)

end Grp_

namespace CategoryTheory.Functor

variable {C} {D : Type uâ‚‚} [Category.{vâ‚‚} D] [ChosenFiniteProducts.{vâ‚‚} D] (F : C â¥¤ D)
variable [PreservesFiniteProducts F]

attribute [local instance] monoidalOfChosenFiniteProducts

/-- A finite-product-preserving functor takes group objects to group objects. -/
@[simps!]
noncomputable def mapGrp : Grp_ C â¥¤ Grp_ D where
  obj A :=
    { F.mapMon.obj A.toMon_ with
      inv := F.map A.inv
      left_inv := by
        simp [â† Functor.map_id, Functor.Monoidal.lift_Î¼_assoc,
          Functor.Monoidal.toUnit_Îµ_assoc, â† Functor.map_comp]
      right_inv := by
        simp [â† Functor.map_id, Functor.Monoidal.lift_Î¼_assoc,
          Functor.Monoidal.toUnit_Îµ_assoc, â† Functor.map_comp] }
  map f := F.mapMon.map f

/-- `mapGrp` is functorial in the left-exact functor. -/
@[simps]
noncomputable def mapGrpFunctor : (C â¥¤â‚— D) â¥¤ Grp_ C â¥¤ Grp_ D where
  obj F := F.1.mapGrp
  map {F G} Î± := { app := fun A => { hom := Î±.app A.X } }

end CategoryTheory.Functor
