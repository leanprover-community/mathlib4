/-
Copyright (c) 2025 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Monoidal.Cartesian.Mon_
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq
import Mathlib.CategoryTheory.Limits.ExactFunctor

/-!
# The category of groups in a cartesian monoidal category

We define group objects in cartesian monoidal categories.

We show that the associativity diagram of a group object is always cartesian and deduce that
morphisms of group objects commute with taking inverses.

We show that a finite-product-preserving functor takes group objects to group objects.
-/

universe vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ u

open CategoryTheory Category Limits MonoidalCategory CartesianMonoidalCategory Mon_ Mon_Class

variable {C : Type uâ‚} [Category.{vâ‚} C] [CartesianMonoidalCategory.{vâ‚} C]

section

/-- A group object internal to a cartesian monoidal category. Also see the bundled `Grp_`. -/
class Grp_Class (X : C) extends Mon_Class X where
  /-- The inverse in a group object -/
  inv : X âŸ¶ X
  left_inv' : lift inv (ğŸ™ X) â‰« mul = toUnit _ â‰« one := by aesop_cat
  right_inv' : lift (ğŸ™ X) inv â‰« mul = toUnit _ â‰« one := by aesop_cat

namespace Mon_Class

@[inherit_doc] scoped notation "Î¹" => Grp_Class.inv
@[inherit_doc] scoped notation "Î¹["M"]" => Grp_Class.inv (X := M)

end Mon_Class

namespace Grp_Class

@[reassoc (attr := simp)]
theorem left_inv (X : C) [Grp_Class X] : lift Î¹ (ğŸ™ X) â‰« Î¼ = toUnit _ â‰« Î· := left_inv'

@[reassoc (attr := simp)]
theorem right_inv (X : C) [Grp_Class X] : lift (ğŸ™ X) Î¹ â‰« Î¼ = toUnit _ â‰« Î· := right_inv'

@[simps inv]
instance : Grp_Class (ğŸ™_ C) where
  inv := ğŸ™ (ğŸ™_ C)

end Grp_Class

end

variable (C) in
/-- A group object in a cartesian monoidal category. -/
structure Grp_ where
  /-- The underlying object in the ambient monoidal category -/
  X : C
  [grp : Grp_Class X]

attribute [instance] Grp_.grp

namespace Grp_

/-- A group object is a monoid object. -/
@[simps X]
def toMon_ (A : Grp_ C) : Mon_ C := âŸ¨A.XâŸ©

variable (C) in
/-- The trivial group object. -/
@[simps!]
def trivial : Grp_ C :=
  { Mon_.trivial C with grp := inferInstanceAs (Grp_Class (ğŸ™_ C)) }

instance : Inhabited (Grp_ C) where
  default := trivial C

@[deprecated (since := "2025-06-15")] alias mk' := mk

instance : Category (Grp_ C) :=
  InducedCategory.category Grp_.toMon_

@[simp]
theorem id_hom (A : Grp_ C) : Mon_.Hom.hom (ğŸ™ A) = ğŸ™ A.X :=
  rfl

@[simp]
theorem comp_hom {R S T : Grp_ C} (f : R âŸ¶ S) (g : S âŸ¶ T) :
    Mon_.Hom.hom (f â‰« g) = f.hom â‰« g.hom :=
  rfl

@[ext]
theorem hom_ext {A B : Grp_ C} (f g : A âŸ¶ B) (h : f.hom = g.hom) : f = g :=
  Mon_.Hom.ext h

@[simp]
lemma id' (A : Grp_ C) : (ğŸ™ A : A.toMon_ âŸ¶ A.toMon_) = ğŸ™ (A.toMon_) := rfl

@[simp]
lemma comp' {Aâ‚ Aâ‚‚ Aâ‚ƒ : Grp_ C} (f : Aâ‚ âŸ¶ Aâ‚‚) (g : Aâ‚‚ âŸ¶ Aâ‚ƒ) :
    ((f â‰« g : Aâ‚ âŸ¶ Aâ‚ƒ) : Aâ‚.toMon_ âŸ¶ Aâ‚ƒ.toMon_) = @CategoryStruct.comp (Mon_ C) _ _ _ _ f g := rfl

end Grp_

namespace Grp_Class

variable {A : C} {B : C}

@[reassoc (attr := simp)]
theorem lift_comp_inv_right [Grp_Class B] (f : A âŸ¶ B) :
    lift f (f â‰« Î¹) â‰« Î¼ = toUnit _ â‰« Î· := by
  have := f â‰«= right_inv B
  rwa [comp_lift_assoc, comp_id, reassoc_of% toUnit_unique (f â‰« toUnit B) (toUnit A)] at this

@[reassoc]
theorem lift_inv_comp_right [Grp_Class A] [Grp_Class B] (f : A âŸ¶ B) [IsMon_Hom f] :
    lift f (Î¹ â‰« f) â‰« Î¼ = toUnit _ â‰« Î· := by
  have := right_inv A =â‰« f
  rwa [assoc, IsMon_Hom.mul_hom, assoc, IsMon_Hom.one_hom, lift_map_assoc, id_comp] at this

@[reassoc (attr := simp)]
theorem lift_comp_inv_left [Grp_Class B] (f : A âŸ¶ B) :
    lift (f â‰« Î¹) f â‰« Î¼ = toUnit _ â‰« Î· := by
  have := f â‰«= left_inv B
  rwa [comp_lift_assoc, comp_id, reassoc_of% toUnit_unique (f â‰« toUnit B) (toUnit A)] at this

@[reassoc]
theorem lift_inv_comp_left [Grp_Class A] [Grp_Class B] (f : A âŸ¶ B) [IsMon_Hom f] :
    lift (Î¹ â‰« f) f â‰« Î¼ = toUnit _ â‰« Î· := by
  have := left_inv A =â‰« f
  rwa [assoc, IsMon_Hom.mul_hom, assoc, IsMon_Hom.one_hom, lift_map_assoc, id_comp] at this

theorem eq_lift_inv_left [Grp_Class B] (f g h : A âŸ¶ B) :
    f = lift (g â‰« Î¹) h â‰« Î¼ â†” lift g f â‰« Î¼ = h := by
  refine âŸ¨?_, ?_âŸ© <;> (rintro rfl; simp [â† lift_lift_assoc])

theorem lift_inv_left_eq [Grp_Class B] (f g h : A âŸ¶ B) :
    lift (f â‰« Î¹) g â‰« Î¼ = h â†” g = lift f h â‰« Î¼ := by
  rw [eq_comm, eq_lift_inv_left, eq_comm]

theorem eq_lift_inv_right [Grp_Class B] (f g h : A âŸ¶ B) :
    f = lift g (h â‰« Î¹) â‰« Î¼ â†” lift f h â‰« Î¼ = g := by
  refine âŸ¨?_, ?_âŸ© <;> (rintro rfl; simp [lift_lift_assoc])

theorem lift_inv_right_eq [Grp_Class B] (f g h : A âŸ¶ B) :
    lift f (g â‰« Î¹) â‰« Î¼ = h â†” f = lift h g â‰« Î¼ := by
  rw [eq_comm, eq_lift_inv_right, eq_comm]

theorem lift_left_mul_ext [Grp_Class B] {f g : A âŸ¶ B} (i : A âŸ¶ B)
    (h : lift f i â‰« Î¼ = lift g i â‰« Î¼) : f = g := by
  rwa [â† eq_lift_inv_right, lift_lift_assoc, lift_comp_inv_right, lift_comp_one_right] at h

@[reassoc (attr := simp)]
theorem inv_comp_inv (A : C) [Grp_Class A] : Î¹ â‰« Î¹ = ğŸ™ A := by
  apply lift_left_mul_ext Î¹[A]
  rw [right_inv, â† comp_toUnit_assoc Î¹, â† left_inv, comp_lift_assoc, Category.comp_id]

instance (A : C) [Grp_Class A] : IsIso Î¹[A] := âŸ¨Î¹, by simp, by simpâŸ©

/-- For `inv â‰« inv = ğŸ™` see `inv_comp_inv`. -/
@[simp]
theorem inv_inv (A : C) [Grp_Class A] : CategoryTheory.inv Î¹ = Î¹[A] := by
  rw [eq_comm, â† CategoryTheory.inv_comp_eq_id, IsIso.inv_inv, inv_comp_inv]

@[reassoc]
theorem mul_inv [BraidedCategory C] (A : C) [Grp_Class A] :
    Î¼ â‰« Î¹ = (Î²_ A A).hom â‰« (Î¹ âŠ—â‚˜ Î¹) â‰« Î¼ := by
  apply lift_left_mul_ext Î¼
  nth_rw 2 [â† Category.comp_id Î¼]
  rw [â† comp_lift, Category.assoc, left_inv, â† Category.assoc (Î²_ A A).hom,
    â† lift_snd_fst, lift_map, lift_lift_assoc]
  nth_rw 2 [â† Category.id_comp Î¼]
  rw [â† lift_fst_snd, â† lift_lift_assoc (fst A A â‰« _), lift_comp_inv_left, lift_comp_one_left,
    lift_comp_inv_left, comp_toUnit_assoc]

@[reassoc]
theorem tensorHom_inv_inv_mul [BraidedCategory C] (A : C) [Grp_Class A] :
    (Î¹[A] âŠ—â‚˜ Î¹[A]) â‰« Î¼ = (Î²_ A A).hom â‰« Î¼ â‰« Î¹ := by
  rw [mul_inv A, SymmetricCategory.symmetry_assoc]

@[reassoc]
lemma mul_inv_rev [BraidedCategory C] (G : C) [Grp_Class G] :
    Î¼ â‰« Î¹ = (Î¹[G] âŠ—â‚˜ Î¹) â‰« (Î²_ _ _).hom â‰« Î¼ := by simp [tensorHom_inv_inv_mul]

/-- The map `(Â· * f)`. -/
@[simps]
def mulRight {A : C} [Grp_Class A] (f : ğŸ™_ C âŸ¶ A) : A â‰… A where
  hom := lift (ğŸ™ _) (toUnit _ â‰« f) â‰« Î¼
  inv := lift (ğŸ™ _) (toUnit _ â‰« f â‰« Î¹) â‰« Î¼
  hom_inv_id := by simp [comp_lift_assoc, lift_lift_assoc, â† comp_lift]
  inv_hom_id := by simp [comp_lift_assoc, lift_lift_assoc, â† comp_lift]

@[simp]
lemma mulRight_one (A : C) [Grp_Class A] : mulRight Î·[A] = Iso.refl A := by
  ext; simp

/-- The associativity diagram of a group object is cartesian.

In fact, any monoid object whose associativity diagram is cartesian can be made into a group object
(we do not prove this in this file), so we should expect that many properties of group objects
follow from this result. -/
theorem isPullback (A : C) [Grp_Class A] :
    IsPullback (Î¼ â–· A) ((Î±_ A A A).hom â‰« (A â— Î¼)) Î¼ Î¼ where
  w := by simp
  isLimit' := Nonempty.intro <| PullbackCone.IsLimit.mk _
    (fun s => lift
      (lift
        (s.snd â‰« fst _ _)
        (lift (s.snd â‰« fst _ _ â‰« Î¹) (s.fst â‰« fst _ _) â‰« Î¼))
      (s.fst â‰« snd _ _))
    (by
      refine fun s => CartesianMonoidalCategory.hom_ext _ _ ?_ (by simp)
      simp only [lift_whiskerRight, lift_fst]
      rw [â† lift_lift_assoc, â† assoc, lift_comp_inv_right, lift_comp_one_left])
    (by
      refine fun s => CartesianMonoidalCategory.hom_ext _ _ (by simp) ?_
      simp only [lift_lift_associator_hom_assoc, lift_whiskerLeft, lift_snd]
      have : lift (s.snd â‰« fst _ _ â‰« Î¹) (s.fst â‰« fst _ _) â‰« Î¼ =
          lift (s.snd â‰« snd _ _) (s.fst â‰« snd _ _ â‰« Î¹) â‰« Î¼ := by
        rw [â† assoc s.fst, eq_lift_inv_right, lift_lift_assoc, â† assoc s.snd, lift_inv_left_eq,
          lift_comp_fst_snd, lift_comp_fst_snd, s.condition]
      rw [this, lift_lift_assoc, â† assoc, lift_comp_inv_left, lift_comp_one_right])
    (by
      intro s m hmâ‚ hmâ‚‚
      refine CartesianMonoidalCategory.hom_ext _ _ (CartesianMonoidalCategory.hom_ext _ _ ?_ ?_) ?_
      Â· simpa using hmâ‚‚ =â‰« fst _ _
      Â· have h : m â‰« fst _ _ â‰« fst _ _ = s.snd â‰« fst _ _ := by simpa using hmâ‚‚ =â‰« fst _ _
        have := hmâ‚ =â‰« fst _ _
        simp only [assoc, whiskerRight_fst, lift_fst, lift_snd] at this âŠ¢
        rw [â† assoc, â† lift_comp_fst_snd (m â‰« _), assoc, assoc, h] at this
        rwa [â† assoc s.snd, eq_lift_inv_left]
      Â· simpa using hmâ‚ =â‰« snd _ _)

/-- Morphisms of group objects preserve inverses. -/
@[reassoc (attr := simp)]
theorem inv_hom [Grp_Class A] [Grp_Class B] (f : A âŸ¶ B) [IsMon_Hom f] : Î¹ â‰« f = f â‰« Î¹ := by
  suffices lift (lift f (Î¹ â‰« f)) f =
      lift (lift f (f â‰« Î¹)) f by simpa using (this =â‰« fst _ _) =â‰« snd _ _
  apply (isPullback B).hom_ext <;> apply CartesianMonoidalCategory.hom_ext <;>
    simp [lift_inv_comp_right, lift_inv_comp_left]

lemma toMon_Class_injective {X : C} :
    Function.Injective (@Grp_Class.toMon_Class C â€¹_â€º â€¹_â€º X) := by
  intro hâ‚ hâ‚‚ e
  let Xâ‚ : Grp_ C := @Grp_.mk _ _ _ X hâ‚
  let Xâ‚‚ : Grp_ C := @Grp_.mk _ _ _ X hâ‚‚
  suffices hâ‚.inv = hâ‚‚.inv by cases hâ‚; congr!
  apply lift_left_mul_ext (ğŸ™ _)
  rw [left_inv]
  convert @left_inv _ _ _ _ hâ‚ using 2
  exacts [congr(($e.symm).mul), congr(($e.symm).one)]

@[ext]
lemma _root_.Grp_Class.ext {X : C} (hâ‚ hâ‚‚ : Grp_Class X)
    (H : hâ‚.toMon_Class = hâ‚‚.toMon_Class) : hâ‚ = hâ‚‚ :=
  Grp_Class.toMon_Class_injective H

end Grp_Class

namespace Grp_

section

variable (C)

/-- The forgetful functor from group objects to monoid objects. -/
def forgetâ‚‚Mon_ : Grp_ C â¥¤ Mon_ C :=
  inducedFunctor Grp_.toMon_

/-- The forgetful functor from group objects to monoid objects is fully faithful. -/
def fullyFaithfulForgetâ‚‚Mon_ : (forgetâ‚‚Mon_ C).FullyFaithful :=
  fullyFaithfulInducedFunctor _

instance : (forgetâ‚‚Mon_ C).Full := InducedCategory.full _
instance : (forgetâ‚‚Mon_ C).Faithful := InducedCategory.faithful _

variable {C}

@[simp]
theorem forgetâ‚‚Mon_obj_one (A : Grp_ C) : Î·[((forgetâ‚‚Mon_ C).obj A).X] = Î·[A.X] :=
  rfl

@[simp]
theorem forgetâ‚‚Mon_obj_mul (A : Grp_ C) : Î¼[((forgetâ‚‚Mon_ C).obj A).X] = Î¼[A.X] :=
  rfl

@[simp]
theorem forgetâ‚‚Mon_map_hom {A B : Grp_ C} (f : A âŸ¶ B) : ((forgetâ‚‚Mon_ C).map f).hom = f.hom :=
  rfl

variable (C)

/-- The forgetful functor from group objects to the ambient category. -/
@[simps!]
def forget : Grp_ C â¥¤ C :=
  forgetâ‚‚Mon_ C â‹™ Mon_.forget C

instance : (forget C).Faithful where

@[simp]
theorem forgetâ‚‚Mon_comp_forget : forgetâ‚‚Mon_ C â‹™ Mon_.forget C = forget C := rfl

end

section

variable {M N : Grp_ C} (f : M.X â‰… N.X) (one_f : Î·[M.X] â‰« f.hom = Î·[N.X] := by aesop_cat)
  (mul_f : Î¼[M.X] â‰« f.hom = (f.hom âŠ—â‚˜ f.hom) â‰« Î¼[N.X] := by aesop_cat)

/-- Constructor for isomorphisms in the category `Grp_ C`. -/
def mkIso : M â‰… N :=
  (fullyFaithfulForgetâ‚‚Mon_ C).preimageIso (Mon_.mkIso f one_f mul_f)

@[simp] lemma mkIso_hom_hom : (mkIso f one_f mul_f).hom.hom = f.hom := rfl
@[simp] lemma mkIso_inv_hom : (mkIso f one_f mul_f).inv.hom = f.inv := rfl

end

instance uniqueHomFromTrivial (A : Grp_ C) : Unique (trivial C âŸ¶ A) :=
  Mon_.uniqueHomFromTrivial A.toMon_

instance : HasInitial (Grp_ C) :=
  hasInitial_of_unique (trivial C)

end Grp_

namespace CategoryTheory
variable
  {D : Type uâ‚‚} [Category.{vâ‚‚} D] [CartesianMonoidalCategory D]
  {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E] [CartesianMonoidalCategory E]

namespace Functor
variable {F F' : C â¥¤ D} [F.Monoidal] [F'.Monoidal] {G : D â¥¤ E} [G.Monoidal]

open Monoidal

variable (F) in
/-- A finite-product-preserving functor takes group objects to group objects. -/
@[simps!]
noncomputable def mapGrp : Grp_ C â¥¤ Grp_ D where
  obj A :=
    { F.mapMon.obj A.toMon_ with
      grp :=
      { inv := F.map Î¹[A.X]
        left_inv' := by
          simp [â† Functor.map_id, Functor.Monoidal.lift_Î¼_assoc,
            Functor.Monoidal.toUnit_Îµ_assoc, â† Functor.map_comp]
        right_inv' := by
          simp [â† Functor.map_id, Functor.Monoidal.lift_Î¼_assoc,
            Functor.Monoidal.toUnit_Îµ_assoc, â† Functor.map_comp] } }
  map f := F.mapMon.map f

@[simp]
theorem mapGrp_id_one (A : Grp_ C) :
    Î·[((ğŸ­ C).mapGrp.obj A).X] = ğŸ™ _ â‰« Î·[A.X] :=
  rfl

@[simp]
theorem mapGrp_id_mul (A : Grp_ C) :
    Î¼[((ğŸ­ C).mapGrp.obj A).X] = ğŸ™ _ â‰« Î¼[A.X] :=
  rfl

@[simp]
theorem comp_mapGrp_one (A : Grp_ C) :
    Î·[((F â‹™ G).mapGrp.obj A).X] = LaxMonoidal.Îµ (F â‹™ G) â‰« (F â‹™ G).map Î·[A.X] :=
  rfl

@[simp]
theorem comp_mapGrp_mul (A : Grp_ C) :
    Î¼[((F â‹™ G).mapGrp.obj A).X] = LaxMonoidal.Î¼ (F â‹™ G) _ _ â‰« (F â‹™ G).map Î¼[A.X] :=
  rfl

/-- The identity functor is also the identity on group objects. -/
@[simps!]
noncomputable def mapGrpIdIso : mapGrp (ğŸ­ C) â‰… ğŸ­ (Grp_ C) :=
  NatIso.ofComponents (fun X â†¦ Grp_.mkIso (.refl _) (by simp [Îµ_of_cartesianMonoidalCategory])
    (by simp [Î¼_of_cartesianMonoidalCategory]))

/-- The composition functor is also the composition on group objects. -/
@[simps!]
noncomputable def mapGrpCompIso : (F â‹™ G).mapGrp â‰… F.mapGrp â‹™ G.mapGrp :=
  NatIso.ofComponents (fun X â†¦ Grp_.mkIso (.refl _) (by simp [Îµ_of_cartesianMonoidalCategory])
    (by simp [Î¼_of_cartesianMonoidalCategory]))

/-- Natural transformations between functors lift to group objects. -/
@[simps!]
noncomputable def mapGrpNatTrans (f : F âŸ¶ F') : F.mapGrp âŸ¶ F'.mapGrp where
  app X := .mk' (f.app _)

/-- Natural isomorphisms between functors lift to group objects. -/
@[simps!]
noncomputable def mapGrpNatIso (e : F â‰… F') : F.mapGrp â‰… F'.mapGrp :=
  NatIso.ofComponents fun X â†¦ Grp_.mkIso (e.app _)

attribute [local instance] Monoidal.ofChosenFiniteProducts in
/-- `mapGrp` is functorial in the left-exact functor. -/
@[simps]
noncomputable def mapGrpFunctor : (C â¥¤â‚— D) â¥¤ Grp_ C â¥¤ Grp_ D where
  obj F := F.1.mapGrp
  map {F G} Î± := { app A := .mk' (Î±.app A.X) }

end Functor

open Functor

namespace Adjunction
variable {F : C â¥¤ D} {G : D â¥¤ C} (a : F âŠ£ G) [F.Monoidal] [G.Monoidal]

/-- An adjunction of monoidal functors lifts to an adjunction of their lifts to group objects. -/
@[simps] noncomputable def mapGrp : F.mapGrp âŠ£ G.mapGrp where
  unit := mapGrpIdIso.inv â‰« mapGrpNatTrans a.unit â‰« mapGrpCompIso.hom
  counit := mapGrpCompIso.inv â‰« mapGrpNatTrans a.counit â‰« mapGrpIdIso.hom

end Adjunction

namespace Equivalence
variable (e : C â‰Œ D) [e.functor.Monoidal] [e.inverse.Monoidal]

/-- An equivalence of categories lifts to an equivalence of their group objects. -/
@[simps] noncomputable def mapGrp : Grp_ C â‰Œ Grp_ D where
  functor := e.functor.mapGrp
  inverse := e.inverse.mapGrp
  unitIso := mapGrpIdIso.symm â‰ªâ‰« mapGrpNatIso e.unitIso â‰ªâ‰« mapGrpCompIso
  counitIso := mapGrpCompIso.symm â‰ªâ‰« mapGrpNatIso e.counitIso â‰ªâ‰« mapGrpIdIso

end CategoryTheory.Equivalence
