/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Monoidal.NaturalTransformation

#align_import category_theory.monoidal.transport from "leanprover-community/mathlib"@"31529827d0f68d1fbd429edc393a928f677f4aba"

/-!
# Transport a monoidal structure along an equivalence.

When `C` and `D` are equivalent as categories,
we can transport a monoidal structure on `C` along the equivalence as
`CategoryTheory.Monoidal.transport`, obtaining a monoidal structure on `D`.

More generally, we can transport the lawfulness of a monoidal structure along a suitable faithful
functor, as `CategoryTheory.Monoidal.induced`.
The comparison is analogous to the difference between `Equiv.monoid` and
`Function.Injective.Monoid`.

We then upgrade the original functor and its inverse to monoidal functors
with respect to the new monoidal structure on `D`.
-/


universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

noncomputable section

open CategoryTheory

open CategoryTheory.Category

open CategoryTheory.MonoidalCategory

namespace CategoryTheory.Monoidal

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] [MonoidalCategory.{v‚ÇÅ} C]

variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]

/-- The data needed to induce a `MonoidalCategory` via the functor `F`; namely, pre-existing
definitions of `‚äó`, `ùüô_`, `‚ñ∑`, `‚óÅ` that are preserved by `F`.
-/
structure InducingFunctorData (F : D ‚•§ C) where
  tensorObj : D ‚Üí D ‚Üí D
  /-- Analogous to the reversed version of `CategoryTheory.LaxMonoidalFunctor.ŒºIso` -/
  ŒºIsoSymm : ‚àÄ X Y,
    F.obj (tensorObj X Y) ‚âÖ F.obj X ‚äó F.obj Y
  whiskerLeft : ‚àÄ (X : D) {Y‚ÇÅ Y‚ÇÇ : D} (_f : Y‚ÇÅ ‚ü∂ Y‚ÇÇ), tensorObj X Y‚ÇÅ ‚ü∂ tensorObj X Y‚ÇÇ
  whiskerLeft_eq : ‚àÄ (X : D) {Y‚ÇÅ Y‚ÇÇ : D} (f : Y‚ÇÅ ‚ü∂ Y‚ÇÇ),
    F.map (whiskerLeft X f)
      = (ŒºIsoSymm _ _).hom ‚â´ (F.obj X ‚óÅ F.map f) ‚â´ (ŒºIsoSymm _ _).inv :=
    by aesop_cat
  whiskerRight : ‚àÄ {X‚ÇÅ X‚ÇÇ : D} (_f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (Y : D), tensorObj X‚ÇÅ Y ‚ü∂ tensorObj X‚ÇÇ Y
  whiskerRight_eq : ‚àÄ {X‚ÇÅ X‚ÇÇ : D} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (Y : D),
    F.map (whiskerRight f Y)
      = (ŒºIsoSymm _ _).hom ‚â´ (F.map f ‚ñ∑ F.obj Y) ‚â´ (ŒºIsoSymm _ _).inv :=
    by aesop_cat
  tensorHom :
    ‚àÄ {X‚ÇÅ Y‚ÇÅ X‚ÇÇ Y‚ÇÇ : D} (_f : X‚ÇÅ ‚ü∂ Y‚ÇÅ) (_g : X‚ÇÇ ‚ü∂ Y‚ÇÇ), tensorObj X‚ÇÅ X‚ÇÇ ‚ü∂ tensorObj Y‚ÇÅ Y‚ÇÇ
  tensorHom_eq :
    ‚àÄ {X‚ÇÅ Y‚ÇÅ X‚ÇÇ Y‚ÇÇ : D} (f : X‚ÇÅ ‚ü∂ Y‚ÇÅ) (g : X‚ÇÇ ‚ü∂ Y‚ÇÇ),
      F.map (tensorHom f g)
        = (ŒºIsoSymm _ _).hom ‚â´ (F.map f ‚äó F.map g) ‚â´ (ŒºIsoSymm _ _).inv :=
    by aesop_cat
  tensorUnit' : D
  /-- Analogous to the reversed version of `CategoryTheory.LaxMonoidalFunctor.ŒµIso` -/
  ŒµIsoSymm : F.obj tensorUnit' ‚âÖ ùüô_ _
  associator : ‚àÄ X Y Z : D, tensorObj (tensorObj X Y) Z ‚âÖ tensorObj X (tensorObj Y Z)
  associator_eq : ‚àÄ X Y Z : D,
    F.map (associator X Y Z).hom =
      ((ŒºIsoSymm _ _ ‚â™‚â´ (ŒºIsoSymm _ _ ‚äó .refl _))
        ‚â™‚â´ Œ±_ (F.obj X) (F.obj Y) (F.obj Z)
        ‚â™‚â´ ((.refl _ ‚äó (ŒºIsoSymm _ _).symm) ‚â™‚â´ (ŒºIsoSymm _ _).symm)).hom :=
    by aesop_cat
  leftUnitor : ‚àÄ X : D, tensorObj tensorUnit' X ‚âÖ X
  leftUnitor_eq : ‚àÄ X : D,
    F.map (leftUnitor X).hom =
      ((ŒºIsoSymm _ _ ‚â™‚â´ (ŒµIsoSymm ‚äó .refl _)) ‚â™‚â´ Œª_ (F.obj X)).hom :=
    by aesop_cat
  rightUnitor : ‚àÄ X : D, tensorObj X tensorUnit' ‚âÖ X
  rightUnitor_eq : ‚àÄ X : D,
    F.map (rightUnitor X).hom =
      ((ŒºIsoSymm _ _ ‚â™‚â´ (.refl _ ‚äó ŒµIsoSymm)) ‚â™‚â´ œÅ_ (F.obj X)).hom :=
    by aesop_cat

attribute [inherit_doc MonoidalCategory.tensorObj] InducingFunctorData.tensorObj
attribute [inherit_doc MonoidalCategory.whiskerLeft] InducingFunctorData.whiskerLeft
attribute [inherit_doc MonoidalCategory.whiskerRight] InducingFunctorData.whiskerRight
attribute [inherit_doc MonoidalCategory.tensorHom] InducingFunctorData.tensorHom
attribute [inherit_doc MonoidalCategory.tensorUnit'] InducingFunctorData.tensorUnit'
attribute [inherit_doc MonoidalCategory.associator] InducingFunctorData.associator
attribute [inherit_doc MonoidalCategory.leftUnitor] InducingFunctorData.leftUnitor
attribute [inherit_doc MonoidalCategory.rightUnitor] InducingFunctorData.rightUnitor

-- these are theorems so don't need docstrings (std4#217)
attribute [nolint docBlame]
  InducingFunctorData.whiskerLeft_eq
  InducingFunctorData.whiskerRight_eq
  InducingFunctorData.tensorHom_eq
  InducingFunctorData.associator_eq
  InducingFunctorData.leftUnitor_eq
  InducingFunctorData.rightUnitor_eq

/--
Induce the lawfulness of the monoidal structure along an faithful functor of (plain) categories,
where the operations are already defined on the destination type `D`.

The functor `F` must preserve all the data parts of the monoidal structure between the two
categories.

-/
@[simps]
abbrev induced (F : D ‚•§ C) [Faithful F] (fData : InducingFunctorData F):
    MonoidalCategory.{v‚ÇÇ} D where
  -- the data fields are exactly as provided
  tensorObj := fData.tensorObj
  whiskerLeft := fData.whiskerLeft
  whiskerRight := fData.whiskerRight
  tensorHom := fData.tensorHom
  tensorUnit' := fData.tensorUnit'
  associator := fData.associator
  leftUnitor := fData.leftUnitor
  rightUnitor := fData.rightUnitor
  tensorHom_def {X‚ÇÅ Y‚ÇÅ X‚ÇÇ Y‚ÇÇ} f g := F.map_injective <| by
    dsimp
    rw [fData.tensorHom_eq, Functor.map_comp, fData.whiskerRight_eq, fData.whiskerLeft_eq]
    simp only [tensorHom_def, assoc, Iso.inv_hom_id_assoc]
  tensor_id X‚ÇÅ X‚ÇÇ := F.map_injective <| by cases fData; aesop_cat
  tensor_comp {X‚ÇÅ Y‚ÇÅ Z‚ÇÅ X‚ÇÇ Y‚ÇÇ Z‚ÇÇ} f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ := F.map_injective <| by cases fData; aesop_cat
  whiskerLeft_id X Y := F.map_injective <| by simp [fData.whiskerLeft_eq]
  id_whiskerRight X Y := F.map_injective <| by simp [fData.whiskerRight_eq]
  triangle X Y := F.map_injective <| by cases fData; aesop_cat
  pentagon W X Y Z := F.map_injective <| by
    have := MonoidalCategory.pentagon (F.obj W) (F.obj X) (F.obj Y) (F.obj Z)
    dsimp
    simp only [Functor.map_comp, fData.tensorHom_eq, fData.associator_eq, Iso.trans_assoc,
      Iso.trans_hom, tensorIso_hom, Iso.refl_hom, Iso.symm_hom, Functor.map_id, comp_tensor_id,
      associator_conjugation, tensor_id, assoc, id_tensor_comp, Iso.inv_hom_id_assoc,
      tensor_inv_hom_id_assoc, id_comp, inv_hom_id_tensor_assoc, id_tensor_comp_tensor_id_assoc,
      Iso.cancel_iso_hom_left]
    congr 1
    simp only [‚Üêassoc]
    congr 2
    simp only [assoc, ‚Üêtensor_comp, id_comp, Iso.inv_hom_id, tensor_id]
    congr 1
    conv_rhs => rw [‚Üêtensor_id_comp_id_tensor]
    simp only [assoc]
    congr 1
    rw [Iso.inv_comp_eq]
    conv_lhs => rw [‚Üêid_comp (ùüô (F.obj W)), tensor_comp]
    slice_lhs 0 2 => rw [this]
    rw [assoc]
    congr 1
    rw [‚Üêassociator_naturality, tensor_id]
  leftUnitor_naturality {X Y : D} f := F.map_injective <| by
    have := leftUnitor_naturality (F.map f)
    dsimp
    simp only [Functor.map_comp, fData.tensorHom_eq, Functor.map_id, fData.leftUnitor_eq,
      Iso.trans_assoc, Iso.trans_hom, tensorIso_hom, Iso.refl_hom, assoc, Iso.inv_hom_id_assoc,
      id_tensor_comp_tensor_id_assoc, Iso.cancel_iso_hom_left]
    rw [‚Üêthis, ‚Üêassoc, ‚Üêtensor_comp, id_comp, comp_id]
  rightUnitor_naturality {X Y : D} f := F.map_injective <| by
    have := rightUnitor_naturality (F.map f)
    dsimp
    simp only [Functor.map_comp, fData.tensorHom_eq, Functor.map_id, fData.rightUnitor_eq,
      Iso.trans_assoc, Iso.trans_hom, tensorIso_hom, Iso.refl_hom, assoc, Iso.inv_hom_id_assoc,
      tensor_id_comp_id_tensor_assoc, Iso.cancel_iso_hom_left]
    rw [‚Üêthis, ‚Üêassoc, ‚Üêtensor_comp, id_comp, comp_id]
  associator_naturality {X‚ÇÅ X‚ÇÇ X‚ÇÉ Y‚ÇÅ Y‚ÇÇ Y‚ÇÉ} f‚ÇÅ f‚ÇÇ f‚ÇÉ := F.map_injective <| by
    have := associator_naturality (F.map f‚ÇÅ) (F.map f‚ÇÇ) (F.map f‚ÇÉ)
    dsimp
    simp [fData.associator_eq, fData.tensorHom_eq]
    simp_rw [‚Üêassoc, ‚Üêtensor_comp, assoc, Iso.inv_hom_id, ‚Üêassoc]
    congr 1
    conv_rhs => rw [‚Üêcomp_id (F.map f‚ÇÅ), ‚Üêid_comp (F.map f‚ÇÅ)]
    simp only [tensor_comp]
    simp only [tensor_id, comp_id, assoc, tensor_inv_hom_id_assoc, id_comp]
    slice_rhs 2 3 => rw [‚Üêthis]
    simp only [‚Üê assoc, Iso.inv_hom_id, comp_id]
    congr 2
    simp_rw [‚Üêtensor_comp, id_comp]


/--
We can upgrade `F` to a monoidal functor from `D` to `E` with the induced structure.
-/
@[simps]
def fromInduced (F : D ‚•§ C) [Faithful F] (fData : InducingFunctorData F):
    letI := induced F fData
    MonoidalFunctor D C :=
  letI := induced F fData
  { toFunctor := F
    Œµ := fData.ŒµIsoSymm.inv
    Œº := fun X Y => (fData.ŒºIsoSymm X Y).inv
    Œº_natural := by cases fData; aesop_cat
    associativity := by cases fData; aesop_cat
    left_unitality := by cases fData; aesop_cat
    right_unitality := by cases fData; aesop_cat }

/-- Transport a monoidal structure along an equivalence of (plain) categories.
-/
def transport (e : C ‚âå D) : MonoidalCategory.{v‚ÇÇ} D :=
  induced e.inverse
    { tensorObj := fun X Y => e.functor.obj (e.inverse.obj X ‚äó e.inverse.obj Y)
      ŒºIsoSymm := fun X Y => (e.unitIso.app _).symm
      whiskerLeft := fun X _ _ f ‚Ü¶ e.functor.map (e.inverse.obj X ‚óÅ e.inverse.map f)
      whiskerRight := fun f X ‚Ü¶ e.functor.map (e.inverse.map f ‚ñ∑ e.inverse.obj X)
      tensorHom := fun f g => e.functor.map (e.inverse.map f ‚äó e.inverse.map g)
      tensorUnit' := e.functor.obj (ùüô_ C)
      ŒµIsoSymm := (e.unitIso.app _).symm
      associator := fun X Y Z =>
        e.functor.mapIso
          (((e.unitIso.app _).symm ‚äó Iso.refl _) ‚â™‚â´
            Œ±_ (e.inverse.obj X) (e.inverse.obj Y) (e.inverse.obj Z) ‚â™‚â´
            (Iso.refl _ ‚äó e.unitIso.app _))
      leftUnitor := fun X =>
        e.functor.mapIso (((e.unitIso.app _).symm ‚äó Iso.refl _) ‚â™‚â´ Œª_ (e.inverse.obj X)) ‚â™‚â´
          e.counitIso.app _
      rightUnitor := fun X =>
        e.functor.mapIso ((Iso.refl _ ‚äó (e.unitIso.app _).symm) ‚â™‚â´ œÅ_ (e.inverse.obj X)) ‚â™‚â´
          e.counitIso.app _ }
#align category_theory.monoidal.transport CategoryTheory.Monoidal.transport

/-- A type synonym for `D`, which will carry the transported monoidal structure. -/
@[nolint unusedArguments]
def Transported (_ : C ‚âå D) := D
#align category_theory.monoidal.transported CategoryTheory.Monoidal.Transported

instance (e : C ‚âå D) : Category (Transported e) := (inferInstance : Category D)

instance Transported.instMonoidalCategory (e : C ‚âå D): MonoidalCategory (Transported e) :=
  transport e

instance (e : C ‚âå D) : Inhabited (Transported e) :=
  ‚ü®ùüô_ _‚ü©

/-- We can upgrade `e.inverse` to a monoidal functor from `D` with the transported structure to `C`.
-/
@[simps!]
def fromTransported (e : C ‚âå D) : MonoidalFunctor (Transported e) C := fromInduced e.inverse _
#align category_theory.monoidal.from_transported CategoryTheory.Monoidal.fromTransported

instance instIsEquivalence_fromTransported (e : C ‚âå D) :
    IsEquivalence (fromTransported e).toFunctor := by
  dsimp [fromTransported]
  infer_instance

#noalign category_theory.monoidal.lax_to_transported

/-- We can upgrade `e.functor` to a monoidal functor from `C` to `D` with the transported structure.
-/
@[simps!]
def toTransported (e : C ‚âå D) : MonoidalFunctor C (Transported e) :=
  monoidalInverse (fromTransported e)
#align category_theory.monoidal.to_transported CategoryTheory.Monoidal.toTransported

instance (e : C ‚âå D) : IsEquivalence (toTransported e).toFunctor := by
  dsimp [toTransported]
  infer_instance

/-- The unit isomorphism upgrades to a monoidal isomorphism. -/
@[simps! hom inv]
def transportedMonoidalUnitIso (e : C ‚âå D) :
    LaxMonoidalFunctor.id C ‚âÖ
      (toTransported e).toLaxMonoidalFunctor ‚äó‚ãô (fromTransported e).toLaxMonoidalFunctor :=
  asIso (monoidalCounit (fromTransported e)) |>.symm
#align category_theory.monoidal.transported_monoidal_unit_iso CategoryTheory.Monoidal.transportedMonoidalUnitIso

/-- The counit isomorphism upgrades to a monoidal isomorphism. -/
@[simps! hom inv]
def transportedMonoidalCounitIso (e : C ‚âå D) :
    (fromTransported e).toLaxMonoidalFunctor ‚äó‚ãô (toTransported e).toLaxMonoidalFunctor ‚âÖ
      LaxMonoidalFunctor.id (Transported e) :=
  asIso (monoidalUnit (fromTransported e)) |>.symm
#align category_theory.monoidal.transported_monoidal_counit_iso CategoryTheory.Monoidal.transportedMonoidalCounitIso

end CategoryTheory.Monoidal
