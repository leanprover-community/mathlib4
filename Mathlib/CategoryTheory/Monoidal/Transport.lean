/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Monoidal.NaturalTransformation

/-!
# Transport a monoidal structure along an equivalence.

When `C` and `D` are equivalent as categories,
we can transport a monoidal structure on `C` along the equivalence as
`CategoryTheory.Monoidal.transport`, obtaining a monoidal structure on `D`.

More generally, we can transport the lawfulness of a monoidal structure along a suitable faithful
functor, as `CategoryTheory.Monoidal.induced`.
The comparison is analogous to the difference between `Equiv.monoid` and
`Function.Injective.monoid`.

We then upgrade the original functor and its inverse to monoidal functors
with respect to the new monoidal structure on `D`.
-/


universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

noncomputable section

open CategoryTheory

open CategoryTheory.Category

open CategoryTheory.MonoidalCategory

namespace CategoryTheory.Monoidal

variable {C : Type uâ‚} [Category.{vâ‚} C] [MonoidalCategory.{vâ‚} C]
variable {D : Type uâ‚‚} [Category.{vâ‚‚} D]

/-- The data needed to induce a `MonoidalCategory` via the functor `F`; namely, pre-existing
definitions of `âŠ—`, `ğŸ™_`, `â–·`, `â—` that are preserved by `F`.
-/
structure InducingFunctorData [MonoidalCategoryStruct D] (F : D â¥¤ C) where
  /-- Analogous to `CategoryTheory.LaxMonoidalFunctor.Î¼Iso` -/
  Î¼Iso : âˆ€ X Y,
    F.obj X âŠ— F.obj Y â‰… F.obj (X âŠ— Y)
  whiskerLeft_eq : âˆ€ (X : D) {Yâ‚ Yâ‚‚ : D} (f : Yâ‚ âŸ¶ Yâ‚‚),
    F.map (X â— f) = (Î¼Iso _ _).inv â‰« (F.obj X â— F.map f) â‰« (Î¼Iso _ _).hom := by
    aesop_cat
  whiskerRight_eq : âˆ€ {Xâ‚ Xâ‚‚ : D} (f : Xâ‚ âŸ¶ Xâ‚‚) (Y : D),
    F.map (f â–· Y) = (Î¼Iso _ _).inv â‰« (F.map f â–· F.obj Y) â‰« (Î¼Iso _ _).hom := by
    aesop_cat
  tensorHom_eq : âˆ€ {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : D} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚),
    F.map (f âŠ—â‚˜ g) = (Î¼Iso _ _).inv â‰« (F.map f âŠ—â‚˜ F.map g) â‰« (Î¼Iso _ _).hom := by
    aesop_cat
  /-- Analogous to `CategoryTheory.LaxMonoidalFunctor.ÎµIso` -/
  ÎµIso : ğŸ™_ _ â‰… F.obj (ğŸ™_ _)
  associator_eq : âˆ€ X Y Z : D,
    F.map (Î±_ X Y Z).hom =
      (((Î¼Iso _ _).symm â‰ªâ‰« ((Î¼Iso _ _).symm âŠ—áµ¢ .refl _))
        â‰ªâ‰« Î±_ (F.obj X) (F.obj Y) (F.obj Z)
        â‰ªâ‰« ((.refl _ âŠ—áµ¢ Î¼Iso _ _) â‰ªâ‰« Î¼Iso _ _)).hom := by
    aesop_cat
  leftUnitor_eq : âˆ€ X : D,
    F.map (Î»_ X).hom =
      (((Î¼Iso _ _).symm â‰ªâ‰« (ÎµIso.symm âŠ—áµ¢ .refl _)) â‰ªâ‰« Î»_ (F.obj X)).hom := by
    aesop_cat
  rightUnitor_eq : âˆ€ X : D,
    F.map (Ï_ X).hom =
      (((Î¼Iso _ _).symm â‰ªâ‰« (.refl _ âŠ—áµ¢ ÎµIso.symm)) â‰ªâ‰« Ï_ (F.obj X)).hom := by
    aesop_cat

/--
Induce the lawfulness of the monoidal structure along an faithful functor of (plain) categories,
where the operations are already defined on the destination type `D`.

The functor `F` must preserve all the data parts of the monoidal structure between the two
categories.
-/
def induced [MonoidalCategoryStruct D] (F : D â¥¤ C) [F.Faithful]
    (fData : InducingFunctorData F) :
    MonoidalCategory.{vâ‚‚} D where
  tensorHom_def {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚} f g := F.map_injective <| by
    rw [fData.tensorHom_eq, Functor.map_comp, fData.whiskerRight_eq, fData.whiskerLeft_eq]
    simp only [tensorHom_def, assoc, Iso.hom_inv_id_assoc]
  tensor_id Xâ‚ Xâ‚‚ := F.map_injective <| by cases fData; aesop_cat
  tensor_comp {Xâ‚ Yâ‚ Zâ‚ Xâ‚‚ Yâ‚‚ Zâ‚‚} fâ‚ fâ‚‚ gâ‚ gâ‚‚ := F.map_injective <| by cases fData; aesop_cat
  whiskerLeft_id X Y := F.map_injective <| by simp [fData.whiskerLeft_eq]
  id_whiskerRight X Y := F.map_injective <| by simp [fData.whiskerRight_eq]
  triangle X Y := F.map_injective <| by cases fData; aesop_cat
  pentagon W X Y Z := F.map_injective <| by
    simp only [Functor.map_comp, fData.whiskerRight_eq, fData.associator_eq, Iso.trans_assoc,
      Iso.trans_hom, Iso.symm_hom, tensorIso_hom, Iso.refl_hom, tensorHom_id, id_tensorHom,
      comp_whiskerRight, whisker_assoc, assoc, fData.whiskerLeft_eq,
      MonoidalCategory.whiskerLeft_comp, Iso.hom_inv_id_assoc, whiskerLeft_hom_inv_assoc,
      hom_inv_whiskerRight_assoc, Iso.inv_hom_id_assoc, Iso.cancel_iso_inv_left]
    slice_lhs 5 6 =>
      rw [â† MonoidalCategory.whiskerLeft_comp, hom_inv_whiskerRight]
    rw [whisker_exchange_assoc]
    simp
  leftUnitor_naturality {X Y : D} f := F.map_injective <| by
    simp [fData.leftUnitor_eq, fData.whiskerLeft_eq, whisker_exchange_assoc]
  rightUnitor_naturality {X Y : D} f := F.map_injective <| by
    simp [fData.rightUnitor_eq, fData.whiskerRight_eq, â† whisker_exchange_assoc]
  associator_naturality {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ} fâ‚ fâ‚‚ fâ‚ƒ := F.map_injective <| by
    simp [fData.tensorHom_eq, fData.associator_eq, tensorHom_def, whisker_exchange_assoc]

/-- A faithful functor equipped with a `InducingFunctorData` structure is monoidal. -/
def fromInducedCoreMonoidal [MonoidalCategoryStruct D] (F : D â¥¤ C) [F.Faithful]
    (fData : InducingFunctorData F) :
    letI := induced F fData
    F.CoreMonoidal := by
  letI := induced F fData
  exact
    { ÎµIso := fData.ÎµIso
      Î¼Iso := fData.Î¼Iso
      Î¼Iso_hom_natural_left := fun _ â†¦ by simp [fData.whiskerRight_eq]
      Î¼Iso_hom_natural_right := fun _ â†¦ by simp [fData.whiskerLeft_eq]
      associativity := fun _ _ _ â†¦ by simp [fData.associator_eq]
      left_unitality := fun _ â†¦ by simp [fData.leftUnitor_eq]
      right_unitality := fun _ â†¦ by simp [fData.rightUnitor_eq] }

instance fromInducedMonoidal [MonoidalCategoryStruct D] (F : D â¥¤ C) [F.Faithful]
    (fData : InducingFunctorData F) :
    letI := induced F fData
    F.Monoidal :=
  letI := induced F fData
  (fromInducedCoreMonoidal F fData).toMonoidal

/-- Transport a monoidal structure along an equivalence of (plain) categories.
-/
@[simps -isSimp]
def transportStruct (e : C â‰Œ D) : MonoidalCategoryStruct.{vâ‚‚} D where
  tensorObj X Y := e.functor.obj (e.inverse.obj X âŠ— e.inverse.obj Y)
  whiskerLeft X _ _ f := e.functor.map (e.inverse.obj X â— e.inverse.map f)
  whiskerRight f X := e.functor.map (e.inverse.map f â–· e.inverse.obj X)
  tensorHom f g := e.functor.map (e.inverse.map f âŠ—â‚˜ e.inverse.map g)
  tensorUnit := e.functor.obj (ğŸ™_ C)
  associator X Y Z :=
    e.functor.mapIso
      (whiskerRightIso (e.unitIso.app _).symm _ â‰ªâ‰«
        Î±_ (e.inverse.obj X) (e.inverse.obj Y) (e.inverse.obj Z) â‰ªâ‰«
        whiskerLeftIso _ (e.unitIso.app _))
  leftUnitor X :=
    e.functor.mapIso ((whiskerRightIso (e.unitIso.app _).symm _) â‰ªâ‰« Î»_ (e.inverse.obj X)) â‰ªâ‰«
      e.counitIso.app _
  rightUnitor X :=
    e.functor.mapIso ((whiskerLeftIso _ (e.unitIso.app _).symm) â‰ªâ‰« Ï_ (e.inverse.obj X)) â‰ªâ‰«
      e.counitIso.app _

attribute [local simp] transportStruct in
/-- Transport a monoidal structure along an equivalence of (plain) categories.
-/
def transport (e : C â‰Œ D) : MonoidalCategory.{vâ‚‚} D :=
  letI : MonoidalCategoryStruct.{vâ‚‚} D := transportStruct e
  induced e.inverse
    { Î¼Iso := fun _ _ => e.unitIso.app _
      ÎµIso := e.unitIso.app _ }

/-- A type synonym for `D`, which will carry the transported monoidal structure. -/
@[nolint unusedArguments]
def Transported (_ : C â‰Œ D) := D

instance (e : C â‰Œ D) : Category (Transported e) := (inferInstance : Category D)

instance Transported.instMonoidalCategoryStruct (e : C â‰Œ D) :
    MonoidalCategoryStruct (Transported e) :=
  transportStruct e

instance Transported.instMonoidalCategory (e : C â‰Œ D) : MonoidalCategory (Transported e) :=
  transport e

instance (e : C â‰Œ D) : Inhabited (Transported e) :=
  âŸ¨ğŸ™_ _âŸ©

section

variable (e : C â‰Œ D)

/-- We upgrade the equivalence of categories `e : C â‰Œ D` to a monoidal category
equivalence `C â‰Œ Transported e`. -/
abbrev equivalenceTransported : C â‰Œ Transported e := e

instance : (equivalenceTransported e).inverse.Monoidal := by
  dsimp only [Transported.instMonoidalCategory]
  infer_instance

instance : (equivalenceTransported e).symm.functor.Monoidal :=
  inferInstanceAs (equivalenceTransported e).inverse.Monoidal

instance : (equivalenceTransported e).functor.Monoidal :=
  (equivalenceTransported e).symm.inverseMonoidal

instance : (equivalenceTransported e).symm.inverse.Monoidal :=
  inferInstanceAs (equivalenceTransported e).functor.Monoidal

instance : (equivalenceTransported e).symm.IsMonoidal := by
  infer_instance

/-- The unit isomorphism upgrades to a monoidal isomorphism. -/
instance : NatTrans.IsMonoidal (equivalenceTransported e).unit :=
  inferInstanceAs (NatTrans.IsMonoidal (equivalenceTransported e).symm.counitIso.inv)

/-- The counit isomorphism upgrades to a monoidal isomorphism. -/
instance : NatTrans.IsMonoidal (equivalenceTransported e).counit :=
  inferInstanceAs (NatTrans.IsMonoidal (equivalenceTransported e).symm.unitIso.inv)

end

end CategoryTheory.Monoidal
