/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Monoidal.NaturalTransformation

#align_import category_theory.monoidal.transport from "leanprover-community/mathlib"@"31529827d0f68d1fbd429edc393a928f677f4aba"

/-!
# Transport a monoidal structure along an equivalence.

When `C` and `D` are equivalent as categories,
we can transport a monoidal structure on `C` along the equivalence as
`CategoryTheory.Monoidal.transport`, obtaining a monoidal structure on `D`.

More generally, we can transport the lawfulness of a monoidal structure along a suitable faithful
functor, as `CategoryTheory.Monoidal.induced`.
The comparison is analogous to the difference between `Equiv.monoid` and
`Function.Injective.Monoid`.

We then upgrade the original functor and its inverse to monoidal functors
with respect to the new monoidal structure on `D`.
-/


universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

noncomputable section

open CategoryTheory

open CategoryTheory.Category

open CategoryTheory.MonoidalCategory

namespace CategoryTheory.Monoidal

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] [MonoidalCategory.{v‚ÇÅ} C]

variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]

/--
Induce the lawfulness of the monoidal structure along an faithful functor of (plain) categories,
where the operations are already defined on the destination type `D`.

The functor `F` must preserve all the data parts of the monoidal structure between the two
categories.

Note that `ŒºIsoSymm` and `ŒµIsoSymm` correspond to the reversed versions of
`CategoryTheory.LaxMonoidalFunctor.ŒºIso` and `CategoryTheory.LaxMonoidalFunctor.ŒµIso`.
-/
abbrev induced (F : D ‚•§ C) [Faithful F]
    (tensorObj : D ‚Üí D ‚Üí D)
    (ŒºIsoSymm : ‚àÄ X Y,
      F.obj (tensorObj X Y) ‚âÖ F.obj X ‚äó F.obj Y)
    (whiskerLeft : ‚àÄ (X : D) {Y‚ÇÅ Y‚ÇÇ : D} (_f : Y‚ÇÅ ‚ü∂ Y‚ÇÇ), tensorObj X Y‚ÇÅ ‚ü∂ tensorObj X Y‚ÇÇ)
    (whiskerLeft_eq : ‚àÄ (X : D) {Y‚ÇÅ Y‚ÇÇ : D} (f : Y‚ÇÅ ‚ü∂ Y‚ÇÇ),
      F.map (whiskerLeft X f)
        = (ŒºIsoSymm _ _).hom ‚â´ (F.obj X ‚óÅ F.map f) ‚â´ (ŒºIsoSymm _ _).inv :=
      by aesop_cat)
    (whiskerRight : ‚àÄ {X‚ÇÅ X‚ÇÇ : D} (_f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (Y : D), tensorObj X‚ÇÅ Y ‚ü∂ tensorObj X‚ÇÇ Y)
    (whiskerRight_eq : ‚àÄ {X‚ÇÅ X‚ÇÇ : D} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (Y : D),
      F.map (whiskerRight f Y)
        = (ŒºIsoSymm _ _).hom ‚â´ (F.map f ‚ñ∑ F.obj Y) ‚â´ (ŒºIsoSymm _ _).inv :=
      by aesop_cat)
    (tensorHom :
      ‚àÄ {X‚ÇÅ Y‚ÇÅ X‚ÇÇ Y‚ÇÇ : D} (_f : X‚ÇÅ ‚ü∂ Y‚ÇÅ) (_g : X‚ÇÇ ‚ü∂ Y‚ÇÇ), tensorObj X‚ÇÅ X‚ÇÇ ‚ü∂ tensorObj Y‚ÇÅ Y‚ÇÇ)
    (tensorHom_eq :
      ‚àÄ {X‚ÇÅ Y‚ÇÅ X‚ÇÇ Y‚ÇÇ : D} (f : X‚ÇÅ ‚ü∂ Y‚ÇÅ) (g : X‚ÇÇ ‚ü∂ Y‚ÇÇ),
        F.map (tensorHom f g)
          = (ŒºIsoSymm _ _).hom ‚â´ (F.map f ‚äó F.map g) ‚â´ (ŒºIsoSymm _ _).inv :=
      by aesop_cat)
    (tensorUnit' : D)
    (ŒµIsoSymm : F.obj tensorUnit' ‚âÖ ùüô_ _)
    (associator : ‚àÄ X Y Z : D, tensorObj (tensorObj X Y) Z ‚âÖ tensorObj X (tensorObj Y Z))
    (associator_eq : ‚àÄ X Y Z : D,
      F.map (associator X Y Z).hom =
        ((ŒºIsoSymm _ _ ‚â™‚â´ (ŒºIsoSymm _ _ ‚äó .refl _))
          ‚â™‚â´ Œ±_ (F.obj X) (F.obj Y) (F.obj Z)
          ‚â™‚â´ ((.refl _ ‚äó (ŒºIsoSymm _ _).symm) ‚â™‚â´ (ŒºIsoSymm _ _).symm)).hom :=
      by aesop_cat)
    (leftUnitor : ‚àÄ X : D, tensorObj tensorUnit' X ‚âÖ X)
    (leftUnitor_eq : ‚àÄ X : D,
      F.map (leftUnitor X).hom =
        ((ŒºIsoSymm _ _ ‚â™‚â´ (ŒµIsoSymm ‚äó .refl _)) ‚â™‚â´ Œª_ (F.obj X)).hom :=
      by aesop_cat)
    (rightUnitor : ‚àÄ X : D, tensorObj X tensorUnit' ‚âÖ X)
    (rightUnitor_eq : ‚àÄ X : D,
      F.map (rightUnitor X).hom =
        ((ŒºIsoSymm _ _ ‚â™‚â´ (.refl _ ‚äó ŒµIsoSymm)) ‚â™‚â´ œÅ_ (F.obj X)).hom :=
      by aesop_cat) :
    MonoidalCategory.{v‚ÇÇ} D where
  -- the data fields are exactly as provided
  tensorObj := tensorObj
  whiskerLeft := whiskerLeft
  whiskerRight := whiskerRight
  tensorHom := tensorHom
  tensorUnit' := tensorUnit'
  associator := associator
  leftUnitor := leftUnitor
  rightUnitor := rightUnitor
  tensorHom_def {X‚ÇÅ Y‚ÇÅ X‚ÇÇ Y‚ÇÇ} f g := F.map_injective <| by
    dsimp
    rw [tensorHom_eq, Functor.map_comp, whiskerRight_eq, whiskerLeft_eq]
    simp only [tensorHom_def, assoc, Iso.inv_hom_id_assoc]
  tensor_id X‚ÇÅ X‚ÇÇ := F.map_injective <| by aesop_cat
  tensor_comp {X‚ÇÅ Y‚ÇÅ Z‚ÇÅ X‚ÇÇ Y‚ÇÇ Z‚ÇÇ} f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ := F.map_injective <| by aesop_cat
  whiskerLeft_id X Y := F.map_injective <| by simp [whiskerLeft_eq]
  id_whiskerRight X Y := F.map_injective <| by simp [whiskerRight_eq]
  triangle X Y := F.map_injective <| by aesop_cat
  pentagon W X Y Z := F.map_injective <| by
    have := MonoidalCategory.pentagon (F.obj W) (F.obj X) (F.obj Y) (F.obj Z)
    dsimp
    simp only [Functor.map_comp, tensorHom_eq, associator_eq, Iso.trans_assoc, Iso.trans_hom,
      tensorIso_hom, Iso.refl_hom, Iso.symm_hom, Functor.map_id, comp_tensor_id,
      associator_conjugation, tensor_id, assoc, id_tensor_comp, Iso.inv_hom_id_assoc,
      tensor_inv_hom_id_assoc, id_comp, inv_hom_id_tensor_assoc, id_tensor_comp_tensor_id_assoc,
      Iso.cancel_iso_hom_left]
    congr 1
    simp only [‚Üêassoc]
    congr 2
    simp only [assoc, ‚Üêtensor_comp, id_comp, Iso.inv_hom_id, tensor_id]
    congr 1
    conv_rhs => rw [‚Üêtensor_id_comp_id_tensor]
    simp only [assoc]
    congr 1
    rw [Iso.inv_comp_eq]
    conv_lhs => rw [‚Üêid_comp (ùüô (F.obj W)), tensor_comp]
    slice_lhs 0 2 => rw [this]
    rw [assoc]
    congr 1
    rw [‚Üêassociator_naturality, tensor_id]
  leftUnitor_naturality {X Y : D} f := F.map_injective <| by
    have := leftUnitor_naturality (F.map f)
    dsimp
    simp only [Functor.map_comp, tensorHom_eq, Functor.map_id, leftUnitor_eq, Iso.trans_assoc,
      Iso.trans_hom, tensorIso_hom, Iso.refl_hom, assoc, Iso.inv_hom_id_assoc,
      id_tensor_comp_tensor_id_assoc, Iso.cancel_iso_hom_left]
    rw [‚Üêthis, ‚Üêassoc, ‚Üêtensor_comp, id_comp, comp_id]
  rightUnitor_naturality {X Y : D} f := F.map_injective <| by
    have := rightUnitor_naturality (F.map f)
    dsimp
    simp only [Functor.map_comp, tensorHom_eq, Functor.map_id, rightUnitor_eq, Iso.trans_assoc,
      Iso.trans_hom, tensorIso_hom, Iso.refl_hom, assoc, Iso.inv_hom_id_assoc,
      tensor_id_comp_id_tensor_assoc, Iso.cancel_iso_hom_left]
    rw [‚Üêthis, ‚Üêassoc, ‚Üêtensor_comp, id_comp, comp_id]
  associator_naturality {X‚ÇÅ X‚ÇÇ X‚ÇÉ Y‚ÇÅ Y‚ÇÇ Y‚ÇÉ} f‚ÇÅ f‚ÇÇ f‚ÇÉ := F.map_injective <| by
    have := associator_naturality (F.map f‚ÇÅ) (F.map f‚ÇÇ) (F.map f‚ÇÉ)
    dsimp
    simp [associator_eq, tensorHom_eq]
    simp_rw [‚Üêassoc, ‚Üêtensor_comp, assoc, Iso.inv_hom_id, ‚Üêassoc]
    congr 1
    conv_rhs => rw [‚Üêcomp_id (F.map f‚ÇÅ), ‚Üêid_comp (F.map f‚ÇÅ)]
    simp only [tensor_comp]
    simp only [tensor_id, comp_id, assoc, tensor_inv_hom_id_assoc, id_comp]
    slice_rhs 2 3 => rw [‚Üêthis]
    simp only [‚Üê assoc, Iso.inv_hom_id, comp_id]
    congr 2
    simp_rw [‚Üêtensor_comp, id_comp]

/-- Transport a monoidal structure along an equivalence of (plain) categories.
-/
@[simps
  tensorObj whiskerLeft whiskerRight tensorHom tensorUnit' associator leftUnitor rightUnitor]
abbrev transport (e : C ‚âå D) : MonoidalCategory.{v‚ÇÇ} D :=
  induced
    (F := e.inverse)
    (tensorObj := fun X Y => e.functor.obj (e.inverse.obj X ‚äó e.inverse.obj Y))
    (ŒºIsoSymm := fun X Y => (e.unitIso.app _).symm)
    (whiskerLeft := fun X _ _ f ‚Ü¶ e.functor.map (e.inverse.obj X ‚óÅ e.inverse.map f))
    (whiskerRight := fun f X ‚Ü¶ e.functor.map (e.inverse.map f ‚ñ∑ e.inverse.obj X))
    (tensorHom := fun f g => e.functor.map (e.inverse.map f ‚äó e.inverse.map g))
    (tensorUnit' := e.functor.obj (ùüô_ C))
    (ŒµIsoSymm := (e.unitIso.app _).symm)
    (associator := fun X Y Z =>
      e.functor.mapIso
        (((e.unitIso.app _).symm ‚äó Iso.refl _) ‚â™‚â´
          Œ±_ (e.inverse.obj X) (e.inverse.obj Y) (e.inverse.obj Z) ‚â™‚â´
          (Iso.refl _ ‚äó e.unitIso.app _)))
    (leftUnitor := fun X =>
      e.functor.mapIso (((e.unitIso.app _).symm ‚äó Iso.refl _) ‚â™‚â´ Œª_ (e.inverse.obj X)) ‚â™‚â´
        e.counitIso.app _)
    (rightUnitor := fun X =>
      e.functor.mapIso ((Iso.refl _ ‚äó (e.unitIso.app _).symm) ‚â™‚â´ œÅ_ (e.inverse.obj X)) ‚â™‚â´
        e.counitIso.app _)
#align category_theory.monoidal.transport CategoryTheory.Monoidal.transport

/-- A type synonym for `D`, which will carry the transported monoidal structure. -/
@[nolint unusedArguments]
def Transported (_ : C ‚âå D) := D
#align category_theory.monoidal.transported CategoryTheory.Monoidal.Transported

instance (e : C ‚âå D) : Category (Transported e) := (inferInstance : Category D)

instance (e : C ‚âå D) : MonoidalCategory (Transported e) :=
  transport e

instance (e : C ‚âå D) : Inhabited (Transported e) :=
  ‚ü®ùüô_ _‚ü©

section

attribute [local simp] transport_tensorUnit'

section

attribute [local simp]
  transport_tensorHom transport_associator transport_leftUnitor transport_rightUnitor

/--
We can upgrade `e.functor` to a lax monoidal functor from `C` to `D` with the transported structure.
-/
@[simps]
def laxToTransported (e : C ‚âå D) : LaxMonoidalFunctor C (Transported e) where
  toFunctor := e.functor
  Œµ := ùüô (e.functor.obj (ùüô_ C))
  Œº X Y := e.functor.map (e.unitInv.app X ‚äó e.unitInv.app Y)
  Œº_natural f g := by
    dsimp
    rw [Equivalence.inv_fun_map, Equivalence.inv_fun_map, tensor_comp, Functor.map_comp,
      tensor_comp, ‚Üê e.functor.map_comp, ‚Üê e.functor.map_comp, ‚Üê e.functor.map_comp,
      assoc, assoc, ‚Üê tensor_comp, Iso.hom_inv_id_app, Iso.hom_inv_id_app, ‚Üê tensor_comp]
    dsimp
    rw [comp_id, comp_id]
  associativity X Y Z := by
    dsimp
    rw [Equivalence.inv_fun_map, Equivalence.inv_fun_map, Functor.map_comp,
      Functor.map_comp, assoc, assoc, e.inverse.map_id, e.inverse.map_id,
      comp_tensor_id, id_tensor_comp, Functor.map_comp, assoc, id_tensor_comp,
      comp_tensor_id, ‚Üê e.functor.map_comp, ‚Üê e.functor.map_comp, ‚Üê e.functor.map_comp,
      ‚Üê e.functor.map_comp, ‚Üê e.functor.map_comp, ‚Üê e.functor.map_comp, ‚Üê e.functor.map_comp]
    congr 2
    slice_lhs 3 3 => rw [‚Üê tensor_id_comp_id_tensor]
    slice_lhs 2 3 =>
      rw [‚Üê comp_tensor_id, Iso.hom_inv_id_app]
      dsimp
      rw [tensor_id]
    rw [id_comp]
    slice_rhs 2 3 =>
      rw [‚Üêid_tensor_comp, Iso.hom_inv_id_app]
      dsimp
      rw [tensor_id]
    rw [id_comp]
    conv_rhs => rw [‚Üê id_tensor_comp_tensor_id _ (e.unitInv.app X)]
    dsimp only [Functor.comp_obj]
    slice_rhs 3 4 =>
      rw [‚Üê id_tensor_comp, Iso.hom_inv_id_app]
      dsimp
      rw [tensor_id]
    simp only [associator_conjugation, ‚Üêtensor_id, ‚Üêtensor_comp, Iso.inv_hom_id,
      Iso.inv_hom_id_assoc, assoc, id_comp, comp_id]
  left_unitality X := by
    dsimp
    rw [e.inverse.map_id, e.inverse.map_id, tensor_id, Functor.map_comp, assoc,
      Equivalence.counit_app_functor, ‚Üê e.functor.map_comp, ‚Üê e.functor.map_comp,
      ‚Üê e.functor.map_comp, ‚Üê e.functor.map_comp, ‚Üê leftUnitor_naturality,
      ‚Üê tensor_comp_assoc, comp_id, id_comp, id_comp]
    rfl
  right_unitality X := by
    dsimp
    rw [Functor.map_comp, assoc, e.inverse.map_id, e.inverse.map_id, tensor_id,
      Functor.map_id, id_comp, Equivalence.counit_app_functor, ‚Üê e.functor.map_comp,
      ‚Üê e.functor.map_comp, ‚Üê e.functor.map_comp, ‚Üê rightUnitor_naturality,
      ‚Üê tensor_comp_assoc, id_comp, comp_id]
    rfl
#align category_theory.monoidal.lax_to_transported CategoryTheory.Monoidal.laxToTransported

end

/-- We can upgrade `e.functor` to a monoidal functor from `C` to `D` with the transported structure.
-/
@[simps]
def toTransported (e : C ‚âå D) : MonoidalFunctor C (Transported e) where
  toLaxMonoidalFunctor := laxToTransported e
  Œµ_isIso := by
    dsimp
    infer_instance
  Œº_isIso X Y := by
    dsimp
    infer_instance
#align category_theory.monoidal.to_transported CategoryTheory.Monoidal.toTransported

end

instance (e : C ‚âå D) : IsEquivalence (toTransported e).toFunctor := by
  dsimp
  infer_instance

/-- We can upgrade `e.inverse` to a monoidal functor from `D` with the transported structure to `C`.
-/
@[simps!]
def fromTransported (e : C ‚âå D) : MonoidalFunctor (Transported e) C :=
  monoidalInverse (toTransported e)
#align category_theory.monoidal.from_transported CategoryTheory.Monoidal.fromTransported

instance instIsEquivalence_fromTransported (e : C ‚âå D) :
    IsEquivalence (fromTransported e).toFunctor := by
  dsimp [fromTransported]
  infer_instance

/-- The unit isomorphism upgrades to a monoidal isomorphism. -/
@[simps! hom inv]
def transportedMonoidalUnitIso (e : C ‚âå D) :
    LaxMonoidalFunctor.id C ‚âÖ laxToTransported e ‚äó‚ãô (fromTransported e).toLaxMonoidalFunctor :=
  asIso (monoidalUnit (toTransported e))
#align category_theory.monoidal.transported_monoidal_unit_iso CategoryTheory.Monoidal.transportedMonoidalUnitIso

/-- The counit isomorphism upgrades to a monoidal isomorphism. -/
@[simps! hom inv]
def transportedMonoidalCounitIso (e : C ‚âå D) :
    (fromTransported e).toLaxMonoidalFunctor ‚äó‚ãô laxToTransported e ‚âÖ
      LaxMonoidalFunctor.id (Transported e) :=
  asIso (monoidalCounit (toTransported e))
#align category_theory.monoidal.transported_monoidal_counit_iso CategoryTheory.Monoidal.transportedMonoidalCounitIso

end CategoryTheory.Monoidal
