/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Monoidal.NaturalTransformation

#align_import category_theory.monoidal.transport from "leanprover-community/mathlib"@"31529827d0f68d1fbd429edc393a928f677f4aba"

/-!
# Transport a monoidal structure along an equivalence.

When `C` and `D` are equivalent as categories,
we can transport a monoidal structure on `C` along the equivalence as
`CategoryTheory.Monoidal.transport`, obtaining a monoidal structure on `D`.

More generally, we can transport the lawfulness of a monoidal structure along a suitable faithful
functor, as `CategoryTheory.Monoidal.induced`.
The comparison is analogous to the difference between `Equiv.monoid` and
`Function.Injective.monoid`.

We then upgrade the original functor and its inverse to monoidal functors
with respect to the new monoidal structure on `D`.
-/


universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

noncomputable section

open CategoryTheory

open CategoryTheory.Category

open CategoryTheory.MonoidalCategory

namespace CategoryTheory.Monoidal

variable {C : Type uâ‚} [Category.{vâ‚} C] [MonoidalCategory.{vâ‚} C]
variable {D : Type uâ‚‚} [Category.{vâ‚‚} D]

/-- The data needed to induce a `MonoidalCategory` via the functor `F`; namely, pre-existing
definitions of `âŠ—`, `ğŸ™_`, `â–·`, `â—` that are preserved by `F`.
-/
structure InducingFunctorData [MonoidalCategoryStruct D] (F : D â¥¤ C) where
  /-- Analogous to `CategoryTheory.LaxMonoidalFunctor.Î¼Iso` -/
  Î¼Iso : âˆ€ X Y,
    F.obj X âŠ— F.obj Y â‰… F.obj (X âŠ— Y)
  whiskerLeft_eq : âˆ€ (X : D) {Yâ‚ Yâ‚‚ : D} (f : Yâ‚ âŸ¶ Yâ‚‚),
    F.map (X â— f) = (Î¼Iso _ _).inv â‰« (F.obj X â— F.map f) â‰« (Î¼Iso _ _).hom := by
    aesop_cat
  whiskerRight_eq : âˆ€ {Xâ‚ Xâ‚‚ : D} (f : Xâ‚ âŸ¶ Xâ‚‚) (Y : D),
    F.map (f â–· Y) = (Î¼Iso _ _).inv â‰« (F.map f â–· F.obj Y) â‰« (Î¼Iso _ _).hom := by
    aesop_cat
  tensorHom_eq : âˆ€ {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : D} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚),
    F.map (f âŠ— g) = (Î¼Iso _ _).inv â‰« (F.map f âŠ— F.map g) â‰« (Î¼Iso _ _).hom := by
    aesop_cat
  /-- Analogous to `CategoryTheory.LaxMonoidalFunctor.ÎµIso` -/
  ÎµIso : ğŸ™_ _ â‰… F.obj (ğŸ™_ _)
  associator_eq : âˆ€ X Y Z : D,
    F.map (Î±_ X Y Z).hom =
      (((Î¼Iso _ _).symm â‰ªâ‰« ((Î¼Iso _ _).symm âŠ— .refl _))
        â‰ªâ‰« Î±_ (F.obj X) (F.obj Y) (F.obj Z)
        â‰ªâ‰« ((.refl _ âŠ— Î¼Iso _ _) â‰ªâ‰« Î¼Iso _ _)).hom := by
    aesop_cat
  leftUnitor_eq : âˆ€ X : D,
    F.map (Î»_ X).hom =
      (((Î¼Iso _ _).symm â‰ªâ‰« (ÎµIso.symm âŠ— .refl _)) â‰ªâ‰« Î»_ (F.obj X)).hom := by
    aesop_cat
  rightUnitor_eq : âˆ€ X : D,
    F.map (Ï_ X).hom =
      (((Î¼Iso _ _).symm â‰ªâ‰« (.refl _ âŠ— ÎµIso.symm)) â‰ªâ‰« Ï_ (F.obj X)).hom := by
    aesop_cat

-- these are theorems so don't need docstrings (std4#217)
attribute [nolint docBlame]
  InducingFunctorData.whiskerLeft_eq
  InducingFunctorData.whiskerRight_eq
  InducingFunctorData.tensorHom_eq
  InducingFunctorData.associator_eq
  InducingFunctorData.leftUnitor_eq
  InducingFunctorData.rightUnitor_eq

/--
Induce the lawfulness of the monoidal structure along an faithful functor of (plain) categories,
where the operations are already defined on the destination type `D`.

The functor `F` must preserve all the data parts of the monoidal structure between the two
categories.

-/
abbrev induced [MonoidalCategoryStruct D] (F : D â¥¤ C) [F.Faithful]
    (fData : InducingFunctorData F) :
    MonoidalCategory.{vâ‚‚} D where
  tensorHom_def {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚} f g := F.map_injective <| by
    rw [fData.tensorHom_eq, Functor.map_comp, fData.whiskerRight_eq, fData.whiskerLeft_eq]
    simp only [tensorHom_def, assoc, Iso.hom_inv_id_assoc]
  tensor_id Xâ‚ Xâ‚‚ := F.map_injective <| by cases fData; aesop_cat
  tensor_comp {Xâ‚ Yâ‚ Zâ‚ Xâ‚‚ Yâ‚‚ Zâ‚‚} fâ‚ fâ‚‚ gâ‚ gâ‚‚ := F.map_injective <| by cases fData; aesop_cat
  whiskerLeft_id X Y := F.map_injective <| by simp [fData.whiskerLeft_eq]
  id_whiskerRight X Y := F.map_injective <| by simp [fData.whiskerRight_eq]
  triangle X Y := F.map_injective <| by cases fData; aesop_cat
  pentagon W X Y Z := F.map_injective <| by
    simp only [Functor.map_comp, fData.whiskerRight_eq, fData.associator_eq, Iso.trans_assoc,
      Iso.trans_hom, Iso.symm_hom, tensorIso_hom, Iso.refl_hom, tensorHom_id, id_tensorHom,
      comp_whiskerRight, whisker_assoc, assoc, fData.whiskerLeft_eq,
      MonoidalCategory.whiskerLeft_comp, Iso.hom_inv_id_assoc, whiskerLeft_hom_inv_assoc,
      hom_inv_whiskerRight_assoc, Iso.inv_hom_id_assoc, Iso.cancel_iso_inv_left]
    slice_lhs 5 6 =>
      rw [â† MonoidalCategory.whiskerLeft_comp, hom_inv_whiskerRight]
    rw [whisker_exchange_assoc]
    simp
  leftUnitor_naturality {X Y : D} f := F.map_injective <| by
    simp [fData.leftUnitor_eq, fData.whiskerLeft_eq, whisker_exchange_assoc]
  rightUnitor_naturality {X Y : D} f := F.map_injective <| by
    simp [fData.rightUnitor_eq, fData.whiskerRight_eq, â† whisker_exchange_assoc]
  associator_naturality {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ} fâ‚ fâ‚‚ fâ‚ƒ := F.map_injective <| by
    simp [fData.tensorHom_eq, fData.associator_eq, tensorHom_def, whisker_exchange_assoc]

/--
We can upgrade `F` to a monoidal functor from `D` to `E` with the induced structure.
-/
@[simps]
def fromInduced [MonoidalCategoryStruct D] (F : D â¥¤ C) [F.Faithful]
    (fData : InducingFunctorData F) :
    letI := induced F fData
    MonoidalFunctor D C :=
  letI := induced F fData
  { toFunctor := F
    Îµ := fData.ÎµIso.hom
    Î¼ := fun X Y => (fData.Î¼Iso X Y).hom
    Î¼_natural_left := by cases fData; aesop_cat
    Î¼_natural_right := by cases fData; aesop_cat
    associativity := by cases fData; aesop_cat
    left_unitality := by cases fData; aesop_cat
    right_unitality := by cases fData; aesop_cat }

/-- Transport a monoidal structure along an equivalence of (plain) categories.
-/
@[simps]
def transportStruct (e : C â‰Œ D) : MonoidalCategoryStruct.{vâ‚‚} D where
  tensorObj X Y := e.functor.obj (e.inverse.obj X âŠ— e.inverse.obj Y)
  whiskerLeft X _ _ f := e.functor.map (e.inverse.obj X â— e.inverse.map f)
  whiskerRight f X := e.functor.map (e.inverse.map f â–· e.inverse.obj X)
  tensorHom f g := e.functor.map (e.inverse.map f âŠ— e.inverse.map g)
  tensorUnit := e.functor.obj (ğŸ™_ C)
  associator X Y Z :=
    e.functor.mapIso
      (((e.unitIso.app _).symm âŠ— Iso.refl _) â‰ªâ‰«
        Î±_ (e.inverse.obj X) (e.inverse.obj Y) (e.inverse.obj Z) â‰ªâ‰«
        (Iso.refl _ âŠ— e.unitIso.app _))
  leftUnitor X :=
    e.functor.mapIso (((e.unitIso.app _).symm âŠ— Iso.refl _) â‰ªâ‰« Î»_ (e.inverse.obj X)) â‰ªâ‰«
      e.counitIso.app _
  rightUnitor X :=
    e.functor.mapIso ((Iso.refl _ âŠ— (e.unitIso.app _).symm) â‰ªâ‰« Ï_ (e.inverse.obj X)) â‰ªâ‰«
      e.counitIso.app _

/-- Transport a monoidal structure along an equivalence of (plain) categories.
-/
def transport (e : C â‰Œ D) : MonoidalCategory.{vâ‚‚} D :=
  letI : MonoidalCategoryStruct.{vâ‚‚} D := transportStruct e
  induced e.inverse
    { Î¼Iso := fun X Y => e.unitIso.app _
      ÎµIso := e.unitIso.app _ }
#align category_theory.monoidal.transport CategoryTheory.Monoidal.transport

/-- A type synonym for `D`, which will carry the transported monoidal structure. -/
@[nolint unusedArguments]
def Transported (_ : C â‰Œ D) := D
#align category_theory.monoidal.transported CategoryTheory.Monoidal.Transported

instance (e : C â‰Œ D) : Category (Transported e) := (inferInstance : Category D)

instance Transported.instMonoidalCategoryStruct (e : C â‰Œ D) :
    MonoidalCategoryStruct (Transported e) :=
  transportStruct e

instance Transported.instMonoidalCategory (e : C â‰Œ D) : MonoidalCategory (Transported e) :=
  transport e

instance (e : C â‰Œ D) : Inhabited (Transported e) :=
  âŸ¨ğŸ™_ _âŸ©

/-- We can upgrade `e.inverse` to a monoidal functor from `D` with the transported structure to `C`.
-/
@[simps!]
def fromTransported (e : C â‰Œ D) : MonoidalFunctor (Transported e) C := by
  dsimp only [transport, Transported.instMonoidalCategory]
  exact fromInduced (D := Transported e) e.inverse _
#align category_theory.monoidal.from_transported CategoryTheory.Monoidal.fromTransported

instance instIsEquivalence_fromTransported (e : C â‰Œ D) :
    (fromTransported e).IsEquivalence := by
  dsimp [fromTransported]
  infer_instance

#noalign category_theory.monoidal.lax_to_transported

/-- We can upgrade `e.functor` to a monoidal functor from `C` to `D` with the transported structure.
-/
@[simps!]
def toTransported (e : C â‰Œ D) : MonoidalFunctor C (Transported e) :=
  monoidalInverse (fromTransported e) e.symm.toAdjunction
#align category_theory.monoidal.to_transported CategoryTheory.Monoidal.toTransported

instance (e : C â‰Œ D) : (toTransported e).IsEquivalence :=
  e.isEquivalence_functor

/-- The unit isomorphism upgrades to a monoidal isomorphism. -/
@[simps! hom inv]
def transportedMonoidalUnitIso (e : C â‰Œ D) :
    LaxMonoidalFunctor.id C â‰…
      (toTransported e).toLaxMonoidalFunctor âŠ—â‹™ (fromTransported e).toLaxMonoidalFunctor :=
  asIso (monoidalCounit (fromTransported e) e.symm.toAdjunction) |>.symm
#align category_theory.monoidal.transported_monoidal_unit_iso CategoryTheory.Monoidal.transportedMonoidalUnitIso

/-- The counit isomorphism upgrades to a monoidal isomorphism. -/
@[simps! hom inv]
def transportedMonoidalCounitIso (e : C â‰Œ D) :
    (fromTransported e).toLaxMonoidalFunctor âŠ—â‹™ (toTransported e).toLaxMonoidalFunctor â‰…
      LaxMonoidalFunctor.id (Transported e) :=
  asIso (monoidalUnit (fromTransported e) e.symm.toAdjunction) |>.symm
#align category_theory.monoidal.transported_monoidal_counit_iso CategoryTheory.Monoidal.transportedMonoidalCounitIso

end CategoryTheory.Monoidal
