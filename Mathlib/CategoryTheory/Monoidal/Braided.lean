/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Monoidal.CoherenceLemmas
import Mathlib.CategoryTheory.Monoidal.NaturalTransformation
import Mathlib.CategoryTheory.Monoidal.Discrete

#align_import category_theory.monoidal.braided from "leanprover-community/mathlib"@"2efd2423f8d25fa57cf7a179f5d8652ab4d0df44"

/-!
# Braided and symmetric monoidal categories

The basic definitions of braided monoidal categories, and symmetric monoidal categories,
as well as braided functors.

## Implementation note

We make `BraidedCategory` another typeclass, but then have `SymmetricCategory` extend this.
The rationale is that we are not carrying any additional data, just requiring a property.

## Future work

* Construct the Drinfeld center of a monoidal category as a braided monoidal category.
* Say something about pseudo-natural transformations.

-/


open CategoryTheory MonoidalCategory

universe v vâ‚ vâ‚‚ vâ‚ƒ u uâ‚ uâ‚‚ uâ‚ƒ

namespace CategoryTheory

/-- A braided monoidal category is a monoidal category equipped with a braiding isomorphism
`Î²_ X Y : X âŠ— Y â‰… Y âŠ— X`
which is natural in both arguments,
and also satisfies the two hexagon identities.
-/
class BraidedCategory (C : Type u) [Category.{v} C] [MonoidalCategory.{v} C] where
  /-- braiding natural isomorphism -/
  braiding : âˆ€ X Y : C, X âŠ— Y â‰… Y âŠ— X
  braiding_naturality_right :
    âˆ€ (X : C) {Y Z : C} (f : Y âŸ¶ Z),
      X â— f â‰« (braiding X Z).hom = (braiding X Y).hom â‰« f â–· X := by
    aesop_cat
  braiding_naturality_left :
    âˆ€ {X Y : C} (f : X âŸ¶ Y) (Z : C),
      f â–· Z â‰« (braiding Y Z).hom = (braiding X Z).hom â‰« Z â— f := by
    aesop_cat
  -- hexagon identities:
  hexagon_forward :
    âˆ€ X Y Z : C,
      (Î±_ X Y Z).hom â‰« (braiding X (Y âŠ— Z)).hom â‰« (Î±_ Y Z X).hom =
        ((braiding X Y).hom â–· Z) â‰« (Î±_ Y X Z).hom â‰« (Y â— (braiding X Z).hom) := by
    aesop_cat
  hexagon_reverse :
    âˆ€ X Y Z : C,
      (Î±_ X Y Z).inv â‰« (braiding (X âŠ— Y) Z).hom â‰« (Î±_ Z X Y).inv =
        (X â— (braiding Y Z).hom) â‰« (Î±_ X Z Y).inv â‰« ((braiding X Z).hom â–· Y) := by
    aesop_cat
#align category_theory.braided_category CategoryTheory.BraidedCategory

attribute [reassoc (attr := simp)]
  BraidedCategory.braiding_naturality_left
  BraidedCategory.braiding_naturality_right
attribute [reassoc] BraidedCategory.hexagon_forward BraidedCategory.hexagon_reverse

open Category

open MonoidalCategory

open BraidedCategory

notation "Î²_" => BraidedCategory.braiding

namespace BraidedCategory

variable {C : Type u} [Category.{v} C] [MonoidalCategory.{v} C] [BraidedCategory.{v} C]

@[simp]
theorem braiding_tensor_left (X Y Z : C) :
    (Î²_ (X âŠ— Y) Z).hom  =
      (Î±_ X Y Z).hom â‰« X â— (Î²_ Y Z).hom â‰« (Î±_ X Z Y).inv â‰«
        (Î²_ X Z).hom â–· Y â‰« (Î±_ Z X Y).hom := by
  intros
  apply (cancel_epi (Î±_ X Y Z).inv).1
  apply (cancel_mono (Î±_ Z X Y).inv).1
  simp [hexagon_reverse]

@[simp]
theorem braiding_tensor_right (X Y Z : C) :
    (Î²_ X (Y âŠ— Z)).hom  =
      (Î±_ X Y Z).inv â‰« (Î²_ X Y).hom â–· Z â‰« (Î±_ Y X Z).hom â‰«
        Y â— (Î²_ X Z).hom â‰« (Î±_ Y Z X).inv := by
  intros
  apply (cancel_epi (Î±_ X Y Z).hom).1
  apply (cancel_mono (Î±_ Y Z X).hom).1
  simp [hexagon_forward]

@[simp]
theorem braiding_inv_tensor_left (X Y Z : C) :
    (Î²_ (X âŠ— Y) Z).inv  =
      (Î±_ Z X Y).inv â‰« (Î²_ X Z).inv â–· Y â‰« (Î±_ X Z Y).hom â‰«
        X â— (Î²_ Y Z).inv â‰« (Î±_ X Y Z).inv :=
  eq_of_inv_eq_inv (by simp)

@[simp]
theorem braiding_inv_tensor_right (X Y Z : C) :
    (Î²_ X (Y âŠ— Z)).inv  =
      (Î±_ Y Z X).hom â‰« Y â— (Î²_ X Z).inv â‰« (Î±_ Y X Z).inv â‰«
        (Î²_ X Y).inv â–· Z â‰« (Î±_ X Y Z).hom :=
  eq_of_inv_eq_inv (by simp)

@[reassoc (attr := simp)]
theorem braiding_naturality {X X' Y Y' : C} (f : X âŸ¶ Y) (g : X' âŸ¶ Y') :
    (f âŠ— g) â‰« (braiding Y Y').hom = (braiding X X').hom â‰« (g âŠ— f) := by
  rw [tensorHom_def' f g, tensorHom_def g f]
  simp_rw [Category.assoc, braiding_naturality_left, braiding_naturality_right_assoc]

end BraidedCategory

/--
Verifying the axioms for a braiding by checking that the candidate braiding is sent to a braiding
by a faithful monoidal functor.
-/
def braidedCategoryOfFaithful {C D : Type*} [Category C] [Category D] [MonoidalCategory C]
    [MonoidalCategory D] (F : MonoidalFunctor C D) [Faithful F.toFunctor] [BraidedCategory D]
    (Î² : âˆ€ X Y : C, X âŠ— Y â‰… Y âŠ— X)
    (w : âˆ€ X Y, F.Î¼ _ _ â‰« F.map (Î² X Y).hom = (Î²_ _ _).hom â‰« F.Î¼ _ _) : BraidedCategory C where
  braiding := Î²
  braiding_naturality_left := by
    intros
    apply F.map_injective
    refine (cancel_epi (F.Î¼ ?_ ?_)).1 ?_
    rw [Functor.map_comp, â† LaxMonoidalFunctor.Î¼_natural_left'_assoc, w, Functor.map_comp,
      reassoc_of% w, braiding_naturality_left_assoc, LaxMonoidalFunctor.Î¼_natural_right']
  braiding_naturality_right := by
    intros
    apply F.map_injective
    refine (cancel_epi (F.Î¼ ?_ ?_)).1 ?_
    rw [Functor.map_comp, â† LaxMonoidalFunctor.Î¼_natural_right'_assoc, w, Functor.map_comp,
      reassoc_of% w, braiding_naturality_right_assoc, LaxMonoidalFunctor.Î¼_natural_left']
  hexagon_forward := by
    intros
    apply F.map_injective
    refine (cancel_epi (F.Î¼ _ _)).1 ?_
    refine (cancel_epi (F.Î¼ _ _ â–· _)).1 ?_
    rw [Functor.map_comp, Functor.map_comp, Functor.map_comp, Functor.map_comp, â†
      LaxMonoidalFunctor.Î¼_natural_left'_assoc, â† comp_whiskerRight_assoc, w,
      comp_whiskerRight_assoc, LaxMonoidalFunctor.associativity'_assoc,
      LaxMonoidalFunctor.associativity'_assoc, â† LaxMonoidalFunctor.Î¼_natural_right', â†
      MonoidalCategory.whiskerLeft_comp_assoc, w, MonoidalCategory.whiskerLeft_comp_assoc,
      reassoc_of% w, braiding_naturality_right_assoc,
      LaxMonoidalFunctor.associativity', hexagon_forward_assoc]
  hexagon_reverse := by
    intros
    apply F.toFunctor.map_injective
    refine (cancel_epi (F.Î¼ _ _)).1 ?_
    refine (cancel_epi (_ â— F.Î¼ _ _)).1 ?_
    rw [Functor.map_comp, Functor.map_comp, Functor.map_comp, Functor.map_comp, â†
      LaxMonoidalFunctor.Î¼_natural_right'_assoc, â† MonoidalCategory.whiskerLeft_comp_assoc, w,
      MonoidalCategory.whiskerLeft_comp_assoc, LaxMonoidalFunctor.associativity_inv'_assoc,
      LaxMonoidalFunctor.associativity_inv'_assoc, â† LaxMonoidalFunctor.Î¼_natural_left',
      â† comp_whiskerRight_assoc, w, comp_whiskerRight_assoc, reassoc_of% w,
      braiding_naturality_left_assoc, LaxMonoidalFunctor.associativity_inv', hexagon_reverse_assoc]
#align category_theory.braided_category_of_faithful CategoryTheory.braidedCategoryOfFaithful

/-- Pull back a braiding along a fully faithful monoidal functor. -/
noncomputable def braidedCategoryOfFullyFaithful {C D : Type*} [Category C] [Category D]
    [MonoidalCategory C] [MonoidalCategory D] (F : MonoidalFunctor C D) [Full F.toFunctor]
    [Faithful F.toFunctor] [BraidedCategory D] : BraidedCategory C :=
  braidedCategoryOfFaithful F
    (fun X Y => F.toFunctor.preimageIso
      ((asIso (F.Î¼ _ _)).symm â‰ªâ‰« Î²_ (F.obj X) (F.obj Y) â‰ªâ‰« asIso (F.Î¼ _ _)))
    (by aesop_cat)
#align category_theory.braided_category_of_fully_faithful CategoryTheory.braidedCategoryOfFullyFaithful

section

/-!
We now establish how the braiding interacts with the unitors.

I couldn't find a detailed proof in print, but this is discussed in:

* Proposition 1 of AndrÃ© Joyal and Ross Street,
  "Braided monoidal categories", Macquarie Math Reports 860081 (1986).
* Proposition 2.1 of AndrÃ© Joyal and Ross Street,
  "Braided tensor categories" , Adv. Math. 102 (1993), 20â€“78.
* Exercise 8.1.6 of Etingof, Gelaki, Nikshych, Ostrik,
  "Tensor categories", vol 25, Mathematical Surveys and Monographs (2015), AMS.
-/

variable (C : Type uâ‚) [Category.{vâ‚} C] [MonoidalCategory C] [BraidedCategory C]

theorem braiding_leftUnitor_auxâ‚ (X : C) :
    (Î±_ (ğŸ™_ C) (ğŸ™_ C) X).hom â‰«
        (ğŸ™_ C â— (Î²_ X (ğŸ™_ C)).inv) â‰« (Î±_ _ X _).inv â‰« ((Î»_ X).hom â–· _) =
      ((Î»_ _).hom â–· X) â‰« (Î²_ X (ğŸ™_ C)).inv := by
  coherence
#align category_theory.braiding_left_unitor_auxâ‚ CategoryTheory.braiding_leftUnitor_auxâ‚

theorem braiding_leftUnitor_auxâ‚‚ (X : C) :
    ((Î²_ X (ğŸ™_ C)).hom â–· ğŸ™_ C) â‰« ((Î»_ X).hom â–· ğŸ™_ C) = (Ï_ X).hom â–· ğŸ™_ C :=
  calc
    ((Î²_ X (ğŸ™_ C)).hom â–· ğŸ™_ C) â‰« ((Î»_ X).hom â–· ğŸ™_ C) =
      ((Î²_ X (ğŸ™_ C)).hom â–· ğŸ™_ C) â‰« (Î±_ _ _ _).hom â‰« (Î±_ _ _ _).inv â‰« ((Î»_ X).hom â–· ğŸ™_ C) :=
      by coherence
    _ = ((Î²_ X (ğŸ™_ C)).hom â–· ğŸ™_ C) â‰« (Î±_ _ _ _).hom â‰« (_ â— (Î²_ X _).hom) â‰«
          (_ â— (Î²_ X _).inv) â‰« (Î±_ _ _ _).inv â‰« ((Î»_ X).hom â–· ğŸ™_ C) :=
      by simp
    _ = (Î±_ _ _ _).hom â‰« (Î²_ _ _).hom â‰« (Î±_ _ _ _).hom â‰« (_ â— (Î²_ X _).inv) â‰« (Î±_ _ _ _).inv â‰«
          ((Î»_ X).hom â–· ğŸ™_ C) :=
      by (slice_lhs 1 3 => rw [â† hexagon_forward]); simp only [assoc]
    _ = (Î±_ _ _ _).hom â‰« (Î²_ _ _).hom â‰« ((Î»_ _).hom â–· X) â‰« (Î²_ X _).inv :=
      by rw [braiding_leftUnitor_auxâ‚]
    _ = (Î±_ _ _ _).hom â‰« (_ â— (Î»_ _).hom) â‰« (Î²_ _ _).hom â‰« (Î²_ X _).inv :=
      by (slice_lhs 2 3 => rw [â† braiding_naturality_right]); simp only [assoc]
    _ = (Î±_ _ _ _).hom â‰« (_ â— (Î»_ _).hom) := by rw [Iso.hom_inv_id, comp_id]
    _ = (Ï_ X).hom â–· ğŸ™_ C := by rw [triangle']

#align category_theory.braiding_left_unitor_auxâ‚‚ CategoryTheory.braiding_leftUnitor_auxâ‚‚

theorem braiding_leftUnitor (X : C) : (Î²_ X (ğŸ™_ C)).hom â‰« (Î»_ X).hom = (Ï_ X).hom := by
  rw [â† whiskerRight_iff, comp_whiskerRight, braiding_leftUnitor_auxâ‚‚]
#align category_theory.braiding_left_unitor CategoryTheory.braiding_leftUnitor

theorem braiding_rightUnitor_auxâ‚ (X : C) :
    (Î±_ X (ğŸ™_ C) (ğŸ™_ C)).inv â‰«
        ((Î²_ (ğŸ™_ C) X).inv â–· ğŸ™_ C) â‰« (Î±_ _ X _).hom â‰« (_ â— (Ï_ X).hom) =
      (X â— (Ï_ _).hom) â‰« (Î²_ (ğŸ™_ C) X).inv := by
  coherence
#align category_theory.braiding_right_unitor_auxâ‚ CategoryTheory.braiding_rightUnitor_auxâ‚

theorem braiding_rightUnitor_auxâ‚‚ (X : C) :
    (ğŸ™_ C â— (Î²_ (ğŸ™_ C) X).hom) â‰« (ğŸ™_ C â— (Ï_ X).hom) = ğŸ™_ C â— (Î»_ X).hom :=
  calc
    (ğŸ™_ C â— (Î²_ (ğŸ™_ C) X).hom) â‰« (ğŸ™_ C â— (Ï_ X).hom) =
      (ğŸ™_ C â— (Î²_ (ğŸ™_ C) X).hom) â‰« (Î±_ _ _ _).inv â‰« (Î±_ _ _ _).hom â‰« (ğŸ™_ C â— (Ï_ X).hom) :=
      by coherence
    _ = (ğŸ™_ C â— (Î²_ (ğŸ™_ C) X).hom) â‰« (Î±_ _ _ _).inv â‰« ((Î²_ _ X).hom â–· _) â‰«
          ((Î²_ _ X).inv â–· _) â‰« (Î±_ _ _ _).hom â‰« (ğŸ™_ C â— (Ï_ X).hom) :=
      by simp
    _ = (Î±_ _ _ _).inv â‰« (Î²_ _ _).hom â‰« (Î±_ _ _ _).inv â‰« ((Î²_ _ X).inv â–· _) â‰« (Î±_ _ _ _).hom â‰«
          (ğŸ™_ C â— (Ï_ X).hom) :=
      by (slice_lhs 1 3 => rw [â† hexagon_reverse]); simp only [assoc]
    _ = (Î±_ _ _ _).inv â‰« (Î²_ _ _).hom â‰« (X â— (Ï_ _).hom) â‰« (Î²_ _ X).inv :=
      by rw [braiding_rightUnitor_auxâ‚]
    _ = (Î±_ _ _ _).inv â‰« ((Ï_ _).hom â–· _) â‰« (Î²_ _ X).hom â‰« (Î²_ _ _).inv :=
      by (slice_lhs 2 3 => rw [â† braiding_naturality_left]); simp only [assoc]
    _ = (Î±_ _ _ _).inv â‰« ((Ï_ _).hom â–· _) := by rw [Iso.hom_inv_id, comp_id]
    _ = ğŸ™_ C â— (Î»_ X).hom := by rw [triangle_assoc_comp_right']

#align category_theory.braiding_right_unitor_auxâ‚‚ CategoryTheory.braiding_rightUnitor_auxâ‚‚

theorem braiding_rightUnitor (X : C) : (Î²_ (ğŸ™_ C) X).hom â‰« (Ï_ X).hom = (Î»_ X).hom := by
  rw [â† whiskerLeft_iff, MonoidalCategory.whiskerLeft_comp, braiding_rightUnitor_auxâ‚‚]
#align category_theory.braiding_right_unitor CategoryTheory.braiding_rightUnitor

@[simp]
theorem braiding_tensorUnit_left (X : C) : (Î²_ (ğŸ™_ C) X).hom = (Î»_ X).hom â‰« (Ï_ X).inv := by
  simp [â† braiding_rightUnitor]

theorem leftUnitor_inv_braiding (X : C) : (Î»_ X).inv â‰« (Î²_ (ğŸ™_ C) X).hom = (Ï_ X).inv := by
  simp
#align category_theory.left_unitor_inv_braiding CategoryTheory.leftUnitor_inv_braiding

theorem rightUnitor_inv_braiding (X : C) : (Ï_ X).inv â‰« (Î²_ X (ğŸ™_ C)).hom = (Î»_ X).inv := by
  apply (cancel_mono (Î»_ X).hom).1
  simp only [assoc, braiding_leftUnitor, Iso.inv_hom_id]
#align category_theory.right_unitor_inv_braiding CategoryTheory.rightUnitor_inv_braiding

@[simp]
theorem braiding_tensorUnit_right (X : C) : (Î²_ X (ğŸ™_ C)).hom = (Ï_ X).hom â‰« (Î»_ X).inv := by
  simp [â† rightUnitor_inv_braiding]

end

/--
A symmetric monoidal category is a braided monoidal category for which the braiding is symmetric.

See <https://stacks.math.columbia.edu/tag/0FFW>.
-/
class SymmetricCategory (C : Type u) [Category.{v} C] [MonoidalCategory.{v} C] extends
    BraidedCategory.{v} C where
  -- braiding symmetric:
  symmetry : âˆ€ X Y : C, (Î²_ X Y).hom â‰« (Î²_ Y X).hom = ğŸ™ (X âŠ— Y) := by aesop_cat
#align category_theory.symmetric_category CategoryTheory.SymmetricCategory

attribute [reassoc (attr := simp)] SymmetricCategory.symmetry

variable (C : Type uâ‚) [Category.{vâ‚} C] [MonoidalCategory C] [BraidedCategory C]
variable (D : Type uâ‚‚) [Category.{vâ‚‚} D] [MonoidalCategory D] [BraidedCategory D]
variable (E : Type uâ‚ƒ) [Category.{vâ‚ƒ} E] [MonoidalCategory E] [BraidedCategory E]

/-- A lax braided functor between braided monoidal categories is a lax monoidal functor
which preserves the braiding.
-/
structure LaxBraidedFunctor extends LaxMonoidalFunctor C D where
  braided : âˆ€ X Y : C, Î¼ X Y â‰« map (Î²_ X Y).hom = (Î²_ (obj X) (obj Y)).hom â‰« Î¼ Y X := by aesop_cat
#align category_theory.lax_braided_functor CategoryTheory.LaxBraidedFunctor

namespace LaxBraidedFunctor

/-- The identity lax braided monoidal functor. -/
@[simps!]
def id : LaxBraidedFunctor C C :=
  { MonoidalFunctor.id C with }
#align category_theory.lax_braided_functor.id CategoryTheory.LaxBraidedFunctor.id

instance : Inhabited (LaxBraidedFunctor C C) :=
  âŸ¨id CâŸ©

variable {C D E}

/-- The composition of lax braided monoidal functors. -/
@[simps!]
def comp (F : LaxBraidedFunctor C D) (G : LaxBraidedFunctor D E) : LaxBraidedFunctor C E :=
  { LaxMonoidalFunctor.comp F.toLaxMonoidalFunctor G.toLaxMonoidalFunctor with
    braided := fun X Y => by
      dsimp
      slice_lhs 2 3 =>
        rw [â† CategoryTheory.Functor.map_comp, F.braided, CategoryTheory.Functor.map_comp]
      slice_lhs 1 2 => rw [G.braided]
      simp only [Category.assoc] }
#align category_theory.lax_braided_functor.comp CategoryTheory.LaxBraidedFunctor.comp

instance categoryLaxBraidedFunctor : Category (LaxBraidedFunctor C D) :=
  InducedCategory.category LaxBraidedFunctor.toLaxMonoidalFunctor
#align category_theory.lax_braided_functor.category_lax_braided_functor CategoryTheory.LaxBraidedFunctor.categoryLaxBraidedFunctor

-- Porting note: added, as `MonoidalNatTrans.ext` does not apply to morphisms.
@[ext]
lemma ext' {F G : LaxBraidedFunctor C D} {Î± Î² : F âŸ¶ G} (w : âˆ€ X : C, Î±.app X = Î².app X) : Î± = Î² :=
  MonoidalNatTrans.ext _ _ (funext w)

@[simp]
theorem comp_toNatTrans {F G H : LaxBraidedFunctor C D} {Î± : F âŸ¶ G} {Î² : G âŸ¶ H} :
    (Î± â‰« Î²).toNatTrans = @CategoryStruct.comp (C â¥¤ D) _ _ _ _ Î±.toNatTrans Î².toNatTrans :=
  rfl
#align category_theory.lax_braided_functor.comp_to_nat_trans CategoryTheory.LaxBraidedFunctor.comp_toNatTrans

/-- Interpret a natural isomorphism of the underlying lax monoidal functors as an
isomorphism of the lax braided monoidal functors.
-/
@[simps]
def mkIso {F G : LaxBraidedFunctor C D} (i : F.toLaxMonoidalFunctor â‰… G.toLaxMonoidalFunctor) :
    F â‰… G :=
  { i with }
#align category_theory.lax_braided_functor.mk_iso CategoryTheory.LaxBraidedFunctor.mkIso

end LaxBraidedFunctor

/-- A braided functor between braided monoidal categories is a monoidal functor
which preserves the braiding.
-/
structure BraidedFunctor extends MonoidalFunctor C D where
  -- Note this is stated differently than for `LaxBraidedFunctor`.
  -- We move the `Î¼ X Y` to the right hand side,
  -- so that this makes a good `@[simp]` lemma.
  braided : âˆ€ X Y : C, map (Î²_ X Y).hom = inv (Î¼ X Y) â‰« (Î²_ (obj X) (obj Y)).hom â‰« Î¼ Y X := by
    aesop_cat
#align category_theory.braided_functor CategoryTheory.BraidedFunctor

attribute [simp] BraidedFunctor.braided

/--
A braided category with a faithful braided functor to a symmetric category is itself symmetric.
-/
def symmetricCategoryOfFaithful {C D : Type*} [Category C] [Category D] [MonoidalCategory C]
    [MonoidalCategory D] [BraidedCategory C] [SymmetricCategory D] (F : BraidedFunctor C D)
    [Faithful F.toFunctor] : SymmetricCategory C where
  symmetry X Y := F.map_injective (by simp)
#align category_theory.symmetric_category_of_faithful CategoryTheory.symmetricCategoryOfFaithful

namespace BraidedFunctor

/-- Turn a braided functor into a lax braided functor. -/
@[simps toLaxMonoidalFunctor]
def toLaxBraidedFunctor (F : BraidedFunctor C D) : LaxBraidedFunctor C D :=
  { toLaxMonoidalFunctor := F.toLaxMonoidalFunctor
    braided := fun X Y => by rw [F.braided]; simp }
#align category_theory.braided_functor.to_lax_braided_functor CategoryTheory.BraidedFunctor.toLaxBraidedFunctor

/-- The identity braided monoidal functor. -/
@[simps!]
def id : BraidedFunctor C C :=
  { MonoidalFunctor.id C with }
#align category_theory.braided_functor.id CategoryTheory.BraidedFunctor.id

instance : Inhabited (BraidedFunctor C C) :=
  âŸ¨id CâŸ©

variable {C D E}

/-- The composition of braided monoidal functors. -/
@[simps!]
def comp (F : BraidedFunctor C D) (G : BraidedFunctor D E) : BraidedFunctor C E :=
  { MonoidalFunctor.comp F.toMonoidalFunctor G.toMonoidalFunctor with }
#align category_theory.braided_functor.comp CategoryTheory.BraidedFunctor.comp

instance categoryBraidedFunctor : Category (BraidedFunctor C D) :=
  InducedCategory.category BraidedFunctor.toMonoidalFunctor
#align category_theory.braided_functor.category_braided_functor CategoryTheory.BraidedFunctor.categoryBraidedFunctor

-- Porting note: added, as `MonoidalNatTrans.ext` does not apply to morphisms.
@[ext]
lemma ext' {F G : BraidedFunctor C D} {Î± Î² : F âŸ¶ G} (w : âˆ€ X : C, Î±.app X = Î².app X) : Î± = Î² :=
  MonoidalNatTrans.ext _ _ (funext w)

@[simp]
theorem comp_toNatTrans {F G H : BraidedFunctor C D} {Î± : F âŸ¶ G} {Î² : G âŸ¶ H} :
    (Î± â‰« Î²).toNatTrans = @CategoryStruct.comp (C â¥¤ D) _ _ _ _ Î±.toNatTrans Î².toNatTrans :=
  rfl
#align category_theory.braided_functor.comp_to_nat_trans CategoryTheory.BraidedFunctor.comp_toNatTrans

/-- Interpret a natural isomorphism of the underlying monoidal functors as an
isomorphism of the braided monoidal functors.
-/
@[simps]
def mkIso {F G : BraidedFunctor C D} (i : F.toMonoidalFunctor â‰… G.toMonoidalFunctor) : F â‰… G :=
  { i with }
#align category_theory.braided_functor.mk_iso CategoryTheory.BraidedFunctor.mkIso

end BraidedFunctor

section CommMonoid

variable (M : Type u) [CommMonoid M]

instance : BraidedCategory (Discrete M) where
  braiding X Y := Discrete.eqToIso (mul_comm X.as Y.as)

variable {M} {N : Type u} [CommMonoid N]

/-- A multiplicative morphism between commutative monoids gives a braided functor between
the corresponding discrete braided monoidal categories.
-/
@[simps!]
def Discrete.braidedFunctor (F : M â†’* N) : BraidedFunctor (Discrete M) (Discrete N) :=
  { Discrete.monoidalFunctor F with }
#align category_theory.discrete.braided_functor CategoryTheory.Discrete.braidedFunctor

end CommMonoid

section Tensor

/-- The strength of the tensor product functor from `C Ã— C` to `C`. -/
def tensor_Î¼ (X Y : C Ã— C) : (X.1 âŠ— X.2) âŠ— Y.1 âŠ— Y.2 âŸ¶ (X.1 âŠ— Y.1) âŠ— X.2 âŠ— Y.2 :=
  (Î±_ X.1 X.2 (Y.1 âŠ— Y.2)).hom â‰«
    (X.1 â— (Î±_ X.2 Y.1 Y.2).inv) â‰«
      (X.1 â— (Î²_ X.2 Y.1).hom â–· Y.2) â‰«
        (X.1 â— (Î±_ Y.1 X.2 Y.2).hom) â‰« (Î±_ X.1 Y.1 (X.2 âŠ— Y.2)).inv
#align category_theory.tensor_Î¼ CategoryTheory.tensor_Î¼

theorem tensor_Î¼_natural {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ Uâ‚ Uâ‚‚ Vâ‚ Vâ‚‚ : C} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (gâ‚ : Uâ‚ âŸ¶ Vâ‚)
    (gâ‚‚ : Uâ‚‚ âŸ¶ Vâ‚‚) :
    ((fâ‚ âŠ— fâ‚‚) âŠ— gâ‚ âŠ— gâ‚‚) â‰« tensor_Î¼ C (Yâ‚, Yâ‚‚) (Vâ‚, Vâ‚‚) =
      tensor_Î¼ C (Xâ‚, Xâ‚‚) (Uâ‚, Uâ‚‚) â‰« ((fâ‚ âŠ— gâ‚) âŠ— fâ‚‚ âŠ— gâ‚‚) := by
  dsimp only [tensor_Î¼]
  simp_rw [â† id_tensorHom, â† tensorHom_id]
  slice_lhs 1 2 => rw [associator_naturality]
  slice_lhs 2 3 =>
    rw [â† tensor_comp, comp_id fâ‚, â† id_comp fâ‚, associator_inv_naturality, tensor_comp]
  slice_lhs 3 4 =>
    rw [â† tensor_comp, â† tensor_comp, comp_id fâ‚, â† id_comp fâ‚, comp_id gâ‚‚, â† id_comp gâ‚‚,
      braiding_naturality, tensor_comp, tensor_comp]
  slice_lhs 4 5 => rw [â† tensor_comp, comp_id fâ‚, â† id_comp fâ‚, associator_naturality, tensor_comp]
  slice_lhs 5 6 => rw [associator_inv_naturality]
  simp only [assoc]
#align category_theory.tensor_Î¼_natural CategoryTheory.tensor_Î¼_natural

attribute [local simp] id_tensorHom tensorHom_id

@[reassoc]
theorem tensor_Î¼_natural_left {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : C} (fâ‚: Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (Zâ‚ Zâ‚‚ : C) :
    (fâ‚ âŠ— fâ‚‚) â–· (Zâ‚ âŠ— Zâ‚‚) â‰« tensor_Î¼ C (Yâ‚, Yâ‚‚) (Zâ‚, Zâ‚‚) =
      tensor_Î¼ C (Xâ‚, Xâ‚‚) (Zâ‚, Zâ‚‚) â‰« (fâ‚ â–· Zâ‚ âŠ— fâ‚‚ â–· Zâ‚‚) := by
  convert tensor_Î¼_natural C fâ‚ fâ‚‚ (ğŸ™ Zâ‚) (ğŸ™ Zâ‚‚) using 1 <;> simp

@[reassoc]
theorem tensor_Î¼_natural_right (Zâ‚ Zâ‚‚ : C) {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : C} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) :
    (Zâ‚ âŠ— Zâ‚‚) â— (fâ‚ âŠ— fâ‚‚) â‰« tensor_Î¼ C (Zâ‚, Zâ‚‚) (Yâ‚, Yâ‚‚) =
      tensor_Î¼ C (Zâ‚, Zâ‚‚) (Xâ‚, Xâ‚‚) â‰« (Zâ‚ â— fâ‚ âŠ— Zâ‚‚ â— fâ‚‚) := by
  convert tensor_Î¼_natural C (ğŸ™ Zâ‚) (ğŸ™ Zâ‚‚) fâ‚ fâ‚‚ using 1 <;> simp

theorem tensor_left_unitality (Xâ‚ Xâ‚‚ : C) :
    (Î»_ (Xâ‚ âŠ— Xâ‚‚)).hom =
      ((Î»_ (ğŸ™_ C)).inv â–· (Xâ‚ âŠ— Xâ‚‚)) â‰«
        tensor_Î¼ C (ğŸ™_ C, ğŸ™_ C) (Xâ‚, Xâ‚‚) â‰« ((Î»_ Xâ‚).hom âŠ— (Î»_ Xâ‚‚).hom) := by
  dsimp only [tensor_Î¼]
  have :
    ((Î»_ (ğŸ™_ C)).inv â–· (Xâ‚ âŠ— Xâ‚‚)) â‰«
        (Î±_ (ğŸ™_ C) (ğŸ™_ C) (Xâ‚ âŠ— Xâ‚‚)).hom â‰« (ğŸ™_ C â— (Î±_ (ğŸ™_ C) Xâ‚ Xâ‚‚).inv) =
      ğŸ™_ C â— (Î»_ Xâ‚).inv â–· Xâ‚‚ :=
    by coherence
  slice_rhs 1 3 => rw [this]
  clear this
  slice_rhs 1 2 => rw [â† MonoidalCategory.whiskerLeft_comp, â† comp_whiskerRight,
    leftUnitor_inv_braiding]
  simp [tensorHom_id, id_tensorHom, tensorHom_def]
#align category_theory.tensor_left_unitality CategoryTheory.tensor_left_unitality

theorem tensor_right_unitality (Xâ‚ Xâ‚‚ : C) :
    (Ï_ (Xâ‚ âŠ— Xâ‚‚)).hom =
      ((Xâ‚ âŠ— Xâ‚‚) â— (Î»_ (ğŸ™_ C)).inv) â‰«
        tensor_Î¼ C (Xâ‚, Xâ‚‚) (ğŸ™_ C, ğŸ™_ C) â‰« ((Ï_ Xâ‚).hom âŠ— (Ï_ Xâ‚‚).hom) := by
  dsimp only [tensor_Î¼]
  have :
    ((Xâ‚ âŠ— Xâ‚‚) â— (Î»_ (ğŸ™_ C)).inv) â‰«
        (Î±_ Xâ‚ Xâ‚‚ (ğŸ™_ C âŠ— ğŸ™_ C)).hom â‰« (Xâ‚ â— (Î±_ Xâ‚‚ (ğŸ™_ C) (ğŸ™_ C)).inv) =
      (Î±_ Xâ‚ Xâ‚‚ (ğŸ™_ C)).hom â‰« (Xâ‚ â— (Ï_ Xâ‚‚).inv â–· ğŸ™_ C) :=
    by coherence
  slice_rhs 1 3 => rw [this]
  clear this
  slice_rhs 2 3 => rw [â† MonoidalCategory.whiskerLeft_comp, â† comp_whiskerRight,
    rightUnitor_inv_braiding]
  simp [tensorHom_id, id_tensorHom, tensorHom_def]
#align category_theory.tensor_right_unitality CategoryTheory.tensor_right_unitality

theorem tensor_associativity (Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ Zâ‚ Zâ‚‚ : C) :
    (tensor_Î¼ C (Xâ‚, Xâ‚‚) (Yâ‚, Yâ‚‚) â–· (Zâ‚ âŠ— Zâ‚‚)) â‰«
        tensor_Î¼ C (Xâ‚ âŠ— Yâ‚, Xâ‚‚ âŠ— Yâ‚‚) (Zâ‚, Zâ‚‚) â‰« ((Î±_ Xâ‚ Yâ‚ Zâ‚).hom âŠ— (Î±_ Xâ‚‚ Yâ‚‚ Zâ‚‚).hom) =
      (Î±_ (Xâ‚ âŠ— Xâ‚‚) (Yâ‚ âŠ— Yâ‚‚) (Zâ‚ âŠ— Zâ‚‚)).hom â‰«
        ((Xâ‚ âŠ— Xâ‚‚) â— tensor_Î¼ C (Yâ‚, Yâ‚‚) (Zâ‚, Zâ‚‚)) â‰« tensor_Î¼ C (Xâ‚, Xâ‚‚) (Yâ‚ âŠ— Zâ‚, Yâ‚‚ âŠ— Zâ‚‚) := by
  dsimp only [tensor_obj, prodMonoidal_tensorObj, tensor_Î¼]
  simp only [whiskerRight_tensor, comp_whiskerRight, whisker_assoc, assoc, Iso.inv_hom_id_assoc,
    tensor_whiskerLeft, braiding_tensor_left, MonoidalCategory.whiskerLeft_comp,
    braiding_tensor_right]
  calc
    _ = ğŸ™ _ âŠ—â‰«
      Xâ‚ â— ((Î²_ Xâ‚‚ Yâ‚).hom â–· (Yâ‚‚ âŠ— Zâ‚) â‰« (Yâ‚ âŠ— Xâ‚‚) â— (Î²_ Yâ‚‚ Zâ‚).hom) â–· Zâ‚‚ âŠ—â‰«
        Xâ‚ â— Yâ‚ â— (Î²_ Xâ‚‚ Zâ‚).hom â–· Yâ‚‚ â–· Zâ‚‚ âŠ—â‰« ğŸ™ _ := by coherence
    _ = _ := by rw [â† whisker_exchange]; coherence
#align category_theory.tensor_associativity CategoryTheory.tensor_associativity

/-- The tensor product functor from `C Ã— C` to `C` as a monoidal functor. -/
@[simps!]
def tensorMonoidal : MonoidalFunctor (C Ã— C) C :=
  { tensor C with
    Îµ := (Î»_ (ğŸ™_ C)).inv
    Î¼ := tensor_Î¼ C
    Î¼_natural_left := fun f Z => by
      -- `simpa` will be not needed when we define `Î¼_natural_left` in terms of the whiskerings.
      simpa using tensor_Î¼_natural_left C f.1 f.2 Z.1 Z.2
    Î¼_natural_right := fun Z f => by
      simpa using tensor_Î¼_natural_right C Z.1 Z.2 f.1 f.2
    associativity := fun X Y Z => by
      simpa using tensor_associativity C X.1 X.2 Y.1 Y.2 Z.1 Z.2
    left_unitality := fun âŸ¨Xâ‚, Xâ‚‚âŸ© => by
      simpa using tensor_left_unitality C Xâ‚ Xâ‚‚
    right_unitality := fun âŸ¨Xâ‚, Xâ‚‚âŸ© => by
      simpa using tensor_right_unitality C Xâ‚ Xâ‚‚
    Î¼_isIso := by dsimp [tensor_Î¼]; infer_instance }

theorem leftUnitor_monoidal (Xâ‚ Xâ‚‚ : C) :
    (Î»_ Xâ‚).hom âŠ— (Î»_ Xâ‚‚).hom =
      tensor_Î¼ C (ğŸ™_ C, Xâ‚) (ğŸ™_ C, Xâ‚‚) â‰« ((Î»_ (ğŸ™_ C)).hom â–· (Xâ‚ âŠ— Xâ‚‚)) â‰« (Î»_ (Xâ‚ âŠ— Xâ‚‚)).hom := by
  dsimp only [tensor_Î¼]
  have :
    (Î»_ Xâ‚).hom âŠ— (Î»_ Xâ‚‚).hom =
      (Î±_ (ğŸ™_ C) Xâ‚ (ğŸ™_ C âŠ— Xâ‚‚)).hom â‰«
        (ğŸ™_ C â— (Î±_ Xâ‚ (ğŸ™_ C) Xâ‚‚).inv) â‰« (Î»_ ((Xâ‚ âŠ— ğŸ™_ C) âŠ— Xâ‚‚)).hom â‰« ((Ï_ Xâ‚).hom â–· Xâ‚‚) :=
    by coherence
  rw [this]; clear this
  rw [â† braiding_leftUnitor]
  dsimp only [tensor_obj, prodMonoidal_tensorObj]
  coherence
#align category_theory.left_unitor_monoidal CategoryTheory.leftUnitor_monoidal

theorem rightUnitor_monoidal (Xâ‚ Xâ‚‚ : C) :
    (Ï_ Xâ‚).hom âŠ— (Ï_ Xâ‚‚).hom =
      tensor_Î¼ C (Xâ‚, ğŸ™_ C) (Xâ‚‚, ğŸ™_ C) â‰« ((Xâ‚ âŠ— Xâ‚‚) â— (Î»_ (ğŸ™_ C)).hom) â‰« (Ï_ (Xâ‚ âŠ— Xâ‚‚)).hom := by
  dsimp only [tensor_Î¼]
  have :
    (Ï_ Xâ‚).hom âŠ— (Ï_ Xâ‚‚).hom =
      (Î±_ Xâ‚ (ğŸ™_ C) (Xâ‚‚ âŠ— ğŸ™_ C)).hom â‰«
        (Xâ‚ â— (Î±_ (ğŸ™_ C) Xâ‚‚ (ğŸ™_ C)).inv) â‰« (Xâ‚ â— (Ï_ (ğŸ™_ C âŠ— Xâ‚‚)).hom) â‰« (Xâ‚ â— (Î»_ Xâ‚‚).hom) :=
    by coherence
  rw [this]; clear this
  rw [â† braiding_rightUnitor]
  dsimp only [tensor_obj, prodMonoidal_tensorObj]
  coherence
#align category_theory.right_unitor_monoidal CategoryTheory.rightUnitor_monoidal

theorem associator_monoidal (Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ : C) :
    tensor_Î¼ C (Xâ‚ âŠ— Xâ‚‚, Xâ‚ƒ) (Yâ‚ âŠ— Yâ‚‚, Yâ‚ƒ) â‰«
        (tensor_Î¼ C (Xâ‚, Xâ‚‚) (Yâ‚, Yâ‚‚) â–· (Xâ‚ƒ âŠ— Yâ‚ƒ)) â‰« (Î±_ (Xâ‚ âŠ— Yâ‚) (Xâ‚‚ âŠ— Yâ‚‚) (Xâ‚ƒ âŠ— Yâ‚ƒ)).hom =
      ((Î±_ Xâ‚ Xâ‚‚ Xâ‚ƒ).hom âŠ— (Î±_ Yâ‚ Yâ‚‚ Yâ‚ƒ).hom) â‰«
        tensor_Î¼ C (Xâ‚, Xâ‚‚ âŠ— Xâ‚ƒ) (Yâ‚, Yâ‚‚ âŠ— Yâ‚ƒ) â‰« ((Xâ‚ âŠ— Yâ‚) â— tensor_Î¼ C (Xâ‚‚, Xâ‚ƒ) (Yâ‚‚, Yâ‚ƒ)) := by
  dsimp only [tensor_Î¼]
  calc
    _ = ğŸ™ _ âŠ—â‰« Xâ‚ â— Xâ‚‚ â— (Î²_ Xâ‚ƒ Yâ‚).hom â–· Yâ‚‚ â–· Yâ‚ƒ âŠ—â‰«
      Xâ‚ â— ((Xâ‚‚ âŠ— Yâ‚) â— (Î²_ Xâ‚ƒ Yâ‚‚).hom â‰«
        (Î²_ Xâ‚‚ Yâ‚).hom â–· (Yâ‚‚ âŠ— Xâ‚ƒ)) â–· Yâ‚ƒ âŠ—â‰« ğŸ™ _ := by simp; coherence
    _ = _ := by rw [whisker_exchange]; simp; coherence
#align category_theory.associator_monoidal CategoryTheory.associator_monoidal

end Tensor

end CategoryTheory
