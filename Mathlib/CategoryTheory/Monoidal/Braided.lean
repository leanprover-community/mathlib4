/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Monoidal.CoherenceLemmas
import Mathlib.CategoryTheory.Monoidal.NaturalTransformation
import Mathlib.CategoryTheory.Monoidal.Discrete

#align_import category_theory.monoidal.braided from "leanprover-community/mathlib"@"2efd2423f8d25fa57cf7a179f5d8652ab4d0df44"

/-!
# Braided and symmetric monoidal categories

The basic definitions of braided monoidal categories, and symmetric monoidal categories,
as well as braided functors.

## Implementation note

We make `BraidedCategory` another typeclass, but then have `SymmetricCategory` extend this.
The rationale is that we are not carrying any additional data, just requiring a property.

## Future work

* Construct the Drinfeld center of a monoidal category as a braided monoidal category.
* Say something about pseudo-natural transformations.

-/


open CategoryTheory MonoidalCategory

universe v vâ‚ vâ‚‚ vâ‚ƒ u uâ‚ uâ‚‚ uâ‚ƒ

namespace CategoryTheory

/-- A braided monoidal category is a monoidal category equipped with a braiding isomorphism
`Î²_ X Y : X âŠ— Y â‰… Y âŠ— X`
which is natural in both arguments,
and also satisfies the two hexagon identities.
-/
class BraidedCategory (C : Type u) [Category.{v} C] [MonoidalCategory.{v} C] where
  -- braiding natural iso:
  braiding : âˆ€ X Y : C, X âŠ— Y â‰… Y âŠ— X
  braiding_naturality :
    âˆ€ {X X' Y Y' : C} (f : X âŸ¶ Y) (g : X' âŸ¶ Y'),
      (f âŠ— g) â‰« (braiding Y Y').hom = (braiding X X').hom â‰« (g âŠ— f) := by
    aesop_cat
  -- hexagon identities:
  hexagon_forward :
    âˆ€ X Y Z : C,
      (Î±_ X Y Z).hom â‰« (braiding X (Y âŠ— Z)).hom â‰« (Î±_ Y Z X).hom =
        ((braiding X Y).hom âŠ— ğŸ™ Z) â‰« (Î±_ Y X Z).hom â‰« (ğŸ™ Y âŠ— (braiding X Z).hom) := by
    aesop_cat
  hexagon_reverse :
    âˆ€ X Y Z : C,
      (Î±_ X Y Z).inv â‰« (braiding (X âŠ— Y) Z).hom â‰« (Î±_ Z X Y).inv =
        (ğŸ™ X âŠ— (braiding Y Z).hom) â‰« (Î±_ X Z Y).inv â‰« ((braiding X Z).hom âŠ— ğŸ™ Y) := by
    aesop_cat
#align category_theory.braided_category CategoryTheory.BraidedCategory

attribute [reassoc (attr := simp)] BraidedCategory.braiding_naturality
attribute [reassoc] BraidedCategory.hexagon_forward BraidedCategory.hexagon_reverse

open Category

open MonoidalCategory

open BraidedCategory

notation "Î²_" => braiding

/--
Verifying the axioms for a braiding by checking that the candidate braiding is sent to a braiding
by a faithful monoidal functor.
-/
def braidedCategoryOfFaithful {C D : Type*} [Category C] [Category D] [MonoidalCategory C]
    [MonoidalCategory D] (F : MonoidalFunctor C D) [Faithful F.toFunctor] [BraidedCategory D]
    (Î² : âˆ€ X Y : C, X âŠ— Y â‰… Y âŠ— X)
    (w : âˆ€ X Y, F.Î¼ _ _ â‰« F.map (Î² X Y).hom = (Î²_ _ _).hom â‰« F.Î¼ _ _) : BraidedCategory C where
  braiding := Î²
  braiding_naturality := by
    intros
    apply F.map_injective
    refine (cancel_epi (F.Î¼ ?_ ?_)).1 ?_
    rw [Functor.map_comp, â† LaxMonoidalFunctor.Î¼_natural_assoc, w, Functor.map_comp, reassoc_of% w,
      braiding_naturality_assoc, LaxMonoidalFunctor.Î¼_natural]
  hexagon_forward := by
    intros
    apply F.map_injective
    refine (cancel_epi (F.Î¼ _ _)).1 ?_
    refine (cancel_epi (F.Î¼ _ _ âŠ— ğŸ™ _)).1 ?_
    rw [Functor.map_comp, Functor.map_comp, Functor.map_comp, Functor.map_comp, â†
      LaxMonoidalFunctor.Î¼_natural_assoc, Functor.map_id, â† comp_tensor_id_assoc, w,
      comp_tensor_id, assoc, LaxMonoidalFunctor.associativity_assoc,
      LaxMonoidalFunctor.associativity_assoc, â† LaxMonoidalFunctor.Î¼_natural, Functor.map_id, â†
      id_tensor_comp_assoc, w, id_tensor_comp_assoc, reassoc_of% w, braiding_naturality_assoc,
      LaxMonoidalFunctor.associativity, hexagon_forward_assoc]
  hexagon_reverse := by
    intros
    apply F.toFunctor.map_injective
    refine (cancel_epi (F.Î¼ _ _)).1 ?_
    refine (cancel_epi (ğŸ™ _ âŠ— F.Î¼ _ _)).1 ?_
    rw [Functor.map_comp, Functor.map_comp, Functor.map_comp, Functor.map_comp, â†
      LaxMonoidalFunctor.Î¼_natural_assoc, Functor.map_id, â† id_tensor_comp_assoc, w,
      id_tensor_comp_assoc, LaxMonoidalFunctor.associativity_inv_assoc,
      LaxMonoidalFunctor.associativity_inv_assoc, â† LaxMonoidalFunctor.Î¼_natural,
      Functor.map_id, â† comp_tensor_id_assoc, w, comp_tensor_id_assoc, reassoc_of% w,
      braiding_naturality_assoc, LaxMonoidalFunctor.associativity_inv, hexagon_reverse_assoc]
#align category_theory.braided_category_of_faithful CategoryTheory.braidedCategoryOfFaithful

/-- Pull back a braiding along a fully faithful monoidal functor. -/
noncomputable def braidedCategoryOfFullyFaithful {C D : Type*} [Category C] [Category D]
    [MonoidalCategory C] [MonoidalCategory D] (F : MonoidalFunctor C D) [Full F.toFunctor]
    [Faithful F.toFunctor] [BraidedCategory D] : BraidedCategory C :=
  braidedCategoryOfFaithful F
    (fun X Y => F.toFunctor.preimageIso
      ((asIso (F.Î¼ _ _)).symm â‰ªâ‰« Î²_ (F.obj X) (F.obj Y) â‰ªâ‰« asIso (F.Î¼ _ _)))
    (by aesop_cat)
#align category_theory.braided_category_of_fully_faithful CategoryTheory.braidedCategoryOfFullyFaithful

section

/-!
We now establish how the braiding interacts with the unitors.

I couldn't find a detailed proof in print, but this is discussed in:

* Proposition 1 of AndrÃ© Joyal and Ross Street,
  "Braided monoidal categories", Macquarie Math Reports 860081 (1986).
* Proposition 2.1 of AndrÃ© Joyal and Ross Street,
  "Braided tensor categories" , Adv. Math. 102 (1993), 20â€“78.
* Exercise 8.1.6 of Etingof, Gelaki, Nikshych, Ostrik,
  "Tensor categories", vol 25, Mathematical Surveys and Monographs (2015), AMS.
-/

variable (C : Type uâ‚) [Category.{vâ‚} C] [MonoidalCategory C] [BraidedCategory C]

theorem braiding_leftUnitor_auxâ‚ (X : C) :
    (Î±_ (ğŸ™_ C) (ğŸ™_ C) X).hom â‰«
        (ğŸ™ (ğŸ™_ C) âŠ— (Î²_ X (ğŸ™_ C)).inv) â‰« (Î±_ _ X _).inv â‰« ((Î»_ X).hom âŠ— ğŸ™ _) =
      ((Î»_ _).hom âŠ— ğŸ™ X) â‰« (Î²_ X (ğŸ™_ C)).inv :=
  by rw [â† leftUnitor_tensor, leftUnitor_naturality]; simp
#align category_theory.braiding_left_unitor_auxâ‚ CategoryTheory.braiding_leftUnitor_auxâ‚

theorem braiding_leftUnitor_auxâ‚‚ (X : C) :
    ((Î²_ X (ğŸ™_ C)).hom âŠ— ğŸ™ (ğŸ™_ C)) â‰« ((Î»_ X).hom âŠ— ğŸ™ (ğŸ™_ C)) = (Ï_ X).hom âŠ— ğŸ™ (ğŸ™_ C) :=
  calc
    ((Î²_ X (ğŸ™_ C)).hom âŠ— ğŸ™ (ğŸ™_ C)) â‰« ((Î»_ X).hom âŠ— ğŸ™ (ğŸ™_ C)) =
      ((Î²_ X (ğŸ™_ C)).hom âŠ— ğŸ™ (ğŸ™_ C)) â‰« (Î±_ _ _ _).hom â‰« (Î±_ _ _ _).inv â‰« ((Î»_ X).hom âŠ— ğŸ™ (ğŸ™_ C)) :=
      by coherence
    _ = ((Î²_ X (ğŸ™_ C)).hom âŠ— ğŸ™ (ğŸ™_ C)) â‰« (Î±_ _ _ _).hom â‰« (ğŸ™ _ âŠ— (Î²_ X _).hom) â‰«
          (ğŸ™ _ âŠ— (Î²_ X _).inv) â‰« (Î±_ _ _ _).inv â‰« ((Î»_ X).hom âŠ— ğŸ™ (ğŸ™_ C)) :=
      by (slice_rhs 3 4 => rw [â† id_tensor_comp, Iso.hom_inv_id, tensor_id]); rw [id_comp]
    _ = (Î±_ _ _ _).hom â‰« (Î²_ _ _).hom â‰« (Î±_ _ _ _).hom â‰« (ğŸ™ _ âŠ— (Î²_ X _).inv) â‰« (Î±_ _ _ _).inv â‰«
          ((Î»_ X).hom âŠ— ğŸ™ (ğŸ™_ C)) :=
      by (slice_lhs 1 3 => rw [â† hexagon_forward]); simp only [assoc]
    _ = (Î±_ _ _ _).hom â‰« (Î²_ _ _).hom â‰« ((Î»_ _).hom âŠ— ğŸ™ X) â‰« (Î²_ X _).inv :=
      by rw [braiding_leftUnitor_auxâ‚]
    _ = (Î±_ _ _ _).hom â‰« (ğŸ™ _ âŠ— (Î»_ _).hom) â‰« (Î²_ _ _).hom â‰« (Î²_ X _).inv :=
      by (slice_lhs 2 3 => rw [â† braiding_naturality]); simp only [assoc]
    _ = (Î±_ _ _ _).hom â‰« (ğŸ™ _ âŠ— (Î»_ _).hom) := by rw [Iso.hom_inv_id, comp_id]
    _ = (Ï_ X).hom âŠ— ğŸ™ (ğŸ™_ C) := by rw [triangle]

#align category_theory.braiding_left_unitor_auxâ‚‚ CategoryTheory.braiding_leftUnitor_auxâ‚‚

@[simp]
theorem braiding_leftUnitor (X : C) : (Î²_ X (ğŸ™_ C)).hom â‰« (Î»_ X).hom = (Ï_ X).hom := by
  rw [â† tensor_right_iff, comp_tensor_id, braiding_leftUnitor_auxâ‚‚]
#align category_theory.braiding_left_unitor CategoryTheory.braiding_leftUnitor

theorem braiding_rightUnitor_auxâ‚ (X : C) :
    (Î±_ X (ğŸ™_ C) (ğŸ™_ C)).inv â‰«
        ((Î²_ (ğŸ™_ C) X).inv âŠ— ğŸ™ (ğŸ™_ C)) â‰« (Î±_ _ X _).hom â‰« (ğŸ™ _ âŠ— (Ï_ X).hom) =
      (ğŸ™ X âŠ— (Ï_ _).hom) â‰« (Î²_ (ğŸ™_ C) X).inv :=
  by rw [â† rightUnitor_tensor, rightUnitor_naturality]; simp
#align category_theory.braiding_right_unitor_auxâ‚ CategoryTheory.braiding_rightUnitor_auxâ‚

theorem braiding_rightUnitor_auxâ‚‚ (X : C) :
    (ğŸ™ (ğŸ™_ C) âŠ— (Î²_ (ğŸ™_ C) X).hom) â‰« (ğŸ™ (ğŸ™_ C) âŠ— (Ï_ X).hom) = ğŸ™ (ğŸ™_ C) âŠ— (Î»_ X).hom :=
  calc
    (ğŸ™ (ğŸ™_ C) âŠ— (Î²_ (ğŸ™_ C) X).hom) â‰« (ğŸ™ (ğŸ™_ C) âŠ— (Ï_ X).hom) =
      (ğŸ™ (ğŸ™_ C) âŠ— (Î²_ (ğŸ™_ C) X).hom) â‰« (Î±_ _ _ _).inv â‰« (Î±_ _ _ _).hom â‰« (ğŸ™ (ğŸ™_ C) âŠ— (Ï_ X).hom) :=
      by coherence
    _ = (ğŸ™ (ğŸ™_ C) âŠ— (Î²_ (ğŸ™_ C) X).hom) â‰« (Î±_ _ _ _).inv â‰« ((Î²_ _ X).hom âŠ— ğŸ™ _) â‰«
          ((Î²_ _ X).inv âŠ— ğŸ™ _) â‰« (Î±_ _ _ _).hom â‰« (ğŸ™ (ğŸ™_ C) âŠ— (Ï_ X).hom) :=
      by (slice_rhs 3 4 => rw [â† comp_tensor_id, Iso.hom_inv_id, tensor_id]); rw [id_comp]
    _ = (Î±_ _ _ _).inv â‰« (Î²_ _ _).hom â‰« (Î±_ _ _ _).inv â‰« ((Î²_ _ X).inv âŠ— ğŸ™ _) â‰« (Î±_ _ _ _).hom â‰«
          (ğŸ™ (ğŸ™_ C) âŠ— (Ï_ X).hom) :=
      by (slice_lhs 1 3 => rw [â† hexagon_reverse]); simp only [assoc]
    _ = (Î±_ _ _ _).inv â‰« (Î²_ _ _).hom â‰« (ğŸ™ X âŠ— (Ï_ _).hom) â‰« (Î²_ _ X).inv :=
      by rw [braiding_rightUnitor_auxâ‚]
    _ = (Î±_ _ _ _).inv â‰« ((Ï_ _).hom âŠ— ğŸ™ _) â‰« (Î²_ _ X).hom â‰« (Î²_ _ _).inv :=
      by (slice_lhs 2 3 => rw [â† braiding_naturality]); simp only [assoc]
    _ = (Î±_ _ _ _).inv â‰« ((Ï_ _).hom âŠ— ğŸ™ _) := by rw [Iso.hom_inv_id, comp_id]
    _ = ğŸ™ (ğŸ™_ C) âŠ— (Î»_ X).hom := by rw [triangle_assoc_comp_right]

#align category_theory.braiding_right_unitor_auxâ‚‚ CategoryTheory.braiding_rightUnitor_auxâ‚‚

@[simp]
theorem braiding_rightUnitor (X : C) : (Î²_ (ğŸ™_ C) X).hom â‰« (Ï_ X).hom = (Î»_ X).hom := by
  rw [â† tensor_left_iff, id_tensor_comp, braiding_rightUnitor_auxâ‚‚]
#align category_theory.braiding_right_unitor CategoryTheory.braiding_rightUnitor

@[simp]
theorem leftUnitor_inv_braiding (X : C) : (Î»_ X).inv â‰« (Î²_ (ğŸ™_ C) X).hom = (Ï_ X).inv := by
  apply (cancel_mono (Ï_ X).hom).1
  simp only [assoc, braiding_rightUnitor, Iso.inv_hom_id]
#align category_theory.left_unitor_inv_braiding CategoryTheory.leftUnitor_inv_braiding

@[simp]
theorem rightUnitor_inv_braiding (X : C) : (Ï_ X).inv â‰« (Î²_ X (ğŸ™_ C)).hom = (Î»_ X).inv := by
  apply (cancel_mono (Î»_ X).hom).1
  simp only [assoc, braiding_leftUnitor, Iso.inv_hom_id]
#align category_theory.right_unitor_inv_braiding CategoryTheory.rightUnitor_inv_braiding

end

/--
A symmetric monoidal category is a braided monoidal category for which the braiding is symmetric.

See <https://stacks.math.columbia.edu/tag/0FFW>.
-/
class SymmetricCategory (C : Type u) [Category.{v} C] [MonoidalCategory.{v} C] extends
    BraidedCategory.{v} C where
  -- braiding symmetric:
  symmetry : âˆ€ X Y : C, (Î²_ X Y).hom â‰« (Î²_ Y X).hom = ğŸ™ (X âŠ— Y) := by aesop_cat
#align category_theory.symmetric_category CategoryTheory.SymmetricCategory

attribute [reassoc (attr := simp)] SymmetricCategory.symmetry

variable (C : Type uâ‚) [Category.{vâ‚} C] [MonoidalCategory C] [BraidedCategory C]
variable (D : Type uâ‚‚) [Category.{vâ‚‚} D] [MonoidalCategory D] [BraidedCategory D]
variable (E : Type uâ‚ƒ) [Category.{vâ‚ƒ} E] [MonoidalCategory E] [BraidedCategory E]

/-- A lax braided functor between braided monoidal categories is a lax monoidal functor
which preserves the braiding.
-/
structure LaxBraidedFunctor extends LaxMonoidalFunctor C D where
  braided : âˆ€ X Y : C, Î¼ X Y â‰« map (Î²_ X Y).hom = (Î²_ (obj X) (obj Y)).hom â‰« Î¼ Y X := by aesop_cat
#align category_theory.lax_braided_functor CategoryTheory.LaxBraidedFunctor

namespace LaxBraidedFunctor

/-- The identity lax braided monoidal functor. -/
@[simps!]
def id : LaxBraidedFunctor C C :=
  { MonoidalFunctor.id C with }
#align category_theory.lax_braided_functor.id CategoryTheory.LaxBraidedFunctor.id

instance : Inhabited (LaxBraidedFunctor C C) :=
  âŸ¨id CâŸ©

variable {C D E}

/-- The composition of lax braided monoidal functors. -/
@[simps!]
def comp (F : LaxBraidedFunctor C D) (G : LaxBraidedFunctor D E) : LaxBraidedFunctor C E :=
  { LaxMonoidalFunctor.comp F.toLaxMonoidalFunctor G.toLaxMonoidalFunctor with
    braided := fun X Y => by
      dsimp
      slice_lhs 2 3 =>
        rw [â† CategoryTheory.Functor.map_comp, F.braided, CategoryTheory.Functor.map_comp]
      slice_lhs 1 2 => rw [G.braided]
      simp only [Category.assoc] }
#align category_theory.lax_braided_functor.comp CategoryTheory.LaxBraidedFunctor.comp

instance categoryLaxBraidedFunctor : Category (LaxBraidedFunctor C D) :=
  InducedCategory.category LaxBraidedFunctor.toLaxMonoidalFunctor
#align category_theory.lax_braided_functor.category_lax_braided_functor CategoryTheory.LaxBraidedFunctor.categoryLaxBraidedFunctor

-- Porting note: added, as `MonoidalNatTrans.ext` does not apply to morphisms.
@[ext]
lemma ext' {F G : LaxBraidedFunctor C D} {Î± Î² : F âŸ¶ G} (w : âˆ€ X : C, Î±.app X = Î².app X) : Î± = Î² :=
  MonoidalNatTrans.ext _ _ (funext w)

@[simp]
theorem comp_toNatTrans {F G H : LaxBraidedFunctor C D} {Î± : F âŸ¶ G} {Î² : G âŸ¶ H} :
    (Î± â‰« Î²).toNatTrans = @CategoryStruct.comp (C â¥¤ D) _ _ _ _ Î±.toNatTrans Î².toNatTrans :=
  rfl
#align category_theory.lax_braided_functor.comp_to_nat_trans CategoryTheory.LaxBraidedFunctor.comp_toNatTrans

/-- Interpret a natural isomorphism of the underlying lax monoidal functors as an
isomorphism of the lax braided monoidal functors.
-/
@[simps]
def mkIso {F G : LaxBraidedFunctor C D} (i : F.toLaxMonoidalFunctor â‰… G.toLaxMonoidalFunctor) :
    F â‰… G :=
  { i with }
#align category_theory.lax_braided_functor.mk_iso CategoryTheory.LaxBraidedFunctor.mkIso

end LaxBraidedFunctor

/-- A braided functor between braided monoidal categories is a monoidal functor
which preserves the braiding.
-/
structure BraidedFunctor extends MonoidalFunctor C D where
  -- Note this is stated differently than for `LaxBraidedFunctor`.
  -- We move the `Î¼ X Y` to the right hand side,
  -- so that this makes a good `@[simp]` lemma.
  braided : âˆ€ X Y : C, map (Î²_ X Y).hom = inv (Î¼ X Y) â‰« (Î²_ (obj X) (obj Y)).hom â‰« Î¼ Y X := by
    aesop_cat
#align category_theory.braided_functor CategoryTheory.BraidedFunctor

attribute [simp] BraidedFunctor.braided

/--
A braided category with a faithful braided functor to a symmetric category is itself symmetric.
-/
def symmetricCategoryOfFaithful {C D : Type*} [Category C] [Category D] [MonoidalCategory C]
    [MonoidalCategory D] [BraidedCategory C] [SymmetricCategory D] (F : BraidedFunctor C D)
    [Faithful F.toFunctor] : SymmetricCategory C where
  symmetry X Y := F.map_injective (by simp)
#align category_theory.symmetric_category_of_faithful CategoryTheory.symmetricCategoryOfFaithful

namespace BraidedFunctor

/-- Turn a braided functor into a lax braided functor. -/
@[simps toLaxMonoidalFunctor]
def toLaxBraidedFunctor (F : BraidedFunctor C D) : LaxBraidedFunctor C D :=
  { toLaxMonoidalFunctor := F.toLaxMonoidalFunctor
    braided := fun X Y => by rw [F.braided]; simp }
#align category_theory.braided_functor.to_lax_braided_functor CategoryTheory.BraidedFunctor.toLaxBraidedFunctor

/-- The identity braided monoidal functor. -/
@[simps!]
def id : BraidedFunctor C C :=
  { MonoidalFunctor.id C with }
#align category_theory.braided_functor.id CategoryTheory.BraidedFunctor.id

instance : Inhabited (BraidedFunctor C C) :=
  âŸ¨id CâŸ©

variable {C D E}

/-- The composition of braided monoidal functors. -/
@[simps!]
def comp (F : BraidedFunctor C D) (G : BraidedFunctor D E) : BraidedFunctor C E :=
  { MonoidalFunctor.comp F.toMonoidalFunctor G.toMonoidalFunctor with }
#align category_theory.braided_functor.comp CategoryTheory.BraidedFunctor.comp

instance categoryBraidedFunctor : Category (BraidedFunctor C D) :=
  InducedCategory.category BraidedFunctor.toMonoidalFunctor
#align category_theory.braided_functor.category_braided_functor CategoryTheory.BraidedFunctor.categoryBraidedFunctor

-- Porting note: added, as `MonoidalNatTrans.ext` does not apply to morphisms.
@[ext]
lemma ext' {F G : BraidedFunctor C D} {Î± Î² : F âŸ¶ G} (w : âˆ€ X : C, Î±.app X = Î².app X) : Î± = Î² :=
  MonoidalNatTrans.ext _ _ (funext w)

@[simp]
theorem comp_toNatTrans {F G H : BraidedFunctor C D} {Î± : F âŸ¶ G} {Î² : G âŸ¶ H} :
    (Î± â‰« Î²).toNatTrans = @CategoryStruct.comp (C â¥¤ D) _ _ _ _ Î±.toNatTrans Î².toNatTrans :=
  rfl
#align category_theory.braided_functor.comp_to_nat_trans CategoryTheory.BraidedFunctor.comp_toNatTrans

/-- Interpret a natural isomorphism of the underlying monoidal functors as an
isomorphism of the braided monoidal functors.
-/
@[simps]
def mkIso {F G : BraidedFunctor C D} (i : F.toMonoidalFunctor â‰… G.toMonoidalFunctor) : F â‰… G :=
  { i with }
#align category_theory.braided_functor.mk_iso CategoryTheory.BraidedFunctor.mkIso

end BraidedFunctor

section CommMonoid

variable (M : Type u) [CommMonoid M]

instance : BraidedCategory (Discrete M) where
  braiding X Y := Discrete.eqToIso (mul_comm X.as Y.as)

variable {M} {N : Type u} [CommMonoid N]

/-- A multiplicative morphism between commutative monoids gives a braided functor between
the corresponding discrete braided monoidal categories.
-/
@[simps!]
def Discrete.braidedFunctor (F : M â†’* N) : BraidedFunctor (Discrete M) (Discrete N) :=
  { Discrete.monoidalFunctor F with }
#align category_theory.discrete.braided_functor CategoryTheory.Discrete.braidedFunctor

end CommMonoid

section Tensor

/-- The strength of the tensor product functor from `C Ã— C` to `C`. -/
def tensor_Î¼ (X Y : C Ã— C) : (tensor C).obj X âŠ— (tensor C).obj Y âŸ¶ (tensor C).obj (X âŠ— Y) :=
  (Î±_ X.1 X.2 (Y.1 âŠ— Y.2)).hom â‰«
    (ğŸ™ X.1 âŠ— (Î±_ X.2 Y.1 Y.2).inv) â‰«
      (ğŸ™ X.1 âŠ— (Î²_ X.2 Y.1).hom âŠ— ğŸ™ Y.2) â‰«
        (ğŸ™ X.1 âŠ— (Î±_ Y.1 X.2 Y.2).hom) â‰« (Î±_ X.1 Y.1 (X.2 âŠ— Y.2)).inv
#align category_theory.tensor_Î¼ CategoryTheory.tensor_Î¼

theorem tensor_Î¼_defâ‚ (Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : C) :
    tensor_Î¼ C (Xâ‚, Xâ‚‚) (Yâ‚, Yâ‚‚) â‰« (Î±_ Xâ‚ Yâ‚ (Xâ‚‚ âŠ— Yâ‚‚)).hom â‰« (ğŸ™ Xâ‚ âŠ— (Î±_ Yâ‚ Xâ‚‚ Yâ‚‚).inv) =
      (Î±_ Xâ‚ Xâ‚‚ (Yâ‚ âŠ— Yâ‚‚)).hom â‰« (ğŸ™ Xâ‚ âŠ— (Î±_ Xâ‚‚ Yâ‚ Yâ‚‚).inv) â‰« (ğŸ™ Xâ‚ âŠ— (Î²_ Xâ‚‚ Yâ‚).hom âŠ— ğŸ™ Yâ‚‚) :=
  by dsimp [tensor_Î¼]; simp
#align category_theory.tensor_Î¼_defâ‚ CategoryTheory.tensor_Î¼_defâ‚

theorem tensor_Î¼_defâ‚‚ (Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : C) :
    (ğŸ™ Xâ‚ âŠ— (Î±_ Xâ‚‚ Yâ‚ Yâ‚‚).hom) â‰« (Î±_ Xâ‚ Xâ‚‚ (Yâ‚ âŠ— Yâ‚‚)).inv â‰« tensor_Î¼ C (Xâ‚, Xâ‚‚) (Yâ‚, Yâ‚‚) =
      (ğŸ™ Xâ‚ âŠ— (Î²_ Xâ‚‚ Yâ‚).hom âŠ— ğŸ™ Yâ‚‚) â‰« (ğŸ™ Xâ‚ âŠ— (Î±_ Yâ‚ Xâ‚‚ Yâ‚‚).hom) â‰« (Î±_ Xâ‚ Yâ‚ (Xâ‚‚ âŠ— Yâ‚‚)).inv :=
  by dsimp [tensor_Î¼]; simp
#align category_theory.tensor_Î¼_defâ‚‚ CategoryTheory.tensor_Î¼_defâ‚‚

theorem tensor_Î¼_natural {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ Uâ‚ Uâ‚‚ Vâ‚ Vâ‚‚ : C} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (gâ‚ : Uâ‚ âŸ¶ Vâ‚)
    (gâ‚‚ : Uâ‚‚ âŸ¶ Vâ‚‚) :
    ((fâ‚ âŠ— fâ‚‚) âŠ— gâ‚ âŠ— gâ‚‚) â‰« tensor_Î¼ C (Yâ‚, Yâ‚‚) (Vâ‚, Vâ‚‚) =
      tensor_Î¼ C (Xâ‚, Xâ‚‚) (Uâ‚, Uâ‚‚) â‰« ((fâ‚ âŠ— gâ‚) âŠ— fâ‚‚ âŠ— gâ‚‚) := by
  dsimp [tensor_Î¼]
  slice_lhs 1 2 => rw [associator_naturality]
  slice_lhs 2 3 =>
    rw [â† tensor_comp, comp_id fâ‚, â† id_comp fâ‚, associator_inv_naturality, tensor_comp]
  slice_lhs 3 4 =>
    rw [â† tensor_comp, â† tensor_comp, comp_id fâ‚, â† id_comp fâ‚, comp_id gâ‚‚, â† id_comp gâ‚‚,
      braiding_naturality, tensor_comp, tensor_comp]
  slice_lhs 4 5 => rw [â† tensor_comp, comp_id fâ‚, â† id_comp fâ‚, associator_naturality, tensor_comp]
  slice_lhs 5 6 => rw [associator_inv_naturality]
  simp only [assoc]
#align category_theory.tensor_Î¼_natural CategoryTheory.tensor_Î¼_natural

@[reassoc]
theorem tensor_Î¼_natural_left {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : C} (fâ‚: Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (Zâ‚ Zâ‚‚ : C) :
    ((fâ‚ âŠ— fâ‚‚) âŠ— ğŸ™ (Zâ‚ âŠ— Zâ‚‚)) â‰« tensor_Î¼ C (Yâ‚, Yâ‚‚) (Zâ‚, Zâ‚‚) =
      tensor_Î¼ C (Xâ‚, Xâ‚‚) (Zâ‚, Zâ‚‚) â‰« ((fâ‚ âŠ— ğŸ™ Zâ‚) âŠ— (fâ‚‚ âŠ— ğŸ™ Zâ‚‚)) := by
  convert tensor_Î¼_natural C fâ‚ fâ‚‚ (ğŸ™ Zâ‚) (ğŸ™ Zâ‚‚) using 1; simp

@[reassoc]
theorem tensor_Î¼_natural_right (Zâ‚ Zâ‚‚ : C) {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : C} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) :
    (ğŸ™ (Zâ‚ âŠ— Zâ‚‚) âŠ— (fâ‚ âŠ— fâ‚‚)) â‰« tensor_Î¼ C (Zâ‚, Zâ‚‚) (Yâ‚, Yâ‚‚) =
      tensor_Î¼ C (Zâ‚, Zâ‚‚) (Xâ‚, Xâ‚‚) â‰« ((ğŸ™ Zâ‚ âŠ— fâ‚) âŠ— (ğŸ™ Zâ‚‚ âŠ— fâ‚‚)) := by
  convert tensor_Î¼_natural C (ğŸ™ Zâ‚) (ğŸ™ Zâ‚‚) fâ‚ fâ‚‚ using 1; simp

theorem tensor_left_unitality (Xâ‚ Xâ‚‚ : C) :
    (Î»_ (Xâ‚ âŠ— Xâ‚‚)).hom =
      ((Î»_ (ğŸ™_ C)).inv âŠ— ğŸ™ (Xâ‚ âŠ— Xâ‚‚)) â‰«
        tensor_Î¼ C (ğŸ™_ C, ğŸ™_ C) (Xâ‚, Xâ‚‚) â‰« ((Î»_ Xâ‚).hom âŠ— (Î»_ Xâ‚‚).hom) := by
  dsimp [tensor_Î¼]
  have :
    ((Î»_ (ğŸ™_ C)).inv âŠ— ğŸ™ (Xâ‚ âŠ— Xâ‚‚)) â‰«
        (Î±_ (ğŸ™_ C) (ğŸ™_ C) (Xâ‚ âŠ— Xâ‚‚)).hom â‰« (ğŸ™ (ğŸ™_ C) âŠ— (Î±_ (ğŸ™_ C) Xâ‚ Xâ‚‚).inv) =
      ğŸ™ (ğŸ™_ C) âŠ— (Î»_ Xâ‚).inv âŠ— ğŸ™ Xâ‚‚ :=
    by pure_coherence
  slice_rhs 1 3 => rw [this]
  clear this
  slice_rhs 1 2 => rw [â† tensor_comp, â† tensor_comp, comp_id, comp_id, leftUnitor_inv_braiding]
  simp only [assoc]
  coherence
#align category_theory.tensor_left_unitality CategoryTheory.tensor_left_unitality

theorem tensor_right_unitality (Xâ‚ Xâ‚‚ : C) :
    (Ï_ (Xâ‚ âŠ— Xâ‚‚)).hom =
      (ğŸ™ (Xâ‚ âŠ— Xâ‚‚) âŠ— (Î»_ (ğŸ™_ C)).inv) â‰«
        tensor_Î¼ C (Xâ‚, Xâ‚‚) (ğŸ™_ C, ğŸ™_ C) â‰« ((Ï_ Xâ‚).hom âŠ— (Ï_ Xâ‚‚).hom) := by
  dsimp [tensor_Î¼]
  have :
    (ğŸ™ (Xâ‚ âŠ— Xâ‚‚) âŠ— (Î»_ (ğŸ™_ C)).inv) â‰«
        (Î±_ Xâ‚ Xâ‚‚ (ğŸ™_ C âŠ— ğŸ™_ C)).hom â‰« (ğŸ™ Xâ‚ âŠ— (Î±_ Xâ‚‚ (ğŸ™_ C) (ğŸ™_ C)).inv) =
      (Î±_ Xâ‚ Xâ‚‚ (ğŸ™_ C)).hom â‰« (ğŸ™ Xâ‚ âŠ— (Ï_ Xâ‚‚).inv âŠ— ğŸ™ (ğŸ™_ C)) :=
    by pure_coherence
  slice_rhs 1 3 => rw [this]
  clear this
  slice_rhs 2 3 => rw [â† tensor_comp, â† tensor_comp, comp_id, comp_id, rightUnitor_inv_braiding]
  simp only [assoc]
  coherence
#align category_theory.tensor_right_unitality CategoryTheory.tensor_right_unitality

/-
Diagram B6 from Proposition 1 of [Joyal and Street, *Braided monoidal categories*][Joyal_Street].
-/
theorem tensor_associativity_aux (W X Y Z : C) :
    ((Î²_ W X).hom âŠ— ğŸ™ (Y âŠ— Z)) â‰«
        (Î±_ X W (Y âŠ— Z)).hom â‰«
          (ğŸ™ X âŠ— (Î±_ W Y Z).inv) â‰« (ğŸ™ X âŠ— (Î²_ (W âŠ— Y) Z).hom) â‰« (ğŸ™ X âŠ— (Î±_ Z W Y).inv) =
      (ğŸ™ (W âŠ— X) âŠ— (Î²_ Y Z).hom) â‰«
        (Î±_ (W âŠ— X) Z Y).inv â‰«
          ((Î±_ W X Z).hom âŠ— ğŸ™ Y) â‰«
            ((Î²_ W (X âŠ— Z)).hom âŠ— ğŸ™ Y) â‰« ((Î±_ X Z W).hom âŠ— ğŸ™ Y) â‰« (Î±_ X (Z âŠ— W) Y).hom := by
  slice_rhs 3 5 => rw [â† tensor_comp, â† tensor_comp, hexagon_forward, tensor_comp, tensor_comp]
  slice_rhs 5 6 => rw [associator_naturality]
  slice_rhs 2 3 => rw [â† associator_inv_naturality]
  slice_rhs 3 5 => rw [â† pentagon_hom_inv]
  slice_rhs 1 2 => rw [tensor_id, id_tensor_comp_tensor_id, â† tensor_id_comp_id_tensor]
  slice_rhs 2 3 => rw [â† tensor_id, associator_naturality]
  slice_rhs 3 5 => rw [â† tensor_comp, â† tensor_comp, â† hexagon_reverse, tensor_comp, tensor_comp]
#align category_theory.tensor_associativity_aux CategoryTheory.tensor_associativity_aux

set_option maxHeartbeats 400000 in
theorem tensor_associativity (Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ Zâ‚ Zâ‚‚ : C) :
    (tensor_Î¼ C (Xâ‚, Xâ‚‚) (Yâ‚, Yâ‚‚) âŠ— ğŸ™ (Zâ‚ âŠ— Zâ‚‚)) â‰«
        tensor_Î¼ C (Xâ‚ âŠ— Yâ‚, Xâ‚‚ âŠ— Yâ‚‚) (Zâ‚, Zâ‚‚) â‰« ((Î±_ Xâ‚ Yâ‚ Zâ‚).hom âŠ— (Î±_ Xâ‚‚ Yâ‚‚ Zâ‚‚).hom) =
      (Î±_ (Xâ‚ âŠ— Xâ‚‚) (Yâ‚ âŠ— Yâ‚‚) (Zâ‚ âŠ— Zâ‚‚)).hom â‰«
        (ğŸ™ (Xâ‚ âŠ— Xâ‚‚) âŠ— tensor_Î¼ C (Yâ‚, Yâ‚‚) (Zâ‚, Zâ‚‚)) â‰« tensor_Î¼ C (Xâ‚, Xâ‚‚) (Yâ‚ âŠ— Zâ‚, Yâ‚‚ âŠ— Zâ‚‚) := by
  have :
    (Î±_ Xâ‚ Yâ‚ Zâ‚).hom âŠ— (Î±_ Xâ‚‚ Yâ‚‚ Zâ‚‚).hom =
      (Î±_ (Xâ‚ âŠ— Yâ‚) Zâ‚ ((Xâ‚‚ âŠ— Yâ‚‚) âŠ— Zâ‚‚)).hom â‰«
        (ğŸ™ (Xâ‚ âŠ— Yâ‚) âŠ— (Î±_ Zâ‚ (Xâ‚‚ âŠ— Yâ‚‚) Zâ‚‚).inv) â‰«
          (Î±_ Xâ‚ Yâ‚ ((Zâ‚ âŠ— Xâ‚‚ âŠ— Yâ‚‚) âŠ— Zâ‚‚)).hom â‰«
            (ğŸ™ Xâ‚ âŠ— (Î±_ Yâ‚ (Zâ‚ âŠ— Xâ‚‚ âŠ— Yâ‚‚) Zâ‚‚).inv) â‰«
              (Î±_ Xâ‚ (Yâ‚ âŠ— Zâ‚ âŠ— Xâ‚‚ âŠ— Yâ‚‚) Zâ‚‚).inv â‰«
                ((ğŸ™ Xâ‚ âŠ— ğŸ™ Yâ‚ âŠ— (Î±_ Zâ‚ Xâ‚‚ Yâ‚‚).inv) âŠ— ğŸ™ Zâ‚‚) â‰«
                  ((ğŸ™ Xâ‚ âŠ— (Î±_ Yâ‚ (Zâ‚ âŠ— Xâ‚‚) Yâ‚‚).inv) âŠ— ğŸ™ Zâ‚‚) â‰«
                    ((ğŸ™ Xâ‚ âŠ— (Î±_ Yâ‚ Zâ‚ Xâ‚‚).inv âŠ— ğŸ™ Yâ‚‚) âŠ— ğŸ™ Zâ‚‚) â‰«
                      (Î±_ Xâ‚ (((Yâ‚ âŠ— Zâ‚) âŠ— Xâ‚‚) âŠ— Yâ‚‚) Zâ‚‚).hom â‰«
                        (ğŸ™ Xâ‚ âŠ— (Î±_ ((Yâ‚ âŠ— Zâ‚) âŠ— Xâ‚‚) Yâ‚‚ Zâ‚‚).hom) â‰«
                          (ğŸ™ Xâ‚ âŠ— (Î±_ (Yâ‚ âŠ— Zâ‚) Xâ‚‚ (Yâ‚‚ âŠ— Zâ‚‚)).hom) â‰«
                            (Î±_ Xâ‚ (Yâ‚ âŠ— Zâ‚) (Xâ‚‚ âŠ— Yâ‚‚ âŠ— Zâ‚‚)).inv :=
    by pure_coherence
  rw [this]; clear this
  slice_lhs 2 4 => rw [tensor_Î¼_defâ‚]
  slice_lhs 4 5 => rw [â† tensor_id, associator_naturality]
  slice_lhs 5 6 => rw [â† tensor_comp, associator_inv_naturality, tensor_comp]
  slice_lhs 6 7 => rw [associator_inv_naturality]
  have :
    (Î±_ (Xâ‚ âŠ— Yâ‚) (Xâ‚‚ âŠ— Yâ‚‚) (Zâ‚ âŠ— Zâ‚‚)).hom â‰«
        (ğŸ™ (Xâ‚ âŠ— Yâ‚) âŠ— (Î±_ (Xâ‚‚ âŠ— Yâ‚‚) Zâ‚ Zâ‚‚).inv) â‰«
          (Î±_ Xâ‚ Yâ‚ (((Xâ‚‚ âŠ— Yâ‚‚) âŠ— Zâ‚) âŠ— Zâ‚‚)).hom â‰«
            (ğŸ™ Xâ‚ âŠ— (Î±_ Yâ‚ ((Xâ‚‚ âŠ— Yâ‚‚) âŠ— Zâ‚) Zâ‚‚).inv) â‰« (Î±_ Xâ‚ (Yâ‚ âŠ— (Xâ‚‚ âŠ— Yâ‚‚) âŠ— Zâ‚) Zâ‚‚).inv =
      ((Î±_ Xâ‚ Yâ‚ (Xâ‚‚ âŠ— Yâ‚‚)).hom âŠ— ğŸ™ (Zâ‚ âŠ— Zâ‚‚)) â‰«
        ((ğŸ™ Xâ‚ âŠ— (Î±_ Yâ‚ Xâ‚‚ Yâ‚‚).inv) âŠ— ğŸ™ (Zâ‚ âŠ— Zâ‚‚)) â‰«
          (Î±_ (Xâ‚ âŠ— (Yâ‚ âŠ— Xâ‚‚) âŠ— Yâ‚‚) Zâ‚ Zâ‚‚).inv â‰«
            ((Î±_ Xâ‚ ((Yâ‚ âŠ— Xâ‚‚) âŠ— Yâ‚‚) Zâ‚).hom âŠ— ğŸ™ Zâ‚‚) â‰«
              ((ğŸ™ Xâ‚ âŠ— (Î±_ (Yâ‚ âŠ— Xâ‚‚) Yâ‚‚ Zâ‚).hom) âŠ— ğŸ™ Zâ‚‚) â‰«
                ((ğŸ™ Xâ‚ âŠ— (Î±_ Yâ‚ Xâ‚‚ (Yâ‚‚ âŠ— Zâ‚)).hom) âŠ— ğŸ™ Zâ‚‚) â‰«
                  ((ğŸ™ Xâ‚ âŠ— ğŸ™ Yâ‚ âŠ— (Î±_ Xâ‚‚ Yâ‚‚ Zâ‚).inv) âŠ— ğŸ™ Zâ‚‚) :=
    by pure_coherence
  slice_lhs 2 6 => rw [this]
  clear this
  slice_lhs 1 3 => rw [â† tensor_comp, â† tensor_comp, tensor_Î¼_defâ‚, tensor_comp, tensor_comp]
  slice_lhs 3 4 => rw [â† tensor_id, associator_inv_naturality]
  slice_lhs 4 5 => rw [â† tensor_comp, associator_naturality, tensor_comp]
  slice_lhs 5 6 =>
    rw [â† tensor_comp, â† tensor_comp, associator_naturality, tensor_comp, tensor_comp]
  slice_lhs 6 10 =>
    rw [â† tensor_comp, â† tensor_comp, â† tensor_comp, â† tensor_comp, â† tensor_comp, â† tensor_comp, â†
      tensor_comp, â† tensor_comp, tensor_id, tensor_associativity_aux, â† tensor_id, â†
      id_comp (ğŸ™ Xâ‚ â‰« ğŸ™ Xâ‚ â‰« ğŸ™ Xâ‚ â‰« ğŸ™ Xâ‚ â‰« ğŸ™ Xâ‚), â† id_comp (ğŸ™ Zâ‚‚ â‰« ğŸ™ Zâ‚‚ â‰« ğŸ™ Zâ‚‚ â‰« ğŸ™ Zâ‚‚ â‰« ğŸ™ Zâ‚‚),
      tensor_comp, tensor_comp, tensor_comp, tensor_comp, tensor_comp, tensor_comp, tensor_comp,
      tensor_comp, tensor_comp, tensor_comp]
  slice_lhs 11 12 =>
    rw [â† tensor_comp, â† tensor_comp, Iso.hom_inv_id]
    simp
  simp only [assoc, id_comp]
  slice_lhs 10 11 =>
    rw [â† tensor_comp, â† tensor_comp, â† tensor_comp, Iso.hom_inv_id]
    simp
  simp only [assoc, id_comp]
  slice_lhs 9 10 => rw [associator_naturality]
  slice_lhs 10 11 => rw [â† tensor_comp, associator_naturality, tensor_comp]
  slice_lhs 11 13 => rw [tensor_id, â† tensor_Î¼_defâ‚‚]
  have :
    ((ğŸ™ Xâ‚ âŠ— (Î±_ (Xâ‚‚ âŠ— Yâ‚) Zâ‚ Yâ‚‚).inv) âŠ— ğŸ™ Zâ‚‚) â‰«
        ((ğŸ™ Xâ‚ âŠ— (Î±_ Xâ‚‚ Yâ‚ Zâ‚).hom âŠ— ğŸ™ Yâ‚‚) âŠ— ğŸ™ Zâ‚‚) â‰«
          (Î±_ Xâ‚ ((Xâ‚‚ âŠ— Yâ‚ âŠ— Zâ‚) âŠ— Yâ‚‚) Zâ‚‚).hom â‰«
            (ğŸ™ Xâ‚ âŠ— (Î±_ (Xâ‚‚ âŠ— Yâ‚ âŠ— Zâ‚) Yâ‚‚ Zâ‚‚).hom) â‰«
              (ğŸ™ Xâ‚ âŠ— (Î±_ Xâ‚‚ (Yâ‚ âŠ— Zâ‚) (Yâ‚‚ âŠ— Zâ‚‚)).hom) â‰« (Î±_ Xâ‚ Xâ‚‚ ((Yâ‚ âŠ— Zâ‚) âŠ— Yâ‚‚ âŠ— Zâ‚‚)).inv =
      (Î±_ Xâ‚ ((Xâ‚‚ âŠ— Yâ‚) âŠ— Zâ‚ âŠ— Yâ‚‚) Zâ‚‚).hom â‰«
        (ğŸ™ Xâ‚ âŠ— (Î±_ (Xâ‚‚ âŠ— Yâ‚) (Zâ‚ âŠ— Yâ‚‚) Zâ‚‚).hom) â‰«
          (ğŸ™ Xâ‚ âŠ— (Î±_ Xâ‚‚ Yâ‚ ((Zâ‚ âŠ— Yâ‚‚) âŠ— Zâ‚‚)).hom) â‰«
            (Î±_ Xâ‚ Xâ‚‚ (Yâ‚ âŠ— (Zâ‚ âŠ— Yâ‚‚) âŠ— Zâ‚‚)).inv â‰«
              (ğŸ™ (Xâ‚ âŠ— Xâ‚‚) âŠ— ğŸ™ Yâ‚ âŠ— (Î±_ Zâ‚ Yâ‚‚ Zâ‚‚).hom) â‰« (ğŸ™ (Xâ‚ âŠ— Xâ‚‚) âŠ— (Î±_ Yâ‚ Zâ‚ (Yâ‚‚ âŠ— Zâ‚‚)).inv) :=
    by pure_coherence
  slice_lhs 7 12 => rw [this]
  clear this
  slice_lhs 6 7 => rw [associator_naturality]
  slice_lhs 7 8 => rw [â† tensor_comp, associator_naturality, tensor_comp]
  slice_lhs 8 9 => rw [â† tensor_comp, associator_naturality, tensor_comp]
  slice_lhs 9 10 => rw [associator_inv_naturality]
  slice_lhs 10 12 => rw [â† tensor_comp, â† tensor_comp, â† tensor_Î¼_defâ‚‚, tensor_comp, tensor_comp]
  dsimp
  coherence
#align category_theory.tensor_associativity CategoryTheory.tensor_associativity

/-- The tensor product functor from `C Ã— C` to `C` as a monoidal functor. -/
@[simps!]
def tensorMonoidal : MonoidalFunctor (C Ã— C) C :=
  { tensor C with
    Îµ := (Î»_ (ğŸ™_ C)).inv
    Î¼ := tensor_Î¼ C
    Î¼_natural_left := fun f Z => tensor_Î¼_natural_left C f.1 f.2 Z.1 Z.2
    Î¼_natural_right := fun Z f => tensor_Î¼_natural_right C Z.1 Z.2 f.1 f.2
    associativity := fun X Y Z => tensor_associativity C X.1 X.2 Y.1 Y.2 Z.1 Z.2
    left_unitality := fun âŸ¨Xâ‚, Xâ‚‚âŸ© => tensor_left_unitality C Xâ‚ Xâ‚‚
    right_unitality := fun âŸ¨Xâ‚, Xâ‚‚âŸ© => tensor_right_unitality C Xâ‚ Xâ‚‚
    Î¼_isIso := by dsimp [tensor_Î¼]; infer_instance }
#align category_theory.tensor_monoidal CategoryTheory.tensorMonoidal

theorem leftUnitor_monoidal (Xâ‚ Xâ‚‚ : C) :
    (Î»_ Xâ‚).hom âŠ— (Î»_ Xâ‚‚).hom =
      tensor_Î¼ C (ğŸ™_ C, Xâ‚) (ğŸ™_ C, Xâ‚‚) â‰« ((Î»_ (ğŸ™_ C)).hom âŠ— ğŸ™ (Xâ‚ âŠ— Xâ‚‚)) â‰« (Î»_ (Xâ‚ âŠ— Xâ‚‚)).hom := by
  dsimp [tensor_Î¼]
  have :
    (Î»_ Xâ‚).hom âŠ— (Î»_ Xâ‚‚).hom =
      (Î±_ (ğŸ™_ C) Xâ‚ (ğŸ™_ C âŠ— Xâ‚‚)).hom â‰«
        (ğŸ™ (ğŸ™_ C) âŠ— (Î±_ Xâ‚ (ğŸ™_ C) Xâ‚‚).inv) â‰« (Î»_ ((Xâ‚ âŠ— ğŸ™_ C) âŠ— Xâ‚‚)).hom â‰« ((Ï_ Xâ‚).hom âŠ— ğŸ™ Xâ‚‚) :=
    by pure_coherence
  rw [this]; clear this
  rw [â† braiding_leftUnitor]
  slice_lhs 3 4 => rw [â† id_comp (ğŸ™ Xâ‚‚), tensor_comp]
  slice_lhs 3 4 => rw [â† leftUnitor_naturality]
  coherence
#align category_theory.left_unitor_monoidal CategoryTheory.leftUnitor_monoidal

theorem rightUnitor_monoidal (Xâ‚ Xâ‚‚ : C) :
    (Ï_ Xâ‚).hom âŠ— (Ï_ Xâ‚‚).hom =
      tensor_Î¼ C (Xâ‚, ğŸ™_ C) (Xâ‚‚, ğŸ™_ C) â‰« (ğŸ™ (Xâ‚ âŠ— Xâ‚‚) âŠ— (Î»_ (ğŸ™_ C)).hom) â‰« (Ï_ (Xâ‚ âŠ— Xâ‚‚)).hom := by
  dsimp [tensor_Î¼]
  have :
    (Ï_ Xâ‚).hom âŠ— (Ï_ Xâ‚‚).hom =
      (Î±_ Xâ‚ (ğŸ™_ C) (Xâ‚‚ âŠ— ğŸ™_ C)).hom â‰«
        (ğŸ™ Xâ‚ âŠ— (Î±_ (ğŸ™_ C) Xâ‚‚ (ğŸ™_ C)).inv) â‰« (ğŸ™ Xâ‚ âŠ— (Ï_ (ğŸ™_ C âŠ— Xâ‚‚)).hom) â‰« (ğŸ™ Xâ‚ âŠ— (Î»_ Xâ‚‚).hom) :=
    by pure_coherence
  rw [this]; clear this
  rw [â† braiding_rightUnitor]
  slice_lhs 3 4 => rw [â† id_comp (ğŸ™ Xâ‚), tensor_comp, id_comp]
  slice_lhs 3 4 => rw [â† tensor_comp, â† rightUnitor_naturality, tensor_comp]
  coherence
#align category_theory.right_unitor_monoidal CategoryTheory.rightUnitor_monoidal

theorem associator_monoidal_aux (W X Y Z : C) :
    (ğŸ™ W âŠ— (Î²_ X (Y âŠ— Z)).hom) â‰«
        (ğŸ™ W âŠ— (Î±_ Y Z X).hom) â‰« (Î±_ W Y (Z âŠ— X)).inv â‰« ((Î²_ W Y).hom âŠ— ğŸ™ (Z âŠ— X)) =
      (Î±_ W X (Y âŠ— Z)).inv â‰«
        (Î±_ (W âŠ— X) Y Z).inv â‰«
          ((Î²_ (W âŠ— X) Y).hom âŠ— ğŸ™ Z) â‰«
            ((Î±_ Y W X).inv âŠ— ğŸ™ Z) â‰« (Î±_ (Y âŠ— W) X Z).hom â‰« (ğŸ™ (Y âŠ— W) âŠ— (Î²_ X Z).hom) := by
  slice_rhs 1 2 => rw [â† pentagon_inv]
  slice_rhs 3 5 => rw [â† tensor_comp, â† tensor_comp, hexagon_reverse, tensor_comp, tensor_comp]
  slice_rhs 5 6 => rw [associator_naturality]
  slice_rhs 6 7 => rw [tensor_id, tensor_id_comp_id_tensor, â† id_tensor_comp_tensor_id]
  slice_rhs 2 3 => rw [â† associator_inv_naturality]
  slice_rhs 3 5 => rw [pentagon_inv_inv_hom]
  slice_rhs 4 5 => rw [â† tensor_id, â† associator_inv_naturality]
  slice_rhs 2 4 => rw [â† tensor_comp, â† tensor_comp, â† hexagon_forward, tensor_comp, tensor_comp]
  simp
#align category_theory.associator_monoidal_aux CategoryTheory.associator_monoidal_aux

set_option maxHeartbeats 400000 in
theorem associator_monoidal (Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ : C) :
    tensor_Î¼ C (Xâ‚ âŠ— Xâ‚‚, Xâ‚ƒ) (Yâ‚ âŠ— Yâ‚‚, Yâ‚ƒ) â‰«
        (tensor_Î¼ C (Xâ‚, Xâ‚‚) (Yâ‚, Yâ‚‚) âŠ— ğŸ™ (Xâ‚ƒ âŠ— Yâ‚ƒ)) â‰« (Î±_ (Xâ‚ âŠ— Yâ‚) (Xâ‚‚ âŠ— Yâ‚‚) (Xâ‚ƒ âŠ— Yâ‚ƒ)).hom =
      ((Î±_ Xâ‚ Xâ‚‚ Xâ‚ƒ).hom âŠ— (Î±_ Yâ‚ Yâ‚‚ Yâ‚ƒ).hom) â‰«
        tensor_Î¼ C (Xâ‚, Xâ‚‚ âŠ— Xâ‚ƒ) (Yâ‚, Yâ‚‚ âŠ— Yâ‚ƒ) â‰« (ğŸ™ (Xâ‚ âŠ— Yâ‚) âŠ— tensor_Î¼ C (Xâ‚‚, Xâ‚ƒ) (Yâ‚‚, Yâ‚ƒ)) := by
  have :
    (Î±_ (Xâ‚ âŠ— Yâ‚) (Xâ‚‚ âŠ— Yâ‚‚) (Xâ‚ƒ âŠ— Yâ‚ƒ)).hom =
      ((Î±_ Xâ‚ Yâ‚ (Xâ‚‚ âŠ— Yâ‚‚)).hom âŠ— ğŸ™ (Xâ‚ƒ âŠ— Yâ‚ƒ)) â‰«
        ((ğŸ™ Xâ‚ âŠ— (Î±_ Yâ‚ Xâ‚‚ Yâ‚‚).inv) âŠ— ğŸ™ (Xâ‚ƒ âŠ— Yâ‚ƒ)) â‰«
          (Î±_ (Xâ‚ âŠ— (Yâ‚ âŠ— Xâ‚‚) âŠ— Yâ‚‚) Xâ‚ƒ Yâ‚ƒ).inv â‰«
            ((Î±_ Xâ‚ ((Yâ‚ âŠ— Xâ‚‚) âŠ— Yâ‚‚) Xâ‚ƒ).hom âŠ— ğŸ™ Yâ‚ƒ) â‰«
              ((ğŸ™ Xâ‚ âŠ— (Î±_ (Yâ‚ âŠ— Xâ‚‚) Yâ‚‚ Xâ‚ƒ).hom) âŠ— ğŸ™ Yâ‚ƒ) â‰«
                (Î±_ Xâ‚ ((Yâ‚ âŠ— Xâ‚‚) âŠ— Yâ‚‚ âŠ— Xâ‚ƒ) Yâ‚ƒ).hom â‰«
                  (ğŸ™ Xâ‚ âŠ— (Î±_ (Yâ‚ âŠ— Xâ‚‚) (Yâ‚‚ âŠ— Xâ‚ƒ) Yâ‚ƒ).hom) â‰«
                    (ğŸ™ Xâ‚ âŠ— (Î±_ Yâ‚ Xâ‚‚ ((Yâ‚‚ âŠ— Xâ‚ƒ) âŠ— Yâ‚ƒ)).hom) â‰«
                      (Î±_ Xâ‚ Yâ‚ (Xâ‚‚ âŠ— (Yâ‚‚ âŠ— Xâ‚ƒ) âŠ— Yâ‚ƒ)).inv â‰«
                        (ğŸ™ (Xâ‚ âŠ— Yâ‚) âŠ— ğŸ™ Xâ‚‚ âŠ— (Î±_ Yâ‚‚ Xâ‚ƒ Yâ‚ƒ).hom) â‰«
                          (ğŸ™ (Xâ‚ âŠ— Yâ‚) âŠ— (Î±_ Xâ‚‚ Yâ‚‚ (Xâ‚ƒ âŠ— Yâ‚ƒ)).inv) :=
    by pure_coherence
  rw [this]; clear this
  slice_lhs 2 4 => rw [â† tensor_comp, â† tensor_comp, tensor_Î¼_defâ‚, tensor_comp, tensor_comp]
  slice_lhs 4 5 => rw [â† tensor_id, associator_inv_naturality]
  slice_lhs 5 6 => rw [â† tensor_comp, associator_naturality, tensor_comp]
  slice_lhs 6 7 =>
    rw [â† tensor_comp, â† tensor_comp, associator_naturality, tensor_comp, tensor_comp]
  have :
    ((Î±_ Xâ‚ Xâ‚‚ (Yâ‚ âŠ— Yâ‚‚)).hom âŠ— ğŸ™ (Xâ‚ƒ âŠ— Yâ‚ƒ)) â‰«
        ((ğŸ™ Xâ‚ âŠ— (Î±_ Xâ‚‚ Yâ‚ Yâ‚‚).inv) âŠ— ğŸ™ (Xâ‚ƒ âŠ— Yâ‚ƒ)) â‰«
          (Î±_ (Xâ‚ âŠ— (Xâ‚‚ âŠ— Yâ‚) âŠ— Yâ‚‚) Xâ‚ƒ Yâ‚ƒ).inv â‰«
            ((Î±_ Xâ‚ ((Xâ‚‚ âŠ— Yâ‚) âŠ— Yâ‚‚) Xâ‚ƒ).hom âŠ— ğŸ™ Yâ‚ƒ) â‰« ((ğŸ™ Xâ‚ âŠ— (Î±_ (Xâ‚‚ âŠ— Yâ‚) Yâ‚‚ Xâ‚ƒ).hom) âŠ— ğŸ™ Yâ‚ƒ) =
      (Î±_ (Xâ‚ âŠ— Xâ‚‚) (Yâ‚ âŠ— Yâ‚‚) (Xâ‚ƒ âŠ— Yâ‚ƒ)).hom â‰«
        (ğŸ™ (Xâ‚ âŠ— Xâ‚‚) âŠ— (Î±_ (Yâ‚ âŠ— Yâ‚‚) Xâ‚ƒ Yâ‚ƒ).inv) â‰«
          (Î±_ Xâ‚ Xâ‚‚ (((Yâ‚ âŠ— Yâ‚‚) âŠ— Xâ‚ƒ) âŠ— Yâ‚ƒ)).hom â‰«
            (ğŸ™ Xâ‚ âŠ— (Î±_ Xâ‚‚ ((Yâ‚ âŠ— Yâ‚‚) âŠ— Xâ‚ƒ) Yâ‚ƒ).inv) â‰«
              (Î±_ Xâ‚ (Xâ‚‚ âŠ— (Yâ‚ âŠ— Yâ‚‚) âŠ— Xâ‚ƒ) Yâ‚ƒ).inv â‰«
                ((ğŸ™ Xâ‚ âŠ— ğŸ™ Xâ‚‚ âŠ— (Î±_ Yâ‚ Yâ‚‚ Xâ‚ƒ).hom) âŠ— ğŸ™ Yâ‚ƒ) â‰«
                  ((ğŸ™ Xâ‚ âŠ— (Î±_ Xâ‚‚ Yâ‚ (Yâ‚‚ âŠ— Xâ‚ƒ)).inv) âŠ— ğŸ™ Yâ‚ƒ) :=
    by pure_coherence
  slice_lhs 2 6 => rw [this]
  clear this
  slice_lhs 1 3 => rw [tensor_Î¼_defâ‚]
  slice_lhs 3 4 => rw [â† tensor_id, associator_naturality]
  slice_lhs 4 5 => rw [â† tensor_comp, associator_inv_naturality, tensor_comp]
  slice_lhs 5 6 => rw [associator_inv_naturality]
  slice_lhs 6 9 =>
    rw [â† tensor_comp, â† tensor_comp, â† tensor_comp, â† tensor_comp, â† tensor_comp, â† tensor_comp,
      tensor_id, associator_monoidal_aux, â† id_comp (ğŸ™ Xâ‚ â‰« ğŸ™ Xâ‚ â‰« ğŸ™ Xâ‚ â‰« ğŸ™ Xâ‚), â†
      id_comp (ğŸ™ Xâ‚ â‰« ğŸ™ Xâ‚ â‰« ğŸ™ Xâ‚ â‰« ğŸ™ Xâ‚ â‰« ğŸ™ Xâ‚), â† id_comp (ğŸ™ Yâ‚ƒ â‰« ğŸ™ Yâ‚ƒ â‰« ğŸ™ Yâ‚ƒ â‰« ğŸ™ Yâ‚ƒ), â†
      id_comp (ğŸ™ Yâ‚ƒ â‰« ğŸ™ Yâ‚ƒ â‰« ğŸ™ Yâ‚ƒ â‰« ğŸ™ Yâ‚ƒ â‰« ğŸ™ Yâ‚ƒ), tensor_comp, tensor_comp, tensor_comp,
      tensor_comp, tensor_comp, tensor_comp, tensor_comp, tensor_comp, tensor_comp, tensor_comp]
  slice_lhs 11 12 => rw [associator_naturality]
  slice_lhs 12 13 => rw [â† tensor_comp, associator_naturality, tensor_comp]
  slice_lhs 13 14 => rw [â† tensor_comp, â† tensor_id, associator_naturality, tensor_comp]
  slice_lhs 14 15 => rw [associator_inv_naturality]
  slice_lhs 15 17 =>
    rw [tensor_id, â† tensor_comp, â† tensor_comp, â† tensor_Î¼_defâ‚‚, tensor_comp, tensor_comp]
  have :
    ((ğŸ™ Xâ‚ âŠ— (Î±_ Yâ‚ Xâ‚‚ Xâ‚ƒ).inv âŠ— ğŸ™ Yâ‚‚) âŠ— ğŸ™ Yâ‚ƒ) â‰«
        ((ğŸ™ Xâ‚ âŠ— (Î±_ (Yâ‚ âŠ— Xâ‚‚) Xâ‚ƒ Yâ‚‚).hom) âŠ— ğŸ™ Yâ‚ƒ) â‰«
          (Î±_ Xâ‚ ((Yâ‚ âŠ— Xâ‚‚) âŠ— Xâ‚ƒ âŠ— Yâ‚‚) Yâ‚ƒ).hom â‰«
            (ğŸ™ Xâ‚ âŠ— (Î±_ (Yâ‚ âŠ— Xâ‚‚) (Xâ‚ƒ âŠ— Yâ‚‚) Yâ‚ƒ).hom) â‰«
              (ğŸ™ Xâ‚ âŠ— (Î±_ Yâ‚ Xâ‚‚ ((Xâ‚ƒ âŠ— Yâ‚‚) âŠ— Yâ‚ƒ)).hom) â‰«
                (Î±_ Xâ‚ Yâ‚ (Xâ‚‚ âŠ— (Xâ‚ƒ âŠ— Yâ‚‚) âŠ— Yâ‚ƒ)).inv â‰«
                  (ğŸ™ (Xâ‚ âŠ— Yâ‚) âŠ— ğŸ™ Xâ‚‚ âŠ— (Î±_ Xâ‚ƒ Yâ‚‚ Yâ‚ƒ).hom) â‰«
                    (ğŸ™ (Xâ‚ âŠ— Yâ‚) âŠ— (Î±_ Xâ‚‚ Xâ‚ƒ (Yâ‚‚ âŠ— Yâ‚ƒ)).inv) =
      (Î±_ Xâ‚ ((Yâ‚ âŠ— Xâ‚‚ âŠ— Xâ‚ƒ) âŠ— Yâ‚‚) Yâ‚ƒ).hom â‰«
        (ğŸ™ Xâ‚ âŠ— (Î±_ (Yâ‚ âŠ— Xâ‚‚ âŠ— Xâ‚ƒ) Yâ‚‚ Yâ‚ƒ).hom) â‰«
          (ğŸ™ Xâ‚ âŠ— (Î±_ Yâ‚ (Xâ‚‚ âŠ— Xâ‚ƒ) (Yâ‚‚ âŠ— Yâ‚ƒ)).hom) â‰« (Î±_ Xâ‚ Yâ‚ ((Xâ‚‚ âŠ— Xâ‚ƒ) âŠ— Yâ‚‚ âŠ— Yâ‚ƒ)).inv :=
    by pure_coherence
  slice_lhs 9 16 => rw [this]
  clear this
  slice_lhs 8 9 => rw [associator_naturality]
  slice_lhs 9 10 => rw [â† tensor_comp, associator_naturality, tensor_comp]
  slice_lhs 10 12 => rw [tensor_id, â† tensor_Î¼_defâ‚‚]
  dsimp
  coherence
#align category_theory.associator_monoidal CategoryTheory.associator_monoidal

end Tensor

end CategoryTheory
