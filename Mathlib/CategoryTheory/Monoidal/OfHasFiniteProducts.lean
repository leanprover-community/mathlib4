/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Simon Hudon
-/
import Mathlib.CategoryTheory.Monoidal.Braided.Basic
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Terminal

/-!
# The natural monoidal structure on any category with finite (co)products.

A category with a monoidal structure provided in this way
is sometimes called a (co)cartesian category,
although this is also sometimes used to mean a finitely complete category.
(See <https://ncatlab.org/nlab/show/cartesian+category>.)

As this works with either products or coproducts,
and sometimes we want to think of a different monoidal structure entirely,
we don't set up either construct as an instance.

## TODO

Replace `monoidalOfHasFiniteProducts` and `symmetricOfHasFiniteProducts`
with `CartesianMonoidalCategory.ofHasFiniteProducts`.
-/


universe v u

noncomputable section

namespace CategoryTheory

variable (C : Type u) [Category.{v} C] {X Y : C}

open CategoryTheory.Limits

section

/-- A category with a terminal object and binary products has a natural monoidal structure. -/
def monoidalOfHasFiniteProducts [HasTerminal C] [HasBinaryProducts C] : MonoidalCategory C :=
  letI : MonoidalCategoryStruct C := {
    tensorObj := fun X Y ‚Ü¶ X ‚®Ø Y
    whiskerLeft := fun _ _ _ g ‚Ü¶ Limits.prod.map (ùüô _) g
    whiskerRight := fun {_ _} f _ ‚Ü¶ Limits.prod.map f (ùüô _)
    tensorHom := fun f g ‚Ü¶ Limits.prod.map f g
    tensorUnit := ‚ä§_ C
    associator := prod.associator
    leftUnitor := fun P ‚Ü¶ Limits.prod.leftUnitor P
    rightUnitor := fun P ‚Ü¶ Limits.prod.rightUnitor P
  }
  .ofTensorHom
    (pentagon := prod.pentagon)
    (triangle := prod.triangle)
    (associator_naturality := @prod.associator_naturality _ _ _)

end

namespace monoidalOfHasFiniteProducts

variable [HasTerminal C] [HasBinaryProducts C]

attribute [local instance] monoidalOfHasFiniteProducts

open scoped MonoidalCategory

@[ext] theorem unit_ext {X : C} (f g : X ‚ü∂ ùüô_ C) : f = g := terminal.hom_ext f g

@[ext] theorem tensor_ext {X Y Z : C} (f g : X ‚ü∂ Y ‚äó Z)
    (w‚ÇÅ : f ‚â´ prod.fst = g ‚â´ prod.fst) (w‚ÇÇ : f ‚â´ prod.snd = g ‚â´ prod.snd) : f = g :=
  Limits.prod.hom_ext w‚ÇÅ w‚ÇÇ

@[simp] theorem tensorUnit : ùüô_ C = ‚ä§_ C := rfl

@[simp]
theorem tensorObj (X Y : C) : X ‚äó Y = (X ‚®Ø Y) :=
  rfl

@[simp]
theorem tensorHom {W X Y Z : C} (f : W ‚ü∂ X) (g : Y ‚ü∂ Z) : f ‚äó‚Çò g = Limits.prod.map f g :=
  rfl

@[simp]
theorem whiskerLeft (X : C) {Y Z : C} (f : Y ‚ü∂ Z) : X ‚óÅ f = Limits.prod.map (ùüô X) f :=
  rfl

@[simp]
theorem whiskerRight {X Y : C} (f : X ‚ü∂ Y) (Z : C) : f ‚ñ∑ Z = Limits.prod.map f (ùüô Z) :=
  rfl

@[simp]
theorem leftUnitor_hom (X : C) : (Œª_ X).hom = Limits.prod.snd :=
  rfl

@[simp]
theorem leftUnitor_inv (X : C) : (Œª_ X).inv = prod.lift (terminal.from X) (ùüô _) :=
  rfl

@[simp]
theorem rightUnitor_hom (X : C) : (œÅ_ X).hom = Limits.prod.fst :=
  rfl

@[simp]
theorem rightUnitor_inv (X : C) : (œÅ_ X).inv = prod.lift (ùüô _) (terminal.from X) :=
  rfl

-- We don't mark this as a simp lemma, even though in many particular
-- categories the right hand side will simplify significantly further.
-- For now, we'll plan to create specialised simp lemmas in each particular category.
theorem associator_hom (X Y Z : C) :
    (Œ±_ X Y Z).hom =
      prod.lift (Limits.prod.fst ‚â´ Limits.prod.fst)
        (prod.lift (Limits.prod.fst ‚â´ Limits.prod.snd) Limits.prod.snd) :=
  rfl

theorem associator_inv (X Y Z : C) :
    (Œ±_ X Y Z).inv =
      prod.lift (prod.lift prod.fst (prod.snd ‚â´ prod.fst)) (prod.snd ‚â´ prod.snd) :=
  rfl

@[reassoc] theorem associator_hom_fst (X Y Z : C) :
    (Œ±_ X Y Z).hom ‚â´ prod.fst = prod.fst ‚â´ prod.fst := by simp [associator_hom]

@[reassoc] theorem associator_hom_snd_fst (X Y Z : C) :
    (Œ±_ X Y Z).hom ‚â´ prod.snd ‚â´ prod.fst = prod.fst ‚â´ prod.snd := by simp [associator_hom]

@[reassoc] theorem associator_hom_snd_snd (X Y Z : C) :
    (Œ±_ X Y Z).hom ‚â´ prod.snd ‚â´ prod.snd = prod.snd := by simp [associator_hom]

@[reassoc] theorem associator_inv_fst_fst (X Y Z : C) :
    (Œ±_ X Y Z).inv ‚â´ prod.fst ‚â´ prod.fst = prod.fst := by simp [associator_inv]

@[reassoc] theorem associator_inv_fst_snd (X Y Z : C) :
    (Œ±_ X Y Z).inv ‚â´ prod.fst ‚â´ prod.snd = prod.snd ‚â´ prod.fst := by simp [associator_inv]

@[reassoc] theorem associator_inv_snd (X Y Z : C) :
    (Œ±_ X Y Z).inv ‚â´ prod.snd = prod.snd ‚â´ prod.snd := by simp [associator_inv]

end monoidalOfHasFiniteProducts

section

attribute [local instance] monoidalOfHasFiniteProducts

open MonoidalCategory

/-- The monoidal structure coming from finite products is symmetric.
-/
@[simps]
def symmetricOfHasFiniteProducts [HasTerminal C] [HasBinaryProducts C] : SymmetricCategory C where
  braiding X Y := Limits.prod.braiding X Y
  braiding_naturality_left f X := by simp
  braiding_naturality_right X _ _ f := by simp
  hexagon_forward X Y Z := by dsimp [monoidalOfHasFiniteProducts.associator_hom]; simp
  hexagon_reverse X Y Z := by dsimp [monoidalOfHasFiniteProducts.associator_inv]; simp
  symmetry X Y := by simp

end

section

/-- A category with an initial object and binary coproducts has a natural monoidal structure. -/
def monoidalOfHasFiniteCoproducts [HasInitial C] [HasBinaryCoproducts C] : MonoidalCategory C :=
  letI : MonoidalCategoryStruct C := {
    tensorObj := fun X Y ‚Ü¶ X ‚®ø Y
    whiskerLeft := fun _ _ _ g ‚Ü¶ Limits.coprod.map (ùüô _) g
    whiskerRight := fun {_ _} f _ ‚Ü¶ Limits.coprod.map f (ùüô _)
    tensorHom := fun f g ‚Ü¶ Limits.coprod.map f g
    tensorUnit := ‚ä•_ C
    associator := coprod.associator
    leftUnitor := fun P ‚Ü¶ coprod.leftUnitor P
    rightUnitor := fun P ‚Ü¶ coprod.rightUnitor P
  }
  .ofTensorHom
    (pentagon := coprod.pentagon)
    (triangle := coprod.triangle)
    (associator_naturality := @coprod.associator_naturality _ _ _)

end

namespace monoidalOfHasFiniteCoproducts

variable [HasInitial C] [HasBinaryCoproducts C]

attribute [local instance] monoidalOfHasFiniteCoproducts

open scoped MonoidalCategory

@[simp]
theorem tensorObj (X Y : C) : X ‚äó Y = (X ‚®ø Y) :=
  rfl

@[simp]
theorem tensorHom {W X Y Z : C} (f : W ‚ü∂ X) (g : Y ‚ü∂ Z) : f ‚äó‚Çò g = Limits.coprod.map f g :=
  rfl

@[simp]
theorem whiskerLeft (X : C) {Y Z : C} (f : Y ‚ü∂ Z) : X ‚óÅ f = Limits.coprod.map (ùüô X) f :=
  rfl

@[simp]
theorem whiskerRight {X Y : C} (f : X ‚ü∂ Y) (Z : C) : f ‚ñ∑ Z = Limits.coprod.map f (ùüô Z) :=
  rfl

@[simp]
theorem leftUnitor_hom (X : C) : (Œª_ X).hom = coprod.desc (initial.to X) (ùüô _) :=
  rfl

@[simp]
theorem rightUnitor_hom (X : C) : (œÅ_ X).hom = coprod.desc (ùüô _) (initial.to X) :=
  rfl

@[simp]
theorem leftUnitor_inv (X : C) : (Œª_ X).inv = Limits.coprod.inr :=
  rfl

@[simp]
theorem rightUnitor_inv (X : C) : (œÅ_ X).inv = Limits.coprod.inl :=
  rfl

-- We don't mark this as a simp lemma, even though in many particular
-- categories the right hand side will simplify significantly further.
-- For now, we'll plan to create specialised simp lemmas in each particular category.
theorem associator_hom (X Y Z : C) :
    (Œ±_ X Y Z).hom =
      coprod.desc (coprod.desc coprod.inl (coprod.inl ‚â´ coprod.inr)) (coprod.inr ‚â´ coprod.inr) :=
  rfl

theorem associator_inv (X Y Z : C) :
    (Œ±_ X Y Z).inv =
      coprod.desc (coprod.inl ‚â´ coprod.inl) (coprod.desc (coprod.inr ‚â´ coprod.inl) coprod.inr) :=
  rfl

end monoidalOfHasFiniteCoproducts

section

attribute [local instance] monoidalOfHasFiniteCoproducts

open MonoidalCategory

/-- The monoidal structure coming from finite coproducts is symmetric.
-/
@[simps]
def symmetricOfHasFiniteCoproducts [HasInitial C] [HasBinaryCoproducts C] :
    SymmetricCategory C where
  braiding := Limits.coprod.braiding
  braiding_naturality_left f g := by simp
  braiding_naturality_right f g := by simp
  hexagon_forward X Y Z := by dsimp [monoidalOfHasFiniteCoproducts.associator_hom]; simp
  hexagon_reverse X Y Z := by dsimp [monoidalOfHasFiniteCoproducts.associator_inv]; simp
  symmetry X Y := by simp

end

namespace monoidalOfHasFiniteProducts

attribute [local instance] monoidalOfHasFiniteProducts

variable {C}
variable {D : Type*} [Category D] (F : C ‚•§ D)
  [HasTerminal C] [HasBinaryProducts C]
  [HasTerminal D] [HasBinaryProducts D]

attribute [local simp] associator_hom_fst
instance : F.OplaxMonoidal where
  Œ∑ := terminalComparison F
  Œ¥ X Y := prodComparison F X Y
  Œ¥_natural_left _ _ := by simp [prodComparison_natural]
  Œ¥_natural_right _ _ := by simp [prodComparison_natural]
  oplax_associativity _ _ _ := by
    dsimp
    ext
    ¬∑ dsimp
      simp only [Category.assoc, prod.map_fst, Category.comp_id, prodComparison_fst, ‚Üê
        Functor.map_comp]
      erw [associator_hom_fst, associator_hom_fst]
      simp
    ¬∑ dsimp
      simp only [Category.assoc, prod.map_snd, prodComparison_snd_assoc, prodComparison_fst,
        ‚Üê Functor.map_comp]
      erw [associator_hom_snd_fst, associator_hom_snd_fst]
      simp
    ¬∑ dsimp
      simp only [Category.assoc, prod.map_snd, prodComparison_snd_assoc, prodComparison_snd, ‚Üê
        Functor.map_comp]
      erw [associator_hom_snd_snd, associator_hom_snd_snd]
      simp
  oplax_left_unitality _ := by ext; simp [‚Üê Functor.map_comp]
  oplax_right_unitality _ := by ext; simp [‚Üê Functor.map_comp]

open Functor.OplaxMonoidal

lemma Œ∑_eq : Œ∑ F = terminalComparison F := rfl
lemma Œ¥_eq (X Y : C) : Œ¥ F X Y = prodComparison F X Y := rfl

variable [PreservesLimit (Functor.empty.{0} C) F]
  [PreservesLimitsOfShape (Discrete WalkingPair) F]

instance : IsIso (Œ∑ F) := by dsimp [Œ∑_eq]; infer_instance
instance (X Y : C) : IsIso (Œ¥ F X Y) := by dsimp [Œ¥_eq]; infer_instance

/-- Promote a finite products preserving functor to a monoidal functor between
categories equipped with the monoidal category structure given by finite products. -/
instance : F.Monoidal := .ofOplaxMonoidal F

end monoidalOfHasFiniteProducts

end CategoryTheory
