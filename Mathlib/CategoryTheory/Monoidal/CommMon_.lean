/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Monoidal.Braided.Basic
import Mathlib.CategoryTheory.Monoidal.Mon_

/-!
# The category of commutative monoids in a braided monoidal category.
-/


universe vâ‚ vâ‚‚ uâ‚ uâ‚‚ u

open CategoryTheory MonoidalCategory

variable (C : Type uâ‚) [Category.{vâ‚} C] [MonoidalCategory.{vâ‚} C] [BraidedCategory.{vâ‚} C]

/-- A commutative monoid object internal to a monoidal category.
-/
structure CommMon_ extends Mon_ C where
  mul_comm : (Î²_ _ _).hom â‰« mul = mul := by aesop_cat

attribute [reassoc (attr := simp)] CommMon_.mul_comm

namespace CommMon_

/-- The trivial commutative monoid object. We later show this is initial in `CommMon_ C`.
-/
@[simps!]
def trivial : CommMon_ C :=
  { Mon_.trivial C with mul_comm := by dsimp; rw [braiding_leftUnitor, unitors_equal] }

instance : Inhabited (CommMon_ C) :=
  âŸ¨trivial CâŸ©

variable {C}
variable {M : CommMon_ C}

instance : Category (CommMon_ C) :=
  InducedCategory.category CommMon_.toMon_

@[simp]
theorem id_hom (A : CommMon_ C) : Mon_.Hom.hom (ğŸ™ A) = ğŸ™ A.X :=
  rfl

@[simp]
theorem comp_hom {R S T : CommMon_ C} (f : R âŸ¶ S) (g : S âŸ¶ T) :
    Mon_.Hom.hom (f â‰« g) = f.hom â‰« g.hom :=
  rfl

-- Porting note (#5229): added because `Mon_.Hom.ext` is not triggered automatically
-- for morphisms in `CommMon_ C`
@[ext]
lemma hom_ext {A B : CommMon_ C} (f g : A âŸ¶ B) (h : f.hom = g.hom) : f = g :=
  Mon_.Hom.ext h

-- Porting note (#10688): the following two lemmas `id'` and `comp'`
-- have been added to ease automation;
@[simp]
lemma id' (A : CommMon_ C) : (ğŸ™ A : A.toMon_ âŸ¶ A.toMon_) = ğŸ™ (A.toMon_) := rfl

@[simp]
lemma comp' {Aâ‚ Aâ‚‚ Aâ‚ƒ : CommMon_ C} (f : Aâ‚ âŸ¶ Aâ‚‚) (g : Aâ‚‚ âŸ¶ Aâ‚ƒ) :
    ((f â‰« g : Aâ‚ âŸ¶ Aâ‚ƒ) : Aâ‚.toMon_ âŸ¶ Aâ‚ƒ.toMon_) = @CategoryStruct.comp (Mon_ C) _ _ _ _ f g := rfl

section

variable (C)

/-- The forgetful functor from commutative monoid objects to monoid objects. -/
def forgetâ‚‚Mon_ : CommMon_ C â¥¤ Mon_ C :=
  inducedFunctor CommMon_.toMon_

-- Porting note: no delta derive handler, see https://github.com/leanprover-community/mathlib4/issues/5020
instance : (forgetâ‚‚Mon_ C).Full := InducedCategory.full _
instance : (forgetâ‚‚Mon_ C).Faithful := InducedCategory.faithful _

@[simp]
theorem forgetâ‚‚_Mon_obj_one (A : CommMon_ C) : ((forgetâ‚‚Mon_ C).obj A).one = A.one :=
  rfl

@[simp]
theorem forgetâ‚‚_Mon_obj_mul (A : CommMon_ C) : ((forgetâ‚‚Mon_ C).obj A).mul = A.mul :=
  rfl

@[simp]
theorem forgetâ‚‚_Mon_map_hom {A B : CommMon_ C} (f : A âŸ¶ B) : ((forgetâ‚‚Mon_ C).map f).hom = f.hom :=
  rfl

end

instance uniqueHomFromTrivial (A : CommMon_ C) : Unique (trivial C âŸ¶ A) :=
  Mon_.uniqueHomFromTrivial A.toMon_

open CategoryTheory.Limits

instance : HasInitial (CommMon_ C) :=
  hasInitial_of_unique (trivial C)

end CommMon_

namespace CategoryTheory.LaxBraidedFunctor

variable {C} {D : Type uâ‚‚} [Category.{vâ‚‚} D] [MonoidalCategory.{vâ‚‚} D] [BraidedCategory.{vâ‚‚} D]

/-- A lax braided functor takes commutative monoid objects to commutative monoid objects.

That is, a lax braided functor `F : C â¥¤ D` induces a functor `CommMon_ C â¥¤ CommMon_ D`.
-/
@[simps!]
def mapCommMon (F : LaxBraidedFunctor C D) : CommMon_ C â¥¤ CommMon_ D where
  obj A :=
    { F.toLaxMonoidalFunctor.mapMon.obj A.toMon_ with
      mul_comm := by
        dsimp
        have := F.braided
        slice_lhs 1 2 => rw [â† this]
        slice_lhs 2 3 => rw [â† CategoryTheory.Functor.map_comp, A.mul_comm] }
  map f := F.toLaxMonoidalFunctor.mapMon.map f

variable (C) (D)

-- Porting note (#10688): added @[simps] to ease automation
/-- `mapCommMon` is functorial in the lax braided functor. -/
@[simps]
def mapCommMonFunctor : LaxBraidedFunctor C D â¥¤ CommMon_ C â¥¤ CommMon_ D where
  obj := mapCommMon
  map Î± :=
    { app := fun A => { hom := Î±.app A.X }
      naturality := by intros; ext; simp }

end CategoryTheory.LaxBraidedFunctor

namespace CommMon_

open CategoryTheory.LaxBraidedFunctor

namespace EquivLaxBraidedFunctorPUnit

/-- Implementation of `CommMon_.equivLaxBraidedFunctorPUnit`. -/
@[simps]
def laxBraidedToCommMon : LaxBraidedFunctor (Discrete PUnit.{u + 1}) C â¥¤ CommMon_ C where
  obj F := (F.mapCommMon : CommMon_ _ â¥¤ CommMon_ C).obj (trivial (Discrete PUnit.{u+1}))
  map Î± := ((mapCommMonFunctor (Discrete PUnit.{u+1}) C).map Î±).app _

/-- Implementation of `CommMon_.equivLaxBraidedFunctorPunit`. -/
@[simps]
def commMonToLaxBraided : CommMon_ C â¥¤ LaxBraidedFunctor (Discrete PUnit.{u + 1}) C where
  obj A :=
    { obj := fun _ => A.X
      map := fun _ => ğŸ™ _
      Îµ := A.one
      Î¼ := fun _ _ => A.mul
      map_id := fun _ => rfl
      map_comp := fun _ _ => (Category.id_comp (ğŸ™ A.X)).symm }
  map f :=
    { app := fun _ => f.hom
      naturality := fun _ _ _ => by dsimp; rw [Category.id_comp, Category.comp_id]
      unit := Mon_.Hom.one_hom f
      tensor := fun _ _ => Mon_.Hom.mul_hom f }

/-- Implementation of `CommMon_.equivLaxBraidedFunctorPUnit`. -/
@[simps!]
def unitIso :
    ğŸ­ (LaxBraidedFunctor (Discrete PUnit.{u + 1}) C) â‰…
      laxBraidedToCommMon C â‹™ commMonToLaxBraided C :=
  NatIso.ofComponents
    (fun F =>
      LaxBraidedFunctor.mkIso
        (MonoidalNatIso.ofComponents
          (fun _ => F.toLaxMonoidalFunctor.toFunctor.mapIso (eqToIso (by ext)))
          (by rintro âŸ¨âŸ© âŸ¨âŸ© f; aesop_cat) (by aesop_cat) (by aesop_cat)))

/-- Implementation of `CommMon_.equivLaxBraidedFunctorPUnit`. -/
@[simps!]
def counitIso : commMonToLaxBraided C â‹™ laxBraidedToCommMon C â‰… ğŸ­ (CommMon_ C) :=
  NatIso.ofComponents
    (fun F =>
      { hom := { hom := ğŸ™ _ }
        inv := { hom := ğŸ™ _ } })

end EquivLaxBraidedFunctorPUnit

open EquivLaxBraidedFunctorPUnit

/-- Commutative monoid objects in `C` are "just" braided lax monoidal functors from the trivial
braided monoidal category to `C`.
-/
@[simps]
def equivLaxBraidedFunctorPUnit : LaxBraidedFunctor (Discrete PUnit.{u + 1}) C â‰Œ CommMon_ C where
  functor := laxBraidedToCommMon C
  inverse := commMonToLaxBraided C
  unitIso := unitIso C
  counitIso := counitIso C

end CommMon_
