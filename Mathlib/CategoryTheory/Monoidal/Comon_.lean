/-
Copyright (c) 2024 Lean FRO LLC. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Monoidal.Mon_
import Mathlib.CategoryTheory.Monoidal.Braided.Opposite
import Mathlib.CategoryTheory.Monoidal.Transport
import Mathlib.CategoryTheory.Monoidal.CoherenceLemmas
import Mathlib.CategoryTheory.Limits.Shapes.Terminal

/-!
# The category of comonoids in a monoidal category.

We define comonoids in a monoidal category `C`,
and show that they are equivalently monoid objects in the opposite category.

We construct the monoidal structure on `Comon C`, when `C` is braided.

An oplax monoidal functor takes comonoid objects to comonoid objects.
That is, a oplax monoidal functor `F : C â¥¤ D` induces a functor `Comon C â¥¤ Comon D`.

## TODO
* Comonoid objects in `C` are "just"
  oplax monoidal functors from the trivial monoidal category to `C`.
-/

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚ u

open CategoryTheory MonoidalCategory

namespace CategoryTheory
variable {C : Type uâ‚} [Category.{vâ‚} C] [MonoidalCategory.{vâ‚} C]

/-- A comonoid object internal to a monoidal category.

When the monoidal category is preadditive, this is also sometimes called a "coalgebra object".
-/
class ComonObj (X : C) where
  /-- The counit morphism of a comonoid object. -/
  counit : X âŸ¶ ðŸ™_ C
  /-- The comultiplication morphism of a comonoid object. -/
  comul : X âŸ¶ X âŠ— X
  counit_comul (X) : comul â‰« counit â–· X = (Î»_ X).inv := by cat_disch
  comul_counit (X) : comul â‰« X â— counit = (Ï_ X).inv := by cat_disch
  comul_assoc (X) : comul â‰« X â— comul = comul â‰« (comul â–· X) â‰« (Î±_ X X X).hom := by cat_disch

namespace ComonObj

@[inherit_doc] scoped notation "Î”" => ComonObj.comul
@[inherit_doc] scoped notation "Î”["M"]" => ComonObj.comul (X := M)
@[inherit_doc] scoped notation "Îµ" => ComonObj.counit
@[inherit_doc] scoped notation "Îµ["M"]" => ComonObj.counit (X := M)

attribute [reassoc (attr := simp)] counit_comul comul_counit comul_assoc

/-- The canonical comonoid structure on the monoidal unit.
This is not a global instance to avoid conflicts with other comonoid structures. -/
@[simps]
def instTensorUnit (C : Type uâ‚) [Category.{vâ‚} C] [MonoidalCategory.{vâ‚} C] : ComonObj (ðŸ™_ C) where
  counit := ðŸ™ _
  comul := (Î»_ _).inv
  counit_comul := by simp
  comul_counit := by monoidal_coherence
  comul_assoc := by monoidal_coherence

end ComonObj

open scoped ComonObj

variable {M N O : C} [ComonObj M] [ComonObj N] [ComonObj O]

/-- The property that a morphism between comonoid objects is a comonoid morphism. -/
class IsComonHom (f : M âŸ¶ N) : Prop where
  hom_counit (f) : f â‰« Îµ = Îµ := by cat_disch
  hom_comul (f) : f â‰« Î” = Î” â‰« (f âŠ—â‚˜ f) := by cat_disch

@[deprecated (since := "2025-09-15")] alias IsComon_Hom := IsComonHom

attribute [reassoc (attr := simp)] IsComonHom.hom_counit IsComonHom.hom_comul

instance : IsComonHom (ðŸ™ M) where

instance (f : M âŸ¶ N) (g : N âŸ¶ O) [IsComonHom f] [IsComonHom g] : IsComonHom (f â‰« g) where

instance (f : M â‰… N) [IsComonHom f.hom] : IsComonHom f.inv where
  hom_counit := by simp [Iso.inv_comp_eq]
  hom_comul := by simp [Iso.inv_comp_eq]

instance (f : M âŸ¶ N) [IsComonHom f] [IsIso f] : IsComonHom (inv f) where

variable (C) in
/-- A comonoid object internal to a monoidal category.

When the monoidal category is preadditive, this is also sometimes called a "coalgebra object".
-/
structure Comon where
  /-- The underlying object of a comonoid object. -/
  X : C
  [comon : ComonObj X]

@[deprecated (since := "2025-09-15")] alias Comon_ := Comon

attribute [instance] Comon.comon

namespace Comon

attribute [local instance] ComonObj.instTensorUnit in
variable (C) in
/-- The trivial comonoid object. We later show this is terminal in `Comon C`.
-/
@[simps!]
def trivial : Comon C := mk (ðŸ™_ C)

instance : Inhabited (Comon C) :=
  âŸ¨trivial CâŸ©

end Comon

namespace ComonObj

variable {M : C} [ComonObj M]

@[reassoc (attr := simp)]
theorem counit_comul_hom {Z : C} (f : M âŸ¶ Z) : Î”[M] â‰« (Îµ[M] âŠ—â‚˜ f) = f â‰« (Î»_ Z).inv := by
  rw [leftUnitor_inv_naturality, tensorHom_def, counit_comul_assoc]

@[reassoc (attr := simp)]
theorem comul_counit_hom {Z : C} (f : M âŸ¶ Z) : Î”[M] â‰« (f âŠ—â‚˜ Îµ[M]) = f â‰« (Ï_ Z).inv := by
  rw [rightUnitor_inv_naturality, tensorHom_def', comul_counit_assoc]

@[reassoc]
theorem comul_assoc_flip (X : C) [ComonObj X] :
    Î” â‰« Î” â–· X = Î” â‰« X â— Î” â‰« (Î±_ X X X).inv := by
  simp

end ComonObj

namespace Comon

open MonObj ComonObj

/-- A morphism of comonoid objects. -/
@[ext]
structure Hom (M N : Comon C) where
  /-- The underlying morphism of a morphism of comonoid objects. -/
  hom : M.X âŸ¶ N.X
  [isComonHom_hom : IsComonHom hom]

attribute [instance] Hom.isComonHom_hom

/-- Construct a morphism `M âŸ¶ N` of `Comon C` from a map `f : M âŸ¶ N` and a `IsComonHom f`
instance. -/
abbrev Hom.mk' {M N : Comon C} (f : M.X âŸ¶ N.X)
    (f_counit : f â‰« Îµ[N.X] = Îµ[M.X] := by cat_disch)
    (f_comul : f â‰« Î”[N.X] = Î”[M.X] â‰« (f âŠ—â‚˜ f) := by cat_disch) :
    Hom M N :=
  have : IsComonHom f := âŸ¨f_counit, f_comulâŸ©
  .mk f

/-- The identity morphism on a comonoid object. -/
@[simps]
def id (M : Comon C) : Hom M M where
  hom := ðŸ™ M.X

instance homInhabited (M : Comon C) : Inhabited (Hom M M) :=
  âŸ¨id MâŸ©

/-- Composition of morphisms of monoid objects. -/
@[simps]
def comp {M N O : Comon C} (f : Hom M N) (g : Hom N O) : Hom M O where
  hom := f.hom â‰« g.hom

instance : Category (Comon C) where
  Hom M N := Hom M N
  id := id
  comp f g := comp f g

instance {M N : Comon C} (f : M âŸ¶ N) : IsComonHom f.hom := inferInstanceAs (IsComonHom f.hom)

@[ext] lemma ext {X Y : Comon C} {f g : X âŸ¶ Y} (w : f.hom = g.hom) : f = g := Hom.ext w

@[simp] theorem id_hom' (M : Comon C) : (ðŸ™ M : Hom M M).hom = ðŸ™ M.X := rfl

@[simp]
theorem comp_hom' {M N K : Comon C} (f : M âŸ¶ N) (g : N âŸ¶ K) : (f â‰« g).hom = f.hom â‰« g.hom :=
  rfl

section

variable (C)

/-- The forgetful functor from comonoid objects to the ambient category. -/
@[simps]
def forget : Comon C â¥¤ C where
  obj A := A.X
  map f := f.hom

end

instance forget_faithful : (@forget C _ _).Faithful where

instance {A B : Comon C} (f : A âŸ¶ B) [e : IsIso ((forget C).map f)] : IsIso f.hom := e

/-- The forgetful functor from comonoid objects to the ambient category reflects isomorphisms. -/
instance : (forget C).ReflectsIsomorphisms where
  reflects f e :=
    âŸ¨âŸ¨{ hom := inv f.hom }, by cat_dischâŸ©âŸ©

/-- Construct an isomorphism of comonoids by giving an isomorphism between the underlying objects
and checking compatibility with counit and comultiplication only in the forward direction.
-/
@[simps]
def mkIso' {M N : Comon C} (f : M.X â‰… N.X) [IsComonHom f.hom] : M â‰… N where
  hom := Hom.mk f.hom
  inv := Hom.mk f.inv

/-- Construct an isomorphism of comonoids by giving an isomorphism between the underlying objects
and checking compatibility with counit and comultiplication only in the forward direction.
-/
@[simps]
def mkIso {M N : Comon C} (f : M.X â‰… N.X) (f_counit : f.hom â‰« Îµ[N.X] = Îµ[M.X] := by cat_disch)
    (f_comul : f.hom â‰« Î”[N.X] = Î”[M.X] â‰« (f.hom âŠ—â‚˜ f.hom) := by cat_disch) : M â‰… N :=
  have : IsComonHom f.hom := âŸ¨f_counit, f_comulâŸ©
  âŸ¨âŸ¨f.homâŸ©, âŸ¨f.invâŸ©, by cat_disch, by cat_dischâŸ©

@[simps]
instance uniqueHomToTrivial (A : Comon C) : Unique (A âŸ¶ trivial C) where
  default.hom := Îµ[A.X]
  default.isComonHom_hom.hom_comul := by simp [unitors_inv_equal]
  uniq f := by
    ext
    rw [â† Category.comp_id f.hom]
    dsimp only [trivial_X]
    rw [â† trivial_comon_counit, IsComonHom.hom_counit]

open CategoryTheory.Limits

instance : HasTerminal (Comon C) :=
  hasTerminal_of_unique (trivial C)

open Opposite

/-- Auxiliary definition for `ComonToMonOpOpObj`. -/
abbrev ComonToMonOpOpObjMon (A : Comon C) : MonObj (op A.X) where
  one := Îµ[A.X].op
  mul := Î”[A.X].op
  one_mul := by
    rw [â† op_whiskerRight, â† op_comp, counit_comul]
    rfl
  mul_one := by
    rw [â† op_whiskerLeft, â† op_comp, comul_counit]
    rfl
  mul_assoc := by
    rw [â† op_inv_associator, â† op_whiskerRight, â† op_comp, â† op_whiskerLeft, â† op_comp,
      comul_assoc_flip, op_comp, op_comp_assoc]
    rfl

@[deprecated (since := "2025-09-15")] alias Comon_ToMon_OpOpObjMon := ComonToMonOpOpObjMon

/--
Turn a comonoid object into a monoid object in the opposite category.
-/
@[simps] def ComonToMonOpOpObj (A : Comon C) : Mon Cáµ’áµ– where
  X := op A.X
  mon := ComonToMonOpOpObjMon A

@[deprecated (since := "2025-09-15")] alias Comon_ToMon_OpOpObj := ComonToMonOpOpObj

variable (C) in
/--
The contravariant functor turning comonoid objects into monoid objects in the opposite category.
-/
@[simps] def ComonToMonOpOp : Comon C â¥¤ (Mon Cáµ’áµ–)áµ’áµ– where
  obj A := op (ComonToMonOpOpObj A)
  map := fun f => op <|
    { hom := f.hom.op
      isMonHom_hom.one_hom := by apply Quiver.Hom.unop_inj; simp
      isMonHom_hom.mul_hom := by apply Quiver.Hom.unop_inj; simp }

@[deprecated (since := "2025-09-15")] alias Comon_ToMon_OpOp := ComonToMonOpOp

/-- Auxiliary definition for `MonOpOpToComonObj`. -/
abbrev MonOpOpToComonObjComon (A : Mon Cáµ’áµ–) : ComonObj (unop A.X) where
  counit := Î·[A.X].unop
  comul := Î¼[A.X].unop
  counit_comul := by rw [â† unop_whiskerRight, â† unop_comp, MonObj.one_mul]; rfl
  comul_counit := by rw [â† unop_whiskerLeft, â† unop_comp, MonObj.mul_one]; rfl
  comul_assoc := by
    rw [â† unop_whiskerRight, â† unop_whiskerLeft, â† unop_comp_assoc, â† unop_comp,
      MonObj.mul_assoc_flip]
    rfl

@[deprecated (since := "2025-09-15")] alias Mon_OpOpToComonObjComon := MonOpOpToComonObjComon

/--
Turn a monoid object in the opposite category into a comonoid object.
-/
@[simps] def MonOpOpToComonObj (A : Mon Cáµ’áµ–) : Comon C where
  X := unop A.X
  comon := MonOpOpToComonObjComon A

@[deprecated (since := "2025-09-15")] alias Mon_OpOpToComonObj := MonOpOpToComonObj

variable (C)

/--
The contravariant functor turning monoid objects in the opposite category into comonoid objects.
-/
@[simps]
def MonOpOpToComon : (Mon Cáµ’áµ–)áµ’áµ– â¥¤ Comon C where
  obj A := MonOpOpToComonObj (unop A)
  map := fun f =>
    { hom := f.unop.hom.unop
      isComonHom_hom.hom_counit := by apply Quiver.Hom.op_inj; simp
      isComonHom_hom.hom_comul := by apply Quiver.Hom.op_inj; simp [op_tensorHom] }

@[deprecated (since := "2025-09-15")] alias Mon_OpOpToComon_ := MonOpOpToComon

/--
Comonoid objects are contravariantly equivalent to monoid objects in the opposite category.
-/
@[simps]
def Comon_EquivMon_OpOp : Comon C â‰Œ (Mon Cáµ’áµ–)áµ’áµ– where
  functor := ComonToMonOpOp C
  inverse := MonOpOpToComon C
  unitIso := NatIso.ofComponents fun _ => .refl _
  counitIso := NatIso.ofComponents fun _ => .refl _

/--
Comonoid objects in a braided category form a monoidal category.

This definition is via transporting back and forth to monoids in the opposite category.
-/
@[simps!]
instance monoidal [BraidedCategory C] : MonoidalCategory (Comon C) :=
  Monoidal.transport (Comon_EquivMon_OpOp C).symm

variable {C} [BraidedCategory C]

theorem tensorObj_X (A B : Comon C) : (A âŠ— B).X = A.X âŠ— B.X := rfl

instance (A B : C) [ComonObj A] [ComonObj B] : ComonObj (A âŠ— B) :=
  inferInstanceAs <| ComonObj (Comon.mk A âŠ— Comon.mk B).X

@[simp]
theorem tensorObj_counit (A B : C) [ComonObj A] [ComonObj B] :
    Îµ[A âŠ— B] = (Îµ[A] âŠ—â‚˜ Îµ[B]) â‰« (Î»_ _).hom :=
  rfl

/--
Preliminary statement of the comultiplication for a tensor product of comonoids.
This version is the definitional equality provided by transport, and not quite as good as
the version provided in `tensorObj_comul` below.
-/
theorem tensorObj_comul' (A B : C) [ComonObj A] [ComonObj B] :
    Î”[A âŠ— B] =
      (Î”[A] âŠ—â‚˜ Î”[B]) â‰« (tensorÎ¼ (op A) (op B) (op A) (op B)).unop := by
  rfl

/--
The comultiplication on the tensor product of two comonoids is
the tensor product of the comultiplications followed by the tensor strength
(to shuffle the factors back into order).
-/
@[simp]
theorem tensorObj_comul (A B : C) [ComonObj A] [ComonObj B] :
    Î”[A âŠ— B] = (Î”[A] âŠ—â‚˜ Î”[B]) â‰« tensorÎ¼ A A B B := by
  rw [tensorObj_comul']
  congr
  simp only [tensorÎ¼, unop_tensorObj, unop_op]
  apply Quiver.Hom.unop_inj
  dsimp [op_tensorObj, op_associator]
  rw [Category.assoc, Category.assoc, Category.assoc]

/-- The forgetful functor from `Comon C` to `C` is monoidal when `C` is monoidal. -/
instance : (forget C).Monoidal :=
  Functor.CoreMonoidal.toMonoidal
    { ÎµIso := Iso.refl _
      Î¼Iso := fun _ _ â†¦ Iso.refl _ }

open Functor.LaxMonoidal Functor.OplaxMonoidal

@[simp] theorem forget_Îµ : Â«ÎµÂ» (forget C) = ðŸ™ (ðŸ™_ C) := rfl
@[simp] theorem forget_Î· : Â«Î·Â» (forget C) = ðŸ™ (ðŸ™_ C) := rfl
@[simp] theorem forget_Î¼ (X Y : Comon C) : Â«Î¼Â» (forget C) X Y = ðŸ™ (X.X âŠ— Y.X) := rfl
@[simp] theorem forget_Î´ (X Y : Comon C) : Î´ (forget C) X Y = ðŸ™ (X.X âŠ— Y.X) := rfl

end Comon

namespace Functor

variable {D : Type uâ‚‚} [Category.{vâ‚‚} D] [MonoidalCategory.{vâ‚‚} D]

open OplaxMonoidal ComonObj IsComonHom

/-- The image of a comonoid object under a oplax monoidal functor is a comonoid object. -/
abbrev obj.instComonObj (A : C) [ComonObj A] (F : C â¥¤ D) [F.OplaxMonoidal] :
    ComonObj (F.obj A) where
  counit := F.map Îµ[A] â‰« Î· F
  comul := F.map Î”[A] â‰« Î´ F _ _
  counit_comul := by
    simp_rw [comp_whiskerRight, Category.assoc, Î´_natural_left_assoc, left_unitality,
      â† F.map_comp_assoc, counit_comul]
  comul_counit := by
    simp_rw [MonoidalCategory.whiskerLeft_comp, Category.assoc, Î´_natural_right_assoc,
      right_unitality, â† F.map_comp_assoc, comul_counit]
  comul_assoc := by
    simp_rw [comp_whiskerRight, Category.assoc, Î´_natural_left_assoc,
      MonoidalCategory.whiskerLeft_comp, Î´_natural_right_assoc,
      â† F.map_comp_assoc, comul_assoc, F.map_comp, Category.assoc, associativity]

attribute [local instance] obj.instComonObj

@[reassoc, simp] lemma obj.Îµ_def (F : C â¥¤ D) [F.OplaxMonoidal] (X : C) [ComonObj X] :
    Îµ[F.obj X] = F.map Îµ â‰« Î· F :=
  rfl

@[reassoc, simp] lemma obj.Î”_def (F : C â¥¤ D) [F.OplaxMonoidal] (X : C) [ComonObj X] :
    Î”[F.obj X] = F.map Î” â‰« Î´ F _ _ :=
  rfl

instance map.instIsComon_Hom
    (F : C â¥¤ D) [F.OplaxMonoidal]
    {X Y : C} [ComonObj X] [ComonObj Y] (f : X âŸ¶ Y) [IsComonHom f] :
    IsComonHom (F.map f) where
  hom_counit := by dsimp; rw [â† F.map_comp_assoc, hom_counit]
  hom_comul := by
    dsimp
    rw [Category.assoc, Î´_natural, â† F.map_comp_assoc, â† F.map_comp_assoc, hom_comul]

/-- A oplax monoidal functor takes comonoid objects to comonoid objects.

That is, a oplax monoidal functor `F : C â¥¤ D` induces a functor `Comon C â¥¤ Comon D`.
-/
@[simps]
def mapComon (F : C â¥¤ D) [F.OplaxMonoidal] : Comon C â¥¤ Comon D where
  obj A :=
    { X := F.obj A.X }
  map f :=
    { hom := F.map f.hom }
  map_id A := by ext; simp
  map_comp f g := by ext; simp

-- TODO We haven't yet set up the category structure on `OplaxMonoidalFunctor C D`
-- and so can't state `mapComonFunctor : OplaxMonoidalFunctor C D â¥¤ Comon C â¥¤ Comon D`.

end Functor

variable [BraidedCategory.{vâ‚} C]

/-- Predicate for a comonoid object to be commutative. -/
class IsCommComonObj (X : C) [ComonObj X] where
  comul_comm (X) : Î” â‰« (Î²_ X X).hom = Î” := by cat_disch

open scoped ComonObj

attribute [reassoc (attr := simp)] IsCommComonObj.comul_comm

end CategoryTheory
