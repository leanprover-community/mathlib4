/-
Copyright (c) 2023 Lean FRO LLC. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Monoidal.Mon_
import Mathlib.CategoryTheory.Monoidal.Braided.Opposite
import Mathlib.CategoryTheory.Monoidal.Transport

/-!
# The category of comonoids in a monoidal category.

We define comonoids in a monoidal category `C`,
and show that they are equivalently monoid objects in the opposite category.

We construct the monoidal structure on `Comon_ C`, when `C` is braided.

## TODO
* An oplax monoidal functor takes comonoid objects to comonoid objects.
  That is, a oplax monoidal functor `F : C ‚•§ D` induces a functor `Comon_ C ‚•§ Comon_ D`.
* Comonoid objects in `C` are "just"
  oplax monoidal functors from the trivial monoidal category to `C`.
-/

set_option linter.uppercaseLean3 false

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ u

open CategoryTheory MonoidalCategory

variable (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] [MonoidalCategory.{v‚ÇÅ} C]

/-- A comonoid object internal to a monoidal category.

When the monoidal category is preadditive, this is also sometimes called a "coalgebra object".
-/
structure Comon_ where
  /-- The underlying object of a comonoid object. -/
  X : C
  /-- The counit of a comonoid object. -/
  counit : X ‚ü∂ ùüô_ C
  /-- The comultiplication morphism of a comonoid object. -/
  comul : X ‚ü∂ X ‚äó X
  counit_comul : comul ‚â´ (counit ‚äó ùüô X) = (Œª_ X).inv := by aesop_cat
  comul_counit : comul ‚â´ (ùüô X ‚äó counit) = (œÅ_ X).inv := by aesop_cat
  comul_assoc : comul ‚â´ (comul ‚äó ùüô X) = comul ‚â´ (ùüô X ‚äó comul) ‚â´ (Œ±_ X X X).inv := by aesop_cat

attribute [reassoc] Comon_.counit_comul Comon_.comul_counit

-- We prove a more general `@[simp]` lemma below.
attribute [reassoc (attr := simp)] Comon_.comul_assoc

namespace Comon_

/-- The trivial comonoid object. We later show this is initial in `Comon_ C`.
-/
@[simps]
def trivial : Comon_ C where
  X := ùüô_ C
  counit := ùüô _
  comul := (Œª_ _).inv
  comul_assoc := by coherence
  counit_comul := by coherence
  comul_counit := by coherence

instance : Inhabited (Comon_ C) :=
  ‚ü®trivial C‚ü©

variable {C}
variable {M : Comon_ C}

@[simp]
theorem counit_comul_hom {Z : C} (f : M.X ‚ü∂ Z) : M.comul ‚â´ (M.counit ‚äó f) = f ‚â´ (Œª_ Z).inv := by
  rw [‚Üê tensor_id_comp_id_tensor, ‚Üê Category.assoc, M.counit_comul, leftUnitor_inv_naturality]

@[simp]
theorem mul_one_hom {Z : C} (f : M.X ‚ü∂ Z) : M.comul ‚â´ (f ‚äó M.counit) = f ‚â´ (œÅ_ Z).inv := by
  rw [‚Üê id_tensor_comp_tensor_id, ‚Üê Category.assoc, M.comul_counit, rightUnitor_inv_naturality]

theorem assoc_flip :
    M.comul ‚â´ (ùüô M.X ‚äó M.comul) = M.comul ‚â´ (M.comul ‚äó ùüô M.X) ‚â´ (Œ±_ M.X M.X M.X).hom := by simp

/-- A morphism of comonoid objects. -/
@[ext]
structure Hom (M N : Comon_ C) where
  /-- The underlying morphism of a morphism of comonoid objects. -/
  hom : M.X ‚ü∂ N.X
  hom_counit : hom ‚â´ N.counit = M.counit := by aesop_cat
  hom_comul : hom ‚â´ N.comul = M.comul ‚â´ (hom ‚äó hom) := by aesop_cat

attribute [reassoc (attr := simp)] Hom.hom_counit Hom.hom_comul

/-- The identity morphism on a comonoid object. -/
@[simps]
def id (M : Comon_ C) : Hom M M where
  hom := ùüô M.X

instance homInhabited (M : Comon_ C) : Inhabited (Hom M M) :=
  ‚ü®id M‚ü©

/-- Composition of morphisms of monoid objects. -/
@[simps]
def comp {M N O : Comon_ C} (f : Hom M N) (g : Hom N O) : Hom M O where
  hom := f.hom ‚â´ g.hom

instance : Category (Comon_ C) where
  Hom M N := Hom M N
  id := id
  comp f g := comp f g

@[ext] lemma ext {X Y : Comon_ C} {f g : X ‚ü∂ Y} (w : f.hom = g.hom) : f = g := Hom.ext _ _ w

@[simp] theorem id_hom' (M : Comon_ C) : (ùüô M : Hom M M).hom = ùüô M.X := rfl

@[simp]
theorem comp_hom' {M N K : Comon_ C} (f : M ‚ü∂ N) (g : N ‚ü∂ K) :
    (f ‚â´ g : Hom M K).hom = f.hom ‚â´ g.hom :=
  rfl

section

variable (C)

/-- The forgetful functor from comonoid objects to the ambient category. -/
@[simps]
def forget : Comon_ C ‚•§ C where
  obj A := A.X
  map f := f.hom

end

instance forget_faithful : Faithful (@forget C _ _) where

instance {A B : Comon_ C} (f : A ‚ü∂ B) [e : IsIso ((forget C).map f)] : IsIso f.hom := e

/-- The forgetful functor from comonoid objects to the ambient category reflects isomorphisms. -/
instance : ReflectsIsomorphisms (forget C) where
  reflects f e :=
    ‚ü®‚ü®{ hom := inv f.hom }, by aesop_cat‚ü©‚ü©

/-- Construct an isomorphism of monoids by giving an isomorphism between the underlying objects
and checking compatibility with unit and multiplication only in the forward direction.
-/
def isoOfIso {M N : Comon_ C} (f : M.X ‚âÖ N.X) (f_counit : f.hom ‚â´ N.counit = M.counit)
    (f_comul : f.hom ‚â´ N.comul = M.comul ‚â´ (f.hom ‚äó f.hom)) : M ‚âÖ N where
  hom :=
    { hom := f.hom
      hom_counit := f_counit
      hom_comul := f_comul }
  inv :=
    { hom := f.inv
      hom_counit := by rw [‚Üê f_counit]; simp
      hom_comul := by
        rw [‚Üê cancel_epi f.hom]
        slice_rhs 1 2 => rw [f_comul]
        simp }

instance uniqueHomToTrivial (A : Comon_ C) : Unique (A ‚ü∂ trivial C) where
  default :=
    { hom := A.counit
      hom_counit := by dsimp; simp
      hom_comul := by dsimp; simp [A.comul_counit, unitors_inv_equal] }
  uniq f := by
    ext; simp
    rw [‚Üê Category.comp_id f.hom]
    erw [f.hom_counit]

open CategoryTheory.Limits

instance : HasTerminal (Comon_ C) :=
  hasTerminal_of_unique (trivial C)

open Opposite

variable (C)

/--
Turn a comonoid object into a monoid object in the opposite category.
-/
@[simps] def Comon_to_Mon_op_op_obj' (A : Comon_ C) : Mon_ (C·µí·µñ) where
  X := op A.X
  one := A.counit.op
  mul := A.comul.op
  one_mul := by
    rw [op_leftUnitor]
    dsimp
    rw [‚Üê A.counit_comul, op_comp, op_tensorHom]
    rfl
  mul_one := by
    rw [op_rightUnitor]
    dsimp
    rw [‚Üê A.comul_counit, op_comp, op_tensorHom]
    rfl
  mul_assoc := by
    rw [op_associator]
    dsimp
    rw [‚Üê op_id, op_tensor_op, op_tensor_op, ‚Üê op_comp, ‚Üê op_comp, ‚Üê op_comp]
    rw [Category.assoc, ‚Üê A.comul_assoc]

/--
The contravariant functor turning comonoid objects into monoid objects in the opposite category.
-/
@[simps] def Comon_to_Mon_op_op : Comon_ C ‚•§ (Mon_ (C·µí·µñ))·µí·µñ where
  obj A := op (Comon_to_Mon_op_op_obj' C A)
  map := fun f => op <|
    { hom := f.hom.op
      one_hom := by apply Quiver.Hom.unop_inj; simp
      mul_hom := by apply Quiver.Hom.unop_inj; simp [op_tensorHom] }

/--
Turn a monoid object in the opposite category into a comonoid object.
-/
@[simps] def Mon_op_op_to_Comon_obj' (A : (Mon_ (C·µí·µñ))) : Comon_ C where
  X := unop A.X
  counit := A.one.unop
  comul := A.mul.unop
  counit_comul := by
    rw [‚Üê unop_id, unop_tensor_unop, ‚Üê unop_comp, A.one_mul]
    rfl
  comul_counit := by
    rw [‚Üê unop_id, unop_tensor_unop, ‚Üê unop_comp, A.mul_one]
    rfl
  comul_assoc := by
    rw [‚Üê unop_id, unop_tensor_unop, unop_tensor_unop, ‚Üê unop_comp, A.mul_assoc,
      op_associator]
    simp

/--
The contravariant functor turning monoid objects in the opposite category into comonoid objects.
-/
@[simps]
def Mon_op_op_to_Comon : (Mon_ (C·µí·µñ))·µí·µñ ‚•§ Comon_ C where
  obj A := Mon_op_op_to_Comon_obj' C (unop A)
  map := fun f =>
    { hom := f.unop.hom.unop
      hom_counit := by apply Quiver.Hom.op_inj; simp
      hom_comul := by apply Quiver.Hom.op_inj; simp [op_tensorHom] }

/--
Comonoid objects are contravariantly equivalent to monoid objects in the opposite category.
-/
@[simps]
def Comon_equiv_Mon_op_op : Comon_ C ‚âå (Mon_ (C·µí·µñ))·µí·µñ :=
  { functor := Comon_to_Mon_op_op C
    inverse := Mon_op_op_to_Comon C
    unitIso := NatIso.ofComponents (fun _ => Iso.refl _)
    counitIso := NatIso.ofComponents (fun _ => Iso.refl _) }

/--
Comonoid objects in a braided category form a monoidal category.

This definition is via transporting back and forth to monoids in the opposite category,
-/
instance [BraidedCategory C] : MonoidalCategory (Comon_ C) :=
  Monoidal.transport (Comon_equiv_Mon_op_op C).symm

variable [BraidedCategory C]

@[simp] theorem tensorObj_X (A B : Comon_ C) : (A ‚äó B).X = A.X ‚äó B.X := rfl

@[simp] theorem tensorObj_counit (A B : Comon_ C) :
    (A ‚äó B).counit = (A.counit ‚äó B.counit) ‚â´ (Œª_ _).hom :=
  rfl

/--
Preliminary statement of the comultiplication for a tensor product of comonoids.
This version is the definitional equality provided by transport, and not quite as good as
the version provided in `tensorObj_comul` below.
-/
theorem tensorObj_comul' (A B : Comon_ C) :
    (A ‚äó B).comul =
      (A.comul ‚äó B.comul) ‚â´ (tensor_Œº C·µí·µñ (op A.X, op B.X) (op A.X, op B.X)).unop := by
  rfl

/--
The comultiplication on the tensor product of two comonoids is
the tensor product of the comultiplications followed by the tensor strength
(to shuffle the factors back into order).
-/
@[simp] theorem tensorObj_comul (A B : Comon_ C) :
    (A ‚äó B).comul = (A.comul ‚äó B.comul) ‚â´ tensor_Œº C (A.X, A.X) (B.X, B.X) := by
  rw [tensorObj_comul']
  congr
  simp only [tensor_obj, prodMonoidal_tensorObj, tensor_Œº, braiding_op, unop_op, Iso.op_hom]
  apply Quiver.Hom.unop_inj
  dsimp [op_tensorObj, op_associator]
  rw [‚Üê op_id, ‚Üê op_id, op_tensor_op, op_tensor_op, op_tensor_op, op_tensor_op]
  simp [Category.assoc]
  rfl

end Comon_
