/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Monoidal.Braided
import Mathlib.CategoryTheory.Functor.ReflectsIso

#align_import category_theory.monoidal.center from "leanprover-community/mathlib"@"14b69e9f3c16630440a2cbd46f1ddad0d561dee7"

/-!
# Half braidings and the Drinfeld center of a monoidal category

We define `Center C` to be pairs `‚ü®X, b‚ü©`, where `X : C` and `b` is a half-braiding on `X`.

We show that `Center C` is braided monoidal,
and provide the monoidal functor `Center.forget` from `Center C` back to `C`.

## Implementation notes

Verifying the various axioms directly requires tedious rewriting.
Using the `slice` tactic may make the proofs marginally more readable.

More exciting, however, would be to make possible one of the following options:
1. Integration with homotopy.io / globular to give "picture proofs".
2. The monoidal coherence theorem, so we can ignore associators
   (after which most of these proofs are trivial).
3. Automating these proofs using `rewrite_search` or some relative.

In this file, we take the second approach using the monoidal composition `‚äó‚â´` and the
`coherence` tactic.
-/


open CategoryTheory

open CategoryTheory.MonoidalCategory

universe v v‚ÇÅ v‚ÇÇ v‚ÇÉ u u‚ÇÅ u‚ÇÇ u‚ÇÉ

noncomputable section

namespace CategoryTheory

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] [MonoidalCategory C]

/-- A half-braiding on `X : C` is a family of isomorphisms `X ‚äó U ‚âÖ U ‚äó X`,
monoidally natural in `U : C`.

Thinking of `C` as a 2-category with a single `0`-morphism, these are the same as natural
transformations (in the pseudo- sense) of the identity 2-functor on `C`, which send the unique
`0`-morphism to `X`.
-/
-- @[nolint has_nonempty_instance] -- Porting note: This linter does not exist yet.
structure HalfBraiding (X : C) where
  Œ≤ : ‚àÄ U, X ‚äó U ‚âÖ U ‚äó X
  monoidal : ‚àÄ U U', (Œ≤ (U ‚äó U')).hom =
      (Œ±_ _ _ _).inv ‚â´
        ((Œ≤ U).hom ‚ñ∑ U') ‚â´ (Œ±_ _ _ _).hom ‚â´ (U ‚óÅ (Œ≤ U').hom) ‚â´ (Œ±_ _ _ _).inv := by
    aesop_cat
  naturality : ‚àÄ {U U'} (f : U ‚ü∂ U'), (X ‚óÅ f) ‚â´ (Œ≤ U').hom = (Œ≤ U).hom ‚â´ (f ‚ñ∑ X) := by
    aesop_cat
#align category_theory.half_braiding CategoryTheory.HalfBraiding

attribute [reassoc, simp] HalfBraiding.monoidal -- the reassoc lemma is redundant as a simp lemma

attribute [simp, reassoc] HalfBraiding.naturality

variable (C)

/-- The Drinfeld center of a monoidal category `C` has as objects pairs `‚ü®X, b‚ü©`, where `X : C`
and `b` is a half-braiding on `X`.
-/
-- @[nolint has_nonempty_instance] -- Porting note: This linter does not exist yet.
def Center :=
  Œ£ X : C, HalfBraiding X
#align category_theory.center CategoryTheory.Center

namespace Center

variable {C}

/-- A morphism in the Drinfeld center of `C`. -/
@[ext] -- @[nolint has_nonempty_instance] -- Porting note: This linter does not exist yet.
structure Hom (X Y : Center C) where
  f : X.1 ‚ü∂ Y.1
  comm : ‚àÄ U, (f ‚ñ∑ U) ‚â´ (Y.2.Œ≤ U).hom = (X.2.Œ≤ U).hom ‚â´ (U ‚óÅ f) := by aesop_cat
#align category_theory.center.hom CategoryTheory.Center.Hom

attribute [reassoc (attr := simp)] Hom.comm

instance : Quiver (Center C) where
  Hom := Hom

@[ext]
theorem ext {X Y : Center C} (f g : X ‚ü∂ Y) (w : f.f = g.f) : f = g := by
  cases f; cases g; congr
#align category_theory.center.ext CategoryTheory.Center.ext

instance : Category (Center C) where
  id X := { f := ùüô X.1 }
  comp f g := { f := f.f ‚â´ g.f }

@[simp]
theorem id_f (X : Center C) : Hom.f (ùüô X) = ùüô X.1 :=
  rfl
#align category_theory.center.id_f CategoryTheory.Center.id_f

@[simp]
theorem comp_f {X Y Z : Center C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : (f ‚â´ g).f = f.f ‚â´ g.f :=
  rfl
#align category_theory.center.comp_f CategoryTheory.Center.comp_f

/-- Construct an isomorphism in the Drinfeld center from
a morphism whose underlying morphism is an isomorphism.
-/
@[simps]
def isoMk {X Y : Center C} (f : X ‚ü∂ Y) [IsIso f.f] : X ‚âÖ Y where
  hom := f
  inv := ‚ü®inv f.f,
    fun U => by simp [‚Üê cancel_epi (f.f ‚ñ∑ U), ‚Üê comp_whiskerRight_assoc,
      ‚Üê MonoidalCategory.whiskerLeft_comp] ‚ü©
#align category_theory.center.iso_mk CategoryTheory.Center.isoMk

instance isIso_of_f_isIso {X Y : Center C} (f : X ‚ü∂ Y) [IsIso f.f] : IsIso f := by
  change IsIso (isoMk f).hom
  infer_instance
#align category_theory.center.is_iso_of_f_is_iso CategoryTheory.Center.isIso_of_f_isIso

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
@[simps]
def tensorObj (X Y : Center C) : Center C :=
  ‚ü®X.1 ‚äó Y.1,
    { Œ≤ := fun U =>
        Œ±_ _ _ _ ‚â™‚â´
          (whiskerLeftIso X.1 (Y.2.Œ≤ U)) ‚â™‚â´ (Œ±_ _ _ _).symm ‚â™‚â´
            (whiskerRightIso (X.2.Œ≤ U) Y.1) ‚â™‚â´ Œ±_ _ _ _
      monoidal := fun U U' => by
        dsimp only [Iso.trans_hom, whiskerLeftIso_hom, Iso.symm_hom, whiskerRightIso_hom]
        simp only [HalfBraiding.monoidal]
        -- We'd like to commute `X.1 ‚óÅ U ‚óÅ (HalfBraiding.Œ≤ Y.2 U').hom`
        -- and `((HalfBraiding.Œ≤ X.2 U).hom ‚ñ∑ U' ‚ñ∑ Y.1)` past each other.
        -- We do this with the help of the monoidal composition `‚äó‚â´` and the `coherence` tactic.
        calc
          _ = ùüô _ ‚äó‚â´
            X.1 ‚óÅ (HalfBraiding.Œ≤ Y.2 U).hom ‚ñ∑ U' ‚äó‚â´
              (_ ‚óÅ (HalfBraiding.Œ≤ Y.2 U').hom ‚â´
                (HalfBraiding.Œ≤ X.2 U).hom ‚ñ∑ _) ‚äó‚â´
                  U ‚óÅ (HalfBraiding.Œ≤ X.2 U').hom ‚ñ∑ Y.1 ‚äó‚â´ ùüô _ := by coherence
          _ = _ := by rw [whisker_exchange]; coherence
      naturality := fun {U U'} f => by
        dsimp only [Iso.trans_hom, whiskerLeftIso_hom, Iso.symm_hom, whiskerRightIso_hom]
        calc
          _ = ùüô _ ‚äó‚â´
            (X.1 ‚óÅ (Y.1 ‚óÅ f ‚â´ (HalfBraiding.Œ≤ Y.2 U').hom)) ‚äó‚â´
              (HalfBraiding.Œ≤ X.2 U').hom ‚ñ∑ Y.1 ‚äó‚â´ ùüô _ := by coherence
          _ = ùüô _ ‚äó‚â´
            X.1 ‚óÅ (HalfBraiding.Œ≤ Y.2 U).hom ‚äó‚â´
              (X.1 ‚óÅ f ‚â´ (HalfBraiding.Œ≤ X.2 U').hom) ‚ñ∑ Y.1 ‚äó‚â´ ùüô _ := by
            rw [HalfBraiding.naturality]; coherence
          _ = _ := by rw [HalfBraiding.naturality]; coherence }‚ü©
#align category_theory.center.tensor_obj CategoryTheory.Center.tensorObj

@[reassoc]
theorem whiskerLeft_comm (X : Center C) {Y‚ÇÅ Y‚ÇÇ : Center C} (f : Y‚ÇÅ ‚ü∂ Y‚ÇÇ) (U : C) :
    (X.1 ‚óÅ f.f) ‚ñ∑ U ‚â´ ((tensorObj X Y‚ÇÇ).2.Œ≤ U).hom =
      ((tensorObj X Y‚ÇÅ).2.Œ≤ U).hom ‚â´ U ‚óÅ X.1 ‚óÅ f.f := by
  dsimp only [tensorObj_fst, tensorObj_snd_Œ≤, Iso.trans_hom, whiskerLeftIso_hom,
    Iso.symm_hom, whiskerRightIso_hom]
  calc
    _ = ùüô _ ‚äó‚â´
      X.fst ‚óÅ (f.f ‚ñ∑ U ‚â´ (HalfBraiding.Œ≤ Y‚ÇÇ.snd U).hom) ‚äó‚â´
        (HalfBraiding.Œ≤ X.snd U).hom ‚ñ∑ Y‚ÇÇ.fst ‚äó‚â´ ùüô _ := by coherence
    _ = ùüô _ ‚äó‚â´
      X.fst ‚óÅ (HalfBraiding.Œ≤ Y‚ÇÅ.snd U).hom ‚äó‚â´
        ((X.fst ‚äó U) ‚óÅ f.f ‚â´ (HalfBraiding.Œ≤ X.snd U).hom ‚ñ∑ Y‚ÇÇ.fst) ‚äó‚â´ ùüô _ := by
      rw [f.comm]; coherence
    _ = _ := by rw [whisker_exchange]; coherence

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
def whiskerLeft (X : Center C) {Y‚ÇÅ Y‚ÇÇ : Center C} (f : Y‚ÇÅ ‚ü∂ Y‚ÇÇ) :
    tensorObj X Y‚ÇÅ ‚ü∂ tensorObj X Y‚ÇÇ where
  f := X.1 ‚óÅ f.f
  comm U := whiskerLeft_comm X f U

@[reassoc]
theorem whiskerRight_comm {X‚ÇÅ X‚ÇÇ: Center C} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (Y : Center C) (U : C) :
    f.f ‚ñ∑ Y.1 ‚ñ∑ U ‚â´ ((tensorObj X‚ÇÇ Y).2.Œ≤ U).hom =
      ((tensorObj X‚ÇÅ Y).2.Œ≤ U).hom ‚â´ U ‚óÅ f.f ‚ñ∑ Y.1 := by
  dsimp only [tensorObj_fst, tensorObj_snd_Œ≤, Iso.trans_hom, whiskerLeftIso_hom,
    Iso.symm_hom, whiskerRightIso_hom]
  calc
    _ = ùüô _ ‚äó‚â´
      (f.f ‚ñ∑ (Y.fst ‚äó U) ‚â´ X‚ÇÇ.fst ‚óÅ (HalfBraiding.Œ≤ Y.snd U).hom) ‚äó‚â´
        (HalfBraiding.Œ≤ X‚ÇÇ.snd U).hom ‚ñ∑ Y.fst ‚äó‚â´ ùüô _ := by coherence
    _ = ùüô _ ‚äó‚â´
      X‚ÇÅ.fst ‚óÅ (HalfBraiding.Œ≤ Y.snd U).hom ‚äó‚â´
        (f.f ‚ñ∑ U ‚â´ (HalfBraiding.Œ≤ X‚ÇÇ.snd U).hom) ‚ñ∑ Y.fst ‚äó‚â´ ùüô _ := by
      rw [‚Üê whisker_exchange]; coherence
    _ = _ := by rw [f.comm]; coherence

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
def whiskerRight {X‚ÇÅ X‚ÇÇ : Center C} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (Y : Center C) :
    tensorObj X‚ÇÅ Y ‚ü∂ tensorObj X‚ÇÇ Y where
  f := f.f ‚ñ∑ Y.1
  comm U := whiskerRight_comm f Y U

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
@[simps]
def tensorHom {X‚ÇÅ Y‚ÇÅ X‚ÇÇ Y‚ÇÇ : Center C} (f : X‚ÇÅ ‚ü∂ Y‚ÇÅ) (g : X‚ÇÇ ‚ü∂ Y‚ÇÇ) :
    tensorObj X‚ÇÅ X‚ÇÇ ‚ü∂ tensorObj Y‚ÇÅ Y‚ÇÇ where
  f := f.f ‚äó g.f
  comm U := by
    rw [tensorHom_def, comp_whiskerRight_assoc, whiskerLeft_comm, whiskerRight_comm_assoc,
      MonoidalCategory.whiskerLeft_comp]
#align category_theory.center.tensor_hom CategoryTheory.Center.tensorHom

section

attribute [local simp] id_tensorHom tensorHom_id

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
@[simps]
def tensorUnit : Center C :=
  ‚ü®ùüô_ C, { Œ≤ := fun U => Œª_ U ‚â™‚â´ (œÅ_ U).symm }‚ü©
#align category_theory.center.tensor_unit CategoryTheory.Center.tensorUnit

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
def associator (X Y Z : Center C) : tensorObj (tensorObj X Y) Z ‚âÖ tensorObj X (tensorObj Y Z) :=
  isoMk ‚ü®(Œ±_ X.1 Y.1 Z.1).hom, fun U => by simp‚ü©
#align category_theory.center.associator CategoryTheory.Center.associator

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
def leftUnitor (X : Center C) : tensorObj tensorUnit X ‚âÖ X :=
  isoMk ‚ü®(Œª_ X.1).hom, fun U => by simp‚ü©
#align category_theory.center.left_unitor CategoryTheory.Center.leftUnitor

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
def rightUnitor (X : Center C) : tensorObj X tensorUnit ‚âÖ X :=
  isoMk ‚ü®(œÅ_ X.1).hom, fun U => by simp‚ü©
#align category_theory.center.right_unitor CategoryTheory.Center.rightUnitor

end

section

attribute [local simp] associator_naturality leftUnitor_naturality rightUnitor_naturality pentagon

attribute [local simp] Center.associator Center.leftUnitor Center.rightUnitor

attribute [local simp] Center.whiskerLeft Center.whiskerRight Center.tensorHom

instance : MonoidalCategory (Center C) where
  tensorObj X Y := tensorObj X Y
  tensorHom f g := tensorHom f g
  tensorHom_def := by intros; ext; simp [tensorHom_def]
  whiskerLeft X _ _ f := whiskerLeft X f
  whiskerRight f Y := whiskerRight f Y
  tensorUnit := tensorUnit
  associator := associator
  leftUnitor := leftUnitor
  rightUnitor := rightUnitor

@[simp]
theorem tensor_fst (X Y : Center C) : (X ‚äó Y).1 = X.1 ‚äó Y.1 :=
  rfl
#align category_theory.center.tensor_fst CategoryTheory.Center.tensor_fst

@[simp]
theorem tensor_Œ≤ (X Y : Center C) (U : C) :
    (X ‚äó Y).2.Œ≤ U =
      Œ±_ _ _ _ ‚â™‚â´
        (whiskerLeftIso X.1 (Y.2.Œ≤ U)) ‚â™‚â´ (Œ±_ _ _ _).symm ‚â™‚â´
          (whiskerRightIso (X.2.Œ≤ U) Y.1) ‚â™‚â´ Œ±_ _ _ _ :=
  rfl
#align category_theory.center.tensor_Œ≤ CategoryTheory.Center.tensor_Œ≤

@[simp]
theorem whiskerLeft_f (X : Center C) {Y‚ÇÅ Y‚ÇÇ : Center C} (f : Y‚ÇÅ ‚ü∂ Y‚ÇÇ) : (X ‚óÅ f).f = X.1 ‚óÅ f.f :=
  rfl

@[simp]
theorem whiskerRight_f {X‚ÇÅ X‚ÇÇ : Center C} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (Y : Center C) : (f ‚ñ∑ Y).f = f.f ‚ñ∑ Y.1 :=
  rfl

@[simp]
theorem tensor_f {X‚ÇÅ Y‚ÇÅ X‚ÇÇ Y‚ÇÇ : Center C} (f : X‚ÇÅ ‚ü∂ Y‚ÇÅ) (g : X‚ÇÇ ‚ü∂ Y‚ÇÇ) : (f ‚äó g).f = f.f ‚äó g.f :=
  rfl
#align category_theory.center.tensor_f CategoryTheory.Center.tensor_f

@[simp]
theorem tensorUnit_Œ≤ (U : C) : (ùüô_ (Center C)).2.Œ≤ U = Œª_ U ‚â™‚â´ (œÅ_ U).symm :=
  rfl
#align category_theory.center.tensor_unit_Œ≤ CategoryTheory.Center.tensorUnit_Œ≤

@[simp]
theorem associator_hom_f (X Y Z : Center C) : Hom.f (Œ±_ X Y Z).hom = (Œ±_ X.1 Y.1 Z.1).hom :=
  rfl
#align category_theory.center.associator_hom_f CategoryTheory.Center.associator_hom_f

@[simp]
theorem associator_inv_f (X Y Z : Center C) : Hom.f (Œ±_ X Y Z).inv = (Œ±_ X.1 Y.1 Z.1).inv := by
  apply Iso.inv_ext' -- Porting note: Originally `ext`
  rw [‚Üê associator_hom_f, ‚Üê comp_f, Iso.hom_inv_id]; rfl
#align category_theory.center.associator_inv_f CategoryTheory.Center.associator_inv_f

@[simp]
theorem leftUnitor_hom_f (X : Center C) : Hom.f (Œª_ X).hom = (Œª_ X.1).hom :=
  rfl
#align category_theory.center.left_unitor_hom_f CategoryTheory.Center.leftUnitor_hom_f

@[simp]
theorem leftUnitor_inv_f (X : Center C) : Hom.f (Œª_ X).inv = (Œª_ X.1).inv := by
  apply Iso.inv_ext' -- Porting note: Originally `ext`
  rw [‚Üê leftUnitor_hom_f, ‚Üê comp_f, Iso.hom_inv_id]; rfl
#align category_theory.center.left_unitor_inv_f CategoryTheory.Center.leftUnitor_inv_f

@[simp]
theorem rightUnitor_hom_f (X : Center C) : Hom.f (œÅ_ X).hom = (œÅ_ X.1).hom :=
  rfl
#align category_theory.center.right_unitor_hom_f CategoryTheory.Center.rightUnitor_hom_f

@[simp]
theorem rightUnitor_inv_f (X : Center C) : Hom.f (œÅ_ X).inv = (œÅ_ X.1).inv := by
  apply Iso.inv_ext' -- Porting note: Originally `ext`
  rw [‚Üê rightUnitor_hom_f, ‚Üê comp_f, Iso.hom_inv_id]; rfl
#align category_theory.center.right_unitor_inv_f CategoryTheory.Center.rightUnitor_inv_f

end

section

variable (C)

/-- The forgetful monoidal functor from the Drinfeld center to the original category. -/
@[simps]
def forget : MonoidalFunctor (Center C) C where
  obj X := X.1
  map f := f.f
  Œµ := ùüô (ùüô_ C)
  Œº X Y := ùüô (X.1 ‚äó Y.1)
#align category_theory.center.forget CategoryTheory.Center.forget

instance : ReflectsIsomorphisms (forget C).toFunctor where
  reflects f i := by dsimp at i; change IsIso (isoMk f).hom; infer_instance

end

/-- Auxiliary definition for the `BraidedCategory` instance on `Center C`. -/
@[simps!]
def braiding (X Y : Center C) : X ‚äó Y ‚âÖ Y ‚äó X :=
  isoMk
    ‚ü®(X.2.Œ≤ Y.1).hom, fun U => by
      dsimp
      simp only [Category.assoc]
      rw [‚Üê IsIso.inv_comp_eq, IsIso.Iso.inv_hom, ‚Üê HalfBraiding.monoidal_assoc,
        ‚Üê HalfBraiding.naturality_assoc, HalfBraiding.monoidal]
      simp‚ü©
#align category_theory.center.braiding CategoryTheory.Center.braiding

instance braidedCategoryCenter : BraidedCategory (Center C) where
  braiding := braiding
#align category_theory.center.braided_category_center CategoryTheory.Center.braidedCategoryCenter

-- `aesop_cat` handles the hexagon axioms
section

variable [BraidedCategory C]

open BraidedCategory

/-- Auxiliary construction for `ofBraided`. -/
@[simps]
def ofBraidedObj (X : C) : Center C :=
  ‚ü®X, { Œ≤ := fun Y => Œ≤_ X Y}‚ü©
#align category_theory.center.of_braided_obj CategoryTheory.Center.ofBraidedObj

variable (C)

/-- The functor lifting a braided category to its center, using the braiding as the half-braiding.
-/
@[simps]
def ofBraided : MonoidalFunctor C (Center C) where
  obj := ofBraidedObj
  map f :=
    { f
      comm := fun U => braiding_naturality_left f U }
  Œµ := { f := ùüô _ }
  Œº X Y := { f := ùüô _ }
#align category_theory.center.of_braided CategoryTheory.Center.ofBraided

end

end Center

end CategoryTheory
