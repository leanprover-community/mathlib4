/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Monoidal.Braided.Basic
import Mathlib.CategoryTheory.Functor.ReflectsIso

#align_import category_theory.monoidal.center from "leanprover-community/mathlib"@"14b69e9f3c16630440a2cbd46f1ddad0d561dee7"

/-!
# Half braidings and the Drinfeld center of a monoidal category

We define `Center C` to be pairs `âŸ¨X, bâŸ©`, where `X : C` and `b` is a half-braiding on `X`.

We show that `Center C` is braided monoidal,
and provide the monoidal functor `Center.forget` from `Center C` back to `C`.

## Implementation notes

Verifying the various axioms directly requires tedious rewriting.
Using the `slice` tactic may make the proofs marginally more readable.

More exciting, however, would be to make possible one of the following options:
1. Integration with homotopy.io / globular to give "picture proofs".
2. The monoidal coherence theorem, so we can ignore associators
   (after which most of these proofs are trivial).
3. Automating these proofs using `rewrite_search` or some relative.

In this file, we take the second approach using the monoidal composition `âŠ—â‰«` and the
`coherence` tactic.
-/


open CategoryTheory

open CategoryTheory.MonoidalCategory

universe v vâ‚ vâ‚‚ vâ‚ƒ u uâ‚ uâ‚‚ uâ‚ƒ

noncomputable section

namespace CategoryTheory

variable {C : Type uâ‚} [Category.{vâ‚} C] [MonoidalCategory C]

/-- A half-braiding on `X : C` is a family of isomorphisms `X âŠ— U â‰… U âŠ— X`,
monoidally natural in `U : C`.

Thinking of `C` as a 2-category with a single `0`-morphism, these are the same as natural
transformations (in the pseudo- sense) of the identity 2-functor on `C`, which send the unique
`0`-morphism to `X`.
-/
-- @[nolint has_nonempty_instance] -- Porting note(#5171): This linter does not exist yet.
structure HalfBraiding (X : C) where
  Î² : âˆ€ U, X âŠ— U â‰… U âŠ— X
  monoidal : âˆ€ U U', (Î² (U âŠ— U')).hom =
      (Î±_ _ _ _).inv â‰«
        ((Î² U).hom â–· U') â‰« (Î±_ _ _ _).hom â‰« (U â— (Î² U').hom) â‰« (Î±_ _ _ _).inv := by
    aesop_cat
  naturality : âˆ€ {U U'} (f : U âŸ¶ U'), (X â— f) â‰« (Î² U').hom = (Î² U).hom â‰« (f â–· X) := by
    aesop_cat
#align category_theory.half_braiding CategoryTheory.HalfBraiding

attribute [reassoc, simp] HalfBraiding.monoidal -- the reassoc lemma is redundant as a simp lemma

attribute [simp, reassoc] HalfBraiding.naturality

variable (C)

/-- The Drinfeld center of a monoidal category `C` has as objects pairs `âŸ¨X, bâŸ©`, where `X : C`
and `b` is a half-braiding on `X`.
-/
-- @[nolint has_nonempty_instance] -- Porting note(#5171): This linter does not exist yet.
def Center :=
  Î£ X : C, HalfBraiding X
#align category_theory.center CategoryTheory.Center

namespace Center

variable {C}

/-- A morphism in the Drinfeld center of `C`. -/
-- Porting note(#5171): linter not ported yet
@[ext] -- @[nolint has_nonempty_instance]
structure Hom (X Y : Center C) where
  f : X.1 âŸ¶ Y.1
  comm : âˆ€ U, (f â–· U) â‰« (Y.2.Î² U).hom = (X.2.Î² U).hom â‰« (U â— f) := by aesop_cat
#align category_theory.center.hom CategoryTheory.Center.Hom

attribute [reassoc (attr := simp)] Hom.comm

instance : Quiver (Center C) where
  Hom := Hom

@[ext]
theorem ext {X Y : Center C} (f g : X âŸ¶ Y) (w : f.f = g.f) : f = g := by
  cases f; cases g; congr
#align category_theory.center.ext CategoryTheory.Center.ext

instance : Category (Center C) where
  id X := { f := ğŸ™ X.1 }
  comp f g := { f := f.f â‰« g.f }

@[simp]
theorem id_f (X : Center C) : Hom.f (ğŸ™ X) = ğŸ™ X.1 :=
  rfl
#align category_theory.center.id_f CategoryTheory.Center.id_f

@[simp]
theorem comp_f {X Y Z : Center C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) : (f â‰« g).f = f.f â‰« g.f :=
  rfl
#align category_theory.center.comp_f CategoryTheory.Center.comp_f

/-- Construct an isomorphism in the Drinfeld center from
a morphism whose underlying morphism is an isomorphism.
-/
@[simps]
def isoMk {X Y : Center C} (f : X âŸ¶ Y) [IsIso f.f] : X â‰… Y where
  hom := f
  inv := âŸ¨inv f.f,
    fun U => by simp [â† cancel_epi (f.f â–· U), â† comp_whiskerRight_assoc,
      â† MonoidalCategory.whiskerLeft_comp] âŸ©
#align category_theory.center.iso_mk CategoryTheory.Center.isoMk

instance isIso_of_f_isIso {X Y : Center C} (f : X âŸ¶ Y) [IsIso f.f] : IsIso f := by
  change IsIso (isoMk f).hom
  infer_instance
#align category_theory.center.is_iso_of_f_is_iso CategoryTheory.Center.isIso_of_f_isIso

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
@[simps]
def tensorObj (X Y : Center C) : Center C :=
  âŸ¨X.1 âŠ— Y.1,
    { Î² := fun U =>
        Î±_ _ _ _ â‰ªâ‰«
          (whiskerLeftIso X.1 (Y.2.Î² U)) â‰ªâ‰« (Î±_ _ _ _).symm â‰ªâ‰«
            (whiskerRightIso (X.2.Î² U) Y.1) â‰ªâ‰« Î±_ _ _ _
      monoidal := fun U U' => by
        dsimp only [Iso.trans_hom, whiskerLeftIso_hom, Iso.symm_hom, whiskerRightIso_hom]
        simp only [HalfBraiding.monoidal]
        -- We'd like to commute `X.1 â— U â— (HalfBraiding.Î² Y.2 U').hom`
        -- and `((HalfBraiding.Î² X.2 U).hom â–· U' â–· Y.1)` past each other.
        -- We do this with the help of the monoidal composition `âŠ—â‰«` and the `coherence` tactic.
        calc
          _ = ğŸ™ _ âŠ—â‰«
            X.1 â— (HalfBraiding.Î² Y.2 U).hom â–· U' âŠ—â‰«
              (_ â— (HalfBraiding.Î² Y.2 U').hom â‰«
                (HalfBraiding.Î² X.2 U).hom â–· _) âŠ—â‰«
                  U â— (HalfBraiding.Î² X.2 U').hom â–· Y.1 âŠ—â‰« ğŸ™ _ := by coherence
          _ = _ := by rw [whisker_exchange]; coherence
      naturality := fun {U U'} f => by
        dsimp only [Iso.trans_hom, whiskerLeftIso_hom, Iso.symm_hom, whiskerRightIso_hom]
        calc
          _ = ğŸ™ _ âŠ—â‰«
            (X.1 â— (Y.1 â— f â‰« (HalfBraiding.Î² Y.2 U').hom)) âŠ—â‰«
              (HalfBraiding.Î² X.2 U').hom â–· Y.1 âŠ—â‰« ğŸ™ _ := by coherence
          _ = ğŸ™ _ âŠ—â‰«
            X.1 â— (HalfBraiding.Î² Y.2 U).hom âŠ—â‰«
              (X.1 â— f â‰« (HalfBraiding.Î² X.2 U').hom) â–· Y.1 âŠ—â‰« ğŸ™ _ := by
            rw [HalfBraiding.naturality]; coherence
          _ = _ := by rw [HalfBraiding.naturality]; coherence }âŸ©
#align category_theory.center.tensor_obj CategoryTheory.Center.tensorObj

@[reassoc]
theorem whiskerLeft_comm (X : Center C) {Yâ‚ Yâ‚‚ : Center C} (f : Yâ‚ âŸ¶ Yâ‚‚) (U : C) :
    (X.1 â— f.f) â–· U â‰« ((tensorObj X Yâ‚‚).2.Î² U).hom =
      ((tensorObj X Yâ‚).2.Î² U).hom â‰« U â— X.1 â— f.f := by
  dsimp only [tensorObj_fst, tensorObj_snd_Î², Iso.trans_hom, whiskerLeftIso_hom,
    Iso.symm_hom, whiskerRightIso_hom]
  calc
    _ = ğŸ™ _ âŠ—â‰«
      X.fst â— (f.f â–· U â‰« (HalfBraiding.Î² Yâ‚‚.snd U).hom) âŠ—â‰«
        (HalfBraiding.Î² X.snd U).hom â–· Yâ‚‚.fst âŠ—â‰« ğŸ™ _ := by coherence
    _ = ğŸ™ _ âŠ—â‰«
      X.fst â— (HalfBraiding.Î² Yâ‚.snd U).hom âŠ—â‰«
        ((X.fst âŠ— U) â— f.f â‰« (HalfBraiding.Î² X.snd U).hom â–· Yâ‚‚.fst) âŠ—â‰« ğŸ™ _ := by
      rw [f.comm]; coherence
    _ = _ := by rw [whisker_exchange]; coherence

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
def whiskerLeft (X : Center C) {Yâ‚ Yâ‚‚ : Center C} (f : Yâ‚ âŸ¶ Yâ‚‚) :
    tensorObj X Yâ‚ âŸ¶ tensorObj X Yâ‚‚ where
  f := X.1 â— f.f
  comm U := whiskerLeft_comm X f U

@[reassoc]
theorem whiskerRight_comm {Xâ‚ Xâ‚‚: Center C} (f : Xâ‚ âŸ¶ Xâ‚‚) (Y : Center C) (U : C) :
    f.f â–· Y.1 â–· U â‰« ((tensorObj Xâ‚‚ Y).2.Î² U).hom =
      ((tensorObj Xâ‚ Y).2.Î² U).hom â‰« U â— f.f â–· Y.1 := by
  dsimp only [tensorObj_fst, tensorObj_snd_Î², Iso.trans_hom, whiskerLeftIso_hom,
    Iso.symm_hom, whiskerRightIso_hom]
  calc
    _ = ğŸ™ _ âŠ—â‰«
      (f.f â–· (Y.fst âŠ— U) â‰« Xâ‚‚.fst â— (HalfBraiding.Î² Y.snd U).hom) âŠ—â‰«
        (HalfBraiding.Î² Xâ‚‚.snd U).hom â–· Y.fst âŠ—â‰« ğŸ™ _ := by coherence
    _ = ğŸ™ _ âŠ—â‰«
      Xâ‚.fst â— (HalfBraiding.Î² Y.snd U).hom âŠ—â‰«
        (f.f â–· U â‰« (HalfBraiding.Î² Xâ‚‚.snd U).hom) â–· Y.fst âŠ—â‰« ğŸ™ _ := by
      rw [â† whisker_exchange]; coherence
    _ = _ := by rw [f.comm]; coherence

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
def whiskerRight {Xâ‚ Xâ‚‚ : Center C} (f : Xâ‚ âŸ¶ Xâ‚‚) (Y : Center C) :
    tensorObj Xâ‚ Y âŸ¶ tensorObj Xâ‚‚ Y where
  f := f.f â–· Y.1
  comm U := whiskerRight_comm f Y U

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
@[simps]
def tensorHom {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : Center C} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚) :
    tensorObj Xâ‚ Xâ‚‚ âŸ¶ tensorObj Yâ‚ Yâ‚‚ where
  f := f.f âŠ— g.f
  comm U := by
    rw [tensorHom_def, comp_whiskerRight_assoc, whiskerLeft_comm, whiskerRight_comm_assoc,
      MonoidalCategory.whiskerLeft_comp]
#align category_theory.center.tensor_hom CategoryTheory.Center.tensorHom

section

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
@[simps]
def tensorUnit : Center C :=
  âŸ¨ğŸ™_ C, { Î² := fun U => Î»_ U â‰ªâ‰« (Ï_ U).symm }âŸ©
#align category_theory.center.tensor_unit CategoryTheory.Center.tensorUnit

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
def associator (X Y Z : Center C) : tensorObj (tensorObj X Y) Z â‰… tensorObj X (tensorObj Y Z) :=
  isoMk âŸ¨(Î±_ X.1 Y.1 Z.1).hom, fun U => by simpâŸ©
#align category_theory.center.associator CategoryTheory.Center.associator

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
def leftUnitor (X : Center C) : tensorObj tensorUnit X â‰… X :=
  isoMk âŸ¨(Î»_ X.1).hom, fun U => by simpâŸ©
#align category_theory.center.left_unitor CategoryTheory.Center.leftUnitor

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
def rightUnitor (X : Center C) : tensorObj X tensorUnit â‰… X :=
  isoMk âŸ¨(Ï_ X.1).hom, fun U => by simpâŸ©
#align category_theory.center.right_unitor CategoryTheory.Center.rightUnitor

end

section

attribute [local simp] associator_naturality leftUnitor_naturality rightUnitor_naturality pentagon

attribute [local simp] Center.associator Center.leftUnitor Center.rightUnitor

attribute [local simp] Center.whiskerLeft Center.whiskerRight Center.tensorHom

instance : MonoidalCategory (Center C) where
  tensorObj X Y := tensorObj X Y
  tensorHom f g := tensorHom f g
  tensorHom_def := by intros; ext; simp [tensorHom_def]
  whiskerLeft X _ _ f := whiskerLeft X f
  whiskerRight f Y := whiskerRight f Y
  tensorUnit := tensorUnit
  associator := associator
  leftUnitor := leftUnitor
  rightUnitor := rightUnitor

@[simp]
theorem tensor_fst (X Y : Center C) : (X âŠ— Y).1 = X.1 âŠ— Y.1 :=
  rfl
#align category_theory.center.tensor_fst CategoryTheory.Center.tensor_fst

@[simp]
theorem tensor_Î² (X Y : Center C) (U : C) :
    (X âŠ— Y).2.Î² U =
      Î±_ _ _ _ â‰ªâ‰«
        (whiskerLeftIso X.1 (Y.2.Î² U)) â‰ªâ‰« (Î±_ _ _ _).symm â‰ªâ‰«
          (whiskerRightIso (X.2.Î² U) Y.1) â‰ªâ‰« Î±_ _ _ _ :=
  rfl
#align category_theory.center.tensor_Î² CategoryTheory.Center.tensor_Î²

@[simp]
theorem whiskerLeft_f (X : Center C) {Yâ‚ Yâ‚‚ : Center C} (f : Yâ‚ âŸ¶ Yâ‚‚) : (X â— f).f = X.1 â— f.f :=
  rfl

@[simp]
theorem whiskerRight_f {Xâ‚ Xâ‚‚ : Center C} (f : Xâ‚ âŸ¶ Xâ‚‚) (Y : Center C) : (f â–· Y).f = f.f â–· Y.1 :=
  rfl

@[simp]
theorem tensor_f {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : Center C} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚) : (f âŠ— g).f = f.f âŠ— g.f :=
  rfl
#align category_theory.center.tensor_f CategoryTheory.Center.tensor_f

@[simp]
theorem tensorUnit_Î² (U : C) : (ğŸ™_ (Center C)).2.Î² U = Î»_ U â‰ªâ‰« (Ï_ U).symm :=
  rfl
#align category_theory.center.tensor_unit_Î² CategoryTheory.Center.tensorUnit_Î²

@[simp]
theorem associator_hom_f (X Y Z : Center C) : Hom.f (Î±_ X Y Z).hom = (Î±_ X.1 Y.1 Z.1).hom :=
  rfl
#align category_theory.center.associator_hom_f CategoryTheory.Center.associator_hom_f

@[simp]
theorem associator_inv_f (X Y Z : Center C) : Hom.f (Î±_ X Y Z).inv = (Î±_ X.1 Y.1 Z.1).inv := by
  apply Iso.inv_ext' -- Porting note: Originally `ext`
  rw [â† associator_hom_f, â† comp_f, Iso.hom_inv_id]; rfl
#align category_theory.center.associator_inv_f CategoryTheory.Center.associator_inv_f

@[simp]
theorem leftUnitor_hom_f (X : Center C) : Hom.f (Î»_ X).hom = (Î»_ X.1).hom :=
  rfl
#align category_theory.center.left_unitor_hom_f CategoryTheory.Center.leftUnitor_hom_f

@[simp]
theorem leftUnitor_inv_f (X : Center C) : Hom.f (Î»_ X).inv = (Î»_ X.1).inv := by
  apply Iso.inv_ext' -- Porting note: Originally `ext`
  rw [â† leftUnitor_hom_f, â† comp_f, Iso.hom_inv_id]; rfl
#align category_theory.center.left_unitor_inv_f CategoryTheory.Center.leftUnitor_inv_f

@[simp]
theorem rightUnitor_hom_f (X : Center C) : Hom.f (Ï_ X).hom = (Ï_ X.1).hom :=
  rfl
#align category_theory.center.right_unitor_hom_f CategoryTheory.Center.rightUnitor_hom_f

@[simp]
theorem rightUnitor_inv_f (X : Center C) : Hom.f (Ï_ X).inv = (Ï_ X.1).inv := by
  apply Iso.inv_ext' -- Porting note: Originally `ext`
  rw [â† rightUnitor_hom_f, â† comp_f, Iso.hom_inv_id]; rfl
#align category_theory.center.right_unitor_inv_f CategoryTheory.Center.rightUnitor_inv_f

end

section

variable (C)

/-- The forgetful monoidal functor from the Drinfeld center to the original category. -/
@[simps]
def forget : MonoidalFunctor (Center C) C where
  obj X := X.1
  map f := f.f
  Îµ := ğŸ™ (ğŸ™_ C)
  Î¼ X Y := ğŸ™ (X.1 âŠ— Y.1)
#align category_theory.center.forget CategoryTheory.Center.forget

instance : (forget C).ReflectsIsomorphisms where
  reflects f i := by dsimp at i; change IsIso (isoMk f).hom; infer_instance

end

/-- Auxiliary definition for the `BraidedCategory` instance on `Center C`. -/
@[simps!]
def braiding (X Y : Center C) : X âŠ— Y â‰… Y âŠ— X :=
  isoMk
    âŸ¨(X.2.Î² Y.1).hom, fun U => by
      dsimp
      simp only [Category.assoc]
      rw [â† IsIso.inv_comp_eq, IsIso.Iso.inv_hom, â† HalfBraiding.monoidal_assoc,
        â† HalfBraiding.naturality_assoc, HalfBraiding.monoidal]
      simpâŸ©
#align category_theory.center.braiding CategoryTheory.Center.braiding

instance braidedCategoryCenter : BraidedCategory (Center C) where
  braiding := braiding
#align category_theory.center.braided_category_center CategoryTheory.Center.braidedCategoryCenter

-- `aesop_cat` handles the hexagon axioms
section

variable [BraidedCategory C]

open BraidedCategory

/-- Auxiliary construction for `ofBraided`. -/
@[simps]
def ofBraidedObj (X : C) : Center C :=
  âŸ¨X, { Î² := fun Y => Î²_ X Y}âŸ©
#align category_theory.center.of_braided_obj CategoryTheory.Center.ofBraidedObj

variable (C)

/-- The functor lifting a braided category to its center, using the braiding as the half-braiding.
-/
@[simps]
def ofBraided : MonoidalFunctor C (Center C) where
  obj := ofBraidedObj
  map f :=
    { f
      comm := fun U => braiding_naturality_left f U }
  Îµ := { f := ğŸ™ _ }
  Î¼ X Y := { f := ğŸ™ _ }
#align category_theory.center.of_braided CategoryTheory.Center.ofBraided

end

end Center

end CategoryTheory
