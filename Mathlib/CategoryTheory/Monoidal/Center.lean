/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Monoidal.Braided
import Mathlib.CategoryTheory.Functor.ReflectsIso
import Mathlib.CategoryTheory.Monoidal.CoherenceLemmas

#align_import category_theory.monoidal.center from "leanprover-community/mathlib"@"14b69e9f3c16630440a2cbd46f1ddad0d561dee7"

/-!
# Half braidings and the Drinfeld center of a monoidal category

We define `Center C` to be pairs `âŸ¨X, bâŸ©`, where `X : C` and `b` is a half-braiding on `X`.

We show that `Center C` is braided monoidal,
and provide the monoidal functor `Center.forget` from `Center C` back to `C`.

## Future work

Verifying the various axioms here is done by tedious rewriting.
Using the `slice` tactic may make the proofs marginally more readable.

More exciting, however, would be to make possible one of the following options:
1. Integration with homotopy.io / globular to give "picture proofs".
2. The monoidal coherence theorem, so we can ignore associators
   (after which most of these proofs are trivial;
   I'm unsure if the monoidal coherence theorem is even usable in dependent type theory).
3. Automating these proofs using `rewrite_search` or some relative.

-/


open CategoryTheory

open CategoryTheory.MonoidalCategory

universe v vâ‚ vâ‚‚ vâ‚ƒ u uâ‚ uâ‚‚ uâ‚ƒ

noncomputable section

namespace CategoryTheory

variable {C : Type uâ‚} [Category.{vâ‚} C] [MonoidalCategory C]

/-- A half-braiding on `X : C` is a family of isomorphisms `X âŠ— U â‰… U âŠ— X`,
monoidally natural in `U : C`.

Thinking of `C` as a 2-category with a single `0`-morphism, these are the same as natural
transformations (in the pseudo- sense) of the identity 2-functor on `C`, which send the unique
`0`-morphism to `X`.
-/
-- @[nolint has_nonempty_instance] -- Porting note: This linter does not exist yet.
structure HalfBraiding (X : C) where
  Î² : âˆ€ U, X âŠ— U â‰… U âŠ— X
  monoidal : âˆ€ U U', (Î² (U âŠ— U')).hom =
      (Î±_ _ _ _).inv â‰«
        ((Î² U).hom âŠ— ğŸ™ U') â‰« (Î±_ _ _ _).hom â‰« (ğŸ™ U âŠ— (Î² U').hom) â‰« (Î±_ _ _ _).inv := by
    aesop_cat
  naturality : âˆ€ {U U'} (f : U âŸ¶ U'), (ğŸ™ X âŠ— f) â‰« (Î² U').hom = (Î² U).hom â‰« (f âŠ— ğŸ™ X) := by
    aesop_cat
#align category_theory.half_braiding CategoryTheory.HalfBraiding

attribute [reassoc, simp] HalfBraiding.monoidal -- the reassoc lemma is redundant as a simp lemma

attribute [simp, reassoc] HalfBraiding.naturality

variable (C)

/-- The Drinfeld center of a monoidal category `C` has as objects pairs `âŸ¨X, bâŸ©`, where `X : C`
and `b` is a half-braiding on `X`.
-/
-- @[nolint has_nonempty_instance] -- Porting note: This linter does not exist yet.
def Center :=
  Î£ X : C, HalfBraiding X
#align category_theory.center CategoryTheory.Center

namespace Center

variable {C}

/-- A morphism in the Drinfeld center of `C`. -/
@[ext] -- @[nolint has_nonempty_instance] -- Porting note: This linter does not exist yet.
structure Hom (X Y : Center C) where
  f : X.1 âŸ¶ Y.1
  comm : âˆ€ U, (f âŠ— ğŸ™ U) â‰« (Y.2.Î² U).hom = (X.2.Î² U).hom â‰« (ğŸ™ U âŠ— f) := by aesop_cat
#align category_theory.center.hom CategoryTheory.Center.Hom

attribute [reassoc (attr := simp)] Hom.comm

instance : Quiver (Center C) where
  Hom := Hom

@[ext]
theorem ext {X Y : Center C} (f g : X âŸ¶ Y) (w : f.f = g.f) : f = g := by
  cases f; cases g; congr
#align category_theory.center.ext CategoryTheory.Center.ext

instance : Category (Center C) where
  id X := { f := ğŸ™ X.1 }
  comp f g := { f := f.f â‰« g.f }

@[simp]
theorem id_f (X : Center C) : Hom.f (ğŸ™ X) = ğŸ™ X.1 :=
  rfl
#align category_theory.center.id_f CategoryTheory.Center.id_f

@[simp]
theorem comp_f {X Y Z : Center C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) : (f â‰« g).f = f.f â‰« g.f :=
  rfl
#align category_theory.center.comp_f CategoryTheory.Center.comp_f

/-- Construct an isomorphism in the Drinfeld center from
a morphism whose underlying morphism is an isomorphism.
-/
@[simps]
def isoMk {X Y : Center C} (f : X âŸ¶ Y) [IsIso f.f] : X â‰… Y where
  hom := f
  inv := âŸ¨inv f.f,
    fun U => by simp [â† cancel_epi (f.f âŠ— ğŸ™ U), â† comp_tensor_id_assoc, â† id_tensor_comp]âŸ©
#align category_theory.center.iso_mk CategoryTheory.Center.isoMk

instance isIso_of_f_isIso {X Y : Center C} (f : X âŸ¶ Y) [IsIso f.f] : IsIso f := by
  change IsIso (isoMk f).hom
  infer_instance
#align category_theory.center.is_iso_of_f_is_iso CategoryTheory.Center.isIso_of_f_isIso

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
@[simps]
def tensorObj (X Y : Center C) : Center C :=
  âŸ¨X.1 âŠ— Y.1,
    { Î² := fun U =>
        Î±_ _ _ _ â‰ªâ‰«
          (Iso.refl X.1 âŠ— Y.2.Î² U) â‰ªâ‰« (Î±_ _ _ _).symm â‰ªâ‰« (X.2.Î² U âŠ— Iso.refl Y.1) â‰ªâ‰« Î±_ _ _ _
      monoidal := fun U U' => by
        dsimp
        simp only [comp_tensor_id, id_tensor_comp, Category.assoc, HalfBraiding.monoidal]
        -- On the RHS, we'd like to commute `((X.snd.Î² U).hom âŠ— ğŸ™ Y.fst) âŠ— ğŸ™ U'`
        -- and `ğŸ™ U âŠ— ğŸ™ X.fst âŠ— (Y.snd.Î² U').hom` past each other,
        -- but there are some associators we need to get out of the way first.
        slice_rhs 6 8 => rw [pentagon]
        slice_rhs 5 6 => rw [associator_naturality]
        slice_rhs 7 8 => rw [â† associator_naturality]
        slice_rhs 6 7 =>
          rw [tensor_id, tensor_id, tensor_id_comp_id_tensor, â† id_tensor_comp_tensor_id,
            â† tensor_id, â† tensor_id]
        -- Now insert associators as needed to make the four half-braidings look identical
        slice_rhs 10 10 => rw [associator_inv_conjugation]
        slice_rhs 7 7 => rw [associator_inv_conjugation]
        slice_rhs 6 6 => rw [associator_conjugation]
        slice_rhs 3 3 => rw [associator_conjugation]
        -- Finish with an application of the coherence theorem.
        coherence
      naturality := fun f => by
        dsimp
        rw [Category.assoc, Category.assoc, Category.assoc, Category.assoc,
          id_tensor_associator_naturality_assoc, â† id_tensor_comp_assoc, HalfBraiding.naturality,
          id_tensor_comp_assoc, associator_inv_naturality_assoc, â† comp_tensor_id_assoc,
          HalfBraiding.naturality, comp_tensor_id_assoc, associator_naturality, â† tensor_id] }âŸ©
#align category_theory.center.tensor_obj CategoryTheory.Center.tensorObj

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
@[simps]
def tensorHom {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : Center C} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚) :
    tensorObj Xâ‚ Xâ‚‚ âŸ¶ tensorObj Yâ‚ Yâ‚‚ where
  f := f.f âŠ— g.f
  comm U := by
    dsimp
    rw [Category.assoc, Category.assoc, Category.assoc, Category.assoc, associator_naturality_assoc,
      â† tensor_id_comp_id_tensor, Category.assoc, â† id_tensor_comp_assoc, g.comm,
      id_tensor_comp_assoc, tensor_id_comp_id_tensor_assoc, â† id_tensor_comp_tensor_id,
      Category.assoc, associator_inv_naturality_assoc, id_tensor_associator_inv_naturality_assoc,
      tensor_id, id_tensor_comp_tensor_id_assoc, â† tensor_id_comp_id_tensor g.f, Category.assoc,
      â† comp_tensor_id_assoc, f.comm, comp_tensor_id_assoc, id_tensor_associator_naturality,
      associator_naturality_assoc, â† id_tensor_comp, tensor_id_comp_id_tensor]
#align category_theory.center.tensor_hom CategoryTheory.Center.tensorHom

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
@[simps]
def tensorUnit : Center C :=
  âŸ¨ğŸ™_ C,
    { Î² := fun U => Î»_ U â‰ªâ‰« (Ï_ U).symm
      monoidal := fun U U' => by simp
      naturality := fun f => by
        dsimp
        rw [leftUnitor_naturality_assoc, rightUnitor_inv_naturality, Category.assoc] }âŸ©
#align category_theory.center.tensor_unit CategoryTheory.Center.tensorUnit

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
def associator (X Y Z : Center C) : tensorObj (tensorObj X Y) Z â‰… tensorObj X (tensorObj Y Z) :=
  isoMk
    âŸ¨(Î±_ X.1 Y.1 Z.1).hom, fun U => by
      dsimp
      simp only [comp_tensor_id, id_tensor_comp, â† tensor_id, associator_conjugation]
      coherenceâŸ©
#align category_theory.center.associator CategoryTheory.Center.associator

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
def leftUnitor (X : Center C) : tensorObj tensorUnit X â‰… X :=
  isoMk
    âŸ¨(Î»_ X.1).hom, fun U => by
      dsimp
      simp only [Category.comp_id, Category.assoc, tensor_inv_hom_id, comp_tensor_id,
        tensor_id_comp_id_tensor, triangle_assoc_comp_right_inv]
      rw [â† leftUnitor_tensor, leftUnitor_naturality, leftUnitor_tensor'_assoc]âŸ©
#align category_theory.center.left_unitor CategoryTheory.Center.leftUnitor

/-- Auxiliary definition for the `MonoidalCategory` instance on `Center C`. -/
def rightUnitor (X : Center C) : tensorObj X tensorUnit â‰… X :=
  isoMk
    âŸ¨(Ï_ X.1).hom, fun U => by
      dsimp
      simp only [tensor_id_comp_id_tensor_assoc, triangle_assoc, id_tensor_comp, Category.assoc]
      rw [â† tensor_id_comp_id_tensor_assoc (Ï_ U).inv, cancel_epi, â† rightUnitor_tensor_inv_assoc,
        â† rightUnitor_inv_naturality_assoc]
      simpâŸ©
#align category_theory.center.right_unitor CategoryTheory.Center.rightUnitor

section

attribute [local simp] associator_naturality leftUnitor_naturality rightUnitor_naturality pentagon

attribute [local simp] Center.associator Center.leftUnitor Center.rightUnitor

instance : MonoidalCategory (Center C) where
  tensorObj X Y := tensorObj X Y
  tensorHom f g := tensorHom f g
  -- Todo: replace it by `X.1 â— f.f`
  whiskerLeft X _ _ f := tensorHom (ğŸ™ X) f
  -- Todo: replace it by `f.f â–· Y.1`
  whiskerRight f Y := tensorHom f (ğŸ™ Y)
  tensorUnit := tensorUnit
  associator := associator
  leftUnitor := leftUnitor
  rightUnitor := rightUnitor

@[simp]
theorem tensor_fst (X Y : Center C) : (X âŠ— Y).1 = X.1 âŠ— Y.1 :=
  rfl
#align category_theory.center.tensor_fst CategoryTheory.Center.tensor_fst

@[simp]
theorem tensor_Î² (X Y : Center C) (U : C) :
    (X âŠ— Y).2.Î² U =
      Î±_ _ _ _ â‰ªâ‰«
        (Iso.refl X.1 âŠ— Y.2.Î² U) â‰ªâ‰« (Î±_ _ _ _).symm â‰ªâ‰« (X.2.Î² U âŠ— Iso.refl Y.1) â‰ªâ‰« Î±_ _ _ _ :=
  rfl
#align category_theory.center.tensor_Î² CategoryTheory.Center.tensor_Î²

@[simp]
theorem tensor_f {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : Center C} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚) : (f âŠ— g).f = f.f âŠ— g.f :=
  rfl
#align category_theory.center.tensor_f CategoryTheory.Center.tensor_f

@[simp]
theorem tensorUnit_Î² (U : C) : (ğŸ™_ (Center C)).2.Î² U = Î»_ U â‰ªâ‰« (Ï_ U).symm :=
  rfl
#align category_theory.center.tensor_unit_Î² CategoryTheory.Center.tensorUnit_Î²

@[simp]
theorem associator_hom_f (X Y Z : Center C) : Hom.f (Î±_ X Y Z).hom = (Î±_ X.1 Y.1 Z.1).hom :=
  rfl
#align category_theory.center.associator_hom_f CategoryTheory.Center.associator_hom_f

@[simp]
theorem associator_inv_f (X Y Z : Center C) : Hom.f (Î±_ X Y Z).inv = (Î±_ X.1 Y.1 Z.1).inv := by
  apply Iso.inv_ext' -- Porting note: Originally `ext`
  rw [â† associator_hom_f, â† comp_f, Iso.hom_inv_id]; rfl
#align category_theory.center.associator_inv_f CategoryTheory.Center.associator_inv_f

@[simp]
theorem leftUnitor_hom_f (X : Center C) : Hom.f (Î»_ X).hom = (Î»_ X.1).hom :=
  rfl
#align category_theory.center.left_unitor_hom_f CategoryTheory.Center.leftUnitor_hom_f

@[simp]
theorem leftUnitor_inv_f (X : Center C) : Hom.f (Î»_ X).inv = (Î»_ X.1).inv := by
  apply Iso.inv_ext' -- Porting note: Originally `ext`
  rw [â† leftUnitor_hom_f, â† comp_f, Iso.hom_inv_id]; rfl
#align category_theory.center.left_unitor_inv_f CategoryTheory.Center.leftUnitor_inv_f

@[simp]
theorem rightUnitor_hom_f (X : Center C) : Hom.f (Ï_ X).hom = (Ï_ X.1).hom :=
  rfl
#align category_theory.center.right_unitor_hom_f CategoryTheory.Center.rightUnitor_hom_f

@[simp]
theorem rightUnitor_inv_f (X : Center C) : Hom.f (Ï_ X).inv = (Ï_ X.1).inv := by
  apply Iso.inv_ext' -- Porting note: Originally `ext`
  rw [â† rightUnitor_hom_f, â† comp_f, Iso.hom_inv_id]; rfl
#align category_theory.center.right_unitor_inv_f CategoryTheory.Center.rightUnitor_inv_f

end

section

variable (C)

/-- The forgetful monoidal functor from the Drinfeld center to the original category. -/
@[simps]
def forget : MonoidalFunctor (Center C) C where
  obj X := X.1
  map f := f.f
  Îµ := ğŸ™ (ğŸ™_ C)
  Î¼ X Y := ğŸ™ (X.1 âŠ— Y.1)
#align category_theory.center.forget CategoryTheory.Center.forget

instance : ReflectsIsomorphisms (forget C).toFunctor where
  reflects f i := by dsimp at i; change IsIso (isoMk f).hom; infer_instance

end

/-- Auxiliary definition for the `BraidedCategory` instance on `Center C`. -/
@[simps!]
def braiding (X Y : Center C) : X âŠ— Y â‰… Y âŠ— X :=
  isoMk
    âŸ¨(X.2.Î² Y.1).hom, fun U => by
      dsimp
      simp only [Category.assoc]
      rw [â† IsIso.inv_comp_eq, IsIso.Iso.inv_hom, â† HalfBraiding.monoidal_assoc,
        â† HalfBraiding.naturality_assoc, HalfBraiding.monoidal]
      simpâŸ©
#align category_theory.center.braiding CategoryTheory.Center.braiding

instance braidedCategoryCenter : BraidedCategory (Center C) where
  braiding := braiding
  braiding_naturality f g := by
    ext
    dsimp
    rw [â† tensor_id_comp_id_tensor, Category.assoc, HalfBraiding.naturality, f.comm_assoc,
      id_tensor_comp_tensor_id]
#align category_theory.center.braided_category_center CategoryTheory.Center.braidedCategoryCenter

-- `aesop_cat` handles the hexagon axioms
section

variable [BraidedCategory C]

open BraidedCategory

/-- Auxiliary construction for `ofBraided`. -/
@[simps]
def ofBraidedObj (X : C) : Center C :=
  âŸ¨X, {
      Î² := fun Y => Î²_ X Y
      monoidal := fun U U' => by
        rw [Iso.eq_inv_comp, â† Category.assoc, â† Category.assoc, Iso.eq_comp_inv, Category.assoc,
          Category.assoc]
        exact hexagon_forward X U U' }âŸ©
#align category_theory.center.of_braided_obj CategoryTheory.Center.ofBraidedObj

variable (C)

/-- The functor lifting a braided category to its center, using the braiding as the half-braiding.
-/
@[simps]
def ofBraided : MonoidalFunctor C (Center C) where
  obj := ofBraidedObj
  map f :=
    { f
      comm := fun U => braiding_naturality _ _ }
  Îµ :=
    { f := ğŸ™ _
      comm := fun U => by
        dsimp
        rw [tensor_id, Category.id_comp, tensor_id, Category.comp_id, â† braiding_rightUnitor,
          Category.assoc, Iso.hom_inv_id, Category.comp_id] }
  Î¼ X Y :=
    { f := ğŸ™ _
      comm := fun U => by
        dsimp
        rw [tensor_id, tensor_id, Category.id_comp, Category.comp_id, â† Iso.inv_comp_eq,
          â† Category.assoc, â† Category.assoc, â† Iso.comp_inv_eq, Category.assoc, hexagon_reverse,
          Category.assoc] }
#align category_theory.center.of_braided CategoryTheory.Center.ofBraided

end

end Center

end CategoryTheory
