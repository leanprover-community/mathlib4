/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.Monoidal.DayConvolution
import Mathlib.CategoryTheory.Closed.Monoidal
import Mathlib.CategoryTheory.Limits.Shapes.End

/-! # Internal homs for day convolution

Given a category `V` that is monoidal closed, a category `C` that
is monoidal, a functor `C ‚•§ V`, and given the data of suitable day convolutions
and suitable ends of profunctors `c c‚ÇÅ c‚ÇÇ ‚Ü¶ ihom (F c‚ÇÅ) (¬∑.obj (c‚ÇÇ ‚äó c))`,
we prove that the data of the units of the left Kan extensions that define
day convolutions and the data of the canonical morphisms to the aforementioned
ends can be organised as data that exhibit `F` as monoidal closed in `C ‚•§ V` for
the Day convolution monoidal structure.

## TODOs
* When `LawfulDayConvolutionMonoidalStruct` (#26820) lands, transport the
constructions here to produce actual `CategoryTheory.MonoidalClosed` instances.
-/

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory.MonoidalCategory
open scoped ExternalProduct
open Opposite

noncomputable section

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {V : Type u‚ÇÇ} [Category.{v‚ÇÇ} V]
  [MonoidalCategory C] [MonoidalCategory V] [MonoidalClosed V]

/-- Given `F : C ‚•§ V`, this is the functor
`G ‚Ü¶ c c‚ÇÅ c‚ÇÇ ‚Ü¶ ihom (F c‚ÇÅ) (G.obj (c‚ÇÇ ‚äó c))`.
The internal hom functor for Day convolution `[F, -]` is naturally isomorphic
to the functor `G ‚Ü¶ c ‚Ü¶ end_ (c‚ÇÅ c‚ÇÇ ‚Ü¶ ihom (F c‚ÇÅ) (G.obj (c‚ÇÇ ‚äó c)))`, hence
this definition. -/
@[simps!]
def internalHomDiagramFunctor (F : C ‚•§ V) : (C ‚•§ V) ‚•§ C ‚•§ C·µí·µñ ‚•§ C ‚•§ V where
  obj G :=
    { obj c := Functor.whiskeringLeft‚ÇÇ _|>.obj F.op|>.obj
        (tensorRight c ‚ãô G)|>.obj MonoidalClosed.internalHom
      map {c c'} f := Functor.whiskeringLeft‚ÇÇ _|>.obj F.op|>.map
        (Functor.whiskerRight (curriedTensor C|>.flip.map f) G)|>.app
          MonoidalClosed.internalHom }
  map {G G'} Œ∑ :=
    { app c := Functor.whiskeringLeft‚ÇÇ _|>.obj F.op|>.map
        (Functor.whiskerLeft _ Œ∑) |>.app MonoidalClosed.internalHom
      naturality {c c'} f := by
        ext j k
        dsimp
        simpa [-NatTrans.naturality] using congr_arg (ihom (F.obj (unop j))).map
          (Œ∑.naturality <| k ‚óÅ f) }

/-- `DayConvolutionInternalHom F H` asserts that `H` is an
internal hom functor of `F` for the Day convolution monoidal structure.
This is phrased as the data of a limit `CategoryTheory.Limits.Wedge`
(i.e an end) on `internalHomDiagramFunctor F|>.obj G|>.obj c` for every `G` and
`c`, with tip `(H.obj G).obj c` and two compatibility conditions asserting that
the functoriality of `H` identifies to the functoriality of ends. -/
structure DayConvolutionInternalHom (F : C ‚•§ V) (H : (C ‚•§ V) ‚•§ C ‚•§ V) where
  /-- The canonical projections maps -/
  œÄ (G : C ‚•§ V) (c j : C) :
    (H.obj G).obj c ‚ü∂ (ihom (F.obj j)).obj (G.obj (j ‚äó c))
  /-- The projections maps assemble into a wedge. -/
  hœÄ (G : C ‚•§ V) (c : C) ‚¶Éi j : C‚¶Ñ (f : i ‚ü∂ j) :
    œÄ G c i ‚â´ (ihom (F.obj i)).map (G.map (f ‚ñ∑ c)) =
    œÄ G c j ‚â´ (MonoidalClosed.pre (F.map f)).app (G.obj (j ‚äó c))
  /-- The wedge defined by `œÄ` and `hœÄ` is a limit wedge, i.e `H.obj c` is
  an end of `internalHomDiagramFunctor F G|>.obj c`. -/
  isLimitWedge G c :
    Limits.IsLimit <|
      Limits.Wedge.mk (F := internalHomDiagramFunctor F|>.obj G|>.obj c)
        (H.obj G|>.obj c) (œÄ G c) (hœÄ G c)
  /-- The functoriality of `H.obj G` identifies (through
  `Limits.Wedge.IsLimit.hom_ext`) with the functoriality on ends induced by
  functoriality of `internalHomDiagramFunctor F|>.obj G`. -/
  obj_map_comp_œÄ (G : C ‚•§ V) {c c' : C} (f : c ‚ü∂ c') (j : C) :
    (H.obj G).map f ‚â´ œÄ G c' j =
    œÄ G c j ‚â´ (ihom (F.obj j)).map (G.map (j ‚óÅ f))
  /-- The functoriality of `H` in its first variable identifies (through
  `Limits.Wedge.IsLimit.hom_ext`) with the functoriality on ends
  induced by functoriality of `internalHomDiagramFunctor F`. -/
  map_app_comp_œÄ {G G' : C ‚•§ V} (Œ∑ : G ‚ü∂ G') (c j : C) :
    (H.map Œ∑).app c ‚â´ œÄ G' c j =
    œÄ G c j ‚â´ (ihom (F.obj j)).map (Œ∑.app (j ‚äó c))


namespace DayConvolutionInternalHom
open scoped DayConvolution

attribute [reassoc (attr := simp)] obj_map_comp_œÄ map_app_comp_œÄ hœÄ

variable {F : C ‚•§ V} {H : (C ‚•§ V) ‚•§ C ‚•§ V}
  (‚Ñå : DayConvolutionInternalHom F H)

section ev

variable (G : C ‚•§ V) [DayConvolution F (H.obj G)]

/-- Given `‚Ñå : DayConvolutionInternalHom F H`, if we think of `H.obj G`
as the internal hom `[F, G]`, then this is the transformation
corresponding to the component at `G` of the "evaluation" natural morphism
`F ‚äõ [F, _] ‚ü∂ ùü≠`. -/
def ev_app : F ‚äõ (H.obj G) ‚ü∂ G :=
  DayConvolution.corepresentableBy F (H.obj G)|>.homEquiv.symm <|
    { app := fun x => MonoidalClosed.uncurry <| ‚Ñå.œÄ G x.2 x.1
      naturality {x y} f := by
        haveI := congrArg (fun t ‚Ü¶ F.obj x.1 ‚óÅ t) <| ‚Ñå.hœÄ G x.2 f.1
        dsimp at this ‚ä¢
        simp only [whiskerLeft_comp] at this
        simp only [Category.assoc, MonoidalClosed.uncurry_eq, Functor.id_obj]
        rw [‚Üê whiskerLeft_comp_assoc, obj_map_comp_œÄ]
        simp [‚Üê whisker_exchange_assoc, tensorHom_def,
          ‚Üê ihom.ev_naturality_assoc]
        rw [reassoc_of% this]
        simp }

@[reassoc (attr := simp)]
lemma curry_unit_app_comp_ev_app_app (x y : C) :
    ((DayConvolution.unit F (H.obj G)).app (x, y) ‚â´
      (ev_app ‚Ñå G).app (x ‚äó y)) =
    MonoidalClosed.uncurry (‚Ñå.œÄ G y x) := by
  simp [ev_app]
  haveI := Functor.descOfIsLeftKanExtension_fac_app
    (F ‚äõ H.obj G) (DayConvolution.unit F (H.obj G)) G
  dsimp at this
  rw [this]

variable {G} in
lemma ev_naturality_app {G' : C ‚•§ V} [DayConvolution F (H.obj G')] (Œ∑ : G ‚ü∂ G') :
    DayConvolution.map (ùüô F) (H.map Œ∑) ‚â´ (ev_app ‚Ñå G') =
    (ev_app ‚Ñå G) ‚â´ Œ∑ := by
  apply DayConvolution.corepresentableBy F (H.obj G)|>.homEquiv.injective
  dsimp
  ext ‚ü®x, y‚ü©
  dsimp
  simp only [DayConvolution.unit_app_map_app_assoc,
    externalProductBifunctor_obj_obj, NatTrans.id_app, id_tensorHom,
    curry_unit_app_comp_ev_app_app, MonoidalClosed.uncurry_eq,
    Functor.id_obj, curry_unit_app_comp_ev_app_app_assoc, Category.assoc]
  rw [‚Üê whiskerLeft_comp_assoc, map_app_comp_œÄ]
  simp

end ev

section coev

variable (G : C ‚•§ V) [DayConvolution F G]

/-- Given `‚Ñå : DayConvolutionInternalHom F H`, if we think of `H.obj G`
as the internal hom `[F, G]`, then this is the transformation
corresponding to the component at `G` of the "coevaluation" natural morphism
`ùü≠ ‚ü∂ [F, F ‚äõ _]`. -/
def coev_app : G ‚ü∂ H.obj (F ‚äõ G) where
  app c :=
    Limits.Wedge.IsLimit.lift (‚Ñå.isLimitWedge (F ‚äõ G) c)
      (fun c' => MonoidalClosed.curry <|
        (DayConvolution.unit F G).app (c', c))
        (fun {c' c''} f => by
          haveI := DayConvolution.unit_naturality F G f (ùüô c)
          simp only [Functor.map_id, tensorHom_id] at this
          replace this := congrArg MonoidalClosed.curry this
          simp only [MonoidalClosed.curry_natural_right] at this
          dsimp
          rw [‚Üê this]
          simp [MonoidalClosed.curry_eq])
  naturality {c c'} f := by
    dsimp
    apply Limits.Wedge.IsLimit.hom_ext (‚Ñå.isLimitWedge (F ‚äõ G) c')
    intro (j : C)
    simp [Limits.multicospanIndexEnd_left,
      Limits.Multifork.ofŒπ_pt, Limits.Wedge.mk_Œπ, Category.assoc]
    rw [‚Üê Limits.Wedge.mk_Œπ (F := internalHomDiagramFunctor F|>.obj _|>.obj c)
        (H.obj (F ‚äõ G)|>.obj c) (‚Ñå.œÄ (F ‚äõ G) c) (‚Ñå.hœÄ (F ‚äõ G) c),
      ‚Üê Limits.Wedge.mk_Œπ (F := internalHomDiagramFunctor F|>.obj _|>.obj c')
        (H.obj (F ‚äõ G)|>.obj c') (‚Ñå.œÄ (F ‚äõ G) c') (‚Ñå.hœÄ (F ‚äõ G) c'),
      Limits.Wedge.IsLimit.lift_Œπ_assoc, Limits.Wedge.IsLimit.lift_Œπ]
    haveI := DayConvolution.unit_naturality F G (ùüô j) f
    simp only [Functor.map_id, id_tensorHom] at this
    replace this := congrArg MonoidalClosed.curry this
    simp only [MonoidalClosed.curry_natural_right] at this
    rw [‚Üê this]
    simp [MonoidalClosed.curry_eq]

@[reassoc (attr := simp)]
lemma coev_app_comp_œÄ (c j : C) :
    (‚Ñå.coev_app G).app c ‚â´ ‚Ñå.œÄ (F ‚äõ G) c j =
    MonoidalClosed.curry ((DayConvolution.unit F G).app (j, c)) := by
  dsimp [coev_app]
  rw [‚Üê Limits.Wedge.mk_Œπ (F := internalHomDiagramFunctor F|>.obj _|>.obj c)
      (H.obj (F ‚äõ G)|>.obj c) (‚Ñå.œÄ (F ‚äõ G) c) (‚Ñå.hœÄ (F ‚äõ G) c),
    Limits.Wedge.IsLimit.lift_Œπ]

variable {G} in
@[simp]
lemma coev_naturality_app {G' : C ‚•§ V} [DayConvolution F G'] (Œ∑ : G ‚ü∂ G') :
    Œ∑ ‚â´ ‚Ñå.coev_app G' = ‚Ñå.coev_app G ‚â´ H.map (DayConvolution.map (ùüô _) Œ∑) := by
  ext c
  dsimp
  apply Limits.Wedge.IsLimit.hom_ext (‚Ñå.isLimitWedge (F ‚äõ G') c)
  intro j
  apply MonoidalClosed.uncurry_injective
  dsimp
  simp only [Category.assoc, coev_app_comp_œÄ, Functor.comp_obj, tensor_obj,
    map_app_comp_œÄ, coev_app_comp_œÄ_assoc, MonoidalClosed.uncurry_natural_right,
    MonoidalClosed.uncurry_curry, DayConvolution.unit_app_map_app,
    NatTrans.id_app, id_tensorHom]
  simp [MonoidalClosed.uncurry_natural_left]

end coev

theorem left_triangle_component (G : C ‚•§ V) [DayConvolution F G]
    [DayConvolution F (H.obj (F ‚äõ G))] :
    DayConvolution.map (ùüô F) (‚Ñå.coev_app G) ‚â´ ‚Ñå.ev_app (F ‚äõ G) = ùüô (F ‚äõ G) := by
  apply DayConvolution.corepresentableBy F G|>.homEquiv.injective
  dsimp
  ext ‚ü®x, y‚ü©
  apply MonoidalClosed.curry_injective
  simp [MonoidalClosed.curry_natural_left]

theorem right_triangle_component (G : C ‚•§ V) [DayConvolution F (H.obj G)] :
    ‚Ñå.coev_app (H.obj G) ‚â´ H.map (‚Ñå.ev_app G) = ùüô (H.obj G) := by
  ext c
  apply Limits.Wedge.IsLimit.hom_ext (‚Ñå.isLimitWedge _ c)
  intro j
  apply MonoidalClosed.uncurry_injective
  simp [MonoidalClosed.uncurry_natural_right]

end DayConvolutionInternalHom

end

end CategoryTheory.MonoidalCategory
