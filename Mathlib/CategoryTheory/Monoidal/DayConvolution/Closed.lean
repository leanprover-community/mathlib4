/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.Monoidal.DayConvolution
import Mathlib.CategoryTheory.Closed.Monoidal
import Mathlib.CategoryTheory.Limits.Shapes.End

/-! # Internal homs for day convolution

Given a category `V` that is monoidal closed, a category `C` that
is monoidal, a functor `C ‚•§ V`, and given the data of suitable day convolutions
and suitable ends of profunctors `c c‚ÇÅ c‚ÇÇ ‚Ü¶ ihom (F c‚ÇÅ) (¬∑.obj (c‚ÇÇ ‚äó c))`,
we prove that the data of the units of the left Kan extensions that define
day convolutions and the data of the canonical morphisms to the aforementioned
ends can be organised as data that exhibit `F` as monoidal closed in `C ‚•§ V` for
the Day convolution monoidal structure.

## TODOs
* When `LawfulDayConvolutionMonoidalStruct` (#26820) lands, transport the
constructions here to produce actual `CategoryTheory.MonoidalClosed` instances.
-/

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory.MonoidalCategory
open scoped ExternalProduct
open Opposite Limits

noncomputable section

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {V : Type u‚ÇÇ} [Category.{v‚ÇÇ} V]
  [MonoidalCategory C] [MonoidalCategory V] [MonoidalClosed V]

/-- Given `F : C ‚•§ V`, this is the functor
`G ‚Ü¶ c c‚ÇÅ c‚ÇÇ ‚Ü¶ ihom (F c‚ÇÅ) (G.obj (c‚ÇÇ ‚äó c))`.
The internal hom functor for Day convolution `[F, -]` is naturally isomorphic
to the functor `G ‚Ü¶ c ‚Ü¶ end_ (c‚ÇÅ c‚ÇÇ ‚Ü¶ ihom (F c‚ÇÅ) (G.obj (c‚ÇÇ ‚äó c)))`, hence
this definition. -/
@[simps!]
def dayConvolutionInternalHomDiagramFunctor (F : C ‚•§ V) :
    (C ‚•§ V) ‚•§ C ‚•§ C·µí·µñ ‚•§ C ‚•§ V where
  obj G :=
    { obj c := Functor.whiskeringLeft‚ÇÇ _|>.obj F.op|>.obj
        (tensorRight c ‚ãô G)|>.obj MonoidalClosed.internalHom
      map {c c'} f := Functor.whiskeringLeft‚ÇÇ _|>.obj F.op|>.map
        (Functor.whiskerRight (curriedTensor C|>.flip.map f) G)|>.app
          MonoidalClosed.internalHom }
  map {G G'} Œ∑ :=
    { app c := Functor.whiskeringLeft‚ÇÇ _|>.obj F.op|>.map
        (Functor.whiskerLeft _ Œ∑) |>.app MonoidalClosed.internalHom
      naturality {c c'} f := by
        ext j k
        dsimp
        simpa [-NatTrans.naturality] using
          congr_arg (ihom <| F.obj <| unop j).map (Œ∑.naturality <| k ‚óÅ f) }

/-- `DayConvolutionInternalHom F G H` asserts that `H` is the value at `G` of
an internal hom functor of `F` for the Day convolution monoidal structure.
This is phrased as the data of a limit `CategoryTheory.Wedge`
(i.e an end) on `internalHomDiagramFunctor F|>.obj G|>.obj c` and
`c`, with tip `(H.obj G).obj c` and a compatibility condition asserting that
the functoriality of `H` identifies to the functoriality of ends. -/
structure DayConvolutionInternalHom (F : C ‚•§ V) (G : C ‚•§ V) (H : C ‚•§ V) where
  /-- The canonical projections maps -/
  œÄ (c j : C) : H.obj c ‚ü∂ (ihom <| F.obj j).obj (G.obj <| j ‚äó c)
  /-- The projections maps assemble into a wedge. -/
  hœÄ (c : C) ‚¶Éi j : C‚¶Ñ (f : i ‚ü∂ j) :
    œÄ c i ‚â´ (ihom (F.obj i)).map (G.map <| f ‚ñ∑ c) =
    œÄ c j ‚â´ (MonoidalClosed.pre <| F.map f).app (G.obj <| j ‚äó c)
  /-- The wedge defined by `œÄ` and `hœÄ` is a limit wedge, i.e `H.obj c` is
  an end of `internalHomDiagramFunctor F G|>.obj c`. -/
  isLimitWedge (c : C) :
    IsLimit <| Wedge.mk
      (F := dayConvolutionInternalHomDiagramFunctor F|>.obj G|>.obj c)
      (H.obj c) (œÄ c) (hœÄ c)
  /-- The functoriality of `H.obj G` identifies (through
  `Wedge.IsLimit.hom_ext`) with the functoriality on ends induced by
  functoriality of `internalHomDiagramFunctor F|>.obj G`. -/
  map_comp_œÄ {c c' : C} (f : c ‚ü∂ c') (j : C) :
    H.map f ‚â´ œÄ c' j = œÄ c j ‚â´ (ihom <| F.obj j).map (G.map <| j ‚óÅ f)

namespace DayConvolutionInternalHom

open scoped DayConvolution

attribute [reassoc (attr := simp)] map_comp_œÄ
attribute [reassoc] hœÄ

variable {F : C ‚•§ V} {G : C ‚•§ V} {H : C ‚•§ V}

/-- If we have a map `G ‚ü∂ G'` and a `DayConvolutionInternalHom F G' H'`, then
there is a unique map `H ‚ü∂ H'` induced by functoriality of ends and functoriality
of `internalHomDiagramFunctor F`. -/
def map (‚Ñå : DayConvolutionInternalHom F G H) {G' : C ‚•§ V} {H' : C ‚•§ V}
    (f : G ‚ü∂ G') (‚Ñå' : DayConvolutionInternalHom F G' H') :
    H ‚ü∂ H' where
  app c := Wedge.IsLimit.lift (‚Ñå'.isLimitWedge c)
    (fun j ‚Ü¶ (‚Ñå.œÄ c j) ‚â´
      (dayConvolutionInternalHomDiagramFunctor
        F|>.map f|>.app c|>.app (op j)|>.app j))
    (fun ‚¶Éj j'‚¶Ñ œÜ ‚Ü¶ by
      have := congrArg (fun t ‚Ü¶ t.app j') <|
        dayConvolutionInternalHomDiagramFunctor
          F|>.map f|>.app c|>.naturality œÜ.op
      dsimp at this ‚ä¢
      rw [Category.assoc, ‚Üê (ihom (F.obj j)).map_comp, ‚Üê f.naturality,
        Functor.map_comp, reassoc_of% ‚Ñå.hœÄ]
      simp)
  naturality {c c'} f := by
    apply Wedge.IsLimit.hom_ext (‚Ñå'.isLimitWedge c')
    intro j
    dsimp
    simp only [Category.assoc, map_comp_œÄ]
    rw [‚Üê Wedge.mk_Œπ
        (F := dayConvolutionInternalHomDiagramFunctor F|>.obj _|>.obj c')
        (H'.obj c') (‚Ñå'.œÄ c') (‚Ñå'.hœÄ c'),
      ‚Üê Wedge.mk_Œπ
        (F := dayConvolutionInternalHomDiagramFunctor F|>.obj _|>.obj c)
        (H'.obj c) (‚Ñå'.œÄ c) (‚Ñå'.hœÄ c),
      Wedge.IsLimit.lift_Œπ (‚Ñå'.isLimitWedge c'),
      Wedge.IsLimit.lift_Œπ_assoc (‚Ñå'.isLimitWedge c) ]
    simp [‚Üê Functor.map_comp]

@[reassoc (attr := simp)]
lemma map_app_comp_œÄ (‚Ñå : DayConvolutionInternalHom F G H)
    {G' : C ‚•§ V} {H' : C ‚•§ V} (f : G ‚ü∂ G')
    (‚Ñå' : DayConvolutionInternalHom F G' H') (c : C) (j : C) :
    (‚Ñå.map f ‚Ñå').app c ‚â´ ‚Ñå'.œÄ c j =
    ‚Ñå.œÄ c j ‚â´ (ihom <| F.obj j).map (f.app <| j ‚äó c) := by
  dsimp [map]
  rw [‚Üê Wedge.mk_Œπ
      (F := dayConvolutionInternalHomDiagramFunctor F|>.obj _|>.obj c)
      (H'.obj c) (‚Ñå'.œÄ c) (‚Ñå'.hœÄ c),
    Wedge.IsLimit.lift_Œπ (‚Ñå'.isLimitWedge c)]

section ev

variable [DayConvolution F H] (‚Ñå : DayConvolutionInternalHom F G H)

/-- Given `‚Ñå : DayConvolutionInternalHom F H`, if we think of `H.obj G`
as the internal hom `[F, G]`, then this is the transformation
corresponding to the component at `G` of the "evaluation" natural morphism
`F ‚äõ [F, _] ‚ü∂ ùü≠`. -/
def ev_app : F ‚äõ H ‚ü∂ G :=
  DayConvolution.corepresentableBy F H|>.homEquiv.symm <|
    { app x := MonoidalClosed.uncurry <| ‚Ñå.œÄ x.2 x.1
      naturality {x y} f := by
        have := congrArg (fun t ‚Ü¶ F.obj x.1 ‚óÅ t) <| ‚Ñå.hœÄ x.2 f.1
        dsimp at this ‚ä¢
        simp only [whiskerLeft_comp] at this
        simp only [Category.assoc, MonoidalClosed.uncurry_eq, Functor.id_obj,
          ‚Üê whiskerLeft_comp_assoc, map_comp_œÄ]
        simp only [whiskerLeft_comp, Category.assoc, ihom.ev_naturality,
          Functor.comp_obj, curriedTensor_obj_obj, Functor.id_obj,
          ‚Üê whisker_exchange_assoc, tensorHom_def, Functor.map_comp,
          ‚Üê ihom.ev_naturality_assoc]
        rw [reassoc_of% this]
        simp }

@[reassoc (attr := simp)]
lemma unit_app_ev_app_app (x y : C) :
    ((DayConvolution.unit F H).app (x, y) ‚â´ (‚Ñå.ev_app).app (x ‚äó y)) =
    MonoidalClosed.uncurry (‚Ñå.œÄ y x) := by
  have := Functor.descOfIsLeftKanExtension_fac_app (F ‚äõ H)
    (DayConvolution.unit F H) G
  dsimp at this
  simp [this, ev_app]

lemma ev_naturality_app {G' H' : C ‚•§ V} (‚Ñå' : DayConvolutionInternalHom F G' H')
    [DayConvolution F H'] (Œ∑ : G ‚ü∂ G') :
    DayConvolution.map (ùüô F) (‚Ñå.map Œ∑ ‚Ñå') ‚â´ ‚Ñå'.ev_app = ‚Ñå.ev_app ‚â´ Œ∑ := by
  apply DayConvolution.corepresentableBy F H|>.homEquiv.injective
  dsimp
  ext ‚ü®x, y‚ü©
  simp [MonoidalClosed.uncurry_eq, ‚Üê whiskerLeft_comp_assoc]

end ev

section coev

variable {G : C ‚•§ V} [DayConvolution F G]
    (‚Ñå : DayConvolutionInternalHom F (F ‚äõ G) H)

/-- Given `‚Ñå : DayConvolutionInternalHom F H`, if we think of `H.obj G`
as the internal hom `[F, G]`, then this is the transformation
corresponding to the component at `G` of the "coevaluation" natural morphism
`ùü≠ ‚ü∂ [F, F ‚äõ _]`. -/
def coev_app : G ‚ü∂ H where
  app c :=
    Wedge.IsLimit.lift (‚Ñå.isLimitWedge c)
      (fun c' => MonoidalClosed.curry <|
        (DayConvolution.unit F G).app (c', c))
        (fun {c' c''} f => by
          have := DayConvolution.unit_naturality F G f (ùüô c)
          simp only [Functor.map_id, tensorHom_id] at this
          replace this := congrArg MonoidalClosed.curry this
          simp only [MonoidalClosed.curry_natural_right] at this
          dsimp
          rw [‚Üê this]
          simp [MonoidalClosed.curry_eq])
  naturality {c c'} f := by
    dsimp
    apply Wedge.IsLimit.hom_ext <| ‚Ñå.isLimitWedge c'
    intro (j : C)
    simp only [multicospanIndexEnd_left,
      dayConvolutionInternalHomDiagramFunctor_obj_obj_obj_obj, Multifork.ofŒπ_pt,
      Wedge.mk_Œπ, Category.assoc, map_comp_œÄ]
    rw [‚Üê Wedge.mk_Œπ
        (F := dayConvolutionInternalHomDiagramFunctor F|>.obj _|>.obj c)
        (H.obj c) (‚Ñå.œÄ c) (‚Ñå.hœÄ c),
      ‚Üê Wedge.mk_Œπ
        (F := dayConvolutionInternalHomDiagramFunctor F|>.obj _|>.obj c')
        (H.obj c') (‚Ñå.œÄ c') (‚Ñå.hœÄ c'),
      Wedge.IsLimit.lift_Œπ_assoc, Wedge.IsLimit.lift_Œπ]
    have := DayConvolution.unit_naturality F G (ùüô j) f
    simp only [Functor.map_id, id_tensorHom] at this
    replace this := congrArg MonoidalClosed.curry this
    simp only [MonoidalClosed.curry_natural_right] at this
    rw [‚Üê this]
    simp [MonoidalClosed.curry_eq]

@[reassoc (attr := simp)]
lemma coev_app_œÄ (c j : C) :
    ‚Ñå.coev_app.app c ‚â´ ‚Ñå.œÄ c j =
    MonoidalClosed.curry ((DayConvolution.unit F G).app (j, c)) := by
  dsimp [coev_app]
  rw [‚Üê Wedge.mk_Œπ
      (F := dayConvolutionInternalHomDiagramFunctor F|>.obj _|>.obj c)
      (H.obj c) (‚Ñå.œÄ c) (‚Ñå.hœÄ c),
    Wedge.IsLimit.lift_Œπ]

lemma coev_naturality_app {G' H' : C ‚•§ V} [DayConvolution F G'] (Œ∑ : G ‚ü∂ G')
    (‚Ñå' : DayConvolutionInternalHom F (F ‚äõ G') H') :
    Œ∑ ‚â´ ‚Ñå'.coev_app =
    ‚Ñå.coev_app ‚â´ ‚Ñå.map (DayConvolution.map (ùüô _) Œ∑) ‚Ñå' := by
  ext c
  dsimp
  apply Wedge.IsLimit.hom_ext <| ‚Ñå'.isLimitWedge c
  intro j
  apply MonoidalClosed.uncurry_injective
  dsimp
  simp only [Category.assoc, coev_app_œÄ, Functor.comp_obj, tensor_obj,
    map_app_comp_œÄ, coev_app_œÄ_assoc, MonoidalClosed.uncurry_natural_right,
    MonoidalClosed.uncurry_curry, DayConvolution.unit_app_map_app,
    NatTrans.id_app, id_tensorHom]
  simp [MonoidalClosed.uncurry_natural_left]

end coev

theorem left_triangle_components (G : C ‚•§ V) [DayConvolution F G]
    (‚Ñå : DayConvolutionInternalHom F (F ‚äõ G) H) [DayConvolution F H] :
    DayConvolution.map (ùüô F) ‚Ñå.coev_app ‚â´ ‚Ñå.ev_app = ùüô (F ‚äõ G) := by
  apply DayConvolution.corepresentableBy F G|>.homEquiv.injective
  dsimp
  ext ‚ü®x, y‚ü©
  apply MonoidalClosed.curry_injective
  simp [MonoidalClosed.curry_natural_left]

theorem right_triangle_components (G : C ‚•§ V) [DayConvolution F H]
    (‚Ñå : DayConvolutionInternalHom F G H) {H' : C ‚•§ V}
    (‚Ñå' : DayConvolutionInternalHom F (F ‚äõ H) H') :
    ‚Ñå'.coev_app ‚â´ ‚Ñå'.map ‚Ñå.ev_app ‚Ñå = ùüô H := by
  ext c
  apply Wedge.IsLimit.hom_ext <| ‚Ñå.isLimitWedge c
  intro j
  apply MonoidalClosed.uncurry_injective
  simp [MonoidalClosed.uncurry_natural_right]

end DayConvolutionInternalHom

end

end CategoryTheory.MonoidalCategory
