/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.Monoidal.DayConvolution
import Mathlib.CategoryTheory.Monoidal.Closed.Basic
import Mathlib.CategoryTheory.Limits.Shapes.End

/-! # Internal homs for day convolution

Given a category `V` that is monoidal closed, a category `C` that
is monoidal, a functor `C â¥¤ V`, and given the data of suitable day convolutions
and suitable ends of profunctors `c câ‚ câ‚‚ â†¦ ihom (F câ‚) (Â·.obj (câ‚‚ âŠ— c))`,
we prove that the data of the units of the left Kan extensions that define
day convolutions and the data of the canonical morphisms to the aforementioned
ends can be organised as data that exhibit `F` as monoidal closed in `C â¥¤ V` for
the Day convolution monoidal structure.

## TODOs
* When `LawfulDayConvolutionMonoidalStruct` (https://github.com/leanprover-community/mathlib4/issues/26820) lands, transport the
  constructions here to produce actual `CategoryTheory.MonoidalClosed` instances.
-/

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

namespace CategoryTheory.MonoidalCategory
open scoped ExternalProduct
open Opposite Limits

noncomputable section

variable {C : Type uâ‚} [Category.{vâ‚} C] {V : Type uâ‚‚} [Category.{vâ‚‚} V]
  [MonoidalCategory C] [MonoidalCategory V] [MonoidalClosed V]

/-- Given `F : C â¥¤ V`, this is the functor
`G â†¦ c câ‚ câ‚‚ â†¦ ihom (F câ‚) (G.obj (câ‚‚ âŠ— c))`.
The internal hom functor for Day convolution `[F, -]` is naturally isomorphic
to the functor `G â†¦ c â†¦ end_ (câ‚ câ‚‚ â†¦ ihom (F câ‚) (G.obj (câ‚‚ âŠ— c)))`, hence
this definition. -/
@[simps!]
def dayConvolutionInternalHomDiagramFunctor (F : C â¥¤ V) :
    (C â¥¤ V) â¥¤ C â¥¤ Cáµ’áµ– â¥¤ C â¥¤ V where
  obj G :=
    { obj c := Functor.whiskeringLeftâ‚‚ _|>.obj F.op|>.obj
        (tensorRight c â‹™ G)|>.obj MonoidalClosed.internalHom
      map {c c'} f := Functor.whiskeringLeftâ‚‚ _|>.obj F.op|>.map
        (Functor.whiskerRight (curriedTensor C|>.flip.map f) G)|>.app
          MonoidalClosed.internalHom }
  map {G G'} Î· :=
    { app c := Functor.whiskeringLeftâ‚‚ _|>.obj F.op|>.map
        (Functor.whiskerLeft _ Î·) |>.app MonoidalClosed.internalHom
      naturality {c c'} f := by
        ext j k
        dsimp
        simpa [-NatTrans.naturality] using
          congr_arg (ihom <| F.obj <| unop j).map (Î·.naturality <| k â— f) }

/-- `DayConvolutionInternalHom F G H` asserts that `H` is the value at `G` of
an internal hom functor of `F` for the Day convolution monoidal structure.
This is phrased as the data of a limit `CategoryTheory.Wedge`
(i.e an end) on `internalHomDiagramFunctor F|>.obj G|>.obj c` and
`c`, with tip `(H.obj G).obj c` and a compatibility condition asserting that
the functoriality of `H` identifies to the functoriality of ends. -/
structure DayConvolutionInternalHom (F : C â¥¤ V) (G : C â¥¤ V) (H : C â¥¤ V) where
  /-- The canonical projections maps -/
  Ï€ (c j : C) : H.obj c âŸ¶ (ihom <| F.obj j).obj (G.obj <| j âŠ— c)
  /-- The projections maps assemble into a wedge. -/
  hÏ€ (c : C) â¦ƒi j : Câ¦„ (f : i âŸ¶ j) :
    Ï€ c i â‰« (ihom (F.obj i)).map (G.map <| f â–· c) =
    Ï€ c j â‰« (MonoidalClosed.pre <| F.map f).app (G.obj <| j âŠ— c)
  /-- The wedge defined by `Ï€` and `hÏ€` is a limit wedge, i.e `H.obj c` is
  an end of `internalHomDiagramFunctor F G|>.obj c`. -/
  isLimitWedge (c : C) :
    IsLimit <| Wedge.mk
      (F := dayConvolutionInternalHomDiagramFunctor F|>.obj G|>.obj c)
      (H.obj c) (Ï€ c) (hÏ€ c)
  /-- The functoriality of `H.obj G` identifies (through
  `Wedge.IsLimit.hom_ext`) with the functoriality on ends induced by
  functoriality of `internalHomDiagramFunctor F|>.obj G`. -/
  map_comp_Ï€ {c c' : C} (f : c âŸ¶ c') (j : C) :
    H.map f â‰« Ï€ c' j = Ï€ c j â‰« (ihom <| F.obj j).map (G.map <| j â— f)

namespace DayConvolutionInternalHom

open scoped DayConvolution

attribute [reassoc (attr := simp)] map_comp_Ï€
attribute [reassoc] hÏ€

variable {F : C â¥¤ V} {G : C â¥¤ V} {H : C â¥¤ V}

/-- If we have a map `G âŸ¶ G'` and a `DayConvolutionInternalHom F G' H'`, then
there is a unique map `H âŸ¶ H'` induced by functoriality of ends and functoriality
of `internalHomDiagramFunctor F`. -/
def map (â„Œ : DayConvolutionInternalHom F G H) {G' : C â¥¤ V} {H' : C â¥¤ V}
    (f : G âŸ¶ G') (â„Œ' : DayConvolutionInternalHom F G' H') :
    H âŸ¶ H' where
  app c := Wedge.IsLimit.lift (â„Œ'.isLimitWedge c)
    (fun j â†¦ (â„Œ.Ï€ c j) â‰«
      (dayConvolutionInternalHomDiagramFunctor
        F|>.map f|>.app c|>.app (op j)|>.app j))
    (fun â¦ƒj j'â¦„ Ï† â†¦ by
      have := congrArg (fun t â†¦ t.app j') <|
        dayConvolutionInternalHomDiagramFunctor
          F|>.map f|>.app c|>.naturality Ï†.op
      dsimp at this âŠ¢
      rw [Category.assoc, â† (ihom (F.obj j)).map_comp, â† f.naturality,
        Functor.map_comp, reassoc_of% â„Œ.hÏ€]
      simp)
  naturality {c c'} f := by
    apply Wedge.IsLimit.hom_ext (â„Œ'.isLimitWedge c')
    intro j
    dsimp
    simp only [Category.assoc, map_comp_Ï€]
    rw [â† Wedge.mk_Î¹
        (F := dayConvolutionInternalHomDiagramFunctor F|>.obj _|>.obj c')
        (H'.obj c') (â„Œ'.Ï€ c') (â„Œ'.hÏ€ c'),
      â† Wedge.mk_Î¹
        (F := dayConvolutionInternalHomDiagramFunctor F|>.obj _|>.obj c)
        (H'.obj c) (â„Œ'.Ï€ c) (â„Œ'.hÏ€ c),
      Wedge.IsLimit.lift_Î¹ (â„Œ'.isLimitWedge c'),
      Wedge.IsLimit.lift_Î¹_assoc (â„Œ'.isLimitWedge c) ]
    simp [â† Functor.map_comp]

@[reassoc (attr := simp)]
lemma map_app_comp_Ï€ (â„Œ : DayConvolutionInternalHom F G H)
    {G' : C â¥¤ V} {H' : C â¥¤ V} (f : G âŸ¶ G')
    (â„Œ' : DayConvolutionInternalHom F G' H') (c : C) (j : C) :
    (â„Œ.map f â„Œ').app c â‰« â„Œ'.Ï€ c j =
    â„Œ.Ï€ c j â‰« (ihom <| F.obj j).map (f.app <| j âŠ— c) := by
  dsimp [map]
  rw [â† Wedge.mk_Î¹
      (F := dayConvolutionInternalHomDiagramFunctor F|>.obj _|>.obj c)
      (H'.obj c) (â„Œ'.Ï€ c) (â„Œ'.hÏ€ c),
    Wedge.IsLimit.lift_Î¹ (â„Œ'.isLimitWedge c)]

section ev

variable [DayConvolution F H] (â„Œ : DayConvolutionInternalHom F G H)

/-- Given `â„Œ : DayConvolutionInternalHom F H`, if we think of `H.obj G`
as the internal hom `[F, G]`, then this is the transformation
corresponding to the component at `G` of the "evaluation" natural morphism
`F âŠ› [F, _] âŸ¶ ğŸ­`. -/
def ev_app : F âŠ› H âŸ¶ G :=
  DayConvolution.corepresentableBy F H|>.homEquiv.symm <|
    { app x := MonoidalClosed.uncurry <| â„Œ.Ï€ x.2 x.1
      naturality {x y} f := by
        have := congrArg (fun t â†¦ F.obj x.1 â— t) <| â„Œ.hÏ€ x.2 f.1
        dsimp at this âŠ¢
        simp only [whiskerLeft_comp] at this
        simp only [Category.assoc, MonoidalClosed.uncurry_eq, Functor.id_obj,
          â† whiskerLeft_comp_assoc, map_comp_Ï€]
        simp only [whiskerLeft_comp, Category.assoc, ihom.ev_naturality,
          Functor.comp_obj, curriedTensor_obj_obj, Functor.id_obj,
          â† whisker_exchange_assoc, tensorHom_def, Functor.map_comp,
          â† ihom.ev_naturality_assoc]
        rw [reassoc_of% this]
        simp }

@[reassoc (attr := simp)]
lemma unit_app_ev_app_app (x y : C) :
    ((DayConvolution.unit F H).app (x, y) â‰« (â„Œ.ev_app).app (x âŠ— y)) =
    MonoidalClosed.uncurry (â„Œ.Ï€ y x) := by
  have := Functor.descOfIsLeftKanExtension_fac_app (F âŠ› H)
    (DayConvolution.unit F H) G
  dsimp at this
  simp [this, ev_app]

lemma ev_naturality_app {G' H' : C â¥¤ V} (â„Œ' : DayConvolutionInternalHom F G' H')
    [DayConvolution F H'] (Î· : G âŸ¶ G') :
    DayConvolution.map (ğŸ™ F) (â„Œ.map Î· â„Œ') â‰« â„Œ'.ev_app = â„Œ.ev_app â‰« Î· := by
  apply DayConvolution.corepresentableBy F H|>.homEquiv.injective
  dsimp
  ext âŸ¨x, yâŸ©
  simp [MonoidalClosed.uncurry_eq, â† whiskerLeft_comp_assoc]

end ev

section coev

variable {G : C â¥¤ V} [DayConvolution F G]
    (â„Œ : DayConvolutionInternalHom F (F âŠ› G) H)

/-- Given `â„Œ : DayConvolutionInternalHom F H`, if we think of `H.obj G`
as the internal hom `[F, G]`, then this is the transformation
corresponding to the component at `G` of the "coevaluation" natural morphism
`ğŸ­ âŸ¶ [F, F âŠ› _]`. -/
def coev_app : G âŸ¶ H where
  app c :=
    Wedge.IsLimit.lift (â„Œ.isLimitWedge c)
      (fun c' => MonoidalClosed.curry <|
        (DayConvolution.unit F G).app (c', c))
        (fun {c' c''} f => by
          have := DayConvolution.unit_naturality F G f (ğŸ™ c)
          simp only [Functor.map_id, tensorHom_id] at this
          replace this := congrArg MonoidalClosed.curry this
          simp only [MonoidalClosed.curry_natural_right] at this
          dsimp
          rw [â† this]
          simp [MonoidalClosed.curry_eq])
  naturality {c c'} f := by
    dsimp
    apply Wedge.IsLimit.hom_ext <| â„Œ.isLimitWedge c'
    intro (j : C)
    simp only [multicospanIndexEnd_left,
      dayConvolutionInternalHomDiagramFunctor_obj_obj_obj_obj, Multifork.ofÎ¹_pt,
      Wedge.mk_Î¹, Category.assoc, map_comp_Ï€]
    rw [â† Wedge.mk_Î¹
        (F := dayConvolutionInternalHomDiagramFunctor F|>.obj _|>.obj c)
        (H.obj c) (â„Œ.Ï€ c) (â„Œ.hÏ€ c),
      â† Wedge.mk_Î¹
        (F := dayConvolutionInternalHomDiagramFunctor F|>.obj _|>.obj c')
        (H.obj c') (â„Œ.Ï€ c') (â„Œ.hÏ€ c'),
      Wedge.IsLimit.lift_Î¹_assoc, Wedge.IsLimit.lift_Î¹]
    have := DayConvolution.unit_naturality F G (ğŸ™ j) f
    simp only [Functor.map_id, id_tensorHom] at this
    replace this := congrArg MonoidalClosed.curry this
    simp only [MonoidalClosed.curry_natural_right] at this
    rw [â† this]
    simp [MonoidalClosed.curry_eq]

@[reassoc (attr := simp)]
lemma coev_app_Ï€ (c j : C) :
    â„Œ.coev_app.app c â‰« â„Œ.Ï€ c j =
    MonoidalClosed.curry ((DayConvolution.unit F G).app (j, c)) := by
  dsimp [coev_app]
  rw [â† Wedge.mk_Î¹
      (F := dayConvolutionInternalHomDiagramFunctor F|>.obj _|>.obj c)
      (H.obj c) (â„Œ.Ï€ c) (â„Œ.hÏ€ c),
    Wedge.IsLimit.lift_Î¹]

lemma coev_naturality_app {G' H' : C â¥¤ V} [DayConvolution F G'] (Î· : G âŸ¶ G')
    (â„Œ' : DayConvolutionInternalHom F (F âŠ› G') H') :
    Î· â‰« â„Œ'.coev_app =
    â„Œ.coev_app â‰« â„Œ.map (DayConvolution.map (ğŸ™ _) Î·) â„Œ' := by
  ext c
  dsimp
  apply Wedge.IsLimit.hom_ext <| â„Œ'.isLimitWedge c
  intro j
  apply MonoidalClosed.uncurry_injective
  dsimp
  simp only [Category.assoc, coev_app_Ï€, Functor.comp_obj, tensor_obj,
    map_app_comp_Ï€, coev_app_Ï€_assoc, MonoidalClosed.uncurry_natural_right,
    MonoidalClosed.uncurry_curry, DayConvolution.unit_app_map_app,
    NatTrans.id_app, id_tensorHom]
  simp [MonoidalClosed.uncurry_natural_left]

end coev

theorem left_triangle_components (G : C â¥¤ V) [DayConvolution F G]
    (â„Œ : DayConvolutionInternalHom F (F âŠ› G) H) [DayConvolution F H] :
    DayConvolution.map (ğŸ™ F) â„Œ.coev_app â‰« â„Œ.ev_app = ğŸ™ (F âŠ› G) := by
  apply DayConvolution.corepresentableBy F G|>.homEquiv.injective
  dsimp
  ext âŸ¨x, yâŸ©
  apply MonoidalClosed.curry_injective
  simp [MonoidalClosed.curry_natural_left]

theorem right_triangle_components (G : C â¥¤ V) [DayConvolution F H]
    (â„Œ : DayConvolutionInternalHom F G H) {H' : C â¥¤ V}
    (â„Œ' : DayConvolutionInternalHom F (F âŠ› H) H') :
    â„Œ'.coev_app â‰« â„Œ'.map â„Œ.ev_app â„Œ = ğŸ™ H := by
  ext c
  apply Wedge.IsLimit.hom_ext <| â„Œ.isLimitWedge c
  intro j
  apply MonoidalClosed.uncurry_injective
  simp [MonoidalClosed.uncurry_natural_right]

end DayConvolutionInternalHom

end

end CategoryTheory.MonoidalCategory
