/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.Monoidal.DayConvolution.DayFunctor
import Mathlib.CategoryTheory.Closed.Types
import Mathlib.CategoryTheory.Monoidal.Limits.Preserves

/-!
# Day functors to type

In this file, we study day functors `Cáµ’áµ– âŠ›â¥¤ Type uâ‚`, i.e presheaves
with the Day convolution monoidal structure when `C` is an `uâ‚`-small category.
Such restriction on size is necessary in order to obtain existence of
relevant Kan extensions in `Type uâ‚`.


## Implementation

The way we state and prove monoidality of the Yoneda embedding is by registering
a `LawfulDayConvolutionMonoidalCategoryStruct Cáµ’áµ– (Type vâ‚) C`. An actual
`Functor.Monoidal` instance on `yoneda â‹™ (DayFunctor.equiv Cáµ’áµ– (Type vâ‚))` will
then follow from the general fact that whenever there is a
`LawfulDayConvolutionMonoidalCategoryStruct C V D`, the induced functor
`D â¥¤ (C âŠ›â¥¤ V)` is monoidal.

## TODOs
- Construct an explicit model of the internal hom in terms of the
Yoneda embedding.
- Show that left Kan extension along Yoneda of a monoidal functor
remains monoidal.
-/

universe vâ‚ uâ‚

namespace CategoryTheory.MonoidalCategory.DayFunctor
open scoped ExternalProduct

section

variable (C : Type uâ‚) [Category.{vâ‚} C] [MonoidalCategory C]

open Opposite

@[simps! Î¹ convolutionExtensionUnit unitUnit]
instance : LawfulDayConvolutionMonoidalCategoryStruct Cáµ’áµ– (Type vâ‚) C where
  Î¹ := yoneda
  convolutionExtensionUnit c c' := { app X := fun Y â†¦ Y.1 âŠ—â‚˜ Y.2 }
  unitUnit := fun _ â†¦ ğŸ™ _
  isPointwiseLeftKanExtensionConvolutionExtensionUnit c c' c'' :=
    { desc s v := s.Î¹.app
        (CostructuredArrow.mk (Y := (op c, op c')) v.op)
        (ğŸ™ _, ğŸ™ _)
      fac s j := by
        ext âŸ¨t, t'âŸ©
        let u :
            (CostructuredArrow.mk (Y := (op c, op c'))
              ((t.op âŠ—â‚˜ t'.op) â‰« j.hom)) âŸ¶ j :=
          CostructuredArrow.homMk (t.op, t'.op)
        haveI := congr_arg (fun t â†¦ t (ğŸ™ c, ğŸ™ c')) (s.w u)
        dsimp at this
        simpa using this.symm
      uniq s m hm := by
        ext x
        simpa using congr_arg (fun t â†¦ t (ğŸ™ c, ğŸ™ c')) <|
          hm (CostructuredArrow.mk (Y := (op c, op c')) x.op) }
  isPointwiseLeftKanExtensionUnitUnit c :=
    { desc s v := s.Î¹.app (CostructuredArrow.mk (Y := default) v.op) PUnit.unit
      fac s j := by
        dsimp
        let u : CostructuredArrow.mk (Y := default) (ğŸ™ (ğŸ™_ Cáµ’áµ–) â‰« j.hom) âŸ¶ j :=
          CostructuredArrow.homMk (Discrete.eqToHom rfl)
        simpa using (s.w u).symm
      uniq s m hm := by
        ext x
        simpa using congr_fun
          (hm (CostructuredArrow.mk (Y := default) x.op)) PUnit.unit }
  convolutionExtensionUnit_comp_Î¹_map_tensorHom_app := by aesop_cat
  convolutionExtensionUnit_comp_Î¹_map_whiskerLeft_app _ _ _ _ _ _ := by
    ext âŸ¨t, t'âŸ©
    simp [tensorHom_def]
  convolutionExtensionUnit_comp_Î¹_map_whiskerRight_app _ _ _ _ := by
    ext âŸ¨t, t'âŸ©
    simp [tensorHom_def']
  associator_hom_unit_unit _ _ _ _ _ _ := by
    ext âŸ¨âŸ¨t, t'âŸ©, t''âŸ©
    simp
  leftUnitor_hom_unit_app _ _ := by
    ext âŸ¨_, _âŸ©
    simp
  rightUnitor_hom_unit_app _ _ := by
    ext âŸ¨_, _âŸ©
    simp

/-- An abbreviation for the "monoidal" version of Yoneda, taking
values in Day functors rather than presheaves. -/
@[simps!]
def dayYoneda : C â¥¤ (Cáµ’áµ– âŠ›â¥¤ Type vâ‚) := yoneda â‹™ (equiv Cáµ’áµ– _).inverse

end

section SmallCategory

variable (C : Type uâ‚) [SmallCategory C] [MonoidalCategory C]

-- We need to help type class resolution here.
local instance : âˆ€ (v : Type uâ‚),
    Limits.PreservesColimitsOfSize.{uâ‚, uâ‚} (tensorRight v) := fun _ â†¦
  âŸ¨âŸ¨Limits.preservesColimit_of_braided_and_preservesColimit_tensor_left _ _âŸ©âŸ©

local instance : âˆ€ (v : Type uâ‚),
    Limits.PreservesColimitsOfSize.{0, uâ‚} (tensorRight v) := fun _ â†¦
  âŸ¨âŸ¨Limits.preservesColimit_of_braided_and_preservesColimit_tensor_left _ _âŸ©âŸ©

open LawfulDayConvolutionMonoidalCategoryStruct DayFunctor in
noncomputable instance : (dayYoneda C).Monoidal :=
  inferInstanceAs <| (Î¹ Cáµ’áµ– (Type uâ‚) C â‹™ (equiv _ _).inverse).Monoidal

section lemmas

open Opposite Functor LawfulDayConvolutionMonoidalCategoryStruct
open scoped Prod

variable {C}

@[reassoc]
lemma Î·_dayYoneda_Î¼ (c c' : C) (x y : Cáµ’áµ–) :
    (Î· (dayYoneda C|>.obj c) (dayYoneda C|>.obj c')).app (x, y) â‰«
    (LaxMonoidal.Î¼ (dayYoneda C) c c').natTrans.app (x âŠ— y) =
    (convolutionExtensionUnit Cáµ’áµ– (Type uâ‚) c c').app (x, y) :=
  Î·_Î¹_equivInverse_Î¼ Cáµ’áµ– (Type uâ‚) c c' _ _

@[simp]
lemma Î·_dayYoneda_Î¼_apply (c c' : C) (x y : Cáµ’áµ–)
    (fâ‚ : unop x âŸ¶ c) (fâ‚‚ : unop y âŸ¶ c') :
    (LaxMonoidal.Î¼ (dayYoneda C) c c').natTrans.app (x âŠ— y)
      ((Î· (dayYoneda C|>.obj c) (dayYoneda C|>.obj c')).app (x, y)
        (fâ‚ Ã—â‚˜ fâ‚‚)) =
    fâ‚ âŠ—â‚˜ fâ‚‚ :=
  congr_fun (Î·_Î¹_equivInverse_Î¼ Cáµ’áµ– (Type uâ‚) c c' _ _) (fâ‚ Ã—â‚˜ fâ‚‚)

@[reassoc]
lemma convolutionExtensionUnit_dayYoneda_Î´ (c c' : C) (x y : Cáµ’áµ–) :
    (convolutionExtensionUnit Cáµ’áµ– (Type uâ‚) c c').app (x, y) â‰«
    (OplaxMonoidal.Î´ (dayYoneda C) c c').natTrans.app (x âŠ— y) =
  (Î· (dayYoneda C|>.obj c) (dayYoneda C|>.obj c')).app (x, y) :=
  convolutionExtensionUnit_Î¹_equivInverse_Î´ Cáµ’áµ– (Type uâ‚) c c' _ _

@[simp]
lemma convolutionExtensionUnit_dayYoneda_Î´_apply (c c' : C) (x y : Cáµ’áµ–)
    (fâ‚ : unop x âŸ¶ c) (fâ‚‚ : unop y âŸ¶ c') :
    (OplaxMonoidal.Î´ (dayYoneda C) c c').natTrans.app (x âŠ— y) (fâ‚ âŠ—â‚˜ fâ‚‚) =
    (Î· (dayYoneda C|>.obj c) (dayYoneda C|>.obj c')).app (x, y) (fâ‚ Ã—â‚˜ fâ‚‚) :=
  congr_fun (convolutionExtensionUnit_Î¹_equivInverse_Î´ Cáµ’áµ– (Type uâ‚) c c' _ _)
    (fâ‚ Ã—â‚˜ fâ‚‚)

variable (C)

@[reassoc]
lemma Î½_dayYoneda_Îµ :
    Î½ Cáµ’áµ– (Type uâ‚) â‰« (LaxMonoidal.Îµ (dayYoneda C)).natTrans.app (ğŸ™_ Cáµ’áµ–) =
    unitUnit Cáµ’áµ– (Type uâ‚) C :=
  Î½_Î¹_equivInverse_Îµ Cáµ’áµ– (Type uâ‚) _

@[simp]
lemma Î½_dayYoneda_Îµ_punit :
  (LaxMonoidal.Îµ (dayYoneda C)).natTrans.app (ğŸ™_ Cáµ’áµ–)
    (Î½ Cáµ’áµ– (Type uâ‚) PUnit.unit) = ğŸ™ _ :=
  congr_fun (Î½_Î¹_equivInverse_Îµ Cáµ’áµ– (Type uâ‚) _) PUnit.unit

@[reassoc]
lemma Î½_dayYoneda_Î· :
    unitUnit Cáµ’áµ– (Type uâ‚) C â‰«
      (OplaxMonoidal.Î· (dayYoneda C)).natTrans.app (ğŸ™_ Cáµ’áµ–) =
    Î½ Cáµ’áµ– (Type uâ‚) :=
  Î½_Î¹_equivInverse_Î· Cáµ’áµ– (Type uâ‚) _

@[simp]
lemma Î½_dayYoneda_Î·_punit :
    (OplaxMonoidal.Î· (dayYoneda C)).natTrans.app (ğŸ™_ Cáµ’áµ–) (ğŸ™ (ğŸ™_ C)) =
    Î½ Cáµ’áµ– (Type uâ‚) PUnit.unit :=
  congr_fun (Î½_Î¹_equivInverse_Î· Cáµ’áµ– (Type uâ‚) _) PUnit.unit

end lemmas

end SmallCategory

end CategoryTheory.MonoidalCategory.DayFunctor
