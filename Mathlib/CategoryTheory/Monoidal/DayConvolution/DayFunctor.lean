/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.Monoidal.DayConvolution

/-!
# Day functors

In this file, given a monoidal category `C` and a monoidal category `V`,
we define a basic type synonym `DayFunctor C V` (denoted `C âŠ›â¥¤ D`)
for the category `C â¥¤ V` and endow it with the monoidal structure coming
from Day convolution. Such a setup is necessary as by default,
the `MonoidalCategory` instance on `C â¥¤ V` is the "pointwise" one,
where the tensor product of `F` and `G` is the functor `x â†¦ F.obj x âŠ— G.obj x`.

## TODOs
- Given a `LawfulDayConvolutionMonoidalCategoryStruct C V D`, show that
Î¹ induce a monoidal functor `D â¥¤ (C âŠ›â¥¤ V)`.
- Specialize to the case `V := Type _`, and prove a universal property stating
that for every monoidal category `W` with suitable colimits,
colimit-preserving monoidal functors `(Cáµ’áµ– âŠ›â¥¤ Type u) â¥¤ W` are equivalent to
to monoidal functors `C â¥¤ W`. Show that the Yoneda embedding is monoidal.
-/

universe vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

namespace CategoryTheory.MonoidalCategory
open scoped ExternalProduct

noncomputable section

/-- `DayFunctor C V` is a type synonym for `C â¥¤ V`, implemented as a one-field
structure. -/
structure DayFunctor
    (C : Type uâ‚) [Category.{vâ‚} C] (V : Type uâ‚‚) [Category.{vâ‚‚} V]
    [MonoidalCategory C] [MonoidalCategory V] where
  /-- the underlying functor. -/
  functor : C â¥¤ V

namespace DayFunctor

/-- Notation for `DayFunctor`. -/
scoped infixr:26 " âŠ›â¥¤ " => DayFunctor

variable {C : Type uâ‚} [Category.{vâ‚} C] {V : Type uâ‚‚} [Category.{vâ‚‚} V]
    [MonoidalCategory C] [MonoidalCategory V]

lemma mk_functor (F : C â¥¤ V) : (mk F).functor = F := rfl

@[simp]
lemma functor_mk (F : C âŠ›â¥¤ V) : mk F.functor = F := rfl

/-- Morphisms of Day functors are natural transformations of the underlying
functors. -/
structure Hom (F G : C âŠ›â¥¤ V) where
  /-- the underlying natural transformation -/
  natTrans : F.functor âŸ¶ G.functor

@[simps id_natTrans comp_natTrans]
instance : Category (C âŠ›â¥¤ V) where
  Hom := Hom
  id x := .mk <| ğŸ™ x.functor
  comp Î± Î² := .mk <| Î±.natTrans â‰« Î².natTrans

@[ext]
lemma hom_ext {F G : C âŠ›â¥¤ V} {Î± Î² : F âŸ¶ G} (h : Î±.natTrans = Î².natTrans) :
    Î± = Î² := by
  cases Î±
  cases Î²
  grind

variable (C V) in
/-- The tautological equivalence of categories between `C â¥¤ V` and `C âŠ›â¥¤ V`. -/
@[simps! functor_obj functor_map inverse_obj_functor inverse_map_natTrans
  unitIso_hom_app unitIso_inv_app counitIso_hom_app counitIso_inv_app]
def equiv : (C âŠ›â¥¤ V) â‰Œ (C â¥¤ V) where
  functor :=
    { obj F := F.functor
      map Î± := Î±.natTrans }
  inverse :=
    { obj F := .mk F
      map Î± := .mk Î± }
  unitIso := .refl _
  counitIso := .refl _

variable
    [hasDayConvolution : âˆ€ (F G : C â¥¤ V),
      (tensor C).HasPointwiseLeftKanExtension (F âŠ  G)]
    [hasDayConvolutionUnit :
      (Functor.fromPUnit.{0} <| ğŸ™_ C).HasPointwiseLeftKanExtension
        (Functor.fromPUnit.{0} <| ğŸ™_ V)]
    [âˆ€ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (tensor C) d) (tensorLeft v)]
    [âˆ€ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (tensor C) d) (tensorRight v)]
    [âˆ€ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (Functor.fromPUnit.{0} <| ğŸ™_ C) d) (tensorLeft v)]
    [âˆ€ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (Functor.fromPUnit.{0} <| ğŸ™_ C) d) (tensorRight v)]
    [âˆ€ (v : V) (d : C Ã— C),
      Limits.PreservesColimitsOfShape
        (CostructuredArrow ((ğŸ­ C).prod <| Functor.fromPUnit.{0} <| ğŸ™_ C) d)
        (tensorRight v)]
    [âˆ€ (v : V) (d : C Ã— C),
      Limits.PreservesColimitsOfShape
        (CostructuredArrow ((tensor C).prod (ğŸ­ C)) d) (tensorRight v)]

instance : MonoidalCategory (C âŠ›â¥¤ V) :=
  monoidalOfHasDayConvolutions
    (equiv C V).functor
    (equiv C V).fullyFaithfulFunctor
    (fun _ _ => âŸ¨_, âŸ¨equiv C V|>.counitIso.app _âŸ©âŸ©)
    âŸ¨_, âŸ¨equiv C V|>.counitIso.app _âŸ©âŸ©

@[simps! Î¹_obj Î¹_map]
instance : LawfulDayConvolutionMonoidalCategoryStruct C V (C âŠ›â¥¤ V) :=
  lawfulDayConvolutionMonoidalCategoryStructOfHasDayConvolutions
    (equiv C V).functor
    (equiv C V).fullyFaithfulFunctor
    (fun _ _ => âŸ¨_, âŸ¨equiv C V|>.counitIso.app _âŸ©âŸ©)
    âŸ¨_, âŸ¨equiv C V|>.counitIso.app _âŸ©âŸ©

/-- The unit transformation exhibiting `(F âŠ— G).functor` as a left Kan extension of
`F.functor âŠ  G.functor` along `tensor C`. -/
def Î· (F G : C âŠ›â¥¤ V) :
    F.functor âŠ  G.functor âŸ¶ tensor C â‹™ (F âŠ— G).functor :=
  LawfulDayConvolutionMonoidalCategoryStruct.convolutionExtensionUnit
    C V F G

open LawfulDayConvolutionMonoidalCategoryStruct in
instance (F G : C âŠ›â¥¤ V) : (F âŠ— G).functor.IsLeftKanExtension (Î· F G) :=
  (isPointwiseLeftKanExtensionConvolutionExtensionUnit F G).isLeftKanExtension

open LawfulDayConvolutionMonoidalCategoryStruct in
theorem tensor_hom_ext {F G H : C âŠ›â¥¤ V} {Î± Î² : F âŠ— G âŸ¶ H}
    (h : âˆ€ (x y : C),
      (Î· F G).app (x, y) â‰« Î±.natTrans.app (x âŠ— y) =
      (Î· F G).app (x, y) â‰« Î².natTrans.app (x âŠ— y)) :
    Î± = Î² := by
  ext : 1
  apply Functor.hom_ext_of_isLeftKanExtension
    (F âŠ— G).functor (Î· F G) _
  ext âŸ¨x, yâŸ©
  exact h x y

/-- A natural transformation `F.functor âŠ  G.functor âŸ¶ tensor C â‹™ H.functor`
defines a morphism `F â¨‚ G âŸ¶ H`. -/
def tensorDesc {F G H : C âŠ›â¥¤ V}
    (Î± : F.functor âŠ  G.functor âŸ¶ tensor C â‹™ H.functor) :
    F âŠ— G âŸ¶ H :=
  .mk <| (F âŠ— G).functor.descOfIsLeftKanExtension (Î· F G) H.functor Î±

lemma Î·_comp_tensorDec {F G H : C âŠ›â¥¤ V}
    (Î± : F.functor âŠ  G.functor âŸ¶ tensor C â‹™ H.functor) :
   Î· F G â‰« Functor.whiskerLeft _ (tensorDesc Î±).natTrans = Î± :=
  Functor.descOfIsLeftKanExtension_fac _ _ _ _

@[reassoc (attr := simp)]
lemma Î·_comp_tensorDesc_app {F G H : C âŠ›â¥¤ V}
    (Î± : F.functor âŠ  G.functor âŸ¶ tensor C â‹™ H.functor) (x y : C) :
   (Î· F G).app (x , y) â‰« (tensorDesc Î±).natTrans.app (x âŠ— y) = Î±.app (x, y) :=
  Functor.descOfIsLeftKanExtension_fac_app _ _ _ _ _

open LawfulDayConvolutionMonoidalCategoryStruct
/-- An abstract isomorphism between `(F âŠ— G).functor` and the generic pointwise
left Kan extension of `F.functor âŠ  G.functor` along the -/
def isoPointwiseLeftKanExtension (F G : C âŠ›â¥¤ V) :
    (F âŠ— G).functor â‰…
    (tensor C).pointwiseLeftKanExtension (F.functor âŠ  G.functor) :=
  Functor.leftKanExtensionUnique
    (F âŠ— G).functor (Î· F G) _
    ((tensor C).pointwiseLeftKanExtensionUnit (F.functor âŠ  G.functor))

@[simp]
lemma Î·_comp_isoPointwiseLeftKanExtension_hom (F G : C âŠ›â¥¤ V) (x y : C) :
    (Î· F G).app (x, y) â‰« (isoPointwiseLeftKanExtension F G).hom.app (x âŠ— y) =
    Limits.colimit.Î¹
      (CostructuredArrow.proj (tensor C) (x âŠ— y) â‹™ F.functor âŠ  G.functor)
      (.mk (Y := (x, y)) <| ğŸ™ (x âŠ— y)) := by
  simpa [Î·, isoPointwiseLeftKanExtension] using
    Functor.descOfIsLeftKanExtension_fac_app
      (F âŠ— G).functor (Î· F G) _
      ((tensor C).pointwiseLeftKanExtensionUnit (F.functor âŠ  G.functor)) (x, y)

@[simp]
lemma Î¹_comp_isoPointwiseLeftKanExtension_inv (F G : C âŠ›â¥¤ V) (x y : C) :
    Limits.colimit.Î¹
      (CostructuredArrow.proj (tensor C) (x âŠ— y) â‹™ F.functor âŠ  G.functor)
        (.mk (Y := (x, y)) <| ğŸ™ (x âŠ— y)) â‰«
      (isoPointwiseLeftKanExtension F G).inv.app (x âŠ— y) =
    (Î· F G).app (x, y) := by
  simp [Î·, isoPointwiseLeftKanExtension]

variable (C V) in
/-- The canonical map `ğŸ™_ V âŸ¶ (ğŸ™_ (C âŠ›â¥¤ V)).functor.obj (ğŸ™_ C)`
that exhibits `(ğŸ™_ (C âŠ›â¥¤ V)).functor` as a Day convolution unit. -/
def Î½ : ğŸ™_ V âŸ¶ (ğŸ™_ (C âŠ›â¥¤ V)).functor.obj (ğŸ™_ C) :=
  LawfulDayConvolutionMonoidalCategoryStruct.unitUnit C V (C âŠ›â¥¤ V)

variable (C V) in
/-- The reinterpretation of `Î½` as a natural transformation. -/
@[simps]
def Î½NatTrans :
    Functor.fromPUnit.{0} (ğŸ™_ V) âŸ¶
      Functor.fromPUnit.{0} (ğŸ™_ C) â‹™ (ğŸ™_ (C âŠ›â¥¤ V)).functor where
  app _ := Î½ C V

open LawfulDayConvolutionMonoidalCategoryStruct in
instance : (ğŸ™_ (C âŠ›â¥¤ V)).functor.IsLeftKanExtension (Î½NatTrans C V) :=
  isPointwiseLeftKanExtensionUnitUnit C V (C âŠ›â¥¤ V)|>.isLeftKanExtension

lemma unit_hom_ext {F : C âŠ›â¥¤ V} {Î± Î² : ğŸ™_ (C âŠ›â¥¤ V) âŸ¶ F}
    (h : Î½ C V â‰« Î±.natTrans.app (ğŸ™_ C) = Î½ C V â‰« Î².natTrans.app (ğŸ™_ C)) :
    Î± = Î² := by
  ext1
  apply Functor.hom_ext_of_isLeftKanExtension
    (ğŸ™_ (C âŠ›â¥¤ V)).functor (Î½NatTrans C V)
  ext
  exact h

/-- Given `F : C âŠ›â¥¤ V`, a morphism `ğŸ™_ V âŸ¶ F.functor.obj (ğŸ™_ C)` induces a
(unique) morphism `ğŸ™_ (C âŠ›â¥¤ V) âŸ¶ F`. -/
def unitDesc {F : C âŠ›â¥¤ V} (Ï† : ğŸ™_ V âŸ¶ F.functor.obj (ğŸ™_ C)) :
    ğŸ™_ (C âŠ›â¥¤ V) âŸ¶ F :=
  .mk <| Functor.descOfIsLeftKanExtension (ğŸ™_ (C âŠ›â¥¤ V)).functor (Î½NatTrans C V)
    F.functor { app _ := Ï† }

@[reassoc (attr := simp)]
lemma Î½_comp_unitDesc {F : C âŠ›â¥¤ V} (Ï† : ğŸ™_ V âŸ¶ F.functor.obj (ğŸ™_ C)) :
    Î½ C V â‰« (unitDesc Ï†).natTrans.app (ğŸ™_ C) = Ï† :=
  Functor.descOfIsLeftKanExtension_fac_app (ğŸ™_ (C âŠ›â¥¤ V)).functor (Î½NatTrans C V)
    F.functor { app _ := Ï† } default

end DayFunctor

end

end CategoryTheory.MonoidalCategory
