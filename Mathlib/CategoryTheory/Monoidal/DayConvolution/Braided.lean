/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.Monoidal.DayConvolution

/-!
# Braidings for Day convolution

In this file, we show that if `C` is a braided monoidal category and
`V` also a braided monoidal category, then the day convolution monoidal structure
on `C ‚•§ V` is also braided monoidal. We prove it by constructing an explicit
braiding isomorphism whenever sufficient day convolutions exist, and we
prove that it satisfies the forward and reverse hexagon identities.

Furthermore, we show that when both `C` and `V` are symmetric monoidal
categories, then the Day convolution monoidal structure is symmetric as well.
-/

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ v‚ÇÑ v‚ÇÖ u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ u‚ÇÖ

namespace CategoryTheory.MonoidalCategory.DayConvolution
open scoped ExternalProduct
open Opposite

noncomputable section

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {V : Type u‚ÇÇ} [Category.{v‚ÇÇ} V]
  [MonoidalCategory C] [BraidedCategory C]
  [MonoidalCategory V] [BraidedCategory V]
  (F G : C ‚•§ V)

section

variable [DayConvolution F G] [DayConvolution G F]

/-- The natural transformation `F ‚ä† G ‚ü∂ (tensor C) ‚ãô (G ‚äõ F)` that corepresents
the braiding morphism `F ‚äõ G ‚ü∂ G ‚äõ F`. -/
@[simps]
def braidingHomCorepresenting : F ‚ä† G ‚ü∂ tensor C ‚ãô G ‚äõ F where
  app _ := (Œ≤_ _ _).hom ‚â´ (unit G F).app (_, _) ‚â´ (G ‚äõ F).map (Œ≤_ _ _).hom
  naturality {x y} f := by simp [tensorHom_def, ‚Üê Functor.map_comp]

/-- The natural transformation `F ‚ä† G ‚ü∂ (tensor C) ‚ãô (G ‚äõ F)` that corepresents
the braiding morphism `F ‚äõ G ‚ü∂ G ‚äõ F`. -/
@[simps]
def braidingInvCorepresenting : G ‚ä† F ‚ü∂ tensor C ‚ãô F ‚äõ G where
  app _ := (Œ≤_ _ _).inv ‚â´ (unit F G).app (_, _) ‚â´ (F ‚äõ G).map (Œ≤_ _ _).inv
  naturality {x y} f := by simp [tensorHom_def, ‚Üê Functor.map_comp]

/-- The braiding isomorphism for Day convolution. -/
def braiding : F ‚äõ G ‚âÖ G ‚äõ F where
  hom := corepresentableBy F G|>.homEquiv.symm <| braidingHomCorepresenting F G
  inv := corepresentableBy G F|>.homEquiv.symm <| braidingInvCorepresenting F G
  hom_inv_id := by
    apply Functor.hom_ext_of_isLeftKanExtension (F ‚äõ G) (unit F G)
    ext
    simp [-tensor_obj]
  inv_hom_id := by
    apply Functor.hom_ext_of_isLeftKanExtension (G ‚äõ F) (unit G F)
    ext
    simp [-tensor_obj]

@[reassoc (attr := simp)]
lemma unit_app_braiding_hom_app (x y : C) :
    (unit F G).app (x, y) ‚â´ (braiding F G).hom.app (x ‚äó y) =
    (Œ≤_ _ _).hom ‚â´ (unit G F).app (_, _) ‚â´ (G ‚äõ F).map (Œ≤_ _ _).hom := by
  change
    (unit F G).app (x, y) ‚â´ (braiding F G).hom.app ((tensor C).obj (x, y)) = _
  simp [braiding, braidingHomCorepresenting, -tensor_obj]

@[reassoc (attr := simp)]
lemma unit_app_braiding_inv_app (x y : C) :
    (unit G F).app (x, y) ‚â´ (braiding F G).inv.app (x ‚äó y) =
    (Œ≤_ _ _).inv ‚â´ (unit F G).app (_, _) ‚â´ (F ‚äõ G).map (Œ≤_ _ _).inv := by
  change
    (unit G F).app (x, y) ‚â´ (braiding F G).inv.app ((tensor C).obj (x, y)) = _
  simp [braiding, braidingHomCorepresenting, -tensor_obj]

end

variable {F G}

@[reassoc (attr := simp)]
lemma braiding_naturality_right (H : C ‚•§ V) (Œ∑ : F ‚ü∂ G)
    [DayConvolution F H] [DayConvolution H F]
    [DayConvolution G H] [DayConvolution H G] :
    DayConvolution.map (ùüô H) Œ∑ ‚â´ (braiding H G).hom =
    (braiding H F).hom ‚â´ DayConvolution.map Œ∑ (ùüô H) := by
  apply Functor.hom_ext_of_isLeftKanExtension (H ‚äõ F) (unit H F)
  ext ‚ü®_, _‚ü©
  simp

@[reassoc (attr := simp)]
lemma braiding_naturality_left (Œ∑ : F ‚ü∂ G) (H : C ‚•§ V)
    [DayConvolution F H] [DayConvolution H F]
    [DayConvolution G H] [DayConvolution H G] :
    DayConvolution.map Œ∑ (ùüô H) ‚â´ (braiding G H).hom =
    (braiding F H).hom ‚â´ DayConvolution.map (ùüô H) Œ∑ := by
  apply Functor.hom_ext_of_isLeftKanExtension (F ‚äõ H) (unit F H)
  ext ‚ü®_, _‚ü©
  simp

variable
  [‚àÄ (v : V) (d : C),
    Limits.PreservesColimitsOfShape (CostructuredArrow (tensor C) d) (tensorLeft v)]
  [‚àÄ (v : V) (d : C),
    Limits.PreservesColimitsOfShape (CostructuredArrow (tensor C) d) (tensorRight v)]

variable (F G) in
lemma hexagon_forward (H : C ‚•§ V)
    [DayConvolution F G] [DayConvolution G H] [DayConvolution F (G ‚äõ H)]
    [DayConvolution (F ‚äõ G) H] [DayConvolution H F] [DayConvolution G (H ‚äõ F)]
    [DayConvolution (G ‚äõ H) F] [DayConvolution G F] [DayConvolution (G ‚äõ F) H]
    [DayConvolution F H] [DayConvolution G (F ‚äõ H)] :
    (associator F G H).hom ‚â´ (braiding F (G ‚äõ H)).hom ‚â´ (associator G H F).hom =
    (DayConvolution.map (braiding F G).hom (ùüô H)) ‚â´ (associator G F H).hom ‚â´
      (DayConvolution.map (ùüô G) (braiding F H).hom) := by
  apply Functor.hom_ext_of_isLeftKanExtension ((F ‚äõ G) ‚äõ H) (unit _ H)
  apply Functor.hom_ext_of_isLeftKanExtension ((F ‚äõ G) ‚ä† H)
    (ExternalProduct.extensionUnitLeft (F ‚äõ G) (unit F G) H)
  ext ‚ü®‚ü®x, y‚ü©, z‚ü©
  dsimp
  simp only [whiskerLeft_id, Category.comp_id, associator_hom_unit_unit_assoc,
    externalProductBifunctor_obj_obj, tensor_obj, NatTrans.naturality_assoc,
    NatTrans.naturality, unit_app_braiding_hom_app_assoc,
    BraidedCategory.braiding_tensor_left_hom, Functor.map_comp, Category.assoc,
    Iso.map_hom_inv_id, BraidedCategory.braiding_naturality_right_assoc,
    BraidedCategory.braiding_tensor_right_hom, Iso.map_inv_hom_id_assoc,
    Iso.inv_hom_id_assoc, Iso.hom_inv_id_assoc, unit_app_map_app_assoc,
    NatTrans.id_app, tensorHom_id]
  simp only [‚Üê comp_whiskerRight_assoc, ‚Üê whiskerLeft_comp_assoc,
    unit_app_braiding_hom_app]
  simp only [whiskerLeft_comp, ‚Üê Functor.map_comp, Category.assoc,
    Functor.comp_obj, tensor_obj, comp_whiskerRight,
    whiskerRight_comp_unit_app_assoc, NatTrans.naturality_assoc,
    NatTrans.naturality, associator_hom_unit_unit_assoc,
    externalProductBifunctor_obj_obj, unit_app_map_app_assoc, NatTrans.id_app,
    id_tensorHom]
  rw [‚Üê BraidedCategory.hexagon_reverse, ‚Üê whiskerLeft_comp_assoc]
  haveI := unit_app_braiding_hom_app F H x z =‚â´ (H ‚äõ F).map (Œ≤_ z x).inv
  dsimp at this
  simp only [Category.assoc, Iso.map_hom_inv_id, Category.comp_id] at this
  rw [‚Üê this, whiskerLeft_comp_assoc]
  simp [‚Üê Functor.map_comp]

variable (F G) in
lemma hexagon_reverse (H : C ‚•§ V)
    [DayConvolution F G] [DayConvolution G H] [DayConvolution F (G ‚äõ H)]
    [DayConvolution (F ‚äõ G) H] [DayConvolution H (F ‚äõ G)] [DayConvolution H F]
    [DayConvolution (H ‚äõ F) G] [DayConvolution H G] [DayConvolution F (H ‚äõ G)]
    [DayConvolution F H] [DayConvolution (F ‚äõ H) G] :
    (associator F G H).inv ‚â´ (braiding (F ‚äõ G) H).hom ‚â´ (associator H F G).inv =
    (DayConvolution.map (ùüô F) (braiding G H).hom) ‚â´ (associator F H G).inv ‚â´
      (DayConvolution.map (braiding F H).hom (ùüô G)) := by
  apply Functor.hom_ext_of_isLeftKanExtension (F ‚äõ G ‚äõ H) (unit _ _)
  apply Functor.hom_ext_of_isLeftKanExtension (F ‚ä† (G ‚äõ H))
    (ExternalProduct.extensionUnitRight (G ‚äõ H) (unit G H) F)
  ext ‚ü®x, y, z‚ü©
  dsimp
  simp only [whiskerRight_tensor, id_whiskerRight, Category.id_comp,
    Iso.inv_hom_id, associator_inv_unit_unit_assoc,
    externalProductBifunctor_obj_obj, tensor_obj, NatTrans.naturality_assoc,
    NatTrans.naturality, unit_app_braiding_hom_app_assoc,
    BraidedCategory.braiding_tensor_right_hom, Functor.map_comp, Category.assoc,
    Iso.map_inv_hom_id, Category.comp_id,
    BraidedCategory.braiding_naturality_left_assoc,
    BraidedCategory.braiding_tensor_left_hom, Iso.map_hom_inv_id_assoc,
    Iso.hom_inv_id_assoc, Iso.inv_hom_id_assoc, unit_app_map_app_assoc,
    NatTrans.id_app, id_tensorHom]
  simp only [‚Üê comp_whiskerRight_assoc, ‚Üê whiskerLeft_comp_assoc,
    unit_app_braiding_hom_app]
  simp [‚Üê Functor.map_comp]
  congr 2
  rw [‚Üê BraidedCategory.hexagon_forward, ‚Üê comp_whiskerRight_assoc]
  haveI := unit_app_braiding_hom_app F H x z =‚â´ (H ‚äõ F).map (Œ≤_ z x).inv
  dsimp at this
  simp only [Category.assoc, Iso.map_hom_inv_id, Category.comp_id] at this
  rw [‚Üê this, comp_whiskerRight_assoc]
  simp [‚Üê Functor.map_comp]

end

section

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {V : Type u‚ÇÇ} [Category.{v‚ÇÇ} V]
  [MonoidalCategory C] [SymmetricCategory C]
  [MonoidalCategory V] [SymmetricCategory V]
  (F G : C ‚•§ V)

lemma symmetry [DayConvolution F G] [DayConvolution G F] :
    (braiding F G).hom ‚â´ (braiding G F).hom = ùüô _ := by
  apply Functor.hom_ext_of_isLeftKanExtension (F ‚äõ G) (unit F G)
  ext ‚ü®x, y‚ü©
  simp [‚Üê Functor.map_comp]

end

end DayConvolution

section

open LawfulDayConvolutionMonoidalCategoryStruct

/-- The data of a `LawfulDayConvolutionBraidedMonoidalCategoryStruct` adds
the data of a braiding isomorphism to a
`LawfulDayConvolutionMonoidalCategoryStruct C V D`, provided `C` and `V` are
braided. This braiding is required to behave well with respect to the
universal maps that exhibits (the realization as a functor `C ‚•§ V` of)
`d ‚äó d'` as a Day convolution, in a way that makes it equal to
`DayConvolution.braiding (Œπ C V D|>.obj d) (Œπ C V D|>.obj d')`. -/
class LawfulDayConvolutionBraidedMonoidalCategoryStruct
    (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] (V : Type u‚ÇÇ) [Category.{v‚ÇÇ} V]
    [MonoidalCategory C] [BraidedCategory C]
    [MonoidalCategory V] [BraidedCategory V]
    (D : Type u‚ÇÉ) [Category.{v‚ÇÉ} D] [MonoidalCategoryStruct D]
    [LawfulDayConvolutionMonoidalCategoryStruct C V D] where
  /-- The braiding isomorphism. -/
  braiding (C) (V) (d d' : D) : d ‚äó d' ‚âÖ d' ‚äó d
  unit_app_braiding_hom_app (C) (V) (d d' : D) (x y : C) :
    (convolutionExtensionUnit C V d d').app (x, y) ‚â´
      (Œπ C V D|>.map (braiding d d').hom).app (x ‚äó y) =
    (Œ≤_ _ _).hom ‚â´ (convolutionExtensionUnit C V d' d).app (_, _) ‚â´
      ((Œπ C V D).obj (d' ‚äó d)).map (Œ≤_ _ _).hom

namespace LawfulDayConvolutionBraidedMonoidalCategoryStruct

attribute [reassoc (attr := simp)] unit_app_braiding_hom_app

section

variable (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] (V : Type u‚ÇÇ) [Category.{v‚ÇÇ} V]
    [MonoidalCategory C] [BraidedCategory C]
    [MonoidalCategory V] [BraidedCategory V]
    {D : Type u‚ÇÉ} [Category.{v‚ÇÉ} D] [MonoidalCategoryStruct D]
    [LawfulDayConvolutionMonoidalCategoryStruct C V D]

@[reassoc (attr := simp)]
lemma unit_app_braiding_inv_app
    [LawfulDayConvolutionBraidedMonoidalCategoryStruct C V D]
    (d d' : D) (x y : C) :
    (convolutionExtensionUnit C V d' d).app (x, y) ‚â´
      ((Œπ C V D).map (braiding C V d d').inv).app (x ‚äó y) =
    (Œ≤_ _ _).inv ‚â´ (convolutionExtensionUnit C V d d').app (_, _) ‚â´
      ((Œπ C V D).obj (d ‚äó d')).map (Œ≤_ _ _).inv := by
  rw [‚Üê cancel_mono (Œπ C V D|>.map (braiding C V d d').hom|>.app (x ‚äó y))]
  simp

attribute [local instance] convolution in
lemma Œπ_map_braiding_hom
    [LawfulDayConvolutionBraidedMonoidalCategoryStruct C V D]
    (d d' : D) :
    (Œπ C V D).map (braiding C V d d').hom =
    (DayConvolution.braiding (Œπ C V D|>.obj d) (Œπ C V D|>.obj d')).hom := by
  apply DayConvolution.corepresentableBy
    (Œπ C V D|>.obj d) (Œπ C V D|>.obj d')|>.homEquiv.injective
  dsimp
  ext ‚ü®x, y‚ü©
  dsimp
  simp only [DayConvolution.unit_app_braiding_hom_app,
    Functor.comp_obj, tensor_obj]
  slice_lhs 1 2 => dsimp [DayConvolution.unit]
  simp only [unit_app_braiding_hom_app, Functor.comp_obj, tensor_obj,
    Iso.cancel_iso_hom_left]
  rfl

attribute [local instance] convolution in
lemma Œπ_map_braiding_inv
    [LawfulDayConvolutionBraidedMonoidalCategoryStruct C V D]
    (d d' : D) :
    (Œπ C V D).map (braiding C V d d').inv =
    (DayConvolution.braiding (Œπ C V D|>.obj d) (Œπ C V D|>.obj d')).inv := by
  apply IsIso.inv_eq_inv.mp
  simp only [‚Üê Functor.map_inv, IsIso.Iso.inv_inv]
  exact Œπ_map_braiding_hom C V d d'

attribute [local instance] convolution in
/-- A data-creating definition that takes an existing
`LawfulDayConvolutionMonoidalCategoryStruct` and puts the
"canonical" braiding induced from `DayConvolution.braiding`. -/
noncomputable def mkOfLawfulDayConvolutionMonoidalCategoryStruct
    [(Œπ C V D).Full] :
    LawfulDayConvolutionBraidedMonoidalCategoryStruct C V D where
  braiding d d' := (Œπ C V D).preimageIso <|
    DayConvolution.braiding (Œπ C V D|>.obj d) (Œπ C V D|>.obj d')
  unit_app_braiding_hom_app d d' x y := by
    simpa [-DayConvolution.unit_app_braiding_hom_app] using
      DayConvolution.unit_app_braiding_hom_app
        (Œπ C V D|>.obj d) (Œπ C V D|>.obj d') x y

end

section

variable (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] (V : Type u‚ÇÇ) [Category.{v‚ÇÇ} V]
    [MonoidalCategory C] [MonoidalCategory V]
    (D : Type u‚ÇÉ) [Category.{v‚ÇÉ} D] [MonoidalCategory D]
    [LawfulDayConvolutionMonoidalCategoryStruct C V D]

attribute [local instance] convolution convolution‚ÇÇ convolution‚ÇÇ' in
/-- Promote the monoidal structure induced by a
`LawfulDayConvolutionMonoidalCategoryStruct` to a braided monoidal category
structure. -/
def braided [BraidedCategory C] [BraidedCategory V]
    [LawfulDayConvolutionBraidedMonoidalCategoryStruct C V D]
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (tensor C) d) (tensorLeft v)]
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (tensor C) d) (tensorRight v)] :
    BraidedCategory D :=
  { braiding d d' := braiding C V d d'
    braiding_naturality_left f d := by
      apply (Œπ C V D).map_injective
      dsimp
      simp [Functor.map_comp, ‚Üê id_tensorHom, ‚Üê tensorHom_id,
        Œπ_map_tensorHom_hom_eq_tensorHom, Functor.map_id,
        Œπ_map_braiding_hom]
    braiding_naturality_right _ _ _ f := by
      apply (Œπ C V D).map_injective
      dsimp
      simp [Functor.map_comp, ‚Üê id_tensorHom, ‚Üê tensorHom_id,
        Œπ_map_tensorHom_hom_eq_tensorHom, Functor.map_id,
        Œπ_map_braiding_hom]
    hexagon_reverse d d' d'' := by
      apply (Œπ C V D).map_injective
      dsimp
      simp only [Functor.map_comp, ‚Üê id_tensorHom, ‚Üê tensorHom_id,
        Œπ_map_tensorHom_hom_eq_tensorHom,
        Œπ_map_associator_inv_eq_associator_inv, Functor.map_id,
        Œπ_map_braiding_hom]
      exact DayConvolution.hexagon_reverse
        (Œπ C V D|>.obj d) (Œπ C V D|>.obj d') (Œπ C V D|>.obj d'')
    hexagon_forward d d' d'' := by
      apply (Œπ C V D).map_injective
      dsimp
      simp only [Functor.map_comp, ‚Üê id_tensorHom, ‚Üê tensorHom_id,
        Œπ_map_tensorHom_hom_eq_tensorHom,
        Œπ_map_associator_hom_eq_associator_hom, Functor.map_id,
        Œπ_map_braiding_hom]
      exact DayConvolution.hexagon_forward
        (Œπ C V D|>.obj d) (Œπ C V D|>.obj d') (Œπ C V D|>.obj d'') }

/-- promote `braided` to a symmetric monoidal category structure
when `C` and `V` are symmetric monoidal. -/
def symmetric [SymmetricCategory V] [SymmetricCategory C]
    [LawfulDayConvolutionBraidedMonoidalCategoryStruct C V D]
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (tensor C) d) (tensorLeft v)]
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (tensor C) d) (tensorRight v)] :
    SymmetricCategory D where
  __ := braided C V D
  symmetry x y := by
    change (braiding C V x y).hom ‚â´ (braiding C V y x).hom = _
    apply (Œπ C V D).map_injective
    simp only [Functor.map_comp, Œπ_map_braiding_hom, DayConvolution.symmetry,
      Functor.map_id]
    rfl

end

end LawfulDayConvolutionBraidedMonoidalCategoryStruct

end

end CategoryTheory.MonoidalCategory
