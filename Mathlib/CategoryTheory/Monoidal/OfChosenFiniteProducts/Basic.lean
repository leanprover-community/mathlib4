/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Simon Hudon
-/
import Mathlib.CategoryTheory.Monoidal.Category
import Mathlib.CategoryTheory.Limits.Shapes.BinaryProducts
import Mathlib.CategoryTheory.PEmpty

/-!
# The monoidal structure on a category with chosen finite products.

This is a variant of the development in `CategoryTheory.Monoidal.OfHasFiniteProducts`,
which uses specified choices of the terminal object and binary product,
enabling the construction of a cartesian category with specific definitions of the tensor unit
and tensor product.

(Because the construction in `CategoryTheory.Monoidal.OfHasFiniteProducts` uses `HasLimit`
classes, the actual definitions there are opaque behind `Classical.choice`.)

We use this in `CategoryTheory.Monoidal.TypeCat` to construct the monoidal category of types
so that the tensor product is the usual cartesian product of types.

For now we only do the construction from products, and not from coproducts,
which seems less often useful.
-/


universe v u


namespace CategoryTheory

variable (C : Type u) [Category.{v} C] {X Y : C}

namespace Limits

section

variable {C}

/-- Swap the two sides of a `BinaryFan`. -/
def BinaryFan.swap {P Q : C} (t : BinaryFan P Q) : BinaryFan Q P :=
  BinaryFan.mk t.snd t.fst

@[simp]
theorem BinaryFan.swap_fst {P Q : C} (t : BinaryFan P Q) : t.swap.fst = t.snd :=
  rfl

@[simp]
theorem BinaryFan.swap_snd {P Q : C} (t : BinaryFan P Q) : t.swap.snd = t.fst :=
  rfl

/-- If a binary fan `t` over `P Q` is a limit cone, then `t.swap` is a limit cone over `Q P`.
-/
@[simps]
def IsLimit.swapBinaryFan {P Q : C} {t : BinaryFan P Q} (I : IsLimit t) : IsLimit t.swap where
  lift s := I.lift (BinaryFan.swap s)
  fac s := by rintro ‚ü®‚ü®‚ü©‚ü© <;> simp
  uniq s m w := by
    have h := I.uniq (BinaryFan.swap s) m
    rw [h]
    rintro ‚ü®j‚ü©
    specialize w ‚ü®WalkingPair.swap j‚ü©
    cases j <;> exact w

/-- Construct `HasBinaryProduct Q P` from `HasBinaryProduct P Q`.
This can't be an instance, as it would cause a loop in typeclass search.
-/
theorem HasBinaryProduct.swap (P Q : C) [HasBinaryProduct P Q] : HasBinaryProduct Q P :=
  HasLimit.mk ‚ü®BinaryFan.swap (limit.cone (pair P Q)), (limit.isLimit (pair P Q)).swapBinaryFan‚ü©

/-- Given a limit cone over `X` and `Y`, and another limit cone over `Y` and `X`, we can construct
an isomorphism between the cone points. Relative to some fixed choice of limits cones for every
pair, these isomorphisms constitute a braiding.
-/
def BinaryFan.braiding {X Y : C} {s : BinaryFan X Y} (P : IsLimit s) {t : BinaryFan Y X}
    (Q : IsLimit t) : s.pt ‚âÖ t.pt :=
  IsLimit.conePointUniqueUpToIso P Q.swapBinaryFan

/-- Given binary fans `sXY` over `X Y`, and `sYZ` over `Y Z`, and `s` over `sXY.X Z`,
if `sYZ` is a limit cone we can construct a binary fan over `X sYZ.X`.

This is an ingredient of building the associator for a cartesian category.
-/
def BinaryFan.assoc {X Y Z : C} {sXY : BinaryFan X Y} {sYZ : BinaryFan Y Z} (Q : IsLimit sYZ)
    (s : BinaryFan sXY.pt Z) : BinaryFan X sYZ.pt :=
  BinaryFan.mk (s.fst ‚â´ sXY.fst) (Q.lift (BinaryFan.mk (s.fst ‚â´ sXY.snd) s.snd))

@[simp]
theorem BinaryFan.assoc_fst {X Y Z : C} {sXY : BinaryFan X Y} {sYZ : BinaryFan Y Z}
    (Q : IsLimit sYZ) (s : BinaryFan sXY.pt Z) : (BinaryFan.assoc Q s).fst = s.fst ‚â´ sXY.fst :=
  rfl

@[simp]
theorem BinaryFan.assoc_snd {X Y Z : C} {sXY : BinaryFan X Y} {sYZ : BinaryFan Y Z}
    (Q : IsLimit sYZ) (s : BinaryFan sXY.pt Z) :
    (BinaryFan.assoc Q s).snd = Q.lift (BinaryFan.mk (s.fst ‚â´ sXY.snd) s.snd) :=
  rfl

/-- Given binary fans `sXY` over `X Y`, and `sYZ` over `Y Z`, and `s` over `X sYZ.X`,
if `sYZ` is a limit cone we can construct a binary fan over `sXY.X Z`.

This is an ingredient of building the associator for a cartesian category.
-/
def BinaryFan.assocInv {X Y Z : C} {sXY : BinaryFan X Y} (P : IsLimit sXY) {sYZ : BinaryFan Y Z}
    (s : BinaryFan X sYZ.pt) : BinaryFan sXY.pt Z :=
  BinaryFan.mk (P.lift (BinaryFan.mk s.fst (s.snd ‚â´ sYZ.fst))) (s.snd ‚â´ sYZ.snd)

@[simp]
theorem BinaryFan.assocInv_fst {X Y Z : C} {sXY : BinaryFan X Y} (P : IsLimit sXY)
    {sYZ : BinaryFan Y Z} (s : BinaryFan X sYZ.pt) :
    (BinaryFan.assocInv P s).fst = P.lift (BinaryFan.mk s.fst (s.snd ‚â´ sYZ.fst)) :=
  rfl

@[simp]
theorem BinaryFan.assocInv_snd {X Y Z : C} {sXY : BinaryFan X Y} (P : IsLimit sXY)
    {sYZ : BinaryFan Y Z} (s : BinaryFan X sYZ.pt) :
    (BinaryFan.assocInv P s).snd = s.snd ‚â´ sYZ.snd :=
  rfl

/-- If all the binary fans involved a limit cones, `BinaryFan.assoc` produces another limit cone.
-/
@[simps]
def IsLimit.assoc {X Y Z : C} {sXY : BinaryFan X Y} (P : IsLimit sXY) {sYZ : BinaryFan Y Z}
    (Q : IsLimit sYZ) {s : BinaryFan sXY.pt Z} (R : IsLimit s) : IsLimit (BinaryFan.assoc Q s) where
  lift t := R.lift (BinaryFan.assocInv P t)
  fac t := by
    rintro ‚ü®‚ü®‚ü©‚ü© <;> simp
    apply Q.hom_ext
    rintro ‚ü®‚ü®‚ü©‚ü© <;> simp
  uniq t m w := by
    have h := R.uniq (BinaryFan.assocInv P t) m
    rw [h]
    rintro ‚ü®‚ü®‚ü©‚ü© <;> simp
    ¬∑ apply P.hom_ext
      rintro ‚ü®‚ü®‚ü©‚ü© <;> simp
      ¬∑ exact w ‚ü®WalkingPair.left‚ü©
      ¬∑ specialize w ‚ü®WalkingPair.right‚ü©
        simp? at w says
          simp only [pair_obj_right, BinaryFan.œÄ_app_right, BinaryFan.assoc_snd,
            Functor.const_obj_obj, pair_obj_left] at w
        rw [‚Üê w]
        simp
    ¬∑ specialize w ‚ü®WalkingPair.right‚ü©
      simp? at w says
        simp only [pair_obj_right, BinaryFan.œÄ_app_right, BinaryFan.assoc_snd,
          Functor.const_obj_obj, pair_obj_left] at w
      rw [‚Üê w]
      simp

/-- Given two pairs of limit cones corresponding to the parenthesisations of `X √ó Y √ó Z`,
we obtain an isomorphism between the cone points.
-/
abbrev BinaryFan.associator {X Y Z : C} {sXY : BinaryFan X Y} (P : IsLimit sXY)
    {sYZ : BinaryFan Y Z} (Q : IsLimit sYZ) {s : BinaryFan sXY.pt Z} (R : IsLimit s)
    {t : BinaryFan X sYZ.pt} (S : IsLimit t) : s.pt ‚âÖ t.pt :=
  IsLimit.conePointUniqueUpToIso (IsLimit.assoc P Q R) S

/-- Given a fixed family of limit data for every pair `X Y`, we obtain an associator.
-/
abbrev BinaryFan.associatorOfLimitCone (L : ‚àÄ X Y : C, LimitCone (pair X Y)) (X Y Z : C) :
    (L (L X Y).cone.pt Z).cone.pt ‚âÖ (L X (L Y Z).cone.pt).cone.pt :=
  BinaryFan.associator (L X Y).isLimit (L Y Z).isLimit (L (L X Y).cone.pt Z).isLimit
    (L X (L Y Z).cone.pt).isLimit

/-- Construct a left unitor from specified limit cones.
-/
@[simps]
def BinaryFan.leftUnitor {X : C} {s : Cone (Functor.empty.{0} C)} (P : IsLimit s)
    {t : BinaryFan s.pt X} (Q : IsLimit t) : t.pt ‚âÖ X where
  hom := t.snd
  inv := Q.lift <| BinaryFan.mk (P.lift ‚ü®_, fun x => x.as.elim, fun {x} => x.as.elim‚ü©) (ùüô _)
  hom_inv_id := by
    apply Q.hom_ext
    rintro ‚ü®‚ü®‚ü©‚ü©
    ¬∑ apply P.hom_ext
      rintro ‚ü®‚ü®‚ü©‚ü©
    ¬∑ simp

/-- Construct a right unitor from specified limit cones.
-/
@[simps]
def BinaryFan.rightUnitor {X : C} {s : Cone (Functor.empty.{0} C)} (P : IsLimit s)
    {t : BinaryFan X s.pt} (Q : IsLimit t) : t.pt ‚âÖ X where
  hom := t.fst
  inv := Q.lift <| BinaryFan.mk (ùüô _) <| P.lift ‚ü®_, fun x => x.as.elim, fun {x} => x.as.elim‚ü©
  hom_inv_id := by
    apply Q.hom_ext
    rintro ‚ü®‚ü®‚ü©‚ü©
    ¬∑ simp
    ¬∑ apply P.hom_ext
      rintro ‚ü®‚ü®‚ü©‚ü©

end

end Limits

open CategoryTheory.Limits

section

-- Porting note: no tidy
-- attribute [local tidy] tactic.case_bash

variable {C}
variable (ùíØ : LimitCone (Functor.empty.{0} C))
variable (‚Ñ¨ : ‚àÄ X Y : C, LimitCone (pair X Y))

namespace MonoidalOfChosenFiniteProducts

/-- Implementation of the tensor product for `MonoidalOfChosenFiniteProducts`. -/
abbrev tensorObj (X Y : C) : C :=
  (‚Ñ¨ X Y).cone.pt

/-- Implementation of the tensor product of morphisms for `MonoidalOfChosenFiniteProducts`. -/
abbrev tensorHom {W X Y Z : C} (f : W ‚ü∂ X) (g : Y ‚ü∂ Z) : tensorObj ‚Ñ¨ W Y ‚ü∂ tensorObj ‚Ñ¨ X Z :=
  (BinaryFan.IsLimit.lift' (‚Ñ¨ X Z).isLimit ((‚Ñ¨ W Y).cone.œÄ.app ‚ü®WalkingPair.left‚ü© ‚â´ f)
      (((‚Ñ¨ W Y).cone.œÄ.app ‚ü®WalkingPair.right‚ü© : (‚Ñ¨ W Y).cone.pt ‚ü∂ Y) ‚â´ g)).val

theorem tensor_id (X‚ÇÅ X‚ÇÇ : C) : tensorHom ‚Ñ¨ (ùüô X‚ÇÅ) (ùüô X‚ÇÇ) = ùüô (tensorObj ‚Ñ¨ X‚ÇÅ X‚ÇÇ) := by
  apply IsLimit.hom_ext (‚Ñ¨ _ _).isLimit
  rintro ‚ü®‚ü®‚ü©‚ü© <;>
    ¬∑ dsimp [tensorHom]
      simp

theorem tensor_comp {X‚ÇÅ Y‚ÇÅ Z‚ÇÅ X‚ÇÇ Y‚ÇÇ Z‚ÇÇ : C} (f‚ÇÅ : X‚ÇÅ ‚ü∂ Y‚ÇÅ) (f‚ÇÇ : X‚ÇÇ ‚ü∂ Y‚ÇÇ) (g‚ÇÅ : Y‚ÇÅ ‚ü∂ Z‚ÇÅ)
    (g‚ÇÇ : Y‚ÇÇ ‚ü∂ Z‚ÇÇ) : tensorHom ‚Ñ¨ (f‚ÇÅ ‚â´ g‚ÇÅ) (f‚ÇÇ ‚â´ g‚ÇÇ) = tensorHom ‚Ñ¨ f‚ÇÅ f‚ÇÇ ‚â´ tensorHom ‚Ñ¨ g‚ÇÅ g‚ÇÇ := by
  apply IsLimit.hom_ext (‚Ñ¨ _ _).isLimit
  rintro ‚ü®‚ü®‚ü©‚ü© <;>
    ¬∑ dsimp [tensorHom]
      simp

theorem pentagon (W X Y Z : C) :
    tensorHom ‚Ñ¨ (BinaryFan.associatorOfLimitCone ‚Ñ¨ W X Y).hom (ùüô Z) ‚â´
        (BinaryFan.associatorOfLimitCone ‚Ñ¨ W (tensorObj ‚Ñ¨ X Y) Z).hom ‚â´
          tensorHom ‚Ñ¨ (ùüô W) (BinaryFan.associatorOfLimitCone ‚Ñ¨ X Y Z).hom =
      (BinaryFan.associatorOfLimitCone ‚Ñ¨ (tensorObj ‚Ñ¨ W X) Y Z).hom ‚â´
        (BinaryFan.associatorOfLimitCone ‚Ñ¨ W X (tensorObj ‚Ñ¨ Y Z)).hom := by
  dsimp [tensorHom]
  apply IsLimit.hom_ext (‚Ñ¨ _ _).isLimit; rintro ‚ü®‚ü®‚ü©‚ü©
  ¬∑ simp
  ¬∑ apply IsLimit.hom_ext (‚Ñ¨ _ _).isLimit
    rintro ‚ü®‚ü®‚ü©‚ü©
    ¬∑ simp
    apply IsLimit.hom_ext (‚Ñ¨ _ _).isLimit
    rintro ‚ü®‚ü®‚ü©‚ü©
    ¬∑ simp
    ¬∑ simp

theorem triangle (X Y : C) :
    (BinaryFan.associatorOfLimitCone ‚Ñ¨ X ùíØ.cone.pt Y).hom ‚â´
        tensorHom ‚Ñ¨ (ùüô X) (BinaryFan.leftUnitor ùíØ.isLimit (‚Ñ¨ ùíØ.cone.pt Y).isLimit).hom =
      tensorHom ‚Ñ¨ (BinaryFan.rightUnitor ùíØ.isLimit (‚Ñ¨ X ùíØ.cone.pt).isLimit).hom (ùüô Y) := by
  dsimp [tensorHom]
  apply IsLimit.hom_ext (‚Ñ¨ _ _).isLimit; rintro ‚ü®‚ü®‚ü©‚ü© <;> simp

theorem leftUnitor_naturality {X‚ÇÅ X‚ÇÇ : C} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) :
    tensorHom ‚Ñ¨ (ùüô ùíØ.cone.pt) f ‚â´ (BinaryFan.leftUnitor ùíØ.isLimit (‚Ñ¨ ùíØ.cone.pt X‚ÇÇ).isLimit).hom =
      (BinaryFan.leftUnitor ùíØ.isLimit (‚Ñ¨ ùíØ.cone.pt X‚ÇÅ).isLimit).hom ‚â´ f := by
  dsimp [tensorHom]
  simp

theorem rightUnitor_naturality {X‚ÇÅ X‚ÇÇ : C} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) :
    tensorHom ‚Ñ¨ f (ùüô ùíØ.cone.pt) ‚â´ (BinaryFan.rightUnitor ùíØ.isLimit (‚Ñ¨ X‚ÇÇ ùíØ.cone.pt).isLimit).hom =
      (BinaryFan.rightUnitor ùíØ.isLimit (‚Ñ¨ X‚ÇÅ ùíØ.cone.pt).isLimit).hom ‚â´ f := by
  dsimp [tensorHom]
  simp

theorem associator_naturality {X‚ÇÅ X‚ÇÇ X‚ÇÉ Y‚ÇÅ Y‚ÇÇ Y‚ÇÉ : C} (f‚ÇÅ : X‚ÇÅ ‚ü∂ Y‚ÇÅ) (f‚ÇÇ : X‚ÇÇ ‚ü∂ Y‚ÇÇ) (f‚ÇÉ : X‚ÇÉ ‚ü∂ Y‚ÇÉ) :
    tensorHom ‚Ñ¨ (tensorHom ‚Ñ¨ f‚ÇÅ f‚ÇÇ) f‚ÇÉ ‚â´ (BinaryFan.associatorOfLimitCone ‚Ñ¨ Y‚ÇÅ Y‚ÇÇ Y‚ÇÉ).hom =
      (BinaryFan.associatorOfLimitCone ‚Ñ¨ X‚ÇÅ X‚ÇÇ X‚ÇÉ).hom ‚â´ tensorHom ‚Ñ¨ f‚ÇÅ (tensorHom ‚Ñ¨ f‚ÇÇ f‚ÇÉ) := by
  dsimp [tensorHom]
  apply IsLimit.hom_ext (‚Ñ¨ _ _).isLimit; rintro ‚ü®‚ü®‚ü©‚ü©
  ¬∑ simp
  ¬∑ apply IsLimit.hom_ext (‚Ñ¨ _ _).isLimit
    rintro ‚ü®‚ü®‚ü©‚ü©
    ¬∑ simp
    ¬∑ simp

end MonoidalOfChosenFiniteProducts

open MonoidalOfChosenFiniteProducts

/-- A category with a terminal object and binary products has a natural monoidal structure. -/
def monoidalOfChosenFiniteProducts : MonoidalCategory C :=
  letI : MonoidalCategoryStruct C :=
    { tensorUnit := ùíØ.cone.pt
      tensorObj := tensorObj ‚Ñ¨
      tensorHom := tensorHom ‚Ñ¨
      whiskerLeft := @fun X {_ _} g ‚Ü¶ tensorHom ‚Ñ¨ (ùüô X) g
      whiskerRight := @fun{_ _} f Y ‚Ü¶ tensorHom ‚Ñ¨ f (ùüô Y)
      associator := BinaryFan.associatorOfLimitCone ‚Ñ¨
      leftUnitor := fun X ‚Ü¶ BinaryFan.leftUnitor ùíØ.isLimit (‚Ñ¨ ùíØ.cone.pt X).isLimit
      rightUnitor := fun X ‚Ü¶ BinaryFan.rightUnitor ùíØ.isLimit (‚Ñ¨ X ùíØ.cone.pt).isLimit}
  .ofTensorHom
    (tensor_id := tensor_id ‚Ñ¨)
    (tensor_comp := tensor_comp ‚Ñ¨)
    (pentagon := pentagon ‚Ñ¨)
    (triangle := triangle ùíØ ‚Ñ¨)
    (leftUnitor_naturality := leftUnitor_naturality ùíØ ‚Ñ¨)
    (rightUnitor_naturality := rightUnitor_naturality ùíØ ‚Ñ¨)
    (associator_naturality := associator_naturality ‚Ñ¨)

namespace MonoidalOfChosenFiniteProducts

open MonoidalCategory

/-- A type synonym for `C` carrying a monoidal category structure corresponding to
a fixed choice of limit data for the empty functor, and for `pair X Y` for every `X Y : C`.

This is an implementation detail for `SymmetricOfChosenFiniteProducts`.
-/
-- Porting note (https://github.com/leanprover-community/mathlib4/issues/5171): linter `has_nonempty_instance` not ported yet
-- @[nolint has_nonempty_instance]
@[nolint unusedArguments]
def MonoidalOfChosenFiniteProductsSynonym (_ùíØ : LimitCone (Functor.empty.{0} C))
    (_‚Ñ¨ : ‚àÄ X Y : C, LimitCone (pair X Y)) :=
  C

instance : Category (MonoidalOfChosenFiniteProductsSynonym ùíØ ‚Ñ¨) := by
  dsimp [MonoidalOfChosenFiniteProductsSynonym]
  infer_instance

instance : MonoidalCategory (MonoidalOfChosenFiniteProductsSynonym ùíØ ‚Ñ¨) :=
  monoidalOfChosenFiniteProducts ùíØ ‚Ñ¨

end MonoidalOfChosenFiniteProducts

end

end CategoryTheory
