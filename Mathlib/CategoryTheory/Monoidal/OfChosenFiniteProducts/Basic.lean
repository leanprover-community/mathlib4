/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Simon Hudon
-/
import Mathlib.CategoryTheory.Monoidal.Category
import Mathlib.CategoryTheory.Limits.Shapes.BinaryProducts
import Mathlib.CategoryTheory.PEmpty

#align_import category_theory.monoidal.of_chosen_finite_products.basic from "leanprover-community/mathlib"@"95a87616d63b3cb49d3fe678d416fbe9c4217bf4"

/-!
# The monoidal structure on a category with chosen finite products.

This is a variant of the development in `CategoryTheory.Monoidal.OfHasFiniteProducts`,
which uses specified choices of the terminal object and binary product,
enabling the construction of a cartesian category with specific definitions of the tensor unit
and tensor product.

(Because the construction in `CategoryTheory.Monoidal.OfHasFiniteProducts` uses `HasLimit`
classes, the actual definitions there are opaque behind `Classical.choice`.)

We use this in `CategoryTheory.Monoidal.TypeCat` to construct the monoidal category of types
so that the tensor product is the usual cartesian product of types.

For now we only do the construction from products, and not from coproducts,
which seems less often useful.
-/


universe v u


namespace CategoryTheory

variable (C : Type u) [Category.{v} C] {X Y : C}

namespace Limits

section

variable {C}

/-- Swap the two sides of a `BinaryFan`. -/
def BinaryFan.swap {P Q : C} (t : BinaryFan P Q) : BinaryFan Q P :=
  BinaryFan.mk t.snd t.fst
#align category_theory.limits.binary_fan.swap CategoryTheory.Limits.BinaryFan.swap

@[simp]
theorem BinaryFan.swap_fst {P Q : C} (t : BinaryFan P Q) : t.swap.fst = t.snd :=
  rfl
#align category_theory.limits.binary_fan.swap_fst CategoryTheory.Limits.BinaryFan.swap_fst

@[simp]
theorem BinaryFan.swap_snd {P Q : C} (t : BinaryFan P Q) : t.swap.snd = t.fst :=
  rfl
#align category_theory.limits.binary_fan.swap_snd CategoryTheory.Limits.BinaryFan.swap_snd

/-- If a binary fan `t` over `P Q` is a limit cone, then `t.swap` is a limit cone over `Q P`.
-/
@[simps]
def IsLimit.swapBinaryFan {P Q : C} {t : BinaryFan P Q} (I : IsLimit t) : IsLimit t.swap where
  lift s := I.lift (BinaryFan.swap s)
  fac s := by rintro âŸ¨âŸ¨âŸ©âŸ© <;> simp
  uniq s m w := by
    have h := I.uniq (BinaryFan.swap s) m
    rw [h]
    rintro âŸ¨jâŸ©
    specialize w âŸ¨WalkingPair.swap jâŸ©
    cases j <;> exact w
#align category_theory.limits.is_limit.swap_binary_fan CategoryTheory.Limits.IsLimit.swapBinaryFan

/-- Construct `HasBinaryProduct Q P` from `HasBinaryProduct P Q`.
This can't be an instance, as it would cause a loop in typeclass search.
-/
theorem HasBinaryProduct.swap (P Q : C) [HasBinaryProduct P Q] : HasBinaryProduct Q P :=
  HasLimit.mk âŸ¨BinaryFan.swap (limit.cone (pair P Q)), (limit.isLimit (pair P Q)).swapBinaryFanâŸ©
#align category_theory.limits.has_binary_product.swap CategoryTheory.Limits.HasBinaryProduct.swap

/-- Given a limit cone over `X` and `Y`, and another limit cone over `Y` and `X`, we can construct
an isomorphism between the cone points. Relative to some fixed choice of limits cones for every
pair, these isomorphisms constitute a braiding.
-/
def BinaryFan.braiding {X Y : C} {s : BinaryFan X Y} (P : IsLimit s) {t : BinaryFan Y X}
    (Q : IsLimit t) : s.pt â‰… t.pt :=
  IsLimit.conePointUniqueUpToIso P Q.swapBinaryFan
#align category_theory.limits.binary_fan.braiding CategoryTheory.Limits.BinaryFan.braiding

/-- Given binary fans `sXY` over `X Y`, and `sYZ` over `Y Z`, and `s` over `sXY.X Z`,
if `sYZ` is a limit cone we can construct a binary fan over `X sYZ.X`.

This is an ingredient of building the associator for a cartesian category.
-/
def BinaryFan.assoc {X Y Z : C} {sXY : BinaryFan X Y} {sYZ : BinaryFan Y Z} (Q : IsLimit sYZ)
    (s : BinaryFan sXY.pt Z) : BinaryFan X sYZ.pt :=
  BinaryFan.mk (s.fst â‰« sXY.fst) (Q.lift (BinaryFan.mk (s.fst â‰« sXY.snd) s.snd))
#align category_theory.limits.binary_fan.assoc CategoryTheory.Limits.BinaryFan.assoc

@[simp]
theorem BinaryFan.assoc_fst {X Y Z : C} {sXY : BinaryFan X Y} {sYZ : BinaryFan Y Z}
    (Q : IsLimit sYZ) (s : BinaryFan sXY.pt Z) : (BinaryFan.assoc Q s).fst = s.fst â‰« sXY.fst :=
  rfl
#align category_theory.limits.binary_fan.assoc_fst CategoryTheory.Limits.BinaryFan.assoc_fst

@[simp]
theorem BinaryFan.assoc_snd {X Y Z : C} {sXY : BinaryFan X Y} {sYZ : BinaryFan Y Z}
    (Q : IsLimit sYZ) (s : BinaryFan sXY.pt Z) :
    (BinaryFan.assoc Q s).snd = Q.lift (BinaryFan.mk (s.fst â‰« sXY.snd) s.snd) :=
  rfl
#align category_theory.limits.binary_fan.assoc_snd CategoryTheory.Limits.BinaryFan.assoc_snd

/-- Given binary fans `sXY` over `X Y`, and `sYZ` over `Y Z`, and `s` over `X sYZ.X`,
if `sYZ` is a limit cone we can construct a binary fan over `sXY.X Z`.

This is an ingredient of building the associator for a cartesian category.
-/
def BinaryFan.assocInv {X Y Z : C} {sXY : BinaryFan X Y} (P : IsLimit sXY) {sYZ : BinaryFan Y Z}
    (s : BinaryFan X sYZ.pt) : BinaryFan sXY.pt Z :=
  BinaryFan.mk (P.lift (BinaryFan.mk s.fst (s.snd â‰« sYZ.fst))) (s.snd â‰« sYZ.snd)
#align category_theory.limits.binary_fan.assoc_inv CategoryTheory.Limits.BinaryFan.assocInv

@[simp]
theorem BinaryFan.assocInv_fst {X Y Z : C} {sXY : BinaryFan X Y} (P : IsLimit sXY)
    {sYZ : BinaryFan Y Z} (s : BinaryFan X sYZ.pt) :
    (BinaryFan.assocInv P s).fst = P.lift (BinaryFan.mk s.fst (s.snd â‰« sYZ.fst)) :=
  rfl
#align category_theory.limits.binary_fan.assoc_inv_fst CategoryTheory.Limits.BinaryFan.assocInv_fst

@[simp]
theorem BinaryFan.assocInv_snd {X Y Z : C} {sXY : BinaryFan X Y} (P : IsLimit sXY)
    {sYZ : BinaryFan Y Z} (s : BinaryFan X sYZ.pt) :
    (BinaryFan.assocInv P s).snd = s.snd â‰« sYZ.snd :=
  rfl
#align category_theory.limits.binary_fan.assoc_inv_snd CategoryTheory.Limits.BinaryFan.assocInv_snd

/-- If all the binary fans involved a limit cones, `BinaryFan.assoc` produces another limit cone.
-/
@[simps]
def IsLimit.assoc {X Y Z : C} {sXY : BinaryFan X Y} (P : IsLimit sXY) {sYZ : BinaryFan Y Z}
    (Q : IsLimit sYZ) {s : BinaryFan sXY.pt Z} (R : IsLimit s) : IsLimit (BinaryFan.assoc Q s) where
  lift t := R.lift (BinaryFan.assocInv P t)
  fac t := by
    rintro âŸ¨âŸ¨âŸ©âŸ© <;> simp
    apply Q.hom_ext
    rintro âŸ¨âŸ¨âŸ©âŸ© <;> simp
  uniq t m w := by
    have h := R.uniq (BinaryFan.assocInv P t) m
    rw [h]
    rintro âŸ¨âŸ¨âŸ©âŸ© <;> simp
    Â· apply P.hom_ext
      rintro âŸ¨âŸ¨âŸ©âŸ© <;> simp
      Â· exact w âŸ¨WalkingPair.leftâŸ©
      Â· specialize w âŸ¨WalkingPair.rightâŸ©
        simp? at w says
          simp only [pair_obj_right, BinaryFan.Ï€_app_right, BinaryFan.assoc_snd,
            Functor.const_obj_obj, pair_obj_left] at w
        rw [â† w]
        simp
    Â· specialize w âŸ¨WalkingPair.rightâŸ©
      simp? at w says
        simp only [pair_obj_right, BinaryFan.Ï€_app_right, BinaryFan.assoc_snd,
          Functor.const_obj_obj, pair_obj_left] at w
      rw [â† w]
      simp
#align category_theory.limits.is_limit.assoc CategoryTheory.Limits.IsLimit.assoc

/-- Given two pairs of limit cones corresponding to the parenthesisations of `X Ã— Y Ã— Z`,
we obtain an isomorphism between the cone points.
-/
abbrev BinaryFan.associator {X Y Z : C} {sXY : BinaryFan X Y} (P : IsLimit sXY)
    {sYZ : BinaryFan Y Z} (Q : IsLimit sYZ) {s : BinaryFan sXY.pt Z} (R : IsLimit s)
    {t : BinaryFan X sYZ.pt} (S : IsLimit t) : s.pt â‰… t.pt :=
  IsLimit.conePointUniqueUpToIso (IsLimit.assoc P Q R) S
#align category_theory.limits.binary_fan.associator CategoryTheory.Limits.BinaryFan.associator

/-- Given a fixed family of limit data for every pair `X Y`, we obtain an associator.
-/
abbrev BinaryFan.associatorOfLimitCone (L : âˆ€ X Y : C, LimitCone (pair X Y)) (X Y Z : C) :
    (L (L X Y).cone.pt Z).cone.pt â‰… (L X (L Y Z).cone.pt).cone.pt :=
  BinaryFan.associator (L X Y).isLimit (L Y Z).isLimit (L (L X Y).cone.pt Z).isLimit
    (L X (L Y Z).cone.pt).isLimit
#align category_theory.limits.binary_fan.associator_of_limit_cone CategoryTheory.Limits.BinaryFan.associatorOfLimitCone

/-- Construct a left unitor from specified limit cones.
-/
@[simps]
def BinaryFan.leftUnitor {X : C} {s : Cone (Functor.empty.{0} C)} (P : IsLimit s)
    {t : BinaryFan s.pt X} (Q : IsLimit t) : t.pt â‰… X where
  hom := t.snd
  inv := Q.lift <| BinaryFan.mk (P.lift âŸ¨_, fun x => x.as.elim, fun {x} => x.as.elimâŸ©) (ğŸ™ _)
  hom_inv_id := by
    apply Q.hom_ext
    rintro âŸ¨âŸ¨âŸ©âŸ©
    Â· apply P.hom_ext
      rintro âŸ¨âŸ¨âŸ©âŸ©
    Â· simp
#align category_theory.limits.binary_fan.left_unitor CategoryTheory.Limits.BinaryFan.leftUnitor

/-- Construct a right unitor from specified limit cones.
-/
@[simps]
def BinaryFan.rightUnitor {X : C} {s : Cone (Functor.empty.{0} C)} (P : IsLimit s)
    {t : BinaryFan X s.pt} (Q : IsLimit t) : t.pt â‰… X where
  hom := t.fst
  inv := Q.lift <| BinaryFan.mk (ğŸ™ _) <| P.lift âŸ¨_, fun x => x.as.elim, fun {x} => x.as.elimâŸ©
  hom_inv_id := by
    apply Q.hom_ext
    rintro âŸ¨âŸ¨âŸ©âŸ©
    Â· simp
    Â· apply P.hom_ext
      rintro âŸ¨âŸ¨âŸ©âŸ©
#align category_theory.limits.binary_fan.right_unitor CategoryTheory.Limits.BinaryFan.rightUnitor

end

end Limits

open CategoryTheory.Limits

section

-- Porting note: no tidy
-- attribute [local tidy] tactic.case_bash

variable {C}
variable (ğ’¯ : LimitCone (Functor.empty.{0} C))
variable (â„¬ : âˆ€ X Y : C, LimitCone (pair X Y))

namespace MonoidalOfChosenFiniteProducts

/-- Implementation of the tensor product for `MonoidalOfChosenFiniteProducts`. -/
abbrev tensorObj (X Y : C) : C :=
  (â„¬ X Y).cone.pt
#align category_theory.monoidal_of_chosen_finite_products.tensor_obj CategoryTheory.MonoidalOfChosenFiniteProducts.tensorObj

/-- Implementation of the tensor product of morphisms for `MonoidalOfChosenFiniteProducts`. -/
abbrev tensorHom {W X Y Z : C} (f : W âŸ¶ X) (g : Y âŸ¶ Z) : tensorObj â„¬ W Y âŸ¶ tensorObj â„¬ X Z :=
  (BinaryFan.IsLimit.lift' (â„¬ X Z).isLimit ((â„¬ W Y).cone.Ï€.app âŸ¨WalkingPair.leftâŸ© â‰« f)
      (((â„¬ W Y).cone.Ï€.app âŸ¨WalkingPair.rightâŸ© : (â„¬ W Y).cone.pt âŸ¶ Y) â‰« g)).val
#align category_theory.monoidal_of_chosen_finite_products.tensor_hom CategoryTheory.MonoidalOfChosenFiniteProducts.tensorHom

theorem tensor_id (Xâ‚ Xâ‚‚ : C) : tensorHom â„¬ (ğŸ™ Xâ‚) (ğŸ™ Xâ‚‚) = ğŸ™ (tensorObj â„¬ Xâ‚ Xâ‚‚) := by
  apply IsLimit.hom_ext (â„¬ _ _).isLimit;
  rintro âŸ¨âŸ¨âŸ©âŸ© <;>
    Â· dsimp [tensorHom]
      simp
#align category_theory.monoidal_of_chosen_finite_products.tensor_id CategoryTheory.MonoidalOfChosenFiniteProducts.tensor_id

theorem tensor_comp {Xâ‚ Yâ‚ Zâ‚ Xâ‚‚ Yâ‚‚ Zâ‚‚ : C} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (gâ‚ : Yâ‚ âŸ¶ Zâ‚)
    (gâ‚‚ : Yâ‚‚ âŸ¶ Zâ‚‚) : tensorHom â„¬ (fâ‚ â‰« gâ‚) (fâ‚‚ â‰« gâ‚‚) = tensorHom â„¬ fâ‚ fâ‚‚ â‰« tensorHom â„¬ gâ‚ gâ‚‚ := by
  apply IsLimit.hom_ext (â„¬ _ _).isLimit;
  rintro âŸ¨âŸ¨âŸ©âŸ© <;>
    Â· dsimp [tensorHom]
      simp
#align category_theory.monoidal_of_chosen_finite_products.tensor_comp CategoryTheory.MonoidalOfChosenFiniteProducts.tensor_comp

theorem pentagon (W X Y Z : C) :
    tensorHom â„¬ (BinaryFan.associatorOfLimitCone â„¬ W X Y).hom (ğŸ™ Z) â‰«
        (BinaryFan.associatorOfLimitCone â„¬ W (tensorObj â„¬ X Y) Z).hom â‰«
          tensorHom â„¬ (ğŸ™ W) (BinaryFan.associatorOfLimitCone â„¬ X Y Z).hom =
      (BinaryFan.associatorOfLimitCone â„¬ (tensorObj â„¬ W X) Y Z).hom â‰«
        (BinaryFan.associatorOfLimitCone â„¬ W X (tensorObj â„¬ Y Z)).hom := by
  dsimp [tensorHom]
  apply IsLimit.hom_ext (â„¬ _ _).isLimit; rintro âŸ¨âŸ¨âŸ©âŸ©
  Â· simp
  Â· apply IsLimit.hom_ext (â„¬ _ _).isLimit
    rintro âŸ¨âŸ¨âŸ©âŸ©
    Â· simp
    apply IsLimit.hom_ext (â„¬ _ _).isLimit
    rintro âŸ¨âŸ¨âŸ©âŸ©
    Â· simp
    Â· simp
#align category_theory.monoidal_of_chosen_finite_products.pentagon CategoryTheory.MonoidalOfChosenFiniteProducts.pentagon

theorem triangle (X Y : C) :
    (BinaryFan.associatorOfLimitCone â„¬ X ğ’¯.cone.pt Y).hom â‰«
        tensorHom â„¬ (ğŸ™ X) (BinaryFan.leftUnitor ğ’¯.isLimit (â„¬ ğ’¯.cone.pt Y).isLimit).hom =
      tensorHom â„¬ (BinaryFan.rightUnitor ğ’¯.isLimit (â„¬ X ğ’¯.cone.pt).isLimit).hom (ğŸ™ Y) := by
  dsimp [tensorHom]
  apply IsLimit.hom_ext (â„¬ _ _).isLimit; rintro âŸ¨âŸ¨âŸ©âŸ© <;> simp
#align category_theory.monoidal_of_chosen_finite_products.triangle CategoryTheory.MonoidalOfChosenFiniteProducts.triangle

theorem leftUnitor_naturality {Xâ‚ Xâ‚‚ : C} (f : Xâ‚ âŸ¶ Xâ‚‚) :
    tensorHom â„¬ (ğŸ™ ğ’¯.cone.pt) f â‰« (BinaryFan.leftUnitor ğ’¯.isLimit (â„¬ ğ’¯.cone.pt Xâ‚‚).isLimit).hom =
      (BinaryFan.leftUnitor ğ’¯.isLimit (â„¬ ğ’¯.cone.pt Xâ‚).isLimit).hom â‰« f := by
  dsimp [tensorHom]
  simp
#align category_theory.monoidal_of_chosen_finite_products.left_unitor_naturality CategoryTheory.MonoidalOfChosenFiniteProducts.leftUnitor_naturality

theorem rightUnitor_naturality {Xâ‚ Xâ‚‚ : C} (f : Xâ‚ âŸ¶ Xâ‚‚) :
    tensorHom â„¬ f (ğŸ™ ğ’¯.cone.pt) â‰« (BinaryFan.rightUnitor ğ’¯.isLimit (â„¬ Xâ‚‚ ğ’¯.cone.pt).isLimit).hom =
      (BinaryFan.rightUnitor ğ’¯.isLimit (â„¬ Xâ‚ ğ’¯.cone.pt).isLimit).hom â‰« f := by
  dsimp [tensorHom]
  simp
#align category_theory.monoidal_of_chosen_finite_products.right_unitor_naturality CategoryTheory.MonoidalOfChosenFiniteProducts.rightUnitor_naturality

theorem associator_naturality {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ : C} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ) :
    tensorHom â„¬ (tensorHom â„¬ fâ‚ fâ‚‚) fâ‚ƒ â‰« (BinaryFan.associatorOfLimitCone â„¬ Yâ‚ Yâ‚‚ Yâ‚ƒ).hom =
      (BinaryFan.associatorOfLimitCone â„¬ Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« tensorHom â„¬ fâ‚ (tensorHom â„¬ fâ‚‚ fâ‚ƒ) := by
  dsimp [tensorHom]
  apply IsLimit.hom_ext (â„¬ _ _).isLimit; rintro âŸ¨âŸ¨âŸ©âŸ©
  Â· simp
  Â· apply IsLimit.hom_ext (â„¬ _ _).isLimit
    rintro âŸ¨âŸ¨âŸ©âŸ©
    Â· simp
    Â· simp
#align category_theory.monoidal_of_chosen_finite_products.associator_naturality CategoryTheory.MonoidalOfChosenFiniteProducts.associator_naturality

end MonoidalOfChosenFiniteProducts

open MonoidalOfChosenFiniteProducts

/-- A category with a terminal object and binary products has a natural monoidal structure. -/
def monoidalOfChosenFiniteProducts : MonoidalCategory C :=
  letI : MonoidalCategoryStruct C :=
    { tensorUnit := ğ’¯.cone.pt
      tensorObj := tensorObj â„¬
      tensorHom := tensorHom â„¬
      whiskerLeft := @fun X {_ _} g â†¦ tensorHom â„¬ (ğŸ™ X) g
      whiskerRight := @fun{_ _} f Y â†¦ tensorHom â„¬ f (ğŸ™ Y)
      associator := BinaryFan.associatorOfLimitCone â„¬
      leftUnitor := fun X â†¦ BinaryFan.leftUnitor ğ’¯.isLimit (â„¬ ğ’¯.cone.pt X).isLimit
      rightUnitor := fun X â†¦ BinaryFan.rightUnitor ğ’¯.isLimit (â„¬ X ğ’¯.cone.pt).isLimit}
  .ofTensorHom
    (tensor_id := tensor_id â„¬)
    (tensor_comp := tensor_comp â„¬)
    (pentagon := pentagon â„¬)
    (triangle := triangle ğ’¯ â„¬)
    (leftUnitor_naturality := leftUnitor_naturality ğ’¯ â„¬)
    (rightUnitor_naturality := rightUnitor_naturality ğ’¯ â„¬)
    (associator_naturality := associator_naturality â„¬)
#align category_theory.monoidal_of_chosen_finite_products CategoryTheory.monoidalOfChosenFiniteProducts

namespace MonoidalOfChosenFiniteProducts

open MonoidalCategory

/-- A type synonym for `C` carrying a monoidal category structure corresponding to
a fixed choice of limit data for the empty functor, and for `pair X Y` for every `X Y : C`.

This is an implementation detail for `SymmetricOfChosenFiniteProducts`.
-/
-- Porting note(#5171): linter `has_nonempty_instance` not ported yet
-- @[nolint has_nonempty_instance]
@[nolint unusedArguments]
def MonoidalOfChosenFiniteProductsSynonym (_ğ’¯ : LimitCone (Functor.empty.{0} C))
    (_â„¬ : âˆ€ X Y : C, LimitCone (pair X Y)) :=
  C
#align category_theory.monoidal_of_chosen_finite_products.monoidal_of_chosen_finite_products_synonym CategoryTheory.MonoidalOfChosenFiniteProducts.MonoidalOfChosenFiniteProductsSynonym

instance : Category (MonoidalOfChosenFiniteProductsSynonym ğ’¯ â„¬) := by
  dsimp [MonoidalOfChosenFiniteProductsSynonym]
  infer_instance

instance : MonoidalCategory (MonoidalOfChosenFiniteProductsSynonym ğ’¯ â„¬) :=
  monoidalOfChosenFiniteProducts ğ’¯ â„¬

end MonoidalOfChosenFiniteProducts

end

end CategoryTheory
