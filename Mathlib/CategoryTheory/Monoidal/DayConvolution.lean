/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.Monoidal.ExternalProduct.KanExtension
import Mathlib.CategoryTheory.Products.Associator

/-!
# Day convolution monoidal structure

Given functors `F G : C â¥¤ V` between two monoidal categories,
this file defines a typeclass `DayConvolution` on functors `F` `G` that contains
a functor `F âŠ› G`, as well as the required data to exhibit `F âŠ› G` as a pointwise
left Kan extension of `F âŠ  G` (see `CategoryTheory/Monoidal/ExternalProduct` for the definition)
along the tensor product of `C`. Such a functor is called a Day convolution of `F` and `G`, and
although we do not show it yet, this operation defines a monoidal structure on `C â¥¤ V`.

We also define a typeclass `DayConvolutionUnit` on a functor `U : C â¥¤ V` that bundle the data
required to make it a unit for the Day convolution monoidal structure: said data is that of
a map `ğŸ™_ V âŸ¶ U.obj (ğŸ™_ C)` that exhibits `U` as a pointwise left Kan extension of
`fromPUnit (ğŸ™_ V)` along `fromPUnit (ğŸ™_ C)`.

## References
- [nLab page: Day convolution](https://ncatlab.org/nlab/show/Day+convolution)

## TODOs (@robin-carlier)
- Define associators and unitors, prove the pentagon and triangle identities.
- Braided/symmetric case.
- Case where `V` is closed.
- Define a typeclass `DayConvolutionMonoidalCategory` extending `MonoidalCategory`
- Characterization of lax monoidal functors out of a day convolution monoidal category.
- Case `V = Type u` and its universal property.

-/

universe vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ vâ‚… uâ‚ uâ‚‚ uâ‚ƒ uâ‚„ uâ‚…

namespace CategoryTheory.MonoidalCategory
open scoped ExternalProduct

noncomputable section

variable {C : Type uâ‚} [Category.{vâ‚} C] {V : Type uâ‚‚} [Category.{vâ‚‚} V]
  [MonoidalCategory C] [MonoidalCategory V]

/-- A `DayConvolution` structure on functors `F G : C â¥¤ V` is the data of
a functor `F âŠ› G : C â¥¤ V`, along with a unit `F âŠ  G to tensor C â‹™ F âŠ› G`
that exhibits this functor as a pointwise left Kan extension of `F âŠ  G` along
`tensor C`. This is a `class` used to prove various property of such extensions,
but registering global instances of this class is probably a bad idea. -/
class DayConvolution (F G : C â¥¤ V) where
  /-- The chosen convolution between the functors. Denoted `F âŠ› G`. -/
  convolution : C â¥¤ V
  /-- The chosen convolution between the functors. -/
  unit (F) (G) : F âŠ  G âŸ¶ tensor C â‹™ convolution
  /-- The transformation `unit` exhibits `F âŠ› G` as a pointwise left Kan extension
  of `F âŠ  G` along `tensor C`. -/
  unitPointwiseKan (F G) :
    (Functor.LeftExtension.mk (convolution) unit).IsPointwiseLeftKanExtension

namespace DayConvolution

section

/-- A notation for the Day convolution of two functors. -/
scoped infixr:80 " âŠ› " => convolution

variable (F G : C â¥¤ V)

instance leftKanExtension [DayConvolution F G] :
    (F âŠ› G).IsLeftKanExtension (unit F G) :=
  unitPointwiseKan F G|>.isLeftKanExtension

variable {F G}

/-- Two day convolution structures on the same functors gives an isomorphic functor. -/
def uniqueUpToIso (h : DayConvolution F G) (h' : DayConvolution F G) :
    h.convolution â‰… h'.convolution :=
  Functor.leftKanExtensionUnique h.convolution h.unit h'.convolution h'.unit

@[simp]
lemma uniqueUpToIso_hom_unit (h : DayConvolution F G) (h' : DayConvolution F G) :
    h.unit â‰« CategoryTheory.whiskerLeft (tensor C) (h.uniqueUpToIso h').hom = h'.unit := by
  simp [uniqueUpToIso]

@[simp]
lemma uniqueUpToIso_inv_unit (h : DayConvolution F G) (h' : DayConvolution F G) :
    h'.unit â‰« CategoryTheory.whiskerLeft (tensor C) (h.uniqueUpToIso h').inv = h.unit := by
  simp [uniqueUpToIso]

variable (F G) [DayConvolution F G]

section unit

variable {x x' y y' : C}

@[reassoc (attr := simp)]
lemma unit_naturality (f : x âŸ¶ x') (g : y âŸ¶ y') :
    (F.map f âŠ— G.map g) â‰« (unit F G).app (x', y') =
    (unit F G).app (x, y) â‰« (F âŠ› G).map (f âŠ— g) := by
  simpa [tensorHom_def] using (unit F G).naturality ((f, g) : (x, y) âŸ¶ (x', y'))

variable (y) in
@[reassoc (attr := simp)]
lemma unit_naturality_id_right (f : x âŸ¶ x') :
    F.map f â–· G.obj y â‰« (unit F G).app (x', y) =
    (unit F G).app (x, y) â‰« (F âŠ› G).map (f â–· y) := by
  simpa [tensorHom_def] using (unit F G).naturality ((f, ğŸ™ _) : (x, y) âŸ¶ (x', y))

variable (x) in
@[reassoc (attr := simp)]
lemma unit_naturality_id_left (g : y âŸ¶ y') :
    F.obj x â— G.map g â‰« (unit F G).app (x, y') =
    (unit F G).app (x, y) â‰« (F âŠ› G).map (x â— g) := by
  simpa [tensorHom_def] using (unit F G).naturality ((ğŸ™ _, g) : (x, y) âŸ¶ (x, y'))

end unit

variable {F G}

section map

variable {F' G' : C â¥¤ V} [DayConvolution F' G']

/-- The morphism between day convolutions (provided they exist) induced by a pair of morphisms. -/
def map (f : F âŸ¶ F') (g : G âŸ¶ G') : F âŠ› G âŸ¶ F' âŠ› G' :=
  Functor.descOfIsLeftKanExtension (F âŠ› G) (unit F G) (F' âŠ› G') <|
    (externalProductBifunctor C C V).map ((f, g) : (F, G) âŸ¶ (F', G')) â‰« unit F' G'

variable (f : F âŸ¶ F') (g : G âŸ¶ G') (x y : C)

@[reassoc (attr := simp)]
lemma map_unit_app :
  (unit F G).app (x, y) â‰« (map f g).app (x âŠ— y : C) =
    (f.app x âŠ— g.app y) â‰« (unit F' G').app (x, y) := by
  simpa [tensorHom_def] using
    (Functor.descOfIsLeftKanExtension_fac_app (F âŠ› G) (unit F G) (F' âŠ› G') <|
      (externalProductBifunctor C C V).map ((f, g) : (F, G) âŸ¶ (F', G')) â‰« unit F' G') (x, y)

end map

variable (F G)
/-- The universal property of left Kan extensions characterizes the functor
corepresented by `F âŠ› G`. -/
@[simps!]
def corepresentableIso : coyoneda.obj (.op <| F âŠ› G) â‰…
    (whiskeringLeft _ _ _).obj (tensor C) â‹™ coyoneda.obj (.op <| F âŠ  G) :=
  NatIso.ofComponents
    (fun H â†¦ Equiv.toIso <| Functor.homEquivOfIsLeftKanExtension _ (unit F G) _)

/-- The universal property of left Kan extensions characterizes the functor
corepresented by `F âŠ› G`. -/
def corepresentable :
    (whiskeringLeft _ _ _).obj (tensor C) â‹™ coyoneda.obj (.op <| F âŠ  G)|>.CorepresentableBy
      (F âŠ› G) :=
  Functor.corepresentableByEquiv.symm <| corepresentableIso F G

/-- Use the fact that `(F âŠ› G).obj c` is a colimit to characterize morphisms out of it at a
point. -/
theorem convolution_hom_ext_at (c : C) {v : V} {f g : (F âŠ› G).obj c âŸ¶ v}
    (h : âˆ€ {x y : C} (u : x âŠ— y âŸ¶ c),
      (unit F G).app (x, y) â‰« (F âŠ› G).map u â‰« f = (unit F G).app (x, y) â‰« (F âŠ› G).map u â‰« g) :
    f = g :=
  (unitPointwiseKan F G c).hom_ext (by simpa using h Â·.hom)


section associator

variable (H : C â¥¤ V)
    [DayConvolution G H]
    [DayConvolution F (G âŠ› H)]
    [DayConvolution (F âŠ› G) H]
    [âˆ€ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (tensor C) d) (tensorLeft v)]
    [âˆ€ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (tensor C) d) (tensorRight v)]

open MonoidalCategory.ExternalProduct

instance : (F âŠ  G âŠ› H).IsLeftKanExtension <|
    extensionUnitRight (G âŠ› H) (unit G H) F :=
  (pointwiseLeftKanExtensionRight _ _ _ <| unitPointwiseKan G H).isLeftKanExtension

instance : ((F âŠ› G) âŠ  H).IsLeftKanExtension <|
    extensionUnitLeft (F âŠ› G) (unit F G) H :=
  (pointwiseLeftKanExtensionLeft _ _ _ <| unitPointwiseKan F G).isLeftKanExtension

/-- An auxiliary equivalence used to build the associators,
characterizing morphism out of `F âŠ› G âŠ› H` via the universal property of Kan extensions.
-/
@[simps!]
noncomputable def corepresentableIsoâ‚‚ :
    coyoneda.obj (.op <| F âŠ› G âŠ› H) â‰…
    (whiskeringLeft _ _ _).obj (tensor C) â‹™
      (whiskeringLeft _ _ _).obj ((ğŸ­ C).prod (tensor C)) â‹™
      coyoneda.obj (.op <| F âŠ  G âŠ  H) :=
  calc
    _ â‰… (whiskeringLeft _ _ _).obj (tensor C) â‹™ coyoneda.obj (.op <| F âŠ  (G âŠ› H)) :=
      corepresentableIso F (G âŠ› H)
    _ â‰… _ := NatIso.ofComponents
      (fun _ â†¦ Equiv.toIso <| Functor.homEquivOfIsLeftKanExtension _
        (extensionUnitRight (G âŠ› H) (unit G H) F) _)

/-- An auxiliary equivalence used to build the associators,
characterizing morphism out of `F âŠ› G âŠ› H` via the universal property of Kan extensions.
-/
@[simps!]
noncomputable def corepresentableIsoâ‚‚' :
    coyoneda.obj (.op <| (F âŠ› G) âŠ› H) â‰…
    (whiskeringLeft _ _ _).obj (tensor C) â‹™
      (whiskeringLeft _ _ _).obj ((tensor C).prod (ğŸ­ C)) â‹™
      coyoneda.obj (.op <| (F âŠ  G) âŠ  H) :=
  calc
    _ â‰… (whiskeringLeft _ _ _).obj (tensor C) â‹™ coyoneda.obj (.op <| (F âŠ› G) âŠ  H) :=
      corepresentableIso (F âŠ› G) H
    _ â‰… _ := NatIso.ofComponents
      (fun _ â†¦ Equiv.toIso <| Functor.homEquivOfIsLeftKanExtension _
        (extensionUnitLeft (F âŠ› G) (unit F G) H) _)

/-- The `CorepresentableBy` structure on `F âŠ  G âŠ  H âŸ¶ (ğŸ­ C).prod (tensor C) â‹™ tensor C â‹™ -`
derived from `tensorCorepresentableIsoâ‚‚`. -/
def corepresentableâ‚‚ :
    (whiskeringLeft _ _ _).obj (tensor C) â‹™
      (whiskeringLeft _ _ _).obj ((ğŸ­ C).prod (tensor C)) â‹™
      coyoneda.obj (.op <| F âŠ  G âŠ  H)|>.CorepresentableBy (F âŠ› G âŠ› H) :=
  Functor.corepresentableByEquiv.symm (corepresentableIsoâ‚‚ F G H)

/-- The `CorepresentableBy` structure on `(F âŠ  G) âŠ  H âŸ¶ (tensor C).prod (ğŸ­ C) â‹™ tensor C â‹™ -`
derived from `tensorCorepresentableIsoâ‚‚`. -/
def corepresentableâ‚‚' :
    (whiskeringLeft _ _ _).obj (tensor C) â‹™
      (whiskeringLeft _ _ _).obj ((tensor C).prod (ğŸ­ C)) â‹™
      coyoneda.obj (.op <| (F âŠ  G) âŠ  H)|>.CorepresentableBy ((F âŠ› G) âŠ› H) :=
  Functor.corepresentableByEquiv.symm (corepresentableIsoâ‚‚' F G H)

/-- The isomorphism of functors between
`((F âŠ  G) âŠ  H âŸ¶ (tensor C).prod (ğŸ­ C) â‹™ tensor C â‹™ -)` and
`(F âŠ  G âŠ  H âŸ¶ (ğŸ­ C).prod (tensor C) â‹™ tensor C â‹™ -)` that copresents the associator isomorphism
for Day convolution. -/
@[simps!]
def associatorCorepresentingIso :
    (whiskeringLeft _ _ _).obj (tensor C) â‹™
      (whiskeringLeft _ _ _).obj ((tensor C).prod (ğŸ­ C)) â‹™
      coyoneda.obj (.op <| (F âŠ  G) âŠ  H) â‰…
    (whiskeringLeft _ _ _).obj (tensor C) â‹™
      (whiskeringLeft _ _ _).obj ((ğŸ­ C).prod (tensor C)) â‹™
      coyoneda.obj (.op <| F âŠ  G âŠ  H) :=
  calc
    _ â‰… (whiskeringLeft _ _ _).obj (tensor C) â‹™
          (whiskeringLeft _ _ _).obj ((tensor C).prod (ğŸ­ C)) â‹™
          (whiskeringLeft _ _ _).obj (prod.associativity C C C).inverse â‹™
          coyoneda.obj (.op <| (prod.associativity C C C).inverse â‹™ (F âŠ  G) âŠ  H) :=
      isoWhiskerLeft _ (isoWhiskerLeft _
        (NatIso.ofComponents fun _ â†¦ Equiv.toIso <|
          (prod.associativity C C C).congrLeft.fullyFaithfulFunctor.homEquiv))
    _ â‰… (whiskeringLeft _ _ _).obj
            ((prod.associativity C C C).inverse â‹™ (tensor C).prod (ğŸ­ C) â‹™ tensor C) â‹™
          coyoneda.obj (.op <| (prod.associativity C C C).inverse â‹™ (F âŠ  G) âŠ  H) :=
      .refl _
    _ â‰… (whiskeringLeft _ _ _).obj ((ğŸ­ C).prod (tensor C) â‹™ tensor C) â‹™
          coyoneda.obj (.op <| (prod.associativity C C C).inverse â‹™ (F âŠ  G) âŠ  H) :=
      isoWhiskerRight ((whiskeringLeft _ _ _).mapIso <| NatIso.ofComponents (fun _ â†¦ Î±_ _ _ _)) _
    _ â‰… (whiskeringLeft _ _ _).obj ((ğŸ­ C).prod (tensor C) â‹™ tensor C) â‹™
          coyoneda.obj (.op <| F âŠ  G âŠ  H) :=
      isoWhiskerLeft _ <|
        coyoneda.mapIso <| Iso.op <| NatIso.ofComponents (fun _ â†¦ Î±_ _ _ _|>.symm)

/-- The asociator morphism for Day convolution -/
def associator : (F âŠ› G) âŠ› H â‰… F âŠ› G âŠ› H :=
  corepresentableâ‚‚' F G H|>.ofIso (associatorCorepresentingIso F G H)|>.uniqueUpToIso <|
    corepresentableâ‚‚ F G H

/-- Characterizing the forward direction of the associator isomorphism
with respect to the unit transformations. -/
@[reassoc (attr := simp)]
lemma associator_hom_unit_unit (x y z : C) :
    (unit F G).app (x, y) â–· (H.obj z) â‰«
      (unit (F âŠ› G) H).app (x âŠ— y, z) â‰«
      (associator F G H).hom.app ((x âŠ— y) âŠ— z) =
    (Î±_ _ _ _).hom â‰«
      (F.obj x â— (unit G H).app (y, z)) â‰«
      (unit F (G âŠ› H)).app (x, y âŠ— z) â‰«
      (F âŠ› G âŠ› H).map (Î±_ _ _ _).inv := by
  letI := congrArg (fun t â†¦ t.app ((x, y), z)) <|
      (corepresentableIsoâ‚‚' F G H).app (F âŠ› (G âŠ› H))|>.toEquiv.rightInverse_symm <|
        (corepresentableâ‚‚ F G H|>.ofIso
          (associatorCorepresentingIso F G H).symm|>.homEquiv (ğŸ™ _))
  dsimp [associator, Coyoneda.fullyFaithful, corepresentableâ‚‚,
    corepresentableâ‚‚', Functor.CorepresentableBy.ofIso, corepresentableâ‚‚,
    Functor.corepresentableByEquiv, associatorCorepresentingIso] at this âŠ¢
  simp only [Category.assoc, corepresentableIsoâ‚‚'_hom_app_app] at this
  simp only [Category.assoc, this]
  simp [Functor.FullyFaithful.homEquiv, Equivalence.fullyFaithfulFunctor, prod.associativity]

/-- Characterizing associator_inv with respect to the unit transformations -/
@[reassoc (attr := simp)]
lemma associator_inv_unit_unit (x y z : C) :
    F.obj x â— (unit G H).app (y, z) â‰«
      (unit F (G âŠ› H)).app (x, y âŠ— z) â‰«
      (associator F G H).inv.app (x âŠ— y âŠ— z) =
    (Î±_ (F.obj x) (G.obj y) (H.obj z)).inv â‰« (unit F G).app (x, y) â–· H.obj z â‰«
      (unit (F âŠ› G) H).app (x âŠ— y, z) â‰«
      ((F âŠ› G) âŠ› H).map (Î±_ x y z).hom := by
  letI := congrArg (fun t â†¦ t.app (x, y, z)) <|
      (corepresentableIsoâ‚‚ F G H).app ((F âŠ› G) âŠ› H)|>.toEquiv.rightInverse_symm <|
        (corepresentableâ‚‚' F G H|>.ofIso
          (associatorCorepresentingIso F G H)|>.homEquiv (ğŸ™ _))
  dsimp [associator, Coyoneda.fullyFaithful, corepresentableâ‚‚,
    corepresentableâ‚‚', Functor.CorepresentableBy.ofIso, corepresentableâ‚‚,
    Functor.corepresentableByEquiv, associatorCorepresentingIso] at this âŠ¢
  simp only [Category.assoc, corepresentableIsoâ‚‚_hom_app_app] at this
  simp only [Category.assoc, this]
  simp [Functor.FullyFaithful.homEquiv, Equivalence.fullyFaithfulFunctor, prod.associativity]

variable {F G H} in
theorem associator_naturality {F' G' H' : C â¥¤ V}
  [DayConvolution F' G'] [DayConvolution G' H']
  [DayConvolution F' (G' âŠ› H')] [DayConvolution (F' âŠ› G') H']
  (f : F âŸ¶ F') (g : G âŸ¶ G') (h : H âŸ¶ H') :
    map (map f g) h â‰«
      (associator F' G' H').hom =
    (associator F G H).hom â‰« map f (map g h) := by
  apply (corepresentableIsoâ‚‚' F G H).app (F' âŠ› G' âŠ› H')|>.toEquiv.injective
  dsimp
  ext
  simp only [externalProductBifunctor_obj_obj, whiskeringLeft_obj_obj, Functor.comp_obj,
    Functor.prod_obj, tensor_obj, Functor.id_obj, corepresentableIsoâ‚‚'_hom_app_app,
    NatTrans.comp_app, map_unit_app_assoc]
  rw [associator_hom_unit_unit_assoc]
  simp only [tensorHom_def, Category.assoc, externalProductBifunctor_obj_obj, tensor_obj,
    NatTrans.naturality, map_unit_app_assoc]
  rw  [â† comp_whiskerRight_assoc, map_unit_app]
  simp only [Functor.comp_obj, tensor_obj, comp_whiskerRight, Category.assoc]
  rw [â† whisker_exchange_assoc, associator_hom_unit_unit, whisker_exchange_assoc,
    â† MonoidalCategory.whiskerLeft_comp_assoc, map_unit_app]
  simp [tensorHom_def]

section pentagon

variable [âˆ€ (v : V) (d : C Ã— C),
    Limits.PreservesColimitsOfShape (CostructuredArrow ((tensor C).prod (ğŸ­ C)) d) (tensorRight v)]

lemma pentagon (H K : C â¥¤ V)
    [DayConvolution G H] [DayConvolution (F âŠ› G) H] [DayConvolution F (G âŠ› H)]
    [DayConvolution H K] [DayConvolution G (H âŠ› K)] [DayConvolution (G âŠ› H) K]
    [DayConvolution ((F âŠ› G) âŠ› H) K] [DayConvolution (F âŠ› G) (H âŠ› K)]
    [DayConvolution (F âŠ› G âŠ› H) K] [DayConvolution F  (G âŠ› H âŠ› K)]
    [DayConvolution F ((G âŠ› H) âŠ› K)] :
    map (associator F G H).hom (ğŸ™ K) â‰«
        (associator F (G âŠ› H) K).hom â‰« map (ğŸ™ F) (associator G H K).hom =
      (associator (F âŠ› G) H K).hom â‰« (associator F G (H âŠ› K)).hom := by
  -- We repeatedly apply the fact that the functors are left Kan extended
  apply Functor.hom_ext_of_isLeftKanExtension (Î± := unit ((F âŠ› G) âŠ› H) K)
  apply Functor.hom_ext_of_isLeftKanExtension
    (Î± := extensionUnitLeft ((F âŠ› G) âŠ› H) (unit (F âŠ› G) H) K)
  letI : (((F âŠ› G) âŠ  H) âŠ  K).IsLeftKanExtension
    (Î± := extensionUnitLeft ((F âŠ› G) âŠ  H)
      (extensionUnitLeft _ (unit F G) H) K) :=
    pointwiseLeftKanExtensionLeft _ _ _
      (pointwiseLeftKanExtensionLeft _ _ _ (unitPointwiseKan F G))|>.isLeftKanExtension
  apply Functor.hom_ext_of_isLeftKanExtension (Î± := extensionUnitLeft ((F âŠ› G) âŠ  H)
      (extensionUnitLeft _ (unit F G) H) K)
  -- And then we compute...
  ext âŸ¨âŸ¨âŸ¨i, jâŸ©, kâŸ©, lâŸ©
  have aux :
      ((unit F G).app (i, j) âŠ— (unit H K).app (k, l)) â‰«
        (unit (F âŠ› G) (H âŠ› K)).app ((i âŠ— j), (k âŠ— l)) =
      (Î±_ (F.obj i) (G.obj j) (H.obj k âŠ— K.obj l)).hom â‰«
        F.obj i â— (G.obj j â— (unit H K).app (k, l)) â‰« F.obj i â— (unit G (H âŠ› K)).app (j, (k âŠ— l)) â‰«
        (unit F (G âŠ› H âŠ› K)).app (i, (j âŠ— k âŠ— l)) â‰« (F âŠ› G âŠ› H âŠ› K).map (Î±_ i j (k âŠ— l)).inv â‰«
        (associator F G (H âŠ› K)).inv.app ((i âŠ— j) âŠ— k âŠ— l) := by
    conv_rhs => simp only [Functor.comp_obj, tensor_obj, NatTrans.naturality,
      associator_inv_unit_unit_assoc, externalProductBifunctor_obj_obj, Iso.map_hom_inv_id,
      Category.comp_id]
    simp only [tensor_whiskerLeft_symm, Category.assoc, Iso.hom_inv_id_assoc,
    â† tensorHom_def'_assoc]
  dsimp
  simp only [MonoidalCategory.whiskerLeft_id, Category.comp_id, map_unit_app_assoc,
    externalProductBifunctor_obj_obj, NatTrans.id_app, tensorHom_id, associator_hom_unit_unit_assoc,
    tensor_obj, NatTrans.naturality]
  conv_rhs =>
    simp only [whiskerRight_tensor_symm_assoc, Iso.inv_hom_id_assoc, â† tensorHom_def_assoc]
    rw [reassoc_of% aux]
  simp only [Iso.inv_hom_id_app_assoc, â† comp_whiskerRight_assoc, associator_hom_unit_unit F G H]
  simp only [Functor.comp_obj, tensor_obj, comp_whiskerRight, whisker_assoc, Category.assoc,
    reassoc_of% unit_naturality_id_right (F âŠ› G âŠ› H) K l (Î±_ i j k).inv, NatTrans.naturality_assoc,
    NatTrans.naturality, associator_hom_unit_unit_assoc, externalProductBifunctor_obj_obj,
    tensor_obj, NatTrans.naturality_assoc, map_unit_app_assoc, NatTrans.id_app,
    id_tensorHom, Iso.inv_hom_id_assoc, â† MonoidalCategory.whiskerLeft_comp_assoc,
    associator_hom_unit_unit]
  simp [â† Functor.map_comp, reassoc_of% unit_naturality_id_left F (G âŠ› H âŠ› K) i (Î±_ j k l).inv,
    pentagon_inv, pentagon_assoc]

end pentagon

end associator

end

end DayConvolution

/-- A dayConvolutionUnit structure on a functor `C â¥¤ V` is the data of a pointwise
left Kan extension of `fromPUnit (ğŸ™_ V)` along `fromPUnit (ğŸ™_ C)`. Again, this is
made a class to ease proofs when constructing `DayConvolutionMonoidalCategory` structures, but one
should avoid registering it globally. -/
class DayConvolutionUnit (F : C â¥¤ V) where
  /-- A "canonical" structure map `ğŸ™_ V âŸ¶ F.obj (ğŸ™_ C)` that defines a natural transformation
  `fromPUnit (ğŸ™_ V) âŸ¶ fromPUnit (ğŸ™_ C) â‹™ F`. -/
  can : ğŸ™_ V âŸ¶ F.obj (ğŸ™_ C)
  /-- The canonical map `ğŸ™_ V âŸ¶ F.obj (ğŸ™_ C)` exhibits `F` as a pointwise left kan extension
  of `fromPUnit.{0} ğŸ™_ V` along `fromPUnit.{0} ğŸ™_ C`. -/
  canPointwiseLeftKanExtension : Functor.LeftExtension.mk F
    ({app _ := can} : Functor.fromPUnit.{0} (ğŸ™_ V) âŸ¶
      Functor.fromPUnit.{0} (ğŸ™_ C) â‹™ F)|>.IsPointwiseLeftKanExtension

namespace DayConvolutionUnit

variable (U : C â¥¤ V) [DayConvolutionUnit U]
open scoped DayConvolution
open ExternalProduct

/-- A shorthand for the natural transformation of functors out of PUnit defined by
the canonical morphism `ğŸ™_ V âŸ¶ U.obj (ğŸ™_ C)` when `U` is a unit for Day convolution. -/
abbrev Ï† : Functor.fromPUnit.{0} (ğŸ™_ V) âŸ¶ Functor.fromPUnit.{0} (ğŸ™_ C) â‹™ U where
  app _ := can

/-- Since a convolution unit is a pointwise left Kan extension, maps out of it at
any object are uniquely characterized. -/
lemma hom_ext {c : C} {v : V} {g h : U.obj c âŸ¶ v}
    (e : âˆ€ f : ğŸ™_ C âŸ¶ c, can â‰« U.map f â‰« g = can â‰« U.map f â‰« h) :
    g = h := by
  apply (canPointwiseLeftKanExtension c).hom_ext
  intro j
  simpa using e j.hom

variable (F : C â¥¤ V)
    [âˆ€ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (Functor.fromPUnit (ğŸ™_ C)) d) (tensorLeft v)]
    [âˆ€ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (Functor.fromPUnit (ğŸ™_ C)) d) (tensorRight v)]
    -- [âˆ€ v : V, Limits.PreservesColimitsOfSize.{0, vâ‚} (tensorRight v)]

instance : (F âŠ  U).IsLeftKanExtension <| extensionUnitRight U (Ï† U) F :=
  pointwiseLeftKanExtensionRight U (Ï† U) F canPointwiseLeftKanExtension|>.isLeftKanExtension

instance : (U âŠ  F).IsLeftKanExtension <| extensionUnitLeft U (Ï† U) F :=
  pointwiseLeftKanExtensionLeft U (Ï† U) F canPointwiseLeftKanExtension|>.isLeftKanExtension

/-- An isomorphism that computes the functor corepresented by `U âŠ› F` using the
fact that `U âŠ  F` is left Kan extended from `(fromPUnit ğŸ™_ V) âŠ  F`. -/
@[simps!]
def corepresentableIsoLeft [DayConvolution U F] :
    coyoneda.obj (.op <| U âŠ› F) â‰…
    (whiskeringLeft _ _ _).obj (tensor C) â‹™
      (whiskeringLeft _ _ _).obj ((Functor.fromPUnit.{0} (ğŸ™_ C)).prod (ğŸ­ C)) â‹™
      coyoneda.obj (.op <| Functor.fromPUnit.{0} (ğŸ™_ V) âŠ  F) :=
  NatIso.ofComponents
    (fun H â†¦ Equiv.toIso <|
      calc
        _ â‰ƒ (U âŠ  F âŸ¶ (tensor C) â‹™ H) :=
          Functor.homEquivOfIsLeftKanExtension _ (DayConvolution.unit U F) _
        _ â‰ƒ _ :=
          Functor.homEquivOfIsLeftKanExtension _ (extensionUnitLeft U (Ï† U) F) (tensor C â‹™ H))

/-- The `CorepresentableBy` structure attached to the isomorphism
`convolutionUnitCorepresentableIsoLeft`. -/
def corepresentableLeft [DayConvolution U F] :
    (whiskeringLeft _ _ _).obj (tensor C) â‹™
      (whiskeringLeft _ _ _).obj ((Functor.fromPUnit.{0} (ğŸ™_ C)).prod (ğŸ­ C)) â‹™
      coyoneda.obj (.op <| Functor.fromPUnit.{0} (ğŸ™_ V) âŠ  F)|>.CorepresentableBy (U âŠ› F) :=
  Functor.corepresentableByEquiv.symm <| corepresentableIsoLeft U F

/-- An isomorphism that computes the functor corepresented by `U âŠ› F` using the
fact that `F âŠ  U` is left Kan extended from `F âŠ  (fromPUnit ğŸ™_ V)`. -/
@[simps!]
def corepresentableIsoRight [DayConvolution F U] :
    coyoneda.obj (.op <| F âŠ› U) â‰…
    (whiskeringLeft _ _ _).obj (tensor C) â‹™
      (whiskeringLeft _ _ _).obj ((ğŸ­ C).prod (Functor.fromPUnit.{0} (ğŸ™_ C))) â‹™
      coyoneda.obj (.op <| F âŠ  Functor.fromPUnit.{0} (ğŸ™_ V)) :=
  NatIso.ofComponents
    (fun H â†¦ Equiv.toIso <|
      calc
        _ â‰ƒ (F âŠ  U âŸ¶ (tensor C) â‹™ H) :=
          Functor.homEquivOfIsLeftKanExtension _ (DayConvolution.unit F U) _
        _ â‰ƒ _ :=
          Functor.homEquivOfIsLeftKanExtension _ (extensionUnitRight U (Ï† U) F) (tensor C â‹™ H))

/-- The `CorepresentableBy` structure attached to the isomorphism
`convolutionUnitCorepresentableIsoRight`. -/
def corepresentableRight [DayConvolution F U] :
    (whiskeringLeft _ _ _).obj (tensor C) â‹™
      (whiskeringLeft _ _ _).obj ((ğŸ­ C).prod (Functor.fromPUnit.{0} (ğŸ™_ C))) â‹™
      coyoneda.obj (.op <| F âŠ  Functor.fromPUnit.{0} (ğŸ™_ V))|>.CorepresentableBy (F âŠ› U) :=
  Functor.corepresentableByEquiv.symm <| corepresentableIsoRight U F

/-- The isomorphism of corepresentable functors that defines the left unitor for
Day convolution. -/
@[simps!]
def leftUnitorCorepresentingIso :
    (whiskeringLeft _ _ _).obj (tensor C) â‹™
      (whiskeringLeft _ _ _).obj ((Functor.fromPUnit.{0} (ğŸ™_ C)).prod (ğŸ­ C)) â‹™
      coyoneda.obj (.op <| Functor.fromPUnit.{0} (ğŸ™_ V) âŠ  F) â‰…
    coyoneda.obj (.op <| F) := by
  calc
    _ â‰… (whiskeringLeft _ _ _).obj (tensor C) â‹™
          (whiskeringLeft _ _ _).obj ((Functor.fromPUnit.{0} (ğŸ™_ C)).prod (ğŸ­ C)) â‹™
          (whiskeringLeft _ _ _).obj (prod.leftUnitorEquivalence C).inverse â‹™
          coyoneda.obj (.op <|
           (prod.leftUnitorEquivalence C).inverse â‹™ Functor.fromPUnit.{0} (ğŸ™_ V) âŠ  F) :=
      isoWhiskerLeft _ (isoWhiskerLeft _
        (NatIso.ofComponents fun _ â†¦ Equiv.toIso <|
          (prod.leftUnitorEquivalence C).congrLeft.fullyFaithfulFunctor.homEquiv))
    _ â‰… (whiskeringLeft _ _ _).obj
            ((prod.leftUnitorEquivalence C).inverse â‹™ (Functor.fromPUnit.{0} (ğŸ™_ C)).prod (ğŸ­ C) â‹™
              tensor C) â‹™
          coyoneda.obj (.op <|
            (prod.leftUnitorEquivalence C).inverse â‹™ Functor.fromPUnit.{0} (ğŸ™_ V) âŠ  F) :=
      .refl _
    _ â‰… (whiskeringLeft _ _ _).obj (ğŸ­ _) â‹™ coyoneda.obj (.op <|
          (prod.leftUnitorEquivalence C).inverse â‹™ Functor.fromPUnit.{0} (ğŸ™_ V) âŠ  F) :=
      isoWhiskerRight ((whiskeringLeft _ _ _).mapIso <| NatIso.ofComponents fun _ â†¦ Î»_ _) _
    _ â‰… _ := coyoneda.mapIso <| Iso.op <| NatIso.ofComponents fun _ â†¦ (Î»_ _).symm

/-- The isomorphism of corepresentable functors that defines the right unitor for
Day convolution. -/
@[simps!]
def rightUnitorCorepresentingIso :
    (whiskeringLeft _ _ _).obj (tensor C) â‹™
      (whiskeringLeft _ _ _).obj ((ğŸ­ C).prod (Functor.fromPUnit.{0} (ğŸ™_ C))) â‹™
      coyoneda.obj (.op <| F âŠ  Functor.fromPUnit.{0} (ğŸ™_ V)) â‰…
    coyoneda.obj (.op <| F) := by
  calc
    _ â‰… (whiskeringLeft _ _ _).obj (tensor C) â‹™
          (whiskeringLeft _ _ _).obj ((ğŸ­ C).prod (Functor.fromPUnit.{0} (ğŸ™_ C))) â‹™
          (whiskeringLeft _ _ _).obj (prod.rightUnitorEquivalence C).inverse â‹™
          coyoneda.obj (.op <|
           (prod.rightUnitorEquivalence C).inverse â‹™ F âŠ  Functor.fromPUnit.{0} (ğŸ™_ V)) :=
      isoWhiskerLeft _ (isoWhiskerLeft _
        (NatIso.ofComponents fun _ â†¦ Equiv.toIso <|
          (prod.rightUnitorEquivalence C).congrLeft.fullyFaithfulFunctor.homEquiv))
    _ â‰… (whiskeringLeft _ _ _).obj
            ((prod.rightUnitorEquivalence C).inverse â‹™
              ((ğŸ­ C).prod (Functor.fromPUnit.{uâ‚} (ğŸ™_ C))) â‹™ tensor C) â‹™
          coyoneda.obj (.op <|
            (prod.rightUnitorEquivalence C).inverse â‹™ F âŠ  Functor.fromPUnit.{0} (ğŸ™_ V)) :=
      .refl _
    _ â‰… (whiskeringLeft _ _ _).obj (ğŸ­ _) â‹™ coyoneda.obj (.op <|
          (prod.rightUnitorEquivalence C).inverse â‹™ F âŠ  Functor.fromPUnit.{0} (ğŸ™_ V)) :=
      isoWhiskerRight ((whiskeringLeft _ _ _).mapIso <| NatIso.ofComponents fun _ â†¦ Ï_ _) _
    _ â‰… _ := coyoneda.mapIso <| Iso.op <| NatIso.ofComponents fun _ â†¦ (Ï_ _).symm

/-- The left unitor isomorphism for Day convolution. -/
def leftUnitor [DayConvolution U F] : U âŠ› F â‰… F :=
  corepresentableLeft U F|>.ofIso (leftUnitorCorepresentingIso F)|>.uniqueUpToIso
    <| Functor.corepresentableByEquiv.symm (.refl _)

/-- The right unitor isomorphism for Day convolution. -/
def rightUnitor [DayConvolution F U] : F âŠ› U â‰… F :=
  corepresentableRight U F|>.ofIso (rightUnitorCorepresentingIso F)|>.uniqueUpToIso
    <| Functor.corepresentableByEquiv.symm (.refl _)

section

omit [âˆ€ (v : V) (d : C), Limits.PreservesColimitsOfShape
  (CostructuredArrow (Functor.fromPUnit (ğŸ™_ C)) d) (tensorLeft v)]
variable [DayConvolution U F]

/-- Characterizing the forward direction of `leftUnitor` via the universal maps. -/
@[reassoc (attr := simp)]
lemma leftUnitor_hom_unit_app (y : C) :
    can â–· F.obj y â‰« (DayConvolution.unit U F).app (ğŸ™_ C, y) â‰«
      (leftUnitor U F).hom.app (ğŸ™_ C âŠ— y) =
    (Î»_ (F.obj y)).hom â‰« F.map ((Î»_ y).inv) := by
  letI := congrArg (fun t â†¦ t.app (.mk PUnit.unit, y)) <|
      (corepresentableIsoLeft U F).app F|>.toEquiv.rightInverse_symm <|
        ((leftUnitorCorepresentingIso F).symm.hom.app (F)) (ğŸ™ _)
  dsimp [leftUnitor, Coyoneda.fullyFaithful, corepresentableLeft, corepresentableIsoLeft,
    leftUnitorCorepresentingIso, Functor.CorepresentableBy.ofIso,
    Functor.corepresentableByEquiv] at this âŠ¢
  simp only [whiskerLeft_id, Category.comp_id] at this
  simp only [Category.comp_id, this]
  simp [prod.leftUnitorEquivalence, Equivalence.congrLeft, Equivalence.fullyFaithfulFunctor,
    Functor.FullyFaithful.homEquiv]

@[simp, reassoc]
lemma leftUnitor_inv_app (x : C) :
    (leftUnitor U F).inv.app x =
    (Î»_ (F.obj x)).inv â‰« can â–· F.obj x â‰« (DayConvolution.unit U F).app (ğŸ™_ C, x) â‰«
      (U âŠ› F).map (Î»_ x).hom := by
  dsimp [leftUnitor, Coyoneda.fullyFaithful, corepresentableLeft, corepresentableIsoLeft,
    leftUnitorCorepresentingIso, Functor.CorepresentableBy.ofIso,
    Functor.corepresentableByEquiv]
  simp [prod.leftUnitorEquivalence, Equivalence.congrLeft, Equivalence.fullyFaithfulFunctor,
    Functor.FullyFaithful.homEquiv]

variable {F} in
@[reassoc (attr := simp)]
lemma leftUnitor_naturality {G : C â¥¤ V} [DayConvolution U G] (f : F âŸ¶ G) :
    DayConvolution.map (ğŸ™ _) f â‰« (leftUnitor U G).hom =
    (leftUnitor U F).hom â‰« f := by
  apply Functor.hom_ext_of_isLeftKanExtension _ (DayConvolution.unit _ _) _
  apply Functor.hom_ext_of_isLeftKanExtension _ (extensionUnitLeft U (Ï† U) F) _
  ext âŸ¨xâ‚, xâ‚‚âŸ©
  simp [â† whisker_exchange_assoc]

end

section

omit [âˆ€ (v : V) (d : C), Limits.PreservesColimitsOfShape
  (CostructuredArrow (Functor.fromPUnit (ğŸ™_ C)) d) (tensorRight v)]
variable [DayConvolution F U]

/-- Characterizing the forward direction of `leftUnitor` via the universal maps. -/
@[reassoc (attr := simp)]
lemma rightUnitor_hom_unit_app (x : C) :
    F.obj x â— can â‰« (DayConvolution.unit F U).app (x, ğŸ™_ C) â‰«
      (rightUnitor U F).hom.app (x âŠ— ğŸ™_ C) =
    (Ï_ _).hom â‰« F.map ((Ï_ x).inv) := by
  letI := congrArg (fun t â†¦ t.app (x, .mk PUnit.unit)) <|
      (corepresentableIsoRight U F).app F|>.toEquiv.rightInverse_symm <|
        ((rightUnitorCorepresentingIso F).symm.hom.app (F)) (ğŸ™ _)
  dsimp [rightUnitor, Coyoneda.fullyFaithful, corepresentableRight, corepresentableIsoRight,
    rightUnitorCorepresentingIso, Functor.CorepresentableBy.ofIso,
    Functor.corepresentableByEquiv] at this âŠ¢
  simp only [MonoidalCategory.whiskerRight_id, Category.id_comp, Iso.hom_inv_id,
    Category.comp_id] at this
  simp only [Category.comp_id, this]
  simp [prod.rightUnitorEquivalence, Equivalence.congrLeft, Equivalence.fullyFaithfulFunctor,
    Functor.FullyFaithful.homEquiv]

@[simp, reassoc]
lemma rightUnitor_inv_app (x : C) :
    (rightUnitor U F).inv.app x =
    (Ï_ (F.obj x)).inv â‰« F.obj x â— can â‰« (DayConvolution.unit F U).app (x, ğŸ™_ C) â‰«
      (F âŠ› U).map (Ï_ x).hom := by
  dsimp [rightUnitor, Coyoneda.fullyFaithful, corepresentableRight, corepresentableIsoRight,
    rightUnitorCorepresentingIso, Functor.CorepresentableBy.ofIso,
    Functor.corepresentableByEquiv]
  simp [prod.rightUnitorEquivalence, Equivalence.congrLeft, Equivalence.fullyFaithfulFunctor,
    Functor.FullyFaithful.homEquiv]

variable {F} in
@[reassoc (attr := simp)]
lemma rightUnitor_naturality {G : C â¥¤ V} [DayConvolution G U] (f : F âŸ¶ G) :
    DayConvolution.map f (ğŸ™ _) â‰« (rightUnitor U G).hom =
    (rightUnitor U F).hom â‰« f := by
  apply Functor.hom_ext_of_isLeftKanExtension _ (DayConvolution.unit _ _) _
  apply Functor.hom_ext_of_isLeftKanExtension _ (extensionUnitRight U (Ï† U) F) _
  ext âŸ¨xâ‚, xâ‚‚âŸ©
  simp [whisker_exchange_assoc]

end

end DayConvolutionUnit

section triangle

open scoped DayConvolution
open DayConvolutionUnit
open ExternalProduct

variable [âˆ€ (v : V) (d : C), Limits.PreservesColimitsOfShape
    (CostructuredArrow (tensor C) d) (tensorLeft v)]
  [âˆ€ (v : V) (d : C), Limits.PreservesColimitsOfShape
    (CostructuredArrow (tensor C) d) (tensorRight v)]
  [âˆ€ (v : V) (d : C), Limits.PreservesColimitsOfShape
    (CostructuredArrow (Functor.fromPUnit <| ğŸ™_ C) d) (tensorLeft v)]
  [âˆ€ (v : V) (d : C), Limits.PreservesColimitsOfShape
    (CostructuredArrow (Functor.fromPUnit <| ğŸ™_ C) d) (tensorRight v)]
  [âˆ€ (v : V) (d : C Ã— C), Limits.PreservesColimitsOfShape
    (CostructuredArrow ((ğŸ­ C).prod <| Functor.fromPUnit.{0} <| ğŸ™_ C) d) (tensorRight v)]

lemma DayConvolution.triangle (F G U : C â¥¤ V) [DayConvolutionUnit U]
    [DayConvolution F U] [DayConvolution U G]
    [DayConvolution F (U âŠ› G)] [DayConvolution (F âŠ› U) G] [DayConvolution F G] :
    (DayConvolution.associator F U G).hom â‰«
      DayConvolution.map (ğŸ™ F) (DayConvolutionUnit.leftUnitor U G).hom =
    DayConvolution.map (DayConvolutionUnit.rightUnitor U F).hom (ğŸ™ G) := by
  apply Functor.hom_ext_of_isLeftKanExtension _ (DayConvolution.unit _ _) _
  apply Functor.hom_ext_of_isLeftKanExtension
    (Î± := extensionUnitLeft (F âŠ› U) (DayConvolution.unit F U) G)
  letI : (F âŠ  U) âŠ  G|>.IsLeftKanExtension
      (Î± := extensionUnitLeft (F âŠ  U) (extensionUnitRight U (DayConvolutionUnit.Ï† U) F) G) :=
    pointwiseLeftKanExtensionLeft (F âŠ  U) _ G
      (pointwiseLeftKanExtensionRight U (DayConvolutionUnit.Ï† U) F <|
        DayConvolutionUnit.canPointwiseLeftKanExtension (F := U))|>.isLeftKanExtension
  apply Functor.hom_ext_of_isLeftKanExtension
    (Î± := extensionUnitLeft (F âŠ  U) (extensionUnitRight U (DayConvolutionUnit.Ï† U) F) G)
  ext âŸ¨âŸ¨x, _âŸ©, yâŸ©
  dsimp
  simp only [MonoidalCategory.whiskerRight_id, Category.id_comp, Iso.hom_inv_id, whisker_assoc,
    MonoidalCategory.whiskerLeft_id, Category.comp_id,
    DayConvolution.associator_hom_unit_unit_assoc, externalProductBifunctor_obj_obj, tensor_obj,
    NatTrans.naturality, DayConvolution.map_unit_app_assoc, NatTrans.id_app, id_tensorHom,
    Category.assoc, Iso.inv_hom_id_assoc, DayConvolution.map_unit_app, Functor.comp_obj,
    tensorHom_id, Iso.cancel_iso_hom_left]
  simp only [â† MonoidalCategory.whiskerLeft_comp_assoc, leftUnitor_hom_unit_app,
    associator_inv_naturality_middle_assoc, â† comp_whiskerRight_assoc, rightUnitor_hom_unit_app]
  simp [â† Functor.map_comp]

end triangle

end

end CategoryTheory.MonoidalCategory
