/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.Monoidal.ExternalProduct.KanExtension
import Mathlib.CategoryTheory.Products.Associator

/-!
# Day convolution monoidal structure

Given functors `F G : C ‚•§ V` between two monoidal categories,
this file defines a typeclass `DayConvolution` on functors `F` `G` that contains
a functor `F ‚äõ G`, as well as the required data to exhibit `F ‚äõ G` as a pointwise
left Kan extension of `F ‚ä† G` (see `Mathlib/CategoryTheory/Monoidal/ExternalProduct/Basic.lean`
for the definition) along the tensor product of `C`.
Such a functor is called a Day convolution of `F` and `G`, and
although we do not show it yet, this operation defines a monoidal structure on `C ‚•§ V`.

We also define a typeclass `DayConvolutionUnit` on a functor `U : C ‚•§ V` that bundle the data
required to make it a unit for the Day convolution monoidal structure: said data is that of
a map `ùüô_ V ‚ü∂ U.obj (ùüô_ C)` that exhibits `U` as a pointwise left Kan extension of
`fromPUnit (ùüô_ V)` along `fromPUnit (ùüô_ C)`.

The main way to assert that a given monoidal category structure on a category `D`
arises as a "Day convolution monoidal structure" is given by the typeclass
`LawfulDayConvolutionMonoidalCategoryStruct C V D`, which bundles the data and
equations needed to exhibit `D` as a monoidal full subcategory of `C ‚•§ V` if
the latter were to have the Day convolution monoidal structure. The definition
`monoidalOfLawfulDayConvolutionMonoidalCategoryStruct` promotes (under suitable
assumptions on `V`) a `LawfulDayConvolutionMonoidalCategoryStruct C V D` to
a monoidal structure.


## References
- [nLab page: Day convolution](https://ncatlab.org/nlab/show/Day+convolution)

## TODOs (@robin-carlier)
- Type alias for `C ‚•§ V` with a `LawfulDayConvolutionMonoidalCategoryStruct`.
- Characterization of lax monoidal functors out of a Day convolution monoidal category.
- Case `V = Type u` and its universal property.

-/

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ v‚ÇÑ v‚ÇÖ u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ u‚ÇÖ

namespace CategoryTheory.MonoidalCategory
open scoped ExternalProduct

noncomputable section

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {V : Type u‚ÇÇ} [Category.{v‚ÇÇ} V]
  [MonoidalCategory C] [MonoidalCategory V]

/-- A `DayConvolution` structure on functors `F G : C ‚•§ V` is the data of
a functor `F ‚äõ G : C ‚•§ V`, along with a unit `F ‚ä† G ‚ü∂ tensor C ‚ãô F ‚äõ G`
that exhibits this functor as a pointwise left Kan extension of `F ‚ä† G` along
`tensor C`. This is a `class` used to prove various property of such extensions,
but registering global instances of this class is probably a bad idea. -/
class DayConvolution (F G : C ‚•§ V) where
  /-- The chosen convolution between the functors. Denoted `F ‚äõ G`. -/
  convolution : C ‚•§ V
  /-- The chosen convolution between the functors. -/
  unit (F) (G) : F ‚ä† G ‚ü∂ tensor C ‚ãô convolution
  /-- The transformation `unit` exhibits `F ‚äõ G` as a pointwise left Kan extension
  of `F ‚ä† G` along `tensor C`. -/
  isPointwiseLeftKanExtensionUnit (F G) :
    (Functor.LeftExtension.mk (convolution) unit).IsPointwiseLeftKanExtension

namespace DayConvolution

open scoped Prod

section

/-- A notation for the Day convolution of two functors. -/
scoped infixr:80 " ‚äõ " => convolution

variable (F G : C ‚•§ V)

instance leftKanExtension [DayConvolution F G] :
    (F ‚äõ G).IsLeftKanExtension (unit F G) :=
  isPointwiseLeftKanExtensionUnit F G |>.isLeftKanExtension

variable {F G}

/-- Two Day convolution structures on the same functors gives an isomorphic functor. -/
def uniqueUpToIso (h : DayConvolution F G) (h' : DayConvolution F G) :
    h.convolution ‚âÖ h'.convolution :=
  Functor.leftKanExtensionUnique h.convolution h.unit h'.convolution h'.unit

@[reassoc (attr := simp)]
lemma unit_uniqueUpToIso_hom (h : DayConvolution F G) (h' : DayConvolution F G) :
    h.unit ‚â´ Functor.whiskerLeft (tensor C) (h.uniqueUpToIso h').hom = h'.unit := by
  simp [uniqueUpToIso]

@[reassoc (attr := simp)]
lemma unit_uniqueUpToIso_inv (h : DayConvolution F G) (h' : DayConvolution F G) :
    h'.unit ‚â´ Functor.whiskerLeft (tensor C) (h.uniqueUpToIso h').inv = h.unit := by
  simp [uniqueUpToIso]

variable (F G) [DayConvolution F G]

section unit

variable {x x' y y' : C}

@[reassoc (attr := simp)]
lemma unit_naturality (f : x ‚ü∂ x') (g : y ‚ü∂ y') :
    (F.map f ‚äó‚Çò G.map g) ‚â´ (unit F G).app (x', y') =
    (unit F G).app (x, y) ‚â´ (F ‚äõ G).map (f ‚äó‚Çò g) := by
  simpa [tensorHom_def] using (unit F G).naturality (f √ó‚Çò g)

variable (y) in
@[reassoc (attr := simp)]
lemma whiskerRight_comp_unit_app (f : x ‚ü∂ x') :
    F.map f ‚ñ∑ G.obj y ‚â´ (unit F G).app (x', y) =
    (unit F G).app (x, y) ‚â´ (F ‚äõ G).map (f ‚ñ∑ y) := by
  simpa [tensorHom_def] using (unit F G).naturality (f √ó‚Çò ùüô _)

variable (x) in
@[reassoc (attr := simp)]
lemma whiskerLeft_comp_unit_app (g : y ‚ü∂ y') :
    F.obj x ‚óÅ G.map g ‚â´ (unit F G).app (x, y') =
    (unit F G).app (x, y) ‚â´ (F ‚äõ G).map (x ‚óÅ g) := by
  simpa [tensorHom_def] using (unit F G).naturality (ùüô _ √ó‚Çò g)

end unit

variable {F G}

section map

variable {F' G' : C ‚•§ V} [DayConvolution F' G']

/-- The morphism between Day convolutions (provided they exist) induced by a pair of morphisms. -/
def map (f : F ‚ü∂ F') (g : G ‚ü∂ G') : F ‚äõ G ‚ü∂ F' ‚äõ G' :=
  Functor.descOfIsLeftKanExtension (F ‚äõ G) (unit F G) (F' ‚äõ G') <|
    (externalProductBifunctor C C V).map (f √ó‚Çò g) ‚â´ unit F' G'

variable (f : F ‚ü∂ F') (g : G ‚ü∂ G') (x y : C)

@[reassoc (attr := simp)]
lemma unit_app_map_app :
    (unit F G).app (x, y) ‚â´ (map f g).app (x ‚äó y : C) =
    (f.app x ‚äó‚Çò g.app y) ‚â´ (unit F' G').app (x, y) := by
  simpa [tensorHom_def] using
    (Functor.descOfIsLeftKanExtension_fac_app (F ‚äõ G) (unit F G) (F' ‚äõ G') <|
      (externalProductBifunctor C C V).map (f √ó‚Çò g) ‚â´ unit F' G') (x, y)

end map

variable (F G)

/-- The universal property of left Kan extensions characterizes the functor
corepresented by `F ‚äõ G`. -/
@[simps!]
def corepresentableBy :
    (Functor.whiskeringLeft _ _ _).obj (tensor C) ‚ãô coyoneda.obj (.op <| F ‚ä† G) |>.CorepresentableBy
      (F ‚äõ G) where
  homEquiv := Functor.homEquivOfIsLeftKanExtension _ (unit F G) _
  homEquiv_comp := by aesop

/-- Use the fact that `(F ‚äõ G).obj c` is a colimit to characterize morphisms out of it at a
point. -/
theorem convolution_hom_ext_at (c : C) {v : V} {f g : (F ‚äõ G).obj c ‚ü∂ v}
    (h : ‚àÄ {x y : C} (u : x ‚äó y ‚ü∂ c),
      (unit F G).app (x, y) ‚â´ (F ‚äõ G).map u ‚â´ f = (unit F G).app (x, y) ‚â´ (F ‚äõ G).map u ‚â´ g) :
    f = g :=
  ((isPointwiseLeftKanExtensionUnit F G) c).hom_ext (fun j ‚Ü¶ by simpa using h j.hom)

section associator

open Functor

variable (H : C ‚•§ V) [DayConvolution G H] [DayConvolution F (G ‚äõ H)] [DayConvolution (F ‚äõ G) H]
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (tensor C) d) (tensorLeft v)]
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (tensor C) d) (tensorRight v)]

open MonoidalCategory.ExternalProduct

instance : (F ‚ä† G ‚äõ H).IsLeftKanExtension <|
    extensionUnitRight (G ‚äõ H) (unit G H) F :=
  (isPointwiseLeftKanExtensionExtensionUnitRight _ _ _ <|
    isPointwiseLeftKanExtensionUnit G H).isLeftKanExtension

instance : ((F ‚äõ G) ‚ä† H).IsLeftKanExtension <|
    extensionUnitLeft (F ‚äõ G) (unit F G) H :=
  (isPointwiseLeftKanExtensionExtensionUnitLeft _ _ _ <|
    isPointwiseLeftKanExtensionUnit F G).isLeftKanExtension

/-- The `CorepresentableBy` structure asserting that the Type-valued functor
`Y ‚Ü¶ (F ‚ä† G ‚ä† H ‚ü∂ (ùü≠ C).prod (tensor C) ‚ãô tensor C ‚ãô Y)` is corepresented by
`F ‚äõ G ‚äõ H`. -/
@[simps]
def corepresentableBy‚ÇÇ :
    (whiskeringLeft _ _ _).obj (tensor C) ‚ãô
      (whiskeringLeft _ _ _).obj ((ùü≠ C).prod (tensor C)) ‚ãô
      coyoneda.obj (.op <| F ‚ä† G ‚ä† H) |>.CorepresentableBy (F ‚äõ G ‚äõ H) where
  homEquiv :=
    (corepresentableBy F (G ‚äõ H)).homEquiv.trans <|
      Functor.homEquivOfIsLeftKanExtension _ (extensionUnitRight (G ‚äõ H) (unit G H) F) _
  homEquiv_comp := by aesop

/-- The `CorepresentableBy` structure asserting that the Type-valued functor
`Y ‚Ü¶ ((F ‚ä† G) ‚ä† H ‚ü∂ (tensor C).prod (ùü≠ C) ‚ãô tensor C ‚ãô Y)` is corepresented by
`(F ‚äõ G) ‚äõ H`. -/
@[simps]
def corepresentableBy‚ÇÇ' :
    (whiskeringLeft _ _ _).obj (tensor C) ‚ãô
      (whiskeringLeft _ _ _).obj ((tensor C).prod (ùü≠ C)) ‚ãô
      coyoneda.obj (.op <| (F ‚ä† G) ‚ä† H) |>.CorepresentableBy ((F ‚äõ G) ‚äõ H) where
  homEquiv :=
    (corepresentableBy (F ‚äõ G) H).homEquiv.trans <|
      Functor.homEquivOfIsLeftKanExtension _ (extensionUnitLeft (F ‚äõ G) (unit F G) H) _
  homEquiv_comp := by aesop

/-- The isomorphism of functors between
`((F ‚ä† G) ‚ä† H ‚ü∂ (tensor C).prod (ùü≠ C) ‚ãô tensor C ‚ãô -)` and
`(F ‚ä† G ‚ä† H ‚ü∂ (ùü≠ C).prod (tensor C) ‚ãô tensor C ‚ãô -)` that corresponds to the associator
isomorphism for Day convolution through `corepresentableBy‚ÇÇ` and `corepresentableBy‚ÇÇ`. -/
@[simps!]
def associatorCorepresentingIso :
    (whiskeringLeft _ _ _).obj (tensor C) ‚ãô
      (whiskeringLeft _ _ _).obj ((tensor C).prod (ùü≠ C)) ‚ãô
      coyoneda.obj (.op <| (F ‚ä† G) ‚ä† H) ‚âÖ
    (whiskeringLeft _ _ _).obj (tensor C) ‚ãô
      (whiskeringLeft _ _ _).obj ((ùü≠ C).prod (tensor C)) ‚ãô
      coyoneda.obj (.op <| F ‚ä† G ‚ä† H) :=
  calc
    _ ‚âÖ (whiskeringLeft _ _ _).obj (tensor C) ‚ãô
          (whiskeringLeft _ _ _).obj ((tensor C).prod (ùü≠ C)) ‚ãô
          (whiskeringLeft _ _ _).obj (prod.associativity C C C).inverse ‚ãô
          coyoneda.obj (.op <| (prod.associativity C C C).inverse ‚ãô (F ‚ä† G) ‚ä† H) :=
      isoWhiskerLeft _ (isoWhiskerLeft _
        (NatIso.ofComponents fun _ ‚Ü¶ Equiv.toIso <|
          (prod.associativity C C C).congrLeft.fullyFaithfulFunctor.homEquiv))
    _ ‚âÖ (whiskeringLeft _ _ _).obj
            ((prod.associativity C C C).inverse ‚ãô (tensor C).prod (ùü≠ C) ‚ãô tensor C) ‚ãô
          coyoneda.obj (.op <| (prod.associativity C C C).inverse ‚ãô (F ‚ä† G) ‚ä† H) :=
      .refl _
    _ ‚âÖ (whiskeringLeft _ _ _).obj ((ùü≠ C).prod (tensor C) ‚ãô tensor C) ‚ãô
          coyoneda.obj (.op <| (prod.associativity C C C).inverse ‚ãô (F ‚ä† G) ‚ä† H) :=
      isoWhiskerRight ((whiskeringLeft _ _ _).mapIso <| NatIso.ofComponents (fun _ ‚Ü¶ Œ±_ _ _ _)) _
    _ ‚âÖ (whiskeringLeft _ _ _).obj ((ùü≠ C).prod (tensor C) ‚ãô tensor C) ‚ãô
          coyoneda.obj (.op <| F ‚ä† G ‚ä† H) :=
      isoWhiskerLeft _ <|
        coyoneda.mapIso <| Iso.op <| NatIso.ofComponents (fun _ ‚Ü¶ Œ±_ _ _ _ |>.symm)

/-- The associator isomorphism for Day convolution -/
def associator : (F ‚äõ G) ‚äõ H ‚âÖ F ‚äõ G ‚äõ H :=
  corepresentableBy‚ÇÇ' F G H |>.ofIso (associatorCorepresentingIso F G H) |>.uniqueUpToIso <|
    corepresentableBy‚ÇÇ F G H

/-- Characterizing the forward direction of the associator isomorphism
with respect to the unit transformations. -/
@[reassoc (attr := simp)]
lemma associator_hom_unit_unit (x y z : C) :
    (unit F G).app (x, y) ‚ñ∑ (H.obj z) ‚â´
      (unit (F ‚äõ G) H).app (x ‚äó y, z) ‚â´
      (associator F G H).hom.app ((x ‚äó y) ‚äó z) =
    (Œ±_ _ _ _).hom ‚â´
      (F.obj x ‚óÅ (unit G H).app (y, z)) ‚â´
      (unit F (G ‚äõ H)).app (x, y ‚äó z) ‚â´
      (F ‚äõ G ‚äõ H).map (Œ±_ _ _ _).inv := by
  have := congrArg (fun t ‚Ü¶ t.app ((x, y), z)) <|
      (corepresentableBy‚ÇÇ' F G H).homEquiv.rightInverse_symm <|
        (corepresentableBy‚ÇÇ F G H |>.ofIso
          (associatorCorepresentingIso F G H).symm |>.homEquiv (ùüô _))
  dsimp [associator, Coyoneda.fullyFaithful, corepresentableBy‚ÇÇ,
    corepresentableBy‚ÇÇ', Functor.CorepresentableBy.ofIso, corepresentableBy‚ÇÇ,
    Functor.corepresentableByEquiv, associatorCorepresentingIso] at this ‚ä¢
  simp only [whiskerLeft_id, Category.comp_id, Category.assoc] at this
  simp only [Category.assoc, this]
  simp [Functor.FullyFaithful.homEquiv, Equivalence.fullyFaithfulFunctor, prod.associativity]

/-- Characterizing the inverse direction of the associator
with respect to the unit transformations -/
@[reassoc (attr := simp)]
lemma associator_inv_unit_unit (x y z : C) :
    F.obj x ‚óÅ (unit G H).app (y, z) ‚â´
      (unit F (G ‚äõ H)).app (x, y ‚äó z) ‚â´
      (associator F G H).inv.app (x ‚äó y ‚äó z) =
    (Œ±_ (F.obj x) (G.obj y) (H.obj z)).inv ‚â´ (unit F G).app (x, y) ‚ñ∑ H.obj z ‚â´
      (unit (F ‚äõ G) H).app (x ‚äó y, z) ‚â´
      ((F ‚äõ G) ‚äõ H).map (Œ±_ x y z).hom := by
  have := congrArg (fun t ‚Ü¶ t.app (x, y, z)) <|
      (corepresentableBy‚ÇÇ F G H).homEquiv.rightInverse_symm <|
        (corepresentableBy‚ÇÇ' F G H |>.ofIso
          (associatorCorepresentingIso F G H) |>.homEquiv (ùüô _))
  dsimp [associator, Coyoneda.fullyFaithful, corepresentableBy‚ÇÇ,
    corepresentableBy‚ÇÇ', Functor.CorepresentableBy.ofIso, corepresentableBy‚ÇÇ,
    Functor.corepresentableByEquiv, associatorCorepresentingIso] at this ‚ä¢
  simp only [whiskerRight_tensor, id_whiskerRight, Category.id_comp, Iso.inv_hom_id] at this
  simp only [this]
  simp [Functor.FullyFaithful.homEquiv, Equivalence.fullyFaithfulFunctor, prod.associativity]

variable {F G H} in
theorem associator_naturality {F' G' H' : C ‚•§ V}
    [DayConvolution F' G'] [DayConvolution G' H']
    [DayConvolution F' (G' ‚äõ H')] [DayConvolution (F' ‚äõ G') H']
    (f : F ‚ü∂ F') (g : G ‚ü∂ G') (h : H ‚ü∂ H') :
      map (map f g) h ‚â´
        (associator F' G' H').hom =
      (associator F G H).hom ‚â´ map f (map g h) := by
  apply (corepresentableBy‚ÇÇ' F G H) |>.homEquiv.injective
  dsimp
  ext
  simp only [externalProductBifunctor_obj_obj, Functor.comp_obj, Functor.prod_obj, tensor_obj,
    Functor.id_obj, Functor.homEquivOfIsLeftKanExtension_apply_app,
    externalProductBifunctor_map_app, Functor.leftUnitor_inv_app, whiskerLeft_id, Category.comp_id,
    corepresentableBy_homEquiv_apply_app, NatTrans.comp_app, unit_app_map_app_assoc]
  rw [associator_hom_unit_unit_assoc]
  simp only [tensorHom_def, Category.assoc, externalProductBifunctor_obj_obj, tensor_obj,
    NatTrans.naturality, unit_app_map_app_assoc]
  rw [‚Üê comp_whiskerRight_assoc, unit_app_map_app]
  simp only [Functor.comp_obj, tensor_obj, comp_whiskerRight, Category.assoc]
  rw [‚Üê whisker_exchange_assoc, associator_hom_unit_unit, whisker_exchange_assoc,
    ‚Üê MonoidalCategory.whiskerLeft_comp_assoc, unit_app_map_app]
  simp [tensorHom_def]

section pentagon

variable [‚àÄ (v : V) (d : C √ó C),
    Limits.PreservesColimitsOfShape (CostructuredArrow ((tensor C).prod (ùü≠ C)) d) (tensorRight v)]

lemma pentagon (H K : C ‚•§ V)
    [DayConvolution G H] [DayConvolution (F ‚äõ G) H] [DayConvolution F (G ‚äõ H)]
    [DayConvolution H K] [DayConvolution G (H ‚äõ K)] [DayConvolution (G ‚äõ H) K]
    [DayConvolution ((F ‚äõ G) ‚äõ H) K] [DayConvolution (F ‚äõ G) (H ‚äõ K)]
    [DayConvolution (F ‚äõ G ‚äõ H) K] [DayConvolution F (G ‚äõ H ‚äõ K)]
    [DayConvolution F ((G ‚äõ H) ‚äõ K)] :
    map (associator F G H).hom (ùüô K) ‚â´
        (associator F (G ‚äõ H) K).hom ‚â´ map (ùüô F) (associator G H K).hom =
      (associator (F ‚äõ G) H K).hom ‚â´ (associator F G (H ‚äõ K)).hom := by
  -- We repeatedly apply the fact that the functors are left Kan extensions
  apply Functor.hom_ext_of_isLeftKanExtension (Œ± := unit ((F ‚äõ G) ‚äõ H) K)
  apply Functor.hom_ext_of_isLeftKanExtension
    (Œ± := extensionUnitLeft ((F ‚äõ G) ‚äõ H) (unit (F ‚äõ G) H) K)
  have : (((F ‚äõ G) ‚ä† H) ‚ä† K).IsLeftKanExtension
    (Œ± := extensionUnitLeft ((F ‚äõ G) ‚ä† H)
      (extensionUnitLeft _ (unit F G) H) K) :=
    isPointwiseLeftKanExtensionExtensionUnitLeft _ _ _
      (isPointwiseLeftKanExtensionExtensionUnitLeft _ _ _
        (isPointwiseLeftKanExtensionUnit F G)) |>.isLeftKanExtension
  apply Functor.hom_ext_of_isLeftKanExtension (Œ± := extensionUnitLeft ((F ‚äõ G) ‚ä† H)
      (extensionUnitLeft _ (unit F G) H) K)
  -- And then we compute...
  ext ‚ü®‚ü®‚ü®i, j‚ü©, k‚ü©, l‚ü©
  have aux :
      ((unit F G).app (i, j) ‚äó‚Çò (unit H K).app (k, l)) ‚â´
        (unit (F ‚äõ G) (H ‚äõ K)).app ((i ‚äó j), (k ‚äó l)) =
      (Œ±_ (F.obj i) (G.obj j) (H.obj k ‚äó K.obj l)).hom ‚â´
        F.obj i ‚óÅ G.obj j ‚óÅ (unit H K).app (k, l) ‚â´ F.obj i ‚óÅ (unit G (H ‚äõ K)).app (j, (k ‚äó l)) ‚â´
        (unit F (G ‚äõ H ‚äõ K)).app (i, (j ‚äó k ‚äó l)) ‚â´ (F ‚äõ G ‚äõ H ‚äõ K).map (Œ±_ i j (k ‚äó l)).inv ‚â´
        (associator F G (H ‚äõ K)).inv.app ((i ‚äó j) ‚äó k ‚äó l) := by
    conv_rhs => simp only [Functor.comp_obj, tensor_obj, NatTrans.naturality,
      associator_inv_unit_unit_assoc, externalProductBifunctor_obj_obj, Iso.map_hom_inv_id,
      Category.comp_id]
    simp only [tensor_whiskerLeft_symm, Category.assoc, Iso.hom_inv_id_assoc,
    ‚Üê tensorHom_def'_assoc]
  dsimp
  simp only [MonoidalCategory.whiskerLeft_id, Category.comp_id, unit_app_map_app_assoc,
    externalProductBifunctor_obj_obj, NatTrans.id_app, tensorHom_id, associator_hom_unit_unit_assoc,
    tensor_obj, NatTrans.naturality]
  conv_rhs =>
    simp only [whiskerRight_tensor_symm_assoc, Iso.inv_hom_id_assoc, ‚Üê tensorHom_def_assoc]
    rw [reassoc_of% aux]
  simp only [Iso.inv_hom_id_app_assoc, ‚Üê comp_whiskerRight_assoc, associator_hom_unit_unit F G H]
  simp only [Functor.comp_obj, tensor_obj, comp_whiskerRight, whisker_assoc, Category.assoc,
    whiskerRight_comp_unit_app_assoc (F ‚äõ G ‚äõ H) K l (Œ±_ i j k).inv,
    NatTrans.naturality_assoc, NatTrans.naturality, associator_hom_unit_unit_assoc,
    externalProductBifunctor_obj_obj, unit_app_map_app_assoc, NatTrans.id_app, id_tensorHom,
    Iso.inv_hom_id_assoc, ‚Üê MonoidalCategory.whiskerLeft_comp_assoc, associator_hom_unit_unit]
  simp [‚Üê Functor.map_comp, pentagon_inv, pentagon_assoc]

end pentagon

end associator

end

end DayConvolution

/-- A `DayConvolutionUnit` structure on a functor `C ‚•§ V` is the data of a pointwise
left Kan extension of `fromPUnit (ùüô_ V)` along `fromPUnit (ùüô_ C)`. Again, this is
made a class to ease proofs when constructing `DayConvolutionMonoidalCategory` structures, but one
should avoid registering it globally. -/
class DayConvolutionUnit (F : C ‚•§ V) where
  /-- A "canonical" structure map `ùüô_ V ‚ü∂ F.obj (ùüô_ C)` that defines a natural transformation
  `fromPUnit (ùüô_ V) ‚ü∂ fromPUnit (ùüô_ C) ‚ãô F`. -/
  can : ùüô_ V ‚ü∂ F.obj (ùüô_ C)
  /-- The canonical map `ùüô_ V ‚ü∂ F.obj (ùüô_ C)` exhibits `F` as a pointwise left kan extension
  of `fromPUnit.{0} ùüô_ V` along `fromPUnit.{0} ùüô_ C`. -/
  isPointwiseLeftKanExtensionCan : Functor.LeftExtension.mk F
    ({app _ := can} : Functor.fromPUnit.{0} (ùüô_ V) ‚ü∂
      Functor.fromPUnit.{0} (ùüô_ C) ‚ãô F) |>.IsPointwiseLeftKanExtension

namespace DayConvolutionUnit

variable (U : C ‚•§ V) [DayConvolutionUnit U]
open scoped DayConvolution
open ExternalProduct Functor

/-- A shorthand for the natural transformation of functors out of PUnit defined by
the canonical morphism `ùüô_ V ‚ü∂ U.obj (ùüô_ C)` when `U` is a unit for Day convolution. -/
abbrev œÜ : Functor.fromPUnit.{0} (ùüô_ V) ‚ü∂ Functor.fromPUnit.{0} (ùüô_ C) ‚ãô U where
  app _ := can

/-- Since a convolution unit is a pointwise left Kan extension, maps out of it at
any object are uniquely characterized. -/
lemma hom_ext {c : C} {v : V} {g h : U.obj c ‚ü∂ v}
    (e : ‚àÄ f : ùüô_ C ‚ü∂ c, can ‚â´ U.map f ‚â´ g = can ‚â´ U.map f ‚â´ h) :
    g = h := by
  apply (isPointwiseLeftKanExtensionCan c).hom_ext
  intro j
  simpa using e j.hom

variable (F : C ‚•§ V)
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (Functor.fromPUnit.{0} (ùüô_ C)) d) (tensorLeft v)]
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (Functor.fromPUnit.{0} (ùüô_ C)) d) (tensorRight v)]

instance : (F ‚ä† U).IsLeftKanExtension <| extensionUnitRight U (œÜ U) F :=
  isPointwiseLeftKanExtensionExtensionUnitRight
    U (œÜ U) F isPointwiseLeftKanExtensionCan |>.isLeftKanExtension

instance : (U ‚ä† F).IsLeftKanExtension <| extensionUnitLeft U (œÜ U) F :=
  isPointwiseLeftKanExtensionExtensionUnitLeft
    U (œÜ U) F isPointwiseLeftKanExtensionCan |>.isLeftKanExtension

/-- A `CorepresentableBy` structure that characterizes maps out of `U ‚äõ F`
by leveraging the fact that `U ‚ä† F` is a left Kan extension of `(fromPUnit ùüô_ V) ‚ä† F`. -/
@[simps]
def corepresentableByLeft [DayConvolution U F] :
    (whiskeringLeft _ _ _).obj (tensor C) ‚ãô
      (whiskeringLeft _ _ _).obj ((Functor.fromPUnit.{0} (ùüô_ C)).prod (ùü≠ C)) ‚ãô
      coyoneda.obj (.op <| Functor.fromPUnit.{0} (ùüô_ V) ‚ä† F) |>.CorepresentableBy (U ‚äõ F) where
  homEquiv :=
    Functor.homEquivOfIsLeftKanExtension _ (DayConvolution.unit U F) _ |>.trans <|
      Functor.homEquivOfIsLeftKanExtension _ (extensionUnitLeft U (œÜ U) F) _
  homEquiv_comp := by aesop

/-- A `CorepresentableBy` structure that characterizes maps out of `F ‚äõ U` by
leveraging the fact that `F ‚ä† U` is a left Kan extension of `F ‚ä† (fromPUnit ùüô_ V)`. -/
@[simps]
def corepresentableByRight [DayConvolution F U] :
    (whiskeringLeft _ _ _).obj (tensor C) ‚ãô
      (whiskeringLeft _ _ _).obj ((ùü≠ C).prod (Functor.fromPUnit.{0} (ùüô_ C))) ‚ãô
      coyoneda.obj (.op <| F ‚ä† Functor.fromPUnit.{0} (ùüô_ V)) |>.CorepresentableBy (F ‚äõ U) where
  homEquiv :=
    Functor.homEquivOfIsLeftKanExtension _ (DayConvolution.unit F U) _ |>.trans <|
      Functor.homEquivOfIsLeftKanExtension _ (extensionUnitRight U (œÜ U) F) _
  homEquiv_comp := by aesop

/-- The isomorphism of corepresentable functors that defines the left unitor for
Day convolution. -/
@[simps!]
def leftUnitorCorepresentingIso :
    (whiskeringLeft _ _ _).obj (tensor C) ‚ãô
      (whiskeringLeft _ _ _).obj ((Functor.fromPUnit.{0} (ùüô_ C)).prod (ùü≠ C)) ‚ãô
      coyoneda.obj (.op <| Functor.fromPUnit.{0} (ùüô_ V) ‚ä† F) ‚âÖ
    coyoneda.obj (.op <| F) := by
  calc
    _ ‚âÖ (whiskeringLeft _ _ _).obj (tensor C) ‚ãô
          (whiskeringLeft _ _ _).obj ((Functor.fromPUnit.{0} (ùüô_ C)).prod (ùü≠ C)) ‚ãô
          (whiskeringLeft _ _ _).obj (prod.leftUnitorEquivalence C).inverse ‚ãô
          coyoneda.obj (.op <|
           (prod.leftUnitorEquivalence C).inverse ‚ãô Functor.fromPUnit.{0} (ùüô_ V) ‚ä† F) :=
      isoWhiskerLeft _ (isoWhiskerLeft _
        (NatIso.ofComponents fun _ ‚Ü¶ Equiv.toIso <|
          (prod.leftUnitorEquivalence C).congrLeft.fullyFaithfulFunctor.homEquiv))
    _ ‚âÖ (whiskeringLeft _ _ _).obj
            ((prod.leftUnitorEquivalence C).inverse ‚ãô (Functor.fromPUnit.{0} (ùüô_ C)).prod (ùü≠ C) ‚ãô
              tensor C) ‚ãô
          coyoneda.obj (.op <|
            (prod.leftUnitorEquivalence C).inverse ‚ãô Functor.fromPUnit.{0} (ùüô_ V) ‚ä† F) :=
      .refl _
    _ ‚âÖ (whiskeringLeft _ _ _).obj (ùü≠ _) ‚ãô coyoneda.obj (.op <|
          (prod.leftUnitorEquivalence C).inverse ‚ãô Functor.fromPUnit.{0} (ùüô_ V) ‚ä† F) :=
      isoWhiskerRight ((whiskeringLeft _ _ _).mapIso <| NatIso.ofComponents fun _ ‚Ü¶ Œª_ _) _
    _ ‚âÖ _ := coyoneda.mapIso <| Iso.op <| NatIso.ofComponents fun _ ‚Ü¶ (Œª_ _).symm

/-- The isomorphism of corepresentable functors that defines the right unitor for
Day convolution. -/
@[simps!]
def rightUnitorCorepresentingIso :
    (whiskeringLeft _ _ _).obj (tensor C) ‚ãô
      (whiskeringLeft _ _ _).obj ((ùü≠ C).prod (Functor.fromPUnit.{0} (ùüô_ C))) ‚ãô
      coyoneda.obj (.op <| F ‚ä† Functor.fromPUnit.{0} (ùüô_ V)) ‚âÖ
    coyoneda.obj (.op <| F) := by
  calc
    _ ‚âÖ (whiskeringLeft _ _ _).obj (tensor C) ‚ãô
          (whiskeringLeft _ _ _).obj ((ùü≠ C).prod (Functor.fromPUnit.{0} (ùüô_ C))) ‚ãô
          (whiskeringLeft _ _ _).obj (prod.rightUnitorEquivalence C).inverse ‚ãô
          coyoneda.obj (.op <|
           (prod.rightUnitorEquivalence C).inverse ‚ãô F ‚ä† Functor.fromPUnit.{0} (ùüô_ V)) :=
      isoWhiskerLeft _ (isoWhiskerLeft _
        (NatIso.ofComponents fun _ ‚Ü¶ Equiv.toIso <|
          (prod.rightUnitorEquivalence C).congrLeft.fullyFaithfulFunctor.homEquiv))
    _ ‚âÖ (whiskeringLeft _ _ _).obj
            ((prod.rightUnitorEquivalence C).inverse ‚ãô
              ((ùü≠ C).prod (Functor.fromPUnit.{0} (ùüô_ C))) ‚ãô tensor C) ‚ãô
          coyoneda.obj (.op <|
            (prod.rightUnitorEquivalence C).inverse ‚ãô F ‚ä† Functor.fromPUnit.{0} (ùüô_ V)) :=
      .refl _
    _ ‚âÖ (whiskeringLeft _ _ _).obj (ùü≠ _) ‚ãô coyoneda.obj (.op <|
          (prod.rightUnitorEquivalence C).inverse ‚ãô F ‚ä† Functor.fromPUnit.{0} (ùüô_ V)) :=
      isoWhiskerRight ((whiskeringLeft _ _ _).mapIso <| NatIso.ofComponents fun _ ‚Ü¶ œÅ_ _) _
    _ ‚âÖ _ := coyoneda.mapIso <| Iso.op <| NatIso.ofComponents fun _ ‚Ü¶ (œÅ_ _).symm

/-- The left unitor isomorphism for Day convolution. -/
def leftUnitor [DayConvolution U F] : U ‚äõ F ‚âÖ F :=
  corepresentableByLeft U F |>.ofIso (leftUnitorCorepresentingIso F) |>.uniqueUpToIso
    <| Functor.corepresentableByEquiv.symm (.refl _)

/-- The right unitor isomorphism for Day convolution. -/
def rightUnitor [DayConvolution F U] : F ‚äõ U ‚âÖ F :=
  corepresentableByRight U F |>.ofIso (rightUnitorCorepresentingIso F) |>.uniqueUpToIso
    <| Functor.corepresentableByEquiv.symm (.refl _)

section

omit [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
  (CostructuredArrow (Functor.fromPUnit.{0} (ùüô_ C)) d) (tensorLeft v)]
variable [DayConvolution U F]

/-- Characterizing the forward direction of `leftUnitor` via the universal maps. -/
@[reassoc (attr := simp)]
lemma leftUnitor_hom_unit_app (y : C) :
    can ‚ñ∑ F.obj y ‚â´ (DayConvolution.unit U F).app (ùüô_ C, y) ‚â´
      (leftUnitor U F).hom.app (ùüô_ C ‚äó y) =
    (Œª_ (F.obj y)).hom ‚â´ F.map (Œª_ y).inv := by
  have := congrArg (fun t ‚Ü¶ t.app (.mk PUnit.unit, y)) <|
      (corepresentableByLeft U F).homEquiv.rightInverse_symm <|
        ((leftUnitorCorepresentingIso F).symm.hom.app F) (ùüô _)
  dsimp [leftUnitor, Coyoneda.fullyFaithful, corepresentableByLeft,
    leftUnitorCorepresentingIso, Functor.CorepresentableBy.ofIso,
    Functor.corepresentableByEquiv] at this ‚ä¢
  simp only [whiskerLeft_id, Category.comp_id] at this
  simp only [Category.comp_id, this]
  simp [prod.leftUnitorEquivalence, Equivalence.congrLeft, Equivalence.fullyFaithfulFunctor,
    Functor.FullyFaithful.homEquiv]

@[simp, reassoc]
lemma leftUnitor_inv_app (x : C) :
    (leftUnitor U F).inv.app x =
    (Œª_ (F.obj x)).inv ‚â´ can ‚ñ∑ F.obj x ‚â´ (DayConvolution.unit U F).app (ùüô_ C, x) ‚â´
      (U ‚äõ F).map (Œª_ x).hom := by
  dsimp [leftUnitor, Coyoneda.fullyFaithful, corepresentableByLeft,
    leftUnitorCorepresentingIso, Functor.CorepresentableBy.ofIso,
    Functor.corepresentableByEquiv]
  simp [prod.leftUnitorEquivalence, Equivalence.congrLeft, Equivalence.fullyFaithfulFunctor,
    Functor.FullyFaithful.homEquiv]

variable {F} in
@[reassoc (attr := simp)]
lemma leftUnitor_naturality {G : C ‚•§ V} [DayConvolution U G] (f : F ‚ü∂ G) :
    DayConvolution.map (ùüô _) f ‚â´ (leftUnitor U G).hom =
    (leftUnitor U F).hom ‚â´ f := by
  apply Functor.hom_ext_of_isLeftKanExtension _ (DayConvolution.unit _ _) _
  apply Functor.hom_ext_of_isLeftKanExtension _ (extensionUnitLeft U (œÜ U) F) _
  ext
  simp [‚Üê whisker_exchange_assoc]

end

section

omit [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
  (CostructuredArrow (Functor.fromPUnit.{0} (ùüô_ C)) d) (tensorRight v)]
variable [DayConvolution F U]

/-- Characterizing the forward direction of `rightUnitor` via the universal maps. -/
@[reassoc (attr := simp)]
lemma rightUnitor_hom_unit_app (x : C) :
    F.obj x ‚óÅ can ‚â´ (DayConvolution.unit F U).app (x, ùüô_ C) ‚â´
      (rightUnitor U F).hom.app (x ‚äó ùüô_ C) =
    (œÅ_ _).hom ‚â´ F.map (œÅ_ x).inv := by
  have := congrArg (fun t ‚Ü¶ t.app (x, .mk PUnit.unit)) <|
      (corepresentableByRight U F).homEquiv.rightInverse_symm <|
        ((rightUnitorCorepresentingIso F).symm.hom.app F) (ùüô _)
  dsimp [rightUnitor, Coyoneda.fullyFaithful, corepresentableByRight,
    rightUnitorCorepresentingIso, Functor.CorepresentableBy.ofIso,
    Functor.corepresentableByEquiv] at this ‚ä¢
  simp only [MonoidalCategory.whiskerRight_id, Category.id_comp, Iso.hom_inv_id,
    Category.comp_id] at this
  simp only [Category.comp_id, this]
  simp [prod.rightUnitorEquivalence, Equivalence.congrLeft, Equivalence.fullyFaithfulFunctor,
    Functor.FullyFaithful.homEquiv]

@[simp, reassoc]
lemma rightUnitor_inv_app (x : C) :
    (rightUnitor U F).inv.app x =
    (œÅ_ (F.obj x)).inv ‚â´ F.obj x ‚óÅ can ‚â´ (DayConvolution.unit F U).app (x, ùüô_ C) ‚â´
      (F ‚äõ U).map (œÅ_ x).hom := by
  dsimp [rightUnitor, Coyoneda.fullyFaithful, corepresentableByRight,
    rightUnitorCorepresentingIso, Functor.CorepresentableBy.ofIso,
    Functor.corepresentableByEquiv]
  simp [prod.rightUnitorEquivalence, Equivalence.congrLeft, Equivalence.fullyFaithfulFunctor,
    Functor.FullyFaithful.homEquiv]

variable {F} in
@[reassoc (attr := simp)]
lemma rightUnitor_naturality {G : C ‚•§ V} [DayConvolution G U] (f : F ‚ü∂ G) :
    DayConvolution.map f (ùüô _) ‚â´ (rightUnitor U G).hom =
    (rightUnitor U F).hom ‚â´ f := by
  apply Functor.hom_ext_of_isLeftKanExtension _ (DayConvolution.unit _ _) _
  apply Functor.hom_ext_of_isLeftKanExtension _ (extensionUnitRight U (œÜ U) F) _
  ext
  simp [whisker_exchange_assoc]

end

end DayConvolutionUnit

section triangle

open DayConvolution
open DayConvolutionUnit
open ExternalProduct

variable [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
    (CostructuredArrow (tensor C) d) (tensorLeft v)]
  [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
    (CostructuredArrow (tensor C) d) (tensorRight v)]
  [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
    (CostructuredArrow (Functor.fromPUnit.{0} <| ùüô_ C) d) (tensorLeft v)]
  [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
    (CostructuredArrow (Functor.fromPUnit.{0} <| ùüô_ C) d) (tensorRight v)]
  [‚àÄ (v : V) (d : C √ó C), Limits.PreservesColimitsOfShape
    (CostructuredArrow ((ùü≠ C).prod <| Functor.fromPUnit.{0} <| ùüô_ C) d) (tensorRight v)]

lemma DayConvolution.triangle (F G U : C ‚•§ V) [DayConvolutionUnit U]
    [DayConvolution F U] [DayConvolution U G]
    [DayConvolution F (U ‚äõ G)] [DayConvolution (F ‚äõ U) G] [DayConvolution F G] :
    (DayConvolution.associator F U G).hom ‚â´
      DayConvolution.map (ùüô F) (DayConvolutionUnit.leftUnitor U G).hom =
    DayConvolution.map (DayConvolutionUnit.rightUnitor U F).hom (ùüô G) := by
  apply Functor.hom_ext_of_isLeftKanExtension _ (DayConvolution.unit _ _) _
  apply Functor.hom_ext_of_isLeftKanExtension
    (Œ± := extensionUnitLeft (F ‚äõ U) (DayConvolution.unit F U) G)
  have : (F ‚ä† U) ‚ä† G |>.IsLeftKanExtension
      (Œ± := extensionUnitLeft (F ‚ä† U) (extensionUnitRight U (DayConvolutionUnit.œÜ U) F) G) :=
    isPointwiseLeftKanExtensionExtensionUnitLeft (F ‚ä† U) _ G
      (isPointwiseLeftKanExtensionExtensionUnitRight U (DayConvolutionUnit.œÜ U) F <|
        DayConvolutionUnit.isPointwiseLeftKanExtensionCan (F := U)) |>.isLeftKanExtension
  apply Functor.hom_ext_of_isLeftKanExtension
    (Œ± := extensionUnitLeft (F ‚ä† U) (extensionUnitRight U (DayConvolutionUnit.œÜ U) F) G)
  ext
  dsimp
  simp only [MonoidalCategory.whiskerRight_id, Category.id_comp, Iso.hom_inv_id, whisker_assoc,
    MonoidalCategory.whiskerLeft_id, Category.comp_id,
    DayConvolution.associator_hom_unit_unit_assoc, externalProductBifunctor_obj_obj, tensor_obj,
    NatTrans.naturality, unit_app_map_app_assoc, NatTrans.id_app, id_tensorHom,
    Category.assoc, Iso.inv_hom_id_assoc, unit_app_map_app, Functor.comp_obj,
    tensorHom_id, Iso.cancel_iso_hom_left]
  simp only [‚Üê MonoidalCategory.whiskerLeft_comp_assoc, leftUnitor_hom_unit_app,
    associator_inv_naturality_middle_assoc, ‚Üê comp_whiskerRight_assoc, rightUnitor_hom_unit_app]
  simp [‚Üê Functor.map_comp]

end triangle

section

variable (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] (V : Type u‚ÇÇ) [Category.{v‚ÇÇ} V]
    [MonoidalCategory C] [MonoidalCategory V]

/-- The class `DayConvolutionMonoidalCategory C V D` bundles the necessary data to
turn a monoidal category `D` into a monoidal full subcategory of a category of
functors `C ‚•§ V` endowed with a Day convolution monoidal structure.
The design of this class is to bundle a fully faithful functor into `C ‚•§ V` with
left extensions on its values representing the fact that it maps tensors products
in `D` to Day convolutions, and furthermore ask that this data is "lawful", i.e that
once realized in the functor category, the objects behave like the corresponding ones
in the category `C ‚•§ V`. -/
class LawfulDayConvolutionMonoidalCategoryStruct
    (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] (V : Type u‚ÇÇ) [Category.{v‚ÇÇ} V]
    [MonoidalCategory C] [MonoidalCategory V]
    (D : Type u‚ÇÉ) [Category.{v‚ÇÉ} D] [MonoidalCategoryStruct D] where
  /-- a functor that interprets element of `D` as functors `C ‚•§ V` -/
  Œπ (C V D) : D ‚•§ C ‚•§ V
  /-- a natural transformation `Œπ.obj d ‚ä† Œπ.obj d' ‚ü∂ tensor C ‚ãô Œπ.obj (d ‚äó d')` -/
  convolutionExtensionUnit (C) (V) (d d' : D) :
    Œπ.obj d ‚ä† Œπ.obj d' ‚ü∂ tensor C ‚ãô Œπ.obj (d ‚äó d')
  /-- `convolutionUnitUnit` exhibits `Œπ.obj (d ‚äó d')` as a left Ken extension of
  `Œπ.obj d ‚ä† Œπ.obj d'` along `tensor C`. -/
  isPointwiseLeftKanExtensionConvolutionExtensionUnit (d d' : D) :
    (Functor.LeftExtension.mk _ <|
      convolutionExtensionUnit d d').IsPointwiseLeftKanExtension
  /-- a map `ùüô_ V ‚ü∂ (Œπ.obj <| ùüô_ D).obj (ùüô_ C)`, that we think of as defining a
  natural transformation
  `fromPUnit.{0} (ùüô_ V) ‚ü∂ Functor.fromPUnit.{0} (ùüô_ C) ‚ãô (Œπ.obj <| ùüô_ D)`. -/
  unitUnit (C) (V) (D) : ùüô_ V ‚ü∂ (Œπ.obj <| ùüô_ D).obj (ùüô_ C)
  /-- the natural transformation induced by `unitUnit` exhibits
  `(Œπ.obj <| ùüô_ D).obj (ùüô_ C)` as a left Kan extension of `fromPUnit.{0} (ùüô_ V)` as a
  along `fromPUnit.{0} (ùüô_ C)`. -/
  isPointwiseLeftKanExtensionUnitUnit (C) (V) (D) :
    Functor.LeftExtension.mk _
      ({app _ := unitUnit} : Functor.fromPUnit.{0} (ùüô_ V) ‚ü∂
        Functor.fromPUnit.{0} (ùüô_ C) ‚ãô (Œπ.obj <| ùüô_ D))|>.IsPointwiseLeftKanExtension
  /-- The field `Œπ` interprets an element of `D` as a functor `C ‚•§ V`. -/
  faithful_Œπ : Œπ.Faithful := by infer_instance
  convolutionExtensionUnit_comp_Œπ_map_tensorHom_app (C) (V) {d‚ÇÅ d‚ÇÇ d‚ÇÅ' d‚ÇÇ' : D}
    (f‚ÇÅ : d‚ÇÅ ‚ü∂ d‚ÇÅ') (f‚ÇÇ : d‚ÇÇ ‚ü∂ d‚ÇÇ') (x y : C) :
    (convolutionExtensionUnit d‚ÇÅ d‚ÇÇ).app (x, y) ‚â´
      (Œπ.map (f‚ÇÅ ‚äó‚Çò f‚ÇÇ)).app (x ‚äó y) =
    ((Œπ.map f‚ÇÅ).app x ‚äó‚Çò (Œπ.map f‚ÇÇ).app y) ‚â´
      (convolutionExtensionUnit d‚ÇÅ' d‚ÇÇ').app (x, y)
  convolutionExtensionUnit_comp_Œπ_map_whiskerLeft_app (V)
    (d‚ÇÅ : D) {d‚ÇÇ d‚ÇÇ' : D}
    (f‚ÇÇ : d‚ÇÇ ‚ü∂ d‚ÇÇ') (x y : C) :
    (convolutionExtensionUnit d‚ÇÅ d‚ÇÇ).app (x, y) ‚â´
      (Œπ.map (d‚ÇÅ ‚óÅ f‚ÇÇ)).app (x ‚äó y) =
    ((Œπ.obj d‚ÇÅ).obj x ‚óÅ (Œπ.map f‚ÇÇ).app y) ‚â´
      (convolutionExtensionUnit d‚ÇÅ d‚ÇÇ').app (x, y)
  convolutionExtensionUnit_comp_Œπ_map_whiskerRight_app (C) (V)
    {d‚ÇÅ d‚ÇÅ': D} (f‚ÇÅ : d‚ÇÅ ‚ü∂ d‚ÇÅ') (d‚ÇÇ : D) (x y : C) :
    (convolutionExtensionUnit d‚ÇÅ d‚ÇÇ).app (x, y) ‚â´
      (Œπ.map (f‚ÇÅ ‚ñ∑ d‚ÇÇ)).app (x ‚äó y) =
    ((Œπ.map f‚ÇÅ).app x ‚ñ∑ (Œπ.obj d‚ÇÇ).obj y) ‚â´
      (convolutionExtensionUnit d‚ÇÅ' d‚ÇÇ).app (x, y)
  associator_hom_unit_unit (V) (d d' d'': D) (x y z : C) :
    (convolutionExtensionUnit d d').app (x, y) ‚ñ∑ (Œπ.obj d'').obj z ‚â´
      (convolutionExtensionUnit (d ‚äó d') d'').app (x ‚äó y, z) ‚â´
      (Œπ.map (Œ±_ d d' d'').hom).app ((x ‚äó y) ‚äó z) =
    (Œ±_ _ _ _).hom ‚â´
      ((Œπ.obj d).obj x ‚óÅ (convolutionExtensionUnit d' d'').app (y, z)) ‚â´
      (convolutionExtensionUnit d (d' ‚äó d'')).app (x, y ‚äó z) ‚â´
      (Œπ.obj (d ‚äó d' ‚äó d'')).map (Œ±_ _ _ _).inv
  leftUnitor_hom_unit_app (V) (d : D) (y : C) :
    unitUnit ‚ñ∑ (Œπ.obj d).obj y ‚â´
      (convolutionExtensionUnit (ùüô_ D) d).app
        (ùüô_ C, y) ‚â´
      (Œπ.map (Œª_ d).hom).app (ùüô_ C ‚äó y) =
    (Œª_ ((Œπ.obj d).obj y)).hom ‚â´ (Œπ.obj d).map (Œª_ y).inv
  rightUnitor_hom_unit_app (V) (d : D) (y : C) :
    (Œπ.obj d).obj y ‚óÅ unitUnit ‚â´
      (convolutionExtensionUnit d (ùüô_ D)).app (y, ùüô_ C) ‚â´
      (Œπ.map (œÅ_ d).hom).app (y ‚äó ùüô_ C) =
    (œÅ_ _).hom ‚â´ (Œπ.obj d).map (œÅ_ y).inv

namespace LawfulDayConvolutionMonoidalCategoryStruct

attribute [instance] faithful_Œπ

variable (D : Type u‚ÇÉ) [Category.{v‚ÇÉ} D] [MonoidalCategoryStruct D]
  [LawfulDayConvolutionMonoidalCategoryStruct C V D]

open scoped DayConvolution

/-- In a `LawfulDayConvolutionMonoidalCategoryStruct`, `Œπ.obj (d ‚äó d')` is a
Day convolution of `(Œπ C V).obj d` and `(Œπ C V).d'`. -/
def convolution (d d' : D) :
    DayConvolution (Œπ C V D|>.obj d) (Œπ C V D|>.obj d') where
  convolution := (Œπ C V D).obj (d ‚äó d')
  unit := convolutionExtensionUnit C V d d'
  isPointwiseLeftKanExtensionUnit :=
    isPointwiseLeftKanExtensionConvolutionExtensionUnit d d'

attribute [local instance] convolution

/-- In a `LawfulDayConvolutionMonoidalCategoryStruct`, `Œπ.obj (d ‚äó d' ‚äó d'')`
is a (triple) Day convolution of `(Œπ C V).obj d`, `(Œπ C V).obj d'` and
`(Œπ C V).obj d''`. -/
def convolution‚ÇÇ (d d' d'' : D) :
    DayConvolution (Œπ C V D|>.obj d) ((Œπ C V D|>.obj d') ‚äõ (Œπ C V D|>.obj d'')) :=
  convolution C V D _ _

/-- In a `LawfulDayConvolutionMonoidalCategoryStruct`, `Œπ.obj ((d ‚äó d') ‚äó d'')`
is a (triple) Day convolution of `(Œπ C V).obj d`, `(Œπ C V).obj d'` and
`(Œπ C V).obj d''`. -/
def convolution‚ÇÇ' (d d' d'' : D) :
    DayConvolution ((Œπ C V D|>.obj d) ‚äõ (Œπ C V D|>.obj d')) (Œπ C V D|>.obj d'') :=
  convolution C V D _ _

attribute [local instance] convolution‚ÇÇ convolution‚ÇÇ'

lemma Œπ_map_tensorHom_hom_eq_tensorHom
    {d‚ÇÅ d‚ÇÇ : D} {d‚ÇÅ' d‚ÇÇ' : D}
    (f : d‚ÇÅ ‚ü∂ d‚ÇÇ) (f' : d‚ÇÅ' ‚ü∂ d‚ÇÇ') :
    (Œπ C V D).map (f ‚äó‚Çò f') =
    DayConvolution.map (Œπ C V D|>.map f) (Œπ C V D|>.map f') := by
  apply DayConvolution.corepresentableBy
    (Œπ C V D|>.obj d‚ÇÅ) (Œπ C V D|>.obj d‚ÇÅ')|>.homEquiv.injective
  dsimp
  ext ‚ü®x, y‚ü©
  simp only [externalProductBifunctor_obj_obj, Functor.comp_obj, tensor_obj,
    DayConvolution.corepresentableBy_homEquiv_apply_app,
    DayConvolution.unit_app_map_app]
  exact convolutionExtensionUnit_comp_Œπ_map_tensorHom_app C V _ _ _ _

open DayConvolution in
lemma Œπ_map_associator_hom_eq_associator_hom (d d' d'')
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (tensor C) d) (tensorLeft v)]
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (tensor C) d) (tensorRight v)] :
    (Œπ C V D).map (Œ±_ d d' d'').hom =
    (DayConvolution.associator
      (Œπ C V D|>.obj d) (Œπ C V D|>.obj d') (Œπ C V D|>.obj d'')).hom := by
  apply corepresentableBy‚ÇÇ'
    (Œπ C V D|>.obj d) (Œπ C V D|>.obj d') (Œπ C V D|>.obj d'')|>.homEquiv.injective
  dsimp
  ext ‚ü®‚ü®x, y‚ü©, z‚ü©
  simp only [externalProductBifunctor_obj_obj, Functor.comp_obj, Functor.prod_obj,
    tensor_obj, Functor.id_obj, Functor.homEquivOfIsLeftKanExtension_apply_app,
    externalProductBifunctor_map_app, Functor.leftUnitor_inv_app, whiskerLeft_id,
    Category.comp_id, corepresentableBy_homEquiv_apply_app,
    DayConvolution.associator_hom_unit_unit]
  exact associator_hom_unit_unit V _ _ _ _ _ _

/-- In a `LawfulDayConvolutionMonoidalCategoryStruct`, `Œπ.obj (ùüô_ D)`
is a Day convolution unit`. -/
def convolutionUnit : DayConvolutionUnit (Œπ C V D|>.obj <| ùüô_ D) where
  can := unitUnit _ _ _
  isPointwiseLeftKanExtensionCan := isPointwiseLeftKanExtensionUnitUnit _ _ _

attribute [local instance] convolutionUnit

open DayConvolutionUnit in
lemma Œπ_map_leftUnitor_hom_eq_leftUnitor_hom (d : D)
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (Functor.fromPUnit.{0} <| ùüô_ C) d) (tensorRight v)] :
    (Œπ C V D).map (Œª_ d).hom =
    (DayConvolutionUnit.leftUnitor
      (Œπ C V D|>.obj <| ùüô_ D) (Œπ C V D|>.obj d)).hom := by
  apply corepresentableByLeft
    (Œπ C V D|>.obj <| ùüô_ D) (Œπ C V D|>.obj d)|>.homEquiv.injective
  dsimp
  ext ‚ü®_, x‚ü©
  dsimp [corepresentableByLeft]
  simp only [whiskerLeft_id, Category.comp_id,
    DayConvolutionUnit.leftUnitor_hom_unit_app]
  exact leftUnitor_hom_unit_app V d x

open DayConvolutionUnit in
lemma Œπ_map_rightUnitor_hom_eq_rightUnitor_hom (d : D)
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (Functor.fromPUnit.{0} <| ùüô_ C) d) (tensorLeft v)] :
    (Œπ C V D).map (œÅ_ d).hom =
    (DayConvolutionUnit.rightUnitor
      (Œπ C V D|>.obj <| ùüô_ D) (Œπ C V D|>.obj d)).hom := by
  apply corepresentableByRight
    (Œπ C V D|>.obj <| ùüô_ D) (Œπ C V D|>.obj d)|>.homEquiv.injective
  dsimp
  ext ‚ü®x, _‚ü©
  dsimp [corepresentableByRight]
  simp only [id_whiskerRight, Category.id_comp,
    DayConvolutionUnit.rightUnitor_hom_unit_app]
  exact rightUnitor_hom_unit_app V d x

end LawfulDayConvolutionMonoidalCategoryStruct

open LawfulDayConvolutionMonoidalCategoryStruct in
attribute [local instance] convolution convolution‚ÇÇ convolution‚ÇÇ' convolutionUnit in
open DayConvolution DayConvolutionUnit in
/-- We can promote a `LawfulDayConvolutionMonoidalCategoryStruct` to a monoidal category,
note that every non-prop data is already here, so this is just about showing that they
satisfy the axioms of a monoidal category. -/
def monoidalOfLawfulDayConvolutionMonoidalCategoryStruct
    (D : Type u‚ÇÉ) [Category.{v‚ÇÉ} D]
    [MonoidalCategoryStruct D]
    [LawfulDayConvolutionMonoidalCategoryStruct C V D]
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (tensor C) d) (tensorLeft v)]
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (tensor C) d) (tensorRight v)]
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (Functor.fromPUnit.{0} <| ùüô_ C) d) (tensorLeft v)]
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (Functor.fromPUnit.{0} <| ùüô_ C) d) (tensorRight v)]
    [‚àÄ (v : V) (d : C √ó C),
      Limits.PreservesColimitsOfShape
        (CostructuredArrow ((ùü≠ C).prod <| Functor.fromPUnit.{0} <| ùüô_ C) d)
        (tensorRight v)]
    [‚àÄ (v : V) (d : C √ó C),
      Limits.PreservesColimitsOfShape
        (CostructuredArrow ((tensor C).prod (ùü≠ C)) d) (tensorRight v)] :
    MonoidalCategory D :=
  MonoidalCategory.ofTensorHom
    (id_tensorHom_id := fun x y => by
      apply Functor.Faithful.map_injective (F := Œπ C V D)
      simp only [Œπ_map_tensorHom_hom_eq_tensorHom, Functor.map_id]
      apply (DayConvolution.corepresentableBy
        (Œπ C V D|>.obj _) (Œπ C V D|>.obj _)).homEquiv.injective
      dsimp
      ext ‚ü®_, _‚ü©
      simp only [externalProductBifunctor_obj_obj, Functor.comp_obj, tensor_obj,
        corepresentableBy_homEquiv_apply_app, unit_app_map_app, NatTrans.id_app,
        tensorHom_id, id_whiskerRight, Category.id_comp]
      dsimp [DayConvolution.convolution]
      simp)
    (tensorHom_comp_tensorHom := fun _ _ _ _ => by
      apply Functor.Faithful.map_injective (F := Œπ C V D)
      simp only [Œπ_map_tensorHom_hom_eq_tensorHom, Functor.map_comp]
      apply (corepresentableBy (Œπ C V D|>.obj _) (Œπ C V D|>.obj _)).homEquiv.injective
      dsimp
      ext ‚ü®_, _‚ü©
      simp)
    (id_tensorHom := fun x {y‚ÇÅ y‚ÇÇ} f => by
      apply Functor.Faithful.map_injective (F := Œπ C V D)
      simp only [Œπ_map_tensorHom_hom_eq_tensorHom]
      apply (corepresentableBy (Œπ C V D|>.obj _) (Œπ C V D|>.obj _)).homEquiv.injective
      dsimp
      ext ‚ü®x, y‚ü©
      dsimp
      simp only [Functor.map_id, unit_app_map_app, Functor.comp_obj, tensor_obj,
        NatTrans.id_app, id_tensorHom]
      dsimp [unit]
      rw [convolutionExtensionUnit_comp_Œπ_map_whiskerLeft_app])
    (tensorHom_id := fun x {y‚ÇÅ y‚ÇÇ} f => by
      apply Functor.Faithful.map_injective (F := Œπ C V D)
      simp only [Œπ_map_tensorHom_hom_eq_tensorHom]
      apply (corepresentableBy (Œπ C V D|>.obj _) (Œπ C V D|>.obj _)).homEquiv.injective
      dsimp
      ext ‚ü®x, y‚ü©
      dsimp
      simp only [Functor.map_id, DayConvolution.unit_app_map_app, Functor.comp_obj,
        tensor_obj, NatTrans.id_app, tensorHom_id]
      dsimp [DayConvolution.unit]
      rw [convolutionExtensionUnit_comp_Œπ_map_whiskerRight_app])
    (associator_naturality := fun f‚ÇÅ f‚ÇÇ f‚ÇÉ => by
      apply Functor.Faithful.map_injective (F := Œπ C V D)
      simp only [Functor.map_comp, Œπ_map_associator_hom_eq_associator_hom,
        Œπ_map_tensorHom_hom_eq_tensorHom]
      exact DayConvolution.associator_naturality
        ((Œπ C V D).map f‚ÇÅ) ((Œπ C V D).map f‚ÇÇ) ((Œπ C V D).map f‚ÇÉ))
    (leftUnitor_naturality := fun f => by
      apply Functor.Faithful.map_injective (F := Œπ C V D)
      simp only [Functor.map_comp, Œπ_map_tensorHom_hom_eq_tensorHom, Functor.map_id]
      rw [Œπ_map_leftUnitor_hom_eq_leftUnitor_hom,
        Œπ_map_leftUnitor_hom_eq_leftUnitor_hom]
      exact DayConvolutionUnit.leftUnitor_naturality
        (Œπ C V D|>.obj <| ùüô_ D) (Œπ C V D|>.map f))
    (rightUnitor_naturality := fun f => by
      apply Functor.Faithful.map_injective (F := Œπ C V D)
      simp only [Functor.map_comp, Œπ_map_tensorHom_hom_eq_tensorHom, Functor.map_id]
      rw [Œπ_map_rightUnitor_hom_eq_rightUnitor_hom, Œπ_map_rightUnitor_hom_eq_rightUnitor_hom]
      exact DayConvolutionUnit.rightUnitor_naturality
        (Œπ C V D|>.obj <| ùüô_ D) (Œπ C V D|>.map f))
    (pentagon := fun a b c d => by
      apply Functor.Faithful.map_injective (F := Œπ C V D)
      simp only [Functor.map_comp, Functor.map_id, Œπ_map_tensorHom_hom_eq_tensorHom,
        Œπ_map_associator_hom_eq_associator_hom]
      -- this is a bit painful...
      letI : DayConvolution
          (((Œπ C V D|>.obj a) ‚äõ (Œπ C V D|>.obj b)) ‚äõ (Œπ C V D|>.obj c))
          (Œπ C V D|>.obj d) :=
        convolution C V D _ _
      letI : DayConvolution
          ((Œπ C V D|>.obj a) ‚äõ (Œπ C V D|>.obj b))
          ((Œπ C V D|>.obj c) ‚äõ (Œπ C V D|>.obj d)) :=
        convolution C V D _ _
      letI : DayConvolution
          ((Œπ C V D|>.obj a) ‚äõ ((Œπ C V D|>.obj b) ‚äõ (Œπ C V D|>.obj c)))
          (Œπ C V D|>.obj d) :=
        convolution C V D _ _
      letI : DayConvolution
          (Œπ C V D|>.obj a)
          ((Œπ C V D|>.obj b) ‚äõ ((Œπ C V D|>.obj c) ‚äõ (Œπ C V D|>.obj d))) :=
        convolution C V D _ _
      letI : DayConvolution
          (Œπ C V D|>.obj a)
          (((Œπ C V D|>.obj b) ‚äõ (Œπ C V D|>.obj c)) ‚äõ (Œπ C V D|>.obj d)) :=
        convolution C V D _ _
      exact DayConvolution.pentagon
        (Œπ C V D|>.obj a) (Œπ C V D|>.obj b) (Œπ C V D|>.obj c) (Œπ C V D|>.obj d))
    (triangle := fun a b => by
      apply Functor.Faithful.map_injective (F := Œπ C V D)
      simp only [Functor.map_comp, Functor.map_id, Œπ_map_tensorHom_hom_eq_tensorHom,
        Œπ_map_associator_hom_eq_associator_hom, Œπ_map_leftUnitor_hom_eq_leftUnitor_hom,
        Œπ_map_rightUnitor_hom_eq_rightUnitor_hom]
      exact DayConvolution.triangle
        (Œπ C V D|>.obj a) (Œπ C V D|>.obj b) (Œπ C V D|>.obj <| ùüô_ D))

/-! In what follows, we give a constructor for `LawfulDayConvolutionMonoidalCategoryStruct`
that does not assume a pre-existing `MonoidalCategoryStruct` and builds one from
the data of suitable convolutions, while giving definitional control over
as many parameters as we can. -/

/-- An `InducedLawfulDayConvolutionMonoidalCategoryStructCore C V D` bundles the
core data needed to construct a full `LawfulDayConvolutionMonoidalCategoryStructCore`.
We‚Äôre making this a class so that it can act as a "proxy" for inferring `DayConvolution`
instances (which is all the more important that we are modifying the instances given in the
constructor to get better ones defeq-wise). As this object is purely about the internals
of definitions of Day convolutions monoidal structures, it is advised to not register
this class globally. -/
class InducedLawfulDayConvolutionMonoidalCategoryStructCore
    (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] (V : Type u‚ÇÇ) [Category.{v‚ÇÇ} V]
    [MonoidalCategory C] [MonoidalCategory V]
    (D : Type u‚ÇÉ) [Category.{v‚ÇÉ} D] where
  /-- A functor that interprets elements of `D` as functors `C ‚•§ V`. -/
  Œπ (C V D) : D ‚•§ C ‚•§ V
  /-- The functor `Œπ` is fully faithful. -/
  fullyFaithulŒπ : Œπ.FullyFaithful
  /-- Candidate function for the tensor product of objects. -/
  tensorObj (C) (V) : D ‚Üí D ‚Üí D
  /-- First candidate Day convolutions between objects.
  Note that the name here is primed as in fact, we will use the other fields
  in this class to produce convolutions with more controlled defeq properties. -/
  convolutions' : ‚àÄ (d d' : D), DayConvolution (Œπ.obj d) (Œπ.obj d')
  /-- Isomorphisms that exhibits the essential image of `Œπ` as closed under day
  convolution. -/
  tensorObjIsoConvolution (C) (V) : ‚àÄ (d d' : D),
    Œπ.obj (tensorObj d d') ‚âÖ (convolutions' d d').convolution
  /-- Candidate component of units for the `LawfulDayConvolutionMonoidalCategoryStruct`,
  this defaults to the ones deduced by `convolutions'` and `tensorObjIsoConvolution`. -/
  convolutionUnitApp (V) :
      ‚àÄ (d d' : D) (x y : C),
        (Œπ.obj d).obj x ‚äó (Œπ.obj d').obj y ‚ü∂ (Œπ.obj (tensorObj d d')).obj (x ‚äó y) :=
    fun d d' x y =>
      (convolutions' d d').unit.app (x, y) ‚â´
        (tensorObjIsoConvolution d d').inv.app (x ‚äó y)
  /-- Lawfulness of `convolutionUnitApp`. -/
  convolutionUnitApp_eq (V) :
      ‚àÄ (d d' : D) (x y : C),
        convolutionUnitApp d d' x y =
        (convolutions' d d').unit.app (x, y) ‚â´
          (tensorObjIsoConvolution d d').inv.app (x ‚äó y) := by
    cat_disch
  /-- Candidate `tensorHom`. This defaults to the one that corresponds to
  `DayConvolution.map` through `convolutions'`. -/
  tensorHom :
      ‚àÄ {d‚ÇÅ d‚ÇÇ : D} {d‚ÇÅ' d‚ÇÇ' : D},
        (d‚ÇÅ ‚ü∂ d‚ÇÇ) ‚Üí (d‚ÇÅ' ‚ü∂ d‚ÇÇ') ‚Üí (tensorObj d‚ÇÅ d‚ÇÅ' ‚ü∂ tensorObj d‚ÇÇ d‚ÇÇ') :=
    fun {d‚ÇÅ d‚ÇÇ} {d‚ÇÅ' d‚ÇÇ' : D} f f' => fullyFaithulŒπ.preimage <|
      (tensorObjIsoConvolution d‚ÇÅ d‚ÇÅ').hom ‚â´
        (DayConvolution.map (Œπ.map f) (Œπ.map f')) ‚â´ (tensorObjIsoConvolution d‚ÇÇ d‚ÇÇ').inv
  /-- Lawfulness of `tensorHom`. -/
  tensorHom_eq :
      ‚àÄ {d‚ÇÅ d‚ÇÇ : D} {d‚ÇÅ' d‚ÇÇ' : D} (f : d‚ÇÅ ‚ü∂ d‚ÇÇ) (f' : d‚ÇÅ' ‚ü∂ d‚ÇÇ'),
        Œπ.map (tensorHom f f') = (tensorObjIsoConvolution d‚ÇÅ d‚ÇÅ').hom ‚â´
          (DayConvolution.map (Œπ.map f) (Œπ.map f')) ‚â´
          (tensorObjIsoConvolution d‚ÇÇ d‚ÇÇ').inv := by
    cat_disch
  /-- Candidate tensor unit. -/
  tensorUnit (C) (V) (D) : D
  /-- DayConvolutionUnit structure on the candidate. -/
  tensorUnitConvolutionUnit : DayConvolutionUnit (Œπ.obj tensorUnit)

namespace InducedLawfulDayConvolutionMonoidalCategoryStructCore

attribute [local instance] tensorUnitConvolutionUnit

section

variable (D : Type u‚ÇÉ) [Category.{v‚ÇÉ} D]
    [InducedLawfulDayConvolutionMonoidalCategoryStructCore C V D]

variable {D} in
/-- With the data of chosen isomorphic objects to given day convolutions,
and provably equal unit maps through these isomorphisms,
we can transform a given family of Day convolutions to one with
convolutions definitionally equals to the given objects, and component of units
definitionally equal to the provided map family. -/
def convolutions (d d' : D) :
    DayConvolution ((Œπ C V D).obj d) ((Œπ C V D).obj d') where
  convolution := (Œπ C V D).obj (tensorObj C V d d')
  unit :=
    { app x := convolutionUnitApp V d d' x.1 x.2
      naturality := by
        intros
        simp only [convolutionUnitApp_eq, Category.assoc, NatTrans.naturality_assoc]
        simp }
  isPointwiseLeftKanExtensionUnit :=
    Functor.LeftExtension.isPointwiseLeftKanExtensionEquivOfIso
      (StructuredArrow.isoMk
        (tensorObjIsoConvolution C V d d').symm
        (by
          ext ‚ü®x, y‚ü©
          simp [convolutionUnitApp_eq V d d' x y]))
      (convolutions' d d'|>.isPointwiseLeftKanExtensionUnit)

attribute [local instance] convolutions

variable
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (tensor C) d) (tensorLeft v)]
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (tensor C) d) (tensorRight v)]
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (Functor.fromPUnit.{0} <| ùüô_ C) d) (tensorRight v)]
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (Functor.fromPUnit.{0} <| ùüô_ C) d) (tensorLeft v)]

open scoped DayConvolution

/-- Given a fully faithful functor `Œπ : C ‚•§ V ‚•§ D`,
a family of Day convolutions, candidate functions for `tensorObj` and `tensorHom`,
suitable isomorphisms
`Œπ.obj (tensorObj d d') ‚âÖ Œπ.obj (tensorObj d) ‚äõ Œπ.obj (tensorObj d')`
that behave in a lawful way with respect to the chosen Day convolutions, we can
construct a `MonoidalCategoryStruct` on `D`. -/
abbrev mkMonoidalCategoryStruct : MonoidalCategoryStruct D where
  tensorObj := tensorObj C V
  tensorHom := tensorHom
  tensorUnit := tensorUnit C V D
  whiskerLeft x {_ _} f := tensorHom (ùüô x) f
  whiskerRight f x := tensorHom f (ùüô x)
  associator x y z :=
    -- To make this work we use the better instance `convolutions`
    letI : DayConvolution (Œπ C V D|>.obj x) ((Œπ C V D|>.obj y) ‚äõ (Œπ C V D|>.obj z)) :=
      convolutions C V _ _
    letI : DayConvolution ((Œπ C V D|>.obj x) ‚äõ (Œπ C V D|>.obj y)) (Œπ C V D|>.obj z) :=
      convolutions C V _ _
    fullyFaithulŒπ.preimageIso <|
      DayConvolution.associator (Œπ C V D|>.obj x) (Œπ C V D|>.obj y) (Œπ C V D|>.obj z)
  leftUnitor x :=
    letI : DayConvolution (Œπ C V D|>.obj <| tensorUnit C V D) (Œπ C V D|>.obj x) :=
      convolutions C V _ _
    fullyFaithulŒπ.preimageIso <|
      DayConvolutionUnit.leftUnitor (Œπ C V D|>.obj <| tensorUnit C V D) (Œπ C V D|>.obj x)
  rightUnitor x :=
    letI : DayConvolution (Œπ C V D|>.obj x) (Œπ C V D|>.obj <| tensorUnit C V D) :=
      convolutions C V _ _
    fullyFaithulŒπ.preimageIso <|
      DayConvolutionUnit.rightUnitor (Œπ C V D|>.obj <| tensorUnit C V D) (Œπ C V D|>.obj x)

lemma id_tensorHom (x : D) {y y' : D} (f : y ‚ü∂ y') :
    letI := mkMonoidalCategoryStruct C V D
    (ùüô x) ‚äó‚Çò f = x ‚óÅ f :=
  rfl

lemma tensorHom_id {x x' : D} (f : x ‚ü∂ x') (y : D) :
    letI := mkMonoidalCategoryStruct C V D
    f ‚äó‚Çò (ùüô y) = f ‚ñ∑ y :=
  rfl

lemma Œπ_map_tensorHom_eq {d‚ÇÅ d‚ÇÅ' d‚ÇÇ d‚ÇÇ' : D} (f : d‚ÇÅ ‚ü∂ d‚ÇÇ) (f' : d‚ÇÅ' ‚ü∂ d‚ÇÇ') :
    letI := mkMonoidalCategoryStruct C V D
    (Œπ C V D).map (f ‚äó‚Çò f') =
    DayConvolution.map ((Œπ C V D).map f) ((Œπ C V D).map f') := by
  dsimp [mkMonoidalCategoryStruct]
  rw [tensorHom_eq]
  apply (convolutions C V d‚ÇÅ d‚ÇÅ').corepresentableBy.homEquiv.injective
  dsimp
  ext ‚ü®u‚ÇÅ, u‚ÇÇ‚ü©
  dsimp
  simp only [DayConvolution.unit_app_map_app, Functor.comp_obj, tensor_obj]
  simp [convolutions, convolutionUnitApp_eq]

/-- The monoidal category struct constructed in `DayConvolution.mkMonoidalCategoryStruct` extends
to a `LawfulDayConvolutionMonoidalCategoryStruct`. -/
def mkLawfulDayConvolutionMonoidalCategoryStruct :
    letI : MonoidalCategoryStruct D := mkMonoidalCategoryStruct C V D
    LawfulDayConvolutionMonoidalCategoryStruct C V D :=
  letI : MonoidalCategoryStruct D := mkMonoidalCategoryStruct C V D
  { Œπ := Œπ C V D
    faithful_Œπ := fullyFaithulŒπ.faithful
    convolutionExtensionUnit d d' :=
      (convolutions C V d d').unit
    isPointwiseLeftKanExtensionConvolutionExtensionUnit d d' :=
      (convolutions C V d d').isPointwiseLeftKanExtensionUnit
    unitUnit := tensorUnitConvolutionUnit.can
    isPointwiseLeftKanExtensionUnitUnit :=
      tensorUnitConvolutionUnit.isPointwiseLeftKanExtensionCan
    convolutionExtensionUnit_comp_Œπ_map_tensorHom_app := by
      intros d‚ÇÅ d‚ÇÅ' d‚ÇÇ d‚ÇÇ' f f' x y
      simp [Œπ_map_tensorHom_eq C V D f f']
    convolutionExtensionUnit_comp_Œπ_map_whiskerLeft_app := by
      intros d‚ÇÅ d‚ÇÇ d‚ÇÇ' f x y
      simp [‚Üê id_tensorHom, Œπ_map_tensorHom_eq C V D]
    convolutionExtensionUnit_comp_Œπ_map_whiskerRight_app := by
      intros
      simp [‚Üê tensorHom_id, Œπ_map_tensorHom_eq C V D]
    associator_hom_unit_unit d‚ÇÅ d‚ÇÇ d‚ÇÉ x‚ÇÅ x‚ÇÇ x‚ÇÉ := by
      simp only [externalProductBifunctor_obj_obj, Functor.comp_obj, tensor_obj, associator,
        Functor.FullyFaithful.preimageIso_hom, Functor.FullyFaithful.map_preimage]
      letI : DayConvolution (Œπ C V D|>.obj d‚ÇÅ) ((Œπ C V D|>.obj d‚ÇÇ) ‚äõ (Œπ C V D|>.obj d‚ÇÉ)) :=
        convolutions C V _ _
      letI : DayConvolution ((Œπ C V D|>.obj d‚ÇÅ) ‚äõ (Œπ C V D|>.obj d‚ÇÇ)) (Œπ C V D|>.obj d‚ÇÉ) :=
        convolutions C V _ _
      apply DayConvolution.associator_hom_unit_unit
    leftUnitor_hom_unit_app _ _ := by
      simp only [Functor.comp_obj, tensor_obj, leftUnitor,
        Functor.FullyFaithful.preimageIso_hom, Functor.FullyFaithful.map_preimage]
      apply DayConvolutionUnit.leftUnitor_hom_unit_app
    rightUnitor_hom_unit_app _ _ := by
      simp only [Functor.comp_obj, tensor_obj, rightUnitor,
        Functor.FullyFaithful.preimageIso_hom, Functor.FullyFaithful.map_preimage]
      apply DayConvolutionUnit.rightUnitor_hom_unit_app }

end

variable {C V} in
/-- Given a fully faithful functor `Œπ : D ‚•§ C ‚•§ V` and mere existence of Day convolutions of
`Œπ.obj d` and `Œπ.obj d'` such that the convolution remains in the essential image of `Œπ`,
construct an `InducedLawfulDayConvolutionMonoidalCategoryStructCore` by letting all other
data be the generic ones from the `HasPointwiseLeftKanExtension` API. -/
noncomputable def ofHasDayConvolutions
    {D : Type u‚ÇÉ} [Category.{v‚ÇÉ} D]
    (Œπ : D ‚•§ C ‚•§ V)
    (ffŒπ : Œπ.FullyFaithful)
    [hasDayConvolution : ‚àÄ (d d' : D),
      (tensor C).HasPointwiseLeftKanExtension (Œπ.obj d ‚ä† Œπ.obj d')]
    (essImageDayConvolution : ‚àÄ (d d' : D),
      Œπ.essImage <| (tensor C).pointwiseLeftKanExtension (Œπ.obj d ‚ä† Œπ.obj d'))
    [hasDayConvolutionUnit :
      (Functor.fromPUnit.{0} <| ùüô_ C).HasPointwiseLeftKanExtension
        (Functor.fromPUnit.{0} <| ùüô_ V)]
    (essImageDayConvolutionUnit :
      Œπ.essImage <|
        (Functor.fromPUnit.{0} <| ùüô_ C).pointwiseLeftKanExtension
          (Functor.fromPUnit.{0} <| ùüô_ V)) :
    InducedLawfulDayConvolutionMonoidalCategoryStructCore C V D where
  Œπ := Œπ
  fullyFaithulŒπ := ffŒπ
  tensorObj := fun d d' ‚Ü¶ essImageDayConvolution d d'|>.witness
  convolutions' := fun d d' ‚Ü¶
    { convolution := (tensor C).pointwiseLeftKanExtension (Œπ.obj d ‚ä† Œπ.obj d')
      unit := (tensor C).pointwiseLeftKanExtensionUnit (Œπ.obj d ‚ä† Œπ.obj d')
      isPointwiseLeftKanExtensionUnit :=
        (tensor C).pointwiseLeftKanExtensionIsPointwiseLeftKanExtension (Œπ.obj d ‚ä† Œπ.obj d') }
  tensorObjIsoConvolution := fun d d' => Functor.essImage.getIso _
  tensorUnit := essImageDayConvolutionUnit.witness
  tensorUnitConvolutionUnit :=
    { can :=
        ((Functor.fromPUnit.{0} <| ùüô_ C).pointwiseLeftKanExtensionUnit
            (Functor.fromPUnit.{0} <| ùüô_ V)).app (.mk PUnit.unit) ‚â´
          (essImageDayConvolutionUnit.getIso.inv.app (ùüô_ C))
      isPointwiseLeftKanExtensionCan :=
        Functor.LeftExtension.isPointwiseLeftKanExtensionEquivOfIso
        (StructuredArrow.isoMk
          (essImageDayConvolutionUnit.getIso).symm)
        (Functor.pointwiseLeftKanExtensionIsPointwiseLeftKanExtension
          (Functor.fromPUnit.{0} <| ùüô_ C)
          (Functor.fromPUnit.{0} <| ùüô_ V))}

end InducedLawfulDayConvolutionMonoidalCategoryStructCore

section

variable {C V}
    {D : Type u‚ÇÉ} [Category.{v‚ÇÉ} D]
    (Œπ : D ‚•§ C ‚•§ V)
    (ffŒπ : Œπ.FullyFaithful)
    [hasDayConvolution : ‚àÄ (d d' : D),
      (tensor C).HasPointwiseLeftKanExtension (Œπ.obj d ‚ä† Œπ.obj d')]
    (essImageDayConvolution : ‚àÄ (d d' : D),
      Œπ.essImage <| (tensor C).pointwiseLeftKanExtension (Œπ.obj d ‚ä† Œπ.obj d'))
    [hasDayConvolutionUnit :
      (Functor.fromPUnit.{0} <| ùüô_ C).HasPointwiseLeftKanExtension
        (Functor.fromPUnit.{0} <| ùüô_ V)]
    (essImageDayConvolutionUnit :
      Œπ.essImage <|
        (Functor.fromPUnit.{0} <| ùüô_ C).pointwiseLeftKanExtension
          (Functor.fromPUnit.{0} <| ùüô_ V))
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (tensor C) d) (tensorLeft v)]
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (tensor C) d) (tensorRight v)]
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (Functor.fromPUnit.{0} <| ùüô_ C) d) (tensorLeft v)]
    [‚àÄ (v : V) (d : C), Limits.PreservesColimitsOfShape
      (CostructuredArrow (Functor.fromPUnit.{0} <| ùüô_ C) d) (tensorRight v)]
    [‚àÄ (v : V) (d : C √ó C),
      Limits.PreservesColimitsOfShape
        (CostructuredArrow ((ùü≠ C).prod <| Functor.fromPUnit.{0} <| ùüô_ C) d)
        (tensorRight v)]
    [‚àÄ (v : V) (d : C √ó C),
      Limits.PreservesColimitsOfShape
        (CostructuredArrow ((tensor C).prod (ùü≠ C)) d) (tensorRight v)]

/-- Under suitable assumptions on existence of relevant Kan extensions and preservation
of relevant colimits by the tensor product of `V`, we can define a `MonoidalCategory D`
from the data of a fully faithful functor `Œπ : D ‚•§ C ‚•§ V` whose essential image
contains a Day convolution unit and is stable under binary Day convolutions. -/
noncomputable def monoidalOfHasDayConvolutions : MonoidalCategory D :=
  letI induced : InducedLawfulDayConvolutionMonoidalCategoryStructCore C V D :=
    .ofHasDayConvolutions Œπ ffŒπ essImageDayConvolution essImageDayConvolutionUnit
  letI := induced.mkMonoidalCategoryStruct
  letI : LawfulDayConvolutionMonoidalCategoryStruct C V D :=
    induced.mkLawfulDayConvolutionMonoidalCategoryStruct
  monoidalOfLawfulDayConvolutionMonoidalCategoryStruct C V D

open InducedLawfulDayConvolutionMonoidalCategoryStructCore in
/-- The monoidal category constructed via `monoidalOfHasDayConvolutions` has a canonical
`LawfulDayConvolutionMonoidalCategoryStruct C V D`. -/
noncomputable def lawfulDayConvolutionMonoidalCategoryStructOfHasDayConvolutions :
    letI := monoidalOfHasDayConvolutions
      Œπ ffŒπ essImageDayConvolution essImageDayConvolutionUnit
    LawfulDayConvolutionMonoidalCategoryStruct C V D :=
  letI : InducedLawfulDayConvolutionMonoidalCategoryStructCore C V D :=
    .ofHasDayConvolutions Œπ ffŒπ essImageDayConvolution essImageDayConvolutionUnit
  letI := monoidalOfHasDayConvolutions
    Œπ ffŒπ essImageDayConvolution essImageDayConvolutionUnit
  mkLawfulDayConvolutionMonoidalCategoryStruct C V D

end

end

end

end CategoryTheory.MonoidalCategory
