/-
Copyright (c) 2024 Lean FRO LLC. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Monoidal.Bimon_
import Mathlib.CategoryTheory.Monoidal.Conv

/-!
# The category of Hopf monoids in a braided monoidal category.


## TODO

* Show that in a cartesian monoidal category Hopf monoids are exactly group objects.
* Show that `Hopf_ (ModuleCat R) â‰Œ HopfAlgebraCat R`.
-/

noncomputable section

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚ u

open CategoryTheory MonoidalCategory Mon_Class

variable {C : Type uâ‚} [Category.{vâ‚} C] [MonoidalCategory.{vâ‚} C] [BraidedCategory C]

open scoped Mon_Class Comon_Class

/--
A Hopf monoid in a braided category `C` is a bimonoid object in `C` equipped with an antipode.
-/
class Hopf_Class (X : C) extends Bimon_Class X where
  /-- The antipode is an endomorphism of the underlying object of the Hopf monoid. -/
  antipode : X âŸ¶ X
  /- For the names of the conditions below, the unprimed names are reserved for the version where
  the argument `X` is explicit. -/
  antipode_left' : Î” â‰« antipode â–· X â‰« Î¼ = Îµ â‰« Î· := by aesop_cat
  antipode_right' : Î” â‰« X â— antipode â‰« Î¼ = Îµ â‰« Î· := by aesop_cat

namespace Hopf_Class

@[inherit_doc] scoped notation "ğ’®" => Hopf_Class.antipode
@[inherit_doc] scoped notation "ğ’®["X"]" => Hopf_Class.antipode (X := X)

/- The simp attribute is reserved for the unprimed versions. -/
attribute [reassoc] antipode_left' antipode_right'

/-- The object is provided as an explicit argument. -/
@[reassoc (attr := simp)]
theorem antipode_left (X : C) [Hopf_Class X] : Î” â‰« ğ’® â–· X â‰« Î¼ = Îµ â‰« Î· := antipode_left'

/-- The object is provided as an explicit argument. -/
@[reassoc (attr := simp)]
theorem antipode_right (X : C) [Hopf_Class X] : Î” â‰« X â— ğ’® â‰« Î¼ = Îµ â‰« Î· := antipode_right'

end Hopf_Class

variable (C)

/--
A Hopf monoid in a braided category `C` is a bimonoid object in `C` equipped with an antipode.
-/
structure Hopf_ where
  /-- The underlying bimonoid of a Hopf monoid. -/
  X : Bimon_ C
  /-- The antipode is an endomorphism of the underlying object of the Hopf monoid. -/
  antipode : X.X.X âŸ¶ X.X.X
  antipode_left : X.comul.hom â‰« (antipode â–· X.X.X) â‰« Î¼[X.X.X] = X.counit.hom â‰« Î·[X.X.X]
  antipode_right : X.comul.hom â‰« (X.X.X â— antipode) â‰« Î¼[X.X.X] = X.counit.hom â‰« Î·[X.X.X]

attribute [reassoc (attr := simp)] Hopf_.antipode_left Hopf_.antipode_right

namespace Hopf_

/--
Morphisms of Hopf monoids are just morphisms of the underlying bimonoids.
In fact they automatically intertwine the antipodes, proved below.
-/
instance : Category (Hopf_ C) := inferInstanceAs <| Category (InducedCategory (Bimon_ C) Hopf_.X)

variable {C}

/-- Morphisms of Hopf monoids intertwine the antipodes. -/
theorem hom_antipode {A B : Hopf_ C} (f : A âŸ¶ B) :
    f.hom.hom â‰« B.antipode = A.antipode â‰« f.hom.hom := by
  -- We show these elements are equal by exhibiting an element in the convolution algebra
  -- between `A` (as a comonoid) and `B` (as a monoid),
  -- such that the LHS is a left inverse, and the RHS is a right inverse.
  apply left_inv_eq_right_inv
    (M := Conv ((Bimon_.toComon_ C).obj A.X) B.X.X)
    (a := f.hom.hom)
  Â· erw [Conv.mul_eq, Conv.one_eq]
    simp only [Bimon_.toComon__obj_X, Bimon_.toComon__obj_comul, comp_whiskerRight, Category.assoc,
      Bimon_.toComon__obj_counit]
    slice_lhs 3 4 =>
      rw [â† whisker_exchange]
    slice_lhs 2 3 =>
      rw [â† tensorHom_def]
    slice_lhs 1 2 =>
      rw [â† Bimon_.hom_comul_hom f]
    slice_lhs 2 4 =>
      rw [B.antipode_left]
    slice_lhs 1 2 =>
      rw [Bimon_.hom_counit_hom f]
  Â· erw [Conv.mul_eq, Conv.one_eq]
    simp only [Bimon_.toComon__obj_X, Bimon_.toComon__obj_comul, MonoidalCategory.whiskerLeft_comp,
      Category.assoc, Bimon_.toComon__obj_counit]
    slice_lhs 2 3 =>
      rw [â† whisker_exchange]
    slice_lhs 3 4 =>
      rw [â† tensorHom_def]
    slice_lhs 3 4 =>
      rw [â† f.hom.mul_hom]
    slice_lhs 1 3 =>
      rw [A.antipode_right]
    slice_lhs 2 3 =>
      rw [f.hom.one_hom]

@[reassoc (attr := simp)]
theorem one_antipode (A : Hopf_ C) : Î·[A.X.X.X] â‰« A.antipode = Î·[A.X.X.X] := by
  have := (rfl : Î·[A.X.X.X] â‰« A.X.comul.hom â‰« (A.antipode â–· A.X.X.X) â‰« Î¼[A.X.X.X] = _)
  conv at this =>
    rhs
    rw [A.antipode_left]
  rw [A.X.one_comul_assoc, tensorHom_def, Category.assoc, whisker_exchange_assoc] at this
  simpa [unitors_inv_equal]

@[reassoc (attr := simp)]
theorem antipode_counit (A : Hopf_ C) : A.antipode â‰« A.X.counit.hom = A.X.counit.hom := by
  have := (rfl : A.X.comul.hom â‰« (A.antipode â–· A.X.X.X) â‰« Î¼[A.X.X.X] â‰« A.X.counit.hom = _)
  conv at this =>
    rhs
    rw [A.antipode_left_assoc]
  rw [A.X.mul_counit, tensorHom_def', Category.assoc, â† whisker_exchange_assoc] at this
  simpa [unitors_equal]

/-!
## The antipode is an antihomomorphism with respect to both the monoid and comonoid structures.
-/

theorem antipode_comulâ‚ (A : Hopf_ C) :
    A.X.comul.hom â‰«
      A.antipode â–· A.X.X.X â‰«
      A.X.comul.hom â–· A.X.X.X â‰«
      (Î±_ A.X.X.X A.X.X.X A.X.X.X).hom â‰«
      A.X.X.X â— A.X.X.X â— A.X.comul.hom â‰«
      A.X.X.X â— (Î±_ A.X.X.X A.X.X.X A.X.X.X).inv â‰«
      A.X.X.X â— (Î²_ A.X.X.X A.X.X.X).hom â–· A.X.X.X â‰«
      A.X.X.X â— (Î±_ A.X.X.X A.X.X.X A.X.X.X).hom â‰«
      (Î±_ A.X.X.X A.X.X.X (A.X.X.X âŠ— A.X.X.X)).inv â‰«
      (Î¼[A.X.X.X] âŠ— Î¼[A.X.X.X]) =
    A.X.counit.hom â‰« (Î»_ (ğŸ™_ C)).inv â‰« (Î·[A.X.X.X] âŠ— Î·[A.X.X.X]) := by
  dsimp
  slice_lhs 3 5 =>
    rw [â† associator_naturality_right, â† Category.assoc, â† tensorHom_def]
  slice_lhs 3 9 =>
    erw [Bimon_.compatibility]
  slice_lhs 1 3 =>
    erw [A.antipode_left]
  simp

/--
Auxiliary calculation for `antipode_comul`.
This calculation calls for some ASCII art out of This Week's Finds.

   |   |
   n   n
  | \ / |
  |  /  |
  | / \ |
  | | S S
  | | \ /
  | |  /
  | | / \
  \ / \ /
   v   v
    \ /
     v
     |

We move the left antipode up through the crossing,
the right antipode down through the crossing,
the right multiplication down across the strand,
reassociate the comultiplications,
then use `antipode_right` then `antipode_left` to simplify.
-/
theorem antipode_comulâ‚‚ (A : Hopf_ C) :
    A.X.comul.hom â‰«
      A.X.comul.hom â–· A.X.X.X â‰«
      (Î±_ A.X.X.X A.X.X.X A.X.X.X).hom â‰«
      A.X.X.X â— A.X.X.X â— A.X.comul.hom â‰«
      A.X.X.X â— A.X.X.X â— (Î²_ A.X.X.X A.X.X.X).hom â‰«
      A.X.X.X â— A.X.X.X â— (A.antipode âŠ— A.antipode) â‰«
      A.X.X.X â— (Î±_ A.X.X.X A.X.X.X A.X.X.X).inv â‰«
      A.X.X.X â— (Î²_ A.X.X.X A.X.X.X).hom â–· A.X.X.X â‰«
      A.X.X.X â— (Î±_ A.X.X.X A.X.X.X A.X.X.X).hom â‰«
      (Î±_ A.X.X.X A.X.X.X (A.X.X.X âŠ— A.X.X.X)).inv â‰«
      (Î¼[A.X.X.X] âŠ— Î¼[A.X.X.X]) =
    A.X.counit.hom â‰« (Î»_ (ğŸ™_ C)).inv â‰« (Î·[A.X.X.X] âŠ— Î·[A.X.X.X]) := by
  -- We should write a version of `slice_lhs` that zooms through whiskerings.
  slice_lhs 6 6 =>
    simp only [tensorHom_def', MonoidalCategory.whiskerLeft_comp]
  slice_lhs 7 8 =>
    rw [â† MonoidalCategory.whiskerLeft_comp, associator_inv_naturality_middle,
      MonoidalCategory.whiskerLeft_comp]
  slice_lhs 8 9 =>
    rw [â† MonoidalCategory.whiskerLeft_comp, â† comp_whiskerRight,
      BraidedCategory.braiding_naturality_right,
      comp_whiskerRight, MonoidalCategory.whiskerLeft_comp]
  slice_lhs 9 10 =>
    rw [â† MonoidalCategory.whiskerLeft_comp,
      associator_naturality_left,
      MonoidalCategory.whiskerLeft_comp]
  slice_lhs 5 6 =>
    rw [â† MonoidalCategory.whiskerLeft_comp, â† MonoidalCategory.whiskerLeft_comp,
      â† BraidedCategory.braiding_naturality_left,
      MonoidalCategory.whiskerLeft_comp, MonoidalCategory.whiskerLeft_comp]
  slice_lhs 11 12 =>
    rw [tensorHom_def', â† Category.assoc, â† associator_inv_naturality_right]
  slice_lhs 10 11 =>
    rw [â† MonoidalCategory.whiskerLeft_comp, â† whisker_exchange,
      MonoidalCategory.whiskerLeft_comp]
  slice_lhs 6 10 =>
    simp only [â† MonoidalCategory.whiskerLeft_comp]
    rw [â† BraidedCategory.hexagon_reverse_assoc, Iso.inv_hom_id_assoc,
      â† BraidedCategory.braiding_naturality_left]
    simp only [MonoidalCategory.whiskerLeft_comp]
  rw [Bimon_.comul_assoc_flip_hom_assoc, Iso.inv_hom_id_assoc]
  slice_lhs 2 3 =>
    simp only [â† MonoidalCategory.whiskerLeft_comp]
    rw [Bimon_.comul_assoc_hom]
    simp only [MonoidalCategory.whiskerLeft_comp]
  slice_lhs 3 7 =>
    simp only [â† MonoidalCategory.whiskerLeft_comp]
    rw [â† associator_naturality_middle_assoc, Iso.hom_inv_id_assoc]
    simp only [â† comp_whiskerRight]
    rw [antipode_right]
    simp only [comp_whiskerRight]
    simp only [MonoidalCategory.whiskerLeft_comp]
  slice_lhs 2 3 =>
    simp only [â† MonoidalCategory.whiskerLeft_comp]
    rw [Bimon_.counit_comul_hom]
    simp only [MonoidalCategory.whiskerLeft_comp]
  slice_lhs 3 4 =>
    simp only [â† MonoidalCategory.whiskerLeft_comp]
    rw [BraidedCategory.braiding_naturality_left]
    simp only [MonoidalCategory.whiskerLeft_comp]
  slice_lhs 4 5 =>
    simp only [â† MonoidalCategory.whiskerLeft_comp]
    rw [whisker_exchange]
    simp only [MonoidalCategory.whiskerLeft_comp]
  slice_lhs 5 7 =>
    rw [associator_inv_naturality_right_assoc, whisker_exchange]
  simp only [Mon_.monMonoidalStruct_tensorObj_X, Mon_.monMonoidalStruct_tensorUnit_X,
    braiding_tensorUnit_left, MonoidalCategory.whiskerLeft_comp, whiskerLeft_rightUnitor_inv,
    MonoidalCategory.whiskerRight_id, whiskerLeft_rightUnitor, Category.assoc, Iso.hom_inv_id_assoc,
    Iso.inv_hom_id_assoc, whiskerLeft_inv_hom_assoc, antipode_right_assoc]
  rw [rightUnitor_inv_naturality_assoc, tensorHom_def]
  monoidal

theorem antipode_comul (A : Hopf_ C) :
    A.antipode â‰« A.X.comul.hom = A.X.comul.hom â‰« (Î²_ _ _).hom â‰« (A.antipode âŠ— A.antipode) := by
  -- Again, it is a "left inverse equals right inverse" argument in the convolution monoid.
  apply left_inv_eq_right_inv
    (M := Conv ((Bimon_.toComon_ C).obj A.X) (A.X.X âŠ— A.X.X))
    (a := A.X.comul.hom)
  Â· erw [Conv.mul_eq, Conv.one_eq]
    simp only [Bimon_.toComon__obj_X, Mon_.monMonoidalStruct_tensorObj_X, Bimon_.toComon__obj_comul,
      comp_whiskerRight, tensor_whiskerLeft, instTensorObj_mul, Category.assoc,
      Bimon_.toComon__obj_counit, instTensorObj_one]
    simp only [tensor_Î¼]
    simp only [Category.assoc, Iso.inv_hom_id_assoc]
    exact antipode_comulâ‚ A
  Â· erw [Conv.mul_eq, Conv.one_eq]
    simp only [Bimon_.toComon__obj_X, Mon_.monMonoidalStruct_tensorObj_X, Bimon_.toComon__obj_comul,
      MonoidalCategory.whiskerLeft_comp, tensor_whiskerLeft, Category.assoc, Iso.inv_hom_id_assoc,
      instTensorObj_mul, Bimon_.toComon__obj_counit, instTensorObj_one]
    simp only [tensor_Î¼]
    simp only [Category.assoc, Iso.inv_hom_id_assoc]
    exact antipode_comulâ‚‚ A

theorem mul_antipodeâ‚ (A : Hopf_ C) :
    (A.X.comul.hom âŠ— A.X.comul.hom) â‰«
      (Î±_ A.X.X.X A.X.X.X (A.X.X.X âŠ— A.X.X.X)).hom â‰«
      A.X.X.X â— (Î±_ A.X.X.X A.X.X.X A.X.X.X).inv â‰«
      A.X.X.X â— (Î²_ A.X.X.X A.X.X.X).hom â–· A.X.X.X â‰«
      (Î±_ A.X.X.X (A.X.X.X âŠ— A.X.X.X) A.X.X.X).inv â‰«
      (Î±_ A.X.X.X A.X.X.X A.X.X.X).inv â–· A.X.X.X â‰«
      Î¼[A.X.X.X] â–· A.X.X.X â–· A.X.X.X â‰«
      A.antipode â–· A.X.X.X â–· A.X.X.X â‰«
      (Î±_ A.X.X.X A.X.X.X A.X.X.X).hom â‰«
      A.X.X.X â— Î¼[A.X.X.X] â‰«
      Î¼[A.X.X.X] =
    (A.X.counit.hom âŠ— A.X.counit.hom) â‰« (Î»_ (ğŸ™_ C)).hom â‰« Î·[A.X.X.X] := by
  slice_lhs 8 9 =>
    rw [associator_naturality_left]
  slice_lhs 9 10 =>
    rw [â† whisker_exchange]
  slice_lhs 7 8 =>
    rw [associator_naturality_left]
  slice_lhs 8 9 =>
    rw [â† tensorHom_def]
  simp only [Mon_.monMonoidalStruct_tensorObj_X, Category.assoc, pentagon_inv_inv_hom_hom_inv_assoc]
  slice_lhs 1 7 =>
    erw [Bimon_.compatibility]
  slice_lhs 2 4 =>
    rw [antipode_left]
  simp


/--
Auxiliary calculation for `mul_antipode`.

       |
       n
      /  \
     |   n
     |  / \
     |  S S
     |  \ /
     n   /
    / \ / \
    |  /  |
    \ / \ /
     v   v
     |   |

We move the leftmost multiplication up, so we can reassociate.
We then move the rightmost comultiplication under the strand,
and simplify using `antipode_right`.
-/
theorem mul_antipodeâ‚‚ (A : Hopf_ C) :
    (A.X.comul.hom âŠ— A.X.comul.hom) â‰«
      (Î±_ A.X.X.X A.X.X.X (A.X.X.X âŠ— A.X.X.X)).hom â‰«
      A.X.X.X â— (Î±_ A.X.X.X A.X.X.X A.X.X.X).inv â‰«
      A.X.X.X â— (Î²_ A.X.X.X A.X.X.X).hom â–· A.X.X.X â‰«
      (Î±_ A.X.X.X (A.X.X.X âŠ— A.X.X.X) A.X.X.X).inv â‰«
      (Î±_ A.X.X.X A.X.X.X A.X.X.X).inv â–· A.X.X.X â‰«
      Î¼[A.X.X.X] â–· A.X.X.X â–· A.X.X.X â‰«
      (Î±_ A.X.X.X A.X.X.X A.X.X.X).hom â‰«
      A.X.X.X â— (Î²_ A.X.X.X A.X.X.X).hom â‰«
      A.X.X.X â— (A.antipode âŠ— A.antipode) â‰«
      A.X.X.X â— Î¼[A.X.X.X] â‰« Î¼[A.X.X.X] =
    (A.X.counit.hom âŠ— A.X.counit.hom) â‰« (Î»_ (ğŸ™_ C)).hom â‰« Î·[A.X.X.X] := by
  slice_lhs 7 8 =>
    rw [associator_naturality_left]
  slice_lhs 8 9 =>
    rw [â† whisker_exchange]
  slice_lhs 9 10 =>
    rw [â† whisker_exchange]
  slice_lhs 11 12 =>
    rw [Mon_Class.mul_assoc_flip]
  slice_lhs 10 11 =>
    rw [associator_inv_naturality_left]
  slice_lhs 11 12 =>
    simp only [â† comp_whiskerRight]
    rw [Mon_Class.mul_assoc]
    simp only [comp_whiskerRight]
  rw [tensorHom_def]
  rw [tensor_whiskerLeft]
  rw [pentagon_inv_inv_hom_hom_inv_assoc]
  slice_lhs 7 8 =>
    rw [Iso.inv_hom_id]
  rw [Category.id_comp]
  slice_lhs 5 7 =>
    simp only [â† MonoidalCategory.whiskerLeft_comp]
    rw [â† BraidedCategory.hexagon_forward]
    simp only [MonoidalCategory.whiskerLeft_comp]
  simp only [Mon_.monMonoidalStruct_tensorObj_X, tensor_whiskerLeft,
    MonoidalCategory.whiskerLeft_comp, Category.assoc,
    whiskerLeft_inv_hom, Category.comp_id, whiskerLeft_hom_inv_assoc, Iso.inv_hom_id_assoc,
    pentagon_inv_inv_hom_inv_inv, whisker_assoc, Mon_Class.mul_assoc]
  slice_lhs 4 5 =>
    simp only [â† MonoidalCategory.whiskerLeft_comp]
    rw [Iso.inv_hom_id]
    simp only [MonoidalCategory.whiskerLeft_comp]
  rw [MonoidalCategory.whiskerLeft_id, Category.id_comp]
  slice_lhs 3 4 =>
    simp only [â† MonoidalCategory.whiskerLeft_comp]
    rw [BraidedCategory.braiding_naturality_right]
    simp only [MonoidalCategory.whiskerLeft_comp]
  rw [tensorHom_def']
  simp only [MonoidalCategory.whiskerLeft_comp]
  slice_lhs 5 6 =>
    simp only [â† MonoidalCategory.whiskerLeft_comp]
    rw [â† associator_naturality_right]
    simp only [MonoidalCategory.whiskerLeft_comp]
  slice_lhs 4 5 =>
    simp only [â† MonoidalCategory.whiskerLeft_comp]
    rw [â† whisker_exchange]
    simp only [MonoidalCategory.whiskerLeft_comp]
  slice_lhs 5 9 =>
    simp only [â† MonoidalCategory.whiskerLeft_comp]
    rw [associator_inv_naturality_middle_assoc, Iso.hom_inv_id_assoc]
    simp only [â† comp_whiskerRight]
    rw [antipode_right]
    simp only [comp_whiskerRight]
    simp only [MonoidalCategory.whiskerLeft_comp]
  slice_lhs 6 7 =>
    simp only [â† MonoidalCategory.whiskerLeft_comp]
    rw [one_mul']
    simp only [MonoidalCategory.whiskerLeft_comp]
  slice_lhs 3 4 =>
    simp only [â† MonoidalCategory.whiskerLeft_comp]
    rw [â† BraidedCategory.braiding_naturality_left]
    simp only [MonoidalCategory.whiskerLeft_comp]
  slice_lhs 4 5 =>
    simp only [â† MonoidalCategory.whiskerLeft_comp]
    rw [â† BraidedCategory.braiding_naturality_right]
    simp only [MonoidalCategory.whiskerLeft_comp]
  rw [â† associator_naturality_middle_assoc]
  simp only [Mon_.monMonoidalStruct_tensorUnit_X, braiding_tensorUnit_right,
    MonoidalCategory.whiskerLeft_comp]
  slice_lhs 6 7 =>
    simp only [â† MonoidalCategory.whiskerLeft_comp]
    rw [Iso.inv_hom_id]
    simp only [MonoidalCategory.whiskerLeft_comp]
  simp only [MonoidalCategory.whiskerLeft_id, Category.id_comp]
  slice_lhs 5 6 =>
    rw [whiskerLeft_rightUnitor, Category.assoc, â† rightUnitor_naturality]
  rw [associator_inv_naturality_right_assoc, Iso.hom_inv_id_assoc]
  slice_lhs 3 4 =>
    rw [whisker_exchange]
  slice_lhs 1 3 =>
    simp only [â† comp_whiskerRight]
    rw [antipode_right]
    simp only [comp_whiskerRight]
  slice_lhs 2 3 =>
    rw [â† whisker_exchange]
  slice_lhs 1 2 =>
    dsimp
    rw [â† tensorHom_def]
  slice_lhs 2 3 =>
    rw [rightUnitor_naturality]
  simp only [Mon_.monMonoidalStruct_tensorUnit_X]
  monoidal

theorem mul_antipode (A : Hopf_ C) :
    Î¼[A.X.X.X] â‰« A.antipode = (A.antipode âŠ— A.antipode) â‰« (Î²_ _ _).hom â‰« Î¼[A.X.X.X] := by
  -- Again, it is a "left inverse equals right inverse" argument in the convolution monoid.
  apply left_inv_eq_right_inv
    (M := Conv (((Bimon_.toComon_ C).obj A.X) âŠ— ((Bimon_.toComon_ C).obj A.X)) A.X.X)
    (a := Î¼[A.X.X.X])
  Â· -- Unfold the algebra structure in the convolution monoid,
    -- then `simp?, simp only [tensor_Î¼], simp?`.
    erw [Conv.mul_eq, Conv.one_eq]
    simp only [Comon_.monoidal_tensorObj_X, Bimon_.toComon__obj_X, Comon_.monoidal_tensorObj_comul,
      Bimon_.toComon__obj_comul, whiskerRight_tensor, comp_whiskerRight, Category.assoc,
      Comon_.monoidal_tensorObj_counit, Bimon_.toComon__obj_counit]
    simp only [tensor_Î¼]
    simp only [unop_comp, unop_tensorObj, unop_inv_associator, unop_whiskerLeft,
      unop_hom_associator, unop_whiskerRight, unop_hom_braiding, Category.assoc,
      pentagon_hom_inv_inv_inv_inv_assoc]
    exact mul_antipodeâ‚ A
  Â· erw [Conv.mul_eq, Conv.one_eq]
    simp only [Comon_.monoidal_tensorObj_X, Bimon_.toComon__obj_X, Comon_.monoidal_tensorObj_comul,
      Bimon_.toComon__obj_comul, whiskerRight_tensor, BraidedCategory.braiding_naturality_assoc,
      MonoidalCategory.whiskerLeft_comp, Category.assoc, Comon_.monoidal_tensorObj_counit,
      Bimon_.toComon__obj_counit]
    simp only [tensor_Î¼]
    simp only [unop_comp, unop_tensorObj, unop_inv_associator, unop_whiskerLeft,
      unop_hom_associator, unop_whiskerRight, unop_hom_braiding, Category.assoc,
      pentagon_hom_inv_inv_inv_inv_assoc]
    exact mul_antipodeâ‚‚ A

/--
In a commutative Hopf algebra, the antipode squares to the identity.
-/
theorem antipode_antipode (A : Hopf_ C) (comm : (Î²_ _ _).hom â‰« Î¼[A.X.X.X] = Î¼[A.X.X.X]) :
    A.antipode â‰« A.antipode = ğŸ™ A.X.X.X := by
  -- Again, it is a "left inverse equals right inverse" argument in the convolution monoid.
  apply left_inv_eq_right_inv
    (M := Conv ((Bimon_.toComon_ C).obj A.X) A.X.X)
    (a := A.antipode)
  Â· -- Unfold the algebra structure in the convolution monoid,
    -- then `simp?`.
    erw [Conv.mul_eq, Conv.one_eq]
    simp only [Bimon_.toComon__obj_X, Bimon_.toComon__obj_comul, comp_whiskerRight, Category.assoc,
      Bimon_.toComon__obj_counit]
    rw [â† comm, â† tensorHom_def_assoc, â† mul_antipode]
    simp
  Â· erw [Conv.mul_eq, Conv.one_eq]
    simp

end Hopf_

end
