/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.Monoidal.Action.Basic
import Mathlib.CategoryTheory.Monoidal.End
import Mathlib.CategoryTheory.Monoidal.Opposite

/-! # Actions as monoidal functors to endofunctor categories

In this file, we show that given a right action of a monoidal category `C`
on a category `D`, the curried action functor `C ‚•§ D ‚•§ D` is monoidal.
Conversely, given a monoidal functor `C ‚•§ D ‚•§ D`, we can define a right action
of `C` on `D`.

The corresponding results are also available for left actions: given a left
action of `C` on `D`, composing
`CategoryTheory.MonoidalCategory.MonoidalLeftAction.curriedAction C D` with
`CategoryTheory.MonoidalCategory.MonoidalOpposite.mopFunctor (D ‚•§ D)` is
monoidal, and conversely one can define a left action of `C` on `D` from a monoidal
functor `C ‚•§ (D ‚•§ D)·¥π·µí·µñ`.

-/

namespace CategoryTheory.MonoidalCategory

variable (C D : Type*)

variable [Category C] [MonoidalCategory C] [Category D]

attribute [local instance] endofunctorMonoidalCategory

attribute [local aesop safe apply (rule_sets := [CategoryTheory])]
  MonoidalOpposite.hom_ext

namespace MonoidalLeftAction

section

variable [MonoidalLeftAction C D]
/-- A variant of
`CategoryTheory.MonoidalCategory.MonoidalLeftAction.curriedAction`
that takes value in the monoidal opposite of `D ‚•§ D`. -/
@[simps! obj_unmop_obj obj_unmop_map]
def curriedActionMop : C ‚•§ (D ‚•§ D)·¥π·µí·µñ :=
  (curriedAction C D) ‚ãô mopFunctor _

-- This simp lemma is necessary because the simps projection generated for
-- unmop of a morphism is actually its underlying unmop field, rather than
-- the application of `Quiver.Hom.unmop`.
variable {C D} in
@[simp]
lemma curriedActionMop_map_unmop_app {c c' : C} (f : c ‚ü∂ c') (d : D) :
    ((curriedActionMop C D).map f).unmop.app d = f ‚äµ‚Çó d :=
  rfl

open MonoidalOpposite in
/-- When `C` acts on the left on `D`, the functor
`curriedActionMop : C ‚•§ (D ‚•§ D)·¥π·µí·µñ` is monoidal, where `D ‚•§ D` has the
composition monoidal structure. -/
@[simps!]
instance curriedActionMopMonoidal : (curriedActionMop C D).Monoidal where
  Œµ := .mop <| (actionUnitNatIso C D).inv
  Œº _ _ := .mop <| {app _ := Œ±‚Çó _ _ _ |>.inv}
  Œ¥ _ _ := .mop <| {app _ := Œ±‚Çó _ _ _ |>.hom}
  Œ∑ := .mop <| (actionUnitNatIso C D).hom
  associativity c‚ÇÅ c‚ÇÇ c‚ÇÉ := by
    apply (mopEquiv (D ‚•§ D)).fullyFaithfulInverse.map_injective
    ext d
    simpa [-associator_actionHom] using
      (IsIso.inv_eq_inv.mpr <| associator_actionHom c‚ÇÅ c‚ÇÇ c‚ÇÉ d).symm =‚â´
        (Œ±_ c‚ÇÅ c‚ÇÇ c‚ÇÉ).hom ‚äµ‚Çó d
  oplax_right_unitality x := by
    apply MonoidalOpposite.hom_ext
    ext t
    simpa [-rightUnitor_actionHom] using
      (œÅ_ x).inv ‚äµ‚Çó t ‚â´= rightUnitor_actionHom x t
  oplax_left_unitality x := by
    apply MonoidalOpposite.hom_ext
    ext t
    simpa [-leftUnitor_actionHom] using
      (Œª_ x).inv ‚äµ‚Çó t ‚â´= leftUnitor_actionHom x t

end

variable {C D}

/-- A monoidal functor `F : C ‚•§ (D ‚•§ D)·¥π·µí·µñ` can be thought of as a left action
of `C` on `D`. -/
@[simps!]
def actionOfMonoidalFunctorToEndofunctorMop (F : C ‚•§ (D ‚•§ D)·¥π·µí·µñ) [F.Monoidal] :
    MonoidalLeftAction C D where
  actionObj c d := (F.obj c).unmop.obj d
  actionHomLeft f d := (F.map f).unmop.app d
  actionHomRight c _ _ f := (F.obj c).unmop.map f
  actionAssocIso c c' d := Functor.Monoidal.ŒºIso F c c'|>.unmop.app d|>.symm
  actionUnitIso d := Functor.Monoidal.ŒµIso F|>.unmop.app d|>.symm
  actionAssocIso_hom_naturality {c‚ÇÅ c‚ÇÅ' c‚ÇÇ c‚ÇÇ' c‚ÇÉ c‚ÇÉ'} f g h := by
    have e := congrArg (fun t ‚Ü¶ t.unmop.app c‚ÇÉ) <|
      Functor.OplaxMonoidal.Œ¥_natural F f g
    dsimp at e
    simp [reassoc_of% e]
  whiskerRight_actionHomLeft {x y} c f d := by
    have e := congrArg (fun t ‚Ü¶ t.unmop.app d) <|
      Functor.LaxMonoidal.Œº_natural_left F f c
    dsimp at e
    simp [e, ‚Üê NatTrans.comp_app, ‚Üê unmop_comp]
  whiskerLeft_actionHomLeft c {x y} f d := by
    have e := congrArg (fun t ‚Ü¶ t.unmop.app d) <|
      Functor.LaxMonoidal.Œº_natural_right F c f
    dsimp at e
    simp [e, ‚Üê NatTrans.comp_app, ‚Üê unmop_comp]
  associator_actionHom c‚ÇÅ c‚ÇÇ c‚ÇÉ d := by
    have e := congrArg (fun t ‚Ü¶ t.unmop.app d) <|
      Functor.OplaxMonoidal.associativity F c‚ÇÅ c‚ÇÇ c‚ÇÉ
    dsimp at e
    simp only [Category.comp_id] at e
    simp [e]
  leftUnitor_actionHom c d := by
    have e := (F.map (Œª_ c).hom).unmop.app d ‚â´=
      (congrArg (fun t ‚Ü¶ t.unmop.app d) <|
        Functor.OplaxMonoidal.left_unitality F c)
    dsimp at e
    simp only [Category.comp_id, ‚Üê NatTrans.comp_app_assoc, ‚Üê unmop_comp,
      ‚Üê F.map_comp_assoc, Iso.hom_inv_id, Functor.map_id, Category.id_comp] at e
    simp [e]
  rightUnitor_actionHom c d := by
    have e := (F.map (œÅ_ c).hom).unmop.app d ‚â´=
      (congrArg (fun t ‚Ü¶ t.unmop.app d) <|
        Functor.OplaxMonoidal.right_unitality F c)
    dsimp at e
    simp only [Category.comp_id, ‚Üê NatTrans.comp_app_assoc, ‚Üê unmop_comp,
      ‚Üê F.map_comp_assoc, Iso.hom_inv_id, Functor.map_id, Category.id_comp] at e
    simp [e]

/-- If the (left) action of `C` on `D` comes from a monoidal functor
`C ‚•§ (D ‚•§ D)·¥π·µí·µñ`, then `curriedActionMop C D` is naturally isomorphic to that
functor. -/
@[simps!]
def curriedActionActionOfMonoidalFunctorToEndofunctorMopIso
    (F : C ‚•§ (D ‚•§ D)·¥π·µí·µñ) [F.Monoidal] :
    letI := actionOfMonoidalFunctorToEndofunctorMop F
    curriedActionMop C D ‚âÖ F :=
  .refl _

end MonoidalLeftAction

namespace MonoidalRightAction

variable {C D}

open MonoidalOpposite in
/-- When `C` acts on the right on `D`, the functor `curriedAction : C ‚•§ (D ‚•§ D)`
is monoidal, where `D ‚•§ D` has the composition monoidal structure. -/
@[simps!]
instance curriedActionMonoidal [MonoidalRightAction C D] :
    (curriedAction C D).Monoidal where
  Œµ := (actionUnitNatIso C D).inv
  Œº _ _ := {app _ := Œ±·µ£ _ _ _ |>.inv}
  Œ¥ _ _ := {app _ := Œ±·µ£ _ _ _ |>.hom}
  Œ∑ := (actionUnitNatIso C D).hom
  associativity c‚ÇÅ c‚ÇÇ c‚ÇÉ := by
    ext d
    simpa [-actionHom_associator] using
      (IsIso.inv_eq_inv.mpr <| actionHom_associator c‚ÇÅ c‚ÇÇ c‚ÇÉ d).symm =‚â´
        d ‚ä¥·µ£ (Œ±_ c‚ÇÅ c‚ÇÇ c‚ÇÉ).hom
  oplax_right_unitality x := by
    ext t
    simpa [-actionHom_rightUnitor] using
      t ‚ä¥·µ£ (œÅ_ x).inv ‚â´= actionHom_rightUnitor x t
  oplax_left_unitality x := by
    ext t
    simpa [-actionHom_leftUnitor] using
      t ‚ä¥·µ£ (Œª_ x).inv ‚â´= actionHom_leftUnitor x t

/-- A monoidal functor `F : C ‚•§ D ‚•§ D` can be thought of as a right action
of `C` on `D`. -/
@[simps!]
def actionOfMonoidalFunctorToEndofunctor (F : C ‚•§ D ‚•§ D) [F.Monoidal] :
    MonoidalRightAction C D where
  actionObj d c := (F.obj c).obj d
  actionHomLeft f c := (F.obj c).map f
  actionHomRight d _ _ f := (F.map f).app d
  actionAssocIso d c c' := Functor.Monoidal.ŒºIso F c c'|>.app d|>.symm
  actionUnitIso d := Functor.Monoidal.ŒµIso F|>.app d|>.symm
  actionAssocIso_hom_naturality {c‚ÇÅ c‚ÇÅ' c‚ÇÇ c‚ÇÇ' c‚ÇÉ c‚ÇÉ'} f g h := by
    have e := congrArg (fun t ‚Ü¶ t.app c‚ÇÅ) <|
      Functor.OplaxMonoidal.Œ¥_natural F g h
    dsimp at e
    simp [reassoc_of% e]

/-- If the action of `C` on `D` comes from a monoidal functor `C ‚•§ (D ‚•§ D)`,
then `curriedActionMop C D` is naturally isomorphic to that functor. -/
@[simps!]
def curriedActionActionOfMonoidalFunctorToEndofunctorIso
    (F : C ‚•§ (D ‚•§ D)) [F.Monoidal] :
    letI := actionOfMonoidalFunctorToEndofunctor F
    curriedAction C D ‚âÖ F :=
  .refl _

end MonoidalRightAction

namespace endofunctorMonoidalCategory
/-- Functor evaluation gives a right action of `C ‚•§ C`.

Note that in the literature, this is defined as a left action, but mathlib's
monoidal structure on `C ‚•§ C` is the monoidal opposite of the one usually
considered in the literature. -/
@[simps! actionObj actionHomLeft actionHomRight actionAssocIso actionUnitIso]
scoped instance evaluationRightAction : MonoidalRightAction (C ‚•§ C) C :=
  MonoidalRightAction.actionOfMonoidalFunctorToEndofunctor (ùü≠ (C ‚•§ C))

end endofunctorMonoidalCategory

end CategoryTheory.MonoidalCategory
