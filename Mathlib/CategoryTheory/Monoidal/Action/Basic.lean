/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.Monoidal.Category
import Mathlib.CategoryTheory.Functor.Trifunctor

/-!

# Actions from a monoidal category on a category

Given a monoidal category `C`, and a category `D`, we define a left action of
`C` on `D` as the data of an object `c âŠ™â‚— d` of `D` for every
`c : C` and `d : D`, as well as the data required to turn `- âŠ™â‚— -` into
a bifunctor, along with structure natural isomorphisms
`(- âŠ— -) âŠ™â‚— - â‰… - âŠ™â‚— - âŠ™â‚— -` and `ğŸ™_ C âŠ™â‚— - â‰… -`, subject to coherence conditions.

We also define right actions, for these, the notation for the action of `c`
on `d` is `d âŠ™áµ£ c`, and the structure isomorphisms are of the form
`- âŠ™áµ£ (- âŠ— -) â‰… (- âŠ™áµ£ -) âŠ™áµ£ -` and `- âŠ™â‚— ğŸ™_ C â‰… -`.


## References
* [Janelidze, G, and Kelly, G.M., *A note on actions of a monoidal category*][JanelidzeKelly2001]

## TODOs/Projects
* Equivalence between actions of `C` on `D` and pseudofunctors from the
  classifying bicategory of `C` to `Cat`.
* Left actions as monoidal functors C â¥¤ (D â¥¤ D)á´¹áµ’áµ–.
* Right actions as monoidal functors C â¥¤ D â¥¤ D.
* (Right) Action of `(C â¥¤ C)` on `C`.
* Left/Right Modules in `D` over a monoid object in `C`.
  Equivalence with `Mod_` when `D` is `C`. Bimodules objects.
* Given a monad `M` on `C`, equivalence between `Algebra M`, and modules in `C`
  on `M.toMon : Mon_ (C â¥¤ C)`.
* Canonical left action of `Type u` on `u`-small cocomplete categories via the
  copower.

-/

namespace CategoryTheory.MonoidalCategory

variable (C D : Type*)

variable [Category C] [Category D]
/-- A class that carries the non-Prop data required to define a left action of a
monoidal category `C` on a category `D`, to set up notations. -/
class MonoidalLeftActionStruct [MonoidalCategoryStruct C] where
  /-- The left action on objects. This is denoted `c âŠ™â‚— d`. -/
  actionObj : C â†’ D â†’ D
  /-- The left action of a map `f : c âŸ¶ c'` in `C` on an object `d` in `D`.
  If we are to consider the action as a functor `Î‘ : C â¥¤ D â¥¤ D`,
  this is (Î‘.map f).app d`. This is denoted `f âŠµâ‚— d` -/
  actionHomLeft {c c' : C} (f : c âŸ¶ c') (d : D) :
    actionObj c d âŸ¶ actionObj c' d
  /-- The action of an object `c : C` on a map `f : d âŸ¶ d'` in `D`.
  If we are to consider the action as a functor `Î‘ : C â¥¤ D â¥¤ D`,
  this is (Î‘.obj c).map f`. This is denoted `c âŠ´â‚— f`. -/
  actionHomRight (c : C) {d d' : D} (f : d âŸ¶ d') :
    actionObj c d âŸ¶ actionObj c d'
  /-- The action of a pair of maps `f : c âŸ¶ c'` and `d âŸ¶ d'`. By default,
  this is defined in terms of `actionHomLeft` and `actionHomRight`. -/
  actionHom {c c' : C} {d d' : D} (f : c âŸ¶ c') (g : d âŸ¶ d') :
    actionObj c d âŸ¶ actionObj c' d' := actionHomLeft f d â‰« actionHomRight c' g
  /-- The structural isomorphism `(c âŠ— c') âŠ™â‚— d â‰… c âŠ™â‚— (c' âŠ™â‚— d)`. -/
  actionAssocIso (c c' : C) (d : D) :
    actionObj (c âŠ— c') d â‰… actionObj c (actionObj c' d)
  /-- The structural isomorphism between `ğŸ™_ C âŠ™â‚— d` and `d`. -/
  actionUnitIso (d : D) : actionObj (ğŸ™_ C) d â‰… d

namespace MonoidalLeftAction

export MonoidalLeftActionStruct
  (actionObj actionHomLeft actionHomRight actionHom actionAssocIso
    actionUnitIso)

-- infix priorities are aligned with the ones from `MonoidalCategoryStruct`.

/-- Notation for `actionObj`, the action of `C` on `D`. -/
scoped infixr:70 " âŠ™â‚— " => MonoidalLeftActionStruct.actionObj

/-- Notation for `actionHomLeft`, the action of `C` on morphisms in `D`. -/
scoped infixr:81 " âŠµâ‚— " => MonoidalLeftActionStruct.actionHomLeft

/-- Notation for `actionHomRight`, the action of morphism in `C` on `D`. -/
scoped infixr:81 " âŠ´â‚— " => MonoidalLeftActionStruct.actionHomRight

/-- Notation for `actionHom`, the bifunctorial action of morphisms in `C` and
`D` on `- âŠ™â‚— -`. -/
scoped infixr:70 " âŠ™â‚—â‚˜ " => MonoidalLeftActionStruct.actionHom

/-- Notation for `actionAssocIso`, the structural isomorphism
`- âŠ— - âŠ™â‚— - â‰… - âŠ™â‚— - âŠ™â‚— -`. -/
scoped notation "Î±â‚— " => MonoidalLeftActionStruct.actionAssocIso

/-- Notation for `actionUnitIso`, the structural isomorphism `ğŸ™_ C âŠ™â‚— - â‰… -`. -/
scoped notation "Î»â‚— " => MonoidalLeftActionStruct.actionUnitIso
/-- Notation for `actionUnitIso`, the structural isomorphism `ğŸ™_ C âŠ™â‚— - â‰… -`,
allowing one to specify the acting category. -/
scoped notation "Î»â‚—["J"]" => MonoidalLeftActionStruct.actionUnitIso (C := J)

end MonoidalLeftAction

open scoped MonoidalLeftAction in
/-- A `MonoidalLeftAction C D` is is the data of:
- For every object `c : C` and `d : D`, an object `c âŠ™â‚— d` of `D`.
- For every morphism `f : (c : C) âŸ¶ c'` and every `d : D`, a morphism
  `f âŠµâ‚— d : c âŠ™â‚— d âŸ¶ c' âŠ™â‚— d`.
- For every morphism `f : (d : D) âŸ¶ d'` and every `c : C`, a morphism
  `c âŠ´â‚— f : c âŠ™â‚— d âŸ¶ c âŠ™â‚— d'`.
- For every pair of morphisms `f : (c : C) âŸ¶ c'` and
  `f : (d : D) âŸ¶ d'`, a morphism `f âŠ™â‚— f' : c âŠ™â‚— d âŸ¶ c' âŠ™â‚— d'`.
- A structure isomorphism `Î±â‚— c c' d : c âŠ— c' âŠ™â‚— d â‰… c âŠ™â‚— c' âŠ™â‚— d`.
- A structure isomorphism `Î»â‚— d : (ğŸ™_ C) âŠ™â‚— d â‰… d`.
Furthermore, we require identities that turn `- âŠ™â‚— -` into a bifunctor,
ensure naturality of `Î±â‚—` and `Î»â‚—`, and ensure compatibilities with
the associator and unitor isomorphisms in `C`. -/
class MonoidalLeftAction [MonoidalCategory C] extends
    MonoidalLeftActionStruct C D where
  actionHom_def {c c' : C} {d d' : D} (f : c âŸ¶ c') (g : d âŸ¶ d') :
      f âŠ™â‚—â‚˜ g = f âŠµâ‚— d â‰« c' âŠ´â‚— g := by
    cat_disch
  actionHomRight_id (c : C) (d : D) : c âŠ´â‚— ğŸ™ d = ğŸ™ (c âŠ™â‚— d) := by cat_disch
  id_actionHomLeft (c : C) (d : D) : ğŸ™ c âŠµâ‚— d = ğŸ™ (c âŠ™â‚— d) := by cat_disch
  actionHom_comp
      {c c' c'' : C} {d d' d'' : D} (fâ‚ : c âŸ¶ c') (fâ‚‚ : c' âŸ¶ c'')
      (gâ‚ : d âŸ¶ d') (gâ‚‚ : d' âŸ¶ d'') :
      (fâ‚ â‰« fâ‚‚) âŠ™â‚—â‚˜ (gâ‚ â‰« gâ‚‚) = (fâ‚ âŠ™â‚—â‚˜ gâ‚) â‰« (fâ‚‚ âŠ™â‚—â‚˜ gâ‚‚) := by
    cat_disch
  actionAssocIso_hom_naturality
      {câ‚ câ‚‚ câ‚ƒ câ‚„ : C} {dâ‚ dâ‚‚ : D} (f : câ‚ âŸ¶ câ‚‚) (g : câ‚ƒ âŸ¶ câ‚„) (h : dâ‚ âŸ¶ dâ‚‚) :
      ((f âŠ—â‚˜ g) âŠ™â‚—â‚˜ h) â‰« (Î±â‚— câ‚‚ câ‚„ dâ‚‚).hom =
        (Î±â‚— câ‚ câ‚ƒ dâ‚).hom â‰« (f âŠ™â‚—â‚˜ g âŠ™â‚—â‚˜ h) := by
    cat_disch
  actionUnitIso_hom_naturality {d d' : D} (f : d âŸ¶ d') :
      (Î»â‚— d).hom â‰« f = (ğŸ™_ C) âŠ´â‚— f â‰« (Î»â‚— d').hom := by
    cat_disch
  whiskerLeft_actionHomLeft (c : C) {c' c'' : C} (f : c' âŸ¶ c'') (d : D) :
      (c â— f) âŠµâ‚— d = (Î±â‚— _ _ _).hom â‰« c âŠ´â‚— f âŠµâ‚— d â‰« (Î±â‚— _ _ _).inv := by
    cat_disch
  whiskerRight_actionHomLeft {c c' : C} (c'' : C) (f : c âŸ¶ c') (d : D) :
      (f â–· c'') âŠµâ‚— d = (Î±â‚— c c'' d).hom â‰«
        f âŠµâ‚— (c'' âŠ™â‚— d : D) â‰« (Î±â‚— c' c'' d).inv := by
    cat_disch
  associator_actionHom (câ‚ câ‚‚ câ‚ƒ : C) (d : D) :
      (Î±_ câ‚ câ‚‚ câ‚ƒ).hom âŠµâ‚— d â‰« (Î±â‚— câ‚ (câ‚‚ âŠ— câ‚ƒ) d).hom â‰«
        câ‚ âŠ´â‚— (Î±â‚— câ‚‚ câ‚ƒ d).hom =
      (Î±â‚— (câ‚ âŠ— câ‚‚ : C) câ‚ƒ d).hom â‰« (Î±â‚— câ‚ câ‚‚ (câ‚ƒ âŠ™â‚— d)).hom := by
    cat_disch
  leftUnitor_actionHom (c : C) (d : D) :
      (Î»_ c).hom âŠµâ‚— d = (Î±â‚— _ _ _).hom â‰« (Î»â‚— _).hom := by
    cat_disch
  rightUnitor_actionHom (c : C) (d : D) :
      (Ï_ c).hom âŠµâ‚— d = (Î±â‚— _ _ _).hom â‰« c âŠ´â‚— (Î»â‚— _).hom := by
    cat_disch

attribute [reassoc] MonoidalLeftAction.actionHom_def
attribute [reassoc, simp] MonoidalLeftAction.id_actionHomLeft
attribute [reassoc, simp] MonoidalLeftAction.actionHomRight_id
attribute [reassoc, simp] MonoidalLeftAction.whiskerLeft_actionHomLeft
attribute [simp, reassoc] MonoidalLeftAction.actionHom_comp
attribute [reassoc] MonoidalLeftAction.actionAssocIso_hom_naturality
attribute [reassoc] MonoidalLeftAction.actionUnitIso_hom_naturality
attribute [reassoc (attr := simp)] MonoidalLeftAction.associator_actionHom
attribute [simp, reassoc] MonoidalLeftAction.leftUnitor_actionHom
attribute [simp, reassoc] MonoidalLeftAction.rightUnitor_actionHom

/--
A monoidal category acts on itself on the left through the tensor product.
-/
@[simps!]
instance selfLeftAction [MonoidalCategory C] : MonoidalLeftAction C C where
  actionObj x y := x âŠ— y
  actionHom f g := f âŠ—â‚˜ g
  actionUnitIso x := Î»_ x
  actionAssocIso x y z := Î±_ x y z
  actionHomLeft f x := f â–· x
  actionHomRight x _ _ f := x â— f
  actionHom_def := by simp [tensorHom_def]

@[deprecated (since := "2025-06-13")] alias selfAction := selfLeftAction

namespace MonoidalLeftAction

open Category

variable {C D} [MonoidalCategory C] [MonoidalLeftAction C D]

-- Simp normal forms are aligned with the ones in `MonoidalCateogry`.

@[simp]
lemma id_actionHom (c : C) {d d' : D} (f : d âŸ¶ d') :
    (ğŸ™ c) âŠ™â‚—â‚˜ f = c âŠ´â‚— f := by
  simp [actionHom_def]

@[simp]
lemma actionHom_id {c c' : C} (f : c âŸ¶ c') (d : D) :
    f âŠ™â‚—â‚˜ (ğŸ™ d) = f âŠµâ‚— d := by
  simp [actionHom_def]

@[reassoc, simp]
theorem actionHomRight_comp (w : C) {x y z : D} (f : x âŸ¶ y) (g : y âŸ¶ z) :
    w âŠ´â‚— (f â‰« g) = w âŠ´â‚— f â‰« w âŠ´â‚— g := by
  simp [â† id_actionHom, â† actionHom_comp]

@[reassoc, simp]
theorem unit_actionHomRight {x y : D} (f : x âŸ¶ y) :
    (ğŸ™_ C) âŠ´â‚— f = (Î»â‚— x).hom â‰« f â‰« (Î»â‚— y).inv := by
  rw [â† Category.assoc, actionUnitIso_hom_naturality]
  simp

@[reassoc, simp]
theorem tensor_actionHomRight (x y : C) {z z' : D} (f : z âŸ¶ z') :
    (x âŠ— y) âŠ´â‚— f = (Î±â‚— x y z).hom â‰« x âŠ´â‚— y âŠ´â‚— f â‰« (Î±â‚— x y z').inv := by
  simp only [â† id_actionHom]
  rw [â† Category.assoc, â† actionAssocIso_hom_naturality]
  simp

@[reassoc, simp]
theorem comp_actionHomLeft {w x y : C} (f : w âŸ¶ x) (g : x âŸ¶ y) (z : D) :
    (f â‰« g) âŠµâ‚— z = f âŠµâ‚— z â‰« g âŠµâ‚— z := by
  simp only [â† actionHom_id, â† actionHom_comp, Category.id_comp]

@[reassoc, simp]
theorem actionHomLeft_action {x x' : C} (f : x âŸ¶ x') (y : C) (z : D) :
    f âŠµâ‚— (y âŠ™â‚— z) = (Î±â‚— x y z).inv â‰« (f â–· y) âŠµâ‚— z â‰« (Î±â‚— x' y z).hom := by
  simp [whiskerRight_actionHomLeft]

@[reassoc]
theorem action_exchange {w x : C} {y z : D} (f : w âŸ¶ x) (g : y âŸ¶ z) :
    w âŠ´â‚— g â‰« f âŠµâ‚— z = f âŠµâ‚— y â‰« x âŠ´â‚— g := by
  simp only [â† id_actionHom, â† actionHom_id, â† actionHom_comp, id_comp, comp_id]

@[reassoc]
theorem actionHom_def' {xâ‚ yâ‚ : C} {xâ‚‚ yâ‚‚ : D} (f : xâ‚ âŸ¶ yâ‚) (g : xâ‚‚ âŸ¶ yâ‚‚) :
    f âŠ™â‚—â‚˜ g = xâ‚ âŠ´â‚— g â‰« f âŠµâ‚— yâ‚‚ :=
  action_exchange f g â–¸ actionHom_def f g

@[reassoc]
theorem actionAssocIso_inv_naturality
    {câ‚ câ‚‚ câ‚ƒ câ‚„ : C} {dâ‚ dâ‚‚ : D} (f : câ‚ âŸ¶ câ‚‚) (g : câ‚ƒ âŸ¶ câ‚„) (h : dâ‚ âŸ¶ dâ‚‚) :
    (f âŠ™â‚—â‚˜ g âŠ™â‚—â‚˜ h) â‰« (Î±â‚— câ‚‚ câ‚„ dâ‚‚).inv =
    (Î±â‚— câ‚ câ‚ƒ dâ‚).inv â‰« ((f âŠ—â‚˜ g) âŠ™â‚—â‚˜ h) := by
  rw [Iso.comp_inv_eq, Category.assoc, Eq.comm, Iso.inv_comp_eq, actionAssocIso_hom_naturality]

@[reassoc]
theorem actionUnitIso_inv_naturality {d d' : D} (f : d âŸ¶ d') :
      (Î»â‚— d).inv â‰« (ğŸ™_ C) âŠ´â‚— f = f â‰« (Î»â‚— d').inv := by
  rw [Iso.inv_comp_eq, â† Category.assoc, Eq.comm, Iso.comp_inv_eq, actionUnitIso_hom_naturality]

@[reassoc (attr := simp)]
theorem actionHomRight_hom_inv (x : C) {y z : D} (f : y â‰… z) :
    x âŠ´â‚— f.hom â‰« x âŠ´â‚— f.inv = ğŸ™ (x âŠ™â‚— y : D) := by
  rw [â† actionHomRight_comp, Iso.hom_inv_id, actionHomRight_id]

@[reassoc (attr := simp)]
theorem hom_inv_actionHomLeft {x y : C} (f : x â‰… y) (z : D) :
    f.hom âŠµâ‚— z â‰« f.inv âŠµâ‚— z = ğŸ™ (x âŠ™â‚— z) := by
  rw [â† comp_actionHomLeft, Iso.hom_inv_id, id_actionHomLeft]

@[reassoc (attr := simp)]
theorem actionHomRight_inv_hom (x : C) {y z : D} (f : y â‰… z) :
    x âŠ´â‚— f.inv â‰« x âŠ´â‚— f.hom = ğŸ™ (x âŠ™â‚— z) := by
  rw [â† actionHomRight_comp, Iso.inv_hom_id, actionHomRight_id]

@[reassoc (attr := simp)]
theorem inv_hom_actionHomLeft {x y : C} (f : x â‰… y) (z : D) :
    f.inv âŠµâ‚— z â‰« f.hom âŠµâ‚— z = ğŸ™ (y âŠ™â‚— z) := by
  rw [â† comp_actionHomLeft, Iso.inv_hom_id, id_actionHomLeft]

@[reassoc (attr := simp)]
theorem actionHomRight_hom_inv' (x : C) {y z : D} (f : y âŸ¶ z) [IsIso f] :
    x âŠ´â‚— f â‰« x âŠ´â‚— inv f = ğŸ™ (x âŠ™â‚— y) := by
  rw [â† actionHomRight_comp, IsIso.hom_inv_id, actionHomRight_id]

@[reassoc (attr := simp)]
theorem hom_inv_actionHomLeft' {x y : C} (f : x âŸ¶ y) [IsIso f] (z : D) :
    f âŠµâ‚— z â‰« inv f âŠµâ‚— z = ğŸ™ (x âŠ™â‚— z) := by
  rw [â† comp_actionHomLeft, IsIso.hom_inv_id, id_actionHomLeft]

@[reassoc (attr := simp)]
theorem actionHomRight_inv_hom' (x : C) {y z : D} (f : y âŸ¶ z) [IsIso f] :
    x âŠ´â‚— inv f â‰« x âŠ´â‚— f = ğŸ™ (x âŠ™â‚— z) := by
  rw [â† actionHomRight_comp, IsIso.inv_hom_id, actionHomRight_id]

@[reassoc (attr := simp)]
theorem inv_hom_actionHomLeft' {x y : C} (f : x âŸ¶ y) [IsIso f] (z : D) :
    inv f âŠµâ‚— z â‰« f âŠµâ‚— z = ğŸ™ (y âŠ™â‚— z) := by
  rw [â† comp_actionHomLeft, IsIso.inv_hom_id, id_actionHomLeft]

instance isIso_actionHomRight (x : C) {y z : D} (f : y âŸ¶ z) [IsIso f] :
    IsIso (x âŠ´â‚— f) :=
  âŸ¨x âŠ´â‚— inv f, by simpâŸ©

instance isIso_actionHomLeft {x y : C} (f : x âŸ¶ y) [IsIso f] (z : D) :
    IsIso (f âŠµâ‚— z) :=
  âŸ¨inv f âŠµâ‚— z, by simpâŸ©

instance isIso_actionHom {x y : C} {x' y' : D}
    (f : x âŸ¶ y) (g : x' âŸ¶ y') [IsIso f] [IsIso g] :
    IsIso (f âŠ™â‚—â‚˜ g) :=
  âŸ¨(inv f) âŠ™â‚—â‚˜ (inv g), by simp [â† actionHom_comp]âŸ©

@[simp]
lemma inv_actionHomLeft {x y : C} (f : x âŸ¶ y) [IsIso f] (z : D) :
    inv (f âŠµâ‚— z) = inv f âŠµâ‚— z :=
  IsIso.inv_eq_of_hom_inv_id <| hom_inv_actionHomLeft' f z

@[simp]
lemma inv_actionHomRight (x : C) {y z : D} (f : y âŸ¶ z) [IsIso f] :
    inv (x âŠ´â‚— f) = x âŠ´â‚— inv f :=
  IsIso.inv_eq_of_hom_inv_id <| actionHomRight_hom_inv' x f

@[simp]
lemma inv_actionHom {x y : C} {x' y' : D}
    (f : x âŸ¶ y) (g : x' âŸ¶ y') [IsIso f] [IsIso g] :
    inv (f âŠ™â‚—â‚˜ g) = (inv f) âŠ™â‚—â‚˜ (inv g) :=
  IsIso.inv_eq_of_hom_inv_id <| by simp [â† actionHom_comp]

section

variable (C D)
/-- Bundle the action of `C` on `D` as a functor `C â¥¤ D â¥¤ D`. -/
@[simps!]
def curriedAction : C â¥¤ D â¥¤ D where
  obj x :=
    { obj y := x âŠ™â‚— y
      map f := x âŠ´â‚— f }
  map f :=
    { app y := f âŠµâ‚— y
      naturality _ _ _ := by simp [action_exchange] }

variable {C} in
/-- Bundle `d â†¦ c âŠ™â‚— d` as a functor. -/
@[simps!]
abbrev actionLeft (c : C) : D â¥¤ D := curriedAction C D|>.obj c

variable {D} in
/-- Bundle `c â†¦ c âŠ™â‚— d` as a functor. -/
@[simps!]
abbrev actionRight (d : D) : C â¥¤ D := curriedAction C D|>.flip.obj d

/-- Bundle `Î±â‚— _ _ _` as an isomorphism of trifunctors. -/
@[simps!]
def actionAssocNatIso :
    bifunctorCompâ‚â‚‚ (curriedTensor C) (curriedAction C D) â‰…
    bifunctorCompâ‚‚â‚ƒ (curriedAction C D) (curriedAction C D) :=
  NatIso.ofComponents fun _ â†¦
    NatIso.ofComponents fun _ â†¦
     NatIso.ofComponents fun _ â†¦ Î±â‚— _ _ _

/-- Bundle `Î»â‚— _` as an isomorphism of functors. -/
@[simps!]
def actionUnitNatIso : actionLeft D (ğŸ™_ C) â‰… ğŸ­ D := NatIso.ofComponents (Î»â‚— Â·)

end

end MonoidalLeftAction

/-- A class that carries the non-Prop data required to define a right action of
a monoidal category `C` on a category `D`, to set up notations. -/
class MonoidalRightActionStruct [MonoidalCategoryStruct C] where
  /-- The right action on objects. This is denoted `d âŠ™áµ£ c`. -/
  actionObj : D â†’ C â†’ D
  /-- The right action of a map `f : c âŸ¶ c'` in `C` on an object `d` in `D`.
  If we are to consider the action as a functor `Î‘ : C â¥¤ D â¥¤ D`,
  this is (Î‘.map f).app d`. This is denoted `d âŠ´áµ£ f` -/
  actionHomRight (d : D) {c c' : C} (f : c âŸ¶ c') :
    actionObj d c âŸ¶ actionObj d c'
  /-- The action of an object `c : C` on a map `f : d âŸ¶ d'` in `D`.
  If we are to consider the action as a functor `Î‘ : C â¥¤ D â¥¤ D`,
  this is (Î‘.obj c).map f`. This is denoted `f âŠµáµ£ c`. -/
  actionHomLeft {d d' : D} (f : d âŸ¶ d') (c : C) :
    actionObj d c âŸ¶ actionObj d' c
  /-- The action of a pair of maps `f : c âŸ¶ c'` and `d âŸ¶ d'`. By default,
  this is defined in terms of `actionHomLeft` and `actionHomRight`. -/
  actionHom {c c' : C} {d d' : D} (f : d âŸ¶ d') (g : c âŸ¶ c') :
    actionObj d c âŸ¶ actionObj d' c' := actionHomLeft f c â‰« actionHomRight d' g
  /-- The structural isomorphism `d âŠ™áµ£ (c âŠ— c') â‰… (d âŠ™áµ£ c) âŠ™áµ£ c'`. -/
  actionAssocIso (d : D) (c c' : C) :
    actionObj d (c âŠ— c') â‰… actionObj (actionObj d c) c'
  /-- The structural isomorphism between `d âŠ™áµ£ ğŸ™_ C` and `d`. -/
  actionUnitIso (d : D) : actionObj d (ğŸ™_ C) â‰… d

namespace MonoidalRightAction

export MonoidalRightActionStruct
  (actionObj actionHomLeft actionHomRight actionHom actionAssocIso
    actionUnitIso)

-- infix priorities are aligned with the ones from `MonoidalCategoryStruct`.

/-- Notation for `actionObj`, the action of `C` on `D`. -/
scoped infixr:70 " âŠ™áµ£ " => MonoidalRightActionStruct.actionObj

/-- Notation for `actionHomLeft`, the action of `D` on morphisms in `C`. -/
scoped infixr:81 " âŠµáµ£ " => MonoidalRightActionStruct.actionHomLeft

/-- Notation for `actionHomRight`, the action of morphism in `D` on `C`. -/
scoped infixr:81 " âŠ´áµ£ " => MonoidalRightActionStruct.actionHomRight

/-- Notation for `actionHom`, the bifunctorial action of morphisms in `C` and
`D` on `- âŠ™ -`. -/
scoped infixr:70 " âŠ™áµ£â‚˜ " => MonoidalRightActionStruct.actionHom

/-- Notation for `actionAssocIso`, the structural isomorphism
`- âŠ™áµ£ (- âŠ— -) â‰… (- âŠ™áµ£ -) âŠ™áµ£ -`. -/
scoped notation "Î±áµ£ " => MonoidalRightActionStruct.actionAssocIso

/-- Notation for `actionUnitIso`, the structural isomorphism `- âŠ™áµ£ ğŸ™_ C  â‰… -`. -/
scoped notation "Ïáµ£ " => MonoidalRightActionStruct.actionUnitIso
/-- Notation for `actionUnitIso`, the structural isomorphism `- âŠ™áµ£ ğŸ™_ C  â‰… -`,
allowing one to specify the acting category. -/
scoped notation "Ïáµ£["J"]" => MonoidalRightActionStruct.actionUnitIso (C := J)

end MonoidalRightAction

open scoped MonoidalRightAction in
/-- A `MonoidalRightAction C D` is is the data of:
- For every object `c : C` and `d : D`, an object `c âŠ™áµ£ d` of `D`.
- For every morphism `f : (c : C) âŸ¶ c'` and every `d : D`, a morphism
  `f âŠµáµ£ d : c âŠ™áµ£ d âŸ¶ c' âŠ™áµ£ d`.
- For every morphism `f : (d : D) âŸ¶ d'` and every `c : C`, a morphism
  `c âŠ´áµ£ f : c âŠ™áµ£ d âŸ¶ c âŠ™áµ£ d'`.
- For every pair of morphisms `f : (c : C) âŸ¶ c'` and
  `f : (d : D) âŸ¶ d'`, a morphism `f âŠ™áµ£â‚˜ f' : c âŠ™áµ£ d âŸ¶ c' âŠ™áµ£ d'`.
- A structure isomorphism `Î±áµ£ c c' d : c âŠ— c' âŠ™áµ£ d â‰… c âŠ™áµ£ c' âŠ™áµ£ d`.
- A structure isomorphism `Ïáµ£ d : (ğŸ™_ C) âŠ™áµ£ d â‰… d`.
Furthermore, we require identities that turn `- âŠ™áµ£ -` into a bifunctor,
ensure naturality of `Î±áµ£` and `Ïáµ£`, and ensure compatibilities with
the associator and unitor isomorphisms in `C`. -/
class MonoidalRightAction [MonoidalCategory C] extends
    MonoidalRightActionStruct C D where
  actionHom_def {c c' : C} {d d' : D} (f : d âŸ¶ d') (g : c âŸ¶ c') :
      f âŠ™áµ£â‚˜ g = f âŠµáµ£ c â‰« d' âŠ´áµ£ g := by
    cat_disch
  actionHomRight_id (c : C) (d : D) : d âŠ´áµ£ ğŸ™ c = ğŸ™ (d âŠ™áµ£ c) := by cat_disch
  id_actionHomLeft (c : C) (d : D) : ğŸ™ d âŠµáµ£ c = ğŸ™ (d âŠ™áµ£ c) := by cat_disch
  actionHom_comp
      {c c' c'' : C} {d d' d'' : D} (fâ‚ : d âŸ¶ d') (fâ‚‚ : d' âŸ¶ d'')
      (gâ‚ : c âŸ¶ c') (gâ‚‚ : c' âŸ¶ c'') :
      (fâ‚ â‰« fâ‚‚) âŠ™áµ£â‚˜ (gâ‚ â‰« gâ‚‚) = (fâ‚ âŠ™áµ£â‚˜ gâ‚) â‰« (fâ‚‚ âŠ™áµ£â‚˜ gâ‚‚) := by
    cat_disch
  actionAssocIso_hom_naturality
      {dâ‚ dâ‚‚ : D} {câ‚ câ‚‚ câ‚ƒ câ‚„ : C} (f : dâ‚ âŸ¶ dâ‚‚) (g : câ‚ âŸ¶ câ‚‚) (h : câ‚ƒ âŸ¶ câ‚„) :
      (f âŠ™áµ£â‚˜ g âŠ—â‚˜ h) â‰« (Î±áµ£ dâ‚‚ câ‚‚ câ‚„).hom =
        (Î±áµ£ dâ‚ câ‚ câ‚ƒ).hom â‰« ((f âŠ™áµ£â‚˜ g) âŠ™áµ£â‚˜ h) := by
    cat_disch
  actionUnitIso_hom_naturality {d d' : D} (f : d âŸ¶ d') :
      (Ïáµ£ d).hom â‰« f = f âŠµáµ£ (ğŸ™_ C) â‰« (Ïáµ£ d').hom := by
    cat_disch
  actionHomRight_whiskerRight {c' c'' : C} (f : c' âŸ¶ c'') (c : C) (d : D) :
     d âŠ´áµ£ (f â–· c) = (Î±áµ£ _ _ _).hom â‰« ((d âŠ´áµ£ f) âŠµáµ£ c) â‰« (Î±áµ£ _ _ _).inv := by
    cat_disch
  whiskerRight_actionHomLeft (c : C) {c' c'' : C} (f : c' âŸ¶ c'') (d : D) :
     d âŠ´áµ£ (c â— f) = (Î±áµ£ d c c').hom â‰« (d âŠ™áµ£ c) âŠ´áµ£ f â‰« (Î±áµ£ d c c'').inv := by
    cat_disch
  actionHom_associator (câ‚ câ‚‚ câ‚ƒ : C) (d : D) :
      d âŠ´áµ£ (Î±_ câ‚ câ‚‚ câ‚ƒ).hom â‰« (Î±áµ£ d câ‚ (câ‚‚ âŠ— câ‚ƒ)).hom â‰«
        (Î±áµ£ (d âŠ™áµ£ câ‚ : D) câ‚‚ câ‚ƒ).hom =
      (Î±áµ£ d (câ‚ âŠ— câ‚‚ : C) câ‚ƒ).hom â‰« (Î±áµ£ d câ‚ câ‚‚).hom âŠµáµ£ câ‚ƒ := by
    cat_disch
  actionHom_leftUnitor (c : C) (d : D) :
      d âŠ´áµ£ (Î»_ c).hom = (Î±áµ£ _ _ _).hom â‰« (Ïáµ£ _).hom âŠµáµ£ c := by
    cat_disch
  actionHom_rightUnitor (c : C) (d : D) :
      d âŠ´áµ£ (Ï_ c).hom = (Î±áµ£ _ _ _).hom â‰« (Ïáµ£ _).hom := by
    cat_disch

attribute [reassoc] MonoidalRightAction.actionHom_def
attribute [reassoc, simp] MonoidalRightAction.id_actionHomLeft
attribute [reassoc, simp] MonoidalRightAction.actionHomRight_id
attribute [reassoc, simp] MonoidalRightAction.actionHomRight_whiskerRight
attribute [simp, reassoc] MonoidalRightAction.actionHom_comp
attribute [reassoc] MonoidalRightAction.actionAssocIso_hom_naturality
attribute [reassoc] MonoidalRightAction.actionUnitIso_hom_naturality
attribute [reassoc (attr := simp)] MonoidalRightAction.actionHom_associator
attribute [simp, reassoc] MonoidalRightAction.actionHom_leftUnitor
attribute [simp, reassoc] MonoidalRightAction.actionHom_rightUnitor

/-- A monoidal category acts on itself through the tensor product. -/
@[simps!]
instance selRightfAction [MonoidalCategory C] : MonoidalRightAction C C where
  actionObj x y := x âŠ— y
  actionHom f g := f âŠ—â‚˜ g
  actionUnitIso x := Ï_ x
  actionAssocIso x y z := Î±_ x y z|>.symm
  actionHomLeft f x := f â–· x
  actionHomRight x _ _ f := x â— f
  actionHom_def := by simp [tensorHom_def]

namespace MonoidalRightAction

open Category

variable {C D} [MonoidalCategory C] [MonoidalRightAction C D]

-- Simp normal forms are aligned with the ones in `MonoidalCateogry`.

@[simp]
lemma actionHom_id {d d' : D} (f : d âŸ¶ d') (c : C) :
    f âŠ™áµ£â‚˜ (ğŸ™ c) = f âŠµáµ£ c := by
  simp [actionHom_def]

@[simp]
lemma id_actionHom (d : D) {c c' : C} (f : c âŸ¶ c') :
    (ğŸ™ d) âŠ™áµ£â‚˜ f = d âŠ´áµ£ f := by
  simp [actionHom_def]

@[reassoc, simp]
theorem actionHomRight_comp (w : D) {x y z : C} (f : x âŸ¶ y) (g : y âŸ¶ z) :
    w âŠ´áµ£ (f â‰« g) = w âŠ´áµ£ f â‰« w âŠ´áµ£ g := by
  simp [â† id_actionHom, â† actionHom_comp]

@[reassoc, simp]
theorem unit_actionHomRight {x y : D} (f : x âŸ¶ y) :
    f âŠµáµ£ (ğŸ™_ C) = (Ïáµ£ x).hom â‰« f â‰« (Ïáµ£ y).inv := by
  rw [â† Category.assoc, actionUnitIso_hom_naturality]
  simp

@[reassoc, simp]
theorem actionHomLeft_tensor {z z' : D} (f : z âŸ¶ z') (x y : C) :
    (f âŠµáµ£ (x âŠ— y)) = (Î±áµ£ z x y).hom â‰« (f âŠµáµ£ x) âŠµáµ£ y â‰« (Î±áµ£ z' x y).inv := by
  simp only [â† actionHom_id]
  rw [â† Category.assoc, â† actionAssocIso_hom_naturality]
  simp

@[reassoc, simp]
theorem comp_actionHomLeft {w x y : D} (f : w âŸ¶ x) (g : x âŸ¶ y) (z : C) :
    (f â‰« g) âŠµáµ£ z = f âŠµáµ£ z â‰« g âŠµáµ£ z := by
  simp only [â† actionHom_id, â† actionHom_comp, Category.id_comp]

@[reassoc, simp]
theorem action_actionHomRight (y : D) (z : C) {x x' : C} (f : x âŸ¶ x') :
    (y âŠ™áµ£ z) âŠ´áµ£ f = (Î±áµ£ y z x).inv â‰« y âŠ´áµ£ (z â— f) â‰« (Î±áµ£ y z x').hom := by
  simp [whiskerRight_actionHomLeft]

@[reassoc]
theorem action_exchange {w x : D} {y z : C} (f : w âŸ¶ x) (g : y âŸ¶ z) :
    w âŠ´áµ£ g â‰« f âŠµáµ£ z = f âŠµáµ£ y â‰« x âŠ´áµ£ g := by
  simp only [â† id_actionHom, â† actionHom_id, â† actionHom_comp, id_comp, comp_id]

@[reassoc]
theorem actionHom_def' {xâ‚ yâ‚ : D} {xâ‚‚ yâ‚‚ : C} (f : xâ‚ âŸ¶ yâ‚) (g : xâ‚‚ âŸ¶ yâ‚‚) :
    f âŠ™áµ£â‚˜ g = xâ‚ âŠ´áµ£ g â‰« f âŠµáµ£ yâ‚‚ :=
  action_exchange f g â–¸ actionHom_def f g

@[reassoc]
theorem actionAssocIso_inv_naturality
    {dâ‚ dâ‚‚ : D} {câ‚ câ‚‚ câ‚ƒ câ‚„ : C} (f : dâ‚ âŸ¶ dâ‚‚) (g : câ‚ âŸ¶ câ‚‚) (h : câ‚ƒ âŸ¶ câ‚„) :
    ((f âŠ™áµ£â‚˜ g) âŠ™áµ£â‚˜ h) â‰« (Î±áµ£ dâ‚‚ câ‚‚ câ‚„).inv =
    (Î±áµ£ dâ‚ câ‚ câ‚ƒ).inv â‰« (f âŠ™áµ£â‚˜ g âŠ—â‚˜ h) := by
  rw [Iso.comp_inv_eq, Category.assoc, Eq.comm, Iso.inv_comp_eq, actionAssocIso_hom_naturality]

@[reassoc]
theorem actionUnitIso_inv_naturality {d d' : D} (f : d âŸ¶ d') :
      (Ïáµ£ d).inv â‰« f âŠµáµ£ (ğŸ™_ C) = f â‰« (Ïáµ£ d').inv := by
  rw [Iso.inv_comp_eq, â† Category.assoc, Eq.comm, Iso.comp_inv_eq, actionUnitIso_hom_naturality]

@[reassoc (attr := simp)]
theorem actionHomRight_hom_inv (x : D) {y z : C} (f : y â‰… z) :
    x âŠ´áµ£ f.hom â‰« x âŠ´áµ£ f.inv = ğŸ™ (x âŠ™áµ£ y : D) := by
  rw [â† actionHomRight_comp, Iso.hom_inv_id, actionHomRight_id]

@[reassoc (attr := simp)]
theorem hom_inv_actionHomLeft {x y : D} (f : x â‰… y) (z : C) :
    f.hom âŠµáµ£ z â‰« f.inv âŠµáµ£ z = ğŸ™ (x âŠ™áµ£ z) := by
  rw [â† comp_actionHomLeft, Iso.hom_inv_id, id_actionHomLeft]

@[reassoc (attr := simp)]
theorem actionHomRight_inv_hom (x : D) {y z : C} (f : y â‰… z) :
    x âŠ´áµ£ f.inv â‰« x âŠ´áµ£ f.hom = ğŸ™ (x âŠ™áµ£ z) := by
  rw [â† actionHomRight_comp, Iso.inv_hom_id, actionHomRight_id]

@[reassoc (attr := simp)]
theorem inv_hom_actionHomLeft {x y : D} (f : x â‰… y) (z : C) :
    f.inv âŠµáµ£ z â‰« f.hom âŠµáµ£ z = ğŸ™ (y âŠ™áµ£ z) := by
  rw [â† comp_actionHomLeft, Iso.inv_hom_id, id_actionHomLeft]

@[reassoc (attr := simp)]
theorem actionHomRight_hom_inv' (x : D) {y z : C} (f : y âŸ¶ z) [IsIso f] :
    x âŠ´áµ£ f â‰« x âŠ´áµ£ inv f = ğŸ™ (x âŠ™áµ£ y) := by
  rw [â† actionHomRight_comp, IsIso.hom_inv_id, actionHomRight_id]

@[reassoc (attr := simp)]
theorem hom_inv_actionHomLeft' {x y : D} (f : x âŸ¶ y) [IsIso f] (z : C) :
    f âŠµáµ£ z â‰« inv f âŠµáµ£ z = ğŸ™ (x âŠ™áµ£ z) := by
  rw [â† comp_actionHomLeft, IsIso.hom_inv_id, id_actionHomLeft]

@[reassoc (attr := simp)]
theorem actionHomRight_inv_hom' (x : D) {y z : C} (f : y âŸ¶ z) [IsIso f] :
    x âŠ´áµ£ inv f â‰« x âŠ´áµ£ f = ğŸ™ (x âŠ™áµ£ z) := by
  rw [â† actionHomRight_comp, IsIso.inv_hom_id, actionHomRight_id]

@[reassoc (attr := simp)]
theorem inv_hom_actionHomLeft' {x y : D} (f : x âŸ¶ y) [IsIso f] (z : C) :
    inv f âŠµáµ£ z â‰« f âŠµáµ£ z = ğŸ™ (y âŠ™áµ£ z) := by
  rw [â† comp_actionHomLeft, IsIso.inv_hom_id, id_actionHomLeft]

instance isIso_actionHomLeft {x y : D} (f : x âŸ¶ y) [IsIso f] (z : C) :
    IsIso (f âŠµáµ£ z) :=
  âŸ¨inv f âŠµáµ£ z, by simpâŸ©

instance isIso_actionHomRight (x : D) {y z : C} (f : y âŸ¶ z) [IsIso f] :
    IsIso (x âŠ´áµ£ f) :=
  âŸ¨x âŠ´áµ£ inv f, by simpâŸ©

instance isIso_actionHom {x y : D} {x' y' : C}
    (f : x âŸ¶ y) (g : x' âŸ¶ y') [IsIso f] [IsIso g] :
    IsIso (f âŠ™áµ£â‚˜ g) :=
  âŸ¨(inv f) âŠ™áµ£â‚˜ (inv g), by simp [â† actionHom_comp]âŸ©

@[simp]
lemma inv_actionHomLeft {x y : D} (f : x âŸ¶ y) [IsIso f] (z : C) :
    inv (f âŠµáµ£ z) = inv f âŠµáµ£ z :=
  IsIso.inv_eq_of_hom_inv_id <| hom_inv_actionHomLeft' f z

@[simp]
lemma inv_actionHomRight (x : D) {y z : C} (f : y âŸ¶ z) [IsIso f] :
    inv (x âŠ´áµ£ f) = x âŠ´áµ£ inv f :=
  IsIso.inv_eq_of_hom_inv_id <| actionHomRight_hom_inv' x f

@[simp]
lemma inv_actionHom
    {x y : D} {x' y' : C}
    (f : x âŸ¶ y) (g : x' âŸ¶ y') [IsIso f] [IsIso g] :
    inv (f âŠ™áµ£â‚˜ g) = (inv f) âŠ™áµ£â‚˜ (inv g) :=
  IsIso.inv_eq_of_hom_inv_id <| by simp [â† actionHom_comp]

section

variable (C D)
/-- Bundle the action of `C` on `D` as a functor `C â¥¤ D â¥¤ D`. -/
@[simps!]
def curriedAction : C â¥¤ D â¥¤ D where
  obj x :=
    { obj y := y âŠ™áµ£ x
      map f := f âŠµáµ£ x }
  map f :=
    { app y := y âŠ´áµ£ f
      naturality _ _ _ := by simp [action_exchange] }

variable {C} in
/-- Bundle `d â†¦ d âŠ™áµ£ c` as a functor. -/
@[simps!]
abbrev actionRight (c : C) : D â¥¤ D := curriedAction C D|>.obj c

variable {D} in
/-- Bundle `c â†¦ d âŠ™áµ£ c` as a functor. -/
@[simps!]
abbrev actionLeft (d : D) : C â¥¤ D := curriedAction C D|>.flip.obj d

/-- Bundle `Î±áµ£ _ _ _` as an isomorphism of trifunctors. -/
@[simps!]
def actionAssocNatIso :
    bifunctorCompâ‚â‚‚ (curriedTensor C) (curriedAction C D) â‰…
    (bifunctorCompâ‚‚â‚ƒ (curriedAction C D) (curriedAction C D)).flip :=
  NatIso.ofComponents fun _ â†¦
    NatIso.ofComponents fun _ â†¦
     NatIso.ofComponents fun _ â†¦ Î±áµ£ _ _ _

/-- Bundle `Ïáµ£ _` as an isomorphism of functors. -/
@[simps!]
def actionUnitNatIso : actionRight D (ğŸ™_ C) â‰… ğŸ­ D := NatIso.ofComponents (Ïáµ£ Â·)

end

end MonoidalRightAction

end CategoryTheory.MonoidalCategory
