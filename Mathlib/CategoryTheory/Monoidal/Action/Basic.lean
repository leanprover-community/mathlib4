/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.Monoidal.Category
import Mathlib.CategoryTheory.Functor.Trifunctor

/-!

# Left actions from a monoidal category on a category

Given a monoidal category `C`, and a category `D`, we define a left action of
`C` on `D` as the data of an object `c âŠ™ d` of `D` for every
`c : C` and `d : D`, as well as the data required to turn `- âŠ™ -` into
a bifunctor, along with structure natural isomorphisms
`(- âŠ— -) âŠ™ - â‰… - âŠ™ - âŠ™ -` and `ğŸ™_ C âŠ™ - â‰… -`, subject to coherence conditions.


## References
* [Janelidze, G, and Kelly, G.M., *A note on actions of a monoidal category*][JanelidzeKelly2001]

## TODOs/Projects
* Equivalence between actions of `C` on `D` and pseudofunctors from the
  classifying bicategory of `C` to `Cat`.
* Functors that respects left actions.
* Actions as monoidal functors C â¥¤ (D â¥¤ D).
* Action of `(C â¥¤ C)` on `C`.
* Modules in `D` over a monoid object in `C`. Equivalence with `Mod_` when
  `D` is `C`.
* Given a monad `M` on `C`, equivalence between `Algebra M`, and modules in `C`
  on `M.toMon : Mon_ (C â¥¤ C)`.
* Canonical action of `Type u` on `u`-small cocomplete categories via the
  copower.

-/

namespace CategoryTheory.MonoidalCategory

variable (C D : Type*)

variable [Category C] [Category D]
/-- A class that carries the non-Prop data required to define a left action of a
monoidal category `C` on a category `D`, to set up notations. -/
class MonoidalLeftActionStruct [MonoidalCategoryStruct C] where
  /-- The left action on objects. This is denoted `c âŠ™ d`. -/
  actionObj : C â†’ D â†’ D
  /-- The left action of a map `f : c âŸ¶ c'` in `C` on an object `d` in `D`.
  If we are to consider the action as a functor `Î‘ : C â¥¤ D â¥¤ D`,
  this is (Î‘.map f).obj d`. This is denoted `f âŠµ d` -/
  actionHomLeft {c c' : C} (f : c âŸ¶ c') (d : D) :
    actionObj c d âŸ¶ actionObj c' d
  /-- The action of an object `c : C` on a map `f : d âŸ¶ d'` in `D`.
  If we are to consider the action as a functor `Î‘ : C â¥¤ D â¥¤ D`,
  this is (Î‘.obj c).map f`. This is denoted `c âŠ´ f`. -/
  actionHomRight (c : C) {d d' : D} (f : d âŸ¶ d') :
    actionObj c d âŸ¶ actionObj c d'
  /-- The action of a pair of maps `f : c âŸ¶ c'` and `d âŸ¶ d'`. By default,
  this is defined in terms of `actionHomLeft` and `actionHomRight`. -/
  actionHom {c c' : C} {d d' : D} (f : c âŸ¶ c') (g : d âŸ¶ d') :
    actionObj c d âŸ¶ actionObj c' d' := actionHomLeft f d â‰« actionHomRight c' g
  /-- The structural isomorphism `(c âŠ— c') âŠ™ d â‰… c âŠ™ (c' âŠ™ d)`. -/
  actionAssocIso (c c' : C) (d : D) :
    actionObj (c âŠ— c') d â‰… actionObj c (actionObj c' d)
  /-- The structural isomorphism between `ğŸ™_ C âŠ™ d` and `d`. -/
  actionUnitIso (d : D) : actionObj (ğŸ™_ C) d â‰… d

namespace MonoidalLeftAction

export MonoidalLeftActionStruct
  (actionObj actionHomLeft actionHomRight actionHom actionAssocIso
    actionUnitIso)

-- infix priorities are aligned with the ones from `MonoidalCategoryStruct`.

/-- Notation for `actionObj`, the action of `C` on `D`.
so that `c âŠ— c' âŠ™ d` means `(c âŠ— c') âŠ™ d`. -/
scoped infixr:70 " âŠ™ " => MonoidalLeftActionStruct.actionObj

scoped infixr:81 " âŠµ " => MonoidalLeftActionStruct.actionHomLeft

scoped infixr:81 " âŠ´ " => MonoidalLeftActionStruct.actionHomRight

scoped infixr:70 " âŠ™ " => MonoidalLeftActionStruct.actionHom

scoped notation "Ïƒ_ " => MonoidalLeftActionStruct.actionAssocIso

scoped notation "Ï…_ " => MonoidalLeftActionStruct.actionUnitIso

end MonoidalLeftAction

open scoped MonoidalLeftAction in

/-- A `MonoidalLeftAction C D` is is the data of:
- For every object `c : C` and `d : D`, an object `c âŠ™ d` of `D`.
- For every morphism `f : (c : C) âŸ¶ c'` and every `d : D`, a morphism
  `f âŠµ d : c âŠ™ d âŸ¶ c' âŠ™ d`.
- For every morphism `f : (d : D) âŸ¶ d'` and every `c : C`, a morphism
  `c âŠ´ f : c âŠ™ d âŸ¶ c âŠ™ d'`.
- For every pair of morphisms `f : (c : C) âŸ¶ c'` and
  `f : (d : D) âŸ¶ d'`, a morphism `f âŠ™ f' : c âŠ™ d âŸ¶ c' âŠ™ d'`.
- A structure isomorphism `Ïƒ_ c c' d : c âŠ— c' âŠ™ d â‰… c âŠ™ c' âŠ™ d`.
- A structure isomorphism `Ï…_ d : (ğŸ™_ C) âŠ™ d â‰… d`.
Furthermore, we require identities that turn `- âŠ™ -` into a bifunctor,
ensure naturality of `Ïƒ_` and `Ï…_`, and ensure compatibilies with
the associator and unitor isomorphisms in `C`. -/
class MonoidalLeftAction [MonoidalCategory C] extends MonoidalLeftActionStruct C D where
  actionHom_def {c c' : C} {d d' : D} (f : c âŸ¶ c') (g : d âŸ¶ d') :
      f âŠ™ g = f âŠµ d â‰« c' âŠ´ g := by
    aesop_cat

  actionHomRight_id (c : C) (d : D) : c âŠ´ ğŸ™ d = ğŸ™ (c âŠ™ d) := by aesop_cat
  id_actionHomLeft (c : C) (d : D) : ğŸ™ c âŠµ d = ğŸ™ (c âŠ™ d) := by aesop_cat

  actionHom_comp
      {c c' c'' : C} {d d' d'' : D} (fâ‚ : c âŸ¶ c') (fâ‚‚ : c' âŸ¶ c'')
      (gâ‚ : d âŸ¶ d') (gâ‚‚ : d' âŸ¶ d'') :
      (fâ‚ â‰« fâ‚‚) âŠ™ (gâ‚ â‰« gâ‚‚) = (fâ‚ âŠ™ gâ‚) â‰« (fâ‚‚ âŠ™ gâ‚‚) := by
    aesop_cat

  actionAssocIso_naturality
      {câ‚ câ‚‚ câ‚ƒ câ‚„ : C} {dâ‚ dâ‚‚ : D} (f : câ‚ âŸ¶ câ‚‚) (g : câ‚ƒ âŸ¶ câ‚„) (h : dâ‚ âŸ¶ dâ‚‚) :
      ((f âŠ— g) âŠ™ h) â‰« (Ïƒ_ câ‚‚ câ‚„ dâ‚‚).hom = (Ïƒ_ câ‚ câ‚ƒ dâ‚).hom â‰« (f âŠ™ g âŠ™ h) := by
    aesop_cat

  actionUnitIso_naturality {d d' : D} (f : d âŸ¶ d') :
      (Ï…_ d).hom â‰« f = (ğŸ™_ C) âŠ´ f â‰« (Ï…_ d').hom := by
    aesop_cat

  whiskerLeft_actionHomLeft (c : C) {c' c'' : C} (f : c' âŸ¶ c'') (d : D) :
      (c â— f) âŠµ d = (Ïƒ_ _ _ _).hom â‰« c âŠ´ (f âŠµ d) â‰« (Ïƒ_ _ _ _).inv := by
    aesop_cat

  whiskerRight_actionHomLeft {c c' : C} (c'' : C) (f : c âŸ¶ c') (d : D) :
      (f â–· c'') âŠµ d = (Ïƒ_ c c'' d).hom â‰«
        f âŠµ (c'' âŠ™ d : D) â‰« (Ïƒ_ c' c'' d).inv := by
    aesop_cat

  associator_actionHom (câ‚ câ‚‚ câ‚ƒ : C) (d : D) :
      (Î±_ câ‚ câ‚‚ câ‚ƒ).hom âŠµ d â‰« (Ïƒ_ câ‚ (câ‚‚ âŠ— câ‚ƒ) d).hom â‰«
        câ‚ âŠ´ (Ïƒ_ câ‚‚ câ‚ƒ d).hom =
      (Ïƒ_ (câ‚ âŠ— câ‚‚ : C) câ‚ƒ d).hom â‰« (Ïƒ_ câ‚ câ‚‚ (câ‚ƒ âŠ™ d)).hom := by
    aesop_cat

  leftUnitor_actionHom (c : C) (d : D) :
      (Î»_ c).hom âŠµ d = (Ïƒ_ _ _ _).hom â‰« (Ï…_ _).hom := by
    aesop_cat

  rightUnitor_actionHom (c : C) (d : D) :
      (Ï_ c).hom âŠµ d = (Ïƒ_ _ _ _).hom â‰« c âŠ´ (Ï…_ _).hom := by
    aesop_cat

attribute [reassoc] MonoidalLeftAction.actionHom_def
attribute [reassoc, simp] MonoidalLeftAction.id_actionHomLeft
attribute [reassoc, simp] MonoidalLeftAction.actionHomRight_id
attribute [simp, reassoc] MonoidalLeftAction.actionHom_comp
attribute [reassoc] MonoidalLeftAction.actionAssocIso_naturality
attribute [reassoc] MonoidalLeftAction.actionUnitIso_naturality
attribute [reassoc (attr := simp)] MonoidalLeftAction.associator_actionHom
attribute [reassoc (attr := simp)] MonoidalLeftAction.leftUnitor_actionHom
attribute [reassoc (attr := simp)] MonoidalLeftAction.rightUnitor_actionHom

/-- A monoidal category acts on itself through the tensor product. -/
instance [MonoidalCategory C] : MonoidalLeftAction C C where
  actionObj x y := x âŠ— y
  actionHom f g := f âŠ— g
  actionUnitIso x := Î»_ x
  actionAssocIso x y z := Î±_ x y z
  actionHomLeft f x := f â–· x
  actionHomRight x _ _ f := x â— f
  actionHom_def := by simp [tensorHom_def]

namespace MonoidalLeftAction

open Category

variable {C D} [MonoidalCategory C] [MonoidalLeftAction C D]

-- Simp normal forms are aligned with the ones in `MonoidalCateogry`.

@[simp]
lemma id_actionHom (c : C) {d d' : D} (f : d âŸ¶ d') :
    (ğŸ™ c) âŠ™ f = c âŠ´ f := by
  simp [actionHom_def]

@[simp]
lemma actionHom_id {c c' : C} (f : c âŸ¶ c') (d : D) :
    f âŠ™ (ğŸ™ d) = f âŠµ d := by
  simp [actionHom_def]

@[reassoc, simp]
theorem actionHomRight_comp (w : C) {x y z : D} (f : x âŸ¶ y) (g : y âŸ¶ z) :
    w âŠ´ (f â‰« g) = w âŠ´ f â‰« w âŠ´ g := by
  simp [â† id_actionHom, â† actionHom_comp]

@[reassoc, simp]
theorem unit_actionHomRight {x y : D} (f : x âŸ¶ y) :
    (ğŸ™_ C) âŠ´ f = (Ï…_ x).hom â‰« f â‰« (Ï…_ y).inv := by
  rw [â† Category.assoc, actionUnitIso_naturality]
  simp

@[reassoc, simp]
theorem tensor_actionHomRight (x y : C) {z z' : D} (f : z âŸ¶ z') :
    ((x âŠ— y) âŠ´ f) = (Ïƒ_ x y z).hom â‰« x âŠ´ y âŠ´ f â‰« (Ïƒ_ x y z').inv := by
  simp only [â† id_actionHom, â† actionHom_id]
  rw [â† Category.assoc, â† actionAssocIso_naturality]
  simp

@[reassoc, simp]
theorem comp_actionHomLeft {w x y : C} (f : w âŸ¶ x) (g : x âŸ¶ y) (z : D) :
    (f â‰« g) âŠµ z = f âŠµ z â‰« g âŠµ z := by
  simp only [â† actionHom_id, â† actionHom_comp, Category.id_comp]

@[reassoc, simp]
theorem actionHomLeft_action {x x' : C} (f : x âŸ¶ x') (y : C) (z : D) :
    f âŠµ (y âŠ™ z) = (Ïƒ_ x y z).inv â‰« (f â–· y) âŠµ z â‰« (Ïƒ_ x' y z).hom := by
  simp only [â† id_actionHom, â† tensorHom_id, â† actionHom_id]
  rw [actionAssocIso_naturality]
  simp [actionHom_id]

@[reassoc, simp]
theorem action_assoc (x : C) {y y' : C} (f : y âŸ¶ y') (z : D) :
    (x â— f) âŠµ z = (Ïƒ_ x y z).hom â‰« x âŠ´ f âŠµ z â‰« (Ïƒ_ x y' z).inv := by
  simp only [â† id_actionHom, â† tensorHom_id, â† actionHom_id]
  rw [â† Category.assoc, â† actionAssocIso_naturality]
  simp

@[reassoc]
theorem action_exchange {w x : C} {y z : D} (f : w âŸ¶ x) (g : y âŸ¶ z) :
    w âŠ´ g â‰« f âŠµ z = f âŠµ y â‰« x âŠ´ g := by
  simp only [â† id_actionHom, â† actionHom_id, â† actionHom_comp, id_comp, comp_id]

@[reassoc]
theorem actionHom_def' {xâ‚ yâ‚ : C} {xâ‚‚ yâ‚‚ : D} (f : xâ‚ âŸ¶ yâ‚) (g : xâ‚‚ âŸ¶ yâ‚‚) :
    f âŠ™ g = xâ‚ âŠ´ g â‰« f âŠµ yâ‚‚ :=
  action_exchange f g â–¸ actionHom_def f g

@[reassoc (attr := simp)]
theorem actionHomRight_hom_inv (x : C) {y z : D} (f : y â‰… z) :
    (x âŠ´ f.hom â‰« x âŠ´ f.inv) = ğŸ™ (x âŠ™ y : D) := by
  rw [â† actionHomRight_comp, Iso.hom_inv_id, actionHomRight_id]

@[reassoc (attr := simp)]
theorem hom_inv_actionHomLeft {x y : C} (f : x â‰… y) (z : D) :
    f.hom âŠµ z â‰« f.inv âŠµ z = ğŸ™ (x âŠ™ z) := by
  rw [â† comp_actionHomLeft, Iso.hom_inv_id, id_actionHomLeft]

@[reassoc (attr := simp)]
theorem actionHomRight_inv_hom (x : C) {y z : D} (f : y â‰… z) :
    x âŠ´ f.inv â‰« x âŠ´ f.hom = ğŸ™ (x âŠ™ z) := by
  rw [â† actionHomRight_comp, Iso.inv_hom_id, actionHomRight_id]

@[reassoc (attr := simp)]
theorem inv_hom_actionHomLeft {x y : C} (f : x â‰… y) (z : D) :
    f.inv âŠµ z â‰« f.hom âŠµ z = ğŸ™ (y âŠ™ z) := by
  rw [â† comp_actionHomLeft, Iso.inv_hom_id, id_actionHomLeft]

@[reassoc (attr := simp)]
theorem actionHomRight_hom_inv' (x : C) {y z : D} (f : y âŸ¶ z) [IsIso f] :
    x âŠ´ f â‰« x âŠ´ inv f = ğŸ™ (x âŠ™ y) := by
  rw [â† actionHomRight_comp, IsIso.hom_inv_id, actionHomRight_id]

@[reassoc (attr := simp)]
theorem hom_inv_actionHomLeft' {x y : C} (f : x âŸ¶ y) [IsIso f] (z : D) :
    f âŠµ z â‰« inv f âŠµ z = ğŸ™ (x âŠ™ z) := by
  rw [â† comp_actionHomLeft, IsIso.hom_inv_id, id_actionHomLeft]

@[reassoc (attr := simp)]
theorem actionHomRight_inv_hom' (x : C) {y z : D} (f : y âŸ¶ z) [IsIso f] :
    x âŠ´ inv f â‰« x âŠ´ f = ğŸ™ (x âŠ™ z) := by
  rw [â† actionHomRight_comp, IsIso.inv_hom_id, actionHomRight_id]

@[reassoc (attr := simp)]
theorem inv_hom_actionHomLeft' {x y : C} (f : x âŸ¶ y) [IsIso f] (z : D) :
    inv f âŠµ z â‰« f âŠµ z = ğŸ™ (y âŠ™ z) := by
  rw [â† comp_actionHomLeft, IsIso.inv_hom_id, id_actionHomLeft]

section

variable (C D)
/-- Bundle the action of `C` on `D` as a functor `C â¥¤ D â¥¤ D`. -/
@[simps!]
def curriedAction : C â¥¤ D â¥¤ D where
  obj x :=
    { obj y := x âŠ™ y
      map f := x âŠ´ f }
  map f :=
    { app y := f âŠµ y
      naturality _ _ _ := by simp [action_exchange] }

variable {C} in
/-- Bundle `d â†¦ c âŠ™ d` as a functor. -/
@[simps!]
abbrev actionLeft (c : C) : D â¥¤ D := curriedAction C D|>.obj c

variable {D} in
/-- Bundle `c â†¦ c âŠ™ d` as a functor. -/
@[simps!]
abbrev actionRight (d : D) : C â¥¤ D := curriedAction C D|>.flip.obj d

/-- Bundle `Ïƒ_ _ _ _` as an isomorphism of trifunctors. -/
@[simps!]
def actionAssocNatIso :
    (Functor.postcomposeâ‚‚.obj (curriedAction C D)|>.obj
      (curriedTensor C)) â‰…
    bifunctorCompâ‚‚â‚ƒFunctor|>.obj (curriedAction C D)|>.obj
      (curriedAction C D) :=
  NatIso.ofComponents fun _ â†¦
    NatIso.ofComponents fun _ â†¦
     NatIso.ofComponents fun _ â†¦ Ïƒ_ _ _ _

/-- Bundle `Ï…_ _` as an isomorphism of functors. -/
@[simps!]
def actionUnitNatIso :
    actionLeft D (ğŸ™_ C) â‰… ğŸ­ D :=
  NatIso.ofComponents (fun _ â†¦ Ï…_ _)

end

end MonoidalLeftAction

end CategoryTheory.MonoidalCategory
