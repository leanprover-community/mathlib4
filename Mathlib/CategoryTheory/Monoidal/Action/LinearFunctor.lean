/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.Monoidal.Action.Basic

/-! # Functors that are linear with respect to an action

Given a monoidal category `C` acting on the left or on the right on categories
`D` and `D'`, we introduce the following typeclasses on functors `F : D â¥¤ D'` to
express compatibility of `F` with the action of `C`:
* `F.LaxLeftLinear C` bundles the "lineator" as a morphism
  `Î¼â‚— : c âŠ™â‚— F.obj d âŸ¶ F.obj (c âŠ™â‚— d)`.
* `F.LaxRightLinear C` bundles the "lineator" as a morphism
  `Î¼áµ£ : F.obj d âŠ™áµ£ c âŸ¶ F.obj (d âŠ™áµ£ c)`.
* `F.OplaxLeftLinear C` bundles the "lineator" as a morphism
  `Î´â‚— : F.obj (c âŠ™â‚— d) âŸ¶ c âŠ™â‚— F.obj d `.
* `F.OplaxRightLinear C` bundles the "lineator" as a morphism
  `Î´áµ£ : F.obj (d âŠ™áµ£ d) âŸ¶ F.obj d âŠ™áµ£ c`.
* `F.LeftLinear C` expresses that `F` has both a `LaxLeftLinear C` and
  an `F.OplaxLeftLinear C` structure, and that they are compatible, i.e
  `Î´â‚— F` is a left and right inverse to `Î¼â‚—`.
* `F.RightLinear C` expresses that `F` has both a `LaxRightLinear C` and
  an `F.OplaxRightLinear C` structure, and that they are compatible, i.e
  `Î´áµ£ F` is a left and right inverse to `Î¼áµ£`.

-/

namespace CategoryTheory.Functor

variable {D D' : Type*} [Category D] [Category D']

open MonoidalCategory

section leftAction

open MonoidalLeftAction

/-- `F.LaxLinear C` equips `F : D â¥¤ D'` with a family of morphisms
`Î¼â‚— F : âˆ€ (c : C) (d : D), c âŠ™â‚— F.obj d âŸ¶ F.obj (c âŠ™â‚— d)`
that is natural in each variable and coherent with respect to left actions
of `C` on `D` and `D'`. -/
class LaxLeftLinear
    (F : D â¥¤ D') (C : Type*) [Category C] [MonoidalCategory C]
    [MonoidalLeftAction C D] [MonoidalLeftAction C D'] where
  /-- The "Î¼â‚—" morphism. -/
  Î¼â‚— (F) (c : C) (d : D) : c âŠ™â‚— F.obj d âŸ¶ F.obj (c âŠ™â‚— d)
  Î¼â‚—_naturality_left (F) {c c': C} (f : c âŸ¶ c') (d : D) :
    f âŠµâ‚— F.obj d â‰« Î¼â‚— c' d = Î¼â‚— c d â‰« F.map (f âŠµâ‚— d) := by cat_disch
  Î¼â‚—_naturality_right (F) (c : C) {d d' : D} (f : d âŸ¶ d') :
    c âŠ´â‚— F.map f â‰« Î¼â‚— c d' = Î¼â‚— c d â‰« F.map (c âŠ´â‚— f) := by cat_disch
  Î¼â‚—_associativity (F) (c c' : C) (d : D) :
    Î¼â‚— (c âŠ— c') d â‰« F.map (Î±â‚— _ _ _).hom =
    (Î±â‚— c c' (F.obj d)).hom â‰« c âŠ´â‚— Î¼â‚— c' d â‰«
      Î¼â‚— c (c' âŠ™â‚— d) := by cat_disch
  Î¼â‚—_unitality (F) (d : D) :
    (Î»â‚— (F.obj d)).hom = Î¼â‚— (ğŸ™_ C) d â‰« F.map (Î»â‚— d).hom := by cat_disch

namespace LaxLeftLinear

-- These are [reassoc (attr := simp)] on the basis that analog lemmas for
-- lax monoidal functors are also [reassoc (attr := simp)].
attribute [reassoc (attr := simp)] Î¼â‚—_naturality_right
attribute [reassoc (attr := simp)] Î¼â‚—_naturality_left
attribute [reassoc (attr := simp)] Î¼â‚—_associativity
attribute [simp, reassoc] Î¼â‚—_unitality

variable
  (F : D â¥¤ D') {C : Type*} [Category C] [MonoidalCategory C]
  [MonoidalLeftAction C D] [MonoidalLeftAction C D']
  [F.LaxLeftLinear C]

@[reassoc (attr := simp)]
lemma Î¼â‚—_associativity_inv (c c' : C) (d : D) :
    c âŠ´â‚— Î¼â‚— F c' d â‰« Î¼â‚— F c (c' âŠ™â‚— d) â‰« F.map (Î±â‚— _ _ _).inv =
    (Î±â‚— c c' (F.obj d)).inv â‰« Î¼â‚— F (c âŠ— c') d := by
  simpa [-Î¼â‚—_associativity, -Î¼â‚—_associativity_assoc] using
    (Î±â‚— _ _ _).inv â‰«=
      (Î¼â‚—_associativity F c c' d).symm =â‰«
      F.map (Î±â‚— _ _ _).inv

@[reassoc (attr := simp)]
lemma Î¼â‚—_unitality_inv (d : D) :
     (Î»â‚— (F.obj d)).inv â‰« Î¼â‚— F (ğŸ™_ C) d = F.map (Î»â‚— d).inv := by
  simpa [-Î¼â‚—_unitality] using
    (Î»â‚—[C] (F.obj d)).inv â‰«=
      (Î¼â‚—_unitality F d).symm =â‰«
      F.map (Î»â‚—[C] d).inv

end LaxLeftLinear

/-- `F.OplaxLinear C` equips `F : D â¥¤ D'` with a family of morphisms
`Î´â‚— F : âˆ€ (c : C) (d : D), c âŠ™â‚— F.obj d âŸ¶ F.obj (c âŠ™â‚— d)`
that is natural in each variable and coherent with respect to left actions
of `C` on `D` and `D'`. -/
class OplaxLeftLinear
    (F : D â¥¤ D') (C : Type*) [Category C] [MonoidalCategory C]
    [MonoidalLeftAction C D] [MonoidalLeftAction C D'] where
  /-- The oplax lineator morphism. -/
  Î´â‚— (F) (c : C) (d : D) : F.obj (c âŠ™â‚— d) âŸ¶ c âŠ™â‚— F.obj d
  Î´â‚—_naturality_left (F) {c c': C} (f : c âŸ¶ c') (d : D) :
    F.map (f âŠµâ‚— d) â‰« Î´â‚— c' d =
    Î´â‚— c d â‰« f âŠµâ‚— F.obj d := by cat_disch
  Î´â‚—_naturality_right (F) (c : C) {d d' : D} (f : d âŸ¶ d') :
    F.map (c âŠ´â‚— f) â‰« Î´â‚— c d' =
    Î´â‚— c d â‰« c âŠ´â‚— F.map f := by cat_disch
  Î´â‚—_associativity (F) (c c' : C) (d : D) :
    Î´â‚— (c âŠ— c') d â‰« (Î±â‚— _ _ _).hom =
    F.map (Î±â‚— _ _ _).hom â‰« Î´â‚— c (c' âŠ™â‚— d) â‰«
      c âŠ´â‚— Î´â‚— c' d := by cat_disch
  Î´â‚—_unitality_inv (F) (d : D) :
    (Î»â‚— (F.obj d)).inv =
    F.map (Î»â‚— d).inv â‰« Î´â‚— (ğŸ™_ C) d := by cat_disch

namespace OplaxLeftLinear

-- These are [reassoc (attr := simp)] on the basis that analog lemmas for
-- oplax monoidal functors are also [reassoc (attr := simp)].
attribute [reassoc (attr := simp)] Î´â‚—_naturality_right
attribute [reassoc (attr := simp)] Î´â‚—_naturality_left
attribute [reassoc (attr := simp)] Î´â‚—_associativity
attribute [simp, reassoc] Î´â‚—_unitality_inv

variable
  (F : D â¥¤ D') {C : Type*} [Category C] [MonoidalCategory C]
  [MonoidalLeftAction C D] [MonoidalLeftAction C D']
  [F.OplaxLeftLinear C]

@[reassoc (attr := simp)]
lemma Î´â‚—_associativity_inv (c c' : C) (d : D) :
    Î´â‚— F c (c' âŠ™â‚— d) â‰«
      c âŠ´â‚— Î´â‚— F c' d â‰« (Î±â‚— _ _ _).inv =
    F.map (Î±â‚— _ _ _).inv â‰« Î´â‚— F (c âŠ— c' : C) d := by
  simpa [-Î´â‚—_associativity, -Î´â‚—_associativity_assoc] using
    F.map (Î±â‚— _ _ _).inv â‰«=
      (Î´â‚—_associativity F c c' d).symm =â‰«
      (Î±â‚— _ _ _).inv

@[reassoc (attr := simp)]
lemma Î´â‚—_unitality_hom (d : D) :
    Î´â‚— F (ğŸ™_ C) d â‰« (Î»â‚— (F.obj d)).hom = F.map (Î»â‚— d).hom := by
  simpa [-Î´â‚—_unitality_inv] using
    F.map (Î»â‚—[C] d).hom â‰«=
      (Î´â‚—_unitality_inv F d).symm =â‰«
      (Î»â‚—[C] (F.obj d)).hom

end OplaxLeftLinear

/-- `F.LeftLinear C` asserts that `F` is both lax and oplax left-linear,
in a compatible way, i.e that `Î¼â‚—` is inverse to `Î´â‚—`. -/
class LeftLinear
    (F : D â¥¤ D') (C : Type*) [Category C] [MonoidalCategory C]
    [MonoidalLeftAction C D] [MonoidalLeftAction C D'] extends
    F.LaxLeftLinear C, F.OplaxLeftLinear C where
  Î¼â‚—_comp_Î´â‚— (F) (c : C) (d : D) :
    LaxLeftLinear.Î¼â‚— F c d â‰« OplaxLeftLinear.Î´â‚— F c d = ğŸ™ _
  Î´â‚—_comp_Î¼â‚— (F) (c : C) (d : D) :
    OplaxLeftLinear.Î´â‚— F c d â‰« LaxLeftLinear.Î¼â‚— F c d = ğŸ™ _

namespace LeftLinear

open LaxLeftLinear OplaxLeftLinear

variable
  (F : D â¥¤ D') {C : Type*} [Category C] [MonoidalCategory C]
  [MonoidalLeftAction C D] [MonoidalLeftAction C D']
  [F.LeftLinear C]

attribute [reassoc (attr := simp)] Î¼â‚—_comp_Î´â‚—
attribute [reassoc (attr := simp)] Î´â‚—_comp_Î¼â‚—

/-- A shorthand to bundle the Î¼â‚— as an isomorphism -/
abbrev Î¼â‚—Iso (c : C) (d : D) : c âŠ™â‚— F.obj d â‰… F.obj (c âŠ™â‚— d) where
  hom := LaxLeftLinear.Î¼â‚— F c d
  inv := OplaxLeftLinear.Î´â‚— F c d

variable (c : C) (d : D)

instance : IsIso (Î¼â‚— F c d) := Iso.isIso_hom (Î¼â‚—Iso F c d)

instance : IsIso (Î´â‚— F c d) := Iso.isIso_inv (Î¼â‚—Iso F c d)

@[simp]
lemma inv_Î¼â‚— :
    CategoryTheory.inv (Î¼â‚— F c d) = Î´â‚— F c d :=
  Eq.symm <| IsIso.eq_inv_of_hom_inv_id <| Î¼â‚—_comp_Î´â‚— F c d

@[simp]
lemma inv_Î´â‚— :
    CategoryTheory.inv (Î´â‚— F c d) = Î¼â‚— F c d :=
  Eq.symm <| IsIso.eq_inv_of_hom_inv_id <| Î´â‚—_comp_Î¼â‚— F c d

end LeftLinear

end leftAction

section rightAction

open MonoidalRightAction

/-- `F.LaxLinear C` equips `F : D â¥¤ D'` with a family of morphisms
`Î¼â‚— F : âˆ€ (c : C) (d : D), c âŠ™â‚— F.obj d âŸ¶ F.obj (c âŠ™â‚— d)`
that is natural in each variable and coherent with respect to left actions
of `C` on `D` and `D'`. -/
class LaxRightLinear
    (F : D â¥¤ D') (C : Type*) [Category C] [MonoidalCategory C]
    [MonoidalRightAction C D] [MonoidalRightAction C D'] where
  /-- The "Î¼áµ£" morphism. -/
  Î¼áµ£ (F) (d : D) (c : C) : F.obj d âŠ™áµ£ c âŸ¶ F.obj (d âŠ™áµ£ c)
  Î¼áµ£_naturality_right (F) (d : D) {c c': C} (f : c âŸ¶ c') :
    F.obj d âŠ´áµ£ f â‰« Î¼áµ£ d c' = Î¼áµ£ d c â‰« F.map (d âŠ´áµ£ f) := by cat_disch
  Î¼áµ£_naturality_left (F) {d d' : D} (f : d âŸ¶ d') (c : C) :
    F.map f âŠµáµ£ c â‰« Î¼áµ£ d' c = Î¼áµ£ d c â‰« F.map (f âŠµáµ£ c) := by cat_disch
  Î¼áµ£_associativity (F) (d : D) (c c' : C) :
    Î¼áµ£ d (c âŠ— c') â‰« F.map (Î±áµ£ _ _ _).hom =
    (Î±áµ£ (F.obj d) c c').hom â‰« (Î¼áµ£ d c) âŠµáµ£ c' â‰«
      Î¼áµ£ (d âŠ™áµ£ c) c' := by cat_disch
  Î¼áµ£_unitality (F) (d : D) :
    (Ïáµ£ (F.obj d)).hom = Î¼áµ£ d (ğŸ™_ C) â‰« F.map (Ïáµ£ d).hom := by cat_disch

namespace LaxRightLinear

-- These are [reassoc (attr := simp)] on the basis that analog lemmas for
-- lax monoidal functors are also [reassoc (attr := simp)].
attribute [reassoc (attr := simp)] Î¼áµ£_naturality_right
attribute [reassoc (attr := simp)] Î¼áµ£_naturality_left
attribute [reassoc (attr := simp)] Î¼áµ£_associativity
attribute [simp, reassoc] Î¼áµ£_unitality

variable
  (F : D â¥¤ D') {C : Type*} [Category C] [MonoidalCategory C]
  [MonoidalRightAction C D] [MonoidalRightAction C D']
  [F.LaxRightLinear C]

@[reassoc (attr := simp)]
lemma Î¼áµ£_associativity_inv (d : D) (c c' : C) :
    Î¼áµ£ F d c âŠµáµ£ c' â‰« Î¼áµ£ F (d âŠ™áµ£ c) c' â‰« F.map (Î±áµ£ _ _ _).inv =
    (Î±áµ£ (F.obj d) c c' ).inv â‰« Î¼áµ£ F d (c âŠ— c') := by
  simpa [-Î¼áµ£_associativity, -Î¼áµ£_associativity_assoc] using
    (Î±áµ£ _ _ _).inv â‰«=
      (Î¼áµ£_associativity F d c c').symm =â‰«
      F.map (Î±áµ£ _ _ _).inv

@[reassoc (attr := simp)]
lemma Î¼áµ£_unitality_inv (d : D) :
     (Ïáµ£ (F.obj d)).inv â‰« Î¼áµ£ F d (ğŸ™_ C) = F.map (Ïáµ£ d).inv := by
  simpa [-Î¼áµ£_unitality] using
    (Ïáµ£[C] (F.obj d)).inv â‰«=
      (Î¼áµ£_unitality F d).symm =â‰«
      F.map (Ïáµ£[C] d).inv

end LaxRightLinear

/-- `F.OplaxLinear C` equips `F : D â¥¤ D'` with a family of morphisms
`Î´â‚— F : âˆ€ (c : C) (d : D), c âŠ™â‚— F.obj d âŸ¶ F.obj (c âŠ™â‚— d)`
that is natural in each variable and coherent with respect to left actions
of `C` on `D` and `D'`. -/
class OplaxRightLinear
    (F : D â¥¤ D') (C : Type*) [Category C] [MonoidalCategory C]
    [MonoidalRightAction C D] [MonoidalRightAction C D'] where
  /-- The oplax lineator morphism. -/
  Î´áµ£ (F) (d : D) (c : C) : F.obj (d âŠ™áµ£ c) âŸ¶ (F.obj d) âŠ™áµ£ c
  Î´áµ£_naturality_right (F) (d : D) {c c': C} (f : c âŸ¶ c') :
    F.map (d âŠ´áµ£ f) â‰« Î´áµ£ d c' =
    Î´áµ£ d c â‰« F.obj d âŠ´áµ£ f := by cat_disch
  Î´áµ£_naturality_left (F) {d d' : D} (f : d âŸ¶ d') (c : C) :
    F.map (f âŠµáµ£ c) â‰« Î´áµ£ d' c =
    Î´áµ£ d c â‰« F.map f âŠµáµ£ c := by cat_disch
  Î´áµ£_associativity (F) (d : D) (c c' : C) :
    Î´áµ£ d (c âŠ— c') â‰« (Î±áµ£ _ _ _).hom =
    F.map (Î±áµ£ _ _ _).hom â‰« Î´áµ£ (d âŠ™áµ£ c) c' â‰«
      Î´áµ£ d c âŠµáµ£ c' := by cat_disch
  Î´áµ£_unitality_inv (F) (d : D) :
    (Ïáµ£ (F.obj d)).inv =
    F.map (Ïáµ£ d).inv â‰« Î´áµ£ d (ğŸ™_ C) := by cat_disch

namespace OplaxRightLinear

-- These are [reassoc (attr := simp)] on the basis that analog lemmas for
-- oplax monoidal functors are also [reassoc (attr := simp)].
attribute [reassoc (attr := simp)] Î´áµ£_naturality_right
attribute [reassoc (attr := simp)] Î´áµ£_naturality_left
attribute [reassoc (attr := simp)] Î´áµ£_associativity
attribute [simp, reassoc] Î´áµ£_unitality_inv

variable
  (F : D â¥¤ D') {C : Type*} [Category C] [MonoidalCategory C]
  [MonoidalRightAction C D] [MonoidalRightAction C D']
  [F.OplaxRightLinear C]

@[reassoc (attr := simp)]
lemma Î´áµ£_associativity_inv (d : D) (c c' : C) :
    Î´áµ£ F (d âŠ™áµ£ c) c' â‰«
      Î´áµ£ F d c âŠµáµ£ c' â‰« (Î±áµ£ _ _ _).inv =
    F.map (Î±áµ£ _ _ _).inv â‰« Î´áµ£ F d (c âŠ— c' : C) := by
  simpa [-Î´áµ£_associativity, -Î´áµ£_associativity_assoc] using
    F.map (Î±áµ£ _ _ _).inv â‰«=
      (Î´áµ£_associativity F d c c').symm =â‰«
      (Î±áµ£ _ _ _).inv

@[reassoc (attr := simp)]
lemma Î´áµ£_unitality_hom (d : D) :
    Î´áµ£ F d (ğŸ™_ C) â‰« (Ïáµ£ (F.obj d)).hom = F.map (Ïáµ£ d).hom := by
  simpa [-Î´áµ£_unitality_inv] using
    F.map (Ïáµ£[C] d).hom â‰«=
      (Î´áµ£_unitality_inv F d).symm =â‰«
      (Ïáµ£[C] (F.obj d)).hom

end OplaxRightLinear

/-- `F.RightLinear C` asserts that `F` is both lax and oplax left-linear,
in a compatible way, i.e that `Î¼áµ£` is inverse to `Î´áµ£`. -/
class RightLinear
    (F : D â¥¤ D') (C : Type*) [Category C] [MonoidalCategory C]
    [MonoidalRightAction C D] [MonoidalRightAction C D'] extends
    F.LaxRightLinear C, F.OplaxRightLinear C where
  Î¼áµ£_comp_Î´áµ£ (F) (d : D) (c : C) :
    LaxRightLinear.Î¼áµ£ F d c â‰« OplaxRightLinear.Î´áµ£ F d c = ğŸ™ _
  Î´áµ£_comp_Î¼áµ£ (F) (d : D) (c : C) :
    OplaxRightLinear.Î´áµ£ F d c â‰« LaxRightLinear.Î¼áµ£ F d c = ğŸ™ _

namespace RightLinear

open LaxRightLinear OplaxRightLinear

variable
  (F : D â¥¤ D') {C : Type*} [Category C] [MonoidalCategory C]
  [MonoidalRightAction C D] [MonoidalRightAction C D']
  [F.RightLinear C]

attribute [reassoc (attr := simp)] Î¼áµ£_comp_Î´áµ£
attribute [reassoc (attr := simp)] Î´áµ£_comp_Î¼áµ£

/-- A shorthand to bundle the Î¼áµ£ as an isomorphism -/
abbrev Î¼áµ£Iso (d : D) (c : C) : F.obj d âŠ™áµ£ c â‰… F.obj (d âŠ™áµ£ c) where
  hom := LaxRightLinear.Î¼áµ£ F d c
  inv := OplaxRightLinear.Î´áµ£ F d c

variable (c : C) (d : D)

instance : IsIso (Î¼áµ£ F d c) := Iso.isIso_hom (Î¼áµ£Iso F d c)

instance : IsIso (Î´áµ£ F d c) := Iso.isIso_inv (Î¼áµ£Iso F d c)

@[simp]
lemma inv_Î¼áµ£ :
    CategoryTheory.inv (Î¼áµ£ F d c) = Î´áµ£ F d c :=
  Eq.symm <| IsIso.eq_inv_of_hom_inv_id <| Î¼áµ£_comp_Î´áµ£ F d c

@[simp]
lemma inv_Î´áµ£ :
    CategoryTheory.inv (Î´áµ£ F d c) = Î¼áµ£ F d c :=
  Eq.symm <| IsIso.eq_inv_of_hom_inv_id <| Î´áµ£_comp_Î¼áµ£ F d c

end RightLinear

end rightAction

end CategoryTheory.Functor
