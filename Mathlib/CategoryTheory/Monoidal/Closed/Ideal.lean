/-
Copyright (c) 2021 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Terminal
import Mathlib.CategoryTheory.Limits.Constructions.FiniteProductsOfBinaryProducts
import Mathlib.CategoryTheory.Monad.Limits
import Mathlib.CategoryTheory.Adjunction.FullyFaithful
import Mathlib.CategoryTheory.Adjunction.Limits
import Mathlib.CategoryTheory.Adjunction.Reflective
import Mathlib.CategoryTheory.Monoidal.Closed.Cartesian
import Mathlib.CategoryTheory.Subterminal

/-!
# Exponential ideals

An exponential ideal of a Cartesian-closed category `C` is a subcategory `D âŠ† C` such that for any
`B : D` and `A : C`, the exponential `A âŸ¹ B` is in `D`: resembling ring-theoretic ideals. We
define the notion here for inclusion functors `i : D â¥¤ C` rather than explicit subcategories to
preserve the principle of equivalence.

We additionally show that if `C` is Cartesian closed and `i : D â¥¤ C` is a reflective functor, the
following are equivalent.
* The left adjoint to `i` preserves binary (equivalently, finite) products.
* `i` is an exponential ideal.
-/


universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

noncomputable section

namespace CategoryTheory

open Category

section Ideal

variable {C : Type uâ‚} {D : Type uâ‚‚} [Category.{vâ‚} C] [Category.{vâ‚} D] {i : D â¥¤ C}
variable (i) [CartesianMonoidalCategory C] [CartesianClosed C]

/-- The subcategory `D` of `C` expressed as an inclusion functor is an *exponential ideal* if
`B âˆˆ D` implies `A âŸ¹ B âˆˆ D` for all `A`.
-/
class ExponentialIdeal : Prop where
  exp_closed : âˆ€ {B}, i.essImage B â†’ âˆ€ A, i.essImage (A âŸ¹ B)
attribute [nolint docBlame] ExponentialIdeal.exp_closed

/-- To show `i` is an exponential ideal it suffices to show that `A âŸ¹ iB` is "in" `D` for any `A` in
`C` and `B` in `D`.
-/
theorem ExponentialIdeal.mk' (h : âˆ€ (B : D) (A : C), i.essImage (A âŸ¹ i.obj B)) :
    ExponentialIdeal i :=
  âŸ¨fun hB A => by
    rcases hB with âŸ¨B', âŸ¨iB'âŸ©âŸ©
    exact Functor.essImage.ofIso ((exp A).mapIso iB') (h B' A)âŸ©

/-- The entire category viewed as a subcategory is an exponential ideal. -/
instance : ExponentialIdeal (ðŸ­ C) :=
  ExponentialIdeal.mk' _ fun _ _ => âŸ¨_, âŸ¨Iso.refl _âŸ©âŸ©

open CartesianClosed

/-- The subcategory of subterminal objects is an exponential ideal. -/
instance : ExponentialIdeal (subterminalInclusion C) := by
  apply ExponentialIdeal.mk'
  intro B A
  refine âŸ¨âŸ¨A âŸ¹ B.1, fun Z g h => ?_âŸ©, âŸ¨Iso.refl _âŸ©âŸ©
  exact uncurry_injective (B.2 (CartesianClosed.uncurry g) (CartesianClosed.uncurry h))

/-- If `D` is a reflective subcategory, the property of being an exponential ideal is equivalent to
the presence of a natural isomorphism `i â‹™ exp A â‹™ leftAdjoint i â‹™ i â‰… i â‹™ exp A`, that is:
`(A âŸ¹ iB) â‰… i L (A âŸ¹ iB)`, naturally in `B`.
The converse is given in `ExponentialIdeal.mk_of_iso`.
-/
def exponentialIdealReflective (A : C) [Reflective i] [ExponentialIdeal i] :
    i â‹™ exp A â‹™ reflector i â‹™ i â‰… i â‹™ exp A := by
  symm
  apply NatIso.ofComponents _ _
  Â· intro X
    haveI := Functor.essImage.unit_isIso (ExponentialIdeal.exp_closed (i.obj_mem_essImage X) A)
    apply asIso ((reflectorAdjunction i).unit.app (A âŸ¹ i.obj X))
  Â· simp [asIso]

/-- Given a natural isomorphism `i â‹™ exp A â‹™ leftAdjoint i â‹™ i â‰… i â‹™ exp A`, we can show `i`
is an exponential ideal.
-/
theorem ExponentialIdeal.mk_of_iso [Reflective i]
    (h : âˆ€ A : C, i â‹™ exp A â‹™ reflector i â‹™ i â‰… i â‹™ exp A) : ExponentialIdeal i := by
  apply ExponentialIdeal.mk'
  intro B A
  exact âŸ¨_, âŸ¨(h A).app BâŸ©âŸ©

end Ideal

section

variable {C : Type uâ‚} {D : Type uâ‚‚} [Category.{vâ‚} C] [Category.{vâ‚} D]
variable (i : D â¥¤ C)

/- This cannot be a local instance since it has free variables,
it can instead be used as a have when needed.
We assume HasFiniteProducts D as a hypothesis below, to avoid making this a local instance.
-/
theorem reflective_products [Limits.HasFiniteProducts C] [Reflective i] :
    Limits.HasFiniteProducts D := âŸ¨fun _ => hasLimitsOfShape_of_reflective iâŸ©

open CartesianClosed MonoidalCategory CartesianMonoidalCategory

open Limits in
/-- Given a reflective subcategory `D` of a category with chosen finite products `C`, `D` admits
finite chosen products. -/
-- Note: This is not an instance as one might already have a (different) `CartesianMonoidalCategory`
-- instance on `D` (as for example with sheaves).
-- See note [reducible non-instances]
abbrev CartesianMonoidalCategory.ofReflective [CartesianMonoidalCategory C] [Reflective i] :
    CartesianMonoidalCategory D :=
  .ofChosenFiniteProducts
    ({  cone := Limits.asEmptyCone <| (reflector i).obj (ðŸ™_ C)
        isLimit := by
          apply isLimitOfReflects i
          apply isLimitChangeEmptyCone _ isTerminalTensorUnit
          letI : IsIso ((reflectorAdjunction i).unit.app (ðŸ™_ C)) := by
            have := reflective_products i
            refine Functor.essImage.unit_isIso âŸ¨terminal D, âŸ¨PreservesTerminal.iso i |>.trans ?_âŸ©âŸ©
            exact IsLimit.conePointUniqueUpToIso (limit.isLimit _) isTerminalTensorUnit
          exact asIso ((reflectorAdjunction i).unit.app (ðŸ™_ C)) })
  fun X Y â†¦
    { cone := BinaryFan.mk
        ((reflector i).map (fst (i.obj X) (i.obj Y)) â‰« (reflectorAdjunction i).counit.app _)
        ((reflector i).map (snd (i.obj X) (i.obj Y)) â‰« (reflectorAdjunction i).counit.app _)
      isLimit := by
        apply isLimitOfReflects i
        apply IsLimit.equivOfNatIsoOfIso (pairComp X Y _) _ _ _|>.invFun
          (tensorProductIsBinaryProduct (i.obj X) (i.obj Y))
        fapply BinaryFan.ext
        Â· change (reflector i â‹™ i).obj (i.obj X âŠ— i.obj Y) â‰… (ðŸ­ C).obj (i.obj X âŠ— i.obj Y)
          letI : IsIso ((reflectorAdjunction i).unit.app (i.obj X âŠ— i.obj Y)) := by
            apply Functor.essImage.unit_isIso
            haveI := reflective_products i
            use Limits.prod X Y
            constructor
            apply Limits.PreservesLimitPair.iso i _ _|>.trans
            refine Limits.IsLimit.conePointUniqueUpToIso (limit.isLimit (pair (i.obj X) (i.obj Y)))
              (tensorProductIsBinaryProduct _ _)
          exact asIso ((reflectorAdjunction i).unit.app (i.obj X âŠ— i.obj Y))|>.symm
        Â· simp only [BinaryFan.fst, Cones.postcompose, pairComp]
          simp [â† Functor.comp_map, â† NatTrans.naturality_assoc]
        Â· simp only [BinaryFan.snd, Cones.postcompose, pairComp]
          simp [â† Functor.comp_map, â† NatTrans.naturality_assoc] }

@[deprecated (since := "2025-05-15")]
noncomputable alias reflectiveChosenFiniteProducts := CartesianMonoidalCategory.ofReflective

variable [CartesianMonoidalCategory C] [Reflective i] [CartesianClosed C]
  [CartesianMonoidalCategory D]

/-- If the reflector preserves binary products, the subcategory is an exponential ideal.
This is the converse of `preservesBinaryProductsOfExponentialIdeal`.
-/
instance (priority := 10) exponentialIdeal_of_preservesBinaryProducts
    [Limits.PreservesLimitsOfShape (Discrete Limits.WalkingPair) (reflector i)] :
    ExponentialIdeal i := by
  let ir := reflectorAdjunction i
  let L : C â¥¤ D := reflector i
  let Î· : ðŸ­ C âŸ¶ L â‹™ i := ir.unit
  let Îµ : i â‹™ L âŸ¶ ðŸ­ D := ir.counit
  apply ExponentialIdeal.mk'
  intro B A
  let q : i.obj (L.obj (A âŸ¹ i.obj B)) âŸ¶ A âŸ¹ i.obj B := by
    apply CartesianClosed.curry (ir.homEquiv _ _ _)
    apply _ â‰« (ir.homEquiv _ _).symm ((exp.ev A).app (i.obj B))
    exact prodComparison L A _ â‰« (_ â— (Îµ.app _)) â‰« inv (prodComparison _ _ _)
  have : Î·.app (A âŸ¹ i.obj B) â‰« q = ðŸ™ (A âŸ¹ i.obj B) := by
    dsimp
    rw [â† curry_natural_left, curry_eq_iff, uncurry_id_eq_ev, â† ir.homEquiv_naturality_left,
      ir.homEquiv_apply_eq, assoc, assoc, prodComparison_natural_whiskerLeft_assoc,
      â† whiskerLeft_comp_assoc, ir.left_triangle_components, whiskerLeft_id, id_comp]
    apply IsIso.hom_inv_id_assoc
  haveI : IsSplitMono (Î·.app (A âŸ¹ i.obj B)) := IsSplitMono.mk' âŸ¨_, thisâŸ©
  apply mem_essImage_of_unit_isSplitMono

variable [ExponentialIdeal i]

/-- If `i` witnesses that `D` is a reflective subcategory and an exponential ideal, then `D` is
itself Cartesian closed.

To allow for better control of definitional equality, this construction
takes in an explicit choice of lift of the essential image of `i` to `D`, in the form of a functor
`l : i.EssImageSubcategory â¥¤ D` and natural isomorphism `Ï† : l â‹™ i â‰… i.essImage.Î¹`. When
`l â‹™ i` is defeq to `i.essImage.Î¹`, images of exponential objects in `D` under `i` will be defeq
to the respective exponential objects in `C`. -/
def cartesianClosedOfReflective' (l : i.EssImageSubcategory â¥¤ D) (Ï† : l â‹™ i â‰… i.essImage.Î¹) :
    CartesianClosed D where
  closed := fun B =>
    { rightAdj := i.essImage.lift (i â‹™ exp (i.obj B))
        (fun X â†¦ ExponentialIdeal.exp_closed (i.obj_mem_essImage X) _) â‹™ l
      adj := by
        apply (exp.adjunction (i.obj B)).restrictFullyFaithful i.fullyFaithfulOfReflective
          i.fullyFaithfulOfReflective
        Â· symm
          refine NatIso.ofComponents (fun X => ?_) (fun f => ?_)
          Â· haveI :=
              Adjunction.rightAdjoint_preservesLimits.{0, 0} (reflectorAdjunction i)
            apply asIso (prodComparison i B X)
          Â· dsimp [asIso]
            rw [prodComparison_natural_whiskerLeft]
        Â· exact (i.essImage.liftCompÎ¹Iso _ _).symm.trans <|
            (Functor.isoWhiskerLeft _ Ï†.symm).trans (Functor.associator _ _ _).symm }

/-- If `i` witnesses that `D` is a reflective subcategory and an exponential ideal, then `D` is
itself Cartesian closed.

Unlike `cartesianClosedOfReflective'` this construction lifts exponential objects in `C` to
exponential objects in `D` by applying the reflector to them, even though they already lie in the
essential image of `i`; if you need better control over definitional equality, use
`cartesianClosedOfReflective'` instead. -/
def cartesianClosedOfReflective : CartesianClosed D :=
  cartesianClosedOfReflective' i (i.essImage.Î¹ â‹™ reflector i)
    (NatIso.ofComponents (fun X â†¦
      have := Functor.essImage.unit_isIso X.2
      (asIso ((reflectorAdjunction i).unit.app X.obj)).symm))

variable [BraidedCategory C]

/-- We construct a bijection between morphisms `L(A âŠ— B) âŸ¶ X` and morphisms `LA âŠ— LB âŸ¶ X`.
This bijection has two key properties:
* It is natural in `X`: See `bijection_natural`.
* When `X = LA â¨¯ LB`, then the backwards direction sends the identity morphism to the product
  comparison morphism: See `bijection_symm_apply_id`.

Together these help show that `L` preserves binary products. This should be considered
*internal implementation* towards `preservesBinaryProductsOfExponentialIdeal`.
-/
noncomputable def bijection (A B : C) (X : D) :
    ((reflector i).obj (A âŠ— B) âŸ¶ X) â‰ƒ ((reflector i).obj A âŠ— (reflector i).obj B âŸ¶ X) :=
  calc
    _ â‰ƒ (A âŠ— B âŸ¶ i.obj X) := (reflectorAdjunction i).homEquiv _ _
    _ â‰ƒ (B âŠ— A âŸ¶ i.obj X) := (Î²_ _ _).homCongr (Iso.refl _)
    _ â‰ƒ (A âŸ¶ B âŸ¹ i.obj X) := (exp.adjunction _).homEquiv _ _
    _ â‰ƒ (i.obj ((reflector i).obj A) âŸ¶ B âŸ¹ i.obj X) :=
      (unitCompPartialBijective _ (ExponentialIdeal.exp_closed (i.obj_mem_essImage _) _))
    _ â‰ƒ (B âŠ— i.obj ((reflector i).obj A) âŸ¶ i.obj X) := ((exp.adjunction _).homEquiv _ _).symm
    _ â‰ƒ (i.obj ((reflector i).obj A) âŠ— B âŸ¶ i.obj X) :=
      ((Î²_ _ _).homCongr (Iso.refl _))
    _ â‰ƒ (B âŸ¶ i.obj ((reflector i).obj A) âŸ¹ i.obj X) := (exp.adjunction _).homEquiv _ _
    _ â‰ƒ (i.obj ((reflector i).obj B) âŸ¶ i.obj ((reflector i).obj A) âŸ¹ i.obj X) :=
      (unitCompPartialBijective _ (ExponentialIdeal.exp_closed (i.obj_mem_essImage _) _))
    _ â‰ƒ (i.obj ((reflector i).obj A) âŠ— i.obj ((reflector i).obj B) âŸ¶ i.obj X) :=
      ((exp.adjunction _).homEquiv _ _).symm
    _ â‰ƒ (i.obj ((reflector i).obj A âŠ— (reflector i).obj B) âŸ¶ i.obj X) :=
      haveI : Limits.PreservesLimits i := (reflectorAdjunction i).rightAdjoint_preservesLimits
      haveI := Limits.preservesSmallestLimits_of_preservesLimits i
      Iso.homCongr (prodComparisonIso _ _ _).symm (Iso.refl (i.obj X))
    _ â‰ƒ ((reflector i).obj A âŠ— (reflector i).obj B âŸ¶ X) :=
      i.fullyFaithfulOfReflective.homEquiv.symm

theorem bijection_symm_apply_id (A B : C) :
    (bijection i A B _).symm (ðŸ™ _) = prodComparison _ _ _ := by
  simp only [bijection, Equiv.trans_def, curriedTensor_obj_obj, Equiv.symm_trans_apply,
    Equiv.symm_symm, Functor.FullyFaithful.homEquiv_apply, Functor.map_id, Iso.homCongr_symm,
    Iso.symm_symm_eq, Iso.refl_symm, Iso.homCongr_apply, Iso.refl_hom, comp_id,
    unitCompPartialBijective_symm_apply, Functor.id_obj, Functor.comp_obj, Iso.symm_inv]
  -- Porting note: added
  erw [homEquiv_symm_apply_eq, homEquiv_symm_apply_eq, homEquiv_apply_eq, homEquiv_apply_eq]
  rw [uncurry_natural_left, uncurry_curry, uncurry_natural_left, uncurry_curry,
    â† BraidedCategory.braiding_naturality_left_assoc, SymmetricCategory.symmetry_assoc,
    â† MonoidalCategory.whisker_exchange_assoc, â† tensorHom_def'_assoc,
    Adjunction.homEquiv_symm_apply, â† Adjunction.eq_unit_comp_map_iff, Iso.comp_inv_eq, assoc,
    prodComparisonIso_hom i ((reflector i).obj A) ((reflector i).obj B)]
  apply hom_ext
  Â· rw [tensorHom_fst, assoc, assoc, prodComparison_fst, â† i.map_comp,
    prodComparison_fst]
    apply (reflectorAdjunction i).unit.naturality
  Â· rw [tensorHom_snd, assoc, assoc, prodComparison_snd, â† i.map_comp,
    prodComparison_snd]
    apply (reflectorAdjunction i).unit.naturality

theorem bijection_natural (A B : C) (X X' : D) (f : (reflector i).obj (A âŠ— B) âŸ¶ X) (g : X âŸ¶ X') :
    bijection i _ _ _ (f â‰« g) = bijection i _ _ _ f â‰« g := by
  dsimp [bijection]
  -- Porting note: added
  erw [homEquiv_symm_apply_eq, homEquiv_symm_apply_eq, homEquiv_apply_eq, homEquiv_apply_eq,
    homEquiv_symm_apply_eq, homEquiv_symm_apply_eq, homEquiv_apply_eq, homEquiv_apply_eq]
  apply i.map_injective
  rw [Functor.FullyFaithful.map_preimage, i.map_comp,
    Adjunction.homEquiv_unit, Adjunction.homEquiv_unit]
  simp only [comp_id, Functor.map_comp, Functor.FullyFaithful.map_preimage, assoc]
  rw [â† assoc, â† assoc, curry_natural_right _ (i.map g),
    unitCompPartialBijective_natural, uncurry_natural_right, â† assoc, curry_natural_right,
    unitCompPartialBijective_natural, uncurry_natural_right, assoc]

/--
The bijection allows us to show that `prodComparison L A B` is an isomorphism, where the inverse
is the forward map of the identity morphism.
-/
theorem prodComparison_iso (A B : C) : IsIso
    (prodComparison (reflector i) A B) :=
  âŸ¨âŸ¨bijection i _ _ _ (ðŸ™ _), by
      rw [â† (bijection i _ _ _).injective.eq_iff, bijection_natural, â† bijection_symm_apply_id,
        Equiv.apply_symm_apply, id_comp],
      by rw [â† bijection_natural, id_comp, â† bijection_symm_apply_id, Equiv.apply_symm_apply]âŸ©âŸ©

attribute [local instance] prodComparison_iso

open Limits

/--
If a reflective subcategory is an exponential ideal, then the reflector preserves binary products.
This is the converse of `exponentialIdeal_of_preserves_binary_products`.
-/
lemma preservesBinaryProducts_of_exponentialIdeal :
    PreservesLimitsOfShape (Discrete WalkingPair) (reflector i) where
  preservesLimit {K} :=
    letI := preservesLimit_pair_of_isIso_prodComparison
      (reflector i) (K.obj âŸ¨WalkingPair.leftâŸ©) (K.obj âŸ¨WalkingPair.rightâŸ©)
    Limits.preservesLimit_of_iso_diagram _ (diagramIsoPair K).symm

/--
If a reflective subcategory is an exponential ideal, then the reflector preserves finite products.
-/
lemma Limits.PreservesFiniteProducts.of_exponentialIdeal : PreservesFiniteProducts (reflector i) :=
  have := preservesBinaryProducts_of_exponentialIdeal i
  have : PreservesLimitsOfShape _ (reflector i) := leftAdjoint_preservesTerminal_of_reflective.{0} i
  .of_preserves_binary_and_terminal _

@[deprecated (since := "2025-04-22")]
alias preservesFiniteProducts_of_exponentialIdeal := PreservesFiniteProducts.of_exponentialIdeal

end

end CategoryTheory
