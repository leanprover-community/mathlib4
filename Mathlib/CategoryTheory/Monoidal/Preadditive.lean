/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Preadditive.AdditiveFunctor
import Mathlib.CategoryTheory.Monoidal.Functor

#align_import category_theory.monoidal.preadditive from "leanprover-community/mathlib"@"986c4d5761f938b2e1c43c01f001b6d9d88c2055"

/-!
# Preadditive monoidal categories

A monoidal category is `MonoidalPreadditive` if it is preadditive and tensor product of morphisms
is linear in both factors.
-/


noncomputable section

open Classical

namespace CategoryTheory

open CategoryTheory.Limits

open CategoryTheory.MonoidalCategory

variable (C : Type _) [Category C] [Preadditive C] [MonoidalCategory C]

/-- A category is `MonoidalPreadditive` if tensoring is additive in both factors.

Note we don't `extend Preadditive C` here, as `Abelian C` already extends it,
and we'll need to have both typeclasses sometimes.
-/
class MonoidalPreadditive : Prop where
  whiskerLeft_zero : ‚àÄ {X Y Z : C}, X ‚óÅ (0 : Y ‚ü∂ Z) = 0 := by aesop_cat
  zero_whiskerRight : ‚àÄ {X Y Z : C}, (0 : Y ‚ü∂ Z) ‚ñ∑ X = 0 := by aesop_cat
  whiskerLeft_add : ‚àÄ {X Y Z : C} (f g : Y ‚ü∂ Z), X ‚óÅ (f + g) = X ‚óÅ f + X ‚óÅ g := by aesop_cat
  add_whiskerRight : ‚àÄ {X Y Z : C} (f g : Y ‚ü∂ Z), (f + g) ‚ñ∑ X = f ‚ñ∑ X + g ‚ñ∑ X := by aesop_cat
#align category_theory.monoidal_preadditive CategoryTheory.MonoidalPreadditive

-- attribute [simp] MonoidalPreadditive.tensor_zero MonoidalPreadditive.zero_tensor
attribute [simp] MonoidalPreadditive.whiskerLeft_zero MonoidalPreadditive.zero_whiskerRight
attribute [simp] MonoidalPreadditive.whiskerLeft_add MonoidalPreadditive.add_whiskerRight

variable {C}
variable [MonoidalPreadditive C]

-- attribute [local simp] MonoidalPreadditive.tensor_add MonoidalPreadditive.add_tensor

instance tensorLeft_additive (X : C) : (tensorLeft X).Additive where
#align category_theory.tensor_left_additive CategoryTheory.tensorLeft_additive

instance tensorRight_additive (X : C) : (tensorRight X).Additive where
#align category_theory.tensor_right_additive CategoryTheory.tensorRight_additive

instance tensoringLeft_additive (X : C) : ((tensoringLeft C).obj X).Additive where
#align category_theory.tensoring_left_additive CategoryTheory.tensoringLeft_additive

instance tensoringRight_additive (X : C) : ((tensoringRight C).obj X).Additive where
#align category_theory.tensoring_right_additive CategoryTheory.tensoringRight_additive

/-- A faithful additive monoidal functor to a monoidal preadditive category
ensures that the domain is monoidal preadditive. -/
theorem monoidalPreadditive_of_faithful {D} [Category D] [Preadditive D] [MonoidalCategory D]
    (F : MonoidalFunctor D C) [Faithful F.toFunctor] [F.toFunctor.Additive] :
    MonoidalPreadditive D :=
  { whiskerLeft_zero := by
      intros
      apply F.toFunctor.map_injective
      simp [F.map_whiskerLeft]
    zero_whiskerRight := by
      intros
      apply F.toFunctor.map_injective
      simp [F.map_whiskerRight]
    whiskerLeft_add := by
      intros
      apply F.toFunctor.map_injective
      simp only [F.map_whiskerLeft, Functor.map_add, Preadditive.comp_add, Preadditive.add_comp,
        MonoidalPreadditive.whiskerLeft_add]
    add_whiskerRight := by
      intros
      apply F.toFunctor.map_injective
      simp only [F.map_whiskerRight, Functor.map_add, Preadditive.comp_add, Preadditive.add_comp,
        MonoidalPreadditive.add_whiskerRight] }
#align category_theory.monoidal_preadditive_of_faithful CategoryTheory.monoidalPreadditive_of_faithful

open BigOperators

attribute [local simp] tensorHom_def

theorem whiskerLeft_sum (P : C) {Q R : C} {J : Type _} (s : Finset J) (g : J ‚Üí (Q ‚ü∂ R)) :
    P ‚óÅ ‚àë j in s, g j = ‚àë j in s, P ‚óÅ g j :=
  map_sum ((tensoringLeft C).obj P).mapAddHom g s

theorem sum_whiskerRight {Q R : C} {J : Type _} (s : Finset J) (g : J ‚Üí (Q ‚ü∂ R)) (P : C) :
    (‚àë j in s, g j) ‚ñ∑ P = ‚àë j in s, g j ‚ñ∑ P :=
  map_sum ((tensoringRight C).obj P).mapAddHom g s

theorem tensor_sum {P Q R S : C} {J : Type _} (s : Finset J) (f : P ‚ü∂ Q) (g : J ‚Üí (R ‚ü∂ S)) :
    (f ‚äó ‚àë j in s, g j) = ‚àë j in s, f ‚äó g j := by
  simp [whiskerLeft_sum, sum_whiskerRight, Preadditive.sum_comp]
#align category_theory.tensor_sum CategoryTheory.tensor_sum

theorem sum_tensor {P Q R S : C} {J : Type _} (s : Finset J) (f : P ‚ü∂ Q) (g : J ‚Üí (R ‚ü∂ S)) :
    (‚àë j in s, g j) ‚äó f = ‚àë j in s, g j ‚äó f := by
  simp [whiskerLeft_sum, sum_whiskerRight, Preadditive.comp_sum]
#align category_theory.sum_tensor CategoryTheory.sum_tensor

-- In a closed monoidal category, this would hold because
-- `tensorLeft X` is a left adjoint and hence preserves all colimits.
-- In any case it is true in any preadditive category.
instance (X : C) : PreservesFiniteBiproducts (tensorLeft X) where
  preserves {J} :=
    { preserves := fun {f} =>
        { preserves := fun {b} i => isBilimitOfTotal _ (by
            dsimp
            simp_rw [‚Üê whiskerLeft_comp]
            simp_rw [‚Üê tensorHom_id, ‚Üê id_tensorHom]
            simp only [‚Üê tensor_comp, Category.comp_id, ‚Üê tensor_sum, ‚Üê tensor_id,
              IsBilimit.total i]) } }

instance (X : C) : PreservesFiniteBiproducts (tensorRight X) where
  preserves {J} :=
    { preserves := fun {f} =>
        { preserves := fun {b} i => isBilimitOfTotal _ (by
            dsimp
            simp_rw [‚Üê tensorHom_id, ‚Üê id_tensorHom]
            simp only [‚Üê tensor_comp, Category.comp_id, ‚Üê sum_tensor, ‚Üê tensor_id,
               IsBilimit.total i]) } }

variable [HasFiniteBiproducts C]

/-- The isomorphism showing how tensor product on the left distributes over direct sums. -/
def leftDistributor {J : Type} [Fintype J] (X : C) (f : J ‚Üí C) : X ‚äó ‚®Å f ‚âÖ ‚®Å fun j => X ‚äó f j :=
  (tensorLeft X).mapBiproduct f
#align category_theory.left_distributor CategoryTheory.leftDistributor

@[simp]
theorem leftDistributor_hom {J : Type} [Fintype J] (X : C) (f : J ‚Üí C) :
    (leftDistributor X f).hom =
      ‚àë j : J, (X ‚óÅ biproduct.œÄ f j) ‚â´ biproduct.Œπ (fun j => X ‚äó f j) j := by
  ext
  dsimp [leftDistributor, Functor.mapBiproduct, Functor.mapBicone]
  erw [biproduct.lift_œÄ]
  simp only [Preadditive.sum_comp, Category.assoc, biproduct.Œπ_œÄ, comp_dite, comp_zero,
    Finset.sum_dite_eq', Finset.mem_univ, ite_true, eqToHom_refl, Category.comp_id]
#align category_theory.left_distributor_hom CategoryTheory.leftDistributor_hom

@[simp]
theorem leftDistributor_inv {J : Type} [Fintype J] (X : C) (f : J ‚Üí C) :
    (leftDistributor X f).inv = ‚àë j : J, biproduct.œÄ _ j ‚â´ (X ‚óÅ biproduct.Œπ f j) := by
  ext
  dsimp [leftDistributor, Functor.mapBiproduct, Functor.mapBicone]
  simp only [Preadditive.comp_sum, biproduct.Œπ_œÄ_assoc, dite_comp, zero_comp,
    Finset.sum_dite_eq, Finset.mem_univ, ite_true, eqToHom_refl, Category.id_comp,
    biproduct.Œπ_desc]
#align category_theory.left_distributor_inv CategoryTheory.leftDistributor_inv

theorem leftDistributor_assoc {J : Type} [Fintype J] (X Y : C) (f : J ‚Üí C) :
    (asIso (ùüô X) ‚äó leftDistributor Y f) ‚â™‚â´ leftDistributor X _ =
      (Œ±_ X Y (‚®Å f)).symm ‚â™‚â´ leftDistributor (X ‚äó Y) f ‚â™‚â´ biproduct.mapIso fun j => Œ±_ X Y _ := by
  ext
  simp only [Category.comp_id, Category.assoc, eqToHom_refl, Iso.trans_hom, Iso.symm_hom,
    asIso_hom, comp_zero, comp_dite, Preadditive.sum_comp, Preadditive.comp_sum, tensor_sum,
    id_tensor_comp, tensorIso_hom, leftDistributor_hom, biproduct.mapIso_hom, biproduct.Œπ_map,
    biproduct.Œπ_œÄ, Finset.sum_dite_irrel, Finset.sum_dite_eq', Finset.sum_const_zero]
  simp_rw [‚Üê tensorHom_id, ‚Üê id_tensorHom]
  simp only [‚Üê id_tensor_comp, biproduct.Œπ_œÄ]
  simp only [id_tensor_comp, tensor_dite, comp_dite]
  simp
#align category_theory.left_distributor_assoc CategoryTheory.leftDistributor_assoc

/-- The isomorphism showing how tensor product on the right distributes over direct sums. -/
def rightDistributor {J : Type} [Fintype J] (X : C) (f : J ‚Üí C) : (‚®Å f) ‚äó X ‚âÖ ‚®Å fun j => f j ‚äó X :=
  (tensorRight X).mapBiproduct f
#align category_theory.right_distributor CategoryTheory.rightDistributor

@[simp]
theorem rightDistributor_hom {J : Type} [Fintype J] (X : C) (f : J ‚Üí C) :
    (rightDistributor X f).hom =
      ‚àë j : J, (biproduct.œÄ f j ‚ñ∑ X) ‚â´ biproduct.Œπ (fun j => f j ‚äó X) j := by
  ext
  dsimp [rightDistributor, Functor.mapBiproduct, Functor.mapBicone]
  erw [biproduct.lift_œÄ]
  simp only [Preadditive.sum_comp, Category.assoc, biproduct.Œπ_œÄ, comp_dite, comp_zero,
    Finset.sum_dite_eq', Finset.mem_univ, eqToHom_refl, Category.comp_id, ite_true]
#align category_theory.right_distributor_hom CategoryTheory.rightDistributor_hom

@[simp]
theorem rightDistributor_inv {J : Type} [Fintype J] (X : C) (f : J ‚Üí C) :
    (rightDistributor X f).inv = ‚àë j : J, biproduct.œÄ _ j ‚â´ (biproduct.Œπ f j ‚ñ∑ X) := by
  ext
  dsimp [rightDistributor, Functor.mapBiproduct, Functor.mapBicone]
  simp only [biproduct.Œπ_desc, Preadditive.comp_sum, ne_eq, biproduct.Œπ_œÄ_assoc, dite_comp,
    zero_comp, Finset.sum_dite_eq, Finset.mem_univ, eqToHom_refl, Category.id_comp, ite_true]
#align category_theory.right_distributor_inv CategoryTheory.rightDistributor_inv

theorem rightDistributor_assoc {J : Type} [Fintype J] (X Y : C) (f : J ‚Üí C) :
    (rightDistributor X f ‚äó asIso (ùüô Y)) ‚â™‚â´ rightDistributor Y _ =
      Œ±_ (‚®Å f) X Y ‚â™‚â´ rightDistributor (X ‚äó Y) f ‚â™‚â´ biproduct.mapIso fun j => (Œ±_ _ X Y).symm := by
  ext
  simp only [Category.comp_id, Category.assoc, eqToHom_refl, Iso.symm_hom, Iso.trans_hom,
    asIso_hom, comp_zero, comp_dite, Preadditive.sum_comp, Preadditive.comp_sum, sum_tensor,
    comp_tensor_id, tensorIso_hom, rightDistributor_hom, biproduct.mapIso_hom, biproduct.Œπ_map,
    biproduct.Œπ_œÄ, Finset.sum_dite_irrel, Finset.sum_dite_eq', Finset.sum_const_zero,
    Finset.mem_univ, if_true]
  simp_rw [‚Üê tensorHom_id, ‚Üê id_tensorHom]
  simp only [‚Üê comp_tensor_id, biproduct.Œπ_œÄ, dite_tensor, comp_dite]
  simp
#align category_theory.right_distributor_assoc CategoryTheory.rightDistributor_assoc

theorem leftDistributor_rightDistributor_assoc {J : Type _} [Fintype J] (X Y : C) (f : J ‚Üí C) :
    (leftDistributor X f ‚äó asIso (ùüô Y)) ‚â™‚â´ rightDistributor Y _ =
      Œ±_ X (‚®Å f) Y ‚â™‚â´
        (asIso (ùüô X) ‚äó rightDistributor Y _) ‚â™‚â´
          leftDistributor X _ ‚â™‚â´ biproduct.mapIso fun j => (Œ±_ _ _ _).symm := by
  ext
  simp only [Category.comp_id, Category.assoc, eqToHom_refl, Iso.symm_hom, Iso.trans_hom,
    asIso_hom, comp_zero, comp_dite, Preadditive.sum_comp, Preadditive.comp_sum, sum_tensor,
    tensor_sum, comp_tensor_id, tensorIso_hom, leftDistributor_hom, rightDistributor_hom,
    biproduct.mapIso_hom, biproduct.Œπ_map, biproduct.Œπ_œÄ, Finset.sum_dite_irrel,
    Finset.sum_dite_eq', Finset.sum_const_zero, Finset.mem_univ, if_true]
  simp_rw [‚Üê tensorHom_id, ‚Üê id_tensorHom]
  simp only [‚Üê comp_tensor_id, ‚Üê id_tensor_comp_assoc, Category.assoc, biproduct.Œπ_œÄ, comp_dite,
    dite_comp, tensor_dite, dite_tensor]
  simp
#align category_theory.left_distributor_right_distributor_assoc CategoryTheory.leftDistributor_rightDistributor_assoc

end CategoryTheory
