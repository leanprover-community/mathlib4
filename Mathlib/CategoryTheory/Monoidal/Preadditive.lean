/-
Copyright (c) 2021 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Preadditive.AdditiveFunctor
import Mathlib.CategoryTheory.Monoidal.Functor

/-!
# Preadditive monoidal categories

A monoidal category is `MonoidalPreadditive` if it is preadditive and tensor product of morphisms
is linear in both factors.
-/

noncomputable section

namespace CategoryTheory

open CategoryTheory.Limits

open CategoryTheory.MonoidalCategory

variable (C : Type*) [Category C] [Preadditive C] [MonoidalCategory C]

/-- A category is `MonoidalPreadditive` if tensoring is additive in both factors.

Note we don't `extend Preadditive C` here, as `Abelian C` already extends it,
and we'll need to have both typeclasses sometimes.
-/
class MonoidalPreadditive : Prop where
  whiskerLeft_zero : âˆ€ {X Y Z : C}, X â— (0 : Y âŸ¶ Z) = 0 := by cat_disch
  zero_whiskerRight : âˆ€ {X Y Z : C}, (0 : Y âŸ¶ Z) â–· X = 0 := by cat_disch
  whiskerLeft_add : âˆ€ {X Y Z : C} (f g : Y âŸ¶ Z), X â— (f + g) = X â— f + X â— g := by cat_disch
  add_whiskerRight : âˆ€ {X Y Z : C} (f g : Y âŸ¶ Z), (f + g) â–· X = f â–· X + g â–· X := by cat_disch

attribute [simp] MonoidalPreadditive.whiskerLeft_zero MonoidalPreadditive.zero_whiskerRight
attribute [simp] MonoidalPreadditive.whiskerLeft_add MonoidalPreadditive.add_whiskerRight

variable {C}
variable [MonoidalPreadditive C]

namespace MonoidalPreadditive

-- The priority setting will not be needed when we replace `ğŸ™ X âŠ—â‚˜ f` by `X â— f`.
@[simp (low)]
theorem tensor_zero {W X Y Z : C} (f : W âŸ¶ X) : f âŠ—â‚˜ (0 : Y âŸ¶ Z) = 0 := by
  simp [tensorHom_def]

-- The priority setting will not be needed when we replace `f âŠ—â‚˜ ğŸ™ X` by `f â–· X`.
@[simp (low)]
theorem zero_tensor {W X Y Z : C} (f : Y âŸ¶ Z) : (0 : W âŸ¶ X) âŠ—â‚˜ f = 0 := by
  simp [tensorHom_def]

theorem tensor_add {W X Y Z : C} (f : W âŸ¶ X) (g h : Y âŸ¶ Z) : f âŠ—â‚˜ (g + h) = f âŠ—â‚˜ g + f âŠ—â‚˜ h := by
  simp [tensorHom_def]

theorem add_tensor {W X Y Z : C} (f g : W âŸ¶ X) (h : Y âŸ¶ Z) : (f + g) âŠ—â‚˜ h = f âŠ—â‚˜ h + g âŠ—â‚˜ h := by
  simp [tensorHom_def]

end MonoidalPreadditive

instance tensorLeft_additive (X : C) : (tensorLeft X).Additive where

instance tensorRight_additive (X : C) : (tensorRight X).Additive where

instance tensoringLeft_additive (X : C) : ((tensoringLeft C).obj X).Additive where

instance tensoringRight_additive (X : C) : ((tensoringRight C).obj X).Additive where

/-- A faithful additive monoidal functor to a monoidal preadditive category
ensures that the domain is monoidal preadditive. -/
theorem monoidalPreadditive_of_faithful {D} [Category D] [Preadditive D] [MonoidalCategory D]
    (F : D â¥¤ C) [F.Monoidal] [F.Faithful] [F.Additive] :
    MonoidalPreadditive D :=
  { whiskerLeft_zero := by
      intros
      apply F.map_injective
      simp [Functor.Monoidal.map_whiskerLeft]
    zero_whiskerRight := by
      intros
      apply F.map_injective
      simp [Functor.Monoidal.map_whiskerRight]
    whiskerLeft_add := by
      intros
      apply F.map_injective
      simp only [Functor.Monoidal.map_whiskerLeft, Functor.map_add, Preadditive.comp_add,
        Preadditive.add_comp, MonoidalPreadditive.whiskerLeft_add]
    add_whiskerRight := by
      intros
      apply F.map_injective
      simp only [Functor.Monoidal.map_whiskerRight, Functor.map_add, Preadditive.comp_add,
        Preadditive.add_comp, MonoidalPreadditive.add_whiskerRight] }

theorem whiskerLeft_sum (P : C) {Q R : C} {J : Type*} (s : Finset J) (g : J â†’ (Q âŸ¶ R)) :
    P â— âˆ‘ j âˆˆ s, g j = âˆ‘ j âˆˆ s, P â— g j :=
  map_sum ((tensoringLeft C).obj P).mapAddHom g s

theorem sum_whiskerRight {Q R : C} {J : Type*} (s : Finset J) (g : J â†’ (Q âŸ¶ R)) (P : C) :
    (âˆ‘ j âˆˆ s, g j) â–· P = âˆ‘ j âˆˆ s, g j â–· P :=
  map_sum ((tensoringRight C).obj P).mapAddHom g s

theorem tensor_sum {P Q R S : C} {J : Type*} (s : Finset J) (f : P âŸ¶ Q) (g : J â†’ (R âŸ¶ S)) :
    (f âŠ—â‚˜ âˆ‘ j âˆˆ s, g j) = âˆ‘ j âˆˆ s, f âŠ—â‚˜ g j := by
  simp only [tensorHom_def, whiskerLeft_sum, Preadditive.comp_sum]

theorem sum_tensor {P Q R S : C} {J : Type*} (s : Finset J) (f : P âŸ¶ Q) (g : J â†’ (R âŸ¶ S)) :
    (âˆ‘ j âˆˆ s, g j) âŠ—â‚˜ f = âˆ‘ j âˆˆ s, g j âŠ—â‚˜ f := by
  simp only [tensorHom_def, sum_whiskerRight, Preadditive.sum_comp]

-- In a closed monoidal category, this would hold because
-- `tensorLeft X` is a left adjoint and hence preserves all colimits.
-- In any case it is true in any preadditive category.
instance (X : C) : PreservesFiniteBiproducts (tensorLeft X) where
  preserves {J} :=
    let âŸ¨_âŸ© := nonempty_fintype J
    { preserves := fun {f} =>
        { preserves := fun {b} i => âŸ¨isBilimitOfTotal _ (by
            dsimp
            simp_rw [â† id_tensorHom]
            simp only [â† tensor_comp, Category.comp_id, â† tensor_sum, â† id_tensorHom_id,
              IsBilimit.total i])âŸ© } }

instance (X : C) : PreservesFiniteBiproducts (tensorRight X) where
  preserves {J} :=
    let âŸ¨_âŸ© := nonempty_fintype J
    { preserves := fun {f} =>
        { preserves := fun {b} i => âŸ¨isBilimitOfTotal _ (by
            dsimp
            simp_rw [â† tensorHom_id]
            simp only [â† tensor_comp, Category.comp_id, â† sum_tensor, â† id_tensorHom_id,
               IsBilimit.total i])âŸ© } }

variable [HasFiniteBiproducts C]

/-- The isomorphism showing how tensor product on the left distributes over direct sums. -/
def leftDistributor {J : Type} [Finite J] (X : C) (f : J â†’ C) : X âŠ— â¨ f â‰… â¨ fun j => X âŠ— f j :=
  (tensorLeft X).mapBiproduct f

theorem leftDistributor_hom {J : Type} [Fintype J] (X : C) (f : J â†’ C) :
    (leftDistributor X f).hom =
      âˆ‘ j : J, (X â— biproduct.Ï€ f j) â‰« biproduct.Î¹ (fun j => X âŠ— f j) j := by
  classical
  ext
  dsimp [leftDistributor, Functor.mapBiproduct, Functor.mapBicone]
  erw [biproduct.lift_Ï€]
  simp only [Preadditive.sum_comp, Category.assoc, biproduct.Î¹_Ï€, comp_dite, comp_zero,
    Finset.sum_dite_eq', Finset.mem_univ, ite_true, eqToHom_refl, Category.comp_id]

theorem leftDistributor_inv {J : Type} [Fintype J] (X : C) (f : J â†’ C) :
    (leftDistributor X f).inv = âˆ‘ j : J, biproduct.Ï€ _ j â‰« (X â— biproduct.Î¹ f j) := by
  classical
  ext
  dsimp [leftDistributor, Functor.mapBiproduct, Functor.mapBicone]
  simp only [Preadditive.comp_sum, biproduct.Î¹_Ï€_assoc, dite_comp, zero_comp,
    Finset.sum_dite_eq, Finset.mem_univ, ite_true, eqToHom_refl, Category.id_comp,
    biproduct.Î¹_desc]

@[reassoc (attr := simp)]
theorem leftDistributor_hom_comp_biproduct_Ï€ {J : Type} [Finite J] (X : C) (f : J â†’ C) (j : J) :
    (leftDistributor X f).hom â‰« biproduct.Ï€ _ j = X â— biproduct.Ï€ _ j := by
  classical
  cases nonempty_fintype J
  simp [leftDistributor_hom, Preadditive.sum_comp, biproduct.Î¹_Ï€, comp_dite]

@[reassoc (attr := simp)]
theorem biproduct_Î¹_comp_leftDistributor_hom {J : Type} [Finite J] (X : C) (f : J â†’ C) (j : J) :
    (X â— biproduct.Î¹ _ j) â‰« (leftDistributor X f).hom = biproduct.Î¹ (fun j => X âŠ— f j) j := by
  classical
  cases nonempty_fintype J
  simp [leftDistributor_hom, Preadditive.comp_sum, â† whiskerLeft_comp_assoc,
    biproduct.Î¹_Ï€, whiskerLeft_dite, dite_comp]

@[reassoc (attr := simp)]
theorem leftDistributor_inv_comp_biproduct_Ï€ {J : Type} [Finite J] (X : C) (f : J â†’ C) (j : J) :
    (leftDistributor X f).inv â‰« (X â— biproduct.Ï€ _ j) = biproduct.Ï€ _ j := by
  classical
  cases nonempty_fintype J
  simp [leftDistributor_inv, Preadditive.sum_comp, â† whiskerLeft_comp,
    biproduct.Î¹_Ï€, whiskerLeft_dite, comp_dite]

@[reassoc (attr := simp)]
theorem biproduct_Î¹_comp_leftDistributor_inv {J : Type} [Finite J] (X : C) (f : J â†’ C) (j : J) :
    biproduct.Î¹ _ j â‰« (leftDistributor X f).inv = X â— biproduct.Î¹ _ j := by
  classical
  cases nonempty_fintype J
  simp [leftDistributor_inv, Preadditive.comp_sum, biproduct.Î¹_Ï€_assoc, dite_comp]

theorem leftDistributor_assoc {J : Type} [Finite J] (X Y : C) (f : J â†’ C) :
    (asIso (ğŸ™ X) âŠ—áµ¢ leftDistributor Y f) â‰ªâ‰« leftDistributor X _ =
      (Î±_ X Y (â¨ f)).symm â‰ªâ‰« leftDistributor (X âŠ— Y) f â‰ªâ‰« biproduct.mapIso fun _ => Î±_ X Y _ := by
  classical
  cases nonempty_fintype J
  ext
  simp only [Category.comp_id, Category.assoc, eqToHom_refl, Iso.trans_hom, Iso.symm_hom,
    asIso_hom, comp_zero, comp_dite, Preadditive.sum_comp, Preadditive.comp_sum, tensor_sum,
    id_tensor_comp, tensorIso_hom, leftDistributor_hom, biproduct.mapIso_hom, biproduct.Î¹_map,
    biproduct.Î¹_Ï€, Finset.sum_dite_irrel, Finset.sum_dite_eq', Finset.sum_const_zero]
  simp_rw [â† id_tensorHom]
  simp only [â† id_tensor_comp, biproduct.Î¹_Ï€]
  simp only [id_tensor_comp, tensor_dite, comp_dite]
  simp

/-- The isomorphism showing how tensor product on the right distributes over direct sums. -/
def rightDistributor {J : Type} [Finite J] (f : J â†’ C) (X : C) : (â¨ f) âŠ— X â‰… â¨ fun j => f j âŠ— X :=
  (tensorRight X).mapBiproduct f

theorem rightDistributor_hom {J : Type} [Fintype J] (f : J â†’ C) (X : C) :
    (rightDistributor f X).hom =
      âˆ‘ j : J, (biproduct.Ï€ f j â–· X) â‰« biproduct.Î¹ (fun j => f j âŠ— X) j := by
  classical
  ext
  dsimp [rightDistributor, Functor.mapBiproduct, Functor.mapBicone]
  erw [biproduct.lift_Ï€]
  simp only [Preadditive.sum_comp, Category.assoc, biproduct.Î¹_Ï€, comp_dite, comp_zero,
    Finset.sum_dite_eq', Finset.mem_univ, eqToHom_refl, Category.comp_id, ite_true]

theorem rightDistributor_inv {J : Type} [Fintype J] (f : J â†’ C) (X : C) :
    (rightDistributor f X).inv = âˆ‘ j : J, biproduct.Ï€ _ j â‰« (biproduct.Î¹ f j â–· X) := by
  classical
  ext
  dsimp [rightDistributor, Functor.mapBiproduct, Functor.mapBicone]
  simp only [biproduct.Î¹_desc, Preadditive.comp_sum, biproduct.Î¹_Ï€_assoc, dite_comp,
    zero_comp, Finset.sum_dite_eq, Finset.mem_univ, eqToHom_refl, Category.id_comp, ite_true]

@[reassoc (attr := simp)]
theorem rightDistributor_hom_comp_biproduct_Ï€ {J : Type} [Finite J] (f : J â†’ C) (X : C) (j : J) :
    (rightDistributor f X).hom â‰« biproduct.Ï€ _ j = biproduct.Ï€ _ j â–· X := by
  classical
  cases nonempty_fintype J
  simp [rightDistributor_hom, Preadditive.sum_comp, biproduct.Î¹_Ï€, comp_dite]

@[reassoc (attr := simp)]
theorem biproduct_Î¹_comp_rightDistributor_hom {J : Type} [Finite J] (f : J â†’ C) (X : C) (j : J) :
    (biproduct.Î¹ _ j â–· X) â‰« (rightDistributor f X).hom = biproduct.Î¹ (fun j => f j âŠ— X) j := by
  classical
  cases nonempty_fintype J
  simp [rightDistributor_hom, Preadditive.comp_sum, â† comp_whiskerRight_assoc, biproduct.Î¹_Ï€,
    dite_whiskerRight, dite_comp]

@[reassoc (attr := simp)]
theorem rightDistributor_inv_comp_biproduct_Ï€ {J : Type} [Finite J] (f : J â†’ C) (X : C) (j : J) :
    (rightDistributor f X).inv â‰« (biproduct.Ï€ _ j â–· X) = biproduct.Ï€ _ j := by
  classical
  cases nonempty_fintype J
  simp [rightDistributor_inv, Preadditive.sum_comp, â† comp_whiskerRight,
    biproduct.Î¹_Ï€, dite_whiskerRight, comp_dite]

@[reassoc (attr := simp)]
theorem biproduct_Î¹_comp_rightDistributor_inv {J : Type} [Finite J] (f : J â†’ C) (X : C) (j : J) :
    biproduct.Î¹ _ j â‰« (rightDistributor f X).inv = biproduct.Î¹ _ j â–· X := by
  classical
  cases nonempty_fintype J
  simp [rightDistributor_inv, Preadditive.comp_sum, biproduct.Î¹_Ï€_assoc,
    dite_comp]

theorem rightDistributor_assoc {J : Type} [Finite J] (f : J â†’ C) (X Y : C) :
    (rightDistributor f X âŠ—áµ¢ asIso (ğŸ™ Y)) â‰ªâ‰« rightDistributor _ Y =
      Î±_ (â¨ f) X Y â‰ªâ‰« rightDistributor f (X âŠ— Y) â‰ªâ‰« biproduct.mapIso fun _ => (Î±_ _ X Y).symm := by
  classical
  cases nonempty_fintype J
  ext
  simp only [Category.comp_id, Category.assoc, eqToHom_refl, Iso.symm_hom, Iso.trans_hom,
    asIso_hom, comp_zero, comp_dite, Preadditive.sum_comp, Preadditive.comp_sum, sum_tensor,
    comp_tensor_id, tensorIso_hom, rightDistributor_hom, biproduct.mapIso_hom, biproduct.Î¹_map,
    biproduct.Î¹_Ï€, Finset.sum_dite_irrel, Finset.sum_dite_eq', Finset.sum_const_zero,
    Finset.mem_univ, if_true]
  simp_rw [â† tensorHom_id]
  simp only [â† comp_tensor_id, biproduct.Î¹_Ï€, dite_tensor, comp_dite]
  simp

theorem leftDistributor_rightDistributor_assoc {J : Type _} [Finite J]
    (X : C) (f : J â†’ C) (Y : C) :
    (leftDistributor X f âŠ—áµ¢ asIso (ğŸ™ Y)) â‰ªâ‰« rightDistributor _ Y =
      Î±_ X (â¨ f) Y â‰ªâ‰«
        (asIso (ğŸ™ X) âŠ—áµ¢ rightDistributor _ Y) â‰ªâ‰«
          leftDistributor X _ â‰ªâ‰« biproduct.mapIso fun _ => (Î±_ _ _ _).symm := by
  classical
  cases nonempty_fintype J
  ext
  simp only [Category.comp_id, Category.assoc, eqToHom_refl, Iso.symm_hom, Iso.trans_hom,
    asIso_hom, comp_zero, comp_dite, Preadditive.sum_comp, Preadditive.comp_sum, sum_tensor,
    tensor_sum, comp_tensor_id, tensorIso_hom, leftDistributor_hom, rightDistributor_hom,
    biproduct.mapIso_hom, biproduct.Î¹_map, biproduct.Î¹_Ï€, Finset.sum_dite_irrel,
    Finset.sum_dite_eq', Finset.sum_const_zero, Finset.mem_univ, if_true]
  simp_rw [â† tensorHom_id, â† id_tensorHom]
  simp only [â† comp_tensor_id, â† id_tensor_comp_assoc, Category.assoc, biproduct.Î¹_Ï€, comp_dite,
    dite_comp, tensor_dite, dite_tensor]
  simp

@[ext]
theorem leftDistributor_ext_left {J : Type} [Finite J] {X Y : C} {f : J â†’ C} {g h : X âŠ— â¨ f âŸ¶ Y}
    (w : âˆ€ j, (X â— biproduct.Î¹ f j) â‰« g = (X â— biproduct.Î¹ f j) â‰« h) : g = h := by
  classical
  cases nonempty_fintype J
  apply (cancel_epi (leftDistributor X f).inv).mp
  ext
  simp [w]

@[ext]
theorem leftDistributor_ext_right {J : Type} [Finite J] {X Y : C} {f : J â†’ C} {g h : X âŸ¶ Y âŠ— â¨ f}
    (w : âˆ€ j, g â‰« (Y â— biproduct.Ï€ f j) = h â‰« (Y â— biproduct.Ï€ f j)) : g = h := by
  classical
  cases nonempty_fintype J
  apply (cancel_mono (leftDistributor Y f).hom).mp
  ext
  simp [w]

-- One might wonder how many iterated tensor products we need simp lemmas for.
-- The answer is two: this lemma is needed to verify the pentagon identity.
@[ext]
theorem leftDistributor_extâ‚‚_left {J : Type} [Finite J]
    {X Y Z : C} {f : J â†’ C} {g h : X âŠ— (Y âŠ— â¨ f) âŸ¶ Z}
    (w : âˆ€ j, (X â— (Y â— biproduct.Î¹ f j)) â‰« g = (X â— (Y â— biproduct.Î¹ f j)) â‰« h) :
    g = h := by
  apply (cancel_epi (Î±_ _ _ _).hom).mp
  ext
  simp [w]

@[ext]
theorem leftDistributor_extâ‚‚_right {J : Type} [Finite J]
    {X Y Z : C} {f : J â†’ C} {g h : X âŸ¶ Y âŠ— (Z âŠ— â¨ f)}
    (w : âˆ€ j, g â‰« (Y â— (Z â— biproduct.Ï€ f j)) = h â‰« (Y â— (Z â— biproduct.Ï€ f j))) :
    g = h := by
  apply (cancel_mono (Î±_ _ _ _).inv).mp
  ext
  simp [w]

@[ext]
theorem rightDistributor_ext_left {J : Type} [Finite J]
    {f : J â†’ C} {X Y : C} {g h : (â¨ f) âŠ— X âŸ¶ Y}
    (w : âˆ€ j, (biproduct.Î¹ f j â–· X) â‰« g = (biproduct.Î¹ f j â–· X) â‰« h) : g = h := by
  classical
  cases nonempty_fintype J
  apply (cancel_epi (rightDistributor f X).inv).mp
  ext
  simp [w]

@[ext]
theorem rightDistributor_ext_right {J : Type} [Finite J]
    {f : J â†’ C} {X Y : C} {g h : X âŸ¶ (â¨ f) âŠ— Y}
    (w : âˆ€ j, g â‰« (biproduct.Ï€ f j â–· Y) = h â‰« (biproduct.Ï€ f j â–· Y)) : g = h := by
  classical
  cases nonempty_fintype J
  apply (cancel_mono (rightDistributor f Y).hom).mp
  ext
  simp [w]

@[ext]
theorem rightDistributor_extâ‚‚_left {J : Type} [Finite J]
    {f : J â†’ C} {X Y Z : C} {g h : ((â¨ f) âŠ— X) âŠ— Y âŸ¶ Z}
    (w : âˆ€ j, ((biproduct.Î¹ f j â–· X) â–· Y) â‰« g = ((biproduct.Î¹ f j â–· X) â–· Y) â‰« h) :
    g = h := by
  apply (cancel_epi (Î±_ _ _ _).inv).mp
  ext
  simp [w]

@[ext]
theorem rightDistributor_extâ‚‚_right {J : Type} [Finite J]
    {f : J â†’ C} {X Y Z : C} {g h : X âŸ¶ ((â¨ f) âŠ— Y) âŠ— Z}
    (w : âˆ€ j, g â‰« ((biproduct.Ï€ f j â–· Y) â–· Z) = h â‰« ((biproduct.Ï€ f j â–· Y) â–· Z)) :
    g = h := by
  apply (cancel_mono (Î±_ _ _ _).hom).mp
  ext
  simp [w]

end CategoryTheory
