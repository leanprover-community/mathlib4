/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.Algebra.Group.PUnit
import Mathlib.CategoryTheory.Monoidal.Braided.Basic
import Mathlib.CategoryTheory.Monoidal.CoherenceLemmas
import Mathlib.CategoryTheory.Monoidal.Discrete
import Mathlib.CategoryTheory.Limits.Shapes.Terminal

/-!
# The category of monoids in a monoidal category.

We define monoids in a monoidal category `C` and show that the category of monoids is equivalent to
the category of lax monoidal functors from the unit monoidal category to `C`.  We also show that if
`C` is braided, then the category of monoids is naturally monoidal.

-/


universe vâ‚ vâ‚‚ uâ‚ uâ‚‚ u

open CategoryTheory MonoidalCategory Functor.LaxMonoidal Functor.OplaxMonoidal

variable {C : Type uâ‚} [Category.{vâ‚} C] [MonoidalCategory.{vâ‚} C]

/-- A monoid object internal to a monoidal category.

When the monoidal category is preadditive, this is also sometimes called an "algebra object".
-/
class Mon_Class (X : C) where
  /-- The unit morphism of a monoid object. -/
  one : ğŸ™_ C âŸ¶ X
  /-- The multiplication morphism of a monoid object. -/
  mul : X âŠ— X âŸ¶ X
  /- For the names of the conditions below, the unprimed names are reserved for the version where
  the argument `X` is explicit. -/
  one_mul' : one â–· X â‰« mul = (Î»_ X).hom := by aesop_cat
  mul_one' : X â— one â‰« mul = (Ï_ X).hom := by aesop_cat
  -- Obviously there is some flexibility stating this axiom.
  -- This one has left- and right-hand sides matching the statement of `Monoid.mul_assoc`,
  -- and chooses to place the associator on the right-hand side.
  -- The heuristic is that unitors and associators "don't have much weight".
  mul_assoc' : (mul â–· X) â‰« mul = (Î±_ X X X).hom â‰« (X â— mul) â‰« mul := by aesop_cat

namespace Mon_Class

@[inherit_doc] scoped notation "Î¼" => Mon_Class.mul
@[inherit_doc] scoped notation "Î¼["M"]" => Mon_Class.mul (X := M)
@[inherit_doc] scoped notation "Î·" => Mon_Class.one
@[inherit_doc] scoped notation "Î·["M"]" => Mon_Class.one (X := M)

/- The simp attribute is reserved for the unprimed versions. -/
attribute [reassoc] one_mul' mul_one' mul_assoc'

@[reassoc (attr := simp)]
theorem one_mul (X : C) [Mon_Class X] : Î· â–· X â‰« Î¼ = (Î»_ X).hom := one_mul'

@[reassoc (attr := simp)]
theorem mul_one (X : C) [Mon_Class X] : X â— Î· â‰« Î¼ = (Ï_ X).hom := mul_one'

@[reassoc (attr := simp)]
theorem mul_assoc (X : C) [Mon_Class X] : Î¼ â–· X â‰« Î¼ = (Î±_ X X X).hom â‰« X â— Î¼ â‰« Î¼ := mul_assoc'

@[ext]
theorem ext {X : C} (hâ‚ hâ‚‚ : Mon_Class X) (H : hâ‚.mul = hâ‚‚.mul) : hâ‚ = hâ‚‚ := by
  suffices hâ‚.one = hâ‚‚.one by cases hâ‚; cases hâ‚‚; subst H this; rfl
  trans (Î»_ _).inv â‰« (hâ‚.one âŠ— hâ‚‚.one) â‰« hâ‚.mul
  Â· simp [tensorHom_def, H, â† unitors_equal]
  Â· simp [tensorHom_def']

end Mon_Class

open scoped Mon_Class

variable {M N : C} [Mon_Class M] [Mon_Class N]

/-- The property that a morphism between monoid objects is a monoid morphism. -/
class IsMon_Hom (f : M âŸ¶ N) : Prop where
  one_hom (f) : Î· â‰« f = Î· := by aesop_cat
  mul_hom (f) : Î¼ â‰« f = (f âŠ— f) â‰« Î¼ := by aesop_cat

attribute [reassoc (attr := simp)] IsMon_Hom.one_hom IsMon_Hom.mul_hom

variable (C)

/-- A monoid object internal to a monoidal category.

When the monoidal category is preadditive, this is also sometimes called an "algebra object".
-/
structure Mon_ where
  /-- The underlying object in the ambient monoidal category -/
  X : C
  /-- The unit morphism of the monoid object -/
  one : ğŸ™_ C âŸ¶ X
  /-- The multiplication morphism of a monoid object -/
  mul : X âŠ— X âŸ¶ X
  one_mul : (one â–· X) â‰« mul = (Î»_ X).hom := by aesop_cat
  mul_one : (X â— one) â‰« mul = (Ï_ X).hom := by aesop_cat
  -- Obviously there is some flexibility stating this axiom.
  -- This one has left- and right-hand sides matching the statement of `Monoid.mul_assoc`,
  -- and chooses to place the associator on the right-hand side.
  -- The heuristic is that unitors and associators "don't have much weight".
  mul_assoc : (mul â–· X) â‰« mul = (Î±_ X X X).hom â‰« (X â— mul) â‰« mul := by aesop_cat

attribute [reassoc] Mon_.one_mul Mon_.mul_one

attribute [simp] Mon_.one_mul Mon_.mul_one

-- We prove a more general `@[simp]` lemma below.
attribute [reassoc (attr := simp)] Mon_.mul_assoc

namespace Mon_

variable {C}

/-- Construct an object of `Mon_ C` from an object `X : C` and `Mon_Class X` instance. -/
abbrev mk' (X : C) [Mon_Class X] : Mon_ C where
  X := X
  one := Î·
  mul := Î¼

instance X.instMon_Class {M : Mon_ C} : Mon_Class M.X where
  one := M.one
  mul := M.mul
  one_mul' := M.one_mul
  mul_one' := M.mul_one
  mul_assoc' := M.mul_assoc

variable (C)

/-- The trivial monoid object. We later show this is initial in `Mon_ C`.
-/
@[simps]
def trivial : Mon_ C where
  X := ğŸ™_ C
  one := ğŸ™ _
  mul := (Î»_ _).hom
  mul_assoc := by monoidal_coherence
  mul_one := by monoidal_coherence

instance : Inhabited (Mon_ C) :=
  âŸ¨trivial CâŸ©

variable {C}
variable {M : Mon_ C}

@[simp]
theorem one_mul_hom {Z : C} (f : Z âŸ¶ M.X) : (M.one âŠ— f) â‰« M.mul = (Î»_ Z).hom â‰« f := by
  rw [tensorHom_def'_assoc, M.one_mul, leftUnitor_naturality]

@[simp]
theorem mul_one_hom {Z : C} (f : Z âŸ¶ M.X) : (f âŠ— M.one) â‰« M.mul = (Ï_ Z).hom â‰« f := by
  rw [tensorHom_def_assoc, M.mul_one, rightUnitor_naturality]

theorem mul_assoc_flip :
    (M.X â— M.mul) â‰« M.mul = (Î±_ M.X M.X M.X).inv â‰« (M.mul â–· M.X) â‰« M.mul := by simp

/-- A morphism of monoid objects. -/
@[ext]
structure Hom (M N : Mon_ C) where
  /-- The underlying morphism -/
  hom : M.X âŸ¶ N.X
  one_hom : M.one â‰« hom = N.one := by aesop_cat
  mul_hom : M.mul â‰« hom = (hom âŠ— hom) â‰« N.mul := by aesop_cat

/-- Construct a morphism `M âŸ¶ N` of `Mon_ C` from a map `f : M âŸ¶ N` and a `IsMon_Hom f` instance. -/
abbrev Hom.mk' {M N : C} [Mon_Class M] [Mon_Class N] (f : M âŸ¶ N) [IsMon_Hom f] :
    Hom (.mk' M) (.mk' N) := .mk f

attribute [reassoc (attr := simp)] Hom.one_hom Hom.mul_hom

/-- The identity morphism on a monoid object. -/
@[simps]
def id (M : Mon_ C) : Hom M M where
  hom := ğŸ™ M.X

instance homInhabited (M : Mon_ C) : Inhabited (Hom M M) :=
  âŸ¨id MâŸ©

/-- Composition of morphisms of monoid objects. -/
@[simps]
def comp {M N O : Mon_ C} (f : Hom M N) (g : Hom N O) : Hom M O where
  hom := f.hom â‰« g.hom

instance : Category (Mon_ C) where
  Hom M N := Hom M N
  id := id
  comp f g := comp f g

instance {M N : Mon_ C} (f : M âŸ¶ N) : IsMon_Hom f.hom := âŸ¨f.2, f.3âŸ©

@[ext]
lemma ext {X Y : Mon_ C} {f g : X âŸ¶ Y} (w : f.hom = g.hom) : f = g :=
  Hom.ext w

@[simp]
theorem id_hom' (M : Mon_ C) : (ğŸ™ M : Hom M M).hom = ğŸ™ M.X :=
  rfl

@[simp]
theorem comp_hom' {M N K : Mon_ C} (f : M âŸ¶ N) (g : N âŸ¶ K) :
    (f â‰« g : Hom M K).hom = f.hom â‰« g.hom :=
  rfl

section

variable (C)

/-- The forgetful functor from monoid objects to the ambient category. -/
@[simps]
def forget : Mon_ C â¥¤ C where
  obj A := A.X
  map f := f.hom

end

instance forget_faithful : (forget C).Faithful where

instance {A B : Mon_ C} (f : A âŸ¶ B) [e : IsIso ((forget C).map f)] : IsIso f.hom :=
  e

/-- The forgetful functor from monoid objects to the ambient category reflects isomorphisms. -/
instance : (forget C).ReflectsIsomorphisms where
  reflects f e := âŸ¨âŸ¨{ hom := inv f.hom }, by aesop_catâŸ©âŸ©

/-- Construct an isomorphism of monoids by giving an isomorphism between the underlying objects
and checking compatibility with unit and multiplication only in the forward direction.
-/
@[simps]
def mkIso {M N : Mon_ C} (f : M.X â‰… N.X) (one_f : M.one â‰« f.hom = N.one := by aesop_cat)
    (mul_f : M.mul â‰« f.hom = (f.hom âŠ— f.hom) â‰« N.mul := by aesop_cat) : M â‰… N where
  hom := { hom := f.hom }
  inv :=
  { hom := f.inv
    one_hom := by rw [â† one_f]; simp
    mul_hom := by
      rw [â† cancel_mono f.hom]
      slice_rhs 2 3 => rw [mul_f]
      simp }

@[simps]
instance uniqueHomFromTrivial (A : Mon_ C) : Unique (trivial C âŸ¶ A) where
  default :=
  { hom := A.one
    mul_hom := by simp [A.one_mul, unitors_equal] }
  uniq f := by
    ext
    simp only [trivial_X]
    rw [â† Category.id_comp f.hom]
    erw [f.one_hom]

open CategoryTheory.Limits

instance : HasInitial (Mon_ C) :=
  hasInitial_of_unique (trivial C)

end Mon_

namespace CategoryTheory.Functor

variable {C} {D : Type uâ‚‚} [Category.{vâ‚‚} D] [MonoidalCategory.{vâ‚‚} D] (F : C â¥¤ D)

section LaxMonoidal
variable [F.LaxMonoidal] (X Y : C) [Mon_Class X] [Mon_Class Y] (f : X âŸ¶ Y) [IsMon_Hom f]

/-- The image of a monoid object under a lax monoidal functor is a monoid object. -/
abbrev obj.instMon_Class : Mon_Class (F.obj X) where
  one := Îµ F â‰« F.map Î·
  mul := LaxMonoidal.Î¼ F X X â‰« F.map Î¼
  one_mul' := by simp [â† F.map_comp]
  mul_one' := by simp [â† F.map_comp]
  mul_assoc' := by
    simp_rw [comp_whiskerRight, Category.assoc, Î¼_natural_left_assoc,
      MonoidalCategory.whiskerLeft_comp, Category.assoc, Î¼_natural_right_assoc]
    slice_lhs 3 4 => rw [â† F.map_comp, Mon_Class.mul_assoc]
    simp

attribute [local instance] obj.instMon_Class

@[reassoc, simp] lemma obj.Î·_def : (Î· : ğŸ™_ D âŸ¶ F.obj X) = Îµ F â‰« F.map Î· := rfl

@[reassoc, simp] lemma obj.Î¼_def : Î¼ = LaxMonoidal.Î¼ F X X â‰« F.map Î¼ := rfl

instance map.instIsMon_Hom : IsMon_Hom (F.map f) where
  one_hom := by simp [â† map_comp]
  mul_hom := by simp [â† map_comp]

-- TODO: mapMod F A : Mod A â¥¤ Mod (F.mapMon A)
/-- A lax monoidal functor takes monoid objects to monoid objects.

That is, a lax monoidal functor `F : C â¥¤ D` induces a functor `Mon_ C â¥¤ Mon_ D`.
-/
@[simps]
def mapMon (F : C â¥¤ D) [F.LaxMonoidal] : Mon_ C â¥¤ Mon_ D where
  -- TODO: The following could be, but it leads to weird `erw`s later down the file
  -- obj A := .mk' (F.obj A.X)
  obj A :=
    { X := F.obj A.X
      one := Îµ F â‰« F.map A.one
      mul := Â«Î¼Â» F _ _ â‰« F.map A.mul
      one_mul := by
        simp_rw [comp_whiskerRight, Category.assoc, Î¼_natural_left_assoc,
          LaxMonoidal.left_unitality]
        slice_lhs 3 4 => rw [â† F.map_comp, A.one_mul]
      mul_one := by
        simp_rw [MonoidalCategory.whiskerLeft_comp, Category.assoc, Î¼_natural_right_assoc,
          LaxMonoidal.right_unitality]
        slice_lhs 3 4 => rw [â† F.map_comp, A.mul_one]
      mul_assoc := by
        simp_rw [comp_whiskerRight, Category.assoc, Î¼_natural_left_assoc,
          MonoidalCategory.whiskerLeft_comp, Category.assoc, Î¼_natural_right_assoc]
        slice_lhs 3 4 => rw [â† F.map_comp, A.mul_assoc]
        simp }
  map f := .mk' (F.map f.hom)

protected instance Faithful.mapMon [F.Faithful] : F.mapMon.Faithful where
  map_injective {_X _Y} _f _g hfg := Mon_.Hom.ext <| map_injective congr(($hfg).hom)

end LaxMonoidal

section Monoidal
variable [F.Monoidal]

attribute [local instance] obj.instMon_Class

protected instance Full.mapMon [F.Full] [F.Faithful] : F.mapMon.Full where
  map_surjective {X Y} f :=
    let âŸ¨g, hgâŸ© := F.map_surjective f.hom
    âŸ¨{
      hom := g
      one_hom := F.map_injective <| by simpa [â† hg, cancel_epi] using f.one_hom
      mul_hom := F.map_injective <| by simpa [â† hg, cancel_epi] using f.mul_hom
    }, Mon_.Hom.ext hgâŸ©

instance FullyFaithful.isMon_Hom_preimage (hF : F.FullyFaithful) {X Y : C}
    [Mon_Class X] [Mon_Class Y] (f : F.obj X âŸ¶ F.obj Y) [IsMon_Hom f] :
    IsMon_Hom (hF.preimage f) where
  one_hom := hF.map_injective <| by simp [â† obj.Î·_def_assoc, â† obj.Î·_def, â† cancel_epi (Îµ F)]
  mul_hom := hF.map_injective <| by
    simp [â† obj.Î¼_def_assoc, â† obj.Î¼_def, â† Î¼_natural_assoc, â† cancel_epi (LaxMonoidal.Î¼ F ..)]

/-- If `F : C â¥¤ D` is a fully faithful monoidal functor, then `Mon(F) : Mon C â¥¤ Mon D` is fully
faithful too. -/
protected def FullyFaithful.mapMon (hF : F.FullyFaithful) : F.mapMon.FullyFaithful where
  preimage {X Y} f := .mk' <| hF.preimage f.hom

end Monoidal

variable (C D)

/-- `mapMon` is functorial in the lax monoidal functor. -/
@[simps] -- Porting note: added this, not sure how it worked previously without.
def mapMonFunctor : LaxMonoidalFunctor C D â¥¤ Mon_ C â¥¤ Mon_ D where
  obj F := F.mapMon
  map Î± := { app := fun A => { hom := Î±.hom.app A.X } }
  map_comp _ _ := rfl



end CategoryTheory.Functor

namespace Mon_

namespace EquivLaxMonoidalFunctorPUnit

/-- Implementation of `Mon_.equivLaxMonoidalFunctorPUnit`. -/
@[simps]
def laxMonoidalToMon : LaxMonoidalFunctor (Discrete PUnit.{u + 1}) C â¥¤ Mon_ C where
  obj F := (F.mapMon : Mon_ _ â¥¤ Mon_ C).obj (trivial (Discrete PUnit))
  map Î± := ((Functor.mapMonFunctor (Discrete PUnit) C).map Î±).app _

variable {C}

/-- Implementation of `Mon_.equivLaxMonoidalFunctorPUnit`. -/
@[simps!]
def monToLaxMonoidalObj (A : Mon_ C) :
    Discrete PUnit.{u + 1} â¥¤ C := (Functor.const _).obj A.X

instance (A : Mon_ C) : (monToLaxMonoidalObj A).LaxMonoidal where
  Îµ' := A.one
  Î¼' := fun _ _ => A.mul

@[simp]
lemma monToLaxMonoidalObj_Îµ (A : Mon_ C) :
    Îµ (monToLaxMonoidalObj A) = A.one := rfl

@[simp]
lemma monToLaxMonoidalObj_Î¼ (A : Mon_ C) (X Y) :
    Â«Î¼Â» (monToLaxMonoidalObj A) X Y = A.mul := rfl

variable (C)
/-- Implementation of `Mon_.equivLaxMonoidalFunctorPUnit`. -/
@[simps]
def monToLaxMonoidal : Mon_ C â¥¤ LaxMonoidalFunctor (Discrete PUnit.{u + 1}) C where
  obj A := LaxMonoidalFunctor.of (monToLaxMonoidalObj A)
  map f :=
    { hom := { app := fun _ => f.hom }
      isMonoidal := { } }

attribute [local aesop safe tactic (rule_sets := [CategoryTheory])]
  CategoryTheory.Discrete.discreteCases

/-- Implementation of `Mon_.equivLaxMonoidalFunctorPUnit`. -/
@[simps!]
def unitIso :
    ğŸ­ (LaxMonoidalFunctor (Discrete PUnit.{u + 1}) C) â‰… laxMonoidalToMon C â‹™ monToLaxMonoidal C :=
  NatIso.ofComponents
    (fun F â†¦ LaxMonoidalFunctor.isoOfComponents (fun _ â†¦ F.mapIso (eqToIso (by ext))))

/-- Implementation of `Mon_.equivLaxMonoidalFunctorPUnit`. -/
@[simps!]
def counitIso : monToLaxMonoidal C â‹™ laxMonoidalToMon C â‰… ğŸ­ (Mon_ C) :=
  NatIso.ofComponents fun F â†¦ mkIso (Iso.refl _)

end EquivLaxMonoidalFunctorPUnit

open EquivLaxMonoidalFunctorPUnit

attribute [local simp] eqToIso_map

/--
Monoid objects in `C` are "just" lax monoidal functors from the trivial monoidal category to `C`.
-/
@[simps]
def equivLaxMonoidalFunctorPUnit : LaxMonoidalFunctor (Discrete PUnit.{u + 1}) C â‰Œ Mon_ C where
  functor := laxMonoidalToMon C
  inverse := monToLaxMonoidal C
  unitIso := unitIso C
  counitIso := counitIso C

end Mon_

namespace Mon_

/-!
In this section, we prove that the category of monoids in a braided monoidal category is monoidal.

Given two monoids `M` and `N` in a braided monoidal category `C`,
the multiplication on the tensor product `M.X âŠ— N.X` is defined in the obvious way:
it is the tensor product of the multiplications on `M` and `N`,
except that the tensor factors in the source come in the wrong order,
which we fix by pre-composing with a permutation isomorphism constructed from the braiding.

(There is a subtlety here: in fact there are two ways to do these,
using either the positive or negative crossing.)

A more conceptual way of understanding this definition is the following:
The braiding on `C` gives rise to a monoidal structure on
the tensor product functor from `C Ã— C` to `C`.
A pair of monoids in `C` gives rise to a monoid in `C Ã— C`,
which the tensor product functor by being monoidal takes to a monoid in `C`.
The permutation isomorphism appearing in the definition of
the multiplication on the tensor product of two monoids is
an instance of a more general family of isomorphisms
which together form a strength that equips the tensor product functor with a monoidal structure,
and the monoid axioms for the tensor product follow from the monoid axioms for the tensor factors
plus the properties of the strength (i.e., monoidal functor axioms).
The strength `tensorÎ¼` of the tensor product functor has been defined in
`Mathlib.CategoryTheory.Monoidal.Braided`.
Its properties, stated as independent lemmas in that module,
are used extensively in the proofs below.
Notice that we could have followed the above plan not only conceptually
but also as a possible implementation and
could have constructed the tensor product of monoids via `mapMon`,
but we chose to give a more explicit definition directly in terms of `tensorÎ¼`.

To complete the definition of the monoidal category structure on the category of monoids,
we need to provide definitions of associator and unitors.
The obvious candidates are the associator and unitors from `C`,
but we need to prove that they are monoid morphisms, i.e., compatible with unit and multiplication.
These properties translate to the monoidality of the associator and unitors
(with respect to the monoidal structures on the functors they relate),
which have also been proved in `Mathlib.CategoryTheory.Monoidal.Braided`.

-/


variable {C}

-- The proofs that associators and unitors preserve monoid units don't require braiding.
theorem one_associator {M N P : Mon_ C} :
    ((Î»_ (ğŸ™_ C)).inv â‰« ((Î»_ (ğŸ™_ C)).inv â‰« (M.one âŠ— N.one) âŠ— P.one)) â‰« (Î±_ M.X N.X P.X).hom =
      (Î»_ (ğŸ™_ C)).inv â‰« (M.one âŠ— (Î»_ (ğŸ™_ C)).inv â‰« (N.one âŠ— P.one)) := by
  simp only [Category.assoc, Iso.cancel_iso_inv_left]
  slice_lhs 1 3 => rw [â† Category.id_comp P.one, tensor_comp]
  slice_lhs 2 3 => rw [associator_naturality]
  slice_rhs 1 2 => rw [â† Category.id_comp M.one, tensor_comp]
  slice_lhs 1 2 => rw [tensorHom_id, â† leftUnitor_tensor_inv]
  rw [â† cancel_epi (Î»_ (ğŸ™_ C)).inv]
  slice_lhs 1 2 => rw [leftUnitor_inv_naturality]
  simp

theorem one_leftUnitor {M : Mon_ C} :
    ((Î»_ (ğŸ™_ C)).inv â‰« (ğŸ™ (ğŸ™_ C) âŠ— M.one)) â‰« (Î»_ M.X).hom = M.one := by
  simp

theorem one_rightUnitor {M : Mon_ C} :
    ((Î»_ (ğŸ™_ C)).inv â‰« (M.one âŠ— ğŸ™ (ğŸ™_ C))) â‰« (Ï_ M.X).hom = M.one := by
  simp [â† unitors_equal]

section BraidedCategory

variable [BraidedCategory C]

theorem Mon_tensor_one_mul (M N : Mon_ C) :
    (((Î»_ (ğŸ™_ C)).inv â‰« (M.one âŠ— N.one)) â–· (M.X âŠ— N.X)) â‰«
        tensorÎ¼ M.X N.X M.X N.X â‰« (M.mul âŠ— N.mul) =
      (Î»_ (M.X âŠ— N.X)).hom := by
  simp only [comp_whiskerRight_assoc]
  slice_lhs 2 3 => rw [tensorÎ¼_natural_left]
  slice_lhs 3 4 => rw [â† tensor_comp, one_mul M, one_mul N]
  symm
  exact tensor_left_unitality M.X N.X

theorem Mon_tensor_mul_one (M N : Mon_ C) :
    (M.X âŠ— N.X) â— ((Î»_ (ğŸ™_ C)).inv â‰« (M.one âŠ— N.one)) â‰«
        tensorÎ¼ M.X N.X M.X N.X â‰« (M.mul âŠ— N.mul) =
      (Ï_ (M.X âŠ— N.X)).hom := by
  simp only [MonoidalCategory.whiskerLeft_comp_assoc]
  slice_lhs 2 3 => rw [tensorÎ¼_natural_right]
  slice_lhs 3 4 => rw [â† tensor_comp, mul_one M, mul_one N]
  symm
  exact tensor_right_unitality M.X N.X

theorem Mon_tensor_mul_assoc (M N : Mon_ C) :
    ((tensorÎ¼ M.X N.X M.X N.X â‰« (M.mul âŠ— N.mul)) â–· (M.X âŠ— N.X)) â‰«
        tensorÎ¼ M.X N.X M.X N.X â‰« (M.mul âŠ— N.mul) =
      (Î±_ (M.X âŠ— N.X) (M.X âŠ— N.X) (M.X âŠ— N.X)).hom â‰«
        ((M.X âŠ— N.X) â— (tensorÎ¼ M.X N.X M.X N.X â‰« (M.mul âŠ— N.mul))) â‰«
          tensorÎ¼ M.X N.X M.X N.X â‰« (M.mul âŠ— N.mul) := by
  simp only [comp_whiskerRight_assoc, MonoidalCategory.whiskerLeft_comp_assoc]
  slice_lhs 2 3 => rw [tensorÎ¼_natural_left]
  slice_lhs 3 4 => rw [â† tensor_comp, mul_assoc M, mul_assoc N, tensor_comp, tensor_comp]
  slice_lhs 1 3 => rw [tensor_associativity]
  slice_lhs 3 4 => rw [â† tensorÎ¼_natural_right]
  simp

theorem mul_associator {M N P : Mon_ C} :
    (tensorÎ¼ (M.X âŠ— N.X) P.X (M.X âŠ— N.X) P.X â‰«
          (tensorÎ¼ M.X N.X M.X N.X â‰« (M.mul âŠ— N.mul) âŠ— P.mul)) â‰«
        (Î±_ M.X N.X P.X).hom =
      ((Î±_ M.X N.X P.X).hom âŠ— (Î±_ M.X N.X P.X).hom) â‰«
        tensorÎ¼ M.X (N.X âŠ— P.X) M.X (N.X âŠ— P.X) â‰«
          (M.mul âŠ— tensorÎ¼ N.X P.X N.X P.X â‰« (N.mul âŠ— P.mul)) := by
  simp only [tensor_obj, prodMonoidal_tensorObj, Category.assoc]
  slice_lhs 2 3 => rw [â† Category.id_comp P.mul, tensor_comp]
  slice_lhs 3 4 => rw [associator_naturality]
  slice_rhs 3 4 => rw [â† Category.id_comp M.mul, tensor_comp]
  simp only [tensorHom_id, id_tensorHom]
  slice_lhs 1 3 => rw [associator_monoidal]
  simp only [Category.assoc]

theorem mul_leftUnitor {M : Mon_ C} :
    (tensorÎ¼ (ğŸ™_ C) M.X (ğŸ™_ C) M.X â‰« ((Î»_ (ğŸ™_ C)).hom âŠ— M.mul)) â‰« (Î»_ M.X).hom =
      ((Î»_ M.X).hom âŠ— (Î»_ M.X).hom) â‰« M.mul := by
  rw [â† Category.comp_id (Î»_ (ğŸ™_ C)).hom, â† Category.id_comp M.mul, tensor_comp]
  simp only [tensorHom_id, id_tensorHom]
  slice_lhs 3 4 => rw [leftUnitor_naturality]
  slice_lhs 1 3 => rw [â† leftUnitor_monoidal]
  simp only [Category.assoc, Category.id_comp]

theorem mul_rightUnitor {M : Mon_ C} :
    (tensorÎ¼ M.X (ğŸ™_ C) M.X (ğŸ™_ C) â‰« (M.mul âŠ— (Î»_ (ğŸ™_ C)).hom)) â‰« (Ï_ M.X).hom =
      ((Ï_ M.X).hom âŠ— (Ï_ M.X).hom) â‰« M.mul := by
  rw [â† Category.id_comp M.mul, â† Category.comp_id (Î»_ (ğŸ™_ C)).hom, tensor_comp]
  simp only [tensorHom_id, id_tensorHom]
  slice_lhs 3 4 => rw [rightUnitor_naturality]
  slice_lhs 1 3 => rw [â† rightUnitor_monoidal]
  simp only [Category.assoc, Category.id_comp]

@[simps tensorObj_X tensorHom_hom]
instance monMonoidalStruct : MonoidalCategoryStruct (Mon_ C) :=
  let tensorObj (M N : Mon_ C) : Mon_ C :=
    { X := M.X âŠ— N.X
      one := (Î»_ (ğŸ™_ C)).inv â‰« (M.one âŠ— N.one)
      mul := tensorÎ¼ M.X N.X M.X N.X â‰« (M.mul âŠ— N.mul)
      one_mul := Mon_tensor_one_mul M N
      mul_one := Mon_tensor_mul_one M N
      mul_assoc := Mon_tensor_mul_assoc M N }
  let tensorHom {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : Mon_ C} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚) :
      tensorObj _ _ âŸ¶ tensorObj _ _ :=
    { hom := f.hom âŠ— g.hom
      one_hom := by
        dsimp [tensorObj]
        slice_lhs 2 3 => rw [â† tensor_comp, Hom.one_hom f, Hom.one_hom g]
      mul_hom := by
        dsimp [tensorObj]
        slice_rhs 1 2 => rw [tensorÎ¼_natural]
        slice_lhs 2 3 => rw [â† tensor_comp, Hom.mul_hom f, Hom.mul_hom g, tensor_comp]
        simp only [Category.assoc] }
  { tensorObj := tensorObj
    tensorHom := tensorHom
    whiskerRight := fun f Y => tensorHom f (ğŸ™ Y)
    whiskerLeft := fun X _ _ g => tensorHom (ğŸ™ X) g
    tensorUnit := trivial C
    associator := fun M N P â†¦ mkIso (Î±_ M.X N.X P.X) one_associator mul_associator
    leftUnitor := fun M â†¦ mkIso (Î»_ M.X) one_leftUnitor mul_leftUnitor
    rightUnitor := fun M â†¦ mkIso (Ï_ M.X) one_rightUnitor mul_rightUnitor }

@[simp]
theorem tensorUnit_X : (ğŸ™_ (Mon_ C)).X = ğŸ™_ C := rfl

@[simp]
theorem tensorUnit_one : (ğŸ™_ (Mon_ C)).one = ğŸ™ (ğŸ™_ C) := rfl

@[simp]
theorem tensorUnit_mul : (ğŸ™_ (Mon_ C)).mul = (Î»_ (ğŸ™_ C)).hom := rfl

@[simp]
theorem tensorObj_one (X Y : Mon_ C) : (X âŠ— Y).one = (Î»_ (ğŸ™_ C)).inv â‰« (X.one âŠ— Y.one) := rfl

@[simp]
theorem tensorObj_mul (X Y : Mon_ C) :
    (X âŠ— Y).mul = tensorÎ¼ X.X Y.X X.X Y.X â‰« (X.mul âŠ— Y.mul) := rfl

@[simp]
theorem whiskerLeft_hom {X Y : Mon_ C} (f : X âŸ¶ Y) (Z : Mon_ C) :
    (f â–· Z).hom = f.hom â–· Z.X := by
  rw [â† tensorHom_id]; rfl

@[simp]
theorem whiskerRight_hom (X : Mon_ C) {Y Z : Mon_ C} (f : Y âŸ¶ Z) :
    (X â— f).hom = X.X â— f.hom := by
  rw [â† id_tensorHom]; rfl

@[simp]
theorem leftUnitor_hom_hom (X : Mon_ C) : (Î»_ X).hom.hom = (Î»_ X.X).hom := rfl

@[simp]
theorem leftUnitor_inv_hom (X : Mon_ C) : (Î»_ X).inv.hom = (Î»_ X.X).inv := rfl

@[simp]
theorem rightUnitor_hom_hom (X : Mon_ C) : (Ï_ X).hom.hom = (Ï_ X.X).hom := rfl

@[simp]
theorem rightUnitor_inv_hom (X : Mon_ C) : (Ï_ X).inv.hom = (Ï_ X.X).inv := rfl

@[simp]
theorem associator_hom_hom (X Y Z : Mon_ C) : (Î±_ X Y Z).hom.hom = (Î±_ X.X Y.X Z.X).hom := rfl

@[simp]
theorem associator_inv_hom (X Y Z : Mon_ C) : (Î±_ X Y Z).inv.hom = (Î±_ X.X Y.X Z.X).inv := rfl

@[simp]
theorem tensor_one (M N : Mon_ C) : (M âŠ— N).one = (Î»_ (ğŸ™_ C)).inv â‰« (M.one âŠ— N.one) := rfl

@[simp]
theorem tensor_mul (M N : Mon_ C) : (M âŠ— N).mul =
    tensorÎ¼ M.X N.X M.X N.X â‰« (M.mul âŠ— N.mul) := rfl

instance monMonoidal : MonoidalCategory (Mon_ C) where
  tensorHom_def := by intros; ext; simp [tensorHom_def]

@[simps!]
instance {M N : C} [Mon_Class M] [Mon_Class N] : Mon_Class (M âŠ— N) :=
  inferInstanceAs <| Mon_Class (Mon_.mk' M âŠ— Mon_.mk' N).X

variable (C)

/-- The forgetful functor from `Mon_ C` to `C` is monoidal when `C` is monoidal. -/
instance : (forget C).Monoidal :=
  Functor.CoreMonoidal.toMonoidal
    { ÎµIso := Iso.refl _
      Î¼Iso := fun _ _ â†¦ Iso.refl _ }

@[simp] theorem forget_Îµ : Îµ (forget C) = ğŸ™ (ğŸ™_ C) := rfl
@[simp] theorem forget_Î· : Â«Î·Â» (forget C) = ğŸ™ (ğŸ™_ C) := rfl
@[simp] theorem forget_Î¼ (X Y : Mon_ C) : Â«Î¼Â» (forget C) X Y = ğŸ™ (X.X âŠ— Y.X) := rfl
@[simp] theorem forget_Î´ (X Y : Mon_ C) : Î´ (forget C) X Y = ğŸ™ (X.X âŠ— Y.X) := rfl

variable {C}

theorem one_braiding {X Y : Mon_ C} : (X âŠ— Y).one â‰« (Î²_ X.X Y.X).hom = (Y âŠ— X).one := by
  simp only [monMonoidalStruct_tensorObj_X, tensor_one, Category.assoc,
    BraidedCategory.braiding_naturality, braiding_tensorUnit_right, Iso.cancel_iso_inv_left]
  monoidal

end BraidedCategory

/-!
We next show that if `C` is symmetric, then `Mon_ C` is braided, and indeed symmetric.

Note that `Mon_ C` is *not* braided in general when `C` is only braided.

The more interesting construction is the 2-category of monoids in `C`,
bimodules between the monoids, and intertwiners between the bimodules.

When `C` is braided, that is a monoidal 2-category.
-/
section SymmetricCategory

variable [SymmetricCategory C]

theorem mul_braiding {X Y : Mon_ C} :
    (X âŠ— Y).mul â‰« (Î²_ X.X Y.X).hom = ((Î²_ X.X Y.X).hom âŠ— (Î²_ X.X Y.X).hom) â‰« (Y âŠ— X).mul := by
  dsimp
  simp only [tensorÎ¼, Category.assoc, BraidedCategory.braiding_naturality,
    BraidedCategory.braiding_tensor_right, BraidedCategory.braiding_tensor_left,
    comp_whiskerRight, whisker_assoc, MonoidalCategory.whiskerLeft_comp, pentagon_assoc,
    pentagon_inv_hom_hom_hom_inv_assoc, Iso.inv_hom_id_assoc, whiskerLeft_hom_inv_assoc]
  slice_lhs 3 4 =>
    -- We use symmetry here:
    rw [â† MonoidalCategory.whiskerLeft_comp, â† comp_whiskerRight, SymmetricCategory.symmetry]
  simp only [id_whiskerRight, MonoidalCategory.whiskerLeft_id, Category.id_comp, Category.assoc,
    pentagon_inv_assoc, Iso.hom_inv_id_assoc]
  slice_lhs 1 2 =>
    rw [â† associator_inv_naturality_left]
  slice_lhs 2 3 =>
    rw [Iso.inv_hom_id]
  rw [Category.id_comp]
  slice_lhs 2 3 =>
    rw [â† associator_naturality_right]
  slice_lhs 1 2 =>
    rw [â† tensorHom_def]
  simp only [Category.assoc]

instance : SymmetricCategory (Mon_ C) where
  braiding := fun X Y => mkIso (Î²_ X.X Y.X) one_braiding mul_braiding
  symmetry := fun X Y => by
    ext
    simp [â† SymmetricCategory.braiding_swap_eq_inv_braiding]

end SymmetricCategory

end Mon_

section

variable {C} [BraidedCategory.{vâ‚} C]

/-- Predicate for a monoid object to be commutative. -/
class IsCommMon (X : C) [Mon_Class X] where
  mul_comm' : (Î²_ X X).hom â‰« Î¼ = Î¼ := by aesop_cat

open scoped Mon_Class

namespace IsCommMon

@[reassoc (attr := simp)]
theorem mul_comm (X : C) [Mon_Class X] [IsCommMon X] : (Î²_ X X).hom â‰« Î¼ = Î¼ := mul_comm'

end IsCommMon

end

/-!
Projects:
* Check that `Mon_ MonCat â‰Œ CommMonCat`, via the Eckmann-Hilton argument.
  (You'll have to hook up the cartesian monoidal structure on `MonCat` first,
  available in https://github.com/leanprover-community/mathlib3/pull/3463)
* More generally, check that `Mon_ (Mon_ C) â‰Œ CommMon_ C` when `C` is braided.
* Check that `Mon_ TopCat â‰Œ [bundled topological monoids]`.
* Check that `Mon_ AddCommGrp â‰Œ RingCat`.
  (We've already got `Mon_ (ModuleCat R) â‰Œ AlgebraCat R`,
  in `Mathlib.CategoryTheory.Monoidal.Internal.Module`.)
* Can you transport this monoidal structure to `RingCat` or `AlgebraCat R`?
  How does it compare to the "native" one?
* Show that when `F` is a lax braided functor `C â¥¤ D`, the functor `map_Mon F : Mon_ C â¥¤ Mon_ D`
  is lax monoidal.
-/
