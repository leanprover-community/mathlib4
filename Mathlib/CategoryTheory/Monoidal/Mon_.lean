/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.Algebra.Group.PUnit
import Mathlib.CategoryTheory.Monoidal.Braided.Basic
import Mathlib.CategoryTheory.Monoidal.CoherenceLemmas
import Mathlib.CategoryTheory.Monoidal.Discrete
import Mathlib.CategoryTheory.Limits.Shapes.Terminal

/-!
# The category of monoids in a monoidal category.

We define monoids in a monoidal category `C` and show that the category of monoids is equivalent to
the category of lax monoidal functors from the unit monoidal category to `C`.  We also show that if
`C` is braided, then the category of monoids is naturally monoidal.

## Simp set for monoid object tautologies

In this file, we also provide a simp set called `mon_tauto` whose goal is to prove all tautologies
about morphisms from some (tensor) power of `M` to `M`, where `M` is a (commutative) monoid object
in a (braided) monoidal category.

Please read the documentation in `Mathlib/Tactic/Attr/Register.lean` for full details.
-/

universe w vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ u

open Function CategoryTheory MonoidalCategory Functor.LaxMonoidal Functor.OplaxMonoidal

variable {C : Type uâ‚} [Category.{vâ‚} C] [MonoidalCategory.{vâ‚} C]

/-- A monoid object internal to a monoidal category.

When the monoidal category is preadditive, this is also sometimes called an "algebra object".
-/
class MonObj (X : C) where
  /-- The unit morphism of a monoid object. -/
  one : ğŸ™_ C âŸ¶ X
  /-- The multiplication morphism of a monoid object. -/
  mul : X âŠ— X âŸ¶ X
  one_mul (X) : one â–· X â‰« mul = (Î»_ X).hom := by cat_disch
  mul_one (X) : X â— one â‰« mul = (Ï_ X).hom := by cat_disch
  -- Obviously there is some flexibility stating this axiom.
  -- This one has left- and right-hand sides matching the statement of `Monoid.mul_assoc`,
  -- and chooses to place the associator on the right-hand side.
  -- The heuristic is that unitors and associators "don't have much weight".
  mul_assoc (X) : (mul â–· X) â‰« mul = (Î±_ X X X).hom â‰« (X â— mul) â‰« mul := by cat_disch

@[deprecated (since := "2025-09-09")] alias Mon_Class := MonObj

namespace MonObj
variable {M X Y : C} [MonObj M]

@[inherit_doc] scoped notation "Î¼" => MonObj.mul
@[inherit_doc] scoped notation "Î¼["M"]" => MonObj.mul (X := M)
@[inherit_doc] scoped notation "Î·" => MonObj.one
@[inherit_doc] scoped notation "Î·["M"]" => MonObj.one (X := M)

attribute [reassoc (attr := simp)] one_mul mul_one mul_assoc

/-- Transfer `MonObj` along an isomorphism. -/
-- Note: The simps lemmas are not tagged simp because their `#discr_tree_simp_key` are too generic.
@[simps! -isSimp]
def ofIso (e : M â‰… X) : MonObj X where
  one := Î·[M] â‰« e.hom
  mul := (e.inv âŠ—â‚˜ e.inv) â‰« Î¼[M] â‰« e.hom
  one_mul := by
    rw [â† cancel_epi (Î»_ X).inv]
    simp only [comp_whiskerRight, tensorHom_def, Category.assoc,
      hom_inv_whiskerRight_assoc]
    simp [â† tensorHom_def_assoc, leftUnitor_inv_comp_tensorHom_assoc]
  mul_one := by
    rw [â† cancel_epi (Ï_ X).inv]
    simp only [MonoidalCategory.whiskerLeft_comp, tensorHom_def', Category.assoc,
      whiskerLeft_hom_inv_assoc, Iso.inv_hom_id]
    simp [â† tensorHom_def'_assoc, rightUnitor_inv_comp_tensorHom_assoc]
  mul_assoc := by simpa [â† id_tensorHom, â† tensorHom_id,
      -associator_conjugation, associator_naturality_assoc] using
      congr(((e.inv âŠ—â‚˜ e.inv) âŠ—â‚˜ e.inv) â‰« $(MonObj.mul_assoc M) â‰« e.hom)

@[simps]
instance : MonObj (ğŸ™_ C) where
  one := ğŸ™ _
  mul := (Î»_ _).hom
  mul_assoc := by monoidal_coherence
  mul_one := by monoidal_coherence

@[ext]
theorem ext {X : C} (hâ‚ hâ‚‚ : MonObj X) (H : hâ‚.mul = hâ‚‚.mul) : hâ‚ = hâ‚‚ := by
  suffices hâ‚.one = hâ‚‚.one by cases hâ‚; cases hâ‚‚; subst H this; rfl
  trans (Î»_ _).inv â‰« (hâ‚.one âŠ—â‚˜ hâ‚‚.one) â‰« hâ‚.mul
  Â· simp [tensorHom_def, H, â† unitors_equal]
  Â· simp [tensorHom_def']

end MonObj

open scoped MonObj

namespace Mathlib.Tactic.MonTauto
variable {C : Type uâ‚} [Category.{vâ‚} C] [MonoidalCategory C]
  {M W X Xâ‚ Xâ‚‚ Xâ‚ƒ Y Yâ‚ Yâ‚‚ Yâ‚ƒ Z Zâ‚ Zâ‚‚ : C} [MonObj M]

attribute [mon_tauto] Category.id_comp Category.comp_id Category.assoc
  id_tensorHom_id tensorÎ¼ tensorÎ´
  tensorHom_comp_tensorHom tensorHom_comp_tensorHom_assoc
  leftUnitor_tensor_hom leftUnitor_tensor_hom_assoc
  leftUnitor_tensor_inv leftUnitor_tensor_inv_assoc
  rightUnitor_tensor_hom rightUnitor_tensor_hom_assoc
  rightUnitor_tensor_inv rightUnitor_tensor_inv_assoc

attribute [mon_tauto â†] tensorHom_id id_tensorHom

@[reassoc (attr := mon_tauto)]
lemma associator_hom_comp_tensorHom_tensorHom (f : Xâ‚ âŸ¶ Xâ‚‚) (g : Yâ‚ âŸ¶ Yâ‚‚) (h : Zâ‚ âŸ¶ Zâ‚‚) :
    (Î±_ Xâ‚ Yâ‚ Zâ‚).hom â‰« (f âŠ—â‚˜ g âŠ—â‚˜ h) = ((f âŠ—â‚˜ g) âŠ—â‚˜ h) â‰« (Î±_ Xâ‚‚ Yâ‚‚ Zâ‚‚).hom := by simp

@[reassoc (attr := mon_tauto)]
lemma associator_inv_comp_tensorHom_tensorHom (f : Xâ‚ âŸ¶ Xâ‚‚) (g : Yâ‚ âŸ¶ Yâ‚‚) (h : Zâ‚ âŸ¶ Zâ‚‚) :
    (Î±_ Xâ‚ Yâ‚ Zâ‚).inv â‰« ((f âŠ—â‚˜ g) âŠ—â‚˜ h) = (f âŠ—â‚˜ g âŠ—â‚˜ h) â‰« (Î±_ Xâ‚‚ Yâ‚‚ Zâ‚‚).inv := by simp

@[reassoc (attr := mon_tauto)]
lemma associator_hom_comp_tensorHom_tensorHom_comp (f : Xâ‚ âŸ¶ Xâ‚‚) (g : Yâ‚ âŸ¶ Yâ‚‚) (h : Zâ‚ âŸ¶ Zâ‚‚)
    (gh : Yâ‚‚ âŠ— Zâ‚‚ âŸ¶ W) :
    (Î±_ Xâ‚ Yâ‚ Zâ‚).hom â‰« (f âŠ—â‚˜ ((g âŠ—â‚˜ h) â‰« gh)) =
      ((f âŠ—â‚˜ g) âŠ—â‚˜ h) â‰« (Î±_ Xâ‚‚ Yâ‚‚ Zâ‚‚).hom â‰« (ğŸ™ _ âŠ—â‚˜ gh) := by simp [tensorHom_def]

@[reassoc (attr := mon_tauto)]
lemma associator_inv_comp_tensorHom_tensorHom_comp (f : Xâ‚ âŸ¶ Xâ‚‚) (g : Yâ‚ âŸ¶ Yâ‚‚) (h : Zâ‚ âŸ¶ Zâ‚‚)
    (fg : Xâ‚‚ âŠ— Yâ‚‚ âŸ¶ W) :
    (Î±_ Xâ‚ Yâ‚ Zâ‚).inv â‰« (((f âŠ—â‚˜ g) â‰« fg) âŠ—â‚˜ h) =
      (f âŠ—â‚˜ g âŠ—â‚˜ h) â‰« (Î±_ Xâ‚‚ Yâ‚‚ Zâ‚‚).inv â‰« (fg âŠ—â‚˜ ğŸ™ _) := by simp [tensorHom_def']

@[mon_tauto] lemma eq_one_mul : (Î»_ M).hom = (Î· âŠ—â‚˜ ğŸ™ M) â‰« Î¼ := by simp
@[mon_tauto] lemma eq_mul_one : (Ï_ M).hom = (ğŸ™ M âŠ—â‚˜ Î·) â‰« Î¼ := by simp

@[reassoc (attr := mon_tauto)] lemma leftUnitor_inv_one_tensor_mul (f : Xâ‚ âŸ¶ M) :
    (Î»_ _).inv â‰« (Î· âŠ—â‚˜ f) â‰« Î¼ = f := by simp [tensorHom_def']

@[reassoc (attr := mon_tauto)] lemma rightUnitor_inv_tensor_one_mul (f : Xâ‚ âŸ¶ M) :
    (Ï_ _).inv â‰« (f âŠ—â‚˜ Î·) â‰« Î¼ = f := by simp [tensorHom_def]

@[reassoc (attr := mon_tauto)]
lemma mul_assoc_hom (f : X âŸ¶ M) :
    (Î±_ X M M).hom â‰« (f âŠ—â‚˜ Î¼) â‰« Î¼ = ((f âŠ—â‚˜ ğŸ™ M) â‰« Î¼ âŠ—â‚˜ ğŸ™ M) â‰« Î¼ := by simp [tensorHom_def]

@[reassoc (attr := mon_tauto)]
lemma mul_assoc_inv (f : X âŸ¶ M) :
    (Î±_ M M X).inv â‰« (Î¼ âŠ—â‚˜ f) â‰« Î¼ = (ğŸ™ M âŠ—â‚˜ (ğŸ™ M âŠ—â‚˜ f) â‰« Î¼) â‰« Î¼  := by simp [tensorHom_def']

end Mathlib.Tactic.MonTauto

variable {M N O X : C} [MonObj M] [MonObj N] [MonObj O]

/-- The property that a morphism between monoid objects is a monoid morphism. -/
class IsMonHom (f : M âŸ¶ N) : Prop where
  one_hom (f) : Î· â‰« f = Î· := by cat_disch
  mul_hom (f) : Î¼ â‰« f = (f âŠ—â‚˜ f) â‰« Î¼ := by cat_disch

@[deprecated (since := "2025-09-15")] alias IsMon_Hom := IsMonHom

attribute [reassoc (attr := simp)] IsMonHom.one_hom IsMonHom.mul_hom

instance : IsMonHom (ğŸ™ M) where

instance (f : M âŸ¶ N) (g : N âŸ¶ O) [IsMonHom f] [IsMonHom g] : IsMonHom (f â‰« g) where

attribute [local simp] MonObj.ofIso_one MonObj.ofIso_mul in
instance isMonHom_ofIso (e : M â‰… X) : letI := MonObj.ofIso e; IsMonHom e.hom := by
  letI := MonObj.ofIso e; exact { }

instance (f : M â‰… N) [IsMonHom f.hom] : IsMonHom f.inv where
  one_hom := by simp [Iso.comp_inv_eq]
  mul_hom := by simp [Iso.comp_inv_eq]

instance {f : M âŸ¶ N} [IsIso f] [IsMonHom f] : IsMonHom (asIso f).hom := â€¹_â€º

variable (C) in
/-- A monoid object internal to a monoidal category.

When the monoidal category is preadditive, this is also sometimes called an "algebra object".
-/
structure Mon where
  /-- The underlying object in the ambient monoidal category -/
  X : C
  [mon : MonObj X]

@[deprecated (since := "2025-09-15")] alias Mon_ := Mon

attribute [instance] Mon.mon

namespace Mon

variable (C) in
/-- The trivial monoid object. We later show this is initial in `Mon C`.
-/
@[simps!]
def trivial : Mon C := mk (ğŸ™_ C)

instance : Inhabited (Mon C) :=
  âŸ¨trivial CâŸ©

end Mon

namespace MonObj

variable {M : C} [MonObj M]

@[simp]
theorem one_mul_hom {Z : C} (f : Z âŸ¶ M) : (Î·[M] âŠ—â‚˜ f) â‰« Î¼[M] = (Î»_ Z).hom â‰« f := by
  rw [tensorHom_def'_assoc, one_mul, leftUnitor_naturality]

@[simp]
theorem mul_one_hom {Z : C} (f : Z âŸ¶ M) : (f âŠ—â‚˜ Î·[M]) â‰« Î¼[M] = (Ï_ Z).hom â‰« f := by
  rw [tensorHom_def_assoc, mul_one, rightUnitor_naturality]

variable (M) in
@[reassoc]
theorem mul_assoc_flip : M â— Î¼ â‰« Î¼ = (Î±_ M M M).inv â‰« Î¼ â–· M â‰« Î¼ := by
  simp

end MonObj

namespace Mon

/-- A morphism of monoid objects. -/
@[ext]
structure Hom (M N : Mon C) where
  /-- The underlying morphism -/
  hom : M.X âŸ¶ N.X
  [isMonHom_hom : IsMonHom hom]

attribute [instance] Hom.isMonHom_hom

/-- Construct a morphism `M âŸ¶ N` of `Mon C` from a map `f : M âŸ¶ N` and a `IsMonHom f` instance. -/
abbrev Hom.mk' {M N : Mon C} (f : M.X âŸ¶ N.X)
    (one_f : Î· â‰« f = Î· := by cat_disch)
    (mul_f : Î¼ â‰« f = (f âŠ—â‚˜ f) â‰« Î¼ := by cat_disch) : Hom M N :=
  have : IsMonHom f := âŸ¨one_f, mul_fâŸ©
  .mk f

/-- The identity morphism on a monoid object. -/
@[simps]
def id (M : Mon C) : Hom M M := âŸ¨ğŸ™ M.XâŸ©

instance homInhabited (M : Mon C) : Inhabited (Hom M M) :=
  âŸ¨id MâŸ©

/-- Composition of morphisms of monoid objects. -/
@[simps]
def comp {M N O : Mon C} (f : Hom M N) (g : Hom N O) : Hom M O where
  hom := f.hom â‰« g.hom

instance : Category (Mon C) where
  Hom M N := Hom M N
  id := id
  comp f g := comp f g

instance {M N : Mon C} (f : M âŸ¶ N) : IsMonHom f.hom := f.isMonHom_hom

@[ext]
lemma Hom.ext' {M N : Mon C} {f g : M âŸ¶ N} (w : f.hom = g.hom) : f = g :=
  Hom.ext w

lemma hom_injective {M N : Mon C} : Injective (Hom.hom : (M âŸ¶ N) â†’ (M.X âŸ¶ N.X)) :=
  fun _ _ â†¦ Hom.ext

@[simp]
theorem id_hom' (M : Mon C) : (ğŸ™ M : Hom M M).hom = ğŸ™ M.X :=
  rfl

@[simp]
theorem comp_hom' {M N K : Mon C} (f : M âŸ¶ N) (g : N âŸ¶ K) :
    (f â‰« g : Hom M K).hom = f.hom â‰« g.hom :=
  rfl

section

variable (C)

/-- The forgetful functor from monoid objects to the ambient category. -/
@[simps]
def forget : Mon C â¥¤ C where
  obj A := A.X
  map f := f.hom

end

instance forget_faithful : (forget C).Faithful where

instance {A B : Mon C} (f : A âŸ¶ B) [e : IsIso ((forget C).map f)] : IsIso f.hom :=
  e

/-- The forgetful functor from monoid objects to the ambient category reflects isomorphisms. -/
instance : (forget C).ReflectsIsomorphisms where
  reflects f e := âŸ¨âŸ¨.mk' (inv f.hom), by cat_dischâŸ©âŸ©

instance {M N : Mon C} {f : M âŸ¶ N} [IsIso f] : IsIso f.hom :=
  inferInstanceAs <| IsIso <| (forget C).map f

/-- Construct an isomorphism of monoid objects by giving a monoid isomorphism between the underlying
objects. -/
@[simps]
def mkIso' {M N : C} [MonObj M] [MonObj N] (e : M â‰… N) [IsMonHom e.hom] : mk M â‰… mk N where
  hom := Hom.mk e.hom
  inv := Hom.mk e.inv

/-- Construct an isomorphism of monoid objects by giving an isomorphism between the underlying
objects and checking compatibility with unit and multiplication only in the forward direction. -/
@[simps!]
abbrev mkIso {M N : Mon C} (e : M.X â‰… N.X) (one_f : Î·[M.X] â‰« e.hom = Î·[N.X] := by cat_disch)
    (mul_f : Î¼[M.X] â‰« e.hom = (e.hom âŠ—â‚˜ e.hom) â‰« Î¼[N.X] := by cat_disch) : M â‰… N :=
  have : IsMonHom e.hom := âŸ¨one_f, mul_fâŸ©
  mkIso' e

@[simps]
instance uniqueHomFromTrivial (A : Mon C) : Unique (trivial C âŸ¶ A) where
  default.hom := Î·[A.X]
  default.isMonHom_hom.mul_hom := by simp [unitors_equal]
  uniq f := by
    ext
    rw [â† Category.id_comp f.hom]
    dsimp only [trivial_X]
    rw [â† trivial_mon_one, IsMonHom.one_hom]

open CategoryTheory.Limits

instance : HasInitial (Mon C) :=
  hasInitial_of_unique (Mon.trivial C)

end Mon

namespace CategoryTheory
variable
  {D : Type uâ‚‚} [Category.{vâ‚‚} D] [MonoidalCategory D]
  {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E] [MonoidalCategory E]
  {F F' : C â¥¤ D} {G : D â¥¤ E}

namespace Functor

section LaxMonoidal
variable [F.LaxMonoidal] [F'.LaxMonoidal] [G.LaxMonoidal] (X Y : C) [MonObj X] [MonObj Y]
  (f : X âŸ¶ Y) [IsMonHom f]

/-- The image of a monoid object under a lax monoidal functor is a monoid object. -/
abbrev monObjObj : MonObj (F.obj X) where
  one := Îµ F â‰« F.map Î·
  mul := LaxMonoidal.Î¼ F X X â‰« F.map Î¼
  one_mul := by simp [â† F.map_comp]
  mul_one := by simp [â† F.map_comp]
  mul_assoc := by
    simp_rw [comp_whiskerRight, Category.assoc, Î¼_natural_left_assoc,
      MonoidalCategory.whiskerLeft_comp, Category.assoc, Î¼_natural_right_assoc]
    slice_lhs 3 4 => rw [â† F.map_comp, MonObj.mul_assoc]
    simp

@[deprecated (since := "2025-09-09")] alias mon_ClassObj := monObjObj

scoped[Obj] attribute [instance] CategoryTheory.Functor.monObjObj

open scoped Obj

@[reassoc, simp] lemma obj.Î·_def : (Î· : ğŸ™_ D âŸ¶ F.obj X) = Îµ F â‰« F.map Î· := rfl

@[reassoc, simp] lemma obj.Î¼_def : Î¼ = LaxMonoidal.Î¼ F X X â‰« F.map Î¼ := rfl

instance map.instIsMonHom : IsMonHom (F.map f) where
  one_hom := by simp [â† map_comp]
  mul_hom := by simp [â† map_comp]

open MonObj

-- TODO: mapMod F A : Mod A â¥¤ Mod (F.mapMon A)
variable (F) in
/-- A lax monoidal functor takes monoid objects to monoid objects.

That is, a lax monoidal functor `F : C â¥¤ D` induces a functor `Mon C â¥¤ Mon D`.
-/
@[simps]
def mapMon : Mon C â¥¤ Mon D where
  obj A := .mk (F.obj A.X)
  map f := .mk (F.map f.hom)

@[simp]
theorem id_mapMon_one (X : Mon C) : Î·[((ğŸ­ C).mapMon.obj X).X] = ğŸ™ _ â‰« Î·[X.X] := rfl

@[simp]
theorem id_mapMon_mul (X : Mon C) : Î¼[((ğŸ­ C).mapMon.obj X).X] = ğŸ™ _ â‰« Î¼[X.X] := rfl

@[simp]
theorem comp_mapMon_one (X : Mon C) :
    Î·[((F â‹™ G).mapMon.obj X).X] = Îµ (F â‹™ G) â‰« (F â‹™ G).map Î·[X.X] :=
  rfl

@[simp]
theorem comp_mapMon_mul (X : Mon C) :
    Î¼[((F â‹™ G).mapMon.obj X).X] = Â«Î¼Â» (F â‹™ G) _ _ â‰« (F â‹™ G).map Î¼[X.X] :=
  rfl

/-- The identity functor is also the identity on monoid objects. -/
@[simps!]
def mapMonIdIso : mapMon (ğŸ­ C) â‰… ğŸ­ (Mon C) :=
  NatIso.ofComponents fun X â†¦ Mon.mkIso (.refl _)

/-- The composition functor is also the composition on monoid objects. -/
@[simps!]
def mapMonCompIso : (F â‹™ G).mapMon â‰… F.mapMon â‹™ G.mapMon :=
  NatIso.ofComponents fun X â†¦ Mon.mkIso (.refl _)

protected instance Faithful.mapMon [F.Faithful] : F.mapMon.Faithful where
  map_injective {_X _Y} _f _g hfg := Mon.Hom.ext <| map_injective congr(($hfg).hom)

/-- Natural transformations between functors lift to monoid objects. -/
@[simps!]
def mapMonNatTrans (f : F âŸ¶ F') [NatTrans.IsMonoidal f] : F.mapMon âŸ¶ F'.mapMon where
  app X := .mk' (f.app _)

/-- Natural isomorphisms between functors lift to monoid objects. -/
@[simps!]
def mapMonNatIso (e : F â‰… F') [NatTrans.IsMonoidal e.hom] : F.mapMon â‰… F'.mapMon :=
  NatIso.ofComponents fun X â†¦ Mon.mkIso (e.app _)

end LaxMonoidal

section OplaxMonoidal
variable [F.OplaxMonoidal]

open scoped MonObj in
/-- Pullback a monoid object along a fully faithful oplax monoidal functor. -/
@[simps]
abbrev FullyFaithful.monObj (hF : F.FullyFaithful) (X : C) [MonObj (F.obj X)] : MonObj X where
  one := hF.preimage <| OplaxMonoidal.Î· F â‰« Î·[F.obj X]
  mul := hF.preimage <| OplaxMonoidal.Î´ F X X â‰« Î¼[F.obj X]
  one_mul := hF.map_injective <| by simp [â† Î´_natural_left_assoc]
  mul_one := hF.map_injective <| by simp [â† Î´_natural_right_assoc]
  mul_assoc := hF.map_injective <| by simp [â† Î´_natural_left_assoc, â† Î´_natural_right_assoc]

@[deprecated (since := "2025-09-09")] alias FullyFaithful.mon_Class := FullyFaithful.monObj

end OplaxMonoidal

section Monoidal
variable [F.Monoidal]

open scoped Obj

protected instance Full.mapMon [F.Full] [F.Faithful] : F.mapMon.Full where
  map_surjective {X Y} f :=
    let âŸ¨g, hgâŸ© := F.map_surjective f.hom
    âŸ¨{
      hom := g
      isMonHom_hom.one_hom :=
        F.map_injective <| by simpa [â† hg, cancel_epi] using IsMonHom.one_hom f.hom
      isMonHom_hom.mul_hom :=
        F.map_injective <| by simpa [â† hg, cancel_epi] using IsMonHom.mul_hom f.hom },
      Mon.Hom.ext hgâŸ©

instance FullyFaithful.isMonHom_preimage (hF : F.FullyFaithful) {X Y : C}
    [MonObj X] [MonObj Y] (f : F.obj X âŸ¶ F.obj Y) [IsMonHom f] :
    IsMonHom (hF.preimage f) where
  one_hom := hF.map_injective <| by simp [â† obj.Î·_def_assoc, â† obj.Î·_def, â† cancel_epi (Îµ F)]
  mul_hom := hF.map_injective <| by
    simp [â† obj.Î¼_def_assoc, â† obj.Î¼_def, â† Î¼_natural_assoc, â† cancel_epi (LaxMonoidal.Î¼ F ..)]

/-- If `F : C â¥¤ D` is a fully faithful monoidal functor, then `Mon(F) : Mon C â¥¤ Mon D` is fully
faithful too. -/
@[simps]
protected def FullyFaithful.mapMon (hF : F.FullyFaithful) : F.mapMon.FullyFaithful where
  preimage {X Y} f := .mk' <| hF.preimage f.hom

attribute [local simp] MonObj.ofIso_one MonObj.ofIso_mul in
open Monoidal in
/-- The essential image of a fully faithful functor between cartesian-monoidal categories is the
same on monoid objects as on objects. -/
@[simp] lemma essImage_mapMon [F.Full] [F.Faithful] {M : Mon D} :
    F.mapMon.essImage M â†” F.essImage M.X where
  mp := by rintro âŸ¨N, âŸ¨eâŸ©âŸ©; exact âŸ¨N.X, âŸ¨(Mon.forget _).mapIso eâŸ©âŸ©
  mpr := by
    rintro âŸ¨N, âŸ¨eâŸ©âŸ©
    let : MonObj (F.obj N) := .ofIso e.symm
    let : MonObj N := (FullyFaithful.ofFullyFaithful F).monObj N
    refine âŸ¨.mk N, âŸ¨Mon.mkIso e ?_ ?_âŸ©âŸ© <;> simp

end Monoidal

variable (C D) in
/-- `mapMon` is functorial in the lax monoidal functor. -/
@[simps]
def mapMonFunctor : LaxMonoidalFunctor C D â¥¤ Mon C â¥¤ Mon D where
  obj F := F.mapMon
  map Î± := { app A := .mk' (Î±.hom.app A.X) }
  map_comp _ _ := rfl

end Functor

open Functor

namespace Adjunction
variable {F : C â¥¤ D} {G : D â¥¤ C} (a : F âŠ£ G) [F.Monoidal] [G.LaxMonoidal] [a.IsMonoidal]

/-- An adjunction of monoidal functors lifts to an adjunction of their lifts to monoid objects. -/
@[simps] def mapMon : F.mapMon âŠ£ G.mapMon where
  unit := mapMonIdIso.inv â‰« mapMonNatTrans a.unit â‰« mapMonCompIso.hom
  counit := mapMonCompIso.inv â‰« mapMonNatTrans a.counit â‰« mapMonIdIso.hom

end Adjunction

namespace Equivalence

/-- An equivalence of categories lifts to an equivalence of their monoid objects. -/
@[simps]
def mapMon (e : C â‰Œ D) [e.functor.Monoidal] [e.inverse.Monoidal] [e.IsMonoidal] :
    Mon C â‰Œ Mon D where
  functor := e.functor.mapMon
  inverse := e.inverse.mapMon
  unitIso := mapMonIdIso.symm â‰ªâ‰« mapMonNatIso e.unitIso â‰ªâ‰« mapMonCompIso
  counitIso := mapMonCompIso.symm â‰ªâ‰« mapMonNatIso e.counitIso â‰ªâ‰« mapMonIdIso

end CategoryTheory.Equivalence

namespace Mon

namespace EquivLaxMonoidalFunctorPUnit

variable (C) in
/-- Implementation of `Mon.equivLaxMonoidalFunctorPUnit`. -/
@[simps]
def laxMonoidalToMon : LaxMonoidalFunctor (Discrete PUnit.{w + 1}) C â¥¤ Mon C where
  obj F := (F.mapMon : Mon _ â¥¤ Mon C).obj (trivial (Discrete PUnit))
  map Î± := ((Functor.mapMonFunctor (Discrete PUnit) C).map Î±).app _

/-- Implementation of `Mon.equivLaxMonoidalFunctorPUnit`. -/
@[simps!]
def monToLaxMonoidalObj (A : Mon C) :
    Discrete PUnit.{w + 1} â¥¤ C := (Functor.const _).obj A.X

instance (A : Mon C) : (monToLaxMonoidalObj A).LaxMonoidal where
  Îµ := Î·[A.X]
  Â«Î¼Â» _ _ := Î¼[A.X]

@[simp]
lemma monToLaxMonoidalObj_Îµ (A : Mon C) :
    Îµ (monToLaxMonoidalObj A) = Î·[A.X] := rfl

@[simp]
lemma monToLaxMonoidalObj_Î¼ (A : Mon C) (X Y) :
    Â«Î¼Â» (monToLaxMonoidalObj A) X Y = Î¼[A.X] := rfl

variable (C)
/-- Implementation of `Mon.equivLaxMonoidalFunctorPUnit`. -/
@[simps]
def monToLaxMonoidal : Mon C â¥¤ LaxMonoidalFunctor (Discrete PUnit.{w + 1}) C where
  obj A := LaxMonoidalFunctor.of (monToLaxMonoidalObj A)
  map f :=
    { hom := { app _ := f.hom }
      isMonoidal := { } }

attribute [local aesop safe tactic (rule_sets := [CategoryTheory])]
  CategoryTheory.Discrete.discreteCases

/-- Implementation of `Mon.equivLaxMonoidalFunctorPUnit`. -/
@[simps!]
def unitIso :
    ğŸ­ (LaxMonoidalFunctor (Discrete PUnit.{w + 1}) C) â‰… laxMonoidalToMon C â‹™ monToLaxMonoidal C :=
  NatIso.ofComponents
    (fun F â†¦ LaxMonoidalFunctor.isoOfComponents (fun _ â†¦ F.mapIso (eqToIso (by ext))))

/-- Auxiliary definition for `counitIso`. -/
@[simps!]
def counitIsoAux (F : Mon C) :
    ((monToLaxMonoidal.{w} C â‹™ laxMonoidalToMon C).obj F).X â‰… ((ğŸ­ (Mon C)).obj F).X :=
  Iso.refl _

@[simp]
theorem monToLaxMonoidal_laxMonoidalToMon_obj_one (F : Mon C) :
    Î·[((monToLaxMonoidal C â‹™ laxMonoidalToMon C).obj F).X] = Î·[F.X] â‰« ğŸ™ _ :=
  rfl

@[simp]
theorem monToLaxMonoidal_laxMonoidalToMon_obj_mul (F : Mon C) :
    Î¼[((monToLaxMonoidal C â‹™ laxMonoidalToMon C).obj F).X] = Î¼[F.X] â‰« ğŸ™ _ :=
  rfl

theorem isMonHom_counitIsoAux (F : Mon C) :
    IsMonHom (counitIsoAux C F).hom where

@[deprecated (since := "2025-09-15")] alias counitIsoAux_IsMon_Hom := isMonHom_counitIsoAux

/-- Implementation of `Mon.equivLaxMonoidalFunctorPUnit`. -/
@[simps!]
def counitIso : monToLaxMonoidal.{w} C â‹™ laxMonoidalToMon C â‰… ğŸ­ (Mon C) :=
  NatIso.ofComponents fun F â†¦
    letI : IsMonHom (counitIsoAux.{w} C F).hom := isMonHom_counitIsoAux C F
    mkIso (counitIsoAux.{w} C F)

end EquivLaxMonoidalFunctorPUnit

open EquivLaxMonoidalFunctorPUnit

attribute [local simp] eqToIso_map

/--
Monoid objects in `C` are "just" lax monoidal functors from the trivial monoidal category to `C`.
-/
@[simps]
def equivLaxMonoidalFunctorPUnit : LaxMonoidalFunctor (Discrete PUnit.{w + 1}) C â‰Œ Mon C where
  functor := laxMonoidalToMon C
  inverse := monToLaxMonoidal C
  unitIso := unitIso C
  counitIso := counitIso C

end Mon

namespace MonObj

/-!
In this section, we prove that the category of monoids in a braided monoidal category is monoidal.

Given two monoids `M` and `N` in a braided monoidal category `C`,
the multiplication on the tensor product `M.X âŠ— N.X` is defined in the obvious way:
it is the tensor product of the multiplications on `M` and `N`,
except that the tensor factors in the source come in the wrong order,
which we fix by pre-composing with a permutation isomorphism constructed from the braiding.

(There is a subtlety here: in fact there are two ways to do these,
using either the positive or negative crossing.)

A more conceptual way of understanding this definition is the following:
The braiding on `C` gives rise to a monoidal structure on
the tensor product functor from `C Ã— C` to `C`.
A pair of monoids in `C` gives rise to a monoid in `C Ã— C`,
which the tensor product functor by being monoidal takes to a monoid in `C`.
The permutation isomorphism appearing in the definition of
the multiplication on the tensor product of two monoids is
an instance of a more general family of isomorphisms
which together form a strength that equips the tensor product functor with a monoidal structure,
and the monoid axioms for the tensor product follow from the monoid axioms for the tensor factors
plus the properties of the strength (i.e., monoidal functor axioms).
The strength `tensorÎ¼` of the tensor product functor has been defined in
`Mathlib/CategoryTheory/Monoidal/Braided.lean`.
Its properties, stated as independent lemmas in that module,
are used extensively in the proofs below.
Notice that we could have followed the above plan not only conceptually
but also as a possible implementation and
could have constructed the tensor product of monoids via `mapMon`,
but we chose to give a more explicit definition directly in terms of `tensorÎ¼`.

To complete the definition of the monoidal category structure on the category of monoids,
we need to provide definitions of associator and unitors.
The obvious candidates are the associator and unitors from `C`,
but we need to prove that they are monoid morphisms, i.e., compatible with unit and multiplication.
These properties translate to the monoidality of the associator and unitors
(with respect to the monoidal structures on the functors they relate),
which have also been proved in `Mathlib/CategoryTheory/Monoidal/Braided.lean`.

-/

-- The proofs that associators and unitors preserve monoid units don't require braiding.
theorem one_associator {M N P : C} [MonObj M] [MonObj N] [MonObj P] :
    ((Î»_ (ğŸ™_ C)).inv â‰« ((Î»_ (ğŸ™_ C)).inv â‰« (Î·[M] âŠ—â‚˜ Î·[N]) âŠ—â‚˜ Î·[P])) â‰« (Î±_ M N P).hom =
      (Î»_ (ğŸ™_ C)).inv â‰« (Î·[M] âŠ—â‚˜ (Î»_ (ğŸ™_ C)).inv â‰« (Î·[N] âŠ—â‚˜ Î·[P])) := by
  simp only [Category.assoc, Iso.cancel_iso_inv_left]
  slice_lhs 1 3 => rw [â† Category.id_comp (Î· : ğŸ™_ C âŸ¶ P), â† tensorHom_comp_tensorHom]
  slice_lhs 2 3 => rw [associator_naturality]
  slice_rhs 1 2 => rw [â† Category.id_comp Î·, â† tensorHom_comp_tensorHom]
  slice_lhs 1 2 => rw [tensorHom_id, â† leftUnitor_tensor_inv]
  rw [â† cancel_epi (Î»_ (ğŸ™_ C)).inv]
  slice_lhs 1 2 => rw [leftUnitor_inv_naturality]
  simp

theorem one_leftUnitor {M : C} [MonObj M] :
    ((Î»_ (ğŸ™_ C)).inv â‰« (ğŸ™ (ğŸ™_ C) âŠ—â‚˜ Î·[M])) â‰« (Î»_ M).hom = Î· := by
  simp

theorem one_rightUnitor {M : C} [MonObj M] :
    ((Î»_ (ğŸ™_ C)).inv â‰« (Î·[M] âŠ—â‚˜ ğŸ™ (ğŸ™_ C))) â‰« (Ï_ M).hom = Î· := by
  simp [â† unitors_equal]

section BraidedCategory

variable [BraidedCategory C]

theorem Mon_tensor_one_mul (M N : C) [MonObj M] [MonObj N] :
    (((Î»_ (ğŸ™_ C)).inv â‰« (Î·[M] âŠ—â‚˜ Î·[N])) â–· (M âŠ— N)) â‰«
        tensorÎ¼ M N M N â‰« (Î¼ âŠ—â‚˜ Î¼) =
      (Î»_ (M âŠ— N)).hom := by
  simp only [comp_whiskerRight_assoc]
  slice_lhs 2 3 => rw [tensorÎ¼_natural_left]
  slice_lhs 3 4 => rw [tensorHom_comp_tensorHom, one_mul, one_mul]
  symm
  exact tensor_left_unitality M N

theorem Mon_tensor_mul_one (M N : C) [MonObj M] [MonObj N] :
    (M âŠ— N) â— ((Î»_ (ğŸ™_ C)).inv â‰« (Î·[M] âŠ—â‚˜ Î·[N])) â‰«
        tensorÎ¼ M N M N â‰« (Î¼[M] âŠ—â‚˜ Î¼[N]) =
      (Ï_ (M âŠ— N)).hom := by
  simp only [whiskerLeft_comp_assoc]
  slice_lhs 2 3 => rw [tensorÎ¼_natural_right]
  slice_lhs 3 4 => rw [tensorHom_comp_tensorHom, mul_one, mul_one]
  symm
  exact tensor_right_unitality M N

theorem Mon_tensor_mul_assoc (M N : C) [MonObj M] [MonObj N] :
    ((tensorÎ¼ M N M N â‰« (Î¼ âŠ—â‚˜ Î¼)) â–· (M âŠ— N)) â‰«
        tensorÎ¼ M N M N â‰« (Î¼ âŠ—â‚˜ Î¼) =
      (Î±_ (M âŠ— N : C) (M âŠ— N) (M âŠ— N)).hom â‰«
        ((M âŠ— N : C) â— (tensorÎ¼ M N M N â‰« (Î¼ âŠ—â‚˜ Î¼))) â‰«
          tensorÎ¼ M N M N â‰« (Î¼ âŠ—â‚˜ Î¼) := by
  simp only [comp_whiskerRight_assoc, whiskerLeft_comp_assoc]
  slice_lhs 2 3 => rw [tensorÎ¼_natural_left]
  slice_lhs 3 4 =>
    rw [tensorHom_comp_tensorHom, mul_assoc, mul_assoc, â† tensorHom_comp_tensorHom,
      â† tensorHom_comp_tensorHom]
  slice_lhs 1 3 => rw [tensor_associativity]
  slice_lhs 3 4 => rw [â† tensorÎ¼_natural_right]
  simp

theorem mul_associator {M N P : C} [MonObj M] [MonObj N] [MonObj P] :
    (tensorÎ¼ (M âŠ— N) P (M âŠ— N) P â‰«
          (tensorÎ¼ M N M N â‰« (Î¼ âŠ—â‚˜ Î¼) âŠ—â‚˜ Î¼)) â‰«
        (Î±_ M N P).hom =
      ((Î±_ M N P).hom âŠ—â‚˜ (Î±_ M N P).hom) â‰«
        tensorÎ¼ M (N âŠ— P) M (N âŠ— P) â‰«
          (Î¼ âŠ—â‚˜ tensorÎ¼ N P N P â‰« (Î¼ âŠ—â‚˜ Î¼)) := by
  simp only [Category.assoc]
  slice_lhs 2 3 => rw [â† Category.id_comp Î¼[P], â† tensorHom_comp_tensorHom]
  slice_lhs 3 4 => rw [associator_naturality]
  slice_rhs 3 4 => rw [â† Category.id_comp Î¼, â† tensorHom_comp_tensorHom]
  simp only [tensorHom_id, id_tensorHom]
  slice_lhs 1 3 => rw [associator_monoidal]
  simp only [Category.assoc]

theorem mul_leftUnitor {M : C} [MonObj M] :
    (tensorÎ¼ (ğŸ™_ C) M (ğŸ™_ C) M â‰« ((Î»_ (ğŸ™_ C)).hom âŠ—â‚˜ Î¼)) â‰« (Î»_ M).hom =
      ((Î»_ M).hom âŠ—â‚˜ (Î»_ M).hom) â‰« Î¼ := by
  rw [â† Category.comp_id (Î»_ (ğŸ™_ C)).hom, â† Category.id_comp Î¼, â† tensorHom_comp_tensorHom]
  simp only [tensorHom_id, id_tensorHom]
  slice_lhs 3 4 => rw [leftUnitor_naturality]
  slice_lhs 1 3 => rw [â† leftUnitor_monoidal]
  simp only [Category.id_comp]

theorem mul_rightUnitor {M : C} [MonObj M] :
    (tensorÎ¼ M (ğŸ™_ C) M (ğŸ™_ C) â‰« (Î¼ âŠ—â‚˜ (Î»_ (ğŸ™_ C)).hom)) â‰« (Ï_ M).hom =
      ((Ï_ M).hom âŠ—â‚˜ (Ï_ M).hom) â‰« Î¼ := by
  rw [â† Category.id_comp Î¼, â† Category.comp_id (Î»_ (ğŸ™_ C)).hom, â† tensorHom_comp_tensorHom]
  simp only [tensorHom_id, id_tensorHom]
  slice_lhs 3 4 => rw [rightUnitor_naturality]
  slice_lhs 1 3 => rw [â† rightUnitor_monoidal]
  simp only [Category.id_comp]

namespace tensorObj

-- We don't want `tensorObj.one_def` to be simp as it would loop with `IsMonHom.one_hom` applied
-- to `(Î»_ N.X).inv`.
@[simps -isSimp]
instance {M N : C} [MonObj M] [MonObj N] : MonObj (M âŠ— N) where
  one := (Î»_ (ğŸ™_ C)).inv â‰« (Î· âŠ—â‚˜ Î·)
  mul := tensorÎ¼ M N M N â‰« (Î¼ âŠ—â‚˜ Î¼)
  one_mul := Mon_tensor_one_mul M N
  mul_one := Mon_tensor_mul_one M N
  mul_assoc := Mon_tensor_mul_assoc M N

end tensorObj

open IsMonHom

variable {X Y Z W : C} [MonObj X] [MonObj Y] [MonObj Z] [MonObj W]

instance {f : X âŸ¶ Y} {g : Z âŸ¶ W} [IsMonHom f] [IsMonHom g] : IsMonHom (f âŠ—â‚˜ g) where
  one_hom := by
    dsimp [tensorObj.one_def]
    slice_lhs 2 3 => rw [tensorHom_comp_tensorHom, one_hom, one_hom]
  mul_hom := by
    dsimp [tensorObj.mul_def]
    slice_rhs 1 2 => rw [tensorÎ¼_natural]
    slice_lhs 2 3 => rw [tensorHom_comp_tensorHom, mul_hom, mul_hom, â† tensorHom_comp_tensorHom]
    simp only [Category.assoc]

instance : IsMonHom (ğŸ™ X) where

instance {f : Y âŸ¶ Z} [IsMonHom f] : IsMonHom (X â— f) where
  one_hom := by simpa using (inferInstanceAs <| IsMonHom (ğŸ™ X âŠ—â‚˜ f)).one_hom
  mul_hom := by simpa using (inferInstanceAs <| IsMonHom (ğŸ™ X âŠ—â‚˜ f)).mul_hom

instance {f : X âŸ¶ Y} [IsMonHom f] : IsMonHom (f â–· Z) where
  one_hom := by simpa using (inferInstanceAs <| IsMonHom (f âŠ—â‚˜ (ğŸ™ Z))).one_hom
  mul_hom := by simpa using (inferInstanceAs <| IsMonHom (f âŠ—â‚˜ (ğŸ™ Z))).mul_hom

instance : IsMonHom (Î±_ X Y Z).hom :=
  âŸ¨one_associator, mul_associatorâŸ©

instance : IsMonHom (Î»_ X).hom :=
  âŸ¨one_leftUnitor, mul_leftUnitorâŸ©

instance : IsMonHom (Ï_ X).hom :=
  âŸ¨one_rightUnitor, mul_rightUnitorâŸ©

theorem one_braiding (X Y : C) [MonObj X] [MonObj Y] : Î· â‰« (Î²_ X Y).hom = Î· := by
  simp only [tensorObj.one_def, Category.assoc, BraidedCategory.braiding_naturality,
    braiding_tensorUnit_right, Iso.cancel_iso_inv_left]
  monoidal

end BraidedCategory

end MonObj

namespace Mon

section BraidedCategory

variable [BraidedCategory C]

@[simps! tensorObj_X tensorHom_hom]
instance monMonoidalStruct : MonoidalCategoryStruct (Mon C) where
  tensorObj M N := âŸ¨M.X âŠ— N.XâŸ©
  tensorHom f g := Hom.mk (f.hom âŠ—â‚˜ g.hom)
  whiskerRight f Y := Hom.mk (f.hom â–· Y.X)
  whiskerLeft X _ _ g := Hom.mk (X.X â— g.hom)
  tensorUnit := âŸ¨ğŸ™_ CâŸ©
  associator M N P := mkIso' <| associator M.X N.X P.X
  leftUnitor M := mkIso' <| leftUnitor M.X
  rightUnitor M := mkIso' <| rightUnitor M.X

@[simp]
theorem tensorUnit_X : (ğŸ™_ (Mon C)).X = ğŸ™_ C := rfl

@[simp]
theorem tensorUnit_one : Î·[(ğŸ™_ (Mon C)).X] = ğŸ™ (ğŸ™_ C) := rfl

@[simp]
theorem tensorUnit_mul : Î¼[(ğŸ™_ (Mon C)).X] = (Î»_ (ğŸ™_ C)).hom := rfl

@[simp]
theorem tensorObj_one (X Y : Mon C) : Î·[(X âŠ— Y).X] = (Î»_ (ğŸ™_ C)).inv â‰« (Î·[X.X] âŠ—â‚˜ Î·[Y.X]) := rfl

@[simp]
theorem tensorObj_mul (X Y : Mon C) :
    Î¼[(X âŠ— Y).X] = tensorÎ¼ X.X Y.X X.X Y.X â‰« (Î¼[X.X] âŠ—â‚˜ Î¼[Y.X]) := rfl

@[simp]
theorem whiskerLeft_hom {X Y : Mon C} (f : X âŸ¶ Y) (Z : Mon C) :
    (f â–· Z).hom = f.hom â–· Z.X := by
  rfl

@[simp]
theorem whiskerRight_hom (X : Mon C) {Y Z : Mon C} (f : Y âŸ¶ Z) :
    (X â— f).hom = X.X â— f.hom := by
  rfl

@[simp]
theorem leftUnitor_hom_hom (X : Mon C) : (Î»_ X).hom.hom = (Î»_ X.X).hom := rfl

@[simp]
theorem leftUnitor_inv_hom (X : Mon C) : (Î»_ X).inv.hom = (Î»_ X.X).inv := rfl

@[simp]
theorem rightUnitor_hom_hom (X : Mon C) : (Ï_ X).hom.hom = (Ï_ X.X).hom := rfl

@[simp]
theorem rightUnitor_inv_hom (X : Mon C) : (Ï_ X).inv.hom = (Ï_ X.X).inv := rfl

@[simp]
theorem associator_hom_hom (X Y Z : Mon C) : (Î±_ X Y Z).hom.hom = (Î±_ X.X Y.X Z.X).hom := rfl

@[simp]
theorem associator_inv_hom (X Y Z : Mon C) : (Î±_ X Y Z).inv.hom = (Î±_ X.X Y.X Z.X).inv := rfl

@[simp]
theorem tensor_one (M N : Mon C) : Î·[(M âŠ— N).X] = (Î»_ (ğŸ™_ C)).inv â‰« (Î·[M.X] âŠ—â‚˜ Î·[N.X]) := rfl

@[simp]
theorem tensor_mul (M N : Mon C) : Î¼[(M âŠ— N).X] =
    tensorÎ¼ M.X N.X M.X N.X â‰« (Î¼[M.X] âŠ—â‚˜ Î¼[N.X]) := rfl

instance monMonoidal : MonoidalCategory (Mon C) where
  tensorHom_def := by intros; ext; simp [tensorHom_def]

-- We don't want `tensorObj.one_def` to be simp as it would loop with `IsMonHom.one_hom` applied
-- to `(Î»_ N.X).inv`.
@[simps! -isSimp]
instance {M N : C} [MonObj M] [MonObj N] : MonObj (M âŠ— N) :=
  inferInstanceAs <| MonObj (Mon.mk M âŠ— Mon.mk N).X

variable (C)

/-- The forgetful functor from `Mon C` to `C` is monoidal when `C` is monoidal. -/
instance : (forget C).Monoidal :=
  Functor.CoreMonoidal.toMonoidal
    { ÎµIso := Iso.refl _
      Î¼Iso _ _ := Iso.refl _ }

@[simp] theorem forget_Îµ : Îµ (forget C) = ğŸ™ (ğŸ™_ C) := rfl
@[simp] theorem forget_Î· : Â«Î·Â» (forget C) = ğŸ™ (ğŸ™_ C) := rfl
@[simp] theorem forget_Î¼ (X Y : Mon C) : Â«Î¼Â» (forget C) X Y = ğŸ™ (X.X âŠ— Y.X) := rfl
@[simp] theorem forget_Î´ (X Y : Mon C) : Î´ (forget C) X Y = ğŸ™ (X.X âŠ— Y.X) := rfl

end BraidedCategory

end Mon

/-!
We next show that if `C` is symmetric, then `Mon C` is braided, and indeed symmetric.

Note that `Mon C` is *not* braided in general when `C` is only braided.

The more interesting construction is the 2-category of monoids in `C`,
bimodules between the monoids, and intertwiners between the bimodules.

When `C` is braided, that is a monoidal 2-category.
-/
section SymmetricCategory

variable [SymmetricCategory C]

namespace MonObj

theorem mul_braiding (X Y : C) [MonObj X] [MonObj Y] :
    Î¼ â‰« (Î²_ X Y).hom = ((Î²_ X Y).hom âŠ—â‚˜ (Î²_ X Y).hom) â‰« Î¼ := by
  dsimp [tensorObj.mul_def]
  simp only [tensorÎ¼, Category.assoc, BraidedCategory.braiding_naturality,
    BraidedCategory.braiding_tensor_right_hom, BraidedCategory.braiding_tensor_left_hom,
    comp_whiskerRight, whisker_assoc, whiskerLeft_comp, pentagon_assoc,
    pentagon_inv_hom_hom_hom_inv_assoc, Iso.inv_hom_id_assoc, whiskerLeft_hom_inv_assoc]
  slice_lhs 3 4 =>
    -- We use symmetry here:
    rw [â† whiskerLeft_comp, â† comp_whiskerRight, SymmetricCategory.symmetry]
  simp only [id_whiskerRight, whiskerLeft_id, Category.id_comp, Category.assoc, pentagon_inv_assoc,
    Iso.hom_inv_id_assoc]
  slice_lhs 1 2 =>
    rw [â† associator_inv_naturality_left]
  slice_lhs 2 3 =>
    rw [Iso.inv_hom_id]
  rw [Category.id_comp]
  slice_lhs 2 3 =>
    rw [â† associator_naturality_right]
  slice_lhs 1 2 =>
    rw [â† tensorHom_def]
  simp only [Category.assoc]

instance {X Y : C} [MonObj X] [MonObj Y] : IsMonHom (Î²_ X Y).hom :=
  âŸ¨one_braiding X Y, mul_braiding X YâŸ©

end MonObj

namespace Mon

instance : SymmetricCategory (Mon C) where
  braiding X Y := mkIso' (Î²_ X.X Y.X)
  symmetry X Y := by
    ext
    simp

@[simp] lemma braiding_hom_hom (M N : Mon C) : (Î²_ M N).hom.hom = (Î²_ M.X N.X).hom := rfl
@[simp] lemma braiding_inv_hom (M N : Mon C) : (Î²_ M N).inv.hom = (Î²_ M.X N.X).inv := rfl

end Mon

end SymmetricCategory

section

variable [BraidedCategory.{vâ‚} C]

/-- Predicate for a monoid object to be commutative. -/
class IsCommMonObj (X : C) [MonObj X] where
  mul_comm (X) : (Î²_ X X).hom â‰« Î¼ = Î¼ := by cat_disch

@[deprecated (since := "2025-09-14")] alias IsCommMon := IsCommMonObj

open scoped MonObj

namespace IsCommMonObj

attribute [reassoc (attr := simp, mon_tauto)] mul_comm

variable (M) in
@[reassoc (attr := simp, mon_tauto)]
lemma mul_comm' [IsCommMonObj M] : (Î²_ M M).inv â‰« Î¼ = Î¼ := by simp [â† cancel_epi (Î²_ M M).hom]

instance : IsCommMonObj (ğŸ™_ C) where
  mul_comm := by dsimp; rw [braiding_leftUnitor, unitors_equal]

end IsCommMonObj

variable (M) in
@[reassoc (attr := simp)]
lemma MonObj.mul_mul_mul_comm [IsCommMonObj M] :
    tensorÎ¼ M M M M â‰« (Î¼ âŠ—â‚˜ Î¼) â‰« Î¼ = (Î¼ âŠ—â‚˜ Î¼) â‰« Î¼ := by simp only [mon_tauto]

@[deprecated (since := "2025-09-09")] alias Mon_Class.mul_mul_mul_comm := MonObj.mul_mul_mul_comm

variable (M) in
@[reassoc (attr := simp)]
lemma MonObj.mul_mul_mul_comm' [IsCommMonObj M] :
    tensorÎ´ M M M M â‰« (Î¼ âŠ—â‚˜ Î¼) â‰« Î¼ = (Î¼ âŠ—â‚˜ Î¼) â‰« Î¼ := by simp only [mon_tauto]

@[deprecated (since := "2025-09-09")] alias Mon_Class.mul_mul_mul_comm' := MonObj.mul_mul_mul_comm'

end

section SymmetricCategory
variable [SymmetricCategory C] {M N W X Y Z : C} [MonObj M] [MonObj N]

instance [IsCommMonObj M] [IsCommMonObj N] : IsCommMonObj (M âŠ— N) where
  mul_comm := by
    simp [â† IsIso.inv_comp_eq, tensorÎ¼, â† associator_inv_naturality_left_assoc,
      â† associator_naturality_right_assoc, SymmetricCategory.braiding_swap_eq_inv_braiding M N,
      â† tensorHom_def_assoc, -whiskerRight_tensor, -tensor_whiskerLeft,
      MonObj.tensorObj.mul_def, â† whiskerLeft_comp_assoc, -whiskerLeft_comp]

end SymmetricCategory

/-!
Projects:
* Check that `Mon MonCat â‰Œ CommMonCat`, via the Eckmann-Hilton argument.
  (You'll have to hook up the Cartesian monoidal structure on `MonCat` first,
  available in https://github.com/leanprover-community/mathlib3/pull/3463)
* More generally, check that `Mon (Mon C) â‰Œ CommMon C` when `C` is braided.
* Check that `Mon TopCat â‰Œ [bundled topological monoids]`.
* Check that `Mon AddCommGrp â‰Œ RingCat`.
  (We've already got `Mon (ModuleCat R) â‰Œ AlgCat R`,
  in `Mathlib/CategoryTheory/Monoidal/Internal/Module.lean`.)
* Can you transport this monoidal structure to `RingCat` or `AlgCat R`?
  How does it compare to the "native" one?
* Show that when `F` is a lax braided functor `C â¥¤ D`, the functor `map_Mon F : Mon C â¥¤ Mon D`
  is lax monoidal.
-/
