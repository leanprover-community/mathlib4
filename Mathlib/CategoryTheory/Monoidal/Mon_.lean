/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Monoidal.Braided.Basic
import Mathlib.CategoryTheory.Monoidal.Discrete
import Mathlib.CategoryTheory.Monoidal.CoherenceLemmas
import Mathlib.CategoryTheory.Limits.Shapes.Terminal
import Mathlib.Algebra.PUnitInstances

#align_import category_theory.monoidal.Mon_ from "leanprover-community/mathlib"@"a836c6dba9bd1ee2a0cdc9af0006a596f243031c"

/-!
# The category of monoids in a monoidal category.

We define monoids in a monoidal category `C` and show that the category of monoids is equivalent to
the category of lax monoidal functors from the unit monoidal category to `C`.  We also show that if
`C` is braided, then the category of monoids is naturally monoidal.

-/

set_option linter.uppercaseLean3 false

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ u

open CategoryTheory MonoidalCategory

variable (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] [MonoidalCategory.{v‚ÇÅ} C]

/-- A monoid object internal to a monoidal category.

When the monoidal category is preadditive, this is also sometimes called an "algebra object".
-/
structure Mon_ where
  X : C
  one : ùüô_ C ‚ü∂ X
  mul : X ‚äó X ‚ü∂ X
  one_mul : (one ‚ñ∑ X) ‚â´ mul = (Œª_ X).hom := by aesop_cat
  mul_one : (X ‚óÅ one) ‚â´ mul = (œÅ_ X).hom := by aesop_cat
  -- Obviously there is some flexibility stating this axiom.
  -- This one has left- and right-hand sides matching the statement of `Monoid.mul_assoc`,
  -- and chooses to place the associator on the right-hand side.
  -- The heuristic is that unitors and associators "don't have much weight".
  mul_assoc : (mul ‚ñ∑ X) ‚â´ mul = (Œ±_ X X X).hom ‚â´ (X ‚óÅ mul) ‚â´ mul := by aesop_cat
#align Mon_ Mon_

attribute [reassoc] Mon_.one_mul Mon_.mul_one

attribute [simp] Mon_.one_mul Mon_.mul_one

-- We prove a more general `@[simp]` lemma below.
attribute [reassoc (attr := simp)] Mon_.mul_assoc

namespace Mon_

/-- The trivial monoid object. We later show this is initial in `Mon_ C`.
-/
@[simps]
def trivial : Mon_ C where
  X := ùüô_ C
  one := ùüô _
  mul := (Œª_ _).hom
  mul_assoc := by coherence
  mul_one := by coherence
#align Mon_.trivial Mon_.trivial

instance : Inhabited (Mon_ C) :=
  ‚ü®trivial C‚ü©

variable {C}
variable {M : Mon_ C}

@[simp]
theorem one_mul_hom {Z : C} (f : Z ‚ü∂ M.X) : (M.one ‚äó f) ‚â´ M.mul = (Œª_ Z).hom ‚â´ f := by
  rw [tensorHom_def'_assoc, M.one_mul, leftUnitor_naturality]
#align Mon_.one_mul_hom Mon_.one_mul_hom

@[simp]
theorem mul_one_hom {Z : C} (f : Z ‚ü∂ M.X) : (f ‚äó M.one) ‚â´ M.mul = (œÅ_ Z).hom ‚â´ f := by
  rw [tensorHom_def_assoc, M.mul_one, rightUnitor_naturality]
#align Mon_.mul_one_hom Mon_.mul_one_hom

theorem assoc_flip :
    (M.X ‚óÅ M.mul) ‚â´ M.mul = (Œ±_ M.X M.X M.X).inv ‚â´ (M.mul ‚ñ∑ M.X) ‚â´ M.mul := by simp
#align Mon_.assoc_flip Mon_.assoc_flip

/-- A morphism of monoid objects. -/
@[ext]
structure Hom (M N : Mon_ C) where
  hom : M.X ‚ü∂ N.X
  one_hom : M.one ‚â´ hom = N.one := by aesop_cat
  mul_hom : M.mul ‚â´ hom = (hom ‚äó hom) ‚â´ N.mul := by aesop_cat
#align Mon_.hom Mon_.Hom

attribute [reassoc (attr := simp)] Hom.one_hom Hom.mul_hom

/-- The identity morphism on a monoid object. -/
@[simps]
def id (M : Mon_ C) : Hom M M where
  hom := ùüô M.X
#align Mon_.id Mon_.id

instance homInhabited (M : Mon_ C) : Inhabited (Hom M M) :=
  ‚ü®id M‚ü©
#align Mon_.hom_inhabited Mon_.homInhabited

/-- Composition of morphisms of monoid objects. -/
@[simps]
def comp {M N O : Mon_ C} (f : Hom M N) (g : Hom N O) : Hom M O where
  hom := f.hom ‚â´ g.hom
#align Mon_.comp Mon_.comp

instance : Category (Mon_ C) where
  Hom M N := Hom M N
  id := id
  comp f g := comp f g

-- Porting note: added, as `Hom.ext` does not apply to a morphism.
@[ext]
lemma ext {X Y : Mon_ C} {f g : X ‚ü∂ Y} (w : f.hom = g.hom) : f = g :=
  Hom.ext _ _ w

@[simp]
theorem id_hom' (M : Mon_ C) : (ùüô M : Hom M M).hom = ùüô M.X :=
  rfl
#align Mon_.id_hom' Mon_.id_hom'

@[simp]
theorem comp_hom' {M N K : Mon_ C} (f : M ‚ü∂ N) (g : N ‚ü∂ K) :
    (f ‚â´ g : Hom M K).hom = f.hom ‚â´ g.hom :=
  rfl
#align Mon_.comp_hom' Mon_.comp_hom'

section

variable (C)

/-- The forgetful functor from monoid objects to the ambient category. -/
@[simps]
def forget : Mon_ C ‚•§ C where
  obj A := A.X
  map f := f.hom
#align Mon_.forget Mon_.forget

end

instance forget_faithful : (forget C).Faithful where
#align Mon_.forget_faithful Mon_.forget_faithful

instance {A B : Mon_ C} (f : A ‚ü∂ B) [e : IsIso ((forget C).map f)] : IsIso f.hom :=
  e

/-- The forgetful functor from monoid objects to the ambient category reflects isomorphisms. -/
instance : (forget C).ReflectsIsomorphisms where
  reflects f e :=
    ‚ü®‚ü®{ hom := inv f.hom
        mul_hom := by
          simp only [IsIso.comp_inv_eq, Hom.mul_hom, Category.assoc, ‚Üê tensor_comp_assoc,
            IsIso.inv_hom_id, tensor_id, Category.id_comp] },
        by aesop_cat‚ü©‚ü©

/-- Construct an isomorphism of monoids by giving an isomorphism between the underlying objects
and checking compatibility with unit and multiplication only in the forward direction.
-/
@[simps]
def mkIso {M N : Mon_ C} (f : M.X ‚âÖ N.X) (one_f : M.one ‚â´ f.hom = N.one := by aesop_cat)
    (mul_f : M.mul ‚â´ f.hom = (f.hom ‚äó f.hom) ‚â´ N.mul := by aesop_cat) : M ‚âÖ N where
  hom :=
    { hom := f.hom
      one_hom := one_f
      mul_hom := mul_f }
  inv :=
    { hom := f.inv
      one_hom := by rw [‚Üê one_f]; simp
      mul_hom := by
        rw [‚Üê cancel_mono f.hom]
        slice_rhs 2 3 => rw [mul_f]
        simp }
#align Mon_.iso_of_iso Mon_.mkIso

instance uniqueHomFromTrivial (A : Mon_ C) : Unique (trivial C ‚ü∂ A) where
  default :=
    { hom := A.one
      one_hom := by dsimp; simp
      mul_hom := by dsimp; simp [A.one_mul, unitors_equal] }
  uniq f := by
    ext; simp
    rw [‚Üê Category.id_comp f.hom]
    erw [f.one_hom]
#align Mon_.unique_hom_from_trivial Mon_.uniqueHomFromTrivial

open CategoryTheory.Limits

instance : HasInitial (Mon_ C) :=
  hasInitial_of_unique (trivial C)

end Mon_

namespace CategoryTheory.LaxMonoidalFunctor

variable {C} {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] [MonoidalCategory.{v‚ÇÇ} D]

-- TODO: mapMod F A : Mod A ‚•§ Mod (F.mapMon A)
/-- A lax monoidal functor takes monoid objects to monoid objects.

That is, a lax monoidal functor `F : C ‚•§ D` induces a functor `Mon_ C ‚•§ Mon_ D`.
-/
@[simps]
def mapMon (F : LaxMonoidalFunctor C D) : Mon_ C ‚•§ Mon_ D where
  obj A :=
    { X := F.obj A.X
      one := F.Œµ ‚â´ F.map A.one
      mul := F.Œº _ _ ‚â´ F.map A.mul
      one_mul := by
        simp_rw [comp_whiskerRight, Category.assoc, Œº_natural_left_assoc, left_unitality]
        slice_lhs 3 4 => rw [‚Üê F.toFunctor.map_comp, A.one_mul]
      mul_one := by
        simp_rw [MonoidalCategory.whiskerLeft_comp, Category.assoc, Œº_natural_right_assoc,
          right_unitality]
        slice_lhs 3 4 => rw [‚Üê F.toFunctor.map_comp, A.mul_one]
      mul_assoc := by
        simp_rw [comp_whiskerRight, Category.assoc, Œº_natural_left_assoc,
          MonoidalCategory.whiskerLeft_comp, Category.assoc, Œº_natural_right_assoc]
        slice_lhs 3 4 => rw [‚Üê F.toFunctor.map_comp, A.mul_assoc]
        simp }
  map f :=
    { hom := F.map f.hom
      one_hom := by dsimp; rw [Category.assoc, ‚Üê F.toFunctor.map_comp, f.one_hom]
      mul_hom := by
        dsimp
        rw [Category.assoc, F.Œº_natural_assoc, ‚Üê F.toFunctor.map_comp, ‚Üê F.toFunctor.map_comp,
          f.mul_hom] }
  map_id A := by ext; simp
  map_comp f g := by ext; simp
#align category_theory.lax_monoidal_functor.map_Mon CategoryTheory.LaxMonoidalFunctor.mapMon

variable (C D)

/-- `mapMon` is functorial in the lax monoidal functor. -/
@[simps] -- Porting note: added this, not sure how it worked previously without.
def mapMonFunctor : LaxMonoidalFunctor C D ‚•§ Mon_ C ‚•§ Mon_ D where
  obj := mapMon
  map Œ± := { app := fun A => { hom := Œ±.app A.X } }
#align category_theory.lax_monoidal_functor.map_Mon_functor CategoryTheory.LaxMonoidalFunctor.mapMonFunctor

end CategoryTheory.LaxMonoidalFunctor

namespace Mon_

open CategoryTheory.LaxMonoidalFunctor

namespace EquivLaxMonoidalFunctorPUnit

/-- Implementation of `Mon_.equivLaxMonoidalFunctorPUnit`. -/
@[simps]
def laxMonoidalToMon : LaxMonoidalFunctor (Discrete PUnit.{u + 1}) C ‚•§ Mon_ C where
  obj F := (F.mapMon : Mon_ _ ‚•§ Mon_ C).obj (trivial (Discrete PUnit))
  map Œ± := ((mapMonFunctor (Discrete PUnit) C).map Œ±).app _
#align Mon_.equiv_lax_monoidal_functor_punit.lax_monoidal_to_Mon Mon_.EquivLaxMonoidalFunctorPUnit.laxMonoidalToMon

/-- Implementation of `Mon_.equivLaxMonoidalFunctorPUnit`. -/
@[simps]
def monToLaxMonoidal : Mon_ C ‚•§ LaxMonoidalFunctor (Discrete PUnit.{u + 1}) C where
  obj A :=
    { obj := fun _ => A.X
      map := fun _ => ùüô _
      Œµ := A.one
      Œº := fun _ _ => A.mul
      map_id := fun _ => rfl
      map_comp := fun _ _ => (Category.id_comp (ùüô A.X)).symm }
  map f :=
    { app := fun _ => f.hom
      naturality := fun _ _ _ => by dsimp; rw [Category.id_comp, Category.comp_id]
      unit := f.one_hom
      tensor := fun _ _ => f.mul_hom }
#align Mon_.equiv_lax_monoidal_functor_punit.Mon_to_lax_monoidal Mon_.EquivLaxMonoidalFunctorPUnit.monToLaxMonoidal

attribute [local aesop safe tactic (rule_sets := [CategoryTheory])]
  CategoryTheory.Discrete.discreteCases

attribute [local simp] eqToIso_map

/-- Implementation of `Mon_.equivLaxMonoidalFunctorPUnit`. -/
@[simps!]
def unitIso :
    ùü≠ (LaxMonoidalFunctor (Discrete PUnit.{u + 1}) C) ‚âÖ laxMonoidalToMon C ‚ãô monToLaxMonoidal C :=
  NatIso.ofComponents
    (fun F =>
      MonoidalNatIso.ofComponents (fun _ => F.toFunctor.mapIso (eqToIso (by ext))) (by aesop_cat)
        (by aesop_cat) (by aesop_cat))
    (by aesop_cat)
#align Mon_.equiv_lax_monoidal_functor_punit.unit_iso Mon_.EquivLaxMonoidalFunctorPUnit.unitIso

/-- Implementation of `Mon_.equivLaxMonoidalFunctorPUnit`. -/
@[simps!]
def counitIso : monToLaxMonoidal C ‚ãô laxMonoidalToMon C ‚âÖ ùü≠ (Mon_ C) :=
  NatIso.ofComponents
    (fun F =>
      { hom := { hom := ùüô _ }
        inv := { hom := ùüô _ } })
    (by aesop_cat)
#align Mon_.equiv_lax_monoidal_functor_punit.counit_iso Mon_.EquivLaxMonoidalFunctorPUnit.counitIso

end EquivLaxMonoidalFunctorPUnit

open EquivLaxMonoidalFunctorPUnit

attribute [local simp] eqToIso_map

/--
Monoid objects in `C` are "just" lax monoidal functors from the trivial monoidal category to `C`.
-/
@[simps]
def equivLaxMonoidalFunctorPUnit : LaxMonoidalFunctor (Discrete PUnit.{u + 1}) C ‚âå Mon_ C where
  functor := laxMonoidalToMon C
  inverse := monToLaxMonoidal C
  unitIso := unitIso C
  counitIso := counitIso C
#align Mon_.equiv_lax_monoidal_functor_punit Mon_.equivLaxMonoidalFunctorPUnit

end Mon_

namespace Mon_

/-!
In this section, we prove that the category of monoids in a braided monoidal category is monoidal.

Given two monoids `M` and `N` in a braided monoidal category `C`,
the multiplication on the tensor product `M.X ‚äó N.X` is defined in the obvious way:
it is the tensor product of the multiplications on `M` and `N`,
except that the tensor factors in the source come in the wrong order,
which we fix by pre-composing with a permutation isomorphism constructed from the braiding.

(There is a subtlety here: in fact there are two ways to do these,
using either the positive or negative crossing.)

A more conceptual way of understanding this definition is the following:
The braiding on `C` gives rise to a monoidal structure on
the tensor product functor from `C √ó C` to `C`.
A pair of monoids in `C` gives rise to a monoid in `C √ó C`,
which the tensor product functor by being monoidal takes to a monoid in `C`.
The permutation isomorphism appearing in the definition of
the multiplication on the tensor product of two monoids is
an instance of a more general family of isomorphisms
which together form a strength that equips the tensor product functor with a monoidal structure,
and the monoid axioms for the tensor product follow from the monoid axioms for the tensor factors
plus the properties of the strength (i.e., monoidal functor axioms).
The strength `tensor_Œº` of the tensor product functor has been defined in
`Mathlib.CategoryTheory.Monoidal.Braided`.
Its properties, stated as independent lemmas in that module,
are used extensively in the proofs below.
Notice that we could have followed the above plan not only conceptually
but also as a possible implementation and
could have constructed the tensor product of monoids via `mapMon`,
but we chose to give a more explicit definition directly in terms of `tensor_Œº`.

To complete the definition of the monoidal category structure on the category of monoids,
we need to provide definitions of associator and unitors.
The obvious candidates are the associator and unitors from `C`,
but we need to prove that they are monoid morphisms, i.e., compatible with unit and multiplication.
These properties translate to the monoidality of the associator and unitors
(with respect to the monoidal structures on the functors they relate),
which have also been proved in `Mathlib.CategoryTheory.Monoidal.Braided`.

-/


variable {C}

-- The proofs that associators and unitors preserve monoid units don't require braiding.
theorem one_associator {M N P : Mon_ C} :
    ((Œª_ (ùüô_ C)).inv ‚â´ ((Œª_ (ùüô_ C)).inv ‚â´ (M.one ‚äó N.one) ‚äó P.one)) ‚â´ (Œ±_ M.X N.X P.X).hom =
      (Œª_ (ùüô_ C)).inv ‚â´ (M.one ‚äó (Œª_ (ùüô_ C)).inv ‚â´ (N.one ‚äó P.one)) := by
  simp only [Category.assoc, Iso.cancel_iso_inv_left]
  slice_lhs 1 3 => rw [‚Üê Category.id_comp P.one, tensor_comp]
  slice_lhs 2 3 => rw [associator_naturality]
  slice_rhs 1 2 => rw [‚Üê Category.id_comp M.one, tensor_comp]
  slice_lhs 1 2 => rw [tensorHom_id, ‚Üê leftUnitor_tensor_inv]
  rw [‚Üê cancel_epi (Œª_ (ùüô_ C)).inv]
  slice_lhs 1 2 => rw [leftUnitor_inv_naturality]
  simp
#align Mon_.one_associator Mon_.one_associator

theorem one_leftUnitor {M : Mon_ C} :
    ((Œª_ (ùüô_ C)).inv ‚â´ (ùüô (ùüô_ C) ‚äó M.one)) ‚â´ (Œª_ M.X).hom = M.one := by
  simp
#align Mon_.one_left_unitor Mon_.one_leftUnitor

theorem one_rightUnitor {M : Mon_ C} :
    ((Œª_ (ùüô_ C)).inv ‚â´ (M.one ‚äó ùüô (ùüô_ C))) ‚â´ (œÅ_ M.X).hom = M.one := by
  simp [‚Üê unitors_equal]
#align Mon_.one_right_unitor Mon_.one_rightUnitor

section BraidedCategory

variable [BraidedCategory C]

theorem Mon_tensor_one_mul (M N : Mon_ C) :
    (((Œª_ (ùüô_ C)).inv ‚â´ (M.one ‚äó N.one)) ‚ñ∑ (M.X ‚äó N.X)) ‚â´
        tensor_Œº C (M.X, N.X) (M.X, N.X) ‚â´ (M.mul ‚äó N.mul) =
      (Œª_ (M.X ‚äó N.X)).hom := by
  simp only [comp_whiskerRight_assoc]
  slice_lhs 2 3 => rw [tensor_Œº_natural_left]
  slice_lhs 3 4 => rw [‚Üê tensor_comp, one_mul M, one_mul N]
  symm
  exact tensor_left_unitality C M.X N.X
#align Mon_.Mon_tensor_one_mul Mon_.Mon_tensor_one_mul

theorem Mon_tensor_mul_one (M N : Mon_ C) :
    (M.X ‚äó N.X) ‚óÅ ((Œª_ (ùüô_ C)).inv ‚â´ (M.one ‚äó N.one)) ‚â´
        tensor_Œº C (M.X, N.X) (M.X, N.X) ‚â´ (M.mul ‚äó N.mul) =
      (œÅ_ (M.X ‚äó N.X)).hom := by
  simp only [MonoidalCategory.whiskerLeft_comp_assoc]
  slice_lhs 2 3 => rw [tensor_Œº_natural_right]
  slice_lhs 3 4 => rw [‚Üê tensor_comp, mul_one M, mul_one N]
  symm
  exact tensor_right_unitality C M.X N.X
#align Mon_.Mon_tensor_mul_one Mon_.Mon_tensor_mul_one

theorem Mon_tensor_mul_assoc (M N : Mon_ C) :
    ((tensor_Œº C (M.X, N.X) (M.X, N.X) ‚â´ (M.mul ‚äó N.mul)) ‚ñ∑ (M.X ‚äó N.X)) ‚â´
        tensor_Œº C (M.X, N.X) (M.X, N.X) ‚â´ (M.mul ‚äó N.mul) =
      (Œ±_ (M.X ‚äó N.X) (M.X ‚äó N.X) (M.X ‚äó N.X)).hom ‚â´
        ((M.X ‚äó N.X) ‚óÅ (tensor_Œº C (M.X, N.X) (M.X, N.X) ‚â´ (M.mul ‚äó N.mul))) ‚â´
          tensor_Œº C (M.X, N.X) (M.X, N.X) ‚â´ (M.mul ‚äó N.mul) := by
  simp only [comp_whiskerRight_assoc, MonoidalCategory.whiskerLeft_comp_assoc]
  slice_lhs 2 3 => rw [tensor_Œº_natural_left]
  slice_lhs 3 4 => rw [‚Üê tensor_comp, mul_assoc M, mul_assoc N, tensor_comp, tensor_comp]
  slice_lhs 1 3 => rw [tensor_associativity]
  slice_lhs 3 4 => rw [‚Üê tensor_Œº_natural_right]
  simp
#align Mon_.Mon_tensor_mul_assoc Mon_.Mon_tensor_mul_assoc

theorem mul_associator {M N P : Mon_ C} :
    (tensor_Œº C (M.X ‚äó N.X, P.X) (M.X ‚äó N.X, P.X) ‚â´
          (tensor_Œº C (M.X, N.X) (M.X, N.X) ‚â´ (M.mul ‚äó N.mul) ‚äó P.mul)) ‚â´
        (Œ±_ M.X N.X P.X).hom =
      ((Œ±_ M.X N.X P.X).hom ‚äó (Œ±_ M.X N.X P.X).hom) ‚â´
        tensor_Œº C (M.X, N.X ‚äó P.X) (M.X, N.X ‚äó P.X) ‚â´
          (M.mul ‚äó tensor_Œº C (N.X, P.X) (N.X, P.X) ‚â´ (N.mul ‚äó P.mul)) := by
  simp only [tensor_obj, prodMonoidal_tensorObj, Category.assoc]
  slice_lhs 2 3 => rw [‚Üê Category.id_comp P.mul, tensor_comp]
  slice_lhs 3 4 => rw [associator_naturality]
  slice_rhs 3 4 => rw [‚Üê Category.id_comp M.mul, tensor_comp]
  simp only [tensorHom_id, id_tensorHom]
  slice_lhs 1 3 => rw [associator_monoidal]
  simp only [Category.assoc]
#align Mon_.mul_associator Mon_.mul_associator

theorem mul_leftUnitor {M : Mon_ C} :
    (tensor_Œº C (ùüô_ C, M.X) (ùüô_ C, M.X) ‚â´ ((Œª_ (ùüô_ C)).hom ‚äó M.mul)) ‚â´ (Œª_ M.X).hom =
      ((Œª_ M.X).hom ‚äó (Œª_ M.X).hom) ‚â´ M.mul := by
  rw [‚Üê Category.comp_id (Œª_ (ùüô_ C)).hom, ‚Üê Category.id_comp M.mul, tensor_comp]
  simp only [tensorHom_id, id_tensorHom]
  slice_lhs 3 4 => rw [leftUnitor_naturality]
  slice_lhs 1 3 => rw [‚Üê leftUnitor_monoidal]
  simp only [Category.assoc, Category.id_comp]
#align Mon_.mul_left_unitor Mon_.mul_leftUnitor

theorem mul_rightUnitor {M : Mon_ C} :
    (tensor_Œº C (M.X, ùüô_ C) (M.X, ùüô_ C) ‚â´ (M.mul ‚äó (Œª_ (ùüô_ C)).hom)) ‚â´ (œÅ_ M.X).hom =
      ((œÅ_ M.X).hom ‚äó (œÅ_ M.X).hom) ‚â´ M.mul := by
  rw [‚Üê Category.id_comp M.mul, ‚Üê Category.comp_id (Œª_ (ùüô_ C)).hom, tensor_comp]
  simp only [tensorHom_id, id_tensorHom]
  slice_lhs 3 4 => rw [rightUnitor_naturality]
  slice_lhs 1 3 => rw [‚Üê rightUnitor_monoidal]
  simp only [Category.assoc, Category.id_comp]
#align Mon_.mul_right_unitor Mon_.mul_rightUnitor

@[simps tensorObj_X tensorHom_hom]
instance monMonoidalStruct : MonoidalCategoryStruct (Mon_ C) :=
  let tensorObj (M N : Mon_ C) : Mon_ C :=
    { X := M.X ‚äó N.X
      one := (Œª_ (ùüô_ C)).inv ‚â´ (M.one ‚äó N.one)
      mul := tensor_Œº C (M.X, N.X) (M.X, N.X) ‚â´ (M.mul ‚äó N.mul)
      one_mul := Mon_tensor_one_mul M N
      mul_one := Mon_tensor_mul_one M N
      mul_assoc := Mon_tensor_mul_assoc M N }
  let tensorHom {X‚ÇÅ Y‚ÇÅ X‚ÇÇ Y‚ÇÇ : Mon_ C} (f : X‚ÇÅ ‚ü∂ Y‚ÇÅ) (g : X‚ÇÇ ‚ü∂ Y‚ÇÇ) :
      tensorObj _ _ ‚ü∂ tensorObj _ _ :=
    { hom := f.hom ‚äó g.hom
      one_hom := by
        dsimp
        slice_lhs 2 3 => rw [‚Üê tensor_comp, Hom.one_hom f, Hom.one_hom g]
      mul_hom := by
        dsimp
        slice_rhs 1 2 => rw [tensor_Œº_natural]
        slice_lhs 2 3 => rw [‚Üê tensor_comp, Hom.mul_hom f, Hom.mul_hom g, tensor_comp]
        simp only [Category.assoc] }
  { tensorObj := tensorObj
    tensorHom := tensorHom
    whiskerRight := fun f Y => tensorHom f (ùüô Y)
    whiskerLeft := fun X _ _ g => tensorHom (ùüô X) g
    tensorUnit := trivial C
    associator := fun M N P ‚Ü¶ mkIso (Œ±_ M.X N.X P.X) one_associator mul_associator
    leftUnitor := fun M ‚Ü¶ mkIso (Œª_ M.X) one_leftUnitor mul_leftUnitor
    rightUnitor := fun M ‚Ü¶ mkIso (œÅ_ M.X) one_rightUnitor mul_rightUnitor }

@[simp]
theorem tensorUnit_X : (ùüô_ (Mon_ C)).X = ùüô_ C := rfl

@[simp]
theorem tensorUnit_one : (ùüô_ (Mon_ C)).one = ùüô (ùüô_ C) := rfl

@[simp]
theorem tensorUnit_mul : (ùüô_ (Mon_ C)).mul = (Œª_ (ùüô_ C)).hom := rfl

@[simp]
theorem tensorObj_one (X Y : Mon_ C) : (X ‚äó Y).one = (Œª_ (ùüô_ C)).inv ‚â´ (X.one ‚äó Y.one) := rfl

@[simp]
theorem tensorObj_mul (X Y : Mon_ C) :
    (X ‚äó Y).mul = tensor_Œº C (X.X, Y.X) (X.X, Y.X) ‚â´ (X.mul ‚äó Y.mul) := rfl

@[simp]
theorem whiskerLeft_hom {X Y : Mon_ C} (f : X ‚ü∂ Y) (Z : Mon_ C) :
    (f ‚ñ∑ Z).hom = f.hom ‚ñ∑ Z.X := by
  rw [‚Üê tensorHom_id]; rfl

@[simp]
theorem whiskerRight_hom (X : Mon_ C) {Y Z : Mon_ C} (f : Y ‚ü∂ Z) :
    (X ‚óÅ f).hom = X.X ‚óÅ f.hom := by
  rw [‚Üê id_tensorHom]; rfl

@[simp]
theorem leftUnitor_hom_hom (X : Mon_ C) : (Œª_ X).hom.hom = (Œª_ X.X).hom := rfl

@[simp]
theorem leftUnitor_inv_hom (X : Mon_ C) : (Œª_ X).inv.hom = (Œª_ X.X).inv := rfl

@[simp]
theorem rightUnitor_hom_hom (X : Mon_ C) : (œÅ_ X).hom.hom = (œÅ_ X.X).hom := rfl

@[simp]
theorem rightUnitor_inv_hom (X : Mon_ C) : (œÅ_ X).inv.hom = (œÅ_ X.X).inv := rfl

@[simp]
theorem associator_hom_hom (X Y Z : Mon_ C) : (Œ±_ X Y Z).hom.hom = (Œ±_ X.X Y.X Z.X).hom := rfl

@[simp]
theorem associator_inv_hom (X Y Z : Mon_ C) : (Œ±_ X Y Z).inv.hom = (Œ±_ X.X Y.X Z.X).inv := rfl

@[simp]
theorem tensor_one (M N : Mon_ C) : (M ‚äó N).one = (Œª_ (ùüô_ C)).inv ‚â´ (M.one ‚äó N.one) := rfl

@[simp]
theorem tensor_mul (M N : Mon_ C) : (M ‚äó N).mul =
    tensor_Œº C (M.X, N.X) (M.X, N.X) ‚â´ (M.mul ‚äó N.mul) := rfl

instance monMonoidal : MonoidalCategory (Mon_ C) where
  tensorHom_def := by intros; ext; simp [tensorHom_def]
#align Mon_.Mon_monoidal Mon_.monMonoidal

variable (C)

/-- The forgetful functor from `Mon_ C` to `C` is monoidal when `C` is braided monoidal. -/
def forgetMonoidal : MonoidalFunctor (Mon_ C) C :=
  { forget C with
    Œµ := ùüô _
    Œº := fun X Y => ùüô _ }

@[simp] theorem forgetMonoidal_toFunctor : (forgetMonoidal C).toFunctor = forget C := rfl
@[simp] theorem forgetMonoidal_Œµ : (forgetMonoidal C).Œµ = ùüô (ùüô_ C) := rfl
@[simp] theorem forgetMonoidal_Œº (X Y : Mon_ C) : (forgetMonoidal C).Œº X Y = ùüô (X.X ‚äó Y.X) := rfl

variable {C}

theorem one_braiding {X Y : Mon_ C} : (X ‚äó Y).one ‚â´ (Œ≤_ X.X Y.X).hom = (Y ‚äó X).one := by
  simp only [monMonoidalStruct_tensorObj_X, tensor_one, Category.assoc,
    BraidedCategory.braiding_naturality, braiding_tensorUnit_right, Iso.cancel_iso_inv_left]
  coherence

end BraidedCategory

/-!
We next show that if `C` is symmetric, then `Mon_ C` is braided, and indeed symmetric.

Note that `Mon_ C` is *not* braided in general when `C` is only braided.

The more interesting construction is the 2-category of monoids in `C`,
bimodules between the monoids, and intertwiners between the bimodules.

When `C` is braided, that is a monoidal 2-category.
-/
section SymmetricCategory

variable [SymmetricCategory C]

theorem mul_braiding {X Y : Mon_ C} :
    (X ‚äó Y).mul ‚â´ (Œ≤_ X.X Y.X).hom = ((Œ≤_ X.X Y.X).hom ‚äó (Œ≤_ X.X Y.X).hom) ‚â´ (Y ‚äó X).mul := by
  simp only [monMonoidalStruct_tensorObj_X, tensor_mul, tensor_Œº, Category.assoc,
    BraidedCategory.braiding_naturality, BraidedCategory.braiding_tensor_right,
    BraidedCategory.braiding_tensor_left, comp_whiskerRight, whisker_assoc,
    MonoidalCategory.whiskerLeft_comp, pentagon_assoc, pentagon_inv_hom_hom_hom_inv_assoc,
    Iso.inv_hom_id_assoc, whiskerLeft_hom_inv_assoc]
  slice_lhs 3 4 =>
    -- We use symmetry here:
    rw [‚Üê MonoidalCategory.whiskerLeft_comp, ‚Üê comp_whiskerRight, SymmetricCategory.symmetry]
  simp only [id_whiskerRight, MonoidalCategory.whiskerLeft_id, Category.id_comp, Category.assoc,
    pentagon_inv_assoc, Iso.hom_inv_id_assoc]
  slice_lhs 1 2 =>
    rw [‚Üê associator_inv_naturality_left]
  slice_lhs 2 3 =>
    rw [Iso.inv_hom_id]
  rw [Category.id_comp]
  slice_lhs 2 3 =>
    rw [‚Üê associator_naturality_right]
  slice_lhs 1 2 =>
    rw [‚Üê tensorHom_def]
  simp only [Category.assoc]

instance : SymmetricCategory (Mon_ C) where
  braiding := fun X Y => mkIso (Œ≤_ X.X Y.X) one_braiding mul_braiding
  symmetry := fun X Y => by
    ext
    simp [‚Üê SymmetricCategory.braiding_swap_eq_inv_braiding]

end SymmetricCategory

end Mon_

/-!
Projects:
* Check that `Mon_ MonCat ‚âå CommMonCat`, via the Eckmann-Hilton argument.
  (You'll have to hook up the cartesian monoidal structure on `MonCat` first,
  available in mathlib3#3463)
* More generally, check that `Mon_ (Mon_ C) ‚âå CommMon_ C` when `C` is braided.
* Check that `Mon_ TopCat ‚âå [bundled topological monoids]`.
* Check that `Mon_ AddCommGroupCat ‚âå RingCat`.
  (We've already got `Mon_ (ModuleCat R) ‚âå AlgebraCat R`,
  in `Mathlib.CategoryTheory.Monoidal.Internal.Module`.)
* Can you transport this monoidal structure to `RingCat` or `AlgebraCat R`?
  How does it compare to the "native" one?
* Show that when `F` is a lax braided functor `C ‚•§ D`, the functor `map_Mon F : Mon_ C ‚•§ Mon_ D`
  is lax monoidal.
-/
