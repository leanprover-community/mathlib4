/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
module

public import Mathlib.CategoryTheory.Monoidal.Braided.Basic
public import Mathlib.CategoryTheory.Functor.Category
public import Mathlib.CategoryTheory.Functor.Const

/-!
# Monoidal structure on `C ‚•§ D` when `D` is monoidal.

When `C` is any category, and `D` is a monoidal category,
there is a natural "pointwise" monoidal structure on `C ‚•§ D`.

The initial intended application is tensor product of presheaves.
-/

@[expose] public section


universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

open CategoryTheory

open CategoryTheory.MonoidalCategory

namespace CategoryTheory

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]
variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] [MonoidalCategory.{v‚ÇÇ} D]

namespace Monoidal

namespace FunctorCategory

variable (F G F' G' : C ‚•§ D)

/-- (An auxiliary definition for `functorCategoryMonoidal`.)
Tensor product of functors `C ‚•§ D`, when `D` is monoidal.
-/
@[simps]
def tensorObj : C ‚•§ D where
  obj X := F.obj X ‚äó G.obj X
  map f := F.map f ‚äó‚Çò G.map f

variable {F G F' G'}
variable (Œ± : F ‚ü∂ G) (Œ≤ : F' ‚ü∂ G')

/-- (An auxiliary definition for `functorCategoryMonoidal`.)
Tensor product of natural transformations into `D`, when `D` is monoidal.
-/
@[simps]
def tensorHom : tensorObj F F' ‚ü∂ tensorObj G G' where
  app X := Œ±.app X ‚äó‚Çò Œ≤.app X
  naturality X Y f := by
    dsimp; rw [tensorHom_comp_tensorHom, Œ±.naturality, Œ≤.naturality, ‚Üê tensorHom_comp_tensorHom]

/-- (An auxiliary definition for `functorCategoryMonoidal`.) -/
@[simps]
def whiskerLeft (F) (Œ≤ : F' ‚ü∂ G') : tensorObj F F' ‚ü∂ tensorObj F G' where
  app X := F.obj X ‚óÅ Œ≤.app X
  naturality X Y f := by
    simp only [‚Üê id_tensorHom]
    apply (tensorHom (ùüô F) Œ≤).naturality

/-- (An auxiliary definition for `functorCategoryMonoidal`.) -/
@[simps]
def whiskerRight (F') : tensorObj F F' ‚ü∂ tensorObj G F' where
  app X := Œ±.app X ‚ñ∑ F'.obj X
  naturality X Y f := by
    simp only [‚Üê tensorHom_id]
    apply (tensorHom Œ± (ùüô F')).naturality

end FunctorCategory

open CategoryTheory.Monoidal.FunctorCategory

/-- When `C` is any category, and `D` is a monoidal category,
the functor category `C ‚•§ D` has a natural pointwise monoidal structure,
where `(F ‚äó G).obj X = F.obj X ‚äó G.obj X`.
-/
instance functorCategoryMonoidalStruct : MonoidalCategoryStruct (C ‚•§ D) where
  tensorObj F G := tensorObj F G
  tensorHom Œ± Œ≤ := tensorHom Œ± Œ≤
  whiskerLeft F _ _ Œ± := FunctorCategory.whiskerLeft F Œ±
  whiskerRight Œ± F := FunctorCategory.whiskerRight Œ± F
  tensorUnit := (CategoryTheory.Functor.const C).obj (ùüô_ D)
  leftUnitor F := NatIso.ofComponents fun X => Œª_ (F.obj X)
  rightUnitor F := NatIso.ofComponents fun X => œÅ_ (F.obj X)
  associator F G H := NatIso.ofComponents fun X => Œ±_ (F.obj X) (G.obj X) (H.obj X)

@[simp]
theorem tensorUnit_obj {X} : (ùüô_ (C ‚•§ D)).obj X = ùüô_ D :=
  rfl

@[simp]
theorem tensorUnit_map {X Y} {f : X ‚ü∂ Y} : (ùüô_ (C ‚•§ D)).map f = ùüô (ùüô_ D) :=
  rfl

@[simp]
theorem tensorObj_obj {F G : C ‚•§ D} {X} : (F ‚äó G).obj X = F.obj X ‚äó G.obj X :=
  rfl

@[simp]
theorem tensorObj_map {F G : C ‚•§ D} {X Y} {f : X ‚ü∂ Y} : (F ‚äó G).map f = F.map f ‚äó‚Çò G.map f :=
  rfl

@[simp]
theorem tensorHom_app {F G F' G' : C ‚•§ D} {Œ± : F ‚ü∂ G} {Œ≤ : F' ‚ü∂ G'} {X} :
    (Œ± ‚äó‚Çò Œ≤).app X = Œ±.app X ‚äó‚Çò Œ≤.app X :=
  rfl

@[simp]
theorem whiskerLeft_app {F F' G' : C ‚•§ D} {Œ≤ : F' ‚ü∂ G'} {X} :
    (F ‚óÅ Œ≤).app X = F.obj X ‚óÅ Œ≤.app X :=
  rfl

@[simp]
theorem whiskerRight_app {F G F' : C ‚•§ D} {Œ± : F ‚ü∂ G} {X} :
    (Œ± ‚ñ∑ F').app X = Œ±.app X ‚ñ∑ F'.obj X :=
  rfl

@[simp]
theorem leftUnitor_hom_app {F : C ‚•§ D} {X} :
    ((Œª_ F).hom : ùüô_ _ ‚äó F ‚ü∂ F).app X = (Œª_ (F.obj X)).hom :=
  rfl

@[simp]
theorem leftUnitor_inv_app {F : C ‚•§ D} {X} :
    ((Œª_ F).inv : F ‚ü∂ ùüô_ _ ‚äó F).app X = (Œª_ (F.obj X)).inv :=
  rfl

@[simp]
theorem rightUnitor_hom_app {F : C ‚•§ D} {X} :
    ((œÅ_ F).hom : F ‚äó ùüô_ _ ‚ü∂ F).app X = (œÅ_ (F.obj X)).hom :=
  rfl

@[simp]
theorem rightUnitor_inv_app {F : C ‚•§ D} {X} :
    ((œÅ_ F).inv : F ‚ü∂ F ‚äó ùüô_ _).app X = (œÅ_ (F.obj X)).inv :=
  rfl

@[simp]
theorem associator_hom_app {F G H : C ‚•§ D} {X} :
    ((Œ±_ F G H).hom : (F ‚äó G) ‚äó H ‚ü∂ F ‚äó G ‚äó H).app X = (Œ±_ (F.obj X) (G.obj X) (H.obj X)).hom :=
  rfl

@[simp]
theorem associator_inv_app {F G H : C ‚•§ D} {X} :
    ((Œ±_ F G H).inv : F ‚äó G ‚äó H ‚ü∂ (F ‚äó G) ‚äó H).app X = (Œ±_ (F.obj X) (G.obj X) (H.obj X)).inv :=
  rfl

/-- When `C` is any category, and `D` is a monoidal category,
the functor category `C ‚•§ D` has a natural pointwise monoidal structure,
where `(F ‚äó G).obj X = F.obj X ‚äó G.obj X`.
-/
instance functorCategoryMonoidal : MonoidalCategory (C ‚•§ D) where
  tensorHom_def := by intros; ext; simp [tensorHom_def]
  pentagon F G H K := by ext X; dsimp; rw [pentagon]

section BraidedCategory

open CategoryTheory.BraidedCategory

variable [BraidedCategory.{v‚ÇÇ} D]

/-- When `C` is any category, and `D` is a braided monoidal category,
the natural pointwise monoidal structure on the functor category `C ‚•§ D`
is also braided.
-/
instance functorCategoryBraided : BraidedCategory (C ‚•§ D) where
  braiding F G := NatIso.ofComponents fun _ => Œ≤_ _ _
  hexagon_forward F G H := by ext X; apply hexagon_forward
  hexagon_reverse F G H := by ext X; apply hexagon_reverse

example : BraidedCategory (C ‚•§ D) :=
  CategoryTheory.Monoidal.functorCategoryBraided

end BraidedCategory

section SymmetricCategory

open CategoryTheory.SymmetricCategory

variable [SymmetricCategory.{v‚ÇÇ} D]

/-- When `C` is any category, and `D` is a symmetric monoidal category,
the natural pointwise monoidal structure on the functor category `C ‚•§ D`
is also symmetric.
-/
instance functorCategorySymmetric : SymmetricCategory (C ‚•§ D) where
  symmetry F G := by ext X; apply symmetry

end SymmetricCategory

end Monoidal

@[simps]
instance Functor.LaxMonoidal.whiskeringRight
    {C D E : Type*} [Category C] [Category D] [Category E] [MonoidalCategory D]
    [MonoidalCategory E] (L : D ‚•§ E) [L.LaxMonoidal] :
    ((Functor.whiskeringRight C D E).obj L).LaxMonoidal where
  Œµ := { app X := Functor.LaxMonoidal.Œµ L }
  Œº F G := { app X := Functor.LaxMonoidal.Œº L (F.obj X) (G.obj X) }

@[simps]
instance Functor.OplaxMonoidal.whiskeringRight
    {C D E : Type*} [Category C] [Category D] [Category E] [MonoidalCategory D]
    [MonoidalCategory E] (L : D ‚•§ E) [L.OplaxMonoidal] :
    ((Functor.whiskeringRight C D E).obj L).OplaxMonoidal where
  Œ∑ := { app X := Functor.OplaxMonoidal.Œ∑ L }
  Œ¥ F G := { app X := Functor.OplaxMonoidal.Œ¥ L (F.obj X) (G.obj X) }
  oplax_left_unitality := by aesop
  oplax_right_unitality := by aesop

instance {C D E : Type*} [Category C] [Category D] [Category E] [MonoidalCategory D]
    [MonoidalCategory E] (L : D ‚•§ E) [L.Monoidal] :
    ((Functor.whiskeringRight C D E).obj L).Monoidal where

@[deprecated (since := "2025-11-06")] alias instLaxMonoidalFunctorObjWhiskeringRight :=
  Functor.LaxMonoidal.whiskeringRight
@[deprecated (since := "2025-11-06")] alias instOplaxMonoidalFunctorObjWhiskeringRight :=
  Functor.OplaxMonoidal.whiskeringRight
@[deprecated (since := "2025-11-06")] alias Œµ_app := Functor.LaxMonoidal.whiskeringRight_Œµ_app
@[deprecated (since := "2025-11-06")] alias Œº_app := Functor.LaxMonoidal.whiskeringRight_Œº_app
@[deprecated (since := "2025-11-06")] alias Œ∑_app := Functor.OplaxMonoidal.whiskeringRight_Œ∑_app
@[deprecated (since := "2025-11-06")] alias Œ¥_app := Functor.OplaxMonoidal.whiskeringRight_Œ¥_app

@[simps!]
instance Functor.Monoidal.whiskeringLeft (E : Type*) [Category E] [MonoidalCategory E] (F : C ‚•§ D) :
    ((whiskeringLeft _ _ E).obj F).Monoidal :=
  CoreMonoidal.toMonoidal { ŒµIso := Iso.refl _, ŒºIso _ _ := Iso.refl _ }

instance (E : Type*) [Category E] [MonoidalCategory E] (e : C ‚âå D) :
    (e.congrLeft (E := E)).functor.Monoidal :=
  inferInstanceAs ((Functor.whiskeringLeft _ _ E).obj e.inverse).Monoidal

instance (E : Type*) [Category E] [MonoidalCategory E] (e : C ‚âå D) :
    (e.congrLeft (E := E)).inverse.Monoidal :=
  inferInstanceAs ((Functor.whiskeringLeft _ _ E).obj e.functor).Monoidal

instance (E : Type*) [Category E] [MonoidalCategory E] (e : C ‚âå D) :
    (e.congrLeft (E := E)).IsMonoidal where
  leftAdjoint_Œº X Y := by
    ext
    simp [‚Üê Functor.map_comp]

end CategoryTheory
