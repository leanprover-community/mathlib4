/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Monoidal.Braided.Basic
import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.Functor.Const

/-!
# Monoidal structure on `C â¥¤ D` when `D` is monoidal.

When `C` is any category, and `D` is a monoidal category,
there is a natural "pointwise" monoidal structure on `C â¥¤ D`.

The initial intended application is tensor product of presheaves.
-/


universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

open CategoryTheory

open CategoryTheory.MonoidalCategory

namespace CategoryTheory.Monoidal

variable {C : Type uâ‚} [Category.{vâ‚} C]
variable {D : Type uâ‚‚} [Category.{vâ‚‚} D] [MonoidalCategory.{vâ‚‚} D]

namespace FunctorCategory

variable (F G F' G' : C â¥¤ D)

/-- (An auxiliary definition for `functorCategoryMonoidal`.)
Tensor product of functors `C â¥¤ D`, when `D` is monoidal.
-/
@[simps]
def tensorObj : C â¥¤ D where
  obj X := F.obj X âŠ— G.obj X
  map f := F.map f âŠ—â‚˜ G.map f

variable {F G F' G'}
variable (Î± : F âŸ¶ G) (Î² : F' âŸ¶ G')

/-- (An auxiliary definition for `functorCategoryMonoidal`.)
Tensor product of natural transformations into `D`, when `D` is monoidal.
-/
@[simps]
def tensorHom : tensorObj F F' âŸ¶ tensorObj G G' where
  app X := Î±.app X âŠ—â‚˜ Î².app X
  naturality X Y f := by dsimp; rw [â† tensor_comp, Î±.naturality, Î².naturality, tensor_comp]

/-- (An auxiliary definition for `functorCategoryMonoidal`.) -/
@[simps]
def whiskerLeft (F) (Î² : F' âŸ¶ G') : tensorObj F F' âŸ¶ tensorObj F G' where
  app X := F.obj X â— Î².app X
  naturality X Y f := by
    simp only [â† id_tensorHom]
    apply (tensorHom (ðŸ™ F) Î²).naturality

/-- (An auxiliary definition for `functorCategoryMonoidal`.) -/
@[simps]
def whiskerRight (F') : tensorObj F F' âŸ¶ tensorObj G F' where
  app X := Î±.app X â–· F'.obj X
  naturality X Y f := by
    simp only [â† tensorHom_id]
    apply (tensorHom Î± (ðŸ™ F')).naturality

end FunctorCategory

open CategoryTheory.Monoidal.FunctorCategory

/-- When `C` is any category, and `D` is a monoidal category,
the functor category `C â¥¤ D` has a natural pointwise monoidal structure,
where `(F âŠ— G).obj X = F.obj X âŠ— G.obj X`.
-/
instance functorCategoryMonoidalStruct : MonoidalCategoryStruct (C â¥¤ D) where
  tensorObj F G := tensorObj F G
  tensorHom Î± Î² := tensorHom Î± Î²
  whiskerLeft F _ _ Î± := FunctorCategory.whiskerLeft F Î±
  whiskerRight Î± F := FunctorCategory.whiskerRight Î± F
  tensorUnit := (CategoryTheory.Functor.const C).obj (ðŸ™_ D)
  leftUnitor F := NatIso.ofComponents fun X => Î»_ (F.obj X)
  rightUnitor F := NatIso.ofComponents fun X => Ï_ (F.obj X)
  associator F G H := NatIso.ofComponents fun X => Î±_ (F.obj X) (G.obj X) (H.obj X)

@[simp]
theorem tensorUnit_obj {X} : (ðŸ™_ (C â¥¤ D)).obj X = ðŸ™_ D :=
  rfl

@[simp]
theorem tensorUnit_map {X Y} {f : X âŸ¶ Y} : (ðŸ™_ (C â¥¤ D)).map f = ðŸ™ (ðŸ™_ D) :=
  rfl

@[simp]
theorem tensorObj_obj {F G : C â¥¤ D} {X} : (F âŠ— G).obj X = F.obj X âŠ— G.obj X :=
  rfl

@[simp]
theorem tensorObj_map {F G : C â¥¤ D} {X Y} {f : X âŸ¶ Y} : (F âŠ— G).map f = F.map f âŠ—â‚˜ G.map f :=
  rfl

@[simp]
theorem tensorHom_app {F G F' G' : C â¥¤ D} {Î± : F âŸ¶ G} {Î² : F' âŸ¶ G'} {X} :
    (Î± âŠ—â‚˜ Î²).app X = Î±.app X âŠ—â‚˜ Î².app X :=
  rfl

@[simp]
theorem whiskerLeft_app {F F' G' : C â¥¤ D} {Î² : F' âŸ¶ G'} {X} :
    (F â— Î²).app X = F.obj X â— Î².app X :=
  rfl

@[simp]
theorem whiskerRight_app {F G F' : C â¥¤ D} {Î± : F âŸ¶ G} {X} :
    (Î± â–· F').app X = Î±.app X â–· F'.obj X :=
  rfl

@[simp]
theorem leftUnitor_hom_app {F : C â¥¤ D} {X} :
    ((Î»_ F).hom : ðŸ™_ _ âŠ— F âŸ¶ F).app X = (Î»_ (F.obj X)).hom :=
  rfl

@[simp]
theorem leftUnitor_inv_app {F : C â¥¤ D} {X} :
    ((Î»_ F).inv : F âŸ¶ ðŸ™_ _ âŠ— F).app X = (Î»_ (F.obj X)).inv :=
  rfl

@[simp]
theorem rightUnitor_hom_app {F : C â¥¤ D} {X} :
    ((Ï_ F).hom : F âŠ— ðŸ™_ _ âŸ¶ F).app X = (Ï_ (F.obj X)).hom :=
  rfl

@[simp]
theorem rightUnitor_inv_app {F : C â¥¤ D} {X} :
    ((Ï_ F).inv : F âŸ¶ F âŠ— ðŸ™_ _).app X = (Ï_ (F.obj X)).inv :=
  rfl

@[simp]
theorem associator_hom_app {F G H : C â¥¤ D} {X} :
    ((Î±_ F G H).hom : (F âŠ— G) âŠ— H âŸ¶ F âŠ— G âŠ— H).app X = (Î±_ (F.obj X) (G.obj X) (H.obj X)).hom :=
  rfl

@[simp]
theorem associator_inv_app {F G H : C â¥¤ D} {X} :
    ((Î±_ F G H).inv : F âŠ— G âŠ— H âŸ¶ (F âŠ— G) âŠ— H).app X = (Î±_ (F.obj X) (G.obj X) (H.obj X)).inv :=
  rfl

/-- When `C` is any category, and `D` is a monoidal category,
the functor category `C â¥¤ D` has a natural pointwise monoidal structure,
where `(F âŠ— G).obj X = F.obj X âŠ— G.obj X`.
-/
instance functorCategoryMonoidal : MonoidalCategory (C â¥¤ D) where
  tensorHom_def := by intros; ext; simp [tensorHom_def]
  pentagon F G H K := by ext X; dsimp; rw [pentagon]

section BraidedCategory

open CategoryTheory.BraidedCategory

variable [BraidedCategory.{vâ‚‚} D]

/-- When `C` is any category, and `D` is a braided monoidal category,
the natural pointwise monoidal structure on the functor category `C â¥¤ D`
is also braided.
-/
instance functorCategoryBraided : BraidedCategory (C â¥¤ D) where
  braiding F G := NatIso.ofComponents fun _ => Î²_ _ _
  hexagon_forward F G H := by ext X; apply hexagon_forward
  hexagon_reverse F G H := by ext X; apply hexagon_reverse

example : BraidedCategory (C â¥¤ D) :=
  CategoryTheory.Monoidal.functorCategoryBraided

end BraidedCategory

section SymmetricCategory

open CategoryTheory.SymmetricCategory

variable [SymmetricCategory.{vâ‚‚} D]

/-- When `C` is any category, and `D` is a symmetric monoidal category,
the natural pointwise monoidal structure on the functor category `C â¥¤ D`
is also symmetric.
-/
instance functorCategorySymmetric : SymmetricCategory (C â¥¤ D) where
  symmetry F G := by ext X; apply symmetry

end SymmetricCategory

end CategoryTheory.Monoidal
