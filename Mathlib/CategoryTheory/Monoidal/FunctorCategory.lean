/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Monoidal.Braided
import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.Functor.Const

#align_import category_theory.monoidal.functor_category from "leanprover-community/mathlib"@"73dd4b5411ec8fafb18a9d77c9c826907730af80"

/-!
# Monoidal structure on `C ‚•§ D` when `D` is monoidal.

When `C` is any category, and `D` is a monoidal category,
there is a natural "pointwise" monoidal structure on `C ‚•§ D`.

The initial intended application is tensor product of presheaves.
-/


universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

open CategoryTheory

open CategoryTheory.MonoidalCategory

namespace CategoryTheory.Monoidal

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]

variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] [MonoidalCategory.{v‚ÇÇ} D]

namespace FunctorCategory

variable (F G F' G' : C ‚•§ D)

/-- (An auxiliary definition for `functorCategoryMonoidal`.)
Tensor product of functors `C ‚•§ D`, when `D` is monoidal.
 -/
@[simps]
def tensorObj : C ‚•§ D where
  obj X := F.obj X ‚äó G.obj X
  map f := F.map f ‚äó G.map f
#align category_theory.monoidal.functor_category.tensor_obj CategoryTheory.Monoidal.FunctorCategory.tensorObj

variable {F G F' G'}

variable (Œ± : F ‚ü∂ G) (Œ≤ : F' ‚ü∂ G')

/-- (An auxiliary definition for `functorCategoryMonoidal`.)
Tensor product of natural transformations into `D`, when `D` is monoidal.
-/
@[simps]
def tensorHom : tensorObj F F' ‚ü∂ tensorObj G G' where
  app X := Œ±.app X ‚äó Œ≤.app X
  naturality X Y f := by dsimp; rw [‚Üê tensor_comp, Œ±.naturality, Œ≤.naturality, tensor_comp]
#align category_theory.monoidal.functor_category.tensor_hom CategoryTheory.Monoidal.FunctorCategory.tensorHom

/-- (An auxiliary definition for `functorCategoryMonoidal`.) -/
@[simps]
def whiskerLeft (F) (Œ≤ : F' ‚ü∂ G') : tensorObj F F' ‚ü∂ tensorObj F G' where
  app X := F.obj X ‚óÅ Œ≤.app X
  naturality X Y f := by
    simp only [‚Üê id_tensorHom]
    apply (tensorHom (ùüô F) Œ≤).naturality

/-- (An auxiliary definition for `functorCategoryMonoidal`.) -/
@[simps]
def whiskerRight (F') : tensorObj F F' ‚ü∂ tensorObj G F' where
  app X := Œ±.app X ‚ñ∑ F'.obj X
  naturality X Y f := by
    simp only [‚Üê tensorHom_id]
    apply (tensorHom Œ± (ùüô F')).naturality

end FunctorCategory

open CategoryTheory.Monoidal.FunctorCategory

attribute [local simp] tensorHom_def

/-- When `C` is any category, and `D` is a monoidal category,
the functor category `C ‚•§ D` has a natural pointwise monoidal structure,
where `(F ‚äó G).obj X = F.obj X ‚äó G.obj X`.
-/
instance functorCategoryMonoidalStruct : MonoidalCategoryStruct (C ‚•§ D) where
  tensorObj F G := tensorObj F G
  tensorHom Œ± Œ≤ := tensorHom Œ± Œ≤
  whiskerLeft F _ _ Œ± := FunctorCategory.whiskerLeft F Œ±
  whiskerRight Œ± F := FunctorCategory.whiskerRight Œ± F
  tensorUnit := (CategoryTheory.Functor.const C).obj (ùüô_ D)
  leftUnitor F := NatIso.ofComponents fun X => Œª_ (F.obj X)
  rightUnitor F := NatIso.ofComponents fun X => œÅ_ (F.obj X)
  associator F G H := NatIso.ofComponents fun X => Œ±_ (F.obj X) (G.obj X) (H.obj X)

@[simp]
theorem tensorUnit_obj {X} : (ùüô_ (C ‚•§ D)).obj X = ùüô_ D :=
  rfl
#align category_theory.monoidal.tensor_unit_obj CategoryTheory.Monoidal.tensorUnit_obj

@[simp]
theorem tensorUnit_map {X Y} {f : X ‚ü∂ Y} : (ùüô_ (C ‚•§ D)).map f = ùüô (ùüô_ D) :=
  rfl
#align category_theory.monoidal.tensor_unit_map CategoryTheory.Monoidal.tensorUnit_map

@[simp]
theorem tensorObj_obj {F G : C ‚•§ D} {X} : (F ‚äó G).obj X = F.obj X ‚äó G.obj X :=
  rfl
#align category_theory.monoidal.tensor_obj_obj CategoryTheory.Monoidal.tensorObj_obj

@[simp]
theorem tensorObj_map {F G : C ‚•§ D} {X Y} {f : X ‚ü∂ Y} : (F ‚äó G).map f = F.map f ‚äó G.map f :=
  rfl
#align category_theory.monoidal.tensor_obj_map CategoryTheory.Monoidal.tensorObj_map

@[simp]
theorem tensorHom_app {F G F' G' : C ‚•§ D} {Œ± : F ‚ü∂ G} {Œ≤ : F' ‚ü∂ G'} {X} :
    (Œ± ‚äó Œ≤).app X = Œ±.app X ‚äó Œ≤.app X :=
  rfl
#align category_theory.monoidal.tensor_hom_app CategoryTheory.Monoidal.tensorHom_app

@[simp]
theorem whiskerLeft_app {F F' G' : C ‚•§ D} {Œ≤ : F' ‚ü∂ G'} {X} :
    (F ‚óÅ Œ≤).app X = F.obj X ‚óÅ Œ≤.app X :=
  rfl

@[simp]
theorem whiskerRight_app {F G F' : C ‚•§ D} {Œ± : F ‚ü∂ G} {X} :
    (Œ± ‚ñ∑ F').app X = Œ±.app X ‚ñ∑ F'.obj X :=
  rfl

@[simp]
theorem leftUnitor_hom_app {F : C ‚•§ D} {X} :
    ((Œª_ F).hom : ùüô_ _ ‚äó F ‚ü∂ F).app X = (Œª_ (F.obj X)).hom :=
  rfl
#align category_theory.monoidal.left_unitor_hom_app CategoryTheory.Monoidal.leftUnitor_hom_app

@[simp]
theorem leftUnitor_inv_app {F : C ‚•§ D} {X} :
    ((Œª_ F).inv : F ‚ü∂ ùüô_ _ ‚äó F).app X = (Œª_ (F.obj X)).inv :=
  rfl
#align category_theory.monoidal.left_unitor_inv_app CategoryTheory.Monoidal.leftUnitor_inv_app

@[simp]
theorem rightUnitor_hom_app {F : C ‚•§ D} {X} :
    ((œÅ_ F).hom : F ‚äó ùüô_ _ ‚ü∂ F).app X = (œÅ_ (F.obj X)).hom :=
  rfl
#align category_theory.monoidal.right_unitor_hom_app CategoryTheory.Monoidal.rightUnitor_hom_app

@[simp]
theorem rightUnitor_inv_app {F : C ‚•§ D} {X} :
    ((œÅ_ F).inv : F ‚ü∂ F ‚äó ùüô_ _).app X = (œÅ_ (F.obj X)).inv :=
  rfl
#align category_theory.monoidal.right_unitor_inv_app CategoryTheory.Monoidal.rightUnitor_inv_app

@[simp]
theorem associator_hom_app {F G H : C ‚•§ D} {X} :
    ((Œ±_ F G H).hom : (F ‚äó G) ‚äó H ‚ü∂ F ‚äó G ‚äó H).app X = (Œ±_ (F.obj X) (G.obj X) (H.obj X)).hom :=
  rfl
#align category_theory.monoidal.associator_hom_app CategoryTheory.Monoidal.associator_hom_app

@[simp]
theorem associator_inv_app {F G H : C ‚•§ D} {X} :
    ((Œ±_ F G H).inv : F ‚äó G ‚äó H ‚ü∂ (F ‚äó G) ‚äó H).app X = (Œ±_ (F.obj X) (G.obj X) (H.obj X)).inv :=
  rfl
#align category_theory.monoidal.associator_inv_app CategoryTheory.Monoidal.associator_inv_app

attribute [local simp] id_tensorHom tensorHom_id in

/-- When `C` is any category, and `D` is a monoidal category,
the functor category `C ‚•§ D` has a natural pointwise monoidal structure,
where `(F ‚äó G).obj X = F.obj X ‚äó G.obj X`.
-/
instance functorCategoryMonoidal : MonoidalCategory (C ‚•§ D) where
  tensorHom_def := by intros; ext; simp [tensorHom_def]
  whisker_exchange := by intros; ext; simp [whisker_exchange]
  pentagon F G H K := by ext X; dsimp; rw [pentagon]
#align category_theory.monoidal.functor_category_monoidal CategoryTheory.Monoidal.functorCategoryMonoidal

section BraidedCategory

open CategoryTheory.BraidedCategory

variable [BraidedCategory.{v‚ÇÇ} D]

/-- When `C` is any category, and `D` is a braided monoidal category,
the natural pointwise monoidal structure on the functor category `C ‚•§ D`
is also braided.
-/
instance functorCategoryBraided : BraidedCategory (C ‚•§ D) where
  braiding F G := NatIso.ofComponents (fun X ‚Ü¶ Œ≤_ _ _) (by simp [whisker_exchange])
  hexagon_forward F G H := by ext X; apply hexagon_forward
  hexagon_reverse F G H := by ext X; apply hexagon_reverse
#align category_theory.monoidal.functor_category_braided CategoryTheory.Monoidal.functorCategoryBraided

example : BraidedCategory (C ‚•§ D) :=
  CategoryTheory.Monoidal.functorCategoryBraided

end BraidedCategory

section SymmetricCategory

open CategoryTheory.SymmetricCategory

variable [SymmetricCategory.{v‚ÇÇ} D]

/-- When `C` is any category, and `D` is a symmetric monoidal category,
the natural pointwise monoidal structure on the functor category `C ‚•§ D`
is also symmetric.
-/
instance functorCategorySymmetric : SymmetricCategory (C ‚•§ D) where
  symmetry F G := by ext X; apply symmetry
#align category_theory.monoidal.functor_category_symmetric CategoryTheory.Monoidal.functorCategorySymmetric

end SymmetricCategory

end CategoryTheory.Monoidal
