/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Brendan Murphy
-/
import Mathlib.CategoryTheory.Monoidal.Functor
import Mathlib.CategoryTheory.Functor.FullyFaithful
import Mathlib.CategoryTheory.CommSq
import Mathlib.CategoryTheory.CatSquares

#align_import category_theory.monoidal.natural_transformation from "leanprover-community/mathlib"@"d047eb4671130d5998b185e49a0443a0d2e9b191"

/-!
# Monoidal natural transformations

Natural transformations between lax monoidal functors must satisfy
an additional compatibility relation with the tensorators:
`F.ฮผ X Y โซ app (X โ Y) = (app X โ app Y) โซ G.ฮผ X Y`.

There is a dual condition for colax monoidal functors, and a hexagonal
condition for transformations `F โ H โ G โ K` when `F, G` are lax and `H, K` colax.

((Co)lax) monoidal functors between a fixed pair of monoidal categories
themselves form a category.
There is a double category with objects monoidal category and lax/colax
functors as the vertical/horizontal 1-cells, with `MonoidalSquare`s as 2-cells.

References: Adjoint for double categories, Grandis and Pare
-/

open CategoryTheory

universe vโ vโ vโ vโ vโ vโ uโ uโ uโ uโ uโ uโ

open CategoryTheory.Category

open CategoryTheory.Functor

namespace CategoryTheory

open MonoidalCategory

variable {B : Type uโ} [Category.{vโ} B] [MonoidalCategory.{vโ} B]
         {C : Type uโ} [Category.{vโ} C] [MonoidalCategory.{vโ} C]
         {D : Type uโ} [Category.{vโ} D] [MonoidalCategory.{vโ} D]
         {E : Type uโ} [Category.{vโ} E] [MonoidalCategory.{vโ} E]
         {M : Type uโ} [Category.{vโ} M] [MonoidalCategory.{vโ} M]
         {N : Type uโ} [Category.{vโ} N] [MonoidalCategory.{vโ} N]

/-- A lax monoidal natural transformation is a natural transformation between
lax monoidal functors additionally satisfying:
`F.ฮผ X Y โซ app (X โ Y) = (app X โ app Y) โซ G.ฮผ X Y`
-/
@[ext]
structure LaxMonoidalNatTrans (F G : C โฅคโโ D) extends
  NatTrans F.toFunctor G.toFunctor where
  /-- The unit condition for a lax monoidal natural transformation. -/
  unit : F.ฮท โซ app (๐_ C) = G.ฮท := by aesop_cat
  /-- The tensor condition for a lax monoidal natural transformation. -/
  tensor : โ X Y, F.ฮผ _ _ โซ app (X โ Y) = (app X โ app Y) โซ G.ฮผ _ _ := by aesop_cat
#align category_theory.monoidal_nat_trans CategoryTheory.LaxMonoidalNatTrans

/-- A colax monoidal natural transformation is a natural transformation between
colax monoidal functors additionally satisfying:
`F.ฮด X Y โซ (app X โ app Y) = app (X โ Y) โซ G.ฮด X Y`
-/
@[ext]
structure ColaxMonoidalNatTrans (F G : C โฅคโc D) extends
  NatTrans F.toFunctor G.toFunctor where
  /-- The counit condition for a colax monoidal natural transformation. -/
  counit : app (๐_ C) โซ G.ฮต = F.ฮต  := by aesop_cat
  /-- The cotensor condition for a colax monoidal natural transformation. -/
  cotensor : โ X Y, F.ฮด X Y โซ (app X โ app Y) = app (X โ Y) โซ G.ฮด X Y := by aesop_cat

/-- A monoidal natural transformation is a natural transformation between
monoidal functors which is both lax and colax; equivalently it is either lax or colax. -/
@[ext]
structure MonoidalNatTrans (F G : C โฅคโs D) extends
  LaxMonoidalNatTrans F.toLaxMonoidalFunctor G.toLaxMonoidalFunctor,
  ColaxMonoidalNatTrans F.toColaxMonoidalFunctor G.toColaxMonoidalFunctor

/-- A monoidal square is a natural transformation between compositions of lax
and colax monoidal functors, satisfying a hexagonal coherence condition about
the (co)tensorators and a trapezoidal coherence condition about the (co)units.
The argument order is chosen to be consistent with `CommSq`. -/
@[ext]
structure MonoidalSq (F : B โฅคโโ C) (G : B โฅคโc D) (H : C โฅคโc E) (K : D โฅคโโ E)
    extends CatColaxSq F.toFunctor G.toFunctor H.toFunctor K.toFunctor where
  trapezoid' : H.map F.ฮท โซ constraint.app (๐_ B) โซ K.map G.ฮต = H.ฮต โซ K.ฮท :=
    by aesop_cat
  hexagon' : โ X Y : B,
      H.map (F.ฮผ X Y) โซ constraint.app (X โ Y) โซ K.map (G.ฮด X Y) =
        H.ฮด (F.obj X) (F.obj Y) โซ (constraint.app X โ constraint.app Y) โซ
          K.ฮผ (G.obj X) (G.obj Y) :=
    by aesop_cat

attribute [reassoc (attr := simp)] LaxMonoidalNatTrans.tensor
attribute [reassoc (attr := simp)] LaxMonoidalNatTrans.unit
attribute [reassoc (attr := simp)] ColaxMonoidalNatTrans.cotensor
attribute [reassoc (attr := simp)] ColaxMonoidalNatTrans.counit

initialize_simps_projections LaxMonoidalNatTrans (+toNatTrans, -app)
initialize_simps_projections ColaxMonoidalNatTrans (+toNatTrans, -app)
initialize_simps_projections MonoidalNatTrans (+toNatTrans, -app)
initialize_simps_projections MonoidalSq (+constraint, -constraint_app)

#align category_theory.monoidal_nat_trans.unit CategoryTheory.LaxMonoidalNatTrans.unit
#align category_theory.monoidal_nat_trans.unit_assoc CategoryTheory.LaxMonoidalNatTrans.unit_assoc
#align category_theory.monoidal_nat_trans.tensor CategoryTheory.LaxMonoidalNatTrans.tensor
#align category_theory.monoidal_nat_trans.tensor_assoc CategoryTheory.LaxMonoidalNatTrans.tensor_assoc

namespace MonoidalSq

variable {F : B โฅคโโ C} {G : B โฅคโc D} {H : C โฅคโc E} {K : D โฅคโโ E}

@[reassoc (attr := simp)]
lemma trapezoid (s : MonoidalSq F G H K) :
    H.map F.ฮท โซ s.app (๐_ B) โซ K.map G.ฮต = H.ฮต โซ K.ฮท :=
  s.trapezoid'

@[reassoc (attr := simp)]
lemma hexagon_components (s : MonoidalSq F G H K) (X Y : B) :
    H.map (F.ฮผ X Y) โซ s.app (X โ Y) โซ K.map (G.ฮด X Y) =
      H.ฮด (F.obj X) (F.obj Y) โซ (s.app X โ s.app Y) โซ K.ฮผ (G.obj X) (G.obj Y) :=
  s.hexagon' X Y

@[reassoc (attr := simp)]
lemma hexagon (s : MonoidalSq F G H K) :
    (whiskerRight F.ฮผNatTrans H.toFunctor) โซ
      (Functor.associator _ _ _).hom โซ (whiskerLeft (tensor B) s.constraint) โซ
        (Functor.associator _ _ _).inv โซ (whiskerRight G.ฮดNatTrans K.toFunctor) =
    (Functor.associator _ _ _).hom โซ
      whiskerLeft (F.toFunctor.prod F.toFunctor) H.ฮดNatTrans โซ
        (Functor.associator _ _ _).inv โซ
          whiskerRight (Functor.prodCompIso _ _ _ _).inv (tensor E) โซ
            whiskerRight (.prod s.constraint s.constraint) (tensor E) โซ
              whiskerRight (Functor.prodCompIso _ _ _ _).hom (tensor E) โซ
                (Functor.associator _ _ _).hom โซ
                  whiskerLeft (.prod G.toFunctor G.toFunctor) K.ฮผNatTrans โซ
                    (Functor.associator _ _ _).inv := by
  aesop_cat

def mkOfNatTransHexagon
    (sq : CatColaxSq F.toFunctor G.toFunctor H.toFunctor K.toFunctor)
    (trapezoid : H.map F.ฮท โซ sq.app (๐_ B) โซ K.map G.ฮต = H.ฮต โซ K.ฮท :=
      by aesop_cat)
    (hexagon :
      (whiskerRight F.ฮผNatTrans H.toFunctor) โซ
        (Functor.associator _ _ _).hom โซ (whiskerLeft (tensor B) sq.constraint) โซ
          (Functor.associator _ _ _).inv โซ (whiskerRight G.ฮดNatTrans K.toFunctor) =
      (Functor.associator _ _ _).hom โซ
        whiskerLeft (F.toFunctor.prod F.toFunctor) H.ฮดNatTrans โซ
          (Functor.associator _ _ _).inv โซ
            whiskerRight (Functor.prodCompIso _ _ _ _).inv (tensor E) โซ
              whiskerRight (.prod sq.constraint sq.constraint) (tensor E) โซ
                whiskerRight (Functor.prodCompIso _ _ _ _).hom (tensor E) โซ
                  (Functor.associator _ _ _).hom โซ
                    whiskerLeft (.prod G.toFunctor G.toFunctor) K.ฮผNatTrans โซ
                      (Functor.associator _ _ _).inv := by aesop_cat) :
    MonoidalSq F G H K where
  __ := sq
  trapezoid' := trapezoid
  hexagon' X Y := by simpa using congrArg (NatTrans.app . (X, Y)) hexagon

end MonoidalSq

section comparison

open Quiver.Hom (op_inj unop_inj)

attribute [local ext] unop_inj in
@[simps!]
def LaxMonoidalNatTrans.op {F G : C โฅคโโ D} (ฮฑ : LaxMonoidalNatTrans F G) :
    ColaxMonoidalNatTrans G.op F.op where
  toNatTrans := .op ฮฑ.toNatTrans

@[simps!]
def LaxMonoidalNatTrans.unop {F G : Cแตแต โฅคโโ Dแตแต} (ฮฑ : LaxMonoidalNatTrans F G) :
    ColaxMonoidalNatTrans G.unop F.unop where
  toNatTrans := .unop ฮฑ.toNatTrans
  counit := op_inj <| by simp
  cotensor X Y := op_inj <| by simp

attribute [local ext] unop_inj in
@[simps!]
def ColaxMonoidalNatTrans.op {F G : C โฅคโc D} (ฮฑ : ColaxMonoidalNatTrans F G) :
    LaxMonoidalNatTrans G.op F.op where
  toNatTrans := .op ฮฑ.toNatTrans

@[simps!]
def ColaxMonoidalNatTrans.unop {F G : Cแตแต โฅคโc Dแตแต} (ฮฑ : ColaxMonoidalNatTrans F G) :
    LaxMonoidalNatTrans G.unop F.unop where
  toNatTrans := .unop ฮฑ.toNatTrans
  unit := op_inj <| by simp
  tensor X Y := op_inj <| by simp

end comparison

namespace LaxMonoidalNatTrans

@[simps]
def equivHGlobularSquare (F G : C โฅคโโ D) :
    LaxMonoidalNatTrans F G โ MonoidalSq F (.id C) (.id D) G where
  toFun ฮฑ := { constraint := F.rightUnitor.hom โซ ฮฑ.toNatTrans โซ G.leftUnitor.hom }
  invFun ฯ := { F.rightUnitor.inv โซ ฯ.constraint โซ G.leftUnitor.inv with
                unit := by simpa using ฯ.trapezoid
                tensor := by simpa using ฯ.hexagon_components }
  left_inv ฮฑ := by aesop_cat
  right_inv ฯ := by aesop_cat

/-- The identity lax monoidal natural transformation. -/
@[simps!]
def id (F : C โฅคโโ D) : LaxMonoidalNatTrans F F :=
  { ๐ F.toFunctor with }
#align category_theory.monoidal_nat_trans.id CategoryTheory.LaxMonoidalNatTrans.id

instance (F : C โฅคโโ D) : Inhabited (LaxMonoidalNatTrans F F) :=
  โจid Fโฉ

/-- Vertical composition of lax monoidal natural transformations. -/
@[simps!]
def vcomp {F G H : C โฅคโโ D} (ฮฑ : LaxMonoidalNatTrans F G)
    (ฮฒ : LaxMonoidalNatTrans G H) : LaxMonoidalNatTrans F H :=
  { NatTrans.vcomp ฮฑ.toNatTrans ฮฒ.toNatTrans with }
#align category_theory.monoidal_nat_trans.vcomp CategoryTheory.LaxMonoidalNatTrans.vcomp

end LaxMonoidalNatTrans

variable (C D)

@[simps! comp_toNatTrans id_toNatTrans]
instance LaxMonoidalFunctor.category : Category (C โฅคโโ D) where
  Hom := LaxMonoidalNatTrans
  id := .id
  comp ฮฑ ฮฒ := .vcomp ฮฑ ฮฒ
#align category_theory.monoidal_nat_trans.category_lax_monoidal_functor CategoryTheory.LaxMonoidalFunctor.category
#align category_theory.monoidal_nat_trans.comp_to_nat_trans_lax CategoryTheory.LaxMonoidalFunctor.category_comp_toNatTrans

variable {C D}

namespace LaxMonoidalNatTrans

-- Porting note: added, as `LaxMonoidalNatTrans.ext` does not apply to morphisms.
@[ext]
lemma ext' {F G : C โฅคโโ D} {ฮฑ ฮฒ : F โถ G} (w : โ X : C, ฮฑ.app X = ฮฒ.app X) : ฮฑ = ฮฒ :=
  LaxMonoidalNatTrans.ext _ _ (funext w)

/-- Horizontal composition of lax monoidal natural transformations. -/
@[simps]
def hcomp {F G : C โฅคโโ D} {H K : D โฅคโโ E} (ฮฑ : LaxMonoidalNatTrans F G)
    (ฮฒ : LaxMonoidalNatTrans H K) : LaxMonoidalNatTrans (F โโ H) (G โโ K) :=
  { NatTrans.hcomp ฮฑ.toNatTrans ฮฒ.toNatTrans with
    unit := by simp [โ K.toFunctor.map_comp, -map_comp]
    tensor := by simp [โ K.toFunctor.map_comp, -map_comp] }
#align category_theory.monoidal_nat_trans.hcomp CategoryTheory.LaxMonoidalNatTrans.hcomp

/-- The cartesian product of two lax monoidal natural transformations is monoidal. -/
@[simps]
def prod {F G : C โฅคโโ D} {H K : C โฅคโโ E} (ฮฑ : LaxMonoidalNatTrans F G)
    (ฮฒ : LaxMonoidalNatTrans H K) : LaxMonoidalNatTrans (F.prod' H) (G.prod' K) where
  app X := (ฮฑ.app X, ฮฒ.app X)
#align category_theory.monoidal_nat_trans.prod CategoryTheory.LaxMonoidalNatTrans.prod

end LaxMonoidalNatTrans

namespace LaxMonoidalNatIso

variable {F G : C โฅคโโ D}

/-- Construct a lax monoidal natural isomorphism from object level isomorphisms,
and the monoidal naturality in the forward direction. -/
@[simps hom_app inv_app]
def ofComponents (app : โ X : C, F.obj X โ G.obj X)
    (naturality' : โ {X Y : C} (f : X โถ Y),
      F.map f โซ (app Y).hom = (app X).hom โซ G.map f := by aesop_cat)
    (unit' : F.ฮท โซ (app (๐_ C)).hom = G.ฮท  := by aesop_cat)
    (tensor' : โ X Y,
      F.ฮผ X Y โซ (app (X โ Y)).hom =
        ((app X).hom โ (app Y).hom) โซ G.ฮผ X Y := by aesop_cat) : F โ G where
  hom := { app := fun X => (app X).hom }
  inv := {
    (NatIso.ofComponents app @naturality').inv with
    app := fun X => (app X).inv
    unit := by
      dsimp
      rw [โ unit', assoc, Iso.hom_inv_id, comp_id]
    tensor := fun X Y => tensor' X Y |> .mk |> .vert_inv
      (g := app X โ app Y) (h := app (X โ Y)) |> CommSq.w }
#align category_theory.monoidal_nat_iso.of_components CategoryTheory.LaxMonoidalNatIso.ofComponents
#align category_theory.monoidal_nat_iso.of_components.hom_app CategoryTheory.LaxMonoidalNatIso.ofComponents_hom_app
#align category_theory.monoidal_nat_iso.of_components.inv_app CategoryTheory.LaxMonoidalNatIso.ofComponents_inv_app

instance isIso_of_isIso_app (ฮฑ : F โถ G) [โ X : C, IsIso (ฮฑ.app X)] : IsIso ฮฑ :=
  โจ(IsIso.of_iso
        (ofComponents (fun X => asIso (ฮฑ.app X)) (fun f => ฮฑ.toNatTrans.naturality f) ฮฑ.unit
          ฮฑ.tensor)).1โฉ
#align category_theory.monoidal_nat_iso.is_iso_of_is_iso_app CategoryTheory.LaxMonoidalNatIso.isIso_of_isIso_app

/-- Construct a lax monoidal natural isomorphism from a natural isomorphism of
underlying functors and coherence of the forward direction. -/
@[simps! hom_app inv_app]
def ofNatIso (ฮฑ : F.toFunctor โ G.toFunctor)
    (unit' : F.ฮท โซ (ฮฑ.app (๐_ C)).hom = G.ฮท := by aesop_cat)
    (tensor' : โ X Y,
      F.ฮผ X Y โซ (ฮฑ.app (X โ Y)).hom =
        ((ฮฑ.app X).hom โ (ฮฑ.app Y).hom) โซ G.ฮผ X Y := by aesop_cat) : F โ G :=
  ofComponents ฮฑ.app

end LaxMonoidalNatIso

namespace LaxMonoidalFunctor

/- The left unitor for functors, upgraded to a lax natural transformation. -/
@[simps! hom_app inv_app]
def leftUnitor (F : C โฅคโโ D) : .id C โโ F โ F :=
  LaxMonoidalNatIso.ofNatIso F.toFunctor.leftUnitor

/- The right unitor for functors, upgraded to a lax natural transformation. -/
@[simps! hom_app inv_app]
def rightUnitor (F : C โฅคโโ D) : F โโ .id D โ F :=
  LaxMonoidalNatIso.ofNatIso F.toFunctor.rightUnitor

/- The associator for functors, upgraded to a lax natural transformation. -/
@[simps! hom_app inv_app]
def associator (F : B โฅคโโ C) (G : C โฅคโโ D) (H : D โฅคโโ E) :
    (F โโ G) โโ H โ F โโ (G โโ H) :=
  LaxMonoidalNatIso.ofNatIso (Functor.associator _ _ _)

end LaxMonoidalFunctor

namespace ColaxMonoidalNatTrans

@[simps]
def equivVGlobularSquare (F G : C โฅคโc D) :
    ColaxMonoidalNatTrans F G โ MonoidalSq (.id C) G F (.id D) where
  toFun ฮฑ := { constraint := F.leftUnitor.hom โซ ฮฑ.toNatTrans โซ G.rightUnitor.inv }
  invFun ฯ := { F.leftUnitor.inv โซ ฯ.constraint โซ G.rightUnitor.hom with
                counit := by simpa using ฯ.trapezoid
                cotensor := fun X Y => by simpa using (ฯ.hexagon_components X Y).symm }
  left_inv ฮฑ := by aesop_cat
  right_inv ฯ := by aesop_cat

/-- The identity colax monoidal natural transformation. -/
@[simps!]
def id (F : C โฅคโc D) : ColaxMonoidalNatTrans F F :=
  LaxMonoidalNatTrans.unop (.id F.op)

instance (F : C โฅคโc D) : Inhabited (ColaxMonoidalNatTrans F F) := โจid Fโฉ

/-- Vertical composition of colax monoidal natural transformations. -/
@[simps!]
def vcomp {F G H : C โฅคโc D} (ฮฑ : ColaxMonoidalNatTrans F G)
    (ฮฒ : ColaxMonoidalNatTrans G H) : ColaxMonoidalNatTrans F H :=
  LaxMonoidalNatTrans.unop (.vcomp ฮฒ.op ฮฑ.op)

end ColaxMonoidalNatTrans

variable (C D)

@[simps! comp_toNatTrans id_toNatTrans]
instance ColaxMonoidalFunctor.category : Category (C โฅคโc D) where
  Hom := ColaxMonoidalNatTrans
  id := .id
  comp ฮฑ ฮฒ := .vcomp ฮฑ ฮฒ

variable {C D}

namespace ColaxMonoidalNatTrans

@[ext]
lemma ext' {F G : C โฅคโc D} {ฮฑ ฮฒ : F โถ G} (w : โ X : C, ฮฑ.app X = ฮฒ.app X) : ฮฑ = ฮฒ :=
  ColaxMonoidalNatTrans.ext _ _ (funext w)

/-- Horizontal composition of colax monoidal natural transformations. -/
@[simps!]
def hcomp {F G : C โฅคโc D} {H K : D โฅคโc E} (ฮฑ : ColaxMonoidalNatTrans F G)
    (ฮฒ : ColaxMonoidalNatTrans H K) : ColaxMonoidalNatTrans (F โโ H) (G โโ K) :=
  { NatTrans.hcomp ฮฑ.toNatTrans ฮฒ.toNatTrans with
    counit := by simp [โ K.toFunctor.map_comp_assoc, โ ฮฒ.naturality_assoc,
                  -map_comp, -NatTrans.naturality, -NatTrans.naturality_assoc]
    cotensor := by simp [โ K.ฮด_natural, โ K.map_comp_assoc,
                  -map_comp, -ColaxMonoidalFunctor.ฮด_natural] }
  -- (ฮฑ.op.hcomp ฮฒ.op).unop gives bad defeqs

/-- The cartesian product of two colax monoidal natural transformations is monoidal. -/
@[simps]
def prod {F G : C โฅคโc D} {H K : C โฅคโc E} (ฮฑ : ColaxMonoidalNatTrans F G)
    (ฮฒ : ColaxMonoidalNatTrans H K) :
    ColaxMonoidalNatTrans (F.prod' H) (G.prod' K) where
  app X := (ฮฑ.app X, ฮฒ.app X)

end ColaxMonoidalNatTrans

section comparison

open Quiver.Hom (op_inj unop_inj)

@[simps!]
def LaxMonoidalNatIso.op {F G : C โฅคโโ D} (ฮฑ : F โ G) : G.op โ F.op where
  hom := LaxMonoidalNatTrans.op ฮฑ.hom
  inv := LaxMonoidalNatTrans.op ฮฑ.inv
  hom_inv_id := ColaxMonoidalNatTrans.ext' fun X => unop_inj <|
    show (ฮฑ.inv โซ ฮฑ.hom).toNatTrans.app _ = _ by simp
  inv_hom_id := ColaxMonoidalNatTrans.ext' fun X => unop_inj <|
    show (ฮฑ.hom โซ ฮฑ.inv).toNatTrans.app _ = _ by simp

@[simps!]
def LaxMonoidalNatIso.unop {F G : C โฅคโc D} (ฮฑ : F.op โ G.op) : G โ F where
  hom := LaxMonoidalNatTrans.unop ฮฑ.hom
  inv := LaxMonoidalNatTrans.unop ฮฑ.inv
  hom_inv_id := ColaxMonoidalNatTrans.ext' fun X => op_inj <|
    show (ฮฑ.inv โซ ฮฑ.hom).toNatTrans.app _ = _ by simp
  inv_hom_id := ColaxMonoidalNatTrans.ext' fun X => op_inj <|
    show (ฮฑ.hom โซ ฮฑ.inv).toNatTrans.app _ = _ by simp

@[simps!]
def ColaxMonoidalNatIso.op {F G : C โฅคโc D} (ฮฑ : F โ G) : G.op โ F.op where
  hom := ColaxMonoidalNatTrans.op ฮฑ.hom
  inv := ColaxMonoidalNatTrans.op ฮฑ.inv
  hom_inv_id := LaxMonoidalNatTrans.ext' fun X => unop_inj <|
    show (ฮฑ.inv โซ ฮฑ.hom).toNatTrans.app _ = _ by simp
  inv_hom_id := LaxMonoidalNatTrans.ext' fun X => unop_inj <|
    show (ฮฑ.hom โซ ฮฑ.inv).toNatTrans.app _ = _ by simp

@[simps!]
def ColaxMonoidalNatIso.unop {F G : C โฅคโโ D} (ฮฑ : F.op โ G.op) : G โ F where
  hom := ColaxMonoidalNatTrans.unop ฮฑ.hom
  inv := ColaxMonoidalNatTrans.unop ฮฑ.inv
  hom_inv_id := LaxMonoidalNatTrans.ext' fun X => op_inj <|
    show (ฮฑ.inv โซ ฮฑ.hom).toNatTrans.app _ = _ by simp
  inv_hom_id := LaxMonoidalNatTrans.ext' fun X => op_inj <|
    show (ฮฑ.hom โซ ฮฑ.inv).toNatTrans.app _ = _ by simp

end comparison

namespace ColaxMonoidalNatIso

variable {F G : C โฅคโc D}

/-- Construct a colax monoidal natural isomorphism from object level isomorphisms,
and the monoidal naturality in the forward direction. -/
@[simps! hom_app inv_app]
def ofComponents (app : โ X : C, F.obj X โ G.obj X)
    (naturality' : โ {X Y : C} (f : X โถ Y),
      F.map f โซ (app Y).hom = (app X).hom โซ G.map f := by aesop_cat)
    (counit' : (app (๐_ C)).hom โซ G.ฮต = F.ฮต := by aesop_cat)
    (cotensor' : โ X Y,
      F.ฮด X Y โซ ((app X).hom โ (app Y).hom) =
        (app (X โ Y)).hom โซ G.ฮด X Y := by aesop_cat) : F โ G :=
  LaxMonoidalNatIso.unop <| LaxMonoidalNatIso.ofComponents (fun X => (app X.unop).op)
    (fun f => Quiver.Hom.unop_inj <| Eq.symm <| naturality' f.unop)
    (Quiver.Hom.unop_inj counit')
    (fun X Y => Quiver.Hom.unop_inj (cotensor' X.unop Y.unop).symm)

instance isIso_of_isIso_app (ฮฑ : F โถ G) [โ X : C, IsIso (ฮฑ.app X)] : IsIso ฮฑ :=
  โจ(IsIso.of_iso
        (ofComponents (fun X => asIso (ฮฑ.app X)) (fun f => ฮฑ.toNatTrans.naturality f) ฮฑ.counit
          ฮฑ.cotensor)).1โฉ

/-- Construct a colax monoidal natural isomorphism from a natural isomorphism
of underlying functors and coherence of the forward direction. -/
@[simps! hom_app inv_app]
def ofNatIso (ฮฑ : F.toFunctor โ G.toFunctor)
    (counit' : (ฮฑ.app (๐_ C)).hom โซ G.ฮต = F.ฮต := by aesop_cat)
    (cotensor' : โ X Y,
      F.ฮด X Y โซ ((ฮฑ.app X).hom โ (ฮฑ.app Y).hom) =
        (ฮฑ.app (X โ Y)).hom โซ G.ฮด X Y := by aesop_cat) : F โ G :=
  ofComponents ฮฑ.app

end ColaxMonoidalNatIso

namespace ColaxMonoidalFunctor

/- The left unitor for functors, upgraded to a colax natural transformation. -/
@[simps! hom_app inv_app]
def leftUnitor (F : C โฅคโc D) : .id C โโ F โ F :=
  ColaxMonoidalNatIso.ofNatIso F.toFunctor.leftUnitor

/- The right unitor for functors, upgraded to a colax natural transformation. -/
@[simps! hom_app inv_app]
def rightUnitor (F : C โฅคโc D) : F โโ .id D โ F :=
  ColaxMonoidalNatIso.ofNatIso F.toFunctor.rightUnitor

/- The associator for functors, upgraded to a colax natural transformation. -/
@[simps! hom_app inv_app]
def associator (F : B โฅคโc C) (G : C โฅคโc D) (H : D โฅคโc E) :
    (F โโ G) โโ H โ F โโ (G โโ H) :=
  ColaxMonoidalNatIso.ofNatIso (Functor.associator _ _ _)

end ColaxMonoidalFunctor

namespace MonoidalNatTrans

def mkOfLax {F G : C โฅคโs D}
    (ฮฑ : LaxMonoidalNatTrans F.toLaxMonoidalFunctor G.toLaxMonoidalFunctor) :
    MonoidalNatTrans F G where
  counit := (cancel_epi F.ฮท).mp <| by simp
  cotensor := fun X Y => (cancel_epi (F.ฮผ X Y)).mp <| by simp
  __ := ฮฑ

@[simp] lemma mkOfLax_toNatTrans {F G : C โฅคโs D}
    (ฮฑ : LaxMonoidalNatTrans F.toLaxMonoidalFunctor G.toLaxMonoidalFunctor) :
    (mkOfLax ฮฑ).toNatTrans = ฮฑ.toNatTrans := rfl

@[simps]
def mkOfColax {F G : C โฅคโs D}
    (ฮฑ : ColaxMonoidalNatTrans F.toColaxMonoidalFunctor G.toColaxMonoidalFunctor) :
    MonoidalNatTrans F G where
  unit := (cancel_mono G.ฮต).mp <| by
    have := ฮฑ.counit; dsimp at this; simp [this]
  tensor := fun X Y => (cancel_mono (G.ฮด X Y)).mp <| by
    have := ฮฑ.cotensor X Y; dsimp at this; simp [โ this]
  __ := ฮฑ

/-- The identity monoidal natural transformation. -/
@[simps!]
def id (F : C โฅคโs D) : MonoidalNatTrans F F where
  __ := LaxMonoidalNatTrans.id F.toLaxMonoidalFunctor
  __ := ColaxMonoidalNatTrans.id F.toColaxMonoidalFunctor

instance (F : C โฅคโs D) : Inhabited (MonoidalNatTrans F F) := โจid Fโฉ

/-- Vertical composition of monoidal natural transformations. -/
@[simps!]
def vcomp {F G H : C โฅคโs D} (ฮฑ : MonoidalNatTrans F G)
    (ฮฒ : MonoidalNatTrans G H) : MonoidalNatTrans F H where
  __ := ฮฑ.toLaxMonoidalNatTrans.vcomp ฮฒ.toLaxMonoidalNatTrans
  __ := ฮฑ.toColaxMonoidalNatTrans.vcomp ฮฒ.toColaxMonoidalNatTrans

end MonoidalNatTrans

variable (C D)

@[simps! comp_toNatTrans id_toNatTrans]
instance MonoidalFunctor.category : Category (C โฅคโs D) where
  Hom := MonoidalNatTrans
  id := .id
  comp ฮฑ ฮฒ := .vcomp ฮฑ ฮฒ
#align category_theory.monoidal_nat_trans.category_monoidal_functor CategoryTheory.MonoidalFunctor.category

variable {C D}

namespace MonoidalNatTrans

@[ext]
lemma ext' {F G : C โฅคโs D} {ฮฑ ฮฒ : F โถ G} (w : โ X : C, ฮฑ.app X = ฮฒ.app X) : ฮฑ = ฮฒ :=
  MonoidalNatTrans.ext _ _ (funext w)

/-- Horizontal composition of monoidal natural transformations. -/
@[simps!]
def hcomp {F G : C โฅคโs D} {H K : D โฅคโs E} (ฮฑ : MonoidalNatTrans F G)
    (ฮฒ : MonoidalNatTrans H K) : MonoidalNatTrans (F โโ H) (G โโ K) where
  __ := ฮฑ.toLaxMonoidalNatTrans.hcomp ฮฒ.toLaxMonoidalNatTrans
  __ := ฮฑ.toColaxMonoidalNatTrans.hcomp ฮฒ.toColaxMonoidalNatTrans

/-- The cartesian product of two monoidal natural transformations is monoidal. -/
@[simps!]
def prod {F G : C โฅคโs D} {H K : C โฅคโs E} (ฮฑ : MonoidalNatTrans F G)
    (ฮฒ : MonoidalNatTrans H K) :
    MonoidalNatTrans (F.prod' H) (G.prod' K) where
  __ := ฮฑ.toLaxMonoidalNatTrans.prod ฮฒ.toLaxMonoidalNatTrans
  __ := ฮฑ.toColaxMonoidalNatTrans.prod ฮฒ.toColaxMonoidalNatTrans

end MonoidalNatTrans

namespace MonoidalNatIso

variable {F G : C โฅคโs D}

/-- Construct a monoidal natural isomorphism from object level isomorphisms,
and lax monoidal naturality in the forward direction. -/
@[simps! hom_app inv_app]
def ofLaxComponents (app : โ X : C, F.obj X โ G.obj X)
    (naturality' : โ {X Y : C} (f : X โถ Y),
      F.map f โซ (app Y).hom = (app X).hom โซ G.map f := by aesop_cat)
    (unit' : F.ฮท โซ (app (๐_ C)).hom = G.ฮท := by aesop_cat)
    (tensor' : โ X Y,
      F.ฮผ X Y โซ (app (X โ Y)).hom =
        ((app X).hom โ (app Y).hom) โซ G.ฮผ X Y := by aesop_cat) : F โ G where
  hom := .mkOfLax <| Iso.hom <|
    LaxMonoidalNatIso.ofComponents app naturality' unit' tensor'
  inv := .mkOfLax <| Iso.inv <|
    LaxMonoidalNatIso.ofComponents app naturality' unit' tensor'

/-- Construct a monoidal natural isomorphism from object level isomorphisms,
and colax monoidal naturality in the forward direction. -/
@[simps! hom_app inv_app]
def ofColaxComponents (app : โ X : C, F.obj X โ G.obj X)
    (naturality' : โ {X Y : C} (f : X โถ Y),
      F.map f โซ (app Y).hom = (app X).hom โซ G.map f := by aesop_cat)
    (counit' : (app (๐_ C)).hom โซ G.ฮต = F.ฮต := by aesop_cat)
    (cotensor' : โ X Y,
      F.ฮด X Y โซ ((app X).hom โ (app Y).hom) =
        (app (X โ Y)).hom โซ G.ฮด X Y := by aesop_cat) : F โ G where
  hom := .mkOfColax <| Iso.hom <|
    ColaxMonoidalNatIso.ofComponents app naturality' counit' cotensor'
  inv := .mkOfColax <| Iso.inv <|
    ColaxMonoidalNatIso.ofComponents app naturality' counit' cotensor'

instance isIso_of_isIso_app (ฮฑ : F โถ G) [โ X : C, IsIso (ฮฑ.app X)] : IsIso ฮฑ :=
  โจ(IsIso.of_iso
        (ofLaxComponents (fun X => asIso (ฮฑ.app X)) (fun f => ฮฑ.toNatTrans.naturality f) ฮฑ.unit
          ฮฑ.tensor)).1โฉ

/-- Construct a monoidal natural isomorphism from a natural isomorphism
of underlying functors and lax coherence of the forward direction. -/
@[simps! hom_app inv_app]
def ofNatIsoLax (ฮฑ : F.toFunctor โ G.toFunctor)
    (unit' : F.ฮท โซ (ฮฑ.app (๐_ C)).hom = G.ฮท := by aesop_cat)
    (tensor' : โ X Y,
      F.ฮผ X Y โซ (ฮฑ.app (X โ Y)).hom =
        ((ฮฑ.app X).hom โ (ฮฑ.app Y).hom) โซ G.ฮผ X Y := by aesop_cat) : F โ G :=
  ofLaxComponents ฮฑ.app

/-- Construct a monoidal natural isomorphism from a natural isomorphism
of underlying functors and colax coherence of the forward direction. -/
@[simps! hom_app inv_app]
def ofNatIsoColax (ฮฑ : F.toFunctor โ G.toFunctor)
    (counit' : (ฮฑ.app (๐_ C)).hom โซ G.ฮต = F.ฮต := by aesop_cat)
    (cotensor' : โ X Y,
      F.ฮด X Y โซ ((ฮฑ.app X).hom โ (ฮฑ.app Y).hom) =
        (ฮฑ.app (X โ Y)).hom โซ G.ฮด X Y := by aesop_cat) : F โ G :=
  ofColaxComponents ฮฑ.app (fun f => ฮฑ.hom.naturality f)

end MonoidalNatIso

namespace MonoidalFunctor

/- The left unitor for functors, upgraded to a monoidal natural transformation. -/
@[simps! hom_app inv_app]
def leftUnitor (F : C โฅคโs D) : .id C โโ F โ F :=
  MonoidalNatIso.ofNatIsoLax F.toFunctor.leftUnitor

/- The right unitor for functors, upgraded to a monoidal natural transformation. -/
@[simps! hom_app inv_app]
def rightUnitor (F : C โฅคโs D) : F โโ .id D โ F :=
  MonoidalNatIso.ofNatIsoLax F.toFunctor.rightUnitor

/- The associator for functors, upgraded to a monoidal natural transformation. -/
@[simps! hom_app inv_app]
def associator (F : B โฅคโs C) (G : C โฅคโs D) (H : D โฅคโs E) :
    (F โโ G) โโ H โ F โโ (G โโ H) :=
  MonoidalNatIso.ofNatIsoLax (Functor.associator _ _ _)

end MonoidalFunctor

variable (C D)

/-- The functor which takes the underlying lax monoidal functor of a
strong monoidal functor. -/
@[simps]
def MonoidalFunctor.toLax : (C โฅคโs D) โฅค (C โฅคโโ D) where
  obj F := F.toLaxMonoidalFunctor
  map ฮฑ := ฮฑ.toLaxMonoidalNatTrans

instance : Faithful (MonoidalFunctor.toLax C D) where

@[simps]
instance : Full (MonoidalFunctor.toLax C D) where
  preimage f := MonoidalNatTrans.mkOfLax f

/-- The isomorphism witnessing that the lax monoidal functor underlying the
identity strong monoidal functor is the lax monoidal identity functor. -/
@[simps!]
def MonoidalFunctor.toLax_id_iso_id :
    (MonoidalFunctor.toLax C C).obj (.id C) โ LaxMonoidalFunctor.id C := Iso.refl _

/-- The functor which takes the underlying colax monoidal functor of a
strong monoidal functor. -/
@[simps obj map]
def MonoidalFunctor.toColax : (C โฅคโs D) โฅค (C โฅคโc D) where
  obj F := F.toColaxMonoidalFunctor
  map ฮฑ := ฮฑ.toColaxMonoidalNatTrans

instance : Faithful (MonoidalFunctor.toColax C D) where
  map_injective h := by ext X; exact congrArg (fun t => t.toNatTrans.app X) h

@[simps]
instance : Full (MonoidalFunctor.toColax C D) where
  preimage f := MonoidalNatTrans.mkOfColax f

/-- The isomorphism witnessing that the colax monoidal functor underlying the
identity strong monoidal functor is the colax monoidal identity functor. -/
@[simps!]
def MonoidalFunctor.toColax_id_iso_id :
    (MonoidalFunctor.toColax C C).obj (.id C) โ ColaxMonoidalFunctor.id C := Iso.refl _

variable {C D}

/-- The isomorphism witnessing that the lax monoidal functor underlying the
composition of strong monoidal functor is the composition of the
underlying lax monoidal functors. -/
@[simps!]
def MonoidalFunctor.toLax_comp_iso_comp (F : C โฅคโs D) (G : D โฅคโs E) :
    (MonoidalFunctor.toLax C E).obj (F โโ G) โ
      (MonoidalFunctor.toLax C D).obj F โโ (MonoidalFunctor.toLax D E).obj G :=
  Iso.refl _

/-- The isomorphism witnessing that the colax monoidal functor underlying the
composition of strong monoidal functor is the composition of the
underlying colax monoidal functors. -/
@[simps!]
def MonoidalFunctor.toColax_comp_iso_comp (F : C โฅคโs D)
    (G : MonoidalFunctor D E) :
    (MonoidalFunctor.toColax C E).obj (F โโ G) โ
      (MonoidalFunctor.toColax C D).obj F โโ (MonoidalFunctor.toColax D E).obj G :=
  Iso.refl _

namespace MonoidalSq

@[simps! constraint]
def vcomp {F : B โฅคโโ C} {G : B โฅคโc D} {H : C โฅคโc E} {K : D โฅคโโ E}
    {I : D โฅคโc M} {J : E โฅคโc N} {L : M โฅคโโ N}
    (ฮฑ : MonoidalSq F G H K) (ฮฒ : MonoidalSq K I J L) :
    MonoidalSq F (G โโ I) (H โโ J) L where
  trapezoid' := by
    dsimp
    simp_rw [comp_id, id_comp, map_comp, assoc, โ ฮฒ.naturality_assoc,
            โ J.map_comp_assoc, trapezoid, J.map_comp_assoc, trapezoid]
  hexagon' := fun X Y => by
    dsimp
    simp only [comp_id, id_comp, map_comp, assoc, tensor_comp]
    simp_rw [โ ฮฒ.naturality_assoc, โ J.map_comp_assoc, ฮฑ.hexagon_components,
            J.map_comp_assoc, ฮฒ.hexagon_components]
    erw [J.ฮด_natural_assoc]
  __ := ฮฑ.toCatColaxSq.vComp ฮฒ.toCatColaxSq

@[simps! constraint]
def hcomp {F : B โฅคโโ C} {G : B โฅคโc D} {H : C โฅคโc E} {K : D โฅคโโ E}
    {I : C โฅคโโ M} {J : M โฅคโc N} {L : E โฅคโโ N}
    (ฮฑ : MonoidalSq F G H K) (ฮฒ : MonoidalSq I H J L) :
    MonoidalSq (F โโ I) G J (K โโ L) where
  trapezoid' := by
    dsimp
    simp only [map_comp, comp_id, id_comp, assoc]
    rw [ฮฒ.naturality_assoc, โ L.map_comp, โ L.map_comp, trapezoid,
        L.map_comp, trapezoid_assoc]
  hexagon' := fun X Y => by
    dsimp
    simp only [map_comp, comp_id, id_comp, assoc, tensor_comp,
              LaxMonoidalFunctor.ฮผ_natural_assoc]
    simp_rw [ฮฒ.naturality_assoc, โ L.map_comp, hexagon_components,
            L.map_comp, hexagon_components_assoc]
  __ := ฮฑ.toCatColaxSq.hComp ฮฒ.toCatColaxSq

end MonoidalSq

end CategoryTheory
