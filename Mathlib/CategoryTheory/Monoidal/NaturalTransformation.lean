/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Monoidal.Functor
import Mathlib.CategoryTheory.Functor.FullyFaithful

#align_import category_theory.monoidal.natural_transformation from "leanprover-community/mathlib"@"d047eb4671130d5998b185e49a0443a0d2e9b191"

/-!
# Monoidal natural transformations

Natural transformations between (lax) monoidal functors must satisfy
an additional compatibility relation with the tensorators:
`F.Î¼ X Y â‰« app (X âŠ— Y) = (app X âŠ— app Y) â‰« G.Î¼ X Y`.

(Lax) monoidal functors between a fixed pair of monoidal categories
themselves form a category.
-/

open CategoryTheory

universe vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

open CategoryTheory.Category

open CategoryTheory.Functor

namespace CategoryTheory

open MonoidalCategory

variable {C : Type uâ‚} [Category.{vâ‚} C] [MonoidalCategory.{vâ‚} C]
         {D : Type uâ‚‚} [Category.{vâ‚‚} D] [MonoidalCategory.{vâ‚‚} D]
         {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E] [MonoidalCategory.{vâ‚ƒ} E]

/-- A monoidal natural transformation is a natural transformation between (lax) monoidal functors
additionally satisfying:
`F.Î¼ X Y â‰« app (X âŠ— Y) = (app X âŠ— app Y) â‰« G.Î¼ X Y`
-/
@[ext]
structure MonoidalNatTrans (F G : LaxMonoidalFunctor C D) extends
  NatTrans F.toFunctor G.toFunctor where
  /-- The unit condition for a monoidal natural transformation. -/
  unit : F.Îµ â‰« app (ðŸ™_ C) = G.Îµ := by aesop_cat
  /-- The tensor condition for a monoidal natural transformation. -/
  tensor : âˆ€ X Y, F.Î¼ _ _ â‰« app (X âŠ— Y) = (app X âŠ— app Y) â‰« G.Î¼ _ _ := by aesop_cat
#align category_theory.monoidal_nat_trans CategoryTheory.MonoidalNatTrans

-- Porting note: `reassoc (attr := simp)` seems to add a `simp`
-- attribute to the original lemma as well.
attribute [reassoc (attr := simp)] MonoidalNatTrans.tensor
attribute [reassoc (attr := simp)] MonoidalNatTrans.unit

initialize_simps_projections MonoidalNatTrans (+toNatTrans, -app)

#align category_theory.monoidal_nat_trans.unit CategoryTheory.MonoidalNatTrans.unit
#align category_theory.monoidal_nat_trans.unit_assoc CategoryTheory.MonoidalNatTrans.unit_assoc
#align category_theory.monoidal_nat_trans.tensor CategoryTheory.MonoidalNatTrans.tensor
#align category_theory.monoidal_nat_trans.tensor_assoc CategoryTheory.MonoidalNatTrans.tensor_assoc

namespace MonoidalNatTrans

/-- The identity monoidal natural transformation. -/
@[simps!]
def id (F : LaxMonoidalFunctor C D) : MonoidalNatTrans F F :=
  { ðŸ™ F.toFunctor with }
#align category_theory.monoidal_nat_trans.id CategoryTheory.MonoidalNatTrans.id

instance (F : LaxMonoidalFunctor C D) : Inhabited (MonoidalNatTrans F F) :=
  âŸ¨id FâŸ©

/-- Vertical composition of monoidal natural transformations. -/
@[simps!]
def vcomp {F G H : LaxMonoidalFunctor C D} (Î± : MonoidalNatTrans F G) (Î² : MonoidalNatTrans G H) :
    MonoidalNatTrans F H :=
  { NatTrans.vcomp Î±.toNatTrans Î².toNatTrans with }
#align category_theory.monoidal_nat_trans.vcomp CategoryTheory.MonoidalNatTrans.vcomp

end MonoidalNatTrans

variable (C D)

instance LaxMonoidalFunctor.categoryLaxMonoidalFunctor :
    Category (LaxMonoidalFunctor C D) where
  Hom := MonoidalNatTrans
  id := MonoidalNatTrans.id
  comp Î± Î² := MonoidalNatTrans.vcomp Î± Î²
#align category_theory.monoidal_nat_trans.category_lax_monoidal_functor CategoryTheory.LaxMonoidalFunctor.categoryLaxMonoidalFunctor

instance MonoidalFunctor.categoryMonoidalFunctor : Category (MonoidalFunctor C D) :=
  InducedCategory.category MonoidalFunctor.toLaxMonoidalFunctor
#align category_theory.monoidal_nat_trans.category_monoidal_functor CategoryTheory.MonoidalFunctor.categoryMonoidalFunctor

def MonoidalFunctor.forget : MonoidalFunctor C D â¥¤ LaxMonoidalFunctor C D :=
  inducedFunctor _

instance : Faithful (MonoidalFunctor.forget C D) :=
  inferInstanceAs (Faithful (inducedFunctor _))

instance : Full (MonoidalFunctor.forget C D) :=
  inferInstanceAs (Full (inducedFunctor _))

/-- The isomorphism witnessing that the lax monoidal functor underlying the
identity strong monoidal functor is the lax monoidal identity functor. -/
@[simps!]
def MonoidalFunctor.forget_id_iso_id :
    (MonoidalFunctor.forget C C).obj (.id C) â‰… LaxMonoidalFunctor.id C := Iso.refl _

variable {C D}

/-- The isomorphism witnessing that the lax monoidal functor underlying the
composition of strong monoidal functor is the composition of the
underlying lax monoidal functors. -/
@[simps!]
def MonoidalFunctor.forget_comp_iso_comp (F : MonoidalFunctor C D)
    (G : MonoidalFunctor D E) :
    (MonoidalFunctor.forget C E).obj (F âŠ—â‹™ G) â‰…
      (MonoidalFunctor.forget C D).obj F âŠ—â‹™ (MonoidalFunctor.forget D E).obj G :=
  Iso.refl _

namespace MonoidalNatTrans

@[simp]
theorem comp_toNatTrans_lax {F G H : LaxMonoidalFunctor C D} {Î± : F âŸ¶ G} {Î² : G âŸ¶ H} :
    (Î± â‰« Î²).toNatTrans = @CategoryStruct.comp (C â¥¤ D) _ _ _ _ Î±.toNatTrans Î².toNatTrans :=
  rfl
#align category_theory.monoidal_nat_trans.comp_to_nat_trans_lax CategoryTheory.MonoidalNatTrans.comp_toNatTrans_lax

-- Porting note: added, as `MonoidalNatTrans.ext` does not apply to morphisms.
@[ext]
lemma ext' {F G : LaxMonoidalFunctor C D} {Î± Î² : F âŸ¶ G} (w : âˆ€ X : C, Î±.app X = Î².app X) : Î± = Î² :=
  MonoidalNatTrans.ext _ _ (funext w)

@[simp]
theorem comp_toNatTrans {F G H : MonoidalFunctor C D} {Î± : F âŸ¶ G} {Î² : G âŸ¶ H} :
    (Î± â‰« Î²).toNatTrans = @CategoryStruct.comp (C â¥¤ D) _ _ _ _ Î±.toNatTrans Î².toNatTrans :=
  rfl
#align category_theory.monoidal_nat_trans.comp_to_nat_trans CategoryTheory.MonoidalNatTrans.comp_toNatTrans

variable {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E] [MonoidalCategory.{vâ‚ƒ} E]

/-- Horizontal composition of monoidal natural transformations. -/
@[simps]
def hcomp {F G : LaxMonoidalFunctor C D} {H K : LaxMonoidalFunctor D E} (Î± : MonoidalNatTrans F G)
    (Î² : MonoidalNatTrans H K) : MonoidalNatTrans (F âŠ—â‹™ H) (G âŠ—â‹™ K) :=
  { NatTrans.hcomp Î±.toNatTrans Î².toNatTrans with
    unit := by
      dsimp; simp
      conv_lhs => rw [â† K.toFunctor.map_comp, Î±.unit]
    tensor := fun X Y => by
      dsimp; simp
      conv_lhs => rw [â† K.toFunctor.map_comp, Î±.tensor, K.toFunctor.map_comp] }
#align category_theory.monoidal_nat_trans.hcomp CategoryTheory.MonoidalNatTrans.hcomp

section

attribute [local simp] NatTrans.naturality MonoidalNatTrans.unit MonoidalNatTrans.tensor

/-- The cartesian product of two monoidal natural transformations is monoidal. -/
@[simps]
def prod {F G : LaxMonoidalFunctor C D} {H K : LaxMonoidalFunctor C E} (Î± : MonoidalNatTrans F G)
    (Î² : MonoidalNatTrans H K) : MonoidalNatTrans (F.prod' H) (G.prod' K) where
  app X := (Î±.app X, Î².app X)
#align category_theory.monoidal_nat_trans.prod CategoryTheory.MonoidalNatTrans.prod

end

end MonoidalNatTrans

namespace MonoidalNatIso

variable {F G : LaxMonoidalFunctor C D}

/-- Construct a monoidal natural isomorphism from object level isomorphisms,
and the monoidal naturality in the forward direction. -/
def ofComponents (app : âˆ€ X : C, F.obj X â‰… G.obj X)
    (naturality' : âˆ€ {X Y : C} (f : X âŸ¶ Y), F.map f â‰« (app Y).hom = (app X).hom â‰« G.map f)
    (unit' : F.Îµ â‰« (app (ðŸ™_ C)).hom = G.Îµ)
    (tensor' : âˆ€ X Y, F.Î¼ X Y â‰« (app (X âŠ— Y)).hom = ((app X).hom âŠ— (app Y).hom) â‰« G.Î¼ X Y) :
    F â‰… G where
  hom := { app := fun X => (app X).hom }
  inv := {
    (NatIso.ofComponents app @naturality').inv with
    app := fun X => (app X).inv
    unit := by
      dsimp
      rw [â† unit', assoc, Iso.hom_inv_id, comp_id]
    tensor := fun X Y => by
      dsimp
      rw [Iso.comp_inv_eq, assoc, tensor', â† tensor_comp_assoc,
        Iso.inv_hom_id, Iso.inv_hom_id, tensor_id, id_comp] }
#align category_theory.monoidal_nat_iso.of_components CategoryTheory.MonoidalNatIso.ofComponents

@[simp]
theorem ofComponents.hom_app (app : âˆ€ X : C, F.obj X â‰… G.obj X) (naturality) (unit) (tensor) (X) :
    (ofComponents app naturality unit tensor).hom.app X = (app X).hom :=
  rfl
#align category_theory.monoidal_nat_iso.of_components.hom_app CategoryTheory.MonoidalNatIso.ofComponents.hom_app

@[simp]
theorem ofComponents.inv_app (app : âˆ€ X : C, F.obj X â‰… G.obj X) (naturality) (unit) (tensor) (X) :
    (ofComponents app naturality unit tensor).inv.app X = (app X).inv := by simp [ofComponents]
#align category_theory.monoidal_nat_iso.of_components.inv_app CategoryTheory.MonoidalNatIso.ofComponents.inv_app

instance isIso_of_isIso_app (Î± : F âŸ¶ G) [âˆ€ X : C, IsIso (Î±.app X)] : IsIso Î± :=
  âŸ¨(IsIso.of_iso
        (ofComponents (fun X => asIso (Î±.app X)) (fun f => Î±.toNatTrans.naturality f) Î±.unit
          Î±.tensor)).1âŸ©
#align category_theory.monoidal_nat_iso.is_iso_of_is_iso_app CategoryTheory.MonoidalNatIso.isIso_of_isIso_app

end MonoidalNatIso

section

/-- The unit of a monoidal adjunction can be upgraded to a monoidal natural transformation. -/
@[simps toNatTrans]
def Adjunction.monoidalUnit (F : MonoidalFunctor C D) [IsLeftAdjoint F.toFunctor] :
    LaxMonoidalFunctor.id C âŸ¶ F.toLaxMonoidalFunctor âŠ—â‹™ monoidalAdjoint F where
  toNatTrans := IsLeftAdjoint.adj.unit
  unit := (IsLeftAdjoint.adj.homEquiv _ _).symm.injective <| by
    simp [F.ÎµIso.eq_inv_comp, â† tensor_comp_assoc]
  tensor X Y := (IsLeftAdjoint.adj.homEquiv _ _).symm.injective <| by
    simp [(F.Î¼Iso X Y).eq_inv_comp, â† tensor_comp_assoc]

/-- The unit of a monoidal equivalence can be upgraded to a monoidal natural transformation. -/
@[simps!] -- Porting note: have to manually specify the toNatTrans projection
def Equivalence.monoidalUnitIso (F : MonoidalFunctor C D) [IsEquivalence F.toFunctor] :
    MonoidalFunctor.id C â‰… F âŠ—â‹™ monoidalInverse F :=
  let Î· := Adjunction.monoidalUnit F
  (isoEquivOfFullyFaithful (MonoidalFunctor.forget _ _)).symm <|
    MonoidalNatIso.ofComponents (fun X => IsEquivalence.unitIso.app X)
      (fun f => Î·.naturality f) Î·.unit Î·.tensor

/-- The counit of a monoidal adjunction can be upgraded to a monoidal natural transformation. -/
@[simps toNatTrans]
def Adjunction.monoidalCounit (F : MonoidalFunctor C D) [IsLeftAdjoint F.toFunctor] :
    monoidalAdjoint F âŠ—â‹™ F.toLaxMonoidalFunctor âŸ¶ LaxMonoidalFunctor.id D where
  toNatTrans := IsLeftAdjoint.adj.counit

/-- The counit of a monoidal equivalence can be upgraded to a monoidal natural transformation. -/
@[simps!] -- Porting note: have to manually specify the toNatTrans projection
def Equivalence.monoidalCounitIso (F : MonoidalFunctor C D) [IsEquivalence F.toFunctor] :
    monoidalInverse F âŠ—â‹™ F â‰… MonoidalFunctor.id D :=
  let Î· := Adjunction.monoidalCounit F
  (isoEquivOfFullyFaithful (MonoidalFunctor.forget _ _)).symm <|
    MonoidalNatIso.ofComponents (fun X => IsEquivalence.counitIso.app X)
      (fun f => Î·.naturality f) Î·.unit Î·.tensor

end

end CategoryTheory
