/-
Copyright (c) 2021 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Monoidal.Free.Basic
import Mathlib.CategoryTheory.Discrete.Basic

/-!
# The monoidal coherence theorem

In this file, we prove the monoidal coherence theorem, stated in the following form: the free
monoidal category over any type `C` is thin.

We follow a proof described by Ilya Beylin and Peter Dybjer, which has been previously formalized
in the proof assistant ALF. The idea is to declare a normal form (with regard to association and
adding units) on objects of the free monoidal category and consider the discrete subcategory of
objects that are in normal form. A normalization procedure is then just a functor
`fullNormalize : FreeMonoidalCategory C ‚•§ Discrete (NormalMonoidalObject C)`, where
functoriality says that two objects which are related by associators and unitors have the
same normal form. Another desirable property of a normalization procedure is that an object is
isomorphic (i.e., related via associators and unitors) to its normal form. In the case of the
specific normalization procedure we use we not only get these isomorphisms, but also that they
assemble into a natural isomorphism `ùü≠ (FreeMonoidalCategory C) ‚âÖ fullNormalize ‚ãô inclusion`.
But this means that any two parallel morphisms in the free monoidal category factor through a
discrete category in the same way, so they must be equal, and hence the free monoidal category
is thin.

## References

* [Ilya Beylin and Peter Dybjer, Extracting a proof of coherence for monoidal categories from a
  proof of normalization for monoids][beylin1996]

-/


universe u

namespace CategoryTheory

open MonoidalCategory Functor

namespace FreeMonoidalCategory

variable {C : Type u}

section

variable (C)

/-- We say an object in the free monoidal category is in normal form if it is of the form
`(((ùüô_ C) ‚äó X‚ÇÅ) ‚äó X‚ÇÇ) ‚äó ‚ãØ`. -/
inductive NormalMonoidalObject : Type u
  | unit : NormalMonoidalObject
  | tensor : NormalMonoidalObject ‚Üí C ‚Üí NormalMonoidalObject

end

local notation "F" => FreeMonoidalCategory

local notation "N" => Discrete ‚àò NormalMonoidalObject

local infixr:10 " ‚ü∂·µê " => Hom

instance (x y : N C) : Subsingleton (x ‚ü∂ y) := Discrete.instSubsingletonDiscreteHom _ _

/-- Auxiliary definition for `inclusion`. -/
@[simp]
def inclusionObj : NormalMonoidalObject C ‚Üí F C
  | NormalMonoidalObject.unit => unit
  | NormalMonoidalObject.tensor n a => tensor (inclusionObj n) (of a)

/-- The discrete subcategory of objects in normal form includes into the free monoidal category. -/
def inclusion : N C ‚•§ F C :=
  Discrete.functor inclusionObj

@[simp]
theorem inclusion_obj (X : N C) :
    inclusion.obj X = inclusionObj X.as :=
  rfl

@[simp]
theorem inclusion_map {X Y : N C} (f : X ‚ü∂ Y) :
    inclusion.map f = eqToHom (congr_arg _ (Discrete.ext (Discrete.eq_of_hom f))) := rfl

/-- Auxiliary definition for `normalize`. -/
def normalizeObj : F C ‚Üí NormalMonoidalObject C ‚Üí NormalMonoidalObject C
  | unit, n => n
  | of X, n => NormalMonoidalObject.tensor n X
  | tensor X Y, n => normalizeObj Y (normalizeObj X n)

@[simp]
theorem normalizeObj_unitor (n : NormalMonoidalObject C) : normalizeObj (ùüô_ (F C)) n = n :=
  rfl

@[simp]
theorem normalizeObj_tensor (X Y : F C) (n : NormalMonoidalObject C) :
    normalizeObj (X ‚äó Y) n = normalizeObj Y (normalizeObj X n) :=
  rfl

/-- Auxiliary definition for `normalize`. -/
def normalizeObj' (X : F C) : N C ‚•§ N C := Discrete.functor fun n ‚Ü¶ ‚ü®normalizeObj X n‚ü©

section

open Hom

/-- Auxiliary definition for `normalize`. Here we prove that objects that are related by
associators and unitors map to the same normal form. -/
@[simp]
def normalizeMapAux : ‚àÄ {X Y : F C}, (X ‚ü∂·µê Y) ‚Üí (normalizeObj' X ‚ü∂ normalizeObj' Y)
  | _, _, Hom.id _ => ùüô _
  | _, _, Œ±_hom X Y Z => by dsimp; exact Discrete.natTrans (fun _ => ùüô _)
  | _, _, Œ±_inv _ _ _ => by dsimp; exact Discrete.natTrans (fun _ => ùüô _)
  | _, _, l_hom _ => by dsimp; exact Discrete.natTrans (fun _ => ùüô _)
  | _, _, l_inv _ => by dsimp; exact Discrete.natTrans (fun _ => ùüô _)
  | _, _, œÅ_hom _ => by dsimp; exact Discrete.natTrans (fun _ => ùüô _)
  | _, _, œÅ_inv _ => by dsimp; exact Discrete.natTrans (fun _ => ùüô _)
  | _, _, (@Hom.comp _ _ _ _ f g) => normalizeMapAux f ‚â´ normalizeMapAux g
  | _, _, (@Hom.tensor _ T _ _ W f g) =>
    Discrete.natTrans <| fun ‚ü®X‚ü© => (normalizeMapAux g).app ‚ü®normalizeObj T X‚ü© ‚â´
      (normalizeObj' W).map ((normalizeMapAux f).app ‚ü®X‚ü©)
  | _, _, (@Hom.whiskerLeft _ T _ W f) =>
    Discrete.natTrans <| fun ‚ü®X‚ü© => (normalizeMapAux f).app ‚ü®normalizeObj T X‚ü©
  | _, _, (@Hom.whiskerRight _ T _ f W) =>
    Discrete.natTrans <| fun X => (normalizeObj' W).map <| (normalizeMapAux f).app X

end

section

variable (C)

/-- Our normalization procedure works by first defining a functor `F C ‚•§ (N C ‚•§ N C)` (which turns
out to be very easy), and then obtain a functor `F C ‚•§ N C` by plugging in the normal object
`ùüô_ C`. -/
@[simp]
def normalize : F C ‚•§ N C ‚•§ N C where
  obj X := normalizeObj' X
  map {X Y} := Quotient.lift normalizeMapAux (by cat_disch)

/-- A variant of the normalization functor where we consider the result as an object in the free
monoidal category (rather than an object of the discrete subcategory of objects in normal form). -/
@[simp]
def normalize' : F C ‚•§ N C ‚•§ F C :=
  normalize C ‚ãô (whiskeringRight _ _ _).obj inclusion

/-- The normalization functor for the free monoidal category over `C`. -/
def fullNormalize : F C ‚•§ N C where
  obj X := ((normalize C).obj X).obj ‚ü®NormalMonoidalObject.unit‚ü©
  map f := ((normalize C).map f).app ‚ü®NormalMonoidalObject.unit‚ü©

/-- Given an object `X` of the free monoidal category and an object `n` in normal form, taking
the tensor product `n ‚äó X` in the free monoidal category is functorial in both `X` and `n`. -/
@[simp]
def tensorFunc : F C ‚•§ N C ‚•§ F C where
  obj X := Discrete.functor fun n => inclusion.obj ‚ü®n‚ü© ‚äó X
  map f := Discrete.natTrans (fun _ => _ ‚óÅ f)

theorem tensorFunc_map_app {X Y : F C} (f : X ‚ü∂ Y) (n) : ((tensorFunc C).map f).app n = _ ‚óÅ f :=
  rfl

theorem tensorFunc_obj_map (Z : F C) {n n' : N C} (f : n ‚ü∂ n') :
    ((tensorFunc C).obj Z).map f = inclusion.map f ‚ñ∑ Z := by
  cases n
  cases n'
  rcases f with ‚ü®‚ü®h‚ü©‚ü©
  dsimp at h
  subst h
  simp

/-- Auxiliary definition for `normalizeIso`. Here we construct the isomorphism between
`n ‚äó X` and `normalize X n`. -/
@[simp]
def normalizeIsoApp :
    ‚àÄ (X : F C) (n : N C), ((tensorFunc C).obj X).obj n ‚âÖ ((normalize' C).obj X).obj n
  | of _, _ => Iso.refl _
  | unit, _ => œÅ_ _
  | tensor X a, n =>
    (Œ±_ _ _ _).symm ‚â™‚â´ whiskerRightIso (normalizeIsoApp X n) a ‚â™‚â´ normalizeIsoApp _ _

/-- Almost non-definitionally equal to `normalizeIsoApp`, but has a better definitional property
in the proof of `normalize_naturality`. -/
@[simp]
def normalizeIsoApp' :
    ‚àÄ (X : F C) (n : NormalMonoidalObject C), inclusionObj n ‚äó X ‚âÖ inclusionObj (normalizeObj X n)
  | of _, _ => Iso.refl _
  | unit, _ => œÅ_ _
  | tensor X Y, n =>
    (Œ±_ _ _ _).symm ‚â™‚â´ whiskerRightIso (normalizeIsoApp' X n) Y ‚â™‚â´ normalizeIsoApp' _ _

theorem normalizeIsoApp_eq :
    ‚àÄ (X : F C) (n : N C), normalizeIsoApp C X n = normalizeIsoApp' C X n.as
  | of _, _ => rfl
  | unit, _ => rfl
  | tensor X Y, n => by
      rw [normalizeIsoApp, normalizeIsoApp']
      rw [normalizeIsoApp_eq X n]
      rw [normalizeIsoApp_eq Y ‚ü®normalizeObj X n.as‚ü©]
      rfl

@[simp]
theorem normalizeIsoApp_tensor (X Y : F C) (n : N C) :
    normalizeIsoApp C (X ‚äó Y) n =
      (Œ±_ _ _ _).symm ‚â™‚â´ whiskerRightIso (normalizeIsoApp C X n) Y ‚â™‚â´ normalizeIsoApp _ _ _ :=
  rfl

@[simp]
theorem normalizeIsoApp_unitor (n : N C) : normalizeIsoApp C (ùüô_ (F C)) n = œÅ_ _ :=
  rfl

/-- Auxiliary definition for `normalizeIso`. -/
@[simps!]
def normalizeIsoAux (X : F C) : (tensorFunc C).obj X ‚âÖ (normalize' C).obj X :=
  NatIso.ofComponents (normalizeIsoApp C X)
    (by
      rintro ‚ü®X‚ü© ‚ü®Y‚ü© ‚ü®‚ü®f‚ü©‚ü©
      dsimp at f
      subst f
      dsimp
      simp)


section

variable {C}

theorem normalizeObj_congr (n : NormalMonoidalObject C) {X Y : F C} (f : X ‚ü∂ Y) :
    normalizeObj X n = normalizeObj Y n := by
  rcases f with ‚ü®f'‚ü©
  apply @congr_fun _ _ fun n => normalizeObj X n
  clear n f
  induction f' with
  | comp _ _ _ _ => apply Eq.trans <;> assumption
  | whiskerLeft  _ _ ih => funext; apply congr_fun ih
  | whiskerRight _ _ ih => funext; apply congr_arg‚ÇÇ _ rfl (congr_fun ih _)
  | @tensor W X Y Z _ _ ih‚ÇÅ ih‚ÇÇ =>
      funext n
      simp [congr_fun ih‚ÇÅ n, congr_fun ih‚ÇÇ (normalizeObj Y n)]
  | _ => funext; rfl

theorem normalize_naturality (n : NormalMonoidalObject C) {X Y : F C} (f : X ‚ü∂ Y) :
    inclusionObj n ‚óÅ f ‚â´ (normalizeIsoApp' C Y n).hom =
      (normalizeIsoApp' C X n).hom ‚â´
        inclusion.map (eqToHom (Discrete.ext (normalizeObj_congr n f))) := by
  revert n
  induction f using Hom.inductionOn
  case comp f g ihf ihg => simp [ihg, reassoc_of% (ihf _)]
  case whiskerLeft X' X Y f ih =>
    intro n
    dsimp only [normalizeObj_tensor, normalizeIsoApp', tensor_eq_tensor, Iso.trans_hom,
      Iso.symm_hom, whiskerRightIso_hom, Function.comp_apply, inclusion_obj]
    rw [associator_inv_naturality_right_assoc, whisker_exchange_assoc, ih]
    simp
  case whiskerRight X Y h Œ∑' ih =>
    intro n
    dsimp only [normalizeObj_tensor, normalizeIsoApp', tensor_eq_tensor, Iso.trans_hom,
      Iso.symm_hom, whiskerRightIso_hom, Function.comp_apply, inclusion_obj]
    rw [associator_inv_naturality_middle_assoc, ‚Üê comp_whiskerRight_assoc, ih]
    have := dcongr_arg (fun x => (normalizeIsoApp' C Œ∑' x).hom) (normalizeObj_congr n h)
    simp [this]
  all_goals simp

end

/-- The isomorphism between `n ‚äó X` and `normalize X n` is natural (in both `X` and `n`, but
naturality in `n` is trivial and was "proved" in `normalizeIsoAux`). This is the real heart
of our proof of the coherence theorem. -/
def normalizeIso : tensorFunc C ‚âÖ normalize' C :=
  NatIso.ofComponents (normalizeIsoAux C) <| by
    intro X Y f
    ext ‚ü®n‚ü©
    convert normalize_naturality n f using 1
    any_goals dsimp; rw [normalizeIsoApp_eq]

/-- The isomorphism between an object and its normal form is natural. -/
def fullNormalizeIso : ùü≠ (F C) ‚âÖ fullNormalize C ‚ãô inclusion :=
  NatIso.ofComponents
  (fun X => (Œª_ X).symm ‚â™‚â´ ((normalizeIso C).app X).app ‚ü®NormalMonoidalObject.unit‚ü©)
    (by
      intro X Y f
      dsimp
      rw [leftUnitor_inv_naturality_assoc, Category.assoc, Iso.cancel_iso_inv_left]
      exact
        congr_arg (fun f => NatTrans.app f (Discrete.mk NormalMonoidalObject.unit))
          ((normalizeIso.{u} C).hom.naturality f))

end

/-- The monoidal coherence theorem. -/
instance subsingleton_hom : Quiver.IsThin (F C) := fun X Y =>
  ‚ü®fun f g => by
    have hfg : (fullNormalize C).map f = (fullNormalize C).map g := Subsingleton.elim _ _
    have hf := NatIso.naturality_2 (fullNormalizeIso.{u} C) f
    have hg := NatIso.naturality_2 (fullNormalizeIso.{u} C) g
    exact hf.symm.trans (Eq.trans (by simp only [Functor.comp_map, hfg]) hg)‚ü©

section Groupoid

section

open Hom

/-- Auxiliary construction for showing that the free monoidal category is a groupoid. Do not use
this, use `IsIso.inv` instead. -/
def inverseAux : ‚àÄ {X Y : F C}, (X ‚ü∂·µê Y) ‚Üí (Y ‚ü∂·µê X)
  | _, _, Hom.id X => id X
  | _, _, Œ±_hom _ _ _ => Œ±_inv _ _ _
  | _, _, Œ±_inv _ _ _ => Œ±_hom _ _ _
  | _, _, œÅ_hom _ => œÅ_inv _
  | _, _, œÅ_inv _ => œÅ_hom _
  | _, _, l_hom _ => l_inv _
  | _, _, l_inv _ => l_hom _
  | _, _, Hom.comp f g => (inverseAux g).comp (inverseAux f)
  | _, _, Hom.whiskerLeft X f => (inverseAux f).whiskerLeft X
  | _, _, Hom.whiskerRight f X => (inverseAux f).whiskerRight X
  | _, _, Hom.tensor f g => (inverseAux f).tensor (inverseAux g)

end

instance : Groupoid.{u} (F C) :=
  { (inferInstance : Category (F C)) with
    inv := Quotient.lift (fun f => ‚ü¶inverseAux f‚üß) (by cat_disch) }

end Groupoid

end FreeMonoidalCategory

end CategoryTheory
