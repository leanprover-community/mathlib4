/-
Copyright (c) 2021 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Monoidal.Functor

/-!
# The free monoidal category over a type

Given a type `C`, the free monoidal category over `C` has as objects formal expressions built from
(formal) tensor products of terms of `C` and a formal unit. Its morphisms are compositions and
tensor products of identities, unitors and associators.

In this file, we construct the free monoidal category and prove that it is a monoidal category. If
`D` is a monoidal category, we construct the functor `FreeMonoidalCategory C ‚•§ D` associated to
a function `C ‚Üí D`.

The free monoidal category has two important properties: it is a groupoid and it is thin. The former
is obvious from the construction, and the latter is what is commonly known as the monoidal coherence
theorem. Both of these properties are proved in the file `Coherence.lean`.

-/


universe v' u u'

namespace CategoryTheory

open MonoidalCategory

variable {C : Type u}

section

variable (C)

-- Don't generate unnecessary `sizeOf_spec` or `injEq` lemmas
-- which the `simpNF` linter will complain about.
set_option genSizeOfSpec false in
set_option genInjectivity false in
/--
Given a type `C`, the free monoidal category over `C` has as objects formal expressions built from
(formal) tensor products of terms of `C` and a formal unit. Its morphisms are compositions and
tensor products of identities, unitors and associators.
-/
inductive FreeMonoidalCategory : Type u
  | of : C ‚Üí FreeMonoidalCategory
  | unit : FreeMonoidalCategory
  | tensor : FreeMonoidalCategory ‚Üí FreeMonoidalCategory ‚Üí FreeMonoidalCategory
  deriving Inhabited

end

local notation "F" => FreeMonoidalCategory

namespace FreeMonoidalCategory

/-- Formal compositions and tensor products of identities, unitors and associators. The morphisms
of the free monoidal category are obtained as a quotient of these formal morphisms by the
relations defining a monoidal category. -/
inductive Hom : F C ‚Üí F C ‚Üí Type u
  | id (X) : Hom X X
  | Œ±_hom (X Y Z : F C) : Hom ((X.tensor Y).tensor Z) (X.tensor (Y.tensor Z))
  | Œ±_inv (X Y Z : F C) : Hom (X.tensor (Y.tensor Z)) ((X.tensor Y).tensor Z)
  | l_hom (X) : Hom (unit.tensor X) X
  | l_inv (X) : Hom X (unit.tensor X)
  | œÅ_hom (X : F C) : Hom (X.tensor unit) X
  | œÅ_inv (X : F C) : Hom X (X.tensor unit)
  | comp {X Y Z} (f : Hom X Y) (g : Hom Y Z) : Hom X Z
  | whiskerLeft (X : F C) {Y‚ÇÅ Y‚ÇÇ} (f : Hom Y‚ÇÅ Y‚ÇÇ) : Hom (X.tensor Y‚ÇÅ) (X.tensor Y‚ÇÇ)
  | whiskerRight {X‚ÇÅ X‚ÇÇ} (f : Hom X‚ÇÅ X‚ÇÇ) (Y : F C) : Hom (X‚ÇÅ.tensor Y) (X‚ÇÇ.tensor Y)
  | tensor {W X Y Z} (f : Hom W Y) (g : Hom X Z) : Hom (W.tensor X) (Y.tensor Z)

local infixr:10 " ‚ü∂·µê " => Hom

/-- The morphisms of the free monoidal category satisfy 21 relations ensuring that the resulting
category is in fact a category and that it is monoidal. -/
inductive HomEquiv : ‚àÄ {X Y : F C}, (X ‚ü∂·µê Y) ‚Üí (X ‚ü∂·µê Y) ‚Üí Prop
  | refl {X Y} (f : X ‚ü∂·µê Y) : HomEquiv f f
  | symm {X Y} (f g : X ‚ü∂·µê Y) : HomEquiv f g ‚Üí HomEquiv g f
  | trans {X Y} {f g h : X ‚ü∂·µê Y} : HomEquiv f g ‚Üí HomEquiv g h ‚Üí HomEquiv f h
  | comp {X Y Z} {f f' : X ‚ü∂·µê Y} {g g' : Y ‚ü∂·µê Z} :
      HomEquiv f f' ‚Üí HomEquiv g g' ‚Üí HomEquiv (f.comp g) (f'.comp g')
  | whiskerLeft (X) {Y Z} (f f' : Y ‚ü∂·µê Z) :
      HomEquiv f f' ‚Üí HomEquiv (f.whiskerLeft X) (f'.whiskerLeft X)
  | whiskerRight {Y Z} (f f' : Y ‚ü∂·µê Z) (X) :
      HomEquiv f f' ‚Üí HomEquiv (f.whiskerRight X) (f'.whiskerRight X)
  | tensor {W X Y Z} {f f' : W ‚ü∂·µê X} {g g' : Y ‚ü∂·µê Z} :
      HomEquiv f f' ‚Üí HomEquiv g g' ‚Üí HomEquiv (f.tensor g) (f'.tensor g')
  | tensorHom_def {X‚ÇÅ Y‚ÇÅ X‚ÇÇ Y‚ÇÇ} (f : X‚ÇÅ ‚ü∂·µê Y‚ÇÅ) (g : X‚ÇÇ ‚ü∂·µê Y‚ÇÇ) :
      HomEquiv (f.tensor g) ((f.whiskerRight X‚ÇÇ).comp (g.whiskerLeft Y‚ÇÅ))
  | comp_id {X Y} (f : X ‚ü∂·µê Y) : HomEquiv (f.comp (Hom.id _)) f
  | id_comp {X Y} (f : X ‚ü∂·µê Y) : HomEquiv ((Hom.id _).comp f) f
  | assoc {X Y U V : F C} (f : X ‚ü∂·µê U) (g : U ‚ü∂·µê V) (h : V ‚ü∂·µê Y) :
      HomEquiv ((f.comp g).comp h) (f.comp (g.comp h))
  | id_tensorHom_id {X Y} : HomEquiv ((Hom.id X).tensor (Hom.id Y)) (Hom.id _)
  | tensor_comp {X‚ÇÅ Y‚ÇÅ Z‚ÇÅ X‚ÇÇ Y‚ÇÇ Z‚ÇÇ : F C} (f‚ÇÅ : X‚ÇÅ ‚ü∂·µê Y‚ÇÅ) (f‚ÇÇ : X‚ÇÇ ‚ü∂·µê Y‚ÇÇ) (g‚ÇÅ : Y‚ÇÅ ‚ü∂·µê Z‚ÇÅ)
      (g‚ÇÇ : Y‚ÇÇ ‚ü∂·µê Z‚ÇÇ) :
    HomEquiv ((f‚ÇÅ.comp g‚ÇÅ).tensor (f‚ÇÇ.comp g‚ÇÇ)) ((f‚ÇÅ.tensor f‚ÇÇ).comp (g‚ÇÅ.tensor g‚ÇÇ))
  | whiskerLeft_id (X Y) : HomEquiv ((Hom.id Y).whiskerLeft X) (Hom.id (X.tensor Y))
  | id_whiskerRight (X Y) : HomEquiv ((Hom.id X).whiskerRight Y) (Hom.id (X.tensor Y))
  | Œ±_hom_inv {X Y Z} : HomEquiv ((Hom.Œ±_hom X Y Z).comp (Hom.Œ±_inv X Y Z)) (Hom.id _)
  | Œ±_inv_hom {X Y Z} : HomEquiv ((Hom.Œ±_inv X Y Z).comp (Hom.Œ±_hom X Y Z)) (Hom.id _)
  | associator_naturality {X‚ÇÅ X‚ÇÇ X‚ÇÉ Y‚ÇÅ Y‚ÇÇ Y‚ÇÉ} (f‚ÇÅ : X‚ÇÅ ‚ü∂·µê Y‚ÇÅ) (f‚ÇÇ : X‚ÇÇ ‚ü∂·µê Y‚ÇÇ) (f‚ÇÉ : X‚ÇÉ ‚ü∂·µê Y‚ÇÉ) :
      HomEquiv (((f‚ÇÅ.tensor f‚ÇÇ).tensor f‚ÇÉ).comp (Hom.Œ±_hom Y‚ÇÅ Y‚ÇÇ Y‚ÇÉ))
        ((Hom.Œ±_hom X‚ÇÅ X‚ÇÇ X‚ÇÉ).comp (f‚ÇÅ.tensor (f‚ÇÇ.tensor f‚ÇÉ)))
  | œÅ_hom_inv {X} : HomEquiv ((Hom.œÅ_hom X).comp (Hom.œÅ_inv X)) (Hom.id _)
  | œÅ_inv_hom {X} : HomEquiv ((Hom.œÅ_inv X).comp (Hom.œÅ_hom X)) (Hom.id _)
  | œÅ_naturality {X Y} (f : X ‚ü∂·µê Y) :
      HomEquiv ((f.whiskerRight unit).comp (Hom.œÅ_hom Y)) ((Hom.œÅ_hom X).comp f)
  | l_hom_inv {X} : HomEquiv ((Hom.l_hom X).comp (Hom.l_inv X)) (Hom.id _)
  | l_inv_hom {X} : HomEquiv ((Hom.l_inv X).comp (Hom.l_hom X)) (Hom.id _)
  | l_naturality {X Y} (f : X ‚ü∂·µê Y) :
      HomEquiv ((f.whiskerLeft unit).comp (Hom.l_hom Y)) ((Hom.l_hom X).comp f)
  | pentagon {W X Y Z} :
      HomEquiv
        (((Hom.Œ±_hom W X Y).whiskerRight Z).comp
          ((Hom.Œ±_hom W (X.tensor Y) Z).comp ((Hom.Œ±_hom X Y Z).whiskerLeft W)))
        ((Hom.Œ±_hom (W.tensor X) Y Z).comp (Hom.Œ±_hom W X (Y.tensor Z)))
  | triangle {X Y} :
      HomEquiv ((Hom.Œ±_hom X unit Y).comp ((Hom.l_hom Y).whiskerLeft X))
        ((Hom.œÅ_hom X).whiskerRight Y)

/-- We say that two formal morphisms in the free monoidal category are equivalent if they become
equal if we apply the relations that are true in a monoidal category. Note that we will prove
that there is only one equivalence class -- this is the monoidal coherence theorem. -/
def setoidHom (X Y : F C) : Setoid (X ‚ü∂·µê Y) :=
  ‚ü®HomEquiv, ‚ü®HomEquiv.refl, HomEquiv.symm _ _, HomEquiv.trans‚ü©‚ü©

attribute [instance] setoidHom

section

open FreeMonoidalCategory.HomEquiv

instance categoryFreeMonoidalCategory : Category.{u} (F C) where
  Hom X Y := Quotient (FreeMonoidalCategory.setoidHom X Y)
  id X := ‚ü¶Hom.id X‚üß
  comp := Quotient.map‚ÇÇ Hom.comp (fun _ _ hf _ _ hg ‚Ü¶ HomEquiv.comp hf hg)
  id_comp := by
    rintro X Y ‚ü®f‚ü©
    exact Quotient.sound (id_comp f)
  comp_id := by
    rintro X Y ‚ü®f‚ü©
    exact Quotient.sound (comp_id f)
  assoc := by
    rintro W X Y Z ‚ü®f‚ü© ‚ü®g‚ü© ‚ü®h‚ü©
    exact Quotient.sound (assoc f g h)

instance : MonoidalCategory (F C) where
  tensorObj X Y := FreeMonoidalCategory.tensor X Y
  tensorHom := Quotient.map‚ÇÇ Hom.tensor (fun _ _ hf _ _ hg ‚Ü¶ HomEquiv.tensor hf hg)
  whiskerLeft X _ _ f := Quot.map (fun f ‚Ü¶ Hom.whiskerLeft X f) (fun f f' ‚Ü¶ .whiskerLeft X f f') f
  whiskerRight f Y := Quot.map (fun f ‚Ü¶ Hom.whiskerRight f Y) (fun f f' ‚Ü¶ .whiskerRight f f' Y) f
  tensorHom_def {W X Y Z} := by
    rintro ‚ü®f‚ü© ‚ü®g‚ü©
    exact Quotient.sound (tensorHom_def _ _)
  id_tensorHom_id _ _ := Quot.sound id_tensorHom_id
  tensor_comp {X‚ÇÅ Y‚ÇÅ Z‚ÇÅ X‚ÇÇ Y‚ÇÇ Z‚ÇÇ} := by
    rintro ‚ü®f‚ÇÅ‚ü© ‚ü®f‚ÇÇ‚ü© ‚ü®g‚ÇÅ‚ü© ‚ü®g‚ÇÇ‚ü©
    exact Quotient.sound (tensor_comp _ _ _ _)
  whiskerLeft_id X Y := Quot.sound (HomEquiv.whiskerLeft_id X Y)
  id_whiskerRight X Y := Quot.sound (HomEquiv.id_whiskerRight X Y)
  tensorUnit := FreeMonoidalCategory.unit
  associator X Y Z :=
    ‚ü®‚ü¶Hom.Œ±_hom X Y Z‚üß, ‚ü¶Hom.Œ±_inv X Y Z‚üß, Quotient.sound Œ±_hom_inv, Quotient.sound Œ±_inv_hom‚ü©
  associator_naturality {X‚ÇÅ X‚ÇÇ X‚ÇÉ Y‚ÇÅ Y‚ÇÇ Y‚ÇÉ} := by
    rintro ‚ü®f‚ÇÅ‚ü© ‚ü®f‚ÇÇ‚ü© ‚ü®f‚ÇÉ‚ü©
    exact Quotient.sound (associator_naturality _ _ _)
  leftUnitor X := ‚ü®‚ü¶Hom.l_hom X‚üß, ‚ü¶Hom.l_inv X‚üß, Quotient.sound l_hom_inv, Quotient.sound l_inv_hom‚ü©
  leftUnitor_naturality {X Y} := by
    rintro ‚ü®f‚ü©
    exact Quotient.sound (l_naturality _)
  rightUnitor X :=
    ‚ü®‚ü¶Hom.œÅ_hom X‚üß, ‚ü¶Hom.œÅ_inv X‚üß, Quotient.sound œÅ_hom_inv, Quotient.sound œÅ_inv_hom‚ü©
  rightUnitor_naturality {X Y} := by
    rintro ‚ü®f‚ü©
    exact Quotient.sound (œÅ_naturality _)
  pentagon _ _ _ _ := Quotient.sound pentagon
  triangle _ _ := Quotient.sound triangle

@[simp]
theorem mk_comp {X Y Z : F C} (f : X ‚ü∂·µê Y) (g : Y ‚ü∂·µê Z) :
    ‚ü¶f.comp g‚üß = @CategoryStruct.comp (F C) _ _ _ _ ‚ü¶f‚üß ‚ü¶g‚üß :=
  rfl

@[simp]
theorem mk_tensor {X‚ÇÅ Y‚ÇÅ X‚ÇÇ Y‚ÇÇ : F C} (f : X‚ÇÅ ‚ü∂·µê Y‚ÇÅ) (g : X‚ÇÇ ‚ü∂·µê Y‚ÇÇ) :
    ‚ü¶f.tensor g‚üß = @MonoidalCategory.tensorHom (F C) _ _ _ _ _ _ ‚ü¶f‚üß ‚ü¶g‚üß :=
  rfl

@[simp]
theorem mk_whiskerLeft (X : F C) {Y‚ÇÅ Y‚ÇÇ : F C} (f : Y‚ÇÅ ‚ü∂·µê Y‚ÇÇ) :
    ‚ü¶f.whiskerLeft X‚üß = MonoidalCategory.whiskerLeft (C := F C) (X := X) (f := ‚ü¶f‚üß) :=
  rfl

@[simp]
theorem mk_whiskerRight {X‚ÇÅ X‚ÇÇ : F C} (f : X‚ÇÅ ‚ü∂·µê X‚ÇÇ) (Y : F C) :
    ‚ü¶f.whiskerRight Y‚üß = MonoidalCategory.whiskerRight (C := F C) (f := ‚ü¶f‚üß) (Y := Y) :=
  rfl

@[simp]
theorem mk_id {X : F C} : ‚ü¶Hom.id X‚üß = ùüô X :=
  rfl

@[simp]
theorem mk_Œ±_hom {X Y Z : F C} : ‚ü¶Hom.Œ±_hom X Y Z‚üß = (Œ±_ X Y Z).hom :=
  rfl

@[simp]
theorem mk_Œ±_inv {X Y Z : F C} : ‚ü¶Hom.Œ±_inv X Y Z‚üß = (Œ±_ X Y Z).inv :=
  rfl

@[simp]
theorem mk_œÅ_hom {X : F C} : ‚ü¶Hom.œÅ_hom X‚üß = (œÅ_ X).hom :=
  rfl

@[simp]
theorem mk_œÅ_inv {X : F C} : ‚ü¶Hom.œÅ_inv X‚üß = (œÅ_ X).inv :=
  rfl

@[simp]
theorem mk_l_hom {X : F C} : ‚ü¶Hom.l_hom X‚üß = (Œª_ X).hom :=
  rfl

@[simp]
theorem mk_l_inv {X : F C} : ‚ü¶Hom.l_inv X‚üß = (Œª_ X).inv :=
  rfl

@[simp]
theorem tensor_eq_tensor {X Y : F C} : X.tensor Y = X ‚äó Y :=
  rfl

@[simp]
theorem unit_eq_unit : FreeMonoidalCategory.unit = ùüô_ (F C) :=
  rfl

/-- The abbreviation for `‚ü¶f‚üß`. -/
/- This is useful since the notation `‚ü¶f‚üß` often behaves like an element of the quotient set,
but not like a morphism. This is why we need weird `@CategoryStruct.comp (F C) ...` in the
statement in `mk_comp` above. -/
abbrev homMk {X Y : F C} (f : X ‚ü∂·µê Y) : X ‚ü∂ Y := ‚ü¶f‚üß

theorem Hom.inductionOn {motive : {X Y : F C} ‚Üí (X ‚ü∂ Y) ‚Üí Prop} {X Y : F C} (t : X ‚ü∂ Y)
    (id : (X : F C) ‚Üí motive (ùüô X))
    (Œ±_hom : (X Y Z : F C) ‚Üí motive (Œ±_ X Y Z).hom)
    (Œ±_inv : (X Y Z : F C) ‚Üí motive (Œ±_ X Y Z).inv)
    (l_hom : (X : F C) ‚Üí motive (Œª_ X).hom)
    (l_inv : (X : F C) ‚Üí motive (Œª_ X).inv)
    (œÅ_hom : (X : F C) ‚Üí motive (œÅ_ X).hom)
    (œÅ_inv : (X : F C) ‚Üí motive (œÅ_ X).inv)
    (comp : {X Y Z : F C} ‚Üí (f : X ‚ü∂ Y) ‚Üí (g : Y ‚ü∂ Z) ‚Üí motive f ‚Üí motive g ‚Üí motive (f ‚â´ g))
    (whiskerLeft : (X : F C) ‚Üí {Y Z : F C} ‚Üí (f : Y ‚ü∂ Z) ‚Üí motive f ‚Üí motive (X ‚óÅ f))
    (whiskerRight : {X Y : F C} ‚Üí (f : X ‚ü∂ Y) ‚Üí (Z : F C) ‚Üí motive f ‚Üí motive (f ‚ñ∑ Z)) :
    motive t := by
  apply Quotient.inductionOn
  intro f
  induction f with
  | id X => exact id X
  | Œ±_hom X Y Z => exact Œ±_hom X Y Z
  | Œ±_inv X Y Z => exact Œ±_inv X Y Z
  | l_hom X => exact l_hom X
  | l_inv X => exact l_inv X
  | œÅ_hom X => exact œÅ_hom X
  | œÅ_inv X => exact œÅ_inv X
  | comp f g hf hg => exact comp _ _ (hf ‚ü¶f‚üß) (hg ‚ü¶g‚üß)
  | whiskerLeft X f hf => exact whiskerLeft X _ (hf ‚ü¶f‚üß)
  | whiskerRight f X hf => exact whiskerRight _ X (hf ‚ü¶f‚üß)
  | @tensor W X Y Z f g hf hg =>
      have : homMk f ‚äó‚Çò homMk g = homMk f ‚ñ∑ X ‚â´ Y ‚óÅ homMk g :=
        Quotient.sound (HomEquiv.tensorHom_def f g)
      change motive (homMk f ‚äó‚Çò homMk g)
      rw [this]
      exact comp _ _ (whiskerRight _ _ (hf ‚ü¶f‚üß)) (whiskerLeft _ _ (hg ‚ü¶g‚üß))

section Functor

variable {D : Type u'} [Category.{v'} D] [MonoidalCategory D] (f : C ‚Üí D)

/-- Auxiliary definition for `free_monoidal_category.project`. -/
def projectObj : F C ‚Üí D
  | FreeMonoidalCategory.of X => f X
  | FreeMonoidalCategory.unit => ùüô_ D
  | FreeMonoidalCategory.tensor X Y => projectObj X ‚äó projectObj Y

section


open Hom

/-- Auxiliary definition for `FreeMonoidalCategory.project`. -/
-- Porting note: here `@[simp]` generates a panic in
-- _private.Lean.Meta.Match.MatchEqs.0.Lean.Meta.Match.SimpH.substRHS
def projectMapAux : ‚àÄ {X Y : F C}, (X ‚ü∂·µê Y) ‚Üí (projectObj f X ‚ü∂ projectObj f Y)
  | _, _, Hom.id _ => ùüô _
  | _, _, Œ±_hom _ _ _ => (Œ±_ _ _ _).hom
  | _, _, Œ±_inv _ _ _ => (Œ±_ _ _ _).inv
  | _, _, l_hom _ => (Œª_ _).hom
  | _, _, l_inv _ => (Œª_ _).inv
  | _, _, œÅ_hom _ => (œÅ_ _).hom
  | _, _, œÅ_inv _ => (œÅ_ _).inv
  | _, _, Hom.comp f g => projectMapAux f ‚â´ projectMapAux g
  | _, _, Hom.whiskerLeft X p => projectObj f X ‚óÅ projectMapAux p
  | _, _, Hom.whiskerRight p X => projectMapAux p ‚ñ∑ projectObj f X
  | _, _, Hom.tensor f g => projectMapAux f ‚äó‚Çò projectMapAux g

-- Porting note: this declaration generates the same panic.
/-- Auxiliary definition for `FreeMonoidalCategory.project`. -/
def projectMap (X Y : F C) : (X ‚ü∂ Y) ‚Üí (projectObj f X ‚ü∂ projectObj f Y) :=
  Quotient.lift (projectMapAux f) <| by
    intro f g h
    induction h with
    | refl => rfl
    | symm _ _ _ hfg' => exact hfg'.symm
    | trans _ _ hfg hgh => exact hfg.trans hgh
    | comp _ _ hf hg => dsimp only [projectMapAux]; rw [hf, hg]
    | whiskerLeft _ _ _ _ hf => dsimp only [projectMapAux, projectObj]; rw [hf]
    | whiskerRight _ _ _ _ hf => dsimp only [projectMapAux, projectObj]; rw [hf]
    | tensor _ _ hfg hfg' => dsimp only [projectMapAux]; rw [hfg, hfg']
    | tensorHom_def _ _ =>
        dsimp only [projectMapAux, projectObj]; rw [MonoidalCategory.tensorHom_def]
    | comp_id => dsimp only [projectMapAux]; rw [Category.comp_id]
    | id_comp => dsimp only [projectMapAux]; rw [Category.id_comp]
    | assoc => dsimp only [projectMapAux]; rw [Category.assoc]
    | id_tensorHom_id => dsimp only [projectMapAux]; rw [MonoidalCategory.id_tensorHom_id]; rfl
    | tensor_comp => dsimp only [projectMapAux]; rw [MonoidalCategory.tensor_comp]
    | whiskerLeft_id =>
        dsimp only [projectMapAux, projectObj]
        rw [MonoidalCategory.whiskerLeft_id]
    | id_whiskerRight =>
        dsimp only [projectMapAux, projectObj]
        rw [MonoidalCategory.id_whiskerRight]
    | Œ±_hom_inv => dsimp only [projectMapAux]; rw [Iso.hom_inv_id]
    | Œ±_inv_hom => dsimp only [projectMapAux]; rw [Iso.inv_hom_id]
    | associator_naturality =>
        dsimp only [projectMapAux]; rw [MonoidalCategory.associator_naturality]
    | œÅ_hom_inv => dsimp only [projectMapAux]; rw [Iso.hom_inv_id]
    | œÅ_inv_hom => dsimp only [projectMapAux]; rw [Iso.inv_hom_id]
    | œÅ_naturality =>
        dsimp only [projectMapAux, projectObj]
        rw [MonoidalCategory.rightUnitor_naturality]
    | l_hom_inv => dsimp only [projectMapAux]; rw [Iso.hom_inv_id]
    | l_inv_hom => dsimp only [projectMapAux]; rw [Iso.inv_hom_id]
    | l_naturality =>
        dsimp only [projectMapAux, projectObj]
        rw [MonoidalCategory.leftUnitor_naturality]
    | pentagon =>
        dsimp only [projectMapAux, projectObj]
        rw [MonoidalCategory.pentagon]
    | triangle =>
        dsimp only [projectMapAux, projectObj]
        rw [MonoidalCategory.triangle]

end

/-- If `D` is a monoidal category and we have a function `C ‚Üí D`, then we have a
monoidal functor from the free monoidal category over `C` to the category `D`. -/
def project : F C ‚•§ D where
  obj := projectObj f
  map := projectMap f _ _
  map_comp := by rintro _ _ _ ‚ü®_‚ü© ‚ü®_‚ü©; rfl

instance : (project f).Monoidal :=
  Functor.CoreMonoidal.toMonoidal
    { ŒµIso := Iso.refl _
      ŒºIso := fun _ _ ‚Ü¶ Iso.refl _
  -- Porting note: `ŒºIso_hom_natural_left` was proved in mathlib3 by tidy, using induction.
  -- We probably don't expect `cat_disch` to handle this yet, see https://leanprover.zulipchat.com/#narrow/stream/287929-mathlib4/topic/Aesop.20and.20cases
  -- In any case I don't understand why we need to specify `using Quotient.recOn`.
      ŒºIso_hom_natural_left := fun f _ => by
        induction f using Quotient.recOn
        all_goals aesop
      ŒºIso_hom_natural_right := fun _ f => by
        induction f using Quotient.recOn
        all_goals aesop }

end Functor

end

end FreeMonoidalCategory

end CategoryTheory
