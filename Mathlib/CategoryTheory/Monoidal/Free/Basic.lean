/-
Copyright (c) 2021 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Monoidal.Functor

/-!
# The free monoidal category over a type

Given a type `C`, the free monoidal category over `C` has as objects formal expressions built from
(formal) tensor products of terms of `C` and a formal unit. Its morphisms are compositions and
tensor products of identities, unitors and associators.

In this file, we construct the free monoidal category and prove that it is a monoidal category. If
`D` is a monoidal category, we construct the functor `FreeMonoidalCategory C â¥¤ D` associated to
a function `C â†’ D`.

The free monoidal category has two important properties: it is a groupoid and it is thin. The former
is obvious from the construction, and the latter is what is commonly known as the monoidal coherence
theorem. Both of these properties are proved in the file `Coherence.lean`.

-/


universe v' u u'

namespace CategoryTheory

open MonoidalCategory

variable {C : Type u}

section

variable (C)

/--
Given a type `C`, the free monoidal category over `C` has as objects formal expressions built from
(formal) tensor products of terms of `C` and a formal unit. Its morphisms are compositions and
tensor products of identities, unitors and associators.
-/
inductive FreeMonoidalCategory : Type u
  | of : C â†’ FreeMonoidalCategory
  | unit : FreeMonoidalCategory
  | tensor : FreeMonoidalCategory â†’ FreeMonoidalCategory â†’ FreeMonoidalCategory
  deriving Inhabited

end

local notation "F" => FreeMonoidalCategory

namespace FreeMonoidalCategory

attribute [nolint simpNF] unit.sizeOf_spec tensor.injEq tensor.sizeOf_spec

/-- Formal compositions and tensor products of identities, unitors and associators. The morphisms
    of the free monoidal category are obtained as a quotient of these formal morphisms by the
    relations defining a monoidal category. -/
-- Porting note(#5171): linter not ported yet
-- @[nolint has_nonempty_instance]
inductive Hom : F C â†’ F C â†’ Type u
  | id (X) : Hom X X
  | Î±_hom (X Y Z : F C) : Hom ((X.tensor Y).tensor Z) (X.tensor (Y.tensor Z))
  | Î±_inv (X Y Z : F C) : Hom (X.tensor (Y.tensor Z)) ((X.tensor Y).tensor Z)
  | l_hom (X) : Hom (unit.tensor X) X
  | l_inv (X) : Hom X (unit.tensor X)
  | Ï_hom (X : F C) : Hom (X.tensor unit) X
  | Ï_inv (X : F C) : Hom X (X.tensor unit)
  | comp {X Y Z} (f : Hom X Y) (g : Hom Y Z) : Hom X Z
  | whiskerLeft (X : F C) {Yâ‚ Yâ‚‚} (f : Hom Yâ‚ Yâ‚‚) : Hom (X.tensor Yâ‚) (X.tensor Yâ‚‚)
  | whiskerRight {Xâ‚ Xâ‚‚} (f : Hom Xâ‚ Xâ‚‚) (Y : F C) : Hom (Xâ‚.tensor Y) (Xâ‚‚.tensor Y)
  | tensor {W X Y Z} (f : Hom W Y) (g : Hom X Z) : Hom (W.tensor X) (Y.tensor Z)

local infixr:10 " âŸ¶áµ " => Hom

/-- The morphisms of the free monoidal category satisfy 21 relations ensuring that the resulting
    category is in fact a category and that it is monoidal. -/
inductive HomEquiv : âˆ€ {X Y : F C}, (X âŸ¶áµ Y) â†’ (X âŸ¶áµ Y) â†’ Prop
  | refl {X Y} (f : X âŸ¶áµ Y) : HomEquiv f f
  | symm {X Y} (f g : X âŸ¶áµ Y) : HomEquiv f g â†’ HomEquiv g f
  | trans {X Y} {f g h : X âŸ¶áµ Y} : HomEquiv f g â†’ HomEquiv g h â†’ HomEquiv f h
  | comp {X Y Z} {f f' : X âŸ¶áµ Y} {g g' : Y âŸ¶áµ Z} :
      HomEquiv f f' â†’ HomEquiv g g' â†’ HomEquiv (f.comp g) (f'.comp g')
  | whiskerLeft (X) {Y Z} (f f' : Y âŸ¶áµ Z) :
      HomEquiv f f' â†’ HomEquiv (f.whiskerLeft X) (f'.whiskerLeft X)
  | whiskerRight {Y Z} (f f' : Y âŸ¶áµ Z) (X) :
      HomEquiv f f' â†’ HomEquiv (f.whiskerRight X) (f'.whiskerRight X)
  | tensor {W X Y Z} {f f' : W âŸ¶áµ X} {g g' : Y âŸ¶áµ Z} :
      HomEquiv f f' â†’ HomEquiv g g' â†’ HomEquiv (f.tensor g) (f'.tensor g')
  | tensorHom_def {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚} (f : Xâ‚ âŸ¶áµ Yâ‚) (g : Xâ‚‚ âŸ¶áµ Yâ‚‚) :
      HomEquiv (f.tensor g) ((f.whiskerRight Xâ‚‚).comp (g.whiskerLeft Yâ‚))
  | comp_id {X Y} (f : X âŸ¶áµ Y) : HomEquiv (f.comp (Hom.id _)) f
  | id_comp {X Y} (f : X âŸ¶áµ Y) : HomEquiv ((Hom.id _).comp f) f
  | assoc {X Y U V : F C} (f : X âŸ¶áµ U) (g : U âŸ¶áµ V) (h : V âŸ¶áµ Y) :
      HomEquiv ((f.comp g).comp h) (f.comp (g.comp h))
  | tensor_id {X Y} : HomEquiv ((Hom.id X).tensor (Hom.id Y)) (Hom.id _)
  | tensor_comp {Xâ‚ Yâ‚ Zâ‚ Xâ‚‚ Yâ‚‚ Zâ‚‚ : F C} (fâ‚ : Xâ‚ âŸ¶áµ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶áµ Yâ‚‚) (gâ‚ : Yâ‚ âŸ¶áµ Zâ‚)
      (gâ‚‚ : Yâ‚‚ âŸ¶áµ Zâ‚‚) :
    HomEquiv ((fâ‚.comp gâ‚).tensor (fâ‚‚.comp gâ‚‚)) ((fâ‚.tensor fâ‚‚).comp (gâ‚.tensor gâ‚‚))
  | whiskerLeft_id (X Y) : HomEquiv ((Hom.id Y).whiskerLeft X) (Hom.id (X.tensor Y))
  | id_whiskerRight (X Y) : HomEquiv ((Hom.id X).whiskerRight Y) (Hom.id (X.tensor Y))
  | Î±_hom_inv {X Y Z} : HomEquiv ((Hom.Î±_hom X Y Z).comp (Hom.Î±_inv X Y Z)) (Hom.id _)
  | Î±_inv_hom {X Y Z} : HomEquiv ((Hom.Î±_inv X Y Z).comp (Hom.Î±_hom X Y Z)) (Hom.id _)
  | associator_naturality {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ} (fâ‚ : Xâ‚ âŸ¶áµ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶áµ Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ âŸ¶áµ Yâ‚ƒ) :
      HomEquiv (((fâ‚.tensor fâ‚‚).tensor fâ‚ƒ).comp (Hom.Î±_hom Yâ‚ Yâ‚‚ Yâ‚ƒ))
        ((Hom.Î±_hom Xâ‚ Xâ‚‚ Xâ‚ƒ).comp (fâ‚.tensor (fâ‚‚.tensor fâ‚ƒ)))
  | Ï_hom_inv {X} : HomEquiv ((Hom.Ï_hom X).comp (Hom.Ï_inv X)) (Hom.id _)
  | Ï_inv_hom {X} : HomEquiv ((Hom.Ï_inv X).comp (Hom.Ï_hom X)) (Hom.id _)
  | Ï_naturality {X Y} (f : X âŸ¶áµ Y) :
      HomEquiv ((f.whiskerRight unit).comp (Hom.Ï_hom Y)) ((Hom.Ï_hom X).comp f)
  | l_hom_inv {X} : HomEquiv ((Hom.l_hom X).comp (Hom.l_inv X)) (Hom.id _)
  | l_inv_hom {X} : HomEquiv ((Hom.l_inv X).comp (Hom.l_hom X)) (Hom.id _)
  | l_naturality {X Y} (f : X âŸ¶áµ Y) :
      HomEquiv ((f.whiskerLeft unit).comp (Hom.l_hom Y)) ((Hom.l_hom X).comp f)
  | pentagon {W X Y Z} :
      HomEquiv
        (((Hom.Î±_hom W X Y).whiskerRight Z).comp
          ((Hom.Î±_hom W (X.tensor Y) Z).comp ((Hom.Î±_hom X Y Z).whiskerLeft W)))
        ((Hom.Î±_hom (W.tensor X) Y Z).comp (Hom.Î±_hom W X (Y.tensor Z)))
  | triangle {X Y} :
      HomEquiv ((Hom.Î±_hom X unit Y).comp ((Hom.l_hom Y).whiskerLeft X))
        ((Hom.Ï_hom X).whiskerRight Y)

/-- We say that two formal morphisms in the free monoidal category are equivalent if they become
    equal if we apply the relations that are true in a monoidal category. Note that we will prove
    that there is only one equivalence class -- this is the monoidal coherence theorem. -/
def setoidHom (X Y : F C) : Setoid (X âŸ¶áµ Y) :=
  âŸ¨HomEquiv,
    âŸ¨fun f => HomEquiv.refl f, @fun f g => HomEquiv.symm f g, @fun _ _ _ hfg hgh =>
      HomEquiv.trans hfg hghâŸ©âŸ©

attribute [instance] setoidHom

section

open FreeMonoidalCategory.HomEquiv

instance categoryFreeMonoidalCategory : Category.{u} (F C) where
  Hom X Y := Quotient (FreeMonoidalCategory.setoidHom X Y)
  id X := âŸ¦Hom.id XâŸ§
  comp := Quotient.mapâ‚‚ Hom.comp (fun _ _ hf _ _ hg â†¦ HomEquiv.comp hf hg)
  id_comp := by
    rintro X Y âŸ¨fâŸ©
    exact Quotient.sound (id_comp f)
  comp_id := by
    rintro X Y âŸ¨fâŸ©
    exact Quotient.sound (comp_id f)
  assoc := by
    rintro W X Y Z âŸ¨fâŸ© âŸ¨gâŸ© âŸ¨hâŸ©
    exact Quotient.sound (assoc f g h)

instance : MonoidalCategory (F C) where
  tensorObj X Y := FreeMonoidalCategory.tensor X Y
  tensorHom := Quotient.mapâ‚‚ Hom.tensor (fun _ _ hf _ _ hg â†¦ HomEquiv.tensor hf hg)
  whiskerLeft X _ _ f := Quot.map (fun f â†¦ Hom.whiskerLeft X f) (fun f f' â†¦ .whiskerLeft X f f') f
  whiskerRight f Y := Quot.map (fun f â†¦ Hom.whiskerRight f Y) (fun f f' â†¦ .whiskerRight f f' Y) f
  tensorHom_def := by
    rintro W X Y Z âŸ¨fâŸ© âŸ¨gâŸ©
    exact Quotient.sound (tensorHom_def _ _)
  tensor_id X Y := Quot.sound tensor_id
  tensor_comp := @fun Xâ‚ Yâ‚ Zâ‚ Xâ‚‚ Yâ‚‚ Zâ‚‚ => by
    rintro âŸ¨fâ‚âŸ© âŸ¨fâ‚‚âŸ© âŸ¨gâ‚âŸ© âŸ¨gâ‚‚âŸ©
    exact Quotient.sound (tensor_comp _ _ _ _)
  whiskerLeft_id X Y := Quot.sound (HomEquiv.whiskerLeft_id X Y)
  id_whiskerRight X Y := Quot.sound (HomEquiv.id_whiskerRight X Y)
  tensorUnit := FreeMonoidalCategory.unit
  associator X Y Z :=
    âŸ¨âŸ¦Hom.Î±_hom X Y ZâŸ§, âŸ¦Hom.Î±_inv X Y ZâŸ§, Quotient.sound Î±_hom_inv, Quotient.sound Î±_inv_homâŸ©
  associator_naturality := @fun Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ => by
    rintro âŸ¨fâ‚âŸ© âŸ¨fâ‚‚âŸ© âŸ¨fâ‚ƒâŸ©
    exact Quotient.sound (associator_naturality _ _ _)
  leftUnitor X := âŸ¨âŸ¦Hom.l_hom XâŸ§, âŸ¦Hom.l_inv XâŸ§, Quotient.sound l_hom_inv, Quotient.sound l_inv_homâŸ©
  leftUnitor_naturality := @fun X Y => by
    rintro âŸ¨fâŸ©
    exact Quotient.sound (l_naturality _)
  rightUnitor X :=
    âŸ¨âŸ¦Hom.Ï_hom XâŸ§, âŸ¦Hom.Ï_inv XâŸ§, Quotient.sound Ï_hom_inv, Quotient.sound Ï_inv_homâŸ©
  rightUnitor_naturality := @fun X Y => by
    rintro âŸ¨fâŸ©
    exact Quotient.sound (Ï_naturality _)
  pentagon W X Y Z := Quotient.sound pentagon
  triangle X Y := Quotient.sound triangle

@[simp]
theorem mk_comp {X Y Z : F C} (f : X âŸ¶áµ Y) (g : Y âŸ¶áµ Z) :
    âŸ¦f.comp gâŸ§ = @CategoryStruct.comp (F C) _ _ _ _ âŸ¦fâŸ§ âŸ¦gâŸ§ :=
  rfl

@[simp]
theorem mk_tensor {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : F C} (f : Xâ‚ âŸ¶áµ Yâ‚) (g : Xâ‚‚ âŸ¶áµ Yâ‚‚) :
    âŸ¦f.tensor gâŸ§ = @MonoidalCategory.tensorHom (F C) _ _ _ _ _ _ âŸ¦fâŸ§ âŸ¦gâŸ§ :=
  rfl

@[simp]
theorem mk_whiskerLeft (X : F C) {Yâ‚ Yâ‚‚ : F C} (f : Yâ‚ âŸ¶áµ Yâ‚‚) :
    âŸ¦f.whiskerLeft XâŸ§ = MonoidalCategory.whiskerLeft (C := F C) (X := X) (f := âŸ¦fâŸ§) :=
  rfl

@[simp]
theorem mk_whiskerRight {Xâ‚ Xâ‚‚ : F C} (f : Xâ‚ âŸ¶áµ Xâ‚‚) (Y : F C) :
    âŸ¦f.whiskerRight YâŸ§ = MonoidalCategory.whiskerRight (C := F C) (f := âŸ¦fâŸ§) (Y := Y) :=
  rfl

@[simp]
theorem mk_id {X : F C} : âŸ¦Hom.id XâŸ§ = ğŸ™ X :=
  rfl

@[simp]
theorem mk_Î±_hom {X Y Z : F C} : âŸ¦Hom.Î±_hom X Y ZâŸ§ = (Î±_ X Y Z).hom :=
  rfl

@[simp]
theorem mk_Î±_inv {X Y Z : F C} : âŸ¦Hom.Î±_inv X Y ZâŸ§ = (Î±_ X Y Z).inv :=
  rfl

@[simp]
theorem mk_Ï_hom {X : F C} : âŸ¦Hom.Ï_hom XâŸ§ = (Ï_ X).hom :=
  rfl

@[simp]
theorem mk_Ï_inv {X : F C} : âŸ¦Hom.Ï_inv XâŸ§ = (Ï_ X).inv :=
  rfl

@[simp]
theorem mk_l_hom {X : F C} : âŸ¦Hom.l_hom XâŸ§ = (Î»_ X).hom :=
  rfl

@[simp]
theorem mk_l_inv {X : F C} : âŸ¦Hom.l_inv XâŸ§ = (Î»_ X).inv :=
  rfl

@[simp]
theorem tensor_eq_tensor {X Y : F C} : X.tensor Y = X âŠ— Y :=
  rfl

@[simp]
theorem unit_eq_unit : FreeMonoidalCategory.unit = ğŸ™_ (F C) :=
  rfl

/-- The abbreviation for `âŸ¦fâŸ§`. -/
/- This is useful since the notation `âŸ¦fâŸ§` often behaves like an element of the quotient set,
but not like a morphism. This is why we need weird `@CategoryStruct.comp (F C) ...` in the
statement in `mk_comp` above. -/
abbrev homMk {X Y : F C} (f : X âŸ¶áµ Y) : X âŸ¶ Y := âŸ¦fâŸ§

theorem Hom.inductionOn {motive : {X Y : F C} â†’ (X âŸ¶ Y) â†’ Prop} {X Y : F C} (t : X âŸ¶ Y)
    (id : (X : F C) â†’ motive (ğŸ™ X))
    (Î±_hom : (X Y Z : F C) â†’ motive (Î±_ X Y Z).hom)
    (Î±_inv : (X Y Z : F C) â†’ motive (Î±_ X Y Z).inv)
    (l_hom : (X : F C) â†’ motive (Î»_ X).hom)
    (l_inv : (X : F C) â†’ motive (Î»_ X).inv)
    (Ï_hom : (X : F C) â†’ motive (Ï_ X).hom)
    (Ï_inv : (X : F C) â†’ motive (Ï_ X).inv)
    (comp : {X Y Z : F C} â†’ (f : X âŸ¶ Y) â†’ (g : Y âŸ¶ Z) â†’ motive f â†’ motive g â†’ motive (f â‰« g))
    (whiskerLeft : (X : F C) â†’ {Y Z : F C} â†’ (f : Y âŸ¶ Z) â†’ motive f â†’ motive (X â— f))
    (whiskerRight : {X Y : F C} â†’ (f : X âŸ¶ Y) â†’ (Z : F C) â†’ motive f â†’ motive (f â–· Z)) :
    motive t := by
  apply Quotient.inductionOn
  intro f
  induction f with
  | id X => exact id X
  | Î±_hom X Y Z => exact Î±_hom X Y Z
  | Î±_inv X Y Z => exact Î±_inv X Y Z
  | l_hom X => exact l_hom X
  | l_inv X => exact l_inv X
  | Ï_hom X => exact Ï_hom X
  | Ï_inv X => exact Ï_inv X
  | comp f g hf hg => exact comp _ _ (hf âŸ¦fâŸ§) (hg âŸ¦gâŸ§)
  | whiskerLeft X f hf => exact whiskerLeft X _ (hf âŸ¦fâŸ§)
  | whiskerRight f X hf => exact whiskerRight _ X (hf âŸ¦fâŸ§)
  | @tensor W X Y Z f g hf hg =>
      have : homMk f âŠ— homMk g = homMk f â–· X â‰« Y â— homMk g :=
        Quotient.sound (HomEquiv.tensorHom_def f g)
      change motive (homMk f âŠ— homMk g)
      rw [this]
      exact comp _ _ (whiskerRight _ _ (hf âŸ¦fâŸ§)) (whiskerLeft _ _ (hg âŸ¦gâŸ§))

section Functor

variable {D : Type u'} [Category.{v'} D] [MonoidalCategory D] (f : C â†’ D)

/-- Auxiliary definition for `free_monoidal_category.project`. -/
def projectObj : F C â†’ D
  | FreeMonoidalCategory.of X => f X
  | FreeMonoidalCategory.unit => ğŸ™_ D
  | FreeMonoidalCategory.tensor X Y => projectObj X âŠ— projectObj Y

section


open Hom

/-- Auxiliary definition for `FreeMonoidalCategory.project`. -/
-- Porting note: here `@[simp]` generates a panic in
-- _private.Lean.Meta.Match.MatchEqs.0.Lean.Meta.Match.SimpH.substRHS
def projectMapAux : âˆ€ {X Y : F C}, (X âŸ¶áµ Y) â†’ (projectObj f X âŸ¶ projectObj f Y)
  | _, _, Hom.id _ => ğŸ™ _
  | _, _, Î±_hom _ _ _ => (Î±_ _ _ _).hom
  | _, _, Î±_inv _ _ _ => (Î±_ _ _ _).inv
  | _, _, l_hom _ => (Î»_ _).hom
  | _, _, l_inv _ => (Î»_ _).inv
  | _, _, Ï_hom _ => (Ï_ _).hom
  | _, _, Ï_inv _ => (Ï_ _).inv
  | _, _, Hom.comp f g => projectMapAux f â‰« projectMapAux g
  | _, _, Hom.whiskerLeft X p => projectObj f X â— projectMapAux p
  | _, _, Hom.whiskerRight p X => projectMapAux p â–· projectObj f X
  | _, _, Hom.tensor f g => projectMapAux f âŠ— projectMapAux g

-- Porting note: this declaration generates the same panic.
/-- Auxiliary definition for `FreeMonoidalCategory.project`. -/
def projectMap (X Y : F C) : (X âŸ¶ Y) â†’ (projectObj f X âŸ¶ projectObj f Y) :=
  Quotient.lift (projectMapAux f) <| by
    intro f g h
    induction h with
    | refl => rfl
    | symm _ _ _ hfg' => exact hfg'.symm
    | trans _ _ hfg hgh => exact hfg.trans hgh
    | comp _ _ hf hg => dsimp only [projectMapAux]; rw [hf, hg]
    | whiskerLeft _ _ _ _ hf => dsimp only [projectMapAux, projectObj]; rw [hf]
    | whiskerRight _ _ _ _ hf => dsimp only [projectMapAux, projectObj]; rw [hf]
    | tensor _ _ hfg hfg' => dsimp only [projectMapAux]; rw [hfg, hfg']
    | tensorHom_def _ _ =>
        dsimp only [projectMapAux, projectObj]; rw [MonoidalCategory.tensorHom_def]
    | comp_id => dsimp only [projectMapAux]; rw [Category.comp_id]
    | id_comp => dsimp only [projectMapAux]; rw [Category.id_comp]
    | assoc => dsimp only [projectMapAux]; rw [Category.assoc]
    | tensor_id => dsimp only [projectMapAux]; rw [MonoidalCategory.tensor_id]; rfl
    | tensor_comp => dsimp only [projectMapAux]; rw [MonoidalCategory.tensor_comp]
    | whiskerLeft_id =>
        dsimp only [projectMapAux, projectObj]
        rw [MonoidalCategory.whiskerLeft_id]
    | id_whiskerRight =>
        dsimp only [projectMapAux, projectObj]
        rw [MonoidalCategory.id_whiskerRight]
    | Î±_hom_inv => dsimp only [projectMapAux]; rw [Iso.hom_inv_id]
    | Î±_inv_hom => dsimp only [projectMapAux]; rw [Iso.inv_hom_id]
    | associator_naturality =>
        dsimp only [projectMapAux]; rw [MonoidalCategory.associator_naturality]
    | Ï_hom_inv => dsimp only [projectMapAux]; rw [Iso.hom_inv_id]
    | Ï_inv_hom => dsimp only [projectMapAux]; rw [Iso.inv_hom_id]
    | Ï_naturality =>
        dsimp only [projectMapAux, projectObj]
        rw [MonoidalCategory.rightUnitor_naturality]
    | l_hom_inv => dsimp only [projectMapAux]; rw [Iso.hom_inv_id]
    | l_inv_hom => dsimp only [projectMapAux]; rw [Iso.inv_hom_id]
    | l_naturality =>
        dsimp only [projectMapAux, projectObj]
        rw [MonoidalCategory.leftUnitor_naturality]
    | pentagon =>
        dsimp only [projectMapAux, projectObj]
        rw [MonoidalCategory.pentagon]
    | triangle =>
        dsimp only [projectMapAux, projectObj]
        rw [MonoidalCategory.triangle]

end

/-- If `D` is a monoidal category and we have a function `C â†’ D`, then we have a functor from the
    free monoidal category over `C` to the category `D`. -/
def project : MonoidalFunctor (F C) D where
  obj := projectObj f
  map := projectMap f _ _
  -- Porting note: `map_comp` and `Î¼_natural` were proved in mathlib3 by tidy, using induction.
  -- We probably don't expect `aesop_cat` to handle this yet, see https://leanprover.zulipchat.com/#narrow/stream/287929-mathlib4/topic/Aesop.20and.20cases
  -- In any case I don't understand why we need to specify `using Quotient.recOn`.
  map_comp := by rintro _ _ _ âŸ¨_âŸ© âŸ¨_âŸ©; rfl
  Îµ := ğŸ™ _
  Î¼ X Y := ğŸ™ _
  Î¼_natural_left := fun f _ => by
    induction' f using Quotient.recOn
    Â· dsimp
      simp only [Category.comp_id, Category.id_comp]
      rw [â† tensorHom_id, â† tensorHom_id]
      rfl
    Â· rfl
  Î¼_natural_right := fun _ f => by
    induction' f using Quotient.recOn
    Â· dsimp
      simp only [Category.comp_id, Category.id_comp]
      rw [â† id_tensorHom, â† id_tensorHom]
      rfl
    Â· rfl

end Functor

end

end FreeMonoidalCategory

end CategoryTheory
