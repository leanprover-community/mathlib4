/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.CategoryTheory.Extensive
import Mathlib.CategoryTheory.Limits.Shapes.KernelPair

#align_import category_theory.adhesive from "leanprover-community/mathlib"@"afff1f24a6b68d0077c9d63782a1d093e337758c"

/-!

# Adhesive categories

## Main definitions
- `CategoryTheory.IsPushout.IsVanKampen`: A convenience formulation for a pushout being
  a van Kampen colimit.
- `CategoryTheory.Adhesive`: A category is adhesive if it has pushouts and pullbacks along
  monomorphisms, and such pushouts are van Kampen.

## Main Results
- `CategoryTheory.Type.adhesive`: The category of `Type` is adhesive.
- `CategoryTheory.Adhesive.isPullback_of_isPushout_of_mono_left`: In adhesive categories,
  pushouts along monomorphisms are pullbacks.
- `CategoryTheory.Adhesive.mono_of_isPushout_of_mono_left`: In adhesive categories,
  monomorphisms are stable under pushouts.
- `CategoryTheory.Adhesive.toRegularMonoCategory`: Monomorphisms in adhesive categories are
  regular (this implies that adhesive categories are balanced).

## TODO

Show that the following are adhesive:
- functor categories into adhesive categories
- the categories of sheaves over a site

## References
- https://ncatlab.org/nlab/show/adhesive+category
- [Stephen Lack and PaweÅ‚ SobociÅ„ski, Adhesive Categories][adhesive2004]

-/


namespace CategoryTheory

open Limits

universe v' u' v u

variable {J : Type v'} [Category.{u'} J] {C : Type u} [Category.{v} C]

variable {W X Y Z : C} {f : W âŸ¶ X} {g : W âŸ¶ Y} {h : X âŸ¶ Z} {i : Y âŸ¶ Z}

-- This only makes sense when the original diagram is a pushout.
/-- A convenience formulation for a pushout being a van Kampen colimit.
See `IsPushout.isVanKampen_iff` below. -/
@[nolint unusedArguments]
def IsPushout.IsVanKampen (_ : IsPushout f g h i) : Prop :=
  âˆ€ â¦ƒW' X' Y' Z' : Câ¦„ (f' : W' âŸ¶ X') (g' : W' âŸ¶ Y') (h' : X' âŸ¶ Z') (i' : Y' âŸ¶ Z') (Î±W : W' âŸ¶ W)
    (Î±X : X' âŸ¶ X) (Î±Y : Y' âŸ¶ Y) (Î±Z : Z' âŸ¶ Z) (_ : IsPullback f' Î±W Î±X f)
    (_ : IsPullback g' Î±W Î±Y g) (_ : CommSq h' Î±X Î±Z h) (_ : CommSq i' Î±Y Î±Z i)
    (_ : CommSq f' g' h' i'), IsPushout f' g' h' i' â†” IsPullback h' Î±X Î±Z h âˆ§ IsPullback i' Î±Y Î±Z i
#align category_theory.is_pushout.is_van_kampen CategoryTheory.IsPushout.IsVanKampen

theorem IsPushout.IsVanKampen.flip {H : IsPushout f g h i} (H' : H.IsVanKampen) :
    H.flip.IsVanKampen := by
  introv W' hf hg hh hi w
  simpa only [IsPushout.flip_iff, IsPullback.flip_iff, and_comm] using
    H' g' f' i' h' Î±W Î±Y Î±X Î±Z hg hf hi hh w.flip
#align category_theory.is_pushout.is_van_kampen.flip CategoryTheory.IsPushout.IsVanKampen.flip

theorem IsPushout.isVanKampen_iff (H : IsPushout f g h i) :
    H.IsVanKampen â†” IsVanKampenColimit (PushoutCocone.mk h i H.w) := by
  constructor
  Â· intro H F' c' Î± fÎ± eÎ± hÎ±
    refine' Iff.trans _
        ((H (F'.map WalkingSpan.Hom.fst) (F'.map WalkingSpan.Hom.snd) (c'.Î¹.app _) (c'.Î¹.app _)
          (Î±.app _) (Î±.app _) (Î±.app _) fÎ± (by convert hÎ± WalkingSpan.Hom.fst)
          (by convert hÎ± WalkingSpan.Hom.snd) _ _ _).trans _)
    Â· have : F'.map WalkingSpan.Hom.fst â‰« c'.Î¹.app WalkingSpan.left =
          F'.map WalkingSpan.Hom.snd â‰« c'.Î¹.app WalkingSpan.right := by
        simp only [Cocone.w]
      rw [(IsColimit.equivOfNatIsoOfIso (diagramIsoSpan F') c' (PushoutCocone.mk _ _ this)
            _).nonempty_congr]
      Â· exact âŸ¨fun h => âŸ¨âŸ¨thisâŸ©, hâŸ©, fun h => h.2âŸ©
      Â· refine' Cocones.ext (Iso.refl c'.pt) _
        rintro (_ | _ | _) <;> dsimp <;>
          simp only [c'.w, Category.assoc, Category.id_comp, Category.comp_id]
    Â· exact âŸ¨NatTrans.congr_app eÎ±.symm _âŸ©
    Â· exact âŸ¨NatTrans.congr_app eÎ±.symm _âŸ©
    Â· exact âŸ¨by simpâŸ©
    constructor
    Â· rintro âŸ¨hâ‚, hâ‚‚âŸ© (_ | _ | _)
      Â· rw [â† c'.w WalkingSpan.Hom.fst]; exact (hÎ± WalkingSpan.Hom.fst).paste_horiz hâ‚
      exacts [hâ‚, hâ‚‚]
    Â· intro h; exact âŸ¨h _, h _âŸ©
  Â· introv H W' hf hg hh hi w
    refine'
      Iff.trans _ ((H w.cocone âŸ¨by rintro (_ | _ | _); exacts [Î±W, Î±X, Î±Y], _âŸ© Î±Z _ _).trans _)
    rotate_left
    Â· rintro i _ (_ | _ | _)
      Â· dsimp; simp only [Functor.map_id, Category.comp_id, Category.id_comp]
      exacts [hf.w, hg.w]
    Â· ext (_ | _ | _)
      Â· dsimp; rw [PushoutCocone.condition_zero]; erw [Category.assoc, hh.w, hf.w_assoc]
      exacts [hh.w.symm, hi.w.symm]
    Â· rintro i _ (_ | _ | _)
      Â· dsimp; simp_rw [Functor.map_id]
        exact IsPullback.of_horiz_isIso âŸ¨by rw [Category.comp_id, Category.id_comp]âŸ©
      exacts [hf, hg]
    Â· constructor
      Â· intro h; exact âŸ¨h WalkingCospan.left, h WalkingCospan.rightâŸ©
      Â· rintro âŸ¨hâ‚, hâ‚‚âŸ© (_ | _ | _)
        Â· dsimp; rw [PushoutCocone.condition_zero]; exact hf.paste_horiz hâ‚
        exacts [hâ‚, hâ‚‚]
    Â· exact âŸ¨fun h => h.2, fun h => âŸ¨w, hâŸ©âŸ©
#align category_theory.is_pushout.is_van_kampen_iff CategoryTheory.IsPushout.isVanKampen_iff

theorem is_coprod_iff_isPushout {X E Y YE : C} (c : BinaryCofan X E) (hc : IsColimit c) {f : X âŸ¶ Y}
    {iY : Y âŸ¶ YE} {fE : c.pt âŸ¶ YE} (H : CommSq f c.inl iY fE) :
    Nonempty (IsColimit (BinaryCofan.mk (c.inr â‰« fE) iY)) â†” IsPushout f c.inl iY fE := by
  constructor
  Â· rintro âŸ¨hâŸ©
    refine' âŸ¨H, âŸ¨Limits.PushoutCocone.isColimitAux' _ _âŸ©âŸ©
    intro s
    dsimp only [PushoutCocone.inr, PushoutCocone.mk] -- Porting note: Originally `dsimp`
    refine' âŸ¨h.desc (BinaryCofan.mk (c.inr â‰« s.inr) s.inl), h.fac _ âŸ¨WalkingPair.rightâŸ©, _, _âŸ©
    Â· apply BinaryCofan.IsColimit.hom_ext hc
      Â· rw [â† H.w_assoc]; erw [h.fac _ âŸ¨WalkingPair.rightâŸ©]; exact s.condition
      Â· rw [â† Category.assoc]; exact h.fac _ âŸ¨WalkingPair.leftâŸ©
    Â· intro m eâ‚ eâ‚‚
      apply BinaryCofan.IsColimit.hom_ext h
      Â· dsimp only [BinaryCofan.mk, id] -- Porting note: Originally `dsimp`
        rw [Category.assoc, eâ‚‚, eq_comm]; exact h.fac _ âŸ¨WalkingPair.leftâŸ©
      Â· refine' eâ‚.trans (Eq.symm _); exact h.fac _ _
  Â· refine' fun H => âŸ¨_âŸ©
    fapply Limits.BinaryCofan.isColimitMk
    Â· exact fun s => H.isColimit.desc (PushoutCocone.mk s.inr _ <|
        (hc.fac (BinaryCofan.mk (f â‰« s.inr) s.inl) âŸ¨WalkingPair.leftâŸ©).symm)
    Â· intro s
      erw [Category.assoc, H.isColimit.fac _ WalkingSpan.right, hc.fac]; rfl
    Â· intro s; exact H.isColimit.fac _ WalkingSpan.left
    Â· intro s m eâ‚ eâ‚‚
      apply PushoutCocone.IsColimit.hom_ext H.isColimit
      Â· symm; exact (H.isColimit.fac _ WalkingSpan.left).trans eâ‚‚.symm
      Â· erw [H.isColimit.fac _ WalkingSpan.right]
        apply BinaryCofan.IsColimit.hom_ext hc
        Â· erw [hc.fac, â† H.w_assoc, eâ‚‚]; rfl
        Â· refine' ((Category.assoc _ _ _).symm.trans eâ‚).trans _; symm; exact hc.fac _ _
#align category_theory.is_coprod_iff_is_pushout CategoryTheory.is_coprod_iff_isPushout

theorem IsPushout.isVanKampen_inl {W E X Z : C} (c : BinaryCofan W E) [FinitaryExtensive C]
    [HasPullbacks C] (hc : IsColimit c) (f : W âŸ¶ X) (h : X âŸ¶ Z) (i : c.pt âŸ¶ Z)
    (H : IsPushout f c.inl h i) : H.IsVanKampen := by
  obtain âŸ¨hcâ‚âŸ© := (is_coprod_iff_isPushout c hc H.1).mpr H
  introv W' hf hg hh hi w
  obtain âŸ¨hcâ‚‚âŸ© := ((BinaryCofan.isVanKampen_iff _).mp (FinitaryExtensive.vanKampen c hc)
    (BinaryCofan.mk _ pullback.fst) _ _ _ hg.w.symm pullback.condition.symm).mpr
    âŸ¨hg, IsPullback.of_hasPullback Î±Y c.inrâŸ©
  refine' (is_coprod_iff_isPushout _ hcâ‚‚ w).symm.trans _
  refine' ((BinaryCofan.isVanKampen_iff _).mp (FinitaryExtensive.vanKampen _ hcâ‚)
    (BinaryCofan.mk _ _) pullback.snd _ _ _ hh.w.symm).trans _
  Â· dsimp; rw [â† pullback.condition_assoc, Category.assoc, hi.w]
  constructor
  Â· rintro âŸ¨hcâ‚ƒ, hcâ‚„âŸ©
    refine' âŸ¨hcâ‚„, _âŸ©
    let Y'' := pullback Î±Z i
    let cmp : Y' âŸ¶ Y'' := pullback.lift i' Î±Y hi.w
    have eâ‚ : (g' â‰« cmp) â‰« pullback.snd = Î±W â‰« c.inl := by
      rw [Category.assoc, pullback.lift_snd, hg.w]
    have eâ‚‚ : (pullback.fst â‰« cmp : pullback Î±Y c.inr âŸ¶ _) â‰« pullback.snd = pullback.snd â‰« c.inr :=
      by rw [Category.assoc, pullback.lift_snd, pullback.condition]
    obtain âŸ¨hcâ‚„âŸ© := ((BinaryCofan.isVanKampen_iff _).mp (FinitaryExtensive.vanKampen c hc)
      (BinaryCofan.mk _ _) Î±W _ _ eâ‚.symm eâ‚‚.symm).mpr <| by
        constructor
        Â· apply IsPullback.of_right _ eâ‚ (IsPullback.of_hasPullback _ _)
          rw [Category.assoc, pullback.lift_fst, â† H.w, â† w.w]; exact hf.paste_horiz hcâ‚„
        Â· apply IsPullback.of_right _ eâ‚‚ (IsPullback.of_hasPullback _ _)
          rw [Category.assoc, pullback.lift_fst]; exact hcâ‚ƒ
    Â· rw [â† Category.id_comp Î±Z, â† show cmp â‰« pullback.snd = Î±Y from pullback.lift_snd _ _ _]
      apply IsPullback.paste_vert _ (IsPullback.of_hasPullback Î±Z i)
      have : cmp = (hcâ‚‚.coconePointUniqueUpToIso hcâ‚„).hom := by
        apply BinaryCofan.IsColimit.hom_ext hcâ‚‚
        exacts [(hcâ‚‚.comp_coconePointUniqueUpToIso_hom hcâ‚„ âŸ¨WalkingPair.leftâŸ©).symm,
          (hcâ‚‚.comp_coconePointUniqueUpToIso_hom hcâ‚„ âŸ¨WalkingPair.rightâŸ©).symm]
      rw [this]
      exact IsPullback.of_vert_isIso âŸ¨by rw [â† this, Category.comp_id, pullback.lift_fst]âŸ©
  Â· rintro âŸ¨hcâ‚ƒ, hcâ‚„âŸ©
    exact âŸ¨(IsPullback.of_hasPullback Î±Y c.inr).paste_horiz hcâ‚„, hcâ‚ƒâŸ©
#align category_theory.is_pushout.is_van_kampen_inl CategoryTheory.IsPushout.isVanKampen_inl

theorem IsPushout.IsVanKampen.isPullback_of_mono_left [Mono f] {H : IsPushout f g h i}
    (H' : H.IsVanKampen) : IsPullback f g h i :=
  ((H' (ðŸ™ _) g g (ðŸ™ Y) (ðŸ™ _) f (ðŸ™ _) i (IsKernelPair.id_of_mono f)
      (IsPullback.of_vert_isIso âŸ¨by simpâŸ©) H.1.flip âŸ¨rflâŸ© âŸ¨by simpâŸ©).mp
    (IsPushout.of_horiz_isIso âŸ¨by simpâŸ©)).1.flip
#align category_theory.is_pushout.is_van_kampen.is_pullback_of_mono_left CategoryTheory.IsPushout.IsVanKampen.isPullback_of_mono_left

theorem IsPushout.IsVanKampen.isPullback_of_mono_right [Mono g] {H : IsPushout f g h i}
    (H' : H.IsVanKampen) : IsPullback f g h i :=
  ((H' f (ðŸ™ _) (ðŸ™ _) f (ðŸ™ _) (ðŸ™ _) g h (IsPullback.of_vert_isIso âŸ¨by simpâŸ©)
      (IsKernelPair.id_of_mono g) âŸ¨rflâŸ© H.1 âŸ¨by simpâŸ©).mp
    (IsPushout.of_vert_isIso âŸ¨by simpâŸ©)).2
#align category_theory.is_pushout.is_van_kampen.is_pullback_of_mono_right CategoryTheory.IsPushout.IsVanKampen.isPullback_of_mono_right

theorem IsPushout.IsVanKampen.mono_of_mono_left [Mono f] {H : IsPushout f g h i}
    (H' : H.IsVanKampen) : Mono i :=
  IsKernelPair.mono_of_isIso_fst
    ((H' (ðŸ™ _) g g (ðŸ™ Y) (ðŸ™ _) f (ðŸ™ _) i (IsKernelPair.id_of_mono f)
        (IsPullback.of_vert_isIso âŸ¨by simpâŸ©) H.1.flip âŸ¨rflâŸ© âŸ¨by simpâŸ©).mp
      (IsPushout.of_horiz_isIso âŸ¨by simpâŸ©)).2
#align category_theory.is_pushout.is_van_kampen.mono_of_mono_left CategoryTheory.IsPushout.IsVanKampen.mono_of_mono_left

theorem IsPushout.IsVanKampen.mono_of_mono_right [Mono g] {H : IsPushout f g h i}
    (H' : H.IsVanKampen) : Mono h :=
  IsKernelPair.mono_of_isIso_fst
    ((H' f (ðŸ™ _) (ðŸ™ _) f (ðŸ™ _) (ðŸ™ _) g h (IsPullback.of_vert_isIso âŸ¨by simpâŸ©)
        (IsKernelPair.id_of_mono g) âŸ¨rflâŸ© H.1 âŸ¨by simpâŸ©).mp
      (IsPushout.of_vert_isIso âŸ¨by simpâŸ©)).1
#align category_theory.is_pushout.is_van_kampen.mono_of_mono_right CategoryTheory.IsPushout.IsVanKampen.mono_of_mono_right

/-- A category is adhesive if it has pushouts and pullbacks along monomorphisms,
and such pushouts are van Kampen. -/
class Adhesive (C : Type u) [Category.{v} C] : Prop where
  [hasPullback_of_mono_left : âˆ€ {X Y S : C} (f : X âŸ¶ S) (g : Y âŸ¶ S) [Mono f], HasPullback f g]
  [hasPushout_of_mono_left : âˆ€ {X Y S : C} (f : S âŸ¶ X) (g : S âŸ¶ Y) [Mono f], HasPushout f g]
  van_kampen : âˆ€ {W X Y Z : C} {f : W âŸ¶ X} {g : W âŸ¶ Y} {h : X âŸ¶ Z} {i : Y âŸ¶ Z} [Mono f]
    (H : IsPushout f g h i), H.IsVanKampen
#align category_theory.adhesive CategoryTheory.Adhesive

attribute [instance] Adhesive.hasPullback_of_mono_left Adhesive.hasPushout_of_mono_left

theorem Adhesive.van_kampen' [Adhesive C] [Mono g] (H : IsPushout f g h i) : H.IsVanKampen :=
  (Adhesive.van_kampen H.flip).flip
#align category_theory.adhesive.van_kampen' CategoryTheory.Adhesive.van_kampen'

theorem Adhesive.isPullback_of_isPushout_of_mono_left [Adhesive C] (H : IsPushout f g h i)
    [Mono f] : IsPullback f g h i :=
  (Adhesive.van_kampen H).isPullback_of_mono_left
#align category_theory.adhesive.is_pullback_of_is_pushout_of_mono_left CategoryTheory.Adhesive.isPullback_of_isPushout_of_mono_left

theorem Adhesive.isPullback_of_isPushout_of_mono_right [Adhesive C] (H : IsPushout f g h i)
    [Mono g] : IsPullback f g h i :=
  (Adhesive.van_kampen' H).isPullback_of_mono_right
#align category_theory.adhesive.is_pullback_of_is_pushout_of_mono_right CategoryTheory.Adhesive.isPullback_of_isPushout_of_mono_right

theorem Adhesive.mono_of_isPushout_of_mono_left [Adhesive C] (H : IsPushout f g h i) [Mono f] :
    Mono i :=
  (Adhesive.van_kampen H).mono_of_mono_left
#align category_theory.adhesive.mono_of_is_pushout_of_mono_left CategoryTheory.Adhesive.mono_of_isPushout_of_mono_left

theorem Adhesive.mono_of_isPushout_of_mono_right [Adhesive C] (H : IsPushout f g h i) [Mono g] :
    Mono h :=
  (Adhesive.van_kampen' H).mono_of_mono_right
#align category_theory.adhesive.mono_of_is_pushout_of_mono_right CategoryTheory.Adhesive.mono_of_isPushout_of_mono_right

instance Type.adhesive : Adhesive (Type u) :=
  âŸ¨fun {_ _ _ _ f _ _ _ _} H =>
    (IsPushout.isVanKampen_inl _ (Types.isCoprodOfMono f) _ _ _ H.flip).flipâŸ©
#align category_theory.type.adhesive CategoryTheory.Type.adhesive

noncomputable instance (priority := 100) Adhesive.toRegularMonoCategory [Adhesive C] :
    RegularMonoCategory C :=
  âŸ¨fun f _ =>
    { Z := pushout f f
      left := pushout.inl
      right := pushout.inr
      w := pushout.condition
      isLimit := (Adhesive.isPullback_of_isPushout_of_mono_left
        (IsPushout.of_hasPushout f f)).isLimitFork }âŸ©
#align category_theory.adhesive.to_regular_mono_category CategoryTheory.Adhesive.toRegularMonoCategory

-- This then implies that adhesive categories are balanced
example [Adhesive C] : Balanced C :=
  inferInstance

end CategoryTheory
