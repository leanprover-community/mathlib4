/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.CategoryTheory.Extensive
import Mathlib.CategoryTheory.Limits.Shapes.KernelPair
import Mathlib.CategoryTheory.Limits.Constructions.EpiMono

/-!

# Adhesive categories

## Main definitions
- `CategoryTheory.IsPushout.IsVanKampen`: A convenience formulation for a pushout being
  a van Kampen colimit.
- `CategoryTheory.Adhesive`: A category is adhesive if it has pushouts and pullbacks along
  monomorphisms, and such pushouts are van Kampen.

## Main Results
- `CategoryTheory.Type.adhesive`: The category of `Type` is adhesive.
- `CategoryTheory.Adhesive.isPullback_of_isPushout_of_mono_left`: In adhesive categories,
  pushouts along monomorphisms are pullbacks.
- `CategoryTheory.Adhesive.mono_of_isPushout_of_mono_left`: In adhesive categories,
  monomorphisms are stable under pushouts.
- `CategoryTheory.Adhesive.toRegularMonoCategory`: Monomorphisms in adhesive categories are
  regular (this implies that adhesive categories are balanced).
- `CategoryTheory.adhesive_functor`: The category `C ‚•§ D` is adhesive if `D`
  has all pullbacks and all pushouts and is adhesive

## References
- https://ncatlab.org/nlab/show/adhesive+category
- [Stephen Lack and Pawe≈Ç Soboci≈Ñski, Adhesive Categories][adhesive2004]

-/


namespace CategoryTheory

open Limits

universe v' u' v u

variable {J : Type v'} [Category.{u'} J] {C : Type u} [Category.{v} C]
variable {W X Y Z : C} {f : W ‚ü∂ X} {g : W ‚ü∂ Y} {h : X ‚ü∂ Z} {i : Y ‚ü∂ Z}

-- This only makes sense when the original diagram is a pushout.
/-- A convenience formulation for a pushout being a van Kampen colimit.
See `IsPushout.isVanKampen_iff` below. -/
@[nolint unusedArguments]
def IsPushout.IsVanKampen (_ : IsPushout f g h i) : Prop :=
  ‚àÄ ‚¶ÉW' X' Y' Z' : C‚¶Ñ (f' : W' ‚ü∂ X') (g' : W' ‚ü∂ Y') (h' : X' ‚ü∂ Z') (i' : Y' ‚ü∂ Z') (Œ±W : W' ‚ü∂ W)
    (Œ±X : X' ‚ü∂ X) (Œ±Y : Y' ‚ü∂ Y) (Œ±Z : Z' ‚ü∂ Z) (_ : IsPullback f' Œ±W Œ±X f)
    (_ : IsPullback g' Œ±W Œ±Y g) (_ : CommSq h' Œ±X Œ±Z h) (_ : CommSq i' Œ±Y Œ±Z i)
    (_ : CommSq f' g' h' i'), IsPushout f' g' h' i' ‚Üî IsPullback h' Œ±X Œ±Z h ‚àß IsPullback i' Œ±Y Œ±Z i

theorem IsPushout.IsVanKampen.flip {H : IsPushout f g h i} (H' : H.IsVanKampen) :
    H.flip.IsVanKampen := by
  introv W' hf hg hh hi w
  simpa only [IsPushout.flip_iff, IsPullback.flip_iff, and_comm] using
    H' g' f' i' h' Œ±W Œ±Y Œ±X Œ±Z hg hf hi hh w.flip

theorem IsPushout.isVanKampen_iff (H : IsPushout f g h i) :
    H.IsVanKampen ‚Üî IsVanKampenColimit (PushoutCocone.mk h i H.w) := by
  constructor
  ¬∑ intro H F' c' Œ± fŒ± eŒ± hŒ±
    refine Iff.trans ?_
        ((H (F'.map WalkingSpan.Hom.fst) (F'.map WalkingSpan.Hom.snd) (c'.Œπ.app _) (c'.Œπ.app _)
          (Œ±.app _) (Œ±.app _) (Œ±.app _) fŒ± (by convert hŒ± WalkingSpan.Hom.fst)
          (by convert hŒ± WalkingSpan.Hom.snd) ?_ ?_ ?_).trans ?_)
    ¬∑ have : F'.map WalkingSpan.Hom.fst ‚â´ c'.Œπ.app WalkingSpan.left =
          F'.map WalkingSpan.Hom.snd ‚â´ c'.Œπ.app WalkingSpan.right := by
        simp only [Cocone.w]
      rw [(IsColimit.equivOfNatIsoOfIso (diagramIsoSpan F') c' (PushoutCocone.mk _ _ this)
            _).nonempty_congr]
      ¬∑ exact ‚ü®fun h => ‚ü®‚ü®this‚ü©, h‚ü©, fun h => h.2‚ü©
      ¬∑ refine Cocones.ext (Iso.refl c'.pt) ?_
        rintro (_ | _ | _) <;> dsimp <;>
          simp only [c'.w, Category.id_comp, Category.comp_id]
    ¬∑ exact ‚ü®NatTrans.congr_app eŒ±.symm _‚ü©
    ¬∑ exact ‚ü®NatTrans.congr_app eŒ±.symm _‚ü©
    ¬∑ exact ‚ü®by simp‚ü©
    constructor
    ¬∑ rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© (_ | _ | _)
      ¬∑ rw [‚Üê c'.w WalkingSpan.Hom.fst]; exact (hŒ± WalkingSpan.Hom.fst).paste_horiz h‚ÇÅ
      exacts [h‚ÇÅ, h‚ÇÇ]
    ¬∑ intro h; exact ‚ü®h _, h _‚ü©
  ¬∑ introv H W' hf hg hh hi w
    refine
      Iff.trans ?_ ((H w.cocone ‚ü®by rintro (_ | _ | _); exacts [Œ±W, Œ±X, Œ±Y], ?_‚ü© Œ±Z ?_ ?_).trans ?_)
    rotate_left
    ¬∑ rintro i _ (_ | _ | _)
      ¬∑ dsimp; simp only [Functor.map_id, Category.comp_id, Category.id_comp]
      exacts [hf.w, hg.w]
    ¬∑ ext (_ | _ | _)
      ¬∑ dsimp
        rw [PushoutCocone.condition_zero, Category.assoc]
        erw [hh.w]
        rw [hf.w_assoc]
      exacts [hh.w.symm, hi.w.symm]
    ¬∑ rintro i _ (_ | _ | _)
      ¬∑ dsimp; simp_rw [Functor.map_id]
        exact IsPullback.of_horiz_isIso ‚ü®by rw [Category.comp_id, Category.id_comp]‚ü©
      exacts [hf, hg]
    ¬∑ constructor
      ¬∑ intro h; exact ‚ü®h WalkingCospan.left, h WalkingCospan.right‚ü©
      ¬∑ rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© (_ | _ | _)
        ¬∑ dsimp; rw [PushoutCocone.condition_zero]; exact hf.paste_horiz h‚ÇÅ
        exacts [h‚ÇÅ, h‚ÇÇ]
    ¬∑ exact ‚ü®fun h => h.2, fun h => ‚ü®w, h‚ü©‚ü©

theorem is_coprod_iff_isPushout {X E Y YE : C} (c : BinaryCofan X E) (hc : IsColimit c) {f : X ‚ü∂ Y}
    {iY : Y ‚ü∂ YE} {fE : c.pt ‚ü∂ YE} (H : CommSq f c.inl iY fE) :
    Nonempty (IsColimit (BinaryCofan.mk (c.inr ‚â´ fE) iY)) ‚Üî IsPushout f c.inl iY fE := by
  constructor
  ¬∑ rintro ‚ü®h‚ü©
    refine ‚ü®H, ‚ü®Limits.PushoutCocone.isColimitAux' _ ?_‚ü©‚ü©
    intro s
    dsimp
    refine ‚ü®h.desc (BinaryCofan.mk (c.inr ‚â´ s.inr) s.inl), h.fac _ ‚ü®WalkingPair.right‚ü©, ?_, ?_‚ü©
    ¬∑ apply BinaryCofan.IsColimit.hom_ext hc
      ¬∑ rw [‚Üê H.w_assoc]; erw [h.fac _ ‚ü®WalkingPair.right‚ü©]; exact s.condition
      ¬∑ rw [‚Üê Category.assoc]; exact h.fac _ ‚ü®WalkingPair.left‚ü©
    ¬∑ intro m e‚ÇÅ e‚ÇÇ
      apply BinaryCofan.IsColimit.hom_ext h
      ¬∑ dsimp
        rw [Category.assoc, e‚ÇÇ, eq_comm]; exact h.fac _ ‚ü®WalkingPair.left‚ü©
      ¬∑ refine e‚ÇÅ.trans (Eq.symm ?_); exact h.fac _ _
  ¬∑ refine fun H => ‚ü®?_‚ü©
    fapply Limits.BinaryCofan.isColimitMk
    ¬∑ exact fun s => H.isColimit.desc (PushoutCocone.mk s.inr _ <|
        (hc.fac (BinaryCofan.mk (f ‚â´ s.inr) s.inl) ‚ü®WalkingPair.left‚ü©).symm)
    ¬∑ intro s
      rw [Category.assoc]
      erw [H.isColimit.fac _ WalkingSpan.right]
      erw [hc.fac]
      rfl
    ¬∑ intro s; exact H.isColimit.fac _ WalkingSpan.left
    ¬∑ intro s m e‚ÇÅ e‚ÇÇ
      apply PushoutCocone.IsColimit.hom_ext H.isColimit
      ¬∑ symm; exact (H.isColimit.fac _ WalkingSpan.left).trans e‚ÇÇ.symm
      ¬∑ rw [H.isColimit.fac _ WalkingSpan.right]
        apply BinaryCofan.IsColimit.hom_ext hc
        ¬∑ erw [hc.fac]
          erw [‚Üê H.w_assoc]
          rw [e‚ÇÇ]
          rfl
        ¬∑ refine ((Category.assoc _ _ _).symm.trans e‚ÇÅ).trans ?_; symm; exact hc.fac _ _

theorem IsPushout.isVanKampen_inl {W E X Z : C} (c : BinaryCofan W E) [FinitaryExtensive C]
    [HasPullbacks C] (hc : IsColimit c) (f : W ‚ü∂ X) (h : X ‚ü∂ Z) (i : c.pt ‚ü∂ Z)
    (H : IsPushout f c.inl h i) : H.IsVanKampen := by
  obtain ‚ü®hc‚ÇÅ‚ü© := (is_coprod_iff_isPushout c hc H.1).mpr H
  introv W' hf hg hh hi w
  obtain ‚ü®hc‚ÇÇ‚ü© := ((BinaryCofan.isVanKampen_iff _).mp (FinitaryExtensive.vanKampen c hc)
    (BinaryCofan.mk _ (pullback.fst _ _)) _ _ _ hg.w.symm pullback.condition.symm).mpr
    ‚ü®hg, IsPullback.of_hasPullback Œ±Y c.inr‚ü©
  refine (is_coprod_iff_isPushout _ hc‚ÇÇ w).symm.trans ?_
  refine ((BinaryCofan.isVanKampen_iff _).mp (FinitaryExtensive.vanKampen _ hc‚ÇÅ)
    (BinaryCofan.mk _ _) (pullback.snd _ _) _ _ ?_ hh.w.symm).trans ?_
  ¬∑ dsimp; rw [‚Üê pullback.condition_assoc, Category.assoc, hi.w]
  constructor
  ¬∑ rintro ‚ü®hc‚ÇÉ, hc‚ÇÑ‚ü©
    refine ‚ü®hc‚ÇÑ, ?_‚ü©
    let Y'' := pullback Œ±Z i
    let cmp : Y' ‚ü∂ Y'' := pullback.lift i' Œ±Y hi.w
    have e‚ÇÅ : (g' ‚â´ cmp) ‚â´ pullback.snd _ _ = Œ±W ‚â´ c.inl := by
      rw [Category.assoc, pullback.lift_snd, hg.w]
    have e‚ÇÇ : (pullback.fst _ _ ‚â´ cmp : pullback Œ±Y c.inr ‚ü∂ _) ‚â´ pullback.snd _ _ =
        pullback.snd _ _ ‚â´ c.inr := by
      rw [Category.assoc, pullback.lift_snd, pullback.condition]
    obtain ‚ü®hc‚ÇÑ‚ü© := ((BinaryCofan.isVanKampen_iff _).mp (FinitaryExtensive.vanKampen c hc)
      (BinaryCofan.mk _ _) Œ±W _ _ e‚ÇÅ.symm e‚ÇÇ.symm).mpr <| by
        constructor
        ¬∑ apply IsPullback.of_right _ e‚ÇÅ (IsPullback.of_hasPullback _ _)
          rw [Category.assoc, pullback.lift_fst, ‚Üê H.w, ‚Üê w.w]; exact hf.paste_horiz hc‚ÇÑ
        ¬∑ apply IsPullback.of_right _ e‚ÇÇ (IsPullback.of_hasPullback _ _)
          rw [Category.assoc, pullback.lift_fst]; exact hc‚ÇÉ
    rw [‚Üê Category.id_comp Œ±Z, ‚Üê show cmp ‚â´ pullback.snd _ _ = Œ±Y from pullback.lift_snd _ _ _]
    apply IsPullback.paste_vert _ (IsPullback.of_hasPullback Œ±Z i)
    have : cmp = (hc‚ÇÇ.coconePointUniqueUpToIso hc‚ÇÑ).hom := by
      apply BinaryCofan.IsColimit.hom_ext hc‚ÇÇ
      exacts [(hc‚ÇÇ.comp_coconePointUniqueUpToIso_hom hc‚ÇÑ ‚ü®WalkingPair.left‚ü©).symm,
        (hc‚ÇÇ.comp_coconePointUniqueUpToIso_hom hc‚ÇÑ ‚ü®WalkingPair.right‚ü©).symm]
    rw [this]
    exact IsPullback.of_vert_isIso ‚ü®by rw [‚Üê this, Category.comp_id, pullback.lift_fst]‚ü©
  ¬∑ rintro ‚ü®hc‚ÇÉ, hc‚ÇÑ‚ü©
    exact ‚ü®(IsPullback.of_hasPullback Œ±Y c.inr).paste_horiz hc‚ÇÑ, hc‚ÇÉ‚ü©

theorem IsPushout.IsVanKampen.isPullback_of_mono_left [Mono f] {H : IsPushout f g h i}
    (H' : H.IsVanKampen) : IsPullback f g h i :=
  ((H' (ùüô _) g g (ùüô Y) (ùüô _) f (ùüô _) i (IsKernelPair.id_of_mono f)
      (IsPullback.of_vert_isIso ‚ü®by simp‚ü©) H.1.flip ‚ü®rfl‚ü© ‚ü®by simp‚ü©).mp
    (IsPushout.of_horiz_isIso ‚ü®by simp‚ü©)).1.flip

theorem IsPushout.IsVanKampen.isPullback_of_mono_right [Mono g] {H : IsPushout f g h i}
    (H' : H.IsVanKampen) : IsPullback f g h i :=
  ((H' f (ùüô _) (ùüô _) f (ùüô _) (ùüô _) g h (IsPullback.of_vert_isIso ‚ü®by simp‚ü©)
      (IsKernelPair.id_of_mono g) ‚ü®rfl‚ü© H.1 ‚ü®by simp‚ü©).mp
    (IsPushout.of_vert_isIso ‚ü®by simp‚ü©)).2

theorem IsPushout.IsVanKampen.mono_of_mono_left [Mono f] {H : IsPushout f g h i}
    (H' : H.IsVanKampen) : Mono i :=
  IsKernelPair.mono_of_isIso_fst
    ((H' (ùüô _) g g (ùüô Y) (ùüô _) f (ùüô _) i (IsKernelPair.id_of_mono f)
        (IsPullback.of_vert_isIso ‚ü®by simp‚ü©) H.1.flip ‚ü®rfl‚ü© ‚ü®by simp‚ü©).mp
      (IsPushout.of_horiz_isIso ‚ü®by simp‚ü©)).2

theorem IsPushout.IsVanKampen.mono_of_mono_right [Mono g] {H : IsPushout f g h i}
    (H' : H.IsVanKampen) : Mono h :=
  IsKernelPair.mono_of_isIso_fst
    ((H' f (ùüô _) (ùüô _) f (ùüô _) (ùüô _) g h (IsPullback.of_vert_isIso ‚ü®by simp‚ü©)
        (IsKernelPair.id_of_mono g) ‚ü®rfl‚ü© H.1 ‚ü®by simp‚ü©).mp
      (IsPushout.of_vert_isIso ‚ü®by simp‚ü©)).1

/-- A category is adhesive if it has pushouts and pullbacks along monomorphisms,
and such pushouts are van Kampen. -/
class Adhesive (C : Type u) [Category.{v} C] : Prop where
  [hasPullback_of_mono_left : ‚àÄ {X Y S : C} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [Mono f], HasPullback f g]
  [hasPushout_of_mono_left : ‚àÄ {X Y S : C} (f : S ‚ü∂ X) (g : S ‚ü∂ Y) [Mono f], HasPushout f g]
  van_kampen : ‚àÄ {W X Y Z : C} {f : W ‚ü∂ X} {g : W ‚ü∂ Y} {h : X ‚ü∂ Z} {i : Y ‚ü∂ Z} [Mono f]
    (H : IsPushout f g h i), H.IsVanKampen

attribute [instance] Adhesive.hasPullback_of_mono_left Adhesive.hasPushout_of_mono_left

theorem Adhesive.van_kampen' [Adhesive C] [Mono g] (H : IsPushout f g h i) : H.IsVanKampen :=
  (Adhesive.van_kampen H.flip).flip

theorem Adhesive.isPullback_of_isPushout_of_mono_left [Adhesive C] (H : IsPushout f g h i)
    [Mono f] : IsPullback f g h i :=
  (Adhesive.van_kampen H).isPullback_of_mono_left

theorem Adhesive.isPullback_of_isPushout_of_mono_right [Adhesive C] (H : IsPushout f g h i)
    [Mono g] : IsPullback f g h i :=
  (Adhesive.van_kampen' H).isPullback_of_mono_right

theorem Adhesive.mono_of_isPushout_of_mono_left [Adhesive C] (H : IsPushout f g h i) [Mono f] :
    Mono i :=
  (Adhesive.van_kampen H).mono_of_mono_left

theorem Adhesive.mono_of_isPushout_of_mono_right [Adhesive C] (H : IsPushout f g h i) [Mono g] :
    Mono h :=
  (Adhesive.van_kampen' H).mono_of_mono_right

instance Type.adhesive : Adhesive (Type u) :=
  ‚ü®fun {_ _ _ _ f _ _ _ _} H =>
    (IsPushout.isVanKampen_inl _ (Types.isCoprodOfMono f) _ _ _ H.flip).flip‚ü©

noncomputable instance (priority := 100) Adhesive.toRegularMonoCategory [Adhesive C] :
    IsRegularMonoCategory C where
  regularMonoOfMono f := ‚ü®‚ü®{
    Z := pushout f f
    left := pushout.inl _ _
    right := pushout.inr _ _
    w := pushout.condition
    isLimit := (Adhesive.isPullback_of_isPushout_of_mono_left
      (IsPushout.of_hasPushout f f)).isLimitFork }‚ü©‚ü©

-- This then implies that adhesive categories are balanced
example [Adhesive C] : Balanced C :=
  inferInstance

section functor

universe v'' u''

variable {D : Type u''} [Category.{v''} D]

instance adhesive_functor [Adhesive C] [HasPullbacks C] [HasPushouts C] :
    Adhesive (D ‚•§ C) := by
  constructor
  intro W X Y Z f g h i hf H
  rw [IsPushout.isVanKampen_iff]
  apply isVanKampenColimit_of_evaluation
  intro x
  refine (IsVanKampenColimit.precompose_isIso_iff (diagramIsoSpan _).inv).mp ?_
  refine IsVanKampenColimit.of_iso ?_ (PushoutCocone.isoMk _).symm
  refine (IsPushout.isVanKampen_iff (H.map ((evaluation _ _).obj x))).mp ?_
  apply Adhesive.van_kampen

theorem adhesive_of_preserves_and_reflects (F : C ‚•§ D) [Adhesive D]
    [H‚ÇÅ : ‚àÄ {X Y S : C} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [Mono f], HasPullback f g]
    [H‚ÇÇ : ‚àÄ {X Y S : C} (f : S ‚ü∂ X) (g : S ‚ü∂ Y) [Mono f], HasPushout f g]
    [PreservesLimitsOfShape WalkingCospan F]
    [ReflectsLimitsOfShape WalkingCospan F]
    [PreservesColimitsOfShape WalkingSpan F]
    [ReflectsColimitsOfShape WalkingSpan F] :
    Adhesive C := by
  apply Adhesive.mk (hasPullback_of_mono_left := H‚ÇÅ) (hasPushout_of_mono_left := H‚ÇÇ)
  intro W X Y Z f g h i hf H
  rw [IsPushout.isVanKampen_iff]
  refine IsVanKampenColimit.of_mapCocone F ?_
  refine (IsVanKampenColimit.precompose_isIso_iff (diagramIsoSpan _).inv).mp ?_
  refine IsVanKampenColimit.of_iso ?_ (PushoutCocone.isoMk _).symm
  refine (IsPushout.isVanKampen_iff (H.map F)).mp ?_
  apply Adhesive.van_kampen

theorem adhesive_of_preserves_and_reflects_isomorphism (F : C ‚•§ D)
    [Adhesive D] [HasPullbacks C] [HasPushouts C]
    [PreservesLimitsOfShape WalkingCospan F]
    [PreservesColimitsOfShape WalkingSpan F]
    [F.ReflectsIsomorphisms] :
    Adhesive C := by
  haveI : ReflectsLimitsOfShape WalkingCospan F :=
    reflectsLimitsOfShape_of_reflectsIsomorphisms
  haveI : ReflectsColimitsOfShape WalkingSpan F :=
    reflectsColimitsOfShape_of_reflectsIsomorphisms
  exact adhesive_of_preserves_and_reflects F

theorem adhesive_of_reflective [HasPullbacks D] [Adhesive C] [HasPullbacks C] [HasPushouts C]
    [H‚ÇÇ : ‚àÄ {X Y S : D} (f : S ‚ü∂ X) (g : S ‚ü∂ Y) [Mono f], HasPushout f g]
    {Gl : C ‚•§ D} {Gr : D ‚•§ C} (adj : Gl ‚ä£ Gr) [Gr.Full] [Gr.Faithful]
    [PreservesLimitsOfShape WalkingCospan Gl] :
    Adhesive D := by
  have := adj.leftAdjoint_preservesColimits
  have := adj.rightAdjoint_preservesLimits
  apply Adhesive.mk (hasPushout_of_mono_left := H‚ÇÇ)
  intro W X Y Z f g h i _ H
  have := Adhesive.van_kampen (IsPushout.of_hasPushout (Gr.map f) (Gr.map g))
  rw [IsPushout.isVanKampen_iff] at this ‚ä¢
  refine (IsVanKampenColimit.precompose_isIso_iff
    (Functor.isoWhiskerLeft _ (asIso adj.counit) ‚â™‚â´ Functor.rightUnitor _).hom).mp ?_
  refine ((this.precompose_isIso (spanCompIso _ _ _).hom).map_reflective adj).of_iso
    (IsColimit.uniqueUpToIso ?_ ?_)
  ¬∑ exact isColimitOfPreserves Gl ((IsColimit.precomposeHomEquiv _ _).symm <| pushoutIsPushout _ _)
  ¬∑ exact (IsColimit.precomposeHomEquiv _ _).symm H.isColimit

end functor

end CategoryTheory
