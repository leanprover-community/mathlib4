/-
Copyright (c) 2025 Calle S√∂nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle S√∂nne
-/
module

public import Mathlib.CategoryTheory.Bicategory.Modification.Pseudo

/-!
# The bicategory of pseudofunctors

Given bicategories `B` and `C`, we define a bicategory structure on `Pseudofunctor B C` whose
* objects are pseudofunctors,
* 1-morphisms are strong natural transformations, and
* 2-morphisms are modifications.

We scope this instance to the `CategoryTheory.Pseudofunctor.StrongTrans` namespace to avoid
potential future conflicts with other bicategory instances on `Pseudofunctor B C`.
-/

@[expose] public section

namespace CategoryTheory.Pseudofunctor

open Bicategory

universe w‚ÇÅ w‚ÇÇ v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

variable {B : Type u‚ÇÅ} [Bicategory.{w‚ÇÅ, v‚ÇÅ} B] {C : Type u‚ÇÇ} [Bicategory.{w‚ÇÇ, v‚ÇÇ} C]

namespace StrongTrans

variable {F G H I : Pseudofunctor B C}

/-- Left whiskering of a strong natural transformation between pseudofunctors
and a modification. -/
abbrev whiskerLeft (Œ∑ : F ‚ü∂ G) {Œ∏ Œπ : G ‚ü∂ H} (Œì : Œ∏ ‚ü∂ Œπ) : Œ∑ ‚â´ Œ∏ ‚ü∂ Œ∑ ‚â´ Œπ where
  as := {
    app a := Œ∑.app a ‚óÅ Œì.as.app a
    naturality {a b} f := by
      dsimp
      rw [associator_inv_naturality_right_assoc, whisker_exchange_assoc]
      simp }

/-- Right whiskering of a strong natural transformation between pseudofunctors
and a modification. -/
abbrev whiskerRight {Œ∑ Œ∏ : F ‚ü∂ G} (Œì : Œ∑ ‚ü∂ Œ∏) (Œπ : G ‚ü∂ H) : Œ∑ ‚â´ Œπ ‚ü∂ Œ∏ ‚â´ Œπ where
  as := {
    app a := Œì.as.app a ‚ñ∑ Œπ.app a
    naturality {a b} f := by
      dsimp
      simp_rw [Category.assoc, ‚Üê associator_inv_naturality_left, whisker_exchange_assoc]
      simp }

/-- Associator for the vertical composition of strong natural transformations
between pseudofunctors. -/
abbrev associator (Œ∑ : F ‚ü∂ G) (Œ∏ : G ‚ü∂ H) (Œπ : H ‚ü∂ I) : (Œ∑ ‚â´ Œ∏) ‚â´ Œπ ‚âÖ Œ∑ ‚â´ Œ∏ ‚â´ Œπ :=
  isoMk (fun a => Œ±_ (Œ∑.app a) (Œ∏.app a) (Œπ.app a))

/-- Left unitor for the vertical composition of strong natural transformations
between pseudofunctors. -/
abbrev leftUnitor (Œ∑ : F ‚ü∂ G) : ùüô F ‚â´ Œ∑ ‚âÖ Œ∑ :=
  isoMk (fun a => Œª_ (Œ∑.app a))

/-- Right unitor for the vertical composition of strong natural transformations
between pseudofunctors. -/
abbrev rightUnitor (Œ∑ : F ‚ü∂ G) : Œ∑ ‚â´ ùüô G ‚âÖ Œ∑ :=
  isoMk (fun a => œÅ_ (Œ∑.app a))

variable (B C)

/-- A bicategory structure on pseudofunctors, with strong transformations as 1-morphisms.

Note that this instance is scoped to the `Pseudofunctor.StrongTrans` namespace. -/
@[simps! whiskerLeft_as_app whiskerRight_as_app associator_hom_as_app associator_inv_as_app
rightUnitor_hom_as_app rightUnitor_inv_as_app leftUnitor_hom_as_app leftUnitor_inv_as_app]
scoped instance : Bicategory (Pseudofunctor B C) where
  whiskerLeft {F G H} Œ∑ _ _ Œì := StrongTrans.whiskerLeft Œ∑ Œì
  whiskerRight {F G H} _ _ Œì Œ∑ := StrongTrans.whiskerRight Œì Œ∑
  associator {F G H} I := StrongTrans.associator
  leftUnitor {F G} := StrongTrans.leftUnitor
  rightUnitor {F G} := StrongTrans.rightUnitor
  whisker_exchange {a b c f g h i} Œ∑ Œ∏ := by ext; exact whisker_exchange _ _

end StrongTrans

end CategoryTheory.Pseudofunctor
