/-
Copyright (c) 2023 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import Mathlib.Tactic.CategoryTheory.Coherence
import Mathlib.CategoryTheory.Bicategory.Coherence

/-!
# Adjunctions in bicategories

For 1-morphisms `f : a âŸ¶ b` and `g : b âŸ¶ a` in a bicategory, an adjunction between `f` and `g`
consists of a pair of 2-morphism `Î· : ğŸ™ a âŸ¶ f â‰« g` and `Îµ : g â‰« f âŸ¶ ğŸ™ b` satisfying the triangle
identities. The 2-morphism `Î·` is called the unit and `Îµ` is called the counit.

## Main definitions

* `Bicategory.Adjunction`: adjunctions between two 1-morphisms.
* `Bicategory.Equivalence`: adjoint equivalences between two objects.
* `Bicategory.mkOfAdjointifyCounit`: construct an adjoint equivalence from 2-isomorphisms
  `Î· : ğŸ™ a â‰… f â‰« g` and `Îµ : g â‰« f â‰… ğŸ™ b`, by upgrading `Îµ` to a counit.

## Implementation notes

The computation of 2-morphisms in the proof is done using `calc` blocks. Typically,
the LHS and the RHS in each step of `calc` are related by simple rewriting up to associators
and unitors. So the proof for each step should be of the form `rw [...]; coherence`. In practice,
our proofs look like `rw [...]; simp [bicategoricalComp]; coherence`. The `simp` is not strictly
necessary, but it speeds up the proof and allow us to avoid increasing the `maxHeartbeats`.
The speedup is probably due to reducing the length of the expression e.g. by absorbing
identity maps or applying the pentagon relation. Such a hack may not be necessary if the
coherence tactic is improved. One possible way would be to perform such a simplification in the
preprocessing of the coherence tactic.

## Todo

* `Bicategory.mkOfAdjointifyUnit`: construct an adjoint equivalence from 2-isomorphisms
  `Î· : ğŸ™ a â‰… f â‰« g` and `Îµ : g â‰« f â‰… ğŸ™ b`, by upgrading `Î·` to a unit.
-/

namespace CategoryTheory

namespace Bicategory

open Category

open scoped Bicategory

open Mathlib.Tactic.BicategoryCoherence (bicategoricalComp bicategoricalIsoComp)

universe w v u

variable {B : Type u} [Bicategory.{w, v} B] {a b c : B} {f : a âŸ¶ b} {g : b âŸ¶ a}

/-- The 2-morphism defined by the following pasting diagram:
```
a ï¼ï¼ï¼ï¼ï¼ï¼ â–¸ a
  ï¼¼    Î·      â—¥   ï¼¼
  f ï¼¼   g  ï¼       ï¼¼ f
       â—¢  ï¼     Îµ      â—¢
        b ï¼ï¼ï¼ï¼ï¼ï¼ â–¸ b
```
-/
def leftZigzag (Î· : ğŸ™ a âŸ¶ f â‰« g) (Îµ : g â‰« f âŸ¶ ğŸ™ b) :=
  Î· â–· f âŠ—â‰« f â— Îµ

/-- The 2-morphism defined by the following pasting diagram:
```
        a ï¼ï¼ï¼ï¼ï¼ï¼ â–¸ a
       â—¥  ï¼¼     Î·      â—¥
  g ï¼      ï¼¼ f     ï¼ g
  ï¼    Îµ      â—¢   ï¼
b ï¼ï¼ï¼ï¼ï¼ï¼ â–¸ b
```
-/
def rightZigzag (Î· : ğŸ™ a âŸ¶ f â‰« g) (Îµ : g â‰« f âŸ¶ ğŸ™ b) :=
  g â— Î· âŠ—â‰« Îµ â–· g

theorem rightZigzag_idempotent_of_left_triangle
    (Î· : ğŸ™ a âŸ¶ f â‰« g) (Îµ : g â‰« f âŸ¶ ğŸ™ b) (h : leftZigzag Î· Îµ = (Î»_ _).hom â‰« (Ï_ _).inv) :
    rightZigzag Î· Îµ âŠ—â‰« rightZigzag Î· Îµ = rightZigzag Î· Îµ := by
  dsimp only [rightZigzag]
  calc
    _ = g â— Î· âŠ—â‰« ((Îµ â–· g â–· ğŸ™ a) â‰« (ğŸ™ b â‰« g) â— Î·) âŠ—â‰« Îµ â–· g := by
      simp [bicategoricalComp]; coherence
    _ = ğŸ™ _ âŠ—â‰« g â— (Î· â–· ğŸ™ a â‰« (f â‰« g) â— Î·) âŠ—â‰« (Îµ â–· (g â‰« f) â‰« ğŸ™ b â— Îµ) â–· g âŠ—â‰« ğŸ™ _ := by
      rw [â† whisker_exchange]; simp [bicategoricalComp]; coherence
    _ = g â— Î· âŠ—â‰« g â— leftZigzag Î· Îµ â–· g âŠ—â‰« Îµ â–· g := by
      rw [â† whisker_exchange,  â† whisker_exchange]; simp [leftZigzag, bicategoricalComp]; coherence
    _ = g â— Î· âŠ—â‰« Îµ â–· g := by
      rw [h]; simp [bicategoricalComp]; coherence

/-- Adjunction between two 1-morphisms. -/
structure Adjunction (f : a âŸ¶ b) (g : b âŸ¶ a) where
  /-- The unit of an adjunction. -/
  unit : ğŸ™ a âŸ¶ f â‰« g
  /-- The counit of an adjunction. -/
  counit : g â‰« f âŸ¶ ğŸ™ b
  /-- The composition of the unit and the counit is equal to the identity up to unitors. -/
  left_triangle : leftZigzag unit counit = (Î»_ _).hom â‰« (Ï_ _).inv := by aesop_cat
  /-- The composition of the unit and the counit is equal to the identity up to unitors. -/
  right_triangle : rightZigzag unit counit = (Ï_ _).hom â‰« (Î»_ _).inv := by aesop_cat

@[inherit_doc] scoped infixr:15 " âŠ£ " => Bicategory.Adjunction

namespace Adjunction

attribute [simp] left_triangle right_triangle

attribute [local simp] leftZigzag rightZigzag

/-- Adjunction between identities. -/
def id (a : B) : ğŸ™ a âŠ£ ğŸ™ a where
  unit := (Ï_ _).inv
  counit := (Ï_ _).hom
  left_triangle := by dsimp; coherence
  right_triangle := by dsimp; coherence

instance : Inhabited (Adjunction (ğŸ™ a) (ğŸ™ a)) :=
  âŸ¨id aâŸ©

section Composition

variable {fâ‚ : a âŸ¶ b} {gâ‚ : b âŸ¶ a} {fâ‚‚ : b âŸ¶ c} {gâ‚‚ : c âŸ¶ b}

/-- Auxiliary definition for `adjunction.comp`. -/
@[simp]
def compUnit (adjâ‚ : fâ‚ âŠ£ gâ‚) (adjâ‚‚ : fâ‚‚ âŠ£ gâ‚‚) : ğŸ™ a âŸ¶ (fâ‚ â‰« fâ‚‚) â‰« gâ‚‚ â‰« gâ‚ :=
  adjâ‚.unit âŠ—â‰« fâ‚ â— adjâ‚‚.unit â–· gâ‚ âŠ—â‰« ğŸ™ _

/-- Auxiliary definition for `adjunction.comp`. -/
@[simp]
def compCounit (adjâ‚ : fâ‚ âŠ£ gâ‚) (adjâ‚‚ : fâ‚‚ âŠ£ gâ‚‚) : (gâ‚‚ â‰« gâ‚) â‰« fâ‚ â‰« fâ‚‚ âŸ¶ ğŸ™ c :=
  ğŸ™ _ âŠ—â‰« gâ‚‚ â— adjâ‚.counit â–· fâ‚‚ âŠ—â‰« adjâ‚‚.counit

theorem comp_left_triangle_aux (adjâ‚ : fâ‚ âŠ£ gâ‚) (adjâ‚‚ : fâ‚‚ âŠ£ gâ‚‚) :
    leftZigzag (compUnit adjâ‚ adjâ‚‚) (compCounit adjâ‚ adjâ‚‚) = (Î»_ _).hom â‰« (Ï_ _).inv := by
  calc
    _ = ğŸ™ _ âŠ—â‰«
          adjâ‚.unit â–· (fâ‚ â‰« fâ‚‚) âŠ—â‰«
            fâ‚ â— (adjâ‚‚.unit â–· (gâ‚ â‰« fâ‚) â‰« (fâ‚‚ â‰« gâ‚‚) â— adjâ‚.counit) â–· fâ‚‚ âŠ—â‰«
              (fâ‚ â‰« fâ‚‚) â— adjâ‚‚.counit âŠ—â‰« ğŸ™ _ := by
      simp [bicategoricalComp]; coherence
    _ = ğŸ™ _ âŠ—â‰«
          (leftZigzag adjâ‚.unit adjâ‚.counit) â–· fâ‚‚ âŠ—â‰«
            fâ‚ â— (leftZigzag adjâ‚‚.unit adjâ‚‚.counit) âŠ—â‰« ğŸ™ _ := by
      rw [â† whisker_exchange]; simp [bicategoricalComp]; coherence
    _ = _ := by
      simp_rw [left_triangle]; simp [bicategoricalComp]

theorem comp_right_triangle_aux (adjâ‚ : fâ‚ âŠ£ gâ‚) (adjâ‚‚ : fâ‚‚ âŠ£ gâ‚‚) :
    rightZigzag (compUnit adjâ‚ adjâ‚‚) (compCounit adjâ‚ adjâ‚‚) = (Ï_ _).hom â‰« (Î»_ _).inv := by
  calc
    _ = ğŸ™ _ âŠ—â‰«
          (gâ‚‚ â‰« gâ‚) â— adjâ‚.unit âŠ—â‰«
            gâ‚‚ â— ((gâ‚ â‰« fâ‚) â— adjâ‚‚.unit â‰« adjâ‚.counit â–· (fâ‚‚ â‰« gâ‚‚)) â–· gâ‚ âŠ—â‰«
              adjâ‚‚.counit â–· (gâ‚‚ â‰« gâ‚) âŠ—â‰« ğŸ™ _ := by
      simp [bicategoricalComp]; coherence
    _ = ğŸ™ _ âŠ—â‰«
          gâ‚‚ â— (rightZigzag adjâ‚.unit adjâ‚.counit) âŠ—â‰«
            (rightZigzag adjâ‚‚.unit adjâ‚‚.counit) â–· gâ‚ âŠ—â‰« ğŸ™ _ := by
      rw [whisker_exchange]; simp [bicategoricalComp]; coherence
    _ = _ := by
      simp_rw [right_triangle]; simp [bicategoricalComp]

/-- Composition of adjunctions. -/
@[simps]
def comp (adjâ‚ : fâ‚ âŠ£ gâ‚) (adjâ‚‚ : fâ‚‚ âŠ£ gâ‚‚) : fâ‚ â‰« fâ‚‚ âŠ£ gâ‚‚ â‰« gâ‚ where
  unit := compUnit adjâ‚ adjâ‚‚
  counit := compCounit adjâ‚ adjâ‚‚
  left_triangle := by apply comp_left_triangle_aux
  right_triangle := by apply comp_right_triangle_aux

end Composition

end Adjunction

noncomputable section

variable (Î· : ğŸ™ a â‰… f â‰« g) (Îµ : g â‰« f â‰… ğŸ™ b)

/-- The isomorphism version of `leftZigzag`. -/
def leftZigzagIso (Î· : ğŸ™ a â‰… f â‰« g) (Îµ : g â‰« f â‰… ğŸ™ b) :=
  whiskerRightIso Î· f â‰ªâŠ—â‰« whiskerLeftIso f Îµ

/-- The isomorphism version of `rightZigzag`. -/
def rightZigzagIso (Î· : ğŸ™ a â‰… f â‰« g) (Îµ : g â‰« f â‰… ğŸ™ b) :=
  whiskerLeftIso g Î· â‰ªâŠ—â‰« whiskerRightIso Îµ g

attribute [local simp] leftZigzagIso rightZigzagIso leftZigzag rightZigzag

@[simp]
theorem leftZigzagIso_hom : (leftZigzagIso Î· Îµ).hom = leftZigzag Î·.hom Îµ.hom :=
  rfl

@[simp]
theorem rightZigzagIso_hom : (rightZigzagIso Î· Îµ).hom = rightZigzag Î·.hom Îµ.hom :=
  rfl

@[simp]
theorem leftZigzagIso_inv : (leftZigzagIso Î· Îµ).inv = rightZigzag Îµ.inv Î·.inv := by
  simp [bicategoricalComp, bicategoricalIsoComp]

@[simp]
theorem rightZigzagIso_inv : (rightZigzagIso Î· Îµ).inv = leftZigzag Îµ.inv Î·.inv := by
  simp [bicategoricalComp, bicategoricalIsoComp]

@[simp]
theorem leftZigzagIso_symm : (leftZigzagIso Î· Îµ).symm = rightZigzagIso Îµ.symm Î·.symm :=
  Iso.ext (leftZigzagIso_inv Î· Îµ)

@[simp]
theorem rightZigzagIso_symm : (rightZigzagIso Î· Îµ).symm = leftZigzagIso Îµ.symm Î·.symm :=
  Iso.ext (rightZigzagIso_inv Î· Îµ)

instance : IsIso (leftZigzag Î·.hom Îµ.hom) := inferInstanceAs <| IsIso (leftZigzagIso Î· Îµ).hom

instance : IsIso (rightZigzag Î·.hom Îµ.hom) := inferInstanceAs <| IsIso (rightZigzagIso Î· Îµ).hom

theorem right_triangle_of_left_triangle (h : leftZigzag Î·.hom Îµ.hom = (Î»_ f).hom â‰« (Ï_ f).inv) :
    rightZigzag Î·.hom Îµ.hom = (Ï_ g).hom â‰« (Î»_ g).inv := by
  rw [â† cancel_epi (rightZigzag Î·.hom Îµ.hom â‰« (Î»_ g).hom â‰« (Ï_ g).inv)]
  calc
    _ = rightZigzag Î·.hom Îµ.hom âŠ—â‰« rightZigzag Î·.hom Îµ.hom := by coherence
    _ = rightZigzag Î·.hom Îµ.hom := rightZigzag_idempotent_of_left_triangle _ _ h
    _ = _ := by simp

/-- An auxiliary definition for `mkOfAdjointifyCounit`. -/
def adjointifyCounit (Î· : ğŸ™ a â‰… f â‰« g) (Îµ : g â‰« f â‰… ğŸ™ b) : g â‰« f â‰… ğŸ™ b :=
  whiskerLeftIso g ((Ï_ f).symm â‰ªâ‰« rightZigzagIso Îµ.symm Î·.symm â‰ªâ‰« Î»_ f) â‰ªâ‰« Îµ

theorem adjointifyCounit_left_triangle (Î· : ğŸ™ a â‰… f â‰« g) (Îµ : g â‰« f â‰… ğŸ™ b) :
    leftZigzagIso Î· (adjointifyCounit Î· Îµ) = Î»_ f â‰ªâ‰« (Ï_ f).symm := by
  apply Iso.ext
  dsimp [adjointifyCounit, bicategoricalIsoComp]
  calc
    _ = ğŸ™ _ âŠ—â‰« (Î·.hom â–· (f â‰« ğŸ™ b) â‰« (f â‰« g) â— f â— Îµ.inv) âŠ—â‰«
          f â— g â— Î·.inv â–· f âŠ—â‰« f â— Îµ.hom := by
      simp [bicategoricalComp]; coherence
    _ = ğŸ™ _ âŠ—â‰« f â— Îµ.inv âŠ—â‰« (Î·.hom â–· (f â‰« g) â‰« (f â‰« g) â— Î·.inv) â–· f âŠ—â‰« f â— Îµ.hom := by
      rw [â† whisker_exchange Î·.hom (f â— Îµ.inv)]; simp [bicategoricalComp]; coherence
    _ = ğŸ™ _ âŠ—â‰« f â— Îµ.inv âŠ—â‰« (Î·.inv â‰« Î·.hom) â–· f âŠ—â‰« f â— Îµ.hom := by
      rw [â† whisker_exchange Î·.hom Î·.inv]; coherence
    _ = ğŸ™ _ âŠ—â‰« f â— (Îµ.inv â‰« Îµ.hom) := by
      rw [Iso.inv_hom_id]; simp [bicategoricalComp]
    _ = _ := by
      rw [Iso.inv_hom_id]; simp [bicategoricalComp]

/-- Adjoint equivalences between two objects. -/
structure Equivalence (a b : B) where
  /-- A 1-morphism in one direction. -/
  hom : a âŸ¶ b
  /-- A 1-morphism in the other direction. -/
  inv : b âŸ¶ a
  /-- The composition `hom â‰« inv` is isomorphic to the identity. -/
  unit : ğŸ™ a â‰… hom â‰« inv
  /-- The composition `inv â‰« hom` is isomorphic to the identity. -/
  counit : inv â‰« hom â‰… ğŸ™ b
  /-- The composition of the unit and the counit is equal to the identity up to unitors. -/
  left_triangle : leftZigzagIso unit counit = Î»_ hom â‰ªâ‰« (Ï_ hom).symm := by aesop_cat

@[inherit_doc] scoped infixr:10 " â‰Œ " => Bicategory.Equivalence

namespace Equivalence

/-- The identity 1-morphism is an equivalence. -/
def id (a : B) : a â‰Œ a := âŸ¨_, _, (Ï_ _).symm, Ï_ _, by ext; simp [bicategoricalIsoComp]âŸ©

instance : Inhabited (Equivalence a a) := âŸ¨id aâŸ©

theorem left_triangle_hom (e : a â‰Œ b) :
    leftZigzag e.unit.hom e.counit.hom = (Î»_ e.hom).hom â‰« (Ï_ e.hom).inv :=
  congrArg Iso.hom e.left_triangle

theorem right_triangle (e : a â‰Œ b) :
    rightZigzagIso e.unit e.counit = Ï_ e.inv â‰ªâ‰« (Î»_ e.inv).symm :=
  Iso.ext (right_triangle_of_left_triangle e.unit e.counit e.left_triangle_hom)

theorem right_triangle_hom (e : a â‰Œ b) :
    rightZigzag e.unit.hom e.counit.hom = (Ï_ e.inv).hom â‰« (Î»_ e.inv).inv :=
  congrArg Iso.hom e.right_triangle

/-- Construct an adjoint equivalence from 2-isomorphisms by upgrading `Îµ` to a counit. -/
def mkOfAdjointifyCounit (Î· : ğŸ™ a â‰… f â‰« g) (Îµ : g â‰« f â‰… ğŸ™ b) : a â‰Œ b where
  hom := f
  inv := g
  unit := Î·
  counit := adjointifyCounit Î· Îµ
  left_triangle := adjointifyCounit_left_triangle Î· Îµ

end Equivalence

end

end Bicategory

end CategoryTheory
