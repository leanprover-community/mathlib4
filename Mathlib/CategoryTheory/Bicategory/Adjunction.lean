/-
Copyright (c) 2023 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import Mathlib.Tactic.CategoryTheory.Coherence

/-!
# Adjunctions in bicategories

For a 1-morphisms `f : a âŸ¶ b` and `g : b âŸ¶ a` in a bicategory, an adjuntion between `f` and `g`
consists of a pair of 2-morphism `Î· : ğŸ™ a âŸ¶ f â‰« g` and `Îµ : g â‰« f âŸ¶ ğŸ™ b` satisfying the triangle
identities. The 2-morphism `Î·` is called the unit and `Îµ` is called the counit.

## Main definitions

* `Bicategiry.Adjunction`: adjunctions between two 1-morphisms.
* `Bicategory.Equivalence`: adjoint equivalences between two objects.
* `Bicategory.mkOfAdjointifyUnit`: construct an adjoint equivalence from 2-isomorphisms
  `Î· : ğŸ™ a â‰… f â‰« g` and `Îµ : g â‰« f â‰… ğŸ™ b`, by upgrading `Î·` to a unit.
* `Bicategory.mkOfAdjointifyCounit`: construct an adjoint equivalence from 2-isomorphisms
  `Î· : ğŸ™ a â‰… f â‰« g` and `Îµ : g â‰« f â‰… ğŸ™ b`, by upgrading `Îµ` to a counit.

## Implementation notes

The computation of 2-morphisms in the proof is done using `calc` blocks. Typically,
the LHS and the RHS in each step of `calc` are related by simple rewriting up to associators
and unitors. So the proof for each step should be of the form `rw [...]; coherence`. In practice,
our proofs look like `rw [...]; simp [bicategoricalComp]; coherence`. The `simp` is not strictly
necessary, but it speeds up the proof and allow us to avoid increasing the `maxHeartbeats`.
The speedup is probably due to reducing the length of the expression e.g. by absorbing
identity maps or applying the pentagon relation. Such a hack may not be necessary if the
coherence tactic are improved. One possible way would be to perform a such simplification in the
preprocessing of the coherence tactic.
-/

namespace CategoryTheory

namespace Bicategory

open Category

open scoped Bicategory

open Mathlib.Tactic.BicategoryCoherence (bicategoricalComp bicategoricalIsoComp)

universe w v u

variable {B : Type u} [Bicategory.{w, v} B] {a b c : B} {f : a âŸ¶ b} {g : b âŸ¶ a}

/-- The 2-morphism defined by the following pasting diagram:
```
a ï¼ï¼ï¼ï¼ï¼ï¼ â–¸ a
  ï¼¼    Î·      â—¥   ï¼¼
  f ï¼¼   g  ï¼       ï¼¼ f
       â—¢  ï¼     Îµ      â—¢
        b ï¼ï¼ï¼ï¼ï¼ï¼ â–¸ b
```
-/
def leftZigzag (Î· : ğŸ™ a âŸ¶ f â‰« g) (Îµ : g â‰« f âŸ¶ ğŸ™ b) :=
  Î· â–· f âŠ—â‰« f â— Îµ

/-- The 2-morphism defined by the following pasting diagram:
```
        a ï¼ï¼ï¼ï¼ï¼ï¼ â–¸ a
       â—¥  ï¼¼     Î·      â—¥
  g ï¼      ï¼¼ f     ï¼ g
  ï¼    Îµ      â—¢   ï¼
b ï¼ï¼ï¼ï¼ï¼ï¼ â–¸ b
```
-/
def rightZigzag (Î· : ğŸ™ a âŸ¶ f â‰« g) (Îµ : g â‰« f âŸ¶ ğŸ™ b) :=
  g â— Î· âŠ—â‰« Îµ â–· g

/-- Adjunction between two 1-morphisms. -/
structure Adjunction (f : a âŸ¶ b) (g : b âŸ¶ a) where
  unit : ğŸ™ a âŸ¶ f â‰« g
  counit : g â‰« f âŸ¶ ğŸ™ b
  left_triangle : leftZigzag unit counit = (Î»_ _).hom â‰« (Ï_ _).inv := by aesop_cat
  right_triangle : rightZigzag unit counit = (Ï_ _).hom â‰« (Î»_ _).inv := by aesop_cat

scoped infixr:15 " âŠ£ " => Bicategory.Adjunction

namespace Adjunction

attribute [simp] left_triangle right_triangle

attribute [local simp] leftZigzag rightZigzag

/-- Adjunction between identities. -/
def id (a : B) : ğŸ™ a âŠ£ ğŸ™ a where
  unit := (Ï_ _).inv
  counit := (Ï_ _).hom
  left_triangle := by dsimp; coherence
  right_triangle := by dsimp; coherence

instance : Inhabited (Adjunction (ğŸ™ a) (ğŸ™ a)) :=
  âŸ¨id aâŸ©

theorem right_adjoint_uniq_aux {f : a âŸ¶ b} {gâ‚ gâ‚‚ : b âŸ¶ a} (adjâ‚ : f âŠ£ gâ‚) (adjâ‚‚ : f âŠ£ gâ‚‚) :
    (ğŸ™ gâ‚ âŠ—â‰« gâ‚ â— adjâ‚‚.unit âŠ—â‰« adjâ‚.counit â–· gâ‚‚ âŠ—â‰« ğŸ™ gâ‚‚) â‰«
        ğŸ™ gâ‚‚ âŠ—â‰« gâ‚‚ â— adjâ‚.unit âŠ—â‰« adjâ‚‚.counit â–· gâ‚ âŠ—â‰« ğŸ™ gâ‚ =
      ğŸ™ gâ‚ := by
  calc
    _ = ğŸ™ gâ‚ âŠ—â‰«
          gâ‚ â— adjâ‚‚.unit âŠ—â‰«
            (adjâ‚.counit â–· (gâ‚‚ â‰« ğŸ™ a) â‰« ğŸ™ b â— gâ‚‚ â— adjâ‚.unit) âŠ—â‰« adjâ‚‚.counit â–· gâ‚ âŠ—â‰« ğŸ™ gâ‚ := by
      simp [bicategoricalComp]; coherence
    _ =
        ğŸ™ gâ‚ âŠ—â‰«
          gâ‚ â— (adjâ‚‚.unit â–· ğŸ™ a â‰« (f â‰« gâ‚‚) â— adjâ‚.unit) âŠ—â‰«
            (adjâ‚.counit â–· (gâ‚‚ â‰« f) â‰« ğŸ™ b â— adjâ‚‚.counit) â–· gâ‚ âŠ—â‰« ğŸ™ gâ‚ := by
      rw [â† whisker_exchange]; simp [bicategoricalComp]; coherence
    _ =
        ğŸ™ gâ‚ âŠ—â‰«
          gâ‚ â— adjâ‚.unit âŠ—â‰«
            gâ‚ â— (leftZigzag adjâ‚‚.unit adjâ‚‚.counit) â–· gâ‚ âŠ—â‰« adjâ‚.counit â–· gâ‚ âŠ—â‰« ğŸ™ gâ‚ := by
      simp_rw [â† whisker_exchange]; simp [bicategoricalComp]; coherence
    _ = ğŸ™ gâ‚ âŠ—â‰« (rightZigzag adjâ‚.unit adjâ‚.counit) âŠ—â‰« ğŸ™ gâ‚ := by
      rw [left_triangle]; simp [bicategoricalComp]; coherence
    _ = _ := by
      rw [right_triangle]; coherence

theorem left_adjoint_uniq_aux {fâ‚ fâ‚‚ : a âŸ¶ b} {g : b âŸ¶ a} (adjâ‚ : fâ‚ âŠ£ g) (adjâ‚‚ : fâ‚‚ âŠ£ g) :
    (ğŸ™ fâ‚ âŠ—â‰« adjâ‚‚.unit â–· fâ‚ âŠ—â‰« fâ‚‚ â— adjâ‚.counit âŠ—â‰« ğŸ™ fâ‚‚) â‰«
        ğŸ™ fâ‚‚ âŠ—â‰« adjâ‚.unit â–· fâ‚‚ âŠ—â‰« fâ‚ â— adjâ‚‚.counit âŠ—â‰« ğŸ™ fâ‚ =
      ğŸ™ fâ‚ := by
  calc
    _ = ğŸ™ fâ‚ âŠ—â‰«
          adjâ‚‚.unit â–· fâ‚ âŠ—â‰«
            (ğŸ™ a â— fâ‚‚ â— adjâ‚.counit â‰« adjâ‚.unit â–· (fâ‚‚ â‰« ğŸ™ b)) âŠ—â‰« fâ‚ â— adjâ‚‚.counit âŠ—â‰« ğŸ™ fâ‚ := by
      simp [bicategoricalComp]; coherence
    _ = ğŸ™ fâ‚ âŠ—â‰«
          (ğŸ™ a â— adjâ‚‚.unit â‰« adjâ‚.unit â–· (fâ‚‚ â‰« g)) â–· fâ‚ âŠ—â‰«
            fâ‚ â— ((g â‰« fâ‚‚) â— adjâ‚.counit â‰« adjâ‚‚.counit â–· ğŸ™ b) âŠ—â‰« ğŸ™ fâ‚ := by
      rw [whisker_exchange]; simp [bicategoricalComp]; coherence
    _ = ğŸ™ fâ‚ âŠ—â‰«
          adjâ‚.unit â–· fâ‚ âŠ—â‰«
            fâ‚ â— (rightZigzag adjâ‚‚.unit adjâ‚‚.counit) â–· fâ‚ âŠ—â‰« fâ‚ â— adjâ‚.counit âŠ—â‰« ğŸ™ fâ‚ := by
      simp_rw [whisker_exchange]; simp [bicategoricalComp]; coherence
    _ = ğŸ™ fâ‚ âŠ—â‰« (leftZigzag adjâ‚.unit adjâ‚.counit) âŠ—â‰« ğŸ™ fâ‚ := by
      rw [right_triangle]; simp [bicategoricalComp]; coherence
    _ = _ := by
      rw [left_triangle]; simp [bicategoricalComp]

/-- If `gâ‚` and `gâ‚‚` are both right adjoint to `f`, then they are isomorphic. -/
def rightAdjointUniq {f : a âŸ¶ b} {gâ‚ gâ‚‚ : b âŸ¶ a} (adjâ‚ : f âŠ£ gâ‚) (adjâ‚‚ : f âŠ£ gâ‚‚) : gâ‚ â‰… gâ‚‚ where
  hom := ğŸ™ gâ‚ âŠ—â‰« gâ‚ â— adjâ‚‚.unit âŠ—â‰« adjâ‚.counit â–· gâ‚‚ âŠ—â‰« ğŸ™ gâ‚‚
  inv := ğŸ™ gâ‚‚ âŠ—â‰« gâ‚‚ â— adjâ‚.unit âŠ—â‰« adjâ‚‚.counit â–· gâ‚ âŠ—â‰« ğŸ™ gâ‚
  hom_inv_id := right_adjoint_uniq_aux adjâ‚ adjâ‚‚
  inv_hom_id := right_adjoint_uniq_aux adjâ‚‚ adjâ‚

/-- If `fâ‚` and `fâ‚‚` are both left adjoint to `g`, then they are isomorphic. -/
def leftAdjointUniq {fâ‚ fâ‚‚ : a âŸ¶ b} {g : b âŸ¶ a} (adjâ‚ : fâ‚ âŠ£ g) (adjâ‚‚ : fâ‚‚ âŠ£ g) : fâ‚ â‰… fâ‚‚ where
  hom := ğŸ™ fâ‚ âŠ—â‰« adjâ‚‚.unit â–· fâ‚ âŠ—â‰« fâ‚‚ â— adjâ‚.counit âŠ—â‰« ğŸ™ fâ‚‚
  inv := ğŸ™ fâ‚‚ âŠ—â‰« adjâ‚.unit â–· fâ‚‚ âŠ—â‰« fâ‚ â— adjâ‚‚.counit âŠ—â‰« ğŸ™ fâ‚
  hom_inv_id := left_adjoint_uniq_aux adjâ‚ adjâ‚‚
  inv_hom_id := left_adjoint_uniq_aux adjâ‚‚ adjâ‚

section Composition

variable {fâ‚ : a âŸ¶ b} {gâ‚ : b âŸ¶ a} {fâ‚‚ : b âŸ¶ c} {gâ‚‚ : c âŸ¶ b}

/-- Auxiliary definition for `adjunction.comp`. -/
@[simp]
def compUnit (adjâ‚ : fâ‚ âŠ£ gâ‚) (adjâ‚‚ : fâ‚‚ âŠ£ gâ‚‚) : ğŸ™ a âŸ¶ (fâ‚ â‰« fâ‚‚) â‰« gâ‚‚ â‰« gâ‚ :=
  ğŸ™ _ âŠ—â‰« adjâ‚.unit âŠ—â‰« fâ‚ â— adjâ‚‚.unit â–· gâ‚ âŠ—â‰« ğŸ™ _

/-- Auxiliary definition for `adjunction.comp`. -/
@[simp]
def compCounit (adjâ‚ : fâ‚ âŠ£ gâ‚) (adjâ‚‚ : fâ‚‚ âŠ£ gâ‚‚) : (gâ‚‚ â‰« gâ‚) â‰« fâ‚ â‰« fâ‚‚ âŸ¶ ğŸ™ c :=
  ğŸ™ _ âŠ—â‰« gâ‚‚ â— adjâ‚.counit â–· fâ‚‚ âŠ—â‰« adjâ‚‚.counit âŠ—â‰« ğŸ™ _

theorem comp_left_triangle_aux (adjâ‚ : fâ‚ âŠ£ gâ‚) (adjâ‚‚ : fâ‚‚ âŠ£ gâ‚‚) :
    leftZigzag (compUnit adjâ‚ adjâ‚‚) (compCounit adjâ‚ adjâ‚‚) = (Î»_ _).hom â‰« (Ï_ _).inv := by
  calc
    _ = ğŸ™ _ âŠ—â‰«
          adjâ‚.unit â–· (fâ‚ â‰« fâ‚‚) âŠ—â‰«
            fâ‚ â— (adjâ‚‚.unit â–· (gâ‚ â‰« fâ‚) â‰« (fâ‚‚ â‰« gâ‚‚) â— adjâ‚.counit) â–· fâ‚‚ âŠ—â‰«
              (fâ‚ â‰« fâ‚‚) â— adjâ‚‚.counit âŠ—â‰« ğŸ™ _ := by
      simp [bicategoricalComp]; coherence
    _ = ğŸ™ _ âŠ—â‰«
          (leftZigzag adjâ‚.unit adjâ‚.counit) â–· fâ‚‚ âŠ—â‰«
            fâ‚ â— (leftZigzag adjâ‚‚.unit adjâ‚‚.counit) âŠ—â‰« ğŸ™ _ := by
      rw [â† whisker_exchange]; simp [bicategoricalComp]; coherence
    _ = _ := by
      simp_rw [left_triangle]; simp [bicategoricalComp]

theorem comp_right_triangle_aux (adjâ‚ : fâ‚ âŠ£ gâ‚) (adjâ‚‚ : fâ‚‚ âŠ£ gâ‚‚) :
    rightZigzag (compUnit adjâ‚ adjâ‚‚) (compCounit adjâ‚ adjâ‚‚) = (Ï_ _).hom â‰« (Î»_ _).inv := by
  calc
    _ = ğŸ™ _ âŠ—â‰«
          (gâ‚‚ â‰« gâ‚) â— adjâ‚.unit âŠ—â‰«
            gâ‚‚ â— ((gâ‚ â‰« fâ‚) â— adjâ‚‚.unit â‰« adjâ‚.counit â–· (fâ‚‚ â‰« gâ‚‚)) â–· gâ‚ âŠ—â‰«
              adjâ‚‚.counit â–· (gâ‚‚ â‰« gâ‚) âŠ—â‰« ğŸ™ _ := by
      simp [bicategoricalComp]; coherence
    _ = ğŸ™ _ âŠ—â‰«
          gâ‚‚ â— (rightZigzag adjâ‚.unit adjâ‚.counit) âŠ—â‰«
            (rightZigzag adjâ‚‚.unit adjâ‚‚.counit) â–· gâ‚ âŠ—â‰« ğŸ™ _ := by
      rw [whisker_exchange]; simp [bicategoricalComp]; coherence
    _ = _ := by
      simp_rw [right_triangle]; simp [bicategoricalComp]

/-- Composition of adjunctions. -/
def comp (adjâ‚ : fâ‚ âŠ£ gâ‚) (adjâ‚‚ : fâ‚‚ âŠ£ gâ‚‚) : fâ‚ â‰« fâ‚‚ âŠ£ gâ‚‚ â‰« gâ‚ where
  unit := compUnit adjâ‚ adjâ‚‚
  counit := compCounit adjâ‚ adjâ‚‚
  left_triangle := by apply comp_left_triangle_aux
  right_triangle := by apply comp_right_triangle_aux

end Composition

end Adjunction

noncomputable section

variable (Î· : ğŸ™ a â‰… f â‰« g) (Îµ : g â‰« f â‰… ğŸ™ b)

/-- The isomorphism version of `leftZigzag`. -/
def leftZigzagIso (Î· : ğŸ™ a â‰… f â‰« g) (Îµ : g â‰« f â‰… ğŸ™ b) :=
  whiskerRightIso Î· f â‰ªâŠ—â‰« whiskerLeftIso f Îµ

/-- The isomorphism version of `rightZigzag`. -/
def rightZigzagIso (Î· : ğŸ™ a â‰… f â‰« g) (Îµ : g â‰« f â‰… ğŸ™ b) :=
  whiskerLeftIso g Î· â‰ªâŠ—â‰« whiskerRightIso Îµ g

attribute [local simp] leftZigzagIso rightZigzagIso leftZigzag rightZigzag

@[simp]
theorem leftZigzagIso_hom : (leftZigzagIso Î· Îµ).hom = leftZigzag Î·.hom Îµ.hom :=
  rfl

@[simp]
theorem rightZigzagIso_hom : (rightZigzagIso Î· Îµ).hom = rightZigzag Î·.hom Îµ.hom :=
  rfl

@[simp]
theorem leftZigzagIso_inv : (leftZigzagIso Î· Îµ).inv = rightZigzag Îµ.inv Î·.inv := by
  simp [bicategoricalComp, bicategoricalIsoComp]

@[simp]
theorem rightZigzagIso_inv : (rightZigzagIso Î· Îµ).inv = leftZigzag Îµ.inv Î·.inv := by
  simp [bicategoricalComp, bicategoricalIsoComp]

@[simp]
theorem leftZigzagIso_symm : (leftZigzagIso Î· Îµ).symm = rightZigzagIso Îµ.symm Î·.symm :=
  Iso.ext (leftZigzagIso_inv Î· Îµ)

@[simp]
theorem rightZigzagIso_symm : (rightZigzagIso Î· Îµ).symm = leftZigzagIso Îµ.symm Î·.symm :=
  Iso.ext (rightZigzagIso_inv Î· Îµ)

set_option maxHeartbeats 400000 in
theorem right_triangle_of_left_triangle {Î· : ğŸ™ a â‰… f â‰« g} {Îµ : g â‰« f â‰… ğŸ™ b} :
    leftZigzagIso Î· Îµ = Î»_ f â‰ªâ‰« (Ï_ f).symm â†’ rightZigzagIso Î· Îµ = Ï_ g â‰ªâ‰« (Î»_ g).symm := by
  intro H
  replace H : leftZigzag Î·.hom Îµ.hom = (Î»_ f).hom â‰« (Ï_ f).inv := congr_arg Iso.hom H
  apply Iso.ext
  dsimp [bicategoricalIsoComp] at H âŠ¢
  calc
    _ = ğŸ™ _ âŠ—â‰« g â— Î·.hom âŠ—â‰« Îµ.hom â–· g âŠ—â‰« ğŸ™ (g â‰« ğŸ™ a) âŠ—â‰« ğŸ™ _ := by
      rw [â† comp_id (Îµ.hom â–· g)]; coherence
    _ = ğŸ™ _ âŠ—â‰« g â— Î·.hom âŠ—â‰« Îµ.hom â–· g âŠ—â‰« g â— (Î·.hom â‰« Î·.inv) âŠ—â‰« ğŸ™ _ := by
      rw [Iso.hom_inv_id Î·, whiskerLeft_id]
    _ = ğŸ™ _ âŠ—â‰« g â— Î·.hom âŠ—â‰« Îµ.hom â–· g âŠ—â‰« g â— Î·.hom âŠ—â‰«
      (Îµ.hom â‰« Îµ.inv) â–· g âŠ—â‰« g â— Î·.inv âŠ—â‰« ğŸ™ _ := by
      rw [Iso.hom_inv_id Îµ]; simp [bicategoricalComp]
    _ = ğŸ™ _ âŠ—â‰«
          g â— Î·.hom âŠ—â‰«
            (Îµ.hom â–· (g â‰« ğŸ™ a) â‰« ğŸ™ b â— g â— Î·.hom) âŠ—â‰«
              Îµ.hom â–· g âŠ—â‰« Îµ.inv â–· g âŠ—â‰« g â— Î·.inv âŠ—â‰« ğŸ™ _ := by
      simp [bicategoricalComp]; coherence
    _ = ğŸ™ _ âŠ—â‰«
          g â— (Î·.hom â–· ğŸ™ a â‰« (f â‰« g) â— Î·.hom) âŠ—â‰«
            Îµ.hom â–· (g â‰« f â‰« g) âŠ—â‰« Îµ.hom â–· g âŠ—â‰« Îµ.inv â–· g âŠ—â‰« g â— Î·.inv âŠ—â‰« ğŸ™ _ := by
      rw [â† whisker_exchange]; simp [bicategoricalComp]; coherence
    _ = ğŸ™ _ âŠ—â‰«
          g â— Î·.hom âŠ—â‰«
            g â— Î·.hom â–· f â–· g âŠ—â‰«
              (Îµ.hom â–· (g â‰« f) â‰« ğŸ™ b â— Îµ.hom) â–· g âŠ—â‰« Îµ.inv â–· g âŠ—â‰« g â— Î·.inv âŠ—â‰« ğŸ™ _ := by
      rw [â† whisker_exchange]; simp [bicategoricalComp]; coherence
    _ = ğŸ™ _ âŠ—â‰«
          g â— Î·.hom âŠ—â‰«
            g â— (Î·.hom â–· f âŠ—â‰« f â— Îµ.hom) â–· g âŠ—â‰«
              Îµ.hom â–· g âŠ—â‰« Îµ.inv â–· g âŠ—â‰« g â— Î·.inv âŠ—â‰« ğŸ™ _ := by
      rw [â† whisker_exchange]; simp [bicategoricalComp]; coherence
    _ = ğŸ™ _ âŠ—â‰« g â— Î·.hom âŠ—â‰« (Îµ.hom â‰« Îµ.inv) â–· g âŠ—â‰« g â— Î·.inv âŠ—â‰« ğŸ™ _ := by
      rw [H]; coherence
    _ = ğŸ™ _ âŠ—â‰« g â— (Î·.hom â‰« Î·.inv) âŠ—â‰« ğŸ™ _ := by
      rw [Iso.hom_inv_id Îµ]; simp [bicategoricalComp]
    _ = _ := by
      rw [Iso.hom_inv_id Î·]; simp [bicategoricalComp]

theorem left_triangle_iff_right_triangle {Î· : ğŸ™ a â‰… f â‰« g} {Îµ : g â‰« f â‰… ğŸ™ b} :
    leftZigzagIso Î· Îµ = Î»_ f â‰ªâ‰« (Ï_ f).symm â†” rightZigzagIso Î· Îµ = Ï_ g â‰ªâ‰« (Î»_ g).symm :=
  .intro right_triangle_of_left_triangle (by
    intro H
    rw [â† Iso.symm_eq_iff] at H âŠ¢
    rw [leftZigzagIso_symm]
    rw [rightZigzagIso_symm] at H
    exact right_triangle_of_left_triangle H)

/-- An auxiliary definition for `mkOfAdjointifyUnit`. -/
def adjointifyUnit (Î· : ğŸ™ a â‰… f â‰« g) (Îµ : g â‰« f â‰… ğŸ™ b) : ğŸ™ a â‰… f â‰« g :=
  Î· â‰ªâ‰« whiskerRightIso ((Ï_ f).symm â‰ªâ‰« rightZigzagIso Îµ.symm Î·.symm â‰ªâ‰« Î»_ f) g

/-- An auxiliary definition for `mkOfAdjointifyCounit`. -/
def adjointifyCounit (Î· : ğŸ™ a â‰… f â‰« g) (Îµ : g â‰« f â‰… ğŸ™ b) : g â‰« f â‰… ğŸ™ b :=
  whiskerLeftIso g ((Ï_ f).symm â‰ªâ‰« rightZigzagIso Îµ.symm Î·.symm â‰ªâ‰« Î»_ f) â‰ªâ‰« Îµ

set_option maxHeartbeats 400000 in
@[simp]
theorem adjointifyCounit_symm (Î· : ğŸ™ a â‰… f â‰« g) (Îµ : g â‰« f â‰… ğŸ™ b) :
    (adjointifyCounit Î· Îµ).symm = adjointifyUnit Îµ.symm Î·.symm := by
  apply Iso.ext
  rw [â† cancel_mono (adjointifyUnit Îµ.symm Î·.symm).inv, Iso.hom_inv_id]
  dsimp [adjointifyUnit, adjointifyCounit, bicategoricalIsoComp]
  rw [â† cancel_mono Îµ.inv, â† cancel_epi Îµ.hom]
  simp_rw [assoc, Iso.hom_inv_id, Iso.hom_inv_id_assoc]
  simp only [id_whiskerRight, id_comp, IsIso.Iso.inv_inv]
  calc
    _ = ğŸ™ _ âŠ—â‰«
          g â— Î·.hom â–· f âŠ—â‰«
            (ğŸ™ b â— (g â‰« f) â— Îµ.hom â‰« Îµ.inv â–· ((g â‰« f) â‰« ğŸ™ b)) âŠ—â‰«
              (g â— Î·.inv) â–· f âŠ—â‰« ğŸ™ _ := by
      simp [bicategoricalComp]; coherence
    _ = ğŸ™ _ âŠ—â‰«
          (ğŸ™ b â— g â— Î·.hom â‰« Îµ.inv â–· (g â‰« f â‰« g)) â–· f âŠ—â‰«
            g â— ((f â‰« g) â— f â— Îµ.hom â‰« Î·.inv â–· (f â‰« ğŸ™ b)) âŠ—â‰« ğŸ™ _ := by
      rw [whisker_exchange]; simp [bicategoricalComp]; coherence
    _ = ğŸ™ _ âŠ—â‰«
          Îµ.inv â–· g â–· f âŠ—â‰«
            g â— ((f â‰« g) â— Î·.hom â‰« Î·.inv â–· (f â‰« g)) â–· f âŠ—â‰« g â— f â— Îµ.hom âŠ—â‰« ğŸ™ _ := by
      rw [whisker_exchange, whisker_exchange]; simp [bicategoricalComp]; coherence
    _ = ğŸ™ _ âŠ—â‰« Îµ.inv â–· g â–· f âŠ—â‰« g â— (Î·.inv â‰« Î·.hom) â–· f âŠ—â‰« g â— f â— Îµ.hom âŠ—â‰« ğŸ™ _ := by
      rw [whisker_exchange]; simp [bicategoricalComp]; coherence
    _ = ğŸ™ _ âŠ—â‰« (Îµ.inv â–· (g â‰« f) â‰« (g â‰« f) â— Îµ.hom) âŠ—â‰« ğŸ™ _ := by
      rw [Iso.inv_hom_id]; simp [bicategoricalComp]
    _ = ğŸ™ _ âŠ—â‰« (Îµ.hom â‰« Îµ.inv) âŠ—â‰« ğŸ™ _ := by
      rw [â† whisker_exchange]; simp [bicategoricalComp]
    _ = _ := by
      simp [bicategoricalComp]

@[simp]
theorem adjointifyUnit_symm (Î· : ğŸ™ a â‰… f â‰« g) (Îµ : g â‰« f â‰… ğŸ™ b) :
    (adjointifyUnit Î· Îµ).symm = adjointifyCounit Îµ.symm Î·.symm :=
  Iso.symm_eq_iff.mpr (adjointifyCounit_symm Îµ.symm Î·.symm).symm

theorem adjointifyCounit_left_triangle (Î· : ğŸ™ a â‰… f â‰« g) (Îµ : g â‰« f â‰… ğŸ™ b) :
    leftZigzagIso Î· (adjointifyCounit Î· Îµ) = Î»_ f â‰ªâ‰« (Ï_ f).symm := by
  apply Iso.ext
  dsimp [adjointifyCounit, bicategoricalIsoComp]
  calc
    _ = ğŸ™ _ âŠ—â‰« (Î·.hom â–· (f â‰« ğŸ™ b) â‰« (f â‰« g) â— f â— Îµ.inv) âŠ—â‰«
          f â— g â— Î·.inv â–· f âŠ—â‰« f â— Îµ.hom := by
      simp [bicategoricalComp]; coherence
    _ = ğŸ™ _ âŠ—â‰« f â— Îµ.inv âŠ—â‰« (Î·.hom â–· (f â‰« g) â‰« (f â‰« g) â— Î·.inv) â–· f âŠ—â‰« f â— Îµ.hom := by
      rw [â† whisker_exchange Î·.hom (f â— Îµ.inv)]; simp [bicategoricalComp]; coherence
    _ = ğŸ™ _ âŠ—â‰« f â— Îµ.inv âŠ—â‰« (Î·.inv â‰« Î·.hom) â–· f âŠ—â‰« f â— Îµ.hom := by
      rw [â† whisker_exchange Î·.hom Î·.inv]; coherence
    _ = ğŸ™ _ âŠ—â‰« f â— (Îµ.inv â‰« Îµ.hom) := by
      rw [Iso.inv_hom_id]; simp [bicategoricalComp]
    _ = _ := by
      rw [Iso.inv_hom_id]; simp [bicategoricalComp]

theorem adjointifyUnit_right_triangle (Î· : ğŸ™ a â‰… f â‰« g) (Îµ : g â‰« f â‰… ğŸ™ b) :
    rightZigzagIso (adjointifyUnit Î· Îµ) Îµ = Ï_ g â‰ªâ‰« (Î»_ g).symm := by
  rw [â† Iso.symm_eq_iff, rightZigzagIso_symm, adjointifyUnit_symm]
  exact adjointifyCounit_left_triangle Îµ.symm Î·.symm

/-- Adjoint equivalences between two objects. -/
structure Equivalence (a b : B) where
  hom : a âŸ¶ b
  inv : b âŸ¶ a
  unit : ğŸ™ a â‰… hom â‰« inv
  counit : inv â‰« hom â‰… ğŸ™ b
  left_triangle : leftZigzagIso unit counit = Î»_ hom â‰ªâ‰« (Ï_ hom).symm := by aesop_cat

scoped infixr:10 " â‰Œ " => Bicategory.Equivalence

namespace Equivalence

attribute [simp] left_triangle

@[simp]
theorem right_triangle (f : a â‰Œ b) :
    rightZigzagIso f.unit f.counit = Ï_ f.inv â‰ªâ‰« (Î»_ f.inv).symm :=
  right_triangle_of_left_triangle f.left_triangle

def id (a : B) : a â‰Œ a := âŸ¨_, _, (Ï_ _).symm, Ï_ _, by ext; simp [bicategoricalIsoComp]âŸ©

instance : Inhabited (Equivalence a a) := âŸ¨id aâŸ©

/-- Construct an adjoint equivalence from 2-isomorphisms by upgrading `Î·` to a unit. -/
def mkOfAdjointifyUnit (Î· : ğŸ™ a â‰… f â‰« g) (Îµ : g â‰« f â‰… ğŸ™ b) : a â‰Œ b where
  hom := f
  inv := g
  unit := adjointifyUnit Î· Îµ
  counit := Îµ
  left_triangle := left_triangle_iff_right_triangle.mpr (adjointifyUnit_right_triangle Î· Îµ)

/-- Construct an adjoint equivalence from 2-isomorphisms by upgrading `Îµ` to a counit. -/
def mkOfAdjointifyCounit (Î· : ğŸ™ a â‰… f â‰« g) (Îµ : g â‰« f â‰… ğŸ™ b) : a â‰Œ b where
  hom := f
  inv := g
  unit := Î·
  counit := adjointifyCounit Î· Îµ
  left_triangle := adjointifyCounit_left_triangle Î· Îµ

end Equivalence

/-- Construct an adjunction from an equivalence. -/
def Adjunction.ofEquivalence (f : a â‰Œ b) : f.hom âŠ£ f.inv where
  unit := f.unit.hom
  counit := f.counit.hom
  left_triangle := congr_arg Iso.hom f.left_triangle
  right_triangle := congr_arg Iso.hom f.right_triangle

/-- Construct an adjunction from an equivalence. -/
def Adjunction.ofEquivalenceSymm (f : a â‰Œ b) : f.inv âŠ£ f.hom where
  unit := f.counit.inv
  counit := f.unit.inv
  left_triangle := rightZigzagIso_inv f.unit f.counit â–¸ congr_arg Iso.inv f.right_triangle
  right_triangle := leftZigzagIso_inv f.unit f.counit â–¸ congr_arg Iso.inv f.left_triangle

end

end Bicategory

end CategoryTheory
