/-
Copyright (c) 2024 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno, Calle S√∂nne
-/

import Mathlib.CategoryTheory.Bicategory.NaturalTransformation.Oplax

/-!
# Modifications between transformations of oplax functors

In this file we define modifications of oplax and strong transformations of oplax functors.

A modification `Œì` between oplax transformations `Œ∑` and `Œ∏` (of oplax functors) consists of a
family of 2-morphisms `Œì.app a : Œ∑.app a ‚ü∂ Œ∏.app a`, which for all 1-morphisms `f : a ‚ü∂ b`
satisfies the equation `(F.map f ‚óÅ app b) ‚â´ Œ∏.naturality f = Œ∑.naturality f ‚â´ app a ‚ñ∑ G.map f`.

Modifications between strong transformations are defined similarly.

## Main definitions

Given two oplax functors `F` and `G`, we define:

* `OplaxTrans.Modification Œ∑ Œ∏`: modifications between oplax transformations `Œ∑` and `Œ∏` between
  `F` and `G`.
* `OplaxTrans.homCategory F G`: the category structure on the oplax transformations
  between `F` and `G`, where composition is given by vertical composition.

* `StrongTrans.Modification Œ∑ Œ∏`: modifications between oplax transformations `Œ∑` and `Œ∏` between
  `F` and `G`.
* `StrongTrans.homCategory F G`: the category structure on the strong transformations
  between `F` and `G`, where composition is given by vertical composition.

## TODO
* Once lax transformations between oplax functors are defined, we should also define
  modifications between lax transformations.

-/

namespace CategoryTheory.Oplax

open Category Bicategory

universe w‚ÇÅ w‚ÇÇ v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

variable {B : Type u‚ÇÅ} [Bicategory.{w‚ÇÅ, v‚ÇÅ} B] {C : Type u‚ÇÇ} [Bicategory.{w‚ÇÇ, v‚ÇÇ} C]
  {F G : OplaxFunctor B C}

namespace OplaxTrans

variable (Œ∑ Œ∏ : F ‚ü∂ G)

/-- A modification `Œì` between oplax natural transformations `Œ∑` and `Œ∏` consists of a family of
2-morphisms `Œì.app a : Œ∑.app a ‚ü∂ Œ∏.app a`, which satisfies the equation
`(F.map f ‚óÅ app b) ‚â´ Œ∏.naturality f = Œ∑.naturality f ‚â´ (app a ‚ñ∑ G.map f)`
for each 1-morphism `f : a ‚ü∂ b`.
-/
@[ext]
structure Modification where
  /-- The underlying family of 2-morphisms. -/
  app (a : B) : Œ∑.app a ‚ü∂ Œ∏.app a
  /-- The naturality condition. -/
  naturality {a b : B} (f : a ‚ü∂ b) :
    F.map f ‚óÅ app b ‚â´ Œ∏.naturality f = Œ∑.naturality f ‚â´ app a ‚ñ∑ G.map f := by aesop_cat

attribute [reassoc (attr := simp)] Modification.naturality

variable {Œ∑ Œ∏}

namespace Modification

section

variable (Œì : Modification Œ∑ Œ∏) {a b c : B} {a' : C}

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality (f : a' ‚ü∂ F.obj b) (g : b ‚ü∂ c) :
    f ‚óÅ F.map g ‚óÅ Œì.app c ‚â´ f ‚óÅ Œ∏.naturality g =
      f ‚óÅ Œ∑.naturality g ‚â´ f ‚óÅ Œì.app b ‚ñ∑ G.map g := by
  simp_rw [‚Üê Bicategory.whiskerLeft_comp, naturality]

@[reassoc (attr := simp)]
theorem whiskerRight_naturality (f : a ‚ü∂ b) (g : G.obj b ‚ü∂ a') :
    F.map f ‚óÅ Œì.app b ‚ñ∑ g ‚â´ (Œ±_ _ _ _).inv ‚â´ Œ∏.naturality f ‚ñ∑ g =
      (Œ±_ _ _ _).inv ‚â´ Œ∑.naturality f ‚ñ∑ g ‚â´ Œì.app a ‚ñ∑ G.map f ‚ñ∑ g := by
  simp_rw [associator_inv_naturality_middle_assoc, ‚Üê comp_whiskerRight, naturality]

end

end Modification

/-- Category structure on the oplax natural transformations between OplaxFunctors. -/
@[simps]
instance homCategory (F G : OplaxFunctor B C) : Category (OplaxTrans F G) where
  Hom := Modification
  id Œ∑ := { app := fun a ‚Ü¶ ùüô (Œ∑.app a) }
  comp Œì Œî := { app := fun a => Œì.app a ‚â´ Œî.app a }

-- TODO: FIX
variable {Œ∑ Œ∏ : OplaxTrans F G}

instance : Inhabited (Modification Œ∑ Œ∑) :=
  ‚ü®ùüô Œ∑‚ü©

@[ext]
lemma homCategory.ext {Œ± Œ≤ : OplaxTrans F G} {m n : Œ± ‚ü∂ Œ≤} (w : ‚àÄ b, m.app b = n.app b) : m = n :=
  Modification.ext (funext w)

/-- Construct a modification isomorphism between oplax natural transformations
by giving object level isomorphisms, and checking naturality only in the forward direction.
-/
@[simps]
def ModificationIso.ofComponents (app : ‚àÄ a, Œ∑.app a ‚âÖ Œ∏.app a)
    (naturality : ‚àÄ {a b} (f : a ‚ü∂ b),
      F.map f ‚óÅ (app b).hom ‚â´ Œ∏.naturality f =
        Œ∑.naturality f ‚â´ (app a).hom ‚ñ∑ G.map f := by aesop_cat) : Œ∑ ‚âÖ Œ∏ where
  hom := { app := fun a => (app a).hom }
  inv :=
    { app := fun a => (app a).inv
      naturality := fun {a b} f => by
        simpa using congr_arg (fun f => _ ‚óÅ (app b).inv ‚â´ f ‚â´ (app a).inv ‚ñ∑ _)
          (naturality f).symm }

end OplaxTrans

namespace StrongTrans

variable (Œ∑ Œ∏ : F ‚ü∂ G)

/-- A modification `Œì` between strong natural transformations `Œ∑` and `Œ∏` (between oplax functors)
consists of a family of 2-morphisms `Œì.app a : Œ∑.app a ‚ü∂ Œ∏.app a`, which satisfies the equation
`(F.map f ‚óÅ app b) ‚â´ (Œ∏.naturality f).hom = (Œ∑.naturality f).hom ‚â´ (app a ‚ñ∑ G.map f)`
for each 1-morphism `f : a ‚ü∂ b`.
-/
@[ext]
structure Modification where
  /-- The underlying family of 2-morphisms. -/
  app (a : B) : Œ∑.app a ‚ü∂ Œ∏.app a
  /-- The naturality condition. -/
  naturality {a b : B} (f : a ‚ü∂ b) :
    F.map f ‚óÅ app b ‚â´ (Œ∏.naturality f).hom =
      (Œ∑.naturality f).hom ‚â´ app a ‚ñ∑ G.map f := by aesop_cat

attribute [reassoc (attr := simp)] Modification.naturality

variable {Œ∑ Œ∏}

namespace Modification

variable (Œì : Modification Œ∑ Œ∏)

/-- The modification between the underlying strong transformations of oplax functors -/
@[simps]
def toOplax : OplaxTrans.Modification Œ∑.toOplax Œ∏.toOplax where
  app a := Œì.app a

instance hasCoeToOplax :
    Coe (Modification Œ∑ Œ∏) (OplaxTrans.Modification Œ∑.toOplax Œ∏.toOplax) :=
  ‚ü®toOplax‚ü©

/-- The modification between strong transformations of oplax functors associated to a modification
between the underlying oplax transformations. -/
@[simps]
def mkOfOplax (Œì : OplaxTrans.Modification Œ∑.toOplax Œ∏.toOplax) : Modification Œ∑ Œ∏ where
  app a := Œì.app a
  naturality f := by simpa using Œì.naturality f

section

variable (Œì : Modification Œ∑ Œ∏) {a b c : B} {a' : C}

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality (f : a' ‚ü∂ F.obj b) (g : b ‚ü∂ c) :
    f ‚óÅ F.map g ‚óÅ Œì.app c ‚â´ f ‚óÅ (Œ∏.naturality g).hom =
      f ‚óÅ (Œ∑.naturality g).hom ‚â´ f ‚óÅ Œì.app b ‚ñ∑ G.map g :=
  OplaxTrans.Modification.whiskerLeft_naturality Œì.toOplax _ _

@[reassoc (attr := simp)]
theorem whiskerRight_naturality (f : a ‚ü∂ b) (g : G.obj b ‚ü∂ a') :
    F.map f ‚óÅ Œì.app b ‚ñ∑ g ‚â´ (Œ±_ _ _ _).inv ‚â´ (Œ∏.naturality f).hom ‚ñ∑ g =
      (Œ±_ _ _ _).inv ‚â´ (Œ∑.naturality f).hom ‚ñ∑ g ‚â´ Œì.app a ‚ñ∑ G.map f ‚ñ∑ g :=
  OplaxTrans.Modification.whiskerRight_naturality Œì.toOplax _ _

end

end Modification

/-- Category structure on the oplax natural transformations between oplax functors. -/
@[simps]
instance homCategory : Category (F ‚ü∂ G) where
  Hom := Modification
  id Œ∑ := { app := fun a ‚Ü¶ ùüô (Œ∑.app a) }
  comp Œì Œî := { app := fun a => Œì.app a ‚â´ Œî.app a }

instance : Inhabited (Modification Œ∑ Œ∑) :=
  ‚ü®ùüô Œ∑‚ü©

@[ext]
lemma homCategory.ext {m n : Œ∑ ‚ü∂ Œ∏} (w : ‚àÄ b, m.app b = n.app b) : m = n :=
  Modification.ext (funext w)

/-- Construct a modification isomorphism between strong natural transformations (of oplax functors)
by giving object level isomorphisms, and checking naturality only in the forward direction.
-/
@[simps]
def ModificationIso.ofComponents (app : ‚àÄ a, Œ∑.app a ‚âÖ Œ∏.app a)
    (naturality :
      ‚àÄ {a b} (f : a ‚ü∂ b),
        F.map f ‚óÅ (app b).hom ‚â´ (Œ∏.naturality f).hom =
          (Œ∑.naturality f).hom ‚â´ (app a).hom ‚ñ∑ G.map f) : Œ∑ ‚âÖ Œ∏ where
  hom := { app := fun a => (app a).hom }
  inv :=
    { app := fun a => (app a).inv
      naturality := fun {a b} f => by
        simpa using congr_arg (fun f => _ ‚óÅ (app b).inv ‚â´ f ‚â´ (app a).inv ‚ñ∑ _)
          (naturality f).symm }

def ModificationIso.mkOfOplax (Œ¶ : Œ∑.toOplax ‚âÖ Œ∏.toOplax) : Œ∑ ‚âÖ Œ∏ where
  hom := Modification.mkOfOplax Œ¶.hom
  inv := Modification.mkOfOplax Œ¶.inv
  hom_inv_id := by simp [Modificatin.mkOfOplax, Œ¶.hom_inv_id]

end StrongTrans

end CategoryTheory.Oplax
