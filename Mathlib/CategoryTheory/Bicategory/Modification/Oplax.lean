/-
Copyright (c) 2024 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno, Calle S√∂nne
-/
module

public import Mathlib.CategoryTheory.Bicategory.NaturalTransformation.Oplax

/-!
# Modifications between transformations of oplax functors

In this file we define modifications of oplax and strong transformations of oplax functors.

A modification `Œì` between oplax transformations `Œ∑` and `Œ∏` (of oplax functors) consists of a
family of 2-morphisms `Œì.app a : Œ∑.app a ‚ü∂ Œ∏.app a`, which for all 1-morphisms `f : a ‚ü∂ b`
satisfies the equation `(F.map f ‚óÅ app b) ‚â´ Œ∏.naturality f = Œ∑.naturality f ‚â´ (app a ‚ñ∑ G.map f)`.

Modifications between strong transformations are defined similarly.

## Main definitions

Given two oplax functors `F` and `G`, we define:

* `OplaxTrans.Modification Œ∑ Œ∏`: modifications between oplax transformations `Œ∑` and `Œ∏` between
  `F` and `G`.
* `OplaxTrans.homCategory F G`: the category structure on the oplax transformations
  between `F` and `G`, where composition is given by vertical composition. Note that this a scoped
  instance in the `Oplax.OplaxTrans` namespace, so you need to run `open scoped Oplax.OplaxTrans`
  to access it.

* `StrongTrans.Modification Œ∑ Œ∏`: modifications between strong transformations `Œ∑` and `Œ∏` between
  `F` and `G`.
* `StrongTrans.homCategory F G`: the category structure on the strong transformations
  between `F` and `G`, where composition is given by vertical composition. Note that this a scoped
  instance in the `Oplax.StrongTrans` namespace, so you need to run `open scoped Oplax.StrongTrans`
  to access it.

-/

@[expose] public section

namespace CategoryTheory.Oplax

open Category Bicategory

universe w‚ÇÅ w‚ÇÇ v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

variable {B : Type u‚ÇÅ} [Bicategory.{w‚ÇÅ, v‚ÇÅ} B] {C : Type u‚ÇÇ} [Bicategory.{w‚ÇÇ, v‚ÇÇ} C]
  {F G : B ‚•§·µí·µñ·¥∏ C}

namespace OplaxTrans

variable (Œ∑ Œ∏ : F ‚ü∂ G)

/-- A modification `Œì` between oplax natural transformations `Œ∑` and `Œ∏` consists of a family of
2-morphisms `Œì.app a : Œ∑.app a ‚ü∂ Œ∏.app a`, which satisfies the equation
`(F.map f ‚óÅ app b) ‚â´ Œ∏.naturality f = Œ∑.naturality f ‚â´ (app a ‚ñ∑ G.map f)`
for each 1-morphism `f : a ‚ü∂ b`.
-/
@[ext]
structure Modification where
  /-- The underlying family of 2-morphisms. -/
  app (a : B) : Œ∑.app a ‚ü∂ Œ∏.app a
  /-- The naturality condition. -/
  naturality :
    ‚àÄ {a b : B} (f : a ‚ü∂ b),
      F.map f ‚óÅ app b ‚â´ Œ∏.naturality f = Œ∑.naturality f ‚â´ app a ‚ñ∑ G.map f := by
    cat_disch

attribute [reassoc (attr := simp)] Modification.naturality

variable {Œ∑ Œ∏}

namespace Modification

section

variable (Œì : Modification Œ∑ Œ∏) {a b c : B} {a' : C}

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality (f : a' ‚ü∂ F.obj b) (g : b ‚ü∂ c) :
    f ‚óÅ F.map g ‚óÅ Œì.app c ‚â´ f ‚óÅ Œ∏.naturality g =
      f ‚óÅ Œ∑.naturality g ‚â´ f ‚óÅ Œì.app b ‚ñ∑ G.map g := by
  simp_rw [‚Üê Bicategory.whiskerLeft_comp, naturality]

@[reassoc (attr := simp)]
theorem whiskerRight_naturality (f : a ‚ü∂ b) (g : G.obj b ‚ü∂ a') :
    F.map f ‚óÅ Œì.app b ‚ñ∑ g ‚â´ (Œ±_ _ _ _).inv ‚â´ Œ∏.naturality f ‚ñ∑ g =
      (Œ±_ _ _ _).inv ‚â´ Œ∑.naturality f ‚ñ∑ g ‚â´ Œì.app a ‚ñ∑ G.map f ‚ñ∑ g := by
  simp_rw [associator_inv_naturality_middle_assoc, ‚Üê comp_whiskerRight, naturality]

end

variable (Œ∑) in
/-- The identity modification. -/
@[simps]
def id : Modification Œ∑ Œ∑ where app a := ùüô (Œ∑.app a)

instance : Inhabited (Modification Œ∑ Œ∑) :=
  ‚ü®Modification.id Œ∑‚ü©

/-- Vertical composition of modifications. -/
@[simps]
def vcomp {Œπ : F ‚ü∂ G} (Œì : Modification Œ∑ Œ∏) (Œî : Modification Œ∏ Œπ) : Modification Œ∑ Œπ where
  app a := Œì.app a ‚â´ Œî.app a

end Modification

variable (Œ∑ Œ∏) in
/-- Type-alias for modifications between oplax transformations of oplax functors. This is the type
used for the 2-homomorphisms in the bicategory of oplax functors equipped with oplax
transformations. -/
@[ext]
structure Hom where
  of ::
  /-- The underlying modification of oplax transformations. -/
  as : Modification Œ∑ Œ∏

/-- Category structure on the oplax natural transformations between OplaxFunctors.

Note that this a scoped instance in the `Oplax.OplaxTrans` namespace. -/
@[simps!]
scoped instance homCategory : Category (F ‚ü∂ G) where
  Hom := Hom
  id Œì := ‚ü®Modification.id Œì‚ü©
  comp Œì Œî := ‚ü®Modification.vcomp Œì.as Œî.as‚ü©

instance : Inhabited (Œ∑ ‚ü∂ Œ∑) :=
  ‚ü®ùüô Œ∑‚ü©

@[ext]
lemma homCategory.ext {m n : Œ∑ ‚ü∂ Œ∏} (w : ‚àÄ b, m.as.app b = n.as.app b) : m = n :=
  Hom.ext <| Modification.ext <| funext w

/-- Construct a modification isomorphism between oplax natural transformations
by giving object level isomorphisms, and checking naturality only in the forward direction.
-/
@[simps]
def isoMk (app : ‚àÄ a, Œ∑.app a ‚âÖ Œ∏.app a)
    (naturality :
      ‚àÄ {a b} (f : a ‚ü∂ b),
        F.map f ‚óÅ (app b).hom ‚â´ Œ∏.naturality f =
          Œ∑.naturality f ‚â´ (app a).hom ‚ñ∑ G.map f := by cat_disch) :
    Œ∑ ‚âÖ Œ∏ where
  hom := ‚ü®{ app a := (app a).hom }‚ü©
  inv := ‚ü®{
      app a := (app a).inv
      naturality {a b} f := by
        simpa using _ ‚óÅ (app b).inv ‚â´= (naturality f).symm =‚â´ (app a).inv ‚ñ∑ _ }‚ü©

@[deprecated (since := "2025-11-11")] alias ModificationIso.ofComponents := isoMk

end OplaxTrans

namespace StrongTrans

variable (Œ∑ Œ∏ : F ‚ü∂ G)

/-- A modification `Œì` between strong natural transformations `Œ∑` and `Œ∏` (between oplax functors)
consists of a family of 2-morphisms `Œì.app a : Œ∑.app a ‚ü∂ Œ∏.app a`, which satisfies the equation
`(F.map f ‚óÅ app b) ‚â´ (Œ∏.naturality f).hom = (Œ∑.naturality f).hom ‚â´ (app a ‚ñ∑ G.map f)`
for each 1-morphism `f : a ‚ü∂ b`.
-/
@[ext]
structure Modification where
  /-- The underlying family of 2-morphisms. -/
  app (a : B) : Œ∑.app a ‚ü∂ Œ∏.app a
  /-- The naturality condition. -/
  naturality {a b : B} (f : a ‚ü∂ b) :
    F.map f ‚óÅ app b ‚â´ (Œ∏.naturality f).hom =
      (Œ∑.naturality f).hom ‚â´ app a ‚ñ∑ G.map f := by cat_disch

attribute [reassoc (attr := simp)] Modification.naturality

variable {Œ∑ Œ∏}

namespace Modification

variable (Œì : Modification Œ∑ Œ∏)

/-- The modification between the underlying strong transformations of oplax functors -/
@[simps]
def toOplax : OplaxTrans.Modification Œ∑.toOplax Œ∏.toOplax where
  app a := Œì.app a

instance hasCoeToOplax :
    Coe (Modification Œ∑ Œ∏) (OplaxTrans.Modification Œ∑.toOplax Œ∏.toOplax) :=
  ‚ü®toOplax‚ü©

/-- The modification between strong transformations of oplax functors associated to a modification
between the underlying oplax transformations. -/
@[simps]
def mkOfOplax (Œì : OplaxTrans.Modification Œ∑.toOplax Œ∏.toOplax) : Modification Œ∑ Œ∏ where
  app a := Œì.app a
  naturality f := by simpa using Œì.naturality f

/-- Modifications between strong transformations of oplax functors are equivalent to modifications
between the underlying oplax transformations. -/
@[simps]
def equivOplax : (OplaxTrans.Modification Œ∑.toOplax Œ∏.toOplax) ‚âÉ Modification Œ∑ Œ∏ where
  toFun := mkOfOplax
  invFun := toOplax
  left_inv _ := rfl
  right_inv _ := rfl

section

variable (Œì : Modification Œ∑ Œ∏) {a b c : B} {a' : C}

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality (f : a' ‚ü∂ F.obj b) (g : b ‚ü∂ c) :
    f ‚óÅ F.map g ‚óÅ Œì.app c ‚â´ f ‚óÅ (Œ∏.naturality g).hom =
      f ‚óÅ (Œ∑.naturality g).hom ‚â´ f ‚óÅ Œì.app b ‚ñ∑ G.map g :=
  OplaxTrans.Modification.whiskerLeft_naturality Œì.toOplax _ _

@[reassoc (attr := simp)]
theorem whiskerRight_naturality (f : a ‚ü∂ b) (g : G.obj b ‚ü∂ a') :
    F.map f ‚óÅ Œì.app b ‚ñ∑ g ‚â´ (Œ±_ _ _ _).inv ‚â´ (Œ∏.naturality f).hom ‚ñ∑ g =
      (Œ±_ _ _ _).inv ‚â´ (Œ∑.naturality f).hom ‚ñ∑ g ‚â´ Œì.app a ‚ñ∑ G.map f ‚ñ∑ g :=
  OplaxTrans.Modification.whiskerRight_naturality Œì.toOplax _ _

end

variable (Œ∑) in
/-- The identity modification. -/
@[simps]
def id : Modification Œ∑ Œ∑ where app a := ùüô (Œ∑.app a)

instance : Inhabited (Modification Œ∑ Œ∑) :=
  ‚ü®Modification.id Œ∑‚ü©

/-- Vertical composition of modifications. -/
@[simps]
def vcomp {Œπ : F ‚ü∂ G} (Œì : Modification Œ∑ Œ∏) (Œî : Modification Œ∏ Œπ) : Modification Œ∑ Œπ where
  app a := Œì.app a ‚â´ Œî.app a

end Modification

variable (Œ∑ Œ∏) in
/-- Type-alias for modifications between strong transformations of oplax functors. This is the type
used for the 2-homomorphisms in the bicategory of oplax functors equipped with strong
transformations. -/
@[ext]
structure Hom where
  of ::
  /-- The underlying modification of strong transformations. -/
  as : Modification Œ∑ Œ∏

/-- Category structure on the strong natural transformations between oplax functors.

Note that this a scoped instance in the `Oplax.StrongTrans` namespace. -/
@[simps!]
scoped instance homCategory : Category (F ‚ü∂ G) where
  Hom := Hom
  id Œì := ‚ü®Modification.id Œì‚ü©
  comp Œì Œî := ‚ü®Modification.vcomp Œì.as Œî.as‚ü©

instance : Inhabited (Œ∑ ‚ü∂ Œ∑) :=
  ‚ü®ùüô Œ∑‚ü©

@[ext]
lemma homCategory.ext {m n : Œ∑ ‚ü∂ Œ∏} (w : ‚àÄ b, m.as.app b = n.as.app b) : m = n :=
  Hom.ext <| Modification.ext <| funext w

/-- Construct a modification isomorphism between strong natural transformations (of oplax functors)
by giving object level isomorphisms, and checking naturality only in the forward direction.
-/
@[simps]
def isoMk (app : ‚àÄ a, Œ∑.app a ‚âÖ Œ∏.app a)
    (naturality : ‚àÄ {a b} (f : a ‚ü∂ b),
      F.map f ‚óÅ (app b).hom ‚â´ (Œ∏.naturality f).hom =
        (Œ∑.naturality f).hom ‚â´ (app a).hom ‚ñ∑ G.map f := by cat_disch) :
    Œ∑ ‚âÖ Œ∏ where
  hom := ‚ü®{ app a := (app a).hom }‚ü©
  inv := ‚ü®{
      app a := (app a).inv
      naturality {a b} f := by
        simpa using _ ‚óÅ (app b).inv ‚â´= (naturality f).symm =‚â´ (app a).inv ‚ñ∑ _ }‚ü©

end StrongTrans

end CategoryTheory.Oplax
