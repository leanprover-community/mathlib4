/-
Copyright (c) 2024 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno, Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.Bicategory.NaturalTransformation.Oplax

/-!
# Modifications between transformations of oplax functors

In this file we define modifications of oplax and strong transformations of oplax functors.

A modification `Î“` between oplax transformations `Î·` and `Î¸` (of oplax functors) consists of a
family of 2-morphisms `Î“.app a : Î·.app a âŸ¶ Î¸.app a`, which for all 1-morphisms `f : a âŸ¶ b`
satisfies the equation `(F.map f â— app b) â‰« Î¸.naturality f = Î·.naturality f â‰« (app a â–· G.map f)`.

Modifications between strong transformations are defined similarly.

## Main definitions

Given two oplax functors `F` and `G`, we define:

* `OplaxTrans.Modification Î· Î¸`: modifications between oplax transformations `Î·` and `Î¸` between
  `F` and `G`.
* `OplaxTrans.homCategory F G`: the category structure on the oplax transformations
  between `F` and `G`, where composition is given by vertical composition.

* `StrongTrans.Modification Î· Î¸`: modifications between strong transformations `Î·` and `Î¸` between
  `F` and `G`.
* `StrongTrans.homCategory F G`: the category structure on the strong transformations
  between `F` and `G`, where composition is given by vertical composition. Note that this a scoped
  instance in the `Oplax.OplaxTrans` namespace, so you need to run `open scoped Oplax.OplaxTrans`
  to access it.

## Note

The category structure in


## TODO
* Once lax transformations between oplax functors are defined, we should also define
  modifications between lax transformations.

-/

namespace CategoryTheory.Oplax

open Category Bicategory

universe wâ‚ wâ‚‚ vâ‚ vâ‚‚ uâ‚ uâ‚‚

variable {B : Type uâ‚} [Bicategory.{wâ‚, vâ‚} B] {C : Type uâ‚‚} [Bicategory.{wâ‚‚, vâ‚‚} C]
  {F G : OplaxFunctor B C}

namespace OplaxTrans

variable (Î· Î¸ : F âŸ¶ G)

/-- A modification `Î“` between oplax natural transformations `Î·` and `Î¸` consists of a family of
2-morphisms `Î“.app a : Î·.app a âŸ¶ Î¸.app a`, which satisfies the equation
`(F.map f â— app b) â‰« Î¸.naturality f = Î·.naturality f â‰« (app a â–· G.map f)`
for each 1-morphism `f : a âŸ¶ b`.
-/
@[ext]
structure Modification where
  /-- The underlying family of 2-morphisms. -/
  app (a : B) : Î·.app a âŸ¶ Î¸.app a
  /-- The naturality condition. -/
  naturality :
    âˆ€ {a b : B} (f : a âŸ¶ b),
      F.map f â— app b â‰« Î¸.naturality f = Î·.naturality f â‰« app a â–· G.map f := by
    cat_disch

attribute [reassoc (attr := simp)] Modification.naturality

variable {Î· Î¸}

namespace Modification

section

variable (Î“ : Modification Î· Î¸) {a b c : B} {a' : C}

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality (f : a' âŸ¶ F.obj b) (g : b âŸ¶ c) :
    f â— F.map g â— Î“.app c â‰« f â— Î¸.naturality g =
      f â— Î·.naturality g â‰« f â— Î“.app b â–· G.map g := by
  simp_rw [â† Bicategory.whiskerLeft_comp, naturality]

@[reassoc (attr := simp)]
theorem whiskerRight_naturality (f : a âŸ¶ b) (g : G.obj b âŸ¶ a') :
    F.map f â— Î“.app b â–· g â‰« (Î±_ _ _ _).inv â‰« Î¸.naturality f â–· g =
      (Î±_ _ _ _).inv â‰« Î·.naturality f â–· g â‰« Î“.app a â–· G.map f â–· g := by
  simp_rw [associator_inv_naturality_middle_assoc, â† comp_whiskerRight, naturality]

end

variable (Î·) in
/-- The identity modification. -/
@[simps]
def id : Modification Î· Î· where app a := ğŸ™ (Î·.app a)

instance : Inhabited (Modification Î· Î·) :=
  âŸ¨Modification.id Î·âŸ©

/-- Vertical composition of modifications. -/
@[simps]
def vcomp {Î¹ : F âŸ¶ G} (Î“ : Modification Î· Î¸) (Î” : Modification Î¸ Î¹) : Modification Î· Î¹ where
  app a := Î“.app a â‰« Î”.app a

end Modification

/-- Category structure on the oplax natural transformations between OplaxFunctors. -/
@[simps!]
scoped instance homCategory (F G : OplaxFunctor B C) : Category (F âŸ¶ G) where
  Hom := Modification
  id := Modification.id
  comp := Modification.vcomp

@[ext]
lemma homCategory.ext {F G : OplaxFunctor B C} {Î± Î² : F âŸ¶ G} {m n : Î± âŸ¶ Î²}
    (w : âˆ€ b, m.app b = n.app b) : m = n :=
  Modification.ext (funext w)

/-- Construct a modification isomorphism between oplax natural transformations
by giving object level isomorphisms, and checking naturality only in the forward direction.
-/
@[simps]
def isoMk (app : âˆ€ a, Î·.app a â‰… Î¸.app a)
    (naturality :
      âˆ€ {a b} (f : a âŸ¶ b),
        F.map f â— (app b).hom â‰« Î¸.naturality f =
          Î·.naturality f â‰« (app a).hom â–· G.map f := by aesop_cat) :
    Î· â‰… Î¸ where
  hom := { app := fun a => (app a).hom }
  inv :=
    { app := fun a => (app a).inv
      naturality := fun {a b} f => by
        simpa using _ â— (app b).inv â‰«= (naturality f).symm =â‰« (app a).inv â–· _ }

end OplaxTrans

namespace StrongTrans

variable (Î· Î¸ : F âŸ¶ G)

/-- A modification `Î“` between strong natural transformations `Î·` and `Î¸` (between oplax functors)
consists of a family of 2-morphisms `Î“.app a : Î·.app a âŸ¶ Î¸.app a`, which satisfies the equation
`(F.map f â— app b) â‰« (Î¸.naturality f).hom = (Î·.naturality f).hom â‰« (app a â–· G.map f)`
for each 1-morphism `f : a âŸ¶ b`.
-/
@[ext]
structure Modification where
  /-- The underlying family of 2-morphisms. -/
  app (a : B) : Î·.app a âŸ¶ Î¸.app a
  /-- The naturality condition. -/
  naturality {a b : B} (f : a âŸ¶ b) :
    F.map f â— app b â‰« (Î¸.naturality f).hom =
      (Î·.naturality f).hom â‰« app a â–· G.map f := by aesop_cat

attribute [reassoc (attr := simp)] Modification.naturality

variable {Î· Î¸}

namespace Modification

variable (Î“ : Modification Î· Î¸)

/-- The modification between the underlying strong transformations of oplax functors -/
@[simps]
def toOplax : OplaxTrans.Modification Î·.toOplax Î¸.toOplax where
  app a := Î“.app a

instance hasCoeToOplax :
    Coe (Modification Î· Î¸) (OplaxTrans.Modification Î·.toOplax Î¸.toOplax) :=
  âŸ¨toOplaxâŸ©

/-- The modification between strong transformations of oplax functors associated to a modification
between the underlying oplax transformations. -/
@[simps]
def mkOfOplax (Î“ : OplaxTrans.Modification Î·.toOplax Î¸.toOplax) : Modification Î· Î¸ where
  app a := Î“.app a
  naturality f := by simpa using Î“.naturality f

/-- Modifications between strong transformations of oplax functors are equivalent to modifications
between the underlying oplax transformations. -/
@[simps]
def equivOplax : (OplaxTrans.Modification Î·.toOplax Î¸.toOplax) â‰ƒ Modification Î· Î¸ where
  toFun := mkOfOplax
  invFun := toOplax
  left_inv _ := rfl
  right_inv _ := rfl

section

variable (Î“ : Modification Î· Î¸) {a b c : B} {a' : C}

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality (f : a' âŸ¶ F.obj b) (g : b âŸ¶ c) :
    f â— F.map g â— Î“.app c â‰« f â— (Î¸.naturality g).hom =
      f â— (Î·.naturality g).hom â‰« f â— Î“.app b â–· G.map g :=
  OplaxTrans.Modification.whiskerLeft_naturality Î“.toOplax _ _

@[reassoc (attr := simp)]
theorem whiskerRight_naturality (f : a âŸ¶ b) (g : G.obj b âŸ¶ a') :
    F.map f â— Î“.app b â–· g â‰« (Î±_ _ _ _).inv â‰« (Î¸.naturality f).hom â–· g =
      (Î±_ _ _ _).inv â‰« (Î·.naturality f).hom â–· g â‰« Î“.app a â–· G.map f â–· g :=
  OplaxTrans.Modification.whiskerRight_naturality Î“.toOplax _ _

end

variable (Î·) in
/-- The identity modification. -/
@[simps]
def id : Modification Î· Î· where app a := ğŸ™ (Î·.app a)

instance : Inhabited (Modification Î· Î·) :=
  âŸ¨Modification.id Î·âŸ©

/-- Vertical composition of modifications. -/
@[simps]
def vcomp {Î¹ : F âŸ¶ G} (Î“ : Modification Î· Î¸) (Î” : Modification Î¸ Î¹) : Modification Î· Î¹ where
  app a := Î“.app a â‰« Î”.app a

end Modification

/-- Category structure on the strong natural transformations between oplax functors. -/
@[simps!]
scoped instance homCategory : Category (F âŸ¶ G) where
  Hom := Modification
  id := Modification.id
  comp := Modification.vcomp

instance : Inhabited (Modification Î· Î·) :=
  âŸ¨ğŸ™ Î·âŸ©

@[ext]
lemma homCategory.ext {m n : Î· âŸ¶ Î¸} (w : âˆ€ b, m.app b = n.app b) : m = n :=
  Modification.ext (funext w)

/-- Construct a modification isomorphism between strong natural transformations (of oplax functors)
by giving object level isomorphisms, and checking naturality only in the forward direction.
-/
@[simps]
def isoMk (app : âˆ€ a, Î·.app a â‰… Î¸.app a)
    (naturality :
      âˆ€ {a b} (f : a âŸ¶ b),
        F.map f â— (app b).hom â‰« (Î¸.naturality f).hom =
          (Î·.naturality f).hom â‰« (app a).hom â–· G.map f := by aesop_cat) : Î· â‰… Î¸ where
  hom := { app a := (app a).hom }
  inv :=
    { app a := (app a).inv
      naturality {a b} f := by
        simpa using _ â— (app b).inv â‰«= (naturality f).symm =â‰« (app a).inv â–· _ }

end StrongTrans

end CategoryTheory.Oplax
