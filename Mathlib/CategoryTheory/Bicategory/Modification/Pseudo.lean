/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno, Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.Bicategory.NaturalTransformation.Pseudo
import Mathlib.CategoryTheory.Bicategory.Modification.Oplax


/-!
# Modifications between strong transformations of pseudofunctors

A modification `Î“` between strong transformations `Î·` and `Î¸` consists of a family of
2-morphisms `Î“.app a : Î·.app a âŸ¶ Î¸.app a`, which satisfies the equation
`(F.map f â— app b) â‰« Î¸.naturality f = Î·.naturality f â‰« (app a â–· G.map f)`
for each 1-morphism `f : a âŸ¶ b`.

## Main definitions

* `Modification Î· Î¸` : modifications between strong transformations `Î·` and `Î¸`
* `Modification.vcomp Î· Î¸` : the vertical composition of strong transformations `Î·`
  and `Î¸`
* `Pseudofunctor.category F G` : the category structure on the strong transformations
  between `F` and `G`

-/

namespace CategoryTheory.Pseudofunctor

open Category Bicategory

open scoped Bicategory

universe wâ‚ wâ‚‚ vâ‚ vâ‚‚ uâ‚ uâ‚‚

variable {B : Type uâ‚} [Bicategory.{wâ‚, vâ‚} B] {C : Type uâ‚‚} [Bicategory.{wâ‚‚, vâ‚‚} C]
  {F G : Pseudofunctor B C} (Î· Î¸ : F âŸ¶ G)

/-- A modification `Î“` between strong transformations `Î·` and `Î¸` consists of a family of
2-morphisms `Î“.app a : Î·.app a âŸ¶ Î¸.app a`, which satisfies the equation
`(F.map f â— app b) â‰« Î¸.naturality f = Î·.naturality f â‰« (app a â–· G.map f)`
for each 1-morphism `f : a âŸ¶ b`.
-/
@[ext]
structure Modification where
  /-- The underlying family of 2-morphism. -/
  app (a : B) : Î·.app a âŸ¶ Î¸.app a
  /-- The naturality condition. -/
  naturality :
    âˆ€ {a b : B} (f : a âŸ¶ b),
      F.map f â— app b â‰« (Î¸.naturality f).hom =
        (Î·.naturality f).hom â‰« app a â–· G.map f := by aesop_cat

attribute [reassoc (attr := simp)] Modification.naturality

namespace Modification

/-- The identity modification. -/
@[simps]
def id : Modification Î· Î· where app a := ðŸ™ (Î·.app a)

instance : Inhabited (Modification Î· Î·) :=
  âŸ¨Modification.id Î·âŸ©

variable {Î· Î¸} {Î¹ : F âŸ¶ G}
variable (Î“ : Modification Î· Î¸)

-- TODO: add coercion for strong transformations? (or is it not infered here?)
/-- The modification between the underlying oplax transformations of oplax functors -/
@[simps]
def toOplax : Oplax.Modification Î·.toOplax Î¸.toOplax where
  app a := Î“.app a

instance hasCoeToOplax : Coe (Modification Î· Î¸) (Oplax.Modification Î·.toOplax Î¸.toOplax) :=
  âŸ¨toOplaxâŸ©

/-- The modification between strong transformations of pseudofunctors associated to a modification
between the underlying oplax transformations of oplax functors. -/
@[simps]
def mkOfOplax (Î“ : Oplax.Modification Î·.toOplax Î¸.toOplax) : Modification Î· Î¸ where
  app a := Î“.app a
  naturality f := by simpa using Î“.naturality f

section

variable {a b c : B} {a' : C}

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality (f : a' âŸ¶ F.obj b) (g : b âŸ¶ c) :
    f â— F.map g â— Î“.app c â‰« f â— (Î¸.naturality g).hom =
      f â— (Î·.naturality g).hom â‰« f â— Î“.app b â–· G.map g := by
  simp_rw [â† whiskerLeft_comp, naturality]

@[reassoc (attr := simp)]
theorem whiskerRight_naturality (f : a âŸ¶ b) (g : G.obj b âŸ¶ a') :
    F.map f â— Î“.app b â–· g â‰« (Î±_ _ _ _).inv â‰« (Î¸.naturality f).hom â–· g =
      (Î±_ _ _ _).inv â‰« (Î·.naturality f).hom â–· g â‰« Î“.app a â–· G.map f â–· g := by
  simp_rw [associator_inv_naturality_middle_assoc, â† comp_whiskerRight, naturality]

end

/-- Vertical composition of modifications. -/
@[simps]
def vcomp (Î” : Modification Î¸ Î¹) : Modification Î· Î¹ where
  app a := Î“.app a â‰« Î”.app a

end Modification

/-- Category structure on the strong transformations between pseudofunctors. -/
@[simps]
instance homcategory (F G : Pseudofunctor B C) : Category (F âŸ¶ G) where
  Hom := Modification
  id := Modification.id
  comp := Modification.vcomp

@[ext]
lemma homcategory.ext {F G : Pseudofunctor B C} {Î± Î² : F âŸ¶ G} {m n : Î± âŸ¶ Î²}
    (w : âˆ€ b, m.app b = n.app b) : m = n := by
  apply Modification.ext
  ext
  apply w

@[simp]
lemma Modification.id_app' {X : B} {F G : Pseudofunctor B C} (Î± : F âŸ¶ G) :
    Modification.app (ðŸ™ Î±) X = ðŸ™ (Î±.app X) :=
  rfl

@[simp]
lemma Modification.comp_app' {X : B} {F G : Pseudofunctor B C} {Î± Î² Î³ : F âŸ¶ G}
    (m : Î± âŸ¶ Î²) (n : Î² âŸ¶ Î³) : (m â‰« n).app X = m.app X â‰« n.app X :=
  rfl

variable {Î· Î¸}

/-- Construct a modification isomorphism between strong transformations
by giving object level isomorphisms, and checking naturality only in the forward direction.
-/
@[simps]
def ModificationIso.ofComponents (app : âˆ€ a, Î·.app a â‰… Î¸.app a)
    (naturality : âˆ€ {a b} (f : a âŸ¶ b),
      F.map f â— (app b).hom â‰« (Î¸.naturality f).hom =
        (Î·.naturality f).hom â‰« (app a).hom â–· G.map f) :
    Î· â‰… Î¸ where
  hom := { app := fun a => (app a).hom }
  inv :=
    { app := fun a => (app a).inv
      naturality := fun {a b} f => by
        simpa using
          congr_arg (fun f => _ â— (app b).inv â‰« f â‰« (app a).inv â–· _) (naturality f).symm }

-- open Oplax in
-- def ModificationIso.ofOplax (Î“ : Î·.toOplax â‰… Î¸.toOplax) : Î· â‰… Î¸ :=
--   ModificationIso.ofComponents (fun a => Î“.app a) Î“.naturality

end CategoryTheory.Pseudofunctor
