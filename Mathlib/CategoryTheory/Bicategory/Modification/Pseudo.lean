/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.Bicategory.NaturalTransformation.Pseudo
import Mathlib.CategoryTheory.Bicategory.Modification.Oplax

/-!
# Modifications between transformations of pseudofunctors

In this file we define modifications of strong transformations of pseudofunctors. They are defined
similarly to modifications of transformations of oplax functors.

## Main definitions

Given two pseudofunctors `F` and `G`, we define:

* `Pseudofunctor.StrongTrans.Modification Î· Î¸` : modifications between strong transformations
  `Î·` and `Î¸` (between `F` and `G`).
* `Pseudofunctor.StrongTrans.homCategory F G` : the category structure on strong transformations
  between `F` and `G`, where the morphisms are modifications, and composition is given by vertical
  composition of modifications.

-/

namespace CategoryTheory.Pseudofunctor

open Category Bicategory

universe wâ‚ wâ‚‚ vâ‚ vâ‚‚ uâ‚ uâ‚‚

variable {B : Type uâ‚} [Bicategory.{wâ‚, vâ‚} B] {C : Type uâ‚‚} [Bicategory.{wâ‚‚, vâ‚‚} C]
  {F G : Pseudofunctor B C}

namespace StrongTrans

variable (Î· Î¸ : F âŸ¶ G)

/-- A modification `Î“` between strong transformations `Î·` and `Î¸` (of pseudofunctors) consists of a
family of 2-morphisms `Î“.app a : Î·.app a âŸ¶ Î¸.app a`, which satisfies the equation
`(F.map f â— app b) â‰« (Î¸.naturality f).hom = (Î·.naturality f).hom â‰« (app a â–· G.map f)`
for each 1-morphism `f : a âŸ¶ b`.
-/
@[ext]
structure Modification where
  /-- The underlying family of 2-morphism. -/
  app (a : B) : Î·.app a âŸ¶ Î¸.app a
  /-- The naturality condition. -/
  naturality :
    âˆ€ {a b : B} (f : a âŸ¶ b),
      F.map f â— app b â‰« (Î¸.naturality f).hom =
        (Î·.naturality f).hom â‰« app a â–· G.map f := by aesop_cat

attribute [reassoc (attr := simp)] Modification.naturality

namespace Modification

variable {Î· Î¸} (Î“ : Modification Î· Î¸)

/-- The modification between the corresponding strong transformation of the underlying oplax
functors. -/
@[simps]
def toOplax : Oplax.StrongTrans.Modification Î·.toOplax Î¸.toOplax where
  app a := Î“.app a

instance hasCoeToOplax :
    Coe (Modification Î· Î¸) (Oplax.StrongTrans.Modification Î·.toOplax Î¸.toOplax) :=
  âŸ¨toOplaxâŸ©

/-- The modification between strong transformations of pseudofunctors associated to a modification
between the underlying strong transformations of oplax functors. -/
@[simps]
def mkOfOplax (Î“ : Oplax.StrongTrans.Modification Î·.toOplax Î¸.toOplax) : Modification Î· Î¸ where
  app a := Î“.app a
  naturality f := Î“.naturality f

section

variable {a b c : B} {a' : C}

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality (f : a' âŸ¶ F.obj b) (g : b âŸ¶ c) :
    f â— F.map g â— Î“.app c â‰« f â— (Î¸.naturality g).hom =
      f â— (Î·.naturality g).hom â‰« f â— Î“.app b â–· G.map g :=
  Oplax.StrongTrans.Modification.whiskerLeft_naturality Î“.toOplax _ _

@[reassoc (attr := simp)]
theorem whiskerRight_naturality (f : a âŸ¶ b) (g : G.obj b âŸ¶ a') :
    F.map f â— Î“.app b â–· g â‰« (Î±_ _ _ _).inv â‰« (Î¸.naturality f).hom â–· g =
      (Î±_ _ _ _).inv â‰« (Î·.naturality f).hom â–· g â‰« Î“.app a â–· G.map f â–· g :=
  Oplax.StrongTrans.Modification.whiskerRight_naturality Î“.toOplax _ _

end


end Modification

/-- Category structure on the strong transformations between pseudofunctors. -/
@[simps]
scoped instance homCategory : Category (F âŸ¶ G) where
  Hom := Modification
  id Î· := { app := fun a â†¦ ğŸ™ (Î·.app a) }
  comp Î“ Î” := { app := fun a â†¦ Î“.app a â‰« Î”.app a }

instance : Inhabited (Modification Î· Î·) :=
  âŸ¨ğŸ™ Î·âŸ©

@[ext]
lemma homCategory.ext {m n : Î· âŸ¶ Î¸} (w : âˆ€ b, m.app b = n.app b) : m = n :=
  Modification.ext (funext w)

/-- Construct a modification isomorphism between strong transformations
by giving object level isomorphisms, and checking naturality only in the forward direction.
-/
@[simps]
def ModificationIso.ofComponents (app : âˆ€ a, Î·.app a â‰… Î¸.app a)
    (naturality : âˆ€ {a b} (f : a âŸ¶ b),
      F.map f â— (app b).hom â‰« (Î¸.naturality f).hom =
        (Î·.naturality f).hom â‰« (app a).hom â–· G.map f) :
    Î· â‰… Î¸ where
  hom := { app := fun a => (app a).hom }
  inv :=
    { app := fun a => (app a).inv
      naturality := fun {a b} f => by
        simpa using
          congr_arg (fun f => _ â— (app b).inv â‰« f â‰« (app a).inv â–· _) (naturality f).symm }

end StrongTrans

end CategoryTheory.Pseudofunctor
