/-
Copyright (c) 2024 Calle S√∂nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle S√∂nne
-/
import Mathlib.CategoryTheory.Bicategory.NaturalTransformation.Pseudo
import Mathlib.CategoryTheory.Bicategory.Modification.Oplax

/-!
# Modifications between transformations of pseudofunctors

In this file we define modifications of strong transformations of pseudofunctors. They are defined
similarly to modifications of transformations of oplax functors.

## Main definitions

Given two pseudofunctors `F` and `G`, we define:

* `Pseudofunctor.StrongTrans.Modification Œ∑ Œ∏` : modifications between strong transformations
  `Œ∑` and `Œ∏` (between `F` and `G`).
* `Pseudofunctor.StrongTrans.homCategory F G` : the category structure on strong transformations
  between `F` and `G`, where the morphisms are modifications, and composition is given by vertical
  composition of modifications. Note that this a scoped instance in the `Pseudofunctor.StrongTrans`
  namespace, so you need to run `open scoped Pseudofunctor.StrongTrans` to access it.

-/

namespace CategoryTheory.Pseudofunctor

open Category Bicategory

universe w‚ÇÅ w‚ÇÇ v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

variable {B : Type u‚ÇÅ} [Bicategory.{w‚ÇÅ, v‚ÇÅ} B] {C : Type u‚ÇÇ} [Bicategory.{w‚ÇÇ, v‚ÇÇ} C]
  {F G : Pseudofunctor B C}

namespace StrongTrans

variable (Œ∑ Œ∏ : F ‚ü∂ G)

/-- A modification `Œì` between strong transformations (of pseudofunctors) `Œ∑` and `Œ∏` consists of a
family of 2-morphisms `Œì.app a : Œ∑.app a ‚ü∂ Œ∏.app a`, which satisfies the equation
`(F.map f ‚óÅ app b) ‚â´ (Œ∏.naturality f).hom = (Œ∑.naturality f).hom ‚â´ (app a ‚ñ∑ G.map f)`
for each 1-morphism `f : a ‚ü∂ b`.
-/
@[ext]
structure Modification where
  /-- The underlying family of 2-morphism. -/
  app (a : B) : Œ∑.app a ‚ü∂ Œ∏.app a
  /-- The naturality condition. -/
  naturality :
    ‚àÄ {a b : B} (f : a ‚ü∂ b),
      F.map f ‚óÅ app b ‚â´ (Œ∏.naturality f).hom =
        (Œ∑.naturality f).hom ‚â´ app a ‚ñ∑ G.map f := by aesop_cat

attribute [reassoc (attr := simp)] Modification.naturality

namespace Modification

variable {Œ∑ Œ∏} (Œì : Modification Œ∑ Œ∏)

/-- The modification between the corresponding strong transformation of the underlying oplax
functors. -/
@[simps]
def toOplax : Oplax.StrongTrans.Modification Œ∑.toOplax Œ∏.toOplax where
  app a := Œì.app a

instance hasCoeToOplax :
    Coe (Modification Œ∑ Œ∏) (Oplax.StrongTrans.Modification Œ∑.toOplax Œ∏.toOplax) :=
  ‚ü®toOplax‚ü©

/-- The modification between strong transformations of pseudofunctors associated to a modification
between the underlying strong transformations of oplax functors. -/
@[simps]
def mkOfOplax (Œì : Oplax.StrongTrans.Modification Œ∑.toOplax Œ∏.toOplax) : Modification Œ∑ Œ∏ where
  app a := Œì.app a
  naturality f := Œì.naturality f

/-- Modifications between strong transformations of pseudofunctors are equivalent to modifications
between the underlying strong transformations of oplax functors. -/
@[simps]
def equivOplax : (Oplax.StrongTrans.Modification Œ∑.toOplax Œ∏.toOplax) ‚âÉ Modification Œ∑ Œ∏ where
  toFun := mkOfOplax
  invFun := toOplax
  left_inv _ := rfl
  right_inv _ := rfl

section

variable {a b c : B} {a' : C}

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality (f : a' ‚ü∂ F.obj b) (g : b ‚ü∂ c) :
    f ‚óÅ F.map g ‚óÅ Œì.app c ‚â´ f ‚óÅ (Œ∏.naturality g).hom =
      f ‚óÅ (Œ∑.naturality g).hom ‚â´ f ‚óÅ Œì.app b ‚ñ∑ G.map g :=
  Oplax.StrongTrans.Modification.whiskerLeft_naturality Œì.toOplax _ _

@[reassoc (attr := simp)]
theorem whiskerRight_naturality (f : a ‚ü∂ b) (g : G.obj b ‚ü∂ a') :
    F.map f ‚óÅ Œì.app b ‚ñ∑ g ‚â´ (Œ±_ _ _ _).inv ‚â´ (Œ∏.naturality f).hom ‚ñ∑ g =
      (Œ±_ _ _ _).inv ‚â´ (Œ∑.naturality f).hom ‚ñ∑ g ‚â´ Œì.app a ‚ñ∑ G.map f ‚ñ∑ g :=
  Oplax.StrongTrans.Modification.whiskerRight_naturality Œì.toOplax _ _

end

variable (Œ∑) in
/-- The identity modification. -/
@[simps]
def id : Modification Œ∑ Œ∑ where app a := ùüô (Œ∑.app a)

instance : Inhabited (Modification Œ∑ Œ∑) :=
  ‚ü®Modification.id Œ∑‚ü©

/-- Vertical composition of modifications. -/
@[simps]
def vcomp {Œπ : F ‚ü∂ G} (Œì : Modification Œ∑ Œ∏) (Œî : Modification Œ∏ Œπ) : Modification Œ∑ Œπ where
  app a := Œì.app a ‚â´ Œî.app a


end Modification

/-- Category structure on the strong transformations between pseudofunctors. -/
@[simps!]
scoped instance homCategory : Category (F ‚ü∂ G) where
  Hom := Modification
  id := Modification.id
  comp := Modification.vcomp

instance : Inhabited (Modification Œ∑ Œ∑) :=
  ‚ü®ùüô Œ∑‚ü©

@[ext]
lemma homCategory.ext {m n : Œ∑ ‚ü∂ Œ∏} (w : ‚àÄ b, m.app b = n.app b) : m = n :=
  Modification.ext (funext w)

/-- Construct a modification isomorphism between strong transformations
by giving object level isomorphisms, and checking naturality only in the forward direction.
-/
@[simps]
def isoMk (app : ‚àÄ a, Œ∑.app a ‚âÖ Œ∏.app a)
    (naturality : ‚àÄ {a b} (f : a ‚ü∂ b),
      F.map f ‚óÅ (app b).hom ‚â´ (Œ∏.naturality f).hom =
        (Œ∑.naturality f).hom ‚â´ (app a).hom ‚ñ∑ G.map f := by aesop_cat) :
    Œ∑ ‚âÖ Œ∏ where
  hom := { app a := (app a).hom }
  inv :=
    { app a := (app a).inv
      naturality {a b} f := by
        simpa using _ ‚óÅ (app b).inv ‚â´= (naturality f).symm =‚â´ (app a).inv ‚ñ∑ _ }

end StrongTrans

end CategoryTheory.Pseudofunctor
