/-
Copyright (c) 2025 Calle S√∂nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle S√∂nne
-/
module

public import Mathlib.CategoryTheory.Bicategory.Functor.StrictPseudofunctor

/-!
# Cartesian products of bicategories

We define the bicategory instance on `B √ó C` when `B` and `C` are bicategories.

We define:
* `sectL B c` : the strictly unitary pseudofunctor `B ‚•§ B √ó C` given by `X ‚Ü¶ ‚ü®X, c‚ü©`
* `sectR b C` : the strictly unitary pseudofunctor `C ‚•§ B √ó C` given by `Y ‚Ü¶ ‚ü®b, Y‚ü©`
* `fst`       : the strict pseudofunctor `‚ü®X, Y‚ü© ‚Ü¶ X`
* `snd`       : the strict pseudofunctor `‚ü®X, Y‚ü© ‚Ü¶ Y`
* `swap`      : the strict pseudofunctor `B √ó C ‚•§ C √ó B` given by `‚ü®X, Y‚ü© ‚Ü¶ ‚ü®Y, X‚ü©`

-/

@[expose] public section

namespace CategoryTheory.Bicategory

open Prod

universe w‚ÇÅ w‚ÇÇ v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

/-- The cartesian product of two bicategories. -/
@[simps! (notRecursive := [])] -- notRecursive to generate simp lemmas like _fst and _snd
instance prod (B : Type u‚ÇÅ) [Bicategory.{w‚ÇÅ, v‚ÇÅ} B] (C : Type u‚ÇÇ) [Bicategory.{w‚ÇÇ, v‚ÇÇ} C] :
    Bicategory (B √ó C) where
  homCategory X Y := CategoryTheory.prod' (X.1 ‚ü∂ Y.1) (X.2 ‚ü∂ Y.2)
  whiskerLeft f g h Œ∏ := f.1 ‚óÅ Œ∏.1 √ó‚Çò f.2 ‚óÅ Œ∏.2
  whiskerRight Œ∏ g := Œ∏.1 ‚ñ∑ g.1 √ó‚Çò Œ∏.2 ‚ñ∑ g.2
  associator f g h := Iso.prod (Œ±_ f.1 g.1 h.1) (Œ±_ f.2 g.2 h.2)
  leftUnitor f := Iso.prod (Œª_ f.1) (Œª_ f.2)
  rightUnitor f := Iso.prod (œÅ_ f.1) (œÅ_ f.2)
  whisker_exchange Œ∑ Œ∏ := Prod.ext (whisker_exchange Œ∑.1 Œ∏.1) (whisker_exchange Œ∑.2 Œ∏.2)

namespace Prod

/-- `sectL B c` is the strictly unitary pseudofunctor `B ‚•§ B √ó C` given by `X ‚Ü¶ (X, c)`. -/
@[simps!]
def sectL (B : Type u‚ÇÅ) [Bicategory.{w‚ÇÅ, v‚ÇÅ} B] {C : Type u‚ÇÇ} [Bicategory.{w‚ÇÇ, v‚ÇÇ} C] (c : C) :
    StrictlyUnitaryPseudofunctor B (B √ó C) := .mk'
  { obj X := (X, c)
    map f := f √ó‚Çò ùüô c
    map‚ÇÇ Œ∑ := Œ∑ √ó‚Çò ùüô _
    mapComp f g := Iso.prod (Iso.refl _) (Œª_ (g, ùüô c).2).symm }

/-- `sectR b C` is the strictly unitary pseudofunctor `C ‚•§ B √ó C` given by `Y ‚Ü¶ (b, Y)`. -/
@[simps!]
def sectR {B : Type u‚ÇÅ} [Bicategory.{w‚ÇÅ, v‚ÇÅ} B] (b : B) (C : Type u‚ÇÇ) [Bicategory.{w‚ÇÇ, v‚ÇÇ} C] :
    StrictlyUnitaryPseudofunctor C (B √ó C) := .mk'
  { obj Y := (b, Y)
    map f := ùüô b √ó‚Çò f
    map‚ÇÇ Œ∑ := ùüô _ √ó‚Çò Œ∑
    mapComp f g := Iso.prod (œÅ_ (ùüô b)).symm (Iso.refl _) }

variable (B : Type u‚ÇÅ) [Bicategory.{w‚ÇÅ, v‚ÇÅ} B] (C : Type u‚ÇÇ) [Bicategory.{w‚ÇÇ, v‚ÇÇ} C]

/-- `fst` is the strict pseudofunctor given by projection to the first factor. -/
@[simps!]
def fst : StrictPseudofunctor (B √ó C) B := .mk'
  { obj X := X.1
    map f := f.1
    map‚ÇÇ Œ∑ := Œ∑.1 }

/-- `snd` is the strict pseudofunctor given by projection to the second factor. -/
@[simps!]
def snd : StrictPseudofunctor (B √ó C) C := .mk'
  { obj X := X.2
    map f := f.2
    map‚ÇÇ Œ∑ := Œ∑.2 }

/-- The pseudofunctor swapping the factors of a cartesian product of bicategories,
`B √ó C ‚•§ C √ó B`. -/
@[simps!]
def swap : StrictPseudofunctor (B √ó C) (C √ó B) := .mk'
  { obj X := (X.2, X.1)
    map f := f.2 √ó‚Çò f.1
    map‚ÇÇ Œ∑ := Œ∑.2 √ó‚Çò Œ∑.1 }

end Prod

section

variable (B : Type u‚ÇÅ) [Bicategory.{w‚ÇÅ, v‚ÇÅ} B] (C : Type u‚ÇÅ) [Bicategory.{w‚ÇÅ, v‚ÇÅ} C]

/-- `Bicategory.uniformProd B C` is an additional instance specialised so both factors have the same
universe levels. This helps typeclass resolution.
-/
instance uniformProd : Bicategory (B √ó C) :=
  Bicategory.prod B C

end

end CategoryTheory.Bicategory
