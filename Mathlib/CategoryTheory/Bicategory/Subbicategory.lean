/-
Copyright (c) 2025 Calle S√∂nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle S√∂nne
-/
module

public import Mathlib.CategoryTheory.Bicategory.InducedBicategory
public import Mathlib.CategoryTheory.ObjectProperty.Basic

/-!
# The full subbicategory associated to a property of objects

Given a bicategory `B` and `P : ObjectProperty B`, we define
a bicategory structure on the type `FullSubbicategory P`
of objects in `B` satisfying `P`.

-/

@[expose] public section

universe w w' v v' u u'

namespace CategoryTheory

-- TODO: wrong namespace?
namespace Bicategory

variable {B : Type u} [Bicategory.{w, v} B]

/--
A subtype-like structure for full subcategories. Morphisms just ignore the property. We don't use
actual subtypes since the simp-normal form `‚ÜëX` of `X.val` does not work well for full
subcategories. -/
@[ext]
structure FullSubbicategory (P : ObjectProperty B) where
  /-- The category of which this is a full subcategory -/
  obj : B
  /-- The predicate satisfied by all objects in this subcategory -/
  property : P obj

namespace FullSubbicategory

section

variable (P : ObjectProperty B)

instance bicategory : Bicategory.{w, v} (FullSubbicategory P) :=
  InducedBicategory.bicategory FullSubbicategory.obj

abbrev mkHom‚ÇÇ {a b : FullSubbicategory P} {f g : a ‚ü∂ b} (Œ∑ : f.hom ‚ü∂ g.hom) : f ‚ü∂ g :=
  InducedBicategory.mkHom‚ÇÇ Œ∑

-- these lemmas are not particularly well-typed, so would probably be dangerous as simp lemmas

lemma id_def (X : FullSubbicategory P) : ùüô X = ‚ü®ùüô X.obj‚ü© := rfl

lemma comp_def {X Y Z : FullSubbicategory P} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).hom = f.hom ‚â´ g.hom := rfl

/-- The forgetful functor from a full subcategory into the original category
("forgetting" the condition).
-/
def forget : StrictPseudofunctor (FullSubbicategory P) B :=
  InducedBicategory.forget FullSubbicategory.obj

@[simp]
theorem forget_obj {X} : (forget P).obj X = X.obj :=
  rfl

@[simp]
theorem forget_map {X Y} {f : X ‚ü∂ Y} : (forget P).map f = f.hom := -- TODO: right statement?
  rfl

/-- Constructor for isomorphisms in `FullSubbicategory P` when
`P : ObjectProperty C`. -/
@[simps]
def isoMk {X Y : FullSubbicategory P} {f g : X ‚ü∂ Y} (e : (forget P).map f ‚âÖ (forget P).map g) :
    f ‚âÖ g where
  hom := InducedBicategory.mkHom‚ÇÇ e.hom -- TODO: need mkHom‚ÇÇ in this namespace
  inv := InducedBicategory.mkHom‚ÇÇ e.inv
  hom_inv_id := InducedBicategory.hom‚ÇÇ_ext <| e.hom_inv_id
  inv_hom_id := InducedBicategory.hom‚ÇÇ_ext <| e.inv_hom_id


variable {P} {P' : ObjectProperty B}

/-- If `P` and `P'` are properties of objects such that `P ‚â§ P'`, there is
an induced functor `FullSubbicategory P ‚•§ P'.FullSubbicategory`. -/
@[simps!]
def ŒπOfLE (h : P ‚â§ P') : StrictPseudofunctor (FullSubbicategory P) (FullSubbicategory P') :=
  StrictPseudofunctor.mk' {
    obj X := ‚ü®X.1, h _ X.2‚ü©
    map f := ‚ü®f.hom‚ü©
    map‚ÇÇ Œ∑ := InducedBicategory.mkHom‚ÇÇ Œ∑.hom }

end

section lift

variable {C : Type u'} [Bicategory.{w', v'} C] (P Q : ObjectProperty C)
  (F : B ‚•§·µñ C) (hF : ‚àÄ X, P (F.obj X))

/-- A pseudofunctor which maps objects to objects satisfying a certain property induces a lift
through the full subcategory of objects satisfying that property. -/
@[simps]
def lift : B ‚•§·µñ FullSubbicategory P where
  obj X := ‚ü®F.obj X, hF X‚ü©
  map f := ‚ü®F.map f‚ü©
  map‚ÇÇ Œ∑ := InducedBicategory.mkHom‚ÇÇ (F.map‚ÇÇ Œ∑)
  mapId X := isoMk P (F.mapId X) -- TODO: P should be implicit
  mapComp f g := isoMk P (F.mapComp f g)

@[simp]
lemma Œπ_obj_lift_obj (X : B) :
    (forget P).obj ((lift P F hF).obj X) = F.obj X := rfl

@[simp]
lemma Œπ_obj_lift_map {X Y : B} (f : X ‚ü∂ Y) :
    (forget P).map ((lift P F hF).map f) = F.map f := rfl

end lift

end FullSubbicategory

end Bicategory

end CategoryTheory
