/-
Copyright (c) 2021 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import Mathlib.CategoryTheory.NatIso

#align_import category_theory.bicategory.basic from "leanprover-community/mathlib"@"4c19a16e4b705bf135cf9a80ac18fcc99c438514"

/-!
# Bicategories

In this file we define typeclass for bicategories.

A bicategory `B` consists of
* objects `a : B`,
* 1-morphisms `f : a âŸ¶ b` between objects `a b : B`, and
* 2-morphisms `Î· : f âŸ¶ g` between 1-morphisms `f g : a âŸ¶ b` between objects `a b : B`.

We use `u`, `v`, and `w` as the universe variables for objects, 1-morphisms, and 2-morphisms,
respectively.

A typeclass for bicategories extends `CategoryTheory.CategoryStruct` typeclass. This means that
we have
* a composition `f â‰« g : a âŸ¶ c` for each 1-morphisms `f : a âŸ¶ b` and `g : b âŸ¶ c`, and
* an identity `ğŸ™ a : a âŸ¶ a` for each object `a : B`.

For each object `a b : B`, the collection of 1-morphisms `a âŸ¶ b` has a category structure. The
2-morphisms in the bicategory are implemented as the morphisms in this family of categories.

The composition of 1-morphisms is in fact an object part of a functor
`(a âŸ¶ b) â¥¤ (b âŸ¶ c) â¥¤ (a âŸ¶ c)`. The definition of bicategories in this file does not
require this functor directly. Instead, it requires the whiskering functions. For a 1-morphism
`f : a âŸ¶ b` and a 2-morphism `Î· : g âŸ¶ h` between 1-morphisms `g h : b âŸ¶ c`, there is a
2-morphism `whiskerLeft f Î· : f â‰« g âŸ¶ f â‰« h`. Similarly, for a 2-morphism `Î· : f âŸ¶ g`
between 1-morphisms `f g : a âŸ¶ b` and a 1-morphism `f : b âŸ¶ c`, there is a 2-morphism
`whiskerRight Î· h : f â‰« h âŸ¶ g â‰« h`. These satisfy the exchange law
`whiskerLeft f Î¸ â‰« whiskerRight Î· i = whiskerRight Î· h â‰« whiskerLeft g Î¸`,
which is required as an axiom in the definition here.
-/

namespace CategoryTheory

universe w v u

open Category Iso

-- intended to be used with explicit universe parameters
/-- In a bicategory, we can compose the 1-morphisms `f : a âŸ¶ b` and `g : b âŸ¶ c` to obtain
a 1-morphism `f â‰« g : a âŸ¶ c`. This composition does not need to be strictly associative,
but there is a specified associator, `Î±_ f g h : (f â‰« g) â‰« h â‰… f â‰« (g â‰« h)`.
There is an identity 1-morphism `ğŸ™ a : a âŸ¶ a`, with specified left and right unitor
isomorphisms `Î»_ f : ğŸ™ a â‰« f â‰… f` and `Ï_ f : f â‰« ğŸ™ a â‰… f`.
These associators and unitors satisfy the pentagon and triangle equations.

See https://ncatlab.org/nlab/show/bicategory.
-/
@[nolint checkUnivs]
class Bicategory (B : Type u) extends CategoryStruct.{v} B where
  -- category structure on the collection of 1-morphisms:
  homCategory : âˆ€ a b : B, Category.{w} (a âŸ¶ b) := by infer_instance
  -- left whiskering:
  whiskerLeft {a b c : B} (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g âŸ¶ h) : f â‰« g âŸ¶ f â‰« h
  -- right whiskering:
  whiskerRight {a b c : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g) (h : b âŸ¶ c) : f â‰« h âŸ¶ g â‰« h
  -- associator:
  associator {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) : (f â‰« g) â‰« h â‰… f â‰« g â‰« h
  -- left unitor:
  leftUnitor {a b : B} (f : a âŸ¶ b) : ğŸ™ a â‰« f â‰… f
  -- right unitor:
  rightUnitor {a b : B} (f : a âŸ¶ b) : f â‰« ğŸ™ b â‰… f
  -- axioms for left whiskering:
  whiskerLeft_id : âˆ€ {a b c} (f : a âŸ¶ b) (g : b âŸ¶ c), whiskerLeft f (ğŸ™ g) = ğŸ™ (f â‰« g) := by
    aesop_cat
  whiskerLeft_comp :
    âˆ€ {a b c} (f : a âŸ¶ b) {g h i : b âŸ¶ c} (Î· : g âŸ¶ h) (Î¸ : h âŸ¶ i),
      whiskerLeft f (Î· â‰« Î¸) = whiskerLeft f Î· â‰« whiskerLeft f Î¸ := by
    aesop_cat
  id_whiskerLeft :
    âˆ€ {a b} {f g : a âŸ¶ b} (Î· : f âŸ¶ g),
      whiskerLeft (ğŸ™ a) Î· = (leftUnitor f).hom â‰« Î· â‰« (leftUnitor g).inv := by
    aesop_cat
  comp_whiskerLeft :
    âˆ€ {a b c d} (f : a âŸ¶ b) (g : b âŸ¶ c) {h h' : c âŸ¶ d} (Î· : h âŸ¶ h'),
      whiskerLeft (f â‰« g) Î· =
        (associator f g h).hom â‰« whiskerLeft f (whiskerLeft g Î·) â‰« (associator f g h').inv := by
    aesop_cat
  -- axioms for right whiskering:
  id_whiskerRight : âˆ€ {a b c} (f : a âŸ¶ b) (g : b âŸ¶ c), whiskerRight (ğŸ™ f) g = ğŸ™ (f â‰« g) := by
    aesop_cat
  comp_whiskerRight :
    âˆ€ {a b c} {f g h : a âŸ¶ b} (Î· : f âŸ¶ g) (Î¸ : g âŸ¶ h) (i : b âŸ¶ c),
      whiskerRight (Î· â‰« Î¸) i = whiskerRight Î· i â‰« whiskerRight Î¸ i := by
    aesop_cat
  whiskerRight_id :
    âˆ€ {a b} {f g : a âŸ¶ b} (Î· : f âŸ¶ g),
      whiskerRight Î· (ğŸ™ b) = (rightUnitor f).hom â‰« Î· â‰« (rightUnitor g).inv := by
    aesop_cat
  whiskerRight_comp :
    âˆ€ {a b c d} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') (g : b âŸ¶ c) (h : c âŸ¶ d),
      whiskerRight Î· (g â‰« h) =
        (associator f g h).inv â‰« whiskerRight (whiskerRight Î· g) h â‰« (associator f' g h).hom := by
    aesop_cat
  -- associativity of whiskerings:
  whisker_assoc :
    âˆ€ {a b c d} (f : a âŸ¶ b) {g g' : b âŸ¶ c} (Î· : g âŸ¶ g') (h : c âŸ¶ d),
      whiskerRight (whiskerLeft f Î·) h =
        (associator f g h).hom â‰« whiskerLeft f (whiskerRight Î· h) â‰« (associator f g' h).inv := by
    aesop_cat
  -- exchange law of left and right whiskerings:
  whisker_exchange :
    âˆ€ {a b c} {f g : a âŸ¶ b} {h i : b âŸ¶ c} (Î· : f âŸ¶ g) (Î¸ : h âŸ¶ i),
      whiskerLeft f Î¸ â‰« whiskerRight Î· i = whiskerRight Î· h â‰« whiskerLeft g Î¸ := by
    aesop_cat
  -- pentagon identity:
  pentagon :
    âˆ€ {a b c d e} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e),
      whiskerRight (associator f g h).hom i â‰«
          (associator f (g â‰« h) i).hom â‰« whiskerLeft f (associator g h i).hom =
        (associator (f â‰« g) h i).hom â‰« (associator f g (h â‰« i)).hom := by
    aesop_cat
  -- triangle identity:
  triangle :
    âˆ€ {a b c} (f : a âŸ¶ b) (g : b âŸ¶ c),
      (associator f (ğŸ™ b) g).hom â‰« whiskerLeft f (leftUnitor g).hom
      = whiskerRight (rightUnitor f).hom g := by
    aesop_cat
#align category_theory.bicategory CategoryTheory.Bicategory
#align category_theory.bicategory.hom_category CategoryTheory.Bicategory.homCategory
#align category_theory.bicategory.whisker_left CategoryTheory.Bicategory.whiskerLeft
#align category_theory.bicategory.whisker_right CategoryTheory.Bicategory.whiskerRight
#align category_theory.bicategory.left_unitor CategoryTheory.Bicategory.leftUnitor
#align category_theory.bicategory.right_unitor CategoryTheory.Bicategory.rightUnitor
#align category_theory.bicategory.whisker_left_id' CategoryTheory.Bicategory.whiskerLeft_id
#align category_theory.bicategory.whisker_left_comp' CategoryTheory.Bicategory.whiskerLeft_comp
#align category_theory.bicategory.id_whisker_left' CategoryTheory.Bicategory.id_whiskerLeft
#align category_theory.bicategory.comp_whisker_left' CategoryTheory.Bicategory.comp_whiskerLeft
#align category_theory.bicategory.id_whisker_right' CategoryTheory.Bicategory.id_whiskerRight
#align category_theory.bicategory.comp_whisker_right' CategoryTheory.Bicategory.comp_whiskerRight
#align category_theory.bicategory.whisker_right_id' CategoryTheory.Bicategory.whiskerRight_id
#align category_theory.bicategory.whisker_right_comp' CategoryTheory.Bicategory.whiskerRight_comp
#align category_theory.bicategory.whisker_assoc' CategoryTheory.Bicategory.whisker_assoc
#align category_theory.bicategory.whisker_exchange' CategoryTheory.Bicategory.whisker_exchange
#align category_theory.bicategory.pentagon' CategoryTheory.Bicategory.pentagon
#align category_theory.bicategory.triangle' CategoryTheory.Bicategory.triangle

namespace Bicategory

scoped infixr:81 " â— " => Bicategory.whiskerLeft
scoped infixl:81 " â–· " => Bicategory.whiskerRight
scoped notation "Î±_" => Bicategory.associator
scoped notation "Î»_" => Bicategory.leftUnitor
scoped notation "Ï_" => Bicategory.rightUnitor

/-!
### Simp-normal form for 2-morphisms

Rewriting involving associators and unitors could be very complicated. We try to ease this
complexity by putting carefully chosen simp lemmas that rewrite any 2-morphisms into simp-normal
form defined below. Rewriting into simp-normal form is also useful when applying (forthcoming)
`coherence` tactic.

The simp-normal form of 2-morphisms is defined to be an expression that has the minimal number of
parentheses. More precisely,
1. it is a composition of 2-morphisms like `Î·â‚ â‰« Î·â‚‚ â‰« Î·â‚ƒ â‰« Î·â‚„ â‰« Î·â‚…` such that each `Î·áµ¢` is
  either a structural 2-morphisms (2-morphisms made up only of identities, associators, unitors)
  or non-structural 2-morphisms, and
2. each non-structural 2-morphism in the composition is of the form `fâ‚ â— fâ‚‚ â— fâ‚ƒ â— Î· â–· fâ‚„ â–· fâ‚…`,
  where each `fáµ¢` is a 1-morphism that is not the identity or a composite and `Î·` is a
  non-structural 2-morphisms that is also not the identity or a composite.

Note that `fâ‚ â— fâ‚‚ â— fâ‚ƒ â— Î· â–· fâ‚„ â–· fâ‚…` is actually `fâ‚ â— (fâ‚‚ â— (fâ‚ƒ â— ((Î· â–· fâ‚„) â–· fâ‚…)))`.
-/

attribute [instance] homCategory

attribute [reassoc]
  whiskerLeft_comp id_whiskerLeft comp_whiskerLeft comp_whiskerRight whiskerRight_id
  whiskerRight_comp whisker_assoc whisker_exchange

attribute [reassoc (attr := simp)] pentagon triangle
/-
The following simp attributes are put in order to rewrite any 2-morphisms into normal forms. There
are associators and unitors in the RHS in the several simp lemmas here (e.g. `id_whiskerLeft`),
which at first glance look more complicated than the LHS, but they will be eventually reduced by
the pentagon or the triangle identities, and more generally, (forthcoming) `coherence` tactic.
-/
attribute [simp]
  whiskerLeft_id whiskerLeft_comp id_whiskerLeft comp_whiskerLeft id_whiskerRight comp_whiskerRight
  whiskerRight_id whiskerRight_comp whisker_assoc


variable {B : Type u} [Bicategory.{w, v} B] {a b c d e : B}

@[reassoc (attr := simp)]
theorem whiskerLeft_hom_inv (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g â‰… h) :
    f â— Î·.hom â‰« f â— Î·.inv = ğŸ™ (f â‰« g) := by rw [â† whiskerLeft_comp, hom_inv_id, whiskerLeft_id]
#align category_theory.bicategory.hom_inv_whisker_left CategoryTheory.Bicategory.whiskerLeft_hom_inv

@[reassoc (attr := simp)]
theorem hom_inv_whiskerRight {f g : a âŸ¶ b} (Î· : f â‰… g) (h : b âŸ¶ c) :
    Î·.hom â–· h â‰« Î·.inv â–· h = ğŸ™ (f â‰« h) := by rw [â† comp_whiskerRight, hom_inv_id, id_whiskerRight]
#align category_theory.bicategory.hom_inv_whisker_right CategoryTheory.Bicategory.hom_inv_whiskerRight

@[reassoc (attr := simp)]
theorem whiskerLeft_inv_hom (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g â‰… h) :
    f â— Î·.inv â‰« f â— Î·.hom = ğŸ™ (f â‰« h) := by rw [â† whiskerLeft_comp, inv_hom_id, whiskerLeft_id]
#align category_theory.bicategory.inv_hom_whisker_left CategoryTheory.Bicategory.whiskerLeft_inv_hom

@[reassoc (attr := simp)]
theorem inv_hom_whiskerRight {f g : a âŸ¶ b} (Î· : f â‰… g) (h : b âŸ¶ c) :
    Î·.inv â–· h â‰« Î·.hom â–· h = ğŸ™ (g â‰« h) := by rw [â† comp_whiskerRight, inv_hom_id, id_whiskerRight]
#align category_theory.bicategory.inv_hom_whisker_right CategoryTheory.Bicategory.inv_hom_whiskerRight

/-- The left whiskering of a 2-isomorphism is a 2-isomorphism. -/
@[simps]
def whiskerLeftIso (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g â‰… h) : f â‰« g â‰… f â‰« h where
  hom := f â— Î·.hom
  inv := f â— Î·.inv
#align category_theory.bicategory.whisker_left_iso CategoryTheory.Bicategory.whiskerLeftIso

instance whiskerLeft_isIso (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g âŸ¶ h) [IsIso Î·] : IsIso (f â— Î·) :=
  (whiskerLeftIso f (asIso Î·)).isIso_hom
#align category_theory.bicategory.whisker_left_is_iso CategoryTheory.Bicategory.whiskerLeft_isIso

@[simp]
theorem inv_whiskerLeft (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g âŸ¶ h) [IsIso Î·] :
    inv (f â— Î·) = f â— inv Î· := by
  apply IsIso.inv_eq_of_hom_inv_id
  simp only [â† whiskerLeft_comp, whiskerLeft_id, IsIso.hom_inv_id]
#align category_theory.bicategory.inv_whisker_left CategoryTheory.Bicategory.inv_whiskerLeft

/-- The right whiskering of a 2-isomorphism is a 2-isomorphism. -/
@[simps!]
def whiskerRightIso {f g : a âŸ¶ b} (Î· : f â‰… g) (h : b âŸ¶ c) : f â‰« h â‰… g â‰« h where
  hom := Î·.hom â–· h
  inv := Î·.inv â–· h
#align category_theory.bicategory.whisker_right_iso CategoryTheory.Bicategory.whiskerRightIso

instance whiskerRight_isIso {f g : a âŸ¶ b} (Î· : f âŸ¶ g) (h : b âŸ¶ c) [IsIso Î·] : IsIso (Î· â–· h) :=
  (whiskerRightIso (asIso Î·) h).isIso_hom
#align category_theory.bicategory.whisker_right_is_iso CategoryTheory.Bicategory.whiskerRight_isIso

@[simp]
theorem inv_whiskerRight {f g : a âŸ¶ b} (Î· : f âŸ¶ g) (h : b âŸ¶ c) [IsIso Î·] :
    inv (Î· â–· h) = inv Î· â–· h := by
  apply IsIso.inv_eq_of_hom_inv_id
  simp only [â† comp_whiskerRight, id_whiskerRight, IsIso.hom_inv_id]
#align category_theory.bicategory.inv_whisker_right CategoryTheory.Bicategory.inv_whiskerRight

@[reassoc (attr := simp)]
theorem pentagon_inv (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
    f â— (Î±_ g h i).inv â‰« (Î±_ f (g â‰« h) i).inv â‰« (Î±_ f g h).inv â–· i =
      (Î±_ f g (h â‰« i)).inv â‰« (Î±_ (f â‰« g) h i).inv :=
  eq_of_inv_eq_inv (by simp)
#align category_theory.bicategory.pentagon_inv CategoryTheory.Bicategory.pentagon_inv

@[reassoc (attr := simp)]
theorem pentagon_inv_inv_hom_hom_inv (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
    (Î±_ f (g â‰« h) i).inv â‰« (Î±_ f g h).inv â–· i â‰« (Î±_ (f â‰« g) h i).hom =
    f â— (Î±_ g h i).hom â‰« (Î±_ f g (h â‰« i)).inv := by
  rw [â† cancel_epi (f â— (Î±_ g h i).inv), â† cancel_mono (Î±_ (f â‰« g) h i).inv]
  simp
#align category_theory.bicategory.pentagon_inv_inv_hom_hom_inv CategoryTheory.Bicategory.pentagon_inv_inv_hom_hom_inv

@[reassoc (attr := simp)]
theorem pentagon_inv_hom_hom_hom_inv (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
    (Î±_ (f â‰« g) h i).inv â‰« (Î±_ f g h).hom â–· i â‰« (Î±_ f (g â‰« h) i).hom =
      (Î±_ f g (h â‰« i)).hom â‰« f â— (Î±_ g h i).inv :=
  eq_of_inv_eq_inv (by simp)
#align category_theory.bicategory.pentagon_inv_hom_hom_hom_inv CategoryTheory.Bicategory.pentagon_inv_hom_hom_hom_inv

@[reassoc (attr := simp)]
theorem pentagon_hom_inv_inv_inv_inv (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
    f â— (Î±_ g h i).hom â‰« (Î±_ f g (h â‰« i)).inv â‰« (Î±_ (f â‰« g) h i).inv =
      (Î±_ f (g â‰« h) i).inv â‰« (Î±_ f g h).inv â–· i := by
  simp [â† cancel_epi (f â— (Î±_ g h i).inv)]
#align category_theory.bicategory.pentagon_hom_inv_inv_inv_inv CategoryTheory.Bicategory.pentagon_hom_inv_inv_inv_inv

@[reassoc (attr := simp)]
theorem pentagon_hom_hom_inv_hom_hom (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
    (Î±_ (f â‰« g) h i).hom â‰« (Î±_ f g (h â‰« i)).hom â‰« f â— (Î±_ g h i).inv =
      (Î±_ f g h).hom â–· i â‰« (Î±_ f (g â‰« h) i).hom :=
  eq_of_inv_eq_inv (by simp)
#align category_theory.bicategory.pentagon_hom_hom_inv_hom_hom CategoryTheory.Bicategory.pentagon_hom_hom_inv_hom_hom

@[reassoc (attr := simp)]
theorem pentagon_hom_inv_inv_inv_hom (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
    (Î±_ f g (h â‰« i)).hom â‰« f â— (Î±_ g h i).inv â‰« (Î±_ f (g â‰« h) i).inv =
    (Î±_ (f â‰« g) h i).inv â‰« (Î±_ f g h).hom â–· i := by
  rw [â† cancel_epi (Î±_ f g (h â‰« i)).inv, â† cancel_mono ((Î±_ f g h).inv â–· i)]
  simp
#align category_theory.bicategory.pentagon_hom_inv_inv_inv_hom CategoryTheory.Bicategory.pentagon_hom_inv_inv_inv_hom

@[reassoc (attr := simp)]
theorem pentagon_hom_hom_inv_inv_hom (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
    (Î±_ f (g â‰« h) i).hom â‰« f â— (Î±_ g h i).hom â‰« (Î±_ f g (h â‰« i)).inv =
      (Î±_ f g h).inv â–· i â‰« (Î±_ (f â‰« g) h i).hom :=
  eq_of_inv_eq_inv (by simp)
#align category_theory.bicategory.pentagon_hom_hom_inv_inv_hom CategoryTheory.Bicategory.pentagon_hom_hom_inv_inv_hom

@[reassoc (attr := simp)]
theorem pentagon_inv_hom_hom_hom_hom (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
    (Î±_ f g h).inv â–· i â‰« (Î±_ (f â‰« g) h i).hom â‰« (Î±_ f g (h â‰« i)).hom =
      (Î±_ f (g â‰« h) i).hom â‰« f â— (Î±_ g h i).hom := by
  simp [â† cancel_epi ((Î±_ f g h).hom â–· i)]
#align category_theory.bicategory.pentagon_inv_hom_hom_hom_hom CategoryTheory.Bicategory.pentagon_inv_hom_hom_hom_hom

@[reassoc (attr := simp)]
theorem pentagon_inv_inv_hom_inv_inv (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
    (Î±_ f g (h â‰« i)).inv â‰« (Î±_ (f â‰« g) h i).inv â‰« (Î±_ f g h).hom â–· i =
      f â— (Î±_ g h i).inv â‰« (Î±_ f (g â‰« h) i).inv :=
  eq_of_inv_eq_inv (by simp)
#align category_theory.bicategory.pentagon_inv_inv_hom_inv_inv CategoryTheory.Bicategory.pentagon_inv_inv_hom_inv_inv

theorem triangle_assoc_comp_left (f : a âŸ¶ b) (g : b âŸ¶ c) :
    (Î±_ f (ğŸ™ b) g).hom â‰« f â— (Î»_ g).hom = (Ï_ f).hom â–· g :=
  triangle f g
#align category_theory.bicategory.triangle_assoc_comp_left CategoryTheory.Bicategory.triangle_assoc_comp_left

@[reassoc (attr := simp)]
theorem triangle_assoc_comp_right (f : a âŸ¶ b) (g : b âŸ¶ c) :
    (Î±_ f (ğŸ™ b) g).inv â‰« (Ï_ f).hom â–· g = f â— (Î»_ g).hom := by rw [â† triangle, inv_hom_id_assoc]
#align category_theory.bicategory.triangle_assoc_comp_right CategoryTheory.Bicategory.triangle_assoc_comp_right

@[reassoc (attr := simp)]
theorem triangle_assoc_comp_right_inv (f : a âŸ¶ b) (g : b âŸ¶ c) :
    (Ï_ f).inv â–· g â‰« (Î±_ f (ğŸ™ b) g).hom = f â— (Î»_ g).inv := by
  simp [â† cancel_mono (f â— (Î»_ g).hom)]
#align category_theory.bicategory.triangle_assoc_comp_right_inv CategoryTheory.Bicategory.triangle_assoc_comp_right_inv

@[reassoc (attr := simp)]
theorem triangle_assoc_comp_left_inv (f : a âŸ¶ b) (g : b âŸ¶ c) :
    f â— (Î»_ g).inv â‰« (Î±_ f (ğŸ™ b) g).inv = (Ï_ f).inv â–· g := by
  simp [â† cancel_mono ((Ï_ f).hom â–· g)]
#align category_theory.bicategory.triangle_assoc_comp_left_inv CategoryTheory.Bicategory.triangle_assoc_comp_left_inv

@[reassoc]
theorem associator_naturality_left {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') (g : b âŸ¶ c) (h : c âŸ¶ d) :
    Î· â–· g â–· h â‰« (Î±_ f' g h).hom = (Î±_ f g h).hom â‰« Î· â–· (g â‰« h) := by simp
#align category_theory.bicategory.associator_naturality_left CategoryTheory.Bicategory.associator_naturality_left

@[reassoc]
theorem associator_inv_naturality_left {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') (g : b âŸ¶ c) (h : c âŸ¶ d) :
    Î· â–· (g â‰« h) â‰« (Î±_ f' g h).inv = (Î±_ f g h).inv â‰« Î· â–· g â–· h := by simp
#align category_theory.bicategory.associator_inv_naturality_left CategoryTheory.Bicategory.associator_inv_naturality_left

@[reassoc]
theorem whiskerRight_comp_symm {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') (g : b âŸ¶ c) (h : c âŸ¶ d) :
    Î· â–· g â–· h = (Î±_ f g h).hom â‰« Î· â–· (g â‰« h) â‰« (Î±_ f' g h).inv := by simp
#align category_theory.bicategory.whisker_right_comp_symm CategoryTheory.Bicategory.whiskerRight_comp_symm

@[reassoc]
theorem associator_naturality_middle (f : a âŸ¶ b) {g g' : b âŸ¶ c} (Î· : g âŸ¶ g') (h : c âŸ¶ d) :
    (f â— Î·) â–· h â‰« (Î±_ f g' h).hom = (Î±_ f g h).hom â‰« f â— Î· â–· h := by simp
#align category_theory.bicategory.associator_naturality_middle CategoryTheory.Bicategory.associator_naturality_middle

@[reassoc]
theorem associator_inv_naturality_middle (f : a âŸ¶ b) {g g' : b âŸ¶ c} (Î· : g âŸ¶ g') (h : c âŸ¶ d) :
    f â— Î· â–· h â‰« (Î±_ f g' h).inv = (Î±_ f g h).inv â‰« (f â— Î·) â–· h := by simp
#align category_theory.bicategory.associator_inv_naturality_middle CategoryTheory.Bicategory.associator_inv_naturality_middle

@[reassoc]
theorem whisker_assoc_symm (f : a âŸ¶ b) {g g' : b âŸ¶ c} (Î· : g âŸ¶ g') (h : c âŸ¶ d) :
    f â— Î· â–· h = (Î±_ f g h).inv â‰« (f â— Î·) â–· h â‰« (Î±_ f g' h).hom := by simp
#align category_theory.bicategory.whisker_assoc_symm CategoryTheory.Bicategory.whisker_assoc_symm

@[reassoc]
theorem associator_naturality_right (f : a âŸ¶ b) (g : b âŸ¶ c) {h h' : c âŸ¶ d} (Î· : h âŸ¶ h') :
    (f â‰« g) â— Î· â‰« (Î±_ f g h').hom = (Î±_ f g h).hom â‰« f â— g â— Î· := by simp
#align category_theory.bicategory.associator_naturality_right CategoryTheory.Bicategory.associator_naturality_right

@[reassoc]
theorem associator_inv_naturality_right (f : a âŸ¶ b) (g : b âŸ¶ c) {h h' : c âŸ¶ d} (Î· : h âŸ¶ h') :
    f â— g â— Î· â‰« (Î±_ f g h').inv = (Î±_ f g h).inv â‰« (f â‰« g) â— Î· := by simp
#align category_theory.bicategory.associator_inv_naturality_right CategoryTheory.Bicategory.associator_inv_naturality_right

@[reassoc]
theorem comp_whiskerLeft_symm (f : a âŸ¶ b) (g : b âŸ¶ c) {h h' : c âŸ¶ d} (Î· : h âŸ¶ h') :
    f â— g â— Î· = (Î±_ f g h).inv â‰« (f â‰« g) â— Î· â‰« (Î±_ f g h').hom := by simp
#align category_theory.bicategory.comp_whisker_left_symm CategoryTheory.Bicategory.comp_whiskerLeft_symm

@[reassoc]
theorem leftUnitor_naturality {f g : a âŸ¶ b} (Î· : f âŸ¶ g) :
    ğŸ™ a â— Î· â‰« (Î»_ g).hom = (Î»_ f).hom â‰« Î· := by
  simp
#align category_theory.bicategory.left_unitor_naturality CategoryTheory.Bicategory.leftUnitor_naturality

@[reassoc]
theorem leftUnitor_inv_naturality {f g : a âŸ¶ b} (Î· : f âŸ¶ g) :
    Î· â‰« (Î»_ g).inv = (Î»_ f).inv â‰« ğŸ™ a â— Î· := by simp
#align category_theory.bicategory.left_unitor_inv_naturality CategoryTheory.Bicategory.leftUnitor_inv_naturality

theorem id_whiskerLeft_symm {f g : a âŸ¶ b} (Î· : f âŸ¶ g) : Î· = (Î»_ f).inv â‰« ğŸ™ a â— Î· â‰« (Î»_ g).hom := by
  simp
#align category_theory.bicategory.id_whisker_left_symm CategoryTheory.Bicategory.id_whiskerLeft_symm

@[reassoc]
theorem rightUnitor_naturality {f g : a âŸ¶ b} (Î· : f âŸ¶ g) :
    Î· â–· ğŸ™ b â‰« (Ï_ g).hom = (Ï_ f).hom â‰« Î· := by simp
#align category_theory.bicategory.right_unitor_naturality CategoryTheory.Bicategory.rightUnitor_naturality

@[reassoc]
theorem rightUnitor_inv_naturality {f g : a âŸ¶ b} (Î· : f âŸ¶ g) :
    Î· â‰« (Ï_ g).inv = (Ï_ f).inv â‰« Î· â–· ğŸ™ b := by simp
#align category_theory.bicategory.right_unitor_inv_naturality CategoryTheory.Bicategory.rightUnitor_inv_naturality

theorem whiskerRight_id_symm {f g : a âŸ¶ b} (Î· : f âŸ¶ g) : Î· = (Ï_ f).inv â‰« Î· â–· ğŸ™ b â‰« (Ï_ g).hom := by
  simp
#align category_theory.bicategory.whisker_right_id_symm CategoryTheory.Bicategory.whiskerRight_id_symm

theorem whiskerLeft_iff {f g : a âŸ¶ b} (Î· Î¸ : f âŸ¶ g) : ğŸ™ a â— Î· = ğŸ™ a â— Î¸ â†” Î· = Î¸ := by simp
#align category_theory.bicategory.whisker_left_iff CategoryTheory.Bicategory.whiskerLeft_iff

theorem whiskerRight_iff {f g : a âŸ¶ b} (Î· Î¸ : f âŸ¶ g) : Î· â–· ğŸ™ b = Î¸ â–· ğŸ™ b â†” Î· = Î¸ := by simp
#align category_theory.bicategory.whisker_right_iff CategoryTheory.Bicategory.whiskerRight_iff

/-- We state it as a simp lemma, which is regarded as an involved version of
`id_whiskerRight f g : ğŸ™ f â–· g = ğŸ™ (f â‰« g)`.
-/
@[reassoc, simp]
theorem leftUnitor_whiskerRight (f : a âŸ¶ b) (g : b âŸ¶ c) :
    (Î»_ f).hom â–· g = (Î±_ (ğŸ™ a) f g).hom â‰« (Î»_ (f â‰« g)).hom := by
  rw [â† whiskerLeft_iff, whiskerLeft_comp, â† cancel_epi (Î±_ _ _ _).hom, â†
      cancel_epi ((Î±_ _ _ _).hom â–· _), pentagon_assoc, triangle, â† associator_naturality_middle, â†
      comp_whiskerRight_assoc, triangle, associator_naturality_left]
#align category_theory.bicategory.left_unitor_whisker_right CategoryTheory.Bicategory.leftUnitor_whiskerRight

@[reassoc, simp]
theorem leftUnitor_inv_whiskerRight (f : a âŸ¶ b) (g : b âŸ¶ c) :
    (Î»_ f).inv â–· g = (Î»_ (f â‰« g)).inv â‰« (Î±_ (ğŸ™ a) f g).inv :=
  eq_of_inv_eq_inv (by simp)
#align category_theory.bicategory.left_unitor_inv_whisker_right CategoryTheory.Bicategory.leftUnitor_inv_whiskerRight

@[reassoc, simp]
theorem whiskerLeft_rightUnitor (f : a âŸ¶ b) (g : b âŸ¶ c) :
    f â— (Ï_ g).hom = (Î±_ f g (ğŸ™ c)).inv â‰« (Ï_ (f â‰« g)).hom := by
  rw [â† whiskerRight_iff, comp_whiskerRight, â† cancel_epi (Î±_ _ _ _).inv, â†
      cancel_epi (f â— (Î±_ _ _ _).inv), pentagon_inv_assoc, triangle_assoc_comp_right, â†
      associator_inv_naturality_middle, â† whiskerLeft_comp_assoc, triangle_assoc_comp_right,
      associator_inv_naturality_right]
#align category_theory.bicategory.whisker_left_right_unitor CategoryTheory.Bicategory.whiskerLeft_rightUnitor

@[reassoc, simp]
theorem whiskerLeft_rightUnitor_inv (f : a âŸ¶ b) (g : b âŸ¶ c) :
    f â— (Ï_ g).inv = (Ï_ (f â‰« g)).inv â‰« (Î±_ f g (ğŸ™ c)).hom :=
  eq_of_inv_eq_inv (by simp)
#align category_theory.bicategory.whisker_left_right_unitor_inv CategoryTheory.Bicategory.whiskerLeft_rightUnitor_inv

/-
It is not so obvious whether `leftUnitor_whiskerRight` or `leftUnitor_comp` should be a simp
lemma. Our choice is the former. One reason is that the latter yields the following loop:
[id_whiskerLeft]   : ğŸ™ a â— (Ï_ f).hom ==> (Î»_ (f â‰« ğŸ™ b)).hom â‰« (Ï_ f).hom â‰« (Î»_ f).inv
[leftUnitor_comp]  : (Î»_ (f â‰« ğŸ™ b)).hom ==> (Î±_ (ğŸ™ a) f (ğŸ™ b)).inv â‰« (Î»_ f).hom â–· ğŸ™ b
[whiskerRight_id]  : (Î»_ f).hom â–· ğŸ™ b ==> (Ï_ (ğŸ™ a â‰« f)).hom â‰« (Î»_ f).hom â‰« (Ï_ f).inv
[rightUnitor_comp] : (Ï_ (ğŸ™ a â‰« f)).hom ==> (Î±_ (ğŸ™ a) f (ğŸ™ b)).hom â‰« ğŸ™ a â— (Ï_ f).hom
-/
@[reassoc]
theorem leftUnitor_comp (f : a âŸ¶ b) (g : b âŸ¶ c) :
    (Î»_ (f â‰« g)).hom = (Î±_ (ğŸ™ a) f g).inv â‰« (Î»_ f).hom â–· g := by simp
#align category_theory.bicategory.left_unitor_comp CategoryTheory.Bicategory.leftUnitor_comp

@[reassoc]
theorem leftUnitor_comp_inv (f : a âŸ¶ b) (g : b âŸ¶ c) :
    (Î»_ (f â‰« g)).inv = (Î»_ f).inv â–· g â‰« (Î±_ (ğŸ™ a) f g).hom := by simp
#align category_theory.bicategory.left_unitor_comp_inv CategoryTheory.Bicategory.leftUnitor_comp_inv

@[reassoc]
theorem rightUnitor_comp (f : a âŸ¶ b) (g : b âŸ¶ c) :
    (Ï_ (f â‰« g)).hom = (Î±_ f g (ğŸ™ c)).hom â‰« f â— (Ï_ g).hom := by simp
#align category_theory.bicategory.right_unitor_comp CategoryTheory.Bicategory.rightUnitor_comp

@[reassoc]
theorem rightUnitor_comp_inv (f : a âŸ¶ b) (g : b âŸ¶ c) :
    (Ï_ (f â‰« g)).inv = f â— (Ï_ g).inv â‰« (Î±_ f g (ğŸ™ c)).inv := by simp
#align category_theory.bicategory.right_unitor_comp_inv CategoryTheory.Bicategory.rightUnitor_comp_inv

@[simp]
theorem unitors_equal : (Î»_ (ğŸ™ a)).hom = (Ï_ (ğŸ™ a)).hom := by
  rw [â† whiskerLeft_iff, â† cancel_epi (Î±_ _ _ _).hom, â† cancel_mono (Ï_ _).hom, triangle, â†
      rightUnitor_comp, rightUnitor_naturality]
#align category_theory.bicategory.unitors_equal CategoryTheory.Bicategory.unitors_equal

@[simp]
theorem unitors_inv_equal : (Î»_ (ğŸ™ a)).inv = (Ï_ (ğŸ™ a)).inv := by simp [Iso.inv_eq_inv]
#align category_theory.bicategory.unitors_inv_equal CategoryTheory.Bicategory.unitors_inv_equal

section

attribute [local simp] whisker_exchange

/-- Precomposition of a 1-morphism as a functor. -/
@[simps]
def precomp (c : B) (f : a âŸ¶ b) : (b âŸ¶ c) â¥¤ (a âŸ¶ c) where
  obj := (f â‰« Â·)
  map := (f â— Â·)

/-- Precomposition of a 1-morphism as a functor from the category of 1-morphisms `a âŸ¶ b` into the
category of functors `(b âŸ¶ c) â¥¤ (a âŸ¶ c)`. -/
@[simps]
def precomposing (a b c : B) : (a âŸ¶ b) â¥¤ (b âŸ¶ c) â¥¤ (a âŸ¶ c) where
  obj f := precomp c f
  map Î· := âŸ¨(Î· â–· Â·), _âŸ©

/-- Postcomposition of a 1-morphism as a functor. -/
@[simps]
def postcomp (a : B) (f : b âŸ¶ c) : (a âŸ¶ b) â¥¤ (a âŸ¶ c) where
  obj := (Â· â‰« f)
  map := (Â· â–· f)

/-- Postcomposition of a 1-morphism as a functor from the category of 1-morphisms `b âŸ¶ c` into the
category of functors `(a âŸ¶ b) â¥¤ (a âŸ¶ c)`. -/
@[simps]
def postcomposing (a b c : B) : (b âŸ¶ c) â¥¤ (a âŸ¶ b) â¥¤ (a âŸ¶ c) where
  obj f := postcomp a f
  map Î· := âŸ¨(Â· â— Î·), _âŸ©

/-- Left component of the associator as a natural isomorphism. -/
@[simps!]
def associatorNatIsoLeft (a : B) (g : b âŸ¶ c) (h : c âŸ¶ d) :
    (postcomposing a ..).obj g â‹™ (postcomposing ..).obj h â‰… (postcomposing ..).obj (g â‰« h) :=
  NatIso.ofComponents (Î±_ Â· g h)

/-- Middle component of the associator as a natural isomorphism. -/
@[simps!]
def associatorNatIsoMiddle (f : a âŸ¶ b) (h : c âŸ¶ d) :
    (precomposing ..).obj f â‹™ (postcomposing ..).obj h â‰…
      (postcomposing ..).obj h â‹™ (precomposing ..).obj f :=
  NatIso.ofComponents (Î±_ f Â· h)

/-- Right component of the associator as a natural isomorphism. -/
@[simps!]
def associatorNatIsoRight (f : a âŸ¶ b) (g : b âŸ¶ c) (d : B) :
    (precomposing _ _ d).obj (f â‰« g) â‰… (precomposing ..).obj g â‹™ (precomposing ..).obj f :=
  NatIso.ofComponents (Î±_ f g Â·)

/-- Left unitor as a natural isomorphism. -/
@[simps!]
def leftUnitorNatIso (a b : B) : (precomposing _ _ b).obj (ğŸ™ a) â‰… ğŸ­ (a âŸ¶ b) :=
  NatIso.ofComponents (Î»_ Â·)

/-- Right unitor as a natural isomorphism. -/
@[simps!]
def rightUnitorNatIso (a b : B) : (postcomposing a _ _).obj (ğŸ™ b) â‰… ğŸ­ (a âŸ¶ b) :=
  NatIso.ofComponents (Ï_ Â·)

end

end Bicategory

end CategoryTheory
