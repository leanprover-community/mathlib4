/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou, Christian Merten
-/
import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor
import Mathlib.CategoryTheory.CommSq

/-!
# Pseudofunctors from strict bicategory

This file provides an API for pseudofunctors `F` from a strict bicategory `B`. In
particular, this shall apply to pseudofunctors from locally discrete bicategories.

Firstly, we study the compatibilities of the flexible variants `mapId'` and `mapComp'`
of `mapId` and `mapComp` with respect to the composition with identities and the
associativity.

Secondly, given a commutative square `t ‚â´ r = l ‚â´ b` in `B`, we construct an
isomorphism `F.map t ‚â´ F.map r ‚âÖ F.map l ‚â´ F.map b`
(see `Pseudofunctor.isoMapOfCommSq`).

-/

namespace CategoryTheory

universe w‚ÇÅ w‚ÇÇ v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

open Bicategory

namespace Pseudofunctor

variable {B : Type u‚ÇÅ} {C : Type u‚ÇÇ} [Bicategory.{w‚ÇÅ, v‚ÇÅ} B]
  [Strict B] [Bicategory.{w‚ÇÇ, v‚ÇÇ} C] (F : Pseudofunctor B C)

lemma mapComp'_comp_id {b‚ÇÄ b‚ÇÅ : B} (f : b‚ÇÄ ‚ü∂ b‚ÇÅ) :
    F.mapComp' f (ùüô b‚ÇÅ) f = (œÅ_ _).symm ‚â™‚â´ whiskerLeftIso _ (F.mapId b‚ÇÅ).symm := by
  ext
  rw [mapComp']
  dsimp
  rw [F.mapComp_id_right_hom f, Strict.rightUnitor_eqToIso, eqToIso.hom,
    ‚Üê F.map‚ÇÇ_comp_assoc, eqToHom_trans, eqToHom_refl, PrelaxFunctor.map‚ÇÇ_id,
    Category.id_comp]

lemma mapComp'_id_comp {b‚ÇÄ b‚ÇÅ : B} (f : b‚ÇÄ ‚ü∂ b‚ÇÅ) :
    F.mapComp' (ùüô b‚ÇÄ) f f = (Œª_ _).symm ‚â™‚â´ whiskerRightIso (F.mapId b‚ÇÄ).symm _ := by
  ext
  rw [mapComp']
  dsimp
  rw [F.mapComp_id_left_hom f, Strict.leftUnitor_eqToIso, eqToIso.hom,
    ‚Üê F.map‚ÇÇ_comp_assoc, eqToHom_trans, eqToHom_refl, PrelaxFunctor.map‚ÇÇ_id,
    Category.id_comp]

section associativity

variable {b‚ÇÄ b‚ÇÅ b‚ÇÇ b‚ÇÉ : B} (f‚ÇÄ‚ÇÅ : b‚ÇÄ ‚ü∂ b‚ÇÅ)
  (f‚ÇÅ‚ÇÇ : b‚ÇÅ ‚ü∂ b‚ÇÇ) (f‚ÇÇ‚ÇÉ : b‚ÇÇ ‚ü∂ b‚ÇÉ) (f‚ÇÄ‚ÇÇ : b‚ÇÄ ‚ü∂ b‚ÇÇ) (f‚ÇÅ‚ÇÉ : b‚ÇÅ ‚ü∂ b‚ÇÉ) (f : b‚ÇÄ ‚ü∂ b‚ÇÉ)
  (h‚ÇÄ‚ÇÇ : f‚ÇÄ‚ÇÅ ‚â´ f‚ÇÅ‚ÇÇ = f‚ÇÄ‚ÇÇ) (h‚ÇÅ‚ÇÉ : f‚ÇÅ‚ÇÇ ‚â´ f‚ÇÇ‚ÇÉ = f‚ÇÅ‚ÇÉ)

@[reassoc]
lemma mapComp'_hom_comp_whiskerLeft_mapComp'_hom (hf : f‚ÇÄ‚ÇÅ ‚â´ f‚ÇÅ‚ÇÉ = f) :
    (F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÉ f).hom ‚â´ F.map f‚ÇÄ‚ÇÅ ‚óÅ (F.mapComp' f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÅ‚ÇÉ h‚ÇÅ‚ÇÉ).hom =
    (F.mapComp' f‚ÇÄ‚ÇÇ f‚ÇÇ‚ÇÉ f).hom ‚â´
      (F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÄ‚ÇÇ h‚ÇÄ‚ÇÇ).hom ‚ñ∑ F.map f‚ÇÇ‚ÇÉ ‚â´ (Œ±_ _ _ _).hom := by
  subst h‚ÇÄ‚ÇÇ h‚ÇÅ‚ÇÉ hf
  simp [mapComp_assoc_right_hom, Strict.associator_eqToIso, mapComp']

@[reassoc]
lemma mapComp'_inv_comp_mapComp'_hom (hf : f‚ÇÄ‚ÇÅ ‚â´ f‚ÇÅ‚ÇÉ = f) :
    (F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÉ f).inv ‚â´ (F.mapComp' f‚ÇÄ‚ÇÇ f‚ÇÇ‚ÇÉ f).hom =
    F.map f‚ÇÄ‚ÇÅ ‚óÅ (F.mapComp' f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÅ‚ÇÉ h‚ÇÅ‚ÇÉ).hom ‚â´
      (Œ±_ _ _ _).inv ‚â´ (F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÄ‚ÇÇ h‚ÇÄ‚ÇÇ).inv ‚ñ∑ F.map f‚ÇÇ‚ÇÉ := by
  rw [‚Üê cancel_epi (F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÉ f hf).hom, Iso.hom_inv_id_assoc,
    F.mapComp'_hom_comp_whiskerLeft_mapComp'_hom_assoc _ _ _ _ _ _ h‚ÇÄ‚ÇÇ h‚ÇÅ‚ÇÉ hf]
  simp

@[reassoc]
lemma whiskerLeft_mapComp'_inv_comp_mapComp'_inv (hf : f‚ÇÄ‚ÇÅ ‚â´ f‚ÇÅ‚ÇÉ = f) :
    F.map f‚ÇÄ‚ÇÅ ‚óÅ (F.mapComp' f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÅ‚ÇÉ h‚ÇÅ‚ÇÉ).inv ‚â´ (F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÉ f hf).inv =
    (Œ±_ _ _ _).inv ‚â´ (F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÄ‚ÇÇ h‚ÇÄ‚ÇÇ).inv ‚ñ∑ F.map f‚ÇÇ‚ÇÉ ‚â´
      (F.mapComp' f‚ÇÄ‚ÇÇ f‚ÇÇ‚ÇÉ f).inv := by
  simp [‚Üê cancel_mono (F.mapComp' f‚ÇÄ‚ÇÇ f‚ÇÇ‚ÇÉ f).hom,
    F.mapComp'_inv_comp_mapComp'_hom _ _ _ _ _ _ h‚ÇÄ‚ÇÇ h‚ÇÅ‚ÇÉ hf]

@[reassoc]
lemma mapComp'_hom_comp_mapComp'_hom_whiskerRight (hf : f‚ÇÄ‚ÇÇ ‚â´ f‚ÇÇ‚ÇÉ = f) :
    (F.mapComp' f‚ÇÄ‚ÇÇ f‚ÇÇ‚ÇÉ f).hom ‚â´ (F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÄ‚ÇÇ h‚ÇÄ‚ÇÇ).hom ‚ñ∑ F.map f‚ÇÇ‚ÇÉ =
    (F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÉ f).hom ‚â´ F.map f‚ÇÄ‚ÇÅ ‚óÅ (F.mapComp' f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÅ‚ÇÉ h‚ÇÅ‚ÇÉ).hom ‚â´
      (Œ±_ _ _ _).inv := by
  rw [F.mapComp'_hom_comp_whiskerLeft_mapComp'_hom_assoc _ _ _ _ _ f h‚ÇÄ‚ÇÇ h‚ÇÅ‚ÇÉ (by cat_disch)]
  simp

@[reassoc]
lemma mapComp'_inv_whiskerRight_comp_mapComp'_inv (hf : f‚ÇÄ‚ÇÇ ‚â´ f‚ÇÇ‚ÇÉ = f) :
    (F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÄ‚ÇÇ h‚ÇÄ‚ÇÇ).inv ‚ñ∑ F.map f‚ÇÇ‚ÇÉ ‚â´ (F.mapComp' f‚ÇÄ‚ÇÇ f‚ÇÇ‚ÇÉ f).inv =
    (Œ±_ _ _ _).hom ‚â´ F.map f‚ÇÄ‚ÇÅ ‚óÅ (F.mapComp' f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÅ‚ÇÉ h‚ÇÅ‚ÇÉ).inv ‚â´
      (F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÉ f).inv := by
  rw [whiskerLeft_mapComp'_inv_comp_mapComp'_inv _ _ _ _ _ _ f h‚ÇÄ‚ÇÇ h‚ÇÅ‚ÇÉ,
    Iso.hom_inv_id_assoc]

end associativity

section CommSq

variable {X‚ÇÅ X‚ÇÇ Y‚ÇÅ Y‚ÇÇ Z‚ÇÅ Z‚ÇÇ : B}

section

variable {t : X‚ÇÅ ‚ü∂ Y‚ÇÅ} {l : X‚ÇÅ ‚ü∂ X‚ÇÇ} {r : Y‚ÇÅ ‚ü∂ Y‚ÇÇ} {b : X‚ÇÇ ‚ü∂ Y‚ÇÇ} (sq : CommSq t l r b)

/-- Given a commutative square `CommSq t l r b` in a strict bicategory `B` and
a pseudofunctor from `B`, this is the natural isomorphism
`F.map t ‚â´ F.map r ‚âÖ F.map l ‚â´ F.map b`. -/
def isoMapOfCommSq : F.map t ‚â´ F.map r ‚âÖ F.map l ‚â´ F.map b :=
  (F.mapComp t r).symm ‚â™‚â´ F.mapComp' _ _ _ (by rw [sq.w])

lemma isoMapOfCommSq_eq (œÜ : X‚ÇÅ ‚ü∂ Y‚ÇÇ) (hœÜ : t ‚â´ r = œÜ) :
    F.isoMapOfCommSq sq = (F.mapComp' t r œÜ (by rw [hœÜ])).symm ‚â™‚â´
      F.mapComp' l b œÜ (by rw [‚Üê hœÜ, sq.w]) := by
  subst hœÜ
  simp [isoMapOfCommSq, mapComp'_eq_mapComp]

end

/-- Equational lemma for `Pseudofunctor.isoMapOfCommSq` when
both vertical maps of the square are the same and horizontal maps are identities. -/
lemma isoMapOfCommSq_horiz_id (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) :
    F.isoMapOfCommSq (t := ùüô _) (l := f) (r := f) (b := ùüô _) ‚ü®by simp‚ü© =
      whiskerRightIso (F.mapId X‚ÇÅ) (F.map f) ‚â™‚â´ Œª_ _ ‚â™‚â´ (œÅ_ _).symm ‚â™‚â´
        (whiskerLeftIso (F.map f) (F.mapId X‚ÇÇ)).symm := by
  ext
  rw [isoMapOfCommSq_eq _ _ f (by simp), mapComp'_comp_id, mapComp'_id_comp]
  simp

/-- Equational lemma for `Pseudofunctor.isoMapOfCommSq` when
both horizontal maps of the square are the same and vertical maps are identities. -/
lemma isoMapOfCommSq_vert_id (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) :
    F.isoMapOfCommSq (t := f) (l := ùüô _) (r := ùüô _) (b := f) ‚ü®by simp‚ü© =
      whiskerLeftIso (F.map f) (F.mapId X‚ÇÇ) ‚â™‚â´ œÅ_ _ ‚â™‚â´ (Œª_ _).symm ‚â™‚â´
        (whiskerRightIso (F.mapId X‚ÇÅ) (F.map f)).symm := by
  ext
  rw [isoMapOfCommSq_eq _ _ f (by simp), mapComp'_comp_id, mapComp'_id_comp]
  simp

end CommSq

end Pseudofunctor

namespace LaxFunctor

variable {B : Type u‚ÇÅ} {C : Type u‚ÇÇ} [Bicategory.{w‚ÇÅ, v‚ÇÅ} B]
  [Strict B] [Bicategory.{w‚ÇÇ, v‚ÇÇ} C] (F : LaxFunctor B C)

section associativity

variable {b‚ÇÄ b‚ÇÅ b‚ÇÇ b‚ÇÉ : B} (f‚ÇÄ‚ÇÅ : b‚ÇÄ ‚ü∂ b‚ÇÅ)
  (f‚ÇÅ‚ÇÇ : b‚ÇÅ ‚ü∂ b‚ÇÇ) (f‚ÇÇ‚ÇÉ : b‚ÇÇ ‚ü∂ b‚ÇÉ) (f‚ÇÄ‚ÇÇ : b‚ÇÄ ‚ü∂ b‚ÇÇ) (f‚ÇÅ‚ÇÉ : b‚ÇÅ ‚ü∂ b‚ÇÉ) (f : b‚ÇÄ ‚ü∂ b‚ÇÉ)
  (h‚ÇÄ‚ÇÇ : f‚ÇÄ‚ÇÅ ‚â´ f‚ÇÅ‚ÇÇ = f‚ÇÄ‚ÇÇ) (h‚ÇÅ‚ÇÉ : f‚ÇÅ‚ÇÇ ‚â´ f‚ÇÇ‚ÇÉ = f‚ÇÅ‚ÇÉ)

@[reassoc]
lemma whiskerLeft_mapComp'_comp_mapComp' (hf : f‚ÇÄ‚ÇÅ ‚â´ f‚ÇÅ‚ÇÉ = f) :
    F.map f‚ÇÄ‚ÇÅ ‚óÅ F.mapComp' f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÅ‚ÇÉ h‚ÇÅ‚ÇÉ ‚â´ F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÉ f hf =
    (Œ±_ _ _ _).inv ‚â´ F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÄ‚ÇÇ h‚ÇÄ‚ÇÇ ‚ñ∑ F.map f‚ÇÇ‚ÇÉ ‚â´
      F.mapComp' f‚ÇÄ‚ÇÇ f‚ÇÇ‚ÇÉ f := by
  subst hf h‚ÇÄ‚ÇÇ h‚ÇÅ‚ÇÉ
  have := F.map‚ÇÇ_associator f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ
  simp only [Strict.associator_eqToIso, eqToIso.hom] at this
  simp [LaxFunctor.mapComp', this]

@[reassoc]
lemma mapComp'_whiskerRight_comp_mapComp' (hf : f‚ÇÄ‚ÇÇ ‚â´ f‚ÇÇ‚ÇÉ = f) :
    F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÄ‚ÇÇ h‚ÇÄ‚ÇÇ ‚ñ∑ F.map f‚ÇÇ‚ÇÉ ‚â´ F.mapComp' f‚ÇÄ‚ÇÇ f‚ÇÇ‚ÇÉ f =
    (Œ±_ _ _ _).hom ‚â´ F.map f‚ÇÄ‚ÇÅ ‚óÅ F.mapComp' f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÅ‚ÇÉ h‚ÇÅ‚ÇÉ ‚â´
      F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÉ f := by
  rw [whiskerLeft_mapComp'_comp_mapComp' _ _ _ _ _ _ f h‚ÇÄ‚ÇÇ h‚ÇÅ‚ÇÉ,
    Iso.hom_inv_id_assoc]

end associativity

end LaxFunctor

namespace OplaxFunctor

variable {B : Type u‚ÇÅ} {C : Type u‚ÇÇ} [Bicategory.{w‚ÇÅ, v‚ÇÅ} B]
  [Strict B] [Bicategory.{w‚ÇÇ, v‚ÇÇ} C] (F : OplaxFunctor B C)

section associativity

variable {b‚ÇÄ b‚ÇÅ b‚ÇÇ b‚ÇÉ : B} (f‚ÇÄ‚ÇÅ : b‚ÇÄ ‚ü∂ b‚ÇÅ)
  (f‚ÇÅ‚ÇÇ : b‚ÇÅ ‚ü∂ b‚ÇÇ) (f‚ÇÇ‚ÇÉ : b‚ÇÇ ‚ü∂ b‚ÇÉ) (f‚ÇÄ‚ÇÇ : b‚ÇÄ ‚ü∂ b‚ÇÇ) (f‚ÇÅ‚ÇÉ : b‚ÇÅ ‚ü∂ b‚ÇÉ) (f : b‚ÇÄ ‚ü∂ b‚ÇÉ)
  (h‚ÇÄ‚ÇÇ : f‚ÇÄ‚ÇÅ ‚â´ f‚ÇÅ‚ÇÇ = f‚ÇÄ‚ÇÇ) (h‚ÇÅ‚ÇÉ : f‚ÇÅ‚ÇÇ ‚â´ f‚ÇÇ‚ÇÉ = f‚ÇÅ‚ÇÉ)

@[reassoc]
lemma mapComp'_comp_whiskerLeft_mapComp' (hf : f‚ÇÄ‚ÇÅ ‚â´ f‚ÇÅ‚ÇÉ = f) :
    F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÉ f ‚â´ F.map f‚ÇÄ‚ÇÅ ‚óÅ F.mapComp' f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÅ‚ÇÉ h‚ÇÅ‚ÇÉ =
    F.mapComp' f‚ÇÄ‚ÇÇ f‚ÇÇ‚ÇÉ f ‚â´
      F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÄ‚ÇÇ h‚ÇÄ‚ÇÇ ‚ñ∑ F.map f‚ÇÇ‚ÇÉ ‚â´ (Œ±_ _ _ _).hom := by
  subst h‚ÇÄ‚ÇÇ h‚ÇÅ‚ÇÉ hf
  have := F.map‚ÇÇ_associator f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ
  simp only [Strict.associator_eqToIso, eqToIso.hom] at this
  simp [OplaxFunctor.mapComp', ‚Üê this, PrelaxFunctor.map‚ÇÇ_eqToHom]


@[reassoc]
lemma mapComp'_comp_mapComp'_whiskerRight (hf : f‚ÇÄ‚ÇÇ ‚â´ f‚ÇÇ‚ÇÉ = f) :
    F.mapComp' f‚ÇÄ‚ÇÇ f‚ÇÇ‚ÇÉ f ‚â´ F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÄ‚ÇÇ h‚ÇÄ‚ÇÇ ‚ñ∑ F.map f‚ÇÇ‚ÇÉ =
    F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÉ f ‚â´ F.map f‚ÇÄ‚ÇÅ ‚óÅ F.mapComp' f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÅ‚ÇÉ h‚ÇÅ‚ÇÉ ‚â´
      (Œ±_ _ _ _).inv := by
  rw [F.mapComp'_comp_whiskerLeft_mapComp'_assoc _ _ _ _ _ f h‚ÇÄ‚ÇÇ h‚ÇÅ‚ÇÉ (by cat_disch)]
  simp

end associativity

end OplaxFunctor

end CategoryTheory
