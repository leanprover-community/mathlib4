/-
Copyright (c) 2025 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno, JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Bicategory.Adjunction.Basic
import Mathlib.CategoryTheory.HomCongr

/-!
# Mates in bicategories

This file establishes the bijection between the 2-cells

```
         lâ‚                  râ‚
      c --â†’ d             c â†-- d
    g â†“  â†—  â†“ h         g â†“  â†˜  â†“ h
      e --â†’ f             e â†-- f
         lâ‚‚                  râ‚‚
```

where `lâ‚ âŠ£ râ‚` and `lâ‚‚ âŠ£ râ‚‚`. The corresponding natural transformations are called mates.

For the bicategory `Cat`, the definitions in this file are provided in
`Mathlib/CategoryTheory/Adjunction/Mates.lean`, where you can find more detailed documentation
about mates.


## Implementation

The correspondence between mates is obtained by combining
bijections of the form `(g âŸ¶ l â‰« h) â‰ƒ (r â‰« g âŸ¶ h)`
and `(g â‰« l âŸ¶ h) â‰ƒ (g âŸ¶ h â‰« r)` when `l âŠ£ r` is an adjunction.
Indeed, `g â‰« lâ‚‚ âŸ¶ lâ‚ â‰« h` identifies to `g âŸ¶ (lâ‚ â‰« h) â‰« râ‚‚` by using the
second bijection applied to `lâ‚‚ âŠ£ râ‚‚`, and this identifies to `râ‚ â‰« g âŸ¶ h â‰« râ‚‚`
by using the first bijection applied to `lâ‚ âŠ£ râ‚`.

## Remarks

To be precise, the definitions in `Mathlib/CategoryTheory/Adjunction/Mates.lean` are universe
polymorphic, so they are not simple specializations of the definitions in this file.

-/

universe w v u

namespace CategoryTheory

namespace Bicategory

open Bicategory

variable {B : Type u} [Bicategory.{w, v} B]

namespace Adjunction

variable {a b c d : B} {l : b âŸ¶ c} {r : c âŸ¶ b} (adj : l âŠ£ r)

/-- The bijection `(g âŸ¶ l â‰« h) â‰ƒ (r â‰« g âŸ¶ h)` induced by an adjunction
`l âŠ£ r` in a bicategory. -/
@[simps -isSimp]
def homEquivâ‚ {g : b âŸ¶ d} {h : c âŸ¶ d} : (g âŸ¶ l â‰« h) â‰ƒ (r â‰« g âŸ¶ h) where
  toFun Î³ := r â— Î³ â‰« (Î±_ _ _ _).inv â‰« adj.counit â–· h â‰« (Î»_ _).hom
  invFun Î² := (Î»_ _).inv â‰« adj.unit â–· _ â‰« (Î±_ _ _ _).hom â‰« l â— Î²
  left_inv Î³ :=
    calc
      _ = ğŸ™ _ âŠ—â‰« (adj.unit â–· g â‰« (l â‰« r) â— Î³) âŠ—â‰« l â— adj.counit â–· h âŠ—â‰« ğŸ™ _:= by
        bicategory
      _ = Î³ âŠ—â‰« leftZigzag adj.unit adj.counit â–· h âŠ—â‰« ğŸ™ _ := by
        rw [â† whisker_exchange]
        bicategory
      _ = Î³ := by
        rw [adj.left_triangle]
        bicategory
  right_inv Î² := by
    calc
      _ = ğŸ™ _ âŠ—â‰« r â— adj.unit â–· g âŠ—â‰« ((r â‰« l) â— Î² â‰« adj.counit â–· h) âŠ—â‰« ğŸ™ _ := by
        bicategory
      _ = ğŸ™ _ âŠ—â‰« rightZigzag adj.unit adj.counit â–· g âŠ—â‰« Î² := by
        rw [whisker_exchange]
        bicategory
      _ = Î² := by
        rw [adj.right_triangle]
        bicategory

/-- The bijection `(g â‰« l âŸ¶ h) â‰ƒ (g âŸ¶ h â‰« r)` induced by an adjunction
`l âŠ£ r` in a bicategory. -/
@[simps -isSimp]
def homEquivâ‚‚ {g : a âŸ¶ b} {h : a âŸ¶ c} : (g â‰« l âŸ¶ h) â‰ƒ (g âŸ¶ h â‰« r) where
  toFun Î± := (Ï_ _).inv â‰« g â— adj.unit â‰« (Î±_ _ _ _).inv â‰« Î± â–· r
  invFun Î³ := Î³ â–· l â‰« (Î±_ _ _ _ ).hom â‰« h â— adj.counit â‰« (Ï_ _).hom
  left_inv Î± :=
    calc
      _ = ğŸ™ _ âŠ—â‰« g â— adj.unit â–· l âŠ—â‰« (Î± â–· (r â‰« l) â‰« h â— adj.counit) âŠ—â‰« ğŸ™ _ := by
        bicategory
      _ = ğŸ™ _ âŠ—â‰« g â— leftZigzag adj.unit adj.counit âŠ—â‰« Î± := by
        rw [â† whisker_exchange]
        bicategory
      _ = Î± := by
        rw [adj.left_triangle]
        bicategory
  right_inv Î³ :=
    calc
      _ = ğŸ™ _ âŠ—â‰« (g â— adj.unit â‰« Î³ â–· (l â‰« r)) âŠ—â‰« h â— adj.counit â–· r âŠ—â‰« ğŸ™ _ := by
        bicategory
      _ = ğŸ™ _ âŠ—â‰« Î³ âŠ—â‰« h â— rightZigzag adj.unit adj.counit âŠ—â‰« ğŸ™ _ := by
        rw [whisker_exchange]
        bicategory
      _ = Î³ := by
        rw [adj.right_triangle]
        bicategory

end Adjunction

section mateEquiv

section

variable {c d e f : B} {g : c âŸ¶ e} {h : d âŸ¶ f} {lâ‚ : c âŸ¶ d} {râ‚ : d âŸ¶ c} {lâ‚‚ : e âŸ¶ f} {râ‚‚ : f âŸ¶ e}
variable (adjâ‚ : lâ‚ âŠ£ râ‚) (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚)

/-- Suppose we have a square of 1-morphisms (where the top and bottom are adjunctions `lâ‚ âŠ£ râ‚`
and `lâ‚‚ âŠ£ râ‚‚` respectively).
```
      c â†” d
    g â†“   â†“ h
      e â†” f
```

Then we have a bijection between natural transformations `g â‰« lâ‚‚ âŸ¶ lâ‚ â‰« h` and
`râ‚ â‰« g âŸ¶ h â‰« râ‚‚`. This can be seen as a bijection of the 2-cells:

```
         lâ‚                  râ‚
      c --â†’ d             c â†-- d
    g â†“  â†—  â†“ h         g â†“  â†˜  â†“ h
      e --â†’ f             e â†-- f
         Lâ‚‚                  Râ‚‚
```

Note that if one of the transformations is an iso, it does not imply the other is an iso.
-/
@[simps! -isSimp]
def mateEquiv : (g â‰« lâ‚‚ âŸ¶ lâ‚ â‰« h) â‰ƒ (râ‚ â‰« g âŸ¶ h â‰« râ‚‚) :=
  adjâ‚‚.homEquivâ‚‚.trans ((Iso.homCongr (Iso.refl _) (Î±_ _ _ _)).trans adjâ‚.homEquivâ‚)

lemma mateEquiv_eq_iff (Î± : g â‰« lâ‚‚ âŸ¶ lâ‚ â‰« h) (Î² : râ‚ â‰« g âŸ¶ h â‰« râ‚‚) :
    mateEquiv adjâ‚ adjâ‚‚ Î± = Î² â†”
    adjâ‚.homEquivâ‚.symm Î² = adjâ‚‚.homEquivâ‚‚ Î± â‰« (Î±_ _ _ _).hom := by
  conv_lhs => rw [eq_comm, â† adjâ‚.homEquivâ‚.symm.injective.eq_iff']
  rw [mateEquiv_apply, Equiv.symm_apply_apply]

lemma mateEquiv_apply' (Î± : g â‰« lâ‚‚ âŸ¶ lâ‚ â‰« h) :
    mateEquiv adjâ‚ adjâ‚‚ Î± =
      ğŸ™ _ âŠ—â‰« râ‚ â— g â— adjâ‚‚.unit âŠ—â‰« râ‚ â— Î± â–· râ‚‚ âŠ—â‰« adjâ‚.counit â–· h â–· râ‚‚ âŠ—â‰« ğŸ™ _ := by
  rw [mateEquiv_apply, Adjunction.homEquivâ‚‚_apply, Adjunction.homEquivâ‚_apply]
  bicategory

lemma mateEquiv_symm_apply' (Î² : râ‚ â‰« g âŸ¶ h â‰« râ‚‚) :
    (mateEquiv adjâ‚ adjâ‚‚).symm Î² =
      ğŸ™ _ âŠ—â‰« adjâ‚.unit â–· g â–· lâ‚‚ âŠ—â‰« lâ‚ â— Î² â–· lâ‚‚ âŠ—â‰« lâ‚ â— h â— adjâ‚‚.counit âŠ—â‰« ğŸ™ _ := by
  rw [mateEquiv_symm_apply, Adjunction.homEquivâ‚‚_symm_apply, Adjunction.homEquivâ‚_symm_apply]
  bicategory

end

section

variable {a b c d : B} {lâ‚ : a âŸ¶ b} {râ‚ : b âŸ¶ a} (adjâ‚ : lâ‚ âŠ£ râ‚)
  {lâ‚‚ : c âŸ¶ d} {râ‚‚ : d âŸ¶ c} (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚)
  {f : a âŸ¶ c} {g : b âŸ¶ d}

lemma mateEquiv_id_comp_right (Ï† : f â‰« ğŸ™ _ â‰« lâ‚‚ âŸ¶ lâ‚ â‰« g) :
    mateEquiv adjâ‚ ((Adjunction.id _).comp adjâ‚‚) Ï† =
      mateEquiv adjâ‚ adjâ‚‚ (f â— (Î»_ lâ‚‚).inv â‰« Ï†) â‰« (Ï_ _).inv â‰« (Î±_ _ _ _).hom := by
  simp only [mateEquiv_apply, Adjunction.homEquivâ‚_apply, Adjunction.homEquivâ‚‚_apply,
    Adjunction.id]
  dsimp
  bicategory

lemma mateEquiv_comp_id_right (Ï† : f â‰« lâ‚‚ â‰« ğŸ™ d âŸ¶ lâ‚ â‰« g) :
    mateEquiv adjâ‚ (adjâ‚‚.comp (Adjunction.id _)) Ï† =
      mateEquiv adjâ‚ adjâ‚‚ ((Ï_ _).inv â‰« (Î±_ _ _ _).hom â‰« Ï†) â‰« g â— (Î»_ râ‚‚).inv := by
  simp only [mateEquiv_apply, Adjunction.homEquivâ‚_apply, Adjunction.homEquivâ‚‚_apply,
    Adjunction.id]
  dsimp
  bicategory

end

end mateEquiv

section mateEquivVComp

variable {a b c d e f : B} {gâ‚ : a âŸ¶ c} {gâ‚‚ : c âŸ¶ e} {hâ‚ : b âŸ¶ d} {hâ‚‚ : d âŸ¶ f}
variable {lâ‚ : a âŸ¶ b} {râ‚ : b âŸ¶ a} {lâ‚‚ : c âŸ¶ d} {râ‚‚ : d âŸ¶ c} {lâ‚ƒ : e âŸ¶ f} {râ‚ƒ : f âŸ¶ e}
variable (adjâ‚ : lâ‚ âŠ£ râ‚) (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚) (adjâ‚ƒ : lâ‚ƒ âŠ£ râ‚ƒ)

/-- Squares between left adjoints can be composed "vertically" by pasting. -/
def leftAdjointSquare.vcomp (Î± : gâ‚ â‰« lâ‚‚ âŸ¶ lâ‚ â‰« hâ‚) (Î² : gâ‚‚ â‰« lâ‚ƒ âŸ¶ lâ‚‚ â‰« hâ‚‚) :
    (gâ‚ â‰« gâ‚‚) â‰« lâ‚ƒ âŸ¶ lâ‚ â‰« (hâ‚ â‰« hâ‚‚) :=
  (Î±_ _ _ _).hom â‰« gâ‚ â— Î² â‰« (Î±_ _ _ _).inv â‰« Î± â–· hâ‚‚ â‰« (Î±_ _ _ _).hom

/-- Squares between right adjoints can be composed "vertically" by pasting. -/
def rightAdjointSquare.vcomp (Î± : râ‚ â‰« gâ‚ âŸ¶ hâ‚ â‰« râ‚‚) (Î² : râ‚‚ â‰« gâ‚‚ âŸ¶ hâ‚‚ â‰« râ‚ƒ) :
    râ‚ â‰« (gâ‚ â‰« gâ‚‚) âŸ¶ (hâ‚ â‰« hâ‚‚) â‰« râ‚ƒ :=
  (Î±_ _ _ _).inv â‰« Î± â–· gâ‚‚ â‰« (Î±_ _ _ _).hom â‰« hâ‚ â— Î² â‰« (Î±_ _ _ _).inv

/-- The mates equivalence commutes with vertical composition. -/
theorem mateEquiv_vcomp (Î± : gâ‚ â‰« lâ‚‚ âŸ¶ lâ‚ â‰« hâ‚) (Î² : gâ‚‚ â‰« lâ‚ƒ âŸ¶ lâ‚‚ â‰« hâ‚‚) :
    mateEquiv adjâ‚ adjâ‚ƒ (leftAdjointSquare.vcomp Î± Î²) =
      rightAdjointSquare.vcomp (mateEquiv adjâ‚ adjâ‚‚ Î±) (mateEquiv adjâ‚‚ adjâ‚ƒ Î²) := by
  simp only [leftAdjointSquare.vcomp, mateEquiv_apply', rightAdjointSquare.vcomp]
  symm
  calc
    _ = ğŸ™ _ âŠ—â‰« râ‚ â— gâ‚ â— adjâ‚‚.unit â–· gâ‚‚ âŠ—â‰« râ‚ â— Î± â–· râ‚‚ â–· gâ‚‚ âŠ—â‰«
          ((adjâ‚.counit â–· (hâ‚ â‰« râ‚‚ â‰« gâ‚‚ â‰« ğŸ™ e)) â‰« ğŸ™ b â— (hâ‚ â— râ‚‚ â— gâ‚‚ â— adjâ‚ƒ.unit)) âŠ—â‰«
            hâ‚ â— râ‚‚ â— Î² â–· râ‚ƒ âŠ—â‰« hâ‚ â— adjâ‚‚.counit â–· hâ‚‚ â–· râ‚ƒ âŠ—â‰« ğŸ™ _ := by
      bicategory
    _ = ğŸ™ _ âŠ—â‰« râ‚ â— gâ‚ â— adjâ‚‚.unit â–· gâ‚‚ âŠ—â‰«
          (râ‚ â— (Î± â–· (râ‚‚ â‰« gâ‚‚ â‰« ğŸ™ e) â‰« (lâ‚ â‰« hâ‚) â— râ‚‚ â— gâ‚‚ â— adjâ‚ƒ.unit)) âŠ—â‰«
            ((adjâ‚.counit â–· (hâ‚ â‰« râ‚‚) â–· (gâ‚‚ â‰« lâ‚ƒ) â‰« (ğŸ™ b â‰« hâ‚ â‰« râ‚‚) â— Î²) â–· râ‚ƒ) âŠ—â‰«
              hâ‚ â— adjâ‚‚.counit â–· hâ‚‚ â–· râ‚ƒ âŠ—â‰« ğŸ™ _ := by
      rw [â† whisker_exchange]
      bicategory
    _ = ğŸ™ _ âŠ—â‰« râ‚ â— gâ‚ â— (adjâ‚‚.unit â–· (gâ‚‚ â‰« ğŸ™ e) â‰« (lâ‚‚ â‰« râ‚‚) â— gâ‚‚ â— adjâ‚ƒ.unit) âŠ—â‰«
          (râ‚ â— (Î± â–· (râ‚‚ â‰« gâ‚‚ â‰« lâ‚ƒ) â‰« (lâ‚ â‰« hâ‚) â— râ‚‚ â— Î²) â–· râ‚ƒ) âŠ—â‰«
            (adjâ‚.counit â–· hâ‚ â–· (râ‚‚ â‰« lâ‚‚) â‰« (ğŸ™ b â‰« hâ‚) â— adjâ‚‚.counit) â–· hâ‚‚ â–· râ‚ƒ âŠ—â‰« ğŸ™ _ := by
      rw [â† whisker_exchange, â† whisker_exchange]
      bicategory
    _ = ğŸ™ _ âŠ—â‰« râ‚ â— gâ‚ â— gâ‚‚ â— adjâ‚ƒ.unit âŠ—â‰«
          râ‚ â— gâ‚ â— (adjâ‚‚.unit â–· (gâ‚‚ â‰« lâ‚ƒ) â‰« (lâ‚‚ â‰« râ‚‚) â— Î²) â–· râ‚ƒ âŠ—â‰«
            râ‚ â— (Î± â–· (râ‚‚ â‰« lâ‚‚) â‰« (lâ‚ â‰« hâ‚) â— adjâ‚‚.counit) â–· hâ‚‚ â–· râ‚ƒ âŠ—â‰«
              adjâ‚.counit â–· hâ‚ â–· hâ‚‚ â–· râ‚ƒ âŠ—â‰« ğŸ™ _ := by
      rw [â† whisker_exchange, â† whisker_exchange, â† whisker_exchange]
      bicategory
    _ = ğŸ™ _ âŠ—â‰« râ‚ â— gâ‚ â— gâ‚‚ â— adjâ‚ƒ.unit âŠ—â‰« râ‚ â— gâ‚ â— Î² â–· râ‚ƒ âŠ—â‰«
          ((râ‚ â‰« gâ‚) â— leftZigzag adjâ‚‚.unit adjâ‚‚.counit â–· (hâ‚‚ â‰« râ‚ƒ)) âŠ—â‰«
            râ‚ â— Î± â–· hâ‚‚ â–· râ‚ƒ âŠ—â‰« adjâ‚.counit â–· hâ‚ â–· hâ‚‚ â–· râ‚ƒ âŠ—â‰« ğŸ™ _ := by
      rw [â† whisker_exchange, â† whisker_exchange]
      bicategory
    _ = _ := by
      rw [adjâ‚‚.left_triangle]
      bicategory

end mateEquivVComp

section mateEquivHComp

variable {a : B} {b : B} {c : B} {d : B} {e : B} {f : B}
variable {g : a âŸ¶ d} {h : b âŸ¶ e} {k : c âŸ¶ f}
variable {lâ‚ : a âŸ¶ b} {râ‚ : b âŸ¶ a} {lâ‚‚ : d âŸ¶ e} {râ‚‚ : e âŸ¶ d}
variable {lâ‚ƒ : b âŸ¶ c} {râ‚ƒ : c âŸ¶ b} {lâ‚„ : e âŸ¶ f} {râ‚„ : f âŸ¶ e}
variable (adjâ‚ : lâ‚ âŠ£ râ‚) (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚) (adjâ‚ƒ : lâ‚ƒ âŠ£ râ‚ƒ) (adjâ‚„ : lâ‚„ âŠ£ râ‚„)

/-- Squares between left adjoints can be composed "horizontally" by pasting. -/
def leftAdjointSquare.hcomp (Î± : g â‰« lâ‚‚ âŸ¶ lâ‚ â‰« h) (Î² : h â‰« lâ‚„ âŸ¶ lâ‚ƒ â‰« k) :
    g â‰« (lâ‚‚ â‰« lâ‚„) âŸ¶ (lâ‚ â‰« lâ‚ƒ) â‰« k :=
  (Î±_ _ _ _).inv â‰« Î± â–· lâ‚„ â‰« (Î±_ _ _ _).hom â‰« lâ‚ â— Î² â‰« (Î±_ _ _ _).inv

/-- Squares between right adjoints can be composed "horizontally" by pasting. -/
def rightAdjointSquare.hcomp (Î± : râ‚ â‰« g âŸ¶ h â‰« râ‚‚) (Î² : râ‚ƒ â‰« h âŸ¶ k â‰« râ‚„) :
    (râ‚ƒ â‰« râ‚) â‰« g âŸ¶ k â‰« (râ‚„ â‰« râ‚‚) :=
  (Î±_ _ _ _).hom â‰« râ‚ƒ â— Î± â‰« (Î±_ _ _ _).inv â‰« Î² â–· râ‚‚ â‰« (Î±_ _ _ _).hom

/-- The mates equivalence commutes with horizontal composition of squares. -/
theorem mateEquiv_hcomp (Î± : g â‰« lâ‚‚ âŸ¶ lâ‚ â‰« h) (Î² : h â‰« lâ‚„ âŸ¶ lâ‚ƒ â‰« k) :
    (mateEquiv (adjâ‚.comp adjâ‚ƒ) (adjâ‚‚.comp adjâ‚„)) (leftAdjointSquare.hcomp Î± Î²) =
      rightAdjointSquare.hcomp (mateEquiv adjâ‚ adjâ‚‚ Î±) (mateEquiv adjâ‚ƒ adjâ‚„ Î²) := by
  simp only [mateEquiv_apply']
  dsimp [leftAdjointSquare.hcomp, rightAdjointSquare.hcomp]
  calc
    _ = ğŸ™ _ âŠ—â‰« râ‚ƒ â— râ‚ â— g â— adjâ‚‚.unit âŠ—â‰«
          râ‚ƒ â— râ‚ â— ((g â‰« lâ‚‚) â— adjâ‚„.unit â‰« Î± â–· (lâ‚„ â‰« râ‚„)) â–· râ‚‚ âŠ—â‰«
            râ‚ƒ â— ((râ‚ â‰« lâ‚) â— Î² â‰« adjâ‚.counit â–· (lâ‚ƒ â‰« k)) â–· râ‚„ â–· râ‚‚ âŠ—â‰«
              adjâ‚ƒ.counit â–· k â–· râ‚„ â–· râ‚‚ âŠ—â‰« ğŸ™ _ := by
      bicategory
    _ = ğŸ™ _ âŠ—â‰« râ‚ƒ â— râ‚ â— g â— adjâ‚‚.unit âŠ—â‰« râ‚ƒ â— râ‚ â— Î± â–· râ‚‚ âŠ—â‰«
          râ‚ƒ â— ((râ‚ â‰« lâ‚) â— h â— adjâ‚„.unit â‰« adjâ‚.counit â–· (h â‰« lâ‚„ â‰« râ‚„)) â–· râ‚‚ âŠ—â‰«
            râ‚ƒ â— Î² â–· râ‚„ â–· râ‚‚ âŠ—â‰« adjâ‚ƒ.counit â–· k â–· râ‚„ â–· râ‚‚ âŠ—â‰« ğŸ™ _ := by
      rw [whisker_exchange, whisker_exchange]
      bicategory
    _ = _ := by
      rw [whisker_exchange]
      bicategory

end mateEquivHComp

section mateEquivSquareComp

variable {a b c d e f x y z : B}
variable {gâ‚ : a âŸ¶ d} {hâ‚ : b âŸ¶ e} {kâ‚ : c âŸ¶ f} {gâ‚‚ : d âŸ¶ x} {hâ‚‚ : e âŸ¶ y} {kâ‚‚ : f âŸ¶ z}
variable {lâ‚ : a âŸ¶ b} {râ‚ : b âŸ¶ a} {lâ‚‚ : b âŸ¶ c} {râ‚‚ : c âŸ¶ b} {lâ‚ƒ : d âŸ¶ e} {râ‚ƒ : e âŸ¶ d}
variable {lâ‚„ : e âŸ¶ f} {râ‚„ : f âŸ¶ e} {lâ‚… : x âŸ¶ y} {râ‚… : y âŸ¶ x} {lâ‚† : y âŸ¶ z} {râ‚† : z âŸ¶ y}
variable (adjâ‚ : lâ‚ âŠ£ râ‚) (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚) (adjâ‚ƒ : lâ‚ƒ âŠ£ râ‚ƒ)
variable (adjâ‚„ : lâ‚„ âŠ£ râ‚„) (adjâ‚… : lâ‚… âŠ£ râ‚…) (adjâ‚† : lâ‚† âŠ£ râ‚†)

section leftAdjointSquare.comp

variable (Î± : gâ‚ â‰« lâ‚ƒ âŸ¶ lâ‚ â‰« hâ‚) (Î² : hâ‚ â‰« lâ‚„ âŸ¶ lâ‚‚ â‰« kâ‚)
variable (Î³ : gâ‚‚ â‰« lâ‚… âŸ¶ lâ‚ƒ â‰« hâ‚‚) (Î´ : hâ‚‚ â‰« lâ‚† âŸ¶ lâ‚„ â‰« kâ‚‚)

/-- Squares of squares between left adjoints can be composed by iterating vertical and horizontal
composition.
-/
def leftAdjointSquare.comp :
    ((gâ‚ â‰« gâ‚‚) â‰« (lâ‚… â‰« lâ‚†)) âŸ¶ ((lâ‚ â‰« lâ‚‚) â‰« (kâ‚ â‰« kâ‚‚)) :=
  vcomp (hcomp Î± Î²) (hcomp Î³ Î´)

theorem leftAdjointSquare.comp_vhcomp : comp Î± Î² Î³ Î´ = vcomp (hcomp Î± Î²) (hcomp Î³ Î´) := rfl

/-- Horizontal and vertical composition of squares commutes. -/
theorem leftAdjointSquare.comp_hvcomp :
    comp Î± Î² Î³ Î´ = hcomp (vcomp Î± Î³) (vcomp Î² Î´) := by
  dsimp only [comp, vcomp, hcomp]
  calc
    _ = ğŸ™ _ âŠ—â‰« gâ‚ â— Î³ â–· lâ‚† âŠ—â‰« ((gâ‚ â‰« lâ‚ƒ) â— Î´ â‰« Î± â–· (lâ‚„ â‰« kâ‚‚)) âŠ—â‰« lâ‚ â— Î² â–· kâ‚‚ âŠ—â‰« ğŸ™ _ := by
      bicategory
    _ = _ := by
      rw [whisker_exchange]
      bicategory

end leftAdjointSquare.comp

section rightAdjointSquare.comp

variable (Î± : râ‚ â‰« gâ‚ âŸ¶ hâ‚ â‰« râ‚ƒ) (Î² : râ‚‚ â‰« hâ‚ âŸ¶ kâ‚ â‰« râ‚„)
variable (Î³ : râ‚ƒ â‰« gâ‚‚ âŸ¶ hâ‚‚ â‰« râ‚…) (Î´ : râ‚„ â‰« hâ‚‚ âŸ¶ kâ‚‚ â‰« râ‚†)

/-- Squares of squares between right adjoints can be composed by iterating vertical and horizontal
composition.
-/
def rightAdjointSquare.comp :
    ((râ‚‚ â‰« râ‚) â‰« (gâ‚ â‰« gâ‚‚) âŸ¶ (kâ‚ â‰« kâ‚‚) â‰« (râ‚† â‰« râ‚…)) :=
  vcomp (hcomp Î± Î²) (hcomp Î³ Î´)

theorem rightAdjointSquare.comp_vhcomp : comp Î± Î² Î³ Î´ = vcomp (hcomp Î± Î²) (hcomp Î³ Î´) := rfl

/-- Horizontal and vertical composition of squares commutes. -/
theorem rightAdjointSquare.comp_hvcomp :
    comp Î± Î² Î³ Î´ = hcomp (vcomp Î± Î³) (vcomp Î² Î´) := by
  dsimp only [comp, vcomp, hcomp]
  calc
    _ = ğŸ™ _ âŠ—â‰« râ‚‚ â— Î± â–· gâ‚‚ âŠ—â‰« (Î² â–· (râ‚ƒ â‰« gâ‚‚) â‰« (kâ‚ â‰« râ‚„) â— Î³) âŠ—â‰« kâ‚ â— Î´ â–· râ‚… âŠ—â‰« ğŸ™ _ := by
      bicategory
    _ = _ := by
      rw [â† whisker_exchange]
      bicategory

end rightAdjointSquare.comp

/-- The mates equivalence commutes with composition of squares of squares. These results form the
basis for an isomorphism of double categories to be proven later.
-/
theorem mateEquiv_square
    (Î± : gâ‚ â‰« lâ‚ƒ âŸ¶ lâ‚ â‰« hâ‚) (Î² : hâ‚ â‰« lâ‚„ âŸ¶ lâ‚‚ â‰« kâ‚)
    (Î³ : gâ‚‚ â‰« lâ‚… âŸ¶ lâ‚ƒ â‰« hâ‚‚) (Î´ : hâ‚‚ â‰« lâ‚† âŸ¶ lâ‚„ â‰« kâ‚‚) :
    (mateEquiv (adjâ‚.comp adjâ‚‚) (adjâ‚….comp adjâ‚†))
        (leftAdjointSquare.comp Î± Î² Î³ Î´) =
      rightAdjointSquare.comp
        (mateEquiv adjâ‚ adjâ‚ƒ Î±) (mateEquiv adjâ‚‚ adjâ‚„ Î²)
        (mateEquiv adjâ‚ƒ adjâ‚… Î³) (mateEquiv adjâ‚„ adjâ‚† Î´) := by
  have vcomp :=
    mateEquiv_vcomp (adjâ‚.comp adjâ‚‚) (adjâ‚ƒ.comp adjâ‚„) (adjâ‚….comp adjâ‚†)
      (leftAdjointSquare.hcomp Î± Î²) (leftAdjointSquare.hcomp Î³ Î´)
  have hcomp1 := mateEquiv_hcomp adjâ‚ adjâ‚ƒ adjâ‚‚ adjâ‚„ Î± Î²
  have hcomp2 := mateEquiv_hcomp adjâ‚ƒ adjâ‚… adjâ‚„ adjâ‚† Î³ Î´
  rw [hcomp1, hcomp2] at vcomp
  exact vcomp

end mateEquivSquareComp

section conjugateEquiv

section

variable {c d : B}
variable {lâ‚ lâ‚‚ : c âŸ¶ d} {râ‚ râ‚‚ : d âŸ¶ c}
variable (adjâ‚ : lâ‚ âŠ£ râ‚) (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚)

/-- Given two adjunctions `lâ‚ âŠ£ râ‚` and `lâ‚‚ âŠ£ râ‚‚` both between objects `c`, `d`, there is a
bijection between 2-morphisms `lâ‚‚ âŸ¶ lâ‚` and 2-morphisms `râ‚ âŸ¶ râ‚‚`. This is
defined as a special case of `mateEquiv`, where the two "vertical" 1-morphisms are identities.
Corresponding 2-morphisms are called `conjugateEquiv`.

Furthermore, this bijection preserves (and reflects) isomorphisms, i.e. a 2-morphism is an iso
iff its image under the bijection is an iso.
-/
def conjugateEquiv : (lâ‚‚ âŸ¶ lâ‚) â‰ƒ (râ‚ âŸ¶ râ‚‚) :=
  calc
    (lâ‚‚ âŸ¶ lâ‚) â‰ƒ _ := (Iso.homCongr (Î»_ lâ‚‚) (Ï_ lâ‚)).symm
    _ â‰ƒ _ := mateEquiv adjâ‚ adjâ‚‚
    _ â‰ƒ (râ‚ âŸ¶ râ‚‚) := Iso.homCongr (Ï_ râ‚) (Î»_ râ‚‚)

theorem conjugateEquiv_apply (Î± : lâ‚‚ âŸ¶ lâ‚) :
    conjugateEquiv adjâ‚ adjâ‚‚ Î± =
      (Ï_ râ‚).inv â‰« mateEquiv adjâ‚ adjâ‚‚ ((Î»_ lâ‚‚).hom â‰« Î± â‰« (Ï_ lâ‚).inv) â‰« (Î»_ râ‚‚).hom :=
  rfl

theorem conjugateEquiv_apply' (Î± : lâ‚‚ âŸ¶ lâ‚) :
    conjugateEquiv adjâ‚ adjâ‚‚ Î± =
      (Ï_ _).inv â‰« râ‚ â— adjâ‚‚.unit â‰« râ‚ â— Î± â–· râ‚‚ â‰« (Î±_ _ _ _).inv â‰«
        adjâ‚.counit â–· râ‚‚ â‰« (Î»_ _).hom := by
  rw [conjugateEquiv_apply, mateEquiv_apply']
  bicategory

theorem conjugateEquiv_symm_apply (Î± : râ‚ âŸ¶ râ‚‚) :
    (conjugateEquiv adjâ‚ adjâ‚‚).symm Î± =
      (Î»_ lâ‚‚).inv â‰« (mateEquiv adjâ‚ adjâ‚‚).symm ((Ï_ râ‚).hom â‰« Î± â‰« (Î»_ râ‚‚).inv) â‰« (Ï_ lâ‚).hom :=
  rfl

theorem conjugateEquiv_symm_apply' (Î± : râ‚ âŸ¶ râ‚‚) :
    (conjugateEquiv adjâ‚ adjâ‚‚).symm Î± =
      (Î»_ _).inv â‰« adjâ‚.unit â–· lâ‚‚ â‰« (Î±_ _ _ _).hom â‰« lâ‚ â— Î± â–· lâ‚‚ â‰«
        lâ‚ â— adjâ‚‚.counit â‰« (Ï_ _).hom := by
  rw [conjugateEquiv_symm_apply, mateEquiv_symm_apply']
  bicategory

@[simp]
theorem conjugateEquiv_id : conjugateEquiv adjâ‚ adjâ‚ (ğŸ™ _) = ğŸ™ _ := by
  rw [conjugateEquiv_apply, mateEquiv_apply']
  calc
    _ = ğŸ™ _ âŠ—â‰« rightZigzag adjâ‚.unit adjâ‚.counit âŠ—â‰« ğŸ™ _ := by
      bicategory
    _ = ğŸ™ râ‚ := by
      rw [adjâ‚.right_triangle]
      bicategory

@[simp]
theorem conjugateEquiv_symm_id : (conjugateEquiv adjâ‚ adjâ‚).symm (ğŸ™ _) = ğŸ™ _ := by
  rw [Equiv.symm_apply_eq, conjugateEquiv_id]

theorem conjugateEquiv_adjunction_id {l r : c âŸ¶ c} (adj : l âŠ£ r) (Î± : ğŸ™ c âŸ¶ l) :
    (conjugateEquiv adj (Adjunction.id c) Î±) = (Ï_ _).inv â‰« r â— Î± â‰« adj.counit := by
  rw [conjugateEquiv_apply, mateEquiv_apply']
  dsimp [Adjunction.id]
  bicategory

theorem conjugateEquiv_adjunction_id_symm {l r : c âŸ¶ c} (adj : l âŠ£ r) (Î± : r âŸ¶ ğŸ™ c) :
    (conjugateEquiv adj (Adjunction.id c)).symm Î± = adj.unit â‰« l â— Î± â‰« (Ï_ _).hom := by
  rw [conjugateEquiv_symm_apply, mateEquiv_symm_apply']
  dsimp [Adjunction.id]
  bicategory

end

@[simp]
lemma mateEquiv_leftUnitor_hom_rightUnitor_inv
    {a b : B} {l : a âŸ¶ b} {r : b âŸ¶ a} (adj : l âŠ£ r) :
    mateEquiv adj adj ((Î»_ _).hom â‰« (Ï_ _).inv) = (Ï_ _).hom â‰« (Î»_ _).inv := by
  simp [â† cancel_mono (Î»_ r).hom,
    â† conjugateEquiv_id adj, conjugateEquiv_apply]

section

variable {a b : B} {l : a âŸ¶ b} {r : b âŸ¶ a} (adj : l âŠ£ r)
    {l' : a âŸ¶ b} {r' : b âŸ¶ a} (adj' : l' âŠ£ r') (Ï† : l' âŸ¶ l)

lemma conjugateEquiv_id_comp_right_apply :
    conjugateEquiv adj ((Adjunction.id _).comp adj') ((Î»_ _).hom â‰« Ï†) =
      conjugateEquiv adj adj' Ï† â‰« (Ï_ _).inv := by
  simp only [conjugateEquiv_apply, mateEquiv_id_comp_right,
    id_whiskerLeft, Category.assoc, Iso.inv_hom_id_assoc]
  bicategory

lemma conjugateEquiv_comp_id_right_apply :
    conjugateEquiv adj (adj'.comp (Adjunction.id _)) ((Ï_ _).hom â‰« Ï†) =
      conjugateEquiv adj adj' Ï† â‰« (Î»_ _).inv := by
  simp only [conjugateEquiv_apply, Category.assoc, mateEquiv_comp_id_right, id_whiskerLeft,
    Iso.inv_hom_id, Category.comp_id, Iso.hom_inv_id, Iso.cancel_iso_inv_left,
    EmbeddingLike.apply_eq_iff_eq]
  bicategory

end

lemma conjugateEquiv_whiskerLeft
    {a b c : B} {lâ‚ : a âŸ¶ b} {râ‚ : b âŸ¶ a} (adjâ‚ : lâ‚ âŠ£ râ‚)
    {lâ‚‚ : b âŸ¶ c} {râ‚‚ : c âŸ¶ b} (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚)
    {lâ‚‚' : b âŸ¶ c} {râ‚‚' : c âŸ¶ b} (adjâ‚‚' : lâ‚‚' âŠ£ râ‚‚') (Ï† : lâ‚‚' âŸ¶ lâ‚‚) :
    conjugateEquiv (adjâ‚.comp adjâ‚‚) (adjâ‚.comp adjâ‚‚') (lâ‚ â— Ï†) =
      conjugateEquiv adjâ‚‚ adjâ‚‚' Ï† â–· râ‚ := by
  have := mateEquiv_hcomp adjâ‚ adjâ‚ adjâ‚‚ adjâ‚‚' ((Î»_ _).hom â‰« (Ï_ _).inv)
    ((Î»_ _).hom â‰« Ï† â‰« (Ï_ _).inv)
  dsimp [leftAdjointSquare.hcomp, rightAdjointSquare.hcomp] at this
  simp only [comp_whiskerRight, leftUnitor_whiskerRight, Category.assoc, whiskerLeft_comp,
    whiskerLeft_rightUnitor_inv, Iso.hom_inv_id, Category.comp_id, triangle_assoc,
    inv_hom_whiskerRight_assoc, Iso.inv_hom_id_assoc, mateEquiv_leftUnitor_hom_rightUnitor_inv,
    whiskerLeft_rightUnitor, triangle_assoc_comp_left_inv_assoc, Iso.hom_inv_id_assoc] at this
  simp [conjugateEquiv_apply, this]

lemma conjugateEquiv_whiskerRight
    {a b c : B} {lâ‚ : a âŸ¶ b} {râ‚ : b âŸ¶ a} (adjâ‚ : lâ‚ âŠ£ râ‚)
    {lâ‚' : a âŸ¶ b} {râ‚' : b âŸ¶ a} (adjâ‚' : lâ‚' âŠ£ râ‚')
    {lâ‚‚ : b âŸ¶ c} {râ‚‚ : c âŸ¶ b} (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚) (Ï† : lâ‚' âŸ¶ lâ‚) :
    conjugateEquiv (adjâ‚.comp adjâ‚‚) (adjâ‚'.comp adjâ‚‚) (Ï† â–· lâ‚‚) =
      râ‚‚ â— conjugateEquiv adjâ‚ adjâ‚' Ï† := by
  have := mateEquiv_hcomp adjâ‚ adjâ‚' adjâ‚‚ adjâ‚‚
    ((Î»_ _).hom â‰« Ï† â‰« (Ï_ _).inv) ((Î»_ _).hom â‰« (Ï_ _).inv)
  dsimp [leftAdjointSquare.hcomp, rightAdjointSquare.hcomp] at this
  simp only [comp_whiskerRight, leftUnitor_whiskerRight, Category.assoc, whiskerLeft_comp,
    whiskerLeft_rightUnitor_inv, Iso.hom_inv_id, Category.comp_id, triangle_assoc,
    inv_hom_whiskerRight_assoc, Iso.inv_hom_id_assoc, mateEquiv_leftUnitor_hom_rightUnitor_inv,
    leftUnitor_inv_whiskerRight, Iso.inv_hom_id, triangle_assoc_comp_right_assoc] at this
  simp [conjugateEquiv_apply, this]

lemma conjugateEquiv_associator_hom
    {a b c d : B} {lâ‚ : a âŸ¶ b} {râ‚ : b âŸ¶ a} (adjâ‚ : lâ‚ âŠ£ râ‚)
    {lâ‚‚ : b âŸ¶ c} {râ‚‚ : c âŸ¶ b} (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚)
    {lâ‚ƒ : c âŸ¶ d} {râ‚ƒ : d âŸ¶ c} (adjâ‚ƒ : lâ‚ƒ âŠ£ râ‚ƒ) :
    conjugateEquiv (adjâ‚.comp (adjâ‚‚.comp adjâ‚ƒ))
      ((adjâ‚.comp adjâ‚‚).comp adjâ‚ƒ) (Î±_ _ _ _).hom = (Î±_ _ _ _).hom := by
  simp [â† cancel_epi (Ï_ ((râ‚ƒ â‰« râ‚‚) â‰« râ‚)).hom, â† cancel_mono (Î»_ (râ‚ƒ â‰« râ‚‚ â‰« râ‚)).inv,
    conjugateEquiv_apply, mateEquiv_eq_iff, Adjunction.homEquivâ‚_symm_apply,
    Adjunction.homEquivâ‚‚_apply]
  bicategory

end conjugateEquiv

section ConjugateComposition
variable {c d : B}
variable {lâ‚ lâ‚‚ lâ‚ƒ : c âŸ¶ d} {râ‚ râ‚‚ râ‚ƒ : d âŸ¶ c}
variable (adjâ‚ : lâ‚ âŠ£ râ‚) (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚) (adjâ‚ƒ : lâ‚ƒ âŠ£ râ‚ƒ)

@[simp]
theorem conjugateEquiv_comp (Î± : lâ‚‚ âŸ¶ lâ‚) (Î² : lâ‚ƒ âŸ¶ lâ‚‚) :
    conjugateEquiv adjâ‚ adjâ‚‚ Î± â‰« conjugateEquiv adjâ‚‚ adjâ‚ƒ Î² =
      conjugateEquiv adjâ‚ adjâ‚ƒ (Î² â‰« Î±) := by
  simp only [conjugateEquiv_apply]
  calc
    _ = ğŸ™ râ‚ âŠ—â‰«
          rightAdjointSquare.vcomp
            (mateEquiv adjâ‚ adjâ‚‚ ((Î»_ _).hom â‰« Î± â‰« (Ï_ _).inv))
            (mateEquiv adjâ‚‚ adjâ‚ƒ ((Î»_ _).hom â‰« Î² â‰« (Ï_ _).inv)) âŠ—â‰« ğŸ™ râ‚ƒ := by
      dsimp only [rightAdjointSquare.vcomp]
      bicategory
    _ = _ := by
      rw [â† mateEquiv_vcomp]
      simp only [leftAdjointSquare.vcomp, mateEquiv_apply']
      bicategory

@[simp]
theorem conjugateEquiv_symm_comp (Î± : râ‚ âŸ¶ râ‚‚) (Î² : râ‚‚ âŸ¶ râ‚ƒ) :
    (conjugateEquiv adjâ‚‚ adjâ‚ƒ).symm Î² â‰« (conjugateEquiv adjâ‚ adjâ‚‚).symm Î± =
      (conjugateEquiv adjâ‚ adjâ‚ƒ).symm (Î± â‰« Î²) := by
  rw [Equiv.eq_symm_apply, â† conjugateEquiv_comp _ adjâ‚‚]
  simp only [Equiv.apply_symm_apply]

theorem conjugateEquiv_comm {Î± : lâ‚‚ âŸ¶ lâ‚} {Î² : lâ‚ âŸ¶ lâ‚‚} (Î²Î± : Î² â‰« Î± = ğŸ™ _) :
    conjugateEquiv adjâ‚ adjâ‚‚ Î± â‰« conjugateEquiv adjâ‚‚ adjâ‚ Î² = ğŸ™ _ := by
  rw [conjugateEquiv_comp, Î²Î±, conjugateEquiv_id]

theorem conjugateEquiv_symm_comm {Î± : râ‚ âŸ¶ râ‚‚} {Î² : râ‚‚ âŸ¶ râ‚} (Î±Î² : Î± â‰« Î² = ğŸ™ _) :
    (conjugateEquiv adjâ‚‚ adjâ‚).symm Î² â‰« (conjugateEquiv adjâ‚ adjâ‚‚).symm Î± = ğŸ™ _ := by
  rw [conjugateEquiv_symm_comp, Î±Î², conjugateEquiv_symm_id]

end ConjugateComposition

section ConjugateIsomorphism

variable {c d : B}
variable {lâ‚ lâ‚‚ : c âŸ¶ d} {râ‚ râ‚‚ : d âŸ¶ c}
variable (adjâ‚ : lâ‚ âŠ£ râ‚) (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚)

/-- If `Î±` is an isomorphism between left adjoints, then its conjugate transformation is an
isomorphism. The converse is given in `conjugateEquiv_of_iso`.
-/
instance conjugateEquiv_iso (Î± : lâ‚‚ âŸ¶ lâ‚) [IsIso Î±] :
    IsIso (conjugateEquiv adjâ‚ adjâ‚‚ Î±) :=
  âŸ¨âŸ¨conjugateEquiv adjâ‚‚ adjâ‚ (inv Î±),
      âŸ¨conjugateEquiv_comm _ _ (by simp), conjugateEquiv_comm _ _ (by simp)âŸ©âŸ©âŸ©

/-- If `Î±` is an isomorphism between right adjoints, then its conjugate transformation is an
isomorphism. The converse is given in `conjugateEquiv_symm_of_iso`.
-/
instance conjugateEquiv_symm_iso (Î± : râ‚ âŸ¶ râ‚‚) [IsIso Î±] :
    IsIso ((conjugateEquiv adjâ‚ adjâ‚‚).symm Î±) :=
  âŸ¨âŸ¨(conjugateEquiv adjâ‚‚ adjâ‚).symm (inv Î±),
      âŸ¨conjugateEquiv_symm_comm _ _ (by simp), conjugateEquiv_symm_comm _ _ (by simp)âŸ©âŸ©âŸ©

/-- If `Î±` is a natural transformation between left adjoints whose conjugate natural transformation
is an isomorphism, then `Î±` is an isomorphism. The converse is given in `Conjugate_iso`.
-/
theorem conjugateEquiv_of_iso (Î± : lâ‚‚ âŸ¶ lâ‚) [IsIso (conjugateEquiv adjâ‚ adjâ‚‚ Î±)] :
    IsIso Î± := by
  suffices IsIso ((conjugateEquiv adjâ‚ adjâ‚‚).symm (conjugateEquiv adjâ‚ adjâ‚‚ Î±))
    by simpa only [Equiv.symm_apply_apply] using this
  infer_instance

/--
If `Î±` is a natural transformation between right adjoints whose conjugate natural transformation is
an isomorphism, then `Î±` is an isomorphism. The converse is given in `conjugateEquiv_symm_iso`.
-/
theorem conjugateEquiv_symm_of_iso (Î± : râ‚ âŸ¶ râ‚‚)
    [IsIso ((conjugateEquiv adjâ‚ adjâ‚‚).symm Î±)] : IsIso Î± := by
  suffices IsIso ((conjugateEquiv adjâ‚ adjâ‚‚) ((conjugateEquiv adjâ‚ adjâ‚‚).symm Î±))
    by simpa only [Equiv.apply_symm_apply] using this
  infer_instance

/-- Thus conjugation defines an equivalence between natural isomorphisms. -/
@[simps]
def conjugateIsoEquiv : (lâ‚‚ â‰… lâ‚) â‰ƒ (râ‚ â‰… râ‚‚) where
  toFun Î± :=
    { hom := conjugateEquiv adjâ‚ adjâ‚‚ Î±.hom
      inv := conjugateEquiv adjâ‚‚ adjâ‚ Î±.inv
      hom_inv_id := by
        rw [conjugateEquiv_comp, Iso.inv_hom_id, conjugateEquiv_id]
      inv_hom_id := by
        rw [conjugateEquiv_comp, Iso.hom_inv_id, conjugateEquiv_id] }
  invFun Î² :=
    { hom := (conjugateEquiv adjâ‚ adjâ‚‚).symm Î².hom
      inv := (conjugateEquiv adjâ‚‚ adjâ‚).symm Î².inv
      hom_inv_id := by
        rw [conjugateEquiv_symm_comp, Iso.inv_hom_id, conjugateEquiv_symm_id]
      inv_hom_id := by
        rw [conjugateEquiv_symm_comp, Iso.hom_inv_id, conjugateEquiv_symm_id] }
  left_inv := by
    intro Î±
    simp only [Equiv.symm_apply_apply]
  right_inv := by
    intro Î±
    simp only [Equiv.apply_symm_apply]

end ConjugateIsomorphism

section IteratedMateEquiv
variable {a b c d : B}
variable {fâ‚ : a âŸ¶ c} {uâ‚ : c âŸ¶ a} {fâ‚‚ : b âŸ¶ d} {uâ‚‚ : d âŸ¶ b}
variable {lâ‚ : a âŸ¶ b} {râ‚ : b âŸ¶ a} {lâ‚‚ : c âŸ¶ d} {râ‚‚ : d âŸ¶ c}
variable (adjâ‚ : lâ‚ âŠ£ râ‚) (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚) (adjâ‚ƒ : fâ‚ âŠ£ uâ‚) (adjâ‚„ : fâ‚‚ âŠ£ uâ‚‚)

/-- When all four morphisms in a square are left adjoints, the mates operation can be iterated:
```
         lâ‚                  râ‚                  râ‚
      c --â†’ d             c â†-- d             c â†-- d
   fâ‚ â†“  â†—  â†“  fâ‚‚      fâ‚ â†“  â†˜  â†“ fâ‚‚       uâ‚ â†‘  â†™  â†‘ uâ‚‚
      a --â†’ b             a â†-- b             a â†-- b
         lâ‚‚                  râ‚‚                  râ‚‚
```
In this case the iterated mate equals the conjugate of the original 2-morphism and is thus an
isomorphism if and only if the original 2-morphism is. This explains why some Beck-Chevalley
2-morphisms are isomorphisms.
-/
theorem iterated_mateEquiv_conjugateEquiv (Î± : fâ‚ â‰« lâ‚‚ âŸ¶ lâ‚ â‰« fâ‚‚) :
    mateEquiv adjâ‚„ adjâ‚ƒ (mateEquiv adjâ‚ adjâ‚‚ Î±) =
      conjugateEquiv (adjâ‚.comp adjâ‚„) (adjâ‚ƒ.comp adjâ‚‚) Î± := by
  simp only [conjugateEquiv_apply, mateEquiv_apply']
  dsimp [Adjunction.comp]
  bicategory

theorem iterated_mateEquiv_conjugateEquiv_symm (Î± : uâ‚‚ â‰« râ‚ âŸ¶ râ‚‚ â‰« uâ‚) :
    (mateEquiv adjâ‚ adjâ‚‚).symm ((mateEquiv adjâ‚„ adjâ‚ƒ).symm Î±) =
      (conjugateEquiv (adjâ‚.comp adjâ‚„) (adjâ‚ƒ.comp adjâ‚‚)).symm Î± := by
  rw [Equiv.eq_symm_apply, â† iterated_mateEquiv_conjugateEquiv]
  simp only [Equiv.apply_symm_apply]

end IteratedMateEquiv

section mateEquiv_conjugateEquiv_vcomp

variable {a b c d : B}
variable {g : a âŸ¶ c} {h : b âŸ¶ d}
variable {lâ‚ : a âŸ¶ b} {râ‚ : b âŸ¶ a} {lâ‚‚ : c âŸ¶ d} {râ‚‚ : d âŸ¶ c} {lâ‚ƒ : c âŸ¶ d} {râ‚ƒ : d âŸ¶ c}
variable (adjâ‚ : lâ‚ âŠ£ râ‚) (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚) (adjâ‚ƒ : lâ‚ƒ âŠ£ râ‚ƒ)

/-- Composition of a squares between left adjoints with a conjugate square. -/
def leftAdjointSquareConjugate.vcomp (Î± : g â‰« lâ‚‚ âŸ¶ lâ‚ â‰« h) (Î² : lâ‚ƒ âŸ¶ lâ‚‚) :
    g â‰« lâ‚ƒ âŸ¶ lâ‚ â‰« h :=
  g â— Î² â‰« Î±

/-- Composition of a squares between right adjoints with a conjugate square. -/
def rightAdjointSquareConjugate.vcomp (Î± : râ‚ â‰« g âŸ¶ h â‰« râ‚‚) (Î² : râ‚‚ âŸ¶ râ‚ƒ) :
    râ‚ â‰« g âŸ¶ h â‰« râ‚ƒ :=
  Î± â‰« h â— Î²

/-- The mates equivalence commutes with this composition, essentially by `mateEquiv_vcomp`. -/
theorem mateEquiv_conjugateEquiv_vcomp
    (Î± : g â‰« lâ‚‚ âŸ¶ lâ‚ â‰« h) (Î² : lâ‚ƒ âŸ¶ lâ‚‚) :
    (mateEquiv adjâ‚ adjâ‚ƒ) (leftAdjointSquareConjugate.vcomp Î± Î²) =
      rightAdjointSquareConjugate.vcomp (mateEquiv adjâ‚ adjâ‚‚ Î±) (conjugateEquiv adjâ‚‚ adjâ‚ƒ Î²) := by
  symm
  calc
    _ = ğŸ™ _ âŠ—â‰«
          rightAdjointSquare.vcomp
            (mateEquiv adjâ‚ adjâ‚‚ Î±)
            (mateEquiv adjâ‚‚ adjâ‚ƒ ((Î»_ lâ‚ƒ).hom â‰« Î² â‰« (Ï_ lâ‚‚).inv)) âŠ—â‰« ğŸ™ _  := by
      dsimp only [conjugateEquiv_apply, rightAdjointSquareConjugate.vcomp,
        rightAdjointSquare.vcomp]
      bicategory
    _ = _ := by
      rw [â† mateEquiv_vcomp]
      simp only [leftAdjointSquare.vcomp, mateEquiv_apply', leftAdjointSquareConjugate.vcomp]
      bicategory

end mateEquiv_conjugateEquiv_vcomp

section conjugateEquiv_mateEquiv_vcomp

variable {a b c d : B}
variable {g : a âŸ¶ c} {h : b âŸ¶ d}
variable {lâ‚ : a âŸ¶ b} {râ‚ : b âŸ¶ a} {lâ‚‚ : a âŸ¶ b} {râ‚‚ : b âŸ¶ a} {lâ‚ƒ : c âŸ¶ d} {râ‚ƒ : d âŸ¶ c}
variable (adjâ‚ : lâ‚ âŠ£ râ‚) (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚) (adjâ‚ƒ : lâ‚ƒ âŠ£ râ‚ƒ)

/-- Composition of a conjugate square with a squares between left adjoints. -/
def leftAdjointConjugateSquare.vcomp (Î± : lâ‚‚ âŸ¶ lâ‚) (Î² : g â‰« lâ‚ƒ âŸ¶ lâ‚‚ â‰« h) :
    g â‰« lâ‚ƒ âŸ¶ lâ‚ â‰« h :=
  Î² â‰« Î± â–· h

/-- Composition of a conjugate square with a squares between right adjoints. -/
def rightAdjointConjugateSquare.vcomp (Î± : râ‚ âŸ¶ râ‚‚) (Î² : râ‚‚ â‰« g âŸ¶ h â‰« râ‚ƒ) :
    râ‚ â‰« g âŸ¶ h â‰« râ‚ƒ :=
  Î± â–· g â‰« Î²

/-- The mates equivalence commutes with this composition, essentially by `mateEquiv_vcomp`. -/
theorem conjugateEquiv_mateEquiv_vcomp
    (Î± : lâ‚‚ âŸ¶ lâ‚) (Î² : g â‰« lâ‚ƒ âŸ¶ lâ‚‚ â‰« h) :
    (mateEquiv adjâ‚ adjâ‚ƒ) (leftAdjointConjugateSquare.vcomp Î± Î²) =
      rightAdjointConjugateSquare.vcomp (conjugateEquiv adjâ‚ adjâ‚‚ Î±) (mateEquiv adjâ‚‚ adjâ‚ƒ Î²) := by
  symm
  calc
    _ = ğŸ™ _ âŠ—â‰«
          rightAdjointSquare.vcomp
            (mateEquiv adjâ‚ adjâ‚‚ ((Î»_ lâ‚‚).hom â‰« Î± â‰« (Ï_ lâ‚).inv))
            (mateEquiv adjâ‚‚ adjâ‚ƒ Î²) âŠ—â‰« ğŸ™ _ := by
      dsimp only [conjugateEquiv_apply, rightAdjointConjugateSquare.vcomp, rightAdjointSquare.vcomp]
      bicategory
    _ = _ := by
      rw [â† mateEquiv_vcomp]
      simp only [leftAdjointSquare.vcomp, mateEquiv_apply', leftAdjointConjugateSquare.vcomp]
      bicategory

end conjugateEquiv_mateEquiv_vcomp

end Bicategory

end CategoryTheory
