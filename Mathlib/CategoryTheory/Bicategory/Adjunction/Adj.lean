/-
Copyright (c) 2025 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Bicategory.Adjunction.Mate
import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor

/-!
# The bicategory of adjunctions in a bicategory

Given a bicategory `B`, we construct a bicategory `Adj B` that has essentially
the same objects as `B` but whose `1`-morphisms are adjunctions (in the same
direction as the left adjoints), and `2`-morphisms are tuples of mate maps
between the left and right adjoints (where the map between right
adjoints is in the opposite direction).

Certain pseudofunctors to the bicategory `Adj Cat` are analogous to bifibered categories:
in various contexts, this may be used in order to formalize the properties of
both pullback and pushforward functors.

## References

* https://ncatlab.org/nlab/show/2-category+of+adjunctions
* https://ncatlab.org/nlab/show/transformation+of+adjoints
* https://ncatlab.org/nlab/show/mate

-/

universe w v u

namespace CategoryTheory

namespace Bicategory

/--
The bicategory that has the same objects as a bicategory `B`, in which `1`-morphisms
are adjunctions (in the same direction as the left adjoints),
and `2`-morphisms are tuples of mate maps between the left and right
adjoints (where the map between right adjoints is in the opposite direction).
-/
structure Adj (B : Type u) [Bicategory.{w, v} B] where
  /-- If `a : Adj B`, `a.obj : B` is the underlying object of the bicategory `B`. -/
  obj : B

variable {B : Type u} [Bicategory.{w, v} B]

namespace Adj

@[simp] lemma mk_obj (b : Adj B) : mk b.obj = b := rfl

section

variable (a b : B)

/--
Given two objects `a` and `b` in a bicategory,
this is the type of adjunctions between `a` and `b`.
-/
structure Hom where
  /-- the left adjoint -/
  {l : a âŸ¶ b}
  /-- the right adjoint -/
  {r : b âŸ¶ a}
  /-- the adjunction -/
  adj : l âŠ£ r

end

@[simps! id_l id_r id_adj comp_l comp_r comp_adj]
instance : CategoryStruct (Adj B) where
  Hom a b := Hom a.obj b.obj
  id a := .mk (Adjunction.id a.obj)
  comp f g := .mk (f.adj.comp g.adj)

variable {a b c d : Adj B}

/-- A morphism between two adjunctions consists of a tuple of mate maps. -/
@[ext]
structure Homâ‚‚ (Î± Î² : a âŸ¶ b) where
  /-- the morphism between left adjoints -/
  Ï„l : Î±.l âŸ¶ Î².l
  /-- the morphism in the opposite direction between right adjoints -/
  Ï„r : Î².r âŸ¶ Î±.r
  conjugateEquiv_Ï„l : conjugateEquiv Î².adj Î±.adj Ï„l = Ï„r := by cat_disch

lemma Homâ‚‚.conjugateEquiv_symm_Ï„r {Î± Î² : a âŸ¶ b} (p : Homâ‚‚ Î± Î²) :
    (conjugateEquiv Î².adj Î±.adj).symm p.Ï„r = p.Ï„l := by
  rw [â† Homâ‚‚.conjugateEquiv_Ï„l, Equiv.symm_apply_apply]

@[simps!]
instance : CategoryStruct (a âŸ¶ b) where
  Hom Î± Î² := Homâ‚‚ Î± Î²
  id Î± :=
    { Ï„l := ğŸ™ _
      Ï„r := ğŸ™ _ }
  comp {a b c} x y :=
    { Ï„l := x.Ï„l â‰« y.Ï„l
      Ï„r := y.Ï„r â‰« x.Ï„r
      conjugateEquiv_Ï„l := by
        simp [â† conjugateEquiv_comp c.adj b.adj a.adj y.Ï„l x.Ï„l,
          Homâ‚‚.conjugateEquiv_Ï„l] }

attribute [reassoc] comp_Ï„l comp_Ï„r

@[ext]
lemma homâ‚‚_ext {Î± Î² : a âŸ¶ b} {x y : Î± âŸ¶ Î²} (hl : x.Ï„l = y.Ï„l) : x = y :=
  Homâ‚‚.ext hl (by simp only [â† Homâ‚‚.conjugateEquiv_Ï„l, hl])

instance : Category (a âŸ¶ b) where

/-- Constructor for isomorphisms between 1-morphisms in the bicategory `Adj B`. -/
@[simps]
def isoâ‚‚Mk {Î± Î² : a âŸ¶ b} (el : Î±.l â‰… Î².l) (er : Î².r â‰… Î±.r)
    (h : conjugateEquiv Î².adj Î±.adj el.hom = er.hom) :
    Î± â‰… Î² where
  hom :=
    { Ï„l := el.hom
      Ï„r := er.hom
      conjugateEquiv_Ï„l := h }
  inv :=
    { Ï„l := el.inv
      Ï„r := er.inv
      conjugateEquiv_Ï„l := by
        rw [â† cancel_mono er.hom, Iso.inv_hom_id, â† h,
          conjugateEquiv_comp, Iso.hom_inv_id, conjugateEquiv_id] }

namespace Bicategory

/-- The associator in the bicategory `Adj B`. -/
@[simps!]
def associator (Î± : a âŸ¶ b) (Î² : b âŸ¶ c) (Î³ : c âŸ¶ d) : (Î± â‰« Î²) â‰« Î³ â‰… Î± â‰« Î² â‰« Î³ :=
  isoâ‚‚Mk (Î±_ _ _ _) (Î±_ _ _ _) (conjugateEquiv_associator_hom _ _ _)

/-- The left unitor in the bicategory `Adj B`. -/
@[simps!]
def leftUnitor (Î± : a âŸ¶ b) : ğŸ™ a â‰« Î± â‰… Î± :=
  isoâ‚‚Mk (Î»_ _) (Ï_ _).symm
    (by simpa using conjugateEquiv_id_comp_right_apply Î±.adj Î±.adj (ğŸ™ _))

/-- The right unitor in the bicategory `Adj B`. -/
@[simps!]
def rightUnitor (Î± : a âŸ¶ b) : Î± â‰« ğŸ™ b â‰… Î± :=
  isoâ‚‚Mk (Ï_ _) (Î»_ _).symm
    (by simpa using conjugateEquiv_comp_id_right_apply Î±.adj Î±.adj (ğŸ™ _))

/-- The left whiskering in the bicategory `Adj B`. -/
@[simps]
def whiskerLeft (Î± : a âŸ¶ b) {Î² Î²' : b âŸ¶ c} (y : Î² âŸ¶ Î²') : Î± â‰« Î² âŸ¶ Î± â‰« Î²' where
  Ï„l := _ â— y.Ï„l
  Ï„r := y.Ï„r â–· _
  conjugateEquiv_Ï„l := by
    simp [conjugateEquiv_whiskerLeft, Homâ‚‚.conjugateEquiv_Ï„l]

/-- The right whiskering in the bicategory `Adj B`. -/
@[simps]
def whiskerRight {Î± Î±' : a âŸ¶ b} (x : Î± âŸ¶ Î±') (Î² : b âŸ¶ c) : Î± â‰« Î² âŸ¶ Î±' â‰« Î² where
  Ï„l := x.Ï„l â–· _
  Ï„r := _ â— x.Ï„r
  conjugateEquiv_Ï„l := by
    simp [conjugateEquiv_whiskerRight, Homâ‚‚.conjugateEquiv_Ï„l]

end Bicategory

attribute [local simp] whisker_exchange

@[simps! whiskerRight_Ï„r whiskerRight_Ï„l whiskerLeft_Ï„r whiskerLeft_Ï„l
  associator_hom_Ï„r associator_inv_Ï„r associator_hom_Ï„l associator_inv_Ï„l
  leftUnitor_hom_Ï„r leftUnitor_inv_Ï„r leftUnitor_hom_Ï„l leftUnitor_inv_Ï„l
  rightUnitor_hom_Ï„r rightUnitor_inv_Ï„r rightUnitor_hom_Ï„l rightUnitor_inv_Ï„l]
instance : Bicategory (Adj B) where
  whiskerLeft := Bicategory.whiskerLeft
  whiskerRight := Bicategory.whiskerRight
  associator := Bicategory.associator
  leftUnitor := Bicategory.leftUnitor
  rightUnitor := Bicategory.rightUnitor

/-- The forget pseudofunctor from `Adj B` to `B`. -/
@[simps]
def forgetâ‚ : Pseudofunctor (Adj B) B where
  obj a := a.obj
  map x := x.l
  mapâ‚‚ Î± := Î±.Ï„l
  mapId _ := Iso.refl _
  mapComp _ _ := Iso.refl _

-- TODO: define `forgetâ‚‚` which sends an adjunction to its right adjoint functor

/-- Given an isomorphism between two 1-morphisms in `Adj B`, this is the
underlying isomorphisms between the left adjoints. -/
@[simps]
def lIso {a b : Adj B} {adjâ‚ adjâ‚‚ : a âŸ¶ b} (e : adjâ‚ â‰… adjâ‚‚) : adjâ‚.l â‰… adjâ‚‚.l where
  hom := e.hom.Ï„l
  inv := e.inv.Ï„l
  hom_inv_id := by rw [â† comp_Ï„l, e.hom_inv_id, id_Ï„l]
  inv_hom_id := by rw [â† comp_Ï„l, e.inv_hom_id, id_Ï„l]

/-- Given an isomorphism between two 1-morphisms in `Adj B`, this is the
underlying isomorphisms between the right adjoints. -/
@[simps]
def rIso {a b : Adj B} {adjâ‚ adjâ‚‚ : a âŸ¶ b} (e : adjâ‚ â‰… adjâ‚‚) : adjâ‚.r â‰… adjâ‚‚.r where
  hom := e.inv.Ï„r
  inv := e.hom.Ï„r
  hom_inv_id := by rw [â† comp_Ï„r, e.hom_inv_id, id_Ï„r]
  inv_hom_id := by rw [â† comp_Ï„r, e.inv_hom_id, id_Ï„r]

end Adj

end Bicategory

end CategoryTheory
