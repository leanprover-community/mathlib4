/-
Copyright (c) 2025 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Bicategory.Adjunction.Mate
import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor
import Mathlib.CategoryTheory.Bicategory.Opposite
import Mathlib.CategoryTheory.Bicategory.Functor.Strict

/-!
# The bicategory of adjunctions in a bicategory

Given a bicategory `B`, we construct a bicategory `Adj B` that has the same
objects but whose `1`-morphisms are adjunctions (in the same direction
as the left adjoints), and `2`-morphisms are tuples of mate maps between
the left and right adjoints (where the map between right
adjoints is in the opposite direction).

Certain pseudofunctors to the bicategory `Adj Cat` are analogous to bifibered categories:
in various contexts, this may be used in order to formalize the properties of
both pullback and pushforward functors.

## References

* https://ncatlab.org/nlab/show/2-category+of+adjunctions
* https://ncatlab.org/nlab/show/transformation+of+adjoints
* https://ncatlab.org/nlab/show/mate

-/

universe w v u

namespace CategoryTheory

namespace Bicategory

variable {B : Type u} [Bicategory.{w, v} B]

section

variable {a b c d : B} {lâ‚ : a âŸ¶ b} {râ‚ : b âŸ¶ a} (adjâ‚ : lâ‚ âŠ£ râ‚)
  {lâ‚‚ : c âŸ¶ d} {râ‚‚ : d âŸ¶ c} (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚)

variable {f : a âŸ¶ c} {g : b âŸ¶ d}

lemma mateEquiv_id_comp_right (Ï† : f â‰« ğŸ™ _ â‰« lâ‚‚ âŸ¶ lâ‚ â‰« g) :
    mateEquiv adjâ‚ ((Adjunction.id _).comp adjâ‚‚) Ï† =
      mateEquiv adjâ‚ adjâ‚‚ (f â— (Î»_ lâ‚‚).inv â‰« Ï†) â‰« (Ï_ _).inv â‰« (Î±_ _ _ _).hom := by
  simp only [mateEquiv_apply, Adjunction.homEquivâ‚_apply, Adjunction.homEquivâ‚‚_apply,
    Adjunction.id]
  dsimp
  bicategory

lemma mateEquiv_comp_id_right (Ï† : f â‰« lâ‚‚ â‰« ğŸ™ d âŸ¶ lâ‚ â‰« g) :
    mateEquiv adjâ‚ (adjâ‚‚.comp (Adjunction.id _)) Ï† =
      mateEquiv adjâ‚ adjâ‚‚ ((Ï_ _).inv â‰« (Î±_ _ _ _).hom â‰« Ï†) â‰« g â— (Î»_ râ‚‚).inv := by
  simp only [mateEquiv_apply, Adjunction.homEquivâ‚_apply, Adjunction.homEquivâ‚‚_apply,
    Adjunction.id]
  dsimp
  bicategory

end

section

variable {a b : B} {l : a âŸ¶ b} {r : b âŸ¶ a} (adj : l âŠ£ r)
    {l' : a âŸ¶ b} {r' : b âŸ¶ a} (adj' : l' âŠ£ r') (Ï† : l' âŸ¶ l)

lemma conjugateEquiv_id_comp_right_apply :
    conjugateEquiv adj ((Adjunction.id _).comp adj') ((Î»_ _).hom â‰« Ï†) =
      conjugateEquiv adj adj' Ï† â‰« (Ï_ _).inv := by
  simp only [conjugateEquiv_apply, mateEquiv_id_comp_right,
    id_whiskerLeft, Category.assoc, Iso.inv_hom_id_assoc]
  bicategory

lemma conjugateEquiv_comp_id_right_apply :
    conjugateEquiv adj (adj'.comp (Adjunction.id _)) ((Ï_ _).hom â‰« Ï†) =
      conjugateEquiv adj adj' Ï† â‰« (Î»_ _).inv := by
  simp only [conjugateEquiv_apply, Category.assoc, mateEquiv_comp_id_right, id_whiskerLeft,
    Iso.inv_hom_id, Category.comp_id, Iso.hom_inv_id, Iso.cancel_iso_inv_left,
    EmbeddingLike.apply_eq_iff_eq]
  bicategory

end

section

variable {a b : B} {l : a âŸ¶ b} {r : b âŸ¶ a} (adj : l âŠ£ r)

@[simp]
lemma mateEquiv_leftUnitor_hom_rightUnitor_inv :
    mateEquiv adj adj ((Î»_ _).hom â‰« (Ï_ _).inv) = (Ï_ _).hom â‰« (Î»_ _).inv := by
  simp only [â† cancel_mono (Î»_ r).hom, â† cancel_epi (Ï_ r).inv,
    Category.assoc, Iso.inv_hom_id_assoc, Iso.inv_hom_id,
    â† conjugateEquiv_id adj, conjugateEquiv_apply, Category.id_comp]

end

section

variable {a b c : B} {lâ‚ : a âŸ¶ b} {râ‚ : b âŸ¶ a} (adjâ‚ : lâ‚ âŠ£ râ‚)
  {lâ‚‚ : b âŸ¶ c} {râ‚‚ : c âŸ¶ b} (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚)
  {lâ‚‚' : b âŸ¶ c} {râ‚‚' : c âŸ¶ b} (adjâ‚‚' : lâ‚‚' âŠ£ râ‚‚')

lemma conjugateEquiv_whiskerLeft (Ï† : lâ‚‚' âŸ¶ lâ‚‚) :
    conjugateEquiv (adjâ‚.comp adjâ‚‚) (adjâ‚.comp adjâ‚‚') (lâ‚ â— Ï†) =
      conjugateEquiv adjâ‚‚ adjâ‚‚' Ï† â–· râ‚ := by
  have := mateEquiv_hcomp adjâ‚ adjâ‚ adjâ‚‚ adjâ‚‚' ((Î»_ _).hom â‰« (Ï_ _).inv)
    ((Î»_ _).hom â‰« Ï† â‰« (Ï_ _).inv)
  dsimp [leftAdjointSquare.hcomp, rightAdjointSquare.hcomp] at this
  simp only [comp_whiskerRight, leftUnitor_whiskerRight, Category.assoc, whiskerLeft_comp,
    whiskerLeft_rightUnitor_inv, Iso.hom_inv_id, Category.comp_id, triangle_assoc,
    inv_hom_whiskerRight_assoc, Iso.inv_hom_id_assoc, mateEquiv_leftUnitor_hom_rightUnitor_inv,
    whiskerLeft_rightUnitor, triangle_assoc_comp_left_inv_assoc, Iso.hom_inv_id_assoc] at this
  simp [conjugateEquiv_apply, this]

end

section

variable {a b c : B} {lâ‚ : a âŸ¶ b} {râ‚ : b âŸ¶ a} (adjâ‚ : lâ‚ âŠ£ râ‚)
  {lâ‚' : a âŸ¶ b} {râ‚' : b âŸ¶ a} (adjâ‚' : lâ‚' âŠ£ râ‚')
  {lâ‚‚ : b âŸ¶ c} {râ‚‚ : c âŸ¶ b} (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚)
  {lâ‚‚' : b âŸ¶ c} {râ‚‚' : c âŸ¶ b} (adjâ‚‚' : lâ‚‚' âŠ£ râ‚‚')

lemma conjugateEquiv_whiskerRight (Ï† : lâ‚' âŸ¶ lâ‚) :
    conjugateEquiv (adjâ‚.comp adjâ‚‚) (adjâ‚'.comp adjâ‚‚) (Ï† â–· lâ‚‚) =
      râ‚‚ â— conjugateEquiv adjâ‚ adjâ‚' Ï† := by
  have := mateEquiv_hcomp adjâ‚ adjâ‚' adjâ‚‚ adjâ‚‚
    ((Î»_ _).hom â‰« Ï† â‰« (Ï_ _).inv) ((Î»_ _).hom â‰« (Ï_ _).inv)
  dsimp [leftAdjointSquare.hcomp, rightAdjointSquare.hcomp] at this
  simp only [comp_whiskerRight, leftUnitor_whiskerRight, Category.assoc, whiskerLeft_comp,
    whiskerLeft_rightUnitor_inv, Iso.hom_inv_id, Category.comp_id, triangle_assoc,
    inv_hom_whiskerRight_assoc, Iso.inv_hom_id_assoc, mateEquiv_leftUnitor_hom_rightUnitor_inv,
    leftUnitor_inv_whiskerRight, Iso.inv_hom_id, triangle_assoc_comp_right_assoc] at this
  simp [conjugateEquiv_apply, this]

end

section

variable {a b c d : B} {lâ‚ : a âŸ¶ b} {râ‚ : b âŸ¶ a} (adjâ‚ : lâ‚ âŠ£ râ‚)
  {lâ‚‚ : b âŸ¶ c} {râ‚‚ : c âŸ¶ b} (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚)
  {lâ‚ƒ : c âŸ¶ d} {râ‚ƒ : d âŸ¶ c} (adjâ‚ƒ : lâ‚ƒ âŠ£ râ‚ƒ)

lemma conjugateEquiv_associator_hom :
    conjugateEquiv (adjâ‚.comp (adjâ‚‚.comp adjâ‚ƒ))
      ((adjâ‚.comp adjâ‚‚).comp adjâ‚ƒ) (Î±_ _ _ _).hom = (Î±_ _ _ _).hom := by
  simp [â† cancel_epi (Ï_ ((râ‚ƒ â‰« râ‚‚) â‰« râ‚)).hom, â† cancel_mono (Î»_ (râ‚ƒ â‰« râ‚‚ â‰« râ‚)).inv,
    conjugateEquiv_apply, mateEquiv_eq_iff, Adjunction.homEquivâ‚_symm_apply,
    Adjunction.homEquivâ‚‚_apply]
  bicategory

end


variable (B) in
/--
The bicategory that has the same objects as a bicategory `B`, in which `1`-morphisms
are adjunctions (in the same direction as the left adjoints),
and `2`-morphisms are tuples of mate maps between the left and right
adjoints (where the map between right adjoints is in the opposite direction).
-/
def Adj : Type u := B

namespace Adj

/-- If `a : Adj B`, `a.obj : B` is the underlying object of `B`. -/
abbrev obj (a : Adj B) : B := a

variable (a b c d : B)

/--
Given two objects `a` and `b` in a bicategory,
this is the type of adjunctions between `a` and `b`.
-/
structure Hom where
  /-- the left adjoint -/
  l : a âŸ¶ b
  /-- the right adjoint -/
  r : b âŸ¶ a
  /-- the adjunction -/
  adj : l âŠ£ r

variable {a b} in
/-- Constructor for `1`-morphisms in the bicategory `Adj B`. -/
@[simps]
def Hom.mk' {l : a âŸ¶ b} {r : b âŸ¶ a} (adj : l âŠ£ r) : Hom a b where
  l := l
  r := r
  adj := adj

instance : CategoryStruct (Adj B) where
  Hom (a : B) b := Hom a b
  id (a : B) := .mk' (Adjunction.id a)
  comp f g := .mk' (f.adj.comp g.adj)

@[simp] lemma id_l (a : Adj B) : Hom.l (ğŸ™ a) = ğŸ™ a.obj := rfl
@[simp] lemma id_r (a : Adj B) : Hom.r (ğŸ™ a) = ğŸ™ a.obj := rfl
@[simp] lemma id_adj (a : Adj B) : Hom.adj (ğŸ™ a) = Adjunction.id a.obj := rfl

variable {a b c d : Adj B}

@[simp] lemma comp_l (Î± : a âŸ¶ b) (Î² : b âŸ¶ c) : (Î± â‰« Î²).l = Î±.l â‰« Î².l := rfl
@[simp] lemma comp_r (Î± : a âŸ¶ b) (Î² : b âŸ¶ c) : (Î± â‰« Î²).r = Î².r â‰« Î±.r := rfl
@[simp] lemma comp_adj (Î± : a âŸ¶ b) (Î² : b âŸ¶ c) : (Î± â‰« Î²).adj = Î±.adj.comp Î².adj := rfl

/-- A morphism between two adjunctions consists of a tuple of mate maps. -/
@[ext]
structure Homâ‚‚ (Î± Î² : a âŸ¶ b) where
  /-- the morphism between left adjoints -/
  Ï„l : Î±.l âŸ¶ Î².l
  /-- the morphism in the opposite direction between right adjoints -/
  Ï„r : Î².r âŸ¶ Î±.r
  conjugateEquiv_Ï„l : conjugateEquiv Î².adj Î±.adj Ï„l = Ï„r := by aesop_cat

namespace Homâ‚‚

variable {Î± Î² : a âŸ¶ b} (p : Homâ‚‚ Î± Î²)

lemma conjugateEquiv_symm_Ï„g :
    (conjugateEquiv Î².adj Î±.adj).symm p.Ï„r = p.Ï„l := by
  rw [â† Homâ‚‚.conjugateEquiv_Ï„l, Equiv.symm_apply_apply]

lemma homEquivâ‚‚_Ï„l_eq :
    Î±.adj.homEquivâ‚‚ ((Î»_ _).hom â‰« p.Ï„l â‰« (Ï_ _).inv) =
      Î².adj.homEquivâ‚.symm ((Ï_ _).hom â‰« p.Ï„r â‰« (Î»_ _).inv) â‰« (Î±_ _ _ _).inv := by
  symm
  rw [â† cancel_mono (Î±_ _ _ _).hom, Category.assoc, Iso.inv_hom_id,
    Category.comp_id, â† mateEquiv_eq_iff, â† p.conjugateEquiv_Ï„l,
    conjugateEquiv_apply, Category.assoc, Category.assoc, Iso.hom_inv_id_assoc,
    Iso.hom_inv_id, Category.comp_id]

lemma homEquivâ‚_Ï„l_eq :
    Î².adj.homEquivâ‚ ((Î»_ Î±.l).hom â‰« p.Ï„l â‰« (Ï_ Î².l).inv) =
      (Î±_ _ _ _).inv â‰« Î±.adj.homEquivâ‚‚.symm ((Ï_ _).hom â‰« p.Ï„r â‰« (Î»_ _).inv) := by
  symm
  rw [â† cancel_epi (Î±_ _ _ _).hom, Iso.hom_inv_id_assoc, â† mateEquiv_eq_iff',
    mateEquiv_eq_iff, homEquivâ‚‚_Ï„l_eq, Category.assoc, Iso.inv_hom_id, Category.comp_id]

end Homâ‚‚

instance : CategoryStruct (a âŸ¶ b) where
  Hom Î± Î² := Homâ‚‚ Î± Î²
  id Î± :=
    { Ï„l := ğŸ™ _
      Ï„r := ğŸ™ _ }
  comp {a b c} x y :=
    { Ï„l := x.Ï„l â‰« y.Ï„l
      Ï„r := y.Ï„r â‰« x.Ï„r
      conjugateEquiv_Ï„l := by simp [â† conjugateEquiv_comp c.adj b.adj a.adj y.Ï„l x.Ï„l,
        Homâ‚‚.conjugateEquiv_Ï„l] }

@[ext]
lemma homâ‚‚_ext {Î± Î² : a âŸ¶ b} {x y : Î± âŸ¶ Î²} (hl : x.Ï„l = y.Ï„l) : x = y :=
  Homâ‚‚.ext hl (by simp only [â† Homâ‚‚.conjugateEquiv_Ï„l, hl])

@[simp] lemma id_Ï„l (Î± : a âŸ¶ b) : Homâ‚‚.Ï„l (ğŸ™ Î±) = ğŸ™ Î±.l := rfl
@[simp] lemma id_Ï„r (Î± : a âŸ¶ b) : Homâ‚‚.Ï„r (ğŸ™ Î±) = ğŸ™ Î±.r := rfl

section

variable {Î± Î² Î³ : a âŸ¶ b}

@[simp, reassoc] lemma comp_Ï„l (x : Î± âŸ¶ Î²) (y : Î² âŸ¶ Î³) : (x â‰« y).Ï„l = x.Ï„l â‰« y.Ï„l := rfl
@[simp, reassoc] lemma comp_Ï„r (x : Î± âŸ¶ Î²) (y : Î² âŸ¶ Î³) : (x â‰« y).Ï„r = y.Ï„r â‰« x.Ï„r := rfl

end

instance : Category (a âŸ¶ b) where

/-- Constructor for isomorphisms between 1-morphisms in the bicategory `Adj B`. -/
@[simps]
def isoâ‚‚Mk {Î± Î² : a âŸ¶ b} (el : Î±.l â‰… Î².l) (er : Î².r â‰… Î±.r)
    (h : conjugateEquiv Î².adj Î±.adj el.hom = er.hom) :
    Î± â‰… Î² where
  hom :=
    { Ï„l := el.hom
      Ï„r := er.hom
      conjugateEquiv_Ï„l := h }
  inv :=
    { Ï„l := el.inv
      Ï„r := er.inv
      conjugateEquiv_Ï„l := by
        rw [â† cancel_mono er.hom, Iso.inv_hom_id, â† h,
          conjugateEquiv_comp, Iso.hom_inv_id, conjugateEquiv_id] }

/-- The associator in the bicategory `Adj B`. -/
@[simps!]
def associator (Î± : a âŸ¶ b) (Î² : b âŸ¶ c) (Î³ : c âŸ¶ d) : (Î± â‰« Î²) â‰« Î³ â‰… Î± â‰« Î² â‰« Î³ :=
  isoâ‚‚Mk (Î±_ _ _ _) (Î±_ _ _ _) (conjugateEquiv_associator_hom _ _ _)

/-- The left unitor in the bicategory `Adj B`. -/
@[simps!]
def leftUnitor (Î± : a âŸ¶ b) : ğŸ™ a â‰« Î± â‰… Î± :=
  isoâ‚‚Mk (Î»_ _) (Ï_ _).symm
    (by simpa using conjugateEquiv_id_comp_right_apply Î±.adj Î±.adj (ğŸ™ _))

/-- The right unitor in the bicategory `Adj B`. -/
@[simps!]
def rightUnitor (Î± : a âŸ¶ b) : Î± â‰« ğŸ™ b â‰… Î± :=
  isoâ‚‚Mk (Ï_ _) (Î»_ _).symm
    (by simpa using conjugateEquiv_comp_id_right_apply Î±.adj Î±.adj (ğŸ™ _) )

/-- The left whiskering in the bicategory `Adj B`. -/
@[simps]
def whiskerLeft (Î± : a âŸ¶ b) {Î² Î²' : b âŸ¶ c} (y : Î² âŸ¶ Î²') : Î± â‰« Î² âŸ¶ Î± â‰« Î²' where
  Ï„l := _ â— y.Ï„l
  Ï„r := y.Ï„r â–· _
  conjugateEquiv_Ï„l := by
    dsimp
    simp only [conjugateEquiv_whiskerLeft, Homâ‚‚.conjugateEquiv_Ï„l]

/-- The right whiskering in the bicategory `Adj B`. -/
@[simps]
def whiskerRight {Î± Î±' : a âŸ¶ b} (x : Î± âŸ¶ Î±') (Î² : b âŸ¶ c) : Î± â‰« Î² âŸ¶ Î±' â‰« Î² where
  Ï„l := x.Ï„l â–· _
  Ï„r := _ â— x.Ï„r
  conjugateEquiv_Ï„l := by
    dsimp
    simp only [conjugateEquiv_whiskerRight, Homâ‚‚.conjugateEquiv_Ï„l]

attribute [local simp] whisker_exchange

instance : Bicategory (Adj B) where
  whiskerLeft := whiskerLeft
  whiskerRight := whiskerRight
  associator := associator
  leftUnitor := leftUnitor
  rightUnitor := rightUnitor

@[simp] lemma whiskerRight_Ï„r' {Î± Î±' : a âŸ¶ b} (x : Î± âŸ¶ Î±') (Î² : b âŸ¶ c) :
    (x â–· Î²).Ï„r = Î².r â— x.Ï„r := rfl

@[simp] lemma whiskerRight_Ï„l' {Î± Î±' : a âŸ¶ b} (x : Î± âŸ¶ Î±') (Î² : b âŸ¶ c) :
    (x â–· Î²).Ï„l = x.Ï„l â–· Î².l := rfl

/-- The forget pseudofunctor from `Adj B` to `B`. -/
@[simps obj map mapâ‚‚ mapId mapComp]
def forgetâ‚ : Pseudofunctor (Adj B) B where
  obj a := a.obj
  map x := x.l
  mapâ‚‚ Î± := Î±.Ï„l
  mapId _ := Iso.refl _
  mapComp _ _ := Iso.refl _

-- this forgets the left adjoints
--@[simps obj map, simps -isSimp mapâ‚‚ mapId mapComp]
@[simps obj map mapâ‚‚ mapId mapComp]
def forgetâ‚‚ : Pseudofunctor (Adj B)áµ’áµ– B where
  obj a := a.unop.obj
  map x := x.unop.r
  mapâ‚‚ Î± := Î±.unop.Ï„r
  mapId _ := Iso.refl _
  mapComp _ _ := Iso.refl _

/-- Given an isomorphism between two 1-morphisms in `Adj B`, this is the
underlying isomorphisms between the left adjoints. -/
@[simps]
def lIso {a b : Adj B} {adjâ‚ adjâ‚‚ : a âŸ¶ b} (e : adjâ‚ â‰… adjâ‚‚) : adjâ‚.l â‰… adjâ‚‚.l where
  hom := e.hom.Ï„l
  inv := e.inv.Ï„l
  hom_inv_id := by rw [â† comp_Ï„l, e.hom_inv_id, id_Ï„l]
  inv_hom_id := by rw [â† comp_Ï„l, e.inv_hom_id, id_Ï„l]

@[reassoc (attr := simp)]
lemma hom_inv_Ï„l {a b : Adj B} {adjâ‚ adjâ‚‚ : a âŸ¶ b} (e : adjâ‚ â‰… adjâ‚‚) :
    e.hom.Ï„l â‰« e.inv.Ï„l = ğŸ™ _ :=
  (lIso e).hom_inv_id

@[reassoc (attr := simp)]
lemma inv_hom_Ï„l {a b : Adj B} {adjâ‚ adjâ‚‚ : a âŸ¶ b} (e : adjâ‚ â‰… adjâ‚‚) :
    e.inv.Ï„l â‰« e.hom.Ï„l = ğŸ™ _ :=
  (lIso e).inv_hom_id

/-- Given an isomorphism between two 1-morphisms in `Adj B`, this is the
underlying isomorphisms between the right adjoints. -/
@[simps]
def rIso {a b : Adj B} {adjâ‚ adjâ‚‚ : a âŸ¶ b} (e : adjâ‚ â‰… adjâ‚‚) : adjâ‚.r â‰… adjâ‚‚.r where
  hom := e.inv.Ï„r
  inv := e.hom.Ï„r
  hom_inv_id := by rw [â† comp_Ï„r, e.hom_inv_id, id_Ï„r]
  inv_hom_id := by rw [â† comp_Ï„r, e.inv_hom_id, id_Ï„r]

@[reassoc (attr := simp)]
lemma hom_inv_Ï„r {a b : Adj B} {adjâ‚ adjâ‚‚ : a âŸ¶ b} (e : adjâ‚ â‰… adjâ‚‚) :
    e.hom.Ï„r â‰« e.inv.Ï„r = ğŸ™ _ :=
  (rIso e).inv_hom_id

@[reassoc (attr := simp)]
lemma inv_hom_Ï„r {a b : Adj B} {adjâ‚ adjâ‚‚ : a âŸ¶ b} (e : adjâ‚ â‰… adjâ‚‚) :
    e.inv.Ï„r â‰« e.hom.Ï„r = ğŸ™ _ :=
  (rIso e).hom_inv_id

section

variable {C : Type*} [Bicategory C] (F : Pseudofunctor B (Adj C))
  {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (fg : a âŸ¶ c) (hfg : f â‰« g = fg)

lemma comp_forgetâ‚_mapComp' :
    (F.comp forgetâ‚).mapComp' f g fg hfg = lIso (F.mapComp' f g fg hfg) := by
  subst hfg
  ext
  simp [Pseudofunctor.mapComp'_eq_mapComp, forgetâ‚]

@[reassoc]
lemma unit_comp_mapComp'_hom_Ï„r_comp_counit :
    (F.map g).adj.unit â–· (F.map f).r â–· (F.map fg).l â‰«
      (Î±_ _ _ _).hom â–· _ â‰« (Î±_ _ _ _).hom â‰«
      (F.map g).l â— (F.mapComp' f g fg hfg).hom.Ï„r â–· (F.map fg).l â‰«
        (F.map g).l â— (F.map fg).adj.counit =
    (Î±_ _ _ _).hom â‰« (Î»_ _).hom â‰« (F.map f).r â— (F.mapComp' f g fg hfg).hom.Ï„l â‰«
      (Î±_ _ _ _).inv â‰« (F.map f).adj.counit â–· _ â‰« (Î»_ _).hom â‰« (Ï_ _).inv := by
  -- this proof needs some improvements...
  rw [â† cancel_mono (Ï_ _).hom, â† cancel_epi (Î±_ _ _ _).inv, â† cancel_epi (Î»_ _).inv]
  apply (F.map f).adj.homEquivâ‚.symm.injective
  simp only [Adjunction.homEquivâ‚_symm_apply]
  trans (F.mapComp' f g fg hfg).hom.Ï„l
  Â· simp only [comp_r, Category.assoc, whiskerLeft_comp, whiskerLeft_rightUnitor,
      â† Homâ‚‚.conjugateEquiv_symm_Ï„g, comp_l, comp_adj, conjugateEquiv_symm_apply',
      Adjunction.comp_unit, Adjunction.compUnit, comp_whiskerRight, whisker_assoc,
      leftUnitor_inv_whiskerRight, Iso.inv_hom_id_assoc, comp_whiskerLeft,
      pentagon_inv_hom_hom_hom_hom_assoc]
  Â· simp only [comp_l, Category.assoc, Iso.inv_hom_id, Category.comp_id, Iso.inv_hom_id_assoc,
      whiskerLeft_comp]
    trans (Î»_ _).inv â‰« ((F.map f).adj.unit â–· (F.map fg).l â‰«
      ((F.map f).l â‰« (F.map f).r) â— (F.mapComp' f g fg hfg).hom.Ï„l) â‰«
        ((Î±_ _ _ _ ).hom â‰« _ â— (Î±_ _ _ _).inv) â‰«
        ((F.map f).l â— (F.map f).adj.counit â–· (F.map g).l) â‰« _ â— (Î»_ _).hom
    Â· rw [â† whisker_exchange, id_whiskerLeft, Category.assoc, Category.assoc,
        Category.assoc, Category.assoc, Iso.inv_hom_id_assoc]
      trans (F.mapComp' f g fg hfg).hom.Ï„l â‰« (Î»_ _).inv â–· _ â‰«
        leftZigzag (F.map f).adj.unit (F.map f).adj.counit â–· (F.map g).l â‰« (Ï_ _).hom â–· _
      Â· simp
      Â· dsimp only [leftZigzag]
        simp [-Adjunction.left_triangle, bicategoricalComp]
    Â· simp

end

end Adj

end Bicategory

end CategoryTheory
