/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Bicategory.Adjunction.Mate
import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor
import Mathlib.CategoryTheory.Bicategory.Opposite

/-!
# The bicategory of adjunctions in a bicategory

Given a bicategory `B`, we construct a bicategory `Adj B` that has the same
objects but whose `1`-morphisms are adjunctions, and `2`-morphisms are tuples
of mate maps between the left and right adjoints (where the map between right
adjoints is in the opposite direction).

Certain pseudofunctors to the bicategory `Adj Cat` are analogous to bifibered categories:
in various contexts, this may be used in order to formalize the properties of
both pushforward and pullback functors.

## References

* https://ncatlab.org/nlab/show/2-category+of+adjunctions
* https://ncatlab.org/nlab/show/transformation+of+adjoints
* https://ncatlab.org/nlab/show/mate

-/

universe w v u

namespace CategoryTheory

namespace Bicategory

variable {B : Type u} [Bicategory.{w, v} B]

namespace Adjunction

/- TODO: refactor `mateEquiv` by using `homEquivâ‚/â‚‚`.  -/
variable {a b c d : B} {l : b âŸ¶ c} {r : c âŸ¶ b} (adj : l âŠ£ r)

@[simps -isSimp]
def homEquivâ‚ {g : b âŸ¶ d} {h : c âŸ¶ d} : (g âŸ¶ l â‰« h) â‰ƒ (r â‰« g âŸ¶ h) where
  toFun Î³ := r â— Î³ â‰« (Î±_ _ _ _).inv â‰« adj.counit â–· h â‰« (Î»_ _).hom
  invFun Î² := (Î»_ _).inv â‰« adj.unit â–· _ â‰« (Î±_ _ _ _).hom â‰« l â— Î²
  left_inv Î³ :=
    calc
      _ = ğŸ™ _ âŠ—â‰« (adj.unit â–· g â‰« (l â‰« r) â— Î³) âŠ—â‰« l â— adj.counit â–· h âŠ—â‰« ğŸ™ _:= by
        bicategory
      _ = Î³ âŠ—â‰« leftZigzag adj.unit adj.counit â–· h âŠ—â‰« ğŸ™ _ := by
        rw [â† whisker_exchange]
        bicategory
      _ = Î³ := by
        rw [adj.left_triangle]
        bicategory
  right_inv Î² := by
    calc
      _ = ğŸ™ _ âŠ—â‰« r â— adj.unit â–· g âŠ—â‰« ((r â‰« l) â— Î² â‰« adj.counit â–· h) âŠ—â‰« ğŸ™ _ := by
        bicategory
      _ = ğŸ™ _ âŠ—â‰« rightZigzag adj.unit adj.counit â–· g âŠ—â‰« Î² := by
        rw [whisker_exchange]
        bicategory
      _ = Î² := by
        rw [adj.right_triangle]
        bicategory

@[simps -isSimp]
def homEquivâ‚‚ {g : a âŸ¶ b} {h : a âŸ¶ c} : (g â‰« l âŸ¶ h) â‰ƒ (g âŸ¶ h â‰« r) where
  toFun Î± := (Ï_ _).inv â‰« g â— adj.unit â‰« (Î±_ _ _ _).inv â‰« Î± â–· r
  invFun Î³ := Î³ â–· l â‰« (Î±_ _ _ _ ).hom â‰« h â— adj.counit â‰« (Ï_ _).hom
  left_inv Î± :=
    calc
      _ = ğŸ™ _ âŠ—â‰« g â— adj.unit â–· l âŠ—â‰« (Î± â–· (r â‰« l) â‰« h â— adj.counit) âŠ—â‰« ğŸ™ _ := by
        bicategory
      _ = ğŸ™ _ âŠ—â‰« g â— leftZigzag adj.unit adj.counit âŠ—â‰« Î± := by
        rw [â† whisker_exchange]
        bicategory
      _ = Î± := by
        rw [adj.left_triangle]
        bicategory
  right_inv Î³ :=
    calc
      _ = ğŸ™ _ âŠ—â‰« (g â— adj.unit â‰« Î³ â–· (l â‰« r)) âŠ—â‰« h â— adj.counit â–· r âŠ—â‰« ğŸ™ _ := by
        bicategory
      _ = ğŸ™ _ âŠ—â‰« Î³ âŠ—â‰« h â— rightZigzag adj.unit adj.counit âŠ—â‰« ğŸ™ _ := by
        rw [whisker_exchange]
        bicategory
      _ = Î³ := by
        rw [adj.right_triangle]
        bicategory

end Adjunction

section

variable {a b c d : B} {lâ‚ : a âŸ¶ b} {râ‚ : b âŸ¶ a} (adjâ‚ : lâ‚ âŠ£ râ‚)
  {lâ‚‚ : c âŸ¶ d} {râ‚‚ : d âŸ¶ c} (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚)

lemma mateEquiv_eq_trans {g : a âŸ¶ c} {h : b âŸ¶ d} :
    mateEquiv adjâ‚ adjâ‚‚ (g := g) (h := h) =
      adjâ‚‚.homEquivâ‚‚.trans
        ((Iso.homCongr (Iso.refl _) (Î±_ _ _ _)).trans adjâ‚.homEquivâ‚) := by
  ext Î³
  dsimp [mateEquiv, Adjunction.homEquivâ‚, Adjunction.homEquivâ‚‚]
  bicategory

lemma mateEquiv_eq_iff {g : a âŸ¶ c} {h : b âŸ¶ d}
    (Î± : g â‰« lâ‚‚ âŸ¶ lâ‚ â‰« h) (Î² : râ‚ â‰« g âŸ¶ h â‰« râ‚‚) :
  mateEquiv adjâ‚ adjâ‚‚ Î± = Î² â†”
    adjâ‚.homEquivâ‚.symm Î² = adjâ‚‚.homEquivâ‚‚ Î± â‰« (Î±_ _ _ _).hom := by
  conv_lhs => rw [eq_comm, â† adjâ‚.homEquivâ‚.symm.injective.eq_iff']
  simp [mateEquiv_eq_trans]

variable {f : a âŸ¶ c} {g : b âŸ¶ d}

lemma mateEquiv_id_comp_right (Ï† : f â‰« ğŸ™ _ â‰« lâ‚‚ âŸ¶ lâ‚ â‰« g) :
    mateEquiv adjâ‚ ((Adjunction.id _).comp adjâ‚‚) Ï† =
      mateEquiv adjâ‚ adjâ‚‚ (f â— (Î»_ lâ‚‚).inv â‰« Ï†) â‰« (Ï_ _).inv â‰« (Î±_ _ _ _).hom := by
  dsimp [mateEquiv_apply, Adjunction.id]
  bicategory

lemma mateEquiv_comp_id_right (Ï† : f â‰« lâ‚‚ â‰« ğŸ™ d âŸ¶ lâ‚ â‰« g) :
    mateEquiv adjâ‚ (adjâ‚‚.comp (Adjunction.id _)) Ï† =
      mateEquiv adjâ‚ adjâ‚‚ ((Ï_ _).inv â‰« (Î±_ _ _ _).hom â‰« Ï†) â‰« g â— (Î»_ râ‚‚).inv := by
  dsimp [mateEquiv_apply, Adjunction.id]
  bicategory

end

section

variable {a b : B} {l : a âŸ¶ b} {r : b âŸ¶ a} (adj : l âŠ£ r)
    {l' : a âŸ¶ b} {r' : b âŸ¶ a} (adj' : l' âŠ£ r') (Ï† : l' âŸ¶ l)

lemma conjugateEquiv_id_comp_right_apply :
    conjugateEquiv adj ((Adjunction.id _).comp adj') ((Î»_ _).hom â‰« Ï†) =
      conjugateEquiv adj adj' Ï† â‰« (Ï_ _).inv := by
  simp only [conjugateEquiv_apply, mateEquiv_id_comp_right,
    id_whiskerLeft, Category.assoc, Iso.inv_hom_id_assoc]
  bicategory

lemma conjugateEquiv_comp_id_right_apply :
    conjugateEquiv adj (adj'.comp (Adjunction.id _)) ((Ï_ _).hom â‰« Ï†) =
      conjugateEquiv adj adj' Ï† â‰« (Î»_ _).inv := by
  simp only [conjugateEquiv_apply, Category.assoc, mateEquiv_comp_id_right, id_whiskerLeft,
    Iso.inv_hom_id, Category.comp_id, Iso.hom_inv_id, Iso.cancel_iso_inv_left,
    EmbeddingLike.apply_eq_iff_eq]
  bicategory

end

section

variable {a b : B} {l : a âŸ¶ b} {r : b âŸ¶ a} (adj : l âŠ£ r)

@[simp]
lemma mateEquiv_leftUnitor_hom_rightUnitor_inv :
    mateEquiv adj adj ((Î»_ _).hom â‰« (Ï_ _).inv) = (Ï_ _).hom â‰« (Î»_ _).inv := by
  simp only [â† cancel_mono (Î»_ r).hom, â† cancel_epi (Ï_ r).inv,
    Category.assoc, Iso.inv_hom_id_assoc, Iso.inv_hom_id,
    â† conjugateEquiv_id adj, conjugateEquiv_apply, Category.id_comp]

end

section

variable {a b c : B} {lâ‚ : a âŸ¶ b} {râ‚ : b âŸ¶ a} (adjâ‚ : lâ‚ âŠ£ râ‚)
  {lâ‚‚ : b âŸ¶ c} {râ‚‚ : c âŸ¶ b} (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚)
  {lâ‚‚' : b âŸ¶ c} {râ‚‚' : c âŸ¶ b} (adjâ‚‚' : lâ‚‚' âŠ£ râ‚‚')

lemma conjugateEquiv_whiskerLeft (Ï† : lâ‚‚' âŸ¶ lâ‚‚) :
    conjugateEquiv (adjâ‚.comp adjâ‚‚) (adjâ‚.comp adjâ‚‚') (lâ‚ â— Ï†) =
      conjugateEquiv adjâ‚‚ adjâ‚‚' Ï† â–· râ‚ := by
  have := mateEquiv_hcomp adjâ‚ adjâ‚ adjâ‚‚ adjâ‚‚' ((Î»_ _).hom â‰« (Ï_ _).inv)
    ((Î»_ _).hom â‰« Ï† â‰« (Ï_ _).inv)
  dsimp [leftAdjointSquare.hcomp, rightAdjointSquare.hcomp] at this
  simp only [comp_whiskerRight, leftUnitor_whiskerRight, Category.assoc, whiskerLeft_comp,
    whiskerLeft_rightUnitor_inv, Iso.hom_inv_id, Category.comp_id, triangle_assoc,
    inv_hom_whiskerRight_assoc, Iso.inv_hom_id_assoc, mateEquiv_leftUnitor_hom_rightUnitor_inv,
    whiskerLeft_rightUnitor, triangle_assoc_comp_left_inv_assoc, Iso.hom_inv_id_assoc] at this
  simp [conjugateEquiv_apply, this]

end

section

variable {a b c : B} {lâ‚ : a âŸ¶ b} {râ‚ : b âŸ¶ a} (adjâ‚ : lâ‚ âŠ£ râ‚)
  {lâ‚' : a âŸ¶ b} {râ‚' : b âŸ¶ a} (adjâ‚' : lâ‚' âŠ£ râ‚')
  {lâ‚‚ : b âŸ¶ c} {râ‚‚ : c âŸ¶ b} (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚)
  {lâ‚‚' : b âŸ¶ c} {râ‚‚' : c âŸ¶ b} (adjâ‚‚' : lâ‚‚' âŠ£ râ‚‚')

lemma conjugateEquiv_whiskerRight (Ï† : lâ‚' âŸ¶ lâ‚) :
    conjugateEquiv (adjâ‚.comp adjâ‚‚) (adjâ‚'.comp adjâ‚‚) (Ï† â–· lâ‚‚) =
      râ‚‚ â— conjugateEquiv adjâ‚ adjâ‚' Ï† := by
  have := mateEquiv_hcomp adjâ‚ adjâ‚' adjâ‚‚ adjâ‚‚
    ((Î»_ _).hom â‰« Ï† â‰« (Ï_ _).inv) ((Î»_ _).hom â‰« (Ï_ _).inv)
  dsimp [leftAdjointSquare.hcomp, rightAdjointSquare.hcomp] at this
  simp only [comp_whiskerRight, leftUnitor_whiskerRight, Category.assoc, whiskerLeft_comp,
    whiskerLeft_rightUnitor_inv, Iso.hom_inv_id, Category.comp_id, triangle_assoc,
    inv_hom_whiskerRight_assoc, Iso.inv_hom_id_assoc, mateEquiv_leftUnitor_hom_rightUnitor_inv,
    leftUnitor_inv_whiskerRight, Iso.inv_hom_id, triangle_assoc_comp_right_assoc] at this
  simp [conjugateEquiv_apply, this]

end

section

variable {a b c d : B} {lâ‚ : a âŸ¶ b} {râ‚ : b âŸ¶ a} (adjâ‚ : lâ‚ âŠ£ râ‚)
  {lâ‚‚ : b âŸ¶ c} {râ‚‚ : c âŸ¶ b} (adjâ‚‚ : lâ‚‚ âŠ£ râ‚‚)
  {lâ‚ƒ : c âŸ¶ d} {râ‚ƒ : d âŸ¶ c} (adjâ‚ƒ : lâ‚ƒ âŠ£ râ‚ƒ)

lemma conjugateEquiv_associator_hom :
    conjugateEquiv (adjâ‚.comp (adjâ‚‚.comp adjâ‚ƒ))
      ((adjâ‚.comp adjâ‚‚).comp adjâ‚ƒ) (Î±_ _ _ _).hom = (Î±_ _ _ _).hom := by
  simp [â† cancel_epi (Ï_ ((râ‚ƒ â‰« râ‚‚) â‰« râ‚)).hom, â† cancel_mono (Î»_ (râ‚ƒ â‰« râ‚‚ â‰« râ‚)).inv,
    conjugateEquiv_apply, mateEquiv_eq_iff, Adjunction.homEquivâ‚_symm_apply,
    Adjunction.homEquivâ‚‚_apply]
  bicategory

end


variable (B) in
/--
The bicategory that has the same objects as a bicategory `B`, in which `1`-morphisms
are adjunctions, and `2`-morphisms are tuples of mate maps between the left and right
adjoints (where the map between right adjoints is in the opposite direction).
-/
def Adj : Type u := B

namespace Adj

abbrev obj (a : Adj B) : B := a

variable (a b c d : B)

/--
Given two objects `a` and `b` in a bicategory,
this is the type of adjunctions between `a` and `b`.
-/
structure Hom where
  /-- the left adjoint -/
  f : a âŸ¶ b
  /-- the right adjoint -/
  g : b âŸ¶ a
  /-- the adjunction -/
  adj : f âŠ£ g

variable {a b} in
def Hom.mk' {f : a âŸ¶ b} {g : b âŸ¶ a} (adj : f âŠ£ g) : Hom a b where
  f := f
  g := g
  adj := adj

instance : CategoryStruct (Adj B) where
  Hom (a : B) b := Hom a b
  id (a : B) := .mk' (Adjunction.id a)
  comp f g := .mk' (f.adj.comp g.adj)

@[simp] lemma id_f (a : Adj B) : Hom.f (ğŸ™ a) = ğŸ™ a.obj := rfl
@[simp] lemma id_g (a : Adj B) : Hom.g (ğŸ™ a) = ğŸ™ a.obj := rfl
@[simp] lemma id_adj (a : Adj B) : Hom.adj (ğŸ™ a) = Adjunction.id a.obj := rfl

variable {a b c d : Adj B}

@[simp] lemma comp_f (Î± : a âŸ¶ b) (Î² : b âŸ¶ c) : (Î± â‰« Î²).f = Î±.f â‰« Î².f := rfl
@[simp] lemma comp_g (Î± : a âŸ¶ b) (Î² : b âŸ¶ c) : (Î± â‰« Î²).g = Î².g â‰« Î±.g := rfl
@[simp] lemma comp_adj (Î± : a âŸ¶ b) (Î² : b âŸ¶ c) : (Î± â‰« Î²).adj = Î±.adj.comp Î².adj := rfl

/-- A morphism between two adjunctions consists of a tuple of mate maps. -/
@[ext]
structure Homâ‚‚ (Î± Î² : a âŸ¶ b) where
  /-- the morphism between left adjoints -/
  Ï„f : Î±.f âŸ¶ Î².f
  /-- the morphism in the opposite direction between right adjoints -/
  Ï„g : Î².g âŸ¶ Î±.g
  conjugateEquiv_Ï„f : conjugateEquiv Î².adj Î±.adj Ï„f = Ï„g := by aesop_cat

lemma Homâ‚‚.conjugateEquiv_symm_Ï„g {Î± Î² : a âŸ¶ b} (p : Homâ‚‚ Î± Î²) :
    (conjugateEquiv Î².adj Î±.adj).symm p.Ï„g = p.Ï„f := by
  rw [â† Homâ‚‚.conjugateEquiv_Ï„f, Equiv.symm_apply_apply]

instance : CategoryStruct (a âŸ¶ b) where
  Hom Î± Î² := Homâ‚‚ Î± Î²
  id Î± :=
    { Ï„f := ğŸ™ _
      Ï„g := ğŸ™ _ }
  comp {a b c} x y :=
    { Ï„f := x.Ï„f â‰« y.Ï„f
      Ï„g := y.Ï„g â‰« x.Ï„g
      conjugateEquiv_Ï„f := by simp [â† conjugateEquiv_comp c.adj b.adj a.adj y.Ï„f x.Ï„f,
        Homâ‚‚.conjugateEquiv_Ï„f] }

@[ext]
lemma homâ‚‚_ext {Î± Î² : a âŸ¶ b} {x y : Î± âŸ¶ Î²} (hf : x.Ï„f = y.Ï„f) : x = y := by
  apply Homâ‚‚.ext hf
  simp only [â† Homâ‚‚.conjugateEquiv_Ï„f, hf]

@[simp] lemma id_Ï„f (Î± : a âŸ¶ b) : Homâ‚‚.Ï„f (ğŸ™ Î±) = ğŸ™ Î±.f := rfl
@[simp] lemma id_Ï„g (Î± : a âŸ¶ b) : Homâ‚‚.Ï„g (ğŸ™ Î±) = ğŸ™ Î±.g := rfl

section

variable {Î± Î² Î³ : a âŸ¶ b}

@[simp, reassoc] lemma comp_Ï„f (x : Î± âŸ¶ Î²) (y : Î² âŸ¶ Î³) : (x â‰« y).Ï„f = x.Ï„f â‰« y.Ï„f := rfl
@[simp, reassoc] lemma comp_Ï„g (x : Î± âŸ¶ Î²) (y : Î² âŸ¶ Î³) : (x â‰« y).Ï„g = y.Ï„g â‰« x.Ï„g := rfl

end

instance : Category (a âŸ¶ b) where

/-- Constructor for isomorphisms between 1-morphisms in the bicategory `Adj B`. -/
@[simps]
def isoâ‚‚Mk {Î± Î² : a âŸ¶ b} (ef : Î±.f â‰… Î².f) (eg : Î².g â‰… Î±.g)
    (h : conjugateEquiv Î².adj Î±.adj ef.hom = eg.hom) :
    Î± â‰… Î² where
  hom :=
    { Ï„f := ef.hom
      Ï„g := eg.hom
      conjugateEquiv_Ï„f := h }
  inv :=
    { Ï„f := ef.inv
      Ï„g := eg.inv
      conjugateEquiv_Ï„f := by
        rw [â† cancel_mono eg.hom, Iso.inv_hom_id, â† h,
          conjugateEquiv_comp, Iso.hom_inv_id, conjugateEquiv_id] }

/-- The associator in the bicategory `Adj B`. -/
@[simps!]
def associator (Î± : a âŸ¶ b) (Î² : b âŸ¶ c) (Î³ : c âŸ¶ d) : (Î± â‰« Î²) â‰« Î³ â‰… Î± â‰« Î² â‰« Î³ :=
  isoâ‚‚Mk (Î±_ _ _ _) (Î±_ _ _ _) (conjugateEquiv_associator_hom _ _ _)

/-- The left unitor in the bicategory `Adj B`. -/
@[simps!]
def leftUnitor (Î± : a âŸ¶ b) : ğŸ™ a â‰« Î± â‰… Î± :=
  isoâ‚‚Mk (Î»_ _) (Ï_ _).symm
    (by simpa using conjugateEquiv_id_comp_right_apply Î±.adj Î±.adj (ğŸ™ _))

/-- The right unitor in the bicategory `Adj B`. -/
@[simps!]
def rightUnitor (Î± : a âŸ¶ b) : Î± â‰« ğŸ™ b â‰… Î± :=
  isoâ‚‚Mk (Ï_ _) (Î»_ _).symm
    (by simpa using conjugateEquiv_comp_id_right_apply Î±.adj Î±.adj (ğŸ™ _) )

/-- The left whiskering in the bicategory `Adj B`. -/
@[simps]
def whiskerLeft (Î± : a âŸ¶ b) {Î² Î²' : b âŸ¶ c} (y : Î² âŸ¶ Î²') : Î± â‰« Î² âŸ¶ Î± â‰« Î²' where
  Ï„f := _ â— y.Ï„f
  Ï„g := y.Ï„g â–· _
  conjugateEquiv_Ï„f := by
    dsimp
    simp only [conjugateEquiv_whiskerLeft, Homâ‚‚.conjugateEquiv_Ï„f]

/-- The right whiskering in the bicategory `Adj B`. -/
@[simps]
def whiskerRight {Î± Î±' : a âŸ¶ b} (x : Î± âŸ¶ Î±') (Î² : b âŸ¶ c) : Î± â‰« Î² âŸ¶ Î±' â‰« Î² where
  Ï„f := x.Ï„f â–· _
  Ï„g := _ â— x.Ï„g
  conjugateEquiv_Ï„f := by
    dsimp
    simp only [conjugateEquiv_whiskerRight, Homâ‚‚.conjugateEquiv_Ï„f]

attribute [local simp] whisker_exchange

instance : Bicategory (Adj B) where
  whiskerLeft := whiskerLeft
  whiskerRight := whiskerRight
  associator := associator
  leftUnitor := leftUnitor
  rightUnitor := rightUnitor

@[simp] lemma whiskerRight_Ï„g' {Î± Î±' : a âŸ¶ b} (x : Î± âŸ¶ Î±') (Î² : b âŸ¶ c) :
    (x â–· Î²).Ï„g = Î².g â— x.Ï„g := rfl

@[simp] lemma whiskerRight_Ï„f' {Î± Î±' : a âŸ¶ b} (x : Î± âŸ¶ Î±') (Î² : b âŸ¶ c) :
    (x â–· Î²).Ï„f = x.Ï„f â–· Î².f := rfl

-- this forgets the right adjoints
@[simps obj map, simps -isSimp mapâ‚‚ mapId mapComp]
def forgetâ‚ : Pseudofunctor (Adj B) B where
  obj a := a.obj
  map x := x.f
  mapâ‚‚ Î± := Î±.Ï„f
  mapId _ := Iso.refl _
  mapComp _ _ := Iso.refl _

-- this forgets the left adjoints
@[simps obj map, simps -isSimp mapâ‚‚ mapId mapComp]
def forgetâ‚‚ : Pseudofunctor (Adj B)áµ’áµ– B where
  obj a := a.unop.obj
  map x := x.unop.g
  mapâ‚‚ Î± := Î±.unop.Ï„g
  mapId _ := Iso.refl _
  mapComp _ _ := Iso.refl _

@[simps]
def fIso {a b : Adj B} {adjâ‚ adjâ‚‚ : a âŸ¶ b} (e : adjâ‚ â‰… adjâ‚‚) : adjâ‚.f â‰… adjâ‚‚.f where
  hom := e.hom.Ï„f
  inv := e.inv.Ï„f
  hom_inv_id := by rw [â† comp_Ï„f, e.hom_inv_id, id_Ï„f]
  inv_hom_id := by rw [â† comp_Ï„f, e.inv_hom_id, id_Ï„f]

@[simps]
def gIso {a b : Adj B} {adjâ‚ adjâ‚‚ : a âŸ¶ b} (e : adjâ‚ â‰… adjâ‚‚) : adjâ‚.g â‰… adjâ‚‚.g where
  hom := e.inv.Ï„g
  inv := e.hom.Ï„g
  hom_inv_id := by rw [â† comp_Ï„g, e.hom_inv_id, id_Ï„g]
  inv_hom_id := by rw [â† comp_Ï„g, e.inv_hom_id, id_Ï„g]

end Adj

end Bicategory

end CategoryTheory
