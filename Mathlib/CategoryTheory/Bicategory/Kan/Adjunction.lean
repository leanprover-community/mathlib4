/-
Copyright (c) 2024 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import Mathlib.CategoryTheory.Bicategory.Kan.HasKan
import Mathlib.CategoryTheory.Bicategory.Adjunction.Basic
import Mathlib.Tactic.TFAE

/-!
# Adjunctions as Kan extensions

We show that adjunctions are realized as Kan extensions or Kan lifts.

We also show that a left adjoint commutes with a left Kan extension. Under the assumption that
`IsLeftAdjoint h`, the isomorphism `fâº (g â‰« h) â‰… fâº g â‰« h` can be accessed by
`Lan.CommuteWith.lanCompIso f g h`.

## References

* [Riehl, *Category theory in context*, Proposition 6.5.2][riehl2017]

## TODO

At the moment, the results are stated for left Kan extensions and left Kan lifts. We can prove the
similar results for right Kan extensions and right Kan lifts.

-/

namespace CategoryTheory

namespace Bicategory

universe w v u

variable {B : Type u} [Bicategory.{w, v} B] {a b c : B}

section LeftExtension

open LeftExtension

/-- For an adjuntion `f âŠ£ u`, `u` is an absolute left Kan extension of the identity along `f`.
The unit of this Kan extension is given by the unit of the adjunction. -/
def Adjunction.isAbsoluteLeftKan {f : a âŸ¶ b} {u : b âŸ¶ a} (adj : f âŠ£ u) :
    IsAbsKan (.mk u adj.unit) := fun {x} h â†¦
  .mk (fun s  â†¦ LeftExtension.homMk
    (ğŸ™ _ âŠ—â‰« u â— s.unit âŠ—â‰« adj.counit â–· s.extension âŠ—â‰« ğŸ™ _ : u â‰« h âŸ¶ s.extension) <|
      calc _
        _ = ğŸ™ _ âŠ—â‰« (adj.unit â–· _ â‰« _ â— s.unit) âŠ—â‰« f â— adj.counit â–· s.extension âŠ—â‰« ğŸ™ _ := by
          dsimp only [whisker_extension, StructuredArrow.mk_right, whisker_unit,
            StructuredArrow.mk_hom_eq_self]
          bicategory
        _ = ğŸ™ _ âŠ—â‰« s.unit âŠ—â‰« leftZigzag adj.unit adj.counit â–· s.extension âŠ—â‰« ğŸ™ _ := by
          rw [â† whisker_exchange]; bicategory
        _ = s.unit := by
          rw [adj.left_triangle]; bicategory) <| by
    intro s Ï„â‚€
    ext
    /- We need to specify the type of `Ï„` to use the notation `âŠ—â‰«`. -/
    let Ï„ : u â‰« h âŸ¶ s.extension := Ï„â‚€.right
    have hÏ„ : adj.unit â–· h âŠ—â‰« f â— Ï„ = s.unit := by
      simpa [bicategoricalComp] using LeftExtension.w Ï„â‚€
    calc Ï„
      _ = ğŸ™ _ âŠ—â‰« rightZigzag adj.unit adj.counit â–· h âŠ—â‰« Ï„ âŠ—â‰« ğŸ™ _ := by
        rw [adj.right_triangle]; bicategory
      _ = ğŸ™ _ âŠ—â‰« u â— adj.unit â–· h âŠ—â‰« (adj.counit â–· _ â‰« _ â— Ï„) âŠ—â‰« ğŸ™ _ := by
        rw [rightZigzag]; bicategory
      _ = ğŸ™ _ âŠ—â‰« u â— (adj.unit â–· h âŠ—â‰« f â— Ï„) âŠ—â‰« adj.counit â–· s.extension âŠ—â‰« ğŸ™ _ := by
        rw [â† whisker_exchange]; bicategory
      _ = _ := by
        rw [hÏ„]; dsimp only [StructuredArrow.homMk_right]

/-- A left Kan extension of the identity along `f` such that `f` commutes with is a right adjoint
to `f`. The unit of this adjoint is given by the unit of the Kan extension. -/
def LeftExtension.IsKan.adjunction {f : a âŸ¶ b} {t : LeftExtension f (ğŸ™ a)}
    (H : IsKan t) (H' : IsKan (t.whisker f)) :
      f âŠ£ t.extension :=
  let Îµ : t.extension â‰« f âŸ¶ ğŸ™ b := H'.desc <| .mk _ <| (Î»_ f).hom â‰« (Ï_ f).inv
  have HÎµ : leftZigzag t.unit Îµ = (Î»_ f).hom â‰« (Ï_ f).inv := by
    simpa [leftZigzag, bicategoricalComp] using H'.fac <| .mk _ <| (Î»_ f).hom â‰« (Ï_ f).inv
  { unit := t.unit
    counit := Îµ
    left_triangle := HÎµ
    right_triangle := by
      apply (cancel_epi (Ï_ _).inv).mp
      apply H.hom_ext
      calc _
        _ = ğŸ™ _ âŠ—â‰« t.unit âŠ—â‰« f â— rightZigzag t.unit Îµ âŠ—â‰« ğŸ™ _ := by
          bicategory
        _ = ğŸ™ _ âŠ—â‰« (t.unit â–· _ â‰« _ â— t.unit) âŠ—â‰« f â— Îµ â–· t.extension âŠ—â‰« ğŸ™ _ := by
          rw [rightZigzag]; bicategory
        _ = ğŸ™ _ âŠ—â‰« t.unit âŠ—â‰« (t.unit â–· f âŠ—â‰« f â— Îµ) â–· t.extension âŠ—â‰« ğŸ™ _ := by
          rw [â† whisker_exchange]; bicategory
        _ = _ := by
          rw [â† leftZigzag, HÎµ]; bicategory }

/-- For an adjuntion `f âŠ£ u`, `u` is a left Kan extension of the identity along `f`.
The unit of this Kan extension is given by the unit of the adjunction. -/
def LeftExtension.IsAbsKan.adjunction {f : a âŸ¶ b} (t : LeftExtension f (ğŸ™ a)) (H : IsAbsKan t) :
    f âŠ£ t.extension :=
  H.isKan.adjunction (H f)

theorem isLeftAdjoint_TFAE (f : a âŸ¶ b) :
    List.TFAE [
      IsLeftAdjoint f,
      HasAbsLeftKanExtension f (ğŸ™ a),
      âˆƒ _ : HasLeftKanExtension f (ğŸ™ a), Lan.CommuteWith f (ğŸ™ a) f] := by
  tfae_have 1 â†’ 2
  | h => IsAbsKan.hasAbsLeftKanExtension (Adjunction.ofIsLeftAdjoint f).isAbsoluteLeftKan
  tfae_have 2 â†’ 3
  | h => âŸ¨inferInstance, inferInstanceâŸ©
  tfae_have 3 â†’ 1
  | âŸ¨h, h'âŸ© => .mk <| (lanIsKan f (ğŸ™ a)).adjunction <| Lan.CommuteWith.isKan f (ğŸ™ a) f
  tfae_finish

end LeftExtension

section LeftLift

open LeftLift

/-- For an adjuntion `f âŠ£ u`, `f` is an absolute left Kan lift of the identity along `u`.
The unit of this Kan lift is given by the unit of the adjunction. -/
def Adjunction.isAbsoluteLeftKanLift {f : a âŸ¶ b} {u : b âŸ¶ a} (adj : f âŠ£ u) :
    IsAbsKan (.mk f adj.unit) := fun {x} h â†¦
  .mk (fun s â†¦ LeftLift.homMk
    (ğŸ™ _ âŠ—â‰« s.unit â–· f âŠ—â‰« s.lift â— adj.counit âŠ—â‰« ğŸ™ _ : h â‰« f âŸ¶ s.lift) <|
      calc _
      _ = ğŸ™ _ âŠ—â‰« (_ â— adj.unit â‰« s.unit â–· _) âŠ—â‰« s.lift â— adj.counit â–· u âŠ—â‰« ğŸ™ _ := by
        dsimp only [whisker_lift, StructuredArrow.mk_right, whisker_unit,
          StructuredArrow.mk_hom_eq_self]
        bicategory
      _ = s.unit âŠ—â‰« s.lift â— (rightZigzag adj.unit adj.counit) âŠ—â‰« ğŸ™ _ := by
        rw [whisker_exchange, rightZigzag]; bicategory
      _ = s.unit := by
        rw [adj.right_triangle]; bicategory) <| by
      intro s Ï„â‚€
      ext
      /- We need to specify the type of `Ï„` to use the notation `âŠ—â‰«`. -/
      let Ï„ : h â‰« f âŸ¶ s.lift := Ï„â‚€.right
      have hÏ„ : h â— adj.unit âŠ—â‰« Ï„ â–· u = s.unit := by simpa [bicategoricalComp] using LeftLift.w Ï„â‚€
      calc Ï„
        _ = ğŸ™ _ âŠ—â‰« h â— leftZigzag adj.unit adj.counit âŠ—â‰« Ï„ âŠ—â‰« ğŸ™ _ := by
          rw [adj.left_triangle]; bicategory
        _ = ğŸ™ _ âŠ—â‰« h â— adj.unit â–· f âŠ—â‰« (_ â— adj.counit â‰« Ï„ â–· _) âŠ—â‰« ğŸ™ _ := by
          rw [leftZigzag]; bicategory
        _ = ğŸ™ _ âŠ—â‰« (h â— adj.unit âŠ—â‰« Ï„ â–· u) â–· f âŠ—â‰« s.lift â— adj.counit âŠ—â‰« ğŸ™ _ := by
          rw [whisker_exchange]; bicategory
        _ = _ := by
          rw [hÏ„]; dsimp only [StructuredArrow.homMk_right]

/-- A left Kan lift of the identity along `u` such that `u` commutes with is a left adjoint
to `u`. The unit of this adjoint is given by the unit of the Kan lift. -/
def LeftLift.IsKan.adjunction {u : b âŸ¶ a} {t : LeftLift u (ğŸ™ a)}
    (H : IsKan t) (H' : IsKan (t.whisker u)) :
      t.lift âŠ£ u :=
  let Îµ : u â‰« t.lift âŸ¶ ğŸ™ b := H'.desc <| .mk _ <| (Ï_ u).hom â‰« (Î»_ u).inv
  have HÎµ : rightZigzag t.unit Îµ = (Ï_ u).hom â‰« (Î»_ u).inv := by
    simpa [rightZigzag, bicategoricalComp] using H'.fac <| .mk _ <| (Ï_ u).hom â‰« (Î»_ u).inv
  { unit := t.unit
    counit := Îµ
    left_triangle := by
      apply (cancel_epi (Î»_ _).inv).mp
      apply H.hom_ext
      calc _
        _ = ğŸ™ _ âŠ—â‰« t.unit âŠ—â‰« leftZigzag t.unit Îµ â–· u âŠ—â‰« ğŸ™ _ := by
          bicategory
        _ = ğŸ™ _ âŠ—â‰« (_ â— t.unit â‰« t.unit â–· _) âŠ—â‰« t.lift â— Îµ â–· u âŠ—â‰« ğŸ™ _ := by
          rw [leftZigzag]; bicategory
        _ = ğŸ™ _ âŠ—â‰« t.unit âŠ—â‰« t.lift â— (u â— t.unit âŠ—â‰« Îµ â–· u) âŠ—â‰« ğŸ™ _ := by
          rw [whisker_exchange]; bicategory
        _ = _ := by
          rw [â† rightZigzag, HÎµ]; bicategory
    right_triangle := HÎµ }

/-- For an adjuntion `f âŠ£ u`, `f` is a left Kan lift of the identity along `u`.
The unit of this Kan lift is given by the unit of the adjunction. -/
def LeftLift.IsAbsKan.adjunction {u : b âŸ¶ a} (t : LeftLift u (ğŸ™ a)) (H : IsAbsKan t) :
    t.lift âŠ£ u :=
  H.isKan.adjunction (H u)

theorem isRightAdjoint_TFAE (u : b âŸ¶ a) :
    List.TFAE [
      IsRightAdjoint u,
      HasAbsLeftKanLift u (ğŸ™ a),
      âˆƒ _ : HasLeftKanLift u (ğŸ™ a), LanLift.CommuteWith u (ğŸ™ a) u] := by
  tfae_have 1 â†’ 2
  | h => IsAbsKan.hasAbsLeftKanLift (Adjunction.ofIsRightAdjoint u).isAbsoluteLeftKanLift
  tfae_have 2 â†’ 3
  | h => âŸ¨inferInstance, inferInstanceâŸ©
  tfae_have 3 â†’ 1
  | âŸ¨h, h'âŸ© => .mk <| (lanLiftIsKan u (ğŸ™ a)).adjunction <| LanLift.CommuteWith.isKan u (ğŸ™ a) u
  tfae_finish

end LeftLift

namespace LeftExtension

/-- A left adjoint commutes with a left Kan extension. -/
def isKanOfWhiskerLeftAdjoint
    {f : a âŸ¶ b} {g : a âŸ¶ c} {t : LeftExtension f g} (H : LeftExtension.IsKan t)
      {x : B} {h : c âŸ¶ x} {u : x âŸ¶ c} (adj : h âŠ£ u) :
        LeftExtension.IsKan (t.whisker h) :=
  let Î·' := adj.unit
  let H' : LeftLift.IsAbsKan (.mk _ Î·') := adj.isAbsoluteLeftKanLift
  .mk (fun s â†¦
    let k := s.extension
    let Î¸ := s.unit
    let sÏ„ := LeftExtension.mk _ <| ğŸ™ _ âŠ—â‰« g â— Î·' âŠ—â‰« Î¸ â–· u âŠ—â‰« ğŸ™ _
    let Ï„ : t.extension âŸ¶ k â‰« u := H.desc sÏ„
    let sÏƒ := LeftLift.mk _ <| (Ï_ _).hom â‰« Ï„
    let Ïƒ : t.extension â‰« h âŸ¶ k := H'.desc sÏƒ
    LeftExtension.homMk Ïƒ <| (H' g).hom_ext <| by
      have HÏƒ : t.extension â— Î·' âŠ—â‰« Ïƒ â–· u  = ğŸ™ _ âŠ—â‰« Ï„ := by
        simpa [bicategoricalComp] using (H' _).fac (.mk _ <| (Ï_ _).hom â‰« Ï„)
      dsimp only [LeftLift.whisker_lift, StructuredArrow.mk_right, LeftLift.whisker_unit,
        StructuredArrow.mk_hom_eq_self, whisker_extension, whisker_unit]
      calc _
        _ = (g â— Î·' â‰« t.unit â–· (h â‰« u)) âŠ—â‰« f â— Ïƒ â–· u âŠ—â‰« ğŸ™ _ := by
          bicategory
        _ = t.unit â–· (ğŸ™ c) âŠ—â‰« f â— (t.extension â— Î·' âŠ—â‰« Ïƒ â–· u) âŠ—â‰« ğŸ™ _ := by
          rw [whisker_exchange]; bicategory
        _ = (Ï_ g).hom â‰« t.unit â‰« f â— H.desc sÏ„ â‰« (Î±_ f s.extension u).inv := by
          rw [HÏƒ]
          dsimp only [Ï„]
          bicategory
        _ = _ := by
          rw [IsKan.fac_assoc]
          dsimp only [StructuredArrow.mk_right, StructuredArrow.mk_hom_eq_self, sÏ„]
          bicategory) <| by
    intro s' Ï„â‚€'
    let Ï„' : t.extension â‰« h âŸ¶ s'.extension := Ï„â‚€'.right
    have HÏ„' : t.unit â–· h âŠ—â‰« f â— Ï„' = s'.unit := by simpa [bicategoricalComp] using Ï„â‚€'.w.symm
    ext
    apply (H' _).hom_ext
    dsimp only [StructuredArrow.homMk_right]
    rw [(H' _).fac]
    apply (cancel_epi (Ï_ _).inv).mp
    apply H.hom_ext
    dsimp only [LeftLift.whisker_lift, StructuredArrow.mk_right, LeftLift.whisker_unit,
      StructuredArrow.mk_hom_eq_self]
    let Ïƒs' := LeftExtension.mk (s'.extension â‰« u)
      (ğŸ™ g âŠ—â‰« g â— Î·' âŠ—â‰« s'.unit â–· u âŠ—â‰« ğŸ™ (f â‰« s'.extension â‰« u))
    calc _
      _ = ğŸ™ _ âŠ—â‰« (t.unit â–· (ğŸ™ c) â‰« (f â‰« t.extension) â— Î·') âŠ—â‰« f â— Ï„' â–· u := by
        bicategory
      _ = ğŸ™ g âŠ—â‰« g â— Î·' âŠ—â‰« (t.unit â–· h âŠ—â‰« f â— Ï„') â–· u âŠ—â‰« ğŸ™ _ := by
        rw [â† whisker_exchange]; bicategory
      _ = t.unit â‰« f â— H.desc Ïƒs' := by
        rw [HÏ„', IsKan.fac]
        dsimp only [StructuredArrow.mk_hom_eq_self, Ïƒs']
      _ = _ := by
        bicategory

instance {f : a âŸ¶ b} {g : a âŸ¶ c} {x : B} {h : c âŸ¶ x} [IsLeftAdjoint h] [HasLeftKanExtension f g] :
    Lan.CommuteWith f g h :=
  âŸ¨âŸ¨isKanOfWhiskerLeftAdjoint (lanIsKan f g) (Adjunction.ofIsLeftAdjoint h)âŸ©âŸ©

end LeftExtension

end Bicategory

end CategoryTheory
