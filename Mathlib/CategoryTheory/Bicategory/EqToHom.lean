/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.EqToHom
import Mathlib.CategoryTheory.Bicategory.Basic

/-!
# `eqToHom` in bicategories

This file records some of the behavior of `eqToHom` 1-morphisms and
2-morphisms in bicategories.

Given an equality of objects `h : x = y` in a bicategory, there is a 1-morphism
`eqToHom h : x âŸ¶ y` just like in an ordinary category. The definitional property
of this morphism is that if `h : x = x`, `eqToHom h = ğŸ™ x`. This is
implemented as the `eqToHom` morphism in the `CategoryStruct` underlying the
bicategory.

Unlike the situation in ordinary category theory, these 1-morphisms do not
compose strictly: `eqToHom h.trans h'` is merely isomorphic to
`eqToHom h â‰« eqToHom h'`. We define this isomorphism as
`CategoryTheory.Bicategory.eqToHomTransIso`.

Given an equality of 1-morphisms, we show that various bicategorical
structure morphisms such as unitors, associators and whiskering conjugate
well under `eqToHom`s.

## TODO
* Define `eqToEquiv` that puts the `eqToHom`s in an `Equivalence` between
  objects.
-/

universe w v u

namespace CategoryTheory.Bicategory

variable {B : Type u} [Bicategory.{w, v} B]

/-- In a bicategory, `eqToHom`s do not compose strictly,
but they do up to isomorphism. -/
def eqToHomTransIso {x y z : B} (eâ‚ : x = y) (eâ‚‚ : y = z) :
    eqToHom (eâ‚.trans eâ‚‚) â‰… eqToHom eâ‚ â‰« eqToHom eâ‚‚ :=
  eâ‚‚ â–¸ eâ‚ â–¸ (Î»_ (ğŸ™ x)).symm

@[simp]
lemma eqToHomTransIso_refl_refl (x : B) :
    eqToHomTransIso (rfl : x = x) rfl = (Î»_ (ğŸ™ x)).symm :=
  rfl

lemma eqToHomTransIso_refl_right {x y : B} (eâ‚ : x = y) :
    eqToHomTransIso eâ‚ rfl = (Ï_ (eqToHom eâ‚)).symm := by
  ext
  subst eâ‚
  simp

lemma eqToHomTransIso_refl_left {x y : B} (eâ‚ : x = y) :
    eqToHomTransIso rfl eâ‚ = (Î»_ (eqToHom eâ‚)).symm := by
  ext
  subst eâ‚
  simp

@[reassoc]
lemma associator_eqToHom_hom {x y z t : B}
    (eâ‚ : x = y) (eâ‚‚ : y = z) (eâ‚ƒ : z = t) :
    (Î±_ (eqToHom eâ‚) (eqToHom eâ‚‚) (eqToHom eâ‚ƒ)).hom =
    (eqToHomTransIso eâ‚ eâ‚‚).inv â–· eqToHom eâ‚ƒ â‰«
      (eqToHomTransIso (eâ‚.trans eâ‚‚) eâ‚ƒ).inv â‰«
      (eqToHomTransIso eâ‚ (eâ‚‚.trans eâ‚ƒ)).hom â‰«
      eqToHom eâ‚ â— (eqToHomTransIso eâ‚‚ eâ‚ƒ).hom := by
  subst_vars
  simp

@[reassoc]
lemma associator_eqToHom_inv {x y z t : B}
    (eâ‚ : x = y) (eâ‚‚ : y = z) (eâ‚ƒ : z = t) :
    (Î±_ (eqToHom eâ‚) (eqToHom eâ‚‚) (eqToHom eâ‚ƒ)).inv =
    eqToHom eâ‚ â— (eqToHomTransIso eâ‚‚ eâ‚ƒ).inv â‰«
      (eqToHomTransIso eâ‚ (eâ‚‚.trans eâ‚ƒ)).inv â‰«
      (eqToHomTransIso (eâ‚.trans eâ‚‚) eâ‚ƒ).hom â‰«
      (eqToHomTransIso eâ‚ eâ‚‚).hom â–· eqToHom eâ‚ƒ := by
  subst_vars
  simp

lemma associator_hom_congr {x y z t : B} {f f' : x âŸ¶ y} {g g' : y âŸ¶ z}
    {h h' : z âŸ¶ t} (ef : f = f') (eg : g = g') (eh : h = h') :
    (Î±_ f g h).hom =
    eqToHom (by grind) â‰« (Î±_ f' g' h').hom â‰« eqToHom (by grind) := by
  subst_vars
  simp

lemma associator_inv_congr {x y z t : B} {f f' : x âŸ¶ y} {g g' : y âŸ¶ z}
    {h h' : z âŸ¶ t} (ef : f = f') (eg : g = g') (eh : h = h') :
    (Î±_ f g h).inv =
    eqToHom (by grind) â‰« (Î±_ f' g' h').inv â‰« eqToHom (by grind) := by
  subst_vars
  simp

lemma congr_whiskerLeft {x y : B} {f f' : x âŸ¶ y} (h : f = f') {z : B}
    {g g' : y âŸ¶ z} (Î· : g âŸ¶ g') :
      f â— Î· = eqToHom (by rw [h]) â‰« f' â— Î· â‰« eqToHom (by rw [h]) := by
  subst h
  simp

lemma whiskerRight_congr {y z : B} {g g' : y âŸ¶ z} (h : g = g') {x : B}
    {f f' : x âŸ¶ y} (Î· : f âŸ¶ f') :
      Î· â–· g = eqToHom (by rw [h]) â‰« Î· â–· g' â‰« eqToHom (by rw [h]) := by
  subst h
  simp

lemma leftUnitor_hom_congr {x y : B} {f f' : x âŸ¶ y} (h : f = f') :
    (Î»_ f).hom = ğŸ™ _ â— (eqToHom h)  â‰« (Î»_ f').hom â‰« eqToHom h.symm := by
  subst h
  simp

lemma leftUnitor_inv_congr {x y : B} {f f' : x âŸ¶ y} (h : f = f') :
    (Î»_ f).inv = (eqToHom h) â‰« (Î»_ f').inv â‰« ğŸ™ _ â— eqToHom h.symm := by
  subst h
  simp

lemma rightUnitor_hom_congr {x y : B} {f f' : x âŸ¶ y} (h : f = f') :
    (Ï_ f).hom = (eqToHom h) â–· ğŸ™ _  â‰« (Ï_ f').hom â‰« eqToHom h.symm := by
  subst h
  simp

lemma rightUnitor_inv_congr {x y : B} {f f' : x âŸ¶ y} (h : f = f') :
    (Ï_ f).inv = (eqToHom h) â‰« (Ï_ f').inv â‰« eqToHom h.symm â–· ğŸ™ _ := by
  subst h
  simp

end CategoryTheory.Bicategory
