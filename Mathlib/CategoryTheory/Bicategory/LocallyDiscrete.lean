/-
Copyright (c) 2022 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno, Calle S√∂nne
-/
import Mathlib.CategoryTheory.Discrete.Basic
import Mathlib.CategoryTheory.Bicategory.Strict

/-!
# Locally discrete bicategories

A category `C` can be promoted to a strict bicategory `LocallyDiscrete C`. The objects and the
1-morphisms in `LocallyDiscrete C` are the same as the objects and the morphisms, respectively,
in `C`, and the 2-morphisms in `LocallyDiscrete C` are the equalities between 1-morphisms. In
other words, the category consisting of the 1-morphisms between each pair of objects `X` and `Y`
in `LocallyDiscrete C` is defined as the discrete category associated with the type `X ‚ü∂ Y`.
-/

namespace CategoryTheory

open Bicategory Discrete

universe w‚ÇÇ w‚ÇÅ v‚ÇÇ v‚ÇÅ v u‚ÇÇ u‚ÇÅ u

section

variable {C : Type u}

/-- A wrapper for promoting any category to a bicategory,
with the only 2-morphisms being equalities.
-/
@[ext]
structure LocallyDiscrete (C : Type u) where
  /-- A wrapper for promoting any category to a bicategory,
  with the only 2-morphisms being equalities.
  -/
  as : C

namespace LocallyDiscrete

@[simp]
theorem mk_as (a : LocallyDiscrete C) : mk a.as = a := rfl

/-- `LocallyDiscrete C` is equivalent to the original type `C`. -/
@[simps]
def locallyDiscreteEquiv : LocallyDiscrete C ‚âÉ C where
  toFun := LocallyDiscrete.as
  invFun := LocallyDiscrete.mk
  left_inv := by cat_disch
  right_inv := by cat_disch

instance [DecidableEq C] : DecidableEq (LocallyDiscrete C) :=
  locallyDiscreteEquiv.decidableEq

instance [Inhabited C] : Inhabited (LocallyDiscrete C) :=
  ‚ü®‚ü®default‚ü©‚ü©

-- abbrev Hom [CategoryStruct.{v} C] (a b : LocallyDiscrete C) : Type v :=
--   Discrete (a.as ‚ü∂ b.as)

instance categoryStruct [CategoryStruct.{v} C] : CategoryStruct (LocallyDiscrete C) where
  Hom a b := Discrete (a.as ‚ü∂ b.as)
  id a := ‚ü®ùüô a.as‚ü©
  comp f g := ‚ü®f.as ‚â´ g.as‚ü©

variable [CategoryStruct.{v} C]

abbrev mkHom {a b : C} (f : a ‚ü∂ b) :
    mk a ‚ü∂ mk b :=
  ‚ü®f‚ü©

@[simp]
lemma id_as (a : LocallyDiscrete C) : (ùüô a : a ‚ü∂ a).as = ùüô a.as :=
  rfl

@[simp]
lemma comp_as {a b c : LocallyDiscrete C} (f : a ‚ü∂ b) (g : b ‚ü∂ c) : (f ‚â´ g).as = f.as ‚â´ g.as :=
  rfl

instance (priority := 900) homSmallCategory (a b : LocallyDiscrete C) : SmallCategory (a ‚ü∂ b) :=
  CategoryTheory.discreteCategory (a.as ‚ü∂ b.as)

/-- This instance is used to see through the synonym `a ‚ü∂ b = Discrete (a.as ‚ü∂ b.as)`. -/
instance subsingleton2Hom {a b : LocallyDiscrete C} (f g : a ‚ü∂ b) : Subsingleton (f ‚ü∂ g) :=
  instSubsingletonDiscreteHom f g

/-- Extract the equation from a 2-morphism in a locally discrete 2-category. -/
theorem eq_of_hom {X Y : LocallyDiscrete C} {f g : X ‚ü∂ Y} (Œ∑ : f ‚ü∂ g) : f = g :=
  Discrete.ext Œ∑.1.1

theorem eqToHom_eq_of_hom {X Y : LocallyDiscrete C} {f g : X ‚ü∂ Y} (Œ∑ : f ‚ü∂ g) :
    Œ∑ = eqToHom (eq_of_hom Œ∑) :=
  Subsingleton.elim _ _

end LocallyDiscrete

namespace LocallyDiscrete

variable [Category.{v} C]

def idIso (a : C) :
    mkHom (ùüô a) ‚âÖ ùüô (mk a) :=
  eqToIso rfl

def compIso {a b c : C} (f : a ‚ü∂ b) (g : b ‚ü∂ c) :
    mkHom (f ‚â´ g) ‚âÖ mkHom f ‚â´ mkHom g :=
  eqToIso rfl

end LocallyDiscrete

variable (C)
variable [Category.{v} C]

/-- The locally discrete bicategory on a category is a bicategory in which the objects and the
1-morphisms are the same as those in the underlying category, and the 2-morphisms are the
equalities between 1-morphisms.
-/
instance locallyDiscreteBicategory : Bicategory (LocallyDiscrete C) where
  whiskerLeft _ _ _ Œ∑ := eqToHom (congr_arg‚ÇÇ (¬∑ ‚â´ ¬∑) rfl (LocallyDiscrete.eq_of_hom Œ∑))
  whiskerRight Œ∑ _ := eqToHom (congr_arg‚ÇÇ (¬∑ ‚â´ ¬∑) (LocallyDiscrete.eq_of_hom Œ∑) rfl)
  associator f g h := eqToIso <| by apply Discrete.ext; simp
  leftUnitor f := eqToIso <| by apply Discrete.ext; simp
  rightUnitor f := eqToIso <| by apply Discrete.ext; simp

/-- A locally discrete bicategory is strict. -/
instance locallyDiscreteBicategory.strict : Strict (LocallyDiscrete C) where
  id_comp _ := Discrete.ext (Category.id_comp _)
  comp_id _ := Discrete.ext (Category.comp_id _)
  assoc _ _ _ := Discrete.ext (Category.assoc _ _ _)

end

namespace Bicategory

namespace LocallyDiscrete

theorem associator_hom {C : Type u} [Category.{v} C] {a b c d : LocallyDiscrete C}
    (f : a ‚ü∂ b) (g : b ‚ü∂ c) (h : c ‚ü∂ d) :
    (Œ±_ f g h).hom = eqToHom (Category.assoc _ _ _) :=
  rfl

theorem associator_inv {C : Type u} [Category.{v} C] {a b c d : LocallyDiscrete C}
    (f : a ‚ü∂ b) (g : b ‚ü∂ c) (h : c ‚ü∂ d) :
    (Œ±_ f g h).inv = eqToHom (Category.assoc _ _ _).symm :=
  rfl

theorem leftUnitor_hom {C : Type u} [Category.{v} C] {a b : LocallyDiscrete C} (f : a ‚ü∂ b) :
    (Œª_ f).hom = eqToHom (Category.id_comp _) :=
  rfl

theorem leftUnitor_inv {C : Type u} [Category.{v} C] {a b : LocallyDiscrete C} (f : a ‚ü∂ b) :
    (Œª_ f).inv = eqToHom (Category.id_comp _).symm :=
  rfl

theorem rightUnitor_hom {C : Type u} [Category.{v} C] {a b : LocallyDiscrete C} (f : a ‚ü∂ b) :
    (œÅ_ f).hom = eqToHom (Category.comp_id _) :=
  rfl

theorem rightUnitor_inv {C : Type u} [Category.{v} C] {a b : LocallyDiscrete C} (f : a ‚ü∂ b) :
    (œÅ_ f).inv = eqToHom (Category.comp_id _).symm :=
  rfl

end LocallyDiscrete

/-- A bicategory is locally discrete if the categories of 1-morphisms are discrete. -/
abbrev IsLocallyDiscrete (B : Type*) [Bicategory B] := ‚àÄ (b c : B), IsDiscrete (b ‚ü∂ c)

instance (C : Type*) [Category C] : IsLocallyDiscrete (LocallyDiscrete C) :=
  fun _ _ ‚Ü¶ Discrete.isDiscrete _

instance (B : Type*) [Bicategory B] [IsLocallyDiscrete B] : Strict B where
  id_comp f := obj_ext_of_isDiscrete (leftUnitor f).hom
  comp_id f := obj_ext_of_isDiscrete (rightUnitor f).hom
  assoc f g h := obj_ext_of_isDiscrete (associator f g h).hom

end Bicategory

end CategoryTheory

section

open CategoryTheory LocallyDiscrete

universe v u

namespace Quiver.Hom

variable {C : Type u} [CategoryStruct.{v} C]

/-- The 1-morphism in `LocallyDiscrete C` associated to a given morphism `f : a ‚ü∂ b` in `C` -/
@[simps]
def toLoc {a b : C} (f : a ‚ü∂ b) : LocallyDiscrete.mk a ‚ü∂ LocallyDiscrete.mk b :=
  ‚ü®f‚ü©

@[simp]
lemma id_toLoc (a : C) : (ùüô a).toLoc = ùüô (LocallyDiscrete.mk a) :=
  rfl

@[simp]
lemma comp_toLoc {a b c : C} (f : a ‚ü∂ b) (g : b ‚ü∂ c) : (f ‚â´ g).toLoc = f.toLoc ‚â´ g.toLoc :=
  rfl

end Quiver.Hom

@[simp]
lemma CategoryTheory.LocallyDiscrete.eqToHom_toLoc {C : Type u} [Category.{v} C] {a b : C}
    (h : a = b) : (eqToHom h).toLoc = eqToHom (congrArg LocallyDiscrete.mk h) := by
  subst h; rfl

end
