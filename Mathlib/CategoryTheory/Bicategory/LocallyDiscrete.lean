/-
Copyright (c) 2022 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno, Calle SÃ¶nne
-/
import Mathlib.CategoryTheory.DiscreteCategory
import Mathlib.CategoryTheory.Bicategory.Functor
import Mathlib.CategoryTheory.Bicategory.Strict

#align_import category_theory.bicategory.locally_discrete from "leanprover-community/mathlib"@"c9c9fa15fec7ca18e9ec97306fb8764bfe988a7e"

/-!
# Locally discrete bicategories

A category `C` can be promoted to a strict bicategory `LocallyDiscrete C`. The objects and the
1-morphisms in `LocallyDiscrete C` are the same as the objects and the morphisms, respectively,
in `C`, and the 2-morphisms in `LocallyDiscrete C` are the equalities between 1-morphisms. In
other words, the category consisting of the 1-morphisms between each pair of objects `X` and `Y`
in `LocallyDiscrete C` is defined as the discrete category associated with the type `X âŸ¶ Y`.
-/

namespace CategoryTheory

open Bicategory Discrete

open Bicategory


universe wâ‚‚ v vâ‚ vâ‚‚ u uâ‚ uâ‚‚

variable {C : Type u}

/-- A wrapper for promoting any category to a bicategory,
with the only 2-morphisms being equalities.
-/
@[ext]
structure LocallyDiscrete (C : Type u) where
  /-- A wrapper for promoting any category to a bicategory,
  with the only 2-morphisms being equalities.
  -/
  as : C

namespace LocallyDiscrete

@[simp]
theorem mk_as (a : LocallyDiscrete C) : mk a.as = a := rfl

/-- `LocallyDiscrete C` is equivalent to the original type `C`. -/
@[simps]
def locallyDiscreteEquiv : LocallyDiscrete C â‰ƒ C where
  toFun := LocallyDiscrete.as
  invFun := LocallyDiscrete.mk
  left_inv := by aesop_cat
  right_inv := by aesop_cat

instance [DecidableEq C] : DecidableEq (LocallyDiscrete C) :=
  locallyDiscreteEquiv.decidableEq

instance [Inhabited C] : Inhabited (LocallyDiscrete C) :=
  âŸ¨âŸ¨defaultâŸ©âŸ©

instance categoryStruct [CategoryStruct.{v} C] : CategoryStruct (LocallyDiscrete C) where
  Hom := fun a b => Discrete (a.as âŸ¶ b.as)
  id := fun a => âŸ¨ğŸ™ a.asâŸ©
  comp f g := âŸ¨f.as â‰« g.asâŸ©

variable [CategoryStruct.{v} C]

@[simp]
lemma id_as (a : LocallyDiscrete C) : (ğŸ™ a : Discrete (a.as âŸ¶ a.as)).as = ğŸ™ a.as :=
  rfl

@[simp]
lemma comp_as {a b c : LocallyDiscrete C} (f : a âŸ¶ b) (g : b âŸ¶ c) : (f â‰« g).as = f.as â‰« g.as :=
  rfl

instance (priority := 900) homSmallCategory (a b : LocallyDiscrete C) : SmallCategory (a âŸ¶ b) :=
  CategoryTheory.discreteCategory (a.as âŸ¶ b.as)
#align category_theory.locally_discrete.hom_small_category CategoryTheory.LocallyDiscrete.homSmallCategory

-- Porting note: Manually adding this instance (inferInstance doesn't work)
instance subsingleton2Hom {a b : LocallyDiscrete C} (f g : a âŸ¶ b) : Subsingleton (f âŸ¶ g) :=
  instSubsingletonDiscreteHom f g

/-- Extract the equation from a 2-morphism in a locally discrete 2-category. -/
theorem eq_of_hom {X Y : LocallyDiscrete C} {f g : X âŸ¶ Y} (Î· : f âŸ¶ g) : f = g :=
  Discrete.ext _ _ Î·.1.1
#align category_theory.locally_discrete.eq_of_hom CategoryTheory.LocallyDiscrete.eq_of_hom

end LocallyDiscrete

variable (C)
variable [Category.{v} C]

/-- The locally discrete bicategory on a category is a bicategory in which the objects and the
1-morphisms are the same as those in the underlying category, and the 2-morphisms are the
equalities between 1-morphisms.
-/
instance locallyDiscreteBicategory : Bicategory (LocallyDiscrete C) where
  whiskerLeft f g h Î· := eqToHom (congr_argâ‚‚ (Â· â‰« Â·) rfl (LocallyDiscrete.eq_of_hom Î·))
  whiskerRight Î· h := eqToHom (congr_argâ‚‚ (Â· â‰« Â·) (LocallyDiscrete.eq_of_hom Î·) rfl)
  associator f g h := eqToIso <| by apply Discrete.ext; simp
  leftUnitor f := eqToIso <| by apply Discrete.ext; simp
  rightUnitor f := eqToIso <| by apply Discrete.ext; simp
#align category_theory.locally_discrete_bicategory CategoryTheory.locallyDiscreteBicategory

/-- A locally discrete bicategory is strict. -/
instance locallyDiscreteBicategory.strict : Strict (LocallyDiscrete C) where
  id_comp f := Discrete.ext _ _ (Category.id_comp _)
  comp_id f := Discrete.ext _ _ (Category.comp_id _)
  assoc f g h := Discrete.ext _ _ (Category.assoc _ _ _)
#align category_theory.locally_discrete_bicategory.strict CategoryTheory.locallyDiscreteBicategory.strict

variable {I : Type uâ‚} [Category.{vâ‚} I] {B : Type uâ‚‚} [Bicategory.{wâ‚‚, vâ‚‚} B] [Strict B]

/--
If `B` is a strict bicategory and `I` is a (1-)category, any functor (of 1-categories) `I â¥¤ B` can
be promoted to a pseudofunctor from `LocallyDiscrete I` to `B`.
-/
@[simps]
def Functor.toPseudoFunctor (F : I â¥¤ B) : Pseudofunctor (LocallyDiscrete I) B where
  obj i := F.obj i.as
  map f := F.map f.as
  mapâ‚‚ Î· := eqToHom (congr_arg _ (LocallyDiscrete.eq_of_hom Î·))
  mapId i := eqToIso (F.map_id i.as)
  mapComp f g := eqToIso (F.map_comp f.as g.as)

/--
If `B` is a strict bicategory and `I` is a (1-)category, any functor (of 1-categories) `I â¥¤ B` can
be promoted to an oplax functor from `LocallyDiscrete I` to `B`.
-/
@[simps]
def Functor.toOplaxFunctor (F : I â¥¤ B) : OplaxFunctor (LocallyDiscrete I) B where
  obj i := F.obj i.as
  map f := F.map f.as
  mapâ‚‚ Î· := eqToHom (congr_arg _ (LocallyDiscrete.eq_of_hom Î·))
  mapId i := eqToHom (F.map_id i.as)
  mapComp f g := eqToHom (F.map_comp f.as g.as)
#align category_theory.functor.to_oplax_functor CategoryTheory.Functor.toOplaxFunctor

end CategoryTheory

section Quiver

open CategoryTheory LocallyDiscrete

universe v u

variable {C : Type u} [CategoryStruct.{v} C]

/-- The 1-morphism in `LocallyDiscrete C` associated to a given morphism `f : a âŸ¶ b` in `C` -/
@[simps]
def Quiver.Hom.toLoc {a b : C} (f : a âŸ¶ b) : LocallyDiscrete.mk a âŸ¶ LocallyDiscrete.mk b :=
  âŸ¨fâŸ©

@[simp]
lemma Quiver.Hom.id_toLoc (a : C) : (ğŸ™ a).toLoc = ğŸ™ (LocallyDiscrete.mk a) :=
  rfl

@[simp]
lemma Quiver.Hom.comp_toLoc {a b c : C} (f : a âŸ¶ b) (g : b âŸ¶ c) :
    (f â‰« g).toLoc = f.toLoc â‰« g.toLoc :=
  rfl

end Quiver
