/-
Copyright (c) 2022 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno, Junyan Xu
-/
import Mathlib.CategoryTheory.PathCategory.Basic
import Mathlib.CategoryTheory.Functor.FullyFaithful
import Mathlib.CategoryTheory.Bicategory.Free
import Mathlib.CategoryTheory.Bicategory.LocallyDiscrete

/-!
# The coherence theorem for bicategories

In this file, we prove the coherence theorem for bicategories, stated in the following form: the
free bicategory over any quiver is locally thin.

The proof is almost the same as the proof of the coherence theorem for monoidal categories that
has been previously formalized in mathlib, which is based on the proof described by Ilya Beylin
and Peter Dybjer. The idea is to view a path on a quiver as a normal form of a 1-morphism in the
free bicategory on the same quiver. A normalization procedure is then described by
`normalize : Pseudofunctor (FreeBicategory B) (LocallyDiscrete (Paths B))`, which is a
pseudofunctor from the free bicategory to the locally discrete bicategory on the path category.
It turns out that this pseudofunctor is locally an equivalence of categories, and the coherence
theorem follows immediately from this fact.

## Main statements

* `locally_thin` : the free bicategory is locally thin, that is, there is at most one
  2-morphism between two fixed 1-morphisms.

## References

* [Ilya Beylin and Peter Dybjer, Extracting a proof of coherence for monoidal categories from a
  proof of normalization for monoids][beylin1996]
-/


open Quiver (Path)

open Quiver.Path

namespace CategoryTheory

open Bicategory Category

universe v u

namespace FreeBicategory

variable {B : Type u} [Quiver.{v + 1} B]

/-- Auxiliary definition for `inclusionPath`. -/
@[simp]
def inclusionPathAux {a : B} : ‚àÄ {b : B}, Path a b ‚Üí Hom a b
  | _, nil => Hom.id a
  | _, cons p f => (inclusionPathAux p).comp (Hom.of f)

/-- Category structure on `Hom a b`. In this file, we will use `Hom a b` for `a b : B`
(precisely, `FreeBicategory.Hom a b`) instead of the definitionally equal expression
`a ‚ü∂ b` for `a b : FreeBicategory B`. The main reason is that we have to annoyingly write
`@Quiver.Hom (FreeBicategory B) _ a b` to get the latter expression when given `a b : B`. -/
local instance homCategory' (a b : B) : Category (Hom a b) :=
  homCategory a b

/-- The discrete category on the paths includes into the category of 1-morphisms in the free
bicategory.
-/
def inclusionPath (a b : B) : Discrete (Path.{v + 1} a b) ‚•§ Hom a b :=
  Discrete.functor inclusionPathAux

/-- The inclusion from the locally discrete bicategory on the path category into the free bicategory
as a prelax functor. This will be promoted to a pseudofunctor after proving the coherence theorem.
See `inclusion`.
-/
def preinclusion (B : Type u) [Quiver.{v + 1} B] :
    PrelaxFunctor (LocallyDiscrete (Paths B)) (FreeBicategory B) where
  obj a := a.as
  map {a b} f := (@inclusionPath B _ a.as b.as).obj f
  map‚ÇÇ Œ∑ := (inclusionPath _ _).map Œ∑

@[simp]
theorem preinclusion_obj (a : B) : (preinclusion B).obj ‚ü®a‚ü© = a :=
  rfl

@[simp]
theorem preinclusion_map‚ÇÇ {a b : B} (f g : Discrete (Path.{v + 1} a b)) (Œ∑ : f ‚ü∂ g) :
    (preinclusion B).map‚ÇÇ Œ∑ = eqToHom (congr_arg _ (Discrete.ext (Discrete.eq_of_hom Œ∑))) :=
  rfl

/-- The normalization of the composition of `p : Path a b` and `f : Hom b c`.
`p` will eventually be taken to be `nil` and we then get the normalization
of `f` alone, but the auxiliary `p` is necessary for Lean to accept the definition of
`normalizeIso` and the `whisker_left` case of `normalizeAux_congr` and `normalize_naturality`.
-/
@[simp]
def normalizeAux {a : B} : ‚àÄ {b c : B}, Path a b ‚Üí Hom b c ‚Üí Path a c
  | _, _, p, Hom.of f => p.cons f
  | _, _, p, Hom.id _ => p
  | _, _, p, Hom.comp f g => normalizeAux (normalizeAux p f) g

/-
We may define
```
def normalizeAux' : ‚àÄ {a b : B}, Hom a b ‚Üí Path a b
  | _, _, (Hom.of f) => f.toPath
  | _, _, (Hom.id b) => nil
  | _, _, (Hom.comp f g) => (normalizeAux' f).comp (normalizeAux' g)
```
and define `normalizeAux p f` to be `p.comp (normalizeAux' f)` and this will be
equal to the above definition, but the equality proof requires `comp_assoc`, and it
thus lacks the correct definitional property to make the definition of `normalizeIso`
typecheck.
```
example {a b c : B} (p : Path a b) (f : Hom b c) :
    normalizeAux p f = p.comp (normalizeAux' f) := by
  induction f; rfl; rfl;
  case comp _ _ _ _ _ ihf ihg => rw [normalizeAux, ihf, ihg]; apply comp_assoc
```
-/
/-- A 2-isomorphism between a partially-normalized 1-morphism in the free bicategory to the
fully-normalized 1-morphism.
-/
@[simp]
def normalizeIso {a : B} :
    ‚àÄ {b c : B} (p : Path a b) (f : Hom b c),
      (preinclusion B).map ‚ü®p‚ü© ‚â´ f ‚âÖ (preinclusion B).map ‚ü®normalizeAux p f‚ü©
  | _, _, _, Hom.of _ => Iso.refl _
  | _, _, _, Hom.id b => œÅ_ _
  | _, _, p, Hom.comp f g =>
    (Œ±_ _ _ _).symm ‚â™‚â´ whiskerRightIso (normalizeIso p f) g ‚â™‚â´ normalizeIso (normalizeAux p f) g

/-- Given a 2-morphism between `f` and `g` in the free bicategory, we have the equality
`normalizeAux p f = normalizeAux p g`.
-/
theorem normalizeAux_congr {a b c : B} (p : Path a b) {f g : Hom b c} (Œ∑ : f ‚ü∂ g) :
    normalizeAux p f = normalizeAux p g := by
  rcases Œ∑ with ‚ü®Œ∑'‚ü©
  apply @congr_fun _ _ fun p => normalizeAux p f
  clear p Œ∑
  induction Œ∑' with
  | vcomp _ _ _ _ => apply Eq.trans <;> assumption
  | whisker_left _ _ ih => funext; apply congr_fun ih
  | whisker_right _ _ ih => funext; apply congr_arg‚ÇÇ _ (congr_fun ih _) rfl
  | _ => funext; rfl

/-- The 2-isomorphism `normalizeIso p f` is natural in `f`. -/
theorem normalize_naturality {a b c : B} (p : Path a b) {f g : Hom b c} (Œ∑ : f ‚ü∂ g) :
    (preinclusion B).map ‚ü®p‚ü© ‚óÅ Œ∑ ‚â´ (normalizeIso p g).hom =
      (normalizeIso p f).hom ‚â´
        (preinclusion B).map‚ÇÇ (eqToHom (Discrete.ext (normalizeAux_congr p Œ∑))) := by
  rcases Œ∑ with ‚ü®Œ∑'‚ü©; clear Œ∑
  induction Œ∑' with
  | id => simp
  | vcomp Œ∑ Œ∏ ihf ihg =>
    simp only [mk_vcomp, whiskerLeft_comp]
    slice_lhs 2 3 => rw [ihg]
    slice_lhs 1 2 => rw [ihf]
    simp
  -- p ‚â† nil required! See the docstring of `normalizeAux`.
  | whisker_left _ _ ih =>
    dsimp
    rw [associator_inv_naturality_right_assoc, whisker_exchange_assoc, ih]
    simp
  | whisker_right h Œ∑' ih =>
    dsimp
    rw [associator_inv_naturality_middle_assoc, ‚Üê comp_whiskerRight_assoc, ih, comp_whiskerRight]
    have := dcongr_arg (fun x => (normalizeIso x h).hom) (normalizeAux_congr p (Quot.mk _ Œ∑'))
    dsimp at this; simp [this]
  | _ => simp

-- Not `@[simp]` because it is not in `simp`-normal form.
theorem normalizeAux_nil_comp {a b c : B} (f : Hom a b) (g : Hom b c) :
    normalizeAux nil (f.comp g) = (normalizeAux nil f).comp (normalizeAux nil g) := by
  induction g generalizing a with
  | id => rfl
  | of => rfl
  | comp g _ ihf ihg => erw [ihg (f.comp g), ihf f, ihg g, comp_assoc]

/-- The normalization pseudofunctor for the free bicategory on a quiver `B`. -/
def normalize (B : Type u) [Quiver.{v + 1} B] :
    Pseudofunctor (FreeBicategory B) (LocallyDiscrete (Paths B)) where
  obj a := ‚ü®a‚ü©
  map f := ‚ü®normalizeAux nil f‚ü©
  map‚ÇÇ Œ∑ := eqToHom <| Discrete.ext <| normalizeAux_congr nil Œ∑
  mapId _ := eqToIso <| Discrete.ext rfl
  mapComp f g := eqToIso <| Discrete.ext <| normalizeAux_nil_comp f g

/-- Auxiliary definition for `normalizeEquiv`. -/
def normalizeUnitIso (a b : FreeBicategory B) :
    ùü≠ (a ‚ü∂ b) ‚âÖ (normalize B).mapFunctor a b ‚ãô @inclusionPath B _ a b :=
  NatIso.ofComponents (fun f => (Œª_ f).symm ‚â™‚â´ normalizeIso nil f)
    (by
      intro f g Œ∑
      erw [leftUnitor_inv_naturality_assoc, assoc]
      congr 1
      exact normalize_naturality nil Œ∑)

/-- Normalization as an equivalence of categories. -/
def normalizeEquiv (a b : B) : Hom a b ‚âå Discrete (Path.{v + 1} a b) :=
  Equivalence.mk ((normalize _).mapFunctor a b) (inclusionPath a b) (normalizeUnitIso a b)
    (Discrete.natIso fun f => eqToIso (by
      obtain ‚ü®f‚ü© := f
      induction f with
      | nil => rfl
      | cons _ _ ih =>
        ext1 -- Porting note: `tidy` closes the goal in mathlib3 but `aesop` doesn't here.
        injection ih with ih
        conv_rhs => rw [‚Üê ih]
        rfl))

/-- The coherence theorem for bicategories. -/
instance locally_thin {a b : FreeBicategory B} : Quiver.IsThin (a ‚ü∂ b) := fun _ _ =>
  ‚ü®fun _ _ =>
    (@normalizeEquiv B _ a b).functor.map_injective (Subsingleton.elim _ _)‚ü©

/-- Auxiliary definition for `inclusion`. -/
def inclusionMapCompAux {a b : B} :
    ‚àÄ {c : B} (f : Path a b) (g : Path b c),
      (preinclusion _).map (‚ü®f‚ü© ‚â´ ‚ü®g‚ü©) ‚âÖ (preinclusion _).map ‚ü®f‚ü© ‚â´ (preinclusion _).map ‚ü®g‚ü©
  | _, f, nil => (œÅ_ ((preinclusion _).map ‚ü®f‚ü©)).symm
  | _, f, cons g‚ÇÅ g‚ÇÇ => whiskerRightIso (inclusionMapCompAux f g‚ÇÅ) (Hom.of g‚ÇÇ) ‚â™‚â´ Œ±_ _ _ _

/-- The inclusion pseudofunctor from the locally discrete bicategory on the path category into the
free bicategory.
-/
def inclusion (B : Type u) [Quiver.{v + 1} B] :
    Pseudofunctor (LocallyDiscrete (Paths B)) (FreeBicategory B) :=
  { -- All the conditions for 2-morphisms are trivial thanks to the coherence theorem!
    preinclusion B with
    mapId := fun _ => Iso.refl _
    mapComp := fun f g => inclusionMapCompAux f.as g.as }

end FreeBicategory

end CategoryTheory
