/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.Bicategory.LocallyDiscrete
import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor
import Mathlib.CategoryTheory.FiberedCategory.Fibered

/-!
# The Grothendieck construction

Given a category `ğ’®` and any pseudofunctor `F` from `ğ’®áµ’áµ–` to `Cat`, we associate to it a category
`âˆ« F`, equipped with a functor `âˆ« F â¥¤ ğ’®`.

The category `âˆ« F` is defined as follows:
* Objects: pairs `(S, a)` where `S` is an object of the base category and `a` is an object of the
  category `F(S)`.
* Morphisms: morphisms `(R, b) âŸ¶ (S, a)` are defined as pairs `(f, h)` where `f : R âŸ¶ S` is a
  morphism in `ğ’®` and `h : b âŸ¶ F(f)(a)`

The projection functor `âˆ« F â¥¤ ğ’®` is then given by projecting to the first factors, i.e.
* On objects, it sends `(S, a)` to `S`
* On morphisms, it sends `(f, h)` to `f`

## References
[Vistoli2008] "Notes on Grothendieck Topologies, Fibered Categories and Descent Theory" by
Angelo Vistoli
-/

namespace CategoryTheory

universe w vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

open CategoryTheory Functor Category Opposite Discrete Bicategory

variable {ğ’® : Type uâ‚} [Category.{vâ‚} ğ’®] {F : Pseudofunctor (LocallyDiscrete ğ’®áµ’áµ–) Cat.{vâ‚‚, uâ‚‚}}

/-- The type of objects in the fibered category associated to a presheaf valued in types. -/
@[ext]
structure Pseudofunctor.Grothendieck (F : Pseudofunctor (LocallyDiscrete ğ’®áµ’áµ–) Cat.{vâ‚‚, uâ‚‚}) where
  /-- The underlying object in the base category. -/
  base : ğ’®
  /-- The object in the fiber of the base object. -/
  fiber : F.obj âŸ¨op baseâŸ©

namespace Pseudofunctor.Grothendieck

/-- Notation for the Grothendieck category associated to a pseudofunctor `F`. -/
scoped prefix:75 "âˆ« " => Pseudofunctor.Grothendieck

/-- A morphism in the Grothendieck category `F : C â¥¤ Cat` consists of
`base : X.base âŸ¶ Y.base` and `f.fiber : (F.map base).obj X.fiber âŸ¶ Y.fiber`.
-/
structure Hom (X Y : âˆ« F) where
  /-- The morphism between base objects. -/
  base : X.base âŸ¶ Y.base
  /-- The morphism in the fiber over the domain. -/
  fiber : X.fiber âŸ¶ (F.map base.op.toLoc).obj Y.fiber

@[simps!]
instance categoryStruct : CategoryStruct (âˆ« F) where
  Hom X Y := Hom X Y
  id X := {
    base := ğŸ™ X.base
    fiber := (F.mapId âŸ¨op X.baseâŸ©).inv.app X.fiber }
  comp {_ _ Z} f g := {
    base := f.base â‰« g.base
    fiber := f.fiber â‰« (F.map f.base.op.toLoc).map g.fiber â‰«
      (F.mapComp g.base.op.toLoc f.base.op.toLoc).inv.app Z.fiber }

section

variable {a b : âˆ« F}

@[ext (iff := false)]
lemma Hom.ext (f g : a âŸ¶ b) (hfgâ‚ : f.base = g.base)
    (hfgâ‚‚ : f.fiber = g.fiber â‰« eqToHom (hfgâ‚ â–¸ rfl)) : f = g := by
  cases f; cases g
  congr
  dsimp at hfgâ‚
  rw [â† conj_eqToHom_iff_heq _ _ rfl (hfgâ‚ â–¸ rfl)]
  simpa only [eqToHom_refl, id_comp] using hfgâ‚‚

lemma Hom.ext_iff (f g : a âŸ¶ b) :
    f = g â†” âˆƒ (hfg : f.base = g.base), f.fiber = g.fiber â‰« eqToHom (hfg â–¸ rfl) where
  mp hfg := âŸ¨by rw [hfg], by simp [hfg]âŸ©
  mpr := fun âŸ¨hfgâ‚, hfgâ‚‚âŸ© => Hom.ext f g hfgâ‚ hfgâ‚‚

lemma Hom.congr {a b : âˆ« F} {f g : a âŸ¶ b} (h : f = g) :
    f.fiber = g.fiber â‰« eqToHom (h â–¸ rfl) := by
  simp [h]

end

/-- The category structure on `âˆ« F`. -/
instance category : Category (âˆ« F) where
  toCategoryStruct := Pseudofunctor.Grothendieck.categoryStruct
  id_comp {a b} f := by
    ext
    Â· simp
    Â· simp [F.mapComp_id_right_inv_app, Strict.rightUnitor_eqToIso, â† NatTrans.naturality_assoc]
  comp_id {a b} f := by
    ext
    Â· simp
    Â· simp [F.mapComp_id_left_inv_app, â† Functor.map_comp_assoc, Strict.leftUnitor_eqToIso]
  assoc f g h := by
    ext
    Â· simp
    Â· simp [â† NatTrans.naturality_assoc, F.mapComp_assoc_right_inv_app, Strict.associator_eqToIso]

variable (F)

/-- The projection `âˆ« F â¥¤ ğ’®` given by projecting both objects and homs to the first
factor. -/
@[simps]
def forget : âˆ« F â¥¤ ğ’® where
  obj X := X.base
  map f := f.base

variable {a : âˆ« F} {b : ğ’®} (f : b âŸ¶ (forget F).obj a)

/-- The domain of the cartesian lift of `f`. -/
@[simps]
def domainCartesianLift : âˆ« F :=
  âŸ¨b, (F.map f.op.toLoc).obj a.fiberâŸ©

/-- The cartesian lift of `f`. -/
@[simps]
def cartesianLift : domainCartesianLift F f âŸ¶ a :=
  âŸ¨f, ğŸ™ _âŸ©

lemma isHomLift_cartesianLift :
    IsHomLift (forget F) f (cartesianLift F f) :=
  âŸ¨IsHomLiftAux.map (p := forget F) (a := domainCartesianLift F f) âŸ¨f, ğŸ™ _âŸ©âŸ©

/-- Given some lift `g` of `f`, the canonical map from the domain of `g` to the domain of
the cartesian lift of `f`. -/
def homCartesianLift {a' : âˆ« F} (g : a' âŸ¶ a) [(forget F).IsHomLift f g] :
    a' âŸ¶ domainCartesianLift F f where
  base := eqToHom <| IsHomLift.domain_eq (forget F) f g
  fiber :=
    have : g.base = eqToHom _ â‰« f := by simpa using IsHomLift.fac' (forget F) f g
    g.fiber â‰« ((eqToIso (congrArg (fun u â†¦ F.map u.op.toLoc) this)).app a.fiber).hom â‰«
      ((F.mapComp f.op.toLoc _).app _).hom

lemma isHomLift_homCartesianLift {a' : âˆ« F} (g : a' âŸ¶ a) [(forget F).IsHomLift f g] :
    (forget F).IsHomLift (ğŸ™ b) (homCartesianLift F f g) := by
  apply IsHomLift.of_fac'
  Â· simp; rfl
  Â· apply IsHomLift.domain_eq (forget F) f g
  Â· rfl

/-- The preFibered structure on `âˆ« F`, using the forgetful functor `forget F`. -/
instance isPreFibered : IsPreFibered (forget F) := by
  refine âŸ¨fun {a b} f â†¦ âŸ¨domainCartesianLift F f, cartesianLift F f, ?_âŸ©âŸ©
  refine {cond := (isHomLift_cartesianLift F f).cond, universal_property := ?_}
  intro a' g hfg
  refine âŸ¨homCartesianLift F f g, ?_âŸ©
  simp only [categoryStruct_Hom, and_imp, homCartesianLift, cartesianLift]
  refine âŸ¨âŸ¨isHomLift_homCartesianLift _ _ _, ?_âŸ©, ?_âŸ©
  Â· exact Hom.ext _ _ (by simpa using (IsHomLift.fac' (forget F) f g).symm) (by simp)
  Â· rintro H K rfl
    apply Hom.ext
    Â· have := by simpa using IsHomLift.fac' (forget F) (ğŸ™ b) H
      simp [F.mapComp_congr rfl (congrArg (fun u â†¦ u.op.toLoc) this)]
    Â· simpa using IsHomLift.fac' (forget F) (ğŸ™ b) H

end Pseudofunctor.Grothendieck

end CategoryTheory
