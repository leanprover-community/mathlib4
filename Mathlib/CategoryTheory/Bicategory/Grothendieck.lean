/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.Bicategory.LocallyDiscrete
import Mathlib.CategoryTheory.Bicategory.NaturalTransformation.Pseudo

/-!
# The Grothendieck construction

Given a category `ğ’®` and any pseudofunctor `F` from `ğ’®áµ’áµ–` to `Cat`, we associate to it a category
`âˆ«á¶œ F`, equipped with a functor `âˆ«á¶œ F â¥¤ ğ’®`.

The category `âˆ«á¶œ F` is defined as follows:
* Objects: pairs `(S, a)` where `S` is an object of the base category and `a` is an object of the
  category `F(S)`.
* Morphisms: morphisms `(R, b) âŸ¶ (S, a)` are defined as pairs `(f, h)` where `f : R âŸ¶ S` is a
  morphism in `ğ’®` and `h : b âŸ¶ F(f)(a)`

The projection functor `âˆ«á¶œ F â¥¤ ğ’®` is then given by projecting to the first factors, i.e.
* On objects, it sends `(S, a)` to `S`
* On morphisms, it sends `(f, h)` to `f`

## Naming conventions

The name `Grothendieck` is reserved for the construction on covariant pseudofunctors from `ğ’®` to
`Cat`, whereas the word `CoGrothendieck` will be used for the contravariant construction.
This is consistent with the convention for the Grothendieck construction on 1-functors
`CategoryTheory.Grothendieck`.

## Future work / TODO

1. Once the bicategory of pseudofunctors has been defined, show that this construction forms a
pseudofunctor from `Pseudofunctor (LocallyDiscrete ğ’®) Catáµ’áµ–` to `Cat`.
2. Develop the covariant version of `CoGrothendieck` and
deduce the results in `CategoryTheory.Grothendieck` as a specialization of the
results in this file.

## References
[Vistoli2008] "Notes on Grothendieck Topologies, Fibered Categories and Descent Theory" by
Angelo Vistoli

-/

namespace CategoryTheory.Pseudofunctor

universe w vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

open Functor Category Opposite Discrete Bicategory StrongTrans

variable {ğ’® : Type uâ‚} [Category.{vâ‚} ğ’®] {F : Pseudofunctor (LocallyDiscrete ğ’®áµ’áµ–) Cat.{vâ‚‚, uâ‚‚}}

/-- The type of objects in the fibered category associated to a presheaf valued in types. -/
@[ext]
structure CoGrothendieck (F : Pseudofunctor (LocallyDiscrete ğ’®áµ’áµ–) Cat.{vâ‚‚, uâ‚‚}) where
  /-- The underlying object in the base category. -/
  base : ğ’®
  /-- The object in the fiber of the base object. -/
  fiber : F.obj âŸ¨op baseâŸ©

namespace CoGrothendieck

/-- Notation for the Grothendieck category associated to a pseudofunctor `F`. -/
scoped prefix:75 "âˆ«á¶œ " => CoGrothendieck

/-- A morphism in the Grothendieck category consists of
`base : X.base âŸ¶ Y.base` and `f.fiber : X.fiber âŸ¶ (F.map base.op.toLoc).obj Y.fiber`.
-/
structure Hom (X Y : âˆ«á¶œ F) where
  /-- The morphism between base objects. -/
  base : X.base âŸ¶ Y.base
  /-- The morphism in the fiber over the domain. -/
  fiber : X.fiber âŸ¶ (F.map base.op.toLoc).obj Y.fiber

@[simps! id_base id_fiber comp_base comp_fiber]
instance categoryStruct : CategoryStruct (âˆ«á¶œ F) where
  Hom X Y := Hom X Y
  id X := {
    base := ğŸ™ X.base
    fiber := (F.mapId âŸ¨op X.baseâŸ©).inv.app X.fiber }
  comp {_ _ Z} f g := {
    base := f.base â‰« g.base
    fiber := f.fiber â‰« (F.map f.base.op.toLoc).map g.fiber â‰«
      (F.mapComp g.base.op.toLoc f.base.op.toLoc).inv.app Z.fiber }

section

variable {a b : âˆ«á¶œ F}

@[ext (iff := false)]
lemma Hom.ext (f g : a âŸ¶ b) (hfgâ‚ : f.base = g.base)
    (hfgâ‚‚ : f.fiber = g.fiber â‰« eqToHom (hfgâ‚ â–¸ rfl)) : f = g := by
  cases f; cases g
  congr
  dsimp at hfgâ‚
  rw [â† conj_eqToHom_iff_heq _ _ rfl (hfgâ‚ â–¸ rfl)]
  simpa only [eqToHom_refl, id_comp] using hfgâ‚‚

lemma Hom.ext_iff (f g : a âŸ¶ b) :
    f = g â†” âˆƒ (hfg : f.base = g.base), f.fiber = g.fiber â‰« eqToHom (hfg â–¸ rfl) where
  mp hfg := âŸ¨by rw [hfg], by simp [hfg]âŸ©
  mpr := fun âŸ¨hfgâ‚, hfgâ‚‚âŸ© => Hom.ext f g hfgâ‚ hfgâ‚‚

lemma Hom.congr {a b : âˆ«á¶œ F} {f g : a âŸ¶ b} (h : f = g) :
    f.fiber = g.fiber â‰« eqToHom (h â–¸ rfl) := by
  simp [h]

end

attribute [local simp] PrelaxFunctor.mapâ‚‚_eqToHom in
/-- The category structure on `âˆ«á¶œ F`. -/
instance category : Category (âˆ«á¶œ F) where
  toCategoryStruct := Pseudofunctor.CoGrothendieck.categoryStruct
  id_comp {a b} f := by
    ext
    Â· simp
    Â· simp [F.mapComp_id_right_inv_app, Strict.rightUnitor_eqToIso, â† NatTrans.naturality_assoc]
  comp_id {a b} f := by
    ext
    Â· simp
    Â· simp [F.mapComp_id_left_inv_app, â† Functor.map_comp_assoc, Strict.leftUnitor_eqToIso]
  assoc f g h := by
    ext
    Â· simp
    Â· simp [â† NatTrans.naturality_assoc, F.mapComp_assoc_right_inv_app, Strict.associator_eqToIso]

variable (F)

/-- The projection `âˆ«á¶œ F â¥¤ ğ’®` given by projecting both objects and homs to the first
factor. -/
@[simps]
def forget (F : Pseudofunctor (LocallyDiscrete ğ’®áµ’áµ–) Cat.{vâ‚‚, uâ‚‚}) : âˆ«á¶œ F â¥¤ ğ’® where
  obj X := X.base
  map f := f.base

section

attribute [local simp]
  Strict.leftUnitor_eqToIso Strict.rightUnitor_eqToIso Strict.associator_eqToIso

variable {F} {G : Pseudofunctor (LocallyDiscrete ğ’®áµ’áµ–) Cat.{vâ‚‚, uâ‚‚}}
  {H : Pseudofunctor (LocallyDiscrete ğ’®áµ’áµ–) Cat.{vâ‚‚, uâ‚‚}}

/-- The Grothendieck construction is functorial: a strong natural transformation `Î± : F âŸ¶ G`
induces a functor `Grothendieck.map : âˆ«á¶œ F â¥¤ âˆ«á¶œ G`.
-/
@[simps!]
def map (Î± : F âŸ¶ G) : âˆ«á¶œ F â¥¤ âˆ«á¶œ G where
  obj a := {
    base := a.base
    fiber := (Î±.app âŸ¨op a.baseâŸ©).obj a.fiber }
  map {a b} f := {
    base := f.1
    fiber := (Î±.app âŸ¨op a.baseâŸ©).map f.2 â‰« (Î±.naturality f.1.op.toLoc).hom.app b.fiber }
  map_id a := by
    ext1
    Â· dsimp
    Â· simp [StrongTrans.naturality_id_hom_app, â† Functor.map_comp_assoc]
  map_comp {a b c} f g := by
    ext
    Â· dsimp
    Â· dsimp
      rw [StrongTrans.naturality_comp_hom_app]
      simp only [map_comp, Cat.comp_obj, Strict.associator_eqToIso,
        eqToIso_refl, Iso.refl_hom, Cat.id_app, Iso.refl_inv, id_comp, assoc, comp_id]
      slice_lhs 2 4 => simp only [â† Functor.map_comp, Iso.inv_hom_id_app, Cat.comp_obj, comp_id]
      simp [â† Functor.comp_map]

@[simp]
lemma map_id_map {x y : âˆ«á¶œ F} (f : x âŸ¶ y) : (map (ğŸ™ F)).map f = f := by
  ext <;> simp

@[simp]
theorem map_comp_forget (Î± : F âŸ¶ G) : map Î± â‹™ forget G = forget F := rfl

section

variable (F)

/-- The natural isomorphism witnessing the pseudo-unity constraint of `Grothendieck.map`. -/
def mapIdIso : map (ğŸ™ F) â‰… ğŸ­ (âˆ«á¶œ F) :=
  NatIso.ofComponents (fun _ â†¦ eqToIso (by cat_disch))

lemma map_id_eq : map (ğŸ™ F) = ğŸ­ (âˆ«á¶œ F) :=
  Functor.ext_of_iso (mapIdIso F) (fun x â†¦ by simp [map]) (fun x â†¦ by simp [mapIdIso])

end

/-- The natural isomorphism witnessing the pseudo-functoriality of `CoGrothendieck.map`. -/
def mapCompIso (Î± : F âŸ¶ G) (Î² : G âŸ¶ H) : map (Î± â‰« Î²) â‰… map Î± â‹™ map Î² :=
  NatIso.ofComponents (fun _ â†¦ eqToIso (by cat_disch)) (fun f â†¦ by
    dsimp
    simp only [comp_id, id_comp]
    ext <;> simp)

lemma map_comp_eq (Î± : F âŸ¶ G) (Î² : G âŸ¶ H) : map (Î± â‰« Î²) = map Î± â‹™ map Î² :=
  Functor.ext_of_iso (mapCompIso Î± Î²) (fun _ â†¦ by simp [map]) (fun _ â†¦ by simp [mapCompIso])

end

end Pseudofunctor.CoGrothendieck

end CategoryTheory
