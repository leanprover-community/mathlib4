/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.Bicategory.LocallyDiscrete
import Mathlib.CategoryTheory.Bicategory.NaturalTransformation.Strong

/-!
# The Grothendieck construction

Given a category `ğ’®` and any pseudofunctor `F` from `ğ’®áµ’áµ–` to `Cat`, we associate to it a category
`âˆ« F`, equipped with a functor `âˆ« F â¥¤ ğ’®`.

The category `âˆ« F` is defined as follows:
* Objects: pairs `(S, a)` where `S` is an object of the base category and `a` is an object of the
  category `F(S)`.
* Morphisms: morphisms `(R, b) âŸ¶ (S, a)` are defined as pairs `(f, h)` where `f : R âŸ¶ S` is a
  morphism in `ğ’®` and `h : b âŸ¶ F(f)(a)`

The projection functor `âˆ« F â¥¤ ğ’®` is then given by projecting to the first factors, i.e.
* On objects, it sends `(S, a)` to `S`
* On morphisms, it sends `(f, h)` to `f`

## References
[Vistoli2008] "Notes on Grothendieck Topologies, Fibered Categories and Descent Theory" by
Angelo Vistoli
-/

namespace CategoryTheory

universe w vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

open CategoryTheory Functor Category Opposite Discrete Bicategory

variable {ğ’® : Type uâ‚} [Category.{vâ‚} ğ’®] {F : Pseudofunctor (LocallyDiscrete ğ’®áµ’áµ–) Cat.{vâ‚‚, uâ‚‚}}

/-- The type of objects in the fibered category associated to a presheaf valued in types. -/
@[ext]
structure Pseudofunctor.Grothendieck (F : Pseudofunctor (LocallyDiscrete ğ’®áµ’áµ–) Cat.{vâ‚‚, uâ‚‚}) where
  /-- The underlying object in the base category. -/
  base : ğ’®
  /-- The object in the fiber of the base object. -/
  fiber : F.obj âŸ¨op baseâŸ©

namespace Pseudofunctor.Grothendieck

/-- Notation for the Grothendieck category associated to a pseudofunctor `F`. -/
scoped prefix:75 "âˆ« " => Pseudofunctor.Grothendieck

/-- A morphism in the Grothendieck category `F : C â¥¤ Cat` consists of
`base : X.base âŸ¶ Y.base` and `f.fiber : (F.map base).obj X.fiber âŸ¶ Y.fiber`.
-/
structure Hom (X Y : âˆ« F) where
  /-- The morphism between base objects. -/
  base : X.base âŸ¶ Y.base
  /-- The morphism in the fiber over the domain. -/
  fiber : X.fiber âŸ¶ (F.map base.op.toLoc).obj Y.fiber

@[simps!]
instance categoryStruct : CategoryStruct (âˆ« F) where
  Hom X Y := Hom X Y
  id X := {
    base := ğŸ™ X.base
    fiber := (F.mapId âŸ¨op X.baseâŸ©).inv.app X.fiber }
  comp {_ _ Z} f g := {
    base := f.base â‰« g.base
    fiber := f.fiber â‰« (F.map f.base.op.toLoc).map g.fiber â‰«
      (F.mapComp g.base.op.toLoc f.base.op.toLoc).inv.app Z.fiber }

section

variable {a b : âˆ« F}

@[ext (iff := false)]
lemma Hom.ext (f g : a âŸ¶ b) (hfgâ‚ : f.base = g.base)
    (hfgâ‚‚ : f.fiber = g.fiber â‰« eqToHom (hfgâ‚ â–¸ rfl)) : f = g := by
  cases f; cases g
  congr
  dsimp at hfgâ‚
  rw [â† conj_eqToHom_iff_heq _ _ rfl (hfgâ‚ â–¸ rfl)]
  simpa only [eqToHom_refl, id_comp] using hfgâ‚‚

lemma Hom.ext_iff (f g : a âŸ¶ b) :
    f = g â†” âˆƒ (hfg : f.base = g.base), f.fiber = g.fiber â‰« eqToHom (hfg â–¸ rfl) where
  mp hfg := âŸ¨by rw [hfg], by simp [hfg]âŸ©
  mpr := fun âŸ¨hfgâ‚, hfgâ‚‚âŸ© => Hom.ext f g hfgâ‚ hfgâ‚‚

lemma Hom.congr {a b : âˆ« F} {f g : a âŸ¶ b} (h : f = g) :
    f.fiber = g.fiber â‰« eqToHom (h â–¸ rfl) := by
  simp [h]

end

/-- The category structure on `âˆ« F`. -/
instance category : Category (âˆ« F) where
  toCategoryStruct := Pseudofunctor.Grothendieck.categoryStruct
  id_comp {a b} f := by
    ext
    Â· simp
    Â· simp [F.mapComp_id_right_inv_app, Strict.rightUnitor_eqToIso, â† NatTrans.naturality_assoc]
  comp_id {a b} f := by
    ext
    Â· simp
    Â· simp [F.mapComp_id_left_inv_app, â† Functor.map_comp_assoc, Strict.leftUnitor_eqToIso]
  assoc f g h := by
    ext
    Â· simp
    Â· simp [â† NatTrans.naturality_assoc, F.mapComp_assoc_right_inv_app, Strict.associator_eqToIso]

variable (F)

/-- The projection `âˆ« F â¥¤ ğ’®` given by projecting both objects and homs to the first
factor. -/
@[simps]
def forget : âˆ« F â¥¤ ğ’® where
  obj X := X.base
  map f := f.base

variable {F}
variable {G : Pseudofunctor (LocallyDiscrete ğ’®áµ’áµ–) Cat.{vâ‚‚, uâ‚‚}}
variable (Î± : F âŸ¶ G)

/-- A (strong oplax) natural transformation of pseudofunctor induces a functor between the
Grothendieck constructions. -/
@[simps]
def map (Î± : F âŸ¶ G) : âˆ« F â¥¤ âˆ« G where
  obj X :=
  { base := X.base
    fiber := (Î±.app âŸ¨op X.baseâŸ©).obj X.fiber }
  map {X Y} f :=
  { base := f.base
    fiber := (Î±.app âŸ¨op X.baseâŸ©).map f.fiber â‰« (Î±.naturality f.base.op.toLoc).hom.app Y.fiber }
  map_id X := by
    ext
    Â· simp
    Â· simp only [toOplax_toPrelaxFunctor, categoryStruct_Hom, categoryStruct_id_base, op_id,
        Quiver.Hom.id_toLoc, categoryStruct_id_fiber, eqToHom_refl, comp_id]
      rw [â† NatIso.app_inv, â† Functor.mapIso_inv, Iso.inv_comp_eq]
      symm
      rw [â† NatIso.app_inv, Iso.comp_inv_eq]
      simp only [mapIso_hom, Iso.app_hom]
      haveI := congr_arg (Â·.app X.fiber) (Î±.naturality_id âŸ¨op X.baseâŸ©)
      simp only [toOplax_toPrelaxFunctor, Cat.comp_obj, Cat.id_obj, toOplax_mapId, Cat.comp_app,
        Cat.whiskerLeft_app, Cat.whiskerRight_app, NatTrans.naturality_assoc, NatTrans.naturality,
        Cat.comp_map, Cat.id_map] at this
      rw [this]
      simp [F.mapComp_id_right_inv_app, â† Functor.map_comp_assoc, Strict.leftUnitor_eqToIso,
        Strict.rightUnitor_eqToIso]
  map_comp {X Y Z} f g := by
    ext
    Â· simp
    Â· simp only [toOplax_toPrelaxFunctor, categoryStruct_Hom, categoryStruct_comp_base, op_comp,
        Quiver.Hom.comp_toLoc, categoryStruct_comp_fiber, map_comp, assoc, eqToHom_refl, comp_id]
      congr 1
      haveI := congr_arg (Â·.app Z.fiber) (Î±.naturality_comp g.base.op.toLoc f.base.op.toLoc)
      simp only [toOplax_toPrelaxFunctor, Cat.comp_obj, toOplax_mapComp, Cat.comp_app,
        Cat.whiskerLeft_app, Cat.whiskerRight_app] at this
      conv_rhs => rw [â† NatIso.app_inv]
      symm
      slice_lhs 1 3 => rfl
      rw [Iso.comp_inv_eq]
      simp only [Iso.app_hom, assoc]
      rw [this]
      simp only [Strict.associator_eqToIso, eqToIso_refl, Iso.refl_hom, Cat.id_app, Cat.comp_obj,
        Iso.refl_inv, comp_id, id_comp]
      simp only [Category.assoc, â† Functor.map_comp, â† Functor.map_comp_assoc, Iso.inv_hom_id_app,
        Cat.comp_obj, comp_id]
      haveI := (Î±.naturality f.base.op.toLoc).hom.naturality g.fiber
      simp only [toOplax_toPrelaxFunctor, Cat.comp_obj, Cat.comp_map] at this
      rw [reassoc_of%(this)]
      simp

variable {Î±}

@[simp]
lemma map_obj (X : âˆ« F) : (map Î±).obj X = âŸ¨X.base, (Î±.app âŸ¨op X.baseâŸ©).obj X.fiberâŸ© := rfl

@[simp]
lemma map_map (X Y : âˆ« F) (f : X âŸ¶ Y) : (map Î±).map f =
    âŸ¨f.base, (Î±.app âŸ¨op X.baseâŸ©).map f.fiber â‰« (Î±.naturality f.base.op.toLoc).hom.app Y.fiberâŸ© :=
  rfl

/-- The functor `Pseudofunctor.Grothendieck.map Î±` lies over `C` -/
lemma map_comp_forget (Î± : F âŸ¶ G) :
    map Î± â‹™ forget G = forget F := rfl

/-- Making the equality of functors into an isomorphism. Note: we should avoid equality of functors
if possible, and we should prefer `mapCompIso` to `map_comp_eq` whenever we can. -/
def mapCompForgetIso (Î± : F âŸ¶ G) :
    map Î± â‹™ forget G â‰… forget F := Iso.refl _

/-- The natural transformation induced by the identity is the identity. -/
theorem map_id_eq : map (ğŸ™ F) = ğŸ­ (âˆ« F) := by
  fapply Functor.ext
  Â· intro X; rfl
  Â· intro X Y f
    ext
    Â· simp
    Â· dsimp
      simp [F.mapComp_id_left_inv, F.mapComp_id_right_inv, Strict.leftUnitor_eqToIso,
        Strict.rightUnitor_eqToIso]
      simp only [â† Functor.map_comp_assoc, â† NatTrans.naturality_assoc]
      simp only [Cat.id_obj, Cat.id_map, id_comp, eqToHom_naturality_assoc, Iso.inv_hom_id_app,
        comp_id, assoc]
      slice_rhs 3 5 => equals (F.map _).map ((F.mapId _).hom.app _) =>
        symm
        rw [conj_eqToHom_iff_heq']
        congr
        simp
      rw [â† NatIso.app_inv, â† Functor.map_comp]
      simp

/-- Making the equality of functors into an isomorphism. Note: we should avoid equality of functors
if possible, and we should prefer `mapCompIso` to `map_comp_eq` whenever we can. -/
def mapIdIso : map (ğŸ™ F) â‰… ğŸ­ (âˆ« F) := eqToIso map_id_eq

variable (Î±)

variable {H : Pseudofunctor (LocallyDiscrete ğ’®áµ’áµ–) Cat.{vâ‚‚, uâ‚‚}}
set_option maxHeartbeats 225000 in -- We need this for the second-to last simp to work.
/-- The construction `map` strictly commutes with functor composition. -/
theorem map_comp_eq (Î² : G âŸ¶ H) : map (Î± â‰« Î²) = map Î± â‹™ map Î² := by
  fapply Functor.ext
  Â· intro x
    rfl
  Â· intro X Y f
    ext
    Â· simp
    Â· dsimp
      simp only [Strict.associator_eqToIso, eqToIso_refl, Iso.refl_inv, Cat.id_app, Cat.comp_obj,
        Iso.refl_hom, comp_id, id_comp, map_comp, assoc, H.mapComp_id_left_inv,
        Strict.leftUnitor_eqToIso, eqToIso.inv, PrelaxFunctor.mapâ‚‚_eqToHom, eqToHom_naturality,
        Cat.comp_app, Cat.eqToHom_app, Cat.whiskerRight_app, Cat.id_obj, H.mapComp_id_right_inv,
        Strict.rightUnitor_eqToIso, Cat.whiskerLeft_app, eqToHom_trans_assoc]
      rw [eqToHom_map]
      slice_rhs 6 8 => equals (H.map (ğŸ™ âŸ¨op X.baseâŸ©)).map <| (H.map f.base.op.toLoc).map <|
        (H.mapId âŸ¨op Y.baseâŸ©).hom.app _ =>
        symm
        rw [conj_eqToHom_iff_heq']
        congr 1 <;> simp only [Cat.id_obj, id_comp]
        congr
        simp
      simp only [â† Cat.whiskerLeft_app, â† Cat.whiskerRight_app, â† Cat.whiskerLeft_app,
        â† NatTrans.comp_app_assoc, â† NatTrans.comp_app, â† Functor.comp_map]
      slice_rhs 1 2 => erw [â† NatTrans.naturality]
      simp only [â† Cat.whiskerLeft_app, â† Cat.whiskerRight_app, â† Cat.whiskerLeft_app,
        â† NatTrans.comp_app_assoc, â† NatTrans.comp_app, â† Functor.comp_map]
      simp only [Functor.comp_map, toOplax_toPrelaxFunctor, NatTrans.comp_app, Cat.comp_obj,
        Cat.whiskerRight_app, Cat.whiskerLeft_app, Cat.id_obj, Cat.comp_map, Cat.id_map,
        whisker_assoc, Strict.associator_eqToIso, eqToIso_refl, Iso.refl_hom, Iso.refl_inv, id_comp,
        Bicategory.whiskerLeft_comp, assoc, comp_whiskerRight, Cat.id_app, map_id,
        NatTrans.naturality, NatTrans.naturality_assoc, Iso.inv_hom_id_app_assoc]
      congr
      rw [â† NatIso.app_inv, â† Functor.map_comp]
      simp

/-- Making the equality of functors into an isomorphism. Note: we should avoid equality of functors
if possible, and we should prefer `mapCompIso` to `map_comp_eq` whenever we can. -/
def mapCompIso (Î² : G âŸ¶ H) : map (Î± â‰« Î²) â‰… map Î± â‹™ map Î² := eqToIso (map_comp_eq Î± Î²)

end Pseudofunctor.Grothendieck

end CategoryTheory
