/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor
import Mathlib.CategoryTheory.Bicategory.LocallyDiscrete
import Mathlib.CategoryTheory.Category.Cat

/-!
# The Grothendieck construction

Given a category `ð’®` and any pseudofunctor `F` from `ð’®áµ’áµ–` to `Cat`, we associate to it a category
`âˆ« F`, equipped with a functor `âˆ« F â¥¤ ð’®`.

The category `âˆ« F` is defined as follows:
* Objects: pairs `(S, a)` where `S` is an object of the base category and `a` is an object of the
  category `F(S)`.
* Morphisms: morphisms `(R, b) âŸ¶ (S, a)` are defined as pairs `(f, h)` where `f : R âŸ¶ S` is a
  morphism in `ð’®` and `h : b âŸ¶ F(f)(a)`

The projection functor `âˆ« F â¥¤ ð’®` is then given by projecting to the first factors, i.e.
* On objects, it sends `(S, a)` to `S`
* On morphisms, it sends `(f, h)` to `f`

## TODO
1. Implement functoriality for the Grothendieck construction.
2. Obtain the results in `CategoryTheory.Grothendieck` as a specialization of these results.

## References
[Vistoli2008] "Notes on Grothendieck Topologies, Fibered Categories and Descent Theory" by
Angelo Vistoli

-/

namespace CategoryTheory

universe w vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

open CategoryTheory Functor Category Opposite Discrete Bicategory

variable {ð’® : Type uâ‚} [Category.{vâ‚} ð’®] {F : Pseudofunctor (LocallyDiscrete ð’®áµ’áµ–) Cat.{vâ‚‚, uâ‚‚}}

/-- The type of objects in the fibered category associated to a presheaf valued in types. -/
structure Pseudofunctor.Grothendieck (F : Pseudofunctor (LocallyDiscrete ð’®áµ’áµ–) Cat.{vâ‚‚, uâ‚‚}) where
  /-- The underlying object in the base category. -/
  base : ð’®
  /-- The object in the fiber of the base object. -/
  fiber : F.obj âŸ¨op baseâŸ©

namespace Pseudofunctor.Grothendieck

/-- Notation for the Grothendieck category associated to a pseudofunctor `F`. -/
scoped prefix:75 "âˆ« " => Pseudofunctor.Grothendieck

@[simps]
instance categoryStruct : CategoryStruct (âˆ« F) where
  Hom X Y := (f : X.1 âŸ¶ Y.1) Ã— (X.2 âŸ¶ (F.map f.op.toLoc).obj Y.2)
  id X := âŸ¨ðŸ™ X.1, (F.mapId âŸ¨op X.1âŸ©).inv.app X.2âŸ©
  comp {_ _ Z} f g := âŸ¨f.1 â‰« g.1, f.2 â‰« (F.map f.1.op.toLoc).map g.2 â‰«
    (F.mapComp g.1.op.toLoc f.1.op.toLoc).inv.app Z.2âŸ©

section

variable {a b : âˆ« F} (f : a âŸ¶ b)

@[ext]
lemma hom_ext (g : a âŸ¶ b) (hfgâ‚ : f.1 = g.1) (hfgâ‚‚ : f.2 = g.2 â‰« eqToHom (hfgâ‚ â–¸ rfl)) :
    f = g := by
  apply Sigma.ext hfgâ‚
  rw [â† conj_eqToHom_iff_heq _ _ rfl (hfgâ‚ â–¸ rfl)]
  simp only [hfgâ‚‚, eqToHom_refl, id_comp]

lemma hom_ext_iff (g : a âŸ¶ b) : f = g â†” âˆƒ (hfg : f.1 = g.1), f.2 = g.2 â‰« eqToHom (hfg â–¸ rfl) where
  mp hfg := âŸ¨by rw [hfg], by simp [hfg]âŸ©
  mpr := fun âŸ¨hfgâ‚, hfgâ‚‚âŸ© => hom_ext f g hfgâ‚ hfgâ‚‚

protected lemma id_comp : ðŸ™ a â‰« f = f := by
  ext
  Â· simp
  dsimp
  rw [F.mapComp_id_right_inv f.1.op.toLoc]
  rw [â† (F.mapId âŸ¨op a.1âŸ©).inv.naturality_assoc f.2]
  slice_lhs 2 4 =>
    rw [â† Cat.whiskerLeft_app, â† NatTrans.comp_app, â† assoc]
    rw [â† Bicategory.whiskerLeft_comp, Iso.inv_hom_id]
  simp

protected lemma comp_id : f â‰« ðŸ™ b = f := by
  ext
  Â· simp
  dsimp
  rw [â† Cat.whiskerRight_app, â† NatTrans.comp_app]
  rw [F.mapComp_id_left_inv]
  nth_rw 1 [â† assoc]
  rw [â† Bicategory.comp_whiskerRight, Iso.inv_hom_id]
  simp

end

protected lemma assoc {a b c d : âˆ« F} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) :
    (f â‰« g) â‰« h = f â‰« g â‰« h := by
  ext
  Â· simp
  dsimp
  slice_lhs 3 5 =>
    rw [â† (F.mapComp g.1.op.toLoc f.1.op.toLoc).inv.naturality_assoc h.2]
    rw [â† Cat.whiskerLeft_app, â† NatTrans.comp_app]
    rw [F.mapComp_assoc_right_inv h.1.op.toLoc g.1.op.toLoc f.1.op.toLoc]
    simp only [Strict.associator_eqToIso, eqToIso_refl, Iso.refl_inv, eqToIso.hom]
    repeat rw [NatTrans.comp_app]
    rw [F.mapâ‚‚_eqToHom, NatTrans.id_app]
  simp only [Cat.comp_obj, Cat.comp_map, map_comp, assoc]
  congr 3
  rw [â† Cat.whiskerRight_app, eqToHom_app]
  simp only [Cat.whiskerRight_app, Cat.comp_obj, id_comp]

/-- The category structure on `âˆ« F`. -/
instance category : Category (âˆ« F) where
  toCategoryStruct := Pseudofunctor.Grothendieck.categoryStruct
  id_comp := Pseudofunctor.Grothendieck.id_comp
  comp_id := Pseudofunctor.Grothendieck.comp_id
  assoc := Pseudofunctor.Grothendieck.assoc

/-- The projection `âˆ« F â¥¤ ð’®` given by projecting both objects and homs to the first
factor. -/
@[simps]
def forget (F : Pseudofunctor (LocallyDiscrete ð’®áµ’áµ–) Cat.{vâ‚‚, uâ‚‚}) : âˆ« F â¥¤ ð’® where
  obj := fun X => X.1
  map := fun f => f.1

end Pseudofunctor.Grothendieck

end CategoryTheory
