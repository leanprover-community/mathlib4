/-
Copyright (c) 2025 Calle Sรถnne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle Sรถnne
-/

import Mathlib.CategoryTheory.Bicategory.Basic
import Mathlib.CategoryTheory.Opposites

/-!
# Opposite bicategories

We construct the 1-cell opposite of a bicategory `B`, called `Bแตแต`. It is defined as follows
* The objects of `Bแตแต` correspond to objects of `B`.
* The morphisms `X โถ Y` in `Bแตแต` are the morphisms `Y โถ X` in `B`.
* The 2-morphisms `f โถ g` in `Bแตแต` are the 2-morphisms `f โถ g` in `B`. In other words, the
  directions of the 2-morphisms are preserved.

## Remarks
There are multiple notions of opposite categories for bicategories.
- There is 1-cell dual `Bแตแต` as defined above.
- There is the 2-cell dual, `Cแถแต` where only the natural transformations are reversed
- There is the bi-dual `Cแถแตแตแต` where the directions of both the morphisms and the natural
  transformations are reversed.

## TODO

* Define the 2-cell dual `Cแถแต`.
* Provide various lemmas for going between `LocallyDiscrete Cแตแต` and `(LocallyDiscrete C)แตแต`.

Note: `Cแถแตแตแต` is WIP by Christian Merten.

-/

universe w v u

open CategoryTheory Bicategory Opposite

namespace Bicategory.Opposite

variable {B : Type u} [Bicategory.{w, v} B]

/-- Type synonym for 2-morphisms in the opposite bicategory. -/
structure Hom2 {a b : Bแตแต} (f g : a โถ b) where
  op2' ::
  /-- `Bแตแต` preserves the direction of all 2-morphisms in `B` -/
  unop2 : f.unop โถ g.unop

open Hom2

@[simps!]
instance homCategory (a b : Bแตแต) : Category.{w} (a โถ b) where
  Hom f g := Hom2 f g
  id f := op2' (๐ f.unop)
  comp ฮท ฮธ := op2' (ฮท.unop2 โซ ฮธ.unop2)

/-- Synonym for constructor of `Hom2` where the 1-morphisms `f` and `g` lie in `B` and not `Bแตแต`. -/
def op2 {a b : B} {f g : a โถ b} (ฮท : f โถ g) : f.op โถ g.op :=
  op2' ฮท

@[simp]
theorem unop2_op2 {a b : B} {f g : a โถ b} (ฮท : f โถ g) : (op2 ฮท).unop2 = ฮท :=
  rfl

@[simp]
theorem op2_unop2 {a b : Bแตแต} {f g : a โถ b} (ฮท : f โถ g) : op2 ฮท.unop2 = ฮท :=
  rfl

@[simp]
theorem op2_comp {a b : B} {f g h : a โถ b} (ฮท : f โถ g) (ฮธ : g โถ h) :
    op2 (ฮท โซ ฮธ) = (op2 ฮท) โซ (op2 ฮธ) :=
  rfl

@[simp]
theorem op2_id {a b : B} {f : a โถ b} : op2 (๐ f) = ๐ f.op :=
  rfl

@[simp]
theorem unop2_comp {a b : Bแตแต} {f g h : a โถ b} (ฮท : f โถ g) (ฮธ : g โถ h) :
    unop2 (ฮท โซ ฮธ) = unop2 ฮท โซ unop2 ฮธ :=
  rfl

@[simp]
theorem unop2_id {a b : Bแตแต} {f : a โถ b} : unop2 (๐ f) = ๐ f.unop :=
  rfl

@[simp]
theorem unop2_id_bop {a b : B} {f : a โถ b} : unop2 (๐ f.op) = ๐ f :=
  rfl

@[simp]
theorem op2_id_unbop {a b : Bแตแต} {f : a โถ b} : op2 (๐ f.unop) = ๐ f :=
  rfl

/-- The natural functor from the hom-category `a โถ b` in `B` to its bicategorical opposite
`bop b โถ bop a`. -/
@[simps]
def opFunctor (a b : B) : (a โถ b) โฅค (op b โถ op a) where
  obj f := f.op
  map ฮท := op2 ฮท

/-- The functor from the hom-category `a โถ b` in `Bแตแต` to its bicategorical opposite
`unop b โถ unop a`. -/
@[simps]
def unopFunctor (a b : Bแตแต) : (a โถ b) โฅค (unop b โถ unop a) where
  obj f := f.unop
  map ฮท := unop2 ฮท

end Bicategory.Opposite

namespace CategoryTheory.Iso

open Bicategory.Opposite

variable {B : Type u} [Bicategory.{w, v} B]

/-- A 2-isomorphism in `B` gives a 2-isomorphism in `Bแตแต` -/
@[simps!]
abbrev op2 {a b : B} {f g : a โถ b} (ฮท : f โ g) : f.op โ g.op := (opFunctor a b).mapIso ฮท

/-- A 2-isomorphism in `B` gives a 2-isomorphism in `Bแตแต` -/
@[simps!]
abbrev op2_unop {a b : Bแตแต} {f g : a โถ b} (ฮท : f.unop โ g.unop) : f โ g :=
  (opFunctor b.unop a.unop).mapIso ฮท

/-- A 2-isomorphism in `Bแตแต` gives a 2-isomorphism in `B` -/
@[simps!]
abbrev unop2 {a b : Bแตแต} {f g : a โถ b} (ฮท : f โ g) : f.unop โ g.unop :=
  (unopFunctor a b).mapIso ฮท

/-- A 2-isomorphism in `Bแตแต` gives a 2-isomorphism in `B` -/
@[simps!]
abbrev unop2_op {a b : B} {f g : a โถ b} (ฮท : f.op โ g.op) : f โ g :=
  (unopFunctor (op b) (op a)).mapIso ฮท

@[simp]
theorem unop2_op2 {a b : Bแตแต} {f g : a โถ b} (ฮท : f โ g) : ฮท.unop2.op2 = ฮท := rfl

end CategoryTheory.Iso

namespace Bicategory.Opposite

open Hom2

variable {B : Type u} [Bicategory.{w, v} B]

/-- The 1-cell dual bicategory `Bแตแต`.

It is defined as follows.
* The objects of `Bแตแต` correspond to objects of `B`.
* The morphisms `X โถ Y` in `Bแตแต` are the morphisms `Y โถ X` in `B`.
* The 2-morphisms `f โถ g` in `Bแตแต` are the 2-morphisms `f โถ g` in `B`. In other words, the
  directions of the 2-morphisms are preserved.
-/
@[simps! homCategory_id_unop2 homCategory_comp_unop2 whiskerLeft_unop2 whiskerRight_unop2
  associator_hom_unop2 associator_inv_unop2 leftUnitor_hom_unop2 leftUnitor_inv_unop2
  rightUnitor_hom_unop2 rightUnitor_inv_unop2]
instance bicategory : Bicategory.{w, v} Bแตแต where
  homCategory := homCategory
  whiskerLeft f g h ฮท := op2 <| (unop2 ฮท) โท f.unop
  whiskerRight ฮท h := op2 <| h.unop โ unop2 ฮท
  associator f g h := (associator h.unop g.unop f.unop).op2_unop.symm
  leftUnitor f := (rightUnitor f.unop).op2_unop
  rightUnitor f := (leftUnitor f.unop).op2_unop
  whisker_exchange ฮท ฮธ := congrArg op2 <| (whisker_exchange _ _).symm
  whisker_assoc f g g' ฮท i := congrArg op2 <| by simp
  pentagon f g h i := congrArg op2 <| by simp
  triangle f g := congrArg op2 <| by simp

@[simp]
lemma op2_whiskerLeft {a b c : B} {f : a โถ b} {g g' : b โถ c} (ฮท : g โถ g') :
    op2 (f โ ฮท) = (op2 ฮท) โท f.op :=
  rfl

@[simp]
lemma op2_whiskerRight {a b c : B} {f f' : a โถ b} {g : b โถ c} (ฮท : f โถ f') :
    op2 (ฮท โท g) = g.op โ (op2 ฮท) :=
  rfl

@[simp]
lemma op2_associator {a b c d : B} (f : a โถ b) (g : b โถ c) (h : c โถ d) :
    (ฮฑ_ f g h).op2 = (ฮฑ_ h.op g.op f.op).symm :=
  rfl

@[simp]
lemma op2_associator_hom {a b c d : B} (f : a โถ b) (g : b โถ c) (h : c โถ d) :
    op2 (ฮฑ_ f g h).hom = (ฮฑ_ h.op g.op f.op).symm.hom :=
  rfl

@[simp]
lemma op2_associator_inv {a b c d : B} (f : a โถ b) (g : b โถ c) (h : c โถ d) :
    op2 (ฮฑ_ f g h).inv = (ฮฑ_ h.op g.op f.op).symm.inv :=
  rfl

@[simp]
lemma op2_leftUnitor {a b : B} (f : a โถ b) :
    (ฮป_ f).op2 = ฯ_ f.op :=
  rfl

@[simp]
lemma op2_leftUnitor_hom {a b : B} (f : a โถ b) :
    op2 (ฮป_ f).hom = (ฯ_ f.op).hom :=
  rfl

@[simp]
lemma op2_leftUnitor_inv {a b : B} (f : a โถ b) :
    op2 (ฮป_ f).inv = (ฯ_ f.op).inv :=
  rfl

@[simp]
lemma op2_rightUnitor {a b : B} (f : a โถ b) :
    (ฯ_ f).op2 = ฮป_ f.op :=
  rfl

@[simp]
lemma op2_rightUnitor_hom {a b : B} (f : a โถ b) :
    op2 (ฯ_ f).hom = (ฮป_ f.op).hom :=
  rfl

@[simp]
lemma op2_rightUnitor_inv {a b : B} (f : a โถ b) :
    op2 (ฯ_ f).inv = (ฮป_ f.op).inv :=
  rfl

end Opposite

end Bicategory
