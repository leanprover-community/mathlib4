/-
Copyright (c) 2025 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Emily Riehl
-/
import Mathlib.CategoryTheory.Monoidal.Cartesian.Cat
import Mathlib.CategoryTheory.Enriched.Basic

/-!
# The strict bicategory associated to a Cat-enriched category

If `C` is a type with a `EnrichedCategory Cat C` structure, then it has hom-categories, whose
objects define 1-dimensional arrows on `C` and whose morphisms define 2-dimensional arrows between
these. The enriched category axioms equip this data with the structure of a strict bicategory.

We define a type alias `CatEnriched C` for a type `C` with a `EnrichedCategory Cat C` structure.

We provide this with an instance of a strict bicategory structure constructing
`Bicategory.Strict (CatEnriched C)`.
-/

namespace CategoryTheory
open Category

variable {C : Type*} [EnrichedCategory Cat C]

/-- A type synonym for `C`, which should come equipped with a `Cat`-enriched category structure.
This converts it to a strict bicategory where `X âŸ¶ Y` is `(ğŸ™_ Cat) âŸ¶ (X âŸ¶[W] Y)`. -/
def CatEnriched (C : Type*) := C

namespace CatEnriched

instance : EnrichedCategory Cat (CatEnriched C) := inferInstanceAs (EnrichedCategory Cat C)

/-- Any enriched category has an underlying category structure defined by `ForgetEnrichment`.
This is equivalent but not definitionally equal the category structure constructed here, which is
more canonically associated to the data of an `EnrichedCategory Cat` structure. -/
instance : CategoryStruct (CatEnriched C) where
  Hom X Y := X âŸ¶[Cat] Y
  id X := (eId Cat X).obj âŸ¨âŸ¨()âŸ©âŸ©
  comp {X Y Z} f g := (eComp Cat X Y Z).obj (f, g)

theorem id_eq (X : CatEnriched C) : ğŸ™ X = (eId Cat X).obj âŸ¨âŸ¨()âŸ©âŸ© := rfl

theorem comp_eq {X Y Z : CatEnriched C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    f â‰« g = (eComp Cat X Y Z).obj (f, g) := rfl

instance {X Y : CatEnriched C} : Category (X âŸ¶ Y) := inferInstanceAs (Category (X âŸ¶[Cat] Y).Î±)

/-- The horizonal composition on 2-morphisms is defined using the action on arrows of the
composition bifunctor from the enriched category structure. -/
def hcomp {a b c : CatEnriched C} {f f' : a âŸ¶ b} {g g' : b âŸ¶ c}
  (Î· : f âŸ¶ f') (Î¸ : g âŸ¶ g') : f â‰« g âŸ¶ f' â‰« g' := (eComp Cat a b c).map (Î·, Î¸)

@[simp]
theorem id_hcomp_id {a b c : CatEnriched C} (f : a âŸ¶ b) (g : b âŸ¶ c) :
    hcomp (ğŸ™ f) (ğŸ™ g) = ğŸ™ (f â‰« g) := Functor.map_id ..

/-- The interchange law for horizontal and vertical composition of 2-cells in a bicategory. -/
@[simp]
theorem hcomp_comp {a b c : CatEnriched C} {fâ‚ fâ‚‚ fâ‚ƒ : a âŸ¶ b} {gâ‚ gâ‚‚ gâ‚ƒ : b âŸ¶ c}
    (Î· : fâ‚ âŸ¶ fâ‚‚) (Î·' : fâ‚‚ âŸ¶ fâ‚ƒ) (Î¸ : gâ‚ âŸ¶ gâ‚‚) (Î¸' : gâ‚‚ âŸ¶ gâ‚ƒ) :
    hcomp Î· Î¸ â‰« hcomp Î·' Î¸' = hcomp (Î· â‰« Î·') (Î¸ â‰« Î¸') :=
  ((eComp Cat a b c).map_comp (Y := (_, _)) (_, _) (_, _)).symm

/-- The action on objects of the `EnrichedCategory Cat` coherences proves the category axioms. -/
instance : Category (CatEnriched C) where
  id_comp {X Y} f := congrArg (Â·.obj f) (e_id_comp (V := Cat) X Y)
  comp_id {X Y} f := congrArg (Â·.obj f) (e_comp_id (V := Cat) X Y)
  assoc {X Y Z W} f g h := congrArg (Â·.obj (f, g, h)) (e_assoc (V := Cat) X Y Z W)

theorem id_hcomp_heq {a b : CatEnriched C} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') :
    HEq (hcomp (ğŸ™ (ğŸ™ a)) Î·) Î· := by
  rw [id_eq, â† Functor.map_id]
  exact congr_arg_heq (Â·.map Î·) (e_id_comp (V := Cat) a b)

theorem id_hcomp {a b : CatEnriched C} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') :
    hcomp (ğŸ™ (ğŸ™ a)) Î· = eqToHom (id_comp f) â‰« Î· â‰« eqToHom (id_comp f').symm := by
  simp [â† heq_eq_eq, id_hcomp_heq]

theorem hcomp_id_heq {a b : CatEnriched C} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') :
    HEq (hcomp Î· (ğŸ™ (ğŸ™ b))) Î· := by
  rw [id_eq, â† Functor.map_id]
  exact congr_arg_heq (Â·.map Î·) (e_comp_id (V := Cat) a b)

theorem hcomp_id {a b : CatEnriched C} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') :
    hcomp Î· (ğŸ™ (ğŸ™ b)) = eqToHom (comp_id f) â‰« Î· â‰« eqToHom (comp_id f').symm := by
  simp [â† heq_eq_eq, hcomp_id_heq]

theorem hcomp_assoc_heq {a b c d : CatEnriched C} {f f' : a âŸ¶ b} {g g' : b âŸ¶ c} {h h' : c âŸ¶ d}
    (Î· : f âŸ¶ f') (Î¸ : g âŸ¶ g') (Îº : h âŸ¶ h') :
    HEq (hcomp (hcomp Î· Î¸) Îº) (hcomp Î· (hcomp Î¸ Îº)) :=
  congr_arg_heq (Â·.map (X := (_, _, _)) (Y := (_, _, _)) (Î·, Î¸, Îº)) (e_assoc (V := Cat) a b c d)

theorem hcomp_assoc {a b c d : CatEnriched C} {f f' : a âŸ¶ b} {g g' : b âŸ¶ c} {h h' : c âŸ¶ d}
    (Î· : f âŸ¶ f') (Î¸ : g âŸ¶ g') (Îº : h âŸ¶ h') :
    hcomp (hcomp Î· Î¸) Îº =
      eqToHom (assoc f g h) â‰« hcomp Î· (hcomp Î¸ Îº) â‰« eqToHom (assoc f' g' h').symm := by
  simp [â† heq_eq_eq, hcomp_assoc_heq]

instance : Bicategory (CatEnriched C) where
  homCategory := inferInstance
  whiskerLeft {_ _ _} f {_ _} Î· := hcomp (ğŸ™ f) Î·
  whiskerRight Î· h := hcomp Î· (ğŸ™ h)
  associator f g h := eqToIso (assoc f g h)
  leftUnitor f := eqToIso (id_comp f)
  rightUnitor f := eqToIso (comp_id f)
  id_whiskerLeft := id_hcomp
  comp_whiskerLeft := by simp [â† id_hcomp_id, hcomp_assoc]
  whiskerRight_id := hcomp_id
  whiskerRight_comp := by simp [hcomp_assoc]
  whisker_assoc := by simp [hcomp_assoc]
  pentagon {a b c d e} f g h i := by
    generalize_proofs h1 h2 h3 h4; revert h1 h2 h3 h4
    generalize (f â‰« g) â‰« h = x, (g â‰« h) â‰« i = w
    rintro rfl _ rfl _; simp
  triangle {a b c} f g := by
    generalize_proofs h1 h2 h3; revert h1 h2 h3
    generalize ğŸ™ b â‰« g = g, f â‰« ğŸ™ b = f
    rintro _ rfl rfl; simp

/-- As the associator and left and right unitors are defined as eqToIso of category axioms, the
bicategory structure on `CatEnriched C` is strict. -/
instance : Bicategory.Strict (CatEnriched C) where

end CatEnriched

end CategoryTheory
