/-
Copyright (c) 2025 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Emily Riehl
-/
import Mathlib.CategoryTheory.Monoidal.Cartesian.Cat
import Mathlib.CategoryTheory.Enriched.Basic
import Mathlib.CategoryTheory.Enriched.Ordinary.Basic

/-!
# The strict bicategory associated to a Cat-enriched category

If `C` is a type with a `EnrichedCategory Cat C` structure, then it has hom-categories, whose
objects define 1-dimensional arrows on `C` and whose morphisms define 2-dimensional arrows between
these. The enriched category axioms equip this data with the structure of a strict bicategory.

We define a type alias `CatEnriched C` for a type `C` with a `EnrichedCategory Cat C` structure. We
provide this with an instance of a strict bicategory structure constructing
`Bicategory.Strict (CatEnriched C)`.

If `C` is a type with a `EnrichedOrdinaryCategory Cat C` structure, then it has an `Enrichred Cat C`
structure, so the previous construction would again produce a strict bicategory. However, in this
setting `C` is also given a `Category C` structure, together with an equivalence between this
category and the underlying category of the `Enriched Cat C`, and in examples the given category
structure is the preferred one.

Thus, we define a type alias `CatEnrichedOrdinary C` for a type `C` with an
`EnrichedOrdinaryCategory Cat C` structure. We provide this with an instance of a strict bicategory
structure extending the category structure provided by the given instance `Category C` constructing
`Bicategory.Strict (CatEnrichedOrdinary C)`.

-/

universe u v u' v'
namespace CategoryTheory
open Category

section
variable {C : Type*} [EnrichedCategory Cat C]

/-- A type synonym for `C`, which should come equipped with a `Cat`-enriched category structure.
This converts it to a strict bicategory where `Category (X âŸ¶ Y)` is `(X âŸ¶[Cat] Y)`. -/
def CatEnriched (C : Type*) := C

namespace CatEnriched

instance : EnrichedCategory Cat (CatEnriched C) := inferInstanceAs (EnrichedCategory Cat C)

/-- Any enriched category has an underlying category structure defined by `ForgetEnrichment`.
This is equivalent but not definitionally equal the category structure constructed here, which is
more canonically associated to the data of an `EnrichedCategory Cat` structure. -/
instance : CategoryStruct (CatEnriched C) where
  Hom X Y := X âŸ¶[Cat] Y
  id X := (eId Cat X).obj âŸ¨âŸ¨()âŸ©âŸ©
  comp {X Y Z} f g := (eComp Cat X Y Z).obj (f, g)

theorem id_eq (X : CatEnriched C) : ğŸ™ X = (eId Cat X).obj âŸ¨âŸ¨()âŸ©âŸ© := rfl

theorem comp_eq {X Y Z : CatEnriched C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    f â‰« g = (eComp Cat X Y Z).obj (f, g) := rfl

instance {X Y : CatEnriched C} : Category (X âŸ¶ Y) := inferInstanceAs (Category (X âŸ¶[Cat] Y).Î±)

/-- The horizontal composition on 2-morphisms is defined using the action on arrows of the
composition bifunctor from the enriched category structure. -/
def hComp {a b c : CatEnriched C} {f f' : a âŸ¶ b} {g g' : b âŸ¶ c}
    (Î· : f âŸ¶ f') (Î¸ : g âŸ¶ g') : f â‰« g âŸ¶ f' â‰« g' := (eComp Cat a b c).map (Î·, Î¸)

@[simp]
theorem id_hComp_id {a b c : CatEnriched C} (f : a âŸ¶ b) (g : b âŸ¶ c) :
    hComp (ğŸ™ f) (ğŸ™ g) = ğŸ™ (f â‰« g) := Functor.map_id ..

@[simp]
theorem eqToHom_hComp_eqToHom {a b c : CatEnriched C}
    {f f' : a âŸ¶ b} (Î± : f = f') {g g' : b âŸ¶ c} (Î² : g = g') :
    hComp (eqToHom Î±) (eqToHom Î²) = eqToHom (Î± â–¸ Î² â–¸ rfl) := by cases Î±; cases Î²; simp

/-- The interchange law for horizontal and vertical composition of 2-cells in a bicategory. -/
@[simp]
theorem hComp_comp {a b c : CatEnriched C} {fâ‚ fâ‚‚ fâ‚ƒ : a âŸ¶ b} {gâ‚ gâ‚‚ gâ‚ƒ : b âŸ¶ c}
    (Î· : fâ‚ âŸ¶ fâ‚‚) (Î·' : fâ‚‚ âŸ¶ fâ‚ƒ) (Î¸ : gâ‚ âŸ¶ gâ‚‚) (Î¸' : gâ‚‚ âŸ¶ gâ‚ƒ) :
    hComp Î· Î¸ â‰« hComp Î·' Î¸' = hComp (Î· â‰« Î·') (Î¸ â‰« Î¸') :=
  ((eComp Cat a b c).map_comp (Y := (_, _)) (_, _) (_, _)).symm

/-- The action on objects of the `EnrichedCategory Cat` coherences proves the category axioms. -/
instance : Category (CatEnriched C) where
  id_comp {X Y} f := congrArg (Â·.obj f) (e_id_comp (V := Cat) X Y)
  comp_id {X Y} f := congrArg (Â·.obj f) (e_comp_id (V := Cat) X Y)
  assoc {X Y Z W} f g h := congrArg (Â·.obj (f, g, h)) (e_assoc (V := Cat) X Y Z W)

/-- The category instance on `CatEnriched C` promotes it to a `Cat` enriched ordinary
category. -/
instance : EnrichedOrdinaryCategory Cat (CatEnriched C) where
  homEquiv := Cat.fromChosenTerminalEquiv.symm
  homEquiv_comp _ _ := (Equiv.symm_apply_eq Cat.fromChosenTerminalEquiv).mpr rfl
  homEquiv_id _ := (Equiv.symm_apply_eq Cat.fromChosenTerminalEquiv).mpr rfl

theorem id_hComp_heq {a b : CatEnriched C} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') :
    HEq (hComp (ğŸ™ (ğŸ™ a)) Î·) Î· := by
  rw [id_eq, â† Functor.map_id]
  exact congr_arg_heq (Â·.map Î·) (e_id_comp (V := Cat) a b)

theorem id_hComp {a b : CatEnriched C} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') :
    hComp (ğŸ™ (ğŸ™ a)) Î· = eqToHom (id_comp f) â‰« Î· â‰« eqToHom (id_comp f').symm := by
  simp [â† heq_eq_eq, id_hComp_heq]

theorem hComp_id_heq {a b : CatEnriched C} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') :
    HEq (hComp Î· (ğŸ™ (ğŸ™ b))) Î· := by
  rw [id_eq, â† Functor.map_id]
  exact congr_arg_heq (Â·.map Î·) (e_comp_id (V := Cat) a b)

theorem hComp_id {a b : CatEnriched C} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') :
    hComp Î· (ğŸ™ (ğŸ™ b)) = eqToHom (comp_id f) â‰« Î· â‰« eqToHom (comp_id f').symm := by
  simp [â† heq_eq_eq, hComp_id_heq]

theorem hComp_assoc_heq {a b c d : CatEnriched C} {f f' : a âŸ¶ b} {g g' : b âŸ¶ c} {h h' : c âŸ¶ d}
    (Î· : f âŸ¶ f') (Î¸ : g âŸ¶ g') (Îº : h âŸ¶ h') :
    HEq (hComp (hComp Î· Î¸) Îº) (hComp Î· (hComp Î¸ Îº)) :=
  congr_arg_heq (Â·.map (X := (_, _, _)) (Y := (_, _, _)) (Î·, Î¸, Îº)) (e_assoc (V := Cat) a b c d)

theorem hComp_assoc {a b c d : CatEnriched C} {f f' : a âŸ¶ b} {g g' : b âŸ¶ c} {h h' : c âŸ¶ d}
    (Î· : f âŸ¶ f') (Î¸ : g âŸ¶ g') (Îº : h âŸ¶ h') :
    hComp (hComp Î· Î¸) Îº =
      eqToHom (assoc f g h) â‰« hComp Î· (hComp Î¸ Îº) â‰« eqToHom (assoc f' g' h').symm := by
  simp [â† heq_eq_eq, hComp_assoc_heq]

instance : Bicategory (CatEnriched C) where
  homCategory := inferInstance
  whiskerLeft {_ _ _} f {_ _} Î· := hComp (ğŸ™ f) Î·
  whiskerRight Î· h := hComp Î· (ğŸ™ h)
  associator f g h := eqToIso (assoc f g h)
  leftUnitor f := eqToIso (id_comp f)
  rightUnitor f := eqToIso (comp_id f)
  id_whiskerLeft := id_hComp
  comp_whiskerLeft := by simp [â† id_hComp_id, hComp_assoc]
  whiskerRight_id := hComp_id
  whiskerRight_comp := by simp [hComp_assoc]
  whisker_assoc := by simp [hComp_assoc]
  pentagon f g h i := by
    generalize_proofs h1 h2 h3 h4; revert h1 h2 h3 h4
    generalize (f â‰« g) â‰« h = x, (g â‰« h) â‰« i = w
    rintro rfl _ rfl _; simp
  triangle f g := by
    generalize_proofs h1 h2 h3; revert h1 h2 h3
    generalize ğŸ™ _ â‰« g = g, f â‰« ğŸ™ _ = f
    rintro _ rfl rfl; simp

/-- As the associator and left and right unitors are defined as eqToIso of category axioms, the
bicategory structure on `CatEnriched C` is strict. -/
instance : Bicategory.Strict (CatEnriched C) where

end CatEnriched

end

section
variable {C : Type u} [Category.{v} C] [EnrichedOrdinaryCategory Cat.{v', u'} C]

/-- A type synonym for `C`, which should come equipped with a `Cat`-enriched category structure.
This converts it to a strict bicategory where `Category (X âŸ¶ Y)` is `(X âŸ¶[Cat] Y)`. -/
def CatEnrichedOrdinary (C : Type*) := C

namespace CatEnrichedOrdinary

instance : Category (CatEnrichedOrdinary C) := inferInstanceAs (Category C)

instance : EnrichedCategory Cat (CatEnrichedOrdinary C) := inferInstanceAs (EnrichedCategory Cat C)

instance : EnrichedOrdinaryCategory Cat (CatEnrichedOrdinary C) :=
  inferInstanceAs (EnrichedOrdinaryCategory Cat C)

/-- The forgetful map from the type alias associated to `EnrichedOrdinaryCategory Cat C` and the
type alias associated to `EnrichedCategory Cat C` is the identity on underlying types. -/
def toBase (a : CatEnrichedOrdinary C) : CatEnriched C := a

/-- The hom-types in a `Cat`-enriched ordinary category are equivalent to the types underlying the
hom-categories. -/
def homEquiv {a b : CatEnrichedOrdinary C} : (a âŸ¶ b) â‰ƒ (a.toBase âŸ¶ b.toBase) :=
  (eHomEquiv (V := Cat)).trans Cat.fromChosenTerminalEquiv

theorem homEquiv_id {a : CatEnrichedOrdinary C} : homEquiv (ğŸ™ a) = ğŸ™ a.toBase := by
  unfold homEquiv
  simp only [Equiv.trans_apply]
  rw [eHomEquiv_id]
  rfl

theorem homEquiv_comp {a b c : CatEnrichedOrdinary C} (f : a âŸ¶ b) (g : b âŸ¶ c) :
    homEquiv (f â‰« g) = homEquiv f â‰« homEquiv g := by
  unfold homEquiv
  simp only [Equiv.trans_apply]
  rw [eHomEquiv_comp]
  rfl

/-- The 2-cells between a parallel pair of 1-cells `f g` in `CatEnrichedOrdinary C` are defined to
be the morphisms in the hom-categories provided by the `EnrichedCategory Cat C` structure between
the corresponding objects. -/
structure Hom {X Y : CatEnrichedOrdinary C} (f g : X âŸ¶ Y) where mk' ::
  /-- A 2-cell from `f` to `g` is a 2-cell from `homEquiv f` to `homEquiv g`. -/
  base' : homEquiv f âŸ¶ homEquiv g

instance {X Y : CatEnrichedOrdinary C} : Quiver (X âŸ¶ Y) where
  Hom f g := Hom f g

/-- A 2-cell in `CatEnrichedOrdinary C` has a corresponding "base" 2-cell in `CatEnriched C`. -/
def Hom.base {X Y : CatEnrichedOrdinary C} {f g : X âŸ¶ Y} (Î± : f âŸ¶ g) :
    homEquiv f âŸ¶ homEquiv g := Î±.base'

/-- A 2-cell in `CatEnriched C` can be "made" into a 2-cell in `CatEnrichedOrdinary C`. -/
def Hom.mk {X Y : CatEnrichedOrdinary C} {f g : X âŸ¶ Y} (Î± : homEquiv f âŸ¶ homEquiv g) :
    f âŸ¶ g := .mk' Î±

@[simp] theorem mk_base {X Y : CatEnrichedOrdinary C} {f g : X âŸ¶ Y} (Î± : f âŸ¶ g) :
    Hom.mk (Hom.base Î±) = Î± := rfl

@[simp] theorem base_mk {X Y : CatEnrichedOrdinary C} {f g : X âŸ¶ Y} (Î± : homEquiv f âŸ¶ homEquiv g) :
    Hom.base (Hom.mk Î±) = Î± := rfl

instance {X Y : CatEnrichedOrdinary C} : CategoryStruct (X âŸ¶ Y) where
  id f := Hom.mk (ğŸ™ (homEquiv f))
  comp Î± Î² := Hom.mk (Hom.base Î± â‰« Hom.base Î²)

theorem Hom.id_eq {X Y : CatEnrichedOrdinary C} (f : X âŸ¶ Y) :
    ğŸ™ f = Hom.mk (ğŸ™ (homEquiv f)) := rfl

@[simp] theorem Hom.base_id {X Y : CatEnrichedOrdinary C} (f : X âŸ¶ Y) :
    Hom.base (ğŸ™ f) = ğŸ™ (homEquiv f) := rfl

theorem Hom.comp_eq {X Y : CatEnrichedOrdinary C} {f g h : X âŸ¶ Y}
    (Î± : f âŸ¶ g) (Î² : g âŸ¶ h) : (Î± â‰« Î²) = Hom.mk (Hom.base Î± â‰« Hom.base Î²) := rfl

@[simp] theorem Hom.base_comp {X Y : CatEnrichedOrdinary C} {f g h : X âŸ¶ Y}
    (Î± : f âŸ¶ g) (Î² : g âŸ¶ h) : Hom.base (Î± â‰« Î²) = Hom.base Î± â‰« Hom.base Î² := rfl

theorem Hom.mk_comp {X Y : CatEnrichedOrdinary C} {f g h : X âŸ¶ Y}
    (Î± : homEquiv f âŸ¶ homEquiv g) (Î² : homEquiv g âŸ¶ homEquiv h) :
    Hom.mk (Î± â‰« Î²) = Hom.mk Î± â‰« Hom.mk Î² := rfl

@[ext] theorem Hom.ext {X Y : CatEnrichedOrdinary C} {f g : X âŸ¶ Y} (Î± Î² : f âŸ¶ g)
    (H : Hom.base Î± = Hom.base Î²) : Î± = Î² := by cases Î±; cases Î²; cases H; rfl

/-- A `Cat`-enriched ordinary category comes with hom-categories `X âŸ¶[Cat] Y` whose underlying type
of objects is equivalent to the type `X âŸ¶ Y` defined by the category structure on `C`. The following
definition transfers the category structure to the latter type of objects. -/
instance {X Y : CatEnrichedOrdinary C} : Category (X âŸ¶ Y) where

@[simp] theorem Hom.base_eqToHom {X Y : CatEnrichedOrdinary C} {f g : X âŸ¶ Y} (Î± : f = g) :
    Hom.base (eqToHom Î±) = eqToHom (congrArg _ Î±) := by cases Î±; rfl

/-- The horizontal composition on 2-morphisms is defined using the action on arrows of the
composition bifunctor from the enriched category structure. -/
def hComp {a b c : CatEnrichedOrdinary C} {f f' : a âŸ¶ b} {g g' : b âŸ¶ c}
    (Î· : f âŸ¶ f') (Î¸ : g âŸ¶ g') : f â‰« g âŸ¶ f' â‰« g' :=
  .mk <|
    eqToHom (homEquiv_comp f g) â‰« CatEnriched.hComp (Hom.base Î·) (Hom.base Î¸) â‰«
    eqToHom (homEquiv_comp f' g').symm

@[simp]
theorem id_hComp_id {a b c : CatEnrichedOrdinary C} (f : a âŸ¶ b) (g : b âŸ¶ c) :
    hComp (ğŸ™ f) (ğŸ™ g) = ğŸ™ (f â‰« g) := by simp [hComp, Hom.id_eq]

@[simp]
theorem eqToHom_hComp_eqToHom {a b c : CatEnrichedOrdinary C}
    {f f' : a âŸ¶ b} (Î± : f = f') {g g' : b âŸ¶ c} (Î² : g = g') :
    hComp (eqToHom Î±) (eqToHom Î²) = eqToHom (Î± â–¸ Î² â–¸ rfl) := by cases Î±; cases Î²; simp

/-- The interchange law for horizontal and vertical composition of 2-cells in a bicategory. -/
@[simp]
theorem hComp_comp {a b c : CatEnrichedOrdinary C} {fâ‚ fâ‚‚ fâ‚ƒ : a âŸ¶ b} {gâ‚ gâ‚‚ gâ‚ƒ : b âŸ¶ c}
    (Î· : fâ‚ âŸ¶ fâ‚‚) (Î·' : fâ‚‚ âŸ¶ fâ‚ƒ) (Î¸ : gâ‚ âŸ¶ gâ‚‚) (Î¸' : gâ‚‚ âŸ¶ gâ‚ƒ) :
    hComp Î· Î¸ â‰« hComp Î·' Î¸' = hComp (Î· â‰« Î·') (Î¸ â‰« Î¸') := by
  simp [hComp, â† CatEnriched.hComp_comp, Hom.comp_eq]

theorem id_hComp {a b : CatEnrichedOrdinary C} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') :
    hComp (ğŸ™ (ğŸ™ a)) Î· = eqToHom (id_comp f) â‰« Î· â‰« eqToHom (id_comp f').symm := by
  ext
  simp only [hComp, Hom.base_id, base_mk, â† heq_eq_eq, eqToHom_comp_heq_iff, comp_eqToHom_heq_iff]
  rw [homEquiv_id]; simp [CatEnriched.id_hComp_heq]

theorem id_hComp_heq {a b : CatEnrichedOrdinary C} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') :
    HEq (hComp (ğŸ™ (ğŸ™ a)) Î·) Î· := by simp [id_hComp]

theorem hComp_id {a b : CatEnrichedOrdinary C} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') :
    hComp Î· (ğŸ™ (ğŸ™ b)) = eqToHom (comp_id f) â‰« Î· â‰« eqToHom (comp_id f').symm := by
  ext
  simp only [hComp, Hom.base_id, base_mk, â† heq_eq_eq, eqToHom_comp_heq_iff, comp_eqToHom_heq_iff]
  rw [homEquiv_id]
  simp [CatEnriched.hComp_id_heq]

theorem hComp_id_heq {a b : CatEnrichedOrdinary C} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') :
    HEq (hComp Î· (ğŸ™ (ğŸ™ b))) Î· := by simp [hComp_id]

theorem id_eq_eqToHom {C} [Category C] (X : C) : ğŸ™ X = eqToHom rfl := rfl

theorem hComp_assoc {a b c d : CatEnrichedOrdinary C} {f f' : a âŸ¶ b} {g g' : b âŸ¶ c} {h h' : c âŸ¶ d}
    (Î· : f âŸ¶ f') (Î¸ : g âŸ¶ g') (Îº : h âŸ¶ h') :
    hComp (hComp Î· Î¸) Îº =
      eqToHom (assoc f g h) â‰« hComp Î· (hComp Î¸ Îº) â‰« eqToHom (assoc f' g' h').symm := by
  ext
  simp only [hComp, base_mk, Hom.base_comp, Hom.base_eqToHom,
    â† heq_eq_eq, heq_eqToHom_comp_iff, heq_comp_eqToHom_iff,
    eqToHom_comp_heq_iff, comp_eqToHom_heq_iff]
  conv => enter [1,2]; exact ((id_comp _).trans (comp_id _)).symm
  conv => enter [2,1]; exact ((id_comp _).trans (comp_id _)).symm
  iterate 4 rw [â† CatEnriched.hComp_comp, id_eq_eqToHom, CatEnriched.eqToHom_hComp_eqToHom]
  simp [CatEnriched.hComp_assoc_heq]

theorem hComp_assoc_heq {a b c d : CatEnrichedOrdinary C}
    {f f' : a âŸ¶ b} {g g' : b âŸ¶ c} {h h' : c âŸ¶ d} (Î· : f âŸ¶ f') (Î¸ : g âŸ¶ g') (Îº : h âŸ¶ h') :
    HEq (hComp (hComp Î· Î¸) Îº) (hComp Î· (hComp Î¸ Îº)) := by simp [hComp_assoc]

instance : Bicategory (CatEnrichedOrdinary C) where
  homCategory := inferInstance
  whiskerLeft {_ _ _} f {_ _} Î· := hComp (ğŸ™ f) Î·
  whiskerRight Î· h := hComp Î· (ğŸ™ h)
  associator f g h := eqToIso (assoc f g h)
  leftUnitor f := eqToIso (id_comp f)
  rightUnitor f := eqToIso (comp_id f)
  id_whiskerLeft := by simp [id_hComp]
  comp_whiskerLeft := by simp [â† hComp_assoc]
  whiskerRight_id := by simp [hComp_id]
  whiskerRight_comp := by simp [hComp_assoc]
  whisker_assoc := by simp [hComp_assoc]
  pentagon := by simp [id_eq_eqToHom, -eqToHom_refl]
  triangle := by simp [id_eq_eqToHom, -eqToHom_refl]

/-- As the associator and left and right unitors are defined as eqToIso of category axioms, the
bicategory structure on `CatEnrichedOrdinary C` is strict. -/
instance : Bicategory.Strict (CatEnrichedOrdinary C) where

end CatEnrichedOrdinary

end

end CategoryTheory
