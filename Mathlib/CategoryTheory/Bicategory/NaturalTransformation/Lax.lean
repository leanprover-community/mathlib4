/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno, Calle SÃ¶nne, Robin Carlier
-/
module

public import Mathlib.CategoryTheory.Bicategory.Functor.Lax
public import Mathlib.Tactic.CategoryTheory.Bicategory.Basic

/-!
# Transformations between lax functors

Just as there are natural transformations between functors, there are transformations
between lax functors. The equality in the naturality condition of a natural transformation gets
replaced by a specified 2-morphism. Now, there are three possible types of transformations (between
lax functors):
* Lax natural transformations;
* OpLax natural transformations;
* Strong natural transformations.
These differ in the direction (and invertibility) of the 2-morphisms involved in the naturality
condition.

## Main definitions

* `Lax.LaxTrans F G`: lax transformations between lax functors `F` and `G`. The naturality
  condition is given by a 2-morphism `app a â‰« G.map f âŸ¶ F.map f â‰« app b` for each 1-morphism
  `f : a âŸ¶ b`.
* `Lax.OplaxTrans F G`: oplax transformations between lax functors `F` and `G`. The naturality
  condition is given by a 2-morphism `F.map f â‰« app b âŸ¶ app a â‰« G.map f` for each 1-morphism
  `f : a âŸ¶ b`.
* `Lax.StrongTrans F G`: Strong transformations between lax functors `F` and `G`. The naturality
  condition is given by a 2-morphism `app a â‰« G.map f â‰… F.map f â‰« app b` for each 1-morphism
  `f : a âŸ¶ b`.

Using these, we define three `CategoryStruct` (scoped) instances on `B â¥¤á´¸ C`, in the
`Lax.LaxTrans`, `Lax.Oplax`, and `Lax.StrongTrans` namespaces. The arrows in these
CategoryStruct's are given by lax transformations, oplax transformations, and strong
transformations respectively.

We also provide API for going between lax transformations and strong transformations:
* `Lax.StrongCore F G`: a structure on a lax transformation between lax functors that
promotes it to a strong transformation.
* `Lax.mkOfLax Î· Î·'`: given a lax transformation `Î·` such that each component
  2-morphism is an isomorphism, `mkOfLax` gives the corresponding strong transformation.

## References
* [Niles Johnson, Donald Yau, *2-Dimensional Categories*](https://arxiv.org/abs/2002.06055),
section 4.2.

-/

@[expose] public section

namespace CategoryTheory.Lax

open Category Bicategory

universe wâ‚ wâ‚‚ vâ‚ vâ‚‚ uâ‚ uâ‚‚

variable {B : Type uâ‚} [Bicategory.{wâ‚, vâ‚} B] {C : Type uâ‚‚} [Bicategory.{wâ‚‚, vâ‚‚} C]

/-- If `Î·` is a lax transformation between `F` and `G`, we have a 1-morphism
`Î·.app a : F.obj a âŸ¶ G.obj a` for each object `a : B`. We also have a 2-morphism
`Î·.naturality f : app a â‰« G.map f âŸ¶ F.map f â‰« app b` for each 1-morphism `f : a âŸ¶ b`.
These 2-morphisms satisfy the naturality condition, and preserve the identities and
the compositions modulo some adjustments of domains and codomains of 2-morphisms.
-/
structure LaxTrans (F G : B â¥¤á´¸ C) where
  /-- The component 1-morphisms of a lax transformation. -/
  app (a : B) : F.obj a âŸ¶ G.obj a
  /-- The 2-morphisms underlying the lax naturality constraint. -/
  naturality {a b : B} (f : a âŸ¶ b) : app a â‰« G.map f âŸ¶ F.map f â‰« app b
  /-- Naturality of the lax naturality constraint. -/
  naturality_naturality {a b : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g) :
      naturality f â‰« F.mapâ‚‚ Î· â–· app b = app a â— G.mapâ‚‚ Î· â‰« naturality g := by
    cat_disch
  /-- Lax unity. -/
  naturality_id (a : B) :
      app a â— G.mapId a â‰« naturality (ğŸ™ a) =
        (Ï_ (app a)).hom â‰« (Î»_ (app a)).inv â‰« F.mapId a â–· app a := by
    cat_disch
  /-- Lax functoriality. -/
  naturality_comp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) :
      app a â— G.mapComp f g â‰« naturality (f â‰« g) =
      (Î±_ _ _ _).inv â‰« naturality f â–· G.map g â‰« (Î±_ _ _ _).hom â‰«
        F.map f â— naturality g â‰« (Î±_ _ _ _).inv â‰« F.mapComp f g â–· app c := by
    cat_disch

attribute [reassoc (attr := simp)] LaxTrans.naturality_naturality LaxTrans.naturality_id
  LaxTrans.naturality_comp

namespace LaxTrans

variable {F G H : B â¥¤á´¸ C} (Î· : LaxTrans F G) (Î¸ : LaxTrans G H)

variable (F) in
/-- The identity lax transformation. -/
def id : LaxTrans F F where
  app a := ğŸ™ (F.obj a)
  naturality {_ _} f := (Î»_ (F.map f)).hom â‰« (Ï_ (F.map f)).inv

instance : Inhabited (LaxTrans F F) :=
  âŸ¨id FâŸ©

/-- Auxiliary definition for `vComp`. -/
abbrev vCompApp (a : B) : F.obj a âŸ¶ H.obj a :=
  Î·.app a â‰« Î¸.app a

/-- Auxiliary definition for `vComp`. -/
abbrev vCompNaturality {a b : B} (f : a âŸ¶ b) :
    (Î·.app a â‰« Î¸.app a) â‰« H.map f âŸ¶ F.map f â‰« Î·.app b â‰« Î¸.app b :=
  (Î±_ _ _ _).hom â‰« Î·.app a â— Î¸.naturality f â‰« (Î±_ _ _ _).inv â‰«
    Î·.naturality f â–· Î¸.app b â‰« (Î±_ _ _ _).hom

theorem vComp_naturality_naturality {a b : B} {f g : a âŸ¶ b} (Î² : f âŸ¶ g) :
    Î·.vCompNaturality Î¸ f â‰« F.mapâ‚‚ Î² â–· Î·.vCompApp Î¸ b =
      Î·.vCompApp Î¸ a â— H.mapâ‚‚ Î² â‰« Î·.vCompNaturality Î¸ g :=
  calc
    _ = ğŸ™ _ âŠ—â‰« Î·.app a â— Î¸.naturality f âŠ—â‰«
          (Î·.naturality f â‰« F.mapâ‚‚ Î² â–· Î·.app b) â–· Î¸.app b âŠ—â‰« ğŸ™ _ := by
      bicategory
    _ = ğŸ™ _ âŠ—â‰« Î·.app a â— (Î¸.naturality f â‰« G.mapâ‚‚ Î² â–· Î¸.app b) âŠ—â‰«
          Î·.naturality g â–· Î¸.app b âŠ—â‰« ğŸ™ _ := by
      rw [naturality_naturality]
      bicategory
    _ = _ := by
      rw [naturality_naturality]
      bicategory

theorem vComp_naturality_id (a : B) :
    Î·.vCompApp Î¸ a â— H.mapId a â‰« Î·.vCompNaturality Î¸ (ğŸ™ a) =
      (Ï_ (Î·.vCompApp Î¸ a)).hom â‰« (Î»_ (Î·.vCompApp Î¸ a)).inv â‰« F.mapId a â–· Î·.vCompApp Î¸ a :=
  calc
    _ = ğŸ™ _ âŠ—â‰« Î·.app a â— (Î¸.app a â— H.mapId a â‰« Î¸.naturality (ğŸ™ a)) âŠ—â‰«
          Î·.naturality (ğŸ™ a) â–· Î¸.app a âŠ—â‰« ğŸ™ _ := by
      bicategory
    _ = ğŸ™ _ âŠ—â‰« (Î·.app a â— G.mapId a â‰« Î·.naturality (ğŸ™ a)) â–· Î¸.app a âŠ—â‰« ğŸ™ _ := by
      rw [naturality_id]
      bicategory
    _ = _ := by
      rw [naturality_id]
      bicategory

theorem vComp_naturality_comp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) :
    Î·.vCompApp Î¸ a â— H.mapComp f g â‰« Î·.vCompNaturality Î¸ (f â‰« g) =
      (Î±_ (Î·.vCompApp Î¸ a) (H.map f) (H.map g)).inv â‰«
        Î·.vCompNaturality Î¸ f â–· H.map g â‰«
          (Î±_ (F.map f) (Î·.vCompApp Î¸ b) (H.map g)).hom â‰«
            F.map f â— Î·.vCompNaturality Î¸ g â‰«
              (Î±_ (F.map f) (F.map g) (Î·.vCompApp Î¸ c)).inv â‰« F.mapComp f g â–· Î·.vCompApp Î¸ c :=
  calc
    _ = ğŸ™ _ âŠ—â‰« Î·.app a â— (Î¸.app a â— H.mapComp f g â‰« Î¸.naturality (f â‰« g)) âŠ—â‰«
          Î·.naturality (f â‰« g) â–· Î¸.app c âŠ—â‰« ğŸ™ _ := by
      bicategory
    _ = ğŸ™ _ âŠ—â‰« Î·.app a â— (Î¸.naturality f â–· (H.map g) âŠ—â‰« G.map f â— Î¸.naturality g) âŠ—â‰«
          (Î·.app a â— G.mapComp f g â‰« Î·.naturality (f â‰« g)) â–· Î¸.app c âŠ—â‰« ğŸ™ _ := by
      rw [naturality_comp Î¸]
      bicategory
    _ = ğŸ™ _ âŠ—â‰« Î·.app a â— Î¸.naturality f â–· H.map g âŠ—â‰«
          ((Î·.app a â‰« G.map f) â— Î¸.naturality g â‰« Î·.naturality f â–· (G.map g â‰« Î¸.app c)) âŠ—â‰«
            F.map f â— Î·.naturality g â–· Î¸.app c âŠ—â‰«
              F.mapComp f g â–· Î·.app c â–· Î¸.app c âŠ—â‰« ğŸ™ _ := by
      rw [naturality_comp Î·]
      bicategory
    _ = _ := by
      rw [whisker_exchange]
      bicategory

/-- Vertical composition of lax transformations. -/
@[simps]
def vComp (Î· : LaxTrans F G) (Î¸ : LaxTrans G H) : LaxTrans F H where
  app a := vCompApp Î· Î¸ a
  naturality := vCompNaturality Î· Î¸
  naturality_naturality := vComp_naturality_naturality Î· Î¸
  naturality_id := vComp_naturality_id Î· Î¸
  naturality_comp := vComp_naturality_comp Î· Î¸

/-- `CategoryStruct` on `B â¥¤á´¸ C` where the (1-)morphisms are given by lax
transformations. -/
@[simps! id_app id_naturality comp_app comp_naturality]
scoped instance : CategoryStruct (B â¥¤á´¸ C) where
  Hom := LaxTrans
  id := LaxTrans.id
  comp := LaxTrans.vComp

end LaxTrans

/-- If `Î·` is an oplax transformation between `F` and `G`, we have a 1-morphism
`Î·.app a : F.obj a âŸ¶ G.obj a` for each object `a : B`. We also have a 2-morphism
`Î·.naturality f : F.map f â‰« app b âŸ¶ app a â‰« G.map f` for each 1-morphism `f : a âŸ¶ b`.
These 2-morphisms satisfies the naturality condition, and preserve the identities and
the compositions modulo some adjustments of domains and codomains of 2-morphisms.
-/
structure OplaxTrans (F G : B â¥¤á´¸ C) where
  /-- The component 1-morphisms of an oplax transformation. -/
  app (a : B) : F.obj a âŸ¶ G.obj a
  /-- The 2-morphisms underlying the oplax naturality constraint. -/
  naturality {a b : B} (f : a âŸ¶ b) : F.map f â‰« app b âŸ¶ app a â‰« G.map f
  /-- Naturality of the oplax naturality constraint. -/
  naturality_naturality {a b : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g) :
      F.mapâ‚‚ Î· â–· app b â‰« naturality g = naturality f â‰« app a â— G.mapâ‚‚ Î· := by
    cat_disch
  naturality_id (a : B) :
      F.mapId a â–· app a â‰« naturality (ğŸ™ a) =
        (Î»_ (app a)).hom â‰« (Ï_ (app a)).inv â‰« app a â— G.mapId a := by
    cat_disch
  naturality_comp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) :
      F.mapComp f g â–· app c â‰« naturality (f â‰« g) =
        (Î±_ _ _ _).hom â‰« F.map f â— naturality g â‰«
          (Î±_ _ _ _).inv â‰« naturality f â–· G.map g â‰« (Î±_ _ _ _).hom â‰«
            app a â— G.mapComp f g := by
    cat_disch

namespace OplaxTrans

attribute [reassoc (attr := simp)] naturality_naturality naturality_id naturality_comp

variable {F G H : B â¥¤á´¸ C} (Î· : OplaxTrans F G) (Î¸ : OplaxTrans G H)

variable (F) in
/-- The identity oplax transformation. -/
def id : OplaxTrans F F where
  app a := ğŸ™ (F.obj a)
  naturality {_ _} f := (Ï_ (F.map f)).hom â‰« (Î»_ (F.map f)).inv

instance : Inhabited (OplaxTrans F F) :=
  âŸ¨id FâŸ©

/-- Auxiliary definition for `vComp`. -/
abbrev vCompApp (a : B) : F.obj a âŸ¶ H.obj a := Î·.app a â‰« Î¸.app a

/-- Auxiliary definition for `vComp`. -/
abbrev vCompNaturality {a b : B} (f : a âŸ¶ b) :
    F.map f â‰« Î·.app b â‰« Î¸.app b âŸ¶ (Î·.app a â‰« Î¸.app a) â‰« H.map f :=
  (Î±_ _ _ _).inv â‰« Î·.naturality f â–· Î¸.app b â‰« (Î±_ _ _ _).hom â‰«
    Î·.app a â— Î¸.naturality f â‰« (Î±_ _ _ _).inv

theorem vComp_naturality_naturality {a b : B} {f g : a âŸ¶ b} (Î² : f âŸ¶ g) :
    F.mapâ‚‚ Î² â–· Î·.vCompApp Î¸ b â‰« Î·.vCompNaturality Î¸ g =
      Î·.vCompNaturality Î¸ f â‰« Î·.vCompApp Î¸ a â— H.mapâ‚‚ Î² := by
  calc
    _ = ğŸ™ _ âŠ—â‰« (F.mapâ‚‚ Î² â–· Î·.app b â‰« Î·.naturality g) â–· Î¸.app b âŠ—â‰«
          Î·.app a â— Î¸.naturality g âŠ—â‰« ğŸ™ _ := by
      bicategory
    _ = ğŸ™ _ âŠ—â‰« Î·.naturality f â–· Î¸.app b âŠ—â‰«
          Î·.app a â— (G.mapâ‚‚ Î² â–· Î¸.app b â‰« Î¸.naturality g) âŠ—â‰« ğŸ™ _ := by
      rw [Î·.naturality_naturality]
      bicategory
    _ = _ := by
      rw [Î¸.naturality_naturality]
      bicategory

theorem vComp_naturality_id (a : B) :
    F.mapId a â–· Î·.vCompApp Î¸ a â‰« Î·.vCompNaturality Î¸ (ğŸ™ a) =
      (Î»_ (Î·.vCompApp Î¸ a)).hom â‰« (Ï_ (Î·.vCompApp Î¸ a)).inv â‰« Î·.vCompApp Î¸ a â— H.mapId a := by
  calc
    _ = ğŸ™ _ âŠ—â‰« (F.mapId a â–· Î·.app a â‰« Î·.naturality (ğŸ™ a)) â–· Î¸.app a âŠ—â‰«
          Î·.app a â— Î¸.naturality (ğŸ™ a) âŠ—â‰« ğŸ™ _ := by
      bicategory
    _ = ğŸ™ _ âŠ—â‰« Î·.app a â— (G.mapId a â–· Î¸.app a â‰« Î¸.naturality (ğŸ™ a)) âŠ—â‰« ğŸ™ _ := by
      rw [Î·.naturality_id]
      bicategory
    _ = _ := by
      rw [Î¸.naturality_id]
      bicategory

theorem vComp_naturality_comp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) :
    F.mapComp f g â–· Î·.vCompApp Î¸ c â‰« Î·.vCompNaturality Î¸ (f â‰« g) =
      (Î±_ (F.map f) (F.map g) (Î·.vCompApp Î¸ c)).hom â‰«
        F.map f â— Î·.vCompNaturality Î¸ g â‰«
          (Î±_ (F.map f) (Î·.vCompApp Î¸ b) (H.map g)).inv â‰«
            Î·.vCompNaturality Î¸ f â–· H.map g â‰«
              (Î±_ (Î·.vCompApp Î¸ a) (H.map f) (H.map g)).hom â‰« Î·.vCompApp Î¸ a â— H.mapComp f g := by
  calc
    _ = ğŸ™ _ âŠ—â‰« (F.mapComp f g â–· Î·.app c â‰« Î·.naturality (f â‰« g)) â–· Î¸.app c âŠ—â‰«
          Î·.app a â— Î¸.naturality (f â‰« g) âŠ—â‰« ğŸ™ _ := by
      bicategory
    _ = ğŸ™ _ âŠ—â‰« (F.map f â— Î·.naturality g âŠ—â‰« Î·.naturality f â–· G.map g) â–· Î¸.app c âŠ—â‰«
          Î·.app a â— (G.mapComp f g â–· Î¸.app c â‰« Î¸.naturality (f â‰« g)) âŠ—â‰« ğŸ™ _ := by
      rw [Î·.naturality_comp]
      bicategory
    _ = ğŸ™ _ âŠ—â‰« F.map f â— Î·.naturality g â–· Î¸.app c âŠ—â‰«
          (Î·.naturality f â–· (G.map g â‰« Î¸.app c) â‰« (Î·.app a â‰« G.map f) â— Î¸.naturality g) âŠ—â‰«
            Î·.app a â— (Î¸.naturality f â–· H.map g âŠ—â‰« Î¸.app a â— H.mapComp f g) âŠ—â‰« ğŸ™ _ := by
      rw [Î¸.naturality_comp]
      bicategory
    _ = _ := by
      rw [â† whisker_exchange]
      bicategory

/-- Vertical composition of oplax transformations. -/
def vComp (Î· : OplaxTrans F G) (Î¸ : OplaxTrans G H) : OplaxTrans F H where
  app := vCompApp Î· Î¸
  naturality := vCompNaturality Î· Î¸
  naturality_naturality := vComp_naturality_naturality Î· Î¸
  naturality_id := vComp_naturality_id Î· Î¸
  naturality_comp := vComp_naturality_comp Î· Î¸

/-- `CategoryStruct` on `B â¥¤á´¸ C` where the (1-)morphisms are given by oplax
transformations. -/
@[simps! id_app id_naturality comp_app comp_naturality]
scoped instance : CategoryStruct (B â¥¤á´¸ C) where
  Hom := OplaxTrans
  id := OplaxTrans.id
  comp := OplaxTrans.vComp

end OplaxTrans

/-- A strong natural transformation between lax functors `F` and `G` is a natural transformation
that is "natural up to 2-isomorphisms".

More precisely, it consists of the following:
* a 1-morphism `Î·.app a : F.obj a âŸ¶ G.obj a` for each object `a : B`.
* a 2-isomorphism `Î·.naturality f : app a â‰« G.map f â‰… F.map f â‰« app b` for each 1-morphism
`f : a âŸ¶ b`.
* These 2-isomorphisms satisfy the naturality condition, and preserve the identities and the
  compositions modulo some adjustments of domains and codomains of 2-morphisms.
-/
structure StrongTrans (F G : B â¥¤á´¸ C) where
  app (a : B) : F.obj a âŸ¶ G.obj a
  naturality {a b : B} (f : a âŸ¶ b) : app a â‰« G.map f â‰… F.map f â‰« app b
  naturality_naturality {a b : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g) :
      (naturality f).hom â‰« F.mapâ‚‚ Î· â–· app b = app a â— G.mapâ‚‚ Î· â‰« (naturality g).hom := by
    cat_disch
  /-- Lax unity. -/
  naturality_id (a : B) :
      app a â— G.mapId a â‰« (naturality (ğŸ™ a)).hom =
        (Ï_ (app a)).hom â‰« (Î»_ (app a)).inv â‰« F.mapId a â–· app a := by
    cat_disch
  /-- Lax functoriality. -/
  naturality_comp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) :
      app a â— G.mapComp f g â‰« (naturality (f â‰« g)).hom =
      (Î±_ _ _ _).inv â‰« (naturality f).hom â–· G.map g â‰« (Î±_ _ _ _).hom â‰«
        F.map f â— (naturality g).hom â‰« (Î±_ _ _ _).inv â‰« F.mapComp f g â–· app c := by
    cat_disch

attribute [nolint docBlame] CategoryTheory.Lax.StrongTrans.app
  CategoryTheory.Lax.StrongTrans.naturality

attribute [reassoc (attr := simp)] StrongTrans.naturality_naturality
  StrongTrans.naturality_id StrongTrans.naturality_comp

/-- A structure on a lax transformation that promotes it to a strong transformation.

See `Pseudofunctor.StrongTrans.mkOfLax`. -/
structure LaxTrans.StrongCore {F G : B â¥¤á´¸ C} (Î· : F âŸ¶ G) where
  /-- The underlying 2-isomorphisms of the naturality constraint. -/
  naturality {a b : B} (f : a âŸ¶ b) : Î·.app a â‰« G.map f â‰… F.map f â‰« Î·.app b
  /-- The 2-isomorphisms agree with the underlying 2-morphism of the lax transformation. -/
  naturality_hom {a b : B} (f : a âŸ¶ b) : (naturality f).hom = Î·.naturality f := by cat_disch

attribute [simp] LaxTrans.StrongCore.naturality_hom

namespace StrongTrans

/-- The underlying lax natural transformation of a strong natural transformation. -/
@[simps]
def toLax {F G : B â¥¤á´¸ C} (Î· : StrongTrans F G) : LaxTrans F G where
  app := Î·.app
  naturality f := (Î·.naturality f).hom

/-- Construct a strong natural transformation from a lax natural transformation whose
naturality 2-morphism is an isomorphism. -/
def mkOfLax {F G : B â¥¤á´¸ C} (Î· : LaxTrans F G) (Î·' : LaxTrans.StrongCore Î·) :
    StrongTrans F G where
  app := Î·.app
  naturality := Î·'.naturality

/-- Construct a strong natural transformation from a lax natural transformation whose
naturality 2-morphism is an isomorphism. -/
noncomputable def mkOfLax' {F G : B â¥¤á´¸ C} (Î· : LaxTrans F G)
    [âˆ€ a b (f : a âŸ¶ b), IsIso (Î·.naturality f)] : StrongTrans F G where
  app := Î·.app
  naturality _ := asIso (Î·.naturality _)

variable (F : B â¥¤á´¸ C)

/-- The identity strong natural transformation. -/
@[simps!]
def id : StrongTrans F F :=
  mkOfLax (LaxTrans.id F) { naturality := fun f â†¦ (Î»_ (F.map f)) â‰ªâ‰« (Ï_ (F.map f)).symm }

@[simp]
lemma id.toLax : (id F).toLax = LaxTrans.id F :=
  rfl

instance : Inhabited (StrongTrans F F) :=
  âŸ¨id FâŸ©

variable {F} {G H : B â¥¤á´¸ C} (Î· : StrongTrans F G) (Î¸ : StrongTrans G H)

/-- Vertical composition of strong natural transformations. -/
@[simps!]
def vComp : StrongTrans F H :=
  mkOfLax (LaxTrans.vComp Î·.toLax Î¸.toLax)
    { naturality := fun {a b} f â†¦
        (Î±_ _ _ _) â‰ªâ‰« whiskerLeftIso (Î·.app a) (Î¸.naturality f) â‰ªâ‰« (Î±_ _ _ _).symm â‰ªâ‰«
        whiskerRightIso (Î·.naturality f) (Î¸.app b) â‰ªâ‰« (Î±_ _ _ _) }

/-- `CategoryStruct` on `B â¥¤á´¸ C` where the (1-)morphisms are given by strong
transformations. -/
@[simps! id_app id_naturality comp_app comp_naturality]
scoped instance categoryStruct : CategoryStruct (B â¥¤á´¸ C) where
  Hom := StrongTrans
  id := StrongTrans.id
  comp := StrongTrans.vComp

end StrongTrans

end CategoryTheory.Lax
