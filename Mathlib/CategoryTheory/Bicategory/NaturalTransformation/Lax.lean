/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno, Calle SÃ¶nne, Robin Carlier
-/
import Mathlib.CategoryTheory.Bicategory.Functor.Lax

/-!
# Transformations between lax functors

Just as there are natural transformations between functors, there are transformations
between lax functors. The equality in the naturality condition of a natural transformation gets
replaced by a specified 2-morphism. Now, there are three possible types of transformations (between
lax functors):
* Lax natural transformations;
* OpLax natural transformations;
* Strong natural transformations.
These differ in the direction (and invertibility) of the 2-morphisms involved in the naturality
condition.

## Main definitions

* `Lax.LaxTrans F G`: lax transformations between lax functors `F` and `G`. The naturality
  condition is given by a 2-morphism `F.map f â‰« app b âŸ¶ app a â‰« G.map f` for each 1-morphism
  `f : a âŸ¶ b`.
* `Lax.StrongTrans F G`: Strong transformations between lax functors `F` and `G`.

Using these, we define two `CategoryStruct` (scoped) instances on `LaxFunctor B C`, one in the
`Lax.LaxTrans` namespace and one in the `Lax.StrongTrans` namespace. The arrows in these
CategoryStruct's are given by lax transformations and strong transformations respectively.

We also provide API for going between lax transformations and strong transformations:
* `Lax.StrongCore F G`: a structure on an lax transformation between lax functors that
promotes it to a strong transformation.
* `Lax.mkOfLax Î· Î·'`: given an lax transformation `Î·` such that each component
  2-morphism is an isomorphism, `mkOfLax` gives the corresponding strong transformation.

# TODO
This file could also include oplax transformations between lax functors.

## References
* [Niles Johnson, Donald Yau, *2-Dimensional Categories*](https://arxiv.org/abs/2002.06055),
section 4.2.

-/

namespace CategoryTheory.Lax

open Category Bicategory

universe wâ‚ wâ‚‚ vâ‚ vâ‚‚ uâ‚ uâ‚‚

variable {B : Type uâ‚} [Bicategory.{wâ‚, vâ‚} B] {C : Type uâ‚‚} [Bicategory.{wâ‚‚, vâ‚‚} C]

/-- If `Î·` is an lax transformation between `F` and `G`, we have a 1-morphism
`Î·.app a : F.obj a âŸ¶ G.obj a` for each object `a : B`. We also have a 2-morphism
`Î·.naturality f : app a â‰« G.map f âŸ¶ F.map f â‰« app b` for each 1-morphism `f : a âŸ¶ b`.
These 2-morphisms satisfies the naturality condition, and preserve the identities and
the compositions modulo some adjustments of domains and codomains of 2-morphisms.
-/
structure LaxTrans (F G : LaxFunctor B C) where
  /-- The component 1-morphisms of an lax transformation. -/
  app (a : B) : F.obj a âŸ¶ G.obj a
  /-- The 2-morphisms underlying the lax naturality constraint. -/
  naturality {a b : B} (f : a âŸ¶ b) : app a â‰« G.map f âŸ¶ F.map f â‰« app b
  /-- Naturality of the lax naturality constraint. -/
  naturality_naturality {a b : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g) :
     naturality f â‰« F.mapâ‚‚ Î· â–· app b = app a â— G.mapâ‚‚ Î· â‰« naturality g := by
    aesop_cat
  /-- Lax unity. -/
  naturality_id (a : B) :
      app a â— G.mapId a â‰« naturality (ğŸ™ a) =
        (Ï_ (app a)).hom â‰« (Î»_ (app a)).inv â‰« F.mapId a â–· app a := by
    aesop_cat
  /-- Lax functoriality. -/
  naturality_comp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) :
      app a â— G.mapComp f g â‰« naturality (f â‰« g) =
      (Î±_ _ _ _).inv â‰« naturality f â–· G.map g â‰« (Î±_ _ _ _).hom â‰«
        F.map f â— naturality g â‰« (Î±_ _ _ _).inv â‰« F.mapComp f g â–· app c := by
    aesop_cat

attribute [reassoc (attr := simp)] LaxTrans.naturality_naturality LaxTrans.naturality_id
  LaxTrans.naturality_comp

namespace LaxTrans

variable {F : LaxFunctor B C} {G H : LaxFunctor B C} (Î· : LaxTrans F G) (Î¸ : LaxTrans G H)

section

variable {a b c : B} {a' : C}

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality_naturality (f : a' âŸ¶ G.obj a) {g h : a âŸ¶ b} (Î² : g âŸ¶ h) :
    f â— Î¸.naturality g â‰« f â— G.mapâ‚‚ Î² â–· Î¸.app b =
    f â— Î¸.app a â— H.mapâ‚‚ Î² â‰« f â— Î¸.naturality h := by
  simp_rw [â† Bicategory.whiskerLeft_comp, naturality_naturality]

@[reassoc (attr := simp)]
theorem whiskerRight_naturality_naturality {f g : a âŸ¶ b} (Î² : f âŸ¶ g) (h : G.obj b âŸ¶ a') :
    Î·.naturality f â–· h â‰« F.mapâ‚‚ Î² â–· Î·.app b â–· h =
    (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapâ‚‚ Î² â–· h  â‰« (Î±_ _ _ _).inv â‰« Î·.naturality g â–· h := by
  rw [â† comp_whiskerRight, naturality_naturality, comp_whiskerRight, whisker_assoc,
    Category.assoc, Category.assoc]

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality_comp (f : a' âŸ¶ G.obj a) (g : a âŸ¶ b) (h : b âŸ¶ c) :
    f â— Î¸.app a â— H.mapComp g h â‰« f â— Î¸.naturality (g â‰« h) =
    f â— (Î±_ _ _ _).inv â‰« f â— Î¸.naturality g â–· H.map h â‰« f â— (Î±_ _ _ _).hom â‰«
      f â— G.map g â— Î¸.naturality h â‰« f â— (Î±_ _ _ _).inv â‰« f â— G.mapComp g h â–· Î¸.app c := by
  simp_rw [â† Bicategory.whiskerLeft_comp, naturality_comp]

@[reassoc (attr := simp)]
theorem whiskerRight_naturality_comp (f : a âŸ¶ b) (g : b âŸ¶ c) (h : G.obj c âŸ¶ a') :
    (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapComp f g â–· h â‰« (Î±_ _ _ _).inv â‰«
      Î·.naturality (f â‰« g) â–· h =
    (Î±_ _ _ _).inv â–· h â‰«
      Î·.naturality f â–· G.map g â–· h â‰«
      (Î±_ _ _ _).hom â–· h â‰« (Î±_ _ _ _).hom â‰«
      F.map f â— Î·.naturality g â–· h â‰« (Î±_ _ _ _).inv â‰«
      (Î±_ _ _ _).inv â–· h â‰« F.mapComp f g â–· Î·.app c â–· h := by
  simpa [-naturality_comp] using congr_arg (fun t â†¦ t â–· h) <| naturality_comp _ _ _

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality_id (f : a' âŸ¶ G.obj a) :
    f â— Î¸.app a â— H.mapId a â‰« f â— Î¸.naturality (ğŸ™ a) =
    (Î±_ _ _ _).inv â‰« (Ï_ (f â‰« Î¸.app a)).hom â‰« f â— (Î»_ (Î¸.app a)).inv â‰«
      f â— G.mapId a â–· Î¸.app a := by
  simpa [-naturality_id] using congr_arg (fun t â†¦ f â— t) <| naturality_id _ _

@[reassoc (attr := simp)]
theorem whiskerRight_naturality_id (f : G.obj a âŸ¶ a') :
    (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapId a â–· f â‰« (Î±_ _ _ _).inv â‰« Î·.naturality (ğŸ™ a) â–· f =
    (Ï_ (Î·.app a)).hom â–· f â‰« (Î»_ (Î·.app a â‰« f)).inv â‰« (Î±_ _ _ _).inv â‰« F.mapId a â–· Î·.app a â–· f := by
  simpa [-naturality_id] using congr_arg (fun t â†¦ t â–· f) <| naturality_id _ _

end

variable (F) in
/-- The identity lax transformation. -/
def id : LaxTrans F F where
  app a := ğŸ™ (F.obj a)
  naturality {_ _} f := (Î»_ (F.map f)).hom â‰« (Ï_ (F.map f)).inv

instance : Inhabited (LaxTrans F F) :=
  âŸ¨id FâŸ©

/-- Vertical composition of lax transformations. -/
@[simps]
def vcomp : LaxTrans F H where
  app a := Î·.app a â‰« Î¸.app a
  naturality {a b} f :=
    (Î±_ _ _ _).hom â‰« Î·.app a â— (Î¸.naturality f) â‰« (Î±_ _ _ _).inv â‰«
      Î·.naturality f â–· Î¸.app b â‰« (Î±_ _ _ _).hom
  naturality_comp {a b c} f g := by
    calc
      _ = (Î±_ _ _ _).hom â‰« _ â— (Î±_ _ _ _).inv  â‰«
            Î·.app a â— Î¸.naturality f â–· H.map g â‰«
            _ â— (Î±_ _ _ _).hom â‰« (Î±_ _ _ _).inv â‰«
            Î·.naturality f â–· (Î¸.app b â‰« H.map g) â‰«
            (F.map f â‰« Î·.app b) â— Î¸.naturality g â‰«
            (Î±_ _ _ _).hom â‰« F.map f â— (Î±_ _ _ _).inv â‰«
            F.map f â— Î·.naturality g â–· Î¸.app c â‰«
            (Î±_ _ _ _).inv â‰« (Î±_ _ _ _).inv â–· _ â‰«
            F.mapComp f g â–· Î·.app c â–· Î¸.app c â‰« (Î±_ _ _ _).hom := by
        rw [â† whisker_exchange_assoc]
        simp only [â† whisker_exchange_assoc, comp_whiskerLeft, assoc, Iso.inv_hom_id_assoc,
          whiskerLeft_naturality_comp_assoc, Bicategory.whiskerRight_comp,
          pentagon_hom_hom_inv_hom_hom_assoc, Iso.cancel_iso_hom_left,
          associator_inv_naturality_middle_assoc]
        simp
      _ = _ := by simp
  naturality_id x := by
    simp only [comp_whiskerLeft, assoc, Iso.inv_hom_id_assoc, whiskerLeft_naturality_id_assoc,
      Iso.hom_inv_id_assoc, Bicategory.whiskerRight_comp, Iso.cancel_iso_hom_left,
      associator_inv_naturality_middle_assoc]
    simp

/-- `CategoryStruct` on `LaxFunctor B C` where the (1-)morphisms are given by lax
transformations. -/
@[simps! id_app id_naturality comp_app comp_naturality]
scoped instance : CategoryStruct (LaxFunctor B C) where
  Hom := LaxTrans
  id := LaxTrans.id
  comp := LaxTrans.vcomp

end LaxTrans

/-- A strong natural transformation between lax functors `F` and `G` is a natural transformation
that is "natural up to 2-isomorphisms".

More precisely, it consists of the following:
* a 1-morphism `Î·.app a : F.obj a âŸ¶ G.obj a` for each object `a : B`.
* a 2-isomorphism `Î·.naturality f : F.map f â‰« app b âŸ¶ app a â‰« G.map f` for each 1-morphism
`f : a âŸ¶ b`.
* These 2-isomorphisms satisfy the naturality condition, and preserve the identities and the
  compositions modulo some adjustments of domains and codomains of 2-morphisms.
-/
structure StrongTrans (F G : LaxFunctor B C) where
  app (a : B) : F.obj a âŸ¶ G.obj a
  naturality {a b : B} (f : a âŸ¶ b) : app a â‰« G.map f â‰… F.map f â‰« app b
  naturality_naturality {a b : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g) :
     (naturality f).hom â‰« F.mapâ‚‚ Î· â–· app b = app a â— G.mapâ‚‚ Î· â‰« (naturality g).hom := by
    aesop_cat
  /-- Lax unity. -/
  naturality_id (a : B) :
      app a â— G.mapId a â‰« (naturality (ğŸ™ a)).hom =
        (Ï_ (app a)).hom â‰« (Î»_ (app a)).inv â‰« F.mapId a â–· app a := by
    aesop_cat
  /-- Lax functoriality. -/
  naturality_comp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) :
      app a â— G.mapComp f g â‰« (naturality (f â‰« g)).hom =
      (Î±_ _ _ _).inv â‰« (naturality f).hom â–· G.map g â‰« (Î±_ _ _ _).hom â‰«
        F.map f â— (naturality g).hom â‰« (Î±_ _ _ _).inv â‰« F.mapComp f g â–· app c := by
    aesop_cat

attribute [nolint docBlame] CategoryTheory.Lax.StrongTrans.app
  CategoryTheory.Lax.StrongTrans.naturality

attribute [reassoc (attr := simp)] StrongTrans.naturality_naturality
  StrongTrans.naturality_id StrongTrans.naturality_comp

/-- A structure on an lax transformation that promotes it to a strong transformation.

See `Pseudofunctor.StrongTrans.mkOfLax`. -/
structure LaxTrans.StrongCore {F G : LaxFunctor B C} (Î· : F âŸ¶ G) where
  /-- The underlying 2-isomorphisms of the naturality constraint. -/
  naturality {a b : B} (f : a âŸ¶ b) : Î·.app a â‰« G.map f â‰… F.map f â‰« Î·.app b
  /-- The 2-isomorphisms agree with the underlying 2-morphism of the lax transformation. -/
  naturality_hom {a b : B} (f : a âŸ¶ b) : (naturality f).hom = Î·.naturality f := by aesop_cat

attribute [simp] LaxTrans.StrongCore.naturality_hom

namespace StrongTrans

/-- The underlying lax natural transformation of a strong natural transformation. -/
@[simps]
def toLax {F G : LaxFunctor B C} (Î· : StrongTrans F G) : LaxTrans F G where
  app := Î·.app
  naturality f := (Î·.naturality f).hom

/-- Construct a strong natural transformation from an lax natural transformation whose
naturality 2-morphism is an isomorphism. -/
def mkOfLax {F G : LaxFunctor B C} (Î· : LaxTrans F G) (Î·' : LaxTrans.StrongCore Î·) :
    StrongTrans F G where
  app := Î·.app
  naturality := Î·'.naturality

/-- Construct a strong natural transformation from an lax natural transformation whose
naturality 2-morphism is an isomorphism. -/
noncomputable def mkOfLax' {F G : LaxFunctor B C} (Î· : LaxTrans F G)
    [âˆ€ a b (f : a âŸ¶ b), IsIso (Î·.naturality f)] : StrongTrans F G where
  app := Î·.app
  naturality _ := asIso (Î·.naturality _)

variable (F : LaxFunctor B C)


/-- The identity strong natural transformation. -/
@[simps!]
def id : StrongTrans F F :=
  mkOfLax (LaxTrans.id F) { naturality := fun f â†¦ (Î»_ (F.map f)) â‰ªâ‰« (Ï_ (F.map f)).symm }

@[simp]
lemma id.toLax : (id F).toLax = LaxTrans.id F :=
  rfl

instance : Inhabited (StrongTrans F F) :=
  âŸ¨id FâŸ©


variable {F} {G H : LaxFunctor B C} (Î· : StrongTrans F G) (Î¸ : StrongTrans G H)

/-- Vertical composition of strong natural transformations. -/
@[simps!]
def vcomp : StrongTrans F H :=
  mkOfLax (LaxTrans.vcomp Î·.toLax Î¸.toLax)
    { naturality := fun {a b} f â†¦
        (Î±_ _ _ _) â‰ªâ‰« whiskerLeftIso (Î·.app a) (Î¸.naturality f) â‰ªâ‰« (Î±_ _ _ _).symm â‰ªâ‰«
        whiskerRightIso (Î·.naturality f) (Î¸.app b) â‰ªâ‰« (Î±_ _ _ _) }

/-- `CategoryStruct` on `LaxFunctor B C` where the (1-)morphisms are given by strong
transformations. -/
@[simps! id_app id_naturality comp_app comp_naturality]
scoped instance LaxFunctor.instCategoryStruct : CategoryStruct (LaxFunctor B C) where
  Hom := StrongTrans
  id := StrongTrans.id
  comp := StrongTrans.vcomp

section

variable {a b c : B} {a' : C}

@[reassoc (attr := simp), to_app]
theorem whiskerLeft_naturality_naturality (f : a' âŸ¶ G.obj a) {g h : a âŸ¶ b} (Î² : g âŸ¶ h) :
    f â— (Î¸.naturality g).hom â‰« f â— G.mapâ‚‚ Î² â–· Î¸.app b =
    f â— Î¸.app a â— H.mapâ‚‚ Î² â‰« f â— (Î¸.naturality h).hom := by
  apply Î¸.toLax.whiskerLeft_naturality_naturality

@[reassoc (attr := simp), to_app]
theorem whiskerRight_naturality_naturality {f g : a âŸ¶ b} (Î² : f âŸ¶ g) (h : G.obj b âŸ¶ a') :
    (Î·.naturality f).hom â–· h â‰« F.mapâ‚‚ Î² â–· Î·.app b â–· h =
    (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapâ‚‚ Î² â–· h  â‰« (Î±_ _ _ _).inv â‰« (Î·.naturality g).hom â–· h :=
  Î·.toLax.whiskerRight_naturality_naturality _ _

@[reassoc (attr := simp), to_app]
theorem whiskerLeft_naturality_comp (f : a' âŸ¶ G.obj a) (g : a âŸ¶ b) (h : b âŸ¶ c) :
    f â— Î¸.app a â— H.mapComp g h â‰« f â— (Î¸.naturality (g â‰« h)).hom =
    f â— (Î±_ _ _ _).inv â‰« f â— (Î¸.naturality g).hom â–· H.map h â‰« f â— (Î±_ _ _ _).hom â‰«
      f â— G.map g â— (Î¸.naturality h).hom â‰« f â— (Î±_ _ _ _).inv â‰« f â— G.mapComp g h â–· Î¸.app c  :=
  Î¸.toLax.whiskerLeft_naturality_comp _ _ _

@[reassoc (attr := simp), to_app]
theorem whiskerRight_naturality_comp (f : a âŸ¶ b) (g : b âŸ¶ c) (h : G.obj c âŸ¶ a') :
    (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapComp f g â–· h â‰« (Î±_ _ _ _).inv â‰«
      (Î·.naturality (f â‰« g)).hom â–· h =
    (Î±_ _ _ _).inv â–· h â‰«
      (Î·.naturality f).hom â–· G.map g â–· h â‰«
      (Î±_ _ _ _).hom â–· h â‰« (Î±_ _ _ _).hom â‰«
      F.map f â— (Î·.naturality g).hom â–· h â‰« (Î±_ _ _ _).inv â‰«
      (Î±_ _ _ _).inv â–· h â‰« F.mapComp f g â–· Î·.app c â–· h :=
  Î·.toLax.whiskerRight_naturality_comp _ _ _

@[reassoc (attr := simp), to_app]
theorem whiskerLeft_naturality_id (f : a' âŸ¶ G.obj a) :
    f â— Î¸.app a â— H.mapId a â‰« f â— (Î¸.naturality (ğŸ™ a)).hom =
    (Î±_ _ _ _).inv â‰« (Ï_ (f â‰« Î¸.app a)).hom â‰« f â— (Î»_ (Î¸.app a)).inv â‰«
      f â— G.mapId a â–· Î¸.app a :=
  Î¸.toLax.whiskerLeft_naturality_id _

@[reassoc (attr := simp), to_app]
theorem whiskerRight_naturality_id (f : G.obj a âŸ¶ a') :
    (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapId a â–· f â‰« (Î±_ _ _ _).inv â‰« (Î·.naturality (ğŸ™ a)).hom â–· f =
    (Ï_ (Î·.app a)).hom â–· f â‰« (Î»_ (Î·.app a â‰« f)).inv â‰« (Î±_ _ _ _).inv â‰« F.mapId a â–· Î·.app a â–· f :=
  Î·.toLax.whiskerRight_naturality_id _

end

end StrongTrans

end CategoryTheory.Lax
