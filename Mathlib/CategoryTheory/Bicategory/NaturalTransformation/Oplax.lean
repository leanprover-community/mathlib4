/-
Copyright (c) 2022 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno, Calle SÃ¶nne
-/
module

public import Mathlib.CategoryTheory.Bicategory.Functor.Oplax
public import Mathlib.Tactic.CategoryTheory.Bicategory.Basic

/-!
# Transformations between oplax functors

Just as there are natural transformations between functors, there are transformations
between oplax functors. The equality in the naturality condition of a natural transformation gets
replaced by a specified 2-morphism. Now, there are three possible types of transformations (between
oplax functors):
* Oplax natural transformations;
* Lax natural transformations;
* Strong natural transformations.
These differ in the direction (and invertibility) of the 2-morphisms involved in the naturality
condition.

## Main definitions

* `Oplax.LaxTrans F G`: oplax transformations between oplax functors `F` and `G`. The naturality
  condition is given by a 2-morphism `app a â‰« G.map f âŸ¶ F.map f â‰« app b` for each 1-morphism
  `f : a âŸ¶ b`.
* `Oplax.OplaxTrans F G`: oplax transformations between oplax functors `F` and `G`. The naturality
  condition is given by a 2-morphism `F.map f â‰« app b âŸ¶ app a â‰« G.map f` for each 1-morphism
  `f : a âŸ¶ b`.
* `Oplax.StrongTrans F G`: Strong transformations between oplax functors `F` and `G`. The naturality
  condition is given by a 2-isomorphism `F.map f â‰« app b â‰… app a â‰« G.map f` for each 1-morphism
  `f : a âŸ¶ b`.

Using these, we define three `CategoryStruct` (scoped) instances on `B â¥¤áµ’áµ–á´¸ C`, in the
`Oplax.LaxTrans`, `Oplax.OplaxTrans`, and `Oplax.StrongTrans` namespaces. The arrows in these
CategoryStruct's are given by lax transformations, oplax transformations, and strong
transformations respectively.

We also provide API for going between oplax transformations and strong transformations:
* `Oplax.StrongCore F G`: a structure on an oplax transformation between oplax functors that
  promotes it to a strong transformation.
* `Oplax.mkOfOplax Î· Î·'`: given an oplax transformation `Î·` such that each component
  2-morphism is an isomorphism, `mkOfOplax` gives the corresponding strong transformation.

## References
* [Niles Johnson, Donald Yau, *2-Dimensional Categories*](https://arxiv.org/abs/2002.06055)

-/

@[expose] public section

namespace CategoryTheory.Oplax

open Category Bicategory

universe wâ‚ wâ‚‚ vâ‚ vâ‚‚ uâ‚ uâ‚‚

variable {B : Type uâ‚} [Bicategory.{wâ‚, vâ‚} B] {C : Type uâ‚‚} [Bicategory.{wâ‚‚, vâ‚‚} C]

/-- If `Î·` is a lax transformation between `F` and `G`, we have a 1-morphism
`Î·.app a : F.obj a âŸ¶ G.obj a` for each object `a : B`. We also have a 2-morphism
`Î·.naturality f : app a â‰« G.map f âŸ¶ F.map f â‰« app b` for each 1-morphism `f : a âŸ¶ b`.
These 2-morphisms satisfy the naturality condition, and preserve the identities and
the compositions modulo some adjustments of domains and codomains of 2-morphisms.
-/
structure LaxTrans (F G : OplaxFunctor B C) where
  /-- The component 1-morphisms of an oplax transformation. -/
  app (a : B) : F.obj a âŸ¶ G.obj a
  /-- The 2-morphisms underlying the oplax naturality constraint. -/
  naturality {a b : B} (f : a âŸ¶ b) : app a â‰« G.map f âŸ¶ F.map f â‰« app b
  naturality_naturality {a b : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g) :
      naturality f â‰« F.mapâ‚‚ Î· â–· app b = app a â— G.mapâ‚‚ Î· â‰« naturality g := by
    cat_disch
  naturality_id (a : B) :
      naturality (ğŸ™ a) â‰« F.mapId a â–· app a =
        app a â— G.mapId a â‰« (Ï_ (app a)).hom â‰« (Î»_ (app a)).inv := by
    cat_disch
  naturality_comp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) :
      naturality (f â‰« g) â‰« F.mapComp f g â–· app c =
        app a â— G.mapComp f g â‰« (Î±_ _ _ _).inv â‰«
          naturality f â–· G.map g â‰« (Î±_ _ _ _).hom â‰«
            F.map f â— naturality g â‰« (Î±_ _ _ _).inv := by
    cat_disch

namespace LaxTrans

attribute [reassoc (attr := simp)] naturality_naturality naturality_id naturality_comp

variable {F G H : OplaxFunctor B C}
variable (Î· : LaxTrans F G) (Î¸ : LaxTrans G H)

variable (F) in
/-- The identity lax transformation. -/
def id : LaxTrans F F where
  app a := ğŸ™ (F.obj a)
  naturality {_ _} f := (Î»_ (F.map f)).hom â‰« (Ï_ (F.map f)).inv

instance : Inhabited (LaxTrans F F) :=
  âŸ¨id FâŸ©

/-- Auxiliary definition for `vComp`. -/
abbrev vCompApp (a : B) : F.obj a âŸ¶ H.obj a :=
  Î·.app a â‰« Î¸.app a

/-- Auxiliary definition for `vComp`. -/
abbrev vCompNaturality {a b : B} (f : a âŸ¶ b) :
    (Î·.app a â‰« Î¸.app a) â‰« H.map f âŸ¶ F.map f â‰« Î·.app b â‰« Î¸.app b :=
  (Î±_ _ _ _).hom â‰« Î·.app a â— Î¸.naturality f â‰« (Î±_ _ _ _).inv â‰«
    Î·.naturality f â–· Î¸.app b â‰« (Î±_ _ _ _).hom

theorem vComp_naturality_naturality {a b : B} {f g : a âŸ¶ b} (Î² : f âŸ¶ g) :
    Î·.vCompNaturality Î¸ f â‰« F.mapâ‚‚ Î² â–· Î·.vCompApp Î¸ b =
      Î·.vCompApp Î¸ a â— H.mapâ‚‚ Î² â‰« Î·.vCompNaturality Î¸ g :=
  calc
    _ = ğŸ™ _ âŠ—â‰« Î·.app a â— Î¸.naturality f âŠ—â‰«
          (Î·.naturality f â‰« F.mapâ‚‚ Î² â–· Î·.app b) â–· Î¸.app b âŠ—â‰« ğŸ™ _ := by
      bicategory
    _ = ğŸ™ _ âŠ—â‰« Î·.app a â— (Î¸.naturality f â‰« G.mapâ‚‚ Î² â–· Î¸.app b) âŠ—â‰«
          Î·.naturality g â–· Î¸.app b âŠ—â‰« ğŸ™ _ := by
      rw [naturality_naturality]
      bicategory
    _ = _ := by
      rw [naturality_naturality]
      bicategory

theorem vComp_naturality_id (a : B) :
    Î·.vCompNaturality Î¸ (ğŸ™ a) â‰« F.mapId a â–· Î·.vCompApp Î¸ a =
      Î·.vCompApp Î¸ a â— H.mapId a â‰« (Ï_ (Î·.vCompApp Î¸ a)).hom â‰« (Î»_ (Î·.vCompApp Î¸ a)).inv := by
  calc
    _ = ğŸ™ _ âŠ—â‰« Î·.app a â— Î¸.naturality (ğŸ™ a) âŠ—â‰«
          (Î·.naturality (ğŸ™ a) â‰« F.mapId a â–· Î·.app a) â–· Î¸.app a âŠ—â‰« ğŸ™ _ := by
      bicategory
    _ = ğŸ™ _ âŠ—â‰« Î·.app a â— (Î¸.naturality (ğŸ™ a) â‰« G.mapId a â–· Î¸.app a) âŠ—â‰« ğŸ™ _ := by
      rw [Î·.naturality_id]
      bicategory
    _ = _ := by
      rw [Î¸.naturality_id]
      bicategory

theorem vComp_naturality_comp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) :
    Î·.vCompNaturality Î¸ (f â‰« g) â‰« F.mapComp f g â–· Î·.vCompApp Î¸ c =
      Î·.vCompApp Î¸ a â— H.mapComp f g â‰«
        (Î±_ (Î·.vCompApp Î¸ a) (H.map f) (H.map g)).inv â‰«
          Î·.vCompNaturality Î¸ f â–· H.map g â‰«
            (Î±_ (F.map f) (Î·.vCompApp Î¸ b) (H.map g)).hom â‰«
              F.map f â— Î·.vCompNaturality Î¸ g â‰« (Î±_ (F.map f) (F.map g) (Î·.vCompApp Î¸ c)).inv := by
  calc
    _ = ğŸ™ _ âŠ—â‰« Î·.app a â— Î¸.naturality (f â‰« g) âŠ—â‰«
          (Î·.naturality (f â‰« g) â‰« F.mapComp f g â–· Î·.app c) â–· Î¸.app c âŠ—â‰« ğŸ™ _ := by
      bicategory
    _ = ğŸ™ _ âŠ—â‰« Î·.app a â— (Î¸.naturality (f â‰« g) â‰« G.mapComp f g â–· Î¸.app c) âŠ—â‰«
          (Î·.naturality f â–· G.map g âŠ—â‰« F.map f â— Î·.naturality g) â–· Î¸.app c âŠ—â‰« ğŸ™ _ := by
      rw [Î·.naturality_comp]
      bicategory
    _ = ğŸ™ _ âŠ—â‰« Î·.app a â— (Î¸.app a â— H.mapComp f g âŠ—â‰« Î¸.naturality f â–· H.map g) âŠ—â‰«
          ((Î·.app a â‰« G.map f) â— Î¸.naturality g â‰« Î·.naturality f â–· (G.map g â‰« Î¸.app c)) âŠ—â‰«
            F.map f â— Î·.naturality g â–· Î¸.app c âŠ—â‰« ğŸ™ _ := by
      rw [Î¸.naturality_comp]
      bicategory
    _ = _ := by
      rw [whisker_exchange]
      bicategory

/-- Vertical composition of lax transformations. -/
def vComp (Î· : LaxTrans F G) (Î¸ : LaxTrans G H) : LaxTrans F H where
  app a := vCompApp Î· Î¸ a
  naturality := vCompNaturality Î· Î¸
  naturality_naturality := vComp_naturality_naturality Î· Î¸
  naturality_id := vComp_naturality_id Î· Î¸
  naturality_comp := vComp_naturality_comp Î· Î¸

/-- `CategoryStruct` on `OplaxFunctor B C` where the (1-)morphisms are given by lax
transformations. -/
@[simps! id_app id_naturality comp_app comp_naturality]
scoped instance : CategoryStruct (OplaxFunctor B C) where
  Hom := LaxTrans
  id := LaxTrans.id
  comp := LaxTrans.vComp

end LaxTrans

/-- If `Î·` is an oplax transformation between `F` and `G`, we have a 1-morphism
`Î·.app a : F.obj a âŸ¶ G.obj a` for each object `a : B`. We also have a 2-morphism
`Î·.naturality f : F.map f â‰« app b âŸ¶ app a â‰« G.map f` for each 1-morphism `f : a âŸ¶ b`.
These 2-morphisms satisfies the naturality condition, and preserve the identities and
the compositions modulo some adjustments of domains and codomains of 2-morphisms.
-/
structure OplaxTrans (F G : B â¥¤áµ’áµ–á´¸ C) where
  /-- The component 1-morphisms of an oplax transformation. -/
  app (a : B) : F.obj a âŸ¶ G.obj a
  /-- The 2-morphisms underlying the oplax naturality constraint. -/
  naturality {a b : B} (f : a âŸ¶ b) : F.map f â‰« app b âŸ¶ app a â‰« G.map f
  naturality_naturality {a b : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g) :
      F.mapâ‚‚ Î· â–· app b â‰« naturality g = naturality f â‰« app a â— G.mapâ‚‚ Î· := by
    cat_disch
  naturality_id (a : B) :
      naturality (ğŸ™ a) â‰« app a â— G.mapId a =
        F.mapId a â–· app a â‰« (Î»_ (app a)).hom â‰« (Ï_ (app a)).inv := by
    cat_disch
  naturality_comp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) :
      naturality (f â‰« g) â‰« app a â— G.mapComp f g =
        F.mapComp f g â–· app c â‰« (Î±_ _ _ _).hom â‰« F.map f â— naturality g â‰«
          (Î±_ _ _ _).inv â‰« naturality f â–· G.map g â‰« (Î±_ _ _ _).hom := by
    cat_disch

attribute [reassoc (attr := simp)] OplaxTrans.naturality_naturality OplaxTrans.naturality_id
  OplaxTrans.naturality_comp

@[deprecated (since := "2025-04-23")] alias _root_.CategoryTheory.OplaxNatTrans := OplaxTrans

namespace OplaxTrans

variable {F : B â¥¤áµ’áµ–á´¸ C} {G H : B â¥¤áµ’áµ–á´¸ C} (Î· : OplaxTrans F G) (Î¸ : OplaxTrans G H)

section

variable {a b c : B} {a' : C}

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality_naturality (f : a' âŸ¶ G.obj a) {g h : a âŸ¶ b} (Î² : g âŸ¶ h) :
    f â— G.mapâ‚‚ Î² â–· Î¸.app b â‰« f â— Î¸.naturality h =
      f â— Î¸.naturality g â‰« f â— Î¸.app a â— H.mapâ‚‚ Î² := by
  simp_rw [â† whiskerLeft_comp, naturality_naturality]

@[reassoc (attr := simp)]
theorem whiskerRight_naturality_naturality {f g : a âŸ¶ b} (Î² : f âŸ¶ g) (h : G.obj b âŸ¶ a') :
    F.mapâ‚‚ Î² â–· Î·.app b â–· h â‰« Î·.naturality g â–· h =
      Î·.naturality f â–· h â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapâ‚‚ Î² â–· h â‰« (Î±_ _ _ _).inv := by
  rw [â† comp_whiskerRight, naturality_naturality, comp_whiskerRight, whisker_assoc]

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality_comp (f : a' âŸ¶ G.obj a) (g : a âŸ¶ b) (h : b âŸ¶ c) :
    f â— Î¸.naturality (g â‰« h) â‰« f â— Î¸.app a â— H.mapComp g h =
      f â— G.mapComp g h â–· Î¸.app c â‰«
        f â— (Î±_ _ _ _).hom â‰«
          f â— G.map g â— Î¸.naturality h â‰«
            f â— (Î±_ _ _ _).inv â‰« f â— Î¸.naturality g â–· H.map h â‰« f â— (Î±_ _ _ _).hom := by
  simp_rw [â† whiskerLeft_comp, naturality_comp]

@[reassoc (attr := simp)]
theorem whiskerRight_naturality_comp (f : a âŸ¶ b) (g : b âŸ¶ c) (h : G.obj c âŸ¶ a') :
    Î·.naturality (f â‰« g) â–· h â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapComp f g â–· h =
      F.mapComp f g â–· Î·.app c â–· h â‰«
        (Î±_ _ _ _).hom â–· h â‰«
          (Î±_ _ _ _).hom â‰«
            F.map f â— Î·.naturality g â–· h â‰«
              (Î±_ _ _ _).inv â‰«
                (Î±_ _ _ _).inv â–· h â‰«
                  Î·.naturality f â–· G.map g â–· h â‰« (Î±_ _ _ _).hom â–· h â‰« (Î±_ _ _ _).hom := by
  rw [â† associator_naturality_middle, â† comp_whiskerRight_assoc, naturality_comp]; simp

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality_id (f : a' âŸ¶ G.obj a) :
    f â— Î¸.naturality (ğŸ™ a) â‰« f â— Î¸.app a â— H.mapId a =
      f â— G.mapId a â–· Î¸.app a â‰« f â— (Î»_ (Î¸.app a)).hom â‰« f â— (Ï_ (Î¸.app a)).inv := by
  simp_rw [â† whiskerLeft_comp, naturality_id]

@[reassoc (attr := simp)]
theorem whiskerRight_naturality_id (f : G.obj a âŸ¶ a') :
    Î·.naturality (ğŸ™ a) â–· f â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapId a â–· f =
    F.mapId a â–· Î·.app a â–· f â‰« (Î»_ (Î·.app a)).hom â–· f â‰« (Ï_ (Î·.app a)).inv â–· f â‰« (Î±_ _ _ _).hom := by
  rw [â† associator_naturality_middle, â† comp_whiskerRight_assoc, naturality_id]; simp

end

variable (F) in
/-- The identity oplax transformation. -/
def id : OplaxTrans F F where
  app a := ğŸ™ (F.obj a)
  naturality {_ _} f := (Ï_ (F.map f)).hom â‰« (Î»_ (F.map f)).inv

instance : Inhabited (OplaxTrans F F) :=
  âŸ¨id FâŸ©

/-- Vertical composition of oplax transformations. -/
def vcomp : OplaxTrans F H where
  app a := Î·.app a â‰« Î¸.app a
  naturality {a b} f :=
    (Î±_ _ _ _).inv â‰«
      Î·.naturality f â–· Î¸.app b â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— Î¸.naturality f â‰« (Î±_ _ _ _).inv
  naturality_comp {a b c} f g :=
    calc
      _ =
        (Î±_ _ _ _).inv â‰«
          F.mapComp f g â–· Î·.app c â–· Î¸.app c â‰«
            (Î±_ _ _ _).hom â–· _ â‰« (Î±_ _ _ _).hom â‰«
              F.map f â— Î·.naturality g â–· Î¸.app c â‰«
                _ â— (Î±_ _ _ _).hom â‰« (Î±_ _ _ _).inv â‰«
                  (F.map f â‰« Î·.app b) â— Î¸.naturality g â‰«
                    Î·.naturality f â–· (Î¸.app b â‰« H.map g) â‰«
                      (Î±_ _ _ _).hom â‰« _ â— (Î±_ _ _ _).inv â‰«
                        Î·.app a â— Î¸.naturality f â–· H.map g â‰«
                          _ â— (Î±_ _ _ _).hom â‰« (Î±_ _ _ _).inv := by
        rw [whisker_exchange_assoc]; simp
      _ = _ := by simp

/-- `CategoryStruct` on `B â¥¤áµ’áµ–á´¸ C` where the (1-)morphisms are given by oplax
transformations. -/
@[simps! id_app id_naturality comp_app comp_naturality]
scoped instance categoryStruct : CategoryStruct (B â¥¤áµ’áµ–á´¸ C) where
  Hom := OplaxTrans
  id := OplaxTrans.id
  comp := OplaxTrans.vcomp

end OplaxTrans

/-- A strong natural transformation between oplax functors `F` and `G` is a natural transformation
that is "natural up to 2-isomorphisms".

More precisely, it consists of the following:
* a 1-morphism `Î·.app a : F.obj a âŸ¶ G.obj a` for each object `a : B`.
* a 2-isomorphism `Î·.naturality f : F.map f â‰« app b âŸ¶ app a â‰« G.map f` for each 1-morphism
  `f : a âŸ¶ b`.
* These 2-isomorphisms satisfy the naturality condition, and preserve the identities and the
  compositions modulo some adjustments of domains and codomains of 2-morphisms.
-/
structure StrongTrans (F G : B â¥¤áµ’áµ–á´¸ C) where
  app (a : B) : F.obj a âŸ¶ G.obj a
  naturality {a b : B} (f : a âŸ¶ b) : F.map f â‰« app b â‰… app a â‰« G.map f
  naturality_naturality {a b : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g) :
      F.mapâ‚‚ Î· â–· app b â‰« (naturality g).hom = (naturality f).hom â‰« app a â— G.mapâ‚‚ Î· := by
    cat_disch
  naturality_id (a : B) :
      (naturality (ğŸ™ a)).hom â‰« app a â— G.mapId a =
        F.mapId a â–· app a â‰« (Î»_ (app a)).hom â‰« (Ï_ (app a)).inv := by
    cat_disch
  naturality_comp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) :
      (naturality (f â‰« g)).hom â‰« app a â— G.mapComp f g =
        F.mapComp f g â–· app c â‰« (Î±_ _ _ _).hom â‰« F.map f â— (naturality g).hom â‰«
        (Î±_ _ _ _).inv â‰« (naturality f).hom â–· G.map g â‰« (Î±_ _ _ _).hom := by
    cat_disch

@[deprecated (since := "2025-04-23")] alias StrongOplaxNatTrans := StrongTrans

attribute [nolint docBlame] CategoryTheory.Oplax.StrongTrans.app
  CategoryTheory.Oplax.StrongTrans.naturality

attribute [reassoc (attr := simp)] StrongTrans.naturality_naturality
  StrongTrans.naturality_id StrongTrans.naturality_comp

/-- A structure on an oplax transformation that promotes it to a strong transformation.

See `Pseudofunctor.StrongTrans.mkOfOplax`. -/
structure OplaxTrans.StrongCore {F G : B â¥¤áµ’áµ–á´¸ C} (Î· : F âŸ¶ G) where
  /-- The underlying 2-isomorphisms of the naturality constraint. -/
  naturality {a b : B} (f : a âŸ¶ b) : F.map f â‰« Î·.app b â‰… Î·.app a â‰« G.map f
  /-- The 2-isomorphisms agree with the underlying 2-morphism of the oplax transformation. -/
  naturality_hom {a b : B} (f : a âŸ¶ b) : (naturality f).hom = Î·.naturality f := by cat_disch

attribute [simp] OplaxTrans.StrongCore.naturality_hom

namespace StrongTrans

/-- The underlying oplax natural transformation of a strong natural transformation. -/
@[simps]
def toOplax {F G : B â¥¤áµ’áµ–á´¸ C} (Î· : StrongTrans F G) : OplaxTrans F G where
  app := Î·.app
  naturality f := (Î·.naturality f).hom

/-- Construct a strong natural transformation from an oplax natural transformation whose
naturality 2-morphism is an isomorphism. -/
def mkOfOplax {F G : B â¥¤áµ’áµ–á´¸ C} (Î· : OplaxTrans F G) (Î·' : OplaxTrans.StrongCore Î·) :
    StrongTrans F G where
  app := Î·.app
  naturality := Î·'.naturality

/-- Construct a strong natural transformation from an oplax natural transformation whose
naturality 2-morphism is an isomorphism. -/
noncomputable def mkOfOplax' {F G : B â¥¤áµ’áµ–á´¸ C} (Î· : OplaxTrans F G)
    [âˆ€ a b (f : a âŸ¶ b), IsIso (Î·.naturality f)] : StrongTrans F G where
  app := Î·.app
  naturality _ := asIso (Î·.naturality _)

variable (F : B â¥¤áµ’áµ–á´¸ C)


/-- The identity strong natural transformation. -/
@[simps!]
def id : StrongTrans F F :=
  mkOfOplax (OplaxTrans.id F) { naturality := fun f â†¦ (Ï_ (F.map f)) â‰ªâ‰« (Î»_ (F.map f)).symm }

@[simp]
lemma id.toOplax : (id F).toOplax = OplaxTrans.id F :=
  rfl

instance : Inhabited (StrongTrans F F) :=
  âŸ¨id FâŸ©


variable {F} {G H : B â¥¤áµ’áµ–á´¸ C} (Î· : StrongTrans F G) (Î¸ : StrongTrans G H)

/-- Vertical composition of strong natural transformations. -/
@[simps!]
def vcomp : StrongTrans F H :=
  mkOfOplax (OplaxTrans.vcomp Î·.toOplax Î¸.toOplax)
    { naturality := fun {a b} f â†¦
        (Î±_ _ _ _).symm â‰ªâ‰« whiskerRightIso (Î·.naturality f) (Î¸.app b) â‰ªâ‰«
        (Î±_ _ _ _) â‰ªâ‰« whiskerLeftIso (Î·.app a) (Î¸.naturality f) â‰ªâ‰« (Î±_ _ _ _).symm }

/-- `CategoryStruct` on `B â¥¤áµ’áµ–á´¸ C` where the (1-)morphisms are given by strong
transformations. -/
@[simps! id_app id_naturality comp_app comp_naturality]
scoped instance categoryStruct : CategoryStruct (B â¥¤áµ’áµ–á´¸ C) where
  Hom := StrongTrans
  id := StrongTrans.id
  comp := StrongTrans.vcomp

section

variable {a b c : B} {a' : C}

@[reassoc (attr := simp), to_app]
theorem whiskerLeft_naturality_naturality (f : a' âŸ¶ G.obj a) {g h : a âŸ¶ b} (Î² : g âŸ¶ h) :
    f â— G.mapâ‚‚ Î² â–· Î¸.app b â‰« f â— (Î¸.naturality h).hom =
      f â— (Î¸.naturality g).hom â‰« f â— Î¸.app a â— H.mapâ‚‚ Î² := by
  apply Î¸.toOplax.whiskerLeft_naturality_naturality

@[reassoc (attr := simp), to_app]
theorem whiskerRight_naturality_naturality {f g : a âŸ¶ b} (Î² : f âŸ¶ g) (h : G.obj b âŸ¶ a') :
    F.mapâ‚‚ Î² â–· Î·.app b â–· h â‰« (Î·.naturality g).hom â–· h =
      (Î·.naturality f).hom â–· h â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapâ‚‚ Î² â–· h â‰« (Î±_ _ _ _).inv :=
  Î·.toOplax.whiskerRight_naturality_naturality _ _

@[reassoc (attr := simp), to_app]
theorem whiskerLeft_naturality_comp (f : a' âŸ¶ G.obj a) (g : a âŸ¶ b) (h : b âŸ¶ c) :
    f â— (Î¸.naturality (g â‰« h)).hom â‰« f â— Î¸.app a â— H.mapComp g h =
      f â— G.mapComp g h â–· Î¸.app c â‰«
        f â— (Î±_ _ _ _).hom â‰«
          f â— G.map g â— (Î¸.naturality h).hom â‰«
            f â— (Î±_ _ _ _).inv â‰« f â— (Î¸.naturality g).hom â–· H.map h â‰« f â— (Î±_ _ _ _).hom :=
  Î¸.toOplax.whiskerLeft_naturality_comp _ _ _

@[reassoc (attr := simp), to_app]
theorem whiskerRight_naturality_comp (f : a âŸ¶ b) (g : b âŸ¶ c) (h : G.obj c âŸ¶ a') :
    (Î·.naturality (f â‰« g)).hom â–· h â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapComp f g â–· h =
      F.mapComp f g â–· Î·.app c â–· h â‰«
        (Î±_ _ _ _).hom â–· h â‰«
          (Î±_ _ _ _).hom â‰«
            F.map f â— (Î·.naturality g).hom â–· h â‰«
              (Î±_ _ _ _).inv â‰«
                (Î±_ _ _ _).inv â–· h â‰«
                 (Î·.naturality f).hom â–· G.map g â–· h â‰« (Î±_ _ _ _).hom â–· h â‰« (Î±_ _ _ _).hom :=
  Î·.toOplax.whiskerRight_naturality_comp _ _ _

@[reassoc (attr := simp), to_app]
theorem whiskerLeft_naturality_id (f : a' âŸ¶ G.obj a) :
    f â— (Î¸.naturality (ğŸ™ a)).hom â‰« f â— Î¸.app a â— H.mapId a =
      f â— G.mapId a â–· Î¸.app a â‰« f â— (Î»_ (Î¸.app a)).hom â‰« f â— (Ï_ (Î¸.app a)).inv :=
  Î¸.toOplax.whiskerLeft_naturality_id _

@[reassoc (attr := simp), to_app]
theorem whiskerRight_naturality_id (f : G.obj a âŸ¶ a') :
    (Î·.naturality (ğŸ™ a)).hom â–· f â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapId a â–· f =
    F.mapId a â–· Î·.app a â–· f â‰« (Î»_ (Î·.app a)).hom â–· f â‰« (Ï_ (Î·.app a)).inv â–· f â‰«
    (Î±_ _ _ _).hom :=
  Î·.toOplax.whiskerRight_naturality_id _

end

end StrongTrans

end CategoryTheory.Oplax
