/-
Copyright (c) 2024 Calle S√∂nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle S√∂nne
-/

import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor
import Mathlib.CategoryTheory.Bicategory.NaturalTransformation.Oplax

/-!
# Strong natural transformations

A strong natural transformation is an oplax natural transformation such that each component 2-cell
is an isomorphism.

## Main definitions

* `StrongNatTrans F G` : strong natural transformations between oplax functors `F` and `G`.
* `mkOfOplax Œ∑ Œ∑'` : given an oplax natural transformation `Œ∑` such that each component 2-cell
  is an isomorphism, `mkOfOplax` gives the corresponding strong natural transformation.
* `StrongNatTrans.vcomp Œ∑ Œ∏` : the vertical composition of strong natural transformations `Œ∑`
  and `Œ∏`.
* `StrongNatTrans.category F G` : a category structure on pseudofunctors between `F` and `G`,
  where the morphisms are strong natural transformations.

## TODO

After having defined lax functors, we should define 3 different types of strong natural
transformations:
* strong natural transformations between oplax functors (as defined here).
* strong natural transformations between lax functors.
* strong natural transformations between pseudofunctors. From these types of strong natural
  transformations, we can define the underlying natural transformations between the underlying
  oplax resp. lax functors. Many properties can then be inferred from these.

## References
* [Niles Johnson, Donald Yau, *2-Dimensional Categories*](https://arxiv.org/abs/2002.06055)

-/

namespace CategoryTheory

open Category Bicategory

open scoped Bicategory

universe w‚ÇÅ w‚ÇÇ v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

variable {B : Type u‚ÇÅ} [Bicategory.{w‚ÇÅ, v‚ÇÅ} B] {C : Type u‚ÇÇ} [Bicategory.{w‚ÇÇ, v‚ÇÇ} C]

/-- A strong natural transformation between oplax functors `F` and `G` is a natural transformation
that is "natural up to 2-isomorphisms".

More precisely, it consists of the following:
* a 1-morphism `Œ∑.app a : F.obj a ‚ü∂ G.obj a` for each object `a : B`.
* a 2-isomorphism `Œ∑.naturality f : F.map f ‚â´ app b ‚ü∂ app a ‚â´ G.map f` for each 1-morphism
`f : a ‚ü∂ b`.
* These 2-isomorphisms satisfy the naturality condition, and preserve the identities and the
compositions modulo some adjustments of domains and codomains of 2-morphisms.
-/
structure StrongNatTrans (F G : OplaxFunctor B C) where
  app (a : B) : F.obj a ‚ü∂ G.obj a
  naturality {a b : B} (f : a ‚ü∂ b) : F.map f ‚â´ app b ‚âÖ app a ‚â´ G.map f
  naturality_naturality :
    ‚àÄ {a b : B} {f g : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g),
      F.map‚ÇÇ Œ∑ ‚ñ∑ app b ‚â´ (naturality g).hom = (naturality f).hom ‚â´ app a ‚óÅ G.map‚ÇÇ Œ∑ := by
    aesop_cat
  naturality_id :
    ‚àÄ a : B,
      (naturality (ùüô a)).hom ‚â´ app a ‚óÅ G.mapId a =
        F.mapId a ‚ñ∑ app a ‚â´ (Œª_ (app a)).hom ‚â´ (œÅ_ (app a)).inv := by
    aesop_cat
  naturality_comp :
    ‚àÄ {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c),
      (naturality (f ‚â´ g)).hom ‚â´ app a ‚óÅ G.mapComp f g =
        F.mapComp f g ‚ñ∑ app c ‚â´ (Œ±_ _ _ _).hom ‚â´ F.map f ‚óÅ (naturality g).hom ‚â´
        (Œ±_ _ _ _).inv ‚â´ (naturality f).hom ‚ñ∑ G.map g ‚â´ (Œ±_ _ _ _).hom := by
    aesop_cat

attribute [nolint docBlame] CategoryTheory.StrongNatTrans.app
  CategoryTheory.StrongNatTrans.naturality
  CategoryTheory.StrongNatTrans.naturality_naturality
  CategoryTheory.StrongNatTrans.naturality_id
  CategoryTheory.StrongNatTrans.naturality_comp

attribute [reassoc (attr := simp)] StrongNatTrans.naturality_naturality StrongNatTrans.naturality_id
  StrongNatTrans.naturality_comp

namespace StrongNatTrans

section

/-- The underlying oplax natural transformation of a strong natural transformation. -/
@[simps]
def toOplax {F G : OplaxFunctor B C} (Œ∑ : StrongNatTrans F G) : OplaxNatTrans F G where
  app := Œ∑.app
  naturality f := (Œ∑.naturality f).hom

/-- Construct a strong natural transformation from an oplax natural transformation whose
naturality 2-cell is an isomorphism. -/
def mkOfOplax {F G : OplaxFunctor B C} (Œ∑ : OplaxNatTrans F G) (Œ∑' : OplaxNatTrans.StrongCore Œ∑) :
    StrongNatTrans F G where
  app := Œ∑.app
  naturality := Œ∑'.naturality

/-- Construct a strong natural transformation from an oplax natural transformation whose
naturality 2-cell is an isomorphism. -/
noncomputable def mkOfOplax' {F G : OplaxFunctor B C} (Œ∑ : OplaxNatTrans F G)
    [‚àÄ a b (f : a ‚ü∂ b), IsIso (Œ∑.naturality f)] : StrongNatTrans F G where
  app := Œ∑.app
  naturality := fun f => asIso (Œ∑.naturality _)

variable (F : OplaxFunctor B C)


/-- The identity oplax natural transformation. -/
@[simps!]
def id : StrongNatTrans F F :=
  mkOfOplax (OplaxNatTrans.id F) { naturality := Œª f ‚Ü¶ (œÅ_ (F.map f)) ‚â™‚â´ (Œª_ (F.map f)).symm }

@[simp]
lemma id.toOplax : (id F).toOplax = OplaxNatTrans.id F :=
  rfl

instance : Inhabited (StrongNatTrans F F) :=
  ‚ü®id F‚ü©

variable {F} {G H : OplaxFunctor B C} (Œ∑ : StrongNatTrans F G) (Œ∏ : StrongNatTrans G H)

section

variable {a b c : B} {a' : C}

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality_naturality (f : a' ‚ü∂ G.obj a) {g h : a ‚ü∂ b} (Œ≤ : g ‚ü∂ h) :
    f ‚óÅ G.map‚ÇÇ Œ≤ ‚ñ∑ Œ∏.app b ‚â´ f ‚óÅ (Œ∏.naturality h).hom =
      f ‚óÅ (Œ∏.naturality g).hom ‚â´ f ‚óÅ Œ∏.app a ‚óÅ H.map‚ÇÇ Œ≤ := by
  apply Œ∏.toOplax.whiskerLeft_naturality_naturality

@[reassoc (attr := simp)]
theorem whiskerRight_naturality_naturality {f g : a ‚ü∂ b} (Œ≤ : f ‚ü∂ g) (h : G.obj b ‚ü∂ a') :
    F.map‚ÇÇ Œ≤ ‚ñ∑ Œ∑.app b ‚ñ∑ h ‚â´ (Œ∑.naturality g).hom ‚ñ∑ h =
      (Œ∑.naturality f).hom ‚ñ∑ h ‚â´ (Œ±_ _ _ _).hom ‚â´ Œ∑.app a ‚óÅ G.map‚ÇÇ Œ≤ ‚ñ∑ h ‚â´ (Œ±_ _ _ _).inv := by
  apply Œ∑.toOplax.whiskerRight_naturality_naturality

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality_comp (f : a' ‚ü∂ G.obj a) (g : a ‚ü∂ b) (h : b ‚ü∂ c) :
    f ‚óÅ (Œ∏.naturality (g ‚â´ h)).hom ‚â´ f ‚óÅ Œ∏.app a ‚óÅ H.mapComp g h =
      f ‚óÅ G.mapComp g h ‚ñ∑ Œ∏.app c ‚â´
        f ‚óÅ (Œ±_ _ _ _).hom ‚â´
          f ‚óÅ G.map g ‚óÅ (Œ∏.naturality h).hom ‚â´
            f ‚óÅ (Œ±_ _ _ _).inv ‚â´ f ‚óÅ (Œ∏.naturality g).hom ‚ñ∑ H.map h ‚â´ f ‚óÅ (Œ±_ _ _ _).hom := by
  apply Œ∏.toOplax.whiskerLeft_naturality_comp

@[reassoc (attr := simp)]
theorem whiskerRight_naturality_comp (f : a ‚ü∂ b) (g : b ‚ü∂ c) (h : G.obj c ‚ü∂ a') :
    (Œ∑.naturality (f ‚â´ g)).hom ‚ñ∑ h ‚â´ (Œ±_ _ _ _).hom ‚â´ Œ∑.app a ‚óÅ G.mapComp f g ‚ñ∑ h =
      F.mapComp f g ‚ñ∑ Œ∑.app c ‚ñ∑ h ‚â´
        (Œ±_ _ _ _).hom ‚ñ∑ h ‚â´
          (Œ±_ _ _ _).hom ‚â´
            F.map f ‚óÅ (Œ∑.naturality g).hom ‚ñ∑ h ‚â´
              (Œ±_ _ _ _).inv ‚â´
                (Œ±_ _ _ _).inv ‚ñ∑ h ‚â´
                 (Œ∑.naturality f).hom ‚ñ∑ G.map g ‚ñ∑ h ‚â´ (Œ±_ _ _ _).hom ‚ñ∑ h ‚â´ (Œ±_ _ _ _).hom := by
  apply Œ∑.toOplax.whiskerRight_naturality_comp

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality_id (f : a' ‚ü∂ G.obj a) :
    f ‚óÅ (Œ∏.naturality (ùüô a)).hom ‚â´ f ‚óÅ Œ∏.app a ‚óÅ H.mapId a =
      f ‚óÅ G.mapId a ‚ñ∑ Œ∏.app a ‚â´ f ‚óÅ (Œª_ (Œ∏.app a)).hom ‚â´ f ‚óÅ (œÅ_ (Œ∏.app a)).inv := by
  apply Œ∏.toOplax.whiskerLeft_naturality_id

@[reassoc (attr := simp)]
theorem whiskerRight_naturality_id (f : G.obj a ‚ü∂ a') :
    (Œ∑.naturality (ùüô a)).hom ‚ñ∑ f ‚â´ (Œ±_ _ _ _).hom ‚â´ Œ∑.app a ‚óÅ G.mapId a ‚ñ∑ f =
    F.mapId a ‚ñ∑ Œ∑.app a ‚ñ∑ f ‚â´ (Œª_ (Œ∑.app a)).hom ‚ñ∑ f ‚â´ (œÅ_ (Œ∑.app a)).inv ‚ñ∑ f ‚â´
    (Œ±_ _ _ _).hom := by
  apply Œ∑.toOplax.whiskerRight_naturality_id

end

/-- Vertical composition of oplax natural transformations. -/
@[simps!]
def vcomp (Œ∑ : StrongNatTrans F G) (Œ∏ : StrongNatTrans G H) : StrongNatTrans F H :=
  mkOfOplax (OplaxNatTrans.vcomp Œ∑.toOplax Œ∏.toOplax)
    { naturality := Œª {a b} f ‚Ü¶
        (Œ±_ _ _ _).symm ‚â™‚â´ whiskerRightIso (Œ∑.naturality f) (Œ∏.app b) ‚â™‚â´
        (Œ±_ _ _ _) ‚â™‚â´ whiskerLeftIso (Œ∑.app a) (Œ∏.naturality f) ‚â™‚â´ (Œ±_ _ _ _).symm }

variable (B C)

@[simps id comp]
instance : CategoryStruct (Pseudofunctor B C) where
  Hom F G := StrongNatTrans F.toOplax G.toOplax
  id F := StrongNatTrans.id F.toOplax
  comp := StrongNatTrans.vcomp

end

section

-- TODO: move up?
open OplaxNatTrans

/-- Category structure on the strong natural transformations between pseudofunctors. -/
@[simps]
instance homcategory (F G : Pseudofunctor B C) : Category (F ‚ü∂ G) where
  Hom Œ∑ Œ∏ := Modification Œ∑.toOplax Œ∏.toOplax
  id Œ∑ := Modification.id Œ∑.toOplax
  comp := Modification.vcomp

-- Porting note: duplicating the `ext` lemma.
-- TODO: needed?
@[ext]
lemma ext {F G : Pseudofunctor B C} {Œ± Œ≤ : F ‚ü∂ G} {m n : Œ± ‚ü∂ Œ≤} (w : ‚àÄ b, m.app b = n.app b) :
    m = n :=
  OplaxNatTrans.ext w

-- -- TODO: ARE THESE NEEDED?
-- @[simp]
-- lemma Modification.id_app' {X : B} {F G : Pseudofunctor B C} (Œ± : F ‚ü∂ G) :
--     Modification.app (ùüô Œ±) X = ùüô (Œ±.app X) := rfl

-- @[simp]
-- lemma Modification.comp_app' {X : B} {F G : Pseudofunctor B C} {Œ± Œ≤ Œ≥ : F ‚ü∂ G}
--     (m : Œ± ‚ü∂ Œ≤) (n : Œ≤ ‚ü∂ Œ≥) : (m ‚â´ n).app X = m.app X ‚â´ n.app X :=
--   rfl

-- -- TODO: I might need this one!
-- -- /-- Construct a modification isomorphism between oplax natural transformations
-- -- by giving object level isomorphisms, and checking naturality only in the forward direction.
-- -- -/
-- @[simps]
-- def ModificationIso.ofComponents {F G : Pseudofunctor B C} {Œ∑ Œ∏ : F ‚ü∂ G}
--     (app : ‚àÄ a, Œ∑.app a ‚âÖ Œ∏.app a)
--     (naturality : ‚àÄ {a b} (f : a ‚ü∂ b),
--       F.map f ‚óÅ (app b).hom ‚â´ (Œ∏.naturality f).hom = (Œ∑.naturality f).hom ‚â´ (app a).hom ‚ñ∑ G.map f) :
--     Œ∑ ‚âÖ Œ∏ where
--   hom := { app := fun a => (app a).hom }
--   inv :=
--     { app := fun a => (app a).inv
--       naturality := fun {a b} f => by
--         rw [‚ÜêwhiskerRightIso_inv, Iso.eq_comp_inv, assoc]
--         rw [‚ÜêwhiskerLeftIso_inv, Iso.inv_comp_eq]
--         apply (naturality f).symm }

end

end StrongNatTrans

end CategoryTheory
