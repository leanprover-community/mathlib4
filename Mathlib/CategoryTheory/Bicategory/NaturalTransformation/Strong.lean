/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor
import Mathlib.CategoryTheory.Bicategory.NaturalTransformation.Oplax

/-!
# Strong natural transformations

A strong natural transformation is an oplax natural transformation such that each component 2-cell
is an isomorphism.

## Main definitions

* `StrongPseudoNatTrans F G` : strong natural transformations between oplax functors `F` and `G`.
* `mkOfOplax Î· Î·'` : given an oplax natural transformation `Î·` such that each component 2-cell
  is an isomorphism, `mkOfOplax` gives the corresponding strong natural transformation.
* `StrongPseudoNatTrans.vcomp Î· Î¸` : the vertical composition of strong natural transformations `Î·`
  and `Î¸`.
* `StrongPseudoNatTrans.category F G` : a category structure on Pseudofunctors between `F` and `G`,
  where the morphisms are strong natural transformations.

## TODO

After having defined lax functors, we should define 3 different types of strong natural
transformations:
* strong natural transformations between oplax functors (as defined here).
* strong natural transformations between lax functors.
* strong natural transformations between Pseudofunctors. From these types of strong natural
  transformations, we can define the underlying natural transformations between the underlying
  oplax resp. lax functors. Many properties can then be inferred from these.

## References
* [Niles Johnson, Donald Yau, *2-Dimensional Categories*](https://arxiv.org/abs/2002.06055)

-/

namespace CategoryTheory

open Category Bicategory

open scoped Bicategory

universe wâ‚ wâ‚‚ vâ‚ vâ‚‚ uâ‚ uâ‚‚

variable {B : Type uâ‚} [Bicategory.{wâ‚, vâ‚} B] {C : Type uâ‚‚} [Bicategory.{wâ‚‚, vâ‚‚} C]

/-- A strong natural transformation between oplax functors `F` and `G` is a natural transformation
that is "natural up to 2-isomorphisms".

More precisely, it consists of the following:
* a 1-morphism `Î·.app a : F.obj a âŸ¶ G.obj a` for each object `a : B`.
* a 2-isomorphism `Î·.naturality f : F.map f â‰« app b âŸ¶ app a â‰« G.map f` for each 1-morphism
`f : a âŸ¶ b`.
* These 2-isomorphisms satisfy the naturality condition, and preserve the identities and the
compositions modulo some adjustments of domains and codomains of 2-morphisms.
-/
structure StrongPseudoNatTrans (F G : Pseudofunctor B C) where
  app (a : B) : F.obj a âŸ¶ G.obj a
  naturality {a b : B} (f : a âŸ¶ b) : F.map f â‰« app b â‰… app a â‰« G.map f
  naturality_naturality :
    âˆ€ {a b : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g),
      F.mapâ‚‚ Î· â–· app b â‰« (naturality g).hom = (naturality f).hom â‰« app a â— G.mapâ‚‚ Î· := by
    aesop_cat
  naturality_id :
    âˆ€ a : B,
      (naturality (ğŸ™ a)).hom â‰« app a â— (G.mapId a).hom =
        (F.mapId a).hom â–· app a â‰« (Î»_ (app a)).hom â‰« (Ï_ (app a)).inv := by
    aesop_cat
  naturality_comp :
    âˆ€ {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c),
      (naturality (f â‰« g)).hom â‰« app a â— (G.mapComp f g).hom =
        (F.mapComp f g).hom â–· app c â‰« (Î±_ _ _ _).hom â‰« F.map f â— (naturality g).hom â‰«
        (Î±_ _ _ _).inv â‰« (naturality f).hom â–· G.map g â‰« (Î±_ _ _ _).hom := by
    aesop_cat

attribute [nolint docBlame] CategoryTheory.StrongPseudoNatTrans.app
  CategoryTheory.StrongPseudoNatTrans.naturality
  CategoryTheory.StrongPseudoNatTrans.naturality_naturality
  CategoryTheory.StrongPseudoNatTrans.naturality_id
  CategoryTheory.StrongPseudoNatTrans.naturality_comp

attribute [reassoc (attr := simp)] StrongPseudoNatTrans.naturality_naturality
  StrongPseudoNatTrans.naturality_id StrongPseudoNatTrans.naturality_comp

namespace StrongPseudoNatTrans

section

variable {F G : Pseudofunctor B C}

/-- The underlying oplax natural transformation of a strong natural transformation. -/
@[simps]
def toStrongOplax (Î· : StrongPseudoNatTrans F G) :
    StrongOplaxNatTrans F.toOplax G.toOplax where
  app := Î·.app
  naturality f := Î·.naturality f

instance hasCoeToStrongOplax : Coe (StrongPseudoNatTrans F G)
    (StrongOplaxNatTrans F.toOplax G) :=
  âŸ¨toStrongOplaxâŸ©

/-- Construct a strong natural transformation between pseudofunctors from a strong natural
transformation between the underlying oplax functors. -/
def mkOfStrongOplax (Î· : StrongOplaxNatTrans F.toOplax G) : StrongPseudoNatTrans F G where
  app := Î·.app
  naturality := Î·.naturality
  naturality_naturality := Î·.naturality_naturality
  naturality_id := Î·.naturality_id
  naturality_comp := Î·.naturality_comp

section

variable (F)


/-- The identity strong natural transformation. -/
@[simps!]
def id : StrongPseudoNatTrans F F :=
  mkOfStrongOplax (StrongOplaxNatTrans.id F.toOplax)

@[simp]
lemma id.toOplax : (id F).toStrongOplax = StrongOplaxNatTrans.id F.toOplax :=
  rfl

instance : Inhabited (StrongPseudoNatTrans F F) :=
  âŸ¨id FâŸ©

end

variable {H : Pseudofunctor B C} (Î· : StrongPseudoNatTrans F G) (Î¸ : StrongPseudoNatTrans G H)

section

variable {a b c : B} {a' : C}

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality_naturality (f : a' âŸ¶ G.obj a) {g h : a âŸ¶ b} (Î² : g âŸ¶ h) :
    f â— G.mapâ‚‚ Î² â–· Î¸.app b â‰« f â— (Î¸.naturality h).hom =
      f â— (Î¸.naturality g).hom â‰« f â— Î¸.app a â— H.mapâ‚‚ Î² := by
  apply Î¸.toStrongOplax.whiskerLeft_naturality_naturality

@[reassoc (attr := simp)]
theorem whiskerRight_naturality_naturality {f g : a âŸ¶ b} (Î² : f âŸ¶ g) (h : G.obj b âŸ¶ a') :
    F.mapâ‚‚ Î² â–· Î·.app b â–· h â‰« (Î·.naturality g).hom â–· h =
      (Î·.naturality f).hom â–· h â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapâ‚‚ Î² â–· h â‰« (Î±_ _ _ _).inv := by
  apply Î·.toStrongOplax.whiskerRight_naturality_naturality

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality_comp (f : a' âŸ¶ G.obj a) (g : a âŸ¶ b) (h : b âŸ¶ c) :
    f â— (Î¸.naturality (g â‰« h)).hom â‰« f â— Î¸.app a â— (H.mapComp g h).hom =
      f â— (G.mapComp g h).hom â–· Î¸.app c â‰«
        f â— (Î±_ _ _ _).hom â‰«
          f â— G.map g â— (Î¸.naturality h).hom â‰«
            f â— (Î±_ _ _ _).inv â‰« f â— (Î¸.naturality g).hom â–· H.map h â‰« f â— (Î±_ _ _ _).hom := by
  apply Î¸.toStrongOplax.whiskerLeft_naturality_comp

@[reassoc (attr := simp)]
theorem whiskerRight_naturality_comp (f : a âŸ¶ b) (g : b âŸ¶ c) (h : G.obj c âŸ¶ a') :
    (Î·.naturality (f â‰« g)).hom â–· h â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— (G.mapComp f g).hom â–· h =
      (F.mapComp f g).hom â–· Î·.app c â–· h â‰«
        (Î±_ _ _ _).hom â–· h â‰«
          (Î±_ _ _ _).hom â‰«
            F.map f â— (Î·.naturality g).hom â–· h â‰«
              (Î±_ _ _ _).inv â‰«
                (Î±_ _ _ _).inv â–· h â‰«
                 (Î·.naturality f).hom â–· G.map g â–· h â‰« (Î±_ _ _ _).hom â–· h â‰« (Î±_ _ _ _).hom := by
  apply Î·.toStrongOplax.whiskerRight_naturality_comp

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality_id (f : a' âŸ¶ G.obj a) :
    f â— (Î¸.naturality (ğŸ™ a)).hom â‰« f â— Î¸.app a â— (H.mapId a).hom =
      f â— (G.mapId a).hom â–· Î¸.app a â‰« f â— (Î»_ (Î¸.app a)).hom â‰« f â— (Ï_ (Î¸.app a)).inv := by
  apply Î¸.toStrongOplax.whiskerLeft_naturality_id

@[reassoc (attr := simp)]
theorem whiskerRight_naturality_id (f : G.obj a âŸ¶ a') :
    (Î·.naturality (ğŸ™ a)).hom â–· f â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— (G.mapId a).hom â–· f =
    (F.mapId a).hom â–· Î·.app a â–· f â‰« (Î»_ (Î·.app a)).hom â–· f â‰« (Ï_ (Î·.app a)).inv â–· f â‰«
    (Î±_ _ _ _).hom := by
  apply Î·.toStrongOplax.whiskerRight_naturality_id

end

/-- Vertical composition of strong natural transformations. -/
@[simps!]
def vcomp (Î· : StrongPseudoNatTrans F G) (Î¸ : StrongPseudoNatTrans G H) :
    StrongPseudoNatTrans F H :=
  mkOfStrongOplax (StrongOplaxNatTrans.vcomp Î·.toStrongOplax Î¸.toStrongOplax)

end

end StrongPseudoNatTrans

variable (B C)

@[simps id comp]
instance Pseudofunctor.categoryStruct : CategoryStruct (Pseudofunctor B C) where
  Hom F G := StrongPseudoNatTrans F G
  id F := StrongPseudoNatTrans.id F
  comp := StrongPseudoNatTrans.vcomp


end CategoryTheory
