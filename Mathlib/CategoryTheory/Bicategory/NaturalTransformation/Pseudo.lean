/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor
import Mathlib.CategoryTheory.Bicategory.NaturalTransformation.Oplax

/-!

# Strong transformations of pseudofunctors

A strong transformation is an oplax transformation such that each component 2-cell
is an isomorphism.

## Main definitions

* `StrongTrans F G` : strong transformations between oplax functors `F` and `G`.
* `mkOfOplax Î· Î·'` : given an oplax transformation `Î·` such that each component 2-cell
  is an isomorphism, `mkOfOplax` gives the corresponding strong transformation.
* `StrongTrans.vcomp Î· Î¸` : the vertical composition of strong transformations `Î·`
  and `Î¸`.
* `StrongTrans.category F G` : a category structure on Pseudofunctors between `F` and `G`,
  where the morphisms are strong transformations.

## TODO

After having defined lax functors, we should define 3 different types of strong natural
transformations:
* strong transformations between oplax functors (as defined here).
* strong transformations between lax functors.
* strong transformations between Pseudofunctors. From these types of strong natural
  transformations, we can define the underlying natural transformations between the underlying
  oplax resp. lax functors. Many properties can then be inferred from these.

## References
* [Niles Johnson, Donald Yau, *2-Dimensional Categories*](https://arxiv.org/abs/2002.06055)

-/

namespace CategoryTheory.Pseudofunctor

open Category Bicategory Oplax

open scoped Bicategory

universe wâ‚ wâ‚‚ vâ‚ vâ‚‚ uâ‚ uâ‚‚

variable {B : Type uâ‚} [Bicategory.{wâ‚, vâ‚} B] {C : Type uâ‚‚} [Bicategory.{wâ‚‚, vâ‚‚} C]

/-- A strong transformation between pseudofunctors `F` and `G` is a natural transformation
that is "natural up to 2-isomorphisms".
More precisely, it consists of the following:
* a 1-morphism `Î·.app a : F.obj a âŸ¶ G.obj a` for each object `a : B`.
* a 2-isomorphism `Î·.naturality f : F.map f â‰« app b â‰… app a â‰« G.map f` for each 1-morphism
`f : a âŸ¶ b`.
* These 2-isomorphisms satisfy the naturality condition, and preserve the identities and the
compositions modulo some adjustments of domains and codomains of 2-morphisms.
-/
structure StrongTrans (F G : Pseudofunctor B C) where
  /-- The component 1-morphisms of a strong transformation. -/
  app (a : B) : F.obj a âŸ¶ G.obj a
  /-- The 2-isomorphisms underlying the strong naturality constraint. -/
  naturality {a b : B} (f : a âŸ¶ b) : F.map f â‰« app b â‰… app a â‰« G.map f
  /-- Naturality of the strong naturality constraint. -/
  naturality_naturality :
    âˆ€ {a b : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g),
      F.mapâ‚‚ Î· â–· app b â‰« (naturality g).hom = (naturality f).hom â‰« app a â— G.mapâ‚‚ Î· := by
    aesop_cat
  /-- Oplax unity. -/
  naturality_id :
    âˆ€ a : B,
      (naturality (ğŸ™ a)).hom â‰« app a â— (G.mapId a).hom =
        (F.mapId a).hom â–· app a â‰« (Î»_ (app a)).hom â‰« (Ï_ (app a)).inv := by
    aesop_cat
  /-- Oplax functoriality. -/
  naturality_comp :
    âˆ€ {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c),
      (naturality (f â‰« g)).hom â‰« app a â— (G.mapComp f g).hom =
        (F.mapComp f g).hom â–· app c â‰« (Î±_ _ _ _).hom â‰« F.map f â— (naturality g).hom â‰«
        (Î±_ _ _ _).inv â‰« (naturality f).hom â–· G.map g â‰« (Î±_ _ _ _).hom := by
    aesop_cat

attribute [reassoc (attr := simp)] StrongTrans.naturality_naturality
  StrongTrans.naturality_id StrongTrans.naturality_comp

/-- A structure on an oplax transformation that promotes it to a strong transformation.
See `Pseudofunctor.StrongTrans.mkOfOplax`. -/
structure StrongCore {F G : Pseudofunctor B C} (Î· : OplaxTrans F.toOplax G) where
  /-- The underlying 2-isomorphisms of the naturality constraint. -/
  naturality {a b : B} (f : a âŸ¶ b) : F.map f â‰« Î·.app b â‰… Î·.app a â‰« G.map f
  /-- The 2-isomorphisms agree with the underlying 2-morphism of the oplax transformation. -/
  naturality_hom {a b : B} (f : a âŸ¶ b) : (naturality f).hom = Î·.naturality f := by aesop_cat

attribute [simp] StrongCore.naturality_hom

namespace StrongTrans

section

variable {F G : Pseudofunctor B C}

/-- The underlying oplax transformation of a strong transformation. -/
@[simps]
def toOplax (Î· : StrongTrans F G) : F.toOplax âŸ¶ G.toOplax where
  app := Î·.app
  naturality f := (Î·.naturality f).hom

instance hasCoeToOplax : Coe (StrongTrans F G) (F.toOplax âŸ¶ G) :=
  âŸ¨toOplaxâŸ©

/-- Construct a strong transformation from an oplax transformation whose
naturality 2-morphism is an isomorphism. -/
@[simps]
def mkOfOplax {F G : Pseudofunctor B C} (Î· : F.toOplax âŸ¶ G) (Î·' : StrongCore Î·) :
    StrongTrans F G where
  app := Î·.app
  naturality := Î·'.naturality
  -- Not automatic as simp must convert F.toOplax.mapâ‚‚ to F.mapâ‚‚ in Î·.naturality_naturality etc
  naturality_naturality Î¸ := by simpa using Î·.naturality_naturality Î¸
  naturality_id a := by simpa using Î·.naturality_id a
  naturality_comp f g := by simpa using Î·.naturality_comp f g

/-- Construct a strong transformation from an oplax transformation whose
naturality 2-morphism is an isomorphism. -/
@[simps]
noncomputable def mkOfOplax' {F G : Pseudofunctor B C} (Î· : F.toOplax âŸ¶ G)
    [âˆ€ a b (f : a âŸ¶ b), IsIso (Î·.naturality f)] : StrongTrans F G where
  app := Î·.app
  naturality := fun f => asIso (Î·.naturality _)
  naturality_naturality Î¸ := by simpa using Î·.naturality_naturality Î¸
  naturality_id a := by simpa using Î·.naturality_id a
  naturality_comp f g := by simpa using Î·.naturality_comp f g


variable {H : Pseudofunctor B C} (Î· : StrongTrans F G) (Î¸ : StrongTrans G H)

section

variable {a b c : B} {a' : C}

@[reassoc (attr := simp), to_app]
theorem whiskerLeft_naturality_naturality (f : a' âŸ¶ G.obj a) {g h : a âŸ¶ b} (Î² : g âŸ¶ h) :
    f â— G.mapâ‚‚ Î² â–· Î¸.app b â‰« f â— (Î¸.naturality h).hom =
      f â— (Î¸.naturality g).hom â‰« f â— Î¸.app a â— H.mapâ‚‚ Î² :=
  Î¸.toOplax.whiskerLeft_naturality_naturality _ _

@[reassoc (attr := simp), to_app]
theorem whiskerRight_naturality_naturality {f g : a âŸ¶ b} (Î² : f âŸ¶ g) (h : G.obj b âŸ¶ a') :
    F.mapâ‚‚ Î² â–· Î·.app b â–· h â‰« (Î·.naturality g).hom â–· h =
      (Î·.naturality f).hom â–· h â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapâ‚‚ Î² â–· h â‰« (Î±_ _ _ _).inv :=
  Î·.toOplax.whiskerRight_naturality_naturality _ _

@[reassoc (attr := simp), to_app]
theorem whiskerLeft_naturality_comp (f : a' âŸ¶ G.obj a) (g : a âŸ¶ b) (h : b âŸ¶ c) :
    f â— (Î¸.naturality (g â‰« h)).hom â‰« f â— Î¸.app a â— (H.mapComp g h).hom =
      f â— (G.mapComp g h).hom â–· Î¸.app c â‰«
        f â— (Î±_ _ _ _).hom â‰«
          f â— G.map g â— (Î¸.naturality h).hom â‰«
            f â— (Î±_ _ _ _).inv â‰« f â— (Î¸.naturality g).hom â–· H.map h â‰« f â— (Î±_ _ _ _).hom :=
  Î¸.toOplax.whiskerLeft_naturality_comp _ _ _

@[reassoc (attr := simp), to_app]
theorem whiskerRight_naturality_comp (f : a âŸ¶ b) (g : b âŸ¶ c) (h : G.obj c âŸ¶ a') :
    (Î·.naturality (f â‰« g)).hom â–· h â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— (G.mapComp f g).hom â–· h =
      (F.mapComp f g).hom â–· Î·.app c â–· h â‰«
        (Î±_ _ _ _).hom â–· h â‰«
          (Î±_ _ _ _).hom â‰«
            F.map f â— (Î·.naturality g).hom â–· h â‰«
              (Î±_ _ _ _).inv â‰«
                (Î±_ _ _ _).inv â–· h â‰«
                 (Î·.naturality f).hom â–· G.map g â–· h â‰« (Î±_ _ _ _).hom â–· h â‰« (Î±_ _ _ _).hom :=
  Î·.toOplax.whiskerRight_naturality_comp _ _ _

@[reassoc (attr := simp), to_app]
theorem whiskerLeft_naturality_id (f : a' âŸ¶ G.obj a) :
    f â— (Î¸.naturality (ğŸ™ a)).hom â‰« f â— Î¸.app a â— (H.mapId a).hom =
      f â— (G.mapId a).hom â–· Î¸.app a â‰« f â— (Î»_ (Î¸.app a)).hom â‰« f â— (Ï_ (Î¸.app a)).inv :=
  Î¸.toOplax.whiskerLeft_naturality_id _

@[reassoc (attr := simp), to_app]
theorem whiskerRight_naturality_id (f : G.obj a âŸ¶ a') :
    (Î·.naturality (ğŸ™ a)).hom â–· f â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— (G.mapId a).hom â–· f =
    (F.mapId a).hom â–· Î·.app a â–· f â‰« (Î»_ (Î·.app a)).hom â–· f â‰« (Ï_ (Î·.app a)).inv â–· f â‰«
    (Î±_ _ _ _).hom :=
  Î·.toOplax.whiskerRight_naturality_id _

end

variable (F) in
/-- The identity strong transformation. -/
def id : StrongTrans F F where
  app a := ğŸ™ (F.obj a)
  naturality {a b} f := (Ï_ (F.map f)) â‰ªâ‰« (Î»_ (F.map f)).symm

instance : Inhabited (StrongTrans F F) :=
  âŸ¨id FâŸ©

/-- Vertical composition of strong transformations. -/
def vcomp (Î· : StrongTrans F G) (Î¸ : StrongTrans G H) :
    StrongTrans F H :=
  mkOfOplax (OplaxTrans.vcomp Î·.toOplax Î¸.toOplax)
    { naturality := fun {a b} f â†¦
        (Î±_ _ _ _).symm â‰ªâ‰« whiskerRightIso (Î·.naturality f) (Î¸.app b) â‰ªâ‰«
        (Î±_ _ _ _) â‰ªâ‰« whiskerLeftIso (Î·.app a) (Î¸.naturality f) â‰ªâ‰« (Î±_ _ _ _).symm }

end

end StrongTrans

variable (B C)

@[simps! id_app id_naturality_hom id_naturality_inv comp_naturality_hom
comp_naturality_inv]
instance categoryStruct : CategoryStruct (Pseudofunctor B C) where
  Hom F G := StrongTrans F G
  id F := StrongTrans.id F
  comp := StrongTrans.vcomp

section

variable {F G H : Pseudofunctor B C}

@[simp]
lemma StrongTrans.comp_app (Î· : F âŸ¶ G) (Î¸ : G âŸ¶ H) (a : B) :
    (Î· â‰« Î¸).app a = Î·.app a â‰« Î¸.app a :=
  rfl

end
@[simp]
lemma id.toOplax (F : Pseudofunctor B C) : ğŸ™ F = ğŸ™ F.toOplax :=
  rfl

end CategoryTheory.Pseudofunctor
