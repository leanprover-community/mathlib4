/-
Copyright (c) 2022 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno, Calle SÃ¶nne
-/
import Mathlib.CategoryTheory.Bicategory.Functor.Oplax
import Mathlib.CategoryTheory.Bicategory.Functor.Lax
import Mathlib.Tactic.CategoryTheory.ToApp

/-!
# Pseudofunctors

A pseudofunctor is an oplax (or lax) functor whose `mapId` and `mapComp` are isomorphisms.
We provide several constructors for pseudofunctors:
* `Pseudofunctor.mk` : the default constructor, which requires `mapâ‚‚_whiskerLeft` and
  `mapâ‚‚_whiskerRight` instead of naturality of `mapComp`.

* `Pseudofunctor.mkOfOplax` : construct a pseudofunctor from an oplax functor whose
  `mapId` and `mapComp` are isomorphisms. This constructor uses `Iso` to describe isomorphisms.
* `pseudofunctor.mkOfOplax'` : similar to `mkOfOplax`, but uses `IsIso` to describe isomorphisms.

* `Pseudofunctor.mkOfLax` : construct a pseudofunctor from a lax functor whose
  `mapId` and `mapComp` are isomorphisms. This constructor uses `Iso` to describe isomorphisms.
* `pseudofunctor.mkOfLax'` : similar to `mkOfLax`, but uses `IsIso` to describe isomorphisms.

## Main definitions

* `CategoryTheory.Pseudofunctor B C` : a pseudofunctor between bicategories `B` and `C`
* `CategoryTheory.Pseudofunctor.comp F G` : the composition of pseudofunctors

-/

namespace CategoryTheory

open Category Bicategory

universe wâ‚ wâ‚‚ wâ‚ƒ vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

variable {B : Type uâ‚} [Bicategory.{wâ‚, vâ‚} B] {C : Type uâ‚‚} [Bicategory.{wâ‚‚, vâ‚‚} C]
variable {D : Type uâ‚ƒ} [Bicategory.{wâ‚ƒ, vâ‚ƒ} D]

/-- A pseudofunctor `F` between bicategories `B` and `C` consists of a function between objects
`F.obj`, a function between 1-morphisms `F.map`, and a function between 2-morphisms `F.mapâ‚‚`.

Unlike functors between categories, `F.map` do not need to strictly commute with the compositions,
and do not need to strictly preserve the identity. Instead, there are specified 2-isomorphisms
`F.map (ğŸ™ a) â‰… ğŸ™ (F.obj a)` and `F.map (f â‰« g) â‰… F.map f â‰« F.map g`.

`F.mapâ‚‚` strictly commute with compositions and preserve the identity. They also preserve the
associator, the left unitor, and the right unitor modulo some adjustments of domains and codomains
of 2-morphisms.
-/
structure Pseudofunctor (B : Type uâ‚) [Bicategory.{wâ‚, vâ‚} B] (C : Type uâ‚‚)
    [Bicategory.{wâ‚‚, vâ‚‚} C] extends PrelaxFunctor B C where
  mapId (a : B) : map (ğŸ™ a) â‰… ğŸ™ (obj a)
  mapComp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) : map (f â‰« g) â‰… map f â‰« map g
  mapâ‚‚_whisker_left :
    âˆ€ {a b c : B} (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g âŸ¶ h),
      mapâ‚‚ (f â— Î·) = (mapComp f g).hom â‰« map f â— mapâ‚‚ Î· â‰« (mapComp f h).inv := by
    cat_disch
  mapâ‚‚_whisker_right :
    âˆ€ {a b c : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g) (h : b âŸ¶ c),
      mapâ‚‚ (Î· â–· h) = (mapComp f h).hom â‰« mapâ‚‚ Î· â–· map h â‰« (mapComp g h).inv := by
    cat_disch
  mapâ‚‚_associator :
    âˆ€ {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d),
      mapâ‚‚ (Î±_ f g h).hom = (mapComp (f â‰« g) h).hom â‰« (mapComp f g).hom â–· map h â‰«
      (Î±_ (map f) (map g) (map h)).hom â‰« map f â— (mapComp g h).inv â‰«
      (mapComp f (g â‰« h)).inv := by
    cat_disch
  mapâ‚‚_left_unitor :
    âˆ€ {a b : B} (f : a âŸ¶ b),
      mapâ‚‚ (Î»_ f).hom = (mapComp (ğŸ™ a) f).hom â‰« (mapId a).hom â–· map f â‰« (Î»_ (map f)).hom := by
    cat_disch
  mapâ‚‚_right_unitor :
    âˆ€ {a b : B} (f : a âŸ¶ b),
      mapâ‚‚ (Ï_ f).hom = (mapComp f (ğŸ™ b)).hom â‰« map f â— (mapId b).hom â‰« (Ï_ (map f)).hom := by
    cat_disch

initialize_simps_projections Pseudofunctor (+toPrelaxFunctor, -obj, -map, -mapâ‚‚)

namespace Pseudofunctor

attribute [simp, reassoc, to_app]
  mapâ‚‚_whisker_left mapâ‚‚_whisker_right mapâ‚‚_associator mapâ‚‚_left_unitor mapâ‚‚_right_unitor

section

open Iso

/-- The underlying prelax functor. -/
add_decl_doc Pseudofunctor.toPrelaxFunctor


attribute [nolint docBlame] CategoryTheory.Pseudofunctor.mapId
  CategoryTheory.Pseudofunctor.mapComp
  CategoryTheory.Pseudofunctor.mapâ‚‚_whisker_left
  CategoryTheory.Pseudofunctor.mapâ‚‚_whisker_right
  CategoryTheory.Pseudofunctor.mapâ‚‚_associator
  CategoryTheory.Pseudofunctor.mapâ‚‚_left_unitor
  CategoryTheory.Pseudofunctor.mapâ‚‚_right_unitor

variable (F : Pseudofunctor B C)

/-- The oplax functor associated with a pseudofunctor. -/
@[simps]
def toOplax : OplaxFunctor B C where
  toPrelaxFunctor := F.toPrelaxFunctor
  mapId := fun a => (F.mapId a).hom
  mapComp := fun f g => (F.mapComp f g).hom

instance hasCoeToOplax : Coe (Pseudofunctor B C) (OplaxFunctor B C) :=
  âŸ¨toOplaxâŸ©

/-- The Lax functor associated with a pseudofunctor. -/
@[simps]
def toLax : LaxFunctor B C where
  toPrelaxFunctor := F.toPrelaxFunctor
  mapId := fun a => (F.mapId a).inv
  mapComp := fun f g => (F.mapComp f g).inv
  mapâ‚‚_leftUnitor f := by
    rw [â† F.mapâ‚‚Iso_inv, eq_inv_comp, comp_inv_eq]
    simp
  mapâ‚‚_rightUnitor f := by
    rw [â† F.mapâ‚‚Iso_inv, eq_inv_comp, comp_inv_eq]
    simp

instance hasCoeToLax : Coe (Pseudofunctor B C) (LaxFunctor B C) :=
  âŸ¨toLaxâŸ©

/-- The identity pseudofunctor. -/
@[simps]
def id (B : Type uâ‚) [Bicategory.{wâ‚, vâ‚} B] : Pseudofunctor B B where
  toPrelaxFunctor := PrelaxFunctor.id B
  mapId := fun a => Iso.refl (ğŸ™ a)
  mapComp := fun f g => Iso.refl (f â‰« g)

instance : Inhabited (Pseudofunctor B B) :=
  âŸ¨id BâŸ©

/-- Composition of pseudofunctors. -/
@[simps]
def comp (F : Pseudofunctor B C) (G : Pseudofunctor C D) : Pseudofunctor B D where
  toPrelaxFunctor := F.toPrelaxFunctor.comp G.toPrelaxFunctor
  mapId := fun a => G.mapâ‚‚Iso (F.mapId a) â‰ªâ‰« G.mapId (F.obj a)
  mapComp := fun f g => (G.mapâ‚‚Iso (F.mapComp f g)) â‰ªâ‰« G.mapComp (F.map f) (F.map g)
  -- Note: whilst these are all provable by `cat_disch`, the proof is very slow
  mapâ‚‚_whisker_left f Î· := by simp
  mapâ‚‚_whisker_right Î· h := by simp
  mapâ‚‚_associator f g h := by simp
  mapâ‚‚_left_unitor f := by simp
  mapâ‚‚_right_unitor f := by simp

section

variable (F : Pseudofunctor B C) {a b : B}

@[to_app (attr := reassoc)]
lemma mapComp_assoc_right_hom {c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) :
    (F.mapComp f (g â‰« h)).hom â‰« F.map f â— (F.mapComp g h).hom = F.mapâ‚‚ (Î±_ f g h).inv â‰«
    (F.mapComp (f â‰« g) h).hom â‰« (F.mapComp f g).hom â–· F.map h â‰«
    (Î±_ (F.map f) (F.map g) (F.map h)).hom :=
  F.toOplax.mapComp_assoc_right _ _ _

@[to_app (attr := reassoc)]
lemma mapComp_assoc_left_hom {c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) :
    (F.mapComp (f â‰« g) h).hom â‰« (F.mapComp f g).hom â–· F.map h =
    F.mapâ‚‚ (Î±_ f g h).hom â‰« (F.mapComp f (g â‰« h)).hom â‰« F.map f â— (F.mapComp g h).hom
    â‰« (Î±_ (F.map f) (F.map g) (F.map h)).inv :=
  F.toOplax.mapComp_assoc_left _ _ _

@[to_app (attr := reassoc)]
lemma mapComp_assoc_right_inv {c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) :
    F.map f â— (F.mapComp g h).inv â‰« (F.mapComp f (g â‰« h)).inv =
    (Î±_ (F.map f) (F.map g) (F.map h)).inv â‰« (F.mapComp f g).inv â–· F.map h â‰«
    (F.mapComp (f â‰« g) h).inv â‰« F.mapâ‚‚ (Î±_ f g h).hom :=
  F.toLax.mapComp_assoc_right _ _ _

@[to_app (attr := reassoc)]
lemma mapComp_assoc_left_inv {c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) :
    (F.mapComp f g).inv â–· F.map h â‰« (F.mapComp (f â‰« g) h).inv =
    (Î±_ (F.map f) (F.map g) (F.map h)).hom â‰« F.map f â— (F.mapComp g h).inv â‰«
    (F.mapComp f (g â‰« h)).inv â‰« F.mapâ‚‚ (Î±_ f g h).inv :=
  F.toLax.mapComp_assoc_left _ _ _

@[reassoc, to_app]
lemma mapComp_id_left_hom (f : a âŸ¶ b) : (F.mapComp (ğŸ™ a) f).hom =
    F.mapâ‚‚ (Î»_ f).hom â‰« (Î»_ (F.map f)).inv â‰« (F.mapId a).inv â–· F.map f := by
  simp

lemma mapComp_id_left (f : a âŸ¶ b) : (F.mapComp (ğŸ™ a) f) = F.mapâ‚‚Iso (Î»_ f) â‰ªâ‰«
    (Î»_ (F.map f)).symm â‰ªâ‰« (whiskerRightIso (F.mapId a) (F.map f)).symm :=
  Iso.ext <| F.mapComp_id_left_hom f

@[reassoc, to_app]
lemma mapComp_id_left_inv (f : a âŸ¶ b) : (F.mapComp (ğŸ™ a) f).inv =
    (F.mapId a).hom â–· F.map f â‰« (Î»_ (F.map f)).hom â‰« F.mapâ‚‚ (Î»_ f).inv := by
  simp [mapComp_id_left]

lemma whiskerRightIso_mapId (f : a âŸ¶ b) : whiskerRightIso (F.mapId a) (F.map f) =
    (F.mapComp (ğŸ™ a) f).symm â‰ªâ‰« F.mapâ‚‚Iso (Î»_ f) â‰ªâ‰« (Î»_ (F.map f)).symm := by
  simp [mapComp_id_left]

@[reassoc, to_app]
lemma whiskerRight_mapId_hom (f : a âŸ¶ b) : (F.mapId a).hom â–· F.map f =
    (F.mapComp (ğŸ™ a) f).inv â‰« F.mapâ‚‚ (Î»_ f).hom â‰« (Î»_ (F.map f)).inv := by
  simp

@[reassoc, to_app]
lemma whiskerRight_mapId_inv (f : a âŸ¶ b) : (F.mapId a).inv â–· F.map f =
    (Î»_ (F.map f)).hom â‰« F.mapâ‚‚ (Î»_ f).inv â‰« (F.mapComp (ğŸ™ a) f).hom := by
  simpa using congrArg (Â·.inv) (F.whiskerRightIso_mapId f)

@[reassoc, to_app]
lemma mapComp_id_right_hom (f : a âŸ¶ b) : (F.mapComp f (ğŸ™ b)).hom =
    F.mapâ‚‚ (Ï_ f).hom â‰« (Ï_ (F.map f)).inv â‰« F.map f â— (F.mapId b).inv := by
  simp

lemma mapComp_id_right (f : a âŸ¶ b) : (F.mapComp f (ğŸ™ b)) = F.mapâ‚‚Iso (Ï_ f) â‰ªâ‰«
    (Ï_ (F.map f)).symm â‰ªâ‰« (whiskerLeftIso (F.map f) (F.mapId b)).symm :=
  Iso.ext <| F.mapComp_id_right_hom f

@[reassoc, to_app]
lemma mapComp_id_right_inv (f : a âŸ¶ b) : (F.mapComp f (ğŸ™ b)).inv =
    F.map f â— (F.mapId b).hom â‰« (Ï_ (F.map f)).hom â‰« F.mapâ‚‚ (Ï_ f).inv := by
  simp [mapComp_id_right]

lemma whiskerLeftIso_mapId (f : a âŸ¶ b) : whiskerLeftIso (F.map f) (F.mapId b) =
    (F.mapComp f (ğŸ™ b)).symm â‰ªâ‰« F.mapâ‚‚Iso (Ï_ f) â‰ªâ‰« (Ï_ (F.map f)).symm := by
  simp [mapComp_id_right]

@[reassoc, to_app]
lemma whiskerLeft_mapId_hom (f : a âŸ¶ b) : F.map f â— (F.mapId b).hom =
    (F.mapComp f (ğŸ™ b)).inv â‰« F.mapâ‚‚ (Ï_ f).hom â‰« (Ï_ (F.map f)).inv := by
  simp

@[reassoc, to_app]
lemma whiskerLeft_mapId_inv (f : a âŸ¶ b) : F.map f â— (F.mapId b).inv =
    (Ï_ (F.map f)).hom â‰« F.mapâ‚‚ (Ï_ f).inv â‰« (F.mapComp f (ğŸ™ b)).hom := by
  simpa using congrArg (Â·.inv) (F.whiskerLeftIso_mapId f)

/-- More flexible variant of `mapId`. (See the file `Bicategory.Functor.Strict`
for applications to strict bicategories.) -/
def mapId' {b : B} (f : b âŸ¶ b) (hf : f = ğŸ™ b := by cat_disch) :
    F.map f â‰… ğŸ™ (F.obj b) :=
  F.mapâ‚‚Iso (eqToIso (by rw [hf])) â‰ªâ‰« F.mapId _

lemma mapId'_eq_mapId (b : B) :
    F.mapId' (ğŸ™ b) rfl = F.mapId b := by
  simp [mapId']

@[simp]
lemma toLax_mapId' {b : B} (f : b âŸ¶ b) (hf : f = ğŸ™ b := by cat_disch) :
    F.toLax.mapId' f hf = (F.mapId' f hf).inv :=
  rfl

@[simp]
lemma toOplax_mapId' {b : B} (f : b âŸ¶ b) (hf : f = ğŸ™ b := by cat_disch) :
    F.toOplax.mapId' f hf = (F.mapId' f hf).hom :=
  rfl

/-- More flexible variant of `mapComp`. (See `Bicategory.Functor.Strict`
for applications to strict bicategories.) -/
def mapComp' {bâ‚€ bâ‚ bâ‚‚ : B} (f : bâ‚€ âŸ¶ bâ‚) (g : bâ‚ âŸ¶ bâ‚‚) (fg : bâ‚€ âŸ¶ bâ‚‚)
    (h : f â‰« g = fg := by cat_disch) :
    F.map fg â‰… F.map f â‰« F.map g :=
  F.mapâ‚‚Iso (eqToIso (by rw [h])) â‰ªâ‰« F.mapComp f g

lemma mapComp'_eq_mapComp {bâ‚€ bâ‚ bâ‚‚ : B} (f : bâ‚€ âŸ¶ bâ‚) (g : bâ‚ âŸ¶ bâ‚‚) :
    F.mapComp' f g _ rfl = F.mapComp f g := by
  simp [mapComp']

@[simp]
lemma toLax_mapComp' {bâ‚€ bâ‚ bâ‚‚ : B} (f : bâ‚€ âŸ¶ bâ‚) (g : bâ‚ âŸ¶ bâ‚‚) (fg : bâ‚€ âŸ¶ bâ‚‚)
    (h : f â‰« g = fg := by cat_disch) :
    F.toLax.mapComp' f g fg h = (F.mapComp' f g fg h).inv :=
  rfl

@[simp]
lemma toOplax_mapComp' {bâ‚€ bâ‚ bâ‚‚ : B} (f : bâ‚€ âŸ¶ bâ‚) (g : bâ‚ âŸ¶ bâ‚‚) (fg : bâ‚€ âŸ¶ bâ‚‚)
    (h : f â‰« g = fg := by cat_disch) :
    F.toOplax.mapComp' f g fg h = (F.mapComp' f g fg h).hom :=
  rfl

end

/-- Construct a pseudofunctor from an oplax functor whose `mapId` and `mapComp` are isomorphisms. -/
@[simps]
def mkOfOplax (F : OplaxFunctor B C) (F' : F.PseudoCore) : Pseudofunctor B C where
  toPrelaxFunctor := F.toPrelaxFunctor
  mapId := F'.mapIdIso
  mapComp := F'.mapCompIso
  mapâ‚‚_whisker_left := fun f g h Î· => by
    rw [F'.mapCompIso_hom f g, â† F.mapComp_naturality_right_assoc, â† F'.mapCompIso_hom f h,
      hom_inv_id, comp_id]
  mapâ‚‚_whisker_right := fun Î· h => by
    rw [F'.mapCompIso_hom _ h, â† F.mapComp_naturality_left_assoc, â† F'.mapCompIso_hom _ h,
      hom_inv_id, comp_id]
  mapâ‚‚_associator := fun f g h => by
    rw [F'.mapCompIso_hom (f â‰« g) h, F'.mapCompIso_hom f g, â† F.mapâ‚‚_associator_assoc, â†
      F'.mapCompIso_hom f (g â‰« h), â† F'.mapCompIso_hom g h, whiskerLeft_hom_inv_assoc,
      hom_inv_id, comp_id]

/-- Construct a pseudofunctor from an oplax functor whose `mapId` and `mapComp` are isomorphisms. -/
@[simps!]
noncomputable def mkOfOplax' (F : OplaxFunctor B C) [âˆ€ a, IsIso (F.mapId a)]
    [âˆ€ {a b c} (f : a âŸ¶ b) (g : b âŸ¶ c), IsIso (F.mapComp f g)] : Pseudofunctor B C where
  toPrelaxFunctor := F.toPrelaxFunctor
  mapId := fun a => asIso (F.mapId a)
  mapComp := fun f g => asIso (F.mapComp f g)
  mapâ‚‚_whisker_left := fun f g h Î· => by
    dsimp
    rw [â† assoc, IsIso.eq_comp_inv, F.mapComp_naturality_right]
  mapâ‚‚_whisker_right := fun Î· h => by
    dsimp
    rw [â† assoc, IsIso.eq_comp_inv, F.mapComp_naturality_left]
  mapâ‚‚_associator := fun f g h => by
    dsimp
    simp only [â† assoc]
    rw [IsIso.eq_comp_inv, â† Bicategory.inv_whiskerLeft, IsIso.eq_comp_inv]
    simp only [assoc, F.mapâ‚‚_associator]

/-- Construct a pseudofunctor from a lax functor whose `mapId` and `mapComp` are isomorphisms. -/
@[simps]
def mkOfLax (F : LaxFunctor B C) (F' : F.PseudoCore) : Pseudofunctor B C where
  toPrelaxFunctor := F.toPrelaxFunctor
  mapId := F'.mapIdIso
  mapComp := F'.mapCompIso
  mapâ‚‚_whisker_left f g h Î· := by
    rw [F'.mapCompIso_inv, â† LaxFunctor.mapComp_naturality_right, â† F'.mapCompIso_inv,
      hom_inv_id_assoc]
  mapâ‚‚_whisker_right Î· h := by
    rw [F'.mapCompIso_inv, â† LaxFunctor.mapComp_naturality_left, â† F'.mapCompIso_inv,
      hom_inv_id_assoc]
  mapâ‚‚_associator {a b c d} f g h := by
    rw [F'.mapCompIso_inv, F'.mapCompIso_inv, â† inv_comp_eq, â† IsIso.inv_comp_eq]
    simp
  mapâ‚‚_left_unitor {a b} f := by rw [â† IsIso.inv_eq_inv, â† F.mapâ‚‚_inv]; simp
  mapâ‚‚_right_unitor {a b} f := by rw [â† IsIso.inv_eq_inv, â† F.mapâ‚‚_inv]; simp

/-- Construct a pseudofunctor from a lax functor whose `mapId` and `mapComp` are isomorphisms. -/
@[simps!]
noncomputable def mkOfLax' (F : LaxFunctor B C) [âˆ€ a, IsIso (F.mapId a)]
    [âˆ€ {a b c} (f : a âŸ¶ b) (g : b âŸ¶ c), IsIso (F.mapComp f g)] : Pseudofunctor B C :=
  mkOfLax F
  { mapIdIso := fun a => (asIso (F.mapId a)).symm
    mapCompIso := fun f g => (asIso (F.mapComp f g)).symm }

end

end Pseudofunctor

end CategoryTheory
