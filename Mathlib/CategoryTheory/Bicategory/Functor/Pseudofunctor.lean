/-
Copyright (c) 2022 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import Mathlib.CategoryTheory.Bicategory.Functor.Oplax

#align_import category_theory.bicategory.functor from "leanprover-community/mathlib"@"369525b73f229ccd76a6ec0e0e0bf2be57599768"

/-!
# Pseudofunctors

A pseudofunctor is an oplax functor whose `mapId` and `mapComp` are isomorphisms. We provide
several constructors for pseudofunctors:
* `Pseudofunctor.mk` : the default constructor, which requires `map‚ÇÇ_whiskerLeft` and
  `map‚ÇÇ_whiskerRight` instead of naturality of `mapComp`.
* `Pseudofunctor.mkOfOplax` : construct a pseudofunctor from an oplax functor whose
  `mapId` and `mapComp` are isomorphisms. This constructor uses `Iso` to describe isomorphisms.
* `pseudofunctor.mkOfOplax'` : similar to `mkOfOplax`, but uses `IsIso` to describe
  isomorphisms.

The additional constructors are useful when constructing a pseudofunctor where the construction
of the oplax functor associated with it is already done. For example, the composition of
pseudofunctors can be defined by using the composition of oplax functors as follows:
```lean
def comp (F : Pseudofunctor B C) (G : Pseudofunctor C D) : Pseudofunctor B D :=
  mkOfOplax ((F : OplaxFunctor B C).comp G)
  { mapIdIso := fun a ‚Ü¶ (G.mapFunctor _ _).mapIso (F.mapId a) ‚â™‚â´ G.mapId (F.obj a),
    mapCompIso := fun f g ‚Ü¶
      (G.mapFunctor _ _).mapIso (F.mapComp f g) ‚â™‚â´ G.mapComp (F.map f) (F.map g) }
```
although the composition of pseudofunctors in this file is defined by using the default constructor
because `obviously` wasn't smart enough in mathlib3 and the porter of this file was too lazy
to investigate this issue further in mathlib4. Similarly, the composition is also defined by using
`mkOfOplax'` after giving appropriate instances for `IsIso`. The former constructor
`mkOfOplax` requires isomorphisms as data type `Iso`, and so it is useful if you don't want
to forget the definitions of the inverses. On the other hand, the latter constructor
`mkOfOplax'` is useful if you want to use propositional type class `IsIso`.

## Main definitions
* `CategoryTheory.Pseudofunctor B C` : a pseudofunctor between bicategories `B` and `C`
* `CategoryTheory.Pseudofunctor.comp F G` : the composition of pseudofunctors

-/

namespace CategoryTheory

open Category Bicategory

open Bicategory

universe w‚ÇÅ w‚ÇÇ w‚ÇÉ v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

variable {B : Type u‚ÇÅ} [Bicategory.{w‚ÇÅ, v‚ÇÅ} B] {C : Type u‚ÇÇ} [Bicategory.{w‚ÇÇ, v‚ÇÇ} C]
variable {D : Type u‚ÇÉ} [Bicategory.{w‚ÇÉ, v‚ÇÉ} D]

-- Porting note: this auxiliary def was introduced in Lean 3 and only used once, in this file,
-- to avoid a timeout. In Lean 4 the timeout isn't present and the definition causes other
-- things to break (simp proofs) so I removed it.
-- def Pseudofunctor.Map‚ÇÇAssociatorAux (obj : B ‚Üí C) (map : ‚àÄ {X Y : B}, (X ‚ü∂ Y) ‚Üí (obj X ‚ü∂ obj Y))
--     (map‚ÇÇ : ‚àÄ {a b : B} {f g : a ‚ü∂ b}, (f ‚ü∂ g) ‚Üí (map f ‚ü∂ map g))
--    (map_comp : ‚àÄ {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c), map (f ‚â´ g) ‚âÖ map f ‚â´ map g) {a b c d : B}
--     (f : a ‚ü∂ b) (g : b ‚ü∂ c) (h : c ‚ü∂ d) : Prop :=
--   map‚ÇÇ (Œ±_ f g h).hom =
--     (map_comp (f ‚â´ g) h).hom ‚â´
--       (map_comp f g).hom ‚ñ∑ map h ‚â´
--        (Œ±_ (map f) (map g) (map h)).hom ‚â´ map f ‚óÅ (map_comp g h).inv ‚â´ (map_comp f (g ‚â´ h)).inv
#noalign category_theory.pseudofunctor.map‚ÇÇ_associator_aux

/-- A pseudofunctor `F` between bicategories `B` and `C` consists of a function between objects
`F.obj`, a function between 1-morphisms `F.map`, and a function between 2-morphisms `F.map‚ÇÇ`.

Unlike functors between categories, `F.map` do not need to strictly commute with the compositions,
and do not need to strictly preserve the identity. Instead, there are specified 2-isomorphisms
`F.map (ùüô a) ‚âÖ ùüô (F.obj a)` and `F.map (f ‚â´ g) ‚âÖ F.map f ‚â´ F.map g`.

`F.map‚ÇÇ` strictly commute with compositions and preserve the identity. They also preserve the
associator, the left unitor, and the right unitor modulo some adjustments of domains and codomains
of 2-morphisms.
-/
structure Pseudofunctor (B : Type u‚ÇÅ) [Bicategory.{w‚ÇÅ, v‚ÇÅ} B] (C : Type u‚ÇÇ)
    [Bicategory.{w‚ÇÇ, v‚ÇÇ} C] extends PrelaxFunctor B C where
  mapId (a : B) : map (ùüô a) ‚âÖ ùüô (obj a)
  mapComp {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) : map (f ‚â´ g) ‚âÖ map f ‚â´ map g
  map‚ÇÇ_id : ‚àÄ {a b : B} (f : a ‚ü∂ b), map‚ÇÇ (ùüô f) = ùüô (map f) := by aesop_cat
  map‚ÇÇ_comp :
    ‚àÄ {a b : B} {f g h : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g) (Œ∏ : g ‚ü∂ h), map‚ÇÇ (Œ∑ ‚â´ Œ∏) = map‚ÇÇ Œ∑ ‚â´ map‚ÇÇ Œ∏ := by
    aesop_cat
  map‚ÇÇ_whisker_left :
    ‚àÄ {a b c : B} (f : a ‚ü∂ b) {g h : b ‚ü∂ c} (Œ∑ : g ‚ü∂ h),
      map‚ÇÇ (f ‚óÅ Œ∑) = (mapComp f g).hom ‚â´ map f ‚óÅ map‚ÇÇ Œ∑ ‚â´ (mapComp f h).inv := by
    aesop_cat
  map‚ÇÇ_whisker_right :
    ‚àÄ {a b c : B} {f g : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g) (h : b ‚ü∂ c),
      map‚ÇÇ (Œ∑ ‚ñ∑ h) = (mapComp f h).hom ‚â´ map‚ÇÇ Œ∑ ‚ñ∑ map h ‚â´ (mapComp g h).inv := by
    aesop_cat
  map‚ÇÇ_associator :
    ‚àÄ {a b c d : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) (h : c ‚ü∂ d),
      map‚ÇÇ (Œ±_ f g h).hom = (mapComp (f ‚â´ g) h).hom ‚â´ (mapComp f g).hom ‚ñ∑ map h ‚â´
      (Œ±_ (map f) (map g) (map h)).hom ‚â´ map f ‚óÅ (mapComp g h).inv ‚â´
      (mapComp f (g ‚â´ h)).inv := by
    aesop_cat
  map‚ÇÇ_left_unitor :
    ‚àÄ {a b : B} (f : a ‚ü∂ b),
      map‚ÇÇ (Œª_ f).hom = (mapComp (ùüô a) f).hom ‚â´ (mapId a).hom ‚ñ∑ map f ‚â´ (Œª_ (map f)).hom := by
    aesop_cat
  map‚ÇÇ_right_unitor :
    ‚àÄ {a b : B} (f : a ‚ü∂ b),
      map‚ÇÇ (œÅ_ f).hom = (mapComp f (ùüô b)).hom ‚â´ map f ‚óÅ (mapId b).hom ‚â´ (œÅ_ (map f)).hom := by
    aesop_cat
#align category_theory.pseudofunctor CategoryTheory.Pseudofunctor

initialize_simps_projections Pseudofunctor (+toPrelaxFunctor, -obj, -map, -map‚ÇÇ)

namespace Pseudofunctor

attribute [reassoc]
  map‚ÇÇ_comp map‚ÇÇ_whisker_left map‚ÇÇ_whisker_right map‚ÇÇ_associator map‚ÇÇ_left_unitor map‚ÇÇ_right_unitor

attribute [simp]
  map‚ÇÇ_id map‚ÇÇ_comp map‚ÇÇ_whisker_left map‚ÇÇ_whisker_right map‚ÇÇ_associator map‚ÇÇ_left_unitor
  map‚ÇÇ_right_unitor

section

open Iso

/-- The prelax functor between the underlying quivers. -/
add_decl_doc Pseudofunctor.toPrelaxFunctor


attribute [nolint docBlame] CategoryTheory.Pseudofunctor.mapId
  CategoryTheory.Pseudofunctor.mapComp
  CategoryTheory.Pseudofunctor.map‚ÇÇ_id
  CategoryTheory.Pseudofunctor.map‚ÇÇ_comp
  CategoryTheory.Pseudofunctor.map‚ÇÇ_whisker_left
  CategoryTheory.Pseudofunctor.map‚ÇÇ_whisker_right
  CategoryTheory.Pseudofunctor.map‚ÇÇ_associator
  CategoryTheory.Pseudofunctor.map‚ÇÇ_left_unitor
  CategoryTheory.Pseudofunctor.map‚ÇÇ_right_unitor

instance hasCoeToPrelaxFunctor : Coe (Pseudofunctor B C) (PrelaxFunctor B C) :=
  ‚ü®toPrelaxFunctor‚ü©
#align category_theory.pseudofunctor.has_coe_to_prelax_functor CategoryTheory.Pseudofunctor.hasCoeToPrelaxFunctor

variable (F : Pseudofunctor B C)

-- Porting note: `toPrelaxFunctor_eq_coe` and `to_prelaxFunctor_obj`
-- are syntactic tautologies in lean 4
#noalign category_theory.pseudofunctor.to_prelax_functor_eq_coe
#noalign category_theory.pseudofunctor.to_prelax_functor_obj

-- Porting note: removed lemma `to_prelaxFunctor_map` relating the now
-- nonexistent `PrelaxFunctor.map` and the now nonexistent `Pseudofunctor.map`
#noalign category_theory.pseudofunctor.to_prelax_functor_map

-- Porting note: removed lemma `to_prelaxFunctor_map‚ÇÇ` relating
-- `PrelaxFunctor.map‚ÇÇ` to nonexistent `Pseudofunctor.map‚ÇÇ`
#noalign category_theory.pseudofunctor.to_prelax_functor_map‚ÇÇ

/-- The oplax functor associated with a pseudofunctor. -/
def toOplax : OplaxFunctor B C :=
  { (F : PrelaxFunctor B C) with
    mapId := fun a => (F.mapId a).hom
    mapComp := fun f g => (F.mapComp f g).hom }
#align category_theory.pseudofunctor.to_oplax CategoryTheory.Pseudofunctor.toOplax

instance hasCoeToOplax : Coe (Pseudofunctor B C) (OplaxFunctor B C) :=
  ‚ü®toOplax‚ü©
#align category_theory.pseudofunctor.has_coe_to_oplax CategoryTheory.Pseudofunctor.hasCoeToOplax

-- Porting note: `toOplax_eq_coe` is a syntactic tautology in lean 4
#noalign category_theory.pseudofunctor.to_oplax_eq_coe

@[simp]
theorem to_oplax_obj : (F : OplaxFunctor B C).obj = F.obj :=
  rfl
#align category_theory.pseudofunctor.to_oplax_obj CategoryTheory.Pseudofunctor.to_oplax_obj

-- Porting note: to_oplax_map related `OplaxFunctor.map` to `Pseudofunctor.map` but neither
-- of these exist
#noalign category_theory.pseudofunctor.to_oplax_map

-- Porting note: to_oplax_map‚ÇÇ related `OplaxFunctor.map‚ÇÇ` to `Pseudofunctor.map‚ÇÇ` but neither
-- of these exist
#noalign category_theory.pseudofunctor.to_oplax_map‚ÇÇ

@[simp]
theorem to_oplax_mapId (a : B) : (F : OplaxFunctor B C).mapId a = (F.mapId a).hom :=
  rfl
#align category_theory.pseudofunctor.to_oplax_map_id CategoryTheory.Pseudofunctor.to_oplax_mapId

@[simp]
theorem to_oplax_mapComp {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) :
    (F : OplaxFunctor B C).mapComp f g = (F.mapComp f g).hom :=
  rfl
#align category_theory.pseudofunctor.to_oplax_map_comp CategoryTheory.Pseudofunctor.to_oplax_mapComp

-- Porting note: I changed `simps` to `simps!` without understanding what I was doing
-- (lean 4 told me to do this)
/-- Function on 1-morphisms as a functor. -/
@[simps!]
def mapFunctor (a b : B) : (a ‚ü∂ b) ‚•§ (F.obj a ‚ü∂ F.obj b) :=
  (F : OplaxFunctor B C).mapFunctor a b
#align category_theory.pseudofunctor.map_functor CategoryTheory.Pseudofunctor.mapFunctor

/-- The identity pseudofunctor. -/
@[simps]
def id (B : Type u‚ÇÅ) [Bicategory.{w‚ÇÅ, v‚ÇÅ} B] : Pseudofunctor B B :=
  { PrelaxFunctor.id B with
    mapId := fun a => Iso.refl (ùüô a)
    mapComp := fun f g => Iso.refl (f ‚â´ g) }
#align category_theory.pseudofunctor.id CategoryTheory.Pseudofunctor.id

instance : Inhabited (Pseudofunctor B B) :=
  ‚ü®id B‚ü©

/-- Composition of pseudofunctors. -/
def comp (F : Pseudofunctor B C) (G : Pseudofunctor C D) : Pseudofunctor B D :=
  { (F : PrelaxFunctor B C).comp
      (G : PrelaxFunctor C D) with
    mapId := fun a => (G.mapFunctor _ _).mapIso (F.mapId a) ‚â™‚â´ G.mapId (F.obj a)
    mapComp := fun f g =>
      (G.mapFunctor _ _).mapIso (F.mapComp f g) ‚â™‚â´ G.mapComp (F.map f) (F.map g) }
#align category_theory.pseudofunctor.comp CategoryTheory.Pseudofunctor.comp

-- `comp` is near the `maxHeartbeats` limit (and seems to go over in CI),
-- so we defer creating its `@[simp]` lemmas until a separate command.
attribute [simps] comp

/-- Construct a pseudofunctor from an oplax functor whose `mapId` and `mapComp` are isomorphisms.
-/
@[simps]
def mkOfOplax (F : OplaxFunctor B C) (F' : F.PseudoCore) : Pseudofunctor B C :=
  { (F : PrelaxFunctor B C) with
    mapId := F'.mapIdIso
    mapComp := F'.mapCompIso
    map‚ÇÇ_whisker_left := fun f g h Œ∑ => by
      dsimp
      rw [F'.mapCompIso_hom f g, ‚Üê F.mapComp_naturality_right_assoc, ‚Üê F'.mapCompIso_hom f h,
        hom_inv_id, comp_id]
    map‚ÇÇ_whisker_right := fun Œ∑ h => by
      dsimp
      rw [F'.mapCompIso_hom _ h, ‚Üê F.mapComp_naturality_left_assoc, ‚Üê F'.mapCompIso_hom _ h,
        hom_inv_id, comp_id]
    map‚ÇÇ_associator := fun f g h => by
      dsimp
      rw [F'.mapCompIso_hom (f ‚â´ g) h, F'.mapCompIso_hom f g, ‚Üê F.map‚ÇÇ_associator_assoc, ‚Üê
        F'.mapCompIso_hom f (g ‚â´ h), ‚Üê F'.mapCompIso_hom g h, whiskerLeft_hom_inv_assoc,
        hom_inv_id, comp_id] }
#align category_theory.pseudofunctor.mk_of_oplax CategoryTheory.Pseudofunctor.mkOfOplax

/-- Construct a pseudofunctor from an oplax functor whose `mapId` and `mapComp` are isomorphisms.
-/
@[simps]
noncomputable def mkOfOplax' (F : OplaxFunctor B C) [‚àÄ a, IsIso (F.mapId a)]
    [‚àÄ {a b c} (f : a ‚ü∂ b) (g : b ‚ü∂ c), IsIso (F.mapComp f g)] : Pseudofunctor B C :=
  { (F : PrelaxFunctor B C) with
    mapId := fun a => asIso (F.mapId a)
    mapComp := fun f g => asIso (F.mapComp f g)
    map‚ÇÇ_whisker_left := fun f g h Œ∑ => by
      dsimp
      rw [‚Üê assoc, IsIso.eq_comp_inv, F.mapComp_naturality_right]
    map‚ÇÇ_whisker_right := fun Œ∑ h => by
      dsimp
      rw [‚Üê assoc, IsIso.eq_comp_inv, F.mapComp_naturality_left]
    map‚ÇÇ_associator := fun f g h => by
      dsimp
      simp only [‚Üê assoc]
      rw [IsIso.eq_comp_inv, ‚Üê inv_whiskerLeft, IsIso.eq_comp_inv]
      simp only [assoc, F.map‚ÇÇ_associator] }
#align category_theory.pseudofunctor.mk_of_oplax' CategoryTheory.Pseudofunctor.mkOfOplax'

end

end Pseudofunctor

end CategoryTheory
