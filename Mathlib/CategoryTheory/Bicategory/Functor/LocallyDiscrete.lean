/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor
import Mathlib.CategoryTheory.Bicategory.LocallyDiscrete

/-!
# Pseudofunctors from locally discrete bicategories

This file provides various ways of constructing pseudofunctors from locally discrete
bicategories.

Firstly, we define the constructors `pseudofunctorOfIsLocallyDiscrete` and
`oplaxFunctorOfIsLocallyDiscrete` for defining pseudofunctors and oplax functors
from a locally discrete bicategories. In this situation, we do not need to care about
the field `mapâ‚‚`, because all the `2`-morphisms in `B` are identities.

We also define a specialized constructor `LocallyDiscrete.mkPseudofunctor` when
the source bicategory is of the form `B := LocallyDiscrete Bâ‚€` for a category `Bâ‚€`.

We also prove that a functor `F : I â¥¤ B` with `B` a strict bicategory can be promoted
to a pseudofunctor (or oplax functor) (`Functor.toPseudofunctor`) with domain
`LocallyDiscrete I`.

-/

namespace CategoryTheory

open Bicategory

/-- Constructor for pseudofunctors from a locally discrete bicategory. In that
case, we do not need to provide the `mapâ‚‚` field of pseudofunctors. -/
@[simps obj map mapId mapComp]
def pseudofunctorOfIsLocallyDiscrete
    {B C : Type*} [Bicategory B] [IsLocallyDiscrete B] [Bicategory C]
    (obj : B â†’ C)
    (map : âˆ€ {b b' : B}, (b âŸ¶ b') â†’ (obj b âŸ¶ obj b'))
    (mapId : âˆ€ (b : B), map (ğŸ™ b) â‰… ğŸ™ _)
    (mapComp : âˆ€ {bâ‚€ bâ‚ bâ‚‚ : B} (f : bâ‚€ âŸ¶ bâ‚) (g : bâ‚ âŸ¶ bâ‚‚), map (f â‰« g) â‰… map f â‰« map g)
    (mapâ‚‚_associator : âˆ€ {bâ‚€ bâ‚ bâ‚‚ bâ‚ƒ : B} (f : bâ‚€ âŸ¶ bâ‚) (g : bâ‚ âŸ¶ bâ‚‚) (h : bâ‚‚ âŸ¶ bâ‚ƒ),
      (mapComp (f â‰« g) h).hom â‰«
        (mapComp f g).hom â–· map h â‰« (Î±_ (map f) (map g) (map h)).hom â‰«
          map f â— (mapComp g h).inv â‰« (mapComp f (g â‰« h)).inv = eqToHom (by simp) := by cat_disch)
    (mapâ‚‚_left_unitor : âˆ€ {bâ‚€ bâ‚ : B} (f : bâ‚€ âŸ¶ bâ‚),
      (mapComp (ğŸ™ bâ‚€) f).hom â‰« (mapId bâ‚€).hom â–· map f â‰« (Î»_ (map f)).hom = eqToHom (by simp) := by
        cat_disch)
    (mapâ‚‚_right_unitor : âˆ€ {bâ‚€ bâ‚ : B} (f : bâ‚€ âŸ¶ bâ‚),
      (mapComp f (ğŸ™ bâ‚)).hom â‰« map f â— (mapId bâ‚).hom â‰« (Ï_ (map f)).hom = eqToHom (by simp) := by
        cat_disch) :
    Pseudofunctor B C where
  obj := obj
  map := map
  mapâ‚‚ Ï† := eqToHom (by
    obtain rfl := obj_ext_of_isDiscrete Ï†
    dsimp)
  mapId := mapId
  mapComp := mapComp
  mapâ‚‚_whisker_left _ _ _ Î· := by
    obtain rfl := obj_ext_of_isDiscrete Î·
    simp
  mapâ‚‚_whisker_right Î· _ := by
    obtain rfl := obj_ext_of_isDiscrete Î·
    simp

/-- Constructor for oplax functors from a locally discrete bicategory. In that
case, we do not need to provide the `mapâ‚‚` field of oplax functors. -/
@[simps obj map mapId mapComp]
def oplaxFunctorOfIsLocallyDiscrete
    {B C : Type*} [Bicategory B] [IsLocallyDiscrete B] [Bicategory C]
    (obj : B â†’ C)
    (map : âˆ€ {b b' : B}, (b âŸ¶ b') â†’ (obj b âŸ¶ obj b'))
    (mapId : âˆ€ (b : B), map (ğŸ™ b) âŸ¶ ğŸ™ _)
    (mapComp : âˆ€ {bâ‚€ bâ‚ bâ‚‚ : B} (f : bâ‚€ âŸ¶ bâ‚) (g : bâ‚ âŸ¶ bâ‚‚), map (f â‰« g) âŸ¶ map f â‰« map g)
    (mapâ‚‚_associator : âˆ€ {bâ‚€ bâ‚ bâ‚‚ bâ‚ƒ : B} (f : bâ‚€ âŸ¶ bâ‚) (g : bâ‚ âŸ¶ bâ‚‚) (h : bâ‚‚ âŸ¶ bâ‚ƒ),
      eqToHom (by simp) â‰« mapComp f (g â‰« h) â‰« map f â— mapComp g h =
        mapComp (f â‰« g) h â‰« mapComp f g â–· map h â‰« (Î±_ (map f) (map g) (map h)).hom := by
          cat_disch)
    (mapâ‚‚_left_unitor : âˆ€ {bâ‚€ bâ‚ : B} (f : bâ‚€ âŸ¶ bâ‚),
      mapComp (ğŸ™ bâ‚€) f â‰« mapId bâ‚€ â–· map f â‰« (Î»_ (map f)).hom = eqToHom (by simp) := by
        cat_disch)
    (mapâ‚‚_right_unitor : âˆ€ {bâ‚€ bâ‚ : B} (f : bâ‚€ âŸ¶ bâ‚),
      mapComp f (ğŸ™ bâ‚) â‰« map f â— mapId bâ‚ â‰« (Ï_ (map f)).hom = eqToHom (by simp) := by
        cat_disch) :
    OplaxFunctor B C where
  obj := obj
  map := map
  mapâ‚‚ Ï† := eqToHom (by
    obtain rfl := obj_ext_of_isDiscrete Ï†
    dsimp)
  mapId := mapId
  mapComp := mapComp
  mapComp_naturality_left Î· := by
    obtain rfl := obj_ext_of_isDiscrete Î·
    simp
  mapComp_naturality_right _ _ _ Î· := by
    obtain rfl := obj_ext_of_isDiscrete Î·
    simp

section

variable {C D : Type*} [Category C] [Category D] (F : C â¥¤ D)

/--
A functor between two categories `C` and `D` can be lifted to a pseudofunctor between the
corresponding locally discrete bicategories.
-/
@[simps! obj map mapId mapComp]
def Functor.toPseudoFunctor : Pseudofunctor (LocallyDiscrete C) (LocallyDiscrete D) :=
  pseudofunctorOfIsLocallyDiscrete
    (fun âŸ¨XâŸ© â†¦.mk <| F.obj X) (fun âŸ¨fâŸ© â†¦ (F.map f).toLoc)
    (fun âŸ¨XâŸ© â†¦ eqToIso (by simp)) (fun f g â†¦ eqToIso (by simp))

/--
A functor between two categories `C` and `D` can be lifted to an oplax functor between the
corresponding locally discrete bicategories.

This is just an abbreviation of `Functor.toPseudoFunctor.toOplax`.
-/
@[simps! obj map mapId mapComp]
abbrev Functor.toOplaxFunctor : OplaxFunctor (LocallyDiscrete C) (LocallyDiscrete D) :=
  F.toPseudoFunctor.toOplax

end

section

variable {I B : Type*} [Category I] [Bicategory B] [Strict B] (F : I â¥¤ B)

attribute [local simp]
  Strict.leftUnitor_eqToIso Strict.rightUnitor_eqToIso Strict.associator_eqToIso

/--
If `B` is a strict bicategory and `I` is a (1-)category, any functor (of 1-categories) `I â¥¤ B` can
be promoted to a pseudofunctor from `LocallyDiscrete I` to `B`.
-/
@[simps! obj map mapId mapComp]
def Functor.toPseudoFunctor' : Pseudofunctor (LocallyDiscrete I) B :=
  pseudofunctorOfIsLocallyDiscrete
    (fun âŸ¨XâŸ© â†¦ F.obj X) (fun âŸ¨fâŸ© â†¦ F.map f)
    (fun âŸ¨XâŸ© â†¦ eqToIso (by simp)) (fun f g â†¦ eqToIso (by simp))

/--
If `B` is a strict bicategory and `I` is a (1-)category, any functor (of 1-categories) `I â¥¤ B` can
be promoted to an oplax functor from `LocallyDiscrete I` to `B`.
-/
@[simps! obj map mapId mapComp]
abbrev Functor.toOplaxFunctor' : OplaxFunctor (LocallyDiscrete I) B :=
  F.toPseudoFunctor'.toOplax

end

namespace LocallyDiscrete

/-- Constructor for pseudofunctors from a locally discrete bicategory. In that
case, we do not need to provide the `mapâ‚‚` field of pseudofunctors. -/
@[simps! obj map mapId mapComp]
def mkPseudofunctor {Bâ‚€ C : Type*} [Category Bâ‚€] [Bicategory C]
    (obj : Bâ‚€ â†’ C)
    (map : âˆ€ {b b' : Bâ‚€}, (b âŸ¶ b') â†’ (obj b âŸ¶ obj b'))
    (mapId : âˆ€ (b : Bâ‚€), map (ğŸ™ b) â‰… ğŸ™ _)
    (mapComp : âˆ€ {bâ‚€ bâ‚ bâ‚‚ : Bâ‚€} (f : bâ‚€ âŸ¶ bâ‚) (g : bâ‚ âŸ¶ bâ‚‚), map (f â‰« g) â‰… map f â‰« map g)
    (mapâ‚‚_associator : âˆ€ {bâ‚€ bâ‚ bâ‚‚ bâ‚ƒ : Bâ‚€} (f : bâ‚€ âŸ¶ bâ‚) (g : bâ‚ âŸ¶ bâ‚‚) (h : bâ‚‚ âŸ¶ bâ‚ƒ),
      (mapComp (f â‰« g) h).hom â‰«
        (mapComp f g).hom â–· map h â‰« (Î±_ (map f) (map g) (map h)).hom â‰«
          map f â— (mapComp g h).inv â‰« (mapComp f (g â‰« h)).inv = eqToHom (by simp) := by cat_disch)
    (mapâ‚‚_left_unitor : âˆ€ {bâ‚€ bâ‚ : Bâ‚€} (f : bâ‚€ âŸ¶ bâ‚),
      (mapComp (ğŸ™ bâ‚€) f).hom â‰« (mapId bâ‚€).hom â–· map f â‰« (Î»_ (map f)).hom = eqToHom (by simp) := by
        cat_disch)
    (mapâ‚‚_right_unitor : âˆ€ {bâ‚€ bâ‚ : Bâ‚€} (f : bâ‚€ âŸ¶ bâ‚),
      (mapComp f (ğŸ™ bâ‚)).hom â‰« map f â— (mapId bâ‚).hom â‰« (Ï_ (map f)).hom = eqToHom (by simp) := by
        cat_disch) :
    Pseudofunctor (LocallyDiscrete Bâ‚€) C :=
  pseudofunctorOfIsLocallyDiscrete (fun b â†¦ obj b.as) (fun f â†¦ map f.as)
    (fun _ â†¦ mapId _) (fun _ _ â†¦ mapComp _ _) (fun _ _ _ â†¦ mapâ‚‚_associator _ _ _)
    (fun _ â†¦ mapâ‚‚_left_unitor _) (fun _ â†¦ mapâ‚‚_right_unitor _)

end LocallyDiscrete

end CategoryTheory
