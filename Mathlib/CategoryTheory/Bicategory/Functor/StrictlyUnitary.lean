/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor

/-!
# Strictly unitary lax functors and pseudofunctors

In this file, we define strictly unitary Lax functors and
strictly unitary pseudofunctors between bicategories.

A lax functor `F` is said to be *strictly unitary* (sometimes, they are also
called *normal*) if there is an equality `F.obj (ğŸ™ _) = ğŸ™ (F.obj x)` and if.

A pseudofunctor is called *strictly unitary* (or a *normal homomorphism*) if it
satisfies the same condition (i.e its "underlying" lax functor is strictly
unitary).

## References
- [Kerodon, section 2.2.2.4](https://kerodon.net/tag/008G)

## TODOs
* Define lax-composable (resp. pseudo-composable) arrows as strictly unitary
lax (resp. pseudo-) functors out of `LocallyDiscrete Fin n`.
* Define identity-component oplax natural transformations ("icons") between
strictly unitary pseudofunctors and construct a bicategory structure on
bicategories, strictly unitary pseudofunctors and icons.
* Construct the Duskin of a bicategory using lax-composable arrows
* Construct the 2-nerve of a bicategory using pseudo-composable arrows

-/

namespace CategoryTheory

open Category Bicategory

open Bicategory

universe wâ‚ wâ‚‚ wâ‚ƒ wâ‚„ vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ uâ‚ uâ‚‚ uâ‚ƒ uâ‚„

variable {B : Type uâ‚} [Bicategory.{wâ‚, vâ‚} B]
  {C : Type uâ‚‚} [Bicategory.{wâ‚‚, vâ‚‚} C]
  {D : Type uâ‚ƒ} [Bicategory.{wâ‚ƒ, vâ‚ƒ} D]

lemma PrelaxFunctor.mapâ‚‚_eqToHom (F : PrelaxFunctor B C)
    {a b : B} {f g : a âŸ¶ b} (h : f = g) :
    F.mapâ‚‚ (eqToHom h) = eqToHom (congrArg F.map h) := by
  subst h
  simp

variable (B C) in
/-- A strictly unitary lax functor `F` between bicategories `B` and `C` is a
lax functor `F` from `B` to `C` such that the structure 1-cell
`ğŸ™ (obj X) âŸ¶ map (ğŸ™ X)` is in fact an identity 1-cell for every `X : B`. -/
@[kerodon 008R]
structure StrictlyUnitaryLaxFunctor extends LaxFunctor B C where
  map_id (X : B) : map (ğŸ™ X) = ğŸ™ (obj X)
  mapId_eq_eqToHom (X : B) : (mapId X) = eqToHom (map_id X).symm

namespace StrictlyUnitaryLaxFunctor

/-- An alternate constructor for strictly unitary lax functors that does not
require the `mapId` fields, and that adapts the `mapâ‚‚_leftUnitor` and
`mapâ‚‚_rightUnitor` to the fact that the functor is strictly unitary. -/
@[simps]
def mk'
    (obj : B â†’ C)
    (map : âˆ€ {X Y : B}, (X âŸ¶ Y) â†’ (obj X âŸ¶ obj Y))
    (map_id : âˆ€ (X : B), map (ğŸ™ X) = ğŸ™ (obj X))
    (mapâ‚‚ : âˆ€ {a b : B} {f g : a âŸ¶ b}, (f âŸ¶ g) â†’ (map f âŸ¶ map g))
    (mapâ‚‚_id : âˆ€ {a b : B} (f : a âŸ¶ b), mapâ‚‚ (ğŸ™ f) = ğŸ™ (map f) := by aesop_cat)
    (mapâ‚‚_comp :
        âˆ€ {a b : B} {f g h : a âŸ¶ b} (Î· : f âŸ¶ g) (Î¸ : g âŸ¶ h),
          mapâ‚‚ (Î· â‰« Î¸) = mapâ‚‚ Î· â‰« mapâ‚‚ Î¸ := by
      aesop_cat)
    (mapComp : âˆ€ {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c),
      map f â‰« map g âŸ¶ map (f â‰« g))
    (mapComp_naturality_left :
        âˆ€ {a b c : B} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') (g : b âŸ¶ c),
          mapComp f g â‰« mapâ‚‚ (Î· â–· g) = mapâ‚‚ Î· â–· map g â‰« mapComp f' g := by
      aesop_cat)
    (mapComp_naturality_right :
        âˆ€ {a b c : B} (f : a âŸ¶ b) {g g' : b âŸ¶ c} (Î· : g âŸ¶ g'),
          mapComp f g â‰« mapâ‚‚ (f â— Î·) = map f â— mapâ‚‚ Î· â‰« mapComp f g' := by
      aesop_cat)
    (mapâ‚‚_leftUnitor :
        âˆ€ {a b : B} (f : a âŸ¶ b),
          mapâ‚‚ (Î»_ f).inv =
          (Î»_ (map f)).inv â‰« eqToHom (by rw [map_id a]) â‰« mapComp (ğŸ™ a) f := by
      aesop_cat)
    (mapâ‚‚_rightUnitor :
        âˆ€ {a b : B} (f : a âŸ¶ b),
          mapâ‚‚ (Ï_ f).inv =
          (Ï_ (map f)).inv â‰« eqToHom (by rw [map_id b]) â‰« mapComp f (ğŸ™ b) := by
      aesop_cat)
    (mapâ‚‚_associator :
        âˆ€ {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d),
          mapComp f g â–· map h â‰« mapComp (f â‰« g) h â‰« mapâ‚‚ (Î±_ f g h).hom =
          (Î±_ (map f) (map g) (map h)).hom â‰« map f â— mapComp g h â‰«
            mapComp f (g â‰« h) := by
      aesop_cat) :
    StrictlyUnitaryLaxFunctor B C where
  obj := obj
  map := map
  map_id := map_id
  mapId x := eqToHom (map_id x).symm
  mapId_eq_eqToHom x := rfl
  mapâ‚‚ := mapâ‚‚
  mapâ‚‚_id := mapâ‚‚_id
  mapâ‚‚_comp := mapâ‚‚_comp
  mapComp := mapComp
  mapComp_naturality_left := mapComp_naturality_left
  mapComp_naturality_right := mapComp_naturality_right
  mapâ‚‚_leftUnitor {a b} f := by
    simpa using mapâ‚‚_leftUnitor f
  mapâ‚‚_rightUnitor {a b} f := by
    simpa using mapâ‚‚_rightUnitor f

instance mapId_isIso (F : StrictlyUnitaryLaxFunctor B C) (x : B) :
    IsIso (F.mapId x) := by
  rw [mapId_eq_eqToHom]
  infer_instance

/-- Promote the morphism `F.mapId x : ğŸ™ (F.obj x) âŸ¶ F.map (ğŸ™ x)`
to an isomorphism when `F` is strictly unitary. -/
@[simps]
def mapIdIso (F : StrictlyUnitaryLaxFunctor B C) (x : B) :
   ğŸ™ (F.obj x) â‰… F.map (ğŸ™ x) where
  hom := F.mapId x
  inv := eqToHom (F.map_id x)
  hom_inv_id := by simp [F.mapId_eq_eqToHom]
  inv_hom_id := by simp [F.mapId_eq_eqToHom]

variable (B) in
/-- The identity `StrictlyUnitaryLaxFunctor`. -/
@[simps!]
def id : StrictlyUnitaryLaxFunctor B B where
  __ := LaxFunctor.id B
  map_id _ := rfl
  mapId_eq_eqToHom _ := rfl

/-- Composition of `StrictlyUnitaryLaxFunctor`. -/
@[simps!]
def comp (F : StrictlyUnitaryLaxFunctor B C)
    (G : StrictlyUnitaryLaxFunctor C D) :
    StrictlyUnitaryLaxFunctor B D where
  __ := LaxFunctor.comp F.toLaxFunctor G.toLaxFunctor
  map_id _ := by simp [StrictlyUnitaryLaxFunctor.map_id]
  mapId_eq_eqToHom _ := by
    simp [StrictlyUnitaryLaxFunctor.mapId_eq_eqToHom,
      PrelaxFunctor.mapâ‚‚_eqToHom]

section
attribute [local ext] StrictlyUnitaryLaxFunctor

/-- Composition of `StrictlyUnitaryLaxFunctor` is strictly right unitary -/
lemma comp_id (F : StrictlyUnitaryLaxFunctor B C) :
    F.comp (.id C) = F := by
  ext
  Â· simp
  all_goals
  Â· rw [heq_iff_eq]
    ext
    simp

/-- Composition of `StrictlyUnitaryLaxFunctor` is strictly left unitary -/
lemma id_comp (F : StrictlyUnitaryLaxFunctor B C) :
    (StrictlyUnitaryLaxFunctor.id B).comp F = F := by
  ext
  Â· simp
  all_goals
  Â· rw [heq_iff_eq]
    ext
    simp

/-- Composition of `StrictlyUnitaryLaxFunctor` is strictly associative -/
lemma comp_assoc {E : Type uâ‚„} [Bicategory.{wâ‚„, vâ‚„} E]
    (F : StrictlyUnitaryLaxFunctor B C) (G : StrictlyUnitaryLaxFunctor C D)
    (H : StrictlyUnitaryLaxFunctor D E) :
    (F.comp G).comp H = F.comp (G.comp H) := by
  ext
  Â· simp
  all_goals
  Â· rw [heq_iff_eq]
    ext
    simp

end

end StrictlyUnitaryLaxFunctor

variable (B C) in
/-- A strictly unitary pseudofunctor (sometimes called a "normal homomorphism)
`F` between bicategories `B` and `C` is a lax functor `F` from `B` to `C`
such that the structure isomorphism `map (ğŸ™ X) â‰… ğŸ™ (F.obj X)` is in fact an
identity 1-cell for every `X : B`. -/
@[kerodon 008R]
structure StrictlyUnitaryPseudofunctor extends Pseudofunctor B C where
  map_id (X : B) : map (ğŸ™ X) = ğŸ™ (obj X)
  mapId_eq_eqToIso (X : B) : (mapId X) = eqToIso (map_id X)

namespace StrictlyUnitaryPseudofunctor

/-- An alternate constructor for strictly unitary lax functors that does not
require the `mapId` fields, and that adapts the `mapâ‚‚_leftUnitor` and
`mapâ‚‚_rightUnitor` to the fact that the functor is strictly unitary. -/
@[simps]
def mk'
    (obj : B â†’ C)
    (map : âˆ€ {X Y : B}, (X âŸ¶ Y) â†’ (obj X âŸ¶ obj Y))
    (map_id : âˆ€ (X : B), map (ğŸ™ X) = ğŸ™ (obj X))
    (mapâ‚‚ : âˆ€ {a b : B} {f g : a âŸ¶ b}, (f âŸ¶ g) â†’ (map f âŸ¶ map g))
    (mapâ‚‚_id : âˆ€ {a b : B} (f : a âŸ¶ b), mapâ‚‚ (ğŸ™ f) = ğŸ™ (map f) := by aesop_cat)
    (mapâ‚‚_comp :
        âˆ€ {a b : B} {f g h : a âŸ¶ b} (Î· : f âŸ¶ g) (Î¸ : g âŸ¶ h),
          mapâ‚‚ (Î· â‰« Î¸) = mapâ‚‚ Î· â‰« mapâ‚‚ Î¸ := by
      aesop_cat)
    (mapComp : âˆ€ {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c),
      map (f â‰« g) â‰… map f â‰« map g)
    (mapâ‚‚_whiskerLeft :
        âˆ€ {a b c : B} (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g âŸ¶ h),
          mapâ‚‚ (f â— Î·) =
          (mapComp f g).hom â‰« map f â— mapâ‚‚ Î· â‰« (mapComp f h).inv := by
      aesop_cat)
    (mapâ‚‚_whiskerRight :
        âˆ€ {a b c : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g) (h : b âŸ¶ c),
          mapâ‚‚ (Î· â–· h) =
          (mapComp f h).hom â‰« mapâ‚‚ Î· â–· map h â‰« (mapComp g h).inv := by
      aesop_cat)
    (mapâ‚‚_left_unitor :
        âˆ€ {a b : B} (f : a âŸ¶ b),
          mapâ‚‚ (Î»_ f).hom =
          (mapComp (ğŸ™ a) f).hom â‰« eqToHom (by rw [map_id a]) â‰«
            (Î»_ (map f)).hom := by
      aesop_cat)
    (mapâ‚‚_right_unitor :
        âˆ€ {a b : B} (f : a âŸ¶ b),
          mapâ‚‚ (Ï_ f).hom =
          (mapComp f (ğŸ™ b)).hom â‰« eqToHom (by rw [map_id b]) â‰«
            (Ï_ (map f)).hom := by
      aesop_cat)
    (mapâ‚‚_associator :
        âˆ€ {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d),
          mapâ‚‚ (Î±_ f g h).hom =
            (mapComp (f â‰« g) h).hom â‰« (mapComp f g).hom â–· map h â‰«
            (Î±_ (map f) (map g) (map h)).hom â‰« map f â— (mapComp g h).inv â‰«
            (mapComp f (g â‰« h)).inv := by
      aesop_cat) :
    StrictlyUnitaryPseudofunctor B C where
  obj := obj
  map := map
  map_id := map_id
  mapId x := eqToIso (map_id x)
  mapId_eq_eqToIso x := rfl
  mapâ‚‚ := mapâ‚‚
  mapâ‚‚_id := mapâ‚‚_id
  mapâ‚‚_comp := mapâ‚‚_comp
  mapComp := mapComp
  mapâ‚‚_left_unitor {a b} f := by
    simpa using mapâ‚‚_left_unitor f
  mapâ‚‚_right_unitor {a b} f := by
    simpa using mapâ‚‚_right_unitor f

/-- By forgetting the inverse to `mapComp`, a `StrictlyUnitaryPseudofunctor`
is a `StrictlyUnitaryLaxFunctor`. -/
def toStrictlyUnitaryLaxFunctor (F : StrictlyUnitaryPseudofunctor B C) :
    StrictlyUnitaryLaxFunctor B C where
  __ := F.toPseudofunctor.toLax
  map_id x := by simp [F.map_id]
  mapId_eq_eqToHom x := by simp [F.mapId_eq_eqToIso]

section

variable (F : StrictlyUnitaryPseudofunctor B C)

@[simp]
lemma toStrictlyUnitaryLaxFunctor_obj (x : B) :
    F.toStrictlyUnitaryLaxFunctor.obj x = F.obj x := rfl

@[simp]
lemma toStrictlyUnitaryLaxFunctor_map {x y : B} (f : x âŸ¶ y) :
    F.toStrictlyUnitaryLaxFunctor.map f = F.map f := rfl

@[simp]
lemma toStrictlyUnitaryLaxFunctor_mapâ‚‚ {x y : B} {f g : x âŸ¶ y} (Î· : f âŸ¶ g) :
    F.toStrictlyUnitaryLaxFunctor.mapâ‚‚ Î· = F.mapâ‚‚ Î· := rfl

@[simp]
lemma toStrictlyUnitaryLaxFunctor_mapComp {x y z : B} (f : x âŸ¶ y) (g : y âŸ¶ z) :
    F.toStrictlyUnitaryLaxFunctor.mapComp f g = (F.mapComp f g).inv := rfl

@[simp]
lemma toStrictlyUnitaryLaxFunctor_mapId {x : B} :
    F.toStrictlyUnitaryLaxFunctor.mapId x = (F.mapId x).inv := rfl

variable (B) in
/-- The identity `StrictlyUnitaryLaxFunctor`. -/
@[simps!]
def id : StrictlyUnitaryPseudofunctor B B where
  __ := Pseudofunctor.id B
  map_id _ := rfl
  mapId_eq_eqToIso _ := rfl

/-- Composition of `StrictlyUnitaryLaxFunctor`. -/
@[simps!]
def comp (F : StrictlyUnitaryPseudofunctor B C)
    (G : StrictlyUnitaryPseudofunctor C D) :
    StrictlyUnitaryPseudofunctor B D where
  __ := Pseudofunctor.comp F.toPseudofunctor G.toPseudofunctor
  map_id _ := by simp [StrictlyUnitaryPseudofunctor.map_id]
  mapId_eq_eqToIso _ := by
    ext
    simp [StrictlyUnitaryPseudofunctor.mapId_eq_eqToIso,
      PrelaxFunctor.mapâ‚‚_eqToHom]

end

end StrictlyUnitaryPseudofunctor

end CategoryTheory
