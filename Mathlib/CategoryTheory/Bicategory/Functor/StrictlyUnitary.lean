/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor

/-!
# Strictly unitary lax functors and pseudofunctors

In this file, we define strictly unitary Lax functors and
strictly unitary pseudofunctors between bicategories.

A lax functor `F` is said to be *strictly unitary* (sometimes, they are also
called *normal*) if there is an equality `F.obj (ğŸ™ _) = ğŸ™ (F.obj x)` and if the
unit 2-morphism `F.obj (ğŸ™ _) â†’ ğŸ™ (F.obj _)` is the identity 2-morphism induced
by this equality.

A pseudofunctor is called *strictly unitary* (or a *normal homomorphism*) if it
satisfies the same condition (i.e its "underlying" lax functor is strictly
unitary).

## References
- [Kerodon, section 2.2.2.4](https://kerodon.net/tag/008G)

## TODOs
* Define lax-composable (resp. pseudo-composable) arrows as strictly unitary
lax (resp. pseudo-) functors out of `LocallyDiscrete Fin n`.
* Define identity-component oplax natural transformations ("icons") between
strictly unitary pseudofunctors and construct a bicategory structure on
bicategories, strictly unitary pseudofunctors and icons.
* Construct the Duskin of a bicategory using lax-composable arrows
* Construct the 2-nerve of a bicategory using pseudo-composable arrows

-/

namespace CategoryTheory

open Category Bicategory

open Bicategory

universe wâ‚ wâ‚‚ wâ‚ƒ wâ‚„ vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ uâ‚ uâ‚‚ uâ‚ƒ uâ‚„

variable {B : Type uâ‚} [Bicategory.{wâ‚, vâ‚} B]
  {C : Type uâ‚‚} [Bicategory.{wâ‚‚, vâ‚‚} C]
  {D : Type uâ‚ƒ} [Bicategory.{wâ‚ƒ, vâ‚ƒ} D]

variable (B C)

/-- A strictly unitary lax functor `F` between bicategories `B` and `C` is a
lax functor `F` from `B` to `C` such that the structure 1-cell
`ğŸ™ (obj X) âŸ¶ map (ğŸ™ X)` is in fact an identity 1-cell for every `X : B`. -/
@[kerodon 008R]
structure StrictlyUnitaryLaxFunctor extends LaxFunctor B C where
  map_id (X : B) : map (ğŸ™ X) = ğŸ™ (obj X)
  mapId_eq_eqToHom (X : B) : (mapId X) = eqToHom (map_id X).symm

/-- A helper structure that bundles the necessary data to
construct a `StrictlyUnitaryLaxFunctor` without specifying the redundant
field `mapId`. -/
structure StrictlyUnitaryLaxFunctorCore where
  /-- action on objects -/
  obj : B â†’ C
  /-- action on 1-morphisms -/
  map : âˆ€ {X Y : B}, (X âŸ¶ Y) â†’ (obj X âŸ¶ obj Y)
  map_id : âˆ€ (X : B), map (ğŸ™ X) = ğŸ™ (obj X)
  /-- action on 2-morphisms -/
  mapâ‚‚ : âˆ€ {a b : B} {f g : a âŸ¶ b}, (f âŸ¶ g) â†’ (map f âŸ¶ map g)
  mapâ‚‚_id : âˆ€ {a b : B} (f : a âŸ¶ b), mapâ‚‚ (ğŸ™ f) = ğŸ™ (map f) := by aesop_cat
  mapâ‚‚_comp :
      âˆ€ {a b : B} {f g h : a âŸ¶ b} (Î· : f âŸ¶ g) (Î¸ : g âŸ¶ h),
        mapâ‚‚ (Î· â‰« Î¸) = mapâ‚‚ Î· â‰« mapâ‚‚ Î¸ := by
    aesop_cat
  /-- structure 2-morphism for composition of 1-morphism -/
  mapComp : âˆ€ {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c),
    map f â‰« map g âŸ¶ map (f â‰« g)
  mapComp_naturality_left :
      âˆ€ {a b c : B} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') (g : b âŸ¶ c),
        mapComp f g â‰« mapâ‚‚ (Î· â–· g) = mapâ‚‚ Î· â–· map g â‰« mapComp f' g := by
    aesop_cat
  mapComp_naturality_right :
      âˆ€ {a b c : B} (f : a âŸ¶ b) {g g' : b âŸ¶ c} (Î· : g âŸ¶ g'),
        mapComp f g â‰« mapâ‚‚ (f â— Î·) = map f â— mapâ‚‚ Î· â‰« mapComp f g' := by
    aesop_cat
  mapâ‚‚_leftUnitor :
      âˆ€ {a b : B} (f : a âŸ¶ b),
        mapâ‚‚ (Î»_ f).inv =
        (Î»_ (map f)).inv â‰« eqToHom (by rw [map_id a]) â‰« mapComp (ğŸ™ a) f := by
    aesop_cat
  mapâ‚‚_rightUnitor :
      âˆ€ {a b : B} (f : a âŸ¶ b),
        mapâ‚‚ (Ï_ f).inv =
        (Ï_ (map f)).inv â‰« eqToHom (by rw [map_id b]) â‰« mapComp f (ğŸ™ b) := by
    aesop_cat
  mapâ‚‚_associator :
      âˆ€ {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d),
        mapComp f g â–· map h â‰« mapComp (f â‰« g) h â‰« mapâ‚‚ (Î±_ f g h).hom =
        (Î±_ (map f) (map g) (map h)).hom â‰« map f â— mapComp g h â‰«
          mapComp f (g â‰« h) := by
    aesop_cat

namespace StrictlyUnitaryLaxFunctor

variable {B C}

/-- An alternate constructor for strictly unitary lax functors that does not
require the `mapId` fields, and that adapts the `mapâ‚‚_leftUnitor` and
`mapâ‚‚_rightUnitor` to the fact that the functor is strictly unitary. -/
@[simps]
def mk' (S : StrictlyUnitaryLaxFunctorCore B C) :
    StrictlyUnitaryLaxFunctor B C where
  obj := S.obj
  map := S.map
  map_id := S.map_id
  mapId x := eqToHom (S.map_id x).symm
  mapId_eq_eqToHom x := rfl
  mapâ‚‚ := S.mapâ‚‚
  mapâ‚‚_id := S.mapâ‚‚_id
  mapâ‚‚_comp := S.mapâ‚‚_comp
  mapComp := S.mapComp
  mapComp_naturality_left := S.mapComp_naturality_left
  mapComp_naturality_right := S.mapComp_naturality_right
  mapâ‚‚_leftUnitor f := by
    simpa using S.mapâ‚‚_leftUnitor f
  mapâ‚‚_rightUnitor f := by
    simpa using S.mapâ‚‚_rightUnitor f
  mapâ‚‚_associator f g h := by
    simpa using S.mapâ‚‚_associator f g h

instance mapId_isIso (F : StrictlyUnitaryLaxFunctor B C) (x : B) :
    IsIso (F.mapId x) := by
  rw [mapId_eq_eqToHom]
  infer_instance

/-- Promote the morphism `F.mapId x : ğŸ™ (F.obj x) âŸ¶ F.map (ğŸ™ x)`
to an isomorphism when `F` is strictly unitary. -/
@[simps]
def mapIdIso (F : StrictlyUnitaryLaxFunctor B C) (x : B) :
   ğŸ™ (F.obj x) â‰… F.map (ğŸ™ x) where
  hom := F.mapId x
  inv := eqToHom (F.map_id x)
  hom_inv_id := by simp [F.mapId_eq_eqToHom]
  inv_hom_id := by simp [F.mapId_eq_eqToHom]

variable (B) in
/-- The identity `StrictlyUnitaryLaxFunctor`. -/
@[simps!]
def id : StrictlyUnitaryLaxFunctor B B where
  __ := LaxFunctor.id B
  map_id _ := rfl
  mapId_eq_eqToHom _ := rfl

/-- Composition of `StrictlyUnitaryLaxFunctor`. -/
@[simps!]
def comp (F : StrictlyUnitaryLaxFunctor B C)
    (G : StrictlyUnitaryLaxFunctor C D) :
    StrictlyUnitaryLaxFunctor B D where
  __ := LaxFunctor.comp F.toLaxFunctor G.toLaxFunctor
  map_id _ := by simp [StrictlyUnitaryLaxFunctor.map_id]
  mapId_eq_eqToHom _ := by
    simp [StrictlyUnitaryLaxFunctor.mapId_eq_eqToHom,
      PrelaxFunctor.mapâ‚‚_eqToHom]

section
attribute [local ext] StrictlyUnitaryLaxFunctor

/-- Composition of `StrictlyUnitaryLaxFunctor` is strictly right unitary -/
lemma comp_id (F : StrictlyUnitaryLaxFunctor B C) :
    F.comp (.id C) = F := by
  ext
  Â· simp
  all_goals
  Â· rw [heq_iff_eq]
    ext
    simp

/-- Composition of `StrictlyUnitaryLaxFunctor` is strictly left unitary -/
lemma id_comp (F : StrictlyUnitaryLaxFunctor B C) :
    (StrictlyUnitaryLaxFunctor.id B).comp F = F := by
  ext
  Â· simp
  all_goals
  Â· rw [heq_iff_eq]
    ext
    simp

/-- Composition of `StrictlyUnitaryLaxFunctor` is strictly associative -/
lemma comp_assoc {E : Type uâ‚„} [Bicategory.{wâ‚„, vâ‚„} E]
    (F : StrictlyUnitaryLaxFunctor B C) (G : StrictlyUnitaryLaxFunctor C D)
    (H : StrictlyUnitaryLaxFunctor D E) :
    (F.comp G).comp H = F.comp (G.comp H) := by
  ext
  Â· simp
  all_goals
  Â· rw [heq_iff_eq]
    ext
    simp

end

end StrictlyUnitaryLaxFunctor

/-- A strictly unitary pseudofunctor (sometimes called a "normal homomorphism)
`F` between bicategories `B` and `C` is a lax functor `F` from `B` to `C`
such that the structure isomorphism `map (ğŸ™ X) â‰… ğŸ™ (F.obj X)` is in fact an
identity 1-cell for every `X : B` (in particular, there is an equality
`F.map (ğŸ™ X) = ğŸ™ (F.obj x)`). -/
@[kerodon 008R]
structure StrictlyUnitaryPseudofunctor extends Pseudofunctor B C where
  map_id (X : B) : map (ğŸ™ X) = ğŸ™ (obj X)
  mapId_eq_eqToIso (X : B) : (mapId X) = eqToIso (map_id X)

/-- A helper structure that bundles the necessary data to
construct a `StrictlyUnitaryPseudofunctor` without specifying the redundant
field `mapId` -/
structure StrictlyUnitaryPseudofunctorCore where
  /-- action on objects -/
  obj : B â†’ C
  /-- action on 1-morphisms -/
  map : âˆ€ {X Y : B}, (X âŸ¶ Y) â†’ (obj X âŸ¶ obj Y)
  map_id : âˆ€ (X : B), map (ğŸ™ X) = ğŸ™ (obj X)
  /-- action on 2-morphisms -/
  mapâ‚‚ : âˆ€ {a b : B} {f g : a âŸ¶ b}, (f âŸ¶ g) â†’ (map f âŸ¶ map g)
  mapâ‚‚_id : âˆ€ {a b : B} (f : a âŸ¶ b), mapâ‚‚ (ğŸ™ f) = ğŸ™ (map f) := by aesop_cat
  mapâ‚‚_comp :
      âˆ€ {a b : B} {f g h : a âŸ¶ b} (Î· : f âŸ¶ g) (Î¸ : g âŸ¶ h),
        mapâ‚‚ (Î· â‰« Î¸) = mapâ‚‚ Î· â‰« mapâ‚‚ Î¸ := by
    aesop_cat
  /-- structure 2-isomorphism for composition of 1-morphisms -/
  mapComp : âˆ€ {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c),
    map (f â‰« g) â‰… map f â‰« map g
  mapâ‚‚_whisker_left :
      âˆ€ {a b c : B} (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g âŸ¶ h),
        mapâ‚‚ (f â— Î·) =
        (mapComp f g).hom â‰« map f â— mapâ‚‚ Î· â‰« (mapComp f h).inv := by
    aesop_cat
  mapâ‚‚_whisker_right :
      âˆ€ {a b c : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g) (h : b âŸ¶ c),
        mapâ‚‚ (Î· â–· h) =
        (mapComp f h).hom â‰« mapâ‚‚ Î· â–· map h â‰« (mapComp g h).inv := by
    aesop_cat
  mapâ‚‚_left_unitor :
      âˆ€ {a b : B} (f : a âŸ¶ b),
        mapâ‚‚ (Î»_ f).hom =
        (mapComp (ğŸ™ a) f).hom â‰« eqToHom (by rw [map_id a]) â‰«
          (Î»_ (map f)).hom := by
    aesop_cat
  mapâ‚‚_right_unitor :
      âˆ€ {a b : B} (f : a âŸ¶ b),
        mapâ‚‚ (Ï_ f).hom =
        (mapComp f (ğŸ™ b)).hom â‰« eqToHom (by rw [map_id b]) â‰«
          (Ï_ (map f)).hom := by
    aesop_cat
  mapâ‚‚_associator :
      âˆ€ {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d),
        mapâ‚‚ (Î±_ f g h).hom =
          (mapComp (f â‰« g) h).hom â‰« (mapComp f g).hom â–· map h â‰«
          (Î±_ (map f) (map g) (map h)).hom â‰« map f â— (mapComp g h).inv â‰«
          (mapComp f (g â‰« h)).inv := by
    aesop_cat

namespace StrictlyUnitaryPseudofunctor

variable {B C}

/-- An alternate constructor for strictly unitary lax functors that does not
require the `mapId` fields, and that adapts the `mapâ‚‚_leftUnitor` and
`mapâ‚‚_rightUnitor` to the fact that the functor is strictly unitary. -/
@[simps]
def mk' (S : StrictlyUnitaryPseudofunctorCore B C) :
    StrictlyUnitaryPseudofunctor B C where
  obj := S.obj
  map := S.map
  map_id := S.map_id
  mapId x := eqToIso (S.map_id x)
  mapId_eq_eqToIso x := rfl
  mapâ‚‚ := S.mapâ‚‚
  mapâ‚‚_id := S.mapâ‚‚_id
  mapâ‚‚_comp := S.mapâ‚‚_comp
  mapComp := S.mapComp
  mapâ‚‚_left_unitor f := by
    simpa using S.mapâ‚‚_left_unitor f
  mapâ‚‚_right_unitor f := by
    simpa using S.mapâ‚‚_right_unitor f
  mapâ‚‚_associator f g h := by
    simpa using S.mapâ‚‚_associator f g h
  mapâ‚‚_whisker_left f _ _ Î· := by
    simpa using S.mapâ‚‚_whisker_left f Î·
  mapâ‚‚_whisker_right Î· f := by
    simpa using S.mapâ‚‚_whisker_right Î· f

/-- By forgetting the inverse to `mapComp`, a `StrictlyUnitaryPseudofunctor`
is a `StrictlyUnitaryLaxFunctor`. -/
def toStrictlyUnitaryLaxFunctor (F : StrictlyUnitaryPseudofunctor B C) :
    StrictlyUnitaryLaxFunctor B C where
  __ := F.toPseudofunctor.toLax
  map_id x := by simp [F.map_id]
  mapId_eq_eqToHom x := by simp [F.mapId_eq_eqToIso]

section

variable (F : StrictlyUnitaryPseudofunctor B C)

@[simp]
lemma toStrictlyUnitaryLaxFunctor_obj (x : B) :
    F.toStrictlyUnitaryLaxFunctor.obj x = F.obj x :=
  rfl

@[simp]
lemma toStrictlyUnitaryLaxFunctor_map {x y : B} (f : x âŸ¶ y) :
    F.toStrictlyUnitaryLaxFunctor.map f = F.map f :=
  rfl

@[simp]
lemma toStrictlyUnitaryLaxFunctor_mapâ‚‚ {x y : B} {f g : x âŸ¶ y} (Î· : f âŸ¶ g) :
    F.toStrictlyUnitaryLaxFunctor.mapâ‚‚ Î· = F.mapâ‚‚ Î· :=
  rfl

@[simp]
lemma toStrictlyUnitaryLaxFunctor_mapComp {x y z : B} (f : x âŸ¶ y) (g : y âŸ¶ z) :
    F.toStrictlyUnitaryLaxFunctor.mapComp f g = (F.mapComp f g).inv :=
  rfl

@[simp]
lemma toStrictlyUnitaryLaxFunctor_mapId {x : B} :
    F.toStrictlyUnitaryLaxFunctor.mapId x = (F.mapId x).inv :=
  rfl

variable (B) in
/-- The identity `StrictlyUnitaryPseudofunctor`. -/
@[simps!]
def id : StrictlyUnitaryPseudofunctor B B where
  __ := Pseudofunctor.id B
  map_id _ := rfl
  mapId_eq_eqToIso _ := rfl

/-- Composition of `StrictlyUnitaryPseudofunctor`. -/
@[simps!]
def comp (F : StrictlyUnitaryPseudofunctor B C)
    (G : StrictlyUnitaryPseudofunctor C D) :
    StrictlyUnitaryPseudofunctor B D where
  __ := Pseudofunctor.comp F.toPseudofunctor G.toPseudofunctor
  map_id _ := by simp [StrictlyUnitaryPseudofunctor.map_id]
  mapId_eq_eqToIso _ := by
    ext
    simp [StrictlyUnitaryPseudofunctor.mapId_eq_eqToIso,
      PrelaxFunctor.mapâ‚‚_eqToHom]

end

end StrictlyUnitaryPseudofunctor

end CategoryTheory
