/-
Copyright (c) 2022 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import Mathlib.CategoryTheory.Bicategory.Functor.Prelax
import Mathlib.Tactic.CategoryTheory.ToApp

/-!
# Oplax functors

An oplax functor `F` between bicategories `B` and `C` consists of
* a function between objects `F.obj : B âŸ¶ C`,
* a family of functions between 1-morphisms `F.map : (a âŸ¶ b) â†’ (F.obj a âŸ¶ F.obj b)`,
* a family of functions between 2-morphisms `F.mapâ‚‚ : (f âŸ¶ g) â†’ (F.map f âŸ¶ F.map g)`,
* a family of 2-morphisms `F.mapId a : F.map (ğŸ™ a) âŸ¶ ğŸ™ (F.obj a)`,
* a family of 2-morphisms `F.mapComp f g : F.map (f â‰« g) âŸ¶ F.map f â‰« F.map g`, and
* certain consistency conditions on them.

## Main definitions

* `CategoryTheory.OplaxFunctor B C` : an oplax functor between bicategories `B` and `C`
* `CategoryTheory.OplaxFunctor.comp F G` : the composition of oplax functors

-/

namespace CategoryTheory

open Category Bicategory

open Bicategory

universe wâ‚ wâ‚‚ wâ‚ƒ vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

section

variable {B : Type uâ‚} [Bicategory.{wâ‚, vâ‚} B] {C : Type uâ‚‚} [Bicategory.{wâ‚‚, vâ‚‚} C]
variable {D : Type uâ‚ƒ} [Bicategory.{wâ‚ƒ, vâ‚ƒ} D]

/-- An oplax functor `F` between bicategories `B` and `C` consists of a function between objects
`F.obj`, a function between 1-morphisms `F.map`, and a function between 2-morphisms `F.mapâ‚‚`.

Unlike functors between categories, `F.map` do not need to strictly commute with the composition,
and do not need to strictly preserve the identity. Instead, there are specified 2-morphisms
`F.map (ğŸ™ a) âŸ¶ ğŸ™ (F.obj a)` and `F.map (f â‰« g) âŸ¶ F.map f â‰« F.map g`.

`F.mapâ‚‚` strictly commute with compositions and preserve the identity. They also preserve the
associator, the left unitor, and the right unitor modulo some adjustments of domains and codomains
of 2-morphisms.
-/
structure OplaxFunctor (B : Type uâ‚) [Bicategory.{wâ‚, vâ‚} B] (C : Type uâ‚‚)
  [Bicategory.{wâ‚‚, vâ‚‚} C] extends PrelaxFunctor B C where
  /-- The 2-morphism underlying the oplax unity constraint. -/
  mapId (a : B) : map (ğŸ™ a) âŸ¶ ğŸ™ (obj a)
  /-- The 2-morphism underlying the oplax functoriality constraint. -/
  mapComp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) : map (f â‰« g) âŸ¶ map f â‰« map g
  /-- Naturality of the oplax functoriality constraint, on the left. -/
  mapComp_naturality_left :
    âˆ€ {a b c : B} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') (g : b âŸ¶ c),
      mapâ‚‚ (Î· â–· g) â‰« mapComp f' g = mapComp f g â‰« mapâ‚‚ Î· â–· map g := by
    cat_disch
  /-- Naturality of the lax functoriality constraint, on the right. -/
  mapComp_naturality_right :
    âˆ€ {a b c : B} (f : a âŸ¶ b) {g g' : b âŸ¶ c} (Î· : g âŸ¶ g'),
      mapâ‚‚ (f â— Î·) â‰« mapComp f g' = mapComp f g â‰« map f â— mapâ‚‚ Î· := by
    cat_disch
  /-- Oplax associativity. -/
  mapâ‚‚_associator :
    âˆ€ {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d),
      mapâ‚‚ (Î±_ f g h).hom â‰« mapComp f (g â‰« h) â‰« map f â— mapComp g h =
      mapComp (f â‰« g) h â‰« mapComp f g â–· map h â‰« (Î±_ (map f) (map g) (map h)).hom := by
    cat_disch
  /-- Oplax left unity. -/
  mapâ‚‚_leftUnitor :
    âˆ€ {a b : B} (f : a âŸ¶ b),
      mapâ‚‚ (Î»_ f).hom = mapComp (ğŸ™ a) f â‰« mapId a â–· map f â‰« (Î»_ (map f)).hom := by
    cat_disch
  /-- Oplax right unity. -/
  mapâ‚‚_rightUnitor :
    âˆ€ {a b : B} (f : a âŸ¶ b),
      mapâ‚‚ (Ï_ f).hom = mapComp f (ğŸ™ b) â‰« map f â— mapId b â‰« (Ï_ (map f)).hom := by
    cat_disch

initialize_simps_projections OplaxFunctor (+toPrelaxFunctor, -obj, -map, -mapâ‚‚)

namespace OplaxFunctor

attribute [reassoc (attr := simp), to_app (attr := simp)]
  mapComp_naturality_left mapComp_naturality_right mapâ‚‚_associator
attribute [simp, reassoc, to_app] mapâ‚‚_leftUnitor mapâ‚‚_rightUnitor

section

/-- The underlying prelax functor. -/
add_decl_doc OplaxFunctor.toPrelaxFunctor

variable (F : OplaxFunctor B C)

@[reassoc, to_app]
lemma mapComp_assoc_right {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) :
    F.mapComp f (g â‰« h) â‰« F.map f â— F.mapComp g h = F.mapâ‚‚ (Î±_ f g h).inv â‰«
    F.mapComp (f â‰« g) h â‰« F.mapComp f g â–· F.map h â‰«
    (Î±_ (F.map f) (F.map g) (F.map h)).hom := by
  rw [â† F.mapâ‚‚_associator, â† F.mapâ‚‚_comp_assoc]
  simp

@[reassoc, to_app]
lemma mapComp_assoc_left {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) :
    F.mapComp (f â‰« g) h â‰« F.mapComp f g â–· F.map h =
    F.mapâ‚‚ (Î±_ f g h).hom â‰« F.mapComp f (g â‰« h) â‰« F.map f â— F.mapComp g h
    â‰« (Î±_ (F.map f) (F.map g) (F.map h)).inv := by
  simp

@[reassoc]
theorem mapComp_id_left {a b : B} (f : a âŸ¶ b) :
    F.mapComp (ğŸ™ a) f â‰« F.mapId a â–· F.map f = F.mapâ‚‚ (Î»_ f).hom â‰« (Î»_ (F.map f)).inv := by
  rw [Iso.eq_comp_inv]
  simp only [Category.assoc]
  rw [â† F.mapâ‚‚_leftUnitor]

@[reassoc]
theorem mapComp_id_right {a b : B} (f : a âŸ¶ b) :
    F.mapComp f (ğŸ™ b) â‰« F.map f â— F.mapId b = F.mapâ‚‚ (Ï_ f).hom â‰« (Ï_ (F.map f)).inv := by
  rw [Iso.eq_comp_inv]
  simp only [Category.assoc]
  rw [â† F.mapâ‚‚_rightUnitor]

/-- The identity oplax functor. -/
@[simps]
def id (B : Type uâ‚) [Bicategory.{wâ‚, vâ‚} B] : OplaxFunctor B B where
  toPrelaxFunctor := PrelaxFunctor.id B
  mapId := fun a â†¦ ğŸ™ (ğŸ™ a)
  mapComp := fun f g â†¦ ğŸ™ (f â‰« g)

instance : Inhabited (OplaxFunctor B B) :=
  âŸ¨id BâŸ©

/-- Composition of oplax functors. -/
--@[simps]
def comp (F : OplaxFunctor B C) (G : OplaxFunctor C D) : OplaxFunctor B D where
  toPrelaxFunctor := F.toPrelaxFunctor.comp G.toPrelaxFunctor
  mapId := fun a â†¦ (G.mapFunctor _ _).map (F.mapId a) â‰« G.mapId (F.obj a)
  mapComp := fun f g â†¦ (G.mapFunctor _ _).map (F.mapComp f g) â‰« G.mapComp (F.map f) (F.map g)
  mapComp_naturality_left := fun Î· g â†¦ by
    dsimp
    rw [â† G.mapâ‚‚_comp_assoc, mapComp_naturality_left, G.mapâ‚‚_comp_assoc, mapComp_naturality_left,
      assoc]
  mapComp_naturality_right := fun Î· â†¦ by
    dsimp
    intros
    rw [â† G.mapâ‚‚_comp_assoc, mapComp_naturality_right, G.mapâ‚‚_comp_assoc,
      mapComp_naturality_right, assoc]
  mapâ‚‚_associator := fun f g h â†¦ by
    dsimp
    simp only [mapâ‚‚_associator, â† PrelaxFunctor.mapâ‚‚_comp_assoc, â† mapComp_naturality_right_assoc,
      whiskerLeft_comp, assoc]
    simp only [mapâ‚‚_associator, PrelaxFunctor.mapâ‚‚_comp, mapComp_naturality_left_assoc,
      comp_whiskerRight, assoc]
  mapâ‚‚_leftUnitor := fun f â†¦ by
    dsimp
    simp only [mapâ‚‚_leftUnitor, PrelaxFunctor.mapâ‚‚_comp, mapComp_naturality_left_assoc,
      comp_whiskerRight, assoc]
  mapâ‚‚_rightUnitor := fun f â†¦ by
    dsimp
    simp only [mapâ‚‚_rightUnitor, PrelaxFunctor.mapâ‚‚_comp, mapComp_naturality_right_assoc,
      whiskerLeft_comp, assoc]

/-- A structure on an oplax functor that promotes an oplax functor to a pseudofunctor.

See `Pseudofunctor.mkOfOplax`. -/
structure PseudoCore (F : OplaxFunctor B C) where
  /-- The isomorphism giving rise to the oplax unity constraint -/
  mapIdIso (a : B) : F.map (ğŸ™ a) â‰… ğŸ™ (F.obj a)
  /-- The isomorphism giving rise to the oplax functoriality constraint -/
  mapCompIso {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) : F.map (f â‰« g) â‰… F.map f â‰« F.map g
  /-- `mapIdIso` gives rise to the oplax unity constraint -/
  mapIdIso_hom : âˆ€ {a : B}, (mapIdIso a).hom = F.mapId a := by cat_disch
  /-- `mapCompIso` gives rise to the oplax functoriality constraint -/
  mapCompIso_hom :
    âˆ€ {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c), (mapCompIso f g).hom = F.mapComp f g := by cat_disch

attribute [simp] PseudoCore.mapIdIso_hom PseudoCore.mapCompIso_hom

end

end OplaxFunctor

end

end CategoryTheory
