/-
Copyright (c) 2025 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou, Christian Merten
-/
import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor
import Mathlib.CategoryTheory.CommSq

/-!
# Pseudofunctors from strict bicategory

This file provides an API for pseudofunctors `F` from a strict bicategory `B`. In
particular, this shall apply to pseudofunctors from locally discrete bicategories.

We first introduce more flexible variants of `mapId` and `mapComp`: for example,
if `f` and `g` are composable morphisms and `fg` is such that `h : fg = f â‰« f`,
we provide an isomorphism `F.mapComp' f g fg h : F.map fg â‰… F.map f â‰« F.map g`.
We study the compatibilities of these isomorphisms with respect to composition
with identities and associativity.

Secondly, given a commutative square `t â‰« r = l â‰« b` in `B`, we construct an
isomorphism `F.map t â‰« F.map r â‰… F.map l â‰« F.map b`
(see `Pseudofunctor.isoMapOfCommSq`).

-/

namespace CategoryTheory

open Bicategory

namespace Pseudofunctor

variable {B C : Type*} [Bicategory B] [Bicategory C] (F : Pseudofunctor B C)

/-- More flexible variant of `mapId`. -/
def mapId' {b : B} (f : b âŸ¶ b) (hf : f = ğŸ™ b := by aesop_cat) :
    F.map f â‰… ğŸ™ _ :=
  F.mapâ‚‚Iso (eqToIso (by rw [hf])) â‰ªâ‰« F.mapId _

lemma mapId'_eq_mapId (b : B) :
    F.mapId' (ğŸ™ b) rfl = F.mapId b := by
  simp [mapId']

/-- More flexible variant of `mapComp`. -/
def mapComp' {bâ‚€ bâ‚ bâ‚‚ : B} (f : bâ‚€ âŸ¶ bâ‚) (g : bâ‚ âŸ¶ bâ‚‚) (fg : bâ‚€ âŸ¶ bâ‚‚)
    (h : f â‰« g = fg := by aesop_cat) :
    F.map fg â‰… F.map f â‰« F.map g :=
  F.mapâ‚‚Iso (eqToIso (by rw [h])) â‰ªâ‰« F.mapComp f g

lemma mapComp'_eq_mapComp {bâ‚€ bâ‚ bâ‚‚ : B} (f : bâ‚€ âŸ¶ bâ‚) (g : bâ‚ âŸ¶ bâ‚‚) :
    F.mapComp' f g _ rfl = F.mapComp f g := by
  simp [mapComp']

variable [Strict B]

lemma mapComp'_comp_id {bâ‚€ bâ‚ : B} (f : bâ‚€ âŸ¶ bâ‚) :
    F.mapComp' f (ğŸ™ bâ‚) f = (Ï_ _).symm â‰ªâ‰« whiskerLeftIso _ (F.mapId bâ‚).symm := by
  ext
  dsimp [mapComp']
  rw [F.mapComp_id_right_hom f, Strict.rightUnitor_eqToIso, eqToIso.hom,
    â† F.mapâ‚‚_comp_assoc, eqToHom_trans, eqToHom_refl, PrelaxFunctor.mapâ‚‚_id,
    Category.id_comp]

lemma mapComp'_id_comp {bâ‚€ bâ‚ : B} (f : bâ‚€ âŸ¶ bâ‚) :
    F.mapComp' (ğŸ™ bâ‚€) f f = (Î»_ _).symm â‰ªâ‰« whiskerRightIso (F.mapId bâ‚€).symm _ := by
  ext
  dsimp [mapComp']
  rw [F.mapComp_id_left_hom f, Strict.leftUnitor_eqToIso, eqToIso.hom,
    â† F.mapâ‚‚_comp_assoc, eqToHom_trans, eqToHom_refl, PrelaxFunctor.mapâ‚‚_id,
    Category.id_comp]

section associativity

variable {bâ‚€ bâ‚ bâ‚‚ bâ‚ƒ : B} (fâ‚€â‚ : bâ‚€ âŸ¶ bâ‚)
  (fâ‚â‚‚ : bâ‚ âŸ¶ bâ‚‚) (fâ‚‚â‚ƒ : bâ‚‚ âŸ¶ bâ‚ƒ) (fâ‚€â‚‚ : bâ‚€ âŸ¶ bâ‚‚) (fâ‚â‚ƒ : bâ‚ âŸ¶ bâ‚ƒ) (f : bâ‚€ âŸ¶ bâ‚ƒ)
  (hâ‚€â‚‚ : fâ‚€â‚ â‰« fâ‚â‚‚ = fâ‚€â‚‚) (hâ‚â‚ƒ : fâ‚â‚‚ â‰« fâ‚‚â‚ƒ = fâ‚â‚ƒ)

@[reassoc]
lemma mapComp'_hom_comp_whiskerLeft_mapComp'_hom (hf : fâ‚€â‚ â‰« fâ‚â‚ƒ = f) :
    (F.mapComp' fâ‚€â‚ fâ‚â‚ƒ f).hom â‰« F.map fâ‚€â‚ â— (F.mapComp' fâ‚â‚‚ fâ‚‚â‚ƒ fâ‚â‚ƒ hâ‚â‚ƒ).hom =
    (F.mapComp' fâ‚€â‚‚ fâ‚‚â‚ƒ f).hom â‰«
      (F.mapComp' fâ‚€â‚ fâ‚â‚‚ fâ‚€â‚‚ hâ‚€â‚‚).hom â–· F.map fâ‚‚â‚ƒ â‰« (Î±_ _ _ _).hom := by
  subst hâ‚€â‚‚ hâ‚â‚ƒ hf
  simp [mapComp_assoc_right_hom, Strict.associator_eqToIso, mapComp']

@[reassoc]
lemma mapComp'_inv_comp_mapComp'_hom (hf : fâ‚€â‚ â‰« fâ‚â‚ƒ = f) :
    (F.mapComp' fâ‚€â‚ fâ‚â‚ƒ f).inv â‰«
      (F.mapComp' fâ‚€â‚‚ fâ‚‚â‚ƒ f).hom =
    F.map fâ‚€â‚ â— (F.mapComp' fâ‚â‚‚ fâ‚‚â‚ƒ fâ‚â‚ƒ hâ‚â‚ƒ).hom â‰«
      (Î±_ _ _ _).inv â‰« (F.mapComp' fâ‚€â‚ fâ‚â‚‚ fâ‚€â‚‚ hâ‚€â‚‚).inv â–· F.map fâ‚‚â‚ƒ := by
  rw [â† cancel_epi (F.mapComp' fâ‚€â‚ fâ‚â‚ƒ f hf).hom, Iso.hom_inv_id_assoc,
    F.mapComp'_hom_comp_whiskerLeft_mapComp'_hom_assoc _ _ _ _ _ _ hâ‚€â‚‚ hâ‚â‚ƒ hf]
  simp

@[reassoc]
lemma whiskerLeft_mapComp'_inv_comp_mapComp'_inv (hf : fâ‚€â‚ â‰« fâ‚â‚ƒ = f) :
    F.map fâ‚€â‚ â— (F.mapComp' fâ‚â‚‚ fâ‚‚â‚ƒ fâ‚â‚ƒ hâ‚â‚ƒ).inv â‰« (F.mapComp' fâ‚€â‚ fâ‚â‚ƒ f hf).inv =
    (Î±_ _ _ _).inv â‰« (F.mapComp' fâ‚€â‚ fâ‚â‚‚ fâ‚€â‚‚ hâ‚€â‚‚).inv â–· F.map fâ‚‚â‚ƒ â‰«
      (F.mapComp' fâ‚€â‚‚ fâ‚‚â‚ƒ f).inv := by
  simp [â† cancel_mono (F.mapComp' fâ‚€â‚‚ fâ‚‚â‚ƒ f).hom,
    F.mapComp'_inv_comp_mapComp'_hom _ _ _ _ _ _ hâ‚€â‚‚ hâ‚â‚ƒ hf]

@[reassoc]
lemma mapComp'_hom_comp_mapComp'_hom_whiskerRight (hf : fâ‚€â‚‚ â‰« fâ‚‚â‚ƒ = f) :
    (F.mapComp' fâ‚€â‚‚ fâ‚‚â‚ƒ f).hom â‰« (F.mapComp' fâ‚€â‚ fâ‚â‚‚ fâ‚€â‚‚ hâ‚€â‚‚).hom â–· F.map fâ‚‚â‚ƒ =
    (F.mapComp' fâ‚€â‚ fâ‚â‚ƒ f).hom â‰« F.map fâ‚€â‚ â— (F.mapComp' fâ‚â‚‚ fâ‚‚â‚ƒ fâ‚â‚ƒ hâ‚â‚ƒ).hom â‰«
      (Î±_ _ _ _).inv := by
  rw [F.mapComp'_hom_comp_whiskerLeft_mapComp'_hom_assoc _ _ _ _ _ f hâ‚€â‚‚ hâ‚â‚ƒ (by aesop_cat)]
  simp

@[reassoc]
lemma mapComp'_inv_comp_mapComp'_hom' (hf : fâ‚€â‚‚ â‰« fâ‚‚â‚ƒ = f) :
    (F.mapComp' fâ‚€â‚ fâ‚â‚ƒ f).inv â‰« (F.mapComp' fâ‚€â‚‚ fâ‚‚â‚ƒ f).hom =
     F.map fâ‚€â‚ â— (F.mapComp' fâ‚â‚‚ fâ‚‚â‚ƒ fâ‚â‚ƒ hâ‚â‚ƒ).hom â‰«
      (Î±_ _ _ _).inv â‰« (F.mapComp' fâ‚€â‚ fâ‚â‚‚ fâ‚€â‚‚ hâ‚€â‚‚).inv â–· F.map fâ‚‚â‚ƒ:= by
  simp only [â† cancel_mono ((F.mapComp' fâ‚€â‚ fâ‚â‚‚ fâ‚€â‚‚ hâ‚€â‚‚).hom â–· F.map fâ‚‚â‚ƒ),
    mapComp'_hom_comp_mapComp'_hom_whiskerRight _ _ _ _ _ _ _ hâ‚€â‚‚ hâ‚â‚ƒ hf,
    Category.assoc, Iso.inv_hom_id_assoc, inv_hom_whiskerRight, Category.comp_id]

@[reassoc]
lemma mapComp'_inv_whiskerRight_comp_mapComp'_inv (hf : fâ‚€â‚‚ â‰« fâ‚‚â‚ƒ = f) :
    (F.mapComp' fâ‚€â‚ fâ‚â‚‚ fâ‚€â‚‚ hâ‚€â‚‚).inv â–· F.map fâ‚‚â‚ƒ â‰« (F.mapComp' fâ‚€â‚‚ fâ‚‚â‚ƒ f).inv =
    (Î±_ _ _ _).hom â‰« F.map fâ‚€â‚ â— (F.mapComp' fâ‚â‚‚ fâ‚‚â‚ƒ fâ‚â‚ƒ hâ‚â‚ƒ).inv â‰«
      (F.mapComp' fâ‚€â‚ fâ‚â‚ƒ f).inv := by
  rw [whiskerLeft_mapComp'_inv_comp_mapComp'_inv _ _ _ _ _ _ f hâ‚€â‚‚ hâ‚â‚ƒ,
    Iso.hom_inv_id_assoc]

end associativity

section CommSq

variable {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ Zâ‚ Zâ‚‚ : B}

section

variable {t : Xâ‚ âŸ¶ Yâ‚} {l : Xâ‚ âŸ¶ Xâ‚‚} {r : Yâ‚ âŸ¶ Yâ‚‚} {b : Xâ‚‚ âŸ¶ Yâ‚‚} (sq : CommSq t l r b)

/-- Given a commutative square `CommSq t l r b` in a strict bicategory `B` and
a pseudofunctor from `B`, this is the natural isomorphism
`F.map t â‰« F.map r â‰… F.map l â‰« F.map b`. -/
def isoMapOfCommSq : F.map t â‰« F.map r â‰… F.map l â‰« F.map b :=
  (F.mapComp t r).symm â‰ªâ‰« F.mapComp' _ _ _ (by rw [sq.w])

lemma isoMapOfCommSq_eq (Ï† : Xâ‚ âŸ¶ Yâ‚‚) (hÏ† : t â‰« r = Ï†) :
    F.isoMapOfCommSq sq =
    (F.mapComp' t r Ï† (by rw [hÏ†])).symm â‰ªâ‰«
      F.mapComp' l b Ï† (by rw [â† hÏ†, sq.w]) := by
  subst hÏ†
  simp [isoMapOfCommSq, mapComp'_eq_mapComp]

lemma isoMapOfCommSq_flip : F.isoMapOfCommSq sq.flip =
    (F.isoMapOfCommSq sq).symm := by
  rw [F.isoMapOfCommSq_eq sq.flip (t â‰« r) sq.w.symm,
    F.isoMapOfCommSq_eq sq (t â‰« r) rfl,
    Iso.trans_symm, Iso.symm_symm_eq]

end

/-- Equational lemma for `Pseudofunctor.isoMapOfCommSq` when
both vertical maps of the square are the same and horizontal maps are identities. -/
lemma isoMapOfCommSq_horiz_id (f : Xâ‚ âŸ¶ Xâ‚‚) :
    F.isoMapOfCommSq (t := ğŸ™ _) (l := f) (r := f) (b := ğŸ™ _) âŸ¨by simpâŸ© =
      whiskerRightIso (F.mapId Xâ‚) (F.map f) â‰ªâ‰« Î»_ _  â‰ªâ‰« (Ï_ _).symm â‰ªâ‰«
        (whiskerLeftIso (F.map f) (F.mapId Xâ‚‚)).symm := by
  ext
  rw [isoMapOfCommSq_eq _ _ f (by simp), mapComp'_comp_id, mapComp'_id_comp]
  simp

/-- Equational lemma for `Pseudofunctor.isoMapOfCommSq` when
both horizontal maps of the square are the same and vertical maps are identities. -/
lemma isoMapOfCommSq_vert_id (f : Xâ‚ âŸ¶ Xâ‚‚) :
    F.isoMapOfCommSq (t := f)  (l := ğŸ™ _) (r := ğŸ™ _) (b := f) âŸ¨by simpâŸ© =
      whiskerLeftIso (F.map f) (F.mapId Xâ‚‚) â‰ªâ‰« Ï_ _ â‰ªâ‰« (Î»_ _).symm â‰ªâ‰«
        (whiskerRightIso (F.mapId Xâ‚) (F.map f)).symm := by
  ext
  rw [isoMapOfCommSq_eq _ _ f (by simp), mapComp'_comp_id, mapComp'_id_comp]
  simp

lemma isoMapOfCommSq_horiz_comp
    {t : Xâ‚ âŸ¶ Yâ‚} {t' : Yâ‚ âŸ¶ Zâ‚} {l : Xâ‚ âŸ¶ Xâ‚‚} {m : Yâ‚ âŸ¶ Yâ‚‚} {r : Zâ‚ âŸ¶ Zâ‚‚}
    {b : Xâ‚‚ âŸ¶ Yâ‚‚} {b' : Yâ‚‚ âŸ¶ Zâ‚‚} (sq : CommSq t l m b) (sq' : CommSq t' m r b')
    {t'' : Xâ‚ âŸ¶ Zâ‚} {b'' : Xâ‚‚ âŸ¶ Zâ‚‚} (ht : t â‰« t' = t'') (hb : b â‰« b' = b'') :
    F.isoMapOfCommSq (sq.horiz_comp' sq' ht hb) =
      whiskerRightIso (F.mapComp' t t' t'' (by rw [â† ht])) (F.map r) â‰ªâ‰«
      Î±_ _ _ _ â‰ªâ‰« whiskerLeftIso (F.map t) (F.isoMapOfCommSq sq') â‰ªâ‰«
      (Î±_ _ _ _).symm â‰ªâ‰« whiskerRightIso (F.isoMapOfCommSq sq) (F.map b') â‰ªâ‰«
      Î±_ _ _ _ â‰ªâ‰« whiskerLeftIso (F.map l)
        ((F.mapComp' b b' b'' (by rw [â† hb])).symm) := by
  ext
  have w : t'' â‰« r = t â‰« t' â‰« r := by rw [reassoc_of% ht]
  rw [F.isoMapOfCommSq_eq ((sq.horiz_comp' sq' ht hb)) (t â‰« t' â‰« r) w,
    F.isoMapOfCommSq_eq sq' (t' â‰« r) rfl, F.isoMapOfCommSq_eq sq (t â‰« m) rfl]
  dsimp
  simp only [Bicategory.whiskerLeft_comp, comp_whiskerRight, Category.assoc]
  rw [â† F.mapComp'_inv_comp_mapComp'_hom_assoc t _ _ _ (t' â‰« r) _ _ _ rfl,
    F.mapComp'_hom_comp_mapComp'_hom_whiskerRight_assoc _ _ _ _ _ _ _ hb
    (by rw [Category.assoc, â† sq'.w]),
    Iso.inv_hom_id_assoc, whiskerLeft_hom_inv, Category.comp_id,
    â† cancel_epi (F.mapComp' t'' r (t â‰« t' â‰« r) w).hom,
    F.mapComp'_hom_comp_mapComp'_hom_whiskerRight_assoc _ _ _ _ _ _ ht rfl w,
    Iso.hom_inv_id_assoc, Iso.inv_hom_id_assoc,
    whiskerLeft_hom_inv_assoc, Iso.hom_inv_id_assoc]

lemma isoMapOfCommSq_vert_comp
    {t : Xâ‚ âŸ¶ Yâ‚} {m : Xâ‚‚ âŸ¶ Yâ‚‚} {b : Xâ‚ƒ âŸ¶ Yâ‚ƒ}
    {l : Xâ‚ âŸ¶ Xâ‚‚} {l' : Xâ‚‚ âŸ¶ Xâ‚ƒ} {r : Yâ‚ âŸ¶ Yâ‚‚} {r' : Yâ‚‚ âŸ¶ Yâ‚ƒ}
    (sq : CommSq t l r m) (sq' : CommSq m l' r' b)
    {l'' : Xâ‚ âŸ¶ Xâ‚ƒ} {r'' : Yâ‚ âŸ¶ Yâ‚ƒ} (hl : l â‰« l' = l'') (hr : r â‰« r' = r'') :
    F.isoMapOfCommSq (sq.vert_comp' sq' hl hr) =
      whiskerLeftIso (F.map t) (F.mapComp' r r' r'' hr) â‰ªâ‰«
        (Î±_ _ _ _).symm â‰ªâ‰« whiskerRightIso (F.isoMapOfCommSq sq) (F.map r') â‰ªâ‰«
        Î±_ _ _ _ â‰ªâ‰« whiskerLeftIso (F.map l) (F.isoMapOfCommSq sq') â‰ªâ‰«
        (Î±_ _ _ _).symm â‰ªâ‰« whiskerRightIso (F.mapComp' l l' l'' hl).symm (F.map b) := by
  rw [â† Iso.symm_eq_iff, â† isoMapOfCommSq_flip,
    F.isoMapOfCommSq_horiz_comp sq.flip sq'.flip hl hr,
    F.isoMapOfCommSq_flip sq', F.isoMapOfCommSq_flip sq]
  ext
  dsimp
  simp only [Category.assoc]

end CommSq

end Pseudofunctor

end CategoryTheory
