/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou, Christian Merten
-/
import Mathlib.CategoryTheory.Bicategory.Functor.Strict

/-!
# Pseudofunctors to Cat

In this file, the equalities stated in `CategoryTheory.Bicategory.Functor.Strict`
for pseudofunctors (from a strict bicategory) are rephrased in the particular
case the target bicategory is `Cat`. Indeed, in that case, the general lemmas
for pseudofunctors involve equalities between natural transformations:
we rephrase them after the application of `NatTrans.app`.

-/

namespace CategoryTheory

open Bicategory

namespace Pseudofunctor

variable {B : Type*} [Bicategory B] (F : Pseudofunctor B Cat)

attribute [local simp] Cat.leftUnitor_hom_app Cat.rightUnitor_hom_app
  Cat.leftUnitor_inv_app Cat.rightUnitor_inv_app
  Cat.associator_hom_app Cat.associator_inv_app

section naturality

variable {b‚ÇÄ b‚ÇÅ b‚ÇÇ : B} {X Y : F.obj b‚ÇÄ}

section

variable (f : b‚ÇÄ ‚ü∂ b‚ÇÄ) (hf : f = ùüô b‚ÇÄ) (a : X ‚ü∂ Y)

@[reassoc]
lemma mapId'_hom_naturality :
    (F.map f).map a ‚â´ (F.mapId' f hf).hom.app Y = (F.mapId' f hf).hom.app X ‚â´ a :=
  (F.mapId' f hf).hom.naturality a

@[reassoc]
lemma mapId'_inv_naturality :
    (F.mapId' f hf).inv.app X ‚â´ (F.map f).map a = a ‚â´ (F.mapId' f hf).inv.app Y :=
  ((F.mapId' f hf).inv.naturality a).symm

end

section

variable (f : b‚ÇÄ ‚ü∂ b‚ÇÅ) (g : b‚ÇÅ ‚ü∂ b‚ÇÇ) (fg : b‚ÇÄ ‚ü∂ b‚ÇÇ)
  (hfg : f ‚â´ g = fg) (a : X ‚ü∂ Y)

@[reassoc]
lemma mapComp'_hom_naturality :
    (F.map fg).map a ‚â´ (F.mapComp' f g fg hfg).hom.app Y =
      (F.mapComp' f g fg hfg).hom.app X ‚â´ (F.map g).map ((F.map f).map a) :=
  (F.mapComp' f g fg hfg).hom.naturality a

@[reassoc (attr := simp)]
lemma mapComp'_inv_naturality :
    (F.map g).map ((F.map f).map a) ‚â´ (F.mapComp' f g fg hfg).inv.app Y =
    (F.mapComp' f g fg hfg).inv.app X ‚â´ (F.map fg).map a :=
  (F.mapComp' f g fg hfg).inv.naturality a

@[reassoc]
lemma mapComp'_naturality_1 :
    (F.mapComp' f g fg hfg).inv.app X ‚â´
      (F.map fg).map a ‚â´ (F.mapComp' f g fg hfg).hom.app Y =
      (F.map g).map ((F.map f).map a) :=
  NatIso.naturality_1 (F.mapComp' f g fg hfg) a

@[reassoc]
lemma mapComp'_naturality_2 :
    (F.mapComp' f g fg hfg).hom.app X ‚â´ (F.map g).map ((F.map f).map a) ‚â´
      (F.mapComp' f g fg hfg).inv.app Y =
      (F.map fg).map a :=
  NatIso.naturality_2 (F.mapComp' f g fg hfg) a

end

end naturality

variable [Strict B]

section unitality

variable {b‚ÇÄ b‚ÇÅ : B} (f : b‚ÇÄ ‚ü∂ b‚ÇÅ) (X : F.obj b‚ÇÄ)

lemma mapComp'_comp_id_hom_app :
    (F.mapComp' f (ùüô b‚ÇÅ) f).hom.app X = (F.mapId b‚ÇÅ).inv.app ((F.map f).obj X) := by
  simp [mapComp'_comp_id]

lemma mapComp'_comp_id_inv_app :
    (F.mapComp' f (ùüô b‚ÇÅ) f).inv.app X = (F.mapId b‚ÇÅ).hom.app ((F.map f).obj X) := by
  simp [mapComp'_comp_id]

lemma mapComp'_id_comp_hom_app :
    (F.mapComp' (ùüô b‚ÇÄ) f f).hom.app X = (F.map f).map ((F.mapId b‚ÇÄ).inv.app X) := by
  simp [mapComp'_id_comp]

lemma mapComp'_id_comp_inv_app :
    (F.mapComp' (ùüô b‚ÇÄ) f f).inv.app X = (F.map f).map ((F.mapId b‚ÇÄ).hom.app X) := by
  simp [mapComp'_id_comp]

end unitality

section associativity

variable {b‚ÇÄ b‚ÇÅ b‚ÇÇ b‚ÇÉ : B} (f‚ÇÄ‚ÇÅ : b‚ÇÄ ‚ü∂ b‚ÇÅ)
  (f‚ÇÅ‚ÇÇ : b‚ÇÅ ‚ü∂ b‚ÇÇ) (f‚ÇÇ‚ÇÉ : b‚ÇÇ ‚ü∂ b‚ÇÉ) (f‚ÇÄ‚ÇÇ : b‚ÇÄ ‚ü∂ b‚ÇÇ) (f‚ÇÅ‚ÇÉ : b‚ÇÅ ‚ü∂ b‚ÇÉ) (f : b‚ÇÄ ‚ü∂ b‚ÇÉ)
  (h‚ÇÄ‚ÇÇ : f‚ÇÄ‚ÇÅ ‚â´ f‚ÇÅ‚ÇÇ = f‚ÇÄ‚ÇÇ) (h‚ÇÅ‚ÇÉ : f‚ÇÅ‚ÇÇ ‚â´ f‚ÇÇ‚ÇÉ = f‚ÇÅ‚ÇÉ)

@[reassoc]
lemma mapComp'_hom_app_comp_mapComp'_hom_app_map_obj (hf : f‚ÇÄ‚ÇÅ ‚â´ f‚ÇÅ‚ÇÉ = f) (X : F.obj b‚ÇÄ) :
    (F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÉ f).hom.app X ‚â´
      (F.mapComp' f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÅ‚ÇÉ h‚ÇÅ‚ÇÉ).hom.app ((F.map f‚ÇÄ‚ÇÅ).obj X) =
      (F.mapComp' f‚ÇÄ‚ÇÇ f‚ÇÇ‚ÇÉ f).hom.app X ‚â´
        (F.map f‚ÇÇ‚ÇÉ).map ((F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÄ‚ÇÇ h‚ÇÄ‚ÇÇ).hom.app X) := by
  simpa using NatTrans.congr_app (F.mapComp'_hom_comp_whiskerLeft_mapComp'_hom
    f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÄ‚ÇÇ f‚ÇÅ‚ÇÉ f h‚ÇÄ‚ÇÇ h‚ÇÅ‚ÇÉ hf) X

@[reassoc]
lemma mapComp'_inv_app_comp_mapComp'_hom_app (hf : f‚ÇÄ‚ÇÅ ‚â´ f‚ÇÅ‚ÇÉ = f) (X : F.obj b‚ÇÄ) :
    (F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÉ f).inv.app X ‚â´ (F.mapComp' f‚ÇÄ‚ÇÇ f‚ÇÇ‚ÇÉ f).hom.app X =
      (F.mapComp' f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÅ‚ÇÉ h‚ÇÅ‚ÇÉ).hom.app ((F.map f‚ÇÄ‚ÇÅ).obj X) ‚â´
        (F.map f‚ÇÇ‚ÇÉ).map ((F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÄ‚ÇÇ h‚ÇÄ‚ÇÇ).inv.app X) := by
  simpa using NatTrans.congr_app (F.mapComp'_inv_comp_mapComp'_hom
    f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÄ‚ÇÇ f‚ÇÅ‚ÇÉ f h‚ÇÄ‚ÇÇ h‚ÇÅ‚ÇÉ hf) X

@[reassoc]
lemma mapComp'_inv_app_map_obj_comp_mapComp'_inv_app (hf : f‚ÇÄ‚ÇÅ ‚â´ f‚ÇÅ‚ÇÉ = f) (X : F.obj b‚ÇÄ) :
    (F.mapComp' f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÅ‚ÇÉ h‚ÇÅ‚ÇÉ).inv.app ((F.map f‚ÇÄ‚ÇÅ).obj X) ‚â´ (F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÉ f).inv.app X =
    (F.map f‚ÇÇ‚ÇÉ).map ((F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÄ‚ÇÇ h‚ÇÄ‚ÇÇ).inv.app X) ‚â´
      (F.mapComp' f‚ÇÄ‚ÇÇ f‚ÇÇ‚ÇÉ f).inv.app X := by
  simpa using NatTrans.congr_app (F.whiskerLeft_mapComp'_inv_comp_mapComp'_inv
    f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÄ‚ÇÇ f‚ÇÅ‚ÇÉ f h‚ÇÄ‚ÇÇ h‚ÇÅ‚ÇÉ hf) X

@[reassoc]
lemma mapComp'_hom_app_comp_map_map_mapComp'_hom_app (hf : f‚ÇÄ‚ÇÇ ‚â´ f‚ÇÇ‚ÇÉ = f) (X : F.obj b‚ÇÄ) :
    (F.mapComp' f‚ÇÄ‚ÇÇ f‚ÇÇ‚ÇÉ f).hom.app X ‚â´ (F.map f‚ÇÇ‚ÇÉ).map ((F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÄ‚ÇÇ h‚ÇÄ‚ÇÇ).hom.app X) =
      (F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÉ f).hom.app X ‚â´
        (F.mapComp' f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÅ‚ÇÉ h‚ÇÅ‚ÇÉ).hom.app ((F.map f‚ÇÄ‚ÇÅ).obj X) := by
  simpa using NatTrans.congr_app (F.mapComp'_hom_comp_mapComp'_hom_whiskerRight
    f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÄ‚ÇÇ f‚ÇÅ‚ÇÉ f h‚ÇÄ‚ÇÇ h‚ÇÅ‚ÇÉ hf) X

@[reassoc]
lemma map_map_mapComp'_inv_app_comp_mapComp'_inv_app (hf : f‚ÇÄ‚ÇÇ ‚â´ f‚ÇÇ‚ÇÉ = f) (X : F.obj b‚ÇÄ) :
    (F.map f‚ÇÇ‚ÇÉ).map ((F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÄ‚ÇÇ h‚ÇÄ‚ÇÇ).inv.app X) ‚â´ (F.mapComp' f‚ÇÄ‚ÇÇ f‚ÇÇ‚ÇÉ f).inv.app X =
    (F.mapComp' f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÅ‚ÇÉ h‚ÇÅ‚ÇÉ).inv.app ((F.map f‚ÇÄ‚ÇÅ).obj X) ‚â´
      (F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÉ f).inv.app X := by
  simpa using NatTrans.congr_app (F.mapComp'_inv_whiskerRight_comp_mapComp'_inv
    f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÄ‚ÇÇ f‚ÇÅ‚ÇÉ f h‚ÇÄ‚ÇÇ h‚ÇÅ‚ÇÉ hf) X

@[reassoc]
lemma mapComp'‚ÇÄ‚ÇÅ‚ÇÉ_inv_app (hf : f‚ÇÄ‚ÇÅ ‚â´ f‚ÇÅ‚ÇÉ = f) (X : F.obj b‚ÇÄ) :
    (F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÉ f hf).inv.app X =
      (F.mapComp' f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÅ‚ÇÉ h‚ÇÅ‚ÇÉ).hom.app ((F.map f‚ÇÄ‚ÇÅ).obj X) ‚â´
        (F.map f‚ÇÇ‚ÇÉ).map ((F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÄ‚ÇÇ h‚ÇÄ‚ÇÇ).inv.app X) ‚â´
          (F.mapComp' f‚ÇÄ‚ÇÇ f‚ÇÇ‚ÇÉ f).inv.app X := by
  rw [‚Üê F.mapComp'_inv_app_comp_mapComp'_hom_app_assoc _ _ _ _ _ _ _ _ hf X,
    Iso.hom_inv_id_app, Category.comp_id]

@[reassoc]
lemma mapComp'‚ÇÄ‚ÇÅ‚ÇÉ_hom_app (hf : f‚ÇÄ‚ÇÅ ‚â´ f‚ÇÅ‚ÇÉ = f) (X : F.obj b‚ÇÄ) :
    (F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÉ f hf).hom.app X =
      (F.mapComp' f‚ÇÄ‚ÇÇ f‚ÇÇ‚ÇÉ f).hom.app X ‚â´
        (F.map f‚ÇÇ‚ÇÉ).map ((F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÄ‚ÇÇ h‚ÇÄ‚ÇÇ).hom.app X) ‚â´
          (F.mapComp' f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÅ‚ÇÉ h‚ÇÅ‚ÇÉ).inv.app ((F.map f‚ÇÄ‚ÇÅ).obj X) := by
  rw [‚Üê F.mapComp'_hom_app_comp_mapComp'_hom_app_map_obj_assoc _ _ _ _ _ _ h‚ÇÄ‚ÇÇ h‚ÇÅ‚ÇÉ hf X]
  simp

@[reassoc]
lemma mapComp'_inv_app_comp_mapComp'_hom_app' (hf : f‚ÇÄ‚ÇÅ ‚â´ f‚ÇÅ‚ÇÉ = f) (X : F.obj b‚ÇÄ) :
    (F.mapComp' f‚ÇÄ‚ÇÇ f‚ÇÇ‚ÇÉ f).inv.app X ‚â´ (F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÉ f).hom.app X =
    (F.map f‚ÇÇ‚ÇÉ).map ((F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÄ‚ÇÇ h‚ÇÄ‚ÇÇ).hom.app X) ‚â´
      (F.mapComp' f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÅ‚ÇÉ h‚ÇÅ‚ÇÉ).inv.app ((F.map f‚ÇÄ‚ÇÅ).obj X) := by
  simp [F.mapComp'‚ÇÄ‚ÇÅ‚ÇÉ_hom_app f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÄ‚ÇÇ f‚ÇÅ‚ÇÉ f h‚ÇÄ‚ÇÇ h‚ÇÅ‚ÇÉ hf]

@[reassoc]
lemma mapComp'_hom_app_comp_mapComp'_inv_app (hf : f‚ÇÄ‚ÇÅ ‚â´ f‚ÇÅ‚ÇÉ = f) (X : F.obj b‚ÇÄ) :
    (F.mapComp' f‚ÇÄ‚ÇÇ f‚ÇÇ‚ÇÉ f).inv.app X ‚â´ (F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÉ f).hom.app X =
      (F.map f‚ÇÇ‚ÇÉ).map ((F.mapComp' f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÄ‚ÇÇ h‚ÇÄ‚ÇÇ).hom.app X) ‚â´
      (F.mapComp' f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÅ‚ÇÉ h‚ÇÅ‚ÇÉ).inv.app ((F.map f‚ÇÄ‚ÇÅ).obj X) := by
  simp [F.mapComp'‚ÇÄ‚ÇÅ‚ÇÉ_hom_app f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ f‚ÇÄ‚ÇÇ f‚ÇÅ‚ÇÉ f h‚ÇÄ‚ÇÇ h‚ÇÅ‚ÇÉ hf]


end associativity

end Pseudofunctor

end CategoryTheory
