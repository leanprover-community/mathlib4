/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou, Christian Merten
-/
module

public import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor

/-!
# Pseudofunctors to Cat

In this file, we state naturality properties of `mapId'` and `mapComp'`
for pseudofunctors to `Cat`.

-/

public section

universe w v v' u u'

namespace CategoryTheory

open Bicategory

namespace Pseudofunctor

variable {B : Type u} [Bicategory.{w, v} B] (F : B ‚•§·µñ Cat.{v', u'})

section naturality

variable {b‚ÇÄ b‚ÇÅ b‚ÇÇ : B} {X Y : F.obj b‚ÇÄ}

section

variable (f : b‚ÇÄ ‚ü∂ b‚ÇÄ) (hf : f = ùüô b‚ÇÄ) (a : X ‚ü∂ Y)

@[reassoc]
lemma mapId'_hom_naturality :
    (F.map f).toFunctor.map a ‚â´ (F.mapId' f hf).hom.toNatTrans.app Y =
    (F.mapId' f hf).hom.toNatTrans.app X ‚â´ a :=
  (F.mapId' f hf).hom.toNatTrans.naturality a

@[reassoc]
lemma mapId'_inv_naturality :
    (F.mapId' f hf).inv.toNatTrans.app X ‚â´ (F.map f).toFunctor.map a =
    a ‚â´ (F.mapId' f hf).inv.toNatTrans.app Y :=
  ((F.mapId' f hf).inv.toNatTrans.naturality a).symm

end

section

variable (f : b‚ÇÄ ‚ü∂ b‚ÇÅ) (g : b‚ÇÅ ‚ü∂ b‚ÇÇ) (fg : b‚ÇÄ ‚ü∂ b‚ÇÇ)
  (hfg : f ‚â´ g = fg) (a : X ‚ü∂ Y)

@[reassoc]
lemma mapComp'_hom_naturality :
    (F.map fg).toFunctor.map a ‚â´ (F.mapComp' f g fg hfg).hom.toNatTrans.app Y =
    (F.mapComp' f g fg hfg).hom.toNatTrans.app X ‚â´ (F.map g).toFunctor.map
      ((F.map f).toFunctor.map a) :=
  (F.mapComp' f g fg hfg).hom.toNatTrans.naturality a

@[reassoc (attr := simp)]
lemma mapComp'_inv_naturality :
    (F.map g).toFunctor.map ((F.map f).toFunctor.map a) ‚â´
    (F.mapComp' f g fg hfg).inv.toNatTrans.app Y =
    (F.mapComp' f g fg hfg).inv.toNatTrans.app X ‚â´ (F.map fg).toFunctor.map a :=
  (F.mapComp' f g fg hfg).inv.toNatTrans.naturality a

@[reassoc]
lemma mapComp'_naturality_1 :
    (F.mapComp' f g fg hfg).inv.toNatTrans.app X ‚â´
      (F.map fg).toFunctor.map a ‚â´ (F.mapComp' f g fg hfg).hom.toNatTrans.app Y =
    (F.map g).toFunctor.map ((F.map f).toFunctor.map a) :=
  NatIso.naturality_1 (Cat.Hom.toNatIso (F.mapComp' f g fg hfg)) a

@[reassoc]
lemma mapComp'_naturality_2 :
    (F.mapComp' f g fg hfg).hom.toNatTrans.app X ‚â´
      (F.map g).toFunctor.map ((F.map f).toFunctor.map a) ‚â´
        (F.mapComp' f g fg hfg).inv.toNatTrans.app Y =
    (F.map fg).toFunctor.map a :=
  NatIso.naturality_2 (Cat.Hom.toNatIso (F.mapComp' f g fg hfg)) a

end

end naturality

end Pseudofunctor

end CategoryTheory
