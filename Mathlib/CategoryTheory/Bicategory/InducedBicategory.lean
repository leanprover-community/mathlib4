/-
Copyright (c) 2025 Calle S√∂nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle S√∂nne
-/
module

public import Mathlib.CategoryTheory.Bicategory.Functor.StrictPseudofunctor

/-!

# Induced bicategories

In this file we develop API for constructing a full sub-bicategory of a bicategory `C`, given a
map `F : B ‚Üí C`. The objects of the induced bicategory are the objects of `B`, while the
1-morphisms and 2-morphisms are taken as all corresponding morphisms in `C`.

## TODO

One might also want to develop "locally induced" bicategories, which should allow for a sub-class
of 1-morphisms as well. However, this needs more thought. If one tries the naive approach of simply
replacing the map `F` below with a "functor" (between `CategoryStruct`s), one runs into the issue
that `map_comp` and `map_id` might not be definitional equalities (which they should be in
practice). Hence one needs to carefully carry these around, or specify `F` in a way that ensures
they are def-eqs, perhaps constructing it from specified `MorphismProperty`s.
-/

@[expose] public section

namespace CategoryTheory.Bicategory

variable {B : Type*} (C : Type*) [Bicategory C] (F : B ‚Üí C)

/-- `InducedBicategory B C`, where `F : B ‚Üí C`, is a typeclass synonym for `B`. This is given
a bicategory structure where the 1-morphisms `X ‚ü∂ Y` are the 1-morphisms in `C` from `F X` to
`F Y`, and the 2-morphisms `f ‚ü∂ g` are also the 2-morphisms in `C` from `f` to `g`.
-/
@[nolint unusedArguments]
def InducedBicategory (_F : B ‚Üí C) :=
  B

namespace InducedBicategory

variable {C F}

instance hasCoeToSort {Œ± : Sort*} [CoeSort C Œ±] : CoeSort (InducedBicategory C F) Œ± :=
  ‚ü®fun c => F c‚ü©

set_option backward.privateInPublic true in
/-- `InducedBicategory.Hom X Y` is a type-alias for morphisms between `X Y : B` viewed as objects
of `B` with the induced bicategory structure. This is given a `CategoryStruct` instance below,
where the identity and composition is induced from `C`. -/
@[ext]
structure Hom (X Y : InducedBicategory C F) where
  private mk ::
  /-- The morphism in `C` underlying the morphism in `InducedBicategory C F`. -/
  hom : F X ‚ü∂ F Y

set_option backward.privateInPublic true in
set_option backward.privateInPublic.warn false in
@[simps id_hom comp_hom]
instance categoryStruct : CategoryStruct (InducedBicategory C F) where
  Hom X Y := Hom X Y
  id X := ‚ü®ùüô (F X)‚ü©
  comp u v := ‚ü®u.hom ‚â´ v.hom‚ü©

set_option backward.privateInPublic true in
set_option backward.privateInPublic.warn false in
/-- Synonym for `Hom.mk` which makes unification easier. -/
abbrev mkHom {X Y : InducedBicategory C F} (f : F X ‚ü∂ F Y) : X ‚ü∂ Y :=
  ‚ü®f‚ü©

@[ext]
lemma hom_ext {X Y : InducedBicategory C F} {f g : X ‚ü∂ Y} (h : f.hom = g.hom) : f = g :=
  Hom.ext h

/-- `InducedBicategory.Hom‚ÇÇ f g` is a type-alias for 2-morphisms between `f g : X ‚ü∂ Y`, where
`f` and `g` are 1-morphisms for the induced bicategory structure on `B`.

This is given a `Category` instance below, induced from the corresponding one in `C`. -/
@[ext]
structure Hom‚ÇÇ {X Y : InducedBicategory C F} (f g : X ‚ü∂ Y) where
  /-- The 2-morphism in `C` underlying the 2-morphism in `InducedBicategory C F`. -/
  hom : f.hom ‚ü∂ g.hom

@[simps!]
instance Hom.category (X Y : InducedBicategory C F) : Category (X ‚ü∂ Y) where
  Hom f g := Hom‚ÇÇ f g
  id f := ‚ü®ùüô f.hom‚ü©
  comp u v := ‚ü®u.hom ‚â´ v.hom‚ü©

@[ext]
lemma hom‚ÇÇ_ext {X Y : InducedBicategory C F} {f g : X ‚ü∂ Y} {Œ∑ Œ∏ : f ‚ü∂ g} (h : Œ∑.hom = Œ∏.hom) :
    Œ∑ = Œ∏ :=
  Hom‚ÇÇ.ext h

/-- Synonym for the constructor of `Hom‚ÇÇ` where the 1-morphisms `f` and `g` lie in `C`, and not
given in the form `f'.hom`, `g'.hom` for some `f' g' : InducedBicategory.Hom _ _`. -/
abbrev mkHom‚ÇÇ {a b : InducedBicategory C F} {f g : F a ‚ü∂ F b} (Œ∑ : f ‚ü∂ g) : mkHom f ‚ü∂ mkHom g :=
  Hom‚ÇÇ.mk Œ∑

/-- Constructor for 2-isomorphisms in the induced bicategory. -/
@[simps!]
def isoMk {X Y : InducedBicategory C F} {f g : X ‚ü∂ Y} (œÜ : f.hom ‚âÖ g.hom) : f ‚âÖ g where
  hom := ‚ü®œÜ.hom‚ü©
  inv := ‚ü®œÜ.inv‚ü©

@[simps!]
instance bicategory : Bicategory (InducedBicategory C F) where
  whiskerLeft {_ _ _} h {_ _} Œ∑ := mkHom‚ÇÇ <| h.hom ‚óÅ Hom‚ÇÇ.hom Œ∑
  whiskerRight {_ _ _} {_ _} Œ∑ h := mkHom‚ÇÇ <| (Hom‚ÇÇ.hom Œ∑) ‚ñ∑ h.hom
  associator x y z := isoMk (Œ±_ x.hom y.hom z.hom)
  leftUnitor x := isoMk (Œª_ x.hom)
  rightUnitor x := isoMk (œÅ_ x.hom)
  whisker_exchange {_ _ _ _ _ _ _} Œ∑ Œ∏ := by ext; simpa using whisker_exchange _ _

attribute [-simp] bicategory_comp_hom bicategory_Hom

section

/-- The forgetful (strict) pseudofunctor from an induced bicategory to the original bicategory,
forgetting the extra data.
-/
@[simps!]
def forget : StrictPseudofunctor (InducedBicategory C F) C :=
  StrictPseudofunctor.mk' {
    obj X := F X
    map f := f.hom
    map‚ÇÇ Œ∑ := Œ∑.hom }

end

section

@[simp]
lemma eqToHom_hom {X Y : InducedBicategory C F} {f g : X ‚ü∂ Y} (h : f = g) :
    (eqToHom h).hom = eqToHom (h ‚ñ∏ rfl) := by
  subst h; simp only [eqToHom_refl, Hom.category_id_hom]

@[simp]
lemma mkHom_eqToHom {X Y : InducedBicategory C F} {f g : F X ‚ü∂ F Y} (h : f = g) :
    mkHom‚ÇÇ (eqToHom h) = eqToHom (h ‚ñ∏ rfl) := by
  ext; subst h; simp only [eqToHom_refl, Hom.category_id_hom]

variable [Strict C]

attribute [local simp] Strict.leftUnitor_eqToIso Strict.rightUnitor_eqToIso
  Strict.associator_eqToIso

instance : Strict (InducedBicategory C F) where

end

end InducedBicategory

end CategoryTheory.Bicategory
