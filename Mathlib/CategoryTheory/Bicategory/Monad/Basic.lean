/-
Copyright (c) 2025 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import Mathlib.CategoryTheory.Bicategory.LocallyDiscrete
import Mathlib.CategoryTheory.Bicategory.FunctorBicategory.Oplax
import Mathlib.CategoryTheory.Bicategory.End
import Mathlib.CategoryTheory.Monoidal.Comon_

/-!
# Comonads in a bicategory

We define comonads in a bicategory `B` as comonoid objects in an endomorphism monoidal category.
We show that this is equivalent to oplax functors from the trivial bicategory to `B`. From this,
we show that comonads in `B` form a bicategory.

## TODO

We can also define monads in a bicategory. This is not yet done as we don't have the bicategory
structure on the set of lax functors at this point, which is needed to show that monads form a
bicategory.
-/

namespace CategoryTheory

namespace Bicategory

universe w v u

variable {B : Type u} [Bicategory.{w, v} B]

/-- A comonad in a bicategory `B` is a 1-morphism `t : a âŸ¶ a` together with 2-morphisms
`Î” : t âŸ¶ t â‰« t` and `Îµ : t âŸ¶ ğŸ™ a` satisfying the comonad laws. -/
abbrev Comonad {a : B} (t : a âŸ¶ a) := ComonObj t

/-- The counit 2-morphism of the comonad. -/
abbrev Comonad.counit {a : B} {t : a âŸ¶ a} [Comonad t] : t âŸ¶ ğŸ™ a := ComonObj.counit

/-- The comultiplication 2-morphism of the comonad. -/
abbrev Comonad.comul {a : B} {t : a âŸ¶ a} [Comonad t] : t âŸ¶ t â‰« t := ComonObj.comul

@[inherit_doc] scoped notation "Îµ" => Comonad.counit
@[inherit_doc] scoped notation "Îµ["x"]" => Comonad.counit (t := x)
@[inherit_doc] scoped notation "Î”" => Comonad.comul
@[inherit_doc] scoped notation "Î”["x"]" => Comonad.comul (t := x)

namespace Comonad

variable {a : B}

/- Comonad laws -/
section

variable (t : a âŸ¶ a) [Comonad t]

@[reassoc (attr := simp)]
theorem counit_comul : Î” â‰« Îµ â–· t = (Î»_ t).inv := ComonObj.counit_comul t

@[reassoc (attr := simp)]
theorem comul_counit : Î” â‰« t â— Îµ = (Ï_ t).inv := ComonObj.comul_counit t

@[reassoc (attr := simp)]
theorem comul_assoc : Î” â‰« t â— Î” = Î” â‰« Î” â–· t â‰« (Î±_ t t t).hom := ComonObj.comul_assoc t

@[reassoc]
theorem comul_assoc_flip : Î” â‰« Î” â–· t = Î” â‰« t â— Î” â‰« (Î±_ t t t).inv := ComonObj.comul_assoc_flip t

end

@[simps! counit]
instance {a : B} : Comonad (ğŸ™ a) :=
  ComonObj.instTensorUnit (a âŸ¶ a)

/-- An oplax functor from the trivial bicategory to `B` defines a comonad in `B`. -/
def ofOplaxFromUnit (F : OplaxFunctor (LocallyDiscrete (Discrete Unit)) B) :
    Comonad (F.map (ğŸ™ âŸ¨âŸ¨Unit.unitâŸ©âŸ©)) where
  comul := F.mapâ‚‚ (Ï_ _).inv â‰« F.mapComp _ _
  counit := F.mapId _
  comul_assoc := by
    simp only [tensorObj_def, MonoidalCategory.whiskerLeft_comp, whiskerLeft_def, Category.assoc,
      MonoidalCategory.comp_whiskerRight, whiskerRight_def, associator_def]
    rw [â† F.mapComp_naturality_left_assoc, â† F.mapComp_naturality_right_assoc]
    simp only [whiskerLeft_rightUnitor_inv, PrelaxFunctor.mapâ‚‚_comp, Category.assoc,
      OplaxFunctor.mapâ‚‚_associator, whiskerRight_id, Iso.hom_inv_id_assoc]
  counit_comul := by
    simp only [tensorUnit_def, tensorObj_def, whiskerRight_def, Category.assoc, leftUnitor_def]
    rw [F.mapComp_id_left, unitors_equal, F.mapâ‚‚_inv_hom_assoc]
  comul_counit := by
    simp only [tensorUnit_def, tensorObj_def, whiskerLeft_def, rightUnitor_def]
    rw [Category.assoc, F.mapComp_id_right, F.mapâ‚‚_inv_hom_assoc]

/-- A comonad in `B` defines an oplax functor from the trivial bicategory to `B`. -/
def toOplax {a : B} (t : a âŸ¶ a) [Comonad t] :
    OplaxFunctor (LocallyDiscrete (Discrete Unit)) B where
  obj _ := a
  map _ := t
  mapâ‚‚ _ := ğŸ™ _
  mapId _ := Îµ
  mapComp _ _ := Î”
  mapâ‚‚_associator _ _ _ := by
    rw [Category.id_comp]
    apply Comonad.comul_assoc

end Comonad

/- In this section, we define bicategory structure on comonads by using the bicategory structure on
oplax functors. We may use oplax, lax, or pseudonatural transformations to provide the bicategory
structure, and the namespace below indicates that we use oplax transformations here. The
constructions for the other two cases would be given in the corresponding namespaces. -/
namespace OplaxTrans

/-- The bicategory of comonads in `B`. -/
def ComonadBicat (B : Type u) [Bicategory.{w, v} B] :=
  OplaxFunctor (LocallyDiscrete (Discrete Unit)) B

namespace ComonadBicat

open scoped Oplax.OplaxTrans.OplaxFunctor in
/-- The bicategory of comonads in `B`. -/
scoped instance : Bicategory (ComonadBicat B) :=
  inferInstanceAs <| Bicategory (OplaxFunctor (LocallyDiscrete (Discrete PUnit)) B)

/-- The oplax functor from the trivial bicategory to `B` associated with the comonad. -/
def toOplax (m : ComonadBicat B) : OplaxFunctor (LocallyDiscrete (Discrete PUnit)) B :=
  m

/-- The object in `B` associated with the comonad. -/
def obj (m : ComonadBicat B) :=
  m.toOplax.obj âŸ¨âŸ¨PUnit.unitâŸ©âŸ©

/-- The morphism in `B` associated with the comonad. -/
def hom (m : ComonadBicat B) : m.obj  âŸ¶ m.obj :=
  m.toOplax.map (ğŸ™ (âŸ¨âŸ¨PUnit.unitâŸ©âŸ© : LocallyDiscrete (Discrete PUnit)))

instance (m : ComonadBicat B) : Comonad m.hom :=
  Comonad.ofOplaxFromUnit <| m.toOplax

/-- Construct a comonad as an object in `ComonadBicat B`. -/
def mkOfComonad {a : B} (t : a âŸ¶ a) [Comonad t] : ComonadBicat B :=
  Comonad.toOplax t

open Comonad

section

variable {a : B} (t : a âŸ¶ a) [Comonad t]

theorem mkOfComonad_hom : (mkOfComonad t).hom = t := rfl

theorem mkOfComonad_counit : Îµ[(mkOfComonad t).hom] = Îµ[t] := rfl

theorem mkOfComonad_comul : Î”[(mkOfComonad t).hom] = Î”[t] := by
  change ğŸ™ t â‰« Î” = Î”
  apply Category.id_comp

end

end ComonadBicat

end OplaxTrans

end Bicategory

end CategoryTheory
