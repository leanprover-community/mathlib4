/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.EssentiallySmall

/-!
# The Yoneda functor for locally small categories

Let `C` be a locally `w`-small category. We define the Yoneda
embedding `shrinkYoneda : C ‚•§ C·µí·µñ ‚•§ Type w`. (See the
file `CategoryTheory.Yoneda` for the other variants `yoneda` and
`uliftYoneda`.)

-/

@[expose] public section

universe w w' v u

namespace CategoryTheory

open Opposite

variable {C : Type u} [Category.{v} C]

namespace FunctorToTypes

/-- A functor to types `F : C ‚•§ Type w'` is `w`-small if for any `X : C`,
the type `F.obj X` is `w`-small. -/
@[pp_with_univ]
protected abbrev Small (F : C ‚•§ Type w') := ‚àÄ (X : C), _root_.Small.{w} (F.obj X)

/-- If a functor `F : C ‚•§ Type w'` is `w`-small, this is the functor `C ‚•§ Type w`
obtained by shrinking `F.obj X` for all `X : C`. -/
@[simps, pp_with_univ]
noncomputable def shrink (F : C ‚•§ Type w') [FunctorToTypes.Small.{w} F] :
    C ‚•§ Type w where
  obj X := Shrink.{w} (F.obj X)
  map f := equivShrink.{w} _ ‚àò F.map f ‚àò (equivShrink.{w} _).symm

attribute [local simp] FunctorToTypes.naturality in
/-- The natural transformation `shrink.{w} F ‚ü∂ shrink.{w} G` induces by a natural
transformation `œÑ : F ‚ü∂ G` between `w`-small functors to types. -/
@[simps]
noncomputable def shrinkMap {F G : C ‚•§ Type w'} (œÑ : F ‚ü∂ G) [FunctorToTypes.Small.{w} F]
    [FunctorToTypes.Small.{w} G] :
    shrink.{w} F ‚ü∂ shrink.{w} G where
  app X := equivShrink.{w} _ ‚àò œÑ.app X ‚àò (equivShrink.{w} _).symm

end FunctorToTypes

variable [LocallySmall.{w} C]

instance (X : C) : FunctorToTypes.Small.{w} (yoneda.obj X) :=
  fun _ ‚Ü¶ by dsimp; infer_instance

/-- The Yoneda embedding `C ‚•§ C·µí·µñ ‚•§ Type w` for a locally `w`-small category `C`. -/
@[simps -isSimp obj map, pp_with_univ]
noncomputable def shrinkYoneda :
    C ‚•§ C·µí·µñ ‚•§ Type w where
  obj X := FunctorToTypes.shrink (yoneda.obj X)
  map f := FunctorToTypes.shrinkMap (yoneda.map f)

/-- The type `(shrinkYoneda.obj X).obj Y` is equivalent to `Y.unop ‚ü∂ X`. -/
noncomputable def shrinkYonedaObjObjEquiv {X : C} {Y : C·µí·µñ} :
    ((shrinkYoneda.{w}.obj X).obj Y) ‚âÉ (Y.unop ‚ü∂ X) :=
  (equivShrink _).symm

/-- The type of natural transformations `shrinkYoneda.{w}.obj X ‚ü∂ P`
with `X : C` and `P : C·µí·µñ ‚•§ Type w` is equivalent to `P.obj (op X)`. -/
noncomputable def shrinkYonedaEquiv {X : C} {P : C·µí·µñ ‚•§ Type w} :
    (shrinkYoneda.{w}.obj X ‚ü∂ P) ‚âÉ P.obj (op X) where
  toFun œÑ := œÑ.app _ (equivShrink.{w} _ (ùüô X))
  invFun x :=
    { app Y f := P.map ((equivShrink.{w} _).symm f).op x
      naturality Y Z g := by ext; simp [shrinkYoneda] }
  left_inv œÑ := by
    ext Y f
    obtain ‚ü®f, rfl‚ü© := (equivShrink _).surjective f
    simpa [shrinkYoneda] using congr_fun (œÑ.naturality f.op).symm (equivShrink _ (ùüô X))
  right_inv x := by simp

lemma map_shrinkYonedaEquiv {X Y : C} {P : C·µí·µñ ‚•§ Type w} (f : shrinkYoneda.obj X ‚ü∂ P)
    (g : Y ‚ü∂ X) : P.map g.op (shrinkYonedaEquiv f) =
      f.app (op Y) (shrinkYonedaObjObjEquiv.symm g) := by
  simp [shrinkYonedaObjObjEquiv, shrinkYonedaEquiv, shrinkYoneda,
    ‚Üê FunctorToTypes.naturality]

lemma shrinkYonedaEquiv_shrinkYoneda_map {X Y : C} (f : X ‚ü∂ Y) :
    shrinkYonedaEquiv (shrinkYoneda.{w}.map f) = shrinkYonedaObjObjEquiv.symm f := by
  simp [shrinkYonedaEquiv, shrinkYoneda, shrinkYonedaObjObjEquiv]

lemma shrinkYonedaEquiv_comp {X : C} {P Q : C·µí·µñ ‚•§ Type w} (Œ± : shrinkYoneda.obj X ‚ü∂ P)
    (Œ≤ : P ‚ü∂ Q) :
    shrinkYonedaEquiv (Œ± ‚â´ Œ≤) = Œ≤.app _ (shrinkYonedaEquiv Œ±) := by
  simp [shrinkYonedaEquiv]

lemma shrinkYonedaEquiv_naturality {X Y : C} {P : C·µí·µñ ‚•§ Type w}
    (f : shrinkYoneda.obj X ‚ü∂ P) (g : Y ‚ü∂ X) :
    P.map g.op (shrinkYonedaEquiv f) = shrinkYonedaEquiv (shrinkYoneda.map g ‚â´ f) := by
  simpa [shrinkYonedaEquiv, shrinkYoneda]
    using congr_fun (f.naturality g.op).symm ((equivShrink _) (ùüô _))

@[reassoc]
lemma shrinkYonedaEquiv_symm_map {X Y : C·µí·µñ} (f : X ‚ü∂ Y) {P : C·µí·µñ ‚•§ Type w} (t : P.obj X) :
    shrinkYonedaEquiv.symm (P.map f t) =
      shrinkYoneda.map f.unop ‚â´ shrinkYonedaEquiv.symm t :=
  shrinkYonedaEquiv.injective (by
    obtain ‚ü®t, rfl‚ü© := shrinkYonedaEquiv.surjective t
    rw [‚Üê shrinkYonedaEquiv_naturality]
    simp)

variable (C) in
/-- The functor `shrinkYoneda : C ‚•§ C·µí·µñ ‚•§ Type w` for a locally `w`-small category `C`
is fully faithful. -/
noncomputable def fullyFaithfulShrinkYoneda :
    (shrinkYoneda.{w} (C := C)).FullyFaithful where
  preimage f := shrinkYonedaObjObjEquiv (shrinkYonedaEquiv f)
  map_preimage f := by
    obtain ‚ü®f, rfl‚ü© := shrinkYonedaEquiv.symm.surjective f
    cat_disch
  preimage_map f := by simp [shrinkYonedaEquiv_shrinkYoneda_map]

instance : (shrinkYoneda.{w} (C := C)).Faithful := (fullyFaithfulShrinkYoneda C).faithful

instance : (shrinkYoneda.{w} (C := C)).Full := (fullyFaithfulShrinkYoneda C).full

end CategoryTheory
