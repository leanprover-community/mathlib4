/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Yoneda
import Mathlib.CategoryTheory.EssentiallySmall
import Mathlib.CategoryTheory.Limits.Preserves.Basic
import Mathlib.CategoryTheory.Limits.Types.Limits

/-!
# The yoneda embedding

-/

universe w w' v v' u u'

namespace CategoryTheory

open Limits Opposite

section

variable {C : Type u} [Category.{v} C]

namespace FunctorToTypes

protected abbrev Small (F : C ‚•§ Type w') := ‚àÄ (X : C), _root_.Small.{w} (F.obj X)

@[simps]
noncomputable def shrink (F : C ‚•§ Type w') [FunctorToTypes.Small.{w} F] :
    C ‚•§ Type w where
  obj X := Shrink.{w} (F.obj X)
  map {X Y} f := equivShrink.{w} _ ‚àò F.map f ‚àò (equivShrink.{w} _).symm

attribute [local simp] FunctorToTypes.naturality in
@[simps]
noncomputable def shrinkMap {F G : C ‚•§ Type w'} (œÑ : F ‚ü∂ G) [FunctorToTypes.Small.{w} F]
    [FunctorToTypes.Small.{w} G] :
    shrink.{w} F ‚ü∂ shrink.{w} G where
  app X := equivShrink.{w} _ ‚àò œÑ.app X ‚àò (equivShrink.{w} _).symm

end FunctorToTypes

section

variable [LocallySmall.{w} C]

instance (X : C) : FunctorToTypes.Small.{w} (yoneda.obj X) :=
  fun _ ‚Ü¶ by dsimp; infer_instance

@[pp_with_univ, simps -isSimp obj map]
noncomputable def shrinkYoneda :
    C ‚•§ C·µí·µñ ‚•§ Type w where
  obj X := FunctorToTypes.shrink (yoneda.obj X)
  map f := FunctorToTypes.shrinkMap (yoneda.map f)

noncomputable def shrinkYonedaObjObjEquiv {X : C} {Y : C·µí·µñ} :
    ((shrinkYoneda.{w}.obj X).obj Y) ‚âÉ (Y.unop ‚ü∂ X) :=
  (equivShrink _).symm

lemma shrinkYoneda_obj_map_shrinkYonedaObjObjEquiv_symm
    {Y : C} {X X' : C} (f : X ‚ü∂ X') (g : X' ‚ü∂ Y) :
    (shrinkYoneda.{w}.obj Y).map f.op (shrinkYonedaObjObjEquiv.symm g) =
      (shrinkYonedaObjObjEquiv.symm (f ‚â´ g)) := by
  simp [shrinkYonedaObjObjEquiv, shrinkYoneda_obj]

noncomputable def shrinkYonedaFlipObjCompUliftFunctorIso (X : C) :
    shrinkYoneda.{w}.flip.obj (op X) ‚ãô uliftFunctor.{v} ‚âÖ
      coyoneda.obj (op X) ‚ãô uliftFunctor.{w} :=
  NatIso.ofComponents
    (fun Y ‚Ü¶ Equiv.toIso (Equiv.ulift.trans ((equivShrink _).symm.trans Equiv.ulift.symm)))
    (fun _ ‚Ü¶ by ext; simp [shrinkYoneda])

@[simps!]
noncomputable def shrinkYonedaMap
    {D : Type u'} [Category.{v'} D] [LocallySmall.{w} D] (F : C ‚•§ D) (X : C) :
    shrinkYoneda.{w}.obj X ‚ü∂ F.op ‚ãô shrinkYoneda.{w}.obj (F.obj X) where
  app X := equivShrink _ ‚àò F.map ‚àò (equivShrink _).symm
  naturality _ _ _ := by ext; simp [shrinkYoneda]

noncomputable def shrinkYonedaEquiv {X : C} {P : C·µí·µñ ‚•§ Type w} :
    (shrinkYoneda.{w}.obj X ‚ü∂ P) ‚âÉ P.obj (op X) where
  toFun œÑ := œÑ.app _ (equivShrink.{w} _ (ùüô X))
  invFun x :=
    { app Y f := P.map ((equivShrink.{w} _).symm f).op x
      naturality Y Z g := by ext; simp [shrinkYoneda] }
  left_inv œÑ := by
    ext Y f
    obtain ‚ü®f, rfl‚ü© := (equivShrink _).surjective f
    simpa [shrinkYoneda] using congr_fun (œÑ.naturality f.op).symm (equivShrink _ (ùüô X))
  right_inv x := by simp

@[simp]
lemma shrinkYonedaEquiv_app_shrinkYonedaObjObjEquiv_symm {X : C} {P : C·µí·µñ ‚•§ Type w}
    (x : P.obj (op X)) {Y : C} (f : Y ‚ü∂ X) :
    (shrinkYonedaEquiv.symm x).app (op Y) (shrinkYonedaObjObjEquiv.symm f) =
      P.map f.op x := by
  dsimp [shrinkYonedaEquiv, shrinkYonedaObjObjEquiv]
  apply congr_fun
  congr
  apply Equiv.symm_apply_apply

lemma map_shrinkYonedaEquiv {X Y : C} {P : C·µí·µñ ‚•§ Type w} (f : shrinkYoneda.obj X ‚ü∂ P)
    (g : Y ‚ü∂ X) : P.map g.op (shrinkYonedaEquiv f) =
      f.app (op Y) (shrinkYonedaObjObjEquiv.symm g) := by
  simp [shrinkYonedaObjObjEquiv, shrinkYonedaEquiv, shrinkYoneda,
    ‚Üê FunctorToTypes.naturality]

lemma shrinkYonedaEquiv_shrinkYoneda_map {X Y : C} (f : X ‚ü∂ Y) :
    shrinkYonedaEquiv (shrinkYoneda.{w}.map f) = shrinkYonedaObjObjEquiv.symm f := by
  simp [shrinkYonedaEquiv, shrinkYoneda, shrinkYonedaObjObjEquiv]

lemma shrinkYonedaEquiv_comp {X : C} {P Q : C·µí·µñ ‚•§ Type w} (Œ± : shrinkYoneda.obj X ‚ü∂ P)
    (Œ≤ : P ‚ü∂ Q) :
    shrinkYonedaEquiv (Œ± ‚â´ Œ≤) = Œ≤.app _ (shrinkYonedaEquiv Œ±) := by
  simp [shrinkYonedaEquiv]

lemma shrinkYonedaEquiv_naturality {X Y : C} {P : C·µí·µñ ‚•§ Type w}
    (f : shrinkYoneda.obj X ‚ü∂ P) (g : Y ‚ü∂ X) :
    P.map g.op (shrinkYonedaEquiv f) = shrinkYonedaEquiv (shrinkYoneda.map g ‚â´ f) := by
  simpa [shrinkYonedaEquiv, shrinkYoneda]
    using congr_fun (f.naturality g.op).symm ((equivShrink _) (ùüô _))

@[reassoc]
lemma shrinkYonedaEquiv_symm_map {X Y : C·µí·µñ} (f : X ‚ü∂ Y) {P : C·µí·µñ ‚•§ Type w} (t : P.obj X) :
    shrinkYonedaEquiv.symm (P.map f t) =
      shrinkYoneda.map f.unop ‚â´ shrinkYonedaEquiv.symm t :=
  shrinkYonedaEquiv.injective (by
    obtain ‚ü®t, rfl‚ü© := shrinkYonedaEquiv.surjective t
    rw [‚Üê shrinkYonedaEquiv_naturality]
    simp)

@[reassoc]
lemma shrinkYonedaEquiv_symm_comp {X : C·µí·µñ} {P Q : C·µí·µñ ‚•§ Type w} (x : P.obj X) (Œ± : P ‚ü∂ Q) :
    shrinkYonedaEquiv.symm x ‚â´ Œ± = shrinkYonedaEquiv.symm (Œ±.app _ x) :=
  shrinkYonedaEquiv.injective (by simp [shrinkYonedaEquiv])

instance (X : C) (J : Type*) [Category J] :
    PreservesLimitsOfShape J (shrinkYoneda.{w}.obj X) where
  preservesLimit {F} := ‚ü®fun {c} hc ‚Ü¶ by
    rw [Types.isLimit_iff_bijective_sectionOfCone]
    refine ‚ü®fun f‚ÇÅ f‚ÇÇ h ‚Ü¶ ?_, fun s ‚Ü¶ ?_‚ü©
    ¬∑ obtain ‚ü®f‚ÇÅ, rfl‚ü© := (equivShrink _).surjective f‚ÇÅ
      obtain ‚ü®f‚ÇÇ, rfl‚ü© := (equivShrink _).surjective f‚ÇÇ
      apply (equivShrink _).symm.injective
      simp only [Equiv.symm_apply_apply]
      apply Quiver.Hom.op_inj
      refine hc.hom_ext (fun j ‚Ü¶ Quiver.Hom.unop_inj ?_)
      have := congr_fun (congr_arg Subtype.val h) j
      simpa [shrinkYoneda] using congr_fun (congr_arg Subtype.val h) j
    ¬∑ refine ‚ü®equivShrink _ ((hc.homEquiv.symm
        { app j := ((equivShrink _).symm (s.1 j)).op
          naturality _ _ f := Quiver.Hom.unop_inj
            (by simp [-Functor.sections_property, shrinkYoneda, ‚Üê s.2 f])}).unop), ?_‚ü©
      ext
      apply (equivShrink _).symm.injective (Quiver.Hom.op_inj (by simp [shrinkYoneda]))‚ü©

end

end

end CategoryTheory
