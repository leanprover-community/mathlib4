/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.ExactCategory.Basic
import Mathlib.GroupTheory.FreeAbelianGroup
import Mathlib.GroupTheory.Subgroup.Basic
import Mathlib.GroupTheory.QuotientGroup

/-!
# The Grothendieck group of an exact category

-/

namespace CategoryTheory

open Category Limits ZeroObject

variable (C : Type _) [Category C] [Preadditive C] [HasZeroObject C]
  [HasBinaryBiproducts C] [ExactCategory C]

namespace ExactCategory

namespace GrothendieckGroup

inductive relations : Set (FreeAbelianGroup C)
  | ofShortExact (S : ShortComplex C) (hS : S ‚àà shortExact C) :
      relations (FreeAbelianGroup.of S.X‚ÇÅ + FreeAbelianGroup.of S.X‚ÇÉ -
        FreeAbelianGroup.of S.X‚ÇÇ)

end GrothendieckGroup

def GrothendieckGroup :=
  FreeAbelianGroup C ‚ß∏ AddSubgroup.closure (GrothendieckGroup.relations C)

instance : AddCommGroup (GrothendieckGroup C) := by
  dsimp [GrothendieckGroup]
  infer_instance

variable {C}

namespace GrothendieckGroup

def of (X : C) : GrothendieckGroup C :=
  QuotientAddGroup.mk' _ (FreeAbelianGroup.of X)

lemma additivity (S : ShortComplex C) (hS : S ‚àà shortExact C) :
    of S.X‚ÇÇ = of S.X‚ÇÅ + of S.X‚ÇÉ := by
  symm
  rw [‚Üê sub_eq_zero]
  apply (QuotientAddGroup.eq_zero_iff _).2
  apply AddSubgroup.subset_closure
  exact relations.ofShortExact S hS

@[simp]
lemma of_biprod (X‚ÇÅ X‚ÇÇ : C) :
    of (X‚ÇÅ ‚äû X‚ÇÇ) = of X‚ÇÅ + of X‚ÇÇ :=
  additivity _ (binaryBiproduct_shortExact X‚ÇÅ X‚ÇÇ)

lemma of_eq_zero_of_isZero (X : C) (hX : IsZero X) :
    of X = 0 := by
  simpa using additivity (ShortComplex.mk (ùüô X) (ùüô X) (hX.eq_of_src _ _))
    (shortExact_of_isZero _ hX hX hX)

variable (C)

@[simp]
lemma of_zero : of (0 : C) = 0 :=
  of_eq_zero_of_isZero _ (isZero_zero C)

variable {C}

lemma of_eq_of_iso {X Y : C} (e : X ‚âÖ Y) :
    of X = of Y := by
  simpa using additivity (ShortComplex.mk (0 : 0 ‚ü∂ X) e.hom zero_comp)
    (shortExact_of_isZero_of_isIso _ (isZero_zero C) (by dsimp ; infer_instance))

end GrothendieckGroup

end ExactCategory

end CategoryTheory
