/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Category.Preorder
import Mathlib.CategoryTheory.Limits.IsLimit
import Mathlib.Order.CompleteLattice.Basic

/-!
# The category of "pairwise intersections".

Given `Î¹ : Type v`, we build the diagram category `Pairwise Î¹`
with objects `single i` and `pair i j`, for `i j : Î¹`,
whose only non-identity morphisms are
`left : pair i j âŸ¶ single i` and `right : pair i j âŸ¶ single j`.

We use this later in describing (one formulation of) the sheaf condition.

Given any function `U : Î¹ â†’ Î±`, where `Î±` is some complete lattice (e.g. `(Opens X)áµ’áµ–`),
we produce a functor `Pairwise Î¹ â¥¤ Î±` in the obvious way,
and show that `iSup U` provides a colimit cocone over this functor.
-/


noncomputable section

universe v u

open CategoryTheory

open CategoryTheory.Limits

namespace CategoryTheory

/-- An inductive type representing either a single term of a type `Î¹`, or a pair of terms.
We use this as the objects of a category to describe the sheaf condition.
-/
inductive Pairwise (Î¹ : Type v)
  | single : Î¹ â†’ Pairwise Î¹
  | pair : Î¹ â†’ Î¹ â†’ Pairwise Î¹

variable {Î¹ : Type v}

namespace Pairwise

instance pairwiseInhabited [Inhabited Î¹] : Inhabited (Pairwise Î¹) :=
  âŸ¨single defaultâŸ©

/-- Morphisms in the category `Pairwise Î¹`. The only non-identity morphisms are
`left i j : single i âŸ¶ pair i j` and `right i j : single j âŸ¶ pair i j`.
-/
inductive Hom : Pairwise Î¹ â†’ Pairwise Î¹ â†’ Type v
  | id_single : âˆ€ i, Hom (single i) (single i)
  | id_pair : âˆ€ i j, Hom (pair i j) (pair i j)
  | left : âˆ€ i j, Hom (pair i j) (single i)
  | right : âˆ€ i j, Hom (pair i j) (single j)

open Hom

instance homInhabited [Inhabited Î¹] : Inhabited (Hom (single (default : Î¹)) (single default)) :=
  âŸ¨id_single defaultâŸ©

/-- The identity morphism in `Pairwise Î¹`.
-/
def id : âˆ€ o : Pairwise Î¹, Hom o o
  | single i => id_single i
  | pair i j => id_pair i j

/-- Composition of morphisms in `Pairwise Î¹`. -/
def comp : âˆ€ {oâ‚ oâ‚‚ oâ‚ƒ : Pairwise Î¹} (_ : Hom oâ‚ oâ‚‚) (_ : Hom oâ‚‚ oâ‚ƒ), Hom oâ‚ oâ‚ƒ
  | _, _, _, id_single _, g => g
  | _, _, _, id_pair _ _, g => g
  | _, _, _, left i j, id_single _ => left i j
  | _, _, _, right i j, id_single _ => right i j

instance : CategoryStruct (Pairwise Î¹) where
  Hom := Hom
  id := id
  comp := @comp _

section

open Lean Elab Tactic in
/-- A helper tactic for `cat_disch` and `Pairwise`. -/
def pairwiseCases : TacticM Unit := do
  evalTactic (â† `(tactic| casesm* (_ : Pairwise _) âŸ¶ (_ : Pairwise _)))

attribute [local aesop safe tactic (rule_sets := [CategoryTheory])] pairwiseCases in
instance : Category (Pairwise Î¹) where

end

variable {Î± : Type u} (U : Î¹ â†’ Î±)

section

variable [SemilatticeInf Î±]

/-- Auxiliary definition for `diagram`. -/
@[simp]
def diagramObj : Pairwise Î¹ â†’ Î±
  | single i => U i
  | pair i j => U i âŠ“ U j

/-- Auxiliary definition for `diagram`. -/
@[simp]
def diagramMap : âˆ€ {oâ‚ oâ‚‚ : Pairwise Î¹} (_ : oâ‚ âŸ¶ oâ‚‚), diagramObj U oâ‚ âŸ¶ diagramObj U oâ‚‚
  | _, _, id_single _ => ðŸ™ _
  | _, _, id_pair _ _ => ðŸ™ _
  | _, _, left _ _ => homOfLE inf_le_left
  | _, _, right _ _ => homOfLE inf_le_right

/-- Given a function `U : Î¹ â†’ Î±` for `[SemilatticeInf Î±]`, we obtain a functor `Pairwise Î¹ â¥¤ Î±`,
sending `single i` to `U i` and `pair i j` to `U i âŠ“ U j`,
and the morphisms to the obvious inequalities.
-/
@[simps]
def diagram : Pairwise Î¹ â¥¤ Î± where
  obj := diagramObj U
  map := diagramMap U

end

section

-- `CompleteLattice` is not really needed, as we only ever use `inf`,
-- but the appropriate structure has not been defined.
variable [CompleteLattice Î±]

/-- Auxiliary definition for `cocone`. -/
def coconeÎ¹App : âˆ€ o : Pairwise Î¹, diagramObj U o âŸ¶ iSup U
  | single i => homOfLE (le_iSup U i)
  | pair i _ => homOfLE inf_le_left â‰« homOfLE (le_iSup U i)

/-- Given a function `U : Î¹ â†’ Î±` for `[CompleteLattice Î±]`,
`iSup U` provides a cocone over `diagram U`.
-/
@[simps]
def cocone : Cocone (diagram U) where
  pt := iSup U
  Î¹ := { app := coconeÎ¹App U }

/-- Given a function `U : Î¹ â†’ Î±` for `[CompleteLattice Î±]`,
`iInf U` provides a limit cone over `diagram U`.
-/
def coconeIsColimit : IsColimit (cocone U) where
  desc s := homOfLE
    (by
      apply CompleteSemilatticeSup.sSup_le
      rintro _ âŸ¨j, rflâŸ©
      exact (s.Î¹.app (single j)).le)

end

end Pairwise

end CategoryTheory
