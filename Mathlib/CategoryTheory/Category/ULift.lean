/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import Mathlib.CategoryTheory.Category.Basic
import Mathlib.CategoryTheory.Equivalence
import Mathlib.CategoryTheory.EqToHom
import Mathlib.Data.ULift

/-!
# Basic API for ULift

This file contains a very basic API for working with the categorical
instance on `ULift C` where `C` is a type with a category instance.

1. `CategoryTheory.ULift.upFunctor` is the functorial version of the usual `ULift.up`.
2. `CategoryTheory.ULift.downFunctor` is the functorial version of the usual `ULift.down`.
3. `CategoryTheory.ULift.equivalence` is the categorical equivalence between
  `C` and `ULift C`.

# ULiftHom

Given a type `C : Type u`, `ULiftHom.{w} C` is just an alias for `C`.
If we have `category.{v} C`, then `ULiftHom.{w} C` is endowed with a category instance
whose morphisms are obtained by applying `ULift.{w}` to the morphisms from `C`.

This is a category equivalent to `C`. The forward direction of the equivalence is `ULiftHom.up`,
the backward direction is `ULiftHom.down` and the equivalence is `ULiftHom.equiv`.

# AsSmall

This file also contains a construction which takes a type `C : Type u` with a
category instance `Category.{v} C` and makes a small category
`AsSmall.{w} C : Type (max w v u)` equivalent to `C`.

The forward direction of the equivalence, `C â¥¤ AsSmall C`, is denoted `AsSmall.up`
and the backward direction is `AsSmall.down`. The equivalence itself is `AsSmall.equiv`.
-/

universe wâ‚ vâ‚ vâ‚‚ uâ‚ uâ‚‚

namespace CategoryTheory

variable {C : Type uâ‚} [Category.{vâ‚} C]

/-- The functorial version of `ULift.up`. -/
@[simps]
def ULift.upFunctor : C â¥¤ ULift.{uâ‚‚} C where
  obj := ULift.up
  map f := f

/-- The functorial version of `ULift.down`. -/
@[simps]
def ULift.downFunctor : ULift.{uâ‚‚} C â¥¤ C where
  obj := ULift.down
  map f := f

/-- The categorical equivalence between `C` and `ULift C`. -/
@[simps]
def ULift.equivalence : C â‰Œ ULift.{uâ‚‚} C where
  functor := ULift.upFunctor
  inverse := ULift.downFunctor
  unitIso :=
    { hom := ğŸ™ _
      inv := ğŸ™ _ }
  counitIso :=
    { hom :=
        { app := fun X => ğŸ™ _
          naturality := fun X Y f => by
            change f â‰« ğŸ™ _ = ğŸ™ _ â‰« f
            simp }
      inv :=
        { app := fun X => ğŸ™ _
          naturality := fun X Y f => by
            change f â‰« ğŸ™ _ = ğŸ™ _ â‰« f
            simp }
      hom_inv_id := by
        ext
        change ğŸ™ _ â‰« ğŸ™ _ = ğŸ™ _
        simp
      inv_hom_id := by
        ext
        change ğŸ™ _ â‰« ğŸ™ _ = ğŸ™ _
        simp }
  functor_unitIso_comp X := by
    change ğŸ™ X â‰« ğŸ™ X = ğŸ™ X
    simp

section ULiftHom
/- Porting note: obviously we don't want code that looks like this long term
the ability to turn off unused universe parameter error is desirable -/
/-- `ULiftHom.{w} C` is an alias for `C`, which is endowed with a category instance
  whose morphisms are obtained by applying `ULift.{w}` to the morphisms from `C`.
-/
def ULiftHom.{w,u} (C : Type u) : Type u :=
  let _ := ULift.{w} C
  C

instance {C} [Inhabited C] : Inhabited (ULiftHom C) :=
  âŸ¨(default : C)âŸ©

/-- The obvious function `ULiftHom C â†’ C`. -/
def ULiftHom.objDown {C} (A : ULiftHom C) : C :=
  A

/-- The obvious function `C â†’ ULiftHom C`. -/
def ULiftHom.objUp {C} (A : C) : ULiftHom C :=
  A

@[simp]
theorem objDown_objUp {C} (A : C) : (ULiftHom.objUp A).objDown = A :=
  rfl

@[simp]
theorem objUp_objDown {C} (A : ULiftHom C) : ULiftHom.objUp A.objDown = A :=
  rfl

instance ULiftHom.category : Category.{max vâ‚‚ vâ‚} (ULiftHom.{vâ‚‚} C) where
  Hom A B := ULift.{vâ‚‚} <| A.objDown âŸ¶ B.objDown
  id A := âŸ¨ğŸ™ _âŸ©
  comp f g := âŸ¨f.down â‰« g.downâŸ©

/-- One half of the quivalence between `C` and `ULiftHom C`. -/
@[simps]
def ULiftHom.up : C â¥¤ ULiftHom C where
  obj := ULiftHom.objUp
  map f := âŸ¨fâŸ©

/-- One half of the quivalence between `C` and `ULiftHom C`. -/
@[simps]
def ULiftHom.down : ULiftHom C â¥¤ C where
  obj := ULiftHom.objDown
  map f := f.down

/-- The equivalence between `C` and `ULiftHom C`. -/
def ULiftHom.equiv : C â‰Œ ULiftHom C where
  functor := ULiftHom.up
  inverse := ULiftHom.down
  unitIso := NatIso.ofComponents fun A => eqToIso rfl
  counitIso := NatIso.ofComponents fun A => eqToIso rfl

end ULiftHom
/- Porting note: we want to keep around the category instance on `D`
so Lean can figure out things further down. So `AsSmall` has been
nolinted. -/
/-- `AsSmall C` is a small category equivalent to `C`.
  More specifically, if `C : Type u` is endowed with `Category.{v} C`, then
  `AsSmall.{w} C : Type (max w v u)` is endowed with an instance of a small category.

  The objects and morphisms of `AsSmall C` are defined by applying `ULift` to the
  objects and morphisms of `C`.

  Note: We require a category instance for this definition in order to have direct
  access to the universe level `v`.
-/
@[nolint unusedArguments]
def AsSmall.{w, v, u} (D : Type u) [Category.{v} D] := ULift.{max w v} D

instance : SmallCategory (AsSmall.{wâ‚} C) where
  Hom X Y := ULift.{max wâ‚ uâ‚} <| X.down âŸ¶ Y.down
  id X := âŸ¨ğŸ™ _âŸ©
  comp f g := âŸ¨f.down â‰« g.downâŸ©

/-- One half of the equivalence between `C` and `AsSmall C`. -/
@[simps]
def AsSmall.up : C â¥¤ AsSmall C where
  obj X := âŸ¨XâŸ©
  map f := âŸ¨fâŸ©

/-- One half of the equivalence between `C` and `AsSmall C`. -/
@[simps]
def AsSmall.down : AsSmall C â¥¤ C where
  obj X := ULift.down X
  map f := f.down

/-- The equivalence between `C` and `AsSmall C`. -/
@[simps]
def AsSmall.equiv : C â‰Œ AsSmall C where
  functor := AsSmall.up
  inverse := AsSmall.down
  unitIso := NatIso.ofComponents fun X => eqToIso rfl
  counitIso := NatIso.ofComponents fun X => eqToIso <| ULift.ext _ _ rfl

instance [Inhabited C] : Inhabited (AsSmall C) :=
  âŸ¨âŸ¨defaultâŸ©âŸ©

/-- The equivalence between `C` and `ULiftHom (ULift C)`. -/
def ULiftHomULiftCategory.equiv.{v', u', v, u} (C : Type u) [Category.{v} C] :
    C â‰Œ ULiftHom.{v'} (ULift.{u'} C) :=
  ULift.equivalence.trans ULiftHom.equiv

end CategoryTheory
