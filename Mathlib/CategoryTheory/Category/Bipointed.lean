/-
Copyright (c) 2022 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies
-/
import Mathlib.CategoryTheory.Category.Pointed

/-!
# The category of bipointed types

This defines `Bipointed`, the category of bipointed types.

## TODO

Monoidal structure
-/


open CategoryTheory

universe u

/-- The category of bipointed types. -/
structure Bipointed : Type (u + 1) where
  /-- The underlying type of a bipointed type. -/
  protected X : Type u
  /-- The two points of a bipointed type, bundled together as a pair. -/
  toProd : X √ó X

namespace Bipointed

instance : CoeSort Bipointed Type* := ‚ü®Bipointed.X‚ü©

/-- Turns a bipointing into a bipointed type. -/
abbrev of {X : Type*} (to_prod : X √ó X) : Bipointed :=
  ‚ü®X, to_prod‚ü©

theorem coe_of {X : Type*} (to_prod : X √ó X) : ‚Ü•(of to_prod) = X :=
  rfl

alias _root_.Prod.Bipointed := of

instance : Inhabited Bipointed :=
  ‚ü®of ((), ())‚ü©

/-- Morphisms in `Bipointed`. -/
@[ext]
protected structure Hom (X Y : Bipointed.{u}) : Type u where
  /-- The underlying function of a morphism of bipointed types. -/
  toFun : X ‚Üí Y
  map_fst : toFun X.toProd.1 = Y.toProd.1
  map_snd : toFun X.toProd.2 = Y.toProd.2

namespace Hom

/-- The identity morphism of `X : Bipointed`. -/
@[simps]
nonrec def id (X : Bipointed) : Bipointed.Hom X X :=
  ‚ü®id, rfl, rfl‚ü©

instance (X : Bipointed) : Inhabited (Bipointed.Hom X X) :=
  ‚ü®id X‚ü©

/-- Composition of morphisms of `Bipointed`. -/
@[simps]
def comp {X Y Z : Bipointed.{u}} (f : Bipointed.Hom X Y) (g : Bipointed.Hom Y Z) :
    Bipointed.Hom X Z :=
  ‚ü®g.toFun ‚àò f.toFun, by rw [Function.comp_apply, f.map_fst, g.map_fst], by
    rw [Function.comp_apply, f.map_snd, g.map_snd]‚ü©

end Hom

instance largeCategory : LargeCategory Bipointed where
  Hom := Bipointed.Hom
  id := Hom.id
  comp := @Hom.comp

/-- The subtype of functions corresponding to the morphisms in `Bipointed`. -/
abbrev HomSubtype (X Y : Bipointed) :=
  { f : X ‚Üí Y // f X.toProd.1 = Y.toProd.1 ‚àß f X.toProd.2 = Y.toProd.2 }

instance (X Y : Bipointed) : FunLike (HomSubtype X Y) X Y where
  coe f := f
  coe_injective' _ _ := Subtype.ext

instance hasForget : ConcreteCategory Bipointed HomSubtype where
  hom f := ‚ü®f.1, ‚ü®f.2, f.3‚ü©‚ü©
  ofHom f := ‚ü®f.1, f.2.1, f.2.2‚ü©

/-- Swaps the pointed elements of a bipointed type. `Prod.swap` as a functor. -/
@[simps]
def swap : Bipointed ‚•§ Bipointed where
  obj X := ‚ü®X, X.toProd.swap‚ü©
  map f := ‚ü®f.toFun, f.map_snd, f.map_fst‚ü©

/-- The equivalence between `Bipointed` and itself induced by `Prod.swap` both ways. -/
@[simps!]
def swapEquiv : Bipointed ‚âå Bipointed where
  functor := swap
  inverse := swap
  unitIso := Iso.refl _
  counitIso := Iso.refl _

@[simp]
theorem swapEquiv_symm : swapEquiv.symm = swapEquiv :=
  rfl

end Bipointed

/-- The forgetful functor from `Bipointed` to `Pointed` which forgets about the second point. -/
def bipointedToPointedFst : Bipointed ‚•§ Pointed where
  obj X := ‚ü®X, X.toProd.1‚ü©
  map f := ‚ü®f.toFun, f.map_fst‚ü©

/-- The forgetful functor from `Bipointed` to `Pointed` which forgets about the first point. -/
def bipointedToPointedSnd : Bipointed ‚•§ Pointed where
  obj X := ‚ü®X, X.toProd.2‚ü©
  map f := ‚ü®f.toFun, f.map_snd‚ü©

@[simp]
theorem bipointedToPointedFst_comp_forget :
    bipointedToPointedFst ‚ãô forget Pointed = forget Bipointed :=
  rfl

@[simp]
theorem bipointedToPointedSnd_comp_forget :
    bipointedToPointedSnd ‚ãô forget Pointed = forget Bipointed :=
  rfl

@[simp]
theorem swap_comp_bipointedToPointedFst :
    Bipointed.swap ‚ãô bipointedToPointedFst = bipointedToPointedSnd :=
  rfl

@[simp]
theorem swap_comp_bipointedToPointedSnd :
    Bipointed.swap ‚ãô bipointedToPointedSnd = bipointedToPointedFst :=
  rfl

/-- The functor from `Pointed` to `Bipointed` which bipoints the point. -/
def pointedToBipointed : Pointed.{u} ‚•§ Bipointed where
  obj X := ‚ü®X, X.point, X.point‚ü©
  map f := ‚ü®f.toFun, f.map_point, f.map_point‚ü©

/-- The functor from `Pointed` to `Bipointed` which adds a second point. -/
def pointedToBipointedFst : Pointed.{u} ‚•§ Bipointed where
  obj X := ‚ü®Option X, X.point, none‚ü©
  map f := ‚ü®Option.map f.toFun, congr_arg _ f.map_point, rfl‚ü©
  map_id _ := Bipointed.Hom.ext Option.map_id
  map_comp f g := Bipointed.Hom.ext (Option.map_comp_map f.1 g.1).symm

/-- The functor from `Pointed` to `Bipointed` which adds a first point. -/
def pointedToBipointedSnd : Pointed.{u} ‚•§ Bipointed where
  obj X := ‚ü®Option X, none, X.point‚ü©
  map f := ‚ü®Option.map f.toFun, rfl, congr_arg _ f.map_point‚ü©
  map_id _ := Bipointed.Hom.ext Option.map_id
  map_comp f g := Bipointed.Hom.ext (Option.map_comp_map f.1 g.1).symm

@[simp]
theorem pointedToBipointedFst_comp_swap :
    pointedToBipointedFst ‚ãô Bipointed.swap = pointedToBipointedSnd :=
  rfl

@[simp]
theorem pointedToBipointedSnd_comp_swap :
    pointedToBipointedSnd ‚ãô Bipointed.swap = pointedToBipointedFst :=
  rfl

/-- `BipointedToPointed_fst` is inverse to `PointedToBipointed`. -/
@[simps!]
def pointedToBipointedCompBipointedToPointedFst :
    pointedToBipointed ‚ãô bipointedToPointedFst ‚âÖ ùü≠ _ :=
  NatIso.ofComponents fun X ‚Ü¶
    { hom := ‚ü®id, rfl‚ü©
      inv := ‚ü®id, rfl‚ü© }

/-- `BipointedToPointed_snd` is inverse to `PointedToBipointed`. -/
@[simps!]
def pointedToBipointedCompBipointedToPointedSnd :
    pointedToBipointed ‚ãô bipointedToPointedSnd ‚âÖ ùü≠ _ :=
  NatIso.ofComponents fun X ‚Ü¶
    { hom := ‚ü®id, rfl‚ü©
      inv := ‚ü®id, rfl‚ü© }

/-- The free/forgetful adjunction between `PointedToBipointed_fst` and `BipointedToPointed_fst`.
-/
def pointedToBipointedFstBipointedToPointedFstAdjunction :
    pointedToBipointedFst ‚ä£ bipointedToPointedFst :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun X Y ‚Ü¶
        { toFun := fun f ‚Ü¶ ‚ü®f.toFun ‚àò Option.some, f.map_fst‚ü©
          invFun := fun f ‚Ü¶ ‚ü®fun o ‚Ü¶ o.elim Y.toProd.2 f.toFun, f.map_point, rfl‚ü©
          left_inv := fun f ‚Ü¶ by
            apply Bipointed.Hom.ext
            funext x
            cases x
            ¬∑ exact f.map_snd.symm
            ¬∑ rfl }
      homEquiv_naturality_left_symm := fun f g ‚Ü¶ by
        apply Bipointed.Hom.ext
        funext x
        cases x <;> rfl }

/-- The free/forgetful adjunction between `PointedToBipointed_snd` and `BipointedToPointed_snd`.
-/
def pointedToBipointedSndBipointedToPointedSndAdjunction :
    pointedToBipointedSnd ‚ä£ bipointedToPointedSnd :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun X Y ‚Ü¶
        { toFun := fun f ‚Ü¶ ‚ü®f.toFun ‚àò Option.some, f.map_snd‚ü©
          invFun := fun f ‚Ü¶ ‚ü®fun o ‚Ü¶ o.elim Y.toProd.1 f.toFun, rfl, f.map_point‚ü©
          left_inv := fun f ‚Ü¶ by
            apply Bipointed.Hom.ext
            funext x
            cases x
            ¬∑ exact f.map_fst.symm
            ¬∑ rfl }
      homEquiv_naturality_left_symm := fun f g ‚Ü¶ by
        apply Bipointed.Hom.ext
        funext x
        cases x <;> rfl }
