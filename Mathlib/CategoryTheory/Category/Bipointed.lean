/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.CategoryTheory.Category.Pointed

#align_import category_theory.category.Bipointed from "leanprover-community/mathlib"@"c8ab806ef73c20cab1d87b5157e43a82c205f28e"

/-!
# The category of bipointed types

This defines `Bipointed`, the category of bipointed types.

## TODO

Monoidal structure
-/


open CategoryTheory

universe u

variable {Î± Î² : Type*}

set_option linter.uppercaseLean3 false

/-- The category of bipointed types. -/
structure Bipointed : Type (u + 1) where
  /-- The underlying type of a bipointed type. -/
  protected X : Type u
  /-- The two points of a bipointed type, bundled together as a pair. -/
  toProd : X Ã— X
#align Bipointed Bipointed

namespace Bipointed

instance : CoeSort Bipointed Type* := âŸ¨Bipointed.XâŸ©

/-- Turns a bipointing into a bipointed type. -/
def of {X : Type*} (to_prod : X Ã— X) : Bipointed :=
  âŸ¨X, to_prodâŸ©
#align Bipointed.of Bipointed.of

@[simp]
theorem coe_of {X : Type*} (to_prod : X Ã— X) : â†¥(of to_prod) = X :=
  rfl
#align Bipointed.coe_of Bipointed.coe_of

alias _root_.Prod.Bipointed := of
#align prod.Bipointed Prod.Bipointed

instance : Inhabited Bipointed :=
  âŸ¨of ((), ())âŸ©

/-- Morphisms in `Bipointed`. -/
@[ext]
protected structure Hom (X Y : Bipointed.{u}) : Type u where
  /-- The underlying function of a morphism of bipointed types. -/
  toFun : X â†’ Y
  map_fst : toFun X.toProd.1 = Y.toProd.1
  map_snd : toFun X.toProd.2 = Y.toProd.2
#align Bipointed.hom Bipointed.Hom

namespace Hom

/-- The identity morphism of `X : Bipointed`. -/
@[simps]
nonrec def id (X : Bipointed) : Bipointed.Hom X X :=
  âŸ¨id, rfl, rflâŸ©
#align Bipointed.hom.id Bipointed.Hom.id

instance (X : Bipointed) : Inhabited (Bipointed.Hom X X) :=
  âŸ¨id XâŸ©

/-- Composition of morphisms of `Bipointed`. -/
@[simps]
def comp {X Y Z : Bipointed.{u}} (f : Bipointed.Hom X Y) (g : Bipointed.Hom Y Z) :
    Bipointed.Hom X Z :=
  âŸ¨g.toFun âˆ˜ f.toFun, by rw [Function.comp_apply, f.map_fst, g.map_fst], by
    rw [Function.comp_apply, f.map_snd, g.map_snd]âŸ©
#align Bipointed.hom.comp Bipointed.Hom.comp

end Hom

instance largeCategory : LargeCategory Bipointed where
  Hom := Bipointed.Hom
  id := Hom.id
  comp := @Hom.comp
#align Bipointed.large_category Bipointed.largeCategory

instance concreteCategory : ConcreteCategory Bipointed where
  forget :=
    { obj := Bipointed.X
      map := @Hom.toFun }
  forget_faithful := âŸ¨@Hom.extâŸ©
#align Bipointed.concrete_category Bipointed.concreteCategory

/-- Swaps the pointed elements of a bipointed type. `Prod.swap` as a functor. -/
@[simps]
def swap : Bipointed â¥¤ Bipointed where
  obj X := âŸ¨X, X.toProd.swapâŸ©
  map f := âŸ¨f.toFun, f.map_snd, f.map_fstâŸ©
#align Bipointed.swap Bipointed.swap

/-- The equivalence between `Bipointed` and itself induced by `Prod.swap` both ways. -/
@[simps!]
def swapEquiv : Bipointed â‰Œ Bipointed :=
  CategoryTheory.Equivalence.mk swap swap
    (NatIso.ofComponents fun X =>
        { hom := âŸ¨id, rfl, rflâŸ©
          inv := âŸ¨id, rfl, rflâŸ© })
    (NatIso.ofComponents fun X =>
        { hom := âŸ¨id, rfl, rflâŸ©
          inv := âŸ¨id, rfl, rflâŸ© })
#align Bipointed.swap_equiv Bipointed.swapEquiv

@[simp]
theorem swapEquiv_symm : swapEquiv.symm = swapEquiv :=
  rfl
#align Bipointed.swap_equiv_symm Bipointed.swapEquiv_symm

end Bipointed

/-- The forgetful functor from `Bipointed` to `Pointed` which forgets about the second point. -/
def bipointedToPointedFst : Bipointed â¥¤ Pointed where
  obj X := âŸ¨X, X.toProd.1âŸ©
  map f := âŸ¨f.toFun, f.map_fstâŸ©
#align Bipointed_to_Pointed_fst bipointedToPointedFst

/-- The forgetful functor from `Bipointed` to `Pointed` which forgets about the first point. -/
def bipointedToPointedSnd : Bipointed â¥¤ Pointed where
  obj X := âŸ¨X, X.toProd.2âŸ©
  map f := âŸ¨f.toFun, f.map_sndâŸ©
#align Bipointed_to_Pointed_snd bipointedToPointedSnd

@[simp]
theorem bipointedToPointedFst_comp_forget :
    bipointedToPointedFst â‹™ forget Pointed = forget Bipointed :=
  rfl
#align Bipointed_to_Pointed_fst_comp_forget bipointedToPointedFst_comp_forget

@[simp]
theorem bipointedToPointedSnd_comp_forget :
    bipointedToPointedSnd â‹™ forget Pointed = forget Bipointed :=
  rfl
#align Bipointed_to_Pointed_snd_comp_forget bipointedToPointedSnd_comp_forget

@[simp]
theorem swap_comp_bipointedToPointedFst :
    Bipointed.swap â‹™ bipointedToPointedFst = bipointedToPointedSnd :=
  rfl
#align swap_comp_Bipointed_to_Pointed_fst swap_comp_bipointedToPointedFst

@[simp]
theorem swap_comp_bipointedToPointedSnd :
    Bipointed.swap â‹™ bipointedToPointedSnd = bipointedToPointedFst :=
  rfl
#align swap_comp_Bipointed_to_Pointed_snd swap_comp_bipointedToPointedSnd

/-- The functor from `Pointed` to `Bipointed` which bipoints the point. -/
def pointedToBipointed : Pointed.{u} â¥¤ Bipointed where
  obj X := âŸ¨X, X.point, X.pointâŸ©
  map f := âŸ¨f.toFun, f.map_point, f.map_pointâŸ©
#align Pointed_to_Bipointed pointedToBipointed

/-- The functor from `Pointed` to `Bipointed` which adds a second point. -/
def pointedToBipointedFst : Pointed.{u} â¥¤ Bipointed where
  obj X := âŸ¨Option X, X.point, noneâŸ©
  map f := âŸ¨Option.map f.toFun, congr_arg _ f.map_point, rflâŸ©
  map_id _ := Bipointed.Hom.ext _ _ Option.map_id
  map_comp f g := Bipointed.Hom.ext _ _ (Option.map_comp_map f.1 g.1).symm
#align Pointed_to_Bipointed_fst pointedToBipointedFst

/-- The functor from `Pointed` to `Bipointed` which adds a first point. -/
def pointedToBipointedSnd : Pointed.{u} â¥¤ Bipointed where
  obj X := âŸ¨Option X, none, X.pointâŸ©
  map f := âŸ¨Option.map f.toFun, rfl, congr_arg _ f.map_pointâŸ©
  map_id _ := Bipointed.Hom.ext _ _ Option.map_id
  map_comp f g := Bipointed.Hom.ext _ _ (Option.map_comp_map f.1 g.1).symm
#align Pointed_to_Bipointed_snd pointedToBipointedSnd

@[simp]
theorem pointedToBipointedFst_comp_swap :
    pointedToBipointedFst â‹™ Bipointed.swap = pointedToBipointedSnd :=
  rfl
#align Pointed_to_Bipointed_fst_comp_swap pointedToBipointedFst_comp_swap

@[simp]
theorem pointedToBipointedSnd_comp_swap :
    pointedToBipointedSnd â‹™ Bipointed.swap = pointedToBipointedFst :=
  rfl
#align Pointed_to_Bipointed_snd_comp_swap pointedToBipointedSnd_comp_swap

/-- `BipointedToPointed_fst` is inverse to `PointedToBipointed`. -/
@[simps!]
def pointedToBipointedCompBipointedToPointedFst :
    pointedToBipointed â‹™ bipointedToPointedFst â‰… ðŸ­ _ :=
  NatIso.ofComponents fun X =>
    { hom := âŸ¨id, rflâŸ©
      inv := âŸ¨id, rflâŸ© }
#align Pointed_to_Bipointed_comp_Bipointed_to_Pointed_fst pointedToBipointedCompBipointedToPointedFst

/-- `BipointedToPointed_snd` is inverse to `PointedToBipointed`. -/
@[simps!]
def pointedToBipointedCompBipointedToPointedSnd :
    pointedToBipointed â‹™ bipointedToPointedSnd â‰… ðŸ­ _ :=
  NatIso.ofComponents fun X =>
    { hom := âŸ¨id, rflâŸ©
      inv := âŸ¨id, rflâŸ© }
#align Pointed_to_Bipointed_comp_Bipointed_to_Pointed_snd pointedToBipointedCompBipointedToPointedSnd

/-- The free/forgetful adjunction between `PointedToBipointed_fst` and `BipointedToPointed_fst`.
-/
def pointedToBipointedFstBipointedToPointedFstAdjunction :
    pointedToBipointedFst âŠ£ bipointedToPointedFst :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun X Y =>
        { toFun := fun f => âŸ¨f.toFun âˆ˜ Option.some, f.map_fstâŸ©
          invFun := fun f => âŸ¨fun o => o.elim Y.toProd.2 f.toFun, f.map_point, rflâŸ©
          left_inv := fun f => by
            apply Bipointed.Hom.ext
            funext x
            cases x
            Â· exact f.map_snd.symm
            Â· rfl
          right_inv := fun f => Pointed.Hom.ext _ _ rfl }
      homEquiv_naturality_left_symm := fun f g => by
        apply Bipointed.Hom.ext
        funext x
        cases x <;> rfl }
#align Pointed_to_Bipointed_fst_Bipointed_to_Pointed_fst_adjunction pointedToBipointedFstBipointedToPointedFstAdjunction

/-- The free/forgetful adjunction between `PointedToBipointed_snd` and `BipointedToPointed_snd`.
-/
def pointedToBipointedSndBipointedToPointedSndAdjunction :
    pointedToBipointedSnd âŠ£ bipointedToPointedSnd :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun X Y =>
        { toFun := fun f => âŸ¨f.toFun âˆ˜ Option.some, f.map_sndâŸ©
          invFun := fun f => âŸ¨fun o => o.elim Y.toProd.1 f.toFun, rfl, f.map_pointâŸ©
          left_inv := fun f => by
            apply Bipointed.Hom.ext
            funext x
            cases x
            Â· exact f.map_fst.symm
            Â· rfl
          right_inv := fun f => Pointed.Hom.ext _ _ rfl }
      homEquiv_naturality_left_symm := fun f g => by
        apply Bipointed.Hom.ext
        funext x
        cases x <;> rfl }
#align Pointed_to_Bipointed_snd_Bipointed_to_Pointed_snd_adjunction pointedToBipointedSndBipointedToPointedSndAdjunction
