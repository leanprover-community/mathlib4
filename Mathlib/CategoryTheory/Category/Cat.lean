/-
Copyright (c) 2019 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
module

public import Mathlib.CategoryTheory.Bicategory.Strict.Basic
public import Mathlib.CategoryTheory.ConcreteCategory.Bundled
public import Mathlib.CategoryTheory.Discrete.Basic
public import Mathlib.CategoryTheory.Types.Basic

/-!
# Category of categories

This file contains the definition of the category `Cat` of all categories.
In this category objects are categories and
morphisms are functors between these categories.

## Implementation notes

Though `Cat` is not a concrete category, we use `bundled` to define
its carrier type.
-/

@[expose] public section


universe v u

namespace CategoryTheory

open Bicategory Functor

-- intended to be used with explicit universe parameters
/-- Category of categories. -/
@[nolint checkUnivs]
def Cat :=
  Bundled Category.{v, u}

namespace Cat

instance : Inhabited Cat :=
  ‚ü®‚ü®Type u, CategoryTheory.types‚ü©‚ü©

-- TODO: maybe this coercion should be defined to be `objects.obj`?
instance : CoeSort Cat (Type u) :=
  ‚ü®Bundled.Œ±‚ü©

instance str (C : Cat.{v, u}) : Category.{v, u} C :=
  Bundled.str C

/-- Construct a bundled `Cat` from the underlying type and the typeclass. -/
def of (C : Type u) [Category.{v} C] : Cat.{v, u} :=
  Bundled.of C

section

#adaptation_note /-- Removed `private`:
`ofFunctor` was marked `private` in #31807,
but we have removed this when disabling `set_option backward.privateInPublic` as a global option. -/
/--
The type of 1-morphisms in the bicategory of categories `Cat`.
This is a structure around `Functor` to prevent defeq-abuse
-/
@[ext]
structure Hom (C D : Cat.{v, u}) where
  ofFunctor ::
  /-- The Functor underlying a 1-morphism in Cat -/
  toFunctor : C ‚•§ D

instance : Quiver (Cat.{v, u}) where
  Hom C D := Hom C D

/-- The 1-morphism in `Cat` corresponding to a functor. -/
@[simps]
def _root_.CategoryTheory.Functor.toCatHom {C D : Type u} [Category.{v} C] [Category.{v} D]
    (F : C ‚•§ D) : Cat.of C ‚ü∂ Cat.of D where
  toFunctor := F

@[ext]
lemma ext {C D : Cat.{v, u}} {F G : C ‚ü∂ D} (h : F.toFunctor = G.toFunctor) : F = G :=
  congrArg (Functor.toCatHom) h

/--
The equivalence between the type of functors between two categories and
the type of 1-morphisms in Cat between the objects corresponding to those categories.
-/
@[simps]
def _root_.CategoryTheory.Functor.equivCatHom (C D : Type u) [Category.{v} C] [Category.{v} D] :
    C ‚•§ D ‚âÉ ((Cat.of C) ‚ü∂ (Cat.of D)) where
  toFun := Functor.toCatHom
  invFun := Cat.Hom.toFunctor
  left_inv _ := rfl
  right_inv _ := rfl

/--
The equivalence between the type of 1-morphisms in Cat between two objects
and the type of functors between the categories corresponding to those objects.
-/
@[simps! apply symm_apply]
def Hom.equivFunctor (C D : Cat.{v, u}) :
    (C ‚ü∂ D) ‚âÉ C ‚•§ D := (equivCatHom _ _).symm

#adaptation_note /-- Removed `private`:
`ofNatTrans` was marked `private` in #31807,
but we have removed this when disabling `set_option backward.privateInPublic` as a global option. -/
/--
The type of 2-morphisms in the bicategory of categories `Cat`.
This is a wrapper around `NatTrans` to prevent defeq-abuse.
-/
structure Hom‚ÇÇ {C D : Cat.{v, u}} (F G : C ‚ü∂ D) where
  ofNatTrans ::
  /-- The natural transformation underlying a 2-morphism in `Cat` -/
  toNatTrans : F.toFunctor ‚ü∂ G.toFunctor

namespace Hom

instance instQuiver {C D : Cat.{v, u}} : Quiver (C ‚ü∂ D) where
  Hom F G := Hom‚ÇÇ F G

/-- The 2-morphism in `Cat` corresponding to a natural transformation between functors. -/
@[simps]
def _root_.CategoryTheory.NatTrans.toCatHom‚ÇÇ {C D : Type u} [Category.{v} C]
    [Category.{v} D] {F G : C ‚•§ D} (Œ∑ : F ‚ü∂ G) : F.toCatHom ‚ü∂ G.toCatHom where
  toNatTrans := Œ∑

instance instCategory {X Y : Cat.{v, u}} : Category (X ‚ü∂ Y) where
  id F := NatTrans.toCatHom‚ÇÇ (ùüô F.toFunctor)
  comp Œ∑‚ÇÅ Œ∑‚ÇÇ := NatTrans.toCatHom‚ÇÇ (Œ∑‚ÇÅ.toNatTrans ‚â´ Œ∑‚ÇÇ.toNatTrans)
  id_comp Œ∑ := congrArg (NatTrans.toCatHom‚ÇÇ) (Category.id_comp Œ∑.toNatTrans)
  comp_id Œ∑ := congrArg (NatTrans.toCatHom‚ÇÇ) (Category.comp_id Œ∑.toNatTrans)
  assoc Œ∑‚ÇÅ Œ∑‚ÇÇ Œ∑‚ÇÉ :=
    congrArg (NatTrans.toCatHom‚ÇÇ) (Category.assoc Œ∑‚ÇÅ.toNatTrans Œ∑‚ÇÇ.toNatTrans Œ∑‚ÇÉ.toNatTrans)

@[simp, push_cast]
lemma _root_.CategoryTheory.NatTrans.toCatHom‚ÇÇ_id {C D : Type u} [Category.{v} C] [Category.{v} D]
    (F : C ‚•§ D) :
    (ùüô F : F ‚ü∂ F).toCatHom‚ÇÇ = ùüô F.toCatHom := rfl

@[simp, push_cast]
lemma _root_.CategoryTheory.NatTrans.toCatHom‚ÇÇ_comp {C D : Type u} [Category.{v} C] [Category.{v} D]
    {F G H : C ‚•§ D} (Œ∑‚ÇÅ : F ‚ü∂ G) (Œ∑‚ÇÇ : G ‚ü∂ H) :
    (Œ∑‚ÇÅ ‚â´ Œ∑‚ÇÇ).toCatHom‚ÇÇ = Œ∑‚ÇÅ.toCatHom‚ÇÇ ‚â´ Œ∑‚ÇÇ.toCatHom‚ÇÇ := rfl

@[simp, push_cast]
lemma toNatTrans_id {C D : Cat.{v, u}} (F : C ‚ü∂ D) :
  (ùüô F : F ‚ü∂ F).toNatTrans = ùüô (F.toFunctor) := rfl

@[simp, push_cast]
lemma toNatTrans_comp {C D : Cat.{v, u}} {F G H : C ‚ü∂ D} (Œ∑‚ÇÅ : F ‚ü∂ G) (Œ∑‚ÇÇ : G ‚ü∂ H) :
  (Œ∑‚ÇÅ ‚â´ Œ∑‚ÇÇ).toNatTrans = Œ∑‚ÇÅ.toNatTrans ‚â´ Œ∑‚ÇÇ.toNatTrans := rfl

@[ext]
lemma _root_.CategoryTheory.Cat.Hom‚ÇÇ.ext {C D : Cat.{v, u}} {F G : C ‚ü∂ D} {Œ∑‚ÇÅ Œ∑‚ÇÇ : F ‚ü∂ G}
    (h : Œ∑‚ÇÅ.toNatTrans = Œ∑‚ÇÇ.toNatTrans) : Œ∑‚ÇÅ = Œ∑‚ÇÇ := congr($(h).toCatHom‚ÇÇ)

/-- The 2-iso in Cat corresponding to a natural isomorphism. -/
@[simps]
def isoMk {C D : Type u} [Category.{v} C] [Category.{v} D] {F G : C ‚•§ D} (e : F ‚âÖ G) :
    F.toCatHom ‚âÖ G.toCatHom where
  hom := e.hom.toCatHom‚ÇÇ
  inv := e.inv.toCatHom‚ÇÇ
  hom_inv_id := congrArg NatTrans.toCatHom‚ÇÇ e.hom_inv_id
  inv_hom_id := congrArg NatTrans.toCatHom‚ÇÇ e.inv_hom_id

/-- The natural isomorphism corresponding to a 2-iso in `Cat` -/
@[simps]
def toNatIso {X Y : Cat.{v, u}} {F G : X ‚ü∂ Y} (e : F ‚âÖ G) : F.toFunctor ‚âÖ G.toFunctor where
  hom := e.hom.toNatTrans
  inv := e.inv.toNatTrans
  hom_inv_id := congrArg Hom‚ÇÇ.toNatTrans e.hom_inv_id
  inv_hom_id := congrArg Hom‚ÇÇ.toNatTrans e.inv_hom_id

@[simp]
lemma isoMk_toNatIso {X Y : Cat.{v, u}} {F G : X ‚ü∂ Y} (e : F ‚âÖ G) :
    isoMk (Hom.toNatIso e) = e := rfl

@[simp]
lemma toNatIso_isoMk {C D : Type u} [Category.{v} C] [Category.{v} D] {F G : C ‚•§ D} (e : F ‚âÖ G) :
    Hom.toNatIso (isoMk e) = e := rfl

instance {X Y : Cat.{v, u}} {F G : X ‚ü∂ Y} (e : F ‚âÖ G) :
    IsIso e.hom.toNatTrans :=
  (toNatIso e).isIso_hom

instance {X Y : Cat.{v, u}} {F G : X ‚ü∂ Y} (e : F ‚âÖ G) :
    IsIso e.inv.toNatTrans :=
  (toNatIso e).isIso_inv

@[reassoc (attr := simp)]
lemma hom_inv_id_toNatTrans {X Y : Cat.{v, u}} {F G : X ‚ü∂ Y} (e : F ‚âÖ G) :
    e.hom.toNatTrans ‚â´ e.inv.toNatTrans = ùüô _ :=
  (toNatIso e).hom_inv_id

@[reassoc (attr := simp)]
lemma inv_hom_id_toNatTrans {X Y : Cat.{v, u}} {F G : X ‚ü∂ Y} (e : F ‚âÖ G) :
    e.inv.toNatTrans ‚â´ e.hom.toNatTrans = ùüô _ :=
  (toNatIso e).inv_hom_id

@[reassoc (attr := simp)]
lemma hom_inv_id_toNatTrans_app {X Y : Cat.{v, u}} {F G : X ‚ü∂ Y} (e : F ‚âÖ G) (A : X) :
    e.hom.toNatTrans.app A ‚â´ e.inv.toNatTrans.app A = ùüô _ :=
  (toNatIso e).hom_inv_id_app A

@[reassoc (attr := simp)]
lemma inv_hom_id_toNatTrans_app {X Y : Cat.{v, u}} {F G : X ‚ü∂ Y} (e : F ‚âÖ G) (A : X) :
    e.inv.toNatTrans.app A ‚â´ e.hom.toNatTrans.app A = ùüô _ :=
  (toNatIso e).inv_hom_id_app A

end Hom

end

/-- Bicategory structure on `Cat` -/
instance bicategory : Bicategory.{max v u, max v u} Cat.{v, u} where
  id C := (ùü≠ C).toCatHom
  comp F G := (F.toFunctor ‚ãô G.toFunctor).toCatHom
  homCategory := fun _ _ => Hom.instCategory
  whiskerLeft F _ _ Œ∑ := (Functor.whiskerLeft F.toFunctor Œ∑.toNatTrans).toCatHom‚ÇÇ
  whiskerRight Œ∑ H := (Functor.whiskerRight Œ∑.toNatTrans H.toFunctor).toCatHom‚ÇÇ
  associator F G H := Hom.isoMk
    (Functor.associator F.toFunctor G.toFunctor H.toFunctor)
  leftUnitor F := Hom.isoMk (Functor.leftUnitor F.toFunctor)
  rightUnitor F := Hom.isoMk (Functor.rightUnitor F.toFunctor)

/-- `Cat` is a strict bicategory. -/
instance bicategory.strict : Bicategory.Strict Cat.{v, u} where
  id_comp {C} {D} F := by cases F; rfl
  comp_id {C} {D} F := by cases F; rfl
  assoc := by intros; rfl

/-- Category structure on `Cat` -/
instance category : LargeCategory.{max v u} Cat.{v, u} :=
  StrictBicategory.category Cat.{v, u}

@[simp, push_cast]
lemma Hom.id_toFunctor {C : Cat.{v, u}} : (ùüô C : C ‚ü∂ C).toFunctor = ùü≠ C := rfl

@[simp]
theorem Hom.id_obj {C : Cat.{v, u}} (X : C) : (ùüô C : C ‚ü∂ C).toFunctor.obj X = X := by
  simp

@[simp]
theorem Hom.id_map {C : Cat.{v, u}} {X Y : C} (f : X ‚ü∂ Y) : (ùüô C : C ‚ü∂ C).toFunctor.map f = f := by
  simp

@[simp, push_cast]
lemma Hom.comp_toFunctor {C D E : Cat.{v, u}} (F : C ‚ü∂ D) (G : D ‚ü∂ E) :
  (F ‚â´ G).toFunctor = F.toFunctor ‚ãô G.toFunctor := rfl

@[simp]
theorem Hom.comp_obj {C D E : Cat.{v, u}} (F : C ‚ü∂ D) (G : D ‚ü∂ E) (X : C) :
    (F ‚â´ G).toFunctor.obj X = G.toFunctor.obj (F.toFunctor.obj X) := by
  simp

@[simp]
theorem Hom.comp_map {C D E : Cat.{v, u}} (F : C ‚ü∂ D) (G : D ‚ü∂ E) {X Y : C} (f : X ‚ü∂ Y) :
    (F ‚â´ G).toFunctor.map f = G.toFunctor.map (F.toFunctor.map f) := by
  simp

@[simp]
theorem Hom‚ÇÇ.id_app {C D : Cat.{v, u}} (F : C ‚ü∂ D) (X : C) :
    (ùüô F : F ‚ü∂ F).toNatTrans.app X = ùüô (F.toFunctor.obj X) := by
  simp

@[simp, reassoc]
theorem Hom‚ÇÇ.comp_app {C D : Cat.{v, u}} {F G H : C ‚ü∂ D} (Œ± : F ‚ü∂ G) (Œ≤ : G ‚ü∂ H) (X : C) :
    (Œ± ‚â´ Œ≤).toNatTrans.app X = Œ±.toNatTrans.app X ‚â´ Œ≤.toNatTrans.app X := rfl

@[simp]
theorem Hom‚ÇÇ.eqToHom_toNatTrans {C D : Cat.{v, u}} {F G : C ‚ü∂ D} (h : F = G) :
  (eqToHom h).toNatTrans = eqToHom congr(($h).toFunctor) := by cases h; simp

theorem eqToHom_app {C D : Cat.{v, u}} (F G : C ‚ü∂ D) (h : F = G) (X : C) :
    (eqToHom h).toNatTrans.app X = eqToHom congr(($h).toFunctor.obj X) := by
  simp

@[simp, push_cast]
lemma whiskerLeft_toNatTrans {C D E : Cat.{v, u}} (F : C ‚ü∂ D) {G H : D ‚ü∂ E} (Œ∑ : G ‚ü∂ H) :
  (F ‚óÅ Œ∑).toNatTrans = F.toFunctor.whiskerLeft Œ∑.toNatTrans := rfl

@[simp]
lemma whiskerLeft_app {C D E : Cat.{v, u}} (F : C ‚ü∂ D) {G H : D ‚ü∂ E} (Œ∑ : G ‚ü∂ H) (X : C) :
    (F ‚óÅ Œ∑).toNatTrans.app X = Œ∑.toNatTrans.app (F.toFunctor.obj X) := by simp

@[simp, push_cast]
lemma whiskerRight_toNatTrans {C D E : Cat.{v, u}} {F G : C ‚ü∂ D} (H : D ‚ü∂ E) (Œ∑ : F ‚ü∂ G) :
    (Œ∑ ‚ñ∑ H).toNatTrans = Functor.whiskerRight Œ∑.toNatTrans H.toFunctor := rfl

@[simp]
lemma whiskerRight_app {C D E : Cat.{v, u}} {F G : C ‚ü∂ D} (H : D ‚ü∂ E) (Œ∑ : F ‚ü∂ G) (X : C) :
    (Œ∑ ‚ñ∑ H).toNatTrans.app X = H.toFunctor.map (Œ∑.toNatTrans.app X) := by simp

@[simp, push_cast]
lemma Hom.toNatIso_leftUnitor {B C : Cat.{v, u}} (F : B ‚ü∂ C) :
    Hom.toNatIso (Œª_ F) = F.toFunctor.leftUnitor := rfl

@[simp, push_cast]
lemma leftUnitor_hom_toNatTrans {B C : Cat.{v, u}} (F : B ‚ü∂ C) :
    (Œª_ F).hom.toNatTrans = (F.toFunctor.leftUnitor).hom := rfl

@[simp, push_cast]
lemma leftUnitor_inv_toNatTrans {B C : Cat.{v, u}} (F : B ‚ü∂ C) :
    (Œª_ F).inv.toNatTrans = (F.toFunctor.leftUnitor).inv := rfl

lemma leftUnitor_hom_app {B C : Cat} (F : B ‚ü∂ C) (X : B) :
    (Œª_ F).hom.toNatTrans.app X = eqToHom (by simp) := by simp

lemma leftUnitor_inv_app {B C : Cat} (F : B ‚ü∂ C) (X : B) :
    (Œª_ F).inv.toNatTrans.app X = eqToHom (by simp) := by simp

@[simp, push_cast]
lemma Hom.toNatIso_rightUnitor {B C : Cat.{v, u}} (F : B ‚ü∂ C) :
    Hom.toNatIso (œÅ_ F) = eqToIso rfl ‚â™‚â´ F.toFunctor.rightUnitor ‚â™‚â´ eqToIso rfl := by simp; rfl

@[simp, push_cast]
lemma rightUnitor_hom_toNatTrans {B C : Cat.{v, u}} (F : B ‚ü∂ C) :
    (œÅ_ F).hom.toNatTrans = (F.toFunctor.rightUnitor).hom := rfl

@[simp, push_cast]
lemma rightUnitor_inv_toNatTrans {B C : Cat.{v, u}} (F : B ‚ü∂ C) :
    (œÅ_ F).inv.toNatTrans = (F.toFunctor.rightUnitor).inv := rfl

lemma rightUnitor_hom_app {B C : Cat.{v, u}} (F : B ‚ü∂ C) (X : B) :
    (œÅ_ F).hom.toNatTrans.app X = eqToHom (by simp) := by simp

lemma rightUnitor_inv_app {B C : Cat.{v, u}} (F : B ‚ü∂ C) (X : B) :
    (œÅ_ F).inv.toNatTrans.app X = eqToHom (by simp) := by simp

@[simp, push_cast]
lemma Hom.toNatIso_associator {B C D E : Cat.{v, u}} (F : B ‚ü∂ C) (G : C ‚ü∂ D) (H : D ‚ü∂ E) :
    Hom.toNatIso (Œ±_ F G H) = Functor.associator F.toFunctor G.toFunctor H.toFunctor := rfl

@[simp, push_cast]
lemma associator_hom_toNatTrans {B C D E : Cat.{v, u}} (F : B ‚ü∂ C) (G : C ‚ü∂ D) (H : D ‚ü∂ E) :
    (Œ±_ F G H).hom.toNatTrans = (Functor.associator F.toFunctor G.toFunctor H.toFunctor).hom := rfl

@[simp, push_cast]
lemma associator_inv_toNatTrans {B C D E : Cat.{v, u}} (F : B ‚ü∂ C) (G : C ‚ü∂ D) (H : D ‚ü∂ E) :
    (Œ±_ F G H).inv.toNatTrans = (Functor.associator F.toFunctor G.toFunctor H.toFunctor).inv := rfl

lemma associator_hom_app {B C D E : Cat} (F : B ‚ü∂ C) (G : C ‚ü∂ D) (H : D ‚ü∂ E) (X : B) :
    (Œ±_ F G H).hom.toNatTrans.app X = eqToHom (by simp) := by simp

lemma associator_inv_app {B C D E : Cat} (F : B ‚ü∂ C) (G : C ‚ü∂ D) (H : D ‚ü∂ E) (X : B) :
    (Œ±_ F G H).inv.toNatTrans.app X = eqToHom (by simp) := by simp

/-- The identity in the category of categories equals the identity functor. -/
theorem id_eq_id (X : Cat.{u, v}) : (ùüô X : X ‚ü∂ X).toFunctor = ùü≠ X := rfl

/-- Composition in the category of categories equals functor composition. -/
theorem comp_eq_comp {X Y Z : Cat} (F : X ‚ü∂ Y) (G : Y ‚ü∂ Z) :
    (F ‚â´ G).toFunctor = F.toFunctor ‚ãô G.toFunctor := rfl

@[simp] theorem of_Œ± (C) [Category* C] : (of C).Œ± = C := rfl

@[simp] theorem coe_of (C : Cat.{v, u}) : Cat.of C = C := rfl

/-- Functor that gets the set of objects of a category. It is not
called `forget`, because it is not a faithful functor. -/
def objects : Cat.{v, u} ‚•§ Type u where
  obj C := C
  map F := F.toFunctor.obj

/-- See through the defeq `objects.obj X = X`. -/
instance (X : Cat.{v, u}) : Category (objects.obj X) := inferInstanceAs <| Category X

section

attribute [local simp] eqToHom_map

/-- Any isomorphism in `Cat` induces an equivalence of the underlying categories. -/
def equivOfIso {C D : Cat} (Œ≥ : C ‚âÖ D) : C ‚âå D where
  functor := Œ≥.hom.toFunctor
  inverse := Œ≥.inv.toFunctor
  unitIso := eqToIso <| congr($(Œ≥.hom_inv_id).toFunctor).symm
  counitIso := eqToIso <| congr($(Œ≥.inv_hom_id).toFunctor)

/-- Under certain hypotheses, an equivalence of categories actually
defines an isomorphism in `Cat`. -/
@[simps]
def isoOfEquiv {C D : Cat.{v, u}} (e : C ‚âå D)
    (h‚ÇÅ : ‚àÄ (X : C), e.inverse.obj (e.functor.obj X) = X)
    (h‚ÇÇ : ‚àÄ (Y : D), e.functor.obj (e.inverse.obj Y) = Y)
    (h‚ÇÉ : ‚àÄ (X : C), e.unitIso.hom.app X = eqToHom (h‚ÇÅ X).symm := by cat_disch)
    (h‚ÇÑ : ‚àÄ (Y : D), e.counitIso.hom.app Y = eqToHom (h‚ÇÇ Y) := by cat_disch) :
    C ‚âÖ D where
  hom := e.functor.toCatHom
  inv := e.inverse.toCatHom
  hom_inv_id := congrArg Functor.toCatHom
    (Functor.ext_of_iso e.unitIso (fun X ‚Ü¶ (h‚ÇÅ X).symm) h‚ÇÉ).symm
  inv_hom_id := congrArg Functor.toCatHom (Functor.ext_of_iso e.counitIso h‚ÇÇ h‚ÇÑ)

end

end Cat

/-- Embedding `Type` into `Cat` as discrete categories.

This ought to be modelled as a 2-functor!
-/
@[simps]
def typeToCat : Type u ‚•§ Cat where
  obj X := Cat.of (Discrete X)
  map f := (Discrete.functor (Discrete.mk ‚àò f)).toCatHom
  map_id X := by
    ext
    simp only [Cat.of_Œ±, toCatHom_toFunctor, Cat.Hom.id_toFunctor]
    fapply Functor.ext
    ¬∑ simp
    ¬∑ intro X Y f
      cases f
      simp only [Discrete.functor_obj_eq_as, Function.comp_apply, types_id_apply, Discrete.mk_as,
        id_obj, eqToHom_refl, Functor.id_map, Category.comp_id, Category.id_comp]
      apply ULift.ext
      cat_disch
  map_comp f g := by
    ext
    simp only [Cat.of_Œ±, toCatHom_toFunctor, Cat.Hom.comp_toFunctor]
    apply Functor.ext
    cat_disch

instance : Functor.Faithful typeToCat.{u} where
  map_injective {_X} {_Y} _f _g h :=
    funext (fun x => congrArg (Discrete.as) (Functor.congr_obj congr(($h).toFunctor) ‚ü®x‚ü©))

instance : Functor.Full typeToCat.{u} where
  map_surjective F := ‚ü®Discrete.as ‚àò F.toFunctor.obj ‚àò Discrete.mk, by
    ext
    apply Functor.ext
    ¬∑ intro x y f
      dsimp
      apply ULift.ext
      cat_disch
    ¬∑ rintro ‚ü®x‚ü©
      apply Discrete.ext
      rfl‚ü©

end CategoryTheory
