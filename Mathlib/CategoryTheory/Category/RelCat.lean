/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Uni Marx
-/
import Mathlib.CategoryTheory.Iso
import Mathlib.CategoryTheory.EssentialImage
import Mathlib.CategoryTheory.Types
import Mathlib.CategoryTheory.Opposites
import Mathlib.Data.Rel

/-!
# Basics on the category of relations

We define the category of types `CategoryTheory.RelCat` with binary relations as morphisms.
Associating each function with the relation defined by its graph yields a faithful and
essentially surjective functor `graphFunctor` that also characterizes all isomorphisms
(see `rel_iso_iff`).

By flipping the arguments to a relation, we construct an equivalence `opEquivalence` between
`RelCat` and its opposite.
-/

open SetRel

namespace CategoryTheory

universe u

/-- A type synonym for `Type u`, which carries the category instance for which
morphisms are binary relations. -/
def RelCat :=
  Type u

namespace RelCat
variable {X Y Z : RelCat.{u}}

instance inhabited : Inhabited RelCat := by unfold RelCat; infer_instance

/-- The morphisms in the relation category are relations. -/
structure Hom (X Y : RelCat.{u}) : Type u where
  /-- Build a morphism `X ‚ü∂ Y` for `X Y : RelCat` from a relation between `X` and `Y`. -/
  ofRel ::
  /-- The underlying relation between `X` and `Y` of a morphism `X ‚ü∂ Y` for `X Y : RelCat`. -/
  rel : SetRel X Y

initialize_simps_projections Hom (as_prefix rel)

/-- The category of types with binary relations as morphisms. -/
instance instLargeCategory : LargeCategory RelCat where
  Hom := Hom
  id _ := .ofRel .id
  comp f g := .ofRel <| f.rel ‚óã g.rel

namespace Hom

@[ext] lemma ext (f g : X ‚ü∂ Y) (h : f.rel = g.rel) : f = g := by cases f; cases g; congr

@[simp] protected lemma rel_id (X : RelCat.{u}) : rel (ùüô X) = .id := rfl
@[simp] protected lemma rel_comp (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : (f ‚â´ g).rel = f.rel.comp g.rel := rfl

theorem rel_id_apply‚ÇÇ (x y : X) : x ~[rel (ùüô X)] y ‚Üî x = y := .rfl

theorem rel_comp_apply‚ÇÇ (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : X) (z : Z) :
    x ~[(f ‚â´ g).rel] z ‚Üî ‚àÉ y, x ~[f.rel] y ‚àß y ~[g.rel] z := .rfl

end Hom

/-- The essentially surjective faithful embedding
from the category of types and functions into the category of types and relations. -/
@[simps obj map_rel]
def graphFunctor : Type u ‚•§ RelCat.{u} where
  obj X := X
  map f := .ofRel f.graph

@[deprecated rel_graphFunctor_map (since := "2025-06-08")]
theorem graphFunctor_map {X Y : Type u} (f : X ‚ü∂ Y) (x : X) (y : Y) :
    x ~[(graphFunctor.map f).rel] y ‚Üî f x = y := .rfl

instance graphFunctor_faithful : graphFunctor.Faithful where
  map_injective h := Function.graph_injective congr(($h).rel)

instance graphFunctor_essSurj : graphFunctor.EssSurj :=
    graphFunctor.essSurj_of_surj Function.surjective_id

/-- A relation is an isomorphism in `RelCat` iff it is the image of an isomorphism in
`Type u`. -/
theorem rel_iso_iff {X Y : RelCat} (r : X ‚ü∂ Y) :
    IsIso (C := RelCat) r ‚Üî ‚àÉ f : Iso (C := Type u) X Y, graphFunctor.map f.hom = r := by
  constructor
  ¬∑ intro h
    have h1 := congr_fun‚ÇÇ congr((¬∑ ~[($h.hom_inv_id).rel] ¬∑))
    have h2 := congr_fun‚ÇÇ congr((¬∑ ~[($h.inv_hom_id).rel] ¬∑))
    simp only [RelCat.Hom.rel_comp_apply‚ÇÇ, RelCat.Hom.rel_id_apply‚ÇÇ, eq_iff_iff] at h1 h2
    obtain ‚ü®f, hf‚ü© := Classical.axiomOfChoice (fun a => (h1 a a).mpr rfl)
    obtain ‚ü®g, hg‚ü© := Classical.axiomOfChoice (fun a => (h2 a a).mpr rfl)
    suffices hif : IsIso (C := Type u) f by
      use asIso f
      ext ‚ü®x, y‚ü©
      exact ‚ü®by aesop, fun hxy ‚Ü¶ (h2 (f x) y).1 ‚ü®x, (hf x).2, hxy‚ü©‚ü©
    use g
    constructor
    ¬∑ ext x
      apply (h1 _ _).mp
      use f x, (hg _).2, (hf _).2
    ¬∑ ext y
      apply (h2 _ _).mp
      use g y, (hf (g y)).2, (hg y).2
  ¬∑ rintro ‚ü®f, rfl‚ü©
    apply graphFunctor.map_isIso

section Opposite
open Opposite

/-- The argument-swap isomorphism from `RelCat` to its opposite. -/
def opFunctor : RelCat ‚•§ RelCat·µí·µñ where
  obj X := op X
  map {_ _} r := .op <| .ofRel r.rel.inv

/-- The other direction of `opFunctor`. -/
def unopFunctor : RelCat·µí·µñ ‚•§ RelCat where
  obj X := unop X
  map {_ _} r := .ofRel r.unop.rel.inv

@[simp] theorem opFunctor_comp_unopFunctor_eq :
    Functor.comp opFunctor unopFunctor = Functor.id _ := rfl

@[simp] theorem unopFunctor_comp_opFunctor_eq :
    Functor.comp unopFunctor opFunctor = Functor.id _ := rfl

/-- `RelCat` is self-dual: The map that swaps the argument order of a
relation induces an equivalence between `RelCat` and its opposite. -/
@[simps]
def opEquivalence : RelCat ‚âå RelCat·µí·µñ where
  functor := opFunctor
  inverse := unopFunctor
  unitIso := Iso.refl _
  counitIso := Iso.refl _

instance : opFunctor.IsEquivalence := by
  change opEquivalence.functor.IsEquivalence
  infer_instance

instance : unopFunctor.IsEquivalence := by
  change opEquivalence.inverse.IsEquivalence
  infer_instance

end Opposite

end RelCat

end CategoryTheory
