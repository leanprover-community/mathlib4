/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Category.Cat
import Mathlib.CategoryTheory.Limits.Types.Limits
import Mathlib.CategoryTheory.Limits.Preserves.Basic

/-!
# The category of small categories has all small limits.

An object in the limit consists of a family of objects,
which are carried to one another by the functors in the diagram.
A morphism between two such objects is a family of morphisms between the corresponding objects,
which are carried to one another by the action on morphisms of the functors in the diagram.

## Future work
Can the indexing category live in a lower universe?
-/


noncomputable section

universe v u

open CategoryTheory.Limits

namespace CategoryTheory

variable {J : Type v} [SmallCategory J]

namespace Cat

namespace HasLimits

instance categoryObjects {F : J â¥¤ Cat.{u, u}} {j} :
    SmallCategory ((F â‹™ Cat.objects.{u, u}).obj j) :=
  (F.obj j).str

/-- Auxiliary definition:
the diagram whose limit gives the morphism space between two objects of the limit category. -/
@[simps]
def homDiagram {F : J â¥¤ Cat.{v, v}} (X Y : limit (F â‹™ Cat.objects.{v, v})) : J â¥¤ Type v where
  obj j := limit.Ï€ (F â‹™ Cat.objects) j X âŸ¶ limit.Ï€ (F â‹™ Cat.objects) j Y
  map f g := by
    refine eqToHom ?_ â‰« (F.map f).map g â‰« eqToHom ?_
    Â· exact (congr_fun (limit.w (F â‹™ Cat.objects) f) X).symm
    Â· exact congr_fun (limit.w (F â‹™ Cat.objects) f) Y
  map_id X := by
    funext f
    letI : Category (objects.obj (F.obj X)) := (inferInstance : Category (F.obj X))
    simp [Functor.congr_hom (F.map_id X) f]
  map_comp {_ _ Z} f g := by
    funext h
    letI : Category (objects.obj (F.obj Z)) := (inferInstance : Category (F.obj Z))
    simp [Functor.congr_hom (F.map_comp f g) h, eqToHom_map]

@[simps]
instance (F : J â¥¤ Cat.{v, v}) : Category (limit (F â‹™ Cat.objects)) where
  Hom X Y := limit (homDiagram X Y)
  id X := Types.Limit.mk.{v, v} (homDiagram X X) (fun _ => ðŸ™ _) fun j j' f => by simp
  comp {X Y Z} f g :=
    Types.Limit.mk.{v, v} (homDiagram X Z)
      (fun j => limit.Ï€ (homDiagram X Y) j f â‰« limit.Ï€ (homDiagram Y Z) j g) fun j j' h => by
      simp [â† congr_fun (limit.w (homDiagram X Y) h) f,
        â† congr_fun (limit.w (homDiagram Y Z) h) g]
  id_comp _ := by
    apply Types.limit_ext.{v, v}
    simp
  comp_id _ := by
    apply Types.limit_ext.{v, v}
    simp

/-- Auxiliary definition: the limit category. -/
@[simps]
def limitConeX (F : J â¥¤ Cat.{v, v}) : Cat.{v, v} where Î± := limit (F â‹™ Cat.objects)

/-- Auxiliary definition: the cone over the limit category. -/
@[simps]
def limitCone (F : J â¥¤ Cat.{v, v}) : Cone F where
  pt := limitConeX F
  Ï€ :=
    { app := fun j =>
        { obj := limit.Ï€ (F â‹™ Cat.objects) j
          map := fun f => limit.Ï€ (homDiagram _ _) j f }
      naturality := fun _ _ f =>
        CategoryTheory.Functor.ext (fun X => (congr_fun (limit.w (F â‹™ Cat.objects) f) X).symm)
          fun X Y h => (congr_fun (limit.w (homDiagram X Y) f) h).symm }

/-- Auxiliary definition: the universal morphism to the proposed limit cone. -/
@[simps]
def limitConeLift (F : J â¥¤ Cat.{v, v}) (s : Cone F) : s.pt âŸ¶ limitConeX F where
  obj :=
    limit.lift (F â‹™ Cat.objects)
      { pt := s.pt
        Ï€ :=
          { app := fun j => (s.Ï€.app j).obj
            naturality := fun _ _ f => objects.congr_map (s.Ï€.naturality f) } }
  map f := by
    fapply Types.Limit.mk.{v, v}
    Â· intro j
      refine eqToHom ?_ â‰« (s.Ï€.app j).map f â‰« eqToHom ?_ <;> simp
    Â· intro j j' h
      dsimp
      simp only [Category.assoc, Functor.map_comp, eqToHom_map, eqToHom_trans,
        eqToHom_trans_assoc, â† Functor.comp_map]
      have := (s.Ï€.naturality h).symm
      dsimp at this
      rw [Category.id_comp] at this
      erw [Functor.congr_hom this f]
      simp

@[simp]
theorem limit_Ï€_homDiagram_eqToHom {F : J â¥¤ Cat.{v, v}} (X Y : limit (F â‹™ Cat.objects.{v, v}))
    (j : J) (h : X = Y) :
    limit.Ï€ (homDiagram X Y) j (eqToHom h) =
      eqToHom (congr_arg (limit.Ï€ (F â‹™ Cat.objects.{v, v}) j) h) := by
  subst h
  simp

/-- Auxiliary definition: the proposed cone is a limit cone. -/
def limitConeIsLimit (F : J â¥¤ Cat.{v, v}) : IsLimit (limitCone F) where
  lift := limitConeLift F
  fac s j := CategoryTheory.Functor.ext (by simp) fun X Y f => by
    dsimp [limitConeLift]
    exact Types.Limit.Ï€_mk.{v, v} _ _ _ _
  uniq s m w := by
    symm
    refine CategoryTheory.Functor.ext ?_ ?_
    Â· intro X
      apply Types.limit_ext.{v, v}
      intro j
      simp [Types.Limit.lift_Ï€_apply', â† w j]
    Â· intro X Y f
      simp [fun j => Functor.congr_hom (w j).symm f]

end HasLimits

/-- The category of small categories has all small limits. -/
instance : HasLimits Cat.{v, v} where
  has_limits_of_shape _ :=
    { has_limit := fun F => âŸ¨âŸ¨âŸ¨HasLimits.limitCone F, HasLimits.limitConeIsLimit FâŸ©âŸ©âŸ© }

instance : PreservesLimits Cat.objects.{v, v} where
  preservesLimitsOfShape :=
    { preservesLimit := fun {F} =>
        preservesLimit_of_preserves_limit_cone (HasLimits.limitConeIsLimit F)
          (Limits.IsLimit.ofIsoLimit (limit.isLimit (F â‹™ Cat.objects))
            (Cones.ext (by rfl) (by aesop_cat))) }

end Cat

end CategoryTheory
