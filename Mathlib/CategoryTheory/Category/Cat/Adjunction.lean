/-
Copyright (c) 2024 Nicolas Rolland. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nicolas Rolland
-/
import Mathlib.CategoryTheory.Category.Cat
import Mathlib.CategoryTheory.Adjunction.Basic
import Mathlib.CategoryTheory.ConnectedComponents

/-!
# Adjunctions related to Cat, the category of categories

The embedding `typeToCat: Type ‚•§ Cat`, mapping a type to the corresponding discrete category, is
left adjoint to the functor `Cat.objects`, which maps a category to its set of objects.

Another functor `connectedComponents : Cat ‚•§ Type` maps a category to the set of its connected
components and functors to functions between those sets.

## Notes
All this could be made with 2-functors

-/

universe u
namespace CategoryTheory.Cat

variable (X : Type u) (C : Cat)

private def typeToCatObjectsAdjHomEquiv : (typeToCat.obj X ‚ü∂ C) ‚âÉ (X ‚ü∂ Cat.objects.obj C) where
  toFun f x := f.obj ‚ü®x‚ü©
  invFun := Discrete.functor
  left_inv F := Functor.ext (fun _ ‚Ü¶ rfl) (fun ‚ü®_‚ü© ‚ü®_‚ü© f => by
    obtain rfl := Discrete.eq_of_hom f
    simp)
  right_inv _ := rfl

private def typeToCatObjectsAdjCounitApp : (Cat.objects ‚ãô typeToCat).obj C ‚•§ C where
  obj := Discrete.as
  map := eqToHom ‚àò Discrete.eq_of_hom

/-- `typeToCat : Type ‚•§ Cat` is left adjoint to `Cat.objects : Cat ‚•§ Type` -/
def typeToCatObjectsAdj : typeToCat ‚ä£ Cat.objects where
  homEquiv := typeToCatObjectsAdjHomEquiv
  unit := { app:= fun _  ‚Ü¶ Discrete.mk }
  counit := {
    app := typeToCatObjectsAdjCounitApp
    naturality := fun _ _ _  ‚Ü¶  Functor.hext (fun _ ‚Ü¶ rfl)
      (by intro ‚ü®_‚ü© ‚ü®_‚ü© f
          obtain rfl := Discrete.eq_of_hom f
          aesop_cat ) }


/-- The connected components functor -/
def connectedComponents.{v} : Cat.{v, u} ‚•§ Type u where
  obj C := ConnectedComponents C
  map F :=
    Quotient.lift (Quotient.mk (Zigzag.setoid _) ‚àò F.obj)
      (fun _ _ ‚Ü¶ Quot.sound ‚àò zigzag_obj_of_zigzag F)
  map_id _ := funext fun x ‚Ü¶ (Quotient.exists_rep x).elim (fun _ h ‚Ü¶ by simp [<- h]; rfl)
  map_comp _ _ := funext fun x ‚Ü¶ (Quotient.exists_rep x).elim (fun _ h => by simp [<- h])


/-- Functions from connected components and functors to discrete category are in bijection -/
def connectedComponentsTypeToCatHomEquiv  (C : Cat) (X : Type u)
    : ( ConnectedComponents C ‚ü∂ X) ‚âÉ (C ‚•§ Discrete X)   where
  toFun := ConnectedComponents.connectedToDiscrete
  invFun := ConnectedComponents.discreteToConnected
  left_inv := fun f ‚Ü¶ funext fun x ‚Ü¶ by
    obtain ‚ü®x, h‚ü© := Quotient.exists_rep x
    rw [‚Üê h]
    rfl
  right_inv  := fun fctr ‚Ü¶
    Functor.hext (fun _ ‚Ü¶ rfl) (fun c d f ‚Ü¶
      have : Subsingleton (fctr.obj c ‚ü∂ fctr.obj d) := Discrete.instSubsingletonDiscreteHom _ _
      (Subsingleton.elim (fctr.map f) _).symm.heq)

/-- `typeToCat : Type ‚•§ Cat` is right adjoint to `connectedComponents : Cat ‚•§ Type` -/
def connectedComponentsTypeToCatAdj : connectedComponents ‚ä£ typeToCat where
  homEquiv := connectedComponentsTypeToCatHomEquiv
  unit := { app:= fun C  ‚Ü¶ ConnectedComponents.connectedToDiscrete (ùüô (connectedComponents.obj C)) }
  counit :=  {
      app := fun X => ConnectedComponents.discreteToConnected (ùüô typeToCat.obj X)
      naturality := fun _ _ _ =>
        funext (fun xcc => by
          obtain ‚ü®x,h‚ü© := Quotient.exists_rep xcc
          aesop_cat) }
  homEquiv_unit := fun {C X h} => Functor.hext (fun _ => by rfl) (fun _ _ _ => by rfl)
  homEquiv_counit := fun {C X G} => by funext cc;obtain ‚ü®_,_‚ü© := Quotient.exists_rep cc; aesop_cat

end CategoryTheory.Cat
