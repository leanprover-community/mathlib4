/-
Copyright (c) 2024 Nicolas Rolland. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nicolas Rolland
-/
module

public import Mathlib.CategoryTheory.Category.Cat
public import Mathlib.CategoryTheory.Adjunction.Basic
public import Mathlib.CategoryTheory.ConnectedComponents

/-!
# Adjunctions related to Cat, the category of categories

The embedding `typeToCat: Type ‚•§ Cat`, mapping a type to the corresponding discrete category, is
left adjoint to the functor `Cat.objects`, which maps a category to its set of objects.

Another functor `connectedComponents : Cat ‚•§ Type` maps a category to the set of its connected
components and functors to functions between those sets.

## Notes
All this could be made with 2-functors

-/

@[expose] public section

universe v u
namespace CategoryTheory.Cat

variable (X : Type u) (C : Cat)

set_option backward.privateInPublic true in
set_option backward.privateInPublic.warn false in
private def typeToCatObjectsAdjHomEquiv : (typeToCat.obj X ‚ü∂ C) ‚âÉ (X ‚ü∂ Cat.objects.obj C) where
  toFun F x := F.toFunctor.obj ‚ü®x‚ü©
  invFun f := (Discrete.functor f).toCatHom
  left_inv F := Hom.ext <| Functor.ext (fun _ ‚Ü¶ rfl) (fun ‚ü®_‚ü© ‚ü®_‚ü© f => by
    obtain rfl := Discrete.eq_of_hom f
    simp)

set_option backward.privateInPublic true in
set_option backward.privateInPublic.warn false in
private def typeToCatObjectsAdjCounitApp : (Cat.objects ‚ãô typeToCat).obj C ‚•§ C where
  obj := Discrete.as
  map := eqToHom ‚àò Discrete.eq_of_hom

set_option backward.privateInPublic true in
set_option backward.privateInPublic.warn false in
/-- `typeToCat : Type ‚•§ Cat` is left adjoint to `Cat.objects : Cat ‚•§ Type` -/
def typeToCatObjectsAdj : typeToCat ‚ä£ Cat.objects :=
  Adjunction.mk' {
    homEquiv := typeToCatObjectsAdjHomEquiv
    unit := { app := fun _ ‚Ü¶ Discrete.mk }
    counit := {
      app C := (typeToCatObjectsAdjCounitApp C).toCatHom
      naturality := fun _ _ _ ‚Ü¶ Hom.ext <| Functor.hext (fun _ ‚Ü¶ rfl)
        (by intro ‚ü®_‚ü© ‚ü®_‚ü© f
            obtain rfl := Discrete.eq_of_hom f
            cat_disch) } }

/-- The connected components functor -/
def connectedComponents : Cat.{v, u} ‚•§ Type u where
  obj C := ConnectedComponents C
  map F := Functor.mapConnectedComponents F.toFunctor
  map_id _ := funext fun x ‚Ü¶ (Quotient.exists_rep x).elim (fun _ h ‚Ü¶ by subst h; rfl)
  map_comp _ _ := funext fun x ‚Ü¶ (Quotient.exists_rep x).elim (fun _ h => by subst h; rfl)

/-- `typeToCat : Type ‚•§ Cat` is right adjoint to `connectedComponents : Cat ‚•§ Type` -/
def connectedComponentsTypeToCatAdj : connectedComponents ‚ä£ typeToCat :=
  Adjunction.mk' {
    homEquiv := fun C X ‚Ü¶ (ConnectedComponents.typeToCatHomEquiv C X).trans
      (Functor.equivCatHom C (Discrete X))
    unit :=
      { app := fun C ‚Ü¶ Functor.toCatHom <|
        ConnectedComponents.functorToDiscrete _ (ùüô (connectedComponents.obj C)) }
    counit := {
        app := fun X => ConnectedComponents.liftFunctor _ (ùüô typeToCat.obj X).toFunctor
        naturality := fun _ _ _ =>
          funext (fun xcc => by
            obtain ‚ü®x, h‚ü© := Quotient.exists_rep xcc
            cat_disch) }
    homEquiv_counit := fun {C X G} => by
      funext cc
      obtain ‚ü®_, _‚ü© := Quotient.exists_rep cc
      cat_disch }

end CategoryTheory.Cat
