/-
Copyright (c) 2025 Emily Riehl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Emily Riehl
-/
import Mathlib.CategoryTheory.Closed.Cartesian
import Mathlib.CategoryTheory.Functor.Currying
import Mathlib.CategoryTheory.Monoidal.Cartesian.Cat

/-!
# Cartesian closed structure on `Cat`

The category of small categories is cartesian closed, with the exponential at a category `C`
defined by the functor category mapping out of `C`.

Adjoint transposition is defined by currying and uncurrying.

-/

universe v u vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ uâ‚ uâ‚‚ uâ‚ƒ uâ‚„

namespace CategoryTheory

open Functor

namespace Cat

variable (C : Type u) [Category.{v} C]

/-- A category `C` induces a functor from `Cat` to itself defined
by forming the category of functors out of `C`. -/
@[simps]
def exp : Cat â¥¤ Cat where
  obj D := Cat.of (C â¥¤ D)
  map F := {
    obj G := G â‹™ F
    map Î± := whiskerRight Î± F
  }

end Cat

section

variable {B : Type uâ‚} [Category.{vâ‚} B] {C : Type uâ‚‚} [Category.{vâ‚‚} C] {D : Type uâ‚ƒ}
  [Category.{vâ‚ƒ} D] {E : Type uâ‚„} [Category.{vâ‚„} E]

/-- The isomorphism of categories of bifunctors given by currying. -/
def curryingIso : C â¥¤ D â¥¤ E â‰… C Ã— D â¥¤ E where
  hom F := uncurry.obj F
  inv G := curry.obj G
  hom_inv_id := types_ext _ _ (fun F â†¦ curry_obj_uncurry_obj F)
  inv_hom_id := types_ext _ _ (fun F â†¦ uncurry_obj_curry_obj F)

/-- The isomorphism of categories of bifunctors given by flipping the arguments. -/
@[simps]
def flipIso : C â¥¤ D â¥¤ E â‰… D â¥¤ C â¥¤ E where
  hom F := F.flip
  inv F := F.flip
  hom_inv_id := types_ext _ _ (fun _ â†¦ rfl)
  inv_hom_id := types_ext _ _ (fun _ â†¦ rfl)

/-- The equivalence of types of bifunctors given by currying. -/
@[simps]
def curryingEquiv : C â¥¤ D â¥¤ E â‰ƒ C Ã— D â¥¤ E :=
  curryingIso.toEquiv

/-- The flipped equivalence of types of bifunctors given by currying. -/
@[simps]
def curryingFlipEquiv : D â¥¤ C â¥¤ E â‰ƒ C Ã— D â¥¤ E :=
  (flipIso â‰ªâ‰« curryingIso).toEquiv

lemma comp_flip_uncurry_eq (F : B â¥¤ D) (G : D â¥¤ C â¥¤ E) :
    uncurry.obj (F â‹™ G).flip = (ðŸ­ C).prod F â‹™ (uncurry.obj G.flip) :=
  Functor.ext (by simp) (fun âŸ¨câ‚, bâ‚âŸ© âŸ¨câ‚‚, bâ‚‚âŸ© âŸ¨fâ‚, fâ‚‚âŸ© => by simp)

end

section
variable {B C D E: Type u} [Category.{u} B] [Category.{u} C]
  [Category.{u} D] [Category.{u} E]

lemma comp_flip_curry_eq (F : C Ã— B â¥¤ D) (G : D â¥¤ E) :
    (curry.obj (F â‹™ G)).flip =
      (curry.obj F).flip â‹™ (Cat.exp (Cat.of C)).map G.toCatHom := by
  refine Functor.ext (fun _ â†¦ rfl) ?_
  Â· intro X Y f
    simp only [Cat.of_Î±, comp_obj, eqToHom_refl, Functor.comp_map, Category.comp_id,
      Category.id_comp]
    rfl

end

namespace Cat

section
variable (C : Type u) [Category.{u} C]

instance closed : Closed (Cat.of C) where
  rightAdj := exp C
  adj := Adjunction.mkOfHomEquiv {
    homEquiv _ _ := curryingFlipEquiv.symm
    homEquiv_naturality_left_symm :=
      comp_flip_uncurry_eq
    homEquiv_naturality_right :=
      comp_flip_curry_eq
  }

instance cartesianClosed : CartesianClosed Cat.{u, u} where
  closed C := closed C

end

end Cat

end CategoryTheory
