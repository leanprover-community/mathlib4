/-
Copyright (c) 2022 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies
-/
import Mathlib.CategoryTheory.ConcreteCategory.Basic
import Mathlib.CategoryTheory.Adjunction.Basic

/-!
# The category of pointed types

This defines `Pointed`, the category of pointed types.

## TODO

* Monoidal structure
* Upgrade `typeToPointed` to an equivalence
-/


open CategoryTheory

universe u

/-- The category of pointed types. -/
structure Pointed : Type (u + 1) where
  /-- the underlying type -/
  protected X : Type u
  /-- the distinguished element -/
  point : X

namespace Pointed

instance : CoeSort Pointed Type* :=
  ‚ü®Pointed.X‚ü©

/-- Turns a point into a pointed type. -/
abbrev of {X : Type*} (point : X) : Pointed :=
  ‚ü®X, point‚ü©

theorem coe_of {X : Type*} (point : X) : ‚Ü•(of point) = X :=
  rfl

alias _root_.Prod.Pointed := of

instance : Inhabited Pointed :=
  ‚ü®of ((), ())‚ü©

/-- Morphisms in `Pointed`. -/
@[ext]
protected structure Hom (X Y : Pointed.{u}) : Type u where
  /-- the underlying map -/
  toFun : X ‚Üí Y
  /-- compatibility with the distinguished points -/
  map_point : toFun X.point = Y.point

namespace Hom

/-- The identity morphism of `X : Pointed`. -/
@[simps]
def id (X : Pointed) : Pointed.Hom X X :=
  ‚ü®_root_.id, rfl‚ü©

instance (X : Pointed) : Inhabited (Pointed.Hom X X) :=
  ‚ü®id X‚ü©

/-- Composition of morphisms of `Pointed`. -/
@[simps]
def comp {X Y Z : Pointed.{u}} (f : Pointed.Hom X Y) (g : Pointed.Hom Y Z) : Pointed.Hom X Z :=
  ‚ü®g.toFun ‚àò f.toFun, by rw [Function.comp_apply, f.map_point, g.map_point]‚ü©

end Hom

instance largeCategory : LargeCategory Pointed where
  Hom := Pointed.Hom
  id := Hom.id
  comp := @Hom.comp

@[simp] lemma Hom.id_toFun' (X : Pointed.{u}) : (ùüô X : X ‚ü∂ X).toFun = _root_.id := rfl

@[simp] lemma Hom.comp_toFun' {X Y Z : Pointed.{u}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).toFun = g.toFun ‚àò f.toFun := rfl

instance (X Y : Pointed) : FunLike { f : X ‚Üí Y // f X.point = Y.point } X Y where
  coe f := f
  coe_injective' _ _ := Subtype.ext

instance hasForget : ConcreteCategory Pointed fun X Y ‚Ü¶ { f : X ‚Üí Y // f X.point = Y.point } where
  hom f := ‚ü®f.1, f.2‚ü©
  ofHom f := ‚ü®f.1, f.2‚ü©

/-- Constructs an isomorphism between pointed types from an equivalence that preserves the point
between them. -/
@[simps]
def Iso.mk {Œ± Œ≤ : Pointed} (e : Œ± ‚âÉ Œ≤) (he : e Œ±.point = Œ≤.point) : Œ± ‚âÖ Œ≤ where
  hom := ‚ü®e, he‚ü©
  inv := ‚ü®e.symm, e.symm_apply_eq.2 he.symm‚ü©
  hom_inv_id := Pointed.Hom.ext e.symm_comp_self
  inv_hom_id := Pointed.Hom.ext e.self_comp_symm

end Pointed

/-- `Option` as a functor from types to pointed types. This is the free functor. -/
@[simps]
def typeToPointed : Type u ‚•§ Pointed.{u} where
  obj X := ‚ü®Option X, none‚ü©
  map f := ‚ü®Option.map f, rfl‚ü©
  map_id _ := Pointed.Hom.ext Option.map_id
  map_comp _ _ := Pointed.Hom.ext (Option.map_comp_map _ _).symm

/-- `typeToPointed` is the free functor. -/
def typeToPointedForgetAdjunction : typeToPointed ‚ä£ forget Pointed :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun X Y ‚Ü¶
        { toFun := fun f ‚Ü¶ f.toFun ‚àò Option.some
          invFun := fun f ‚Ü¶ ‚ü®fun o ‚Ü¶ o.elim Y.point f, rfl‚ü©
          left_inv := fun f ‚Ü¶ by
            apply Pointed.Hom.ext
            funext x
            cases x
            ¬∑ exact f.map_point.symm
            ¬∑ rfl }
      homEquiv_naturality_left_symm := fun f g ‚Ü¶ by
        apply Pointed.Hom.ext
        funext x
        cases x <;> rfl }
