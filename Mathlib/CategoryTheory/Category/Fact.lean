/-
Copyright (c) 2023 Jakob von Raumer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jakob von Raumer
-/
import Mathlib.CategoryTheory.Category.Basic
import Mathlib.CategoryTheory.Arrow
import Mathlib.CategoryTheory.Limits.Shapes.Terminal

/-!
# The Factorisation Category of a Category

`Fact f` is the category containing as objects all factorisations of a morphism `f`.

We show that `Fact f` always has an initial and a terminal object.

TODO: Show that `Fact f` is isomorphic to a comma category in two ways.

TODO: Make `MonoFactorisation f` a special case of a `Fact f`.
-/

namespace CategoryTheory

universe v u

variable {C : Type u} [Category.{v} C]

/-- Factorisations of a morphism `f` as a structure, containing, one object, two morphisms,
and the condition that their composition equals `f`. -/
structure Fact {X Y : C} (f : X ‚ü∂ Y) where
  /-- The midpoint of the factorisation. -/
  D   : C
  /-- The morphism into the factorisation midpoint. -/
  Œπ   : X ‚ü∂ D
  /-- The morphism out of the factorisation midpoint. -/
  œÄ   : D ‚ü∂ Y
  /-- The factorisation condition. -/
  Œπ_œÄ : Œπ ‚â´ œÄ = f := by aesop_cat

attribute [simp] Fact.Œπ_œÄ

namespace Fact

variable {X Y : C} {f : X ‚ü∂ Y}

/-- Morphisms of `Fact f` consist of morphism between their midpoints and the obvious
commutativity conditions. -/
@[ext]
protected structure Hom (d e : Fact f) : Type (max u v) where
  /-- The morphism between the midpoints of the factorizations. -/
  h : d.D ‚ü∂ e.D
  /-- The left commuting triangle of the factorization morphism. -/
  Œπ_h : d.Œπ ‚â´ h = e.Œπ := by aesop_cat
  /-- The right commuting triangle of the factorization morphism. -/
  h_œÄ : h ‚â´ e.œÄ = d.œÄ := by aesop_cat

attribute [simp] Fact.Hom.Œπ_h Fact.Hom.h_œÄ

/-- The identity morphism of `Fact f`. -/
@[simps]
protected def Hom.id (d : Fact f) : Fact.Hom d d where
  h := ùüô _

/-- Composition of morphisms in `Fact f`. -/
@[simps]
protected def Hom.comp {d‚ÇÅ d‚ÇÇ d‚ÇÉ : Fact f} (f : Fact.Hom d‚ÇÅ d‚ÇÇ) (g : Fact.Hom d‚ÇÇ d‚ÇÉ) :
    Fact.Hom d‚ÇÅ d‚ÇÉ where
  h := f.h ‚â´ g.h
  Œπ_h := by rw [‚Üê Category.assoc, f.Œπ_h, g.Œπ_h]
  h_œÄ := by rw [Category.assoc, g.h_œÄ, f.h_œÄ]

instance : Category.{max u v} (Fact f) where
  Hom d e := Fact.Hom d e
  id d := Fact.Hom.id d
  comp f g := Fact.Hom.comp f g

variable (d : Fact f)

/-- The initial object in `Fact f`, with the domain of `f` as its midpoint. -/
@[simps]
protected def initial : Fact f where
  D := X
  Œπ := ùüô _
  œÄ := f

/-- The unique morphism out of `Fact.initial f`. -/
@[simps]
protected def initialHom (d : Fact f) : Fact.Hom (Fact.initial : Fact f) d where
  h := d.Œπ

instance : Unique ((Fact.initial : Fact f) ‚ü∂ d) where
  default := Fact.initialHom d
  uniq f := by apply Fact.Hom.ext; simp [‚Üê f.Œπ_h]

/-- The terminal object in `Fact f`, with the codomain of `f` as its midpoint. -/
@[simps]
protected def terminal : Fact f where
  D := Y
  Œπ := f
  œÄ := ùüô _

/-- The unique morphism into `Fact.terminal f`. -/
@[simps]
protected def terminalHom (d : Fact f) : Fact.Hom d (Fact.terminal : Fact f) where
  h := d.œÄ

instance : Unique (d ‚ü∂ (Fact.terminal : Fact f)) where
  default := Fact.terminalHom d
  uniq f := by apply Fact.Hom.ext; simp [‚Üê f.h_œÄ]

open Limits

instance : IsInitial (Fact.initial : Fact f) := IsInitial.ofUnique _

instance : HasInitial (Fact f) := Limits.hasInitial_of_unique Fact.initial

instance : IsTerminal (Fact.terminal : Fact f) := IsTerminal.ofUnique _

instance : HasTerminal (Fact f) := Limits.hasTerminal_of_unique Fact.terminal

/-- The forgetful functor from `Fact f` to the underlying category `C`. -/
@[simps]
def forget : Fact f ‚•§ C where
  obj := Fact.D
  map f := f.h

end Fact
