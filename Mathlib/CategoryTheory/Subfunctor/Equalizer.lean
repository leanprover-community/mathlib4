/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Limits.Shapes.Equalizers
public import Mathlib.CategoryTheory.Subfunctor.Image

/-!
# The equalizer of two morphisms of functors, as a subfunctor

If `F‚ÇÅ` and `F‚ÇÇ` are type-valued functors, `A : Subfunctor F‚ÇÅ`, and
`f` and `g` are two morphisms `A.toFunctor ‚ü∂ F‚ÇÇ`, we introduce
`Subcomplex.equalizer f g`, which is the subfunctor of `F‚ÇÅ` contained in `A`
where `f` and `g` coincide.

-/

@[expose] public section

universe w v u

namespace CategoryTheory

variable {C : Type u} [Category.{v} C] {F‚ÇÅ F‚ÇÇ : C ‚•§ Type w} {A : Subfunctor F‚ÇÅ}
  (f g : A.toFunctor ‚ü∂ F‚ÇÇ)

namespace Subfunctor

/-- The equalizer of two morphisms of type-valued functors of types of the form
`A.toFunctor ‚ü∂ F‚ÇÇ` with `A : Subfunctor F‚ÇÅ`, as a subcomplex of `F‚ÇÅ`. -/
@[simps -isSimp]
protected def equalizer : Subfunctor F‚ÇÅ where
  obj U := setOf (fun x ‚Ü¶ ‚àÉ (hx : x ‚àà A.obj _), f.app _ ‚ü®x, hx‚ü© = g.app _ ‚ü®x, hx‚ü©)
  map œÜ x := by
    rintro ‚ü®hx, h‚ü©
    exact ‚ü®A.map _ hx,
      (FunctorToTypes.naturality _ _ f œÜ ‚ü®x, hx‚ü©).trans (Eq.trans (by rw [h])
        (FunctorToTypes.naturality _ _ g œÜ ‚ü®x, hx‚ü©).symm)‚ü©

attribute [local simp] equalizer_obj

lemma equalizer_le : Subfunctor.equalizer f g ‚â§ A :=
  fun _ _ h ‚Ü¶ h.1

@[simp]
lemma equalizer_self : Subfunctor.equalizer f f = A := by aesop

lemma mem_equalizer_iff {i : C} (x : A.toFunctor.obj i) :
    x.1 ‚àà (Subfunctor.equalizer f g).obj i ‚Üî f.app i x = g.app i x := by
  simp

lemma range_le_equalizer_iff {G : C ‚•§ Type w} (œÜ : G ‚ü∂ A.toFunctor) :
    range (œÜ ‚â´ A.Œπ) ‚â§ Subfunctor.equalizer f g ‚Üî œÜ ‚â´ f = œÜ ‚â´ g := by
  rw [NatTrans.ext_iff]
  simp [le_def, Set.subset_def, funext_iff, CategoryTheory.types_ext_iff]

lemma equalizer_eq_iff :
    Subfunctor.equalizer f g = A ‚Üî f = g := by
  have := range_le_equalizer_iff f g (ùüô _)
  simp only [Category.id_comp, range_Œπ] at this
  rw [‚Üê this]
  constructor
  ¬∑ intro h
    rw [h]
  ¬∑ intro h
    exact le_antisymm (equalizer_le f g) h

/-- Given two morphisms `f` and `g` in `A.toFunctor ‚ü∂ F‚ÇÇ`, this is the monomorphism
of functors corresponding to the inclusion `Subfunctor.equalizer f g ‚â§ A`. -/
def equalizer.Œπ : (Subfunctor.equalizer f g).toFunctor ‚ü∂ A.toFunctor :=
  homOfLe (equalizer_le f g)

instance : Mono (equalizer.Œπ f g) := by
  dsimp [equalizer.Œπ]
  infer_instance

@[reassoc (attr := simp)]
lemma equalizer.Œπ_Œπ : equalizer.Œπ f g ‚â´ A.Œπ = (Subfunctor.equalizer f g).Œπ := rfl

@[reassoc]
lemma equalizer.condition : equalizer.Œπ f g ‚â´ f = equalizer.Œπ f g ‚â´ g := by
  simp [‚Üê range_le_equalizer_iff]

/-- Given two morphisms `f` and `g` in `A.toFunctor ‚ü∂ F‚ÇÇ`, if `œÜ : G ‚ü∂ A.toFunctor`
is such that `œÜ ‚â´ f = œÜ ‚â´ g`, then this is the lifted morphism
`G ‚ü∂ (Subfunctor.equalizer f g).toFunctor`. This is part of the universal
property of the equalizer that is satisfied by
the functor `(Subfunctor.equalizer f g).toFunctor`. -/
def equalizer.lift {G : C ‚•§ Type w} (œÜ : G ‚ü∂ A.toFunctor)
    (w : œÜ ‚â´ f = œÜ ‚â´ g) :
    G ‚ü∂ (Subfunctor.equalizer f g).toFunctor :=
  Subfunctor.lift (œÜ ‚â´ A.Œπ) (by simpa only [range_le_equalizer_iff] using w)

@[reassoc (attr := simp)]
lemma equalizer.lift_Œπ' {G : C ‚•§ Type w} (œÜ : G ‚ü∂ A.toFunctor)
    (w : œÜ ‚â´ f = œÜ ‚â´ g) :
    equalizer.lift f g œÜ w ‚â´ (Subfunctor.equalizer f g).Œπ = œÜ ‚â´ A.Œπ :=
  rfl

@[reassoc (attr := simp)]
lemma equalizer.lift_Œπ {G : C ‚•§ Type w} (œÜ : G ‚ü∂ A.toFunctor)
    (w : œÜ ‚â´ f = œÜ ‚â´ g) :
    equalizer.lift f g œÜ w ‚â´ equalizer.Œπ f g = œÜ :=
  rfl

/-- The (limit) fork which expresses `(Subfunctor.equalizer f g).toFunctor` as
the equalizer of `f` and `g`. -/
@[simps! pt]
def equalizer.fork : Limits.Fork f g :=
  Limits.Fork.ofŒπ (equalizer.Œπ f g) (equalizer.condition f g)

@[simp]
lemma equalizer.fork_Œπ :
    (equalizer.fork f g).Œπ = equalizer.Œπ f g := rfl

/-- `(Subfunctor.equalizer f g).toFunctor` is the equalizer of `f` and `g`. -/
def equalizer.forkIsLimit : Limits.IsLimit (equalizer.fork f g) :=
  Limits.Fork.IsLimit.mk _
    (fun s ‚Ü¶ equalizer.lift _ _ s.Œπ s.condition)
    (fun s ‚Ü¶ by dsimp)
    (fun s m hm ‚Ü¶ by simp [‚Üê cancel_mono (Subfunctor.equalizer f g).Œπ, ‚Üê hm])

@[deprecated (since := "2025-12-11")] alias Subpresheaf.equalizer := Subfunctor.equalizer
@[deprecated (since := "2025-12-11")] alias Subpresheaf.equalizer_le := equalizer_le
@[deprecated (since := "2025-12-11")] alias Subpresheaf.equalizer_self := equalizer_self
@[deprecated (since := "2025-12-11")] alias Subpresheaf.mem_equalizer_iff := mem_equalizer_iff
@[deprecated (since := "2025-12-11")] alias Subpresheaf.range_le_equalizer_iff :=
  range_le_equalizer_iff
@[deprecated (since := "2025-12-11")] alias Subpresheaf.equalizer_eq_iff := equalizer_eq_iff
@[deprecated (since := "2025-12-11")] alias Subpresheaf.equalizer.Œπ := equalizer.Œπ
@[deprecated (since := "2025-12-11")] alias Subpresheaf.equalizer.Œπ_Œπ := equalizer.Œπ_Œπ
@[deprecated (since := "2025-12-11")] alias Subpresheaf.equalizer.condition := equalizer.condition
@[deprecated (since := "2025-12-11")] alias Subpresheaf.equalizer.lift := equalizer.lift
@[deprecated (since := "2025-12-11")] alias Subpresheaf.equalizer.lift_Œπ' := equalizer.lift_Œπ'
@[deprecated (since := "2025-12-11")] alias Subpresheaf.equalizer.lift_Œπ := equalizer.lift_Œπ
@[deprecated (since := "2025-12-11")] alias Subpresheaf.equalizer.fork := equalizer.fork
@[deprecated (since := "2025-12-11")] alias Subpresheaf.equalizer.fork_Œπ := equalizer.fork_Œπ
@[deprecated (since := "2025-12-11")] alias Subpresheaf.equalizer.forkIsLimit :=
  equalizer.forkIsLimit

end Subfunctor

end CategoryTheory
