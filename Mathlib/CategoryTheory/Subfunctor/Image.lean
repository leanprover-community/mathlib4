/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang, Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Subfunctor.Basic
public import Mathlib.CategoryTheory.Limits.FunctorCategory.EpiMono
public import Mathlib.CategoryTheory.Limits.Types.Colimits

/-!
# The image of a subfunctor

Given a morphism of type-valued functors `p : F' ‚ü∂ F`, we define its range
`Subfunctor.range p`. More generally, if `G' : Subfunctor F'`, we
define `G'.image p : Subfunctor F` as the image of `G'` by `f`, and
if `G : Subfunctor F`, we define its preimage `G.preimage f : Subfunctor F'`.

-/

@[expose] public section

universe w v u

namespace CategoryTheory

variable {C : Type u} [Category.{v} C] {F F' F'' : C ‚•§ Type w}

namespace Subfunctor

section range

/-- The range of a morphism of type-valued functors, as a subfunctor of the target. -/
@[simps]
def range (p : F' ‚ü∂ F) : Subfunctor F where
  obj U := Set.range (p.app U)
  map := by
    rintro U V i _ ‚ü®x, rfl‚ü©
    exact ‚ü®_, FunctorToTypes.naturality  _ _ p i x‚ü©

variable (F) in
lemma range_id : range (ùüô F) = ‚ä§ := by aesop

@[simp]
lemma range_Œπ (G : Subfunctor F) : range G.Œπ = G := by aesop

end range

section lift

variable (f : F' ‚ü∂ F) {G : Subfunctor F} (hf : range f ‚â§ G)

/-- If the image of a morphism falls in a subfunctor, then the morphism factors through it. -/
@[simps!]
def lift : F' ‚ü∂ G.toFunctor where
  app U x := ‚ü®f.app U x, hf U (by simp)‚ü©
  naturality _ _ g := by
    ext x
    simpa [Subtype.ext_iff] using FunctorToTypes.naturality _ _ f g x

@[reassoc (attr := simp)]
theorem lift_Œπ : lift f hf ‚â´ G.Œπ = f := rfl

end lift

section range

variable (p : F' ‚ü∂ F)

/-- Given a morphism `p : F' ‚ü∂ F` of type-valued functors, this is the morphism
from `F'` to its range. -/
def toRange :
    F' ‚ü∂ (range p).toFunctor :=
  lift p (by rfl)

@[reassoc (attr := simp)]
lemma toRange_Œπ : toRange p ‚â´ (range p).Œπ = p := rfl

lemma toRange_app_val {i : C} (x : F'.obj i) :
    ((toRange p).app i x).val = p.app i x := by
  simp [toRange]

@[simp]
lemma range_toRange : range (toRange p) = ‚ä§ := by
  ext i ‚ü®x, hx‚ü©
  dsimp at hx ‚ä¢
  simp only [Set.mem_range, Set.mem_univ, iff_true]
  simp only [Set.mem_range] at hx
  obtain ‚ü®y, rfl‚ü© := hx
  exact ‚ü®y, rfl‚ü©

lemma epi_iff_range_eq_top :
    Epi p ‚Üî range p = ‚ä§ := by
  simp [NatTrans.epi_iff_epi_app, epi_iff_surjective, Subfunctor.ext_iff, funext_iff,
    Set.range_eq_univ]

lemma range_eq_top [Epi p] : range p = ‚ä§ := by rwa [‚Üê epi_iff_range_eq_top]

instance : Epi (toRange p) := by simp [epi_iff_range_eq_top]

instance [Mono p] : IsIso (toRange p) := by
  have := mono_of_mono_fac (toRange_Œπ p)
  rw [NatTrans.isIso_iff_isIso_app]
  intro i
  rw [isIso_iff_bijective]
  constructor
  ¬∑ rw [‚Üê mono_iff_injective]
    infer_instance
  ¬∑ rw [‚Üê epi_iff_surjective]
    infer_instance

lemma range_comp_le (f : F ‚ü∂ F') (g : F' ‚ü∂ F'') :
    range (f ‚â´ g) ‚â§ range g := fun _ _ _ ‚Ü¶ by aesop

end range

section image

variable (G : Subfunctor F) (f : F ‚ü∂ F')

/-- The image of a subfunctor by a morphism of type-valued functors. -/
@[simps]
def image : Subfunctor F' where
  obj i := (f.app i) '' (G.obj i)
  map := by
    rintro Œî Œî' œÜ _ ‚ü®x, hx, rfl‚ü©
    exact ‚ü®F.map œÜ x, G.map œÜ hx, by apply FunctorToTypes.naturality‚ü©

lemma image_top : (‚ä§ : Subfunctor F).image f = range f := by aesop

@[simp]
lemma image_iSup {Œπ : Type*} (G : Œπ ‚Üí Subfunctor F) (f : F ‚ü∂ F') :
    (‚®Ü i, G i).image f = ‚®Ü i, (G i).image f := by aesop

lemma image_comp (g : F' ‚ü∂ F'') :
    G.image (f ‚â´ g) = (G.image f).image g := by aesop

lemma range_comp (g : F' ‚ü∂ F'') :
    range (f ‚â´ g) = (range f).image g := by aesop

end image

section preimage

/-- The preimage of a subfunctor by a morphism of type-valued functors. -/
@[simps]
def preimage (G : Subfunctor F) (p : F' ‚ü∂ F) : Subfunctor F' where
  obj n := p.app n ‚Åª¬π' (G.obj n)
  map f := (Set.preimage_mono (G.map f)).trans (by
    simp only [Set.preimage_preimage, FunctorToTypes.naturality _ _ p f]
    rfl)

@[simp]
lemma preimage_id (G : Subfunctor F) :
    G.preimage (ùüô F) = G := by aesop

lemma preimage_comp (G : Subfunctor F) (f : F'' ‚ü∂ F') (g : F' ‚ü∂ F) :
    G.preimage (f ‚â´ g) = (G.preimage g).preimage f := by aesop

lemma image_le_iff (G : Subfunctor F) (f : F ‚ü∂ F') (G' : Subfunctor F') :
    G.image f ‚â§ G' ‚Üî G ‚â§ G'.preimage f := by
  simp [Subfunctor.le_def]

/-- Given a morphism `p : F' ‚ü∂ F` of type-valued functors and `G : Subfunctor F`,
this is the morphism from the preimage of `G` by `p` to `G`. -/
def fromPreimage (G : Subfunctor F) (p : F' ‚ü∂ F) :
    (G.preimage p).toFunctor ‚ü∂ G.toFunctor :=
  lift ((G.preimage p).Œπ ‚â´ p) (by
    rw [range_comp, range_Œπ, image_le_iff])

@[reassoc]
lemma fromPreimage_Œπ (G : Subfunctor F) (p : F' ‚ü∂ F) :
    G.fromPreimage p ‚â´ G.Œπ = (G.preimage p).Œπ ‚â´ p := rfl

lemma preimage_eq_top_iff (G : Subfunctor F) (p : F' ‚ü∂ F) :
    G.preimage p = ‚ä§ ‚Üî range p ‚â§ G := by
  rw [‚Üê image_top, image_le_iff]
  simp

@[simp]
lemma preimage_image_of_epi (G : Subfunctor F) (p : F' ‚ü∂ F) [hp : Epi p] :
    (G.preimage p).image p = G := by
  apply le_antisymm
  ¬∑ rw [image_le_iff]
  ¬∑ intro i x hx
    simp only [NatTrans.epi_iff_epi_app, epi_iff_surjective] at hp
    obtain ‚ü®y, rfl‚ü© := hp _ x
    exact ‚ü®y, hx, rfl‚ü©

end preimage

@[deprecated (since := "2025-12-11")] alias Subpresheaf.range := range
@[deprecated (since := "2025-12-11")] alias Subpresheaf.range_id := range_id
@[deprecated (since := "2025-12-11")] alias Subpresheaf.range_Œπ := range_Œπ
@[deprecated (since := "2025-12-11")] alias Subpresheaf.lift := lift
@[deprecated (since := "2025-12-11")] alias Subpresheaf.lift_Œπ := lift_Œπ
@[deprecated (since := "2025-12-11")] alias Subpresheaf.toRange := toRange
@[deprecated (since := "2025-12-11")] alias Subpresheaf.toRange_Œπ := toRange_Œπ
@[deprecated (since := "2025-12-11")] alias Subpresheaf.toRange_app_val := toRange_app_val
@[deprecated (since := "2025-12-11")] alias Subpresheaf.range_toRange := range_toRange
@[deprecated (since := "2025-12-11")] alias Subpresheaf.epi_iff_range_eq_top := epi_iff_range_eq_top
@[deprecated (since := "2025-12-11")] alias Subpresheaf.range_eq_top := range_eq_top
@[deprecated (since := "2025-12-11")] alias Subpresheaf.range_comp_le := range_comp_le
@[deprecated (since := "2025-12-11")] alias Subpresheaf.image := image
@[deprecated (since := "2025-12-11")] alias Subpresheaf.image_top := image_top
@[deprecated (since := "2025-12-11")] alias Subpresheaf.image_iSup := image_iSup
@[deprecated (since := "2025-12-11")] alias Subpresheaf.image_comp := image_comp
@[deprecated (since := "2025-12-11")] alias Subpresheaf.range_comp := range_comp
@[deprecated (since := "2025-12-11")] alias Subpresheaf.preimage := preimage
@[deprecated (since := "2025-12-11")] alias Subpresheaf.preimage_id := preimage_id
@[deprecated (since := "2025-12-11")] alias Subpresheaf.preimage_comp := preimage_comp
@[deprecated (since := "2025-12-11")] alias Subpresheaf.image_le_iff := image_le_iff
@[deprecated (since := "2025-12-11")] alias Subpresheaf.fromPreimage := fromPreimage
@[deprecated (since := "2025-12-11")] alias Subpresheaf.fromPreimage_Œπ := fromPreimage_Œπ
@[deprecated (since := "2025-12-11")] alias Subpresheaf.preimage_eq_top_iff := preimage_eq_top_iff
@[deprecated (since := "2025-12-11")] alias Subpresheaf.preimage_image_of_epi :=
  preimage_image_of_epi

end Subfunctor

end CategoryTheory
