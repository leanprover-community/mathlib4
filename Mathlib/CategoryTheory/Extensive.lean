/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.CategoryTheory.Limits.Shapes.CommSq
import Mathlib.CategoryTheory.Limits.Shapes.StrictInitial
import Mathlib.CategoryTheory.Limits.Shapes.Types
import Mathlib.Topology.Category.TopCat.Limits.Pullbacks
import Mathlib.CategoryTheory.Limits.FunctorCategory
import Mathlib.CategoryTheory.Limits.Constructions.FiniteProductsOfBinaryProducts

#align_import category_theory.extensive from "leanprover-community/mathlib"@"178a32653e369dce2da68dc6b2694e385d484ef1"

/-!

# Extensive categories

## Main definitions
- `CategoryTheory.IsVanKampenColimit`: A (colimit) cocone over a diagram `F : J â¥¤ C` is van
  Kampen if for every cocone `c'` over the pullback of the diagram `F' : J â¥¤ C'`,
  `c'` is colimiting iff `c'` is the pullback of `c`.
- `CategoryTheory.FinitaryExtensive`: A category is (finitary) extensive if it has finite
  coproducts, and binary coproducts are van Kampen.

## Main Results
- `CategoryTheory.hasStrictInitialObjects_of_finitaryExtensive`: The initial object
  in extensive categories is strict.
- `CategoryTheory.FinitaryExtensive.mono_inr_of_isColimit`: Coproduct injections are monic in
  extensive categories.
- `CategoryTheory.BinaryCofan.isPullback_initial_to_of_isVanKampen`: In extensive categories,
  sums are disjoint, i.e. the pullback of `X âŸ¶ X â¨¿ Y` and `Y âŸ¶ X â¨¿ Y` is the initial object.
- `CategoryTheory.types.finitaryExtensive`: The category of types is extensive.
- `CategoryTheory.FinitaryExtensive.isVanKampen_finiteCoproducts`: Finite coproducts in a
  finitary extensive category are van Kampen.

## TODO

Show that the following are finitary extensive:
- the categories of sheaves over a site
- `Scheme`
- `AffineScheme` (`CommRingáµ’áµ–`)

## References
- https://ncatlab.org/nlab/show/extensive+category
- [Carboni et al, Introduction to extensive and distributive categories][CARBONI1993145]

-/


open CategoryTheory.Limits

namespace CategoryTheory

universe v' u' v u

variable {J : Type v'} [Category.{u'} J] {C : Type u} [Category.{v} C]

/-- A natural transformation is equifibered if every commutative square of the following form is
a pullback.
```
F(X) â†’ F(Y)
 â†“      â†“
G(X) â†’ G(Y)
```
-/
def NatTrans.Equifibered {F G : J â¥¤ C} (Î± : F âŸ¶ G) : Prop :=
  âˆ€ â¦ƒi j : Jâ¦„ (f : i âŸ¶ j), IsPullback (F.map f) (Î±.app i) (Î±.app j) (G.map f)
#align category_theory.nat_trans.equifibered CategoryTheory.NatTrans.Equifibered

theorem NatTrans.equifibered_of_isIso {F G : J â¥¤ C} (Î± : F âŸ¶ G) [IsIso Î±] : Equifibered Î± :=
  fun _ _ f => IsPullback.of_vert_isIso âŸ¨NatTrans.naturality _ fâŸ©
#align category_theory.nat_trans.equifibered_of_is_iso CategoryTheory.NatTrans.equifibered_of_isIso

theorem NatTrans.Equifibered.comp {F G H : J â¥¤ C} {Î± : F âŸ¶ G} {Î² : G âŸ¶ H} (hÎ± : Equifibered Î±)
    (hÎ² : Equifibered Î²) : Equifibered (Î± â‰« Î²) :=
  fun _ _ f => (hÎ± f).paste_vert (hÎ² f)
#align category_theory.nat_trans.equifibered.comp CategoryTheory.NatTrans.Equifibered.comp

/-- A (colimit) cocone over a diagram `F : J â¥¤ C` is universal if it is stable under pullbacks. -/
def IsUniversalColimit {F : J â¥¤ C} (c : Cocone F) : Prop :=
  âˆ€ â¦ƒF' : J â¥¤ Câ¦„ (c' : Cocone F') (Î± : F' âŸ¶ F) (f : c'.pt âŸ¶ c.pt)
    (_ : Î± â‰« c.Î¹ = c'.Î¹ â‰« (Functor.const J).map f) (_ : NatTrans.Equifibered Î±),
    (âˆ€ j : J, IsPullback (c'.Î¹.app j) (Î±.app j) f (c.Î¹.app j)) â†’ Nonempty (IsColimit c')
#align category_theory.is_universal_colimit CategoryTheory.IsUniversalColimit

/-- A (colimit) cocone over a diagram `F : J â¥¤ C` is van Kampen if for every cocone `c'` over the
pullback of the diagram `F' : J â¥¤ C'`, `c'` is colimiting iff `c'` is the pullback of `c`.

TODO: Show that this is iff the functor `C â¥¤ Catáµ’áµ–` sending `x` to `C/x` preserves it.
TODO: Show that this is iff the inclusion functor `C â¥¤ Span(C)` preserves it.
-/
def IsVanKampenColimit {F : J â¥¤ C} (c : Cocone F) : Prop :=
  âˆ€ â¦ƒF' : J â¥¤ Câ¦„ (c' : Cocone F') (Î± : F' âŸ¶ F) (f : c'.pt âŸ¶ c.pt)
    (_ : Î± â‰« c.Î¹ = c'.Î¹ â‰« (Functor.const J).map f) (_ : NatTrans.Equifibered Î±),
    Nonempty (IsColimit c') â†” âˆ€ j : J, IsPullback (c'.Î¹.app j) (Î±.app j) f (c.Î¹.app j)
#align category_theory.is_van_kampen_colimit CategoryTheory.IsVanKampenColimit

theorem IsVanKampenColimit.isUniversal {F : J â¥¤ C} {c : Cocone F} (H : IsVanKampenColimit c) :
    IsUniversalColimit c :=
  fun _ c' Î± f h hÎ± => (H c' Î± f h hÎ±).mpr
#align category_theory.is_van_kampen_colimit.is_universal CategoryTheory.IsVanKampenColimit.isUniversal

/-- A van Kampen colimit is a colimit. -/
noncomputable def IsVanKampenColimit.isColimit {F : J â¥¤ C} {c : Cocone F}
    (h : IsVanKampenColimit c) : IsColimit c := by
  refine' ((h c (ğŸ™ F) (ğŸ™ c.pt : _) (by rw [Functor.map_id, Category.comp_id, Category.id_comp])
    (NatTrans.equifibered_of_isIso _)).mpr fun j => _).some
  haveI : IsIso (ğŸ™ c.pt) := inferInstance
  exact IsPullback.of_vert_isIso âŸ¨by erw [NatTrans.id_app, Category.comp_id, Category.id_comp]âŸ©
#align category_theory.is_van_kampen_colimit.is_colimit CategoryTheory.IsVanKampenColimit.isColimit

theorem IsInitial.isVanKampenColimit [HasStrictInitialObjects C] {X : C} (h : IsInitial X) :
    IsVanKampenColimit (asEmptyCocone X) := by
  intro F' c' Î± f hf hÎ±
  have : F' = Functor.empty C := by apply Functor.hext <;> rintro âŸ¨âŸ¨âŸ©âŸ©
  subst this
  haveI := h.isIso_to f
  refine' âŸ¨by rintro _ âŸ¨âŸ¨âŸ©âŸ©,
    fun _ => âŸ¨IsColimit.ofIsoColimit h (Cocones.ext (asIso f).symm <| by rintro âŸ¨âŸ¨âŸ©âŸ©)âŸ©âŸ©
#align category_theory.is_initial.is_van_kampen_colimit CategoryTheory.IsInitial.isVanKampenColimit

section Extensive

variable {X Y : C}

/-- A category is (finitary) extensive if it has finite coproducts,
and binary coproducts are van Kampen. -/
class FinitaryExtensive (C : Type u) [Category.{v} C] : Prop where
  [hasFiniteCoproducts : HasFiniteCoproducts C]
  [hasPullbackInl : âˆ€ {X Y Z : C} (f : Z âŸ¶ X â¨¿ Y), HasPullback coprod.inl f]
  /-- In a finitary extensive category, all coproducts are van Kampen-/
  van_kampen' : âˆ€ {X Y : C} (c : BinaryCofan X Y), IsColimit c â†’ IsVanKampenColimit c
#align category_theory.finitary_extensive CategoryTheory.FinitaryExtensive

attribute [instance] FinitaryExtensive.hasFiniteCoproducts FinitaryExtensive.hasPullbackInl

theorem FinitaryExtensive.vanKampen [FinitaryExtensive C] {F : Discrete WalkingPair â¥¤ C}
    (c : Cocone F) (hc : IsColimit c) : IsVanKampenColimit c := by
  let X := F.obj âŸ¨WalkingPair.leftâŸ©
  let Y := F.obj âŸ¨WalkingPair.rightâŸ©
  have : F = pair X Y := by
    apply Functor.hext
    Â· rintro âŸ¨âŸ¨âŸ©âŸ© <;> rfl
    Â· rintro âŸ¨âŸ¨âŸ©âŸ© âŸ¨jâŸ© âŸ¨âŸ¨rfl : _ = jâŸ©âŸ© <;> simp
  clear_value X Y
  subst this
  exact FinitaryExtensive.van_kampen' c hc
#align category_theory.finitary_extensive.van_kampen CategoryTheory.FinitaryExtensive.vanKampen

instance FinitaryExtensive.hasPullbackInl' [FinitaryExtensive C] {X Y Z : C} (f : Z âŸ¶ X â¨¿ Y) :
    HasPullback f coprod.inl :=
  hasPullback_symmetry _ _

instance FinitaryExtensive.hasPullbackInr' [FinitaryExtensive C] {X Y Z : C} (f : Z âŸ¶ X â¨¿ Y) :
    HasPullback f coprod.inr := by
  have : IsPullback (ğŸ™ _) (f â‰« (coprod.braiding X Y).hom) f (coprod.braiding Y X).hom :=
    IsPullback.of_horiz_isIso âŸ¨by simpâŸ©
  have := (IsPullback.of_hasPullback (f â‰« (coprod.braiding X Y).hom) coprod.inl).paste_horiz this
  simp only [coprod.braiding_hom, Category.comp_id, colimit.Î¹_desc, BinaryCofan.mk_pt,
    BinaryCofan.Î¹_app_left, BinaryCofan.mk_inl] at this
  exact âŸ¨âŸ¨âŸ¨_, this.isLimitâŸ©âŸ©âŸ©

instance FinitaryExtensive.hasPullbackInr [FinitaryExtensive C] {X Y Z : C} (f : Z âŸ¶ X â¨¿ Y) :
    HasPullback coprod.inr f :=
  hasPullback_symmetry _ _

theorem mapPair_equifibered {F F' : Discrete WalkingPair â¥¤ C} (Î± : F âŸ¶ F') :
    NatTrans.Equifibered Î± := by
  rintro âŸ¨âŸ¨âŸ©âŸ© âŸ¨jâŸ© âŸ¨âŸ¨rfl : _ = jâŸ©âŸ©
  all_goals
    dsimp; simp only [Discrete.functor_map_id]
    exact IsPullback.of_horiz_isIso âŸ¨by simp only [Category.comp_id, Category.id_comp]âŸ©
#align category_theory.map_pair_equifibered CategoryTheory.mapPair_equifibered

theorem BinaryCofan.isVanKampen_iff (c : BinaryCofan X Y) :
    IsVanKampenColimit c â†”
      âˆ€ {X' Y' : C} (c' : BinaryCofan X' Y') (Î±X : X' âŸ¶ X) (Î±Y : Y' âŸ¶ Y) (f : c'.pt âŸ¶ c.pt)
        (_ : Î±X â‰« c.inl = c'.inl â‰« f) (_ : Î±Y â‰« c.inr = c'.inr â‰« f),
        Nonempty (IsColimit c') â†” IsPullback c'.inl Î±X f c.inl âˆ§ IsPullback c'.inr Î±Y f c.inr := by
  constructor
  Â· introv H hÎ±X hÎ±Y
    rw [H c' (mapPair Î±X Î±Y) f (by ext âŸ¨âŸ¨âŸ©âŸ© <;> dsimp <;> assumption) (mapPair_equifibered _)]
    constructor
    Â· intro H
      exact âŸ¨H _, H _âŸ©
    Â· rintro H âŸ¨âŸ¨âŸ©âŸ©
      exacts [H.1, H.2]
  Â· introv H F' hÎ± h
    let X' := F'.obj âŸ¨WalkingPair.leftâŸ©
    let Y' := F'.obj âŸ¨WalkingPair.rightâŸ©
    have : F' = pair X' Y' := by
      apply Functor.hext
      Â· rintro âŸ¨âŸ¨âŸ©âŸ© <;> rfl
      Â· rintro âŸ¨âŸ¨âŸ©âŸ© âŸ¨jâŸ© âŸ¨âŸ¨rfl : _ = jâŸ©âŸ© <;> simp
    clear_value X' Y'
    subst this
    change BinaryCofan X' Y' at c'
    rw [H c' _ _ _ (NatTrans.congr_app hÎ± âŸ¨WalkingPair.leftâŸ©)
        (NatTrans.congr_app hÎ± âŸ¨WalkingPair.rightâŸ©)]
    constructor
    Â· rintro H âŸ¨âŸ¨âŸ©âŸ©
      exacts [H.1, H.2]
    Â· intro H
      exact âŸ¨H _, H _âŸ©
#align category_theory.binary_cofan.is_van_kampen_iff CategoryTheory.BinaryCofan.isVanKampen_iff

theorem BinaryCofan.isVanKampen_mk {X Y : C} (c : BinaryCofan X Y)
    (cofans : âˆ€ X Y : C, BinaryCofan X Y) (colimits : âˆ€ X Y, IsColimit (cofans X Y))
    (cones : âˆ€ {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z), PullbackCone f g)
    (limits : âˆ€ {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z), IsLimit (cones f g))
    (hâ‚ : âˆ€ {X' Y' : C} (Î±X : X' âŸ¶ X) (Î±Y : Y' âŸ¶ Y) (f : (cofans X' Y').pt âŸ¶ c.pt)
      (_ : Î±X â‰« c.inl = (cofans X' Y').inl â‰« f) (_ : Î±Y â‰« c.inr = (cofans X' Y').inr â‰« f),
      IsPullback (cofans X' Y').inl Î±X f c.inl âˆ§ IsPullback (cofans X' Y').inr Î±Y f c.inr)
    (hâ‚‚ : âˆ€ {Z : C} (f : Z âŸ¶ c.pt),
      IsColimit (BinaryCofan.mk (cones f c.inl).fst (cones f c.inr).fst)) :
    IsVanKampenColimit c := by
  rw [BinaryCofan.isVanKampen_iff]
  introv hX hY
  constructor
  Â· rintro âŸ¨hâŸ©
    let e := h.coconePointUniqueUpToIso (colimits _ _)
    obtain âŸ¨hl, hrâŸ© := hâ‚ Î±X Î±Y (e.inv â‰« f) (by simp [hX]) (by simp [hY])
    constructor
    Â· rw [â† Category.id_comp Î±X, â† Iso.hom_inv_id_assoc e f]
      haveI : IsIso (ğŸ™ X') := inferInstance
      have : c'.inl â‰« e.hom = ğŸ™ X' â‰« (cofans X' Y').inl := by
        dsimp
        simp
      exact (IsPullback.of_vert_isIso âŸ¨thisâŸ©).paste_vert hl
    Â· rw [â† Category.id_comp Î±Y, â† Iso.hom_inv_id_assoc e f]
      haveI : IsIso (ğŸ™ Y') := inferInstance
      have : c'.inr â‰« e.hom = ğŸ™ Y' â‰« (cofans X' Y').inr := by
        dsimp
        simp
      exact (IsPullback.of_vert_isIso âŸ¨thisâŸ©).paste_vert hr
  Â· rintro âŸ¨Hâ‚, Hâ‚‚âŸ©
    refine' âŸ¨IsColimit.ofIsoColimit _ <| (isoBinaryCofanMk _).symmâŸ©
    let eâ‚ : X' â‰… _ := Hâ‚.isLimit.conePointUniqueUpToIso (limits _ _)
    let eâ‚‚ : Y' â‰… _ := Hâ‚‚.isLimit.conePointUniqueUpToIso (limits _ _)
    have heâ‚ : c'.inl = eâ‚.hom â‰« (cones f c.inl).fst := by simp
    have heâ‚‚ : c'.inr = eâ‚‚.hom â‰« (cones f c.inr).fst := by simp
    rw [heâ‚, heâ‚‚]
    apply BinaryCofan.isColimitCompRightIso (BinaryCofan.mk _ _)
    apply BinaryCofan.isColimitCompLeftIso (BinaryCofan.mk _ _)
    exact hâ‚‚ f
#align category_theory.binary_cofan.is_van_kampen_mk CategoryTheory.BinaryCofan.isVanKampen_mk

theorem BinaryCofan.mono_inr_of_isVanKampen [HasInitial C] {X Y : C} {c : BinaryCofan X Y}
    (h : IsVanKampenColimit c) : Mono c.inr := by
  refine' PullbackCone.mono_of_isLimitMkIdId _ (IsPullback.isLimit _)
  refine' (h (BinaryCofan.mk (initial.to Y) (ğŸ™ Y)) (mapPair (initial.to X) (ğŸ™ Y)) c.inr _
      (mapPair_equifibered _)).mp âŸ¨_âŸ© âŸ¨WalkingPair.rightâŸ©
  Â· ext âŸ¨âŸ¨âŸ©âŸ© <;> dsimp; simp
  Â· exact ((BinaryCofan.isColimit_iff_isIso_inr initialIsInitial _).mpr (by
      dsimp
      infer_instance)).some
#align category_theory.binary_cofan.mono_inr_of_is_van_kampen CategoryTheory.BinaryCofan.mono_inr_of_isVanKampen

theorem FinitaryExtensive.mono_inr_of_isColimit [FinitaryExtensive C] {c : BinaryCofan X Y}
    (hc : IsColimit c) : Mono c.inr :=
  BinaryCofan.mono_inr_of_isVanKampen (FinitaryExtensive.vanKampen c hc)
#align category_theory.finitary_extensive.mono_inr_of_is_colimit CategoryTheory.FinitaryExtensive.mono_inr_of_isColimit

theorem FinitaryExtensive.mono_inl_of_isColimit [FinitaryExtensive C] {c : BinaryCofan X Y}
    (hc : IsColimit c) : Mono c.inl :=
  FinitaryExtensive.mono_inr_of_isColimit (BinaryCofan.isColimitFlip hc)
#align category_theory.finitary_extensive.mono_inl_of_is_colimit CategoryTheory.FinitaryExtensive.mono_inl_of_isColimit

instance [FinitaryExtensive C] (X Y : C) : Mono (coprod.inl : X âŸ¶ X â¨¿ Y) :=
  (FinitaryExtensive.mono_inl_of_isColimit (coprodIsCoprod X Y) : _)

instance [FinitaryExtensive C] (X Y : C) : Mono (coprod.inr : Y âŸ¶ X â¨¿ Y) :=
  (FinitaryExtensive.mono_inr_of_isColimit (coprodIsCoprod X Y) : _)

theorem BinaryCofan.isPullback_initial_to_of_isVanKampen [HasInitial C] {c : BinaryCofan X Y}
    (h : IsVanKampenColimit c) : IsPullback (initial.to _) (initial.to _) c.inl c.inr := by
  refine' ((h (BinaryCofan.mk (initial.to Y) (ğŸ™ Y)) (mapPair (initial.to X) (ğŸ™ Y)) c.inr _
      (mapPair_equifibered _)).mp âŸ¨_âŸ© âŸ¨WalkingPair.leftâŸ©).flip
  Â· ext âŸ¨âŸ¨âŸ©âŸ© <;> dsimp; simp
  Â· exact ((BinaryCofan.isColimit_iff_isIso_inr initialIsInitial _).mpr (by
      dsimp
      infer_instance)).some
#align category_theory.binary_cofan.is_pullback_initial_to_of_is_van_kampen CategoryTheory.BinaryCofan.isPullback_initial_to_of_isVanKampen

theorem FinitaryExtensive.isPullback_initial_to_binaryCofan [FinitaryExtensive C]
    {c : BinaryCofan X Y} (hc : IsColimit c) :
    IsPullback (initial.to _) (initial.to _) c.inl c.inr :=
  BinaryCofan.isPullback_initial_to_of_isVanKampen (FinitaryExtensive.vanKampen c hc)
#align category_theory.finitary_extensive.is_pullback_initial_to_binary_cofan CategoryTheory.FinitaryExtensive.isPullback_initial_to_binaryCofan

theorem hasStrictInitial_of_isUniversal [HasInitial C]
    (H : IsUniversalColimit (BinaryCofan.mk (ğŸ™ (âŠ¥_ C)) (ğŸ™ (âŠ¥_ C)))) : HasStrictInitialObjects C :=
  hasStrictInitialObjects_of_initial_is_strict
    (by
      intro A f
      suffices IsColimit (BinaryCofan.mk (ğŸ™ A) (ğŸ™ A)) by
        obtain âŸ¨l, hâ‚, hâ‚‚âŸ© := Limits.BinaryCofan.IsColimit.desc' this (f â‰« initial.to A) (ğŸ™ A)
        rcases(Category.id_comp _).symm.trans hâ‚‚ with rfl
        exact âŸ¨âŸ¨_, ((Category.id_comp _).symm.trans hâ‚).symm, initialIsInitial.hom_ext _ _âŸ©âŸ©
      refine' (H (BinaryCofan.mk (ğŸ™ _) (ğŸ™ _)) (mapPair f f) f (by ext âŸ¨âŸ¨âŸ©âŸ© <;> dsimp <;> simp)
        (mapPair_equifibered _) _).some
      rintro âŸ¨âŸ¨âŸ©âŸ© <;> dsimp <;>
        exact IsPullback.of_horiz_isIso âŸ¨(Category.id_comp _).trans (Category.comp_id _).symmâŸ©)
#align category_theory.has_strict_initial_of_is_universal CategoryTheory.hasStrictInitial_of_isUniversal

instance (priority := 100) hasStrictInitialObjects_of_finitaryExtensive [FinitaryExtensive C] :
    HasStrictInitialObjects C :=
  hasStrictInitial_of_isUniversal (FinitaryExtensive.vanKampen _
    ((BinaryCofan.isColimit_iff_isIso_inr initialIsInitial _).mpr (by
      dsimp
      infer_instance)).some).isUniversal
#align category_theory.has_strict_initial_objects_of_finitary_extensive CategoryTheory.hasStrictInitialObjects_of_finitaryExtensive

theorem finitaryExtensive_iff_of_isTerminal (C : Type u) [Category.{v} C] [HasFiniteCoproducts C]
    [hasPullbackInl : âˆ€ {X Y Z : C} (f : Z âŸ¶ X â¨¿ Y), HasPullback coprod.inl f]
    (T : C) (HT : IsTerminal T) (câ‚€ : BinaryCofan T T) (hcâ‚€ : IsColimit câ‚€) :
    FinitaryExtensive C â†” IsVanKampenColimit câ‚€ := by
  refine' âŸ¨fun H => H.van_kampen' câ‚€ hcâ‚€, fun H => _âŸ©
  constructor
  simp_rw [BinaryCofan.isVanKampen_iff] at H âŠ¢
  intro X Y c hc X' Y' c' Î±X Î±Y f hX hY
  obtain âŸ¨d, hd, hd'âŸ© :=
    Limits.BinaryCofan.IsColimit.desc' hc (HT.from _ â‰« câ‚€.inl) (HT.from _ â‰« câ‚€.inr)
  rw [H c' (Î±X â‰« HT.from _) (Î±Y â‰« HT.from _) (f â‰« d) (by rw [â† reassoc_of% hX, hd, Category.assoc])
      (by rw [â† reassoc_of% hY, hd', Category.assoc])]
  obtain âŸ¨hl, hrâŸ© := (H c (HT.from _) (HT.from _) d hd.symm hd'.symm).mp âŸ¨hcâŸ©
  rw [hl.paste_vert_iff hX.symm, hr.paste_vert_iff hY.symm]
#align category_theory.finitary_extensive_iff_of_is_terminal CategoryTheory.finitaryExtensive_iff_of_isTerminal

instance types.finitaryExtensive : FinitaryExtensive (Type u) := by
  classical
  rw [finitaryExtensive_iff_of_isTerminal (Type u) PUnit Types.isTerminalPunit _
      (Types.binaryCoproductColimit _ _)]
  apply BinaryCofan.isVanKampen_mk _ _ (fun X Y => Types.binaryCoproductColimit X Y) _
      fun f g => (Limits.Types.pullbackLimitCone f g).2
  Â· intros _ _ _ _ f hÎ±X hÎ±Y
    constructor
    Â· refine' âŸ¨âŸ¨hÎ±X.symmâŸ©, âŸ¨PullbackCone.isLimitAux' _ _âŸ©âŸ©
      intro s
      have : âˆ€ x, âˆƒ! y, s.fst x = Sum.inl y := by
        intro x
        cases' h : s.fst x with val val
        Â· simp only [Types.binaryCoproductCocone_pt, Functor.const_obj_obj, Sum.inl.injEq,
            exists_unique_eq']
        Â· apply_fun f at h
          cases ((congr_fun s.condition x).symm.trans h).trans (congr_fun hÎ±Y val : _).symm
      delta ExistsUnique at this
      choose l hl hl' using this
      exact âŸ¨l, (funext hl).symm, Types.isTerminalPunit.hom_ext _ _,
        fun {l'} hâ‚ _ => funext fun x => hl' x (l' x) (congr_fun hâ‚ x).symmâŸ©
    Â· refine' âŸ¨âŸ¨hÎ±Y.symmâŸ©, âŸ¨PullbackCone.isLimitAux' _ _âŸ©âŸ©
      intro s
      have : âˆ€ x, âˆƒ! y, s.fst x = Sum.inr y := by
        intro x
        cases' h : s.fst x with val val
        Â· apply_fun f at h
          cases ((congr_fun s.condition x).symm.trans h).trans (congr_fun hÎ±X val : _).symm
        Â· simp only [Types.binaryCoproductCocone_pt, Functor.const_obj_obj, Sum.inr.injEq,
            exists_unique_eq']
      delta ExistsUnique at this
      choose l hl hl' using this
      exact âŸ¨l, (funext hl).symm, Types.isTerminalPunit.hom_ext _ _,
        fun {l'} hâ‚ _ => funext fun x => hl' x (l' x) (congr_fun hâ‚ x).symmâŸ©
  Â· intro Z f
    dsimp [Limits.Types.binaryCoproductCocone]
    delta Types.PullbackObj
    have : âˆ€ x, f x = Sum.inl PUnit.unit âˆ¨ f x = Sum.inr PUnit.unit := by
      intro x
      rcases f x with (âŸ¨âŸ¨âŸ©âŸ© | âŸ¨âŸ¨âŸ©âŸ©)
      exacts [Or.inl rfl, Or.inr rfl]
    let eX : { p : Z Ã— PUnit // f p.fst = Sum.inl p.snd } â‰ƒ { x : Z // f x = Sum.inl PUnit.unit } :=
      âŸ¨fun p => âŸ¨p.1.1, by convert p.2âŸ©, fun x => âŸ¨âŸ¨_, _âŸ©, x.2âŸ©, fun _ => by ext; rfl,
        fun _ => by ext; rflâŸ©
    let eY : { p : Z Ã— PUnit // f p.fst = Sum.inr p.snd } â‰ƒ { x : Z // f x = Sum.inr PUnit.unit } :=
      âŸ¨fun p => âŸ¨p.1.1, p.2.trans (congr_arg Sum.inr <| Subsingleton.elim _ _)âŸ©,
        fun x => âŸ¨âŸ¨_, _âŸ©, x.2âŸ©, fun _ => by ext; rfl, fun _ => by ext; rflâŸ©
    fapply BinaryCofan.isColimitMk
    Â· exact fun s x => dite _ (fun h => s.inl <| eX.symm âŸ¨x, hâŸ©)
        fun h => s.inr <| eY.symm âŸ¨x, (this x).resolve_left hâŸ©
    Â· intro s
      ext âŸ¨âŸ¨x, âŸ¨âŸ©âŸ©, _âŸ©
      dsimp
      split_ifs <;> rfl
    Â· intro s
      ext âŸ¨âŸ¨x, âŸ¨âŸ©âŸ©, hxâŸ©
      dsimp
      split_ifs with h
      Â· cases h.symm.trans hx
      Â· rfl
    Â· intro s m eâ‚ eâ‚‚
      ext x
      split_ifs
      Â· rw [â† eâ‚]
        rfl
      Â· rw [â† eâ‚‚]
        rfl
#align category_theory.types.finitary_extensive CategoryTheory.types.finitaryExtensive

section TopCat

/-- (Implementation) An auxiliary lemma for the proof that `TopCat` is finitary extensive. -/
-- Porting note : needs to add noncomputable modifier
noncomputable def finitaryExtensiveTopCatAux (Z : TopCat.{u})
    (f : Z âŸ¶ TopCat.of (Sum PUnit.{u + 1} PUnit.{u + 1})) :
    IsColimit (BinaryCofan.mk
      (TopCat.pullbackFst f (TopCat.binaryCofan (TopCat.of PUnit) (TopCat.of PUnit)).inl)
      (TopCat.pullbackFst f (TopCat.binaryCofan (TopCat.of PUnit) (TopCat.of PUnit)).inr)) := by
  have : âˆ€ x, f x = Sum.inl PUnit.unit âˆ¨ f x = Sum.inr PUnit.unit := by
    intro x
    rcases f x with (âŸ¨âŸ¨âŸ©âŸ© | âŸ¨âŸ¨âŸ©âŸ©)
    exacts [Or.inl rfl, Or.inr rfl]
  letI eX : { p : Z Ã— PUnit // f p.fst = Sum.inl p.snd } â‰ƒ { x : Z // f x = Sum.inl PUnit.unit } :=
    âŸ¨fun p => âŸ¨p.1.1, p.2.trans (congr_arg Sum.inl <| Subsingleton.elim _ _)âŸ©,
      fun x => âŸ¨âŸ¨_, PUnit.unitâŸ©, x.2âŸ©, fun _ => by ext; rfl, fun _ => by ext; rflâŸ©
  letI eY : { p : Z Ã— PUnit // f p.fst = Sum.inr p.snd } â‰ƒ { x : Z // f x = Sum.inr PUnit.unit } :=
    âŸ¨fun p => âŸ¨p.1.1, p.2.trans (congr_arg Sum.inr <| Subsingleton.elim _ _)âŸ©,
      fun x => âŸ¨âŸ¨_, PUnit.unitâŸ©, x.2âŸ©, fun _ => by ext; rfl, fun _ => by ext; rflâŸ©
  fapply BinaryCofan.isColimitMk
  classical -- Porting note: Added
  Â· refine' fun s => âŸ¨fun x => dite _
      (fun h => s.inl <| eX.symm âŸ¨x, hâŸ©) fun h => s.inr <| eY.symm âŸ¨x, (this x).resolve_left hâŸ©, _âŸ©
    rw [continuous_iff_continuousAt]
    intro x
    by_cases f x = Sum.inl PUnit.unit
    Â· revert h x
      apply (IsOpen.continuousOn_iff _).mp
      Â· rw [continuousOn_iff_continuous_restrict]
        convert_to Continuous fun x : { x | f x = Sum.inl PUnit.unit } =>
            s.inl âŸ¨(x.1, PUnit.unit), x.2âŸ©
        Â· ext âŸ¨x, hxâŸ©
          exact dif_pos hx
        -- Porting note : this `(BinaryCofan.inl s).2` was unnecessary
        have := (BinaryCofan.inl s).2
        continuity
      Â· convert f.2.1 _ openEmbedding_inl.open_range
        rename_i x
        exact âŸ¨fun h => âŸ¨_, h.symmâŸ©,
          fun âŸ¨e, hâŸ© => h.symm.trans (congr_arg Sum.inl <| Subsingleton.elim _ _)âŸ©
    Â· revert h x
      apply (IsOpen.continuousOn_iff _).mp
      Â· rw [continuousOn_iff_continuous_restrict]
        convert_to Continuous fun x : { x | f x â‰  Sum.inl PUnit.unit } =>
            s.inr âŸ¨(x.1, PUnit.unit), (this _).resolve_left x.2âŸ©
        Â· ext âŸ¨x, hxâŸ©
          exact dif_neg hx
        -- Porting note : this `(BinaryCofan.inr s).2` was unnecessary
        have := (BinaryCofan.inr s).2
        continuity
      Â· convert f.2.1 _ openEmbedding_inr.open_range
        rename_i x
        change f x â‰  Sum.inl PUnit.unit â†” f x âˆˆ Set.range Sum.inr
        trans f x = Sum.inr PUnit.unit
        Â· rcases f x with (âŸ¨âŸ¨âŸ©âŸ© | âŸ¨âŸ¨âŸ©âŸ©) <;>
            simp only [iff_self_iff, eq_self_iff_true, not_true, Ne.def, not_false_iff]
        Â· exact âŸ¨fun h => âŸ¨_, h.symmâŸ©,
            fun âŸ¨e, hâŸ© => h.symm.trans (congr_arg Sum.inr <| Subsingleton.elim _ _)âŸ©
  Â· intro s
    ext âŸ¨âŸ¨x, âŸ¨âŸ©âŸ©, (hx : f x = Sum.inl PUnit.unit)âŸ©
    change dite _ _ _ = _
    split_ifs with h
    Â· rfl
    Â· cases (h hx) -- Porting note : in Lean3 it is `rfl`
  Â· intro s
    ext âŸ¨âŸ¨x, âŸ¨âŸ©âŸ©, hxâŸ©
    change dite _ _ _ = _
    split_ifs with h
    Â· cases h.symm.trans hx
    Â· rfl
  Â· intro s m eâ‚ eâ‚‚
    ext x
    change m x = dite _ _ _
    split_ifs
    Â· rw [â† eâ‚]
      rfl
    Â· rw [â† eâ‚‚]
      rfl
set_option linter.uppercaseLean3 false in
#align category_theory.finitary_extensive_Top_aux CategoryTheory.finitaryExtensiveTopCatAux

instance : FinitaryExtensive TopCat.{u} := by
  rw [finitaryExtensive_iff_of_isTerminal TopCat.{u} _ TopCat.isTerminalPUnit _
      (TopCat.binaryCofanIsColimit _ _)]
  apply BinaryCofan.isVanKampen_mk _ _ (fun X Y => TopCat.binaryCofanIsColimit X Y) _
      fun f g => TopCat.pullbackConeIsLimit f g
  Â· intro X' Y' Î±X Î±Y f hÎ±X hÎ±Y
    constructor
    Â· refine' âŸ¨âŸ¨hÎ±X.symmâŸ©, âŸ¨PullbackCone.isLimitAux' _ _âŸ©âŸ©
      intro s
      have : âˆ€ x, âˆƒ! y, s.fst x = Sum.inl y := by
        intro x
        cases' h : s.fst x with val val
        Â· exact âŸ¨val, rfl, fun y h => Sum.inl_injective h.symmâŸ©
        Â· apply_fun f at h
          cases ((ConcreteCategory.congr_hom s.condition x).symm.trans h).trans
            (ConcreteCategory.congr_hom hÎ±Y val : _).symm
      delta ExistsUnique at this
      choose l hl hl' using this
      refine' âŸ¨âŸ¨l, _âŸ©, ContinuousMap.ext fun a => (hl a).symm, TopCat.isTerminalPUnit.hom_ext _ _,
        fun {l'} hâ‚ _ => ContinuousMap.ext fun x =>
          hl' x (l' x) (ConcreteCategory.congr_hom hâ‚ x).symmâŸ©
      apply (embedding_inl (Î± := X') (Î² := Y')).toInducing.continuous_iff.mpr
      convert s.fst.2 using 1
      exact (funext hl).symm
    Â· refine' âŸ¨âŸ¨hÎ±Y.symmâŸ©, âŸ¨PullbackCone.isLimitAux' _ _âŸ©âŸ©
      intro s
      have : âˆ€ x, âˆƒ! y, s.fst x = Sum.inr y := by
        intro x
        cases' h : s.fst x with val val
        Â· apply_fun f at h
          cases ((ConcreteCategory.congr_hom s.condition x).symm.trans h).trans
            (ConcreteCategory.congr_hom hÎ±X val : _).symm
        Â· exact âŸ¨val, rfl, fun y h => Sum.inr_injective h.symmâŸ©
      delta ExistsUnique at this
      choose l hl hl' using this
      refine' âŸ¨âŸ¨l, _âŸ©, ContinuousMap.ext fun a => (hl a).symm, TopCat.isTerminalPUnit.hom_ext _ _,
        fun {l'} hâ‚ _ =>
          ContinuousMap.ext fun x => hl' x (l' x) (ConcreteCategory.congr_hom hâ‚ x).symmâŸ©
      apply (embedding_inr (Î± := X') (Î² := Y')).toInducing.continuous_iff.mpr
      convert s.fst.2 using 1
      exact (funext hl).symm
  Â· intro Z f
    exact finitaryExtensiveTopCatAux Z f

end TopCat

section Functor

universe v'' u''

variable {D : Type u''} [Category.{v''} D]

theorem NatTrans.Equifibered.whiskerRight {F G : J â¥¤ C} {Î± : F âŸ¶ G} (hÎ± : Equifibered Î±)
    (H : C â¥¤ D) [PreservesLimitsOfShape WalkingCospan H] : Equifibered (whiskerRight Î± H) :=
  fun _ _ f => (hÎ± f).map H
#align category_theory.nat_trans.equifibered.whisker_right CategoryTheory.NatTrans.Equifibered.whiskerRight

theorem NatTrans.Equifibered.whiskerLeft {K : Type*} [Category K]  {F G : J â¥¤ C} {Î± : F âŸ¶ G}
    (hÎ± : Equifibered Î±) (H : K â¥¤ J) : Equifibered (whiskerLeft H Î±) :=
  fun _ _ f => hÎ± (H.map f)

theorem IsVanKampenColimit.of_iso {F : J â¥¤ C} {c c' : Cocone F} (H : IsVanKampenColimit c)
    (e : c â‰… c') : IsVanKampenColimit c' := by
  intro F' c'' Î± f h hÎ±
  have : c'.Î¹ â‰« (Functor.const J).map e.inv.hom = c.Î¹ := by
    ext j
    exact e.inv.2 j
  rw [H c'' Î± (f â‰« e.inv.1) (by rw [Functor.map_comp, â† reassoc_of% h, this]) hÎ±]
  apply forall_congr'
  intro j
  conv_lhs => rw [â† Category.comp_id (Î±.app j)]
  haveI : IsIso e.inv.hom := Functor.map_isIso (Cocones.forget _) e.inv
  exact (IsPullback.of_vert_isIso âŸ¨by simpâŸ©).paste_vert_iff (NatTrans.congr_app h j).symm
#align category_theory.is_van_kampen_colimit.of_iso CategoryTheory.IsVanKampenColimit.of_iso

theorem IsVanKampenColimit.of_map {D : Type*} [Category D] (G : C â¥¤ D) {F : J â¥¤ C} {c : Cocone F}
    [PreservesLimitsOfShape WalkingCospan G] [ReflectsLimitsOfShape WalkingCospan G]
    [PreservesColimitsOfShape J G] [ReflectsColimitsOfShape J G]
    (H : IsVanKampenColimit (G.mapCocone c)) : IsVanKampenColimit c := by
  intro F' c' Î± f h hÎ±
  refine' (Iff.trans _ (H (G.mapCocone c') (whiskerRight Î± G) (G.map f)
      (by ext j; simpa using G.congr_map (NatTrans.congr_app h j))
      (hÎ±.whiskerRight G))).trans (forall_congr' fun j => _)
  Â· exact âŸ¨fun h => âŸ¨isColimitOfPreserves G h.someâŸ©, fun h => âŸ¨isColimitOfReflects G h.someâŸ©âŸ©
  Â· exact IsPullback.map_iff G (NatTrans.congr_app h.symm j)
#align category_theory.is_van_kampen_colimit.of_map CategoryTheory.IsVanKampenColimit.of_map

theorem IsVanKampenColimit.precompose_isIso {F G : J â¥¤ C} (Î± : F âŸ¶ G) [IsIso Î±]
    {c : Cocone G} (hc : IsVanKampenColimit c) :
    IsVanKampenColimit ((Cocones.precompose Î±).obj c) := by
  intros F' c' Î±' f e hÎ±
  refine (hc c' (Î±' â‰« Î±) f ((Category.assoc _ _ _).trans e)
    (hÎ±.comp (NatTrans.equifibered_of_isIso _))).trans ?_
  apply forall_congr'
  intro j
  simp only [Functor.const_obj_obj, NatTrans.comp_app,
    Cocones.precompose_obj_pt, Cocones.precompose_obj_Î¹]
  have : IsPullback (Î±.app j â‰« c.Î¹.app j) (Î±.app j) (ğŸ™ _) (c.Î¹.app j) :=
    IsPullback.of_vert_isIso âŸ¨Category.comp_id _âŸ©
  rw [â† IsPullback.paste_vert_iff this _, Category.comp_id]
  exact (congr_app e j).symm

theorem IsVanKampenColimit.precompose_isIso_iff {F G : J â¥¤ C} (Î± : F âŸ¶ G) [IsIso Î±]
    {c : Cocone G} : IsVanKampenColimit ((Cocones.precompose Î±).obj c) â†” IsVanKampenColimit c :=
  âŸ¨fun hc â†¦ IsVanKampenColimit.of_iso (IsVanKampenColimit.precompose_isIso (inv Î±) hc)
    (Cocones.ext (Iso.refl _) (by simp)),
    IsVanKampenColimit.precompose_isIso Î±âŸ©

theorem IsVanKampenColimit.whiskerEquivalence {K : Type*} [Category K] (e : J â‰Œ K)
    {F : K â¥¤ C} {c : Cocone F} (hc : IsVanKampenColimit c) :
    IsVanKampenColimit (c.whisker e.functor) := by
  intro F' c' Î± f e' hÎ±
  convert hc (c'.whisker e.inverse) (whiskerLeft e.inverse Î± â‰« (e.invFunIdAssoc F).hom) f ?_
    ((hÎ±.whiskerLeft _).comp (NatTrans.equifibered_of_isIso _)) using 1
  Â· exact (IsColimit.whiskerEquivalenceEquiv e.symm).nonempty_congr
  Â· simp only [Functor.const_obj_obj, Functor.comp_obj, Cocone.whisker_pt, Cocone.whisker_Î¹,
      whiskerLeft_app, NatTrans.comp_app, Equivalence.invFunIdAssoc_hom_app, Functor.id_obj]
    constructor
    Â· intro H k
      rw [â† Category.comp_id f]
      refine (H (e.inverse.obj k)).paste_vert ?_
      have : IsIso (ğŸ™ (Cocone.whisker e.functor c).pt) := inferInstance
      exact IsPullback.of_vert_isIso âŸ¨by simpâŸ©
    Â· intro H j
      have : Î±.app j = F'.map (e.unit.app _) â‰« Î±.app _ â‰« F.map (e.counit.app (e.functor.obj j))
      Â· simp [â† Functor.map_comp]
      rw [â† Category.id_comp f, this]
      refine IsPullback.paste_vert ?_ (H (e.functor.obj j))
      exact IsPullback.of_vert_isIso âŸ¨by simpâŸ©
  Â· ext k
    simpa using congr_app e' (e.inverse.obj k)

theorem IsVanKampenColimit.whiskerEquivalence_iff {K : Type*} [Category K] (e : J â‰Œ K)
    {F : K â¥¤ C} {c : Cocone F} :
    IsVanKampenColimit (c.whisker e.functor) â†” IsVanKampenColimit c :=
  âŸ¨fun hc â†¦ ((hc.whiskerEquivalence e.symm).precompose_isIso (e.invFunIdAssoc F).inv).of_iso
      (Cocones.ext (Iso.refl _) (by simp)), IsVanKampenColimit.whiskerEquivalence eâŸ©

theorem isVanKampenColimit_of_evaluation [HasPullbacks D] [HasColimitsOfShape J D] (F : J â¥¤ C â¥¤ D)
    (c : Cocone F) (hc : âˆ€ x : C, IsVanKampenColimit (((evaluation C D).obj x).mapCocone c)) :
    IsVanKampenColimit c := by
  intro F' c' Î± f e hÎ±
  have := fun x => hc x (((evaluation C D).obj x).mapCocone c') (whiskerRight Î± _)
      (((evaluation C D).obj x).map f)
      (by
        ext y
        dsimp
        exact NatTrans.congr_app (NatTrans.congr_app e y) x)
      (hÎ±.whiskerRight _)
  constructor
  Â· rintro âŸ¨hc'âŸ© j
    refine' âŸ¨âŸ¨(NatTrans.congr_app e j).symmâŸ©, âŸ¨evaluationJointlyReflectsLimits _ _âŸ©âŸ©
    refine' fun x => (isLimitMapConePullbackConeEquiv _ _).symm _
    exact ((this x).mp âŸ¨PreservesColimit.preserves hc'âŸ© _).isLimit
  Â· exact fun H => âŸ¨evaluationJointlyReflectsColimits _ fun x =>
      ((this x).mpr fun j => (H j).map ((evaluation C D).obj x)).someâŸ©
#align category_theory.is_van_kampen_colimit_of_evaluation CategoryTheory.isVanKampenColimit_of_evaluation

instance [HasPullbacks C] [FinitaryExtensive C] : FinitaryExtensive (D â¥¤ C) :=
  haveI : HasFiniteCoproducts (D â¥¤ C) := âŸ¨fun _ => Limits.functorCategoryHasColimitsOfShapeâŸ©
  âŸ¨fun c hc => isVanKampenColimit_of_evaluation _ c fun _ =>
    FinitaryExtensive.vanKampen _ <| PreservesColimit.preserves hcâŸ©

theorem finitaryExtensive_of_preserves_and_reflects (F : C â¥¤ D) [FinitaryExtensive D]
    [HasFiniteCoproducts C] [âˆ€ {X Y Z : C} (f : Z âŸ¶ X â¨¿ Y), HasPullback coprod.inl f]
    [PreservesLimitsOfShape WalkingCospan F]
    [ReflectsLimitsOfShape WalkingCospan F] [PreservesColimitsOfShape (Discrete WalkingPair) F]
    [ReflectsColimitsOfShape (Discrete WalkingPair) F] : FinitaryExtensive C :=
  âŸ¨fun _ hc => (FinitaryExtensive.vanKampen _ (isColimitOfPreserves F hc)).of_map FâŸ©
#align category_theory.finitary_extensive_of_preserves_and_reflects CategoryTheory.finitaryExtensive_of_preserves_and_reflects

theorem finitaryExtensive_of_preserves_and_reflects_isomorphism (F : C â¥¤ D) [FinitaryExtensive D]
    [HasFiniteCoproducts C] [HasPullbacks C] [PreservesLimitsOfShape WalkingCospan F]
    [PreservesColimitsOfShape (Discrete WalkingPair) F] [ReflectsIsomorphisms F] :
    FinitaryExtensive C := by
  haveI : ReflectsLimitsOfShape WalkingCospan F := reflectsLimitsOfShapeOfReflectsIsomorphisms
  haveI : ReflectsColimitsOfShape (Discrete WalkingPair) F :=
    reflectsColimitsOfShapeOfReflectsIsomorphisms
  exact finitaryExtensive_of_preserves_and_reflects F
#align category_theory.finitary_extensive_of_preserves_and_reflects_isomorphism CategoryTheory.finitaryExtensive_of_preserves_and_reflects_isomorphism

end Functor

section FiniteCoproducts

theorem NatTrans.equifibered_of_discrete {Î¹ : Type*} {F G : Discrete Î¹ â¥¤ C}
    (Î± : F âŸ¶ G) : NatTrans.Equifibered Î± := by
  rintro âŸ¨iâŸ© âŸ¨jâŸ© âŸ¨âŸ¨rfl : i = jâŸ©âŸ©
  simp only [Discrete.functor_map_id]
  refine IsPullback.of_horiz_isIso âŸ¨by rw [Category.id_comp, Category.comp_id]âŸ©

theorem isUniversalColimit_extendCofan {n : â„•} (f : Fin (n + 1) â†’ C)
    {câ‚ : Cofan fun i : Fin n â†¦ f i.succ} {câ‚‚ : BinaryCofan (f 0) câ‚.pt}
    (tâ‚ : IsUniversalColimit câ‚) (tâ‚‚ : IsUniversalColimit câ‚‚)
    [âˆ€ {Z} (i : Z âŸ¶ câ‚‚.pt), HasPullback câ‚‚.inr i] :
    IsUniversalColimit (extendCofan câ‚ câ‚‚) := by
  intro F c Î± i e hÎ± H
  let F' : Fin (n + 1) â†’ C := F.obj âˆ˜ Discrete.mk
  have : F = Discrete.functor F'
  Â· apply Functor.hext
    Â· exact fun i â†¦ rfl
    Â· rintro âŸ¨iâŸ© âŸ¨jâŸ© âŸ¨âŸ¨rfl : i = jâŸ©âŸ©
      simp
  have tâ‚' := @tâ‚ (Discrete.functor (fun j â†¦ F.obj âŸ¨j.succâŸ©))
    (Cofan.mk (pullback câ‚‚.inr i) <| fun j â†¦ pullback.lift (Î±.app _ â‰« câ‚.inj _) (c.Î¹.app _) ?_)
    (Discrete.natTrans <| fun i â†¦ Î±.app _) pullback.fst ?_ (NatTrans.equifibered_of_discrete _) ?_
  rotate_left
  Â· simpa only [Functor.const_obj_obj, pair_obj_right, Discrete.functor_obj, Category.assoc,
      extendCofan_pt, Functor.const_obj_obj, NatTrans.comp_app, extendCofan_Î¹_app,
      Fin.cases_succ, Functor.const_map_app] using congr_app e âŸ¨j.succâŸ©
  Â· ext j
    dsimp
    simp only [limit.lift_Ï€, PullbackCone.mk_pt, PullbackCone.mk_Ï€_app, Cofan.inj]
  Â· intro j
    simp only [pair_obj_right, Functor.const_obj_obj, Discrete.functor_obj, id_eq,
      extendCofan_pt, eq_mpr_eq_cast, Cofan.mk_pt, Cofan.mk_Î¹_app, Discrete.natTrans_app]
    refine IsPullback.of_right ?_ ?_ (IsPullback.of_hasPullback (BinaryCofan.inr câ‚‚) i).flip
    Â· simp only [Functor.const_obj_obj, pair_obj_right, limit.lift_Ï€,
        PullbackCone.mk_pt, PullbackCone.mk_Ï€_app]
      exact H _
    Â· simp only [limit.lift_Ï€, PullbackCone.mk_pt, PullbackCone.mk_Ï€_app, Cofan.inj]
  obtain âŸ¨Hâ‚âŸ© := tâ‚'
  have tâ‚‚' := @tâ‚‚ (pair (F.obj âŸ¨0âŸ©) (pullback câ‚‚.inr i)) (BinaryCofan.mk (c.Î¹.app âŸ¨0âŸ©) pullback.snd)
    (mapPair (Î±.app _) pullback.fst) i ?_ (mapPair_equifibered _) ?_
  rotate_left
  Â· ext âŸ¨âŸ¨âŸ©âŸ©
    Â· simpa [mapPair] using congr_app e âŸ¨0âŸ©
    Â· simpa using pullback.condition
  Â· rintro âŸ¨âŸ¨âŸ©âŸ©
    Â· simp only [pair_obj_right, Functor.const_obj_obj, pair_obj_left, BinaryCofan.mk_pt,
        BinaryCofan.Î¹_app_left, BinaryCofan.mk_inl, mapPair_left]
      exact H âŸ¨0âŸ©
    Â· simp only [pair_obj_right, Functor.const_obj_obj, BinaryCofan.mk_pt, BinaryCofan.Î¹_app_right,
        BinaryCofan.mk_inr, mapPair_right]
      exact (IsPullback.of_hasPullback (BinaryCofan.inr câ‚‚) i).flip
  obtain âŸ¨Hâ‚‚âŸ© := tâ‚‚'
  clear_value F'
  subst this
  refine âŸ¨IsColimit.ofIsoColimit (extendCofanIsColimit
    (fun i â†¦ (Discrete.functor F').obj âŸ¨iâŸ©) Hâ‚ Hâ‚‚) <| Cocones.ext (Iso.refl _) ?_âŸ©
  dsimp
  rintro âŸ¨jâŸ©
  simp only [Discrete.functor_obj, limit.lift_Ï€, PullbackCone.mk_pt,
    PullbackCone.mk_Ï€_app, Category.comp_id]
  induction' j using Fin.inductionOn
  Â· simp only [Fin.cases_zero]
  Â· simp only [Fin.cases_succ]

theorem isVanKampenColimit_extendCofan {n : â„•} (f : Fin (n + 1) â†’ C)
    {câ‚ : Cofan fun i : Fin n â†¦ f i.succ} {câ‚‚ : BinaryCofan (f 0) câ‚.pt}
    (tâ‚ : IsVanKampenColimit câ‚) (tâ‚‚ : IsVanKampenColimit câ‚‚)
    [âˆ€ {Z} (i : Z âŸ¶ câ‚‚.pt), HasPullback câ‚‚.inr i]
    [HasFiniteCoproducts C] :
    IsVanKampenColimit (extendCofan câ‚ câ‚‚) := by
  intro F c Î± i e hÎ±
  refine âŸ¨?_, isUniversalColimit_extendCofan f tâ‚.isUniversal tâ‚‚.isUniversal c Î± i e hÎ±âŸ©
  intro âŸ¨HcâŸ© âŸ¨jâŸ©
  have tâ‚‚' := (@tâ‚‚ (pair (F.obj âŸ¨0âŸ©) (âˆ fun (j : Fin n) â†¦ F.obj âŸ¨j.succâŸ©))
    (BinaryCofan.mk (P := c.pt) (c.Î¹.app _) (Sigma.desc <| fun b â†¦ c.Î¹.app _))
    (mapPair (Î±.app _) (Sigma.desc <| fun b â†¦ Î±.app _ â‰« câ‚.inj _)) i ?_
    (mapPair_equifibered _)).mp âŸ¨?_âŸ©
  rotate_left
  Â· ext âŸ¨âŸ¨âŸ©âŸ©
    Â· simpa only [pair_obj_left, Functor.const_obj_obj, pair_obj_right, Discrete.functor_obj,
        NatTrans.comp_app, mapPair_left, BinaryCofan.Î¹_app_left, BinaryCofan.mk_pt,
        BinaryCofan.mk_inl, Functor.const_map_app, extendCofan_pt,
        extendCofan_Î¹_app, Fin.cases_zero] using congr_app e âŸ¨0âŸ©
    Â· dsimp
      ext j
      simpa only [colimit.Î¹_desc_assoc, Discrete.functor_obj, Cofan.mk_pt, Cofan.mk_Î¹_app,
        Category.assoc, extendCofan_pt, Functor.const_obj_obj, NatTrans.comp_app, extendCofan_Î¹_app,
        Fin.cases_succ, Functor.const_map_app] using congr_app e âŸ¨j.succâŸ©
  Â· let F' : Fin (n + 1) â†’ C := F.obj âˆ˜ Discrete.mk
    have : F = Discrete.functor F'
    Â· apply Functor.hext
      Â· exact fun i â†¦ rfl
      Â· rintro âŸ¨iâŸ© âŸ¨jâŸ© âŸ¨âŸ¨rfl : i = jâŸ©âŸ©
        simp
    clear_value F'
    subst this
    apply BinaryCofan.IsColimit.mk _ (fun {T} fâ‚ fâ‚‚ â†¦ Hc.desc (Cofan.mk T (Fin.cases fâ‚
      (fun i â†¦ Sigma.Î¹ (fun (j : Fin n) â†¦ (Discrete.functor F').obj âŸ¨j.succâŸ©) _ â‰« fâ‚‚))))
    Â· intro T fâ‚ fâ‚‚
      simp only [Discrete.functor_obj, pair_obj_left, BinaryCofan.mk_pt, Functor.const_obj_obj,
        BinaryCofan.Î¹_app_left, BinaryCofan.mk_inl, IsColimit.fac, Cofan.mk_pt, Cofan.mk_Î¹_app,
        Fin.cases_zero]
    Â· intro T fâ‚ fâ‚‚
      simp only [Discrete.functor_obj, pair_obj_right, BinaryCofan.mk_pt, Functor.const_obj_obj,
        BinaryCofan.Î¹_app_right, BinaryCofan.mk_inr]
      ext j
      simp only [colimit.Î¹_desc_assoc, Discrete.functor_obj, Cofan.mk_pt,
        Cofan.mk_Î¹_app, IsColimit.fac, Fin.cases_succ]
    Â· intro T fâ‚ fâ‚‚ fâ‚ƒ mâ‚ mâ‚‚
      simp at mâ‚ mâ‚‚ âŠ¢
      refine Hc.uniq (Cofan.mk T (Fin.cases fâ‚
        (fun i â†¦ Sigma.Î¹ (fun (j : Fin n) â†¦ (Discrete.functor F').obj âŸ¨j.succâŸ©) _ â‰« fâ‚‚))) _ ?_
      intro âŸ¨jâŸ©
      simp only [Discrete.functor_obj, Cofan.mk_pt, Functor.const_obj_obj, Cofan.mk_Î¹_app]
      induction' j using Fin.inductionOn with j _
      Â· simp only [Fin.cases_zero, mâ‚]
      Â· simp only [â† mâ‚‚, colimit.Î¹_desc_assoc, Discrete.functor_obj,
          Cofan.mk_pt, Cofan.mk_Î¹_app, Fin.cases_succ]
  induction' j using Fin.inductionOn with j _
  Â· exact tâ‚‚' âŸ¨WalkingPair.leftâŸ©
  Â· have tâ‚' := (@tâ‚ (Discrete.functor (fun j â†¦ F.obj âŸ¨j.succâŸ©)) (Cofan.mk _ _) (Discrete.natTrans
      <| fun i â†¦ Î±.app _) (Sigma.desc (fun j â†¦ Î±.app _ â‰« câ‚.inj _)) ?_
      (NatTrans.equifibered_of_discrete _)).mp âŸ¨coproductIsCoproduct _âŸ© âŸ¨jâŸ©
    rotate_left
    Â· ext âŸ¨jâŸ©
      dsimp
      erw [colimit.Î¹_desc] -- Why?
      rfl
    simpa [Functor.const_obj_obj, Discrete.functor_obj, extendCofan_pt, extendCofan_Î¹_app,
      Fin.cases_succ, BinaryCofan.mk_pt, colimit.cocone_x, Cofan.mk_pt, Cofan.mk_Î¹_app,
      BinaryCofan.Î¹_app_right, BinaryCofan.mk_inr, colimit.Î¹_desc,
      Discrete.natTrans_app] using tâ‚'.paste_horiz (tâ‚‚' âŸ¨WalkingPair.rightâŸ©)

def functorOfIsEmpty (J : Type*) [Category J] (C : Type*) [Category C] [IsEmpty J] : J â¥¤ C where
  obj := isEmptyElim
  map := fun {X} â†¦ isEmptyElim X
  map_id := fun {X} â†¦ isEmptyElim X
  map_comp := fun {X} â†¦ isEmptyElim X

instance (Î± : Type*) [IsEmpty Î±] : IsEmpty (Discrete Î±) := Function.isEmpty Discrete.as

def equivalenceOfIsEmpty (J : Type*) [Category J] [IsEmpty J] (K : Type*) [Category K] [IsEmpty K] :
  J â‰Œ K where
  functor := functorOfIsEmpty J K
  inverse := functorOfIsEmpty K J
  unitIso := NatIso.ofComponents isEmptyElim (fun {X} â†¦ isEmptyElim X)
  counitIso := NatIso.ofComponents isEmptyElim (fun {X} â†¦ isEmptyElim X)
  functor_unitIso_comp := fun {X} â†¦ isEmptyElim X

theorem isVanKampenColimit_of_isEmpty [HasStrictInitialObjects C] [IsEmpty J] {F : J â¥¤ C}
  (c : Cocone F) (hc : IsColimit c) : IsVanKampenColimit c := by
  have : IsInitial c.pt
  Â· have := (IsColimit.precomposeInvEquiv (Functor.uniqueFromEmpty _) _).symm
      (hc.whiskerEquivalence (equivalenceOfIsEmpty (Discrete PEmpty.{1}) J))
    exact IsColimit.ofIsoColimit this (Cocones.ext (Iso.refl c.pt) (fun {X} â†¦ isEmptyElim X))
  replace this := IsInitial.isVanKampenColimit this
  apply (IsVanKampenColimit.whiskerEquivalence_iff
    (equivalenceOfIsEmpty (Discrete PEmpty.{1}) J)).mp
  exact (this.precompose_isIso (Functor.uniqueFromEmpty
    ((equivalenceOfIsEmpty (Discrete PEmpty.{1}) J).functor â‹™ F)).hom).of_iso
    (Cocones.ext (Iso.refl _) (by simp))

theorem FinitaryExtensive.isVanKampen_finiteCoproducts_Fin [FinitaryExtensive C] {n : â„•}
  {F : Discrete (Fin n) â¥¤ C} {c : Cocone F} (hc : IsColimit c) : IsVanKampenColimit c := by
  let f : Fin n â†’ C := F.obj âˆ˜ Discrete.mk
  have : F = Discrete.functor f
  Â· refine Functor.hext (fun i â†¦ rfl) ?_
    rintro âŸ¨iâŸ© âŸ¨jâŸ© âŸ¨âŸ¨rfl : i = jâŸ©âŸ©
    simp
  clear_value f
  subst this
  induction' n with n IH
  Â· exact isVanKampenColimit_of_isEmpty _ hc
  Â· apply IsVanKampenColimit.of_iso _
      ((extendCofanIsColimit f (coproductIsCoproduct _) (coprodIsCoprod _ _)).uniqueUpToIso hc)
    apply @isVanKampenColimit_extendCofan _ _ _ _ _ _ _ _ ?_
    Â· apply IH
      exact coproductIsCoproduct _
    Â· apply FinitaryExtensive.van_kampen'
      exact coprodIsCoprod _ _
    Â· dsimp
      infer_instance

theorem FinitaryExtensive.isVanKampen_finiteCoproducts [FinitaryExtensive C] {Î¹ : Type*}
    [Finite Î¹] {F : Discrete Î¹ â¥¤ C} {c : Cocone F} (hc : IsColimit c) : IsVanKampenColimit c := by
  obtain âŸ¨n, âŸ¨eâŸ©âŸ© := Finite.exists_equiv_fin Î¹
  apply (IsVanKampenColimit.whiskerEquivalence_iff (Discrete.equivalence e).symm).mp
  apply FinitaryExtensive.isVanKampen_finiteCoproducts_Fin
  exact (IsColimit.whiskerEquivalenceEquiv (Discrete.equivalence e).symm) hc

lemma FinitaryExtensive.hasPullbacks_of_is_coproduct [FinitaryExtensive C] {Î¹ : Type*}
    [Finite Î¹] {F : Discrete Î¹ â¥¤ C} {c : Cocone F} (hc : IsColimit c) (i : Discrete Î¹) {X : C}
    (g : X âŸ¶ _) : HasPullback g (c.Î¹.app i) := by
  classical
  let f : Î¹ â†’ C := F.obj âˆ˜ Discrete.mk
  have : F = Discrete.functor f
  Â· refine Functor.hext (fun i â†¦ rfl) ?_
    rintro âŸ¨iâŸ© âŸ¨jâŸ© âŸ¨âŸ¨rfl : i = jâŸ©âŸ©
    simp
  clear_value f
  subst this
  change Cofan f at c
  obtain âŸ¨iâŸ© := i
  let e : âˆ f â‰… f i â¨¿ (âˆ fun j : ({i}á¶œ : Set Î¹) â†¦ f j) :=
  { hom := Sigma.desc (fun j â†¦ if h : j = i then eqToHom (congr_arg f h) â‰« coprod.inl else
      Sigma.Î¹ (fun j : ({i}á¶œ : Set Î¹) â†¦ f j) âŸ¨j, hâŸ© â‰« coprod.inr)
    inv := coprod.desc (Sigma.Î¹ f i) (Sigma.desc <| fun j â†¦ Sigma.Î¹ f j)
    hom_inv_id := by aesop_cat
    inv_hom_id := by
      ext j
      Â· simp
      Â· simp only [coprod.desc_comp, colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app,
          eqToHom_refl, Category.id_comp, dite_true, BinaryCofan.mk_pt, BinaryCofan.Î¹_app_right,
          BinaryCofan.mk_inr, colimit.Î¹_desc_assoc, Discrete.functor_obj, Category.comp_id]
        exact dif_neg j.prop }
  let e' : c.pt â‰… f i â¨¿ (âˆ fun j : ({i}á¶œ : Set Î¹) â†¦ f j) :=
    hc.coconePointUniqueUpToIso (getColimitCocone _).2 â‰ªâ‰« e
  have : coprod.inl â‰« e'.inv = c.Î¹.app âŸ¨iâŸ©
  Â· simp only [Iso.trans_inv, coprod.desc_comp, colimit.Î¹_desc, BinaryCofan.mk_pt,
      BinaryCofan.Î¹_app_left, BinaryCofan.mk_inl]
    exact colimit.comp_coconePointUniqueUpToIso_inv _ _
  clear_value e'
  rw [â† this]
  have : IsPullback (ğŸ™ _) (g â‰« e'.hom) g e'.inv := IsPullback.of_horiz_isIso âŸ¨by simpâŸ©
  exact âŸ¨âŸ¨âŸ¨_, ((IsPullback.of_hasPullback (g â‰« e'.hom) coprod.inl).paste_horiz this).isLimitâŸ©âŸ©âŸ©

instance FinitaryExtensive.hasPullbacks_of_inclusions [FinitaryExtensive C] {X Z : C} {Î± : Type*}
    (f : X âŸ¶ Z) {Y : (a : Î±) â†’ C} (i : (a : Î±) â†’ Y a âŸ¶ Z) [Finite Î±]
    [hi : IsIso (Sigma.desc i)] (a : Î±) : HasPullback f (i a) := by
  apply FinitaryExtensive.hasPullbacks_of_is_coproduct (c := Cofan.mk Z i)
  exact @IsColimit.ofPointIso (t := Cofan.mk Z i) (P := _) hi

lemma FinitaryExtensive.sigma_desc_iso [FinitaryExtensive C] {Î± : Type} [Finite Î±] {X : C}
    {Z : Î± â†’ C} (Ï€ : (a : Î±) â†’ Z a âŸ¶ X) {Y : C} (f : Y âŸ¶ X) (hÏ€ : IsIso (Sigma.desc Ï€)) :
    IsIso (Sigma.desc ((fun _ â†¦ pullback.fst) : (a : Î±) â†’ pullback f (Ï€ a) âŸ¶ _)) := by
  suffices IsColimit (Cofan.mk _ ((fun _ â†¦ pullback.fst) : (a : Î±) â†’ pullback f (Ï€ a) âŸ¶ _)) by
    change IsIso (this.coconePointUniqueUpToIso (getColimitCocone _).2).inv
    infer_instance
  let : IsColimit (Cofan.mk X Ï€)
  Â· refine @IsColimit.ofPointIso (t := Cofan.mk X Ï€) (P := coproductIsCoproduct Z) ?_
    convert hÏ€
    simp [coproductIsCoproduct]
  refine ((FinitaryExtensive.isVanKampen_finiteCoproducts this
    (Cofan.mk _ ((fun _ â†¦ pullback.fst) : (a : Î±) â†’ pullback f (Ï€ a) âŸ¶ _))
    (Discrete.natTrans <| fun i â†¦ pullback.snd) f ?_
    (NatTrans.equifibered_of_discrete _)).mpr ?_).some
  Â· ext
    simp [pullback.condition]
  Â· exact fun j â†¦ IsPullback.of_hasPullback f (Ï€ j.as)

end FiniteCoproducts

end Extensive

end CategoryTheory
