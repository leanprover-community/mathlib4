/-
Copyright (c) 2020 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Comma

#align_import category_theory.arrow from "leanprover-community/mathlib"@"32253a1a1071173b33dc7d6a218cf722c6feb514"

/-!
# The category of arrows

The category of arrows, with morphisms commutative squares.
We set this up as a specialization of the comma category `Comma L R`,
where `L` and `R` are both the identity functor.

## Tags

comma, arrow
-/


namespace CategoryTheory

universe v u

-- morphism levels before object levels. See note [CategoryTheory universes].
variable {T : Type u} [Category.{v} T]

section

variable (T)

/-- The arrow category of `T` has as objects all morphisms in `T` and as morphisms commutative
     squares in `T`. -/
def Arrow :=
  Comma.{v, v, v} (ğŸ­ T) (ğŸ­ T)
#align category_theory.arrow CategoryTheory.Arrow

/- Porting note: could not derive `Category` above so this instance works in its place-/
instance : Category (Arrow T) := commaCategory

-- Satisfying the inhabited linter
instance Arrow.inhabited [Inhabited T] : Inhabited (Arrow T)
    where default := show Comma (ğŸ­ T) (ğŸ­ T) from default
#align category_theory.arrow.inhabited CategoryTheory.Arrow.inhabited

end

namespace Arrow

@[ext]
lemma hom_ext {X Y : Arrow T} (f g : X âŸ¶ Y) (hâ‚ : f.left = g.left) (hâ‚‚ : f.right = g.right) :
    f = g :=
  CommaMorphism.ext _ _ hâ‚ hâ‚‚

@[simp]
theorem id_left (f : Arrow T) : CommaMorphism.left (ğŸ™ f) = ğŸ™ f.left :=
  rfl
#align category_theory.arrow.id_left CategoryTheory.Arrow.id_left

@[simp]
theorem id_right (f : Arrow T) : CommaMorphism.right (ğŸ™ f) = ğŸ™ f.right :=
  rfl
#align category_theory.arrow.id_right CategoryTheory.Arrow.id_right

-- porting note: added to ease automation
@[simp]
theorem comp_left {X Y Z : Arrow T} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
  (f â‰« g).left = f.left â‰« g.left := rfl

-- porting note: added to ease automation
@[simp]
theorem comp_right {X Y Z : Arrow T} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
  (f â‰« g).right = f.right â‰« g.right := rfl

/-- An object in the arrow category is simply a morphism in `T`. -/
@[simps]
def mk {X Y : T} (f : X âŸ¶ Y) : Arrow T where
  left := X
  right := Y
  hom := f
#align category_theory.arrow.mk CategoryTheory.Arrow.mk

@[simp]
theorem mk_eq (f : Arrow T) : Arrow.mk f.hom = f := by
  cases f
  -- âŠ¢ mk { left := leftâœ, right := rightâœ, hom := homâœ }.hom = { left := leftâœ, ri â€¦
  rfl
  -- ğŸ‰ no goals
#align category_theory.arrow.mk_eq CategoryTheory.Arrow.mk_eq

theorem mk_injective (A B : T) :
    Function.Injective (Arrow.mk : (A âŸ¶ B) â†’ Arrow T) := fun f g h => by
  cases h
  -- âŠ¢ f = f
  rfl
  -- ğŸ‰ no goals
#align category_theory.arrow.mk_injective CategoryTheory.Arrow.mk_injective

theorem mk_inj (A B : T) {f g : A âŸ¶ B} : Arrow.mk f = Arrow.mk g â†” f = g :=
  (mk_injective A B).eq_iff
#align category_theory.arrow.mk_inj CategoryTheory.Arrow.mk_inj

/- Porting note : was marked as dangerous instance so changed from `Coe` to `CoeOut` -/
instance {X Y : T} : CoeOut (X âŸ¶ Y) (Arrow T) where
  coe := mk

/-- A morphism in the arrow category is a commutative square connecting two objects of the arrow
    category. -/
@[simps]
def homMk {f g : Arrow T} {u : f.left âŸ¶ g.left} {v : f.right âŸ¶ g.right}
    (w : u â‰« g.hom = f.hom â‰« v) : f âŸ¶ g where
  left := u
  right := v
  w := w
#align category_theory.arrow.hom_mk CategoryTheory.Arrow.homMk

/-- We can also build a morphism in the arrow category out of any commutative square in `T`. -/
@[simps]
def homMk' {X Y : T} {f : X âŸ¶ Y} {P Q : T} {g : P âŸ¶ Q} {u : X âŸ¶ P} {v : Y âŸ¶ Q} (w : u â‰« g = f â‰« v) :
    Arrow.mk f âŸ¶ Arrow.mk g where
  left := u
  right := v
  w := w
#align category_theory.arrow.hom_mk' CategoryTheory.Arrow.homMk'

/- Porting note : was warned simp could prove reassoc'd version. Found simp could not.
Added nolint. -/
@[reassoc (attr := simp, nolint simpNF)]
theorem w {f g : Arrow T} (sq : f âŸ¶ g) : sq.left â‰« g.hom = f.hom â‰« sq.right :=
  sq.w
#align category_theory.arrow.w CategoryTheory.Arrow.w

-- `w_mk_left` is not needed, as it is a consequence of `w` and `mk_hom`.
@[reassoc (attr := simp)]
theorem w_mk_right {f : Arrow T} {X Y : T} {g : X âŸ¶ Y} (sq : f âŸ¶ mk g) :
    sq.left â‰« g = f.hom â‰« sq.right :=
  sq.w
#align category_theory.arrow.w_mk_right CategoryTheory.Arrow.w_mk_right

theorem isIso_of_isIso_left_of_isIso_right {f g : Arrow T} (ff : f âŸ¶ g) [IsIso ff.left]
    [IsIso ff.right] : IsIso ff where
  out := by
    let inverse : g âŸ¶ f := âŸ¨inv ff.left, inv ff.right, (by simp)âŸ©
    -- âŠ¢ âˆƒ inv, ff â‰« inv = ğŸ™ f âˆ§ inv â‰« ff = ğŸ™ g
    apply Exists.intro inverse
    -- âŠ¢ ff â‰« inverse = ğŸ™ f âˆ§ inverse â‰« ff = ğŸ™ g
    aesop_cat
    -- ğŸ‰ no goals
#align category_theory.arrow.is_iso_of_iso_left_of_is_iso_right CategoryTheory.Arrow.isIso_of_isIso_left_of_isIso_right

/-- Create an isomorphism between arrows,
by providing isomorphisms between the domains and codomains,
and a proof that the square commutes. -/
@[simps!]
def isoMk {f g : Arrow T} (l : f.left â‰… g.left) (r : f.right â‰… g.right)
    (h : l.hom â‰« g.hom = f.hom â‰« r.hom := by aesop_cat) : f â‰… g :=
  Comma.isoMk l r h
#align category_theory.arrow.iso_mk CategoryTheory.Arrow.isoMk

/-- A variant of `Arrow.isoMk` that creates an iso between two `Arrow.mk`s with a better type
signature. -/
abbrev isoMk' {W X Y Z : T} (f : W âŸ¶ X) (g : Y âŸ¶ Z) (eâ‚ : W â‰… Y) (eâ‚‚ : X â‰… Z)
    (h : eâ‚.hom â‰« g = f â‰« eâ‚‚.hom := by aesop_cat) : Arrow.mk f â‰… Arrow.mk g :=
  Arrow.isoMk eâ‚ eâ‚‚ h
#align category_theory.arrow.iso_mk' CategoryTheory.Arrow.isoMk'

theorem hom.congr_left {f g : Arrow T} {Ï†â‚ Ï†â‚‚ : f âŸ¶ g} (h : Ï†â‚ = Ï†â‚‚) : Ï†â‚.left = Ï†â‚‚.left := by
  rw [h]
  -- ğŸ‰ no goals
#align category_theory.arrow.hom.congr_left CategoryTheory.Arrow.hom.congr_left

@[simp]
theorem hom.congr_right {f g : Arrow T} {Ï†â‚ Ï†â‚‚ : f âŸ¶ g} (h : Ï†â‚ = Ï†â‚‚) : Ï†â‚.right = Ï†â‚‚.right := by
  rw [h]
  -- ğŸ‰ no goals
#align category_theory.arrow.hom.congr_right CategoryTheory.Arrow.hom.congr_right

theorem iso_w {f g : Arrow T} (e : f â‰… g) : g.hom = e.inv.left â‰« f.hom â‰« e.hom.right := by
  have eq := Arrow.hom.congr_right e.inv_hom_id
  -- âŠ¢ g.hom = e.inv.left â‰« f.hom â‰« e.hom.right
  rw [Arrow.comp_right, Arrow.id_right] at eq
  -- âŠ¢ g.hom = e.inv.left â‰« f.hom â‰« e.hom.right
  erw [Arrow.w_assoc, eq, Category.comp_id]
  -- ğŸ‰ no goals
#align category_theory.arrow.iso_w CategoryTheory.Arrow.iso_w

theorem iso_w' {W X Y Z : T} {f : W âŸ¶ X} {g : Y âŸ¶ Z} (e : Arrow.mk f â‰… Arrow.mk g) :
    g = e.inv.left â‰« f â‰« e.hom.right :=
  iso_w e
#align category_theory.arrow.iso_w' CategoryTheory.Arrow.iso_w'

section

variable {f g : Arrow T} (sq : f âŸ¶ g)

instance isIso_left [IsIso sq] : IsIso sq.left where
  out := by
    apply Exists.intro (inv sq).left
    -- âŠ¢ sq.left â‰« (inv sq).left = ğŸ™ f.left âˆ§ (inv sq).left â‰« sq.left = ğŸ™ g.left
    simp only [â† Comma.comp_left, IsIso.hom_inv_id, IsIso.inv_hom_id, Arrow.id_left,
      eq_self_iff_true, and_self_iff]
    simp
    -- ğŸ‰ no goals
#align category_theory.arrow.is_iso_left CategoryTheory.Arrow.isIso_left

instance isIso_right [IsIso sq] : IsIso sq.right where
  out := by
    apply Exists.intro (inv sq).right
    -- âŠ¢ sq.right â‰« (inv sq).right = ğŸ™ f.right âˆ§ (inv sq).right â‰« sq.right = ğŸ™ g.right
    simp only [â† Comma.comp_right, IsIso.hom_inv_id, IsIso.inv_hom_id, Arrow.id_right,
      eq_self_iff_true, and_self_iff]
    simp
    -- ğŸ‰ no goals
#align category_theory.arrow.is_iso_right CategoryTheory.Arrow.isIso_right

@[simp]
theorem inv_left [IsIso sq] : (inv sq).left = inv sq.left :=
  IsIso.eq_inv_of_hom_inv_id <| by rw [â† Comma.comp_left, IsIso.hom_inv_id, id_left]
                                   -- ğŸ‰ no goals
#align category_theory.arrow.inv_left CategoryTheory.Arrow.inv_left

@[simp]
theorem inv_right [IsIso sq] : (inv sq).right = inv sq.right :=
  IsIso.eq_inv_of_hom_inv_id <| by rw [â† Comma.comp_right, IsIso.hom_inv_id, id_right]
                                   -- ğŸ‰ no goals
#align category_theory.arrow.inv_right CategoryTheory.Arrow.inv_right

/- Porting note : simp can prove this so removed @[simp] -/
theorem left_hom_inv_right [IsIso sq] : sq.left â‰« g.hom â‰« inv sq.right = f.hom := by
  simp only [â† Category.assoc, IsIso.comp_inv_eq, w]
  -- ğŸ‰ no goals
#align category_theory.arrow.left_hom_inv_right CategoryTheory.Arrow.left_hom_inv_right

-- simp proves this
theorem inv_left_hom_right [IsIso sq] : inv sq.left â‰« f.hom â‰« sq.right = g.hom := by
  simp only [w, IsIso.inv_comp_eq]
  -- ğŸ‰ no goals
#align category_theory.arrow.inv_left_hom_right CategoryTheory.Arrow.inv_left_hom_right

instance mono_left [Mono sq] : Mono sq.left where
  right_cancellation {Z} Ï† Ïˆ h := by
    let aux : (Z âŸ¶ f.left) â†’ (Arrow.mk (ğŸ™ Z) âŸ¶ f) := fun Ï† =>
      { left := Ï†
        right := Ï† â‰« f.hom }
    have : âˆ€ g, (aux g).right = g â‰« f.hom := fun g => by dsimp
    -- âŠ¢ Ï† = Ïˆ
    show (aux Ï†).left = (aux Ïˆ).left
    -- âŠ¢ (aux Ï†).left = (aux Ïˆ).left
    congr 1
    -- âŠ¢ aux Ï† = aux Ïˆ
    rw [â† cancel_mono sq]
    -- âŠ¢ aux Ï† â‰« sq = aux Ïˆ â‰« sq
    apply CommaMorphism.ext
    -- âŠ¢ (aux Ï† â‰« sq).left = (aux Ïˆ â‰« sq).left
    Â· exact h
      -- ğŸ‰ no goals
    Â· rw [Comma.comp_right, Comma.comp_right, this, this, Category.assoc, Category.assoc]
      -- âŠ¢ Ï† â‰« f.hom â‰« sq.right = Ïˆ â‰« f.hom â‰« sq.right
      rw [â†Arrow.w]
      -- âŠ¢ Ï† â‰« sq.left â‰« g.hom = Ïˆ â‰« sq.left â‰« g.hom
      simp only [â† Category.assoc, h]
      -- ğŸ‰ no goals
#align category_theory.arrow.mono_left CategoryTheory.Arrow.mono_left

instance epi_right [Epi sq] : Epi sq.right where
  left_cancellation {Z} Ï† Ïˆ h := by
    let aux : (g.right âŸ¶ Z) â†’ (g âŸ¶ Arrow.mk (ğŸ™ Z)) := fun Ï† =>
      { right := Ï†
        left := g.hom â‰« Ï† }
    show (aux Ï†).right = (aux Ïˆ).right
    -- âŠ¢ (aux Ï†).right = (aux Ïˆ).right
    congr 1
    -- âŠ¢ aux Ï† = aux Ïˆ
    rw [â† cancel_epi sq]
    -- âŠ¢ sq â‰« aux Ï† = sq â‰« aux Ïˆ
    apply CommaMorphism.ext
    -- âŠ¢ (sq â‰« aux Ï†).left = (sq â‰« aux Ïˆ).left
    Â· rw [Comma.comp_left, Comma.comp_left, Arrow.w_assoc, Arrow.w_assoc, h]
      -- ğŸ‰ no goals
    Â· exact h
      -- ğŸ‰ no goals
#align category_theory.arrow.epi_right CategoryTheory.Arrow.epi_right

end

/-- Given a square from an arrow `i` to an isomorphism `p`, express the source part of `sq`
in terms of the inverse of `p`. -/
@[simp]
theorem square_to_iso_invert (i : Arrow T) {X Y : T} (p : X â‰… Y) (sq : i âŸ¶ Arrow.mk p.hom) :
    i.hom â‰« sq.right â‰« p.inv = sq.left := by
  simpa only [Category.assoc] using (Iso.comp_inv_eq p).mpr (Arrow.w_mk_right sq).symm
  -- ğŸ‰ no goals
#align category_theory.arrow.square_to_iso_invert CategoryTheory.Arrow.square_to_iso_invert

/-- Given a square from an isomorphism `i` to an arrow `p`, express the target part of `sq`
in terms of the inverse of `i`. -/
theorem square_from_iso_invert {X Y : T} (i : X â‰… Y) (p : Arrow T) (sq : Arrow.mk i.hom âŸ¶ p) :
    i.inv â‰« sq.left â‰« p.hom = sq.right := by simp only [Iso.inv_hom_id_assoc, Arrow.w, Arrow.mk_hom]
                                             -- ğŸ‰ no goals
#align category_theory.arrow.square_from_iso_invert CategoryTheory.Arrow.square_from_iso_invert

variable {C : Type u} [Category.{v} C]

/-- A helper construction: given a square between `i` and `f â‰« g`, produce a square between
`i` and `g`, whose top leg uses `f`:
A  â†’ X
     â†“f
â†“i   Y             --> A â†’ Y
     â†“g                â†“i  â†“g
B  â†’ Z                 B â†’ Z
 -/
@[simps]
def squareToSnd {X Y Z : C} {i : Arrow C} {f : X âŸ¶ Y} {g : Y âŸ¶ Z} (sq : i âŸ¶ Arrow.mk (f â‰« g)) :
    i âŸ¶ Arrow.mk g where
  left := sq.left â‰« f
  right := sq.right
#align category_theory.arrow.square_to_snd CategoryTheory.Arrow.squareToSnd

/-- The functor sending an arrow to its source. -/
@[simps!]
def leftFunc : Arrow C â¥¤ C :=
  Comma.fst _ _
#align category_theory.arrow.left_func CategoryTheory.Arrow.leftFunc

/-- The functor sending an arrow to its target. -/
@[simps!]
def rightFunc : Arrow C â¥¤ C :=
  Comma.snd _ _
#align category_theory.arrow.right_func CategoryTheory.Arrow.rightFunc

/-- The natural transformation from `leftFunc` to `rightFunc`, given by the arrow itself. -/
@[simps]
def leftToRight : (leftFunc : Arrow C â¥¤ C) âŸ¶ rightFunc where app f := f.hom
#align category_theory.arrow.left_to_right CategoryTheory.Arrow.leftToRight

end Arrow

namespace Functor

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D]

/-- A functor `C â¥¤ D` induces a functor between the corresponding arrow categories. -/
@[simps]
def mapArrow (F : C â¥¤ D) : Arrow C â¥¤ Arrow D where
  obj a :=
    { left := F.obj a.left
      right := F.obj a.right
      hom := F.map a.hom }
  map f :=
    { left := F.map f.left
      right := F.map f.right
      w := by
        let w := f.w
        -- âŠ¢ (ğŸ­ D).map (F.map f.left) â‰« ((fun a => { left := F.obj a.left, right := F.obj â€¦
        simp only [id_map] at w
        -- âŠ¢ (ğŸ­ D).map (F.map f.left) â‰« ((fun a => { left := F.obj a.left, right := F.obj â€¦
        dsimp
        -- âŠ¢ F.map f.left â‰« F.map Yâœ.hom = F.map Xâœ.hom â‰« F.map f.right
        simp only [â† F.map_comp, w] }
        -- ğŸ‰ no goals
#align category_theory.functor.map_arrow CategoryTheory.Functor.mapArrow

end Functor

/-- The images of `f : Arrow C` by two isomorphic functors `F : C â¥¤ D` are
isomorphic arrows in `D`. -/
def Arrow.isoOfNatIso {C D : Type*} [Category C] [Category D] {F G : C â¥¤ D} (e : F â‰… G)
    (f : Arrow C) : F.mapArrow.obj f â‰… G.mapArrow.obj f :=
  Arrow.isoMk (e.app f.left) (e.app f.right)
#align category_theory.arrow.iso_of_nat_iso CategoryTheory.Arrow.isoOfNatIso

end CategoryTheory
