/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.Products.Basic

/-!
# Joins of categories

Given categories `C, D`, this file constructs a category `C â‹† D`. Its objects are either
objects of `C` or objects of `D`, morphisms between objects of `C` are morphisms in `C`,
morphisms between objects of `D` are morphisms in `D`, and finally, given `c : C` and `d : D`,
there is a unique morphism `c âŸ¶ d` in `C â‹† D`.

## Main constructions

* `Join.edge c d`: the unique map from `c` to `d`.
* `Join.inclLeft : C â¥¤ C â‹† D`, the left inclusion. Its action on morphisms is the main entry point
  to construct maps in `C â‹† D` between objects coming from `C`.
* `Join.inclRight : D â¥¤ C â‹† D`, the right inclusion. Its action on morphisms is the main entry point
  to construct maps in `C â‹† D` between objects coming from `D`.
* `Join.mkFunctor`, A constructor for functors out of a join of categories.
* `Join.mkNatTrans`, A constructor for natural transformations between functors out of a join
  of categories.
* `Join.mkNatIso`, A constructor for natural isomorphisms between functors out of a join
  of categories.

## References

* [Kerodon: section 1.4.3.2](https://kerodon.net/tag/0160)

-/

universe vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ vâ‚… vâ‚† uâ‚ uâ‚‚ uâ‚ƒ uâ‚„ uâ‚… uâ‚†

namespace CategoryTheory

open Functor

/-- Elements of `Join C D` are either elements of `C` or elements of `D`. -/
-- Impl. : We are not defining it as a type alias for `C âŠ• D` so that we can have
-- aesop to call cases on `Join C D`
inductive Join (C : Type uâ‚) [Category.{vâ‚} C] (D : Type uâ‚‚) [Category.{vâ‚‚} D] : Type (max uâ‚ uâ‚‚)
  | left : C â†’ Join C D
  | right : D â†’ Join C D

attribute [aesop safe cases (rule_sets := [CategoryTheory])] Join

namespace Join

@[inherit_doc] scoped infixr:30 " â‹† " => Join

variable (C : Type uâ‚) [Category.{vâ‚} C] (D : Type uâ‚‚) [Category.{vâ‚‚} D]

section CategoryStructure

variable {C D}

/-- Morphisms in `C â‹† D` are those of `C` and `D`, plus a unique
morphism `(left c âŸ¶ right d)` for every `c : C` and `d : D`. -/
def Hom : C â‹† D â†’ C â‹† D â†’ Type (max vâ‚ vâ‚‚)
  | .left x, .left y => ULift (x âŸ¶ y)
  | .right x, .right y => ULift (x âŸ¶ y)
  | .left _, .right _ => PUnit
  | .right _, .left _ => PEmpty

/-- Identity morphisms in `C â‹† D` are inherited from those in `C` and `D`. -/
def id : âˆ€ X : C â‹† D, Hom X X
  | .left x => ULift.up (ğŸ™ x)
  | .right x => ULift.up (ğŸ™ x)

/-- Composition in `C â‹† D` is inherited from the compositions in `C` and `D`. -/
def comp : âˆ€ {x y z : C â‹† D}, Hom x y â†’ Hom y z â†’ Hom x z
  | .left _x, .left _y, .left _z, f, g => ULift.up (ULift.down f â‰« ULift.down g)
  | .left _x, .left _y, .right _z, _, _ => PUnit.unit
  | .left _x, .right _y, .right _z, _, _ =>  PUnit.unit
  | .right _x, .right _y, .right _z, f, g => ULift.up (ULift.down f â‰« ULift.down g)

instance : Category.{max vâ‚ vâ‚‚} (C â‹† D) where
  Hom X Y := Hom X Y
  id _ := id _
  comp := comp
  assoc {a b c d} f g h := by
    cases a <;>
    cases b <;>
    cases c <;>
    cases d <;>
    simp only [Hom, comp, Category.assoc] <;>
    tauto
  id_comp {x y} f := by
    cases x <;> cases y <;> simp only [Hom, id, comp, Category.id_comp] <;> tauto
  comp_id {x y} f := by
    cases x <;> cases y <;> simp only [Hom, id, comp, Category.comp_id] <;> tauto

@[aesop safe destruct (rule_sets := [CategoryTheory])]
lemma false_of_right_to_left {X : D} {Y : C} (f : right X âŸ¶ left Y) : False := (f : PEmpty).elim

instance {X : C} {Y : D} : Unique (left X âŸ¶ right Y) := inferInstanceAs (Unique PUnit)

/-- Join.edge c d is the unique morphism from c to d. -/
def edge (c : C) (d : D) : left c âŸ¶ right d := default

end CategoryStructure

section Inclusions

/-- The canonical inclusion from C to `C â‹† D`.
Terms of the form `(inclLeft C D).map f` should be treated as primitive when working with joins
and one should avoid trying to reduce them. For this reason, there is no `inclLeft_map` simp
lemma. -/
@[simps! obj]
def inclLeft : C â¥¤ C â‹† D where
  obj := left
  map := ULift.up

/-- The canonical inclusion from D to `C â‹† D`.
Terms of the form `(inclRight C D).map f` should be treated as primitive when working with joins
and one should avoid trying to reduce them. For this reason, there is no `inclRight_map` simp
lemma. -/
@[simps! obj]
def inclRight : D â¥¤ C â‹† D where
  obj := right
  map := ULift.up

variable {C D}

/-- An induction principle for morphisms in a join of categories: a morphism is either of the form
`(inclLeft _ _).map _`, `(inclRight _ _).map _`, or is `edge _ _`. -/
@[elab_as_elim, cases_eliminator, induction_eliminator]
def homInduction {P : {x y : C â‹† D} â†’ (x âŸ¶ y) â†’ Sort*}
    (left : âˆ€ x y : C, (f : x âŸ¶ y) â†’ P ((inclLeft C D).map f))
    (right : âˆ€ x y : D, (f : x âŸ¶ y) â†’ P ((inclRight C D).map f))
    (edge : âˆ€ (c : C) (d : D), P (edge c d))
    {x y : C â‹† D} (f : x âŸ¶ y) : P f :=
  match x, y, f with
  | .left x, .left y, .up f => left x y f
  | .right x, .right y, .up f => right x y f
  | .left x, .right y, _ => edge x y

@[simp]
lemma homInduction_left {P : {x y : C â‹† D} â†’ (x âŸ¶ y) â†’ Sort*}
    (left : âˆ€ x y : C, (f : x âŸ¶ y) â†’ P ((inclLeft C D).map f))
    (right : âˆ€ x y : D, (f : x âŸ¶ y) â†’ P ((inclRight C D).map f))
    (edge : âˆ€ (c : C) (d : D), P (edge c d))
    {x y : C} (f : x âŸ¶ y) : homInduction left right edge ((inclLeft C D).map f) = left x y f :=
  rfl

@[simp]
lemma homInduction_right {P : {x y : C â‹† D} â†’ (x âŸ¶ y) â†’ Sort*}
    (left : âˆ€ x y : C, (f : x âŸ¶ y) â†’ P ((inclLeft C D).map f))
    (right : âˆ€ x y : D, (f : x âŸ¶ y) â†’ P ((inclRight C D).map f))
    (edge : âˆ€ (c : C) (d : D), P (edge c d))
    {x y : D} (f : x âŸ¶ y) : homInduction left right edge ((inclRight C D).map f) = right x y f :=
  rfl

@[simp]
lemma homInduction_edge {P : {x y : C â‹† D} â†’ (x âŸ¶ y) â†’ Sort*}
    (left : âˆ€ x y : C, (f : x âŸ¶ y) â†’ P ((inclLeft C D).map f))
    (right : âˆ€ x y : D, (f : x âŸ¶ y) â†’ P ((inclRight C D).map f))
    (edge : âˆ€ (c : C) (d : D), P (edge c d))
    {c : C} {d : D} : homInduction left right edge (Join.edge c d) = edge c d :=
  rfl

variable (C D)

/-- The left inclusion is fully faithful. -/
def inclLeftFullyFaithful : (inclLeft C D).FullyFaithful where
  preimage f := f.down

/-- The right inclusion is fully faithful. -/
def inclRightFullyFaithful : (inclRight C D).FullyFaithful where
  preimage f := f.down

instance inclLeftFull : (inclLeft C D).Full := inclLeftFullyFaithful C D |>.full

instance inclRightFull : (inclRight C D).Full := inclRightFullyFaithful C D |>.full

instance inclLeftFaithful : (inclLeft C D).Faithful := inclLeftFullyFaithful C D |>.faithful

instance inclRightFaithful : (inclRight C D).Faithful := inclRightFullyFaithful C D |>.faithful

variable {C} in
/-- A situational lemma to help putting identities in the form `(inclLeft _ _).map _` when using
`homInduction`. -/
lemma id_left (c : C) : ğŸ™ (left c) = (inclLeft C D).map (ğŸ™ c) := rfl

variable {D} in
/-- A situational lemma to help putting identities in the form `(inclRight _ _).map _` when using
`homInduction`. -/
lemma id_right (d : D) : ğŸ™ (right d) = (inclRight C D).map (ğŸ™ d) := rfl

/-- The "canonical" natural transformation from `(Prod.fst C D) â‹™ inclLeft C D` to
`(Prod.snd C D) â‹™ inclRight C D`. This is bundling together all the edge morphisms
into the data of a natural transformation. -/
@[simps!]
def edgeTransform :
    Prod.fst C D â‹™ inclLeft C D âŸ¶ Prod.snd C D â‹™ inclRight C D where
  app := fun (c, d) â†¦ edge c d

end Inclusions

section Functoriality

variable {C D} {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E] {E' : Type uâ‚„} [Category.{vâ‚„} E']

/-- A pair of functors `F : C â¥¤ E, G : D â¥¤ E` as well as a natural transformation
`Î± : (Prod.fst C D) â‹™ F âŸ¶ (Prod.snd C D) â‹™ G` defines a functor out of `C â‹† D`.
This is the main entry point to define functors out of a join of categories. -/
def mkFunctor (F : C â¥¤ E) (G : D â¥¤ E) (Î± : Prod.fst C D â‹™ F âŸ¶ Prod.snd C D â‹™ G) :
    C â‹† D â¥¤ E where
  obj X :=
    match X with
    | .left x => F.obj x
    | .right x => G.obj x
  map f :=
    homInduction
      (left := fun _ _ f â†¦ F.map f)
      (right := fun _ _ g â†¦ G.map g)
      (edge := fun c d â†¦ Î±.app (c,d))
      f
  map_id x := by
    cases x
    Â· dsimp only [id_left, homInduction_left]
      simp
    Â· dsimp only [id_right, homInduction_right]
      simp
  map_comp {x y z} f g := by
    cases f <;> cases g
    Â· simp [â† Functor.map_comp]
    Â· case left.edge f d => simpa using (Î±.naturality <| (Prod.sectL _ d).map f).symm
    Â· simp [â† Functor.map_comp]
    Â· case edge.right c _ _ f => simpa using Î±.naturality <| (Prod.sectR c _).map f

section

variable (F : C â¥¤ E) (G : D â¥¤ E) (Î± : Prod.fst C D â‹™ F âŸ¶ Prod.snd C D â‹™ G)

-- As these equalities of objects are definitional, they should be fine.
@[simp]
lemma mkFunctor_obj_left (c : C) : (mkFunctor F G Î±).obj (left c) = F.obj c := rfl

@[simp]
lemma mkFunctor_obj_right (d : D) : (mkFunctor F G Î±).obj (right d) = G.obj d := rfl

@[simp]
lemma mkFunctor_map_inclLeft {c c' : C} (f : c âŸ¶ c') :
    (mkFunctor F G Î±).map ((inclLeft C D).map f) = F.map f :=
  rfl

/-- Precomposing `mkFunctor F G Î±` with the left inclusion gives back `F`. -/
@[simps!]
def mkFunctorLeft : inclLeft C D â‹™ mkFunctor F G Î± â‰… F := Iso.refl _

/-- Precomposing `mkFunctor F G Î±` with the right inclusion gives back `G`. -/
@[simps!]
def mkFunctorRight : inclRight C D â‹™ mkFunctor F G Î± â‰… G := Iso.refl _

@[simp]
lemma mkFunctor_map_inclRight {d d' : D} (f : d âŸ¶ d') :
    (mkFunctor F G Î±).map ((inclRight C D).map f) = G.map f :=
  rfl

/-- Whiskering `mkFunctor F G Î±` with the universal transformation gives back `Î±`. -/
@[simp]
lemma mkFunctor_edgeTransform :
    whiskerRight (edgeTransform C D) (mkFunctor F G Î±) = Î± := by
  ext x
  simp [mkFunctor]

@[simp]
lemma mkFunctor_map_edge (c : C) (d : D) :
    (mkFunctor F G Î±).map (edge c d) = Î±.app (c, d) :=
  rfl

end

/-- Construct a natural transformation between functors out of a join from
the data of natural transformations between each side that are compatible with the
action on edge maps. -/
def mkNatTrans {F : C â‹† D â¥¤ E} {F' : C â‹† D â¥¤ E}
    (Î±â‚— : inclLeft C D â‹™ F âŸ¶ inclLeft C D â‹™ F') (Î±áµ£ : inclRight C D â‹™ F âŸ¶ inclRight C D â‹™ F')
    (h : whiskerRight (edgeTransform C D) F â‰« whiskerLeft (Prod.snd C D) Î±áµ£ =
      whiskerLeft (Prod.fst C D) Î±â‚— â‰« whiskerRight (edgeTransform C D) F' :=
      by cat_disch) :
    F âŸ¶ F' where
  app x := match x with
    | left x => Î±â‚—.app x
    | right x => Î±áµ£.app x
  naturality {x y} f := by
    cases f with
    | @left x y f => simpa using Î±â‚—.naturality f
    | @right x y f => simpa using Î±áµ£.naturality f
    | @edge c d => exact funext_iff.mp (NatTrans.ext_iff.mp h) (c, d)

section

variable {F : C â‹† D â¥¤ E} {F' : C â‹† D â¥¤ E}
    (Î±â‚— : inclLeft C D â‹™ F âŸ¶ inclLeft C D â‹™ F') (Î±áµ£ : inclRight C D â‹™ F âŸ¶ inclRight C D â‹™ F')
    (h : whiskerRight (edgeTransform C D) F â‰« whiskerLeft (Prod.snd C D) Î±áµ£ =
      whiskerLeft (Prod.fst C D) Î±â‚— â‰« whiskerRight (edgeTransform C D) F' :=
      by cat_disch)

@[simp]
lemma mkNatTrans_app_left (c : C) : (mkNatTrans Î±â‚— Î±áµ£ h).app (left c) = Î±â‚—.app c := rfl

@[simp]
lemma mkNatTrans_app_right (d : D) : (mkNatTrans Î±â‚— Î±áµ£ h).app (right d) = Î±áµ£.app d := rfl

@[simp]
lemma whiskerLeft_inclLeft_mkNatTrans : whiskerLeft (inclLeft C D) (mkNatTrans Î±â‚— Î±áµ£ h) = Î±â‚— := rfl

@[simp]
lemma whiskerLeft_inclRight_mkNatTrans :
    whiskerLeft (inclRight C D) (mkNatTrans Î±â‚— Î±áµ£ h) = Î±áµ£ := rfl

end

/-- Two natural transformations between functors out of a join are equal if they are so
after whiskering with the inclusions. -/
lemma natTrans_ext {F F' : C â‹† D â¥¤ E} {Î± Î² : F âŸ¶ F'}
    (hâ‚ : whiskerLeft (inclLeft C D) Î± = whiskerLeft (inclLeft C D) Î²)
    (hâ‚‚ : whiskerLeft (inclRight C D) Î± = whiskerLeft (inclRight C D) Î²) :
    Î± = Î² := by
  ext t
  cases t with
  | left t => exact congrArg (fun x â†¦ x.app t) hâ‚
  | right t => exact congrArg (fun x â†¦ x.app t) hâ‚‚

lemma eq_mkNatTrans {F F' : C â‹† D â¥¤ E} (Î± : F âŸ¶ F') :
    mkNatTrans (whiskerLeft (inclLeft C D) Î±) (whiskerLeft (inclRight C D) Î±) = Î± := by
  apply natTrans_ext <;> simp

section

/-- `mkNatTrans` respects vertical composition. -/
lemma mkNatTransComp
    {F F' F'' : C â‹† D â¥¤ E}
    (Î±â‚— : inclLeft C D â‹™ F âŸ¶ inclLeft C D â‹™ F')
    (Î±áµ£ : inclRight C D â‹™ F âŸ¶ inclRight C D â‹™ F')
    (Î²â‚— : inclLeft C D â‹™ F' âŸ¶ inclLeft C D â‹™ F'')
    (Î²áµ£ : inclRight C D â‹™ F' âŸ¶ inclRight C D â‹™ F'')
    (h : whiskerRight (edgeTransform C D) F â‰« whiskerLeft (Prod.snd C D) Î±áµ£ =
      whiskerLeft (Prod.fst C D) Î±â‚— â‰« whiskerRight (edgeTransform C D) F' :=
      by cat_disch)
    (h' : whiskerRight (edgeTransform C D) F' â‰« whiskerLeft (Prod.snd C D) Î²áµ£ =
      whiskerLeft (Prod.fst C D) Î²â‚— â‰« whiskerRight (edgeTransform C D) F'' := by cat_disch) :
    mkNatTrans (Î±â‚— â‰« Î²â‚—) (Î±áµ£ â‰« Î²áµ£) (by simp [â† h', reassoc_of% h]) =
    mkNatTrans Î±â‚— Î±áµ£ h â‰« mkNatTrans Î²â‚— Î²áµ£ h' := by
  apply natTrans_ext <;> cat_disch

end

/-- Two functors out of a join of categories are naturally isomorphic if their
compositions with the inclusions are isomorphic and the whiskering with the canonical
transformation is respected through these isomorphisms. -/
@[simps]
def mkNatIso {F : C â‹† D â¥¤ E} {G : C â‹† D â¥¤ E}
    (eâ‚— : inclLeft C D â‹™ F â‰… inclLeft C D â‹™ G)
    (eáµ£ : inclRight C D â‹™ F â‰… inclRight C D â‹™ G)
    (h : whiskerRight (edgeTransform C D) F â‰« (isoWhiskerLeft (Prod.snd C D) eáµ£).hom =
      (isoWhiskerLeft (Prod.fst C D) eâ‚—).hom â‰« whiskerRight (edgeTransform C D) G := by cat_disch) :
    F â‰… G where
  hom := mkNatTrans eâ‚—.hom eáµ£.hom (by simpa using h)
  inv := mkNatTrans eâ‚—.inv eáµ£.inv (by rw [Eq.comm, â† isoWhiskerLeft_inv, â† isoWhiskerLeft_inv,
    Iso.inv_comp_eq, â† Category.assoc, Eq.comm, Iso.comp_inv_eq, h])

/-- A pair of functors ((C â¥¤ E), (D â¥¤ E')) induces a functor `C â‹† D â¥¤ E â‹† E'`. -/
def mapPair (Fâ‚— : C â¥¤ E) (Fáµ£ : D â¥¤ E') : C â‹† D â¥¤ E â‹† E' :=
  mkFunctor (Fâ‚— â‹™ inclLeft _ _) (Fáµ£ â‹™ inclRight _ _) { app := fun _ â†¦ edge _ _ }

section mapPair

variable (Fâ‚— : C â¥¤ E) (Fáµ£ : D â¥¤ E')

@[simp]
lemma mapPair_obj_left (c : C) : (mapPair Fâ‚— Fáµ£).obj (left c) = left (Fâ‚—.obj c) := rfl

@[simp]
lemma mapPair_obj_right (d : D) : (mapPair Fâ‚— Fáµ£).obj (right d) = right (Fáµ£.obj d) := rfl

@[simp]
lemma mapPair_map_inclLeft {c c' : C} (f : c âŸ¶ c') :
    (mapPair Fâ‚— Fáµ£).map ((inclLeft C D).map f) = (inclLeft E E').map (Fâ‚—.map f) := rfl

@[simp]
lemma mapPair_map_inclRight {d d' : D} (f : d âŸ¶ d') :
    (mapPair Fâ‚— Fáµ£).map ((inclRight C D).map f) = (inclRight E E').map (Fáµ£.map f) := rfl

/-- Characterizing `mapPair` on left morphisms. -/
@[simps! hom_app inv_app]
def mapPairLeft : inclLeft _ _ â‹™ mapPair Fâ‚— Fáµ£ â‰… Fâ‚— â‹™ inclLeft _ _ := mkFunctorLeft _ _ _

/-- Characterizing `mapPair` on right morphisms. -/
@[simps! hom_app inv_app]
def mapPairRight : inclRight _ _ â‹™ mapPair Fâ‚— Fáµ£ â‰… Fáµ£ â‹™ inclRight _ _ := mkFunctorRight _ _ _

end mapPair

/-- Any functor out of a join is naturally isomorphic to a functor of the form `mkFunctor F G Î±`. -/
@[simps!]
def isoMkFunctor (F : C â‹† D â¥¤ E) :
    F â‰… mkFunctor (inclLeft C D â‹™ F) (inclRight C D â‹™ F) (whiskerRight (edgeTransform C D) F) :=
  mkNatIso (mkFunctorLeft _ _ _).symm (mkFunctorRight _ _ _).symm

/-- `mapPair` respects identities -/
@[simps!]
def mapPairId : mapPair (ğŸ­ C) (ğŸ­ D) â‰… ğŸ­ (C â‹† D) :=
  mkNatIso
    (mapPairLeft _ _ â‰ªâ‰« Functor.leftUnitor _ â‰ªâ‰« (Functor.rightUnitor _).symm)
    (mapPairRight _ _ â‰ªâ‰« Functor.leftUnitor _ â‰ªâ‰« (Functor.rightUnitor _).symm)

variable {J : Type uâ‚…} [Category.{vâ‚…} J]
  {K : Type uâ‚†} [Category.{vâ‚†} K]

-- @[simps!] times out here
/-- `mapPair` respects composition -/
def mapPairComp (Fâ‚— : C â¥¤ E) (Fáµ£ : D â¥¤ E') (Gâ‚— : E â¥¤ J) (Gáµ£ : E' â¥¤ K) :
    mapPair (Fâ‚— â‹™ Gâ‚—) (Fáµ£ â‹™ Gáµ£) â‰… mapPair Fâ‚— Fáµ£ â‹™ mapPair Gâ‚— Gáµ£ :=
  mkNatIso
    (mapPairLeft (Fâ‚— â‹™ Gâ‚—) (Fáµ£ â‹™ Gáµ£) â‰ªâ‰«
      Functor.associator Fâ‚— Gâ‚— (inclLeft J K) â‰ªâ‰«
      (isoWhiskerLeft Fâ‚— (mapPairLeft Gâ‚— Gáµ£).symm) â‰ªâ‰«
      (Functor.associator Fâ‚— (inclLeft E E') (mapPair Gâ‚— Gáµ£)).symm â‰ªâ‰«
      isoWhiskerRight (mapPairLeft Fâ‚— Fáµ£).symm (mapPair Gâ‚— Gáµ£))
    (mapPairRight (Fâ‚— â‹™ Gâ‚—) (Fáµ£ â‹™ Gáµ£) â‰ªâ‰«
      Functor.associator Fáµ£ Gáµ£ (inclRight J K) â‰ªâ‰«
      (isoWhiskerLeft Fáµ£ (mapPairRight Gâ‚— Gáµ£).symm) â‰ªâ‰«
      (Functor.associator Fáµ£ (inclRight E E') (mapPair Gâ‚— Gáµ£)).symm â‰ªâ‰«
      isoWhiskerRight (mapPairRight Fâ‚— Fáµ£).symm (mapPair Gâ‚— Gáµ£))

section mapPairComp

variable (Fâ‚— : C â¥¤ E) (Fáµ£ : D â¥¤ E') (Gâ‚— : E â¥¤ J) (Gáµ£ : E' â¥¤ K)

@[simp]
lemma mapPairComp_hom_app_left (c : C) :
    (mapPairComp Fâ‚— Fáµ£ Gâ‚— Gáµ£).hom.app (left c) = ğŸ™ (left (Gâ‚—.obj (Fâ‚—.obj c))) := by
  dsimp [mapPairComp]
  simp

@[simp]
lemma mapPairComp_hom_app_right (d : D) :
    (mapPairComp Fâ‚— Fáµ£ Gâ‚— Gáµ£).hom.app (right d) = ğŸ™ (right (Gáµ£.obj (Fáµ£.obj d))) := by
  dsimp [mapPairComp]
  simp

@[simp]
lemma mapPairComp_inv_app_left (c : C) :
    (mapPairComp Fâ‚— Fáµ£ Gâ‚— Gáµ£).inv.app (left c) = ğŸ™ (left (Gâ‚—.obj (Fâ‚—.obj c))) := by
  dsimp [mapPairComp]
  simp

@[simp]
lemma mapPairComp_inv_app_right (d : D) :
    (mapPairComp Fâ‚— Fáµ£ Gâ‚— Gáµ£).inv.app (right d) = ğŸ™ (right (Gáµ£.obj (Fáµ£.obj d))) := by
  dsimp [mapPairComp]
  simp

end mapPairComp

end Functoriality

section NaturalTransforms

variable {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E]
  {E' : Type uâ‚„} [Category.{vâ‚„} E']

variable {C D}

/-- A natural transformation `Fâ‚— âŸ¶ Gâ‚—` induces a natural transformation
  `mapPair Fâ‚— H âŸ¶ mapPair Gâ‚— H` for every `H : D â¥¤ E'`. -/
@[simps!]
def mapWhiskerRight {Fâ‚— : C â¥¤ E} {Gâ‚— : C â¥¤ E} (Î± : Fâ‚— âŸ¶ Gâ‚—) (H : D â¥¤ E') :
    mapPair Fâ‚— H âŸ¶ mapPair Gâ‚— H :=
  mkNatTrans
    ((mapPairLeft Fâ‚— H).hom â‰« whiskerRight Î± (inclLeft E E') â‰« (mapPairLeft Gâ‚— H).inv)
    ((mapPairRight Fâ‚— H).hom â‰« whiskerRight (ğŸ™ H) (inclRight E E') â‰« (mapPairRight Gâ‚— H).inv)

@[simp]
lemma mapWhiskerRight_comp {Fâ‚— : C â¥¤ E} {Gâ‚— : C â¥¤ E} {Hâ‚— : C â¥¤ E}
    (Î± : Fâ‚— âŸ¶ Gâ‚—) (Î² : Gâ‚— âŸ¶ Hâ‚—) (H : D â¥¤ E') :
    mapWhiskerRight (Î± â‰« Î²) H = mapWhiskerRight Î± H â‰« mapWhiskerRight Î² H := by
  cat_disch

@[simp]
lemma mapWhiskerRight_id (Fâ‚— : C â¥¤ E) (H : D â¥¤ E') :
    mapWhiskerRight (ğŸ™ Fâ‚—) H = ğŸ™ _ := by
  cat_disch

/-- A natural transformation `Fáµ£ âŸ¶ Gáµ£` induces a natural transformation
  `mapPair H Fáµ£ âŸ¶ mapPair H Gáµ£` for every `H : C â¥¤ E`. -/
@[simps!]
def mapWhiskerLeft (H : C â¥¤ E) {Fáµ£ : D â¥¤ E'} {Gáµ£ : D â¥¤ E'} (Î± : Fáµ£ âŸ¶ Gáµ£) :
    mapPair H Fáµ£ âŸ¶ mapPair H Gáµ£ :=
  mkNatTrans
    ((mapPairLeft H Fáµ£).hom â‰« whiskerRight (ğŸ™ H) (inclLeft E E') â‰« (mapPairLeft H Gáµ£).inv)
    ((mapPairRight H Fáµ£).hom â‰« whiskerRight Î± (inclRight E E') â‰« (mapPairRight H Gáµ£).inv)

@[simp]
lemma mapWhiskerLeft_comp {Fáµ£ : D â¥¤ E'} {Gáµ£ : D â¥¤ E'} {Háµ£ : D â¥¤ E'}
    (H : C â¥¤ E) (Î± : Fáµ£ âŸ¶ Gáµ£) (Î² : Gáµ£ âŸ¶ Háµ£) :
    mapWhiskerLeft H (Î± â‰« Î²) = mapWhiskerLeft H Î± â‰« mapWhiskerLeft H Î² := by
  cat_disch

@[simp]
lemma mapWhiskerLeft_id (H : C â¥¤ E) (Fáµ£ : D â¥¤ E') :
    mapWhiskerLeft H (ğŸ™ Fáµ£) = ğŸ™ _ := by
  cat_disch

/-- One can exchange `mapWhiskerLeft` and `mapWhiskerRight`. -/
lemma mapWhisker_exchange (Fâ‚— : C â¥¤ E) (Gâ‚— : C â¥¤ E) (Fáµ£ : D â¥¤ E') (Gáµ£ : D â¥¤ E')
    (Î±â‚— : Fâ‚— âŸ¶ Gâ‚—) (Î±áµ£ : Fáµ£ âŸ¶ Gáµ£) :
    mapWhiskerLeft Fâ‚— Î±áµ£ â‰« mapWhiskerRight Î±â‚— Gáµ£ =
      mapWhiskerRight Î±â‚— Fáµ£ â‰« mapWhiskerLeft Gâ‚— Î±áµ£ := by
  ext
  cat_disch

/-- A natural isomorphism `Fáµ£ â‰… Gáµ£` induces a natural isomorphism
  `mapPair H Fáµ£ â‰… mapPair H Gáµ£` for every `H : C â¥¤ E`. -/
@[simps!]
def mapIsoWhiskerLeft (H : C â¥¤ E) {Fáµ£ : D â¥¤ E'} {Gáµ£ : D â¥¤ E'} (Î± : Fáµ£ â‰… Gáµ£) :
    mapPair H Fáµ£ â‰… mapPair H Gáµ£ :=
  mkNatIso
    (mapPairLeft H Fáµ£ â‰ªâ‰« isoWhiskerRight (Iso.refl H) (inclLeft _ _) â‰ªâ‰« (mapPairLeft H Gáµ£).symm)
    (mapPairRight H Fáµ£ â‰ªâ‰« isoWhiskerRight Î± (inclRight E E') â‰ªâ‰« (mapPairRight H Gáµ£).symm)

/-- A natural isomorphism `Fáµ£ â‰… Gáµ£` induces a natural isomorphism
  `mapPair Fâ‚— H â‰… mapPair Gâ‚— H` for every `H : C â¥¤ E`. -/
@[simps!]
def mapIsoWhiskerRight {Fâ‚— : C â¥¤ E} {Gâ‚— : C â¥¤ E} (Î± : Fâ‚— â‰… Gâ‚—) (H : D â¥¤ E') :
    mapPair Fâ‚— H â‰… mapPair Gâ‚— H :=
  mkNatIso
    (mapPairLeft Fâ‚— H â‰ªâ‰« isoWhiskerRight Î± (inclLeft E E') â‰ªâ‰« (mapPairLeft Gâ‚— H).symm)
    (mapPairRight Fâ‚— H â‰ªâ‰« isoWhiskerRight (Iso.refl H) (inclRight E E') â‰ªâ‰« (mapPairRight Gâ‚— H).symm)

lemma mapIsoWhiskerRight_hom {Fâ‚— : C â¥¤ E} {Gâ‚— : C â¥¤ E} (Î± : Fâ‚— â‰… Gâ‚—) (H : D â¥¤ E') :
    (mapIsoWhiskerRight Î± H).hom = mapWhiskerRight Î±.hom H := rfl

lemma mapIsoWhiskerRight_inv {Fâ‚— : C â¥¤ E} {Gâ‚— : C â¥¤ E} (Î± : Fâ‚— â‰… Gâ‚—) (H : D â¥¤ E') :
    (mapIsoWhiskerRight Î± H).inv = mapWhiskerRight Î±.inv H := by
  ext x
  cases x <;> simp [mapIsoWhiskerRight]

lemma mapIsoWhiskerLeft_hom (H : C â¥¤ E) {Fáµ£ : D â¥¤ E'} {Gáµ£ : D â¥¤ E'} (Î± : Fáµ£ â‰… Gáµ£) :
    (mapIsoWhiskerLeft H Î±).hom = mapWhiskerLeft H Î±.hom := rfl

lemma mapIsoWhiskerLeft_inv (H : C â¥¤ E) {Fáµ£ : D â¥¤ E'} {Gáµ£ : D â¥¤ E'} (Î± : Fáµ£ â‰… Gáµ£) :
    (mapIsoWhiskerLeft H Î±).inv = mapWhiskerLeft H Î±.inv := by
  ext x
  cases x <;> simp [mapIsoWhiskerLeft]

end NaturalTransforms

section mapPairEquiv

variable {C' : Type uâ‚ƒ} [Category.{vâ‚ƒ} C']
  {D' : Type uâ‚„} [Category.{vâ‚„} D']

variable {C D}

/-- Equivalent categories have equivalent joins. -/
@[simps]
def mapPairEquiv (e : C â‰Œ C') (e' : D â‰Œ D') : C â‹† D â‰Œ C' â‹† D' where
  functor := mapPair e.functor e'.functor
  inverse := mapPair e.inverse e'.inverse
  unitIso :=
    mapPairId.symm â‰ªâ‰«
      mapIsoWhiskerRight e.unitIso _ â‰ªâ‰«
      mapIsoWhiskerLeft _ e'.unitIso â‰ªâ‰«
      mapPairComp _ _ _ _
  counitIso :=
    (mapPairComp _ _ _ _).symm â‰ªâ‰«
      mapIsoWhiskerRight e.counitIso _ â‰ªâ‰«
      mapIsoWhiskerLeft _ e'.counitIso â‰ªâ‰«
      mapPairId
  functor_unitIso_comp x := by
    cases x <;>
    simp [â† (inclLeft C' D').map_comp, â† (inclRight C' D').map_comp]

instance isEquivalenceMapPair {F : C â¥¤ C'} {F' : D â¥¤ D'} [F.IsEquivalence] [F'.IsEquivalence] :
    (mapPair F F').IsEquivalence :=
  inferInstanceAs (mapPairEquiv F.asEquivalence F'.asEquivalence).functor.IsEquivalence

end mapPairEquiv

end Join

end CategoryTheory
