/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.Join.Basic
import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor

/-!
# Pseudofunctoriality of categorical joins

In this file, we promote the join construction to two pseudofunctors
`Join.pseudofunctorLeft` and `Join.pseudoFunctorRight`, expressing its pseudofunctoriality in
each variable.

-/

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

namespace CategoryTheory.Join

open Bicategory

/-- The `PrelaxFunctor` structure underlying `Join.pseudofunctorRight`. -/
@[simps]
def prelaxFunctorRight (C : Type uâ‚) [Category.{vâ‚} C] :
    PrelaxFunctor Cat.{vâ‚‚, uâ‚‚} Cat.{max vâ‚ vâ‚‚, max uâ‚ uâ‚‚} where
  obj D := Cat.of (C â‹† D)
  map F := mapPair (ğŸ­ C) F
  mapâ‚‚ := mapWhiskerLeft _
  mapâ‚‚_id {x y} f := by
    apply natTrans_ext <;> aesop_cat
  mapâ‚‚_comp Î· Î¸ := by
    apply natTrans_ext <;>
    ( dsimp
      rw [â† mapWhiskerLeft_comp] )

/-- The `PrelaxFunctor` structure underlying `Join.pseudofunctorLeft`. -/
@[simps]
def prelaxFunctorLeft (D : Type uâ‚‚) [Category.{vâ‚‚} D] :
    PrelaxFunctor Cat.{vâ‚, uâ‚} Cat.{max vâ‚ vâ‚‚, max uâ‚ uâ‚‚} where
  obj C := Cat.of (C â‹† D)
  map F := mapPair F (ğŸ­ D)
  mapâ‚‚ := (mapWhiskerRight Â· _)
  mapâ‚‚_id {x y} f := by
    apply natTrans_ext <;> aesop_cat
  mapâ‚‚_comp Î· Î¸ := by
    apply natTrans_ext <;>
    ( dsimp
      rw [â† mapWhiskerRight_comp] )

-- The proof gets too slow if we put it in a single `pseudofunctor` constructor,
-- so we break down the component proofs for the pseudofunctors over several lemmas.

section
variable {A B C D : Type*} [Category A] [Category B] [Category C] [Category D]


variable (A) in
/-- The structural isomorphism for composition of `pseudoFunctorRight`. -/
def mapCompRight (F : B â¥¤ C) (G : C â¥¤ D) :
    mapPair (ğŸ­ A) (F â‹™ G) â‰… mapPair (ğŸ­ A) F â‹™ mapPair (ğŸ­ A) G :=
  mapIsoWhiskerRight (Functor.leftUnitor _).symm _ â‰ªâ‰« mapPairComp (ğŸ­ A) F (ğŸ­ A) G

variable (D) in
/-- The structural isomorphism for composition of `pseudoFunctorLeft`. -/
def mapCompLeft (F : A â¥¤ B) (G : B â¥¤ C) :
    mapPair (F â‹™ G) (ğŸ­ D) â‰… mapPair F (ğŸ­ D) â‹™ mapPair G (ğŸ­ D) :=
  mapIsoWhiskerLeft _ (Functor.leftUnitor _).symm â‰ªâ‰« mapPairComp F (ğŸ­ D) G (ğŸ­ D)

variable (A) in
@[reassoc]
lemma mapWhiskerLeft_whiskerLeft (F : B â¥¤ C) {G H : C â¥¤ D} (Î· : G âŸ¶ H) :
    mapWhiskerLeft _ (whiskerLeft F Î·) =
      (mapCompRight A F G).hom â‰« whiskerLeft (mapPair (ğŸ­ A) F) (mapWhiskerLeft _ Î·) â‰«
      (mapCompRight A F H).inv := by
  apply natTrans_ext <;> ext <;> simp [mapCompRight]

variable (D) in
@[reassoc]
lemma mapWhiskerRight_whiskerLeft (F : A â¥¤ B) {G H : B â¥¤ C} (Î· : G âŸ¶ H) :
    mapWhiskerRight (whiskerLeft F Î·) (ğŸ­ D) =
      (mapCompLeft D F G).hom â‰« whiskerLeft (mapPair F (ğŸ­ D)) (mapWhiskerRight Î· _) â‰«
      (mapCompLeft D F H).inv := by
  apply natTrans_ext <;> ext <;> simp [mapCompLeft]

variable (A) in
@[reassoc]
lemma mapWhiskerLeft_whiskerRight {F G: B â¥¤ C} (Î· : F âŸ¶ G) (H : C â¥¤ D) :
    mapWhiskerLeft _ (whiskerRight Î· H) =
      (mapCompRight A F H).hom â‰« whiskerRight (mapWhiskerLeft _ Î·) (mapPair (ğŸ­ A) H) â‰«
      (mapCompRight A G H).inv := by
  apply natTrans_ext <;> ext <;> simp [mapCompRight]

variable (D) in
@[reassoc]
lemma mapWhiskerRight_whiskerRight {F G: A â¥¤ B} (Î· : F âŸ¶ G) (H : B â¥¤ C) :
    mapWhiskerRight (whiskerRight Î· H) _ =
      (mapCompLeft D F H).hom â‰« whiskerRight (mapWhiskerRight Î· _) (mapPair H (ğŸ­ D)) â‰«
      (mapCompLeft D G H).inv := by
  apply natTrans_ext <;> ext <;> simp [mapCompLeft]

variable {E : Type*} [Category E]

variable (A) in
@[reassoc]
lemma mapWhiskerLeft_associator (F : B â¥¤ C) (G : C â¥¤ D) (H : D â¥¤ E) :
    mapWhiskerLeft _ (F.associator G H).hom =
      (mapCompRight A (F â‹™ G) H).hom â‰« whiskerRight (mapCompRight A F G).hom (mapPair (ğŸ­ A) H) â‰«
        ((mapPair (ğŸ­ A) F).associator (mapPair (ğŸ­ A) G) (mapPair (ğŸ­ A) H)).hom â‰«
      whiskerLeft (mapPair (ğŸ­ A) F) (mapCompRight A G H).inv â‰« (mapCompRight A F (G â‹™ H)).inv := by
  apply natTrans_ext <;> ext <;> simp [mapCompRight]

variable (E) in
lemma mapWhiskerRight_associator (F : A â¥¤ B) (G : B â¥¤ C) (H : C â¥¤ D) :
    mapWhiskerRight (F.associator G H).hom _ =
      (mapCompLeft E (F â‹™ G) H).hom â‰« whiskerRight (mapCompLeft E F G).hom (mapPair H (ğŸ­ E)) â‰«
        ((mapPair F (ğŸ­ E)).associator (mapPair G (ğŸ­ E)) (mapPair H (ğŸ­ E))).hom â‰«
      whiskerLeft (mapPair F (ğŸ­ E)) (mapCompLeft E G H).inv â‰« (mapCompLeft E F (G â‹™ H)).inv := by
  apply natTrans_ext <;> ext <;> simp [mapCompLeft]

end

/-- The pseudofunctor sending `D` to `C â‹† D`. -/
@[simps!]
def pseudofunctorRight (C : Type uâ‚) [Category.{vâ‚} C] :
    Pseudofunctor Cat.{vâ‚‚, uâ‚‚} Cat.{max vâ‚ vâ‚‚, max uâ‚ uâ‚‚} where
  toPrelaxFunctor := prelaxFunctorRight C
  mapId D := mapPairId
  mapComp := mapCompRight C
  mapâ‚‚_whisker_left := mapWhiskerLeft_whiskerLeft C
  mapâ‚‚_whisker_right := mapWhiskerLeft_whiskerRight C
  mapâ‚‚_associator := mapWhiskerLeft_associator C
  mapâ‚‚_left_unitor {_ _} f := by
    apply NatTrans.ext
    ext x
    cases x
    all_goals
      dsimp [mapCompRight, mapCompLeft, leftUnitor]
      simp only [mapWhiskerLeft_app, Functor.id_obj, Category.assoc]
      repeat rw [NatTrans.comp_app]
      simp
  mapâ‚‚_right_unitor {_ _} g := by
    apply NatTrans.ext
    ext x
    cases x
    all_goals
      dsimp [mapCompRight, mapCompLeft, rightUnitor]
      simp only [mapWhiskerLeft_app, Functor.id_obj, Category.assoc]
      repeat rw [NatTrans.comp_app]
      simp

/-- The pseudofunctor sending `C` to `C â‹† D`. -/
@[simps!]
def pseudofunctorLeft (D : Type uâ‚‚) [Category.{vâ‚‚} D] :
    Pseudofunctor Cat.{vâ‚, uâ‚} Cat.{max vâ‚ vâ‚‚, max uâ‚ uâ‚‚} where
  toPrelaxFunctor := prelaxFunctorLeft D
  mapId D := mapPairId
  mapComp := mapCompLeft D
  mapâ‚‚_whisker_left := mapWhiskerRight_whiskerLeft D
  mapâ‚‚_whisker_right := mapWhiskerRight_whiskerRight D
  mapâ‚‚_associator := mapWhiskerRight_associator D
  mapâ‚‚_left_unitor {_ _} f := by
    apply NatTrans.ext
    ext x
    cases x
    all_goals
      dsimp [mapCompRight, mapCompLeft, leftUnitor]
      simp only [mapWhiskerLeft_app, Functor.id_obj, Category.assoc]
      repeat rw [NatTrans.comp_app]
      simp
  mapâ‚‚_right_unitor {_ _} g := by
    apply NatTrans.ext
    ext x
    cases x
    all_goals
      dsimp [mapCompRight, mapCompLeft, rightUnitor]
      simp only [mapWhiskerLeft_app, Functor.id_obj, Category.assoc]
      repeat rw [NatTrans.comp_app]
      simp

end CategoryTheory.Join
