/-
Copyright (c) 2021 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Comma.Over.Basic
import Mathlib.CategoryTheory.Limits.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Monad.Algebra

/-!
# Algebras for the coproduct monad

The functor `Y ‚Ü¶ X ‚®ø Y` forms a monad, whose category of monads is equivalent to the under category
of `X`. Similarly, `Y ‚Ü¶ X ‚®Ø Y` forms a comonad, whose category of comonads is equivalent to the
over category of `X`.

## TODO

Show that `Over.forget X : Over X ‚•§ C` is a comonadic left adjoint and `Under.forget : Under X ‚•§ C`
is a monadic right adjoint.
-/


noncomputable section

universe v u

namespace CategoryTheory

open Category Limits

variable {C : Type u} [Category.{v} C] (X : C)

section

open Comonad

variable [HasBinaryProducts C]

/-- `X ‚®Ø -` has a comonad structure. This is sometimes called the writer comonad. -/
@[simps!]
def prodComonad : Comonad C where
  toFunctor := prod.functor.obj X
  Œµ := { app := fun _ ‚Ü¶ Limits.prod.snd }
  Œ¥ := { app := fun _ ‚Ü¶ prod.lift Limits.prod.fst (ùüô _) }

/-- The forward direction of the equivalence from coalgebras for the product comonad to the over
category.
-/
@[simps]
def coalgebraToOver : Coalgebra (prodComonad X) ‚•§ Over X where
  obj A := Over.mk (A.a ‚â´ Limits.prod.fst)
  map f :=
    Over.homMk f.f
      (by
        rw [Over.mk_hom, ‚Üê f.h_assoc]
        simp)

/-- The backward direction of the equivalence from coalgebras for the product comonad to the over
category.
-/
@[simps]
def overToCoalgebra : Over X ‚•§ Coalgebra (prodComonad X) where
  obj f :=
    { A := f.left
      a := prod.lift f.hom (ùüô _) }
  map g := { f := g.left }

/-- The equivalence from coalgebras for the product comonad to the over category. -/
@[simps]
def coalgebraEquivOver : Coalgebra (prodComonad X) ‚âå Over X where
  functor := coalgebraToOver X
  inverse := overToCoalgebra X
  unitIso := NatIso.ofComponents fun A ‚Ü¶
    Coalgebra.isoMk (Iso.refl _) (Limits.prod.hom_ext (by simp) (by simpa using A.counit))
  counitIso := NatIso.ofComponents fun f ‚Ü¶ Over.isoMk (Iso.refl _)

end

section

open Monad

variable [HasBinaryCoproducts C]

/-- `X ‚®ø -` has a monad structure. This is sometimes called the either monad. -/
@[simps!]
def coprodMonad : Monad C where
  toFunctor := coprod.functor.obj X
  Œ∑ := { app := fun _ ‚Ü¶ coprod.inr }
  Œº := { app := fun _ ‚Ü¶ coprod.desc coprod.inl (ùüô _) }

/-- The forward direction of the equivalence from algebras for the coproduct monad to the under
category.
-/
@[simps]
def algebraToUnder : Monad.Algebra (coprodMonad X) ‚•§ Under X where
  obj A := Under.mk (coprod.inl ‚â´ A.a)
  map f :=
    Under.homMk f.f
      (by
        rw [Under.mk_hom, Category.assoc, ‚Üê f.h]
        simp)

/-- The backward direction of the equivalence from algebras for the coproduct monad to the under
category.
-/
@[simps]
def underToAlgebra : Under X ‚•§ Monad.Algebra (coprodMonad X) where
  obj f :=
    { A := f.right
      a := coprod.desc f.hom (ùüô _) }
  map g := { f := g.right }

/-- The equivalence from algebras for the coproduct monad to the under category.
-/
@[simps]
def algebraEquivUnder : Monad.Algebra (coprodMonad X) ‚âå Under X where
  functor := algebraToUnder X
  inverse := underToAlgebra X
  unitIso := NatIso.ofComponents fun A ‚Ü¶
    Monad.Algebra.isoMk (Iso.refl _) (coprod.hom_ext (by simp) (by simpa using A.unit.symm))
  counitIso :=
    NatIso.ofComponents fun f ‚Ü¶ Under.isoMk (Iso.refl _)

end

end CategoryTheory
