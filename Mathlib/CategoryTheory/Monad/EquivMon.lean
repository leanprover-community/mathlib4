/-
Copyright (c) 2020 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import Mathlib.CategoryTheory.Monad.Basic
import Mathlib.CategoryTheory.Monoidal.End
import Mathlib.CategoryTheory.Monoidal.Mon_

/-!

# The equivalence between `Monad C` and `Mon_ (C тед C)`.

A monad "is just" a monoid in the category of endofunctors.

# Definitions/Theorems

1. `toMon` associates a monoid object in `C тед C` to any monad on `C`.
2. `monadToMon` is the functorial version of `toMon`.
3. `ofMon` associates a monad on `C` to any monoid object in `C тед C`.
4. `monadMonEquiv` is the equivalence between `Monad C` and `Mon_ (C тед C)`.

-/


namespace CategoryTheory

open Category

universe v u -- morphism levels before object levels. See note [category_theory universes].

variable {C : Type u} [Category.{v} C]

namespace Monad

attribute [local instance] endofunctorMonoidalCategory

/-- To every `Monad C` we associated a monoid object in `C тед C`. -/
@[simps]
def toMon (M : Monad C) : Mon_ (C тед C) where
  X := (M : C тед C)
  one := M.╬╖
  mul := M.╬╝
  mul_assoc := by ext; simp [M.assoc]

variable (C) in
/-- Passing from `Monad C` to `Mon_ (C тед C)` is functorial. -/
@[simps]
def monadToMon : Monad C тед Mon_ (C тед C) where
  obj := toMon
  map f := { hom := f.toNatTrans }

/-- To every monoid object in `C тед C` we associate a `Monad C`. -/
@[simps ╬╖ ╬╝]
def ofMon (M : Mon_ (C тед C)) : Monad C where
  toFunctor := M.X
  ╬╖ := M.one
  ╬╝ := M.mul
  left_unit := fun X => by
    simpa [-Mon_.mul_one] using congrArg (fun t тЖж t.app X) M.mul_one
  right_unit := fun X => by
    simpa [-Mon_.one_mul] using congrArg (fun t тЖж t.app X) M.one_mul
  assoc := fun X => by
    simpa [-Mon_.mul_assoc] using congrArg (fun t тЖж t.app X) M.mul_assoc

-- Porting note: `@[simps]` fails to generate `ofMon_obj`:
@[simp] lemma ofMon_obj (M : Mon_ (C тед C)) (X : C) : (ofMon M).obj X = M.X.obj X := rfl

variable (C)

/-- Passing from `Mon_ (C тед C)` to `Monad C` is functorial. -/
@[simps]
def monToMonad : Mon_ (C тед C) тед Monad C where
  obj := ofMon
  map {X Y} f :=
    { f.hom with
      app_╬╖ X := by
        simpa [-Mon_.Hom.one_hom] using congrArg (fun t тЖж t.app X) f.one_hom
      app_╬╝ Z := by
        simpa [-Mon_.Hom.mul_hom] using congrArg (fun t тЖж t.app Z) f.mul_hom }

/-- Oh, monads are just monoids in the category of endofunctors (equivalence of categories). -/
@[simps]
def monadMonEquiv : Monad C тЙМ Mon_ (C тед C) where
  functor := monadToMon _
  inverse := monToMonad _
  unitIso :=
  { hom := { app := fun _ => { app := fun _ => ЁЭЯЩ _ } }
    inv := { app := fun _ => { app := fun _ => ЁЭЯЩ _ } } }
  counitIso :=
  { hom := { app := fun _ => { hom := ЁЭЯЩ _ } }
    inv := { app := fun _ => { hom := ЁЭЯЩ _ } } }

-- Sanity check
example (A : Monad C) {X : C} : ((monadMonEquiv C).unitIso.app A).hom.app X = ЁЭЯЩ _ :=
  rfl

end Monad

end CategoryTheory
