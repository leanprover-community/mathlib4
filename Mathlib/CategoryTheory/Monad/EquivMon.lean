/-
Copyright (c) 2020 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import Mathlib.CategoryTheory.Monad.Basic
import Mathlib.CategoryTheory.Monoidal.End
import Mathlib.CategoryTheory.Monoidal.Mon_

#align_import category_theory.monad.equiv_mon from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!

# The equivalence between `Monad C` and `Mon_ (C â¥¤ C)`.

A monad "is just" a monoid in the category of endofunctors.

# Definitions/Theorems

1. `toMon` associates a monoid object in `C â¥¤ C` to any monad on `C`.
2. `monadToMon` is the functorial version of `toMon`.
3. `ofMon` associates a monad on `C` to any monoid object in `C â¥¤ C`.
4. `monadMonEquiv` is the equivalence between `Monad C` and `Mon_ (C â¥¤ C)`.

-/

set_option linter.uppercaseLean3 false

namespace CategoryTheory

open Category

universe v u -- morphism levels before object levels. See note [category_theory universes].

variable {C : Type u} [Category.{v} C]

namespace Monad

attribute [local instance] endofunctorMonoidalCategory

/-- To every `Monad C` we associated a monoid object in `C â¥¤ C`. -/
@[simps]
def toMon (M : Monad C) : Mon_ (C â¥¤ C) where
  X := (M : C â¥¤ C)
  one := M.Î·
  mul := M.Î¼
  mul_assoc := by ext; simp [M.assoc]
#align category_theory.Monad.to_Mon CategoryTheory.Monad.toMon

variable (C)

/-- Passing from `Monad C` to `Mon_ (C â¥¤ C)` is functorial. -/
@[simps]
def monadToMon : Monad C â¥¤ Mon_ (C â¥¤ C) where
  obj := toMon
  map f := { hom := f.toNatTrans }
#align category_theory.Monad.Monad_to_Mon CategoryTheory.Monad.monadToMon

variable {C}

/-- To every monoid object in `C â¥¤ C` we associate a `Monad C`. -/
@[simps Î· Î¼]
def ofMon (M : Mon_ (C â¥¤ C)) : Monad C where
  toFunctor := M.X
  Î·' := M.one
  Î¼' := M.mul
  left_unit' := fun X => by
    -- Porting note: now using `erw`
    erw [â† whiskerLeft_app, â† NatTrans.comp_app, M.mul_one]
    rfl
  right_unit' := fun X => by
    -- Porting note: now using `erw`
    erw [â† whiskerRight_app, â† NatTrans.comp_app, M.one_mul]
    rfl
  assoc' := fun X => by
    rw [â† whiskerLeft_app, â† whiskerRight_app, â† NatTrans.comp_app]
    -- Porting note: had to add this step:
    erw [M.mul_assoc]
    simp
#align category_theory.Monad.of_Mon CategoryTheory.Monad.ofMon

-- Porting note: `@[simps]` fails to generate `ofMon_obj`:
@[simp] lemma ofMon_obj (M : Mon_ (C â¥¤ C)) (X : C) : (ofMon M).obj X = M.X.obj X := rfl

variable (C)

/-- Passing from `Mon_ (C â¥¤ C)` to `Monad C` is functorial. -/
@[simps]
def monToMonad : Mon_ (C â¥¤ C) â¥¤ Monad C where
  obj := ofMon
  map {X Y} f :=
    { f.hom with
      app_Î· := by
        intro X
        erw [â† NatTrans.comp_app, f.one_hom]
        simp only [Functor.id_obj, ofMon_obj, ofMon_Î·]
      app_Î¼ := by
        intro Z
        erw [â† NatTrans.comp_app, f.mul_hom]
        dsimp
        simp only [Category.assoc, NatTrans.naturality, ofMon_obj, ofMon] }
#align category_theory.Monad.Mon_to_Monad CategoryTheory.Monad.monToMonad

/-- Oh, monads are just monoids in the category of endofunctors (equivalence of categories). -/
@[simps]
def monadMonEquiv : Monad C â‰Œ Mon_ (C â¥¤ C) where
  functor := monadToMon _
  inverse := monToMonad _
  unitIso :=
  { hom := { app := fun _ => { app := fun _ => ğŸ™ _ } }
    inv := { app := fun _ => { app := fun _ => ğŸ™ _ } } }
  counitIso :=
  { hom := { app := fun _ => { hom := ğŸ™ _ } }
    inv := { app := fun _ => { hom := ğŸ™ _ } } }
#align category_theory.Monad.Monad_Mon_equiv CategoryTheory.Monad.monadMonEquiv

-- Sanity check
example (A : Monad C) {X : C} : ((monadMonEquiv C).unitIso.app A).hom.app X = ğŸ™ _ :=
  rfl

end Monad

end CategoryTheory
