/-
Copyright (c) 2020 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import Mathlib.CategoryTheory.Monad.Basic
import Mathlib.CategoryTheory.Monoidal.End
import Mathlib.CategoryTheory.Monoidal.Mon_

/-!

# The equivalence between `Monad C` and `Mon_ (C â¥¤ C)`.

A monad "is just" a monoid in the category of endofunctors.

# Definitions/Theorems

1. `toMon` associates a monoid object in `C â¥¤ C` to any monad on `C`.
2. `monadToMon` is the functorial version of `toMon`.
3. `ofMon` associates a monad on `C` to any monoid object in `C â¥¤ C`.
4. `monadMonEquiv` is the equivalence between `Monad C` and `Mon_ (C â¥¤ C)`.

-/


namespace CategoryTheory

open Category Mon_Class

universe v u -- morphism levels before object levels. See note [category_theory universes].

variable {C : Type u} [Category.{v} C]

namespace Monad

attribute [local instance] endofunctorMonoidalCategory

@[simps]
instance (M : Monad C) : Mon_Class (M : C â¥¤ C) where
  one := M.Î·
  mul := M.Î¼
  mul_assoc' := by ext; simp [M.assoc]

/-- To every `Monad C` we associated a monoid object in `C â¥¤ C`. -/
@[simps]
def toMon (M : Monad C) : Mon_ (C â¥¤ C) where
  X := (M : C â¥¤ C)

variable (C) in
/-- Passing from `Monad C` to `Mon_ (C â¥¤ C)` is functorial. -/
@[simps]
def monadToMon : Monad C â¥¤ Mon_ (C â¥¤ C) where
  obj := toMon
  map f := .mk' f.toNatTrans

/-- To every monoid object in `C â¥¤ C` we associate a `Monad C`. -/
@[simps Â«Î·Â» Â«Î¼Â»]
def ofMon (M : Mon_ (C â¥¤ C)) : Monad C where
  toFunctor := M.X
  Â«Î·Â» := Î·[M.X]
  Â«Î¼Â» := Î¼[M.X]
  left_unit := fun X => by
    simpa [-Mon_Class.mul_one] using congrArg (fun t â†¦ t.app X) (mul_one M.X)
  right_unit := fun X => by
    simpa [-Mon_Class.one_mul] using congrArg (fun t â†¦ t.app X) (one_mul M.X)
  assoc := fun X => by
    simpa [-Mon_Class.mul_assoc] using congrArg (fun t â†¦ t.app X) (mul_assoc M.X)

-- Porting note: `@[simps]` fails to generate `ofMon_obj`:
@[simp] lemma ofMon_obj (M : Mon_ (C â¥¤ C)) (X : C) : (ofMon M).obj X = M.X.obj X := rfl

variable (C)

/-- Passing from `Mon_ (C â¥¤ C)` to `Monad C` is functorial. -/
@[simps]
def monToMonad : Mon_ (C â¥¤ C) â¥¤ Monad C where
  obj := ofMon
  map {X Y} f :=
    { f.hom with
      app_Î· X := by
        simpa [-IsMon_Hom.one_hom] using congrArg (fun t â†¦ t.app X) (IsMon_Hom.one_hom f.hom)
      app_Î¼ Z := by
        simpa [-IsMon_Hom.mul_hom] using congrArg (fun t â†¦ t.app Z) (IsMon_Hom.mul_hom f.hom) }

/-- Oh, monads are just monoids in the category of endofunctors (equivalence of categories). -/
@[simps]
def monadMonEquiv : Monad C â‰Œ Mon_ (C â¥¤ C) where
  functor := monadToMon _
  inverse := monToMonad _
  unitIso :=
  { hom := { app := fun _ => { app := fun _ => ğŸ™ _ } }
    inv := { app := fun _ => { app := fun _ => ğŸ™ _ } } }
  counitIso :=
  { hom := { app := fun _ => { hom := ğŸ™ _ } }
    inv := { app := fun _ => { hom := ğŸ™ _ } } }

-- Sanity check
example (A : Monad C) {X : C} : ((monadMonEquiv C).unitIso.app A).hom.app X = ğŸ™ _ :=
  rfl

end Monad

end CategoryTheory
