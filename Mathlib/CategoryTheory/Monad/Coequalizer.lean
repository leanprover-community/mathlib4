/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Limits.Shapes.Reflexive
import Mathlib.CategoryTheory.Limits.Shapes.SplitCoequalizer
import Mathlib.CategoryTheory.Monad.Algebra

#align_import category_theory.monad.coequalizer from "leanprover-community/mathlib"@"3a061790136d13594ec10c7c90d202335ac5d854"

/-!
# Special coequalizers associated to a monad

Associated to a monad `T : C â¥¤ C` we have important coequalizer constructions:
Any algebra is a coequalizer (in the category of algebras) of free algebras. Furthermore, this
coequalizer is reflexive.
In `C`, this cofork diagram is a split coequalizer (in particular, it is still a coequalizer).
This split coequalizer is known as the Beck coequalizer (as it features heavily in Beck's
monadicity theorem).
-/


universe vâ‚ uâ‚

namespace CategoryTheory

namespace Monad

open Limits

variable {C : Type uâ‚}

variable [Category.{vâ‚} C]

variable {T : Monad C} (X : Algebra T)

/-!
Show that any algebra is a coequalizer of free algebras.
-/


/-- The top map in the coequalizer diagram we will construct. -/
@[simps!]
def FreeCoequalizer.topMap : (Monad.free T).obj (T.obj X.A) âŸ¶ (Monad.free T).obj X.A :=
  (Monad.free T).map X.a
#align category_theory.monad.free_coequalizer.top_map CategoryTheory.Monad.FreeCoequalizer.topMap

/-- The bottom map in the coequalizer diagram we will construct. -/
@[simps]
def FreeCoequalizer.bottomMap : (Monad.free T).obj (T.obj X.A) âŸ¶ (Monad.free T).obj X.A where
  f := T.Î¼.app X.A
  h := T.assoc X.A
#align category_theory.monad.free_coequalizer.bottom_map CategoryTheory.Monad.FreeCoequalizer.bottomMap

/-- The cofork map in the coequalizer diagram we will construct. -/
@[simps]
def FreeCoequalizer.Ï€ : (Monad.free T).obj X.A âŸ¶ X where
  f := X.a
  h := X.assoc.symm
#align category_theory.monad.free_coequalizer.Ï€ CategoryTheory.Monad.FreeCoequalizer.Ï€

theorem FreeCoequalizer.condition :
    FreeCoequalizer.topMap X â‰« FreeCoequalizer.Ï€ X =
      FreeCoequalizer.bottomMap X â‰« FreeCoequalizer.Ï€ X :=
  Algebra.Hom.ext _ _ X.assoc.symm
#align category_theory.monad.free_coequalizer.condition CategoryTheory.Monad.FreeCoequalizer.condition

instance : IsReflexivePair (FreeCoequalizer.topMap X) (FreeCoequalizer.bottomMap X) := by
  apply IsReflexivePair.mk' _ _ _
  apply (free T).map (T.Î·.app X.A)
  -- âŠ¢ (free T).map (NatTrans.app (Î· T) X.A) â‰« FreeCoequalizer.topMap X = ğŸ™ ((free  â€¦
  Â· ext
    -- âŠ¢ ((free T).map (NatTrans.app (Î· T) X.A) â‰« FreeCoequalizer.topMap X).f = (ğŸ™ (( â€¦
    dsimp
    -- âŠ¢ T.map (NatTrans.app (Î· T) X.A) â‰« T.map X.a = ğŸ™ (T.obj X.A)
    rw [â† Functor.map_comp, X.unit, Functor.map_id]
    -- ğŸ‰ no goals
  Â· ext
    -- âŠ¢ ((free T).map (NatTrans.app (Î· T) X.A) â‰« FreeCoequalizer.bottomMap X).f = (ğŸ™ â€¦
    apply Monad.right_unit
    -- ğŸ‰ no goals

/-- Construct the Beck cofork in the category of algebras. This cofork is reflexive as well as a
coequalizer.
-/
@[simps!]
def beckAlgebraCofork : Cofork (FreeCoequalizer.topMap X) (FreeCoequalizer.bottomMap X) :=
  Cofork.ofÏ€ _ (FreeCoequalizer.condition X)
#align category_theory.monad.beck_algebra_cofork CategoryTheory.Monad.beckAlgebraCofork

/-- The cofork constructed is a colimit. This shows that any algebra is a (reflexive) coequalizer of
free algebras.
-/
def beckAlgebraCoequalizer : IsColimit (beckAlgebraCofork X) :=
  Cofork.IsColimit.mk' _ fun s => by
    have hâ‚ : (T : C â¥¤ C).map X.a â‰« s.Ï€.f = T.Î¼.app X.A â‰« s.Ï€.f :=
      congr_arg Monad.Algebra.Hom.f s.condition
    have hâ‚‚ : (T : C â¥¤ C).map s.Ï€.f â‰« s.pt.a = T.Î¼.app X.A â‰« s.Ï€.f := s.Ï€.h
    -- âŠ¢ { l // Cofork.Ï€ (beckAlgebraCofork X) â‰« l = Cofork.Ï€ s âˆ§ âˆ€ {m : ((Functor.co â€¦
    refine' âŸ¨âŸ¨T.Î·.app _ â‰« s.Ï€.f, _âŸ©, _, _âŸ©
    Â· dsimp
      -- âŠ¢ T.map (NatTrans.app (Î· T) X.1 â‰« (Cofork.Ï€ s).f) â‰« s.pt.a = X.a â‰« NatTrans.ap â€¦
      rw [Functor.map_comp, Category.assoc, hâ‚‚, Monad.right_unit_assoc,
        show X.a â‰« _ â‰« _ = _ from T.Î·.naturality_assoc _ _, hâ‚, Monad.left_unit_assoc]
    Â· ext
      -- âŠ¢ (Cofork.Ï€ (beckAlgebraCofork X) â‰« Algebra.Hom.mk (NatTrans.app (Î· T) (beckAl â€¦
      simpa [â† T.Î·.naturality_assoc, T.left_unit_assoc] using T.Î·.app ((T : C â¥¤ C).obj X.A) â‰«= hâ‚
      -- ğŸ‰ no goals
    Â· intro m hm
      -- âŠ¢ m = Algebra.Hom.mk (NatTrans.app (Î· T) (beckAlgebraCofork X).pt.1 â‰« (Cofork. â€¦
      ext
      -- âŠ¢ m.f = (Algebra.Hom.mk (NatTrans.app (Î· T) (beckAlgebraCofork X).pt.1 â‰« (Cofo â€¦
      dsimp only
      -- âŠ¢ m.f = NatTrans.app (Î· T) (beckAlgebraCofork X).pt.1 â‰« (Cofork.Ï€ s).f
      rw [â† hm]
      -- âŠ¢ m.f = NatTrans.app (Î· T) (beckAlgebraCofork X).pt.1 â‰« (Cofork.Ï€ (beckAlgebra â€¦
      apply (X.unit_assoc _).symm
      -- ğŸ‰ no goals
#align category_theory.monad.beck_algebra_coequalizer CategoryTheory.Monad.beckAlgebraCoequalizer

/-- The Beck cofork is a split coequalizer. -/
def beckSplitCoequalizer : IsSplitCoequalizer (T.map X.a) (T.Î¼.app _) X.a :=
  âŸ¨T.Î·.app _, T.Î·.app _, X.assoc.symm, X.unit, T.left_unit _, (T.Î·.naturality _).symmâŸ©
#align category_theory.monad.beck_split_coequalizer CategoryTheory.Monad.beckSplitCoequalizer

/-- This is the Beck cofork. It is a split coequalizer, in particular a coequalizer. -/
@[simps! pt]
def beckCofork : Cofork (T.map X.a) (T.Î¼.app _) :=
  (beckSplitCoequalizer X).asCofork
#align category_theory.monad.beck_cofork CategoryTheory.Monad.beckCofork

@[simp]
theorem beckCofork_Ï€ : (beckCofork X).Ï€ = X.a :=
  rfl
#align category_theory.monad.beck_cofork_Ï€ CategoryTheory.Monad.beckCofork_Ï€

/-- The Beck cofork is a coequalizer. -/
def beckCoequalizer : IsColimit (beckCofork X) :=
  (beckSplitCoequalizer X).isCoequalizer
#align category_theory.monad.beck_coequalizer CategoryTheory.Monad.beckCoequalizer

@[simp]
theorem beckCoequalizer_desc (s : Cofork (T.toFunctor.map X.a) (T.Î¼.app X.A)) :
    (beckCoequalizer X).desc s = T.Î·.app _ â‰« s.Ï€ :=
  rfl
#align category_theory.monad.beck_coequalizer_desc CategoryTheory.Monad.beckCoequalizer_desc

end Monad

end CategoryTheory
