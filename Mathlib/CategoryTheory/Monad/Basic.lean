/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Bhavik Mehta, Adam Topaz
-/
import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.Functor.FullyFaithful
import Mathlib.CategoryTheory.Functor.ReflectsIso.Balanced

/-!
# Monads

We construct the categories of monads and comonads, and their forgetful functors to endofunctors.

(Note that these are the category theorist's monads, not the programmers monads.
For the translation, see the file `CategoryTheory.Monad.Types`.)

For the fact that monads are "just" monoids in the category of endofunctors, see the file
`CategoryTheory.Monad.EquivMon`.
-/


namespace CategoryTheory

open Category

universe v‚ÇÅ u‚ÇÅ

-- morphism levels before object levels. See note [CategoryTheory universes].
variable (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C]

/-- The data of a monad on C consists of an endofunctor T together with natural transformations
Œ∑ : ùü≠ C ‚ü∂ T and Œº : T ‚ãô T ‚ü∂ T satisfying three equations:
- T Œº_X ‚â´ Œº_X = Œº_(TX) ‚â´ Œº_X (associativity)
- Œ∑_(TX) ‚â´ Œº_X = 1_X (left unit)
- TŒ∑_X ‚â´ Œº_X = 1_X (right unit)
-/
structure Monad extends C ‚•§ C where
  /-- The unit for the monad. -/
  Œ∑ : ùü≠ _ ‚ü∂ toFunctor
  /-- The multiplication for the monad. -/
  Œº : toFunctor ‚ãô toFunctor ‚ü∂ toFunctor
  assoc : ‚àÄ X, toFunctor.map (NatTrans.app Œº X) ‚â´ Œº.app _ = Œº.app _ ‚â´ Œº.app _ := by aesop_cat
  left_unit : ‚àÄ X : C, Œ∑.app (toFunctor.obj X) ‚â´ Œº.app _ = ùüô _ := by aesop_cat
  right_unit : ‚àÄ X : C, toFunctor.map (Œ∑.app X) ‚â´ Œº.app _ = ùüô _ := by aesop_cat

/-- The data of a comonad on C consists of an endofunctor G together with natural transformations
Œµ : G ‚ü∂ ùü≠ C and Œ¥ : G ‚ü∂ G ‚ãô G satisfying three equations:
- Œ¥_X ‚â´ G Œ¥_X = Œ¥_X ‚â´ Œ¥_(GX) (coassociativity)
- Œ¥_X ‚â´ Œµ_(GX) = 1_X (left counit)
- Œ¥_X ‚â´ G Œµ_X = 1_X (right counit)
-/
structure Comonad extends C ‚•§ C where
  /-- The counit for the comonad. -/
  Œµ : toFunctor ‚ü∂ ùü≠ _
  /-- The comultiplication for the comonad. -/
  Œ¥ : toFunctor ‚ü∂ toFunctor ‚ãô toFunctor
  coassoc : ‚àÄ X, NatTrans.app Œ¥ _ ‚â´ toFunctor.map (Œ¥.app X) = Œ¥.app _ ‚â´ Œ¥.app _ := by
    aesop_cat
  left_counit : ‚àÄ X : C, Œ¥.app X ‚â´ Œµ.app (toFunctor.obj X) = ùüô _ := by aesop_cat
  right_counit : ‚àÄ X : C, Œ¥.app X ‚â´ toFunctor.map (Œµ.app X) = ùüô _ := by aesop_cat

variable {C}
variable (T : Monad C) (G : Comonad C)

instance coeMonad : Coe (Monad C) (C ‚•§ C) :=
  ‚ü®fun T => T.toFunctor‚ü©

instance coeComonad : Coe (Comonad C) (C ‚•§ C) :=
  ‚ü®fun G => G.toFunctor‚ü©

-- Porting note: these lemmas are syntactic tautologies
--@[simp]
--theorem monad_toFunctor_eq_coe : T.toFunctor = T :=
--  rfl
--
--@[simp]
--theorem comonad_toFunctor_eq_coe : G.toFunctor = G :=
--  rfl

initialize_simps_projections CategoryTheory.Monad (toFunctor ‚Üí coe)

initialize_simps_projections CategoryTheory.Comonad (toFunctor ‚Üí coe)

-- Porting note: investigate whether `Monad.assoc` can be a `simp` lemma?
attribute [reassoc (attr := simp)] Monad.left_unit Monad.right_unit
attribute [reassoc (attr := simp)] Comonad.coassoc Comonad.left_counit Comonad.right_counit

/-- A morphism of monads is a natural transformation compatible with Œ∑ and Œº. -/
@[ext]
structure MonadHom (T‚ÇÅ T‚ÇÇ : Monad C) extends NatTrans (T‚ÇÅ : C ‚•§ C) T‚ÇÇ where
  app_Œ∑ : ‚àÄ X, T‚ÇÅ.Œ∑.app X ‚â´ app X = T‚ÇÇ.Œ∑.app X := by aesop_cat
  app_Œº : ‚àÄ X, T‚ÇÅ.Œº.app X ‚â´ app X = (T‚ÇÅ.map (app X) ‚â´ app _) ‚â´ T‚ÇÇ.Œº.app X := by
    aesop_cat

initialize_simps_projections MonadHom (+toNatTrans, -app)

/-- A morphism of comonads is a natural transformation compatible with Œµ and Œ¥. -/
@[ext]
structure ComonadHom (M N : Comonad C) extends NatTrans (M : C ‚•§ C) N where
  app_Œµ : ‚àÄ X, app X ‚â´ N.Œµ.app X = M.Œµ.app X := by aesop_cat
  app_Œ¥ : ‚àÄ X, app X ‚â´ N.Œ¥.app X = M.Œ¥.app X ‚â´ app _ ‚â´ N.map (app X) := by aesop_cat

initialize_simps_projections ComonadHom (+toNatTrans, -app)

attribute [reassoc (attr := simp)] MonadHom.app_Œ∑ MonadHom.app_Œº
attribute [reassoc (attr := simp)] ComonadHom.app_Œµ ComonadHom.app_Œ¥

instance : Quiver (Monad C) where
  Hom := MonadHom

instance : Quiver (Comonad C) where
  Hom := ComonadHom

-- Porting note (https://github.com/leanprover-community/mathlib4/issues/10688): added to ease automation
@[ext]
lemma MonadHom.ext' {T‚ÇÅ T‚ÇÇ : Monad C} (f g : T‚ÇÅ ‚ü∂ T‚ÇÇ) (h : f.app = g.app) : f = g :=
  MonadHom.ext h

-- Porting note (https://github.com/leanprover-community/mathlib4/issues/10688): added to ease automation
@[ext]
lemma ComonadHom.ext' {T‚ÇÅ T‚ÇÇ : Comonad C} (f g : T‚ÇÅ ‚ü∂ T‚ÇÇ) (h : f.app = g.app) : f = g :=
  ComonadHom.ext h

instance : Category (Monad C) where
  id M := { toNatTrans := ùüô (M : C ‚•§ C) }
  comp f g :=
    { toNatTrans :=
        { app := fun X => f.app X ‚â´ g.app X
          naturality := fun X Y h => by rw [assoc, f.1.naturality_assoc, g.1.naturality] } }
  -- `aesop_cat` can fill in these proofs, but is unfortunately slightly slow.
  id_comp _ := MonadHom.ext (by funext; simp only [NatTrans.id_app, id_comp])
  comp_id _ := MonadHom.ext (by funext; simp only [NatTrans.id_app, comp_id])
  assoc _ _ _ := MonadHom.ext (by funext; simp only [assoc])

instance : Category (Comonad C) where
  id M := { toNatTrans := ùüô (M : C ‚•§ C) }
  comp f g :=
    { toNatTrans :=
        { app := fun X => f.app X ‚â´ g.app X
          naturality := fun X Y h => by rw [assoc, f.1.naturality_assoc, g.1.naturality] } }
  -- `aesop_cat` can fill in these proofs, but is unfortunately slightly slow.
  id_comp _ := ComonadHom.ext (by funext; simp only [NatTrans.id_app, id_comp])
  comp_id _ := ComonadHom.ext (by funext; simp only [NatTrans.id_app, comp_id])
  assoc _ _ _ := ComonadHom.ext (by funext; simp only [assoc])

instance {T : Monad C} : Inhabited (MonadHom T T) :=
  ‚ü®ùüô T‚ü©

@[simp]
theorem MonadHom.id_toNatTrans (T : Monad C) : (ùüô T : T ‚ü∂ T).toNatTrans = ùüô (T : C ‚•§ C) :=
  rfl

@[simp]
theorem MonadHom.comp_toNatTrans {T‚ÇÅ T‚ÇÇ T‚ÇÉ : Monad C} (f : T‚ÇÅ ‚ü∂ T‚ÇÇ) (g : T‚ÇÇ ‚ü∂ T‚ÇÉ) :
    (f ‚â´ g).toNatTrans = ((f.toNatTrans : _ ‚ü∂ (T‚ÇÇ : C ‚•§ C)) ‚â´ g.toNatTrans : (T‚ÇÅ : C ‚•§ C) ‚ü∂ T‚ÇÉ) :=
  rfl

instance {G : Comonad C} : Inhabited (ComonadHom G G) :=
  ‚ü®ùüô G‚ü©

@[simp]
theorem ComonadHom.id_toNatTrans (T : Comonad C) : (ùüô T : T ‚ü∂ T).toNatTrans = ùüô (T : C ‚•§ C) :=
  rfl

@[simp]
theorem comp_toNatTrans {T‚ÇÅ T‚ÇÇ T‚ÇÉ : Comonad C} (f : T‚ÇÅ ‚ü∂ T‚ÇÇ) (g : T‚ÇÇ ‚ü∂ T‚ÇÉ) :
    (f ‚â´ g).toNatTrans = ((f.toNatTrans : _ ‚ü∂ (T‚ÇÇ : C ‚•§ C)) ‚â´ g.toNatTrans : (T‚ÇÅ : C ‚•§ C) ‚ü∂ T‚ÇÉ) :=
  rfl

/-- Construct a monad isomorphism from a natural isomorphism of functors where the forward
direction is a monad morphism. -/
@[simps]
def MonadIso.mk {M N : Monad C} (f : (M : C ‚•§ C) ‚âÖ N)
    (f_Œ∑ : ‚àÄ (X : C), M.Œ∑.app X ‚â´ f.hom.app X = N.Œ∑.app X := by aesop_cat)
    (f_Œº : ‚àÄ (X : C), M.Œº.app X ‚â´ f.hom.app X =
    (M.map (f.hom.app X) ‚â´ f.hom.app (N.obj X)) ‚â´ N.Œº.app X := by aesop_cat) : M ‚âÖ N where
  hom :=
    { toNatTrans := f.hom
      app_Œ∑ := f_Œ∑
      app_Œº := f_Œº }
  inv :=
    { toNatTrans := f.inv
      app_Œ∑ := fun X => by simp [‚Üê f_Œ∑]
      app_Œº := fun X => by
        rw [‚Üê NatIso.cancel_natIso_hom_right f]
        simp only [NatTrans.naturality, Iso.inv_hom_id_app, assoc, comp_id, f_Œº,
          NatTrans.naturality_assoc, Iso.inv_hom_id_app_assoc, ‚Üê Functor.map_comp_assoc]
        simp }

/-- Construct a comonad isomorphism from a natural isomorphism of functors where the forward
direction is a comonad morphism. -/
@[simps]
def ComonadIso.mk {M N : Comonad C} (f : (M : C ‚•§ C) ‚âÖ N)
    (f_Œµ : ‚àÄ (X : C), f.hom.app X ‚â´ N.Œµ.app X = M.Œµ.app X := by aesop_cat)
    (f_Œ¥ : ‚àÄ (X : C), f.hom.app X ‚â´ N.Œ¥.app X =
    M.Œ¥.app X ‚â´ f.hom.app (M.obj X) ‚â´ N.map (f.hom.app X) := by aesop_cat) : M ‚âÖ N where
  hom :=
    { toNatTrans := f.hom
      app_Œµ := f_Œµ
      app_Œ¥ := f_Œ¥ }
  inv :=
    { toNatTrans := f.inv
      app_Œµ := fun X => by simp [‚Üê f_Œµ]
      app_Œ¥ := fun X => by
        rw [‚Üê NatIso.cancel_natIso_hom_left f]
        simp only [reassoc_of% (f_Œ¥ X), Iso.hom_inv_id_app_assoc, NatTrans.naturality_assoc]
        rw [‚Üê Functor.map_comp, Iso.hom_inv_id_app, Functor.map_id]
        apply (comp_id _).symm }

variable (C)

/-- The forgetful functor from the category of monads to the category of endofunctors.
-/
@[simps!]
def monadToFunctor : Monad C ‚•§ C ‚•§ C where
  obj T := T
  map f := f.toNatTrans

instance : (monadToFunctor C).Faithful where

theorem monadToFunctor_mapIso_monad_iso_mk {M N : Monad C} (f : (M : C ‚•§ C) ‚âÖ N) (f_Œ∑ f_Œº) :
    (monadToFunctor _).mapIso (MonadIso.mk f f_Œ∑ f_Œº) = f := by
  ext
  rfl

instance : (monadToFunctor C).ReflectsIsomorphisms where
  reflects f _ := (MonadIso.mk (asIso ((monadToFunctor C).map f)) f.app_Œ∑ f.app_Œº).isIso_hom

/-- The forgetful functor from the category of comonads to the category of endofunctors.
-/
@[simps!]
def comonadToFunctor : Comonad C ‚•§ C ‚•§ C where
  obj G := G
  map f := f.toNatTrans

instance : (comonadToFunctor C).Faithful where

theorem comonadToFunctor_mapIso_comonad_iso_mk {M N : Comonad C} (f : (M : C ‚•§ C) ‚âÖ N) (f_Œµ f_Œ¥) :
    (comonadToFunctor _).mapIso (ComonadIso.mk f f_Œµ f_Œ¥) = f := by
  ext
  rfl

instance : (comonadToFunctor C).ReflectsIsomorphisms where
  reflects f _ := (ComonadIso.mk (asIso ((comonadToFunctor C).map f)) f.app_Œµ f.app_Œ¥).isIso_hom

variable {C}

/-- An isomorphism of monads gives a natural isomorphism of the underlying functors.
-/
/- Porting note: removed
`@[simps (config := { rhsMd := semireducible })]`
and replaced with `@[simps]` in the two declarations below -/
@[simps!]
def MonadIso.toNatIso {M N : Monad C} (h : M ‚âÖ N) : (M : C ‚•§ C) ‚âÖ N :=
  (monadToFunctor C).mapIso h

/-- An isomorphism of comonads gives a natural isomorphism of the underlying functors.
-/
@[simps!]
def ComonadIso.toNatIso {M N : Comonad C} (h : M ‚âÖ N) : (M : C ‚•§ C) ‚âÖ N :=
  (comonadToFunctor C).mapIso h

variable (C)

namespace Monad

/-- The identity monad. -/
@[simps!]
def id : Monad C where
  toFunctor := ùü≠ C
  Œ∑ := ùüô (ùü≠ C)
  Œº := ùüô (ùü≠ C)

instance : Inhabited (Monad C) :=
  ‚ü®Monad.id C‚ü©

end Monad

namespace Comonad

/-- The identity comonad. -/
@[simps!]
def id : Comonad C where
  toFunctor := ùü≠ _
  Œµ := ùüô (ùü≠ C)
  Œ¥ := ùüô (ùü≠ C)

instance : Inhabited (Comonad C) :=
  ‚ü®Comonad.id C‚ü©

end Comonad

open Iso Functor

variable {C}

namespace Monad

/-- Transport a monad structure on a functor along an isomorphism of functors. -/
def transport {F : C ‚•§ C} (T : Monad C) (i : (T : C ‚•§ C) ‚âÖ F) : Monad C where
  toFunctor := F
  Œ∑ := T.Œ∑ ‚â´ i.hom
  Œº := (i.inv ‚ó´ i.inv) ‚â´ T.Œº ‚â´ i.hom
  left_unit X := by
    simp only [Functor.id_obj, NatTrans.comp_app, comp_obj, NatTrans.hcomp_app, Category.assoc,
      hom_inv_id_app_assoc]
    slice_lhs 1 2 => rw [‚Üê T.Œ∑.naturality (i.inv.app X), ]
    simp
  right_unit X := by
    simp only [id_obj, NatTrans.comp_app, Functor.map_comp, comp_obj, NatTrans.hcomp_app,
      Category.assoc, NatTrans.naturality_assoc]
    slice_lhs 2 4 =>
      simp only [‚Üê T.map_comp]
    simp
  assoc X := by
    simp only [comp_obj, NatTrans.comp_app, NatTrans.hcomp_app, Category.assoc, Functor.map_comp,
      NatTrans.naturality_assoc, hom_inv_id_app_assoc, NatIso.cancel_natIso_inv_left]
    slice_lhs 4 5 => rw [‚Üê T.map_comp]
    simp only [hom_inv_id_app, Functor.map_id, id_comp]
    slice_lhs 1 2 => rw [‚Üê T.map_comp]
    simp only [Functor.map_comp, Category.assoc]
    congr 1
    simp only [‚Üê Category.assoc, NatIso.cancel_natIso_hom_right]
    rw [‚Üê T.Œº.naturality]
    simp [T.assoc X]

end Monad

namespace Comonad

/-- Transport a comonad structure on a functor along an isomorphism of functors. -/
def transport {F : C ‚•§ C} (T : Comonad C) (i : (T : C ‚•§ C) ‚âÖ F) : Comonad C where
  toFunctor := F
  Œµ := i.inv ‚â´ T.Œµ
  Œ¥ := i.inv ‚â´ T.Œ¥ ‚â´ (i.hom ‚ó´ i.hom)
  right_counit X := by
    simp only [id_obj, comp_obj, NatTrans.comp_app, NatTrans.hcomp_app, Functor.map_comp, assoc]
    slice_lhs 4 5 => rw [‚Üê F.map_comp]
    simp only [hom_inv_id_app, Functor.map_id, id_comp, ‚Üê i.hom.naturality]
    slice_lhs 2 3 => rw [T.right_counit]
    simp
  coassoc X := by
    simp only [comp_obj, NatTrans.comp_app, NatTrans.hcomp_app, Functor.map_comp, assoc,
      NatTrans.naturality_assoc, Functor.comp_map, hom_inv_id_app_assoc,
      NatIso.cancel_natIso_inv_left]
    slice_lhs 3 4 => rw [‚Üê F.map_comp]
    simp only [hom_inv_id_app, Functor.map_id, id_comp, assoc]
    rw [‚Üê i.hom.naturality_assoc, ‚Üê T.coassoc_assoc]
    simp only [NatTrans.naturality_assoc]
    congr 3
    simp only [‚Üê Functor.map_comp, i.hom.naturality]

end Comonad

namespace Monad

lemma map_unit_app (T : Monad C) (X : C) [IsIso T.Œº] :
    T.map (T.Œ∑.app X) = T.Œ∑.app (T.obj X) := by
  simp [‚Üê cancel_mono (T.Œº.app _)]

lemma isSplitMono_iff_isIso_unit (T : Monad C) (X : C) [IsIso T.Œº] :
    IsSplitMono (T.Œ∑.app X) ‚Üî IsIso (T.Œ∑.app X) := by
  refine ‚ü®fun _ ‚Ü¶ ‚ü®retraction (T.Œ∑.app X), by simp, ?_‚ü©, fun _ ‚Ü¶ inferInstance‚ü©
  erw [‚Üê map_id, ‚Üê IsSplitMono.id (T.Œ∑.app X), map_comp, T.map_unit_app X, T.Œ∑.naturality]
  rfl

end Monad

namespace Comonad

lemma map_counit_app (T : Comonad C) (X : C) [IsIso T.Œ¥] :
    T.map (T.Œµ.app X) = T.Œµ.app (T.obj X) := by
  simp [‚Üê cancel_epi (T.Œ¥.app _)]

lemma isSplitEpi_iff_isIso_counit (T : Comonad C) (X : C) [IsIso T.Œ¥] :
    IsSplitEpi (T.Œµ.app X) ‚Üî IsIso (T.Œµ.app X) := by
  refine ‚ü®fun _ ‚Ü¶ ‚ü®section_ (T.Œµ.app X), ?_, by simp‚ü©, fun _ ‚Ü¶ inferInstance‚ü©
  erw [‚Üê map_id, ‚Üê IsSplitEpi.id (T.Œµ.app X), map_comp, T.map_counit_app X, T.Œµ.naturality]
  rfl

end Comonad

end CategoryTheory
