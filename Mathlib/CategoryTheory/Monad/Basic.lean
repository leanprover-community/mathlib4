/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Bhavik Mehta, Adam Topaz
-/
import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.Functor.FullyFaithful
import Mathlib.CategoryTheory.Functor.ReflectsIso

#align_import category_theory.monad.basic from "leanprover-community/mathlib"@"9c6816cab5872990d450d2c2e7832176167b1c07"

/-!
# Monads

We construct the categories of monads and comonads, and their forgetful functors to endofunctors.

(Note that these are the category theorist's monads, not the programmers monads.
For the translation, see the file `CategoryTheory.Monad.Types`.)

For the fact that monads are "just" monoids in the category of endofunctors, see the file
`CategoryTheory.Monad.EquivMon`.
-/


namespace CategoryTheory

open Category

universe vâ‚ uâ‚

-- morphism levels before object levels. See note [CategoryTheory universes].
variable (C : Type uâ‚) [Category.{vâ‚} C]

/-- The data of a monad on C consists of an endofunctor T together with natural transformations
Î· : ğŸ­ C âŸ¶ T and Î¼ : T â‹™ T âŸ¶ T satisfying three equations:
- T Î¼_X â‰« Î¼_X = Î¼_(TX) â‰« Î¼_X (associativity)
- Î·_(TX) â‰« Î¼_X = 1_X (left unit)
- TÎ·_X â‰« Î¼_X = 1_X (right unit)
-/
structure Monad extends C â¥¤ C where
  Î·' : ğŸ­ _ âŸ¶ toFunctor
  Î¼' : toFunctor â‹™ toFunctor âŸ¶ toFunctor
  assoc' : âˆ€ X, toFunctor.map (NatTrans.app Î¼' X) â‰« Î¼'.app _ = Î¼'.app _ â‰« Î¼'.app _ := by aesop_cat
  left_unit' : âˆ€ X : C, Î·'.app (toFunctor.obj X) â‰« Î¼'.app _ = ğŸ™ _ := by aesop_cat
  right_unit' : âˆ€ X : C, toFunctor.map (Î·'.app X) â‰« Î¼'.app _ = ğŸ™ _ := by aesop_cat
#align category_theory.monad CategoryTheory.Monad

/-- The data of a comonad on C consists of an endofunctor G together with natural transformations
Îµ : G âŸ¶ ğŸ­ C and Î´ : G âŸ¶ G â‹™ G satisfying three equations:
- Î´_X â‰« G Î´_X = Î´_X â‰« Î´_(GX) (coassociativity)
- Î´_X â‰« Îµ_(GX) = 1_X (left counit)
- Î´_X â‰« G Îµ_X = 1_X (right counit)
-/
structure Comonad extends C â¥¤ C where
  Îµ' : toFunctor âŸ¶ ğŸ­ _
  Î´' : toFunctor âŸ¶ toFunctor â‹™ toFunctor
  coassoc' : âˆ€ X, NatTrans.app Î´' _ â‰« toFunctor.map (Î´'.app X) = Î´'.app _ â‰« Î´'.app _ := by
    aesop_cat
  left_counit' : âˆ€ X : C, Î´'.app X â‰« Îµ'.app (toFunctor.obj X) = ğŸ™ _ := by aesop_cat
  right_counit' : âˆ€ X : C, Î´'.app X â‰« toFunctor.map (Îµ'.app X) = ğŸ™ _ := by aesop_cat
#align category_theory.comonad CategoryTheory.Comonad

variable {C}
variable (T : Monad C) (G : Comonad C)

instance coeMonad : Coe (Monad C) (C â¥¤ C) :=
  âŸ¨fun T => T.toFunctorâŸ©
#align category_theory.coe_monad CategoryTheory.coeMonad

instance coeComonad : Coe (Comonad C) (C â¥¤ C) :=
  âŸ¨fun G => G.toFunctorâŸ©
#align category_theory.coe_comonad CategoryTheory.coeComonad

-- Porting note: these lemmas are syntactic tautologies
--@[simp]
--theorem monad_toFunctor_eq_coe : T.toFunctor = T :=
--  rfl
--#align category_theory.monad_to_functor_eq_coe CategoryTheory.monad_toFunctor_eq_coe
--
--@[simp]
--theorem comonad_toFunctor_eq_coe : G.toFunctor = G :=
--  rfl
--#align category_theory.comonad_to_functor_eq_coe CategoryTheory.comonad_toFunctor_eq_coe

/-- The unit for the monad `T`. -/
def Monad.Î· : ğŸ­ _ âŸ¶ (T : C â¥¤ C) :=
  T.Î·'
#align category_theory.monad.Î· CategoryTheory.Monad.Î·

/-- The multiplication for the monad `T`. -/
def Monad.Î¼ : (T : C â¥¤ C) â‹™ (T : C â¥¤ C) âŸ¶ T :=
  T.Î¼'
#align category_theory.monad.Î¼ CategoryTheory.Monad.Î¼

/-- The counit for the comonad `G`. -/
def Comonad.Îµ : (G : C â¥¤ C) âŸ¶ ğŸ­ _ :=
  G.Îµ'
#align category_theory.comonad.Îµ CategoryTheory.Comonad.Îµ

/-- The comultiplication for the comonad `G`. -/
def Comonad.Î´ : (G : C â¥¤ C) âŸ¶ (G : C â¥¤ C) â‹™ G :=
  G.Î´'
#align category_theory.comonad.Î´ CategoryTheory.Comonad.Î´

/-- A custom simps projection for the functor part of a monad, as a coercion. -/
def Monad.Simps.coe :=
  (T : C â¥¤ C)
#align category_theory.monad.simps.coe CategoryTheory.Monad.Simps.coe

/-- A custom simps projection for the unit of a monad, in simp normal form. -/
def Monad.Simps.Î· : ğŸ­ _ âŸ¶ (T : C â¥¤ C) :=
  T.Î·
#align category_theory.monad.simps.Î· CategoryTheory.Monad.Simps.Î·

/-- A custom simps projection for the multiplication of a monad, in simp normal form. -/
def Monad.Simps.Î¼ : (T : C â¥¤ C) â‹™ (T : C â¥¤ C) âŸ¶ (T : C â¥¤ C) :=
  T.Î¼
#align category_theory.monad.simps.Î¼ CategoryTheory.Monad.Simps.Î¼

/-- A custom simps projection for the functor part of a comonad, as a coercion. -/
def Comonad.Simps.coe :=
  (G : C â¥¤ C)
#align category_theory.comonad.simps.coe CategoryTheory.Comonad.Simps.coe

/-- A custom simps projection for the counit of a comonad, in simp normal form. -/
def Comonad.Simps.Îµ : (G : C â¥¤ C) âŸ¶ ğŸ­ _ :=
  G.Îµ
#align category_theory.comonad.simps.Îµ CategoryTheory.Comonad.Simps.Îµ

/-- A custom simps projection for the comultiplication of a comonad, in simp normal form. -/
def Comonad.Simps.Î´ : (G : C â¥¤ C) âŸ¶ (G : C â¥¤ C) â‹™ (G : C â¥¤ C) :=
  G.Î´
#align category_theory.comonad.simps.Î´ CategoryTheory.Comonad.Simps.Î´

initialize_simps_projections CategoryTheory.Monad
  (obj â†’ obj, map â†’ map, toFunctor â†’ coe, Î·' â†’ Î·, Î¼' â†’ Î¼)

initialize_simps_projections CategoryTheory.Comonad
  (obj â†’ obj, map â†’ map, toFunctor â†’ coe, Îµ' â†’ Îµ, Î´' â†’ Î´)

-- Porting note: investigate whether this can be a `simp` lemma?
@[reassoc]
theorem Monad.assoc (T : Monad C) (X : C) :
    (T : C â¥¤ C).map (T.Î¼.app X) â‰« T.Î¼.app _ = T.Î¼.app _ â‰« T.Î¼.app _ :=
  T.assoc' X
#align category_theory.monad.assoc CategoryTheory.Monad.assoc

@[reassoc (attr := simp)]
theorem Monad.left_unit (T : Monad C) (X : C) :
    T.Î·.app ((T : C â¥¤ C).obj X) â‰« T.Î¼.app X = ğŸ™ ((T : C â¥¤ C).obj X) :=
  T.left_unit' X
#align category_theory.monad.left_unit CategoryTheory.Monad.left_unit

@[reassoc (attr := simp)]
theorem Monad.right_unit (T : Monad C) (X : C) :
    (T : C â¥¤ C).map (T.Î·.app X) â‰« T.Î¼.app X = ğŸ™ ((T : C â¥¤ C).obj X) :=
  T.right_unit' X
#align category_theory.monad.right_unit CategoryTheory.Monad.right_unit

@[reassoc (attr := simp)]
theorem Comonad.coassoc (G : Comonad C) (X : C) :
    G.Î´.app _ â‰« (G : C â¥¤ C).map (G.Î´.app X) = G.Î´.app _ â‰« G.Î´.app _ :=
  G.coassoc' X
#align category_theory.comonad.coassoc CategoryTheory.Comonad.coassoc

@[reassoc (attr := simp)]
theorem Comonad.left_counit (G : Comonad C) (X : C) :
    G.Î´.app X â‰« G.Îµ.app ((G : C â¥¤ C).obj X) = ğŸ™ ((G : C â¥¤ C).obj X) :=
  G.left_counit' X
#align category_theory.comonad.left_counit CategoryTheory.Comonad.left_counit

@[reassoc (attr := simp)]
theorem Comonad.right_counit (G : Comonad C) (X : C) :
    G.Î´.app X â‰« (G : C â¥¤ C).map (G.Îµ.app X) = ğŸ™ ((G : C â¥¤ C).obj X) :=
  G.right_counit' X
#align category_theory.comonad.right_counit CategoryTheory.Comonad.right_counit

/-- A morphism of monads is a natural transformation compatible with Î· and Î¼. -/
@[ext]
structure MonadHom (Tâ‚ Tâ‚‚ : Monad C) extends NatTrans (Tâ‚ : C â¥¤ C) Tâ‚‚ where
  app_Î· : âˆ€ X, Tâ‚.Î·.app X â‰« app X = Tâ‚‚.Î·.app X := by aesop_cat
  app_Î¼ : âˆ€ X, Tâ‚.Î¼.app X â‰« app X = (Tâ‚.map (app X) â‰« app _) â‰« Tâ‚‚.Î¼.app X := by
    aesop_cat
#align category_theory.monad_hom CategoryTheory.MonadHom

initialize_simps_projections MonadHom (+toNatTrans, -app)

/-- A morphism of comonads is a natural transformation compatible with Îµ and Î´. -/
@[ext]
structure ComonadHom (M N : Comonad C) extends NatTrans (M : C â¥¤ C) N where
  app_Îµ : âˆ€ X, app X â‰« N.Îµ.app X = M.Îµ.app X := by aesop_cat
  app_Î´ : âˆ€ X, app X â‰« N.Î´.app X = M.Î´.app X â‰« app _ â‰« N.map (app X) := by aesop_cat
#align category_theory.comonad_hom CategoryTheory.ComonadHom

initialize_simps_projections ComonadHom (+toNatTrans, -app)

attribute [reassoc (attr := simp)] MonadHom.app_Î· MonadHom.app_Î¼
attribute [reassoc (attr := simp)] ComonadHom.app_Îµ ComonadHom.app_Î´

instance : Quiver (Monad C) where
  Hom := MonadHom

instance : Quiver (Comonad C) where
  Hom := ComonadHom

-- Porting note (#10688): added to ease automation
@[ext]
lemma MonadHom.ext' {Tâ‚ Tâ‚‚ : Monad C} (f g : Tâ‚ âŸ¶ Tâ‚‚) (h : f.app = g.app) : f = g :=
  MonadHom.ext f g h

-- Porting note (#10688): added to ease automation
@[ext]
lemma ComonadHom.ext' {Tâ‚ Tâ‚‚ : Comonad C} (f g : Tâ‚ âŸ¶ Tâ‚‚) (h : f.app = g.app) : f = g :=
  ComonadHom.ext f g h

instance : Category (Monad C) where
  id M := { toNatTrans := ğŸ™ (M : C â¥¤ C) }
  comp f g :=
    { toNatTrans :=
        { app := fun X => f.app X â‰« g.app X
          naturality := fun X Y h => by rw [assoc, f.1.naturality_assoc, g.1.naturality] } }
  -- `aesop_cat` can fill in these proofs, but is unfortunately slightly slow.
  id_comp _ := MonadHom.ext _ _ (by funext; simp only [NatTrans.id_app, id_comp])
  comp_id _ := MonadHom.ext _ _ (by funext; simp only [NatTrans.id_app, comp_id])
  assoc _ _ _ := MonadHom.ext _ _ (by funext; simp only [assoc])

instance : Category (Comonad C) where
  id M := { toNatTrans := ğŸ™ (M : C â¥¤ C) }
  comp f g :=
    { toNatTrans :=
        { app := fun X => f.app X â‰« g.app X
          naturality := fun X Y h => by rw [assoc, f.1.naturality_assoc, g.1.naturality] } }
  -- `aesop_cat` can fill in these proofs, but is unfortunately slightly slow.
  id_comp _ := ComonadHom.ext _ _ (by funext; simp only [NatTrans.id_app, id_comp])
  comp_id _ := ComonadHom.ext _ _ (by funext; simp only [NatTrans.id_app, comp_id])
  assoc _ _ _ := ComonadHom.ext _ _ (by funext; simp only [assoc])

instance {T : Monad C} : Inhabited (MonadHom T T) :=
  âŸ¨ğŸ™ TâŸ©

@[simp]
theorem MonadHom.id_toNatTrans (T : Monad C) : (ğŸ™ T : T âŸ¶ T).toNatTrans = ğŸ™ (T : C â¥¤ C) :=
  rfl
#align category_theory.monad_hom.id_to_nat_trans CategoryTheory.MonadHom.id_toNatTrans

@[simp]
theorem MonadHom.comp_toNatTrans {Tâ‚ Tâ‚‚ Tâ‚ƒ : Monad C} (f : Tâ‚ âŸ¶ Tâ‚‚) (g : Tâ‚‚ âŸ¶ Tâ‚ƒ) :
    (f â‰« g).toNatTrans = ((f.toNatTrans : _ âŸ¶ (Tâ‚‚ : C â¥¤ C)) â‰« g.toNatTrans : (Tâ‚ : C â¥¤ C) âŸ¶ Tâ‚ƒ) :=
  rfl
#align category_theory.monad_hom.comp_to_nat_trans CategoryTheory.MonadHom.comp_toNatTrans

instance {G : Comonad C} : Inhabited (ComonadHom G G) :=
  âŸ¨ğŸ™ GâŸ©

@[simp]
theorem ComonadHom.id_toNatTrans (T : Comonad C) : (ğŸ™ T : T âŸ¶ T).toNatTrans = ğŸ™ (T : C â¥¤ C) :=
  rfl
#align category_theory.comonad_hom.id_to_nat_trans CategoryTheory.ComonadHom.id_toNatTrans

@[simp]
theorem comp_toNatTrans {Tâ‚ Tâ‚‚ Tâ‚ƒ : Comonad C} (f : Tâ‚ âŸ¶ Tâ‚‚) (g : Tâ‚‚ âŸ¶ Tâ‚ƒ) :
    (f â‰« g).toNatTrans = ((f.toNatTrans : _ âŸ¶ (Tâ‚‚ : C â¥¤ C)) â‰« g.toNatTrans : (Tâ‚ : C â¥¤ C) âŸ¶ Tâ‚ƒ) :=
  rfl
#align category_theory.comp_to_nat_trans CategoryTheory.comp_toNatTrans

/-- Construct a monad isomorphism from a natural isomorphism of functors where the forward
direction is a monad morphism. -/
@[simps]
def MonadIso.mk {M N : Monad C} (f : (M : C â¥¤ C) â‰… N)
    (f_Î· : âˆ€ (X : C), M.Î·.app X â‰« f.hom.app X = N.Î·.app X := by aesop_cat)
    (f_Î¼ : âˆ€ (X : C), M.Î¼.app X â‰« f.hom.app X =
    (M.map (f.hom.app X) â‰« f.hom.app (N.obj X)) â‰« N.Î¼.app X := by aesop_cat) : M â‰… N where
  hom :=
    { toNatTrans := f.hom
      app_Î· := f_Î·
      app_Î¼ := f_Î¼ }
  inv :=
    { toNatTrans := f.inv
      app_Î· := fun X => by simp [â† f_Î·]
      app_Î¼ := fun X => by
        rw [â† NatIso.cancel_natIso_hom_right f]
        simp only [NatTrans.naturality, Iso.inv_hom_id_app, assoc, comp_id, f_Î¼,
          NatTrans.naturality_assoc, Iso.inv_hom_id_app_assoc, â† Functor.map_comp_assoc]
        simp }
#align category_theory.monad_iso.mk CategoryTheory.MonadIso.mk

/-- Construct a comonad isomorphism from a natural isomorphism of functors where the forward
direction is a comonad morphism. -/
@[simps]
def ComonadIso.mk {M N : Comonad C} (f : (M : C â¥¤ C) â‰… N)
    (f_Îµ : âˆ€ (X : C), f.hom.app X â‰« N.Îµ.app X = M.Îµ.app X := by aesop_cat)
    (f_Î´ : âˆ€ (X : C), f.hom.app X â‰« N.Î´.app X =
    M.Î´.app X â‰« f.hom.app (M.obj X) â‰« N.map (f.hom.app X) := by aesop_cat) : M â‰… N where
  hom :=
    { toNatTrans := f.hom
      app_Îµ := f_Îµ
      app_Î´ := f_Î´ }
  inv :=
    { toNatTrans := f.inv
      app_Îµ := fun X => by simp [â† f_Îµ]
      app_Î´ := fun X => by
        rw [â† NatIso.cancel_natIso_hom_left f]
        simp only [reassoc_of% (f_Î´ X), Iso.hom_inv_id_app_assoc, NatTrans.naturality_assoc]
        rw [â† Functor.map_comp, Iso.hom_inv_id_app, Functor.map_id]
        apply (comp_id _).symm }
#align category_theory.comonad_iso.mk CategoryTheory.ComonadIso.mk

variable (C)

/-- The forgetful functor from the category of monads to the category of endofunctors.
-/
@[simps!]
def monadToFunctor : Monad C â¥¤ C â¥¤ C where
  obj T := T
  map f := f.toNatTrans
#align category_theory.monad_to_functor CategoryTheory.monadToFunctor

instance : (monadToFunctor C).Faithful where

theorem monadToFunctor_mapIso_monad_iso_mk {M N : Monad C} (f : (M : C â¥¤ C) â‰… N) (f_Î· f_Î¼) :
    (monadToFunctor _).mapIso (MonadIso.mk f f_Î· f_Î¼) = f := by
  ext
  rfl
#align category_theory.monad_to_functor_map_iso_monad_iso_mk CategoryTheory.monadToFunctor_mapIso_monad_iso_mk

instance : (monadToFunctor C).ReflectsIsomorphisms where
  reflects f _ := (MonadIso.mk (asIso ((monadToFunctor C).map f)) f.app_Î· f.app_Î¼).isIso_hom

/-- The forgetful functor from the category of comonads to the category of endofunctors.
-/
@[simps!]
def comonadToFunctor : Comonad C â¥¤ C â¥¤ C where
  obj G := G
  map f := f.toNatTrans
#align category_theory.comonad_to_functor CategoryTheory.comonadToFunctor

instance : (comonadToFunctor C).Faithful where

theorem comonadToFunctor_mapIso_comonad_iso_mk {M N : Comonad C} (f : (M : C â¥¤ C) â‰… N) (f_Îµ f_Î´) :
    (comonadToFunctor _).mapIso (ComonadIso.mk f f_Îµ f_Î´) = f := by
  ext
  rfl
#align category_theory.comonad_to_functor_map_iso_comonad_iso_mk CategoryTheory.comonadToFunctor_mapIso_comonad_iso_mk

instance : (comonadToFunctor C).ReflectsIsomorphisms where
  reflects f _ := (ComonadIso.mk (asIso ((comonadToFunctor C).map f)) f.app_Îµ f.app_Î´).isIso_hom

variable {C}

/-- An isomorphism of monads gives a natural isomorphism of the underlying functors.
-/
/- Porting note: removed
`@[simps (config := { rhsMd := semireducible })]`
and replaced with `@[simps]` in the two declarations below-/
@[simps!]
def MonadIso.toNatIso {M N : Monad C} (h : M â‰… N) : (M : C â¥¤ C) â‰… N :=
  (monadToFunctor C).mapIso h
#align category_theory.monad_iso.to_nat_iso CategoryTheory.MonadIso.toNatIso

/-- An isomorphism of comonads gives a natural isomorphism of the underlying functors.
-/
@[simps!]
def ComonadIso.toNatIso {M N : Comonad C} (h : M â‰… N) : (M : C â¥¤ C) â‰… N :=
  (comonadToFunctor C).mapIso h
#align category_theory.comonad_iso.to_nat_iso CategoryTheory.ComonadIso.toNatIso

variable (C)

namespace Monad

/-- The identity monad. -/
@[simps!]
def id : Monad C where
  toFunctor := ğŸ­ C
  Î·' := ğŸ™ (ğŸ­ C)
  Î¼' := ğŸ™ (ğŸ­ C)
#align category_theory.monad.id CategoryTheory.Monad.id

instance : Inhabited (Monad C) :=
  âŸ¨Monad.id CâŸ©

end Monad

namespace Comonad

/-- The identity comonad. -/
@[simps!]
def id : Comonad C where
  toFunctor := ğŸ­ _
  Îµ' := ğŸ™ (ğŸ­ C)
  Î´' := ğŸ™ (ğŸ­ C)
#align category_theory.comonad.id CategoryTheory.Comonad.id

instance : Inhabited (Comonad C) :=
  âŸ¨Comonad.id CâŸ©

end Comonad

open Iso Functor

variable {C}

namespace Monad

/-- Transport a monad structure on a functor along an isomorphism of functors. -/
def transport {F : C â¥¤ C} (T : Monad C) (i : (T : C â¥¤ C) â‰… F) : Monad C where
  toFunctor := F
  Î·' := T.Î· â‰« i.hom
  Î¼' := (i.inv â—« i.inv) â‰« T.Î¼ â‰« i.hom
  left_unit' X := by
    simp only [Functor.id_obj, NatTrans.comp_app, comp_obj, NatTrans.hcomp_app, Category.assoc,
      hom_inv_id_app_assoc]
    slice_lhs 1 2 => rw [â† T.Î·.naturality (i.inv.app X), ]
    simp
  right_unit' X := by
    simp only [id_obj, NatTrans.comp_app, Functor.map_comp, comp_obj, NatTrans.hcomp_app,
      Category.assoc, NatTrans.naturality_assoc]
    slice_lhs 2 4 =>
      simp only [â† T.map_comp]
    simp
  assoc' X := by
    simp only [comp_obj, NatTrans.comp_app, NatTrans.hcomp_app, Category.assoc, Functor.map_comp,
      NatTrans.naturality_assoc, hom_inv_id_app_assoc, NatIso.cancel_natIso_inv_left]
    slice_lhs 4 5 => rw [â† T.map_comp]
    simp only [hom_inv_id_app, Functor.map_id, id_comp]
    slice_lhs 1 2 => rw [â† T.map_comp]
    simp only [Functor.map_comp, Category.assoc]
    congr 1
    simp only [â† Category.assoc, NatIso.cancel_natIso_hom_right]
    rw [â† T.Î¼.naturality]
    simp [T.assoc X]

end Monad

namespace Comonad

/-- Transport a comonad structure on a functor along an isomorphism of functors. -/
def transport {F : C â¥¤ C} (T : Comonad C) (i : (T : C â¥¤ C) â‰… F) : Comonad C where
  toFunctor := F
  Îµ' := i.inv â‰« T.Îµ
  Î´' := i.inv â‰« T.Î´ â‰« (i.hom â—« i.hom)
  right_counit' X := by
    simp only [id_obj, comp_obj, NatTrans.comp_app, NatTrans.hcomp_app, Functor.map_comp, assoc]
    slice_lhs 4 5 => rw [â† F.map_comp]
    simp only [hom_inv_id_app, Functor.map_id, id_comp, â† i.hom.naturality]
    slice_lhs 2 3 => rw [T.right_counit]
    simp
  coassoc' X := by
    simp only [comp_obj, NatTrans.comp_app, NatTrans.hcomp_app, Functor.map_comp, assoc,
      NatTrans.naturality_assoc, Functor.comp_map, hom_inv_id_app_assoc,
      NatIso.cancel_natIso_inv_left]
    slice_lhs 3 4 => rw [â† F.map_comp]
    simp only [hom_inv_id_app, Functor.map_id, id_comp, assoc]
    rw [â† i.hom.naturality_assoc, â† T.coassoc_assoc]
    simp only [NatTrans.naturality_assoc]
    congr 3
    simp only [â† Functor.map_comp, i.hom.naturality]

end Comonad

end CategoryTheory
