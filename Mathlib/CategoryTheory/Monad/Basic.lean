/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Bhavik Mehta, Adam Topaz
-/
import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.Functor.FullyFaithful
import Mathlib.CategoryTheory.Functor.ReflectsIso

#align_import category_theory.monad.basic from "leanprover-community/mathlib"@"9c6816cab5872990d450d2c2e7832176167b1c07"

/-!
# Monads

We construct the categories of monads and comonads, and their forgetful functors to endofunctors.

(Note that these are the category theorist's monads, not the programmers monads.
For the translation, see the file `CategoryTheory.Monad.Types`.)

For the fact that monads are "just" monoids in the category of endofunctors, see the file
`CategoryTheory.Monad.EquivMon`.
-/


namespace CategoryTheory

open Category

universe v‚ÇÅ u‚ÇÅ

-- morphism levels before object levels. See note [CategoryTheory universes].
variable (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C]

/-- The data of a monad on C consists of an endofunctor T together with natural transformations
Œ∑ : ùü≠ C ‚ü∂ T and Œº : T ‚ãô T ‚ü∂ T satisfying three equations:
- T Œº_X ‚â´ Œº_X = Œº_(TX) ‚â´ Œº_X (associativity)
- Œ∑_(TX) ‚â´ Œº_X = 1_X (left unit)
- TŒ∑_X ‚â´ Œº_X = 1_X (right unit)
-/
structure Monad extends C ‚•§ C where
  Œ∑' : ùü≠ _ ‚ü∂ toFunctor
  Œº' : toFunctor ‚ãô toFunctor ‚ü∂ toFunctor
  assoc' : ‚àÄ X, toFunctor.map (NatTrans.app Œº' X) ‚â´ Œº'.app _ = Œº'.app _ ‚â´ Œº'.app _ := by aesop_cat
  left_unit' : ‚àÄ X : C, Œ∑'.app (toFunctor.obj X) ‚â´ Œº'.app _ = ùüô _ := by aesop_cat
  right_unit' : ‚àÄ X : C, toFunctor.map (Œ∑'.app X) ‚â´ Œº'.app _ = ùüô _ := by aesop_cat
#align category_theory.monad CategoryTheory.Monad

/-- The data of a comonad on C consists of an endofunctor G together with natural transformations
Œµ : G ‚ü∂ ùü≠ C and Œ¥ : G ‚ü∂ G ‚ãô G satisfying three equations:
- Œ¥_X ‚â´ G Œ¥_X = Œ¥_X ‚â´ Œ¥_(GX) (coassociativity)
- Œ¥_X ‚â´ Œµ_(GX) = 1_X (left counit)
- Œ¥_X ‚â´ G Œµ_X = 1_X (right counit)
-/
structure Comonad extends C ‚•§ C where
  Œµ' : toFunctor ‚ü∂ ùü≠ _
  Œ¥' : toFunctor ‚ü∂ toFunctor ‚ãô toFunctor
  coassoc' : ‚àÄ X, NatTrans.app Œ¥' _ ‚â´ toFunctor.map (Œ¥'.app X) = Œ¥'.app _ ‚â´ Œ¥'.app _ := by
    aesop_cat
  left_counit' : ‚àÄ X : C, Œ¥'.app X ‚â´ Œµ'.app (toFunctor.obj X) = ùüô _ := by aesop_cat
  right_counit' : ‚àÄ X : C, Œ¥'.app X ‚â´ toFunctor.map (Œµ'.app X) = ùüô _ := by aesop_cat
#align category_theory.comonad CategoryTheory.Comonad

variable {C}
variable (T : Monad C) (G : Comonad C)

instance coeMonad : Coe (Monad C) (C ‚•§ C) :=
  ‚ü®fun T => T.toFunctor‚ü©
#align category_theory.coe_monad CategoryTheory.coeMonad

instance coeComonad : Coe (Comonad C) (C ‚•§ C) :=
  ‚ü®fun G => G.toFunctor‚ü©
#align category_theory.coe_comonad CategoryTheory.coeComonad

-- Porting note: these lemmas are syntactic tautologies
--@[simp]
--theorem monad_toFunctor_eq_coe : T.toFunctor = T :=
--  rfl
--#align category_theory.monad_to_functor_eq_coe CategoryTheory.monad_toFunctor_eq_coe
--
--@[simp]
--theorem comonad_toFunctor_eq_coe : G.toFunctor = G :=
--  rfl
--#align category_theory.comonad_to_functor_eq_coe CategoryTheory.comonad_toFunctor_eq_coe

/-- The unit for the monad `T`. -/
def Monad.Œ∑ : ùü≠ _ ‚ü∂ (T : C ‚•§ C) :=
  T.Œ∑'
#align category_theory.monad.Œ∑ CategoryTheory.Monad.Œ∑

/-- The multiplication for the monad `T`. -/
def Monad.Œº : (T : C ‚•§ C) ‚ãô (T : C ‚•§ C) ‚ü∂ T :=
  T.Œº'
#align category_theory.monad.Œº CategoryTheory.Monad.Œº

/-- The counit for the comonad `G`. -/
def Comonad.Œµ : (G : C ‚•§ C) ‚ü∂ ùü≠ _ :=
  G.Œµ'
#align category_theory.comonad.Œµ CategoryTheory.Comonad.Œµ

/-- The comultiplication for the comonad `G`. -/
def Comonad.Œ¥ : (G : C ‚•§ C) ‚ü∂ (G : C ‚•§ C) ‚ãô G :=
  G.Œ¥'
#align category_theory.comonad.Œ¥ CategoryTheory.Comonad.Œ¥

/-- A custom simps projection for the functor part of a monad, as a coercion. -/
def Monad.Simps.coe :=
  (T : C ‚•§ C)
#align category_theory.monad.simps.coe CategoryTheory.Monad.Simps.coe

/-- A custom simps projection for the unit of a monad, in simp normal form. -/
def Monad.Simps.Œ∑ : ùü≠ _ ‚ü∂ (T : C ‚•§ C) :=
  T.Œ∑
#align category_theory.monad.simps.Œ∑ CategoryTheory.Monad.Simps.Œ∑

/-- A custom simps projection for the multiplication of a monad, in simp normal form. -/
def Monad.Simps.Œº : (T : C ‚•§ C) ‚ãô (T : C ‚•§ C) ‚ü∂ (T : C ‚•§ C) :=
  T.Œº
#align category_theory.monad.simps.Œº CategoryTheory.Monad.Simps.Œº

/-- A custom simps projection for the functor part of a comonad, as a coercion. -/
def Comonad.Simps.coe :=
  (G : C ‚•§ C)
#align category_theory.comonad.simps.coe CategoryTheory.Comonad.Simps.coe

/-- A custom simps projection for the counit of a comonad, in simp normal form. -/
def Comonad.Simps.Œµ : (G : C ‚•§ C) ‚ü∂ ùü≠ _ :=
  G.Œµ
#align category_theory.comonad.simps.Œµ CategoryTheory.Comonad.Simps.Œµ

/-- A custom simps projection for the comultiplication of a comonad, in simp normal form. -/
def Comonad.Simps.Œ¥ : (G : C ‚•§ C) ‚ü∂ (G : C ‚•§ C) ‚ãô (G : C ‚•§ C) :=
  G.Œ¥
#align category_theory.comonad.simps.Œ¥ CategoryTheory.Comonad.Simps.Œ¥

initialize_simps_projections CategoryTheory.Monad
  (obj ‚Üí obj, map ‚Üí map, toFunctor ‚Üí coe, Œ∑' ‚Üí Œ∑, Œº' ‚Üí Œº)

initialize_simps_projections CategoryTheory.Comonad
  (obj ‚Üí obj, map ‚Üí map, toFunctor ‚Üí coe, Œµ' ‚Üí Œµ, Œ¥' ‚Üí Œ¥)

-- Porting note: investigate whether this can be a `simp` lemma?
@[reassoc]
theorem Monad.assoc (T : Monad C) (X : C) :
    (T : C ‚•§ C).map (T.Œº.app X) ‚â´ T.Œº.app _ = T.Œº.app _ ‚â´ T.Œº.app _ :=
  T.assoc' X
#align category_theory.monad.assoc CategoryTheory.Monad.assoc

@[reassoc (attr := simp)]
theorem Monad.left_unit (T : Monad C) (X : C) :
    T.Œ∑.app ((T : C ‚•§ C).obj X) ‚â´ T.Œº.app X = ùüô ((T : C ‚•§ C).obj X) :=
  T.left_unit' X
#align category_theory.monad.left_unit CategoryTheory.Monad.left_unit

@[reassoc (attr := simp)]
theorem Monad.right_unit (T : Monad C) (X : C) :
    (T : C ‚•§ C).map (T.Œ∑.app X) ‚â´ T.Œº.app X = ùüô ((T : C ‚•§ C).obj X) :=
  T.right_unit' X
#align category_theory.monad.right_unit CategoryTheory.Monad.right_unit

@[reassoc (attr := simp)]
theorem Comonad.coassoc (G : Comonad C) (X : C) :
    G.Œ¥.app _ ‚â´ (G : C ‚•§ C).map (G.Œ¥.app X) = G.Œ¥.app _ ‚â´ G.Œ¥.app _ :=
  G.coassoc' X
#align category_theory.comonad.coassoc CategoryTheory.Comonad.coassoc

@[reassoc (attr := simp)]
theorem Comonad.left_counit (G : Comonad C) (X : C) :
    G.Œ¥.app X ‚â´ G.Œµ.app ((G : C ‚•§ C).obj X) = ùüô ((G : C ‚•§ C).obj X) :=
  G.left_counit' X
#align category_theory.comonad.left_counit CategoryTheory.Comonad.left_counit

@[reassoc (attr := simp)]
theorem Comonad.right_counit (G : Comonad C) (X : C) :
    G.Œ¥.app X ‚â´ (G : C ‚•§ C).map (G.Œµ.app X) = ùüô ((G : C ‚•§ C).obj X) :=
  G.right_counit' X
#align category_theory.comonad.right_counit CategoryTheory.Comonad.right_counit

/-- A morphism of monads is a natural transformation compatible with Œ∑ and Œº. -/
@[ext]
structure MonadHom (T‚ÇÅ T‚ÇÇ : Monad C) extends NatTrans (T‚ÇÅ : C ‚•§ C) T‚ÇÇ where
  app_Œ∑ : ‚àÄ X, T‚ÇÅ.Œ∑.app X ‚â´ app X = T‚ÇÇ.Œ∑.app X := by aesop_cat
  app_Œº : ‚àÄ X, T‚ÇÅ.Œº.app X ‚â´ app X = (T‚ÇÅ.map (app X) ‚â´ app _) ‚â´ T‚ÇÇ.Œº.app X := by
    aesop_cat
#align category_theory.monad_hom CategoryTheory.MonadHom

initialize_simps_projections MonadHom (+toNatTrans, -app)

/-- A morphism of comonads is a natural transformation compatible with Œµ and Œ¥. -/
@[ext]
structure ComonadHom (M N : Comonad C) extends NatTrans (M : C ‚•§ C) N where
  app_Œµ : ‚àÄ X, app X ‚â´ N.Œµ.app X = M.Œµ.app X := by aesop_cat
  app_Œ¥ : ‚àÄ X, app X ‚â´ N.Œ¥.app X = M.Œ¥.app X ‚â´ app _ ‚â´ N.map (app X) := by aesop_cat
#align category_theory.comonad_hom CategoryTheory.ComonadHom

initialize_simps_projections ComonadHom (+toNatTrans, -app)

attribute [reassoc (attr := simp)] MonadHom.app_Œ∑ MonadHom.app_Œº
attribute [reassoc (attr := simp)] ComonadHom.app_Œµ ComonadHom.app_Œ¥

instance : Quiver (Monad C) where
  Hom := MonadHom

instance : Quiver (Comonad C) where
  Hom := ComonadHom

-- Porting note (#10688): added to ease automation
@[ext]
lemma MonadHom.ext' {T‚ÇÅ T‚ÇÇ : Monad C} (f g : T‚ÇÅ ‚ü∂ T‚ÇÇ) (h : f.app = g.app) : f = g :=
  MonadHom.ext f g h

-- Porting note (#10688): added to ease automation
@[ext]
lemma ComonadHom.ext' {T‚ÇÅ T‚ÇÇ : Comonad C} (f g : T‚ÇÅ ‚ü∂ T‚ÇÇ) (h : f.app = g.app) : f = g :=
  ComonadHom.ext f g h

instance : Category (Monad C) where
  id M := { toNatTrans := ùüô (M : C ‚•§ C) }
  comp f g :=
    { toNatTrans :=
        { app := fun X => f.app X ‚â´ g.app X
          naturality := fun X Y h => by rw [assoc, f.1.naturality_assoc, g.1.naturality] } }
  -- `aesop_cat` can fill in these proofs, but is unfortunately slightly slow.
  id_comp _ := MonadHom.ext _ _ (by funext; simp only [NatTrans.id_app, id_comp])
  comp_id _ := MonadHom.ext _ _ (by funext; simp only [NatTrans.id_app, comp_id])
  assoc _ _ _ := MonadHom.ext _ _ (by funext; simp only [assoc])

instance : Category (Comonad C) where
  id M := { toNatTrans := ùüô (M : C ‚•§ C) }
  comp f g :=
    { toNatTrans :=
        { app := fun X => f.app X ‚â´ g.app X
          naturality := fun X Y h => by rw [assoc, f.1.naturality_assoc, g.1.naturality] } }
  -- `aesop_cat` can fill in these proofs, but is unfortunately slightly slow.
  id_comp _ := ComonadHom.ext _ _ (by funext; simp only [NatTrans.id_app, id_comp])
  comp_id _ := ComonadHom.ext _ _ (by funext; simp only [NatTrans.id_app, comp_id])
  assoc _ _ _ := ComonadHom.ext _ _ (by funext; simp only [assoc])

instance {T : Monad C} : Inhabited (MonadHom T T) :=
  ‚ü®ùüô T‚ü©

@[simp]
theorem MonadHom.id_toNatTrans (T : Monad C) : (ùüô T : T ‚ü∂ T).toNatTrans = ùüô (T : C ‚•§ C) :=
  rfl
#align category_theory.monad_hom.id_to_nat_trans CategoryTheory.MonadHom.id_toNatTrans

@[simp]
theorem MonadHom.comp_toNatTrans {T‚ÇÅ T‚ÇÇ T‚ÇÉ : Monad C} (f : T‚ÇÅ ‚ü∂ T‚ÇÇ) (g : T‚ÇÇ ‚ü∂ T‚ÇÉ) :
    (f ‚â´ g).toNatTrans = ((f.toNatTrans : _ ‚ü∂ (T‚ÇÇ : C ‚•§ C)) ‚â´ g.toNatTrans : (T‚ÇÅ : C ‚•§ C) ‚ü∂ T‚ÇÉ) :=
  rfl
#align category_theory.monad_hom.comp_to_nat_trans CategoryTheory.MonadHom.comp_toNatTrans

instance {G : Comonad C} : Inhabited (ComonadHom G G) :=
  ‚ü®ùüô G‚ü©

@[simp]
theorem ComonadHom.id_toNatTrans (T : Comonad C) : (ùüô T : T ‚ü∂ T).toNatTrans = ùüô (T : C ‚•§ C) :=
  rfl
#align category_theory.comonad_hom.id_to_nat_trans CategoryTheory.ComonadHom.id_toNatTrans

@[simp]
theorem comp_toNatTrans {T‚ÇÅ T‚ÇÇ T‚ÇÉ : Comonad C} (f : T‚ÇÅ ‚ü∂ T‚ÇÇ) (g : T‚ÇÇ ‚ü∂ T‚ÇÉ) :
    (f ‚â´ g).toNatTrans = ((f.toNatTrans : _ ‚ü∂ (T‚ÇÇ : C ‚•§ C)) ‚â´ g.toNatTrans : (T‚ÇÅ : C ‚•§ C) ‚ü∂ T‚ÇÉ) :=
  rfl
#align category_theory.comp_to_nat_trans CategoryTheory.comp_toNatTrans

/-- Construct a monad isomorphism from a natural isomorphism of functors where the forward
direction is a monad morphism. -/
@[simps]
def MonadIso.mk {M N : Monad C} (f : (M : C ‚•§ C) ‚âÖ N)
    (f_Œ∑ : ‚àÄ (X : C), M.Œ∑.app X ‚â´ f.hom.app X = N.Œ∑.app X := by aesop_cat)
    (f_Œº : ‚àÄ (X : C), M.Œº.app X ‚â´ f.hom.app X =
    (M.map (f.hom.app X) ‚â´ f.hom.app (N.obj X)) ‚â´ N.Œº.app X := by aesop_cat) : M ‚âÖ N where
  hom :=
    { toNatTrans := f.hom
      app_Œ∑ := f_Œ∑
      app_Œº := f_Œº }
  inv :=
    { toNatTrans := f.inv
      app_Œ∑ := fun X => by simp [‚Üê f_Œ∑]
      app_Œº := fun X => by
        rw [‚Üê NatIso.cancel_natIso_hom_right f]
        simp only [NatTrans.naturality, Iso.inv_hom_id_app, assoc, comp_id, f_Œº,
          NatTrans.naturality_assoc, Iso.inv_hom_id_app_assoc, ‚Üê Functor.map_comp_assoc]
        simp }
#align category_theory.monad_iso.mk CategoryTheory.MonadIso.mk

/-- Construct a comonad isomorphism from a natural isomorphism of functors where the forward
direction is a comonad morphism. -/
@[simps]
def ComonadIso.mk {M N : Comonad C} (f : (M : C ‚•§ C) ‚âÖ N)
    (f_Œµ : ‚àÄ (X : C), f.hom.app X ‚â´ N.Œµ.app X = M.Œµ.app X := by aesop_cat)
    (f_Œ¥ : ‚àÄ (X : C), f.hom.app X ‚â´ N.Œ¥.app X =
    M.Œ¥.app X ‚â´ f.hom.app (M.obj X) ‚â´ N.map (f.hom.app X) := by aesop_cat) : M ‚âÖ N where
  hom :=
    { toNatTrans := f.hom
      app_Œµ := f_Œµ
      app_Œ¥ := f_Œ¥ }
  inv :=
    { toNatTrans := f.inv
      app_Œµ := fun X => by simp [‚Üê f_Œµ]
      app_Œ¥ := fun X => by
        rw [‚Üê NatIso.cancel_natIso_hom_left f]
        simp only [reassoc_of% (f_Œ¥ X), Iso.hom_inv_id_app_assoc, NatTrans.naturality_assoc]
        rw [‚Üê Functor.map_comp, Iso.hom_inv_id_app, Functor.map_id]
        apply (comp_id _).symm }
#align category_theory.comonad_iso.mk CategoryTheory.ComonadIso.mk

variable (C)

/-- The forgetful functor from the category of monads to the category of endofunctors.
-/
@[simps!]
def monadToFunctor : Monad C ‚•§ C ‚•§ C where
  obj T := T
  map f := f.toNatTrans
#align category_theory.monad_to_functor CategoryTheory.monadToFunctor

instance : (monadToFunctor C).Faithful where

theorem monadToFunctor_mapIso_monad_iso_mk {M N : Monad C} (f : (M : C ‚•§ C) ‚âÖ N) (f_Œ∑ f_Œº) :
    (monadToFunctor _).mapIso (MonadIso.mk f f_Œ∑ f_Œº) = f := by
  ext
  rfl
#align category_theory.monad_to_functor_map_iso_monad_iso_mk CategoryTheory.monadToFunctor_mapIso_monad_iso_mk

instance : (monadToFunctor C).ReflectsIsomorphisms where
  reflects f _ := (MonadIso.mk (asIso ((monadToFunctor C).map f)) f.app_Œ∑ f.app_Œº).isIso_hom

/-- The forgetful functor from the category of comonads to the category of endofunctors.
-/
@[simps!]
def comonadToFunctor : Comonad C ‚•§ C ‚•§ C where
  obj G := G
  map f := f.toNatTrans
#align category_theory.comonad_to_functor CategoryTheory.comonadToFunctor

instance : (comonadToFunctor C).Faithful where

theorem comonadToFunctor_mapIso_comonad_iso_mk {M N : Comonad C} (f : (M : C ‚•§ C) ‚âÖ N) (f_Œµ f_Œ¥) :
    (comonadToFunctor _).mapIso (ComonadIso.mk f f_Œµ f_Œ¥) = f := by
  ext
  rfl
#align category_theory.comonad_to_functor_map_iso_comonad_iso_mk CategoryTheory.comonadToFunctor_mapIso_comonad_iso_mk

instance : (comonadToFunctor C).ReflectsIsomorphisms where
  reflects f _ := (ComonadIso.mk (asIso ((comonadToFunctor C).map f)) f.app_Œµ f.app_Œ¥).isIso_hom

variable {C}

/-- An isomorphism of monads gives a natural isomorphism of the underlying functors.
-/
/- Porting note: removed
`@[simps (config := { rhsMd := semireducible })]`
and replaced with `@[simps]` in the two declarations below-/
@[simps!]
def MonadIso.toNatIso {M N : Monad C} (h : M ‚âÖ N) : (M : C ‚•§ C) ‚âÖ N :=
  (monadToFunctor C).mapIso h
#align category_theory.monad_iso.to_nat_iso CategoryTheory.MonadIso.toNatIso

/-- An isomorphism of comonads gives a natural isomorphism of the underlying functors.
-/
@[simps!]
def ComonadIso.toNatIso {M N : Comonad C} (h : M ‚âÖ N) : (M : C ‚•§ C) ‚âÖ N :=
  (comonadToFunctor C).mapIso h
#align category_theory.comonad_iso.to_nat_iso CategoryTheory.ComonadIso.toNatIso

variable (C)

namespace Monad

/-- The identity monad. -/
@[simps!]
def id : Monad C where
  toFunctor := ùü≠ C
  Œ∑' := ùüô (ùü≠ C)
  Œº' := ùüô (ùü≠ C)
#align category_theory.monad.id CategoryTheory.Monad.id

instance : Inhabited (Monad C) :=
  ‚ü®Monad.id C‚ü©

end Monad

namespace Comonad

/-- The identity comonad. -/
@[simps!]
def id : Comonad C where
  toFunctor := ùü≠ _
  Œµ' := ùüô (ùü≠ C)
  Œ¥' := ùüô (ùü≠ C)
#align category_theory.comonad.id CategoryTheory.Comonad.id

instance : Inhabited (Comonad C) :=
  ‚ü®Comonad.id C‚ü©

end Comonad

end CategoryTheory
