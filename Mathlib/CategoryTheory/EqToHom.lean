/-
Copyright (c) 2018 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Reid Barton, Kim Morrison
-/
import Mathlib.CategoryTheory.Opposites

/-!
# Morphisms from equations between objects.

When working categorically, sometimes one encounters an equation `h : X = Y` between objects.

Your initial aversion to this is natural and appropriate:
you're in for some trouble, and if there is another way to approach the problem that won't
rely on this equality, it may be worth pursuing.

You have two options:
1. Use the equality `h` as one normally would in Lean (e.g. using `rw` and `subst`).
   This may immediately cause difficulties, because in category theory everything is dependently
   typed, and equations between objects quickly lead to nasty goals with `eq.rec`.
2. Promote `h` to a morphism using `eqToHom h : X ‚ü∂ Y`, or `eqToIso h : X ‚âÖ Y`.

This file introduces various `simp` lemmas which in favourable circumstances
result in the various `eqToHom` morphisms to drop out at the appropriate moment!
-/

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

-- morphism levels before object levels. See note [CategoryTheory universes].
namespace CategoryTheory

open Opposite

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]

/-- An equality `X = Y` gives us a morphism `X ‚ü∂ Y`.

It is typically better to use this, rather than rewriting by the equality then using `ùüô _`
which usually leads to dependent type theory hell.
-/
def eqToHom {X Y : C} (p : X = Y) : X ‚ü∂ Y := by rw [p]; exact ùüô _

@[simp]
theorem eqToHom_refl (X : C) (p : X = X) : eqToHom p = ùüô X :=
  rfl

@[reassoc (attr := simp)]
theorem eqToHom_trans {X Y Z : C} (p : X = Y) (q : Y = Z) :
    eqToHom p ‚â´ eqToHom q = eqToHom (p.trans q) := by
  cases p
  cases q
  simp

/-- `eqToHom h` is heterogeneously equal to the identity of its domain. -/
lemma eqToHom_heq_id_dom (X Y : C) (h : X = Y) : eqToHom h ‚âç ùüô X := by
  subst h; rfl

/-- `eqToHom h` is heterogeneously equal to the identity of its codomain. -/
lemma eqToHom_heq_id_cod (X Y : C) (h : X = Y) : eqToHom h ‚âç ùüô Y := by
  subst h; rfl

/-- Two morphisms are conjugate via eqToHom if and only if they are heterogeneously equal.
Note this used to be in the Functor namespace, where it doesn't belong. -/
theorem conj_eqToHom_iff_heq {W X Y Z : C} (f : W ‚ü∂ X) (g : Y ‚ü∂ Z) (h : W = Y) (h' : X = Z) :
    f = eqToHom h ‚â´ g ‚â´ eqToHom h'.symm ‚Üî f ‚âç g := by
  cases h
  cases h'
  simp

theorem conj_eqToHom_iff_heq' {C} [Category C] {W X Y Z : C}
    (f : W ‚ü∂ X) (g : Y ‚ü∂ Z) (h : W = Y) (h' : Z = X) :
    f = eqToHom h ‚â´ g ‚â´ eqToHom h' ‚Üî f ‚âç g := conj_eqToHom_iff_heq _ _ _ h'.symm

theorem comp_eqToHom_iff {X Y Y' : C} (p : Y = Y') (f : X ‚ü∂ Y) (g : X ‚ü∂ Y') :
    f ‚â´ eqToHom p = g ‚Üî f = g ‚â´ eqToHom p.symm :=
  { mp := fun h => h ‚ñ∏ by simp
    mpr := fun h => by simp [eq_whisker h (eqToHom p)] }

theorem eqToHom_comp_iff {X X' Y : C} (p : X = X') (f : X ‚ü∂ Y) (g : X' ‚ü∂ Y) :
    eqToHom p ‚â´ g = f ‚Üî g = eqToHom p.symm ‚â´ f :=
  { mp := fun h => h ‚ñ∏ by simp
    mpr := fun h => h ‚ñ∏ by simp }

theorem eqToHom_comp_heq {C} [Category C] {W X Y : C}
    (f : Y ‚ü∂ X) (h : W = Y) : eqToHom h ‚â´ f ‚âç f := by
  rw [‚Üê conj_eqToHom_iff_heq _ _ h rfl, eqToHom_refl, Category.comp_id]

@[simp] theorem eqToHom_comp_heq_iff {C} [Category C] {W X Y Z Z' : C}
    (f : Y ‚ü∂ X) (g : Z ‚ü∂ Z') (h : W = Y) :
    eqToHom h ‚â´ f ‚âç g ‚Üî f ‚âç g :=
  ‚ü®(eqToHom_comp_heq ..).symm.trans, (eqToHom_comp_heq ..).trans‚ü©

@[simp] theorem heq_eqToHom_comp_iff {C} [Category C] {W X Y Z Z' : C}
    (f : Y ‚ü∂ X) (g : Z ‚ü∂ Z') (h : W = Y) :
    g ‚âç eqToHom h ‚â´ f ‚Üî g ‚âç f :=
  ‚ü®(¬∑.trans (eqToHom_comp_heq ..)), (¬∑.trans (eqToHom_comp_heq ..).symm)‚ü©

theorem comp_eqToHom_heq {C} [Category C] {X Y Z : C}
    (f : X ‚ü∂ Y) (h : Y = Z) : f ‚â´ eqToHom h ‚âç f := by
  rw [‚Üê conj_eqToHom_iff_heq' _ _ rfl h, eqToHom_refl, Category.id_comp]

@[simp] theorem comp_eqToHom_heq_iff {C} [Category C] {W X Y Z Z' : C}
    (f : X ‚ü∂ Y) (g : Z ‚ü∂ Z') (h : Y = W) :
    f ‚â´ eqToHom h ‚âç g ‚Üî f ‚âç g :=
  ‚ü®(comp_eqToHom_heq ..).symm.trans, (comp_eqToHom_heq ..).trans‚ü©

@[simp] theorem heq_comp_eqToHom_iff {C} [Category C] {W X Y Z Z' : C}
    (f : X ‚ü∂ Y) (g : Z ‚ü∂ Z') (h : Y = W) :
    g ‚âç f ‚â´ eqToHom h ‚Üî g ‚âç f :=
  ‚ü®(¬∑.trans (comp_eqToHom_heq ..)), (¬∑.trans (comp_eqToHom_heq ..).symm)‚ü©

theorem heq_comp {C} [Category C] {X Y Z X' Y' Z' : C}
    {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z} {f' : X' ‚ü∂ Y'} {g' : Y' ‚ü∂ Z'}
    (eq1 : X = X') (eq2 : Y = Y') (eq3 : Z = Z')
    (H1 : f ‚âç f') (H2 : g ‚âç g') :
    f ‚â´ g ‚âç f' ‚â´ g' := by
  grind

variable {Œ≤ : Sort*}

/-- We can push `eqToHom` to the left through families of morphisms. -/
@[reassoc (attr := simp)]
theorem eqToHom_naturality {f g : Œ≤ ‚Üí C} (z : ‚àÄ b, f b ‚ü∂ g b) {j j' : Œ≤} (w : j = j') :
    z j ‚â´ eqToHom (by simp [w]) = eqToHom (by simp [w]) ‚â´ z j' := by
  cases w
  simp

/-- A variant on `eqToHom_naturality` that helps Lean identify the families `f` and `g`. -/
@[reassoc (attr := simp)]
theorem eqToHom_iso_hom_naturality {f g : Œ≤ ‚Üí C} (z : ‚àÄ b, f b ‚âÖ g b) {j j' : Œ≤} (w : j = j') :
    (z j).hom ‚â´ eqToHom (by simp [w]) = eqToHom (by simp [w]) ‚â´ (z j').hom := by
  cases w
  simp

/-- A variant on `eqToHom_naturality` that helps Lean identify the families `f` and `g`. -/
@[reassoc (attr := simp)]
theorem eqToHom_iso_inv_naturality {f g : Œ≤ ‚Üí C} (z : ‚àÄ b, f b ‚âÖ g b) {j j' : Œ≤} (w : j = j') :
    (z j).inv ‚â´ eqToHom (by simp [w]) = eqToHom (by simp [w]) ‚â´ (z j').inv := by
  cases w
  simp

/-- Reducible form of congrArg_mpr_hom_left -/
@[simp]
theorem congrArg_cast_hom_left {X Y Z : C} (p : X = Y) (q : Y ‚ü∂ Z) :
    cast (congrArg (fun W : C => W ‚ü∂ Z) p.symm) q = eqToHom p ‚â´ q := by
  cases p
  simp

/-- If we (perhaps unintentionally) perform equational rewriting on
the source object of a morphism,
we can replace the resulting `_.mpr f` term by a composition with an `eqToHom`.

It may be advisable to introduce any necessary `eqToHom` morphisms manually,
rather than relying on this lemma firing.
-/
theorem congrArg_mpr_hom_left {X Y Z : C} (p : X = Y) (q : Y ‚ü∂ Z) :
    (congrArg (fun W : C => W ‚ü∂ Z) p).mpr q = eqToHom p ‚â´ q := by
  cases p
  simp

/-- Reducible form of `congrArg_mpr_hom_right` -/
@[simp]
theorem congrArg_cast_hom_right {X Y Z : C} (p : X ‚ü∂ Y) (q : Z = Y) :
    cast (congrArg (fun W : C => X ‚ü∂ W) q.symm) p = p ‚â´ eqToHom q.symm := by
  cases q
  simp

/-- If we (perhaps unintentionally) perform equational rewriting on
the target object of a morphism,
we can replace the resulting `_.mpr f` term by a composition with an `eqToHom`.

It may be advisable to introduce any necessary `eqToHom` morphisms manually,
rather than relying on this lemma firing.
-/
theorem congrArg_mpr_hom_right {X Y Z : C} (p : X ‚ü∂ Y) (q : Z = Y) :
    (congrArg (fun W : C => X ‚ü∂ W) q).mpr p = p ‚â´ eqToHom q.symm := by
  cases q
  simp

/-- An equality `X = Y` gives us an isomorphism `X ‚âÖ Y`.

It is typically better to use this, rather than rewriting by the equality then using `Iso.refl _`
which usually leads to dependent type theory hell.
-/
def eqToIso {X Y : C} (p : X = Y) : X ‚âÖ Y :=
  ‚ü®eqToHom p, eqToHom p.symm, by simp, by simp‚ü©

@[simp]
theorem eqToIso.hom {X Y : C} (p : X = Y) : (eqToIso p).hom = eqToHom p :=
  rfl

@[simp]
theorem eqToIso.inv {X Y : C} (p : X = Y) : (eqToIso p).inv = eqToHom p.symm :=
  rfl

@[simp]
theorem eqToIso_refl {X : C} (p : X = X) : eqToIso p = Iso.refl X :=
  rfl

@[simp]
theorem eqToIso_trans {X Y Z : C} (p : X = Y) (q : Y = Z) :
    eqToIso p ‚â™‚â´ eqToIso q = eqToIso (p.trans q) := by ext; simp

@[simp]
theorem eqToHom_op {X Y : C} (h : X = Y) : (eqToHom h).op = eqToHom (congr_arg op h.symm) := by
  cases h
  rfl

@[simp]
theorem eqToHom_unop {X Y : C·µí·µñ} (h : X = Y) :
    (eqToHom h).unop = eqToHom (congr_arg unop h.symm) := by
  cases h
  rfl

instance {X Y : C} (h : X = Y) : IsIso (eqToHom h) :=
  (eqToIso h).isIso_hom

@[simp]
theorem inv_eqToHom {X Y : C} (h : X = Y) : inv (eqToHom h) = eqToHom h.symm := by
  cat_disch

variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]

namespace Functor

/-- Proving equality between functors. This isn't an extensionality lemma,
  because usually you don't really want to do this. -/
theorem ext {F G : C ‚•§ D} (h_obj : ‚àÄ X, F.obj X = G.obj X)
    (h_map : ‚àÄ X Y f,
      F.map f = eqToHom (h_obj X) ‚â´ G.map f ‚â´ eqToHom (h_obj Y).symm := by cat_disch) :
    F = G := by
  match F, G with
  | mk F_obj _ _ _, mk G_obj _ _ _ =>
    obtain rfl : F_obj = G_obj := by
      ext X
      apply h_obj
    congr
    funext X Y f
    simpa using h_map X Y f

lemma ext_of_iso {F G : C ‚•§ D} (e : F ‚âÖ G) (hobj : ‚àÄ X, F.obj X = G.obj X)
    (happ : ‚àÄ X, e.hom.app X = eqToHom (hobj X) := by cat_disch) : F = G :=
  Functor.ext hobj (fun X Y f => by
    rw [‚Üê cancel_mono (e.hom.app Y), e.hom.naturality f, happ, happ, Category.assoc,
    Category.assoc, eqToHom_trans, eqToHom_refl, Category.comp_id])

/-- Proving equality between functors using heterogeneous equality. -/
theorem hext {F G : C ‚•§ D} (h_obj : ‚àÄ X, F.obj X = G.obj X)
    (h_map : ‚àÄ (X Y) (f : X ‚ü∂ Y), F.map f ‚âç G.map f) : F = G :=
  Functor.ext h_obj fun _ _ f => (conj_eqToHom_iff_heq _ _ (h_obj _) (h_obj _)).2 <| h_map _ _ f

-- Using equalities between functors.
theorem congr_obj {F G : C ‚•§ D} (h : F = G) (X) : F.obj X = G.obj X := by rw [h]

@[reassoc]
theorem congr_hom {F G : C ‚•§ D} (h : F = G) {X Y} (f : X ‚ü∂ Y) :
    F.map f = eqToHom (congr_obj h X) ‚â´ G.map f ‚â´ eqToHom (congr_obj h Y).symm := by
  subst h; simp

theorem congr_inv_of_congr_hom (F G : C ‚•§ D) {X Y : C} (e : X ‚âÖ Y) (hX : F.obj X = G.obj X)
    (hY : F.obj Y = G.obj Y)
    (h‚ÇÇ : F.map e.hom = eqToHom (by rw [hX]) ‚â´ G.map e.hom ‚â´ eqToHom (by rw [hY])) :
    F.map e.inv = eqToHom (by rw [hY]) ‚â´ G.map e.inv ‚â´ eqToHom (by rw [hX]) := by
  simp only [‚Üê IsIso.Iso.inv_hom e, Functor.map_inv, h‚ÇÇ, IsIso.inv_comp, inv_eqToHom,
    Category.assoc]

section HEq

-- Composition of functors and maps w.r.t. heq
variable {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E] {F G : C ‚•§ D} {X Y Z : C} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z}

theorem map_comp_heq (hx : F.obj X = G.obj X) (hy : F.obj Y = G.obj Y) (hz : F.obj Z = G.obj Z)
    (hf : F.map f ‚âç G.map f) (hg : F.map g ‚âç G.map g) :
    F.map (f ‚â´ g) ‚âç G.map (f ‚â´ g) := by
  rw [F.map_comp, G.map_comp]
  congr

theorem map_comp_heq' (hobj : ‚àÄ X : C, F.obj X = G.obj X)
    (hmap : ‚àÄ {X Y} (f : X ‚ü∂ Y), F.map f ‚âç G.map f) :
    F.map (f ‚â´ g) ‚âç G.map (f ‚â´ g) := by
  rw [Functor.hext hobj fun _ _ => hmap]

theorem precomp_map_heq (H : E ‚•§ C) (hmap : ‚àÄ {X Y} (f : X ‚ü∂ Y), F.map f ‚âç G.map f) {X Y : E}
    (f : X ‚ü∂ Y) : (H ‚ãô F).map f ‚âç (H ‚ãô G).map f :=
  hmap _

theorem postcomp_map_heq (H : D ‚•§ E) (hx : F.obj X = G.obj X) (hy : F.obj Y = G.obj Y)
    (hmap : F.map f ‚âç G.map f) : (F ‚ãô H).map f ‚âç (G ‚ãô H).map f := by
  dsimp
  congr

theorem postcomp_map_heq' (H : D ‚•§ E) (hobj : ‚àÄ X : C, F.obj X = G.obj X)
    (hmap : ‚àÄ {X Y} (f : X ‚ü∂ Y), F.map f ‚âç G.map f) :
    (F ‚ãô H).map f ‚âç (G ‚ãô H).map f := by rw [Functor.hext hobj fun _ _ => hmap]

theorem hcongr_hom {F G : C ‚•§ D} (h : F = G) {X Y} (f : X ‚ü∂ Y) : F.map f ‚âç G.map f := by
  rw [h]

end HEq

end Functor

/-- This is not always a good idea as a `@[simp]` lemma,
as we lose the ability to use results that interact with `F`,
e.g. the naturality of a natural transformation.

In some files it may be appropriate to use `attribute [local simp] eqToHom_map`, however.
-/
theorem eqToHom_map (F : C ‚•§ D) {X Y : C} (p : X = Y) :
    F.map (eqToHom p) = eqToHom (congr_arg F.obj p) := by cases p; simp

@[reassoc (attr := simp)]
theorem eqToHom_map_comp (F : C ‚•§ D) {X Y Z : C} (p : X = Y) (q : Y = Z) :
    F.map (eqToHom p) ‚â´ F.map (eqToHom q) = F.map (eqToHom <| p.trans q) := by cat_disch

/-- See the note on `eqToHom_map` regarding using this as a `simp` lemma.
-/
theorem eqToIso_map (F : C ‚•§ D) {X Y : C} (p : X = Y) :
    F.mapIso (eqToIso p) = eqToIso (congr_arg F.obj p) := by ext; cases p; simp

@[simp]
theorem eqToIso_map_trans (F : C ‚•§ D) {X Y Z : C} (p : X = Y) (q : Y = Z) :
    F.mapIso (eqToIso p) ‚â™‚â´ F.mapIso (eqToIso q) = F.mapIso (eqToIso <| p.trans q) := by cat_disch

@[simp]
theorem eqToHom_app {F G : C ‚•§ D} (h : F = G) (X : C) :
    (eqToHom h : F ‚ü∂ G).app X = eqToHom (Functor.congr_obj h X) := by subst h; rfl

theorem NatTrans.congr {F G : C ‚•§ D} (Œ± : F ‚ü∂ G) {X Y : C} (h : X = Y) :
    Œ±.app X = F.map (eqToHom h) ‚â´ Œ±.app Y ‚â´ G.map (eqToHom h.symm) := by
  rw [Œ±.naturality_assoc]
  simp [eqToHom_map]

theorem eq_conj_eqToHom {X Y : C} (f : X ‚ü∂ Y) : f = eqToHom rfl ‚â´ f ‚â´ eqToHom rfl := by
  simp only [Category.id_comp, eqToHom_refl, Category.comp_id]

theorem dcongr_arg {Œπ : Type*} {F G : Œπ ‚Üí C} (Œ± : ‚àÄ i, F i ‚ü∂ G i) {i j : Œπ} (h : i = j) :
    Œ± i = eqToHom (congr_arg F h) ‚â´ Œ± j ‚â´ eqToHom (congr_arg G h.symm) := by
  subst h
  simp

/-- If `T ‚âÉ D` is a bijection and `D` is a category, then
`InducedCategory D e` is equivalent to `D`. -/
@[simps]
def Equivalence.induced {T : Type*} (e : T ‚âÉ D) :
    InducedCategory D e ‚âå D where
  functor := inducedFunctor e
  inverse :=
    { obj := e.symm
      map {X Y} f := show e (e.symm X) ‚ü∂ e (e.symm Y) from
        eqToHom (e.apply_symm_apply X) ‚â´ f ‚â´
          eqToHom (e.apply_symm_apply Y).symm
      map_comp {X Y Z} f g := by
        dsimp
        rw [Category.assoc]
        erw [Category.assoc]
        rw [Category.assoc, eqToHom_trans_assoc, eqToHom_refl, Category.id_comp] }
  unitIso := NatIso.ofComponents (fun _ ‚Ü¶ eqToIso (by simp)) (fun {X Y} f ‚Ü¶ by
    dsimp
    erw [eqToHom_trans_assoc _ (by simp), eqToHom_refl, Category.id_comp]
    rfl )
  counitIso := NatIso.ofComponents (fun _ ‚Ü¶ eqToIso (by simp))
  functor_unitIso_comp X := eqToHom_trans (by simp) (by simp)

end CategoryTheory
