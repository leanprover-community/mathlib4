/-
Copyright (c) 2018 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Reid Barton, Kim Morrison
-/
import Mathlib.CategoryTheory.Opposites

/-!
# Morphisms from equations between objects.

When working categorically, sometimes one encounters an equation `h : X = Y` between objects.

Your initial aversion to this is natural and appropriate:
you're in for some trouble, and if there is another way to approach the problem that won't
rely on this equality, it may be worth pursuing.

You have two options:
1. Use the equality `h` as one normally would in Lean (e.g. using `rw` and `subst`).
   This may immediately cause difficulties, because in category theory everything is dependently
   typed, and equations between objects quickly lead to nasty goals with `eq.rec`.
2. Promote `h` to a morphism using `eqToHom h : X âŸ¶ Y`, or `eqToIso h : X â‰… Y`.

This file introduces various `simp` lemmas which in favourable circumstances
result in the various `eqToHom` morphisms to drop out at the appropriate moment!
-/

universe vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

-- morphism levels before object levels. See note [CategoryTheory universes].
namespace CategoryTheory

open Opposite

variable {C : Type uâ‚} [Category.{vâ‚} C]

/-- An equality `X = Y` gives us a morphism `X âŸ¶ Y`.

It is typically better to use this, rather than rewriting by the equality then using `ðŸ™ _`
which usually leads to dependent type theory hell.
-/
def eqToHom {X Y : C} (p : X = Y) : X âŸ¶ Y := by rw [p]; exact ðŸ™ _

@[simp]
theorem eqToHom_refl (X : C) (p : X = X) : eqToHom p = ðŸ™ X :=
  rfl

@[reassoc (attr := simp)]
theorem eqToHom_trans {X Y Z : C} (p : X = Y) (q : Y = Z) :
    eqToHom p â‰« eqToHom q = eqToHom (p.trans q) := by
  cases p
  cases q
  simp

/-- `eqToHom h` is heterogeneously equal to the identity of its domain. -/
lemma eqToHom_heq_id_dom (X Y : C) (h : X = Y) : HEq (eqToHom h) (ðŸ™ X) := by
  subst h; rfl

/-- `eqToHom h` is heterogeneously equal to the identity of its codomain. -/
lemma eqToHom_heq_id_cod (X Y : C) (h : X = Y) : HEq (eqToHom h) (ðŸ™ Y) := by
  subst h; rfl

/-- Two morphisms are conjugate via eqToHom if and only if they are heterogeneously equal.
Note this used to be in the Functor namespace, where it doesn't belong. -/
theorem conj_eqToHom_iff_heq {W X Y Z : C} (f : W âŸ¶ X) (g : Y âŸ¶ Z) (h : W = Y) (h' : X = Z) :
    f = eqToHom h â‰« g â‰« eqToHom h'.symm â†” HEq f g := by
  cases h
  cases h'
  simp

theorem conj_eqToHom_iff_heq' {C} [Category C] {W X Y Z : C}
    (f : W âŸ¶ X) (g : Y âŸ¶ Z) (h : W = Y) (h' : Z = X) :
    f = eqToHom h â‰« g â‰« eqToHom h' â†” HEq f g := conj_eqToHom_iff_heq _ _ _ h'.symm

theorem comp_eqToHom_iff {X Y Y' : C} (p : Y = Y') (f : X âŸ¶ Y) (g : X âŸ¶ Y') :
    f â‰« eqToHom p = g â†” f = g â‰« eqToHom p.symm :=
  { mp := fun h => h â–¸ by simp
    mpr := fun h => by simp [eq_whisker h (eqToHom p)] }

theorem eqToHom_comp_iff {X X' Y : C} (p : X = X') (f : X âŸ¶ Y) (g : X' âŸ¶ Y) :
    eqToHom p â‰« g = f â†” g = eqToHom p.symm â‰« f :=
  { mp := fun h => h â–¸ by simp
    mpr := fun h => h â–¸ by simp [whisker_eq _ h] }

theorem eqToHom_comp_heq {C} [Category C] {W X Y : C}
    (f : Y âŸ¶ X) (h : W = Y) : HEq (eqToHom h â‰« f) f := by
  rw [â† conj_eqToHom_iff_heq _ _ h rfl, eqToHom_refl, Category.comp_id]

@[simp] theorem eqToHom_comp_heq_iff {C} [Category C] {W X Y Z Z' : C}
    (f : Y âŸ¶ X) (g : Z âŸ¶ Z') (h : W = Y) :
    HEq (eqToHom h â‰« f) g â†” HEq f g :=
  âŸ¨(eqToHom_comp_heq ..).symm.trans, (eqToHom_comp_heq ..).transâŸ©

@[simp] theorem heq_eqToHom_comp_iff {C} [Category C] {W X Y Z Z' : C}
    (f : Y âŸ¶ X) (g : Z âŸ¶ Z') (h : W = Y) :
    HEq g (eqToHom h â‰« f) â†” HEq g f :=
  âŸ¨(Â·.trans (eqToHom_comp_heq ..)), (Â·.trans (eqToHom_comp_heq ..).symm)âŸ©

theorem comp_eqToHom_heq {C} [Category C] {X Y Z : C}
    (f : X âŸ¶ Y) (h : Y = Z) : HEq (f â‰« eqToHom h) f := by
  rw [â† conj_eqToHom_iff_heq' _ _ rfl h, eqToHom_refl, Category.id_comp]

@[simp] theorem comp_eqToHom_heq_iff {C} [Category C] {W X Y Z Z' : C}
    (f : X âŸ¶ Y) (g : Z âŸ¶ Z') (h : Y = W) :
    HEq (f â‰« eqToHom h) g â†” HEq f g :=
  âŸ¨(comp_eqToHom_heq ..).symm.trans, (comp_eqToHom_heq ..).transâŸ©

@[simp] theorem heq_comp_eqToHom_iff {C} [Category C] {W X Y Z Z' : C}
    (f : X âŸ¶ Y) (g : Z âŸ¶ Z') (h : Y = W) :
    HEq g (f â‰« eqToHom h) â†” HEq g f :=
  âŸ¨(Â·.trans (comp_eqToHom_heq ..)), (Â·.trans (comp_eqToHom_heq ..).symm)âŸ©

theorem heq_comp {C} [Category C] {X Y Z X' Y' Z' : C}
    {f : X âŸ¶ Y} {g : Y âŸ¶ Z} {f' : X' âŸ¶ Y'} {g' : Y' âŸ¶ Z'}
    (eq1 : X = X') (eq2 : Y = Y') (eq3 : Z = Z')
    (H1 : HEq f f') (H2 : HEq g g') :
    HEq (f â‰« g) (f' â‰« g') := by
  cases eq1; cases eq2; cases eq3; cases H1; cases H2; rfl

variable {Î² : Sort*}

/-- We can push `eqToHom` to the left through families of morphisms. -/
@[reassoc (attr := simp)]
theorem eqToHom_naturality {f g : Î² â†’ C} (z : âˆ€ b, f b âŸ¶ g b) {j j' : Î²} (w : j = j') :
    z j â‰« eqToHom (by simp [w]) = eqToHom (by simp [w]) â‰« z j' := by
  cases w
  simp

/-- A variant on `eqToHom_naturality` that helps Lean identify the families `f` and `g`. -/
@[reassoc (attr := simp)]
theorem eqToHom_iso_hom_naturality {f g : Î² â†’ C} (z : âˆ€ b, f b â‰… g b) {j j' : Î²} (w : j = j') :
    (z j).hom â‰« eqToHom (by simp [w]) = eqToHom (by simp [w]) â‰« (z j').hom := by
  cases w
  simp

/-- A variant on `eqToHom_naturality` that helps Lean identify the families `f` and `g`. -/
@[reassoc (attr := simp)]
theorem eqToHom_iso_inv_naturality {f g : Î² â†’ C} (z : âˆ€ b, f b â‰… g b) {j j' : Î²} (w : j = j') :
    (z j).inv â‰« eqToHom (by simp [w]) = eqToHom (by simp [w]) â‰« (z j').inv := by
  cases w
  simp

/-- Reducible form of congrArg_mpr_hom_left -/
@[simp]
theorem congrArg_cast_hom_left {X Y Z : C} (p : X = Y) (q : Y âŸ¶ Z) :
    cast (congrArg (fun W : C => W âŸ¶ Z) p.symm) q = eqToHom p â‰« q := by
  cases p
  simp

/-- If we (perhaps unintentionally) perform equational rewriting on
the source object of a morphism,
we can replace the resulting `_.mpr f` term by a composition with an `eqToHom`.

It may be advisable to introduce any necessary `eqToHom` morphisms manually,
rather than relying on this lemma firing.
-/
theorem congrArg_mpr_hom_left {X Y Z : C} (p : X = Y) (q : Y âŸ¶ Z) :
    (congrArg (fun W : C => W âŸ¶ Z) p).mpr q = eqToHom p â‰« q := by
  cases p
  simp

/-- Reducible form of `congrArg_mpr_hom_right` -/
@[simp]
theorem congrArg_cast_hom_right {X Y Z : C} (p : X âŸ¶ Y) (q : Z = Y) :
    cast (congrArg (fun W : C => X âŸ¶ W) q.symm) p = p â‰« eqToHom q.symm := by
  cases q
  simp

/-- If we (perhaps unintentionally) perform equational rewriting on
the target object of a morphism,
we can replace the resulting `_.mpr f` term by a composition with an `eqToHom`.

It may be advisable to introduce any necessary `eqToHom` morphisms manually,
rather than relying on this lemma firing.
-/
theorem congrArg_mpr_hom_right {X Y Z : C} (p : X âŸ¶ Y) (q : Z = Y) :
    (congrArg (fun W : C => X âŸ¶ W) q).mpr p = p â‰« eqToHom q.symm := by
  cases q
  simp

/-- An equality `X = Y` gives us an isomorphism `X â‰… Y`.

It is typically better to use this, rather than rewriting by the equality then using `Iso.refl _`
which usually leads to dependent type theory hell.
-/
def eqToIso {X Y : C} (p : X = Y) : X â‰… Y :=
  âŸ¨eqToHom p, eqToHom p.symm, by simp, by simpâŸ©

@[simp]
theorem eqToIso.hom {X Y : C} (p : X = Y) : (eqToIso p).hom = eqToHom p :=
  rfl

@[simp]
theorem eqToIso.inv {X Y : C} (p : X = Y) : (eqToIso p).inv = eqToHom p.symm :=
  rfl

@[simp]
theorem eqToIso_refl {X : C} (p : X = X) : eqToIso p = Iso.refl X :=
  rfl

@[simp]
theorem eqToIso_trans {X Y Z : C} (p : X = Y) (q : Y = Z) :
    eqToIso p â‰ªâ‰« eqToIso q = eqToIso (p.trans q) := by ext; simp

@[simp]
theorem eqToHom_op {X Y : C} (h : X = Y) : (eqToHom h).op = eqToHom (congr_arg op h.symm) := by
  cases h
  rfl

@[simp]
theorem eqToHom_unop {X Y : Cáµ’áµ–} (h : X = Y) :
    (eqToHom h).unop = eqToHom (congr_arg unop h.symm) := by
  cases h
  rfl

instance {X Y : C} (h : X = Y) : IsIso (eqToHom h) :=
  (eqToIso h).isIso_hom

@[simp]
theorem inv_eqToHom {X Y : C} (h : X = Y) : inv (eqToHom h) = eqToHom h.symm := by
  aesop_cat

variable {D : Type uâ‚‚} [Category.{vâ‚‚} D]

namespace Functor

/-- Proving equality between functors. This isn't an extensionality lemma,
  because usually you don't really want to do this. -/
theorem ext {F G : C â¥¤ D} (h_obj : âˆ€ X, F.obj X = G.obj X)
    (h_map : âˆ€ X Y f,
      F.map f = eqToHom (h_obj X) â‰« G.map f â‰« eqToHom (h_obj Y).symm := by aesop_cat) :
    F = G := by
  match F, G with
  | mk F_pre _ _ , mk G_pre _ _ =>
    match F_pre, G_pre with
    | Prefunctor.mk F_obj _ , Prefunctor.mk G_obj _ =>
    obtain rfl : F_obj = G_obj := by
      ext X
      apply h_obj
    congr
    funext X Y f
    simpa using h_map X Y f

lemma ext_of_iso {F G : C â¥¤ D} (e : F â‰… G) (hobj : âˆ€ X, F.obj X = G.obj X)
    (happ : âˆ€ X, e.hom.app X = eqToHom (hobj X) := by aesop_cat) : F = G :=
  Functor.ext hobj (fun X Y f => by
    rw [â† cancel_mono (e.hom.app Y), e.hom.naturality f, happ, happ, Category.assoc,
    Category.assoc, eqToHom_trans, eqToHom_refl, Category.comp_id])

/-- Proving equality between functors using heterogeneous equality. -/
theorem hext {F G : C â¥¤ D} (h_obj : âˆ€ X, F.obj X = G.obj X)
    (h_map : âˆ€ (X Y) (f : X âŸ¶ Y), HEq (F.map f) (G.map f)) : F = G :=
  Functor.ext h_obj fun _ _ f => (conj_eqToHom_iff_heq _ _ (h_obj _) (h_obj _)).2 <| h_map _ _ f

-- Using equalities between functors.
theorem congr_obj {F G : C â¥¤ D} (h : F = G) (X) : F.obj X = G.obj X := by rw [h]

@[reassoc]
theorem congr_hom {F G : C â¥¤ D} (h : F = G) {X Y} (f : X âŸ¶ Y) :
    F.map f = eqToHom (congr_obj h X) â‰« G.map f â‰« eqToHom (congr_obj h Y).symm := by
  subst h; simp

theorem congr_inv_of_congr_hom (F G : C â¥¤ D) {X Y : C} (e : X â‰… Y) (hX : F.obj X = G.obj X)
    (hY : F.obj Y = G.obj Y)
    (hâ‚‚ : F.map e.hom = eqToHom (by rw [hX]) â‰« G.map e.hom â‰« eqToHom (by rw [hY])) :
    F.map e.inv = eqToHom (by rw [hY]) â‰« G.map e.inv â‰« eqToHom (by rw [hX]) := by
  simp only [â† IsIso.Iso.inv_hom e, Functor.map_inv, hâ‚‚, IsIso.inv_comp, inv_eqToHom,
    Category.assoc]

section HEq

-- Composition of functors and maps w.r.t. heq
variable {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E] {F G : C â¥¤ D} {X Y Z : C} {f : X âŸ¶ Y} {g : Y âŸ¶ Z}

theorem map_comp_heq (hx : F.obj X = G.obj X) (hy : F.obj Y = G.obj Y) (hz : F.obj Z = G.obj Z)
    (hf : HEq (F.map f) (G.map f)) (hg : HEq (F.map g) (G.map g)) :
    HEq (F.map (f â‰« g)) (G.map (f â‰« g)) := by
  rw [F.map_comp, G.map_comp]
  congr

theorem map_comp_heq' (hobj : âˆ€ X : C, F.obj X = G.obj X)
    (hmap : âˆ€ {X Y} (f : X âŸ¶ Y), HEq (F.map f) (G.map f)) :
    HEq (F.map (f â‰« g)) (G.map (f â‰« g)) := by
  rw [Functor.hext hobj fun _ _ => hmap]

theorem precomp_map_heq (H : E â¥¤ C) (hmap : âˆ€ {X Y} (f : X âŸ¶ Y), HEq (F.map f) (G.map f)) {X Y : E}
    (f : X âŸ¶ Y) : HEq ((H â‹™ F).map f) ((H â‹™ G).map f) :=
  hmap _

theorem postcomp_map_heq (H : D â¥¤ E) (hx : F.obj X = G.obj X) (hy : F.obj Y = G.obj Y)
    (hmap : HEq (F.map f) (G.map f)) : HEq ((F â‹™ H).map f) ((G â‹™ H).map f) := by
  dsimp
  congr

theorem postcomp_map_heq' (H : D â¥¤ E) (hobj : âˆ€ X : C, F.obj X = G.obj X)
    (hmap : âˆ€ {X Y} (f : X âŸ¶ Y), HEq (F.map f) (G.map f)) :
    HEq ((F â‹™ H).map f) ((G â‹™ H).map f) := by rw [Functor.hext hobj fun _ _ => hmap]

theorem hcongr_hom {F G : C â¥¤ D} (h : F = G) {X Y} (f : X âŸ¶ Y) : HEq (F.map f) (G.map f) := by
  rw [h]

end HEq

end Functor

/-- This is not always a good idea as a `@[simp]` lemma,
as we lose the ability to use results that interact with `F`,
e.g. the naturality of a natural transformation.

In some files it may be appropriate to use `attribute [local simp] eqToHom_map`, however.
-/
theorem eqToHom_map (F : C â¥¤ D) {X Y : C} (p : X = Y) :
    F.map (eqToHom p) = eqToHom (congr_arg F.obj p) := by cases p; simp

@[reassoc (attr := simp)]
theorem eqToHom_map_comp (F : C â¥¤ D) {X Y Z : C} (p : X = Y) (q : Y = Z) :
    F.map (eqToHom p) â‰« F.map (eqToHom q) = F.map (eqToHom <| p.trans q) := by aesop_cat

/-- See the note on `eqToHom_map` regarding using this as a `simp` lemma.
-/
theorem eqToIso_map (F : C â¥¤ D) {X Y : C} (p : X = Y) :
    F.mapIso (eqToIso p) = eqToIso (congr_arg F.obj p) := by ext; cases p; simp

@[simp]
theorem eqToIso_map_trans (F : C â¥¤ D) {X Y Z : C} (p : X = Y) (q : Y = Z) :
    F.mapIso (eqToIso p) â‰ªâ‰« F.mapIso (eqToIso q) = F.mapIso (eqToIso <| p.trans q) := by aesop_cat

@[simp]
theorem eqToHom_app {F G : C â¥¤ D} (h : F = G) (X : C) :
    (eqToHom h : F âŸ¶ G).app X = eqToHom (Functor.congr_obj h X) := by subst h; rfl

theorem NatTrans.congr {F G : C â¥¤ D} (Î± : F âŸ¶ G) {X Y : C} (h : X = Y) :
    Î±.app X = F.map (eqToHom h) â‰« Î±.app Y â‰« G.map (eqToHom h.symm) := by
  rw [Î±.naturality_assoc]
  simp [eqToHom_map]

theorem eq_conj_eqToHom {X Y : C} (f : X âŸ¶ Y) : f = eqToHom rfl â‰« f â‰« eqToHom rfl := by
  simp only [Category.id_comp, eqToHom_refl, Category.comp_id]

theorem dcongr_arg {Î¹ : Type*} {F G : Î¹ â†’ C} (Î± : âˆ€ i, F i âŸ¶ G i) {i j : Î¹} (h : i = j) :
    Î± i = eqToHom (congr_arg F h) â‰« Î± j â‰« eqToHom (congr_arg G h.symm) := by
  subst h
  simp

/-- If `T â‰ƒ D` is a bijection and `D` is a category, then
`InducedCategory D e` is equivalent to `D`. -/
@[simps]
def Equivalence.induced {T : Type*} (e : T â‰ƒ D) :
    InducedCategory D e â‰Œ D where
  functor := inducedFunctor e
  inverse :=
    { obj := e.symm
      map {X Y} f := show e (e.symm X) âŸ¶ e (e.symm Y) from
        eqToHom (e.apply_symm_apply X) â‰« f â‰«
          eqToHom (e.apply_symm_apply Y).symm
      map_comp {X Y Z} f g := by
        dsimp
        rw [Category.assoc]
        erw [Category.assoc]
        rw [Category.assoc, eqToHom_trans_assoc, eqToHom_refl, Category.id_comp] }
  unitIso := NatIso.ofComponents (fun _ â†¦ eqToIso (by simp)) (fun {X Y} f â†¦ by
    dsimp
    erw [eqToHom_trans_assoc _ (by simp), eqToHom_refl, Category.id_comp]
    rfl )
  counitIso := NatIso.ofComponents (fun _ â†¦ eqToIso (by simp))
  functor_unitIso_comp X := eqToHom_trans (by simp) (by simp)

end CategoryTheory
