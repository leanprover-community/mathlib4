/-
Copyright (c) 2020 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Adam Topaz
-/
import Mathlib.CategoryTheory.ConcreteCategory.Basic
import Mathlib.CategoryTheory.Endomorphism
import Mathlib.CategoryTheory.Skeletal
import Mathlib.Data.Finite.Basic

/-!
# The category of finite types.

We define the category of finite types, denoted `FintypeCat` as
(bundled) types with a `Fintype` instance.

We also define `FintypeCat.Skeleton`, the standard skeleton of `FintypeCat` whose objects
are `Fin n` for `n : ‚Ñï`. We prove that the obvious inclusion functor
`FintypeCat.Skeleton ‚•§ FintypeCat` is an equivalence of categories in
`FintypeCat.Skeleton.equivalence`.
We prove that `FintypeCat.Skeleton` is a skeleton of `FintypeCat` in `FintypeCat.isSkeleton`.
-/


open scoped Classical

open CategoryTheory

/-- The category of finite types. -/
def FintypeCat :=
  Bundled Fintype

namespace FintypeCat

instance : CoeSort FintypeCat Type* :=
  Bundled.coeSort

/-- Construct a bundled `FintypeCat` from the underlying type and typeclass. -/
def of (X : Type*) [Fintype X] : FintypeCat :=
  Bundled.of X

instance : Inhabited FintypeCat :=
  ‚ü®of PEmpty‚ü©

instance {X : FintypeCat} : Fintype X :=
  X.2

instance : Category FintypeCat :=
  InducedCategory.category Bundled.Œ±

/-- The fully faithful embedding of `FintypeCat` into the category of types. -/
@[simps!]
def incl : FintypeCat ‚•§ Type* :=
  inducedFunctor _

instance : incl.Full := InducedCategory.full _
instance : incl.Faithful := InducedCategory.faithful _

instance concreteCategoryFintype : ConcreteCategory FintypeCat :=
  ‚ü®incl‚ü©

/- Help typeclass inference infer fullness of forgetful functor. -/
instance : (forget FintypeCat).Full := inferInstanceAs <| FintypeCat.incl.Full

attribute [local instance] ConcreteCategory.instFunLike

/-- Constructor ffor morphisms in `FintypeCat`. -/
@[simps]
def homMk {X Y : FintypeCat} (f : X ‚Üí Y) : X ‚ü∂ Y where
  hom := f

@[simp]
lemma homMk_apply {X Y : FintypeCat} (f : X ‚Üí Y) (x : X) :
    homMk f x = f x := rfl

@[simp]
theorem id_apply (X : FintypeCat) (x : X) :
    (ùüô X) x = x :=
  rfl

@[simp]
theorem comp_apply {X Y Z : FintypeCat} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : X) :
    (f ‚â´ g) x = g (f x) :=
  rfl

@[simp]
lemma hom_inv_id_apply {X Y : FintypeCat} (f : X ‚âÖ Y) (x : X) : f.inv (f.hom x) = x := by
  rw [‚Üê comp_apply, f.hom_inv_id, id_apply]

@[simp]
lemma inv_hom_id_apply {X Y : FintypeCat} (f : X ‚âÖ Y) (y : Y) : f.hom (f.inv y) = y := by
  rw [‚Üê comp_apply, f.inv_hom_id, id_apply]

-- Porting note (#10688): added to ease automation
@[ext]
lemma hom_ext {X Y : FintypeCat} (f g : X ‚ü∂ Y) (h : ‚àÄ x, f x = g x) : f = g := by
  apply InducedCategory.hom_ext
  funext
  apply h

-- See `equivEquivIso` in the root namespace for the analogue in `Type`.
/-- Equivalences between finite types are the same as isomorphisms in `FintypeCat`. -/
@[simps]
def equivEquivIso {A B : FintypeCat} : A ‚âÉ B ‚âÉ (A ‚âÖ B) where
  toFun e := InducedCategory.isoMk e.toIso
  invFun i :=
    { toFun := i.hom
      invFun := i.inv
      left_inv := by aesop_cat
      right_inv := by aesop_cat }
  left_inv := by aesop_cat
  right_inv := by aesop_cat

instance (X Y : FintypeCat) : Finite (X ‚ü∂ Y) :=
  Finite.of_injective (show _ ‚Üí (X ‚Üí Y) from fun f x ‚Ü¶ f x)
    (fun _ _ h ‚Ü¶ by ext x; apply congr_fun h)

instance (X Y : FintypeCat) : Finite (X ‚âÖ Y) :=
  Finite.of_injective _ (fun _ _ h ‚Ü¶ Iso.ext h)

instance (X : FintypeCat) : Finite (Aut X) :=
  inferInstanceAs <| Finite (X ‚âÖ X)

universe u

/--
The "standard" skeleton for `FintypeCat`. This is the full subcategory of `FintypeCat`
spanned by objects of the form `ULift (Fin n)` for `n : ‚Ñï`. We parameterize the objects
of `Fintype.Skeleton` directly as `ULift ‚Ñï`, as the type `ULift (Fin m) ‚âÉ ULift (Fin n)`
is nonempty if and only if `n = m`. Specifying universes, `Skeleton : Type u` is a small
skeletal category equivalent to `Fintype.{u}`.
-/
def Skeleton : Type u :=
  ULift ‚Ñï

namespace Skeleton

/-- Given any natural number `n`, this creates the associated object of `Fintype.Skeleton`. -/
def mk : ‚Ñï ‚Üí Skeleton :=
  ULift.up

instance : Inhabited Skeleton :=
  ‚ü®mk 0‚ü©

/-- Given any object of `Fintype.Skeleton`, this returns the associated natural number. -/
def len : Skeleton ‚Üí ‚Ñï :=
  ULift.down

@[ext]
theorem ext (X Y : Skeleton) : X.len = Y.len ‚Üí X = Y :=
  ULift.ext _ _

instance : SmallCategory Skeleton.{u} where
  Hom X Y := ULift.{u} (Fin X.len) ‚Üí ULift.{u} (Fin Y.len)
  id _ := id
  comp f g := g ‚àò f

theorem is_skeletal : Skeletal Skeleton.{u} := fun X Y ‚ü®h‚ü© =>
  ext _ _ <|
    Fin.equiv_iff_eq.mp <|
      Nonempty.intro <|
        { toFun := fun x => (h.hom ‚ü®x‚ü©).down
          invFun := fun x => (h.inv ‚ü®x‚ü©).down
          left_inv := by
            intro a
            change ULift.down _ = _
            rw [ULift.up_down]
            change ((h.hom ‚â´ h.inv) _).down = _
            simp
            rfl
          right_inv := by
            intro a
            change ULift.down _ = _
            rw [ULift.up_down]
            change ((h.inv ‚â´ h.hom) _).down = _
            simp
            rfl }

/-- The canonical fully faithful embedding of `Fintype.Skeleton` into `FintypeCat`. -/
def incl : Skeleton.{u} ‚•§ FintypeCat.{u} where
  obj X := FintypeCat.of (ULift (Fin X.len))
  map f := { hom := f }

def fullyFaithfulIncl : incl.FullyFaithful where
  preimage f := f.hom

instance : incl.Full := fullyFaithfulIncl.full

instance : incl.Faithful := fullyFaithfulIncl.faithful

instance : incl.EssSurj :=
  Functor.EssSurj.mk fun X =>
    let F := Fintype.equivFin X
    ‚ü®mk (Fintype.card X),
      Nonempty.intro (equivEquivIso (Equiv.ulift.trans F.symm))‚ü©

noncomputable instance : incl.IsEquivalence where

/-- The equivalence between `Fintype.Skeleton` and `Fintype`. -/
noncomputable def equivalence : Skeleton ‚âå FintypeCat :=
  incl.asEquivalence

@[simp]
theorem incl_mk_nat_card (n : ‚Ñï) : Fintype.card (incl.obj (mk n)) = n := by
  convert Finset.card_fin n
  apply Fintype.ofEquiv_card

end Skeleton

/-- `Fintype.Skeleton` is a skeleton of `Fintype`. -/
lemma isSkeleton : IsSkeletonOf FintypeCat Skeleton Skeleton.incl where
  skel := Skeleton.is_skeletal
  eqv := by infer_instance

section Universes

universe v

/-- If `u` and `v` are two arbitrary universes, we may construct a functor
`uSwitch.{u, v} : FintypeCat.{u} ‚•§ FintypeCat.{v}` by sending
`X : FintypeCat.{u}` to `ULift.{v} (Fin (Fintype.card X))`. -/
noncomputable def uSwitch : FintypeCat.{u} ‚•§ FintypeCat.{v} where
  obj X := FintypeCat.of <| ULift.{v} (Fin (Fintype.card X))
  map {X Y} f := homMk (fun x ‚Ü¶ ULift.up <| (Fintype.equivFin Y)
    (f ((Fintype.equivFin X).symm x.down)))
  map_comp {X Y Z} f g := by ext; simp

/-- Switching the universe of an object `X : FintypeCat.{u}` does not change `X` up to equivalence
of types. This is natural in the sense that it commutes with `uSwitch.map f` for
any `f : X ‚ü∂ Y` in `FintypeCat.{u}`. -/
noncomputable def uSwitchEquiv (X : FintypeCat.{u}) :
    uSwitch.{u, v}.obj X ‚âÉ X :=
  Equiv.ulift.trans (Fintype.equivFin X).symm

lemma uSwitchEquiv_naturality {X Y : FintypeCat.{u}} (f : X ‚ü∂ Y)
    (x : uSwitch.{u, v}.obj X) :
    f (X.uSwitchEquiv x) = Y.uSwitchEquiv (uSwitch.map f x) := by
  simp only [uSwitch, uSwitchEquiv, Equiv.trans_apply]
  erw [Equiv.ulift_apply, Equiv.ulift_apply]
  simp only [homMk_apply, Equiv.symm_apply_apply]

lemma uSwitchEquiv_symm_naturality {X Y : FintypeCat.{u}} (f : X ‚ü∂ Y) (x : X) :
    uSwitch.map f (X.uSwitchEquiv.symm x) = Y.uSwitchEquiv.symm (f x) := by
  erw [‚Üê Equiv.apply_eq_iff_eq_symm_apply, ‚Üê uSwitchEquiv_naturality f,
    Equiv.apply_symm_apply]

lemma uSwitch_map_uSwitch_map {X Y : FintypeCat.{u}} (f : X ‚ü∂ Y) :
    uSwitch.map (uSwitch.map f) =
    (equivEquivIso ((uSwitch.obj X).uSwitchEquiv.trans X.uSwitchEquiv)).hom ‚â´
      f ‚â´ (equivEquivIso ((uSwitch.obj Y).uSwitchEquiv.trans
      Y.uSwitchEquiv)).inv := by
  ext x
  sorry
  --simp only [comp_apply, equivEquivIso_apply_hom, Equiv.trans_apply]
  --rw [uSwitchEquiv_naturality f, ‚Üê uSwitchEquiv_naturality]
  --rfl

/-- `uSwitch.{u, v}` is an equivalence of categories with quasi-inverse `uSwitch.{v, u}`. -/
noncomputable def uSwitchEquivalence : FintypeCat.{u} ‚âå FintypeCat.{v} where
  functor := uSwitch
  inverse := uSwitch
  unitIso := NatIso.ofComponents (fun X ‚Ü¶ (equivEquivIso <|
    (uSwitch.obj X).uSwitchEquiv.trans X.uSwitchEquiv).symm) <| by
    simp [uSwitch_map_uSwitch_map]
  counitIso := NatIso.ofComponents (fun X ‚Ü¶ equivEquivIso <|
    (uSwitch.obj X).uSwitchEquiv.trans X.uSwitchEquiv) <| by
    simp [uSwitch_map_uSwitch_map]
  functor_unitIso_comp X := by
    ext x
    sorry
    --simp [‚Üê uSwitchEquiv_naturality]

instance : uSwitch.IsEquivalence :=
  uSwitchEquivalence.isEquivalence_functor

end Universes

end FintypeCat

namespace FunctorToFintypeCat

universe u v w

variable {C : Type u} [Category.{v} C] (F G : C ‚•§ FintypeCat.{w}) {X Y : C}

attribute [local instance] ConcreteCategory.instFunLike

lemma naturality (œÉ : F ‚ü∂ G) (f : X ‚ü∂ Y) (x : F.obj X) :
    œÉ.app Y (F.map f x) = G.map f (œÉ.app X x) :=
  NatTrans.naturality_apply œÉ _ _

end FunctorToFintypeCat
