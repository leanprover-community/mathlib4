/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.CategoryTheory.Galois.GaloisObjects
import Mathlib.CategoryTheory.Limits.Shapes.CombinedProducts
import Mathlib.Data.Finite.Sum

/-!
# Decomposition of objects into connected components and applications

We show that in a Galois category every object is the (finite) coproduct of connected subobjects.
This has many useful corollaries, in particular that the fiber of every object
is represented by a Galois object.

## Main results

* `has_decomp_connected_components`: Every object is the sum of its (finitely many) connected
  components.
* `fiber_in_connected_component`: An element of the fiber of `X` lies in the fiber of some
  connected component.
* `connected_component_unique`: Up to isomorphism, for each element `x` in the fiber of `X` there
  is only one connected component whose fiber contains `x`.
* `exists_galois_representative`: The fiber of `X` is represented by some Galois object `A`:
  Evaluation at some `a` in the fiber of `A` induces a bijection `A ‚ü∂ X` to `F.obj X`.

## References

* [lenstraGSchemes]: H. W. Lenstra. Galois theory for schemes.

-/

universe u‚ÇÅ u‚ÇÇ w

namespace CategoryTheory

open Limits Functor

variable {C : Type u‚ÇÅ} [Category.{u‚ÇÇ} C]

namespace PreGaloisCategory


section Decomposition

/-! ### Decomposition in connected components

To show that an object `X` of a Galois category admits a decomposition into connected objects,
we proceed by induction on the cardinality of the fiber under an arbitrary fiber functor.

If `X` is connected, there is nothing to show. If not, we can write `X` as the sum of two
non-trivial subobjects which have strictly smaller fiber and conclude by the induction hypothesis.

-/

/-- The trivial case if `X` is connected. -/
private lemma has_decomp_connected_components_aux_conn (X : C) [IsConnected X] :
    ‚àÉ (Œπ : Type) (f : Œπ ‚Üí C) (g : (i : Œπ) ‚Üí (f i) ‚ü∂ X) (_ : IsColimit (Cofan.mk X g)),
    (‚àÄ i, IsConnected (f i)) ‚àß Finite Œπ := by
  refine ‚ü®Unit, fun _ ‚Ü¶ X, fun _ ‚Ü¶ ùüô X, mkCofanColimit _ (fun s ‚Ü¶ s.inj ()), ?_‚ü©
  exact ‚ü®fun _ ‚Ü¶ inferInstance, inferInstance‚ü©

/-- The trivial case if `X` is initial. -/
private lemma has_decomp_connected_components_aux_initial (X : C) (h : IsInitial X) :
    ‚àÉ (Œπ : Type) (f : Œπ ‚Üí C) (g : (i : Œπ) ‚Üí (f i) ‚ü∂ X) (_ : IsColimit (Cofan.mk X g)),
    (‚àÄ i, IsConnected (f i)) ‚àß Finite Œπ := by
  refine ‚ü®Empty, fun _ ‚Ü¶ X, fun _ ‚Ü¶ ùüô X, ?_‚ü©
  use mkCofanColimit _ (fun s ‚Ü¶ IsInitial.to h s.pt) (fun s ‚Ü¶ by simp)
    (fun s m _ ‚Ü¶ IsInitial.hom_ext h m _)
  exact ‚ü®by simp only [IsEmpty.forall_iff], inferInstance‚ü©

variable [GaloisCategory C]

/- Show decomposition by inducting on `Nat.card (F.obj X)`. -/
private lemma has_decomp_connected_components_aux (F : C ‚•§ FintypeCat.{w}) [FiberFunctor F]
    (n : ‚Ñï) : ‚àÄ (X : C), n = Nat.card (F.obj X) ‚Üí ‚àÉ (Œπ : Type) (f : Œπ ‚Üí C)
    (g : (i : Œπ) ‚Üí (f i) ‚ü∂ X) (_ : IsColimit (Cofan.mk X g)),
    (‚àÄ i, IsConnected (f i)) ‚àß Finite Œπ := by
  induction n using Nat.strongRecOn with | _ n hi
  intro X hn
  by_cases h : IsConnected X
  ¬∑ exact has_decomp_connected_components_aux_conn X
  by_cases nhi : IsInitial X ‚Üí False
  ¬∑ obtain ‚ü®Y, v, hni, hvmono, hvnoiso‚ü© :=
      has_non_trivial_subobject_of_not_isConnected_of_not_initial X h nhi
    obtain ‚ü®Z, u, ‚ü®c‚ü©‚ü© := PreGaloisCategory.monoInducesIsoOnDirectSummand v
    let t : ColimitCocone (pair Y Z) := { cocone := BinaryCofan.mk v u, isColimit := c }
    have hn1 : Nat.card (F.obj Y) < n := by
      rw [hn]
      exact lt_card_fiber_of_mono_of_notIso F v hvnoiso
    have i : X ‚âÖ Y ‚®ø Z := (colimit.isoColimitCocone t).symm
    have hnn : Nat.card (F.obj X) = Nat.card (F.obj Y) + Nat.card (F.obj Z) := by
      rw [card_fiber_eq_of_iso F i]
      exact card_fiber_coprod_eq_sum F Y Z
    have hn2 : Nat.card (F.obj Z) < n := by
      rw [hn, hnn, lt_add_iff_pos_left]
      exact Nat.pos_of_ne_zero (non_zero_card_fiber_of_not_initial F Y hni)
    let ‚ü®Œπ‚ÇÅ, f‚ÇÅ, g‚ÇÅ, hc‚ÇÅ, hf‚ÇÅ, he‚ÇÅ‚ü© := hi (Nat.card (F.obj Y)) hn1 Y rfl
    let ‚ü®Œπ‚ÇÇ, f‚ÇÇ, g‚ÇÇ, hc‚ÇÇ, hf‚ÇÇ, he‚ÇÇ‚ü© := hi (Nat.card (F.obj Z)) hn2 Z rfl
    refine ‚ü®Œπ‚ÇÅ ‚äï Œπ‚ÇÇ, Sum.elim f‚ÇÅ f‚ÇÇ,
      Cofan.combPairHoms (Cofan.mk Y g‚ÇÅ) (Cofan.mk Z g‚ÇÇ) (BinaryCofan.mk v u), ?_‚ü©
    use Cofan.combPairIsColimit hc‚ÇÅ hc‚ÇÇ c
    refine ‚ü®fun i ‚Ü¶ ?_, inferInstance‚ü©
    cases i
    ¬∑ exact hf‚ÇÅ _
    ¬∑ exact hf‚ÇÇ _
  ¬∑ simp only [not_forall, not_false_eq_true] at nhi
    obtain ‚ü®hi‚ü© := nhi
    exact has_decomp_connected_components_aux_initial X hi

/-- In a Galois category, every object is the sum of connected objects. -/
theorem has_decomp_connected_components (X : C) :
    ‚àÉ (Œπ : Type) (f : Œπ ‚Üí C) (g : (i : Œπ) ‚Üí f i ‚ü∂ X) (_ : IsColimit (Cofan.mk X g)),
      (‚àÄ i, IsConnected (f i)) ‚àß Finite Œπ := by
  let F := GaloisCategory.getFiberFunctor C
  exact has_decomp_connected_components_aux F (Nat.card <| F.obj X) X rfl

/-- In a Galois category, every object is the sum of connected objects. -/
theorem has_decomp_connected_components' (X : C) :
    ‚àÉ (Œπ : Type) (_ : Finite Œπ) (f : Œπ ‚Üí C) (_ : ‚àê f ‚âÖ X), ‚àÄ i, IsConnected (f i) := by
  obtain ‚ü®Œπ, f, g, hl, hc, hf‚ü© := has_decomp_connected_components X
  exact ‚ü®Œπ, hf, f, colimit.isoColimitCocone ‚ü®Cofan.mk X g, hl‚ü©, hc‚ü©

variable (F : C ‚•§ FintypeCat.{w}) [FiberFunctor F]

/-- Every element in the fiber of `X` lies in some connected component of `X`. -/
lemma fiber_in_connected_component (X : C) (x : F.obj X) : ‚àÉ (Y : C) (i : Y ‚ü∂ X) (y : F.obj Y),
    F.map i y = x ‚àß IsConnected Y ‚àß Mono i := by
  obtain ‚ü®Œπ, f, g, hl, hc, he‚ü© := has_decomp_connected_components X
  let s : Cocone (Discrete.functor f ‚ãô F) := F.mapCocone (Cofan.mk X g)
  let s' : IsColimit s := isColimitOfPreserves F hl
  obtain ‚ü®‚ü®j‚ü©, z, h‚ü© := Concrete.isColimit_exists_rep _ s' x
  refine ‚ü®f j, g j, z, ‚ü®?_, hc j, MonoCoprod.mono_inj _ (Cofan.mk X g) hl j‚ü©‚ü©
  subst h
  rfl

/-- Up to isomorphism an element of the fiber of `X` only lies in one connected component. -/
lemma connected_component_unique {X A B : C} [IsConnected A] [IsConnected B] (a : F.obj A)
    (b : F.obj B) (i : A ‚ü∂ X) (j : B ‚ü∂ X) (h : F.map i a = F.map j b) [Mono i] [Mono j] :
    ‚àÉ (f : A ‚âÖ B), F.map f.hom a = b := by
  /- We consider the fiber product of A and B over X. This is a non-empty (because of `h`)
  subobject of `A` and `B` and hence isomorphic to `A` and `B` by connectedness. -/
  let Y : C := pullback i j
  let u : Y ‚ü∂ A := pullback.fst i j
  let v : Y ‚ü∂ B := pullback.snd i j
  let G := F ‚ãô FintypeCat.incl
  let e : F.obj Y ‚âÉ { p : F.obj A √ó F.obj B // F.map i p.1 = F.map j p.2 } :=
    fiberPullbackEquiv F i j
  let y : F.obj Y := e.symm ‚ü®(a, b), h‚ü©
  have hn : IsInitial Y ‚Üí False := not_initial_of_inhabited F y
  have : IsIso u := IsConnected.noTrivialComponent Y u hn
  have : IsIso v := IsConnected.noTrivialComponent Y v hn
  use (asIso u).symm ‚â™‚â´ asIso v
  have hu : G.map u y = a := by
    simp only [u, G, y, e, ‚Üê PreservesPullback.iso_hom_fst G, fiberPullbackEquiv,
      Iso.toEquiv_comp, Equiv.symm_trans_apply, Iso.toEquiv_symm_fun, types_comp_apply,
      inv_hom_id_apply]
    erw [Types.pullbackIsoPullback_inv_fst_apply (F.map i) (F.map j)]
  have hv : G.map v y = b := by
    simp only [v, G, y, e, ‚Üê PreservesPullback.iso_hom_snd G, fiberPullbackEquiv,
      Iso.toEquiv_comp, Equiv.symm_trans_apply, Iso.toEquiv_symm_fun, types_comp_apply,
      inv_hom_id_apply]
    erw [Types.pullbackIsoPullback_inv_snd_apply (F.map i) (F.map j)]
  rw [‚Üê hu, ‚Üê hv]
  change (F.map u ‚â´ F.map _) y = F.map v y
  simp only [‚Üê F.map_comp, Iso.trans_hom, Iso.symm_hom, asIso_inv, asIso_hom,
    IsIso.hom_inv_id_assoc]

end Decomposition

section GaloisRep

/-! ### Galois representative of fiber

If `X` is any object, then its fiber is represented by some Galois object: There exists
a Galois object `A` and an element `a` in the fiber of `A` such that the
evaluation at `a` from `A ‚ü∂ X` to `F.obj X` is bijective.

To show this we consider the product `‚àè·∂ú (fun _ : F.obj X ‚Ü¶ X)` and let `A`
be the connected component whose fiber contains the element `a` in the fiber of the self product
that has at each index `x : F.obj X` the element `x`.

This `A` is Galois and evaluation at `a` is bijective.

Reference: [lenstraGSchemes, 3.14]

-/

variable [GaloisCategory C] (F : C ‚•§ FintypeCat.{w}) [FiberFunctor F]

section GaloisRepAux

variable (X : C)

/-- The self product of `X` indexed by its fiber. -/
@[simp]
private noncomputable def selfProd : C := ‚àè·∂ú (fun _ : F.obj X ‚Ü¶ X)

/-- For `g : F.obj X ‚Üí F.obj X`, this is the element in the fiber of the self product,
which has at index `x : F.obj X` the element `g x`. -/
private noncomputable def mkSelfProdFib : F.obj (selfProd F X) :=
  (PreservesProduct.iso F _).inv ((Concrete.productEquiv (fun _ : F.obj X ‚Ü¶ F.obj X)).symm id)

@[simp]
private lemma mkSelfProdFib_map_œÄ (t : F.obj X) : F.map (Pi.œÄ _ t) (mkSelfProdFib F X) = t := by
  rw [‚Üê congrFun (piComparison_comp_œÄ F _ t), FintypeCat.comp_apply,
    ‚Üê PreservesProduct.iso_hom]
  simp only [mkSelfProdFib, FintypeCat.inv_hom_id_apply]
  exact Concrete.productEquiv_symm_apply_œÄ.{w, w, w+1} (fun _ : F.obj X ‚Ü¶ F.obj X) id t

variable {X} {A : C} (u : A ‚ü∂ selfProd F X)
  (a : F.obj A) (h : F.map u a = mkSelfProdFib F X) {F}
include h

/-- For each `x : F.obj X`, this is the composition of `u` with the projection at `x`. -/
@[simp]
private noncomputable def selfProdProj (x : F.obj X) : A ‚ü∂ X := u ‚â´ Pi.œÄ _ x

variable {u a}

private lemma selfProdProj_fiber (x : F.obj X) :
    F.map (selfProdProj u x) a = x := by
  simp only [selfProdProj, selfProd, F.map_comp, FintypeCat.comp_apply, h]
  rw [mkSelfProdFib_map_œÄ F X x]

variable [IsConnected A]

/-- An element `b : F.obj A` defines a permutation of the fiber of `X` by projecting onto the
`F.map u b` factor. -/
private noncomputable def fiberPerm (b : F.obj A) : F.obj X ‚âÉ F.obj X := by
  let œÉ (t : F.obj X) : F.obj X := F.map (selfProdProj u t) b
  apply Equiv.ofBijective œÉ
  apply Finite.injective_iff_bijective.mp
  intro t s (hs : F.map (selfProdProj u t) b = F.map (selfProdProj u s) b)
  change id t = id s
  have h' : selfProdProj u t = selfProdProj u s := evaluation_injective_of_isConnected F A X b hs
  rw [‚Üê selfProdProj_fiber h s, ‚Üê selfProdProj_fiber h t, h']

/-- Twisting `u` by `fiberPerm h b` yields an inclusion of `A` into `selfProd F X`. -/
private noncomputable def selfProdPermIncl (b : F.obj A) : A ‚ü∂ selfProd F X :=
  u ‚â´ (Pi.whiskerEquiv (fiberPerm h b) (fun _ => Iso.refl X)).inv

private instance [Mono u] (b : F.obj A) : Mono (selfProdPermIncl h b) := mono_comp _ _

/-- Key technical lemma: the twisted inclusion `selfProdPermIncl h b` maps `a` to `F.map u b`. -/
private lemma selfProdTermIncl_fib_eq (b : F.obj A) :
    F.map u b = F.map (selfProdPermIncl h b) a := by
  apply Concrete.Pi.map_ext _ F
  intro (t : F.obj X)
  convert_to F.map (selfProdProj u t) b = _
  ¬∑ simp only [selfProdProj, map_comp, FintypeCat.comp_apply]; rfl
  ¬∑ dsimp only [selfProdPermIncl, Pi.whiskerEquiv]
    rw [map_comp, FintypeCat.comp_apply, h]
    convert_to F.map (selfProdProj u t) b =
      (F.map (Pi.map' (fiberPerm h b) fun _ ‚Ü¶ ùüô X) ‚â´
      F.map (Pi.œÄ (fun _ ‚Ü¶ X) t)) (mkSelfProdFib F X)
    rw [‚Üê map_comp, Pi.map'_comp_œÄ, Category.comp_id, mkSelfProdFib_map_œÄ F X (fiberPerm h b t)]
    rfl

/-- There exists an automorphism `f` of `A` that maps `b` to `a`.
`f` is obtained by considering `u` and `selfProdPermIncl h b`.
Both are inclusions of `A` into `selfProd F X` mapping `b` respectively `a` to the same element
in the fiber of `selfProd F X`. Applying `connected_component_unique` yields the result. -/
private lemma subobj_selfProd_trans [Mono u] (b : F.obj A) : ‚àÉ (f : A ‚âÖ A), F.map f.hom b = a := by
  apply connected_component_unique F b a u (selfProdPermIncl h b)
  exact selfProdTermIncl_fib_eq h b

end GaloisRepAux

/-- The fiber of any object in a Galois category is represented by a Galois object. -/
lemma exists_galois_representative (X : C) : ‚àÉ (A : C) (a : F.obj A),
    IsGalois A ‚àß Function.Bijective (fun (f : A ‚ü∂ X) ‚Ü¶ F.map f a) := by
  obtain ‚ü®A, u, a, h1, h2, h3‚ü© := fiber_in_connected_component F (selfProd F X)
    (mkSelfProdFib F X)
  use A
  use a
  constructor
  ¬∑ refine (isGalois_iff_pretransitive F A).mpr ‚ü®fun x y ‚Ü¶ ?_‚ü©
    obtain ‚ü®fi1, hfi1‚ü© := subobj_selfProd_trans h1 x
    obtain ‚ü®fi2, hfi2‚ü© := subobj_selfProd_trans h1 y
    use fi1 ‚â™‚â´ fi2.symm
    change F.map (fi1.hom ‚â´ fi2.inv) x = y
    simp only [map_comp, FintypeCat.comp_apply]
    rw [hfi1, ‚Üê hfi2]
    exact congr_fun (F.mapIso fi2).hom_inv_id y
  ¬∑ refine ‚ü®evaluation_injective_of_isConnected F A X a, ?_‚ü©
    intro x
    use u ‚â´ Pi.œÄ _ x
    exact (selfProdProj_fiber h1) x

/-- Any element in the fiber of an object `X` is the evaluation of a morphism from a
Galois object. -/
lemma exists_hom_from_galois_of_fiber (X : C) (x : F.obj X) :
    ‚àÉ (A : C) (f : A ‚ü∂ X) (a : F.obj A), IsGalois A ‚àß F.map f a = x := by
  obtain ‚ü®A, a, h1, h2‚ü© := exists_galois_representative F X
  obtain ‚ü®f, hf‚ü© := h2.surjective x
  exact ‚ü®A, f, a, h1, hf‚ü©

/-- Any object with non-empty fiber admits a hom from a Galois object. -/
lemma exists_hom_from_galois_of_fiber_nonempty (X : C) (h : Nonempty (F.obj X)) :
    ‚àÉ (A : C) (_ : A ‚ü∂ X), IsGalois A := by
  obtain ‚ü®x‚ü© := h
  obtain ‚ü®A, f, a, h1, _‚ü© := exists_hom_from_galois_of_fiber F X x
  exact ‚ü®A, f, h1‚ü©

include F in
/-- Any connected object admits a hom from a Galois object. -/
lemma exists_hom_from_galois_of_connected (X : C) [IsConnected X] :
    ‚àÉ (A : C) (_ : A ‚ü∂ X), IsGalois A :=
  exists_hom_from_galois_of_fiber_nonempty F X inferInstance

/-- To check equality of natural transformations `F ‚ü∂ G`, it suffices to check it on
Galois objects. -/
lemma natTrans_ext_of_isGalois {G : C ‚•§ FintypeCat.{w}} {t s : F ‚ü∂ G}
    (h : ‚àÄ (X : C) [IsGalois X], t.app X = s.app X) :
    t = s := by
  ext X x
  obtain ‚ü®A, f, a, _, rfl‚ü© := exists_hom_from_galois_of_fiber F X x
  rw [FunctorToFintypeCat.naturality, FunctorToFintypeCat.naturality, h A]

end GaloisRep

end PreGaloisCategory

end CategoryTheory
