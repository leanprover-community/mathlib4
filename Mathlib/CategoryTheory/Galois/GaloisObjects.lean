/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.CategoryTheory.Galois.Basic
import Mathlib.CategoryTheory.Limits.FintypeCat
import Mathlib.CategoryTheory.Limits.Preserves.Limits
import Mathlib.CategoryTheory.Limits.Shapes.SingleObj
import Mathlib.Logic.Equiv.TransferInstance

/-!
# Galois objects in Galois categories

We define when a connected object of a Galois category `C` is Galois in a fiber functor independent
way and show equivalent characterisations.

## Main definitions

* `IsGalois` : Connected object `X` of `C` such that `X / Aut X` is terminal.

## Main results

* `galois_iff_pretransitive` : A connected object `X` is Galois if and only if `Aut X`
                               acts transitively on `F.obj X` for a fiber functor `F`.

-/
universe u‚ÇÅ u‚ÇÇ v‚ÇÅ v‚ÇÇ v w

namespace CategoryTheory

namespace PreGaloisCategory

open Limits Functor

noncomputable instance {G : Type v} [Group G] [Finite G] :
    PreservesColimitsOfShape (SingleObj G) FintypeCat.incl.{w} := by
  choose G' hg hf e using Finite.exists_type_zero_nonempty_mulEquiv G
  exact Limits.preservesColimitsOfShapeOfEquiv (Classical.choice e).toSingleObjEquiv.symm _

/-- A connected object `X` of `C` is Galois if the quotient `X / Aut X` is terminal. -/
class IsGalois {C : Type u‚ÇÅ} [Category.{u‚ÇÇ, u‚ÇÅ} C] [GaloisCategory C] (X : C)
    extends IsConnected X : Prop where
  quotientByAutTerminal : Nonempty (IsTerminal <| colimit <| SingleObj.functor <| Aut.toEnd X)

variable {C : Type u‚ÇÅ} [Category.{u‚ÇÇ, u‚ÇÅ} C]

/-- The natural action of `Aut X` on `F.obj X`. -/
instance autMulFiber (F : C ‚•§ FintypeCat.{w}) (X : C) : MulAction (Aut X) (F.obj X) where
  smul œÉ a := F.map œÉ.hom a
  one_smul a := by
    show F.map (ùüô X) a = a
    simp only [map_id, FintypeCat.id_apply]
  mul_smul g h a := by
    show F.map (h.hom ‚â´ g.hom) a = (F.map h.hom ‚â´ F.map g.hom) a
    simp only [map_comp, FintypeCat.comp_apply]

variable [GaloisCategory C] (F : C ‚•§ FintypeCat.{w}) [FiberFunctor F]

/-- For a connected object `X` of `C`, the quotient `X / Aut X` is terminal if and only if
the quotient `F.obj X / Aut X` has exactly one element. -/
noncomputable def quotientByAutTerminalEquivUniqueQuotient
    (X : C) [IsConnected X] :
    IsTerminal (colimit <| SingleObj.functor <| Aut.toEnd X) ‚âÉ
    Unique (MulAction.orbitRel.Quotient (Aut X) (F.obj X)) := by
  let J : SingleObj (Aut X) ‚•§ C := SingleObj.functor (Aut.toEnd X)
  let e : (F ‚ãô FintypeCat.incl).obj (colimit J) ‚âÖ _ :=
    preservesColimitIso (F ‚ãô FintypeCat.incl) J ‚â™‚â´
    (Equiv.toIso <| SingleObj.Types.colimitEquivQuotient (J ‚ãô F ‚ãô FintypeCat.incl))
  apply Equiv.trans
  ¬∑ apply (IsTerminal.isTerminalIffObj (F ‚ãô FintypeCat.incl) _).trans
      (isLimitEmptyConeEquiv _ (asEmptyCone _) (asEmptyCone _) e)
  exact Types.isTerminalEquivUnique _

lemma isGalois_iff_aux (X : C) [IsConnected X] :
    IsGalois X ‚Üî Nonempty (IsTerminal <| colimit <| SingleObj.functor <| Aut.toEnd X) :=
  ‚ü®fun h ‚Ü¶ h.quotientByAutTerminal, fun h ‚Ü¶ ‚ü®h‚ü©‚ü©

/-- Given a fiber functor `F` and a connected object `X` of `C`. Then `X` is Galois if and only if
the natural action of `Aut X` on `F.obj X` is transitive. -/
theorem isGalois_iff_pretransitive (X : C) [IsConnected X] :
    IsGalois X ‚Üî MulAction.IsPretransitive (Aut X) (F.obj X) := by
  rw [isGalois_iff_aux, Equiv.nonempty_congr <| quotientByAutTerminalEquivUniqueQuotient F X]
  exact (MulAction.pretransitive_iff_unique_quotient_of_nonempty (Aut X) (F.obj X)).symm

/-- If `X` is Galois, the quotient `X / Aut X` is terminal.  -/
noncomputable def isTerminalQuotientOfIsGalois (X : C) [IsGalois X] :
    IsTerminal <| colimit <| SingleObj.functor <| Aut.toEnd X :=
  Nonempty.some IsGalois.quotientByAutTerminal

/-- If `X` is Galois, then the action of `Aut X` on `F.obj X` is
transitive for every fiber functor `F`. -/
instance isPretransitive_of_isGalois (X : C) [IsGalois X] :
    MulAction.IsPretransitive (Aut X) (F.obj X) := by
  rw [‚Üê isGalois_iff_pretransitive]
  infer_instance

theorem evaluation_aut_surjective_of_isGalois (A : C) [IsGalois A] (a : F.obj A) :
    Function.Surjective (fun f : Aut A ‚Ü¶ F.map f.hom a) :=
  MulAction.IsPretransitive.exists_smul_eq a

theorem evaluation_aut_bijective_of_isGalois (A : C) [IsGalois A] (a : F.obj A) :
    Function.Bijective (fun f : Aut A ‚Ü¶ F.map f.hom a) :=
  ‚ü®evaluation_aut_injective_of_isConnected F A a, evaluation_aut_surjective_of_isGalois F A a‚ü©

/-- For Galois `A` and a point `a` of the fiber of `A`, the evaluation at `A` as an equivalence. -/
noncomputable def evaluationEquivOfIsGalois (A : C) [IsGalois A] (a : F.obj A) : Aut A ‚âÉ F.obj A :=
  Equiv.ofBijective _ (evaluation_aut_bijective_of_isGalois F A a)

@[simp]
lemma evaluationEquivOfIsGalois_apply (A : C) [IsGalois A] (a : F.obj A) (œÜ : Aut A) :
    evaluationEquivOfIsGalois F A a œÜ = F.map œÜ.hom a :=
  rfl

@[simp]
lemma evaluationEquivOfIsGalois_symm_fiber (A : C) [IsGalois A] (a b : F.obj A) :
    F.map ((evaluationEquivOfIsGalois F A a).symm b).hom a = b := by
  change (evaluationEquivOfIsGalois F A a) _ = _
  simp

section AutMap

/-- For a morphism from a connected object `A` to a Galois object `B` and an automorphism
of `A`, there exists a unique automorphism of `B` making the canonical diagram commute. -/
lemma exists_autMap {A B : C} (f : A ‚ü∂ B) [IsConnected A] [IsGalois B] (œÉ : Aut A) :
    ‚àÉ! (œÑ : Aut B), f ‚â´ œÑ.hom = œÉ.hom ‚â´ f := by
  let F := GaloisCategory.getFiberFunctor C
  obtain ‚ü®a‚ü© := nonempty_fiber_of_isConnected F A
  refine ‚ü®?_, ?_, ?_‚ü©
  ¬∑ exact (evaluationEquivOfIsGalois F B (F.map f a)).symm (F.map (œÉ.hom ‚â´ f) a)
  ¬∑ apply evaluation_injective_of_isConnected F A B a
    simp
  ¬∑ intro œÑ hœÑ
    apply evaluation_aut_injective_of_isConnected F B (F.map f a)
    simpa using congr_fun (F.congr_map hœÑ) a

/-- A morphism from a connected object to a Galois object induces a map on automorphism
groups. This is a group homomorphism (see `autMapHom`). -/
noncomputable def autMap {A B : C} [IsConnected A] [IsGalois B] (f : A ‚ü∂ B) (œÉ : Aut A) :
    Aut B :=
  (exists_autMap f œÉ).choose

@[simp]
lemma comp_autMap {A B : C} [IsConnected A] [IsGalois B] (f : A ‚ü∂ B) (œÉ : Aut A) :
    f ‚â´ (autMap f œÉ).hom = œÉ.hom ‚â´ f :=
  (exists_autMap f œÉ).choose_spec.left

@[simp]
lemma comp_autMap_apply {A B : C} [IsConnected A] [IsGalois B] (f : A ‚ü∂ B) (œÉ : Aut A)
    (a : F.obj A) :
    F.map (autMap f œÉ).hom (F.map f a) = F.map f (F.map œÉ.hom a) := by
  simpa [-comp_autMap] using congrFun (F.congr_map (comp_autMap f œÉ)) a

/-- `autMap` is uniquely characterized by making the canonical diagram commute. -/
lemma autMap_unique {A B : C} [IsConnected A] [IsGalois B] (f : A ‚ü∂ B) (œÉ : Aut A)
    (œÑ : Aut B) (h : f ‚â´ œÑ.hom = œÉ.hom ‚â´ f) :
    autMap f œÉ = œÑ :=
  ((exists_autMap f œÉ).choose_spec.right œÑ h).symm

@[simp]
lemma autMap_id {A : C} [IsGalois A] : autMap (ùüô A) = id :=
  funext fun œÉ ‚Ü¶ autMap_unique (ùüô A) œÉ _ (by simp)

@[simp]
lemma autMap_comp {X Y Z : C} [IsConnected X] [IsGalois Y] [IsGalois Z] (f : X ‚ü∂ Y)
    (g : Y ‚ü∂ Z) : autMap (f ‚â´ g) = autMap g ‚àò autMap f := by
  refine funext fun œÉ ‚Ü¶ autMap_unique _ œÉ _ ?_
  rw [Function.comp_apply, Category.assoc, comp_autMap, ‚Üê Category.assoc]
  simp

/-- `autMap` is surjective, if the source is also Galois. -/
lemma autMap_surjective_of_isGalois {A B : C} [IsGalois A] [IsGalois B] (f : A ‚ü∂ B) :
    Function.Surjective (autMap f) := by
  intro œÉ
  let F := GaloisCategory.getFiberFunctor C
  obtain ‚ü®a‚ü© := nonempty_fiber_of_isConnected F A
  obtain ‚ü®a', ha'‚ü© := surjective_of_nonempty_fiber_of_isConnected F f (F.map œÉ.hom (F.map f a))
  obtain ‚ü®œÑ, (hœÑ : F.map œÑ.hom a = a')‚ü© := MulAction.exists_smul_eq (Aut A) a a'
  use œÑ
  apply evaluation_aut_injective_of_isConnected F B (F.map f a)
  simp [hœÑ, ha']

@[simp]
lemma autMap_apply_mul {A B : C} [IsConnected A] [IsGalois B] (f : A ‚ü∂ B) (œÉ œÑ : Aut A) :
    autMap f (œÉ * œÑ) = autMap f œÉ * autMap f œÑ := by
  let F := GaloisCategory.getFiberFunctor C
  obtain ‚ü®a‚ü© := nonempty_fiber_of_isConnected F A
  apply evaluation_aut_injective_of_isConnected F (B : C) (F.map f a)
  simp [Aut.Aut_mul_def]

/-- `MonoidHom` version of `autMap`. -/
@[simps!]
noncomputable def autMapHom {A B : C} [IsConnected A] [IsGalois B] (f : A ‚ü∂ B) :
     Aut A ‚Üí* Aut B :=
  MonoidHom.mk' (autMap f) (autMap_apply_mul f)

end AutMap

end PreGaloisCategory

end CategoryTheory
