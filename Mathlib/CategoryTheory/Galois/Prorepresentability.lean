/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.Algebra.Category.GroupCat.Limits
import Mathlib.CategoryTheory.CofilteredSystem
import Mathlib.CategoryTheory.Galois.Decomposition
import Mathlib.CategoryTheory.Limits.FunctorCategory
import Mathlib.CategoryTheory.Limits.IndYoneda

/-!
# Pro-Representability of fiber functors

We show that any fiber functor is pro-representable, i.e. there exists a pro-object
`X : I ‚•§ C` such that `F` is naturally isomorphic to `X ‚ãô coyoneda`.

From this we deduce the canonical isomorphism of `Aut F` with the limit over the automorphism
groups of all Galois objects.

## Main definitions

- `PointedGaloisObject`: the category of pointed Galois objects
- `PointedGaloisObject.cocone`: a cocone on `(PointedGaloisObject.incl F).op ‚â´ coyoneda` with
  point `F ‚ãô FintypeCat.incl`.
- `autGaloisSystem`: the system of automorphism groups indexed by the pointed Galois objects.

## Main results

- `PointedGaloisObject.isColimit`: the cocone `PointedGaloisObject.cocone` is a colimit cocone.
- `autMulEquivAutGalois`: `Aut F` is canonically isomorphic to the limit over the automorphism
  groups of all Galois objects.
- `FiberFunctor.isPretransitive_of_isConnected`: The `Aut F` action on the fiber of a connected
  object is transitive.

## References

* [lenstraGSchemes]: H. W. Lenstra. Galois theory for schemes.

-/

universe u‚ÇÅ u‚ÇÇ w

namespace CategoryTheory

namespace PreGaloisCategory

open Limits Functor

variable {C : Type u‚ÇÅ} [Category.{u‚ÇÇ} C] [GaloisCategory C]
variable (F : C ‚•§ FintypeCat.{u‚ÇÇ}) [FiberFunctor F]

/-- A pointed Galois object is a Galois object with a fixed point of its fiber. -/
structure PointedGaloisObject : Type (max u‚ÇÅ u‚ÇÇ) where
  /-- The underlying object of `C`. -/
  obj : C
  /-- An element of the fiber of `obj`. -/
  pt : F.obj obj
  /-- `obj` is Galois. -/
  isGalois : IsGalois obj := by infer_instance

namespace PointedGaloisObject

attribute [instance] isGalois

instance (X : PointedGaloisObject F) : CoeDep (PointedGaloisObject F) X C where
  coe := X.obj

variable {F} in
/-- The type of homomorphisms between two pointed Galois objects. This is a homomorphism
of the underlying objects of `C` that maps the distinguished points to each other. -/
structure Hom (A B : PointedGaloisObject F) where
  /-- The underlying homomorphism of `C`. -/
  val : A.obj ‚ü∂ B.obj
  /-- The distinguished point of `A` is mapped to the distinguished point of `B`. -/
  comp : F.map val A.pt = B.pt

/-- The category of pointed Galois objects. -/
instance : Category.{u‚ÇÇ} (PointedGaloisObject F) where
  Hom A B := Hom A B
  id A := ‚ü®ùüô (A : C), by simp‚ü©
  comp {A B C} f g := by
    refine ‚ü®f.val ‚â´ g.val, ?_‚ü©
    simp only [F.map_comp, FintypeCat.comp_apply, f.comp, g.comp]

instance {A B : PointedGaloisObject F} : Coe (Hom A B) (A.obj ‚ü∂ B.obj) where
  coe f := f.val

variable {F}

@[ext]
lemma Hom.ext {A B : PointedGaloisObject F} {f g : A ‚ü∂ B} (_ : f.val = g.val) : f = g :=
  match f, g with | ‚ü®_, _‚ü©, ‚ü®_, _‚ü© => by congr

@[simp]
lemma Hom.map_point {A B : PointedGaloisObject F} (f : A ‚ü∂ B) :
    F.map f A.pt = B.pt :=
  f.comp

@[simp]
lemma id_val (A : PointedGaloisObject F) : ùüô A = ùüô A.obj :=
  rfl

@[simp]
lemma comp_val {A B C : PointedGaloisObject F} (f : A ‚ü∂ B) (g : B ‚ü∂ C) :
    (f ‚â´ g).val = f.val ‚â´ g.val :=
  rfl

variable (F)

/-- The category of pointed Galois objects is cofiltered. -/
instance : IsCofilteredOrEmpty (PointedGaloisObject F) where
  cone_objs := fun ‚ü®A, a, _‚ü© ‚ü®B, b, _‚ü© ‚Ü¶ by
    obtain ‚ü®Z, f, z, hgal, hfz‚ü© := exists_hom_from_galois_of_fiber F (A ‚®Ø B)
      <| (fiberBinaryProductEquiv F A B).symm (a, b)
    refine ‚ü®‚ü®Z, z, hgal‚ü©, ‚ü®f ‚â´ prod.fst, ?_‚ü©, ‚ü®f ‚â´ prod.snd, ?_‚ü©, trivial‚ü©
    ¬∑ simp only [F.map_comp, hfz, FintypeCat.comp_apply, fiberBinaryProductEquiv_symm_fst_apply]
    ¬∑ simp only [F.map_comp, hfz, FintypeCat.comp_apply, fiberBinaryProductEquiv_symm_snd_apply]
  cone_maps := fun ‚ü®A, a, _‚ü© ‚ü®B, b, _‚ü© ‚ü®f, hf‚ü© ‚ü®g, hg‚ü© ‚Ü¶ by
    obtain ‚ü®Z, h, z, hgal, hhz‚ü© := exists_hom_from_galois_of_fiber F A a
    refine ‚ü®‚ü®Z, z, hgal‚ü©, ‚ü®h, hhz‚ü©, Hom.ext ?_‚ü©
    apply evaluationInjective_of_isConnected F Z B z
    show F.map (h ‚â´ f) z = F.map (h ‚â´ g) z
    simp only [map_comp, FintypeCat.comp_apply, hhz, hf, hg]

/-- The canonical functor from pointed Galois objects to `C`. -/
def incl : PointedGaloisObject F ‚•§ C where
  obj := fun A ‚Ü¶ A
  map := fun ‚ü®f, _‚ü© ‚Ü¶ f

@[simp]
lemma incl_obj (A : PointedGaloisObject F) : (incl F).obj A = A :=
  rfl

@[simp]
lemma incl_map {A B : PointedGaloisObject F} (f : A ‚ü∂ B) : ((incl F).map f) = f.val :=
  rfl

/-- `F ‚ãô FintypeCat.incl` as a cocone over `(can F).op ‚ãô coyoneda`.
This is a colimit cocone (see `PreGaloisCategory.isColim√¨t`) -/
def cocone : Cocone ((incl F).op ‚ãô coyoneda) where
  pt := F ‚ãô FintypeCat.incl
  Œπ := {
    app := fun ‚ü®A, a, _‚ü© ‚Ü¶ { app := fun X (f : (A : C) ‚ü∂ X) ‚Ü¶ F.map f a }
    naturality := fun ‚ü®A, a, _‚ü© ‚ü®B, b, _‚ü© ‚ü®f, (hf : F.map f b = a)‚ü© ‚Ü¶ by
      ext Y (g : (A : C) ‚ü∂ Y)
      suffices h : F.map g (F.map f b) = F.map g a by
        simpa
      rw [hf]
  }

@[simp]
lemma cocone_app (A : PointedGaloisObject F) (B : C) (f : (A : C) ‚ü∂ B) :
    ((cocone F).Œπ.app ‚ü®A‚ü©).app B f = F.map f A.pt :=
  rfl

/-- `cocone F` is a colimit cocone, i.e. `F` is pro-represented by `incl F`. -/
noncomputable def isColimit : IsColimit (cocone F) := by
  refine evaluationJointlyReflectsColimits _ (fun X ‚Ü¶ ?_)
  refine Types.FilteredColimit.isColimitOf _ _ ?_ ?_
  ¬∑ intro (x : F.obj X)
    obtain ‚ü®Y, i, y, h1, _, _‚ü© := fiber_in_connected_component F X x
    obtain ‚ü®Z, f, z, hgal, hfz‚ü© := exists_hom_from_galois_of_fiber F Y y
    refine ‚ü®‚ü®Z, z, hgal‚ü©, f ‚â´ i, ?_‚ü©
    simp only [mapCocone_Œπ_app, evaluation_obj_map, cocone_app, map_comp,
      ‚Üê h1, FintypeCat.comp_apply, hfz]
  ¬∑ intro ‚ü®A, a, _‚ü© ‚ü®B, b, _‚ü© (u : (A : C) ‚ü∂ X) (v : (B : C) ‚ü∂ X) (h : F.map u a = F.map v b)
    obtain ‚ü®‚ü®Z, z, _‚ü©, ‚ü®f, hf‚ü©, ‚ü®g, hg‚ü©, _‚ü© :=
      @IsFilteredOrEmpty.cocone_objs (PointedGaloisObject F)·µí·µñ _ _
      ‚ü®{ obj := A, pt := a}‚ü© ‚ü®{obj := B, pt := b}‚ü©
    refine ‚ü®‚ü®{ obj := Z, pt := z }‚ü©, ‚ü®f, hf‚ü©, ‚ü®g, hg‚ü©, ?_‚ü©
    apply evaluationInjective_of_isConnected F Z X z
    change F.map (f ‚â´ u) z = F.map (g ‚â´ v) z
    rw [map_comp, FintypeCat.comp_apply, hf, map_comp, FintypeCat.comp_apply, hg, h]

instance : HasColimit ((incl F).op ‚ãô coyoneda) where
  exists_colimit := ‚ü®cocone F, isColimit F‚ü©

variable {F}

/-- A morphism of pointed Galois objects induces a map on automorphism groups
of the underlying objects in `C`. This is a group homomorphism (see `autMapMul`). -/
noncomputable def autMap {A B : PointedGaloisObject F} (f : A ‚ü∂ B) (œÉ : Aut A.obj) : Aut B.obj :=
  (evaluationEquivOfIsGalois F B B.pt).symm (F.map (œÉ.hom ‚â´ f) A.pt)

@[simp]
lemma autMap_eval {A B : PointedGaloisObject F} (f : A ‚ü∂ B) (œÉ : Aut A.obj) :
    F.map (autMap f œÉ).hom B.pt = F.map f (F.map œÉ.hom A.pt) := by
  simp [autMap]

lemma autMap_surjective {A B : PointedGaloisObject F} (f : A ‚ü∂ B) :
    Function.Surjective (autMap f) := by
  intro œÉ
  obtain ‚ü®a', ha'‚ü© := surjective_of_nonempty_fiber_of_isConnected F f.val (F.map œÉ.hom B.pt)
  obtain ‚ü®œÑ, (hœÑ : F.map œÑ.hom A.pt = a')‚ü© := MulAction.exists_smul_eq (Aut A.obj) A.pt a'
  use œÑ
  apply evaluation_aut_injective_of_isConnected F B B.pt
  simp [hœÑ, ha']

@[simp]
lemma comp_autMap {A B : PointedGaloisObject F} (f : A ‚ü∂ B) (œÉ : Aut A.obj) :
    f.val ‚â´ (autMap f œÉ).hom = œÉ.hom ‚â´ f := by
  apply evaluationInjective_of_isConnected F A B A.pt
  simp

@[simp]
lemma comp_autMap_apply {A B : PointedGaloisObject F} (f : A ‚ü∂ B) (œÉ : Aut A.obj) (a : F.obj A) :
    F.map (autMap f œÉ).hom (F.map f.val a) = F.map f.val (F.map œÉ.hom a) := by
  simpa [-comp_autMap] using congrFun (congrArg F.map (comp_autMap f œÉ)) a

@[simp]
lemma autMap_apply_mul {A B : PointedGaloisObject F} (f : A ‚ü∂ B) (œÉ œÑ : Aut A.obj) :
    autMap f (œÉ * œÑ) = autMap f œÉ * autMap f œÑ := by
  apply evaluation_aut_injective_of_isConnected F (B : C) B.pt
  simp [Aut.Aut_mul_def]

/-- `MonoidHom` version of `autMap`. -/
noncomputable def autMapMul {A B : PointedGaloisObject F} (f : A ‚ü∂ B) :
     Aut (A : C) ‚Üí* Aut (B : C) :=
  MonoidHom.mk' (autMap f) (autMap_apply_mul f)

end PointedGaloisObject

open PointedGaloisObject

/-- The diagram sending each pointed Galois object to its automorphism group
as an object of `C`. -/
noncomputable def autGaloisSystem : PointedGaloisObject F ‚•§ GroupCat.{u‚ÇÇ} where
  obj := fun A ‚Ü¶ GroupCat.of <| Aut (A : C)
  map := fun {A B} f ‚Ü¶ (autMapMul f : Aut (A : C) ‚Üí* Aut (B : C))
  map_id := fun A ‚Ü¶ by
    ext (œÉ : Aut (A : C))
    show autMap (ùüô A) œÉ = œÉ
    apply evaluation_aut_injective_of_isConnected F A A.pt
    simp
  map_comp {A B C} f g := by
    ext (œÉ : Aut A.obj)
    show autMap (f ‚â´ g) œÉ = autMap g (autMap f œÉ)
    apply evaluation_aut_injective_of_isConnected F C C.pt
    simp

/-- `autGaloisSystem` but lifted to a bigger universe. This is needed to compute its limit. -/
noncomputable def autGaloisSystem' : PointedGaloisObject F ‚•§ GroupCat.{max u‚ÇÅ u‚ÇÇ} :=
  autGaloisSystem F ‚ãô GroupCat.uliftFunctor.{u‚ÇÇ, u‚ÇÅ}

@[simp]
lemma autGaloisSystem'_map {A B : PointedGaloisObject F} (f : A ‚ü∂ B) (œÜ : Aut (A : C)) :
    ((autGaloisSystem' F).map f) ‚ü®œÜ‚ü© = ‚ü®autMapMul f œÜ‚ü© :=
  rfl

/-- The limit of `autGaloisSystem` computed in `GroupCat.{max u‚ÇÅ u‚ÇÇ}`. -/
noncomputable def autGalois : GroupCat.{max u‚ÇÅ u‚ÇÇ} := limit (autGaloisSystem' F)

/-- The canonical projection from `autGalois F` to the `C`-automorphism group of each
pointed Galois object. -/
noncomputable def autGalois.œÄ (A : PointedGaloisObject F) : autGalois F ‚Üí* Aut (A : C) :=
  MonoidHom.comp MulEquiv.ulift.toMonoidHom (limit.œÄ (autGaloisSystem' F) A)

/- Not a `simp` lemma, because we usually don't want to expose the internals here. -/
lemma autGalois.œÄ_apply (A : PointedGaloisObject F) (x : autGalois F) :
    autGalois.œÄ F A x = Equiv.ulift (limit.œÄ (autGaloisSystem' F) A x) :=
  rfl

lemma autGaloisSystem'_map_surjective ‚¶ÉA B : PointedGaloisObject F‚¶Ñ (f : A ‚ü∂ B) :
    Function.Surjective ((autGaloisSystem' F).map f) := by
  intro ‚ü®(œÜ : Aut B.obj)‚ü©
  obtain ‚ü®œà, hœà‚ü© := autMap_surjective f œÜ
  use ‚ü®œà‚ü©
  simp only [autGaloisSystem'_map]
  apply ULift.ext
  exact hœà

/-- `autGalois.œÄ` is surjective for every pointed Galois object. -/
theorem autGalois.œÄ_surjective (A : PointedGaloisObject F) :
    Function.Surjective (autGalois.œÄ F A) := fun (œÉ : Aut A.obj) ‚Ü¶ by
  have (i : PointedGaloisObject F) : Finite ((autGaloisSystem' F ‚ãô forget _).obj i) :=
    inferInstanceAs <| Finite (ULift (Aut (i.obj)))
  obtain ‚ü®s', hs‚ü© := eval_section_surjective_of_surjective
    (autGaloisSystem' F ‚ãô forget _) (autGaloisSystem'_map_surjective F) A ‚ü®œÉ‚ü©
  simp only [comp_obj] at hs
  use (preservesLimitIso (forget GroupCat) (autGaloisSystem' F)).inv
    ((Types.limitEquivSections (autGaloisSystem' F ‚ãô forget _)).symm s')
  dsimp [autGalois.œÄ]
  change MulEquiv.ulift (((preservesLimitIso _ _).inv
    ‚â´ (forget _).map (limit.œÄ (autGaloisSystem' F) A)) _) = œÉ
  simp only [preservesLimitsIso_inv_œÄ, comp_obj, Types.limitEquivSections_symm_apply, hs]
  rfl

/-- Equality of elements of `autGalois F` can be checked on the projections on each pointed
Galois object. -/
lemma autGalois_ext {f g : autGalois F}
    (h : ‚àÄ (A : PointedGaloisObject F), autGalois.œÄ F A f = autGalois.œÄ F A g) : f = g :=
  Concrete.limit_ext (autGaloisSystem' F) f g (fun A ‚Ü¶ EquivLike.injective MulEquiv.ulift (h A))

section EndAutGaloisIsomorphism

/-!

### Isomorphism between `Aut F` and `autGalois F`

In this section we establish the isomorphism between the automorphism group of `F` and
the limit over the automorphism groups of all Galois objects.

We first establish the isomorphism between `End F` and `autGalois F`, from which we deduce that
`End F` is a group, hence `End F = Aut F`. The isomorphism is built in multiple steps:

- `endIsoLimitFibers : End F ‚âÖ limit (incl F ‚ãô F' ‚ãô uliftFunctor.{u‚ÇÅ})`: the endomorphisms of
  `F` are isomorphic to the limit over `F.obj A` for all Galois objects `A`.
  This is obtained as the composition (slighty simplified):

  `End F ‚âÖ (colimit ((incl F).op ‚ãô coyoneda) ‚ü∂ F) ‚âÖ limit (incl F ‚ãô F)`

  Where the first isomorphism is induced from the pro-representability of `F` and the second one
  from the pro-coyoneda lemma.

- `endIsoAutGalois : End F ‚âÖ autGalois F`: this is the composition of `endIsoLimitFibers` with:

  `limit (incl F ‚ãô F) ‚âÖ limit (autGaloisSystem' F ‚ãô forget GroupCat)`

  which is induced from the level-wise equivalence `Aut A ‚âÉ F.obj A` for a Galois object `A`.

-/

-- Local notation for `F` considered as a functor to types instead of finite types.
local notation "F'" => F ‚ãô FintypeCat.incl

/-- The endomorphisms of `F` are isomorphic to the limit over the fibers of `F` on all
Galois objects. -/
noncomputable def endIsoLimitFibers : End F ‚âÖ limit (incl F ‚ãô F' ‚ãô uliftFunctor.{u‚ÇÅ}) :=
  let i1 : End F ‚âÖ End F' :=
    Equiv.toIso (NatTrans.equivOfCompFullyFaithful FintypeCat.incl)
  let i2 : End F' ‚âÖ (colimit ((incl F).op ‚ãô coyoneda) ‚ü∂ F') :=
    (yoneda.obj (F ‚ãô FintypeCat.incl)).mapIso (colimit.isoColimitCocone ‚ü®cocone F, isColimit F‚ü©).op
  let i3 : (colimit ((incl F).op ‚ãô coyoneda) ‚ü∂ F') ‚âÖ limit (incl F ‚ãô F' ‚ãô uliftFunctor.{u‚ÇÅ}) :=
    colimitCoyonedaHomIsoLimit' (incl F) F'
  i1 ‚â™‚â´ i2 ‚â™‚â´ i3

@[simp]
lemma endIsoLimitFibers_œÄ (f : End F) (A : PointedGaloisObject F) :
    limit.œÄ (incl F ‚ãô F' ‚ãô uliftFunctor.{u‚ÇÅ}) A ((endIsoLimitFibers F).hom f) = ‚ü®f.app A A.pt‚ü© := by
  apply ULift.ext
  simp [endIsoLimitFibers]
  change ((NatTrans.equivOfCompFullyFaithful FintypeCat.incl) f).app A
    (((colimit.Œπ _ _) ‚â´ (colimit.isoColimitCocone ‚ü®cocone F, isColimit F‚ü©).hom).app
      A _) = f.app A A.pt
  simp

/-- Functorial isomorphism `Aut A ‚âÖ F.obj A` for Galois objects `A`. -/
noncomputable def autIsoFibers :
    autGaloisSystem' F ‚ãô forget GroupCat.{max u‚ÇÅ u‚ÇÇ} ‚âÖ incl F ‚ãô F' ‚ãô uliftFunctor.{u‚ÇÅ} :=
  NatIso.ofComponents (fun A ‚Ü¶ ((Equiv.ulift.{u‚ÇÅ, u‚ÇÇ}).trans
      ((evaluationEquivOfIsGalois F A A.pt).trans Equiv.ulift.{u‚ÇÅ, u‚ÇÇ}.symm)).toIso)
    (fun {A B} f ‚Ü¶ by
      ext ‚ü®œÜ : Aut A.obj‚ü©
      apply ULift.ext
      dsimp
      erw [Equiv.ulift_symm_down, Equiv.ulift_symm_down, Equiv.apply_symm_apply]
      simp)

lemma autIsoFibers_inv_app (A : PointedGaloisObject F) (b : F.obj A) :
    (autIsoFibers F).inv.app A ‚ü®b‚ü© = ‚ü®(evaluationEquivOfIsGalois F A A.pt).symm b‚ü© :=
  rfl

/-- The isomorphism between endomorphisms of `F` and the limit over the automorphism groups
of all Galois objects. -/
noncomputable def endIsoAutGalois : End F ‚âÖ autGalois F := by
  let i1 := endIsoLimitFibers F
  let i2 := lim.mapIso (autIsoFibers F).symm
  let i3 := (preservesLimitIso (forget GroupCat.{max u‚ÇÅ u‚ÇÇ}) (autGaloisSystem' F)).symm
  exact i1 ‚â™‚â´ i2 ‚â™‚â´ i3

/-- `endIsoAutGalois` as an equiv. -/
noncomputable def endEquivAutGalois : End F ‚âÉ autGalois F := (endIsoAutGalois F).toEquiv

lemma endEquivAutGalois_œÄ (f : End F) (A : PointedGaloisObject F) :
    F.map (autGalois.œÄ F A (endEquivAutGalois F f)).hom A.2 = f.app A A.pt := by
  dsimp [endEquivAutGalois, endIsoAutGalois, autGalois.œÄ_apply]
  change F.map (Equiv.ulift (((preservesLimitIso (forget GroupCat) (autGaloisSystem' F)).inv
      ‚â´ (forget GroupCat).map (limit.œÄ (autGaloisSystem' F) A)) _)).hom A.pt = f.app A.obj A.pt
  rw [preservesLimitsIso_inv_œÄ, Types.Limit.map_œÄ_apply', endIsoLimitFibers_œÄ, Equiv.ulift_apply,
    autIsoFibers_inv_app, evaluationEquivOfIsGalois_symm_fiber]

@[simp]
theorem endEquivAutGalois_mul (f g : End F) :
    (endEquivAutGalois F) (g ‚â´ f) = (endEquivAutGalois F g) * (endEquivAutGalois F f) := by
  refine autGalois_ext F (fun A ‚Ü¶ evaluation_aut_injective_of_isConnected F A A.pt ?_)
  simp only [map_mul, endEquivAutGalois_œÄ, Aut.Aut_mul_def, NatTrans.comp_app, Iso.trans_hom]
  simp only [map_comp, FintypeCat.comp_apply, endEquivAutGalois_œÄ]
  change f.app A (g.app A A.pt) =
    (f.app A ‚â´ F.map ((autGalois.œÄ F A) ((endEquivAutGalois F) g)).hom) A.pt
  rw [‚Üê f.naturality, FintypeCat.comp_apply, endEquivAutGalois_œÄ]

/-- The monoid isomorphism between endomorphisms of `F` and the (multiplicative oppososite of the)
limit of automorphism groups of all Galois objects. -/
noncomputable def endMulEquivAutGalois : End F ‚âÉ* (autGalois F)·µê·µí·µñ :=
  MulEquiv.mk (Equiv.trans (endEquivAutGalois F) MulOpposite.opEquiv) (by simp)

lemma endMulEquivAutGalois_pi (f : End F) (A : PointedGaloisObject F) :
    F.map (autGalois.œÄ F A (endMulEquivAutGalois F f).unop).hom A.2 = f.app A A.pt :=
  endEquivAutGalois_œÄ F f A

/-- Any endomorphism of a fiber functor is a unit. -/
theorem FibreFunctor.end_isUnit (f : End F) : IsUnit f :=
  (MulEquiv.map_isUnit_iff (endMulEquivAutGalois F)).mp
    (Group.isUnit ((endMulEquivAutGalois F) f))

/-- Any endomorphism of a fiber functor is an isomorphism. -/
instance FibreFunctor.end_isIso (f : End F) : IsIso f := by
  rw [‚Üê isUnit_iff_isIso]
  exact FibreFunctor.end_isUnit F f

/-- The automorphism group of `F` is multiplicatively isomorphic to
(the multiplicative opposite of) the limit over the automorphism groups of
the Galois objects. -/
noncomputable def autMulEquivAutGalois : Aut F ‚âÉ* (autGalois F)·µê·µí·µñ where
  toFun := MonoidHom.comp (endMulEquivAutGalois F) (Aut.toEnd F)
  invFun t := asIso ((endMulEquivAutGalois F).symm t)
  left_inv t := by
    simp only [MonoidHom.coe_comp, MonoidHom.coe_coe, Function.comp_apply,
      MulEquiv.symm_apply_apply]
    exact Aut.ext rfl
  right_inv t := by
    simp only [MonoidHom.coe_comp, MonoidHom.coe_coe, Function.comp_apply, Aut.toEnd_apply]
    exact (MulEquiv.eq_symm_apply (endMulEquivAutGalois F)).mp rfl
  map_mul' := by simp

lemma autMulEquivAutGalois_œÄ (f : Aut F) (A : C) [IsGalois A] (a : F.obj A) :
    F.map (autGalois.œÄ F { obj := A, pt := a } (autMulEquivAutGalois F f).unop).hom a
      = f.hom.app A a := by
  dsimp [autMulEquivAutGalois, endMulEquivAutGalois]
  rw [endEquivAutGalois_œÄ]
  rfl

@[simp]
lemma autMulEquivAutGalois_symm_app (x : autGalois F) (A : C) [IsGalois A] (a : F.obj A) :
    ((autMulEquivAutGalois F).symm ‚ü®x‚ü©).hom.app A a =
      F.map (autGalois.œÄ F ‚ü®A, a, inferInstance‚ü© x).hom a := by
  rw [‚Üê autMulEquivAutGalois_œÄ, MulEquiv.apply_symm_apply]
  rfl

end EndAutGaloisIsomorphism

/-- The `Aut F` action on the fiber of a Galois object is transitive. See
`pretransitive_of_isConnected` for the same result for connected objects. -/
theorem FiberFunctor.isPretransitive_of_isGalois (X : C) [IsGalois X] :
    MulAction.IsPretransitive (Aut F) (F.obj X) := by
  refine ‚ü®fun x y ‚Ü¶ ?_‚ü©
  obtain ‚ü®(œÜ : Aut X), h‚ü© := MulAction.IsPretransitive.exists_smul_eq (M := Aut X) x y
  obtain ‚ü®a, ha‚ü© := autGalois.œÄ_surjective F ‚ü®X, x, inferInstance‚ü© œÜ
  use (autMulEquivAutGalois F).symm ‚ü®a‚ü©
  simpa [mulAction_def, ha]

/-- The `Aut F` action on the fiber of a connected object is transitive. -/
instance FiberFunctor.isPretransitive_of_isConnected (X : C) [IsConnected X] :
    MulAction.IsPretransitive (Aut F) (F.obj X) := by
  obtain ‚ü®A, f, hgal‚ü© := exists_hom_from_galois_of_connected F X
  have hs : Function.Surjective (F.map f) := surjective_of_nonempty_fiber_of_isConnected F f
  refine ‚ü®fun x y ‚Ü¶ ?_‚ü©
  obtain ‚ü®a, ha‚ü© := hs x
  obtain ‚ü®b, hb‚ü© := hs y
  have : MulAction.IsPretransitive (Aut F) (F.obj A) := isPretransitive_of_isGalois F A
  obtain ‚ü®œÉ, (hœÉ : œÉ.hom.app A a = b)‚ü© := MulAction.exists_smul_eq (Aut F) a b
  use œÉ
  rw [‚Üê ha, ‚Üê hb]
  show (F.map f ‚â´ œÉ.hom.app X) a = F.map f b
  rw [œÉ.hom.naturality, FintypeCat.comp_apply, hœÉ]

end PreGaloisCategory

end CategoryTheory
