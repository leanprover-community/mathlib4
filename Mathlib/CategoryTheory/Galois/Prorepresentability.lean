/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.Algebra.Category.GroupCat.Basic
import Mathlib.CategoryTheory.Galois.Decomposition
import Mathlib.CategoryTheory.Limits.FunctorCategory

/-!
# Pro-Representability of fiber functors

We show that any fiber functor is pro-representable, i.e. there exists a pro-object
`X : I â¥¤ C` such that `F` is naturally isomorphic to `X â‹™ coyoneda`.

## Main definitions

- `PointedGaloisObject`: the category of pointed Galois objects
- `PointedGaloisObject.cocone`: a cocone on `(PointedGaloisObject.incl F).op â‰« coyoneda` with
  point `F â‹™ FintypeCat.incl`.
- `autGaloisSystem`: the system of automorphism groups indexed by the pointed Galois objects.

## Main results

- `PointedGaloisObject.isColimit`: the cocone `PointedGaloisObject.cocone` is a colimit cocone.

## References

* [lenstraGSchemes]: H. W. Lenstra. Galois theory for schemes.

-/

universe uâ‚ uâ‚‚ w

namespace CategoryTheory

namespace PreGaloisCategory

open Limits Functor

variable {C : Type uâ‚} [Category.{uâ‚‚} C] [GaloisCategory C]
variable (F : C â¥¤ FintypeCat.{uâ‚‚}) [FiberFunctor F]

/-- A pointed Galois object is a Galois object with a fixed point of its fiber. -/
structure PointedGaloisObject : Type (max uâ‚ uâ‚‚) where
  /-- The underlying object of `C`. -/
  obj : C
  /-- An element of the fiber of `obj`. -/
  pt : F.obj obj
  /-- `obj` is Galois. -/
  isGalois : IsGalois obj := by infer_instance

namespace PointedGaloisObject

attribute [instance] isGalois

instance (X : PointedGaloisObject F) : CoeDep (PointedGaloisObject F) X C where
  coe := X.obj

variable {F} in
/-- The type of homomorphisms between two pointed Galois objects. This is a homomorphism
of the underlying objects of `C` that maps the distinguished points to each other. -/
structure Hom (A B : PointedGaloisObject F) where
  /-- The underlying homomorphism of `C`. -/
  val : A.obj âŸ¶ B.obj
  /-- The distinguished point of `A` is mapped to the distinguished point of `B`. -/
  comp : F.map val A.pt = B.pt

/-- The category of pointed Galois objects. -/
instance : Category.{uâ‚‚} (PointedGaloisObject F) where
  Hom A B := Hom A B
  id A := âŸ¨ğŸ™ (A : C), by simpâŸ©
  comp {A B C} f g := by
    refine âŸ¨f.val â‰« g.val, ?_âŸ©
    simp only [F.map_comp, FintypeCat.comp_apply, f.comp, g.comp]

instance {A B : PointedGaloisObject F} : Coe (Hom A B) (A.obj âŸ¶ B.obj) where
  coe f := f.val

variable {F}

@[ext]
lemma Hom.ext {A B : PointedGaloisObject F} {f g : A âŸ¶ B} (_ : f.val = g.val) : f = g :=
  match f, g with | âŸ¨_, _âŸ©, âŸ¨_, _âŸ© => by congr

@[simp]
lemma Hom.map_point {A B : PointedGaloisObject F} (f : A âŸ¶ B) :
    F.map f A.pt = B.pt :=
  f.comp

@[simp]
lemma id_val (A : PointedGaloisObject F) : ğŸ™ A = ğŸ™ A.obj :=
  rfl

@[simp]
lemma comp_val {A B C : PointedGaloisObject F} (f : A âŸ¶ B) (g : B âŸ¶ C) :
    (f â‰« g).val = f.val â‰« g.val :=
  rfl

variable (F)

/-- The category of pointed Galois objects is cofiltered. -/
instance : IsCofilteredOrEmpty (PointedGaloisObject F) where
  cone_objs := fun âŸ¨A, a, _âŸ© âŸ¨B, b, _âŸ© â†¦ by
    obtain âŸ¨Z, f, z, hgal, hfzâŸ© := exists_hom_from_galois_of_fiber F (A â¨¯ B)
      <| (fiberBinaryProductEquiv F A B).symm (a, b)
    refine âŸ¨âŸ¨Z, z, hgalâŸ©, âŸ¨f â‰« prod.fst, ?_âŸ©, âŸ¨f â‰« prod.snd, ?_âŸ©, trivialâŸ©
    Â· simp only [F.map_comp, hfz, FintypeCat.comp_apply, fiberBinaryProductEquiv_symm_fst_apply]
    Â· simp only [F.map_comp, hfz, FintypeCat.comp_apply, fiberBinaryProductEquiv_symm_snd_apply]
  cone_maps := fun âŸ¨A, a, _âŸ© âŸ¨B, b, _âŸ© âŸ¨f, hfâŸ© âŸ¨g, hgâŸ© â†¦ by
    obtain âŸ¨Z, h, z, hgal, hhzâŸ© := exists_hom_from_galois_of_fiber F A a
    refine âŸ¨âŸ¨Z, z, hgalâŸ©, âŸ¨h, hhzâŸ©, Hom.ext ?_âŸ©
    apply evaluationInjective_of_isConnected F Z B z
    show F.map (h â‰« f) z = F.map (h â‰« g) z
    simp only [map_comp, FintypeCat.comp_apply, hhz, hf, hg]

/-- The canonical functor from pointed Galois objects to `C`. -/
def incl : PointedGaloisObject F â¥¤ C where
  obj := fun A â†¦ A
  map := fun âŸ¨f, _âŸ© â†¦ f

@[simp]
lemma incl_obj (A : PointedGaloisObject F) : (incl F).obj A = A :=
  rfl

@[simp]
lemma incl_map {A B : PointedGaloisObject F} (f : A âŸ¶ B) : ((incl F).map f) = f.val :=
  rfl

/-- `F â‹™ FintypeCat.incl` as a cocone over `(can F).op â‹™ coyoneda`.
This is a colimit cocone (see `PreGaloisCategory.isColimÃ¬t`) -/
def cocone : Cocone ((incl F).op â‹™ coyoneda) where
  pt := F â‹™ FintypeCat.incl
  Î¹ := {
    app := fun âŸ¨A, a, _âŸ© â†¦ { app := fun X (f : (A : C) âŸ¶ X) â†¦ F.map f a }
    naturality := fun âŸ¨A, a, _âŸ© âŸ¨B, b, _âŸ© âŸ¨f, (hf : F.map f b = a)âŸ© â†¦ by
      ext Y (g : (A : C) âŸ¶ Y)
      suffices h : F.map g (F.map f b) = F.map g a by
        simpa
      rw [hf]
  }

@[simp]
lemma cocone_app (A : PointedGaloisObject F) (B : C) (f : (A : C) âŸ¶ B) :
    ((cocone F).Î¹.app âŸ¨AâŸ©).app B f = F.map f A.pt :=
  rfl

/-- `cocone F` is a colimit cocone, i.e. `F` is pro-represented by `incl F`. -/
noncomputable def isColimit : IsColimit (cocone F) := by
  refine evaluationJointlyReflectsColimits _ (fun X â†¦ ?_)
  refine Types.FilteredColimit.isColimitOf _ _ ?_ ?_
  Â· intro (x : F.obj X)
    obtain âŸ¨Y, i, y, h1, _, _âŸ© := fiber_in_connected_component F X x
    obtain âŸ¨Z, f, z, hgal, hfzâŸ© := exists_hom_from_galois_of_fiber F Y y
    refine âŸ¨âŸ¨Z, z, hgalâŸ©, f â‰« i, ?_âŸ©
    simp only [mapCocone_Î¹_app, evaluation_obj_map, cocone_app, map_comp,
      â† h1, FintypeCat.comp_apply, hfz]
  Â· intro âŸ¨A, a, _âŸ© âŸ¨B, b, _âŸ© (u : (A : C) âŸ¶ X) (v : (B : C) âŸ¶ X) (h : F.map u a = F.map v b)
    obtain âŸ¨âŸ¨Z, z, _âŸ©, âŸ¨f, hfâŸ©, âŸ¨g, hgâŸ©, _âŸ© :=
      @IsFilteredOrEmpty.cocone_objs (PointedGaloisObject F)áµ’áµ– _ _
      âŸ¨{ obj := A, pt := a}âŸ© âŸ¨{obj := B, pt := b}âŸ©
    refine âŸ¨âŸ¨{ obj := Z, pt := z }âŸ©, âŸ¨f, hfâŸ©, âŸ¨g, hgâŸ©, ?_âŸ©
    apply evaluationInjective_of_isConnected F Z X z
    change F.map (f â‰« u) z = F.map (g â‰« v) z
    rw [map_comp, FintypeCat.comp_apply, hf, map_comp, FintypeCat.comp_apply, hg, h]

instance : HasColimit ((incl F).op â‹™ coyoneda) where
  exists_colimit := âŸ¨cocone F, isColimit FâŸ©

variable {F}

/-- A morphism of pointed Galois objects induces a map on automorphism groups
of the underlying objects in `C`. This is a group homomorphism (see `autMapMul`). -/
noncomputable def autMap {A B : PointedGaloisObject F} (f : A âŸ¶ B) (Ïƒ : Aut A.obj) : Aut B.obj :=
  (evaluationEquivOfIsGalois F B B.pt).symm (F.map (Ïƒ.hom â‰« f) A.pt)

@[simp]
lemma autMap_eval {A B : PointedGaloisObject F} (f : A âŸ¶ B) (Ïƒ : Aut A.obj) :
    F.map (autMap f Ïƒ).hom B.pt = F.map f (F.map Ïƒ.hom A.pt) := by
  simp [autMap]

lemma autMap_surjective {A B : PointedGaloisObject F} (f : A âŸ¶ B) :
    Function.Surjective (autMap f) := by
  intro Ïƒ
  obtain âŸ¨a', ha'âŸ© := surjective_of_nonempty_fiber_of_isConnected F f.val (F.map Ïƒ.hom B.pt)
  obtain âŸ¨Ï„, (hÏ„ : F.map Ï„.hom A.pt = a')âŸ© := MulAction.exists_smul_eq (Aut A.obj) A.pt a'
  use Ï„
  apply evaluation_aut_injective_of_isConnected F B B.pt
  simp [hÏ„, ha']

@[simp]
lemma comp_autMap {A B : PointedGaloisObject F} (f : A âŸ¶ B) (Ïƒ : Aut A.obj) :
    f.val â‰« (autMap f Ïƒ).hom = Ïƒ.hom â‰« f := by
  apply evaluationInjective_of_isConnected F A B A.pt
  simp

@[simp]
lemma comp_autMap_apply {A B : PointedGaloisObject F} (f : A âŸ¶ B) (Ïƒ : Aut A.obj) (a : F.obj A) :
    F.map (autMap f Ïƒ).hom (F.map f.val a) = F.map f.val (F.map Ïƒ.hom a) := by
  simpa [-comp_autMap] using congrFun (congrArg F.map (comp_autMap f Ïƒ)) a

@[simp]
lemma autMap_apply_mul {A B : PointedGaloisObject F} (f : A âŸ¶ B) (Ïƒ Ï„ : Aut A.obj) :
    autMap f (Ïƒ * Ï„) = autMap f Ïƒ * autMap f Ï„ := by
  apply evaluation_aut_injective_of_isConnected F (B : C) B.pt
  simp [Aut.Aut_mul_def]

/-- `MonoidHom` version of `autMap`. -/
noncomputable def autMapMul {A B : PointedGaloisObject F} (f : A âŸ¶ B) :
     Aut (A : C) â†’* Aut (B : C) :=
  MonoidHom.mk' (autMap f) (autMap_apply_mul f)

end PointedGaloisObject

open PointedGaloisObject

/-- The diagram sending each pointed Galois object to its automorphism group
as an object of `C`. -/
noncomputable def autGaloisSystem : PointedGaloisObject F â¥¤ GroupCat.{uâ‚‚} where
  obj := fun A â†¦ GroupCat.of <| Aut (A : C)
  map := fun {A B} f â†¦ (autMapMul f : Aut (A : C) â†’* Aut (B : C))
  map_id := fun A â†¦ by
    ext (Ïƒ : Aut (A : C))
    show autMap (ğŸ™ A) Ïƒ = Ïƒ
    apply evaluation_aut_injective_of_isConnected F A A.pt
    simp
  map_comp {A B C} f g := by
    ext (Ïƒ : Aut A.obj)
    show autMap (f â‰« g) Ïƒ = autMap g (autMap f Ïƒ)
    apply evaluation_aut_injective_of_isConnected F C C.pt
    simp

  end PreGaloisCategory

  end CategoryTheory
