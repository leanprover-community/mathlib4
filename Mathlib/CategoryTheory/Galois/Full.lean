/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.CategoryTheory.Galois.Examples
import Mathlib.CategoryTheory.Galois.Prorepresentability

/-!

# Fiber functors are (faithfully) full

Any (fiber) functor `F : C ‚•§ FintypeCat` factors via the forgetful functor
from finite `Aut F`-sets to finite sets. The induced functor
`H : C ‚•§ Action FintypeCat (MonCat.of (Aut F))` is faithfully full. The faithfulness
follows easily from the faithfulness of `F`. In this file we show that `H` is also full.

## Main results

- `PreGaloisCategory.exists_lift_of_mono`: If `Y` is a sub-`Aut F`-set of `F.obj X`, there exists
  a sub-object `Z` of `X` such that `F.obj Z ‚âÖ Y` as `Aut F`-sets.
- `PreGaloisCategory.functorToAction_full`: The induced functor `H` from above is full.

The main input for this is that the induced functor `H : C ‚•§ Action FintypeCat (MonCat.of (Aut F))`
preserves connectedness, which translates to the fact that `Aut F` acts transitively on
the fibers of connected objects.

## Implementation details

We only show this for small categories, because the preservation of connectedness result as it
is currently in Mathlib is only shown for
`(C : Type u‚ÇÅ) [Category.{u‚ÇÇ} C] (F : C ‚•§ FintypeCat.{u‚ÇÇ})` and by the definition of `Action`,
this forces `u‚ÇÅ = u‚ÇÇ` for the definition of `functorToAction`. Mathematically there should
be no obstruction to generalizing the results of this file to arbitrary universes.

-/
universe u v

namespace CategoryTheory

namespace PreGaloisCategory

open Limits Functor

variable {C : Type u} [Category.{u} C] (F : C ‚•§ FintypeCat.{u})

/-- Any (fiber) functor `F : C ‚•§ FintypeCat` naturally factors via
the forgetful functor from `Action FintypeCat (MonCat.of (Aut F))` to `FintypeCat`. -/
def functorToAction : C ‚•§ Action FintypeCat.{u} (MonCat.of (Aut F)) where
  obj X := Action.FintypeCat.ofMulAction (Aut F) (F.obj X)
  map f := {
    hom := F.map f
    comm := fun g ‚Ü¶ symm <| g.hom.naturality f
  }

lemma functorToAction_comp_forget‚ÇÇ_eq : functorToAction F ‚ãô forget‚ÇÇ _ FintypeCat = F := rfl

variable [GaloisCategory C] [FiberFunctor F]

instance : Functor.Faithful (functorToAction F) :=
  have : Functor.Faithful (functorToAction F ‚ãô forget‚ÇÇ _ FintypeCat) :=
    inferInstanceAs <| Functor.Faithful F
  Functor.Faithful.of_comp (functorToAction F) (forget‚ÇÇ _ FintypeCat)

instance : PreservesMonomorphisms (functorToAction F) :=
  have : PreservesMonomorphisms (functorToAction F ‚ãô forget‚ÇÇ _ FintypeCat) :=
    inferInstanceAs <| PreservesMonomorphisms F
  preservesMonomorphisms_of_preserves_of_reflects (functorToAction F) (forget‚ÇÇ _ FintypeCat)

instance : ReflectsMonomorphisms (functorToAction F) := reflectsMonomorphisms_of_faithful _

instance : Functor.ReflectsIsomorphisms (functorToAction F) where
  reflects f _ :=
    have : IsIso (F.map f) := (forget‚ÇÇ _ FintypeCat).map_isIso ((functorToAction F).map f)
    isIso_of_reflects_iso f F

noncomputable instance : PreservesFiniteCoproducts (functorToAction F) :=
  ‚ü®fun J _ ‚Ü¶ Action.preservesColimitsOfShapeOfPreserves (functorToAction F)
    (inferInstanceAs <| PreservesColimitsOfShape (Discrete J) F)‚ü©

noncomputable instance : PreservesFiniteProducts (functorToAction F) :=
  ‚ü®fun J _ ‚Ü¶ Action.preservesLimitsOfShapeOfPreserves (functorToAction F)
    (inferInstanceAs <| PreservesLimitsOfShape (Discrete J) F)‚ü©

instance : PreservesIsConnected (functorToAction F) :=
  ‚ü®fun {X} _ ‚Ü¶ FintypeCat.Action.isConnected_of_transitive (Aut F) (F.obj X)‚ü©

/--
Let `X` be an object of a Galois category with fiber functor `F` and `Y` a sub-`Aut F`-set
of `F.obj X`, on which `Aut F` acts transitively (i.e. which is connected in the Galois category
of finite `Aut F`-sets). Then there exists a connected sub-object `Z` of `X` and an isomorphism
`Y ‚âÖ F.obj X` as `Aut F`-sets such that the obvious triangle commutes.

For a version without the connectedness assumption, see `exists_Lift_of_mono`.
-/
lemma exists_lift_of_mono_of_isConnected (X : C) (Y : Action FintypeCat.{u} (MonCat.of (Aut F)))
    (i : Y ‚ü∂ (functorToAction F).obj X) [Mono i] [IsConnected Y] : ‚àÉ (Z : C) (f : Z ‚ü∂ X)
    (u : Y ‚âÖ (functorToAction F).obj Z),
    IsConnected Z ‚àß Mono f ‚àß i = u.hom ‚â´ (functorToAction F).map f := by
  obtain ‚ü®y‚ü© := nonempty_fiber_of_isConnected (forget‚ÇÇ _ FintypeCat) Y
  obtain ‚ü®Z, f, z, hz, hc, hm‚ü© := fiber_in_connected_component F X (i.hom y)
  have : IsConnected ((functorToAction F).obj Z) := PreservesIsConnected.preserves
  obtain ‚ü®u, hu‚ü© := connected_component_unique
    (forget‚ÇÇ (Action FintypeCat (MonCat.of (Aut F))) FintypeCat) (B := (functorToAction F).obj Z)
    y z i ((functorToAction F).map f) hz.symm
  refine ‚ü®Z, f, u, hc, hm, ?_‚ü©
  apply evaluation_injective_of_isConnected
    (forget‚ÇÇ (Action FintypeCat (MonCat.of (Aut F))) FintypeCat) Y ((functorToAction F).obj X) y
  suffices h : i.hom y = F.map f z by simpa [hu]
  exact hz.symm

/--
Let `X` be an object of a Galois category with fiber functor `F` and `Y` a sub-`Aut F`-set
of `F.obj X`. Then there exists a sub-object `Z` of `X` and an isomorphism
`Y ‚âÖ F.obj X` as `Aut F`-sets such that the obvious triangle commutes.
-/
lemma exists_lift_of_mono (X : C) (Y : Action FintypeCat.{u} (MonCat.of (Aut F)))
    (i : Y ‚ü∂ (functorToAction F).obj X) [Mono i] : ‚àÉ (Z : C) (f : Z ‚ü∂ X)
    (u : Y ‚âÖ (functorToAction F).obj Z), Mono f ‚àß u.hom ‚â´ (functorToAction F).map f = i := by
  obtain ‚ü®Œπ, hf, f, t, hc‚ü© := has_decomp_connected_components' Y
  let i' (j : Œπ) : f j ‚ü∂ (functorToAction F).obj X := Sigma.Œπ f j ‚â´ t.hom ‚â´ i
  have (j : Œπ) : Mono (i' j) :=
    have : Mono (Sigma.Œπ f j) := MonoCoprod.mono_Œπ f j
    have : Mono (t.hom ‚â´ i) := mono_comp _ _
    mono_comp _ _
  choose gZ gf gu _ _ h using fun i ‚Ü¶ exists_lift_of_mono_of_isConnected F X (f i) (i' i)
  let is2 : (functorToAction F).obj (‚àê gZ) ‚âÖ ‚àê fun i => (functorToAction F).obj (gZ i) :=
    PreservesCoproduct.iso (functorToAction F) gZ
  let u' : ‚àê f ‚âÖ ‚àê fun i => (functorToAction F).obj (gZ i) := Sigma.mapIso gu
  have heq : (functorToAction F).map (Sigma.desc gf) = (t.symm ‚â™‚â´ u' ‚â™‚â´ is2.symm).inv ‚â´ i := by
    simp only [Iso.trans_inv, Iso.symm_inv, Category.assoc]
    rw [‚Üê Iso.inv_comp_eq]
    refine Sigma.hom_ext _ _ (fun j ‚Ü¶ ?_)
    suffices (functorToAction F).map (gf j) = (gu j).inv ‚â´ i' j by
      simpa [is2, u']
    simp only [h, Iso.inv_hom_id_assoc]
  refine ‚ü®‚àê gZ, Sigma.desc gf, t.symm ‚â™‚â´ u' ‚â™‚â´ is2.symm, ?_, by simp [heq]‚ü©
  ¬∑ exact mono_of_mono_map (functorToAction F) (heq ‚ñ∏ mono_comp _ _)

/-- The by a fiber functor `F : C ‚•§ FintypeCat` induced functor `functorToAction F` to
finite `Aut F`-sets is full. -/
instance functorToAction_full : Functor.Full (functorToAction F) where
  map_surjective {X Y} f := by
    let u : (functorToAction F).obj X ‚ü∂ (functorToAction F).obj X ‚®Ø (functorToAction F).obj Y :=
      prod.lift (ùüô _) f
    let i : (functorToAction F).obj X ‚ü∂ (functorToAction F).obj (X ‚®Ø Y) :=
      u ‚â´ (PreservesLimitPair.iso (functorToAction F) X Y).inv
    have : Mono i := by
      have : Mono (u ‚â´ prod.fst) := prod.lift_fst (ùüô _) f ‚ñ∏ inferInstance
      have : Mono u := mono_of_mono u prod.fst
      apply mono_comp u _
    obtain ‚ü®Z, g, v, _, hvgi‚ü© := exists_lift_of_mono F (Limits.prod X Y)
      ((functorToAction F).obj X) i
    let œà : Z ‚ü∂ X := g ‚â´ prod.fst
    have hgvi : (functorToAction F).map g = v.inv ‚â´ i := by simp [‚Üê hvgi]
    have : IsIso ((functorToAction F).map œà) := by
      simp only [map_comp, hgvi, Category.assoc, œà]
      have : IsIso (i ‚â´ (functorToAction F).map prod.fst) := by
        suffices h : IsIso (ùüô ((functorToAction F).obj X)) by simpa [i, u]
        infer_instance
      apply IsIso.comp_isIso
    have : IsIso œà := isIso_of_reflects_iso œà (functorToAction F)
    use inv œà ‚â´ g ‚â´ prod.snd
    rw [‚Üê cancel_epi ((functorToAction F).map œà)]
    ext (z : F.obj Z)
    simp [-FintypeCat.comp_apply, -Action.comp_hom, i, u, œà, hgvi]

end PreGaloisCategory

end CategoryTheory
