/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.CategoryTheory.Galois.Action

/-!

# Fiber functors are (faithfully) full

Any (fiber) functor `F : C â¥¤ FintypeCat` factors via the forgetful functor
from finite `Aut F`-sets to finite sets. The induced functor
`H : C â¥¤ Action FintypeCat (Aut F)` is faithfully full. The faithfulness
follows easily from the faithfulness of `F`. In this file we show that `H` is also full.

## Main results

- `PreGaloisCategory.exists_lift_of_mono`: If `Y` is a sub-`Aut F`-set of `F.obj X`, there exists
  a sub-object `Z` of `X` such that `F.obj Z â‰… Y` as `Aut F`-sets.
- `PreGaloisCategory.functorToAction_full`: The induced functor `H` from above is full.

The main input for this is that the induced functor `H : C â¥¤ Action FintypeCat (Aut F)`
preserves connectedness, which translates to the fact that `Aut F` acts transitively on
the fibers of connected objects.

-/

universe u

namespace CategoryTheory

namespace PreGaloisCategory

open Limits Functor

variable {C : Type*} [Category C] (F : C â¥¤ FintypeCat.{u}) [GaloisCategory C] [FiberFunctor F]

/--
Let `X` be an object of a Galois category with fiber functor `F` and `Y` a sub-`Aut F`-set
of `F.obj X`, on which `Aut F` acts transitively (i.e. which is connected in the Galois category
of finite `Aut F`-sets). Then there exists a connected sub-object `Z` of `X` and an isomorphism
`Y â‰… F.obj X` as `Aut F`-sets such that the obvious triangle commutes.

For a version without the connectedness assumption, see `exists_lift_of_mono`.
-/
lemma exists_lift_of_mono_of_isConnected (X : C) (Y : Action FintypeCat.{u} (Aut F))
    (i : Y âŸ¶ (functorToAction F).obj X) [Mono i] [IsConnected Y] : âˆƒ (Z : C) (f : Z âŸ¶ X)
    (u : Y â‰… (functorToAction F).obj Z),
    IsConnected Z âˆ§ Mono f âˆ§ i = u.hom â‰« (functorToAction F).map f := by
  obtain âŸ¨yâŸ© := nonempty_fiber_of_isConnected (forgetâ‚‚ _ FintypeCat) Y
  obtain âŸ¨Z, f, z, hz, hc, hmâŸ© := fiber_in_connected_component F X (i.hom y)
  have : IsConnected ((functorToAction F).obj Z) := PreservesIsConnected.preserves
  obtain âŸ¨u, huâŸ© := connected_component_unique
    (forgetâ‚‚ (Action FintypeCat (Aut F)) FintypeCat) (B := (functorToAction F).obj Z)
    y z i ((functorToAction F).map f) hz.symm
  refine âŸ¨Z, f, u, hc, hm, ?_âŸ©
  apply evaluation_injective_of_isConnected
    (forgetâ‚‚ (Action FintypeCat (Aut F)) FintypeCat) Y ((functorToAction F).obj X) y
  suffices h : i.hom y = F.map f z by simpa [hu]
  exact hz.symm

/--
Let `X` be an object of a Galois category with fiber functor `F` and `Y` a sub-`Aut F`-set
of `F.obj X`. Then there exists a sub-object `Z` of `X` and an isomorphism
`Y â‰… F.obj X` as `Aut F`-sets such that the obvious triangle commutes.
-/
lemma exists_lift_of_mono (X : C) (Y : Action FintypeCat.{u} (Aut F))
    (i : Y âŸ¶ (functorToAction F).obj X) [Mono i] : âˆƒ (Z : C) (f : Z âŸ¶ X)
    (u : Y â‰… (functorToAction F).obj Z), Mono f âˆ§ u.hom â‰« (functorToAction F).map f = i := by
  obtain âŸ¨Î¹, hf, f, t, hcâŸ© := has_decomp_connected_components' Y
  let i' (j : Î¹) : f j âŸ¶ (functorToAction F).obj X := Sigma.Î¹ f j â‰« t.hom â‰« i
  choose gZ gf gu _ _ h using fun i â†¦ exists_lift_of_mono_of_isConnected F X (f i) (i' i)
  let is2 : (functorToAction F).obj (âˆ gZ) â‰… âˆ fun i => (functorToAction F).obj (gZ i) :=
    PreservesCoproduct.iso (functorToAction F) gZ
  let u' : âˆ f â‰… âˆ fun i => (functorToAction F).obj (gZ i) := Sigma.mapIso gu
  have heq : (functorToAction F).map (Sigma.desc gf) = (t.symm â‰ªâ‰« u' â‰ªâ‰« is2.symm).inv â‰« i := by
    simp only [Iso.trans_inv, Iso.symm_inv, Category.assoc]
    rw [â† Iso.inv_comp_eq]
    refine Sigma.hom_ext _ _ (fun j â†¦ ?_)
    suffices (functorToAction F).map (gf j) = (gu j).inv â‰« i' j by
      simpa [is2, u']
    simp only [h, Iso.inv_hom_id_assoc]
  refine âŸ¨âˆ gZ, Sigma.desc gf, t.symm â‰ªâ‰« u' â‰ªâ‰« is2.symm, ?_, by simp [heq]âŸ©
  Â· exact mono_of_mono_map (functorToAction F) (heq â–¸ mono_comp _ _)

/-- The by a fiber functor `F : C â¥¤ FintypeCat` induced functor `functorToAction F` to
finite `Aut F`-sets is full. -/
instance functorToAction_full : Functor.Full (functorToAction F) where
  map_surjective {X Y} f := by
    let u : (functorToAction F).obj X âŸ¶ (functorToAction F).obj X â¨¯ (functorToAction F).obj Y :=
      prod.lift (ğŸ™ _) f
    let i : (functorToAction F).obj X âŸ¶ (functorToAction F).obj (X â¨¯ Y) :=
      u â‰« (PreservesLimitPair.iso (functorToAction F) X Y).inv
    have : Mono i := by
      have : Mono (u â‰« prod.fst) := prod.lift_fst (ğŸ™ _) f â–¸ inferInstance
      have : Mono u := mono_of_mono u prod.fst
      apply mono_comp u _
    obtain âŸ¨Z, g, v, _, hvgiâŸ© := exists_lift_of_mono F (Limits.prod X Y)
      ((functorToAction F).obj X) i
    let Ïˆ : Z âŸ¶ X := g â‰« prod.fst
    have hgvi : (functorToAction F).map g = v.inv â‰« i := by simp [â† hvgi]
    have : IsIso ((functorToAction F).map Ïˆ) := by
      simp only [map_comp, hgvi, Category.assoc, Ïˆ]
      have : IsIso (i â‰« (functorToAction F).map prod.fst) := by
        suffices h : IsIso (ğŸ™ ((functorToAction F).obj X)) by simpa [i, u]
        infer_instance
      apply IsIso.comp_isIso
    have : IsIso Ïˆ := isIso_of_reflects_iso Ïˆ (functorToAction F)
    use inv Ïˆ â‰« g â‰« prod.snd
    rw [â† cancel_epi ((functorToAction F).map Ïˆ)]
    ext (z : F.obj Z)
    simp [-FintypeCat.comp_apply, -Action.comp_hom, i, u, Ïˆ, hgvi]

end PreGaloisCategory

end CategoryTheory
