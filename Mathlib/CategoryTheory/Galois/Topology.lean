/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.CategoryTheory.Endomorphism
import Mathlib.CategoryTheory.FintypeCat
import Mathlib.Topology.Algebra.Group.Basic

/-!

# Topology of fundamental group

In this file we define a natural topology on the automorphism group of a functor
`F : C â¥¤ FintypeCat`: It is defined as the subspace topology induced by the natural
embedding of `Aut F` into `âˆ€ X, Aut (F.obj X)` where
`Aut (F.obj X)` carries the discrete topology.

## References

- Stacks Project: Tag 0BMQ

-/
universe uâ‚ uâ‚‚ vâ‚ vâ‚‚ v w

namespace CategoryTheory

namespace PreGaloisCategory

open Functor

variable {C : Type uâ‚} [Category.{uâ‚‚} C] (F : C â¥¤ FintypeCat.{w})

/-- For a functor `F : C â¥¤ FintypeCat`, the canonical embedding of `Aut F` into
the product over `Aut (F.obj X)` for all objects `X`. -/
def autEmbedding : Aut F â†’* âˆ€ X, Aut (F.obj X) :=
  MonoidHom.mk' (fun Ïƒ X â†¦ Ïƒ.app X) (fun _ _ â†¦ rfl)

@[simp]
lemma autEmbedding_apply (Ïƒ : Aut F) (X : C) : autEmbedding F Ïƒ X = Ïƒ.app X :=
  rfl

lemma autEmbedding_injective : Function.Injective (autEmbedding F) := by
  intro Ïƒ Ï„ h
  ext X x
  have : Ïƒ.app X = Ï„.app X := congr_fun h X
  rw [â† Iso.app_hom, â† Iso.app_hom, this]

instance (X : C) : TopologicalSpace (F.obj X) := âŠ¥
instance (X : C) : DiscreteTopology (F.obj X) := âŸ¨rflâŸ©
instance (X : C) : TopologicalSpace (Aut (F.obj X)) := âŠ¥
instance (X : C) : DiscreteTopology (Aut (F.obj X)) := âŸ¨rflâŸ©

/-- `Aut F` is equipped with the by the embedding into `âˆ€ X, Aut (F.obj X)` induced embedding. -/
instance : TopologicalSpace (Aut F) := TopologicalSpace.induced (autEmbedding F) inferInstance

/-- The image of `Aut F` in `âˆ€ X, Aut (F.obj X)` are precisely the compatible families of
automorphisms. -/
lemma autEmbedding_range :
    Set.range (autEmbedding F) =
      { a | âˆ€ (X Y : C) (f : X âŸ¶ Y), F.map f â‰« (a Y).hom = (a X).hom â‰« F.map f } := by
  ext a
  simp only [Set.mem_range, Set.mem_setOf_eq]
  constructor
  Â· intro âŸ¨Ïƒ, hâŸ©
    rw [â† h]
    exact Ïƒ.hom.naturality
  Â· intro h
    use NatIso.ofComponents (fun X => (a X))
    rfl

/-- The image of `Aut F` in `âˆ€ X, Aut (F.obj X)` is closed. -/
lemma autEmbedding_range_isClosed : IsClosed (Set.range (autEmbedding F)) := by
  rw [autEmbedding_range, â† isOpen_compl_iff, isOpen_iff_forall_mem_open]
  intro a h
  simp only [Set.mem_compl_iff, Set.mem_setOf_eq, not_forall] at h
  obtain âŸ¨X, Y, f, (h : (a Y).hom âˆ˜ F.map f â‰  F.map f âˆ˜ (a X).hom)âŸ© := h
  rw [Function.ne_iff] at h
  obtain âŸ¨x, hxâŸ© := h
  let sx (A : C) : Set (Aut (F.obj A)) :=
    { Î³ | âˆ€ (h : X âŸ¶ A), Î³.hom (F.map h x) = (a A).hom (F.map h x) }
  let sy (A : C) : Set (Aut (F.obj A)) :=
    { Î³ | âˆ€ (h : Y âŸ¶ A), Î³.hom (F.map h (F.map f x)) = (a A).hom (F.map h (F.map f x)) }
  have hx : IsOpen (Set.pi {X} sx) := isOpen_set_pi (Set.toFinite {X}) (fun _ _ â†¦ trivial)
  have hy : IsOpen (Set.pi {Y} sy) := isOpen_set_pi (Set.toFinite {Y}) (fun _ _ â†¦ trivial)
  use Set.pi {X} sx âˆ© Set.pi {Y} sy
  refine âŸ¨?_, IsOpen.inter hx hy, ?_âŸ©
  Â· intro Î³ hÎ³
    simp only [Set.singleton_pi] at hÎ³
    simp only [Set.mem_compl_iff, Set.mem_setOf_eq, not_forall]
    use X, Y, f
    rw [â† ne_eq, Function.ne_iff]
    use x
    simp only [FintypeCat.comp_apply]
    have hty : (Î³ Y).hom (F.map f x) = (a Y).hom (F.map f x) := by simpa using hÎ³.2 (ğŸ™ Y)
    have htx : (Î³ X).hom x = (a X).hom x := by simpa using hÎ³.1 (ğŸ™ X)
    rwa [htx, hty]
  Â· simp [sx, sy]

lemma autEmbedding_closedEmbedding : ClosedEmbedding (autEmbedding F) where
  induced := rfl
  inj := autEmbedding_injective F
  isClosed_range := autEmbedding_range_isClosed F

instance (X Y : C) : Finite (F.obj X âŸ¶ F.obj Y) :=
  inferInstanceAs <| Finite (F.obj X â†’ F.obj Y)

instance (X : C) : Finite (Aut (F.obj X)) :=
  Finite.of_injective _ (fun _ _ h â†¦ Iso.ext h)

instance : CompactSpace (Aut F) := ClosedEmbedding.compactSpace (autEmbedding_closedEmbedding F)

instance : T2Space (Aut F) :=
  T2Space.of_injective_continuous (autEmbedding_injective F) continuous_induced_dom

instance : TotallyDisconnectedSpace (Aut F) :=
  (Embedding.isTotallyDisconnected_range (autEmbedding_closedEmbedding F).embedding).mp
    (isTotallyDisconnected_of_totallyDisconnectedSpace _)

instance : ContinuousMul (Aut F) :=
  Inducing.continuousMul (autEmbedding F)
    (autEmbedding_closedEmbedding F).toInducing

instance : ContinuousInv (Aut F) :=
  Inducing.continuousInv (autEmbedding_closedEmbedding F).toInducing (fun _ â†¦ rfl)

instance : TopologicalGroup (Aut F) := âŸ¨âŸ©

instance (X : C) : SMul (Aut (F.obj X)) (F.obj X) := âŸ¨fun Ïƒ a => Ïƒ.hom aâŸ©

instance (X : C) : ContinuousSMul (Aut (F.obj X)) (F.obj X) := by
  constructor
  fun_prop

end PreGaloisCategory

end CategoryTheory
