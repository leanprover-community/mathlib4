/-
Copyright (c) 2020 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel, Adam Topaz, Johan Commelin, Jakob von Raumer
-/
import Mathlib.CategoryTheory.Abelian.Opposite
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Zero
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Kernels
import Mathlib.CategoryTheory.Preadditive.LeftExact
import Mathlib.CategoryTheory.Adjunction.Limits
import Mathlib.Algebra.Homology.Exact
import Mathlib.Tactic.TFAE

#align_import category_theory.abelian.exact from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# Exact sequences in abelian categories

In an abelian category, we get several interesting results related to exactness which are not
true in more general settings.

## Main results
* `(f, g)` is exact if and only if `f ‚â´ g = 0` and `kernel.Œπ g ‚â´ cokernel.œÄ f = 0`. This
  characterisation tends to be less cumbersome to work with than the original definition involving
  the comparison map `image f ‚ü∂ kernel g`.
* If `(f, g)` is exact, then `image.Œπ f` has the universal property of the kernel of `g`.
* `f` is a monomorphism iff `kernel.Œπ f = 0` iff `Exact 0 f`, and `f` is an epimorphism iff
  `cokernel.œÄ = 0` iff `Exact f 0`.
* A faithful functor between abelian categories that preserves zero morphisms reflects exact
  sequences.
* `X ‚ü∂ Y ‚ü∂ Z ‚ü∂ 0` is exact if and only if the second map is a cokernel of the first, and
  `0 ‚ü∂ X ‚ü∂ Y ‚ü∂ Z` is exact if and only if the first map is a kernel of the second.
* An exact functor preserves exactness, more specifically, `F` preserves finite colimits and
  finite limits, if and only if `Exact f g` implies `Exact (F.map f) (F.map g)`.
-/


universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

noncomputable section

open CategoryTheory Limits Preadditive

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] [Abelian C]

namespace CategoryTheory

namespace Abelian

variable {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)

attribute [local instance] hasEqualizers_of_hasKernels

/-- In an abelian category, a pair of morphisms `f : X ‚ü∂ Y`, `g : Y ‚ü∂ Z` is exact
iff `imageSubobject f = kernelSubobject g`.
-/
theorem exact_iff_image_eq_kernel : Exact f g ‚Üî imageSubobject f = kernelSubobject g := by
  constructor
  ¬∑ intro h
    have : IsIso (imageToKernel f g h.w) := have := h.epi; isIso_of_mono_of_epi _
    refine Subobject.eq_of_comm (asIso (imageToKernel _ _ h.w)) ?_
    simp
  ¬∑ apply exact_of_image_eq_kernel
#align category_theory.abelian.exact_iff_image_eq_kernel CategoryTheory.Abelian.exact_iff_image_eq_kernel

theorem exact_iff : Exact f g ‚Üî f ‚â´ g = 0 ‚àß kernel.Œπ g ‚â´ cokernel.œÄ f = 0 := by
  constructor
  ¬∑ exact fun h ‚Ü¶ ‚ü®h.1, kernel_comp_cokernel f g h‚ü©
  ¬∑ refine fun h ‚Ü¶ ‚ü®h.1, ?_‚ü©
    suffices hl : IsLimit
        (KernelFork.ofŒπ (imageSubobject f).arrow (imageSubobject_arrow_comp_eq_zero h.1)) by
      have : imageToKernel f g h.1 = (hl.conePointUniqueUpToIso (limit.isLimit _)).hom ‚â´
          (kernelSubobjectIso _).inv := by ext; simp
      rw [this]
      infer_instance
    refine KernelFork.IsLimit.ofŒπ _ _ (fun u hu ‚Ü¶ ?_) ?_ (fun _ _ _ h ‚Ü¶ ?_)
    ¬∑ refine kernel.lift (cokernel.œÄ f) u ?_ ‚â´ (imageIsoImage f).hom ‚â´ (imageSubobjectIso _).inv
      rw [‚Üê kernel.lift_Œπ g u hu, Category.assoc, h.2, comp_zero]
    ¬∑ aesop_cat
    ¬∑ rw [‚Üê cancel_mono (imageSubobject f).arrow, h]
      simp
#align category_theory.abelian.exact_iff CategoryTheory.Abelian.exact_iff

theorem exact_iff' {cg : KernelFork g} (hg : IsLimit cg) {cf : CokernelCofork f}
    (hf : IsColimit cf) : Exact f g ‚Üî f ‚â´ g = 0 ‚àß cg.Œπ ‚â´ cf.œÄ = 0 := by
  constructor
  ¬∑ intro h
    exact ‚ü®h.1, fork_Œπ_comp_cofork_œÄ f g h cg cf‚ü©
  ¬∑ rw [exact_iff]
    refine fun h => ‚ü®h.1, ?_‚ü©
    apply zero_of_epi_comp (IsLimit.conePointUniqueUpToIso hg (limit.isLimit _)).hom
    apply zero_of_comp_mono (IsColimit.coconePointUniqueUpToIso (colimit.isColimit _) hf).hom
    simp [h.2]
#align category_theory.abelian.exact_iff' CategoryTheory.Abelian.exact_iff'

open List in
theorem exact_tfae :
    TFAE [Exact f g, f ‚â´ g = 0 ‚àß kernel.Œπ g ‚â´ cokernel.œÄ f = 0,
      imageSubobject f = kernelSubobject g] := by
  tfae_have 1 ‚Üî 2; ¬∑ apply exact_iff
  tfae_have 1 ‚Üî 3; ¬∑ apply exact_iff_image_eq_kernel
  tfae_finish
#align category_theory.abelian.exact_tfae CategoryTheory.Abelian.exact_tfae

nonrec theorem IsEquivalence.exact_iff {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D] [Abelian D] (F : C ‚•§ D)
    [F.IsEquivalence] : Exact (F.map f) (F.map g) ‚Üî Exact f g := by
  simp only [exact_iff, ‚Üê F.map_eq_zero_iff, F.map_comp, Category.assoc, ‚Üê
    kernelComparison_comp_Œπ g F, ‚Üê œÄ_comp_cokernelComparison f F]
  rw [IsIso.comp_left_eq_zero (kernelComparison g F), ‚Üê Category.assoc,
    IsIso.comp_right_eq_zero _ (cokernelComparison f F)]
#align category_theory.abelian.is_equivalence.exact_iff CategoryTheory.Abelian.IsEquivalence.exact_iff

/-- The dual result is true even in non-abelian categories, see
    `CategoryTheory.exact_comp_mono_iff`. -/
theorem exact_epi_comp_iff {W : C} (h : W ‚ü∂ X) [Epi h] : Exact (h ‚â´ f) g ‚Üî Exact f g := by
  refine ‚ü®fun hfg => ?_, fun h => exact_epi_comp h‚ü©
  let hc := isCokernelOfComp _ _ (colimit.isColimit (parallelPair (h ‚â´ f) 0))
    (by rw [‚Üê cancel_epi h, ‚Üê Category.assoc, CokernelCofork.condition, comp_zero]) rfl
  refine (exact_iff' _ _ (limit.isLimit _) hc).2 ‚ü®?_, ((exact_iff _ _).1 hfg).2‚ü©
  exact zero_of_epi_comp h (by rw [‚Üê hfg.1, Category.assoc])
#align category_theory.abelian.exact_epi_comp_iff CategoryTheory.Abelian.exact_epi_comp_iff

/-- If `(f, g)` is exact, then `Abelian.image.Œπ f` is a kernel of `g`. -/
def isLimitImage (h : Exact f g) :
    IsLimit (KernelFork.ofŒπ (Abelian.image.Œπ f) (image_Œπ_comp_eq_zero h.1) : KernelFork g) := by
  rw [exact_iff] at h
  exact KernelFork.IsLimit.ofŒπ _ _
    (fun u hu ‚Ü¶ kernel.lift (cokernel.œÄ f) u
      (by rw [‚Üê kernel.lift_Œπ g u hu, Category.assoc, h.2, comp_zero])) (by aesop_cat)
    (fun _ _ _ hm => by
      rw [‚Üê cancel_mono (image.Œπ f), hm, kernel.lift_Œπ])
#align category_theory.abelian.is_limit_image CategoryTheory.Abelian.isLimitImage

/-- If `(f, g)` is exact, then `image.Œπ f` is a kernel of `g`. -/
def isLimitImage' (h : Exact f g) :
    IsLimit (KernelFork.ofŒπ (Limits.image.Œπ f) (Limits.image_Œπ_comp_eq_zero h.1)) :=
  IsKernel.isoKernel _ _ (isLimitImage f g h) (imageIsoImage f).symm <| IsImage.lift_fac _ _
#align category_theory.abelian.is_limit_image' CategoryTheory.Abelian.isLimitImage'

/-- If `(f, g)` is exact, then `Abelian.coimage.œÄ g` is a cokernel of `f`. -/
def isColimitCoimage (h : Exact f g) :
    IsColimit
      (CokernelCofork.ofœÄ (Abelian.coimage.œÄ g) (Abelian.comp_coimage_œÄ_eq_zero h.1) :
        CokernelCofork f) := by
  rw [exact_iff] at h
  refine CokernelCofork.IsColimit.ofœÄ _ _
    (fun u hu => cokernel.desc (kernel.Œπ g) u
      (by rw [‚Üê cokernel.œÄ_desc f u hu, ‚Üê Category.assoc, h.2, zero_comp]))
    (by aesop_cat) ?_
  intros _ _ _ _ hm
  ext
  rw [hm, cokernel.œÄ_desc]
#align category_theory.abelian.is_colimit_coimage CategoryTheory.Abelian.isColimitCoimage

/-- If `(f, g)` is exact, then `factorThruImage g` is a cokernel of `f`. -/
def isColimitImage (h : Exact f g) :
    IsColimit (CokernelCofork.ofœÄ (Limits.factorThruImage g) (comp_factorThruImage_eq_zero h.1)) :=
  IsCokernel.cokernelIso _ _ (isColimitCoimage f g h) (coimageIsoImage' g) <|
    (cancel_mono (Limits.image.Œπ g)).1 <| by simp
#align category_theory.abelian.is_colimit_image CategoryTheory.Abelian.isColimitImage

theorem exact_cokernel : Exact f (cokernel.œÄ f) := by
  rw [exact_iff]
  aesop_cat
#align category_theory.abelian.exact_cokernel CategoryTheory.Abelian.exact_cokernel

-- Porting note: this can no longer be an instance in Lean4
lemma mono_cokernel_desc_of_exact (h : Exact f g) : Mono (cokernel.desc f g h.w) :=
  suffices h : cokernel.desc f g h.w =
      (IsColimit.coconePointUniqueUpToIso (colimit.isColimit _) (isColimitImage f g h)).hom ‚â´
        Limits.image.Œπ g
    from h.symm ‚ñ∏ mono_comp _ _
  (cancel_epi (cokernel.œÄ f)).1 <| by simp

-- Porting note: this can no longer be an instance in Lean4
/-- If `ex : Exact f g` and `epi g`, then `cokernel.desc _ _ ex.w` is an isomorphism. -/
lemma isIso_cokernel_desc_of_exact_of_epi (ex : Exact f g) [Epi g] :
    IsIso (cokernel.desc f g ex.w) :=
  have := mono_cokernel_desc_of_exact _ _ ex
  isIso_of_mono_of_epi (Limits.cokernel.desc f g ex.w)

-- Porting note: removed the simp attribute because the lemma may never apply automatically
@[reassoc (attr := nolint unusedHavesSuffices)]
theorem cokernel.desc.inv [Epi g] (ex : Exact f g) :
    have := isIso_cokernel_desc_of_exact_of_epi _ _ ex
    g ‚â´ inv (cokernel.desc _ _ ex.w) = cokernel.œÄ _ := by
  have := isIso_cokernel_desc_of_exact_of_epi _ _ ex
  simp
#align category_theory.abelian.cokernel.desc.inv CategoryTheory.Abelian.cokernel.desc.inv

-- Porting note: this can no longer be an instance in Lean4
lemma isIso_kernel_lift_of_exact_of_mono (ex : Exact f g) [Mono f] : IsIso (kernel.lift g f ex.w) :=
  have := ex.epi_kernel_lift
  isIso_of_mono_of_epi (Limits.kernel.lift g f ex.w)

-- Porting note: removed the simp attribute because the lemma may never apply automatically
@[reassoc (attr := nolint unusedHavesSuffices)]
theorem kernel.lift.inv [Mono f] (ex : Exact f g) :
    have := isIso_kernel_lift_of_exact_of_mono _ _ ex
    inv (kernel.lift _ _ ex.w) ‚â´ f = kernel.Œπ g := by
  have := isIso_kernel_lift_of_exact_of_mono _ _ ex
  simp
#align category_theory.abelian.kernel.lift.inv CategoryTheory.Abelian.kernel.lift.inv

/-- If `X ‚ü∂ Y ‚ü∂ Z ‚ü∂ 0` is exact, then the second map is a cokernel of the first. -/
def isColimitOfExactOfEpi [Epi g] (h : Exact f g) : IsColimit (CokernelCofork.ofœÄ _ h.w) :=
  IsColimit.ofIsoColimit (colimit.isColimit _) <|
    Cocones.ext
      ‚ü®cokernel.desc _ _ h.w, epiDesc g (cokernel.œÄ f) ((exact_iff _ _).1 h).2,
        (cancel_epi (cokernel.œÄ f)).1 (by aesop_cat), (cancel_epi g).1 (by aesop_cat)‚ü©
          (by rintro (_|_) <;> simp [h.w])
#align category_theory.abelian.is_colimit_of_exact_of_epi CategoryTheory.Abelian.isColimitOfExactOfEpi

/-- If `0 ‚ü∂ X ‚ü∂ Y ‚ü∂ Z` is exact, then the first map is a kernel of the second. -/
def isLimitOfExactOfMono [Mono f] (h : Exact f g) : IsLimit (KernelFork.ofŒπ _ h.w) :=
  IsLimit.ofIsoLimit (limit.isLimit _) <|
    Cones.ext
      ‚ü®monoLift f (kernel.Œπ g) ((exact_iff _ _).1 h).2, kernel.lift _ _ h.w,
        (cancel_mono (kernel.Œπ g)).1 (by aesop_cat), (cancel_mono f).1 (by aesop_cat)‚ü©
      fun j => by cases j <;> simp
#align category_theory.abelian.is_limit_of_exact_of_mono CategoryTheory.Abelian.isLimitOfExactOfMono

theorem exact_of_is_cokernel (w : f ‚â´ g = 0)
    (h : IsColimit (CokernelCofork.ofœÄ _ w)) : Exact f g := by
  refine (exact_iff _ _).2 ‚ü®w, ?_‚ü©
  have := h.fac (CokernelCofork.ofœÄ _ (cokernel.condition f)) WalkingParallelPair.one
  simp only [Cofork.ofœÄ_Œπ_app] at this
  rw [‚Üê this, ‚Üê Category.assoc, kernel.condition, zero_comp]
#align category_theory.abelian.exact_of_is_cokernel CategoryTheory.Abelian.exact_of_is_cokernel

theorem exact_of_is_kernel (w : f ‚â´ g = 0) (h : IsLimit (KernelFork.ofŒπ _ w)) : Exact f g := by
  refine (exact_iff _ _).2 ‚ü®w, ?_‚ü©
  have := h.fac (KernelFork.ofŒπ _ (kernel.condition g)) WalkingParallelPair.zero
  simp only [Fork.ofŒπ_œÄ_app] at this
  rw [‚Üê this, Category.assoc, cokernel.condition, comp_zero]
#align category_theory.abelian.exact_of_is_kernel CategoryTheory.Abelian.exact_of_is_kernel

theorem exact_iff_exact_image_Œπ : Exact f g ‚Üî Exact (Abelian.image.Œπ f) g := by
  conv_lhs => rw [‚Üê Abelian.image.fac f]
  rw [exact_epi_comp_iff]
#align category_theory.abelian.exact_iff_exact_image_Œπ CategoryTheory.Abelian.exact_iff_exact_image_Œπ

theorem exact_iff_exact_coimage_œÄ : Exact f g ‚Üî Exact f (coimage.œÄ g) := by
  conv_lhs => rw [‚Üê Abelian.coimage.fac g]
  rw [exact_comp_mono_iff]
#align category_theory.abelian.exact_iff_exact_coimage_œÄ CategoryTheory.Abelian.exact_iff_exact_coimage_œÄ

section

variable (Z)

open List in
theorem tfae_mono : TFAE [Mono f, kernel.Œπ f = 0, Exact (0 : Z ‚ü∂ X) f] := by
  tfae_have 3 ‚Üí 2
  ¬∑ exact kernel_Œπ_eq_zero_of_exact_zero_left Z
  tfae_have 1 ‚Üí 3
  ¬∑ intros
    exact exact_zero_left_of_mono Z
  tfae_have 2 ‚Üí 1
  ¬∑ exact mono_of_kernel_Œπ_eq_zero _
  tfae_finish
#align category_theory.abelian.tfae_mono CategoryTheory.Abelian.tfae_mono

-- Note we've already proved `mono_iff_exact_zero_left : mono f ‚Üî Exact (0 : Z ‚ü∂ X) f`
-- in any preadditive category with kernels and images.
theorem mono_iff_kernel_Œπ_eq_zero : Mono f ‚Üî kernel.Œπ f = 0 :=
  (tfae_mono X f).out 0 1
#align category_theory.abelian.mono_iff_kernel_Œπ_eq_zero CategoryTheory.Abelian.mono_iff_kernel_Œπ_eq_zero

open List in
theorem tfae_epi : TFAE [Epi f, cokernel.œÄ f = 0, Exact f (0 : Y ‚ü∂ Z)] := by
  tfae_have 3 ‚Üí 2
  ¬∑ rw [exact_iff]
    rintro ‚ü®-, h‚ü©
    exact zero_of_epi_comp _ h
  tfae_have 1 ‚Üí 3
  ¬∑ rw [exact_iff]
    intro
    exact ‚ü®by simp, by simp [cokernel.œÄ_of_epi]‚ü©
  tfae_have 2 ‚Üí 1
  ¬∑ exact epi_of_cokernel_œÄ_eq_zero _
  tfae_finish
#align category_theory.abelian.tfae_epi CategoryTheory.Abelian.tfae_epi

-- Note we've already proved `epi_iff_exact_zero_right : epi f ‚Üî exact f (0 : Y ‚ü∂ Z)`
-- in any preadditive category with equalizers and images.
theorem epi_iff_cokernel_œÄ_eq_zero : Epi f ‚Üî cokernel.œÄ f = 0 :=
  (tfae_epi X f).out 0 1
#align category_theory.abelian.epi_iff_cokernel_œÄ_eq_zero CategoryTheory.Abelian.epi_iff_cokernel_œÄ_eq_zero

end

section Opposite

theorem Exact.op (h : Exact f g) : Exact g.op f.op := by
  rw [exact_iff]
  refine ‚ü®by simp [‚Üê op_comp, h.w], Quiver.Hom.unop_inj ?_‚ü©
  simp only [unop_comp, cokernel.œÄ_op, eqToHom_refl, kernel.Œπ_op, Category.id_comp,
    Category.assoc, kernel_comp_cokernel_assoc _ _ h, zero_comp, comp_zero, unop_zero]
#align category_theory.abelian.exact.op CategoryTheory.Abelian.Exact.op

theorem Exact.op_iff : Exact g.op f.op ‚Üî Exact f g :=
  ‚ü®fun e => by
    rw [‚Üê IsEquivalence.exact_iff _ _ (opOpEquivalence C).inverse]
    exact Exact.op _ _ e, Exact.op _ _‚ü©
#align category_theory.abelian.exact.op_iff CategoryTheory.Abelian.Exact.op_iff

theorem Exact.unop {X Y Z : C·µí·µñ} (g : X ‚ü∂ Y) (f : Y ‚ü∂ Z) (h : Exact g f) : Exact f.unop g.unop := by
  rw [‚Üê f.op_unop, ‚Üê g.op_unop] at h
  rwa [‚Üê Exact.op_iff]
#align category_theory.abelian.exact.unop CategoryTheory.Abelian.Exact.unop

theorem Exact.unop_iff {X Y Z : C·µí·µñ} (g : X ‚ü∂ Y) (f : Y ‚ü∂ Z) : Exact f.unop g.unop ‚Üî Exact g f :=
  ‚ü®fun e => by rwa [‚Üê f.op_unop, ‚Üê g.op_unop, ‚Üê Exact.op_iff] at e, fun e => by
    rw [‚Üê Exact.op_iff]
    exact e‚ü©
#align category_theory.abelian.exact.unop_iff CategoryTheory.Abelian.Exact.unop_iff

end Opposite

end Abelian

namespace Functor

section

variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] [Abelian D]
variable (F : C ‚•§ D) [PreservesZeroMorphisms F]

instance (priority := 100) reflectsExactSequencesOfPreservesZeroMorphismsOfFaithful [Faithful F] :
    ReflectsExactSequences F where
  reflects {X Y Z} f g hfg := by
    rw [Abelian.exact_iff, ‚Üê F.map_comp, F.map_eq_zero_iff] at hfg
    refine (Abelian.exact_iff _ _).2 ‚ü®hfg.1, F.zero_of_map_zero _ ?_‚ü©
    obtain ‚ü®k, hk‚ü© :=
      kernel.lift' (F.map g) (F.map (kernel.Œπ g))
        (by simp only [‚Üê F.map_comp, kernel.condition, CategoryTheory.Functor.map_zero])
    obtain ‚ü®l, hl‚ü© :=
      cokernel.desc' (F.map f) (F.map (cokernel.œÄ f))
        (by simp only [‚Üê F.map_comp, cokernel.condition, CategoryTheory.Functor.map_zero])
    rw [F.map_comp, ‚Üê hk, ‚Üê hl, Category.assoc, reassoc_of% hfg.2, zero_comp, comp_zero]
#align category_theory.functor.reflects_exact_sequences_of_preserves_zero_morphisms_of_faithful CategoryTheory.Functor.reflectsExactSequencesOfPreservesZeroMorphismsOfFaithful

end

end Functor

namespace Functor

open Limits Abelian

variable {A : Type u‚ÇÅ} {B : Type u‚ÇÇ} [Category.{v‚ÇÅ} A] [Category.{v‚ÇÇ} B]
variable [Abelian A] [Abelian B]
variable (L : A ‚•§ B)

section

variable [PreservesFiniteLimits L] [PreservesFiniteColimits L]

/-- A functor preserving finite limits and finite colimits preserves exactness. The converse
result is also true, see `Functor.preservesFiniteLimitsOfMapExact` and
`Functor.preservesFiniteColimitsOfMapExact`. -/
theorem map_exact {X Y Z : A} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (e1 : Exact f g) :
    Exact (L.map f) (L.map g) := by
  let hcoker := isColimitOfHasCokernelOfPreservesColimit L f
  let hker := isLimitOfHasKernelOfPreservesLimit L g
  refine (exact_iff' _ _ hker hcoker).2 ‚ü®by simp [‚Üê L.map_comp, e1.1], ?_‚ü©
  simp only [Fork.Œπ_ofŒπ, Cofork.œÄ_ofœÄ, ‚Üê L.map_comp, kernel_comp_cokernel _ _ e1, L.map_zero]
#align category_theory.functor.map_exact CategoryTheory.Functor.map_exact

end

section

variable (h : ‚àÄ ‚¶ÉX Y Z : A‚¶Ñ {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z}, Exact f g ‚Üí Exact (L.map f) (L.map g))

open ZeroObject

/-- A functor which preserves exactness preserves zero morphisms. -/
theorem preservesZeroMorphisms_of_map_exact : L.PreservesZeroMorphisms := by
  replace h := (h (exact_of_zero (ùüô 0) (ùüô 0))).w
  rw [L.map_id, Category.comp_id] at h
  exact preservesZeroMorphisms_of_map_zero_object (idZeroEquivIsoZero _ h)
#align category_theory.functor.preserves_zero_morphisms_of_map_exact CategoryTheory.Functor.preservesZeroMorphisms_of_map_exact

/-- A functor which preserves exactness preserves monomorphisms. -/
theorem preservesMonomorphisms_of_map_exact : L.PreservesMonomorphisms where
  preserves f hf := by
    letI := preservesZeroMorphisms_of_map_exact L h
    apply ((tfae_mono (L.obj 0) (L.map f)).out 2 0).mp
    rw [‚Üê L.map_zero]
    exact h (((tfae_mono 0 f).out 0 2).mp hf)
#align category_theory.functor.preserves_monomorphisms_of_map_exact CategoryTheory.Functor.preservesMonomorphisms_of_map_exact

/-- A functor which preserves exactness preserves epimorphisms. -/
theorem preservesEpimorphisms_of_map_exact : L.PreservesEpimorphisms where
  preserves f hf := by
    letI := preservesZeroMorphisms_of_map_exact L h
    apply ((tfae_epi (L.obj 0) (L.map f)).out 2 0).mp
    rw [‚Üê L.map_zero]
    exact h (((tfae_epi 0 f).out 0 2).mp hf)
#align category_theory.functor.preserves_epimorphisms_of_map_exact CategoryTheory.Functor.preservesEpimorphisms_of_map_exact

/-- A functor which preserves exactness preserves kernels. -/
def preservesKernelsOfMapExact (X Y : A) (f : X ‚ü∂ Y) : PreservesLimit (parallelPair f 0) L where
  preserves {c} ic := by
    letI := preservesZeroMorphisms_of_map_exact L h
    letI := preservesMonomorphisms_of_map_exact L h
    letI := mono_of_isLimit_fork ic
    have hf :=
      (isLimitMapConeForkEquiv' L (KernelFork.condition c)).symm
        (isLimitOfExactOfMono (L.map (Fork.Œπ c)) (L.map f)
          (h (exact_of_is_kernel (Fork.Œπ c) f (KernelFork.condition c)
              (ic.ofIsoLimit (isoOfŒπ _)))))
    exact hf.ofIsoLimit ((Cones.functoriality _ L).mapIso (isoOfŒπ _).symm)
#align category_theory.functor.preserves_kernels_of_map_exact CategoryTheory.Functor.preservesKernelsOfMapExact

/-- A functor which preserves exactness preserves zero cokernels. -/
def preservesCokernelsOfMapExact (X Y : A) (f : X ‚ü∂ Y) :
    PreservesColimit (parallelPair f 0) L where
  preserves {c} ic := by
    letI := preservesZeroMorphisms_of_map_exact L h
    letI := preservesEpimorphisms_of_map_exact L h
    letI := epi_of_isColimit_cofork ic
    have hf :=
      (isColimitMapCoconeCoforkEquiv' L (CokernelCofork.condition c)).symm
        (isColimitOfExactOfEpi (L.map f) (L.map (Cofork.œÄ c))
          (h (exact_of_is_cokernel f (Cofork.œÄ c) (CokernelCofork.condition c)
              (ic.ofIsoColimit (isoOfœÄ _)))))
    exact hf.ofIsoColimit ((Cocones.functoriality _ L).mapIso (isoOfœÄ _).symm)
#align category_theory.functor.preserves_cokernels_of_map_exact CategoryTheory.Functor.preservesCokernelsOfMapExact

/-- A functor which preserves exactness is left exact, i.e. preserves finite limits.
This is part of the inverse implication to `Functor.map_exact`. -/
def preservesFiniteLimitsOfMapExact : PreservesFiniteLimits L := by
  letI := preservesZeroMorphisms_of_map_exact L h
  letI := preservesKernelsOfMapExact L h
  apply preservesFiniteLimitsOfPreservesKernels
#align category_theory.functor.preserves_finite_limits_of_map_exact CategoryTheory.Functor.preservesFiniteLimitsOfMapExact

/-- A functor which preserves exactness is right exact, i.e. preserves finite colimits.
This is part of the inverse implication to `Functor.map_exact`. -/
def preservesFiniteColimitsOfMapExact : PreservesFiniteColimits L := by
  letI := preservesZeroMorphisms_of_map_exact L h
  letI := preservesCokernelsOfMapExact L h
  apply preservesFiniteColimitsOfPreservesCokernels
#align category_theory.functor.preserves_finite_colimits_of_map_exact CategoryTheory.Functor.preservesFiniteColimitsOfMapExact

end

section

/-- A functor preserving zero morphisms, monos, and cokernels preserves finite limits. -/
def preservesFiniteLimitsOfPreservesMonosAndCokernels [PreservesZeroMorphisms L]
    [PreservesMonomorphisms L] [‚àÄ {X Y} (f : X ‚ü∂ Y), PreservesColimit (parallelPair f 0) L] :
    PreservesFiniteLimits L := by
  apply preservesFiniteLimitsOfMapExact
  intro X Y Z f g h
  rw [‚Üê Abelian.coimage.fac g, L.map_comp, exact_comp_mono_iff]
  exact
    exact_of_is_cokernel _ _ _ (isColimitCoforkMapOfIsColimit' L _ (isColimitCoimage f g h))
#align category_theory.functor.preserves_finite_limits_of_preserves_monos_and_cokernels CategoryTheory.Functor.preservesFiniteLimitsOfPreservesMonosAndCokernels

/-- A functor preserving zero morphisms, epis, and kernels preserves finite colimits. -/
def preservesFiniteColimitsOfPreservesEpisAndKernels [PreservesZeroMorphisms L]
    [PreservesEpimorphisms L] [‚àÄ {X Y} (f : X ‚ü∂ Y), PreservesLimit (parallelPair f 0) L] :
    PreservesFiniteColimits L := by
  apply preservesFiniteColimitsOfMapExact
  intro X Y Z f g h
  rw [‚Üê Abelian.image.fac f, L.map_comp, exact_epi_comp_iff]
  exact exact_of_is_kernel _ _ _ (isLimitForkMapOfIsLimit' L _ (isLimitImage f g h))
#align category_theory.functor.preserves_finite_colimits_of_preserves_epis_and_kernels CategoryTheory.Functor.preservesFiniteColimitsOfPreservesEpisAndKernels

end

end Functor

end CategoryTheory
