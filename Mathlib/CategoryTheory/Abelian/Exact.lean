/-
Copyright (c) 2020 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel, Adam Topaz, Johan Commelin, Jakob von Raumer
-/
import Mathlib.Algebra.Homology.ImageToKernel
import Mathlib.Algebra.Homology.ShortComplex.Exact
import Mathlib.CategoryTheory.Abelian.Opposite
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Zero
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Kernels
import Mathlib.CategoryTheory.Adjunction.Limits
import Mathlib.Tactic.TFAE

/-!
# Exact sequences in abelian categories

In an abelian category, we get several interesting results related to exactness which are not
true in more general settings.

## Main results
* A short complex `S` is exact iff `imageSubobject S.f = kernelSubobject S.g`.
* If `(f, g)` is exact, then `image.Œπ f` has the universal property of the kernel of `g`.
* `f` is a monomorphism iff `kernel.Œπ f = 0` iff `Exact 0 f`, and `f` is an epimorphism iff
  `cokernel.œÄ = 0` iff `Exact f 0`.
* A faithful functor between abelian categories that preserves zero morphisms reflects exact
  sequences.
* `X ‚ü∂ Y ‚ü∂ Z ‚ü∂ 0` is exact if and only if the second map is a cokernel of the first, and
  `0 ‚ü∂ X ‚ü∂ Y ‚ü∂ Z` is exact if and only if the first map is a kernel of the second.
* A functor `F` such that for all `S`, we have `S.Exact ‚Üí (S.map F).Exact` preserves both
  finite limits and colimits.

-/

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

noncomputable section

open CategoryTheory Limits Preadditive

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] [Abelian C]

namespace CategoryTheory

namespace ShortComplex

variable (S : ShortComplex C)

attribute [local instance] hasEqualizers_of_hasKernels

theorem exact_iff_epi_imageToKernel' : S.Exact ‚Üî Epi (imageToKernel' S.f S.g S.zero) := by
  rw [S.exact_iff_epi_kernel_lift]
  have : factorThruImage S.f ‚â´ imageToKernel' S.f S.g S.zero = kernel.lift S.g S.f S.zero := by
    simp only [‚Üê cancel_mono (kernel.Œπ _), kernel.lift_Œπ, imageToKernel',
      Category.assoc, image.fac]
  constructor
  ¬∑ intro
    exact epi_of_epi_fac this
  ¬∑ intro
    rw [‚Üê this]
    apply epi_comp

theorem exact_iff_epi_imageToKernel : S.Exact ‚Üî Epi (imageToKernel S.f S.g S.zero) := by
  rw [S.exact_iff_epi_imageToKernel']
  apply (MorphismProperty.epimorphisms C).arrow_mk_iso_iff
  exact Arrow.isoMk (imageSubobjectIso S.f).symm (kernelSubobjectIso S.g).symm

theorem exact_iff_isIso_imageToKernel : S.Exact ‚Üî IsIso (imageToKernel S.f S.g S.zero) := by
  rw [S.exact_iff_epi_imageToKernel]
  constructor
  ¬∑ intro
    apply isIso_of_mono_of_epi
  ¬∑ intro
    infer_instance

/-- In an abelian category, a short complex `S` is exact
iff `imageSubobject S.f = kernelSubobject S.g`.
-/
theorem exact_iff_image_eq_kernel : S.Exact ‚Üî imageSubobject S.f = kernelSubobject S.g := by
  rw [exact_iff_isIso_imageToKernel]
  constructor
  ¬∑ intro
    exact Subobject.eq_of_comm (asIso (imageToKernel _ _ S.zero)) (by simp)
  ¬∑ intro h
    exact ‚ü®Subobject.ofLE _ _ h.ge, by ext; simp, by ext; simp‚ü©

theorem exact_iff_of_forks {cg : KernelFork S.g} (hg : IsLimit cg) {cf : CokernelCofork S.f}
    (hf : IsColimit cf) : S.Exact ‚Üî cg.Œπ ‚â´ cf.œÄ = 0 := by
  rw [exact_iff_kernel_Œπ_comp_cokernel_œÄ_zero]
  let e‚ÇÅ := IsLimit.conePointUniqueUpToIso (kernelIsKernel S.g) hg
  let e‚ÇÇ := IsColimit.coconePointUniqueUpToIso (cokernelIsCokernel S.f) hf
  have : cg.Œπ ‚â´ cf.œÄ = e‚ÇÅ.inv ‚â´ kernel.Œπ S.g ‚â´ cokernel.œÄ S.f ‚â´ e‚ÇÇ.hom := by
    have eq‚ÇÅ := IsLimit.conePointUniqueUpToIso_inv_comp (kernelIsKernel S.g) hg (.zero)
    have eq‚ÇÇ := IsColimit.comp_coconePointUniqueUpToIso_hom (cokernelIsCokernel S.f) hf (.one)
    dsimp at eq‚ÇÅ eq‚ÇÇ
    rw [‚Üê eq‚ÇÅ, ‚Üê eq‚ÇÇ, Category.assoc]
  rw [this, IsIso.comp_left_eq_zero e‚ÇÅ.inv, ‚Üê Category.assoc,
    IsIso.comp_right_eq_zero _ e‚ÇÇ.hom]

variable {S}

/-- If `(f, g)` is exact, then `Abelian.image.Œπ S.f` is a kernel of `S.g`. -/
def Exact.isLimitImage (h : S.Exact) :
    IsLimit (KernelFork.ofŒπ (Abelian.image.Œπ S.f)
      (Abelian.image_Œπ_comp_eq_zero S.zero) : KernelFork S.g) := by
  rw [exact_iff_kernel_Œπ_comp_cokernel_œÄ_zero] at h
  exact KernelFork.IsLimit.ofŒπ _ _
    (fun u hu ‚Ü¶ kernel.lift (cokernel.œÄ S.f) u
      (by rw [‚Üê kernel.lift_Œπ S.g u hu, Category.assoc, h, comp_zero])) (by simp)
    (fun _ _ _ hm => by rw [‚Üê cancel_mono (Abelian.image.Œπ S.f), hm, kernel.lift_Œπ])

/-- If `(f, g)` is exact, then `image.Œπ f` is a kernel of `g`. -/
def Exact.isLimitImage' (h : S.Exact) :
    IsLimit (KernelFork.ofŒπ (Limits.image.Œπ S.f)
      (image_Œπ_comp_eq_zero S.zero) : KernelFork S.g) :=
  IsKernel.isoKernel _ _ h.isLimitImage (Abelian.imageIsoImage S.f).symm <| IsImage.lift_fac _ _

/-- If `(f, g)` is exact, then `Abelian.coimage.œÄ g` is a cokernel of `f`. -/
def Exact.isColimitCoimage (h : S.Exact) :
    IsColimit
      (CokernelCofork.ofœÄ (Abelian.coimage.œÄ S.g) (Abelian.comp_coimage_œÄ_eq_zero S.zero) :
        CokernelCofork S.f) := by
  rw [exact_iff_kernel_Œπ_comp_cokernel_œÄ_zero] at h
  refine CokernelCofork.IsColimit.ofœÄ _ _
    (fun u hu => cokernel.desc (kernel.Œπ S.g) u
      (by rw [‚Üê cokernel.œÄ_desc S.f u hu, ‚Üê Category.assoc, h, zero_comp]))
    (by simp) ?_
  intro _ _ _ _ hm
  ext
  rw [hm, cokernel.œÄ_desc]

/-- If `(f, g)` is exact, then `factorThruImage g` is a cokernel of `f`. -/
def Exact.isColimitImage (h : S.Exact) :
    IsColimit (CokernelCofork.ofœÄ (Limits.factorThruImage S.g)
        (comp_factorThruImage_eq_zero S.zero)) :=
  IsCokernel.cokernelIso _ _ h.isColimitCoimage (Abelian.coimageIsoImage' S.g) <|
    (cancel_mono (Limits.image.Œπ S.g)).1 <| by simp

theorem exact_kernel {X Y : C} (f : X ‚ü∂ Y) :
    (ShortComplex.mk (kernel.Œπ f) f (by simp)).Exact :=
  exact_of_f_is_kernel _ (kernelIsKernel f)

theorem exact_cokernel {X Y : C} (f : X ‚ü∂ Y) :
    (ShortComplex.mk f (cokernel.œÄ f) (by simp)).Exact :=
  exact_of_g_is_cokernel _ (cokernelIsCokernel f)

variable (S)

theorem exact_iff_exact_image_Œπ :
    S.Exact ‚Üî (ShortComplex.mk (Abelian.image.Œπ S.f) S.g
      (Abelian.image_Œπ_comp_eq_zero S.zero)).Exact :=
  ShortComplex.exact_iff_of_epi_of_isIso_of_mono
    { œÑ‚ÇÅ := Abelian.factorThruImage S.f
      œÑ‚ÇÇ := ùüô _
      œÑ‚ÇÉ := ùüô _ }

theorem exact_iff_exact_coimage_œÄ :
    S.Exact ‚Üî (ShortComplex.mk S.f (Abelian.coimage.œÄ S.g)
      (Abelian.comp_coimage_œÄ_eq_zero S.zero)).Exact := by
  symm
  exact ShortComplex.exact_iff_of_epi_of_isIso_of_mono
    { œÑ‚ÇÅ := ùüô _
      œÑ‚ÇÇ := ùüô _
      œÑ‚ÇÉ := Abelian.factorThruCoimage S.g }

end ShortComplex

section

open List in
theorem Abelian.tfae_mono {X Y : C} (f : X ‚ü∂ Y) (Z : C) :
    TFAE [Mono f, kernel.Œπ f = 0, (ShortComplex.mk (0 : Z ‚ü∂ X) f zero_comp).Exact] := by
  tfae_have 2 ‚Üí 1 := mono_of_kernel_Œπ_eq_zero _
  tfae_have 1 ‚Üí 2
  | _ => by rw [‚Üê cancel_mono f, kernel.condition, zero_comp]
  tfae_have 3 ‚Üî 1 := ShortComplex.exact_iff_mono _ (by simp)
  tfae_finish

open List in
theorem Abelian.tfae_epi {X Y : C} (f : X ‚ü∂ Y) (Z : C) :
    TFAE [Epi f, cokernel.œÄ f = 0, (ShortComplex.mk f (0 : Y ‚ü∂ Z) comp_zero).Exact] := by
  tfae_have 2 ‚Üí 1 := epi_of_cokernel_œÄ_eq_zero _
  tfae_have 1 ‚Üí 2
  | _ => by rw [‚Üê cancel_epi f, cokernel.condition, comp_zero]
  tfae_have 3 ‚Üî 1 := ShortComplex.exact_iff_epi _ (by simp)
  tfae_finish

end

namespace Functor

section

variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] [Abelian D]
variable (F : C ‚•§ D) [PreservesZeroMorphisms F]

lemma reflects_exact_of_faithful [F.Faithful] (S : ShortComplex C) (hS : (S.map F).Exact) :
    S.Exact := by
  rw [ShortComplex.exact_iff_kernel_Œπ_comp_cokernel_œÄ_zero] at hS ‚ä¢
  dsimp at hS
  apply F.zero_of_map_zero
  obtain ‚ü®k, hk‚ü© :=
    kernel.lift' (F.map S.g) (F.map (kernel.Œπ S.g))
      (by simp only [‚Üê F.map_comp, kernel.condition, CategoryTheory.Functor.map_zero])
  obtain ‚ü®l, hl‚ü© :=
    cokernel.desc' (F.map S.f) (F.map (cokernel.œÄ S.f))
      (by simp only [‚Üê F.map_comp, cokernel.condition, CategoryTheory.Functor.map_zero])
  rw [F.map_comp, ‚Üê hl, ‚Üê hk, Category.assoc, reassoc_of% hS, zero_comp, comp_zero]

end

end Functor

namespace Functor

open Limits Abelian

variable {A : Type u‚ÇÅ} {B : Type u‚ÇÇ} [Category.{v‚ÇÅ} A] [Category.{v‚ÇÇ} B]
variable [Abelian A] [Abelian B]
variable (L : A ‚•§ B)

section

variable [L.PreservesZeroMorphisms]
variable (hL : ‚àÄ (S : ShortComplex A), S.Exact ‚Üí (S.map L).Exact)
include hL

open ZeroObject

/-- A functor which preserves exactness preserves monomorphisms. -/
theorem preservesMonomorphisms_of_map_exact : L.PreservesMonomorphisms where
  preserves f hf := by
    apply ((Abelian.tfae_mono (L.map f) (L.obj 0)).out 2 0).mp
    refine ShortComplex.exact_of_iso ?_ (hL _ (((tfae_mono f 0).out 0 2).mp hf))
    exact ShortComplex.isoMk (Iso.refl _) (Iso.refl _) (Iso.refl _)

/-- A functor which preserves exactness preserves epimorphisms. -/
theorem preservesEpimorphisms_of_map_exact : L.PreservesEpimorphisms where
  preserves f hf := by
    apply ((Abelian.tfae_epi (L.map f) (L.obj 0)).out 2 0).mp
    refine ShortComplex.exact_of_iso ?_ (hL _ (((tfae_epi f 0).out 0 2).mp hf))
    exact ShortComplex.isoMk (Iso.refl _) (Iso.refl _) (Iso.refl _)

/-- A functor which preserves the exactness of short complexes preserves homology. -/
lemma preservesHomology_of_map_exact : L.PreservesHomology where
  preservesCokernels X Y f := by
    have := preservesEpimorphisms_of_map_exact _ hL
    apply preservesColimit_of_preserves_colimit_cocone (cokernelIsCokernel f)
    apply (CokernelCofork.isColimitMapCoconeEquiv _ L).2
    have : Epi ((ShortComplex.mk _ _ (cokernel.condition f)).map L).g := by
      dsimp
      infer_instance
    exact (hL (ShortComplex.mk _ _ (cokernel.condition f))
      (ShortComplex.exact_of_g_is_cokernel _ (cokernelIsCokernel f))).gIsCokernel
  preservesKernels X Y f := by
    have := preservesMonomorphisms_of_map_exact _ hL
    apply preservesLimit_of_preserves_limit_cone (kernelIsKernel f)
    apply (KernelFork.isLimitMapConeEquiv _ L).2
    have : Mono ((ShortComplex.mk _ _ (kernel.condition f)).map L).f := by
      dsimp
      infer_instance
    exact (hL (ShortComplex.mk _ _ (kernel.condition f))
      (ShortComplex.exact_of_f_is_kernel _ (kernelIsKernel f))).fIsKernel

end

section

/-- A functor preserving zero morphisms, monos, and cokernels preserves homology. -/
lemma preservesHomology_of_preservesMonos_and_cokernels [PreservesZeroMorphisms L]
    [PreservesMonomorphisms L] [‚àÄ {X Y} (f : X ‚ü∂ Y), PreservesColimit (parallelPair f 0) L] :
    PreservesHomology L := by
  apply preservesHomology_of_map_exact
  intro S hS
  let œÜ : (ShortComplex.mk _ _ (Abelian.comp_coimage_œÄ_eq_zero S.zero)).map L ‚ü∂ S.map L :=
    { œÑ‚ÇÅ := ùüô _
      œÑ‚ÇÇ := ùüô _
      œÑ‚ÇÉ := L.map (Abelian.factorThruCoimage S.g)
      comm‚ÇÇ‚ÇÉ := by
        dsimp
        rw [Category.id_comp, ‚Üê L.map_comp, cokernel.œÄ_desc] }
  apply (ShortComplex.exact_iff_of_epi_of_isIso_of_mono œÜ).1
  apply ShortComplex.exact_of_g_is_cokernel
  exact CokernelCofork.mapIsColimit _ ((S.exact_iff_exact_coimage_œÄ).1 hS).gIsCokernel L

/-- A functor preserving zero morphisms, epis, and kernels preserves homology. -/
lemma preservesHomology_of_preservesEpis_and_kernels [PreservesZeroMorphisms L]
    [PreservesEpimorphisms L] [‚àÄ {X Y} (f : X ‚ü∂ Y), PreservesLimit (parallelPair f 0) L] :
    PreservesHomology L := by
  apply preservesHomology_of_map_exact
  intro S hS
  let œÜ : S.map L ‚ü∂ (ShortComplex.mk _ _ (Abelian.image_Œπ_comp_eq_zero S.zero)).map L :=
    { œÑ‚ÇÅ := L.map (Abelian.factorThruImage S.f)
      œÑ‚ÇÇ := ùüô _
      œÑ‚ÇÉ := ùüô _
      comm‚ÇÅ‚ÇÇ := by
        dsimp
        rw [Category.comp_id, ‚Üê L.map_comp, kernel.lift_Œπ] }
  apply (ShortComplex.exact_iff_of_epi_of_isIso_of_mono œÜ).2
  apply ShortComplex.exact_of_f_is_kernel
  exact KernelFork.mapIsLimit _ ((S.exact_iff_exact_image_Œπ).1 hS).fIsKernel L

end

end Functor

end CategoryTheory
