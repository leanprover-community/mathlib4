/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Scott Morrison
-/
import Mathlib.Algebra.Homology.QuasiIso
import Mathlib.CategoryTheory.Preadditive.InjectiveResolution
import Mathlib.Algebra.Homology.HomotopyCategory

#align_import category_theory.abelian.injective_resolution from "leanprover-community/mathlib"@"f0c8bf9245297a541f468be517f1bde6195105e9"

/-!
# Main result

When the underlying category is abelian:
* `CategoryTheory.InjectiveResolution.desc`: Given `I : InjectiveResolution X` and
  `J : InjectiveResolution Y`, any morphism `X âŸ¶ Y` admits a descent to a chain map
  `J.cocomplex âŸ¶ I.cocomplex`. It is a descent in the sense that `I.Î¹` intertwines the descent and
  the original morphism, see `CategoryTheory.InjectiveResolution.desc_commutes`.
* `CategoryTheory.InjectiveResolution.descHomotopy`: Any two such descents are homotopic.
* `CategoryTheory.InjectiveResolution.homotopyEquiv`: Any two injective resolutions of the same
  object are homotopy equivalent.
* `CategoryTheory.injectiveResolutions`: If every object admits an injective resolution, we can
  construct a functor `injectiveResolutions C : C â¥¤ HomotopyCategory C`.

* `CategoryTheory.exact_f_d`: `f` and `Injective.d f` are exact.
* `CategoryTheory.InjectiveResolution.of`: Hence, starting from a monomorphism `X âŸ¶ J`, where `J`
  is injective, we can apply `Injective.d` repeatedly to obtain an injective resolution of `X`.
-/


noncomputable section

open CategoryTheory

open CategoryTheory.Limits

universe v u

namespace CategoryTheory

variable {C : Type u} [Category.{v} C]

open Injective

namespace InjectiveResolution
set_option linter.uppercaseLean3 false -- `InjectiveResolution`

section

variable [HasZeroMorphisms C] [HasZeroObject C] [HasEqualizers C] [HasImages C]

/-- Auxiliary construction for `desc`. -/
def descFZero {Y Z : C} (f : Z âŸ¶ Y) (I : InjectiveResolution Y) (J : InjectiveResolution Z) :
    J.cocomplex.X 0 âŸ¶ I.cocomplex.X 0 :=
  factorThru (f â‰« I.Î¹.f 0) (J.Î¹.f 0)
#align category_theory.InjectiveResolution.desc_f_zero CategoryTheory.InjectiveResolution.descFZero

end

section Abelian

variable [Abelian C]

/-- Auxiliary construction for `desc`. -/
def descFOne {Y Z : C} (f : Z âŸ¶ Y) (I : InjectiveResolution Y) (J : InjectiveResolution Z) :
    J.cocomplex.X 1 âŸ¶ I.cocomplex.X 1 :=
  Exact.desc (descFZero f I J â‰« I.cocomplex.d 0 1) (J.Î¹.f 0) (J.cocomplex.d 0 1)
    (Abelian.Exact.op _ _ J.exactâ‚€) (by simp [â† Category.assoc, descFZero])
                                        -- ğŸ‰ no goals
#align category_theory.InjectiveResolution.desc_f_one CategoryTheory.InjectiveResolution.descFOne

@[simp]
theorem descFOne_zero_comm {Y Z : C} (f : Z âŸ¶ Y) (I : InjectiveResolution Y)
    (J : InjectiveResolution Z) :
    J.cocomplex.d 0 1 â‰« descFOne f I J = descFZero f I J â‰« I.cocomplex.d 0 1 := by
  simp [descFZero, descFOne]
  -- ğŸ‰ no goals
#align category_theory.InjectiveResolution.desc_f_one_zero_comm CategoryTheory.InjectiveResolution.descFOne_zero_comm

/-- Auxiliary construction for `desc`. -/
def descFSucc {Y Z : C} (I : InjectiveResolution Y) (J : InjectiveResolution Z) (n : â„•)
    (g : J.cocomplex.X n âŸ¶ I.cocomplex.X n) (g' : J.cocomplex.X (n + 1) âŸ¶ I.cocomplex.X (n + 1))
    (w : J.cocomplex.d n (n + 1) â‰« g' = g â‰« I.cocomplex.d n (n + 1)) :
    Î£'g'' : J.cocomplex.X (n + 2) âŸ¶ I.cocomplex.X (n + 2),
      J.cocomplex.d (n + 1) (n + 2) â‰« g'' = g' â‰« I.cocomplex.d (n + 1) (n + 2) :=
  âŸ¨@Exact.desc C _ _ _ _ _ _ _ _ _ (g' â‰« I.cocomplex.d (n + 1) (n + 2)) (J.cocomplex.d n (n + 1))
      (J.cocomplex.d (n + 1) (n + 2)) (Abelian.Exact.op _ _ (J.exact _))
      (by simp [â† Category.assoc, w]),
          -- ğŸ‰ no goals
    by simpâŸ©
       -- ğŸ‰ no goals
#align category_theory.InjectiveResolution.desc_f_succ CategoryTheory.InjectiveResolution.descFSucc

/-- A morphism in `C` descends to a chain map between injective resolutions. -/
def desc {Y Z : C} (f : Z âŸ¶ Y) (I : InjectiveResolution Y) (J : InjectiveResolution Z) :
    J.cocomplex âŸ¶ I.cocomplex :=
  CochainComplex.mkHom _ _ (descFZero f _ _) (descFOne f _ _) (descFOne_zero_comm f I J).symm
    fun n âŸ¨g, g', wâŸ© => âŸ¨(descFSucc I J n g g' w.symm).1, (descFSucc I J n g g' w.symm).2.symmâŸ©
#align category_theory.InjectiveResolution.desc CategoryTheory.InjectiveResolution.desc

/-- The resolution maps intertwine the descent of a morphism and that morphism. -/
@[reassoc (attr := simp)]
theorem desc_commutes {Y Z : C} (f : Z âŸ¶ Y) (I : InjectiveResolution Y)
    (J : InjectiveResolution Z) : J.Î¹ â‰« desc f I J = (CochainComplex.singleâ‚€ C).map f â‰« I.Î¹ := by
  ext
  -- âŠ¢ HomologicalComplex.Hom.f (J.Î¹ â‰« desc f I J) 0 = HomologicalComplex.Hom.f ((C â€¦
  simp [desc, descFOne, descFZero]
  -- ğŸ‰ no goals
#align category_theory.InjectiveResolution.desc_commutes CategoryTheory.InjectiveResolution.desc_commutes

-- Now that we've checked this property of the descent, we can seal away the actual definition.
/-- An auxiliary definition for `descHomotopyZero`. -/
def descHomotopyZeroZero {Y Z : C} {I : InjectiveResolution Y} {J : InjectiveResolution Z}
    (f : I.cocomplex âŸ¶ J.cocomplex) (comm : I.Î¹ â‰« f = 0) : I.cocomplex.X 1 âŸ¶ J.cocomplex.X 0 :=
  Exact.desc (f.f 0) (I.Î¹.f 0) (I.cocomplex.d 0 1) (Abelian.Exact.op _ _ I.exactâ‚€)
    (congr_fun (congr_arg HomologicalComplex.Hom.f comm) 0)
#align category_theory.InjectiveResolution.desc_homotopy_zero_zero CategoryTheory.InjectiveResolution.descHomotopyZeroZero

/-- An auxiliary definition for `descHomotopyZero`. -/
def descHomotopyZeroOne {Y Z : C} {I : InjectiveResolution Y} {J : InjectiveResolution Z}
    (f : I.cocomplex âŸ¶ J.cocomplex) (comm : I.Î¹ â‰« f = (0 : _ âŸ¶ J.cocomplex)) :
    I.cocomplex.X 2 âŸ¶ J.cocomplex.X 1 :=
  Exact.desc (f.f 1 - descHomotopyZeroZero f comm â‰« J.cocomplex.d 0 1) (I.cocomplex.d 0 1)
    (I.cocomplex.d 1 2) (Abelian.Exact.op _ _ (I.exact _))
    (by simp [descHomotopyZeroZero, â† Category.assoc])
        -- ğŸ‰ no goals
#align category_theory.InjectiveResolution.desc_homotopy_zero_one CategoryTheory.InjectiveResolution.descHomotopyZeroOne

/-- An auxiliary definition for `descHomotopyZero`. -/
def descHomotopyZeroSucc {Y Z : C} {I : InjectiveResolution Y} {J : InjectiveResolution Z}
    (f : I.cocomplex âŸ¶ J.cocomplex) (n : â„•) (g : I.cocomplex.X (n + 1) âŸ¶ J.cocomplex.X n)
    (g' : I.cocomplex.X (n + 2) âŸ¶ J.cocomplex.X (n + 1))
    (w : f.f (n + 1) = I.cocomplex.d (n + 1) (n + 2) â‰« g' + g â‰« J.cocomplex.d n (n + 1)) :
    I.cocomplex.X (n + 3) âŸ¶ J.cocomplex.X (n + 2) :=
  Exact.desc (f.f (n + 2) - g' â‰« J.cocomplex.d _ _) (I.cocomplex.d (n + 1) (n + 2))
    (I.cocomplex.d (n + 2) (n + 3)) (Abelian.Exact.op _ _ (I.exact _))
    (by
      simp [Preadditive.comp_sub, â† Category.assoc, Preadditive.sub_comp,
        show I.cocomplex.d (n + 1) (n + 2) â‰« g' = f.f (n + 1) - g â‰« J.cocomplex.d n (n + 1) by
          rw [w]
          simp only [add_sub_cancel]])
#align category_theory.InjectiveResolution.desc_homotopy_zero_succ CategoryTheory.InjectiveResolution.descHomotopyZeroSucc

/-- Any descent of the zero morphism is homotopic to zero. -/
def descHomotopyZero {Y Z : C} {I : InjectiveResolution Y} {J : InjectiveResolution Z}
    (f : I.cocomplex âŸ¶ J.cocomplex) (comm : I.Î¹ â‰« f = 0) : Homotopy f 0 :=
  Homotopy.mkCoinductive _ (descHomotopyZeroZero f comm) (by simp [descHomotopyZeroZero])
                                                             -- ğŸ‰ no goals
    (descHomotopyZeroOne f comm) (by simp [descHomotopyZeroOne]) fun n âŸ¨g, g', wâŸ© =>
                                     -- ğŸ‰ no goals
    âŸ¨descHomotopyZeroSucc f n g g' (by simp only [w, add_comm]), by simp [descHomotopyZeroSucc, w]âŸ©
                                       -- ğŸ‰ no goals
                                                                    -- ğŸ‰ no goals
#align category_theory.InjectiveResolution.desc_homotopy_zero CategoryTheory.InjectiveResolution.descHomotopyZero

/-- Two descents of the same morphism are homotopic. -/
def descHomotopy {Y Z : C} (f : Y âŸ¶ Z) {I : InjectiveResolution Y} {J : InjectiveResolution Z}
    (g h : I.cocomplex âŸ¶ J.cocomplex) (g_comm : I.Î¹ â‰« g = (CochainComplex.singleâ‚€ C).map f â‰« J.Î¹)
    (h_comm : I.Î¹ â‰« h = (CochainComplex.singleâ‚€ C).map f â‰« J.Î¹) : Homotopy g h :=
  Homotopy.equivSubZero.invFun (descHomotopyZero _ (by simp [g_comm, h_comm]))
                                                       -- ğŸ‰ no goals
#align category_theory.InjectiveResolution.desc_homotopy CategoryTheory.InjectiveResolution.descHomotopy

/-- The descent of the identity morphism is homotopic to the identity cochain map. -/
def descIdHomotopy (X : C) (I : InjectiveResolution X) :
    Homotopy (desc (ğŸ™ X) I I) (ğŸ™ I.cocomplex) := by
  apply descHomotopy (ğŸ™ X) <;> simp
  -- âŠ¢ I.Î¹ â‰« desc (ğŸ™ X) I I = (CochainComplex.singleâ‚€ C).map (ğŸ™ X) â‰« I.Î¹
                               -- ğŸ‰ no goals
                               -- ğŸ‰ no goals
#align category_theory.InjectiveResolution.desc_id_homotopy CategoryTheory.InjectiveResolution.descIdHomotopy

/-- The descent of a composition is homotopic to the composition of the descents. -/
def descCompHomotopy {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (I : InjectiveResolution X)
    (J : InjectiveResolution Y) (K : InjectiveResolution Z) :
    Homotopy (desc (f â‰« g) K I) (desc f J I â‰« desc g K J) := by
  apply descHomotopy (f â‰« g) <;> simp
  -- âŠ¢ I.Î¹ â‰« desc (f â‰« g) K I = (CochainComplex.singleâ‚€ C).map (f â‰« g) â‰« K.Î¹
                                 -- ğŸ‰ no goals
                                 -- ğŸ‰ no goals
#align category_theory.InjectiveResolution.desc_comp_homotopy CategoryTheory.InjectiveResolution.descCompHomotopy

-- We don't care about the actual definitions of these homotopies.
/-- Any two injective resolutions are homotopy equivalent. -/
def homotopyEquiv {X : C} (I J : InjectiveResolution X) :
    HomotopyEquiv I.cocomplex J.cocomplex where
  hom := desc (ğŸ™ X) J I
  inv := desc (ğŸ™ X) I J
  homotopyHomInvId := (descCompHomotopy (ğŸ™ X) (ğŸ™ X) I J I).symm.trans <| by
    simpa [Category.id_comp] using descIdHomotopy _ _
    -- ğŸ‰ no goals
  homotopyInvHomId := (descCompHomotopy (ğŸ™ X) (ğŸ™ X) J I J).symm.trans <| by
    simpa [Category.id_comp] using descIdHomotopy _ _
    -- ğŸ‰ no goals
#align category_theory.InjectiveResolution.homotopy_equiv CategoryTheory.InjectiveResolution.homotopyEquiv

@[reassoc (attr := simp)] -- Porting note: Originally `@[simp, reassoc.1]`
theorem homotopyEquiv_hom_Î¹ {X : C} (I J : InjectiveResolution X) :
    I.Î¹ â‰« (homotopyEquiv I J).hom = J.Î¹ := by simp [homotopyEquiv]
                                              -- ğŸ‰ no goals
#align category_theory.InjectiveResolution.homotopy_equiv_hom_Î¹ CategoryTheory.InjectiveResolution.homotopyEquiv_hom_Î¹

@[reassoc (attr := simp)] -- Porting note: Originally `@[simp, reassoc.1]`
theorem homotopyEquiv_inv_Î¹ {X : C} (I J : InjectiveResolution X) :
    J.Î¹ â‰« (homotopyEquiv I J).inv = I.Î¹ := by simp [homotopyEquiv]
                                              -- ğŸ‰ no goals
#align category_theory.InjectiveResolution.homotopy_equiv_inv_Î¹ CategoryTheory.InjectiveResolution.homotopyEquiv_inv_Î¹

end Abelian

end InjectiveResolution

section

variable [Abelian C]

/-- An arbitrarily chosen injective resolution of an object. -/
abbrev injectiveResolution (Z : C) [HasInjectiveResolution Z] : CochainComplex C â„• :=
  (HasInjectiveResolution.out (Z := Z)).some.cocomplex
#align category_theory.injective_resolution CategoryTheory.injectiveResolution

/-- The cochain map from cochain complex consisting of `Z` supported in degree `0`
back to the arbitrarily chosen injective resolution `injectiveResolution Z`. -/
abbrev injectiveResolution.Î¹ (Z : C) [HasInjectiveResolution Z] :
    (CochainComplex.singleâ‚€ C).obj Z âŸ¶ injectiveResolution Z :=
  (HasInjectiveResolution.out (Z := Z)).some.Î¹
#align category_theory.injective_resolution.Î¹ CategoryTheory.injectiveResolution.Î¹

/-- The descent of a morphism to a cochain map between the arbitrarily chosen injective resolutions.
-/
abbrev injectiveResolution.desc {X Y : C} (f : X âŸ¶ Y) [HasInjectiveResolution X]
    [HasInjectiveResolution Y] : injectiveResolution X âŸ¶ injectiveResolution Y :=
  InjectiveResolution.desc f _ _
#align category_theory.injective_resolution.desc CategoryTheory.injectiveResolution.desc

variable (C)
variable [HasInjectiveResolutions C]

/-- Taking injective resolutions is functorial,
if considered with target the homotopy category
(`â„•`-indexed cochain complexes and chain maps up to homotopy).
-/
def injectiveResolutions : C â¥¤ HomotopyCategory C (ComplexShape.up â„•) where
  obj X := (HomotopyCategory.quotient _ _).obj (injectiveResolution X)
  map f := (HomotopyCategory.quotient _ _).map (injectiveResolution.desc f)
  map_id X := by
    rw [â† (HomotopyCategory.quotient _ _).map_id]
    -- âŠ¢ { obj := fun X => (HomotopyCategory.quotient C (ComplexShape.up â„•)).obj (inj â€¦
    apply HomotopyCategory.eq_of_homotopy
    -- âŠ¢ Homotopy (injectiveResolution.desc (ğŸ™ X)) (ğŸ™ (injectiveResolution X))
    apply InjectiveResolution.descIdHomotopy
    -- ğŸ‰ no goals
  map_comp f g := by
    rw [â† (HomotopyCategory.quotient _ _).map_comp]
    -- âŠ¢ { obj := fun X => (HomotopyCategory.quotient C (ComplexShape.up â„•)).obj (inj â€¦
    apply HomotopyCategory.eq_of_homotopy
    -- âŠ¢ Homotopy (injectiveResolution.desc (f â‰« g)) (injectiveResolution.desc f â‰« in â€¦
    apply InjectiveResolution.descCompHomotopy
    -- ğŸ‰ no goals
#align category_theory.injective_resolutions CategoryTheory.injectiveResolutions

end

section

variable [Abelian C] [EnoughInjectives C]

theorem exact_f_d {X Y : C} (f : X âŸ¶ Y) : Exact f (d f) :=
  (Abelian.exact_iff _ _).2 <|
    âŸ¨by simp, zero_of_comp_mono (Î¹ _) <| by rw [Category.assoc, kernel.condition]âŸ©
        -- ğŸ‰ no goals
                                            -- ğŸ‰ no goals
#align category_theory.exact_f_d CategoryTheory.exact_f_d

end

namespace InjectiveResolution

/-!
Our goal is to define `InjectiveResolution.of Z : InjectiveResolution Z`.
The `0`-th object in this resolution will just be `Injective.under Z`,
i.e. an arbitrarily chosen injective object with a map from `Z`.
After that, we build the `n+1`-st object as `Injective.syzygies`
applied to the previously constructed morphism,
and the map from the `n`-th object as `Injective.d`.
-/


variable [Abelian C] [EnoughInjectives C]

/-- Auxiliary definition for `InjectiveResolution.of`. -/
@[simps!]
def ofCocomplex (Z : C) : CochainComplex C â„• :=
  CochainComplex.mk' (Injective.under Z) (Injective.syzygies (Injective.Î¹ Z))
    (Injective.d (Injective.Î¹ Z)) fun âŸ¨_, _, fâŸ© =>
    âŸ¨Injective.syzygies f, Injective.d f, (exact_f_d f).wâŸ©
set_option linter.uppercaseLean3 false in
#align category_theory.InjectiveResolution.of_cocomplex CategoryTheory.InjectiveResolution.ofCocomplex

-- Porting note: the Î¹ field in `of` was very, very slow. To assist,
-- implicit arguments were filled in and this particular proof was broken
-- out into a separate result
theorem ofCocomplex_sq_01_comm (Z : C) :
    Injective.Î¹ Z â‰« HomologicalComplex.d (ofCocomplex Z) 0 1 =
    HomologicalComplex.d ((CochainComplex.singleâ‚€ C).obj Z) 0 1 â‰« 0 := by
  simp only [ofCocomplex_d, eq_self_iff_true, eqToHom_refl, Category.comp_id,
    dite_eq_ite, if_true, comp_zero]
  exact (exact_f_d (Injective.Î¹ Z)).w
  -- ğŸ‰ no goals

-- Porting note: the `exact` in `of` was very, very slow. To assist,
-- the whole proof was broken out into a separate result
theorem exact_ofCocomplex (Z : C) (n : â„•) :
    Exact (HomologicalComplex.d (ofCocomplex Z) n (n + 1))
    (HomologicalComplex.d (ofCocomplex Z) (n + 1) (n + 2)) :=
  match n with
-- Porting note: used to be simp; apply exact_f_d on both branches
    | 0 => by simp; apply exact_f_d
              -- âŠ¢ Exact (CochainComplex.mkAux (under Z) (syzygies (Injective.Î¹ Z)) (syzygies ( â€¦
                    -- ğŸ‰ no goals
    | m+1 => by
      simp only [ofCocomplex_X, ComplexShape.up_Rel, not_true, ofCocomplex_d,
        eqToHom_refl, Category.comp_id, dite_eq_ite, ite_true]
      erw [if_pos (c := m + 1 + 1 + 1 = m + 2 + 1) rfl]
      -- âŠ¢ Exact (CochainComplex.mkAux (under Z) (syzygies (Injective.Î¹ Z)) (syzygies ( â€¦
      apply exact_f_d
      -- ğŸ‰ no goals

-- Porting note: still very slow but with `ofCocomplex_sq_01_comm` and
-- `exact_ofCocomplex` as separate results it is more reasonable
/-- In any abelian category with enough injectives,
`InjectiveResolution.of Z` constructs an injective resolution of the object `Z`.
-/
irreducible_def of (Z : C) : InjectiveResolution Z :=
  { cocomplex := ofCocomplex Z
    Î¹ :=
      CochainComplex.mkHom
        ((CochainComplex.singleâ‚€ C).obj Z) (ofCocomplex Z) (Injective.Î¹ Z) 0
          (ofCocomplex_sq_01_comm Z) fun n _ => by
          -- Porting note: used to be âŸ¨0, by extâŸ©
            use 0
            -- âŠ¢ xâœ.snd.fst â‰« HomologicalComplex.d (ofCocomplex Z) (n + 1) (n + 2) = Homologi â€¦
            apply HasZeroObject.from_zero_ext
            -- ğŸ‰ no goals
    injective := by rintro (_ | _ | _ | n) <;> Â· apply Injective.injective_under
                                                 -- ğŸ‰ no goals
                                                 -- ğŸ‰ no goals
                                                 -- ğŸ‰ no goals
                                                 -- ğŸ‰ no goals
    exactâ‚€ := by simpa using exact_f_d (Injective.Î¹ Z)
                 -- ğŸ‰ no goals
    exact := exact_ofCocomplex Z
    mono := Injective.Î¹_mono Z }
set_option linter.uppercaseLean3 false in
#align category_theory.InjectiveResolution.of CategoryTheory.InjectiveResolution.of


instance (priority := 100) (Z : C) : HasInjectiveResolution Z where out := âŸ¨of ZâŸ©

instance (priority := 100) : HasInjectiveResolutions C where out _ := inferInstance

end InjectiveResolution

end CategoryTheory

namespace HomologicalComplex.Hom

variable {C : Type u} [Category.{v} C] [Abelian C]

/-- If `X` is a cochain complex of injective objects and we have a quasi-isomorphism
`f : Y[0] âŸ¶ X`, then `X` is an injective resolution of `Y`. -/
def HomologicalComplex.Hom.fromSingleâ‚€InjectiveResolution (X : CochainComplex C â„•) (Y : C)
    (f : (CochainComplex.singleâ‚€ C).obj Y âŸ¶ X) [QuasiIso f] (H : âˆ€ n, Injective (X.X n)) :
    InjectiveResolution Y where
  cocomplex := X
  Î¹ := f
  injective := H
  exactâ‚€ := from_singleâ‚€_exact_f_d_at_zero f
  exact := from_singleâ‚€_exact_at_succ f
  mono := from_singleâ‚€_mono_at_zero f
set_option linter.uppercaseLean3 false in
#align homological_complex.hom.homological_complex.hom.from_singleâ‚€_InjectiveResolution HomologicalComplex.Hom.HomologicalComplex.Hom.fromSingleâ‚€InjectiveResolution

end HomologicalComplex.Hom
