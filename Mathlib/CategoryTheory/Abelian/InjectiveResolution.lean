/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Scott Morrison
-/
import Mathlib.Algebra.Homology.QuasiIso
import Mathlib.CategoryTheory.Preadditive.InjectiveResolution
import Mathlib.Algebra.Homology.HomotopyCategory
import Mathlib.Algebra.Homology.ShortComplex.Abelian

#align_import category_theory.abelian.injective_resolution from "leanprover-community/mathlib"@"f0c8bf9245297a541f468be517f1bde6195105e9"

/-!
# Main result

When the underlying category is abelian:
* `CategoryTheory.InjectiveResolution.desc`: Given `I : InjectiveResolution X` and
  `J : InjectiveResolution Y`, any morphism `X ‚ü∂ Y` admits a descent to a chain map
  `J.cocomplex ‚ü∂ I.cocomplex`. It is a descent in the sense that `I.Œπ` intertwines the descent and
  the original morphism, see `CategoryTheory.InjectiveResolution.desc_commutes`.
* `CategoryTheory.InjectiveResolution.descHomotopy`: Any two such descents are homotopic.
* `CategoryTheory.InjectiveResolution.homotopyEquiv`: Any two injective resolutions of the same
  object are homotopy equivalent.
* `CategoryTheory.injectiveResolutions`: If every object admits an injective resolution, we can
  construct a functor `injectiveResolutions C : C ‚•§ HomotopyCategory C`.

* `CategoryTheory.exact_f_d`: `f` and `Injective.d f` are exact.
* `CategoryTheory.InjectiveResolution.of`: Hence, starting from a monomorphism `X ‚ü∂ J`, where `J`
  is injective, we can apply `Injective.d` repeatedly to obtain an injective resolution of `X`.
-/


noncomputable section

open CategoryTheory

open CategoryTheory.Limits

universe v u

namespace CategoryTheory

variable {C : Type u} [Category.{v} C]

namespace ShortComplex

variable [Abelian C] {S : ShortComplex C}

def Exact.descToInjective (hS : S.Exact) {J : C} (f : S.X‚ÇÇ ‚ü∂ J) [Injective J] (hf : S.f ‚â´ f = 0) :
    S.X‚ÇÉ ‚ü∂ J := by
  have := hS.mono_fromOpcycles
  exact Injective.factorThru (S.descOpcycles f hf) S.fromOpcycles

@[reassoc (attr := simp)]
lemma Exact.comp_descToInjective
    (hS : S.Exact) {J : C} (f : S.X‚ÇÇ ‚ü∂ J) [Injective J] (hf : S.f ‚â´ f = 0) :
    S.g ‚â´ hS.descToInjective f hf = f := by
  have := hS.mono_fromOpcycles
  dsimp [descToInjective ]
  simp only [‚Üê p_fromOpcycles, Category.assoc, Injective.comp_factorThru, p_descOpcycles]

end ShortComplex

open Injective

namespace InjectiveResolution
set_option linter.uppercaseLean3 false -- `InjectiveResolution`

section

variable [HasZeroMorphisms C] [HasZeroObject C]

/-- Auxiliary construction for `desc`. -/
def descFZero {Y Z : C} (f : Z ‚ü∂ Y) (I : InjectiveResolution Y) (J : InjectiveResolution Z) :
    J.cocomplex.X 0 ‚ü∂ I.cocomplex.X 0 :=
  factorThru (f ‚â´ I.Œπ.f 0) (J.Œπ.f 0)
#align category_theory.InjectiveResolution.desc_f_zero CategoryTheory.InjectiveResolution.descFZero

end

section Abelian

variable [Abelian C]

lemma exact‚ÇÄ {Z : C} (I : InjectiveResolution Z) :
    (ShortComplex.mk _ _ I.Œπ_f_zero_comp_complex_d).Exact :=
  ShortComplex.exact_of_f_is_kernel _ I.isLimitFork

lemma exact_succ {Z : C} (I : InjectiveResolution Z) (n : ‚Ñï):
    (ShortComplex.mk _ _ (I.cocomplex.d_comp_d n (n+1) (n+2))).Exact :=
  (HomologicalComplex.exactAt_iff' _ n (n+1) (n+2) (by simp)
    (by simp only [CochainComplex.next]; linarith)).1 (I.cocomplex_exactAt_succ n)

/-- Auxiliary construction for `desc`. -/
def descFOne {Y Z : C} (f : Z ‚ü∂ Y) (I : InjectiveResolution Y) (J : InjectiveResolution Z) :
    J.cocomplex.X 1 ‚ü∂ I.cocomplex.X 1 :=
  ShortComplex.Exact.descToInjective J.exact‚ÇÄ
    (descFZero f I J ‚â´ I.cocomplex.d 0 1) (by
      dsimp
      simp [‚Üê Category.assoc, descFZero])
#align category_theory.InjectiveResolution.desc_f_one CategoryTheory.InjectiveResolution.descFOne

@[simp]
theorem descFOne_zero_comm {Y Z : C} (f : Z ‚ü∂ Y) (I : InjectiveResolution Y)
    (J : InjectiveResolution Z) :
    J.cocomplex.d 0 1 ‚â´ descFOne f I J = descFZero f I J ‚â´ I.cocomplex.d 0 1 := by
  apply ShortComplex.Exact.comp_descToInjective J.exact‚ÇÄ
#align category_theory.InjectiveResolution.desc_f_one_zero_comm CategoryTheory.InjectiveResolution.descFOne_zero_comm

/-- Auxiliary construction for `desc`. -/
def descFSucc {Y Z : C} (I : InjectiveResolution Y) (J : InjectiveResolution Z) (n : ‚Ñï)
    (g : J.cocomplex.X n ‚ü∂ I.cocomplex.X n) (g' : J.cocomplex.X (n + 1) ‚ü∂ I.cocomplex.X (n + 1))
    (w : J.cocomplex.d n (n + 1) ‚â´ g' = g ‚â´ I.cocomplex.d n (n + 1)) :
    Œ£'g'' : J.cocomplex.X (n + 2) ‚ü∂ I.cocomplex.X (n + 2),
      J.cocomplex.d (n + 1) (n + 2) ‚â´ g'' = g' ‚â´ I.cocomplex.d (n + 1) (n + 2) :=
  ‚ü®ShortComplex.Exact.descToInjective (J.exact_succ n)
    (g' ‚â´ I.cocomplex.d (n + 1) (n + 2)) (by simp [reassoc_of% w]),
      by apply ShortComplex.Exact.comp_descToInjective (J.exact_succ n)‚ü©
#align category_theory.InjectiveResolution.desc_f_succ CategoryTheory.InjectiveResolution.descFSucc

/-- A morphism in `C` descends to a chain map between injective resolutions. -/
def desc {Y Z : C} (f : Z ‚ü∂ Y) (I : InjectiveResolution Y) (J : InjectiveResolution Z) :
    J.cocomplex ‚ü∂ I.cocomplex :=
  CochainComplex.mkHom _ _ (descFZero f _ _) (descFOne f _ _) (descFOne_zero_comm f I J).symm
    fun n ‚ü®g, g', w‚ü© => ‚ü®(descFSucc I J n g g' w.symm).1, (descFSucc I J n g g' w.symm).2.symm‚ü©
#align category_theory.InjectiveResolution.desc CategoryTheory.InjectiveResolution.desc

/-- The resolution maps intertwine the descent of a morphism and that morphism. -/
@[reassoc (attr := simp)]
theorem desc_commutes {Y Z : C} (f : Z ‚ü∂ Y) (I : InjectiveResolution Y)
    (J : InjectiveResolution Z) : J.Œπ ‚â´ desc f I J = (CochainComplex.single‚ÇÄ C).map f ‚â´ I.Œπ := by
  ext
  simp [desc, descFOne, descFZero]
#align category_theory.InjectiveResolution.desc_commutes CategoryTheory.InjectiveResolution.desc_commutes

-- Now that we've checked this property of the descent, we can seal away the actual definition.
/-- An auxiliary definition for `descHomotopyZero`. -/
def descHomotopyZeroZero {Y Z : C} {I : InjectiveResolution Y} {J : InjectiveResolution Z}
    (f : I.cocomplex ‚ü∂ J.cocomplex) (comm : I.Œπ ‚â´ f = 0) : I.cocomplex.X 1 ‚ü∂ J.cocomplex.X 0 :=
  ShortComplex.Exact.descToInjective I.exact‚ÇÄ (f.f 0)
    (congr_fun (congr_arg HomologicalComplex.Hom.f comm) 0)
#align category_theory.InjectiveResolution.desc_homotopy_zero_zero CategoryTheory.InjectiveResolution.descHomotopyZeroZero

@[reassoc (attr := simp)]
lemma comp_descHomotopyZeroZero {Y Z : C} {I : InjectiveResolution Y} {J : InjectiveResolution Z}
    (f : I.cocomplex ‚ü∂ J.cocomplex) (comm : I.Œπ ‚â´ f = 0) :
    I.cocomplex.d 0 1 ‚â´ descHomotopyZeroZero f comm = f.f 0 := by
  apply ShortComplex.Exact.comp_descToInjective I.exact‚ÇÄ

/-- An auxiliary definition for `descHomotopyZero`. -/
def descHomotopyZeroOne {Y Z : C} {I : InjectiveResolution Y} {J : InjectiveResolution Z}
    (f : I.cocomplex ‚ü∂ J.cocomplex) (comm : I.Œπ ‚â´ f = (0 : _ ‚ü∂ J.cocomplex)) :
    I.cocomplex.X 2 ‚ü∂ J.cocomplex.X 1 :=
  ShortComplex.Exact.descToInjective (I.exact_succ 0)
    (f.f 1 - descHomotopyZeroZero f comm ‚â´ J.cocomplex.d 0 1) (by
      rw [Preadditive.comp_sub, comp_descHomotopyZeroZero_assoc f comm,
        HomologicalComplex.Hom.comm, sub_self])
#align category_theory.InjectiveResolution.desc_homotopy_zero_one CategoryTheory.InjectiveResolution.descHomotopyZeroOne

@[reassoc (attr := simp)]
lemma comp_descHomotopyZeroOne {Y Z : C} {I : InjectiveResolution Y} {J : InjectiveResolution Z}
    (f : I.cocomplex ‚ü∂ J.cocomplex) (comm : I.Œπ ‚â´ f = (0 : _ ‚ü∂ J.cocomplex)) :
    I.cocomplex.d 1 2 ‚â´ descHomotopyZeroOne f comm =
      f.f 1 - descHomotopyZeroZero f comm ‚â´ J.cocomplex.d 0 1 := by
  apply ShortComplex.Exact.comp_descToInjective (I.exact_succ 0)

/-- An auxiliary definition for `descHomotopyZero`. -/
def descHomotopyZeroSucc {Y Z : C} {I : InjectiveResolution Y} {J : InjectiveResolution Z}
    (f : I.cocomplex ‚ü∂ J.cocomplex) (n : ‚Ñï) (g : I.cocomplex.X (n + 1) ‚ü∂ J.cocomplex.X n)
    (g' : I.cocomplex.X (n + 2) ‚ü∂ J.cocomplex.X (n + 1))
    (w : f.f (n + 1) = I.cocomplex.d (n + 1) (n + 2) ‚â´ g' + g ‚â´ J.cocomplex.d n (n + 1)) :
    I.cocomplex.X (n + 3) ‚ü∂ J.cocomplex.X (n + 2) :=
  ShortComplex.Exact.descToInjective (I.exact_succ (n+1))
    (f.f (n + 2) - g' ‚â´ J.cocomplex.d _ _) (by
      dsimp
      rw [Preadditive.comp_sub, ‚Üê HomologicalComplex.Hom.comm, w, Preadditive.add_comp,
        Category.assoc, Category.assoc, HomologicalComplex.d_comp_d, comp_zero,
        add_zero, sub_self])
#align category_theory.InjectiveResolution.desc_homotopy_zero_succ CategoryTheory.InjectiveResolution.descHomotopyZeroSucc

@[reassoc (attr := simp)]
def comp_descHomotopyZeroSucc {Y Z : C} {I : InjectiveResolution Y} {J : InjectiveResolution Z}
    (f : I.cocomplex ‚ü∂ J.cocomplex) (n : ‚Ñï) (g : I.cocomplex.X (n + 1) ‚ü∂ J.cocomplex.X n)
    (g' : I.cocomplex.X (n + 2) ‚ü∂ J.cocomplex.X (n + 1))
    (w : f.f (n + 1) = I.cocomplex.d (n + 1) (n + 2) ‚â´ g' + g ‚â´ J.cocomplex.d n (n + 1)) :
    I.cocomplex.d (n+2) (n+3) ‚â´ descHomotopyZeroSucc f n g g' w =
      f.f (n + 2) - g' ‚â´ J.cocomplex.d _ _ := by
  apply ShortComplex.Exact.comp_descToInjective (I.exact_succ (n+1))

/-- Any descent of the zero morphism is homotopic to zero. -/
def descHomotopyZero {Y Z : C} {I : InjectiveResolution Y} {J : InjectiveResolution Z}
    (f : I.cocomplex ‚ü∂ J.cocomplex) (comm : I.Œπ ‚â´ f = 0) : Homotopy f 0 :=
  Homotopy.mkCoinductive _ (descHomotopyZeroZero f comm) (by simp)
    (descHomotopyZeroOne f comm) (by simp)
       fun n ‚ü®g, g', w‚ü© =>
    ‚ü®descHomotopyZeroSucc f n g g' (by simp only [w, add_comm]), by simp‚ü©
#align category_theory.InjectiveResolution.desc_homotopy_zero CategoryTheory.InjectiveResolution.descHomotopyZero

/-- Two descents of the same morphism are homotopic. -/
def descHomotopy {Y Z : C} (f : Y ‚ü∂ Z) {I : InjectiveResolution Y} {J : InjectiveResolution Z}
    (g h : I.cocomplex ‚ü∂ J.cocomplex) (g_comm : I.Œπ ‚â´ g = (CochainComplex.single‚ÇÄ C).map f ‚â´ J.Œπ)
    (h_comm : I.Œπ ‚â´ h = (CochainComplex.single‚ÇÄ C).map f ‚â´ J.Œπ) : Homotopy g h :=
  Homotopy.equivSubZero.invFun (descHomotopyZero _ (by simp [g_comm, h_comm]))
#align category_theory.InjectiveResolution.desc_homotopy CategoryTheory.InjectiveResolution.descHomotopy

/-- The descent of the identity morphism is homotopic to the identity cochain map. -/
def descIdHomotopy (X : C) (I : InjectiveResolution X) :
    Homotopy (desc (ùüô X) I I) (ùüô I.cocomplex) := by
  apply descHomotopy (ùüô X) <;> simp
#align category_theory.InjectiveResolution.desc_id_homotopy CategoryTheory.InjectiveResolution.descIdHomotopy

/-- The descent of a composition is homotopic to the composition of the descents. -/
def descCompHomotopy {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (I : InjectiveResolution X)
    (J : InjectiveResolution Y) (K : InjectiveResolution Z) :
    Homotopy (desc (f ‚â´ g) K I) (desc f J I ‚â´ desc g K J) := by
  apply descHomotopy (f ‚â´ g) <;> simp
#align category_theory.InjectiveResolution.desc_comp_homotopy CategoryTheory.InjectiveResolution.descCompHomotopy

-- We don't care about the actual definitions of these homotopies.
/-- Any two injective resolutions are homotopy equivalent. -/
def homotopyEquiv {X : C} (I J : InjectiveResolution X) :
    HomotopyEquiv I.cocomplex J.cocomplex where
  hom := desc (ùüô X) J I
  inv := desc (ùüô X) I J
  homotopyHomInvId := (descCompHomotopy (ùüô X) (ùüô X) I J I).symm.trans <| by
    simpa [Category.id_comp] using descIdHomotopy _ _
  homotopyInvHomId := (descCompHomotopy (ùüô X) (ùüô X) J I J).symm.trans <| by
    simpa [Category.id_comp] using descIdHomotopy _ _
#align category_theory.InjectiveResolution.homotopy_equiv CategoryTheory.InjectiveResolution.homotopyEquiv

@[reassoc (attr := simp)] -- Porting note: Originally `@[simp, reassoc.1]`
theorem homotopyEquiv_hom_Œπ {X : C} (I J : InjectiveResolution X) :
    I.Œπ ‚â´ (homotopyEquiv I J).hom = J.Œπ := by simp [homotopyEquiv]
#align category_theory.InjectiveResolution.homotopy_equiv_hom_Œπ CategoryTheory.InjectiveResolution.homotopyEquiv_hom_Œπ

@[reassoc (attr := simp)] -- Porting note: Originally `@[simp, reassoc.1]`
theorem homotopyEquiv_inv_Œπ {X : C} (I J : InjectiveResolution X) :
    J.Œπ ‚â´ (homotopyEquiv I J).inv = I.Œπ := by simp [homotopyEquiv]
#align category_theory.InjectiveResolution.homotopy_equiv_inv_Œπ CategoryTheory.InjectiveResolution.homotopyEquiv_inv_Œπ

end Abelian

end InjectiveResolution

section

variable [Abelian C]

abbrev injectiveResolution' (Z : C) [HasInjectiveResolution Z] : InjectiveResolution Z :=
  (HasInjectiveResolution.out (Z := Z)).some

/-- An arbitrarily chosen injective resolution of an object. -/
abbrev injectiveResolution (Z : C) [HasInjectiveResolution Z] : CochainComplex C ‚Ñï :=
  (injectiveResolution' Z).cocomplex
#align category_theory.injective_resolution CategoryTheory.injectiveResolution

/-- The cochain map from cochain complex consisting of `Z` supported in degree `0`
back to the arbitrarily chosen injective resolution `injectiveResolution Z`. -/
abbrev injectiveResolution.Œπ (Z : C) [HasInjectiveResolution Z] :
    (CochainComplex.single‚ÇÄ C).obj Z ‚ü∂ injectiveResolution Z :=
  (injectiveResolution' Z).Œπ
#align category_theory.injective_resolution.Œπ CategoryTheory.injectiveResolution.Œπ

/-- The descent of a morphism to a cochain map between the arbitrarily chosen injective resolutions.
-/
abbrev injectiveResolution.desc {X Y : C} (f : X ‚ü∂ Y) [HasInjectiveResolution X]
    [HasInjectiveResolution Y] : injectiveResolution X ‚ü∂ injectiveResolution Y :=
  InjectiveResolution.desc f _ _
#align category_theory.injective_resolution.desc CategoryTheory.injectiveResolution.desc

@[reassoc (attr := simp)]
lemma injectiveResolution.desc_comm {X Y : C} (f : X ‚ü∂ Y) [HasInjectiveResolution X]
    [HasInjectiveResolution Y] :
    (injectiveResolution.Œπ X).f 0 ‚â´ (injectiveResolution.desc f).f 0 =
      f ‚â´ (injectiveResolution.Œπ Y).f 0 := by
  rw [‚Üê HomologicalComplex.comp_f, InjectiveResolution.desc_commutes,
    HomologicalComplex.comp_f, CochainComplex.single‚ÇÄ_map_f_0]

variable (C)
variable [HasInjectiveResolutions C]

/-- Taking injective resolutions is functorial,
if considered with target the homotopy category
(`‚Ñï`-indexed cochain complexes and chain maps up to homotopy).
-/
def injectiveResolutions : C ‚•§ HomotopyCategory C (ComplexShape.up ‚Ñï) where
  obj X := (HomotopyCategory.quotient _ _).obj (injectiveResolution X)
  map f := (HomotopyCategory.quotient _ _).map (injectiveResolution.desc f)
  map_id X := by
    rw [‚Üê (HomotopyCategory.quotient _ _).map_id]
    apply HomotopyCategory.eq_of_homotopy
    apply InjectiveResolution.descIdHomotopy
  map_comp f g := by
    rw [‚Üê (HomotopyCategory.quotient _ _).map_comp]
    apply HomotopyCategory.eq_of_homotopy
    apply InjectiveResolution.descCompHomotopy
#align category_theory.injective_resolutions CategoryTheory.injectiveResolutions

variable {C}

def InjectiveResolution.iso {X : C} (I : InjectiveResolution X) :
    (injectiveResolutions C).obj X ‚âÖ
      (HomotopyCategory.quotient _ _).obj I.cocomplex :=
  HomotopyCategory.isoOfHomotopyEquiv (homotopyEquiv _ _)

@[reassoc]
lemma InjectiveResolution.iso_hom_naturality {X Y : C} (f : X ‚ü∂ Y)
    (I : InjectiveResolution X) (J : InjectiveResolution Y)
    (œÜ : I.cocomplex ‚ü∂ J.cocomplex) (comm : I.Œπ.f 0 ‚â´ œÜ.f 0 = f ‚â´ J.Œπ.f 0) :
    (injectiveResolutions C).map f ‚â´ J.iso.hom =
      I.iso.hom ‚â´ (HomotopyCategory.quotient _ _).map œÜ := by
  apply HomotopyCategory.eq_of_homotopy
  apply descHomotopy f
  all_goals aesop_cat

@[reassoc]
lemma InjectiveResolution.iso_inv_naturality {X Y : C} (f : X ‚ü∂ Y)
    (I : InjectiveResolution X) (J : InjectiveResolution Y)
    (œÜ : I.cocomplex ‚ü∂ J.cocomplex) (comm : I.Œπ.f 0 ‚â´ œÜ.f 0 = f ‚â´ J.Œπ.f 0) :
    I.iso.inv ‚â´ (injectiveResolutions C).map f =
      (HomotopyCategory.quotient _ _).map œÜ ‚â´ J.iso.inv := by
  rw [‚Üê cancel_mono (J.iso).hom, Category.assoc, iso_hom_naturality f I J œÜ comm,
    Iso.inv_hom_id_assoc, Category.assoc, Iso.inv_hom_id, Category.comp_id]

end

section

variable [Abelian C] [EnoughInjectives C]

--theorem exact_f_d {X Y : C} (f : X ‚ü∂ Y) : Exact f (d f) :=
--  (Abelian.exact_iff _ _).2 <|
--    ‚ü®by simp, zero_of_comp_mono (Œπ _) <| by rw [Category.assoc, kernel.condition]‚ü©
--#align category_theory.exact_f_d CategoryTheory.exact_f_d

theorem exact_f_d {X Y : C} (f : X ‚ü∂ Y) : (ShortComplex.mk f (d f) (by simp)).Exact := by
  let Œ± : ShortComplex.mk f (cokernel.œÄ f) (by simp) ‚ü∂ ShortComplex.mk f (d f) (by simp) :=
    { œÑ‚ÇÅ := ùüô _
      œÑ‚ÇÇ := ùüô _
      œÑ‚ÇÉ := Injective.Œπ _  }
  have : Epi Œ±.œÑ‚ÇÅ := by dsimp; infer_instance
  have : IsIso Œ±.œÑ‚ÇÇ := by dsimp; infer_instance
  have : Mono Œ±.œÑ‚ÇÉ := by dsimp; infer_instance
  rw [‚Üê ShortComplex.exact_iff_of_epi_of_isIso_of_mono Œ±]
  apply ShortComplex.exact_of_g_is_cokernel
  apply cokernelIsCokernel

end

namespace InjectiveResolution

/-!
Our goal is to define `InjectiveResolution.of Z : InjectiveResolution Z`.
The `0`-th object in this resolution will just be `Injective.under Z`,
i.e. an arbitrarily chosen injective object with a map from `Z`.
After that, we build the `n+1`-st object as `Injective.syzygies`
applied to the previously constructed morphism,
and the map from the `n`-th object as `Injective.d`.
-/


variable [Abelian C] [EnoughInjectives C]

/-- Auxiliary definition for `InjectiveResolution.of`. -/
@[simps!]
def ofCocomplex (Z : C) : CochainComplex C ‚Ñï :=
  CochainComplex.mk' (Injective.under Z) (Injective.syzygies (Injective.Œπ Z))
    (Injective.d (Injective.Œπ Z)) fun ‚ü®_, _, f‚ü© =>
    ‚ü®Injective.syzygies f, Injective.d f, by simp‚ü©
set_option linter.uppercaseLean3 false in
#align category_theory.InjectiveResolution.of_cocomplex CategoryTheory.InjectiveResolution.ofCocomplex

-- Porting note: the Œπ field in `of` was very, very slow. To assist,
-- implicit arguments were filled in and this particular proof was broken
-- out into a separate result
theorem ofCocomplex_sq_01_comm (Z : C) :
    Injective.Œπ Z ‚â´ HomologicalComplex.d (ofCocomplex Z) 0 1 =
    HomologicalComplex.d ((CochainComplex.single‚ÇÄ C).obj Z) 0 1 ‚â´ 0 := by
  simp only [ofCocomplex_d, eq_self_iff_true, eqToHom_refl, Category.comp_id,
    dite_eq_ite, if_true, comp_zero]
  erw [cokernel.condition_assoc, zero_comp]

lemma ofCocomplex_exactAt_succ (Z : C) (n : ‚Ñï) : (ofCocomplex Z).ExactAt (n+1) := by
  rw [HomologicalComplex.exactAt_iff' _ n (n+1) (n+1+1) (by simp) (by simp)]
  obtain (_|n) := n
  all_goals
    dsimp [ofCocomplex, CochainComplex.mk', HomologicalComplex.sc',
      HomologicalComplex.shortComplexFunctor', CochainComplex.mk, CochainComplex.of]
    simp
    apply exact_f_d

-- Porting note: still very slow but with `ofCocomplex_sq_01_comm` and
-- `exact_ofCocomplex` as separate results it is more reasonable
/-- In any abelian category with enough injectives,
`InjectiveResolution.of Z` constructs an injective resolution of the object `Z`.
-/
irreducible_def of (Z : C) : InjectiveResolution Z :=
  { cocomplex := ofCocomplex Z
    Œπ :=
      CochainComplex.mkHom
        ((CochainComplex.single‚ÇÄ C).obj Z) (ofCocomplex Z) (Injective.Œπ Z) 0
          (ofCocomplex_sq_01_comm Z) fun n _ => by
          -- Porting note: used to be ‚ü®0, by ext‚ü©
            use 0
            apply HasZeroObject.from_zero_ext
    injective := by rintro (_ | _ | _ | n) <;> ¬∑ apply Injective.injective_under
    hŒπ := ‚ü®fun n => by
      cases n
      ¬∑ rw [CochainComplex.quasiIsoAt‚ÇÄ_iff,
          ShortComplex.quasiIso_iff_of_zeros]
        ¬∑ exact ‚ü®Injective.Œπ_mono Z, by simpa using exact_f_d (Injective.Œπ Z)‚ü©
        all_goals rfl
      ¬∑ rw [quasiIsoAt_iff_exactAt]
        apply ofCocomplex_exactAt_succ
        apply CochainComplex.single‚ÇÄ_exactAt‚ü© }
    --exact‚ÇÄ := by simpa using exact_f_d (Injective.Œπ Z)
    --exact := exact_ofCocomplex Z
    --mono := Injective.Œπ_mono Z }
set_option linter.uppercaseLean3 false in
#align category_theory.InjectiveResolution.of CategoryTheory.InjectiveResolution.of


instance (priority := 100) (Z : C) : HasInjectiveResolution Z where out := ‚ü®of Z‚ü©

instance (priority := 100) : HasInjectiveResolutions C where out _ := inferInstance

end InjectiveResolution

end CategoryTheory

namespace HomologicalComplex.Hom

variable {C : Type u} [Category.{v} C] [Abelian C]

/-- If `X` is a cochain complex of injective objects and we have a quasi-isomorphism
`f : Y[0] ‚ü∂ X`, then `X` is an injective resolution of `Y`. -/
def HomologicalComplex.Hom.fromSingle‚ÇÄInjectiveResolution (X : CochainComplex C ‚Ñï) (Y : C)
    (f : (CochainComplex.single‚ÇÄ C).obj Y ‚ü∂ X) [QuasiIso f] (H : ‚àÄ n, Injective (X.X n)) :
    InjectiveResolution Y where
  cocomplex := X
  Œπ := f
  injective := H
set_option linter.uppercaseLean3 false in
#align homological_complex.hom.homological_complex.hom.from_single‚ÇÄ_InjectiveResolution HomologicalComplex.Hom.HomologicalComplex.Hom.fromSingle‚ÇÄInjectiveResolution

end HomologicalComplex.Hom
