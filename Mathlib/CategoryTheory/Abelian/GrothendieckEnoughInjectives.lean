/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import Mathlib.CategoryTheory.MorphismProperty.Limits
import Mathlib.CategoryTheory.Abelian.GrothendieckCategory
import Mathlib.CategoryTheory.Abelian.Refinements
import Mathlib.CategoryTheory.MorphismProperty.LiftingProperty
import Mathlib.CategoryTheory.Subobject.Lattice
import Mathlib.CategoryTheory.Presentable.GrothendieckAbelian
import Mathlib.CategoryTheory.SmallObject.Basic
import Mathlib.CategoryTheory.SmallObject.TransfiniteCompositionLifting
import Mathlib.Order.TransfiniteIteration
import Mathlib.SetTheory.Ordinal.Basic

/-!
# Grothendieck abelian categories have enough injectives

TODO

-/

universe w v u

lemma Cardinal.zero_lt_ord_iff (Îº : Cardinal.{w}) : 0 < Îº.ord â†” Îº â‰  0 := by
  constructor
  Â· intro h h'
    simp only [h', ord_zero, lt_self_iff_false] at h
  Â· intro h
    by_contra!
    exact h (ord_eq_zero.1 (le_antisymm this (Ordinal.zero_le _)))

namespace CategoryTheory

open Category Opposite Limits ZeroObject

lemma IsFiltered.set_iio {J : Type w} [LinearOrder J] [OrderBot J]
    (j : J) (hj : Order.IsSuccLimit j) : IsFiltered (Set.Iio j) := by
  have : Nonempty (Set.Iio j) := âŸ¨âŸ¨âŠ¥, by
    simp only [Set.mem_Iio]
    by_contra!
    simp only [le_bot_iff] at this
    subst this
    have := hj.not_isMin
    simp at thisâŸ©âŸ©
  infer_instance

noncomputable instance (o : Ordinal.{w}) : SuccOrder o.toType :=
  SuccOrder.ofLinearWellFoundedLT o.toType

variable {C : Type u} [Category.{v} C]

section

lemma Injective.hasLiftingProperty_of_isZero
    {A B I Z : C} (i : A âŸ¶ B) [Mono i] [Injective I] (p : I âŸ¶ Z) (hZ : IsZero Z) :
    HasLiftingProperty i p where
  sq_hasLift {f g} sq := âŸ¨âŸ¨{
    l := Injective.factorThru f i
    fac_right := hZ.eq_of_tgt _ _ }âŸ©âŸ©

instance {A B I : C} (i : A âŸ¶ B)  [Mono i] [Injective I] [HasZeroObject C] (p : I âŸ¶ 0) :
    HasLiftingProperty i (p : I âŸ¶ 0) :=
  Injective.hasLiftingProperty_of_isZero i p (isZero_zero C)

lemma injective_iff_eq_zero [HasZeroMorphisms C] {I Z : C} (p : I âŸ¶ Z) (hZ : IsZero Z) :
    Injective I â†” (MorphismProperty.monomorphisms C).rlp p := by
  obtain rfl := hZ.eq_of_tgt p 0
  constructor
  Â· intro _ A B i (_ : Mono i)
    exact Injective.hasLiftingProperty_of_isZero i 0 hZ
  Â· intro h
    constructor
    intro A B f i hi
    have := h _ hi
    have sq : CommSq f i (0 : I âŸ¶ Z) 0 := âŸ¨by simpâŸ©
    exact âŸ¨sq.lift, by simpâŸ©

lemma injective_iff_monomorphisms_rlp_zero [HasZeroMorphisms C] [HasZeroObject C] (I : C) :
    Injective I â†” (MorphismProperty.monomorphisms C).rlp (0 : I âŸ¶ 0) :=
  injective_iff_eq_zero _ (isZero_zero C)

end

namespace Subobject

lemma mk_lt_mk_of_comm {X Aâ‚ Aâ‚‚ : C} {iâ‚ : Aâ‚ âŸ¶ X} {iâ‚‚ : Aâ‚‚ âŸ¶ X} [Mono iâ‚] [Mono iâ‚‚]
    (f : Aâ‚ âŸ¶ Aâ‚‚) (fac : f â‰« iâ‚‚ = iâ‚) (hf : Â¬ IsIso f) :
    Subobject.mk iâ‚ < Subobject.mk iâ‚‚ := by
  obtain _ | h := (mk_le_mk_of_comm _ fac).lt_or_eq
  Â· assumption
  Â· exfalso
    refine hf âŸ¨ofMkLEMk iâ‚‚ iâ‚ (by rw [h]), ?_, ?_âŸ©
    Â· simp only [â† cancel_mono iâ‚, assoc, ofMkLEMk_comp, fac, id_comp]
    Â· simp only [â† cancel_mono iâ‚‚, assoc, ofMkLEMk_comp, fac, id_comp]

lemma mk_lt_mk_iff_of_comm {X Aâ‚ Aâ‚‚ : C} {iâ‚ : Aâ‚ âŸ¶ X} {iâ‚‚ : Aâ‚‚ âŸ¶ X} [Mono iâ‚] [Mono iâ‚‚]
    (f : Aâ‚ âŸ¶ Aâ‚‚) (fac : f â‰« iâ‚‚ = iâ‚) :
    Subobject.mk iâ‚ < Subobject.mk iâ‚‚ â†” Â¬ IsIso f :=
  âŸ¨fun h hf â†¦ by simp only [mk_eq_mk_of_comm iâ‚ iâ‚‚ (asIso f) fac, lt_self_iff_false] at h,
    mk_lt_mk_of_comm f facâŸ©

end Subobject

section Preadditive

variable [Preadditive C]

variable {A B X Y : C} {f : A âŸ¶ X} {i : A âŸ¶ B} {j : X âŸ¶ Y} {g : B âŸ¶ Y}
  [HasBinaryBiproduct B X]

namespace CommSq

variable (h : CommSq f i j g)

@[simps]
noncomputable def shortComplex : ShortComplex C where
  f := biprod.lift i (-f)
  g := biprod.desc g j
  zero := by simp [h.w]

end CommSq

namespace IsPushout

variable (h : IsPushout f i j g)

noncomputable def isColimitCokernelCoforkShortComplex :
    IsColimit (CokernelCofork.ofÏ€ _ h.shortComplex.zero) :=
  CokernelCofork.IsColimit.ofÏ€ _ _
    (fun b hb â†¦ h.desc (biprod.inr â‰« b) (biprod.inl â‰« b) (by
      dsimp at hb
      simp only [biprod.lift_eq, Preadditive.neg_comp,
        Preadditive.sub_comp, assoc, â† sub_eq_add_neg, sub_eq_zero] at hb
      exact hb.symm)) (by aesop_cat) (fun _ _ _ hm â†¦ by
        refine h.hom_ext (by simpa using biprod.inr â‰«= hm)
          (by simpa using biprod.inl â‰«= hm))

lemma epi_shortComplex_g : Epi h.shortComplex.g := by
  rw [Preadditive.epi_iff_cancel_zero]
  intro _ b hb
  exact Cofork.IsColimit.hom_ext h.isColimitCokernelCoforkShortComplex
    (by simpa using hb)

end IsPushout

end Preadditive

section Abelian

namespace IsPushout

variable [Abelian C] {A B X Y : C} {f : A âŸ¶ X} {i : A âŸ¶ B} {j : X âŸ¶ Y} {g : B âŸ¶ Y}

lemma shortComplex_exact (h : IsPushout f i j g) : h.shortComplex.Exact :=
  h.shortComplex.exact_of_g_is_cokernel
    h.isColimitCokernelCoforkShortComplex

lemma hom_eq_add_up_to_refinements (h : IsPushout f i j g) {T : C} (y : T âŸ¶ Y) :
    âˆƒ (T' : C) (Ï€ : T' âŸ¶ T) (_ : Epi Ï€) (b : T' âŸ¶ B) (x : T' âŸ¶ X),
      Ï€ â‰« y = b â‰« g + x â‰« j := by
  have := h.epi_shortComplex_g
  obtain âŸ¨T', Ï€, _, z, hzâŸ© := surjective_up_to_refinements_of_epi h.shortComplex.g y
  refine âŸ¨T', Ï€, inferInstance, z â‰« biprod.fst, z â‰« biprod.snd, ?_âŸ©
  simp only [hz, assoc, â† Preadditive.comp_add]
  congr
  aesop_cat

lemma mono_of_isPushout_of_isPullback_of_mono {A B X Y : C}
    {f : A âŸ¶ X} {i : A âŸ¶ B} {j : X âŸ¶ Y} {g : B âŸ¶ Y}
    (hâ‚ : IsPushout f i j g) {Z : C} {j' : X âŸ¶ Z} {g' : B âŸ¶ Z}
    (hâ‚‚ : IsPullback f i j' g') (k : Y âŸ¶ Z)
    (facâ‚ : j â‰« k = j') (facâ‚‚ : g â‰« k = g') [Mono j'] : Mono k :=
  Preadditive.mono_of_cancel_zero _ (fun {Tâ‚€} y hy â†¦ by
    obtain âŸ¨Tâ‚, Ï€, _, b, x, eqâŸ© := hom_eq_add_up_to_refinements hâ‚ y
    have facâ‚ƒ : (-x) â‰« j' = b â‰« g' := by
      rw [Preadditive.neg_comp, neg_eq_iff_add_eq_zero, add_comm, â† facâ‚‚, â† facâ‚,
        â† assoc, â† assoc, â† Preadditive.add_comp, â† eq, assoc, hy, comp_zero]
    obtain âŸ¨x', hx'âŸ© : âˆƒ x', Ï€ â‰« y = x' â‰« j := by
      refine âŸ¨x + hâ‚‚.lift (-x) b facâ‚ƒ â‰« f, ?_âŸ©
      rw [eq, Preadditive.add_comp, assoc, hâ‚.w, IsPullback.lift_snd_assoc, add_comm]
    rw [â† cancel_epi Ï€, comp_zero, reassoc_of% hx', facâ‚] at hy
    obtain rfl := zero_of_comp_mono _ hy
    rw [zero_comp] at hx'
    rw [â† cancel_epi Ï€, hx', comp_zero])

end IsPushout

end Abelian

namespace Abelian

variable [Abelian C]


end Abelian

namespace IsDetecting

lemma isIso_iff_of_mono {S : Set C} (hS : IsDetecting S)
    {X Y : C} (f : X âŸ¶ Y) [Mono f] :
    IsIso f â†” âˆ€ (s : S), Function.Surjective ((coyoneda.obj (op s.1)).map f) := by
  constructor
  Â· intro h
    rw [isIso_iff_yoneda_map_bijective] at h
    rintro âŸ¨A, _âŸ©
    exact (h A).2
  Â· intro hf
    apply hS
    rintro A hA g
    apply existsUnique_of_exists_of_unique
    Â· exact hf âŸ¨A, hAâŸ© g
    Â· intro lâ‚ lâ‚‚ hâ‚ hâ‚‚
      rw [â† cancel_mono f, hâ‚, hâ‚‚]

end IsDetecting

namespace Abelian

namespace IsGrothendieckAbelian

def generatingMonomorphisms (G : C) : MorphismProperty C :=
  MorphismProperty.ofHoms (fun (X : Subobject G) â†¦ X.arrow)

lemma generatingMonomorphisms_le_monomorphisms (G : C) :
    generatingMonomorphisms G â‰¤ MorphismProperty.monomorphisms C := by
  rintro _ _ _ âŸ¨XâŸ©
  exact inferInstanceAs (Mono _)

variable (G : C)

abbrev generatingMonomorphismsPushouts := (generatingMonomorphisms G).pushouts

variable [Abelian C]

lemma isomorphisms_le_generatingMonomorphismsPushouts :
    MorphismProperty.isomorphisms C â‰¤ generatingMonomorphismsPushouts G :=
  MorphismProperty.isomorphisms_le_pushouts _ (fun _ â†¦ âŸ¨_, _, _, âŸ¨âŠ¤âŸ©, 0, inferInstanceâŸ©)

variable {G} (hG : IsSeparator G)

namespace TransfiniteCompositionMonoPushouts

include hG

lemma exists_generatingMonomorphismsPushouts {X Y : C} (p : X âŸ¶ Y) [Mono p]
    (hp : Â¬ IsIso p) :
    âˆƒ (X' : C) (i : X âŸ¶ X') (p' : X' âŸ¶ Y) (_ : generatingMonomorphismsPushouts G i)
      (_ : Â¬ IsIso i) (_ : Mono p'), i â‰« p' = p := by
  rw [hG.isDetector.isIso_iff_of_mono] at hp
  simp only [coyoneda_obj_obj, Subtype.forall, Set.mem_singleton_iff, forall_eq,
    Function.Surjective, not_forall, not_exists] at hp
  obtain âŸ¨f, hfâŸ© := hp
  let Ï† : pushout (pullback.fst p f) (pullback.snd p f) âŸ¶ Y :=
    pushout.desc p f pullback.condition
  refine âŸ¨pushout (pullback.fst p f) (pullback.snd p f), pushout.inl _ _, Ï†,
    âŸ¨_, _, _, (Subobject.underlyingIso _).hom â‰« pullback.fst _ _,
    pushout.inr _ _, âŸ¨Subobject.mk (pullback.snd p f)âŸ©,
    (IsPushout.of_hasPushout (pullback.fst p f) (pullback.snd p f)).of_iso
      ((Subobject.underlyingIso _).symm) (Iso.refl _) (Iso.refl _)
      (Iso.refl _) (by simp) (by simp) (by simp) (by simp)âŸ©, ?_, ?_, by simp [Ï†]âŸ©
  Â· intro h
    rw [isIso_iff_yoneda_map_bijective] at h
    obtain âŸ¨a, haâŸ© := (h G).2 (pushout.inr _ _)
    exact hf a (by simpa [Ï†] using ha =â‰« Ï†)
  Â· exact (IsPushout.of_hasPushout _ _).mono_of_isPushout_of_isPullback_of_mono
      (IsPullback.of_hasPullback p f) Ï† (by simp [Ï†]) (by simp [Ï†])

variable {X : C}

lemma exists_larger_subobject {X : C} (A : Subobject X) (hA : A â‰  âŠ¤) :
    âˆƒ (A' : Subobject X) (h : A < A'),
      generatingMonomorphismsPushouts G (Subobject.ofLE _ _ h.le) := by
  induction' A using Subobject.ind with Y f _
  obtain âŸ¨X', i, p', hi, hi', hp', facâŸ© := exists_generatingMonomorphismsPushouts hG f
    (by simpa only [Subobject.isIso_iff_mk_eq_top] using hA)
  refine âŸ¨Subobject.mk p', Subobject.mk_lt_mk_of_comm i fac hi',
    (MorphismProperty.arrow_mk_iso_iff _ ?_).2 hiâŸ©
  refine Arrow.isoMk (Subobject.underlyingIso f) (Subobject.underlyingIso p') ?_
  dsimp
  simp only [â† cancel_mono p', assoc, fac,
    Subobject.underlyingIso_hom_comp_eq_mk, Subobject.ofLE_arrow]

open Classical in
noncomputable def largerSubobject (A : Subobject X) : Subobject X :=
  if hA : A = âŠ¤ then âŠ¤ else (exists_larger_subobject hG A hA).choose

variable (X) in
@[simp]
lemma largerSubobject_top : largerSubobject hG (âŠ¤ : Subobject X) = âŠ¤ := dif_pos rfl

lemma lt_largerSubobject (A : Subobject X) (hA : A â‰  âŠ¤) :
    A < largerSubobject hG A := by
  dsimp only [largerSubobject]
  rw [dif_neg hA]
  exact (exists_larger_subobject hG A hA).choose_spec.choose

lemma le_largerSubobject (A : Subobject X) :
    A â‰¤ largerSubobject hG A := by
  by_cases hA : A = âŠ¤
  Â· subst hA
    simp only [largerSubobject_top, le_refl]
  Â· exact (lt_largerSubobject hG A hA).le

lemma generatingMonomorphismsPushouts_ofLE_le_largerSubobject (A : Subobject X) :
      generatingMonomorphismsPushouts G (Subobject.ofLE _ _ (le_largerSubobject hG A)) := by
  by_cases hA : A = âŠ¤
  Â· subst hA
    have := (Subobject.isIso_arrow_iff_eq_top (largerSubobject hG (âŠ¤ : Subobject X))).2 (by simp)
    exact (MorphismProperty.arrow_mk_iso_iff _
      (Arrow.isoMk (asIso (Subobject.arrow _)) (asIso (Subobject.arrow _)) (by simp))).2
        (isomorphisms_le_generatingMonomorphismsPushouts G (ðŸ™ X)
          (MorphismProperty.isomorphisms.infer_property _))
  Â· refine (MorphismProperty.arrow_mk_iso_iff _ ?_).1
      (exists_larger_subobject hG A hA).choose_spec.choose_spec
    exact Arrow.isoMk (Iso.refl _)
      (Subobject.isoOfEq _ _ ((by simp [largerSubobject, dif_neg hA])))

variable [IsGrothendieckAbelian.{w} C]

lemma top_mem_range (Aâ‚€ : Subobject X) {J : Type w} [LinearOrder J] [OrderBot J] [SuccOrder J]
  [WellFoundedLT J] (hJ : Cardinal.mk (Shrink (Subobject X)) < Cardinal.mk J) :
    âˆƒ (j : J), transfiniteIterate (largerSubobject hG) j Aâ‚€ = âŠ¤ :=
  top_mem_range_transfiniteIterate (largerSubobject hG) Aâ‚€ (lt_largerSubobject hG) (by simp)
    (fun h â†¦ (lt_self_iff_false _).1 (lt_of_le_of_lt
      (Cardinal.mk_le_of_injective ((equivShrink.{w} (Subobject X)).injective.comp h)) hJ))

lemma exists_ordinal (Aâ‚€ : Subobject X) :
    âˆƒ (o : Ordinal.{w}) (j : o.toType), transfiniteIterate (largerSubobject hG) j Aâ‚€ = âŠ¤ := by
  let Îº := Order.succ (Cardinal.mk (Shrink.{w} (Subobject X)))
  have : OrderBot Îº.ord.toType := Ordinal.toTypeOrderBotOfPos (by
    rw [Cardinal.zero_lt_ord_iff]
    exact Cardinal.succ_ne_zero _)
  exact âŸ¨Îº.ord, top_mem_range hG Aâ‚€ (lt_of_lt_of_le (Order.lt_succ _) (by simp))âŸ©

section

variable (Aâ‚€ : Subobject X) (J : Type w) [LinearOrder J] [OrderBot J] [SuccOrder J]
  [WellFoundedLT J]

@[simps]
noncomputable def functorToMonoOver : J â¥¤ MonoOver X where
  obj j := MonoOver.mk' (transfiniteIterate (largerSubobject hG) j Aâ‚€).arrow
  map {j j'} f := MonoOver.homMk (Subobject.ofLE _ _
      (monotone_transfiniteIterate _ _ (le_largerSubobject hG) (leOfHom f)))

noncomputable abbrev functor : J â¥¤ C :=
  functorToMonoOver hG Aâ‚€ J â‹™ MonoOver.forget _ â‹™ Over.forget _

instance : (functor hG Aâ‚€ J).IsWellOrderContinuous where
  nonempty_isColimit m hm := âŸ¨by
    have := IsFiltered.set_iio _ hm
    let c := (functorToMonoOver hG Aâ‚€ J â‹™ MonoOver.forget _).coconeLT m
    have : Mono c.pt.hom := by dsimp [c]; infer_instance
    apply IsGrothendieckAbelian.isColimitMapCoconeOfSubobjectMkEqISup
      ((functorToMonoOver hG Aâ‚€ J).restrictionLT m) c
    dsimp [c]
    simp only [Subobject.mk_arrow]
    exact transfiniteIterate_limit (largerSubobject hG) Aâ‚€ m hmâŸ©

example (j : J) : ((functor hG Aâ‚€ J).restrictionLE j).IsWellOrderContinuous := inferInstance

lemma mono_functor_map_le_succ (j : J) (hj : Â¬IsMax j) :
    generatingMonomorphismsPushouts G ((functor hG Aâ‚€ J).map (homOfLE (Order.le_succ j))) := by
  refine (MorphismProperty.arrow_mk_iso_iff _ ?_).2
    (generatingMonomorphismsPushouts_ofLE_le_largerSubobject hG
      (transfiniteIterate (largerSubobject hG) j Aâ‚€))
  exact Arrow.isoMk (Iso.refl _) (Subobject.isoOfEq _ _ (transfiniteIterate_succ _ _ _ hj))
    (by simp [MonoOver.forget])

end

section

variable {A : C} {f : A âŸ¶ X} [Mono f] {J : Type w} [LinearOrder J] [OrderBot J] [SuccOrder J]
  [WellFoundedLT J]
  {j : J} (hj : transfiniteIterate (largerSubobject hG) j (Subobject.mk f) = âŠ¤)

noncomputable def arrowIso :
    Arrow.mk f â‰… Arrow.mk (((functor hG (Subobject.mk f) J).coconeLE j).Î¹.app âŠ¥) := by
  let t := transfiniteIterate (largerSubobject hG) j (Subobject.mk f)
  have := (Subobject.isIso_arrow_iff_eq_top t).mpr hj
  refine (Arrow.isoMk (Subobject.isoOfEq _ _ (transfiniteIterate_bot _ _) â‰ªâ‰«
    Subobject.underlyingIso f) (asIso t.arrow) ?_).symm
  simp [MonoOver.forget]

variable (f)

include hj in
lemma generatingMonomorphismsPushouts_transfiniteCompositionOfShape :
    (generatingMonomorphismsPushouts G).transfiniteCompositionsOfShape (Set.Iic j) f := by
  refine (MorphismProperty.arrow_iso_iff _ (arrowIso hG hj)).2 ?_
  dsimp [MonoOver.forget]
  refine âŸ¨_, fun âŸ¨k, hkâŸ© hk' â†¦ ?_, _,
    (functor hG (Subobject.mk f) J).isColimitCoconeLE jâŸ©
  dsimp [MonoOver.forget]
  convert generatingMonomorphismsPushouts_ofLE_le_largerSubobject hG
    (transfiniteIterate (largerSubobject hG) k (Subobject.mk f)) using 2
  all_goals
    rw [Set.Iic.succ_eq _ hk', transfiniteIterate_succ _ _ _ (Set.Iic.not_isMax_coe _ hk')]

end

end TransfiniteCompositionMonoPushouts

include hG in
open TransfiniteCompositionMonoPushouts in
@[simp]
lemma generatingMonomorphismsPushouts_rlp [IsGrothendieckAbelian.{w} C] :
    (generatingMonomorphisms G).rlp = (MorphismProperty.monomorphisms C).rlp := by
  apply le_antisymm
  Â· intro X Y p hp A B i (_ : Mono i)
    obtain âŸ¨o, j, hjâŸ© := exists_ordinal hG (Subobject.mk i)
    have ho : Nonempty o.toType := âŸ¨jâŸ©
    rw [o.toType_nonempty_iff_ne_zero] at ho
    let _ : OrderBot o.toType := by
      apply Ordinal.toTypeOrderBotOfPos
      by_contra!
      exact ho (by simpa using this)
    exact MorphismProperty.hasLiftingProperty_of_transfiniteCompositionsOfShape _ i
      (generatingMonomorphismsPushouts_transfiniteCompositionOfShape hG i hj) p
      (by simpa only [MorphismProperty.pushouts_rlp] using hp)
  Â· exact MorphismProperty.antitone_rlp (generatingMonomorphisms_le_monomorphisms _)

end IsGrothendieckAbelian

end Abelian

end CategoryTheory
