/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Scott Morrison, JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Abelian.InjectiveResolution
import Mathlib.Algebra.Homology.Additive
import Mathlib.CategoryTheory.Limits.Constructions.EpiMono
import Mathlib.CategoryTheory.Abelian.Homology
import Mathlib.CategoryTheory.Abelian.Exact

#align_import category_theory.abelian.right_derived from "leanprover-community/mathlib"@"024a4231815538ac739f52d08dd20a55da0d6b23"

/-!
# Right-derived functors

We define the right-derived functors `F.rightDerived n : C â¥¤ D` for any additive functor `F`
out of a category with injective resolutions.

The definition is
```
injectiveResolutions C â‹™ F.mapHomotopyCategory _ â‹™ HomotopyCategory.homologyFunctor D _ n
```
that is, we pick an injective resolution (thought of as an object of the homotopy category),
we apply `F` objectwise, and compute `n`-th homology.

We show that these right-derived functors can be calculated
on objects using any choice of injective resolution,
and on morphisms by any choice of lift to a cochain map between chosen injective resolutions.

Similarly we define natural transformations between right-derived functors coming from
natural transformations between the original additive functors,
and show how to compute the components.

## Main results
* `CategoryTheory.Functor.rightDerivedObj_injective_zero`: the `0`-th derived functor of `F` on
  an injective object `X` is isomorphic to `F.obj X`.
* `CategoryTheory.Functor.rightDerivedObj_injective_succ`: injective objects have no higher
  right derived functor.
* `CategoryTheory.NatTrans.rightDerived`: the natural isomorphism between right derived functors
  induced by natural transformation.

Now, we assume `PreservesFiniteLimits F`, then
* `CategoryTheory.Abelian.Functor.preserves_exact_of_preservesFiniteLimits_of_mono`: if `f` is
  mono and `Exact f g`, then `Exact (F.map f) (F.map g)`.
* `CategoryTheory.Abelian.Functor.rightDerivedZeroIsoSelf`: if there are enough injectives,
  then there is a natural isomorphism `(F.rightDerived 0) â‰… F`.
-/


noncomputable section

open CategoryTheory

open CategoryTheory.Limits

namespace CategoryTheory

universe v u

variable {C : Type u} [Category.{v} C] {D : Type*} [Category D]

variable [Abelian C] [HasInjectiveResolutions C] [Abelian D]

def Functor.rightDerivedToHomotopyCategory (F : C â¥¤ D) [F.Additive] :
    C â¥¤ HomotopyCategory D (ComplexShape.up â„•) :=
  injectiveResolutions C â‹™ F.mapHomotopyCategory _

def InjectiveResolution.isoRightDerivedToHomotopyCategoryObj {X : C}
    (I : InjectiveResolution X) (F : C â¥¤ D) [F.Additive] :
    F.rightDerivedToHomotopyCategory.obj X â‰…
      (F.mapHomologicalComplex _ â‹™ HomotopyCategory.quotient _ _).obj I.cocomplex :=
  (F.mapHomotopyCategory _).mapIso I.iso â‰ªâ‰«
    (F.mapHomotopyCategoryFactors _).app I.cocomplex

@[reassoc]
def InjectiveResolution.isoRightDerivedToHomotopyCategoryObj_hom_naturality
    {X Y : C} (f : X âŸ¶ Y) (I : InjectiveResolution X) (J : InjectiveResolution Y)
    (Ï† : I.cocomplex âŸ¶ J.cocomplex) (comm : I.Î¹.f 0 â‰« Ï†.f 0 = f â‰« J.Î¹.f 0)
    (F : C â¥¤ D) [F.Additive] :
    F.rightDerivedToHomotopyCategory.map f â‰« (J.isoRightDerivedToHomotopyCategoryObj F).hom =
      (I.isoRightDerivedToHomotopyCategoryObj F).hom â‰«
        (F.mapHomologicalComplex _ â‹™ HomotopyCategory.quotient _ _).map Ï† := by
  dsimp [Functor.rightDerivedToHomotopyCategory, isoRightDerivedToHomotopyCategoryObj]
  rw [â† Functor.map_comp_assoc, iso_hom_naturality f I J Ï† comm, Functor.map_comp,
    Category.assoc, Category.assoc]
  erw [(F.mapHomotopyCategoryFactors (ComplexShape.up â„•)).hom.naturality]
  rfl

@[reassoc]
def InjectiveResolution.isoRightDerivedToHomotopyCategoryObj_inv_naturality
    {X Y : C} (f : X âŸ¶ Y) (I : InjectiveResolution X) (J : InjectiveResolution Y)
    (Ï† : I.cocomplex âŸ¶ J.cocomplex) (comm : I.Î¹.f 0 â‰« Ï†.f 0 = f â‰« J.Î¹.f 0)
    (F : C â¥¤ D) [F.Additive] :
    (I.isoRightDerivedToHomotopyCategoryObj F).inv â‰« F.rightDerivedToHomotopyCategory.map f =
      (F.mapHomologicalComplex _ â‹™ HomotopyCategory.quotient _ _).map Ï† â‰«
        (J.isoRightDerivedToHomotopyCategoryObj F).inv := by
    rw [â† cancel_epi (I.isoRightDerivedToHomotopyCategoryObj F).hom, Iso.hom_inv_id_assoc]
    dsimp
    rw [â† isoRightDerivedToHomotopyCategoryObj_hom_naturality_assoc f I J Ï† comm F,
      Iso.hom_inv_id, Category.comp_id]

/-- The right derived functors of an additive functor. -/
def Functor.rightDerived (F : C â¥¤ D) [F.Additive] (n : â„•) : C â¥¤ D :=
  F.rightDerivedToHomotopyCategory â‹™ HomotopyCategory.homologyFunctor D _ n
#align category_theory.functor.right_derived CategoryTheory.Functor.rightDerived

/-- We can compute a right derived functor using a chosen injective resolution. -/
def InjectiveResolution.isoRightDerivedObj {X : C} (I : InjectiveResolution X)
    (F : C â¥¤ D) [F.Additive] (n : â„•) :
    (F.rightDerived n).obj X â‰…
      (HomologicalComplex.homologyFunctor D _ n).obj
        ((F.mapHomologicalComplex _).obj I.cocomplex) :=
  (HomotopyCategory.homologyFunctor D _ n).mapIso (I.isoRightDerivedToHomotopyCategoryObj F) â‰ªâ‰« (HomotopyCategory.homologyFunctorFactors D (ComplexShape.up â„•) n).app _

@[reassoc]
lemma InjectiveResolution.isoRightDerivedObj_hom_naturality
    {X Y : C} (f : X âŸ¶ Y) (I : InjectiveResolution X) (J : InjectiveResolution Y)
    (Ï† : I.cocomplex âŸ¶ J.cocomplex) (comm : I.Î¹.f 0 â‰« Ï†.f 0 = f â‰« J.Î¹.f 0)
    (F : C â¥¤ D) [F.Additive] (n : â„•) :
    (F.rightDerived n).map f â‰« (J.isoRightDerivedObj F n).hom =
      (I.isoRightDerivedObj F n).hom â‰«
        (F.mapHomologicalComplex _ â‹™ HomologicalComplex.homologyFunctor _ _ n).map Ï† := by
  dsimp [isoRightDerivedObj, Functor.rightDerived]
  rw [Category.assoc, â† Functor.map_comp_assoc,
    InjectiveResolution.isoRightDerivedToHomotopyCategoryObj_hom_naturality f I J Ï† comm F,
    Functor.map_comp, Category.assoc]
  erw [(HomotopyCategory.homologyFunctorFactors D (ComplexShape.up â„•) n).hom.naturality]
  rfl

@[reassoc]
lemma InjectiveResolution.isoRightDerivedObj_inv_naturality
    {X Y : C} (f : X âŸ¶ Y) (I : InjectiveResolution X) (J : InjectiveResolution Y)
    (Ï† : I.cocomplex âŸ¶ J.cocomplex) (comm : I.Î¹.f 0 â‰« Ï†.f 0 = f â‰« J.Î¹.f 0)
    (F : C â¥¤ D) [F.Additive] (n : â„•) :
    (I.isoRightDerivedObj F n).inv â‰« (F.rightDerived n).map f =
        (F.mapHomologicalComplex _ â‹™ HomologicalComplex.homologyFunctor _ _ n).map Ï† â‰«
          (J.isoRightDerivedObj F n).inv := by
  rw [â† cancel_mono (J.isoRightDerivedObj F n).hom, Category.assoc, Category.assoc,
    InjectiveResolution.isoRightDerivedObj_hom_naturality f I J Ï† comm F n,
    Iso.inv_hom_id_assoc, Iso.inv_hom_id, Category.comp_id]

open ZeroObject

lemma Functor.isZero_rightDerived_obj_injective_succ
    (F : C â¥¤ D) [F.Additive] (n : â„•) (X : C) [Injective X] :
    IsZero ((F.rightDerived (n+1)).obj X) := by
  refine' IsZero.of_iso _ ((InjectiveResolution.self X).isoRightDerivedObj F (n+1))
  erw [HomologicalComplex.isZero_homology_iff]
  apply ShortComplex.exact_of_isZero_Xâ‚‚
  dsimp [InjectiveResolution.self]
  exact IsZero.of_iso (isZero_zero _) F.mapZeroObject

/-- The higher derived functors vanish on injective objects. -/
def Functor.rightDerivedObjInjectiveSucc (F : C â¥¤ D) [F.Additive] (n : â„•) (X : C) [Injective X] :
    (F.rightDerived (n + 1)).obj X â‰… 0 :=
  (F.isZero_rightDerived_obj_injective_succ n X).isoZero
#align category_theory.functor.right_derived_obj_injective_succ CategoryTheory.Functor.rightDerivedObjInjectiveSucc

/-- We can compute a right derived functor on a morphism using a descent of that morphism
to a cochain map between chosen injective resolutions.
-/
theorem Functor.rightDerived_map_eq (F : C â¥¤ D) [F.Additive] (n : â„•) {X Y : C} (f : X âŸ¶ Y)
    {P : InjectiveResolution X} {Q : InjectiveResolution Y} (g : P.cocomplex âŸ¶ Q.cocomplex)
    (w : P.Î¹ â‰« g = (CochainComplex.singleâ‚€ C).map f â‰« Q.Î¹) :
    (F.rightDerived n).map f =
      (P.isoRightDerivedObj F n).hom â‰«
        (F.mapHomologicalComplex _ â‹™ HomologicalComplex.homologyFunctor _ _ n).map g â‰«
          (Q.isoRightDerivedObj F n).inv := by
  rw [â† cancel_mono (Q.isoRightDerivedObj F n).hom,
    InjectiveResolution.isoRightDerivedObj_hom_naturality f P Q g _ F n,
    Category.assoc, Category.assoc, Iso.inv_hom_id, Category.comp_id]
  rw [â† HomologicalComplex.comp_f, w, HomologicalComplex.comp_f,
    CochainComplex.singleâ‚€_map_f_0]
#align category_theory.functor.right_derived_map_eq CategoryTheory.Functor.rightDerived_map_eq

def NatTrans.rightDerivedToHomotopyCategory {F G : C â¥¤ D} [F.Additive] [G.Additive] (Î± : F âŸ¶ G) :
    F.rightDerivedToHomotopyCategory âŸ¶ G.rightDerivedToHomotopyCategory :=
  whiskerLeft _ (NatTrans.mapHomotopyCategory Î± (ComplexShape.up â„•))

lemma InjectiveResolution.rightDerivedToHomotopyCategory_app_eq
    {F G : C â¥¤ D} [F.Additive] [G.Additive] (Î± : F âŸ¶ G) {X : C} (P : InjectiveResolution X) :
    (NatTrans.rightDerivedToHomotopyCategory Î±).app X =
      (P.isoRightDerivedToHomotopyCategoryObj F).hom â‰«
        (HomotopyCategory.quotient _ _).map
          ((NatTrans.mapHomologicalComplex Î± _).app P.cocomplex) â‰«
          (P.isoRightDerivedToHomotopyCategoryObj G).inv := by
  rw [â† cancel_mono (P.isoRightDerivedToHomotopyCategoryObj G).hom, Category.assoc,
    Category.assoc, Iso.inv_hom_id, Category.comp_id]
  dsimp [isoRightDerivedToHomotopyCategoryObj, Functor.mapHomotopyCategoryFactors,
    NatTrans.rightDerivedToHomotopyCategory]
  rw [Category.assoc]
  erw [Category.id_comp, Category.comp_id]
  obtain âŸ¨Î², hÎ²âŸ© := (HomotopyCategory.quotient _ _).map_surjective (iso P).hom
  rw [â† hÎ²]
  dsimp
  simp only [â† Functor.map_comp, NatTrans.mapHomologicalComplex_naturality]
  rfl

@[simp]
lemma NatTrans.rightDerivedToHomotopyCategory_id (F : C â¥¤ D) [F.Additive] :
    NatTrans.rightDerivedToHomotopyCategory (ğŸ™ F) = ğŸ™ _ := rfl

@[simp]
lemma NatTrans.rightDerivedToHomotopyCategory_comp {F G H : C â¥¤ D} (Î± : F âŸ¶ G) (Î² : G âŸ¶ H)
    [F.Additive] [G.Additive] [H.Additive] :
    NatTrans.rightDerivedToHomotopyCategory (Î± â‰« Î²) =
      NatTrans.rightDerivedToHomotopyCategory Î± â‰«
        NatTrans.rightDerivedToHomotopyCategory Î² := rfl

/-- The natural transformation between right-derived functors induced by a natural transformation.-/
def NatTrans.rightDerived {F G : C â¥¤ D} [F.Additive] [G.Additive] (Î± : F âŸ¶ G) (n : â„•) :
    F.rightDerived n âŸ¶ G.rightDerived n :=
  whiskerRight (NatTrans.rightDerivedToHomotopyCategory Î±) _
#align category_theory.nat_trans.right_derived CategoryTheory.NatTrans.rightDerived

@[simp]
theorem NatTrans.rightDerived_id (F : C â¥¤ D) [F.Additive] (n : â„•) :
    NatTrans.rightDerived (ğŸ™ F) n = ğŸ™ (F.rightDerived n) := by
  dsimp only [rightDerived]
  simp only [rightDerivedToHomotopyCategory_id, whiskerRight_id']
  rfl
#align category_theory.nat_trans.right_derived_id CategoryTheory.NatTrans.rightDerived_id

@[simp, nolint simpNF]
theorem NatTrans.rightDerived_comp {F G H : C â¥¤ D} [F.Additive] [G.Additive] [H.Additive]
    (Î± : F âŸ¶ G) (Î² : G âŸ¶ H) (n : â„•) :
    NatTrans.rightDerived (Î± â‰« Î²) n = NatTrans.rightDerived Î± n â‰« NatTrans.rightDerived Î² n := by
  simp [NatTrans.rightDerived]
#align category_theory.nat_trans.right_derived_comp CategoryTheory.NatTrans.rightDerived_comp

/-- A component of the natural transformation between right-derived functors can be computed
using a chosen injective resolution. -/
lemma InjectiveResolution.rightDerived_app_eq
    {F G : C â¥¤ D} [F.Additive] [G.Additive] (Î± : F âŸ¶ G) {X : C} (P : InjectiveResolution X)
    (n : â„•) : (NatTrans.rightDerived Î± n).app X =
      (P.isoRightDerivedObj F n).hom â‰«
        (HomologicalComplex.homologyFunctor D (ComplexShape.up â„•) n).map
        ((NatTrans.mapHomologicalComplex Î± _).app P.cocomplex) â‰«
        (P.isoRightDerivedObj G n).inv := by
  dsimp [NatTrans.rightDerived, isoRightDerivedObj]
  rw [InjectiveResolution.rightDerivedToHomotopyCategory_app_eq Î± P,
    Functor.map_comp, Functor.map_comp, Category.assoc]
  erw [â† (HomotopyCategory.homologyFunctorFactors D (ComplexShape.up â„•) n).hom.naturality_assoc
    ((NatTrans.mapHomologicalComplex Î± (ComplexShape.up â„•)).app P.cocomplex)]
  simp only [Functor.comp_map, Iso.hom_inv_id_app_assoc]

def InjectiveResolution.toRightDerivedZero' {X : C}
    (P : InjectiveResolution X) (F : C â¥¤ D) [F.Additive] :
    F.obj X âŸ¶ ((F.mapHomologicalComplex _).obj P.cocomplex).homology 0 := by
  refine' _ â‰« (CochainComplex.isoHomologyÏ€â‚€ _).hom
  exact HomologicalComplex.liftCycles _ (F.map (P.Î¹.f 0)) 1 (by simp) (by
    dsimp
    rw [â† F.map_comp, HomologicalComplex.Hom.comm, CochainComplex.singleâ‚€_obj_X_d, zero_comp,
      F.map_zero])

@[reassoc (attr := simp)]
lemma InjectiveResolution.toRightDerivedZero'_comp_isoHomologyâ‚€_inv_comp_iCycles {X : C}
    (P : InjectiveResolution X) (F : C â¥¤ D) [F.Additive] :
    P.toRightDerivedZero' F â‰« (CochainComplex.isoHomologyÏ€â‚€ _).inv â‰«
      HomologicalComplex.iCycles _ _ = F.map (P.Î¹.f 0) := by
  dsimp only [toRightDerivedZero' ]
  simp

@[reassoc]
def InjectiveResolution.toRightDerivedZero'_naturality {X Y : C} (f : X âŸ¶ Y)
    (P : InjectiveResolution X) (Q : InjectiveResolution Y)
    (Ï† : P.cocomplex âŸ¶ Q.cocomplex) (comm : P.Î¹.f 0 â‰« Ï†.f 0 = f â‰« Q.Î¹.f 0)
    (F : C â¥¤ D) [F.Additive] :
    F.map f â‰« Q.toRightDerivedZero' F =
      P.toRightDerivedZero' F â‰«
        (F.mapHomologicalComplex _ â‹™ HomologicalComplex.homologyFunctor _ _ 0).map Ï† := by
  simp only [â† cancel_mono (CochainComplex.isoHomologyÏ€â‚€ _).inv,
    â† cancel_mono (HomologicalComplex.iCycles _ _), Category.assoc,
    toRightDerivedZero'_comp_isoHomologyâ‚€_inv_comp_iCycles, Functor.comp_map,
    HomologicalComplex.homologyFunctor_map, CochainComplex.isoHomologyÏ€â‚€_inv_naturality,
    HomologicalComplex.cyclesMap_i, Functor.mapHomologicalComplex_map_f,
    toRightDerivedZero'_comp_isoHomologyâ‚€_inv_comp_iCycles_assoc, â† F.map_comp, comm]

instance (F : C â¥¤ D) [F.Additive] (X : C) [Injective X] :
    IsIso ((InjectiveResolution.self X).toRightDerivedZero' F) := by
  dsimp [InjectiveResolution.toRightDerivedZero',
    InjectiveResolution.self]
  refine @IsIso.comp_isIso  _ _ _ _ _ _ _ ?_ inferInstance
  rw [CochainComplex.isIso_liftCycles_iff]
  constructor
  Â· infer_instance
  Â· rw [ShortComplex.exact_iff_epi]
    Â· dsimp
      simp only [Functor.map_id]
      infer_instance
    Â· simp

def Functor.toRightDerivedZero (F : C â¥¤ D) [F.Additive] :
    F âŸ¶ F.rightDerived 0 where
  app X := (injectiveResolution' X).toRightDerivedZero' F â‰«
    (HomotopyCategory.homologyFunctorFactors D (ComplexShape.up â„•) 0).inv.app _
  naturality {X Y} f := by
    rw [InjectiveResolution.toRightDerivedZero'_naturality_assoc f
      (injectiveResolution' X) (injectiveResolution' Y)
      (injectiveResolution.desc f) (by simp) F, Category.assoc,
      NatTrans.naturality]
    rfl

lemma InjectiveResolution.toRightDerivedZero_eq
    {X : C} (I : InjectiveResolution X) (F : C â¥¤ D) [F.Additive] :
    F.toRightDerivedZero.app X =
      I.toRightDerivedZero' F â‰« (I.isoRightDerivedObj F 0).inv := by
  dsimp [Functor.toRightDerivedZero, isoRightDerivedObj]
  have hâ‚ : (I.isoRightDerivedToHomotopyCategoryObj F).hom =
    (F.mapHomologicalComplex _ â‹™ HomotopyCategory.quotient _ _).map (desc (ğŸ™ X) _ _) :=
    Category.comp_id _
  have hâ‚‚ := InjectiveResolution.toRightDerivedZero'_naturality
    (ğŸ™ X) (injectiveResolution' X) I (desc (ğŸ™ X) _ _) (by
      rw [â† HomologicalComplex.comp_f, desc_commutes, Functor.map_id,
        Category.id_comp, Category.id_comp]) F
  rw [F.map_id, Category.id_comp] at hâ‚‚
  rw [â† cancel_mono ((HomotopyCategory.homologyFunctor _ _ 0).map (I.isoRightDerivedToHomotopyCategoryObj F).hom),
    Category.assoc, Category.assoc, Category.assoc, â† Functor.map_comp, Iso.inv_hom_id,
    Functor.map_id, Category.comp_id, hâ‚‚, hâ‚, Category.assoc]
  erw [â† NatTrans.naturality]
  rfl

-- this replaced the previous `Functor.rightDerivedObjInjectiveZero` which
-- is generalized as `Functor.rightDerivedZeroIsoSelf` for all `X` when
-- `F` preserves finite limits
instance (F : C â¥¤ D) [F.Additive] (X : C) [Injective X] :
    IsIso (F.toRightDerivedZero.app X) := by
  rw [(InjectiveResolution.self X).toRightDerivedZero_eq F]
  infer_instance

section

variable (F : C â¥¤ D) [F.Additive]

instance [PreservesFiniteLimits F] {X : C} (P : InjectiveResolution X) :
    IsIso (P.toRightDerivedZero' F) := by
  dsimp [InjectiveResolution.toRightDerivedZero']
  refine @IsIso.comp_isIso  _ _ _ _ _ _ _ ?_ inferInstance
  rw [CochainComplex.isIso_liftCycles_iff]
  constructor
  Â· infer_instance
  Â· let S : ShortComplex C := ShortComplex.mk (P.Î¹.f 0) (P.cocomplex.d 0 1) (by simp)
    -- this exactness property should be moved to Abelian/InjectiveResolution.lean
    have hS : S.Exact := by
      have : QuasiIsoAt P.Î¹ 0 := inferInstance
      rw [CochainComplex.quasiIsoAtâ‚€_iff,
        ShortComplex.quasiIso_iff_of_zeros] at this
      rotate_left
      Â· rfl
      Â· rfl
      Â· simp
      exact this.2
    exact hS.map_of_mono_of_preservesKernel F
      (by dsimp; infer_instance) inferInstance

instance [PreservesFiniteLimits F] : IsIso F.toRightDerivedZero := by
  have : âˆ€ X, IsIso (F.toRightDerivedZero.app X) := fun X => by
    dsimp [Functor.toRightDerivedZero]
    infer_instance
  apply NatIso.isIso_of_isIso_app

variable [PreservesFiniteLimits F]

@[simps! inv]
def Functor.rightDerivedZeroIsoSelf : F.rightDerived 0 â‰… F :=
  (asIso F.toRightDerivedZero).symm

@[reassoc (attr := simp)]
lemma Functor.rightDerivedZeroIsoSelf_hom_inv_id :
    F.rightDerivedZeroIsoSelf.hom â‰« F.toRightDerivedZero = ğŸ™ _ :=
  F.rightDerivedZeroIsoSelf.hom_inv_id

@[reassoc (attr := simp)]
lemma Functor.rightDerivedZeroIsoSelf_inv_hom_id :
    F.toRightDerivedZero â‰« F.rightDerivedZeroIsoSelf.hom = ğŸ™ _ :=
  F.rightDerivedZeroIsoSelf.inv_hom_id

@[reassoc (attr := simp)]
lemma Functor.rightDerivedZeroIsoSelf_hom_inv_id_app (X : C) :
    F.rightDerivedZeroIsoSelf.hom.app X â‰« F.toRightDerivedZero.app X = ğŸ™ _ :=
  F.rightDerivedZeroIsoSelf.hom_inv_id_app X

@[reassoc (attr := simp)]
lemma Functor.rightDerivedZeroIsoSelf_inv_hom_id_app (X : C) :
    F.toRightDerivedZero.app X â‰« F.rightDerivedZeroIsoSelf.hom.app X = ğŸ™ _ :=
  F.rightDerivedZeroIsoSelf.inv_hom_id_app X

end

end CategoryTheory
