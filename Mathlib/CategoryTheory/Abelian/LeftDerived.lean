/-
Copyright (c) 2021 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Riccardo Brasca, Adam Topaz, Jujian Zhang, Jo√´l Riou
-/
import Mathlib.Algebra.Homology.Additive
import Mathlib.CategoryTheory.Abelian.Projective.Resolution

/-!
# Left-derived functors

We define the left-derived functors `F.leftDerived n : C ‚•§ D` for any additive functor `F`
out of a category with projective resolutions.

We first define a functor
`F.leftDerivedToHomotopyCategory : C ‚•§ HomotopyCategory D (ComplexShape.down ‚Ñï)` which is
`projectiveResolutions C ‚ãô F.mapHomotopyCategory _`. We show that if `X : C` and
`P : ProjectiveResolution X`, then `F.leftDerivedToHomotopyCategory.obj X` identifies
to the image in the homotopy category of the functor `F` applied objectwise to `P.complex`
(this isomorphism is `P.isoLeftDerivedToHomotopyCategoryObj F`).

Then, the left-derived functors `F.leftDerived n : C ‚•§ D` are obtained by composing
`F.leftDerivedToHomotopyCategory` with the homology functors on the homotopy category.

Similarly we define natural transformations between left-derived functors coming from
natural transformations between the original additive functors,
and show how to compute the components.

## Main results
* `Functor.isZero_leftDerived_obj_projective_succ`: projective objects have no higher
  left derived functor.
* `NatTrans.leftDerived`: the natural isomorphism between left derived functors
  induced by natural transformation.
* `Functor.fromLeftDerivedZero`: the natural transformation `F.leftDerived 0 ‚ü∂ F`,
  which is an isomorphism when `F` is right exact (i.e. preserves finite colimits),
  see also `Functor.leftDerivedZeroIsoSelf`.

## TODO

* refactor `Functor.leftDerived` (and `Functor.rightDerived`) when the necessary
  material enters mathlib: derived categories, injective/projective derivability
  structures, existence of derived functors from derivability structures.
  Eventually, we shall get a right derived functor
  `F.leftDerivedFunctorMinus : DerivedCategory.Minus C ‚•§ DerivedCategory.Minus D`,
  and `F.leftDerived` shall be redefined using `F.leftDerivedFunctorMinus`.

-/

universe v u

namespace CategoryTheory

open Category Limits

variable {C : Type u} [Category.{v} C] {D : Type*} [Category D]
  [Abelian C] [HasProjectiveResolutions C] [Abelian D]

/-- When `F : C ‚•§ D` is an additive functor, this is
the functor `C ‚•§ HomotopyCategory D (ComplexShape.down ‚Ñï)` which
sends `X : C` to `F` applied to a projective resolution of `X`. -/
noncomputable def Functor.leftDerivedToHomotopyCategory (F : C ‚•§ D) [F.Additive] :
    C ‚•§ HomotopyCategory D (ComplexShape.down ‚Ñï) :=
  projectiveResolutions C ‚ãô F.mapHomotopyCategory _

/-- If `P : ProjectiveResolution Z` and `F : C ‚•§ D` is an additive functor, this is
an isomorphism between `F.leftDerivedToHomotopyCategory.obj X` and the complex
obtained by applying `F` to `P.complex`. -/
noncomputable def ProjectiveResolution.isoLeftDerivedToHomotopyCategoryObj {X : C}
    (P : ProjectiveResolution X) (F : C ‚•§ D) [F.Additive] :
    F.leftDerivedToHomotopyCategory.obj X ‚âÖ
      (F.mapHomologicalComplex _ ‚ãô HomotopyCategory.quotient _ _).obj P.complex :=
  (F.mapHomotopyCategory _).mapIso P.iso ‚â™‚â´
    (F.mapHomotopyCategoryFactors _).app P.complex

@[reassoc]
lemma ProjectiveResolution.isoLeftDerivedToHomotopyCategoryObj_inv_naturality
    {X Y : C} (f : X ‚ü∂ Y) (P : ProjectiveResolution X) (Q : ProjectiveResolution Y)
    (œÜ : P.complex ‚ü∂ Q.complex) (comm : œÜ.f 0 ‚â´ Q.œÄ.f 0 = P.œÄ.f 0 ‚â´ f)
    (F : C ‚•§ D) [F.Additive] :
    (P.isoLeftDerivedToHomotopyCategoryObj F).inv ‚â´ F.leftDerivedToHomotopyCategory.map f =
      (F.mapHomologicalComplex _ ‚ãô HomotopyCategory.quotient _ _).map œÜ ‚â´
        (Q.isoLeftDerivedToHomotopyCategoryObj F).inv := by
  dsimp [Functor.leftDerivedToHomotopyCategory, isoLeftDerivedToHomotopyCategoryObj]
  rw [assoc, ‚Üê Functor.map_comp, iso_inv_naturality f P Q œÜ comm, Functor.map_comp]
  erw [(F.mapHomotopyCategoryFactors (ComplexShape.down ‚Ñï)).inv.naturality_assoc]
  rfl

@[reassoc]
lemma ProjectiveResolution.isoLeftDerivedToHomotopyCategoryObj_hom_naturality
    {X Y : C} (f : X ‚ü∂ Y) (P : ProjectiveResolution X) (Q : ProjectiveResolution Y)
    (œÜ : P.complex ‚ü∂ Q.complex) (comm : œÜ.f 0 ‚â´ Q.œÄ.f 0 = P.œÄ.f 0 ‚â´ f)
    (F : C ‚•§ D) [F.Additive] :
    F.leftDerivedToHomotopyCategory.map f ‚â´ (Q.isoLeftDerivedToHomotopyCategoryObj F).hom =
      (P.isoLeftDerivedToHomotopyCategoryObj F).hom ‚â´
        (F.mapHomologicalComplex _ ‚ãô HomotopyCategory.quotient _ _).map œÜ := by
    dsimp
    rw [‚Üê cancel_epi (P.isoLeftDerivedToHomotopyCategoryObj F).inv, Iso.inv_hom_id_assoc,
      isoLeftDerivedToHomotopyCategoryObj_inv_naturality_assoc f P Q œÜ comm F,
      Iso.inv_hom_id, comp_id]

/-- The left derived functors of an additive functor. -/
noncomputable def Functor.leftDerived (F : C ‚•§ D) [F.Additive] (n : ‚Ñï) : C ‚•§ D :=
  F.leftDerivedToHomotopyCategory ‚ãô HomotopyCategory.homologyFunctor D _ n

/-- We can compute a left derived functor using a chosen projective resolution. -/
noncomputable def ProjectiveResolution.isoLeftDerivedObj {X : C} (P : ProjectiveResolution X)
    (F : C ‚•§ D) [F.Additive] (n : ‚Ñï) :
    (F.leftDerived n).obj X ‚âÖ
      (HomologicalComplex.homologyFunctor D _ n).obj
        ((F.mapHomologicalComplex _).obj P.complex) :=
  (HomotopyCategory.homologyFunctor D _ n).mapIso
    (P.isoLeftDerivedToHomotopyCategoryObj F) ‚â™‚â´
    (HomotopyCategory.homologyFunctorFactors D (ComplexShape.down ‚Ñï) n).app _

@[reassoc]
lemma ProjectiveResolution.isoLeftDerivedObj_hom_naturality
    {X Y : C} (f : X ‚ü∂ Y) (P : ProjectiveResolution X) (Q : ProjectiveResolution Y)
    (œÜ : P.complex ‚ü∂ Q.complex) (comm : œÜ.f 0 ‚â´ Q.œÄ.f 0 = P.œÄ.f 0 ‚â´ f)
    (F : C ‚•§ D) [F.Additive] (n : ‚Ñï) :
    (F.leftDerived n).map f ‚â´ (Q.isoLeftDerivedObj F n).hom =
      (P.isoLeftDerivedObj F n).hom ‚â´
        (F.mapHomologicalComplex _ ‚ãô HomologicalComplex.homologyFunctor _ _ n).map œÜ := by
  dsimp [isoLeftDerivedObj, Functor.leftDerived]
  rw [assoc, ‚Üê Functor.map_comp_assoc,
    ProjectiveResolution.isoLeftDerivedToHomotopyCategoryObj_hom_naturality f P Q œÜ comm F,
    Functor.map_comp, assoc]
  erw [(HomotopyCategory.homologyFunctorFactors D (ComplexShape.down ‚Ñï) n).hom.naturality]
  rfl

@[reassoc]
lemma ProjectiveResolution.isoLeftDerivedObj_inv_naturality
    {X Y : C} (f : X ‚ü∂ Y) (P : ProjectiveResolution X) (Q : ProjectiveResolution Y)
    (œÜ : P.complex ‚ü∂ Q.complex) (comm : œÜ.f 0 ‚â´ Q.œÄ.f 0 = P.œÄ.f 0 ‚â´ f)
    (F : C ‚•§ D) [F.Additive] (n : ‚Ñï) :
    (P.isoLeftDerivedObj F n).inv ‚â´ (F.leftDerived n).map f =
        (F.mapHomologicalComplex _ ‚ãô HomologicalComplex.homologyFunctor _ _ n).map œÜ ‚â´
          (Q.isoLeftDerivedObj F n).inv := by
  rw [‚Üê cancel_mono (Q.isoLeftDerivedObj F n).hom, assoc, assoc,
    ProjectiveResolution.isoLeftDerivedObj_hom_naturality f P Q œÜ comm F n,
    Iso.inv_hom_id_assoc, Iso.inv_hom_id, comp_id]

/-- The higher derived functors vanish on projective objects. -/
lemma Functor.isZero_leftDerived_obj_projective_succ
    (F : C ‚•§ D) [F.Additive] (n : ‚Ñï) (X : C) [Projective X] :
    IsZero ((F.leftDerived (n + 1)).obj X) := by
  refine IsZero.of_iso ?_ ((ProjectiveResolution.self X).isoLeftDerivedObj F (n + 1))
  erw [‚Üê HomologicalComplex.exactAt_iff_isZero_homology]
  exact ShortComplex.exact_of_isZero_X‚ÇÇ _ (F.map_isZero (by apply isZero_zero))

/-- We can compute a left derived functor on a morphism using a descent of that morphism
to a chain map between chosen projective resolutions.
-/
theorem Functor.leftDerived_map_eq (F : C ‚•§ D) [F.Additive] (n : ‚Ñï) {X Y : C} (f : X ‚ü∂ Y)
    {P : ProjectiveResolution X} {Q : ProjectiveResolution Y} (g : P.complex ‚ü∂ Q.complex)
    (w : g ‚â´ Q.œÄ = P.œÄ ‚â´ (ChainComplex.single‚ÇÄ C).map f) :
    (F.leftDerived n).map f =
      (P.isoLeftDerivedObj F n).hom ‚â´
        (F.mapHomologicalComplex _ ‚ãô HomologicalComplex.homologyFunctor _ _ n).map g ‚â´
          (Q.isoLeftDerivedObj F n).inv := by
  rw [‚Üê cancel_mono (Q.isoLeftDerivedObj F n).hom,
    ProjectiveResolution.isoLeftDerivedObj_hom_naturality f P Q g _ F n,
    assoc, assoc, Iso.inv_hom_id, comp_id]
  rw [‚Üê HomologicalComplex.comp_f, w, HomologicalComplex.comp_f,
    ChainComplex.single‚ÇÄ_map_f_zero]

/-- The natural transformation
`F.leftDerivedToHomotopyCategory ‚ü∂ G.leftDerivedToHomotopyCategory` induced by
a natural transformation `F ‚ü∂ G` between additive functors. -/
noncomputable def NatTrans.leftDerivedToHomotopyCategory
    {F G : C ‚•§ D} [F.Additive] [G.Additive] (Œ± : F ‚ü∂ G) :
    F.leftDerivedToHomotopyCategory ‚ü∂ G.leftDerivedToHomotopyCategory :=
  whiskerLeft _ (NatTrans.mapHomotopyCategory Œ± (ComplexShape.down ‚Ñï))

lemma ProjectiveResolution.leftDerivedToHomotopyCategory_app_eq
    {F G : C ‚•§ D} [F.Additive] [G.Additive] (Œ± : F ‚ü∂ G) {X : C} (P : ProjectiveResolution X) :
    (NatTrans.leftDerivedToHomotopyCategory Œ±).app X =
      (P.isoLeftDerivedToHomotopyCategoryObj F).hom ‚â´
        (HomotopyCategory.quotient _ _).map
          ((NatTrans.mapHomologicalComplex Œ± _).app P.complex) ‚â´
          (P.isoLeftDerivedToHomotopyCategoryObj G).inv := by
  rw [‚Üê cancel_mono (P.isoLeftDerivedToHomotopyCategoryObj G).hom, assoc, assoc,
      Iso.inv_hom_id, comp_id]
  dsimp [isoLeftDerivedToHomotopyCategoryObj, Functor.mapHomotopyCategoryFactors,
    NatTrans.leftDerivedToHomotopyCategory]
  rw [assoc]
  erw [id_comp, comp_id]
  obtain ‚ü®Œ≤, hŒ≤‚ü© := (HomotopyCategory.quotient _ _).map_surjective (iso P).hom
  rw [‚Üê hŒ≤]
  dsimp
  simp only [‚Üê Functor.map_comp, NatTrans.mapHomologicalComplex_naturality]
  rfl

@[simp]
lemma NatTrans.leftDerivedToHomotopyCategory_id (F : C ‚•§ D) [F.Additive] :
    NatTrans.leftDerivedToHomotopyCategory (ùüô F) = ùüô _ := rfl

@[simp, reassoc]
lemma NatTrans.leftDerivedToHomotopyCategory_comp {F G H : C ‚•§ D} (Œ± : F ‚ü∂ G) (Œ≤ : G ‚ü∂ H)
    [F.Additive] [G.Additive] [H.Additive] :
    NatTrans.leftDerivedToHomotopyCategory (Œ± ‚â´ Œ≤) =
      NatTrans.leftDerivedToHomotopyCategory Œ± ‚â´
        NatTrans.leftDerivedToHomotopyCategory Œ≤ := rfl

/-- The natural transformation between left-derived functors induced by a natural transformation. -/
noncomputable def NatTrans.leftDerived
    {F G : C ‚•§ D} [F.Additive] [G.Additive] (Œ± : F ‚ü∂ G) (n : ‚Ñï) :
    F.leftDerived n ‚ü∂ G.leftDerived n :=
  whiskerRight (NatTrans.leftDerivedToHomotopyCategory Œ±) _

@[simp]
theorem NatTrans.leftDerived_id (F : C ‚•§ D) [F.Additive] (n : ‚Ñï) :
    NatTrans.leftDerived (ùüô F) n = ùüô (F.leftDerived n) := by
  dsimp only [leftDerived]
  simp only [leftDerivedToHomotopyCategory_id, whiskerRight_id']
  rfl

@[simp, reassoc]
theorem NatTrans.leftDerived_comp {F G H : C ‚•§ D} [F.Additive] [G.Additive] [H.Additive]
    (Œ± : F ‚ü∂ G) (Œ≤ : G ‚ü∂ H) (n : ‚Ñï) :
    NatTrans.leftDerived (Œ± ‚â´ Œ≤) n = NatTrans.leftDerived Œ± n ‚â´ NatTrans.leftDerived Œ≤ n := by
  simp [NatTrans.leftDerived]

namespace ProjectiveResolution

/-- A component of the natural transformation between left-derived functors can be computed
using a chosen projective resolution. -/
lemma leftDerived_app_eq
    {F G : C ‚•§ D} [F.Additive] [G.Additive] (Œ± : F ‚ü∂ G) {X : C} (P : ProjectiveResolution X)
    (n : ‚Ñï) : (NatTrans.leftDerived Œ± n).app X =
      (P.isoLeftDerivedObj F n).hom ‚â´
        (HomologicalComplex.homologyFunctor D (ComplexShape.down ‚Ñï) n).map
        ((NatTrans.mapHomologicalComplex Œ± _).app P.complex) ‚â´
        (P.isoLeftDerivedObj G n).inv := by
  dsimp [NatTrans.leftDerived, isoLeftDerivedObj]
  rw [ProjectiveResolution.leftDerivedToHomotopyCategory_app_eq Œ± P,
    Functor.map_comp, Functor.map_comp, assoc]
  erw [‚Üê (HomotopyCategory.homologyFunctorFactors D (ComplexShape.down ‚Ñï) n).hom.naturality_assoc
    ((NatTrans.mapHomologicalComplex Œ± (ComplexShape.down ‚Ñï)).app P.complex)]
  simp only [Functor.comp_map, Iso.hom_inv_id_app_assoc]

/-- If `P : ProjectiveResolution X` and `F` is an additive functor, this is
the canonical morphism from the opcycles in degree `0` of
`(F.mapHomologicalComplex _).obj P.complex` to `F.obj X`. -/
noncomputable def fromLeftDerivedZero' {X : C}
    (P : ProjectiveResolution X) (F : C ‚•§ D) [F.Additive] :
    ((F.mapHomologicalComplex _).obj P.complex).opcycles 0 ‚ü∂ F.obj X :=
  HomologicalComplex.descOpcycles _ (F.map (P.œÄ.f 0)) 1 (by simp) (by
    dsimp
    rw [‚Üê F.map_comp, complex_d_comp_œÄ_f_zero, F.map_zero])

@[reassoc (attr := simp)]
lemma pOpcycles_comp_fromLeftDerivedZero' {C} [Category C] [Abelian C] {X : C}
    (P : ProjectiveResolution X) (F : C ‚•§ D) [F.Additive] :
    HomologicalComplex.pOpcycles _ _ ‚â´ P.fromLeftDerivedZero' F = F.map (P.œÄ.f 0) := by
  simp [fromLeftDerivedZero']

@[reassoc]
lemma fromLeftDerivedZero'_naturality {C} [Category C] [Abelian C] {X Y : C} (f : X ‚ü∂ Y)
    (P : ProjectiveResolution X) (Q : ProjectiveResolution Y)
    (œÜ : P.complex ‚ü∂ Q.complex) (comm : œÜ.f 0 ‚â´ Q.œÄ.f 0 = P.œÄ.f 0 ‚â´ f)
    (F : C ‚•§ D) [F.Additive] :
    HomologicalComplex.opcyclesMap ((F.mapHomologicalComplex _).map œÜ) 0 ‚â´
        Q.fromLeftDerivedZero' F = P.fromLeftDerivedZero' F ‚â´ F.map f := by
  simp only [‚Üê cancel_epi (HomologicalComplex.pOpcycles _ _), ‚Üê F.map_comp, comm,
    HomologicalComplex.p_opcyclesMap_assoc, Functor.mapHomologicalComplex_map_f,
    pOpcycles_comp_fromLeftDerivedZero', pOpcycles_comp_fromLeftDerivedZero'_assoc]

instance (F : C ‚•§ D) [F.Additive] (X : C) [Projective X] :
    IsIso ((ProjectiveResolution.self X).fromLeftDerivedZero' F) := by
  dsimp [ProjectiveResolution.fromLeftDerivedZero']
  rw [ChainComplex.isIso_descOpcycles_iff]
  refine ‚ü®ShortComplex.Splitting.exact ?_, inferInstance‚ü©
  exact
    { r := 0
      s := ùüô _
      f_r := (F.map_isZero (isZero_zero _)).eq_of_src _ _ }

end ProjectiveResolution

/-- The natural transformation `F.leftDerived 0 ‚ü∂ F`. -/
noncomputable def Functor.fromLeftDerivedZero (F : C ‚•§ D) [F.Additive] :
    F.leftDerived 0 ‚ü∂ F where
  app X := (HomotopyCategory.homologyFunctorFactors D (ComplexShape.down ‚Ñï) 0).hom.app _ ‚â´
      (ChainComplex.isoHomologyŒπ‚ÇÄ _).hom ‚â´ (projectiveResolution X).fromLeftDerivedZero' F
  naturality {X Y} f := by
    dsimp [leftDerived]
    rw [assoc, assoc, ‚Üê ProjectiveResolution.fromLeftDerivedZero'_naturality f
      (projectiveResolution X) (projectiveResolution Y)
      (ProjectiveResolution.lift f _ _) (by simp),
      ‚Üê HomologicalComplex.homologyŒπ_naturality_assoc]
    erw [‚Üê NatTrans.naturality_assoc]
    rfl

lemma ProjectiveResolution.fromLeftDerivedZero_eq
    {X : C} (P : ProjectiveResolution X) (F : C ‚•§ D) [F.Additive] :
    F.fromLeftDerivedZero.app X = (P.isoLeftDerivedObj F 0).hom ‚â´
      (ChainComplex.isoHomologyŒπ‚ÇÄ _).hom ‚â´
        P.fromLeftDerivedZero' F := by
  dsimp [Functor.fromLeftDerivedZero, isoLeftDerivedObj]
  have h‚ÇÅ := ProjectiveResolution.fromLeftDerivedZero'_naturality
    (ùüô X) P (projectiveResolution X) (lift (ùüô X) _ _) (by simp) F
  have h‚ÇÇ : (P.isoLeftDerivedToHomotopyCategoryObj F).inv =
    (F.mapHomologicalComplex _ ‚ãô HomotopyCategory.quotient _ _).map (lift (ùüô X) _ _) :=
      id_comp _
  simp only [Functor.map_id, comp_id] at h‚ÇÅ
  rw [assoc, ‚Üê cancel_epi ((HomotopyCategory.homologyFunctor _ _ 0).map
      (P.isoLeftDerivedToHomotopyCategoryObj F).inv), ‚Üê Functor.map_comp_assoc,
      Iso.inv_hom_id, Functor.map_id, id_comp, ‚Üê h‚ÇÅ, h‚ÇÇ,
      ‚Üê HomologicalComplex.homologyŒπ_naturality_assoc]
  erw [‚Üê NatTrans.naturality_assoc]
  rfl

instance (F : C ‚•§ D) [F.Additive] (X : C) [Projective X] :
    IsIso (F.fromLeftDerivedZero.app X) := by
  rw [(ProjectiveResolution.self X).fromLeftDerivedZero_eq F]
  infer_instance

section

variable (F : C ‚•§ D) [F.Additive] [PreservesFiniteColimits F]

instance {X : C} (P : ProjectiveResolution X) :
    IsIso (P.fromLeftDerivedZero' F) := by
  dsimp [ProjectiveResolution.fromLeftDerivedZero']
  rw [ChainComplex.isIso_descOpcycles_iff, ShortComplex.exact_and_epi_g_iff_g_is_cokernel]
  exact ‚ü®CokernelCofork.mapIsColimit _ (P.isColimitCokernelCofork) F‚ü©

instance (X : C) : IsIso (F.fromLeftDerivedZero.app X) := by
  dsimp [Functor.fromLeftDerivedZero]
  infer_instance

instance : IsIso F.fromLeftDerivedZero :=
  NatIso.isIso_of_isIso_app _

namespace Functor

/-- The canonical isomorphism `F.leftDerived 0 ‚âÖ F` when `F` is right exact
(i.e. preserves finite colimits). -/
@[simps! hom]
noncomputable def leftDerivedZeroIsoSelf : F.leftDerived 0 ‚âÖ F :=
  (asIso F.fromLeftDerivedZero)

@[reassoc (attr := simp)]
lemma leftDerivedZeroIsoSelf_hom_inv_id :
    F.fromLeftDerivedZero ‚â´ F.leftDerivedZeroIsoSelf.inv = ùüô _ :=
  F.leftDerivedZeroIsoSelf.hom_inv_id

@[reassoc (attr := simp)]
lemma leftDerivedZeroIsoSelf_inv_hom_id :
    F.leftDerivedZeroIsoSelf.inv ‚â´ F.fromLeftDerivedZero =  ùüô _ :=
  F.leftDerivedZeroIsoSelf.inv_hom_id

@[reassoc (attr := simp)]
lemma leftDerivedZeroIsoSelf_hom_inv_id_app (X : C) :
    F.fromLeftDerivedZero.app X ‚â´ F.leftDerivedZeroIsoSelf.inv.app X = ùüô _ :=
  F.leftDerivedZeroIsoSelf.hom_inv_id_app X

@[reassoc (attr := simp)]
lemma leftDerivedZeroIsoSelf_inv_hom_id_app (X : C) :
    F.leftDerivedZeroIsoSelf.inv.app X ‚â´ F.fromLeftDerivedZero.app X = ùüô _ :=
  F.leftDerivedZeroIsoSelf.inv_hom_id_app X

end Functor

end

end CategoryTheory
