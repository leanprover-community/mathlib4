/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Homology.ShortComplex.Exact

/-!
# Refinements

In order to prove injectivity/surjectivity/exactness properties for diagrams
in the category of abelian groups, we often need to do diagram chases.
Some of these can be carried out in more general abelian categories:
for example, a morphism `X âŸ¶ Y` in an abelian category `C` is a
monomorphism if and only if for all `A : C`, the induced map
`(A âŸ¶ X) â†’ (A âŸ¶ Y)` of abelian groups is a monomorphism, i.e. injective.
Alternatively, the yoneda presheaf functor which sends `X` to the
presheaf of maps `A âŸ¶ X` for all `A : C` preserves and reflects
monomorphisms.

However, if `p : X âŸ¶ Y` is an epimorphism in `C` and `A : C`,
`(A âŸ¶ X) â†’ (A âŸ¶ Y)` may fail to be surjective (unless `p` is a split
epimorphism).

In this file, the basic result is `epi_iff_surjective_up_to_refinements`
which states that `f : X âŸ¶ Y` is a morphism in an abelian category,
then it is an epimorphism if and only if for all `y : A âŸ¶ Y`,
there exists an epimorphism `Ï€ : A' âŸ¶ A` and `x : A' âŸ¶ X` such
that `Ï€ â‰« y = x â‰« f`. In order words, if we allow a precomposition
with an epimorphism, we may lift a morphism to `Y` to a morphism to `X`.
Following unpublished notes by George Bergman, we shall say that the
precomposition by an epimorphism `Ï€ â‰« y` is a refinement of `y`. Then,
we get that an epimorphism is a morphism that is "surjective up to refinements".
(This result is similar to the fact that a morphism of sheaves on
a topological space or a site is epi iff sections can be lifted
locally. Then, arguing "up to refinements" is very similar to
arguing locally for a Grothendieck topology (TODO: indeed,
show that it corresponds to the "refinements" topology on an
abelian category `C` that is defined by saying that
a sieve is covering if it contains an epimorphism).

Similarly, it is possible to show that a short complex in an abelian
category is exact if and only if it is exact up to refinements
(see `ShortComplex.exact_iff_exact_up_to_refinements`).

As it is outlined in the documentation of the file
`CategoryTheory.Abelian.Pseudoelements`, the Freyd-Mitchell
embedding theorem implies the existence of a faithful and exact functor `Î¹`
from an abelian category `C` to the category of abelian groups. If we
define a pseudo-element of `X : C` to be an element in `Î¹.obj X`, one
may do diagram chases in any abelian category using these pseudo-elements.
However, using this approach would require proving this embedding theorem!
Currently, mathlib contains a weaker notion of pseudo-elements
`CategoryTheory.Abelian.Pseudoelements`. Some theorems can be obtained
using this notion, but there is the issue that for this notion
of pseudo-elements a morphism `X âŸ¶ Y` in `C` is not determined by
its action on pseudo-elements (see also `Counterexamples/Pseudoelement`).
On the contrary, the approach consisting of working up to refinements
does not require the introduction of other types: we only need to work
with morphisms `A âŸ¶ X` in `C` which we may consider as being
"sort of elements of `X`". One may carry diagram-chasing by tracking
these morphisms and sometimes introducing an auxiliary epimorphism `A' âŸ¶ A`.

## References
* George Bergman, A note on abelian categories â€“ translating element-chasing proofs,
and exact embedding in abelian groups (1974)
http://math.berkeley.edu/~gbergman/papers/unpub/elem-chase.pdf

-/

namespace CategoryTheory

open Category Limits

variable {C : Type _} [Category C] [Abelian C] {X Y : C} (S : ShortComplex C)
  {Sâ‚ Sâ‚‚ : ShortComplex C}

lemma epi_iff_surjective_up_to_refinements (f : X âŸ¶ Y) :
    Epi f â†” âˆ€ â¦ƒA : Câ¦„ (y : A âŸ¶ Y),
      âˆƒ (A' : C) (Ï€ : A' âŸ¶ A) (_ : Epi Ï€) (x : A' âŸ¶ X), Ï€ â‰« y = x â‰« f := by
  constructor
  Â· intro _ A a
    exact âŸ¨pullback a f, pullback.fst, inferInstance, pullback.snd, pullback.conditionâŸ©
  Â· intro hf
    obtain âŸ¨A, Ï€, hÏ€, a', facâŸ© := hf (ğŸ™ Y)
    rw [comp_id] at fac
    exact epi_of_epi_fac fac.symm

lemma surjective_up_to_refinements_of_epi (f : X âŸ¶ Y) [Epi f] {A : C} (y : A âŸ¶ Y) :
    âˆƒ (A' : C) (Ï€ : A' âŸ¶ A) (_ : Epi Ï€) (x : A' âŸ¶ X), Ï€ â‰« y = x â‰« f :=
  (epi_iff_surjective_up_to_refinements f).1 inferInstance y

lemma ShortComplex.exact_iff_exact_up_to_refinements :
    S.Exact â†” âˆ€ â¦ƒA : Câ¦„ (xâ‚‚ : A âŸ¶ S.Xâ‚‚) (_ : xâ‚‚ â‰« S.g = 0),
      âˆƒ (A' : C) (Ï€ : A' âŸ¶ A) (_ : Epi Ï€) (xâ‚ : A' âŸ¶ S.Xâ‚), Ï€ â‰« xâ‚‚ = xâ‚ â‰« S.f := by
  rw [S.exact_iff_epi_toCycles, epi_iff_surjective_up_to_refinements]
  constructor
  Â· intro hS A a ha
    obtain âŸ¨A', Ï€, hÏ€, xâ‚, facâŸ© := hS (S.liftCycles a ha)
    exact âŸ¨A', Ï€, hÏ€, xâ‚, by simpa only [assoc, liftCycles_i, toCycles_i] using fac =â‰« S.iCyclesâŸ©
  Â· intro hS A a
    obtain âŸ¨A', Ï€, hÏ€, xâ‚, facâŸ© := hS (a â‰« S.iCycles) (by simp)
    exact âŸ¨A', Ï€, hÏ€, xâ‚, by simp only [â† cancel_mono S.iCycles, assoc, toCycles_i, fac]âŸ©

variable {S}

lemma ShortComplex.Exact.exact_up_to_refinements
    (hS : S.Exact) {A : C} (xâ‚‚ : A âŸ¶ S.Xâ‚‚) (hxâ‚‚ : xâ‚‚ â‰« S.g = 0) :
    âˆƒ (A' : C) (Ï€ : A' âŸ¶ A) (_ : Epi Ï€) (xâ‚ : A' âŸ¶ S.Xâ‚), Ï€ â‰« xâ‚‚ = xâ‚ â‰« S.f := by
  rw [ShortComplex.exact_iff_exact_up_to_refinements] at hS
  exact hS xâ‚‚ hxâ‚‚

end CategoryTheory
