/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel, Scott Morrison, Jakob von Raumer, Jo√´l Riou
-/
import Mathlib.CategoryTheory.Preadditive.ProjectiveResolution
import Mathlib.Algebra.Homology.HomotopyCategory
import Mathlib.Tactic.SuppressCompilation

/-!
# Abelian categories with enough projectives have projective resolutions

## Main results
When the underlying category is abelian:
* `CategoryTheory.ProjectiveResolution.lift`: Given `P : ProjectiveResolution X` and
  `Q : ProjectiveResolution Y`, any morphism `X ‚ü∂ Y` admits a lifting to a chain map
  `P.complex ‚ü∂ Q.complex`. It is a lifting in the sense that `P.Œπ` intertwines the lift and
  the original morphism, see `CategoryTheory.ProjectiveResolution.lift_commutes`.
* `CategoryTheory.ProjectiveResolution.liftHomotopy`: Any two such descents are homotopic.
* `CategoryTheory.ProjectiveResolution.homotopyEquiv`: Any two projective resolutions of the same
  object are homotopy equivalent.
* `CategoryTheory.projectiveResolutions`: If every object admits a projective resolution, we can
  construct a functor `projectiveResolutions C : C ‚•§ HomotopyCategory C (ComplexShape.down ‚Ñï)`.

* `CategoryTheory.exact_d_f`: `Projective.d f` and `f` are exact.
* `CategoryTheory.ProjectiveResolution.of`: Hence, starting from an epimorphism `P ‚ü∂ X`, where `P`
  is projective, we can apply `Projective.d` repeatedly to obtain a projective resolution of `X`.
-/

suppress_compilation

noncomputable section

universe v u

namespace CategoryTheory

variable {C : Type u} [Category.{v} C]

open Category Limits Projective

set_option linter.uppercaseLean3 false -- `ProjectiveResolution`

namespace ProjectiveResolution

section

variable [HasZeroObject C] [HasZeroMorphisms C]

/-- Auxiliary construction for `lift`. -/
def liftFZero {Y Z : C} (f : Y ‚ü∂ Z) (P : ProjectiveResolution Y) (Q : ProjectiveResolution Z) :
    P.complex.X 0 ‚ü∂ Q.complex.X 0 :=
  Projective.factorThru (P.œÄ.f 0 ‚â´ f) (Q.œÄ.f 0)
#align category_theory.ProjectiveResolution.lift_f_zero CategoryTheory.ProjectiveResolution.liftFZero

end

section Abelian

variable [Abelian C]

lemma exact‚ÇÄ {Z : C} (P : ProjectiveResolution Z) :
    (ShortComplex.mk _ _ P.complex_d_comp_œÄ_f_zero).Exact :=
  ShortComplex.exact_of_g_is_cokernel _ P.isColimitCokernelCofork

/-- Auxiliary construction for `lift`. -/
def liftFOne {Y Z : C} (f : Y ‚ü∂ Z) (P : ProjectiveResolution Y) (Q : ProjectiveResolution Z) :
    P.complex.X 1 ‚ü∂ Q.complex.X 1 :=
  Q.exact‚ÇÄ.liftFromProjective (P.complex.d 1 0 ‚â´ liftFZero f P Q) (by simp [liftFZero])
#align category_theory.ProjectiveResolution.lift_f_one CategoryTheory.ProjectiveResolution.liftFOne

@[simp]
theorem liftFOne_zero_comm {Y Z : C} (f : Y ‚ü∂ Z) (P : ProjectiveResolution Y)
    (Q : ProjectiveResolution Z) :
    liftFOne f P Q ‚â´ Q.complex.d 1 0 = P.complex.d 1 0 ‚â´ liftFZero f P Q := by
  apply Q.exact‚ÇÄ.liftFromProjective_comp
#align category_theory.ProjectiveResolution.lift_f_one_zero_comm CategoryTheory.ProjectiveResolution.liftFOne_zero_comm

/-- Auxiliary construction for `lift`. -/
def liftFSucc {Y Z : C} (P : ProjectiveResolution Y) (Q : ProjectiveResolution Z) (n : ‚Ñï)
    (g : P.complex.X n ‚ü∂ Q.complex.X n) (g' : P.complex.X (n + 1) ‚ü∂ Q.complex.X (n + 1))
    (w : g' ‚â´ Q.complex.d (n + 1) n = P.complex.d (n + 1) n ‚â´ g) :
    Œ£'g'' : P.complex.X (n + 2) ‚ü∂ Q.complex.X (n + 2),
      g'' ‚â´ Q.complex.d (n + 2) (n + 1) = P.complex.d (n + 2) (n + 1) ‚â´ g' :=
  ‚ü®(Q.exact_succ n).liftFromProjective
    (P.complex.d (n + 2) (n + 1) ‚â´ g') (by simp [w]),
      (Q.exact_succ n).liftFromProjective_comp _ _‚ü©
#align category_theory.ProjectiveResolution.lift_f_succ CategoryTheory.ProjectiveResolution.liftFSucc

/-- A morphism in `C` lift to a chain map between projective resolutions. -/
def lift {Y Z : C} (f : Y ‚ü∂ Z) (P : ProjectiveResolution Y) (Q : ProjectiveResolution Z) :
    P.complex ‚ü∂ Q.complex :=
  ChainComplex.mkHom _ _ (liftFZero f _ _) (liftFOne f _ _) (liftFOne_zero_comm f P Q)
    fun n ‚ü®g, g', w‚ü© => ‚ü®(liftFSucc P Q n g g' w).1, (liftFSucc P Q n g g' w).2‚ü©
#align category_theory.ProjectiveResolution.lift CategoryTheory.ProjectiveResolution.lift

/-- The resolution maps intertwine the lift of a morphism and that morphism. -/
@[reassoc (attr := simp)]
theorem lift_commutes {Y Z : C} (f : Y ‚ü∂ Z) (P : ProjectiveResolution Y)
    (Q : ProjectiveResolution Z) : lift f P Q ‚â´ Q.œÄ = P.œÄ ‚â´ (ChainComplex.single‚ÇÄ C).map f := by
  ext
  simp [lift, liftFZero, liftFOne]
#align category_theory.ProjectiveResolution.lift_commutes CategoryTheory.ProjectiveResolution.lift_commutes

@[reassoc (attr := simp)]
lemma lift_commutes_zero {Y Z : C} (f : Y ‚ü∂ Z)
    (P : ProjectiveResolution Y) (Q : ProjectiveResolution Z) :
    (lift f P Q).f 0 ‚â´ Q.œÄ.f 0 = P.œÄ.f 0 ‚â´ f :=
  (HomologicalComplex.congr_hom (lift_commutes f P Q) 0).trans (by simp)

/-- An auxiliary definition for `liftHomotopyZero`. -/
def liftHomotopyZeroZero {Y Z : C} {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
    (f : P.complex ‚ü∂ Q.complex) (comm : f ‚â´ Q.œÄ = 0) : P.complex.X 0 ‚ü∂ Q.complex.X 1 :=
  Q.exact‚ÇÄ.liftFromProjective (f.f 0) (congr_fun (congr_arg HomologicalComplex.Hom.f comm) 0)
#align category_theory.ProjectiveResolution.lift_homotopy_zero_zero CategoryTheory.ProjectiveResolution.liftHomotopyZeroZero

@[reassoc (attr := simp)]
lemma liftHomotopyZeroZero_comp {Y Z : C} {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
    (f : P.complex ‚ü∂ Q.complex) (comm : f ‚â´ Q.œÄ = 0) :
    liftHomotopyZeroZero f comm ‚â´ Q.complex.d 1 0 = f.f 0 :=
  Q.exact‚ÇÄ.liftFromProjective_comp  _ _

/-- An auxiliary definition for `liftHomotopyZero`. -/
def liftHomotopyZeroOne {Y Z : C} {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
    (f : P.complex ‚ü∂ Q.complex) (comm : f ‚â´ Q.œÄ = 0) :
    P.complex.X 1 ‚ü∂ Q.complex.X 2 :=
  (Q.exact_succ 0).liftFromProjective (f.f 1 - P.complex.d 1 0 ‚â´ liftHomotopyZeroZero f comm)
    (by rw [Preadditive.sub_comp, assoc, HomologicalComplex.Hom.comm,
              liftHomotopyZeroZero_comp, sub_self])
#align category_theory.ProjectiveResolution.lift_homotopy_zero_one CategoryTheory.ProjectiveResolution.liftHomotopyZeroOne

@[reassoc (attr := simp)]
lemma liftHomotopyZeroOne_comp {Y Z : C} {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
    (f : P.complex ‚ü∂ Q.complex) (comm : f ‚â´ Q.œÄ = 0) :
    liftHomotopyZeroOne f comm ‚â´ Q.complex.d 2 1 =
      f.f 1 - P.complex.d 1 0 ‚â´ liftHomotopyZeroZero f comm :=
  (Q.exact_succ 0).liftFromProjective_comp _ _

/-- An auxiliary definition for `liftHomotopyZero`. -/
def liftHomotopyZeroSucc {Y Z : C} {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
    (f : P.complex ‚ü∂ Q.complex) (n : ‚Ñï) (g : P.complex.X n ‚ü∂ Q.complex.X (n + 1))
    (g' : P.complex.X (n + 1) ‚ü∂ Q.complex.X (n + 2))
    (w : f.f (n + 1) = P.complex.d (n + 1) n ‚â´ g + g' ‚â´ Q.complex.d (n + 2) (n + 1)) :
    P.complex.X (n + 2) ‚ü∂ Q.complex.X (n + 3) :=
  (Q.exact_succ (n + 1)).liftFromProjective (f.f (n + 2) - P.complex.d _ _ ‚â´ g') (by simp [w])
#align category_theory.ProjectiveResolution.lift_homotopy_zero_succ CategoryTheory.ProjectiveResolution.liftHomotopyZeroSucc

@[reassoc (attr := simp)]
lemma liftHomotopyZeroSucc_comp {Y Z : C} {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
    (f : P.complex ‚ü∂ Q.complex) (n : ‚Ñï) (g : P.complex.X n ‚ü∂ Q.complex.X (n + 1))
    (g' : P.complex.X (n + 1) ‚ü∂ Q.complex.X (n + 2))
    (w : f.f (n + 1) = P.complex.d (n + 1) n ‚â´ g + g' ‚â´ Q.complex.d (n + 2) (n + 1)) :
    liftHomotopyZeroSucc f n g g' w ‚â´ Q.complex.d (n + 3) (n + 2) =
      f.f (n + 2) - P.complex.d _ _ ‚â´ g' :=
  (Q.exact_succ (n+1)).liftFromProjective_comp  _ _

/-- Any lift of the zero morphism is homotopic to zero. -/
def liftHomotopyZero {Y Z : C} {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
    (f : P.complex ‚ü∂ Q.complex) (comm : f ‚â´ Q.œÄ = 0) : Homotopy f 0 :=
  Homotopy.mkInductive _ (liftHomotopyZeroZero f comm) (by simp )
    (liftHomotopyZeroOne f comm) (by simp) fun n ‚ü®g, g', w‚ü© =>
    ‚ü®liftHomotopyZeroSucc f n g g' w, by simp‚ü©
#align category_theory.ProjectiveResolution.lift_homotopy_zero CategoryTheory.ProjectiveResolution.liftHomotopyZero

/-- Two lifts of the same morphism are homotopic. -/
def liftHomotopy {Y Z : C} (f : Y ‚ü∂ Z) {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
    (g h : P.complex ‚ü∂ Q.complex) (g_comm : g ‚â´ Q.œÄ = P.œÄ ‚â´ (ChainComplex.single‚ÇÄ C).map f)
    (h_comm : h ‚â´ Q.œÄ = P.œÄ ‚â´ (ChainComplex.single‚ÇÄ C).map f) : Homotopy g h :=
  Homotopy.equivSubZero.invFun (liftHomotopyZero _ (by simp [g_comm, h_comm]))
#align category_theory.ProjectiveResolution.lift_homotopy CategoryTheory.ProjectiveResolution.liftHomotopy

/-- The lift of the identity morphism is homotopic to the identity chain map. -/
def liftIdHomotopy (X : C) (P : ProjectiveResolution X) :
    Homotopy (lift (ùüô X) P P) (ùüô P.complex) := by
  apply liftHomotopy (ùüô X) <;> simp
#align category_theory.ProjectiveResolution.lift_id_homotopy CategoryTheory.ProjectiveResolution.liftIdHomotopy

/-- The lift of a composition is homotopic to the composition of the lifts. -/
def liftCompHomotopy {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (P : ProjectiveResolution X)
    (Q : ProjectiveResolution Y) (R : ProjectiveResolution Z) :
    Homotopy (lift (f ‚â´ g) P R) (lift f P Q ‚â´ lift g Q R) := by
  apply liftHomotopy (f ‚â´ g) <;> simp
#align category_theory.ProjectiveResolution.lift_comp_homotopy CategoryTheory.ProjectiveResolution.liftCompHomotopy

-- We don't care about the actual definitions of these homotopies.
/-- Any two projective resolutions are homotopy equivalent. -/
def homotopyEquiv {X : C} (P Q : ProjectiveResolution X) :
    HomotopyEquiv P.complex Q.complex where
  hom := lift (ùüô X) P Q
  inv := lift (ùüô X) Q P
  homotopyHomInvId := (liftCompHomotopy (ùüô X) (ùüô X) P Q P).symm.trans <| by
    simpa [id_comp] using liftIdHomotopy _ _
  homotopyInvHomId := (liftCompHomotopy (ùüô X) (ùüô X) Q P Q).symm.trans <| by
    simpa [id_comp] using liftIdHomotopy _ _
#align category_theory.ProjectiveResolution.homotopy_equiv CategoryTheory.ProjectiveResolution.homotopyEquiv

@[reassoc (attr := simp)]
theorem homotopyEquiv_hom_œÄ {X : C} (P Q : ProjectiveResolution X) :
    (homotopyEquiv P Q).hom ‚â´ Q.œÄ = P.œÄ := by simp [homotopyEquiv]
#align category_theory.ProjectiveResolution.homotopy_equiv_hom_œÄ CategoryTheory.ProjectiveResolution.homotopyEquiv_hom_œÄ

@[reassoc (attr := simp)]
theorem homotopyEquiv_inv_œÄ {X : C} (P Q : ProjectiveResolution X) :
    (homotopyEquiv P Q).inv ‚â´ P.œÄ = Q.œÄ := by simp [homotopyEquiv]
#align category_theory.ProjectiveResolution.homotopy_equiv_inv_œÄ CategoryTheory.ProjectiveResolution.homotopyEquiv_inv_œÄ

end Abelian

end ProjectiveResolution

/-- An arbitrarily chosen projective resolution of an object. -/
abbrev projectiveResolution (Z : C) [HasZeroObject C]
    [HasZeroMorphisms C] [HasProjectiveResolution Z] :
    ProjectiveResolution Z :=
  (HasProjectiveResolution.out (Z := Z)).some
#align category_theory.projective_resolution CategoryTheory.projectiveResolution

variable (C)
variable [Abelian C] [HasProjectiveResolutions C]

/-- Taking projective resolutions is functorial,
if considered with target the homotopy category
(`‚Ñï`-indexed chain complexes and chain maps up to homotopy).
-/
def projectiveResolutions : C ‚•§ HomotopyCategory C (ComplexShape.down ‚Ñï) where
  obj X := (HomotopyCategory.quotient _ _).obj (projectiveResolution X).complex
  map f := (HomotopyCategory.quotient _ _).map (ProjectiveResolution.lift f _ _)
  map_id X := by
    rw [‚Üê (HomotopyCategory.quotient _ _).map_id]
    apply HomotopyCategory.eq_of_homotopy
    apply ProjectiveResolution.liftIdHomotopy
  map_comp f g := by
    rw [‚Üê (HomotopyCategory.quotient _ _).map_comp]
    apply HomotopyCategory.eq_of_homotopy
    apply ProjectiveResolution.liftCompHomotopy
#align category_theory.projective_resolutions CategoryTheory.projectiveResolutions
variable {C}

/-- If `P : ProjectiveResolution X`, then the chosen `(projectiveResolutions C).obj X`
is isomorphic (in the homotopy category) to `P.complex`. -/
def ProjectiveResolution.iso {X : C} (P : ProjectiveResolution X) :
    (projectiveResolutions C).obj X ‚âÖ
      (HomotopyCategory.quotient _ _).obj P.complex :=
  HomotopyCategory.isoOfHomotopyEquiv (homotopyEquiv _ _)

@[reassoc]
lemma ProjectiveResolution.iso_inv_naturality {X Y : C} (f : X ‚ü∂ Y)
    (P : ProjectiveResolution X) (Q : ProjectiveResolution Y)
    (œÜ : P.complex ‚ü∂ Q.complex) (comm : œÜ.f 0 ‚â´ Q.œÄ.f 0 = P.œÄ.f 0 ‚â´ f) :
    P.iso.inv ‚â´ (projectiveResolutions C).map f =
      (HomotopyCategory.quotient _ _).map œÜ ‚â´ Q.iso.inv := by
  apply HomotopyCategory.eq_of_homotopy
  apply liftHomotopy f
  all_goals
    aesop_cat

@[reassoc]
lemma ProjectiveResolution.iso_hom_naturality {X Y : C} (f : X ‚ü∂ Y)
    (P : ProjectiveResolution X) (Q : ProjectiveResolution Y)
    (œÜ : P.complex ‚ü∂ Q.complex) (comm : œÜ.f 0 ‚â´ Q.œÄ.f 0 = P.œÄ.f 0 ‚â´ f) :
    (projectiveResolutions C).map f ‚â´ Q.iso.hom =
      P.iso.hom ‚â´ (HomotopyCategory.quotient _ _).map œÜ := by
  rw [‚Üê cancel_epi (P.iso).inv, iso_inv_naturality_assoc f P Q œÜ comm,
    Iso.inv_hom_id_assoc, Iso.inv_hom_id, comp_id]

variable [EnoughProjectives C]

theorem exact_d_f {X Y : C} (f : X ‚ü∂ Y) :
    (ShortComplex.mk (d f) f (by simp)).Exact := by
  let Œ± : ShortComplex.mk (d f) f (by simp) ‚ü∂ ShortComplex.mk (kernel.Œπ f) f (by simp) :=
    { œÑ‚ÇÅ := Projective.œÄ _
      œÑ‚ÇÇ := ùüô _
      œÑ‚ÇÉ := ùüô _ }
  have : Epi Œ±.œÑ‚ÇÅ := by dsimp; infer_instance
  have : IsIso Œ±.œÑ‚ÇÇ := by dsimp; infer_instance
  have : Mono Œ±.œÑ‚ÇÉ := by dsimp; infer_instance
  rw [ShortComplex.exact_iff_of_epi_of_isIso_of_mono Œ±]
  apply ShortComplex.exact_of_f_is_kernel
  apply kernelIsKernel
#align category_theory.exact_d_f CategoryTheory.exact_d_f

namespace ProjectiveResolution

/-!
Our goal is to define `ProjectiveResolution.of Z : ProjectiveResolution Z`.
The `0`-th object in this resolution will just be `Projective.over Z`,
i.e. an arbitrarily chosen projective object with a map to `Z`.
After that, we build the `n+1`-st object as `Projective.syzygies`
applied to the previously constructed morphism,
and the map from the `n`-th object as `Projective.d`.
-/

variable (Z : C)

-- The construction of the projective resolution `of` would be very, very slow
-- if it were not broken into separate definitions and lemmas

/-- Auxiliary definition for `ProjectiveResolution.of`. -/
def ofComplex : ChainComplex C ‚Ñï :=
  ChainComplex.mk' (Projective.over Z) (Projective.syzygies (Projective.œÄ Z))
    (Projective.d (Projective.œÄ Z)) (fun f => ‚ü®_, Projective.d f, by simp‚ü©)
#align category_theory.ProjectiveResolution.of_complex CategoryTheory.ProjectiveResolution.ofComplex

lemma ofComplex_d_1_0 :
    (ofComplex Z).d 1 0 = d (Projective.œÄ Z) := by
  simp [ofComplex]

lemma ofComplex_exactAt_succ (n : ‚Ñï) :
    (ofComplex Z).ExactAt (n + 1) := by
  rw [HomologicalComplex.exactAt_iff' _ (n + 1 + 1) (n + 1) n (by simp) (by simp)]
  dsimp [ofComplex, HomologicalComplex.sc', HomologicalComplex.shortComplexFunctor',
      ChainComplex.mk', ChainComplex.mk]
  simp only [ChainComplex.of_d]
  -- TODO: this should just be apply exact_d_f so something is missing
  match n with
  | 0 =>
    apply exact_d_f ((ChainComplex.mkAux _ _ _ (d (Projective.œÄ Z)) (d (d (Projective.œÄ Z))) _ _
      0).g)
  | n+1 =>
    apply exact_d_f ((ChainComplex.mkAux _ _ _ (d (Projective.œÄ Z)) (d (d (Projective.œÄ Z))) _ _
      (n+1)).g)

instance (n : ‚Ñï) : Projective ((ofComplex Z).X n) := by
  obtain (_ | _ | _ | n) := n <;> apply Projective.projective_over

/-- In any abelian category with enough projectives,
`ProjectiveResolution.of Z` constructs an projective resolution of the object `Z`.
-/
irreducible_def of : ProjectiveResolution Z where
  complex := ofComplex Z
  œÄ := (ChainComplex.toSingle‚ÇÄEquiv _ _).symm ‚ü®Projective.œÄ Z, by
          rw [ofComplex_d_1_0, assoc, kernel.condition, comp_zero]‚ü©
  quasiIso := ‚ü®fun n => by
    cases n
    ¬∑ rw [ChainComplex.quasiIsoAt‚ÇÄ_iff, ShortComplex.quasiIso_iff_of_zeros']
      ¬∑ dsimp
        refine (ShortComplex.exact_and_epi_g_iff_of_iso ?_).2
          ‚ü®exact_d_f (Projective.œÄ Z), by dsimp; infer_instance‚ü©
        exact ShortComplex.isoMk (Iso.refl _) (Iso.refl _) (Iso.refl _)
          (by simp [ofComplex]) (by simp)
      all_goals rfl
    ¬∑ rw [quasiIsoAt_iff_exactAt']
      ¬∑ apply ofComplex_exactAt_succ
      ¬∑ apply ChainComplex.exactAt_succ_single_obj‚ü©
#align category_theory.ProjectiveResolution.of CategoryTheory.ProjectiveResolution.of

instance (priority := 100) (Z : C) : HasProjectiveResolution Z where out := ‚ü®of Z‚ü©

instance (priority := 100) : HasProjectiveResolutions C where out _ := inferInstance

end ProjectiveResolution

end CategoryTheory
