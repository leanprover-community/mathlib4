/-
Copyright (c) 2020 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Limits.Shapes.FiniteProducts
import Mathlib.CategoryTheory.Limits.Shapes.Kernels
import Mathlib.CategoryTheory.Limits.Shapes.NormalMono.Equalizers
import Mathlib.CategoryTheory.Abelian.Images
import Mathlib.CategoryTheory.Preadditive.Basic

/-!
# Every NonPreadditiveAbelian category is preadditive

In mathlib, we define an abelian category as a preadditive category with a zero object,
kernels and cokernels, products and coproducts and in which every monomorphism and epimorphism is
normal.

While virtually every interesting abelian category has a natural preadditive structure (which is why
it is included in the definition), preadditivity is not actually needed: Every category that has
all of the other properties appearing in the definition of an abelian category admits a preadditive
structure. This is the construction we carry out in this file.

The proof proceeds in roughly five steps:
1. Prove some results (for example that all equalizers exist) that would be trivial if we already
   had the preadditive structure but are a bit of work without it.
2. Develop images and coimages to show that every monomorphism is the kernel of its cokernel.

The results of the first two steps are also useful for the "normal" development of abelian
categories, and will be used there.

3. For every object `A`, define a "subtraction" morphism `Ïƒ : A â¨¯ A âŸ¶ A` and use it to define
   subtraction on morphisms as `f - g := prod.lift f g â‰« Ïƒ`.
4. Prove a small number of identities about this subtraction from the definition of `Ïƒ`.
5. From these identities, prove a large number of other identities that imply that defining
   `f + g := f - (0 - g)` indeed gives an abelian group structure on morphisms such that composition
   is bilinear.

The construction is non-trivial and it is quite remarkable that this abelian group structure can
be constructed purely from the existence of a few limits and colimits. Even more remarkably,
since abelian categories admit exactly one preadditive structure (see
`subsingletonPreadditiveOfHasBinaryBiproducts`), the construction manages to exactly
reconstruct any natural preadditive structure the category may have.

## References

* [F. Borceux, *Handbook of Categorical Algebra 2*][borceux-vol2]

-/


noncomputable section

open CategoryTheory

open CategoryTheory.Limits

namespace CategoryTheory

section

universe v u

variable (C : Type u) [Category.{v} C]

/-- We call a category `NonPreadditiveAbelian` if it has a zero object, kernels, cokernels, binary
    products and coproducts, and every monomorphism and every epimorphism is normal. -/
class NonPreadditiveAbelian extends HasZeroMorphisms C, IsNormalMonoCategory C,
    IsNormalEpiCategory C where
  [has_zero_object : HasZeroObject C]
  [has_kernels : HasKernels C]
  [has_cokernels : HasCokernels C]
  [has_finite_products : HasFiniteProducts C]
  [has_finite_coproducts : HasFiniteCoproducts C]

attribute [instance] NonPreadditiveAbelian.has_zero_object

attribute [instance] NonPreadditiveAbelian.has_kernels

attribute [instance] NonPreadditiveAbelian.has_cokernels

attribute [instance] NonPreadditiveAbelian.has_finite_products

attribute [instance] NonPreadditiveAbelian.has_finite_coproducts

end

end CategoryTheory

open CategoryTheory

universe v u

variable {C : Type u} [Category.{v} C] [NonPreadditiveAbelian C]

namespace CategoryTheory.NonPreadditiveAbelian

section Factor

variable {P Q : C} (f : P âŸ¶ Q)

/-- The map `p : P âŸ¶ image f` is an epimorphism -/
instance : Epi (Abelian.factorThruImage f) :=
  let I := Abelian.image f
  let p := Abelian.factorThruImage f
  let i := kernel.Î¹ (cokernel.Ï€ f)
  -- It will suffice to consider some g : I âŸ¶ R such that p â‰« g = 0 and show that g = 0.
  NormalMonoCategory.epi_of_zero_cancel
  _ fun R (g : I âŸ¶ R) (hpg : p â‰« g = 0) => by
  -- Since C is abelian, u := ker g â‰« i is the kernel of some morphism h.
  let u := kernel.Î¹ g â‰« i
  haveI hu := normalMonoOfMono u
  let h := hu.g
  -- By hypothesis, p factors through the kernel of g via some t.
  obtain âŸ¨t, htâŸ© := kernel.lift' g p hpg
  have fh : f â‰« h = 0 :=
    calc
      f â‰« h = (p â‰« i) â‰« h := (Abelian.image.fac f).symm â–¸ rfl
      _ = ((t â‰« kernel.Î¹ g) â‰« i) â‰« h := ht â–¸ rfl
      _ = t â‰« u â‰« h := by simp only [u, Category.assoc]
      _ = t â‰« 0 := hu.w â–¸ rfl
      _ = 0 := HasZeroMorphisms.comp_zero _ _
  -- h factors through the cokernel of f via some l.
  obtain âŸ¨l, hlâŸ© := cokernel.desc' f h fh
  have hih : i â‰« h = 0 :=
    calc
      i â‰« h = i â‰« cokernel.Ï€ f â‰« l := hl â–¸ rfl
      _ = 0 â‰« l := by rw [â† Category.assoc, kernel.condition]
      _ = 0 := zero_comp
  -- i factors through u = ker h via some s.
  obtain âŸ¨s, hsâŸ© := NormalMono.lift' u i hih
  have hs' : (s â‰« kernel.Î¹ g) â‰« i = ğŸ™ I â‰« i := by rw [Category.assoc, hs, Category.id_comp]
  haveI : Epi (kernel.Î¹ g) := epi_of_epi_fac ((cancel_mono _).1 hs')
  -- ker g is an epimorphism, but ker g â‰« g = 0 = ker g â‰« 0, so g = 0 as required.
  exact zero_of_epi_comp _ (kernel.condition g)

instance isIso_factorThruImage [Mono f] : IsIso (Abelian.factorThruImage f) :=
  isIso_of_mono_of_epi <| Abelian.factorThruImage f

/-- The canonical morphism `i : coimage f âŸ¶ Q` is a monomorphism -/
instance : Mono (Abelian.factorThruCoimage f) :=
  let I := Abelian.coimage f
  let i := Abelian.factorThruCoimage f
  let p := cokernel.Ï€ (kernel.Î¹ f)
  NormalEpiCategory.mono_of_cancel_zero _ fun R (g : R âŸ¶ I) (hgi : g â‰« i = 0) => by
    -- Since C is abelian, u := p â‰« coker g is the cokernel of some morphism h.
    let u := p â‰« cokernel.Ï€ g
    haveI hu := normalEpiOfEpi u
    let h := hu.g
    -- By hypothesis, i factors through the cokernel of g via some t.
    obtain âŸ¨t, htâŸ© := cokernel.desc' g i hgi
    have hf : h â‰« f = 0 :=
      calc
        h â‰« f = h â‰« p â‰« i := (Abelian.coimage.fac f).symm â–¸ rfl
        _ = h â‰« p â‰« cokernel.Ï€ g â‰« t := ht â–¸ rfl
        _ = h â‰« u â‰« t := by simp only [u, Category.assoc]
        _ = 0 â‰« t := by rw [â† Category.assoc, hu.w]
        _ = 0 := zero_comp
    -- h factors through the kernel of f via some l.
    obtain âŸ¨l, hlâŸ© := kernel.lift' f h hf
    have hhp : h â‰« p = 0 :=
      calc
        h â‰« p = (l â‰« kernel.Î¹ f) â‰« p := hl â–¸ rfl
        _ = l â‰« 0 := by rw [Category.assoc, cokernel.condition]
        _ = 0 := comp_zero
    -- p factors through u = coker h via some s.
    obtain âŸ¨s, hsâŸ© := NormalEpi.desc' u p hhp
    have hs' : p â‰« cokernel.Ï€ g â‰« s = p â‰« ğŸ™ I := by rw [â† Category.assoc, hs, Category.comp_id]
    haveI : Mono (cokernel.Ï€ g) := mono_of_mono_fac ((cancel_epi _).1 hs')
    -- coker g is a monomorphism, but g â‰« coker g = 0 = 0 â‰« coker g, so g = 0 as required.
    exact zero_of_comp_mono _ (cokernel.condition g)

instance isIso_factorThruCoimage [Epi f] : IsIso (Abelian.factorThruCoimage f) :=
  isIso_of_mono_of_epi _

end Factor

section CokernelOfKernel

variable {X Y : C} {f : X âŸ¶ Y}

/-- In a `NonPreadditiveAbelian` category, an epi is the cokernel of its kernel. More precisely:
    If `f` is an epimorphism and `s` is some limit kernel cone on `f`, then `f` is a cokernel
    of `Fork.Î¹ s`. -/
def epiIsCokernelOfKernel [Epi f] (s : Fork f 0) (h : IsLimit s) :
    IsColimit (CokernelCofork.ofÏ€ f (KernelFork.condition s)) :=
  IsCokernel.cokernelIso _ _
    (cokernel.ofIsoComp _ _ (Limits.IsLimit.conePointUniqueUpToIso (limit.isLimit _) h)
      (ConeMorphism.w (Limits.IsLimit.uniqueUpToIso (limit.isLimit _) h).hom _))
    (asIso <| Abelian.factorThruCoimage f) (Abelian.coimage.fac f)

/-- In a `NonPreadditiveAbelian` category, a mono is the kernel of its cokernel. More precisely:
    If `f` is a monomorphism and `s` is some colimit cokernel cocone on `f`, then `f` is a kernel
    of `Cofork.Ï€ s`. -/
def monoIsKernelOfCokernel [Mono f] (s : Cofork f 0) (h : IsColimit s) :
    IsLimit (KernelFork.ofÎ¹ f (CokernelCofork.condition s)) :=
  IsKernel.isoKernel _ _
    (kernel.ofCompIso _ _ (Limits.IsColimit.coconePointUniqueUpToIso h (colimit.isColimit _))
      (CoconeMorphism.w (Limits.IsColimit.uniqueUpToIso h <| colimit.isColimit _).hom _))
    (asIso <| Abelian.factorThruImage f) (Abelian.image.fac f)

end CokernelOfKernel

section

/-- The composite `A âŸ¶ A â¨¯ A âŸ¶ cokernel (Î” A)`, where the first map is `(ğŸ™ A, 0)` and the second map
    is the canonical projection into the cokernel. -/
abbrev r (A : C) : A âŸ¶ cokernel (diag A) :=
  prod.lift (ğŸ™ A) 0 â‰« cokernel.Ï€ (diag A)

instance mono_Î” {A : C} : Mono (diag A) :=
  mono_of_mono_fac <| prod.lift_fst _ _

instance mono_r {A : C} : Mono (r A) := by
  let hl : IsLimit (KernelFork.ofÎ¹ (diag A) (cokernel.condition (diag A))) :=
    monoIsKernelOfCokernel _ (colimit.isColimit _)
  apply NormalEpiCategory.mono_of_cancel_zero
  intro Z x hx
  have hxx : (x â‰« prod.lift (ğŸ™ A) (0 : A âŸ¶ A)) â‰« cokernel.Ï€ (diag A) = 0 := by
    rw [Category.assoc, hx]
  obtain âŸ¨y, hyâŸ© := KernelFork.IsLimit.lift' hl _ hxx
  rw [KernelFork.Î¹_ofÎ¹] at hy
  have hyy : y = 0 := by
    erw [â† Category.comp_id y, â† Limits.prod.lift_snd (ğŸ™ A) (ğŸ™ A), â† Category.assoc, hy,
      Category.assoc, prod.lift_snd, HasZeroMorphisms.comp_zero]
  haveI : Mono (prod.lift (ğŸ™ A) (0 : A âŸ¶ A)) := mono_of_mono_fac (prod.lift_fst _ _)
  apply (cancel_mono (prod.lift (ğŸ™ A) (0 : A âŸ¶ A))).1
  rw [â† hy, hyy, zero_comp, zero_comp]

instance epi_r {A : C} : Epi (r A) := by
  have hlp : prod.lift (ğŸ™ A) (0 : A âŸ¶ A) â‰« Limits.prod.snd = 0 := prod.lift_snd _ _
  let hp1 : IsLimit (KernelFork.ofÎ¹ (prod.lift (ğŸ™ A) (0 : A âŸ¶ A)) hlp) := by
    refine Fork.IsLimit.mk _ (fun s => Fork.Î¹ s â‰« Limits.prod.fst) ?_ ?_
    Â· intro s
      apply Limits.prod.hom_ext <;> simp
    Â· intro s m h
      haveI : Mono (prod.lift (ğŸ™ A) (0 : A âŸ¶ A)) := mono_of_mono_fac (prod.lift_fst _ _)
      apply (cancel_mono (prod.lift (ğŸ™ A) (0 : A âŸ¶ A))).1
      convert h
      apply Limits.prod.hom_ext <;> simp
  let hp2 : IsColimit (CokernelCofork.ofÏ€ (Limits.prod.snd : A â¨¯ A âŸ¶ A) hlp) :=
    epiIsCokernelOfKernel _ hp1
  apply NormalMonoCategory.epi_of_zero_cancel
  intro Z z hz
  have h : prod.lift (ğŸ™ A) (0 : A âŸ¶ A) â‰« cokernel.Ï€ (diag A) â‰« z = 0 := by rw [â† Category.assoc, hz]
  obtain âŸ¨t, htâŸ© := CokernelCofork.IsColimit.desc' hp2 _ h
  rw [CokernelCofork.Ï€_ofÏ€] at ht
  have htt : t = 0 := by
    rw [â† Category.id_comp t]
    change ğŸ™ A â‰« t = 0
    rw [â† Limits.prod.lift_snd (ğŸ™ A) (ğŸ™ A), Category.assoc, ht, â† Category.assoc,
      cokernel.condition, zero_comp]
  apply (cancel_epi (cokernel.Ï€ (diag A))).1
  rw [â† ht, htt, comp_zero, comp_zero]

instance isIso_r {A : C} : IsIso (r A) :=
  isIso_of_mono_of_epi _

/-- The composite `A â¨¯ A âŸ¶ cokernel (diag A) âŸ¶ A` given by the natural projection into the cokernel
    followed by the inverse of `r`. In the category of modules, using the normal kernels and
    cokernels, this map is equal to the map `(a, b) â†¦ a - b`, hence the name `Ïƒ` for
    "subtraction". -/
abbrev Ïƒ {A : C} : A â¨¯ A âŸ¶ A :=
  cokernel.Ï€ (diag A) â‰« inv (r A)

end

@[reassoc]
theorem diag_Ïƒ {X : C} : diag X â‰« Ïƒ = 0 := by rw [cokernel.condition_assoc, zero_comp]

@[reassoc (attr := simp)]
theorem lift_Ïƒ {X : C} : prod.lift (ğŸ™ X) 0 â‰« Ïƒ = ğŸ™ X := by rw [â† Category.assoc, IsIso.hom_inv_id]

@[reassoc]
theorem lift_map {X Y : C} (f : X âŸ¶ Y) :
    prod.lift (ğŸ™ X) 0 â‰« Limits.prod.map f f = f â‰« prod.lift (ğŸ™ Y) 0 := by simp

/-- Ïƒ is a cokernel of Î” X. -/
def isColimitÏƒ {X : C} : IsColimit (CokernelCofork.ofÏ€ (Ïƒ : X â¨¯ X âŸ¶ X) diag_Ïƒ) :=
  cokernel.cokernelIso _ Ïƒ (asIso (r X)).symm (by rw [Iso.symm_hom, asIso_inv])

/-- This is the key identity satisfied by `Ïƒ`. -/
theorem Ïƒ_comp {X Y : C} (f : X âŸ¶ Y) : Ïƒ â‰« f = Limits.prod.map f f â‰« Ïƒ := by
  obtain âŸ¨g, hgâŸ© :=
    CokernelCofork.IsColimit.desc' isColimitÏƒ (Limits.prod.map f f â‰« Ïƒ) (by
      rw [prod.diag_map_assoc, diag_Ïƒ, comp_zero])
  suffices hfg : f = g by rw [â† hg, Cofork.Ï€_ofÏ€, hfg]
  calc
    f = f â‰« prod.lift (ğŸ™ Y) 0 â‰« Ïƒ := by rw [lift_Ïƒ, Category.comp_id]
    _ = prod.lift (ğŸ™ X) 0 â‰« Limits.prod.map f f â‰« Ïƒ := by rw [lift_map_assoc]
    _ = prod.lift (ğŸ™ X) 0 â‰« Ïƒ â‰« g := by rw [â† hg, CokernelCofork.Ï€_ofÏ€]
    _ = g := by rw [â† Category.assoc, lift_Ïƒ, Category.id_comp]

section

-- We write `f - g` for `prod.lift f g â‰« Ïƒ`.
/-- Subtraction of morphisms in a `NonPreadditiveAbelian` category. -/
def hasSub {X Y : C} : Sub (X âŸ¶ Y) :=
  âŸ¨fun f g => prod.lift f g â‰« ÏƒâŸ©

attribute [local instance] hasSub

-- We write `-f` for `0 - f`.
/-- Negation of morphisms in a `NonPreadditiveAbelian` category. -/
def hasNeg {X Y : C} : Neg (X âŸ¶ Y) where
  neg := fun f => 0 - f

attribute [local instance] hasNeg

-- We write `f + g` for `f - (-g)`.
/-- Addition of morphisms in a `NonPreadditiveAbelian` category. -/
def hasAdd {X Y : C} : Add (X âŸ¶ Y) :=
  âŸ¨fun f g => f - -gâŸ©

attribute [local instance] hasAdd

theorem sub_def {X Y : C} (a b : X âŸ¶ Y) : a - b = prod.lift a b â‰« Ïƒ := rfl

theorem add_def {X Y : C} (a b : X âŸ¶ Y) : a + b = a - -b := rfl

theorem neg_def {X Y : C} (a : X âŸ¶ Y) : -a = 0 - a := rfl

theorem sub_zero {X Y : C} (a : X âŸ¶ Y) : a - 0 = a := by
  rw [sub_def]
  conv_lhs =>
    congr; congr; rw [â† Category.comp_id a]
    case a.g => rw [show 0 = a â‰« (0 : Y âŸ¶ Y) by simp]
  rw [â† prod.comp_lift, Category.assoc, lift_Ïƒ, Category.comp_id]

theorem sub_self {X Y : C} (a : X âŸ¶ Y) : a - a = 0 := by
  rw [sub_def, â† Category.comp_id a, â† prod.comp_lift, Category.assoc, diag_Ïƒ, comp_zero]

theorem lift_sub_lift {X Y : C} (a b c d : X âŸ¶ Y) :
    prod.lift a b - prod.lift c d = prod.lift (a - c) (b - d) := by
  simp only [sub_def]
  ext
  Â· rw [Category.assoc, Ïƒ_comp, prod.lift_map_assoc, prod.lift_fst, prod.lift_fst, prod.lift_fst]
  Â· rw [Category.assoc, Ïƒ_comp, prod.lift_map_assoc, prod.lift_snd, prod.lift_snd, prod.lift_snd]

theorem sub_sub_sub {X Y : C} (a b c d : X âŸ¶ Y) : a - c - (b - d) = a - b - (c - d) := by
  rw [sub_def, â† lift_sub_lift, sub_def, Category.assoc, Ïƒ_comp, prod.lift_map_assoc]; rfl

theorem neg_sub {X Y : C} (a b : X âŸ¶ Y) : -a - b = -b - a := by
  conv_lhs => rw [neg_def, â† sub_zero b, sub_sub_sub, sub_zero, â† neg_def]

theorem neg_neg {X Y : C} (a : X âŸ¶ Y) : - -a = a := by
  rw [neg_def, neg_def]
  conv_lhs =>
    congr; rw [â† sub_self a]
  rw [sub_sub_sub, sub_zero, sub_self, sub_zero]

theorem add_comm {X Y : C} (a b : X âŸ¶ Y) : a + b = b + a := by
  rw [add_def]
  conv_lhs => rw [â† neg_neg a]
  rw [neg_def, neg_def, neg_def, sub_sub_sub]
  conv_lhs =>
    congr
    next => skip
    rw [â† neg_def, neg_sub]
  rw [sub_sub_sub, add_def, â† neg_def, neg_neg b, neg_def]

theorem add_neg {X Y : C} (a b : X âŸ¶ Y) : a + -b = a - b := by rw [add_def, neg_neg]

theorem add_neg_cancel {X Y : C} (a : X âŸ¶ Y) : a + -a = 0 := by rw [add_neg, sub_self]

theorem neg_add_cancel {X Y : C} (a : X âŸ¶ Y) : -a + a = 0 := by rw [add_comm, add_neg_cancel]

theorem neg_sub' {X Y : C} (a b : X âŸ¶ Y) : -(a - b) = -a + b := by
  rw [neg_def, neg_def]
  conv_lhs => rw [â† sub_self (0 : X âŸ¶ Y)]
  rw [sub_sub_sub, add_def, neg_def]

theorem neg_add {X Y : C} (a b : X âŸ¶ Y) : -(a + b) = -a - b := by rw [add_def, neg_sub', add_neg]

theorem sub_add {X Y : C} (a b c : X âŸ¶ Y) : a - b + c = a - (b - c) := by
  rw [add_def, neg_def, sub_sub_sub, sub_zero]

theorem add_assoc {X Y : C} (a b c : X âŸ¶ Y) : a + b + c = a + (b + c) := by
  conv_lhs =>
    congr; rw [add_def]
  rw [sub_add, â† add_neg, neg_sub', neg_neg]

theorem add_zero {X Y : C} (a : X âŸ¶ Y) : a + 0 = a := by rw [add_def, neg_def, sub_self, sub_zero]

theorem comp_sub {X Y Z : C} (f : X âŸ¶ Y) (g h : Y âŸ¶ Z) : f â‰« (g - h) = f â‰« g - f â‰« h := by
  rw [sub_def, â† Category.assoc, prod.comp_lift, sub_def]

theorem sub_comp {X Y Z : C} (f g : X âŸ¶ Y) (h : Y âŸ¶ Z) : (f - g) â‰« h = f â‰« h - g â‰« h := by
  rw [sub_def, Category.assoc, Ïƒ_comp, â† Category.assoc, prod.lift_map, sub_def]

theorem comp_add (X Y Z : C) (f : X âŸ¶ Y) (g h : Y âŸ¶ Z) : f â‰« (g + h) = f â‰« g + f â‰« h := by
  rw [add_def, comp_sub, neg_def, comp_sub, comp_zero, add_def, neg_def]

theorem add_comp (X Y Z : C) (f g : X âŸ¶ Y) (h : Y âŸ¶ Z) : (f + g) â‰« h = f â‰« h + g â‰« h := by
  rw [add_def, sub_comp, neg_def, sub_comp, zero_comp, add_def, neg_def]

/-- Every `NonPreadditiveAbelian` category is preadditive. -/
def preadditive : Preadditive C where
  homGroup X Y :=
    { add := (Â· + Â·)
      add_assoc := add_assoc
      zero := 0
      zero_add := neg_neg
      add_zero := add_zero
      neg := fun f => -f
      neg_add_cancel := neg_add_cancel
      sub_eq_add_neg := fun f g => (add_neg f g).symm -- Porting note: autoParam failed
      add_comm := add_comm
      nsmul := nsmulRec
      zsmul := zsmulRec }
  add_comp := add_comp
  comp_add := comp_add

end

end CategoryTheory.NonPreadditiveAbelian
