/-
Copyright (c) 2025 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Abelian.GrothendieckAxioms.Basic
import Mathlib.CategoryTheory.Limits.Connected
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq
import Mathlib.CategoryTheory.Limits.FunctorCategory.Shapes.Pullbacks

/-!
# Pulling back connected colimits

If `c` is a cocone over a functor `J â¥¤ C` and `f : X âŸ¶ c.pt`, then for every `j : J` we can take
the pullback of `c.Î¹.app j` and `f`. This gives a new cocone with cone point `X`. We show that if
`c` is a colimit cocone, then this is again a colimit cocone as long as `J` is connected and `C`
has exact colimits of shape `J`.

From this we deduce a `hom_ext` principle for morphisms factoring through a colimit. Usually, we
only get `hom_ext` for morphisms *from* a colimit, so this is something a bit special.

The connectedness assumption on `J` is necessary: take `C` to be the category of abelian groups,
let `f : â„¤ â†’ â„¤ âŠ• â„¤` be the diagonal map, and let `g := ğŸ™ (â„¤ âŠ• â„¤)`. Then the hypotheses of
`IsColimit.pullback_zero_ext` are satisfied, but `f â‰« g` is not zero.

-/

universe w' w v u

namespace CategoryTheory.Limits

variable {J : Type w} [Category.{w'} J] [IsConnected J] {C : Type u} [Category.{v} C]

/--
If `c` is a cocone over a functor `J â¥¤ C` and `f : X âŸ¶ c.pt`, then for every `j : J` we can take
the pullback of `c.Î¹.app j` and `f`. This gives a new cocone with cone point `X`, and this cocone
is again a colimit cocone as long as `J` is connected and `C` has exact colimits of shape `J`.
-/
noncomputable def IsColimit.pullbackOfHasExactColimitsOfShape [HasPullbacks C]
    [HasColimitsOfShape J C] [HasExactColimitsOfShape J C] {F : J â¥¤ C} {c : Cocone F}
    (hc : IsColimit c) {X : C} (f : X âŸ¶ c.pt) :
    IsColimit (Cocone.mk _ (pullback.snd c.Î¹ ((Functor.const J).map f))) := by
  suffices IsIso (colimMap (pullback.snd c.Î¹ ((Functor.const J).map f))) from
    Cocone.isColimitOfIsIsoColimMapÎ¹ _
  have hpull := colim.map_isPullback (IsPullback.of_hasPullback c.Î¹ ((Functor.const J).map f))
  dsimp only [colim_obj, colim_map] at hpull
  have := hc.isIso_colimMap_Î¹
  apply hpull.isIso_snd_of_isIso

/-- Detecting equality of morphisms factoring through a connected colimit by pulling back along
the inclusions of the colimit. -/
theorem IsColimit.pullback_hom_ext [HasPullbacks C] [HasColimitsOfShape J C]
    [HasExactColimitsOfShape J C] {F : J â¥¤ C} {c : Cocone F} (hc : IsColimit c) {X Y : C}
    {f : X âŸ¶ c.pt} {g h : c.pt âŸ¶ Y}
    (hf : âˆ€ j, pullback.snd (c.Î¹.app j) f â‰« f â‰« g = pullback.snd (c.Î¹.app j) f â‰« f â‰« h) :
    f â‰« g = f â‰« h := by
  refine (hc.pullbackOfHasExactColimitsOfShape f).hom_ext (fun j => ?_)
  rw [â† cancel_epi (pullbackObjIso _ _ _).inv]
  simpa using hf j

/-- Detecting vanishing of a morphism factoring through a connected colimit by pulling back along
the inclusions of the colimit. -/
theorem IsColimit.pullback_zero_ext [HasZeroMorphisms C] [HasPullbacks C] [HasColimitsOfShape J C]
    [HasExactColimitsOfShape J C] {F : J â¥¤ C} {c : Cocone F} (hc : IsColimit c) {X Y : C}
    {f : X âŸ¶ c.pt} {g : c.pt âŸ¶ Y} (hf : âˆ€ j, pullback.snd (c.Î¹.app j) f â‰« f â‰« g = 0) :
    f â‰« g = 0 := by
  suffices f â‰« g = f â‰« 0 by simpa
  exact hc.pullback_hom_ext (by simpa using hf)

/--
If `c` is a cone over a functor `J â¥¤ C` and `f : c.pt âŸ¶ X`, then for every `j : J` we can take
the pushout of `c.Ï€.app j` and `f`. This gives a new cone with cone point `X`, and this cone is
again a limit cone as long as `J` is connected and `C` has exact limits of shape `J`.
-/
noncomputable def IsLimit.pushoutOfHasExactLimitsOfShape [HasPushouts C]
    [HasLimitsOfShape J C] [HasExactLimitsOfShape J C] {F : J â¥¤ C} {c : Cone F}
    (hc : IsLimit c) {X : C} (f : c.pt âŸ¶ X) :
    IsLimit (Cone.mk _ (pushout.inr c.Ï€ ((Functor.const J).map f))) := by
  suffices IsIso (limMap (pushout.inr c.Ï€ ((Functor.const J).map f))) from
    Cone.isLimitOfIsIsoLimMapÏ€ _
  have hpush := lim.map_isPushout (IsPushout.of_hasPushout c.Ï€ ((Functor.const J).map f))
  dsimp only [lim_obj, lim_map] at hpush
  have := hc.isIso_limMap_Ï€
  apply hpush.isIso_inr_of_isIso

/-- Detecting equality of morphisms factoring through a connected limit by pushing out along
the projections of the limit. -/
theorem IsLimit.pushout_hom_ext [HasPushouts C] [HasLimitsOfShape J C]
    [HasExactLimitsOfShape J C] {F : J â¥¤ C} {c : Cone F} (hc : IsLimit c) {X Y : C}
    {g h : Y âŸ¶ c.pt} {f : c.pt âŸ¶ X}
    (hf : âˆ€ j, g â‰« f â‰« pushout.inr (c.Ï€.app j) f = h â‰« f â‰« pushout.inr (c.Ï€.app j) f) :
    g â‰« f = h â‰« f := by
  refine (hc.pushoutOfHasExactLimitsOfShape f).hom_ext (fun j => ?_)
  rw [â† cancel_mono (pushoutObjIso _ _ _).hom]
  simpa using hf j

/-- Detecting vanishing of a morphism factoring though a connected limit by pushing out along the
projections of the limit. -/
theorem IsLimit.pushout_zero_ext [HasZeroMorphisms C] [HasPushouts C] [HasLimitsOfShape J C]
    [HasExactLimitsOfShape J C] {F : J â¥¤ C} {c : Cone F} (hc : IsLimit c) {X Y : C}
    {g : Y âŸ¶ c.pt} {f : c.pt âŸ¶ X} (hf : âˆ€ j, g â‰« f â‰« pushout.inr (c.Ï€.app j) f = 0) :
    g â‰« f = 0 := by
  suffices g â‰« f = 0 â‰« f by simpa
  exact hc.pushout_hom_ext (by simpa using hf)

end CategoryTheory.Limits
