import Mathlib.CategoryTheory.Abelian.Refinements
import Mathlib.CategoryTheory.Sites.Canonical
import Mathlib.CategoryTheory.Sites.Sheaf

universe w v u

namespace CategoryTheory

open Opposite Limits

namespace Abelian

variable (C : Type u) [Category.{v} C] [Abelian C]

def refinementsTopology : GrothendieckTopology C where
  sieves X S := âˆƒ (T : C) (p : T âŸ¶ X) (_ : Epi p), S p
  top_mem' X := âŸ¨X, ğŸ™ X, inferInstance, by simpâŸ©
  pullback_stable' X Y S f hS := by
    obtain âŸ¨T, p, hp, hâŸ© := hS
    refine' âŸ¨pullback f p, pullback.fst, inferInstance, _âŸ©
    dsimp
    rw [pullback.condition]
    apply S.downward_closed h
  transitive' X S hS R hR := by
    obtain âŸ¨T, p, hp, hâŸ© := hS
    obtain âŸ¨U, q, hq, h'âŸ© := hR h
    exact âŸ¨_, q â‰« p, epi_comp _ _, h'âŸ©

end Abelian

namespace Sheaf

variable {C : Type u} [Category.{v} C] {J : GrothendieckTopology C}
  {F G : Sheaf J (Type w)} (Ï† : F âŸ¶ G)

lemma mono_of_injective
    (hÏ† : âˆ€ (X : Cáµ’áµ–), Function.Injective (fun (x : F.1.obj X) => Ï†.1.app _ x)) : Mono Ï† where
  right_cancellation := by
    intro H fâ‚ fâ‚‚ h
    ext Z x
    exact hÏ† Z (congr_fun (congr_app (congr_arg Sheaf.Hom.val h) Z) x)

lemma epi_of_locally_surjective (hÏ† : âˆ€ (X : Cáµ’áµ–) (x : G.1.obj X),
    âˆƒ (S : Sieve X.unop) (_ : S âˆˆ J X.unop),
    âˆ€ (Y : C) (f : Y âŸ¶ X.unop) (_ : S f), âˆƒ (y : F.1.obj (op Y)),
    Ï†.1.app _ y = G.1.map f.op x) : Epi Ï† where
  left_cancellation := by
    intro H fâ‚ fâ‚‚ h
    ext X x
    obtain âŸ¨S, hS, hS'âŸ© := hÏ† _ x
    apply ((Presieve.isSeparated_of_isSheaf _ _
      ((isSheaf_iff_isSheaf_of_type _ _).1 H.2)) S hS).ext
    intro Y f hf
    obtain âŸ¨y, hyâŸ© := hS' Y f hf
    have hâ‚ := congr_fun (fâ‚.1.naturality f.op) x
    have hâ‚‚ := congr_fun (fâ‚‚.1.naturality f.op) x
    dsimp at hâ‚ hâ‚‚
    simp only [â† hâ‚, â† hâ‚‚, â† hy]
    exact congr_fun (congr_app (congr_arg Sheaf.Hom.val h) (op Y)) y

namespace EpiMonoFactorization

@[simps]
def presheafI : Cáµ’áµ– â¥¤ Type w where
  obj X := { x : G.1.obj X | âˆƒ (S : Sieve X.unop) (_ : S âˆˆ J X.unop),
    âˆ€ (Y : C) (f : Y âŸ¶ X.unop) (_ : S f), âˆƒ (y : F.1.obj (op Y)),
      Ï†.1.app _ y = G.1.map f.op x }
  map {X X'} g a := âŸ¨G.1.map g a.1, by
    obtain âŸ¨S, hS, hâŸ© := a.2
    refine' âŸ¨S.pullback g.unop, J.pullback_stable _ hS, fun Y f hf => _âŸ©
    obtain âŸ¨y, hyâŸ© := h Y (f â‰« g.unop) hf
    exact âŸ¨y, by simp [hy]âŸ©âŸ©

@[simps]
def presheafÎ¹ : presheafI Ï† âŸ¶ G.1 where
  app _ x := x.1
  naturality _ _ _ := rfl

@[simps]
def I : Sheaf J (Type w) := âŸ¨presheafI Ï†, by
  rw [isSheaf_iff_isSheaf_of_type]
  intro X S hS Î± hÎ±
  have hS' := (((isSheaf_iff_isSheaf_of_type _ _).1 G.2) _ hS)
  refine' âŸ¨âŸ¨hS'.amalgamate _
    (hÎ±.compPresheafMap (presheafÎ¹ Ï†)), _âŸ©, _, _âŸ©
  Â· let U := fun â¦ƒYâ¦„ â¦ƒf : Y âŸ¶ Xâ¦„ (hf : S.arrows f) => (Î± f hf).2.choose
    have hU : âˆ€ â¦ƒYâ¦„ â¦ƒf : Y âŸ¶ Xâ¦„ (hf : S.arrows f), U hf âˆˆ J _:= fun Y f hf =>
        (Î± f hf).2.choose_spec.choose
    refine' âŸ¨_, J.bind_covering hS hU, fun Y f hf => _âŸ©
    obtain âŸ¨T, a, b, hb, ha : U hb a, facâŸ© := hf
    obtain âŸ¨y, hyâŸ© := (Î± _ hb).2.choose_spec.choose_spec _ a ha
    refine' âŸ¨y, _âŸ©
    have hf : S.arrows f := by
      rw [â† fac]
      apply S.downward_closed hb
    rw [hy, Presieve.IsSheafFor.valid_glue hS' (hÎ±.compPresheafMap (presheafÎ¹ Ï†)) f hf]
    simpa using (hÎ±.compPresheafMap (presheafÎ¹ Ï†)) a (ğŸ™ _) hb hf (by simpa using fac)
  Â· intro Y f hf
    apply Subtype.ext
    apply Presieve.IsSheafFor.valid_glue hS' (hÎ±.compPresheafMap (presheafÎ¹ Ï†))
  Â· rintro âŸ¨y, _âŸ© hy
    apply Subtype.ext
    apply ((Presieve.isSeparated_of_isSheaf _ _
      ((isSheaf_iff_isSheaf_of_type _ _).1 G.2)) S hS).ext
    intro Y f hf
    dsimp
    replace hy := hy f hf
    rw [Subtype.ext_iff] at hy
    dsimp at hy
    rw [hy]
    symm
    apply Presieve.IsSheafFor.valid_glueâŸ©

@[simps]
def Î¹ : I Ï† âŸ¶ G := Sheaf.Hom.mk (presheafÎ¹ Ï†)

@[simps]
def Ï€ : F âŸ¶ I Ï† where
  val :=
    { app := fun X x => âŸ¨Ï†.1.app X x, âŸ¨âŠ¤, J.top_mem X.unop, fun Y f _ =>
        âŸ¨F.1.map f.op x, congr_fun (Ï†.val.naturality f.op) xâŸ©âŸ©âŸ©
      naturality := fun X X' g => by
        ext x
        exact Subtype.ext (congr_fun (Ï†.val.naturality g) x) }

instance : Epi (Ï€ Ï†) := by
  apply epi_of_locally_surjective
  intro X x
  obtain âŸ¨S, hS, hS'âŸ© := x.2
  refine' âŸ¨S, hS, fun Y f hf => _âŸ©
  obtain âŸ¨y, hyâŸ© := hS' Y f hf
  exact âŸ¨y, Subtype.ext hyâŸ©

instance : Mono (Î¹ Ï†) := by
  apply mono_of_injective
  intro X xâ‚ xâ‚‚ h
  exact Subtype.ext h

@[reassoc (attr := simp)]
lemma Ï€_Î¹ : Ï€ Ï† â‰« Î¹ Ï† = Ï† := rfl

end EpiMonoFactorization

end Sheaf

end CategoryTheory
