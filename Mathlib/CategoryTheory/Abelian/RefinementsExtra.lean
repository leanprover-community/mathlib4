import Mathlib.CategoryTheory.Abelian.Refinements
import Mathlib.CategoryTheory.Sites.Coherent.RegularSheaves
import Mathlib.CategoryTheory.Sites.Coherent.Comparison
import Mathlib.CategoryTheory.Sites.Balanced
import Mathlib.CategoryTheory.Sites.Limits

universe w v u

namespace CategoryTheory

open Opposite Limits Category

namespace Limits

variable {C : Type u} [Category.{v} C] [Preadditive C]

namespace PushoutCocone

variable {Z Xâ‚ Xâ‚‚ : C} {fâ‚ : Z âŸ¶ Xâ‚} {fâ‚‚ : Z âŸ¶ Xâ‚‚} (c : PushoutCocone fâ‚ fâ‚‚)
  [HasBinaryBiproduct Xâ‚ Xâ‚‚]

@[simps]
noncomputable def shortComplex : ShortComplex C where
  Xâ‚ := Z
  Xâ‚‚ := Xâ‚ âŠ Xâ‚‚
  Xâ‚ƒ := c.pt
  f := biprod.lift fâ‚ (-fâ‚‚)
  g := biprod.desc c.inl c.inr
  zero := by simp [c.condition]

noncomputable def isColimitOfExactShortComplex [Balanced C]
    (hc : c.shortComplex.Exact) (hc' : Epi c.shortComplex.g) :
    IsColimit c :=
  PushoutCocone.isColimitAux _
    (fun s => hc.desc (biprod.desc s.inl s.inr) (by simp [s.condition]))
    (fun s => by simpa using biprod.inl â‰«= hc.g_desc (biprod.desc s.inl s.inr) _)
    (fun s => by simpa using biprod.inr â‰«= hc.g_desc (biprod.desc s.inl s.inr) _)
    (fun s m hm => by
      rw [â† cancel_epi (PushoutCocone.shortComplex c).g, hc.g_desc]
      aesop_cat)


end PushoutCocone

namespace PullbackCone

variable {Xâ‚ Xâ‚‚ Y : C} {fâ‚ : Xâ‚ âŸ¶ Y} {fâ‚‚ : Xâ‚‚ âŸ¶ Y} (c : PullbackCone fâ‚ fâ‚‚)
  [HasBinaryBiproduct Xâ‚ Xâ‚‚]


@[simps]
noncomputable def shortComplex : ShortComplex C where
  Xâ‚ := c.pt
  Xâ‚‚ := Xâ‚ âŠ Xâ‚‚
  Xâ‚ƒ := Y
  f := biprod.lift c.fst c.snd
  g := biprod.desc fâ‚ (-fâ‚‚)
  zero := by simp [c.condition]

lemma exact_shortComplex_of_isLimit (hc : IsLimit c)
    [(shortComplex c).HasHomology] : c.shortComplex.Exact := by
  apply ShortComplex.exact_of_f_is_kernel
  let pullbackCone : âˆ€ {W : C} (Ï† : W âŸ¶ Xâ‚ âŠ Xâ‚‚), Ï† â‰« biprod.desc fâ‚ (-fâ‚‚) = 0 â†’
        PullbackCone fâ‚ fâ‚‚ := fun {W} Ï† hÏ† =>
    PullbackCone.mk (Ï† â‰« biprod.fst) (Ï† â‰« biprod.snd) (by
      rw [â† sub_eq_zero, assoc, assoc, â† Preadditive.comp_sub]
      convert hÏ†
      aesop_cat)
  exact KernelFork.IsLimit.ofÎ¹ _ _
    (fun f hf => hc.lift (pullbackCone f hf))
    (fun f hf => by
      dsimp
      ext <;> simp [pullbackCone])
    (fun f hf m hm => by
      have hmâ‚ := hm =â‰« biprod.fst
      have hmâ‚‚ := hm =â‰« biprod.snd
      dsimp at hmâ‚ hmâ‚‚
      simp only [assoc, biprod.lift_fst, biprod.lift_snd] at hmâ‚ hmâ‚‚
      apply PullbackCone.IsLimit.hom_ext hc
      Â· simp [hmâ‚, pullbackCone]
      Â· simp [hmâ‚‚, pullbackCone])

end PullbackCone

end Limits

namespace Abelian

section

variable {C : Type u} [Category.{v} C] [Abelian C]

section

variable {X Y Z : C} {f : X âŸ¶ Y} [Epi f] {Ï€â‚ Ï€â‚‚ : Z âŸ¶ X}

noncomputable def effectiveEpiStructOfEpiOfIsPushout (hc : IsPushout Ï€â‚ Ï€â‚‚ f f) :
    EffectiveEpiStruct f where
  desc {W} Ï† h := PushoutCocone.IsColimit.desc hc.isColimit Ï† Ï† (h _ _ hc.w)
  fac {W} Ï† h := PushoutCocone.IsColimit.inl_desc hc.isColimit _ _ _
  uniq {W} Ï† h m hm := by
    rw [â† cancel_epi f, hm]
    symm
    apply PushoutCocone.IsColimit.inl_desc hc.isColimit

lemma isPushout_of_isPullback_of_epi (hc : IsPullback Ï€â‚ Ï€â‚‚ f f) :
    IsPushout Ï€â‚ Ï€â‚‚ f f := by
  have hc' : IsColimit (PushoutCocone.mk _ _ hc.w) := by
    apply PushoutCocone.isColimitOfExactShortComplex
    Â· refine' ShortComplex.exact_of_iso _ (PullbackCone.exact_shortComplex_of_isLimit _ hc.isLimit)
      refine' ShortComplex.isoMk (Iso.refl _)
        { hom := biprod.desc biprod.inl (-biprod.inr)
          inv := biprod.desc biprod.inl (-biprod.inr) }
        (Iso.refl _) (by aesop_cat) (by aesop_cat)
    Â· dsimp; infer_instance
  exact IsPushout.of_isColimit hc'

end

lemma effective_epi_of_epi {X Y : C} (f : X âŸ¶ Y) [Epi f] :
    EffectiveEpi f where
  effectiveEpi := âŸ¨effectiveEpiStructOfEpiOfIsPushout
    (isPushout_of_isPullback_of_epi (IsPullback.of_isLimit (pullbackIsPullback f f)))âŸ©

variable (C)

def refinementsTopology :
    GrothendieckTopology C where
  sieves X S := âˆƒ (T : C) (p : T âŸ¶ X) (_ : Epi p), S p
  top_mem' X := âŸ¨X, ğŸ™ X, inferInstance, by simpâŸ©
  pullback_stable' X Y S f hS := by
    obtain âŸ¨T, p, hp, hâŸ© := hS
    refine' âŸ¨pullback f p, pullback.fst, inferInstance, _âŸ©
    dsimp
    rw [pullback.condition]
    apply S.downward_closed h
  transitive' X S hS R hR := by
    obtain âŸ¨T, p, hp, hâŸ© := hS
    obtain âŸ¨U, q, hq, h'âŸ© := hR h
    exact âŸ¨_, q â‰« p, epi_comp _ _, h'âŸ©

instance : Preregular C where
  exists_fac {X Y Z} f g _ := by
    obtain âŸ¨A, Ï€, hÏ€, i, facâŸ© := surjective_up_to_refinements_of_epi g f
    exact âŸ¨A, Ï€, effective_epi_of_epi Ï€, i, fac.symmâŸ©

lemma refinementsTopology_eq_regularTopology :
    refinementsTopology C = regularTopology C := by
  apply le_antisymm
  Â· rintro X S âŸ¨T, p, _, hpâŸ©
    refine (regularTopology C).superset_covering ?_
      (Coverage.saturate.of X _ âŸ¨_, p, rfl, effective_epi_of_epi pâŸ©)
    rw [Sieve.generate_le_iff]
    rintro _ _ âŸ¨âŸ¨âŸ©âŸ©
    exact hp
  Â· apply ((Coverage.gi C).gc _ _).2
    rintro X S âŸ¨Y, s, rfl, _âŸ©
    exact âŸ¨_, s, inferInstance, _, ğŸ™ _, s, âŸ¨Unit.unitâŸ©, by simpâŸ©

lemma refinementsTopology_subcanonical :
    Sheaf.Subcanonical (refinementsTopology C) := by
  rw [refinementsTopology_eq_regularTopology]
  exact regularCoverage.subcanonical

end

section EpiIffLocallySurjective

variable {C : Type u} [SmallCategory C] [Abelian C]
  {X Y : C} (f : X âŸ¶ Y)

lemma epi_iff_isLocallySurjective_yoneda_map :
    Epi f â†” Sheaf.IsLocallySurjective ((refinementsTopology_subcanonical C).yoneda.map f) := by
  rw [epi_iff_surjective_up_to_refinements f]
  constructor
  Â· intro hf
    refine' âŸ¨fun {U} (y : U.unop âŸ¶ Y) => _âŸ©
    obtain âŸ¨A', Ï€, hÏ€, x, facâŸ© := hf y
    dsimp
    exact âŸ¨A', Ï€, hÏ€, x, fac.symmâŸ©
  Â· intro hf
    intro A y
    obtain âŸ¨A', Ï€, hÏ€, x, facâŸ© := Presheaf.imageSieve_mem (refinementsTopology C)
      ((refinementsTopology_subcanonical C).yoneda.map f).val y
    exact âŸ¨A', Ï€, hÏ€, x, fac.symmâŸ©

end EpiIffLocallySurjective

end Abelian

end CategoryTheory
