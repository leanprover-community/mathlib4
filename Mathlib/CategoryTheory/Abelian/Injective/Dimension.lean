/-
Copyright (c) 2025 Nailin Guan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nailin Guan
-/
import Mathlib.Algebra.Homology.DerivedCategory.Ext.EnoughInjectives
import Mathlib.CategoryTheory.Abelian.Exact
import Mathlib.Data.ENat.Lattice

/-!
# Injective dimension

In an abelian category `C`, we shall say that `X : C` has Injective dimension `< n`
if all `Ext Y X i` vanish when `n ‚â§ i`. This defines a type class
`HasInjectiveDimensionLT X n`. We also define a type class
`HasInjectiveDimensionLE X n` as an abbreviation for
`HasInjectiveDimensionLT X (n + 1)`.
(Note that the fact that `X` is a zero object is equivalent to the condition
`HasInjectiveDimensionLT X 0`, but this cannot be expressed in terms of
`HasInjectiveDimensionLE`.)

We also define the Injective dimension in `WithBot ‚Ñï‚àû` as `injectiveDimension`,
`injectiveDimension X = ‚ä•` iff `X` is zero and acts in common sense in the non-negative values.

-/

universe w v u

namespace CategoryTheory

open Abelian Limits ZeroObject

variable {C : Type u} [Category.{v} C] [Abelian C]

/-- An object `X` in an abelian category has Injective dimension `< n` if
all `Ext X Y i` vanish when `n ‚â§ i`. See also `HasInjectiveDimensionLE`.
(Do not use the `subsingleton'` field directly. Use the constructor
`HasInjectiveDimensionLT.mk`, and the lemmas `hasInjectiveDimensionLT_iff` and
`Ext.eq_zero_of_hasInjectiveDimensionLT`.) -/
class HasInjectiveDimensionLT (X : C) (n : ‚Ñï) : Prop where mk' ::
  subsingleton' (i : ‚Ñï) (hi : n ‚â§ i) ‚¶ÉY : C‚¶Ñ :
    letI := HasExt.standard C
    Subsingleton (Ext.{max u v} Y X i)

/-- An object `X` in an abelian category has Injective dimension `‚â§ n` if
all `Ext X Y i` vanish when `n + 1 ‚â§ i` -/
abbrev HasInjectiveDimensionLE (X : C) (n : ‚Ñï) : Prop :=
  HasInjectiveDimensionLT X (n + 1)

namespace HasInjectiveDimensionLT

variable [HasExt.{w} C] (X : C) (n : ‚Ñï)

lemma subsingleton [hX : HasInjectiveDimensionLT X n] (i : ‚Ñï) (hi : n ‚â§ i) (Y : C) :
    Subsingleton (Ext.{w} Y X i) := by
  letI := HasExt.standard C
  have := hX.subsingleton' i hi
  exact Ext.chgUniv.{w, max u v}.symm.subsingleton

variable {X n} in
lemma mk (hX : ‚àÄ (i : ‚Ñï) (_ : n ‚â§ i) ‚¶ÉY : C‚¶Ñ, ‚àÄ (e : Ext Y X i), e = 0) :
    HasInjectiveDimensionLT X n where
  subsingleton' i hi Y := by
    have : Subsingleton (Ext Y X i) := ‚ü®fun e‚ÇÅ e‚ÇÇ ‚Ü¶ by simp only [hX i hi]‚ü©
    letI := HasExt.standard C
    exact Ext.chgUniv.{max u v, w}.symm.subsingleton

end HasInjectiveDimensionLT

lemma Abelian.Ext.eq_zero_of_hasInjectiveDimensionLT [HasExt.{w} C]
    {X Y : C} {i : ‚Ñï} (e : Ext Y X i) (n : ‚Ñï) [HasInjectiveDimensionLT X n]
    (hi : n ‚â§ i) : e = 0 :=
  (HasInjectiveDimensionLT.subsingleton X n i hi Y).elim _ _

section

variable (X : C) (n : ‚Ñï)

lemma hasInjectiveDimensionLT_iff [HasExt.{w} C] :
    HasInjectiveDimensionLT X n ‚Üî
      ‚àÄ (i : ‚Ñï) (_ : n ‚â§ i) ‚¶ÉY : C‚¶Ñ, ‚àÄ (e : Ext Y X i), e = 0 :=
  ‚ü®fun _ _ hi _ e ‚Ü¶ e.eq_zero_of_hasInjectiveDimensionLT n hi,
    HasInjectiveDimensionLT.mk‚ü©

variable {X} in
lemma Limits.IsZero.hasInjectiveDimensionLT_zero (hX : IsZero X) :
    HasInjectiveDimensionLT X 0 := by
  letI := HasExt.standard C
  rw [hasInjectiveDimensionLT_iff]
  intro i hi Y e
  rw [‚Üê e.comp_mk‚ÇÄ_id, hX.eq_zero_of_tgt (ùüô X), Ext.mk‚ÇÄ_zero, Ext.comp_zero]

instance : HasInjectiveDimensionLT (0 : C) 0 :=
  (isZero_zero C).hasInjectiveDimensionLT_zero

lemma isZero_of_hasInjectiveDimensionLT_zero [HasInjectiveDimensionLT X 0] : IsZero X := by
  letI := HasExt.standard C
  rw [IsZero.iff_id_eq_zero]
  apply Ext.homEquiv‚ÇÄ.symm.injective
  simpa only [Ext.homEquiv‚ÇÄ_symm_apply, Ext.mk‚ÇÄ_zero]
    using Abelian.Ext.eq_zero_of_hasInjectiveDimensionLT _ 0 (by rfl)

lemma hasInjectiveDimensionLT_zero_iff_isZero : HasInjectiveDimensionLT X 0 ‚Üî IsZero X :=
  ‚ü®fun _ ‚Ü¶ isZero_of_hasInjectiveDimensionLT_zero X, fun h ‚Ü¶ h.hasInjectiveDimensionLT_zero‚ü©

lemma hasInjectiveDimensionLT_of_ge (m : ‚Ñï) (h : n ‚â§ m)
    [HasInjectiveDimensionLT X n] :
    HasInjectiveDimensionLT X m := by
  letI := HasExt.standard C
  rw [hasInjectiveDimensionLT_iff]
  intro i hi Y e
  exact e.eq_zero_of_hasInjectiveDimensionLT n (by cutsat)

instance [HasInjectiveDimensionLT X n] (k : ‚Ñï) :
    HasInjectiveDimensionLT X (n + k) :=
  hasInjectiveDimensionLT_of_ge X n (n + k) (by cutsat)

instance [HasInjectiveDimensionLT X n] (k : ‚Ñï) :
    HasInjectiveDimensionLT X (k + n) :=
  hasInjectiveDimensionLT_of_ge X n (k + n) (by cutsat)

instance [HasInjectiveDimensionLT X n] :
    HasInjectiveDimensionLT X n.succ :=
  inferInstanceAs (HasInjectiveDimensionLT X (n + 1))

instance [Injective X] : HasInjectiveDimensionLT X 1 := by
  letI := HasExt.standard C
  rw [hasInjectiveDimensionLT_iff]
  intro i hi Y e
  obtain _ | i := i
  ¬∑ simp at hi
  ¬∑ exact e.eq_zero_of_injective

lemma injective_iff_subsingleton_ext_one [HasExt.{w} C] {X : C} :
    Injective X ‚Üî ‚àÄ ‚¶ÉY : C‚¶Ñ, Subsingleton (Ext Y X 1) := by
  refine ‚ü®fun h ‚Ü¶ HasInjectiveDimensionLT.subsingleton X 1 1 (by rfl),
    fun h ‚Ü¶ ‚ü®fun f g _ ‚Ü¶ ?_‚ü©‚ü©
  obtain ‚ü®œÜ, hœÜ‚ü© := Ext.contravariant_sequence_exact‚ÇÅ { exact := ShortComplex.exact_cokernel g } _
    (Ext.mk‚ÇÄ f) (zero_add 1) (by subsingleton)
  obtain ‚ü®œÜ, rfl‚ü© := Ext.homEquiv‚ÇÄ.symm.surjective œÜ
  exact ‚ü®œÜ, Ext.homEquiv‚ÇÄ.symm.injective (by simpa using hœÜ)‚ü©

lemma injective_iff_hasInjectiveDimensionLT_one (X : C) :
    Injective X ‚Üî HasInjectiveDimensionLT X 1 := by
  letI := HasExt.standard C
  exact ‚ü®fun _ ‚Ü¶ inferInstance, fun _ ‚Ü¶ injective_iff_subsingleton_ext_one.2
    (HasInjectiveDimensionLT.subsingleton X 1 1 (by rfl))‚ü©

end

lemma Retract.hasInjectiveDimensionLT {X Y : C} (h : Retract X Y) (n : ‚Ñï)
    [HasInjectiveDimensionLT Y n] :
    HasInjectiveDimensionLT X n := by
  letI := HasExt.standard C
  rw [hasInjectiveDimensionLT_iff]
  intro i hi T x
  rw [‚Üê x.comp_mk‚ÇÄ_id, ‚Üê h.retract, ‚Üê Ext.mk‚ÇÄ_comp_mk‚ÇÄ, ‚Üê Ext.comp_assoc_of_second_deg_zero,
    (x.comp (Ext.mk‚ÇÄ h.i) (add_zero i)).eq_zero_of_hasInjectiveDimensionLT n hi, Ext.zero_comp]

lemma hasInjectiveDimensionLT_of_iso {X X' : C} (e : X ‚âÖ X') (n : ‚Ñï)
    [HasInjectiveDimensionLT X n] :
    HasInjectiveDimensionLT X' n :=
  e.symm.retract.hasInjectiveDimensionLT n

namespace ShortComplex

namespace ShortExact

variable {S : ShortComplex C} (hS : S.ShortExact) (n : ‚Ñï)
include hS

-- In the following lemmas, the parameters `HasInjectiveDimensionLT` are
-- explicit as it is unlikely we may infer them, unless the short complex `S`
-- was declared reducible

lemma hasInjectiveDimensionLT_X‚ÇÇ (h‚ÇÅ : HasInjectiveDimensionLT S.X‚ÇÅ n)
    (h‚ÇÉ : HasInjectiveDimensionLT S.X‚ÇÉ n) :
    HasInjectiveDimensionLT S.X‚ÇÇ n := by
  letI := HasExt.standard C
  rw [hasInjectiveDimensionLT_iff]
  intro i hi Y x‚ÇÇ
  obtain ‚ü®x‚ÇÉ, rfl‚ü© := Ext.covariant_sequence_exact‚ÇÇ _ hS x‚ÇÇ
    (Ext.eq_zero_of_hasInjectiveDimensionLT _ n hi)
  rw [x‚ÇÉ.eq_zero_of_hasInjectiveDimensionLT n hi, Ext.zero_comp]

lemma hasInjectiveDimensionLT_X‚ÇÅ (h‚ÇÅ : HasInjectiveDimensionLT S.X‚ÇÉ n)
    (h‚ÇÇ : HasInjectiveDimensionLT S.X‚ÇÇ (n + 1)) :
    HasInjectiveDimensionLT S.X‚ÇÅ (n + 1) := by
  letI := HasExt.standard C
  rw [hasInjectiveDimensionLT_iff]
  rintro (_ | i) hi Y x‚ÇÉ
  ¬∑ simp at hi
  ¬∑ obtain ‚ü®x‚ÇÅ, rfl‚ü© := Ext.covariant_sequence_exact‚ÇÅ _ hS x‚ÇÉ
      (Ext.eq_zero_of_hasInjectiveDimensionLT _ (n + 1) hi) rfl
    rw [x‚ÇÅ.eq_zero_of_hasInjectiveDimensionLT n (by cutsat), Ext.zero_comp]

lemma hasInjectiveDimensionLT_X‚ÇÉ (h‚ÇÇ : HasInjectiveDimensionLT S.X‚ÇÇ n)
    (h‚ÇÉ : HasInjectiveDimensionLT S.X‚ÇÅ (n + 1)) :
    HasInjectiveDimensionLT S.X‚ÇÉ n := by
  letI := HasExt.standard C
  rw [hasInjectiveDimensionLT_iff]
  intro i hi Y x‚ÇÅ
  obtain ‚ü®x‚ÇÇ, rfl‚ü© := Ext.covariant_sequence_exact‚ÇÉ _ hS x‚ÇÅ (add_comm _ _)
    (Ext.eq_zero_of_hasInjectiveDimensionLT _ (n + 1) (by cutsat))
  rw [x‚ÇÇ.eq_zero_of_hasInjectiveDimensionLT n (by cutsat), Ext.zero_comp]

lemma hasInjectiveDimensionLT_X‚ÇÉ_iff (n : ‚Ñï) (h‚ÇÇ : Injective S.X‚ÇÇ) :
    HasInjectiveDimensionLT S.X‚ÇÉ (n + 1) ‚Üî HasInjectiveDimensionLT S.X‚ÇÅ (n + 2) :=
  ‚ü®fun _ ‚Ü¶ hS.hasInjectiveDimensionLT_X‚ÇÅ (n + 1) inferInstance inferInstance,
    fun _ ‚Ü¶ hS.hasInjectiveDimensionLT_X‚ÇÉ (n + 1) inferInstance inferInstance‚ü©

end ShortExact

end ShortComplex

instance (X Y : C) (n : ‚Ñï) [HasInjectiveDimensionLT X n]
    [HasInjectiveDimensionLT Y n] :
    HasInjectiveDimensionLT (X ‚äû Y) n :=
  (ShortComplex.Splitting.ofHasBinaryBiproduct X Y).shortExact.hasInjectiveDimensionLT_X‚ÇÇ n
    (by assumption) (by assumption)

end CategoryTheory

section InjectiveDimension

namespace CategoryTheory

variable {C : Type u} [Category.{v, u} C] [Abelian C]

/-- The injective dimension of an object in an abelian category. -/
noncomputable def injectiveDimension (X : C) : WithBot ‚Ñï‚àû :=
  sInf {n : WithBot ‚Ñï‚àû | ‚àÄ (i : ‚Ñï), n < i ‚Üí HasInjectiveDimensionLT X i}

lemma injectiveDimension_eq_of_iso {X Y : C} (e : X ‚âÖ Y) :
    injectiveDimension X = injectiveDimension Y := by
  simp only [injectiveDimension]
  congr! 5
  exact ‚ü®fun h ‚Ü¶ hasInjectiveDimensionLT_of_iso e _,
    fun h ‚Ü¶ hasInjectiveDimensionLT_of_iso e.symm _‚ü©

lemma Retract.injectiveDimension_le {X Y : C} (h : Retract X Y) :
    injectiveDimension X ‚â§ injectiveDimension Y :=
  sInf_le_sInf_of_subset_insert_top (fun n hn ‚Ü¶ by
    simp only [Set.mem_setOf_eq, not_top_lt, IsEmpty.forall_iff, implies_true,
      Set.insert_eq_of_mem] at hn ‚ä¢
    intro i hi
    have := hn i hi
    exact h.hasInjectiveDimensionLT i)

lemma injectiveDimension_lt_iff {X : C} {n : ‚Ñï} :
    injectiveDimension X < n ‚Üî HasInjectiveDimensionLT X n := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ sInf_lt_iff.2 ?_‚ü©
  ¬∑ have : injectiveDimension X ‚àà _ := csInf_mem ‚ü®‚ä§, by simp‚ü©
    simp only [Set.mem_setOf_eq] at this
    exact this _ h
  ¬∑ obtain _ | n := n
    ¬∑ exact ‚ü®‚ä•, fun _ _ ‚Ü¶ hasInjectiveDimensionLT_of_ge _ 0 _ (by simp), by decide‚ü©
    ¬∑ exact ‚ü®n, fun i hi ‚Ü¶ hasInjectiveDimensionLT_of_ge _ (n + 1) _ (by simpa using hi),
        by simp [WithBot.lt_add_one_iff]‚ü©

lemma injectiveDimension_le_iff (X : C) (n : ‚Ñï) :
    injectiveDimension X ‚â§ n ‚Üî HasInjectiveDimensionLE X n := by
  simp [‚Üê injectiveDimension_lt_iff, ‚Üê WithBot.lt_add_one_iff]

lemma injectiveDimension_ge_iff (X : C) (n : ‚Ñï) :
    n ‚â§ injectiveDimension X ‚Üî ¬¨ HasInjectiveDimensionLT X n := by
  rw [‚Üê not_iff_not, not_le, not_not, injectiveDimension_lt_iff]

lemma injectiveDimension_eq_bot_iff (X : C) :
    injectiveDimension X = ‚ä• ‚Üî Limits.IsZero X := by
  rw [‚Üê hasInjectiveDimensionLT_zero_iff_isZero, ‚Üê injectiveDimension_lt_iff,
    Nat.cast_zero, ‚Üê WithBot.lt_coe_bot, bot_eq_zero', WithBot.coe_zero]

lemma injectiveDimension_ne_top_iff (X : C) :
    injectiveDimension X ‚â† ‚ä§ ‚Üî ‚àÉ n, HasInjectiveDimensionLE X n := by
  generalize hd : injectiveDimension X = d
  induction d with
  | bot =>
    simp only [ne_eq, bot_ne_top, not_false_eq_true, true_iff]
    exact ‚ü®0, by simp [‚Üê injectiveDimension_le_iff, hd]‚ü©
  | coe d =>
    induction d with
    | top =>
      by_contra!
      simp only [WithBot.coe_top, ne_eq, not_true_eq_false, false_and, true_and, false_or] at this
      obtain ‚ü®n, hn‚ü© := this
      rw [‚Üê injectiveDimension_le_iff, hd, WithBot.coe_top, top_le_iff] at hn
      exact ENat.coe_ne_top _ ((WithBot.coe_eq_coe).1 hn)
    | coe d =>
      simp only [ne_eq, WithBot.coe_eq_top, ENat.coe_ne_top, not_false_eq_true, true_iff]
      exact ‚ü®d, by simpa only [‚Üê injectiveDimension_le_iff] using hd.le‚ü©

end CategoryTheory

end InjectiveDimension
