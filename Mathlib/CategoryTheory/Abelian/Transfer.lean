/-
Copyright (c) 2022 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.Algebra.Equiv.TransferInstance
import Mathlib.CategoryTheory.Abelian.Basic
import Mathlib.CategoryTheory.Adjunction.Limits
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Kernels
import Mathlib.CategoryTheory.Preadditive.AdditiveFunctor
import Mathlib.CategoryTheory.Preadditive.Transfer

/-!
# Transferring "abelian-ness" across a functor

If `C` is an additive category, `D` is an abelian category,
we have `F : C ‚•§ D` `G : D ‚•§ C` (both preserving zero morphisms),
`G` is left exact (that is, preserves finite limits),
and further we have `adj : G ‚ä£ F` and `i : F ‚ãô G ‚âÖ ùü≠ C`,
then `C` is also abelian.

A particular example is the transfer of `Abelian` instances from a category `C` to `ShrinkHoms C`;
see `ShrinkHoms.abelian`. In this case, we also transfer the `Preadditive` structure.

See <https://stacks.math.columbia.edu/tag/03A3>

## Notes
The hypotheses, following the statement from the Stacks project,
may appear surprising: we don't ask that the counit of the adjunction is an isomorphism,
but just that we have some potentially unrelated isomorphism `i : F ‚ãô G ‚âÖ ùü≠ C`.

However Lemma A1.1.1 from [Elephant] shows that in this situation the counit itself
must be an isomorphism, and thus that `C` is a reflective subcategory of `D`.

Someone may like to formalize that lemma, and restate this theorem in terms of `Reflective`.
(That lemma has a nice string diagrammatic proof that holds in any bicategory.)
-/


noncomputable section

namespace CategoryTheory

open Limits

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace AbelianOfAdjunction

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] [Preadditive C]
variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] [Abelian D]
variable (F : C ‚•§ D)
variable (G : D ‚•§ C) [Functor.PreservesZeroMorphisms G]

/-- No point making this an instance, as it requires `i`. -/
theorem hasKernels [PreservesFiniteLimits G] (i : F ‚ãô G ‚âÖ ùü≠ C) : HasKernels C :=
  { has_limit := fun f => by
      have := NatIso.naturality_1 i f
      simp? at this says
        simp only [Functor.id_obj, Functor.comp_obj, Functor.comp_map, Functor.id_map] at this
      rw [‚Üê this]
      haveI : HasKernel (G.map (F.map f) ‚â´ i.hom.app _) := Limits.hasKernel_comp_mono _ _
      apply Limits.hasKernel_iso_comp }

/-- No point making this an instance, as it requires `i` and `adj`. -/
theorem hasCokernels (i : F ‚ãô G ‚âÖ ùü≠ C) (adj : G ‚ä£ F) : HasCokernels C :=
  { has_colimit := fun f => by
      have : PreservesColimits G := adj.leftAdjoint_preservesColimits
      have := NatIso.naturality_1 i f
      simp? at this says
        simp only [Functor.id_obj, Functor.comp_obj, Functor.comp_map, Functor.id_map] at this
      rw [‚Üê this]
      haveI : HasCokernel (G.map (F.map f) ‚â´ i.hom.app _) := Limits.hasCokernel_comp_iso _ _
      apply Limits.hasCokernel_epi_comp }

variable [Limits.HasCokernels C]

/-- Auxiliary construction for `coimageIsoImage` -/
def cokernelIso (i : F ‚ãô G ‚âÖ ùü≠ C) (adj : G ‚ä£ F) {X Y : C} (f : X ‚ü∂ Y) :
    G.obj (cokernel (F.map f)) ‚âÖ cokernel f := by
  -- We have to write an explicit `PreservesColimits` type here,
  -- as `leftAdjointPreservesColimits` has universe variables.
  have : PreservesColimits G := adj.leftAdjoint_preservesColimits
  calc
    G.obj (cokernel (F.map f)) ‚âÖ cokernel (G.map (F.map f)) :=
      (asIso (cokernelComparison _ G)).symm
    _ ‚âÖ cokernel (i.hom.app X ‚â´ f ‚â´ i.inv.app Y) := cokernelIsoOfEq (NatIso.naturality_2 i f).symm
    _ ‚âÖ cokernel (f ‚â´ i.inv.app Y) := cokernelEpiComp (i.hom.app X) (f ‚â´ i.inv.app Y)
    _ ‚âÖ cokernel f := cokernelCompIsIso f (i.inv.app Y)

variable [Limits.HasKernels C] [PreservesFiniteLimits G]

/-- Auxiliary construction for `coimageIsoImage` -/
def coimageIsoImageAux (i : F ‚ãô G ‚âÖ ùü≠ C) (adj : G ‚ä£ F) {X Y : C} (f : X ‚ü∂ Y) :
    kernel (G.map (cokernel.œÄ (F.map f))) ‚âÖ kernel (cokernel.œÄ f) := by
  have : PreservesColimits G := adj.leftAdjoint_preservesColimits
  calc
    kernel (G.map (cokernel.œÄ (F.map f))) ‚âÖ
        kernel (cokernel.œÄ (G.map (F.map f)) ‚â´ cokernelComparison (F.map f) G) :=
      kernelIsoOfEq (œÄ_comp_cokernelComparison _ _).symm
    _ ‚âÖ kernel (cokernel.œÄ (G.map (F.map f))) := kernelCompMono _ _
    _ ‚âÖ kernel (cokernel.œÄ (_ ‚â´ f ‚â´ _) ‚â´ (cokernelIsoOfEq _).hom) :=
      (kernelIsoOfEq (œÄ_comp_cokernelIsoOfEq_hom (NatIso.naturality_2 i f)).symm)
    _ ‚âÖ kernel (cokernel.œÄ (_ ‚â´ f ‚â´ _)) := kernelCompMono _ _
    _ ‚âÖ kernel (cokernel.œÄ (f ‚â´ i.inv.app Y) ‚â´ (cokernelEpiComp (i.hom.app X) _).inv) :=
      (kernelIsoOfEq (by simp only [cokernel.œÄ_desc, cokernelEpiComp_inv]))
    _ ‚âÖ kernel (cokernel.œÄ (f ‚â´ _)) := kernelCompMono _ _
    _ ‚âÖ kernel (inv (i.inv.app Y) ‚â´ cokernel.œÄ f ‚â´ (cokernelCompIsIso f (i.inv.app Y)).inv) :=
      (kernelIsoOfEq
        (by simp only [cokernel.œÄ_desc, cokernelCompIsIso_inv, Iso.hom_inv_id_app_assoc,
          NatIso.inv_inv_app]))
    _ ‚âÖ kernel (cokernel.œÄ f ‚â´ _) := kernelIsIsoComp _ _
    _ ‚âÖ kernel (cokernel.œÄ f) := kernelCompMono _ _

variable [Functor.PreservesZeroMorphisms F]

/-- Auxiliary definition: the abelian coimage and abelian image agree.
We still need to check that this agrees with the canonical morphism.
-/
def coimageIsoImage (i : F ‚ãô G ‚âÖ ùü≠ C) (adj : G ‚ä£ F) {X Y : C} (f : X ‚ü∂ Y) :
    Abelian.coimage f ‚âÖ Abelian.image f := by
  have : PreservesLimits F := adj.rightAdjoint_preservesLimits
  calc
    Abelian.coimage f ‚âÖ cokernel (kernel.Œπ f) := Iso.refl _
    _ ‚âÖ G.obj (cokernel (F.map (kernel.Œπ f))) := (cokernelIso _ _ i adj _).symm
    _ ‚âÖ G.obj (cokernel (kernelComparison f F ‚â´ kernel.Œπ (F.map f))) :=
      (G.mapIso (cokernelIsoOfEq (by simp)))
    _ ‚âÖ G.obj (cokernel (kernel.Œπ (F.map f))) := G.mapIso (cokernelEpiComp _ _)
    _ ‚âÖ G.obj (Abelian.coimage (F.map f)) := Iso.refl _
    _ ‚âÖ G.obj (Abelian.image (F.map f)) := G.mapIso (Abelian.coimageIsoImage _)
    _ ‚âÖ G.obj (kernel (cokernel.œÄ (F.map f))) := Iso.refl _
    _ ‚âÖ kernel (G.map (cokernel.œÄ (F.map f))) := PreservesKernel.iso _ _
    _ ‚âÖ kernel (cokernel.œÄ f) := coimageIsoImageAux F G i adj f
    _ ‚âÖ Abelian.image f := Iso.refl _

-- The account of this proof in the Stacks project omits this calculation.
theorem coimageIsoImage_hom (i : F ‚ãô G ‚âÖ ùü≠ C) (adj : G ‚ä£ F) {X Y : C} (f : X ‚ü∂ Y) :
    (coimageIsoImage F G i adj f).hom = Abelian.coimageImageComparison f := by
  dsimp [coimageIsoImage, cokernelIso, cokernelEpiComp, cokernelCompIsIso_inv,
    coimageIsoImageAux, kernelCompMono]
  simpa only [‚Üê cancel_mono (Abelian.image.Œπ f), ‚Üê cancel_epi (Abelian.coimage.œÄ f),
    Category.assoc, Category.id_comp, cokernel.œÄ_desc_assoc,
    œÄ_comp_cokernelIsoOfEq_inv_assoc, PreservesKernel.iso_hom,
    œÄ_comp_cokernelComparison_assoc, ‚Üê G.map_comp_assoc, kernel.lift_Œπ,
    Abelian.coimage_image_factorisation, lift_comp_kernelIsoOfEq_hom_assoc,
    kernelIsIsoComp_hom, kernel.lift_Œπ_assoc, kernelIsoOfEq_hom_comp_Œπ_assoc,
    kernelComparison_comp_Œπ_assoc, œÄ_comp_cokernelIsoOfEq_hom_assoc,
    asIso_hom, NatIso.inv_inv_app] using NatIso.naturality_1 i f

end AbelianOfAdjunction

open AbelianOfAdjunction

/-- If `C` is an additive category, `D` is an abelian category,
we have `F : C ‚•§ D` `G : D ‚•§ C` (both preserving zero morphisms),
`G` is left exact (that is, preserves finite limits),
and further we have `adj : G ‚ä£ F` and `i : F ‚ãô G ‚âÖ ùü≠ C`,
then `C` is also abelian. -/
@[stacks 03A3]
def abelianOfAdjunction {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] [Preadditive C] [HasFiniteProducts C]
    {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] [Abelian D] (F : C ‚•§ D) [Functor.PreservesZeroMorphisms F]
    (G : D ‚•§ C) [Functor.PreservesZeroMorphisms G] [PreservesFiniteLimits G] (i : F ‚ãô G ‚âÖ ùü≠ C)
    (adj : G ‚ä£ F) : Abelian C := by
  haveI := hasKernels F G i
  haveI := hasCokernels F G i adj
  have : ‚àÄ {X Y : C} (f : X ‚ü∂ Y), IsIso (Abelian.coimageImageComparison f) := by
    intro X Y f
    rw [‚Üê coimageIsoImage_hom F G i adj f]
    infer_instance
  apply Abelian.ofCoimageImageComparisonIsIso

/-- If `C` is an additive category equivalent to an abelian category `D`
via a functor that preserves zero morphisms,
then `C` is also abelian.
-/
def abelianOfEquivalence {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] [Preadditive C] [HasFiniteProducts C]
    {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] [Abelian D] (F : C ‚•§ D) [Functor.PreservesZeroMorphisms F]
    [F.IsEquivalence] : Abelian C :=
  abelianOfAdjunction F F.inv F.asEquivalence.unitIso.symm F.asEquivalence.symm.toAdjunction

namespace ShrinkHoms

universe w

variable {C : Type*} [Category C] [LocallySmall.{w} C]

section Preadditive

variable [Preadditive C]

noncomputable instance homGroup (P Q : ShrinkHoms C) : AddCommGroup (P ‚ü∂ Q : Type w) :=
  Equiv.addCommGroup (equivShrink _).symm

lemma functor_map_add {P Q : C} (f g : P ‚ü∂ Q) :
    (functor C).map (f + g) =
      (functor C).map f + (functor C).map g := by
  exact map_add (equivShrink.{w} (P ‚ü∂ Q)).symm.addEquiv.symm f g

lemma inverse_map_add {P Q : ShrinkHoms C} (f g : P ‚ü∂ Q) :
    (inverse C).map (f + g) =
      (inverse C).map f + (ShrinkHoms.inverse C).map g :=
  map_add (equivShrink.{w} (P.fromShrinkHoms ‚ü∂ Q.fromShrinkHoms)).symm.addEquiv f g

variable (C)

noncomputable instance preadditive : Preadditive.{w} (ShrinkHoms C) :=
  .ofFullyFaithful (equivalence C).fullyFaithfulInverse

instance : (functor C).Additive :=
  inferInstanceAs (equivalence C).symm.inverse.Additive

instance hasLimitsOfShape (J : Type*) [Category J]
    [HasLimitsOfShape J C] : HasLimitsOfShape.{_, _, w} J (ShrinkHoms C) :=
  Adjunction.hasLimitsOfShape_of_equivalence (inverse C)

instance hasFiniteLimits [HasFiniteLimits C] :
    HasFiniteLimits.{w} (ShrinkHoms C) := ‚ü®fun _ => inferInstance‚ü©

end Preadditive

variable (C) in
noncomputable instance abelian [Abelian C] :
    Abelian.{w} (ShrinkHoms C) := abelianOfEquivalence (inverse C)

end ShrinkHoms

end CategoryTheory
