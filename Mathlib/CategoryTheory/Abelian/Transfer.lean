/-
Copyright (c) 2022 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Kernels
import Mathlib.CategoryTheory.Adjunction.Limits
import Mathlib.CategoryTheory.Abelian.Exact
import Mathlib.CategoryTheory.Abelian.Injective

#align_import category_theory.abelian.transfer from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# Transferring properties across a functor

## abelian-ness
If `C` is an additive category, `D` is an abelian category,
we have `F : C â¥¤ D` `G : D â¥¤ C` (both preserving zero morphisms),
`G` is left exact (that is, preserves finite limits),
and further we have `adj : G âŠ£ F` and `i : F â‹™ G â‰… ğŸ­ C`,
then `C` is also abelian.

See <https://stacks.math.columbia.edu/tag/03A3>

### Notes
The hypotheses, following the statement from the Stacks project,
may appear surprising: we don't ask that the counit of the adjunction is an isomorphism,
but just that we have some potentially unrelated isomorphism `i : F â‹™ G â‰… ğŸ­ C`.

However Lemma A1.1.1 from [Elephant] shows that in this situation the counit itself
must be an isomorphism, and thus that `C` is a reflective subcategory of `D`.

Someone may like to formalize that lemma, and restate this theorem in terms of `Reflective`.
(That lemma has a nice string diagrammatic proof that holds in any bicategory.)

## enough-injectives
If `C, D` are abelian categories with adjoint functors `L âŠ£ R` where `L` is a faithful exact
functor from `C` to `D`, then `D` having enough injectives implies that `C` has enough injectives.

### Notes

In `EnoughInjectives.of_equivalence`, if we require `C` and `D` to have morphisms at the same
universe level, then it suffices to assume only `abelian C`, since `abelian D` would be implied by
`abelian_of_adjunction`. Maybe "transferring abelian-ness" should have a more relaxed universe
level?

-/


noncomputable section

namespace CategoryTheory

open Limits

universe v vâ‚ vâ‚‚ uâ‚ uâ‚‚

namespace AbelianOfAdjunction

variable {C : Type uâ‚} [Category.{v} C] [Preadditive C]

variable {D : Type uâ‚‚} [Category.{v} D] [Abelian D]

variable (F : C â¥¤ D)

variable (G : D â¥¤ C) [Functor.PreservesZeroMorphisms G]

variable (i : F â‹™ G â‰… ğŸ­ C) (adj : G âŠ£ F)

/-- No point making this an instance, as it requires `i`. -/
theorem hasKernels [PreservesFiniteLimits G] : HasKernels C :=
  { has_limit := fun f => by
      have := NatIso.naturality_1 i f
      simp at this
      rw [â† this]
      haveI : HasKernel (G.map (F.map f) â‰« i.hom.app _) := Limits.hasKernel_comp_mono _ _
      apply Limits.hasKernel_iso_comp }
#align category_theory.abelian_of_adjunction.has_kernels CategoryTheory.AbelianOfAdjunction.hasKernels

/-- No point making this an instance, as it requires `i` and `adj`. -/
theorem hasCokernels : HasCokernels C :=
  { has_colimit := fun f => by
      have : PreservesColimits G := adj.leftAdjointPreservesColimits
      have := NatIso.naturality_1 i f
      simp at this
      rw [â† this]
      haveI : HasCokernel (G.map (F.map f) â‰« i.hom.app _) := Limits.hasCokernel_comp_iso _ _
      apply Limits.hasCokernel_epi_comp }
#align category_theory.abelian_of_adjunction.has_cokernels CategoryTheory.AbelianOfAdjunction.hasCokernels

variable [Limits.HasCokernels C]

/-- Auxiliary construction for `coimageIsoImage` -/
def cokernelIso {X Y : C} (f : X âŸ¶ Y) : G.obj (cokernel (F.map f)) â‰… cokernel f := by
  -- We have to write an explicit `PreservesColimits` type here,
  -- as `leftAdjointPreservesColimits` has universe variables.
  have : PreservesColimits G := adj.leftAdjointPreservesColimits
  -- porting note: the next `have` has been added, otherwise some instance were not found
  have : âˆ€ (X' Y' : C) (f' : X' âŸ¶ Y'), HasCokernel f' := inferInstance
  calc
    G.obj (cokernel (F.map f)) â‰… cokernel (G.map (F.map f)) :=
      (asIso (cokernelComparison _ G)).symm
    _ â‰… cokernel (i.hom.app X â‰« f â‰« i.inv.app Y) := cokernelIsoOfEq (NatIso.naturality_2 i f).symm
    _ â‰… cokernel (f â‰« i.inv.app Y) := cokernelEpiComp (i.hom.app X) (f â‰« i.inv.app Y)
    _ â‰… cokernel f := cokernelCompIsIso f (i.inv.app Y)
#align category_theory.abelian_of_adjunction.cokernel_iso CategoryTheory.AbelianOfAdjunction.cokernelIso

variable [Limits.HasKernels C] [PreservesFiniteLimits G]

/-- Auxiliary construction for `coimageIsoImage` -/
def coimageIsoImageAux {X Y : C} (f : X âŸ¶ Y) :
    kernel (G.map (cokernel.Ï€ (F.map f))) â‰… kernel (cokernel.Ï€ f) := by
  have : PreservesColimits G := adj.leftAdjointPreservesColimits
  -- porting note: the next `have` has been added, otherwise some instance were not found
  have : âˆ€ (X' Y' : C) (f' : X' âŸ¶ Y'), HasCokernel f' := inferInstance
  calc
    kernel (G.map (cokernel.Ï€ (F.map f))) â‰…
        kernel (cokernel.Ï€ (G.map (F.map f)) â‰« cokernelComparison (F.map f) G) :=
      kernelIsoOfEq (Ï€_comp_cokernelComparison _ _).symm
    _ â‰… kernel (cokernel.Ï€ (G.map (F.map f))) := (kernelCompMono _ _)
    _ â‰… kernel (cokernel.Ï€ (_ â‰« f â‰« _) â‰« (cokernelIsoOfEq _).hom) :=
      (kernelIsoOfEq (Ï€_comp_cokernelIsoOfEq_hom (NatIso.naturality_2 i f)).symm)
    _ â‰… kernel (cokernel.Ï€ (_ â‰« f â‰« _)) := (kernelCompMono _ _)
    _ â‰… kernel (cokernel.Ï€ (f â‰« i.inv.app Y) â‰« (cokernelEpiComp (i.hom.app X) _).inv) :=
      (kernelIsoOfEq (by simp only [cokernel.Ï€_desc, cokernelEpiComp_inv]))
    _ â‰… kernel (cokernel.Ï€ (f â‰« _)) := (kernelCompMono _ _)
    _ â‰… kernel (inv (i.inv.app Y) â‰« cokernel.Ï€ f â‰« (cokernelCompIsIso f (i.inv.app Y)).inv) :=
      (kernelIsoOfEq
        (by simp only [cokernel.Ï€_desc, cokernelCompIsIso_inv, Iso.hom_inv_id_app_assoc,
          NatIso.inv_inv_app]))
    _ â‰… kernel (cokernel.Ï€ f â‰« _) := (kernelIsIsoComp _ _)
    _ â‰… kernel (cokernel.Ï€ f) := kernelCompMono _ _
#align category_theory.abelian_of_adjunction.coimage_iso_image_aux CategoryTheory.AbelianOfAdjunction.coimageIsoImageAux

variable [Functor.PreservesZeroMorphisms F]

/-- Auxiliary definition: the abelian coimage and abelian image agree.
We still need to check that this agrees with the canonical morphism.
-/
def coimageIsoImage {X Y : C} (f : X âŸ¶ Y) : Abelian.coimage f â‰… Abelian.image f := by
  have : PreservesLimits F := adj.rightAdjointPreservesLimits
  -- porting note: the next `have` has been added, otherwise some instance were not found
  haveI : âˆ€ (X' Y' : D) (f' : X' âŸ¶ Y'), HasCokernel f' := inferInstance
  calc
    Abelian.coimage f â‰… cokernel (kernel.Î¹ f) := Iso.refl _
    _ â‰… G.obj (cokernel (F.map (kernel.Î¹ f))) := (cokernelIso _ _ i adj _).symm
    _ â‰… G.obj (cokernel (kernelComparison f F â‰« kernel.Î¹ (F.map f))) :=
      (G.mapIso (cokernelIsoOfEq (by simp)))
    _ â‰… G.obj (cokernel (kernel.Î¹ (F.map f))) := (G.mapIso (cokernelEpiComp _ _))
    _ â‰… G.obj (Abelian.coimage (F.map f)) := (Iso.refl _)
    _ â‰… G.obj (Abelian.image (F.map f)) := (G.mapIso (Abelian.coimageIsoImage _))
    _ â‰… G.obj (kernel (cokernel.Ï€ (F.map f))) := (Iso.refl _)
    _ â‰… kernel (G.map (cokernel.Ï€ (F.map f))) := (PreservesKernel.iso _ _)
    _ â‰… kernel (cokernel.Ï€ f) := (coimageIsoImageAux F G i adj f)
    _ â‰… Abelian.image f := Iso.refl _
#align category_theory.abelian_of_adjunction.coimage_iso_image CategoryTheory.AbelianOfAdjunction.coimageIsoImage

-- The account of this proof in the Stacks project omits this calculation.
@[nolint unusedHavesSuffices]
theorem coimageIsoImage_hom {X Y : C} (f : X âŸ¶ Y) :
    (coimageIsoImage F G i adj f).hom = Abelian.coimageImageComparison f := by
  -- porting note: the next `have` have been added, otherwise some instance were not found
  have : âˆ€ (X' Y' : C) (f' : X' âŸ¶ Y'), HasCokernel f' := inferInstance
  have : âˆ€ (X' Y' : C) (f' : X' âŸ¶ Y'), HasKernel f' := inferInstance
  have : âˆ€ (X' Y' : D) (f' : X' âŸ¶ Y'), HasCokernel f' := inferInstance
  have : âˆ€ (X' Y' : D) (f' : X' âŸ¶ Y'), HasKernel f' := inferInstance
  dsimp only [coimageIsoImage, Iso.instTransIso_trans, Iso.refl, Iso.trans, Iso.symm,
    Functor.mapIso, cokernelEpiComp, cokernelIso, cokernelCompIsIso_inv,
    asIso, coimageIsoImageAux, kernelCompMono]
  simpa only [â† cancel_mono (Abelian.image.Î¹ f), â† cancel_epi (Abelian.coimage.Ï€ f),
    Category.assoc, Category.id_comp, cokernel.Ï€_desc_assoc,
    Ï€_comp_cokernelIsoOfEq_inv_assoc, PreservesKernel.iso_hom,
    Ï€_comp_cokernelComparison_assoc, â† G.map_comp_assoc, kernel.lift_Î¹,
    Abelian.coimage_image_factorisation, lift_comp_kernelIsoOfEq_hom_assoc,
    kernelIsIsoComp_hom, kernel.lift_Î¹_assoc, kernelIsoOfEq_hom_comp_Î¹_assoc,
    kernelComparison_comp_Î¹_assoc, Ï€_comp_cokernelIsoOfEq_hom_assoc,
    asIso_hom, NatIso.inv_inv_app] using NatIso.naturality_1 i f
#align category_theory.abelian_of_adjunction.coimage_iso_image_hom CategoryTheory.AbelianOfAdjunction.coimageIsoImage_hom

end AbelianOfAdjunction

open AbelianOfAdjunction

/-- If `C` is an additive category, `D` is an abelian category,
we have `F : C â¥¤ D` `G : D â¥¤ C` (both preserving zero morphisms),
`G` is left exact (that is, preserves finite limits),
and further we have `adj : G âŠ£ F` and `i : F â‹™ G â‰… ğŸ­ C`,
then `C` is also abelian.

See <https://stacks.math.columbia.edu/tag/03A3>
-/
def abelianOfAdjunction {C : Type uâ‚} [Category.{v} C] [Preadditive C] [HasFiniteProducts C]
    {D : Type uâ‚‚} [Category.{v} D] [Abelian D] (F : C â¥¤ D) [Functor.PreservesZeroMorphisms F]
    (G : D â¥¤ C) [Functor.PreservesZeroMorphisms G] [PreservesFiniteLimits G] (i : F â‹™ G â‰… ğŸ­ C)
    (adj : G âŠ£ F) : Abelian C := by
  haveI := hasKernels F G i
  haveI := hasCokernels F G i adj
  have : âˆ€ {X Y : C} (f : X âŸ¶ Y), IsIso (Abelian.coimageImageComparison f) := by
    intro X Y f
    rw [â† coimageIsoImage_hom F G i adj f]
    infer_instance
  apply Abelian.ofCoimageImageComparisonIsIso
#align category_theory.abelian_of_adjunction CategoryTheory.abelianOfAdjunction

/-- If `C` is an additive category equivalent to an abelian category `D`
via a functor that preserves zero morphisms,
then `C` is also abelian.
-/
def abelianOfEquivalence {C : Type uâ‚} [Category.{v} C] [Preadditive C] [HasFiniteProducts C]
    {D : Type uâ‚‚} [Category.{v} D] [Abelian D] (F : C â¥¤ D) [Functor.PreservesZeroMorphisms F]
    [IsEquivalence F] : Abelian C :=
  abelianOfAdjunction F F.inv F.asEquivalence.unitIso.symm F.asEquivalence.symm.toAdjunction
#align category_theory.abelian_of_equivalence CategoryTheory.abelianOfEquivalence

namespace transfer_enough_injectives

variable {ğ’œ: Type uâ‚} {â„¬ : Type uâ‚‚} [Category.{vâ‚} ğ’œ] [Category.{vâ‚‚} â„¬]
variable (L : ğ’œ â¥¤ â„¬) (R : â„¬ â¥¤ ğ’œ)

/--
Give a pair of functors
```
  --- L -->
ğ’œ          â„¬,
  <-- R ---
```
for `A : ğ’œ`, pick an injective presentation `L A âŸ¶ J` which always exists by enough
injectives of `â„¬`. we pullback `J` across `R`.
-/
def adjointObjectOfInjectivePresentation {A : ğ’œ}
    (a : InjectivePresentation <| L.obj A) :=
  R.obj <| a.J

variable {L R}
variable (adj : L âŠ£ R)

/-
If `g : X â†’ R(J)` and `f : X â†’ Y` is mono in `ğ“`, then there is an morphism `L(Y) â†’ J`
See the diagram below:
```
ğ“                             ğ“‘
A ---> R(J)                 L(A) -----> J <--------
      /                                /          |
     /                                /           |
    /  g                           by adjunction  |
   /                                /             |
  /                                /         by injectivity
X                              L(X)               |
|                               |L.map f          |
v                               v                 |
Y                              L(Y) ---------------
```
-/

/--
Let `L(A) âŸ¶ J` be an injective presentation.
If `g : X â†’ R(J)` and `f : X â†’ Y` is mono in `ğ“`, then there is an morphism `L(Y) â†’ J`:
* Since `L` preserves finite limits, `L(f)` is mono
* If `L âŠ£ R`, then `g` gives a `L(X) âŸ¶ J`
* we then factor `X âŸ¶ R(J)` into `L(f)` and `L(Y) âŸ¶ J`
-/
def toInjectiveObject [PreservesFiniteLimits L] {A X Y : ğ’œ} (a : InjectivePresentation <| L.obj A)
    (g : X âŸ¶ adjointObjectOfInjectivePresentation L R a) (f : X âŸ¶ Y) [Mono f] :
    L.obj Y âŸ¶ a.J :=
  let i1 := a.injective.factors
  (i1 ((adj.homEquiv X <| a.J).symm g) (L.map f)).choose

lemma toInjectiveObject_spec [PreservesFiniteLimits L] {A X Y : ğ’œ}
    (a : InjectivePresentation <| L.obj A)
    (g : X âŸ¶ adjointObjectOfInjectivePresentation L R a) (f : X âŸ¶ Y) [Mono f] :
    L.map f â‰« toInjectiveObject adj a g f =
    (adj.homEquiv X <| a.J).symm g :=
  let i1 := a.injective.factors
  (i1 ((adj.homEquiv X <| a.J).symm g) (L.map f)).choose_spec

/--
Let `L(A) âŸ¶ J` be an injective presentation.
If `g : X â†’ R(J)` and `f : X â†’ Y` is mono in `ğ“`, then there is an morphism `L(Y) â†’ J` as in
`toInjectiveUnder`, then we obtain a map `Y âŸ¶ R(J)` via adjunction
-/
def adjointToInjective [PreservesFiniteLimits L] {A X Y : ğ’œ}
    (a : InjectivePresentation <| L.obj A)
    (g : X âŸ¶ adjointObjectOfInjectivePresentation L R a) (f : X âŸ¶ Y) [Mono f] :
    Y âŸ¶ adjointObjectOfInjectivePresentation L R a :=
  adj.homEquiv _ _ <| toInjectiveObject adj a g f

lemma adjointToInjective_spec [PreservesFiniteLimits L] {A X Y : ğ’œ}
    (a : InjectivePresentation <| L.obj A)
    (g : X âŸ¶ adjointObjectOfInjectivePresentation L R a) (f : X âŸ¶ Y) [Mono f] :
    f â‰« adjointToInjective adj a g f = g := by
  have := toInjectiveObject_spec adj a g f
  rw [â† adj.homEquiv_apply_eq] at this
  rw [â† this]
  simp only [adjointToInjective, toInjectiveObject, Adjunction.homEquiv_counit, Functor.id_obj,
    Adjunction.homEquiv_unit, Functor.comp_obj, Functor.map_comp, Adjunction.unit_naturality_assoc,
    Category.assoc, Adjunction.counit_naturality, Adjunction.left_triangle_components_assoc]
  generalize_proofs h1 h2
  congr 4
  ext
  rw [h1.choose_spec]

lemma injective_adjointObjectOfInjectivePresentation_of_adj [PreservesFiniteLimits L] {A : ğ’œ}
    (a : InjectivePresentation <| L.obj A) :
    Injective (adjointObjectOfInjectivePresentation L R a) where
  factors _ _ _ := âŸ¨_, adjointToInjective_spec adj a _ _âŸ©

variable (L R)
/--
Let `L(A) âŸ¶ J` be an injective presentation of `L(A)`, then `A âŸ¶ R(J)` is an injective
presentation of `A`
-/
def under {A : ğ’œ} (a : InjectivePresentation <| L.obj A) : ğ’œ :=
  adjointObjectOfInjectivePresentation L R a

variable {L R}
/--
Let `L(A) âŸ¶ J` be an injective presentation of `L(A)`, then `A âŸ¶ R(J)` is an injective
presentation of `A`
-/
def toUnder {A : ğ’œ} (a : InjectivePresentation <| L.obj A) :
    A âŸ¶ under L R a := adj.homEquiv _ _ <| a.f

lemma mono_toUnder [Abelian ğ’œ] [Abelian â„¬] [PreservesFiniteLimits L] [Faithful L]
    {A : ğ’œ} (a : InjectivePresentation <| L.obj A) : Mono (toUnder adj a) := by
  have eq1 : L.map (toUnder adj a) â‰« (adj.counit.app _) = a.f
  Â·  simp [toUnder]
  have m1 : Mono (L.map (toUnder adj a) â‰« (adj.counit.app _))
  Â· rw [eq1]
    exact a.mono
  have m2 : Mono (L.map (toUnder adj a))
  Â· exact mono_of_mono _ (adj.counit.app a.J)
  have eq2 : L.map (kernel.Î¹ (toUnder adj a)) =
    (PreservesKernel.iso L (toUnder adj a)).hom â‰« kernel.Î¹ (L.map (toUnder adj a))
  Â· simp
  have eq3 : kernel.Î¹ (toUnder adj a) = 0
  Â· refine L.zero_of_map_zero _ ?_
    rw [Abelian.mono_iff_kernel_Î¹_eq_zero] at m2
    rw [eq2, m2, comp_zero]
  rw [Abelian.mono_iff_kernel_Î¹_eq_zero, eq3]

end transfer_enough_injectives

open transfer_enough_injectives in
/--
[Lemma 3.8](https://ncatlab.org/nlab/show/injective+object#preservation_of_injective_objects)
-/
lemma EnoughInjectives.of_adjunction {C : Type uâ‚} {D : Type uâ‚‚}
    [Category.{vâ‚} C] [Category.{vâ‚‚} D] [Abelian C] [Abelian D]
    {L : C â¥¤ D} {R : D â¥¤ C} (adj : L âŠ£ R) [Faithful L] [PreservesFiniteLimits L]
    [EnoughInjectives D] : EnoughInjectives C where
  presentation _ :=
    âŸ¨âŸ¨_, injective_adjointObjectOfInjectivePresentation_of_adj adj
      (EnoughInjectives.presentation _).some, _, mono_toUnder adj _âŸ©âŸ©

/-- An equivalence of categories transfers enough injectives. -/
lemma EnoughInjectives.of_equivalence {C : Type uâ‚} {D : Type uâ‚‚}
  [Category.{vâ‚} C] [Category.{vâ‚‚} D] [Abelian C] [Abelian D]
  (e : C â¥¤ D) [IsEquivalence e] [EnoughInjectives D] : EnoughInjectives C :=
EnoughInjectives.of_adjunction (adj := e.asEquivalence.toAdjunction)

end CategoryTheory
