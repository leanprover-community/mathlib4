/-
Copyright (c) 2020 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Abelian.Pseudoelements

#align_import category_theory.abelian.diagram_lemmas.four from "leanprover-community/mathlib"@"d34cbcf6c94953e965448c933cd9cc485115ebbd"

/-!
# The four and five lemmas

Consider the following commutative diagram with exact rows in an abelian category:

```
A ---f--> B ---g--> C ---h--> D ---i--> E
|         |         |         |         |
Î±         Î²         Î³         Î´         Îµ
|         |         |         |         |
v         v         v         v         v
A' --f'-> B' --g'-> C' --h'-> D' --i'-> E'
```

We show:
- the "mono" version of the four lemma: if `Î±` is an epimorphism and `Î²` and `Î´` are monomorphisms,
  then `Î³` is a monomorphism,
- the "epi" version of the four lemma: if `Î²` and `Î´` are epimorphisms and `Îµ` is a monomorphism,
  then `Î³` is an epimorphism,
- the five lemma: if `Î±`, `Î²`, `Î´` and `Îµ` are isomorphisms, then `Î³` is an isomorphism.

## Implementation details

To show the mono version, we use pseudoelements. For the epi version, we use a completely different
arrow-theoretic proof. In theory, it should be sufficient to have one version and the other should
follow automatically by duality. In practice, mathlib's knowledge about duality isn't quite at the
point where this is doable easily.

However, one key duality statement about exactness is needed in the proof of the epi version of the
four lemma: we need to know that exactness of a pair `(f, g)`, which we defined via the map from
the image of `f` to the kernel of `g`, is the same as "co-exactness", defined via the map from the
cokernel of `f` to the coimage of `g` (more precisely, we only need the consequence that if `(f, g)`
is exact, then the factorization of `g` through the cokernel of `f` is monomorphic). Luckily, in the
case of abelian categories, we have the characterization that `(f, g)` is exact if and only if
`f â‰« g = 0` and `kernel.Î¹ g â‰« cokernel.Ï€ f = 0`, and the latter condition is self dual, so the
equivalence of exactness and co-exactness follows easily.

## Tags

four lemma, five lemma, diagram lemma, diagram chase
-/


open CategoryTheory hiding comp_apply

open CategoryTheory.Abelian.Pseudoelement

open CategoryTheory.Limits

universe v u

variable {V : Type u} [Category.{v} V] [Abelian V]

attribute [local instance] Preadditive.hasEqualizers_of_hasKernels

open Pseudoelement

namespace CategoryTheory.Abelian

variable {A B C D A' B' C' D' : V}

variable {f : A âŸ¶ B} {g : B âŸ¶ C} {h : C âŸ¶ D}

variable {f' : A' âŸ¶ B'} {g' : B' âŸ¶ C'} {h' : C' âŸ¶ D'}

variable {Î± : A âŸ¶ A'} {Î² : B âŸ¶ B'} {Î³ : C âŸ¶ C'} {Î´ : D âŸ¶ D'}

variable (commâ‚ : Î± â‰« f' = f â‰« Î²) (commâ‚‚ : Î² â‰« g' = g â‰« Î³) (commâ‚ƒ : Î³ â‰« h' = h â‰« Î´)

section

variable (hfg : Exact f g) (hgh : Exact g h) (hf'g' : Exact f' g')

/-- The four lemma, mono version. For names of objects and morphisms, refer to the following
    diagram:

```
A ---f--> B ---g--> C ---h--> D
|         |         |         |
Î±         Î²         Î³         Î´
|         |         |         |
v         v         v         v
A' --f'-> B' --g'-> C' --h'-> D'
```
-/
theorem mono_of_epi_of_mono_of_mono (hÎ± : Epi Î±) (hÎ² : Mono Î²) (hÎ´ : Mono Î´) : Mono Î³ :=
  mono_of_zero_of_map_zero _ fun c hc =>
    have : h c = 0 :=
      suffices Î´ (h c) = 0 from zero_of_map_zero _ (pseudo_injective_of_mono _) _ this
      calc
        Î´ (h c) = h' (Î³ c) := by rw [â† Pseudoelement.comp_apply, â† commâ‚ƒ, Pseudoelement.comp_apply]
                                 -- ğŸ‰ no goals
        _ = h' 0 := by rw [hc]
                       -- ğŸ‰ no goals
        _ = 0 := apply_zero _
    Exists.elim ((pseudo_exact_of_exact hgh).2 _ this) fun b hb =>
      have : g' (Î² b) = 0 :=
        calc
          g' (Î² b) = Î³ (g b) := by rw [â† Pseudoelement.comp_apply, commâ‚‚, Pseudoelement.comp_apply]
                                   -- ğŸ‰ no goals
          _ = Î³ c := by rw [hb]
                        -- ğŸ‰ no goals
          _ = 0 := hc
      Exists.elim ((pseudo_exact_of_exact hf'g').2 _ this) fun a' ha' =>
        Exists.elim (pseudo_surjective_of_epi Î± a') fun a ha =>
          have : f a = b :=
            suffices Î² (f a) = Î² b from pseudo_injective_of_mono _ this
            calc
              Î² (f a) = f' (Î± a) := by
                rw [â† Pseudoelement.comp_apply, â† commâ‚, Pseudoelement.comp_apply]
                -- ğŸ‰ no goals
              _ = f' a' := by rw [ha]
                              -- ğŸ‰ no goals
              _ = Î² b := ha'
          calc
            c = g b := hb.symm
            _ = g (f a) := by rw [this]
                              -- ğŸ‰ no goals
            _ = 0 := (pseudo_exact_of_exact hfg).1 _
#align category_theory.abelian.mono_of_epi_of_mono_of_mono CategoryTheory.Abelian.mono_of_epi_of_mono_of_mono

end

section

variable (hgh : Exact g h) (hf'g' : Exact f' g') (hg'h' : Exact g' h')

/-- The four lemma, epi version. For names of objects and morphisms, refer to the following
    diagram:

```
A ---f--> B ---g--> C ---h--> D
|         |         |         |
Î±         Î²         Î³         Î´
|         |         |         |
v         v         v         v
A' --f'-> B' --g'-> C' --h'-> D'
```
-/
theorem epi_of_epi_of_epi_of_mono (hÎ± : Epi Î±) (hÎ³ : Epi Î³) (hÎ´ : Mono Î´) : Epi Î² :=
  Preadditive.epi_of_cancel_zero _ fun {R} r hÎ²r => by
    have hf'r : f' â‰« r = 0 :=
      Limits.zero_of_epi_comp Î± <|
        calc
          Î± â‰« f' â‰« r = f â‰« Î² â‰« r := by rw [reassoc_of% commâ‚]
          _ = f â‰« 0 := by rw [hÎ²r]
          _ = 0 := HasZeroMorphisms.comp_zero _ _
    let y : R âŸ¶ pushout r g' := pushout.inl
    -- âŠ¢ r = 0
    let z : C' âŸ¶ pushout r g' := pushout.inr
    -- âŠ¢ r = 0
    -- Porting note: Added instance for `Mono (cokernel.desc f' g' hf'g'.w)`
    have : Mono (cokernel.desc f' g' hf'g'.w) := mono_cokernel_desc_of_exact _ _ hf'g'
    -- âŠ¢ r = 0
    have : Mono y :=
      mono_inl_of_factor_thru_epi_mono_factorization r g' (cokernel.Ï€ f')
        (cokernel.desc f' g' hf'g'.w) (by simp) (cokernel.desc f' r hf'r) (by simp) _
        (colimit.isColimit _)
    have hz : g â‰« Î³ â‰« z = 0 :=
      calc
        g â‰« Î³ â‰« z = Î² â‰« g' â‰« z := by rw [â† reassoc_of% commâ‚‚]
        _ = Î² â‰« r â‰« y := by rw [â† pushout.condition]
        _ = 0 â‰« y := by rw [reassoc_of% hÎ²r]
        _ = 0 := HasZeroMorphisms.zero_comp _ _
    let v : pushout r g' âŸ¶ pushout (Î³ â‰« z) (h â‰« Î´) := pushout.inl
    -- âŠ¢ r = 0
    let w : D' âŸ¶ pushout (Î³ â‰« z) (h â‰« Î´) := pushout.inr
    -- âŠ¢ r = 0
    -- Porting note: Added instance for `Mono (cokernel.desc g h hgh.w)`
    have : Mono (cokernel.desc g h hgh.w) := mono_cokernel_desc_of_exact _ _ hgh
    -- âŠ¢ r = 0
    have : Mono v :=
      mono_inl_of_factor_thru_epi_mono_factorization _ _ (cokernel.Ï€ g)
        (cokernel.desc g h hgh.w â‰« Î´) (by simp) (cokernel.desc _ _ hz) (by simp) _
        (colimit.isColimit _)
    have hzv : z â‰« v = h' â‰« w :=
      (cancel_epi Î³).1 <|
        calc
          Î³ â‰« z â‰« v = h â‰« Î´ â‰« w := by rw [â† Category.assoc, pushout.condition, Category.assoc]
          _ = Î³ â‰« h' â‰« w := by rw [reassoc_of% commâ‚ƒ]
    suffices (r â‰« y) â‰« v = 0 from zero_of_comp_mono _ (zero_of_comp_mono _ this)
    -- âŠ¢ (r â‰« y) â‰« v = 0
    calc
      (r â‰« y) â‰« v = g' â‰« z â‰« v := by rw [pushout.condition, Category.assoc]
      _ = g' â‰« h' â‰« w := by rw [hzv]
      _ = 0 â‰« w := (hg'h'.w_assoc _)
      _ = 0 := HasZeroMorphisms.zero_comp _ _
#align category_theory.abelian.epi_of_epi_of_epi_of_mono CategoryTheory.Abelian.epi_of_epi_of_epi_of_mono

end

section Five

variable {E E' : V} {i : D âŸ¶ E} {i' : D' âŸ¶ E'} {Îµ : E âŸ¶ E'} (commâ‚„ : Î´ â‰« i' = i â‰« Îµ)

variable (hfg : Exact f g) (hgh : Exact g h) (hhi : Exact h i)

variable (hf'g' : Exact f' g') (hg'h' : Exact g' h') (hh'i' : Exact h' i')

variable [Epi Î±] [IsIso Î²] [IsIso Î´] [Mono Îµ]

/-- The five lemma. For names of objects and morphisms, refer to the following diagram:

```
A ---f--> B ---g--> C ---h--> D ---i--> E
|         |         |         |         |
Î±         Î²         Î³         Î´         Îµ
|         |         |         |         |
v         v         v         v         v
A' --f'-> B' --g'-> C' --h'-> D' --i'-> E'
```
-/
theorem isIso_of_epi_of_isIso_of_isIso_of_mono : IsIso Î³ :=
  have : Mono Î³ := by
    apply mono_of_epi_of_mono_of_mono commâ‚ commâ‚‚ commâ‚ƒ hfg hgh hf'g' <;> infer_instance
                                                                          -- ğŸ‰ no goals
                                                                          -- ğŸ‰ no goals
                                                                          -- ğŸ‰ no goals
  have : Epi Î³ := by
    apply epi_of_epi_of_epi_of_mono commâ‚‚ commâ‚ƒ commâ‚„ hhi hg'h' hh'i' <;> infer_instance
                                                                          -- ğŸ‰ no goals
                                                                          -- ğŸ‰ no goals
                                                                          -- ğŸ‰ no goals
  isIso_of_mono_of_epi _
#align category_theory.abelian.is_iso_of_is_iso_of_is_iso_of_is_iso_of_is_iso CategoryTheory.Abelian.isIso_of_epi_of_isIso_of_isIso_of_mono

end Five

end CategoryTheory.Abelian
