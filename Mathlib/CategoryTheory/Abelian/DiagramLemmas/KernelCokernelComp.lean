/-
Copyright (c) 2025 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Homology.ShortComplex.SnakeLemma

/-!
# Long exact sequence for the kernel and cokernel of a composition

If `f : X âŸ¶ Y` and `g : Y âŸ¶ Z` are composable morphisms in an
abelian category, we construct a long exact sequence :
`0 âŸ¶ ker f âŸ¶ ker (f â‰« g) âŸ¶ ker g âŸ¶ coker f âŸ¶ coker (f â‰« g) âŸ¶ coker g âŸ¶ 0`.

This is obtained by applying the snake lemma to the following morphism of
exact sequences, where the rows are the obvious split exact sequences
```
0 âŸ¶ X âŸ¶ X âŠ Y âŸ¶ Y âŸ¶ 0
    |f    |Ï†    |g
    v     v     v
0 âŸ¶ Y âŸ¶ Y âŠ Z âŸ¶ Z âŸ¶ 0
```
and `Ï†` is given by the following matrix:
```
(f  -ğŸ™ Y)
(0     g)
```

Indeed the snake lemma gives an exact sequence involving the kernels and cokernels
of the vertical maps: in order to get the expected long exact sequence, it suffices
to obtain isomorphisms `ker Ï† â‰… ker (f â‰« g)` and `coker Ï† â‰… coker (f â‹™ g)`.

-/

universe v u

namespace CategoryTheory

open Limits Category Preadditive

variable {C : Type u} [Category.{v} C] [Abelian C]
  {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)

namespace kernelCokernelCompSequence

/-- If `f : X âŸ¶ Y` and `g : Y âŸ¶ Z` are composable morphisms,
this is the morphism `kernel (f â‰« g) âŸ¶ X âŠ Y` which
"sends `x` to `(x, f(x))`". -/
noncomputable def Î¹ : kernel (f â‰« g) âŸ¶ X âŠ Y :=
  biprod.lift (kernel.Î¹ (f â‰« g)) (kernel.Î¹ (f â‰« g) â‰« f)

@[reassoc (attr := simp)]
lemma Î¹_fst : Î¹ f g â‰« biprod.fst = kernel.Î¹ (f â‰« g) := by simp [Î¹]

@[reassoc (attr := simp)]
lemma Î¹_snd : Î¹ f g â‰« biprod.snd = kernel.Î¹ (f â‰« g) â‰« f := by simp [Î¹]

/-- If `f : X âŸ¶ Y` and `g : Y âŸ¶ Z` are composable morphisms,
this is the morphism `X âŠ Y âŸ¶ Y âŠ Z` given by the matrix
```
(f  -ğŸ™ Y)
(0     g)
```
-/
noncomputable def Ï† : X âŠ Y âŸ¶ Y âŠ Z :=
  biprod.desc (f â‰« biprod.inl) (biprod.lift (-ğŸ™ Y) g)

@[reassoc (attr := simp)]
lemma inl_Ï† : biprod.inl â‰« Ï† f g = f â‰« biprod.inl := by simp [Ï†]

@[reassoc (attr := simp)]
lemma inr_Ï†_fst : biprod.inr â‰« Ï† f g â‰« biprod.fst = - ğŸ™ Y := by simp [Ï†]

@[reassoc (attr := simp)]
lemma Ï†_snd : Ï† f g â‰« biprod.snd = biprod.snd â‰« g := by
  dsimp [Ï†]
  aesop

/-- If `f : X âŸ¶ Y` and `g : Y âŸ¶ Z` are composable morphisms,
this is the morphism `Y âŠ Z âŸ¶ cokernel (f â‰« g)` which
"sends `(y, z)` to `[g(y)] + [z]`". -/
noncomputable def Ï€ : Y âŠ Z âŸ¶ cokernel (f â‰« g) :=
  biprod.desc (g â‰« cokernel.Ï€ (f â‰« g)) (cokernel.Ï€ (f â‰« g))

@[reassoc (attr := simp)]
lemma inl_Ï€ : biprod.inl â‰« Ï€ f g = g â‰« cokernel.Ï€ (f â‰« g) := by simp [Ï€]

@[reassoc (attr := simp)]
lemma inr_Ï€ : biprod.inr â‰« Ï€ f g = cokernel.Ï€ (f â‰« g) := by simp [Ï€]

@[reassoc (attr := simp)]
lemma Î¹_Ï† : Î¹ f g â‰« Ï† f g = 0 := by
  dsimp [Î¹, Ï†]
  aesop

@[reassoc (attr := simp)]
lemma Ï†_Ï€ : Ï† f g â‰« Ï€ f g = 0 := by
  dsimp [Ï†, Ï€]
  ext
  Â· rw [biprod.inl_desc_assoc, assoc, biprod.inl_desc, comp_zero,
      â† assoc, cokernel.condition]
  Â· simp

instance : Mono (Î¹ f g) := mono_of_mono_fac (Î¹_fst f g)

instance : Epi (Ï€ f g) := epi_of_epi_fac (inr_Ï€ f g)

/-- If `f : X âŸ¶ Y` and `g : Y âŸ¶ Z` are composable morphisms,
then the kernel of `Ï† f g : X âŠ Y âŸ¶ Y âŠ Z` identifies
to `kernel (f â‰« g)`. -/
noncomputable def isLimit : IsLimit (KernelFork.ofÎ¹ _ (Î¹_Ï† f g)) :=
  KernelFork.IsLimit.ofÎ¹' _ _ (fun {A} k hk â†¦ by
    refine âŸ¨kernel.lift _ (k â‰« biprod.fst) ?_, ?_âŸ©
    all_goals
      obtain âŸ¨kâ‚, kâ‚‚, rflâŸ© := biprod.decomp_hom_to k
      simp only [biprod.ext_to_iff, add_comp, assoc, inl_Ï†, BinaryBicone.inl_fst,
        comp_id, inr_Ï†_fst, comp_neg, zero_comp, BinaryBicone.inl_snd, comp_zero, Ï†_snd,
        BinaryBicone.inr_snd_assoc, zero_add, add_neg_eq_zero] at hk
      obtain âŸ¨rfl, hkâŸ© := hk
      aesop)

/-- If `f : X âŸ¶ Y` and `g : Y âŸ¶ Z` are composable morphisms,
then the cokernel of `Ï† f g : X âŠ Y âŸ¶ Y âŠ Z` identifies
to `cokernel (f â‰« g)`. -/
noncomputable def isColimit : IsColimit (CokernelCofork.ofÏ€ _ (Ï†_Ï€ f g)) :=
  CokernelCofork.IsColimit.ofÏ€' _ _ (fun {A} k hk â†¦ by
    refine âŸ¨cokernel.desc _ (biprod.inr â‰« k) ?_, ?_âŸ©
    all_goals
      obtain âŸ¨kâ‚, kâ‚‚, rflâŸ© := biprod.decomp_hom_from k
      simp only [comp_add, Ï†_snd_assoc, biprod.ext_from_iff, inl_Ï†_assoc,
        BinaryBicone.inl_fst_assoc, BinaryBicone.inl_snd_assoc, zero_comp, add_zero, comp_zero,
        inr_Ï†_fst_assoc, neg_comp, id_comp, BinaryBicone.inr_snd_assoc, neg_add_eq_zero] at hk
      obtain âŸ¨hk, rflâŸ© := hk
      aesop)

/-- The "snake input" which gives the exact sequence
`0 âŸ¶ ker f âŸ¶ ker (f â‰« g) âŸ¶ ker g âŸ¶ coker f âŸ¶ coker (f â‰« g) âŸ¶ coker g âŸ¶ 0`,
see `kernelCokernelCompSequence_exact`. -/
@[simps]
noncomputable def snakeInput : ShortComplex.SnakeInput C where
  Lâ‚€ :=
    { f := kernel.map f (f â‰« g) (ğŸ™ _) g (by simp)
      g := kernel.map (f â‰« g) g f (ğŸ™ _) (by simp)
      zero := by aesop }
  Lâ‚ := ShortComplex.mk (biprod.inl : X âŸ¶ _) (biprod.snd : _ âŸ¶ Y) (by simp)
  Lâ‚‚ := ShortComplex.mk (biprod.inl : Y âŸ¶ _) (biprod.snd : _ âŸ¶ Z) (by simp)
  Lâ‚ƒ :=
    { f := cokernel.map f (f â‰« g) (ğŸ™ _) g (by simp)
      g := cokernel.map (f â‰« g) g f (ğŸ™ _) (by simp)
      zero := by aesop }
  vâ‚€â‚ :=
    { Ï„â‚ := kernel.Î¹ f
      Ï„â‚‚ := Î¹ f g
      Ï„â‚ƒ := kernel.Î¹ g }
  vâ‚â‚‚ :=
    { Ï„â‚ := f
      Ï„â‚‚ := Ï† f g
      Ï„â‚ƒ := g }
  vâ‚‚â‚ƒ :=
    { Ï„â‚ := cokernel.Ï€ f
      Ï„â‚‚ := Ï€ f g
      Ï„â‚ƒ := cokernel.Ï€ g }
  hâ‚€ := by
    apply ShortComplex.isLimitOfIsLimitÏ€ <;>
      apply (KernelFork.isLimitMapConeEquiv _ _).2
    Â· exact kernelIsKernel _
    Â· exact isLimit f g
    Â· exact kernelIsKernel _
  hâ‚ƒ := by
    apply ShortComplex.isColimitOfIsColimitÏ€ <;>
      apply (CokernelCofork.isColimitMapCoconeEquiv _ _).2
    Â· exact cokernelIsCokernel _
    Â· exact isColimit f g
    Â· exact cokernelIsCokernel _
  epi_Lâ‚_g := by dsimp; infer_instance
  mono_Lâ‚‚_f := by dsimp; infer_instance
  Lâ‚_exact := (ShortComplex.Splitting.ofHasBinaryBiproduct X Y).exact
  Lâ‚‚_exact := (ShortComplex.Splitting.ofHasBinaryBiproduct Y Z).exact

/-- If `f : X âŸ¶ Y` and `g : Y âŸ¶ Z` are composable morphisms, this
is the connecting homomorphism `kernel g âŸ¶ cokernel f`. -/
noncomputable def Î´ : kernel g âŸ¶ cokernel f := (snakeInput f g).Î´

lemma Î´_fac : Î´ f g = - kernel.Î¹ g â‰« cokernel.Ï€ f := by
  simpa using (snakeInput f g).Î´_eq (ğŸ™ _) (kernel.Î¹ g â‰« biprod.inr) (-kernel.Î¹ g)
    (by simp) (by aesop)

end kernelCokernelCompSequence

open kernelCokernelCompSequence

/-- If `f : X âŸ¶ Y` and `g : Y âŸ¶ Z` are composable morphisms in an
abelian category, this is the long exact sequence
`0 âŸ¶ ker f âŸ¶ ker (f â‰« g) âŸ¶ ker g âŸ¶ coker f âŸ¶ coker (f â‰« g) âŸ¶ coker g âŸ¶ 0`. -/
noncomputable abbrev kernelCokernelCompSequence : ComposableArrows C 5 :=
  .mkâ‚… (kernel.map f (f â‰« g) (ğŸ™ _) g (by simp))
    (kernel.map (f â‰« g) g f (ğŸ™ _) (by simp))
    (Î´ f g)
    (cokernel.map f (f â‰« g) (ğŸ™ _) g (by simp))
    (cokernel.map (f â‰« g) g f (ğŸ™ _) (by simp))

instance : Mono ((kernelCokernelCompSequence f g).map' 0 1) := by
  dsimp; infer_instance

instance : Epi ((kernelCokernelCompSequence f g).map' 4 5) := by
  dsimp [ComposableArrows.Precomp.map]
  infer_instance

lemma kernelCokernelCompSequence_exact :
    (kernelCokernelCompSequence f g).Exact :=
  (snakeInput f g).snake_lemma

end CategoryTheory
