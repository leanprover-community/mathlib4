/-
Copyright (c) 2022 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Abelian.Subobject
import Mathlib.CategoryTheory.Limits.EssentiallySmall
import Mathlib.CategoryTheory.Preadditive.Injective
import Mathlib.CategoryTheory.Preadditive.Generator
import Mathlib.CategoryTheory.Abelian.Opposite

#align_import category_theory.abelian.generator from "leanprover-community/mathlib"@"f0c8bf9245297a541f468be517f1bde6195105e9"

/-!
# A complete abelian category with enough injectives and a separator has an injective coseparator

## Future work
* Once we know that Grothendieck categories have enough injectives, we can use this to conclude
  that Grothendieck categories have an injective coseparator.

## References
* [Peter J Freyd, *Abelian Categories* (Theorem 3.37)][freyd1964abelian]

-/


open CategoryTheory CategoryTheory.Limits Opposite

universe v u

namespace CategoryTheory.Abelian

variable {C : Type u} [Category.{v} C] [Abelian C]

theorem has_injective_coseparator [HasLimits C] [EnoughInjectives C] (G : C) (hG : IsSeparator G) :
    âˆƒ G : C, Injective G âˆ§ IsCoseparator G := by
  haveI : WellPowered C := wellPowered_of_isDetector G hG.isDetector
  -- âŠ¢ âˆƒ G, Injective G âˆ§ IsCoseparator G
  haveI : HasProductsOfShape (Subobject (op G)) C := hasProductsOfShape_of_small _ _
  -- âŠ¢ âˆƒ G, Injective G âˆ§ IsCoseparator G
  let T : C := Injective.under (piObj fun P : Subobject (op G) => unop P)
  -- âŠ¢ âˆƒ G, Injective G âˆ§ IsCoseparator G
  refine' âŸ¨T, inferInstance, (Preadditive.isCoseparator_iff _).2 fun X Y f hf => _âŸ©
  -- âŠ¢ f = 0
  refine' (Preadditive.isSeparator_iff _).1 hG _ fun h => _
  -- âŠ¢ h â‰« f = 0
  suffices hh : factorThruImage (h â‰« f) = 0
  -- âŠ¢ h â‰« f = 0
  Â· rw [â† Limits.image.fac (h â‰« f), hh, zero_comp]
    -- ğŸ‰ no goals
  let R := Subobject.mk (factorThruImage (h â‰« f)).op
  -- âŠ¢ factorThruImage (h â‰« f) = 0
  let qâ‚ : image (h â‰« f) âŸ¶ unop R :=
    (Subobject.underlyingIso (factorThruImage (h â‰« f)).op).unop.hom
  let qâ‚‚ : unop (R : Cáµ’áµ–) âŸ¶ piObj fun P : Subobject (op G) => unop P :=
    section_ (Pi.Ï€ (fun P : Subobject (op G) => (unop P : C)) R)
  let q : image (h â‰« f) âŸ¶ T := qâ‚ â‰« qâ‚‚ â‰« Injective.Î¹ _
  -- âŠ¢ factorThruImage (h â‰« f) = 0
  exact zero_of_comp_mono q
    (by rw [â† Injective.comp_factorThru q (Limits.image.Î¹ (h â‰« f)), Limits.image.fac_assoc,
      Category.assoc, hf, comp_zero])
#align category_theory.abelian.has_injective_coseparator CategoryTheory.Abelian.has_injective_coseparator

theorem has_projective_separator [HasColimits C] [EnoughProjectives C] (G : C)
    (hG : IsCoseparator G) : âˆƒ G : C, Projective G âˆ§ IsSeparator G := by
  obtain âŸ¨T, hTâ‚, hTâ‚‚âŸ© := has_injective_coseparator (op G) ((isSeparator_op_iff _).2 hG)
  -- âŠ¢ âˆƒ G, Projective G âˆ§ IsSeparator G
  exact âŸ¨unop T, inferInstance, (isSeparator_unop_iff _).2 hTâ‚‚âŸ©
  -- ğŸ‰ no goals
#align category_theory.abelian.has_projective_separator CategoryTheory.Abelian.has_projective_separator

end CategoryTheory.Abelian
