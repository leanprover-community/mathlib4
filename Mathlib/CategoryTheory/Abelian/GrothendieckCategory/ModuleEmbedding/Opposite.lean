/-
Copyright (c) 2025 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Abelian.Yoneda
import Mathlib.CategoryTheory.Generator.Abelian
import Mathlib.CategoryTheory.Abelian.GrothendieckCategory.EnoughInjectives

/-!
# Embedding opposites of Grothendieck categories

If `C` is Grothendieck abelian and `F : D ‚•§ C·µí·µñ` is a functor from a small category, we construct
an object `G : C·µí·µñ` such that `preadditiveCoyonedaObj G : C·µí·µñ ‚•§ ModuleCat (End G)·µê·µí·µñ` is faithful
and exact and its precomposition with `F` is full if `F` is.
-/

universe v u

open CategoryTheory Limits Opposite ZeroObject

namespace CategoryTheory.Abelian.IsGrothendieckAbelian

variable {C : Type u} [Category.{v} C] {D : Type v} [SmallCategory D] (F : D ‚•§ C·µí·µñ)

namespace OppositeModuleEmbedding

variable [Abelian C] [IsGrothendieckAbelian.{v} C]

variable (C) in
private noncomputable def projectiveSeparator : C·µí·µñ :=
  (has_projective_separator (coseparator C·µí·µñ) (isCoseparator_coseparator C·µí·µñ)).choose

private instance : Projective (projectiveSeparator C) :=
  (has_projective_separator (coseparator C·µí·µñ) (isCoseparator_coseparator C·µí·µñ)).choose_spec.1

private theorem isSeparator_projectiveSeparator : IsSeparator (projectiveSeparator C) :=
  (has_projective_separator (coseparator C·µí·µñ) (isCoseparator_coseparator C·µí·µñ)).choose_spec.2

private noncomputable def generator : C·µí·µñ :=
  ‚àê (fun (X : D) => ‚àê fun (_ : projectiveSeparator C ‚ü∂ F.obj X) => projectiveSeparator C)

private theorem exists_epi (X : D) : ‚àÉ f : generator F ‚ü∂ F.obj X, Epi f := by
  classical
  refine ‚ü®Sigma.desc (Pi.single X (ùüô _)) ‚â´ Sigma.desc (fun f => f), ?_‚ü©
  have h := (isSeparator_iff_epi (projectiveSeparator C)).1
    isSeparator_projectiveSeparator (F.obj X)
  suffices Epi (Sigma.desc (Pi.single X (ùüô _))) from epi_comp' this h
  exact SplitEpi.epi ‚ü®Sigma.Œπ (fun (X : D) => ‚àê fun _ => projectiveSeparator C) X, by simp‚ü©

private instance : Projective (generator F) := by
  rw [generator]
  infer_instance

private theorem isSeparator [Nonempty D] : IsSeparator (generator F) := by
  apply isSeparator_sigma_of_isSeparator _ Classical.ofNonempty
  apply isSeparator_sigma_of_isSeparator _ 0
  exact isSeparator_projectiveSeparator

/-- Given a functor `F : D ‚•§ C·µí·µñ`, where `C` is Grothendieck abelian, this is a ring `R` such that
`C·µí·µñ` has a nice embedding into `ModuleCat (EmbeddingRing F)`; see
`OppositeModuleEmbedding.embedding`. -/
def EmbeddingRing : Type v := (End (generator F))·µê·µí·µñ

noncomputable instance : Ring (EmbeddingRing F) :=
  inferInstanceAs <| Ring (End (generator F))·µê·µí·µñ

/-- This is a functor `embedding F : C·µí·µñ ‚•§ ModuleCat (EmbeddingRing F)`. We have that `embedding F`
is faithful and preserves finite limits and colimits. Furthermore, `F ‚ãô embedding F` is full. -/
noncomputable def embedding : C·µí·µñ ‚•§ ModuleCat.{v} (EmbeddingRing F) :=
  preadditiveCoyonedaObj (generator F)

instance faithful_embedding [Nonempty D] : (embedding F).Faithful :=
  (isSeparator_iff_faithful_preadditiveCoyonedaObj _).1 (isSeparator F)

instance full_embedding [Nonempty D] [F.Full] : (F ‚ãô embedding F).Full :=
  full_comp_preadditiveCoyonedaObj _ (isSeparator F) (exists_epi F)

instance preservesFiniteLimits_embedding : PreservesFiniteLimits (embedding F) := by
  rw [embedding]
  apply preservesFiniteLimits_of_preservesFiniteLimitsOfSize
  infer_instance

instance preservesFiniteColimits_embedding : PreservesFiniteColimits (embedding F) := by
  apply preservesFiniteColimits_preadditiveCoyonedaObj_of_projective

end OppositeModuleEmbedding

end CategoryTheory.Abelian.IsGrothendieckAbelian
