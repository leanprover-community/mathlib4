/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Abelian.GrothendieckCategory.Basic
import Mathlib.CategoryTheory.Abelian.GrothendieckAxioms.Colim
import Mathlib.CategoryTheory.MorphismProperty.TransfiniteComposition

/-!
# Monomorphisms in Grothendieck abelian categories

In this file, we show that in a Grothendieck abelian category,
monomorphisms are stable under transfinite composition.

-/

universe w v u

namespace CategoryTheory

open Limits

variable {C : Type u} [Category.{v} C] [Abelian C] [IsGrothendieckAbelian.{w} C]

namespace MorphismProperty.TransfiniteCompositionOfShape

variable {J : Type w} [LinearOrder J] [SuccOrder J] [OrderBot J] [WellFoundedLT J]
  {X Y : C} {f : X ‚ü∂ Y} (h : (monomorphisms C).TransfiniteCompositionOfShape J f)

attribute [local instance] IsCofiltered.isConnected

namespace mono

instance map_bot_le (j : J) : Mono (h.F.map (homOfLE bot_le : ‚ä• ‚ü∂ j)) := by
  induction j using SuccOrder.limitRecOn with
  | isMin j hj =>
    obtain rfl := hj.eq_bot
    exact inferInstanceAs (Mono (h.F.map (ùüô _)))
  | succ j hj hj' =>
    have : Mono _ := h.map_mem j hj
    rw [‚Üê homOfLE_comp bot_le (Order.le_succ j), Functor.map_comp]
    infer_instance
  | isSuccLimit j hj hj' =>
    have : OrderBot (Set.Iio j) :=
      { bot := ‚ü®‚ä•, Order.IsSuccLimit.bot_lt hj ‚ü©
        bot_le _ := bot_le }
    let œÜ : (Functor.const _).obj (h.F.obj ‚ä•) ‚ü∂
        (Set.principalSegIio j).monotone.functor ‚ãô h.F :=
      { app k := h.F.map (homOfLE bot_le)
        naturality k k' hkk' := by
          dsimp
          rw [Category.id_comp, ‚Üê Functor.map_comp]
          rfl }
    have (k : Set.Iio j) : Mono (œÜ.app k) := hj' k.1 k.2
    have : Mono œÜ := NatTrans.mono_of_mono_app œÜ
    apply colim.map_mono' œÜ
      (isColimitConstCocone (Set.Iio j) (h.F.obj ‚ä•))
      (h.F.isColimitOfIsWellOrderContinuous j hj)
    rintro k
    dsimp [œÜ]
    rw [Category.id_comp, ‚Üê Functor.map_comp]
    rfl

end mono

include h in
lemma mono : Mono f := by
  let œÜ : (Functor.const _).obj X ‚ü∂ h.F :=
    { app k := h.isoBot.inv ‚â´ h.F.map (homOfLE bot_le)
      naturality k k' hkk' := by
        dsimp
        rw [Category.id_comp, Category.assoc, ‚Üê Functor.map_comp]
        rfl }
  have : Mono œÜ := NatTrans.mono_of_mono_app œÜ
  exact colim.map_mono' œÜ (isColimitConstCocone J X) (h.isColimit) f (by cat_disch)

instance mono_map (j j' : J) (f : j ‚ü∂ j') : Mono (h.F.map f) :=
  ((h.ici j).iic ‚ü®j', leOfHom f‚ü©).mono

end MorphismProperty.TransfiniteCompositionOfShape

namespace IsGrothendieckAbelian

open MorphismProperty

instance : IsStableUnderTransfiniteComposition.{w} (monomorphisms C) where
  isStableUnderTransfiniteCompositionOfShape _ _ _ _ _ :=
    ‚ü®fun _ _ _ ‚ü®hf‚ü© ‚Ü¶ hf.mono‚ü©

end IsGrothendieckAbelian

end CategoryTheory
