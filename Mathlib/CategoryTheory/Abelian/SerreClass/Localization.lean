/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.Algebra.Homology.ShortComplex.ExactFunctor
public import Mathlib.CategoryTheory.Abelian.SerreClass.MorphismProperty
public import Mathlib.CategoryTheory.Localization.CalculusOfFractions.Preadditive
public import Mathlib.CategoryTheory.Limits.ExactFunctor

/-!
# Localization with respect to a Serre class

The main definition in this file is `ObjectProperty.SerreClassLocalization.abelian`
which shows that if `L : C ‚•§ D` is a localization functor with respect to
the class of morphisms `P.isoModSerre` for a Serre class `P : ObjectProperty C`
in the abelian category `C`, then `D` is an abelian category.

We also show that a functor `G : D ‚•§ E` to an abelian category is exact iff
the composition `L ‚ãô G` is.

-/

@[expose] public section

universe v'' v' v u'' u' u

namespace CategoryTheory

open Limits ZeroObject

namespace ObjectProperty

variable {C : Type u} [Category.{v} C] [Abelian C]
  {D : Type u'} [Category.{v'} D]
  (L : C ‚•§ D) (P : ObjectProperty C) [P.IsSerreClass]
  {E : Type u''} [Category.{v''} E] [Abelian E]

lemma exists_epiModSerre_comp_eq_zero_iff {X Y : C} (f : X ‚ü∂ Y) :
    (‚àÉ (X' : C) (s : X' ‚ü∂ X) (_ : P.epiModSerre s), s ‚â´ f = 0) ‚Üî
        P (Abelian.image f) := by
  refine ‚ü®?_, fun hf ‚Ü¶ ?_‚ü©
  ¬∑ rintro ‚ü®X', s, hs, eq‚ü©
    have := P.epiModSerre.comp_mem s (Abelian.factorThruImage f) hs
      (epiModSerre_of_epi _ _)
    rwa [show s ‚â´ Abelian.factorThruImage f = 0 by cat_disch,
      epiModSerre_zero_iff] at this
  ¬∑ exact ‚ü®_, kernel.Œπ f, P.prop_of_iso (Abelian.coimageIsoImage f).symm hf, by simp‚ü©

lemma exists_isoModSerre_comp_eq_zero_iff {X Y : C} (f : X ‚ü∂ Y) :
    (‚àÉ (X' : C) (s : X' ‚ü∂ X) (_ : P.isoModSerre s), s ‚â´ f = 0) ‚Üî
        P (Abelian.image f) := by
  refine ‚ü®?_, fun hf ‚Ü¶ ?_‚ü©
  ¬∑ rintro ‚ü®Y', s, hs, eq‚ü©
    rw [‚Üê exists_epiModSerre_comp_eq_zero_iff P]
    exact ‚ü®Y', s, hs.2, eq‚ü©
  ¬∑ refine ‚ü®_, kernel.Œπ f, ?_, by simp‚ü©
    simpa only [isoModSerre_iff_of_mono] using
      P.prop_of_iso (Abelian.coimageIsoImage f).symm hf

lemma exists_comp_monoModSerre_eq_zero_iff {X Y : C} (f : X ‚ü∂ Y) :
    (‚àÉ (Y' : C) (s : Y ‚ü∂ Y') (_ : P.monoModSerre s), f ‚â´ s = 0) ‚Üî
        P (Abelian.image f) := by
  refine ‚ü®?_, fun hf ‚Ü¶ ?_‚ü©
  ¬∑ rintro ‚ü®Y', s, hs, eq‚ü©
    apply P.prop_of_iso (Abelian.coimageIsoImage f)
    have := P.monoModSerre.comp_mem (Abelian.factorThruCoimage f) s
      (monoModSerre_of_mono _ _) hs
    rwa [show Abelian.factorThruCoimage f ‚â´ s = 0 by cat_disch,
      monoModSerre_zero_iff] at this
  ¬∑ exact ‚ü®_, cokernel.œÄ f, hf, by simp‚ü©

lemma exists_comp_isoModSerre_eq_zero_iff {X Y : C} (f : X ‚ü∂ Y) :
    (‚àÉ (Y' : C) (s : Y ‚ü∂ Y') (_ : P.isoModSerre s), f ‚â´ s = 0) ‚Üî
        P (Abelian.image f) := by
  refine ‚ü®?_, fun hf ‚Ü¶ ?_‚ü©
  ¬∑ rintro ‚ü®Y', s, hs, eq‚ü©
    rw [‚Üê exists_comp_monoModSerre_eq_zero_iff P]
    exact ‚ü®Y', s, hs.1, eq‚ü©
  ¬∑ refine ‚ü®_, cokernel.œÄ f, by rwa [isoModSerre_iff_of_epi], by simp‚ü©

variable {P} in
lemma monoModSerre.isoModSerre_factorThruImage
    {X Y : C} {f : X ‚ü∂ Y} (hf : P.monoModSerre f) :
    P.isoModSerre (Abelian.factorThruImage f) := by
  rw [isoModSerre_iff_of_epi]
  exact P.prop_of_iso
    (asIso (kernel.map _ f (ùüô _) (Abelian.image.Œπ f) (by simp))).symm hf

variable {P} in
lemma epiModSerre.isoModSerre_image_Œπ
    {X Y : C} {f : X ‚ü∂ Y} (hf : P.epiModSerre f) :
    P.isoModSerre (Abelian.image.Œπ f) := by
  rw [isoModSerre_iff_of_mono]
  dsimp [epiModSerre] at hf ‚ä¢
  exact P.prop_of_iso
    (asIso (cokernel.map f _ (Abelian.factorThruImage f) (ùüô Y) (by simp))) hf

namespace SerreClassLocalization

instance : P.isoModSerre.HasLeftCalculusOfFractions where
  exists_leftFraction X Y œÜ :=
    ‚ü®{s := pushout.inl œÜ.f œÜ.s
      f := pushout.inr œÜ.f œÜ.s,
      hs := MorphismProperty.pushout_inl _ _ œÜ.hs}, pushout.condition‚ü©
  ext X' X Y f‚ÇÅ f‚ÇÇ s hs eq := by
    refine ‚ü®_, cokernel.œÄ (f‚ÇÅ - f‚ÇÇ), ?_, ?_‚ü©
    ¬∑ rw [isoModSerre_iff_of_epi]
      exact (exists_isoModSerre_comp_eq_zero_iff P _).1 ‚ü®_, s, hs, by simpa [sub_eq_zero]‚ü©
    ¬∑ simpa only [Preadditive.sub_comp, sub_eq_zero] using cokernel.condition (f‚ÇÅ - f‚ÇÇ)

instance : P.isoModSerre.HasRightCalculusOfFractions where
  exists_rightFraction X Y œÜ :=
    ‚ü®{s := pullback.fst œÜ.f œÜ.s
      f := pullback.snd œÜ.f œÜ.s,
      hs := MorphismProperty.pullback_fst _ _ œÜ.hs}, pullback.condition‚ü©
  ext X Y Y' f‚ÇÅ f‚ÇÇ s hs eq := by
    refine ‚ü®_, kernel.Œπ (f‚ÇÅ - f‚ÇÇ), ?_, ?_‚ü©
    ¬∑ rw [isoModSerre_iff_of_mono]
      exact P.prop_of_iso (Abelian.coimageIsoImage (f‚ÇÅ - f‚ÇÇ)).symm
        ((exists_comp_isoModSerre_eq_zero_iff P _).1 ‚ü®_ ,s, hs, by simpa [sub_eq_zero]‚ü©)
    ¬∑ simpa only [Preadditive.comp_sub, sub_eq_zero] using kernel.condition (f‚ÇÅ - f‚ÇÇ)

noncomputable example : Preadditive P.isoModSerre.Localization := inferInstance

variable [L.IsLocalization P.isoModSerre] [Preadditive D] [L.Additive]

include L P

lemma isZero_obj_iff (X : C) :
    IsZero (L.obj X) ‚Üî P X := by
  simp only [IsZero.iff_id_eq_zero, ‚Üê L.map_id, ‚Üê L.map_zero,
    MorphismProperty.map_eq_iff_precomp L P.isoModSerre,
    Category.comp_id, comp_zero, exists_prop, exists_eq_right]
  refine ‚ü®?_, fun _ ‚Ü¶ ‚ü®X, by simpa‚ü©‚ü©
  rintro ‚ü®Y, h‚ü©
  simpa using h.2

lemma hasZeroObject : HasZeroObject D :=
  ‚ü®L.obj 0, by simpa [isZero_obj_iff L P] using P.prop_zero‚ü©

lemma map_eq_zero_iff {X Y : C} (f : X ‚ü∂ Y) :
    L.map f = 0 ‚Üî P (Abelian.image f) := by
  rw [‚Üê L.map_zero, MorphismProperty.map_eq_iff_precomp L P.isoModSerre]
  simp [‚Üê exists_isoModSerre_comp_eq_zero_iff P]

lemma map_comp_eq_zero_iff_of_epi_mono {X Z Y : C} (f : X ‚ü∂ Z) (g : Z ‚ü∂ Y)
    [Epi f] [Mono g] :
    L.map f ‚â´ L.map g = 0 ‚Üî P Z := by
  rw [‚Üê L.map_comp, map_eq_zero_iff L P]
  have := strongEpi_of_epi f
  exact P.prop_iff_of_iso (Abelian.imageIsoImage _ ‚â™‚â´ (image.isoStrongEpiMono f g rfl).symm)

lemma mono_map_iff {X Y : C} (f : X ‚ü∂ Y) :
    Mono (L.map f) ‚Üî P.monoModSerre f := by
  have := Localization.essSurj L P.isoModSerre
  refine ‚ü®fun _ ‚Ü¶ ?_, fun hf ‚Ü¶ ?_‚ü©
  ¬∑ have hf : L.map (kernel.Œπ f) = 0 := by
      rw [‚Üê cancel_mono (L.map f), zero_comp, ‚Üê L.map_comp,
        kernel.condition, L.map_zero]
    simpa [hf] using map_comp_eq_zero_iff_of_epi_mono L P (ùüô _) (kernel.Œπ f)
  ¬∑ suffices ‚àÄ ‚¶ÉZ : C‚¶Ñ (z : Z ‚ü∂ X) (hz : L.map z ‚â´ L.map f = 0), L.map z = 0 by
      rw [Preadditive.mono_iff_cancel_zero]
      intro W z hz
      obtain ‚ü®œÜ, hœÜ‚ü© := Localization.exists_rightFraction L P.isoModSerre
        ((L.objObjPreimageIso W).hom ‚â´ z)
      have hs := Localization.inverts L P.isoModSerre œÜ.s œÜ.hs
      rw [‚Üê cancel_epi (L.objObjPreimageIso W).hom, comp_zero, hœÜ,
        ‚Üê cancel_epi (L.map œÜ.s), comp_zero,
        MorphismProperty.RightFraction.map_s_comp_map]
      apply this œÜ.f
      have : L.map œÜ.s ‚â´ (L.objObjPreimageIso W).hom ‚â´ z = L.map œÜ.f := by cat_disch
      rw [‚Üê this, Category.assoc, Category.assoc, hz, comp_zero, comp_zero]
    intro Z z hz
    rw [‚Üê L.map_comp] at hz
    rw [map_eq_zero_iff L P, ‚Üê exists_comp_monoModSerre_eq_zero_iff P] at hz ‚ä¢
    obtain ‚ü®W, s, hs, eq‚ü© := hz
    exact ‚ü®W, f ‚â´ s, MorphismProperty.comp_mem _ _ _ hf hs, by simpa using eq‚ü©

lemma epi_map_iff {X Y : C} (f : X ‚ü∂ Y) :
    Epi (L.map f) ‚Üî P.epiModSerre f := by
  have := Localization.essSurj L P.isoModSerre
  refine ‚ü®fun _ ‚Ü¶ ?_, fun hf ‚Ü¶ ?_‚ü©
  ¬∑ have hf : L.map (cokernel.œÄ f) = 0 := by
      rw [‚Üê cancel_epi (L.map f), comp_zero, ‚Üê L.map_comp,
        cokernel.condition, L.map_zero]
    simpa [hf] using map_comp_eq_zero_iff_of_epi_mono L P (cokernel.œÄ f) (ùüô _)
  ¬∑ suffices ‚àÄ ‚¶ÉZ : C‚¶Ñ (z : Y ‚ü∂ Z) (hz : L.map f ‚â´ L.map z = 0), L.map z = 0 by
      rw [Preadditive.epi_iff_cancel_zero]
      intro W z hz
      obtain ‚ü®œÜ, hœÜ‚ü© := Localization.exists_leftFraction L P.isoModSerre
        (z ‚â´ (L.objObjPreimageIso W).inv)
      have hs := Localization.inverts L P.isoModSerre œÜ.s œÜ.hs
      rw [‚Üê cancel_mono (L.objObjPreimageIso W).inv, zero_comp, hœÜ,
        ‚Üê cancel_mono (L.map œÜ.s), zero_comp,
        MorphismProperty.LeftFraction.map_comp_map_s]
      apply this œÜ.f
      have : L.map œÜ.f = z ‚â´ (L.objObjPreimageIso W).inv ‚â´ L.map œÜ.s := by
        simp [reassoc_of% hœÜ]
      rw [this, reassoc_of% hz, zero_comp]
    intro Z z hz
    rw [‚Üê L.map_comp] at hz
    rw [map_eq_zero_iff L P, ‚Üê exists_epiModSerre_comp_eq_zero_iff P] at hz ‚ä¢
    obtain ‚ü®W, s, hs, eq‚ü© := hz
    refine ‚ü®_, s ‚â´ f, MorphismProperty.comp_mem _ _ _ hs hf, by simpa‚ü©

lemma inverseImage_monomorphisms :
    (MorphismProperty.monomorphisms _).inverseImage L = P.monoModSerre := by
  ext
  simp [mono_map_iff L P]

lemma inverseImage_epimorphisms :
    (MorphismProperty.epimorphisms _).inverseImage L = P.epiModSerre := by
  ext
  simp [epi_map_iff L P]

lemma preservesMonomorphisms : L.PreservesMonomorphisms where
  preserves f _ := by simpa only [mono_map_iff _ P] using P.monoModSerre_of_mono f

lemma preservesEpimorphisms : L.PreservesEpimorphisms where
  preserves f _ := by simpa only [epi_map_iff _ P] using P.epiModSerre_of_epi f

lemma mono_iff {X Y : D} (f : X ‚ü∂ Y) :
    Mono f ‚Üî ‚àÉ (X' Y' : C) (f' : X' ‚ü∂ Y') (_ : Mono f'),
      Nonempty (Arrow.mk (L.map f') ‚âÖ Arrow.mk f) := by
  have := preservesMonomorphisms L P
  have := Localization.essSurj_mapArrow L P.isoModSerre
  refine ‚ü®fun _ ‚Ü¶ ?_, ?_‚ü©
  ¬∑ suffices ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y) (_ : Mono (L.map f)),
      ‚àÉ (X' Y' : C) (f' : X' ‚ü∂ Y') (_ : Mono f'),
          Nonempty (Arrow.mk (L.map f') ‚âÖ Arrow.mk (L.map f)) by
        let e := L.mapArrow.objObjPreimageIso (Arrow.mk f)
        obtain ‚ü®X', Y', f', _, ‚ü®e'‚ü©‚ü© := this _
          (((MorphismProperty.monomorphisms D).arrow_iso_iff e).2 (.infer_property f))
        exact ‚ü®_, _, f', inferInstance, ‚ü®e' ‚â™‚â´ e‚ü©‚ü©
    intro X Y f hf
    rw [mono_map_iff L P] at hf
    refine ‚ü®_, _, Abelian.image.Œπ f, inferInstance, ‚ü®Iso.symm ?_‚ü©‚ü©
    have := Localization.inverts L P.isoModSerre _ hf.isoModSerre_factorThruImage
    exact Arrow.isoMk (asIso (L.map (Abelian.factorThruImage f))) (Iso.refl _)
      (by simp [‚Üê L.map_comp])
  ¬∑ rintro ‚ü®X', Y', f', _, ‚ü®e‚ü©‚ü©
    exact ((MorphismProperty.monomorphisms D).arrow_mk_iso_iff e).1
      (by simpa using inferInstanceAs (Mono (L.map f')))

lemma epi_iff {X Y : D} (f : X ‚ü∂ Y) :
    Epi f ‚Üî ‚àÉ (X' Y' : C) (f' : X' ‚ü∂ Y') (_ : Epi f'),
      Nonempty (Arrow.mk (L.map f') ‚âÖ Arrow.mk f) := by
  have := preservesEpimorphisms L P
  have := Localization.essSurj_mapArrow L P.isoModSerre
  refine ‚ü®fun _ ‚Ü¶ ?_, ?_‚ü©
  ¬∑ suffices ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y) (_ : Epi (L.map f)),
      ‚àÉ (X' Y' : C) (f' : X' ‚ü∂ Y') (_ : Epi f'),
          Nonempty (Arrow.mk (L.map f') ‚âÖ Arrow.mk (L.map f)) by
        let e := L.mapArrow.objObjPreimageIso (Arrow.mk f)
        obtain ‚ü®X', Y', f', _, ‚ü®e'‚ü©‚ü© := this _
          (((MorphismProperty.epimorphisms D).arrow_iso_iff e).2 (.infer_property f))
        exact ‚ü®_, _, f', inferInstance, ‚ü®e' ‚â™‚â´ e‚ü©‚ü©
    intro X Y f hf
    rw [epi_map_iff L P] at hf
    refine ‚ü®_, _, Abelian.factorThruImage f, inferInstance, ‚ü®?_‚ü©‚ü©
    have := Localization.inverts L P.isoModSerre _ hf.isoModSerre_image_Œπ
    refine Arrow.isoMk (Iso.refl _) (asIso (L.map (Abelian.image.Œπ f))) (by simp [‚Üê L.map_comp])
  ¬∑ rintro ‚ü®X', Y', f', _, ‚ü®e‚ü©‚ü©
    exact ((MorphismProperty.epimorphisms D).arrow_mk_iso_iff e).1
      (by simpa using inferInstanceAs (Epi (L.map f')))

lemma preservesKernel {X Y : C} (f : X ‚ü∂ Y) :
    PreservesLimit (parallelPair f 0) L := by
  have := preservesMonomorphisms L P
  have := Localization.essSurj L P.isoModSerre
  suffices ‚àÄ (W : D) (z : W ‚ü∂ L.obj X) (hz : z ‚â´ L.map f = 0),
      ‚àÉ (l : W ‚ü∂ L.obj (kernel f)), l ‚â´ L.map (kernel.Œπ f) = z from
    preservesLimit_of_preserves_limit_cone (kernelIsKernel f)
      ((KernelFork.isLimitMapConeEquiv _ L).2
        (Fork.IsLimit.ofExistsUnique
          (fun s ‚Ü¶ existsUnique_of_exists_of_unique
            (this _ _ (KernelFork.condition s))
            (fun _ _ h‚ÇÅ h‚ÇÇ ‚Ü¶ by simpa [cancel_mono] using h‚ÇÅ.trans h‚ÇÇ.symm))))
  intro W w hw
  wlog hw' : ‚àÉ (Z : C) (hZ : W = L.obj Z) (z : Z ‚ü∂ X), w = eqToHom hZ ‚â´ L.map z
      generalizing W
  ¬∑ obtain ‚ü®œÜ, hœÜ‚ü© := Localization.exists_rightFraction L P.isoModSerre
      ((L.objObjPreimageIso W).hom ‚â´ w)
    have _ := Localization.inverts L P.isoModSerre œÜ.s œÜ.hs
    rw [‚Üê cancel_epi (L.map œÜ.s),
      MorphismProperty.RightFraction.map_s_comp_map] at hœÜ
    obtain ‚ü®l, hl‚ü© := this _ (L.map œÜ.f) (by
      rw [‚Üê hœÜ, Category.assoc, Category.assoc, hw, comp_zero, comp_zero]) ‚ü®_, rfl, by simp‚ü©
    exact ‚ü®(L.objObjPreimageIso W).inv ‚â´ inv (L.map œÜ.s) ‚â´ l, by simp [hl, ‚Üê hœÜ]‚ü©
  obtain ‚ü®Z, rfl, z, rfl‚ü© := hw'
  simp only [eqToHom_refl, Category.id_comp, ‚Üê L.map_comp] at hw
  rw [map_eq_zero_iff L P, ‚Üê exists_isoModSerre_comp_eq_zero_iff P] at hw
  obtain ‚ü®Z', t, ht, fac‚ü© := hw
  have := Localization.inverts L P.isoModSerre t ht
  rw [‚Üê Category.assoc] at fac
  refine ‚ü®inv (L.map t) ‚â´ L.map (kernel.lift _ _ fac), ?_‚ü©
  simp only [Category.assoc, eqToHom_refl, Category.id_comp, IsIso.inv_comp_eq,
    ‚Üê L.map_comp, kernel.lift_Œπ]

lemma preservesCokernel {X Y : C} (f : X ‚ü∂ Y) :
    PreservesColimit (parallelPair f 0) L := by
  have := preservesEpimorphisms L P
  have := Localization.essSurj L P.isoModSerre
  suffices ‚àÄ (W : D) (z : L.obj Y ‚ü∂ W) (hz : L.map f ‚â´ z = 0),
      ‚àÉ (l : L.obj (cokernel f) ‚ü∂ W), L.map (cokernel.œÄ  f) ‚â´ l = z from
    preservesColimit_of_preserves_colimit_cocone (cokernelIsCokernel f)
      ((CokernelCofork.isColimitMapCoconeEquiv _ L).2
        (Cofork.IsColimit.ofExistsUnique
          (fun s ‚Ü¶ existsUnique_of_exists_of_unique
            (this _ _ (CokernelCofork.condition s))
            (fun _ _ h‚ÇÅ h‚ÇÇ ‚Ü¶ by simpa [cancel_epi] using h‚ÇÅ.trans h‚ÇÇ.symm))))
  intro W w hw
  wlog hw' : ‚àÉ (Z : C) (hZ : L.obj Z = W) (z : Y ‚ü∂ Z), w = L.map z ‚â´ eqToHom hZ
      generalizing W
  ¬∑ obtain ‚ü®œÜ, hœÜ‚ü© := Localization.exists_leftFraction L P.isoModSerre
      (w ‚â´ (L.objObjPreimageIso W).inv)
    have _ := Localization.inverts L P.isoModSerre œÜ.s œÜ.hs
    rw [‚Üê cancel_mono (L.map œÜ.s), Category.assoc,
      MorphismProperty.LeftFraction.map_comp_map_s] at hœÜ
    obtain ‚ü®l, hl‚ü© := this _ (L.map œÜ.f) (by rw [‚Üê hœÜ, reassoc_of% hw, zero_comp]) ‚ü®_, rfl, by simp‚ü©
    exact ‚ü®l ‚â´ inv (L.map œÜ.s) ‚â´ (L.objObjPreimageIso W).hom, by simp [reassoc_of% hl, ‚Üê hœÜ]‚ü©
  obtain ‚ü®Z, rfl, z, rfl‚ü© := hw'
  simp only [eqToHom_refl, Category.comp_id, ‚Üê L.map_comp] at hw
  rw [map_eq_zero_iff L P, ‚Üê exists_comp_isoModSerre_eq_zero_iff P] at hw
  obtain ‚ü®Z', t, ht, fac‚ü© := hw
  rw [Category.assoc] at fac
  have := Localization.inverts L P.isoModSerre t ht
  exact ‚ü®L.map (cokernel.desc _ _ fac) ‚â´ inv (L.map t), by simp [‚Üê L.map_comp_assoc]‚ü©

lemma hasKernels : HasKernels D where
  has_limit f := by
    obtain ‚ü®g, ‚ü®e‚ü©‚ü© :=
      (Localization.essSurj_mapArrow L P.isoModSerre).mem_essImage (Arrow.mk f)
    have := preservesKernel L P g.hom
    have : HasLimit (parallelPair (L.map g.hom) 0) :=
      ‚ü®_, (KernelFork.isLimitMapConeEquiv _ L).1
        (isLimitOfPreserves L (kernelIsKernel g.hom))‚ü©
    exact hasLimit_of_iso (show parallelPair (L.map g.hom) 0 ‚âÖ _ from
      parallelPair.ext (Arrow.leftFunc.mapIso e) (Arrow.rightFunc.mapIso e))

lemma hasCokernels : HasCokernels D where
  has_colimit f := by
    obtain ‚ü®g, ‚ü®e‚ü©‚ü© :=
      (Localization.essSurj_mapArrow L P.isoModSerre).mem_essImage (Arrow.mk f)
    have := preservesCokernel L P g.hom
    have : HasColimit (parallelPair (L.map g.hom) 0) :=
      ‚ü®_, (CokernelCofork.isColimitMapCoconeEquiv _ L).1
        (isColimitOfPreserves L (cokernelIsCokernel g.hom))‚ü©
    exact hasColimit_of_iso (show _ ‚âÖ parallelPair (L.map g.hom) 0 from
      parallelPair.ext (Arrow.leftFunc.mapIso e.symm) (Arrow.rightFunc.mapIso e.symm))

lemma hasEqualizers : HasEqualizers D :=
  have := hasKernels L P
  have {X Y : D} (f g : X ‚ü∂ Y) : HasEqualizer f g :=
    Preadditive.hasEqualizer_of_hasKernel _ _
  hasEqualizers_of_hasLimit_parallelPair _

lemma hasCoequalizers : HasCoequalizers D :=
  have := hasCokernels L P
  have {X Y : D} (f g : X ‚ü∂ Y) : HasCoequalizer f g := by
    exact Preadditive.hasCoequalizer_of_hasCokernel _ _
  hasCoequalizers_of_hasColimit_parallelPair _

lemma hasBinaryProducts : HasBinaryProducts D := by
  have := Localization.essSurj L P.isoModSerre
  have (X Y : D) : HasBinaryProduct X Y :=
    hasLimit_of_iso (show Limits.pair _ _ ‚âÖ _ from
      mapPairIso (L.objObjPreimageIso X) (L.objObjPreimageIso Y))
  exact hasBinaryProducts_of_hasLimit_pair D

lemma hasFiniteProducts : HasFiniteProducts D :=
  have := hasZeroObject L P
  have := hasBinaryProducts L P
  hasFiniteProducts_of_has_binary_and_terminal

lemma isNormalMonoCategory : IsNormalMonoCategory D where
  normalMonoOfMono f hf := by
    rw [mono_iff L P] at hf
    obtain ‚ü®X', Y', f', _, ‚ü®e‚ü©‚ü© := hf
    let hf' := normalMonoOfMono f'
    have := preservesKernel L P hf'.g
    refine ‚ü®NormalMono.ofArrowIso ?_ e‚ü©
    exact {
      Z := L.obj hf'.Z
      g := L.map hf'.g
      w := by rw [‚Üê L.map_comp]; simp [hf'.w]
      isLimit :=
        (KernelFork.isLimitMapConeEquiv _ L).1
          (isLimitOfPreserves L hf'.isLimit) }

lemma isNormalEpiCategory : IsNormalEpiCategory D where
  normalEpiOfEpi f hf := by
    rw [epi_iff L P] at hf
    obtain ‚ü®X', Y', f', _, ‚ü®e‚ü©‚ü© := hf
    let hf' := normalEpiOfEpi f'
    have := preservesCokernel L P hf'.g
    refine ‚ü®NormalEpi.ofArrowIso ?_ e‚ü©
    exact {
      W := L.obj hf'.W
      g := L.map hf'.g
      w := by rw [‚Üê L.map_comp]; simp [hf'.w]
      isColimit :=
        (CokernelCofork.isColimitMapCoconeEquiv _ L).1
          (isColimitOfPreserves L hf'.isColimit) }

/-- If `L : C ‚•§ D` is a localization functor with respect to a Serre class `P` in
the abelian category `C`, then `D` is an abelian category.
(Note that we assume that `D` has already been equipped with a preadditive structure,
and that `L` is additive. Otherwise, see the results in the file
`Mathlib/CategoryTheory/Localization/CalculusOfFractions/Preadditive.lean`
which applies because `P.isoModSerre` has a calculus of left and right fractions.) -/
@[stacks 02MS]
def abelian : Abelian D := by
  have := hasFiniteProducts L P
  have := hasKernels L P
  have := hasCokernels L P
  have := isNormalMonoCategory L P
  have := isNormalEpiCategory L P
  constructor

lemma preservesFiniteLimits : PreservesFiniteLimits L := by
  letI := abelian L P
  have := (Functor.preservesFiniteLimits_tfae L).out 3 2
  rw [this]
  intro _ _ f
  exact preservesKernel L P f

lemma preservesFiniteColimits : PreservesFiniteColimits L := by
  letI := abelian L P
  have := (Functor.preservesFiniteColimits_tfae L).out 3 2
  rw [this]
  intro _ _ f
  exact preservesCokernel L P f

lemma isIso_map_iff {X Y : C} (f : X ‚ü∂ Y) :
    IsIso (L.map f) ‚Üî P.isoModSerre f := by
  letI := abelian L P
  rw [isIso_iff_mono_and_epi, mono_map_iff L P, epi_map_iff L P, isoModSerre_iff]

lemma inverseImage_isomorphisms :
    (MorphismProperty.isomorphisms _).inverseImage L = P.isoModSerre := by
  ext
  simp [isIso_map_iff L P]

variable (G : D ‚•§ E)

lemma preservesFiniteLimits_comp_iff :
    PreservesFiniteLimits (L ‚ãô G) ‚Üî PreservesFiniteLimits G := by
  letI := abelian L P
  have := preservesFiniteLimits L P
  refine ‚ü®fun _ ‚Ü¶ ?_, fun _ ‚Ü¶ comp_preservesFiniteLimits _ _‚ü©
  have := (Localization.functor_additive_iff L P.isoModSerre G).2
    ((L ‚ãô G).additive_of_preserves_binary_products)
  refine ((Functor.preservesFiniteLimits_tfae G).out 2 3).1 (fun _ _ f ‚Ü¶ ?_)
  obtain ‚ü®f', ‚ü®iso‚ü©‚ü© :=
    (Localization.essSurj_mapArrow L P.isoModSerre).mem_essImage (Arrow.mk f)
  have : PreservesLimit (parallelPair (L.map f'.hom) 0) G :=
    preservesLimit_of_preserves_limit_cone
      ((KernelFork.isLimitMapConeEquiv _ _).1
        (isLimitOfPreserves L (kernelIsKernel f'.hom)))
          ((KernelFork.isLimitMapConeEquiv _ G).2
            ((KernelFork.isLimitMapConeEquiv _ (L ‚ãô G)).1
              (isLimitOfPreserves (L ‚ãô G) (kernelIsKernel f'.hom))))
  exact preservesLimit_of_iso_diagram G
    (show parallelPair (L.map f'.hom) 0 ‚âÖ parallelPair f 0 from
      parallelPair.ext (Arrow.leftFunc.mapIso iso) (Arrow.rightFunc.mapIso iso))

lemma preservesFiniteColimits_comp_iff :
    PreservesFiniteColimits (L ‚ãô G) ‚Üî PreservesFiniteColimits G := by
  letI := abelian L P
  have := preservesFiniteColimits L P
  refine ‚ü®fun _ ‚Ü¶ ?_, fun _ ‚Ü¶ comp_preservesFiniteColimits _ _‚ü©
  have := (Localization.functor_additive_iff L P.isoModSerre G).2 (by
    have := preservesBinaryBiproducts_of_preservesBinaryCoproducts (L ‚ãô G)
    exact Functor.additive_of_preservesBinaryBiproducts _)
  refine ((Functor.preservesFiniteColimits_tfae G).out 2 3).1 (fun _ _ f ‚Ü¶ ?_)
  obtain ‚ü®f', ‚ü®iso‚ü©‚ü© :=
    (Localization.essSurj_mapArrow L P.isoModSerre).mem_essImage (Arrow.mk f)
  have : PreservesColimit (parallelPair (L.map f'.hom) 0) G :=
    preservesColimit_of_preserves_colimit_cocone
      ((CokernelCofork.isColimitMapCoconeEquiv _ _).1
        (isColimitOfPreserves L (cokernelIsCokernel f'.hom)))
          ((CokernelCofork.isColimitMapCoconeEquiv _ G).2
            ((CokernelCofork.isColimitMapCoconeEquiv _ (L ‚ãô G)).1
              (isColimitOfPreserves (L ‚ãô G) (cokernelIsCokernel f'.hom))))
  exact preservesColimit_of_iso_diagram G
    (show parallelPair (L.map f'.hom) 0 ‚âÖ parallelPair f 0 from
      parallelPair.ext (Arrow.leftFunc.mapIso iso) (Arrow.rightFunc.mapIso iso))

lemma exactFunctor_comp_iff :
    exactFunctor _ _ (L ‚ãô G) ‚Üî exactFunctor _ _ G := by
  simp [preservesFiniteLimits_comp_iff L P, preservesFiniteColimits_comp_iff L P]

variable (E)

/-- When `L : C ‚•§ D` is a localization functor with respect to a Serre class
in the abelian category `C`, this is the functor `(D ‚•§‚Çë E) ‚•§ C ‚•§‚Çë E`
obtained by precomposition with `L`. -/
def whiskeringLeft : (D ‚•§‚Çë E) ‚•§ C ‚•§‚Çë E :=
  ObjectProperty.lift _
    (ObjectProperty.Œπ _ ‚ãô (Functor.whiskeringLeft _ _ _).obj L) (fun G ‚Ü¶ by
      dsimp
      simpa only [exactFunctor_comp_iff L P] using G.property)

@[simp]
lemma whiskeringLeft_obj_obj (G : D ‚•§‚Çë E) :
    ((whiskeringLeft L P E).obj G).obj = L ‚ãô G.obj := rfl

/-- When `L : C ‚•§ D` is a localization functor with respect to a Serre class
in the abelian category `C`, the functor `whiskeringLeft L P E: (D ‚•§‚Çë E) ‚•§ C ‚•§‚Çë E`
is fully faithful. -/
noncomputable def fullyFaithfulWhiskeringLeft :
    (whiskeringLeft L P E).FullyFaithful :=
  Functor.FullyFaithful.ofCompFaithful (G := ObjectProperty.Œπ _)
   ((exactFunctor D E).fullyFaithfulŒπ.comp
    (Localization.fullyFaithfulWhiskeringLeft L P.isoModSerre E))

instance : (whiskeringLeft L P E).Faithful :=
  (fullyFaithfulWhiskeringLeft L P E).faithful

instance : (whiskeringLeft L P E).Full :=
  (fullyFaithfulWhiskeringLeft L P E).full

lemma essImage_whiskeringLeft :
    (whiskeringLeft L P E).essImage =
      fun G ‚Ü¶ P.isoModSerre.IsInvertedBy G.obj := by
  ext F
  refine ‚ü®?_, fun hF ‚Ü¶ ?_‚ü©
  ¬∑ rintro ‚ü®G, ‚ü®e‚ü©‚ü©
    rw [‚Üê MorphismProperty.IsInvertedBy.iff_of_iso _
      (show  L ‚ãô G.obj ‚âÖ F.obj from (ObjectProperty.Œπ _).mapIso e)]
    exact MorphismProperty.IsInvertedBy.of_comp _ _ (Localization.inverts L _) _
  ¬∑ refine ‚ü®‚ü®Localization.lift F.obj hF L, ?_‚ü©,
      ‚ü®ObjectProperty.isoMk _ (Localization.fac F.obj hF L)‚ü©‚ü©
    rw [‚Üê exactFunctor_comp_iff L P]
    exact ObjectProperty.prop_of_iso _ (Localization.fac F.obj hF L).symm F.property

end SerreClassLocalization

end ObjectProperty

end CategoryTheory
