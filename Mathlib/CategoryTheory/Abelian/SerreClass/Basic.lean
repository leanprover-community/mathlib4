/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Abelian.Basic
import Mathlib.CategoryTheory.ClosedUnderIsomorphisms
import Mathlib.Algebra.Homology.ShortComplex.ShortExact

/-!
# Serre classes

For any abelian category `C`, we introduce the type `SerreClass C` of Serre classes
(also known as "Serre subcategories"). A Serre class consists of a predicate
on objects of `C` which hold for the zero object and is such that if
`0 ‚ü∂ X‚ÇÅ ‚ü∂ X‚ÇÇ ‚ü∂ X‚ÇÉ ‚ü∂ 0` is a short exact sequence, then `X‚ÇÇ` belongs to the class
iff `X‚ÇÅ` and `X‚ÇÉ` do. This implies that the class is stable under subobjects,
quotients, extensions. As a result, if `X‚ÇÅ ‚ü∂ X‚ÇÇ ‚ü∂ X‚ÇÉ` is an exact sequence such
that `X‚ÇÅ` and `X‚ÇÉ` belong to the class, then so does `X‚ÇÇ` (see `SerreClass.prop_of_exact`).

## Future works

* Construct the localization of `C` with respect to a Serre class.

-/

universe v u

namespace CategoryTheory

open Limits ZeroObject

-- to be moved to `Algebra.Homology.ShortComplex.ShortExact`
lemma ShortComplex.Exact.shortExact {C : Type u} [Category.{v} C] [Preadditive C]
    {S : ShortComplex C} (hS : S.Exact) (h : S.HomologyData) :
    (ShortComplex.mk _ _ (h.exact_iff_i_p_zero.1 hS)).ShortExact where
  exact := by
    have := hS.epi_f' h.left
    have := hS.mono_g' h.right
    let S' := ShortComplex.mk h.left.i S.g (by simp)
    let S'' := ShortComplex.mk _ _ (h.exact_iff_i_p_zero.1 hS)
    let a : S ‚ü∂ S' :=
      { œÑ‚ÇÅ := h.left.f'
        œÑ‚ÇÇ := ùüô _
        œÑ‚ÇÉ := ùüô _ }
    let b : S'' ‚ü∂ S' :=
      { œÑ‚ÇÅ := ùüô _
        œÑ‚ÇÇ := ùüô _
        œÑ‚ÇÉ := h.right.g' }
    rwa [ShortComplex.exact_iff_of_epi_of_isIso_of_mono b,
      ‚Üê ShortComplex.exact_iff_of_epi_of_isIso_of_mono a]

variable (C : Type u) [Category.{v} C] [Abelian C]

/-- A Serre class in an abelian category consists of predicate which
is stable by subobject, quotient, extension and for the zero object. -/
structure SerreClass where
  /-- a predicate on objects -/
  prop : C ‚Üí Prop
  prop_zero : prop 0
  iff_of_shortExact {S : ShortComplex C} (hS : S.ShortExact) :
    prop S.X‚ÇÇ ‚Üî prop S.X‚ÇÅ ‚àß prop S.X‚ÇÉ

namespace SerreClass

variable {C} (c : SerreClass C)

section

variable {S : ShortComplex C} (hS : S.ShortExact)

include hS

lemma prop_X‚ÇÇ_of_shortExact (h‚ÇÅ : c.prop S.X‚ÇÅ) (h‚ÇÉ : c.prop S.X‚ÇÉ) : c.prop S.X‚ÇÇ := by
  rw [c.iff_of_shortExact hS]
  constructor <;> assumption

lemma prop_X‚ÇÅ_of_shortExact (h‚ÇÇ : c.prop S.X‚ÇÇ) : c.prop S.X‚ÇÅ := by
  rw [c.iff_of_shortExact hS] at h‚ÇÇ
  exact h‚ÇÇ.1

lemma prop_X‚ÇÉ_of_shortExact (h‚ÇÇ : c.prop S.X‚ÇÇ) : c.prop S.X‚ÇÉ := by
  rw [c.iff_of_shortExact hS] at h‚ÇÇ
  exact h‚ÇÇ.2

end

lemma prop_of_mono {X Y : C} (f : X ‚ü∂ Y) [Mono f] (hY : c.prop Y) : c.prop X := by
  have hS : (ShortComplex.mk _ _ (cokernel.condition f)).ShortExact :=
    { exact := ShortComplex.exact_of_g_is_cokernel _ (cokernelIsCokernel f) }
  exact c.prop_X‚ÇÅ_of_shortExact hS hY

lemma prop_of_epi {X Y : C} (f : X ‚ü∂ Y) [Epi f] (hX : c.prop X) : c.prop Y := by
  have hS : (ShortComplex.mk _ _ (kernel.condition f)).ShortExact :=
    { exact := ShortComplex.exact_of_f_is_kernel _ (kernelIsKernel f) }
  exact c.prop_X‚ÇÉ_of_shortExact hS hX

lemma prop_iff_of_iso {X Y : C} (e : X ‚âÖ Y) : c.prop X ‚Üî c.prop Y :=
  ‚ü®c.prop_of_epi e.hom, c.prop_of_mono e.hom‚ü©

instance : ClosedUnderIsomorphisms c.prop where
  of_iso e hX := by rwa [‚Üê c.prop_iff_of_iso e]

lemma prop_of_isZero {X : C} (hX : IsZero X) : c.prop X := by
  simpa only [c.prop_iff_of_iso (hX.iso (isZero_zero _))] using c.prop_zero

lemma prop_biprod {X‚ÇÅ X‚ÇÇ : C} (h‚ÇÅ : c.prop X‚ÇÅ) (h‚ÇÇ : c.prop X‚ÇÇ) :
    c.prop (X‚ÇÅ ‚äû X‚ÇÇ) :=
  c.prop_X‚ÇÇ_of_shortExact
    (ShortComplex.Splitting.ofHasBinaryBiproduct X‚ÇÅ X‚ÇÇ).shortExact h‚ÇÅ h‚ÇÇ

lemma prop_of_exact {S : ShortComplex C} (hS : S.Exact)
    (h‚ÇÅ : c.prop S.X‚ÇÅ) (h‚ÇÉ : c.prop S.X‚ÇÉ) : c.prop S.X‚ÇÇ := by
  let d := S.homologyData
  have := hS.epi_f' d.left
  have := hS.mono_g' d.right
  exact (c.prop_X‚ÇÇ_of_shortExact (hS.shortExact d)
    (c.prop_of_epi d.left.f' h‚ÇÅ) (c.prop_of_mono d.right.g' h‚ÇÉ) :)

end SerreClass

end CategoryTheory
