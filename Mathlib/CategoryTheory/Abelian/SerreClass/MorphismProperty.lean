/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.Algebra.Homology.Square
public import Mathlib.CategoryTheory.Abelian.SerreClass.Basic
public import Mathlib.CategoryTheory.Abelian.DiagramLemmas.KernelCokernelComp
public import Mathlib.CategoryTheory.MorphismProperty.Composition
public import Mathlib.CategoryTheory.MorphismProperty.Retract
public import Mathlib.CategoryTheory.MorphismProperty.Limits
public import Mathlib.CategoryTheory.MorphismProperty.IsInvertedBy

/-!
# The class of isomorphisms modulo a Serre class

Let `C` be an abelian category and `P : ObjectProperty C` a Serre class.
We define `P.isoModSerre : MorphismProperty C`, which is the class of
morphisms `f` such that `kernel f` and `cokernel f` satisfy `P`.
We show that `P.isoModSerre` is multiplicative, satisfies the two out
of three property and is stable under retracts. (Similarly, we define
`P.monoModSerre` and `P.epiModSerre`.)

## TODO

* show that a localized category with respect to `P.isoModSerre` is abelian.

-/

@[expose] public section

universe v v' u u'

namespace CategoryTheory

open Category Limits ZeroObject MorphismProperty

section -- to be moved

variable {C : Type*} [Category* C] [HasZeroMorphisms C]
  {X‚ÇÅ X‚ÇÇ X‚ÇÉ X‚ÇÑ : C} {t : X‚ÇÅ ‚ü∂ X‚ÇÇ} {l : X‚ÇÅ ‚ü∂ X‚ÇÉ} {r : X‚ÇÇ ‚ü∂ X‚ÇÑ} {b : X‚ÇÉ ‚ü∂ X‚ÇÑ}

lemma Abelian.isIso_kernel_map_of_isPullback [HasKernel t] [HasKernel b]
    (sq : IsPullback t l r b) :
    IsIso (kernel.map _ _ _ _ sq.w) :=
  ‚ü®kernel.lift _ (sq.lift 0 (kernel.Œπ b) (by simp)) (by simp),
    by ext; exact sq.hom_ext (by cat_disch) (by cat_disch), by cat_disch‚ü©

lemma Abelian.isIso_cokernel_map_of_isPushout [HasCokernel t] [HasCokernel b]
    (sq : IsPushout t l r b) :
    IsIso (cokernel.map _ _ _ _ sq.w) :=
  ‚ü®cokernel.desc _ (sq.desc (cokernel.œÄ t) 0 (by simp)) (by simp),
    by cat_disch, by ext; exact sq.hom_ext (by cat_disch) (by cat_disch)‚ü©

end

variable {C : Type u} [Category.{v} C] [Abelian C]
  {D : Type u'} [Category.{v'} D] [Abelian D]

namespace Abelian -- to be moved

variable {X‚ÇÅ X‚ÇÇ X‚ÇÉ X‚ÇÑ : C} {t : X‚ÇÅ ‚ü∂ X‚ÇÇ} {l : X‚ÇÅ ‚ü∂ X‚ÇÉ} {r : X‚ÇÇ ‚ü∂ X‚ÇÑ} {b : X‚ÇÉ ‚ü∂ X‚ÇÑ}

lemma mono_cokernel_map_of_isPullback (sq : IsPullback t l r b) :
    Mono (cokernel.map _ _ _ _ sq.w) := by
  rw [Preadditive.mono_iff_cancel_zero]
  intro A‚ÇÄ z hz
  obtain ‚ü®A‚ÇÅ, œÄ‚ÇÅ, _, x‚ÇÇ, hx‚ÇÇ‚ü© :=
    surjective_up_to_refinements_of_epi (cokernel.œÄ t) z
  have : (ShortComplex.mk _ _ (cokernel.condition b)).Exact :=
    ShortComplex.exact_of_g_is_cokernel _ (cokernelIsCokernel b)
  obtain ‚ü®A‚ÇÇ, œÄ‚ÇÇ, _, x‚ÇÉ, hx‚ÇÉ‚ü© := this.exact_up_to_refinements (x‚ÇÇ ‚â´ r) (by
    simpa [hz] using hx‚ÇÇ.symm =‚â´ cokernel.map _ _ _ _ sq.w)
  obtain ‚ü®x‚ÇÅ, hx‚ÇÅ, rfl‚ü© := sq.exists_lift (œÄ‚ÇÇ ‚â´ x‚ÇÇ) x‚ÇÉ (by simpa)
  simp [‚Üê cancel_epi œÄ‚ÇÅ, ‚Üê cancel_epi œÄ‚ÇÇ, hx‚ÇÇ, ‚Üê reassoc_of% hx‚ÇÅ]

lemma epi_kernel_map_of_isPushout (sq : IsPushout t l r b) :
    Epi (kernel.map _ _ _ _ sq.w) := by
  rw [epi_iff_surjective_up_to_refinements]
  intro A‚ÇÄ z
  obtain ‚ü®A‚ÇÅ, œÄ‚ÇÅ, _, x‚ÇÅ, hx‚ÇÅ‚ü© := ((ShortComplex.mk _ _
    sq.cokernelCofork.condition).exact_of_g_is_cokernel
      sq.isColimitCokernelCofork).exact_up_to_refinements
        (z ‚â´ kernel.Œπ _ ‚â´ biprod.inr) (by simp)
  refine ‚ü®A‚ÇÅ, œÄ‚ÇÅ, inferInstance, -kernel.lift _ x‚ÇÅ ?_, ?_‚ü©
  ¬∑ simpa using hx‚ÇÅ.symm =‚â´ biprod.fst
  ¬∑ ext
    simpa using hx‚ÇÅ =‚â´ biprod.snd

end Abelian

instance : (monomorphisms C).IsStableUnderCobaseChange :=
  .mk' (fun _ _ _ _ _ _ (_ : Mono _) ‚Ü¶ inferInstanceAs (Mono _))

instance : (epimorphisms C).IsStableUnderBaseChange :=
  .mk' (fun _ _ _ _ _ _ (_ : Epi _) ‚Ü¶ inferInstanceAs (Epi _))

namespace ObjectProperty

variable (P : ObjectProperty C)

/-- The class of monomorphisms modulo a Serre class: given a
Serre class `P : ObjectProperty C`, this is the class of morphisms `f`
such that `kernel f` satisfies `P`. -/
@[nolint unusedArguments]
def monoModSerre [P.IsSerreClass] : MorphismProperty C :=
  fun _ _ f ‚Ü¶ P (kernel f)

/-- The class of epimorphisms modulo a Serre class: given a
Serre class `P : ObjectProperty C`, this is the class of morphisms `f`
such that `cokernel f` satisfies `P`. -/
@[nolint unusedArguments]
def epiModSerre [P.IsSerreClass] : MorphismProperty C :=
  fun _ _ f ‚Ü¶ P (cokernel f)

/-- The class of isomorphisms modulo a Serre class: given a
Serre class `P : ObjectProperty C`, this is the class of morphisms `f`
such that `kernel f` and `cokernel f` satisfy `P`. -/
@[nolint unusedArguments]
def isoModSerre [P.IsSerreClass] : MorphismProperty C :=
  P.monoModSerre ‚äì P.epiModSerre

variable [P.IsSerreClass]

lemma monoModSerre_iff {X Y : C} (f : X ‚ü∂ Y) :
    P.monoModSerre f ‚Üî P (kernel f) := Iff.rfl

lemma monomorphisms_le_monoModSerre : monomorphisms C ‚â§ P.monoModSerre :=
  fun _ _ f (_ : Mono f) ‚Ü¶ P.prop_of_isZero (isZero_kernel_of_mono f)

lemma monoModSerre_of_mono {X Y : C} (f : X ‚ü∂ Y) [Mono f] :
    P.monoModSerre f :=
  P.monomorphisms_le_monoModSerre f (monomorphisms.infer_property f)

lemma epiModSerre_iff {X Y : C} (f : X ‚ü∂ Y) :
    P.epiModSerre f ‚Üî P (cokernel f) := Iff.rfl

lemma epimorphisms_le_epiModSerre : epimorphisms C ‚â§ P.epiModSerre :=
  fun _ _ f (_ : Epi f) ‚Ü¶ P.prop_of_isZero (isZero_cokernel_of_epi f)

lemma epiModSerre_of_epi {X Y : C} (f : X ‚ü∂ Y) [Epi f] :
    P.epiModSerre f :=
  P.epimorphisms_le_epiModSerre f (epimorphisms.infer_property f)

lemma isoModSerre_iff {X Y : C} (f : X ‚ü∂ Y) :
    P.isoModSerre f ‚Üî P.monoModSerre f ‚àß P.epiModSerre f := Iff.rfl

lemma isoModSerre_iff_of_mono {X Y : C} (f : X ‚ü∂ Y) [Mono f] :
    P.isoModSerre f ‚Üî P.epiModSerre f := by
  have := P.monoModSerre_of_mono f
  rw [isoModSerre_iff]
  tauto

lemma isoModSerre_iff_of_epi {X Y : C} (f : X ‚ü∂ Y) [Epi f] :
    P.isoModSerre f ‚Üî P.monoModSerre f := by
  have := P.epiModSerre_of_epi f
  rw [isoModSerre_iff]
  tauto

lemma isoModSerre_of_mono {X Y : C} (f : X ‚ü∂ Y) [Mono f] (hf : P.epiModSerre f) :
    P.isoModSerre f := by
  rwa [isoModSerre_iff_of_mono]

lemma isoModSerre_of_epi {X Y : C} (f : X ‚ü∂ Y) [Epi f] (hf : P.monoModSerre f) :
    P.isoModSerre f := by
  rwa [isoModSerre_iff_of_epi]

lemma isomorphisms_le_isoModSerre : isomorphisms C ‚â§ P.isoModSerre :=
  fun _ _ f (_ : IsIso f) ‚Ü¶ ‚ü®P.monoModSerre_of_mono f, P.epiModSerre_of_epi f‚ü©

lemma isoModSerre_of_isIso {X Y : C} (f : X ‚ü∂ Y) [IsIso f] : P.isoModSerre f :=
  P.isomorphisms_le_isoModSerre f (isomorphisms.infer_property f)

instance : P.monoModSerre.IsMultiplicative where
  id_mem _ := P.monoModSerre_of_mono _
  comp_mem f g hf hg :=
    P.prop_X‚ÇÇ_of_exact ((kernelCokernelCompSequence_exact f g).exact 0) hf hg

instance : P.epiModSerre.IsMultiplicative where
  id_mem _ := P.epiModSerre_of_epi _
  comp_mem f g hf hg :=
    P.prop_X‚ÇÇ_of_exact ((kernelCokernelCompSequence_exact f g).exact 3) hf hg

instance : P.isoModSerre.IsMultiplicative := by
  dsimp only [isoModSerre]
  infer_instance

instance : P.monoModSerre.IsStableUnderRetracts where
  of_retract {X' Y' X Y} f' f h hf :=
    P.prop_of_mono (kernel.map f' f h.left.i h.right.i (by simp)) hf

instance : P.epiModSerre.IsStableUnderRetracts where
  of_retract {X' Y' X Y} f' f h hf :=
    P.prop_of_epi (cokernel.map f f' h.left.r h.right.r (by simp)) hf

instance : P.isoModSerre.IsStableUnderRetracts := by
  dsimp only [isoModSerre]
  infer_instance

instance : P.isoModSerre.HasTwoOutOfThreeProperty where
  of_postcomp f g hg hfg :=
    ‚ü®P.prop_of_mono (kernel.map f (f ‚â´ g) (ùüô _) g (by simp)) hfg.1,
      P.prop_X‚ÇÇ_of_exact ((kernelCokernelCompSequence_exact f g).exact 2) hg.1 hfg.2‚ü©
  of_precomp f g hf hfg :=
    ‚ü®P.prop_X‚ÇÇ_of_exact ((kernelCokernelCompSequence_exact f g).exact 1) hfg.1 hf.2,
      P.prop_of_epi (cokernel.map (f ‚â´ g) g f (ùüô _) (by simp)) hfg.2‚ü©

lemma le_kernel_of_isoModSerre_isInvertedBy (F : C ‚•§ D) [F.PreservesZeroMorphisms]
    (hF : P.isoModSerre.IsInvertedBy F) :
    P ‚â§ F.kernel := by
  intro X hX
  let f : 0 ‚ü∂ X := 0
  have := hF _ ((P.isoModSerre_iff_of_mono f).2
    ((P.prop_iff_of_iso cokernelZeroIsoTarget).2 hX))
  exact (asIso (F.map f)).isZero_iff.1 (F.map_isZero (isZero_zero C))

lemma isoModSerre_isInvertedBy_iff (F : C ‚•§ D)
    [PreservesFiniteLimits F] [PreservesFiniteColimits F] :
    P.isoModSerre.IsInvertedBy F ‚Üî P ‚â§ F.kernel := by
  refine ‚ü®P.le_kernel_of_isoModSerre_isInvertedBy F, fun hF X Y f ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ ?_‚ü©
  have : Mono (F.map f) :=
    (((ShortComplex.mk _ _ (kernel.condition f)).exact_of_f_is_kernel
      (kernelIsKernel f)).map F).mono_g (((hF _ h‚ÇÅ).eq_of_src _ _))
  have : Epi (F.map f) :=
    (((ShortComplex.mk _ _ (cokernel.condition f)).exact_of_g_is_cokernel
      (cokernelIsCokernel f)).map F).epi_f (((hF _ h‚ÇÇ).eq_of_tgt _ _))
  exact isIso_of_mono_of_epi (F.map f)

instance : P.monoModSerre.IsStableUnderBaseChange where
  of_isPullback sq h :=
    have := Abelian.isIso_kernel_map_of_isPullback sq.flip
    P.prop_of_iso (asIso (kernel.map _ _ _ _ sq.w.symm)).symm h

instance : P.epiModSerre.IsStableUnderBaseChange where
  of_isPullback sq h :=
    have := Abelian.mono_cokernel_map_of_isPullback sq.flip
    P.prop_of_mono (cokernel.map _ _ _ _ sq.w.symm) h

instance : P.isoModSerre.IsStableUnderBaseChange := by
  dsimp [isoModSerre]
  infer_instance

instance : P.monoModSerre.IsStableUnderCobaseChange where
  of_isPushout sq h :=
    have := Abelian.epi_kernel_map_of_isPushout sq.flip
    P.prop_of_epi (kernel.map _ _ _ _ sq.w.symm) h

instance : P.epiModSerre.IsStableUnderCobaseChange where
  of_isPushout sq h :=
    have := Abelian.isIso_cokernel_map_of_isPushout sq.flip
    P.prop_of_iso (asIso (cokernel.map _ _ _ _ sq.w.symm)) h

instance : P.isoModSerre.IsStableUnderCobaseChange := by
  dsimp [isoModSerre]
  infer_instance

end ObjectProperty

end CategoryTheory
