/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou, Nailin Guan
-/
import Mathlib.Algebra.Homology.DerivedCategory.Ext.EnoughProjectives
import Mathlib.CategoryTheory.Abelian.Exact
import Mathlib.Data.ENat.Lattice

/-!
# Projective dimension

In an abelian category `C`, we shall say that `X : C` has projective dimension `< n`
if all `Ext X Y i` vanish when `n ‚â§ i`. This defines a type class
`HasProjectiveDimensionLT X n`. We also define a type class
`HasProjectiveDimensionLE X n` as an abbreviation for
`HasProjectiveDimensionLT X (n + 1)`.
(Note that the fact that `X` is a zero object is equivalent to the condition
`HasProjectiveDimensionLT X 0`, but this cannot be expressed in terms of
`HasProjectiveDimensionLE`.)

We also define the projective dimension in `WithBot ‚Ñï‚àû` as `projectiveDimension`,
`projectiveDimension X = ‚ä•` iff `X` is zero and acts in common sense in the non-negative values.

-/

universe w v u

namespace CategoryTheory

open Abelian Limits ZeroObject

variable {C : Type u} [Category.{v} C] [Abelian C]

/-- An object `X` in an abelian category has projective dimension `< n` if
all `Ext X Y i` vanish when `n ‚â§ i`. See also `HasProjectiveDimensionLE`.
(Do not use the `subsingleton'` field directly. Use the constructor
`HasProjectiveDimensionLT.mk`, and the lemmas `hasProjectiveDimensionLT_iff` and
`Ext.eq_zero_of_hasProjectiveDimensionLT`.) -/
class HasProjectiveDimensionLT (X : C) (n : ‚Ñï) : Prop where mk' ::
  subsingleton' (i : ‚Ñï) (hi : n ‚â§ i) ‚¶ÉY : C‚¶Ñ :
    letI := HasExt.standard C
    Subsingleton (Ext.{max u v} X Y i)

/-- An object `X` in an abelian category has projective dimension `‚â§ n` if
all `Ext X Y i` vanish when `n + 1 ‚â§ i` -/
abbrev HasProjectiveDimensionLE (X : C) (n : ‚Ñï) : Prop :=
  HasProjectiveDimensionLT X (n + 1)

namespace HasProjectiveDimensionLT

variable [HasExt.{w} C] (X : C) (n : ‚Ñï)

lemma subsingleton [hX : HasProjectiveDimensionLT X n] (i : ‚Ñï) (hi : n ‚â§ i) (Y : C) :
    Subsingleton (Ext.{w} X Y i) := by
  letI := HasExt.standard C
  have := hX.subsingleton' i hi
  exact Ext.chgUniv.{w, max u v}.symm.subsingleton

variable {X n} in
lemma mk (hX : ‚àÄ (i : ‚Ñï) (_ : n ‚â§ i) ‚¶ÉY : C‚¶Ñ, ‚àÄ (e : Ext X Y i), e = 0) :
    HasProjectiveDimensionLT X n where
  subsingleton' i hi Y := by
    have : Subsingleton (Ext X Y i) := ‚ü®fun e‚ÇÅ e‚ÇÇ ‚Ü¶ by simp only [hX i hi]‚ü©
    letI := HasExt.standard C
    exact Ext.chgUniv.{max u v, w}.symm.subsingleton

end HasProjectiveDimensionLT

lemma Abelian.Ext.eq_zero_of_hasProjectiveDimensionLT [HasExt.{w} C]
    {X Y : C} {i : ‚Ñï} (e : Ext X Y i) (n : ‚Ñï) [HasProjectiveDimensionLT X n]
    (hi : n ‚â§ i) : e = 0 :=
  (HasProjectiveDimensionLT.subsingleton X n i hi Y).elim _ _

section

variable (X : C) (n : ‚Ñï)

lemma hasProjectiveDimensionLT_iff [HasExt.{w} C] :
    HasProjectiveDimensionLT X n ‚Üî
      ‚àÄ (i : ‚Ñï) (_ : n ‚â§ i) ‚¶ÉY : C‚¶Ñ, ‚àÄ (e : Ext X Y i), e = 0 :=
  ‚ü®fun _ _ hi _ e ‚Ü¶ e.eq_zero_of_hasProjectiveDimensionLT n hi,
    HasProjectiveDimensionLT.mk‚ü©

variable {X} in
lemma Limits.IsZero.hasProjectiveDimensionLT_zero (hX : IsZero X) :
    HasProjectiveDimensionLT X 0 := by
  letI := HasExt.standard C
  rw [hasProjectiveDimensionLT_iff]
  intro i hi Y e
  rw [‚Üê e.mk‚ÇÄ_id_comp, hX.eq_of_src (ùüô X) 0, Ext.mk‚ÇÄ_zero, Ext.zero_comp]

instance : HasProjectiveDimensionLT (0 : C) 0 :=
  (isZero_zero C).hasProjectiveDimensionLT_zero

lemma isZero_of_hasProjectiveDimensionLT_zero [HasProjectiveDimensionLT X 0] : IsZero X := by
  letI := HasExt.standard C
  rw [IsZero.iff_id_eq_zero]
  apply Ext.homEquiv‚ÇÄ.symm.injective
  simpa only [Ext.homEquiv‚ÇÄ_symm_apply, Ext.mk‚ÇÄ_zero]
    using Abelian.Ext.eq_zero_of_hasProjectiveDimensionLT _ 0 (by rfl)

lemma hasProjectiveDimensionLT_zero_iff_isZero : HasProjectiveDimensionLT X 0 ‚Üî IsZero X :=
  ‚ü®fun _ ‚Ü¶ isZero_of_hasProjectiveDimensionLT_zero X, fun h ‚Ü¶ h.hasProjectiveDimensionLT_zero‚ü©

lemma hasProjectiveDimensionLT_of_ge (m : ‚Ñï) (h : n ‚â§ m)
    [HasProjectiveDimensionLT X n] :
    HasProjectiveDimensionLT X m := by
  letI := HasExt.standard C
  rw [hasProjectiveDimensionLT_iff]
  intro i hi Y e
  exact e.eq_zero_of_hasProjectiveDimensionLT n (by cutsat)

instance [HasProjectiveDimensionLT X n] (k : ‚Ñï) :
    HasProjectiveDimensionLT X (n + k) :=
  hasProjectiveDimensionLT_of_ge X n (n + k) (by cutsat)

instance [HasProjectiveDimensionLT X n] (k : ‚Ñï) :
    HasProjectiveDimensionLT X (k + n) :=
  hasProjectiveDimensionLT_of_ge X n (k + n) (by cutsat)

instance [HasProjectiveDimensionLT X n] :
    HasProjectiveDimensionLT X n.succ :=
  inferInstanceAs (HasProjectiveDimensionLT X (n + 1))

instance [Projective X] : HasProjectiveDimensionLT X 1 := by
  letI := HasExt.standard C
  rw [hasProjectiveDimensionLT_iff]
  intro i hi Y e
  obtain _ | i := i
  ¬∑ simp at hi
  ¬∑ exact e.eq_zero_of_projective

lemma projective_iff_subsingleton_ext_one [HasExt.{w} C] {X : C} :
    Projective X ‚Üî ‚àÄ ‚¶ÉY : C‚¶Ñ, Subsingleton (Ext X Y 1) := by
  refine ‚ü®fun h ‚Ü¶ HasProjectiveDimensionLT.subsingleton X 1 1 (by rfl),
    fun h ‚Ü¶ ‚ü®fun f g _ ‚Ü¶ ?_‚ü©‚ü©
  obtain ‚ü®œÜ, hœÜ‚ü© :=
    Ext.covariant_sequence_exact‚ÇÉ _ { exact := ShortComplex.exact_kernel g }
      (Ext.mk‚ÇÄ f) (zero_add 1) (by subsingleton)
  obtain ‚ü®œÜ, rfl‚ü© := Ext.homEquiv‚ÇÄ.symm.surjective œÜ
  exact ‚ü®œÜ, Ext.homEquiv‚ÇÄ.symm.injective (by simpa using hœÜ)‚ü©

lemma projective_iff_hasProjectiveDimensionLT_one (X : C) :
    Projective X ‚Üî HasProjectiveDimensionLT X 1 := by
  letI := HasExt.standard C
  exact ‚ü®fun _ ‚Ü¶ inferInstance, fun _ ‚Ü¶ projective_iff_subsingleton_ext_one.2
    (HasProjectiveDimensionLT.subsingleton X 1 1 (by rfl))‚ü©

end

lemma Retract.hasProjectiveDimensionLT {X Y : C} (h : Retract X Y) (n : ‚Ñï)
    [HasProjectiveDimensionLT Y n] :
    HasProjectiveDimensionLT X n := by
  letI := HasExt.standard C
  rw [hasProjectiveDimensionLT_iff]
  intro i hi T x
  rw [‚Üê x.mk‚ÇÄ_id_comp, ‚Üê h.retract, ‚Üê Ext.mk‚ÇÄ_comp_mk‚ÇÄ,
    Ext.comp_assoc_of_second_deg_zero,
    ((Ext.mk‚ÇÄ h.r).comp x (zero_add i)).eq_zero_of_hasProjectiveDimensionLT n hi,
    Ext.comp_zero]

lemma hasProjectiveDimensionLT_of_iso {X X' : C} (e : X ‚âÖ X') (n : ‚Ñï)
    [HasProjectiveDimensionLT X n] :
    HasProjectiveDimensionLT X' n :=
  e.symm.retract.hasProjectiveDimensionLT n

namespace ShortComplex

namespace ShortExact

variable {S : ShortComplex C} (hS : S.ShortExact) (n : ‚Ñï)
include hS

-- In the following lemmas, the parameters `HasProjectiveDimensionLT` are
-- explicit as it is unlikely we may infer them, unless the short complex `S`
-- was declared reducible

lemma hasProjectiveDimensionLT_X‚ÇÇ (h‚ÇÅ : HasProjectiveDimensionLT S.X‚ÇÅ n)
    (h‚ÇÉ : HasProjectiveDimensionLT S.X‚ÇÉ n) :
    HasProjectiveDimensionLT S.X‚ÇÇ n := by
  letI := HasExt.standard C
  rw [hasProjectiveDimensionLT_iff]
  intro i hi Y x‚ÇÇ
  obtain ‚ü®x‚ÇÉ, rfl‚ü© := Ext.contravariant_sequence_exact‚ÇÇ hS _ x‚ÇÇ
    (Ext.eq_zero_of_hasProjectiveDimensionLT _ n hi)
  rw [x‚ÇÉ.eq_zero_of_hasProjectiveDimensionLT n hi, Ext.comp_zero]

lemma hasProjectiveDimensionLT_X‚ÇÉ (h‚ÇÅ : HasProjectiveDimensionLT S.X‚ÇÅ n)
    (h‚ÇÇ : HasProjectiveDimensionLT S.X‚ÇÇ (n + 1)) :
    HasProjectiveDimensionLT S.X‚ÇÉ (n + 1) := by
  letI := HasExt.standard C
  rw [hasProjectiveDimensionLT_iff]
  rintro (_ | i) hi Y x‚ÇÉ
  ¬∑ simp at hi
  ¬∑ obtain ‚ü®x‚ÇÅ, rfl‚ü© := Ext.contravariant_sequence_exact‚ÇÉ hS _ x‚ÇÉ
      (Ext.eq_zero_of_hasProjectiveDimensionLT _ (n + 1) hi) (add_comm _ _)
    rw [x‚ÇÅ.eq_zero_of_hasProjectiveDimensionLT n (by cutsat), Ext.comp_zero]

lemma hasProjectiveDimensionLT_X‚ÇÅ (h‚ÇÇ : HasProjectiveDimensionLT S.X‚ÇÇ n)
    (h‚ÇÉ : HasProjectiveDimensionLT S.X‚ÇÉ (n + 1)) :
    HasProjectiveDimensionLT S.X‚ÇÅ n := by
  letI := HasExt.standard C
  rw [hasProjectiveDimensionLT_iff]
  intro i hi Y x‚ÇÅ
  obtain ‚ü®x‚ÇÇ, rfl‚ü© := Ext.contravariant_sequence_exact‚ÇÅ hS _ x‚ÇÅ (add_comm _ _)
    (Ext.eq_zero_of_hasProjectiveDimensionLT _ (n + 1) (by cutsat))
  rw [x‚ÇÇ.eq_zero_of_hasProjectiveDimensionLT n (by cutsat), Ext.comp_zero]

lemma hasProjectiveDimensionLT_X‚ÇÉ_iff (n : ‚Ñï) (h‚ÇÇ : Projective S.X‚ÇÇ) :
    HasProjectiveDimensionLT S.X‚ÇÉ (n + 2) ‚Üî HasProjectiveDimensionLT S.X‚ÇÅ (n + 1) :=
  ‚ü®fun _ ‚Ü¶ hS.hasProjectiveDimensionLT_X‚ÇÅ (n + 1) inferInstance inferInstance,
    fun _ ‚Ü¶ hS.hasProjectiveDimensionLT_X‚ÇÉ (n + 1) inferInstance inferInstance‚ü©

end ShortExact

end ShortComplex

instance (X Y : C) (n : ‚Ñï) [HasProjectiveDimensionLT X n]
    [HasProjectiveDimensionLT Y n] :
    HasProjectiveDimensionLT (X ‚äû Y) n :=
  (ShortComplex.Splitting.ofHasBinaryBiproduct X Y).shortExact.hasProjectiveDimensionLT_X‚ÇÇ n
    (by assumption) (by assumption)

end CategoryTheory

section ProjectiveDimension

namespace CategoryTheory

variable {C : Type u} [Category.{v, u} C] [Abelian C]

/-- The projective dimension of an object in an abelian category. -/
noncomputable def projectiveDimension (X : C) : WithBot ‚Ñï‚àû :=
  sInf {n : WithBot ‚Ñï‚àû | ‚àÄ (i : ‚Ñï), n < i ‚Üí HasProjectiveDimensionLT X i}

lemma projectiveDimension_eq_of_iso {X Y : C} (e : X ‚âÖ Y) :
    projectiveDimension X = projectiveDimension Y := by
  simp only [projectiveDimension]
  congr! 5
  exact ‚ü®fun h ‚Ü¶ hasProjectiveDimensionLT_of_iso e _,
    fun h ‚Ü¶ hasProjectiveDimensionLT_of_iso e.symm _‚ü©

lemma Retract.projectiveDimension_le {X Y : C} (h : Retract X Y) :
    projectiveDimension X ‚â§ projectiveDimension Y :=
  sInf_le_sInf_of_subset_insert_top (fun n hn ‚Ü¶ by
    simp only [Set.mem_setOf_eq, not_top_lt, IsEmpty.forall_iff, implies_true,
      Set.insert_eq_of_mem] at hn ‚ä¢
    intro i hi
    have := hn i hi
    exact h.hasProjectiveDimensionLT i)

lemma projectiveDimension_lt_iff {X : C} {n : ‚Ñï} :
    projectiveDimension X < n ‚Üî HasProjectiveDimensionLT X n := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ sInf_lt_iff.2 ?_‚ü©
  ¬∑ have : projectiveDimension X ‚àà _ := csInf_mem ‚ü®‚ä§, by simp‚ü©
    simp only [Set.mem_setOf_eq] at this
    exact this _ h
  ¬∑ obtain _ | n := n
    ¬∑ exact ‚ü®‚ä•, fun _ _ ‚Ü¶ hasProjectiveDimensionLT_of_ge _ 0 _ (by simp), by decide‚ü©
    ¬∑ exact ‚ü®n, fun i hi ‚Ü¶ hasProjectiveDimensionLT_of_ge _ (n + 1) _ (by simpa using hi),
        by simp [WithBot.lt_add_one_iff]‚ü©

lemma projectiveDimension_le_iff (X : C) (n : ‚Ñï) :
    projectiveDimension X ‚â§ n ‚Üî HasProjectiveDimensionLE X n := by
  simp [‚Üê projectiveDimension_lt_iff, ‚Üê WithBot.lt_add_one_iff]

lemma projectiveDimension_ge_iff (X : C) (n : ‚Ñï) :
    n ‚â§ projectiveDimension X ‚Üî ¬¨ HasProjectiveDimensionLT X n := by
  rw [‚Üê not_iff_not, not_le, not_not, projectiveDimension_lt_iff]

lemma projectiveDimension_eq_bot_iff (X : C) :
    projectiveDimension X = ‚ä• ‚Üî Limits.IsZero X := by
  rw [‚Üê hasProjectiveDimensionLT_zero_iff_isZero, ‚Üê projectiveDimension_lt_iff,
    Nat.cast_zero, ‚Üê WithBot.lt_coe_bot, bot_eq_zero', WithBot.coe_zero]

lemma projectiveDimension_ne_top_iff (X : C) :
    projectiveDimension X ‚â† ‚ä§ ‚Üî ‚àÉ n, HasProjectiveDimensionLE X n := by
  generalize hd : projectiveDimension X = d
  induction d with
  | bot =>
    simp only [ne_eq, bot_ne_top, not_false_eq_true, true_iff]
    exact ‚ü®0, by simp [‚Üê projectiveDimension_le_iff, hd]‚ü©
  | coe d =>
    induction d with
    | top =>
      by_contra!
      simp only [WithBot.coe_top, ne_eq, not_true_eq_false, false_and, true_and, false_or] at this
      obtain ‚ü®n, hn‚ü© := this
      rw [‚Üê projectiveDimension_le_iff, hd, WithBot.coe_top, top_le_iff] at hn
      exact ENat.coe_ne_top _ ((WithBot.coe_eq_coe).1 hn)
    | coe d =>
      simp only [ne_eq, WithBot.coe_eq_top, ENat.coe_ne_top, not_false_eq_true, true_iff]
      exact ‚ü®d, by simpa only [‚Üê projectiveDimension_le_iff] using hd.le‚ü©

end CategoryTheory

end ProjectiveDimension
