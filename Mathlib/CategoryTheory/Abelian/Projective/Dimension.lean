/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou, Nailin Guan
-/
import Mathlib.Algebra.Homology.DerivedCategory.Ext.EnoughProjectives
import Mathlib.Data.ENat.Lattice
import Mathlib.CategoryTheory.Abelian.Exact
import Mathlib.Algebra.Category.Grp.Zero

/-!
# Projective dimension

In an abelian category `C`, we shall say that `X : C` has projective dimension `< n`
if all `Ext X Y i` vanish when `n â‰¤ i`. This defines a type class
`HasProjectiveDimensionLT X n`. We also define a type class
`HasProjectiveDimensionLE X n` as an abbreviation for
`HasProjectiveDimensionLT X (n + 1)`.
(Note that the fact that `X` is a zero object is equivalent to the condition
`HasProjectiveDimensionLT X 0`, but this cannot be expressed in terms of
`HasProjectiveDimensionLE`.)

We also defined the projective dimension as `WithBot â„•âˆž` as `projectiveDimension`,
`projectiveDimension X = âŠ¥` iff `X` is zero and acts in common sense in the non-negative values.

-/

universe w v u

namespace CategoryTheory

open Abelian Limits ZeroObject

variable {C : Type u} [Category.{v} C] [Abelian C]

/-- An object `X` in an abelian category has projective dimension `< n` if
all `Ext X Y i` vanish when `n â‰¤ i`. See also `HasProjectiveDimensionLE`.
(Do not use the `subsingleton'` field directly. Use the constructor
`HasProjectiveDimensionLT.mk`, and the lemmas `hasProjectiveDimensionLT_iff` and
`Ext.eq_zero_of_hasProjectiveDimensionLT`.) -/
class HasProjectiveDimensionLT (X : C) (n : â„•) : Prop where mk' ::
  subsingleton' (i : â„•) (hi : n â‰¤ i) â¦ƒY : Câ¦„ :
    letI := HasExt.standard C
    Subsingleton (Ext.{max u v} X Y i)

/-- An object `X` in an abelian category has projective dimension `â‰¤ n` if
all `Ext X Y i` vanish when `n + 1 â‰¤ i` -/
abbrev HasProjectiveDimensionLE (X : C) (n : â„•) : Prop :=
  HasProjectiveDimensionLT X (n + 1)

namespace HasProjectiveDimensionLT

variable [HasExt.{w} C] (X : C) (n : â„•)

lemma subsingleton [hX : HasProjectiveDimensionLT X n] (i : â„•) (hi : n â‰¤ i) (Y : C) :
    Subsingleton (Ext.{w} X Y i) := by
  letI := HasExt.standard C
  have := hX.subsingleton' i hi
  exact Ext.chgUniv.{w, max u v}.symm.subsingleton

variable {X n} in
lemma mk (hX : âˆ€ (i : â„•) (_ : n â‰¤ i) â¦ƒY : Câ¦„, âˆ€ (e : Ext X Y i), e = 0) :
    HasProjectiveDimensionLT X n where
  subsingleton' i hi Y := by
    have : Subsingleton (Ext X Y i) := âŸ¨fun eâ‚ eâ‚‚ â†¦ by simp only [hX i hi]âŸ©
    letI := HasExt.standard C
    exact Ext.chgUniv.{max u v, w}.symm.subsingleton

end HasProjectiveDimensionLT

lemma Abelian.Ext.eq_zero_of_hasProjectiveDimensionLT [HasExt.{w} C]
    {X Y : C} {i : â„•} (e : Ext X Y i) (n : â„•) [HasProjectiveDimensionLT X n]
    (hi : n â‰¤ i) : e = 0 :=
  (HasProjectiveDimensionLT.subsingleton X n i hi Y).elim _ _

section

variable (X : C) (n : â„•)

lemma hasProjectiveDimensionLT_iff [HasExt.{w} C] :
    HasProjectiveDimensionLT X n â†”
      âˆ€ (i : â„•) (_ : n â‰¤ i) â¦ƒY : Câ¦„, âˆ€ (e : Ext X Y i), e = 0 :=
  âŸ¨fun _ _ hi _ e â†¦ e.eq_zero_of_hasProjectiveDimensionLT n hi,
    HasProjectiveDimensionLT.mkâŸ©

variable {X} in
lemma Limits.IsZero.hasProjectiveDimensionLT_zero (hX : IsZero X) :
    HasProjectiveDimensionLT X 0 := by
  letI := HasExt.standard C
  rw [hasProjectiveDimensionLT_iff]
  intro i hi Y e
  rw [â† e.mkâ‚€_id_comp, hX.eq_of_src (ðŸ™ X) 0, Ext.mkâ‚€_zero, Ext.zero_comp]

instance : HasProjectiveDimensionLT (0 : C) 0 :=
  (isZero_zero C).hasProjectiveDimensionLT_zero

lemma isZero_of_hasProjectiveDimensionLT_zero [HasProjectiveDimensionLT X 0] : IsZero X := by
  letI := HasExt.standard C
  rw [IsZero.iff_id_eq_zero]
  apply Ext.homEquivâ‚€.symm.injective
  simpa only [Ext.homEquivâ‚€_symm_apply, Ext.mkâ‚€_zero]
    using Abelian.Ext.eq_zero_of_hasProjectiveDimensionLT _ 0 (by rfl)

lemma hasProjectiveDimensionLT_zero_iff_isZero : HasProjectiveDimensionLT X 0 â†” IsZero X :=
  âŸ¨fun _ â†¦ isZero_of_hasProjectiveDimensionLT_zero X, fun h â†¦ h.hasProjectiveDimensionLT_zeroâŸ©

lemma hasProjectiveDimensionLT_of_ge (m : â„•) (h : n â‰¤ m)
    [HasProjectiveDimensionLT X n] :
    HasProjectiveDimensionLT X m := by
  letI := HasExt.standard C
  rw [hasProjectiveDimensionLT_iff]
  intro i hi Y e
  exact e.eq_zero_of_hasProjectiveDimensionLT n (by omega)

instance [HasProjectiveDimensionLT X n] (k : â„•) :
    HasProjectiveDimensionLT X (n + k) :=
  hasProjectiveDimensionLT_of_ge X n (n + k) (by omega)

instance [HasProjectiveDimensionLT X n] (k : â„•) :
    HasProjectiveDimensionLT X (k + n) :=
  hasProjectiveDimensionLT_of_ge X n (k + n) (by omega)

instance [HasProjectiveDimensionLT X n] :
    HasProjectiveDimensionLT X n.succ :=
  inferInstanceAs (HasProjectiveDimensionLT X (n + 1))

instance [Projective X] : HasProjectiveDimensionLT X 1 := by
  letI := HasExt.standard C
  rw [hasProjectiveDimensionLT_iff]
  intro i hi Y e
  obtain _ | i := i
  Â· simp at hi
  Â· exact e.eq_zero_of_projective

lemma hasProjectiveDimensionLT_one_iff (X : C) :
    Projective X â†” HasProjectiveDimensionLT X 1 := by
  letI := HasExt.standard C
  refine âŸ¨fun h â†¦ inferInstance, fun âŸ¨hâŸ© â†¦ âŸ¨?_âŸ©âŸ©
  intro Z Y f g epi
  let S := ShortComplex.mk (kernel.Î¹ g) g (kernel.condition g)
  have S_exact : S.ShortExact := {
    exact := ShortComplex.exact_kernel g
    mono_f := equalizer.Î¹_mono
    epi_g := epi}
  have : IsZero (AddCommGrp.of (Ext X S.Xâ‚ 1)) := by
    let _ := h 1 (le_refl 1) (Y := S.Xâ‚)
    exact AddCommGrp.isZero_of_subsingleton _
  have exac := Ext.covariant_sequence_exactâ‚ƒ' X S_exact 0 1 (zero_add 1)
  have surj: Function.Surjective ((Ext.mkâ‚€ S.g).postcomp X (add_zero 0)) :=
    (AddCommGrp.epi_iff_surjective _).mp (exac.epi_f (this.eq_zero_of_tgt _))
  rcases surj (Ext.mkâ‚€ f) with âŸ¨f', hf'âŸ©
  use Ext.addEquivâ‚€ f'
  simp only [AddMonoidHom.flip_apply, Ext.bilinearComp_apply_apply] at hf'
  rw [â† Ext.mkâ‚€_addEquivâ‚€_apply f', Ext.mkâ‚€_comp_mkâ‚€] at hf'
  exact (Ext.mkâ‚€_bijective X Y).1 hf'

end

lemma Retract.hasProjectiveDimensionLT {X Y : C} (h : Retract X Y) (n : â„•)
    [HasProjectiveDimensionLT Y n] :
    HasProjectiveDimensionLT X n := by
  letI := HasExt.standard C
  rw [hasProjectiveDimensionLT_iff]
  intro i hi T x
  rw [â† x.mkâ‚€_id_comp, â† h.retract, â† Ext.mkâ‚€_comp_mkâ‚€,
    Ext.comp_assoc_of_second_deg_zero,
    ((Ext.mkâ‚€ h.r).comp x (zero_add i)).eq_zero_of_hasProjectiveDimensionLT n hi,
    Ext.comp_zero]

lemma hasProjectiveDimensionLT_of_iso {X X' : C} (e : X â‰… X') (n : â„•)
    [HasProjectiveDimensionLT X n] :
    HasProjectiveDimensionLT X' n :=
  e.symm.retract.hasProjectiveDimensionLT n

namespace ShortComplex

namespace ShortExact

variable {S : ShortComplex C} (hS : S.ShortExact) (n : â„•)
include hS

-- In the following lemmas, the parameters `HasProjectiveDimensionLT` are
-- explicit as it is unlikely we may infer them, unless the short complex `S`
-- was declared reducible

lemma hasProjectiveDimensionLT_Xâ‚‚ (hâ‚ : HasProjectiveDimensionLT S.Xâ‚ n)
    (hâ‚ƒ : HasProjectiveDimensionLT S.Xâ‚ƒ n) :
    HasProjectiveDimensionLT S.Xâ‚‚ n := by
  letI := HasExt.standard C
  rw [hasProjectiveDimensionLT_iff]
  intro i hi Y xâ‚‚
  obtain âŸ¨xâ‚ƒ, rflâŸ© := Ext.contravariant_sequence_exactâ‚‚ hS _ xâ‚‚
    (Ext.eq_zero_of_hasProjectiveDimensionLT _ n hi)
  rw [xâ‚ƒ.eq_zero_of_hasProjectiveDimensionLT n hi, Ext.comp_zero]

lemma hasProjectiveDimensionLT_Xâ‚ƒ (hâ‚ : HasProjectiveDimensionLT S.Xâ‚ n)
    (hâ‚‚ : HasProjectiveDimensionLT S.Xâ‚‚ (n + 1)) :
    HasProjectiveDimensionLT S.Xâ‚ƒ (n + 1) := by
  letI := HasExt.standard C
  rw [hasProjectiveDimensionLT_iff]
  rintro (_ | i) hi Y xâ‚ƒ
  Â· simp at hi
  Â· obtain âŸ¨xâ‚, rflâŸ© := Ext.contravariant_sequence_exactâ‚ƒ hS _ xâ‚ƒ
      (Ext.eq_zero_of_hasProjectiveDimensionLT _ (n + 1) hi) (add_comm _ _)
    rw [xâ‚.eq_zero_of_hasProjectiveDimensionLT n (by omega), Ext.comp_zero]

lemma hasProjectiveDimensionLT_Xâ‚ (hâ‚‚ : HasProjectiveDimensionLT S.Xâ‚‚ n)
    (hâ‚ƒ : HasProjectiveDimensionLT S.Xâ‚ƒ (n + 1)) :
    HasProjectiveDimensionLT S.Xâ‚ n := by
  letI := HasExt.standard C
  rw [hasProjectiveDimensionLT_iff]
  intro i hi Y xâ‚
  obtain âŸ¨xâ‚‚, rflâŸ© := Ext.contravariant_sequence_exactâ‚ hS _ xâ‚ (add_comm _ _)
    (Ext.eq_zero_of_hasProjectiveDimensionLT _ (n + 1) (by omega))
  rw [xâ‚‚.eq_zero_of_hasProjectiveDimensionLT n (by omega), Ext.comp_zero]

lemma hasProjectiveDimensionLT_Xâ‚ƒ_iff (n : â„•) (hâ‚‚ : Projective S.Xâ‚‚) :
    HasProjectiveDimensionLT S.Xâ‚ƒ (n + 2) â†” HasProjectiveDimensionLT S.Xâ‚ (n + 1) :=
  âŸ¨fun _ â†¦ hS.hasProjectiveDimensionLT_Xâ‚ (n + 1) inferInstance inferInstance,
    fun _ â†¦ hS.hasProjectiveDimensionLT_Xâ‚ƒ (n + 1) inferInstance inferInstanceâŸ©

end ShortExact

end ShortComplex

instance (X Y : C) (n : â„•) [HasProjectiveDimensionLT X n]
    [HasProjectiveDimensionLT Y n] :
    HasProjectiveDimensionLT (X âŠž Y) n :=
  (ShortComplex.Splitting.ofHasBinaryBiproduct X Y).shortExact.hasProjectiveDimensionLT_Xâ‚‚ n
    (by assumption) (by assumption)

end CategoryTheory

section ProjectiveDimension

open CategoryTheory

variable {C : Type u} [Category.{v, u} C] [Abelian C]

/-- The projective dimension of object of abelian category. -/
noncomputable def projectiveDimension (X : C) : WithBot â„•âˆž :=
  sInf {n : WithBot â„•âˆž | âˆ€ (i : â„•), n < i â†’ HasProjectiveDimensionLT X i}

lemma projectiveDimension_eq_of_iso {X Y : C} (e : X â‰… Y) :
    projectiveDimension X = projectiveDimension Y := by
  simp only [projectiveDimension]
  congr! 5
  exact âŸ¨fun h â†¦ hasProjectiveDimensionLT_of_iso e _,
    fun h â†¦ hasProjectiveDimensionLT_of_iso e.symm _âŸ©

lemma hasProjectiveDimensionLT_of_projectiveDimension_lt (X : C) (n : â„•)
    (h : projectiveDimension X < n) : HasProjectiveDimensionLT X n := by
  have : projectiveDimension X âˆˆ _ := csInf_mem (by
    use âŠ¤
    simp)
  simp only [Set.mem_setOf_eq] at this
  exact this n h

lemma projectiveDimension_le_iff (X : C) (n : â„•) : projectiveDimension X â‰¤ n â†”
    HasProjectiveDimensionLE X n := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· apply hasProjectiveDimensionLT_of_projectiveDimension_lt X (n + 1)
    exact lt_of_le_of_lt h (Nat.cast_lt.mpr (lt_add_one n))
  Â· apply sInf_le
    simp only [Set.mem_setOf_eq, Nat.cast_lt]
    intro i hi
    exact hasProjectiveDimensionLT_of_ge X (n + 1) i hi

lemma projectiveDimension_ge_iff (X : C) (n : â„•) : n â‰¤ projectiveDimension X  â†”
    Â¬ HasProjectiveDimensionLT X n := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· simp only [projectiveDimension, le_sInf_iff, Set.mem_setOf_eq] at h
    by_contra lt
    by_cases eq0 : n = 0
    Â· have := h âŠ¥ (fun i _ â†¦ (hasProjectiveDimensionLT_of_ge X n i (by simp [eq0])))
      simp [eq0] at this
    Â· have : âˆ€ (i : â„•), (n - 1 : â„•) < (i : WithBot â„•âˆž) â†’ HasProjectiveDimensionLT X i := by
        intro i hi
        exact hasProjectiveDimensionLT_of_ge X n i (Nat.le_of_pred_lt (Nat.cast_lt.mp hi))
      have not := Nat.cast_le.mp (h (n - 1 : â„•) this)
      omega
  Â· simp only [projectiveDimension, le_sInf_iff, Set.mem_setOf_eq]
    intro m hm
    by_contra nle
    exact h (hm _ (lt_of_not_ge nle))

lemma projectiveDimension_eq_bot_iff (X : C) : projectiveDimension X = âŠ¥ â†”
    Limits.IsZero X := by
  rw [â† hasProjectiveDimensionLT_zero_iff_isZero]
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· apply hasProjectiveDimensionLT_of_projectiveDimension_lt X 0
    simp [h, bot_lt_iff_ne_bot]
  Â· rw [eq_bot_iff]
    apply sInf_le
    intro i _
    apply hasProjectiveDimensionLT_of_ge X 0 i (Nat.zero_le i)

lemma projectiveDimension_eq_find (X : C) (h : âˆƒ n, HasProjectiveDimensionLE X n)
    (nzero : Â¬ Limits.IsZero X) [DecidablePred (HasProjectiveDimensionLE X)] :
    projectiveDimension X = Nat.find h := by
  apply le_antisymm ((projectiveDimension_le_iff _ _).mpr (Nat.find_spec h))
  apply (projectiveDimension_ge_iff _ _).mpr
  by_cases eq0 : Nat.find h = 0
  Â· simp only [eq0]
    by_contra
    exact nzero (isZero_of_hasProjectiveDimensionLT_zero X)
  Â· rw [â† Nat.succ_pred_eq_of_ne_zero eq0]
    exact (Nat.find_min h (Nat.sub_one_lt eq0))

lemma projectiveDimension_ne_top_iff (X : C) : projectiveDimension X â‰  âŠ¤ â†”
    âˆƒ n, HasProjectiveDimensionLE X n := by
  simp only [projectiveDimension, ne_eq, sInf_eq_top, Set.mem_setOf_eq, not_forall]
  refine âŸ¨fun âŸ¨x, hx, neâŸ© â†¦ ?_, fun âŸ¨n, hnâŸ© â†¦ ?_âŸ©
  Â· by_cases eqbot : x = âŠ¥
    Â· use 0
      have := hx 0 (by simp [eqbot, bot_lt_iff_ne_bot])
      exact instHasProjectiveDimensionLTSucc X 0
    Â· have : x.unbot eqbot â‰  âŠ¤ := by
        by_contra eq
        rw [â† WithBot.coe_inj, WithBot.coe_unbot, WithBot.coe_top] at eq
        exact ne eq
      use (x.unbot eqbot).toNat
      have eq : x = (x.unbot eqbot).toNat := (WithBot.coe_unbot x eqbot).symm.trans
        (WithBot.coe_inj.mpr (ENat.coe_toNat this).symm)
      have : x < ((x.unbot eqbot).toNat + 1 : â„•) := by
        nth_rw 1 [eq]
        exact Nat.cast_lt.mpr (lt_add_one _)
      exact hx ((x.unbot eqbot).toNat + 1 : â„•) this
  Â· use n
    simpa using âŸ¨fun i hi â†¦ hasProjectiveDimensionLT_of_ge X (n + 1) i hi,
      WithBot.coe_inj.not.mpr (ENat.coe_ne_top n)âŸ©

end ProjectiveDimension
