/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel, Kim Morrison, Jakob von Raumer, JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Preadditive.Projective.Resolution
import Mathlib.Algebra.Homology.HomotopyCategory
import Mathlib.Tactic.SuppressCompilation

/-!
# Abelian categories with enough projectives have projective resolutions

## Main results
When the underlying category is abelian:
* `CategoryTheory.ProjectiveResolution.lift`: Given `P : ProjectiveResolution X` and
  `Q : ProjectiveResolution Y`, any morphism `X âŸ¶ Y` admits a lifting to a chain map
  `P.complex âŸ¶ Q.complex`. It is a lifting in the sense that `P.Î¹` intertwines the lift and
  the original morphism, see `CategoryTheory.ProjectiveResolution.lift_commutes`.
* `CategoryTheory.ProjectiveResolution.liftHomotopy`: Any two such descents are homotopic.
* `CategoryTheory.ProjectiveResolution.homotopyEquiv`: Any two projective resolutions of the same
  object are homotopy equivalent.
* `CategoryTheory.projectiveResolutions`: If every object admits a projective resolution, we can
  construct a functor `projectiveResolutions C : C â¥¤ HomotopyCategory C (ComplexShape.down â„•)`.

* `CategoryTheory.exact_d_f`: `Projective.d f` and `f` are exact.
* `CategoryTheory.ProjectiveResolution.of`: Hence, starting from an epimorphism `P âŸ¶ X`, where `P`
  is projective, we can apply `Projective.d` repeatedly to obtain a projective resolution of `X`.
-/

suppress_compilation

noncomputable section

universe v u

namespace CategoryTheory

variable {C : Type u} [Category.{v} C]

open Category Limits Projective


namespace ProjectiveResolution

section

variable [HasZeroObject C] [HasZeroMorphisms C]

/-- Auxiliary construction for `lift`. -/
def liftFZero {Y Z : C} (f : Y âŸ¶ Z) (P : ProjectiveResolution Y) (Q : ProjectiveResolution Z) :
    P.complex.X 0 âŸ¶ Q.complex.X 0 :=
  Projective.factorThru (P.Ï€.f 0 â‰« f) (Q.Ï€.f 0)

end

section Abelian

variable [Abelian C]

lemma exactâ‚€ {Z : C} (P : ProjectiveResolution Z) :
    (ShortComplex.mk _ _ P.complex_d_comp_Ï€_f_zero).Exact :=
  ShortComplex.exact_of_g_is_cokernel _ P.isColimitCokernelCofork

/-- Auxiliary construction for `lift`. -/
def liftFOne {Y Z : C} (f : Y âŸ¶ Z) (P : ProjectiveResolution Y) (Q : ProjectiveResolution Z) :
    P.complex.X 1 âŸ¶ Q.complex.X 1 :=
  Q.exactâ‚€.liftFromProjective (P.complex.d 1 0 â‰« liftFZero f P Q) (by simp [liftFZero])

@[simp]
theorem liftFOne_zero_comm {Y Z : C} (f : Y âŸ¶ Z) (P : ProjectiveResolution Y)
    (Q : ProjectiveResolution Z) :
    liftFOne f P Q â‰« Q.complex.d 1 0 = P.complex.d 1 0 â‰« liftFZero f P Q := by
  apply Q.exactâ‚€.liftFromProjective_comp

/-- Auxiliary construction for `lift`. -/
def liftFSucc {Y Z : C} (P : ProjectiveResolution Y) (Q : ProjectiveResolution Z) (n : â„•)
    (g : P.complex.X n âŸ¶ Q.complex.X n) (g' : P.complex.X (n + 1) âŸ¶ Q.complex.X (n + 1))
    (w : g' â‰« Q.complex.d (n + 1) n = P.complex.d (n + 1) n â‰« g) :
    Î£' g'' : P.complex.X (n + 2) âŸ¶ Q.complex.X (n + 2),
      g'' â‰« Q.complex.d (n + 2) (n + 1) = P.complex.d (n + 2) (n + 1) â‰« g' :=
  âŸ¨(Q.exact_succ n).liftFromProjective
    (P.complex.d (n + 2) (n + 1) â‰« g') (by simp [w]),
      (Q.exact_succ n).liftFromProjective_comp _ _âŸ©

/-- A morphism in `C` lift to a chain map between projective resolutions. -/
def lift {Y Z : C} (f : Y âŸ¶ Z) (P : ProjectiveResolution Y) (Q : ProjectiveResolution Z) :
    P.complex âŸ¶ Q.complex :=
  ChainComplex.mkHom _ _ (liftFZero f _ _) (liftFOne f _ _) (liftFOne_zero_comm f P Q)
    fun n âŸ¨g, g', wâŸ© => âŸ¨(liftFSucc P Q n g g' w).1, (liftFSucc P Q n g g' w).2âŸ©

/-- The resolution maps intertwine the lift of a morphism and that morphism. -/
@[reassoc (attr := simp)]
theorem lift_commutes {Y Z : C} (f : Y âŸ¶ Z) (P : ProjectiveResolution Y)
    (Q : ProjectiveResolution Z) : lift f P Q â‰« Q.Ï€ = P.Ï€ â‰« (ChainComplex.singleâ‚€ C).map f := by
  ext
  simp [lift, liftFZero, liftFOne]

@[reassoc (attr := simp)]
lemma lift_commutes_zero {Y Z : C} (f : Y âŸ¶ Z)
    (P : ProjectiveResolution Y) (Q : ProjectiveResolution Z) :
    (lift f P Q).f 0 â‰« Q.Ï€.f 0 = P.Ï€.f 0 â‰« f :=
  (HomologicalComplex.congr_hom (lift_commutes f P Q) 0).trans (by simp)

/-- An auxiliary definition for `liftHomotopyZero`. -/
def liftHomotopyZeroZero {Y Z : C} {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
    (f : P.complex âŸ¶ Q.complex) (comm : f â‰« Q.Ï€ = 0) : P.complex.X 0 âŸ¶ Q.complex.X 1 :=
  Q.exactâ‚€.liftFromProjective (f.f 0) (congr_fun (congr_arg HomologicalComplex.Hom.f comm) 0)

@[reassoc (attr := simp)]
lemma liftHomotopyZeroZero_comp {Y Z : C} {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
    (f : P.complex âŸ¶ Q.complex) (comm : f â‰« Q.Ï€ = 0) :
    liftHomotopyZeroZero f comm â‰« Q.complex.d 1 0 = f.f 0 :=
  Q.exactâ‚€.liftFromProjective_comp  _ _

/-- An auxiliary definition for `liftHomotopyZero`. -/
def liftHomotopyZeroOne {Y Z : C} {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
    (f : P.complex âŸ¶ Q.complex) (comm : f â‰« Q.Ï€ = 0) :
    P.complex.X 1 âŸ¶ Q.complex.X 2 :=
  (Q.exact_succ 0).liftFromProjective (f.f 1 - P.complex.d 1 0 â‰« liftHomotopyZeroZero f comm)
    (by rw [Preadditive.sub_comp, assoc, HomologicalComplex.Hom.comm,
              liftHomotopyZeroZero_comp, sub_self])

@[reassoc (attr := simp)]
lemma liftHomotopyZeroOne_comp {Y Z : C} {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
    (f : P.complex âŸ¶ Q.complex) (comm : f â‰« Q.Ï€ = 0) :
    liftHomotopyZeroOne f comm â‰« Q.complex.d 2 1 =
      f.f 1 - P.complex.d 1 0 â‰« liftHomotopyZeroZero f comm :=
  (Q.exact_succ 0).liftFromProjective_comp _ _

/-- An auxiliary definition for `liftHomotopyZero`. -/
def liftHomotopyZeroSucc {Y Z : C} {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
    (f : P.complex âŸ¶ Q.complex) (n : â„•) (g : P.complex.X n âŸ¶ Q.complex.X (n + 1))
    (g' : P.complex.X (n + 1) âŸ¶ Q.complex.X (n + 2))
    (w : f.f (n + 1) = P.complex.d (n + 1) n â‰« g + g' â‰« Q.complex.d (n + 2) (n + 1)) :
    P.complex.X (n + 2) âŸ¶ Q.complex.X (n + 3) :=
  (Q.exact_succ (n + 1)).liftFromProjective (f.f (n + 2) - P.complex.d _ _ â‰« g') (by simp [w])

@[reassoc (attr := simp)]
lemma liftHomotopyZeroSucc_comp {Y Z : C} {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
    (f : P.complex âŸ¶ Q.complex) (n : â„•) (g : P.complex.X n âŸ¶ Q.complex.X (n + 1))
    (g' : P.complex.X (n + 1) âŸ¶ Q.complex.X (n + 2))
    (w : f.f (n + 1) = P.complex.d (n + 1) n â‰« g + g' â‰« Q.complex.d (n + 2) (n + 1)) :
    liftHomotopyZeroSucc f n g g' w â‰« Q.complex.d (n + 3) (n + 2) =
      f.f (n + 2) - P.complex.d _ _ â‰« g' :=
  (Q.exact_succ (n + 1)).liftFromProjective_comp  _ _

/-- Any lift of the zero morphism is homotopic to zero. -/
def liftHomotopyZero {Y Z : C} {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
    (f : P.complex âŸ¶ Q.complex) (comm : f â‰« Q.Ï€ = 0) : Homotopy f 0 :=
  Homotopy.mkInductive _ (liftHomotopyZeroZero f comm) (by simp )
    (liftHomotopyZeroOne f comm) (by simp) fun n âŸ¨g, g', wâŸ© =>
    âŸ¨liftHomotopyZeroSucc f n g g' w, by simpâŸ©

/-- Two lifts of the same morphism are homotopic. -/
def liftHomotopy {Y Z : C} (f : Y âŸ¶ Z) {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
    (g h : P.complex âŸ¶ Q.complex) (g_comm : g â‰« Q.Ï€ = P.Ï€ â‰« (ChainComplex.singleâ‚€ C).map f)
    (h_comm : h â‰« Q.Ï€ = P.Ï€ â‰« (ChainComplex.singleâ‚€ C).map f) : Homotopy g h :=
  Homotopy.equivSubZero.invFun (liftHomotopyZero _ (by simp [g_comm, h_comm]))

/-- The lift of the identity morphism is homotopic to the identity chain map. -/
def liftIdHomotopy (X : C) (P : ProjectiveResolution X) :
    Homotopy (lift (ğŸ™ X) P P) (ğŸ™ P.complex) := by
  apply liftHomotopy (ğŸ™ X) <;> simp

/-- The lift of a composition is homotopic to the composition of the lifts. -/
def liftCompHomotopy {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (P : ProjectiveResolution X)
    (Q : ProjectiveResolution Y) (R : ProjectiveResolution Z) :
    Homotopy (lift (f â‰« g) P R) (lift f P Q â‰« lift g Q R) := by
  apply liftHomotopy (f â‰« g) <;> simp

-- We don't care about the actual definitions of these homotopies.
/-- Any two projective resolutions are homotopy equivalent. -/
def homotopyEquiv {X : C} (P Q : ProjectiveResolution X) :
    HomotopyEquiv P.complex Q.complex where
  hom := lift (ğŸ™ X) P Q
  inv := lift (ğŸ™ X) Q P
  homotopyHomInvId := (liftCompHomotopy (ğŸ™ X) (ğŸ™ X) P Q P).symm.trans <| by
    simpa [id_comp] using liftIdHomotopy _ _
  homotopyInvHomId := (liftCompHomotopy (ğŸ™ X) (ğŸ™ X) Q P Q).symm.trans <| by
    simpa [id_comp] using liftIdHomotopy _ _

@[reassoc (attr := simp)]
theorem homotopyEquiv_hom_Ï€ {X : C} (P Q : ProjectiveResolution X) :
    (homotopyEquiv P Q).hom â‰« Q.Ï€ = P.Ï€ := by simp [homotopyEquiv]

@[reassoc (attr := simp)]
theorem homotopyEquiv_inv_Ï€ {X : C} (P Q : ProjectiveResolution X) :
    (homotopyEquiv P Q).inv â‰« P.Ï€ = Q.Ï€ := by simp [homotopyEquiv]

end Abelian

end ProjectiveResolution

/-- An arbitrarily chosen projective resolution of an object. -/
abbrev projectiveResolution (Z : C) [HasZeroObject C]
    [HasZeroMorphisms C] [HasProjectiveResolution Z] :
    ProjectiveResolution Z :=
  (HasProjectiveResolution.out (Z := Z)).some

variable (C)
variable [Abelian C]

section
variable [HasProjectiveResolutions C]

/-- Taking projective resolutions is functorial,
if considered with target the homotopy category
(`â„•`-indexed chain complexes and chain maps up to homotopy).
-/
def projectiveResolutions : C â¥¤ HomotopyCategory C (ComplexShape.down â„•) where
  obj X := (HomotopyCategory.quotient _ _).obj (projectiveResolution X).complex
  map f := (HomotopyCategory.quotient _ _).map (ProjectiveResolution.lift f _ _)
  map_id X := by
    rw [â† (HomotopyCategory.quotient _ _).map_id]
    apply HomotopyCategory.eq_of_homotopy
    apply ProjectiveResolution.liftIdHomotopy
  map_comp f g := by
    rw [â† (HomotopyCategory.quotient _ _).map_comp]
    apply HomotopyCategory.eq_of_homotopy
    apply ProjectiveResolution.liftCompHomotopy

variable {C}

/-- If `P : ProjectiveResolution X`, then the chosen `(projectiveResolutions C).obj X`
is isomorphic (in the homotopy category) to `P.complex`. -/
def ProjectiveResolution.iso {X : C} (P : ProjectiveResolution X) :
    (projectiveResolutions C).obj X â‰…
      (HomotopyCategory.quotient _ _).obj P.complex :=
  HomotopyCategory.isoOfHomotopyEquiv (homotopyEquiv _ _)

@[reassoc]
lemma ProjectiveResolution.iso_inv_naturality {X Y : C} (f : X âŸ¶ Y)
    (P : ProjectiveResolution X) (Q : ProjectiveResolution Y)
    (Ï† : P.complex âŸ¶ Q.complex) (comm : Ï†.f 0 â‰« Q.Ï€.f 0 = P.Ï€.f 0 â‰« f) :
    P.iso.inv â‰« (projectiveResolutions C).map f =
      (HomotopyCategory.quotient _ _).map Ï† â‰« Q.iso.inv := by
  apply HomotopyCategory.eq_of_homotopy
  apply liftHomotopy f
  all_goals
    cat_disch

@[reassoc]
lemma ProjectiveResolution.iso_hom_naturality {X Y : C} (f : X âŸ¶ Y)
    (P : ProjectiveResolution X) (Q : ProjectiveResolution Y)
    (Ï† : P.complex âŸ¶ Q.complex) (comm : Ï†.f 0 â‰« Q.Ï€.f 0 = P.Ï€.f 0 â‰« f) :
    (projectiveResolutions C).map f â‰« Q.iso.hom =
      P.iso.hom â‰« (HomotopyCategory.quotient _ _).map Ï† := by
  rw [â† cancel_epi (P.iso).inv, iso_inv_naturality_assoc f P Q Ï† comm,
    Iso.inv_hom_id_assoc, Iso.inv_hom_id, comp_id]

end

variable [EnoughProjectives C]

variable {C} in
theorem exact_d_f {X Y : C} (f : X âŸ¶ Y) :
    (ShortComplex.mk (d f) f (by simp)).Exact := by
  let Î± : ShortComplex.mk (d f) f (by simp) âŸ¶ ShortComplex.mk (kernel.Î¹ f) f (by simp) :=
    { Ï„â‚ := Projective.Ï€ _
      Ï„â‚‚ := ğŸ™ _
      Ï„â‚ƒ := ğŸ™ _ }
  rw [ShortComplex.exact_iff_of_epi_of_isIso_of_mono Î±]
  apply ShortComplex.exact_of_f_is_kernel
  apply kernelIsKernel

namespace ProjectiveResolution

/-!
Our goal is to define `ProjectiveResolution.of Z : ProjectiveResolution Z`.
The `0`-th object in this resolution will just be `Projective.over Z`,
i.e. an arbitrarily chosen projective object with a map to `Z`.
After that, we build the `n+1`-st object as `Projective.syzygies`
applied to the previously constructed morphism,
and the map from the `n`-th object as `Projective.d`.
-/

variable {C}
variable (Z : C)

-- The construction of the projective resolution `of` would be very, very slow
-- if it were not broken into separate definitions and lemmas

/-- Auxiliary definition for `ProjectiveResolution.of`. -/
def ofComplex : ChainComplex C â„• :=
  ChainComplex.mk' (Projective.over Z) (Projective.syzygies (Projective.Ï€ Z))
    (Projective.d (Projective.Ï€ Z)) (fun f => âŸ¨_, Projective.d f, by simpâŸ©)

lemma ofComplex_d_1_0 :
    (ofComplex Z).d 1 0 = d (Projective.Ï€ Z) := by
  simp [ofComplex]

lemma ofComplex_exactAt_succ (n : â„•) :
    (ofComplex Z).ExactAt (n + 1) := by
  rw [HomologicalComplex.exactAt_iff' _ (n + 1 + 1) (n + 1) n (by simp) (by simp)]
  dsimp [ofComplex, HomologicalComplex.sc', HomologicalComplex.shortComplexFunctor',
      ChainComplex.mk', ChainComplex.mk]
  simp only [ChainComplex.of_d]
  -- TODO: this should just be apply exact_d_f so something is missing
  match n with
  | 0 => apply exact_d_f
  | n + 1 => apply exact_d_f

instance (n : â„•) : Projective ((ofComplex Z).X n) := by
  obtain (_ | _ | _ | n) := n <;> apply Projective.projective_over

/-- In any abelian category with enough projectives,
`ProjectiveResolution.of Z` constructs an projective resolution of the object `Z`.
-/
irreducible_def of : ProjectiveResolution Z where
  complex := ofComplex Z
  Ï€ := (ChainComplex.toSingleâ‚€Equiv _ _).symm âŸ¨Projective.Ï€ Z, by
          rw [ofComplex_d_1_0, assoc, kernel.condition, comp_zero]âŸ©
  quasiIso := âŸ¨fun n => by
    cases n
    Â· rw [ChainComplex.quasiIsoAtâ‚€_iff, ShortComplex.quasiIso_iff_of_zeros']
      Â· dsimp
        refine (ShortComplex.exact_and_epi_g_iff_of_iso ?_).2
          âŸ¨exact_d_f (Projective.Ï€ Z), by dsimp; infer_instanceâŸ©
        exact ShortComplex.isoMk (Iso.refl _) (Iso.refl _) (Iso.refl _)
          (by simp [ofComplex]) (by simp)
      all_goals rfl
    Â· rw [quasiIsoAt_iff_exactAt']
      Â· apply ofComplex_exactAt_succ
      Â· apply ChainComplex.exactAt_succ_single_objâŸ©

instance (priority := 100) (Z : C) : HasProjectiveResolution Z where out := âŸ¨of ZâŸ©

instance (priority := 100) : HasProjectiveResolutions C where out _ := inferInstance

end ProjectiveResolution

end CategoryTheory
