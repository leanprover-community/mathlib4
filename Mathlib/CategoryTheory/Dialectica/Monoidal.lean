/-
Copyright (c) 2024 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro
-/
import Mathlib.CategoryTheory.Subobject.Lattice
import Mathlib.CategoryTheory.Monoidal.Braided.Basic
import Mathlib.CategoryTheory.Dialectica.Basic

/-!
# The Dialectica category is symmetric monoidal

We show that the category `Dial` has a symmetric monoidal category structure.
-/

noncomputable section

namespace CategoryTheory

open MonoidalCategory Limits

universe v u
variable {C : Type u} [Category.{v} C] [HasFiniteProducts C] [HasPullbacks C]

namespace Dial

local notation "Ï€â‚" => prod.fst
local notation "Ï€â‚‚" => prod.snd
local notation "Ï€(" a ", " b ")" => prod.lift a b

/-- The object `X âŠ— Y` in the `Dial C` category just tuples the left and right components. -/
@[simps] def tensorObjImpl (X Y : Dial C) : Dial C where
  src := X.src â¨¯ Y.src
  tgt := X.tgt â¨¯ Y.tgt
  rel :=
    (Subobject.pullback (prod.map Ï€â‚ Ï€â‚)).obj X.rel âŠ“
    (Subobject.pullback (prod.map Ï€â‚‚ Ï€â‚‚)).obj Y.rel

/-- The functorial action of `X âŠ— Y` in `Dial C`. -/
@[simps] def tensorHomImpl {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : Dial C} (f : Xâ‚ âŸ¶ Xâ‚‚) (g : Yâ‚ âŸ¶ Yâ‚‚) :
    tensorObjImpl Xâ‚ Yâ‚ âŸ¶ tensorObjImpl Xâ‚‚ Yâ‚‚ where
  f := prod.map f.f g.f
  F := Ï€(prod.map Ï€â‚ Ï€â‚ â‰« f.F, prod.map Ï€â‚‚ Ï€â‚‚ â‰« g.F)
  le := by
    simp only [tensorObjImpl, Subobject.inf_pullback]
    apply inf_le_inf <;> rw [â† Subobject.pullback_comp, â† Subobject.pullback_comp]
    Â· have := (Subobject.pullback (prod.map Ï€â‚ Ï€â‚ :
        (Xâ‚.src â¨¯ Yâ‚.src) â¨¯ Xâ‚‚.tgt â¨¯ Yâ‚‚.tgt âŸ¶ _)).monotone (Hom.le f)
      rw [â† Subobject.pullback_comp, â† Subobject.pullback_comp] at this
      convert this using 3 <;> simp
    Â· have := (Subobject.pullback (prod.map Ï€â‚‚ Ï€â‚‚ :
        (Xâ‚.src â¨¯ Yâ‚.src) â¨¯ Xâ‚‚.tgt â¨¯ Yâ‚‚.tgt âŸ¶ _)).monotone (Hom.le g)
      rw [â† Subobject.pullback_comp, â† Subobject.pullback_comp] at this
      convert this using 3 <;> simp

/-- The unit for the tensor `X âŠ— Y` in `Dial C`. -/
@[simps] def tensorUnitImpl : Dial C := { src := âŠ¤_ _, tgt := âŠ¤_ _, rel := âŠ¤ }

/-- Left unit cancellation `1 âŠ— X â‰… X` in `Dial C`. -/
@[simps!] def leftUnitorImpl (X : Dial C) : tensorObjImpl tensorUnitImpl X â‰… X :=
  isoMk (Limits.prod.leftUnitor _) (Limits.prod.leftUnitor _) <| by simp [Subobject.pullback_top]

/-- Right unit cancellation `X âŠ— 1 â‰… X` in `Dial C`. -/
@[simps!] def rightUnitorImpl (X : Dial C) : tensorObjImpl X tensorUnitImpl â‰… X :=
  isoMk (Limits.prod.rightUnitor _) (Limits.prod.rightUnitor _) <| by simp [Subobject.pullback_top]

/-- The associator for tensor, `(X âŠ— Y) âŠ— Z â‰… X âŠ— (Y âŠ— Z)` in `Dial C`. -/
@[simps!]
def associatorImpl (X Y Z : Dial C) :
    tensorObjImpl (tensorObjImpl X Y) Z â‰… tensorObjImpl X (tensorObjImpl Y Z) :=
  isoMk (prod.associator ..) (prod.associator ..) <| by
    simp [Subobject.inf_pullback, â† Subobject.pullback_comp, inf_assoc]

@[simps!]
instance : MonoidalCategoryStruct (Dial C) where
  tensorUnit := tensorUnitImpl
  tensorObj := tensorObjImpl
  whiskerLeft X _ _ f := tensorHomImpl (ğŸ™ X) f
  whiskerRight f Y := tensorHomImpl f (ğŸ™ Y)
  tensorHom := tensorHomImpl
  leftUnitor := leftUnitorImpl
  rightUnitor := rightUnitorImpl
  associator := associatorImpl

theorem tensor_id (Xâ‚ Xâ‚‚ : Dial C) : (ğŸ™ Xâ‚ âŠ—â‚˜ ğŸ™ Xâ‚‚ : _ âŸ¶ _) = ğŸ™ (Xâ‚ âŠ— Xâ‚‚ : Dial C) := by aesop_cat

theorem tensor_comp {Xâ‚ Yâ‚ Zâ‚ Xâ‚‚ Yâ‚‚ Zâ‚‚ : Dial C}
    (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (gâ‚ : Yâ‚ âŸ¶ Zâ‚) (gâ‚‚ : Yâ‚‚ âŸ¶ Zâ‚‚) :
    tensorHom (fâ‚ â‰« gâ‚) (fâ‚‚ â‰« gâ‚‚) = tensorHom fâ‚ fâ‚‚ â‰« tensorHom gâ‚ gâ‚‚ := by
  ext <;> simp; ext <;> simp <;> (rw [â† Category.assoc]; congr 1; simp)

theorem associator_naturality {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ : Dial C}
    (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ) :
    tensorHom (tensorHom fâ‚ fâ‚‚) fâ‚ƒ â‰« (associator Yâ‚ Yâ‚‚ Yâ‚ƒ).hom =
    (associator Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« tensorHom fâ‚ (tensorHom fâ‚‚ fâ‚ƒ) := by aesop_cat

theorem leftUnitor_naturality {X Y : Dial C} (f : X âŸ¶ Y) :
    (ğŸ™ (ğŸ™_ (Dial C)) âŠ—â‚˜ f) â‰« (Î»_ Y).hom = (Î»_ X).hom â‰« f := by
  ext <;> simp; ext; simp; congr 1; ext <;> simp

theorem rightUnitor_naturality {X Y : Dial C} (f : X âŸ¶ Y) :
    (f âŠ—â‚˜ ğŸ™ (ğŸ™_ (Dial C))) â‰« (Ï_ Y).hom = (Ï_ X).hom â‰« f := by
  ext <;> simp; ext; simp; congr 1; ext <;> simp

theorem pentagon (W X Y Z : Dial C) :
    (tensorHom (associator W X Y).hom (ğŸ™ Z)) â‰« (associator W (tensorObj X Y) Z).hom â‰«
      (tensorHom (ğŸ™ W) (associator X Y Z).hom) =
    (associator (tensorObj W X) Y Z).hom â‰« (associator W X (tensorObj Y Z)).hom := by
  ext <;> simp

theorem triangle (X Y : Dial C) :
    (associator X (ğŸ™_ (Dial C)) Y).hom â‰« tensorHom (ğŸ™ X) (leftUnitor Y).hom =
    tensorHom (rightUnitor X).hom (ğŸ™ Y) := by aesop_cat

instance : MonoidalCategory (Dial C) :=
  .ofTensorHom
    (tensor_id := tensor_id)
    (tensor_comp := tensor_comp)
    (associator_naturality := associator_naturality)
    (leftUnitor_naturality := leftUnitor_naturality)
    (rightUnitor_naturality := rightUnitor_naturality)
    (pentagon := pentagon)
    (triangle := triangle)

/-- The braiding isomorphism `X âŠ— Y â‰… Y âŠ— X` in `Dial C`. -/
@[simps!] def braiding (X Y : Dial C) : tensorObj X Y â‰… tensorObj Y X :=
  isoMk (prod.braiding ..) (prod.braiding ..) <| by
    simp [Subobject.inf_pullback, â† Subobject.pullback_comp, inf_comm]

theorem symmetry (X Y : Dial C) :
    (braiding X Y).hom â‰« (braiding Y X).hom = ğŸ™ (tensorObj X Y) := by aesop_cat

theorem braiding_naturality_right (X : Dial C) {Y Z : Dial C} (f : Y âŸ¶ Z) :
    tensorHom (ğŸ™ X) f â‰« (braiding X Z).hom = (braiding X Y).hom â‰« tensorHom f (ğŸ™ X) := by aesop_cat

theorem braiding_naturality_left {X Y : Dial C} (f : X âŸ¶ Y) (Z : Dial C) :
    tensorHom f (ğŸ™ Z) â‰« (braiding Y Z).hom = (braiding X Z).hom â‰« tensorHom (ğŸ™ Z) f := by aesop_cat

theorem hexagon_forward (X Y Z : Dial C) :
    (associator X Y Z).hom â‰« (braiding X (Y âŠ— Z)).hom â‰« (associator Y Z X).hom =
      tensorHom (braiding X Y).hom (ğŸ™ Z) â‰« (associator Y X Z).hom â‰«
      tensorHom (ğŸ™ Y) (braiding X Z).hom := by aesop_cat

theorem hexagon_reverse (X Y Z : Dial C) :
    (associator X Y Z).inv â‰« (braiding (X âŠ— Y) Z).hom â‰« (associator Z X Y).inv =
      tensorHom (ğŸ™ X) (braiding Y Z).hom â‰« (associator X Z Y).inv â‰«
      tensorHom (braiding X Z).hom (ğŸ™ Y) := by aesop_cat

instance : SymmetricCategory (Dial C) where
  braiding := braiding
  braiding_naturality_right := braiding_naturality_right
  braiding_naturality_left := braiding_naturality_left
  hexagon_forward := hexagon_forward
  hexagon_reverse := hexagon_reverse
  symmetry := symmetry

end Dial

end CategoryTheory
