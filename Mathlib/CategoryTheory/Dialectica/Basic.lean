/-
Copyright (c) 2024 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro
-/
import Mathlib.CategoryTheory.Subobject.Basic
import Mathlib.CategoryTheory.Limits.Shapes.FiniteProducts

/-!
# Dialectica category

We define the category `Dial` of the Dialectica interpretation, after [dialectica1989].

## Background

Dialectica categories are important models of linear type theory. They satisfy most of the
distinctions that linear logic was meant to introduce and many models do not satisfy, like the
independence of constants. Many linear type theories are being used at the
moment--[nLab] describes some of them: for quantum systems, for effects in programming, for linear
dependent types. In particular, dialectica categories are connected to polynomial functors, being a
slightly more sophisticated version of polynomial types, as discussed, for instance, in Moss and
von Glehn's [*Dialectica models of type theory*]. As such they are related to the polynomial
constructions being [developed][Poly] by Awodey, Riehl, and Hazratpour. For the non-dependent
version developed here several applications are known to Petri Nets, small cardinals
in Set Theory, state in imperative programming, and others, see [Dialectica Categories].

## References

* [Valeria de Paiva, The Dialectica Categories.][dialectica1989]
  ([pdf](https://www.cl.cam.ac.uk/techreports/UCAM-CL-TR-213.pdf))

[nLab]: https://ncatlab.org/nlab/show/linear+type+theory
[*Dialectica models of type theory*]: https://arxiv.org/abs/2105.00283
[Poly]: https://github.com/sinhp/Poly
[Dialectica Categories]: https://github.com/vcvpaiva/DialecticaCategories

-/

noncomputable section

namespace CategoryTheory

open Limits

universe v u
variable {C : Type u} [Category.{v} C] [HasFiniteProducts C] [HasPullbacks C]

variable (C) in
/-- The Dialectica category. An object of the category is a triple `âŸ¨U, X, Î± âŠ† U Ã— XâŸ©`,
and a morphism from `âŸ¨U, X, Î±âŸ©` to `âŸ¨V, Y, Î²âŸ©` is a pair `(f : U âŸ¶ V, F : U â¨¯ Y âŸ¶ X)` such that
`{(u,y) | Î±(u, F(u, y))} âŠ† {(u,y) | Î²(f(u), y)}`. The subset `Î±` is actually encoded as an element
of `Subobject (U Ã— X)`, and the above inequality is expressed using pullbacks. -/
structure Dial where
  /-- The source object -/
  src : C
  /-- The target object -/
  tgt : C
  /-- A subobject of `src â¨¯ tgt`, interpreted as a relation -/
  rel : Subobject (src â¨¯ tgt)

namespace Dial

local notation "Ï€â‚" => prod.fst
local notation "Ï€â‚‚" => prod.snd
local notation "Ï€(" a ", " b ")" => prod.lift a b

/-- A morphism in the `Dial C` category from `âŸ¨U, X, Î±âŸ©` to `âŸ¨V, Y, Î²âŸ©` is a pair
`(f : U âŸ¶ V, F : U â¨¯ Y âŸ¶ X)` such that `{(u,y) | Î±(u, F(u, y))} â‰¤ {(u,y) | Î²(f(u), y)}`. -/
@[ext] structure Hom (X Y : Dial C) where
  /-- Maps the sources -/
  f : X.src âŸ¶ Y.src
  /-- Maps the targets (contravariantly) -/
  F : X.src â¨¯ Y.tgt âŸ¶ X.tgt
  /-- This says `{(u, y) | Î±(u, F(u, y))} âŠ† {(u, y) | Î²(f(u), y)}` using subobject pullbacks -/
  le :
    (Subobject.pullback Ï€(Ï€â‚, F)).obj X.rel â‰¤
    (Subobject.pullback (prod.map f (ğŸ™ _))).obj Y.rel

theorem comp_le_lemma {X Y Z : Dial C} (F : Dial.Hom X Y) (G : Dial.Hom Y Z) :
    (Subobject.pullback Ï€(Ï€â‚, Ï€(Ï€â‚, prod.map F.f (ğŸ™ _) â‰« G.F) â‰« F.F)).obj X.rel â‰¤
    (Subobject.pullback (prod.map (F.f â‰« G.f) (ğŸ™ Z.tgt))).obj Z.rel := by
  refine
    le_trans ?_ <| ((Subobject.pullback (Ï€(Ï€â‚, prod.map F.f (ğŸ™ _) â‰« G.F))).monotone F.le).trans <|
    le_trans ?_ <| ((Subobject.pullback (prod.map F.f (ğŸ™ Z.tgt))).monotone G.le).trans ?_
    <;> simp [â† Subobject.pullback_comp]

@[simps]
instance : Category (Dial C) where
  Hom := Dial.Hom
  id X := {
    f := ğŸ™ _
    F := Ï€â‚‚
    le := by simp
  }
  comp {_ _ _} (F G : Dial.Hom ..) := {
    f := F.f â‰« G.f
    F := Ï€(Ï€â‚, prod.map F.f (ğŸ™ _) â‰« G.F) â‰« F.F
    le := comp_le_lemma F G
  }
  assoc f g h := by
    simp only [Category.assoc, Hom.mk.injEq, true_and]
    rw [â† Category.assoc, â† Category.assoc]; congr 1
    ext <;> simp

@[ext] theorem hom_ext {X Y : Dial C} {x y : X âŸ¶ Y} (hf : x.f = y.f) (hF : x.F = y.F) : x = y :=
  Hom.ext hf hF

/--
An isomorphism in `Dial C` can be induced by isomorphisms on the source and target,
which respect the respective relations on `X` and `Y`.
-/
@[simps] def isoMk {X Y : Dial C} (eâ‚ : X.src â‰… Y.src) (eâ‚‚ : X.tgt â‰… Y.tgt)
    (eq : X.rel = (Subobject.pullback (prod.map eâ‚.hom eâ‚‚.hom)).obj Y.rel) : X â‰… Y where
  hom := {
    f := eâ‚.hom
    F := Ï€â‚‚ â‰« eâ‚‚.inv
    le := by rw [eq, â† Subobject.pullback_comp]; apply le_of_eq; congr; ext <;> simp
  }
  inv := {
    f := eâ‚.inv
    F := Ï€â‚‚ â‰« eâ‚‚.hom
    le := by rw [eq, â† Subobject.pullback_comp]; apply le_of_eq; congr; ext <;> simp
  }

end Dial

end CategoryTheory
