/-
Copyright (c) 2017 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stephen Morgan, Kim Morrison, Floris van Doorn
-/
import Mathlib.CategoryTheory.EqToHom
import Mathlib.CategoryTheory.Pi.Basic
import Mathlib.Data.ULift

/-!
# Discrete categories

We define `Discrete Œ±` as a structure containing a term `a : Œ±` for any type `Œ±`,
and use this type alias to provide a `SmallCategory` instance
whose only morphisms are the identities.

There is an annoying technical difficulty that it has turned out to be inconvenient
to allow categories with morphisms living in `Prop`,
so instead of defining `X ‚ü∂ Y` in `Discrete Œ±` as `X = Y`,
one might define it as `PLift (X = Y)`.
In fact, to allow `Discrete Œ±` to be a `SmallCategory`
(i.e. with morphisms in the same universe as the objects),
we actually define the hom type `X ‚ü∂ Y` as `ULift (PLift (X = Y))`.

`Discrete.functor` promotes a function `f : I ‚Üí C` (for any category `C`) to a functor
`Discrete.functor f : Discrete I ‚•§ C`.

Similarly, `Discrete.natTrans` and `Discrete.natIso` promote `I`-indexed families of morphisms,
or `I`-indexed families of isomorphisms to natural transformations or natural isomorphism.

We show equivalences of types are the same as (categorical) equivalences of the corresponding
discrete categories.
-/

namespace CategoryTheory

-- morphism levels before object levels. See note [category theory universes].
universe v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÅ' u‚ÇÇ u‚ÇÉ

-- This is intentionally a structure rather than a type synonym
-- to enforce using `DiscreteEquiv` (or `Discrete.mk` and `Discrete.as`) to move between
-- `Discrete Œ±` and `Œ±`. Otherwise there is too much API leakage.
/-- A wrapper for promoting any type to a category,
with the only morphisms being equalities.
-/
@[ext, aesop safe cases (rule_sets := [CategoryTheory])]
structure Discrete (Œ± : Type u‚ÇÅ) where
  /-- A wrapper for promoting any type to a category,
  with the only morphisms being equalities. -/
  as : Œ±

@[simp]
theorem Discrete.mk_as {Œ± : Type u‚ÇÅ} (X : Discrete Œ±) : Discrete.mk X.as = X :=
  rfl

/-- `Discrete Œ±` is equivalent to the original type `Œ±`. -/
@[simps]
def discreteEquiv {Œ± : Type u‚ÇÅ} : Discrete Œ± ‚âÉ Œ± where
  toFun := Discrete.as
  invFun := Discrete.mk
  left_inv := by cat_disch
  right_inv := by cat_disch

instance {Œ± : Type u‚ÇÅ} [DecidableEq Œ±] : DecidableEq (Discrete Œ±) :=
  discreteEquiv.decidableEq

/-- The "Discrete" category on a type, whose morphisms are equalities.

Because we do not allow morphisms in `Prop` (only in `Type`),
somewhat annoyingly we have to define `X ‚ü∂ Y` as `ULift (PLift (X = Y))`. -/
@[stacks 001A]
instance discreteCategory (Œ± : Type u‚ÇÅ) : SmallCategory (Discrete Œ±) where
  Hom X Y := ULift (PLift (X.as = Y.as))
  id _ := ULift.up (PLift.up rfl)
  comp {X Y Z} g f := by
    cases X
    cases Y
    cases Z
    rcases f with ‚ü®‚ü®‚ü®‚ü©‚ü©‚ü©
    exact g

namespace Discrete

variable {Œ± : Type u‚ÇÅ}

instance [Inhabited Œ±] : Inhabited (Discrete Œ±) :=
  ‚ü®‚ü®default‚ü©‚ü©

instance [Subsingleton Œ±] : Subsingleton (Discrete Œ±) :=
  ‚ü®by cat_disch‚ü©

instance instSubsingletonDiscreteHom (X Y : Discrete Œ±) : Subsingleton (X ‚ü∂ Y) :=
  show Subsingleton (ULift (PLift _)) from inferInstance

/-- A simple tactic to run `cases` on any `Discrete Œ±` hypotheses. -/
macro "discrete_cases" : tactic =>
  `(tactic| fail_if_no_progress casesm* Discrete _, (_ : Discrete _) ‚ü∂ (_ : Discrete _), PLift _)

open Lean Elab Tactic in
/--
Use:
```
attribute [local aesop safe tactic (rule_sets := [CategoryTheory])]
  CategoryTheory.Discrete.discreteCases
```
to locally gives `cat_disch` the ability to call `cases` on
`Discrete` and `(_ : Discrete _) ‚ü∂ (_ : Discrete _)` hypotheses.
-/
def discreteCases : TacticM Unit := do
  evalTactic (‚Üê `(tactic| discrete_cases))

-- TODO: investigate turning on either
-- `attribute [aesop safe cases (rule_sets := [CategoryTheory])] Discrete`
-- or
-- `attribute [aesop safe tactic (rule_sets := [CategoryTheory])] discreteCases`
-- globally.

instance [Unique Œ±] : Unique (Discrete Œ±) :=
  Unique.mk' (Discrete Œ±)

/-- Extract the equation from a morphism in a discrete category. -/
theorem eq_of_hom {X Y : Discrete Œ±} (i : X ‚ü∂ Y) : X.as = Y.as :=
  i.down.down

/-- Promote an equation between the wrapped terms in `X Y : Discrete Œ±` to a morphism `X ‚ü∂ Y`
in the discrete category. -/
protected abbrev eqToHom {X Y : Discrete Œ±} (h : X.as = Y.as) : X ‚ü∂ Y :=
  eqToHom (by cat_disch)

/-- Promote an equation between the wrapped terms in `X Y : Discrete Œ±` to an isomorphism `X ‚âÖ Y`
in the discrete category. -/
protected abbrev eqToIso {X Y : Discrete Œ±} (h : X.as = Y.as) : X ‚âÖ Y :=
  eqToIso (by cat_disch)

/-- A variant of `eqToHom` that lifts terms to the discrete category. -/
abbrev eqToHom' {a b : Œ±} (h : a = b) : Discrete.mk a ‚ü∂ Discrete.mk b :=
  Discrete.eqToHom h

/-- A variant of `eqToIso` that lifts terms to the discrete category. -/
abbrev eqToIso' {a b : Œ±} (h : a = b) : Discrete.mk a ‚âÖ Discrete.mk b :=
  Discrete.eqToIso h

@[simp]
theorem id_def (X : Discrete Œ±) : ULift.up (PLift.up (Eq.refl X.as)) = ùüô X :=
  rfl

variable {C : Type u‚ÇÇ} [Category.{v‚ÇÇ} C]

instance {I : Type u‚ÇÅ} {i j : Discrete I} (f : i ‚ü∂ j) : IsIso f :=
  ‚ü®‚ü®Discrete.eqToHom (eq_of_hom f).symm, by cat_disch‚ü©‚ü©

attribute [local aesop safe tactic (rule_sets := [CategoryTheory])]
  CategoryTheory.Discrete.discreteCases

/-- Any function `I ‚Üí C` gives a functor `Discrete I ‚•§ C`. -/
def functor {I : Type u‚ÇÅ} (F : I ‚Üí C) : Discrete I ‚•§ C where
  obj := F ‚àò Discrete.as
  map {X Y} f := by
    dsimp
    rcases f with ‚ü®‚ü®h‚ü©‚ü©
    exact eqToHom (congrArg _ h)

@[simp]
theorem functor_obj {I : Type u‚ÇÅ} (F : I ‚Üí C) (i : I) :
    (Discrete.functor F).obj (Discrete.mk i) = F i :=
  rfl

theorem functor_map {I : Type u‚ÇÅ} (F : I ‚Üí C) {i : Discrete I} (f : i ‚ü∂ i) :
    (Discrete.functor F).map f = ùüô (F i.as) := by cat_disch

@[simp]
theorem functor_obj_eq_as {I : Type u‚ÇÅ} (F : I ‚Üí C) (X : Discrete I) :
    (Discrete.functor F).obj X = F X.as :=
  rfl
/-- The discrete functor induced by a composition of maps can be written as a
composition of two discrete functors.
-/
@[simps!]
def functorComp {I : Type u‚ÇÅ} {J : Type u‚ÇÅ'} (f : J ‚Üí C) (g : I ‚Üí J) :
    Discrete.functor (f ‚àò g) ‚âÖ Discrete.functor (Discrete.mk ‚àò g) ‚ãô Discrete.functor f :=
  NatIso.ofComponents fun _ => Iso.refl _

/-- For functors out of a discrete category,
a natural transformation is just a collection of maps,
as the naturality squares are trivial.
-/
@[simps]
def natTrans {I : Type u‚ÇÅ} {F G : Discrete I ‚•§ C} (f : ‚àÄ i : Discrete I, F.obj i ‚ü∂ G.obj i) :
    F ‚ü∂ G where
  app := f
  naturality := fun {X Y} ‚ü®‚ü®g‚ü©‚ü© => by
    discrete_cases
    rcases g
    change F.map (ùüô _) ‚â´ _ = _ ‚â´ G.map (ùüô _)
    simp

/-- For functors out of a discrete category,
a natural isomorphism is just a collection of isomorphisms,
as the naturality squares are trivial.
-/
@[simps!]
def natIso {I : Type u‚ÇÅ} {F G : Discrete I ‚•§ C} (f : ‚àÄ i : Discrete I, F.obj i ‚âÖ G.obj i) :
    F ‚âÖ G :=
  NatIso.ofComponents f fun ‚ü®‚ü®g‚ü©‚ü© => by
    discrete_cases
    rcases g
    change F.map (ùüô _) ‚â´ _ = _ ‚â´ G.map (ùüô _)
    simp

instance {I : Type*} {F G : Discrete I ‚•§ C} (f : ‚àÄ i, F.obj i ‚ü∂ G.obj i) [‚àÄ i, IsIso (f i)] :
    IsIso (Discrete.natTrans f) := by
  change IsIso (Discrete.natIso (fun i => asIso (f i))).hom
  infer_instance

@[simp]
theorem natIso_app {I : Type u‚ÇÅ} {F G : Discrete I ‚•§ C} (f : ‚àÄ i : Discrete I, F.obj i ‚âÖ G.obj i)
    (i : Discrete I) : (Discrete.natIso f).app i = f i := by cat_disch

/-- Every functor `F` from a discrete category is naturally isomorphic (actually, equal) to
  `Discrete.functor (F.obj)`. -/
@[simps!]
def natIsoFunctor {I : Type u‚ÇÅ} {F : Discrete I ‚•§ C} : F ‚âÖ Discrete.functor (F.obj ‚àò Discrete.mk) :=
  natIso fun _ => Iso.refl _

/-- Composing `Discrete.functor F` with another functor `G` amounts to composing `F` with `G.obj` -/
@[simps!]
def compNatIsoDiscrete {I : Type u‚ÇÅ} {D : Type u‚ÇÉ} [Category.{v‚ÇÉ} D] (F : I ‚Üí C) (G : C ‚•§ D) :
    Discrete.functor F ‚ãô G ‚âÖ Discrete.functor (G.obj ‚àò F) :=
  natIso fun _ => Iso.refl _

/-- We can promote a type-level `Equiv` to
an equivalence between the corresponding `discrete` categories.
-/
@[simps]
def equivalence {I : Type u‚ÇÅ} {J : Type u‚ÇÇ} (e : I ‚âÉ J) : Discrete I ‚âå Discrete J where
  functor := Discrete.functor (Discrete.mk ‚àò (e : I ‚Üí J))
  inverse := Discrete.functor (Discrete.mk ‚àò (e.symm : J ‚Üí I))
  unitIso :=
    Discrete.natIso fun i => eqToIso (by simp)
  counitIso :=
    Discrete.natIso fun j => eqToIso (by simp)

/-- We can convert an equivalence of `discrete` categories to a type-level `Equiv`. -/
@[simps]
def equivOfEquivalence {Œ± : Type u‚ÇÅ} {Œ≤ : Type u‚ÇÇ} (h : Discrete Œ± ‚âå Discrete Œ≤) : Œ± ‚âÉ Œ≤ where
  toFun := Discrete.as ‚àò h.functor.obj ‚àò Discrete.mk
  invFun := Discrete.as ‚àò h.inverse.obj ‚àò Discrete.mk
  left_inv a := by simpa using eq_of_hom (h.unitIso.app (Discrete.mk a)).2
  right_inv a := by simpa using eq_of_hom (h.counitIso.app (Discrete.mk a)).1

end Discrete

namespace Discrete

variable {J : Type v‚ÇÅ}

open Opposite

/-- A discrete category is equivalent to its opposite category. -/
@[simps! functor_obj_as inverse_obj]
protected def opposite (Œ± : Type u‚ÇÅ) : (Discrete Œ±)·µí·µñ ‚âå Discrete Œ± :=
  let F : Discrete Œ± ‚•§ (Discrete Œ±)·µí·µñ := Discrete.functor fun x => op (Discrete.mk x)
  { functor := F.leftOp
    inverse := F
    unitIso := NatIso.ofComponents fun ‚ü®_‚ü© => Iso.refl _
    counitIso := Discrete.natIso fun ‚ü®_‚ü© => Iso.refl _ }

variable {C : Type u‚ÇÇ} [Category.{v‚ÇÇ} C]

@[simp]
theorem functor_map_id (F : Discrete J ‚•§ C) {j : Discrete J} (f : j ‚ü∂ j) :
    F.map f = ùüô (F.obj j) := by
  have h : f = ùüô j := by cat_disch
  rw [h]
  simp

end Discrete

@[simp]
lemma Discrete.forall {Œ± : Type*} {p : Discrete Œ± ‚Üí Prop} :
    (‚àÄ (a : Discrete Œ±), p a) ‚Üî ‚àÄ (a' : Œ±), p ‚ü®a'‚ü© := by
  rw [iff_iff_eq, discreteEquiv.forall_congr_left]
  simp [discreteEquiv]

@[simp]
lemma Discrete.exists {Œ± : Type*} {p : Discrete Œ± ‚Üí Prop} :
    (‚àÉ (a : Discrete Œ±), p a) ‚Üî ‚àÉ (a' : Œ±), p ‚ü®a'‚ü© := by
  rw [iff_iff_eq, discreteEquiv.exists_congr_left]
  simp [discreteEquiv]

/-- The equivalence of categories `(J ‚Üí C) ‚âå (Discrete J ‚•§ C)`. -/
@[simps]
def piEquivalenceFunctorDiscrete (J : Type u‚ÇÇ) (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] :
    (J ‚Üí C) ‚âå (Discrete J ‚•§ C) where
  functor :=
    { obj := fun F => Discrete.functor F
      map := fun f => Discrete.natTrans (fun j => f j.as) }
  inverse :=
    { obj := fun F j => F.obj ‚ü®j‚ü©
      map := fun f j => f.app ‚ü®j‚ü© }
  unitIso := Iso.refl _
  counitIso := NatIso.ofComponents (fun F => (NatIso.ofComponents (fun _ => Iso.refl _)
    (by
      rintro ‚ü®x‚ü© ‚ü®y‚ü© f
      obtain rfl : x = y := Discrete.eq_of_hom f
      obtain rfl : f = ùüô _ := rfl
      simp))) (by cat_disch)

/-- A category is discrete when there is at most one morphism between two objects,
in which case they are equal. -/
class IsDiscrete (C : Type*) [Category C] : Prop where
  subsingleton (X Y : C) : Subsingleton (X ‚ü∂ Y) := by infer_instance
  eq_of_hom {X Y : C} (f : X ‚ü∂ Y) : X = Y

attribute [instance] IsDiscrete.subsingleton

instance Discrete.isDiscrete (C : Type*) : IsDiscrete (Discrete C) where
  eq_of_hom := by rintro ‚ü®_‚ü© ‚ü®_‚ü© ‚ü®‚ü®rfl‚ü©‚ü©; rfl

section

variable {C : Type*} [Category C] [IsDiscrete C]

lemma obj_ext_of_isDiscrete {X Y : C} (f : X ‚ü∂ Y) : X = Y := IsDiscrete.eq_of_hom f

instance isIso_of_isDiscrete {X Y : C} (f : X ‚ü∂ Y) : IsIso f :=
  ‚ü®eqToHom (IsDiscrete.eq_of_hom f).symm, by cat_disch‚ü©

instance : IsDiscrete C·µí·µñ where
  eq_of_hom := by
    rintro ‚ü®_‚ü© ‚ü®_‚ü© ‚ü®f‚ü©
    obtain rfl := obj_ext_of_isDiscrete f
    rfl

end

end CategoryTheory
