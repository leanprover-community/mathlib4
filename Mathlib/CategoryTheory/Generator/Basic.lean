/-
Copyright (c) 2022 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Limits.EssentiallySmall
import Mathlib.CategoryTheory.Limits.Opposites
import Mathlib.CategoryTheory.Subobject.Lattice
import Mathlib.Data.Set.Opposite

/-!
# Separating and detecting sets

There are several non-equivalent notions of a generator of a category. Here, we consider two of
them:

* We say that `ğ’¢` is a separating set if the functors `C(G, -)` for `G âˆˆ ğ’¢` are collectively
    faithful, i.e., if `h â‰« f = h â‰« g` for all `h` with domain in `ğ’¢` implies `f = g`.
* We say that `ğ’¢` is a detecting set if the functors `C(G, -)` collectively reflect isomorphisms,
    i.e., if any `h` with domain in `ğ’¢` uniquely factors through `f`, then `f` is an isomorphism.

There are, of course, also the dual notions of coseparating and codetecting sets.

## Main results

We
* define predicates `IsSeparating`, `IsCoseparating`, `IsDetecting` and `IsCodetecting` on
  sets of objects;
* show that equivalences of categories preserves these notions;
* show that separating and coseparating are dual notions;
* show that detecting and codetecting are dual notions;
* show that if `C` has equalizers, then detecting implies separating;
* show that if `C` has coequalizers, then codetecting implies coseparating;
* show that if `C` is balanced, then separating implies detecting and coseparating implies
  codetecting;
* show that `âˆ…` is separating if and only if `âˆ…` is coseparating if and only if `C` is thin;
* show that `âˆ…` is detecting if and only if `âˆ…` is codetecting if and only if `C` is a groupoid;
* define predicates `IsSeparator`, `IsCoseparator`, `IsDetector` and `IsCodetector` as the
  singleton counterparts to the definitions for sets above and restate the above results in this
  situation;
* show that `G` is a separator if and only if `coyoneda.obj (op G)` is faithful (and the dual);
* show that `G` is a detector if and only if `coyoneda.obj (op G)` reflects isomorphisms (and the
  dual);
* show that `C` is `WellPowered` if it admits small pullbacks and a detector;
* define corresponding typeclasses `HasSeparator`, `HasCoseparator`, `HasDetector`
  and `HasCodetector` on categories and prove analogous results for these.

## Future work

* We currently don't have any examples yet.

-/


universe w vâ‚ vâ‚‚ uâ‚ uâ‚‚

open CategoryTheory.Limits Opposite

namespace CategoryTheory

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D]

/-- We say that `ğ’¢` is a separating set if the functors `C(G, -)` for `G âˆˆ ğ’¢` are collectively
    faithful, i.e., if `h â‰« f = h â‰« g` for all `h` with domain in `ğ’¢` implies `f = g`. -/
def IsSeparating (ğ’¢ : Set C) : Prop :=
  âˆ€ â¦ƒX Y : Câ¦„ (f g : X âŸ¶ Y), (âˆ€ G âˆˆ ğ’¢, âˆ€ (h : G âŸ¶ X), h â‰« f = h â‰« g) â†’ f = g

/-- We say that `ğ’¢` is a coseparating set if the functors `C(-, G)` for `G âˆˆ ğ’¢` are collectively
    faithful, i.e., if `f â‰« h = g â‰« h` for all `h` with codomain in `ğ’¢` implies `f = g`. -/
def IsCoseparating (ğ’¢ : Set C) : Prop :=
  âˆ€ â¦ƒX Y : Câ¦„ (f g : X âŸ¶ Y), (âˆ€ G âˆˆ ğ’¢, âˆ€ (h : Y âŸ¶ G), f â‰« h = g â‰« h) â†’ f = g

/-- We say that `ğ’¢` is a detecting set if the functors `C(G, -)` collectively reflect isomorphisms,
    i.e., if any `h` with domain in `ğ’¢` uniquely factors through `f`, then `f` is an isomorphism. -/
def IsDetecting (ğ’¢ : Set C) : Prop :=
  âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), (âˆ€ G âˆˆ ğ’¢, âˆ€ (h : G âŸ¶ Y), âˆƒ! h' : G âŸ¶ X, h' â‰« f = h) â†’ IsIso f

/-- We say that `ğ’¢` is a codetecting set if the functors `C(-, G)` collectively reflect
    isomorphisms, i.e., if any `h` with codomain in `G` uniquely factors through `f`, then `f` is
    an isomorphism. -/
def IsCodetecting (ğ’¢ : Set C) : Prop :=
  âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), (âˆ€ G âˆˆ ğ’¢, âˆ€ (h : X âŸ¶ G), âˆƒ! h' : Y âŸ¶ G, f â‰« h' = h) â†’ IsIso f

section Equivalence

lemma IsSeparating.of_equivalence
    {ğ’¢ : Set C} (h : IsSeparating ğ’¢) {D : Type*} [Category D] (Î± : C â‰Œ D) :
    IsSeparating (Î±.functor.obj '' ğ’¢) := fun X Y f g H =>
  Î±.inverse.map_injective (h _ _ (fun Z hZ h => by
    obtain âŸ¨h', rflâŸ© := (Î±.toAdjunction.homEquiv _ _).surjective h
    simp only [Adjunction.homEquiv_unit, Category.assoc, â† Functor.map_comp,
      H (Î±.functor.obj Z) (Set.mem_image_of_mem _ hZ) h']))

lemma IsCoseparating.of_equivalence
    {ğ’¢ : Set C} (h : IsCoseparating ğ’¢) {D : Type*} [Category D] (Î± : C â‰Œ D) :
    IsCoseparating (Î±.functor.obj '' ğ’¢) := fun X Y f g H =>
  Î±.inverse.map_injective (h _ _ (fun Z hZ h => by
    obtain âŸ¨h', rflâŸ© := (Î±.symm.toAdjunction.homEquiv _ _).symm.surjective h
    simp only [Adjunction.homEquiv_symm_apply, â† Category.assoc, â† Functor.map_comp,
      Equivalence.symm_functor, H (Î±.functor.obj Z) (Set.mem_image_of_mem _ hZ) h']))

end Equivalence

section Dual

theorem isSeparating_op_iff (ğ’¢ : Set C) : IsSeparating ğ’¢.op â†” IsCoseparating ğ’¢ := by
  refine âŸ¨fun hğ’¢ X Y f g hfg => ?_, fun hğ’¢ X Y f g hfg => ?_âŸ©
  Â· refine Quiver.Hom.op_inj (hğ’¢ _ _ fun G hG h => Quiver.Hom.unop_inj ?_)
    simpa only [unop_comp, Quiver.Hom.unop_op] using hfg _ (Set.mem_op.1 hG) _
  Â· refine Quiver.Hom.unop_inj (hğ’¢ _ _ fun G hG h => Quiver.Hom.op_inj ?_)
    simpa only [op_comp, Quiver.Hom.op_unop] using hfg _ (Set.op_mem_op.2 hG) _

theorem isCoseparating_op_iff (ğ’¢ : Set C) : IsCoseparating ğ’¢.op â†” IsSeparating ğ’¢ := by
  refine âŸ¨fun hğ’¢ X Y f g hfg => ?_, fun hğ’¢ X Y f g hfg => ?_âŸ©
  Â· refine Quiver.Hom.op_inj (hğ’¢ _ _ fun G hG h => Quiver.Hom.unop_inj ?_)
    simpa only [unop_comp, Quiver.Hom.unop_op] using hfg _ (Set.mem_op.1 hG) _
  Â· refine Quiver.Hom.unop_inj (hğ’¢ _ _ fun G hG h => Quiver.Hom.op_inj ?_)
    simpa only [op_comp, Quiver.Hom.op_unop] using hfg _ (Set.op_mem_op.2 hG) _

theorem isCoseparating_unop_iff (ğ’¢ : Set Cáµ’áµ–) : IsCoseparating ğ’¢.unop â†” IsSeparating ğ’¢ := by
  rw [â† isSeparating_op_iff, Set.unop_op]

theorem isSeparating_unop_iff (ğ’¢ : Set Cáµ’áµ–) : IsSeparating ğ’¢.unop â†” IsCoseparating ğ’¢ := by
  rw [â† isCoseparating_op_iff, Set.unop_op]

theorem isDetecting_op_iff (ğ’¢ : Set C) : IsDetecting ğ’¢.op â†” IsCodetecting ğ’¢ := by
  refine âŸ¨fun hğ’¢ X Y f hf => ?_, fun hğ’¢ X Y f hf => ?_âŸ©
  Â· refine (isIso_op_iff _).1 (hğ’¢ _ fun G hG h => ?_)
    obtain âŸ¨t, ht, ht'âŸ© := hf (unop G) (Set.mem_op.1 hG) h.unop
    exact
      âŸ¨t.op, Quiver.Hom.unop_inj ht, fun y hy => Quiver.Hom.unop_inj (ht' _ (Quiver.Hom.op_inj hy))âŸ©
  Â· refine (isIso_unop_iff _).1 (hğ’¢ _ fun G hG h => ?_)
    obtain âŸ¨t, ht, ht'âŸ© := hf (op G) (Set.op_mem_op.2 hG) h.op
    refine âŸ¨t.unop, Quiver.Hom.op_inj ht, fun y hy => Quiver.Hom.op_inj (ht' _ ?_)âŸ©
    exact Quiver.Hom.unop_inj (by simpa only using hy)

theorem isCodetecting_op_iff (ğ’¢ : Set C) : IsCodetecting ğ’¢.op â†” IsDetecting ğ’¢ := by
  refine âŸ¨fun hğ’¢ X Y f hf => ?_, fun hğ’¢ X Y f hf => ?_âŸ©
  Â· refine (isIso_op_iff _).1 (hğ’¢ _ fun G hG h => ?_)
    obtain âŸ¨t, ht, ht'âŸ© := hf (unop G) (Set.mem_op.1 hG) h.unop
    exact
      âŸ¨t.op, Quiver.Hom.unop_inj ht, fun y hy => Quiver.Hom.unop_inj (ht' _ (Quiver.Hom.op_inj hy))âŸ©
  Â· refine (isIso_unop_iff _).1 (hğ’¢ _ fun G hG h => ?_)
    obtain âŸ¨t, ht, ht'âŸ© := hf (op G) (Set.op_mem_op.2 hG) h.op
    refine âŸ¨t.unop, Quiver.Hom.op_inj ht, fun y hy => Quiver.Hom.op_inj (ht' _ ?_)âŸ©
    exact Quiver.Hom.unop_inj (by simpa only using hy)

theorem isDetecting_unop_iff (ğ’¢ : Set Cáµ’áµ–) : IsDetecting ğ’¢.unop â†” IsCodetecting ğ’¢ := by
  rw [â† isCodetecting_op_iff, Set.unop_op]

theorem isCodetecting_unop_iff {ğ’¢ : Set Cáµ’áµ–} : IsCodetecting ğ’¢.unop â†” IsDetecting ğ’¢ := by
  rw [â† isDetecting_op_iff, Set.unop_op]

end Dual

theorem IsDetecting.isSeparating [HasEqualizers C] {ğ’¢ : Set C} (hğ’¢ : IsDetecting ğ’¢) :
    IsSeparating ğ’¢ := fun _ _ f g hfg =>
  have : IsIso (equalizer.Î¹ f g) := hğ’¢ _ fun _ hG _ => equalizer.existsUnique _ (hfg _ hG _)
  eq_of_epi_equalizer

theorem IsCodetecting.isCoseparating [HasCoequalizers C] {ğ’¢ : Set C} :
    IsCodetecting ğ’¢ â†’ IsCoseparating ğ’¢ := by
  simpa only [â† isSeparating_op_iff, â† isDetecting_op_iff] using IsDetecting.isSeparating

theorem IsSeparating.isDetecting [Balanced C] {ğ’¢ : Set C} (hğ’¢ : IsSeparating ğ’¢) :
    IsDetecting ğ’¢ := by
  intro X Y f hf
  refine
    (isIso_iff_mono_and_epi _).2 âŸ¨âŸ¨fun g h hgh => hğ’¢ _ _ fun G hG i => ?_âŸ©, âŸ¨fun g h hgh => ?_âŸ©âŸ©
  Â· obtain âŸ¨t, -, htâŸ© := hf G hG (i â‰« g â‰« f)
    rw [ht (i â‰« g) (Category.assoc _ _ _), ht (i â‰« h) (hgh.symm â–¸ Category.assoc _ _ _)]
  Â· refine hğ’¢ _ _ fun G hG i => ?_
    obtain âŸ¨t, rfl, -âŸ© := hf G hG i
    rw [Category.assoc, hgh, Category.assoc]

lemma IsDetecting.isIso_iff_of_mono {ğ’¢ : Set C} (hğ’¢ : IsDetecting ğ’¢)
    {X Y : C} (f : X âŸ¶ Y) [Mono f] :
    IsIso f â†” âˆ€ s âˆˆ ğ’¢, Function.Surjective ((coyoneda.obj (op s)).map f) := by
  constructor
  Â· intro h
    rw [isIso_iff_yoneda_map_bijective] at h
    intro A _
    exact (h A).2
  Â· intro hf
    refine hğ’¢ _ (fun A hA g â†¦ existsUnique_of_exists_of_unique ?_ ?_)
    Â· exact hf A hA g
    Â· intro lâ‚ lâ‚‚ hâ‚ hâ‚‚
      rw [â† cancel_mono f, hâ‚, hâ‚‚]

lemma IsCodetecting.isIso_iff_of_epi {ğ’¢ : Set C} (hğ’¢ : IsCodetecting ğ’¢)
    {X Y : C} (f : X âŸ¶ Y) [Epi f] :
    IsIso f â†” âˆ€ s âˆˆ ğ’¢, Function.Surjective ((yoneda.obj s).map f.op) := by
  constructor
  Â· intro h
    rw [isIso_iff_coyoneda_map_bijective] at h
    intro A _
    exact (h A).2
  Â· intro hf
    refine hğ’¢ _ (fun A hA g â†¦ existsUnique_of_exists_of_unique ?_ ?_)
    Â· exact hf A hA g
    Â· intro lâ‚ lâ‚‚ hâ‚ hâ‚‚
      rw [â† cancel_epi f, hâ‚, hâ‚‚]

section

attribute [local instance] balanced_opposite

theorem IsCoseparating.isCodetecting [Balanced C] {ğ’¢ : Set C} :
    IsCoseparating ğ’¢ â†’ IsCodetecting ğ’¢ := by
  simpa only [â† isDetecting_op_iff, â† isSeparating_op_iff] using IsSeparating.isDetecting

end

theorem isDetecting_iff_isSeparating [HasEqualizers C] [Balanced C] (ğ’¢ : Set C) :
    IsDetecting ğ’¢ â†” IsSeparating ğ’¢ :=
  âŸ¨IsDetecting.isSeparating, IsSeparating.isDetectingâŸ©

theorem isCodetecting_iff_isCoseparating [HasCoequalizers C] [Balanced C] {ğ’¢ : Set C} :
    IsCodetecting ğ’¢ â†” IsCoseparating ğ’¢ :=
  âŸ¨IsCodetecting.isCoseparating, IsCoseparating.isCodetectingâŸ©

section Mono

theorem IsSeparating.mono {ğ’¢ : Set C} (hğ’¢ : IsSeparating ğ’¢) {â„‹ : Set C} (hğ’¢â„‹ : ğ’¢ âŠ† â„‹) :
    IsSeparating â„‹ := fun _ _ _ _ hfg => hğ’¢ _ _ fun _ hG _ => hfg _ (hğ’¢â„‹ hG) _

theorem IsCoseparating.mono {ğ’¢ : Set C} (hğ’¢ : IsCoseparating ğ’¢) {â„‹ : Set C} (hğ’¢â„‹ : ğ’¢ âŠ† â„‹) :
    IsCoseparating â„‹ := fun _ _ _ _ hfg => hğ’¢ _ _ fun _ hG _ => hfg _ (hğ’¢â„‹ hG) _

theorem IsDetecting.mono {ğ’¢ : Set C} (hğ’¢ : IsDetecting ğ’¢) {â„‹ : Set C} (hğ’¢â„‹ : ğ’¢ âŠ† â„‹) :
    IsDetecting â„‹ := fun _ _ _ hf => hğ’¢ _ fun _ hG _ => hf _ (hğ’¢â„‹ hG) _

theorem IsCodetecting.mono {ğ’¢ : Set C} (hğ’¢ : IsCodetecting ğ’¢) {â„‹ : Set C} (hğ’¢â„‹ : ğ’¢ âŠ† â„‹) :
    IsCodetecting â„‹ := fun _ _ _ hf => hğ’¢ _ fun _ hG _ => hf _ (hğ’¢â„‹ hG) _

end Mono

section Empty

theorem thin_of_isSeparating_empty (h : IsSeparating (âˆ… : Set C)) : Quiver.IsThin C := fun _ _ =>
  âŸ¨fun _ _ => h _ _ fun _ => False.elimâŸ©

theorem isSeparating_empty_of_thin [Quiver.IsThin C] : IsSeparating (âˆ… : Set C) :=
  fun _ _ _ _ _ => Subsingleton.elim _ _

theorem thin_of_isCoseparating_empty (h : IsCoseparating (âˆ… : Set C)) : Quiver.IsThin C :=
  fun _ _ => âŸ¨fun _ _ => h _ _ fun _ => False.elimâŸ©

theorem isCoseparating_empty_of_thin [Quiver.IsThin C] : IsCoseparating (âˆ… : Set C) :=
  fun _ _ _ _ _ => Subsingleton.elim _ _

theorem groupoid_of_isDetecting_empty (h : IsDetecting (âˆ… : Set C)) {X Y : C} (f : X âŸ¶ Y) :
    IsIso f :=
  h _ fun _ => False.elim

theorem isDetecting_empty_of_groupoid [âˆ€ {X Y : C} (f : X âŸ¶ Y), IsIso f] :
    IsDetecting (âˆ… : Set C) := fun _ _ _ _ => inferInstance

theorem groupoid_of_isCodetecting_empty (h : IsCodetecting (âˆ… : Set C)) {X Y : C} (f : X âŸ¶ Y) :
    IsIso f :=
  h _ fun _ => False.elim

theorem isCodetecting_empty_of_groupoid [âˆ€ {X Y : C} (f : X âŸ¶ Y), IsIso f] :
    IsCodetecting (âˆ… : Set C) := fun _ _ _ _ => inferInstance

end Empty

theorem isSeparating_iff_epi (ğ’¢ : Set C)
    [âˆ€ A : C, HasCoproduct fun f : Î£ G : ğ’¢, (G : C) âŸ¶ A => (f.1 : C)] :
    IsSeparating ğ’¢ â†” âˆ€ A : C, Epi (Sigma.desc (@Sigma.snd ğ’¢ fun G => (G : C) âŸ¶ A)) := by
  refine âŸ¨fun h A => âŸ¨fun u v huv => h _ _ fun G hG f => ?_âŸ©, fun h X Y f g hh => ?_âŸ©
  Â· simpa using Sigma.Î¹ (fun f : Î£ G : ğ’¢, (G : C) âŸ¶ A => (f.1 : C)) âŸ¨âŸ¨G, hGâŸ©, fâŸ© â‰«= huv
  Â· haveI := h X
    refine
      (cancel_epi (Sigma.desc (@Sigma.snd ğ’¢ fun G => (G : C) âŸ¶ X))).1 (colimit.hom_ext fun j => ?_)
    simpa using hh j.as.1.1 j.as.1.2 j.as.2

theorem isCoseparating_iff_mono (ğ’¢ : Set C)
    [âˆ€ A : C, HasProduct fun f : Î£ G : ğ’¢, A âŸ¶ (G : C) => (f.1 : C)] :
    IsCoseparating ğ’¢ â†” âˆ€ A : C, Mono (Pi.lift (@Sigma.snd ğ’¢ fun G => A âŸ¶ (G : C))) := by
  refine âŸ¨fun h A => âŸ¨fun u v huv => h _ _ fun G hG f => ?_âŸ©, fun h X Y f g hh => ?_âŸ©
  Â· simpa using huv =â‰« Pi.Ï€ (fun f : Î£ G : ğ’¢, A âŸ¶ (G : C) => (f.1 : C)) âŸ¨âŸ¨G, hGâŸ©, fâŸ©
  Â· haveI := h Y
    refine (cancel_mono (Pi.lift (@Sigma.snd ğ’¢ fun G => Y âŸ¶ (G : C)))).1 (limit.hom_ext fun j => ?_)
    simpa using hh j.as.1.1 j.as.1.2 j.as.2

/-- An ingredient of the proof of the Special Adjoint Functor Theorem: a complete well-powered
    category with a small coseparating set has an initial object.

    In fact, it follows from the Special Adjoint Functor Theorem that `C` is already cocomplete,
    see `hasColimits_of_hasLimits_of_isCoseparating`. -/
theorem hasInitial_of_isCoseparating [LocallySmall.{w} C] [WellPowered.{w} C]
    [HasLimitsOfSize.{w, w} C] {ğ’¢ : Set C} [Small.{w} ğ’¢]
    (hğ’¢ : IsCoseparating ğ’¢) : HasInitial C := by
  have := hasFiniteLimits_of_hasLimitsOfSize C
  haveI : HasProductsOfShape ğ’¢ C := hasProductsOfShape_of_small C ğ’¢
  haveI := fun A => hasProductsOfShape_of_small.{w} C (Î£ G : ğ’¢, A âŸ¶ (G : C))
  letI := completeLatticeOfCompleteSemilatticeInf (Subobject (piObj (Subtype.val : ğ’¢ â†’ C)))
  suffices âˆ€ A : C, Unique (((âŠ¥ : Subobject (piObj (Subtype.val : ğ’¢ â†’ C))) : C) âŸ¶ A) by
    exact hasInitial_of_unique ((âŠ¥ : Subobject (piObj (Subtype.val : ğ’¢ â†’ C))) : C)
  refine fun A => âŸ¨âŸ¨?_âŸ©, fun f => ?_âŸ©
  Â· let s := Pi.lift fun f : Î£ G : ğ’¢, A âŸ¶ (G : C) => id (Pi.Ï€ (Subtype.val : ğ’¢ â†’ C)) f.1
    let t := Pi.lift (@Sigma.snd ğ’¢ fun G => A âŸ¶ (G : C))
    haveI : Mono t := (isCoseparating_iff_mono ğ’¢).1 hğ’¢ A
    exact Subobject.ofLEMk _ (pullback.fst _ _ : pullback s t âŸ¶ _) bot_le â‰« pullback.snd _ _
  Â· suffices âˆ€ (g : Subobject.underlying.obj âŠ¥ âŸ¶ A), f = g by
      apply this
    intro g
    suffices IsSplitEpi (equalizer.Î¹ f g) by exact eq_of_epi_equalizer
    exact IsSplitEpi.mk' âŸ¨Subobject.ofLEMk _ (equalizer.Î¹ f g â‰« Subobject.arrow _) bot_le, by
      ext
      simpâŸ©

/-- An ingredient of the proof of the Special Adjoint Functor Theorem: a cocomplete well-copowered
    category with a small separating set has a terminal object.

    In fact, it follows from the Special Adjoint Functor Theorem that `C` is already complete, see
    `hasLimits_of_hasColimits_of_isSeparating`. -/
theorem hasTerminal_of_isSeparating [LocallySmall.{w} Cáµ’áµ–] [WellPowered.{w} Cáµ’áµ–]
    [HasColimitsOfSize.{w, w} C] {ğ’¢ : Set C} [Small.{w} ğ’¢]
    (hğ’¢ : IsSeparating ğ’¢) : HasTerminal C := by
  haveI : Small.{w} ğ’¢.op := small_of_injective (Set.opEquiv_self ğ’¢).injective
  haveI : HasInitial Cáµ’áµ– := hasInitial_of_isCoseparating ((isCoseparating_op_iff _).2 hğ’¢)
  exact hasTerminal_of_hasInitial_op

section WellPowered

namespace Subobject

theorem eq_of_le_of_isDetecting {ğ’¢ : Set C} (hğ’¢ : IsDetecting ğ’¢) {X : C} (P Q : Subobject X)
    (hâ‚ : P â‰¤ Q) (hâ‚‚ : âˆ€ G âˆˆ ğ’¢, âˆ€ {f : G âŸ¶ X}, Q.Factors f â†’ P.Factors f) : P = Q := by
  suffices IsIso (ofLE _ _ hâ‚) by exact le_antisymm hâ‚ (le_of_comm (inv (ofLE _ _ hâ‚)) (by simp))
  refine hğ’¢ _ fun G hG f => ?_
  have : P.Factors (f â‰« Q.arrow) := hâ‚‚ _ hG ((factors_iff _ _).2 âŸ¨_, rflâŸ©)
  refine âŸ¨factorThru _ _ this, ?_, fun g (hg : g â‰« _ = f) => ?_âŸ©
  Â· simp only [â† cancel_mono Q.arrow, Category.assoc, ofLE_arrow, factorThru_arrow]
  Â· simp only [â† cancel_mono (Subobject.ofLE _ _ hâ‚), â† cancel_mono Q.arrow, hg, Category.assoc,
      ofLE_arrow, factorThru_arrow]

theorem inf_eq_of_isDetecting [HasPullbacks C] {ğ’¢ : Set C} (hğ’¢ : IsDetecting ğ’¢) {X : C}
    (P Q : Subobject X) (h : âˆ€ G âˆˆ ğ’¢, âˆ€ {f : G âŸ¶ X}, P.Factors f â†’ Q.Factors f) : P âŠ“ Q = P :=
  eq_of_le_of_isDetecting hğ’¢ _ _ _root_.inf_le_left
    fun _ hG _ hf => (inf_factors _).2 âŸ¨hf, h _ hG hfâŸ©

theorem eq_of_isDetecting [HasPullbacks C] {ğ’¢ : Set C} (hğ’¢ : IsDetecting ğ’¢) {X : C}
    (P Q : Subobject X) (h : âˆ€ G âˆˆ ğ’¢, âˆ€ {f : G âŸ¶ X}, P.Factors f â†” Q.Factors f) : P = Q :=
  calc
    P = P âŠ“ Q := Eq.symm <| inf_eq_of_isDetecting hğ’¢ _ _ fun G hG _ hf => (h G hG).1 hf
    _ = Q âŠ“ P := inf_comm ..
    _ = Q := inf_eq_of_isDetecting hğ’¢ _ _ fun G hG _ hf => (h G hG).2 hf

end Subobject

/-- A category with pullbacks and a small detecting set is well-powered. -/
theorem wellPowered_of_isDetecting [HasPullbacks C] {ğ’¢ : Set C} [Small.{w} ğ’¢]
    [LocallySmall.{w} C] (hğ’¢ : IsDetecting ğ’¢) : WellPowered.{w} C :=
  âŸ¨fun X =>
    @small_of_injective _ _ _ (fun P : Subobject X => { f : Î£ G : ğ’¢, G.1 âŸ¶ X | P.Factors f.2 })
      fun P Q h => Subobject.eq_of_isDetecting hğ’¢ _ _
        (by simpa [Set.ext_iff, Sigma.forall] using h)âŸ©

end WellPowered

namespace StructuredArrow

variable (S : D) (T : C â¥¤ D)

theorem isCoseparating_proj_preimage {ğ’¢ : Set C} (hğ’¢ : IsCoseparating ğ’¢) :
    IsCoseparating ((proj S T).obj â»Â¹' ğ’¢) := by
  refine fun X Y f g hfg => ext _ _ (hğ’¢ _ _ fun G hG h => ?_)
  exact congr_arg CommaMorphism.right (hfg (mk (Y.hom â‰« T.map h)) hG (homMk h rfl))

end StructuredArrow

namespace CostructuredArrow

variable (S : C â¥¤ D) (T : D)

theorem isSeparating_proj_preimage {ğ’¢ : Set C} (hğ’¢ : IsSeparating ğ’¢) :
    IsSeparating ((proj S T).obj â»Â¹' ğ’¢) := by
  refine fun X Y f g hfg => ext _ _ (hğ’¢ _ _ fun G hG h => ?_)
  exact congr_arg CommaMorphism.left (hfg (mk (S.map h â‰« X.hom)) hG (homMk h rfl))

end CostructuredArrow

/-- We say that `G` is a separator if the functor `C(G, -)` is faithful. -/
def IsSeparator (G : C) : Prop :=
  IsSeparating ({G} : Set C)

/-- We say that `G` is a coseparator if the functor `C(-, G)` is faithful. -/
def IsCoseparator (G : C) : Prop :=
  IsCoseparating ({G} : Set C)

/-- We say that `G` is a detector if the functor `C(G, -)` reflects isomorphisms. -/
def IsDetector (G : C) : Prop :=
  IsDetecting ({G} : Set C)

/-- We say that `G` is a codetector if the functor `C(-, G)` reflects isomorphisms. -/
def IsCodetector (G : C) : Prop :=
  IsCodetecting ({G} : Set C)


section Equivalence

theorem IsSeparator.of_equivalence {G : C} (h : IsSeparator G) (Î± : C â‰Œ D) :
    IsSeparator (Î±.functor.obj G) := by simpa using IsSeparating.of_equivalence h Î±

theorem IsCoseparator.of_equivalence {G : C} (h : IsCoseparator G) (Î± : C â‰Œ D) :
    IsCoseparator (Î±.functor.obj G) := by simpa using IsCoseparating.of_equivalence h Î±

end Equivalence

section Dual

theorem isSeparator_op_iff (G : C) : IsSeparator (op G) â†” IsCoseparator G := by
  rw [IsSeparator, IsCoseparator, â† isSeparating_op_iff, Set.singleton_op]

theorem isCoseparator_op_iff (G : C) : IsCoseparator (op G) â†” IsSeparator G := by
  rw [IsSeparator, IsCoseparator, â† isCoseparating_op_iff, Set.singleton_op]

theorem isCoseparator_unop_iff (G : Cáµ’áµ–) : IsCoseparator (unop G) â†” IsSeparator G := by
  rw [IsSeparator, IsCoseparator, â† isCoseparating_unop_iff, Set.singleton_unop]

theorem isSeparator_unop_iff (G : Cáµ’áµ–) : IsSeparator (unop G) â†” IsCoseparator G := by
  rw [IsSeparator, IsCoseparator, â† isSeparating_unop_iff, Set.singleton_unop]

theorem isDetector_op_iff (G : C) : IsDetector (op G) â†” IsCodetector G := by
  rw [IsDetector, IsCodetector, â† isDetecting_op_iff, Set.singleton_op]

theorem isCodetector_op_iff (G : C) : IsCodetector (op G) â†” IsDetector G := by
  rw [IsDetector, IsCodetector, â† isCodetecting_op_iff, Set.singleton_op]

theorem isCodetector_unop_iff (G : Cáµ’áµ–) : IsCodetector (unop G) â†” IsDetector G := by
  rw [IsDetector, IsCodetector, â† isCodetecting_unop_iff, Set.singleton_unop]

theorem isDetector_unop_iff (G : Cáµ’áµ–) : IsDetector (unop G) â†” IsCodetector G := by
  rw [IsDetector, IsCodetector, â† isDetecting_unop_iff, Set.singleton_unop]

end Dual

theorem IsDetector.isSeparator [HasEqualizers C] {G : C} : IsDetector G â†’ IsSeparator G :=
  IsDetecting.isSeparating

theorem IsCodetector.isCoseparator [HasCoequalizers C] {G : C} : IsCodetector G â†’ IsCoseparator G :=
  IsCodetecting.isCoseparating

theorem IsSeparator.isDetector [Balanced C] {G : C} : IsSeparator G â†’ IsDetector G :=
  IsSeparating.isDetecting

theorem IsCoseparator.isCodetector [Balanced C] {G : C} : IsCoseparator G â†’ IsCodetector G :=
  IsCoseparating.isCodetecting

theorem isSeparator_def (G : C) :
    IsSeparator G â†” âˆ€ â¦ƒX Y : Câ¦„ (f g : X âŸ¶ Y), (âˆ€ h : G âŸ¶ X, h â‰« f = h â‰« g) â†’ f = g :=
  âŸ¨fun hG X Y f g hfg =>
    hG _ _ fun H hH h => by
      obtain rfl := Set.mem_singleton_iff.1 hH
      exact hfg h,
    fun hG _ _ _ _ hfg => hG _ _ fun _ => hfg _ (Set.mem_singleton _) _âŸ©

theorem IsSeparator.def {G : C} :
    IsSeparator G â†’ âˆ€ â¦ƒX Y : Câ¦„ (f g : X âŸ¶ Y), (âˆ€ h : G âŸ¶ X, h â‰« f = h â‰« g) â†’ f = g :=
  (isSeparator_def _).1

theorem isCoseparator_def (G : C) :
    IsCoseparator G â†” âˆ€ â¦ƒX Y : Câ¦„ (f g : X âŸ¶ Y), (âˆ€ h : Y âŸ¶ G, f â‰« h = g â‰« h) â†’ f = g :=
  âŸ¨fun hG X Y f g hfg =>
    hG _ _ fun H hH h => by
      obtain rfl := Set.mem_singleton_iff.1 hH
      exact hfg h,
    fun hG _ _ _ _ hfg => hG _ _ fun _ => hfg _ (Set.mem_singleton _) _âŸ©

theorem IsCoseparator.def {G : C} :
    IsCoseparator G â†’ âˆ€ â¦ƒX Y : Câ¦„ (f g : X âŸ¶ Y), (âˆ€ h : Y âŸ¶ G, f â‰« h = g â‰« h) â†’ f = g :=
  (isCoseparator_def _).1

theorem isDetector_def (G : C) :
    IsDetector G â†” âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), (âˆ€ h : G âŸ¶ Y, âˆƒ! h', h' â‰« f = h) â†’ IsIso f :=
  âŸ¨fun hG X Y f hf =>
    hG _ fun H hH h => by
      obtain rfl := Set.mem_singleton_iff.1 hH
      exact hf h,
    fun hG _ _ _ hf => hG _ fun _ => hf _ (Set.mem_singleton _) _âŸ©

theorem IsDetector.def {G : C} :
    IsDetector G â†’ âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), (âˆ€ h : G âŸ¶ Y, âˆƒ! h', h' â‰« f = h) â†’ IsIso f :=
  (isDetector_def _).1

theorem isCodetector_def (G : C) :
    IsCodetector G â†” âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), (âˆ€ h : X âŸ¶ G, âˆƒ! h', f â‰« h' = h) â†’ IsIso f :=
  âŸ¨fun hG X Y f hf =>
    hG _ fun H hH h => by
      obtain rfl := Set.mem_singleton_iff.1 hH
      exact hf h,
    fun hG _ _ _ hf => hG _ fun _ => hf _ (Set.mem_singleton _) _âŸ©

theorem IsCodetector.def {G : C} :
    IsCodetector G â†’ âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), (âˆ€ h : X âŸ¶ G, âˆƒ! h', f â‰« h' = h) â†’ IsIso f :=
  (isCodetector_def _).1

theorem isSeparator_iff_faithful_coyoneda_obj (G : C) :
    IsSeparator G â†” (coyoneda.obj (op G)).Faithful :=
  âŸ¨fun hG => âŸ¨fun hfg => hG.def _ _ (congr_fun hfg)âŸ©, fun _ =>
    (isSeparator_def _).2 fun _ _ _ _ hfg => (coyoneda.obj (op G)).map_injective (funext hfg)âŸ©

theorem isCoseparator_iff_faithful_yoneda_obj (G : C) : IsCoseparator G â†” (yoneda.obj G).Faithful :=
  âŸ¨fun hG => âŸ¨fun hfg => Quiver.Hom.unop_inj (hG.def _ _ (congr_fun hfg))âŸ©, fun _ =>
    (isCoseparator_def _).2 fun _ _ _ _ hfg =>
      Quiver.Hom.op_inj <| (yoneda.obj G).map_injective (funext hfg)âŸ©

theorem isSeparator_iff_epi (G : C) [âˆ€ A : C, HasCoproduct fun _ : G âŸ¶ A => G] :
    IsSeparator G â†” âˆ€ A : C, Epi (Sigma.desc fun f : G âŸ¶ A => f) := by
  rw [isSeparator_def]
  refine âŸ¨fun h A => âŸ¨fun u v huv => h _ _ fun i => ?_âŸ©, fun h X Y f g hh => ?_âŸ©
  Â· simpa using Sigma.Î¹ _ i â‰«= huv
  Â· haveI := h X
    refine (cancel_epi (Sigma.desc fun f : G âŸ¶ X => f)).1 (colimit.hom_ext fun j => ?_)
    simpa using hh j.as

theorem isCoseparator_iff_mono (G : C) [âˆ€ A : C, HasProduct fun _ : A âŸ¶ G => G] :
    IsCoseparator G â†” âˆ€ A : C, Mono (Pi.lift fun f : A âŸ¶ G => f) := by
  rw [isCoseparator_def]
  refine âŸ¨fun h A => âŸ¨fun u v huv => h _ _ fun i => ?_âŸ©, fun h X Y f g hh => ?_âŸ©
  Â· simpa using huv =â‰« Pi.Ï€ _ i
  Â· haveI := h Y
    refine (cancel_mono (Pi.lift fun f : Y âŸ¶ G => f)).1 (limit.hom_ext fun j => ?_)
    simpa using hh j.as

section ZeroMorphisms

variable [HasZeroMorphisms C]

theorem isSeparator_coprod (G H : C) [HasBinaryCoproduct G H] :
    IsSeparator (G â¨¿ H) â†” IsSeparating ({G, H} : Set C) := by
  refine
    âŸ¨fun h X Y u v huv => ?_, fun h =>
      (isSeparator_def _).2 fun X Y u v huv => h _ _ fun Z hZ g => ?_âŸ©
  Â· refine h.def _ _ fun g => coprod.hom_ext ?_ ?_
    Â· simpa using huv G (by simp) (coprod.inl â‰« g)
    Â· simpa using huv H (by simp) (coprod.inr â‰« g)
  Â· simp only [Set.mem_insert_iff, Set.mem_singleton_iff] at hZ
    rcases hZ with (rfl | rfl)
    Â· simpa using coprod.inl â‰«= huv (coprod.desc g 0)
    Â· simpa using coprod.inr â‰«= huv (coprod.desc 0 g)

theorem isSeparator_coprod_of_isSeparator_left (G H : C) [HasBinaryCoproduct G H]
    (hG : IsSeparator G) : IsSeparator (G â¨¿ H) :=
  (isSeparator_coprod _ _).2 <| IsSeparating.mono hG <| by simp

theorem isSeparator_coprod_of_isSeparator_right (G H : C) [HasBinaryCoproduct G H]
    (hH : IsSeparator H) : IsSeparator (G â¨¿ H) :=
  (isSeparator_coprod _ _).2 <| IsSeparating.mono hH <| by simp

lemma isSeparator_of_isColimit_cofan {Î² : Type w} {f : Î² â†’ C}
    (hf : IsSeparating (Set.range f)) {c : Cofan f} (hc : IsColimit c) : IsSeparator c.pt := by
  refine (isSeparator_def _).2 fun X Y u v huv => hf _ _ fun Z hZ g => ?_
  obtain âŸ¨b, rflâŸ© := Set.mem_range.1 hZ
  classical simpa using c.Î¹.app âŸ¨bâŸ© â‰«= huv (hc.desc (Cofan.mk _ (Pi.single b g)))

theorem isSeparator_sigma {Î² : Type w} (f : Î² â†’ C) [HasCoproduct f] :
    IsSeparator (âˆ f) â†” IsSeparating (Set.range f) := by
  refine âŸ¨fun h X Y u v huv => ?_, fun h => isSeparator_of_isColimit_cofan h (colimit.isColimit _)âŸ©
  refine h.def _ _ fun g => colimit.hom_ext fun b => ?_
  simpa using huv (f b.as) (by simp) (colimit.Î¹ (Discrete.functor f) _ â‰« g)

theorem IsSeparating.isSeparator_coproduct {Î² : Type w} {f : Î² â†’ C} [HasCoproduct f]
    (hS : IsSeparating (Set.range f)) : IsSeparator (âˆ f) :=
  (isSeparator_sigma _).2 hS

theorem isSeparator_sigma_of_isSeparator {Î² : Type w} (f : Î² â†’ C) [HasCoproduct f] (b : Î²)
    (hb : IsSeparator (f b)) : IsSeparator (âˆ f) :=
  (isSeparator_sigma _).2 <| IsSeparating.mono hb <| by simp

theorem isCoseparator_prod (G H : C) [HasBinaryProduct G H] :
    IsCoseparator (G â¨¯ H) â†” IsCoseparating ({G, H} : Set C) := by
  refine
    âŸ¨fun h X Y u v huv => ?_, fun h =>
      (isCoseparator_def _).2 fun X Y u v huv => h _ _ fun Z hZ g => ?_âŸ©
  Â· refine h.def _ _ fun g => Limits.prod.hom_ext ?_ ?_
    Â· simpa using huv G (by simp) (g â‰« Limits.prod.fst)
    Â· simpa using huv H (by simp) (g â‰« Limits.prod.snd)
  Â· simp only [Set.mem_insert_iff, Set.mem_singleton_iff] at hZ
    rcases hZ with (rfl | rfl)
    Â· simpa using huv (prod.lift g 0) =â‰« Limits.prod.fst
    Â· simpa using huv (prod.lift 0 g) =â‰« Limits.prod.snd

theorem isCoseparator_prod_of_isCoseparator_left (G H : C) [HasBinaryProduct G H]
    (hG : IsCoseparator G) : IsCoseparator (G â¨¯ H) :=
  (isCoseparator_prod _ _).2 <| IsCoseparating.mono hG <| by simp

theorem isCoseparator_prod_of_isCoseparator_right (G H : C) [HasBinaryProduct G H]
    (hH : IsCoseparator H) : IsCoseparator (G â¨¯ H) :=
  (isCoseparator_prod _ _).2 <| IsCoseparating.mono hH <| by simp

theorem isCoseparator_pi {Î² : Type w} (f : Î² â†’ C) [HasProduct f] :
    IsCoseparator (âˆá¶œ f) â†” IsCoseparating (Set.range f) := by
  refine
    âŸ¨fun h X Y u v huv => ?_, fun h =>
      (isCoseparator_def _).2 fun X Y u v huv => h _ _ fun Z hZ g => ?_âŸ©
  Â· refine h.def _ _ fun g => limit.hom_ext fun b => ?_
    simpa using huv (f b.as) (by simp) (g â‰« limit.Ï€ (Discrete.functor f) _)
  Â· obtain âŸ¨b, rflâŸ© := Set.mem_range.1 hZ
    classical simpa using huv (Pi.lift (Pi.single b g)) =â‰« Pi.Ï€ f b

theorem isCoseparator_pi_of_isCoseparator {Î² : Type w} (f : Î² â†’ C) [HasProduct f] (b : Î²)
    (hb : IsCoseparator (f b)) : IsCoseparator (âˆá¶œ f) :=
  (isCoseparator_pi _).2 <| IsCoseparating.mono hb <| by simp

end ZeroMorphisms

theorem isDetector_iff_reflectsIsomorphisms_coyoneda_obj (G : C) :
    IsDetector G â†” (coyoneda.obj (op G)).ReflectsIsomorphisms := by
  refine
    âŸ¨fun hG => âŸ¨fun f hf => hG.def _ fun h => ?_âŸ©, fun h =>
      (isDetector_def _).2 fun X Y f hf => ?_âŸ©
  Â· rw [isIso_iff_bijective, Function.bijective_iff_existsUnique] at hf
    exact hf h
  Â· suffices IsIso ((coyoneda.obj (op G)).map f) by
      exact @isIso_of_reflects_iso _ _ _ _ _ _ _ (coyoneda.obj (op G)) _ h
    rwa [isIso_iff_bijective, Function.bijective_iff_existsUnique]

theorem isCodetector_iff_reflectsIsomorphisms_yoneda_obj (G : C) :
    IsCodetector G â†” (yoneda.obj G).ReflectsIsomorphisms := by
  refine âŸ¨fun hG => âŸ¨fun f hf => ?_âŸ©, fun h => (isCodetector_def _).2 fun X Y f hf => ?_âŸ©
  Â· refine (isIso_unop_iff _).1 (hG.def _ ?_)
    rwa [isIso_iff_bijective, Function.bijective_iff_existsUnique] at hf
  Â· rw [â† isIso_op_iff]
    suffices IsIso ((yoneda.obj G).map f.op) by
      exact @isIso_of_reflects_iso _ _ _ _ _ _ _ (yoneda.obj G) _ h
    rwa [isIso_iff_bijective, Function.bijective_iff_existsUnique]

theorem wellPowered_of_isDetector [HasPullbacks C] (G : C) (hG : IsDetector G) :
    WellPowered.{vâ‚} C :=
  wellPowered_of_isDetecting hG

theorem wellPowered_of_isSeparator [HasPullbacks C] [Balanced C] (G : C) (hG : IsSeparator G) :
    WellPowered.{vâ‚} C := wellPowered_of_isDetecting hG.isDetector

section HasGenerator

section Definitions

variable (C)

/--
For a category `C` and an object `G : C`, `G` is a separator of `C` if
the functor `C(G, -)` is faithful.

While `IsSeparator G : Prop` is the proposition that `G` is a separator of `C`,
an `HasSeparator C : Prop` is the proposition that such a separator exists.
Note that `HasSeparator C` is a proposition. It does not designate a favored separator
and merely asserts the existence of one.
-/
class HasSeparator : Prop where
  hasSeparator : âˆƒ G : C, IsSeparator G

/--
For a category `C` and an object `G : C`, `G` is a coseparator of `C` if
the functor `C(-, G)` is faithful.

While `IsCoseparator G : Prop` is the proposition that `G` is a coseparator of `C`,
an `HasCoseparator C : Prop` is the proposition that such a coseparator exists.
Note that `HasCoseparator C` is a proposition. It does not designate a favored coseparator
and merely asserts the existence of one.
-/
class HasCoseparator : Prop where
  hasCoseparator : âˆƒ G : C, IsCoseparator G

/--
For a category `C` and an object `G : C`, `G` is a detector of `C` if
the functor `C(G, -)` reflects isomorphisms.

While `IsDetector G : Prop` is the proposition that `G` is a detector of `C`,
an `HasDetector C : Prop` is the proposition that such a detector exists.
Note that `HasDetector C` is a proposition. It does not designate a favored detector
and merely asserts the existence of one.
-/
class HasDetector : Prop where
  hasDetector : âˆƒ G : C, IsDetector G

/--
For a category `C` and an object `G : C`, `G` is a codetector of `C` if
the functor `C(-, G)` reflects isomorphisms.

While `IsCodetector G : Prop` is the proposition that `G` is a codetector of `C`,
an `HasCodetector C : Prop` is the proposition that such a codetector exists.
Note that `HasCodetector C` is a proposition. It does not designate a favored codetector
and merely asserts the existence of one.
-/
class HasCodetector : Prop where
  hasCodetector : âˆƒ G : C, IsCodetector G

end Definitions

section Choice

variable (C)

/--
Given a category `C` that has a separator (`HasSeparator C`), `separator C` is an arbitrarily
chosen separator of `C`.
-/
noncomputable def separator [HasSeparator C] : C := HasSeparator.hasSeparator.choose

/--
Given a category `C` that has a coseparator (`HasCoseparator C`), `coseparator C` is an arbitrarily
chosen coseparator of `C`.
-/
noncomputable def coseparator [HasCoseparator C] : C := HasCoseparator.hasCoseparator.choose

/--
Given a category `C` that has a detector (`HasDetector C`), `detector C` is an arbitrarily
chosen detector of `C`.
-/
noncomputable def detector [HasDetector C] : C := HasDetector.hasDetector.choose

/--
Given a category `C` that has a codetector (`HasCodetector C`), `codetector C` is an arbitrarily
chosen codetector of `C`.
-/
noncomputable def codetector [HasCodetector C] : C := HasCodetector.hasCodetector.choose

theorem isSeparator_separator [HasSeparator C] : IsSeparator (separator C) :=
  HasSeparator.hasSeparator.choose_spec

theorem isDetector_separator [Balanced C] [HasSeparator C] : IsDetector (separator C) :=
  isSeparator_separator C |>.isDetector

theorem isCoseparator_coseparator [HasCoseparator C] : IsCoseparator (coseparator C) :=
  HasCoseparator.hasCoseparator.choose_spec

theorem isCodetector_coseparator [Balanced C] [HasCoseparator C] : IsCodetector (coseparator C) :=
  isCoseparator_coseparator C |>.isCodetector

theorem isDetector_detector [HasDetector C] : IsDetector (detector C) :=
  HasDetector.hasDetector.choose_spec

theorem isSeparator_detector [HasEqualizers C] [HasDetector C] : IsSeparator (detector C) :=
  isDetector_detector C |>.isSeparator

theorem isCodetector_codetector [HasCodetector C] : IsCodetector (codetector C) :=
  HasCodetector.hasCodetector.choose_spec

theorem isCoseparator_codetector [HasCoequalizers C] [HasCodetector C] :
    IsCoseparator (codetector C) := isCodetector_codetector C |>.isCoseparator

end Choice

section Instances

theorem HasSeparator.hasDetector [Balanced C] [HasSeparator C] : HasDetector C :=
  âŸ¨_, isDetector_separator CâŸ©

theorem HasDetector.hasSeparator [HasEqualizers C] [HasDetector C] : HasSeparator C :=
  âŸ¨_, isSeparator_detector CâŸ©

theorem HasCoseparator.hasCodetector [Balanced C] [HasCoseparator C] : HasCodetector C :=
  âŸ¨_, isCodetector_coseparator CâŸ©

theorem HasCodetector.hasCoseparator [HasCoequalizers C] [HasCodetector C] : HasCoseparator C :=
  âŸ¨_, isCoseparator_codetector CâŸ©

instance HasDetector.wellPowered [HasPullbacks C] [HasDetector C] : WellPowered.{vâ‚} C :=
  isDetector_detector C |> wellPowered_of_isDetector _

instance HasSeparator.wellPowered [HasPullbacks C] [Balanced C] [HasSeparator C] :
    WellPowered.{vâ‚} C := HasSeparator.hasDetector.wellPowered

end Instances

section Equivalence

theorem HasSeparator.of_equivalence [HasSeparator C] (Î± : C â‰Œ D) : HasSeparator D :=
  âŸ¨Î±.functor.obj (separator C), isSeparator_separator C |>.of_equivalence Î±âŸ©

theorem HasCoseparator.of_equivalence [HasCoseparator C] (Î± : C â‰Œ D) : HasCoseparator D :=
  âŸ¨Î±.functor.obj (coseparator C), isCoseparator_coseparator C |>.of_equivalence Î±âŸ©

end Equivalence

section Dual

@[simp]
theorem hasSeparator_op_iff : HasSeparator Cáµ’áµ– â†” HasCoseparator C :=
  âŸ¨fun âŸ¨G, hGâŸ© => âŸ¨unop G, (isCoseparator_unop_iff G).mpr hGâŸ©,
   fun âŸ¨G, hGâŸ© => âŸ¨op G, (isSeparator_op_iff G).mpr hGâŸ©âŸ©

@[simp]
theorem hasCoseparator_op_iff : HasCoseparator Cáµ’áµ– â†” HasSeparator C :=
  âŸ¨fun âŸ¨G, hGâŸ© => âŸ¨unop G, (isSeparator_unop_iff G).mpr hGâŸ©,
   fun âŸ¨G, hGâŸ© => âŸ¨op G, (isCoseparator_op_iff G).mpr hGâŸ©âŸ©

@[simp]
theorem hasDetector_op_iff : HasDetector Cáµ’áµ– â†” HasCodetector C :=
  âŸ¨fun âŸ¨G, hGâŸ© => âŸ¨unop G, (isCodetector_unop_iff G).mpr hGâŸ©,
   fun âŸ¨G, hGâŸ© => âŸ¨op G, (isDetector_op_iff G).mpr hGâŸ©âŸ©

@[simp]
theorem hasCodetector_op_iff : HasCodetector Cáµ’áµ– â†” HasDetector C :=
  âŸ¨fun âŸ¨G, hGâŸ© => âŸ¨unop G, (isDetector_unop_iff G).mpr hGâŸ©,
   fun âŸ¨G, hGâŸ© => âŸ¨op G, (isCodetector_op_iff G).mpr hGâŸ©âŸ©

instance HasSeparator.hasCoseparator_op [HasSeparator C] : HasCoseparator Cáµ’áµ– := by simp [*]
theorem HasSeparator.hasCoseparator_of_hasSeparator_op [h : HasSeparator Cáµ’áµ–] :
    HasCoseparator C := by simp_all

instance HasCoseparator.hasSeparator_op [HasCoseparator C] : HasSeparator Cáµ’áµ– := by simp [*]
theorem HasCoseparator.hasSeparator_of_hasCoseparator_op [HasCoseparator Cáµ’áµ–] :
    HasSeparator C := by simp_all

instance HasDetector.hasCodetector_op [HasDetector C] : HasCodetector Cáµ’áµ– := by simp [*]
theorem HasDetector.hasCodetector_of_hasDetector_op [HasDetector Cáµ’áµ–] :
    HasCodetector C := by simp_all

instance HasCodetector.hasDetector_op [HasCodetector C] : HasDetector Cáµ’áµ– := by simp [*]
theorem HasCodetector.hasDetector_of_hasCodetector_op [HasCodetector Cáµ’áµ–] :
    HasDetector C := by simp_all

end Dual

end HasGenerator

end CategoryTheory
