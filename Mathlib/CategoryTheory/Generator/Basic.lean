/-
Copyright (c) 2022 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Limits.EssentiallySmall
import Mathlib.CategoryTheory.Limits.Shapes.Opposites.Equalizers
import Mathlib.CategoryTheory.Subobject.Lattice
import Mathlib.CategoryTheory.ObjectProperty.Small

/-!
# Separating and detecting sets

There are several non-equivalent notions of a generator of a category. Here, we consider two of
them:

* We say that `P : ObjectProperty C` is a separating set if the functors `C(G, -)`
    for `G` such that `P G` are collectively faithful, i.e., if
    `h ‚â´ f = h ‚â´ g` for all `h` with domain satisfying `P` implies `f = g`.
* We say that `P : ObjectProperty C` is a detecting set if the functors `C(G, -)`
    collectively reflect isomorphisms, i.e., if any `h` with domain satisfying `P`
    uniquely factors through `f`, then `f` is an isomorphism.

There are, of course, also the dual notions of coseparating and codetecting sets.

## Main results

We
* define predicates `IsSeparating`, `IsCoseparating`, `IsDetecting` and `IsCodetecting` on
  `ObjectProperty C`;
* show that equivalences of categories preserves these notions;
* show that separating and coseparating are dual notions;
* show that detecting and codetecting are dual notions;
* show that if `C` has equalizers, then detecting implies separating;
* show that if `C` has coequalizers, then codetecting implies coseparating;
* show that if `C` is balanced, then separating implies detecting and coseparating implies
  codetecting;
* show that `‚àÖ` is separating if and only if `‚àÖ` is coseparating if and only if `C` is thin;
* show that `‚àÖ` is detecting if and only if `‚àÖ` is codetecting if and only if `C` is a groupoid;
* define predicates `IsSeparator`, `IsCoseparator`, `IsDetector` and `IsCodetector` as the
  singleton counterparts to the definitions for sets above and restate the above results in this
  situation;
* show that `G` is a separator if and only if `coyoneda.obj (op G)` is faithful (and the dual);
* show that `G` is a detector if and only if `coyoneda.obj (op G)` reflects isomorphisms (and the
  dual);
* show that `C` is `WellPowered` if it admits small pullbacks and a detector;
* define corresponding typeclasses `HasSeparator`, `HasCoseparator`, `HasDetector`
  and `HasCodetector` on categories and prove analogous results for these.

## Examples

See the files `CategoryTheory.Generator.Presheaf` and `CategoryTheory.Generator.Sheaf`.

-/


universe w v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

open CategoryTheory.Limits Opposite

namespace CategoryTheory

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]

namespace ObjectProperty

variable (P : ObjectProperty C)

/-- We say that `P : ObjectProperty C` is separating if the functors `C(G, -)`
for `G : C` such that `P G` are collectively faithful,
i.e., if `h ‚â´ f = h ‚â´ g` for all `h` with domain in `ùí¢` implies `f = g`. -/
def IsSeparating : Prop :=
  ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f g : X ‚ü∂ Y), (‚àÄ (G : C) (_ : P G) (h : G ‚ü∂ X), h ‚â´ f = h ‚â´ g) ‚Üí f = g

/-- We say that `P : ObjectProperty C` is coseparating if the functors `C(-, G)`
for `G : C` such that `P G` are collectively faithful,
i.e., if `f ‚â´ h = g ‚â´ h` for all `h` with codomain in `ùí¢` implies `f = g`. -/
def IsCoseparating : Prop :=
  ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f g : X ‚ü∂ Y), (‚àÄ (G : C) (_ : P G) (h : Y ‚ü∂ G), f ‚â´ h = g ‚â´ h) ‚Üí f = g

/-- We say that `P : ObjectProperty C` is detecting if the functors `C(G, -)`
for `G : C` such that `P G` collectively reflect isomorphisms,
i.e., if any `h` with domain `G` that `P G` uniquely factors through `f`,
then `f` is an isomorphism. -/
def IsDetecting : Prop :=
  ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), (‚àÄ (G : C) (_ : P G),
    ‚àÄ (h : G ‚ü∂ Y), ‚àÉ! h' : G ‚ü∂ X, h' ‚â´ f = h) ‚Üí IsIso f

/-- We say that `P : ObjectProperty C` is codetecting if the functors `C(-, G)`
for `G : C` such that `P G` collectively reflect isomorphisms,
i.e., if any `h` with codomain `G` such that `P G` uniquely factors through `f`,
then `f` is an isomorphism. -/
def IsCodetecting : Prop :=
  ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), (‚àÄ (G : C) (_ : P G),
    ‚àÄ (h : X ‚ü∂ G), ‚àÉ! h' : Y ‚ü∂ G, f ‚â´ h' = h) ‚Üí IsIso f

section Equivalence

variable {P}

lemma IsSeparating.of_equivalence
    (h : IsSeparating P) {D : Type*} [Category D] (Œ± : C ‚âå D) :
    IsSeparating (P.strictMap Œ±.functor) := fun X Y f g H =>
  Œ±.inverse.map_injective (h _ _ (fun Z hZ h ‚Ü¶ by
    obtain ‚ü®h', rfl‚ü© := (Œ±.toAdjunction.homEquiv _ _).surjective h
    simp only [Adjunction.homEquiv_unit, Category.assoc, ‚Üê Functor.map_comp,
      H _ (P.strictMap_obj _ hZ) h']))

lemma IsCoseparating.of_equivalence
    (h : IsCoseparating P) {D : Type*} [Category D] (Œ± : C ‚âå D) :
    IsCoseparating (P.strictMap Œ±.functor) := fun X Y f g H =>
  Œ±.inverse.map_injective (h _ _ (fun Z hZ h ‚Ü¶ by
    obtain ‚ü®h', rfl‚ü© := (Œ±.symm.toAdjunction.homEquiv _ _).symm.surjective h
    simp only [Equivalence.symm_inverse, Equivalence.symm_functor,
      Adjunction.homEquiv_counit, ‚Üê Functor.map_comp_assoc,
      H _ (P.strictMap_obj _ hZ) h']))

end Equivalence

section Dual

theorem isSeparating_op_iff : IsSeparating P.op ‚Üî IsCoseparating P := by
  refine ‚ü®fun hP X Y f g hfg => ?_, fun hP X Y f g hfg => ?_‚ü©
  ¬∑ refine Quiver.Hom.op_inj (hP _ _ fun G hG h => Quiver.Hom.unop_inj ?_)
    simpa only [unop_comp, Quiver.Hom.unop_op] using hfg _ hG _
  ¬∑ refine Quiver.Hom.unop_inj (hP _ _ fun G hG h => Quiver.Hom.op_inj ?_)
    simpa only [op_comp, Quiver.Hom.op_unop] using hfg _ hG _

theorem isCoseparating_op_iff : IsCoseparating P.op ‚Üî IsSeparating P := by
  refine ‚ü®fun hP X Y f g hfg => ?_, fun hP X Y f g hfg => ?_‚ü©
  ¬∑ refine Quiver.Hom.op_inj (hP _ _ fun G hG h => Quiver.Hom.unop_inj ?_)
    simpa only [unop_comp, Quiver.Hom.unop_op] using hfg _ hG _
  ¬∑ refine Quiver.Hom.unop_inj (hP _ _ fun G hG h => Quiver.Hom.op_inj ?_)
    simpa only [op_comp, Quiver.Hom.op_unop] using hfg _ hG _

theorem isCoseparating_unop_iff (P : ObjectProperty C·µí·µñ) :
    IsCoseparating P.unop ‚Üî IsSeparating P :=
  P.unop.isSeparating_op_iff.symm

theorem isSeparating_unop_iff (P : ObjectProperty C·µí·µñ) :
    IsSeparating P.unop ‚Üî IsCoseparating P :=
  P.unop.isCoseparating_op_iff.symm

theorem isDetecting_op_iff : IsDetecting P.op ‚Üî IsCodetecting P := by
  refine ‚ü®fun hP X Y f hf => ?_, fun hP X Y f hf => ?_‚ü©
  ¬∑ refine (isIso_op_iff _).1 (hP _ fun G hG h => ?_)
    obtain ‚ü®t, ht, ht'‚ü© := hf (unop G) hG h.unop
    exact
      ‚ü®t.op, Quiver.Hom.unop_inj ht, fun y hy => Quiver.Hom.unop_inj (ht' _ (Quiver.Hom.op_inj hy))‚ü©
  ¬∑ refine (isIso_unop_iff _).1 (hP _ fun G hG h => ?_)
    obtain ‚ü®t, ht, ht'‚ü© := hf (op G) hG h.op
    refine ‚ü®t.unop, Quiver.Hom.op_inj ht, fun y hy => Quiver.Hom.op_inj (ht' _ ?_)‚ü©
    exact Quiver.Hom.unop_inj (by simpa only using hy)

theorem isCodetecting_op_iff : IsCodetecting P.op ‚Üî IsDetecting P := by
  refine ‚ü®fun hP X Y f hf => ?_, fun hP X Y f hf => ?_‚ü©
  ¬∑ refine (isIso_op_iff _).1 (hP _ fun G hG h => ?_)
    obtain ‚ü®t, ht, ht'‚ü© := hf (unop G) hG h.unop
    exact
      ‚ü®t.op, Quiver.Hom.unop_inj ht, fun y hy => Quiver.Hom.unop_inj (ht' _ (Quiver.Hom.op_inj hy))‚ü©
  ¬∑ refine (isIso_unop_iff _).1 (hP _ fun G hG h => ?_)
    obtain ‚ü®t, ht, ht'‚ü© := hf (op G) hG h.op
    refine ‚ü®t.unop, Quiver.Hom.op_inj ht, fun y hy => Quiver.Hom.op_inj (ht' _ ?_)‚ü©
    exact Quiver.Hom.unop_inj (by simpa only using hy)

theorem isDetecting_unop_iff (P : ObjectProperty C·µí·µñ) : IsDetecting P.unop ‚Üî IsCodetecting P :=
  P.unop.isCodetecting_op_iff.symm

theorem isCodetecting_unop_iff (P : ObjectProperty C·µí·µñ) : IsCodetecting P.unop ‚Üî IsDetecting P :=
  P.unop.isDetecting_op_iff.symm

end Dual

variable {P}

theorem IsDetecting.isSeparating [HasEqualizers C] (hP : IsDetecting P) :
    IsSeparating P := fun _ _ f g hfg =>
  have : IsIso (equalizer.Œπ f g) := hP _ fun _ hG _ => equalizer.existsUnique _ (hfg _ hG _)
  eq_of_epi_equalizer

theorem IsCodetecting.isCoseparating [HasCoequalizers C] :
    IsCodetecting P ‚Üí IsCoseparating P := by
  simpa only [‚Üê isSeparating_op_iff, ‚Üê isDetecting_op_iff] using IsDetecting.isSeparating

theorem IsSeparating.isDetecting [Balanced C] (hP : IsSeparating P) :
    IsDetecting P := by
  intro X Y f hf
  refine
    (isIso_iff_mono_and_epi _).2 ‚ü®‚ü®fun g h hgh => hP _ _ fun G hG i => ?_‚ü©, ‚ü®fun g h hgh => ?_‚ü©‚ü©
  ¬∑ obtain ‚ü®t, -, ht‚ü© := hf G hG (i ‚â´ g ‚â´ f)
    rw [ht (i ‚â´ g) (Category.assoc _ _ _), ht (i ‚â´ h) (hgh.symm ‚ñ∏ Category.assoc _ _ _)]
  ¬∑ refine hP _ _ fun G hG i => ?_
    obtain ‚ü®t, rfl, -‚ü© := hf G hG i
    rw [Category.assoc, hgh, Category.assoc]

lemma IsDetecting.isIso_iff_of_mono (hP : IsDetecting P)
    {X Y : C} (f : X ‚ü∂ Y) [Mono f] :
    IsIso f ‚Üî ‚àÄ (G : C) (_ : P G), Function.Surjective ((coyoneda.obj (op G)).map f) := by
  constructor
  ¬∑ intro h
    rw [isIso_iff_yoneda_map_bijective] at h
    intro A _
    exact (h A).2
  ¬∑ intro hf
    refine hP _ (fun A hA g ‚Ü¶ existsUnique_of_exists_of_unique ?_ ?_)
    ¬∑ exact hf A hA g
    ¬∑ intro l‚ÇÅ l‚ÇÇ h‚ÇÅ h‚ÇÇ
      rw [‚Üê cancel_mono f, h‚ÇÅ, h‚ÇÇ]

lemma IsCodetecting.isIso_iff_of_epi (hP : IsCodetecting P)
    {X Y : C} (f : X ‚ü∂ Y) [Epi f] :
    IsIso f ‚Üî ‚àÄ (G : C) (_ : P G), Function.Surjective ((yoneda.obj G).map f.op) := by
  constructor
  ¬∑ intro h
    rw [isIso_iff_coyoneda_map_bijective] at h
    intro A _
    exact (h A).2
  ¬∑ intro hf
    refine hP _ (fun A hA g ‚Ü¶ existsUnique_of_exists_of_unique ?_ ?_)
    ¬∑ exact hf A hA g
    ¬∑ intro l‚ÇÅ l‚ÇÇ h‚ÇÅ h‚ÇÇ
      rw [‚Üê cancel_epi f, h‚ÇÅ, h‚ÇÇ]

section

attribute [local instance] balanced_opposite

theorem IsCoseparating.isCodetecting [Balanced C] :
    IsCoseparating P ‚Üí IsCodetecting P := by
  simpa only [‚Üê isDetecting_op_iff, ‚Üê isSeparating_op_iff] using IsSeparating.isDetecting

end

theorem isDetecting_iff_isSeparating [HasEqualizers C] [Balanced C] :
    IsDetecting P ‚Üî IsSeparating P :=
  ‚ü®IsDetecting.isSeparating, IsSeparating.isDetecting‚ü©

theorem isCodetecting_iff_isCoseparating [HasCoequalizers C] [Balanced C] :
    IsCodetecting P ‚Üî IsCoseparating P :=
  ‚ü®IsCodetecting.isCoseparating, IsCoseparating.isCodetecting‚ü©

section Mono

theorem IsSeparating.of_le (hP : IsSeparating P) {Q : ObjectProperty C} (h : P ‚â§ Q) :
    IsSeparating Q := fun _ _ _ _ hfg => hP _ _ fun _ hG _ => hfg _ (h _ hG) _

theorem IsCoseparating.of_le (hP : IsCoseparating P) {Q : ObjectProperty C} (h : P ‚â§ Q) :
    IsCoseparating Q := fun _ _ _ _ hfg => hP _ _ fun _ hG _ => hfg _ (h _ hG) _

theorem IsDetecting.of_le (hP : IsDetecting P) {Q : ObjectProperty C} (h : P ‚â§ Q) :
    IsDetecting Q := fun _ _ _ hf => hP _ fun _ hG _ => hf _ (h _ hG) _

theorem IsCodetecting.of_le (hùí¢ : IsCodetecting P) {Q : ObjectProperty C} (h : P ‚â§ Q) :
    IsCodetecting Q := fun _ _ _ hf => hùí¢ _ fun _ hG _ => hf _ (h _ hG) _

end Mono

section Empty

lemma isThin_of_isSeparating_bot (h : IsSeparating (‚ä• : ObjectProperty C)) :
    Quiver.IsThin C := fun _ _ ‚Ü¶ ‚ü®fun _ _ ‚Ü¶ h _ _ (by simp)‚ü©

lemma isSeparating_bot_of_isThin [Quiver.IsThin C] : IsSeparating (‚ä• : ObjectProperty C) :=
  fun _ _ _ _ _ ‚Ü¶ Subsingleton.elim _ _

lemma isThin_of_isCoseparating_bot (h : IsCoseparating (‚ä• : ObjectProperty C)) :
    Quiver.IsThin C := fun _ _ ‚Ü¶ ‚ü®fun _ _ ‚Ü¶ h _ _ (by simp)‚ü©

lemma isCoseparating_bot_of_isThin [Quiver.IsThin C] : IsCoseparating (‚ä• : ObjectProperty C) :=
  fun _ _ _ _ _ ‚Ü¶ Subsingleton.elim _ _

lemma isGroupoid_of_isDetecting_bot (h : IsDetecting (‚ä• : ObjectProperty C)) :
    IsGroupoid C where
  all_isIso f := h _ (by simp)

lemma isDetecting_bot_of_isGroupoid [IsGroupoid C] :
    IsDetecting (‚ä• : ObjectProperty C) :=
  fun _ _ _ _ ‚Ü¶ inferInstance

lemma isGroupoid_of_isCodetecting_bot (h : IsCodetecting (‚ä• : ObjectProperty C)) :
    IsGroupoid C where
  all_isIso f := h _ (by simp)

lemma isCodetecting_bot_of_isGroupoid [IsGroupoid C] :
    IsCodetecting (‚ä• : ObjectProperty C) :=
  fun _ _ _ _ ‚Ü¶ inferInstance

end Empty

variable (P)

theorem isSeparating_iff_epi
    [‚àÄ A : C, HasCoproduct fun f : Œ£ G : Subtype P, G.1 ‚ü∂ A ‚Ü¶ f.1.1] :
    IsSeparating P ‚Üî
      ‚àÄ A : C, Epi (Sigma.desc (Sigma.snd (Œ≤ := fun (G : Subtype P) ‚Ü¶ G.1 ‚ü∂ A))) := by
  let Œ≤ (A : C) (G : Subtype P) := G.1 ‚ü∂ A
  let b (A : C) (x : Œ£ G, Œ≤ A G) := x.1.1
  refine ‚ü®fun h A ‚Ü¶ ‚ü®fun u v huv ‚Ü¶ h _ _ fun G hG f ‚Ü¶ ?_‚ü©, fun h X Y f g hh ‚Ü¶ ?_‚ü©
  ¬∑ simpa [Œ≤, b] using Sigma.Œπ (b A) ‚ü®‚ü®G, hG‚ü©, f‚ü© ‚â´= huv
  ¬∑ rw [‚Üê cancel_epi (Sigma.desc (Sigma.snd (Œ≤ := Œ≤ X)))]
    ext ‚ü®‚ü®_, hG‚ü©, _‚ü©
    simpa using hh _ hG _

theorem isCoseparating_iff_mono
    [‚àÄ A : C, HasProduct fun f : Œ£ G : Subtype P, A ‚ü∂ G.1 ‚Ü¶ f.1.1] :
    IsCoseparating P ‚Üî
      ‚àÄ A : C, Mono (Pi.lift (Sigma.snd (Œ≤ := fun (G : Subtype P) ‚Ü¶ A ‚ü∂ G.1))) := by
  let Œ≤ (A : C) (G : Subtype P) := A ‚ü∂ G.1
  let b (A : C) (x : Œ£ G, Œ≤ A G) := x.1.1
  refine ‚ü®fun h A ‚Ü¶ ‚ü®fun u v huv ‚Ü¶ h _ _ fun G hG f ‚Ü¶ ?_‚ü©, fun h X Y f g hh ‚Ü¶ ?_‚ü©
  ¬∑ simpa [Œ≤, b] using huv =‚â´ Pi.œÄ (b A) ‚ü®‚ü®G, hG‚ü©, f‚ü©
  ¬∑ rw [‚Üê cancel_mono (Pi.lift (Sigma.snd (Œ≤ := Œ≤ Y)))]
    ext ‚ü®‚ü®_, hG‚ü©, _‚ü©
    simpa using hh _ hG _

end ObjectProperty

/-- An ingredient of the proof of the Special Adjoint Functor Theorem: a complete well-powered
    category with a small coseparating set has an initial object.

    In fact, it follows from the Special Adjoint Functor Theorem that `C` is already cocomplete,
    see `hasColimits_of_hasLimits_of_isCoseparating`. -/
theorem hasInitial_of_isCoseparating [LocallySmall.{w} C] [WellPowered.{w} C]
    [HasLimitsOfSize.{w, w} C] {P : ObjectProperty C} [ObjectProperty.Small.{w} P]
    (hP : P.IsCoseparating) : HasInitial C := by
  have := hasFiniteLimits_of_hasLimitsOfSize C
  haveI : HasProductsOfShape (Subtype P) C := hasProductsOfShape_of_small C (Subtype P)
  haveI := fun A => hasProductsOfShape_of_small.{w} C (Œ£ G : Subtype P, A ‚ü∂ (G : C))
  letI := completeLatticeOfCompleteSemilatticeInf (Subobject (piObj (Subtype.val : Subtype P ‚Üí C)))
  suffices ‚àÄ A : C, Unique (((‚ä• : Subobject (piObj (Subtype.val : Subtype P ‚Üí C))) : C) ‚ü∂ A) by
    exact hasInitial_of_unique ((‚ä• : Subobject (piObj (Subtype.val : Subtype P ‚Üí C))) : C)
  refine fun A => ‚ü®‚ü®?_‚ü©, fun f => ?_‚ü©
  ¬∑ let s := Pi.lift fun f : Œ£ G : Subtype P, A ‚ü∂ (G : C) => Pi.œÄ (Subtype.val : Subtype P ‚Üí C) f.1
    let t := Pi.lift (@Sigma.snd (Subtype P) fun G => A ‚ü∂ (G : C))
    haveI : Mono t := P.isCoseparating_iff_mono.1 hP A
    exact Subobject.ofLEMk _ (pullback.fst _ _ : pullback s t ‚ü∂ _) bot_le ‚â´ pullback.snd _ _
  ¬∑ suffices ‚àÄ (g : Subobject.underlying.obj ‚ä• ‚ü∂ A), f = g by
      apply this
    intro g
    suffices IsSplitEpi (equalizer.Œπ f g) by exact eq_of_epi_equalizer
    exact IsSplitEpi.mk' ‚ü®Subobject.ofLEMk _ (equalizer.Œπ f g ‚â´ Subobject.arrow _) bot_le, by
      ext
      simp‚ü©

/-- An ingredient of the proof of the Special Adjoint Functor Theorem: a cocomplete well-copowered
    category with a small separating set has a terminal object.

    In fact, it follows from the Special Adjoint Functor Theorem that `C` is already complete, see
    `hasLimits_of_hasColimits_of_isSeparating`. -/
theorem hasTerminal_of_isSeparating [LocallySmall.{w} C·µí·µñ] [WellPowered.{w} C·µí·µñ]
    [HasColimitsOfSize.{w, w} C] {P : ObjectProperty C} [ObjectProperty.Small.{w} P]
    (hP : P.IsSeparating) : HasTerminal C := by
  haveI : HasInitial C·µí·µñ := hasInitial_of_isCoseparating (P.isCoseparating_op_iff.2 hP)
  exact hasTerminal_of_hasInitial_op

section WellPowered

namespace Subobject

theorem eq_of_le_of_isDetecting {ùí¢ : ObjectProperty C} (hùí¢ : ùí¢.IsDetecting) {X : C}
    (P Q : Subobject X) (h‚ÇÅ : P ‚â§ Q)
    (h‚ÇÇ : ‚àÄ (G : C) (_ : ùí¢ G), ‚àÄ {f : G ‚ü∂ X}, Q.Factors f ‚Üí P.Factors f) : P = Q := by
  suffices IsIso (ofLE _ _ h‚ÇÅ) by exact le_antisymm h‚ÇÅ (le_of_comm (inv (ofLE _ _ h‚ÇÅ)) (by simp))
  refine hùí¢ _ fun G hG f => ?_
  have : P.Factors (f ‚â´ Q.arrow) := h‚ÇÇ _ hG ((factors_iff _ _).2 ‚ü®_, rfl‚ü©)
  refine ‚ü®factorThru _ _ this, ?_, fun g (hg : g ‚â´ _ = f) => ?_‚ü©
  ¬∑ simp only [‚Üê cancel_mono Q.arrow, Category.assoc, ofLE_arrow, factorThru_arrow]
  ¬∑ simp only [‚Üê cancel_mono (Subobject.ofLE _ _ h‚ÇÅ), ‚Üê cancel_mono Q.arrow, hg, Category.assoc,
      ofLE_arrow, factorThru_arrow]

theorem inf_eq_of_isDetecting [HasPullbacks C] {ùí¢ : ObjectProperty C} (hùí¢ : ùí¢.IsDetecting) {X : C}
    (P Q : Subobject X) (h : ‚àÄ (G : C) (_ : ùí¢ G), ‚àÄ {f : G ‚ü∂ X}, P.Factors f ‚Üí Q.Factors f) :
    P ‚äì Q = P :=
  eq_of_le_of_isDetecting hùí¢ _ _ _root_.inf_le_left
    fun _ hG _ hf => (inf_factors _).2 ‚ü®hf, h _ hG hf‚ü©

theorem eq_of_isDetecting [HasPullbacks C] {ùí¢ : ObjectProperty C} (hùí¢ : ùí¢.IsDetecting) {X : C}
    (P Q : Subobject X) (h : ‚àÄ (G : C) (_ : ùí¢ G),
      ‚àÄ {f : G ‚ü∂ X}, P.Factors f ‚Üî Q.Factors f) : P = Q :=
  calc
    P = P ‚äì Q := Eq.symm <| inf_eq_of_isDetecting hùí¢ _ _ fun G hG _ hf => (h G hG).1 hf
    _ = Q ‚äì P := inf_comm ..
    _ = Q := inf_eq_of_isDetecting hùí¢ _ _ fun G hG _ hf => (h G hG).2 hf

end Subobject

/-- A category with pullbacks and a small detecting set is well-powered. -/
theorem wellPowered_of_isDetecting [HasPullbacks C] {ùí¢ : ObjectProperty C}
    [ObjectProperty.Small.{w} ùí¢] [LocallySmall.{w} C]
    (hùí¢ : ùí¢.IsDetecting) : WellPowered.{w} C where
  subobject_small X := small_of_injective
    (f := fun P : Subobject X => { f : Œ£ G : Subtype ùí¢, G.1 ‚ü∂ X | P.Factors f.2 })
      fun P Q h => Subobject.eq_of_isDetecting hùí¢ _ _
        (by simpa [Set.ext_iff, Sigma.forall] using h)

end WellPowered

namespace StructuredArrow

variable (S : D) (T : C ‚•§ D)

theorem isCoseparating_inverseImage_proj {P : ObjectProperty C} (hP : P.IsCoseparating) :
    (P.inverseImage (proj S T)).IsCoseparating  := by
  refine fun X Y f g hfg => ext _ _ (hP _ _ fun G hG h => ?_)
  exact congr_arg CommaMorphism.right (hfg (mk (Y.hom ‚â´ T.map h)) hG (homMk h rfl))

end StructuredArrow

namespace CostructuredArrow

variable (S : C ‚•§ D) (T : D)

theorem isSeparating_inverseImage_proj {P : ObjectProperty C} (hP : P.IsSeparating) :
    (P.inverseImage (proj S T)).IsSeparating  := by
  refine fun X Y f g hfg => ext _ _ (hP _ _ fun G hG h => ?_)
  exact congr_arg CommaMorphism.left (hfg (mk (S.map h ‚â´ X.hom)) hG (homMk h rfl))

end CostructuredArrow

/-- We say that `G` is a separator if the functor `C(G, -)` is faithful. -/
def IsSeparator (G : C) : Prop :=
  ObjectProperty.IsSeparating (.singleton G)

/-- We say that `G` is a coseparator if the functor `C(-, G)` is faithful. -/
def IsCoseparator (G : C) : Prop :=
  ObjectProperty.IsCoseparating (.singleton G)

/-- We say that `G` is a detector if the functor `C(G, -)` reflects isomorphisms. -/
def IsDetector (G : C) : Prop :=
  ObjectProperty.IsDetecting (.singleton G)

/-- We say that `G` is a codetector if the functor `C(-, G)` reflects isomorphisms. -/
def IsCodetector (G : C) : Prop :=
  ObjectProperty.IsCodetecting (.singleton G)

section Equivalence

theorem IsSeparator.of_equivalence {G : C} (h : IsSeparator G) (Œ± : C ‚âå D) :
    IsSeparator (Œ±.functor.obj G) := by
  simpa using ObjectProperty.IsSeparating.of_equivalence h Œ±

theorem IsCoseparator.of_equivalence {G : C} (h : IsCoseparator G) (Œ± : C ‚âå D) :
    IsCoseparator (Œ±.functor.obj G) := by
 simpa using ObjectProperty.IsCoseparating.of_equivalence h Œ±

end Equivalence

section Dual

open ObjectProperty

theorem isSeparator_op_iff (G : C) : IsSeparator (op G) ‚Üî IsCoseparator G := by
  rw [IsSeparator, IsCoseparator, ‚Üê isSeparating_op_iff, op_singleton]

theorem isCoseparator_op_iff (G : C) : IsCoseparator (op G) ‚Üî IsSeparator G := by
  rw [IsSeparator, IsCoseparator, ‚Üê isCoseparating_op_iff, op_singleton]

theorem isCoseparator_unop_iff (G : C·µí·µñ) : IsCoseparator (unop G) ‚Üî IsSeparator G := by
  rw [IsSeparator, IsCoseparator, ‚Üê isCoseparating_unop_iff, unop_singleton]

theorem isSeparator_unop_iff (G : C·µí·µñ) : IsSeparator (unop G) ‚Üî IsCoseparator G := by
  rw [IsSeparator, IsCoseparator, ‚Üê isSeparating_unop_iff, unop_singleton]

theorem isDetector_op_iff (G : C) : IsDetector (op G) ‚Üî IsCodetector G := by
  rw [IsDetector, IsCodetector, ‚Üê isDetecting_op_iff, op_singleton]

theorem isCodetector_op_iff (G : C) : IsCodetector (op G) ‚Üî IsDetector G := by
  rw [IsDetector, IsCodetector, ‚Üê isCodetecting_op_iff, op_singleton]

theorem isCodetector_unop_iff (G : C·µí·µñ) : IsCodetector (unop G) ‚Üî IsDetector G := by
  rw [IsDetector, IsCodetector, ‚Üê isCodetecting_unop_iff, unop_singleton]

theorem isDetector_unop_iff (G : C·µí·µñ) : IsDetector (unop G) ‚Üî IsCodetector G := by
  rw [IsDetector, IsCodetector, ‚Üê isDetecting_unop_iff, unop_singleton]

end Dual

theorem IsDetector.isSeparator [HasEqualizers C] {G : C} : IsDetector G ‚Üí IsSeparator G :=
  ObjectProperty.IsDetecting.isSeparating

theorem IsCodetector.isCoseparator [HasCoequalizers C] {G : C} : IsCodetector G ‚Üí IsCoseparator G :=
  ObjectProperty.IsCodetecting.isCoseparating

theorem IsSeparator.isDetector [Balanced C] {G : C} : IsSeparator G ‚Üí IsDetector G :=
  ObjectProperty.IsSeparating.isDetecting

theorem IsCoseparator.isCodetector [Balanced C] {G : C} : IsCoseparator G ‚Üí IsCodetector G :=
  ObjectProperty.IsCoseparating.isCodetecting

theorem isSeparator_def (G : C) :
    IsSeparator G ‚Üî ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f g : X ‚ü∂ Y), (‚àÄ h : G ‚ü∂ X, h ‚â´ f = h ‚â´ g) ‚Üí f = g :=
  ‚ü®fun hG X Y f g hfg =>
    hG _ _ fun H hH h => by
      obtain rfl := (ObjectProperty.singleton_iff _ _).1 hH
      exact hfg h,
    fun hG _ _ _ _ hfg => hG _ _ fun _ => hfg _ (by simp) _‚ü©

theorem IsSeparator.def {G : C} :
    IsSeparator G ‚Üí ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f g : X ‚ü∂ Y), (‚àÄ h : G ‚ü∂ X, h ‚â´ f = h ‚â´ g) ‚Üí f = g :=
  (isSeparator_def _).1

theorem isCoseparator_def (G : C) :
    IsCoseparator G ‚Üî ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f g : X ‚ü∂ Y), (‚àÄ h : Y ‚ü∂ G, f ‚â´ h = g ‚â´ h) ‚Üí f = g :=
  ‚ü®fun hG X Y f g hfg =>
    hG _ _ fun H hH h => by
      obtain rfl := (ObjectProperty.singleton_iff _ _).1 hH
      exact hfg h,
    fun hG _ _ _ _ hfg => hG _ _ fun _ => hfg _ (by simp) _‚ü©

theorem IsCoseparator.def {G : C} :
    IsCoseparator G ‚Üí ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f g : X ‚ü∂ Y), (‚àÄ h : Y ‚ü∂ G, f ‚â´ h = g ‚â´ h) ‚Üí f = g :=
  (isCoseparator_def _).1

theorem isDetector_def (G : C) :
    IsDetector G ‚Üî ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), (‚àÄ h : G ‚ü∂ Y, ‚àÉ! h', h' ‚â´ f = h) ‚Üí IsIso f :=
  ‚ü®fun hG X Y f hf =>
    hG _ fun H hH h => by
      obtain rfl := (ObjectProperty.singleton_iff _ _).1 hH
      exact hf h,
    fun hG _ _ _ hf => hG _ fun _ => hf _ (by simp) _‚ü©

theorem IsDetector.def {G : C} :
    IsDetector G ‚Üí ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), (‚àÄ h : G ‚ü∂ Y, ‚àÉ! h', h' ‚â´ f = h) ‚Üí IsIso f :=
  (isDetector_def _).1

theorem isCodetector_def (G : C) :
    IsCodetector G ‚Üî ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), (‚àÄ h : X ‚ü∂ G, ‚àÉ! h', f ‚â´ h' = h) ‚Üí IsIso f :=
  ‚ü®fun hG X Y f hf =>
    hG _ fun H hH h => by
      obtain rfl := (ObjectProperty.singleton_iff _ _).1 hH
      exact hf h,
    fun hG _ _ _ hf => hG _ fun _ => hf _ (by simp) _‚ü©

theorem IsCodetector.def {G : C} :
    IsCodetector G ‚Üí ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), (‚àÄ h : X ‚ü∂ G, ‚àÉ! h', f ‚â´ h' = h) ‚Üí IsIso f :=
  (isCodetector_def _).1

theorem isSeparator_iff_faithful_coyoneda_obj (G : C) :
    IsSeparator G ‚Üî (coyoneda.obj (op G)).Faithful :=
  ‚ü®fun hG => ‚ü®fun hfg => hG.def _ _ (congr_fun hfg)‚ü©, fun _ =>
    (isSeparator_def _).2 fun _ _ _ _ hfg => (coyoneda.obj (op G)).map_injective (funext hfg)‚ü©

theorem isCoseparator_iff_faithful_yoneda_obj (G : C) : IsCoseparator G ‚Üî (yoneda.obj G).Faithful :=
  ‚ü®fun hG => ‚ü®fun hfg => Quiver.Hom.unop_inj (hG.def _ _ (congr_fun hfg))‚ü©, fun _ =>
    (isCoseparator_def _).2 fun _ _ _ _ hfg =>
      Quiver.Hom.op_inj <| (yoneda.obj G).map_injective (funext hfg)‚ü©

theorem isSeparator_iff_epi (G : C) [‚àÄ A : C, HasCoproduct fun _ : G ‚ü∂ A => G] :
    IsSeparator G ‚Üî ‚àÄ A : C, Epi (Sigma.desc fun f : G ‚ü∂ A => f) := by
  rw [isSeparator_def]
  refine ‚ü®fun h A => ‚ü®fun u v huv => h _ _ fun i => ?_‚ü©, fun h X Y f g hh => ?_‚ü©
  ¬∑ simpa using Sigma.Œπ _ i ‚â´= huv
  ¬∑ haveI := h X
    refine (cancel_epi (Sigma.desc fun f : G ‚ü∂ X => f)).1 (colimit.hom_ext fun j => ?_)
    simpa using hh j.as

theorem isCoseparator_iff_mono (G : C) [‚àÄ A : C, HasProduct fun _ : A ‚ü∂ G => G] :
    IsCoseparator G ‚Üî ‚àÄ A : C, Mono (Pi.lift fun f : A ‚ü∂ G => f) := by
  rw [isCoseparator_def]
  refine ‚ü®fun h A => ‚ü®fun u v huv => h _ _ fun i => ?_‚ü©, fun h X Y f g hh => ?_‚ü©
  ¬∑ simpa using huv =‚â´ Pi.œÄ _ i
  ¬∑ haveI := h Y
    refine (cancel_mono (Pi.lift fun f : Y ‚ü∂ G => f)).1 (limit.hom_ext fun j => ?_)
    simpa using hh j.as

section ZeroMorphisms

variable [HasZeroMorphisms C]

lemma isSeparator_of_isColimit_cofan {Œ≤ : Type w} {f : Œ≤ ‚Üí C}
    (hf : ObjectProperty.IsSeparating (.ofObj f)) {c : Cofan f} (hc : IsColimit c) :
    IsSeparator c.pt := by
  rw [isSeparator_def]
  refine fun _ _ _ _ huv ‚Ü¶ hf _ _ (fun _ h g ‚Ü¶ ?_)
  obtain ‚ü®b‚ü© := h
  classical simpa using c.inj b ‚â´= huv (hc.desc (Cofan.mk _ (Pi.single b g)))

lemma isSeparator_iff_of_isColimit_cofan {Œ≤ : Type w} {f : Œ≤ ‚Üí C}
    {c : Cofan f} (hc : IsColimit c) :
    IsSeparator c.pt ‚Üî ObjectProperty.IsSeparating (.ofObj f) := by
  refine ‚ü®fun h X Y u v huv => ?_, fun h => isSeparator_of_isColimit_cofan h hc‚ü©
  refine h.def _ _ fun g => hc.hom_ext fun b => ?_
  simpa using huv (f b.as) (by simp) (c.inj _ ‚â´ g)

theorem isSeparator_sigma {Œ≤ : Type w} (f : Œ≤ ‚Üí C) [HasCoproduct f] :
    IsSeparator (‚àê f) ‚Üî ObjectProperty.IsSeparating (.ofObj f) :=
  isSeparator_iff_of_isColimit_cofan (hc := colimit.isColimit _)

theorem isSeparator_coprod (G H : C) [HasBinaryCoproduct G H] :
    IsSeparator (G ‚®ø H) ‚Üî ObjectProperty.IsSeparating (.pair G H) := by
  refine (isSeparator_iff_of_isColimit_cofan (coprodIsCoprod G H)).trans ?_
  convert Iff.rfl
  ext X
  simp only [ObjectProperty.pair_iff, ObjectProperty.ofObj_iff]
  constructor
  ¬∑ rintro (rfl | rfl); exacts [‚ü®.left, rfl‚ü©, ‚ü®.right, rfl‚ü©]
  ¬∑ rintro ‚ü®‚ü®_ | _‚ü©, rfl‚ü© <;> tauto

theorem isSeparator_coprod_of_isSeparator_left (G H : C) [HasBinaryCoproduct G H]
    (hG : IsSeparator G) : IsSeparator (G ‚®ø H) :=
  (isSeparator_coprod _ _).2 <| ObjectProperty.IsSeparating.of_le hG <| by simp

theorem isSeparator_coprod_of_isSeparator_right (G H : C) [HasBinaryCoproduct G H]
    (hH : IsSeparator H) : IsSeparator (G ‚®ø H) :=
  (isSeparator_coprod _ _).2 <| ObjectProperty.IsSeparating.of_le hH <| by simp

theorem ObjectProperty.IsSeparating.isSeparator_coproduct
    {Œ≤ : Type w} {f : Œ≤ ‚Üí C} [HasCoproduct f]
    (hS : ObjectProperty.IsSeparating (.ofObj f)) : IsSeparator (‚àê f) :=
  (isSeparator_sigma _).2 hS

theorem isSeparator_sigma_of_isSeparator {Œ≤ : Type w} (f : Œ≤ ‚Üí C) [HasCoproduct f] (b : Œ≤)
    (hb : IsSeparator (f b)) : IsSeparator (‚àê f) :=
  (isSeparator_sigma _).2 <| ObjectProperty.IsSeparating.of_le hb <| by simp

lemma isCoseparator_of_isLimit_fan {Œ≤ : Type w} {f : Œ≤ ‚Üí C}
    (hf : ObjectProperty.IsCoseparating (.ofObj f)) {c : Fan f} (hc : IsLimit c) :
    IsCoseparator c.pt := by
  rw [isCoseparator_def]
  refine fun _ _ _ _ huv ‚Ü¶ hf _ _ (fun _ h g ‚Ü¶ ?_)
  obtain ‚ü®b‚ü© := h
  classical simpa using huv (hc.lift (Fan.mk _ (Pi.single b g))) =‚â´ c.proj b

lemma isCoseparator_iff_of_isLimit_fan {Œ≤ : Type w} {f : Œ≤ ‚Üí C}
    {c : Fan f} (hc : IsLimit c) :
    IsCoseparator c.pt ‚Üî ObjectProperty.IsCoseparating (.ofObj f) := by
  refine ‚ü®fun h X Y u v huv => ?_, fun h => isCoseparator_of_isLimit_fan h hc‚ü©
  refine h.def _ _ fun g => hc.hom_ext fun b => ?_
  simpa using huv (f b.as) (by simp) (g ‚â´ c.proj _)

theorem isCoseparator_pi {Œ≤ : Type w} (f : Œ≤ ‚Üí C) [HasProduct f] :
    IsCoseparator (‚àè·∂ú f) ‚Üî ObjectProperty.IsCoseparating (.ofObj f) :=
  isCoseparator_iff_of_isLimit_fan (hc := limit.isLimit _)

theorem isCoseparator_prod (G H : C) [HasBinaryProduct G H] :
    IsCoseparator (G ‚®Ø H) ‚Üî ObjectProperty.IsCoseparating (.pair G H) := by
  refine (isCoseparator_iff_of_isLimit_fan (prodIsProd G H)).trans ?_
  convert Iff.rfl
  ext X
  simp only [ObjectProperty.pair_iff, ObjectProperty.ofObj_iff]
  constructor
  ¬∑ rintro (rfl | rfl); exacts [‚ü®.left, rfl‚ü©, ‚ü®.right, rfl‚ü©]
  ¬∑ rintro ‚ü®‚ü®_ | _‚ü©, rfl‚ü© <;> tauto

theorem isCoseparator_prod_of_isCoseparator_left (G H : C) [HasBinaryProduct G H]
    (hG : IsCoseparator G) : IsCoseparator (G ‚®Ø H) :=
  (isCoseparator_prod _ _).2 <| ObjectProperty.IsCoseparating.of_le hG <| by simp

theorem isCoseparator_prod_of_isCoseparator_right (G H : C) [HasBinaryProduct G H]
    (hH : IsCoseparator H) : IsCoseparator (G ‚®Ø H) :=
  (isCoseparator_prod _ _).2 <| ObjectProperty.IsCoseparating.of_le hH <| by simp

theorem isCoseparator_pi_of_isCoseparator {Œ≤ : Type w} (f : Œ≤ ‚Üí C) [HasProduct f] (b : Œ≤)
    (hb : IsCoseparator (f b)) : IsCoseparator (‚àè·∂ú f) :=
  (isCoseparator_pi _).2 <| ObjectProperty.IsCoseparating.of_le hb <| by simp

end ZeroMorphisms

theorem isDetector_iff_reflectsIsomorphisms_coyoneda_obj (G : C) :
    IsDetector G ‚Üî (coyoneda.obj (op G)).ReflectsIsomorphisms := by
  refine
    ‚ü®fun hG => ‚ü®fun f hf => hG.def _ fun h => ?_‚ü©, fun h =>
      (isDetector_def _).2 fun X Y f hf => ?_‚ü©
  ¬∑ rw [isIso_iff_bijective, Function.bijective_iff_existsUnique] at hf
    exact hf h
  ¬∑ suffices IsIso ((coyoneda.obj (op G)).map f) by
      exact @isIso_of_reflects_iso _ _ _ _ _ _ _ (coyoneda.obj (op G)) _ h
    rwa [isIso_iff_bijective, Function.bijective_iff_existsUnique]

theorem isCodetector_iff_reflectsIsomorphisms_yoneda_obj (G : C) :
    IsCodetector G ‚Üî (yoneda.obj G).ReflectsIsomorphisms := by
  refine ‚ü®fun hG => ‚ü®fun f hf => ?_‚ü©, fun h => (isCodetector_def _).2 fun X Y f hf => ?_‚ü©
  ¬∑ refine (isIso_unop_iff _).1 (hG.def _ ?_)
    rwa [isIso_iff_bijective, Function.bijective_iff_existsUnique] at hf
  ¬∑ rw [‚Üê isIso_op_iff]
    suffices IsIso ((yoneda.obj G).map f.op) by
      exact @isIso_of_reflects_iso _ _ _ _ _ _ _ (yoneda.obj G) _ h
    rwa [isIso_iff_bijective, Function.bijective_iff_existsUnique]

theorem wellPowered_of_isDetector [HasPullbacks C] (G : C) (hG : IsDetector G) :
    WellPowered.{v‚ÇÅ} C :=
  wellPowered_of_isDetecting hG

theorem wellPowered_of_isSeparator [HasPullbacks C] [Balanced C] (G : C) (hG : IsSeparator G) :
    WellPowered.{v‚ÇÅ} C := wellPowered_of_isDetecting hG.isDetector

section HasGenerator

section Definitions

variable (C)

/--
For a category `C` and an object `G : C`, `G` is a separator of `C` if
the functor `C(G, -)` is faithful.

While `IsSeparator G : Prop` is the proposition that `G` is a separator of `C`,
an `HasSeparator C : Prop` is the proposition that such a separator exists.
Note that `HasSeparator C` is a proposition. It does not designate a favored separator
and merely asserts the existence of one.
-/
class HasSeparator : Prop where
  hasSeparator : ‚àÉ G : C, IsSeparator G

/--
For a category `C` and an object `G : C`, `G` is a coseparator of `C` if
the functor `C(-, G)` is faithful.

While `IsCoseparator G : Prop` is the proposition that `G` is a coseparator of `C`,
an `HasCoseparator C : Prop` is the proposition that such a coseparator exists.
Note that `HasCoseparator C` is a proposition. It does not designate a favored coseparator
and merely asserts the existence of one.
-/
class HasCoseparator : Prop where
  hasCoseparator : ‚àÉ G : C, IsCoseparator G

/--
For a category `C` and an object `G : C`, `G` is a detector of `C` if
the functor `C(G, -)` reflects isomorphisms.

While `IsDetector G : Prop` is the proposition that `G` is a detector of `C`,
an `HasDetector C : Prop` is the proposition that such a detector exists.
Note that `HasDetector C` is a proposition. It does not designate a favored detector
and merely asserts the existence of one.
-/
class HasDetector : Prop where
  hasDetector : ‚àÉ G : C, IsDetector G

/--
For a category `C` and an object `G : C`, `G` is a codetector of `C` if
the functor `C(-, G)` reflects isomorphisms.

While `IsCodetector G : Prop` is the proposition that `G` is a codetector of `C`,
an `HasCodetector C : Prop` is the proposition that such a codetector exists.
Note that `HasCodetector C` is a proposition. It does not designate a favored codetector
and merely asserts the existence of one.
-/
class HasCodetector : Prop where
  hasCodetector : ‚àÉ G : C, IsCodetector G

end Definitions

section Choice

variable (C)

/--
Given a category `C` that has a separator (`HasSeparator C`), `separator C` is an arbitrarily
chosen separator of `C`.
-/
noncomputable def separator [HasSeparator C] : C := HasSeparator.hasSeparator.choose

/--
Given a category `C` that has a coseparator (`HasCoseparator C`), `coseparator C` is an arbitrarily
chosen coseparator of `C`.
-/
noncomputable def coseparator [HasCoseparator C] : C := HasCoseparator.hasCoseparator.choose

/--
Given a category `C` that has a detector (`HasDetector C`), `detector C` is an arbitrarily
chosen detector of `C`.
-/
noncomputable def detector [HasDetector C] : C := HasDetector.hasDetector.choose

/--
Given a category `C` that has a codetector (`HasCodetector C`), `codetector C` is an arbitrarily
chosen codetector of `C`.
-/
noncomputable def codetector [HasCodetector C] : C := HasCodetector.hasCodetector.choose

theorem isSeparator_separator [HasSeparator C] : IsSeparator (separator C) :=
  HasSeparator.hasSeparator.choose_spec

theorem isDetector_separator [Balanced C] [HasSeparator C] : IsDetector (separator C) :=
  isSeparator_separator C |>.isDetector

theorem isCoseparator_coseparator [HasCoseparator C] : IsCoseparator (coseparator C) :=
  HasCoseparator.hasCoseparator.choose_spec

theorem isCodetector_coseparator [Balanced C] [HasCoseparator C] : IsCodetector (coseparator C) :=
  isCoseparator_coseparator C |>.isCodetector

theorem isDetector_detector [HasDetector C] : IsDetector (detector C) :=
  HasDetector.hasDetector.choose_spec

theorem isSeparator_detector [HasEqualizers C] [HasDetector C] : IsSeparator (detector C) :=
  isDetector_detector C |>.isSeparator

theorem isCodetector_codetector [HasCodetector C] : IsCodetector (codetector C) :=
  HasCodetector.hasCodetector.choose_spec

theorem isCoseparator_codetector [HasCoequalizers C] [HasCodetector C] :
    IsCoseparator (codetector C) := isCodetector_codetector C |>.isCoseparator

end Choice

section Instances

theorem HasSeparator.hasDetector [Balanced C] [HasSeparator C] : HasDetector C :=
  ‚ü®_, isDetector_separator C‚ü©

theorem HasDetector.hasSeparator [HasEqualizers C] [HasDetector C] : HasSeparator C :=
  ‚ü®_, isSeparator_detector C‚ü©

theorem HasCoseparator.hasCodetector [Balanced C] [HasCoseparator C] : HasCodetector C :=
  ‚ü®_, isCodetector_coseparator C‚ü©

theorem HasCodetector.hasCoseparator [HasCoequalizers C] [HasCodetector C] : HasCoseparator C :=
  ‚ü®_, isCoseparator_codetector C‚ü©

instance HasDetector.wellPowered [HasPullbacks C] [HasDetector C] : WellPowered.{v‚ÇÅ} C :=
  isDetector_detector C |> wellPowered_of_isDetector _

instance HasSeparator.wellPowered [HasPullbacks C] [Balanced C] [HasSeparator C] :
    WellPowered.{v‚ÇÅ} C := HasSeparator.hasDetector.wellPowered

end Instances

section Equivalence

theorem HasSeparator.of_equivalence [HasSeparator C] (Œ± : C ‚âå D) : HasSeparator D :=
  ‚ü®Œ±.functor.obj (separator C), isSeparator_separator C |>.of_equivalence Œ±‚ü©

theorem HasCoseparator.of_equivalence [HasCoseparator C] (Œ± : C ‚âå D) : HasCoseparator D :=
  ‚ü®Œ±.functor.obj (coseparator C), isCoseparator_coseparator C |>.of_equivalence Œ±‚ü©

end Equivalence

section Dual

@[simp]
theorem hasSeparator_op_iff : HasSeparator C·µí·µñ ‚Üî HasCoseparator C :=
  ‚ü®fun ‚ü®G, hG‚ü© => ‚ü®unop G, (isCoseparator_unop_iff G).mpr hG‚ü©,
   fun ‚ü®G, hG‚ü© => ‚ü®op G, (isSeparator_op_iff G).mpr hG‚ü©‚ü©

@[simp]
theorem hasCoseparator_op_iff : HasCoseparator C·µí·µñ ‚Üî HasSeparator C :=
  ‚ü®fun ‚ü®G, hG‚ü© => ‚ü®unop G, (isSeparator_unop_iff G).mpr hG‚ü©,
   fun ‚ü®G, hG‚ü© => ‚ü®op G, (isCoseparator_op_iff G).mpr hG‚ü©‚ü©

@[simp]
theorem hasDetector_op_iff : HasDetector C·µí·µñ ‚Üî HasCodetector C :=
  ‚ü®fun ‚ü®G, hG‚ü© => ‚ü®unop G, (isCodetector_unop_iff G).mpr hG‚ü©,
   fun ‚ü®G, hG‚ü© => ‚ü®op G, (isDetector_op_iff G).mpr hG‚ü©‚ü©

@[simp]
theorem hasCodetector_op_iff : HasCodetector C·µí·µñ ‚Üî HasDetector C :=
  ‚ü®fun ‚ü®G, hG‚ü© => ‚ü®unop G, (isDetector_unop_iff G).mpr hG‚ü©,
   fun ‚ü®G, hG‚ü© => ‚ü®op G, (isCodetector_op_iff G).mpr hG‚ü©‚ü©

instance HasSeparator.hasCoseparator_op [HasSeparator C] : HasCoseparator C·µí·µñ := by simp [*]
theorem HasSeparator.hasCoseparator_of_hasSeparator_op [h : HasSeparator C·µí·µñ] :
    HasCoseparator C := by simp_all

instance HasCoseparator.hasSeparator_op [HasCoseparator C] : HasSeparator C·µí·µñ := by simp [*]
theorem HasCoseparator.hasSeparator_of_hasCoseparator_op [HasCoseparator C·µí·µñ] :
    HasSeparator C := by simp_all

instance HasDetector.hasCodetector_op [HasDetector C] : HasCodetector C·µí·µñ := by simp [*]
theorem HasDetector.hasCodetector_of_hasDetector_op [HasDetector C·µí·µñ] :
    HasCodetector C := by simp_all

instance HasCodetector.hasDetector_op [HasCodetector C] : HasDetector C·µí·µñ := by simp [*]
theorem HasCodetector.hasDetector_of_hasCodetector_op [HasCodetector C·µí·µñ] :
    HasDetector C := by simp_all

end Dual

end HasGenerator

@[deprecated (since := "2025-10-06")] alias IsSeparating := ObjectProperty.IsSeparating
@[deprecated (since := "2025-10-06")] alias IsCoseparating := ObjectProperty.IsCoseparating
@[deprecated (since := "2025-10-06")] alias IsDetecting := ObjectProperty.IsDetecting
@[deprecated (since := "2025-10-06")] alias IsCodetecting := ObjectProperty.IsCodetecting
@[deprecated (since := "2025-10-06")] alias IsSeparating.of_equivalence :=
  ObjectProperty.IsSeparating.of_equivalence
@[deprecated (since := "2025-10-06")] alias IsCoseparating.of_equivalence :=
  ObjectProperty.IsCoseparating.of_equivalence
@[deprecated (since := "2025-10-06")] alias isSeparating_op_iff :=
  ObjectProperty.isSeparating_op_iff
@[deprecated (since := "2025-10-06")] alias isCoseparating_op_iff :=
  ObjectProperty.isCoseparating_op_iff
@[deprecated (since := "2025-10-06")] alias isSeparating_unop_iff :=
  ObjectProperty.isSeparating_op_iff
@[deprecated (since := "2025-10-06")] alias isCoseparating_unop_iff :=
  ObjectProperty.isCoseparating_op_iff
@[deprecated (since := "2025-10-06")] alias isDetecting_op_iff :=
  ObjectProperty.isDetecting_op_iff
@[deprecated (since := "2025-10-06")] alias isCodetecting_op_iff :=
  ObjectProperty.isCodetecting_op_iff
@[deprecated (since := "2025-10-06")] alias isDetecting_unop_iff :=
  ObjectProperty.isDetecting_op_iff
@[deprecated (since := "2025-10-06")] alias isCodetecting_unop_iff :=
  ObjectProperty.isCodetecting_op_iff
@[deprecated (since := "2025-10-06")] alias IsDetecting.isSeparating :=
  ObjectProperty.IsDetecting.isSeparating
@[deprecated (since := "2025-10-06")] alias IsCodetecting.isCoseparating :=
  ObjectProperty.IsCodetecting.isCoseparating
@[deprecated (since := "2025-10-06")] alias IsSeparating.isDetecting :=
  ObjectProperty.IsSeparating.isDetecting
@[deprecated (since := "2025-10-06")] alias IsDetecting.isIso_iff_of_mono :=
  ObjectProperty.IsDetecting.isIso_iff_of_mono
@[deprecated (since := "2025-10-06")] alias IsCodetecting.isIso_iff_of_epi :=
  ObjectProperty.IsCodetecting.isIso_iff_of_epi
@[deprecated (since := "2025-10-06")] alias IsCoseparating.isCodetecting :=
  ObjectProperty.IsCoseparating.isCodetecting
@[deprecated (since := "2025-10-06")] alias isDetecting_iff_isSeparating :=
  ObjectProperty.isDetecting_iff_isSeparating
@[deprecated (since := "2025-10-06")] alias isCodetecting_iff_isCoseparating :=
  ObjectProperty.isCodetecting_iff_isCoseparating
@[deprecated (since := "2025-10-06")] alias IsSeparating.mono :=
  ObjectProperty.IsSeparating.of_le
@[deprecated (since := "2025-10-06")] alias IsCoseparating.mono :=
  ObjectProperty.IsCoseparating.of_le
@[deprecated (since := "2025-10-06")] alias IsDetecting.mono :=
  ObjectProperty.IsDetecting.of_le
@[deprecated (since := "2025-10-06")] alias IsCodetecting.mono :=
  ObjectProperty.IsCodetecting.of_le
@[deprecated (since := "2025-10-06")] alias thin_of_isSeparating_empty :=
  ObjectProperty.isThin_of_isSeparating_bot
@[deprecated (since := "2025-10-06")] alias isSeparating_empty_of_thin :=
  ObjectProperty.isSeparating_bot_of_isThin
@[deprecated (since := "2025-10-06")] alias thin_of_isCoseparating_empty :=
  ObjectProperty.isThin_of_isCoseparating_bot
@[deprecated (since := "2025-10-06")] alias isCoseparating_empty_of_thin :=
  ObjectProperty.isCoseparating_bot_of_isThin
@[deprecated (since := "2025-10-06")] alias groupoid_of_isDetecting_empty :=
  ObjectProperty.isGroupoid_of_isDetecting_bot
@[deprecated (since := "2025-10-06")] alias isDetecting_empty_of_groupoid :=
  ObjectProperty.isDetecting_bot_of_isGroupoid
@[deprecated (since := "2025-10-06")] alias groupoid_of_isCodetecting_empty :=
  ObjectProperty.isGroupoid_of_isCodetecting_bot
@[deprecated (since := "2025-10-06")] alias isCodetecting_empty_of_groupoid :=
  ObjectProperty.isCodetecting_bot_of_isGroupoid
@[deprecated (since := "2025-10-07")] alias isSeparating_iff_epi :=
  ObjectProperty.isSeparating_iff_epi
@[deprecated (since := "2025-10-07")] alias isCoseparating_iff_mono :=
  ObjectProperty.isCoseparating_iff_mono
@[deprecated (since := "2025-10-07")] alias IsSeparating.isSeparator_coproduct :=
  ObjectProperty.IsSeparating.isSeparator_coproduct
@[deprecated (since := "2025-10-07")] alias StructuredArrow.isCoseparating_proj_preimage :=
  StructuredArrow.isCoseparating_inverseImage_proj
@[deprecated (since := "2025-10-07")] alias CostructuredArrow.isSeparating_proj_preimage :=
  CostructuredArrow.isSeparating_inverseImage_proj

end CategoryTheory
