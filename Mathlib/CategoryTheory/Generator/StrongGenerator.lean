/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.ExtremalEpi
import Mathlib.CategoryTheory.Generator.Basic
import Mathlib.CategoryTheory.Limits.Presentation

/-!
# Strong generators

A set of objects `S` in a category `C` is a strong generator if it is a
generator (in the sense that `IsSeparating S` holds) such that for any
proper subobject `A ‚äÇ X`, there exists a morphism `G ‚ü∂ X` from an
object in `S` which does not factor through `A`.

The main result if the lemma `isStrongGenerator_iff_exists_extremalEpi` which
says that if `S` is `w`-small, `C` is locally `w`-small and
has coproducts of size `w`, then `S` is a strong generator iff any
object of `C` is the target of extremal epimorphism from a coproduct of
objects in `S`. A similar iff lemma for `IsSeparating` is also obtained.

We also show that if any object in `C` is a colimit of objects in `S`,
then `S` is a strong generator.

## References
* [Ad√°mek, J. and Rosick√Ω, J., *Locally presentable and accessible categories*][Adamek_Rosicky_1994]

-/

universe w v u

namespace CategoryTheory

open Limits

variable {C : Type u} [Category.{v} C] (S : Set C)

/-- A set of objects is a strong generator if it is separating and for any
proper subobject `A ‚äÇ X`, there exists a morphism `G ‚ü∂ X` from an
object in `S` which does not factor through `A`. -/
def IsStrongGenerator : Prop :=
  IsSeparating S ‚àß ‚àÄ ‚¶ÉX : C‚¶Ñ (A : Subobject X),
    (‚àÄ (G : S) (f : G.1 ‚ü∂ X), Subobject.Factors A f) ‚Üí A = ‚ä§

variable {S}

lemma isStrongGenerator_iff :
    IsStrongGenerator S ‚Üî IsSeparating S ‚àß
      ‚àÄ ‚¶ÉX Y : C‚¶Ñ (i : X ‚ü∂ Y) [Mono i],
        (‚àÄ (G : S), Function.Surjective (fun (f : G.1 ‚ü∂ X) ‚Ü¶ f ‚â´ i)) ‚Üí IsIso i := by
  refine ‚ü®fun ‚ü®hS‚ÇÅ, hS‚ÇÇ‚ü© ‚Ü¶ ‚ü®hS‚ÇÅ, fun X Y i _ h ‚Ü¶ ?_‚ü©,
    fun ‚ü®hS‚ÇÅ, hS‚ÇÇ‚ü© ‚Ü¶ ‚ü®hS‚ÇÅ, fun X A hA ‚Ü¶ ?_‚ü©‚ü©
  ¬∑ rw [Subobject.isIso_iff_mk_eq_top]
    refine hS‚ÇÇ _ (fun G g ‚Ü¶ ?_)
    rw [Subobject.mk_factors_iff]
    exact h G g
  ¬∑ rw [‚Üê Subobject.isIso_arrow_iff_eq_top]
    exact hS‚ÇÇ A.arrow (fun G g ‚Ü¶ ‚ü®_, Subobject.factorThru_arrow _ _ (hA G g)‚ü©)

namespace IsStrongGenerator

section

variable (hS : IsStrongGenerator S)

include hS

lemma isSeparating : IsSeparating S := hS.1

lemma subobject_eq_top {X : C} {A : Subobject X}
    (hA : ‚àÄ (G : S) (f : G.1 ‚ü∂ X), Subobject.Factors A f) :
    A = ‚ä§ :=
  hS.2 _ hA

lemma isIso_of_mono ‚¶ÉX Y : C‚¶Ñ (i : X ‚ü∂ Y) [Mono i]
    (hi : ‚àÄ (G : S), Function.Surjective (fun (f : G.1 ‚ü∂ X) ‚Ü¶ f ‚â´ i)) : IsIso i :=
  (isStrongGenerator_iff.1 hS).2 i hi

lemma exists_of_subobject_ne_top {X : C} {A : Subobject X} (hA : A ‚â† ‚ä§) :
    ‚àÉ (G : S) (f : G.1 ‚ü∂ X), ¬¨ Subobject.Factors A f := by
  by_contra!
  exact hA (hS.subobject_eq_top this)

lemma exists_of_mono_not_isIso {X Y : C} (i : X ‚ü∂ Y) [Mono i] (hi : ¬¨ IsIso i) :
    ‚àÉ (G : S) (g : G.1 ‚ü∂ Y), ‚àÄ (f : G.1 ‚ü∂ X), f ‚â´ i ‚â† g := by
  by_contra!
  exact hi (hS.isIso_of_mono i this)

end

end IsStrongGenerator

section

variable (S) [HasCoproducts.{w} C] [LocallySmall.{w} C] [Small.{w} S] (X : C)

/-- Given `S : Set C` and `X : C`, this is the family of source objects
in the family of all maps `G ‚ü∂ X` with `G ‚àà S`. -/
abbrev coproductOfSet.obj (p : Œ£ (s : S), s.1 ‚ü∂ X) : C := p.1.1

lemma coproductOfSet.hasCoproduct :
    HasCoproduct (coproductOfSet.obj S X) :=
  hasColimit_of_equivalence_comp
    (Discrete.equivalence (equivShrink.{w} _)).symm

/-- Given `S : Set C` and `X : C`, this is the coproduct of
`G` indexed by the type of all maps `G ‚ü∂ X` with `G ‚àà S`. -/
noncomputable def coproductOfSet (X : C) : C :=
  have := coproductOfSet.hasCoproduct S X
  ‚àê (coproductOfSet.obj S X)

namespace coproductOfSet

/-- The canonical morphism `coproductOfSet S X ‚ü∂ X`. -/
noncomputable def œÄ (X : C) : coproductOfSet S X ‚ü∂ X :=
  have := coproductOfSet.hasCoproduct S X
  Sigma.desc (fun p ‚Ü¶ p.2)

section

variable {S} {X : C} (s : S) (f : s.1 ‚ü∂ X)

/-- The inclusion morphisms in the coproduct `coproductOfSet S X`. -/
noncomputable def Œπ : s.1 ‚ü∂ coproductOfSet S X :=
  have := coproductOfSet.hasCoproduct S X
  Sigma.Œπ (coproductOfSet.obj S X) ‚ü®s, f‚ü©

@[reassoc (attr := simp)]
lemma Œπ_œÄ : Œπ s f ‚â´ œÄ S X = f := by simp [Œπ, œÄ]

end

end coproductOfSet

end

namespace IsSeparating

lemma epi_coproductOfSetœÄ (hS : IsSeparating S)
    [HasCoproducts.{w} C] [LocallySmall.{w} C] [Small.{w} S] (X : C) :
    Epi (coproductOfSet.œÄ S X) where
  left_cancellation {Z} f g h :=
    hS _ _ (fun G hG p ‚Ü¶ by simpa using coproductOfSet.Œπ ‚ü®_, hG‚ü© p ‚â´= h)

lemma mk_of_exists_epi
    (hS : ‚àÄ (X : C), ‚àÉ (Œπ : Type w) (s : Œπ ‚Üí S) (c : Cofan (fun i ‚Ü¶ (s i).1)) (_ : IsColimit c)
      (p : c.pt ‚ü∂ X), Epi p) :
    IsSeparating S := by
  intro X Y f g h
  obtain ‚ü®Œπ, s, c, hc, p, _‚ü© := hS X
  rw [‚Üê cancel_epi p]
  exact Cofan.IsColimit.hom_ext hc _ _
    (fun i ‚Ü¶ by simpa using h _ (s i).2 (c.inj i ‚â´ p))

end IsSeparating

lemma isSeparating_iff_exists_epi
    [HasCoproducts.{w} C] [LocallySmall.{w} C] [Small.{w} S] :
    IsSeparating S ‚Üî
      ‚àÄ (X : C), ‚àÉ (Œπ : Type w) (s : Œπ ‚Üí S) (c : Cofan (fun i ‚Ü¶ (s i).1)) (_ : IsColimit c)
        (p : c.pt ‚ü∂ X), Epi p := by
  refine ‚ü®fun hS X ‚Ü¶ ?_, fun hS ‚Ü¶ .mk_of_exists_epi hS‚ü©
  have := coproductOfSet.hasCoproduct S X
  exact ‚ü®_, fun j ‚Ü¶ ((equivShrink (Œ£ (s : S), s.1 ‚ü∂ X)).symm j).1, _,
    (colimit.isColimit (Discrete.functor (coproductOfSet.obj S X))).whiskerEquivalence
      ((Discrete.equivalence (equivShrink.{w} _))).symm, _,
      hS.epi_coproductOfSetœÄ X‚ü©

namespace IsStrongGenerator

lemma extremalEpi_coproductOfSetœÄ
    (hS : IsStrongGenerator S) [HasCoproducts.{w} C] [LocallySmall.{w} C]
    [Small.{w} S] (X : C) :
    ExtremalEpi (coproductOfSet.œÄ S X) where
  toEpi := hS.isSeparating.epi_coproductOfSetœÄ X
  isIso {Z} p i fac _ := hS.isIso_of_mono _
    (fun G f ‚Ü¶ ‚ü®coproductOfSet.Œπ G f ‚â´ p, by simp [fac]‚ü©)

lemma mk_of_exists_extremalEpi
    (hS : ‚àÄ (X : C), ‚àÉ (Œπ : Type w) (s : Œπ ‚Üí S) (c : Cofan (fun i ‚Ü¶ (s i).1)) (_ : IsColimit c)
      (p : c.pt ‚ü∂ X), ExtremalEpi p) :
    IsStrongGenerator S := by
  rw [isStrongGenerator_iff]
  refine ‚ü®IsSeparating.mk_of_exists_epi.{w} (fun X ‚Ü¶ ?_), fun X Y i _ hi ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®Œπ, s, c, hc, p, _‚ü© := hS X
    exact ‚ü®Œπ, s, c, hc, p, inferInstance‚ü©
  ¬∑ obtain ‚ü®Œπ, s, c, hc, p, _‚ü© := hS Y
    replace hi (j : Œπ) := hi (s j) (c.inj j ‚â´ p)
    choose œÜ hœÜ using hi
    exact ExtremalEpi.isIso p (Cofan.IsColimit.desc hc œÜ) _
      (Cofan.IsColimit.hom_ext hc _ _ (by simp [hœÜ]))

end IsStrongGenerator

lemma isStrongGenerator_iff_exists_extremalEpi
    [HasCoproducts.{w} C] [LocallySmall.{w} C] [Small.{w} S] :
    IsStrongGenerator S ‚Üî
      ‚àÄ (X : C), ‚àÉ (Œπ : Type w) (s : Œπ ‚Üí S) (c : Cofan (fun i ‚Ü¶ (s i).1)) (_ : IsColimit c)
        (p : c.pt ‚ü∂ X), ExtremalEpi p := by
  refine ‚ü®fun hS X ‚Ü¶ ?_, fun hS ‚Ü¶ .mk_of_exists_extremalEpi hS‚ü©
  have := coproductOfSet.hasCoproduct S X
  exact ‚ü®_, fun j ‚Ü¶ ((equivShrink (Œ£ (s : S), s.1 ‚ü∂ X)).symm j).1, _,
    (colimit.isColimit (Discrete.functor (coproductOfSet.obj S X))).whiskerEquivalence
      ((Discrete.equivalence (equivShrink.{w} _))).symm, _,
      hS.extremalEpi_coproductOfSetœÄ X‚ü©

section

variable (hS : ‚àÄ (X : C), ‚àÉ (J : Type w) (_ : SmallCategory J)
  (p : ColimitPresentation J X), ‚àÄ (j : J), ‚àÉ (G : S), Nonempty (p.diag.obj j ‚âÖ G.1))

include hS

lemma IsSeparating.mk_of_exists_colimitPresentation :
    IsSeparating S := by
  intro X Y f g h
  obtain ‚ü®J, _, p, hp‚ü© := hS X
  choose t ht using hp
  let e (j : J) := (ht j).some
  refine p.isColimit.hom_ext (fun j ‚Ü¶ ?_)
  rw [‚Üê cancel_epi (e j).inv]
  simpa only [Category.assoc] using h _ (t j).2 ((e j).inv ‚â´ p.Œπ.app j)

lemma IsStrongGenerator.mk_of_exists_colimitPresentation :
    IsStrongGenerator S := by
  rw [isStrongGenerator_iff]
  refine ‚ü®IsSeparating.mk_of_exists_colimitPresentation hS,
    fun X Y i _ hi ‚Ü¶ ?_‚ü©
  suffices ‚àÉ (r : Y ‚ü∂ X), r ‚â´ i = ùüô Y by
    obtain ‚ü®r, fac‚ü© := this
    exact ‚ü®r, by simp [‚Üê cancel_mono i, fac], fac‚ü©
  obtain ‚ü®J, _, p, hp‚ü© := hS Y
  have (j : J) : ‚àÉ (l : p.diag.obj j ‚ü∂ X), l ‚â´ i = p.Œπ.app j := by
    obtain ‚ü®G, ‚ü®e‚ü©‚ü© := hp j
    obtain ‚ü®l, hl‚ü© := hi G (e.inv ‚â´ p.Œπ.app j)
    exact ‚ü®e.hom ‚â´ l, by simp [hl]‚ü©
  choose œÜ hœÜ using this
  let c : Cocone p.diag := Cocone.mk _
    { app := œÜ
      naturality j‚ÇÅ j‚ÇÇ f := by simp [‚Üê cancel_mono i, hœÜ] }
  refine ‚ü®p.isColimit.desc c, p.isColimit.hom_ext (fun j ‚Ü¶ ?_)‚ü©
  have := p.isColimit.fac c j
  dsimp [c] at this ‚ä¢
  rw [reassoc_of% this, hœÜ, Category.comp_id]

end

end CategoryTheory
