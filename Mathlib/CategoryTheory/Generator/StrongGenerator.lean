/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.ExtremalEpi
import Mathlib.CategoryTheory.Generator.Basic
import Mathlib.CategoryTheory.Limits.Presentation

/-!
# Strong generators

If `P : ObjectProperty C`, we say that `P` is a strong generator if it is a
generator (in the sense that `IsSeparating P` holds) such that for any
proper subobject `A ‚äÇ X`, there exists a morphism `G ‚ü∂ X` which does not factor
through `A` from an object satisfying `P`.

The main result is the lemma `isStrongGenerator_iff_exists_extremalEpi` which
says that if `P` is `w`-small, `C` is locally `w`-small and
has coproducts of size `w`, then `P` is a strong generator iff any
object of `C` is the target of an extremal epimorphism from a coproduct of
objects satisfying `P`.

We also show that if any object in `C` is a colimit of objects in `S`,
then `S` is a strong generator.

## References
* [Ad√°mek, J. and Rosick√Ω, J., *Locally presentable and accessible categories*][Adamek_Rosicky_1994]

-/

universe w v u

namespace CategoryTheory

open Limits


namespace ObjectProperty

variable {C : Type u} [Category.{v} C] (P : ObjectProperty C)

/-- A property `P : ObjectProperty C` is a strong generator
if it is separating and for any proper subobject `A ‚äÇ X`, there exists
a morphism `G ‚ü∂ X` which does not factor through `A` from an object
such that `P G` holds. -/
def IsStrongGenerator : Prop :=
  P.IsSeparating ‚àß ‚àÄ ‚¶ÉX : C‚¶Ñ (A : Subobject X),
    (‚àÄ (G : C) (_ : P G) (f : G ‚ü∂ X), Subobject.Factors A f) ‚Üí A = ‚ä§

variable {P}

lemma isStrongGenerator_iff :
    P.IsStrongGenerator ‚Üî P.IsSeparating ‚àß
      ‚àÄ ‚¶ÉX Y : C‚¶Ñ (i : X ‚ü∂ Y) [Mono i],
        (‚àÄ (G : C) (_ : P G), Function.Surjective (fun (f : G ‚ü∂ X) ‚Ü¶ f ‚â´ i)) ‚Üí IsIso i := by
  refine ‚ü®fun ‚ü®hS‚ÇÅ, hS‚ÇÇ‚ü© ‚Ü¶ ‚ü®hS‚ÇÅ, fun X Y i _ h ‚Ü¶ ?_‚ü©,
    fun ‚ü®hS‚ÇÅ, hS‚ÇÇ‚ü© ‚Ü¶ ‚ü®hS‚ÇÅ, fun X A hA ‚Ü¶ ?_‚ü©‚ü©
  ¬∑ rw [Subobject.isIso_iff_mk_eq_top]
    refine hS‚ÇÇ _ (fun G hG g ‚Ü¶ ?_)
    rw [Subobject.mk_factors_iff]
    exact h G hG g
  ¬∑ rw [‚Üê Subobject.isIso_arrow_iff_eq_top]
    exact hS‚ÇÇ A.arrow (fun G hG g ‚Ü¶ ‚ü®_, Subobject.factorThru_arrow _ _ (hA G hG g)‚ü©)

namespace IsStrongGenerator

section

variable (hP : P.IsStrongGenerator)

include hP

lemma isSeparating : P.IsSeparating := hP.1

lemma subobject_eq_top {X : C} {A : Subobject X}
    (hA : ‚àÄ (G : C) (_ : P G) (f : G ‚ü∂ X), Subobject.Factors A f) :
    A = ‚ä§ :=
  hP.2 _ hA

lemma isIso_of_mono ‚¶ÉX Y : C‚¶Ñ (i : X ‚ü∂ Y) [Mono i]
    (hi : ‚àÄ (G : C) (_ : P G), Function.Surjective (fun (f : G ‚ü∂ X) ‚Ü¶ f ‚â´ i)) : IsIso i :=
  (isStrongGenerator_iff.1 hP).2 i hi

lemma exists_of_subobject_ne_top {X : C} {A : Subobject X} (hA : A ‚â† ‚ä§) :
    ‚àÉ (G : C) (_ : P G) (f : G ‚ü∂ X), ¬¨ Subobject.Factors A f := by
  by_contra!
  exact hA (hP.subobject_eq_top this)

lemma exists_of_mono_not_isIso {X Y : C} (i : X ‚ü∂ Y) [Mono i] (hi : ¬¨ IsIso i) :
    ‚àÉ (G : C) (_ : P G) (g : G ‚ü∂ Y), ‚àÄ (f : G ‚ü∂ X), f ‚â´ i ‚â† g := by
  by_contra!
  exact hi (hP.isIso_of_mono i this)

end

end IsStrongGenerator

namespace IsStrongGenerator

lemma mk_of_exists_extremalEpi
    (hS : ‚àÄ (X : C), ‚àÉ (Œπ : Type w) (s : Œπ ‚Üí C) (_ : ‚àÄ i, P (s i)) (c : Cofan s) (_ : IsColimit c)
      (p : c.pt ‚ü∂ X), ExtremalEpi p) :
    P.IsStrongGenerator := by
  rw [isStrongGenerator_iff]
  refine ‚ü®IsSeparating.mk_of_exists_epi.{w} (fun X ‚Ü¶ ?_), fun X Y i _ hi ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®Œπ, s, hs, c, hc, p, _‚ü© := hS X
    exact ‚ü®Œπ, s, hs, c, hc, p, inferInstance‚ü©
  ¬∑ obtain ‚ü®Œπ, s, hs, c, hc, p, _‚ü© := hS Y
    replace hi (j : Œπ) := hi (s j) (hs j) (c.inj j ‚â´ p)
    choose œÜ hœÜ using hi
    exact ExtremalEpi.isIso p (Cofan.IsColimit.desc hc œÜ) _
      (Cofan.IsColimit.hom_ext hc _ _ (by simp [hœÜ]))

lemma extremalEpi_coproductFrom
    (hP : IsStrongGenerator P) (X : C) [HasCoproduct (P.coproductFromFamily X)] :
    ExtremalEpi (P.coproductFrom X) where
  toEpi := hP.isSeparating.epi_coproductFrom X
  isIso p i fac _ := hP.isIso_of_mono _ (fun G hG f ‚Ü¶ ‚ü®P.ŒπCoproductFrom f hG ‚â´ p, by simp [fac]‚ü©)

end IsStrongGenerator

lemma isStrongGenerator_iff_exists_extremalEpi
    [HasCoproducts.{w} C] [LocallySmall.{w} C] [ObjectProperty.Small.{w} P] :
    P.IsStrongGenerator ‚Üî
      ‚àÄ (X : C), ‚àÉ (Œπ : Type w) (s : Œπ ‚Üí C) (_ : ‚àÄ i, P (s i)) (c : Cofan s) (_ : IsColimit c)
        (p : c.pt ‚ü∂ X), ExtremalEpi p := by
  refine ‚ü®fun hP X ‚Ü¶ ?_, fun hP ‚Ü¶ .mk_of_exists_extremalEpi hP‚ü©
  have := hasCoproductsOfShape_of_small.{w} C (CostructuredArrow P.Œπ X)
  have := (coproductIsCoproduct (P.coproductFromFamily X)).whiskerEquivalence
    ((Discrete.equivalence (equivShrink.{w} _))).symm
  refine ‚ü®_, fun j ‚Ü¶ ((equivShrink.{w} (CostructuredArrow P.Œπ X)).symm j).left.1,
    fun j ‚Ü¶ ((equivShrink.{w} _).symm j).1.2, _,
    (coproductIsCoproduct (P.coproductFromFamily X)).whiskerEquivalence
    ((Discrete.equivalence (equivShrink.{w} _))).symm, _, hP.extremalEpi_coproductFrom X‚ü©

lemma IsStrongGenerator.mk_of_exists_colimitsOfShape
    (hP : ‚àÄ (X : C), ‚àÉ (J : Type w) (_ : SmallCategory J), Nonempty (P.ColimitOfShape J X)) :
    P.IsStrongGenerator := by
  rw [isStrongGenerator_iff]
  refine ‚ü®IsSeparating.mk_of_exists_colimitsOfShape hP,
    fun X Y i _ hi ‚Ü¶ ?_‚ü©
  suffices ‚àÉ (r : Y ‚ü∂ X), r ‚â´ i = ùüô Y by
    obtain ‚ü®r, fac‚ü© := this
    exact ‚ü®r, by simp [‚Üê cancel_mono i, fac], fac‚ü©
  obtain ‚ü®J, _, ‚ü®p‚ü©‚ü© := hP Y
  choose œÜ hœÜ using fun j ‚Ü¶ hi _ (p.prop_diag_obj j) (p.Œπ.app j)
  dsimp at hœÜ
  let c : Cocone p.diag := Cocone.mk _
    { app := œÜ
      naturality j‚ÇÅ j‚ÇÇ f := by simp [‚Üê cancel_mono i, hœÜ] }
  refine ‚ü®p.isColimit.desc c, p.isColimit.hom_ext (fun j ‚Ü¶ ?_)‚ü©
  dsimp
  rw [p.isColimit.fac_assoc, hœÜ, Category.comp_id]

end ObjectProperty

end CategoryTheory
