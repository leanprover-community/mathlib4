/-
Copyright (c) 2020 Bhavik Mehta, Edward Ayers, Thomas Read. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Edward Ayers, Thomas Read
-/
import Mathlib.CategoryTheory.EpiMono
import Mathlib.CategoryTheory.Limits.Shapes.FiniteProducts
import Mathlib.CategoryTheory.Monoidal.OfHasFiniteProducts
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Adjunction.Limits
import Mathlib.CategoryTheory.Adjunction.Mates
import Mathlib.CategoryTheory.Closed.Monoidal

/-!
# Cartesian closed categories

Given a category with finite products, the cartesian monoidal structure is provided by the local
instance `monoidalOfHasFiniteProducts`.

We define exponentiable objects to be closed objects with respect to this monoidal structure,
i.e. `(X Ã— -)` is a left adjoint.

We say a category is cartesian closed if every object is exponentiable
(equivalently, that the category equipped with the cartesian monoidal structure is closed monoidal).

Show that exponential forms a difunctor and define the exponential comparison morphisms.

## TODO
Some of the results here are true more generally for closed objects and
for closed monoidal categories, and these could be generalised.
-/


universe v vâ‚‚ u uâ‚‚

noncomputable section

namespace CategoryTheory

open CategoryTheory CategoryTheory.Category CategoryTheory.Limits

attribute [local instance] monoidalOfHasFiniteProducts

/-- An object `X` is *exponentiable* if `(X Ã— -)` is a left adjoint.
We define this as being `Closed` in the cartesian monoidal structure.
-/
abbrev Exponentiable {C : Type u} [Category.{v} C] [HasFiniteProducts C] (X : C) :=
  Closed X

/-- Constructor for `Exponentiable X` which takes as an input an adjunction
`MonoidalCategory.tensorLeft X âŠ£ exp` for some functor `exp : C â¥¤ C`. -/
abbrev Exponentiable.mk {C : Type u} [Category.{v} C] [HasFiniteProducts C] (X : C)
    (exp : C â¥¤ C) (adj : MonoidalCategory.tensorLeft X âŠ£ exp) :
    Exponentiable X where
  adj := adj

/-- If `X` and `Y` are exponentiable then `X â¨¯ Y` is.
This isn't an instance because it's not usually how we want to construct exponentials, we'll usually
prove all objects are exponential uniformly.
-/
def binaryProductExponentiable {C : Type u} [Category.{v} C] [HasFiniteProducts C] {X Y : C}
    (hX : Exponentiable X) (hY : Exponentiable Y) : Exponentiable (X â¨¯ Y) :=
  tensorClosed hX hY

/-- The terminal object is always exponentiable.
This isn't an instance because most of the time we'll prove cartesian closed for all objects
at once, rather than just for this one.
-/
def terminalExponentiable {C : Type u} [Category.{v} C] [HasFiniteProducts C] :
    Exponentiable (âŠ¤_ C) :=
  unitClosed

/-- A category `C` is cartesian closed if it has finite products and every object is exponentiable.
We define this as `monoidal_closed` with respect to the cartesian monoidal structure.
-/
abbrev CartesianClosed (C : Type u) [Category.{v} C] [HasFiniteProducts C] :=
  MonoidalClosed C

-- Porting note: added to ease the port of `CategoryTheory.Closed.Types`
/-- Constructor for `CartesianClosed C`. -/
def CartesianClosed.mk (C : Type u) [Category.{v} C] [HasFiniteProducts C]
    (exp : âˆ€ (X : C), Exponentiable X) :
    CartesianClosed C where
  closed X := exp X

variable {C : Type u} [Category.{v} C] (A B : C) {X X' Y Y' Z : C}
variable [HasFiniteProducts C] [Exponentiable A]

/-- This is (-)^A. -/
abbrev exp : C â¥¤ C :=
  ihom A

namespace exp

/-- The adjunction between A â¨¯ - and (-)^A. -/
abbrev adjunction : prod.functor.obj A âŠ£ exp A :=
  ihom.adjunction A

/-- The evaluation natural transformation. -/
abbrev ev : exp A â‹™ prod.functor.obj A âŸ¶ ðŸ­ C :=
  ihom.ev A

/-- The coevaluation natural transformation. -/
abbrev coev : ðŸ­ C âŸ¶ prod.functor.obj A â‹™ exp A :=
  ihom.coev A

-- Porting note: notation fails to elaborate with `quotPrecheck` on.
set_option quotPrecheck false in
/-- Morphisms obtained using an exponentiable object. -/
notation:20 A " âŸ¹ " B:19 => (exp A).obj B

open Lean PrettyPrinter.Delaborator SubExpr in
/-- Delaborator for `Prefunctor.obj` -/
@[delab app.Prefunctor.obj]
def delabPrefunctorObjExp : Delab := whenPPOption getPPNotation <| withOverApp 6 <| do
  let e â† getExpr
  guard <| e.isAppOfArity' ``Prefunctor.obj 6
  let A â† withNaryArg 4 do
    let e â† getExpr
    guard <| e.isAppOfArity' ``Functor.toPrefunctor 5
    withNaryArg 4 do
      let e â† getExpr
      guard <| e.isAppOfArity' ``exp 5
      withNaryArg 2 delab
  let B â† withNaryArg 5 delab
  `($A âŸ¹ $B)

-- Porting note: notation fails to elaborate with `quotPrecheck` on.
set_option quotPrecheck false in
/-- Morphisms from an exponentiable object. -/
notation:30 B " ^^ " A:30 => (exp A).obj B

@[simp, reassoc]
theorem ev_coev : Limits.prod.map (ðŸ™ A) ((coev A).app B) â‰« (ev A).app (A â¨¯ B) = ðŸ™ (A â¨¯ B) :=
  ihom.ev_coev A B

@[reassoc]
theorem coev_ev : (coev A).app (A âŸ¹ B) â‰« (exp A).map ((ev A).app B) = ðŸ™ (A âŸ¹ B) :=
  ihom.coev_ev A B

end exp

instance : PreservesColimits (prod.functor.obj A) :=
  (ihom.adjunction A).leftAdjointPreservesColimits

variable {A}

-- Wrap these in a namespace so we don't clash with the core versions.
namespace CartesianClosed

/-- Currying in a cartesian closed category. -/
def curry : (A â¨¯ Y âŸ¶ X) â†’ (Y âŸ¶ A âŸ¹ X) :=
  (exp.adjunction A).homEquiv _ _

/-- Uncurrying in a cartesian closed category. -/
def uncurry : (Y âŸ¶ A âŸ¹ X) â†’ (A â¨¯ Y âŸ¶ X) :=
  ((exp.adjunction A).homEquiv _ _).symm

-- This lemma has always been bad, but the linter only noticed after lean4#2644.
@[simp, nolint simpNF]
theorem homEquiv_apply_eq (f : A â¨¯ Y âŸ¶ X) : (exp.adjunction A).homEquiv _ _ f = curry f :=
  rfl

-- This lemma has always been bad, but the linter only noticed after lean4#2644.
@[simp, nolint simpNF]
theorem homEquiv_symm_apply_eq (f : Y âŸ¶ A âŸ¹ X) :
    ((exp.adjunction A).homEquiv _ _).symm f = uncurry f :=
  rfl

@[reassoc]
theorem curry_natural_left (f : X âŸ¶ X') (g : A â¨¯ X' âŸ¶ Y) :
    curry (Limits.prod.map (ðŸ™ _) f â‰« g) = f â‰« curry g :=
  Adjunction.homEquiv_naturality_left _ _ _

@[reassoc]
theorem curry_natural_right (f : A â¨¯ X âŸ¶ Y) (g : Y âŸ¶ Y') :
    curry (f â‰« g) = curry f â‰« (exp _).map g :=
  Adjunction.homEquiv_naturality_right _ _ _

@[reassoc]
theorem uncurry_natural_right (f : X âŸ¶ A âŸ¹ Y) (g : Y âŸ¶ Y') :
    uncurry (f â‰« (exp _).map g) = uncurry f â‰« g :=
  Adjunction.homEquiv_naturality_right_symm _ _ _

@[reassoc]
theorem uncurry_natural_left (f : X âŸ¶ X') (g : X' âŸ¶ A âŸ¹ Y) :
    uncurry (f â‰« g) = Limits.prod.map (ðŸ™ _) f â‰« uncurry g :=
  Adjunction.homEquiv_naturality_left_symm _ _ _

@[simp]
theorem uncurry_curry (f : A â¨¯ X âŸ¶ Y) : uncurry (curry f) = f :=
  (Closed.adj.homEquiv _ _).left_inv f

@[simp]
theorem curry_uncurry (f : X âŸ¶ A âŸ¹ Y) : curry (uncurry f) = f :=
  (Closed.adj.homEquiv _ _).right_inv f

-- Porting note: extra `(exp.adjunction A)` argument was needed for elaboration to succeed.
theorem curry_eq_iff (f : A â¨¯ Y âŸ¶ X) (g : Y âŸ¶ A âŸ¹ X) : curry f = g â†” f = uncurry g :=
  Adjunction.homEquiv_apply_eq (exp.adjunction A) f g

-- Porting note: extra `(exp.adjunction A)` argument was needed for elaboration to succeed.
theorem eq_curry_iff (f : A â¨¯ Y âŸ¶ X) (g : Y âŸ¶ A âŸ¹ X) : g = curry f â†” uncurry g = f :=
  Adjunction.eq_homEquiv_apply (exp.adjunction A) f g

-- I don't think these two should be simp.
theorem uncurry_eq (g : Y âŸ¶ A âŸ¹ X) : uncurry g = Limits.prod.map (ðŸ™ A) g â‰« (exp.ev A).app X :=
  Adjunction.homEquiv_counit _

theorem curry_eq (g : A â¨¯ Y âŸ¶ X) : curry g = (exp.coev A).app Y â‰« (exp A).map g :=
  Adjunction.homEquiv_unit _

theorem uncurry_id_eq_ev (A X : C) [Exponentiable A] : uncurry (ðŸ™ (A âŸ¹ X)) = (exp.ev A).app X := by
  rw [uncurry_eq, prod.map_id_id, id_comp]

theorem curry_id_eq_coev (A X : C) [Exponentiable A] : curry (ðŸ™ _) = (exp.coev A).app X := by
  rw [curry_eq, (exp A).map_id (A â¨¯ _)]; apply comp_id

theorem curry_injective : Function.Injective (curry : (A â¨¯ Y âŸ¶ X) â†’ (Y âŸ¶ A âŸ¹ X)) :=
  (Closed.adj.homEquiv _ _).injective

theorem uncurry_injective : Function.Injective (uncurry : (Y âŸ¶ A âŸ¹ X) â†’ (A â¨¯ Y âŸ¶ X)) :=
  (Closed.adj.homEquiv _ _).symm.injective

end CartesianClosed

open CartesianClosed

/-- Show that the exponential of the terminal object is isomorphic to itself, i.e. `X^1 â‰… X`.

The typeclass argument is explicit: any instance can be used.
-/
def expTerminalIsoSelf [Exponentiable (âŠ¤_ C)] : (âŠ¤_ C) âŸ¹ X â‰… X :=
  Yoneda.ext ((âŠ¤_ C) âŸ¹ X) X
    (fun {Y} f => (Limits.prod.leftUnitor Y).inv â‰« CartesianClosed.uncurry f)
    (fun {Y} f => CartesianClosed.curry ((Limits.prod.leftUnitor Y).hom â‰« f))
    (fun g => by
      rw [curry_eq_iff, Iso.hom_inv_id_assoc])
    (fun g => by simp)
    (fun f g => by
      -- Porting note: `rw` is a bit brittle here, requiring the `dsimp` rule cancellation.
      dsimp [-prod.leftUnitor_inv]
      rw [uncurry_natural_left, prod.leftUnitor_inv_naturality_assoc f])

/-- The internal element which points at the given morphism. -/
def internalizeHom (f : A âŸ¶ Y) : âŠ¤_ C âŸ¶ A âŸ¹ Y :=
  CartesianClosed.curry (Limits.prod.fst â‰« f)

section Pre

variable {B}

/-- Pre-compose an internal hom with an external hom. -/
def pre (f : B âŸ¶ A) [Exponentiable B] : exp A âŸ¶ exp B :=
  conjugateEquiv (exp.adjunction _) (exp.adjunction _) (prod.functor.map f)

theorem prod_map_pre_app_comp_ev (f : B âŸ¶ A) [Exponentiable B] (X : C) :
    Limits.prod.map (ðŸ™ B) ((pre f).app X) â‰« (exp.ev B).app X =
      Limits.prod.map f (ðŸ™ (A âŸ¹ X)) â‰« (exp.ev A).app X :=
  conjugateEquiv_counit _ _ (prod.functor.map f) X

theorem uncurry_pre (f : B âŸ¶ A) [Exponentiable B] (X : C) :
    CartesianClosed.uncurry ((pre f).app X) = Limits.prod.map f (ðŸ™ _) â‰« (exp.ev A).app X := by
  rw [uncurry_eq, prod_map_pre_app_comp_ev]

theorem coev_app_comp_pre_app (f : B âŸ¶ A) [Exponentiable B] :
    (exp.coev A).app X â‰« (pre f).app (A â¨¯ X) =
      (exp.coev B).app X â‰« (exp B).map (Limits.prod.map f (ðŸ™ _)) :=
  unit_conjugateEquiv _ _ (prod.functor.map f) X

@[simp]
theorem pre_id (A : C) [Exponentiable A] : pre (ðŸ™ A) = ðŸ™ _ := by simp [pre]

@[simp]
theorem pre_map {Aâ‚ Aâ‚‚ Aâ‚ƒ : C} [Exponentiable Aâ‚] [Exponentiable Aâ‚‚] [Exponentiable Aâ‚ƒ]
    (f : Aâ‚ âŸ¶ Aâ‚‚) (g : Aâ‚‚ âŸ¶ Aâ‚ƒ) : pre (f â‰« g) = pre g â‰« pre f := by
  rw [pre, pre, pre, conjugateEquiv_comp, prod.functor.map_comp]

end Pre

/-- The internal hom functor given by the cartesian closed structure. -/
def internalHom [CartesianClosed C] : Cáµ’áµ– â¥¤ C â¥¤ C where
  obj X := exp X.unop
  map f := pre f.unop

/-- If an initial object `I` exists in a CCC, then `A â¨¯ I â‰… I`. -/
@[simps]
def zeroMul {I : C} (t : IsInitial I) : A â¨¯ I â‰… I where
  hom := Limits.prod.snd
  inv := t.to _
  hom_inv_id := by
    have : (prod.snd : A â¨¯ I âŸ¶ I) = CartesianClosed.uncurry (t.to _) := by
      rw [â† curry_eq_iff]
      apply t.hom_ext
    rw [this, â† uncurry_natural_right, â† eq_curry_iff]
    apply t.hom_ext
  inv_hom_id := t.hom_ext _ _

/-- If an initial object `0` exists in a CCC, then `0 â¨¯ A â‰… 0`. -/
def mulZero {I : C} (t : IsInitial I) : I â¨¯ A â‰… I :=
  Limits.prod.braiding _ _ â‰ªâ‰« zeroMul t

/-- If an initial object `0` exists in a CCC then `0^B â‰… 1` for any `B`. -/
def powZero {I : C} (t : IsInitial I) [CartesianClosed C] : I âŸ¹ B â‰… âŠ¤_ C where
  hom := default
  inv := CartesianClosed.curry ((mulZero t).hom â‰« t.to _)
  hom_inv_id := by
    rw [â† curry_natural_left, curry_eq_iff, â† cancel_epi (mulZero t).inv]
    apply t.hom_ext

-- TODO: Generalise the below to its commuted variants.
-- TODO: Define a distributive category, so that zero_mul and friends can be derived from this.
/-- In a CCC with binary coproducts, the distribution morphism is an isomorphism. -/
def prodCoprodDistrib [HasBinaryCoproducts C] [CartesianClosed C] (X Y Z : C) :
    (Z â¨¯ X) â¨¿ Z â¨¯ Y â‰… Z â¨¯ X â¨¿ Y where
  hom := coprod.desc (Limits.prod.map (ðŸ™ _) coprod.inl) (Limits.prod.map (ðŸ™ _) coprod.inr)
  inv :=
    CartesianClosed.uncurry
      (coprod.desc (CartesianClosed.curry coprod.inl) (CartesianClosed.curry coprod.inr))
  hom_inv_id := by
    ext
    Â· rw [coprod.inl_desc_assoc, comp_id, â† uncurry_natural_left, coprod.inl_desc, uncurry_curry]
    rw [coprod.inr_desc_assoc, comp_id, â† uncurry_natural_left, coprod.inr_desc, uncurry_curry]
  inv_hom_id := by
    rw [â† uncurry_natural_right, â† eq_curry_iff]
    ext
    Â· rw [coprod.inl_desc_assoc, â† curry_natural_right, coprod.inl_desc, â† curry_natural_left,
        comp_id]
    rw [coprod.inr_desc_assoc, â† curry_natural_right, coprod.inr_desc, â† curry_natural_left,
      comp_id]

/-- If an initial object `I` exists in a CCC then it is a strict initial object,
i.e. any morphism to `I` is an iso.
This actually shows a slightly stronger version: any morphism to an initial object from an
exponentiable object is an isomorphism.
-/
theorem strict_initial {I : C} (t : IsInitial I) (f : A âŸ¶ I) : IsIso f := by
  haveI : Mono f := by
    rw [â† prod.lift_snd (ðŸ™ A) f, â† zeroMul_hom t]
    exact mono_comp _ _
  haveI : IsSplitEpi f := IsSplitEpi.mk' âŸ¨t.to _, t.hom_ext _ _âŸ©
  apply isIso_of_mono_of_isSplitEpi

instance to_initial_isIso [HasInitial C] (f : A âŸ¶ âŠ¥_ C) : IsIso f :=
  strict_initial initialIsInitial _

/-- If an initial object `0` exists in a CCC then every morphism from it is monic. -/
theorem initial_mono {I : C} (B : C) (t : IsInitial I) [CartesianClosed C] : Mono (t.to B) :=
  âŸ¨fun g h _ => by
    haveI := strict_initial t g
    haveI := strict_initial t h
    exact eq_of_inv_eq_inv (t.hom_ext _ _)âŸ©

instance Initial.mono_to [HasInitial C] (B : C) [CartesianClosed C] : Mono (initial.to B) :=
  initial_mono B initialIsInitial

variable {D : Type uâ‚‚} [Category.{vâ‚‚} D]

section Functor

variable [HasFiniteProducts D]

/-- Transport the property of being cartesian closed across an equivalence of categories.

Note we didn't require any coherence between the choice of finite products here, since we transport
along the `prodComparison` isomorphism.
-/
def cartesianClosedOfEquiv (e : C â‰Œ D) [CartesianClosed C] : CartesianClosed D :=
  MonoidalClosed.ofEquiv (e.inverse.toMonoidalFunctorOfHasFiniteProducts) e.symm.toAdjunction

end Functor

attribute [nolint simpNF] CategoryTheory.CartesianClosed.homEquiv_apply_eq
  CategoryTheory.CartesianClosed.homEquiv_symm_apply_eq
end CategoryTheory
