/-
Copyright (c) 2021 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Terminal
import Mathlib.CategoryTheory.Limits.Constructions.FiniteProductsOfBinaryProducts
import Mathlib.CategoryTheory.Monad.Limits
import Mathlib.CategoryTheory.Adjunction.FullyFaithful
import Mathlib.CategoryTheory.Adjunction.Limits
import Mathlib.CategoryTheory.Adjunction.Reflective
import Mathlib.CategoryTheory.Closed.Cartesian
import Mathlib.CategoryTheory.Subterminal

/-!
# Exponential ideals

An exponential ideal of a Cartesian-closed category `C` is a subcategory `D ‚äÜ C` such that for any
`B : D` and `A : C`, the exponential `A ‚üπ B` is in `D`: resembling ring-theoretic ideals. We
define the notion here for inclusion functors `i : D ‚•§ C` rather than explicit subcategories to
preserve the principle of equivalence.

We additionally show that if `C` is Cartesian closed and `i : D ‚•§ C` is a reflective functor, the
following are equivalent.
* The left adjoint to `i` preserves binary (equivalently, finite) products.
* `i` is an exponential ideal.
-/


universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

noncomputable section

namespace CategoryTheory

open Category

section Ideal

variable {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} [Category.{v‚ÇÅ} C] [Category.{v‚ÇÅ} D] {i : D ‚•§ C}
variable (i) [CartesianMonoidalCategory C] [CartesianClosed C]

/-- The subcategory `D` of `C` expressed as an inclusion functor is an *exponential ideal* if
`B ‚àà D` implies `A ‚üπ B ‚àà D` for all `A`.
-/
class ExponentialIdeal : Prop where
  exp_closed : ‚àÄ {B}, i.essImage B ‚Üí ‚àÄ A, i.essImage (A ‚üπ B)
attribute [nolint docBlame] ExponentialIdeal.exp_closed

/-- To show `i` is an exponential ideal it suffices to show that `A ‚üπ iB` is "in" `D` for any `A` in
`C` and `B` in `D`.
-/
theorem ExponentialIdeal.mk' (h : ‚àÄ (B : D) (A : C), i.essImage (A ‚üπ i.obj B)) :
    ExponentialIdeal i :=
  ‚ü®fun hB A => by
    rcases hB with ‚ü®B', ‚ü®iB'‚ü©‚ü©
    exact Functor.essImage.ofIso ((exp A).mapIso iB') (h B' A)‚ü©

/-- The entire category viewed as a subcategory is an exponential ideal. -/
instance : ExponentialIdeal (ùü≠ C) :=
  ExponentialIdeal.mk' _ fun _ _ => ‚ü®_, ‚ü®Iso.refl _‚ü©‚ü©

open CartesianClosed

/-- The subcategory of subterminal objects is an exponential ideal. -/
instance : ExponentialIdeal (subterminalInclusion C) := by
  apply ExponentialIdeal.mk'
  intro B A
  refine ‚ü®‚ü®A ‚üπ B.1, fun Z g h => ?_‚ü©, ‚ü®Iso.refl _‚ü©‚ü©
  exact uncurry_injective (B.2 (CartesianClosed.uncurry g) (CartesianClosed.uncurry h))

/-- If `D` is a reflective subcategory, the property of being an exponential ideal is equivalent to
the presence of a natural isomorphism `i ‚ãô exp A ‚ãô leftAdjoint i ‚ãô i ‚âÖ i ‚ãô exp A`, that is:
`(A ‚üπ iB) ‚âÖ i L (A ‚üπ iB)`, naturally in `B`.
The converse is given in `ExponentialIdeal.mk_of_iso`.
-/
def exponentialIdealReflective (A : C) [Reflective i] [ExponentialIdeal i] :
    i ‚ãô exp A ‚ãô reflector i ‚ãô i ‚âÖ i ‚ãô exp A := by
  symm
  apply NatIso.ofComponents _ _
  ¬∑ intro X
    haveI := Functor.essImage.unit_isIso (ExponentialIdeal.exp_closed (i.obj_mem_essImage X) A)
    apply asIso ((reflectorAdjunction i).unit.app (A ‚üπ i.obj X))
  ¬∑ simp [asIso]

/-- Given a natural isomorphism `i ‚ãô exp A ‚ãô leftAdjoint i ‚ãô i ‚âÖ i ‚ãô exp A`, we can show `i`
is an exponential ideal.
-/
theorem ExponentialIdeal.mk_of_iso [Reflective i]
    (h : ‚àÄ A : C, i ‚ãô exp A ‚ãô reflector i ‚ãô i ‚âÖ i ‚ãô exp A) : ExponentialIdeal i := by
  apply ExponentialIdeal.mk'
  intro B A
  exact ‚ü®_, ‚ü®(h A).app B‚ü©‚ü©

end Ideal

section

variable {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} [Category.{v‚ÇÅ} C] [Category.{v‚ÇÅ} D]
variable (i : D ‚•§ C)

/- This cannot be a local instance since it has free variables,
it can instead be used as a have when needed.
We assume HasFiniteProducts D as a hypothesis below, to avoid making this a local instance.
-/
theorem reflective_products [Limits.HasFiniteProducts C] [Reflective i] :
    Limits.HasFiniteProducts D := ‚ü®fun _ => hasLimitsOfShape_of_reflective i‚ü©

open CartesianClosed MonoidalCategory CartesianMonoidalCategory

open Limits in
/-- Given a reflective subcategory `D` of a category with chosen finite products `C`, `D` admits
finite chosen products. -/
-- Note: This is not an instance as one might already have a (different) `CartesianMonoidalCategory`
-- instance on `D` (as for example with sheaves).
-- See note [reducible non instances]
abbrev CartesianMonoidalCategory.ofReflective [CartesianMonoidalCategory C] [Reflective i] :
    CartesianMonoidalCategory D :=
  .ofChosenFiniteProducts
    ({  cone := Limits.asEmptyCone <| (reflector i).obj (ùüô_ C)
        isLimit := by
          apply isLimitOfReflects i
          apply isLimitChangeEmptyCone _ isTerminalTensorUnit
          letI : IsIso ((reflectorAdjunction i).unit.app (ùüô_ C)) := by
            have := reflective_products i
            refine Functor.essImage.unit_isIso ‚ü®terminal D, ‚ü®PreservesTerminal.iso i |>.trans ?_‚ü©‚ü©
            exact IsLimit.conePointUniqueUpToIso (limit.isLimit _) isTerminalTensorUnit
          exact asIso ((reflectorAdjunction i).unit.app (ùüô_ C)) })
  fun X Y ‚Ü¶
    { cone := BinaryFan.mk
        ((reflector i).map (fst (i.obj X) (i.obj Y)) ‚â´ (reflectorAdjunction i).counit.app _)
        ((reflector i).map (snd (i.obj X) (i.obj Y)) ‚â´ (reflectorAdjunction i).counit.app _)
      isLimit := by
        apply isLimitOfReflects i
        apply IsLimit.equivOfNatIsoOfIso (pairComp X Y _) _ _ _|>.invFun
          (tensorProductIsBinaryProduct (i.obj X) (i.obj Y))
        fapply BinaryFan.ext
        ¬∑ change (reflector i ‚ãô i).obj (i.obj X ‚äó i.obj Y) ‚âÖ (ùü≠ C).obj (i.obj X ‚äó i.obj Y)
          letI : IsIso ((reflectorAdjunction i).unit.app (i.obj X ‚äó i.obj Y)) := by
            apply Functor.essImage.unit_isIso
            haveI := reflective_products i
            use Limits.prod X Y
            constructor
            apply Limits.PreservesLimitPair.iso i _ _|>.trans
            refine Limits.IsLimit.conePointUniqueUpToIso (limit.isLimit (pair (i.obj X) (i.obj Y)))
              (tensorProductIsBinaryProduct _ _)
          exact asIso ((reflectorAdjunction i).unit.app (i.obj X ‚äó i.obj Y))|>.symm
        ¬∑ simp only [BinaryFan.fst, Cones.postcompose, pairComp]
          simp [‚Üê Functor.comp_map, ‚Üê NatTrans.naturality_assoc]
        ¬∑ simp only [BinaryFan.snd, Cones.postcompose, pairComp]
          simp [‚Üê Functor.comp_map, ‚Üê NatTrans.naturality_assoc] }

@[deprecated (since := "2025-05-15")]
noncomputable alias reflectiveChosenFiniteProducts := CartesianMonoidalCategory.ofReflective

variable [CartesianMonoidalCategory C] [Reflective i] [CartesianClosed C]
  [CartesianMonoidalCategory D]

/-- If the reflector preserves binary products, the subcategory is an exponential ideal.
This is the converse of `preservesBinaryProductsOfExponentialIdeal`.
-/
instance (priority := 10) exponentialIdeal_of_preservesBinaryProducts
    [Limits.PreservesLimitsOfShape (Discrete Limits.WalkingPair) (reflector i)] :
    ExponentialIdeal i := by
  let ir := reflectorAdjunction i
  let L : C ‚•§ D := reflector i
  let Œ∑ : ùü≠ C ‚ü∂ L ‚ãô i := ir.unit
  let Œµ : i ‚ãô L ‚ü∂ ùü≠ D := ir.counit
  apply ExponentialIdeal.mk'
  intro B A
  let q : i.obj (L.obj (A ‚üπ i.obj B)) ‚ü∂ A ‚üπ i.obj B := by
    apply CartesianClosed.curry (ir.homEquiv _ _ _)
    apply _ ‚â´ (ir.homEquiv _ _).symm ((exp.ev A).app (i.obj B))
    exact prodComparison L A _ ‚â´ (_ ‚óÅ (Œµ.app _)) ‚â´ inv (prodComparison _ _ _)
  have : Œ∑.app (A ‚üπ i.obj B) ‚â´ q = ùüô (A ‚üπ i.obj B) := by
    dsimp
    rw [‚Üê curry_natural_left, curry_eq_iff, uncurry_id_eq_ev, ‚Üê ir.homEquiv_naturality_left,
      ir.homEquiv_apply_eq, assoc, assoc, prodComparison_natural_whiskerLeft_assoc,
      ‚Üê whiskerLeft_comp_assoc, ir.left_triangle_components, whiskerLeft_id, id_comp]
    apply IsIso.hom_inv_id_assoc
  haveI : IsSplitMono (Œ∑.app (A ‚üπ i.obj B)) := IsSplitMono.mk' ‚ü®_, this‚ü©
  apply mem_essImage_of_unit_isSplitMono

variable [ExponentialIdeal i]

/-- If `i` witnesses that `D` is a reflective subcategory and an exponential ideal, then `D` is
itself Cartesian closed.
-/
def cartesianClosedOfReflective : CartesianClosed D where
  closed := fun B =>
    { rightAdj := i ‚ãô exp (i.obj B) ‚ãô reflector i
      adj := by
        apply (exp.adjunction (i.obj B)).restrictFullyFaithful i.fullyFaithfulOfReflective
          i.fullyFaithfulOfReflective
        ¬∑ symm
          refine NatIso.ofComponents (fun X => ?_) (fun f => ?_)
          ¬∑ haveI :=
              Adjunction.rightAdjoint_preservesLimits.{0, 0} (reflectorAdjunction i)
            apply asIso (prodComparison i B X)
          ¬∑ dsimp [asIso]
            rw [prodComparison_natural_whiskerLeft]
        ¬∑ apply (exponentialIdealReflective i _).symm }

variable [BraidedCategory C]

/-- We construct a bijection between morphisms `L(A ‚äó B) ‚ü∂ X` and morphisms `LA ‚äó LB ‚ü∂ X`.
This bijection has two key properties:
* It is natural in `X`: See `bijection_natural`.
* When `X = LA ‚®Ø LB`, then the backwards direction sends the identity morphism to the product
  comparison morphism: See `bijection_symm_apply_id`.

Together these help show that `L` preserves binary products. This should be considered
*internal implementation* towards `preservesBinaryProductsOfExponentialIdeal`.
-/
noncomputable def bijection (A B : C) (X : D) :
    ((reflector i).obj (A ‚äó B) ‚ü∂ X) ‚âÉ ((reflector i).obj A ‚äó (reflector i).obj B ‚ü∂ X) :=
  calc
    _ ‚âÉ (A ‚äó B ‚ü∂ i.obj X) := (reflectorAdjunction i).homEquiv _ _
    _ ‚âÉ (B ‚äó A ‚ü∂ i.obj X) := (Œ≤_ _ _).homCongr (Iso.refl _)
    _ ‚âÉ (A ‚ü∂ B ‚üπ i.obj X) := (exp.adjunction _).homEquiv _ _
    _ ‚âÉ (i.obj ((reflector i).obj A) ‚ü∂ B ‚üπ i.obj X) :=
      (unitCompPartialBijective _ (ExponentialIdeal.exp_closed (i.obj_mem_essImage _) _))
    _ ‚âÉ (B ‚äó i.obj ((reflector i).obj A) ‚ü∂ i.obj X) := ((exp.adjunction _).homEquiv _ _).symm
    _ ‚âÉ (i.obj ((reflector i).obj A) ‚äó B ‚ü∂ i.obj X) :=
      ((Œ≤_ _ _).homCongr (Iso.refl _))
    _ ‚âÉ (B ‚ü∂ i.obj ((reflector i).obj A) ‚üπ i.obj X) := (exp.adjunction _).homEquiv _ _
    _ ‚âÉ (i.obj ((reflector i).obj B) ‚ü∂ i.obj ((reflector i).obj A) ‚üπ i.obj X) :=
      (unitCompPartialBijective _ (ExponentialIdeal.exp_closed (i.obj_mem_essImage _) _))
    _ ‚âÉ (i.obj ((reflector i).obj A) ‚äó i.obj ((reflector i).obj B) ‚ü∂ i.obj X) :=
      ((exp.adjunction _).homEquiv _ _).symm
    _ ‚âÉ (i.obj ((reflector i).obj A ‚äó (reflector i).obj B) ‚ü∂ i.obj X) :=
      haveI : Limits.PreservesLimits i := (reflectorAdjunction i).rightAdjoint_preservesLimits
      haveI := Limits.preservesSmallestLimits_of_preservesLimits i
      Iso.homCongr (prodComparisonIso _ _ _).symm (Iso.refl (i.obj X))
    _ ‚âÉ ((reflector i).obj A ‚äó (reflector i).obj B ‚ü∂ X) :=
      i.fullyFaithfulOfReflective.homEquiv.symm

theorem bijection_symm_apply_id (A B : C) :
    (bijection i A B _).symm (ùüô _) = prodComparison _ _ _ := by
  simp only [bijection, Equiv.trans_def, curriedTensor_obj_obj, Equiv.symm_trans_apply,
    Equiv.symm_symm, Functor.FullyFaithful.homEquiv_apply, Functor.map_id, Iso.homCongr_symm,
    Iso.symm_symm_eq, Iso.refl_symm, Iso.homCongr_apply, Iso.refl_hom, comp_id,
    unitCompPartialBijective_symm_apply, Functor.id_obj, Functor.comp_obj, Iso.symm_inv]
  -- Porting note: added
  erw [homEquiv_symm_apply_eq, homEquiv_symm_apply_eq, homEquiv_apply_eq, homEquiv_apply_eq]
  rw [uncurry_natural_left, uncurry_curry, uncurry_natural_left, uncurry_curry,
    ‚Üê BraidedCategory.braiding_naturality_left_assoc, SymmetricCategory.symmetry_assoc,
    ‚Üê MonoidalCategory.whisker_exchange_assoc, ‚Üê tensorHom_def'_assoc,
    Adjunction.homEquiv_symm_apply, ‚Üê Adjunction.eq_unit_comp_map_iff, Iso.comp_inv_eq, assoc,
    prodComparisonIso_hom i ((reflector i).obj A) ((reflector i).obj B)]
  apply hom_ext
  ¬∑ rw [tensorHom_fst, assoc, assoc, prodComparison_fst, ‚Üê i.map_comp,
    prodComparison_fst]
    apply (reflectorAdjunction i).unit.naturality
  ¬∑ rw [tensorHom_snd, assoc, assoc, prodComparison_snd, ‚Üê i.map_comp,
    prodComparison_snd]
    apply (reflectorAdjunction i).unit.naturality

theorem bijection_natural (A B : C) (X X' : D) (f : (reflector i).obj (A ‚äó B) ‚ü∂ X) (g : X ‚ü∂ X') :
    bijection i _ _ _ (f ‚â´ g) = bijection i _ _ _ f ‚â´ g := by
  dsimp [bijection]
  -- Porting note: added
  erw [homEquiv_symm_apply_eq, homEquiv_symm_apply_eq, homEquiv_apply_eq, homEquiv_apply_eq,
    homEquiv_symm_apply_eq, homEquiv_symm_apply_eq, homEquiv_apply_eq, homEquiv_apply_eq]
  apply i.map_injective
  rw [Functor.FullyFaithful.map_preimage, i.map_comp,
    Adjunction.homEquiv_unit, Adjunction.homEquiv_unit]
  simp only [comp_id, Functor.map_comp, Functor.FullyFaithful.map_preimage, assoc]
  rw [‚Üê assoc, ‚Üê assoc, curry_natural_right _ (i.map g),
    unitCompPartialBijective_natural, uncurry_natural_right, ‚Üê assoc, curry_natural_right,
    unitCompPartialBijective_natural, uncurry_natural_right, assoc]

/--
The bijection allows us to show that `prodComparison L A B` is an isomorphism, where the inverse
is the forward map of the identity morphism.
-/
theorem prodComparison_iso (A B : C) : IsIso
    (prodComparison (reflector i) A B) :=
  ‚ü®‚ü®bijection i _ _ _ (ùüô _), by
      rw [‚Üê (bijection i _ _ _).injective.eq_iff, bijection_natural, ‚Üê bijection_symm_apply_id,
        Equiv.apply_symm_apply, id_comp],
      by rw [‚Üê bijection_natural, id_comp, ‚Üê bijection_symm_apply_id, Equiv.apply_symm_apply]‚ü©‚ü©

attribute [local instance] prodComparison_iso

open Limits

/--
If a reflective subcategory is an exponential ideal, then the reflector preserves binary products.
This is the converse of `exponentialIdeal_of_preserves_binary_products`.
-/
lemma preservesBinaryProducts_of_exponentialIdeal :
    PreservesLimitsOfShape (Discrete WalkingPair) (reflector i) where
  preservesLimit {K} :=
    letI := preservesLimit_pair_of_isIso_prodComparison
      (reflector i) (K.obj ‚ü®WalkingPair.left‚ü©) (K.obj ‚ü®WalkingPair.right‚ü©)
    Limits.preservesLimit_of_iso_diagram _ (diagramIsoPair K).symm

/--
If a reflective subcategory is an exponential ideal, then the reflector preserves finite products.
-/
lemma Limits.PreservesFiniteProducts.of_exponentialIdeal : PreservesFiniteProducts (reflector i) :=
  have := preservesBinaryProducts_of_exponentialIdeal i
  have : PreservesLimitsOfShape _ (reflector i) := leftAdjoint_preservesTerminal_of_reflective.{0} i
  .of_preserves_binary_and_terminal _

@[deprecated (since := "2025-04-22")]
alias preservesFiniteProducts_of_exponentialIdeal := PreservesFiniteProducts.of_exponentialIdeal

end

end CategoryTheory
