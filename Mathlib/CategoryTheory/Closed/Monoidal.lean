/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Bhavik Mehta, Daniel Carranza, JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Monoidal.Functor
import Mathlib.CategoryTheory.Monoidal.CoherenceLemmas
import Mathlib.CategoryTheory.Adjunction.Limits
import Mathlib.CategoryTheory.Adjunction.Mates
import Mathlib.CategoryTheory.Adjunction.Parametrized

/-!
# Closed monoidal categories

Define (right) closed objects and (right) closed monoidal categories.

## TODO
Some of the theorems proved about cartesian closed categories
should be generalised and moved to this file.
-/


universe v u uâ‚‚ vâ‚‚

namespace CategoryTheory

open Category MonoidalCategory

-- Note that this class carries a particular choice of right adjoint,
-- (which is only unique up to isomorphism),
-- not merely the existence of such, and
-- so definitional properties of instances may be important.
/-- An object `X` is (right) closed if `(X âŠ— -)` is a left adjoint. -/
class Closed {C : Type u} [Category.{v} C] [MonoidalCategory.{v} C] (X : C) where
  /-- a choice of a right adjoint for `tensorLeft X` -/
  rightAdj : C â¥¤ C
  /-- `tensorLeft X` is a left adjoint -/
  adj : tensorLeft X âŠ£ rightAdj

/-- A monoidal category `C` is (right) monoidal closed if every object is (right) closed. -/
class MonoidalClosed (C : Type u) [Category.{v} C] [MonoidalCategory.{v} C] where
  closed (X : C) : Closed X := by infer_instance

attribute [instance 100] MonoidalClosed.closed

variable {C : Type u} [Category.{v} C] [MonoidalCategory.{v} C]

/-- If `X` and `Y` are closed then `X âŠ— Y` is.
This isn't an instance because it's not usually how we want to construct internal homs,
we'll usually prove all objects are closed uniformly.
-/
def tensorClosed {X Y : C} (hX : Closed X) (hY : Closed Y) : Closed (X âŠ— Y) where
  rightAdj := Closed.rightAdj X â‹™ Closed.rightAdj Y
  adj := (hY.adj.comp hX.adj).ofNatIsoLeft (MonoidalCategory.tensorLeftTensor X Y).symm

/-- The unit object is always closed.
This isn't an instance because most of the time we'll prove closedness for all objects at once,
rather than just for this one.
-/
def unitClosed : Closed (ğŸ™_ C) where
  rightAdj := ğŸ­ C
  adj := Adjunction.id.ofNatIsoLeft (MonoidalCategory.leftUnitorNatIso C).symm

variable (A B : C) {X X' Y Y' Z : C}
variable [Closed A]

/-- This is the internal hom `A âŸ¶[C] -`.
-/
def ihom : C â¥¤ C :=
  Closed.rightAdj (X := A)

namespace ihom

/-- The adjunction between `A âŠ— -` and `A âŸ¹ -`. -/
def adjunction : tensorLeft A âŠ£ ihom A :=
  Closed.adj

instance : (tensorLeft A).IsLeftAdjoint  :=
  (ihom.adjunction A).isLeftAdjoint

/-- The evaluation natural transformation. -/
def ev : ihom A â‹™ tensorLeft A âŸ¶ ğŸ­ C :=
  (ihom.adjunction A).counit

/-- The coevaluation natural transformation. -/
def coev : ğŸ­ C âŸ¶ tensorLeft A â‹™ ihom A :=
  (ihom.adjunction A).unit

@[simp]
theorem ihom_adjunction_counit : (ihom.adjunction A).counit = ev A :=
  rfl

@[simp]
theorem ihom_adjunction_unit : (ihom.adjunction A).unit = coev A :=
  rfl

@[reassoc (attr := simp)]
theorem ev_naturality {X Y : C} (f : X âŸ¶ Y) :
    A â— (ihom A).map f â‰« (ev A).app Y = (ev A).app X â‰« f :=
  (ev A).naturality f

@[reassoc (attr := simp)]
theorem coev_naturality {X Y : C} (f : X âŸ¶ Y) :
    f â‰« (coev A).app Y = (coev A).app X â‰« (ihom A).map (A â— f) :=
  (coev A).naturality f

set_option quotPrecheck false in
/-- `A âŸ¶[C] B` denotes the internal hom from `A` to `B` -/
notation A " âŸ¶[" C "] " B:10 => (@ihom C _ _ A _).obj B

@[reassoc (attr := simp)]
theorem ev_coev : (A â— (coev A).app B) â‰« (ev A).app (A âŠ— B) = ğŸ™ (A âŠ— B) :=
  (ihom.adjunction A).left_triangle_components _

@[reassoc (attr := simp)]
theorem coev_ev : (coev A).app (A âŸ¶[C] B) â‰« (ihom A).map ((ev A).app B) = ğŸ™ (A âŸ¶[C] B) :=
  Adjunction.right_triangle_components (ihom.adjunction A) _

end ihom

open CategoryTheory.Limits

instance : PreservesColimits (tensorLeft A) :=
  (ihom.adjunction A).leftAdjoint_preservesColimits

variable {A}

-- Wrap these in a namespace so we don't clash with the core versions.
namespace MonoidalClosed

/-- Currying in a monoidal closed category. -/
def curry : (A âŠ— Y âŸ¶ X) â†’ (Y âŸ¶ A âŸ¶[C] X) :=
  (ihom.adjunction A).homEquiv _ _

/-- Uncurrying in a monoidal closed category. -/
def uncurry : (Y âŸ¶ A âŸ¶[C] X) â†’ (A âŠ— Y âŸ¶ X) :=
  ((ihom.adjunction A).homEquiv _ _).symm

theorem homEquiv_apply_eq (f : A âŠ— Y âŸ¶ X) : (ihom.adjunction A).homEquiv _ _ f = curry f :=
  rfl

theorem homEquiv_symm_apply_eq (f : Y âŸ¶ A âŸ¶[C] X) :
    ((ihom.adjunction A).homEquiv _ _).symm f = uncurry f :=
  rfl

@[reassoc]
theorem curry_natural_left (f : X âŸ¶ X') (g : A âŠ— X' âŸ¶ Y) : curry (_ â— f â‰« g) = f â‰« curry g :=
  Adjunction.homEquiv_naturality_left _ _ _

@[reassoc]
theorem curry_natural_right (f : A âŠ— X âŸ¶ Y) (g : Y âŸ¶ Y') :
    curry (f â‰« g) = curry f â‰« (ihom _).map g :=
  Adjunction.homEquiv_naturality_right _ _ _

@[reassoc]
theorem uncurry_natural_right (f : X âŸ¶ A âŸ¶[C] Y) (g : Y âŸ¶ Y') :
    uncurry (f â‰« (ihom _).map g) = uncurry f â‰« g :=
  Adjunction.homEquiv_naturality_right_symm _ _ _

@[reassoc]
theorem uncurry_natural_left (f : X âŸ¶ X') (g : X' âŸ¶ A âŸ¶[C] Y) :
    uncurry (f â‰« g) = _ â— f â‰« uncurry g :=
  Adjunction.homEquiv_naturality_left_symm _ _ _

@[simp]
theorem uncurry_curry (f : A âŠ— X âŸ¶ Y) : uncurry (curry f) = f :=
  (Closed.adj.homEquiv _ _).left_inv f

@[simp]
theorem curry_uncurry (f : X âŸ¶ A âŸ¶[C] Y) : curry (uncurry f) = f :=
  (Closed.adj.homEquiv _ _).right_inv f

theorem curry_eq_iff (f : A âŠ— Y âŸ¶ X) (g : Y âŸ¶ A âŸ¶[C] X) : curry f = g â†” f = uncurry g :=
  Adjunction.homEquiv_apply_eq (ihom.adjunction A) f g

theorem eq_curry_iff (f : A âŠ— Y âŸ¶ X) (g : Y âŸ¶ A âŸ¶[C] X) : g = curry f â†” uncurry g = f :=
  Adjunction.eq_homEquiv_apply (ihom.adjunction A) f g

-- I don't think these two should be simp.
theorem uncurry_eq (g : Y âŸ¶ A âŸ¶[C] X) : uncurry g = (A â— g) â‰« (ihom.ev A).app X := by
  rfl

theorem curry_eq (g : A âŠ— Y âŸ¶ X) : curry g = (ihom.coev A).app Y â‰« (ihom A).map g :=
  rfl

theorem curry_injective : Function.Injective (curry : (A âŠ— Y âŸ¶ X) â†’ (Y âŸ¶ A âŸ¶[C] X)) :=
  (Closed.adj.homEquiv _ _).injective

theorem uncurry_injective : Function.Injective (uncurry : (Y âŸ¶ A âŸ¶[C] X) â†’ (A âŠ— Y âŸ¶ X)) :=
  (Closed.adj.homEquiv _ _).symm.injective

variable (A X)

theorem uncurry_id_eq_ev : uncurry (ğŸ™ (A âŸ¶[C] X)) = (ihom.ev A).app X := by
  simp [uncurry_eq]

theorem curry_id_eq_coev : curry (ğŸ™ _) = (ihom.coev A).app X := by
  rw [curry_eq, (ihom A).map_id (A âŠ— _)]
  apply comp_id

@[reassoc (attr := simp)]
lemma whiskerLeft_curry_ihom_ev_app (g : A âŠ— Y âŸ¶ X) :
    A â— curry g â‰« (ihom.ev A).app X = g := by
  simp [curry_eq]

theorem uncurry_ihom_map (g : Y âŸ¶ Y') :
    uncurry ((ihom A).map g) = (ihom.ev A).app Y â‰« g := by
  apply curry_injective
  rw [curry_uncurry, curry_natural_right, â† uncurry_id_eq_ev, curry_uncurry, id_comp]

/-- The internal hom out of the unit is naturally isomorphic to the identity functor. -/
def unitNatIso [Closed (ğŸ™_ C)] : ğŸ­ C â‰… ihom (ğŸ™_ C) :=
  conjugateIsoEquiv (Adjunction.id (C := C)) (ihom.adjunction (ğŸ™_ C))
    (leftUnitorNatIso C)
section Pre

variable {A B}
variable [Closed B]

/-- Pre-compose an internal hom with an external hom. -/
def pre (f : B âŸ¶ A) : ihom A âŸ¶ ihom B :=
  conjugateEquiv (ihom.adjunction _) (ihom.adjunction _) ((tensoringLeft C).map f)

@[reassoc (attr := simp)]
theorem id_tensor_pre_app_comp_ev (f : B âŸ¶ A) (X : C) :
    B â— (pre f).app X â‰« (ihom.ev B).app X = f â–· (A âŸ¶[C] X) â‰« (ihom.ev A).app X :=
  conjugateEquiv_counit _ _ ((tensoringLeft C).map f) X

@[simp]
theorem uncurry_pre (f : B âŸ¶ A) (X : C) :
    MonoidalClosed.uncurry ((pre f).app X) = f â–· _ â‰« (ihom.ev A).app X := by
  simp [uncurry_eq]

@[reassoc]
lemma curry_pre_app (f : B âŸ¶ A) {X Y : C} (g : A âŠ— Y âŸ¶ X) :
    curry g â‰« (pre f).app X = curry (f â–· _ â‰« g) := uncurry_injective (by
  rw [uncurry_curry, uncurry_eq, MonoidalCategory.whiskerLeft_comp, assoc,
    id_tensor_pre_app_comp_ev, whisker_exchange_assoc, whiskerLeft_curry_ihom_ev_app])

@[reassoc (attr := simp)]
theorem coev_app_comp_pre_app (f : B âŸ¶ A) :
    (ihom.coev A).app X â‰« (pre f).app (A âŠ— X) = (ihom.coev B).app X â‰« (ihom B).map (f â–· _) :=
  unit_conjugateEquiv _ _ ((tensoringLeft C).map f) X

@[reassoc]
lemma uncurry_pre_app (f : Y âŸ¶ A âŸ¶[C] X) (g : B âŸ¶ A) :
    uncurry (f â‰« (pre g).app X) = g â–· _ â‰« uncurry f :=
  curry_injective (by
    rw [curry_uncurry, â† curry_pre_app, curry_uncurry])

@[simp]
theorem pre_id (A : C) [Closed A] : pre (ğŸ™ A) = ğŸ™ _ := by
  rw [pre, Functor.map_id]
  apply conjugateEquiv_id

@[simp]
theorem pre_map {Aâ‚ Aâ‚‚ Aâ‚ƒ : C} [Closed Aâ‚] [Closed Aâ‚‚] [Closed Aâ‚ƒ] (f : Aâ‚ âŸ¶ Aâ‚‚) (g : Aâ‚‚ âŸ¶ Aâ‚ƒ) :
    pre (f â‰« g) = pre g â‰« pre f := by
  rw [pre, pre, pre, conjugateEquiv_comp, (tensoringLeft C).map_comp]

theorem pre_comm_ihom_map {W X Y Z : C} [Closed W] [Closed X] (f : W âŸ¶ X) (g : Y âŸ¶ Z) :
    (pre f).app Y â‰« (ihom W).map g = (ihom X).map g â‰« (pre f).app Z := by simp

end Pre

/-- The internal hom functor given by the monoidal closed structure. -/
@[simps]
def internalHom [MonoidalClosed C] : Cáµ’áµ– â¥¤ C â¥¤ C where
  obj X := ihom X.unop
  map f := pre f.unop

/-- The parametrized adjunction between `curriedTensor C : C â¥¤ C â¥¤ C`
and `internalHom : Cáµ’áµ– â¥¤ C â¥¤ C` -/
@[simps!]
def internalHomAdjunctionâ‚‚ [MonoidalClosed C] :
    curriedTensor C âŠ£â‚‚ internalHom where
  adj _ := ihom.adjunction _

section OfEquiv

variable {D : Type uâ‚‚} [Category.{vâ‚‚} D] [MonoidalCategory.{vâ‚‚} D]

variable (F : C â¥¤ D) {G : D â¥¤ C} (adj : F âŠ£ G)
  [F.Monoidal] [F.IsEquivalence] [MonoidalClosed D]

/-- Transport the property of being monoidal closed across a monoidal equivalence of categories -/
noncomputable def ofEquiv : MonoidalClosed C where
  closed X :=
    { rightAdj := F â‹™ ihom (F.obj X) â‹™ G
      adj := (adj.comp ((ihom.adjunction (F.obj X)).comp
          adj.toEquivalence.symm.toAdjunction)).ofNatIsoLeft
            (Iso.compInverseIso (H := adj.toEquivalence) (Functor.Monoidal.commTensorLeft F X)) }

/-- Suppose we have a monoidal equivalence `F : C â‰Œ D`, with `D` monoidal closed. We can pull the
monoidal closed instance back along the equivalence. For `X, Y, Z : C`, this lemma describes the
resulting currying map `Hom(X âŠ— Y, Z) â†’ Hom(Y, (X âŸ¶[C] Z))`. (`X âŸ¶[C] Z` is defined to be
`Fâ»Â¹(F(X) âŸ¶[D] F(Z))`, so currying in `C` is given by essentially conjugating currying in
`D` by `F.`) -/
theorem ofEquiv_curry_def {X Y Z : C} (f : X âŠ— Y âŸ¶ Z) :
    letI := ofEquiv F adj
    MonoidalClosed.curry f =
      adj.homEquiv Y ((ihom (F.obj X)).obj (F.obj Z))
        (MonoidalClosed.curry (adj.toEquivalence.symm.toAdjunction.homEquiv (F.obj X âŠ— F.obj Y) Z
        ((Iso.compInverseIso (H := adj.toEquivalence)
          (Functor.Monoidal.commTensorLeft F X)).hom.app Y â‰« f))) := by
  -- This whole proof used to be `rfl` before https://github.com/leanprover-community/mathlib4/pull/16317.
  change ((adj.comp ((ihom.adjunction (F.obj X)).comp
      adj.toEquivalence.symm.toAdjunction)).ofNatIsoLeft _).homEquiv _ _ _ = _
  dsimp only [Adjunction.ofNatIsoLeft]
  rw [Adjunction.mkOfHomEquiv_homEquiv]
  dsimp
  rw [Adjunction.comp_homEquiv, Adjunction.comp_homEquiv]
  rfl

/-- Suppose we have a monoidal equivalence `F : C â‰Œ D`, with `D` monoidal closed. We can pull the
monoidal closed instance back along the equivalence. For `X, Y, Z : C`, this lemma describes the
resulting uncurrying map `Hom(Y, (X âŸ¶[C] Z)) â†’ Hom(X âŠ— Y âŸ¶ Z)`. (`X âŸ¶[C] Z` is
defined to be `Fâ»Â¹(F(X) âŸ¶[D] F(Z))`, so uncurrying in `C` is given by essentially conjugating
uncurrying in `D` by `F.`) -/
theorem ofEquiv_uncurry_def {X Y Z : C} :
    letI := ofEquiv F adj
    âˆ€ (f : Y âŸ¶ (ihom X).obj Z), MonoidalClosed.uncurry f =
      ((Iso.compInverseIso (H := adj.toEquivalence)
          (Functor.Monoidal.commTensorLeft F X)).inv.app Y) â‰«
            (adj.toEquivalence.symm.toAdjunction.homEquiv _ _).symm
              (MonoidalClosed.uncurry ((adj.homEquiv _ _).symm f)) := by
  intro f
  -- This whole proof used to be `rfl` before https://github.com/leanprover-community/mathlib4/pull/16317.
  change (((adj.comp ((ihom.adjunction (F.obj X)).comp
      adj.toEquivalence.symm.toAdjunction)).ofNatIsoLeft _).homEquiv _ _).symm _ = _
  dsimp only [Adjunction.ofNatIsoLeft]
  rw [Adjunction.mkOfHomEquiv_homEquiv]
  dsimp
  rw [Adjunction.comp_homEquiv, Adjunction.comp_homEquiv]
  rfl

end OfEquiv

-- A closed monoidal category C is always enriched over itself.
-- This section contains the necessary definitions and equalities to endow C with
-- the structure of a C-category, while the instance itself is defined in `Closed/Enrichment`.
-- In particular, we only assume the necessary instances of `Closed x`, rather than assuming
-- C comes with an instance of `MonoidalClosed`
section Enriched

/-- The C-identity morphism
  `ğŸ™_ C âŸ¶ hom(x, x)`
used to equip `C` with the structure of a `C`-category -/
def id (x : C) [Closed x] : ğŸ™_ C âŸ¶ (ihom x).obj x := curry (Ï_ x).hom

/-- The *uncurried* composition morphism
  `x âŠ— (hom(x, y) âŠ— hom(y, z)) âŸ¶ (x âŠ— hom(x, y)) âŠ— hom(y, z) âŸ¶ y âŠ— hom(y, z) âŸ¶ z`.
The `C`-composition morphism will be defined as the adjoint transpose of this map. -/
def compTranspose (x y z : C) [Closed x] [Closed y] : x âŠ— (ihom x).obj y âŠ— (ihom y).obj z âŸ¶ z :=
  (Î±_ x ((ihom x).obj y) ((ihom y).obj z)).inv â‰«
    (ihom.ev x).app y â–· ((ihom y).obj z) â‰« (ihom.ev y).app z

/-- The `C`-composition morphism
  `hom(x, y) âŠ— hom(y, z) âŸ¶ hom(x, z)`
used to equip `C` with the structure of a `C`-category -/
def comp (x y z : C) [Closed x] [Closed y] : (ihom x).obj y âŠ— (ihom y).obj z âŸ¶ (ihom x).obj z :=
  curry (compTranspose x y z)

/-- Unfold the definition of `id`.
This exists to streamline the proofs of `MonoidalClosed.id_comp` and `MonoidalClosed.comp_id` -/
lemma id_eq (x : C) [Closed x] : id x = curry (Ï_ x).hom := rfl

/-- Unfold the definition of `compTranspose`.
This exists to streamline the proof of `MonoidalClosed.assoc` -/
lemma compTranspose_eq (x y z : C) [Closed x] [Closed y] :
    compTranspose x y z = (Î±_ _ _ _).inv â‰« (ihom.ev x).app y â–· _ â‰« (ihom.ev y).app z :=
  rfl

/-- Unfold the definition of `comp`.
This exists to streamline the proof of `MonoidalClosed.assoc` -/
lemma comp_eq (x y z : C) [Closed x] [Closed y] : comp x y z = curry (compTranspose x y z) := rfl

/-!
The proofs of associativity and unitality use the following outline:
  1. Take adjoint transpose on each side of the equality (uncurry_injective)
  2. Do whatever rewrites/simps are necessary to apply uncurry_curry
  3. Conclude with simp
-/

/-- Left unitality of the enriched structure -/
@[reassoc (attr := simp)]
lemma id_comp (x y : C) [Closed x] :
    (Î»_ ((ihom x).obj y)).inv â‰« id x â–· _ â‰« comp x x y = ğŸ™ _:= by
  apply uncurry_injective
  rw [uncurry_natural_left, uncurry_natural_left, comp_eq, uncurry_curry, id_eq, compTranspose_eq,
      associator_inv_naturality_middle_assoc, â† comp_whiskerRight_assoc, â† uncurry_eq,
      uncurry_curry, triangle_assoc_comp_right_assoc, whiskerLeft_inv_hom_assoc,
      uncurry_id_eq_ev _ _]

/-- Right unitality of the enriched structure -/
@[reassoc (attr := simp)]
lemma comp_id (x y : C) [Closed x] [Closed y] :
    (Ï_ ((ihom x).obj y)).inv â‰« _ â— id y â‰« comp x y y = ğŸ™ _ := by
  apply uncurry_injective
  rw [uncurry_natural_left, uncurry_natural_left, comp_eq, uncurry_curry, compTranspose_eq,
    associator_inv_naturality_right_assoc, â† rightUnitor_tensor_inv_assoc,
    whisker_exchange_assoc, â† rightUnitor_inv_naturality_assoc, â† uncurry_id_eq_ev y y]
  simp only [Functor.id_obj]
  rw [â† uncurry_natural_left]
  simp [id_eq, uncurry_id_eq_ev]

/-- Associativity of the enriched structure -/
@[reassoc]
lemma assoc (w x y z : C) [Closed w] [Closed x] [Closed y] :
    (Î±_ _ _ _).inv â‰« comp w x y â–· _ â‰« comp w y z = _ â— comp x y z â‰« comp w x z := by
  apply uncurry_injective
  simp only [uncurry_natural_left, comp_eq]
  rw [uncurry_curry, uncurry_curry]; simp only [compTranspose_eq, Category.assoc]
  rw [associator_inv_naturality_middle_assoc, â† comp_whiskerRight_assoc]; dsimp
  rw [â† uncurry_eq, uncurry_curry, associator_inv_naturality_right_assoc, whisker_exchange_assoc,
    â† uncurry_eq, uncurry_curry]
  simp

end Enriched

section OrdinaryEnriched

/-- The morphism `ğŸ™_ C âŸ¶ (ihom X).obj Y` corresponding to a morphism `X âŸ¶ Y`. -/
def curry' {X Y : C} [Closed X] (f : X âŸ¶ Y) : ğŸ™_ C âŸ¶ (ihom X).obj Y :=
  curry ((Ï_ _).hom â‰« f)

/-- The morphism `X âŸ¶ Y` corresponding to a morphism `ğŸ™_ C âŸ¶ (ihom X).obj Y`. -/
def uncurry' {X Y : C} [Closed X] (g : ğŸ™_ C âŸ¶ (ihom X).obj Y) : X âŸ¶ Y :=
  (Ï_ _).inv â‰« uncurry g

/-- `curry'` and `uncurry`' are inverse bijections. -/
@[simp]
lemma curry'_uncurry' {X Y : C} [Closed X] (g : ğŸ™_ C âŸ¶ (ihom X).obj Y) :
    curry' (uncurry' g) = g := by
  simp [curry', uncurry']

/-- `curry'` and `uncurry`' are inverse bijections. -/
@[simp]
lemma uncurry'_curry' {X Y : C} [Closed X] (f : X âŸ¶ Y) :
    uncurry' (curry' f) = f := by
  simp [curry', uncurry']

/-- The bijection `(X âŸ¶ Y) â‰ƒ (ğŸ™_ C âŸ¶ (ihom X).obj Y)` in a monoidal closed category. -/
@[simps]
def curryHomEquiv' {X Y : C} [Closed X] :
    (X âŸ¶ Y) â‰ƒ (ğŸ™_ C âŸ¶ (ihom X).obj Y) where
  toFun := curry'
  invFun := uncurry'
  left_inv _ := by simp
  right_inv _ := by simp

lemma curry'_injective {X Y : C} [Closed X] {f f' : X âŸ¶ Y} (h : curry' f = curry' f') :
    f = f' :=
  curryHomEquiv'.injective h

lemma uncurry'_injective {X Y : C} [Closed X] {f f' : ğŸ™_ C âŸ¶ (ihom X).obj Y}
    (h : uncurry' f = uncurry' f') : f = f' :=
  curryHomEquiv'.symm.injective h

@[simp]
lemma curry'_id (X : C) [Closed X] : curry' (ğŸ™ X) = id X := by
  dsimp [curry']
  rw [Category.comp_id]
  rfl

@[reassoc]
lemma whiskerLeft_curry'_ihom_ev_app {X Y : C} [Closed X] (f : X âŸ¶ Y) :
    X â— curry' f â‰« (ihom.ev X).app Y = (Ï_ _).hom â‰« f := by
  dsimp [curry']
  simp only [whiskerLeft_curry_ihom_ev_app]

@[reassoc]
lemma curry'_whiskerRight_comp {X Y Z : C} [Closed X] [Closed Y] (f : X âŸ¶ Y) :
    curry' f â–· _ â‰« comp X Y Z = (Î»_ _).hom â‰« (pre f).app Z := by
  rw [â† cancel_epi (Î»_ _).inv, Iso.inv_hom_id_assoc]
  apply uncurry_injective
  rw [uncurry_pre, comp_eq, â† curry_natural_left, â† curry_natural_left, uncurry_curry,
    compTranspose_eq, associator_inv_naturality_middle_assoc, â† comp_whiskerRight_assoc,
    whiskerLeft_curry'_ihom_ev_app, comp_whiskerRight_assoc, triangle_assoc_comp_right_assoc,
    whiskerLeft_inv_hom_assoc]

@[reassoc]
lemma whiskerLeft_curry'_comp {X Y Z : C} [Closed X] [Closed Y] (f : Y âŸ¶ Z) :
    _ â— curry' f â‰« comp X Y Z = (Ï_ _).hom â‰« (ihom X).map f := by
  rw [â† cancel_epi (Ï_ _).inv, Iso.inv_hom_id_assoc]
  apply uncurry_injective
  rw [uncurry_ihom_map, comp_eq, â† curry_natural_left, â† curry_natural_left, uncurry_curry,
    compTranspose_eq, associator_inv_naturality_right_assoc, whisker_exchange_assoc]
  dsimp
  rw [whiskerLeft_curry'_ihom_ev_app, whiskerLeft_rightUnitor_inv,
    MonoidalCategory.whiskerRight_id_assoc, Category.assoc,
    Iso.inv_hom_id_assoc, Iso.hom_inv_id_assoc, Iso.inv_hom_id_assoc,]

lemma curry'_ihom_map {X Y Z : C} [Closed X] (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    curry' f â‰« (ihom X).map g = curry' (f â‰« g) := by
  simp only [curry', â† curry_natural_right, Category.assoc]

lemma curry'_comp {X Y Z : C} [Closed X] [Closed Y] (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    curry' (f â‰« g) = (Î»_ (ğŸ™_ C)).inv â‰« (curry' f âŠ—â‚˜ curry' g) â‰« comp X Y Z := by
  rw [tensorHom_def_assoc, whiskerLeft_curry'_comp, MonoidalCategory.whiskerRight_id,
    Category.assoc, Category.assoc, Iso.inv_hom_id_assoc, â† unitors_equal,
    Iso.inv_hom_id_assoc, curry'_ihom_map]

end OrdinaryEnriched

end MonoidalClosed

end CategoryTheory
