/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Bhavik Mehta
-/
import Mathlib.CategoryTheory.Monoidal.Functor
import Mathlib.CategoryTheory.Adjunction.Limits
import Mathlib.CategoryTheory.Adjunction.Mates
import Mathlib.CategoryTheory.Functor.InvIsos

#align_import category_theory.closed.monoidal from "leanprover-community/mathlib"@"0caf3701139ef2e69c215717665361cda205a90b"

/-!
# Closed monoidal categories

Define (right) closed objects and (right) closed monoidal categories.

## TODO
Some of the theorems proved about cartesian closed categories
should be generalised and moved to this file.
-/


universe v u uâ‚‚ vâ‚‚

namespace CategoryTheory

open Category MonoidalCategory

-- Note that this class carries a particular choice of right adjoint,
-- (which is only unique up to isomorphism),
-- not merely the existence of such, and
-- so definitional properties of instances may be important.
/-- An object `X` is (right) closed if `(X âŠ— -)` is a left adjoint. -/
class Closed {C : Type u} [Category.{v} C] [MonoidalCategory.{v} C] (X : C) where
  /-- a choice of a left adjoint for `tensorLeft X` -/
  isAdj : IsLeftAdjoint (tensorLeft X)
#align category_theory.closed CategoryTheory.Closed

/-- A monoidal category `C` is (right) monoidal closed if every object is (right) closed. -/
class MonoidalClosed (C : Type u) [Category.{v} C] [MonoidalCategory.{v} C] where
  closed : âˆ€ X : C, Closed X
#align category_theory.monoidal_closed CategoryTheory.MonoidalClosed

attribute [instance 100] MonoidalClosed.closed

variable {C : Type u} [Category.{v} C] [MonoidalCategory.{v} C]

/-- If `X` and `Y` are closed then `X âŠ— Y` is.
This isn't an instance because it's not usually how we want to construct internal homs,
we'll usually prove all objects are closed uniformly.
-/
def tensorClosed {X Y : C} (hX : Closed X) (hY : Closed Y) : Closed (X âŠ— Y) where
  isAdj := by
    haveI := hX.isAdj
    -- âŠ¢ IsLeftAdjoint (tensorLeft (X âŠ— Y))
    haveI := hY.isAdj
    -- âŠ¢ IsLeftAdjoint (tensorLeft (X âŠ— Y))
    exact Adjunction.leftAdjointOfNatIso (MonoidalCategory.tensorLeftTensor _ _).symm
    -- ğŸ‰ no goals
#align category_theory.tensor_closed CategoryTheory.tensorClosed

/-- The unit object is always closed.
This isn't an instance because most of the time we'll prove closedness for all objects at once,
rather than just for this one.
-/
def unitClosed : Closed (ğŸ™_ C) where
  isAdj :=
    { right := ğŸ­ C
      adj :=
        Adjunction.mkOfHomEquiv
          { homEquiv := fun X _ =>
              { toFun := fun a => (leftUnitor X).inv â‰« a
                invFun := fun a => (leftUnitor X).hom â‰« a
                left_inv := by aesop_cat
                               -- ğŸ‰ no goals
                right_inv := by aesop_cat }
                                -- ğŸ‰ no goals
            homEquiv_naturality_left_symm := fun f g => by
              dsimp
              -- âŠ¢ (Î»_ X'âœ).hom â‰« f â‰« g = (ğŸ™ (ğŸ™_ C) âŠ— f) â‰« (Î»_ Xâœ).hom â‰« g
              rw [leftUnitor_naturality_assoc] } }
              -- ğŸ‰ no goals
#align category_theory.unit_closed CategoryTheory.unitClosed

variable (A B : C) {X X' Y Y' Z : C}

variable [Closed A]

/-- This is the internal hom `A âŸ¶[C] -`.
-/
def ihom : C â¥¤ C :=
  (@Closed.isAdj _ _ _ A _).right
#align category_theory.ihom CategoryTheory.ihom

namespace ihom

/-- The adjunction between `A âŠ— -` and `A âŸ¹ -`. -/
def adjunction : tensorLeft A âŠ£ ihom A :=
  Closed.isAdj.adj
#align category_theory.ihom.adjunction CategoryTheory.ihom.adjunction

/-- The evaluation natural transformation. -/
def ev : ihom A â‹™ tensorLeft A âŸ¶ ğŸ­ C :=
  (ihom.adjunction A).counit
#align category_theory.ihom.ev CategoryTheory.ihom.ev

/-- The coevaluation natural transformation. -/
def coev : ğŸ­ C âŸ¶ tensorLeft A â‹™ ihom A :=
  (ihom.adjunction A).unit
#align category_theory.ihom.coev CategoryTheory.ihom.coev

@[simp]
theorem ihom_adjunction_counit : (ihom.adjunction A).counit = ev A :=
  rfl
#align category_theory.ihom.ihom_adjunction_counit CategoryTheory.ihom.ihom_adjunction_counit

@[simp]
theorem ihom_adjunction_unit : (ihom.adjunction A).unit = coev A :=
  rfl
#align category_theory.ihom.ihom_adjunction_unit CategoryTheory.ihom.ihom_adjunction_unit

@[reassoc (attr := simp)]
theorem ev_naturality {X Y : C} (f : X âŸ¶ Y) :
    (ğŸ™ A âŠ— (ihom A).map f) â‰« (ev A).app Y = (ev A).app X â‰« f :=
  (ev A).naturality f
#align category_theory.ihom.ev_naturality CategoryTheory.ihom.ev_naturality

@[reassoc (attr := simp)]
theorem coev_naturality {X Y : C} (f : X âŸ¶ Y) :
    f â‰« (coev A).app Y = (coev A).app X â‰« (ihom A).map (ğŸ™ A âŠ— f) :=
  (coev A).naturality f
#align category_theory.ihom.coev_naturality CategoryTheory.ihom.coev_naturality

set_option quotPrecheck false in
/-- `A âŸ¶[C] B` denotes the internal hom from `A` to `B` -/
notation A " âŸ¶[" C "] " B:10 => (@ihom C _ _ A _).obj B

@[reassoc (attr := simp)]
theorem ev_coev : (ğŸ™ A âŠ— (coev A).app B) â‰« (ev A).app (A âŠ— B) = ğŸ™ (A âŠ— B) :=
  Adjunction.left_triangle_components (ihom.adjunction A)
#align category_theory.ihom.ev_coev CategoryTheory.ihom.ev_coev

@[reassoc (attr := simp)]
theorem coev_ev : (coev A).app (A âŸ¶[C] B) â‰« (ihom A).map ((ev A).app B) = ğŸ™ (A âŸ¶[C] B) :=
  Adjunction.right_triangle_components (ihom.adjunction A)
#align category_theory.ihom.coev_ev CategoryTheory.ihom.coev_ev

end ihom

open CategoryTheory.Limits

instance : PreservesColimits (tensorLeft A) :=
  (ihom.adjunction A).leftAdjointPreservesColimits

variable {A}

-- Wrap these in a namespace so we don't clash with the core versions.
namespace MonoidalClosed

/-- Currying in a monoidal closed category. -/
def curry : (A âŠ— Y âŸ¶ X) â†’ (Y âŸ¶ A âŸ¶[C] X) :=
  (ihom.adjunction A).homEquiv _ _
#align category_theory.monoidal_closed.curry CategoryTheory.MonoidalClosed.curry

/-- Uncurrying in a monoidal closed category. -/
def uncurry : (Y âŸ¶ A âŸ¶[C] X) â†’ (A âŠ— Y âŸ¶ X) :=
  ((ihom.adjunction A).homEquiv _ _).symm
#align category_theory.monoidal_closed.uncurry CategoryTheory.MonoidalClosed.uncurry

@[simp]
theorem homEquiv_apply_eq (f : A âŠ— Y âŸ¶ X) : (ihom.adjunction A).homEquiv _ _ f = curry f :=
  rfl
#align category_theory.monoidal_closed.hom_equiv_apply_eq CategoryTheory.MonoidalClosed.homEquiv_apply_eq

@[simp]
theorem homEquiv_symm_apply_eq (f : Y âŸ¶ A âŸ¶[C] X) :
    ((ihom.adjunction A).homEquiv _ _).symm f = uncurry f :=
  rfl
#align category_theory.monoidal_closed.hom_equiv_symm_apply_eq CategoryTheory.MonoidalClosed.homEquiv_symm_apply_eq

@[reassoc]
theorem curry_natural_left (f : X âŸ¶ X') (g : A âŠ— X' âŸ¶ Y) : curry ((ğŸ™ _ âŠ— f) â‰« g) = f â‰« curry g :=
  Adjunction.homEquiv_naturality_left _ _ _
#align category_theory.monoidal_closed.curry_natural_left CategoryTheory.MonoidalClosed.curry_natural_left

@[reassoc]
theorem curry_natural_right (f : A âŠ— X âŸ¶ Y) (g : Y âŸ¶ Y') :
    curry (f â‰« g) = curry f â‰« (ihom _).map g :=
  Adjunction.homEquiv_naturality_right _ _ _
#align category_theory.monoidal_closed.curry_natural_right CategoryTheory.MonoidalClosed.curry_natural_right

@[reassoc]
theorem uncurry_natural_right (f : X âŸ¶ A âŸ¶[C] Y) (g : Y âŸ¶ Y') :
    uncurry (f â‰« (ihom _).map g) = uncurry f â‰« g :=
  Adjunction.homEquiv_naturality_right_symm _ _ _
#align category_theory.monoidal_closed.uncurry_natural_right CategoryTheory.MonoidalClosed.uncurry_natural_right

@[reassoc]
theorem uncurry_natural_left (f : X âŸ¶ X') (g : X' âŸ¶ A âŸ¶[C] Y) :
    uncurry (f â‰« g) = (ğŸ™ _ âŠ— f) â‰« uncurry g :=
  Adjunction.homEquiv_naturality_left_symm _ _ _
#align category_theory.monoidal_closed.uncurry_natural_left CategoryTheory.MonoidalClosed.uncurry_natural_left

@[simp]
theorem uncurry_curry (f : A âŠ— X âŸ¶ Y) : uncurry (curry f) = f :=
  (Closed.isAdj.adj.homEquiv _ _).left_inv f
#align category_theory.monoidal_closed.uncurry_curry CategoryTheory.MonoidalClosed.uncurry_curry

@[simp]
theorem curry_uncurry (f : X âŸ¶ A âŸ¶[C] Y) : curry (uncurry f) = f :=
  (Closed.isAdj.adj.homEquiv _ _).right_inv f
#align category_theory.monoidal_closed.curry_uncurry CategoryTheory.MonoidalClosed.curry_uncurry

theorem curry_eq_iff (f : A âŠ— Y âŸ¶ X) (g : Y âŸ¶ A âŸ¶[C] X) : curry f = g â†” f = uncurry g :=
  Adjunction.homEquiv_apply_eq (ihom.adjunction A) f g
#align category_theory.monoidal_closed.curry_eq_iff CategoryTheory.MonoidalClosed.curry_eq_iff

theorem eq_curry_iff (f : A âŠ— Y âŸ¶ X) (g : Y âŸ¶ A âŸ¶[C] X) : g = curry f â†” uncurry g = f :=
  Adjunction.eq_homEquiv_apply (ihom.adjunction A) f g
#align category_theory.monoidal_closed.eq_curry_iff CategoryTheory.MonoidalClosed.eq_curry_iff

-- I don't think these two should be simp.
theorem uncurry_eq (g : Y âŸ¶ A âŸ¶[C] X) : uncurry g = (ğŸ™ A âŠ— g) â‰« (ihom.ev A).app X :=
  Adjunction.homEquiv_counit _
#align category_theory.monoidal_closed.uncurry_eq CategoryTheory.MonoidalClosed.uncurry_eq

theorem curry_eq (g : A âŠ— Y âŸ¶ X) : curry g = (ihom.coev A).app Y â‰« (ihom A).map g :=
  Adjunction.homEquiv_unit _
#align category_theory.monoidal_closed.curry_eq CategoryTheory.MonoidalClosed.curry_eq

theorem curry_injective : Function.Injective (curry : (A âŠ— Y âŸ¶ X) â†’ (Y âŸ¶ A âŸ¶[C] X)) :=
  (Closed.isAdj.adj.homEquiv _ _).injective
#align category_theory.monoidal_closed.curry_injective CategoryTheory.MonoidalClosed.curry_injective

theorem uncurry_injective : Function.Injective (uncurry : (Y âŸ¶ A âŸ¶[C] X) â†’ (A âŠ— Y âŸ¶ X)) :=
  (Closed.isAdj.adj.homEquiv _ _).symm.injective
#align category_theory.monoidal_closed.uncurry_injective CategoryTheory.MonoidalClosed.uncurry_injective

variable (A X)

theorem uncurry_id_eq_ev : uncurry (ğŸ™ (A âŸ¶[C] X)) = (ihom.ev A).app X := by
  rw [uncurry_eq, tensor_id, id_comp]
  -- ğŸ‰ no goals
#align category_theory.monoidal_closed.uncurry_id_eq_ev CategoryTheory.MonoidalClosed.uncurry_id_eq_ev

theorem curry_id_eq_coev : curry (ğŸ™ _) = (ihom.coev A).app X := by
  rw [curry_eq, (ihom A).map_id (A âŠ— _)]
  -- âŠ¢ NatTrans.app (ihom.coev A) ((ğŸ­ C).obj X) â‰« ğŸ™ ((ihom A).obj (A âŠ— (ğŸ­ C).obj X) â€¦
  apply comp_id
  -- ğŸ‰ no goals
#align category_theory.monoidal_closed.curry_id_eq_coev CategoryTheory.MonoidalClosed.curry_id_eq_coev

section Pre

variable {A B} [Closed B]

/-- Pre-compose an internal hom with an external hom. -/
def pre (f : B âŸ¶ A) : ihom A âŸ¶ ihom B :=
  transferNatTransSelf (ihom.adjunction _) (ihom.adjunction _) ((tensoringLeft C).map f)
#align category_theory.monoidal_closed.pre CategoryTheory.MonoidalClosed.pre

@[reassoc (attr := simp)]
theorem id_tensor_pre_app_comp_ev (f : B âŸ¶ A) (X : C) :
    (ğŸ™ B âŠ— (pre f).app X) â‰« (ihom.ev B).app X = (f âŠ— ğŸ™ (A âŸ¶[C] X)) â‰« (ihom.ev A).app X :=
  transferNatTransSelf_counit _ _ ((tensoringLeft C).map f) X
#align category_theory.monoidal_closed.id_tensor_pre_app_comp_ev CategoryTheory.MonoidalClosed.id_tensor_pre_app_comp_ev

@[simp]
theorem uncurry_pre (f : B âŸ¶ A) (X : C) :
    MonoidalClosed.uncurry ((pre f).app X) = (f âŠ— ğŸ™ _) â‰« (ihom.ev A).app X := by
  rw [uncurry_eq, id_tensor_pre_app_comp_ev]
  -- ğŸ‰ no goals
#align category_theory.monoidal_closed.uncurry_pre CategoryTheory.MonoidalClosed.uncurry_pre

@[reassoc (attr := simp)]
theorem coev_app_comp_pre_app (f : B âŸ¶ A) :
    (ihom.coev A).app X â‰« (pre f).app (A âŠ— X) = (ihom.coev B).app X â‰« (ihom B).map (f âŠ— ğŸ™ _) :=
  unit_transferNatTransSelf _ _ ((tensoringLeft C).map f) X
#align category_theory.monoidal_closed.coev_app_comp_pre_app CategoryTheory.MonoidalClosed.coev_app_comp_pre_app

@[simp]
theorem pre_id (A : C) [Closed A] : pre (ğŸ™ A) = ğŸ™ _ := by
  simp only [pre, Functor.map_id]
  -- âŠ¢ â†‘(transferNatTransSelf (ihom.adjunction A) (ihom.adjunction A)) (ğŸ™ ((tensori â€¦
  dsimp
  -- âŠ¢ â†‘(transferNatTransSelf (ihom.adjunction A) (ihom.adjunction A)) (ğŸ™ (tensorLe â€¦
  simp
  -- ğŸ‰ no goals
#align category_theory.monoidal_closed.pre_id CategoryTheory.MonoidalClosed.pre_id

@[simp]
theorem pre_map {Aâ‚ Aâ‚‚ Aâ‚ƒ : C} [Closed Aâ‚] [Closed Aâ‚‚] [Closed Aâ‚ƒ] (f : Aâ‚ âŸ¶ Aâ‚‚) (g : Aâ‚‚ âŸ¶ Aâ‚ƒ) :
    pre (f â‰« g) = pre g â‰« pre f := by
  rw [pre, pre, pre, transferNatTransSelf_comp, (tensoringLeft C).map_comp]
  -- ğŸ‰ no goals
#align category_theory.monoidal_closed.pre_map CategoryTheory.MonoidalClosed.pre_map

theorem pre_comm_ihom_map {W X Y Z : C} [Closed W] [Closed X] (f : W âŸ¶ X) (g : Y âŸ¶ Z) :
    (pre f).app Y â‰« (ihom W).map g = (ihom X).map g â‰« (pre f).app Z := by simp
                                                                          -- ğŸ‰ no goals
#align category_theory.monoidal_closed.pre_comm_ihom_map CategoryTheory.MonoidalClosed.pre_comm_ihom_map

end Pre

/-- The internal hom functor given by the monoidal closed structure. -/
@[simps]
def internalHom [MonoidalClosed C] : Cáµ’áµ– â¥¤ C â¥¤ C
    where
  obj X := ihom X.unop
  map f := pre f.unop
#align category_theory.monoidal_closed.internal_hom CategoryTheory.MonoidalClosed.internalHom

section OfEquiv

variable {D : Type uâ‚‚} [Category.{vâ‚‚} D] [MonoidalCategory.{vâ‚‚} D]

/-- Transport the property of being monoidal closed across a monoidal equivalence of categories -/
noncomputable def ofEquiv (F : MonoidalFunctor C D) [IsEquivalence F.toFunctor]
    [h : MonoidalClosed D] : MonoidalClosed C
    where closed X :=
    { isAdj := by
        haveI q : Closed (F.obj X) := inferInstance
        -- âŠ¢ IsLeftAdjoint (tensorLeft X)
        haveI : IsLeftAdjoint (tensorLeft (F.obj X)) := q.isAdj
        -- âŠ¢ IsLeftAdjoint (tensorLeft X)
        have i := compInvIso (MonoidalFunctor.commTensorLeft F X)
        -- âŠ¢ IsLeftAdjoint (tensorLeft X)
        exact Adjunction.leftAdjointOfNatIso i }
        -- ğŸ‰ no goals
#align category_theory.monoidal_closed.of_equiv CategoryTheory.MonoidalClosed.ofEquiv

/-- Suppose we have a monoidal equivalence `F : C â‰Œ D`, with `D` monoidal closed. We can pull the
monoidal closed instance back along the equivalence. For `X, Y, Z : C`, this lemma describes the
resulting currying map `Hom(X âŠ— Y, Z) â†’ Hom(Y, (X âŸ¶[C] Z))`. (`X âŸ¶[C] Z` is defined to be
`Fâ»Â¹(F(X) âŸ¶[D] F(Z))`, so currying in `C` is given by essentially conjugating currying in
`D` by `F.`) -/
theorem ofEquiv_curry_def (F : MonoidalFunctor C D) [IsEquivalence F.toFunctor]
    [MonoidalClosed D] {X Y Z : C} (f : X âŠ— Y âŸ¶ Z) :
    @MonoidalClosed.curry _ _ _ _ _ _ ((MonoidalClosed.ofEquiv F).1 _) f =
      (F.1.1.adjunction.homEquiv Y ((ihom _).obj _))
        (MonoidalClosed.curry
          (F.1.1.inv.adjunction.homEquiv (F.1.1.obj X âŠ— F.1.1.obj Y) Z
            ((compInvIso (F.commTensorLeft X)).hom.app Y â‰« f))) :=
  rfl
#align category_theory.monoidal_closed.of_equiv_curry_def CategoryTheory.MonoidalClosed.ofEquiv_curry_def

/-- Suppose we have a monoidal equivalence `F : C â‰Œ D`, with `D` monoidal closed. We can pull the
monoidal closed instance back along the equivalence. For `X, Y, Z : C`, this lemma describes the
resulting uncurrying map `Hom(Y, (X âŸ¶[C] Z)) â†’ Hom(X âŠ— Y âŸ¶ Z)`. (`X âŸ¶[C] Z` is
defined to be `Fâ»Â¹(F(X) âŸ¶[D] F(Z))`, so uncurrying in `C` is given by essentially conjugating
uncurrying in `D` by `F.`) -/
theorem ofEquiv_uncurry_def (F : MonoidalFunctor C D) [IsEquivalence F.toFunctor]
    [MonoidalClosed D] {X Y Z : C}
    (f : Y âŸ¶ (@ihom _ _ _ X <| (MonoidalClosed.ofEquiv F).1 X).obj Z) :
    @MonoidalClosed.uncurry _ _ _ _ _ _ ((MonoidalClosed.ofEquiv F).1 _) f =
      (compInvIso (F.commTensorLeft X)).inv.app Y â‰«
        (F.1.1.inv.adjunction.homEquiv (F.1.1.obj X âŠ— F.1.1.obj Y) Z).symm
          (MonoidalClosed.uncurry
            ((F.1.1.adjunction.homEquiv Y ((ihom (F.1.1.obj X)).obj (F.1.1.obj Z))).symm f)) :=
  rfl
#align category_theory.monoidal_closed.of_equiv_uncurry_def CategoryTheory.MonoidalClosed.ofEquiv_uncurry_def
end OfEquiv

end MonoidalClosed

end CategoryTheory
