/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Closed.Enrichment
import Mathlib.CategoryTheory.Enriched.FunctorCategory

/-!
# Functor categories are monoidal closed

Let `C` be a monoidal closed category. Let `J` be a category. In this file,
we obtain that the category `J ‚•§ C` is monoidal closed if `C` has suitable
limits.

-/

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory

open Category Limits MonoidalCategory

namespace MonoidalClosed

namespace FunctorCategory

open Enriched.FunctorCategory

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] [MonoidalCategory C] [MonoidalClosed C]
  {J : Type u‚ÇÇ} [Category.{v‚ÇÇ} J]
  [‚àÄ (F‚ÇÅ F‚ÇÇ : J ‚•§ C), HasFunctorEnrichedHom C F‚ÇÅ F‚ÇÇ]

attribute [local simp] enrichedCategorySelf_hom

section

variable {F‚ÇÅ F‚ÇÇ F‚ÇÇ' F‚ÇÉ F‚ÇÉ' : J ‚•§ C}

/-- The bijection `(F‚ÇÅ ‚äó F‚ÇÇ ‚ü∂ F‚ÇÉ) ‚âÉ (F‚ÇÇ ‚ü∂ functorEnrichedHom C F‚ÇÅ F‚ÇÉ)` when `F‚ÇÅ`, `F‚ÇÇ`
and `F‚ÇÉ` are functors `J ‚•§ C`, and `C` is monoidal closed. -/
noncomputable def homEquiv : (F‚ÇÅ ‚äó F‚ÇÇ ‚ü∂ F‚ÇÉ) ‚âÉ (F‚ÇÇ ‚ü∂ functorEnrichedHom C F‚ÇÅ F‚ÇÉ) where
  toFun f :=
    { app j := end_.lift (fun k ‚Ü¶ F‚ÇÇ.map k.hom ‚â´ curry (f.app k.right))
        (fun k‚ÇÅ k‚ÇÇ œÜ ‚Ü¶ by
          dsimp
          simp only [enrichedOrdinaryCategorySelf_eHomWhiskerLeft, Category.assoc,
            enrichedOrdinaryCategorySelf_eHomWhiskerRight]
          rw [‚Üê curry_natural_left_assoc, ‚Üê curry_natural_left_assoc,
            ‚Üê curry_natural_right, curry_pre_app, Category.assoc,
            ‚Üê f.naturality œÜ.right, Monoidal.tensorObj_map, tensorHom_def_assoc,
            ‚Üê Under.w œÜ, Functor.map_comp, MonoidalCategory.whiskerLeft_comp_assoc,
            whisker_exchange_assoc]) }
  invFun g :=
    { app j := uncurry (g.app j ‚â´ enrichedHomœÄ C _ _ (Under.mk (ùüô j)) )
      naturality j j' œÜ := by
        dsimp
        rw [‚Üê uncurry_natural_right, tensorHom_def'_assoc, ‚Üê uncurry_pre_app,
          ‚Üê uncurry_natural_left, Category.assoc, Category.assoc,
          NatTrans.naturality_assoc, functorEnrichedHom_map,
          end_.lift_œÄ_assoc, enrichedOrdinaryCategorySelf_eHomWhiskerRight]
        dsimp
        rw [pre_id, NatTrans.id_app, enrichedOrdinaryCategorySelf_eHomWhiskerLeft,
          Functor.map_id, Category.comp_id, Category.comp_id]
        congr 2
        dsimp
        rw [‚Üê enrichedOrdinaryCategorySelf_eHomWhiskerRight,
          ‚Üê enrichedOrdinaryCategorySelf_eHomWhiskerLeft]
        let Œ± : Under.mk (ùüô j) ‚ü∂ (Under.map œÜ).obj (Under.mk (ùüô j')) := Under.homMk œÜ
        exact (enrichedHom_condition C (Under.forget j ‚ãô F‚ÇÅ) (Under.forget j ‚ãô F‚ÇÉ) Œ±).symm }
  left_inv f := by cat_disch
  right_inv g := by
    ext j
    dsimp
    ext k
    -- this following list was obtained by
    -- `simp? [enrichedOrdinaryCategorySelf_eHomWhiskerLeft, Under.map, Comma.mapLeft]`
    simp only [diagram_obj_obj, Functor.comp_obj, Under.forget_obj, enrichedCategorySelf_hom,
      curry_uncurry, NatTrans.naturality_assoc, functorEnrichedHom_obj, functorEnrichedHom_map,
      Under.map, Comma.mapLeft, Functor.const_obj_obj, Functor.id_obj, Discrete.natTrans_app,
      StructuredArrow.left_eq_id, end_.lift_œÄ, Under.mk_right, Under.mk_hom, Iso.refl_inv,
      NatTrans.id_app, enrichedOrdinaryCategorySelf_eHomWhiskerRight, pre_id, Iso.refl_hom,
      enrichedOrdinaryCategorySelf_eHomWhiskerLeft, Functor.map_id, Category.comp_id]
    congr
    simp

lemma homEquiv_naturality_two_symm (f‚ÇÇ : F‚ÇÇ ‚ü∂ F‚ÇÇ') (g : F‚ÇÇ' ‚ü∂ functorEnrichedHom C F‚ÇÅ F‚ÇÉ) :
    homEquiv.symm (f‚ÇÇ ‚â´ g) = F‚ÇÅ ‚óÅ f‚ÇÇ ‚â´ homEquiv.symm g := by
  dsimp [homEquiv]
  ext j
  simp [‚Üê uncurry_natural_left]

lemma homEquiv_naturality_three [‚àÄ (F‚ÇÅ F‚ÇÇ : J ‚•§ C), HasEnrichedHom C F‚ÇÅ F‚ÇÇ]
    (f : F‚ÇÅ ‚äó F‚ÇÇ ‚ü∂ F‚ÇÉ) (f‚ÇÉ : F‚ÇÉ ‚ü∂ F‚ÇÉ') :
    homEquiv (f ‚â´ f‚ÇÉ) = homEquiv f ‚â´ (œÅ_ _).inv ‚â´ _ ‚óÅ functorHomEquiv _ f‚ÇÉ ‚â´
      functorEnrichedComp C F‚ÇÅ F‚ÇÉ F‚ÇÉ' := by
  dsimp [homEquiv]
  ext j
  dsimp
  ext k
  rw [Category.assoc, Category.assoc, Category.assoc, end_.lift_œÄ, enrichedComp_œÄ,
    tensorHom_def, Category.assoc, whisker_exchange_assoc, whiskerRight_id_assoc,
    Iso.inv_hom_id_assoc, end_.lift_œÄ_assoc, Category.assoc,
    ‚Üê MonoidalCategory.whiskerLeft_comp_assoc, Category.assoc, end_.lift_œÄ,
    enrichedOrdinaryCategorySelf_eHomWhiskerRight,
    enrichedOrdinaryCategorySelf_eHomWhiskerLeft]
  dsimp
  rw [pre_id, NatTrans.id_app, Functor.map_id, Category.comp_id,
    Category.comp_id, homEquiv_apply_œÄ, curry_natural_right]
  congr 2
  symm
  apply enrichedOrdinaryCategorySelf_eHomWhiskerLeft

end

variable [‚àÄ (F‚ÇÅ F‚ÇÇ : J ‚•§ C), HasEnrichedHom C F‚ÇÅ F‚ÇÇ]
attribute [local instance] Enriched.FunctorCategory.functorEnrichedOrdinaryCategory

/-- When `C` is monoidal closed and has suitable limits,
then for any `F : J ‚•§ C`, `tensorLeft F` has a right adjoint. -/
noncomputable def adj (F : J ‚•§ C) :
    MonoidalCategory.tensorLeft F ‚ä£ (eHomFunctor _ _).obj ‚ü®F‚ü© :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun _ _ ‚Ü¶ homEquiv
      homEquiv_naturality_left_symm := homEquiv_naturality_two_symm
      homEquiv_naturality_right := homEquiv_naturality_three }

/-- When `C` is monoidal closed and has suitable limits,
then for any `F : J ‚•§ C`, `tensorLeft F` has a right adjoint. -/
noncomputable def closed (F : J ‚•§ C) : Closed F where
  rightAdj := (eHomFunctor _ _).obj ‚ü®F‚ü©
  adj := adj F

/-- If `C` is monoidal closed and has suitable limits, the functor
category `J ‚•§ C` is monoidal closed. -/
noncomputable scoped instance monoidalClosed : MonoidalClosed (J ‚•§ C) where
  closed := closed

end FunctorCategory

end MonoidalClosed

end CategoryTheory
