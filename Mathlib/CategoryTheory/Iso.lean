/-
Copyright (c) 2017 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Tim Baumann, Stephen Morgan, Kim Morrison, Floris van Doorn
-/
import Mathlib.Tactic.CategoryTheory.Reassoc

/-!
# Isomorphisms

This file defines isomorphisms between objects of a category.

## Main definitions

- `structure Iso` : a bundled isomorphism between two objects of a category;
- `class IsIso` : an unbundled version of `iso`;
  note that `IsIso f` is a `Prop`, and only asserts the existence of an inverse.
  Of course, this inverse is unique, so it doesn't cost us much to use choice to retrieve it.
- `inv f`, for the inverse of a morphism with `[IsIso f]`
- `asIso` : convert from `IsIso` to `Iso` (noncomputable);
- `of_iso` : convert from `Iso` to `IsIso`;
- standard operations on isomorphisms (composition, inverse etc)

## Notation

- `X â‰… Y` : same as `Iso X Y`;
- `Î± â‰ªâ‰« Î²` : composition of two isomorphisms; it is called `Iso.trans`

## Tags

category, category theory, isomorphism
-/

set_option mathlib.tactic.category.grind true

universe v u

-- morphism levels before object levels. See note [category theory universes].
namespace CategoryTheory

open Category

/-- An isomorphism (a.k.a. an invertible morphism) between two objects of a category.
The inverse morphism is bundled.

See also `CategoryTheory.Core` for the category with the same objects and isomorphisms playing
the role of morphisms. -/
@[stacks 0017]
structure Iso {C : Type u} [Category.{v} C] (X Y : C) where
  /-- The forward direction of an isomorphism. -/
  hom : X âŸ¶ Y
  /-- The backwards direction of an isomorphism. -/
  inv : Y âŸ¶ X
  /-- Composition of the two directions of an isomorphism is the identity on the source. -/
  hom_inv_id : hom â‰« inv = ğŸ™ X := by cat_disch
  /-- Composition of the two directions of an isomorphism in reverse order
  is the identity on the target. -/
  inv_hom_id : inv â‰« hom = ğŸ™ Y := by cat_disch

attribute [reassoc (attr := simp), grind =] Iso.hom_inv_id Iso.inv_hom_id

/-- Notation for an isomorphism in a category. -/
infixr:10 " â‰… " => Iso -- type as \cong or \iso

variable {C : Type u} [Category.{v} C] {X Y Z : C}

namespace Iso

set_option linter.style.commandStart false in -- false positive, calc blocks
@[ext, grind ext]
theorem ext â¦ƒÎ± Î² : X â‰… Yâ¦„ (w : Î±.hom = Î².hom) : Î± = Î² :=
  suffices Î±.inv = Î².inv by grind [Iso]
  calc
    Î±.inv = Î±.inv â‰« Î².hom â‰« Î².inv := by grind
    _     = Î².inv                 := by grind

/-- Inverse isomorphism. -/
@[symm]
def symm (I : X â‰… Y) : Y â‰… X where
  hom := I.inv
  inv := I.hom

@[simp, grind =]
theorem symm_hom (Î± : X â‰… Y) : Î±.symm.hom = Î±.inv :=
  rfl

@[simp, grind =]
theorem symm_inv (Î± : X â‰… Y) : Î±.symm.inv = Î±.hom :=
  rfl

@[simp, grind =]
theorem symm_mk {X Y : C} (hom : X âŸ¶ Y) (inv : Y âŸ¶ X) (hom_inv_id) (inv_hom_id) :
    Iso.symm { hom, inv, hom_inv_id := hom_inv_id, inv_hom_id := inv_hom_id } =
      { hom := inv, inv := hom, hom_inv_id := inv_hom_id, inv_hom_id := hom_inv_id } :=
  rfl

@[simp, grind =]
theorem symm_symm_eq {X Y : C} (Î± : X â‰… Y) : Î±.symm.symm = Î± := rfl

theorem symm_bijective {X Y : C} : Function.Bijective (symm : (X â‰… Y) â†’ _) :=
  Function.bijective_iff_has_inverse.mpr âŸ¨_, symm_symm_eq, symm_symm_eqâŸ©

@[simp]
theorem symm_eq_iff {X Y : C} {Î± Î² : X â‰… Y} : Î±.symm = Î².symm â†” Î± = Î² :=
  symm_bijective.injective.eq_iff

theorem nonempty_iso_symm (X Y : C) : Nonempty (X â‰… Y) â†” Nonempty (Y â‰… X) :=
  âŸ¨fun h => âŸ¨h.some.symmâŸ©, fun h => âŸ¨h.some.symmâŸ©âŸ©

/-- Identity isomorphism. -/
@[refl, simps (attr := grind =)]
def refl (X : C) : X â‰… X where
  hom := ğŸ™ X
  inv := ğŸ™ X

instance : Inhabited (X â‰… X) := âŸ¨Iso.refl XâŸ©

theorem nonempty_iso_refl (X : C) : Nonempty (X â‰… X) := âŸ¨defaultâŸ©

@[simp, grind =]
theorem refl_symm (X : C) : (Iso.refl X).symm = Iso.refl X := rfl

/-- Composition of two isomorphisms -/
@[simps (attr := grind =)]
def trans (Î± : X â‰… Y) (Î² : Y â‰… Z) : X â‰… Z where
  hom := Î±.hom â‰« Î².hom
  inv := Î².inv â‰« Î±.inv

@[simps]
instance instTransIso : Trans (Î± := C) (Â· â‰… Â·) (Â· â‰… Â·) (Â· â‰… Â·) where
  trans := trans

/-- Notation for composition of isomorphisms. -/
infixr:80 " â‰ªâ‰« " => Iso.trans -- type as `\ll \gg`.

@[simp, grind =]
theorem trans_mk {X Y Z : C} (hom : X âŸ¶ Y) (inv : Y âŸ¶ X) (hom_inv_id) (inv_hom_id)
    (hom' : Y âŸ¶ Z) (inv' : Z âŸ¶ Y) (hom_inv_id') (inv_hom_id') (hom_inv_id'') (inv_hom_id'') :
    Iso.trans âŸ¨hom, inv, hom_inv_id, inv_hom_idâŸ© âŸ¨hom', inv', hom_inv_id', inv_hom_id'âŸ© =
     âŸ¨hom â‰« hom', inv' â‰« inv, hom_inv_id'', inv_hom_id''âŸ© :=
  rfl

@[simp, grind _=_]
theorem trans_symm (Î± : X â‰… Y) (Î² : Y â‰… Z) : (Î± â‰ªâ‰« Î²).symm = Î².symm â‰ªâ‰« Î±.symm :=
  rfl

@[simp, grind _=_]
theorem trans_assoc {Z' : C} (Î± : X â‰… Y) (Î² : Y â‰… Z) (Î³ : Z â‰… Z') :
    (Î± â‰ªâ‰« Î²) â‰ªâ‰« Î³ = Î± â‰ªâ‰« Î² â‰ªâ‰« Î³ := by
  ext; simp only [trans_hom, Category.assoc]

@[simp]
theorem refl_trans (Î± : X â‰… Y) : Iso.refl X â‰ªâ‰« Î± = Î± := by ext; apply Category.id_comp

@[simp]
theorem trans_refl (Î± : X â‰… Y) : Î± â‰ªâ‰« Iso.refl Y = Î± := by ext; apply Category.comp_id

@[simp]
theorem symm_self_id (Î± : X â‰… Y) : Î±.symm â‰ªâ‰« Î± = Iso.refl Y :=
  ext Î±.inv_hom_id

@[simp]
theorem self_symm_id (Î± : X â‰… Y) : Î± â‰ªâ‰« Î±.symm = Iso.refl X :=
  ext Î±.hom_inv_id

@[simp]
theorem symm_self_id_assoc (Î± : X â‰… Y) (Î² : Y â‰… Z) : Î±.symm â‰ªâ‰« Î± â‰ªâ‰« Î² = Î² := by
  rw [â† trans_assoc, symm_self_id, refl_trans]

@[simp]
theorem self_symm_id_assoc (Î± : X â‰… Y) (Î² : X â‰… Z) : Î± â‰ªâ‰« Î±.symm â‰ªâ‰« Î² = Î² := by
  rw [â† trans_assoc, self_symm_id, refl_trans]

theorem inv_comp_eq (Î± : X â‰… Y) {f : X âŸ¶ Z} {g : Y âŸ¶ Z} : Î±.inv â‰« f = g â†” f = Î±.hom â‰« g :=
  âŸ¨fun H => by simp [H.symm], fun H => by simp [H]âŸ©

theorem eq_inv_comp (Î± : X â‰… Y) {f : X âŸ¶ Z} {g : Y âŸ¶ Z} : g = Î±.inv â‰« f â†” Î±.hom â‰« g = f :=
  (inv_comp_eq Î±.symm).symm

theorem comp_inv_eq (Î± : X â‰… Y) {f : Z âŸ¶ Y} {g : Z âŸ¶ X} : f â‰« Î±.inv = g â†” f = g â‰« Î±.hom :=
  âŸ¨fun H => by simp [H.symm], fun H => by simp [H]âŸ©

theorem eq_comp_inv (Î± : X â‰… Y) {f : Z âŸ¶ Y} {g : Z âŸ¶ X} : g = f â‰« Î±.inv â†” g â‰« Î±.hom = f :=
  (comp_inv_eq Î±.symm).symm

theorem inv_eq_inv (f g : X â‰… Y) : f.inv = g.inv â†” f.hom = g.hom :=
  have : âˆ€ {X Y : C} (f g : X â‰… Y), f.hom = g.hom â†’ f.inv = g.inv := fun f g h => by rw [ext h]
  âŸ¨this f.symm g.symm, this f gâŸ©

theorem hom_comp_eq_id (Î± : X â‰… Y) {f : Y âŸ¶ X} : Î±.hom â‰« f = ğŸ™ X â†” f = Î±.inv := by
  rw [â† eq_inv_comp, comp_id]

theorem comp_hom_eq_id (Î± : X â‰… Y) {f : Y âŸ¶ X} : f â‰« Î±.hom = ğŸ™ Y â†” f = Î±.inv := by
  rw [â† eq_comp_inv, id_comp]

theorem inv_comp_eq_id (Î± : X â‰… Y) {f : X âŸ¶ Y} : Î±.inv â‰« f = ğŸ™ Y â†” f = Î±.hom :=
  hom_comp_eq_id Î±.symm

theorem comp_inv_eq_id (Î± : X â‰… Y) {f : X âŸ¶ Y} : f â‰« Î±.inv = ğŸ™ X â†” f = Î±.hom :=
  comp_hom_eq_id Î±.symm

theorem hom_eq_inv (Î± : X â‰… Y) (Î² : Y â‰… X) : Î±.hom = Î².inv â†” Î².hom = Î±.inv := by
  rw [â† symm_inv, inv_eq_inv Î±.symm Î², eq_comm]
  rfl

attribute [local grind] Function.LeftInverse Function.RightInverse

/-- The bijection `(Z âŸ¶ X) â‰ƒ (Z âŸ¶ Y)` induced by `Î± : X â‰… Y`. -/
@[simps]
def homToEquiv (Î± : X â‰… Y) {Z : C} : (Z âŸ¶ X) â‰ƒ (Z âŸ¶ Y) where
  toFun f := f â‰« Î±.hom
  invFun g := g â‰« Î±.inv
  left_inv := by cat_disch
  right_inv := by cat_disch

/-- The bijection `(X âŸ¶ Z) â‰ƒ (Y âŸ¶ Z)` induced by `Î± : X â‰… Y`. -/
@[simps]
def homFromEquiv (Î± : X â‰… Y) {Z : C} : (X âŸ¶ Z) â‰ƒ (Y âŸ¶ Z) where
  toFun f := Î±.inv â‰« f
  invFun g := Î±.hom â‰« g
  left_inv := by cat_disch
  right_inv := by cat_disch

end Iso

/-- The `IsIso` typeclass expresses that a morphism is invertible.

Given a morphism `f` with `IsIso f`, one can view `f` as an isomorphism via `asIso f` and get
the inverse using `inv f`. -/
class IsIso (f : X âŸ¶ Y) : Prop where
  /-- The existence of an inverse morphism. -/
  out : âˆƒ inv : Y âŸ¶ X, f â‰« inv = ğŸ™ X âˆ§ inv â‰« f = ğŸ™ Y

/-- The inverse of a morphism `f` when we have `[IsIso f]`.
-/
noncomputable def inv (f : X âŸ¶ Y) [I : IsIso f] : Y âŸ¶ X :=
  Classical.choose I.1

namespace IsIso

@[simp, grind =]
theorem hom_inv_id (f : X âŸ¶ Y) [I : IsIso f] : f â‰« inv f = ğŸ™ X :=
  (Classical.choose_spec I.1).left

@[simp, grind =]
theorem inv_hom_id (f : X âŸ¶ Y) [I : IsIso f] : inv f â‰« f = ğŸ™ Y :=
  (Classical.choose_spec I.1).right

-- FIXME putting @[reassoc] on the `hom_inv_id` above somehow unfolds `inv`
-- This happens even if we make `inv` irreducible!
-- I don't understand how this is happening: it is likely a bug.

-- attribute [reassoc] hom_inv_id inv_hom_id
-- #print hom_inv_id_assoc
--   theorem CategoryTheory.IsIso.hom_inv_id_assoc {X Y : C} (f : X âŸ¶ Y) [I : IsIso f]
--     {Z : C} (h : X âŸ¶ Z),
--     f â‰« Classical.choose (_ : Exists fun inv â†¦ f â‰« inv = ğŸ™ X âˆ§ inv â‰« f = ğŸ™ Y) â‰« h = h := ...

@[simp]
theorem hom_inv_id_assoc (f : X âŸ¶ Y) [I : IsIso f] {Z} (g : X âŸ¶ Z) : f â‰« inv f â‰« g = g := by
  simp [â† Category.assoc]

@[simp]
theorem inv_hom_id_assoc (f : X âŸ¶ Y) [I : IsIso f] {Z} (g : Y âŸ¶ Z) : inv f â‰« f â‰« g = g := by
  simp [â† Category.assoc]

end IsIso

lemma Iso.isIso_hom (e : X â‰… Y) : IsIso e.hom :=
  âŸ¨e.inv, by simp only [hom_inv_id], by simpâŸ©

lemma Iso.isIso_inv (e : X â‰… Y) : IsIso e.inv := e.symm.isIso_hom

attribute [instance] Iso.isIso_hom Iso.isIso_inv

open IsIso

/-- Reinterpret a morphism `f` with an `IsIso f` instance as an `Iso`. -/
noncomputable def asIso (f : X âŸ¶ Y) [IsIso f] : X â‰… Y :=
  âŸ¨f, inv f, hom_inv_id f, inv_hom_id fâŸ©

-- Porting note: the `IsIso f` argument had been instance implicit,
-- but we've changed it to implicit as a `rw` in `Mathlib/CategoryTheory/Closed/Functor.lean`
-- was failing to generate it by typeclass search.
@[simp]
theorem asIso_hom (f : X âŸ¶ Y) [IsIso f] : (asIso f).hom = f :=
  rfl

-- Porting note: the `IsIso f` argument had been instance implicit,
-- but we've changed it to implicit as a `rw` in `Mathlib/CategoryTheory/Closed/Functor.lean`
-- was failing to generate it by typeclass search.
@[simp]
theorem asIso_inv (f : X âŸ¶ Y) [IsIso f] : (asIso f).inv = inv f :=
  rfl

namespace IsIso

-- see Note [lower instance priority]
instance (priority := 100) epi_of_iso (f : X âŸ¶ Y) [IsIso f] : Epi f where
  left_cancellation g h w := by
    rw [â† IsIso.inv_hom_id_assoc f g, w, IsIso.inv_hom_id_assoc f h]

-- see Note [lower instance priority]
instance (priority := 100) mono_of_iso (f : X âŸ¶ Y) [IsIso f] : Mono f where
  right_cancellation g h w := by
    rw [â† Category.comp_id g, â† Category.comp_id h, â† IsIso.hom_inv_id f,
      â† Category.assoc, w, â† Category.assoc]

@[aesop apply safe (rule_sets := [CategoryTheory]), grind â†=]
theorem inv_eq_of_hom_inv_id {f : X âŸ¶ Y} [IsIso f] {g : Y âŸ¶ X} (hom_inv_id : f â‰« g = ğŸ™ X) :
    inv f = g := by
  have := congrArg (inv f â‰« Â·) hom_inv_id
  grind

theorem inv_eq_of_inv_hom_id {f : X âŸ¶ Y} [IsIso f] {g : Y âŸ¶ X} (inv_hom_id : g â‰« f = ğŸ™ Y) :
    inv f = g := by
  have := congrArg (Â· â‰« inv f) inv_hom_id
  grind

@[aesop apply safe (rule_sets := [CategoryTheory])]
theorem eq_inv_of_hom_inv_id {f : X âŸ¶ Y} [IsIso f] {g : Y âŸ¶ X} (hom_inv_id : f â‰« g = ğŸ™ X) :
    g = inv f :=
  (inv_eq_of_hom_inv_id hom_inv_id).symm

theorem eq_inv_of_inv_hom_id {f : X âŸ¶ Y} [IsIso f] {g : Y âŸ¶ X} (inv_hom_id : g â‰« f = ğŸ™ Y) :
    g = inv f :=
  (inv_eq_of_inv_hom_id inv_hom_id).symm

instance id (X : C) : IsIso (ğŸ™ X) := âŸ¨âŸ¨ğŸ™ X, by simpâŸ©âŸ©

variable {f : X âŸ¶ Y} {h : Y âŸ¶ Z}

instance inv_isIso [IsIso f] : IsIso (inv f) :=
  (asIso f).isIso_inv

/- The following instance has lower priority for the following reason:
Suppose we are given `f : X â‰… Y` with `X Y : Type u`.
Without the lower priority, typeclass inference cannot deduce `IsIso f.hom`
because `f.hom` is defeq to `(fun x â†¦ x) â‰« f.hom`, triggering a loop. -/
instance (priority := 900) comp_isIso [IsIso f] [IsIso h] : IsIso (f â‰« h) :=
  (asIso f â‰ªâ‰« asIso h).isIso_hom

/--
The composition of isomorphisms is an isomorphism. Here the arguments of type `IsIso` are
explicit, to make this easier to use with the `refine` tactic, for instance.
-/
lemma comp_isIso' (_ : IsIso f) (_ : IsIso h) : IsIso (f â‰« h) := inferInstance

@[simp]
theorem inv_id : inv (ğŸ™ X) = ğŸ™ X := by
  apply inv_eq_of_hom_inv_id
  simp

@[simp, reassoc]
theorem inv_comp [IsIso f] [IsIso h] : inv (f â‰« h) = inv h â‰« inv f := by
  apply inv_eq_of_hom_inv_id
  simp

@[simp]
theorem inv_inv [IsIso f] : inv (inv f) = f := by
  apply inv_eq_of_hom_inv_id
  simp

@[simp]
theorem Iso.inv_inv (f : X â‰… Y) : inv f.inv = f.hom := by
  apply inv_eq_of_hom_inv_id
  simp

@[simp]
theorem Iso.inv_hom (f : X â‰… Y) : inv f.hom = f.inv := by
  apply inv_eq_of_hom_inv_id
  simp

@[simp]
theorem inv_comp_eq (Î± : X âŸ¶ Y) [IsIso Î±] {f : X âŸ¶ Z} {g : Y âŸ¶ Z} : inv Î± â‰« f = g â†” f = Î± â‰« g :=
  (asIso Î±).inv_comp_eq

@[simp]
theorem eq_inv_comp (Î± : X âŸ¶ Y) [IsIso Î±] {f : X âŸ¶ Z} {g : Y âŸ¶ Z} : g = inv Î± â‰« f â†” Î± â‰« g = f :=
  (asIso Î±).eq_inv_comp

@[simp]
theorem comp_inv_eq (Î± : X âŸ¶ Y) [IsIso Î±] {f : Z âŸ¶ Y} {g : Z âŸ¶ X} : f â‰« inv Î± = g â†” f = g â‰« Î± :=
  (asIso Î±).comp_inv_eq

@[simp]
theorem eq_comp_inv (Î± : X âŸ¶ Y) [IsIso Î±] {f : Z âŸ¶ Y} {g : Z âŸ¶ X} : g = f â‰« inv Î± â†” g â‰« Î± = f :=
  (asIso Î±).eq_comp_inv

theorem of_isIso_comp_left {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [IsIso f] [IsIso (f â‰« g)] :
    IsIso g := by
  rw [â† id_comp g, â† inv_hom_id f, assoc]
  infer_instance

theorem of_isIso_comp_right {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [IsIso g] [IsIso (f â‰« g)] :
    IsIso f := by
  rw [â† comp_id f, â† hom_inv_id g, â† assoc]
  infer_instance

theorem of_isIso_fac_left {X Y Z : C} {f : X âŸ¶ Y} {g : Y âŸ¶ Z} {h : X âŸ¶ Z} [IsIso f]
    [hh : IsIso h] (w : f â‰« g = h) : IsIso g := by
  rw [â† w] at hh
  exact of_isIso_comp_left f g

theorem of_isIso_fac_right {X Y Z : C} {f : X âŸ¶ Y} {g : Y âŸ¶ Z} {h : X âŸ¶ Z} [IsIso g]
    [hh : IsIso h] (w : f â‰« g = h) : IsIso f := by
  rw [â† w] at hh
  exact of_isIso_comp_right f g

end IsIso

theorem eq_of_inv_eq_inv {f g : X âŸ¶ Y} [IsIso f] [IsIso g] (p : inv f = inv g) : f = g := by
  apply (cancel_epi (inv f)).1
  rw [inv_hom_id, p, inv_hom_id]

theorem IsIso.inv_eq_inv {f g : X âŸ¶ Y} [IsIso f] [IsIso g] : inv f = inv g â†” f = g :=
  Iso.inv_eq_inv (asIso f) (asIso g)

theorem hom_comp_eq_id (g : X âŸ¶ Y) [IsIso g] {f : Y âŸ¶ X} : g â‰« f = ğŸ™ X â†” f = inv g :=
  (asIso g).hom_comp_eq_id

theorem comp_hom_eq_id (g : X âŸ¶ Y) [IsIso g] {f : Y âŸ¶ X} : f â‰« g = ğŸ™ Y â†” f = inv g :=
  (asIso g).comp_hom_eq_id

theorem inv_comp_eq_id (g : X âŸ¶ Y) [IsIso g] {f : X âŸ¶ Y} : inv g â‰« f = ğŸ™ Y â†” f = g :=
  (asIso g).inv_comp_eq_id

theorem comp_inv_eq_id (g : X âŸ¶ Y) [IsIso g] {f : X âŸ¶ Y} : f â‰« inv g = ğŸ™ X â†” f = g :=
  (asIso g).comp_inv_eq_id

theorem isIso_of_hom_comp_eq_id (g : X âŸ¶ Y) [IsIso g] {f : Y âŸ¶ X} (h : g â‰« f = ğŸ™ X) : IsIso f := by
  rw [(hom_comp_eq_id _).mp h]
  infer_instance

theorem isIso_of_comp_hom_eq_id (g : X âŸ¶ Y) [IsIso g] {f : Y âŸ¶ X} (h : f â‰« g = ğŸ™ Y) : IsIso f := by
  rw [(comp_hom_eq_id _).mp h]
  infer_instance

namespace Iso

@[aesop apply safe (rule_sets := [CategoryTheory])]
theorem inv_ext {f : X â‰… Y} {g : Y âŸ¶ X} (hom_inv_id : f.hom â‰« g = ğŸ™ X) : f.inv = g :=
  ((hom_comp_eq_id f).1 hom_inv_id).symm

@[aesop apply safe (rule_sets := [CategoryTheory])]
theorem inv_ext' {f : X â‰… Y} {g : Y âŸ¶ X} (hom_inv_id : f.hom â‰« g = ğŸ™ X) : g = f.inv :=
  (hom_comp_eq_id f).1 hom_inv_id

/-!
All these cancellation lemmas can be solved by `simp [cancel_mono]` (or `simp [cancel_epi]`),
but with the current design `cancel_mono` is not a good `simp` lemma,
because it generates a typeclass search.

When we can see syntactically that a morphism is a `mono` or an `epi`
because it came from an isomorphism, it's fine to do the cancellation via `simp`.

In the longer term, it might be worth exploring making `mono` and `epi` structures,
rather than typeclasses, with coercions back to `X âŸ¶ Y`.
Presumably we could write `X â†ª Y` and `X â†  Y`.
-/


@[simp]
theorem cancel_iso_hom_left {X Y Z : C} (f : X â‰… Y) (g g' : Y âŸ¶ Z) :
    f.hom â‰« g = f.hom â‰« g' â†” g = g' := by
  simp only [cancel_epi]

@[simp]
theorem cancel_iso_inv_left {X Y Z : C} (f : Y â‰… X) (g g' : Y âŸ¶ Z) :
    f.inv â‰« g = f.inv â‰« g' â†” g = g' := by
  simp only [cancel_epi]

@[simp]
theorem cancel_iso_hom_right {X Y Z : C} (f f' : X âŸ¶ Y) (g : Y â‰… Z) :
    f â‰« g.hom = f' â‰« g.hom â†” f = f' := by
  simp only [cancel_mono]

@[simp]
theorem cancel_iso_inv_right {X Y Z : C} (f f' : X âŸ¶ Y) (g : Z â‰… Y) :
    f â‰« g.inv = f' â‰« g.inv â†” f = f' := by
  simp only [cancel_mono]

/-
Unfortunately cancelling an isomorphism from the right of a chain of compositions is awkward.
We would need separate lemmas for each chain length (worse: for each pair of chain lengths).

We provide two more lemmas, for case of three morphisms, because this actually comes up in practice,
but then stop.
-/
@[simp]
theorem cancel_iso_hom_right_assoc {W X X' Y Z : C} (f : W âŸ¶ X) (g : X âŸ¶ Y) (f' : W âŸ¶ X')
    (g' : X' âŸ¶ Y) (h : Y â‰… Z) : f â‰« g â‰« h.hom = f' â‰« g' â‰« h.hom â†” f â‰« g = f' â‰« g' := by
  simp only [â† Category.assoc, cancel_mono]

@[simp]
theorem cancel_iso_inv_right_assoc {W X X' Y Z : C} (f : W âŸ¶ X) (g : X âŸ¶ Y) (f' : W âŸ¶ X')
    (g' : X' âŸ¶ Y) (h : Z â‰… Y) : f â‰« g â‰« h.inv = f' â‰« g' â‰« h.inv â†” f â‰« g = f' â‰« g' := by
  simp only [â† Category.assoc, cancel_mono]

section

variable {D : Type*} [Category D] {X Y : C} (e : X â‰… Y)

@[reassoc (attr := simp), grind =]
lemma map_hom_inv_id (F : C â¥¤ D) :
    F.map e.hom â‰« F.map e.inv = ğŸ™ _ := by grind

@[reassoc (attr := simp), grind =]
lemma map_inv_hom_id (F : C â¥¤ D) :
    F.map e.inv â‰« F.map e.hom = ğŸ™ _ := by grind

end

end Iso

namespace Functor

universe uâ‚ vâ‚ uâ‚‚ vâ‚‚

variable {D : Type uâ‚‚}
variable [Category.{vâ‚‚} D]

/-- A functor `F : C â¥¤ D` sends isomorphisms `i : X â‰… Y` to isomorphisms `F.obj X â‰… F.obj Y` -/
@[simps]
def mapIso (F : C â¥¤ D) {X Y : C} (i : X â‰… Y) : F.obj X â‰… F.obj Y where
  hom := F.map i.hom
  inv := F.map i.inv

@[simp]
theorem mapIso_symm (F : C â¥¤ D) {X Y : C} (i : X â‰… Y) : F.mapIso i.symm = (F.mapIso i).symm :=
  rfl

@[simp]
theorem mapIso_trans (F : C â¥¤ D) {X Y Z : C} (i : X â‰… Y) (j : Y â‰… Z) :
    F.mapIso (i â‰ªâ‰« j) = F.mapIso i â‰ªâ‰« F.mapIso j := by
  ext; apply Functor.map_comp

@[simp]
theorem mapIso_refl (F : C â¥¤ D) (X : C) : F.mapIso (Iso.refl X) = Iso.refl (F.obj X) :=
  Iso.ext <| F.map_id X

instance map_isIso (F : C â¥¤ D) (f : X âŸ¶ Y) [IsIso f] : IsIso (F.map f) :=
  (F.mapIso (asIso f)).isIso_hom

@[simp]
theorem map_inv (F : C â¥¤ D) {X Y : C} (f : X âŸ¶ Y) [IsIso f] : F.map (inv f) = inv (F.map f) := by
  apply eq_inv_of_hom_inv_id
  simp [â† F.map_comp]

@[reassoc]
theorem map_hom_inv (F : C â¥¤ D) {X Y : C} (f : X âŸ¶ Y) [IsIso f] :
    F.map f â‰« F.map (inv f) = ğŸ™ (F.obj X) := by simp

@[reassoc]
theorem map_inv_hom (F : C â¥¤ D) {X Y : C} (f : X âŸ¶ Y) [IsIso f] :
    F.map (inv f) â‰« F.map f = ğŸ™ (F.obj Y) := by simp

end Functor

end CategoryTheory
