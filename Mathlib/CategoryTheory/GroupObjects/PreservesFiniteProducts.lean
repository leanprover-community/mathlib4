import Mathlib.CategoryTheory.GroupObjects.Basic
import Mathlib.CategoryTheory.GroupObjects.StupidLemmas
import Mathlib.CategoryTheory.Limits.Preserves.Finite
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Terminal
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Limits.Yoneda
open CategoryTheory Limits

noncomputable section

universe v u vâ‚ uâ‚ uâ‚‚ vâ‚‚

variable {C : Type u} [Category.{v} C] {D : Type uâ‚} [Category.{vâ‚} D] {E : Type uâ‚‚}
  [Category.{vâ‚‚, uâ‚‚} E]

namespace CategoryTheory.GroupObjectFunctor

variable [HasFiniteProducts C] [HasFiniteProducts D] [HasFiniteProducts E]
variable (F F' F'' : C â¥¤ D) [PreservesFiniteProducts F] [PreservesFiniteProducts F']
  [PreservesFiniteProducts F'']
variable {G G' : D â¥¤ E} [PreservesFiniteProducts G] [PreservesFiniteProducts G']
variable {H H' : E â¥¤ C} [PreservesFiniteProducts H] [PreservesFiniteProducts H']

/-- Lifting a functor `C â¥¤ D` that commutes with finite products to a functor between the
categories of group objects: the action on objects.-/
@[simps]
noncomputable def map_obj (G : GroupObject C) : GroupObject D where
  X := F.obj G.X
  one := (PreservesTerminal.iso F).inv â‰« F.map G.one
  mul := (PreservesLimitPair.iso F _ _).inv â‰« F.map G.mul
  inv := F.map G.inv
  one_mul := by
    rw [prod.map_comp_id]
    slice_lhs 2 3 =>
      rw [PreservesLimitPair.iso_inv, â† F.map_id, â† prodComparison_inv_natural]
    simp [â† F.map_comp, inv_prodComparison_map_snd]
  mul_one := by
    rw [prod.map_id_comp]
    slice_lhs 2 3 =>
      rw [PreservesLimitPair.iso_inv, â† F.map_id, â† prodComparison_inv_natural]
    simp [â† F.map_comp, inv_prodComparison_map_fst]
  mul_assoc := by
    rw [prod.map_comp_id, prod.map_id_comp]
    simp only [PreservesLimitPair.iso_inv]
    slice_lhs 2 3 =>
      rw [â† F.map_id, â† prodComparison_inv_natural]
    slice_lhs 3 4 =>
      rw [â† F.map_comp, G.mul_assoc]
    have := PreservesLimitsPair.iso.inv_comp_prod.associator G.X G.X G.X F
    simp only [PreservesLimitPair.iso_inv] at this
    simp only [F.map_comp]
    slice_lhs 1 3 =>
      rw [this]
    slice_rhs 3 4 =>
      rw [â† F.map_id, â† prodComparison_inv_natural]
    simp only [Category.assoc]
    rfl
  mul_left_inv := by
    slice_lhs 1 2 =>
      rw [â† F.map_id, PreservesLimitPair.iso.inv_comp_lift]
    rw [â† F.map_comp, G.mul_left_inv]
    simp only [Functor.map_comp, PreservesTerminal.iso_inv]
    rw [â† Category.assoc, default_comp_inv_terminalComparison]

/-- Lifting a functor `C â¥¤ D` that commutes with finite products to a functor between the
categories of group objects: the action on maps.-/
@[simps]
def map_map {X Y : GroupObject C}
    (f : X âŸ¶ Y) : map_obj F X âŸ¶ map_obj F Y  where
  hom := F.map f.hom
  one_hom := by simp [â† F.map_comp]
  mul_hom := by
    simp only [map_obj_X, map_obj_mul, PreservesLimitPair.iso_inv, Category.assoc,
      IsIso.inv_comp_eq]
    rw [â† F.map_comp]
    slice_rhs 2 3 =>
      rw [â† prodComparison_inv_natural]
    simp
  inv_hom := by
    simp only [map_obj_X, map_obj_inv]
    rw [â† F.map_comp, f.inv_hom, F.map_comp]

/-- Lifting a functor `C â¥¤ D` that commutes with finite products to a functor between the
categories of group objects.-/
@[simp]
noncomputable def map : GroupObject C â¥¤ GroupObject D where
  obj X := map_obj F X
  map f := map_map F f
  map_id X := by ext; simp
  map_comp f g := by ext; simp

noncomputable abbrev groupYoneda : GroupObject C â¥¤ GroupObject (Cáµ’áµ– â¥¤ Type v) :=
  map (yoneda (C := C))

/-- Lifting a functor `C â¥¤ D` that commutes with finite products to a functor between the
categories of group objects is compatible with the forgetful functors from the categories of
groups objects to the original categories.-/
@[simp]
noncomputable def map_comp_forget :
    map F â‹™ GroupObject.forget D â‰… GroupObject.forget C â‹™ F :=
  NatIso.ofComponents (fun _ â†¦ Iso.refl _)
  (fun _ â†¦ by simp only [map, Functor.comp_obj, GroupObject.forget_obj, map_obj_X,
    Functor.comp_map, GroupObject.forget_map, map_map_hom, Iso.refl_hom, Category.comp_id,
    Category.id_comp])

variable (C)

/-- If `F : C â¥¤ C` is the identity functor, then its lift to categories of group objects
is isomorphic (actually equal) to the identity functor.-/
@[simps!]
noncomputable def mapIdIso : map (ğŸ­ C) â‰… ğŸ­ (GroupObject C) := by
  refine NatIso.ofComponents ?_ ?_
  Â· intro X
    refine GroupObject.isoOfIso (Iso.refl _) ?_ ?_ ?_
    Â· simp only [Functor.id_obj, map, map_obj_X, map_obj_one, PreservesTerminal.iso_inv,
      Functor.id_map, Iso.refl_hom, Category.comp_id, IsIso.inv_comp_eq]
      rw [Subsingleton.elim (terminalComparison (ğŸ­ C)) (ğŸ™ _)]
      erw [Category.id_comp]
    Â· simp only [map, map_obj_X, Functor.id_obj, map_obj_mul, PreservesLimitPair.iso_inv,
      Functor.id_map, Iso.refl_hom, Category.comp_id, prod.map_id_id, Category.id_comp,
      IsIso.inv_comp_eq]
      suffices h : prodComparison (ğŸ­ C) X.X X.X = ğŸ™ _ by
        rw [h]; erw [Category.id_comp]
      ext
      Â· erw [prodComparison_fst]; simp only [Functor.id_obj, Functor.id_map, Category.id_comp]
      Â· erw [prodComparison_snd]; simp only [Functor.id_obj, Functor.id_map, Category.id_comp]
    Â· simp only [map, map_obj_X, Functor.id_obj, map_obj_inv, Functor.id_map, Iso.refl_hom,
      Category.comp_id, Category.id_comp]
  Â· aesop_cat


variable {C}
variable (G)

/-- The construction `map` is compatible with composition of functors.-/
@[simps!]
noncomputable def mapCompIso : map (F â‹™ G) â‰…
    map F â‹™ map G := by
  refine NatIso.ofComponents ?_ ?_
  Â· intro X
    refine GroupObject.isoOfIso (Iso.refl _) ?_ ?_ ?_
    Â· simp only [map, Functor.comp_obj, map_obj_X, map_obj_one, PreservesTerminal.iso_inv,
      Functor.comp_map, Iso.refl_hom, Category.comp_id, Functor.map_comp, Functor.map_inv,
      IsIso.eq_inv_comp]
      suffices h : G.map (terminalComparison F) â‰« terminalComparison G â‰«
        CategoryTheory.inv (terminalComparison (F â‹™ G)) = ğŸ™ _ by
        Â· rw [â† Category.assoc (terminalComparison G) _ _, â† Category.assoc, h,
            Category.id_comp]
      rw [â† Category.assoc, IsIso.comp_inv_eq, Category.id_comp]
      exact Subsingleton.elim _ _
    Â· simp only [map, map_obj_X, Functor.comp_obj, map_obj_mul, PreservesLimitPair.iso_inv,
      Functor.comp_map, Iso.refl_hom, Category.comp_id, prod.map_id_id, Functor.map_comp,
      Functor.map_inv, Category.id_comp, IsIso.eq_inv_comp]
      suffices h : G.map (prodComparison F X.X X.X) â‰« prodComparison G (F.obj X.X) (F.obj X.X) â‰«
        CategoryTheory.inv (prodComparison (F â‹™ G) X.X X.X) = ğŸ™ _ by
        Â· rw [â† Category.assoc (prodComparison G (F.obj X.X) (F.obj X.X)) _ _, â† Category.assoc,
          h, Category.id_comp]
      rw [â† Category.assoc, IsIso.comp_inv_eq, Category.id_comp]
      ext
      Â· simp only [Category.assoc, prodComparison_fst, Functor.comp_obj]
        erw [prodComparison_fst]; rw [â† Functor.map_comp, prodComparison_fst, Functor.comp_map]
      Â· simp only [Category.assoc, prodComparison_snd, Functor.comp_obj]
        erw [prodComparison_snd]; rw [â† Functor.map_comp, prodComparison_snd, Functor.comp_map]
    Â· simp only [map, map_obj_X, Functor.comp_obj, map_obj_inv, Functor.comp_map, Iso.refl_hom,
      Category.comp_id, Category.id_comp]
  Â· aesop_cat

/-- If `F : C â¥¤ D` is faithful, then so is the induced functor `map F` on
group objects.-/
lemma map_faithful [F.Faithful] : (map F).Faithful where
  map_injective := by
    intro X Y f g
    dsimp; intro eq; ext
    apply_fun (fun h â†¦ h.hom) at eq
    dsimp at eq
    exact F.map_injective eq

/-- If `F : C â¥¤ D` is fully faithful, then the induced functor `map F` on
group objects is full (it is also faithful by `map_faithful`).-/
lemma map_full [F.Faithful] [F.Full] : (map F).Full where
  map_surjective := by
    intro X Y h
    obtain âŸ¨f, hfâŸ© := F.map_surjective h.hom
    existsi {hom := f, one_hom := ?_, mul_hom := ?_, inv_hom := ?_}
    Â· refine F.map_injective (Epi.left_cancellation (f := (PreservesTerminal.iso F).inv) _ _ ?_)
      simp only [PreservesTerminal.iso_inv, Functor.map_comp, hf, map, IsIso.eq_inv_comp,
        IsIso.hom_inv_id_assoc]
      have := h.one_hom
      simp only [map, map_obj_X, map_obj_one, PreservesTerminal.iso_inv, Category.assoc,
        IsIso.eq_inv_comp, IsIso.hom_inv_id_assoc] at this
      exact this
    Â· refine F.map_injective (Epi.left_cancellation (f := (PreservesLimitPair.iso F _ _).inv)
        _ _ ?_)
      simp only [PreservesLimitPair.iso_inv, Functor.map_comp, IsIso.eq_inv_comp,
        IsIso.hom_inv_id_assoc]
      have := h.mul_hom
      simp only [map, map_obj_X, map_obj_mul, PreservesLimitPair.iso_inv, Category.assoc,
        IsIso.inv_comp_eq] at this
      rw [â† Category.assoc, â† hf, â† prodComparison_natural] at this
      simp only [Category.assoc, IsIso.hom_inv_id_assoc] at this
      exact this
    Â· refine F.map_injective ?_
      simp only [Functor.map_comp, hf]
      have := h.inv_hom
      simp only [map, map_obj_X, map_obj_inv] at this
      exact this
    ext; simp only [map, map_obj_X, map_map_hom, hf]

variable {G}

@[simp]
noncomputable def mapâ‚‚ (Î± : G âŸ¶ G') : map G âŸ¶ map G' := by
  refine { app := ?_, naturality := ?_}
  Â· intro X; dsimp
    refine {hom := Î±.app X.X, one_hom := ?_, mul_hom := ?_, inv_hom := ?_}
    Â· dsimp
      rw [Category.assoc, Î±.naturality, â† Category.assoc]
      congr 1
      simp only [PreservesTerminal.iso_inv, IsIso.inv_comp_eq, IsIso.eq_comp_inv]
      exact Subsingleton.elim _ _
    Â· simp only [map_obj_X, map_obj_mul, PreservesLimitPair.iso_inv, Category.assoc,
      NatTrans.naturality, IsIso.inv_comp_eq]
      slice_rhs 1 2 => rw [prodComparison_natTrans]
      simp only [Category.assoc, IsIso.hom_inv_id, Category.comp_id]
    Â· simp only [map_obj_X, map_obj_inv, NatTrans.naturality]
  Â· aesop_cat

variable (C D)

/-- The construction `map`, as a functor from the category of functors `C â¥¤ D`
that respect finite limits to the category of functors `GroupObject C â¥¤ GroupObject D`.-/
noncomputable def mapAsFunctor :
    FullSubcategory (fun (F : C â¥¤ D) â†¦ Nonempty (PreservesFiniteProducts F)) â¥¤
    GroupObject C â¥¤ GroupObject D where
  obj F := @map _ _ _ _ _ _ F.1 (Classical.choice F.2)
  map := by
    intro F G Î±
    exact @mapâ‚‚ _ _ _ _ _ _ F.1 G.1 (Classical.choice F.2)
      (Classical.choice G.2) Î±

variable {C D}

variable {F F'}

lemma mapComp_naturality_left (Î± : F âŸ¶ F') :
    mapâ‚‚ (whiskerRight Î± G) â‰« (mapCompIso F' G).hom =
    (mapCompIso F G).hom â‰« whiskerRight (mapâ‚‚ Î±) (map G) := by
  aesop_cat

variable (F)

lemma mapComp_naturality_right (Î± : G âŸ¶ G') :
    mapâ‚‚ (whiskerLeft F Î±) â‰« (mapCompIso F G').hom =
    (mapCompIso F G).hom â‰« whiskerLeft (map F) (mapâ‚‚ Î±) := by
  aesop_cat

lemma mapâ‚‚_id : mapâ‚‚ (ğŸ™ F) = ğŸ™ (map F) := by
  aesop_cat

variable {F''}

lemma mapâ‚‚_comp (Î± : F âŸ¶ F') (Î² : F' âŸ¶ F'') :
    mapâ‚‚ (Î± â‰« Î²) = mapâ‚‚ Î± â‰« mapâ‚‚ Î² := by
  aesop_cat

variable {F}

lemma mapâ‚‚_associator : mapâ‚‚ (Functor.associator F G H).hom â‰«
    (mapCompIso F (G â‹™ H)).hom â‰« whiskerLeft (map F)
    (mapCompIso G H).hom = (mapCompIso (F â‹™ G) H).hom â‰«
    whiskerRight (mapCompIso F G).hom
    (map H) â‰« (Functor.associator (map F) (map G)
    (map H)).hom := by
  aesop_cat

variable (F)

lemma mapâ‚‚_leftUnitor :
    mapâ‚‚ F.leftUnitor.hom = (mapCompIso (ğŸ­ C) F).hom â‰«
    whiskerRight (mapIdIso C).hom (map F) â‰«
    (map F).leftUnitor.hom := by
  aesop_cat

lemma mapâ‚‚_rightUnitor :
    mapâ‚‚ F.rightUnitor.hom = (mapCompIso F (ğŸ­ D)).hom â‰«
    whiskerLeft (map F) (mapIdIso D).hom â‰«
    (map F).rightUnitor.hom := by
  aesop_cat

end CategoryTheory.GroupObjectFunctor
