/-
Copyright (c) 2024 Sina Hazratpour. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sina Hazratpour
-/
import Mathlib.CategoryTheory.Functor.FullyFaithful
import Mathlib.CategoryTheory.MorphismProperty.Composition

/-!
# Wide subcategories

A wide subcategory of a category `C` is a subcategory containing all the objects of `C`.

## Main declarations

Given a category `D`, a function `F : C ‚Üí D` from a type `C` to the objects of `D`,
and a morphism property `P` on `D` which contains identities and is stable under
composition, the type class `InducedWideCategory D F P` is a typeclass
synonym for `C` which comes equipped with a category structure whose morphisms `X ‚ü∂ Y` are the
morphisms in `D` which have the property `P`.

The instance `WideSubcategory.category` provides a category structure on `WideSubcategory P`
whose objects are the objects of `C` and morphisms are the morphisms in `C` which have the
property `P`.
-/

namespace CategoryTheory

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

open MorphismProperty

section Induced

variable {C : Type u‚ÇÅ} (D : Type u‚ÇÇ) [Category.{v‚ÇÅ} D]
variable (F : C ‚Üí D) (P : MorphismProperty D) [P.IsMultiplicative]

/-- `InducedWideCategory D F P`, where `F : C ‚Üí D`, is a typeclass synonym for `C`,
which provides a category structure so that the morphisms `X ‚ü∂ Y` are the morphisms
in `D` from `F X` to `F Y` which satisfy a property `P : MorphismProperty D` that is multiplicative.
-/
@[nolint unusedArguments]
def InducedWideCategory (_F : C ‚Üí D) (_P : MorphismProperty D) [IsMultiplicative _P] :=
  C

variable {D}

instance InducedWideCategory.hasCoeToSort {Œ± : Sort*} [CoeSort D Œ±] :
    CoeSort (InducedWideCategory D F P) Œ± :=
  ‚ü®fun c => F c‚ü©

@[simps!]
instance InducedWideCategory.category :
    Category (InducedWideCategory D F P) where
  Hom X Y := {f : F X ‚ü∂ F Y | P f}
  id X := ‚ü®ùüô (F X), P.id_mem (F X)‚ü©
  comp {_ _ _} f g := ‚ü®f.1 ‚â´ g.1, P.comp_mem _ _ f.2 g.2‚ü©

/-- The forgetful functor from an induced wide category to the original category. -/
@[simps]
def wideInducedFunctor : InducedWideCategory D F P ‚•§ D where
  obj := F
  map {_ _} f := f.1

/-- The induced functor `wideInducedFunctor F P : InducedWideCategory D F P ‚•§ D`
is faithful. -/
instance InducedWideCategory.faithful : (wideInducedFunctor F P).Faithful where
  map_injective {X Y} f g eq := by
    cases f
    cases g
    aesop

end Induced

section WideSubcategory

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]
variable (P : MorphismProperty C) [IsMultiplicative P]

/--
Structure for wide subcategories. Objects ignore the morphism property.
-/
@[ext, nolint unusedArguments]
structure WideSubcategory (_P : MorphismProperty C) [IsMultiplicative _P] where
  /-- The category of which this is a wide subcategory -/
  obj : C

instance WideSubcategory.category : Category.{v‚ÇÅ} (WideSubcategory P) :=
  InducedWideCategory.category WideSubcategory.obj P

@[simp]
lemma WideSubcategory.id_def (X : WideSubcategory P) : (CategoryStruct.id X).1 = ùüô X.obj := rfl

@[simp]
lemma WideSubcategory.comp_def {X Y Z : WideSubcategory P} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).1 = (f.1 ‚â´ g.1 : X.obj ‚ü∂ Z.obj) := rfl

/-- The forgetful functor from a wide subcategory into the original category
("forgetting" the condition).
-/
def wideSubcategoryInclusion : WideSubcategory P ‚•§ C :=
  wideInducedFunctor WideSubcategory.obj P

@[simp]
theorem wideSubcategoryInclusion.obj (X) : (wideSubcategoryInclusion P).obj X = X.obj :=
  rfl

@[simp]
theorem wideSubcategoryInclusion.map {X Y} {f : X ‚ü∂ Y} :
    (wideSubcategoryInclusion P).map f = f.1 :=
  rfl

/-- The inclusion of a wide subcategory is faithful. -/
instance wideSubcategory.faithful : (wideSubcategoryInclusion P).Faithful :=
  inferInstanceAs (wideInducedFunctor WideSubcategory.obj P).Faithful

end WideSubcategory

end CategoryTheory
