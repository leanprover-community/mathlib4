/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten, Andrew Yang
-/
import Mathlib.CategoryTheory.Sites.Hypercover.Zero
import Mathlib.CategoryTheory.MorphismProperty.Limits

/-!
# Locality conditions on morphism properties

In this file we define locality conditions on morphism properties in a category. Let `K` be a
precoverage in a category `C` and `P` be a morphism property on `C` that respects isomorphisms.

We say that

- `P` is local at the target if for every `f : X âŸ¶ Y`, `P` holds for `f` if and only if it holds
  for the restrictions of `f` to `Uáµ¢` for a
  `K`-cover `{Uáµ¢}` of `Y`.
- `P` is local at the source if for every `f : X âŸ¶ Y`, `P` holds for `f` if and only if it holds
  for the restrictions of `f` to `Uáµ¢` for a `K`-cover `{Uáµ¢}` of `X`.

## Implementation details

The covers appearing in the definitions have index type in the morphism universe of `C`.

## TODOs

- Define source and target local closure of a morphism property.
-/

universe w v u

namespace CategoryTheory

open Limits

variable {C : Type u} [Category.{v} C]

namespace MorphismProperty

variable (K : Precoverage C)

/--
A property of morphisms `P` in `C` is local at the target with respect to the precoverage `K` if
it respects ismorphisms, and:
`P` holds for `f : X âŸ¶ Y` if and only if it holds for the restrictions of `f` to `Uáµ¢` for a
`0`-hypercover `{Uáµ¢}` of `Y` in the precoverage `K`.
-/
class IsLocalAtTarget (P : MorphismProperty C) (K : Precoverage C) [K.HasPullbacks]
    extends RespectsIso P where
  /-- If `P` holds for `f : X âŸ¶ Y`, it also holds for `f` restricted to `Uáµ¢` for any
  `K`-cover `ğ’°` of `Y`. -/
  pullbackSnd {X Y : C} {f : X âŸ¶ Y} (ğ’° : Precoverage.ZeroHypercover.{v} K Y)
    (i : ğ’°.Iâ‚€) (hf : P f) : P (pullback.snd f (ğ’°.f i))
  /-- If `P` holds for `f` restricted to `Uáµ¢` for all `i`, it also holds for `f : X âŸ¶ Y` for any
  `K`-cover `ğ’°` of `Y`. -/
  of_zeroHypercover {X Y : C} {f : X âŸ¶ Y} (ğ’° : Precoverage.ZeroHypercover.{v} K Y)
    (h : âˆ€ i, P (pullback.snd f (ğ’°.f i))) : P f

namespace IsLocalAtTarget

variable {P : MorphismProperty C} {K L : Precoverage C} [K.HasPullbacks]

lemma mk_of_iff [P.RespectsIso]
    (H : âˆ€ {X Y : C} (f : X âŸ¶ Y) (ğ’° : Precoverage.ZeroHypercover.{v} K Y),
      P f â†” âˆ€ i, P (pullback.snd f (ğ’°.f i))) :
    P.IsLocalAtTarget K where
  pullbackSnd ğ’° i h := (H _ ğ’°).mp h i
  of_zeroHypercover ğ’° h := (H _ ğ’°).mpr h

lemma mk_of_isStableUnderBaseChange [P.IsStableUnderBaseChange]
    (H : âˆ€ {X Y : C} (f : X âŸ¶ Y) (ğ’° : Precoverage.ZeroHypercover.{v} K Y),
      (âˆ€ i, P (pullback.snd f (ğ’°.f i))) â†’ P f) :
    P.IsLocalAtTarget K where
  pullbackSnd _ _ hf := P.pullback_snd _ _ hf
  of_zeroHypercover _ := H _ _

lemma of_le [L.HasPullbacks] [IsLocalAtTarget P L] (hle : K â‰¤ L) : IsLocalAtTarget P K where
  pullbackSnd ğ’° i hf := pullbackSnd (ğ’°.weaken hle) i hf
  of_zeroHypercover ğ’° := of_zeroHypercover (ğ’°.weaken hle)

instance top : IsLocalAtTarget (âŠ¤ : MorphismProperty C) K where
  pullbackSnd := by simp
  of_zeroHypercover := by simp

variable [IsLocalAtTarget P K] {X Y : C} {f : X âŸ¶ Y} (ğ’° : Precoverage.ZeroHypercover.{v} K Y)

lemma of_isPullback {X' : C} (i : ğ’°.Iâ‚€) {fst : X' âŸ¶ X} {snd : X' âŸ¶ ğ’°.X i}
    (h : IsPullback fst snd f (ğ’°.f i)) (hf : P f) :
    P snd := by
  rw [â† P.cancel_left_of_respectsIso h.isoPullback.inv, h.isoPullback_inv_snd]
  exact pullbackSnd _ _ hf

lemma iff_of_zeroHypercover : P f â†” âˆ€ i, P (pullback.snd f (ğ’°.f i)) :=
  âŸ¨fun hf _ â†¦ pullbackSnd _ _ hf, fun h â†¦ of_zeroHypercover _ hâŸ©

instance inf (P Q : MorphismProperty C) [IsLocalAtTarget P K] [IsLocalAtTarget Q K] :
    IsLocalAtTarget (P âŠ“ Q) K where
  pullbackSnd _ i h := âŸ¨pullbackSnd _ i h.1, pullbackSnd _ i h.2âŸ©
  of_zeroHypercover _ h :=
    âŸ¨of_zeroHypercover _ fun i â†¦ (h i).1, of_zeroHypercover _ fun i â†¦ (h i).2âŸ©

end IsLocalAtTarget

alias of_zeroHypercover_target := IsLocalAtTarget.of_zeroHypercover
alias iff_of_zeroHypercover_target := IsLocalAtTarget.iff_of_zeroHypercover

/--
A property of morphisms `P` in `C` is local at the source with respect to the precoverage `K` if
it respects ismorphisms, and:
`P` holds for `f : X âŸ¶ Y` if and only if it holds for the restrictions of `f` to `Uáµ¢` for a
`0`-hypercover `{Uáµ¢}` of `X` in the precoverage `K`.
-/
class IsLocalAtSource (P : MorphismProperty C) (K : Precoverage C) extends RespectsIso P where
  /-- If `P` holds for `f : X âŸ¶ Y`, it also holds for `ğ’°.f i â‰« f` for any `K`-cover `ğ’°` of `X`. -/
  comp {X Y : C} {f : X âŸ¶ Y} (ğ’° : Precoverage.ZeroHypercover.{v} K X) (i : ğ’°.Iâ‚€)
    (hf : P f) : P (ğ’°.f i â‰« f)
  /-- If `P` holds for `ğ’°.f i â‰« f` for all `i`, it holds for `f : X âŸ¶ Y` for any `K`-cover
  `ğ’°` of X. -/
  of_zeroHypercover {X Y : C} {f : X âŸ¶ Y} (ğ’° : Precoverage.ZeroHypercover.{v} K X) :
    (âˆ€ i, P (ğ’°.f i â‰« f)) â†’ P f

namespace IsLocalAtSource

variable {P : MorphismProperty C} {K L : Precoverage C}

lemma mk_of_iff [P.RespectsIso]
    (H : âˆ€ {X Y : C} (f : X âŸ¶ Y) (ğ’° : Precoverage.ZeroHypercover.{v} K X),
      P f â†” âˆ€ i, P (ğ’°.f i â‰« f)) :
    P.IsLocalAtSource K where
  comp ğ’° i h := (H _ ğ’°).mp h i
  of_zeroHypercover ğ’° h := (H _ ğ’°).mpr h

lemma of_le [IsLocalAtSource P L] (hle : K â‰¤ L) : IsLocalAtSource P K where
  comp ğ’° i hf := comp (ğ’°.weaken hle) i hf
  of_zeroHypercover ğ’° h := of_zeroHypercover (ğ’°.weaken hle) h

instance top : IsLocalAtSource (âŠ¤ : MorphismProperty C) K where
  comp := by simp
  of_zeroHypercover := by simp

variable [IsLocalAtSource P K] {X Y : C} {f : X âŸ¶ Y} (ğ’° : Precoverage.ZeroHypercover.{v} K X)

lemma iff_of_zeroHypercover : P f â†” âˆ€ i, P (ğ’°.f i â‰« f) :=
  âŸ¨fun hf i â†¦ comp _ i hf, fun h â†¦ of_zeroHypercover _ hâŸ©

instance inf (P Q : MorphismProperty C) [IsLocalAtSource P K] [IsLocalAtSource Q K] :
    IsLocalAtSource (P âŠ“ Q) K where
  comp ğ’° i hf := âŸ¨comp ğ’° i hf.1, comp ğ’° i hf.2âŸ©
  of_zeroHypercover _ h :=
    âŸ¨of_zeroHypercover _ fun i â†¦ (h i).1, of_zeroHypercover _ fun i â†¦ (h i).2âŸ©

end IsLocalAtSource

alias of_zeroHypercover_source := IsLocalAtSource.of_zeroHypercover
alias iff_of_zeroHypercover_source := IsLocalAtSource.iff_of_zeroHypercover

end MorphismProperty

end CategoryTheory
