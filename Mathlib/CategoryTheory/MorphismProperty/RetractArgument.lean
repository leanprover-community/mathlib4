/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import Mathlib.CategoryTheory.MorphismProperty.Factorization
import Mathlib.CategoryTheory.MorphismProperty.LiftingProperty

/-!
# The retract argument

Let `Wâ‚` and `Wâ‚‚` be classes of morphisms in a category `C` such that
any morphism can be factored as a morphism in `Wâ‚` followed by
a morphism in `Wâ‚‚` (this is `HasFactorization Wâ‚ Wâ‚‚`).
If `Wâ‚` has the left lifting property with respect to `Wâ‚‚`
(i.e. `Wâ‚ â‰¤ Wâ‚‚.llp`, or equivalently `Wâ‚‚ â‰¤ Wâ‚.rlp`),
then `Wâ‚‚.llp = Wâ‚` if `Wâ‚` is stable under retracts,
and `Wâ‚.rlp = Wâ‚‚` if `Wâ‚‚` is.

## Reference
- https://ncatlab.org/nlab/show/weak+factorization+system#retract_argument

-/

namespace CategoryTheory

variable {C : Type*} [Category C]

/-- If `i : X âŸ¶ Y` and `p : Y âŸ¶ Z` are composable morphisms,
and `i â‰« p` has the left lifting property with respect to `p`,
then `i â‰« p` is a retract of `i`. -/
noncomputable def RetractArrow.ofLeftLiftingProperty
    {X Y Z : C} (i : X âŸ¶ Y) (p : Y âŸ¶ Z)
    [HasLiftingProperty (i â‰« p) p] : RetractArrow (i â‰« p) i :=
  have sq : CommSq i (i â‰« p) p (ğŸ™ _) := âŸ¨by simpâŸ©
  { i := Arrow.homMk (u := ğŸ™ X) (v := sq.lift) (by simp)
    r := Arrow.homMk (u := ğŸ™ X) (v := p) (by simp) }

/-- If `i : X âŸ¶ Y` and `p : Y âŸ¶ Z` are composable morphisms,
and `i â‰« p` has the right lifting property with respect to `i`,
then `i â‰« p` is a retract of `p`. -/
noncomputable def RetractArrow.ofRightLiftingProperty
    {X Y Z : C} (i : X âŸ¶ Y) (p : Y âŸ¶ Z)
    [HasLiftingProperty i (i â‰« p)] : RetractArrow (i â‰« p) p :=
  have sq : CommSq (ğŸ™ _) i (i â‰« p) p := âŸ¨by simpâŸ©
  { i := Arrow.homMk (u := i) (v := ğŸ™ _) (by simp)
    r := Arrow.homMk (u := sq.lift) (v := ğŸ™ _) (by simp) }

namespace MorphismProperty

variable {Wâ‚ Wâ‚‚ : MorphismProperty C}

lemma llp_eq_of_le_llp_of_hasFactorization_of_isStableUnderRetracts
    [HasFactorization Wâ‚ Wâ‚‚] [Wâ‚.IsStableUnderRetracts] (hâ‚ : Wâ‚ â‰¤ Wâ‚‚.llp) :
    Wâ‚‚.llp = Wâ‚ :=
  le_antisymm (by
    intro A B i hi
    have h := factorizationData Wâ‚ Wâ‚‚ i
    have : HasLiftingProperty (h.i â‰« h.p) h.p := by simpa using hi _ h.hp
    simpa using of_retract (RetractArrow.ofLeftLiftingProperty h.i h.p) h.hi) hâ‚

lemma rlp_eq_of_le_rlp_of_hasFactorization_of_isStableUnderRetracts
    [HasFactorization Wâ‚ Wâ‚‚] [Wâ‚‚.IsStableUnderRetracts] (hâ‚‚ : Wâ‚‚ â‰¤ Wâ‚.rlp) :
    Wâ‚.rlp = Wâ‚‚ :=
  le_antisymm (by
    intro X Y p hp
    have h := factorizationData Wâ‚ Wâ‚‚ p
    have : HasLiftingProperty h.i (h.i â‰« h.p) := by simpa using hp _ h.hi
    simpa using of_retract (RetractArrow.ofRightLiftingProperty h.i h.p) h.hp) hâ‚‚

end MorphismProperty

end CategoryTheory
