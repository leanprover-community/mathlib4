/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.CategoryTheory.Comma.Arrow
import Mathlib.Order.CompleteBooleanAlgebra

/-!
# Properties of morphisms

We provide the basic framework for talking about properties of morphisms.
The following meta-property is defined

* `RespectsLeft P Q`: `P` respects the property `Q` on the left if `P f ‚Üí P (i ‚â´ f)` where
  `i` satisfies `Q`.
* `RespectsRight P Q`: `P` respects the property `Q` on the right if `P f ‚Üí P (f ‚â´ i)` where
  `i` satisfies `Q`.
* `Respects`: `P` respects `Q` if `P` respects `Q` both on the left and on the right.

-/


universe w v v' u u'

open CategoryTheory Opposite

noncomputable section

namespace CategoryTheory

variable (C : Type u) [Category.{v} C] {D : Type*} [Category D]

/-- A `MorphismProperty C` is a class of morphisms between objects in `C`. -/
def MorphismProperty :=
  ‚àÄ ‚¶ÉX Y : C‚¶Ñ (_ : X ‚ü∂ Y), Prop

instance : CompleteBooleanAlgebra (MorphismProperty C) where
  le P‚ÇÅ P‚ÇÇ := ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), P‚ÇÅ f ‚Üí P‚ÇÇ f
  __ := inferInstanceAs (CompleteBooleanAlgebra (‚àÄ ‚¶ÉX Y : C‚¶Ñ (_ : X ‚ü∂ Y), Prop))

lemma MorphismProperty.le_def {P Q : MorphismProperty C} :
    P ‚â§ Q ‚Üî ‚àÄ {X Y : C} (f : X ‚ü∂ Y), P f ‚Üí Q f := Iff.rfl

instance : Inhabited (MorphismProperty C) :=
  ‚ü®‚ä§‚ü©

lemma MorphismProperty.top_eq : (‚ä§ : MorphismProperty C) = fun _ _ _ ‚Ü¶ True := rfl

variable {C}

namespace MorphismProperty

@[ext]
lemma ext (W W' : MorphismProperty C) (h : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), W f ‚Üî W' f) :
    W = W' := by
  funext X Y f
  rw [h]

@[simp]
lemma top_apply {X Y : C} (f : X ‚ü∂ Y) : (‚ä§ : MorphismProperty C) f := by
  simp only [top_eq]

lemma of_eq_top {P : MorphismProperty C} (h : P = ‚ä§) {X Y : C} (f : X ‚ü∂ Y) : P f := by
  simp [h]

@[simp]
lemma sSup_iff (S : Set (MorphismProperty C)) {X Y : C} (f : X ‚ü∂ Y) :
    sSup S f ‚Üî ‚àÉ (W : S), W.1 f := by
  dsimp [sSup, iSup]
  constructor
  ¬∑ rintro ‚ü®_, ‚ü®‚ü®_, ‚ü®‚ü®_, ‚ü®_, h‚ü©, rfl‚ü©, rfl‚ü©‚ü©, rfl‚ü©, hf‚ü©
    exact ‚ü®‚ü®_, h‚ü©, hf‚ü©
  ¬∑ rintro ‚ü®‚ü®W, hW‚ü©, hf‚ü©
    exact ‚ü®_, ‚ü®‚ü®_, ‚ü®_, ‚ü®‚ü®W, hW‚ü©, rfl‚ü©‚ü©, rfl‚ü©, rfl‚ü©, hf‚ü©

@[simp]
lemma iSup_iff {Œπ : Sort*} (W : Œπ ‚Üí MorphismProperty C) {X Y : C} (f : X ‚ü∂ Y) :
    iSup W f ‚Üî ‚àÉ i, W i f := by
  apply (sSup_iff (Set.range W) f).trans
  constructor
  ¬∑ rintro ‚ü®‚ü®_, i, rfl‚ü©, hf‚ü©
    exact ‚ü®i, hf‚ü©
  ¬∑ rintro ‚ü®i, hf‚ü©
    exact ‚ü®‚ü®_, i, rfl‚ü©, hf‚ü©

/-- The morphism property in `C·µí·µñ` associated to a morphism property in `C` -/
@[simp]
def op (P : MorphismProperty C) : MorphismProperty C·µí·µñ := fun _ _ f ‚Ü¶ P f.unop

/-- The morphism property in `C` associated to a morphism property in `C·µí·µñ` -/
@[simp]
def unop (P : MorphismProperty C·µí·µñ) : MorphismProperty C := fun _ _ f ‚Ü¶ P f.op

theorem unop_op (P : MorphismProperty C) : P.op.unop = P :=
  rfl

theorem op_unop (P : MorphismProperty C·µí·µñ) : P.unop.op = P :=
  rfl

/-- The inverse image of a `MorphismProperty D` by a functor `C ‚•§ D` -/
def inverseImage (P : MorphismProperty D) (F : C ‚•§ D) : MorphismProperty C := fun _ _ f ‚Ü¶
  P (F.map f)

@[simp]
lemma inverseImage_iff (P : MorphismProperty D) (F : C ‚•§ D) {X Y : C} (f : X ‚ü∂ Y) :
    P.inverseImage F f ‚Üî P (F.map f) := by rfl

/-- The image (up to isomorphisms) of a `MorphismProperty C` by a functor `C ‚•§ D` -/
def map (P : MorphismProperty C) (F : C ‚•§ D) : MorphismProperty D := fun _ _ f ‚Ü¶
  ‚àÉ (X' Y' : C) (f' : X' ‚ü∂ Y') (_ : P f'), Nonempty (Arrow.mk (F.map f') ‚âÖ Arrow.mk f)

lemma map_mem_map (P : MorphismProperty C) (F : C ‚•§ D) {X Y : C} (f : X ‚ü∂ Y) (hf : P f) :
    (P.map F) (F.map f) := ‚ü®X, Y, f, hf, ‚ü®Iso.refl _‚ü©‚ü©

lemma monotone_map (F : C ‚•§ D) :
    Monotone (map ¬∑ F) := by
  intro P Q h X Y f ‚ü®X', Y', f', hf', ‚ü®e‚ü©‚ü©
  exact ‚ü®X', Y', f', h _ hf', ‚ü®e‚ü©‚ü©

section

variable (P : MorphismProperty C)

/-- The set in `Set (Arrow C)` which corresponds to `P : MorphismProperty C`. -/
def toSet : Set (Arrow C) := setOf (fun f ‚Ü¶ P f.hom)

/-- The family of morphisms indexed by `P.toSet` which corresponds
to `P : MorphismProperty C`, see `MorphismProperty.ofHoms_homFamily`. -/
def homFamily (f : P.toSet) : f.1.left ‚ü∂ f.1.right := f.1.hom

lemma homFamily_apply (f : P.toSet) : P.homFamily f = f.1.hom := rfl

@[simp]
lemma homFamily_arrow_mk {X Y : C} (f : X ‚ü∂ Y) (hf : P f) :
    P.homFamily ‚ü®Arrow.mk f, hf‚ü© = f := rfl

@[simp]
lemma arrow_mk_mem_toSet_iff {X Y : C} (f : X ‚ü∂ Y) : Arrow.mk f ‚àà P.toSet ‚Üî P f := Iff.rfl

lemma of_eq {X Y : C} {f : X ‚ü∂ Y} (hf : P f)
    {X' Y' : C} {f' : X' ‚ü∂ Y'}
    (hX : X = X') (hY : Y = Y') (h : f' = eqToHom hX.symm ‚â´ f ‚â´ eqToHom hY) :
    P f' := by
  rw [‚Üê P.arrow_mk_mem_toSet_iff] at hf ‚ä¢
  rwa [(Arrow.mk_eq_mk_iff f' f).2 ‚ü®hX.symm, hY.symm, h‚ü©]

end

/-- The class of morphisms given by a family of morphisms `f i : X i ‚ü∂ Y i`. -/
inductive ofHoms {Œπ : Type*} {X Y : Œπ ‚Üí C} (f : ‚àÄ i, X i ‚ü∂ Y i) : MorphismProperty C
  | mk (i : Œπ) : ofHoms f (f i)

lemma ofHoms_iff {Œπ : Type*} {X Y : Œπ ‚Üí C} (f : ‚àÄ i, X i ‚ü∂ Y i) {A B : C} (g : A ‚ü∂ B) :
    ofHoms f g ‚Üî ‚àÉ i, Arrow.mk g = Arrow.mk (f i) := by
  constructor
  ¬∑ rintro ‚ü®i‚ü©
    exact ‚ü®i, rfl‚ü©
  ¬∑ rintro ‚ü®i, h‚ü©
    rw [‚Üê (ofHoms f).arrow_mk_mem_toSet_iff, h, arrow_mk_mem_toSet_iff]
    constructor

@[simp]
lemma ofHoms_homFamily (P : MorphismProperty C) : ofHoms P.homFamily = P := by
  ext _ _ f
  constructor
  ¬∑ intro hf
    rw [ofHoms_iff] at hf
    obtain ‚ü®‚ü®f, hf‚ü©, ‚ü®_, _‚ü©‚ü© := hf
    exact hf
  ¬∑ intro hf
    exact ‚ü®(‚ü®f, hf‚ü© : P.toSet)‚ü©

/-- A morphism property `P` satisfies `P.RespectsRight Q` if it is stable under post-composition
with morphisms satisfying `Q`, i.e. whenever `P` holds for `f` and `Q` holds for `i` then `P`
holds for `f ‚â´ i`. -/
class RespectsRight (P Q : MorphismProperty C) : Prop where
  postcomp {X Y Z : C} (i : Y ‚ü∂ Z) (hi : Q i) (f : X ‚ü∂ Y) (hf : P f) : P (f ‚â´ i)

/-- A morphism property `P` satisfies `P.RespectsLeft Q` if it is stable under
pre-composition with morphisms satisfying `Q`, i.e. whenever `P` holds for `f`
and `Q` holds for `i` then `P` holds for `i ‚â´ f`. -/
class RespectsLeft (P Q : MorphismProperty C) : Prop where
  precomp {X Y Z : C} (i : X ‚ü∂ Y) (hi : Q i) (f : Y ‚ü∂ Z) (hf : P f) : P (i ‚â´ f)

/-- A morphism property `P` satisfies `P.Respects Q` if it is stable under composition on the
left and right by morphisms satisfying `Q`. -/
class Respects (P Q : MorphismProperty C) : Prop extends P.RespectsLeft Q, P.RespectsRight Q where

instance (P Q : MorphismProperty C) [P.RespectsLeft Q] [P.RespectsRight Q] : P.Respects Q where

instance (P Q : MorphismProperty C) [P.RespectsLeft Q] : P.op.RespectsRight Q.op where
  postcomp i hi f hf := RespectsLeft.precomp (Q := Q) i.unop hi f.unop hf

instance (P Q : MorphismProperty C) [P.RespectsRight Q] : P.op.RespectsLeft Q.op where
  precomp i hi f hf := RespectsRight.postcomp (Q := Q) i.unop hi f.unop hf

instance RespectsLeft.inf (P‚ÇÅ P‚ÇÇ Q : MorphismProperty C) [P‚ÇÅ.RespectsLeft Q]
    [P‚ÇÇ.RespectsLeft Q] : (P‚ÇÅ ‚äì P‚ÇÇ).RespectsLeft Q where
  precomp i hi f hf := ‚ü®precomp i hi f hf.left, precomp i hi f hf.right‚ü©

instance RespectsRight.inf (P‚ÇÅ P‚ÇÇ Q : MorphismProperty C) [P‚ÇÅ.RespectsRight Q]
    [P‚ÇÇ.RespectsRight Q] : (P‚ÇÅ ‚äì P‚ÇÇ).RespectsRight Q where
  postcomp i hi f hf := ‚ü®postcomp i hi f hf.left, postcomp i hi f hf.right‚ü©

variable (C)

/-- The `MorphismProperty C` satisfied by isomorphisms in `C`. -/
def isomorphisms : MorphismProperty C := fun _ _ f ‚Ü¶ IsIso f

/-- The `MorphismProperty C` satisfied by monomorphisms in `C`. -/
def monomorphisms : MorphismProperty C := fun _ _ f ‚Ü¶ Mono f

/-- The `MorphismProperty C` satisfied by epimorphisms in `C`. -/
def epimorphisms : MorphismProperty C := fun _ _ f ‚Ü¶ Epi f

section

variable {C}

/-- `P` respects isomorphisms, if it respects the morphism property `isomorphisms C`, i.e.
it is stable under pre- and postcomposition with isomorphisms. -/
abbrev RespectsIso (P : MorphismProperty C) : Prop := P.Respects (isomorphisms C)

lemma RespectsIso.mk (P : MorphismProperty C)
    (hprecomp : ‚àÄ {X Y Z : C} (e : X ‚âÖ Y) (f : Y ‚ü∂ Z) (_ : P f), P (e.hom ‚â´ f))
    (hpostcomp : ‚àÄ {X Y Z : C} (e : Y ‚âÖ Z) (f : X ‚ü∂ Y) (_ : P f), P (f ‚â´ e.hom)) :
    P.RespectsIso where
  precomp e (_ : IsIso e) f hf := hprecomp (asIso e) f hf
  postcomp e (_ : IsIso e) f hf := hpostcomp (asIso e) f hf

lemma RespectsIso.precomp (P : MorphismProperty C) [P.RespectsIso] {X Y Z : C} (e : X ‚ü∂ Y)
    [IsIso e] (f : Y ‚ü∂ Z) (hf : P f) : P (e ‚â´ f) :=
  RespectsLeft.precomp (Q := isomorphisms C) e ‚ÄπIsIso e‚Ä∫ f hf

instance : RespectsIso (‚ä§ : MorphismProperty C) where
  precomp _ _ _ _ := trivial
  postcomp _ _ _ _ := trivial

lemma RespectsIso.postcomp (P : MorphismProperty C) [P.RespectsIso] {X Y Z : C} (e : Y ‚ü∂ Z)
    [IsIso e] (f : X ‚ü∂ Y) (hf : P f) : P (f ‚â´ e) :=
  RespectsRight.postcomp (Q := isomorphisms C) e ‚ÄπIsIso e‚Ä∫ f hf

instance RespectsIso.op (P : MorphismProperty C) [RespectsIso P] : RespectsIso P.op where
  precomp e (_ : IsIso e) f hf := postcomp P e.unop f.unop hf
  postcomp e (_ : IsIso e) f hf := precomp P e.unop f.unop hf

instance RespectsIso.unop (P : MorphismProperty C·µí·µñ) [RespectsIso P] : RespectsIso P.unop where
  precomp e (_ : IsIso e) f hf := postcomp P e.op f.op hf
  postcomp e (_ : IsIso e) f hf := precomp P e.op f.op hf

/-- The closure by isomorphisms of a `MorphismProperty` -/
def isoClosure (P : MorphismProperty C) : MorphismProperty C :=
  fun _ _ f ‚Ü¶ ‚àÉ (Y‚ÇÅ Y‚ÇÇ : C) (f' : Y‚ÇÅ ‚ü∂ Y‚ÇÇ) (_ : P f'), Nonempty (Arrow.mk f' ‚âÖ Arrow.mk f)

lemma le_isoClosure (P : MorphismProperty C) : P ‚â§ P.isoClosure :=
  fun _ _ f hf ‚Ü¶ ‚ü®_, _, f, hf, ‚ü®Iso.refl _‚ü©‚ü©

instance isoClosure_respectsIso (P : MorphismProperty C) :
    RespectsIso P.isoClosure where
  precomp := fun e (he : IsIso e) f ‚ü®_, _, f', hf', ‚ü®iso‚ü©‚ü© ‚Ü¶ ‚ü®_, _, f', hf',
      ‚ü®Arrow.isoMk (asIso iso.hom.left ‚â™‚â´ asIso (inv e)) (asIso iso.hom.right) (by simp)‚ü©‚ü©
  postcomp := fun e (he : IsIso e) f ‚ü®_, _, f', hf', ‚ü®iso‚ü©‚ü© ‚Ü¶ ‚ü®_, _, f', hf',
      ‚ü®Arrow.isoMk (asIso iso.hom.left) (asIso iso.hom.right ‚â™‚â´ asIso e) (by simp)‚ü©‚ü©

lemma monotone_isoClosure : Monotone (isoClosure (C := C)) := by
  intro P Q h X Y f ‚ü®X', Y', f', hf', ‚ü®e‚ü©‚ü©
  exact ‚ü®X', Y', f', h _ hf', ‚ü®e‚ü©‚ü©

theorem cancel_left_of_respectsIso (P : MorphismProperty C) [hP : RespectsIso P] {X Y Z : C}
    (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsIso f] : P (f ‚â´ g) ‚Üî P g :=
  ‚ü®fun h ‚Ü¶ by simpa using RespectsIso.precomp P (inv f) (f ‚â´ g) h, RespectsIso.precomp P f g‚ü©

theorem cancel_right_of_respectsIso (P : MorphismProperty C) [hP : RespectsIso P] {X Y Z : C}
    (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsIso g] : P (f ‚â´ g) ‚Üî P f :=
  ‚ü®fun h ‚Ü¶ by simpa using RespectsIso.postcomp P (inv g) (f ‚â´ g) h, RespectsIso.postcomp P g f‚ü©

lemma comma_iso_iff (P : MorphismProperty C) [P.RespectsIso] {A B : Type*} [Category A] [Category B]
    {L : A ‚•§ C} {R : B ‚•§ C} {f g : Comma L R} (e : f ‚âÖ g) :
    P f.hom ‚Üî P g.hom := by
  simp [‚Üê Comma.inv_left_hom_right e.hom, cancel_left_of_respectsIso, cancel_right_of_respectsIso]

theorem arrow_iso_iff (P : MorphismProperty C) [RespectsIso P] {f g : Arrow C}
    (e : f ‚âÖ g) : P f.hom ‚Üî P g.hom :=
  P.comma_iso_iff e

theorem arrow_mk_iso_iff (P : MorphismProperty C) [RespectsIso P] {W X Y Z : C}
    {f : W ‚ü∂ X} {g : Y ‚ü∂ Z} (e : Arrow.mk f ‚âÖ Arrow.mk g) : P f ‚Üî P g :=
  P.arrow_iso_iff e

theorem RespectsIso.of_respects_arrow_iso (P : MorphismProperty C)
    (hP : ‚àÄ (f g : Arrow C) (_ : f ‚âÖ g) (_ : P f.hom), P g.hom) : RespectsIso P where
  precomp {X Y Z} e (he : IsIso e) f hf := by
    refine hP (Arrow.mk f) (Arrow.mk (e ‚â´ f)) (Arrow.isoMk (asIso (inv e)) (Iso.refl _) ?_) hf
    simp
  postcomp {X Y Z} e (he : IsIso e) f hf := by
    refine hP (Arrow.mk f) (Arrow.mk (f ‚â´ e)) (Arrow.isoMk (Iso.refl _) (asIso e) ?_) hf
    simp

lemma isoClosure_eq_iff (P : MorphismProperty C) :
    P.isoClosure = P ‚Üî P.RespectsIso := by
  refine ‚ü®(¬∑ ‚ñ∏ P.isoClosure_respectsIso), fun hP ‚Ü¶ le_antisymm ?_ (P.le_isoClosure)‚ü©
  intro X Y f ‚ü®X', Y', f', hf', ‚ü®e‚ü©‚ü©
  exact (P.arrow_mk_iso_iff e).1 hf'

lemma isoClosure_eq_self (P : MorphismProperty C) [P.RespectsIso] :
    P.isoClosure = P := by rwa [isoClosure_eq_iff]

@[simp]
lemma isoClosure_isoClosure (P : MorphismProperty C) :
    P.isoClosure.isoClosure = P.isoClosure :=
  P.isoClosure.isoClosure_eq_self

lemma isoClosure_le_iff (P Q : MorphismProperty C) [Q.RespectsIso] :
    P.isoClosure ‚â§ Q ‚Üî P ‚â§ Q := by
  constructor
  ¬∑ exact P.le_isoClosure.trans
  ¬∑ intro h
    exact (monotone_isoClosure h).trans (by rw [Q.isoClosure_eq_self])

instance map_respectsIso (P : MorphismProperty C) (F : C ‚•§ D) :
    (P.map F).RespectsIso := by
  apply RespectsIso.of_respects_arrow_iso
  intro f g e ‚ü®X', Y', f', hf', ‚ü®e'‚ü©‚ü©
  exact ‚ü®X', Y', f', hf', ‚ü®e' ‚â™‚â´ e‚ü©‚ü©

lemma map_le_iff (P : MorphismProperty C) {F : C ‚•§ D} (Q : MorphismProperty D)
    [RespectsIso Q] :
    P.map F ‚â§ Q ‚Üî P ‚â§ Q.inverseImage F := by
  constructor
  ¬∑ intro h X Y f hf
    exact h (F.map f) (map_mem_map P F f hf)
  ¬∑ intro h X Y f ‚ü®X', Y', f', hf', ‚ü®e‚ü©‚ü©
    exact (Q.arrow_mk_iso_iff e).1 (h _ hf')

@[simp]
lemma map_isoClosure (P : MorphismProperty C) (F : C ‚•§ D) :
    P.isoClosure.map F = P.map F := by
  apply le_antisymm
  ¬∑ rw [map_le_iff]
    intro X Y f ‚ü®X', Y', f', hf', ‚ü®e‚ü©‚ü©
    exact ‚ü®_, _, f', hf', ‚ü®F.mapArrow.mapIso e‚ü©‚ü©
  ¬∑ exact monotone_map _ (le_isoClosure P)

lemma map_id_eq_isoClosure (P : MorphismProperty C) :
    P.map (ùü≠ _) = P.isoClosure := by
  apply le_antisymm
  ¬∑ rw [map_le_iff]
    intro X Y f hf
    exact P.le_isoClosure _ hf
  ¬∑ intro X Y f hf
    exact hf

lemma map_id (P : MorphismProperty C) [RespectsIso P] :
    P.map (ùü≠ _) = P := by
  rw [map_id_eq_isoClosure, P.isoClosure_eq_self]

@[simp]
lemma map_map (P : MorphismProperty C) (F : C ‚•§ D) {E : Type*} [Category E] (G : D ‚•§ E) :
    (P.map F).map G = P.map (F ‚ãô G) := by
  apply le_antisymm
  ¬∑ rw [map_le_iff]
    intro X Y f ‚ü®X', Y', f', hf', ‚ü®e‚ü©‚ü©
    exact ‚ü®X', Y', f', hf', ‚ü®G.mapArrow.mapIso e‚ü©‚ü©
  ¬∑ rw [map_le_iff]
    intro X Y f hf
    exact map_mem_map _ _ _ (map_mem_map _ _ _ hf)

instance RespectsIso.inverseImage (P : MorphismProperty D) [RespectsIso P] (F : C ‚•§ D) :
    RespectsIso (P.inverseImage F) where
  precomp {X Y Z} e (he : IsIso e) f hf := by
    simpa [MorphismProperty.inverseImage, cancel_left_of_respectsIso] using hf
  postcomp {X Y Z} e (he : IsIso e) f hf := by
    simpa [MorphismProperty.inverseImage, cancel_right_of_respectsIso] using hf

lemma map_eq_of_iso (P : MorphismProperty C) {F G : C ‚•§ D} (e : F ‚âÖ G) :
    P.map F = P.map G := by
  revert F G e
  suffices ‚àÄ {F G : C ‚•§ D} (_ : F ‚âÖ G), P.map F ‚â§ P.map G from
    fun F G e ‚Ü¶ le_antisymm (this e) (this e.symm)
  intro F G e X Y f ‚ü®X', Y', f', hf', ‚ü®e'‚ü©‚ü©
  exact ‚ü®X', Y', f', hf', ‚ü®((Functor.mapArrowFunctor _ _).mapIso e.symm).app (Arrow.mk f') ‚â™‚â´ e'‚ü©‚ü©

lemma map_inverseImage_le (P : MorphismProperty D) (F : C ‚•§ D) :
    (P.inverseImage F).map F ‚â§ P.isoClosure :=
  fun _ _ _ ‚ü®_, _, f, hf, ‚ü®e‚ü©‚ü© ‚Ü¶ ‚ü®_, _, F.map f, hf, ‚ü®e‚ü©‚ü©

lemma inverseImage_equivalence_inverse_eq_map_functor
    (P : MorphismProperty D) [RespectsIso P] (E : C ‚âå D) :
    P.inverseImage E.functor = P.map E.inverse := by
  apply le_antisymm
  ¬∑ intro X Y f hf
    refine ‚ü®_, _, _, hf, ‚ü®?_‚ü©‚ü©
    exact ((Functor.mapArrowFunctor _ _).mapIso E.unitIso.symm).app (Arrow.mk f)
  ¬∑ rw [map_le_iff]
    intro X Y f hf
    exact (P.arrow_mk_iso_iff
      (((Functor.mapArrowFunctor _ _).mapIso E.counitIso).app (Arrow.mk f))).2 hf

lemma inverseImage_equivalence_functor_eq_map_inverse
    (Q : MorphismProperty C) [RespectsIso Q] (E : C ‚âå D) :
    Q.inverseImage E.inverse = Q.map E.functor :=
  inverseImage_equivalence_inverse_eq_map_functor Q E.symm

lemma map_inverseImage_eq_of_isEquivalence
    (P : MorphismProperty D) [P.RespectsIso] (F : C ‚•§ D) [F.IsEquivalence] :
    (P.inverseImage F).map F = P := by
  erw [P.inverseImage_equivalence_inverse_eq_map_functor F.asEquivalence, map_map,
    P.map_eq_of_iso F.asEquivalence.counitIso, map_id]

lemma inverseImage_map_eq_of_isEquivalence
    (P : MorphismProperty C) [P.RespectsIso] (F : C ‚•§ D) [F.IsEquivalence] :
    (P.map F).inverseImage F = P := by
  erw [((P.map F).inverseImage_equivalence_inverse_eq_map_functor (F.asEquivalence)), map_map,
    P.map_eq_of_iso F.asEquivalence.unitIso.symm, map_id]

end

section

variable {C}
variable {X Y : C} (f : X ‚ü∂ Y)

@[simp]
theorem isomorphisms.iff : (isomorphisms C) f ‚Üî IsIso f := by rfl

@[simp]
theorem monomorphisms.iff : (monomorphisms C) f ‚Üî Mono f := by rfl

@[simp]
theorem epimorphisms.iff : (epimorphisms C) f ‚Üî Epi f := by rfl

theorem isomorphisms.infer_property [hf : IsIso f] : (isomorphisms C) f :=
  hf

theorem monomorphisms.infer_property [hf : Mono f] : (monomorphisms C) f :=
  hf

theorem epimorphisms.infer_property [hf : Epi f] : (epimorphisms C) f :=
  hf

end

lemma isomorphisms_op : (isomorphisms C).op = isomorphisms C·µí·µñ := by
  ext X Y f
  simp only [op, isomorphisms.iff]
  exact ‚ü®fun _ ‚Ü¶ inferInstanceAs (IsIso f.unop.op), fun _ ‚Ü¶ inferInstance‚ü©

instance RespectsIso.monomorphisms : RespectsIso (monomorphisms C) := by
  apply RespectsIso.mk <;>
    ¬∑ intro X Y Z e f
      simp only [monomorphisms.iff]
      intro
      apply mono_comp

instance RespectsIso.epimorphisms : RespectsIso (epimorphisms C) := by
  apply RespectsIso.mk <;>
    ¬∑ intro X Y Z e f
      simp only [epimorphisms.iff]
      intro
      apply epi_comp

instance RespectsIso.isomorphisms : RespectsIso (isomorphisms C) := by
  apply RespectsIso.mk <;>
    ¬∑ intro X Y Z e f
      simp only [isomorphisms.iff]
      intro
      exact IsIso.comp_isIso

/-- If `W‚ÇÅ` and `W‚ÇÇ` are morphism properties on two categories `C‚ÇÅ` and `C‚ÇÇ`,
this is the induced morphism property on `C‚ÇÅ √ó C‚ÇÇ`. -/
def prod {C‚ÇÅ C‚ÇÇ : Type*} [Category C‚ÇÅ] [Category C‚ÇÇ]
    (W‚ÇÅ : MorphismProperty C‚ÇÅ) (W‚ÇÇ : MorphismProperty C‚ÇÇ) :
    MorphismProperty (C‚ÇÅ √ó C‚ÇÇ) :=
  fun _ _ f ‚Ü¶ W‚ÇÅ f.1 ‚àß W‚ÇÇ f.2

/-- If `W j` are morphism properties on categories `C j` for all `j`, this is the
induced morphism property on the category `‚àÄ j, C j`. -/
def pi {J : Type w} {C : J ‚Üí Type u} [‚àÄ j, Category.{v} (C j)]
    (W : ‚àÄ j, MorphismProperty (C j)) : MorphismProperty (‚àÄ j, C j) :=
  fun _ _ f ‚Ü¶ ‚àÄ j, (W j) (f j)

variable {C}

/-- The morphism property on `J ‚•§ C` which is defined objectwise
from `W : MorphismProperty C`. -/
def functorCategory (W : MorphismProperty C) (J : Type*) [Category J] :
    MorphismProperty (J ‚•§ C) :=
  fun _ _ f ‚Ü¶ ‚àÄ (j : J), W (f.app j)

/-- Given `W : MorphismProperty C`, this is the morphism property on `Arrow C` of morphisms
whose left and right parts are in `W`. -/
def arrow (W : MorphismProperty C) :
    MorphismProperty (Arrow C) :=
  fun _ _ f ‚Ü¶ W f.left ‚àß W f.right

end MorphismProperty

namespace NatTrans

lemma isIso_app_iff_of_iso {F G : C ‚•§ D} (Œ± : F ‚ü∂ G) {X Y : C} (e : X ‚âÖ Y) :
    IsIso (Œ±.app X) ‚Üî IsIso (Œ±.app Y) :=
  (MorphismProperty.isomorphisms D).arrow_mk_iso_iff
    (Arrow.isoMk (F.mapIso e) (G.mapIso e) (by simp))

end NatTrans

end CategoryTheory
