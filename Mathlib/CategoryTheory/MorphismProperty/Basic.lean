/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.CategoryTheory.Comma.Arrow
import Mathlib.Order.CompleteBooleanAlgebra

/-!
# Properties of morphisms

We provide the basic framework for talking about properties of morphisms.
The following meta-property is defined

* `RespectsLeft P Q`: `P` respects the property `Q` on the left if `P f â†’ P (i â‰« f)` where
  `i` satisfies `Q`.
* `RespectsRight P Q`: `P` respects the property `Q` on the right if `P f â†’ P (f â‰« i)` where
  `i` satisfies `Q`.
* `Respects`: `P` respects `Q` if `P` respects `Q` both on the left and on the right.

-/


universe w v v' u u'

open CategoryTheory Opposite

noncomputable section

namespace CategoryTheory

variable (C : Type u) [Category.{v} C] {D : Type*} [Category D]

/-- A `MorphismProperty C` is a class of morphisms between objects in `C`. -/
def MorphismProperty :=
  âˆ€ â¦ƒX Y : Câ¦„ (_ : X âŸ¶ Y), Prop

instance : CompleteBooleanAlgebra (MorphismProperty C) where
  le Pâ‚ Pâ‚‚ := âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), Pâ‚ f â†’ Pâ‚‚ f
  __ := inferInstanceAs (CompleteBooleanAlgebra (âˆ€ â¦ƒX Y : Câ¦„ (_ : X âŸ¶ Y), Prop))

lemma MorphismProperty.le_def {P Q : MorphismProperty C} :
    P â‰¤ Q â†” âˆ€ {X Y : C} (f : X âŸ¶ Y), P f â†’ Q f := Iff.rfl

instance : Inhabited (MorphismProperty C) :=
  âŸ¨âŠ¤âŸ©

lemma MorphismProperty.top_eq : (âŠ¤ : MorphismProperty C) = fun _ _ _ => True := rfl

variable {C}

namespace MorphismProperty

@[ext]
lemma ext (W W' : MorphismProperty C) (h : âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), W f â†” W' f) :
    W = W' := by
  funext X Y f
  rw [h]

@[simp]
lemma top_apply {X Y : C} (f : X âŸ¶ Y) : (âŠ¤ : MorphismProperty C) f := by
  simp only [top_eq]

lemma of_eq_top {P : MorphismProperty C} (h : P = âŠ¤) {X Y : C} (f : X âŸ¶ Y) : P f := by
  simp [h]

@[simp]
lemma sSup_iff (S : Set (MorphismProperty C)) {X Y : C} (f : X âŸ¶ Y) :
    sSup S f â†” âˆƒ (W : S), W.1 f := by
  dsimp [sSup, iSup]
  constructor
  Â· rintro âŸ¨_, âŸ¨âŸ¨_, âŸ¨âŸ¨_, âŸ¨_, hâŸ©, rflâŸ©, rflâŸ©âŸ©, rflâŸ©, hfâŸ©
    exact âŸ¨âŸ¨_, hâŸ©, hfâŸ©
  Â· rintro âŸ¨âŸ¨W, hWâŸ©, hfâŸ©
    exact âŸ¨_, âŸ¨âŸ¨_, âŸ¨_, âŸ¨âŸ¨W, hWâŸ©, rflâŸ©âŸ©, rflâŸ©, rflâŸ©, hfâŸ©

@[simp]
lemma iSup_iff {Î¹ : Sort*} (W : Î¹ â†’ MorphismProperty C) {X Y : C} (f : X âŸ¶ Y) :
    iSup W f â†” âˆƒ i, W i f := by
  apply (sSup_iff (Set.range W) f).trans
  constructor
  Â· rintro âŸ¨âŸ¨_, i, rflâŸ©, hfâŸ©
    exact âŸ¨i, hfâŸ©
  Â· rintro âŸ¨i, hfâŸ©
    exact âŸ¨âŸ¨_, i, rflâŸ©, hfâŸ©

/-- The morphism property in `Cáµ’áµ–` associated to a morphism property in `C` -/
@[simp]
def op (P : MorphismProperty C) : MorphismProperty Cáµ’áµ– := fun _ _ f => P f.unop

/-- The morphism property in `C` associated to a morphism property in `Cáµ’áµ–` -/
@[simp]
def unop (P : MorphismProperty Cáµ’áµ–) : MorphismProperty C := fun _ _ f => P f.op

theorem unop_op (P : MorphismProperty C) : P.op.unop = P :=
  rfl

theorem op_unop (P : MorphismProperty Cáµ’áµ–) : P.unop.op = P :=
  rfl

/-- The inverse image of a `MorphismProperty D` by a functor `C â¥¤ D` -/
def inverseImage (P : MorphismProperty D) (F : C â¥¤ D) : MorphismProperty C := fun _ _ f =>
  P (F.map f)

@[simp]
lemma inverseImage_iff (P : MorphismProperty D) (F : C â¥¤ D) {X Y : C} (f : X âŸ¶ Y) :
    P.inverseImage F f â†” P (F.map f) := by rfl

/-- The image (up to isomorphisms) of a `MorphismProperty C` by a functor `C â¥¤ D` -/
def map (P : MorphismProperty C) (F : C â¥¤ D) : MorphismProperty D := fun _ _ f =>
  âˆƒ (X' Y' : C) (f' : X' âŸ¶ Y') (_ : P f'), Nonempty (Arrow.mk (F.map f') â‰… Arrow.mk f)

lemma map_mem_map (P : MorphismProperty C) (F : C â¥¤ D) {X Y : C} (f : X âŸ¶ Y) (hf : P f) :
    (P.map F) (F.map f) := âŸ¨X, Y, f, hf, âŸ¨Iso.refl _âŸ©âŸ©

lemma monotone_map (F : C â¥¤ D) :
    Monotone (map Â· F) := by
  intro P Q h X Y f âŸ¨X', Y', f', hf', âŸ¨eâŸ©âŸ©
  exact âŸ¨X', Y', f', h _ hf', âŸ¨eâŸ©âŸ©

@[simp]
lemma map_top_eq_top_of_essSurj_of_full (F : C â¥¤ D) [F.EssSurj] [F.Full] :
    (âŠ¤ : MorphismProperty C).map F = âŠ¤ := by
  rw [eq_top_iff]
  intro X Y f _
  refine âŸ¨F.objPreimage X, F.objPreimage Y, F.preimage ?_, âŸ¨âŸ¨âŸ©, âŸ¨?_âŸ©âŸ©âŸ©
  Â· exact (Functor.objObjPreimageIso F X).hom â‰« f â‰« (Functor.objObjPreimageIso F Y).inv
  Â· exact Arrow.isoMk' _ _ (Functor.objObjPreimageIso F X) (Functor.objObjPreimageIso F Y)
      (by simp)

section

variable (P : MorphismProperty C)

/-- The set in `Set (Arrow C)` which corresponds to `P : MorphismProperty C`. -/
def toSet : Set (Arrow C) := setOf (fun f â†¦ P f.hom)

/-- The family of morphisms indexed by `P.toSet` which corresponds
to `P : MorphismProperty C`, see `MorphismProperty.ofHoms_homFamily`. -/
def homFamily (f : P.toSet) : f.1.left âŸ¶ f.1.right := f.1.hom

lemma homFamily_apply (f : P.toSet) : P.homFamily f = f.1.hom := rfl

@[simp]
lemma homFamily_arrow_mk {X Y : C} (f : X âŸ¶ Y) (hf : P f) :
    P.homFamily âŸ¨Arrow.mk f, hfâŸ© = f := rfl

@[simp]
lemma arrow_mk_mem_toSet_iff {X Y : C} (f : X âŸ¶ Y) : Arrow.mk f âˆˆ P.toSet â†” P f := Iff.rfl

lemma of_eq {X Y : C} {f : X âŸ¶ Y} (hf : P f)
    {X' Y' : C} {f' : X' âŸ¶ Y'}
    (hX : X = X') (hY : Y = Y') (h : f' = eqToHom hX.symm â‰« f â‰« eqToHom hY) :
    P f' := by
  rw [â† P.arrow_mk_mem_toSet_iff] at hf âŠ¢
  rwa [(Arrow.mk_eq_mk_iff f' f).2 âŸ¨hX.symm, hY.symm, hâŸ©]

end

/-- The class of morphisms given by a family of morphisms `f i : X i âŸ¶ Y i`. -/
inductive ofHoms {Î¹ : Type*} {X Y : Î¹ â†’ C} (f : âˆ€ i, X i âŸ¶ Y i) : MorphismProperty C
  | mk (i : Î¹) : ofHoms f (f i)

lemma ofHoms_iff {Î¹ : Type*} {X Y : Î¹ â†’ C} (f : âˆ€ i, X i âŸ¶ Y i) {A B : C} (g : A âŸ¶ B) :
    ofHoms f g â†” âˆƒ i, Arrow.mk g = Arrow.mk (f i) := by
  constructor
  Â· rintro âŸ¨iâŸ©
    exact âŸ¨i, rflâŸ©
  Â· rintro âŸ¨i, hâŸ©
    rw [â† (ofHoms f).arrow_mk_mem_toSet_iff, h, arrow_mk_mem_toSet_iff]
    constructor

@[simp]
lemma ofHoms_homFamily (P : MorphismProperty C) : ofHoms P.homFamily = P := by
  ext _ _ f
  constructor
  Â· intro hf
    rw [ofHoms_iff] at hf
    obtain âŸ¨âŸ¨f, hfâŸ©, âŸ¨_, _âŸ©âŸ© := hf
    exact hf
  Â· intro hf
    exact âŸ¨(âŸ¨f, hfâŸ© : P.toSet)âŸ©

/-- A morphism property `P` satisfies `P.RespectsRight Q` if it is stable under post-composition
with morphisms satisfying `Q`, i.e. whenever `P` holds for `f` and `Q` holds for `i` then `P`
holds for `f â‰« i`. -/
class RespectsRight (P Q : MorphismProperty C) : Prop where
  postcomp {X Y Z : C} (i : Y âŸ¶ Z) (hi : Q i) (f : X âŸ¶ Y) (hf : P f) : P (f â‰« i)

/-- A morphism property `P` satisfies `P.RespectsLeft Q` if it is stable under
pre-composition with morphisms satisfying `Q`, i.e. whenever `P` holds for `f`
and `Q` holds for `i` then `P` holds for `i â‰« f`. -/
class RespectsLeft (P Q : MorphismProperty C) : Prop where
  precomp {X Y Z : C} (i : X âŸ¶ Y) (hi : Q i) (f : Y âŸ¶ Z) (hf : P f) : P (i â‰« f)

/-- A morphism property `P` satisfies `P.Respects Q` if it is stable under composition on the
left and right by morphisms satisfying `Q`. -/
class Respects (P Q : MorphismProperty C) : Prop extends P.RespectsLeft Q, P.RespectsRight Q where

instance (P Q : MorphismProperty C) [P.RespectsLeft Q] [P.RespectsRight Q] : P.Respects Q where

instance (P Q : MorphismProperty C) [P.RespectsLeft Q] : P.op.RespectsRight Q.op where
  postcomp i hi f hf := RespectsLeft.precomp (Q := Q) i.unop hi f.unop hf

instance (P Q : MorphismProperty C) [P.RespectsRight Q] : P.op.RespectsLeft Q.op where
  precomp i hi f hf := RespectsRight.postcomp (Q := Q) i.unop hi f.unop hf

instance RespectsLeft.inf (Pâ‚ Pâ‚‚ Q : MorphismProperty C) [Pâ‚.RespectsLeft Q]
    [Pâ‚‚.RespectsLeft Q] : (Pâ‚ âŠ“ Pâ‚‚).RespectsLeft Q where
  precomp i hi f hf := âŸ¨precomp i hi f hf.left, precomp i hi f hf.rightâŸ©

instance RespectsRight.inf (Pâ‚ Pâ‚‚ Q : MorphismProperty C) [Pâ‚.RespectsRight Q]
    [Pâ‚‚.RespectsRight Q] : (Pâ‚ âŠ“ Pâ‚‚).RespectsRight Q where
  postcomp i hi f hf := âŸ¨postcomp i hi f hf.left, postcomp i hi f hf.rightâŸ©

variable (C)

/-- The `MorphismProperty C` satisfied by isomorphisms in `C`. -/
def isomorphisms : MorphismProperty C := fun _ _ f => IsIso f

/-- The `MorphismProperty C` satisfied by monomorphisms in `C`. -/
def monomorphisms : MorphismProperty C := fun _ _ f => Mono f

/-- The `MorphismProperty C` satisfied by epimorphisms in `C`. -/
def epimorphisms : MorphismProperty C := fun _ _ f => Epi f

section

variable {C}

/-- `P` respects isomorphisms, if it respects the morphism property `isomorphisms C`, i.e.
it is stable under pre- and postcomposition with isomorphisms. -/
abbrev RespectsIso (P : MorphismProperty C) : Prop := P.Respects (isomorphisms C)

instance inf (P Q : MorphismProperty C) [P.RespectsIso] [Q.RespectsIso] : (P âŠ“ Q).RespectsIso where

lemma RespectsIso.mk (P : MorphismProperty C)
    (hprecomp : âˆ€ {X Y Z : C} (e : X â‰… Y) (f : Y âŸ¶ Z) (_ : P f), P (e.hom â‰« f))
    (hpostcomp : âˆ€ {X Y Z : C} (e : Y â‰… Z) (f : X âŸ¶ Y) (_ : P f), P (f â‰« e.hom)) :
    P.RespectsIso where
  precomp e (_ : IsIso e) f hf := hprecomp (asIso e) f hf
  postcomp e (_ : IsIso e) f hf := hpostcomp (asIso e) f hf

lemma RespectsIso.precomp (P : MorphismProperty C) [P.RespectsIso] {X Y Z : C} (e : X âŸ¶ Y)
    [IsIso e] (f : Y âŸ¶ Z) (hf : P f) : P (e â‰« f) :=
  RespectsLeft.precomp (Q := isomorphisms C) e â€¹IsIso eâ€º f hf

instance : RespectsIso (âŠ¤ : MorphismProperty C) where
  precomp _ _ _ _ := trivial
  postcomp _ _ _ _ := trivial

lemma RespectsIso.postcomp (P : MorphismProperty C) [P.RespectsIso] {X Y Z : C} (e : Y âŸ¶ Z)
    [IsIso e] (f : X âŸ¶ Y) (hf : P f) : P (f â‰« e) :=
  RespectsRight.postcomp (Q := isomorphisms C) e â€¹IsIso eâ€º f hf

instance RespectsIso.op (P : MorphismProperty C) [RespectsIso P] : RespectsIso P.op where
  precomp e (_ : IsIso e) f hf := postcomp P e.unop f.unop hf
  postcomp e (_ : IsIso e) f hf := precomp P e.unop f.unop hf

instance RespectsIso.unop (P : MorphismProperty Cáµ’áµ–) [RespectsIso P] : RespectsIso P.unop where
  precomp e (_ : IsIso e) f hf := postcomp P e.op f.op hf
  postcomp e (_ : IsIso e) f hf := precomp P e.op f.op hf

/-- The closure by isomorphisms of a `MorphismProperty` -/
def isoClosure (P : MorphismProperty C) : MorphismProperty C :=
  fun _ _ f => âˆƒ (Yâ‚ Yâ‚‚ : C) (f' : Yâ‚ âŸ¶ Yâ‚‚) (_ : P f'), Nonempty (Arrow.mk f' â‰… Arrow.mk f)

lemma le_isoClosure (P : MorphismProperty C) : P â‰¤ P.isoClosure :=
  fun _ _ f hf => âŸ¨_, _, f, hf, âŸ¨Iso.refl _âŸ©âŸ©

instance isoClosure_respectsIso (P : MorphismProperty C) :
    RespectsIso P.isoClosure where
  precomp := fun e (he : IsIso e) f âŸ¨_, _, f', hf', âŸ¨isoâŸ©âŸ© => âŸ¨_, _, f', hf',
      âŸ¨Arrow.isoMk (asIso iso.hom.left â‰ªâ‰« asIso (inv e)) (asIso iso.hom.right) (by simp)âŸ©âŸ©
  postcomp := fun e (he : IsIso e) f âŸ¨_, _, f', hf', âŸ¨isoâŸ©âŸ© => âŸ¨_, _, f', hf',
      âŸ¨Arrow.isoMk (asIso iso.hom.left) (asIso iso.hom.right â‰ªâ‰« asIso e) (by simp)âŸ©âŸ©

lemma monotone_isoClosure : Monotone (isoClosure (C := C)) := by
  intro P Q h X Y f âŸ¨X', Y', f', hf', âŸ¨eâŸ©âŸ©
  exact âŸ¨X', Y', f', h _ hf', âŸ¨eâŸ©âŸ©

theorem cancel_left_of_respectsIso (P : MorphismProperty C) [hP : RespectsIso P] {X Y Z : C}
    (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [IsIso f] : P (f â‰« g) â†” P g :=
  âŸ¨fun h => by simpa using RespectsIso.precomp P (inv f) (f â‰« g) h, RespectsIso.precomp P f gâŸ©

theorem cancel_right_of_respectsIso (P : MorphismProperty C) [hP : RespectsIso P] {X Y Z : C}
    (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [IsIso g] : P (f â‰« g) â†” P f :=
  âŸ¨fun h => by simpa using RespectsIso.postcomp P (inv g) (f â‰« g) h, RespectsIso.postcomp P g fâŸ©

lemma comma_iso_iff (P : MorphismProperty C) [P.RespectsIso] {A B : Type*} [Category A] [Category B]
    {L : A â¥¤ C} {R : B â¥¤ C} {f g : Comma L R} (e : f â‰… g) :
    P f.hom â†” P g.hom := by
  simp [â† Comma.inv_left_hom_right e.hom, cancel_left_of_respectsIso, cancel_right_of_respectsIso]

theorem arrow_iso_iff (P : MorphismProperty C) [RespectsIso P] {f g : Arrow C}
    (e : f â‰… g) : P f.hom â†” P g.hom :=
  P.comma_iso_iff e

theorem arrow_mk_iso_iff (P : MorphismProperty C) [RespectsIso P] {W X Y Z : C}
    {f : W âŸ¶ X} {g : Y âŸ¶ Z} (e : Arrow.mk f â‰… Arrow.mk g) : P f â†” P g :=
  P.arrow_iso_iff e

theorem RespectsIso.of_respects_arrow_iso (P : MorphismProperty C)
    (hP : âˆ€ (f g : Arrow C) (_ : f â‰… g) (_ : P f.hom), P g.hom) : RespectsIso P where
  precomp {X Y Z} e (he : IsIso e) f hf := by
    refine hP (Arrow.mk f) (Arrow.mk (e â‰« f)) (Arrow.isoMk (asIso (inv e)) (Iso.refl _) ?_) hf
    simp
  postcomp {X Y Z} e (he : IsIso e) f hf := by
    refine hP (Arrow.mk f) (Arrow.mk (f â‰« e)) (Arrow.isoMk (Iso.refl _) (asIso e) ?_) hf
    simp

lemma isoClosure_eq_iff (P : MorphismProperty C) :
    P.isoClosure = P â†” P.RespectsIso := by
  refine âŸ¨(Â· â–¸ P.isoClosure_respectsIso), fun hP â†¦ le_antisymm ?_ (P.le_isoClosure)âŸ©
  intro X Y f âŸ¨X', Y', f', hf', âŸ¨eâŸ©âŸ©
  exact (P.arrow_mk_iso_iff e).1 hf'

lemma isoClosure_eq_self (P : MorphismProperty C) [P.RespectsIso] :
    P.isoClosure = P := by rwa [isoClosure_eq_iff]

@[simp]
lemma isoClosure_isoClosure (P : MorphismProperty C) :
    P.isoClosure.isoClosure = P.isoClosure :=
  P.isoClosure.isoClosure_eq_self

lemma isoClosure_le_iff (P Q : MorphismProperty C) [Q.RespectsIso] :
    P.isoClosure â‰¤ Q â†” P â‰¤ Q := by
  constructor
  Â· exact P.le_isoClosure.trans
  Â· intro h
    exact (monotone_isoClosure h).trans (by rw [Q.isoClosure_eq_self])

instance map_respectsIso (P : MorphismProperty C) (F : C â¥¤ D) :
    (P.map F).RespectsIso := by
  apply RespectsIso.of_respects_arrow_iso
  intro f g e âŸ¨X', Y', f', hf', âŸ¨e'âŸ©âŸ©
  exact âŸ¨X', Y', f', hf', âŸ¨e' â‰ªâ‰« eâŸ©âŸ©

lemma map_le_iff (P : MorphismProperty C) {F : C â¥¤ D} (Q : MorphismProperty D)
    [RespectsIso Q] :
    P.map F â‰¤ Q â†” P â‰¤ Q.inverseImage F := by
  constructor
  Â· intro h X Y f hf
    exact h (F.map f) (map_mem_map P F f hf)
  Â· intro h X Y f âŸ¨X', Y', f', hf', âŸ¨eâŸ©âŸ©
    exact (Q.arrow_mk_iso_iff e).1 (h _ hf')

@[simp]
lemma map_isoClosure (P : MorphismProperty C) (F : C â¥¤ D) :
    P.isoClosure.map F = P.map F := by
  apply le_antisymm
  Â· rw [map_le_iff]
    intro X Y f âŸ¨X', Y', f', hf', âŸ¨eâŸ©âŸ©
    exact âŸ¨_, _, f', hf', âŸ¨F.mapArrow.mapIso eâŸ©âŸ©
  Â· exact monotone_map _ (le_isoClosure P)

lemma map_id_eq_isoClosure (P : MorphismProperty C) :
    P.map (ðŸ­ _) = P.isoClosure := rfl

lemma map_id (P : MorphismProperty C) [RespectsIso P] :
    P.map (ðŸ­ _) = P := by
  rw [map_id_eq_isoClosure, P.isoClosure_eq_self]

@[simp]
lemma map_map (P : MorphismProperty C) (F : C â¥¤ D) {E : Type*} [Category E] (G : D â¥¤ E) :
    (P.map F).map G = P.map (F â‹™ G) := by
  apply le_antisymm
  Â· rw [map_le_iff]
    intro X Y f âŸ¨X', Y', f', hf', âŸ¨eâŸ©âŸ©
    exact âŸ¨X', Y', f', hf', âŸ¨G.mapArrow.mapIso eâŸ©âŸ©
  Â· rw [map_le_iff]
    intro X Y f hf
    exact map_mem_map _ _ _ (map_mem_map _ _ _ hf)

instance RespectsIso.inverseImage (P : MorphismProperty D) [RespectsIso P] (F : C â¥¤ D) :
    RespectsIso (P.inverseImage F) where
  precomp {X Y Z} e (he : IsIso e) f hf := by
    simpa [MorphismProperty.inverseImage, cancel_left_of_respectsIso] using hf
  postcomp {X Y Z} e (he : IsIso e) f hf := by
    simpa [MorphismProperty.inverseImage, cancel_right_of_respectsIso] using hf

lemma map_eq_of_iso (P : MorphismProperty C) {F G : C â¥¤ D} (e : F â‰… G) :
    P.map F = P.map G := by
  revert F G e
  suffices âˆ€ {F G : C â¥¤ D} (_ : F â‰… G), P.map F â‰¤ P.map G from
    fun F G e => le_antisymm (this e) (this e.symm)
  intro F G e X Y f âŸ¨X', Y', f', hf', âŸ¨e'âŸ©âŸ©
  exact âŸ¨X', Y', f', hf', âŸ¨((Functor.mapArrowFunctor _ _).mapIso e.symm).app (Arrow.mk f') â‰ªâ‰« e'âŸ©âŸ©

lemma map_inverseImage_le (P : MorphismProperty D) (F : C â¥¤ D) :
    (P.inverseImage F).map F â‰¤ P.isoClosure :=
  fun _ _ _ âŸ¨_, _, f, hf, âŸ¨eâŸ©âŸ© => âŸ¨_, _, F.map f, hf, âŸ¨eâŸ©âŸ©

lemma inverseImage_equivalence_inverse_eq_map_functor
    (P : MorphismProperty D) [RespectsIso P] (E : C â‰Œ D) :
    P.inverseImage E.functor = P.map E.inverse := by
  apply le_antisymm
  Â· intro X Y f hf
    refine âŸ¨_, _, _, hf, âŸ¨?_âŸ©âŸ©
    exact ((Functor.mapArrowFunctor _ _).mapIso E.unitIso.symm).app (Arrow.mk f)
  Â· rw [map_le_iff]
    intro X Y f hf
    exact (P.arrow_mk_iso_iff
      (((Functor.mapArrowFunctor _ _).mapIso E.counitIso).app (Arrow.mk f))).2 hf

lemma inverseImage_equivalence_functor_eq_map_inverse
    (Q : MorphismProperty C) [RespectsIso Q] (E : C â‰Œ D) :
    Q.inverseImage E.inverse = Q.map E.functor :=
  inverseImage_equivalence_inverse_eq_map_functor Q E.symm

lemma map_inverseImage_eq_of_isEquivalence
    (P : MorphismProperty D) [P.RespectsIso] (F : C â¥¤ D) [F.IsEquivalence] :
    (P.inverseImage F).map F = P := by
  erw [P.inverseImage_equivalence_inverse_eq_map_functor F.asEquivalence, map_map,
    P.map_eq_of_iso F.asEquivalence.counitIso, map_id]

lemma inverseImage_map_eq_of_isEquivalence
    (P : MorphismProperty C) [P.RespectsIso] (F : C â¥¤ D) [F.IsEquivalence] :
    (P.map F).inverseImage F = P := by
  erw [((P.map F).inverseImage_equivalence_inverse_eq_map_functor (F.asEquivalence)), map_map,
    P.map_eq_of_iso F.asEquivalence.unitIso.symm, map_id]

end

section

variable {C}
variable {X Y : C} (f : X âŸ¶ Y)

@[simp]
theorem isomorphisms.iff : (isomorphisms C) f â†” IsIso f := by rfl

@[simp]
theorem monomorphisms.iff : (monomorphisms C) f â†” Mono f := by rfl

@[simp]
theorem epimorphisms.iff : (epimorphisms C) f â†” Epi f := by rfl

theorem isomorphisms.infer_property [hf : IsIso f] : (isomorphisms C) f :=
  hf

theorem monomorphisms.infer_property [hf : Mono f] : (monomorphisms C) f :=
  hf

theorem epimorphisms.infer_property [hf : Epi f] : (epimorphisms C) f :=
  hf

end

lemma isomorphisms_op : (isomorphisms C).op = isomorphisms Cáµ’áµ– := by
  ext X Y f
  simp only [op, isomorphisms.iff]
  exact âŸ¨fun _ â†¦ inferInstanceAs (IsIso f.unop.op), fun _ â†¦ inferInstanceâŸ©

instance RespectsIso.monomorphisms : RespectsIso (monomorphisms C) := by
  apply RespectsIso.mk <;>
    Â· intro X Y Z e f
      simp only [monomorphisms.iff]
      intro
      apply mono_comp

instance RespectsIso.epimorphisms : RespectsIso (epimorphisms C) := by
  apply RespectsIso.mk <;>
    Â· intro X Y Z e f
      simp only [epimorphisms.iff]
      intro
      apply epi_comp

instance RespectsIso.isomorphisms : RespectsIso (isomorphisms C) := by
  apply RespectsIso.mk <;>
    Â· intro X Y Z e f
      simp only [isomorphisms.iff]
      intro
      exact IsIso.comp_isIso

/-- If `Wâ‚` and `Wâ‚‚` are morphism properties on two categories `Câ‚` and `Câ‚‚`,
this is the induced morphism property on `Câ‚ Ã— Câ‚‚`. -/
def prod {Câ‚ Câ‚‚ : Type*} [Category Câ‚] [Category Câ‚‚]
    (Wâ‚ : MorphismProperty Câ‚) (Wâ‚‚ : MorphismProperty Câ‚‚) :
    MorphismProperty (Câ‚ Ã— Câ‚‚) :=
  fun _ _ f => Wâ‚ f.1 âˆ§ Wâ‚‚ f.2

/-- If `W j` are morphism properties on categories `C j` for all `j`, this is the
induced morphism property on the category `âˆ€ j, C j`. -/
def pi {J : Type w} {C : J â†’ Type u} [âˆ€ j, Category.{v} (C j)]
    (W : âˆ€ j, MorphismProperty (C j)) : MorphismProperty (âˆ€ j, C j) :=
  fun _ _ f => âˆ€ j, (W j) (f j)

variable {C}

/-- The morphism property on `J â¥¤ C` which is defined objectwise
from `W : MorphismProperty C`. -/
def functorCategory (W : MorphismProperty C) (J : Type*) [Category J] :
    MorphismProperty (J â¥¤ C) :=
  fun _ _ f => âˆ€ (j : J), W (f.app j)

/-- Given `W : MorphismProperty C`, this is the morphism property on `Arrow C` of morphisms
whose left and right parts are in `W`. -/
def arrow (W : MorphismProperty C) :
    MorphismProperty (Arrow C) :=
  fun _ _ f => W f.left âˆ§ W f.right

end MorphismProperty

namespace NatTrans

lemma isIso_app_iff_of_iso {F G : C â¥¤ D} (Î± : F âŸ¶ G) {X Y : C} (e : X â‰… Y) :
    IsIso (Î±.app X) â†” IsIso (Î±.app Y) :=
  (MorphismProperty.isomorphisms D).arrow_mk_iso_iff
    (Arrow.isoMk (F.mapIso e) (G.mapIso e) (by simp))

end NatTrans

end CategoryTheory
