/-
Copyright (c) 2024 Jack McKoen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jack McKoen
-/
import Mathlib.CategoryTheory.Retract
import Mathlib.CategoryTheory.MorphismProperty.Basic

/-!
# Stability under retracts

Given `P : MorphismProperty C`, we introduce a typeclass `P.IsStableUnderRetracts` which
is the property that `P` is stable under retracts.

-/

universe v u

namespace CategoryTheory

variable {C : Type u} [Category.{v} C]

namespace MorphismProperty

/-- A class of morphisms is stable under retracts if a retract of such a morphism still
lies in the class. -/
@[mk_iff]
class IsStableUnderRetracts (P : MorphismProperty C) : Prop where
  of_retract {X Y Z W : C} {f : X ‚ü∂ Y} {g : Z ‚ü∂ W} (h : RetractArrow f g) (hg : P g) : P f

lemma of_retract {P : MorphismProperty C} [P.IsStableUnderRetracts]
    {X Y Z W : C} {f : X ‚ü∂ Y} {g : Z ‚ü∂ W} (h : RetractArrow f g) (hg : P g) : P f :=
  IsStableUnderRetracts.of_retract h hg

instance IsStableUnderRetracts.monomorphisms : (monomorphisms C).IsStableUnderRetracts where
  of_retract {_ _ _ _ f g} h (hg : Mono g) := ‚ü®fun Œ± Œ≤ w ‚Ü¶ by
    rw [‚Üê cancel_mono h.i.left, ‚Üê cancel_mono g, Category.assoc, Category.assoc,
      h.i_w, reassoc_of% w]‚ü©

instance IsStableUnderRetracts.epimorphisms : (epimorphisms C).IsStableUnderRetracts where
  of_retract {_ _ _ _ f g} h (hg : Epi g) := ‚ü®fun Œ± Œ≤ w ‚Ü¶ by
    rw [‚Üê cancel_epi h.r.right, ‚Üê cancel_epi g, ‚Üê Category.assoc, ‚Üê Category.assoc, ‚Üê h.r_w,
      Category.assoc, Category.assoc, w]‚ü©

instance IsStableUnderRetracts.isomorphisms : (isomorphisms C).IsStableUnderRetracts where
  of_retract {X Y Z W f g} h (_ : IsIso _) := by
    refine ‚ü®h.i.right ‚â´ inv g ‚â´ h.r.left, ?_, ?_‚ü©
    ¬∑ rw [‚Üê h.i_w_assoc, IsIso.hom_inv_id_assoc, h.retract_left]
    ¬∑ rw [Category.assoc, Category.assoc, h.r_w, IsIso.inv_hom_id_assoc, h.retract_right]

instance (P‚ÇÅ P‚ÇÇ : MorphismProperty C)
    [P‚ÇÅ.IsStableUnderRetracts] [P‚ÇÇ.IsStableUnderRetracts] :
    (P‚ÇÅ ‚äì P‚ÇÇ).IsStableUnderRetracts where
  of_retract := fun h ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ ‚ü®of_retract h h‚ÇÅ, of_retract h h‚ÇÇ‚ü©

/-- The class of morphisms that are retracts of morphisms
belonging to `P : MorphismProperty C`. -/
def retracts (P : MorphismProperty C) : MorphismProperty C :=
  fun _ _ f ‚Ü¶ ‚àÉ (Z W : C) (g : Z ‚ü∂ W) (_ : RetractArrow f g), P g

lemma le_retracts (P : MorphismProperty C) : P ‚â§ P.retracts := by
  intro X Y f hf
  exact ‚ü®_, _, f, { i := ùüô _, r := ùüô _}, hf‚ü©

lemma retracts_monotone : Monotone (retracts (C := C)) := by
  intro _ _ h _ _ _ ‚ü®_, _, _, hg, hg'‚ü©
  exact ‚ü®_, _, _, hg, h _ hg'‚ü©

lemma isStableUnderRetracts_iff_retracts_le (P : MorphismProperty C) :
    P.IsStableUnderRetracts ‚Üî P.retracts ‚â§ P := by
  rw [isStableUnderRetracts_iff]
  constructor
  ¬∑ intro h‚ÇÅ X Y f ‚ü®_, _, _, h‚ÇÇ, h‚ÇÉ‚ü©
    exact h‚ÇÅ h‚ÇÇ h‚ÇÉ
  ¬∑ intro h‚ÇÅ _ _ _ _ _ _ h‚ÇÇ h‚ÇÉ
    exact h‚ÇÅ _ ‚ü®_, _, _, h‚ÇÇ, h‚ÇÉ‚ü©

lemma retracts_le (P : MorphismProperty C) [P.IsStableUnderRetracts] :
    P.retracts ‚â§ P := by
  rwa [‚Üê isStableUnderRetracts_iff_retracts_le]

@[simp]
lemma retracts_le_iff {P Q : MorphismProperty C} [Q.IsStableUnderRetracts] :
    P.retracts ‚â§ Q ‚Üî P ‚â§ Q := by
  constructor
  ¬∑ exact le_trans P.le_retracts
  ¬∑ intro h
    exact le_trans (retracts_monotone h) Q.retracts_le

instance {P : MorphismProperty C} [P.IsStableUnderRetracts] :
    P.RespectsIso :=
  RespectsIso.of_respects_arrow_iso _
    (fun _ _ e ‚Ü¶ of_retract (Retract.ofIso e.symm))

end MorphismProperty

end CategoryTheory
