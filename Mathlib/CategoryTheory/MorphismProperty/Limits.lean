/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang, Jo√´l Riou
-/
import Mathlib.CategoryTheory.Limits.Final
import Mathlib.CategoryTheory.Limits.Connected
import Mathlib.CategoryTheory.Filtered.Connected
import Mathlib.CategoryTheory.Limits.Shapes.Diagonal
import Mathlib.CategoryTheory.MorphismProperty.Composition

/-!
# Relation of morphism properties with limits

The following predicates are introduces for morphism properties `P`:
* `IsStableUnderBaseChange`: `P` is stable under base change if in all pullback
  squares, the left map satisfies `P` if the right map satisfies it.
* `IsStableUnderCobaseChange`: `P` is stable under cobase change if in all pushout
  squares, the right map satisfies `P` if the left map satisfies it.

We define `P.universally` for the class of morphisms which satisfy `P` after any base change.

We also introduce properties `IsStableUnderProductsOfShape`, `IsStableUnderLimitsOfShape`,
`IsStableUnderFiniteProducts`, and similar properties for colimits and coproducts.

-/

universe w w' v u

namespace CategoryTheory

open Category Limits

namespace MorphismProperty

variable {C : Type u} [Category.{v} C]

section

variable (P : MorphismProperty C)

/-- Given a class of morphisms `P`, this is the class of pullbacks
of morphisms in `P`. -/
def pullbacks : MorphismProperty C := fun A B q ‚Ü¶
  ‚àÉ (X Y : C) (p : X ‚ü∂ Y) (f : A ‚ü∂ X) (g : B ‚ü∂ Y) (_ : P p),
    IsPullback f q p g

lemma pullbacks_mk {A B X Y : C} {f : A ‚ü∂ X} {q : A ‚ü∂ B} {p : X ‚ü∂ Y} {g : B ‚ü∂ Y}
    (sq : IsPullback f q p g) (hp : P p) :
    P.pullbacks q :=
  ‚ü®_, _, _, _, _, hp, sq‚ü©

lemma le_pullbacks : P ‚â§ P.pullbacks := by
  intro A B q hq
  exact P.pullbacks_mk IsPullback.of_id_fst hq

lemma pullbacks_monotone : Monotone (pullbacks (C := C)) := by
  rintro _ _ h _ _ _ ‚ü®_, _, _, _, _, hp, sq‚ü©
  exact ‚ü®_, _, _, _, _, h _ hp, sq‚ü©

/-- Given a class of morphisms `P`, this is the class of pushouts
of morphisms in `P`. -/
def pushouts : MorphismProperty C := fun X Y q ‚Ü¶
  ‚àÉ (A B : C) (p : A ‚ü∂ B) (f : A ‚ü∂ X) (g : B ‚ü∂ Y) (_ : P p),
    IsPushout f p q g

lemma pushouts_mk {A B X Y : C} {f : A ‚ü∂ X} {q : A ‚ü∂ B} {p : X ‚ü∂ Y} {g : B ‚ü∂ Y}
    (sq : IsPushout f q p g) (hq : P q) :
    P.pushouts p :=
  ‚ü®_, _, _, _, _, hq, sq‚ü©

lemma le_pushouts : P ‚â§ P.pushouts := by
  intro X Y p hp
  exact P.pushouts_mk IsPushout.of_id_fst hp

lemma pushouts_monotone : Monotone (pushouts (C := C)) := by
  rintro _ _ h _ _ _ ‚ü®_, _, _, _, _, hp, sq‚ü©
  exact ‚ü®_, _, _, _, _, h _ hp, sq‚ü©

instance : P.pushouts.RespectsIso :=
  RespectsIso.of_respects_arrow_iso _ (by
    rintro q q' e ‚ü®A, B, p, f, g, hp, h‚ü©
    exact ‚ü®A, B, p, f ‚â´ e.hom.left, g ‚â´ e.hom.right, hp,
      IsPushout.paste_horiz h (IsPushout.of_horiz_isIso ‚ü®e.hom.w‚ü©)‚ü©)

instance : P.pullbacks.RespectsIso :=
  RespectsIso.of_respects_arrow_iso _ (by
    rintro q q' e ‚ü®X, Y, p, f, g, hp, h‚ü©
    exact ‚ü®X, Y, p, e.inv.left ‚â´ f, e.inv.right ‚â´ g, hp,
      IsPullback.paste_horiz (IsPullback.of_horiz_isIso ‚ü®e.inv.w‚ü©) h‚ü©)

/-- If `P : MorphismProperty C` is such that any object in `C` maps to the
target of some morphism in `P`, then `P.pushouts` contains the isomorphisms. -/
lemma isomorphisms_le_pushouts
    (h : ‚àÄ (X : C), ‚àÉ (A B : C) (p : A ‚ü∂ B) (_ : P p) (_ : B ‚ü∂ X), IsIso p) :
    isomorphisms C ‚â§ P.pushouts := by
  intro X Y f (_ : IsIso f)
  obtain ‚ü®A, B, p, hp, g, _‚ü© := h X
  exact ‚ü®A, B, p, p ‚â´ g, g ‚â´ f, hp, (IsPushout.of_id_snd (f := p ‚â´ g)).of_iso
    (Iso.refl _) (Iso.refl _) (asIso p) (asIso f) (by simp) (by simp) (by simp) (by simp)‚ü©

/-- A morphism property is `IsStableUnderBaseChange` if the base change of such a morphism
still falls in the class. -/
class IsStableUnderBaseChange : Prop where
  of_isPullback {X Y Y' S : C} {f : X ‚ü∂ S} {g : Y ‚ü∂ S} {f' : Y' ‚ü∂ Y} {g' : Y' ‚ü∂ X}
    (sq : IsPullback f' g' g f) (hg : P g) : P g'

instance : P.pullbacks.IsStableUnderBaseChange where
  of_isPullback := by
    rintro _ _ _ _ _ _ _ _ h ‚ü®_, _, _, _, _, hp, hq‚ü©
    exact P.pullbacks_mk (h.paste_horiz hq) hp

/-- A morphism property is `IsStableUnderCobaseChange` if the cobase change of such a morphism
still falls in the class. -/
class IsStableUnderCobaseChange : Prop where
  of_isPushout {A A' B B' : C} {f : A ‚ü∂ A'} {g : A ‚ü∂ B} {f' : B ‚ü∂ B'} {g' : A' ‚ü∂ B'}
    (sq : IsPushout g f f' g') (hf : P f) : P f'

instance : P.pushouts.IsStableUnderCobaseChange where
  of_isPushout := by
    rintro _ _ _ _ _ _ _ _ h ‚ü®_, _, _, _, _, hp, hq‚ü©
    exact P.pushouts_mk (hq.paste_horiz h) hp

variable {P} in
lemma of_isPullback [P.IsStableUnderBaseChange]
    {X Y Y' S : C} {f : X ‚ü∂ S} {g : Y ‚ü∂ S} {f' : Y' ‚ü∂ Y} {g' : Y' ‚ü∂ X}
    (sq : IsPullback f' g' g f) (hg : P g) : P g' :=
  IsStableUnderBaseChange.of_isPullback sq hg

lemma isStableUnderBaseChange_iff_pullbacks_le :
    P.IsStableUnderBaseChange ‚Üî P.pullbacks ‚â§ P := by
  constructor
  ¬∑ intro h _ _ _ ‚ü®_, _, _, _, _, h‚ÇÅ, h‚ÇÇ‚ü©
    exact of_isPullback h‚ÇÇ h‚ÇÅ
  ¬∑ intro h
    constructor
    intro _ _ _ _ _ _ _ _ h‚ÇÅ h‚ÇÇ
    exact h _ ‚ü®_, _, _, _, _, h‚ÇÇ, h‚ÇÅ‚ü©

lemma pullbacks_le [P.IsStableUnderBaseChange] : P.pullbacks ‚â§ P := by
  rwa [‚Üê isStableUnderBaseChange_iff_pullbacks_le]

variable {P} in
/-- Alternative constructor for `IsStableUnderBaseChange`. -/
theorem IsStableUnderBaseChange.mk' [RespectsIso P]
    (hP‚ÇÇ : ‚àÄ (X Y S : C) (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [HasPullback f g] (_ : P g),
      P (pullback.fst f g)) :
    IsStableUnderBaseChange P where
  of_isPullback {X Y Y' S f g f' g'} sq hg := by
    haveI : HasPullback f g := sq.flip.hasPullback
    let e := sq.flip.isoPullback
    rw [‚Üê P.cancel_left_of_respectsIso e.inv, sq.flip.isoPullback_inv_fst]
    exact hP‚ÇÇ _ _ _ f g hg

variable (C)

instance IsStableUnderBaseChange.isomorphisms :
    (isomorphisms C).IsStableUnderBaseChange where
  of_isPullback {_ _ _ _ f g _ _} h hg :=
    have : IsIso g := hg
    have := hasPullback_of_left_iso g f
    h.isoPullback_hom_snd ‚ñ∏ inferInstanceAs (IsIso _)

instance IsStableUnderBaseChange.monomorphisms :
    (monomorphisms C).IsStableUnderBaseChange where
  of_isPullback {X Y Y' S f g f' g'} h hg := by
    have : Mono g := hg
    constructor
    intro Z f‚ÇÅ f‚ÇÇ h‚ÇÅ‚ÇÇ
    apply PullbackCone.IsLimit.hom_ext h.isLimit
    ¬∑ rw [‚Üê cancel_mono g]
      dsimp
      simp only [Category.assoc, h.w, reassoc_of% h‚ÇÅ‚ÇÇ]
    ¬∑ exact h‚ÇÅ‚ÇÇ

variable {C P}

instance (priority := 900) IsStableUnderBaseChange.respectsIso
    [IsStableUnderBaseChange P] : RespectsIso P := by
  apply RespectsIso.of_respects_arrow_iso
  intro f g e
  exact of_isPullback (IsPullback.of_horiz_isIso (CommSq.mk e.inv.w))

theorem pullback_fst [IsStableUnderBaseChange P]
    {X Y S : C} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [HasPullback f g] (H : P g) :
    P (pullback.fst f g) :=
  of_isPullback (IsPullback.of_hasPullback f g).flip H

theorem pullback_snd [IsStableUnderBaseChange P]
    {X Y S : C} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [HasPullback f g] (H : P f) :
    P (pullback.snd f g) :=
  of_isPullback (IsPullback.of_hasPullback f g) H

theorem baseChange_obj [IsStableUnderBaseChange P] {S S' : C} (f : S' ‚ü∂ S)
    [‚àÄ {W} (h : W ‚ü∂ S), HasPullback h f] (X : Over S) (H : P X.hom) :
    P ((Over.pullback f).obj X).hom :=
  pullback_snd X.hom f H

theorem baseChange_map [IsStableUnderBaseChange P] {S S' : C} (f : S' ‚ü∂ S)
    [‚àÄ {W} (h : W ‚ü∂ S), HasPullback h f] {X Y : Over S} (g : X ‚ü∂ Y)
    (H : P g.left) : P ((Over.pullback f).map g).left := by
  let e :=
    pullbackRightPullbackFstIso Y.hom f g.left ‚â™‚â´
      pullback.congrHom (g.w.trans (Category.comp_id _)) rfl
  have : e.inv ‚â´ (pullback.snd _ _) = ((Over.pullback f).map g).left := by
    ext <;> dsimp [e] <;> simp
  rw [‚Üê this, P.cancel_left_of_respectsIso]
  exact pullback_snd _ _ H

theorem pullback_map [HasPullbacks C]
    [IsStableUnderBaseChange P] [P.IsStableUnderComposition] {S X X' Y Y' : C} {f : X ‚ü∂ S}
    {g : Y ‚ü∂ S} {f' : X' ‚ü∂ S} {g' : Y' ‚ü∂ S} {i‚ÇÅ : X ‚ü∂ X'} {i‚ÇÇ : Y ‚ü∂ Y'} (h‚ÇÅ : P i‚ÇÅ) (h‚ÇÇ : P i‚ÇÇ)
    (e‚ÇÅ : f = i‚ÇÅ ‚â´ f') (e‚ÇÇ : g = i‚ÇÇ ‚â´ g') :
    P (pullback.map f g f' g' i‚ÇÅ i‚ÇÇ (ùüô _) ((Category.comp_id _).trans e‚ÇÅ)
        ((Category.comp_id _).trans e‚ÇÇ)) := by
  have :
    pullback.map f g f' g' i‚ÇÅ i‚ÇÇ (ùüô _) ((Category.comp_id _).trans e‚ÇÅ)
        ((Category.comp_id _).trans e‚ÇÇ) =
      ((pullbackSymmetry _ _).hom ‚â´
          ((Over.pullback _).map (Over.homMk _ e‚ÇÇ.symm : Over.mk g ‚ü∂ Over.mk g')).left) ‚â´
        (pullbackSymmetry _ _).hom ‚â´
          ((Over.pullback g').map (Over.homMk _ e‚ÇÅ.symm : Over.mk f ‚ü∂ Over.mk f')).left := by
    ext <;> simp
  rw [this]
  apply P.comp_mem <;> rw [P.cancel_left_of_respectsIso]
  exacts [baseChange_map _ (Over.homMk _ e‚ÇÇ.symm : Over.mk g ‚ü∂ Over.mk g') h‚ÇÇ,
    baseChange_map _ (Over.homMk _ e‚ÇÅ.symm : Over.mk f ‚ü∂ Over.mk f') h‚ÇÅ]

instance IsStableUnderBaseChange.hasOfPostcompProperty_monomorphisms
    [P.IsStableUnderBaseChange] : P.HasOfPostcompProperty (MorphismProperty.monomorphisms C) where
  of_postcomp {X Y Z} f g (hg : Mono g) hcomp := by
    have : f = (asIso (pullback.fst (f ‚â´ g) g)).inv ‚â´ pullback.snd (f ‚â´ g) g := by
      simp [‚Üê cancel_mono g, pullback.condition]
    rw [this, cancel_left_of_respectsIso (P := P)]
    exact P.pullback_snd _ _ hcomp

lemma of_isPushout [P.IsStableUnderCobaseChange]
    {A A' B B' : C} {f : A ‚ü∂ A'} {g : A ‚ü∂ B} {f' : B ‚ü∂ B'} {g' : A' ‚ü∂ B'}
    (sq : IsPushout g f f' g') (hf : P f) : P f' :=
  IsStableUnderCobaseChange.of_isPushout sq hf

lemma isStableUnderCobaseChange_iff_pushouts_le :
    P.IsStableUnderCobaseChange ‚Üî P.pushouts ‚â§ P := by
  constructor
  ¬∑ intro h _ _ _ ‚ü®_, _, _, _, _, h‚ÇÅ, h‚ÇÇ‚ü©
    exact of_isPushout h‚ÇÇ h‚ÇÅ
  ¬∑ intro h
    constructor
    intro _ _ _ _ _ _ _ _ h‚ÇÅ h‚ÇÇ
    exact h _ ‚ü®_, _, _, _, _, h‚ÇÇ, h‚ÇÅ‚ü©

lemma pushouts_le [P.IsStableUnderCobaseChange] : P.pushouts ‚â§ P := by
  rwa [‚Üê isStableUnderCobaseChange_iff_pushouts_le]

@[simp]
lemma pushouts_le_iff {P Q : MorphismProperty C} [Q.IsStableUnderCobaseChange] :
    P.pushouts ‚â§ Q ‚Üî P ‚â§ Q := by
  constructor
  ¬∑ exact le_trans P.le_pushouts
  ¬∑ intro h
    exact le_trans (pushouts_monotone h) pushouts_le

/-- An alternative constructor for `IsStableUnderCobaseChange`. -/
theorem IsStableUnderCobaseChange.mk' [RespectsIso P]
    (hP‚ÇÇ : ‚àÄ (A B A' : C) (f : A ‚ü∂ A') (g : A ‚ü∂ B) [HasPushout f g] (_ : P f),
      P (pushout.inr f g)) :
    IsStableUnderCobaseChange P where
  of_isPushout {A A' B B' f g f' g'} sq hf := by
    haveI : HasPushout f g := sq.flip.hasPushout
    let e := sq.flip.isoPushout
    rw [‚Üê P.cancel_right_of_respectsIso _ e.hom, sq.flip.inr_isoPushout_hom]
    exact hP‚ÇÇ _ _ _ f g hf

instance IsStableUnderCobaseChange.isomorphisms :
    (isomorphisms C).IsStableUnderCobaseChange where
  of_isPushout {_ _ _ _ f g _ _} h (_ : IsIso f) :=
    have := hasPushout_of_right_iso g f
    h.inl_isoPushout_inv ‚ñ∏ inferInstanceAs (IsIso _)

variable (C) in
instance IsStableUnderCobaseChange.epimorphisms :
    (epimorphisms C).IsStableUnderCobaseChange where
  of_isPushout {X Y Y' S f g f' g'} h hf := by
    have : Epi f := hf
    constructor
    intro Z f‚ÇÅ f‚ÇÇ h‚ÇÅ‚ÇÇ
    apply PushoutCocone.IsColimit.hom_ext h.isColimit
    ¬∑ exact h‚ÇÅ‚ÇÇ
    ¬∑ rw [‚Üê cancel_epi f]
      dsimp
      simp only [‚Üê reassoc_of% h.w, h‚ÇÅ‚ÇÇ]

instance IsStableUnderCobaseChange.respectsIso
    [IsStableUnderCobaseChange P] : RespectsIso P :=
  RespectsIso.of_respects_arrow_iso _ fun _ _ e ‚Ü¶
    of_isPushout (IsPushout.of_horiz_isIso (CommSq.mk e.hom.w))

theorem pushout_inl [IsStableUnderCobaseChange P]
    {A B A' : C} (f : A ‚ü∂ A') (g : A ‚ü∂ B) [HasPushout f g] (H : P g) :
    P (pushout.inl f g) :=
  of_isPushout (IsPushout.of_hasPushout f g) H

theorem pushout_inr [IsStableUnderCobaseChange P]
    {A B A' : C} (f : A ‚ü∂ A') (g : A ‚ü∂ B) [HasPushout f g] (H : P f) : P (pushout.inr f g) :=
  of_isPushout (IsPushout.of_hasPushout f g).flip H

instance IsStableUnderCobaseChange.hasOfPrecompProperty_epimorphisms
    [P.IsStableUnderCobaseChange] : P.HasOfPrecompProperty (MorphismProperty.epimorphisms C) where
  of_precomp {X Y Z} f g (hf : Epi f) hcomp := by
    have : g = pushout.inr (f ‚â´ g) f ‚â´ (asIso (pushout.inl (f ‚â´ g) f)).inv := by
      rw [asIso_inv, IsIso.eq_comp_inv, ‚Üê cancel_epi f, ‚Üê pushout.condition, assoc]
    rw [this, cancel_right_of_respectsIso (P := P)]
    exact P.pushout_inr _ _ hcomp

instance IsStableUnderCobaseChange.op [IsStableUnderCobaseChange P] :
    IsStableUnderBaseChange P.op where
  of_isPullback sq hg := P.of_isPushout sq.unop hg

instance IsStableUnderCobaseChange.unop {P : MorphismProperty C·µí·µñ} [IsStableUnderCobaseChange P] :
    IsStableUnderBaseChange P.unop where
  of_isPullback sq hg := P.of_isPushout sq.op hg

instance IsStableUnderBaseChange.op [IsStableUnderBaseChange P] :
    IsStableUnderCobaseChange P.op where
  of_isPushout sq hf := P.of_isPullback sq.unop hf

instance IsStableUnderBaseChange.unop {P : MorphismProperty C·µí·µñ} [IsStableUnderBaseChange P] :
    IsStableUnderCobaseChange P.unop where
  of_isPushout sq hf := P.of_isPullback sq.op hf

instance IsStableUnderBaseChange.inf {P Q : MorphismProperty C} [IsStableUnderBaseChange P]
    [IsStableUnderBaseChange Q] :
    IsStableUnderBaseChange (P ‚äì Q) where
  of_isPullback hp hg := ‚ü®of_isPullback hp hg.left, of_isPullback hp hg.right‚ü©

instance IsStableUnderCobaseChange.inf {P Q : MorphismProperty C} [IsStableUnderCobaseChange P]
    [IsStableUnderCobaseChange Q] :
    IsStableUnderCobaseChange (P ‚äì Q) where
  of_isPushout hp hg := ‚ü®of_isPushout hp hg.left, of_isPushout hp hg.right‚ü©

instance : (‚ä§ : MorphismProperty C).IsStableUnderBaseChange where
  of_isPullback _ _ := trivial

instance : (‚ä§ : MorphismProperty C).IsStableUnderCobaseChange where
  of_isPushout _ _ := trivial

end

section LimitsOfShape

variable (W : MorphismProperty C) (J : Type*) [Category J]

/-- The class of morphisms in `C` that are limits of shape `J` of
natural transformations involving morphisms in `W`. -/
inductive limitsOfShape : MorphismProperty C
  | mk (X‚ÇÅ X‚ÇÇ : J ‚•§ C) (c‚ÇÅ : Cone X‚ÇÅ) (c‚ÇÇ : Cone X‚ÇÇ)
    (_ : IsLimit c‚ÇÅ) (h‚ÇÇ : IsLimit c‚ÇÇ) (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (_ : W.functorCategory J f) :
      limitsOfShape (h‚ÇÇ.lift (Cone.mk _ (c‚ÇÅ.œÄ ‚â´ f)))

variable {W J} in
lemma limitsOfShape.mk' (X‚ÇÅ X‚ÇÇ : J ‚•§ C) (c‚ÇÅ : Cone X‚ÇÅ) (c‚ÇÇ : Cone X‚ÇÇ)
    (h‚ÇÅ : IsLimit c‚ÇÅ) (h‚ÇÇ : IsLimit c‚ÇÇ) (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (hf : W.functorCategory J f)
    (œÜ : c‚ÇÅ.pt ‚ü∂ c‚ÇÇ.pt) (hœÜ : ‚àÄ j, œÜ ‚â´ c‚ÇÇ.œÄ.app j = c‚ÇÅ.œÄ.app j ‚â´ f.app j) :
    W.limitsOfShape J œÜ := by
  obtain rfl : œÜ = h‚ÇÇ.lift (Cone.mk _ (c‚ÇÅ.œÄ ‚â´ f)) := h‚ÇÇ.hom_ext (fun j ‚Ü¶ by simp [hœÜ])
  exact ‚ü®_, _, _, _, h‚ÇÅ, _, _, hf‚ü©

lemma limitsOfShape_monotone {W‚ÇÅ W‚ÇÇ : MorphismProperty C} (h : W‚ÇÅ ‚â§ W‚ÇÇ)
    (J : Type*) [Category J] :
    W‚ÇÅ.limitsOfShape J ‚â§ W‚ÇÇ.limitsOfShape J := by
  rintro _ _ _ ‚ü®_, _, _, _, h‚ÇÅ, _, f, hf‚ü©
  exact ‚ü®_, _, _, _, h‚ÇÅ, _, f, fun j ‚Ü¶ h _ (hf j)‚ü©

instance : (W.limitsOfShape J).RespectsIso :=
  RespectsIso.of_respects_arrow_iso _ (by
    rintro ‚ü®_, _, f‚ü© ‚ü®Y‚ÇÅ, Y‚ÇÇ, g‚ü© e ‚ü®X‚ÇÅ, X‚ÇÇ, c‚ÇÅ, c‚ÇÇ, h‚ÇÅ, h‚ÇÇ, f, hf‚ü©
    let e‚ÇÅ := Arrow.leftFunc.mapIso e
    let e‚ÇÇ := Arrow.rightFunc.mapIso e
    have fac : g ‚â´ e‚ÇÇ.inv = e‚ÇÅ.inv ‚â´ h‚ÇÇ.lift (Cone.mk _ (c‚ÇÅ.œÄ ‚â´ f)) :=
      e.inv.w.symm
    let c‚ÇÅ' : Cone X‚ÇÅ := { pt := Y‚ÇÅ, œÄ := (Functor.const _).map e‚ÇÅ.inv ‚â´ c‚ÇÅ.œÄ }
    let c‚ÇÇ' : Cone X‚ÇÇ := { pt := Y‚ÇÇ, œÄ := (Functor.const _).map e‚ÇÇ.inv ‚â´ c‚ÇÇ.œÄ }
    have h‚ÇÅ' : IsLimit c‚ÇÅ' := IsLimit.ofIsoLimit h‚ÇÅ (Cones.ext e‚ÇÅ)
    have h‚ÇÇ' : IsLimit c‚ÇÇ' := IsLimit.ofIsoLimit h‚ÇÇ (Cones.ext e‚ÇÇ)
    obtain hg : h‚ÇÇ'.lift (Cone.mk _ (c‚ÇÅ'.œÄ ‚â´ f)) = g :=
      h‚ÇÇ'.hom_ext (fun j ‚Ü¶ by
        rw [h‚ÇÇ'.fac]
        simp [reassoc_of% fac, c‚ÇÅ', c‚ÇÇ'])
    rw [‚Üê hg]
    exact ‚ü®_, _, _, _, h‚ÇÅ', _, _, hf‚ü©)

variable {W J} in
lemma limitsOfShape_limMap {X Y : J ‚•§ C}
    (f : X ‚ü∂ Y) [HasLimit X] [HasLimit Y] (hf : W.functorCategory _ f) :
    W.limitsOfShape J (limMap f) :=
  ‚ü®_, _, _, _, limit.isLimit X, _, _, hf‚ü©

/-- The property that a morphism property `W` is stable under limits
indexed by a category `J`. -/
class IsStableUnderLimitsOfShape : Prop where
  condition (X‚ÇÅ X‚ÇÇ : J ‚•§ C) (c‚ÇÅ : Cone X‚ÇÅ) (c‚ÇÇ : Cone X‚ÇÇ)
    (_ : IsLimit c‚ÇÅ) (h‚ÇÇ : IsLimit c‚ÇÇ) (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (_ : W.functorCategory J f)
    (œÜ : c‚ÇÅ.pt ‚ü∂ c‚ÇÇ.pt) (hœÜ : ‚àÄ j, œÜ ‚â´ c‚ÇÇ.œÄ.app j = c‚ÇÅ.œÄ.app j ‚â´ f.app j) : W œÜ

lemma isStableUnderLimitsOfShape_iff_limitsOfShape_le :
    W.IsStableUnderLimitsOfShape J ‚Üî W.limitsOfShape J ‚â§ W := by
  constructor
  ¬∑ rintro h _ _ _ ‚ü®_, _, _, _, h‚ÇÅ, h‚ÇÇ, f, hf‚ü©
    exact h.condition _ _ _ _ h‚ÇÅ h‚ÇÇ f hf _ (by simp)
  ¬∑ rintro h
    constructor
    intro X‚ÇÅ X‚ÇÇ c‚ÇÅ c‚ÇÇ h‚ÇÅ h‚ÇÇ f hf œÜ hœÜ
    exact h _ (limitsOfShape.mk' X‚ÇÅ X‚ÇÇ c‚ÇÅ c‚ÇÇ h‚ÇÅ h‚ÇÇ f hf œÜ hœÜ)

variable {W J}

lemma limitsOfShape_le [W.IsStableUnderLimitsOfShape J] :
    W.limitsOfShape J ‚â§ W := by
  rwa [‚Üê isStableUnderLimitsOfShape_iff_limitsOfShape_le]

protected lemma limMap [W.IsStableUnderLimitsOfShape J] {X Y : J ‚•§ C}
    (f : X ‚ü∂ Y) [HasLimit X] [HasLimit Y] (hf : W.functorCategory _ f) :
    W (limMap f) :=
  limitsOfShape_le _ (limitsOfShape_limMap _ hf)

@[deprecated (since := "2025-05-11")] alias IsStableUnderLimitsOfShape.limitsOfShape_le :=
  limitsOfShape_le

@[deprecated (since := "2025-05-11")] alias IsStableUnderLimitsOfShape.limMap :=
  MorphismProperty.limMap

end LimitsOfShape

section ColimitsOfShape

variable (W : MorphismProperty C) (J : Type*) [Category J]

/-- The class of morphisms in `C` that are colimits of shape `J` of
natural transformations involving morphisms in `W`. -/
inductive colimitsOfShape : MorphismProperty C
  | mk (X‚ÇÅ X‚ÇÇ : J ‚•§ C) (c‚ÇÅ : Cocone X‚ÇÅ) (c‚ÇÇ : Cocone X‚ÇÇ)
    (h‚ÇÅ : IsColimit c‚ÇÅ) (h‚ÇÇ : IsColimit c‚ÇÇ) (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (_ : W.functorCategory J f) :
      colimitsOfShape (h‚ÇÅ.desc (Cocone.mk _ (f ‚â´ c‚ÇÇ.Œπ)))

variable {W J} in
lemma colimitsOfShape.mk' (X‚ÇÅ X‚ÇÇ : J ‚•§ C) (c‚ÇÅ : Cocone X‚ÇÅ) (c‚ÇÇ : Cocone X‚ÇÇ)
    (h‚ÇÅ : IsColimit c‚ÇÅ) (h‚ÇÇ : IsColimit c‚ÇÇ) (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (hf : W.functorCategory J f)
    (œÜ : c‚ÇÅ.pt ‚ü∂ c‚ÇÇ.pt) (hœÜ : ‚àÄ j, c‚ÇÅ.Œπ.app j ‚â´ œÜ = f.app j ‚â´ c‚ÇÇ.Œπ.app j) :
    W.colimitsOfShape J œÜ := by
  obtain rfl : œÜ = h‚ÇÅ.desc (Cocone.mk _ (f ‚â´ c‚ÇÇ.Œπ)) := h‚ÇÅ.hom_ext (fun j ‚Ü¶ by simp [hœÜ])
  exact ‚ü®_, _, _, _, _, h‚ÇÇ, _, hf‚ü©

lemma colimitsOfShape_monotone {W‚ÇÅ W‚ÇÇ : MorphismProperty C} (h : W‚ÇÅ ‚â§ W‚ÇÇ)
    (J : Type*) [Category J] :
    W‚ÇÅ.colimitsOfShape J ‚â§ W‚ÇÇ.colimitsOfShape J := by
  rintro _ _ _ ‚ü®_, _, _, _, _, h‚ÇÇ, f, hf‚ü©
  exact ‚ü®_, _, _, _, _, h‚ÇÇ, f, fun j ‚Ü¶ h _ (hf j)‚ü©

variable {J} in
lemma colimitsOfShape_le_of_final {J' : Type*} [Category J'] (F : J ‚•§ J') [F.Final] :
    W.colimitsOfShape J' ‚â§ W.colimitsOfShape J := by
  intro _ _ _ ‚ü®X‚ÇÅ, X‚ÇÇ, c‚ÇÅ, c‚ÇÇ, h‚ÇÅ, h‚ÇÇ, f, hf‚ü©
  have h‚ÇÅ' : IsColimit (c‚ÇÅ.whisker F) := (Functor.Final.isColimitWhiskerEquiv F c‚ÇÅ).symm h‚ÇÅ
  have h‚ÇÇ' : IsColimit (c‚ÇÇ.whisker F) := (Functor.Final.isColimitWhiskerEquiv F c‚ÇÇ).symm h‚ÇÇ
  have : h‚ÇÅ.desc (Cocone.mk c‚ÇÇ.pt (f ‚â´ c‚ÇÇ.Œπ)) =
      h‚ÇÅ'.desc (Cocone.mk c‚ÇÇ.pt (Functor.whiskerLeft _ f ‚â´ (c‚ÇÇ.whisker F).Œπ)) :=
    h‚ÇÅ'.hom_ext (fun j ‚Ü¶ by
      have := h‚ÇÅ'.fac (Cocone.mk c‚ÇÇ.pt (Functor.whiskerLeft F f ‚â´ Functor.whiskerLeft F c‚ÇÇ.Œπ)) j
      dsimp at this ‚ä¢
      simp [this])
  rw [this]
  exact ‚ü®_, _, _, _, h‚ÇÅ', h‚ÇÇ', _, fun _ ‚Ü¶ hf _‚ü©

variable {J} in
lemma colimitsOfShape_eq_of_equivalence {J' : Type*} [Category J'] (e : J ‚âå J') :
    W.colimitsOfShape J = W.colimitsOfShape J' :=
  le_antisymm (W.colimitsOfShape_le_of_final e.inverse)
    (W.colimitsOfShape_le_of_final e.functor)

instance : (W.colimitsOfShape J).RespectsIso :=
  RespectsIso.of_respects_arrow_iso _ (by
    rintro ‚ü®_, _, f‚ü© ‚ü®Y‚ÇÅ, Y‚ÇÇ, g‚ü© e ‚ü®X‚ÇÅ, X‚ÇÇ, c‚ÇÅ, c‚ÇÇ, h‚ÇÅ, h‚ÇÇ, f, hf‚ü©
    let e‚ÇÅ := Arrow.leftFunc.mapIso e
    let e‚ÇÇ := Arrow.rightFunc.mapIso e
    have fac : e‚ÇÅ.hom ‚â´ g = h‚ÇÅ.desc (Cocone.mk _ (f ‚â´ c‚ÇÇ.Œπ)) ‚â´ e‚ÇÇ.hom := e.hom.w
    let c‚ÇÅ' : Cocone X‚ÇÅ := { pt := Y‚ÇÅ, Œπ := c‚ÇÅ.Œπ ‚â´ (Functor.const _).map e‚ÇÅ.hom}
    let c‚ÇÇ' : Cocone X‚ÇÇ := { pt := Y‚ÇÇ, Œπ := c‚ÇÇ.Œπ ‚â´ (Functor.const _).map e‚ÇÇ.hom}
    have h‚ÇÅ' : IsColimit c‚ÇÅ' := IsColimit.ofIsoColimit h‚ÇÅ (Cocones.ext e‚ÇÅ)
    have h‚ÇÇ' : IsColimit c‚ÇÇ' := IsColimit.ofIsoColimit h‚ÇÇ (Cocones.ext e‚ÇÇ)
    obtain hg : h‚ÇÅ'.desc (Cocone.mk _ (f ‚â´ c‚ÇÇ'.Œπ)) = g :=
      h‚ÇÅ'.hom_ext (fun j ‚Ü¶ by
        rw [h‚ÇÅ'.fac]
        simp [fac, c‚ÇÅ', c‚ÇÇ'])
    rw [‚Üê hg]
    exact ‚ü®_, _, _, _, _, h‚ÇÇ', _, hf‚ü©)

variable {W J} in
lemma colimitsOfShape_colimMap {X Y : J ‚•§ C}
    (f : X ‚ü∂ Y) [HasColimit X] [HasColimit Y] (hf : W.functorCategory _ f) :
    W.colimitsOfShape J (colimMap f) :=
  ‚ü®_, _, _, _, _, colimit.isColimit Y, _, hf‚ü©

attribute [local instance] IsCofiltered.isConnected in
variable {W} in
lemma colimitsOfShape.of_isColimit
    {J : Type*} [Preorder J] [OrderBot J] {F : J ‚•§ C}
    {c : Cocone F} (hc : IsColimit c) (h : ‚àÄ (j : J), W (F.map (homOfLE bot_le : ‚ä• ‚ü∂ j))) :
    W.colimitsOfShape J (c.Œπ.app ‚ä•) :=
  .mk' _ _ _ _ (isColimitConstCocone J (F.obj ‚ä•)) hc
    { app k := F.map (homOfLE bot_le)
      naturality _ _ _ := by
        dsimp
        rw [Category.id_comp, ‚Üê Functor.map_comp]
        rfl} h _ (by simp)

/-- The property that a morphism property `W` is stable under colimits
indexed by a category `J`. -/
class IsStableUnderColimitsOfShape : Prop where
  condition (X‚ÇÅ X‚ÇÇ : J ‚•§ C) (c‚ÇÅ : Cocone X‚ÇÅ) (c‚ÇÇ : Cocone X‚ÇÇ)
    (h‚ÇÅ : IsColimit c‚ÇÅ) (h‚ÇÅ : IsColimit c‚ÇÇ) (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (_ : W.functorCategory J f)
    (œÜ : c‚ÇÅ.pt ‚ü∂ c‚ÇÇ.pt) (hœÜ : ‚àÄ j, c‚ÇÅ.Œπ.app j ‚â´ œÜ = f.app j ‚â´ c‚ÇÇ.Œπ.app j) : W œÜ

lemma isStableUnderColimitsOfShape_iff_colimitsOfShape_le :
    W.IsStableUnderColimitsOfShape J ‚Üî W.colimitsOfShape J ‚â§ W := by
  constructor
  ¬∑ rintro h _ _ _ ‚ü®_, _, _, _, h‚ÇÅ, h‚ÇÇ, f, hf‚ü©
    exact h.condition _ _ _ _ h‚ÇÅ h‚ÇÇ f hf _ (by simp)
  ¬∑ rintro h
    constructor
    intro X‚ÇÅ X‚ÇÇ c‚ÇÅ c‚ÇÇ h‚ÇÅ h‚ÇÇ f hf œÜ hœÜ
    exact h _ (colimitsOfShape.mk' X‚ÇÅ X‚ÇÇ c‚ÇÅ c‚ÇÇ h‚ÇÅ h‚ÇÇ f hf œÜ hœÜ)

variable {W J}

lemma colimitsOfShape_le [W.IsStableUnderColimitsOfShape J] :
    W.colimitsOfShape J ‚â§ W := by
  rwa [‚Üê isStableUnderColimitsOfShape_iff_colimitsOfShape_le]

protected lemma colimMap [W.IsStableUnderColimitsOfShape J] {X Y : J ‚•§ C}
    (f : X ‚ü∂ Y) [HasColimit X] [HasColimit Y] (hf : W.functorCategory _ f) :
    W (colimMap f) :=
  colimitsOfShape_le _ (colimitsOfShape_colimMap _ hf)

@[deprecated (since := "2025-05-11")] alias IsStableUnderColimitsOfShape.colimMap :=
  MorphismProperty.colimMap

@[deprecated (since := "2025-05-11")] alias IsStableUnderColimitsOfShape.colimitsOfShape_le :=
  colimitsOfShape_le

variable (C J) in
instance IsStableUnderColimitsOfShape.isomorphisms :
    (isomorphisms C).IsStableUnderColimitsOfShape J where
  condition F‚ÇÅ F‚ÇÇ c‚ÇÅ c‚ÇÇ h‚ÇÅ h‚ÇÇ f (_ : ‚àÄ j, IsIso (f.app j)) œÜ hœÜ := by
    have := NatIso.isIso_of_isIso_app f
    exact ‚ü®h‚ÇÇ.desc (Cocone.mk _ (inv f ‚â´ c‚ÇÅ.Œπ)),
      h‚ÇÅ.hom_ext (fun j ‚Ü¶ by simp [reassoc_of% (hœÜ j)]),
      h‚ÇÇ.hom_ext (by simp [hœÜ])‚ü©

end ColimitsOfShape

/-- The condition that a property of morphisms is stable by filtered colimits. -/
@[pp_with_univ]
class IsStableUnderFilteredColimits (W : MorphismProperty C) : Prop where
  isStableUnderColimitsOfShape (J : Type w') [Category.{w} J] [IsFiltered J] :
    W.IsStableUnderColimitsOfShape J := by infer_instance

attribute [instance] IsStableUnderFilteredColimits.isStableUnderColimitsOfShape

instance : IsStableUnderFilteredColimits.{w, w'} (isomorphisms C) where

section Coproducts

variable (W : MorphismProperty C)

/-- Given `W : MorphismProperty C`, this is class of morphisms that are
isomorphic to a coproduct of a family (indexed by some `J : Type w`) of maps in `W`. -/
@[pp_with_univ]
def coproducts : MorphismProperty C := ‚®Ü (J : Type w), W.colimitsOfShape (Discrete J)

lemma colimitsOfShape_le_coproducts (J : Type w) :
    W.colimitsOfShape (Discrete J) ‚â§ coproducts.{w} W :=
  le_iSup (f := fun (J : Type w) ‚Ü¶ W.colimitsOfShape (Discrete J)) J

lemma coproducts_iff {X Y : C} (f : X ‚ü∂ Y) :
    coproducts.{w} W f ‚Üî ‚àÉ (J : Type w), W.colimitsOfShape (Discrete J) f := by
  simp only [coproducts, iSup_iff]

lemma coproducts_of_small {X Y : C} (f : X ‚ü∂ Y) {J : Type w'}
    (hf : W.colimitsOfShape (Discrete J) f) [Small.{w} J] :
    coproducts.{w} W f := by
  rw [coproducts_iff]
  refine ‚ü®Shrink J, ?_‚ü©
  rwa [‚Üê W.colimitsOfShape_eq_of_equivalence (Discrete.equivalence (equivShrink.{w} J))]

lemma le_colimitsOfShape_punit : W ‚â§ W.colimitsOfShape (Discrete PUnit.{w + 1}) := by
  intro X‚ÇÅ X‚ÇÇ f hf
  have h := initialIsInitial (C := Discrete (PUnit.{w + 1}))
  let c‚ÇÅ := coconeOfDiagramInitial (F := Discrete.functor (fun _ ‚Ü¶ X‚ÇÅ)) h
  let c‚ÇÇ := coconeOfDiagramInitial (F := Discrete.functor (fun _ ‚Ü¶ X‚ÇÇ)) h
  have hc‚ÇÅ : IsColimit c‚ÇÅ := colimitOfDiagramInitial h _
  have hc‚ÇÇ : IsColimit c‚ÇÇ := colimitOfDiagramInitial h _
  have : hc‚ÇÅ.desc (Cocone.mk _ (Discrete.natTrans (fun _ ‚Ü¶ by exact f) ‚â´ c‚ÇÇ.Œπ)) = f :=
    hc‚ÇÅ.hom_ext (fun x ‚Ü¶ by
      obtain rfl : x = ‚ä•_ _ := by ext
      rw [IsColimit.fac]
      simp [c‚ÇÅ, c‚ÇÇ])
  rw [‚Üê this]
  exact ‚ü®_, _, _, _, _, hc‚ÇÇ, _, fun _ ‚Ü¶ hf‚ü©

lemma le_coproducts : W ‚â§ coproducts.{w} W :=
  (le_colimitsOfShape_punit.{w} W).trans
    (colimitsOfShape_le_coproducts W PUnit.{w + 1})

lemma coproducts_monotone : Monotone (coproducts.{w} (C := C)) := by
  rintro W‚ÇÅ W‚ÇÇ h X Y f hf
  rw [coproducts_iff] at hf
  obtain ‚ü®J, hf‚ü© := hf
  exact W‚ÇÇ.colimitsOfShape_le_coproducts J _
    (colimitsOfShape_monotone h _ _ hf)

end Coproducts

section Products

variable (W : MorphismProperty C)

/-- The property that a morphism property `W` is stable under products indexed by a type `J`. -/
abbrev IsStableUnderProductsOfShape (J : Type*) := W.IsStableUnderLimitsOfShape (Discrete J)

/-- The property that a morphism property `W` is stable under coproducts indexed by a type `J`. -/
abbrev IsStableUnderCoproductsOfShape (J : Type*) := W.IsStableUnderColimitsOfShape (Discrete J)

lemma IsStableUnderProductsOfShape.mk (J : Type*) [W.RespectsIso]
    (hW : ‚àÄ (X‚ÇÅ X‚ÇÇ : J ‚Üí C) [HasProduct X‚ÇÅ] [HasProduct X‚ÇÇ]
      (f : ‚àÄ j, X‚ÇÅ j ‚ü∂ X‚ÇÇ j) (_ : ‚àÄ (j : J), W (f j)),
      W (Limits.Pi.map f)) : W.IsStableUnderProductsOfShape J where
  condition X‚ÇÅ X‚ÇÇ c‚ÇÅ c‚ÇÇ hc‚ÇÅ hc‚ÇÇ f hf Œ± hŒ± := by
    let œÜ := fun j => f.app (Discrete.mk j)
    have : HasLimit X‚ÇÅ := ‚ü®c‚ÇÅ, hc‚ÇÅ‚ü©
    have : HasLimit X‚ÇÇ := ‚ü®c‚ÇÇ, hc‚ÇÇ‚ü©
    have : HasProduct fun j ‚Ü¶ X‚ÇÅ.obj (Discrete.mk j) :=
      hasLimit_of_iso (Discrete.natIso (fun j ‚Ü¶ Iso.refl (X‚ÇÅ.obj j)))
    have : HasProduct fun j ‚Ü¶ X‚ÇÇ.obj (Discrete.mk j) :=
      hasLimit_of_iso (Discrete.natIso (fun j ‚Ü¶ Iso.refl (X‚ÇÇ.obj j)))
    have hf' := hW _ _ œÜ (fun j => hf (Discrete.mk j))
    refine (W.arrow_mk_iso_iff ?_).2 hf'
    refine Arrow.isoMk
      (IsLimit.conePointUniqueUpToIso hc‚ÇÅ (limit.isLimit X‚ÇÅ) ‚â™‚â´ (Pi.isoLimit X‚ÇÅ).symm)
      (IsLimit.conePointUniqueUpToIso hc‚ÇÇ (limit.isLimit X‚ÇÇ) ‚â™‚â´ (Pi.isoLimit _).symm) ?_
    apply limit.hom_ext
    rintro ‚ü®j‚ü©
    simp [œÜ, hŒ±]

lemma IsStableUnderCoproductsOfShape.mk (J : Type*) [W.RespectsIso]
    (hW : ‚àÄ (X‚ÇÅ X‚ÇÇ : J ‚Üí C) [HasCoproduct X‚ÇÅ] [HasCoproduct X‚ÇÇ]
      (f : ‚àÄ j, X‚ÇÅ j ‚ü∂ X‚ÇÇ j) (_ : ‚àÄ (j : J), W (f j)),
      W (Limits.Sigma.map f)) : W.IsStableUnderCoproductsOfShape J where
  condition X‚ÇÅ X‚ÇÇ c‚ÇÅ c‚ÇÇ hc‚ÇÅ hc‚ÇÇ f hf Œ± hŒ± := by
    let œÜ := fun j => f.app (Discrete.mk j)
    have : HasColimit X‚ÇÅ := ‚ü®c‚ÇÅ, hc‚ÇÅ‚ü©
    have : HasColimit X‚ÇÇ := ‚ü®c‚ÇÇ, hc‚ÇÇ‚ü©
    have : HasCoproduct fun j ‚Ü¶ X‚ÇÅ.obj (Discrete.mk j) :=
      hasColimit_of_iso (Discrete.natIso (fun j ‚Ü¶ Iso.refl (X‚ÇÅ.obj j)))
    have : HasCoproduct fun j ‚Ü¶ X‚ÇÇ.obj (Discrete.mk j) :=
      hasColimit_of_iso (Discrete.natIso (fun j ‚Ü¶ Iso.refl (X‚ÇÇ.obj j)))
    have hf' := hW _ _ œÜ (fun j => hf (Discrete.mk j))
    refine (W.arrow_mk_iso_iff ?_).1 hf'
    refine Arrow.isoMk
      ((Sigma.isoColimit _) ‚â™‚â´ IsColimit.coconePointUniqueUpToIso (colimit.isColimit X‚ÇÅ) hc‚ÇÅ)
      ((Sigma.isoColimit _) ‚â™‚â´ IsColimit.coconePointUniqueUpToIso (colimit.isColimit X‚ÇÇ) hc‚ÇÇ) ?_
    apply colimit.hom_ext
    rintro ‚ü®j‚ü©
    simp [œÜ, hŒ±]

/-- The condition that a property of morphisms is stable by finite products. -/
class IsStableUnderFiniteProducts : Prop where
  isStableUnderProductsOfShape (J : Type) [Finite J] : W.IsStableUnderProductsOfShape J

attribute [instance] IsStableUnderFiniteProducts.isStableUnderProductsOfShape

/-- The condition that a property of morphisms is stable by finite coproducts. -/
class IsStableUnderFiniteCoproducts : Prop where
  isStableUnderCoproductsOfShape (J : Type) [Finite J] : W.IsStableUnderCoproductsOfShape J

attribute [instance] IsStableUnderFiniteCoproducts.isStableUnderCoproductsOfShape

@[deprecated "This is now an instance." (since := "2025-05-11")]
alias isStableUnderProductsOfShape_of_isStableUnderFiniteProducts :=
  IsStableUnderFiniteProducts.isStableUnderProductsOfShape

@[deprecated "This is now an instance." (since := "2025-05-11")]
alias isStableUnderCoproductsOfShape_of_isStableUnderFiniteCoproducts :=
  IsStableUnderFiniteCoproducts.isStableUnderCoproductsOfShape

/-- The condition that a property of morphisms is stable by coproducts. -/
@[pp_with_univ]
class IsStableUnderCoproducts : Prop where
  isStableUnderCoproductsOfShape (J : Type w) : W.IsStableUnderCoproductsOfShape J := by
    infer_instance

attribute [instance] IsStableUnderCoproducts.isStableUnderCoproductsOfShape

lemma coproducts_le [IsStableUnderCoproducts.{w} W] :
    coproducts.{w} W ‚â§ W := by
  intro X Y f hf
  rw [coproducts_iff] at hf
  obtain ‚ü®J, hf‚ü© := hf
  exact colimitsOfShape_le _ hf

@[simp]
lemma coproducts_eq_self [IsStableUnderCoproducts.{w} W] :
    coproducts.{w} W = W :=
  le_antisymm W.coproducts_le W.le_coproducts

@[simp]
lemma coproducts_le_iff {P Q : MorphismProperty C} [IsStableUnderCoproducts.{w} Q] :
    coproducts.{w} P ‚â§ Q ‚Üî P ‚â§ Q := by
  constructor
  ¬∑ exact le_trans P.le_coproducts
  ¬∑ intro h
    exact le_trans (coproducts_monotone h) Q.coproducts_le

end Products

section Diagonal

variable [HasPullbacks C] {P : MorphismProperty C}

/-- For `P : MorphismProperty C`, `P.diagonal` is a morphism property that holds for `f : X ‚ü∂ Y`
whenever `P` holds for `X ‚ü∂ Y x‚Çì Y`. -/
def diagonal (P : MorphismProperty C) : MorphismProperty C := fun _ _ f => P (pullback.diagonal f)

theorem diagonal_iff {X Y : C} {f : X ‚ü∂ Y} : P.diagonal f ‚Üî P (pullback.diagonal f) :=
  Iff.rfl

instance RespectsIso.diagonal [P.RespectsIso] : P.diagonal.RespectsIso := by
  apply RespectsIso.mk
  ¬∑ introv H
    rwa [diagonal_iff, pullback.diagonal_comp, P.cancel_left_of_respectsIso,
      P.cancel_left_of_respectsIso, ‚Üê P.cancel_right_of_respectsIso _
        (pullback.map (e.hom ‚â´ f) (e.hom ‚â´ f) f f e.hom e.hom (ùüô Z) (by simp) (by simp)),
      ‚Üê pullback.condition, P.cancel_left_of_respectsIso]
  ¬∑ introv H
    delta diagonal
    rwa [pullback.diagonal_comp, P.cancel_right_of_respectsIso]

instance diagonal_isStableUnderComposition [P.IsStableUnderComposition] [RespectsIso P]
    [IsStableUnderBaseChange P] : P.diagonal.IsStableUnderComposition where
  comp_mem _ _ h‚ÇÅ h‚ÇÇ := by
    rw [diagonal_iff, pullback.diagonal_comp]
    exact P.comp_mem _ _ h‚ÇÅ
      (by simpa only [cancel_left_of_respectsIso] using P.pullback_snd _ _ h‚ÇÇ)

instance IsStableUnderBaseChange.diagonal [IsStableUnderBaseChange P] [P.RespectsIso] :
    P.diagonal.IsStableUnderBaseChange :=
  IsStableUnderBaseChange.mk'
    (by
      introv h
      rw [diagonal_iff, diagonal_pullback_fst, P.cancel_left_of_respectsIso,
        P.cancel_right_of_respectsIso]
      exact P.baseChange_map f _ (by simpa))

lemma diagonal_isomorphisms : (isomorphisms C).diagonal = monomorphisms C :=
  ext _ _ fun _ _ _ ‚Ü¶ pullback.isIso_diagonal_iff _

/-- If `P` is multiplicative and stable under base change, having the of-postcomp property
w.r.t. `Q` is equivalent to `Q` implying `P` on the diagonal. -/
lemma hasOfPostcompProperty_iff_le_diagonal [P.IsStableUnderBaseChange]
    [P.IsMultiplicative] {Q : MorphismProperty C} [Q.IsStableUnderBaseChange] :
    P.HasOfPostcompProperty Q ‚Üî Q ‚â§ P.diagonal := by
  refine ‚ü®fun hP X Y f hf ‚Ü¶ ?_, fun hP ‚Ü¶ ‚ü®fun {Y X S} g f hf hcomp ‚Ü¶ ?_‚ü©‚ü©
  ¬∑ exact hP.of_postcomp _ _ (Q.pullback_fst _ _ hf) (by simpa using P.id_mem X)
  ¬∑ set gr : Y ‚ü∂ pullback (g ‚â´ f) f := pullback.lift (ùüô Y) g (by simp)
    have : g = gr ‚â´ pullback.snd _ _ := by simp [gr]
    rw [this]
    apply P.comp_mem
    ¬∑ exact P.of_isPullback (pullback_lift_diagonal_isPullback g f) (hP _ hf)
    ¬∑ exact P.pullback_snd _ _ hcomp

end Diagonal

section Universally

/-- `P.universally` holds for a morphism `f : X ‚ü∂ Y` iff `P` holds for all `X √ó[Y] Y' ‚ü∂ Y'`. -/
def universally (P : MorphismProperty C) : MorphismProperty C := fun X Y f =>
  ‚àÄ ‚¶ÉX' Y' : C‚¶Ñ (i‚ÇÅ : X' ‚ü∂ X) (i‚ÇÇ : Y' ‚ü∂ Y) (f' : X' ‚ü∂ Y') (_ : IsPullback f' i‚ÇÅ i‚ÇÇ f), P f'

instance universally_respectsIso (P : MorphismProperty C) : P.universally.RespectsIso := by
  apply RespectsIso.mk
  ¬∑ intro X Y Z e f hf X' Z' i‚ÇÅ i‚ÇÇ f' H
    have : IsPullback (ùüô _) (i‚ÇÅ ‚â´ e.hom) i‚ÇÅ e.inv :=
      IsPullback.of_horiz_isIso
        ‚ü®by rw [Category.id_comp, Category.assoc, e.hom_inv_id, Category.comp_id]‚ü©
    exact hf _ _ _
      (by simpa only [Iso.inv_hom_id_assoc, Category.id_comp] using this.paste_horiz H)
  ¬∑ intro X Y Z e f hf X' Z' i‚ÇÅ i‚ÇÇ f' H
    have : IsPullback (ùüô _) i‚ÇÇ (i‚ÇÇ ‚â´ e.inv) e.inv :=
      IsPullback.of_horiz_isIso ‚ü®Category.id_comp _‚ü©
    exact hf _ _ _ (by simpa only [Category.assoc, Iso.hom_inv_id,
      Category.comp_id, Category.comp_id] using H.paste_horiz this)

instance universally_isStableUnderBaseChange (P : MorphismProperty C) :
    P.universally.IsStableUnderBaseChange where
  of_isPullback H h‚ÇÅ _ _ _ _ _ H' := h‚ÇÅ _ _ _ (H'.paste_vert H.flip)

instance IsStableUnderComposition.universally [HasPullbacks C] (P : MorphismProperty C)
    [hP : P.IsStableUnderComposition] : P.universally.IsStableUnderComposition where
  comp_mem {X Y Z} f g hf hg X' Z' i‚ÇÅ i‚ÇÇ f' H := by
    have := pullback.lift_fst _ _ (H.w.trans (Category.assoc _ _ _).symm)
    rw [‚Üê this] at H ‚ä¢
    apply P.comp_mem _ _ _ (hg _ _ _ <| IsPullback.of_hasPullback _ _)
    exact hf _ _ _ (H.of_right (pullback.lift_snd _ _ _) (IsPullback.of_hasPullback i‚ÇÇ g))

theorem universally_le (P : MorphismProperty C) : P.universally ‚â§ P := by
  intro X Y f hf
  exact hf (ùüô _) (ùüô _) _ (IsPullback.of_vert_isIso ‚ü®by rw [Category.comp_id, Category.id_comp]‚ü©)

theorem universally_inf (P Q : MorphismProperty C) :
    (P ‚äì Q).universally = P.universally ‚äì Q.universally := by
  ext X Y f
  change _ ‚Üî _ ‚àß _
  simp_rw [universally, ‚Üê forall_and]
  rfl

theorem universally_eq_iff {P : MorphismProperty C} :
    P.universally = P ‚Üî P.IsStableUnderBaseChange :=
  ‚ü®(¬∑ ‚ñ∏ P.universally_isStableUnderBaseChange),
    fun hP ‚Ü¶ P.universally_le.antisymm fun _ _ _ hf _ _ _ _ _ H => hP.of_isPullback H.flip hf‚ü©

theorem IsStableUnderBaseChange.universally_eq {P : MorphismProperty C}
    [hP : P.IsStableUnderBaseChange] : P.universally = P := universally_eq_iff.mpr hP

theorem universally_mono : Monotone (universally : MorphismProperty C ‚Üí MorphismProperty C) :=
  fun _ _ h _ _ _ h‚ÇÅ _ _ _ _ _ H => h _ (h‚ÇÅ _ _ _ H)

lemma universally_mk' (P : MorphismProperty C) [P.RespectsIso] {X Y : C} (g : X ‚ü∂ Y)
    (H : ‚àÄ {T : C} (f : T ‚ü∂ Y) [HasPullback f g], P (pullback.fst f g)) :
    universally P g := by
  introv X' h
  have := h.hasPullback
  rw [‚Üê h.isoPullback_hom_fst, P.cancel_left_of_respectsIso]
  exact H ..

end Universally

variable (P : MorphismProperty C)

/-- `P` has pullbacks if for every `f` satisfying `P`, pullbacks of arbitrary morphisms along `f`
exist. -/
protected class HasPullbacks : Prop where
  hasPullback {X Y S : C} {f : X ‚ü∂ S} (g : Y ‚ü∂ S) : P f ‚Üí HasPullback f g := by infer_instance

instance [HasPullbacks C] : P.HasPullbacks where

alias hasPullback := HasPullbacks.hasPullback

end MorphismProperty

end CategoryTheory
