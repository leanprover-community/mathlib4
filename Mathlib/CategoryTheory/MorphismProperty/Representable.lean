/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle SÃ¶nne, JoÃ«l Riou, Ravi Vakil
-/

import Mathlib.CategoryTheory.MorphismProperty.Limits

/-!

# Relatively representable morphisms

In this file we define and develop basic results about relatively representable morphisms.

Classically, a morphism `f : F âŸ¶ G` of presheaves is said to be representable if for any morphism
`g : yoneda.obj X âŸ¶ G`, there exists a pullback square of the following form
```
  yoneda.obj Y --yoneda.map snd--> yoneda.obj X
      |                                |
     fst                               g
      |                                |
      v                                v
      F ------------ f --------------> G
```

In this file, we define a notion of relative representability which works with respect to any
functor, and not just `yoneda`. The fact that a morphism `f : F âŸ¶ G` between presheaves is
representable in the classical case will then be given by `yoneda.relativelyRepresentable f`.

## Main definitions

Throughout this file, `F : C â¥¤ D` is a functor between categories `C` and `D`.

* `Functor.relativelyRepresentable`: A morphism `f : X âŸ¶ Y` in `D` is said to be relatively
  representable with respect to `F`, if for any `g : F.obj a âŸ¶ Y`, there exists a pullback square
  of the following form
```
  F.obj b --F.map snd--> F.obj a
      |                     |
     fst                    g
      |                     |
      v                     v
      X ------- f --------> Y
```

* `MorphismProperty.relative`: Given a morphism property `P` in `C`, a morphism `f : X âŸ¶ Y` in `D`
  satisfies `P.relative F` if it is relatively representable and for any `g : F.obj a âŸ¶ Y`, the
  property `P` holds for any represented pullback of `f` by `g`.

## API

Given `hf : relativelyRepresentable f`, with `f : X âŸ¶ Y` and `g : F.obj a âŸ¶ Y`, we provide:
* `hf.pullback g` which is the object in `C` such that `F.obj (hf.pullback g)` is a
  pullback of `f` and `g`.
* `hf.snd g` is the morphism `hf.pullback g âŸ¶ F.obj a`
* `hf.fst g` is the morphism `F.obj (hf.pullback g) âŸ¶ X`
*  If `F` is full, and `f` is of type `F.obj c âŸ¶ G`, we also have `hf.fst' g : hf.pullback g âŸ¶ X`
  which is the preimage under `F` of `hf.fst g`.
* `hom_ext`, `hom_ext'`, `lift`, `lift'` are variants of the universal property of
  `F.obj (hf.pullback g)`, where as much as possible has been formulated internally to `C`.
  For these theorems we also need that `F` is full and/or faithful.
* `symmetry` and `symmetryIso` are variants of the fact that pullbacks are symmetric for
  representable morphisms, formulated internally to `C`. We assume that `F` is fully faithful here.

We also provide some basic API for dealing with triple pullbacks, i.e. given
`hfâ‚ : relativelyRepresentable fâ‚`, `fâ‚‚ : F.obj Aâ‚‚ âŸ¶ X` and `fâ‚ƒ : F.obj Aâ‚ƒ âŸ¶ X`, we define
`hfâ‚.pullbackâ‚ƒ fâ‚‚ fâ‚ƒ` to be the pullback of `(Aâ‚ Ã—_X Aâ‚‚) Ã—_{Aâ‚} (Aâ‚ Ã—_X Aâ‚ƒ)`. We then develop
some API for working with this object, mirroring the usual API for pullbacks, but where as much
as possible is phrased internally to `C`.

## Main results

* `relativelyRepresentable.isMultiplicative`: The class of relatively representable morphisms is
  multiplicative.
* `relativelyRepresentable.isStableUnderBaseChange`: Being relatively representable is stable under
  base change.
* `relativelyRepresentable.of_isIso`: Isomorphisms are relatively representable.

-/

namespace CategoryTheory

open Category Limits MorphismProperty

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D] (F : C â¥¤ D)

/-- A morphism `f : X âŸ¶ Y` in `D` is said to be relatively representable if for any
`g : F.obj a âŸ¶ Y`, there exists a pullback square of the following form
```
F.obj b --F.map snd--> F.obj a
    |                     |
   fst                    g
    |                     |
    v                     v
    X ------- f --------> Y
```
-/
def Functor.relativelyRepresentable : MorphismProperty D :=
  fun X Y f â†¦ âˆ€ â¦ƒa : Câ¦„ (g : F.obj a âŸ¶ Y), âˆƒ (b : C) (snd : b âŸ¶ a)
    (fst : F.obj b âŸ¶ X), IsPullback fst (F.map snd) f g

namespace Functor.relativelyRepresentable

section

variable {F}
variable {X Y : D} {f : X âŸ¶ Y} (hf : F.relativelyRepresentable f)
  {b : C} {f' : F.obj b âŸ¶ Y} (hf' : F.relativelyRepresentable f')
  {a : C} (g : F.obj a âŸ¶ Y) (hg : F.relativelyRepresentable g)

/-- Let `f : X âŸ¶ Y` be a relatively representable morphism in `D`. Then, for any
`g : F.obj a âŸ¶ Y`, `hf.pullback g` denotes the (choice of) a corresponding object in `C` such that
there is a pullback square of the following form
```
hf.pullback g --F.map snd--> F.obj a
    |                          |
   fst                         g
    |                          |
    v                          v
    X ---------- f ----------> Y
``` -/
noncomputable def pullback : C :=
  (hf g).choose

/-- Given a representable morphism `f : X âŸ¶ Y`, then for any `g : F.obj a âŸ¶ Y`, `hf.snd g`
denotes the morphism in `C` giving rise to the following diagram
```
hf.pullback g --F.map (hf.snd g)--> F.obj a
    |                                 |
   fst                                g
    |                                 |
    v                                 v
    X -------------- f -------------> Y
``` -/
noncomputable abbrev snd : hf.pullback g âŸ¶ a :=
  (hf g).choose_spec.choose

/-- Given a relatively representable morphism `f : X âŸ¶ Y`, then for any `g : F.obj a âŸ¶ Y`,
`hf.fst g` denotes the first projection in the following diagram, given by the defining property
of `f` being relatively representable
```
hf.pullback g --F.map (hf.snd g)--> F.obj a
    |                                 |
hf.fst g                              g
    |                                 |
    v                                 v
    X -------------- f -------------> Y
``` -/
noncomputable abbrev fst : F.obj (hf.pullback g) âŸ¶ X :=
  (hf g).choose_spec.choose_spec.choose

/-- When `F` is full, given a representable morphism `f' : F.obj b âŸ¶ Y`, then `hf'.fst' g` denotes
the preimage of `hf'.fst g` under `F`. -/
noncomputable abbrev fst' [Full F] : hf'.pullback g âŸ¶ b :=
  F.preimage (hf'.fst g)

lemma map_fst' [Full F] : F.map (hf'.fst' g) = hf'.fst g :=
  F.map_preimage _

lemma isPullback : IsPullback (hf.fst g) (F.map (hf.snd g)) f g :=
  (hf g).choose_spec.choose_spec.choose_spec

@[reassoc]
lemma w : hf.fst g â‰« f = F.map (hf.snd g) â‰« g := (hf.isPullback g).w

/-- Variant of the pullback square when `F` is full, and given `f' : F.obj b âŸ¶ Y`. -/
lemma isPullback' [Full F] : IsPullback (F.map (hf'.fst' g)) (F.map (hf'.snd g)) f' g :=
  (hf'.map_fst' _) â–¸ hf'.isPullback g

@[reassoc]
lemma w' {X Y Z : C} {f : X âŸ¶ Z} (hf : F.relativelyRepresentable (F.map f)) (g : Y âŸ¶ Z)
    [Full F] [Faithful F] : hf.fst' (F.map g) â‰« f = hf.snd (F.map g) â‰« g :=
  F.map_injective <| by simp [(hf.w (F.map g))]

lemma isPullback_of_map {X Y Z : C} {f : X âŸ¶ Z} (hf : F.relativelyRepresentable (F.map f))
    (g : Y âŸ¶ Z) [Full F] [Faithful F] :
    IsPullback (hf.fst' (F.map g)) (hf.snd (F.map g)) f g :=
  IsPullback.of_map F (hf.w' g) (hf.isPullback' (F.map g))

variable {g}

/-- Two morphisms `a b : c âŸ¶ hf.pullback g` are equal if
* Their compositions (in `C`) with `hf.snd g : hf.pullback  âŸ¶ X` are equal.
* The compositions of `F.map a` and `F.map b` with `hf.fst g` are equal. -/
@[ext 100]
lemma hom_ext [Faithful F] {c : C} {a b : c âŸ¶ hf.pullback g}
    (hâ‚ : F.map a â‰« hf.fst g = F.map b â‰« hf.fst g)
    (hâ‚‚ : a â‰« hf.snd g = b â‰« hf.snd g) : a = b :=
  F.map_injective <|
    PullbackCone.IsLimit.hom_ext (hf.isPullback g).isLimit hâ‚ (by simpa using F.congr_map hâ‚‚)

/-- In the case of a representable morphism `f' : F.obj Y âŸ¶ G`, whose codomain lies
in the image of `F`, we get that two morphism `a b : Z âŸ¶ hf.pullback g` are equal if
* Their compositions (in `C`) with `hf'.snd g : hf.pullback  âŸ¶ X` are equal.
* Their compositions (in `C`) with `hf'.fst' g : hf.pullback  âŸ¶ Y` are equal. -/
@[ext]
lemma hom_ext' [Full F] [Faithful F] {c : C} {a b : c âŸ¶ hf'.pullback g}
    (hâ‚ : a â‰« hf'.fst' g = b â‰« hf'.fst' g)
    (hâ‚‚ : a â‰« hf'.snd g = b â‰« hf'.snd g) : a = b :=
  hf'.hom_ext (by simpa [map_fst'] using F.congr_map hâ‚) hâ‚‚

section

variable {c : C} (i : F.obj c âŸ¶ X) (h : c âŸ¶ a) (hi : i â‰« f = F.map h â‰« g)

/-- The lift (in `C`) obtained from the universal property of `F.obj (hf.pullback g)`, in the
case when the cone point is in the image of `F.obj`. -/
noncomputable def lift [Full F] : c âŸ¶ hf.pullback g :=
  F.preimage <| PullbackCone.IsLimit.lift (hf.isPullback g).isLimit _ _ hi

@[reassoc (attr := simp)]
lemma lift_fst [Full F] : F.map (hf.lift i h hi) â‰« hf.fst g = i := by
  simpa [lift] using PullbackCone.IsLimit.lift_fst _ _ _ _

@[reassoc (attr := simp)]
lemma lift_snd [Full F] [Faithful F] : hf.lift i h hi â‰« hf.snd g = h :=
  F.map_injective <| by simpa [lift] using PullbackCone.IsLimit.lift_snd _ _ _ _

end

section

variable {c : C} (i : c âŸ¶ b) (h : c âŸ¶ a) (hi : F.map i â‰« f' = F.map h â‰« g)

/-- Variant of `lift` in the case when the domain of `f` lies in the image of `F.obj`. Thus,
in this case, one can obtain the lift directly by giving two morphisms in `C`. -/
noncomputable def lift' [Full F] : c âŸ¶ hf'.pullback g := hf'.lift _ _ hi

@[reassoc (attr := simp)]
lemma lift'_fst [Full F] [Faithful F] : hf'.lift' i h hi â‰« hf'.fst' g = i :=
  F.map_injective (by simp [lift'])

@[reassoc (attr := simp)]
lemma lift'_snd [Full F] [Faithful F] : hf'.lift' i h hi â‰« hf'.snd g = h := by
  simp [lift']

end

/-- Given two representable morphisms `f' : F.obj b âŸ¶ Y` and `g : F.obj a âŸ¶ Y`, we
obtain an isomorphism `hf'.pullback g âŸ¶ hg.pullback f'`. -/
noncomputable def symmetry [Full F] : hf'.pullback g âŸ¶ hg.pullback f' :=
  hg.lift' (hf'.snd g) (hf'.fst' g) (hf'.isPullback' _).w.symm

@[reassoc (attr := simp)]
lemma symmetry_fst [Full F] [Faithful F] : hf'.symmetry hg â‰« hg.fst' f' = hf'.snd g := by
  simp [symmetry]

@[reassoc (attr := simp)]
lemma symmetry_snd [Full F] [Faithful F] : hf'.symmetry hg â‰« hg.snd f' = hf'.fst' g := by
  simp [symmetry]

@[reassoc (attr := simp)]
lemma symmetry_symmetry [Full F] [Faithful F] : hf'.symmetry hg â‰« hg.symmetry hf' = ğŸ™ _ :=
  hom_ext' hf' (by simp) (by simp)

/-- The isomorphism given by `Presheaf.representable.symmetry`. -/
@[simps]
noncomputable def symmetryIso [Full F] [Faithful F] : hf'.pullback g â‰… hg.pullback f' where
  hom := hf'.symmetry hg
  inv := hg.symmetry hf'

instance [Full F] [Faithful F] : IsIso (hf'.symmetry hg) :=
  (hf'.symmetryIso hg).isIso_hom

end

/-- When `C` has pullbacks, then `F.map f` is representable with respect to `F` for any
`f : a âŸ¶ b` in `C`. -/
lemma map [Full F] [HasPullbacks C] {a b : C} (f : a âŸ¶ b)
    [âˆ€ c (g : c âŸ¶ b), PreservesLimit (cospan f g) F] :
    F.relativelyRepresentable (F.map f) := fun c g â†¦ by
  obtain âŸ¨g, rflâŸ© := F.map_surjective g
  refine âŸ¨Limits.pullback f g, Limits.pullback.snd f g, F.map (Limits.pullback.fst f g), ?_âŸ©
  apply F.map_isPullback <| IsPullback.of_hasPullback f g

lemma of_isIso {X Y : D} (f : X âŸ¶ Y) [IsIso f] : F.relativelyRepresentable f :=
  fun a g â†¦ âŸ¨a, ğŸ™ a, g â‰« CategoryTheory.inv f, IsPullback.of_vert_isIso âŸ¨by simpâŸ©âŸ©

lemma isomorphisms_le : MorphismProperty.isomorphisms D â‰¤ F.relativelyRepresentable :=
  fun _ _ f hf â†¦ letI : IsIso f := hf; of_isIso F f

instance isMultiplicative : IsMultiplicative F.relativelyRepresentable where
  id_mem _ := of_isIso F _
  comp_mem {F G H} f g hf hg := fun X h â†¦
    âŸ¨hf.pullback (hg.fst h), hf.snd (hg.fst h) â‰« hg.snd h, hf.fst (hg.fst h),
      by simpa using IsPullback.paste_vert (hf.isPullback (hg.fst h)) (hg.isPullback h)âŸ©

instance isStableUnderBaseChange : IsStableUnderBaseChange F.relativelyRepresentable where
  of_isPullback {X Y Y' X' f g f' g'} Pâ‚ hg a h := by
    refine âŸ¨hg.pullback (h â‰« f), hg.snd (h â‰« f), ?_, ?_âŸ©
    Â· apply Pâ‚.lift (hg.fst (h â‰« f)) (F.map (hg.snd (h â‰« f)) â‰« h) (by simpa using hg.w (h â‰« f))
    Â· apply IsPullback.of_right' (hg.isPullback (h â‰« f)) Pâ‚

instance respectsIso : RespectsIso F.relativelyRepresentable :=
  (isStableUnderBaseChange F).respectsIso

end Functor.relativelyRepresentable

namespace MorphismProperty

open Functor.relativelyRepresentable

variable {X Y : D} (P : MorphismProperty C)

/-- Given a morphism property `P` in a category `C`, a functor `F : C â¥¤ D` and a morphism
`f : X âŸ¶ Y` in `D`. Then `f` satisfies the morphism property `P.relative` with respect to `F` iff:
* The morphism is representable with respect to `F`
* For any morphism `g : F.obj a âŸ¶ Y`, the property `P` holds for any represented pullback of
  `f` by `g`. -/
def relative : MorphismProperty D :=
  fun X Y f â†¦ F.relativelyRepresentable f âˆ§
    âˆ€ â¦ƒa b : Câ¦„ (g : F.obj a âŸ¶ Y) (fst : F.obj b âŸ¶ X) (snd : b âŸ¶ a)
      (_ : IsPullback fst (F.map snd) f g), P snd

/-- Given a morphism property `P` in a category `C`, a morphism `f : F âŸ¶ G` of presheaves in the
category `Cáµ’áµ– â¥¤ Type v` satisfies the morphism property `P.presheaf` iff:
* The morphism is representable.
* For any morphism `g : F.obj a âŸ¶ G`, the property `P` holds for any represented pullback of
  `f` by `g`.
This is implemented as a special case of the more general notion of `P.relative`, to the case when
the functor `F` is `yoneda`. -/
abbrev presheaf : MorphismProperty (Cáµ’áµ– â¥¤ Type vâ‚) := P.relative yoneda

variable {P} {F}

/-- A morphism satisfying `P.relative` is representable. -/
lemma relative.rep {f : X âŸ¶ Y} (hf : P.relative F f) : F.relativelyRepresentable f :=
  hf.1

lemma relative.property {f : X âŸ¶ Y} (hf : P.relative F f) :
    âˆ€ â¦ƒa b : Câ¦„ (g : F.obj a âŸ¶ Y) (fst : F.obj b âŸ¶ X) (snd : b âŸ¶ a)
    (_ : IsPullback fst (F.map snd) f g), P snd :=
  hf.2

lemma relative.property_snd {f : X âŸ¶ Y} (hf : P.relative F f) {a : C} (g : F.obj a âŸ¶ Y) :
    P (hf.rep.snd g) :=
  hf.property g _ _ (hf.rep.isPullback g)

/-- Given a morphism property `P` which respects isomorphisms, then to show that a morphism
`f : X âŸ¶ Y` satisfies `P.relative` it suffices to show that:
* The morphism is representable.
* For any morphism `g : F.obj a âŸ¶ G`, the property `P` holds for *some* represented pullback
  of `f` by `g`. -/
lemma relative.of_exists [F.Faithful] [F.Full] [P.RespectsIso] {f : X âŸ¶ Y}
    (hâ‚€ : âˆ€ â¦ƒa : Câ¦„ (g : F.obj a âŸ¶ Y), âˆƒ (b : C) (fst : F.obj b âŸ¶ X) (snd : b âŸ¶ a)
      (_ : IsPullback fst (F.map snd) f g), P snd) : P.relative F f := by
  refine âŸ¨fun a g â†¦ ?_, fun a b g fst snd h â†¦ ?_âŸ©
  all_goals obtain âŸ¨c, g_fst, g_snd, BC, HâŸ© := hâ‚€ g
  Â· refine âŸ¨c, g_snd, g_fst, BCâŸ©
  Â· refine (P.arrow_mk_iso_iff ?_).2 H
    exact Arrow.isoMk (F.preimageIso (h.isoIsPullback X (F.obj a) BC)) (Iso.refl _)
      (F.map_injective (by simp))

lemma relative_of_snd [F.Faithful] [F.Full] [P.RespectsIso] {f : X âŸ¶ Y}
    (hf : F.relativelyRepresentable f) (h : âˆ€ â¦ƒa : Câ¦„ (g : F.obj a âŸ¶ Y), P (hf.snd g)) :
    P.relative F f :=
  relative.of_exists (fun _ g â†¦ âŸ¨hf.pullback g, hf.fst g, hf.snd g, hf.isPullback g, h gâŸ©)

/-- If `P : MorphismProperty C` is stable under base change, `F` is fully faithful and preserves
pullbacks, and `C` has all pullbacks, then for any `f : a âŸ¶ b` in `C`, `F.map f` satisfies
`P.relative` if `f` satisfies `P`. -/
lemma relative_map [F.Faithful] [F.Full] [HasPullbacks C] [IsStableUnderBaseChange P]
    {a b : C} {f : a âŸ¶ b} [âˆ€ c (g : c âŸ¶ b), PreservesLimit (cospan f g) F]
    (hf : P f) : P.relative F (F.map f) := by
  apply relative.of_exists
  intro Y' g
  obtain âŸ¨g, rflâŸ© := F.map_surjective g
  exact âŸ¨_, _, _, (IsPullback.of_hasPullback f g).map F, P.pullback_snd _ _ hfâŸ©

lemma of_relative_map {a b : C} {f : a âŸ¶ b} (hf : P.relative F (F.map f)) : P f :=
  hf.property (ğŸ™ _) (ğŸ™ _) f (IsPullback.id_horiz (F.map f))

lemma relative_map_iff [F.Faithful] [F.Full] [PreservesLimitsOfShape WalkingCospan F]
    [HasPullbacks C] [IsStableUnderBaseChange P] {X Y : C} {f : X âŸ¶ Y} :
    P.relative F (F.map f) â†” P f :=
  âŸ¨fun hf â†¦ of_relative_map hf, fun hf â†¦ relative_map hfâŸ©

/-- If `P' : MorphismProperty C` is satisfied whenever `P` is, then also `P'.relative` is
satisfied whenever `P.relative` is. -/
lemma relative_monotone {P' : MorphismProperty C} (h : P â‰¤ P') :
    P.relative F â‰¤ P'.relative F := fun _ _ _ hf â†¦
  âŸ¨hf.rep, fun _ _ g fst snd BC â†¦ h _ (hf.property g fst snd BC)âŸ©

section

variable (P)

lemma relative_isStableUnderBaseChange : IsStableUnderBaseChange (P.relative F) where
  of_isPullback hfBC hg :=
    âŸ¨of_isPullback hfBC hg.rep,
      fun _ _ _ _ _ BC â†¦ hg.property _ _ _ (IsPullback.paste_horiz BC hfBC)âŸ©

instance relative_isStableUnderComposition [F.Faithful] [F.Full] [P.IsStableUnderComposition] :
    IsStableUnderComposition (P.relative F) where
  comp_mem {F G H} f g hf hg := by
    refine âŸ¨comp_mem _ _ _ hf.1 hg.1, fun Z X p fst snd h â†¦ ?_âŸ©
    rw [â† hg.1.lift_snd (fst â‰« f) snd (by simpa using h.w)]
    refine comp_mem _ _ _ (hf.property (hg.1.fst p) fst _
      (IsPullback.of_bot ?_ ?_ (hg.1.isPullback p))) (hg.property_snd p)
    Â· rw [â† Functor.map_comp, lift_snd]
      exact h
    Â· symm
      apply hg.1.lift_fst

instance relative_respectsIso : RespectsIso (P.relative F) :=
  (relative_isStableUnderBaseChange P).respectsIso

instance relative_isMultiplicative [F.Faithful] [F.Full] [P.IsMultiplicative] [P.RespectsIso] :
    IsMultiplicative (P.relative F) where
  id_mem X := relative.of_exists
    (fun Y g â†¦ âŸ¨Y, g, ğŸ™ Y, by simpa using IsPullback.of_id_snd, id_mem _ _âŸ©)

end

section

-- TODO(Calle): This could be generalized to functors whose image forms a separating family.
/-- Morphisms satisfying `(monomorphism C).presheaf` are in particular monomorphisms. -/
lemma presheaf_monomorphisms_le_monomorphisms :
    (monomorphisms C).presheaf â‰¤ monomorphisms _ := fun F G f hf â†¦ by
  suffices âˆ€ {X : C} {a b : yoneda.obj X âŸ¶ F}, a â‰« f = b â‰« f â†’ a = b from
    âŸ¨fun _ _ h â†¦ hom_ext_yoneda (fun _ _ â†¦ this (by simp only [assoc, h]))âŸ©
  intro X a b h
  /- It suffices to show that the lifts of `a` and `b` to morphisms
  `X âŸ¶ hf.rep.pullback g` are equal, where `g = a â‰« f = a â‰« f`. -/
  suffices hf.rep.lift (g := a â‰« f) a (ğŸ™ X) (by simp) =
      hf.rep.lift b (ğŸ™ X) (by simp [â† h]) by
    simpa using yoneda.congr_map this =â‰« (hf.rep.fst (a â‰« f))
  -- This follows from the fact that the induced maps `hf.rep.pullback g âŸ¶ X` are mono.
  have : Mono (hf.rep.snd (a â‰« f)) := hf.property_snd (a â‰« f)
  simp only [â† cancel_mono (hf.rep.snd (a â‰« f)), lift_snd]

variable {G : Cáµ’áµ– â¥¤ Type vâ‚}

lemma presheaf_mono_of_le (hP : P â‰¤ MorphismProperty.monomorphisms C)
    {X : C} {f : yoneda.obj X âŸ¶ G} (hf : P.presheaf f) : Mono f :=
  MorphismProperty.presheaf_monomorphisms_le_monomorphisms _
    (MorphismProperty.relative_monotone hP _ hf)

lemma fst'_self_eq_snd (hP : P â‰¤ MorphismProperty.monomorphisms C)
    {X : C} {f : yoneda.obj X âŸ¶ G} (hf : P.presheaf f) : hf.rep.fst' f = hf.rep.snd f := by
  have := P.presheaf_mono_of_le hP hf
  apply yoneda.map_injective
  rw [â† cancel_mono f, (hf.rep.isPullback' f).w]

lemma isIso_fst'_self (hP : P â‰¤ MorphismProperty.monomorphisms C)
    {X : C} {f : yoneda.obj X âŸ¶ G} (hf : P.presheaf f) : IsIso (hf.rep.fst' f) :=
  have := P.presheaf_mono_of_le hP hf
  have := (hf.rep.isPullback' f).isIso_fst_of_mono
  Yoneda.fullyFaithful.isIso_of_isIso_map _

end

end MorphismProperty

namespace Functor.relativelyRepresentable

section Pullbacksâ‚ƒ
/-
In this section we develop some basic API that help deal with certain triple pullbacks obtained
from morphism `fâ‚ : F.obj Aâ‚ âŸ¶ X` which is relatively representable with respect to some functor
`F : C â¥¤ D`.

More precisely, given two objects `Aâ‚‚` and `Aâ‚ƒ` in `C`, and two morphisms `fâ‚‚ : Aâ‚‚ âŸ¶ X` and
`fâ‚ƒ : Aâ‚ƒ âŸ¶ X`, we can consider the pullbacks (in `D`) `(Aâ‚ Ã—_X Aâ‚‚)` and `(Aâ‚ Ã—_X Aâ‚ƒ)`
(which makes sense as objects in `C` due to `F` being relatively representable).

We can then consider the pullback, in `C`, of these two pullbacks. This is the object
`(Aâ‚ Ã—_X Aâ‚‚) Ã—_{Aâ‚} (Aâ‚ Ã—_X Aâ‚ƒ)`. In this section we develop some basic API for dealing with this
pullback. This is used in `Mathlib/AlgebraicGeometry/Sites/Representability.lean` to show that
representability is Zariski-local.
-/
variable {F : C â¥¤ D} [Full F] {Aâ‚ Aâ‚‚ Aâ‚ƒ : C} {X : D}
  {fâ‚ : F.obj Aâ‚ âŸ¶ X} (hfâ‚ : F.relativelyRepresentable fâ‚)
  (fâ‚‚ : F.obj Aâ‚‚ âŸ¶ X) (fâ‚ƒ : F.obj Aâ‚ƒ âŸ¶ X)
  [HasPullback (hfâ‚.fst' fâ‚‚) (hfâ‚.fst' fâ‚ƒ)]

/-- The pullback `(Aâ‚ Ã—_X Aâ‚‚) Ã—_{Aâ‚} (Aâ‚ Ã—_X Aâ‚ƒ)`. -/
noncomputable def pullbackâ‚ƒ := Limits.pullback (hfâ‚.fst' fâ‚‚) (hfâ‚.fst' fâ‚ƒ)
/-- The morphism `(Aâ‚ Ã—_X Aâ‚‚) Ã—_{Aâ‚} (Aâ‚ Ã—_X Aâ‚ƒ) âŸ¶ Aâ‚`. -/
noncomputable def pullbackâ‚ƒ.pâ‚ : hfâ‚.pullbackâ‚ƒ fâ‚‚ fâ‚ƒ âŸ¶ Aâ‚ := pullback.fst _ _ â‰« hfâ‚.fst' fâ‚‚
/-- The morphism `(Aâ‚ Ã—_X Aâ‚‚) Ã—_{Aâ‚} (Aâ‚ Ã—_X Aâ‚ƒ) âŸ¶ Aâ‚‚`. -/
noncomputable def pullbackâ‚ƒ.pâ‚‚ : hfâ‚.pullbackâ‚ƒ fâ‚‚ fâ‚ƒ âŸ¶ Aâ‚‚ := pullback.fst _ _ â‰« hfâ‚.snd fâ‚‚
/-- The morphism `(Aâ‚ Ã—_X Aâ‚‚) Ã—_{Aâ‚} (Aâ‚ Ã—_X Aâ‚ƒ) âŸ¶ Aâ‚ƒ`. -/
noncomputable def pullbackâ‚ƒ.pâ‚ƒ : hfâ‚.pullbackâ‚ƒ fâ‚‚ fâ‚ƒ âŸ¶ Aâ‚ƒ := pullback.snd _ _ â‰« hfâ‚.snd fâ‚ƒ

/-- The morphism `F.obj (Aâ‚ Ã—_X Aâ‚‚) Ã—_{Aâ‚} (Aâ‚ Ã—_X Aâ‚ƒ) âŸ¶ X`. -/
noncomputable def pullbackâ‚ƒ.Ï€ : F.obj (pullbackâ‚ƒ hfâ‚ fâ‚‚ fâ‚ƒ) âŸ¶ X :=
  F.map (pâ‚ hfâ‚ fâ‚‚ fâ‚ƒ) â‰« fâ‚

@[reassoc (attr := simp)]
lemma pullbackâ‚ƒ.map_pâ‚_comp : F.map (pâ‚ hfâ‚ fâ‚‚ fâ‚ƒ) â‰« fâ‚ = Ï€ _ _ _ :=
  rfl

@[reassoc (attr := simp)]
lemma pullbackâ‚ƒ.map_pâ‚‚_comp : F.map (pâ‚‚ hfâ‚ fâ‚‚ fâ‚ƒ) â‰« fâ‚‚ = Ï€ _ _ _ := by
  simp [Ï€, pâ‚, pâ‚‚, â† hfâ‚.w fâ‚‚]

@[reassoc (attr := simp)]
lemma pullbackâ‚ƒ.map_pâ‚ƒ_comp : F.map (pâ‚ƒ hfâ‚ fâ‚‚ fâ‚ƒ) â‰« fâ‚ƒ = Ï€ _ _ _ := by
  simp [Ï€, pâ‚, pâ‚ƒ, â† hfâ‚.w fâ‚ƒ, pullback.condition]

section

variable [Faithful F] {Z : C} (xâ‚ : Z âŸ¶ Aâ‚) (xâ‚‚ : Z âŸ¶ Aâ‚‚) (xâ‚ƒ : Z âŸ¶ Aâ‚ƒ)
  (hâ‚â‚‚ : F.map xâ‚ â‰« fâ‚ = F.map xâ‚‚ â‰« fâ‚‚)
  (hâ‚â‚ƒ : F.map xâ‚ â‰« fâ‚ = F.map xâ‚ƒ â‰« fâ‚ƒ)

/-- The lift obtained from the universal property of `(Aâ‚ Ã—_X Aâ‚‚) Ã—_{Aâ‚} (Aâ‚ Ã—_X Aâ‚ƒ)`. -/
noncomputable def liftâ‚ƒ : Z âŸ¶ pullbackâ‚ƒ hfâ‚ fâ‚‚ fâ‚ƒ :=
  pullback.lift (hfâ‚.lift' xâ‚ xâ‚‚ hâ‚â‚‚)
    (hfâ‚.lift' xâ‚ xâ‚ƒ hâ‚â‚ƒ) (by simp)

@[reassoc (attr := simp)]
lemma liftâ‚ƒ_pâ‚ : hfâ‚.liftâ‚ƒ fâ‚‚ fâ‚ƒ xâ‚ xâ‚‚ xâ‚ƒ hâ‚â‚‚ hâ‚â‚ƒ â‰« pullbackâ‚ƒ.pâ‚ hfâ‚ fâ‚‚ fâ‚ƒ = xâ‚ := by
  simp [liftâ‚ƒ, pullbackâ‚ƒ.pâ‚]

@[reassoc (attr := simp)]
lemma liftâ‚ƒ_pâ‚‚ : hfâ‚.liftâ‚ƒ fâ‚‚ fâ‚ƒ xâ‚ xâ‚‚ xâ‚ƒ hâ‚â‚‚ hâ‚â‚ƒ â‰« pullbackâ‚ƒ.pâ‚‚ hfâ‚ fâ‚‚ fâ‚ƒ = xâ‚‚ := by
  simp [liftâ‚ƒ, pullbackâ‚ƒ.pâ‚‚]

@[reassoc (attr := simp)]
lemma liftâ‚ƒ_pâ‚ƒ : hfâ‚.liftâ‚ƒ fâ‚‚ fâ‚ƒ xâ‚ xâ‚‚ xâ‚ƒ hâ‚â‚‚ hâ‚â‚ƒ â‰« pullbackâ‚ƒ.pâ‚ƒ hfâ‚ fâ‚‚ fâ‚ƒ = xâ‚ƒ := by
  simp [liftâ‚ƒ, pullbackâ‚ƒ.pâ‚ƒ]

end

@[reassoc (attr := simp)]
lemma pullbackâ‚ƒ.fst_fst'_eq_pâ‚ : pullback.fst _ _ â‰« hfâ‚.fst' fâ‚‚ = pullbackâ‚ƒ.pâ‚ hfâ‚ fâ‚‚ fâ‚ƒ := rfl

@[reassoc (attr := simp)]
lemma pullbackâ‚ƒ.fst_snd_eq_pâ‚‚ : pullback.fst _ _ â‰« hfâ‚.snd fâ‚‚ = pullbackâ‚ƒ.pâ‚‚ hfâ‚ fâ‚‚ fâ‚ƒ := rfl

@[reassoc (attr := simp)]
lemma pullbackâ‚ƒ.snd_snd_eq_pâ‚ƒ : pullback.snd _ _ â‰« hfâ‚.snd fâ‚ƒ = pullbackâ‚ƒ.pâ‚ƒ hfâ‚ fâ‚‚ fâ‚ƒ := rfl

@[reassoc (attr := simp)]
lemma pullbackâ‚ƒ.snd_fst'_eq_pâ‚ :
    pullback.snd (hfâ‚.fst' fâ‚‚) (hfâ‚.fst' fâ‚ƒ) â‰« hfâ‚.fst' fâ‚ƒ = pullbackâ‚ƒ.pâ‚ hfâ‚ fâ‚‚ fâ‚ƒ :=
  pullback.condition.symm

variable {hfâ‚ fâ‚‚ fâ‚ƒ} in
@[ext]
lemma pullbackâ‚ƒ.hom_ext [Faithful F] {Z : C} {Ï† Ï†' : Z âŸ¶ pullbackâ‚ƒ hfâ‚ fâ‚‚ fâ‚ƒ}
    (hâ‚ : Ï† â‰« pullbackâ‚ƒ.pâ‚ hfâ‚ fâ‚‚ fâ‚ƒ = Ï†' â‰« pullbackâ‚ƒ.pâ‚ hfâ‚ fâ‚‚ fâ‚ƒ)
    (hâ‚‚ : Ï† â‰« pullbackâ‚ƒ.pâ‚‚ hfâ‚ fâ‚‚ fâ‚ƒ = Ï†' â‰« pullbackâ‚ƒ.pâ‚‚ hfâ‚ fâ‚‚ fâ‚ƒ)
    (hâ‚ƒ : Ï† â‰« pullbackâ‚ƒ.pâ‚ƒ hfâ‚ fâ‚‚ fâ‚ƒ = Ï†' â‰« pullbackâ‚ƒ.pâ‚ƒ hfâ‚ fâ‚‚ fâ‚ƒ) : Ï† = Ï†' := by
  apply pullback.hom_ext <;> ext <;> simpa

end Pullbacksâ‚ƒ

section Diagonal
/-
In this section, we prove a criterion for the diagonal morphisms to be relatively representable.
-/

variable {F : C â¥¤ D}
variable [HasBinaryProducts C]
variable [HasPullbacks D] [HasBinaryProducts D] [HasTerminal D]
variable [Full F]
variable [PreservesLimitsOfShape (Discrete WalkingPair) F]

/-- Assume that
1. `C` has binary products,
2. `D` has pullbacks, binary products and a terminal object, and
3. `F : C â¥¤ D` is full and preserves binary products.
For an object `X` in a category `D`, if the diagonal morphism `X âŸ¶ X Ã— X` is relatively
representable, then every morphism of the form `F.obj a âŸ¶ X` is relatively representable with
respect to `F`.
-/
lemma of_diag {X : D} (h : F.relativelyRepresentable (Limits.diag X))
    â¦ƒa : Câ¦„ (g : F.obj a âŸ¶ X) : F.relativelyRepresentable g := by
  rw [(by cat_disch : Limits.diag X = pullback.lift (ğŸ™ X) (ğŸ™ X) â‰« (prodIsoPullback X X).inv)] at h
  intro a' g'
  obtain âŸ¨_, âŸ¨leftâŸ©âŸ© := pullback_map_diagonal_isPullback g g' (terminal.from X)
  let prodMap : F.obj (a â¨¯ a') âŸ¶ X â¨¯ X :=
    (preservesLimitIso _ (pair _ _) â‰ªâ‰« HasLimit.isoOfNatIso (pairComp _ _ _)).hom â‰« prod.map g g'
  let pbRepr :=
    (h prodMap).choose_spec.choose_spec.choose_spec.isLimit'.some.conePointUniqueUpToIso <|
    pasteHorizIsPullback rfl (IsPullback.of_vert_isIso_mono (snd := pullback.congrHom
      (terminal.comp_from g) (terminal.comp_from g') â‰ªâ‰« (prodIsoPullback _ _).symm â‰ªâ‰«
      (HasLimit.isoOfNatIso (pairComp _ _ _)).symm â‰ªâ‰« (preservesLimitIso _ (pair _ _)).symm|>.hom)
    âŸ¨by cat_dischâŸ©).isLimit'.some left
  exact âŸ¨_, âŸ¨_, âŸ¨_, IsPullback.of_iso_pullback (fst := pbRepr.hom â‰« pullback.fst g g')
    (snd := F.map (Functor.preimage F (pbRepr.hom â‰« pullback.snd g g')))
    âŸ¨by simp [pullback.condition]âŸ© pbRepr (by cat_disch) (by cat_disch)âŸ©âŸ©âŸ©

/-- Assume that
1. `C` has binary products and pullbacks,
2. `D` has pullbacks, binary products and a terminal object, and
3. `F : C â¥¤ D` is full and preserves binary products and pullbacks.
For a morphism `g : F.obj a âŸ¶ pullback (terminal.from X) (terminal.from X)`,
the canonical morphism from `F.obj a` to
`pullback ((g â‰« pullback.fst _ _) â‰« terminal.from X) ((g â‰« pullback.snd _ _) â‰« terminal.from X)`
is relatively representable with respect to `F`.
-/
lemma toPullbackTerminal {X : D} {a : C}
    [HasPullbacks C] [PreservesLimitsOfShape WalkingCospan F]
    (g : F.obj a âŸ¶ Limits.pullback (terminal.from X) (terminal.from X)) :
    F.relativelyRepresentable (pullback.lift (f := (g â‰« pullback.fst _ _) â‰« terminal.from X)
        (g := (g â‰« pullback.snd _ _) â‰« terminal.from X) (ğŸ™ _) (ğŸ™ _) (by cat_disch)) := by
  let pbIso := pullback.congrHom
    (terminal.comp_from _ : (g â‰« pullback.fst _ _) â‰« terminal.from X = terminal.from _)
    (terminal.comp_from _ : (g â‰« pullback.snd _ _) â‰« terminal.from X = terminal.from _) â‰ªâ‰«
    (prodIsoPullback _ _).symm â‰ªâ‰« (HasLimit.isoOfNatIso (pairComp _ _ _)).symm â‰ªâ‰«
    (preservesLimitIso _ (pair _ _)).symm
  rw [â† comp_id (pullback.lift _ _), â† pbIso.hom_inv_id, â† Category.assoc]
  apply (respectsIso F).toRespectsRight.postcomp _ (inferInstance : IsIso _) _
  exact map_preimage F (_ â‰« pbIso.hom) â–¸ map F (F.preimage _)

/-- Assume that
1. `C` has binary products and pullbacks,
2. `D` has pullbacks, binary products and a terminal object, and
3. `F : C â¥¤ D` is full and preserves binary products and pullbacks.
For an object `X` in a category `D`, if every morphism of the form `F.obj a âŸ¶ X` is relatively
representable with respect to `F`, so is the diagonal morphism `X âŸ¶ X Ã— X`.
-/
lemma diag_of_map_from_obj [HasPullbacks C] [PreservesLimitsOfShape WalkingCospan F]
    {X : D} (h : âˆ€ â¦ƒa : Câ¦„ (g : F.obj a âŸ¶ X), F.relativelyRepresentable g) :
    F.relativelyRepresentable (Limits.diag X) := by
  rw [(by cat_disch : Limits.diag X = pullback.lift (ğŸ™ X) (ğŸ™ X) â‰« (prodIsoPullback X X).inv)]
  suffices F.relativelyRepresentable (pullback.lift (ğŸ™ _) (ğŸ™ _)) from
    (respectsIso F).toRespectsRight.postcomp _ (inferInstance : IsIso _) _ this
  intro a g
  obtain âŸ¨_, âŸ¨_, âŸ¨_, pbReprâŸ©âŸ©âŸ© := h (g â‰« pullback.fst _ _) (g â‰« pullback.snd _ _)
  obtain âŸ¨_, âŸ¨botâŸ©âŸ© := IsPullback.of_iso_pullback âŸ¨by rw [assoc]; simp [pullback.condition]âŸ©
    (pbRepr.isoPullback â‰ªâ‰« (pullbackDiagonalMapIdIso (g â‰« pullback.fst _ _) (g â‰« pullback.snd _ _)
      (terminal.from X)).symm) rfl rfl
  obtain âŸ¨_, âŸ¨_, âŸ¨topMap, topâŸ©âŸ©âŸ© := (toPullbackTerminal g) <|
    (pbRepr.isoPullback â‰ªâ‰« (pullbackDiagonalMapIdIso (g â‰« pullback.fst _ _) (g â‰« pullback.snd _ _)
      (terminal.from X)).symm).hom â‰« pullback.snd
        (pullback.diagonal (terminal.from X))
        (pullback.map _ _ _ _ _ _ (ğŸ™ _) (by cat_disch) (by cat_disch))
  have hg : g = pullback.lift (ğŸ™ _) (ğŸ™ _) (by cat_disch) â‰« pullback.map
    ((g â‰« pullback.fst _ _) â‰« terminal.from X) ((g â‰« pullback.snd _ _) â‰« terminal.from X) _ _
      (g â‰« pullback.fst _ _) (g â‰« pullback.snd _ _) (ğŸ™ _) (by cat_disch) (by cat_disch) := by
    apply Limits.pullback.hom_ext <;> simp
  exact hg â–¸ âŸ¨_, âŸ¨_, âŸ¨_, IsPullback.of_isLimit <| pasteVertIsPullback rfl bot
    (map_preimage F topMap â–¸ top).flip.isLimit'.someâŸ©âŸ©âŸ©

/-- Assume that
1. `C` has binary products and pullbacks,
2. `D` has pullbacks, binary products and a terminal object, and
3. `F : C â¥¤ D` is full and preserves binary products and pullbacks.
For an object `X` in a category `D`, the diagonal morphism `X âŸ¶ X Ã— X` is relatively representable
with respect to `F` if and only if so is every morphism of the form `F.obj a âŸ¶ X`.
-/
lemma diag_iff {X : D} [HasPullbacks C] [PreservesLimitsOfShape WalkingCospan F] :
    F.relativelyRepresentable (Limits.diag X) â†”
      âˆ€ â¦ƒa : Câ¦„ (g : F.obj a âŸ¶ X), F.relativelyRepresentable g :=
  âŸ¨fun h _ g => of_diag h g, fun h => diag_of_map_from_obj hâŸ©

end Diagonal

end Functor.relativelyRepresentable

end CategoryTheory
