/-
Copyright (c) 2024 Calle S√∂nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle S√∂nne, Jo√´l Riou, Ravi Vakil
-/

import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq

/-!

# Relatively representable morphisms

In this file we define and develop basic results about relatively representable morphisms.

Clasically, a morphism `f : X ‚ü∂ Y` of presheaves is said to be representable if for any morphism
`g : yoneda.obj X ‚ü∂ G`, there exists a pullback square of the following form
```
  yoneda.obj Y --yoneda.map snd--> yoneda.obj X
      |                                |
     fst                               g
      |                                |
      v                                v
      F ------------ f --------------> G
```

In this file, we define a notion of relative representability which works with respect to any
functor, and not just `yoneda`.


## Main definitions
Throughout this file, `F : C ‚•§ D` is a functor between categories `C` and `D`.

* We define `relativelyRepresentable` as a `MorphsimProperty`. A morphism `f : X ‚ü∂ Y` in `D` is
  said to be relatively representable if for any `g : F.obj a ‚ü∂ Y`, there exists a pullback square
  of the following form
```
  F.obj b --F.map snd--> F.obj a
      |                     |
     fst                    g
      |                     |
      v                     v
      X ------- f --------> Y
```

## API

Given `hf : relativelyRepresentable f`, with `f : X ‚ü∂ Y` and `g : F.obj a ‚ü∂ Y`, we provide:
* `hf.pullback g` which is the object in `C` such that `F.obj (hf.pullback g)` is a
  pullback of `f` and `g`.
* `hf.snd g` is the morphism `hf.pullback g ‚ü∂ F.obj a`
* `hf.fst g` is the morphism `F.obj (hf.pullback g) ‚ü∂ X`
*  If `F` is full, and `f` is of type `F.obj c ‚ü∂ G`, we also have `hf.fst' g : hf.pullback g ‚ü∂ X`
which is the preimage under `F` of `hf.fst g`.
* `hom_ext`, `hom_ext'`, `lift`, `lift'` are variants of the universal property of
  `F.obj (hf.pullback g)`, where as much as possible has been formulated internally to `C`.
  For these theorems we also need that `F` is full and/or faithful.
* `symmetry` and `symmetryIso` are variants of the fact that pullbacks are symmetric for
  representable morphisms, formulated internally to `C`. We assume that `F` is fully faithful here.

-/

namespace CategoryTheory

open Category Limits

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] (F : C ‚•§ D)

def Functor.relativelyRepresentable : MorphismProperty D :=
  fun X Y f ‚Ü¶ ‚àÄ ‚¶Éa : C‚¶Ñ (g : F.obj a ‚ü∂ Y), ‚àÉ (b : C) (snd : b ‚ü∂ a)
    (fst : F.obj b ‚ü∂ X), IsPullback fst (F.map snd) f g

namespace Functor.relativelyRepresentable

section

variable {F}
variable {X Y : D} {f : X ‚ü∂ Y} (hf : F.relativelyRepresentable f)
  {b : C} {f' : F.obj b ‚ü∂ Y} (hf' : F.relativelyRepresentable f')
  {a : C} (g : F.obj a ‚ü∂ Y) (hg : F.relativelyRepresentable g)

/-- Let `f : X ‚ü∂ Y` be a representable morphism in the category of presheaves of types on
a category `C`. Then, for any `g : F.obj a ‚ü∂ Y`, `hf.pullback g` denotes the (choice of) a
corresponding object in `C` such that `F.obj (hf.pullback g)` forms a categorical pullback
of `f` and `g` in the category of presheaves. -/
noncomputable def pullback : C :=
  (hf g).choose

/-- Given a representable morphism `f : X ‚ü∂ Y`, then for any `g : F.obj a ‚ü∂ Y`, `hf.snd g`
denotes the morphism in `C` whose image under `yoneda` is the second projection in the choice of a
pullback square given by the defining property of `f` being representable. -/
noncomputable abbrev snd : hf.pullback g ‚ü∂ a :=
  (hf g).choose_spec.choose

/-- Given a representable morphism `f : X ‚ü∂ Y`, then for any `g : F.obj a ‚ü∂ Y`, `hf.fst g`
denotes the first projection in the choice of a pullback square given by the defining property of
`f` being representable. -/
noncomputable abbrev fst : F.obj (hf.pullback g) ‚ü∂ X :=
  (hf g).choose_spec.choose_spec.choose

/-- Given a representable morphism `f' : F.obj Y ‚ü∂ G`, `hf'.fst' g` denotes the preimage of
`hf'.fst g` under yoneda. -/
noncomputable abbrev fst' [Full F] : hf'.pullback g ‚ü∂ b :=
  F.preimage (hf'.fst g)

lemma map_fst' [Full F] : F.map (hf'.fst' g) = hf'.fst g :=
  F.map_preimage _

lemma isPullback : IsPullback (hf.fst g) (F.map (hf.snd g)) f g :=
  (hf g).choose_spec.choose_spec.choose_spec

@[reassoc]
lemma w : hf.fst g ‚â´ f = F.map (hf.snd g) ‚â´ g := (hf.isPullback g).w

/-- Variant of the pullback square when the first projection lies in the image of yoneda. -/
lemma isPullback' [Full F] : IsPullback (F.map (hf'.fst' g)) (F.map (hf'.snd g)) f' g :=
  (hf'.map_fst' _) ‚ñ∏ hf'.isPullback g

@[reassoc]
lemma w' {X Y Z : C} {f : X ‚ü∂ Z} (hf : F.relativelyRepresentable (F.map f)) (g : Y ‚ü∂ Z)
    [Full F] [Faithful F] : hf.fst' (F.map g) ‚â´ f = hf.snd (F.map g) ‚â´ g :=
  F.map_injective <| by simp [(hf.w (F.map g))]

lemma isPullback_of_map {X Y Z : C} {f : X ‚ü∂ Z} (hf : F.relativelyRepresentable (F.map f))
    (g : Y ‚ü∂ Z) [Full F] [Faithful F] :
    IsPullback (hf.fst' (F.map g)) (hf.snd (F.map g)) f g :=
  IsPullback.of_map F (hf.w' g) (hf.isPullback' (F.map g))

variable {g}

/-- Two morphisms `a b : Z ‚ü∂ hf.pullback g` are equal if
* Their compositions (in `C`) with `hf.snd g : hf.pullback  ‚ü∂ X` are equal.
* The compositions of `F.map a` and `F.map b` with `hf.fst g` are equal. -/
@[ext 100]
lemma hom_ext [Faithful F] {Z : C} {a b : Z ‚ü∂ hf.pullback g}
    (h‚ÇÅ : F.map a ‚â´ hf.fst g = F.map b ‚â´ hf.fst g)
    (h‚ÇÇ : a ‚â´ hf.snd g = b ‚â´ hf.snd g) : a = b :=
  F.map_injective <|
    PullbackCone.IsLimit.hom_ext (hf.isPullback g).isLimit h‚ÇÅ (by simpa using F.congr_map h‚ÇÇ)

/-- In the case of a representable morphism `f' : F.obj Y ‚ü∂ G`, whose codomain lies
in the image of yoneda, we get that two morphism `a b : Z ‚ü∂ hf.pullback g` are equal if
* Their compositions (in `C`) with `hf'.snd g : hf.pullback  ‚ü∂ X` are equal.
* Their compositions (in `C`) with `hf'.fst' g : hf.pullback  ‚ü∂ Y` are equal. -/
@[ext]
lemma hom_ext' [Full F] [Faithful F] {Z : C} {a b : Z ‚ü∂ hf'.pullback g}
    (h‚ÇÅ : a ‚â´ hf'.fst' g = b ‚â´ hf'.fst' g)
    (h‚ÇÇ : a ‚â´ hf'.snd g = b ‚â´ hf'.snd g) : a = b :=
  hf'.hom_ext (by simpa [map_fst'] using F.congr_map h‚ÇÅ) h‚ÇÇ

section

variable {c : C} (i : F.obj c ‚ü∂ X) (h : c ‚ü∂ a) (hi : i ‚â´ f = F.map h ‚â´ g)

/-- The lift (in `C`) obtained from the universal property of `yoneda.obj (hf.pullback g)`, in the
case when the cone point is in the image of `yoneda.obj`. -/
noncomputable def lift [Full F] : c ‚ü∂ hf.pullback g :=
  F.preimage <| PullbackCone.IsLimit.lift (hf.isPullback g).isLimit _ _ hi

@[reassoc (attr := simp)]
lemma lift_fst [Full F] : F.map (hf.lift i h hi) ‚â´ hf.fst g = i := by
  simpa [lift] using PullbackCone.IsLimit.lift_fst _ _ _ _

@[reassoc (attr := simp)]
lemma lift_snd [Full F] [Faithful F] : hf.lift i h hi ‚â´ hf.snd g = h :=
  F.map_injective <| by simpa [lift] using PullbackCone.IsLimit.lift_snd _ _ _ _

end

section

variable {c : C} (i : c ‚ü∂ b) (h : c ‚ü∂ a) (hi : F.map i ‚â´ f' = F.map h ‚â´ g)

/-- Variant of `lift` in the case when the domain of `f` lies in the image of `yoneda.obj`. Thus,
in this case, one can obtain the lift directly by giving two morphisms in `C`. -/
noncomputable def lift' [Full F] : c ‚ü∂ hf'.pullback g := hf'.lift _ _ hi

@[reassoc (attr := simp)]
lemma lift'_fst [Full F] [Faithful F] : hf'.lift' i h hi ‚â´ hf'.fst' g = i :=
  F.map_injective (by simp [map_fst', lift'])

@[reassoc (attr := simp)]
lemma lift'_snd [Full F] [Faithful F] : hf'.lift' i h hi ‚â´ hf'.snd g = h := by
  simp [lift']

end

/-- Given two representable morphisms `f' : yoneda.obj Y ‚ü∂ G` and `g : yoneda.obj X ‚ü∂ G`, we
obtain an isomorphism `hf'.pullback g ‚ü∂ hg.pullback f'`. -/
noncomputable def symmetry [Full F] : hf'.pullback g ‚ü∂ hg.pullback f' :=
  hg.lift' (hf'.snd g) (hf'.fst' g) (hf'.isPullback' _).w.symm

@[reassoc (attr := simp)]
lemma symmetry_fst [Full F] [Faithful F] : hf'.symmetry hg ‚â´ hg.fst' f' = hf'.snd g := by
  simp [symmetry]

@[reassoc (attr := simp)]
lemma symmetry_snd [Full F] [Faithful F] : hf'.symmetry hg ‚â´ hg.snd f' = hf'.fst' g := by
  simp [symmetry]

@[reassoc (attr := simp)]
lemma symmetry_symmetry [Full F] [Faithful F] : hf'.symmetry hg ‚â´ hg.symmetry hf' = ùüô _ :=
  hom_ext' hf' (by simp) (by simp)

/-- The isomorphism given by `Presheaf.representable.symmetry`. -/
@[simps]
noncomputable def symmetryIso [Full F] [Faithful F] : hf'.pullback g ‚âÖ hg.pullback f' where
  hom := hf'.symmetry hg
  inv := hg.symmetry hf'

instance [Full F] [Faithful F] : IsIso (hf'.symmetry hg) :=
  (hf'.symmetryIso hg).isIso_hom

end

end Functor.relativelyRepresentable

end CategoryTheory
