/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle SÃ¶nne, JoÃ«l Riou, Ravi Vakil
-/

import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq

/-!

# Relatively representable morphisms

In this file we define and develop basic results about relatively representable morphisms.

Classically, a morphism `f : X âŸ¶ Y` of presheaves is said to be representable if for any morphism
`g : yoneda.obj X âŸ¶ G`, there exists a pullback square of the following form
```
  yoneda.obj Y --yoneda.map snd--> yoneda.obj X
      |                                |
     fst                               g
      |                                |
      v                                v
      F ------------ f --------------> G
```

In this file, we define a notion of relative representability which works with respect to any
functor, and not just `yoneda`. The fact that a morphism `f : F âŸ¶ G` between presheaves is
representable in the classical case will then be given by `yoneda.relativelyRepresentable f`.



## Main definitions
Throughout this file, `F : C â¥¤ D` is a functor between categories `C` and `D`.

* We define `relativelyRepresentable` as a `MorphsimProperty`. A morphism `f : X âŸ¶ Y` in `D` is
  said to be relatively representable if for any `g : F.obj a âŸ¶ Y`, there exists a pullback square
  of the following form
```
  F.obj b --F.map snd--> F.obj a
      |                     |
     fst                    g
      |                     |
      v                     v
      X ------- f --------> Y
```

## API

Given `hf : relativelyRepresentable f`, with `f : X âŸ¶ Y` and `g : F.obj a âŸ¶ Y`, we provide:
* `hf.pullback g` which is the object in `C` such that `F.obj (hf.pullback g)` is a
  pullback of `f` and `g`.
* `hf.snd g` is the morphism `hf.pullback g âŸ¶ F.obj a`
* `hf.fst g` is the morphism `F.obj (hf.pullback g) âŸ¶ X`
*  If `F` is full, and `f` is of type `F.obj c âŸ¶ G`, we also have `hf.fst' g : hf.pullback g âŸ¶ X`
which is the preimage under `F` of `hf.fst g`.
* `hom_ext`, `hom_ext'`, `lift`, `lift'` are variants of the universal property of
  `F.obj (hf.pullback g)`, where as much as possible has been formulated internally to `C`.
  For these theorems we also need that `F` is full and/or faithful.
* `symmetry` and `symmetryIso` are variants of the fact that pullbacks are symmetric for
  representable morphisms, formulated internally to `C`. We assume that `F` is fully faithful here.

-/

namespace CategoryTheory

open Category Limits

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D] (F : C â¥¤ D)

/-- A morphism `f : X âŸ¶ Y` in `D` is said to be relatively representable if for any
`g : F.obj a âŸ¶ Y`, there exists a pullback square of the following form
```
F.obj b --F.map snd--> F.obj a
    |                     |
   fst                    g
    |                     |
    v                     v
    X ------- f --------> Y
```
-/
def Functor.relativelyRepresentable : MorphismProperty D :=
  fun X Y f â†¦ âˆ€ â¦ƒa : Câ¦„ (g : F.obj a âŸ¶ Y), âˆƒ (b : C) (snd : b âŸ¶ a)
    (fst : F.obj b âŸ¶ X), IsPullback fst (F.map snd) f g

namespace Functor.relativelyRepresentable

section

variable {F}
variable {X Y : D} {f : X âŸ¶ Y} (hf : F.relativelyRepresentable f)
  {b : C} {f' : F.obj b âŸ¶ Y} (hf' : F.relativelyRepresentable f')
  {a : C} (g : F.obj a âŸ¶ Y) (hg : F.relativelyRepresentable g)

/-- Let `f : X âŸ¶ Y` be a relatively representable morphism in `D`. Then, for any
`g : F.obj a âŸ¶ Y`, `hf.pullback g` denotes the (choice of) a corresponding object in `C` such that
there is a pullback square of the following form
```
hf.pullback g --F.map snd--> F.obj a
    |                          |
   fst                         g
    |                          |
    v                          v
    X ---------- f ----------> Y
``` -/
noncomputable def pullback : C :=
  (hf g).choose

/-- Given a representable morphism `f : X âŸ¶ Y`, then for any `g : F.obj a âŸ¶ Y`, `hf.snd g`
denotes the morphism in `C` giving rise to the following diagram
```
hf.pullback g --F.map (hf.snd g)--> F.obj a
    |                                 |
   fst                                g
    |                                 |
    v                                 v
    X -------------- f -------------> Y
``` -/
noncomputable abbrev snd : hf.pullback g âŸ¶ a :=
  (hf g).choose_spec.choose

/-- Given a relatively representable morphism `f : X âŸ¶ Y`, then for any `g : F.obj a âŸ¶ Y`,
`hf.fst g` denotes the first projection in the following diagram, given by the defining property
of `f` being relatively representable
```
hf.pullback g --F.map (hf.snd g)--> F.obj a
    |                                 |
hf.fst g                              g
    |                                 |
    v                                 v
    X -------------- f -------------> Y
``` -/
noncomputable abbrev fst : F.obj (hf.pullback g) âŸ¶ X :=
  (hf g).choose_spec.choose_spec.choose

/-- When `F` is full, given a representable morphism `f' : F.obj b âŸ¶ Y`, then `hf'.fst' g` denotes
the preimage of `hf'.fst g` under `F`. -/
noncomputable abbrev fst' [Full F] : hf'.pullback g âŸ¶ b :=
  F.preimage (hf'.fst g)

lemma map_fst' [Full F] : F.map (hf'.fst' g) = hf'.fst g :=
  F.map_preimage _

lemma isPullback : IsPullback (hf.fst g) (F.map (hf.snd g)) f g :=
  (hf g).choose_spec.choose_spec.choose_spec

@[reassoc]
lemma w : hf.fst g â‰« f = F.map (hf.snd g) â‰« g := (hf.isPullback g).w

/-- Variant of the pullback square when `F` is full, and given `f' : F.obj b âŸ¶ Y`. -/
lemma isPullback' [Full F] : IsPullback (F.map (hf'.fst' g)) (F.map (hf'.snd g)) f' g :=
  (hf'.map_fst' _) â–¸ hf'.isPullback g

@[reassoc]
lemma w' {X Y Z : C} {f : X âŸ¶ Z} (hf : F.relativelyRepresentable (F.map f)) (g : Y âŸ¶ Z)
    [Full F] [Faithful F] : hf.fst' (F.map g) â‰« f = hf.snd (F.map g) â‰« g :=
  F.map_injective <| by simp [(hf.w (F.map g))]

lemma isPullback_of_map {X Y Z : C} {f : X âŸ¶ Z} (hf : F.relativelyRepresentable (F.map f))
    (g : Y âŸ¶ Z) [Full F] [Faithful F] :
    IsPullback (hf.fst' (F.map g)) (hf.snd (F.map g)) f g :=
  IsPullback.of_map F (hf.w' g) (hf.isPullback' (F.map g))

variable {g}

/-- Two morphisms `a b : c âŸ¶ hf.pullback g` are equal if
* Their compositions (in `C`) with `hf.snd g : hf.pullback  âŸ¶ X` are equal.
* The compositions of `F.map a` and `F.map b` with `hf.fst g` are equal. -/
@[ext 100]
lemma hom_ext [Faithful F] {c : C} {a b : c âŸ¶ hf.pullback g}
    (hâ‚ : F.map a â‰« hf.fst g = F.map b â‰« hf.fst g)
    (hâ‚‚ : a â‰« hf.snd g = b â‰« hf.snd g) : a = b :=
  F.map_injective <|
    PullbackCone.IsLimit.hom_ext (hf.isPullback g).isLimit hâ‚ (by simpa using F.congr_map hâ‚‚)

/-- In the case of a representable morphism `f' : F.obj Y âŸ¶ G`, whose codomain lies
in the image of `F`, we get that two morphism `a b : Z âŸ¶ hf.pullback g` are equal if
* Their compositions (in `C`) with `hf'.snd g : hf.pullback  âŸ¶ X` are equal.
* Their compositions (in `C`) with `hf'.fst' g : hf.pullback  âŸ¶ Y` are equal. -/
@[ext]
lemma hom_ext' [Full F] [Faithful F] {c : C} {a b : c âŸ¶ hf'.pullback g}
    (hâ‚ : a â‰« hf'.fst' g = b â‰« hf'.fst' g)
    (hâ‚‚ : a â‰« hf'.snd g = b â‰« hf'.snd g) : a = b :=
  hf'.hom_ext (by simpa [map_fst'] using F.congr_map hâ‚) hâ‚‚

section

variable {c : C} (i : F.obj c âŸ¶ X) (h : c âŸ¶ a) (hi : i â‰« f = F.map h â‰« g)

/-- The lift (in `C`) obtained from the universal property of `F.obj (hf.pullback g)`, in the
case when the cone point is in the image of `F.obj`. -/
noncomputable def lift [Full F] : c âŸ¶ hf.pullback g :=
  F.preimage <| PullbackCone.IsLimit.lift (hf.isPullback g).isLimit _ _ hi

@[reassoc (attr := simp)]
lemma lift_fst [Full F] : F.map (hf.lift i h hi) â‰« hf.fst g = i := by
  simpa [lift] using PullbackCone.IsLimit.lift_fst _ _ _ _

@[reassoc (attr := simp)]
lemma lift_snd [Full F] [Faithful F] : hf.lift i h hi â‰« hf.snd g = h :=
  F.map_injective <| by simpa [lift] using PullbackCone.IsLimit.lift_snd _ _ _ _

end

section

variable {c : C} (i : c âŸ¶ b) (h : c âŸ¶ a) (hi : F.map i â‰« f' = F.map h â‰« g)

/-- Variant of `lift` in the case when the domain of `f` lies in the image of `F.obj`. Thus,
in this case, one can obtain the lift directly by giving two morphisms in `C`. -/
noncomputable def lift' [Full F] : c âŸ¶ hf'.pullback g := hf'.lift _ _ hi

@[reassoc (attr := simp)]
lemma lift'_fst [Full F] [Faithful F] : hf'.lift' i h hi â‰« hf'.fst' g = i :=
  F.map_injective (by simp [map_fst', lift'])

@[reassoc (attr := simp)]
lemma lift'_snd [Full F] [Faithful F] : hf'.lift' i h hi â‰« hf'.snd g = h := by
  simp [lift']

end

/-- Given two representable morphisms `f' : F.obj b âŸ¶ Y` and `g : F.obj a âŸ¶ Y`, we
obtain an isomorphism `hf'.pullback g âŸ¶ hg.pullback f'`. -/
noncomputable def symmetry [Full F] : hf'.pullback g âŸ¶ hg.pullback f' :=
  hg.lift' (hf'.snd g) (hf'.fst' g) (hf'.isPullback' _).w.symm

@[reassoc (attr := simp)]
lemma symmetry_fst [Full F] [Faithful F] : hf'.symmetry hg â‰« hg.fst' f' = hf'.snd g := by
  simp [symmetry]

@[reassoc (attr := simp)]
lemma symmetry_snd [Full F] [Faithful F] : hf'.symmetry hg â‰« hg.snd f' = hf'.fst' g := by
  simp [symmetry]

@[reassoc (attr := simp)]
lemma symmetry_symmetry [Full F] [Faithful F] : hf'.symmetry hg â‰« hg.symmetry hf' = ðŸ™ _ :=
  hom_ext' hf' (by simp) (by simp)

/-- The isomorphism given by `Presheaf.representable.symmetry`. -/
@[simps]
noncomputable def symmetryIso [Full F] [Faithful F] : hf'.pullback g â‰… hg.pullback f' where
  hom := hf'.symmetry hg
  inv := hg.symmetry hf'

instance [Full F] [Faithful F] : IsIso (hf'.symmetry hg) :=
  (hf'.symmetryIso hg).isIso_hom

end

end Functor.relativelyRepresentable

end CategoryTheory
