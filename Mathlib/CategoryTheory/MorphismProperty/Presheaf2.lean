/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle SÃ¶nne, JoÃ«l Riou, Ravi Vakil
-/

import Mathlib.CategoryTheory.Limits.Yoneda
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Pullbacks
import Mathlib.CategoryTheory.MorphismProperty.Limits

/-!

# Representable morphisms of presheaves

In this file we define and develop basic results on the representability of morphisms of presheaves.

## Main definitions

* `Presheaf.representable` is a `MorphismProperty` expressing the fact that a morphism of presheaves
  is representable.
* `MorphismProperty.presheaf`: given a `P : MorphismProperty C`, we say that a morphism of
  presheaves `f : F âŸ¶ G` satisfies `P.presheaf` if it is representable, and the property `P`
  holds for the preimage under yoneda of pullbacks of `f` by morphisms `g : yoneda.obj X âŸ¶ G`.

## API


## Main results
* `representable_isStableUnderComposition`: Being representable is stable under composition.
* `representable_stableUnderBaseChange`: Being representable is stable under base change.
* `representable_ofIso`: Isomorphisms are representable

* `presheaf_yoneda_map`: If `f : X âŸ¶ Y` satisfies `P`, and `P` is stable under compostions,
  then `yoneda.map f` satisfies `P.presheaf`.

For the following results, we assume that `P : MorphismProperty C` is stable under base change:
* `presheaf_stableUnderBaseChange`: `P.presheaf` is stable under base change
* `presheaf_respectsIso`: `P.presheaf` respects isomorphisms
* `presheaf_isStableUnderComp`: If `P` is stable under composition, then so is `P.presheaf`

## TODO
Can improve definitions & basic API
converse of `presheaf_yoneda_map`

-/


namespace CategoryTheory

open Category Limits

universe v u

variable {C : Type u} [Category.{v} C]

section

variable {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} (t : PullbackCone f g) (ht : IsLimit t)

--lemma pullbackCone_eq_mk_self (t : PullbackCone f g) : t = PullbackCone.mk t.fst t.snd t.condition
-- := by
--  sorry

def pullbackCone_iso_mk_self : t â‰… PullbackCone.mk t.fst t.snd t.condition := by
  apply PullbackCone.ext (by apply Iso.refl) <;> simp

def pullbackCone_iso_mk_self_pt : t.pt â‰… (PullbackCone.mk t.fst t.snd t.condition).pt := by
  exact Iso.refl t.pt

-- TODO: look at pullbackIsPullback...!
def pullbackConeMkSelf_isLimit : IsLimit (PullbackCone.mk t.fst t.snd t.condition) := by
  apply IsLimit.ofIsoLimit ht
  apply PullbackCone.ext (by apply Iso.refl) <;> simp

end

/-- A morphism of presheaves `F âŸ¶ G` is representable if for any `X : C`, and any morphism
`g : yoneda.obj X âŸ¶ G`, the pullback `F Ã—_G yoneda.obj X` is also representable. -/
def Presheaf.representable : MorphismProperty (Cáµ’áµ– â¥¤ Type v) :=
  fun F G f â†¦ âˆ€ â¦ƒX : Câ¦„ (g : yoneda.obj X âŸ¶ G), âˆƒ (Y : C) (snd : Y âŸ¶ X)
    (fst : yoneda.obj Y âŸ¶ F), IsPullback fst (yoneda.map snd) f g

namespace Presheaf.representable

section

variable {F G : Cáµ’áµ– â¥¤ Type v} {f : F âŸ¶ G} (hf : Presheaf.representable f)
  {Y : C} {f' : yoneda.obj Y âŸ¶ G} (hf' : Presheaf.representable f')
  {X : C} (g : yoneda.obj X âŸ¶ G) (hg : Presheaf.representable g)

/-- Let `f : F âŸ¶ G` be a representable morphism in the category of presheaves of types on
a category `C`. Then, for any `g : yoneda.obj X âŸ¶ G`, `hf.pullback g` denotes the (choice of) a
corresponding object in `C` equipped with an isomorphism between `yoneda.obj (hf.pullback g)`
and the categorical pullback of `f` and `g` in the category of presheaves. -/
noncomputable def pullback : C :=
  (hf g).choose

-- /-- The pullback cone obtained by the isomorphism `hf.pullbackIso`. -/
-- noncomputable def pullbackCone : PullbackCone f g :=
--   PullbackCone.mk ((hf.pullbackIso g).hom â‰« pullback.fst _ _)
--     ((hf.pullbackIso g).hom â‰« pullback.snd _ _) (by simpa using pullback.condition)

-- /-- The pullback cone obtained via `hf.pullbackIso` is a limit cone. -/
-- noncomputable def pullbackConeIsLimit : IsLimit (hf.pullbackCone g) :=
--   IsLimit.ofIsoLimit (pullbackIsPullback _ _)
--     (PullbackCone.ext (hf.pullbackIso g).symm (by simp [pullbackCone]) (by simp [pullbackCone]))

/-- The preimage under yoneda of the second projection of `hf.pullbackCone g` -/
noncomputable abbrev snd : hf.pullback g âŸ¶ X :=
  (hf g).choose_spec.choose

noncomputable abbrev fst : yoneda.obj (hf.pullback g) âŸ¶ F :=
  (hf g).choose_spec.choose_spec.choose

/-- The preimage under yoneda of the first projection of `hf.pullbackCone g`, whenever this
makes sense. -/
noncomputable abbrev fst' : hf'.pullback g âŸ¶ Y :=
  Yoneda.fullyFaithful.preimage (hf'.fst g)

lemma yoneda_map_fst : yoneda.map (hf'.fst' g) = hf'.fst g := by
  apply Functor.FullyFaithful.map_preimage

noncomputable def isPullback : IsPullback (hf.fst g) (yoneda.map (hf.snd g)) f g :=
  (hf g).choose_spec.choose_spec.choose_spec

-- THIS IS NOW (hf.isPullback g).w

-- /- (Calle): A possibly better approach to the API would be to construct pullbackCone
-- so that `yoneda.map (hf.snd g)` is definitionally `pullbackCone.snd`. Then `condition_yoneda`
-- would just bee pullbackCone.condition -/
-- @[reassoc]
-- lemma condition_yoneda : (hf.pullbackCone g).fst â‰« f = yoneda.map (hf.snd g) â‰« g := by
--   simpa only [yoneda_map_snd] using (hf.pullbackCone g).condition

-- (Calle) maybe this should have a better name?
@[reassoc]
lemma condition : yoneda.map (hf'.fst' g) â‰« f' = yoneda.map (hf'.snd g) â‰« g := by
  simpa only [yoneda_map_fst] using (hf'.isPullback g).w

/-- Variant of `condition` when all vertices of the pullback square lie in the image of yoneda. -/
@[reassoc]
lemma condition' {X Y Z : C} {f : X âŸ¶ Z} (g : yoneda.obj Y âŸ¶ yoneda.obj Z)
    (hf : Presheaf.representable (yoneda.map f)) :
      hf.fst' g â‰« f = hf.snd g â‰« (Yoneda.fullyFaithful.preimage g) :=
  yoneda.map_injective <| by simp [(hf.isPullback g).w]

variable {g}

/-- Two morphisms `a b : Z âŸ¶ hf.pullback g` are equal if
* Their compositions (in `C`) with `hf.snd g : hf.pullback  âŸ¶ X` are equal.
* The compositions of `yoneda.map a` and `yoneda.map b` with `(hf.pullbackCone g).fst` are equal. -/
@[ext 100]
lemma hom_ext {Z : C} {a b : Z âŸ¶ hf.pullback g}
    (hâ‚ : yoneda.map a â‰« hf.fst g = yoneda.map b â‰« hf.fst g)
    (hâ‚‚ : a â‰« hf.snd g = b â‰« hf.snd g) : a = b :=
  yoneda.map_injective <|
    PullbackCone.IsLimit.hom_ext (hf.isPullback g).isLimit hâ‚ (by simpa using yoneda.congr_map hâ‚‚)

/-- In the case of a representable morphism `f' : yoneda.obj Y âŸ¶ G`, whose codomain lies
in the image of yoneda, we get that two morphism `a b : Z âŸ¶ hf.pullback g` are equal if
* Their compositions (in `C`) with `hf'.snd g : hf.pullback  âŸ¶ X` are equal.
* Their compositions (in `C`) with `hf'.fst g : hf.pullback  âŸ¶ X` are equal. -/
@[ext]
lemma hom_ext' {Z : C} {a b : Z âŸ¶ hf'.pullback g} (hâ‚ : a â‰« hf'.fst' g = b â‰« hf'.fst' g)
    (hâ‚‚ : a â‰« hf'.snd g = b â‰« hf'.snd g) : a = b :=
  hf'.hom_ext (by simpa [yoneda_map_fst] using yoneda.congr_map hâ‚) hâ‚‚

section

variable {Z : C} (i : yoneda.obj Z âŸ¶ F) (h : Z âŸ¶ X) (hi : i â‰« f = yoneda.map h â‰« g)

/-- The lift (in `C`) obtained from the universal property of `yoneda.obj (hf.pullback g)`, in the
case when one of the morphisms lies in the image of `yoneda.map`. -/
noncomputable def lift : Z âŸ¶ hf.pullback g :=
  Yoneda.fullyFaithful.preimage <| PullbackCone.IsLimit.lift (hf.isPullback g).isLimit _ _ hi

@[reassoc (attr := simp)]
lemma lift_fst : yoneda.map (hf.lift i h hi) â‰« hf.fst g = i := by
  simpa [lift] using PullbackCone.IsLimit.lift_fst _ _ _ _


@[reassoc (attr := simp)]
lemma lift_snd : hf.lift i h hi â‰« hf.snd g = h :=
  yoneda.map_injective <| by simpa [lift] using PullbackCone.IsLimit.lift_snd _ _ _ _

end

section

variable {Z : C} (i : Z âŸ¶ Y) (h : Z âŸ¶ X) (hi : (yoneda.map i) â‰« f' = yoneda.map h â‰« g)

/-- TODO -/
noncomputable def lift' : Z âŸ¶ hf'.pullback g := hf'.lift _ _ hi

@[reassoc (attr := simp)]
lemma lift'_fst : hf'.lift' i h hi â‰« hf'.fst' g = i :=
  yoneda.map_injective (by simp [yoneda_map_fst, lift'])

@[reassoc (attr := simp)]
lemma lift'_snd : hf'.lift' i h hi â‰« hf'.snd g = h := by
  simp [lift']

end

/-- TODO -/
noncomputable def symmetry : hf'.pullback g âŸ¶ hg.pullback f' :=
  hg.lift' (hf'.snd g) (hf'.fst' g) (condition _ _).symm

@[reassoc (attr := simp)]
lemma symmetry_fst : hf'.symmetry hg â‰« hg.fst' f' = hf'.snd g := by simp [symmetry]

@[reassoc (attr := simp)]
lemma symmetry_snd : hf'.symmetry hg â‰« hg.snd f' = hf'.fst' g := by simp [symmetry]

@[reassoc (attr := simp)]
lemma symmetry_symmetry : hf'.symmetry hg â‰« hg.symmetry hf' = ðŸ™ _ := by aesop_cat

/-- TODO -/
@[simps]
noncomputable def symmetryIso : hf'.pullback g â‰… hg.pullback f' where
  hom := hf'.symmetry hg
  inv := hg.symmetry hf'

instance : IsIso (hf'.symmetry hg) :=
  (hf'.symmetryIso hg).isIso_hom

end

/-- When `C` has pullbacks, then `yoneda.map f` is representable for any `f : X âŸ¶ Y`. -/
lemma yoneda_map [HasPullbacks C] {X Y : C} (f : X âŸ¶ Y) :
    Presheaf.representable (yoneda.map f) := fun Z g â†¦ by
  obtain âŸ¨g, rflâŸ© := yoneda.map_surjective g
  refine âŸ¨Limits.pullback f g, Limits.pullback.snd f g, yoneda.map (Limits.pullback.fst f g), ?_âŸ©
  apply yoneda.map_isPullback <| IsPullback.of_hasPullback f g

end Presheaf.representable

namespace MorphismProperty

variable {F G : Cáµ’áµ– â¥¤ Type v} (P : MorphismProperty C)

/-- Given a morphism property `P` in a category `C`, a morphism `f : F âŸ¶ G` of presheaves in the
category `Cáµ’áµ– â¥¤ Type v` satisfies the morphism property `P.presheaf` iff:
* The morphism is representable.
* The property `P` holds for the pullback of `f` by any morphism `g : yoneda.obj X âŸ¶ G`. -/
def presheaf : MorphismProperty (Cáµ’áµ– â¥¤ Type v) :=
  fun _ G f â†¦ âˆƒ (hf : Presheaf.representable f), âˆ€ â¦ƒX : Câ¦„ (g : yoneda.obj X âŸ¶ G), P (hf.snd g)

variable {P}

/-- A morphism satisfying `P.presheaf` is representable. -/
lemma presheaf.representable {f : F âŸ¶ G} (hf : P.presheaf f) : Presheaf.representable f :=
  hf.choose

lemma presheaf.property {f : F âŸ¶ G} (hf : P.presheaf f) {X : C} (g : yoneda.obj X âŸ¶ G) :
    P (hf.choose.snd g) :=
  hf.choose_spec g

-- (Calle): this can definitely be golfed later. Also maybe provide other versions
-- of this lemma with other pullback API's (e.g. `pullback f g` could be useful)
-- possibly this should be the definition, and the weaker condition should be derived from this?
lemma presheaf.property' (hP : P.RespectsIso) {f : F âŸ¶ G} (hf : P.presheaf f) :
    âˆ€ â¦ƒX Y : Câ¦„ {g : yoneda.obj X âŸ¶ G} {fst : yoneda.obj Y âŸ¶ F} {snd : Y âŸ¶ X}
    (_ : IsPullback fst (yoneda.map snd) f g), P snd := by
  intro X Y g fst snd h

  have comp := h.isoIsPullback_hom_snd <| hf.representable.isPullback g
  apply congr_arg Yoneda.fullyFaithful.preimage at comp
  rw [Yoneda.fullyFaithful.preimage_map] at comp
  rw [â† comp, Yoneda.fullyFaithful.preimage_comp]

  simpa using hP.1 (Yoneda.fullyFaithful.preimageIso <|
    h.isoIsPullback (hf.representable.isPullback g)) _ (hf.property g)

lemma presheaf_mk' (hP : P.RespectsIso) {f : F âŸ¶ G} (hf : Presheaf.representable f)
    (h : (âˆ€ â¦ƒX : Câ¦„ (g : yoneda.obj X âŸ¶ G), âˆƒ (Y : C)
    (fst : yoneda.obj Y âŸ¶ F) (snd : Y âŸ¶ X) (_ : IsPullback fst (yoneda.map snd) f g),
    P snd)) : P.presheaf f := by
  use hf
  intro X g
  obtain âŸ¨Y, fst, snd, âŸ¨h, P_sndâŸ©âŸ© := h g

  have comp := (hf.isPullback g).isoIsPullback_hom_snd h
  apply congr_arg (Yoneda.fullyFaithful.preimage Â·) at comp
  rw [Functor.FullyFaithful.preimage_map] at comp
  rw [â† comp, Yoneda.fullyFaithful.preimage_comp]

  simpa using hP.1 (Yoneda.fullyFaithful.preimageIso <| (hf.isPullback g).isoIsPullback h) _ P_snd

/-- If `P : MorphismProperty C` is stable under base change, then for any `f : X âŸ¶ Y` in `C`,
`yoneda.map f` satisfies `P.presheaf` if `f` does. -/
-- TODO: converse!
lemma presheaf_yoneda_map [HasPullbacks C] (hP : StableUnderBaseChange P) {X Y : C} {f : X âŸ¶ Y}
    (hf : P f) : P.presheaf (yoneda.map f) := by
  use Presheaf.representable.yoneda_map f
  intro Z g
  apply hP (f := (Yoneda.fullyFaithful.preimage g))
    (f' := (Presheaf.representable.yoneda_map f).fst' g) _ hf
  apply IsPullback.of_map yoneda ((Presheaf.representable.yoneda_map f).condition' g)
  simpa using (Presheaf.representable.yoneda_map f).isPullback g

-- TODO: commit w/o this
lemma presheaf_of_yoneda_map {X Y : C} {f : X âŸ¶ Y} (hf : P.presheaf (yoneda.map f)) : P f := by

  sorry

/-- Morphisms satisfying `(monomorphism C).presheaf` are in particular monomorphisms.-/
lemma presheaf_monomorphisms_le_monomorphisms :
    (monomorphisms C).presheaf â‰¤ monomorphisms _ := fun F G f hf â†¦ by
  suffices âˆ€ {X : C} {a b : yoneda.obj X âŸ¶ F}, a â‰« f = b â‰« f â†’ a = b from
    âŸ¨fun _ _ h â†¦ hom_ext_yoneda (fun _ _ â†¦ this (by simp only [assoc, h]))âŸ©
  intro X a b h
  /- It suffices to show that the lifts of `a` and `b` to morphisms
  `X âŸ¶ hf.representable.pullback g` are equal, where `g = a â‰« f = a â‰« f`. -/
  suffices hf.representable.lift (g := a â‰« f) a (ðŸ™ X) (by simp) =
      hf.representable.lift b (ðŸ™ X) (by simp [â† h]) by
    simpa using yoneda.congr_map this =â‰« (hf.representable.fst (a â‰« f))
  -- This follows from the fact that the induced maps `hf.representable.pullback g âŸ¶ X` are Mono.
  have : Mono (hf.representable.snd (a â‰« f)) := hf.property (a â‰« f)
  simp only [â† cancel_mono (hf.representable.snd (a â‰« f)),
    Presheaf.representable.lift_snd]

/-- If `P' : MorphismProperty C` is satisfied whenever `P` is, then also `P'.presheaf` is
satisfied whenever `P.presheaf` is. -/
lemma presheaf_monotone {P' : MorphismProperty C} (h : P â‰¤ P') :
    P.presheaf â‰¤ P'.presheaf := fun _ _ _ hf â†¦
  âŸ¨hf.representable, fun _ g â†¦ h _ (hf.property g)âŸ©

instance representable_isStableUnderComposition :
    IsStableUnderComposition (Presheaf.representable (C:=C)) where
  comp_mem {F G H} f g hf hg := fun X h â†¦
    âŸ¨hf.pullback (hg.fst h), hf.snd (hg.fst h) â‰« hg.snd h, hf.fst (hg.fst h),
      by simpa using IsPullback.paste_vert (hf.isPullback (hg.fst h)) (hg.isPullback h)âŸ©

lemma representable_stableUnderBaseChange :
    StableUnderBaseChange (Presheaf.representable (C:=C)) := by
  intro F G G' H f g f' g' Pâ‚ hg X h
  refine âŸ¨hg.pullback (h â‰« f), hg.snd (h â‰« f), ?_, ?_âŸ©

  apply Pâ‚.lift (hg.fst (h â‰« f)) (yoneda.map (hg.snd (h â‰« f)) â‰« h) (by sorry)

  apply IsPullback.of_right' (hg.isPullback (h â‰« f)) Pâ‚

lemma representable_ofIsIso {F G : Cáµ’áµ– â¥¤ Type v} (f : F âŸ¶ G) [IsIso f] :
    Presheaf.representable f :=
  fun X g â†¦ âŸ¨X, ðŸ™ X, g â‰« inv f, IsPullback.of_vert_isIso âŸ¨by simpâŸ©âŸ©

lemma representable_isomorphisms_le :
    MorphismProperty.isomorphisms (Cáµ’áµ– â¥¤ Type v) â‰¤ Presheaf.representable :=
  fun _ _ f hf â†¦ letI : IsIso f := hf; representable_ofIsIso f

lemma representable_respectsIso : RespectsIso (Presheaf.representable (C:=C)) :=
  representable_stableUnderBaseChange.respectsIso

section

variable [HasPullbacks C] (hPâ‚€ : P.RespectsIso)

lemma presheaf_stableUnderBaseChange : StableUnderBaseChange (MorphismProperty.presheaf P) := by
  intro F G G' H f g f' g' hfBC hg
  have hg' := representable_stableUnderBaseChange hfBC hg.representable
  refine âŸ¨hg', fun X h â†¦ hg.property' hPâ‚€ (IsPullback.paste_horiz (hg'.isPullback h) hfBC)âŸ©

-- if P.presheaf assumes `StableUnderBaseChange`, this could be maybe an instance
-- (Calle): This is definitely golfable
lemma presheaf_isStableUnderComp [P.IsStableUnderComposition] :
    IsStableUnderComposition (P.presheaf) where
  comp_mem {F G H} f g hf hg := by
    apply P.presheaf_mk' hPâ‚€ (Presheaf.representable.comp_mem f g hf.representable hg.representable)
    intro X h
    -- (Calle): Maybe its worth givin P.presheaf.representable a shorter name, e.g. P.presheaf.repr
    have hgBC := hg.representable.isPullback h
    have hfBC := hf.representable.isPullback (hg.representable.fst h)
    have hBC := IsPullback.paste_vert hfBC hgBC

    use hf.representable.pullback (hg.representable.fst h)
    use hBC.cone.fst
    use hf.representable.snd (hg.representable.fst h) â‰« (hg.representable.snd h)

    simp only [IsPullback.cone_fst, Functor.map_comp, Functor.FullyFaithful.map_preimage,
      exists_prop]
    use hBC
    apply P.comp_mem _ _ (hf.property _) (hg.property _)

lemma presheaf_respectsIso : RespectsIso P.presheaf :=
  (presheaf_stableUnderBaseChange hPâ‚€).respectsIso

end

end MorphismProperty

end CategoryTheory
