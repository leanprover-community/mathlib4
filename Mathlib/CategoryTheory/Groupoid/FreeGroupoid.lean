/-
Copyright (c) 2022 R√©mi Bottinelli. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©mi Bottinelli
-/
import Mathlib.CategoryTheory.Groupoid
import Mathlib.CategoryTheory.PathCategory.Basic

/-!
# Free groupoid on a quiver

This file defines the free groupoid on a quiver, the lifting of a prefunctor to its unique
extension as a functor from the free groupoid, and proves uniqueness of this extension.

## Main results

Given the type `V` and a quiver instance on `V`:

- `FreeGroupoid V`: a type synonym for `V`.
- `FreeGroupoid.instGroupoid`: the `Groupoid` instance on `FreeGroupoid V`.
- `lift`: the lifting of a prefunctor from `V` to `V'` where `V'` is a groupoid, to a functor.
  `FreeGroupoid V ‚•§ V'`.
- `lift_spec` and `lift_unique`: the proofs that, respectively, `lift` indeed is a lifting
  and is the unique one.

## Implementation notes

The free groupoid is first defined by symmetrifying the quiver, taking the induced path category
and finally quotienting by the reducibility relation.

-/

open Set Function

namespace CategoryTheory

namespace Groupoid

namespace Free

universe u v u' v' u'' v''

variable {V : Type u} [Quiver.{v + 1} V]

/-- Shorthand for the "forward" arrow corresponding to `f` in `paths <| symmetrify V` -/
abbrev _root_.Quiver.Hom.toPosPath {X Y : V} (f : X ‚ü∂ Y) :
    (CategoryTheory.Paths.categoryPaths <| Quiver.Symmetrify V).Hom X Y :=
  f.toPos.toPath

/-- Shorthand for the "forward" arrow corresponding to `f` in `paths <| symmetrify V` -/
abbrev _root_.Quiver.Hom.toNegPath {X Y : V} (f : X ‚ü∂ Y) :
    (CategoryTheory.Paths.categoryPaths <| Quiver.Symmetrify V).Hom Y X :=
  f.toNeg.toPath

/-- The "reduction" relation -/
inductive redStep : HomRel (Paths (Quiver.Symmetrify V))
  | step (X Z : Quiver.Symmetrify V) (f : X ‚ü∂ Z) :
    redStep (ùüô ((Paths.of (Quiver.Symmetrify V)).obj X)) (f.toPath ‚â´ (Quiver.reverse f).toPath)

/-- The underlying vertices of the free groupoid -/
def _root_.CategoryTheory.FreeGroupoid (V) [Q : Quiver V] :=
  Quotient (@redStep V Q)

instance {V} [Quiver V] [Nonempty V] : Nonempty (FreeGroupoid V) := by
  inhabit V; exact ‚ü®‚ü®@default V _‚ü©‚ü©

theorem congr_reverse {X Y : Paths <| Quiver.Symmetrify V} (p q : X ‚ü∂ Y) :
    Quotient.CompClosure redStep p q ‚Üí Quotient.CompClosure redStep p.reverse q.reverse := by
  rintro ‚ü®XW, pp, qq, WY, _, Z, f‚ü©
  have : Quotient.CompClosure redStep (WY.reverse ‚â´ ùüô _ ‚â´ XW.reverse)
      (WY.reverse ‚â´ (f.toPath ‚â´ (Quiver.reverse f).toPath) ‚â´ XW.reverse) := by
    constructor
    constructor
  simpa only [CategoryStruct.comp, CategoryStruct.id, Quiver.Path.reverse, Quiver.Path.nil_comp,
    Quiver.Path.reverse_comp, Quiver.reverse_reverse, Quiver.Path.reverse_toPath,
    Quiver.Path.comp_assoc] using this

open Relation in
theorem congr_comp_reverse {X Y : Paths <| Quiver.Symmetrify V} (p : X ‚ü∂ Y) :
    Quot.mk (@Quotient.CompClosure _ _ redStep _ _) (p ‚â´ p.reverse) =
      Quot.mk (@Quotient.CompClosure _ _ redStep _ _) (ùüô X) := by
  apply Quot.eqvGen_sound
  induction p with
  | nil => apply EqvGen.refl
  | cons q f ih =>
    simp only [Quiver.Path.reverse]
    fapply EqvGen.trans
    -- Porting note: dot notation for `Quiver.Path.*` and `Quiver.Hom.*` not working
    ¬∑ exact q ‚â´ Quiver.Path.reverse q
    ¬∑ apply EqvGen.symm
      apply EqvGen.rel
      have : Quotient.CompClosure redStep (q ‚â´ ùüô _ ‚â´ Quiver.Path.reverse q)
          (q ‚â´ (Quiver.Hom.toPath f ‚â´ Quiver.Hom.toPath (Quiver.reverse f)) ‚â´
            Quiver.Path.reverse q) := by
        apply Quotient.CompClosure.intro
        apply redStep.step
      simp only [Category.assoc, Category.id_comp] at this ‚ä¢
      -- Porting note: `simp` cannot see how `Quiver.Path.comp_assoc` is relevant, so change to
      -- category notation
      change Quotient.CompClosure redStep (q ‚â´ Quiver.Path.reverse q)
        (Quiver.Path.cons q f ‚â´ (Quiver.Hom.toPath (Quiver.reverse f)) ‚â´ (Quiver.Path.reverse q))
      simp only [‚Üê Category.assoc] at this ‚ä¢
      exact this
    ¬∑ exact ih

theorem congr_reverse_comp {X Y : Paths <| Quiver.Symmetrify V} (p : X ‚ü∂ Y) :
    Quot.mk (@Quotient.CompClosure _ _ redStep _ _) (p.reverse ‚â´ p) =
      Quot.mk (@Quotient.CompClosure _ _ redStep _ _) (ùüô Y) := by
  nth_rw 2 [‚Üê Quiver.Path.reverse_reverse p]
  apply congr_comp_reverse

instance : Category (FreeGroupoid V) :=
  Quotient.category redStep

/-- The inverse of an arrow in the free groupoid -/
def quotInv {X Y : FreeGroupoid V} (f : X ‚ü∂ Y) : Y ‚ü∂ X :=
  Quot.liftOn f (fun pp => Quot.mk _ <| pp.reverse) fun pp qq con =>
    Quot.sound <| congr_reverse pp qq con

instance _root_.CategoryTheory.FreeGroupoid.instGroupoid : Groupoid (FreeGroupoid V) where
  inv := quotInv
  inv_comp p := Quot.inductionOn p fun pp => congr_reverse_comp pp
  comp_inv p := Quot.inductionOn p fun pp => congr_comp_reverse pp

/-- The inclusion of the quiver on `V` to the underlying quiver on `FreeGroupoid V` -/
def of (V) [Quiver V] : V ‚•§q FreeGroupoid V where
  obj X := ‚ü®X‚ü©
  map f := Quot.mk _ f.toPosPath

theorem of_eq :
    of V = (Quiver.Symmetrify.of ‚ãôq (Paths.of (Quiver.Symmetrify V))).comp
      (Quotient.functor <| @redStep V _).toPrefunctor := rfl

section UniversalProperty

variable {V' : Type u'} [Groupoid V']

/-- The lift of a prefunctor to a groupoid, to a functor from `FreeGroupoid V` -/
def lift (œÜ : V ‚•§q V') : FreeGroupoid V ‚•§ V' :=
  Quotient.lift _ (Paths.lift <| Quiver.Symmetrify.lift œÜ) <| by
    rintro _ _ _ _ ‚ü®X, Y, f‚ü©
    -- Porting note: `simp` does not work, so manually `rewrite`
    erw [Paths.lift_nil, Paths.lift_cons, Quiver.Path.comp_nil, Paths.lift_toPath,
      Quiver.Symmetrify.lift_reverse]
    symm
    apply Groupoid.comp_inv

theorem lift_spec (œÜ : V ‚•§q V') : of V ‚ãôq (lift œÜ).toPrefunctor = œÜ := by
  rw [of_eq, Prefunctor.comp_assoc, Prefunctor.comp_assoc, Functor.toPrefunctor_comp]
  dsimp [lift]
  rw [Quotient.lift_spec, Paths.lift_spec, Quiver.Symmetrify.lift_spec]

theorem lift_unique (œÜ : V ‚•§q V') (Œ¶ : FreeGroupoid V ‚•§ V') (hŒ¶ : of V ‚ãôq Œ¶.toPrefunctor = œÜ) :
    Œ¶ = lift œÜ := by
  apply Quotient.lift_unique
  apply Paths.lift_unique
  fapply @Quiver.Symmetrify.lift_unique _ _ _ _ _ _ _ _ _
  ¬∑ rw [‚Üê Functor.toPrefunctor_comp]
    exact hŒ¶
  ¬∑ rintro X Y f
    simp only [‚Üê Functor.toPrefunctor_comp, Prefunctor.comp_map, Paths.of_map]
    change Œ¶.map (inv ((Quotient.functor redStep).toPrefunctor.map f.toPath)) =
      inv (Œ¶.map ((Quotient.functor redStep).toPrefunctor.map f.toPath))
    have := Functor.map_inv Œ¶ ((Quotient.functor redStep).toPrefunctor.map f.toPath)
    convert this <;> simp only [inv_eq_inv]

end UniversalProperty

section Functoriality

variable {V' : Type u'} [Quiver.{v' + 1} V'] {V'' : Type u''} [Quiver.{v'' + 1} V'']

/-- The functor of free groupoid induced by a prefunctor of quivers -/
def _root_.CategoryTheory.freeGroupoidFunctor (œÜ : V ‚•§q V') : FreeGroupoid V ‚•§ FreeGroupoid V' :=
  lift (œÜ ‚ãôq of V')

theorem freeGroupoidFunctor_id :
    freeGroupoidFunctor (Prefunctor.id V) = Functor.id (FreeGroupoid V) := by
  dsimp only [freeGroupoidFunctor]; symm
  apply lift_unique; rfl

theorem freeGroupoidFunctor_comp (œÜ : V ‚•§q V') (œÜ' : V' ‚•§q V'') :
    freeGroupoidFunctor (œÜ ‚ãôq œÜ') = freeGroupoidFunctor œÜ ‚ãô freeGroupoidFunctor œÜ' := by
  dsimp only [freeGroupoidFunctor]; symm
  apply lift_unique; rfl

end Functoriality

end Free

end Groupoid

end CategoryTheory
