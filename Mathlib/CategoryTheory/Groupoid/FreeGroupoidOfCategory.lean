/-
Copyright (c) 2025 Joseph Hua. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Hua
-/
module

public import Mathlib.CategoryTheory.Groupoid.FreeGroupoid
public import Mathlib.CategoryTheory.Category.Grpd
public import Mathlib.CategoryTheory.Adjunction.Reflective
public import Mathlib.CategoryTheory.Localization.Predicate

/-!
# Free groupoid on a category

This file defines the free groupoid on a category, the lifting of a functor to its unique
extension as a functor from the free groupoid, and proves uniqueness of this extension.

## Main results

Given a type `C` and a category instance on `C`:

- `CategoryTheory.FreeGroupoid C`: the underlying type of the free groupoid on `C`.
- `CategoryTheory.FreeGroupoid.instGroupoid`: the `Groupoid` instance on `FreeGroupoid C`.
- `CategoryTheory.FreeGroupoid.lift`: the lifting of a functor `C â¥¤ G` where `G` is a
  groupoid, to a functor `CategoryTheory.FreeGroupoid C â¥¤ G`.
- `CategoryTheory.FreeGroupoid.lift_spec` and
  `CategoryTheory.FreeGroupoid.lift_unique`:
  the proofs that, respectively, `CategoryTheory.FreeGroupoid.lift` indeed is a lifting
  and is the unique one.
- `CategoryTheory.Grpd.free`: the free functor from `Grpd` to `Cat`
- `CategoryTheory.Grpd.freeForgetAdjunction`: that `free` is left adjoint to
  `Grpd.forgetToCat`.

## Implementation notes

The free groupoid on a category `C` is first defined by taking the free groupoid `G`
on the underlying *quiver* of `C`. Then the free groupoid on the *category* `C` is defined as
the quotient of `G` by the relation that makes the inclusion prefunctor `C â¥¤q G` a functor.

-/

@[expose] public section

noncomputable section

namespace CategoryTheory

universe v u vâ‚ uâ‚ vâ‚‚ uâ‚‚

variable (C : Type u) [Category.{v} C]

open Quiver in
/-- The relation on the free groupoid on the underlying *quiver* of C that
promotes the prefunctor `C â¥¤q FreeGroupoid C` into a functor
`C â¥¤ Quotient (FreeGroupoid.homRel C)`. -/
inductive FreeGroupoid.homRel : HomRel (Quiver.FreeGroupoid C) where
| map_id (X : C) : homRel ((FreeGroupoid.of C).map (ğŸ™ X)) (ğŸ™ ((FreeGroupoid.of C).obj X))
| map_comp {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) : homRel ((FreeGroupoid.of C).map (f â‰« g))
  ((FreeGroupoid.of C).map f â‰« (FreeGroupoid.of C).map g)

/-- The underlying type of the free groupoid on a category,
defined by quotienting the free groupoid on the underlying quiver of `C`
by the relation that promotes the prefunctor `C â¥¤q FreeGroupoid C` into a functor
`C â¥¤ Quotient (FreeGroupoid.homRel C)`. -/
def FreeGroupoid := Quotient (FreeGroupoid.homRel C)

instance [Nonempty C] : Nonempty (FreeGroupoid C) :=
  âŸ¨Quotient.mk (Quotient.mk ((Paths.of _).obj (Classical.arbitrary C)))âŸ©

instance : Groupoid (FreeGroupoid C) :=
  Quotient.groupoid (FreeGroupoid.homRel C)

namespace FreeGroupoid

/-- The localization functor from the category `C` to the groupoid `FreeGroupoid C` -/
def of : C â¥¤ FreeGroupoid C where
  __ := Quiver.FreeGroupoid.of C â‹™q (Quotient.functor (FreeGroupoid.homRel C)).toPrefunctor
  map_id X := Quotient.sound _ (FreeGroupoid.homRel.map_id X)
  map_comp f g := Quotient.sound _ (FreeGroupoid.homRel.map_comp f g)

variable {C}

/-- Construct an object in the free groupoid on `C` by providing an object in `C`. -/
abbrev mk (X : C) : FreeGroupoid C := (of C).obj X

/-- Construct a morphism in the free groupoid on `C` by providing a morphism in `C`. -/
abbrev homMk {X Y : C} (f : X âŸ¶ Y) : mk X âŸ¶ mk Y := (of C).map f

lemma eq_mk (X : FreeGroupoid C) : X = .mk (X.as.as) := rfl

lemma of_obj_bijective : Function.Bijective (of C).obj where
  left _ _ h := by cases h; rfl
  right X := âŸ¨X.as.as, rflâŸ©

section UniversalProperty

variable {G : Type uâ‚} [Groupoid.{vâ‚} G]

/-- The lift of a functor from `C` to a groupoid to a functor from
`FreeGroupoid C` to the groupoid -/
def lift (Ï† : C â¥¤ G) : FreeGroupoid C â¥¤ G :=
  Quotient.lift (FreeGroupoid.homRel C) (Quiver.FreeGroupoid.lift Ï†.toPrefunctor)
    (fun _ _ f g r â†¦ by
      have {X Y : C} (f : X âŸ¶ Y) :=
        Prefunctor.congr_hom (Quiver.FreeGroupoid.lift_spec Ï†.toPrefunctor) f
      induction r <;> cat_disch)

theorem lift_spec (Ï† : C â¥¤ G) : of C â‹™ lift Ï† = Ï† :=
  Functor.toPrefunctor_injective (by
    change Quiver.FreeGroupoid.of C â‹™q
      (Quotient.functor (FreeGroupoid.homRel C)).toPrefunctor â‹™q
        (lift Ï†).toPrefunctor = Ï†.toPrefunctor
    simp [lift, Quotient.lift_spec, Quiver.FreeGroupoid.lift_spec])

@[simp]
lemma lift_obj_mk {E : Type uâ‚‚} [Groupoid.{vâ‚‚} E] (Ï† : C â¥¤ E) (X : C) :
    (lift Ï†).obj (mk X) = Ï†.obj X := rfl

@[simp]
lemma lift_map_homMk {E : Type uâ‚‚} [Groupoid.{vâ‚‚} E] (Ï† : C â¥¤ E) {X Y : C} (f : X âŸ¶ Y) :
    (lift Ï†).map (homMk f) = Ï†.map f := by
  simpa using Functor.congr_hom (lift_spec Ï†) f

theorem lift_unique (Ï† : C â¥¤ G) (Î¦ : FreeGroupoid C â¥¤ G) (hÎ¦ : of C â‹™ Î¦ = Ï†) :
    Î¦ = lift Ï† := by
  apply Quotient.lift_unique
  apply Quiver.FreeGroupoid.lift_unique
  exact congr_arg Functor.toPrefunctor hÎ¦

theorem lift_id_comp_of : lift (ğŸ­ G) â‹™ of G = ğŸ­ _ := by
  rw [lift_unique (of G) (lift (ğŸ­ G) â‹™ of G) (by rw [â† Functor.assoc, lift_spec, Functor.id_comp])]
  symm; apply lift_unique
  rw [Functor.comp_id]

theorem lift_comp {H : Type uâ‚‚} [Groupoid.{vâ‚‚} H] (Ï† : C â¥¤ G) (Ïˆ : G â¥¤ H) :
    lift (Ï† â‹™ Ïˆ) = lift Ï† â‹™ Ïˆ := by
  symm
  apply lift_unique
  rw [â† Functor.assoc, lift_spec]

/-- The universal property of the free groupoid. -/
def strictUniversalPropertyFixedTarget :
    Localization.StrictUniversalPropertyFixedTarget (of C) âŠ¤ G where
  inverts _ := inferInstance
  lift F _ := lift F
  fac _ _ := lift_spec ..
  uniq F G h := by rw [lift_unique (of C â‹™ G) F h, â† lift_unique (of C â‹™ G) G rfl]

attribute [local instance] Localization.groupoid

instance : (of C).IsLocalization âŠ¤ :=
  .mk' _ _ strictUniversalPropertyFixedTarget strictUniversalPropertyFixedTarget

/-- In order to define a natural isomorphism `F â‰… G` with `F G : FreeGroupoid â¥¤ D`,
it suffices to do so after precomposing with `FreeGroupoid.of C`. -/
def liftNatIso (Fâ‚ Fâ‚‚ : FreeGroupoid C â¥¤ G) (Ï„ : of C â‹™ Fâ‚ â‰… of C â‹™ Fâ‚‚) : Fâ‚ â‰… Fâ‚‚ :=
  Localization.liftNatIso (of C) âŠ¤ (of C â‹™ Fâ‚) (of C â‹™ Fâ‚‚) _ _ Ï„

@[simp]
lemma liftNatIso_hom_app (Fâ‚ Fâ‚‚ : FreeGroupoid C â¥¤ G) (Ï„ : of C â‹™ Fâ‚ â‰… of C â‹™ Fâ‚‚) (X) :
    (liftNatIso Fâ‚ Fâ‚‚ Ï„).hom.app (mk X) = Ï„.hom.app X := by
  simp [liftNatIso]

@[simp]
lemma liftNatIso_inv_app (Fâ‚ Fâ‚‚ : FreeGroupoid C â¥¤ G) (Ï„ : of C â‹™ Fâ‚ â‰… of C â‹™ Fâ‚‚) (X) :
    (liftNatIso Fâ‚ Fâ‚‚ Ï„).inv.app (mk X) = Ï„.inv.app X := by
  simp [liftNatIso]

end UniversalProperty

section Functoriality

variable {D : Type uâ‚} [Category.{vâ‚} D] {E : Type uâ‚‚} [Category.{vâ‚‚} E]

/-- The functor between free groupoids induced by a functor between categories. -/
def map (Ï† : C â¥¤ D) : FreeGroupoid C â¥¤ FreeGroupoid D :=
  lift (Ï† â‹™ of D)

lemma of_comp_map (F : C â¥¤ D) : of C â‹™ map F = F â‹™ of D := rfl

/-- The operation `of` is natural. -/
def ofCompMapIso (F : C â¥¤ D) : of C â‹™ map F â‰… F â‹™ of D := Iso.refl _

variable (C) in
/-- The functor induced by the identity is the identity. -/
def mapId : map (ğŸ­ C) â‰… ğŸ­ (FreeGroupoid C) :=
  liftNatIso _ _ (Iso.refl _)

@[simp]
lemma mapId_hom_app (X) : (mapId C).hom.app X = ğŸ™ X :=
  liftNatIso_hom_app ..

@[simp]
lemma mapId_inv_app (X) : (mapId C).inv.app X = ğŸ™ X :=
  liftNatIso_inv_app ..

variable (C) in
theorem map_id : map (ğŸ­ C) = ğŸ­ (FreeGroupoid C) := by
  symm; apply lift_unique; rfl

/-- The functor induced by a composition is the composition of the functors they induce. -/
def mapComp (Ï† : C â¥¤ D) (Ï†' : D â¥¤ E) : map (Ï† â‹™ Ï†') â‰… map Ï† â‹™ map Ï†' :=
  liftNatIso _ _ (Iso.refl _)

@[simp]
lemma mapComp_hom_app (Ï† : C â¥¤ D) (Ï†' : D â¥¤ E) (X) : (mapComp Ï† Ï†').hom.app X = ğŸ™ _ :=
  liftNatIso_hom_app ..

@[simp]
lemma mapComp_inv_app (Ï† : C â¥¤ D) (Ï†' : D â¥¤ E) (X) : (mapComp Ï† Ï†').inv.app X = ğŸ™ _ :=
  liftNatIso_inv_app ..

theorem map_comp (Ï† : C â¥¤ D) (Ï†' : D â¥¤ E) : map (Ï† â‹™ Ï†') = map Ï† â‹™ map Ï†' := by
  symm; apply lift_unique; rfl

@[simp]
lemma map_obj_mk (Ï† : C â¥¤ D) (X : C) : (map Ï†).obj (mk X) = mk (Ï†.obj X) := rfl

@[simp]
lemma map_map_homMk (Ï† : C â¥¤ D) {X Y : C} (f : X âŸ¶ Y) :
    (map Ï†).map (homMk f) = homMk (Ï†.map f) := rfl

variable {E : Type uâ‚‚} [Groupoid.{vâ‚‚} E]

lemma map_comp_lift (F : C â¥¤ D) (G : D â¥¤ E) : map F â‹™ lift G = lift (F â‹™ G) := by
  apply lift_unique
  rw [â† Functor.assoc, of_comp_map, Functor.assoc, lift_spec G]

/-- The operation `lift` is natural. -/
def mapCompLift (F : C â¥¤ D) (G : D â¥¤ E) : map F â‹™ lift G â‰… lift (F â‹™ G) :=
  liftNatIso _ _ (Iso.refl _)

@[simp]
lemma mapCompLift_hom_app (F : C â¥¤ D) (G : D â¥¤ E) (X) : (mapCompLift F G).hom.app X = ğŸ™ _ :=
  liftNatIso_hom_app ..

@[simp]
lemma mapCompLift_inv_app (F : C â¥¤ D) (G : D â¥¤ E) (X) : (mapCompLift F G).inv.app X = ğŸ™ _ :=
  liftNatIso_inv_app ..

end Functoriality

/-- Functors out of the free groupoid biject with functors out of the original category. -/
@[simps]
def functorEquiv {D : Type*} [Groupoid D] : (FreeGroupoid C â¥¤ D) â‰ƒ (C â¥¤ D) where
  toFun G := of C â‹™ G
  invFun F := lift F
  right_inv := lift_spec
  left_inv _ := (lift_unique _ _ rfl).symm

end FreeGroupoid

namespace Grpd

open FreeGroupoid

/-- The free groupoid construction on a category as a functor. -/
def free : Cat.{u, u} â¥¤ Grpd.{u, u} where
  obj C := Grpd.of <| FreeGroupoid C
  map {C D} F := map F.toFunctor
  map_id C := by simp [map_id, id_eq_id]
  map_comp F G := by simp [Grpd.comp_eq_comp, map_comp]

@[simp]
lemma free_obj (C : Cat.{u, u}) : free.obj C = FreeGroupoid C :=
  rfl

@[simp]
lemma free_map {C D : Cat.{u, u}} (F : C âŸ¶ D) : free.map F = map F.toFunctor :=
  rfl

/-- The free-forgetful adjunction between `Grpd` and `Cat`. -/
def freeForgetAdjunction : free âŠ£ Grpd.forgetToCat :=
  Adjunction.mkOfHomEquiv
    { homEquiv _ _ := FreeGroupoid.functorEquiv.trans (Functor.equivCatHom _ _)
      homEquiv_naturality_left_symm _ _ := (FreeGroupoid.map_comp_lift _ _).symm
      homEquiv_naturality_right _ _ := rfl }

variable {C : Type u} [Category.{u} C] {D : Type u} [Groupoid.{u} D]

@[simp]
lemma freeForgetAdjunction_homEquiv_apply (F : FreeGroupoid C â¥¤ D) :
    (freeForgetAdjunction.homEquiv (Cat.of C) (Grpd.of D) F).toFunctor = FreeGroupoid.of C â‹™ F :=
  rfl

@[simp]
lemma freeForgetAdjunction_homEquiv_symm_apply (F : C â¥¤ D) :
    (freeForgetAdjunction.homEquiv (Cat.of C) (Grpd.of D)).symm F.toCatHom = map F â‹™ lift (ğŸ­ D) :=
  rfl

@[simp]
lemma freeForgetAdjunction_unit_app :
    (freeForgetAdjunction.unit.app (Cat.of C)).toFunctor = FreeGroupoid.of C :=
  rfl

@[simp]
lemma freeForgetAdjunction_counit_app :
    freeForgetAdjunction.counit.app (Grpd.of D) = lift (ğŸ­ D) :=
  rfl

instance : Reflective Grpd.forgetToCat where
  L := free
  adj := freeForgetAdjunction

end Grpd
end CategoryTheory
end
