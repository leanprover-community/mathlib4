/-
Copyright (c) 2025 Joseph Hua. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Hua
-/
module

public import Mathlib.CategoryTheory.Groupoid.FreeGroupoid
public import Mathlib.CategoryTheory.Groupoid.Grpd.Basic
public import Mathlib.CategoryTheory.Adjunction.Reflective
public import Mathlib.CategoryTheory.Localization.Predicate

/-!
# Free groupoid on a category

This file defines the free groupoid on a category, the lifting of a functor to its unique
extension as a functor from the free groupoid, and proves uniqueness of this extension.

## Main results

Given a type `C` and a category instance on `C`:

- `CategoryTheory.FreeGroupoid C`: the underlying type of the free groupoid on `C`.
- `CategoryTheory.FreeGroupoid.instGroupoid`: the `Groupoid` instance on `FreeGroupoid C`.
- `CategoryTheory.FreeGroupoid.lift`: the lifting of a functor `C ‚•§ G` where `G` is a
  groupoid, to a functor `CategoryTheory.FreeGroupoid C ‚•§ G`.
- `CategoryTheory.FreeGroupoid.lift_spec` and
  `CategoryTheory.FreeGroupoid.lift_unique`:
  the proofs that, respectively, `CategoryTheory.FreeGroupoid.lift` indeed is a lifting
  and is the unique one.
- `CategoryTheory.Grpd.free`: the free functor from `Grpd` to `Cat`
- `CategoryTheory.Grpd.freeForgetAdjunction`: that `free` is left adjoint to
  `Grpd.forgetToCat`.

## Implementation notes

The free groupoid on a category `C` is first defined by taking the free groupoid `G`
on the underlying *quiver* of `C`. Then the free groupoid on the *category* `C` is defined as
the quotient of `G` by the relation that makes the inclusion prefunctor `C ‚•§q G` a functor.

-/

@[expose] public section

noncomputable section

namespace CategoryTheory

universe v u v‚ÇÅ u‚ÇÅ v‚ÇÇ u‚ÇÇ

variable (C : Type u) [Category.{v} C]

open Quiver in
/-- The relation on the free groupoid on the underlying *quiver* of C that
promotes the prefunctor `C ‚•§q FreeGroupoid C` into a functor
`C ‚•§ Quotient (FreeGroupoid.homRel C)`. -/
inductive FreeGroupoid.homRel : HomRel (Quiver.FreeGroupoid C) where
| map_id (X : C) : homRel ((FreeGroupoid.of C).map (ùüô X)) (ùüô ((FreeGroupoid.of C).obj X))
| map_comp {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : homRel ((FreeGroupoid.of C).map (f ‚â´ g))
  ((FreeGroupoid.of C).map f ‚â´ (FreeGroupoid.of C).map g)

/-- The underlying type of the free groupoid on a category,
defined by quotienting the free groupoid on the underlying quiver of `C`
by the relation that promotes the prefunctor `C ‚•§q FreeGroupoid C` into a functor
`C ‚•§ Quotient (FreeGroupoid.homRel C)`. -/
def FreeGroupoid := Quotient (FreeGroupoid.homRel C)

instance [Nonempty C] : Nonempty (FreeGroupoid C) :=
  ‚ü®Quotient.mk (Quotient.mk ((Paths.of _).obj (Classical.arbitrary C)))‚ü©

instance : Groupoid (FreeGroupoid C) :=
  Quotient.groupoid (FreeGroupoid.homRel C)

namespace FreeGroupoid

/-- The localization functor from the category `C` to the groupoid `FreeGroupoid C` -/
def of : C ‚•§ FreeGroupoid C where
  __ := Quiver.FreeGroupoid.of C ‚ãôq (Quotient.functor (FreeGroupoid.homRel C)).toPrefunctor
  map_id X := Quotient.sound _ (FreeGroupoid.homRel.map_id X)
  map_comp f g := Quotient.sound _ (FreeGroupoid.homRel.map_comp f g)

variable {C}

/-- Construct an object in the free groupoid on `C` by providing an object in `C`. -/
abbrev mk (X : C) : FreeGroupoid C := (of C).obj X

/-- Construct a morphism in the free groupoid on `C` by providing a morphism in `C`. -/
abbrev homMk {X Y : C} (f : X ‚ü∂ Y) : mk X ‚ü∂ mk Y := (of C).map f

lemma eq_mk (X : FreeGroupoid C) : X = .mk (X.as.as) := rfl

lemma of_obj_bijective : Function.Bijective (of C).obj where
  left _ _ h := by cases h; rfl
  right X := ‚ü®X.as.as, rfl‚ü©

section UniversalProperty

variable {G : Type u‚ÇÅ} [Groupoid.{v‚ÇÅ} G]

/-- The lift of a functor from `C` to a groupoid to a functor from
`FreeGroupoid C` to the groupoid -/
def lift (œÜ : C ‚•§ G) : FreeGroupoid C ‚•§ G :=
  Quotient.lift (FreeGroupoid.homRel C) (Quiver.FreeGroupoid.lift œÜ.toPrefunctor)
    (fun _ _ f g r ‚Ü¶ by
      have {X Y : C} (f : X ‚ü∂ Y) :=
        Prefunctor.congr_hom (Quiver.FreeGroupoid.lift_spec œÜ.toPrefunctor) f
      induction r <;> cat_disch)

theorem lift_spec (œÜ : C ‚•§ G) : of C ‚ãô lift œÜ = œÜ :=
  Functor.toPrefunctor_injective (by
    change Quiver.FreeGroupoid.of C ‚ãôq
      (Quotient.functor (FreeGroupoid.homRel C)).toPrefunctor ‚ãôq
        (lift œÜ).toPrefunctor = œÜ.toPrefunctor
    simp [lift, Quotient.lift_spec, Quiver.FreeGroupoid.lift_spec])

@[simp]
lemma lift_obj_mk {E : Type u‚ÇÇ} [Groupoid.{v‚ÇÇ} E] (œÜ : C ‚•§ E) (X : C) :
    (lift œÜ).obj (mk X) = œÜ.obj X := rfl

@[simp]
lemma lift_map_homMk {E : Type u‚ÇÇ} [Groupoid.{v‚ÇÇ} E] (œÜ : C ‚•§ E) {X Y : C} (f : X ‚ü∂ Y) :
    (lift œÜ).map (homMk f) = œÜ.map f := by
  simpa using Functor.congr_hom (lift_spec œÜ) f

theorem lift_unique (œÜ : C ‚•§ G) (Œ¶ : FreeGroupoid C ‚•§ G) (hŒ¶ : of C ‚ãô Œ¶ = œÜ) :
    Œ¶ = lift œÜ := by
  apply Quotient.lift_unique
  apply Quiver.FreeGroupoid.lift_unique
  exact congr_arg Functor.toPrefunctor hŒ¶

theorem lift_id_comp_of : lift (ùü≠ G) ‚ãô of G = ùü≠ _ := by
  rw [lift_unique (of G) (lift (ùü≠ G) ‚ãô of G) (by rw [‚Üê Functor.assoc, lift_spec, Functor.id_comp])]
  symm; apply lift_unique
  rw [Functor.comp_id]

theorem lift_comp {H : Type u‚ÇÇ} [Groupoid.{v‚ÇÇ} H] (œÜ : C ‚•§ G) (œà : G ‚•§ H) :
    lift (œÜ ‚ãô œà) = lift œÜ ‚ãô œà := by
  symm
  apply lift_unique
  rw [‚Üê Functor.assoc, lift_spec]

/-- The universal property of the free groupoid. -/
def strictUniversalPropertyFixedTarget :
    Localization.StrictUniversalPropertyFixedTarget (of C) ‚ä§ G where
  inverts _ := inferInstance
  lift F _ := lift F
  fac _ _ := lift_spec ..
  uniq F G h := by rw [lift_unique (of C ‚ãô G) F h, ‚Üê lift_unique (of C ‚ãô G) G rfl]

attribute [local instance] Localization.groupoid

instance : (of C).IsLocalization ‚ä§ :=
  .mk' _ _ strictUniversalPropertyFixedTarget strictUniversalPropertyFixedTarget

/-- In order to define a natural isomorphism `F ‚âÖ G` with `F G : FreeGroupoid ‚•§ D`,
it suffices to do so after precomposing with `FreeGroupoid.of C`. -/
def liftNatIso (F‚ÇÅ F‚ÇÇ : FreeGroupoid C ‚•§ G) (œÑ : of C ‚ãô F‚ÇÅ ‚âÖ of C ‚ãô F‚ÇÇ) : F‚ÇÅ ‚âÖ F‚ÇÇ :=
  Localization.liftNatIso (of C) ‚ä§ (of C ‚ãô F‚ÇÅ) (of C ‚ãô F‚ÇÇ) _ _ œÑ

@[simp]
lemma liftNatIso_hom_app (F‚ÇÅ F‚ÇÇ : FreeGroupoid C ‚•§ G) (œÑ : of C ‚ãô F‚ÇÅ ‚âÖ of C ‚ãô F‚ÇÇ) (X) :
    (liftNatIso F‚ÇÅ F‚ÇÇ œÑ).hom.app (mk X) = œÑ.hom.app X := by
  simp [liftNatIso]

@[simp]
lemma liftNatIso_inv_app (F‚ÇÅ F‚ÇÇ : FreeGroupoid C ‚•§ G) (œÑ : of C ‚ãô F‚ÇÅ ‚âÖ of C ‚ãô F‚ÇÇ) (X) :
    (liftNatIso F‚ÇÅ F‚ÇÇ œÑ).inv.app (mk X) = œÑ.inv.app X := by
  simp [liftNatIso]

end UniversalProperty

section Functoriality

variable {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D] {E : Type u‚ÇÇ} [Category.{v‚ÇÇ} E]

/-- The functor between free groupoids induced by a functor between categories. -/
def map (œÜ : C ‚•§ D) : FreeGroupoid C ‚•§ FreeGroupoid D :=
  lift (œÜ ‚ãô of D)

lemma of_comp_map (F : C ‚•§ D) : of C ‚ãô map F = F ‚ãô of D := rfl

/-- The operation `of` is natural. -/
def ofCompMapIso (F : C ‚•§ D) : of C ‚ãô map F ‚âÖ F ‚ãô of D := Iso.refl _

variable (C) in
/-- The functor induced by the identity is the identity. -/
def mapId : map (ùü≠ C) ‚âÖ ùü≠ (FreeGroupoid C) :=
  liftNatIso _ _ (Iso.refl _)

@[simp]
lemma mapId_hom_app (X) : (mapId C).hom.app X = ùüô X :=
  liftNatIso_hom_app ..

@[simp]
lemma mapId_inv_app (X) : (mapId C).inv.app X = ùüô X :=
  liftNatIso_inv_app ..

variable (C) in
theorem map_id : map (ùü≠ C) = ùü≠ (FreeGroupoid C) := by
  symm; apply lift_unique; rfl

/-- The functor induced by a composition is the composition of the functors they induce. -/
def mapComp (œÜ : C ‚•§ D) (œÜ' : D ‚•§ E) : map (œÜ ‚ãô œÜ') ‚âÖ map œÜ ‚ãô map œÜ' :=
  liftNatIso _ _ (Iso.refl _)

@[simp]
lemma mapComp_hom_app (œÜ : C ‚•§ D) (œÜ' : D ‚•§ E) (X) : (mapComp œÜ œÜ').hom.app X = ùüô _ :=
  liftNatIso_hom_app ..

@[simp]
lemma mapComp_inv_app (œÜ : C ‚•§ D) (œÜ' : D ‚•§ E) (X) : (mapComp œÜ œÜ').inv.app X = ùüô _ :=
  liftNatIso_inv_app ..

theorem map_comp (œÜ : C ‚•§ D) (œÜ' : D ‚•§ E) : map (œÜ ‚ãô œÜ') = map œÜ ‚ãô map œÜ' := by
  symm; apply lift_unique; rfl

@[simp]
lemma map_obj_mk (œÜ : C ‚•§ D) (X : C) : (map œÜ).obj (mk X) = mk (œÜ.obj X) := rfl

@[simp]
lemma map_map_homMk (œÜ : C ‚•§ D) {X Y : C} (f : X ‚ü∂ Y) :
    (map œÜ).map (homMk f) = homMk (œÜ.map f) := rfl

variable {E : Type u‚ÇÇ} [Groupoid.{v‚ÇÇ} E]

lemma map_comp_lift (F : C ‚•§ D) (G : D ‚•§ E) : map F ‚ãô lift G = lift (F ‚ãô G) := by
  apply lift_unique
  rw [‚Üê Functor.assoc, of_comp_map, Functor.assoc, lift_spec G]

/-- The operation `lift` is natural. -/
def mapCompLift (F : C ‚•§ D) (G : D ‚•§ E) : map F ‚ãô lift G ‚âÖ lift (F ‚ãô G) :=
  liftNatIso _ _ (Iso.refl _)

@[simp]
lemma mapCompLift_hom_app (F : C ‚•§ D) (G : D ‚•§ E) (X) : (mapCompLift F G).hom.app X = ùüô _ :=
  liftNatIso_hom_app ..

@[simp]
lemma mapCompLift_inv_app (F : C ‚•§ D) (G : D ‚•§ E) (X) : (mapCompLift F G).inv.app X = ùüô _ :=
  liftNatIso_inv_app ..

end Functoriality

/-- Functors out of the free groupoid biject with functors out of the original category. -/
@[simps]
def functorEquiv {D : Type*} [Groupoid D] : (FreeGroupoid C ‚•§ D) ‚âÉ (C ‚•§ D) where
  toFun G := of C ‚ãô G
  invFun F := lift F
  right_inv := lift_spec
  left_inv _ := (lift_unique _ _ rfl).symm

end FreeGroupoid

namespace Grpd

open FreeGroupoid

/-- The free groupoid construction on a category as a functor. -/
def free : Cat.{u, u} ‚•§ Grpd.{u, u} where
  obj C := Grpd.of <| FreeGroupoid C
  map {C D} F := map F.toFunctor
  map_id C := by simp [map_id, id_eq_id]
  map_comp F G := by simp [Grpd.comp_eq_comp, map_comp]

@[simp]
lemma free_obj (C : Cat.{u, u}) : free.obj C = FreeGroupoid C :=
  rfl

@[simp]
lemma free_map {C D : Cat.{u, u}} (F : C ‚ü∂ D) : free.map F = map F.toFunctor :=
  rfl

/-- The free-forgetful adjunction between `Grpd` and `Cat`. -/
def freeForgetAdjunction : free ‚ä£ Grpd.forgetToCat :=
  Adjunction.mkOfHomEquiv
    { homEquiv _ _ := FreeGroupoid.functorEquiv.trans (Functor.equivCatHom _ _)
      homEquiv_naturality_left_symm _ _ := (FreeGroupoid.map_comp_lift _ _).symm
      homEquiv_naturality_right _ _ := rfl }

variable {C : Type u} [Category.{u} C] {D : Type u} [Groupoid.{u} D]

@[simp]
lemma freeForgetAdjunction_homEquiv_apply (F : FreeGroupoid C ‚•§ D) :
    (freeForgetAdjunction.homEquiv (Cat.of C) (Grpd.of D) F).toFunctor = FreeGroupoid.of C ‚ãô F :=
  rfl

@[simp]
lemma freeForgetAdjunction_homEquiv_symm_apply (F : C ‚•§ D) :
    (freeForgetAdjunction.homEquiv (Cat.of C) (Grpd.of D)).symm F.toCatHom = map F ‚ãô lift (ùü≠ D) :=
  rfl

@[simp]
lemma freeForgetAdjunction_unit_app :
    (freeForgetAdjunction.unit.app (Cat.of C)).toFunctor = FreeGroupoid.of C :=
  rfl

@[simp]
lemma freeForgetAdjunction_counit_app :
    freeForgetAdjunction.counit.app (Grpd.of D) = lift (ùü≠ D) :=
  rfl

instance : Reflective Grpd.forgetToCat where
  L := free
  adj := freeForgetAdjunction

end Grpd
end CategoryTheory
end
