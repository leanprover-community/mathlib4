/-
Copyright (c) 2025 Joseph Hua. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Hua
-/
import Mathlib.CategoryTheory.Groupoid.FreeGroupoid
import Mathlib.CategoryTheory.Category.Grpd
import Mathlib.CategoryTheory.Adjunction.Reflective
import Mathlib.CategoryTheory.Localization.Predicate

/-!
# Free groupoid on a category

This file defines the free groupoid on a category, the lifting of a functor to its unique
extension as a functor from the free groupoid, and proves uniqueness of this extension.

## Main results

Given a type `C` and a category instance on `C`:

- `CategoryTheory.Category.FreeGroupoid C`: the underlying type of the free groupoid on `C`.
- `CategoryTheory.Category.FreeGroupoid.instGroupoid`: the `Groupoid` instance on `FreeGroupoid C`.
- `CategoryTheory.Category.FreeGroupoid.lift`: the lifting of a functor `C ‚•§ G` where `G` is a
  groupoid, to a functor `CategoryTheory.Category.FreeGroupoid C ‚•§ G`.
- `CategoryTheory.Category.FreeGroupoid.lift_spec` and
  `CategoryTheory.Category.FreeGroupoid.lift_unique`:
  the proofs that, respectively, `CategoryTheory.Category.FreeGroupoid.lift` indeed is a lifting
  and is the unique one.
- `CategoryTheory.Category.Grpd.free`: the free functor from `Grpd` to `Cat`
- `CategoryTheory.Category.Grpd.freeForgetAdjunction`: that `free` is left adjoint to
  `Grpd.forgetToCat`.

## Implementation notes

The free groupoid on a category `C` is first defined by taking the free groupoid `G`
on the underlying *quiver* of `C`. Then the free groupoid on the *category* `C` is defined as
the quotient of `G` by the relation that makes the inclusion prefunctor `C ‚•§q G` a functor.

-/

noncomputable section

namespace CategoryTheory

universe v u v‚ÇÅ u‚ÇÅ v‚ÇÇ u‚ÇÇ

namespace Category

variable (C : Type u) [Category.{v} C]

open Quiver in
/-- The relation on the free groupoid on the underlying *quiver* of C that
promotes the prefunctor `C ‚•§q FreeGroupoid C` into a functor
`C ‚•§ Quotient (FreeGroupoid.homRel C)`. -/
inductive FreeGroupoid.homRel : HomRel (Quiver.FreeGroupoid C) where
| map_id (X : C) : homRel ((FreeGroupoid.of C).map (ùüô X)) (ùüô ((FreeGroupoid.of C).obj X))
| map_comp {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : homRel ((FreeGroupoid.of C).map (f ‚â´ g))
  ((FreeGroupoid.of C).map f ‚â´ (FreeGroupoid.of C).map g)

/-- The underlying type of the free groupoid on a category,
defined by quotienting the free groupoid on the underlying quiver of `C`
by the relation that promotes the prefunctor `C ‚•§q FreeGroupoid C` into a functor
`C ‚•§ Quotient (FreeGroupoid.homRel C)`. -/
protected def FreeGroupoid := Quotient (FreeGroupoid.homRel C)

instance [Nonempty C] : Nonempty (Category.FreeGroupoid C) :=
  ‚ü®Quotient.mk (Quotient.mk ((Paths.of _).obj (Classical.arbitrary C)))‚ü©

instance : Groupoid (Category.FreeGroupoid C) :=
  Quotient.groupoid (Category.FreeGroupoid.homRel C)

namespace FreeGroupoid

/-- The localization map from the category `C` to the groupoid `Category.FreeGroupoid C` -/
def of : C ‚•§ Category.FreeGroupoid C where
  __ := Quiver.FreeGroupoid.of C ‚ãôq (Quotient.functor (FreeGroupoid.homRel C)).toPrefunctor
  map_id X := Quotient.sound _ (Category.FreeGroupoid.homRel.map_id X)
  map_comp f g := Quotient.sound _ (Category.FreeGroupoid.homRel.map_comp f g)

variable {C}

lemma of_obj_bijective : Function.Bijective (of C).obj where
  left _ _ h := by cases h; rfl
  right X := ‚ü®X.as.as, rfl‚ü©

section UniversalProperty

variable {G : Type u‚ÇÅ} [Groupoid.{v‚ÇÅ} G]

/-- The lift of a functor from `C` to a groupoid to a functor from
`FreeGroupoid C` to the groupoid -/
def lift (œÜ : C ‚•§ G) : Category.FreeGroupoid C ‚•§ G :=
  Quotient.lift (FreeGroupoid.homRel C) (Quiver.FreeGroupoid.lift œÜ.toPrefunctor)
    (fun _ _ f g r ‚Ü¶ by
      have {X Y : C} (f : X ‚ü∂ Y) :=
        Prefunctor.congr_hom (Quiver.FreeGroupoid.lift_spec œÜ.toPrefunctor) f
      induction r <;> cat_disch)

theorem lift_spec (œÜ : C ‚•§ G) : of C ‚ãô lift œÜ = œÜ :=
  Functor.toPrefunctor_injective (by
    change Quiver.FreeGroupoid.of C ‚ãôq
      (Quotient.functor (FreeGroupoid.homRel C)).toPrefunctor ‚ãôq
        (lift œÜ).toPrefunctor = œÜ.toPrefunctor
    simp [lift, Quotient.lift_spec, Quiver.FreeGroupoid.lift_spec])

theorem lift_unique (œÜ : C ‚•§ G) (Œ¶ : Category.FreeGroupoid C ‚•§ G) (hŒ¶ : of C ‚ãô Œ¶ = œÜ) :
    Œ¶ = lift œÜ := by
  apply Quotient.lift_unique
  apply Quiver.FreeGroupoid.lift_unique
  exact congr_arg Functor.toPrefunctor hŒ¶

theorem lift_comp {H : Type u‚ÇÇ} [Groupoid.{v‚ÇÇ} H] (œÜ : C ‚•§ G) (œà : G ‚•§ H) :
    lift (œÜ ‚ãô œà) = lift œÜ ‚ãô œà := by
  symm
  apply lift_unique
  rw [‚Üê Functor.assoc, lift_spec]

/-- The universal property of the free groupoid. -/
def strictUniversalPropertyFixedTarget :
    Localization.StrictUniversalPropertyFixedTarget (of C) ‚ä§ G where
  inverts _ := inferInstance
  lift F _ := lift F
  fac _ _ := lift_spec ..
  uniq F G h := by rw [lift_unique (of C ‚ãô G) F h, ‚Üê lift_unique (of C ‚ãô G) G rfl]

attribute [local instance] Localization.groupoid

instance : (of C).IsLocalization ‚ä§ :=
  .mk' _ _ strictUniversalPropertyFixedTarget strictUniversalPropertyFixedTarget

end UniversalProperty

section Functoriality

variable {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D] {E : Type u‚ÇÇ} [Category.{v‚ÇÇ} E]

/-- The functor of free groupoid induced by a functor between the original categories. -/
def map (œÜ : C ‚•§ D) : Category.FreeGroupoid C ‚•§ Category.FreeGroupoid D :=
  lift (œÜ ‚ãô of D)

variable (C) in
theorem map_id : map (ùü≠ C) = ùü≠ (Category.FreeGroupoid C) := by
  symm; apply lift_unique; rfl

variable (C) in
/-- The functor induced by the identity is the identity. -/
def mapId : map (ùü≠ C) ‚âÖ ùü≠ (Category.FreeGroupoid C) :=
  eqToIso (map_id C)

theorem map_comp (œÜ : C ‚•§ D) (œÜ' : D ‚•§ E) : map (œÜ ‚ãô œÜ') = map œÜ ‚ãô map œÜ' := by
  symm; apply lift_unique; rfl

/-- The functor induced by a composition is the composition of the functors they induce. -/
def mapComp (œÜ : C ‚•§ D) (œÜ' : D ‚•§ E) : map (œÜ ‚ãô œÜ') ‚âÖ map œÜ ‚ãô map œÜ':=
  eqToIso (map_comp œÜ œÜ')

lemma of_map (F : C ‚•§ D) : of C ‚ãô map F = F ‚ãô of D := rfl

/-- The operation `of` is natural. -/
def ofMap (F : C ‚•§ D) : of C ‚ãô map F ‚âÖ F ‚ãô of D := Iso.refl _

lemma map_lift {E : Type u‚ÇÇ} [Groupoid.{v‚ÇÇ} E] (F : C ‚•§ D) (G : D ‚•§ E) :
  map F ‚ãô lift G = lift (F ‚ãô G) := by
    apply lift_unique
    rw [‚Üê Functor.assoc, of_map, Functor.assoc, lift_spec G]

/-- The operation `lift` is natural. -/
def mapLift {E : Type u‚ÇÇ} [Groupoid.{v‚ÇÇ} E] (F : C ‚•§ D) (G : D ‚•§ E) :
  map F ‚ãô lift G ‚âÖ lift (F ‚ãô G) := eqToIso (map_lift F G)

end Functoriality

/-- Functors out of the free groupoid biject with functors out of the original category. -/
@[simps]
def functorEquiv {D : Type*} [Groupoid D] : (Category.FreeGroupoid C ‚•§ D) ‚âÉ (C ‚•§ D) where
  toFun G := of C ‚ãô G
  invFun := lift
  right_inv := lift_spec
  left_inv _ := (lift_unique _ _ rfl).symm

end FreeGroupoid

end Category

namespace Grpd

open Category.FreeGroupoid

/-- The free groupoid construction on a category as a functor. -/
@[simps]
def free : Cat.{u, u} ‚•§ Grpd.{u, u} where
  obj C := Grpd.of <| Category.FreeGroupoid C
  map {C D} F := map F
  map_id C := by simp [Grpd.id_eq_id, ‚Üê map_id]; rfl
  map_comp F G := by simp [Grpd.comp_eq_comp, ‚Üê map_comp]; rfl

/-- The free-forgetful adjunction between `Grpd` and `Cat`. -/
def freeForgetAdjunction : free ‚ä£ Grpd.forgetToCat :=
  Adjunction.mkOfHomEquiv
    { homEquiv _ _ := Category.FreeGroupoid.functorEquiv
      homEquiv_naturality_left_symm _ _ := (Category.FreeGroupoid.map_lift _ _).symm
      homEquiv_naturality_right _ _ := rfl }

instance : Reflective Grpd.forgetToCat where
  L := free
  adj := freeForgetAdjunction

end Grpd
end CategoryTheory
end
