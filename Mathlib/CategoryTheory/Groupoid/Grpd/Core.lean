/-
Copyright (c) 2019 Joseph Hua. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Hua
-/
import Mathlib.CategoryTheory.Category.Grpd
import Mathlib.CategoryTheory.Core
import Mathlib.CategoryTheory.Monad.Limits

/-!
# The forgetful-core adjunction

Taking the core of a category defines a functor `Cat ‚•§ Grpd`. This functor is left adjoint
to the forgetful functor from `Grpd ‚•§ Cat`.

# Main definitions

* `CategoryTheory.Core.functor`: the functor `Cat ‚•§ Grpd` that on objects takes
  the core of a category
* `CategoryTheory.Core.adjunction`: the adjunction with the forgetful functor
  `Grpd.forgetToCat` on the left, and the core functor `Core.functor` on the right.
* An instance that `Grpd.forgetToCat` is coreflective.

-/

universe w v u v‚ÇÅ u‚ÇÅ v‚ÇÇ u‚ÇÇ v‚ÇÉ u‚ÇÉ

noncomputable section

namespace CategoryTheory
namespace Core

variable {C : Type u} [Category.{v} C] {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D]

/-- The functor from `Cat` to `Grpd` that takes the core of a category, on objects. -/
def functor : Cat.{v,u} ‚•§ Grpd.{v,u} where
  obj C := Grpd.of (Core C)
  map F := F.core

/-- The adjunction between the forgetful functor from `Grpd` to `Cat` and the core
functor from `Cat` to `Grpd`. -/
def adjunction : Grpd.forgetToCat ‚ä£ functor where
  unit := {
    app G := functorToCore (ùü≠ _)
    naturality _ _ F := by
      simp [functor, Grpd.comp_eq_comp, ‚Üê functorToCore_comp_left, ‚Üê functorToCore_comp_right,
        Functor.id_comp, Functor.comp_id, Grpd.forgetToCat]}
  counit := {app C := inclusion C}

end Core

namespace Grpd

open Functor Core

instance : IsLeftAdjoint Grpd.forgetToCat :=
  IsLeftAdjoint.mk ‚ü® functor , ‚ü® adjunction ‚ü© ‚ü©

instance : IsRightAdjoint functor :=
  IsRightAdjoint.mk ‚ü® Grpd.forgetToCat , ‚ü® adjunction ‚ü© ‚ü©

instance : Coreflective Grpd.forgetToCat where
  R := functor
  adj := adjunction

end Grpd

end CategoryTheory
