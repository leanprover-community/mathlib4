/-
Copyright (c) 2022 R√©mi Bottinelli. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©mi Bottinelli
-/
import Mathlib.Algebra.Group.Basic
import Mathlib.Algebra.Group.Equiv.Defs
import Mathlib.CategoryTheory.Groupoid
import Mathlib.CategoryTheory.PathCategory.Basic
import Mathlib.Combinatorics.Quiver.Path

/-!
# Vertex group

This file defines the vertex group (*aka* isotropy group) of a groupoid at a vertex.

## Implementation notes

* The instance is defined "manually", instead of relying on `CategoryTheory.Aut.group` or
  using `CategoryTheory.inv`.
* The multiplication order therefore matches the categorical one: `x * y = x ‚â´ y`.
* The inverse is directly defined in terms of the groupoidal inverse: `x ‚Åª¬π = Groupoid.inv x`.

## Tags

isotropy, vertex group, groupoid
-/


namespace CategoryTheory

namespace Groupoid

universe u v

variable {C : Type u} [Groupoid C]

/-- The vertex group at `c`. -/
@[simps mul one inv]
instance vertexGroup (c : C) : Group (c ‚ü∂ c) where
  mul := fun x y : c ‚ü∂ c ‚Ü¶ x ‚â´ y
  mul_assoc := Category.assoc
  one := ùüô c
  one_mul := Category.id_comp
  mul_one := Category.comp_id
  inv := Groupoid.inv
  inv_mul_cancel := inv_comp

/-- The inverse in the group is equal to the inverse given by `CategoryTheory.inv`. -/
theorem vertexGroup.inv_eq_inv (c : C) (Œ≥ : c ‚ü∂ c) : Œ≥‚Åª¬π = CategoryTheory.inv Œ≥ :=
  Groupoid.inv_eq_inv Œ≥

/-- An arrow in the groupoid defines, by conjugation, an isomorphism of groups between
its endpoints.
-/
@[simps]
def vertexGroupIsomOfMap {c d : C} (f : c ‚ü∂ d) : (c ‚ü∂ c) ‚âÉ* (d ‚ü∂ d) where
  toFun Œ≥ := inv f ‚â´ Œ≥ ‚â´ f
  invFun Œ¥ := f ‚â´ Œ¥ ‚â´ inv f
  left_inv Œ≥ := by
    simp_rw [Category.assoc, comp_inv, Category.comp_id, ‚Üê Category.assoc, comp_inv,
      Category.id_comp]
  right_inv Œ¥ := by
    simp_rw [Category.assoc, inv_comp, ‚Üê Category.assoc, inv_comp, Category.id_comp,
      Category.comp_id]
  map_mul' Œ≥‚ÇÅ Œ≥‚ÇÇ := by
    simp only [vertexGroup_mul, inv_eq_inv, Category.assoc, IsIso.hom_inv_id_assoc]

/-- A path in the groupoid defines an isomorphism between its endpoints.
-/
def vertexGroupIsomOfPath {c d : C} (p : Quiver.Path c d) : (c ‚ü∂ c) ‚âÉ* (d ‚ü∂ d) :=
  vertexGroupIsomOfMap (composePath p)

/-- A functor defines a morphism of vertex group. -/
@[simps]
def CategoryTheory.Functor.mapVertexGroup {D : Type v} [Groupoid D] (œÜ : C ‚•§ D) (c : C) :
    (c ‚ü∂ c) ‚Üí* (œÜ.obj c ‚ü∂ œÜ.obj c) where
  toFun := œÜ.map
  map_one' := œÜ.map_id c
  map_mul' := œÜ.map_comp

end Groupoid

end CategoryTheory
