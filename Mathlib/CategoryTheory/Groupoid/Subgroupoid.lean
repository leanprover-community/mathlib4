/-
Copyright (c) 2022 RÃ©mi Bottinelli. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©mi Bottinelli, Junyan Xu
-/
import Mathlib.Algebra.Group.Subgroup.Defs
import Mathlib.CategoryTheory.Groupoid.VertexGroup
import Mathlib.CategoryTheory.Groupoid.Basic
import Mathlib.CategoryTheory.Groupoid
import Mathlib.Data.Set.Lattice

/-!
# Subgroupoid

This file defines subgroupoids as `structure`s containing the subsets of arrows and their
stability under composition and inversion.
Also defined are:

* containment of subgroupoids is a complete lattice;
* images and preimages of subgroupoids under a functor;
* the notion of normality of subgroupoids and its stability under intersection and preimage;
* compatibility of the above with `CategoryTheory.Groupoid.vertexGroup`.


## Main definitions

Given a type `C` with associated `groupoid C` instance.

* `CategoryTheory.Subgroupoid C` is the type of subgroupoids of `C`
* `CategoryTheory.Subgroupoid.IsNormal` is the property that the subgroupoid is stable under
  conjugation by arbitrary arrows, _and_ that all identity arrows are contained in the subgroupoid.
* `CategoryTheory.Subgroupoid.comap` is the "preimage" map of subgroupoids along a functor.
* `CategoryTheory.Subgroupoid.map` is the "image" map of subgroupoids along a functor _injective on
  objects_.
* `CategoryTheory.Subgroupoid.vertexSubgroup` is the subgroup of the *vertex group* at a given
  vertex `v`, assuming `v` is contained in the `CategoryTheory.Subgroupoid` (meaning, by definition,
  that the arrow `ğŸ™ v` is contained in the subgroupoid).

## Implementation details

The structure of this file is copied from/inspired by `Mathlib/GroupTheory/Subgroup/Basic.lean`
and `Mathlib/Combinatorics/SimpleGraph/Subgraph.lean`.

## TODO

* Equivalent inductive characterization of generated (normal) subgroupoids.
* Characterization of normal subgroupoids as kernels.
* Prove that `CategoryTheory.Subgroupoid.full` and `CategoryTheory.Subgroupoid.disconnect` preserve
  intersections (and `CategoryTheory.Subgroupoid.disconnect` also unions)

## Tags

category theory, groupoid, subgroupoid
-/


namespace CategoryTheory

open Set Groupoid

universe u v

variable {C : Type u} [Groupoid C]

/-- A sugroupoid of `C` consists of a choice of arrows for each pair of vertices, closed
under composition and inverses.
-/
@[ext]
structure Subgroupoid (C : Type u) [Groupoid C] where
  /-- The arrow choice for each pair of vertices -/
  arrows : âˆ€ c d : C, Set (c âŸ¶ d)
  protected inv : âˆ€ {c d} {p : c âŸ¶ d}, p âˆˆ arrows c d â†’ Groupoid.inv p âˆˆ arrows d c
  protected mul : âˆ€ {c d e} {p}, p âˆˆ arrows c d â†’ âˆ€ {q}, q âˆˆ arrows d e â†’ p â‰« q âˆˆ arrows c e

namespace Subgroupoid

variable (S : Subgroupoid C)

theorem inv_mem_iff {c d : C} (f : c âŸ¶ d) :
    Groupoid.inv f âˆˆ S.arrows d c â†” f âˆˆ S.arrows c d := by
  constructor
  Â· intro h
    simpa only [inv_eq_inv, IsIso.inv_inv] using S.inv h
  Â· apply S.inv

theorem mul_mem_cancel_left {c d e : C} {f : c âŸ¶ d} {g : d âŸ¶ e} (hf : f âˆˆ S.arrows c d) :
    f â‰« g âˆˆ S.arrows c e â†” g âˆˆ S.arrows d e := by
  constructor
  Â· rintro h
    suffices Groupoid.inv f â‰« f â‰« g âˆˆ S.arrows d e by
      simpa only [inv_eq_inv, IsIso.inv_hom_id_assoc] using this
    apply S.mul (S.inv hf) h
  Â· apply S.mul hf

theorem mul_mem_cancel_right {c d e : C} {f : c âŸ¶ d} {g : d âŸ¶ e} (hg : g âˆˆ S.arrows d e) :
    f â‰« g âˆˆ S.arrows c e â†” f âˆˆ S.arrows c d := by
  constructor
  Â· rintro h
    suffices (f â‰« g) â‰« Groupoid.inv g âˆˆ S.arrows c d by
      simpa only [inv_eq_inv, IsIso.hom_inv_id, Category.comp_id, Category.assoc] using this
    apply S.mul h (S.inv hg)
  Â· exact fun hf => S.mul hf hg

/-- The vertices of `C` on which `S` has non-trivial isotropy -/
def objs : Set C :=
  {c : C | (S.arrows c c).Nonempty}

theorem mem_objs_of_src {c d : C} {f : c âŸ¶ d} (h : f âˆˆ S.arrows c d) : c âˆˆ S.objs :=
  âŸ¨f â‰« Groupoid.inv f, S.mul h (S.inv h)âŸ©

theorem mem_objs_of_tgt {c d : C} {f : c âŸ¶ d} (h : f âˆˆ S.arrows c d) : d âˆˆ S.objs :=
  âŸ¨Groupoid.inv f â‰« f, S.mul (S.inv h) hâŸ©

theorem id_mem_of_nonempty_isotropy (c : C) : c âˆˆ objs S â†’ ğŸ™ c âˆˆ S.arrows c c := by
  rintro âŸ¨Î³, hÎ³âŸ©
  convert S.mul hÎ³ (S.inv hÎ³)
  simp only [inv_eq_inv, IsIso.hom_inv_id]

theorem id_mem_of_src {c d : C} {f : c âŸ¶ d} (h : f âˆˆ S.arrows c d) : ğŸ™ c âˆˆ S.arrows c c :=
  id_mem_of_nonempty_isotropy S c (mem_objs_of_src S h)

theorem id_mem_of_tgt {c d : C} {f : c âŸ¶ d} (h : f âˆˆ S.arrows c d) : ğŸ™ d âˆˆ S.arrows d d :=
  id_mem_of_nonempty_isotropy S d (mem_objs_of_tgt S h)

/-- A subgroupoid seen as a quiver on vertex set `C` -/
def asWideQuiver : Quiver C :=
  âŸ¨fun c d => Subtype <| S.arrows c dâŸ©

/-- The coercion of a subgroupoid as a groupoid -/
@[simps comp_coe, simps -isSimp inv_coe]
instance coe : Groupoid S.objs where
  Hom a b := S.arrows a.val b.val
  id a := âŸ¨ğŸ™ a.val, id_mem_of_nonempty_isotropy S a.val a.propâŸ©
  comp p q := âŸ¨p.val â‰« q.val, S.mul p.prop q.propâŸ©
  inv p := âŸ¨Groupoid.inv p.val, S.inv p.propâŸ©

@[simp]
theorem coe_inv_coe' {c d : S.objs} (p : c âŸ¶ d) :
    (CategoryTheory.inv p).val = CategoryTheory.inv p.val := by
  simp only [â† inv_eq_inv, coe_inv_coe]

/-- The embedding of the coerced subgroupoid to its parent -/
def hom : S.objs â¥¤ C where
  obj c := c.val
  map f := f.val
  map_id _ := rfl
  map_comp _ _ := rfl

theorem hom.inj_on_objects : Function.Injective (hom S).obj := by
  rintro âŸ¨c, hcâŸ© âŸ¨d, hdâŸ© hcd
  simp only [Subtype.mk_eq_mk]; exact hcd

theorem hom.faithful : âˆ€ c d, Function.Injective fun f : c âŸ¶ d => (hom S).map f := by
  rintro âŸ¨c, hcâŸ© âŸ¨d, hdâŸ© âŸ¨f, hfâŸ© âŸ¨g, hgâŸ© hfg; exact Subtype.ext hfg

/-- The subgroup of the vertex group at `c` given by the subgroupoid -/
def vertexSubgroup {c : C} (hc : c âˆˆ S.objs) : Subgroup (c âŸ¶ c) where
  carrier := S.arrows c c
  mul_mem' hf hg := S.mul hf hg
  one_mem' := id_mem_of_nonempty_isotropy _ _ hc
  inv_mem' hf := S.inv hf

/-- The set of all arrows of a subgroupoid, as a set in `Î£ c d : C, c âŸ¶ d`. -/
@[coe] def toSet (S : Subgroupoid C) : Set (Î£ c d : C, c âŸ¶ d) :=
  {F | F.2.2 âˆˆ S.arrows F.1 F.2.1}

instance : SetLike (Subgroupoid C) (Î£ c d : C, c âŸ¶ d) where
  coe := toSet
  coe_injective' := fun âŸ¨S, _, _âŸ© âŸ¨T, _, _âŸ© h => by ext c d f; apply Set.ext_iff.1 h âŸ¨c, d, fâŸ©

theorem mem_iff (S : Subgroupoid C) (F : Î£ c d, c âŸ¶ d) : F âˆˆ S â†” F.2.2 âˆˆ S.arrows F.1 F.2.1 :=
  Iff.rfl

theorem le_iff (S T : Subgroupoid C) : S â‰¤ T â†” âˆ€ {c d}, S.arrows c d âŠ† T.arrows c d := by
  rw [SetLike.le_def, Sigma.forall]; exact forall_congr' fun c => Sigma.forall

instance : Top (Subgroupoid C) :=
  âŸ¨{  arrows := fun _ _ => Set.univ
      mul := by intros; trivial
      inv := by intros; trivial }âŸ©

theorem mem_top {c d : C} (f : c âŸ¶ d) : f âˆˆ (âŠ¤ : Subgroupoid C).arrows c d :=
  trivial

theorem mem_top_objs (c : C) : c âˆˆ (âŠ¤ : Subgroupoid C).objs := by
  dsimp [Top.top, objs]
  simp only [univ_nonempty]

instance : Bot (Subgroupoid C) :=
  âŸ¨{  arrows := fun _ _ => âˆ…
      mul := False.elim
      inv := False.elim }âŸ©

instance : Inhabited (Subgroupoid C) :=
  âŸ¨âŠ¤âŸ©

instance : Min (Subgroupoid C) :=
  âŸ¨fun S T =>
    { arrows := fun c d => S.arrows c d âˆ© T.arrows c d
      inv := fun hp â†¦ âŸ¨S.inv hp.1, T.inv hp.2âŸ©
      mul := fun hp _ hq â†¦ âŸ¨S.mul hp.1 hq.1, T.mul hp.2 hq.2âŸ© }âŸ©

instance : InfSet (Subgroupoid C) :=
  âŸ¨fun s =>
    { arrows := fun c d => â‹‚ S âˆˆ s, Subgroupoid.arrows S c d
      inv := fun hp â†¦ by rw [mem_iInterâ‚‚] at hp âŠ¢; exact fun S hS => S.inv (hp S hS)
      mul := fun hp _ hq â†¦ by
        rw [mem_iInterâ‚‚] at hp hq âŠ¢
        exact fun S hS => S.mul (hp S hS) (hq S hS) }âŸ©

theorem mem_sInf_arrows {s : Set (Subgroupoid C)} {c d : C} {p : c âŸ¶ d} :
    p âˆˆ (sInf s).arrows c d â†” âˆ€ S âˆˆ s, p âˆˆ S.arrows c d :=
  mem_iInterâ‚‚

theorem mem_sInf {s : Set (Subgroupoid C)} {p : Î£ c d : C, c âŸ¶ d} :
    p âˆˆ sInf s â†” âˆ€ S âˆˆ s, p âˆˆ S :=
  mem_sInf_arrows

instance : CompleteLattice (Subgroupoid C) :=
  { completeLatticeOfInf (Subgroupoid C) (by
      refine fun s => âŸ¨fun S Ss F => ?_, fun T Tl F fT => ?_âŸ© <;> simp only [mem_sInf]
      exacts [fun hp => hp S Ss, fun S Ss => Tl Ss fT]) with
    bot := âŠ¥
    bot_le := fun _ => empty_subset _
    top := âŠ¤
    le_top := fun _ => subset_univ _
    inf := (Â· âŠ“ Â·)
    le_inf := fun _ _ _ RS RT _ pR => âŸ¨RS pR, RT pRâŸ©
    inf_le_left := fun _ _ _ => And.left
    inf_le_right := fun _ _ _ => And.right }

theorem le_objs {S T : Subgroupoid C} (h : S â‰¤ T) : S.objs âŠ† T.objs := fun s âŸ¨Î³, hÎ³âŸ© =>
  âŸ¨Î³, @h âŸ¨s, s, Î³âŸ© hÎ³âŸ©

/-- The functor associated to the embedding of subgroupoids -/
def inclusion {S T : Subgroupoid C} (h : S â‰¤ T) : S.objs â¥¤ T.objs where
  obj s := âŸ¨s.val, le_objs h s.propâŸ©
  map f := âŸ¨f.val, @h âŸ¨_, _, f.valâŸ© f.propâŸ©
  map_id _ := rfl
  map_comp _ _ := rfl

theorem inclusion_inj_on_objects {S T : Subgroupoid C} (h : S â‰¤ T) :
    Function.Injective (inclusion h).obj := fun âŸ¨s, hsâŸ© âŸ¨t, htâŸ© => by
  simpa only [inclusion, Subtype.mk_eq_mk] using id

theorem inclusion_faithful {S T : Subgroupoid C} (h : S â‰¤ T) (s t : S.objs) :
    Function.Injective fun f : s âŸ¶ t => (inclusion h).map f := fun âŸ¨f, hfâŸ© âŸ¨g, hgâŸ© => by
  -- Porting note: was `...; simpa only [Subtype.mk_eq_mk] using id`
  dsimp only [inclusion]; rw [Subtype.mk_eq_mk, Subtype.mk_eq_mk]; exact id

theorem inclusion_refl {S : Subgroupoid C} : inclusion (le_refl S) = ğŸ­ S.objs :=
  Functor.hext (fun _ => rfl) fun _ _ _ => HEq.refl _

theorem inclusion_trans {R S T : Subgroupoid C} (k : R â‰¤ S) (h : S â‰¤ T) :
    inclusion (k.trans h) = inclusion k â‹™ inclusion h :=
  rfl

theorem inclusion_comp_embedding {S T : Subgroupoid C} (h : S â‰¤ T) : inclusion h â‹™ T.hom = S.hom :=
  rfl

/-- The family of arrows of the discrete groupoid -/
inductive Discrete.Arrows : âˆ€ c d : C, (c âŸ¶ d) â†’ Prop
  | id (c : C) : Discrete.Arrows c c (ğŸ™ c)

/-- The only arrows of the discrete groupoid are the identity arrows. -/
def discrete : Subgroupoid C where
  arrows c d := {p | Discrete.Arrows c d p}
  inv := by rintro _ _ _ âŸ¨âŸ©; simp only [inv_eq_inv, IsIso.inv_id]; constructor
  mul := by rintro _ _ _ _ âŸ¨âŸ© _ âŸ¨âŸ©; rw [Category.comp_id]; constructor

theorem mem_discrete_iff {c d : C} (f : c âŸ¶ d) :
    f âˆˆ discrete.arrows c d â†” âˆƒ h : c = d, f = eqToHom h :=
  âŸ¨by rintro âŸ¨âŸ©; exact âŸ¨rfl, rflâŸ©, by rintro âŸ¨rfl, rflâŸ©; constructorâŸ©

/-- A subgroupoid is wide if its carrier set is all of `C`. -/
structure IsWide : Prop where
  wide : âˆ€ c, ğŸ™ c âˆˆ S.arrows c c

theorem isWide_iff_objs_eq_univ : S.IsWide â†” S.objs = Set.univ := by
  constructor
  Â· rintro h
    ext x; constructor <;> simp only [mem_univ, imp_true_iff, forall_true_left]
    apply mem_objs_of_src S (h.wide x)
  Â· rintro h
    refine âŸ¨fun c => ?_âŸ©
    obtain âŸ¨Î³, Î³SâŸ© := (le_of_eq h.symm : âŠ¤ âŠ† S.objs) (Set.mem_univ c)
    exact id_mem_of_src S Î³S

theorem IsWide.id_mem {S : Subgroupoid C} (Sw : S.IsWide) (c : C) : ğŸ™ c âˆˆ S.arrows c c :=
  Sw.wide c

theorem IsWide.eqToHom_mem {S : Subgroupoid C} (Sw : S.IsWide) {c d : C} (h : c = d) :
    eqToHom h âˆˆ S.arrows c d := by cases h; simp only [eqToHom_refl]; apply Sw.id_mem c

/-- A subgroupoid is normal if it is wide and satisfies the expected stability under conjugacy. -/
structure IsNormal : Prop extends IsWide S where
  conj : âˆ€ {c d} (p : c âŸ¶ d) {Î³ : c âŸ¶ c}, Î³ âˆˆ S.arrows c c â†’ Groupoid.inv p â‰« Î³ â‰« p âˆˆ S.arrows d d

theorem IsNormal.conj' {S : Subgroupoid C} (Sn : IsNormal S) :
    âˆ€ {c d} (p : d âŸ¶ c) {Î³ : c âŸ¶ c}, Î³ âˆˆ S.arrows c c â†’ p â‰« Î³ â‰« Groupoid.inv p âˆˆ S.arrows d d :=
  fun p Î³ hs => by convert Sn.conj (Groupoid.inv p) hs; simp

theorem IsNormal.conjugation_bij (Sn : IsNormal S) {c d} (p : c âŸ¶ d) :
    Set.BijOn (fun Î³ : c âŸ¶ c => Groupoid.inv p â‰« Î³ â‰« p) (S.arrows c c) (S.arrows d d) := by
  refine âŸ¨fun Î³ Î³S => Sn.conj p Î³S, fun Î³â‚ _ Î³â‚‚ _ h => ?_, fun Î´ Î´S =>
    âŸ¨p â‰« Î´ â‰« Groupoid.inv p, Sn.conj' p Î´S, ?_âŸ©âŸ©
  Â· simpa only [inv_eq_inv, Category.assoc, IsIso.hom_inv_id, Category.comp_id,
      IsIso.hom_inv_id_assoc] using p â‰«= h =â‰« inv p
  Â· simp only [inv_eq_inv, Category.assoc, IsIso.inv_hom_id, Category.comp_id,
      IsIso.inv_hom_id_assoc]

theorem top_isNormal : IsNormal (âŠ¤ : Subgroupoid C) :=
  { wide := fun _ => trivial
    conj := fun _ _ _ => trivial }

theorem sInf_isNormal (s : Set <| Subgroupoid C) (sn : âˆ€ S âˆˆ s, IsNormal S) : IsNormal (sInf s) :=
  { wide := by simp_rw [sInf, mem_iInterâ‚‚]; exact fun c S Ss => (sn S Ss).wide c
    conj := by simp_rw [sInf, mem_iInterâ‚‚]; exact fun p Î³ hÎ³ S Ss => (sn S Ss).conj p (hÎ³ S Ss) }

theorem discrete_isNormal : (@discrete C _).IsNormal :=
  { wide := fun c => by constructor
    conj := fun f Î³ hÎ³ => by
      cases hÎ³
      simp only [inv_eq_inv, Category.id_comp, IsIso.inv_hom_id]; constructor }

theorem IsNormal.vertexSubgroup (Sn : IsNormal S) (c : C) (cS : c âˆˆ S.objs) :
    (S.vertexSubgroup cS).Normal where
  conj_mem x hx y := by rw [mul_assoc]; exact Sn.conj' y hx

section GeneratedSubgroupoid

-- TODO: proof that generated is just "words in X" and generatedNormal is similarly
variable (X : âˆ€ c d : C, Set (c âŸ¶ d))

/-- The subgroupoid generated by the set of arrows `X` -/
def generated : Subgroupoid C :=
  sInf {S : Subgroupoid C | âˆ€ c d, X c d âŠ† S.arrows c d}

theorem subset_generated (c d : C) : X c d âŠ† (generated X).arrows c d := by
  dsimp only [generated, sInf]
  simp only [subset_iInterâ‚‚_iff]
  exact fun S hS f fS => hS _ _ fS

/-- The normal sugroupoid generated by the set of arrows `X` -/
def generatedNormal : Subgroupoid C :=
  sInf {S : Subgroupoid C | (âˆ€ c d, X c d âŠ† S.arrows c d) âˆ§ S.IsNormal}

theorem generated_le_generatedNormal : generated X â‰¤ generatedNormal X := by
  apply @sInf_le_sInf (Subgroupoid C) _
  exact fun S âŸ¨h, _âŸ© => h

theorem generatedNormal_isNormal : (generatedNormal X).IsNormal :=
  sInf_isNormal _ fun _ h => h.right

theorem IsNormal.generatedNormal_le {S : Subgroupoid C} (Sn : S.IsNormal) :
    generatedNormal X â‰¤ S â†” âˆ€ c d, X c d âŠ† S.arrows c d := by
  constructor
  Â· rintro h c d
    have h' := generated_le_generatedNormal X
    rw [le_iff] at h h'
    exact ((subset_generated X c d).trans (@h' c d)).trans (@h c d)
  Â· rintro h
    apply @sInf_le (Subgroupoid C) _
    exact âŸ¨h, SnâŸ©

end GeneratedSubgroupoid

section Hom

variable {D : Type*} [Groupoid D] (Ï† : C â¥¤ D)

/-- A functor between groupoid defines a map of subgroupoids in the reverse direction
by taking preimages.
-/
def comap (S : Subgroupoid D) : Subgroupoid C where
  arrows c d := {f : c âŸ¶ d | Ï†.map f âˆˆ S.arrows (Ï†.obj c) (Ï†.obj d)}
  inv hp := by rw [mem_setOf, inv_eq_inv, Ï†.map_inv, â† inv_eq_inv]; exact S.inv hp
  mul := by
    intros
    simp only [mem_setOf, Functor.map_comp]
    apply S.mul <;> assumption

theorem comap_mono (S T : Subgroupoid D) : S â‰¤ T â†’ comap Ï† S â‰¤ comap Ï† T := fun ST _ =>
  @ST âŸ¨_, _, _âŸ©

theorem isNormal_comap {S : Subgroupoid D} (Sn : IsNormal S) : IsNormal (comap Ï† S) where
  wide c := by rw [comap, mem_setOf, Functor.map_id]; apply Sn.wide
  conj f Î³ hÎ³ := by
    simp_rw [inv_eq_inv f, comap, mem_setOf, Functor.map_comp, Functor.map_inv, â† inv_eq_inv]
    exact Sn.conj _ hÎ³

@[simp]
theorem comap_comp {E : Type*} [Groupoid E] (Ïˆ : D â¥¤ E) : comap (Ï† â‹™ Ïˆ) = comap Ï† âˆ˜ comap Ïˆ :=
  rfl

/-- The kernel of a functor between subgroupoid is the preimage. -/
def ker : Subgroupoid C :=
  comap Ï† discrete

theorem mem_ker_iff {c d : C} (f : c âŸ¶ d) :
    f âˆˆ (ker Ï†).arrows c d â†” âˆƒ h : Ï†.obj c = Ï†.obj d, Ï†.map f = eqToHom h :=
  mem_discrete_iff (Ï†.map f)

theorem ker_isNormal : (ker Ï†).IsNormal :=
  isNormal_comap Ï† discrete_isNormal

@[simp]
theorem ker_comp {E : Type*} [Groupoid E] (Ïˆ : D â¥¤ E) : ker (Ï† â‹™ Ïˆ) = comap Ï† (ker Ïˆ) :=
  rfl

/-- The family of arrows of the image of a subgroupoid under a functor injective on objects -/
inductive Map.Arrows (hÏ† : Function.Injective Ï†.obj) (S : Subgroupoid C) : âˆ€ c d : D, (c âŸ¶ d) â†’ Prop
  | im {c d : C} (f : c âŸ¶ d) (hf : f âˆˆ S.arrows c d) : Map.Arrows hÏ† S (Ï†.obj c) (Ï†.obj d) (Ï†.map f)

theorem Map.arrows_iff (hÏ† : Function.Injective Ï†.obj) (S : Subgroupoid C) {c d : D} (f : c âŸ¶ d) :
    Map.Arrows Ï† hÏ† S c d f â†”
      âˆƒ (a b : C) (g : a âŸ¶ b) (ha : Ï†.obj a = c) (hb : Ï†.obj b = d) (_hg : g âˆˆ S.arrows a b),
        f = eqToHom ha.symm â‰« Ï†.map g â‰« eqToHom hb := by
  constructor
  Â· rintro âŸ¨g, hgâŸ©; exact âŸ¨_, _, g, rfl, rfl, hg, eq_conj_eqToHom _âŸ©
  Â· rintro âŸ¨a, b, g, rfl, rfl, hg, rflâŸ©; rw [â† eq_conj_eqToHom]; constructor; exact hg

/-- The "forward" image of a subgroupoid under a functor injective on objects -/
def map (hÏ† : Function.Injective Ï†.obj) (S : Subgroupoid C) : Subgroupoid D where
  arrows c d := {x | Map.Arrows Ï† hÏ† S c d x}
  inv := by
    rintro _ _ _ âŸ¨âŸ©
    rw [inv_eq_inv, â† Functor.map_inv, â† inv_eq_inv]
    constructor; apply S.inv; assumption
  mul := by
    rintro _ _ _ _ âŸ¨f, hfâŸ© q hq
    obtain âŸ¨câ‚ƒ, câ‚„, g, he, rfl, hg, gqâŸ© := (Map.arrows_iff Ï† hÏ† S q).mp hq
    cases hÏ† he; rw [gq, â† eq_conj_eqToHom, â† Ï†.map_comp]
    constructor; exact S.mul hf hg

theorem mem_map_iff (hÏ† : Function.Injective Ï†.obj) (S : Subgroupoid C) {c d : D} (f : c âŸ¶ d) :
    f âˆˆ (map Ï† hÏ† S).arrows c d â†”
      âˆƒ (a b : C) (g : a âŸ¶ b) (ha : Ï†.obj a = c) (hb : Ï†.obj b = d) (_hg : g âˆˆ S.arrows a b),
        f = eqToHom ha.symm â‰« Ï†.map g â‰« eqToHom hb :=
  Map.arrows_iff Ï† hÏ† S f

theorem galoisConnection_map_comap (hÏ† : Function.Injective Ï†.obj) :
    GaloisConnection (map Ï† hÏ†) (comap Ï†) := by
  rintro S T; simp_rw [le_iff]; constructor
  Â· exact fun h c d f fS => h (Map.Arrows.im f fS)
  Â· rintro h _ _ g âŸ¨a, gÏ†SâŸ©
    exact h gÏ†S

theorem map_mono (hÏ† : Function.Injective Ï†.obj) (S T : Subgroupoid C) :
    S â‰¤ T â†’ map Ï† hÏ† S â‰¤ map Ï† hÏ† T := fun h => (galoisConnection_map_comap Ï† hÏ†).monotone_l h

theorem le_comap_map (hÏ† : Function.Injective Ï†.obj) (S : Subgroupoid C) :
    S â‰¤ comap Ï† (map Ï† hÏ† S) :=
  (galoisConnection_map_comap Ï† hÏ†).le_u_l S

theorem map_comap_le (hÏ† : Function.Injective Ï†.obj) (T : Subgroupoid D) :
    map Ï† hÏ† (comap Ï† T) â‰¤ T :=
  (galoisConnection_map_comap Ï† hÏ†).l_u_le T

theorem map_le_iff_le_comap (hÏ† : Function.Injective Ï†.obj) (S : Subgroupoid C)
    (T : Subgroupoid D) : map Ï† hÏ† S â‰¤ T â†” S â‰¤ comap Ï† T :=
  (galoisConnection_map_comap Ï† hÏ†).le_iff_le

theorem mem_map_objs_iff (hÏ† : Function.Injective Ï†.obj) (d : D) :
    d âˆˆ (map Ï† hÏ† S).objs â†” âˆƒ c âˆˆ S.objs, Ï†.obj c = d := by
  dsimp [objs, map]
  constructor
  Â· rintro âŸ¨f, hfâŸ©
    change Map.Arrows Ï† hÏ† S d d f at hf; rw [Map.arrows_iff] at hf
    obtain âŸ¨c, d, g, ec, ed, eg, gS, egâŸ© := hf
    exact âŸ¨c, âŸ¨mem_objs_of_src S eg, ecâŸ©âŸ©
  Â· rintro âŸ¨c, âŸ¨Î³, Î³SâŸ©, rflâŸ©
    exact âŸ¨Ï†.map Î³, âŸ¨Î³, Î³SâŸ©âŸ©

@[simp]
theorem map_objs_eq (hÏ† : Function.Injective Ï†.obj) : (map Ï† hÏ† S).objs = Ï†.obj '' S.objs := by
  ext x; convert mem_map_objs_iff S Ï† hÏ† x

/-- The image of a functor injective on objects -/
def im (hÏ† : Function.Injective Ï†.obj) :=
  map Ï† hÏ† âŠ¤

theorem mem_im_iff (hÏ† : Function.Injective Ï†.obj) {c d : D} (f : c âŸ¶ d) :
    f âˆˆ (im Ï† hÏ†).arrows c d â†”
      âˆƒ (a b : C) (g : a âŸ¶ b) (ha : Ï†.obj a = c) (hb : Ï†.obj b = d),
        f = eqToHom ha.symm â‰« Ï†.map g â‰« eqToHom hb := by
  convert Map.arrows_iff Ï† hÏ† âŠ¤ f; simp only [Top.top, mem_univ, exists_true_left]

theorem mem_im_objs_iff (hÏ† : Function.Injective Ï†.obj) (d : D) :
    d âˆˆ (im Ï† hÏ†).objs â†” âˆƒ c : C, Ï†.obj c = d := by
  simp only [im, mem_map_objs_iff, mem_top_objs, true_and]

theorem obj_surjective_of_im_eq_top (hÏ† : Function.Injective Ï†.obj) (hÏ†' : im Ï† hÏ† = âŠ¤) :
    Function.Surjective Ï†.obj := by
  rintro d
  rw [â† mem_im_objs_iff _ hÏ†, hÏ†']
  apply mem_top_objs

theorem isNormal_map (hÏ† : Function.Injective Ï†.obj) (hÏ†' : im Ï† hÏ† = âŠ¤) (Sn : S.IsNormal) :
    (map Ï† hÏ† S).IsNormal :=
  { wide := fun d => by
      obtain âŸ¨c, rflâŸ© := obj_surjective_of_im_eq_top Ï† hÏ† hÏ†' d
      change Map.Arrows Ï† hÏ† S _ _ (ğŸ™ _); rw [â† Functor.map_id]
      constructor; exact Sn.wide c
    conj := fun {d d'} g Î´ hÎ´ => by
      rw [mem_map_iff] at hÎ´
      obtain âŸ¨c, c', Î³, cd, cd', Î³S, hÎ³âŸ© := hÎ´; subst_vars; cases hÏ† cd'
      have : d' âˆˆ (im Ï† hÏ†).objs := by rw [hÏ†']; apply mem_top_objs
      rw [mem_im_objs_iff] at this
      obtain âŸ¨c', rflâŸ© := this
      have : g âˆˆ (im Ï† hÏ†).arrows (Ï†.obj c) (Ï†.obj c') := by rw [hÏ†']; trivial
      rw [mem_im_iff] at this
      obtain âŸ¨b, b', f, hb, hb', _, hfâŸ© := this; cases hÏ† hb; cases hÏ† hb'
      change Map.Arrows Ï† hÏ† S (Ï†.obj c') (Ï†.obj c') _
      simp only [eqToHom_refl, Category.comp_id, Category.id_comp, inv_eq_inv]
      suffices Map.Arrows Ï† hÏ† S (Ï†.obj c') (Ï†.obj c') (Ï†.map <| Groupoid.inv f â‰« Î³ â‰« f) by
        simp only [inv_eq_inv, Functor.map_comp, Functor.map_inv] at this; exact this
      constructor; apply Sn.conj f Î³S }

end Hom

section Thin

/-- A subgroupoid is thin (`CategoryTheory.Subgroupoid.IsThin`) if it has at most one arrow between
any two vertices. -/
abbrev IsThin :=
  Quiver.IsThin S.objs

nonrec theorem isThin_iff : S.IsThin â†” âˆ€ c : S.objs, Subsingleton (S.arrows c c) := isThin_iff _

end Thin

section Disconnected

/-- A subgroupoid `IsTotallyDisconnected` if it has only isotropy arrows. -/
nonrec abbrev IsTotallyDisconnected :=
  IsTotallyDisconnected S.objs

theorem isTotallyDisconnected_iff :
    S.IsTotallyDisconnected â†” âˆ€ c d, (S.arrows c d).Nonempty â†’ c = d := by
  constructor
  Â· rintro h c d âŸ¨f, fSâŸ©
    exact congr_arg Subtype.val <| h âŸ¨c, mem_objs_of_src S fSâŸ© âŸ¨d, mem_objs_of_tgt S fSâŸ© âŸ¨f, fSâŸ©
  Â· rintro h âŸ¨c, hcâŸ© âŸ¨d, hdâŸ© âŸ¨f, fSâŸ©
    simp only [Subtype.mk_eq_mk]
    exact h c d âŸ¨f, fSâŸ©

/-- The isotropy subgroupoid of `S` -/
def disconnect : Subgroupoid C where
  arrows c d := {f | c = d âˆ§ f âˆˆ S.arrows c d}
  inv := by rintro _ _ _ âŸ¨rfl, hâŸ©; exact âŸ¨rfl, S.inv hâŸ©
  mul := by rintro _ _ _ _ âŸ¨rfl, hâŸ© _ âŸ¨rfl, h'âŸ©; exact âŸ¨rfl, S.mul h h'âŸ©

theorem disconnect_le : S.disconnect â‰¤ S := by rw [le_iff]; rintro _ _ _ âŸ¨âŸ©; assumption

theorem disconnect_normal (Sn : S.IsNormal) : S.disconnect.IsNormal :=
  { wide := fun c => âŸ¨rfl, Sn.wide câŸ©
    conj := fun _ _ âŸ¨_, h'âŸ© => âŸ¨rfl, Sn.conj _ h'âŸ© }

@[simp]
theorem mem_disconnect_objs_iff {c : C} : c âˆˆ S.disconnect.objs â†” c âˆˆ S.objs :=
  âŸ¨fun âŸ¨Î³, _, Î³SâŸ© => âŸ¨Î³, Î³SâŸ©, fun âŸ¨Î³, Î³SâŸ© => âŸ¨Î³, rfl, Î³SâŸ©âŸ©

theorem disconnect_objs : S.disconnect.objs = S.objs := Set.ext fun _ â†¦ mem_disconnect_objs_iff _

theorem disconnect_isTotallyDisconnected : S.disconnect.IsTotallyDisconnected := by
  rw [isTotallyDisconnected_iff]; exact fun c d âŸ¨_, h, _âŸ© => h

end Disconnected

section Full

variable (D : Set C)

/-- The full subgroupoid on a set `D : Set C` -/
def full : Subgroupoid C where
  arrows c d := {_f | c âˆˆ D âˆ§ d âˆˆ D}
  inv := by rintro _ _ _ âŸ¨âŸ©; constructor <;> assumption
  mul := by rintro _ _ _ _ âŸ¨âŸ© _ âŸ¨âŸ©; constructor <;> assumption

theorem full_objs : (full D).objs = D :=
  Set.ext fun _ => âŸ¨fun âŸ¨_, h, _âŸ© => h, fun h => âŸ¨ğŸ™ _, h, hâŸ©âŸ©

@[simp]
theorem mem_full_iff {c d : C} {f : c âŸ¶ d} : f âˆˆ (full D).arrows c d â†” c âˆˆ D âˆ§ d âˆˆ D :=
  Iff.rfl

@[simp]
theorem mem_full_objs_iff {c : C} : c âˆˆ (full D).objs â†” c âˆˆ D := by rw [full_objs]

@[simp]
theorem full_empty : full âˆ… = (âŠ¥ : Subgroupoid C) := by
  ext
  simp only [Bot.bot, mem_full_iff, mem_empty_iff_false, and_self_iff]

@[simp]
theorem full_univ : full Set.univ = (âŠ¤ : Subgroupoid C) := by
  ext
  simp only [mem_full_iff, mem_univ, and_self, mem_top]

theorem full_mono {D E : Set C} (h : D â‰¤ E) : full D â‰¤ full E := by
  rw [le_iff]
  rintro c d f
  simp only [mem_full_iff]
  exact fun âŸ¨hc, hdâŸ© => âŸ¨h hc, h hdâŸ©

theorem full_arrow_eq_iff {c d : (full D).objs} {f g : c âŸ¶ d} :
    f = g â†” f.1 = g.1 :=
  Subtype.ext_iff

end Full

end Subgroupoid

end CategoryTheory
