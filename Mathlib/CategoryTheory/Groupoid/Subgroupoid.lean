/-
Copyright (c) 2022 R√©mi Bottinelli. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©mi Bottinelli, Junyan Xu
-/
import Mathlib.Algebra.Group.Subgroup.Basic
import Mathlib.CategoryTheory.Groupoid.VertexGroup
import Mathlib.CategoryTheory.Groupoid.Basic
import Mathlib.CategoryTheory.Groupoid
import Mathlib.Data.Set.Lattice
import Mathlib.Order.GaloisConnection

#align_import category_theory.groupoid.subgroupoid from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# Subgroupoid

This file defines subgroupoids as `structure`s containing the subsets of arrows and their
stability under composition and inversion.
Also defined are:

* containment of subgroupoids is a complete lattice;
* images and preimages of subgroupoids under a functor;
* the notion of normality of subgroupoids and its stability under intersection and preimage;
* compatibility of the above with `CategoryTheory.Groupoid.vertexGroup`.


## Main definitions

Given a type `C` with associated `groupoid C` instance.

* `CategoryTheory.Subgroupoid C` is the type of subgroupoids of `C`
* `CategoryTheory.Subgroupoid.IsNormal` is the property that the subgroupoid is stable under
  conjugation by arbitrary arrows, _and_ that all identity arrows are contained in the subgroupoid.
* `CategoryTheory.Subgroupoid.comap` is the "preimage" map of subgroupoids along a functor.
* `CategoryTheory.Subgroupoid.map` is the "image" map of subgroupoids along a functor _injective on
  objects_.
* `CategoryTheory.Subgroupoid.vertexSubgroup` is the subgroup of the `vertex group` at a given
  vertex `v`, assuming `v` is contained in the `CategoryTheory.Subgroupoid` (meaning, by definition,
  that the arrow `ùüô v` is contained in the subgroupoid).

## Implementation details

The structure of this file is copied from/inspired by `Mathlib/GroupTheory/Subgroup/Basic.lean`
and `Mathlib/Combinatorics/SimpleGraph/Subgraph.lean`.

## TODO

* Equivalent inductive characterization of generated (normal) subgroupoids.
* Characterization of normal subgroupoids as kernels.
* Prove that `CategoryTheory.Subgroupoid.full` and `CategoryTheory.Subgroupoid.disconnect` preserve
  intersections (and `CategoryTheory.Subgroupoid.disconnect` also unions)

## Tags

category theory, groupoid, subgroupoid
-/


namespace CategoryTheory

open Set Groupoid

universe u v

variable {C : Type u} [Groupoid C]

/-- A sugroupoid of `C` consists of a choice of arrows for each pair of vertices, closed
under composition and inverses.
-/
@[ext]
structure Subgroupoid (C : Type u) [Groupoid C] where
  arrows : ‚àÄ c d : C, Set (c ‚ü∂ d)
  protected inv : ‚àÄ {c d} {p : c ‚ü∂ d}, p ‚àà arrows c d ‚Üí Groupoid.inv p ‚àà arrows d c
  protected mul : ‚àÄ {c d e} {p}, p ‚àà arrows c d ‚Üí ‚àÄ {q}, q ‚àà arrows d e ‚Üí p ‚â´ q ‚àà arrows c e
#align category_theory.subgroupoid CategoryTheory.Subgroupoid

namespace Subgroupoid

variable (S : Subgroupoid C)

theorem inv_mem_iff {c d : C} (f : c ‚ü∂ d) :
    Groupoid.inv f ‚àà S.arrows d c ‚Üî f ‚àà S.arrows c d := by
  constructor
  ¬∑ intro h
    simpa only [inv_eq_inv, IsIso.inv_inv] using S.inv h
  ¬∑ apply S.inv
#align category_theory.subgroupoid.inv_mem_iff CategoryTheory.Subgroupoid.inv_mem_iff

theorem mul_mem_cancel_left {c d e : C} {f : c ‚ü∂ d} {g : d ‚ü∂ e} (hf : f ‚àà S.arrows c d) :
    f ‚â´ g ‚àà S.arrows c e ‚Üî g ‚àà S.arrows d e := by
  constructor
  ¬∑ rintro h
    suffices Groupoid.inv f ‚â´ f ‚â´ g ‚àà S.arrows d e by
      simpa only [inv_eq_inv, IsIso.inv_hom_id_assoc] using this
    apply S.mul (S.inv hf) h
  ¬∑ apply S.mul hf
#align category_theory.subgroupoid.mul_mem_cancel_left CategoryTheory.Subgroupoid.mul_mem_cancel_left

theorem mul_mem_cancel_right {c d e : C} {f : c ‚ü∂ d} {g : d ‚ü∂ e} (hg : g ‚àà S.arrows d e) :
    f ‚â´ g ‚àà S.arrows c e ‚Üî f ‚àà S.arrows c d := by
  constructor
  ¬∑ rintro h
    suffices (f ‚â´ g) ‚â´ Groupoid.inv g ‚àà S.arrows c d by
      simpa only [inv_eq_inv, IsIso.hom_inv_id, Category.comp_id, Category.assoc] using this
    apply S.mul h (S.inv hg)
  ¬∑ exact fun hf => S.mul hf hg
#align category_theory.subgroupoid.mul_mem_cancel_right CategoryTheory.Subgroupoid.mul_mem_cancel_right

/-- The vertices of `C` on which `S` has non-trivial isotropy -/
def objs : Set C :=
  {c : C | (S.arrows c c).Nonempty}
#align category_theory.subgroupoid.objs CategoryTheory.Subgroupoid.objs

theorem mem_objs_of_src {c d : C} {f : c ‚ü∂ d} (h : f ‚àà S.arrows c d) : c ‚àà S.objs :=
  ‚ü®f ‚â´ Groupoid.inv f, S.mul h (S.inv h)‚ü©
#align category_theory.subgroupoid.mem_objs_of_src CategoryTheory.Subgroupoid.mem_objs_of_src

theorem mem_objs_of_tgt {c d : C} {f : c ‚ü∂ d} (h : f ‚àà S.arrows c d) : d ‚àà S.objs :=
  ‚ü®Groupoid.inv f ‚â´ f, S.mul (S.inv h) h‚ü©
#align category_theory.subgroupoid.mem_objs_of_tgt CategoryTheory.Subgroupoid.mem_objs_of_tgt

theorem id_mem_of_nonempty_isotropy (c : C) : c ‚àà objs S ‚Üí ùüô c ‚àà S.arrows c c := by
  rintro ‚ü®Œ≥, hŒ≥‚ü©
  convert S.mul hŒ≥ (S.inv hŒ≥)
  simp only [inv_eq_inv, IsIso.hom_inv_id]
#align category_theory.subgroupoid.id_mem_of_nonempty_isotropy CategoryTheory.Subgroupoid.id_mem_of_nonempty_isotropy

theorem id_mem_of_src {c d : C} {f : c ‚ü∂ d} (h : f ‚àà S.arrows c d) : ùüô c ‚àà S.arrows c c :=
  id_mem_of_nonempty_isotropy S c (mem_objs_of_src S h)
#align category_theory.subgroupoid.id_mem_of_src CategoryTheory.Subgroupoid.id_mem_of_src

theorem id_mem_of_tgt {c d : C} {f : c ‚ü∂ d} (h : f ‚àà S.arrows c d) : ùüô d ‚àà S.arrows d d :=
  id_mem_of_nonempty_isotropy S d (mem_objs_of_tgt S h)
#align category_theory.subgroupoid.id_mem_of_tgt CategoryTheory.Subgroupoid.id_mem_of_tgt

/-- A subgroupoid seen as a quiver on vertex set `C` -/
def asWideQuiver : Quiver C :=
  ‚ü®fun c d => Subtype <| S.arrows c d‚ü©
#align category_theory.subgroupoid.as_wide_quiver CategoryTheory.Subgroupoid.asWideQuiver

/-- The coercion of a subgroupoid as a groupoid -/
@[simps comp_coe, simps (config := .lemmasOnly) inv_coe]
instance coe : Groupoid S.objs where
  Hom a b := S.arrows a.val b.val
  id a := ‚ü®ùüô a.val, id_mem_of_nonempty_isotropy S a.val a.prop‚ü©
  comp p q := ‚ü®p.val ‚â´ q.val, S.mul p.prop q.prop‚ü©
  inv p := ‚ü®Groupoid.inv p.val, S.inv p.prop‚ü©
#align category_theory.subgroupoid.coe CategoryTheory.Subgroupoid.coe

@[simp]
theorem coe_inv_coe' {c d : S.objs} (p : c ‚ü∂ d) :
    (CategoryTheory.inv p).val = CategoryTheory.inv p.val := by
  simp only [‚Üê inv_eq_inv, coe_inv_coe]
#align category_theory.subgroupoid.coe_inv_coe' CategoryTheory.Subgroupoid.coe_inv_coe'

/-- The embedding of the coerced subgroupoid to its parent-/
def hom : S.objs ‚•§ C where
  obj c := c.val
  map f := f.val
  map_id _ := rfl
  map_comp _ _ := rfl
#align category_theory.subgroupoid.hom CategoryTheory.Subgroupoid.hom

theorem hom.inj_on_objects : Function.Injective (hom S).obj := by
  rintro ‚ü®c, hc‚ü© ‚ü®d, hd‚ü© hcd
  simp only [Subtype.mk_eq_mk]; exact hcd
#align category_theory.subgroupoid.hom.inj_on_objects CategoryTheory.Subgroupoid.hom.inj_on_objects

theorem hom.faithful : ‚àÄ c d, Function.Injective fun f : c ‚ü∂ d => (hom S).map f := by
  rintro ‚ü®c, hc‚ü© ‚ü®d, hd‚ü© ‚ü®f, hf‚ü© ‚ü®g, hg‚ü© hfg; exact Subtype.eq hfg
#align category_theory.subgroupoid.hom.faithful CategoryTheory.Subgroupoid.hom.faithful

/-- The subgroup of the vertex group at `c` given by the subgroupoid -/
def vertexSubgroup {c : C} (hc : c ‚àà S.objs) : Subgroup (c ‚ü∂ c) where
  carrier := S.arrows c c
  mul_mem' hf hg := S.mul hf hg
  one_mem' := id_mem_of_nonempty_isotropy _ _ hc
  inv_mem' hf := S.inv hf
#align category_theory.subgroupoid.vertex_subgroup CategoryTheory.Subgroupoid.vertexSubgroup

/-- The set of all arrows of a subgroupoid, as a set in `Œ£ c d : C, c ‚ü∂ d`. -/
@[coe] def toSet (S : Subgroupoid C) : Set (Œ£ c d : C, c ‚ü∂ d) :=
  {F | F.2.2 ‚àà S.arrows F.1 F.2.1}

instance : SetLike (Subgroupoid C) (Œ£ c d : C, c ‚ü∂ d) where
  coe := toSet
  coe_injective' := fun ‚ü®S, _, _‚ü© ‚ü®T, _, _‚ü© h => by ext c d f; apply Set.ext_iff.1 h ‚ü®c, d, f‚ü©

theorem mem_iff (S : Subgroupoid C) (F : Œ£ c d, c ‚ü∂ d) : F ‚àà S ‚Üî F.2.2 ‚àà S.arrows F.1 F.2.1 :=
  Iff.rfl
#align category_theory.subgroupoid.mem_iff CategoryTheory.Subgroupoid.mem_iff

theorem le_iff (S T : Subgroupoid C) : S ‚â§ T ‚Üî ‚àÄ {c d}, S.arrows c d ‚äÜ T.arrows c d := by
  rw [SetLike.le_def, Sigma.forall]; exact forall_congr' fun c => Sigma.forall
#align category_theory.subgroupoid.le_iff CategoryTheory.Subgroupoid.le_iff

instance : Top (Subgroupoid C) :=
  ‚ü®{  arrows := fun _ _ => Set.univ
      mul := by intros; trivial
      inv := by intros; trivial }‚ü©

theorem mem_top {c d : C} (f : c ‚ü∂ d) : f ‚àà (‚ä§ : Subgroupoid C).arrows c d :=
  trivial
#align category_theory.subgroupoid.mem_top CategoryTheory.Subgroupoid.mem_top

theorem mem_top_objs (c : C) : c ‚àà (‚ä§ : Subgroupoid C).objs := by
  dsimp [Top.top, objs]
  simp only [univ_nonempty]
#align category_theory.subgroupoid.mem_top_objs CategoryTheory.Subgroupoid.mem_top_objs

instance : Bot (Subgroupoid C) :=
  ‚ü®{  arrows := fun _ _ => ‚àÖ
      mul := False.elim
      inv := False.elim }‚ü©

instance : Inhabited (Subgroupoid C) :=
  ‚ü®‚ä§‚ü©

instance : Inf (Subgroupoid C) :=
  ‚ü®fun S T =>
    { arrows := fun c d => S.arrows c d ‚à© T.arrows c d
      inv := fun hp ‚Ü¶ ‚ü®S.inv hp.1, T.inv hp.2‚ü©
      mul := fun hp _ hq ‚Ü¶ ‚ü®S.mul hp.1 hq.1, T.mul hp.2 hq.2‚ü© }‚ü©

instance : InfSet (Subgroupoid C) :=
  ‚ü®fun s =>
    { arrows := fun c d => ‚ãÇ S ‚àà s, Subgroupoid.arrows S c d
      inv := fun hp ‚Ü¶ by rw [mem_iInter‚ÇÇ] at hp ‚ä¢; exact fun S hS => S.inv (hp S hS)
      mul := fun hp _ hq ‚Ü¶ by
        rw [mem_iInter‚ÇÇ] at hp hq ‚ä¢;
        exact fun S hS => S.mul (hp S hS) (hq S hS) }‚ü©

-- Porting note (#10756): new lemma
theorem mem_sInf_arrows {s : Set (Subgroupoid C)} {c d : C} {p : c ‚ü∂ d} :
    p ‚àà (sInf s).arrows c d ‚Üî ‚àÄ S ‚àà s, p ‚àà S.arrows c d :=
  mem_iInter‚ÇÇ

theorem mem_sInf {s : Set (Subgroupoid C)} {p : Œ£ c d : C, c ‚ü∂ d} :
    p ‚àà sInf s ‚Üî ‚àÄ S ‚àà s, p ‚àà S :=
  mem_sInf_arrows

instance : CompleteLattice (Subgroupoid C) :=
  { completeLatticeOfInf (Subgroupoid C) (by
      refine fun s => ‚ü®fun S Ss F => ?_, fun T Tl F fT => ?_‚ü© <;> simp only [mem_sInf]
      exacts [fun hp => hp S Ss, fun S Ss => Tl Ss fT]) with
    bot := ‚ä•
    bot_le := fun S => empty_subset _
    top := ‚ä§
    le_top := fun S => subset_univ _
    inf := (¬∑ ‚äì ¬∑)
    le_inf := fun R S T RS RT _ pR => ‚ü®RS pR, RT pR‚ü©
    inf_le_left := fun R S _ => And.left
    inf_le_right := fun R S _ => And.right }

theorem le_objs {S T : Subgroupoid C} (h : S ‚â§ T) : S.objs ‚äÜ T.objs := fun s ‚ü®Œ≥, hŒ≥‚ü© =>
  ‚ü®Œ≥, @h ‚ü®s, s, Œ≥‚ü© hŒ≥‚ü©
#align category_theory.subgroupoid.le_objs CategoryTheory.Subgroupoid.le_objs

/-- The functor associated to the embedding of subgroupoids -/
def inclusion {S T : Subgroupoid C} (h : S ‚â§ T) : S.objs ‚•§ T.objs where
  obj s := ‚ü®s.val, le_objs h s.prop‚ü©
  map f := ‚ü®f.val, @h ‚ü®_, _, f.val‚ü© f.prop‚ü©
  map_id _ := rfl
  map_comp _ _ := rfl
#align category_theory.subgroupoid.inclusion CategoryTheory.Subgroupoid.inclusion

theorem inclusion_inj_on_objects {S T : Subgroupoid C} (h : S ‚â§ T) :
    Function.Injective (inclusion h).obj := fun ‚ü®s, hs‚ü© ‚ü®t, ht‚ü© => by
  simpa only [inclusion, Subtype.mk_eq_mk] using id
#align category_theory.subgroupoid.inclusion_inj_on_objects CategoryTheory.Subgroupoid.inclusion_inj_on_objects

theorem inclusion_faithful {S T : Subgroupoid C} (h : S ‚â§ T) (s t : S.objs) :
    Function.Injective fun f : s ‚ü∂ t => (inclusion h).map f := fun ‚ü®f, hf‚ü© ‚ü®g, hg‚ü© => by
  -- Porting note: was `...; simpa only [Subtype.mk_eq_mk] using id`
  dsimp only [inclusion]; rw [Subtype.mk_eq_mk, Subtype.mk_eq_mk]; exact id
#align category_theory.subgroupoid.inclusion_faithful CategoryTheory.Subgroupoid.inclusion_faithful

theorem inclusion_refl {S : Subgroupoid C} : inclusion (le_refl S) = ùü≠ S.objs :=
  Functor.hext (fun _ => rfl) fun _ _ _ => HEq.refl _
#align category_theory.subgroupoid.inclusion_refl CategoryTheory.Subgroupoid.inclusion_refl

theorem inclusion_trans {R S T : Subgroupoid C} (k : R ‚â§ S) (h : S ‚â§ T) :
    inclusion (k.trans h) = inclusion k ‚ãô inclusion h :=
  rfl
#align category_theory.subgroupoid.inclusion_trans CategoryTheory.Subgroupoid.inclusion_trans

theorem inclusion_comp_embedding {S T : Subgroupoid C} (h : S ‚â§ T) : inclusion h ‚ãô T.hom = S.hom :=
  rfl
#align category_theory.subgroupoid.inclusion_comp_embedding CategoryTheory.Subgroupoid.inclusion_comp_embedding

/-- The family of arrows of the discrete groupoid -/
inductive Discrete.Arrows : ‚àÄ c d : C, (c ‚ü∂ d) ‚Üí Prop
  | id (c : C) : Discrete.Arrows c c (ùüô c)
#align category_theory.subgroupoid.discrete.arrows CategoryTheory.Subgroupoid.Discrete.Arrows

/-- The only arrows of the discrete groupoid are the identity arrows. -/
def discrete : Subgroupoid C where
  arrows c d := {p | Discrete.Arrows c d p}
  inv := by rintro _ _ _ ‚ü®‚ü©; simp only [inv_eq_inv, IsIso.inv_id]; constructor
  mul := by rintro _ _ _ _ ‚ü®‚ü© _ ‚ü®‚ü©; rw [Category.comp_id]; constructor
#align category_theory.subgroupoid.discrete CategoryTheory.Subgroupoid.discrete

theorem mem_discrete_iff {c d : C} (f : c ‚ü∂ d) :
    f ‚àà discrete.arrows c d ‚Üî ‚àÉ h : c = d, f = eqToHom h :=
  ‚ü®by rintro ‚ü®‚ü©; exact ‚ü®rfl, rfl‚ü©, by rintro ‚ü®rfl, rfl‚ü©; constructor‚ü©
#align category_theory.subgroupoid.mem_discrete_iff CategoryTheory.Subgroupoid.mem_discrete_iff

/-- A subgroupoid is wide if its carrier set is all of `C`-/
structure IsWide : Prop where
  wide : ‚àÄ c, ùüô c ‚àà S.arrows c c
#align category_theory.subgroupoid.is_wide CategoryTheory.Subgroupoid.IsWide

theorem isWide_iff_objs_eq_univ : S.IsWide ‚Üî S.objs = Set.univ := by
  constructor
  ¬∑ rintro h
    ext x; constructor <;> simp only [top_eq_univ, mem_univ, imp_true_iff, forall_true_left]
    apply mem_objs_of_src S (h.wide x)
  ¬∑ rintro h
    refine ‚ü®fun c => ?_‚ü©
    obtain ‚ü®Œ≥, Œ≥S‚ü© := (le_of_eq h.symm : ‚ä§ ‚äÜ S.objs) (Set.mem_univ c)
    exact id_mem_of_src S Œ≥S
#align category_theory.subgroupoid.is_wide_iff_objs_eq_univ CategoryTheory.Subgroupoid.isWide_iff_objs_eq_univ

theorem IsWide.id_mem {S : Subgroupoid C} (Sw : S.IsWide) (c : C) : ùüô c ‚àà S.arrows c c :=
  Sw.wide c
#align category_theory.subgroupoid.is_wide.id_mem CategoryTheory.Subgroupoid.IsWide.id_mem

theorem IsWide.eqToHom_mem {S : Subgroupoid C} (Sw : S.IsWide) {c d : C} (h : c = d) :
    eqToHom h ‚àà S.arrows c d := by cases h; simp only [eqToHom_refl]; apply Sw.id_mem c
#align category_theory.subgroupoid.is_wide.eq_to_hom_mem CategoryTheory.Subgroupoid.IsWide.eqToHom_mem

/-- A¬†subgroupoid is normal if it is wide and satisfies the expected stability under conjugacy. -/
structure IsNormal extends IsWide S : Prop where
  conj : ‚àÄ {c d} (p : c ‚ü∂ d) {Œ≥ : c ‚ü∂ c}, Œ≥ ‚àà S.arrows c c ‚Üí Groupoid.inv p ‚â´ Œ≥ ‚â´ p ‚àà S.arrows d d
#align category_theory.subgroupoid.is_normal CategoryTheory.Subgroupoid.IsNormal

theorem IsNormal.conj' {S : Subgroupoid C} (Sn : IsNormal S) :
    ‚àÄ {c d} (p : d ‚ü∂ c) {Œ≥ : c ‚ü∂ c}, Œ≥ ‚àà S.arrows c c ‚Üí p ‚â´ Œ≥ ‚â´ Groupoid.inv p ‚àà S.arrows d d :=
  fun p Œ≥ hs => by convert Sn.conj (Groupoid.inv p) hs; simp
#align category_theory.subgroupoid.is_normal.conj' CategoryTheory.Subgroupoid.IsNormal.conj'

theorem IsNormal.conjugation_bij (Sn : IsNormal S) {c d} (p : c ‚ü∂ d) :
    Set.BijOn (fun Œ≥ : c ‚ü∂ c => Groupoid.inv p ‚â´ Œ≥ ‚â´ p) (S.arrows c c) (S.arrows d d) := by
  refine ‚ü®fun Œ≥ Œ≥S => Sn.conj p Œ≥S, fun Œ≥‚ÇÅ _ Œ≥‚ÇÇ _ h => ?_, fun Œ¥ Œ¥S =>
    ‚ü®p ‚â´ Œ¥ ‚â´ Groupoid.inv p, Sn.conj' p Œ¥S, ?_‚ü©‚ü©
  ¬∑ simpa only [inv_eq_inv, Category.assoc, IsIso.hom_inv_id, Category.comp_id,
      IsIso.hom_inv_id_assoc] using p ‚â´= h =‚â´ inv p
  ¬∑ simp only [inv_eq_inv, Category.assoc, IsIso.inv_hom_id, Category.comp_id,
      IsIso.inv_hom_id_assoc]
#align category_theory.subgroupoid.is_normal.conjugation_bij CategoryTheory.Subgroupoid.IsNormal.conjugation_bij

theorem top_isNormal : IsNormal (‚ä§ : Subgroupoid C) :=
  { wide := fun _ => trivial
    conj := fun _ _ _ => trivial }
#align category_theory.subgroupoid.top_is_normal CategoryTheory.Subgroupoid.top_isNormal

theorem sInf_isNormal (s : Set <| Subgroupoid C) (sn : ‚àÄ S ‚àà s, IsNormal S) : IsNormal (sInf s) :=
  { wide := by simp_rw [sInf, mem_iInter‚ÇÇ]; exact fun c S Ss => (sn S Ss).wide c
    conj := by simp_rw [sInf, mem_iInter‚ÇÇ]; exact fun p Œ≥ hŒ≥ S Ss => (sn S Ss).conj p (hŒ≥ S Ss) }
#align category_theory.subgroupoid.Inf_is_normal CategoryTheory.Subgroupoid.sInf_isNormal

theorem discrete_isNormal : (@discrete C _).IsNormal :=
  { wide := fun c => by constructor
    conj := fun f Œ≥ hŒ≥ => by
      cases hŒ≥
      simp only [inv_eq_inv, Category.id_comp, IsIso.inv_hom_id]; constructor }
#align category_theory.subgroupoid.discrete_is_normal CategoryTheory.Subgroupoid.discrete_isNormal

theorem IsNormal.vertexSubgroup (Sn : IsNormal S) (c : C) (cS : c ‚àà S.objs) :
    (S.vertexSubgroup cS).Normal where
  conj_mem x hx y := by rw [mul_assoc]; exact Sn.conj' y hx
#align category_theory.subgroupoid.is_normal.vertex_subgroup CategoryTheory.Subgroupoid.IsNormal.vertexSubgroup

section GeneratedSubgroupoid

-- TODO: proof that generated is just "words in X" and generatedNormal is similarly
variable (X : ‚àÄ c d : C, Set (c ‚ü∂ d))

/-- The subgropoid generated by the set of arrows `X` -/
def generated : Subgroupoid C :=
  sInf {S : Subgroupoid C | ‚àÄ c d, X c d ‚äÜ S.arrows c d}
#align category_theory.subgroupoid.generated CategoryTheory.Subgroupoid.generated

theorem subset_generated (c d : C) : X c d ‚äÜ (generated X).arrows c d := by
  dsimp only [generated, sInf]
  simp only [subset_iInter‚ÇÇ_iff]
  exact fun S hS f fS => hS _ _ fS
#align category_theory.subgroupoid.subset_generated CategoryTheory.Subgroupoid.subset_generated

/-- The normal sugroupoid generated by the set of arrows `X` -/
def generatedNormal : Subgroupoid C :=
  sInf {S : Subgroupoid C | (‚àÄ c d, X c d ‚äÜ S.arrows c d) ‚àß S.IsNormal}
#align category_theory.subgroupoid.generated_normal CategoryTheory.Subgroupoid.generatedNormal

theorem generated_le_generatedNormal : generated X ‚â§ generatedNormal X := by
  apply @sInf_le_sInf (Subgroupoid C) _
  exact fun S ‚ü®h, _‚ü© => h
#align category_theory.subgroupoid.generated_le_generated_normal CategoryTheory.Subgroupoid.generated_le_generatedNormal

theorem generatedNormal_isNormal : (generatedNormal X).IsNormal :=
  sInf_isNormal _ fun _ h => h.right
#align category_theory.subgroupoid.generated_normal_is_normal CategoryTheory.Subgroupoid.generatedNormal_isNormal

theorem IsNormal.generatedNormal_le {S : Subgroupoid C} (Sn : S.IsNormal) :
    generatedNormal X ‚â§ S ‚Üî ‚àÄ c d, X c d ‚äÜ S.arrows c d := by
  constructor
  ¬∑ rintro h c d
    have h' := generated_le_generatedNormal X
    rw [le_iff] at h h'
    exact ((subset_generated X c d).trans (@h' c d)).trans (@h c d)
  ¬∑ rintro h
    apply @sInf_le (Subgroupoid C) _
    exact ‚ü®h, Sn‚ü©
#align category_theory.subgroupoid.is_normal.generated_normal_le CategoryTheory.Subgroupoid.IsNormal.generatedNormal_le

end GeneratedSubgroupoid

section Hom

variable {D : Type*} [Groupoid D] (œÜ : C ‚•§ D)

/-- A functor between groupoid defines a map of subgroupoids in the reverse direction
by taking preimages.
 -/
def comap (S : Subgroupoid D) : Subgroupoid C where
  arrows c d := {f : c ‚ü∂ d | œÜ.map f ‚àà S.arrows (œÜ.obj c) (œÜ.obj d)}
  inv hp := by rw [mem_setOf, inv_eq_inv, œÜ.map_inv, ‚Üê inv_eq_inv]; exact S.inv hp
  mul := by
    intros
    simp only [mem_setOf, Functor.map_comp]
    apply S.mul <;> assumption
#align category_theory.subgroupoid.comap CategoryTheory.Subgroupoid.comap

theorem comap_mono (S T : Subgroupoid D) : S ‚â§ T ‚Üí comap œÜ S ‚â§ comap œÜ T := fun ST _ =>
  @ST ‚ü®_, _, _‚ü©
#align category_theory.subgroupoid.comap_mono CategoryTheory.Subgroupoid.comap_mono

theorem isNormal_comap {S : Subgroupoid D} (Sn : IsNormal S) : IsNormal (comap œÜ S) where
  wide c := by rw [comap, mem_setOf, Functor.map_id]; apply Sn.wide
  conj f Œ≥ hŒ≥ := by
    simp_rw [inv_eq_inv f, comap, mem_setOf, Functor.map_comp, Functor.map_inv, ‚Üê inv_eq_inv]
    exact Sn.conj _ hŒ≥
#align category_theory.subgroupoid.is_normal_comap CategoryTheory.Subgroupoid.isNormal_comap

@[simp]
theorem comap_comp {E : Type*} [Groupoid E] (œà : D ‚•§ E) : comap (œÜ ‚ãô œà) = comap œÜ ‚àò comap œà :=
  rfl
#align category_theory.subgroupoid.comap_comp CategoryTheory.Subgroupoid.comap_comp

/-- The kernel of a functor between subgroupoid is the preimage. -/
def ker : Subgroupoid C :=
  comap œÜ discrete
#align category_theory.subgroupoid.ker CategoryTheory.Subgroupoid.ker

theorem mem_ker_iff {c d : C} (f : c ‚ü∂ d) :
    f ‚àà (ker œÜ).arrows c d ‚Üî ‚àÉ h : œÜ.obj c = œÜ.obj d, œÜ.map f = eqToHom h :=
  mem_discrete_iff (œÜ.map f)
#align category_theory.subgroupoid.mem_ker_iff CategoryTheory.Subgroupoid.mem_ker_iff

theorem ker_isNormal : (ker œÜ).IsNormal :=
  isNormal_comap œÜ discrete_isNormal
#align category_theory.subgroupoid.ker_is_normal CategoryTheory.Subgroupoid.ker_isNormal

@[simp]
theorem ker_comp {E : Type*} [Groupoid E] (œà : D ‚•§ E) : ker (œÜ ‚ãô œà) = comap œÜ (ker œà) :=
  rfl
#align category_theory.subgroupoid.ker_comp CategoryTheory.Subgroupoid.ker_comp

/-- The family of arrows of the image of a subgroupoid under a functor injective on objects -/
inductive Map.Arrows (hœÜ : Function.Injective œÜ.obj) (S : Subgroupoid C) : ‚àÄ c d : D, (c ‚ü∂ d) ‚Üí Prop
  | im {c d : C} (f : c ‚ü∂ d) (hf : f ‚àà S.arrows c d) : Map.Arrows hœÜ S (œÜ.obj c) (œÜ.obj d) (œÜ.map f)
#align category_theory.subgroupoid.map.arrows CategoryTheory.Subgroupoid.Map.Arrows

theorem Map.arrows_iff (hœÜ : Function.Injective œÜ.obj) (S : Subgroupoid C) {c d : D} (f : c ‚ü∂ d) :
    Map.Arrows œÜ hœÜ S c d f ‚Üî
      ‚àÉ (a b : C) (g : a ‚ü∂ b) (ha : œÜ.obj a = c) (hb : œÜ.obj b = d) (_hg : g ‚àà S.arrows a b),
        f = eqToHom ha.symm ‚â´ œÜ.map g ‚â´ eqToHom hb := by
  constructor
  ¬∑ rintro ‚ü®g, hg‚ü©; exact ‚ü®_, _, g, rfl, rfl, hg, eq_conj_eqToHom _‚ü©
  ¬∑ rintro ‚ü®a, b, g, rfl, rfl, hg, rfl‚ü©; rw [‚Üê eq_conj_eqToHom]; constructor; exact hg
#align category_theory.subgroupoid.map.arrows_iff CategoryTheory.Subgroupoid.Map.arrows_iff

/-- The "forward" image of a subgroupoid under a functor injective on objects -/
def map (hœÜ : Function.Injective œÜ.obj) (S : Subgroupoid C) : Subgroupoid D where
  arrows c d := {x | Map.Arrows œÜ hœÜ S c d x}
  inv := by
    rintro _ _ _ ‚ü®‚ü©
    rw [inv_eq_inv, ‚Üê Functor.map_inv, ‚Üê inv_eq_inv]
    constructor; apply S.inv; assumption
  mul := by
    rintro _ _ _ _ ‚ü®f, hf‚ü© q hq
    obtain ‚ü®c‚ÇÉ, c‚ÇÑ, g, he, rfl, hg, gq‚ü© := (Map.arrows_iff œÜ hœÜ S q).mp hq
    cases hœÜ he; rw [gq, ‚Üê eq_conj_eqToHom, ‚Üê œÜ.map_comp]
    constructor; exact S.mul hf hg
#align category_theory.subgroupoid.map CategoryTheory.Subgroupoid.map

theorem mem_map_iff (hœÜ : Function.Injective œÜ.obj) (S : Subgroupoid C) {c d : D} (f : c ‚ü∂ d) :
    f ‚àà (map œÜ hœÜ S).arrows c d ‚Üî
      ‚àÉ (a b : C) (g : a ‚ü∂ b) (ha : œÜ.obj a = c) (hb : œÜ.obj b = d) (_hg : g ‚àà S.arrows a b),
        f = eqToHom ha.symm ‚â´ œÜ.map g ‚â´ eqToHom hb :=
  Map.arrows_iff œÜ hœÜ S f
#align category_theory.subgroupoid.mem_map_iff CategoryTheory.Subgroupoid.mem_map_iff

theorem galoisConnection_map_comap (hœÜ : Function.Injective œÜ.obj) :
    GaloisConnection (map œÜ hœÜ) (comap œÜ) := by
  rintro S T; simp_rw [le_iff]; constructor
  ¬∑ exact fun h c d f fS => h (Map.Arrows.im f fS)
  ¬∑ rintro h _ _ g ‚ü®a, gœÜS‚ü©
    exact h gœÜS
#align category_theory.subgroupoid.galois_connection_map_comap CategoryTheory.Subgroupoid.galoisConnection_map_comap

theorem map_mono (hœÜ : Function.Injective œÜ.obj) (S T : Subgroupoid C) :
    S ‚â§ T ‚Üí map œÜ hœÜ S ‚â§ map œÜ hœÜ T := fun h => (galoisConnection_map_comap œÜ hœÜ).monotone_l h
#align category_theory.subgroupoid.map_mono CategoryTheory.Subgroupoid.map_mono

theorem le_comap_map (hœÜ : Function.Injective œÜ.obj) (S : Subgroupoid C) :
    S ‚â§ comap œÜ (map œÜ hœÜ S) :=
  (galoisConnection_map_comap œÜ hœÜ).le_u_l S
#align category_theory.subgroupoid.le_comap_map CategoryTheory.Subgroupoid.le_comap_map

theorem map_comap_le (hœÜ : Function.Injective œÜ.obj) (T : Subgroupoid D) :
    map œÜ hœÜ (comap œÜ T) ‚â§ T :=
  (galoisConnection_map_comap œÜ hœÜ).l_u_le T
#align category_theory.subgroupoid.map_comap_le CategoryTheory.Subgroupoid.map_comap_le

theorem map_le_iff_le_comap (hœÜ : Function.Injective œÜ.obj) (S : Subgroupoid C)
    (T : Subgroupoid D) : map œÜ hœÜ S ‚â§ T ‚Üî S ‚â§ comap œÜ T :=
  (galoisConnection_map_comap œÜ hœÜ).le_iff_le
#align category_theory.subgroupoid.map_le_iff_le_comap CategoryTheory.Subgroupoid.map_le_iff_le_comap

theorem mem_map_objs_iff (hœÜ : Function.Injective œÜ.obj) (d : D) :
    d ‚àà (map œÜ hœÜ S).objs ‚Üî ‚àÉ c ‚àà S.objs, œÜ.obj c = d := by
  dsimp [objs, map]
  constructor
  ¬∑ rintro ‚ü®f, hf‚ü©
    change Map.Arrows œÜ hœÜ S d d f at hf; rw [Map.arrows_iff] at hf
    obtain ‚ü®c, d, g, ec, ed, eg, gS, eg‚ü© := hf
    exact ‚ü®c, ‚ü®mem_objs_of_src S eg, ec‚ü©‚ü©
  ¬∑ rintro ‚ü®c, ‚ü®Œ≥, Œ≥S‚ü©, rfl‚ü©
    exact ‚ü®œÜ.map Œ≥, ‚ü®Œ≥, Œ≥S‚ü©‚ü©
#align category_theory.subgroupoid.mem_map_objs_iff CategoryTheory.Subgroupoid.mem_map_objs_iff

@[simp]
theorem map_objs_eq (hœÜ : Function.Injective œÜ.obj) : (map œÜ hœÜ S).objs = œÜ.obj '' S.objs := by
  ext x; convert mem_map_objs_iff S œÜ hœÜ x
#align category_theory.subgroupoid.map_objs_eq CategoryTheory.Subgroupoid.map_objs_eq

/-- The image of a functor injective on objects -/
def im (hœÜ : Function.Injective œÜ.obj) :=
  map œÜ hœÜ ‚ä§
#align category_theory.subgroupoid.im CategoryTheory.Subgroupoid.im

theorem mem_im_iff (hœÜ : Function.Injective œÜ.obj) {c d : D} (f : c ‚ü∂ d) :
    f ‚àà (im œÜ hœÜ).arrows c d ‚Üî
      ‚àÉ (a b : C) (g : a ‚ü∂ b) (ha : œÜ.obj a = c) (hb : œÜ.obj b = d),
        f = eqToHom ha.symm ‚â´ œÜ.map g ‚â´ eqToHom hb := by
  convert Map.arrows_iff œÜ hœÜ ‚ä§ f; simp only [Top.top, mem_univ, exists_true_left]
#align category_theory.subgroupoid.mem_im_iff CategoryTheory.Subgroupoid.mem_im_iff

theorem mem_im_objs_iff (hœÜ : Function.Injective œÜ.obj) (d : D) :
    d ‚àà (im œÜ hœÜ).objs ‚Üî ‚àÉ c : C, œÜ.obj c = d := by
  simp only [im, mem_map_objs_iff, mem_top_objs, true_and]
#align category_theory.subgroupoid.mem_im_objs_iff CategoryTheory.Subgroupoid.mem_im_objs_iff

theorem obj_surjective_of_im_eq_top (hœÜ : Function.Injective œÜ.obj) (hœÜ' : im œÜ hœÜ = ‚ä§) :
    Function.Surjective œÜ.obj := by
  rintro d
  rw [‚Üê mem_im_objs_iff, hœÜ']
  apply mem_top_objs
#align category_theory.subgroupoid.obj_surjective_of_im_eq_top CategoryTheory.Subgroupoid.obj_surjective_of_im_eq_top

theorem isNormal_map (hœÜ : Function.Injective œÜ.obj) (hœÜ' : im œÜ hœÜ = ‚ä§) (Sn : S.IsNormal) :
    (map œÜ hœÜ S).IsNormal :=
  { wide := fun d => by
      obtain ‚ü®c, rfl‚ü© := obj_surjective_of_im_eq_top œÜ hœÜ hœÜ' d
      change Map.Arrows œÜ hœÜ S _ _ (ùüô _); rw [‚Üê Functor.map_id]
      constructor; exact Sn.wide c
    conj := fun {d d'} g Œ¥ hŒ¥ => by
      rw [mem_map_iff] at hŒ¥
      obtain ‚ü®c, c', Œ≥, cd, cd', Œ≥S, hŒ≥‚ü© := hŒ¥; subst_vars; cases hœÜ cd'
      have : d' ‚àà (im œÜ hœÜ).objs := by rw [hœÜ']; apply mem_top_objs
      rw [mem_im_objs_iff] at this
      obtain ‚ü®c', rfl‚ü© := this
      have : g ‚àà (im œÜ hœÜ).arrows (œÜ.obj c) (œÜ.obj c') := by rw [hœÜ']; trivial
      rw [mem_im_iff] at this
      obtain ‚ü®b, b', f, hb, hb', _, hf‚ü© := this; cases hœÜ hb; cases hœÜ hb'
      change Map.Arrows œÜ hœÜ S (œÜ.obj c') (œÜ.obj c') _
      simp only [eqToHom_refl, Category.comp_id, Category.id_comp, inv_eq_inv]
      suffices Map.Arrows œÜ hœÜ S (œÜ.obj c') (œÜ.obj c') (œÜ.map <| Groupoid.inv f ‚â´ Œ≥ ‚â´ f) by
        simp only [inv_eq_inv, Functor.map_comp, Functor.map_inv] at this; exact this
      constructor; apply Sn.conj f Œ≥S }
#align category_theory.subgroupoid.is_normal_map CategoryTheory.Subgroupoid.isNormal_map

end Hom

section Thin

/-- A subgroupoid is thin (`CategoryTheory.Subgroupoid.IsThin`) if it has at most one arrow between
any two vertices. -/
abbrev IsThin :=
  Quiver.IsThin S.objs
#align category_theory.subgroupoid.is_thin CategoryTheory.Subgroupoid.IsThin

nonrec theorem isThin_iff : S.IsThin ‚Üî ‚àÄ c : S.objs, Subsingleton (S.arrows c c) := isThin_iff _
#align category_theory.subgroupoid.is_thin_iff CategoryTheory.Subgroupoid.isThin_iff

end Thin

section Disconnected

/-- A subgroupoid `IsTotallyDisconnected` if it has only isotropy arrows. -/
nonrec abbrev IsTotallyDisconnected :=
  IsTotallyDisconnected S.objs
#align category_theory.subgroupoid.is_totally_disconnected CategoryTheory.Subgroupoid.IsTotallyDisconnected

theorem isTotallyDisconnected_iff :
    S.IsTotallyDisconnected ‚Üî ‚àÄ c d, (S.arrows c d).Nonempty ‚Üí c = d := by
  constructor
  ¬∑ rintro h c d ‚ü®f, fS‚ü©
    have := h ‚ü®c, mem_objs_of_src S fS‚ü© ‚ü®d, mem_objs_of_tgt S fS‚ü© ‚ü®f, fS‚ü©
    exact congr_arg Subtype.val this
  ¬∑ rintro h ‚ü®c, hc‚ü© ‚ü®d, hd‚ü© ‚ü®f, fS‚ü©
    simp only [Subtype.mk_eq_mk]
    exact h c d ‚ü®f, fS‚ü©
#align category_theory.subgroupoid.is_totally_disconnected_iff CategoryTheory.Subgroupoid.isTotallyDisconnected_iff

/-- The isotropy subgroupoid of `S` -/
def disconnect : Subgroupoid C where
  arrows c d := {f | c = d ‚àß f ‚àà S.arrows c d}
  inv := by rintro _ _ _ ‚ü®rfl, h‚ü©; exact ‚ü®rfl, S.inv h‚ü©
  mul := by rintro _ _ _ _ ‚ü®rfl, h‚ü© _ ‚ü®rfl, h'‚ü©; exact ‚ü®rfl, S.mul h h'‚ü©
#align category_theory.subgroupoid.disconnect CategoryTheory.Subgroupoid.disconnect

theorem disconnect_le : S.disconnect ‚â§ S := by rw [le_iff]; rintro _ _ _ ‚ü®‚ü©; assumption
#align category_theory.subgroupoid.disconnect_le CategoryTheory.Subgroupoid.disconnect_le

theorem disconnect_normal (Sn : S.IsNormal) : S.disconnect.IsNormal :=
  { wide := fun c => ‚ü®rfl, Sn.wide c‚ü©
    conj := fun _ _ ‚ü®_, h'‚ü© => ‚ü®rfl, Sn.conj _ h'‚ü© }
#align category_theory.subgroupoid.disconnect_normal CategoryTheory.Subgroupoid.disconnect_normal

@[simp]
theorem mem_disconnect_objs_iff {c : C} : c ‚àà S.disconnect.objs ‚Üî c ‚àà S.objs :=
  ‚ü®fun ‚ü®Œ≥, _, Œ≥S‚ü© => ‚ü®Œ≥, Œ≥S‚ü©, fun ‚ü®Œ≥, Œ≥S‚ü© => ‚ü®Œ≥, rfl, Œ≥S‚ü©‚ü©
#align category_theory.subgroupoid.mem_disconnect_objs_iff CategoryTheory.Subgroupoid.mem_disconnect_objs_iff

theorem disconnect_objs : S.disconnect.objs = S.objs := Set.ext fun _ ‚Ü¶ mem_disconnect_objs_iff _
#align category_theory.subgroupoid.disconnect_objs CategoryTheory.Subgroupoid.disconnect_objs

theorem disconnect_isTotallyDisconnected : S.disconnect.IsTotallyDisconnected := by
  rw [isTotallyDisconnected_iff]; exact fun c d ‚ü®_, h, _‚ü© => h
#align category_theory.subgroupoid.disconnect_is_totally_disconnected CategoryTheory.Subgroupoid.disconnect_isTotallyDisconnected

end Disconnected

section Full

variable (D : Set C)

/-- The full subgroupoid on a set `D : Set C` -/
def full : Subgroupoid C where
  arrows c d := {_f | c ‚àà D ‚àß d ‚àà D}
  inv := by rintro _ _ _ ‚ü®‚ü©; constructor <;> assumption
  mul := by rintro _ _ _ _ ‚ü®‚ü© _ ‚ü®‚ü©; constructor <;> assumption
#align category_theory.subgroupoid.full CategoryTheory.Subgroupoid.full

theorem full_objs : (full D).objs = D :=
  Set.ext fun _ => ‚ü®fun ‚ü®_, h, _‚ü© => h, fun h => ‚ü®ùüô _, h, h‚ü©‚ü©
#align category_theory.subgroupoid.full_objs CategoryTheory.Subgroupoid.full_objs

@[simp]
theorem mem_full_iff {c d : C} {f : c ‚ü∂ d} : f ‚àà (full D).arrows c d ‚Üî c ‚àà D ‚àß d ‚àà D :=
  Iff.rfl
#align category_theory.subgroupoid.mem_full_iff CategoryTheory.Subgroupoid.mem_full_iff

@[simp]
theorem mem_full_objs_iff {c : C} : c ‚àà (full D).objs ‚Üî c ‚àà D := by rw [full_objs]
#align category_theory.subgroupoid.mem_full_objs_iff CategoryTheory.Subgroupoid.mem_full_objs_iff

@[simp]
theorem full_empty : full ‚àÖ = (‚ä• : Subgroupoid C) := by
  ext
  simp only [Bot.bot, mem_full_iff, mem_empty_iff_false, and_self_iff]
#align category_theory.subgroupoid.full_empty CategoryTheory.Subgroupoid.full_empty

@[simp]
theorem full_univ : full Set.univ = (‚ä§ : Subgroupoid C) := by
  ext
  simp only [mem_full_iff, mem_univ, and_self, mem_top]
#align category_theory.subgroupoid.full_univ CategoryTheory.Subgroupoid.full_univ

theorem full_mono {D E : Set C} (h : D ‚â§ E) : full D ‚â§ full E := by
  rw [le_iff]
  rintro c d f
  simp only [mem_full_iff]
  exact fun ‚ü®hc, hd‚ü© => ‚ü®h hc, h hd‚ü©
#align category_theory.subgroupoid.full_mono CategoryTheory.Subgroupoid.full_mono

-- Porting note: using `.1` instead of `‚Üë`
theorem full_arrow_eq_iff {c d : (full D).objs} {f g : c ‚ü∂ d} :
    f = g ‚Üî (f.1 : c.val ‚ü∂ d.val) = g.1 :=
  Subtype.ext_iff
#align category_theory.subgroupoid.full_arrow_eq_iff CategoryTheory.Subgroupoid.full_arrow_eq_iff

end Full

end Subgroupoid

end CategoryTheory
