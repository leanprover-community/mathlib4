/-
Copyright (c) 2025 Klaus Gy. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Klaus Gy
-/
import Mathlib.CategoryTheory.Monoidal.Cartesian.Basic
import Mathlib.CategoryTheory.Topos.Classifier
/-!
# Elementary Topos (in Elementary Form)

This ongoing work formalizes the elementary definition of a topos and the direct consequences.

## References

* [S. MacLane and I. Moerdijk, *Sheaves in Geometry and Logic*][MM92]
-/

universe u v

open CategoryTheory Category Functor Limits MonoidalCategory Opposite

variable {â„° : Type u} [Category.{v} â„°] [CartesianMonoidalCategory â„°]

/-- The covariant functor `B âŠ— [] âŸ¶ C` from `â„°áµ’áµ–` to `Type`. -/
def WhiskeredHom (B C : â„°) : â„°áµ’áµ– â¥¤ Type v :=
  âŸ¨ âŸ¨ fun A â†¦ B âŠ— unop A âŸ¶ C, fun f g â†¦ (B â— unop f) â‰« g âŸ©,
    fun A â†¦ by
      have : unop (ğŸ™ A) = ğŸ™ (unop A) := by rfl
      ext; simp[this],
    fun f f' â†¦ by
      have : B â— unop (f â‰« f') = B â— unop f' â‰« B â— unop f := by aesop_cat
      ext; simp[this] âŸ©

/-- `P` is a power object of `B` if it represents the functor `WhiskeredHom B hc.Î©`. -/
def IsPowerObjectOf (sc : Classifier â„° (ğŸ™_ â„°)) (B P : â„°) :=
  (WhiskeredHom B sc.Î©).RepresentableBy P

namespace PowerObject

variable {sc : Classifier â„° (ğŸ™_ â„°)} {B PB : â„°} (hPB : IsPowerObjectOf sc B PB)

/-- The P-transpose of a morphism `g : A âŸ¶ P B`. -/
def hat {A : â„°} (g : A âŸ¶ PB) : B âŠ— A âŸ¶ sc.Î© :=
  hPB.homEquiv.toFun g

/-- The P-transpose of a morphism `f : B Ã— A âŸ¶ Î©`. -/
def unhat {A : â„°} (f : B âŠ— A âŸ¶ sc.Î©) : (A âŸ¶ PB) :=
  hPB.homEquiv.invFun f

@[simp]
lemma hat_unhat {A : â„°} (f : B âŠ— A âŸ¶ sc.Î©) :
  hat hPB (unhat hPB f) = f := hPB.homEquiv.apply_symm_apply f

@[simp]
lemma unhat_hat {A : â„°} (g : A âŸ¶ PB) :
  unhat hPB (hat hPB g) = g := hPB.homEquiv.symm_apply_apply g

/-- The element relation as a subobject of `B â¨¯ (P B)`. -/
def Îµ : B âŠ— (PB) âŸ¶ sc.Î© := hPB.homEquiv.toFun (ğŸ™ (PB))

lemma hatAsComp {A : â„°} (g : A âŸ¶ PB) : hat hPB g = B â— g â‰« Îµ hPB := hPB.homEquiv_eq g

@[simp]
lemma comm {A : â„°} (f : B âŠ— A âŸ¶ sc.Î©) : B â— (unhat hPB f) â‰« Îµ hPB = f := by
  have : hPB.homEquiv (unhat hPB f) = f := by unfold unhat; simp
  simpa [this] using Eq.symm (RepresentableBy.homEquiv_eq hPB (unhat hPB f))

lemma uniq {A : â„°} (f : B âŠ— A âŸ¶ sc.Î©) (g : A âŸ¶ PB)
    (h : f = B â— g â‰« Îµ hPB) : g = unhat hPB f := by
  have : hat hPB g = f := by rw [â† comm hPB (hat hPB g)]; simp [h]
  simpa using congr(unhat hPB $this)

variable {C PC : â„°} (hPC : IsPowerObjectOf sc C PC)

/-- The morphism `map h` is the functorial action on a morphism `h : B âŸ¶ C`,
    defined as the P-transpose of `ÎµC âˆ˜ (h â¨¯ ğŸ™)`. -/
def map (h : B âŸ¶ C) : PC âŸ¶ PB := unhat hPB ((h â–· PC) â‰« Îµ hPC)

/-- Naturality (dinaturality) of `Îµ`. This corresponds to the naturality square of Îµ
    in MM92 diagram (5). -/
lemma dinaturality (h : B âŸ¶ C) : h â–· PC â‰« Îµ hPC = B â— map hPB hPC h â‰« Îµ hPB :=
  Eq.symm (comm hPB _)

/-- `P` covariantly preserves composition, shown by stacking dinaturality squares. -/
lemma compose {D PD : â„°} (hPD : IsPowerObjectOf sc D PD) (h : B âŸ¶ C) (h' : C âŸ¶ D) :
    map hPB hPD (h â‰« h') = map hPC hPD h' â‰« map hPB hPC h := by
  let comm_outer : h â–· PD â‰« h' â–· PD â‰« Îµ hPD =
      B â— (map _ _ h') â‰« B â— (map _ _ h) â‰« Îµ _ := by
    rw [dinaturality hPC hPD, â† reassoc_of% whisker_exchange h, dinaturality hPB hPC]
  rw [map]; simp
  rw[comm_outer, â† uniq _ _ (map hPC hPD h' â‰« map hPB hPC h) (by aesop_cat)]

/-- A function `P` assigning power objects, turns into a functor `P : â„°áµ’áµ– â¥¤ â„°`. -/
def functor (P : â„° â†’ â„°) (hP : âˆ€ B : â„°, IsPowerObjectOf sc B (P B)) : â„°áµ’áµ– â¥¤ â„° :=
    { obj B := P B.unop,
      map {B C} (h : B âŸ¶ C) := map (hP C.unop) (hP B.unop) h.unop,
      map_id _ := Eq.symm (uniq (hP _) _ _ (by simp)),
      map_comp {B C D} _ _ := compose (hP D.unop) (hP C.unop) (hP B.unop) _ _ }

end PowerObject

open PowerObject

variable (â„°)

/-- An elementary topos is a category with a fixed subobject classifier and power objects. -/
class ElementaryTopos [HasPullbacks â„°] where
  /-- A fixed choice of subobject classifier in `â„°`. -/
  sc : Classifier â„° (ğŸ™_ â„°)
  /-- Assignment of power objects. -/
  P (B : â„°) : â„°
  /-- `P` actually assigns power objects. -/
  hP (B : â„°) : IsPowerObjectOf sc B (P B)

namespace ElementaryTopos

variable {â„°} [HasPullbacks â„°] [ElementaryTopos â„°]

/-- The P-transpose of a morphism `g : A âŸ¶ P B`. -/
def hat {A B : â„°} (g : A âŸ¶ (P B)) : B âŠ— A âŸ¶ sc.Î© := (hP B).homEquiv.toFun g

/-- The P-transpose of a morphism `f : B Ã— A âŸ¶ Î©`. -/
def unhat {A B : â„°} (f : B âŠ— A âŸ¶ sc.Î©) : (A âŸ¶ (P B)) := (hP B).homEquiv.invFun f

@[simp]
lemma hat_unhat {A B : â„°} (f : B âŠ— A âŸ¶ sc.Î©) : hat (unhat f) = f :=
  PowerObject.hat_unhat (hP B) f

@[simp]
lemma unhat_hat {A B : â„°} (g : A âŸ¶ (P B)) : unhat (hat g) = g :=
  PowerObject.unhat_hat (hP B) g

/-- The element relation as a subobject of `B â¨¯ (P B)`. -/
def Îµ (B : â„°) : B âŠ— (P B) âŸ¶ sc.Î© := (hP B).homEquiv.toFun (ğŸ™ (P B))

lemma hatAsComp {A B : â„°} (g : A âŸ¶ (P B)) : hat g = B â— g â‰« Îµ B :=
  PowerObject.hatAsComp (hP B) g

@[simp]
lemma P_comm {A B : â„°} (f : B âŠ— A âŸ¶ sc.Î©) : B â— (unhat f) â‰« Îµ B = f :=
  PowerObject.comm (hP B) f

lemma P_uniq {A B : â„°} (f : B âŠ— A âŸ¶ sc.Î©) (g : A âŸ¶ P B)
    (h : f = B â— g â‰« Îµ B) : g = unhat f := PowerObject.uniq (hP B) f g h

/-- The morphism `map h` is the functorial action on a morphism `h : B âŸ¶ C`,
    defined as the P-transpose of `ÎµC âˆ˜ (h â¨¯ ğŸ™)`. -/
def P_map {B C : â„°} (h : B âŸ¶ C) : (P C) âŸ¶ (P B) :=
  PowerObject.map (hP B) (hP C) h

lemma P_dinaturality {B C : â„°} (h : B âŸ¶ C) : h â–· (P C) â‰« Îµ C = B â— P_map h â‰« Îµ B :=
  PowerObject.dinaturality (hP B) (hP C) h

lemma P_compose {B C D : â„°} (h : B âŸ¶ C) (h' : C âŸ¶ D) : P_map (h â‰« h') = P_map h' â‰« P_map h :=
  PowerObject.compose (hP B) (hP C) (hP D) h h'

/-- The power object functor -/
def P_functor : â„°áµ’áµ– â¥¤ â„° := PowerObject.functor P hP

/--
Given morphisms `g : A âŸ¶ P C` and `h : B âŸ¶ C`, if `g^` is the characteristic map of a subobject
`U â†ª C â¨¯ A`, then the transpose `(Ph âˆ˜ g)^ : B â¨¯ A âŸ¶ Î©` is the characteristic map of the pullback
of `U` along `h â¨¯ ğŸ™`. Flipping the classifier squares to follow the diagram layout in the book.
-/
theorem char_of_pullback {A B C U : â„°} (g : A âŸ¶ P C) (h : B âŸ¶ C) (m : U âŸ¶ C âŠ— A) [Mono m]
    (isChar : hat g = sc.Ï‡ m) :
    hat (g â‰« P_map h) = sc.Ï‡ (pullback.snd m (h â–· A)) := by
  let pb_right := IsPullback.flip (sc.isPullback m)
  let pb_left := IsPullback.of_hasPullback m (h â–· A)
  let pb_outer := IsPullback.paste_horiz pb_left pb_right
  have : h â–· A â‰« hat g = sc.Ï‡ (pullback.snd m (h â–· A)) :=
    sc.uniq (pullback.snd m (h â–· A)) (IsPullback.flip (by simpa [isChar] using pb_outer))
  rw [hatAsComp, MonoidalCategory.whiskerLeft_comp, assoc, â† P_dinaturality]
  rw [reassoc_of% (whisker_exchange h g), â† hatAsComp, this]

end ElementaryTopos
