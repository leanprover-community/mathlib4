import Mathlib.CategoryTheory.Topos.Classifier
import Mathlib.CategoryTheory.Opposites

/-!
# Elementary Topos (in Elementary Form)

This ongoing work formalizes the elementary definition of a topos and the direct consequences.

## References

* [S. MacLane and I. Moerdijk, *Sheaves in Geometry and Logic*][MM92]
-/


noncomputable section

universe u v uâ‚€ vâ‚€

namespace CategoryTheory

open Category Limits Functor

/-- A category `â„°` is an elementary topos if it has finite limits
and satisfies a power object condition relative to a fixed subobject classifier `Î©`.

See MM92, Chapter IV, Section 1. -/

class ElementaryTopos (â„° : Type u) [Category.{v} â„°] [HasFiniteLimits â„°] where

  /-- A fixed choice of subobject classifier in `â„°`, supplying mainly
  `Î©`, `true : âŠ¤_ C âŸ¶ Î©`, and `Ï‡` to build the characteristic map. -/
  hc : Classifier â„°
  /-- The power object functor `P : â„°áµ’áµ– â¥¤ â„°`, defined objectwise. -/
  P (B : â„°) : â„°
  /-- The element relation. -/
  Îµ_ (B : â„°) : B â¨¯ (P B) âŸ¶ hc.Î©
  /-- The P-transpose of a morphism `f : B Ã— A âŸ¶ Î©`. See equation (6) of MM92. -/
  unhat {A B : â„°} (f : B â¨¯ A âŸ¶ hc.Î©) : (A âŸ¶ P B)
  /-- Characteristic equation: any `f : B Ã— A âŸ¶ Î©` is equal to `Îµ_B â‰« (1 â¨¯ g)`
      where `g` is the P-transpose of `f`. -/
  comm {A B : â„°} (f : B â¨¯ A âŸ¶ hc.Î©) :
    f = (prod.map (ğŸ™ B) (unhat f)) â‰« Îµ_ B
  /-- Uniqueness: the P-transpose `g : A âŸ¶ P B` is uniquely determined by `f`. -/
  uniq {A B : â„°} (f : B â¨¯ A âŸ¶ hc.Î©) (g : A âŸ¶ P B)
    (_ : f = (prod.map (ğŸ™ B) g) â‰« Îµ_ B) : g = (unhat f)

variable {â„° : Type u} [Category.{v} â„°] [HasFiniteLimits â„°] [ElementaryTopos â„°]

open ElementaryTopos


/-- The morphism `Îµ_B âˆ˜ (1 Ã— g)` associated to a map `g : A âŸ¶ P B`.
    This is the inverse direction of the transpose isomorphism. -/
def hat {A : â„°} (B : â„°) (g : A âŸ¶ P B) : B â¨¯ A âŸ¶ hc.Î© := prod.map (ğŸ™ B) g â‰« Îµ_ B

/-- The morphism `P_morph h` is the functorial action on a morphism `h : B âŸ¶ C`,
    defined as the P-transpose of `h â¨¯ ğŸ™ âˆ˜ Îµ_C`. -/
def P_morph {B C : â„°} (h : B âŸ¶ C) : P C âŸ¶ P B := unhat ((prod.map h (ğŸ™ _)) â‰« Îµ_ C)

/-- Naturality (dinaturality) of `Îµ`. This corresponds to the naturality square of Îµ
    in MM92 diagram (5). -/
def Îµ_dinaturality {B C : â„°} (h : B âŸ¶ C) :
  prod.map h (ğŸ™ _) â‰« Îµ_ C = prod.map (ğŸ™ _) (P_morph h) â‰« Îµ_ B := comm _

/-- Functoriality of `P`: divide the dinaturality square of `h âˆ˜ h'` into three squares,
    one on the left described by `comm_left`, and two smaller dinaturality squares
    for `h` and `h'` respectively stacked atop of each other on the right. -/
lemma P_compose {B C D : â„°} (h : B âŸ¶ C) (h' : C âŸ¶ D) :
    P_morph (h â‰« h') = P_morph h' â‰« P_morph h :=
  let comm_left : prod.map h (ğŸ™ (P D)) â‰« prod.map (ğŸ™ C) (P_morph h') =
      prod.map (ğŸ™ B) (P_morph h') â‰« prod.map h (ğŸ™ (P C)) := by simp
  let comm_outer : prod.map h (ğŸ™ (P D)) â‰« prod.map h' (ğŸ™ (P D)) â‰« Îµ_ D =
      prod.map (ğŸ™ B) (P_morph h') â‰« prod.map (ğŸ™ B) (P_morph h) â‰« Îµ_ B :=
    by rw [Îµ_dinaturality h', â† assoc, comm_left, assoc, Îµ_dinaturality h]
  let eq : prod.map (ğŸ™ B) (P_morph h') â‰« prod.map (ğŸ™ B) (P_morph h) â‰« Îµ_ B =
      prod.map (ğŸ™ B) (P_morph h' â‰« P_morph h) â‰« Îµ_ B := by rw [â† assoc, prod.map_id_comp]
  by rw [P_morph, prod.map_comp_id, assoc, comm_outer, â† uniq _ _ eq]

open Opposite

/-- The power object functor `P : â„°áµ’áµ– â¥¤ â„°` defined by the transpose correspondence.
    This makes the diagram in MM92 (7) commute. -/
def P_functor : â„°áµ’áµ– â¥¤ â„° := {
  obj B := P (unop B),
  map h := P_morph h.unop,
  map_id B := Eq.symm (uniq _ _ (by rfl)),
  map_comp {B C D : â„°áµ’áµ–} (h : B âŸ¶ C) (h' : C âŸ¶ D) := P_compose h'.unop h.unop
}

/--
Given a morphism `g : A âŸ¶ P C` and a morphism `h : B âŸ¶ C`, the characteristic map
of the composite `g â‰« P_morph h : A âŸ¶ P B` is equal to the pullback of the characteristic
map `hat C g` along the morphism `h Ã— id_A : B Ã— A âŸ¶ C Ã— A`.

This expresses the dinaturality of the `hat` construction, or equivalently,
that the transpose of `P(h) âˆ˜ g` is the pullback of the transpose of `g`
along `h Ã— 1`, as in diagram (8) of the reference.

This result reflects how subobjects pull back along morphisms in an elementary topos,
via the classifier `Î©` and the classifying morphisms `Ï‡`.

It shows that `hat B (g â‰« P_morph h)` is equal to the classifying map
associated to the pullback of the subobject classified by `g`.
-/
theorem pullback_of_char {A B C U : â„°} (g : A âŸ¶ P C) (h : B âŸ¶ C) (m : U âŸ¶ C â¨¯ A) [Mono m]
    (isChar : hat C g = hc.Ï‡ m) :
    hat B (g â‰« P_morph h) = hc.Ï‡ (pullback.snd m (prod.map h (ğŸ™ A))) :=
  let pb_right := IsPullback.flip (hc.isPullback m)
  let m' := pullback.snd m (prod.map h (ğŸ™ A))
  let pb_left := IsPullback.of_hasPullback m (prod.map h (ğŸ™ A))
  let pb_outer := IsPullback.paste_horiz pb_left pb_right
  let eqâ‚€ : prod.map (ğŸ™ _) g â‰« prod.map h (ğŸ™ _) = prod.map h (ğŸ™ _) â‰« prod.map (ğŸ™ _) g := by simp
  let eqâ‚ : (prod.map h (ğŸ™ A)) â‰« (hat _ g) = hc.Ï‡ (pullback.snd m (prod.map h (ğŸ™ A))) :=
    have :  _ â‰« terminal.from U = terminal.from _ := by simp
    hc.uniq m' _ (this â–¸ isChar â–¸ IsPullback.flip pb_outer)
  by rw [hat, prod.map_id_comp, assoc, â† Îµ_dinaturality, â† assoc, eqâ‚€, assoc, â† hat, eqâ‚]

end CategoryTheory
