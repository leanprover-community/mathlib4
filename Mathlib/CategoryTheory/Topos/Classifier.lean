/-
Copyright (c) 2024 Charlie Conneen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Charlie Conneen
-/
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq
import Mathlib.CategoryTheory.Limits.Shapes.RegularMono
import Mathlib.CategoryTheory.Subobject.Basic

/-!

# Subobject Classifier

We define what it means for a morphism in a category to be a subobject classifier as
`CategoryTheory.HasClassifier`.

c.f. the following Lean 3 code, where similar work was done:
https://github.com/b-mehta/topos/blob/master/src/subobject_classifier.lean

## Main definitions

Let `C` refer to a category with a terminal object.

* `CategoryTheory.Classifier C` is the data of a subobject classifier in `C`.

* `CategoryTheory.HasClassifier C` says that there is at least one subobject classifier.
  `Î© C` denotes a choice of subobject classifier.

* `CategoryTheory.Subobject.cmap` sends every subobject of `X` to its characteristic map of type
  `X âŸ¶ Î© C`.

* `CategoryTheory.Subobject.compr` sends every map `Ï† : X âŸ¶ Î© C` to the subobject of `X` whose
  characteristic map is `Ï†` by pulling back the truth morphism along `Ï†`. This generalizes the
  construction of a subset "by comprehension" from its characteristic function in set theory.

* `CategoryTheory.Subobject.sub C` is the presheaf that sends every object `X : C` to its category
  of subobjects `Subobject X`, and every morphism `f : X âŸ¶ Y` to the function `Subobject Y â†’
  Subobject X` that maps every subobject of `Y` to its pullback along `f`.

## Main results

* It is a theorem that the truth morphism `âŠ¤_ C âŸ¶ Î© C` is a (split, and therefore regular)
  monomorphism, simply because its source is the terminal object.

* An instance of `IsRegularMonoCategory C` is exhibited for any category with a subobject
  classifier.

* `CategoryTheory.Classifier.representableBy`: any subobject classifier `Î©` in `C` represents the
  subobjects functor `CategoryTheory.Subobject.sub C`.

* `CategoryTheory.Classifier.fromRepresentation`: any representation `Î©` of
  `CategoryTheory.Subobject.sub C` is a subobject classifier in `C`.

* `CategoryTheory.hasClassifier_isRepresentable_iff`: from the two above mappings, we get that a
  category `C` has a subobject classifier if and only if the subobjects presheaf
  `CategoryTheory.Subobject.sub C` is representable (Proposition 1 in Section I.3 of [MM92]).

## References

* [S. MacLane and I. Moerdijk, *Sheaves in Geometry and Logic*][MLM92]

## TODO

* Make API for constructing a subobject classifier without reference to limits (replacing `âŠ¤_ C`
  with an arbitrary `Î©â‚€ : C` and including the assumption `mono truth`)

-/

universe u v uâ‚€ vâ‚€

open CategoryTheory Category Limits Functor

variable (C : Type u) [Category.{v} C] [HasTerminal C]

namespace CategoryTheory

/-- A morphism `truth : âŠ¤_ C âŸ¶ Î©` from the terminal object of a category `C`
is a subobject classifier if, for every monomorphism `m : U âŸ¶ X` in `C`,
there is a unique map `Ï‡ : X âŸ¶ Î©` such that the following square is a pullback square:
```
      U ---------m----------> X
      |                       |
terminal.from U               Ï‡
      |                       |
      v                       v
    âŠ¤_ C ------truth--------> Î©
```
An equivalent formulation replaces the object `âŠ¤_ C` with an arbitrary object, and instead
includes the assumption that `truth` is a monomorphism.
-/
structure Classifier where
  /-- The target of the truth morphism -/
  Î© : C
  /-- the truth morphism for a subobject classifier -/
  truth : âŠ¤_ C âŸ¶ Î©
  /-- For any monomorphism `U âŸ¶ X`, there is an associated characteristic map `X âŸ¶ Î©`. -/
  Ï‡ {U X : C} (m : U âŸ¶ X) [Mono m] : X âŸ¶ Î©
  /-- `Ï‡ m` forms the appropriate pullback square. -/
  isPullback {U X : C} (m : U âŸ¶ X) [Mono m] : IsPullback m (terminal.from U) (Ï‡ m) truth
  /-- `Ï‡ m` is the only map `X âŸ¶ Î©` which forms the appropriate pullback square. -/
  uniq {U X : C} (m : U âŸ¶ X) [Mono m] (Ï‡' : X âŸ¶ Î©)
      (hÏ‡' : IsPullback m (terminal.from U) Ï‡' truth) :
    Ï‡' = Ï‡ m

/-- A category `C` has a subobject classifier if there is at least one subobject classifier. -/
class HasClassifier : Prop where
  /-- There is some classifier. -/
  exists_classifier : Nonempty (Classifier C)

namespace HasClassifier

variable [HasClassifier C]

noncomputable section

/-- Notation for the object in an arbitrary choice of a subobject classifier -/
abbrev Î© : C := HasClassifier.exists_classifier.some.Î©

/-- Notation for the "truth arrow" in an arbitrary choice of a subobject classifier -/
abbrev truth : âŠ¤_ C âŸ¶ Î© C := HasClassifier.exists_classifier.some.truth

variable {C}
variable {U X : C} (m : U âŸ¶ X) [Mono m]

/-- returns the characteristic morphism of the subobject `(m : U âŸ¶ X) [Mono m]` -/
def Ï‡ : X âŸ¶ Î© C :=
  HasClassifier.exists_classifier.some.Ï‡ m

/-- The diagram
```
      U ---------m----------> X
      |                       |
terminal.from U              Ï‡ m
      |                       |
      v                       v
    âŠ¤_ C -----truth C-------> Î©
```
is a pullback square.
-/
lemma isPullback_Ï‡ : IsPullback m (terminal.from U) (Ï‡ m) (truth C) :=
  HasClassifier.exists_classifier.some.isPullback m

/-- The diagram
```
      U ---------m----------> X
      |                       |
terminal.from U              Ï‡ m
      |                       |
      v                       v
    âŠ¤_ C -----truth C-------> Î©
```
commutes.
-/
@[reassoc]
lemma comm : m â‰« Ï‡ m = terminal.from _ â‰« truth C := (isPullback_Ï‡ m).w

/-- `Ï‡ m` is the only map for which the associated square
is a pullback square.
-/
lemma unique (Ï‡' : X âŸ¶ Î© C) (hÏ‡' : IsPullback m (terminal.from _) Ï‡' (truth C)) : Ï‡' = Ï‡ m :=
  HasClassifier.exists_classifier.some.uniq m Ï‡' hÏ‡'

/-- `truth C` is a regular monomorphism (because it is split). -/
noncomputable instance truthIsRegularMono : RegularMono (truth C) :=
  RegularMono.ofIsSplitMono (truth C)

/-- The following diagram
```
      U ---------m----------> X
      |                       |
terminal.from U              Ï‡ m
      |                       |
      v                       v
    âŠ¤_ C -----truth C-------> Î©
```
being a pullback for any monic `m` means that every monomorphism
in `C` is the pullback of a regular monomorphism; since regularity
is stable under base change, every monomorphism is regular.
Hence, `C` is a regular mono category.
It also follows that `C` is a balanced category.
-/
instance isRegularMonoCategory : IsRegularMonoCategory C where
  regularMonoOfMono :=
    fun m => âŸ¨regularOfIsPullbackFstOfRegular (isPullback_Ï‡ m).w (isPullback_Ï‡ m).isLimitâŸ©

/-- If the source of a faithful functor has a subobject classifier, the functor reflects
  isomorphisms. This holds for any balanced category.
-/
instance reflectsIsomorphisms (D : Type uâ‚€) [Category.{vâ‚€} D] (F : C â¥¤ D) [Functor.Faithful F] :
    Functor.ReflectsIsomorphisms F :=
  reflectsIsomorphisms_of_reflectsMonomorphisms_of_reflectsEpimorphisms F

/-- If the source of a faithful functor is the opposite category of one with a subobject classifier,
  the same holds -- the functor reflects isomorphisms.
-/
instance reflectsIsomorphismsOp (D : Type uâ‚€) [Category.{vâ‚€} D] (F : Cáµ’áµ– â¥¤ D)
    [Functor.Faithful F] :
    Functor.ReflectsIsomorphisms F :=
  reflectsIsomorphisms_of_reflectsMonomorphisms_of_reflectsEpimorphisms F

end
end HasClassifier

/-! ### The subobjects presheaf `sub` -/

section SubPresheaf

namespace Subobject

variable [HasPullbacks C]

/-- `sub` is the presheaf that sends every object `X : C` to its category of subobjects
    `Subobject X`, and every morphism `f : X âŸ¶ Y` to the function `Subobject Y â†’ Subobject X`
    that maps every subobject of `Y` to its pullback along `f`. -/
noncomputable def sub : Cáµ’áµ– â¥¤ Type (max u v) where
  obj X := (@Subobject C _ X.unop)

  map f := (pullback f.unop).obj

  map_id := by
    simp only
    intro X
    funext
    rw [unop_id, pullback_id]
    simp

  map_comp := by
    simp only
    intro X Y Z f g
    funext
    rw [unop_comp, pullback_comp]
    simp

end Subobject
end SubPresheaf

/-! ### The representability theorem of subobject classifiers -/

section Representability

open HasClassifier

namespace Subobject

variable {C : Type u} [Category.{v} C] [HasTerminal C] {ğ’ : Classifier C}

/-- `x.cmap` is the unique characteristic map of subobject `x` given by the `IsClassifier` property.
-/
noncomputable def cmap {X : C} (x : Subobject X) : X âŸ¶ ğ’.Î© :=
  ğ’.Ï‡ x.arrow

variable [HasPullbacks C]

/-- `compr Ï‡` builds the subobject whose characteristic map is `Ï†` by pulling back `truth` along
    `Ï†`. This generalizes the construction of a subset "by comprehension" from its characteristic
    function in set theory. -/
noncomputable def compr {X : C} (Ï† : X âŸ¶ ğ’.Î©) : Subobject X :=
  (pullback Ï†).obj (mk ğ’.truth)

lemma compr_isPullback {X : C} (Ï† : X âŸ¶ ğ’.Î©) :
    IsPullback (compr Ï†).arrow (terminal.from (compr Ï† : C)) Ï† ğ’.truth := by
  have h := IsPullback.of_hasPullback ğ’.truth Ï†
  let i : (compr Ï† : C) â‰… Limits.pullback ğ’.truth Ï† := underlyingIso _
  apply IsPullback.flip
  apply IsPullback.of_iso1 h i.symm _ _ <;> try aesop_cat
  have heq : (compr Ï†).arrow = (mk (pullback.snd ğ’.truth Ï†)).arrow := by rfl
  simp [heq, i]

lemma compr_cmap {X : C} (x : Subobject X) :
    compr (ğ’ := ğ’) x.cmap = x := by
  have h : IsPullback x.arrow (terminal.from (x : C)) (cmap x) ğ’.truth :=
    ğ’.isPullback x.arrow
  have h' : IsPullback (compr (ğ’ := ğ’) x.cmap).arrow (terminal.from (compr x.cmap : C))
                       x.cmap ğ’.truth := by
    apply compr_isPullback
  apply IsPullback.flip at h
  apply IsPullback.flip at h'
  apply isPullback_eq h' h

lemma cmap_compr {X : C} (Ï† : X âŸ¶ ğ’.Î©) :
    (compr Ï†).cmap = Ï† := by
  have h := compr_isPullback Ï†
  have h' := compr_isPullback (ğ’ := ğ’) (compr Ï†).cmap
  rw [compr_cmap] at h'
  conv => rhs; rw [ğ’.uniq (compr Ï†).arrow Ï† h]
  simp [cmap]

end Subobject

variable {C : Type u} [Category.{v} C] [HasTerminal C] [HasPullbacks C]

namespace Classifier

open Subobject

/-- Any subobject classifier `Î©` represents the subobjects functor `sub`. -/
noncomputable def representableBy (ğ’ : Classifier C) : (sub C).RepresentableBy ğ’.Î© := by
  exact {
    /- The correspondence `compr` sending each map `Ï† : X âŸ¶ Î©` to the corresponding subobject is a
       bijection with `cmap` as inverse. -/
    homEquiv := {
      toFun := compr
      invFun := cmap
      left_inv := cmap_compr
      right_inv := compr_cmap
    }
    /- Furthermore, this bijection is natural by the fact that two pullback squares placed side by
       side yield a pullback rectangle (lemma `Subobject.pullback_comp`). -/
    homEquiv_comp := by
      intro X X' f g
      dsimp
      simp [sub, compr, pullback_comp]
  }

/-- Any representation `Î©` of `sub` gives a classifier with truth values object `Î©`. -/
noncomputable def fromRepresentation (Î© : C) (h : (sub C).RepresentableBy Î©) : Classifier C := by
  obtain âŸ¨Î¸, hÎ¸âŸ© := h

  /- Each subobject `x` of `X` corresponds to a morphism `Ï†â‚“ : X âŸ¶ Î©` through `Î¸`. -/
  let Ï† := fun {X} (x : Subobject X) â†¦ Î¸.symm x
  have hÏ† : âˆ€ {X} (Ï‡ : X âŸ¶ Î©), Ï‡ = Ï† (Î¸ Ï‡) := by simp [Ï†]

  /- Some subobject `Î©â‚€` of `Î©` corresponds to the identity `ğŸ™ Î©` through `Î¸`. -/
  let Î©â‚€ : Subobject Î© := Î¸ (ğŸ™ Î©)
  /- Let `tâ‚€` be the underlying monomorphism of `Î©â‚€` (this requires the axiom of choice). -/
  let tâ‚€ := Î©â‚€.arrow
  have tâ‚€_mono : Mono tâ‚€ := inferInstance

  /- The naturality of `Î¸` (hypothesis `hÎ¸`) states that `x = sub Ï†â‚“ Î©â‚€` for any `x`. -/
  have hx_pullback : âˆ€ {X} (x : Subobject X), x = (Subobject.pullback (Ï† x)).obj Î©â‚€ := by
    intro X x
    have := hÎ¸ (Î¸.symm x) (ğŸ™ _)
    simp only [Category.comp_id, Equiv.apply_symm_apply] at this
    rw (occs := .pos [1]) [this]
    simp [sub, Ï†, Î©â‚€]

  /- More explicitly, `x` is the canonical representative of the pullback of `tâ‚€` along `Ï†â‚“`. -/
  have hx_mk : âˆ€ {X} (x : Subobject X), x = Subobject.mk (pullback.snd tâ‚€ (Ï† x)) := by
    intro X x
    rw (occs := .pos [1]) [hx_pullback x, pullback_obj]

  /- Even more explicitly, we have an isomorphism `Î¹â‚“` between the underlying object `(x : C)` of
     `x` in `C` (obtained through the axiom of choice) and the pullback of `tâ‚€` and `Ï†â‚“`. -/
  let Î¹ : âˆ€ {X} (x : Subobject X), (x : C) â‰… Limits.pullback tâ‚€ (Ï† x) := by
    intro X x
    rw (occs := .pos [1]) [hx_mk x]
    exact (underlyingIso (pullback.snd tâ‚€ (Ï† x)))

  /- Let `Ï€â‚“ : x âŸ¶ Î©â‚€` be the first projection of the pullback of `tâ‚€` and `Ï†â‚“` modulo `Î¹â‚“`. -/
  let Ï€ := fun {X} (x : Subobject X) â†¦ (Î¹ x).hom â‰« pullback.fst tâ‚€ (Ï† x)

  /- We can finally state that the corresponding pullback square commutes (diagram (5) in [MM92]).

     Here we need to deal with the usual "transport hell" of dependent types, which materializes
     in Lean under the guise of the heterogenous equality type `HEq`. This is because the types of
     morphisms are *propositionally* equal rather than *definitionally* equal, which in turns is
     caused by the need to explicitly manipulate isomorphisms. Univalence would probably make
     things much easier.
  -/
  have isPullback_Ï† : âˆ€ {X} (x : Subobject X), IsPullback (Ï€ x) x.arrow tâ‚€ (Ï† x) := by
    intro X x
    have h := isPullback_mk tâ‚€ (Ï† x)
    have hx := hx_mk x
    dsimp at h
    rw (occs := .pos [1,2,3]) [hx]
    have h1 : ((underlyingIso (pullback.snd tâ‚€ (Ï† x))).hom â‰« pullback.fst tâ‚€ (Ï† x)) =
              (Ï€ (Subobject.mk (pullback.snd tâ‚€ (Ï† x)))) := by
      congr; try exact hx
      dsimp [Î¹]
      set hc := Eq.symm (congrArg (fun _a â†¦ underlying.obj _a â‰… Limits.pullback tâ‚€
                                            (Ï† (Subobject.mk (pullback.snd tâ‚€ (Ï† x)))))
                                  (hx_mk (Subobject.mk (pullback.snd tâ‚€ (Ï† x)))))
      have := cast_heq hc ((underlyingIso (pullback.snd tâ‚€
                                           (Ï† (Subobject.mk (pullback.snd tâ‚€ (Ï† x)))))))
      symm
      apply HEq.trans this
      symm
      congr
    rw [â† h1]
    exact h

  /- Furthermore, `Ï†â‚“` is the unique morphism that makes this pullback square commute by
     bijectivity and naturality of `Î¸`.

     Note that we actually generalize `Ï€â‚“` to any morphism `Ïˆ : x âŸ¶ Î©â‚€`, which will be necessary
     many times later on in the proof.
  -/
  have isPullback_uniq : âˆ€ {X} (x : Subobject X) Ïˆ Ï‡, IsPullback Ïˆ x.arrow tâ‚€ Ï‡ â†’ Ï‡ = Ï† x := by
    intro X x Ïˆ Ï‡ hÏ‡
    rw [hÏ† Ï‡]
    congr
    specialize hÎ¸ Ï‡ (ğŸ™ _)
    rw [Category.comp_id] at hÎ¸
    rw [hÎ¸]
    dsimp [sub]
    rw [pullback_obj, isPullback_eq_mk hÏ‡]
    rfl

  /- It remains to show that `Î©â‚€` is actually a terminal object in `C`. -/
  have isTerminal_Î©â‚€ : IsTerminal (Î©â‚€ : C) := by
    have : (X : C) â†’ Unique (X âŸ¶ Î©â‚€) := by
      intro X
      /- Taking `x` to be the (canonical representative of) the identity `ğŸ™ X`... -/
      let x := Subobject.mk (ğŸ™ X)
      /- ... gives a map `Ï†' : X âŸ¶ Î©â‚€` (modulo the canonical isomorphism `i : X â‰… x`). -/
      let i : X â‰… x := by dsimp [x]; exact (underlyingIso (ğŸ™ X)).symm
      let Ï†' := (i.hom â‰« Ï€ x)

      /- We show that every `Ï†'' : X âŸ¶ Î©â‚€` is equal to `Ï†'`. -/
      refine { default := Ï†', uniq := ?_ }
      intro Ï†''
      dsimp [default]

      /- Since `tâ‚€` is a monomorphism, every `Ïˆ : X âŸ¶ Î©â‚€` forms a "trivial" pullback square. -/
      have hÏˆ : âˆ€ Ïˆ, IsPullback Ïˆ (ğŸ™ X) tâ‚€ (Ïˆ â‰« tâ‚€) := by
        intro Ïˆ
        constructor
        Â· constructor
          apply PullbackCone.IsLimit.mk (lift := fun c â†¦ c.snd) <;> intro c
          Â· apply Mono.right_cancellation (f := tâ‚€)
            rw [c.condition]
            simp
          Â· simp
          Â· intro m hm1 hm2
            rw [â† hm2]
            simp
        Â· simp

      /- This applies in particular to `Ï†` and `Ï†'`. -/
      have h' := hÏˆ Ï†'
      have h'' := hÏˆ Ï†''

      /- This square has the same shape as (5) (modulo the iso `i`), hence by the uniqueness of
         `Ï†â‚“` in (5) we get `tâ‚€ âˆ˜ Ï†' = tâ‚€ âˆ˜ Ï†''`. -/
      have hÏ†' := isPullback_uniq x (Ï€ x) (Ï†' â‰« tâ‚€)
      have hÏ†'' := isPullback_uniq x (i.inv â‰« Ï†'') (Ï†'' â‰« tâ‚€)
      have h : Ï†' â‰« tâ‚€ = Ï†'' â‰« tâ‚€ := by
        rw [hÏ†', hÏ†'']
        Â· apply IsPullback.of_iso1 h'' i
          Â· simp
          Â· simp [i, x]
        Â· apply IsPullback.of_iso1 h' i
          Â· simp only [Ï†']
          Â· simp [i, x]
      /- As `tâ‚€` is monic, this gives `Ï†' = Ï†''`. -/
      exact Mono.right_cancellation _ _ h.symm
    apply IsTerminal.ofUnique

  /- We need to give explicitly the iso `i` with the "canonical" terminal object `âŠ¤_ C`. -/
  have i : âŠ¤_ C â‰… Î©â‚€ := by
    apply IsTerminal.uniqueUpToIso
    exact terminalIsTerminal
    exact isTerminal_Î©â‚€

  /- Finally, we can give `Î©â‚€` as the subobject classifier with `tâ‚€` as truth morphism (modulo `i`)
     and `Ï† âŸ¦mâŸ§` as characteristic map for every monomorphism `m`.  -/
  exact {
    Î© := Î©
    truth := i.hom â‰« tâ‚€
    Ï‡ := fun m â†¦ Ï† (Subobject.mk m)
    isPullback := by
      intro U X m hm
      apply IsPullback.flip
      have h : IsPullback (Ï€ (Subobject.mk m)) (Subobject.mk m).arrow tâ‚€ (Ï† (Subobject.mk m)) :=
        isPullback_Ï† (Subobject.mk m)
      apply IsPullback.of_iso12 h (underlyingIso m) i.symm
      Â· apply Unique.eq (uniqueToTerminal _)
      Â· simp
      Â· simp
    uniq := by
      intro U X m hm Ï‡' hÏ‡'
      dsimp
      apply IsPullback.flip at hÏ‡'
      apply isPullback_uniq (Subobject.mk m) ((underlyingIso m).hom â‰« terminal.from U â‰« i.hom)
      apply IsPullback.of_iso12 hÏ‡' (underlyingIso m).symm i <;> simp
  }

end Classifier

/-- A category has a subobject classifier if and only if the subobjects functor is representable. -/
theorem isRepresentable_hasClassifier_iff :
    HasClassifier C â†” (Subobject.sub C).IsRepresentable := by
  constructor <;> intro h
  Â· obtain âŸ¨âŸ¨ğ’âŸ©âŸ© := h
    apply RepresentableBy.isRepresentable
    exact ğ’.representableBy
  Â· obtain âŸ¨Î©, âŸ¨hâŸ©âŸ© := h
    constructor; constructor
    exact Classifier.fromRepresentation Î© h

end Representability
end CategoryTheory
