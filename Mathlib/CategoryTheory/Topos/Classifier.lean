/-
Copyright (c) 2024 Charlie Conneen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Charlie Conneen, Pablo Donato
-/
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq
import Mathlib.CategoryTheory.Limits.Shapes.RegularMono
import Mathlib.CategoryTheory.Subobject.Basic
import Mathlib.CategoryTheory.Subobject.Presheaf

/-!

# Subobject Classifier

We define what it means for a morphism in a category to be a subobject classifier as
`CategoryTheory.HasClassifier`.

c.f. the following Lean 3 code, where similar work was done:
https://github.com/b-mehta/topos/blob/master/src/subobject_classifier.lean

## Main definitions

Let `C` refer to a category with a terminal object.

* `CategoryTheory.Classifier C` is the data of a subobject classifier in `C`.

* `CategoryTheory.HasClassifier C` says that there is at least one subobject classifier.
  `Î© C` denotes a choice of subobject classifier.

* `CategoryTheory.Classifier.cmap` sends every subobject of `X` to its characteristic map of type
  `X âŸ¶ Î© C`.

* `CategoryTheory.Classifier.compr` sends every map `Ï† : X âŸ¶ Î© C` to the subobject of `X` whose
  characteristic map is `Ï†` by pulling back the truth morphism along `Ï†`. This generalizes the
  construction of a subset "by comprehension" from its characteristic function in set theory.

## Main results

* It is a theorem that the truth morphism `âŠ¤_ C âŸ¶ Î© C` is a (split, and therefore regular)
  monomorphism, simply because its source is the terminal object.

* An instance of `IsRegularMonoCategory C` is exhibited for any category with a subobject
  classifier.

* `CategoryTheory.Classifier.representableBy`: any subobject classifier `Î©` in `C` represents the
  subobjects functor `CategoryTheory.Subobject.presheaf C`.

* `CategoryTheory.Classifier.fromRepresentation`: any representation `Î©` of
  `CategoryTheory.Subobject.presheaf C` is a subobject classifier in `C`.

* `CategoryTheory.hasClassifier_isRepresentable_iff`: from the two above mappings, we get that a
  category `C` has a subobject classifier if and only if the subobjects presheaf
  `CategoryTheory.Subobject.presheaf C` is representable (Proposition 1 in Section I.3 of [MM92]).

## References

* [S. MacLane and I. Moerdijk, *Sheaves in Geometry and Logic*][MM92]

## TODO

* Make API for constructing a subobject classifier without reference to limits (replacing `âŠ¤_ C`
  with an arbitrary `Î©â‚€ : C` and including the assumption `mono truth`)

-/

universe u v uâ‚€ vâ‚€

open CategoryTheory Category Limits Functor

variable (C : Type u) [Category.{v} C] [HasTerminal C]

namespace CategoryTheory

/-- A morphism `truth : âŠ¤_ C âŸ¶ Î©` from the terminal object of a category `C`
is a subobject classifier if, for every monomorphism `m : U âŸ¶ X` in `C`,
there is a unique map `Ï‡ : X âŸ¶ Î©` such that the following square is a pullback square:
```
      U ---------m----------> X
      |                       |
terminal.from U               Ï‡
      |                       |
      v                       v
    âŠ¤_ C ------truth--------> Î©
```
An equivalent formulation replaces the object `âŠ¤_ C` with an arbitrary object, and instead
includes the assumption that `truth` is a monomorphism.
-/
structure Classifier where
  /-- The target of the truth morphism -/
  Î© : C
  /-- the truth morphism for a subobject classifier -/
  truth : âŠ¤_ C âŸ¶ Î©
  /-- For any monomorphism `U âŸ¶ X`, there is an associated characteristic map `X âŸ¶ Î©`. -/
  Ï‡ {U X : C} (m : U âŸ¶ X) [Mono m] : X âŸ¶ Î©
  /-- `Ï‡ m` forms the appropriate pullback square. -/
  isPullback {U X : C} (m : U âŸ¶ X) [Mono m] : IsPullback m (terminal.from U) (Ï‡ m) truth
  /-- `Ï‡ m` is the only map `X âŸ¶ Î©` which forms the appropriate pullback square. -/
  uniq {U X : C} (m : U âŸ¶ X) [Mono m] (Ï‡' : X âŸ¶ Î©)
      (hÏ‡' : IsPullback m (terminal.from U) Ï‡' truth) :
    Ï‡' = Ï‡ m

/-- A category `C` has a subobject classifier if there is at least one subobject classifier. -/
class HasClassifier : Prop where
  /-- There is some classifier. -/
  exists_classifier : Nonempty (Classifier C)

namespace HasClassifier

variable [HasClassifier C]

noncomputable section

/-- Notation for the object in an arbitrary choice of a subobject classifier -/
abbrev Î© : C := HasClassifier.exists_classifier.some.Î©

/-- Notation for the "truth arrow" in an arbitrary choice of a subobject classifier -/
abbrev truth : âŠ¤_ C âŸ¶ Î© C := HasClassifier.exists_classifier.some.truth

variable {C}
variable {U X : C} (m : U âŸ¶ X) [Mono m]

/-- returns the characteristic morphism of the subobject `(m : U âŸ¶ X) [Mono m]` -/
def Ï‡ : X âŸ¶ Î© C :=
  HasClassifier.exists_classifier.some.Ï‡ m

/-- The diagram
```
      U ---------m----------> X
      |                       |
terminal.from U              Ï‡ m
      |                       |
      v                       v
    âŠ¤_ C -----truth C-------> Î©
```
is a pullback square.
-/
lemma isPullback_Ï‡ : IsPullback m (terminal.from U) (Ï‡ m) (truth C) :=
  HasClassifier.exists_classifier.some.isPullback m

/-- The diagram
```
      U ---------m----------> X
      |                       |
terminal.from U              Ï‡ m
      |                       |
      v                       v
    âŠ¤_ C -----truth C-------> Î©
```
commutes.
-/
@[reassoc]
lemma comm : m â‰« Ï‡ m = terminal.from _ â‰« truth C := (isPullback_Ï‡ m).w

/-- `Ï‡ m` is the only map for which the associated square
is a pullback square.
-/
lemma unique (Ï‡' : X âŸ¶ Î© C) (hÏ‡' : IsPullback m (terminal.from _) Ï‡' (truth C)) : Ï‡' = Ï‡ m :=
  HasClassifier.exists_classifier.some.uniq m Ï‡' hÏ‡'

/-- `truth C` is a regular monomorphism (because it is split). -/
noncomputable instance truthIsRegularMono : RegularMono (truth C) :=
  RegularMono.ofIsSplitMono (truth C)

/-- The following diagram
```
      U ---------m----------> X
      |                       |
terminal.from U              Ï‡ m
      |                       |
      v                       v
    âŠ¤_ C -----truth C-------> Î©
```
being a pullback for any monic `m` means that every monomorphism
in `C` is the pullback of a regular monomorphism; since regularity
is stable under base change, every monomorphism is regular.
Hence, `C` is a regular mono category.
It also follows that `C` is a balanced category.
-/
instance isRegularMonoCategory : IsRegularMonoCategory C where
  regularMonoOfMono :=
    fun m => âŸ¨regularOfIsPullbackFstOfRegular (isPullback_Ï‡ m).w (isPullback_Ï‡ m).isLimitâŸ©

/-- If the source of a faithful functor has a subobject classifier, the functor reflects
  isomorphisms. This holds for any balanced category.
-/
instance reflectsIsomorphisms (D : Type uâ‚€) [Category.{vâ‚€} D] (F : C â¥¤ D) [Functor.Faithful F] :
    Functor.ReflectsIsomorphisms F :=
  reflectsIsomorphisms_of_reflectsMonomorphisms_of_reflectsEpimorphisms F

/-- If the source of a faithful functor is the opposite category of one with a subobject classifier,
  the same holds -- the functor reflects isomorphisms.
-/
instance reflectsIsomorphismsOp (D : Type uâ‚€) [Category.{vâ‚€} D] (F : Cáµ’áµ– â¥¤ D)
    [Functor.Faithful F] :
    Functor.ReflectsIsomorphisms F :=
  reflectsIsomorphisms_of_reflectsMonomorphisms_of_reflectsEpimorphisms F

end
end HasClassifier

/-! ### The representability theorem of subobject classifiers -/

section Representability

/-! #### Lemmas about pullbacks and subobjects -/

section SubobjectIsPullback

open Subobject

variable {C : Type u} [Category.{v} C]

lemma eqOfIsPullback {X Y Z : C} {x x' : Subobject X}
    {f : X âŸ¶ Z} {g : Y âŸ¶ Z} {k : (x : C) âŸ¶ Y} {k' : (x' : C) âŸ¶ Y}
    (h : IsPullback k x.arrow g f) (h' : IsPullback k' x'.arrow g f) :
    x = x' :=
  eq_of_comm (IsPullback.isoIsPullback _ _ h h') (by simp)

lemma arrow_isPullback {X Y Z : C}
    (f : Y âŸ¶ Z) (g : X âŸ¶ Z) [HasPullback f g] [Mono f] :
    IsPullback ((underlyingIso _).hom â‰« pullback.fst f g) (mk (pullback.snd f g)).arrow f g :=
  IsPullback.of_iso (IsPullback.of_hasPullback f g)
    (underlyingIso _).symm (Iso.refl _) (Iso.refl _) (Iso.refl _)
    (by simp) (by simp) (by simp) (by simp)

end SubobjectIsPullback

namespace Classifier

open Subobject HasClassifier

variable {C : Type u} [Category.{v} C] [HasTerminal C] {ğ’ : Classifier C}

/-! #### From classifiers to representations -/

section RepresentableBy

/-- `x.cmap` is the unique characteristic map of subobject `x` given by the `IsClassifier` property.
-/
noncomputable def cmap {X : C} (x : Subobject X) : X âŸ¶ ğ’.Î© :=
  ğ’.Ï‡ x.arrow

variable [HasPullbacks C]

/-- `compr Ï‡` builds the subobject whose characteristic map is `Ï†` by pulling back `truth` along
    `Ï†`. This generalizes the construction of a subset "by comprehension" from its characteristic
    function in set theory. -/
noncomputable def compr {X : C} (Ï† : X âŸ¶ ğ’.Î©) : Subobject X :=
  (Subobject.pullback Ï†).obj (Subobject.mk ğ’.truth)

lemma compr_isPullback {X : C} (Ï† : X âŸ¶ ğ’.Î©) :
    IsPullback (compr Ï†).arrow (terminal.from (compr Ï† : C)) Ï† ğ’.truth := by
  have h := IsPullback.of_hasPullback ğ’.truth Ï†
  let i : (compr Ï† : C) â‰… Limits.pullback ğ’.truth Ï† := underlyingIso _
  apply IsPullback.flip
  apply IsPullback.of_iso h
    (underlyingIso _).symm (Iso.refl _) (Iso.refl _) (Iso.refl _)
    <;> try aesop_cat
  have heq : (compr Ï†).arrow = (Subobject.mk (pullback.snd ğ’.truth Ï†)).arrow := by rfl
  simp [heq]

lemma compr_cmap {X : C} (x : Subobject X) :
    compr (ğ’ := ğ’) (cmap x) = x := by
  have h : IsPullback x.arrow (terminal.from (x : C)) (cmap x) ğ’.truth :=
    ğ’.isPullback x.arrow
  have h' : IsPullback (compr (ğ’ := ğ’) (cmap x)).arrow (terminal.from (compr (cmap x) : C))
                       (cmap x) ğ’.truth := by
    apply compr_isPullback
  apply IsPullback.flip at h
  apply IsPullback.flip at h'
  exact eqOfIsPullback h' h

lemma cmap_compr {X : C} (Ï† : X âŸ¶ ğ’.Î©) :
    cmap (compr Ï†) = Ï† := by
  have h := compr_isPullback Ï†
  conv => rhs; rw [ğ’.uniq (compr Ï†).arrow Ï† h]
  simp [cmap]

variable {C : Type u} [Category.{v} C] [HasTerminal C] [HasPullbacks C]

/-- Any subobject classifier `Î©` represents the subobjects functor `sub`. -/
noncomputable def representableBy (ğ’ : Classifier C) :
    (Subobject.presheaf C).RepresentableBy ğ’.Î© := by
  exact {
    /- The correspondence `compr` sending each map `Ï† : X âŸ¶ Î©` to the corresponding subobject is a
       bijection with `cmap` as inverse. -/
    homEquiv := {
      toFun := compr
      invFun := cmap
      left_inv := cmap_compr
      right_inv := compr_cmap
    }
    /- Furthermore, this bijection is natural by the fact that two pullback squares placed side by
       side yield a pullback rectangle (lemma `Subobject.pullback_comp`). -/
    homEquiv_comp := by
      intro X X' f g
      dsimp
      simp [compr, pullback_comp]
  }

end RepresentableBy

/-! #### From representations to classifiers -/

section FromRepresentation

variable {C : Type u} [Category.{v} C] [HasPullbacks C]

abbrev SubobjectRepresentableBy X := (Subobject.presheaf C).RepresentableBy X

variable {Î© : C} (h : SubobjectRepresentableBy Î©)

namespace SubobjectRepresentableBy

/-- Representability of `Subobject.presheaf C` by `Î©` gives us a natural bijection `Î¸`. -/
def Î¸ {X : C} : (X âŸ¶ Î©) â‰ƒ Subobject X :=
  h.homEquiv
lemma hÎ¸ {X X' : C} (f : X âŸ¶ X') (g : X' âŸ¶ Î©) :
    Î¸ h (f â‰« g) = (Subobject.presheaf C).map f.op (Î¸ h g) :=
  h.homEquiv_comp f g

/-- Each subobject `x` of `X` corresponds to a morphism `Ï†â‚“ : X âŸ¶ Î©` through `Î¸`. -/
def Ï† {X : C} (x : Subobject X) : X âŸ¶ Î© := (Î¸ h).symm x
lemma hÏ† {X : C} (Ï‡ : X âŸ¶ Î©) : Ï‡ = Ï† h (Î¸ h Ï‡) := by simp [Ï†]

/-- Some subobject `Î©â‚€` of `Î©` corresponds to the identity `ğŸ™ Î©` through `Î¸`. -/
def Î©â‚€ : Subobject Î© := Î¸ h (ğŸ™ Î©)

/-- Let `tâ‚€` be the underlying monomorphism of `Î©â‚€` (this requires the axiom of choice). -/
noncomputable def tâ‚€ : (Î©â‚€ h : C) âŸ¶ Î© := (Î©â‚€ h).arrow
instance tâ‚€_mono : Mono (tâ‚€ h) := arrow_mono (Î©â‚€ h)

/-- The naturality of `Î¸` (hypothesis `hÎ¸`) states that `x = Ï†â‚“â‹† Î©â‚€` for any `x`. -/
lemma hx_pullback {X : C} (x : Subobject X) :
    x = (Subobject.pullback (Ï† h x)).obj (Î©â‚€ h) := by
  have := hÎ¸ h ((Î¸ h).symm x) (ğŸ™ _)
  simp only [Category.comp_id, Equiv.apply_symm_apply] at this
  rw (occs := .pos [1]) [this]
  simp [Subobject.presheaf, Ï†, Î©â‚€]

/-- More explicitly, `x` is the canonical representative of the pullback of `tâ‚€` along `Ï†â‚“`. -/
lemma hx_mk {X : C} (x : Subobject X) :
    x = Subobject.mk (pullback.snd (tâ‚€ h) (Ï† h x)) := by
  rw (occs := .pos [1]) [hx_pullback h x, pullback_obj]
  dsimp [tâ‚€]

/-- Even more explicitly, we have an isomorphism `Î¹â‚“` between the underlying object `(x : C)` of
    `x` in `C` (obtained through the axiom of choice) and the pullback of `tâ‚€` and `Ï†â‚“`. -/
noncomputable def Î¹ {X : C} (x : Subobject X) :
    (x : C) â‰… Limits.pullback (tâ‚€ h) (Ï† h x) := by
  rw (occs := .pos [1]) [hx_mk h x]
  exact (underlyingIso (pullback.snd (tâ‚€ h) (Ï† h x)))

/-- Let `Ï€â‚“ : x âŸ¶ Î©â‚€` be the first projection of the pullback of `tâ‚€` and `Ï†â‚“` modulo `Î¹â‚“`. -/
noncomputable def Ï€ {X : C} (x : Subobject X) :=
  (Î¹ h x).hom â‰« pullback.fst (tâ‚€ h) (Ï† h x)

instance {X : C} (x : Subobject X) : Mono (pullback.snd (tâ‚€ h) (Ï† h x)) := inferInstance

/-- We can finally state that the corresponding pullback square commutes (diagram (5) in [MM92]).

    Implementation note: Here we need to deal with the usual "transport hell" of dependent types,
    which materializes in Lean under the guise of the heterogenous equality type `HEq`. This is
    because the types of morphisms are *propositionally* equal rather than *definitionally* equal,
    which in turn is caused by the need to explicitly manipulate isomorphisms. Univalence would
    probably make things much easier.
-/
lemma isPullback_Ï† {X : C} (x : Subobject X) :
    IsPullback (Ï€ h x) x.arrow (tâ‚€ h) (Ï† h x) := by
  have hx := hx_mk h x
  rw (occs := .pos [1,2,3]) [hx]
  have h1 : ((underlyingIso (pullback.snd (tâ‚€ h) (Ï† h x))).hom â‰« pullback.fst (tâ‚€ h) (Ï† h x)) =
            (Ï€ h (Subobject.mk (pullback.snd (tâ‚€ h) (Ï† h x)))) := by
    congr; try exact hx
    dsimp [Î¹]
    set hc := Eq.symm (congrArg (fun _a â†¦ underlying.obj _a â‰… Limits.pullback (tâ‚€ h)
                                          (Ï† h (Subobject.mk (pullback.snd (tâ‚€ h) (Ï† h x)))))
                                (hx_mk h (Subobject.mk (pullback.snd (tâ‚€ h) (Ï† h x)))))
    have := cast_heq hc ((underlyingIso (pullback.snd (tâ‚€ h)
                                         (Ï† h (Subobject.mk (pullback.snd (tâ‚€ h) (Ï† h x)))))))
    symm
    apply HEq.trans this
    symm
    congr
  rw [â† h1]
  exact arrow_isPullback (tâ‚€ h) (Ï† h x)

/-- Furthermore, `Ï†â‚“` is the unique morphism that makes this pullback square commute by
    bijectivity and naturality of `Î¸`.

    Note that we actually generalize `Ï€â‚“` to any morphism `Ïˆ : x âŸ¶ Î©â‚€`, which will be necessary
    many times later on in the proof.
-/
lemma isPullback_uniq {X : C} (x : Subobject X) Ïˆ Ï‡ (hÏ‡ : IsPullback Ïˆ x.arrow (tâ‚€ h) Ï‡) :
    Ï‡ = Ï† h x := by
  rw [hÏ† h Ï‡]
  congr
  have hÎ¸ := hÎ¸ h Ï‡ (ğŸ™ _)
  rw [Category.comp_id] at hÎ¸
  rw [hÎ¸]
  dsimp [Subobject.presheaf]
  rw [pullback_obj, eqOfIsPullback hÏ‡ (arrow_isPullback _ _)]
  rfl

/-- It remains to show that `Î©â‚€` is actually a terminal object in `C`. -/
noncomputable def isTerminal_Î©â‚€ : IsTerminal (Î©â‚€ h : C) := by
  have : (X : C) â†’ Unique (X âŸ¶ Î©â‚€ h) := by
    intro X
    /- Taking `x` to be the (canonical representative of) the identity `ğŸ™ X`... -/
    let x := Subobject.mk (ğŸ™ X)
    /- ... gives a map `Ï†' : X âŸ¶ Î©â‚€` (modulo the canonical isomorphism `i : X â‰… x`). -/
    let i : X â‰… x := by dsimp [x]; exact (underlyingIso (ğŸ™ X)).symm
    let Ï†' := (i.hom â‰« Ï€ h x)

    /- We show that every `Ï†'' : X âŸ¶ Î©â‚€` is equal to `Ï†'`. -/
    refine { default := Ï†', uniq := ?_ }
    intro Ï†''
    dsimp [default]

    /- Since `tâ‚€` is a monomorphism, every `Ïˆ : X âŸ¶ Î©â‚€` forms a "trivial" pullback square. -/
    have hÏˆ : âˆ€ Ïˆ, IsPullback Ïˆ (ğŸ™ X) (tâ‚€ h) (Ïˆ â‰« tâ‚€ h) := by
      intro Ïˆ
      constructor
      Â· constructor
        apply PullbackCone.IsLimit.mk (lift := fun c â†¦ c.snd) <;> intro c
        Â· apply Mono.right_cancellation (f := tâ‚€ h)
          rw [c.condition]
          simp
        Â· simp
        Â· intro m hm1 hm2
          rw [â† hm2]
          simp
      Â· simp

    /- This applies in particular to `Ï†` and `Ï†'`. -/
    have h' := hÏˆ Ï†'
    have h'' := hÏˆ Ï†''

    /- This square has the same shape as (5) (modulo the iso `i`), hence by the uniqueness of
       `Ï†â‚“` in (5) we get `tâ‚€ âˆ˜ Ï†' = tâ‚€ âˆ˜ Ï†''`. -/
    have hÏ†' := isPullback_uniq h x (Ï€ h x) (Ï†' â‰« tâ‚€ h)
    have hÏ†'' := isPullback_uniq h x (i.inv â‰« Ï†'') (Ï†'' â‰« tâ‚€ h)
    have h : Ï†' â‰« tâ‚€ h = Ï†'' â‰« tâ‚€ h := by
      rw [hÏ†', hÏ†'']
      Â· exact IsPullback.of_iso h'' i (Iso.refl _) (Iso.refl _) (Iso.refl _)
          (by simp) (by simp [i, x]) (by simp) (by simp)
      Â· exact IsPullback.of_iso h' i (Iso.refl _) (Iso.refl _) (Iso.refl _)
          (by simp [Ï†']) (by simp [i, x]) (by simp) (by simp)
    /- As `tâ‚€` is monic, this gives `Ï†' = Ï†''`. -/
    exact Mono.right_cancellation _ _ h.symm
  apply IsTerminal.ofUnique

/-- We need to give explicitly the iso `i` with the "canonical" terminal object `âŠ¤_ C`. -/
noncomputable def i [HasTerminal C] : âŠ¤_ C â‰… Î©â‚€ h := by
  apply IsTerminal.uniqueUpToIso
  Â· exact terminalIsTerminal
  Â· exact isTerminal_Î©â‚€ h

end SubobjectRepresentableBy

open SubobjectRepresentableBy

/-- Any representation `Î©` of `Subobject.presheaf C` gives a classifier with truth values object
    `Î©`. -/
noncomputable def fromRepresentation [HasTerminal C] : Classifier C := by
  /- Finally, we can give `Î©â‚€` as the subobject classifier with `tâ‚€` as truth morphism (modulo `i`)
     and `Ï† âŸ¦mâŸ§` as characteristic map for every monomorphism `m`.  -/
  exact {
    Î© := Î©
    truth := (i h).hom â‰« tâ‚€ h
    Ï‡ := fun m â†¦ Ï† h (Subobject.mk m)

    isPullback := by
      intro U X m hm
      apply IsPullback.flip
      have h' := isPullback_Ï† h (Subobject.mk m)
      exact IsPullback.of_iso h'
        (underlyingIso m) (i h).symm (Iso.refl _) (Iso.refl _)
        (Subsingleton.elim _ _) (by simp) (by simp) (by simp)

    uniq := by
      intro U X m hm Ï‡' hÏ‡'
      dsimp
      apply IsPullback.flip at hÏ‡'
      apply isPullback_uniq h (Subobject.mk m) ((underlyingIso m).hom â‰« terminal.from U â‰« (i h).hom)
      exact IsPullback.of_iso hÏ‡'
        (underlyingIso m).symm (i h) (Iso.refl _) (Iso.refl _)
        (by simp) (by simp) (by simp) (by simp)
  }

end FromRepresentation
end Classifier

/-- A category has a subobject classifier if and only if the subobjects functor is representable. -/
theorem isRepresentable_hasClassifier_iff [HasPullbacks C] :
    HasClassifier C â†” (Subobject.presheaf C).IsRepresentable := by
  constructor <;> intro h
  Â· obtain âŸ¨âŸ¨ğ’âŸ©âŸ© := h
    apply RepresentableBy.isRepresentable
    exact ğ’.representableBy
  Â· obtain âŸ¨Î©, âŸ¨hâŸ©âŸ© := h
    constructor; constructor
    exact Classifier.fromRepresentation h

end Representability
end CategoryTheory
