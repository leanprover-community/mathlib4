/-
Copyright (c) 2024 Charlie Conneen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Charlie Conneen, Pablo Donato
-/
import Mathlib.CategoryTheory.Limits.Shapes.RegularMono
import Mathlib.CategoryTheory.Functor.ReflectsIso.Balanced
import Mathlib.CategoryTheory.Subobject.Presheaf

/-!

# Subobject Classifier

We define what it means for a morphism in a category to be a subobject classifier as
`CategoryTheory.HasClassifier`.

c.f. the following Lean 3 code, where similar work was done:
https://github.com/b-mehta/topos/blob/master/src/subobject_classifier.lean

## Main definitions

Let `C` refer to a category with a terminal object.

* `CategoryTheory.Classifier C` is the data of a subobject classifier in `C`.

* `CategoryTheory.HasClassifier C` says that there is at least one subobject classifier.
  `Î© C` denotes a choice of subobject classifier.

## Main results

* It is a theorem that the truth morphism `âŠ¤_ C âŸ¶ Î© C` is a (split, and therefore regular)
  monomorphism, simply because its source is the terminal object.

* An instance of `IsRegularMonoCategory C` is exhibited for any category with a subobject
  classifier.

* `CategoryTheory.Classifier.representableBy`: any subobject classifier `Î©` in `C` represents the
  subobjects functor `CategoryTheory.Subobject.presheaf C`.

* `CategoryTheory.Classifier.SubobjectRepresentableBy.classifier`: any representation `Î©` of
  `CategoryTheory.Subobject.presheaf C` is a subobject classifier in `C`.

* `CategoryTheory.hasClassifier_isRepresentable_iff`: from the two above mappings, we get that a
  category `C` has a subobject classifier if and only if the subobjects presheaf
  `CategoryTheory.Subobject.presheaf C` is representable (Proposition 1 in Section I.3 of [MM92]).

## References

* [S. MacLane and I. Moerdijk, *Sheaves in Geometry and Logic*][MM92]

## TODO

* Make API for constructing a subobject classifier without reference to limits (replacing `âŠ¤_ C`
  with an arbitrary `Î©â‚€ : C` and including the assumption `mono truth`)

-/

universe u v uâ‚€ vâ‚€

namespace CategoryTheory

open Category Limits Functor

variable (C : Type u) [Category.{v} C] [HasTerminal C]

/-- A morphism `truth : âŠ¤_ C âŸ¶ Î©` from the terminal object of a category `C`
is a subobject classifier if, for every monomorphism `m : U âŸ¶ X` in `C`,
there is a unique map `Ï‡ : X âŸ¶ Î©` such that the following square is a pullback square:
```
      U ---------m----------> X
      |                       |
terminal.from U               Ï‡
      |                       |
      v                       v
    âŠ¤_ C ------truth--------> Î©
```
An equivalent formulation replaces the object `âŠ¤_ C` with an arbitrary object, and instead
includes the assumption that `truth` is a monomorphism.
-/
structure Classifier where
  /-- The target of the truth morphism -/
  Î© : C
  /-- the truth morphism for a subobject classifier -/
  truth : âŠ¤_ C âŸ¶ Î©
  /-- For any monomorphism `U âŸ¶ X`, there is an associated characteristic map `X âŸ¶ Î©`. -/
  Ï‡ {U X : C} (m : U âŸ¶ X) [Mono m] : X âŸ¶ Î©
  /-- `Ï‡ m` forms the appropriate pullback square. -/
  isPullback {U X : C} (m : U âŸ¶ X) [Mono m] : IsPullback m (terminal.from U) (Ï‡ m) truth
  /-- `Ï‡ m` is the only map `X âŸ¶ Î©` which forms the appropriate pullback square. -/
  uniq {U X : C} (m : U âŸ¶ X) [Mono m] (Ï‡' : X âŸ¶ Î©)
      (hÏ‡' : IsPullback m (terminal.from U) Ï‡' truth) :
    Ï‡' = Ï‡ m

/-- A category `C` has a subobject classifier if there is at least one subobject classifier. -/
class HasClassifier : Prop where
  /-- There is some classifier. -/
  exists_classifier : Nonempty (Classifier C)

namespace HasClassifier

variable [HasClassifier C]

noncomputable section

/-- Notation for the object in an arbitrary choice of a subobject classifier -/
abbrev Î© : C := HasClassifier.exists_classifier.some.Î©

/-- Notation for the "truth arrow" in an arbitrary choice of a subobject classifier -/
abbrev truth : âŠ¤_ C âŸ¶ Î© C := HasClassifier.exists_classifier.some.truth

variable {C}
variable {U X : C} (m : U âŸ¶ X) [Mono m]

/-- returns the characteristic morphism of the subobject `(m : U âŸ¶ X) [Mono m]` -/
def Ï‡ : X âŸ¶ Î© C :=
  HasClassifier.exists_classifier.some.Ï‡ m

/-- The diagram
```
      U ---------m----------> X
      |                       |
terminal.from U              Ï‡ m
      |                       |
      v                       v
    âŠ¤_ C -----truth C-------> Î©
```
is a pullback square.
-/
lemma isPullback_Ï‡ : IsPullback m (terminal.from U) (Ï‡ m) (truth C) :=
  HasClassifier.exists_classifier.some.isPullback m

/-- The diagram
```
      U ---------m----------> X
      |                       |
terminal.from U              Ï‡ m
      |                       |
      v                       v
    âŠ¤_ C -----truth C-------> Î©
```
commutes.
-/
@[reassoc]
lemma comm : m â‰« Ï‡ m = terminal.from _ â‰« truth C := (isPullback_Ï‡ m).w

/-- `Ï‡ m` is the only map for which the associated square
is a pullback square.
-/
lemma unique (Ï‡' : X âŸ¶ Î© C) (hÏ‡' : IsPullback m (terminal.from _) Ï‡' (truth C)) : Ï‡' = Ï‡ m :=
  HasClassifier.exists_classifier.some.uniq m Ï‡' hÏ‡'

/-- `truth C` is a regular monomorphism (because it is split). -/
noncomputable instance truthIsRegularMono : RegularMono (truth C) :=
  RegularMono.ofIsSplitMono (truth C)

/-- The following diagram
```
      U ---------m----------> X
      |                       |
terminal.from U              Ï‡ m
      |                       |
      v                       v
    âŠ¤_ C -----truth C-------> Î©
```
being a pullback for any monic `m` means that every monomorphism
in `C` is the pullback of a regular monomorphism; since regularity
is stable under base change, every monomorphism is regular.
Hence, `C` is a regular mono category.
It also follows that `C` is a balanced category.
-/
instance isRegularMonoCategory : IsRegularMonoCategory C where
  regularMonoOfMono :=
    fun m => âŸ¨regularOfIsPullbackFstOfRegular (isPullback_Ï‡ m).w (isPullback_Ï‡ m).isLimitâŸ©

/-- If the source of a faithful functor has a subobject classifier, the functor reflects
  isomorphisms. This holds for any balanced category.
-/
instance reflectsIsomorphisms (D : Type uâ‚€) [Category.{vâ‚€} D] (F : C â¥¤ D) [Functor.Faithful F] :
    Functor.ReflectsIsomorphisms F :=
  reflectsIsomorphisms_of_reflectsMonomorphisms_of_reflectsEpimorphisms F

/-- If the source of a faithful functor is the opposite category of one with a subobject classifier,
  the same holds -- the functor reflects isomorphisms.
-/
instance reflectsIsomorphismsOp (D : Type uâ‚€) [Category.{vâ‚€} D] (F : Cáµ’áµ– â¥¤ D)
    [Functor.Faithful F] :
    Functor.ReflectsIsomorphisms F :=
  reflectsIsomorphisms_of_reflectsMonomorphisms_of_reflectsEpimorphisms F

end
end HasClassifier

/-! ### The representability theorem of subobject classifiers -/

section Representability

namespace Classifier

open Subobject

/-! #### From classifiers to representations -/

section RepresentableBy

variable {C : Type u} [Category.{v} C] [HasTerminal C] [HasPullbacks C] (ğ’ : Classifier C)

lemma surjective_Ï‡ {X : C} (Ï† : X âŸ¶ ğ’.Î©) :
    âˆƒ (Z : C) (i : Z âŸ¶ X) (_ : Mono i), Ï† = ğ’.Ï‡ i :=
  âŸ¨Limits.pullback Ï† ğ’.truth, pullback.fst _ _, inferInstance, ğ’.uniq _ _ (by
    convert IsPullback.of_hasPullback Ï† ğ’.truth
    apply Subsingleton.elim)âŸ©

@[simp]
lemma pullback_Ï‡_obj_mk_truth {Z X : C} (i : Z âŸ¶ X) [Mono i] :
    (Subobject.pullback (ğ’.Ï‡ i)).obj (.mk ğ’.truth) = .mk i :=
  Subobject.pullback_obj_mk (ğ’.isPullback i).flip

@[simp]
lemma Ï‡_pullback_obj_mk_truth_arrow {X : C} (Ï† : X âŸ¶ ğ’.Î©) :
    ğ’.Ï‡ ((Subobject.pullback Ï†).obj (.mk ğ’.truth)).arrow = Ï† := by
  obtain âŸ¨Z, i, _, rflâŸ© := ğ’.surjective_Ï‡ Ï†
  refine (ğ’.uniq _ _ ?_).symm
  refine (IsPullback.of_hasPullback ğ’.truth (ğ’.Ï‡ i)).flip.of_iso
    (underlyingIso _).symm (Iso.refl _) (Iso.refl _) (Iso.refl _)
    ?_ (Subsingleton.elim _ _) (by simp) (by simp)
  dsimp
  rw [Iso.eq_inv_comp, comp_id, underlyingIso_hom_comp_eq_mk]
  rfl

/-- Any subobject classifier `Î©` represents the subobjects functor `Subobject.presheaf`. -/
noncomputable def representableBy :
    (Subobject.presheaf C).RepresentableBy ğ’.Î© where
  homEquiv := {
    toFun Ï† := (Subobject.pullback Ï†).obj (Subobject.mk ğ’.truth)
    invFun x := ğ’.Ï‡ x.arrow
    left_inv Ï† := by simp
    right_inv x := by simp
  }
  homEquiv_comp _ _ := by simp [pullback_comp]

end RepresentableBy

/-! #### From representations to classifiers -/

section FromRepresentation

variable {C : Type u} [Category.{v} C] [HasPullbacks C] (Î© : C)

/-- Abbreviation to enable dot notation on the hypothesis `h` stating that the subobjects presheaf
    is representable by some object `Î©`. -/
abbrev SubobjectRepresentableBy := (Subobject.presheaf C).RepresentableBy Î©

variable {Î©} (h : SubobjectRepresentableBy Î©)

namespace SubobjectRepresentableBy

/-- `h.Î©â‚€` is the subobject of `Î©` which corresponds to the identity `ğŸ™ Î©`,
    given `h : SubobjectRepresentableBy Î©`. -/
def Î©â‚€ : Subobject Î© := h.homEquiv (ğŸ™ Î©)

/-- `h.homEquiv` acts like an "object comprehension" operator: it maps any characteristic map
    `f : X âŸ¶ Î©` to the associated subobject of `X`, obtained by pulling back `h.Î©â‚€` along `f`. -/
lemma homEquiv_eq {X : C} (f : X âŸ¶ Î©) :
    h.homEquiv f = (Subobject.pullback f).obj h.Î©â‚€ := by
  simpa using h.homEquiv_comp f (ğŸ™ _)

/-- For any subobject `x`, the pullback of `h.Î©â‚€` along the characteristic map of `x`
    given by `h.homEquiv` is `x` itself. -/
lemma pullback_homEquiv_symm_obj_Î©â‚€ {X : C} (x : Subobject X) :
    (Subobject.pullback (h.homEquiv.symm x)).obj h.Î©â‚€ = x := by
  rw [â† homEquiv_eq, Equiv.apply_symm_apply]

section

variable {U X : C} (m : U âŸ¶ X) [Mono m]

/-- `h.Ï‡ m` is the characteristic map of monomorphism `m` given by the bijection `h.homEquiv`. -/
def Ï‡ : X âŸ¶ Î© := h.homEquiv.symm (Subobject.mk m)

/-- `h.iso m` is the isomorphism between `m` and the pullback of `Î©â‚€`
    along the characteristic map of `m`. -/
noncomputable def iso : MonoOver.mk' m â‰…
    Subobject.representative.obj ((Subobject.pullback (h.Ï‡ m)).obj h.Î©â‚€) :=
  (Subobject.representativeIso (.mk' m)).symm â‰ªâ‰« Subobject.representative.mapIso
    (eqToIso (h.pullback_homEquiv_symm_obj_Î©â‚€ (.mk m)).symm)

/-- `h.Ï€ m` is the first projection in the following pullback square:

    ```
    U --h.Ï€ m--> (Î©â‚€ : C)
    |                |
    m             Î©â‚€.arrow
    |                |
    v                v
    X -----h.Ï‡ m---> Î©
    ```
-/
noncomputable def Ï€ : U âŸ¶ Subobject.underlying.obj h.Î©â‚€ :=
  (h.iso m).hom.left â‰« Subobject.pullbackÏ€ (h.Ï‡ m) h.Î©â‚€

@[reassoc (attr := simp)]
lemma iso_inv_left_Ï€ :
    (h.iso m).inv.left â‰« h.Ï€ m = Subobject.pullbackÏ€ (h.Ï‡ m) h.Î©â‚€ := by
  dsimp only [Ï€]
  rw [â† Over.comp_left_assoc]
  convert Category.id_comp _ using 2
  exact (MonoOver.forget _ â‹™ Over.forget _ ).congr_map (h.iso m).inv_hom_id

@[reassoc (attr := simp)]
lemma iso_inv_left_comp :
    (h.iso m).inv.left â‰« m =
      ((Subobject.pullback (h.Ï‡ m)).obj h.Î©â‚€).arrow :=
  MonoOver.w (h.iso m).inv

lemma isPullback {U X : C} (m : U âŸ¶ X) [Mono m] :
    IsPullback m (h.Ï€ m) (h.Ï‡ m) h.Î©â‚€.arrow := by
  fapply (Subobject.isPullback (h.Ï‡ m) h.Î©â‚€).flip.of_iso
    (((MonoOver.forget _ â‹™ Over.forget _).mapIso (h.iso m)).symm) (Iso.refl _)
    (Iso.refl _) (Iso.refl _)
  all_goals simp [MonoOver.forget]

variable {m}
lemma uniq {Ï‡' : X âŸ¶ Î©} {Ï€ : U âŸ¶ h.Î©â‚€}
    (sq : IsPullback m Ï€ Ï‡' h.Î©â‚€.arrow) : Ï‡' = h.Ï‡ m := by
  apply h.homEquiv.injective
  simp only [Ï‡, Equiv.apply_symm_apply, homEquiv_eq]
  simpa using Subobject.pullback_obj_mk sq.flip

end

/-- The main non-trivial result: `h.Î©â‚€` is actually a terminal object. -/
noncomputable def isTerminalÎ©â‚€ : IsTerminal (h.Î©â‚€ : C) :=
  IsTerminal.ofUniqueHom (fun X â†¦ h.Ï€ (ğŸ™ X)) (fun X Ï€' â†¦ by
    have : IsPullback (ğŸ™ X) Ï€' (Ï€' â‰« h.Î©â‚€.arrow) h.Î©â‚€.arrow :=
      { w := by simp
        isLimit' := âŸ¨PullbackCone.IsLimit.mk _ (fun s â†¦ s.fst) (by simp)
          (fun s â†¦ by rw [â† cancel_mono h.Î©â‚€.arrow, â† s.condition, Category.assoc])
          (fun s m hm _ â†¦ by simpa using hm) âŸ© }
    rw [â† cancel_mono h.Î©â‚€.arrow, h.uniq this,
      â† (h.isPullback (ğŸ™ X)).w, Category.id_comp])

include h in
lemma hasTerminal : HasTerminal C := h.isTerminalÎ©â‚€.hasTerminal

variable [HasTerminal C]

/-- `h.isoÎ©â‚€` is the unique isomorphism from `h.Î©â‚€` to the canonical terminal object `âŠ¤_ C`. -/
noncomputable def isoÎ©â‚€ : (h.Î©â‚€ : C) â‰… âŠ¤_ C :=
  h.isTerminalÎ©â‚€.conePointUniqueUpToIso (limit.isLimit _)

/-- Any representation `Î©` of `Subobject.presheaf C` gives a subobject classifier with truth values
    object `Î©`. -/
noncomputable def classifier : Classifier C where
  Î© := Î©
  truth := h.isoÎ©â‚€.inv â‰« h.Î©â‚€.arrow
  Ï‡ m _ := h.Ï‡ m
  isPullback m _ :=
    (h.isPullback m).of_iso (Iso.refl _) (Iso.refl _) h.isoÎ©â‚€ (Iso.refl _)
      (by simp) (Subsingleton.elim _ _) (by simp) (by simp)
  uniq {U X} m _ Ï‡' sq := by
    have : IsPullback m (h.isTerminalÎ©â‚€.from U) Ï‡' h.Î©â‚€.arrow :=
      sq.of_iso (Iso.refl _) (Iso.refl _) (h.isoÎ©â‚€.symm) (Iso.refl _)
        (by simp) (h.isTerminalÎ©â‚€.hom_ext _ _) (by simp) (by simp)
    exact h.uniq this

end SubobjectRepresentableBy
end FromRepresentation
end Classifier

/-- A category has a subobject classifier if and only if the subobjects functor is representable. -/
theorem isRepresentable_hasClassifier_iff [HasPullbacks C] :
    HasClassifier C â†” (Subobject.presheaf C).IsRepresentable := by
  constructor <;> intro h
  Â· obtain âŸ¨âŸ¨ğ’âŸ©âŸ© := h
    apply RepresentableBy.isRepresentable
    exact ğ’.representableBy
  Â· obtain âŸ¨Î©, âŸ¨hâŸ©âŸ© := h
    constructor; constructor
    exact Classifier.SubobjectRepresentableBy.classifier h

end Representability
end CategoryTheory
