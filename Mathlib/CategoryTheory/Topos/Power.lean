/-
Copyright (c) 2024 Charlie Conneen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Charlie Conneen
-/
import Mathlib.CategoryTheory.Topos.Classifier
import Mathlib.CategoryTheory.ChosenFiniteProducts

/-!
# Power Objects

We Define power objects for a category `C` with a subobject classifier and chosen finite products.

## Main definitions

Let `C` be a category with a terminal object, a subobject classifier, and chosen finite products.

* For an object `B : C`, `CategoryTheory.PowerObject B` contains the data
  of a power object for `B`.

* `CategoryTheory.HasPowerObjects C` says that every object in `C` has a power object.

* If `C` has all power objects, then there is a functor `powFunctor C : Cáµ’áµ– â¥¤ C` which
  sends an object `B : C` to its power object `ğ’« B`, and sends a morphism `f : A âŸ¶ B` to the
  corresponding "inverse image" morphism `fâ»Â¹ : ğ’« B âŸ¶ ğ’« A`.

## Main results

* `powFunctor C` is self adjoint, in the sense that its adjoint is the corresponding
  functor `powFunctorOp C : C â¥¤ Cáµ’áµ–`. This adjunction is defined as `powSelfAdj C`.

## Notation

* `ğ’« B` is notation for `ğ’« B`, which is a choice of a power object for `B`.

* if `f : B âŠ— A âŸ¶ Î© C` is a morphism in a category with power objects, then
  `f^` denotes the corresponding morphism `A âŸ¶ ğ’« B`, and `^f` denotes the corresponding
  morphism `B âŸ¶ ğ’« A`.

* If `g : A âŸ¶ ğ’« B` is a morphism in a category with power objects, then
  `g^` denotes the corresponding morphism `B âŠ— A âŸ¶ Î© C`.

* To "curry" maps in the other coordinate, put the caret `^` before the function argument
  instead of after.

* If `f : A âŸ¶ B` is a morphism in a category with power objects, then `fâ»Â¹` denotes
  the corresponding map `inverseImage f : ğ’« B âŸ¶ ğ’« A`.

## References

* [S. MacLane and I. Moerdijk, *Sheaves in Geometry and Logic*][MLM92]

## TODO

* Show that a cartesian closed category with a subobject classifier has power objects.

-/

universe u v

open CategoryTheory Category Limits HasClassifier MonoidalCategory

namespace CategoryTheory

variable {C : Type u} [Category.{v} C] [HasTerminal C] [HasClassifier C]
variable [ChosenFiniteProducts C]

/-- An object `PB` and a map `in_B : B âŠ— PB âŸ¶ Î© C` form a power object for `B : C`
if, for any map `f : B âŠ— A âŸ¶ Î© C`, there is a unique map `f^ : A âŸ¶ PB` such that
the following diagram commutes:
```
        B âŠ— A ---f---> Î© C
          |             ^
          |            /
          |           /
       B â— f^       /
          |         /
          |       in_B
          |       /
          |      /
          |     /
          |    /
          v   /
        B âŠ— PB
```
-/
structure PowerObject (B : C) where
  /-- The power object associated to `B` -/
  pow : C
  /-- The membership predicate for `B` -/
  in_ : B âŠ— pow âŸ¶ Î© C
  /-- The transpose of a map `B âŠ— A âŸ¶ Î© C` -/
  transpose {A} (f : B âŠ— A âŸ¶ Î© C) : A âŸ¶ pow
  /-- the characterizing property of the transpose -/
  comm {A} (f : B âŠ— A âŸ¶ Î© C) : B â— (transpose f) â‰« in_ = f
  /-- `transpose f` is the only map which satisfies the commutativity condition -/
  uniq {A} {f : B âŠ— A âŸ¶ Î© C} {hat' : A âŸ¶ pow} (hat'_comm : B â— hat' â‰« in_ = f) :
      transpose f = hat'


variable (C)

/-- A category has power objects if each of its objects has a power object. -/
class HasPowerObjects : Prop where
  /-- Each `B : C` has a power object. -/
  has_power_object (B : C) : Nonempty (PowerObject B)

namespace HasPowerObjects

variable {C}
variable [HasPowerObjects C]

noncomputable section

/-- Notation for a choice of power object of an object. -/
abbrev pow (B : C) : C := (HasPowerObjects.has_power_object B).some.pow

@[inherit_doc]
prefix:100 "ğ’«" => pow

/-- Notation for the predicate "b âˆˆ S" as a map `B âŠ— ğ’« B âŸ¶ Î©`. -/
abbrev in_ (B : C) : B âŠ— ğ’« B âŸ¶ Î© C := (HasPowerObjects.has_power_object B).some.in_

/-- The map Hom(BâŠ—A,Î©) â†’ Hom(A,P(B)).
This is currying the left argument.
-/
def transpose {A B : C} (f : B âŠ— A âŸ¶ Î© C) : A âŸ¶ ğ’« B :=
  (HasPowerObjects.has_power_object B).some.transpose f

/-- Notation for the `transpose` operator, which curries the left argument -/
scoped postfix:82 "^" => transpose

/-- The characterizing property of `transpose` -/
@[reassoc (attr := simp)]
lemma comm (B) {A : C} (f : B âŠ— A âŸ¶ Î© C) : B â— f^ â‰« in_ B = f :=
  (HasPowerObjects.has_power_object B).some.comm f

/-- `transpose` is the only map which satisfies the commutativity condition. -/
lemma unique (B) {A : C} {f : B âŠ— A âŸ¶ Î© C} {hat' : A âŸ¶ ğ’« B} (hat'_comm : B â— hat' â‰« in_ B = f ) :
    f^ = hat' :=
  (HasPowerObjects.has_power_object B).some.uniq hat'_comm

/-- Un-currying on the left. The inverse of `HasPowerObjects.transpose. -/
def transposeInv {B A : C} (f : A âŸ¶ ğ’« B) : B âŠ— A âŸ¶ Î© C :=
  B â— f â‰« in_ B

/-- Notation for the `transposeInv` operator, which un-curries on the left -/
scoped postfix:82 "^" => transposeInv

/-- Equivalence between Hom(BâŠ—A,Î©) and Hom(A,P(B)). -/
def transposeEquiv (A B : C) : (B âŠ— A âŸ¶ Î© C) â‰ƒ (A âŸ¶ ğ’« B) where
  toFun := transpose
  invFun := transposeInv
  left_inv := fun _ => comm _ _
  right_inv := fun _ => unique _ rfl

variable {A B : C}

/-- `transposeInv` is a left inverse of `transpose`. -/
@[simp]
lemma transpose_left_inv (f : B âŠ— A âŸ¶ Î© C) : (f^)^ = f :=
  (transposeEquiv _ _).left_inv _

/-- `transposeInv` is a right inverse of `transpose`. -/
@[simp]
lemma transpose_right_inv (f : A âŸ¶ ğ’« B) : (f^)^ = f :=
  (transposeEquiv _ _).right_inv _

/-- The map Hom(BâŠ—A,Î©) â†’ Hom(B,P(A)).
This is currying the right argument.
-/
def transposeSymm (f : B âŠ— A âŸ¶ Î© C) : B âŸ¶ ğ’« A :=
  transpose ((Î²_ A B).hom â‰« f)

/-- Notation for the `transposeSymm` operator, which curries the right argument -/
scoped prefix:82 "^" => transposeSymm

/-- Un-currying on the right. -/
def transposeSymmInv (f : B âŸ¶ ğ’« A) : B âŠ— A âŸ¶ Î© C :=
  (Î²_ A B).inv â‰« (transposeInv f)

/-- Notation for the `transposeSymmInv` operator, which un-curries on the right -/
scoped prefix:82 "^" => transposeSymmInv

variable (A B)

/-- Equivalence between Hom(Bâ¨¯A,Î©) and Hom(B,P(A)). -/
def transposeEquivSymm : (B âŠ— A âŸ¶ Î© C) â‰ƒ (B âŸ¶ ğ’« A) where
  toFun := transposeSymm
  invFun := transposeSymmInv
  left_inv := by
    intro
    simp [comm, transposeSymm, transposeSymmInv]
  right_inv := by
    intro
    apply unique
    simp [transposeSymmInv]
    rfl

variable {A B : C}

/-- `transposeSymmInv` is the left inverse of `transposeSymm`. -/
@[simp]
lemma transpose_symm_left_inv (f : B âŠ— A âŸ¶ Î© C) :
    ^(^f) = f :=
  (transposeEquivSymm _ _).left_inv _

/-- `transposeSymmInv` is the right inverse of `transposeSymm`. -/
@[simp]
lemma transpose_symm_right_inv {B A : C} (f : B âŸ¶ ğ’« A) :
    (^(^f)) = f :=
  (transposeEquivSymm _ _).right_inv _

/-- Equivalence between Hom(A,P(B)) and Hom(B, P(A)).
This is just the composition of `transposeEquiv` and `transposeEquivSymm`.
-/
def transposeTransposeEquiv (A B : C) : (B âŸ¶ ğ’« A) â‰ƒ (A âŸ¶ ğ’« B) :=
  Equiv.trans (transposeEquivSymm A B).symm (transposeEquiv A B)

/-- The power object functor's action on arrows.
Sends `h : A âŸ¶ B` to the P-transpose of the map `A âŠ— ğ’« B âŸ¶ B âŠ— ğ’« B âŸ¶ Î©`.
-/
def inverseImage {B A : C} (h : A âŸ¶ B) : ğ’« B âŸ¶ ğ’« A :=
  (h â–· ğ’« B â‰« in_ B)^

@[inherit_doc]
postfix:100 "â»Â¹" => inverseImage

/-- The following diagram commutes:
```
    A âŠ— ğ’« B ----------A â— hâ»Â¹-------> A âŠ— ğ’« A
      |                                     |
      |                                     |
    h â–· ğ’« B                             in_ A
      |                                     |
      v                                     v
    B âŠ— ğ’« B ----------in_ B------------> Î© C
```
-/
lemma inverseImage_comm {A B : C} (h : A âŸ¶ B) :
    A â— hâ»Â¹ â‰« in_ A = h â–· ğ’« B â‰« in_ B := by
  simp only [inverseImage, comm]

/-- `inverseImage` sends the identity on an object `B` to the identity on `ğ’« B`. -/
@[simp]
lemma inverseImage_id {B : C} : (ğŸ™ B)â»Â¹ = ğŸ™ (ğ’« B) := unique _ rfl

variable (C)

/-- The power object functor.
Sends objects `B` to their power objects `ğ’« B`.
Sends arrows `h : A âŸ¶ B` to the P-transpose of the map `A âŠ— ğ’« B âŸ¶ B âŠ— ğ’« B âŸ¶ Î©`,
which is the "inverse image" morphism `ğ’« B âŸ¶ ğ’« A`.
-/
def powFunctor : Cáµ’áµ– â¥¤ C where
  obj := fun âŸ¨BâŸ© â†¦ ğ’« B
  map := fun âŸ¨hâŸ© â†¦ hâ»Â¹
  map_id := fun _ => unique _ rfl
  map_comp := by
    intro âŸ¨XâŸ© âŸ¨YâŸ© âŸ¨ZâŸ© âŸ¨fâŸ© âŸ¨gâŸ©
    apply unique
    dsimp [inverseImage]
    rw [comp_whiskerRight_assoc, â†inverseImage_comm, â†whisker_exchange_assoc]
    simp [inverseImage]

/-- The power object functor, treated as a functor `C â¥¤ Cáµ’áµ–`. -/
def powFunctorOp : C â¥¤ Cáµ’áµ– where
  obj := fun B â†¦ âŸ¨ğ’« BâŸ©
  map := fun h â†¦ âŸ¨inverseImage hâŸ©
  map_id := by
    intro
    apply congrArg Opposite.op
    apply (powFunctor C).map_id
  map_comp := by
    intros
    apply congrArg Opposite.op
    apply (powFunctor C).map_comp

/-- The adjunction between the power object functor and itself. -/
def powSelfAdj : powFunctorOp C âŠ£ powFunctor C := by
  apply Adjunction.mkOfHomEquiv
  fapply Adjunction.CoreHomEquiv.mk
  Â· exact fun X âŸ¨YâŸ© => {
      toFun := fun âŸ¨fâŸ© => (transposeTransposeEquiv X Y).toFun f
      invFun := fun g => âŸ¨(transposeTransposeEquiv X Y).invFun gâŸ©
      left_inv := fun âŸ¨fâŸ© => by simp
      right_inv := fun g => by simp
    }
  Â· intro X' X âŸ¨YâŸ© f g
    simp
    congr
    show ^(f â‰« g)^ = ^g^ â‰« fâ»Â¹
    dsimp only [transposeSymm, transposeInv, inverseImage]
    apply unique
    rw [MonoidalCategory.whiskerLeft_comp, assoc, comm, â† assoc, whisker_exchange]
    simp
  Â· intro X âŸ¨YâŸ© âŸ¨Y'âŸ© âŸ¨fâŸ© âŸ¨gâŸ©
    dsimp only [transposeTransposeEquiv, transposeEquiv, transposeEquivSymm]
    simp
    show transpose ((Î²_ X Y').inv â‰« X â— (g â‰« f) â‰« in_ X) =
      transpose ((Î²_ X Y).inv â‰« X â— f â‰« in_ X) â‰« gâ»Â¹
    dsimp only [transposeInv, inverseImage]
    apply unique
    rw [MonoidalCategory.whiskerLeft_comp, assoc, comm, â† assoc _ _ (in_ Y), whisker_exchange]
    simp

end
end CategoryTheory.HasPowerObjects
