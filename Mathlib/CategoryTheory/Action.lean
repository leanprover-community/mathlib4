/-
Copyright (c) 2020 David WÃ¤rn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David WÃ¤rn
-/
import Mathlib.CategoryTheory.Elements
import Mathlib.CategoryTheory.IsConnected
import Mathlib.CategoryTheory.SingleObj
import Mathlib.GroupTheory.GroupAction.Quotient
import Mathlib.GroupTheory.SemidirectProduct

/-!
# Actions as functors and as categories

From a multiplicative action M â†» X, we can construct a functor from M to the category of
types, mapping the single object of M to X and an element `m : M` to map `X â†’ X` given by
multiplication by `m`.
  This functor induces a category structure on X -- a special case of the category of elements.
A morphism `x âŸ¶ y` in this category is simply a scalar `m : M` such that `m â€¢ x = y`. In the case
where M is a group, this category is a groupoid -- the *action groupoid*.
-/


open MulAction SemidirectProduct

namespace CategoryTheory

universe u

variable (M : Type*) [Monoid M] (X : Type u) [MulAction M X]

/-- A multiplicative action M â†» X viewed as a functor mapping the single object of M to X
  and an element `m : M` to the map `X â†’ X` given by multiplication by `m`. -/
@[simps]
def actionAsFunctor : SingleObj M â¥¤ Type u where
  obj _ := X
  map := (Â· â€¢ Â·)
  map_id _ := funext <| MulAction.one_smul
  map_comp f g := funext fun x â†¦ (smul_smul g f x).symm

/-- A multiplicative action M â†» X induces a category structure on X, where a morphism
from x to y is a scalar taking x to y. Due to implementation details, the object type
of this category is not equal to X, but is in bijection with X. -/
def ActionCategory :=
  (actionAsFunctor M X).Elements

instance : Category (ActionCategory M X) := by
  dsimp only [ActionCategory]
  infer_instance

namespace ActionCategory

/-- The projection from the action category to the monoid, mapping a morphism to its
  label. -/
def Ï€ : ActionCategory M X â¥¤ SingleObj M :=
  CategoryOfElements.Ï€ _

@[simp]
theorem Ï€_map (p q : ActionCategory M X) (f : p âŸ¶ q) : (Ï€ M X).map f = f.val :=
  rfl

@[simp]
theorem Ï€_obj (p : ActionCategory M X) : (Ï€ M X).obj p = SingleObj.star M :=
  Unit.ext _ _

variable {M X}

/-- The canonical map `ActionCategory M X â†’ X`. It is given by `fun x â†¦ x.snd`, but
  has a more explicit type. -/
protected def back : ActionCategory M X â†’ X := fun x â†¦ x.snd

instance : CoeTC X (ActionCategory M X) :=
  âŸ¨fun x â†¦ âŸ¨(), xâŸ©âŸ©

@[simp]
theorem coe_back (x : X) : ActionCategory.back (x : ActionCategory M X) = x :=
  rfl

@[simp]
theorem back_coe (x : ActionCategory M X) : â†‘x.back = x := by cases x; rfl

variable (M X)

/-- An object of the action category given by M â†» X corresponds to an element of X. -/
def objEquiv : X â‰ƒ ActionCategory M X where
  toFun x := x
  invFun x := x.back
  left_inv := coe_back
  right_inv := back_coe

theorem hom_as_subtype (p q : ActionCategory M X) : (p âŸ¶ q) = { m : M // m â€¢ p.back = q.back } :=
  rfl

instance [Inhabited X] : Inhabited (ActionCategory M X) :=
  âŸ¨show X from defaultâŸ©

instance [Nonempty X] : Nonempty (ActionCategory M X) :=
  Nonempty.map (objEquiv M X) inferInstance

variable {X} (x : X)

/-- The stabilizer of a point is isomorphic to the endomorphism monoid at the
  corresponding point. In fact they are definitionally equivalent. -/
def stabilizerIsoEnd : stabilizerSubmonoid M x â‰ƒ* @End (ActionCategory M X) _ x :=
  MulEquiv.refl _

@[simp]
theorem stabilizerIsoEnd_apply (f : stabilizerSubmonoid M x) :
    (stabilizerIsoEnd M x) f = f :=
  rfl

@[simp 1100]
theorem stabilizerIsoEnd_symm_apply (f : End _) : (stabilizerIsoEnd M x).symm f = f :=
  rfl

variable {M}

@[simp]
protected theorem id_val (x : ActionCategory M X) : Subtype.val (ðŸ™ x) = 1 :=
  rfl

@[simp]
protected theorem comp_val {x y z : ActionCategory M X} (f : x âŸ¶ y) (g : y âŸ¶ z) :
    (f â‰« g).val = g.val * f.val :=
  rfl

instance [IsPretransitive M X] [Nonempty X] : IsConnected (ActionCategory M X) :=
  zigzag_isConnected fun x y â†¦
    Relation.ReflTransGen.single <|
      Or.inl <| nonempty_subtype.mpr (show _ from exists_smul_eq M x.back y.back)

section Group

variable {G : Type*} [Group G] [MulAction G X]

instance : Groupoid (ActionCategory G X) :=
  CategoryTheory.groupoidOfElements _

/-- Any subgroup of `G` is a vertex group in its action groupoid. -/
def endMulEquivSubgroup (H : Subgroup G) : End (objEquiv G (G â§¸ H) â†‘(1 : G)) â‰ƒ* H :=
  MulEquiv.trans (stabilizerIsoEnd G ((1 : G) : G â§¸ H)).symm
    (MulEquiv.subgroupCongr <| stabilizer_quotient H)

/-- A target vertex `t` and a scalar `g` determine a morphism in the action groupoid. -/
def homOfPair (t : X) (g : G) : @Quiver.Hom (ActionCategory G X) _ (gâ»Â¹ â€¢ t :) t :=
  Subtype.mk g (smul_inv_smul g t)

@[simp]
theorem homOfPair.val (t : X) (g : G) : (homOfPair t g).val = g :=
  rfl

/-- Any morphism in the action groupoid is given by some pair. -/
protected def cases {P : âˆ€ â¦ƒa b : ActionCategory G Xâ¦„, (a âŸ¶ b) â†’ Sort*}
    (hyp : âˆ€ t g, P (homOfPair t g)) â¦ƒa bâ¦„ (f : a âŸ¶ b) : P f := by
  refine cast ?_ (hyp b.back f.val)
  rcases a with âŸ¨âŸ¨âŸ©, a : XâŸ©
  rcases b with âŸ¨âŸ¨âŸ©, b : XâŸ©
  rcases f with âŸ¨g : G, h : g â€¢ a = bâŸ©
  cases inv_smul_eq_iff.mpr h.symm
  rfl

-- Porting note: added to ease the proof of `uncurry`
lemma cases' â¦ƒa' b' : ActionCategory G Xâ¦„ (f : a' âŸ¶ b') :
    âˆƒ (a b : X) (g : G) (ha : a' = a) (hb : b' = b) (hg : a = gâ»Â¹ â€¢ b),
      f = eqToHom (by rw [ha, hg]) â‰« homOfPair b g â‰« eqToHom (by rw [hb]) := by
  revert a' b' f
  exact ActionCategory.cases (fun t g â†¦ âŸ¨gâ»Â¹ â€¢ t, t, g, rfl, rfl, rfl, by simpâŸ©)

variable {H : Type*} [Group H]

/-- Given `G` acting on `X`, a functor from the corresponding action groupoid to a group `H`
can be curried to a group homomorphism `G â†’* (X â†’ H) â‹Š G`. -/
@[simps]
def curry (F : ActionCategory G X â¥¤ SingleObj H) : G â†’* (X â†’ H) â‹Š[mulAutArrow] G :=
  have F_map_eq : âˆ€ {a b} {f : a âŸ¶ b}, F.map f = (F.map (homOfPair b.back f.val) : H) := by
    apply ActionCategory.cases
    intros
    rfl
  { toFun := fun g â†¦ âŸ¨fun b â†¦ F.map (homOfPair b g), gâŸ©
    map_one' := by
      dsimp
      ext1
      Â· ext b
        exact F_map_eq.symm.trans (F.map_id b)
      rfl
    map_mul' := by
      intro g h
      ext b
      Â· exact F_map_eq.symm.trans (F.map_comp (homOfPair (gâ»Â¹ â€¢ b) h) (homOfPair b g))
      rfl }

/-- Given `G` acting on `X`, a group homomorphism `Ï† : G â†’* (X â†’ H) â‹Š G` can be uncurried to
a functor from the action groupoid to `H`, provided that `Ï† g = (_, g)` for all `g`. -/
@[simps]
def uncurry (F : G â†’* (X â†’ H) â‹Š[mulAutArrow] G) (sane : âˆ€ g, (F g).right = g) :
    ActionCategory G X â¥¤ SingleObj H where
  obj _ := ()
  map {_ b} f := (F f.val).left b.back
  map_id x := by
    dsimp
    rw [F.map_one]
    rfl
  map_comp f g := by
    -- Porting note: I was not able to use `ActionCategory.cases` here,
    -- but `ActionCategory.cases'` seems as good; the original proof was:
    -- intro x y z f g; revert y z g
    -- refine' action_category.cases _
    -- simp [single_obj.comp_as_mul, sane]
    obtain âŸ¨_, z, Î³â‚, rfl, rfl, rfl, rflâŸ© := ActionCategory.cases' g
    obtain âŸ¨_, y, Î³â‚‚, rfl, hy, rfl, rflâŸ© := ActionCategory.cases' f
    obtain rfl : y = Î³â‚â»Â¹ â€¢ z := congr_arg Sigma.snd hy.symm
    simp [sane]
    rfl

end Group

end ActionCategory

end CategoryTheory
