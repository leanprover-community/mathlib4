/-
Copyright (c) 2020 David W√§rn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David W√§rn
-/
import Mathlib.CategoryTheory.Elements
import Mathlib.CategoryTheory.IsConnected
import Mathlib.CategoryTheory.SingleObj
import Mathlib.GroupTheory.GroupAction.Quotient
import Mathlib.GroupTheory.SemidirectProduct

#align_import category_theory.action from "leanprover-community/mathlib"@"aa812bd12a4dbbd2c129b38205f222df282df26d"

/-!
# Actions as functors and as categories

From a multiplicative action M ‚Üª X, we can construct a functor from M to the category of
types, mapping the single object of M to X and an element `m : M` to map `X ‚Üí X` given by
multiplication by `m`.
  This functor induces a category structure on X -- a special case of the category of elements.
A morphism `x ‚ü∂ y` in this category is simply a scalar `m : M` such that `m ‚Ä¢ x = y`. In the case
where M is a group, this category is a groupoid -- the *action groupoid*.
-/


open MulAction SemidirectProduct

namespace CategoryTheory

universe u

variable (M : Type*) [Monoid M] (X : Type u) [MulAction M X]

/-- A multiplicative action M ‚Üª X viewed as a functor mapping the single object of M to X
  and an element `m : M` to the map `X ‚Üí X` given by multiplication by `m`. -/
@[simps]
def actionAsFunctor : SingleObj M ‚•§ Type u where
  obj _ := X
  map := (¬∑ ‚Ä¢ ¬∑)
  map_id _ := funext <| MulAction.one_smul
  map_comp f g := funext fun x => (smul_smul g f x).symm
#align category_theory.action_as_functor CategoryTheory.actionAsFunctor

/-- A multiplicative action M ‚Üª X induces a category structure on X, where a morphism
 from x to y is a scalar taking x to y. Due to implementation details, the object type
 of this category is not equal to X, but is in bijection with X. -/
def ActionCategory :=
  (actionAsFunctor M X).Elements
#align category_theory.action_category CategoryTheory.ActionCategory

instance : Category (ActionCategory M X) := by
  dsimp only [ActionCategory]
  infer_instance

namespace ActionCategory

/-- The projection from the action category to the monoid, mapping a morphism to its
  label. -/
def œÄ : ActionCategory M X ‚•§ SingleObj M :=
  CategoryOfElements.œÄ _
#align category_theory.action_category.œÄ CategoryTheory.ActionCategory.œÄ

@[simp]
theorem œÄ_map (p q : ActionCategory M X) (f : p ‚ü∂ q) : (œÄ M X).map f = f.val :=
  rfl
#align category_theory.action_category.œÄ_map CategoryTheory.ActionCategory.œÄ_map

@[simp]
theorem œÄ_obj (p : ActionCategory M X) : (œÄ M X).obj p = SingleObj.star M :=
  Unit.ext _ _
#align category_theory.action_category.œÄ_obj CategoryTheory.ActionCategory.œÄ_obj

variable {M X}

/-- The canonical map `ActionCategory M X ‚Üí X`. It is given by `fun x => x.snd`, but
  has a more explicit type. -/
protected def back : ActionCategory M X ‚Üí X := fun x => x.snd
#align category_theory.action_category.back CategoryTheory.ActionCategory.back

instance : CoeTC X (ActionCategory M X) :=
  ‚ü®fun x => ‚ü®(), x‚ü©‚ü©

@[simp]
theorem coe_back (x : X) : ActionCategory.back (x : ActionCategory M X) = x :=
  rfl
#align category_theory.action_category.coe_back CategoryTheory.ActionCategory.coe_back

@[simp]
theorem back_coe (x : ActionCategory M X) : ‚Üëx.back = x := by cases x; rfl
#align category_theory.action_category.back_coe CategoryTheory.ActionCategory.back_coe

variable (M X)

/-- An object of the action category given by M ‚Üª X corresponds to an element of X. -/
def objEquiv : X ‚âÉ ActionCategory M X where
  toFun x := x
  invFun x := x.back
  left_inv := coe_back
  right_inv := back_coe
#align category_theory.action_category.obj_equiv CategoryTheory.ActionCategory.objEquiv

theorem hom_as_subtype (p q : ActionCategory M X) : (p ‚ü∂ q) = { m : M // m ‚Ä¢ p.back = q.back } :=
  rfl
#align category_theory.action_category.hom_as_subtype CategoryTheory.ActionCategory.hom_as_subtype

instance [Inhabited X] : Inhabited (ActionCategory M X) :=
  ‚ü®show X from default‚ü©

instance [Nonempty X] : Nonempty (ActionCategory M X) :=
  Nonempty.map (objEquiv M X) inferInstance

variable {X} (x : X)

/-- The stabilizer of a point is isomorphic to the endomorphism monoid at the
  corresponding point. In fact they are definitionally equivalent. -/
def stabilizerIsoEnd : Stabilizer.submonoid M x ‚âÉ* @End (ActionCategory M X) _ x :=
  MulEquiv.refl _
#align category_theory.action_category.stabilizer_iso_End CategoryTheory.ActionCategory.stabilizerIsoEnd

@[simp]
theorem stabilizerIsoEnd_apply (f : Stabilizer.submonoid M x) :
    (stabilizerIsoEnd M x) f = f :=
  rfl
#align category_theory.action_category.stabilizer_iso_End_apply CategoryTheory.ActionCategory.stabilizerIsoEnd_apply

@[simp 1100]
theorem stabilizerIsoEnd_symm_apply (f : End _) : (stabilizerIsoEnd M x).symm f = f :=
  rfl
#align category_theory.action_category.stabilizer_iso_End_symm_apply CategoryTheory.ActionCategory.stabilizerIsoEnd_symm_apply

variable {M}

@[simp]
protected theorem id_val (x : ActionCategory M X) : Subtype.val (ùüô x) = 1 :=
  rfl
#align category_theory.action_category.id_val CategoryTheory.ActionCategory.id_val

@[simp]
protected theorem comp_val {x y z : ActionCategory M X} (f : x ‚ü∂ y) (g : y ‚ü∂ z) :
    (f ‚â´ g).val = g.val * f.val :=
  rfl
#align category_theory.action_category.comp_val CategoryTheory.ActionCategory.comp_val

instance [IsPretransitive M X] [Nonempty X] : IsConnected (ActionCategory M X) :=
  zigzag_isConnected fun x y =>
    Relation.ReflTransGen.single <|
      Or.inl <| nonempty_subtype.mpr (show _ from exists_smul_eq M x.back y.back)

section Group

variable {G : Type*} [Group G] [MulAction G X]

instance : Groupoid (ActionCategory G X) :=
  CategoryTheory.groupoidOfElements _

/-- Any subgroup of `G` is a vertex group in its action groupoid. -/
def endMulEquivSubgroup (H : Subgroup G) : End (objEquiv G (G ‚ß∏ H) ‚Üë(1 : G)) ‚âÉ* H :=
  MulEquiv.trans (stabilizerIsoEnd G ((1 : G) : G ‚ß∏ H)).symm
    (MulEquiv.subgroupCongr <| stabilizer_quotient H)
#align category_theory.action_category.End_mul_equiv_subgroup CategoryTheory.ActionCategory.endMulEquivSubgroup

/-- A target vertex `t` and a scalar `g` determine a morphism in the action groupoid. -/
def homOfPair (t : X) (g : G) : @Quiver.Hom (ActionCategory G X) _ (g‚Åª¬π ‚Ä¢ t) t :=
  Subtype.mk g (smul_inv_smul g t)
#align category_theory.action_category.hom_of_pair CategoryTheory.ActionCategory.homOfPair

@[simp]
theorem homOfPair.val (t : X) (g : G) : (homOfPair t g).val = g :=
  rfl
#align category_theory.action_category.hom_of_pair.val CategoryTheory.ActionCategory.homOfPair.val

/-- Any morphism in the action groupoid is given by some pair. -/
protected def cases {P : ‚àÄ ‚¶Éa b : ActionCategory G X‚¶Ñ, (a ‚ü∂ b) ‚Üí Sort*}
    (hyp : ‚àÄ t g, P (homOfPair t g)) ‚¶Éa b‚¶Ñ (f : a ‚ü∂ b) : P f := by
  refine' cast _ (hyp b.back f.val)
  rcases a with ‚ü®‚ü®‚ü©, a : X‚ü©
  rcases b with ‚ü®‚ü®‚ü©, b : X‚ü©
  rcases f with ‚ü®g : G, h : g ‚Ä¢ a = b‚ü©
  cases inv_smul_eq_iff.mpr h.symm
  rfl
#align category_theory.action_category.cases CategoryTheory.ActionCategory.cases

-- porting note: added to ease the proof of `uncurry`
lemma cases' ‚¶Éa' b' : ActionCategory G X‚¶Ñ (f : a' ‚ü∂ b') :
    ‚àÉ (a b : X) (g : G) (ha : a' = a) (hb : b' = b) (hg : a = g‚Åª¬π ‚Ä¢ b),
      f = eqToHom (by rw [ha, hg]) ‚â´ homOfPair b g ‚â´ eqToHom (by rw [hb]) := by
  revert a' b' f
  exact ActionCategory.cases (fun t g => ‚ü®g‚Åª¬π ‚Ä¢ t, t, g, rfl, rfl, rfl, by simp‚ü©)

variable {H : Type*} [Group H]

/-- Given `G` acting on `X`, a functor from the corresponding action groupoid to a group `H`
    can be curried to a group homomorphism `G ‚Üí* (X ‚Üí H) ‚ãä G`. -/
@[simps]
def curry (F : ActionCategory G X ‚•§ SingleObj H) : G ‚Üí* (X ‚Üí H) ‚ãä[mulAutArrow] G :=
  have F_map_eq : ‚àÄ {a b} {f : a ‚ü∂ b}, F.map f = (F.map (homOfPair b.back f.val) : H) := by
    apply ActionCategory.cases
    intros
    rfl
  { toFun := fun g => ‚ü®fun b => F.map (homOfPair b g), g‚ü©
    map_one' := by
      congr
      dsimp
      ext1
      ext b
      exact F_map_eq.symm.trans (F.map_id b)
      rfl
    map_mul' := by
      intro g h
      congr
      ext b
      exact F_map_eq.symm.trans (F.map_comp (homOfPair (g‚Åª¬π ‚Ä¢ b) h) (homOfPair b g))
      rfl }
#align category_theory.action_category.curry CategoryTheory.ActionCategory.curry

/-- Given `G` acting on `X`, a group homomorphism `œÜ : G ‚Üí* (X ‚Üí H) ‚ãä G` can be uncurried to
    a functor from the action groupoid to `H`, provided that `œÜ g = (_, g)` for all `g`. -/
@[simps]
def uncurry (F : G ‚Üí* (X ‚Üí H) ‚ãä[mulAutArrow] G) (sane : ‚àÄ g, (F g).right = g) :
    ActionCategory G X ‚•§ SingleObj H where
  obj _ := ()
  map {_ b} f := (F f.val).left b.back
  map_id x := by
    dsimp
    rw [F.map_one]
    rfl
  map_comp f g := by
    -- porting note: I was not able to use `ActionCategory.cases` here,
    -- but `ActionCategory.cases'` seems as good; the original proof was:
    -- intro x y z f g; revert y z g
    -- refine' action_category.cases _
    -- simp [single_obj.comp_as_mul, sane]
    obtain ‚ü®_, z, Œ≥‚ÇÅ, rfl, rfl, rfl, rfl‚ü© := ActionCategory.cases' g
    obtain ‚ü®_, y, Œ≥‚ÇÇ, rfl, hy, rfl, rfl‚ü© := ActionCategory.cases' f
    obtain rfl : y = Œ≥‚ÇÅ‚Åª¬π ‚Ä¢ z := congr_arg Sigma.snd hy.symm
    simp [sane]
    rfl
#align category_theory.action_category.uncurry CategoryTheory.ActionCategory.uncurry

end Group

end ActionCategory

end CategoryTheory
