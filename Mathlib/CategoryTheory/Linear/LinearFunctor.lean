/-
Copyright (c) 2021 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Preadditive.AdditiveFunctor
import Mathlib.CategoryTheory.Linear.Basic
import Mathlib.Algebra.Module.LinearMap.Rat

/-!
# Linear Functors

An additive functor between two `R`-linear categories is called *linear*
if the induced map on hom types is a morphism of `R`-modules.

# Implementation details

`Functor.Linear` is a `Prop`-valued class, defined by saying that
for every two objects `X` and `Y`, the map
`F.map : (X âŸ¶ Y) â†’ (F.obj X âŸ¶ F.obj Y)` is a morphism of `R`-modules.

-/


namespace CategoryTheory

variable (R : Type*) [Semiring R]

/-- An additive functor `F` is `R`-linear provided `F.map` is an `R`-module morphism. -/
class Functor.Linear {C D : Type*} [Category C] [Category D] [Preadditive C] [Preadditive D]
  [Linear R C] [Linear R D] (F : C â¥¤ D) [F.Additive] : Prop where
  /-- the functor induces a linear map on morphisms -/
  map_smul : âˆ€ {X Y : C} (f : X âŸ¶ Y) (r : R), F.map (r â€¢ f) = r â€¢ F.map f := by aesop_cat

lemma Functor.linear_iff {C D : Type*} [Category C] [Category D] [Preadditive C] [Preadditive D]
    [CategoryTheory.Linear R C] [CategoryTheory.Linear R D] (F : C â¥¤ D) [F.Additive] :
    Functor.Linear R F â†” âˆ€ (X : C) (r : R), F.map (r â€¢ ðŸ™ X) = r â€¢ ðŸ™ (F.obj X) := by
  constructor
  Â· intro h X r
    rw [h.map_smul, F.map_id]
  Â· refine' fun h => âŸ¨fun {X Y} f r => _âŸ©
    have : r â€¢ f = (r â€¢ ðŸ™ X) â‰« f := by simp
    rw [this, F.map_comp, h, Linear.smul_comp, Category.id_comp]

section Linear

namespace Functor

section

variable {R}
variable {C D : Type*} [Category C] [Category D] [Preadditive C] [Preadditive D]
  [CategoryTheory.Linear R C] [CategoryTheory.Linear R D] (F : C â¥¤ D) [Additive F] [Linear R F]

@[simp]
theorem map_smul {X Y : C} (r : R) (f : X âŸ¶ Y) : F.map (r â€¢ f) = r â€¢ F.map f :=
  Functor.Linear.map_smul _ _

@[simp]
theorem map_units_smul {X Y : C} (r : RË£) (f : X âŸ¶ Y) : F.map (r â€¢ f) = r â€¢ F.map f := by
  apply map_smul

instance : Linear R (ðŸ­ C) where

instance {E : Type*} [Category E] [Preadditive E] [CategoryTheory.Linear R E] (G : D â¥¤ E)
    [Additive G] [Linear R G] : Linear R (F â‹™ G) where

variable (R) {F}

lemma linear_of_iso {G : C â¥¤ D} (e : F â‰… G) [F.Additive] [F.Linear R] :
    have : G.Additive := additive_of_iso e
    G.Linear R := by
  have : G.Additive := additive_of_iso e
  exact
    { map_smul := fun {X Y} f r => by
        simp only [â† NatIso.naturality_1 e (r â€¢ f), F.map_smul, Linear.smul_comp,
          NatTrans.naturality, Linear.comp_smul, Iso.inv_hom_id_app_assoc] }

variable (F)

/-- `F.mapLinearMap` is an `R`-linear map whose underlying function is `F.map`. -/
@[simps]
def mapLinearMap {X Y : C} : (X âŸ¶ Y) â†’â‚—[R] F.obj X âŸ¶ F.obj Y :=
  { F.mapAddHom with map_smul' := fun r f => F.map_smul r f }

theorem coe_mapLinearMap {X Y : C} : â‡‘(F.mapLinearMap R : (X âŸ¶ Y) â†’â‚—[R] _) = F.map := rfl

end

section InducedCategory

variable {C : Type*} {D : Type*} [Category D] [Preadditive D] [CategoryTheory.Linear R D]
  (F : C â†’ D)

instance inducedFunctorLinear : Functor.Linear R (inducedFunctor F) where

end InducedCategory

instance fullSubcategoryInclusionLinear {C : Type*} [Category C] [Preadditive C]
    [CategoryTheory.Linear R C] (Z : C â†’ Prop) : (fullSubcategoryInclusion Z).Linear R where

section

variable {R} {C D : Type*} [Category C] [Category D] [Preadditive C] [Preadditive D] (F : C â¥¤ D)
  [Additive F]

instance natLinear : F.Linear â„• where
  map_smul := F.mapAddHom.map_nsmul

instance intLinear : F.Linear â„¤ where
  map_smul f r := F.mapAddHom.map_zsmul f r

variable [CategoryTheory.Linear â„š C] [CategoryTheory.Linear â„š D]

instance ratLinear : F.Linear â„š where
  map_smul f r := F.mapAddHom.toRatLinearMap.map_smul r f

end

end Functor

namespace Equivalence

variable {C D : Type*} [Category C] [Category D] [Preadditive C] [Linear R C] [Preadditive D]
  [Linear R D]

instance inverseLinear (e : C â‰Œ D) [e.functor.Additive] [e.functor.Linear R] :
  e.inverse.Linear R where
    map_smul r f := by
      apply e.functor.map_injective
      simp

end Equivalence

end Linear

end CategoryTheory
