/-
Copyright (c) 2022 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Algebra.Group.Hom.End
import Mathlib.Algebra.GroupWithZero.Action.Units
import Mathlib.Algebra.Ring.Invertible
import Mathlib.Algebra.Ring.Opposite
import Mathlib.Algebra.SMulWithZero
import Mathlib.Data.Int.Cast.Lemmas



import Batteries.Tactic.ShowUnused
import Mathlib.Tactic.Linter.MinImports

/-!
# Linear structure on functor categories

If `C` and `D` are categories and `D` is `R`-linear,
then `C ‚•§ D` is also `R`-linear.

-/

universe w v u v' u' v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

section Mathlib.Algebra.Module.Defs

open Function Set

variable {Œ± R k S M M‚ÇÇ M‚ÇÉ Œπ : Type*}

/-- A module is a generalization of vector spaces to a scalar semiring.
  It consists of a scalar semiring `R` and an additive monoid of "vectors" `M`,
  connected by a "scalar multiplication" operation `r ‚Ä¢ x : M`
  (where `r : R` and `x : M`) with some natural associativity and
  distributivity axioms similar to those on a ring. -/
@[ext]
class Module (R : Type u) (M : Type v) [Semiring R] [AddCommMonoid M] extends
  DistribMulAction R M where
  /-- Scalar multiplication distributes over addition from the right. -/
  protected add_smul : ‚àÄ (r s : R) (x : M), (r + s) ‚Ä¢ x = r ‚Ä¢ x + s ‚Ä¢ x
  /-- Scalar multiplication by zero gives zero. -/
  protected zero_smul : ‚àÄ x : M, (0 : R) ‚Ä¢ x = 0

section AddCommMonoid

variable [Semiring R] [AddCommMonoid M] [Module R M] (r s : R) (x y : M)

-- see Note [lower instance priority]
/-- A module over a semiring automatically inherits a `MulActionWithZero` structure. -/
instance (priority := 100) Module.toMulActionWithZero : MulActionWithZero R M :=
  { (inferInstance : MulAction R M) with
    smul_zero := smul_zero
    zero_smul := Module.zero_smul }

instance AddCommGroup.toNatModule : Module ‚Ñï M where
  one_smul := one_nsmul
  mul_smul m n a := mul_nsmul' a m n
  smul_add n a b := nsmul_add a b n
  smul_zero := nsmul_zero
  zero_smul := zero_nsmul
  add_smul r s x := add_nsmul x r s

theorem AddMonoid.End.natCast_def (n : ‚Ñï) :
    (‚Üën : AddMonoid.End M) = DistribMulAction.toAddMonoidEnd ‚Ñï M n :=
  rfl

theorem add_smul : (r + s) ‚Ä¢ x = r ‚Ä¢ x + s ‚Ä¢ x :=
  Module.add_smul r s x

theorem Convex.combo_self {a b : R} (h : a + b = 1) (x : M) : a ‚Ä¢ x + b ‚Ä¢ x = x := by
  rw [‚Üê add_smul, h, one_smul]

variable (R)

-- Porting note: this is the letter of the mathlib3 version, but not really the spirit
theorem two_smul : (2 : R) ‚Ä¢ x = x + x := by rw [‚Üê one_add_one_eq_two, add_smul, one_smul]

set_option linter.deprecated false in
@[deprecated (since := "2022-12-31")]
theorem two_smul' : (2 : R) ‚Ä¢ x = (2 : ‚Ñï) ‚Ä¢ x := by
  rw [two_smul, two_nsmul]

@[simp]
theorem invOf_two_smul_add_invOf_two_smul [Invertible (2 : R)] (x : M) :
    (‚Öü 2 : R) ‚Ä¢ x + (‚Öü 2 : R) ‚Ä¢ x = x :=
  Convex.combo_self invOf_two_add_invOf_two _

/-- Pullback a `Module` structure along an injective additive monoid homomorphism.
See note [reducible non-instances]. -/
protected abbrev Function.Injective.module [AddCommMonoid M‚ÇÇ] [SMul R M‚ÇÇ] (f : M‚ÇÇ ‚Üí+ M)
    (hf : Injective f) (smul : ‚àÄ (c : R) (x), f (c ‚Ä¢ x) = c ‚Ä¢ f x) : Module R M‚ÇÇ :=
  { hf.distribMulAction f smul with
    add_smul := fun c‚ÇÅ c‚ÇÇ x => hf <| by simp only [smul, f.map_add, add_smul]
    zero_smul := fun x => hf <| by simp only [smul, zero_smul, f.map_zero] }

/-- Pushforward a `Module` structure along a surjective additive monoid homomorphism.
See note [reducible non-instances]. -/
protected abbrev Function.Surjective.module [AddCommMonoid M‚ÇÇ] [SMul R M‚ÇÇ] (f : M ‚Üí+ M‚ÇÇ)
    (hf : Surjective f) (smul : ‚àÄ (c : R) (x), f (c ‚Ä¢ x) = c ‚Ä¢ f x) : Module R M‚ÇÇ :=
  { toDistribMulAction := hf.distribMulAction f smul
    add_smul := fun c‚ÇÅ c‚ÇÇ x => by
      rcases hf x with ‚ü®x, rfl‚ü©
      simp only [add_smul, ‚Üê smul, ‚Üê f.map_add]
    zero_smul := fun x => by
      rcases hf x with ‚ü®x, rfl‚ü©
      rw [‚Üê f.map_zero, ‚Üê smul, zero_smul] }

/-- Push forward the action of `R` on `M` along a compatible surjective map `f : R ‚Üí+* S`.

See also `Function.Surjective.mulActionLeft` and `Function.Surjective.distribMulActionLeft`.
-/
abbrev Function.Surjective.moduleLeft {R S M : Type*} [Semiring R] [AddCommMonoid M] [Module R M]
    [Semiring S] [SMul S M] (f : R ‚Üí+* S) (hf : Function.Surjective f)
    (hsmul : ‚àÄ (c) (x : M), f c ‚Ä¢ x = c ‚Ä¢ x) : Module S M :=
  { hf.distribMulActionLeft f.toMonoidHom hsmul with
    zero_smul := fun x => by rw [‚Üê f.map_zero, hsmul, zero_smul]
    add_smul := hf.forall‚ÇÇ.mpr fun a b x => by simp only [‚Üê f.map_add, hsmul, add_smul] }

variable {R} (M)

/-- Compose a `Module` with a `RingHom`, with action `f s ‚Ä¢ m`.

See note [reducible non-instances]. -/
abbrev Module.compHom [Semiring S] (f : S ‚Üí+* R) : Module S M :=
  { MulActionWithZero.compHom M f.toMonoidWithZeroHom, DistribMulAction.compHom M (f : S ‚Üí* R) with
    -- Porting note: the `show f (r + s) ‚Ä¢ x = f r ‚Ä¢ x + f s ‚Ä¢ x` wasn't needed in mathlib3.
    -- Somehow, now that `SMul` is heterogeneous, it can't unfold earlier fields of a definition for
    -- use in later fields.  See
    -- https://leanprover.zulipchat.com/#narrow/stream/287929-mathlib4/topic/Heterogeneous.20scalar.20multiplication
    add_smul := fun r s x => show f (r + s) ‚Ä¢ x = f r ‚Ä¢ x + f s ‚Ä¢ x by simp [add_smul] }

variable (R)

/-- `(‚Ä¢)` as an `AddMonoidHom`.

This is a stronger version of `DistribMulAction.toAddMonoidEnd` -/
@[simps! apply_apply]
def Module.toAddMonoidEnd : R ‚Üí+* AddMonoid.End M :=
  { DistribMulAction.toAddMonoidEnd R M with
    -- Porting note: the two `show`s weren't needed in mathlib3.
    -- Somehow, now that `SMul` is heterogeneous, it can't unfold earlier fields of a definition for
    -- use in later fields.  See
    -- https://leanprover.zulipchat.com/#narrow/stream/287929-mathlib4/topic/Heterogeneous.20scalar.20multiplication
    map_zero' := AddMonoidHom.ext fun r => show (0 : R) ‚Ä¢ r = 0 by simp
    map_add' := fun x y =>
      AddMonoidHom.ext fun r => show (x + y) ‚Ä¢ r = x ‚Ä¢ r + y ‚Ä¢ r by simp [add_smul] }

/-- A convenience alias for `Module.toAddMonoidEnd` as an `AddMonoidHom`, usually to allow the
use of `AddMonoidHom.flip`. -/
def smulAddHom : R ‚Üí+ M ‚Üí+ M :=
  (Module.toAddMonoidEnd R M).toAddMonoidHom

variable {R M}

@[simp]
theorem smulAddHom_apply (r : R) (x : M) : smulAddHom R M r x = r ‚Ä¢ x :=
  rfl

theorem Module.eq_zero_of_zero_eq_one (zero_eq_one : (0 : R) = 1) : x = 0 := by
  rw [‚Üê one_smul R x, ‚Üê zero_eq_one, zero_smul]

@[simp]
theorem smul_add_one_sub_smul {R : Type*} [Ring R] [Module R M] {r : R} {m : M} :
    r ‚Ä¢ m + (1 - r) ‚Ä¢ m = m := by rw [‚Üê add_smul, add_sub_cancel, one_smul]

end AddCommMonoid


section AddCommGroup

variable (R M) [Semiring R] [AddCommGroup M]

instance AddCommGroup.toIntModule : Module ‚Ñ§ M where
  one_smul := one_zsmul
  mul_smul m n a := mul_zsmul a m n
  smul_add n a b := zsmul_add a b n
  smul_zero := zsmul_zero
  zero_smul := zero_zsmul
  add_smul r s x := add_zsmul x r s

theorem AddMonoid.End.intCast_def (z : ‚Ñ§) :
    (‚Üëz : AddMonoid.End M) = DistribMulAction.toAddMonoidEnd ‚Ñ§ M z :=
  rfl

variable {R M}

theorem Convex.combo_eq_smul_sub_add [Module R M] {x y : M} {a b : R} (h : a + b = 1) :
    a ‚Ä¢ x + b ‚Ä¢ y = b ‚Ä¢ (y - x) + x :=
  calc
    a ‚Ä¢ x + b ‚Ä¢ y = b ‚Ä¢ y - b ‚Ä¢ x + (a ‚Ä¢ x + b ‚Ä¢ x) := by rw [sub_add_add_cancel, add_comm]
    _ = b ‚Ä¢ (y - x) + x := by rw [smul_sub, Convex.combo_self h]

end AddCommGroup

-- We'll later use this to show `Module ‚Ñï M` and `Module ‚Ñ§ M` are subsingletons.
/-- A variant of `Module.ext` that's convenient for term-mode. -/
theorem Module.ext' {R : Type*} [Semiring R] {M : Type*} [AddCommMonoid M] (P Q : Module R M)
    (w : ‚àÄ (r : R) (m : M), (haveI := P; r ‚Ä¢ m) = (haveI := Q; r ‚Ä¢ m)) :
    P = Q := by
  ext
  exact w _ _

section Module

variable [Ring R] [AddCommGroup M] [Module R M] (r s : R) (x y : M)

@[simp]
theorem neg_smul : -r ‚Ä¢ x = -(r ‚Ä¢ x) :=
  eq_neg_of_add_eq_zero_left <| by rw [‚Üê add_smul, neg_add_cancel, zero_smul]

-- Porting note (#10618): simp can prove this
--@[simp]
theorem neg_smul_neg : -r ‚Ä¢ -x = r ‚Ä¢ x := by rw [neg_smul, smul_neg, neg_neg]

@[simp]
theorem Units.neg_smul (u : RÀ£) (x : M) : -u ‚Ä¢ x = -(u ‚Ä¢ x) := by
  rw [Units.smul_def, Units.val_neg, _root_.neg_smul, Units.smul_def]

variable (R)

theorem neg_one_smul (x : M) : (-1 : R) ‚Ä¢ x = -x := by simp

variable {R}

theorem sub_smul (r s : R) (y : M) : (r - s) ‚Ä¢ y = r ‚Ä¢ y - s ‚Ä¢ y := by
  simp [add_smul, sub_eq_add_neg]

end Module

variable (R)

/-- An `AddCommMonoid` that is a `Module` over a `Ring` carries a natural `AddCommGroup`
structure.
See note [reducible non-instances]. -/
abbrev Module.addCommMonoidToAddCommGroup
    [Ring R] [AddCommMonoid M] [Module R M] : AddCommGroup M :=
  { (inferInstance : AddCommMonoid M) with
    neg := fun a => (-1 : R) ‚Ä¢ a
    neg_add_cancel := fun a =>
      show (-1 : R) ‚Ä¢ a + a = 0 by
        nth_rw 2 [‚Üê one_smul R a]
        rw [‚Üê add_smul, neg_add_cancel, zero_smul]
    zsmul := fun z a => (z : R) ‚Ä¢ a
    zsmul_zero' := fun a => by simpa only [Int.cast_zero] using zero_smul R a
    zsmul_succ' := fun z a => by simp [add_comm, add_smul]
    zsmul_neg' := fun z a => by simp [‚Üê smul_assoc, neg_one_smul] }

variable {R}

/-- A module over a `Subsingleton` semiring is a `Subsingleton`. We cannot register this
as an instance because Lean has no way to guess `R`. -/
protected theorem Module.subsingleton (R M : Type*) [Semiring R] [Subsingleton R] [AddCommMonoid M]
    [Module R M] : Subsingleton M :=
  MulActionWithZero.subsingleton R M

/-- A semiring is `Nontrivial` provided that there exists a nontrivial module over this semiring. -/
protected theorem Module.nontrivial (R M : Type*) [Semiring R] [Nontrivial M] [AddCommMonoid M]
    [Module R M] : Nontrivial R :=
  MulActionWithZero.nontrivial R M

-- see Note [lower instance priority]
instance (priority := 910) Semiring.toModule [Semiring R] : Module R R where
  smul_add := mul_add
  add_smul := add_mul
  zero_smul := zero_mul
  smul_zero := mul_zero

-- see Note [lower instance priority]
/-- Like `Semiring.toModule`, but multiplies on the right. -/
instance (priority := 910) Semiring.toOppositeModule [Semiring R] : Module R·µê·µí·µñ R :=
  { MonoidWithZero.toOppositeMulActionWithZero R with
    smul_add := fun _ _ _ => add_mul _ _ _
    add_smul := fun _ _ _ => mul_add _ _ _ }

/-- A ring homomorphism `f : R ‚Üí+* M` defines a module structure by `r ‚Ä¢ x = f r * x`. -/
def RingHom.toModule [Semiring R] [Semiring S] (f : R ‚Üí+* S) : Module R S :=
  Module.compHom S f

/-- If the module action of `R` on `S` is compatible with multiplication on `S`, then
`fun x ‚Ü¶ x ‚Ä¢ 1` is a ring homomorphism from `R` to `S`.

This is the `RingHom` version of `MonoidHom.smulOneHom`.

When `R` is commutative, usually `algebraMap` should be preferred. -/
@[simps!] def RingHom.smulOneHom
    [Semiring R] [NonAssocSemiring S] [Module R S] [IsScalarTower R S S] : R ‚Üí+* S where
  __ := MonoidHom.smulOneHom
  map_zero' := zero_smul R 1
  map_add' := (add_smul ¬∑ ¬∑ 1)

/-- A homomorphism between semirings R and S can be equivalently specified by a R-module
structure on S such that S/S/R is a scalar tower. -/
def ringHomEquivModuleIsScalarTower [Semiring R] [Semiring S] :
    (R ‚Üí+* S) ‚âÉ {_inst : Module R S // IsScalarTower R S S} where
  toFun f := ‚ü®Module.compHom S f, SMul.comp.isScalarTower _‚ü©
  invFun := fun ‚ü®_, _‚ü© ‚Ü¶ RingHom.smulOneHom
  left_inv f := RingHom.ext fun r ‚Ü¶ mul_one (f r)
  right_inv := fun ‚ü®_, _‚ü© ‚Ü¶ Subtype.ext <| Module.ext <| funext‚ÇÇ <| smul_one_smul S

section AddCommMonoid

variable [Semiring R] [AddCommMonoid M] [Module R M]

section

variable (R)

/-- `nsmul` is equal to any other module structure via a cast. -/
lemma Nat.cast_smul_eq_nsmul (n : ‚Ñï) (b : M) : (n : R) ‚Ä¢ b = n ‚Ä¢ b := by
  induction n with
  | zero => rw [Nat.cast_zero, zero_smul, zero_smul]
  | succ n ih => rw [Nat.cast_succ, add_smul, add_smul, one_smul, ih, one_smul]

/-- `nsmul` is equal to any other module structure via a cast. -/
-- See note [no_index around OfNat.ofNat]
lemma ofNat_smul_eq_nsmul (n : ‚Ñï) [n.AtLeastTwo] (b : M) :
    (no_index OfNat.ofNat n : R) ‚Ä¢ b = OfNat.ofNat n ‚Ä¢ b := Nat.cast_smul_eq_nsmul ..

/-- `nsmul` is equal to any other module structure via a cast. -/
@[deprecated Nat.cast_smul_eq_nsmul (since := "2024-07-23")]
lemma nsmul_eq_smul_cast (n : ‚Ñï) (b : M) : n ‚Ä¢ b = (n : R) ‚Ä¢ b := (Nat.cast_smul_eq_nsmul ..).symm

end

/-- Convert back any exotic `‚Ñï`-smul to the canonical instance. This should not be needed since in
mathlib all `AddCommMonoid`s should normally have exactly one `‚Ñï`-module structure by design.
-/
theorem nat_smul_eq_nsmul (h : Module ‚Ñï M) (n : ‚Ñï) (x : M) : @SMul.smul ‚Ñï M h.toSMul n x = n ‚Ä¢ x :=
  Nat.cast_smul_eq_nsmul ..

/-- All `‚Ñï`-module structures are equal. Not an instance since in mathlib all `AddCommMonoid`
should normally have exactly one `‚Ñï`-module structure by design. -/
def AddCommMonoid.uniqueNatModule : Unique (Module ‚Ñï M) where
  default := by infer_instance
  uniq P := (Module.ext' P _) fun n => by convert nat_smul_eq_nsmul P n

instance AddCommMonoid.nat_isScalarTower : IsScalarTower ‚Ñï R M where
  smul_assoc n x y := by
    induction n with
    | zero => simp only [zero_smul]
    | succ n ih => simp only [add_smul, one_smul, ih]

end AddCommMonoid

section AddCommGroup

variable [Semiring S] [Ring R] [AddCommGroup M] [Module S M] [Module R M]

section

variable (R)

/-- `zsmul` is equal to any other module structure via a cast. -/
lemma Int.cast_smul_eq_zsmul (n : ‚Ñ§) (b : M) : (n : R) ‚Ä¢ b = n ‚Ä¢ b :=
  have : ((smulAddHom R M).flip b).comp (Int.castAddHom R) = (smulAddHom ‚Ñ§ M).flip b := by
    apply AddMonoidHom.ext_int
    simp
  DFunLike.congr_fun this n

@[deprecated (since := "2024-07-23")] alias intCast_smul := Int.cast_smul_eq_zsmul

/-- `zsmul` is equal to any other module structure via a cast. -/
@[deprecated Int.cast_smul_eq_zsmul (since := "2024-07-23")]
theorem zsmul_eq_smul_cast (n : ‚Ñ§) (b : M) : n ‚Ä¢ b = (n : R) ‚Ä¢ b := (Int.cast_smul_eq_zsmul ..).symm

end

/-- Convert back any exotic `‚Ñ§`-smul to the canonical instance. This should not be needed since in
mathlib all `AddCommGroup`s should normally have exactly one `‚Ñ§`-module structure by design. -/
theorem int_smul_eq_zsmul (h : Module ‚Ñ§ M) (n : ‚Ñ§) (x : M) : @SMul.smul ‚Ñ§ M h.toSMul n x = n ‚Ä¢ x :=
  Int.cast_smul_eq_zsmul ..

/-- All `‚Ñ§`-module structures are equal. Not an instance since in mathlib all `AddCommGroup`
should normally have exactly one `‚Ñ§`-module structure by design. -/
def AddCommGroup.uniqueIntModule : Unique (Module ‚Ñ§ M) where
  default := by infer_instance
  uniq P := (Module.ext' P _) fun n => by convert int_smul_eq_zsmul P n

end AddCommGroup

theorem map_intCast_smul [AddCommGroup M] [AddCommGroup M‚ÇÇ] {F : Type*} [FunLike F M M‚ÇÇ]
    [AddMonoidHomClass F M M‚ÇÇ] (f : F) (R S : Type*) [Ring R] [Ring S] [Module R M] [Module S M‚ÇÇ]
    (x : ‚Ñ§) (a : M) :
    f ((x : R) ‚Ä¢ a) = (x : S) ‚Ä¢ f a := by simp only [Int.cast_smul_eq_zsmul, map_zsmul]

theorem map_natCast_smul [AddCommMonoid M] [AddCommMonoid M‚ÇÇ] {F : Type*} [FunLike F M M‚ÇÇ]
    [AddMonoidHomClass F M M‚ÇÇ] (f : F) (R S : Type*) [Semiring R] [Semiring S] [Module R M]
    [Module S M‚ÇÇ] (x : ‚Ñï) (a : M) : f ((x : R) ‚Ä¢ a) = (x : S) ‚Ä¢ f a := by
  simp only [Nat.cast_smul_eq_nsmul, AddMonoidHom.map_nsmul, map_nsmul]

instance AddCommGroup.intIsScalarTower {R : Type u} {M : Type v} [Ring R] [AddCommGroup M]
    [Module R M] : IsScalarTower ‚Ñ§ R M where
  smul_assoc n x y := ((smulAddHom R M).flip y).map_zsmul x n

section NoZeroSMulDivisors

/-! ### `NoZeroSMulDivisors`

This section defines the `NoZeroSMulDivisors` class, and includes some tests
for the vanishing of elements (especially in modules over division rings).
-/


/-- `NoZeroSMulDivisors R M` states that a scalar multiple is `0` only if either argument is `0`.
This is a version of saying that `M` is torsion free, without assuming `R` is zero-divisor free.

The main application of `NoZeroSMulDivisors R M`, when `M` is a module,
is the result `smul_eq_zero`: a scalar multiple is `0` iff either argument is `0`.

It is a generalization of the `NoZeroDivisors` class to heterogeneous multiplication.
-/
@[mk_iff]
class NoZeroSMulDivisors (R M : Type*) [Zero R] [Zero M] [SMul R M] : Prop where
  /-- If scalar multiplication yields zero, either the scalar or the vector was zero. -/
  eq_zero_or_eq_zero_of_smul_eq_zero : ‚àÄ {c : R} {x : M}, c ‚Ä¢ x = 0 ‚Üí c = 0 ‚à® x = 0

export NoZeroSMulDivisors (eq_zero_or_eq_zero_of_smul_eq_zero)

/-- Pullback a `NoZeroSMulDivisors` instance along an injective function. -/
theorem Function.Injective.noZeroSMulDivisors {R M N : Type*} [Zero R] [Zero M] [Zero N]
    [SMul R M] [SMul R N] [NoZeroSMulDivisors R N] (f : M ‚Üí N) (hf : Function.Injective f)
    (h0 : f 0 = 0) (hs : ‚àÄ (c : R) (x : M), f (c ‚Ä¢ x) = c ‚Ä¢ f x) : NoZeroSMulDivisors R M :=
  ‚ü®fun {_ _} h =>
    Or.imp_right (@hf _ _) <| h0.symm ‚ñ∏ eq_zero_or_eq_zero_of_smul_eq_zero (by rw [‚Üê hs, h, h0])‚ü©

-- See note [lower instance priority]
instance (priority := 100) NoZeroDivisors.toNoZeroSMulDivisors [Zero R] [Mul R]
    [NoZeroDivisors R] : NoZeroSMulDivisors R R :=
  ‚ü®fun {_ _} => eq_zero_or_eq_zero_of_mul_eq_zero‚ü©

theorem smul_ne_zero [Zero R] [Zero M] [SMul R M] [NoZeroSMulDivisors R M] {c : R} {x : M}
    (hc : c ‚â† 0) (hx : x ‚â† 0) : c ‚Ä¢ x ‚â† 0 := fun h =>
  (eq_zero_or_eq_zero_of_smul_eq_zero h).elim hc hx

section SMulWithZero

variable [Zero R] [Zero M] [SMulWithZero R M] [NoZeroSMulDivisors R M] {c : R} {x : M}

@[simp]
theorem smul_eq_zero : c ‚Ä¢ x = 0 ‚Üî c = 0 ‚à® x = 0 :=
  ‚ü®eq_zero_or_eq_zero_of_smul_eq_zero, fun h =>
    h.elim (fun h => h.symm ‚ñ∏ zero_smul R x) fun h => h.symm ‚ñ∏ smul_zero c‚ü©

theorem smul_ne_zero_iff : c ‚Ä¢ x ‚â† 0 ‚Üî c ‚â† 0 ‚àß x ‚â† 0 := by rw [Ne, smul_eq_zero, not_or]

lemma smul_eq_zero_iff_left (hx : x ‚â† 0) : c ‚Ä¢ x = 0 ‚Üî c = 0 := by simp [hx]
lemma smul_eq_zero_iff_right (hc : c ‚â† 0) : c ‚Ä¢ x = 0 ‚Üî x = 0 := by simp [hc]
lemma smul_ne_zero_iff_left (hx : x ‚â† 0) : c ‚Ä¢ x ‚â† 0 ‚Üî c ‚â† 0 := by simp [hx]
lemma smul_ne_zero_iff_right (hc : c ‚â† 0) : c ‚Ä¢ x ‚â† 0 ‚Üî x ‚â† 0 := by simp [hc]

end SMulWithZero

section Module

section Nat

theorem Nat.noZeroSMulDivisors
    (R) (M) [Semiring R] [CharZero R] [AddCommMonoid M] [Module R M] [NoZeroSMulDivisors R M] :
    NoZeroSMulDivisors ‚Ñï M where
  eq_zero_or_eq_zero_of_smul_eq_zero {c x} := by rw [‚Üê Nat.cast_smul_eq_nsmul R, smul_eq_zero]; simp

theorem two_nsmul_eq_zero
    (R) (M) [Semiring R] [CharZero R] [AddCommMonoid M] [Module R M] [NoZeroSMulDivisors R M]
    {v : M} : 2 ‚Ä¢ v = 0 ‚Üî v = 0 := by
  haveI := Nat.noZeroSMulDivisors R M
  simp [smul_eq_zero]

end Nat

variable [Semiring R] [AddCommMonoid M] [Module R M]
variable (R M)

/-- If `M` is an `R`-module with one and `M` has characteristic zero, then `R` has characteristic
zero as well. Usually `M` is an `R`-algebra. -/
theorem CharZero.of_module (M) [AddCommMonoidWithOne M] [CharZero M] [Module R M] : CharZero R := by
  refine ‚ü®fun m n h => @Nat.cast_injective M _ _ _ _ ?_‚ü©
  rw [‚Üê nsmul_one, ‚Üê nsmul_one, ‚Üê Nat.cast_smul_eq_nsmul R, ‚Üê Nat.cast_smul_eq_nsmul R, h]

end Module

section AddCommGroup

-- `R` can still be a semiring here
variable [Semiring R] [AddCommGroup M] [Module R M]

section SMulInjective

variable (M)

theorem smul_right_injective [NoZeroSMulDivisors R M] {c : R} (hc : c ‚â† 0) :
    Function.Injective (c ‚Ä¢ ¬∑ : M ‚Üí M) :=
  (injective_iff_map_eq_zero (smulAddHom R M c)).2 fun _ ha => (smul_eq_zero.mp ha).resolve_left hc

variable {M}

theorem smul_right_inj [NoZeroSMulDivisors R M] {c : R} (hc : c ‚â† 0) {x y : M} :
    c ‚Ä¢ x = c ‚Ä¢ y ‚Üî x = y :=
  (smul_right_injective M hc).eq_iff

end SMulInjective

section Nat

theorem self_eq_neg
    (R) (M) [Semiring R] [CharZero R] [AddCommGroup M] [Module R M] [NoZeroSMulDivisors R M]
    {v : M} : v = -v ‚Üî v = 0 := by
  rw [‚Üê two_nsmul_eq_zero R M, two_smul, add_eq_zero_iff_eq_neg]

theorem neg_eq_self
    (R) (M) [Semiring R] [CharZero R] [AddCommGroup M] [Module R M] [NoZeroSMulDivisors R M]
    {v : M} : -v = v ‚Üî v = 0 := by
  rw [eq_comm, self_eq_neg R M]

theorem self_ne_neg
    (R) (M) [Semiring R] [CharZero R] [AddCommGroup M] [Module R M] [NoZeroSMulDivisors R M]
    {v : M} : v ‚â† -v ‚Üî v ‚â† 0 :=
  (self_eq_neg R M).not

theorem neg_ne_self
    (R) (M) [Semiring R] [CharZero R] [AddCommGroup M] [Module R M] [NoZeroSMulDivisors R M]
    {v : M} : -v ‚â† v ‚Üî v ‚â† 0 :=
  (neg_eq_self R M).not

end Nat

end AddCommGroup

end NoZeroSMulDivisors

end Mathlib.Algebra.Module.Defs


section Mathlib.Combinatorics.Quiver.Basic

/-- A quiver `G` on a type `V` of vertices assigns to every pair `a b : V` of vertices
a type `a ‚ü∂ b` of arrows from `a` to `b`.

For graphs with no repeated edges, one can use `Quiver.{0} V`, which ensures
`a ‚ü∂ b : Prop`. For multigraphs, one can use `Quiver.{v+1} V`, which ensures
`a ‚ü∂ b : Type v`.

Because `Category` will later extend this class, we call the field `Hom`.
Except when constructing instances, you should rarely see this, and use the `‚ü∂` notation instead.
-/
class Quiver (V : Type u) where
  /-- The type of edges/arrows/morphisms between a given source and target. -/
  Hom : V ‚Üí V ‚Üí Sort v

/--
Notation for the type of edges/arrows/morphisms between a given source and target
in a quiver or category.
-/
infixr:10 " ‚ü∂ " => Quiver.Hom

/-- A morphism of quivers. As we will later have categorical functors extend this structure,
we call it a `Prefunctor`. -/
structure Prefunctor (V : Type u‚ÇÅ) [Quiver.{v‚ÇÅ} V] (W : Type u‚ÇÇ) [Quiver.{v‚ÇÇ} W] where
  /-- The action of a (pre)functor on vertices/objects. -/
  obj : V ‚Üí W
  /-- The action of a (pre)functor on edges/arrows/morphisms. -/
  map : ‚àÄ {X Y : V}, (X ‚ü∂ Y) ‚Üí (obj X ‚ü∂ obj Y)

end Mathlib.Combinatorics.Quiver.Basic


section Mathlib.CategoryTheory.Category.Basic

namespace CategoryTheory

/-- A preliminary structure on the way to defining a category,
containing the data, but none of the axioms. -/
@[pp_with_univ]
class CategoryStruct (obj : Type u) extends Quiver.{v + 1} obj : Type max u (v + 1) where
  /-- The identity morphism on an object. -/
  id : ‚àÄ X : obj, Hom X X
  /-- Composition of morphisms in a category, written `f ‚â´ g`. -/
  comp : ‚àÄ {X Y Z : obj}, (X ‚ü∂ Y) ‚Üí (Y ‚ü∂ Z) ‚Üí (X ‚ü∂ Z)

/-- Notation for the identity morphism in a category. -/
scoped notation "ùüô" => CategoryStruct.id  -- type as \b1

/-- Notation for composition of morphisms in a category. -/
scoped infixr:80 " ‚â´ " => CategoryStruct.comp -- type as \gg

/-- The typeclass `Category C` describes morphisms associated to objects of type `C`.
The universe levels of the objects and morphisms are unconstrained, and will often need to be
specified explicitly, as `Category.{v} C`. (See also `LargeCategory` and `SmallCategory`.)

See <https://stacks.math.columbia.edu/tag/0014>.
-/
@[pp_with_univ]
class Category (obj : Type u) extends CategoryStruct.{v} obj : Type max u (v + 1) where
  /-- Identity morphisms are left identities for composition. -/
  id_comp : ‚àÄ {X Y : obj} (f : X ‚ü∂ Y), ùüô X ‚â´ f = f
  /-- Identity morphisms are right identities for composition. -/
  comp_id : ‚àÄ {X Y : obj} (f : X ‚ü∂ Y), f ‚â´ ùüô Y = f
  /-- Composition in a category is associative. -/
  assoc : ‚àÄ {W X Y Z : obj} (f : W ‚ü∂ X) (g : X ‚ü∂ Y) (h : Y ‚ü∂ Z), (f ‚â´ g) ‚â´ h = f ‚â´ g ‚â´ h

attribute [simp] Category.id_comp Category.comp_id Category.assoc
attribute [trans] CategoryStruct.comp

end CategoryTheory

end Mathlib.CategoryTheory.Category.Basic


section Mathlib.CategoryTheory.Functor.Basic

namespace CategoryTheory

section

/-- `Functor C D` represents a functor between categories `C` and `D`.

To apply a functor `F` to an object use `F.obj X`, and to a morphism use `F.map f`.

The axiom `map_id` expresses preservation of identities, and
`map_comp` expresses functoriality.

See <https://stacks.math.columbia.edu/tag/001B>.
-/
structure Functor (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] (D : Type u‚ÇÇ) [Category.{v‚ÇÇ} D]
    extends Prefunctor C D : Type max v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ where
  /-- A functor preserves identity morphisms. -/
  map_id : ‚àÄ X : C, map (ùüô X) = ùüô (obj X)
  /-- A functor preserves composition. -/
  map_comp : ‚àÄ {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z), map (f ‚â´ g) = map f ‚â´ map g

/-- The prefunctor between the underlying quivers. -/
add_decl_doc Functor.toPrefunctor

end

/-- Notation for a functor between categories. -/
-- A functor is basically a function, so give ‚•§ a similar precedence to ‚Üí (25).
-- For example, `C √ó D ‚•§ E` should parse as `(C √ó D) ‚•§ E` not `C √ó (D ‚•§ E)`.
scoped [CategoryTheory] infixr:26 " ‚•§ " => Functor -- type as \func

attribute [simp] Functor.map_id Functor.map_comp

end CategoryTheory

end Mathlib.CategoryTheory.Functor.Basic

section Mathlib.CategoryTheory.NatTrans

namespace CategoryTheory

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]

/-- `NatTrans F G` represents a natural transformation between functors `F` and `G`.

The field `app` provides the components of the natural transformation.

Naturality is expressed by `Œ±.naturality`.
-/
@[ext]
structure NatTrans (F G : C ‚•§ D) : Type max u‚ÇÅ v‚ÇÇ where
  /-- The component of a natural transformation. -/
  app : ‚àÄ X : C, F.obj X ‚ü∂ G.obj X
  /-- The naturality square for a given morphism. -/
  naturality : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), F.map f ‚â´ app Y = app X ‚â´ G.map f

-- Rather arbitrarily, we say that the 'simpler' form is
-- components of natural transformations moving earlier.
attribute [simp] NatTrans.naturality

@[simp]
theorem NatTrans.naturality_assoc {F G : C ‚•§ D} (self : NatTrans F G) ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y) {Z : D}
    (h : G.obj Y ‚ü∂ Z) : F.map f ‚â´ self.app Y ‚â´ h = self.app X ‚â´ G.map f ‚â´ h := by
  rw [‚Üê Category.assoc, NatTrans.naturality, Category.assoc]

namespace NatTrans

/-- `NatTrans.id F` is the identity natural transformation on a functor `F`. -/
protected def id (F : C ‚•§ D) : NatTrans F F where
  app X := ùüô (F.obj X)
  naturality := by 
    intro X Y f
    simp_all only [Category.comp_id, Category.id_comp]

@[simp]
theorem id_app' (F : C ‚•§ D) (X : C) : (NatTrans.id F).app X = ùüô (F.obj X) := rfl

open Category

open CategoryTheory.Functor

section

variable {F G H I : C ‚•§ D}

/-- `vcomp Œ± Œ≤` is the vertical compositions of natural transformations. -/
def vcomp (Œ± : NatTrans F G) (Œ≤ : NatTrans G H) : NatTrans F H where
  app X := Œ±.app X ‚â´ Œ≤.app X
  naturality := by 
    intro X Y f
    simp_all only [naturality_assoc, naturality, assoc]

-- functor_category will rewrite (vcomp Œ± Œ≤) to (Œ± ‚â´ Œ≤), so this is not a
-- suitable simp lemma.  We will declare the variant vcomp_app' there.
theorem vcomp_app (Œ± : NatTrans F G) (Œ≤ : NatTrans G H) (X : C) :
    (vcomp Œ± Œ≤).app X = Œ±.app X ‚â´ Œ≤.app X := rfl

end

/-- The diagram
    F(f)      F(g)      F(h)
F X ----> F Y ----> F U ----> F U
 |         |         |         |
 | Œ±(X)    | Œ±(Y)    | Œ±(U)    | Œ±(V)
 v         v         v         v
G X ----> G Y ----> G U ----> G V
    G(f)      G(g)      G(h)
commutes.
-/
example {F G : C ‚•§ D} (Œ± : NatTrans F G) {X Y U V : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ U) (h : U ‚ü∂ V) :
    Œ±.app X ‚â´ G.map f ‚â´ G.map g ‚â´ G.map h = F.map f ‚â´ F.map g ‚â´ F.map h ‚â´ Œ±.app V := by
  simp

end NatTrans

end CategoryTheory
end Mathlib.CategoryTheory.NatTrans

section Mathlib.CategoryTheory.Functor.Category

namespace CategoryTheory

open NatTrans Category CategoryTheory.Functor

variable (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] (D : Type u‚ÇÇ) [Category.{v‚ÇÇ} D]

attribute [local simp] vcomp_app

variable {C D} {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E]
variable {F G H I : C ‚•§ D}

/-- `Functor.category C D` gives the category structure on functors and natural transformations
between categories `C` and `D`.

Notice that if `C` and `D` are both small categories at the same universe level,
this is another small category at that level.
However if `C` and `D` are both large categories at the same universe level,
this is a small category at the next higher level.
-/
instance Functor.category : Category.{max u‚ÇÅ v‚ÇÇ} (C ‚•§ D) where
  Hom F G := NatTrans F G
  id F := NatTrans.id F
  comp Œ± Œ≤ := vcomp Œ± Œ≤
  id_comp := by 
    intro X Y f
    simp_all only
    ext x : 2
    simp_all only [vcomp_app, id_app', id_comp]
  comp_id := by 
    intro X Y f
    simp_all only
    ext x : 2
    simp_all only [vcomp_app, id_app', comp_id]
  assoc := by 
    intro W X Y Z f g h
    simp_all only
    ext x : 2
    simp_all only [vcomp_app, assoc]

namespace NatTrans

-- Porting note: the behaviour of `ext` has changed here.
-- We need to provide a copy of the `NatTrans.ext` lemma,
-- written in terms of `F ‚ü∂ G` rather than `NatTrans F G`,
-- or `ext` will not retrieve it from the cache.
@[ext]
theorem ext' {Œ± Œ≤ : F ‚ü∂ G} (w : Œ±.app = Œ≤.app) : Œ± = Œ≤ := NatTrans.ext w

end NatTrans

open NatTrans

end CategoryTheory
end Mathlib.CategoryTheory.Functor.Category

noncomputable
section Mathlib.CategoryTheory.Limits.Shapes.ZeroMorphisms

open scoped Classical

namespace CategoryTheory.Limits

variable (C : Type u) [Category.{v} C]
variable (D : Type u') [Category.{v'} D]

/-- A category "has zero morphisms" if there is a designated "zero morphism" in each morphism space,
and compositions of zero morphisms with anything give the zero morphism. -/
class HasZeroMorphisms where
  /-- Every morphism space has zero -/
  [zero : ‚àÄ X Y : C, Zero (X ‚ü∂ Y)]
  /-- `f` composed with `0` is `0` -/
  comp_zero : ‚àÄ {X Y : C} (f : X ‚ü∂ Y) (Z : C), f ‚â´ (0 : Y ‚ü∂ Z) = (0 : X ‚ü∂ Z)
  /-- `0` composed with `f` is `0` -/
  zero_comp : ‚àÄ (X : C) {Y Z : C} (f : Y ‚ü∂ Z), (0 : X ‚ü∂ Y) ‚â´ f = (0 : X ‚ü∂ Z)

attribute [instance] HasZeroMorphisms.zero

variable {C}

@[simp]
theorem comp_zero [HasZeroMorphisms C] {X Y : C} {f : X ‚ü∂ Y} {Z : C} :
    f ‚â´ (0 : Y ‚ü∂ Z) = (0 : X ‚ü∂ Z) :=
  HasZeroMorphisms.comp_zero f Z

@[simp]
theorem zero_comp [HasZeroMorphisms C] {X : C} {Y Z : C} {f : Y ‚ü∂ Z} :
    (0 : X ‚ü∂ Y) ‚â´ f = (0 : X ‚ü∂ Z) :=
  HasZeroMorphisms.zero_comp X f

end CategoryTheory.Limits

end Mathlib.CategoryTheory.Limits.Shapes.ZeroMorphisms

section Mathlib.CategoryTheory.Preadditive.Basic

open CategoryTheory.Limits

namespace CategoryTheory

variable (C : Type u) [Category.{v} C]

/-- A category is called preadditive if `P ‚ü∂ Q` is an abelian group such that composition is
    linear in both variables. -/
class Preadditive where
  homGroup : ‚àÄ P Q : C, AddCommGroup (P ‚ü∂ Q) := by infer_instance
  add_comp : ‚àÄ (P Q R : C) (f f' : P ‚ü∂ Q) (g : Q ‚ü∂ R), (f + f') ‚â´ g = f ‚â´ g + f' ‚â´ g
  comp_add : ‚àÄ (P Q R : C) (f : P ‚ü∂ Q) (g g' : Q ‚ü∂ R), f ‚â´ (g + g') = f ‚â´ g + f ‚â´ g'

attribute [inherit_doc Preadditive] Preadditive.homGroup Preadditive.add_comp Preadditive.comp_add

attribute [instance] Preadditive.homGroup

attribute [simp] Preadditive.add_comp

-- (the linter doesn't like `simp` on this lemma)
attribute [simp] Preadditive.comp_add

end CategoryTheory

namespace CategoryTheory

namespace Preadditive

open AddMonoidHom

variable {C : Type u} [Category.{v} C] [Preadditive C]

/-- Composition by a fixed left argument as a group homomorphism -/
def leftComp {P Q : C} (R : C) (f : P ‚ü∂ Q) : (Q ‚ü∂ R) ‚Üí+ (P ‚ü∂ R) :=
  mk' (fun g => f ‚â´ g) fun g g' => by simp

/-- Composition by a fixed right argument as a group homomorphism -/
def rightComp (P : C) {Q R : C} (g : Q ‚ü∂ R) : (P ‚ü∂ Q) ‚Üí+ (P ‚ü∂ R) :=
  mk' (fun f => f ‚â´ g) fun f f' => by simp

variable {P Q R : C} (f f' : P ‚ü∂ Q) (g g' : Q ‚ü∂ R)

@[simp]
theorem sub_comp : (f - f') ‚â´ g = f ‚â´ g - f' ‚â´ g :=
  map_sub (rightComp P g) f f'

@[simp]
theorem comp_sub : f ‚â´ (g - g') = f ‚â´ g - f ‚â´ g' :=
  map_sub (leftComp R f) g g'

@[simp]
theorem neg_comp : (-f) ‚â´ g = -f ‚â´ g :=
  map_neg (rightComp P g) f

@[simp]
theorem comp_neg : f ‚â´ (-g) = -f ‚â´ g :=
  map_neg (leftComp R f) g

instance (priority := 100) preadditiveHasZeroMorphisms : HasZeroMorphisms C where
  zero := inferInstance
  comp_zero f R := show leftComp R f 0 = 0 from map_zero _
  zero_comp P _ _ f := show rightComp P f 0 = 0 from map_zero _

end Preadditive

end CategoryTheory

end Mathlib.CategoryTheory.Preadditive.Basic

section Mathlib.CategoryTheory.Preadditive.Basic

namespace CategoryTheory

open CategoryTheory.Limits Preadditive

variable {C D : Type*} [Category C] [Category D] [Preadditive D]

instance {F G : C ‚•§ D} : Zero (F ‚ü∂ G) where
  zero :=
   { app := fun X => 0
     naturality := by 
       intro X Y f
       simp_all only [comp_zero, zero_comp] }

instance {F G : C ‚•§ D} : Add (F ‚ü∂ G) where
  add Œ± Œ≤ :=
  { app := fun X => Œ±.app X + Œ≤.app X,
    naturality := by 
      intro X Y f
      simp_all only [comp_add, NatTrans.naturality, add_comp] }

instance {F G : C ‚•§ D} : Neg (F ‚ü∂ G) where
  neg Œ± :=
  { app := fun X => -Œ±.app X,
    naturality := by 
      intro X Y f
      simp_all only [comp_neg, NatTrans.naturality, neg_comp] }

instance functorCategoryPreadditive : Preadditive (C ‚•§ D) where
  homGroup F G :=
    { nsmul := nsmulRec
      zsmul := zsmulRec
      sub := fun Œ± Œ≤ =>
      { app := fun X => Œ±.app X - Œ≤.app X
        naturality := by 
          intro X Y f
          simp_all only [comp_sub, NatTrans.naturality, sub_comp] }
      add_assoc := by
        intros
        ext
        apply add_assoc
      zero_add := by
        intros
        dsimp
        ext
        apply zero_add
      add_zero := by
        intros
        dsimp
        ext
        apply add_zero
      add_comm := by
        intros
        dsimp
        ext
        apply add_comm
      sub_eq_add_neg := by
        intros
        dsimp
        ext
        apply sub_eq_add_neg
      neg_add_cancel := by
        intros
        dsimp
        ext
        apply neg_add_cancel }
  add_comp := by
    intros
    dsimp
    ext
    apply add_comp
  comp_add := by
    intros
    dsimp
    ext
    apply comp_add

end CategoryTheory

end Mathlib.CategoryTheory.Preadditive.Basic

section Mathlib.CategoryTheory.Linear.Basic

open CategoryTheory.Limits

namespace CategoryTheory

/-- A category is called `R`-linear if `P ‚ü∂ Q` is an `R`-module such that composition is
    `R`-linear in both variables. -/
class Linear (R : Type w) [Semiring R] (C : Type u) [Category.{v} C] [Preadditive C] where
  homModule : ‚àÄ X Y : C, Module R (X ‚ü∂ Y) := by infer_instance
  /-- compatibility of the scalar multiplication with the post-composition -/
  smul_comp : ‚àÄ (X Y Z : C) (r : R) (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z), (r ‚Ä¢ f) ‚â´ g = r ‚Ä¢ f ‚â´ g
  /-- compatibility of the scalar multiplication with the pre-composition -/
  comp_smul : ‚àÄ (X Y Z : C) (f : X ‚ü∂ Y) (r : R) (g : Y ‚ü∂ Z), f ‚â´ (r ‚Ä¢ g) = r ‚Ä¢ f ‚â´ g

attribute [instance] Linear.homModule

attribute [simp] Linear.smul_comp Linear.comp_smul

-- (the linter doesn't like `simp` on the `_assoc` lemma)
end CategoryTheory

end Mathlib.CategoryTheory.Linear.Basic

open CategoryTheory

namespace CategoryTheory

open CategoryTheory.Limits Linear
open CategoryTheory.Linear

variable {R : Type*} [Semiring R]
variable {C D : Type*} [Category C] [Category D] [Preadditive D] [Linear R D]

#adaptation_note
/--
At nightly-2024-08-08 we needed to significantly increase the maxHeartbeats here.
-/
set_option maxHeartbeats 8000000 in
instance functorCategoryLinear : Linear R (C ‚•§ D) where
  homModule F G :=
    { 
      smul := fun r Œ± ‚Ü¶ 
        { app := fun X ‚Ü¶ r ‚Ä¢ Œ±.app X
          naturality := by
            intros
            rw [Linear.comp_smul, Linear.smul_comp, Œ±.naturality] }
      one_smul := by
        intros
        ext
        apply one_smul
      zero_smul := by
        intros
        ext
        apply zero_smul
      smul_zero := by
        intros
        ext
        apply smul_zero
      add_smul := by
        intros
        ext
        apply add_smul
      smul_add := by
        intros
        ext
        apply smul_add
      mul_smul := by
        intros
        ext
        apply mul_smul
        }
  smul_comp := by
    intros
    ext
    apply Linear.smul_comp
  comp_smul := by
    intros
    ext
    apply Linear.comp_smul

instance functorCategorySMul (F G : C ‚•§ D) : SMul R (F ‚ü∂ G) where
  smul r Œ± := 
    { app := fun X => r ‚Ä¢ Œ±.app X
      naturality := by
        intros
        rw [Linear.comp_smul, Linear.smul_comp, Œ±.naturality] }

instance functorCategoryLinear' : Linear R (C ‚•§ D) where
  homModule F G :=
    { one_smul := by
        intros
        ext
        apply one_smul
      zero_smul := by
        intros
        ext
        apply zero_smul
      smul_zero := by
        intros
        ext
        apply smul_zero
      add_smul := by
        intros
        ext
        apply add_smul
      smul_add := by
        intros
        ext
        apply smul_add
      mul_smul := by
        intros
        ext
        apply mul_smul
        }
  smul_comp := by
    intros
    ext
    apply Linear.smul_comp
  comp_smul := by
    intros
    ext
    apply Linear.comp_smul

end CategoryTheory

#show_unused  CategoryTheory.functorCategoryLinear CategoryTheory.functorCategoryLinear'
#min_imports
