/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Lezeau, Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.FiberedCategory.HomLift
import Mathlib.CategoryTheory.Bicategory.Strict
import Mathlib.CategoryTheory.Functor.Category

/-!
# The bicategory of based categories

In this file we define the type `BasedCategory ğ’®`, and give it the structure of a strict
bicategory. Given a category `ğ’®`, we define the type `BasedCategory ğ’®` as the type of categories
`ğ’³` equiped with a functor `ğ’³.p : ğ’³ â¥¤ ğ’®`.

We also define functors between based categories `ğ’³ ğ’´ : BasedCategory ğ’®`, via the structure
`ğ’³ â¥¤áµ‡ ğ’´ `. These are defined as functors between the underlying categories `ğ’³.obj` and
`ğ’´.obj` which commute with the projections to `ğ’®`.

Natural transformations between based functors `F G : ğ’³ â¥¤áµ‡ ğ’´ ` are given by the structure
`BasedNatTrans F G`. These are defined as natural transformations `Î±` between the functors
underlying `F` and `G` such that `Î±.app a` lifts `ğŸ™ S` whenever `ğ’³.p.obj a = S`.
-/

universe vâ‚… uâ‚… vâ‚„ uâ‚„ vâ‚ƒ uâ‚ƒ vâ‚‚ uâ‚‚ vâ‚ uâ‚

namespace CategoryTheory

open CategoryTheory Functor Category NatTrans IsHomLift

variable {ğ’® : Type uâ‚} [Category.{vâ‚} ğ’®]

/-- A based category over `ğ’®` is a category `ğ’³` together with a functor `p : ğ’³ â¥¤ ğ’®`. -/
@[nolint checkUnivs]
structure BasedCategory (ğ’® : Type uâ‚) [Category.{vâ‚} ğ’®] where
  /-- The type of objects in a `BasedCategory`-/
  obj : Type uâ‚‚
  /-- The underlying category of a `BasedCategory`. -/
  category : Category.{vâ‚‚} obj := by infer_instance
  /-- The functor to the base. -/
  p : obj â¥¤ ğ’®

instance (ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) : Category ğ’³.obj := ğ’³.category

/-- The based category associated to a functor `p : ğ’³ â¥¤ ğ’®`. -/
def BasedCategory.ofFunctor {ğ’³ : Type uâ‚‚} [Category.{vâ‚‚} ğ’³] (p : ğ’³ â¥¤ ğ’®) : BasedCategory ğ’® where
  obj := ğ’³
  p := p

/-- A functor between based categories is a functor between the underlying categories that commutes
with the projections. -/
structure BasedFunctor (ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) (ğ’´ : BasedCategory.{vâ‚ƒ, uâ‚ƒ} ğ’®) extends
    CategoryTheory.Functor ğ’³.obj ğ’´.obj where
  w : toFunctor â‹™ ğ’´.p = ğ’³.p := by aesop_cat

/-- Notation for `BasedFunctor`. -/
scoped infixr:26 " â¥¤áµ‡ " => BasedFunctor

namespace BasedFunctor

/-- The identity based functor. -/
@[simps!]
def id (ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) : ğ’³ â¥¤áµ‡ ğ’³ where
  toFunctor := ğŸ­ ğ’³.obj

variable {ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®} {ğ’´ : BasedCategory.{vâ‚ƒ, uâ‚ƒ} ğ’®}

/-- The composition of two based functors. -/
@[simps!]
def comp {ğ’µ : BasedCategory.{vâ‚„, uâ‚„} ğ’®} (F : ğ’³ â¥¤áµ‡ ğ’´) (G : ğ’´ â¥¤áµ‡ ğ’µ) : ğ’³ â¥¤áµ‡ ğ’µ where
  toFunctor := F.toFunctor â‹™ G.toFunctor
  w := by rw [Functor.assoc, G.w, F.w]

@[simp]
lemma comp_id (F : ğ’³ â¥¤áµ‡ ğ’´) : comp F (id ğ’´) = F :=
  rfl

@[simp]
lemma id_comp (F : ğ’³ â¥¤áµ‡ ğ’´) : comp (id ğ’³) F = F :=
  rfl

@[simp]
lemma comp_assoc {ğ’µ : BasedCategory.{vâ‚„, uâ‚„} ğ’®} {ğ’œ : BasedCategory.{vâ‚…, uâ‚…} ğ’®} (F : ğ’³ â¥¤áµ‡ ğ’´)
    (G : ğ’´ â¥¤áµ‡ ğ’µ) (H : ğ’µ â¥¤áµ‡ ğ’œ) : comp (comp F G) H = comp F (comp G H) :=
  rfl

@[simp]
lemma w_obj (F : ğ’³ â¥¤áµ‡ ğ’´) (a : ğ’³.obj) : ğ’´.p.obj (F.obj a) = ğ’³.p.obj a := by
  rw [â† Functor.comp_obj, F.w]

instance (F : ğ’³ â¥¤áµ‡ ğ’´) (a : ğ’³.obj) : IsHomLift ğ’´.p (ğŸ™ (ğ’³.p.obj a)) (ğŸ™ (F.obj a)) :=
  IsHomLift.id (w_obj F a)

section

variable (F : ğ’³ â¥¤áµ‡ ğ’´) {R S : ğ’®} {a b : ğ’³.obj} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)

/-- For a based functor `F : ğ’³ âŸ¶ ğ’´`, then whenever an arrow `Ï†` in `ğ’³` lifts some `f` in `ğ’®`,
then `F(Ï†)` also lifts `f`. -/
instance preserves_isHomLift [IsHomLift ğ’³.p f Ï†] : IsHomLift ğ’´.p f (F.map Ï†) := by
  apply of_fac ğ’´.p f (F.map Ï†) (Eq.trans (F.w_obj a) (domain_eq ğ’³.p f Ï†))
    (Eq.trans (F.w_obj b) (codomain_eq ğ’³.p f Ï†))
  rw [â† Functor.comp_map, congr_hom F.w]
  simpa using (fac ğ’³.p f Ï†)

/-- For a based functor `F : ğ’³ âŸ¶ ğ’´`, and an arrow `Ï†` in `ğ’³`, then `Ï†` lifts an arrow `f` in `ğ’®`
if `F(Ï†)` does. -/
lemma isHomLift_map [IsHomLift ğ’´.p f (F.map Ï†)] : IsHomLift ğ’³.p f Ï† := by
  apply of_fac ğ’³.p f Ï†  (F.w_obj a â–¸ domain_eq ğ’´.p f (F.map Ï†))
    (F.w_obj b â–¸ codomain_eq ğ’´.p f (F.map Ï†))
  simp [congr_hom F.w.symm, fac ğ’´.p f (F.map Ï†)]

lemma isHomLift_iff : IsHomLift ğ’´.p f (F.map Ï†) â†” IsHomLift ğ’³.p f Ï† :=
  âŸ¨fun _ => isHomLift_map F f Ï†, fun _ => preserves_isHomLift F f Ï†âŸ©

end

end BasedFunctor


/-- A `BasedNatTrans` between two `BasedFunctor`s is a natural transformation `Î±` between the
underlying functors, such that for all `a : ğ’³`, `Î±.app a` lifts `ğŸ™ S` whenever `ğ’³.p.obj a = S`. -/
structure BasedNatTrans {ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®} {ğ’´ : BasedCategory.{vâ‚ƒ, uâ‚ƒ} ğ’®}
    (F G : ğ’³ â¥¤áµ‡ ğ’´) extends CategoryTheory.NatTrans F.toFunctor G.toFunctor where
  isHomLift' : âˆ€ (a : ğ’³.obj), IsHomLift ğ’´.p (ğŸ™ (ğ’³.p.obj a)) (toNatTrans.app a) := by aesop_cat

namespace BasedNatTrans

open BasedFunctor

variable {ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®} {ğ’´ : BasedCategory.{vâ‚ƒ, uâ‚ƒ} ğ’®}

section

variable {F G : ğ’³ â¥¤áµ‡ ğ’´} (Î± : BasedNatTrans F G)

@[ext]
lemma ext (Î² : BasedNatTrans F G) (h : Î±.toNatTrans = Î².toNatTrans) : Î± = Î² := by
  cases Î±; subst h; rfl

instance app_isHomLift (a : ğ’³.obj) : IsHomLift ğ’´.p (ğŸ™ (ğ’³.p.obj a)) (Î±.toNatTrans.app a) :=
  Î±.isHomLift' a

lemma isHomLift {a : ğ’³.obj} {S : ğ’®} (ha : ğ’³.p.obj a = S) :
    IsHomLift ğ’´.p (ğŸ™ S) (Î±.toNatTrans.app a) := by
  subst ha; infer_instance

end

/-- The identity natural transformation is a `BasedNatTrans`. -/
@[simps!]
def id (F : ğ’³ â¥¤áµ‡ ğ’´) : BasedNatTrans F F where
  toNatTrans := CategoryTheory.NatTrans.id F.toFunctor
  isHomLift' := fun a => of_fac ğ’´.p _ _ (w_obj F a) (w_obj F a) (by simp)

/-- Composition of `BasedNatTrans`, given by composition of the underlying natural
transformations. -/
@[simps!]
def comp {F G H : ğ’³ â¥¤áµ‡ ğ’´} (Î± : BasedNatTrans F G) (Î² : BasedNatTrans G H) :
    BasedNatTrans F H where
  toNatTrans := CategoryTheory.NatTrans.vcomp Î±.toNatTrans Î².toNatTrans
  isHomLift' := by
    intro a
    rw [CategoryTheory.NatTrans.vcomp_app]
    infer_instance

@[simps!]
instance homCategory (ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) (ğ’´ : BasedCategory.{vâ‚ƒ, uâ‚ƒ} ğ’®) :
    Category (ğ’³ â¥¤áµ‡ ğ’´) where
  Hom := BasedNatTrans
  id := BasedNatTrans.id
  comp := BasedNatTrans.comp

variable (F : ğ’³ â¥¤áµ‡ ğ’´)

@[simp]
lemma id_toNatTrans : (ğŸ™ F : F âŸ¶ F).toNatTrans = CategoryTheory.NatTrans.id F.toFunctor :=
  rfl

variable {F} {G : ğ’³ â¥¤áµ‡ ğ’´}

@[ext]
lemma homCategory.ext (Î± Î² : F âŸ¶ G) (h : Î±.toNatTrans = Î².toNatTrans) : Î± = Î² :=
  BasedNatTrans.ext Î± Î² h


@[simp]
lemma comp_toNatTrans {H : ğ’³ â¥¤áµ‡ ğ’´} (Î± : F âŸ¶ G) (Î² : G âŸ¶ H) :
    (Î± â‰« Î² : F âŸ¶ H).toNatTrans = (Î±.toNatTrans â‰« Î².toNatTrans : F.toFunctor âŸ¶ H.toFunctor) :=
  rfl


end BasedNatTrans

namespace BasedCategory

open BasedFunctor BasedNatTrans

section

variable {ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®} {ğ’´ : BasedCategory.{vâ‚ƒ, uâ‚ƒ} ğ’®}

/-- The inverse of a based natural transformation whose underlying natural tranformation is an
isomorphism. -/
def mkNatIso {F G : ğ’³ â¥¤áµ‡ ğ’´} (Î± : F.toFunctor â‰… G.toFunctor)
    (isHomLift' : âˆ€ a : ğ’³.obj, IsHomLift ğ’´.p (ğŸ™ (ğ’³.p.obj a)) (Î±.hom.app a)) : F â‰… G where
  hom := { toNatTrans := Î±.hom }
  inv := {
    toNatTrans := Î±.inv
    isHomLift' := by
      intro a
      specialize isHomLift' a
      rw [â† NatIso.app_inv]
      rw [â† NatIso.app_hom] at isHomLift'
      apply IsHomLift.lift_id_inv }

/-- The inverse of a based natural transformation whose underlying natural tranformation carries an
`IsIso` instance. -/
noncomputable def mkNatIsIso {F G : ğ’³ â¥¤áµ‡ ğ’´} (Î± : F.toFunctor âŸ¶ G.toFunctor)
    [IsIso Î±] (isHomLift' : âˆ€ a : ğ’³.obj, IsHomLift ğ’´.p (ğŸ™ (ğ’³.p.obj a)) (Î±.app a)) : F â‰… G where
  hom := { toNatTrans := Î± }
  inv := { toNatTrans := inv Î±, isHomLift' := fun a => by simp [lift_id_inv_isIso] }

/-- The identity natural transformation is a based natural isomorphism. -/
@[simps]
def BasedNatIso.id (F : ğ’³ â¥¤áµ‡ ğ’´) : F â‰… F where
  hom := ğŸ™ F
  inv := ğŸ™ F

/-- Left-whiskering in the bicategory `BasedCategory` is given by whiskering the underlying functors
and natural transformations. -/
@[simps!]
def whiskerLeft {ğ’µ : BasedCategory.{vâ‚„, uâ‚„} ğ’®} (F : ğ’³ â¥¤áµ‡ ğ’´) {G H : ğ’´ â¥¤áµ‡ ğ’µ}
    (Î± : G âŸ¶ H) : BasedFunctor.comp F G âŸ¶ BasedFunctor.comp F H where
  toNatTrans := CategoryTheory.whiskerLeft F.toFunctor Î±.toNatTrans
  isHomLift' := fun a => Î±.isHomLift (F.w_obj a)

/-- Right-whiskering in the bicategory `BasedCategory` is given by whiskering the underlying
functors and natural transformations. -/
@[simps!]
def whiskerRight {ğ’µ : BasedCategory.{vâ‚„, uâ‚„} ğ’®} {F G : ğ’³ â¥¤áµ‡ ğ’´} (Î± : F âŸ¶ G)
    (H : ğ’´ â¥¤áµ‡ ğ’µ) : BasedFunctor.comp F H âŸ¶ BasedFunctor.comp G H where
  toNatTrans := CategoryTheory.whiskerRight Î±.toNatTrans H.toFunctor
  isHomLift' := fun a => by apply BasedFunctor.preserves_isHomLift

end

/-- The category of based categories. -/
@[simps!]
instance : Category (BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) where
  Hom := BasedFunctor
  id := id
  comp := comp

/-- The bicategory of based categories. -/
instance bicategory : Bicategory (BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) where
  Hom ğ’³ ğ’´ :=  ğ’³ â¥¤áµ‡ ğ’´
  id ğ’³ := id ğ’³
  comp F G := BasedFunctor.comp F G
  homCategory ğ’³ ğ’´ := homCategory ğ’³ ğ’´
  whiskerLeft {ğ’³ ğ’´ ğ’µ} F {G H} Î± := whiskerLeft F Î±
  whiskerRight {ğ’³ ğ’´ ğ’µ} F G Î± H := whiskerRight Î± H
  associator F G H := BasedNatIso.id _
  leftUnitor {ğ’³ ğ’´} F := BasedNatIso.id F
  rightUnitor {ğ’³ ğ’´} F := BasedNatIso.id F

/-- The bicategory structure on `BasedCategory.{vâ‚‚, uâ‚‚} ğ’®` is strict. -/
instance : Bicategory.Strict (BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) where

end BasedCategory

end CategoryTheory
