/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Lezeau, Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.FiberedCategory.HomLift
import Mathlib.CategoryTheory.Bicategory.Strict
import Mathlib.CategoryTheory.Functor.Category

/-!
# The bicategory of based categories

In this file we define the type `BasedCategory ğ’®`, and give it the structure of a strict
bicategory. Given a category `ğ’®`, we define the type `BasedCategory ğ’®` as the type of categories
`ğ’³` equiped with a functor `ğ’³.p : ğ’³ â¥¤ ğ’®`.

We also define functors between based categories `ğ’³ ğ’´ : BasedCategory ğ’®`, via the structure
`BasedFunctor ğ’³ ğ’´`. These are defined as functors between the underlying categories `ğ’³.cat` and
`ğ’´.cat` which commute with the projections to `ğ’®`.

Natural transformations between based functors `F G : BasedFunctor ğ’³ ğ’´` are given by the structure
`BasedNatTrans F G`. These are defined as natural transformations `Î±` between the functors
underlying `F` and `G` such that `Î±.app a` lifts `ğŸ™ S` whenever `ğ’³.p.obj a = S`.
-/

universe uâ‚ vâ‚ uâ‚‚ vâ‚‚

open CategoryTheory Functor Category NatTrans IsHomLift

namespace Fibered

variable {ğ’® : Type uâ‚} [Category.{vâ‚} ğ’®]

/-- A based category over `ğ’®` is a category `ğ’³` together with a functor `p : ğ’³ â¥¤ ğ’®` -/
structure BasedCategory (ğ’® : Type uâ‚) [Category.{vâ‚} ğ’®] where
  (cat : Type uâ‚‚)
  [isCat : Category.{vâ‚‚} cat]
  (p : cat â¥¤ ğ’®)

instance (ğ’³ : BasedCategory ğ’®) : Category ğ’³.cat := ğ’³.isCat

/-- A functor between based categories is a functor between the underlying categories that commutes with the projections. -/
structure BasedFunctor (ğ’³ ğ’´ : BasedCategory ğ’®) extends CategoryTheory.Functor ğ’³.cat ğ’´.cat where
  (w : toFunctor â‹™ ğ’´.p = ğ’³.p)

namespace BasedFunctor

/-- The identity functor is also a `BasedFunctor` -/
@[simps!]
protected def id (ğ’³ : BasedCategory ğ’®) : BasedFunctor ğ’³ ğ’³ :=
  { ğŸ­ ğ’³.cat with w := CategoryTheory.Functor.id_comp _ }

section

variable {ğ’³ ğ’´ : BasedCategory ğ’®} (F : BasedFunctor ğ’³ ğ’´)

/-- Composition of `BasedFunctor`s is defined as the composition of the underlying functors -/
@[simps!]
def comp {ğ’µ : BasedCategory ğ’®} (G : BasedFunctor ğ’´ ğ’µ) : BasedFunctor ğ’³ ğ’µ :=
  { F.toFunctor â‹™ G.toFunctor with w := by rw [Functor.assoc, G.w, F.w] }

@[simp]
lemma assoc {ğ’µ ğ’¯ : BasedCategory ğ’®} (G : BasedFunctor ğ’´ ğ’µ) (H : BasedFunctor ğ’µ ğ’¯) :
    comp (comp F G) H = comp F (comp G H) :=
  rfl

@[simp]
lemma comp_id : comp (BasedFunctor.id ğ’³) F = F :=
  rfl

@[simp]
lemma id_comp : comp F (BasedFunctor.id ğ’´) = F :=
  rfl

@[simp]
lemma w_obj (a : ğ’³.cat) : ğ’´.p.obj (F.obj a) = ğ’³.p.obj a := by
  rw [â†Functor.comp_obj, F.w]

/-- For a based functor `F : ğ’³ âŸ¶ ğ’´`, if an arrow `Ï†` in `ğ’³` lifts some `f` in `ğ’®`, then `F(Ï†)`
also lifts `f` -/
instance pres_IsHomLift {R S : ğ’®} {a b : ğ’³.cat} (f : R âŸ¶ S) (Ï† : a âŸ¶ b) [IsHomLift ğ’³.p f Ï†] :
    IsHomLift ğ’´.p f (F.map Ï†) := by
  apply of_fac ğ’´.p f (F.map Ï†) (Eq.trans (F.w_obj a) (domain_eq ğ’³.p f Ï†))
    (Eq.trans (F.w_obj b) (codomain_eq ğ’³.p f Ï†))
  rw [â†Functor.comp_map, congr_hom F.w]
  simpa using (fac ğ’³.p f Ï†)

/-- For a based functor `F`, `F(Ï†)` always lifts `ğ’³.p(Ï†)` -/
-- instance map_isHomLift {a b : ğ’³.cat} (Ï† : a âŸ¶ b) : IsHomLift ğ’´.p (ğ’³.p.map Ï†) (F.map Ï†) :=
--   inferInstance

instance map_isHomLift (a : ğ’³.cat) : IsHomLift ğ’´.p (ğŸ™ (ğ’³.p.obj a)) (ğŸ™ (F.obj a)) := by
  simp_rw [â† Functor.map_id]; infer_instance

/-- For a based functor `F : ğ’³ âŸ¶ ğ’´`, and an arrow `Ï†` in `ğ’³`, then `Ï†` lifts an arrow `f` in `ğ’®`
if `F(Ï†)` does -/
lemma IsHomLift_ofImage {S R : ğ’®} {a b : ğ’³.cat} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)
    [IsHomLift ğ’´.p f (F.map Ï†)] : IsHomLift ğ’³.p f Ï† := by
  apply of_fac ğ’³.p f Ï†  (F.w_obj a â–¸ domain_eq ğ’´.p f (F.map Ï†))
    (F.w_obj b â–¸ codomain_eq ğ’´.p f (F.map Ï†))
  simp [congr_hom F.w.symm, fac ğ’´.p f (F.map Ï†)]

lemma IsHomLift_iff {S R : ğ’®} {a b : ğ’³.cat} {Ï† : a âŸ¶ b} {f : R âŸ¶ S} :
    IsHomLift ğ’´.p f (F.map Ï†) â†” IsHomLift ğ’³.p f Ï† :=
  âŸ¨fun _ => IsHomLift_ofImage F f Ï†, fun _ => pres_IsHomLift F f Ï†âŸ©

end

end BasedFunctor

-- TODO: another costum .mk function here!

/-- A `BasedNatTrans` between two `BasedFunctor`s is a natural transformation `Î±` between the underlying functors,
    such that for all `a : ğ’³`, `Î±.app a` lifts `ğŸ™ S` whenever `ğ’³.p.obj a = S`. -/
structure BasedNatTrans {ğ’³ ğ’´ : BasedCategory ğ’®} (F G : BasedFunctor ğ’³ ğ’´) extends
    CategoryTheory.NatTrans F.toFunctor G.toFunctor where
  (aboveId : âˆ€ {a : ğ’³.cat} {S : ğ’®} (_ : ğ’³.p.obj a = S), IsHomLift ğ’´.p (ğŸ™ S) (toNatTrans.app a) := by aesop_cat)

namespace BasedNatTrans

variable {ğ’³ ğ’´ : BasedCategory ğ’®}

instance app_isHomLift {F G : BasedFunctor ğ’³ ğ’´} (Î± : BasedNatTrans F G) (a : ğ’³.cat) :
    IsHomLift ğ’´.p (ğŸ™ (ğ’³.p.obj a)) (Î±.toNatTrans.app a) :=
  Î±.aboveId rfl

@[ext]
lemma ext {F G : BasedFunctor ğ’³ ğ’´} (Î± Î² : BasedNatTrans F G)
    (h : Î±.toNatTrans = Î².toNatTrans) : Î± = Î² := by
  cases Î±; subst h; rfl

/-- The identity natural transformation is a `BasedNatTrans` -/
@[simps!]
def id (F : BasedFunctor ğ’³ ğ’´) : BasedNatTrans F F where
  toNatTrans := CategoryTheory.NatTrans.id F.toFunctor
  aboveId := by
    intro a S ha
    apply of_fac ğ’´.p (ğŸ™ S) _
    rotate_left
    any_goals rwa [â†CategoryTheory.Functor.comp_obj, F.w]
    simp

@[simp]
lemma id_toNatTrans (F : BasedFunctor ğ’³ ğ’´) :
    (id F).toNatTrans = CategoryTheory.NatTrans.id F.toFunctor :=
  rfl

/-- Composition of `BasedNatTrans`s, given by composition of the underlying natural transformations -/
@[simps!]
def comp {F G H : BasedFunctor ğ’³ ğ’´} (Î± : BasedNatTrans F G)
    (Î² : BasedNatTrans G H) : BasedNatTrans F H where
  toNatTrans := CategoryTheory.NatTrans.vcomp Î±.toNatTrans Î².toNatTrans
  aboveId := by
    intro a S ha
    rw [CategoryTheory.NatTrans.vcomp_app]
    subst ha
    infer_instance

-- TODO: do I need these three lemmas...?

@[simp]
lemma CategoryTheory.NatTrans.id_vcomp {C D : Type _} [Category C] [Category D] {F G : C â¥¤ D}
    (f : NatTrans F G) : NatTrans.vcomp (NatTrans.id F) f = f := by
  aesop_cat

@[simp]
lemma CategoryTheory.NatTrans.vcomp_id {C D : Type _} [Category C] [Category D] {F G : C â¥¤ D}
    (f : NatTrans F G) : NatTrans.vcomp f (NatTrans.id G) = f := by
  aesop_cat

@[simp]
lemma comp_toNatTrans {F G H : BasedFunctor ğ’³ ğ’´} (Î± : BasedNatTrans F G) (Î² : BasedNatTrans G H) :
    (comp Î± Î²).toNatTrans = NatTrans.vcomp Î±.toNatTrans Î².toNatTrans :=
  rfl

@[simp]
lemma id_comp {F G : BasedFunctor ğ’³ ğ’´} (Î± : BasedNatTrans F G) : comp (id F) Î± = Î± := by
  ext1; rw [comp_toNatTrans, id_toNatTrans, CategoryTheory.NatTrans.id_vcomp]

@[simp]
lemma comp_id {F G : BasedFunctor ğ’³ ğ’´} (Î± : BasedNatTrans F G) : comp Î± (id G) = Î± := by
  ext1; rw [comp_toNatTrans, id_toNatTrans, CategoryTheory.NatTrans.vcomp_id]

lemma comp_assoc {F G H I : BasedFunctor ğ’³ ğ’´} (Î± : BasedNatTrans F G) (Î² : BasedNatTrans G H)
    (Î³ : BasedNatTrans H I) : comp (comp Î± Î²) Î³ = comp Î± (comp Î² Î³):= by
  ext1
  rw [comp_toNatTrans, comp_toNatTrans, comp_toNatTrans, comp_toNatTrans, NatTrans.vcomp_eq_comp, NatTrans.vcomp_eq_comp, NatTrans.vcomp_eq_comp, NatTrans.vcomp_eq_comp, assoc]

end BasedNatTrans

@[simps]
instance homCategory (ğ’³ ğ’´ : BasedCategory ğ’®) : Category (BasedFunctor ğ’³ ğ’´) where
  Hom := BasedNatTrans
  id := BasedNatTrans.id
  comp := BasedNatTrans.comp
  -- TODO: can maybe remove these three? (will they still be simp lemmas then?)
  id_comp := BasedNatTrans.id_comp
  comp_id := BasedNatTrans.comp_id
  assoc := BasedNatTrans.comp_assoc

@[ext]
lemma homCategory.ext {ğ’³ ğ’´ : BasedCategory ğ’®} {F G : BasedFunctor ğ’³ ğ’´} (Î± Î² : F âŸ¶ G)
    (h : Î±.toNatTrans = Î².toNatTrans) : Î± = Î² :=
  BasedNatTrans.ext Î± Î² h

-- TODO: I should be able to recycle the identical code in the next few definitions

/-- The associator in the bicategory `BasedCategory` is given by the identity -/
@[simps]
def BasedFunctor.associator {ğ’³ ğ’´ ğ’µ ğ’± : BasedCategory ğ’®} (F : BasedFunctor ğ’³ ğ’´) (G : BasedFunctor ğ’´ ğ’µ)
    (H : BasedFunctor ğ’µ ğ’±) : BasedFunctor.comp (BasedFunctor.comp F G) H â‰…
      BasedFunctor.comp F (BasedFunctor.comp G H) where
    hom := {
      app := fun _ => ğŸ™ _
      -- can this be automated?
      aboveId := by
        intro a S ha
        subst ha
        infer_instance
    }
    inv := {
      app := fun _ => ğŸ™ _
      aboveId := by
        intro a S ha
        subst ha
        infer_instance
    }

/-- The left unitor in the bicategory `BasedCategory` is given by the identity -/
@[simps]
def BasedFunctor.leftUnitor {ğ’³ ğ’´ : BasedCategory ğ’®} (F : BasedFunctor ğ’³ ğ’´) :
  BasedFunctor.comp (BasedFunctor.id ğ’³) F â‰… F where
    hom := {
      app := fun a => ğŸ™ (F.obj a)
      aboveId := by
        intro a S ha
        subst ha
        infer_instance
    }
    inv := {
      app := fun a => ğŸ™ (F.obj a)
      aboveId := by
        intro a S ha
        subst ha
        infer_instance
    }

/-- The right unitor in the bicategory `BasedCategory` is given by the identity -/
@[simps]
def BasedFunctor.rightUnitor {ğ’³ ğ’´ : BasedCategory ğ’®} (F : BasedFunctor ğ’³ ğ’´) :
  BasedFunctor.comp F (BasedFunctor.id ğ’´) â‰… F where
    hom := {
      app := fun a => ğŸ™ (F.obj a)
      aboveId := by
        intro a S ha
        subst ha
        infer_instance
    }
    inv := {
      app := fun a => ğŸ™ (F.obj a)
      aboveId := by
        intro a S ha
        subst ha
        infer_instance
    }

/-- Left-whiskering in the bicategory `BasedCategory` is given by whiskering the underlying functors
    and natural transformations -/
@[simps!]
def BasedCategory.whiskerLeft {ğ’³ ğ’´ ğ’µ : BasedCategory ğ’®} (F : BasedFunctor ğ’³ ğ’´)
    {G H : BasedFunctor ğ’´ ğ’µ} (Î± : G âŸ¶ H) : BasedFunctor.comp F G âŸ¶ BasedFunctor.comp F H where
  toNatTrans := CategoryTheory.whiskerLeft F.toFunctor Î±.toNatTrans
  aboveId := by
    intro a S ha
    apply Î±.aboveId
    simp only [BasedFunctor.w_obj, ha]

/-- Right-whiskering in the bicategory `BasedCategory` is given by whiskering the underlying functors
    and natural transformations -/
@[simps!]
def BasedCategory.whiskerRight {ğ’³ ğ’´ ğ’µ : BasedCategory ğ’®} {F G : BasedFunctor ğ’³ ğ’´} (Î± : F âŸ¶ G)
    (H : BasedFunctor ğ’´ ğ’µ) : BasedFunctor.comp F H âŸ¶ BasedFunctor.comp G H where
  toNatTrans := CategoryTheory.whiskerRight Î±.toNatTrans H.toFunctor
  aboveId := by
    intro a S ha
    subst ha
    apply BasedFunctor.pres_IsHomLift

-- TODO: aesop no longer solves these.....

/-- `BasedCategory ğ’®` forms a bicategory -/
instance BasedCategory.bicategory : Bicategory (BasedCategory ğ’®) where
  Hom := BasedFunctor
  id := BasedFunctor.id
  comp := BasedFunctor.comp
  homCategory ğ’³ ğ’´ := homCategory ğ’³ ğ’´
  whiskerLeft := BasedCategory.whiskerLeft
  whiskerRight {ğ’³ ğ’´ ğ’µ} F G Î± H := BasedCategory.whiskerRight Î± H
  associator := BasedFunctor.associator
  leftUnitor {ğ’³ ğ’´} F := BasedFunctor.leftUnitor F
  rightUnitor {ğ’³ ğ’´} F := BasedFunctor.rightUnitor F

/-- The bicategory structure on `BasedCategory ğ’®` is strict -/
instance : Bicategory.Strict (BasedCategory ğ’®) where
  id_comp := BasedFunctor.id_comp
  comp_id := BasedFunctor.comp_id
  assoc := BasedFunctor.assoc

end Fibered
