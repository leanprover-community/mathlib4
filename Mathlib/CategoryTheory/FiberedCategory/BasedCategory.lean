/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Lezeau, Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.FiberedCategory.HomLift
import Mathlib.CategoryTheory.Bicategory.Strict
import Mathlib.CategoryTheory.Functor.Category

/-!
# The bicategory of based categories

In this file we define the type `BasedCategory ğ’®`, and give it the structure of a strict
bicategory. Given a category `ğ’®`, we define the type `BasedCategory ğ’®` as the type of categories
`ğ’³` equiped with a functor `ğ’³.p : ğ’³ â¥¤ ğ’®`.

We also define functors between based categories `ğ’³ ğ’´ : BasedCategory ğ’®`, via the structure
`BasedFunctor ğ’³ ğ’´`. These are defined as functors between the underlying categories `ğ’³.obj` and
`ğ’´.obj` which commute with the projections to `ğ’®`.

Natural transformations between based functors `F G : BasedFunctor ğ’³ ğ’´` are given by the structure
`BasedNatTrans F G`. These are defined as natural transformations `Î±` between the functors
underlying `F` and `G` such that `Î±.app a` lifts `ğŸ™ S` whenever `ğ’³.p.obj a = S`.
-/

universe vâ‚… uâ‚… vâ‚„ uâ‚„ vâ‚ƒ uâ‚ƒ vâ‚‚ uâ‚‚ vâ‚ uâ‚

open CategoryTheory Functor Category NatTrans IsHomLift

variable {ğ’® : Type uâ‚} [Category.{vâ‚} ğ’®]

/-- A based category over `ğ’®` is a category `ğ’³` together with a functor `p : ğ’³ â¥¤ ğ’®`. -/
@[nolint checkUnivs]
structure BasedCategory (ğ’® : Type uâ‚) [Category.{vâ‚} ğ’®] where
  /-- The type of objects in a `BasedCategory`-/
  obj : Type uâ‚‚
  /-- The underlying category of a `BasedCategory`. -/
  category : Category.{vâ‚‚} obj := by infer_instance
  /-- The functor to the base. -/
  p : obj â¥¤ ğ’®

instance (ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) : Category ğ’³.obj := ğ’³.category

/-- The based category associated to a functor `p : ğ’³ â¥¤ ğ’®`. -/
def BasedCategory.ofFunctor {ğ’³ : Type uâ‚‚} [Category.{vâ‚‚} ğ’³] (p : ğ’³ â¥¤ ğ’®) : BasedCategory ğ’® where
  obj := ğ’³
  p := p

/-- A functor between based categories is a functor between the underlying categories that commutes
with the projections. -/
structure BasedFunctor (ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) (ğ’´ : BasedCategory.{vâ‚ƒ, uâ‚ƒ} ğ’®) extends
    CategoryTheory.Functor ğ’³.obj ğ’´.obj where
  w : toFunctor â‹™ ğ’´.p = ğ’³.p := by aesop_cat

namespace BasedFunctor

/-- The identity based functor. -/
@[simps!]
def id (ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) : BasedFunctor ğ’³ ğ’³ where
  toFunctor := ğŸ­ ğ’³.obj

variable {ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®} {ğ’´ : BasedCategory.{vâ‚ƒ, uâ‚ƒ} ğ’®}

/-- The composition of two based functors. -/
@[simps!]
def comp {ğ’µ : BasedCategory.{vâ‚„, uâ‚„} ğ’®} (F : BasedFunctor ğ’³ ğ’´) (G : BasedFunctor ğ’´ ğ’µ) :
    BasedFunctor ğ’³ ğ’µ where
  toFunctor := F.toFunctor â‹™ G.toFunctor
  w := by rw [Functor.assoc, G.w, F.w]

@[simp]
lemma comp_id (F : BasedFunctor ğ’³ ğ’´) : comp F (id ğ’´) = F :=
  rfl

@[simp]
lemma id_comp (F : BasedFunctor ğ’³ ğ’´) : comp (id ğ’³) F = F :=
  rfl

@[simp]
lemma comp_assoc {ğ’µ : BasedCategory.{vâ‚„, uâ‚„} ğ’®} {ğ’œ : BasedCategory.{vâ‚…, uâ‚…} ğ’®}
    (F : BasedFunctor ğ’³ ğ’´) (G : BasedFunctor ğ’´ ğ’µ) (H : BasedFunctor ğ’µ ğ’œ) :
      comp (comp F G) H = comp F (comp G H) :=
  rfl


@[simp]
lemma w_obj (F : BasedFunctor ğ’³ ğ’´) (a : ğ’³.obj) : ğ’´.p.obj (F.obj a) = ğ’³.p.obj a := by
  rw [â† Functor.comp_obj, F.w]

instance (F : BasedFunctor ğ’³ ğ’´) (a : ğ’³.obj) : IsHomLift ğ’´.p (ğŸ™ (ğ’³.p.obj a)) (ğŸ™ (F.obj a)) :=
  IsHomLift.id (w_obj F a)

section

variable (F : BasedFunctor ğ’³ ğ’´) {R S : ğ’®} {a b : ğ’³.obj} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)

/-- For a based functor `F : ğ’³ âŸ¶ ğ’´`, then whenever an arrow `Ï†` in `ğ’³` lifts some `f` in `ğ’®`,
then `F(Ï†)` also lifts `f`. -/
instance pres_IsHomLift [IsHomLift ğ’³.p f Ï†] : IsHomLift ğ’´.p f (F.map Ï†) := by
  apply of_fac ğ’´.p f (F.map Ï†) (Eq.trans (F.w_obj a) (domain_eq ğ’³.p f Ï†))
    (Eq.trans (F.w_obj b) (codomain_eq ğ’³.p f Ï†))
  rw [â† Functor.comp_map, congr_hom F.w]
  simpa using (fac ğ’³.p f Ï†)

/-- For a based functor `F : ğ’³ âŸ¶ ğ’´`, and an arrow `Ï†` in `ğ’³`, then `Ï†` lifts an arrow `f` in `ğ’®`
if `F(Ï†)` does. -/
lemma IsHomLift_ofImage [IsHomLift ğ’´.p f (F.map Ï†)] : IsHomLift ğ’³.p f Ï† := by
  apply of_fac ğ’³.p f Ï†  (F.w_obj a â–¸ domain_eq ğ’´.p f (F.map Ï†))
    (F.w_obj b â–¸ codomain_eq ğ’´.p f (F.map Ï†))
  simp [congr_hom F.w.symm, fac ğ’´.p f (F.map Ï†)]

lemma IsHomLift_iff : IsHomLift ğ’´.p f (F.map Ï†) â†” IsHomLift ğ’³.p f Ï† :=
  âŸ¨fun _ => IsHomLift_ofImage F f Ï†, fun _ => pres_IsHomLift F f Ï†âŸ©

end

end BasedFunctor


/-- A `BasedNatTrans` between two `BasedFunctor`s is a natural transformation `Î±` between the
underlying functors, such that for all `a : ğ’³`, `Î±.app a` lifts `ğŸ™ S` whenever `ğ’³.p.obj a = S`. -/
structure BasedNatTrans {ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®} {ğ’´ : BasedCategory.{vâ‚ƒ, uâ‚ƒ} ğ’®}
    (F G :  BasedFunctor ğ’³ ğ’´) extends CategoryTheory.NatTrans F.toFunctor G.toFunctor where
  aboveId' : âˆ€ (a : ğ’³.obj), IsHomLift ğ’´.p (ğŸ™ (ğ’³.p.obj a)) (toNatTrans.app a) := by aesop_cat

namespace BasedNatTrans

open BasedFunctor

variable {ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®} {ğ’´ : BasedCategory.{vâ‚ƒ, uâ‚ƒ} ğ’®} (F : BasedFunctor ğ’³ ğ’´)

/-- The identity natural transformation is a `BasedNatTrans`. -/
@[simps!]
def id : BasedNatTrans F F where
  toNatTrans := CategoryTheory.NatTrans.id F.toFunctor
  aboveId' := by
    intro a
    apply of_fac ğ’´.p _ _ (w_obj F a) (w_obj F a)
    simp

@[simp]
lemma id_toNatTrans : (id F).toNatTrans = CategoryTheory.NatTrans.id F.toFunctor :=
  rfl

section

variable {F} {G :  BasedFunctor ğ’³ ğ’´} (Î± : BasedNatTrans F G)

instance app_isHomLift (a : ğ’³.obj) : IsHomLift ğ’´.p (ğŸ™ (ğ’³.p.obj a)) (Î±.toNatTrans.app a) :=
  Î±.aboveId' a

lemma aboveId {a : ğ’³.obj} {S : ğ’®} (ha : ğ’³.p.obj a = S) :
    IsHomLift ğ’´.p (ğŸ™ S) (Î±.toNatTrans.app a) := by
  subst ha; infer_instance

@[ext]
lemma ext (Î² : BasedNatTrans F G) (h : Î±.toNatTrans = Î².toNatTrans) : Î± = Î² := by
  cases Î±; subst h; rfl

end

/-- Composition of `BasedNatTrans`, given by composition of the underlying natural
transformations. -/
@[simps!]
def comp {F G H :  BasedFunctor ğ’³ ğ’´} (Î± : BasedNatTrans F G) (Î² : BasedNatTrans G H) :
    BasedNatTrans F H where
  toNatTrans := CategoryTheory.NatTrans.vcomp Î±.toNatTrans Î².toNatTrans
  aboveId' := by
    intro a
    rw [CategoryTheory.NatTrans.vcomp_app]
    infer_instance

@[simp]
lemma comp_toNatTrans {F G H :  BasedFunctor ğ’³ ğ’´} (Î± : BasedNatTrans F G) (Î² : BasedNatTrans G H) :
    (comp Î± Î²).toNatTrans = NatTrans.vcomp Î±.toNatTrans Î².toNatTrans :=
  rfl

end BasedNatTrans

namespace BasedCategory

open BasedFunctor BasedNatTrans

@[simps!]
instance homCategory (ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) (ğ’´ : BasedCategory.{vâ‚ƒ, uâ‚ƒ} ğ’®) :
    Category (BasedFunctor ğ’³ ğ’´) where
  Hom := BasedNatTrans
  id := BasedNatTrans.id
  comp := BasedNatTrans.comp

section

variable {ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®} {ğ’´ : BasedCategory.{vâ‚ƒ, uâ‚ƒ} ğ’®}

@[ext]
lemma homCategory.ext {F G :  BasedFunctor ğ’³ ğ’´} (Î± Î² : F âŸ¶ G) (h : Î±.toNatTrans = Î².toNatTrans) :
    Î± = Î² :=
  BasedNatTrans.ext Î± Î² h

/-- The inverse of a based natural transformation whose underlying natural tranformation is an
isomorphism. -/
def BasedNatIso {F G : BasedFunctor ğ’³ ğ’´} (Î± : F.toFunctor â‰… G.toFunctor)
    (aboveId' : âˆ€ a : ğ’³.obj, IsHomLift ğ’´.p (ğŸ™ (ğ’³.p.obj a)) (Î±.hom.app a)) : F â‰… G where
  hom := { toNatTrans := Î±.hom }
  inv := {
    toNatTrans := Î±.inv
    aboveId' := by
      intro a
      specialize aboveId' a
      rw [â† NatIso.app_inv]
      rw [â† NatIso.app_hom] at aboveId'
      apply IsHomLift.lift_id_inv }

/-- The inverse of a based natural transformation whose underlying natural tranformation carries an
`IsIso` instance. -/
noncomputable def BasedNatIso_of_isIso {F G : BasedFunctor ğ’³ ğ’´} (Î± : F.toFunctor âŸ¶ G.toFunctor)
    [IsIso Î±] (aboveId' : âˆ€ a : ğ’³.obj, IsHomLift ğ’´.p (ğŸ™ (ğ’³.p.obj a)) (Î±.app a)) : F â‰… G where
  hom := { toNatTrans := Î± }
  inv := { toNatTrans := inv Î±, aboveId' := fun a => by simp [lift_id_inv_isIso] }

/-- The identity natural transformation is a based natural isomorphism. -/
@[simps]
def BasedNatIso.id (F :  BasedFunctor ğ’³ ğ’´) : F â‰… F where
  hom := ğŸ™ F
  inv := ğŸ™ F

/-- Left-whiskering in the bicategory `BasedCategory` is given by whiskering the underlying functors
and natural transformations. -/
@[simps!]
def whiskerLeft {ğ’µ : BasedCategory.{vâ‚„, uâ‚„} ğ’®} (F :  BasedFunctor ğ’³ ğ’´) {G H : BasedFunctor ğ’´ ğ’µ}
    (Î± : G âŸ¶ H) : BasedFunctor.comp F G âŸ¶ BasedFunctor.comp F H where
  toNatTrans := CategoryTheory.whiskerLeft F.toFunctor Î±.toNatTrans
  aboveId' := fun a => Î±.aboveId (F.w_obj a)

/-- Right-whiskering in the bicategory `BasedCategory` is given by whiskering the underlying
functors and natural transformations. -/
@[simps!]
def whiskerRight {ğ’µ : BasedCategory.{vâ‚„, uâ‚„} ğ’®} {F G :  BasedFunctor ğ’³ ğ’´} (Î± : F âŸ¶ G)
    (H : BasedFunctor ğ’´ ğ’µ) : BasedFunctor.comp F H âŸ¶ BasedFunctor.comp G H where
  toNatTrans := CategoryTheory.whiskerRight Î±.toNatTrans H.toFunctor
  aboveId' := fun a => by apply BasedFunctor.pres_IsHomLift

end

/-- The category of based categories. -/
@[simps!]
instance : Category (BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) where
  Hom := BasedFunctor
  id := id
  comp := comp

/-- The bicategory of based categories. -/
instance bicategory : Bicategory (BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) where
  Hom ğ’³ ğ’´ :=  BasedFunctor ğ’³ ğ’´
  id ğ’³ := id ğ’³
  comp F G := BasedFunctor.comp F G
  homCategory ğ’³ ğ’´ := homCategory ğ’³ ğ’´
  whiskerLeft {ğ’³ ğ’´ ğ’µ} F {G H} Î± := whiskerLeft F Î±
  whiskerRight {ğ’³ ğ’´ ğ’µ} F G Î± H := whiskerRight Î± H
  associator F G H := BasedNatIso.id _
  leftUnitor {ğ’³ ğ’´} F := BasedNatIso.id F
  rightUnitor {ğ’³ ğ’´} F := BasedNatIso.id F

/-- The bicategory structure on `BasedCategory.{vâ‚‚, uâ‚‚} ğ’®` is strict. -/
instance : Bicategory.Strict (BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) where

end BasedCategory
