/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Lezeau, Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.FiberedCategory.HomLift
import Mathlib.CategoryTheory.Bicategory.Strict
import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.Functor.ReflectsIso.Basic

/-!
# The bicategory of based categories

In this file we define the type `BasedCategory ğ’®`, and give it the structure of a strict
bicategory. Given a category `ğ’®`, we define the type `BasedCategory ğ’®` as the type of categories
`ğ’³` equipped with a functor `ğ’³.p : ğ’³ â¥¤ ğ’®`.

We also define a type of functors between based categories `ğ’³` and `ğ’´`, which we call
`BasedFunctor ğ’³ ğ’´` and denote as `ğ’³ â¥¤áµ‡ ğ’´`. These are defined as functors between the underlying
categories `ğ’³.obj` and `ğ’´.obj` which commute with the projections to `ğ’®`.

Natural transformations between based functors `F G : ğ’³ â¥¤áµ‡ ğ’´ ` are given by the structure
`BasedNatTrans F G`. These are defined as natural transformations `Î±` between the functors
underlying `F` and `G` such that `Î±.app a` lifts `ğŸ™ S` whenever `ğ’³.p.obj a = S`.
-/

universe vâ‚… uâ‚… vâ‚„ uâ‚„ vâ‚ƒ uâ‚ƒ vâ‚‚ uâ‚‚ vâ‚ uâ‚

namespace CategoryTheory

open Functor Category NatTrans IsHomLift

variable {ğ’® : Type uâ‚} [Category.{vâ‚} ğ’®]

/-- A based category over `ğ’®` is a category `ğ’³` together with a functor `p : ğ’³ â¥¤ ğ’®`. -/
@[nolint checkUnivs]
structure BasedCategory (ğ’® : Type uâ‚) [Category.{vâ‚} ğ’®] where
  /-- The type of objects in a `BasedCategory` -/
  obj : Type uâ‚‚
  /-- The underlying category of a `BasedCategory`. -/
  category : Category.{vâ‚‚} obj := by infer_instance
  /-- The functor to the base. -/
  p : obj â¥¤ ğ’®

instance (ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) : Category ğ’³.obj := ğ’³.category

/-- The based category associated to a functor `p : ğ’³ â¥¤ ğ’®`. -/
def BasedCategory.ofFunctor {ğ’³ : Type uâ‚‚} [Category.{vâ‚‚} ğ’³] (p : ğ’³ â¥¤ ğ’®) : BasedCategory ğ’® where
  obj := ğ’³
  p := p

/-- A functor between based categories is a functor between the underlying categories that commutes
with the projections. -/
structure BasedFunctor (ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) (ğ’´ : BasedCategory.{vâ‚ƒ, uâ‚ƒ} ğ’®) extends
    ğ’³.obj â¥¤ ğ’´.obj where
  w : toFunctor â‹™ ğ’´.p = ğ’³.p := by cat_disch

/-- Notation for `BasedFunctor`. -/
scoped infixr:26 " â¥¤áµ‡ " => BasedFunctor

namespace BasedFunctor

initialize_simps_projections BasedFunctor (+toFunctor, -obj, -map)

/-- The identity based functor. -/
@[simps]
def id (ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) : ğ’³ â¥¤áµ‡ ğ’³ where
  toFunctor := ğŸ­ ğ’³.obj

variable {ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®} {ğ’´ : BasedCategory.{vâ‚ƒ, uâ‚ƒ} ğ’®}

/-- Notation for the identity functor on a based category. -/
scoped notation "ğŸ­" => BasedFunctor.id

/-- The composition of two based functors. -/
@[simps]
def comp {ğ’µ : BasedCategory.{vâ‚„, uâ‚„} ğ’®} (F : ğ’³ â¥¤áµ‡ ğ’´) (G : ğ’´ â¥¤áµ‡ ğ’µ) : ğ’³ â¥¤áµ‡ ğ’µ where
  toFunctor := F.toFunctor â‹™ G.toFunctor
  w := by rw [Functor.assoc, G.w, F.w]

/-- Notation for composition of based functors. -/
scoped infixr:80 " â‹™ " => BasedFunctor.comp

@[simp]
lemma comp_id (F : ğ’³ â¥¤áµ‡ ğ’´) :  F â‹™ ğŸ­ ğ’´ = F :=
  rfl

@[simp]
lemma id_comp (F : ğ’³ â¥¤áµ‡ ğ’´) : ğŸ­ ğ’³ â‹™ F = F :=
  rfl

@[simp]
lemma comp_assoc {ğ’µ : BasedCategory.{vâ‚„, uâ‚„} ğ’®} {ğ’œ : BasedCategory.{vâ‚…, uâ‚…} ğ’®} (F : ğ’³ â¥¤áµ‡ ğ’´)
    (G : ğ’´ â¥¤áµ‡ ğ’µ) (H : ğ’µ â¥¤áµ‡ ğ’œ) : (F â‹™ G) â‹™ H = F â‹™ (G â‹™ H) :=
  rfl

@[simp]
lemma w_obj (F : ğ’³ â¥¤áµ‡ ğ’´) (a : ğ’³.obj) : ğ’´.p.obj (F.obj a) = ğ’³.p.obj a := by
  rw [â† Functor.comp_obj, F.w]

instance (F : ğ’³ â¥¤áµ‡ ğ’´) (a : ğ’³.obj) : IsHomLift ğ’´.p (ğŸ™ (ğ’³.p.obj a)) (ğŸ™ (F.obj a)) :=
  IsHomLift.id (w_obj F a)

section

variable (F : ğ’³ â¥¤áµ‡ ğ’´) {R S : ğ’®} {a b : ğ’³.obj} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)

/-- For a based functor `F : ğ’³ âŸ¶ ğ’´`, then whenever an arrow `Ï†` in `ğ’³` lifts some `f` in `ğ’®`,
then `F(Ï†)` also lifts `f`. -/
instance preserves_isHomLift [IsHomLift ğ’³.p f Ï†] : IsHomLift ğ’´.p f (F.map Ï†) := by
  apply of_fac ğ’´.p f (F.map Ï†) (Eq.trans (F.w_obj a) (domain_eq ğ’³.p f Ï†))
    (Eq.trans (F.w_obj b) (codomain_eq ğ’³.p f Ï†))
  rw [â† Functor.comp_map, congr_hom F.w]
  simpa using (fac ğ’³.p f Ï†)

/-- For a based functor `F : ğ’³ âŸ¶ ğ’´`, and an arrow `Ï†` in `ğ’³`, then `Ï†` lifts an arrow `f` in `ğ’®`
if `F(Ï†)` does. -/
lemma isHomLift_map [IsHomLift ğ’´.p f (F.map Ï†)] : IsHomLift ğ’³.p f Ï† := by
  apply of_fac ğ’³.p f Ï†  (F.w_obj a â–¸ domain_eq ğ’´.p f (F.map Ï†))
    (F.w_obj b â–¸ codomain_eq ğ’´.p f (F.map Ï†))
  simp [congr_hom F.w.symm, fac ğ’´.p f (F.map Ï†)]

lemma isHomLift_iff : IsHomLift ğ’´.p f (F.map Ï†) â†” IsHomLift ğ’³.p f Ï† :=
  âŸ¨fun _ â†¦ isHomLift_map F f Ï†, fun _ â†¦ preserves_isHomLift F f Ï†âŸ©

end

end BasedFunctor


/-- A `BasedNatTrans` between two `BasedFunctor`s is a natural transformation `Î±` between the
underlying functors, such that for all `a : ğ’³`, `Î±.app a` lifts `ğŸ™ S` whenever `ğ’³.p.obj a = S`. -/
structure BasedNatTrans {ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®} {ğ’´ : BasedCategory.{vâ‚ƒ, uâ‚ƒ} ğ’®}
    (F G : ğ’³ â¥¤áµ‡ ğ’´) extends CategoryTheory.NatTrans F.toFunctor G.toFunctor where
  isHomLift' : âˆ€ (a : ğ’³.obj), IsHomLift ğ’´.p (ğŸ™ (ğ’³.p.obj a)) (toNatTrans.app a) := by cat_disch

namespace BasedNatTrans

open BasedFunctor

variable {ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®} {ğ’´ : BasedCategory.{vâ‚ƒ, uâ‚ƒ} ğ’®}

initialize_simps_projections BasedNatTrans (+toNatTrans, -app)

section

variable {F G : ğ’³ â¥¤áµ‡ ğ’´} (Î± : BasedNatTrans F G)

@[ext]
lemma ext (Î² : BasedNatTrans F G) (h : Î±.toNatTrans = Î².toNatTrans) : Î± = Î² := by
  cases Î±; subst h; rfl

instance app_isHomLift (a : ğ’³.obj) : IsHomLift ğ’´.p (ğŸ™ (ğ’³.p.obj a)) (Î±.toNatTrans.app a) :=
  Î±.isHomLift' a

lemma isHomLift {a : ğ’³.obj} {S : ğ’®} (ha : ğ’³.p.obj a = S) :
    IsHomLift ğ’´.p (ğŸ™ S) (Î±.toNatTrans.app a) := by
  subst ha; infer_instance

end

/-- The identity natural transformation is a `BasedNatTrans`. -/
@[simps]
def id (F : ğ’³ â¥¤áµ‡ ğ’´) : BasedNatTrans F F where
  toNatTrans := CategoryTheory.NatTrans.id F.toFunctor
  isHomLift' := fun a â†¦ of_fac ğ’´.p _ _ (w_obj F a) (w_obj F a) (by simp)

/-- Composition of `BasedNatTrans`, given by composition of the underlying natural
transformations. -/
@[simps]
def comp {F G H : ğ’³ â¥¤áµ‡ ğ’´} (Î± : BasedNatTrans F G) (Î² : BasedNatTrans G H) : BasedNatTrans F H where
  toNatTrans := CategoryTheory.NatTrans.vcomp Î±.toNatTrans Î².toNatTrans
  isHomLift' := by
    intro a
    rw [CategoryTheory.NatTrans.vcomp_app]
    infer_instance

@[simps]
instance homCategory (ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) (ğ’´ : BasedCategory.{vâ‚ƒ, uâ‚ƒ} ğ’®) :
    Category (ğ’³ â¥¤áµ‡ ğ’´) where
  Hom := BasedNatTrans
  id := BasedNatTrans.id
  comp := BasedNatTrans.comp

@[ext]
lemma homCategory.ext {F G : ğ’³ â¥¤áµ‡ ğ’´} (Î± Î² : F âŸ¶ G) (h : Î±.toNatTrans = Î².toNatTrans) : Î± = Î² :=
  BasedNatTrans.ext Î± Î² h

/-- The forgetful functor from the category of based functors `ğ’³ â¥¤áµ‡ ğ’´` to the category of
functors of underlying categories, `ğ’³.obj â¥¤ ğ’´.obj`. -/
@[simps]
def forgetful (ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) (ğ’´ : BasedCategory.{vâ‚ƒ, uâ‚ƒ} ğ’®) :
    (ğ’³ â¥¤áµ‡ ğ’´) â¥¤ (ğ’³.obj â¥¤ ğ’´.obj) where
  obj := fun F â†¦ F.toFunctor
  map := fun Î± â†¦ Î±.toNatTrans

instance : (forgetful ğ’³ ğ’´).ReflectsIsomorphisms where
  reflects {F G} Î± _ := by
    constructor
    use {
      toNatTrans := inv ((forgetful ğ’³ ğ’´).map Î±)
      isHomLift' := fun a â†¦ by simp [lift_id_inv_isIso] }
    aesop

instance {F G : ğ’³ â¥¤áµ‡ ğ’´} (Î± : F âŸ¶ G) [IsIso Î±] : IsIso (X := F.toFunctor) Î±.toNatTrans := by
  rw [â† forgetful_map]; infer_instance

end BasedNatTrans

namespace BasedNatIso

open BasedNatTrans

variable {ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®} {ğ’´ : BasedCategory.{vâ‚ƒ, uâ‚ƒ} ğ’®}

/-- The identity natural transformation is a based natural isomorphism. -/
@[simps]
def id (F : ğ’³ â¥¤áµ‡ ğ’´) : F â‰… F where
  hom := ğŸ™ F
  inv := ğŸ™ F

variable {F G : ğ’³ â¥¤áµ‡ ğ’´}

/-- The inverse of a based natural transformation whose underlying natural transformation is an
isomorphism. -/
def mkNatIso (Î± : F.toFunctor â‰… G.toFunctor)
    (isHomLift' : âˆ€ a : ğ’³.obj, IsHomLift ğ’´.p (ğŸ™ (ğ’³.p.obj a)) (Î±.hom.app a)) : F â‰… G where
  hom := { toNatTrans := Î±.hom }
  inv := {
    toNatTrans := Î±.inv
    isHomLift' := fun a â†¦ by
      have : ğ’´.p.IsHomLift (ğŸ™ (ğ’³.p.obj a)) (Î±.app a).hom := (Iso.app_hom Î± a) â–¸ isHomLift' a
      rw [â† Iso.app_inv]
      apply IsHomLift.lift_id_inv }

lemma isIso_of_toNatTrans_isIso (Î± : F âŸ¶ G) [IsIso (X := F.toFunctor) Î±.toNatTrans] : IsIso Î± :=
  have : IsIso ((forgetful ğ’³ ğ’´).map Î±) := by simp_all
  Functor.ReflectsIsomorphisms.reflects (forgetful ğ’³ ğ’´) Î±

end BasedNatIso

namespace BasedCategory

open BasedFunctor BasedNatTrans

section

variable {ğ’³ : BasedCategory.{vâ‚‚, uâ‚‚} ğ’®} {ğ’´ : BasedCategory.{vâ‚ƒ, uâ‚ƒ} ğ’®}

/-- Left-whiskering in the bicategory `BasedCategory` is given by whiskering the underlying functors
and natural transformations. -/
@[simps]
def whiskerLeft {ğ’µ : BasedCategory.{vâ‚„, uâ‚„} ğ’®} (F : ğ’³ â¥¤áµ‡ ğ’´) {G H : ğ’´ â¥¤áµ‡ ğ’µ} (Î± : G âŸ¶ H) :
    F â‹™ G âŸ¶ F â‹™ H where
  toNatTrans := Functor.whiskerLeft F.toFunctor Î±.toNatTrans
  isHomLift' := fun a â†¦ Î±.isHomLift (F.w_obj a)

/-- Right-whiskering in the bicategory `BasedCategory` is given by whiskering the underlying
functors and natural transformations. -/
@[simps]
def whiskerRight {ğ’µ : BasedCategory.{vâ‚„, uâ‚„} ğ’®} {F G : ğ’³ â¥¤áµ‡ ğ’´} (Î± : F âŸ¶ G) (H : ğ’´ â¥¤áµ‡ ğ’µ) :
    F â‹™ H âŸ¶ G â‹™ H where
  toNatTrans := Functor.whiskerRight Î±.toNatTrans H.toFunctor
  isHomLift' := fun _ â†¦ BasedFunctor.preserves_isHomLift _ _ _

end

/-- The category of based categories. -/
@[simps]
instance : Category (BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) where
  Hom := BasedFunctor
  id := id
  comp := comp

/-- The bicategory of based categories. -/
instance bicategory : Bicategory (BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) where
  Hom ğ’³ ğ’´ :=  ğ’³ â¥¤áµ‡ ğ’´
  id ğ’³ := ğŸ­ ğ’³
  comp F G := F â‹™ G
  homCategory ğ’³ ğ’´ := homCategory ğ’³ ğ’´
  whiskerLeft {_ _ _} F {_ _} Î± := whiskerLeft F Î±
  whiskerRight {_ _ _} _ _ Î± H := whiskerRight Î± H
  associator _ _ _ := BasedNatIso.id _
  leftUnitor {_ _} F := BasedNatIso.id F
  rightUnitor {_ _} F := BasedNatIso.id F

/-- The bicategory structure on `BasedCategory.{vâ‚‚, uâ‚‚} ğ’®` is strict. -/
instance : Bicategory.Strict (BasedCategory.{vâ‚‚, uâ‚‚} ğ’®) where

end BasedCategory

end CategoryTheory
