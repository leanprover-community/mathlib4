/-
Copyright (c) 2024 Paul Lezeau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Lezeau, Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.FiberedCategory.HomLift

/-!

# Fibered categories

This file defines what it means for a functor `p : ğ’³ â¥¤ ğ’®` to be fibered`.

## Main definitions

- `IsCartesian p f Ï†` expresses that `Ï†` is a cartesian arrow lying over `f` with respect to `p`.
This structure extends `IsHomLift p f Ï†`.
- `IsFibered p` expresses that `p` gives `ğ’³` the structure of a fibered category over `ğ’®`, i.e.
that for every morphism `f : S âŸ¶ R` in `ğ’®` and every object `b` in `ğ’³` with `p(b)=R` there is a
cartesian arrow `Ï† : a âŸ¶ b`  of `f`.

## Implementation
The standard constructors of `IsCartesian` and `IsFibered` have both been renamed to `.mk'`. We have
provided alternate lemmas `IsCartesian.mk` and `IsFibered.mk` for constructing instances of these
structures, and it is recommended to use these instead to minimize the amount of equalities that
needs to be carried around in the construction.

The reason for this is the following:
Just like `IsHomLift p f Ï†`, we have phrased `IsCartesian p f Ï†` in a way to make its usage as
flexible  as possible with respect to non-definitional equalities of domains / codomains.
In particular, given a lift
```
  a --Ï†--> b
  -        -
  |        |
  v        v
  R --f--> S
```
(by which we mean an object of `IsHomLift p f Ï†`). We say that it is cartesian if for all arrows
`g : R' âŸ¶ R`, and all lifts
```
  a' --Ï†'--> b
  -          -
  |          |
  v          v
  R' --f'--> S
```
such that `f' = g â‰« f`, there is a unique induced map `Ï„ : a' âŸ¶ a` lifting `g` and such that
`Ï„ â‰« Ï† = Ï†'`. This definition gives us some flexibility in that it allows us to take `f'` to be
non-definitionally equal to `g â‰« f`, and `p(a')` to be non-definitionally equal to `R'`.
`IsCartesian.mk` only requires us to check this condition for `f' = g â‰« f` and `R=p(a')`.

Similarly, `IsFibered p` is phrased as saying that for every `f : R âŸ¶ S`, and every `a` such that
`p(a)=S`, there is a cartesian arrow `Ï†` lying over `f`. The alternate constructor `IsFibered.mk`
only requires us to construct this arrow for every `a` and every `f : R âŸ¶ p(a)`.
-/

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

open CategoryTheory Functor Category IsHomLift

namespace CategoryTheory

variable {ğ’® : Type uâ‚} {ğ’³ : Type uâ‚‚} [Category.{vâ‚} ğ’®] [Category.{vâ‚‚} ğ’³]

/-- The proposition that a lift
```
  a --Ï†--> b
  -        -
  |        |
  v        v
  R --f--> S
```
is a cartesian arrow, see SGA 1, VI.5.1.
-/
class Functor.IsCartesian (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b) extends
    IsHomLift p f Ï† : Prop where
  universal_property {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsHomLift p f Ï†'] :
      âˆƒ! Ï‡ : a' âŸ¶ a, IsHomLift p (ğŸ™ R) Ï‡ âˆ§ Ï‡ â‰« Ï† = Ï†'

/-- The proposition that a lift
```
  a --Ï†--> b
  -        -
  |        |
  v        v
  R --f--> S
```
is a strongly cartesian arrow, see STACKS PROJECT. -/
class Functor.IsStronglyCartesian (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)
    extends IsHomLift p f Ï† : Prop where mk' ::
  universal_property {R' : ğ’®} {a' : ğ’³} (g : R' âŸ¶ R) (f' : R' âŸ¶ S)
    (_ : f' = g â‰« f) (Ï†' : a' âŸ¶ b) [IsHomLift p f' Ï†'] :
      âˆƒ! Ï‡ : a' âŸ¶ a, IsHomLift p g Ï‡ âˆ§ Ï‡ â‰« Ï† = Ï†'

protected lemma IsStronglyCartesian.mk {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : b âŸ¶ a}
    [IsHomLift p f Ï†] (h : âˆ€ (a' : ğ’³) (g : p.obj a' âŸ¶ R) (Ï†' : a' âŸ¶ a), IsHomLift p (g â‰« f) Ï†' â†’
      âˆƒ! Ï‡ : a' âŸ¶ b, IsHomLift p g Ï‡ âˆ§ Ï‡ â‰« Ï† = Ï†') : IsStronglyCartesian p f Ï† where
  universal_property := by
    intro R' a' g f' hf' Ï†' hÏ†'
    subst_hom_lift p f' Ï†'
    apply h a' g Ï†' (hf' â–¸ inferInstance)

instance cartesian_of_stronglyCartesian (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)
    [p.IsStronglyCartesian f Ï†] : p.IsCartesian f Ï† where
  universal_property := fun Ï†' =>
    IsStronglyCartesian.universal_property (Ï†:=Ï†) (f:=f) (ğŸ™ R) f (by simp) Ï†'


namespace IsCartesian

/-- Given an arrow `Ï†' : a' âŸ¶ b` and a diagram:
```
a'        a --Ï†--> b
|         |        |
v         v        v
R' ====== R --f--> S
```
such that `Ï†` is a cartesian arrow, then `inducedMap f Ï† Ï†'` is the map `a' âŸ¶ a`
obtained from the universal property of `Ï†`. -/
noncomputable def inducedMap (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)
    [IsCartesian p f Ï†] {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsHomLift p f Ï†'] : a' âŸ¶ a :=
  Classical.choose <| IsCartesian.universal_property (p:=p) (f:=f) (Ï†:=Ï†) Ï†'

instance inducedMap_isHomLift (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)
    [IsCartesian p f Ï†] {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsHomLift p f Ï†'] :
      IsHomLift p (ğŸ™ R) (inducedMap p f Ï† Ï†') :=
  (Classical.choose_spec <| IsCartesian.universal_property (p:=p) (f:=f) (Ï†:=Ï†) Ï†').1.1

@[simp]
lemma inducedMap_comp (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)
    [IsCartesian p f Ï†] {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsHomLift p f Ï†'] :
      (inducedMap p f Ï† Ï†') â‰« Ï† = Ï†' :=
  (Classical.choose_spec <| IsCartesian.universal_property (p:=p) (f:=f) (Ï†:=Ï†) Ï†').1.2

/-- Given a diagram:
```
a'        a --Ï†--> b
|         |        |
v         v        v
R' ====== R --f--> S
```
with `Ï†` a cartesian arrow. Then for any arrow `Ï†' : a' âŸ¶ b`, and `Ïˆ : a' âŸ¶ a` such that
`g â‰« Ïˆ = Ï†'`. Then `Ïˆ` is the map induced by the universal property of `Ï†`. -/
lemma inducedMap_unique (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)
    [IsCartesian p f Ï†] {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsHomLift p f Ï†']
    (Ïˆ : a' âŸ¶ a) [IsHomLift p (ğŸ™ R) Ïˆ] (hÏˆ : Ïˆ â‰« Ï† = Ï†') : Ïˆ = inducedMap p f Ï† Ï†' :=
  (Classical.choose_spec <| IsCartesian.universal_property (p:=p) (f:=f) (Ï†:=Ï†) Ï†').2
    Ïˆ âŸ¨inferInstance, hÏˆâŸ©

/-- Given a diagram:
```
a'        a --Ï†--> b
|         |        |
v         v        v
R' ====== R --f--> S
```
with `Ï†` a cartesian arrow. Then for any arrow `Ï†' : a' âŸ¶ b`, any two arrows `Ïˆ Ïˆ' : a' âŸ¶ a` such
that `g â‰« Ïˆ = Ï†' = g â‰« Ïˆ'`. Then `Ïˆ = Ïˆ'`. -/
protected lemma uniqueness (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)
    [IsCartesian p f Ï†] {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsHomLift p f Ï†'] {Ïˆ Ïˆ' : a' âŸ¶ a}
    [IsHomLift p (ğŸ™ R) Ïˆ] [IsHomLift p (ğŸ™ R) Ïˆ'] (hcomp : Ïˆ â‰« Ï† = Ï†') (hcomp' : Ïˆ' â‰« Ï† = Ï†') :
      Ïˆ = Ïˆ' := by
  rw [inducedMap_unique p f Ï† Ï†' Ïˆ hcomp, inducedMap_unique p f Ï† Ï†' Ïˆ' hcomp']

@[simp]
lemma inducedMap_self_eq_id (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)
    [IsCartesian p f Ï†] : inducedMap p f Ï† Ï† = ğŸ™ a := by
  subst_hom_lift p f Ï†; symm
  apply inducedMap_unique
  simp only [id_comp]

end IsCartesian

namespace IsStronglyCartesian

/-- Given a diagram:
```
a'        a --Ï†--> b
|         |        |
v         v        v
R' --g--> R --f--> S
```
such that `Ï†` is a cartesian arrow, and an arrow `Ï†' : a' âŸ¶ b`,
the induced map is the map `a' âŸ¶ a` obtained from the
universal property of `Ï†`. -/
noncomputable def inducedMap (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)
    [IsStronglyCartesian p f Ï†] {R' : ğ’®} {a' : ğ’³} {g : R' âŸ¶ R} {f' : R' âŸ¶ S} (hf' : f' = g â‰« f)
    (Ï†' : a' âŸ¶ b) [IsHomLift p f' Ï†'] : a' âŸ¶ a :=
  Classical.choose <| IsStronglyCartesian.universal_property (p:=p) (f:=f) (Ï†:=Ï†) _ _ hf' Ï†'

instance inducedMap_isHomLift (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)
    [IsStronglyCartesian p f Ï†] {R' : ğ’®} {a' : ğ’³} {g : R' âŸ¶ R} {f' : R' âŸ¶ S} (hf' : f' = g â‰« f)
    (Ï†' : a' âŸ¶ b) [IsHomLift p f' Ï†'] : IsHomLift p g (inducedMap p f Ï† hf' Ï†') :=
  (Classical.choose_spec <|
    IsStronglyCartesian.universal_property (p:=p) (f:=f) (Ï†:=Ï†) _ _ hf' Ï†').1.1

@[simp]
lemma inducedMap_comp (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)
    [IsStronglyCartesian p f Ï†] {R' : ğ’®} {a' : ğ’³} {g : R' âŸ¶ R} {f' : R' âŸ¶ S} (hf' : f' = g â‰« f)
    (Ï†' : a' âŸ¶ b) [IsHomLift p f' Ï†'] : (inducedMap p f Ï† hf' Ï†') â‰« Ï† = Ï†' :=
  (Classical.choose_spec <|
    IsStronglyCartesian.universal_property (p:=p) (f:=f) (Ï†:=Ï†) _ _ hf' Ï†').1.2

/-- Given a diagram:
```
a'        a --Ï†--> b
|         |        |
v         v        v
R' --g--> R --f--> S
```
with `Ï†` a cartesian arrow. Then for any arrow `Ï†' : a' âŸ¶ b`, and `Ïˆ : a' âŸ¶ a` such that
`g â‰« Ïˆ = Ï†'`. Then `Ïˆ` is the map induced by the universal property. -/
lemma inducedMap_unique (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)
    [IsStronglyCartesian p f Ï†] {R' : ğ’®} {a' : ğ’³} {g : R' âŸ¶ R} {f' : R' âŸ¶ S} (hf' : f' = g â‰« f)
    (Ï†' : a' âŸ¶ b) [IsHomLift p f' Ï†'] (Ïˆ : a' âŸ¶ a) [IsHomLift p g Ïˆ] (hÏˆ : Ïˆ â‰« Ï† = Ï†') :
    Ïˆ = inducedMap p f Ï† hf' Ï†' :=
  (Classical.choose_spec <|
    IsStronglyCartesian.universal_property (p:=p) (f:=f) (Ï†:=Ï†) _ _ hf' Ï†').2 Ïˆ âŸ¨inferInstance, hÏˆâŸ©

/-- Given a diagram:
```
a'        a --Ï†--> b
|         |        |
v         v        v
R' --g--> R --f--> S
```
with `Ï†` a cartesian arrow. Then for any arrow `Ï†' : a' âŸ¶ b`, any two arrows `Ïˆ Ïˆ' : a' âŸ¶ a` such
that `g â‰« Ïˆ = Ï†' = g â‰« Ïˆ'`. Then `Ïˆ = Ïˆ'`. -/
protected lemma uniqueness (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)
    [IsStronglyCartesian p f Ï†] {R' : ğ’®} {a' : ğ’³} {g : R' âŸ¶ R} {f' : R' âŸ¶ S} (hf' : f' = g â‰« f)
    (Ï†' : a' âŸ¶ b) [IsHomLift p f' Ï†'] {Ïˆ Ïˆ' : a' âŸ¶ a} [IsHomLift p g Ïˆ]
    [IsHomLift p g Ïˆ'] (hcomp : Ïˆ â‰« Ï† = Ï†') (hcomp' : Ïˆ' â‰« Ï† = Ï†') : Ïˆ = Ïˆ' := by
  rw [inducedMap_unique p f Ï† hf' Ï†' Ïˆ hcomp, inducedMap_unique p f Ï† hf' Ï†' Ïˆ' hcomp']

@[simp]
lemma inducedMap_self_eq_id (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)
    [IsStronglyCartesian p f Ï†] : inducedMap p f Ï† (id_comp f).symm Ï† = ğŸ™ a := by
  subst_hom_lift p f Ï†; symm
  apply inducedMap_unique
  simp only [id_comp]

/- The composition of two induced maps is also an induced map.
Given a diagrams
```
a''         a'        a --Ï†--> b          a' --Ï†'--> b          a'' --Ï†''--> b
|           |         |        |    and   |          |    and   |            |
v           v         v        v          v          v          v            v
R'' --h'--> R' --h--> R --f--> S          R' --f'--> S          R'' --f''--> S
```
such that `Ï†` and `Ï†'` are cartesian arrows. Composing the induced map from `a'' âŸ¶ a'` with the
induced map from `a' âŸ¶ a` gives the induced map from `a'' âŸ¶ a`. -/
@[simp]
lemma inducedMap_inducedMap (p : ğ’³ â¥¤ ğ’®) {R R' R'' S: ğ’®} {a a' a'' b : ğ’³}
    {f : R âŸ¶ S} {f' : R' âŸ¶ S} {f'' : R'' âŸ¶ S} {g : R' âŸ¶ R} {h : R'' âŸ¶ R'}
    (H : f' = g â‰« f) (H' : f'' = h â‰« f') {Ï† : a âŸ¶ b} {Ï†' : a' âŸ¶ b} {Ï†'' : a'' âŸ¶ b}
    [IsStronglyCartesian p f Ï†] [IsStronglyCartesian p f' Ï†'] [IsHomLift p f'' Ï†''] :
    inducedMap p f' Ï†' H' Ï†'' â‰« inducedMap p f Ï† H Ï†' =
      inducedMap p f Ï† (show f'' = (h â‰« g) â‰« f by rwa [assoc, â† H]) Ï†'' := by
  apply inducedMap_unique p f Ï†
  simp only [assoc, inducedMap_comp]

/-- Given two cartesian arrows `Ï†`, `Ïˆ` as follows
```
a --Ï†--> b --Ïˆ--> c
|        |        |
v        v        v
R --f--> S --g--> T
```
Then the composite `Ï† â‰« Ïˆ` is also cartesian. -/
instance comp (p : ğ’³ â¥¤ ğ’®) {R S T : ğ’®} {a b c: ğ’³} (f : R âŸ¶ S) (g : S âŸ¶ T) (Ï† : a âŸ¶ b)
    (Ïˆ : b âŸ¶ c) [IsStronglyCartesian p f Ï†] [IsStronglyCartesian p g Ïˆ] :
      IsStronglyCartesian p (f â‰« g) (Ï† â‰« Ïˆ) := by
  apply IsStronglyCartesian.mk
  Â· intro a' h Ï„ hÏ„
    -- TODO: can simplify this line??
    use inducedMap p f Ï† (rfl (a := h â‰« f)) (inducedMap p g Ïˆ (assoc h f g).symm Ï„)
    refine âŸ¨âŸ¨inferInstance, ?_âŸ©, ?_âŸ©
    Â· rw [â† assoc, inducedMap_comp, inducedMap_comp]
    Â· intro Ï€' âŸ¨hÏ€'â‚, hÏ€'â‚‚âŸ©
      apply inducedMap_unique
      apply inducedMap_unique
      simp only [assoc, hÏ€'â‚‚]

/-- Given two commutative squares
```
a --Ï†--> b --Ïˆ--> c
|        |        |
v        v        v
R --f--> S --g--> T
```
such that the composite `Ï† â‰« Ïˆ` and `Ïˆ` are cartesian, then so is `Ï†`. -/
protected lemma of_comp (p : ğ’³ â¥¤ ğ’®) {R S T : ğ’®} {a b c: ğ’³} {f : R âŸ¶ S} {g : S âŸ¶ T}
    {Ï† : a âŸ¶ b} {Ïˆ : b âŸ¶ c} [IsStronglyCartesian p g Ïˆ] [IsStronglyCartesian p (f â‰« g) (Ï† â‰« Ïˆ)]
    [IsHomLift p f Ï†] : IsStronglyCartesian p f Ï† := by
  apply IsStronglyCartesian.mk
  -- Fix a morphism `Ï„ : a' âŸ¶ b` and a morphism `h : p(a') âŸ¶ R` such that `Ï„` lifts `h â‰« f`
  intro a' h Ï„ hÏ„
  have hâ‚ : IsHomLift p (h â‰« f â‰« g) (Ï„ â‰« Ïˆ) := by simpa using IsHomLift.comp (h â‰« f) _ Ï„ Ïˆ
  -- We get a morphism `Ï€ : a' âŸ¶ a` from the universal property of `Ï† â‰« Ïˆ`
  use inducedMap p (f â‰« g) (Ï† â‰« Ïˆ) (f' := h â‰« f â‰« g) rfl (Ï„ â‰« Ïˆ)
  refine âŸ¨âŸ¨inferInstance, ?_âŸ©,?_âŸ©
  -- The fact that `Ï€ â‰« Ï† = Ï„` follows from `Ï€ â‰« Ï† â‰« Ïˆ = Ï„ â‰« Ïˆ` and the universal property of `Ïˆ`
  Â· apply IsStronglyCartesian.uniqueness p g Ïˆ (g := h â‰« f) rfl (Ï„ â‰« Ïˆ) (by simp) rfl
  -- Finally, uniqueness of `Ï€` comes from the universal property of `Ï† â‰« Ïˆ`
  intro Ï€' âŸ¨hÏ€'â‚, hÏ€'â‚‚âŸ©
  apply inducedMap_unique
  simp [hÏ€'â‚‚.symm]

instance of_iso (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a â‰… b) [IsHomLift p f Ï†.hom] :
    IsStronglyCartesian p f Ï†.hom := by
  apply IsStronglyCartesian.mk
  intro a' g Ï„ hÏ„
  use Ï„ â‰« Ï†.inv
  refine âŸ¨?_, by aesop_catâŸ©
  simpa using (IsHomLift.comp (g â‰« f) (isoOfIsoLift p f Ï†).inv Ï„ Ï†.inv)

instance of_isIso (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b) [IsHomLift p f Ï†]
    [IsIso Ï†] : IsStronglyCartesian p f Ï† :=
  -- TODO: this is also annoying to have to state
  have : p.IsHomLift f (asIso Ï†).hom := by aesop
  IsStronglyCartesian.of_iso p f (asIso Ï†)

/-- A cartesian arrow lying over an isomorphism is an isomorphism. -/
lemma isIso_of_base_isIso (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)
    [IsStronglyCartesian p f Ï†] (hf : IsIso f) : IsIso Ï† := by
  -- The inverse will be given by applying the universal property to the arrows fâ»Â¹ : S âŸ¶ R and ğŸ™ b
  -- TODO: this is annoying because now we have to state p.map Ï† instead of f....
  subst_hom_lift p f Ï†; clear a b R S -- TODO: make this into meta-code
  let Ï†' := inducedMap p (p.map Ï†) Ï† (IsIso.inv_hom_id (p.map Ï†)).symm (ğŸ™ b)
  use Ï†'
  -- `Ï†' â‰« Ï† = ğŸ™ b` follows immediately from the universal property
  have inv_hom : Ï†' â‰« Ï† = ğŸ™ b := inducedMap_comp p (p.map Ï†) Ï† _ (ğŸ™ b)
  refine âŸ¨?_, inv_homâŸ©
  -- We now show that `Ï† â‰« Ï†' = ğŸ™ a` by applying the universal property of `Ï†` to the equality
  -- `Ï† â‰« Ï†' â‰« Ï† = Ï† â‰« ğŸ™ b = ğŸ™ a â‰« Ï†`
  have hâ‚ : IsHomLift p (ğŸ™ (p.obj a)) (Ï†  â‰« Ï†') := by
    rw [â† IsIso.hom_inv_id (p.map Ï†)]
    apply IsHomLift.comp
  have hâ‚‚ : IsHomLift p (p.map Ï†) (Ï† â‰« Ï†' â‰« Ï†) := by
    simpa using IsHomLift.comp (ğŸ™ (p.obj a)) (p.map Ï†) (Ï† â‰« Ï†') Ï†
  apply IsStronglyCartesian.uniqueness p _ Ï† (id_comp (p.map Ï†)).symm (Ï† â‰« Ï†' â‰« Ï†)
  Â· apply Category.assoc
  Â· simp only [inv_hom, id_comp, comp_id]

/-- The canonical isomorphism between the domains of two cartesian arrows lying over
isomorphic objects. -/
noncomputable def isoOfBaseIso (p : ğ’³ â¥¤ ğ’®) {R R' S : ğ’®} {a a' b : ğ’³} {f : R âŸ¶ S} {f' : R' âŸ¶ S}
  {g : R' â‰… R} (h : f' = g.hom â‰« f) (Ï† : a âŸ¶ b) (Ï†' : a' âŸ¶ b) [IsStronglyCartesian p f Ï†]
    [IsStronglyCartesian p f' Ï†'] : a' â‰… a where
  hom := inducedMap p f Ï† h Ï†'
  -- TODO: make this cleaner...
  inv := @inducedMap _ _ _ _ p _ _ _ _ f' Ï†' _ _ _ _ _ (congrArg (g.inv â‰« Â·) h.symm) Ï†
    (by simp; infer_instance)

/-- The canonical isomorphism between the domains of two cartesian arrows
lying over the same object. -/
noncomputable def naturalIso (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a' a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)
    (Ï†' : a' âŸ¶ b) [IsStronglyCartesian p f Ï†] [IsStronglyCartesian p f Ï†'] : a' â‰… a :=
  isoOfBaseIso p (show f = (Iso.refl R).hom â‰« f by simp) Ï† Ï†'

end IsStronglyCartesian

end CategoryTheory
