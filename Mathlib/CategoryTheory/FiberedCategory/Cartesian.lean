/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Lezeau, Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.FiberedCategory.HomLift

/-!
# Cartesian morphisms

This file defines Cartesian resp. strongly Cartesian morphisms with respect to a functor
`p : ğ’³ â¥¤ ğ’®`.

This file has been adapted to `Mathlib/CategoryTheory/FiberedCategory/Cocartesian.lean`,
please try to change them in sync.

## Main definitions

`IsCartesian p f Ï†` expresses that `Ï†` is a Cartesian morphism lying over `f` with respect to `p` in
the sense of SGA 1 VI 5.1. This means that for any morphism `Ï†' : a' âŸ¶ b` lying over `f` there is
a unique morphism `Ï„ : a' âŸ¶ a` lying over `ğŸ™ R`, such that `Ï†' = Ï„ â‰« Ï†`.

`IsStronglyCartesian p f Ï†` expresses that `Ï†` is a strongly Cartesian morphism lying over `f` with
respect to `p`, see <https://stacks.math.columbia.edu/tag/02XK>.

## Implementation

The constructor of `IsStronglyCartesian` has been named `universal_property'`, and is mainly
intended to be used for constructing instances of this class. To use the universal property, we
generally recommended to use the lemma `IsStronglyCartesian.universal_property` instead. The
difference between the two is that the latter is more flexible with respect to non-definitional
equalities.

## References
* [A. Grothendieck, M. Raynaud, *SGA 1*](https://arxiv.org/abs/math/0206203)
* [Stacks: Fibred Categories](https://stacks.math.columbia.edu/tag/02XJ)
-/

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

open CategoryTheory Functor Category IsHomLift

namespace CategoryTheory.Functor

variable {ğ’® : Type uâ‚} {ğ’³ : Type uâ‚‚} [Category.{vâ‚} ğ’®] [Category.{vâ‚‚} ğ’³] (p : ğ’³ â¥¤ ğ’®)

section

variable {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)

/-- A morphism `Ï† : a âŸ¶ b` in `ğ’³` lying over `f : R âŸ¶ S` in `ğ’®` is Cartesian if for all
morphisms `Ï†' : a' âŸ¶ b`, also lying over `f`, there exists a unique morphism `Ï‡ : a' âŸ¶ a` lifting
`ğŸ™ R` such that `Ï†' = Ï‡ â‰« Ï†`.

See SGA 1 VI 5.1. -/
class IsCartesian : Prop  where
  [toIsHomLift : IsHomLift p f Ï†]
  universal_property {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsHomLift p f Ï†'] :
      âˆƒ! Ï‡ : a' âŸ¶ a, IsHomLift p (ğŸ™ R) Ï‡ âˆ§ Ï‡ â‰« Ï† = Ï†'
attribute [instance] IsCartesian.toIsHomLift

/-- A morphism `Ï† : a âŸ¶ b` in `ğ’³` lying over `f : R âŸ¶ S` in `ğ’®` is strongly Cartesian if for
all morphisms `Ï†' : a' âŸ¶ b` and all diagrams of the form
```
a'        a --Ï†--> b
|         |        |
v         v        v
R' --g--> R --f--> S
```
such that `Ï†'` lifts `g â‰« f`, there exists a lift `Ï‡` of `g` such that `Ï†' = Ï‡ â‰« Ï†`. -/
@[stacks 02XK]
class IsStronglyCartesian : Prop where
  [toIsHomLift : IsHomLift p f Ï†]
  universal_property' {a' : ğ’³} (g : p.obj a' âŸ¶ R) (Ï†' : a' âŸ¶ b) [IsHomLift p (g â‰« f) Ï†'] :
      âˆƒ! Ï‡ : a' âŸ¶ a, IsHomLift p g Ï‡ âˆ§ Ï‡ â‰« Ï† = Ï†'
attribute [instance] IsStronglyCartesian.toIsHomLift

end

namespace IsCartesian

variable {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b) [IsCartesian p f Ï†]

section

variable {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsHomLift p f Ï†']

/-- Given a Cartesian morphism `Ï† : a âŸ¶ b` lying over `f : R âŸ¶ S` in `ğ’³`, and another morphism
`Ï†' : a' âŸ¶ b` which also lifts `f`, then `IsCartesian.map f Ï† Ï†'` is the morphism `a' âŸ¶ a` lifting
`ğŸ™ R` obtained from the universal property of `Ï†`. -/
protected noncomputable def map : a' âŸ¶ a :=
  Classical.choose <| IsCartesian.universal_property (p := p) (f := f) (Ï† := Ï†) Ï†'

instance map_isHomLift : IsHomLift p (ğŸ™ R) (IsCartesian.map p f Ï† Ï†') :=
  (Classical.choose_spec <| IsCartesian.universal_property (p := p) (f := f) (Ï† := Ï†) Ï†').1.1

@[reassoc (attr := simp)]
lemma fac : IsCartesian.map p f Ï† Ï†' â‰« Ï† = Ï†' :=
  (Classical.choose_spec <| IsCartesian.universal_property (p := p) (f := f) (Ï† := Ï†) Ï†').1.2

/-- Given a Cartesian morphism `Ï† : a âŸ¶ b` lying over `f : R âŸ¶ S` in `ğ’³`, and another morphism
`Ï†' : a' âŸ¶ b` which also lifts `f`. Then any morphism `Ïˆ : a' âŸ¶ a` lifting `ğŸ™ R` such that
`g â‰« Ïˆ = Ï†'` must equal the map induced from the universal property of `Ï†`. -/
lemma map_uniq (Ïˆ : a' âŸ¶ a) [IsHomLift p (ğŸ™ R) Ïˆ] (hÏˆ : Ïˆ â‰« Ï† = Ï†') :
    Ïˆ = IsCartesian.map p f Ï† Ï†' :=
  (Classical.choose_spec <| IsCartesian.universal_property (p := p) (f := f) (Ï† := Ï†) Ï†').2
    Ïˆ âŸ¨inferInstance, hÏˆâŸ©

end

/-- Given a Cartesian morphism `Ï† : a âŸ¶ b` lying over `f : R âŸ¶ S` in `ğ’³`, and two morphisms
`Ïˆ Ïˆ' : a' âŸ¶ a` such that `Ïˆ â‰« Ï† = Ïˆ' â‰« Ï†`. Then we must have `Ïˆ = Ïˆ'`. -/
protected lemma ext (Ï† : a âŸ¶ b) [IsCartesian p f Ï†] {a' : ğ’³} (Ïˆ Ïˆ' : a' âŸ¶ a)
    [IsHomLift p (ğŸ™ R) Ïˆ] [IsHomLift p (ğŸ™ R) Ïˆ'] (h : Ïˆ â‰« Ï† = Ïˆ' â‰« Ï†) : Ïˆ = Ïˆ' := by
  rw [map_uniq p f Ï† (Ïˆ â‰« Ï†) Ïˆ rfl, map_uniq p f Ï† (Ïˆ â‰« Ï†) Ïˆ' h.symm]

@[simp]
lemma map_self : IsCartesian.map p f Ï† Ï† = ğŸ™ a := by
  subst_hom_lift p f Ï†; symm
  apply map_uniq
  simp only [id_comp]

instance of_comp_iso {b' : ğ’³} (Ï†' : b â‰… b') [IsHomLift p (ğŸ™ S) Ï†'.hom] :
    IsCartesian p f (Ï† â‰« Ï†'.hom) where
  universal_property := by
    intro c Ïˆ hÏˆ
    use IsCartesian.map p f Ï† (Ïˆ â‰« Ï†'.inv)
    refine âŸ¨âŸ¨inferInstance, by simp only [fac_assoc, assoc, Iso.inv_hom_id, comp_id]âŸ©, ?_âŸ©
    rintro Ï„ âŸ¨hÏ„â‚, hÏ„â‚‚âŸ©
    apply map_uniq
    rw [Iso.eq_comp_inv]
    simp only [assoc, hÏ„â‚‚]

/-- The canonical isomorphism between the domains of two Cartesian arrows
lying over the same object. -/
@[simps]
noncomputable def domainUniqueUpToIso {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsCartesian p f Ï†'] : a' â‰… a where
  hom := IsCartesian.map p f Ï† Ï†'
  inv := IsCartesian.map p f Ï†' Ï†
  hom_inv_id := by
    subst_hom_lift p f Ï†'
    apply IsCartesian.ext p (p.map Ï†') Ï†'
    simp only [assoc, fac, id_comp]
  inv_hom_id := by
    subst_hom_lift p f Ï†
    apply IsCartesian.ext p (p.map Ï†) Ï†
    simp only [assoc, fac, id_comp]

instance domainUniqueUpToIso_inv_isHomLift {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsCartesian p f Ï†'] :
    IsHomLift p (ğŸ™ R) (domainUniqueUpToIso p f Ï† Ï†').hom :=
  domainUniqueUpToIso_hom p f Ï† Ï†' â–¸ IsCartesian.map_isHomLift p f Ï† Ï†'

instance domainUniqueUpToIso_hom_isHomLift {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsCartesian p f Ï†'] :
    IsHomLift p (ğŸ™ R) (domainUniqueUpToIso p f Ï† Ï†').inv :=
  domainUniqueUpToIso_inv p f Ï† Ï†' â–¸ IsCartesian.map_isHomLift p f Ï†' Ï†

/-- Precomposing a Cartesian morphism with an isomorphism lifting the identity is Cartesian. -/
instance of_iso_comp {a' : ğ’³} (Ï†' : a' â‰… a) [IsHomLift p (ğŸ™ R) Ï†'.hom] :
    IsCartesian p f (Ï†'.hom â‰« Ï†) where
  universal_property := by
    intro c Ïˆ hÏˆ
    use IsCartesian.map p f Ï† Ïˆ â‰« Ï†'.inv
    refine âŸ¨âŸ¨inferInstance, by simpâŸ©, ?_âŸ©
    rintro Ï„ âŸ¨hÏ„â‚, hÏ„â‚‚âŸ©
    rw [Iso.eq_comp_inv]
    apply map_uniq
    simp only [assoc, hÏ„â‚‚]

end IsCartesian

namespace IsStronglyCartesian

section

variable {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b) [IsStronglyCartesian p f Ï†]

/-- The universal property of a strongly Cartesian morphism.

This lemma is more flexible with respect to non-definitional equalities than the field
`universal_property'` of `IsStronglyCartesian`. -/
lemma universal_property {R' : ğ’®} {a' : ğ’³} (g : R' âŸ¶ R) (f' : R' âŸ¶ S) (hf' : f' = g â‰« f)
    (Ï†' : a' âŸ¶ b) [IsHomLift p f' Ï†'] : âˆƒ! Ï‡ : a' âŸ¶ a, IsHomLift p g Ï‡ âˆ§ Ï‡ â‰« Ï† = Ï†' := by
  subst_hom_lift p f' Ï†'; clear a b R S
  have : p.IsHomLift (g â‰« f) Ï†' := (hf' â–¸ inferInstance)
  apply IsStronglyCartesian.universal_property' f

instance isCartesian_of_isStronglyCartesian [p.IsStronglyCartesian f Ï†] : p.IsCartesian f Ï† where
  universal_property := fun Ï†' => universal_property p f Ï† (ğŸ™ R) f (by simp) Ï†'

section

variable {R' : ğ’®} {a' : ğ’³} {g : R' âŸ¶ R} {f' : R' âŸ¶ S} (hf' : f' = g â‰« f) (Ï†' : a' âŸ¶ b)
  [IsHomLift p f' Ï†']

/-- Given a diagram
```
a'        a --Ï†--> b
|         |        |
v         v        v
R' --g--> R --f--> S
```
such that `Ï†` is strongly Cartesian, and a morphism `Ï†' : a' âŸ¶ b`. Then `map` is the map `a' âŸ¶ a`
lying over `g` obtained from the universal property of `Ï†`. -/
noncomputable def map : a' âŸ¶ a :=
  Classical.choose <| universal_property p f Ï† _ _ hf' Ï†'

instance map_isHomLift : IsHomLift p g (map p f Ï† hf' Ï†') :=
  (Classical.choose_spec <| universal_property p f Ï† _ _ hf' Ï†').1.1

@[reassoc (attr := simp)]
lemma fac : (map p f Ï† hf' Ï†') â‰« Ï† = Ï†' :=
  (Classical.choose_spec <| universal_property p f Ï† _ _ hf' Ï†').1.2

/-- Given a diagram
```
a'        a --Ï†--> b
|         |        |
v         v        v
R' --g--> R --f--> S
```
such that `Ï†` is strongly Cartesian, and morphisms `Ï†' : a' âŸ¶ b`, `Ïˆ : a' âŸ¶ a` such that
`Ïˆ â‰« Ï† = Ï†'`. Then `Ïˆ` is the map induced by the universal property. -/
lemma map_uniq (Ïˆ : a' âŸ¶ a) [IsHomLift p g Ïˆ] (hÏˆ : Ïˆ â‰« Ï† = Ï†') : Ïˆ = map p f Ï† hf' Ï†' :=
  (Classical.choose_spec <| universal_property p f Ï† _ _ hf' Ï†').2 Ïˆ âŸ¨inferInstance, hÏˆâŸ©

end

/-- Given a diagram
```
a'        a --Ï†--> b
|         |        |
v         v        v
R' --g--> R --f--> S
```
such that `Ï†` is strongly Cartesian, and morphisms `Ïˆ Ïˆ' : a' âŸ¶ a` such that
`g â‰« Ïˆ = Ï†' = g â‰« Ïˆ'`. Then we have that `Ïˆ = Ïˆ'`. -/
protected lemma ext (Ï† : a âŸ¶ b) [IsStronglyCartesian p f Ï†] {R' : ğ’®} {a' : ğ’³} (g : R' âŸ¶ R)
    {Ïˆ Ïˆ' : a' âŸ¶ a} [IsHomLift p g Ïˆ] [IsHomLift p g Ïˆ'] (h : Ïˆ â‰« Ï† = Ïˆ' â‰« Ï†) : Ïˆ = Ïˆ' := by
  rw [map_uniq p f Ï† (g := g) rfl (Ïˆ â‰« Ï†) Ïˆ rfl, map_uniq p f Ï† (g := g) rfl (Ïˆ â‰« Ï†) Ïˆ' h.symm]

@[simp]
lemma map_self : map p f Ï† (id_comp f).symm Ï† = ğŸ™ a := by
  subst_hom_lift p f Ï†; symm
  apply map_uniq
  simp only [id_comp]

/-- When its possible to compare the two, the composition of two `IsStronglyCartesian.map` will also
be given by a `IsStronglyCartesian.map`. In other words, given diagrams
```
a''         a'        a --Ï†--> b
|           |         |        |
v           v         v        v
R'' --g'--> R' --g--> R --f--> S
```
and
```
a' --Ï†'--> b
|          |
v          v
R' --f'--> S
```
and
```
a'' --Ï†''--> b
|            |
v            v
R'' --f''--> S
```
such that `Ï†` and `Ï†'` are strongly Cartesian morphisms, and such that `f' = g â‰« f` and
`f'' = g' â‰« f'`. Then composing the induced map from `a'' âŸ¶ a'` with the induced map from
`a' âŸ¶ a` gives the induced map from `a'' âŸ¶ a`. -/
@[reassoc (attr := simp)]
lemma map_comp_map {R' R'' : ğ’®} {a' a'' : ğ’³} {f' : R' âŸ¶ S} {f'' : R'' âŸ¶ S} {g : R' âŸ¶ R}
    {g' : R'' âŸ¶ R'} (H : f' = g â‰« f) (H' : f'' = g' â‰« f') (Ï†' : a' âŸ¶ b) (Ï†'' : a'' âŸ¶ b)
    [IsStronglyCartesian p f' Ï†'] [IsHomLift p f'' Ï†''] :
    map p f' Ï†' H' Ï†'' â‰« map p f Ï† H Ï†' =
      map p f Ï† (show f'' = (g' â‰« g) â‰« f by rwa [assoc, â† H]) Ï†'' := by
  apply map_uniq p f Ï†
  simp only [assoc, fac]

end

section

variable {R S T : ğ’®} {a b c : ğ’³} {f : R âŸ¶ S} {g : S âŸ¶ T} {Ï† : a âŸ¶ b} {Ïˆ : b âŸ¶ c}

/-- Given two strongly Cartesian morphisms `Ï†`, `Ïˆ` as follows
```
a --Ï†--> b --Ïˆ--> c
|        |        |
v        v        v
R --f--> S --g--> T
```
Then the composite `Ï† â‰« Ïˆ` is also strongly Cartesian. -/
instance comp [IsStronglyCartesian p f Ï†] [IsStronglyCartesian p g Ïˆ] :
    IsStronglyCartesian p (f â‰« g) (Ï† â‰« Ïˆ) where
  universal_property' := by
    intro a' h Ï„ hÏ„
    use map p f Ï† (f' := h â‰« f) rfl (map p g Ïˆ (assoc h f g).symm Ï„)
    refine âŸ¨âŸ¨inferInstance, ?_âŸ©, ?_âŸ©
    Â· rw [â† assoc, fac, fac]
    Â· intro Ï€' âŸ¨hÏ€'â‚, hÏ€'â‚‚âŸ©
      apply map_uniq
      apply map_uniq
      simp only [assoc, hÏ€'â‚‚]

/-- Given two commutative squares
```
a --Ï†--> b --Ïˆ--> c
|        |        |
v        v        v
R --f--> S --g--> T
```
such that `Ï† â‰« Ïˆ` and `Ïˆ` are strongly Cartesian, then so is `Ï†`. -/
protected lemma of_comp [IsStronglyCartesian p g Ïˆ] [IsStronglyCartesian p (f â‰« g) (Ï† â‰« Ïˆ)]
    [IsHomLift p f Ï†] : IsStronglyCartesian p f Ï† where
  universal_property' := by
    intro a' h Ï„ hÏ„
    have hâ‚ : IsHomLift p (h â‰« f â‰« g) (Ï„ â‰« Ïˆ) := by simpa using IsHomLift.comp p (h â‰« f) _ Ï„ Ïˆ
    /- We get a morphism `Ï€ : a' âŸ¶ a` such that `Ï€ â‰« Ï† â‰« Ïˆ = Ï„ â‰« Ïˆ` from the universal property
    of `Ï† â‰« Ïˆ`. This will be the morphism induced by `Ï†`. -/
    use map p (f â‰« g) (Ï† â‰« Ïˆ) (f' := h â‰« f â‰« g) rfl (Ï„ â‰« Ïˆ)
    refine âŸ¨âŸ¨inferInstance, ?_âŸ©, ?_âŸ©
    /- The fact that `Ï€ â‰« Ï† = Ï„` follows from `Ï€ â‰« Ï† â‰« Ïˆ = Ï„ â‰« Ïˆ` and the universal property of
    `Ïˆ`. -/
    Â· apply IsStronglyCartesian.ext p g Ïˆ (h â‰« f) (by simp)
    -- Finally, the uniqueness of `Ï€` comes from the universal property of `Ï† â‰« Ïˆ`.
    Â· intro Ï€' âŸ¨hÏ€'â‚, hÏ€'â‚‚âŸ©
      apply map_uniq
      simp [hÏ€'â‚‚.symm]

end

section

variable {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S)

instance of_iso (Ï† : a â‰… b) [IsHomLift p f Ï†.hom] : IsStronglyCartesian p f Ï†.hom where
  universal_property' := by
    intro a' g Ï„ hÏ„
    use Ï„ â‰« Ï†.inv
    refine âŸ¨?_, by cat_dischâŸ©
    simpa using (IsHomLift.comp p (g â‰« f) (isoOfIsoLift p f Ï†).inv Ï„ Ï†.inv)

instance of_isIso (Ï† : a âŸ¶ b) [IsHomLift p f Ï†] [IsIso Ï†] : IsStronglyCartesian p f Ï† :=
  @IsStronglyCartesian.of_iso _ _ _ _ p _ _ _ _ f (asIso Ï†) (by aesop)

/-- A strongly Cartesian morphism lying over an isomorphism is an isomorphism. -/
lemma isIso_of_base_isIso (Ï† : a âŸ¶ b) [IsStronglyCartesian p f Ï†] [IsIso f] : IsIso Ï† := by
  subst_hom_lift p f Ï†; clear a b R S
  -- Let `Ï†` be the morphism induced by applying universal property to `ğŸ™ b` lying over `fâ»Â¹ â‰« f`.
  let Ï†' := map p (p.map Ï†) Ï† (IsIso.inv_hom_id (p.map Ï†)).symm (ğŸ™ b)
  use Ï†'
  -- `Ï†' â‰« Ï† = ğŸ™ b` follows immediately from the universal property.
  have inv_hom : Ï†' â‰« Ï† = ğŸ™ b := fac p (p.map Ï†) Ï† _ (ğŸ™ b)
  refine âŸ¨?_, inv_homâŸ©
  -- We will now show that `Ï† â‰« Ï†' = ğŸ™ a` by showing that `(Ï† â‰« Ï†') â‰« Ï† = ğŸ™ a â‰« Ï†`.
  have hâ‚ : IsHomLift p (ğŸ™ (p.obj a)) (Ï†  â‰« Ï†') := by
    rw [â† IsIso.hom_inv_id (p.map Ï†)]
    apply IsHomLift.comp
  apply IsStronglyCartesian.ext p (p.map Ï†) Ï† (ğŸ™ (p.obj a))
  simp only [assoc, inv_hom, comp_id, id_comp]

end

section

variable {R R' S : ğ’®} {a a' b : ğ’³} {f : R âŸ¶ S} {f' : R' âŸ¶ S} {g : R' â‰… R}

/-- The canonical isomorphism between the domains of two strongly Cartesian morphisms lying over
isomorphic objects. -/
@[simps]
noncomputable def domainIsoOfBaseIso (h : f' = g.hom â‰« f) (Ï† : a âŸ¶ b) (Ï†' : a' âŸ¶ b)
    [IsStronglyCartesian p f Ï†] [IsStronglyCartesian p f' Ï†'] : a' â‰… a where
  hom := map p f Ï† h Ï†'
  inv :=
    haveI : p.IsHomLift ((fun x â†¦ g.inv â‰« x) (g.hom â‰« f)) Ï† := by
      simpa using IsCartesian.toIsHomLift
    map p f' Ï†' (congrArg (g.inv â‰« Â·) h.symm) Ï†

instance domainUniqueUpToIso_inv_isHomLift (h : f' = g.hom â‰« f) (Ï† : a âŸ¶ b) (Ï†' : a' âŸ¶ b)
    [IsStronglyCartesian p f Ï†] [IsStronglyCartesian p f' Ï†'] :
    IsHomLift p g.hom (domainIsoOfBaseIso p h Ï† Ï†').hom :=
  domainIsoOfBaseIso_hom p h Ï† Ï†' â–¸ IsStronglyCartesian.map_isHomLift p f Ï† h Ï†'

instance domainUniqueUpToIso_hom_isHomLift (h : f' = g.hom â‰« f) (Ï† : a âŸ¶ b) (Ï†' : a' âŸ¶ b)
    [IsStronglyCartesian p f Ï†] [IsStronglyCartesian p f' Ï†'] :
    IsHomLift p g.inv (domainIsoOfBaseIso p h Ï† Ï†').inv := by
  haveI : p.IsHomLift ((fun x â†¦ g.inv â‰« x) (g.hom â‰« f)) Ï† := by
    simpa using IsCartesian.toIsHomLift
  simpa using IsStronglyCartesian.map_isHomLift p f' Ï†' (congrArg (g.inv â‰« Â·) h.symm) Ï†

end

end IsStronglyCartesian

end CategoryTheory.Functor
