/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Lezeau, Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.FiberedCategory.HomLift

/-!
# Cartesian morphisms

This file defines cartesian resp. strongly cartesian morphisms with respect to a functor
`p : ğ’³ âŸ¶ ğ’®`.

## Main definitions

`IsCartesian p f Ï†` expresses that `Ï†` is a cartesian morphism lying over `f` with respect to `p` in
the sense of SGA 1 VI 5.1. This means that for any morphism `Ï†' : a' âŸ¶ b` lying over `f` there is
a unique morphism `Ï„ : a' âŸ¶ a` lying over `ğŸ™ R`, such that `Ï†' = Ï„ â‰« Ï†`.

`IsStronglyCartesian p f Ï†` expresses that `Ï†` is a strongly cartesian morphism lying over `f` with
respect to `p`, see <https://stacks.math.columbia.edu/tag/02XK>.

## Implementation

The constructor of `IsStronglyCartesian` has been named `universal_property'`, and is mainly
intended to be used for constructing instances of this class. To use the universal property, we
generally recommended to use the lemma `IsStronglyCartesian.universal_property` instead. The
difference between the two is that the latter is more flexible with respect to non-definitional
equalities.

## References
* [A. Grothendieck, M. Raynaud, *SGA 1*](https://arxiv.org/abs/math/0206203)
* [Stacks: Fibred Categories](https://stacks.math.columbia.edu/tag/02XJ)
-/

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

open CategoryTheory Functor Category IsHomLift

namespace CategoryTheory

variable {ğ’® : Type uâ‚} {ğ’³ : Type uâ‚‚} [Category.{vâ‚} ğ’®] [Category.{vâ‚‚} ğ’³] (p : ğ’³ â¥¤ ğ’®)

section

variable {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)

/-- The proposition that a morphism `Ï† : a âŸ¶ b` in `ğ’³` lying over `f : R âŸ¶ S` in `ğ’®` is a
cartesian morphism.

See SGA 1 VI 5.1. -/
class Functor.IsCartesian extends IsHomLift p f Ï† : Prop where
  universal_property {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsHomLift p f Ï†'] :
      âˆƒ! Ï‡ : a' âŸ¶ a, IsHomLift p (ğŸ™ R) Ï‡ âˆ§ Ï‡ â‰« Ï† = Ï†'

/-- The proposition that a morphism `Ï† : a âŸ¶ b` in `ğ’³` lying over `f : R âŸ¶ S` in `ğ’®` is a
strongly cartesian morphism.

See <https://stacks.math.columbia.edu/tag/02XK> -/
class Functor.IsStronglyCartesian extends IsHomLift p f Ï† : Prop where
  universal_property' {a' : ğ’³} (g : p.obj a' âŸ¶ R) (Ï†' : a' âŸ¶ b) [IsHomLift p (g â‰« f) Ï†'] :
      âˆƒ! Ï‡ : a' âŸ¶ a, IsHomLift p g Ï‡ âˆ§ Ï‡ â‰« Ï† = Ï†'

end

namespace IsCartesian

variable {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b) [IsCartesian p f Ï†]

section

variable {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsHomLift p f Ï†']

/-- Given a cartesian arrow `Ï† : a âŸ¶ b` lying over `f : R âŸ¶ S` in `ğ’³`, a morphism `Ï†' : a' âŸ¶ b`
lifting `ğŸ™ R`, then `inducedMap f Ï† Ï†'` is the morphism `a' âŸ¶ a` obtained from the universal
property of `Ï†`. -/
noncomputable def inducedMap : a' âŸ¶ a :=
  Classical.choose <| IsCartesian.universal_property (p:=p) (f:=f) (Ï†:=Ï†) Ï†'

instance inducedMap_isHomLift : IsHomLift p (ğŸ™ R) (inducedMap p f Ï† Ï†') :=
  (Classical.choose_spec <| IsCartesian.universal_property (p:=p) (f:=f) (Ï†:=Ï†) Ï†').1.1

@[simp]
lemma inducedMap_comp : (inducedMap p f Ï† Ï†') â‰« Ï† = Ï†' :=
  (Classical.choose_spec <| IsCartesian.universal_property (p:=p) (f:=f) (Ï†:=Ï†) Ï†').1.2

/-- Given a cartesian arrow `Ï† : a âŸ¶ b` lying over `f : R âŸ¶ S` in `ğ’³`, a morphism `Ï†' : a' âŸ¶ b`
lifting `ğŸ™ R`, and a morphism `Ïˆ : a' âŸ¶ a` such that `g â‰« Ïˆ = Ï†'`. Then `Ïˆ` is the map induced
by the universal property of `Ï†`. -/
lemma inducedMap_unique (Ïˆ : a' âŸ¶ a) [IsHomLift p (ğŸ™ R) Ïˆ] (hÏˆ : Ïˆ â‰« Ï† = Ï†') :
    Ïˆ = inducedMap p f Ï† Ï†' :=
  (Classical.choose_spec <| IsCartesian.universal_property (p:=p) (f:=f) (Ï†:=Ï†) Ï†').2
    Ïˆ âŸ¨inferInstance, hÏˆâŸ©

/-- Given a cartesian arrow `Ï† : a âŸ¶ b` lying over `f : R âŸ¶ S` in `ğ’³`, a morphism `Ï†' : a' âŸ¶ b`
lifting `ğŸ™ R`, and two morphisms `Ïˆ Ïˆ' : a' âŸ¶ a` such that `g â‰« Ïˆ = Ï†' = g â‰« Ïˆ'`. Then we must
have `Ïˆ = Ïˆ'`. -/
protected lemma uniqueness {Ïˆ Ïˆ' : a' âŸ¶ a} [IsHomLift p (ğŸ™ R) Ïˆ] [IsHomLift p (ğŸ™ R) Ïˆ']
    (hcomp : Ïˆ â‰« Ï† = Ï†') (hcomp' : Ïˆ' â‰« Ï† = Ï†') : Ïˆ = Ïˆ' := by
  rw [inducedMap_unique p f Ï† Ï†' Ïˆ hcomp, inducedMap_unique p f Ï† Ï†' Ïˆ' hcomp']

end

@[simp]
lemma inducedMap_self_eq_id : inducedMap p f Ï† Ï† = ğŸ™ a := by
  subst_hom_lift p f Ï†; symm
  apply inducedMap_unique
  simp only [id_comp]

instance of_iso_comp {a' : ğ’³} (Ï†' : a' â‰… a) [IsHomLift p (ğŸ™ R) Ï†'.hom] :
    IsCartesian p f (Ï†'.hom â‰« Ï†) where
  universal_property := by
    intro c Ïˆ hÏˆ
    use inducedMap p f Ï† Ïˆ â‰« Ï†'.inv
    refine âŸ¨âŸ¨inferInstance, by simpâŸ©, ?_âŸ©
    rintro Ï„ âŸ¨hÏ„â‚, hÏ„â‚‚âŸ©
    rw [Iso.eq_comp_inv]
    apply inducedMap_unique
    simp only [assoc, hÏ„â‚‚]

instance of_comp_iso {b' : ğ’³} (Ï†' : b â‰… b') [IsHomLift p (ğŸ™ S) Ï†'.hom] :
    IsCartesian p f (Ï† â‰« Ï†'.hom) where
  universal_property := by
    intro c Ïˆ hÏˆ
    use inducedMap p f Ï† (Ïˆ â‰« Ï†'.inv)
    refine âŸ¨âŸ¨inferInstance, by simp [â† assoc, inducedMap_comp]âŸ©, ?_âŸ©
    rintro Ï„ âŸ¨hÏ„â‚, hÏ„â‚‚âŸ©
    apply inducedMap_unique
    rw [Iso.eq_comp_inv]
    simp only [assoc, hÏ„â‚‚]

/-- The canonical isomorphism between the domains of two cartesian arrows
lying over the same object. -/
@[simps]
noncomputable def naturalIso {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsHomLift p f Ï†'] [IsCartesian p f Ï†'] :
    a' â‰… a where
  hom := inducedMap p f Ï† Ï†'
  inv := inducedMap p f Ï†' Ï†
  hom_inv_id := by
    subst_hom_lift p f Ï†'
    apply IsCartesian.uniqueness p (p.map Ï†') Ï†' Ï†' (by simp) (id_comp _)
  inv_hom_id := by
    subst_hom_lift p f Ï†
    apply IsCartesian.uniqueness p (p.map Ï†) Ï† Ï† (by simp) (id_comp _)

instance {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsHomLift p f Ï†'] [IsCartesian p f Ï†'] :
    IsHomLift p (ğŸ™ R) (naturalIso p f Ï† Ï†').hom := by
  simp only [naturalIso_hom]; infer_instance

end IsCartesian

namespace IsStronglyCartesian

section

variable {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b) [IsStronglyCartesian p f Ï†]

lemma universal_property {R' : ğ’®} {a' : ğ’³} (g : R' âŸ¶ R) (f' : R' âŸ¶ S) (hf' : f' = g â‰« f)
    (Ï†' : a' âŸ¶ b) [IsHomLift p f' Ï†'] : âˆƒ! Ï‡ : a' âŸ¶ a, IsHomLift p g Ï‡ âˆ§ Ï‡ â‰« Ï† = Ï†' := by
  subst_hom_lift p f' Ï†'; clear a b R S
  have : p.IsHomLift (g â‰« f) Ï†' := (hf' â–¸ inferInstance)
  apply IsStronglyCartesian.universal_property' f

instance isCartesian_of_isStronglyCartesian [p.IsStronglyCartesian f Ï†] : p.IsCartesian f Ï† where
  universal_property := fun Ï†' => universal_property p f Ï† (ğŸ™ R) f (by simp) Ï†'

section

variable {R' : ğ’®} {a' : ğ’³} {g : R' âŸ¶ R} {f' : R' âŸ¶ S} (hf' : f' = g â‰« f) (Ï†' : a' âŸ¶ b)
  [IsHomLift p f' Ï†']

/-- Given a diagram
```
a'        a --Ï†--> b
|         |        |
v         v        v
R' --g--> R --f--> S
```
such that `Ï†` is a cartesian, and a morphism `Ï†' : a' âŸ¶ b`. Then `inducedMap` is the map `a' âŸ¶ a`
lying over `g` obtained from the universal property of `Ï†`. -/
noncomputable def inducedMap : a' âŸ¶ a :=
  Classical.choose <| universal_property p f Ï† _ _ hf' Ï†'

instance inducedMap_isHomLift : IsHomLift p g (inducedMap p f Ï† hf' Ï†') :=
  (Classical.choose_spec <| universal_property p f Ï† _ _ hf' Ï†').1.1

@[simp]
lemma inducedMap_comp : (inducedMap p f Ï† hf' Ï†') â‰« Ï† = Ï†' :=
  (Classical.choose_spec <| universal_property p f Ï† _ _ hf' Ï†').1.2

/-- Given a diagram
```
a'        a --Ï†--> b
|         |        |
v         v        v
R' --g--> R --f--> S
```
such that `Ï†` is a cartesian, and morphisms`Ï†' : a' âŸ¶ b` and `Ïˆ : a' âŸ¶ a` such that `g â‰« Ïˆ = Ï†'`.
Then `Ïˆ` is the map induced by the universal property. -/
lemma inducedMap_unique (Ïˆ : a' âŸ¶ a) [IsHomLift p g Ïˆ] (hÏˆ : Ïˆ â‰« Ï† = Ï†') :
    Ïˆ = inducedMap p f Ï† hf' Ï†' :=
  (Classical.choose_spec <| universal_property p f Ï† _ _ hf' Ï†').2 Ïˆ âŸ¨inferInstance, hÏˆâŸ©

/-- Given a diagram
```
a'        a --Ï†--> b
|         |        |
v         v        v
R' --g--> R --f--> S
```
such that `Ï†` is cartesian, and morphisms `Ï†' : a' âŸ¶ b`, `Ïˆ Ïˆ' : a' âŸ¶ a` such that
`g â‰« Ïˆ = Ï†' = g â‰« Ïˆ'`. Then `Ïˆ = Ïˆ'`. -/
protected lemma uniqueness {Ïˆ Ïˆ' : a' âŸ¶ a} [IsHomLift p g Ïˆ] [IsHomLift p g Ïˆ']
    (hcomp : Ïˆ â‰« Ï† = Ï†') (hcomp' : Ïˆ' â‰« Ï† = Ï†') : Ïˆ = Ïˆ' := by
  rw [inducedMap_unique p f Ï† hf' Ï†' Ïˆ hcomp, inducedMap_unique p f Ï† hf' Ï†' Ïˆ' hcomp']

end

@[simp]
lemma inducedMap_self_eq_id : inducedMap p f Ï† (id_comp f).symm Ï† = ğŸ™ a := by
  subst_hom_lift p f Ï†; symm
  apply inducedMap_unique
  simp only [id_comp]

/-- The composition of two induced maps is also an induced map. In other words, given diagrams
```
a''         a'        a --Ï†--> b          a' --Ï†'--> b          a'' --Ï†''--> b
|           |         |        |    and   |          |    and   |            |
v           v         v        v          v          v          v            v
R'' --g'--> R' --g--> R --f--> S          R' --f'--> S          R'' --f''--> S
```
such that `Ï†` and `Ï†'` are cartesian arrows. Then composing the induced map from `a'' âŸ¶ a'` with
the induced map from `a' âŸ¶ a` gives the induced map from `a'' âŸ¶ a`. -/
@[simp]
lemma inducedMap_inducedMap {R' R'' : ğ’®} {a' a'' : ğ’³} {f' : R' âŸ¶ S} {f'' : R'' âŸ¶ S} {g : R' âŸ¶ R}
    {g' : R'' âŸ¶ R'} (H : f' = g â‰« f) (H' : f'' = g' â‰« f') (Ï†' : a' âŸ¶ b) (Ï†'' : a'' âŸ¶ b)
    [IsStronglyCartesian p f' Ï†'] [IsHomLift p f'' Ï†''] :
    inducedMap p f' Ï†' H' Ï†'' â‰« inducedMap p f Ï† H Ï†' =
      inducedMap p f Ï† (show f'' = (g' â‰« g) â‰« f by rwa [assoc, â† H]) Ï†'' := by
  apply inducedMap_unique p f Ï†
  simp only [assoc, inducedMap_comp]

end

section

variable {R S T : ğ’®} {a b c : ğ’³} {f : R âŸ¶ S} {g : S âŸ¶ T} {Ï† : a âŸ¶ b} {Ïˆ : b âŸ¶ c}

/-- Given two cartesian morphisms `Ï†`, `Ïˆ` as follows
```
a --Ï†--> b --Ïˆ--> c
|        |        |
v        v        v
R --f--> S --g--> T
```
Then the composite `Ï† â‰« Ïˆ` is also cartesian. -/
instance comp [IsStronglyCartesian p f Ï†] [IsStronglyCartesian p g Ïˆ] :
    IsStronglyCartesian p (f â‰« g) (Ï† â‰« Ïˆ) where
  universal_property' := by
    intro a' h Ï„ hÏ„
    use inducedMap p f Ï† (f' := h â‰« f) rfl (inducedMap p g Ïˆ (assoc h f g).symm Ï„)
    refine âŸ¨âŸ¨inferInstance, ?_âŸ©, ?_âŸ©
    Â· rw [â† assoc, inducedMap_comp, inducedMap_comp]
    Â· intro Ï€' âŸ¨hÏ€'â‚, hÏ€'â‚‚âŸ©
      apply inducedMap_unique
      apply inducedMap_unique
      simp only [assoc, hÏ€'â‚‚]

/-- Given two commutative squares
```
a --Ï†--> b --Ïˆ--> c
|        |        |
v        v        v
R --f--> S --g--> T
```
such that the `Ï† â‰« Ïˆ` and `Ïˆ` are cartesian, then so is `Ï†`. -/
protected lemma of_comp [IsStronglyCartesian p g Ïˆ] [IsStronglyCartesian p (f â‰« g) (Ï† â‰« Ïˆ)]
    [IsHomLift p f Ï†] : IsStronglyCartesian p f Ï† where
  universal_property' := by
    intro a' h Ï„ hÏ„
    have hâ‚ : IsHomLift p (h â‰« f â‰« g) (Ï„ â‰« Ïˆ) := by simpa using IsHomLift.comp p (h â‰« f) _ Ï„ Ïˆ
    /- We get a morphism `Ï€ : a' âŸ¶ a` such that `Ï€ â‰« Ï† â‰« Ïˆ` = `Ï„ = Ïˆ` from the universal property
    of `Ï† â‰« Ïˆ`. -/
    use inducedMap p (f â‰« g) (Ï† â‰« Ïˆ) (f' := h â‰« f â‰« g) rfl (Ï„ â‰« Ïˆ)
    -- This will be the morphism induced by `Ï†`.
    refine âŸ¨âŸ¨inferInstance, ?_âŸ©, ?_âŸ©
    /- The fact that `Ï€ â‰« Ï† = Ï„` follows from `Ï€ â‰« Ï† â‰« Ïˆ = Ï„ â‰« Ïˆ` and the universal property of
    `Ïˆ`. -/
    Â· apply IsStronglyCartesian.uniqueness p g Ïˆ (g := h â‰« f) rfl (Ï„ â‰« Ïˆ) (by simp) rfl
    -- Finally, uniqueness of `Ï€` comes from the universal property of `Ï† â‰« Ïˆ`.
    Â· intro Ï€' âŸ¨hÏ€'â‚, hÏ€'â‚‚âŸ©
      apply inducedMap_unique
      simp [hÏ€'â‚‚.symm]

end

section

variable {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S)

instance of_iso (Ï† : a â‰… b) [IsHomLift p f Ï†.hom] : IsStronglyCartesian p f Ï†.hom where
  universal_property' := by
    intro a' g Ï„ hÏ„
    use Ï„ â‰« Ï†.inv
    refine âŸ¨?_, by aesop_catâŸ©
    simpa using (IsHomLift.comp p (g â‰« f) (isoOfIsoLift p f Ï†).inv Ï„ Ï†.inv)

instance of_isIso (Ï† : a âŸ¶ b) [IsHomLift p f Ï†] [IsIso Ï†] : IsStronglyCartesian p f Ï† :=
  @IsStronglyCartesian.of_iso _ _ _ _ p _ _ _ _ f (asIso Ï†) (by aesop)

/-- A cartesian arrow lying over an isomorphism is an isomorphism. -/
lemma isIso_of_base_isIso (Ï† : a âŸ¶ b) [IsStronglyCartesian p f Ï†] (hf : IsIso f) : IsIso Ï† := by
  subst_hom_lift p f Ï†; clear a b R S
  -- Let `Ï†` be the morphism induced by applying universal property to `ğŸ™ b` lying over `fâ»Â¹ â‰« f`.
  let Ï†' := inducedMap p (p.map Ï†) Ï† (IsIso.inv_hom_id (p.map Ï†)).symm (ğŸ™ b)
  use Ï†'
  -- `Ï†' â‰« Ï† = ğŸ™ b` follows immediately from the universal property.
  have inv_hom : Ï†' â‰« Ï† = ğŸ™ b := inducedMap_comp p (p.map Ï†) Ï† _ (ğŸ™ b)
  refine âŸ¨?_, inv_homâŸ©
  /- We now show that `Ï† â‰« Ï†' = ğŸ™ a` by applying the universal property of `Ï†` to the equality
  `(Ï† â‰« Ï†') â‰« Ï† = Ï† â‰« ğŸ™ b = ğŸ™ a â‰« Ï†`. -/
  have hâ‚ : IsHomLift p (ğŸ™ (p.obj a)) (Ï†  â‰« Ï†') := by
    rw [â† IsIso.hom_inv_id (p.map Ï†)]
    apply IsHomLift.comp
  have hâ‚‚ : IsHomLift p (p.map Ï†) (Ï† â‰« Ï†' â‰« Ï†) := by
    simpa using IsHomLift.comp p (ğŸ™ (p.obj a)) (p.map Ï†) (Ï† â‰« Ï†') Ï†
  apply IsStronglyCartesian.uniqueness p _ Ï† (id_comp (p.map Ï†)).symm (Ï† â‰« Ï†' â‰« Ï†) (assoc _ _ _)
  Â· simp only [inv_hom, id_comp, comp_id]

end

/-- The canonical isomorphism between the domains of two cartesian arrows lying over
isomorphic objects. -/
noncomputable def isoOfBaseIso {R R' S : ğ’®} {a a' b : ğ’³} {f : R âŸ¶ S} {f' : R' âŸ¶ S}
  {g : R' â‰… R} (h : f' = g.hom â‰« f) (Ï† : a âŸ¶ b) (Ï†' : a' âŸ¶ b) [IsStronglyCartesian p f Ï†]
    [IsStronglyCartesian p f' Ï†'] : a' â‰… a where
  hom := inducedMap p f Ï† h Ï†'
  inv := @inducedMap _ _ _ _ p _ _ _ _ f' Ï†' _ _ _ _ _ (congrArg (g.inv â‰« Â·) h.symm) Ï†
    (by simp; infer_instance)

end IsStronglyCartesian

end CategoryTheory
