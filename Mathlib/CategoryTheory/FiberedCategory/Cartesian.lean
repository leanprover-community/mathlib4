/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Lezeau, Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.FiberedCategory.HomLift

/-!
# Cartesian morphisms

This file defines cartesian resp. strongly cartesian morphisms in a based category.

## Main definitions
`IsCartesian p f Ï†` expresses that `Ï†` is a cartesian arrow lying over `f` with respect to `p` in
the sense of SGA 1 VI 5.1. This means that for any morphism `Ï†' : a' âŸ¶ b` lying over `f` there is
a unique morphism `Ï„ : a' âŸ¶ a` lying over `ğŸ™ R`, such that `Ï†' = Ï„ â‰« Ï†`.

## References
* [A. Grothendieck, M. Raynaud, *SGA 1*](https://arxiv.org/abs/math/0206203)
-/

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

open CategoryTheory Functor Category IsHomLift

namespace CategoryTheory

variable {ğ’® : Type uâ‚} {ğ’³ : Type uâ‚‚} [Category.{vâ‚} ğ’®] [Category.{vâ‚‚} ğ’³] (p : ğ’³ â¥¤ ğ’®)

/-- The proposition that a morphism `Ï† : a âŸ¶ b` in `ğ’³` lying over `f : R âŸ¶ S` in `ğ’®` is a
cartesian arrow, see SGA 1 VI 5.1. -/
class Functor.IsCartesian {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b) extends
    IsHomLift p f Ï† : Prop where
  universal_property {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsHomLift p f Ï†'] :
      âˆƒ! Ï‡ : a' âŸ¶ a, IsHomLift p (ğŸ™ R) Ï‡ âˆ§ Ï‡ â‰« Ï† = Ï†'

namespace Functor.IsCartesian

variable {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b) [IsCartesian p f Ï†]

section

variable {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsHomLift p f Ï†']

/-- Given a cartesian arrow `Ï† : a âŸ¶ b` lying over `f : R âŸ¶ S` in `ğ’³`, a morphism `Ï†' : a' âŸ¶ b`
lifting `ğŸ™ R`, then `IsCartesian.map f Ï† Ï†'` is the morphism `a' âŸ¶ a` obtained from the universal
property of `Ï†`. -/
protected noncomputable def map : a' âŸ¶ a :=
  Classical.choose <| IsCartesian.universal_property (p:=p) (f:=f) (Ï†:=Ï†) Ï†'

instance map_isHomLift : IsHomLift p (ğŸ™ R) (IsCartesian.map p f Ï† Ï†') :=
  (Classical.choose_spec <| IsCartesian.universal_property (p:=p) (f:=f) (Ï†:=Ï†) Ï†').1.1

@[reassoc (attr := simp)]
lemma fac : IsCartesian.map p f Ï† Ï†' â‰« Ï† = Ï†' :=
  (Classical.choose_spec <| IsCartesian.universal_property (p:=p) (f:=f) (Ï†:=Ï†) Ï†').1.2

/-- Given a cartesian arrow `Ï† : a âŸ¶ b` lying over `f : R âŸ¶ S` in `ğ’³`, a morphism `Ï†' : a' âŸ¶ b`
lifting `ğŸ™ R`, and a morphism `Ïˆ : a' âŸ¶ a` such that `g â‰« Ïˆ = Ï†'`. Then `Ïˆ` is the map induced
by the universal property of `Ï†`. -/
lemma map_uniq (Ïˆ : a' âŸ¶ a) [IsHomLift p (ğŸ™ R) Ïˆ] (hÏˆ : Ïˆ â‰« Ï† = Ï†') :
    Ïˆ = IsCartesian.map p f Ï† Ï†' :=
  (Classical.choose_spec <| IsCartesian.universal_property (p:=p) (f:=f) (Ï†:=Ï†) Ï†').2
    Ïˆ âŸ¨inferInstance, hÏˆâŸ©

/-- Given a cartesian arrow `Ï† : a âŸ¶ b` lying over `f : R âŸ¶ S` in `ğ’³`, a morphism `Ï†' : a' âŸ¶ b`
lifting `ğŸ™ R`, and two morphisms `Ïˆ Ïˆ' : a' âŸ¶ a` such that `g â‰« Ïˆ = Ï†' = g â‰« Ïˆ'`. Then we must
have `Ïˆ = Ïˆ'`. -/
lemma eq_of_fac {Ïˆ Ïˆ' : a' âŸ¶ a} [IsHomLift p (ğŸ™ R) Ïˆ]
    [IsHomLift p (ğŸ™ R) Ïˆ'] (hcomp : Ïˆ â‰« Ï† = Ï†') (hcomp' : Ïˆ' â‰« Ï† = Ï†') : Ïˆ = Ïˆ' := by
  rw [map_uniq p f Ï† Ï†' Ïˆ hcomp, map_uniq p f Ï† Ï†' Ïˆ' hcomp']

end

@[simp]
lemma map_self : IsCartesian.map p f Ï† Ï† = ğŸ™ a := by
  subst_hom_lift p f Ï†; symm
  apply map_uniq
  simp only [id_comp]

/-- The canonical isomorphism between the domains of two cartesian arrows
lying over the same object. -/
@[simps]
noncomputable def domainUniqueUpToIso {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsCartesian p f Ï†'] : a' â‰… a where
  hom := IsCartesian.map p f Ï† Ï†'
  inv := IsCartesian.map p f Ï†' Ï†
  hom_inv_id := by
    subst_hom_lift p f Ï†'
    apply eq_of_fac p (p.map Ï†') Ï†' Ï†' (by simp) (id_comp _)
  inv_hom_id := by
    subst_hom_lift p f Ï†
    apply eq_of_fac p (p.map Ï†) Ï† Ï† (by simp) (id_comp _)

/-- Precomposing a cartesian morphism with an isomorphism lifting the identity is cartesian. -/
instance of_iso_comp {a' : ğ’³} (Ï†' : a' â‰… a) [IsHomLift p (ğŸ™ R) Ï†'.hom] :
    IsCartesian p f (Ï†'.hom â‰« Ï†) where
  universal_property := by
    intro c Ïˆ hÏˆ
    use IsCartesian.map p f Ï† Ïˆ â‰« Ï†'.inv
    refine âŸ¨âŸ¨inferInstance, by simpâŸ©, ?_âŸ©
    rintro Ï„ âŸ¨hÏ„â‚, hÏ„â‚‚âŸ©
    rw [Iso.eq_comp_inv]
    apply map_uniq
    simp only [assoc, hÏ„â‚‚]

/-- Postcomposing a cartesian morphism with an isomorphism lifting the identity is cartesian. -/
instance of_comp_iso {b' : ğ’³} (Ï†' : b â‰… b') [IsHomLift p (ğŸ™ S) Ï†'.hom] :
    IsCartesian p f (Ï† â‰« Ï†'.hom) where
  universal_property := by
    intro c Ïˆ hÏˆ
    use IsCartesian.map p f Ï† (Ïˆ â‰« Ï†'.inv)
    refine âŸ¨âŸ¨inferInstance, by simpâŸ©, ?_âŸ©
    rintro Ï„ âŸ¨hÏ„â‚, hÏ„â‚‚âŸ©
    apply map_uniq
    simp only [Iso.eq_comp_inv, assoc, hÏ„â‚‚]

end Functor.IsCartesian
