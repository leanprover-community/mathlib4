/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Lezeau, Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.FiberedCategory.HomLift

/-!

# Cartesian morphisms

This file defines cartesian resp. strongly cartesian morphisms in a based category.

## Main definitions
`IsCartesian p f Ï†` expresses that `Ï†` is a cartesian arrow lying over `f` with respect to `p` in
the sense of SGA 1 VI 5.1. This means that for any morphism `Ï†' : a' âŸ¶ b` lying over `f` there is
a unique morphism `Ï„ : a' âŸ¶ a` lying over `ğŸ™ R`, such that `Ï†' = Ï„ â‰« Ï†`.

## References
* [A. Grothendieck, M. Raynaud, *SGA 1*](https://arxiv.org/abs/math/0206203)
-/

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

open CategoryTheory Functor Category IsHomLift

namespace CategoryTheory

variable {ğ’® : Type uâ‚} {ğ’³ : Type uâ‚‚} [Category.{vâ‚} ğ’®] [Category.{vâ‚‚} ğ’³]

/-- The proposition that a lift
```
  a --Ï†--> b
  -        -
  |        |
  v        v
  R --f--> S
```
is a cartesian arrow, see SGA 1 VI 5.1.
-/
class Functor.IsCartesian (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b) extends
    IsHomLift p f Ï† : Prop where
  universal_property {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsHomLift p f Ï†'] :
      âˆƒ! Ï‡ : a' âŸ¶ a, IsHomLift p (ğŸ™ R) Ï‡ âˆ§ Ï‡ â‰« Ï† = Ï†'

namespace IsCartesian

variable (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b) [IsCartesian p f Ï†]

section

variable {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsHomLift p f Ï†']

/-- Given an arrow `Ï†' : a' âŸ¶ b` and a diagram:
```
a'        a --Ï†--> b
|         |        |
v         v        v
R' ====== R --f--> S
```
such that `Ï†` is a cartesian arrow, then `inducedMap f Ï† Ï†'` is the map `a' âŸ¶ a`
obtained from the universal property of `Ï†`. -/
noncomputable def inducedMap : a' âŸ¶ a :=
  Classical.choose <| IsCartesian.universal_property (p:=p) (f:=f) (Ï†:=Ï†) Ï†'

instance inducedMap_isHomLift : IsHomLift p (ğŸ™ R) (inducedMap p f Ï† Ï†') :=
  (Classical.choose_spec <| IsCartesian.universal_property (p:=p) (f:=f) (Ï†:=Ï†) Ï†').1.1

@[simp]
lemma inducedMap_comp : (inducedMap p f Ï† Ï†') â‰« Ï† = Ï†' :=
  (Classical.choose_spec <| IsCartesian.universal_property (p:=p) (f:=f) (Ï†:=Ï†) Ï†').1.2

/-- Given a diagram:
```
a'        a --Ï†--> b
|         |        |
v         v        v
R' ====== R --f--> S
```
with `Ï†` a cartesian arrow. Then for any morphism `Ï†' : a' âŸ¶ b`, and any `Ïˆ : a' âŸ¶ a` such that
`g â‰« Ïˆ = Ï†'`. Then `Ïˆ` equals the map induced by the universal property of `Ï†`. -/
lemma inducedMap_unique (Ïˆ : a' âŸ¶ a) [IsHomLift p (ğŸ™ R) Ïˆ] (hÏˆ : Ïˆ â‰« Ï† = Ï†') :
    Ïˆ = inducedMap p f Ï† Ï†' :=
  (Classical.choose_spec <| IsCartesian.universal_property (p:=p) (f:=f) (Ï†:=Ï†) Ï†').2
    Ïˆ âŸ¨inferInstance, hÏˆâŸ©

/-- Given a diagram a cartesian arrow `Ï† : a âŸ¶ b` in `ğ’³` and a diagram
```
a'        a --Ï†--> b
|         |        |
v         v        v
R' ====== R --f--> S
```
Then for any arrow `Ï†' : a' âŸ¶ b`, and any two arrows `Ïˆ Ïˆ' : a' âŸ¶ a` such that
`g â‰« Ïˆ = Ï†' = g â‰« Ïˆ'`. Then we must have `Ïˆ = Ïˆ'`. -/
protected lemma uniqueness {Ïˆ Ïˆ' : a' âŸ¶ a} [IsHomLift p (ğŸ™ R) Ïˆ] [IsHomLift p (ğŸ™ R) Ïˆ']
    (hcomp : Ïˆ â‰« Ï† = Ï†') (hcomp' : Ïˆ' â‰« Ï† = Ï†') : Ïˆ = Ïˆ' := by
  rw [inducedMap_unique p f Ï† Ï†' Ïˆ hcomp, inducedMap_unique p f Ï† Ï†' Ïˆ' hcomp']

end

@[simp]
lemma inducedMap_self_eq_id : inducedMap p f Ï† Ï† = ğŸ™ a := by
  subst_hom_lift p f Ï†; symm
  apply inducedMap_unique
  simp only [id_comp]

/-- The canonical isomorphism between the domains of two cartesian arrows
lying over the same object. -/
@[simps]
noncomputable def naturalIso {a' : ğ’³} (Ï†' : a' âŸ¶ b) [IsHomLift p f Ï†'] [IsCartesian p f Ï†'] :
    a' â‰… a where
  hom := inducedMap p f Ï† Ï†'
  inv := inducedMap p f Ï†' Ï†
  hom_inv_id := by
    subst_hom_lift p f Ï†'
    apply IsCartesian.uniqueness p (p.map Ï†') Ï†' Ï†' (by simp) (id_comp _)
  inv_hom_id := by
    subst_hom_lift p f Ï†
    apply IsCartesian.uniqueness p (p.map Ï†) Ï† Ï† (by simp) (id_comp _)

end IsCartesian
