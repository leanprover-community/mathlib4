/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle SÃ¶nne, Paul Lezeau
-/
import Mathlib.CategoryTheory.FiberedCategory.Fiber
import Mathlib.CategoryTheory.FiberedCategory.Fibered

/-!

# Fibers of functors

In this file we introduce a typeclass `HasFibers` for a functor `p : ð’³ â¥¤ ð’®`, consisting of:
- A collection of categories `Fib S` for every `S` in `ð’®` (the fiber categories)
- Functors `Î¹ : Fib S â¥¤ ð’³` such that `Î¹ â‹™ p = const (Fib S) S
- The induced functor `Fib S â¥¤ Fiber p S` is an equivalence.

We also provide a canonical `HasFibers` instance, which uses the standard fibers `Fiber p S`
(see `Mathlib/CategoryTheory/FiberedCategory/Fiber.lean`). This makes it so that any result proven
about `HasFibers` can be used for the standard fibers as well.

The reason for introducing this typeclass is that in practice, when working with (pre)fibered
categories one often already has a collection of categories `Fib S` for every `S` that are
equivalent to the fibers `Fiber p S`. One would then like to use these categories `Fib S` directly,
instead of working through this equivalence of categories. By developing an API for the `HasFibers`
typeclass, this will be possible.

Here is an example of when this typeclass is useful. Suppose we have a presheaf of types
`F : ð’®áµ’áµ– â¥¤ Type _`. The associated fibered category then has objects `(S, a)` where `S : ð’®` and `a`
is an element of `F(S)`. The fiber category `Fiber p S` is then equivalent to the discrete category
`Fib S` with objects `a` in `F(S)`. In this case, the `HasFibers` instance is given by the
categories `F(S)` and the functor `Î¹` sends `a : F(S)` to `(S, a)` in the fibered category.

## Main API
The following API is developed so that the fibers from a `HasFibers` instance can be used
analogously to the standard fibers.

- `Fib.homMk Ï†` is a lift of a morphism `Ï† : (Î¹ S).obj a âŸ¶ (Î¹ S).obj b` in `ð’³`, which lies over
  `ðŸ™ S`, to a morphism in the fiber over `S`.
- `Fib.mk` gives an object in the fiber over `S` which is isomorphic to a given `a : ð’³` that
  satisfies `p(a) = S`. The isomorphism is given by `Fib.mkIsoSelf`.
- `HasFibers.mkPullback` is a version of `IsPreFibered.mkPullback` which ensures that the object
  lies in a given fiber. The corresponding Cartesian morphism is given by `HasFibers.pullbackMap`.
- `HasFibers.inducedMap` is a version of `IsCartesian.inducedMap` which gives the corresponding
  morphism in the fiber category.
- `fiber_factorization` is the statement that any morphism in `ð’³` can be factored as a morphism in
  some fiber followed by a pullback.

-/

universe vâ‚ƒ uâ‚ƒ vâ‚‚ uâ‚‚ vâ‚ uâ‚

open CategoryTheory Functor Category IsCartesian IsHomLift Fiber

variable {ð’® : Type uâ‚} {ð’³ : Type uâ‚‚} [Category.{vâ‚} ð’®] [Category.{vâ‚‚} ð’³]

/-- HasFibers is an extrinsic notion of fibers on a functor `p : ð’³ â¥¤ ð’®`. It is given by a
collection of categories `Fib S` for every `S : ð’®` (the fiber categories), each equipped with a
functors `Î¹ : Fib S â¥¤ ð’³` which map constantly to `S` on the base such that the induced functor
`Fib S â¥¤ Fiber p S` is an equivalence. -/
@[nolint checkUnivs]
class HasFibers (p : ð’³ â¥¤ ð’®) where
  /-- The type of objects of the category `Fib S` for each `S`. -/
  Fib (S : ð’®) : Type uâ‚ƒ
  /-- `Fib S` is a category. -/
  category (S : ð’®) : Category.{vâ‚ƒ} (Fib S) := by infer_instance
  /-- The functor `Î¹ : Fib S â¥¤ ð’³`. -/
  Î¹ (S : ð’®) : Fib S â¥¤ ð’³
  /-- The composition with the functor `p` is *equal* to the constant functor mapping to `S`. -/
  comp_const (S : ð’®) : Î¹ S â‹™ p = (const (Fib S)).obj S
  /-- The induced functor from `Fib S` to the fiber of `ð’³ â¥¤ ð’®` over `S` is an equivalence. -/
  equiv (S : ð’®) : Functor.IsEquivalence (inducedFunctor (comp_const S)) := by infer_instance

namespace HasFibers

/-- The `HasFibers` on `p : ð’³ â¥¤ ð’®` given by the fibers of `p` -/
def canonical (p : ð’³ â¥¤ ð’®) : HasFibers p where
  Fib := Fiber p
  Î¹ S := fiberInclusion
  comp_const S := fiberInclusion_comp_eq_const
  equiv S := by exact isEquivalence_of_iso (F := ðŸ­ (Fiber p S)) (Iso.refl _)

section

variable (p : ð’³ â¥¤ ð’®) [HasFibers p] (S : ð’®)

attribute [instance] category

/-- The induced functor from `Fib p S` to the standard fiber. -/
@[simps!]
def inducedFunctor : Fib p S â¥¤ Fiber p S :=
  Fiber.inducedFunctor (comp_const S)

/-- The natural transformation `Î¹ S â‰… (inducedFunctor p S) â‹™ (fiberInclusion p S)` -/
def inducedFunctor.natIso : Î¹ S â‰… (inducedFunctor p S) â‹™ fiberInclusion :=
  Fiber.inducedFunctorCompIsoSelf (comp_const S)

lemma inducedFunctor_comp : Î¹ S = (inducedFunctor p S) â‹™ fiberInclusion :=
  Fiber.inducedFunctor_comp (comp_const S)

instance : Functor.IsEquivalence (inducedFunctor p S) := equiv S

instance : Functor.Faithful (Î¹ (p:=p) S) :=
  Functor.Faithful.of_iso (inducedFunctor.natIso p S).symm

end

section

variable {p : ð’³ â¥¤ ð’®} [HasFibers p]

@[simp]
lemma proj_eq {S : ð’®} (a : Fib p S) : p.obj ((Î¹ S).obj a) = S := by
  simp only [â† comp_obj, comp_const, const_obj_obj]

/-- The morphism `R âŸ¶ S` in `ð’®` obtained by projecting a morphism
`Ï† : (Î¹ R).obj a âŸ¶ (Î¹ S).obj b`. -/
def projMap {R S : ð’®} {a : Fib p R} {b : Fib p S}
    (Ï† : (Î¹ R).obj a âŸ¶ (Î¹ S).obj b) : R âŸ¶ S :=
  eqToHom (proj_eq a).symm â‰« (p.map Ï†) â‰« eqToHom (proj_eq b)

/-- For any homomorphism `Ï†` in a fiber `Fib S`, its image under `Î¹ S` lies over `ðŸ™ S`. -/
instance homLift {S : ð’®} {a b : Fib p S} (Ï† : a âŸ¶ b) : IsHomLift p (ðŸ™ S) ((Î¹ S).map Ï†) := by
  apply of_fac p _ _ (proj_eq a) (proj_eq b)
  rw [â† Functor.comp_map, Functor.congr_hom (comp_const S)]
  simp

/-- A version of fullness of the functor `Fib S â¥¤ Fiber p S` that can be used inside the category
`ð’³`. -/
noncomputable def Fib.homMk {S : ð’®} {a b : Fib p S} (Ï† : (Î¹ S).obj a âŸ¶ (Î¹ S).obj b)
    [IsHomLift p (ðŸ™ S) Ï†] : a âŸ¶ b :=
  (inducedFunctor _ S).preimage (Fiber.homMk p S Ï†)

@[simp]
lemma Fib.map_homMk {S : ð’®} {a b : Fib p S} (Ï† : (Î¹ S).obj a âŸ¶ (Î¹ S).obj b)
    [IsHomLift p (ðŸ™ S) Ï†] : (Î¹ S).map (homMk Ï†) = Ï† := by
  simp [Fib.homMk, congr_hom (inducedFunctor_comp p S)]

@[ext]
lemma Fib.hom_ext {S : ð’®} {a b : Fib p S} {f g : a âŸ¶ b}
    (h : (Î¹ S).map f = (Î¹ S).map g) : f = g :=
  (Î¹ S).map_injective h

/-- The lift of an isomorphism `Î¦ : (Î¹ S).obj a â‰… (Î¹ S).obj b` lying over `ðŸ™ S` to an isomorphism
in `Fib S`. -/
@[simps]
noncomputable def Fib.isoMk {S : ð’®} {a b : Fib p S}
    (Î¦ : (Î¹ S).obj a â‰… (Î¹ S).obj b) (hÎ¦ : IsHomLift p (ðŸ™ S) Î¦.hom) : a â‰… b where
  hom := Fib.homMk Î¦.hom
  inv := Fib.homMk Î¦.inv

/-- An object in `Fib p S` isomorphic in `ð’³` to a given object `a : ð’³` such that `p(a) = S`. -/
noncomputable def Fib.mk {S : ð’®} {a : ð’³} (ha : p.obj a = S) : Fib p S :=
  Functor.objPreimage (inducedFunctor p S) (Fiber.mk ha)

/-- Applying `Î¹ S` to the preimage of `a : ð’³` in `Fib p S` yields an object isomorphic to `a`. -/
noncomputable def Fib.mkIsoSelf {S : ð’®} {a : ð’³} (ha : p.obj a = S) :
    (Î¹ S).obj (Fib.mk ha) â‰… a :=
  fiberInclusion.mapIso (Functor.objObjPreimageIso (inducedFunctor p S) (Fiber.mk ha))

instance Fib.mkIsoSelfIsHomLift {S : ð’®} {a : ð’³} (ha : p.obj a = S) :
    IsHomLift p (ðŸ™ S) (Fib.mkIsoSelf ha).hom :=
  (Functor.objObjPreimageIso (inducedFunctor p S) (Fiber.mk ha)).hom.2

section

variable [IsPreFibered p] {R S : ð’®} {a : ð’³} (f : R âŸ¶ S) (ha : p.obj a = S)

/-- The domain, taken in `Fib p R`, of some Cartesian morphism lifting a given
`f : R âŸ¶ S` in `ð’®` -/
noncomputable def mkPullback : Fib p R :=
  Fib.mk (domain_eq p f (IsPreFibered.pullbackMap ha f))

/-- A Cartesian morphism lifting `f : R âŸ¶ S` with domain in the image of `Fib p R` -/
noncomputable def pullbackMap : (Î¹ R).obj (mkPullback f ha) âŸ¶ a :=
  (Fib.mkIsoSelf (domain_eq p f (IsPreFibered.pullbackMap ha f))).hom â‰«
    (IsPreFibered.pullbackMap ha f)

instance pullbackMap.isCartesian : IsCartesian p f (pullbackMap f ha) := by
  conv in f => rw [â† id_comp f]
  simp only [id_comp, pullbackMap]
  infer_instance

end

section

variable {R S : ð’®} {a : ð’³} {b b' : Fib p R} (f : R âŸ¶ S) (Ïˆ : (Î¹ R).obj b' âŸ¶ a)
    [IsCartesian p f Ïˆ] (Ï† : (Î¹ R).obj b âŸ¶ a) [IsHomLift p f Ï†]

/-- Given a fibered category p, b' b in Fib R, and a pullback Ïˆ : b âŸ¶ a in ð’³, i.e.
```
b'       b --Ïˆ--> a
|        |        |
v        v        v
R ====== R --f--> S
```
Then the induced map Ï„ : b' âŸ¶ b can be lifted to the fiber over R -/
noncomputable def inducedMap : b âŸ¶ b' :=
  Fib.homMk (IsCartesian.map p f Ïˆ Ï†)

@[reassoc]
lemma inducedMap_comp : (Î¹ R).map (inducedMap f Ïˆ Ï†) â‰« Ïˆ = Ï† := by
  simp only [inducedMap, Fib.map_homMk, IsCartesian.fac]

end

section

variable [IsFibered p] {R S : ð’®} {a : ð’³} {b : Fib p R}

/-- Given `a : ð’³`, `b : Fib p R`, and a diagram
```
  b --Ï†--> a
  -        -
  |        |
  v        v
  R --f--> S
```
It can be factorized as
```
  b --Ï„--> b'--Ïˆ--> a
  -        -        -
  |        |        |
  v        v        v
  R ====== R --f--> S
```
with `Ïˆ` Cartesian over `f` and `Ï„` a map in `Fib p R`. -/
lemma fiber_factorization (ha : p.obj a = S) {b : Fib p R} (f : R âŸ¶ S) (Ï† : (Î¹ R).obj b âŸ¶ a)
    [IsHomLift p f Ï†] : âˆƒ (b' : Fib p R) (Ï„ : b âŸ¶ b') (Ïˆ : (Î¹ R).obj b' âŸ¶ a),
      IsStronglyCartesian p f Ïˆ âˆ§ (((Î¹ R).map Ï„) â‰« Ïˆ = Ï†) :=
  let Ïˆ := pullbackMap f ha
  âŸ¨mkPullback f ha, inducedMap f Ïˆ Ï†, Ïˆ, inferInstance, inducedMap_comp f Ïˆ Ï†âŸ©

end

end

end HasFibers
