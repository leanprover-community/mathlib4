/-
Copyright (c) 2025 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle SÃ¶nne, Fernando Chu, Christian Merten
-/

import Mathlib.CategoryTheory.Bicategory.Grothendieck
import Mathlib.CategoryTheory.FiberedCategory.HasFibers

/-!
# The Grothendieck construction gives a fibered category

In this file we show that the Grothendieck construction of a pseudofunctor `F`
gives a fibered category over the base category.

We also provide a `HasFibers` instance to `âˆ« F`, such that the fiber over `S` is the
category `F(S)`.

## References
[Vistoli2008] "Notes on Grothendieck Topologies, Fibered Categories and Descent Theory" by
Angelo Vistoli

-/

namespace CategoryTheory.Pseudofunctor.Grothendieck

open Functor Opposite Bicategory

variable {ğ’® : Type*} [Category ğ’®] {F : Pseudofunctor (LocallyDiscrete ğ’®áµ’áµ–) Cat}

section

variable {R S : ğ’®} (a : F.obj âŸ¨op SâŸ©) (f : R âŸ¶ S)

/-- The domain of the cartesian lift of `f`. -/
abbrev domainCartesianLift : âˆ« F := âŸ¨R, (F.map f.op.toLoc).obj aâŸ©

/-- The cartesian lift of `f`. -/
abbrev cartesianLift : domainCartesianLift a f âŸ¶ âŸ¨S, aâŸ© := âŸ¨f, ğŸ™ _âŸ©

instance isHomLift_cartesianLift : IsHomLift (forget F) f (cartesianLift a f) :=
  IsHomLift.map (forget F) (cartesianLift a f)

variable {a} in
/-- Given some lift `g` of `f`, the canonical map from the domain of `g` to the domain of
the cartesian lift of `f`. -/
abbrev homCartesianLift {a' : âˆ« F} (g : a'.1 âŸ¶ R) (Ï†' : a' âŸ¶ âŸ¨S, aâŸ©)
    [IsHomLift (forget F) (g â‰« f) Ï†'] : a' âŸ¶ domainCartesianLift a f where
  base := g
  fiber :=
    have : Ï†'.base = g â‰« f := by simpa using IsHomLift.fac' (forget F) (g â‰« f) Ï†'
    Ï†'.fiber â‰« eqToHom (by simp [this]) â‰« (F.mapComp f.op.toLoc g.op.toLoc).hom.app a

instance isHomLift_homCartesianLift {a' : âˆ« F} {Ï†' : a' âŸ¶ âŸ¨S, aâŸ©} {g : a'.1 âŸ¶ R}
    [IsHomLift (forget F) (g â‰« f) Ï†'] : IsHomLift (forget F) g (homCartesianLift f g Ï†') :=
  IsHomLift.map (forget F) (homCartesianLift f g Ï†')

lemma isStronglyCartesian_homCartesianLift :
    IsStronglyCartesian (forget F) f (cartesianLift a f) where
  universal_property' {a'} g Ï†' hÏ†' := by
    refine âŸ¨homCartesianLift f g Ï†', âŸ¨inferInstance, ?_âŸ©, ?_âŸ©
    Â· exact Hom.ext _ _ (by simpa using IsHomLift.fac (forget F) (g â‰« f) Ï†') (by simp)
    rintro Ï‡' âŸ¨hÏ‡'.symm, rflâŸ©
    obtain âŸ¨rflâŸ© : g = Ï‡'.1 := by simpa using IsHomLift.fac (forget F) g Ï‡'
    ext <;> simp

end

/-- `forget F : âˆ« F â¥¤ ğ’®` is a fibered category. -/
instance : IsFibered (forget F) :=
  IsFibered.of_exists_isStronglyCartesian (fun a _ f â†¦
    âŸ¨domainCartesianLift a.2 f, cartesianLift a.2 f, isStronglyCartesian_homCartesianLift a.2 fâŸ©)

variable (F) (S : ğ’®)

/-- The inclusion map from `F(S)` into `âˆ« F`. -/
@[simps]
def Î¹ : F.obj âŸ¨op SâŸ© â¥¤ âˆ« F where
  obj a := { base := S, fiber := a}
  map {a b} Ï† := { base := ğŸ™ S, fiber := Ï† â‰« (F.mapId âŸ¨op SâŸ©).inv.app b}
  map_comp {a b c} Ï† Ïˆ := by
    ext
    Â· simp
    Â· simp [â† (F.mapId âŸ¨op SâŸ©).inv.naturality_assoc Ïˆ, F.whiskerRight_mapId_inv_app,
        Strict.leftUnitor_eqToIso, Strict.rightUnitor_eqToIso]

/-- The natural isomorphism encoding `comp_const`. -/
@[simps!]
def compIso : (Î¹ F S) â‹™ forget F â‰… (const (F.obj âŸ¨op SâŸ©)).obj S :=
  NatIso.ofComponents (fun a => eqToIso rfl)

lemma comp_const : (Î¹ F S) â‹™ forget F = (const (F.obj âŸ¨op SâŸ©)).obj S := by
  apply Functor.ext_of_iso (compIso F S) <;> simp

noncomputable instance : Functor.Full (Fiber.inducedFunctor (comp_const F S)) where
  map_surjective {X Y} f := by
    have := f.2 -- TODO: synthesize this
    have hf : (Fiber.fiberInclusion.map f).base = ğŸ™ S := by
      simpa using (IsHomLift.fac (forget F) (ğŸ™ S) f.1).symm
    use (Fiber.fiberInclusion.map f).2 â‰« eqToHom ?_ â‰« (F.mapId âŸ¨op SâŸ©).hom.app Y
    rotate_left
    -- TODO: more simp lemmas, should not need this...
    Â· simp [Fiber.inducedFunctor, hf]
      simp [Fiber.fiberInclusion]
    ext
    Â· simp [Fiber.inducedFunctor, hf]
      simp [Fiber.fiberInclusion]
    Â· simp

instance : Functor.Faithful (Fiber.inducedFunctor (comp_const F S)) where
  map_injective := by
    intros a b f g heq
    -- can be made a one liner...
    rw [â† Subtype.val_inj] at heq
    simp only [Fiber.inducedFunctor] at heq -- TODO...
    obtain âŸ¨_, heqâ‚‚âŸ© := (Hom.ext_iff _ _).1 heq
    simpa [cancel_mono] using heqâ‚‚

noncomputable instance : Functor.EssSurj (Fiber.inducedFunctor (comp_const F S)) := by
  apply essSurj_of_surj
  intro Y
  simp only [Fiber.inducedFunctor] -- TODO...
  have hYS : S = Y.1.1 := by simpa using Y.2.symm
  use (hYS.symm â–¸ Y.1.2)
  apply Subtype.val_inj.1
  ext <;> simp [hYS]

noncomputable instance : Functor.IsEquivalence (Fiber.inducedFunctor (comp_const F S)) where

noncomputable instance : HasFibers (forget F) where
  Fib S := F.obj âŸ¨op SâŸ©
  Î¹ := Î¹ F
  comp_const := comp_const F

end CategoryTheory.Pseudofunctor.Grothendieck
