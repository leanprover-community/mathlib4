/-
Copyright (c) 2024 Paul Lezeau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Lezeau, Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.FiberedCategory.Cartesian

/-!

# Fibered categories

This file defines what it means for a functor `p : ğ’³ â¥¤ ğ’®` to be (pre)fibered.

## Main definitions

- `IsPreFibered p` expresses `ğ’³` is fibered over `ğ’®` via a functor `p : ğ’³ â¥¤ ğ’®`, as in SGA VI.6.1.
This means that any morphism in the base `ğ’®` can be lifted to a cartesian morphism in `ğ’³`.

- `IsFibered p` expresses `ğ’³` is fibered over `ğ’®` via a functor `p : ğ’³ â¥¤ ğ’®`, as in SGA VI.6.1.
This means that it is prefibered, and that the composition of any two cartesian morphisms is
cartesian.

In the literature one often sees the notion of a fibered category defined as the existence of
strongly cartesian morphisms lying over any given morphism in the base. This is equivalent to the
notion above, and we give an alternate constructor `IsFibered.of_exists_isCartesian'` for
constructing a fibered category this way.

## Implementation

The constructor of `IsPreFibered` is called `exists_isCartesian'`. The reason for the prime is that
when wanting to apply this condition, it is recommended to instead use the lemma
`exists_isCartesian` (without the prime), which is more applicable with respect to non-definitional
equalities.

## References
* [A. Grothendieck, M. Raynaud, *SGA 1*](https://arxiv.org/abs/math/0206203)

-/

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

namespace CategoryTheory

open Functor Category IsHomLift

variable {ğ’® : Type uâ‚} {ğ’³ : Type uâ‚‚} [Category.{vâ‚} ğ’®] [Category.{vâ‚‚} ğ’³]

/-- Definition of a prefibered category.

See SGA 1 VI.6.1. -/
class Functor.IsPreFibered (p : ğ’³ â¥¤ ğ’®) : Prop where
  exists_isCartesian' {a : ğ’³} {R : ğ’®} (f : R âŸ¶ p.obj a) : âˆƒ (b : ğ’³) (Ï† : b âŸ¶ a), IsCartesian p f Ï†

protected lemma IsPreFibered.exists_isCartesian (p : ğ’³ â¥¤ ğ’®) [p.IsPreFibered] {a : ğ’³} {R S : ğ’®}
    (ha : p.obj a = S) (f : R âŸ¶ S) : âˆƒ (b : ğ’³) (Ï† : b âŸ¶ a), IsCartesian p f Ï† := by
  subst ha; exact IsPreFibered.exists_isCartesian' f

/-- Definition of a fibered category.

See SGA 1 VI.6.1. -/
class Functor.IsFibered (p : ğ’³ â¥¤ ğ’®) : Prop extends IsPreFibered p where
  comp {R S T : ğ’®} (f : R âŸ¶ S) (g : S âŸ¶ T) {a b c : ğ’³} (Ï† : a âŸ¶ b) (Ïˆ : b âŸ¶ c)
    [IsCartesian p f Ï†] [IsCartesian p g Ïˆ] : IsCartesian p (f â‰« g) (Ï† â‰« Ïˆ)

instance (p : ğ’³ â¥¤ ğ’®) [p.IsFibered] {R S T : ğ’®} (f : R âŸ¶ S) (g : S âŸ¶ T) {a b c : ğ’³} (Ï† : a âŸ¶ b)
    (Ïˆ : b âŸ¶ c) [IsCartesian p f Ï†] [IsCartesian p g Ïˆ] : IsCartesian p (f â‰« g) (Ï† â‰« Ïˆ) :=
  IsFibered.comp f g Ï† Ïˆ

namespace Functor.IsPreFibered

variable {p : ğ’³ â¥¤ ğ’®} [IsPreFibered p] {R S : ğ’®} {a : ğ’³} (ha : p.obj a = S) (f : R âŸ¶ S)

/-- Given a fibered category `p : ğ’³ â¥¤ ğ’«`, a morphism `f : R âŸ¶ S` and an object `a` lying over `S`,
then `pullbackObj` is the domain of some choice of a cartesian morphism lying over `f` with
codomain `a`. -/
noncomputable def pullbackObj : ğ’³ :=
  Classical.choose (IsPreFibered.exists_isCartesian p ha f)

/-- Given a fibered category `p : ğ’³ â¥¤ ğ’«`, a morphism `f : R âŸ¶ S` and an object `a` lying over `S`,
then `pullbackMap` is a choice of a cartesian morphism lying over `f` with codomain `a`. -/
noncomputable def pullbackMap : pullbackObj ha f âŸ¶ a :=
  Classical.choose (Classical.choose_spec (IsPreFibered.exists_isCartesian p ha f))

instance pullbackMap.IsCartesian : IsCartesian p f (pullbackMap ha f) :=
  Classical.choose_spec (Classical.choose_spec (IsPreFibered.exists_isCartesian p ha f))

lemma pullbackObj_proj : p.obj (pullbackObj ha f) = R :=
  domain_eq p f (pullbackMap ha f)

end Functor.IsPreFibered

namespace Functor.IsFibered

open IsCartesian IsPreFibered

/-- In a fibered category, any cartesian morphism is strongly cartesian. -/
instance isStronglyCartesian_of_isCartesian (p : ğ’³ â¥¤ ğ’®) [p.IsFibered] {R S : ğ’®} (f : R âŸ¶ S)
    {a b : ğ’³} (Ï† : a âŸ¶ b) [p.IsCartesian f Ï†] : p.IsStronglyCartesian f Ï† where
  universal_property' g Ï†' hÏ†' := by
    -- Let `Ïˆ` be a cartesian arrow lying over `g`
    let Ïˆ := pullbackMap (domain_eq p f Ï†) g
    -- Let `Ï„` be the map induced by the universal property of `Ïˆ â‰« Ï†`.
    let Ï„ := IsCartesian.map p (g â‰« f) (Ïˆ â‰« Ï†) Ï†'
    use Ï„ â‰« Ïˆ
    -- It is easily verified that `Ï„ â‰« Ïˆ` lifts `g` and `Ï„ â‰« Ïˆ â‰« Ï† = Ï†'`
    refine âŸ¨âŸ¨inferInstance, by simp only [assoc, IsCartesian.fac, Ï„]âŸ©, ?_âŸ©
    -- It remains to check that `Ï„ â‰« Ïˆ` is unique.
    -- So fix another lift `Ï€` of `g` satisfying `Ï€ â‰« Ï† = Ï†'`.
    intro Ï€ âŸ¨hÏ€, hÏ€_compâŸ©
    -- Write `Ï€` as `Ï€ = Ï„' â‰« Ïˆ` for some `Ï„'` induced by the universal property of `Ïˆ`.
    rw [â† fac p g Ïˆ Ï€]
    -- It remains to show that `Ï„' = Ï„`. This follows again from the universal property of `Ïˆ`.
    congr 1
    apply map_uniq
    rwa [â† assoc, IsCartesian.fac]

/-- In a category which admits strongly cartesian pullbacks, any cartesian morphism is
strongly cartesian. This is a helper-lemma for the fact that admitting strongly cartesian pullbacks
implies being fibered. -/
lemma isStronglyCartesian_of_exists_isCartesian (p : ğ’³ â¥¤ ğ’®) (h : âˆ€ (a : ğ’³) (R : ğ’®)
    (f : R âŸ¶ p.obj a), âˆƒ (b : ğ’³) (Ï† : b âŸ¶ a), IsStronglyCartesian p f Ï†) {R S : ğ’®} (f : R âŸ¶ S)
      {a b : ğ’³} (Ï† : a âŸ¶ b) [p.IsCartesian f Ï†] : p.IsStronglyCartesian f Ï† := by
  constructor
  intro c g Ï†' hÏ†'
  subst_hom_lift p f Ï†; clear a b R S
  -- Let `Ïˆ` be a cartesian arrow lying over `g`
  obtain âŸ¨a', Ïˆ, hÏˆâŸ© := h _ _ (p.map Ï†)
  -- Let `Ï„' : c âŸ¶ a'` be the map induced by the universal property of `Ïˆ`
  let Ï„' := IsStronglyCartesian.map p (p.map Ï†) Ïˆ (f':= g â‰« p.map Ï†) rfl Ï†'
  -- Let `Î¦ : a' â‰… a` be natural isomorphism induced between `Ï†` and `Ïˆ`.
  let Î¦ := domainUniqueUpToIso p (p.map Ï†) Ï† Ïˆ
  -- The map induced by `Ï†` will be `Ï„' â‰« Î¦.hom`
  use Ï„' â‰« Î¦.hom
  -- It is easily verified that `Ï„' â‰« Î¦.hom` lifts `g` and `Ï„' â‰« Î¦.hom â‰« Ï† = Ï†'`
  refine âŸ¨âŸ¨by simp only [Î¦]; infer_instance, ?_âŸ©, ?_âŸ©
  Â· simp [Ï„', Î¦]
  -- It remains to check that it is unique. This follows from the universal property of `Ïˆ`.
  intro Ï€ âŸ¨hÏ€, hÏ€_compâŸ©
  rw [â† Iso.comp_inv_eq]
  apply IsStronglyCartesian.map_uniq p (p.map Ï†) Ïˆ rfl Ï†'
  simp [hÏ€_comp, Î¦]

/-- Alternate constructor for `IsFibered`, a functor `p : ğ’³ â¥¤ ğ’´` is fibered if any diagram of the
form
```
          a
          -
          |
          v
R --f--> p(a)
```
admits a strongly cartesian lift `b âŸ¶ a` of `f`. -/
lemma of_exists_isStronglyCartesian {p : ğ’³ â¥¤ ğ’®}
    (h : âˆ€ (a : ğ’³) (R : ğ’®) (f : R âŸ¶ p.obj a),
      âˆƒ (b : ğ’³) (Ï† : b âŸ¶ a), IsStronglyCartesian p f Ï†) :
    IsFibered p where
  exists_isCartesian' := by
    intro a R f
    obtain âŸ¨b, Ï†, hÏ†âŸ© := h a R f
    refine âŸ¨b, Ï†, inferInstanceâŸ©
  comp := fun R S T f g {a b c} Ï† Ïˆ _ _ =>
    have : p.IsStronglyCartesian f Ï† := isStronglyCartesian_of_exists_isCartesian p h _ _
    have : p.IsStronglyCartesian g Ïˆ := isStronglyCartesian_of_exists_isCartesian p h _ _
    inferInstance

/-- Given a diagram
```
                  a
                  -
                  |
                  v
T --g--> R --f--> S
```
we have an isomorphism `T Ã—_S a â‰… T Ã—_R (R Ã—_S a)` -/
noncomputable def pullbackPullbackIso {p : ğ’³ â¥¤ ğ’®} [IsFibered p]
    {R S T : ğ’®} {a : ğ’³} (ha : p.obj a = S) (f : R âŸ¶ S) (g : T âŸ¶ R) :
      pullbackObj ha (g â‰« f) â‰… pullbackObj (pullbackObj_proj ha f) g :=
  domainUniqueUpToIso p (g â‰« f) (pullbackMap (pullbackObj_proj ha f) g â‰« pullbackMap ha f)
    (pullbackMap ha (g â‰« f))

end Functor.IsFibered

end CategoryTheory
