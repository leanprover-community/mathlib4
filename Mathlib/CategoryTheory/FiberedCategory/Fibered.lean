/-
Copyright (c) 2024 Paul Lezeau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Lezeau, Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.FiberedCategory.HomLift

/-!

# Fibered categories

This file defines what it means for a functor `p : ğ’³ â¥¤ ğ’®` to be fibered`.

## Main definitions

- `IsCartesian p f Ï†` expresses that `Ï†` is a cartesian arrow lying over `f` with respect to `p`.
This structure extends `IsHomLift p f Ï†`.
- `IsFibered p` expresses that `p` gives `ğ’³` the structure of a fibered category over `ğ’®`, i.e.
that for every morphism `f : S âŸ¶ R` in `ğ’®` and every object `b` in `ğ’³` with `p(b)=R` there is a
cartesian arrow `Ï† : a âŸ¶ b`  of `f`.

## Implementation
The standard constructors of `IsCartesian` and `IsFibered` have both been renamed to `.mk'`. We have
provided alternate lemmas `IsCartesian.mk` and `IsFibered.mk` for constructing instances of these
structures, and it is recommended to use these instead to minimize the amount of equalities that
needs to be carried around in the construction.

The reason for this is the following:
Just like `IsHomLift p f Ï†`, we have phrased `IsCartesian p f Ï†` in a way to make its usage as
flexible  as possible with respect to non-definitional equalities of domains / codomains.
In particular, given a lift
```
  a --Ï†--> b
  -        -
  |        |
  v        v
  R --f--> S
```
(by which we mean an object of `IsHomLift p f Ï†`). We say that it is cartesian if for all arrows
`g : R' âŸ¶ R`, and all lifts
```
  a' --Ï†'--> b
  -          -
  |          |
  v          v
  R' --f'--> S
```
such that `f' = g â‰« f`, there is a unique induced map `Ï„ : a' âŸ¶ a` lifting `g` and such that
`Ï„ â‰« Ï† = Ï†'`. This definition gives us some flexibility in that it allows us to take `f'` to be
non-definitionally equal to `g â‰« f`, and `p(a')` to be non-definitionally equal to `R'`.

Similarly, `IsFibered p` is phrased as saying that for every `f : R âŸ¶ S`, and every `a` such that
`p(a)=S`, there is a cartesian arrow `Ï†` lying over `f`. The alternate constructor `IsFibered.mk`
only requires us to construct this arrow for every `a` and every `f : R âŸ¶ p(a)`.
-/

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

open CategoryTheory Functor Category IsHomLift

variable {ğ’® : Type uâ‚} {ğ’³ : Type uâ‚‚} [Category.{vâ‚} ğ’®] [Category.{vâ‚‚} ğ’³]

/-- The proposition that a lift
```
  a --Ï†--> b
  -        -
  |        |
  v        v
  R --f--> S
```
is a cartesian arrow.
-/
structure IsCartesian (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b) extends
    IsHomLift p f Ï† : Prop where mk' ::
  (UniversalProperty {R' : ğ’®} {a' : ğ’³} {g : R' âŸ¶ R} {f' : R' âŸ¶ S}
    (_ : f' = g â‰« f) {Ï†' : a' âŸ¶ b} (_ : IsHomLift p f' Ï†') :
      âˆƒ! Ï‡ : a' âŸ¶ a, IsHomLift p g Ï‡ âˆ§ Ï‡ â‰« Ï† = Ï†')

/-- Definition of a Fibered category. -/
class IsFibered (p : ğ’³ â¥¤ ğ’®) : Prop where mk' ::
  (has_pullbacks {a : ğ’³} {R S : ğ’®} (_ : p.obj a = S) (f : R âŸ¶ S) :
    âˆƒ (b : ğ’³) (Ï† : b âŸ¶ a), IsCartesian p f Ï†)

protected lemma IsCartesian.mk {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : b âŸ¶ a}
    (hÏ† : IsHomLift p f Ï†) (h : âˆ€ (a' : ğ’³) (g : p.obj a' âŸ¶ R) (Ï†' : a' âŸ¶ a),
      IsHomLift p (g â‰« f) Ï†' â†’ âˆƒ! Ï‡ : a' âŸ¶ b, IsHomLift p g Ï‡ âˆ§ Ï‡ â‰« Ï† = Ï†') :
        IsCartesian p f Ï† where
  toIsHomLift := hÏ†
  UniversalProperty := by
    intro R' a' g f' hf' Ï†' hÏ†'
    have := hÏ†'.ObjLiftDomain.symm
    subst this
    subst hf'
    apply h a' g Ï†' hÏ†'

protected lemma IsFibered.mk {p : ğ’³ â¥¤ ğ’®} (h : âˆ€ (a : ğ’³) (R : ğ’®) (f : R âŸ¶ p.obj a),
    âˆƒ (b : ğ’³) (Ï† : b âŸ¶ a), IsCartesian p f Ï†) : IsFibered p where
  has_pullbacks := @fun a R S ha f => by subst ha; apply h a R f

namespace IsCartesian

/-- Given a diagram:
```
a'        a --Ï†--> b
|         |        |
v         v        v
R' --g--> R --f--> S
```
such that `Ï†` is a cartesian arrow, and an arrow `Ï†' : a' âŸ¶ b`,
the induced map is the map `a' âŸ¶ a` obtained from the
universal property of `Ï†`. -/
noncomputable def InducedMap {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a âŸ¶ b}
    (hÏ† : IsCartesian p f Ï†) {R' : ğ’®} {a' : ğ’³} {g : R' âŸ¶ R} {f' : R' âŸ¶ S} (hf' : f' = g â‰« f)
    {Ï†' : a' âŸ¶ b} (hÏ†' : IsHomLift p f' Ï†') : a' âŸ¶ a :=
  Classical.choose $ hÏ†.UniversalProperty hf' hÏ†'

lemma InducedMap_IsHomLift {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a âŸ¶ b}
    (hÏ† : IsCartesian p f Ï†) {R' : ğ’®} {a' : ğ’³} {g : R' âŸ¶ R} {f' : R' âŸ¶ S} (hf' : f' = g â‰« f)
    {Ï†' : a' âŸ¶ b} (hÏ†' : IsHomLift p f' Ï†') : IsHomLift p g (InducedMap hÏ† hf' hÏ†') :=
  (Classical.choose_spec (hÏ†.UniversalProperty hf' hÏ†')).1.1

@[simp]
lemma InducedMap_Diagram {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a âŸ¶ b}
    (hÏ† : IsCartesian p f Ï†) {R' : ğ’®} {a' : ğ’³} {g : R' âŸ¶ R} {f' : R' âŸ¶ S} (hf' : f' = g â‰« f)
    {Ï†' : a' âŸ¶ b} (hÏ†' : IsHomLift p f' Ï†') : (InducedMap hÏ† hf' hÏ†') â‰« Ï† = Ï†' :=
  (Classical.choose_spec (hÏ†.UniversalProperty hf' hÏ†')).1.2

/-- Given a diagram:
```
a'        a --Ï†--> b
|         |        |
v         v        v
R' --g--> R --f--> S
```
with `Ï†` a cartesian arrow. Then for any arrow `Ï†' : a' âŸ¶ b`, and `Ïˆ : a' âŸ¶ a` such that
`g â‰« Ïˆ = Ï†'`. Then `Ïˆ` is the map induced by the universal property. -/
lemma InducedMap_unique {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a âŸ¶ b}
    (hÏ† : IsCartesian p f Ï†) {R' : ğ’®} {a' : ğ’³} {g : R' âŸ¶ R} {f' : R' âŸ¶ S} (hf' : f' = g â‰« f)
    {Ï†' : a' âŸ¶ b} (hÏ†' : IsHomLift p f' Ï†') {Ïˆ : a' âŸ¶ a} (hÏˆ : IsHomLift p g Ïˆ)
    (hcomp : Ïˆ â‰« Ï† = Ï†') : Ïˆ = InducedMap hÏ† hf' hÏ†' :=
  (Classical.choose_spec (hÏ†.UniversalProperty hf' hÏ†')).2 Ïˆ âŸ¨hÏˆ, hcompâŸ©

/-- Given a diagram:
```
a'        a --Ï†--> b
|         |        |
v         v        v
R' --g--> R --f--> S
```
with `Ï†` a cartesian arrow. Then for any arrow `Ï†' : a' âŸ¶ b`, any two arrows `Ïˆ Ïˆ' : a' âŸ¶ a` such
that `g â‰« Ïˆ = Ï†' = g â‰« Ïˆ'`. Then `Ïˆ = Ïˆ'`. -/
protected lemma uniqueness {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a âŸ¶ b}
    (hÏ† : IsCartesian p f Ï†) {R' : ğ’®} {a' : ğ’³} {g : R' âŸ¶ R} {f' : R' âŸ¶ S} (hf' : f' = g â‰« f)
    {Ï†' : a' âŸ¶ b} (hÏ†' : IsHomLift p f' Ï†') {Ïˆ Ïˆ' : a' âŸ¶ a} (hÏˆ : IsHomLift p g Ïˆ)
    (hÏˆ' : IsHomLift p g Ïˆ') (hcomp : Ïˆ â‰« Ï† = Ï†') (hcomp' : Ïˆ' â‰« Ï† = Ï†') : Ïˆ = Ïˆ' := by
  rw [InducedMap_unique hÏ† hf' hÏ†' hÏˆ hcomp, InducedMap_unique hÏ† hf' hÏ†' hÏˆ' hcomp']

@[simp]
lemma InducedMap_self_eq_id {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a âŸ¶ b}
    (hÏ† : IsCartesian p f Ï†) : InducedMap hÏ† (id_comp f).symm hÏ†.toIsHomLift = ğŸ™ a:=
  (InducedMap_unique hÏ† (id_comp f).symm hÏ†.toIsHomLift (IsHomLift.id hÏ†.ObjLiftDomain)
    (id_comp _)).symm

/- The composition of two induced maps is also an induced map.
Given a diagrams
```
a''         a'        a --Ï†--> b          a' --Ï†'--> b          a'' --Ï†''--> b
|           |         |        |    and   |          |    and   |            |
v           v         v        v          v          v          v            v
R'' --h'--> R' --h--> R --f--> S          R' --f'--> S          R'' --f''--> S
```
such that `Ï†` and `Ï†'` are cartesian arrows. Composing the induced map from `a'' âŸ¶ a'` with the
induced map from `a' âŸ¶ a` gives the induced map from `a'' âŸ¶ a`. -/
@[simp]
lemma InducedMap_comp {p : ğ’³ â¥¤ ğ’®} {R R' R'' S: ğ’®} {a a' a'' b : ğ’³}
    {f : R âŸ¶ S} {f' : R' âŸ¶ S} {f'' : R'' âŸ¶ S} {g : R' âŸ¶ R} {h : R'' âŸ¶ R'}
    (H : f' = g â‰« f) (H' : f'' = h â‰« f') {Ï† : a âŸ¶ b} {Ï†' : a' âŸ¶ b} {Ï†'' : a'' âŸ¶ b}
    (hÏ† : IsCartesian p f Ï†) (hÏ†' : IsCartesian p f' Ï†') (hÏ†'' : IsHomLift p f'' Ï†'') :
    InducedMap hÏ†' H' hÏ†'' â‰« InducedMap hÏ† H hÏ†'.toIsHomLift
      = InducedMap hÏ† (show f'' = (h â‰« g) â‰« f by rwa [assoc, â† H]) hÏ†'' := by
  apply InducedMap_unique
  Â· apply IsHomLift.comp
    apply InducedMap_IsHomLift
    apply InducedMap_IsHomLift
  Â· simp only [assoc, InducedMap_Diagram]

/-- Given two cartesian arrows `Ï†`, `Ïˆ` as follows
```
a --Ï†--> b --Ïˆ--> c
|        |        |
v        v        v
R --f--> S --g--> T
```
Then the composite `Ï† â‰« Ïˆ` is also cartesian. -/
protected lemma comp {p : ğ’³ â¥¤ ğ’®} {R S T : ğ’®} {a b c: ğ’³} {f : R âŸ¶ S} {g : S âŸ¶ T} {Ï† : a âŸ¶ b}
    {Ïˆ : b âŸ¶ c} (hÏ† : IsCartesian p f Ï†) (hÏˆ : IsCartesian p g Ïˆ) :
      IsCartesian p (f â‰« g) (Ï† â‰« Ïˆ) := by
  apply IsCartesian.mk
  Â· apply IsHomLift.comp hÏ†.toIsHomLift hÏˆ.toIsHomLift
  Â· intro a' h Ï„ hÏ„
    use InducedMap hÏ† rfl (InducedMap_IsHomLift hÏˆ rfl ((assoc h f g).symm â–¸ hÏ„))
    refine âŸ¨âŸ¨InducedMap_IsHomLift hÏ† rfl _, ?_âŸ©, ?_âŸ©
    Â· rw [â† assoc, (InducedMap_Diagram hÏ† rfl _), (InducedMap_Diagram hÏˆ rfl _)]
    Â· intro Ï€' hÏ€'
      apply InducedMap_unique hÏ† _ _ hÏ€'.1
      apply InducedMap_unique hÏˆ _ _ (IsHomLift.comp hÏ€'.1 hÏ†.toIsHomLift)
      simp only [assoc, hÏ€'.2]

/-- Given two commutative squares
```
a --Ï†--> b --Ïˆ--> c
|        |        |
v        v        v
R --f--> S --g--> T
```
such that the composite `Ï† â‰« Ïˆ` and `Ïˆ` are cartesian, then so is `Ï†`. -/
protected lemma of_comp {p : ğ’³ â¥¤ ğ’®} {R S T : ğ’®} {a b c: ğ’³} {f : R âŸ¶ S} {g : S âŸ¶ T}
    {Ï† : a âŸ¶ b} {Ïˆ : b âŸ¶ c} (hÏˆ : IsCartesian p g Ïˆ) (hcomp : IsCartesian p (f â‰« g) (Ï† â‰« Ïˆ))
    (hÏ† : IsHomLift p f Ï†) : IsCartesian p f Ï† := by
  apply IsCartesian.mk hÏ†
  -- Fix a morphism `Ï„ : a' âŸ¶ b` and a morphism `h : p(a') âŸ¶ R` such that `Ï„` lifts `h â‰« f`
  intro a' h Ï„ hÏ„
  have hâ‚ : IsHomLift p (h â‰« f â‰« g) (Ï„ â‰« Ïˆ) := by simpa using IsHomLift.comp hÏ„ hÏˆ.toIsHomLift
  -- We get a morphism `Ï€ : a' âŸ¶ a` from the universal property of `Ï† â‰« Ïˆ`
  use InducedMap hcomp rfl hâ‚
  refine âŸ¨âŸ¨InducedMap_IsHomLift hcomp rfl hâ‚, ?_âŸ©,?_âŸ©
  -- The fact that `Ï€ â‰« Ï† = Ï„` follows from `Ï€ â‰« Ï† â‰« Ïˆ = Ï„ â‰« Ïˆ` and the universal property of `Ïˆ`
  Â· apply IsCartesian.uniqueness hÏˆ rfl (by rwa [assoc]) _ hÏ„ _ rfl
    Â· apply IsHomLift.comp (InducedMap_IsHomLift hcomp rfl hâ‚) hÏ†
    Â· rw [assoc, (InducedMap_Diagram hcomp rfl hâ‚)]
  -- Finally, uniqueness of `Ï€` comes from the universal property of `Ï† â‰« Ïˆ`
  intro Ï€' hÏ€'
  apply InducedMap_unique _ _ _ hÏ€'.1 (by rw [â† hÏ€'.2, assoc])

lemma of_iso {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a â‰… b} (hÏ† : IsHomLift p f Ï†.hom) :
    IsCartesian p f Ï†.hom := by
  apply IsCartesian.mk hÏ†
  intro a' g Ï„ hÏ„
  use Ï„ â‰« Ï†.inv
  refine âŸ¨?_, by aesop_catâŸ©
  simpa using IsHomLift.comp hÏ„ <| IsHomLift.inv_lift_inv' hÏ†

lemma of_isIso {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³}
    {f : R âŸ¶ S} {Ï† : a âŸ¶ b} (hÏ† : IsHomLift p f Ï†) [IsIso Ï†] : IsCartesian p f Ï† :=
  IsCartesian.of_iso (Ï† := asIso Ï†) hÏ†

/-- A cartesian arrow lying over an isomorphism is an isomorphism. -/
lemma isIso_of_base_isIso {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a âŸ¶ b}
    (hÏ† : IsCartesian p f Ï†) (hf : IsIso f) : IsIso Ï† := by
  -- The inverse will be given by applying the universal property to the arrows fâ»Â¹ : S âŸ¶ R and ğŸ™ b
  let Ï†' := InducedMap hÏ† (IsIso.inv_hom_id f).symm (IsHomLift.id hÏ†.ObjLiftCodomain)
  use Ï†'
  -- `Ï†' â‰« Ï† = ğŸ™ b` follows immediately from the universal property
  have inv_hom : Ï†' â‰« Ï† = ğŸ™ b := InducedMap_Diagram hÏ† (IsIso.inv_hom_id f).symm
    (IsHomLift.id hÏ†.ObjLiftCodomain)
  refine âŸ¨?_, inv_homâŸ©
  -- We now show that `Ï† â‰« Ï†' = ğŸ™ a` by applying the universal property of `Ï†` to the equality
  -- `Ï† â‰« Ï†' â‰« Ï† = Ï† â‰« ğŸ™ b = ğŸ™ a â‰« Ï†`
  have hâ‚ : IsHomLift p (ğŸ™ R) (Ï†  â‰« Ï†') := by
    rw [â† IsIso.hom_inv_id f]
    apply IsHomLift.comp hÏ†.toIsHomLift
    apply InducedMap_IsHomLift
  have hâ‚‚ : IsHomLift p f (Ï† â‰« Ï†' â‰« Ï†) := by simpa using IsHomLift.comp hâ‚ hÏ†.toIsHomLift
  apply IsCartesian.uniqueness hÏ† (id_comp f).symm hâ‚‚ hâ‚ (IsHomLift.id hÏ†.ObjLiftDomain)
  Â· apply Category.assoc
  Â· simp only [inv_hom, id_comp, comp_id]

/-- The canonical isomorphism between the domains of two cartesian arrows lying over
isomorphic objects. -/
noncomputable def Iso_of_base_Iso {p : ğ’³ â¥¤ ğ’®}
  {R R' S : ğ’®} {a a' b : ğ’³} {f : R âŸ¶ S} {f' : R' âŸ¶ S} {g : R' â‰… R}
  (H : f' = g.hom â‰« f) {Ï† : a âŸ¶ b} {Ï†' : a' âŸ¶ b}
  (hÏ† : IsCartesian p f Ï†) (hÏ†' : IsCartesian p f' Ï†') : a' â‰… a where
    hom := InducedMap hÏ† H hÏ†'.toIsHomLift
    inv := InducedMap hÏ†' (congrArg (g.inv â‰« Â·) H.symm) (by simpa using hÏ†.toIsHomLift)

/-- The canonical isomorphism between the domains of two cartesian arrows
lying over the same object. -/
noncomputable def NaturalIso {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a' a b : ğ’³} {f : R âŸ¶ S} {Ï† : a âŸ¶ b}
    {Ï†' : a' âŸ¶ b} (hÏ† : IsCartesian p f Ï†) (hÏ†' : IsCartesian p f Ï†') : a' â‰… a :=
  Iso_of_base_Iso (show f = (Iso.refl R).hom â‰« f by simp) hÏ† hÏ†'

end IsCartesian

namespace IsFibered

open IsCartesian

/-- Given a fibered category `p : ğ’³ â¥¤ ğ’«`, and a diagram
```
           a
           -
           |
           v
  R --f--> S
```
`PullbackObj` is defined as the domain `R Ã—_S a` of some cartesian arrow lying over
`f`, which exists by the fibered category structure on `p`. -/
noncomputable def PullbackObj {p : ğ’³ â¥¤ ğ’®} [hp : IsFibered p] {R S : ğ’®}
    {a : ğ’³} (ha : p.obj a = S) (f : R âŸ¶ S) : ğ’³ :=
  Classical.choose (hp.1 ha f)

/-- Given a fibered category `p : ğ’³ â¥¤ ğ’«`, and a diagram
```
          a
          -
          |
          v
R --f--> S
```
we get a map `R Ã—_S b âŸ¶ a` -/
noncomputable def PullbackMap {p : ğ’³ â¥¤ ğ’®} [hp : IsFibered p] {R S : ğ’®}
    {a : ğ’³} (ha : p.obj a = S) (f : R âŸ¶ S) : PullbackObj ha f âŸ¶ a :=
  Classical.choose (Classical.choose_spec (hp.1 ha f))

lemma PullbackMap.IsCartesian {p : ğ’³ â¥¤ ğ’®} [hp : IsFibered p] {R S : ğ’®}
    {a : ğ’³} (ha : p.obj a = S) (f : R âŸ¶ S) : IsCartesian p f (PullbackMap ha f) :=
  Classical.choose_spec (Classical.choose_spec (hp.1 ha f))

lemma PullbackObj_proj {p : ğ’³ â¥¤ ğ’®} [IsFibered p]
    {R S : ğ’®} {a : ğ’³} (ha : p.obj a = S) (f : R âŸ¶ S) : p.obj (PullbackObj ha f) = R :=
  (PullbackMap.IsCartesian ha f).ObjLiftDomain

/-- Given a diagram
```
                  a
                  -
                  |
                  v
T --g--> R --f--> S
```
we have an isomorphism `T Ã—_S a â‰… T Ã—_R (R Ã—_S a)` -/
noncomputable def PullbackPullbackIso {p : ğ’³ â¥¤ ğ’®} [IsFibered p]
    {R S T : ğ’®}  {a : ğ’³} (ha : p.obj a = S) (f : R âŸ¶ S) (g : T âŸ¶ R) :
      PullbackObj ha (g â‰« f) â‰… PullbackObj (PullbackObj_proj ha f) g :=
  NaturalIso (IsCartesian.comp (PullbackMap.IsCartesian (PullbackObj_proj ha f) g)
    (PullbackMap.IsCartesian ha f))
      (PullbackMap.IsCartesian ha (g â‰« f))

end IsFibered
