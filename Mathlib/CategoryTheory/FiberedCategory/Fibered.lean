/-
Copyright (c) 2024 Paul Lezeau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Lezeau, Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.FiberedCategory.Cartesian

/-!

# Fibered categories

This file defines what it means for a functor `p : ğ’³ â¥¤ ğ’®` to be (pre)fibered.

## Main definitions

- `IsPreFibered p` expresses `ğ’³` is fibered over `ğ’®` via a functor `p : ğ’³ â¥¤ ğ’®`, as in SGA VI.6.1.
This means that any morphism in the base `ğ’®` can be lifted to a cartesian morphism in `ğ’³`.

## Implementation

The standard constructor of `IsPreFibered` has been renamed to `.mk'`, and we have provided an
alternate constructor `IsPreFibered.mk`, which peforms substitutions of some superfluous variables.
It is recommended to use these instead to minimize the amount of equalities that needs to be carried
around in the construction.

There are different notions of fibered categories in the literature, and another common definition
is the existence of strongly cartesian morphisms lying over any given morphism in the base. We also
provide an alternate constructor for `IsFibered` in this sense, see `IsFibered.of_has_pullbacks'`.

## References
* [A. Grothendieck, M. Raynaud, *SGA 1*](https://arxiv.org/abs/math/0206203)
-/

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

open CategoryTheory Functor Category IsHomLift

namespace CategoryTheory

variable {ğ’® : Type uâ‚} {ğ’³ : Type uâ‚‚} [Category.{vâ‚} ğ’®] [Category.{vâ‚‚} ğ’³]

/-- Definition of a prefibered category. SGA 1 VI.6.1. -/
class Functor.IsPreFibered (p : ğ’³ â¥¤ ğ’®) : Prop where
  has_pullbacks' {a : ğ’³} {R : ğ’®} (f : R âŸ¶ p.obj a) : âˆƒ (b : ğ’³) (Ï† : b âŸ¶ a), IsCartesian p f Ï†

protected lemma IsPreFibered.has_pullbacks (p : ğ’³ â¥¤ ğ’®) [p.IsPreFibered] {a : ğ’³} {R S : ğ’®}
    (ha : p.obj a = S) (f : R âŸ¶ S) : âˆƒ (b : ğ’³) (Ï† : b âŸ¶ a), IsCartesian p f Ï† := by
  subst ha; exact IsPreFibered.has_pullbacks' f

namespace IsPreFibered

open IsCartesian

/-- Given a prefibered category `p : ğ’³ â¥¤ ğ’«`, and a diagram
```
           a
           -
           |
           v
  R --f--> S
```
`pullbackObj` is defined as the domain `R Ã—_S a` of some cartesian arrow lying over
`f`, which exists by the fibered category structure on `p`. -/
noncomputable def pullbackObj {p : ğ’³ â¥¤ ğ’®} [IsPreFibered p] {R S : ğ’®}
    {a : ğ’³} (ha : p.obj a = S) (f : R âŸ¶ S) : ğ’³ :=
  Classical.choose (IsPreFibered.has_pullbacks p ha f)

/-- Given a fibered category `p : ğ’³ â¥¤ ğ’«`, and a diagram
```
          a
          -
          |
          v
R --f--> S
```
we get a map `R Ã—_S b âŸ¶ a` -/
noncomputable def pullbackMap {p : ğ’³ â¥¤ ğ’®} [IsPreFibered p] {R S : ğ’®}
    {a : ğ’³} (ha : p.obj a = S) (f : R âŸ¶ S) : pullbackObj ha f âŸ¶ a :=
  Classical.choose (Classical.choose_spec (IsPreFibered.has_pullbacks p ha f))

instance pullbackMap.IsCartesian {p : ğ’³ â¥¤ ğ’®} [IsPreFibered p] {R S : ğ’®}
    {a : ğ’³} (ha : p.obj a = S) (f : R âŸ¶ S) : IsCartesian p f (pullbackMap ha f) :=
  Classical.choose_spec (Classical.choose_spec (IsPreFibered.has_pullbacks p ha f))

lemma pullbackObj_proj {p : ğ’³ â¥¤ ğ’®} [IsPreFibered p] {R S : ğ’®} {a : ğ’³} (ha : p.obj a = S)
    (f : R âŸ¶ S) : p.obj (pullbackObj ha f) = R :=
  domain_eq p f (pullbackMap ha f)

end IsPreFibered

end CategoryTheory
