/-
Copyright (c) 2024 Paul Lezeau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Lezeau, Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.FiberedCategory.Cartesian

/-!

# Fibered categories

This file defines what it means for a functor `p : ğ’³ â¥¤ ğ’®` to be (pre)fibered.

## Main definitions

- `IsPreFibered p` expresses that `p` gives `ğ’³` the structure of a prefibered category over `ğ’®`,
as in SGA VI.6.1

- `IsFibered p` expresses `ğ’³` is fibered over `ğ’®` via a functor `p : ğ’³ â¥¤ ğ’®`, as in SGA VI.6.1.
This means that it is prefibered, and that the composition of any two cartesian morphisms is
cartesian.

In the literature one often sees the notion of a fibered category defined as the existence of
strongly cartesian morphisms lying over any given morphism in the base. This is equivalent to the
notion above, and we give an alternate constructor `IsFibered.of_has_pullbacks'` for constructing
a fibered category this way.


## Implementation

The constructor of `IsPreFibered` is called `has_pullbacks'`. The reason for the prime is that when
wanting to apply this condition, it is recommended to instead use the lemma `has_pullbacks`
(without the prime), which is more applicable with respect to non-definitional equalities.

## References
* [A. Grothendieck, M. Raynaud, *SGA 1*](https://arxiv.org/abs/math/0206203)

-/

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

open CategoryTheory Functor Category IsHomLift

namespace CategoryTheory

variable {ğ’® : Type uâ‚} {ğ’³ : Type uâ‚‚} [Category.{vâ‚} ğ’®] [Category.{vâ‚‚} ğ’³]

/-- Definition of a prefibered category.

See SGA 1 VI.6.1. -/
class Functor.IsPreFibered (p : ğ’³ â¥¤ ğ’®) : Prop where
  has_pullbacks' {a : ğ’³} {R : ğ’®} (f : R âŸ¶ p.obj a) : âˆƒ (b : ğ’³) (Ï† : b âŸ¶ a), IsCartesian p f Ï†

protected lemma IsPreFibered.has_pullbacks {p : ğ’³ â¥¤ ğ’®} [p.IsPreFibered] {a : ğ’³} {R S : ğ’®}
    (ha : p.obj a = S) (f : R âŸ¶ S) : âˆƒ (b : ğ’³) (Ï† : b âŸ¶ a), IsCartesian p f Ï† := by
  subst ha; exact IsPreFibered.has_pullbacks' f

/-- Definition of a fibered category.

See SGA 1 VI.6.1. -/
class Functor.IsFibered (p : ğ’³ â¥¤ ğ’®) extends IsPreFibered p : Prop where
  comp {R S T : ğ’®} (f : R âŸ¶ S) (g : S âŸ¶ T) {a b c : ğ’³} (Ï† : a âŸ¶ b) (Ïˆ : b âŸ¶ c)
    [IsCartesian p f Ï†] [IsCartesian p g Ïˆ] : IsCartesian p (f â‰« g) (Ï† â‰« Ïˆ)

instance (p : ğ’³ â¥¤ ğ’®) [p.IsFibered] {R S T : ğ’®} (f : R âŸ¶ S) (g : S âŸ¶ T) {a b c : ğ’³} (Ï† : a âŸ¶ b)
    (Ïˆ : b âŸ¶ c) [IsCartesian p f Ï†] [IsCartesian p g Ïˆ] : IsCartesian p (f â‰« g) (Ï† â‰« Ïˆ) :=
  IsFibered.comp f g Ï† Ïˆ

namespace IsPreFibered

open IsCartesian


variable {p : ğ’³ â¥¤ ğ’®} [IsPreFibered p] {R S : ğ’®} {a : ğ’³} (ha : p.obj a = S) (f : R âŸ¶ S)

/-- Given a fibered category `p : ğ’³ â¥¤ ğ’«`, a morphism `f : R âŸ¶ S` and an object `a` lying over `S`,
then `pullbackObj` is the domain of some choice of a cartesian morphism lying over `f` with
codomain `a`. -/
noncomputable def pullbackObj : ğ’³ :=
  Classical.choose (IsPreFibered.has_pullbacks ha f)

/-- Given a fibered category `p : ğ’³ â¥¤ ğ’«`, a morphism `f : R âŸ¶ S` and an object `a` lying over `S`,
then `pullbackMap` is a choice of a cartesian morphism lying over `f` with codomain `a`. -/
noncomputable def pullbackMap  : pullbackObj ha f âŸ¶ a :=
  Classical.choose (Classical.choose_spec (IsPreFibered.has_pullbacks ha f))

instance pullbackMap.IsCartesian : IsCartesian p f (pullbackMap ha f) :=
  Classical.choose_spec (Classical.choose_spec (IsPreFibered.has_pullbacks ha f))

lemma pullbackObj_proj : p.obj (pullbackObj ha f) = R :=
  domain_eq p f (pullbackMap ha f)

end IsPreFibered

namespace IsFibered

open IsCartesian IsPreFibered

/-- In a fibered category, any cartesian morphism is strongly cartesian. -/
instance isStronglyCartesian_of_isCartesian (p : ğ’³ â¥¤ ğ’®) [p.IsFibered] {R S : ğ’®} (f : R âŸ¶ S)
    {a b : ğ’³} (Ï† : a âŸ¶ b) [p.IsCartesian f Ï†] : p.IsStronglyCartesian f Ï† where
  universal_property' := by
    intro a' g Ï†' hÏ†'
    -- Let `Ïˆ` be a cartesian arrow lying over `g`
    let Ïˆ := pullbackMap (domain_eq p f Ï†) g
    -- Let `Ï„` be the map induced by the universal property of `Ïˆ â‰« Ï†`.
    let Ï„ := inducedMap p (g â‰« f) (Ïˆ â‰« Ï†) Ï†'
    use Ï„ â‰« Ïˆ
    -- It is easily verified that `Ï„ â‰« Ïˆ` lifts `g` and `Ï„ â‰« Ïˆ â‰« Ï† = Ï†'`
    refine âŸ¨âŸ¨inferInstance, by simp only [assoc, inducedMap_comp, Ï„]âŸ©, ?_âŸ©
    -- It remains to check that `Ï„ â‰« Ïˆ` is unique.
    -- So fix another lift `Ï€` of `g` satisfying `Ï€ â‰« Ï† = Ï†'`.
    intro Ï€ âŸ¨hÏ€, hÏ€_compâŸ©
    -- Write `Ï€` as `Ï€ = Ï„' â‰« Ïˆ` for some `Ï„'` induced by the universal property of `Ïˆ`.
    rw [â† inducedMap_comp p g Ïˆ Ï€]
    -- It remains to show that `Ï„' = Ï„`. This follows again from the universal property of `Ïˆ`.
    congr 1
    apply inducedMap_unique
    rwa [â† assoc, inducedMap_comp]

/-- In a category which admits strongly cartesian pullbacks, any cartesian morphism is
strongly cartesian. This is a helper-lemma for the fact that admitting strongly cartesian pullbacks
implies being fibered. -/
lemma isStronglyCartesian_of_has_pullbacks' (p : ğ’³ â¥¤ ğ’®) (h : âˆ€ (a : ğ’³) (R : ğ’®) (f : R âŸ¶ p.obj a),
    âˆƒ (b : ğ’³) (Ï† : b âŸ¶ a), IsStronglyCartesian p f Ï†) {R S : ğ’®} (f : R âŸ¶ S) {a b : ğ’³}
      (Ï† : a âŸ¶ b) [p.IsCartesian f Ï†] : p.IsStronglyCartesian f Ï† := by
  constructor
  intro c g Ï†' hÏ†'
  subst_hom_lift p f Ï†; clear a b R S
  -- Let `Ïˆ` be a cartesian arrow lying over `g`
  obtain âŸ¨a', Ïˆ, hÏˆâŸ© := h _ _ (p.map Ï†)
  -- Let `Ï„' : c âŸ¶ a'` be the map induced by the universal property of `Ïˆ`
  let Ï„' := IsStronglyCartesian.inducedMap p (p.map Ï†) Ïˆ (f':= g â‰« p.map Ï†) rfl Ï†'
  -- Let `Î¦ : a' âŸ¶ a` be natural isomorphism induced between `Ï†` and `Ïˆ`.
  let Î¦ := naturalIso p (p.map Ï†) Ï† Ïˆ
  -- The map induced by `Ï†` will be `Ï„' â‰« Î¦.hom`
  use Ï„' â‰« Î¦.hom
  -- It is easily verified that `Ï„' â‰« Î¦.hom` lifts `g` and `Ï„' â‰« Î¦.hom â‰« Ï† = Ï†'`
  refine âŸ¨âŸ¨inferInstance, ?_âŸ©, ?_âŸ©
  Â· simp [Î¦, IsStronglyCartesian.inducedMap_comp p (p.map Ï†) Ïˆ rfl Ï†']
  -- It remains to check that it is unique. This follows from the universal property of `Ïˆ`.
  intro Ï€ âŸ¨hÏ€, hÏ€_compâŸ©
  rw [â† Iso.comp_inv_eq]
  apply IsStronglyCartesian.inducedMap_unique p (p.map Ï†) Ïˆ rfl Ï†'
  simp [Î¦, hÏ€_comp]


lemma of_has_pullbacks' {p : ğ’³ â¥¤ ğ’®} (h : âˆ€ (a : ğ’³) (R : ğ’®) (f : R âŸ¶ p.obj a),
    âˆƒ (b : ğ’³) (Ï† : b âŸ¶ a), IsStronglyCartesian p f Ï†) : IsFibered p where
  has_pullbacks' := by
    intro a R f
    obtain âŸ¨b, Ï†, hÏ†âŸ© := h a R f
    refine âŸ¨b, Ï†, inferInstanceâŸ©
  comp := by
    intro R S T f g a b c Ï† Ïˆ _ _
    have : p.IsStronglyCartesian f Ï† := by apply isStronglyCartesian_of_has_pullbacks' p h
    have : p.IsStronglyCartesian g Ïˆ := by apply isStronglyCartesian_of_has_pullbacks' p h
    infer_instance

/-- Given a diagram
```
                  a
                  -
                  |
                  v
T --g--> R --f--> S
```
we have an isomorphism `T Ã—_S a â‰… T Ã—_R (R Ã—_S a)` -/
noncomputable def pullbackPullbackIso {p : ğ’³ â¥¤ ğ’®} [IsFibered p]
    {R S T : ğ’®}  {a : ğ’³} (ha : p.obj a = S) (f : R âŸ¶ S) (g : T âŸ¶ R) :
      pullbackObj ha (g â‰« f) â‰… pullbackObj (pullbackObj_proj ha f) g :=
  naturalIso p (g â‰« f) (pullbackMap (pullbackObj_proj ha f) g â‰« pullbackMap ha f)
    (pullbackMap ha (g â‰« f))

end IsFibered

end CategoryTheory
