/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Lezeau, Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.CommSq

/-!

# HomLift

Given a functor `p : ğ’³ â¥¤ ğ’®`, this file provides API for expressing the fact that `p(Ï†) = f`
for given morphisms `Ï†` and `f`.

## Main definition

Given morphism `Ï† : a âŸ¶ b` in `ğ’³` and `f : R âŸ¶ S` in `ğ’®`, `IsHomLift p f Ï†` is defined as a
structure containing the data that `p(a) = R`, `p(b) = S` and the fact that the following square
commutes
```
  p(a) --p(Ï†)--> p(b)
  |               |
  |               |
  v               v
  R -----f------> S
```
where the vertical arrows are given by `eqToHom` corresponding to the equalities `p(a) = R` and
`p(b) = S`.

-/

universe uâ‚ vâ‚ uâ‚‚ vâ‚‚

open CategoryTheory Functor Category

variable {ğ’® : Type uâ‚} {ğ’³ : Type uâ‚‚} [Category ğ’³] [Category ğ’®]

/-- The proposition that an arrow a --Ï†--> b lifts an arrow R --f--> S in ğ’® via p. This is
often drawn as:
```
  a --Ï†--> b
  -        -
  |        |
  v        v
  R --f--> S
``` -/
class Functor.IsHomLift (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b) : Prop where
  domain_eq : p.obj a = R
  codomain_eq : p.obj b = S
  homlift : CommSq (p.map Ï†) (eqToHom domain_eq) (eqToHom codomain_eq) f

namespace Functor.IsHomLift

protected lemma hom_eq {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a âŸ¶ b}
    [hÏ† : IsHomLift p f Ï†] : f = eqToHom hÏ†.domain_eq.symm â‰« p.map Ï† â‰«
      eqToHom hÏ†.codomain_eq :=
  ((eqToHom_comp_iff hÏ†.domain_eq _ _).1 hÏ†.homlift.w.symm)

protected lemma hom_eq' {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a âŸ¶ b}
   [hÏ† : IsHomLift p f Ï†] : p.map Ï† = eqToHom hÏ†.domain_eq â‰« f â‰«
      eqToHom hÏ†.codomain_eq.symm := by
  rw [â† assoc, â† comp_eqToHom_iff hÏ†.codomain_eq _ _]
  exact hÏ†.homlift.w

lemma eq_of_IsHomLift {p : ğ’³ â¥¤ ğ’®} (a b : ğ’³) {f : p.obj a âŸ¶ p.obj b} {Ï† : a âŸ¶ b}
   [hÏ† : IsHomLift p f Ï†] : f = p.map Ï† := by
  simpa using hÏ†.hom_eq

/-- For any arrow `Ï† : a âŸ¶ b` in `ğ’³`, `Ï†` lifts the arrow `p.map Ï†` in the base `ğ’®`-/
@[simp]
protected lemma self (p : ğ’³ â¥¤ ğ’®) {a b : ğ’³} (Ï† : a âŸ¶ b) : IsHomLift p (p.map Ï†) Ï† where
  domain_eq := rfl
  codomain_eq := rfl
  homlift := âŸ¨by simp only [eqToHom_refl, comp_id, id_comp]âŸ©

@[simp]
protected lemma id {p : ğ’³ â¥¤ ğ’®} {R : ğ’®} {a : ğ’³} (ha : p.obj a = R) : IsHomLift p (ğŸ™ R) (ğŸ™ a) :=
  ha â–¸ (p.map_id _ â–¸ IsHomLift.self p (ğŸ™ a))

protected lemma comp {p : ğ’³ â¥¤ ğ’®} {R S T : ğ’®} {a b c : ğ’³} {f : R âŸ¶ S}
    {g : S âŸ¶ T} {Ï† : a âŸ¶ b} {Ïˆ : b âŸ¶ c} [hÏ† : IsHomLift p f Ï†]
    [hÏˆ : IsHomLift p g Ïˆ] : IsHomLift p (f â‰« g) (Ï† â‰« Ïˆ) where
  domain_eq := hÏ†.1
  codomain_eq := hÏˆ.2
  homlift := (p.map_comp _ _).symm â–¸ CommSq.horiz_comp hÏ†.3 hÏˆ.3

/-- If `Ï† : a âŸ¶ b` and `Ïˆ : b âŸ¶ c` lift `ğŸ™ S`, then so does `Ï† â‰« Ïˆ` -/
lemma lift_id_comp {p : ğ’³ â¥¤ ğ’®} {R : ğ’®} {a b c : ğ’³} {Ï† : a âŸ¶ b} {Ïˆ : b âŸ¶ c}
    (hÏ† : IsHomLift p (ğŸ™ R) Ï†) [IsHomLift p (ğŸ™ R) Ïˆ] : IsHomLift p (ğŸ™ R) (Ï† â‰« Ïˆ) :=
  comp_id (ğŸ™ R) â–¸ hÏ†.comp

/-- If `Ï† : a âŸ¶ b` lifts `f` and `Ïˆ : b âŸ¶ c` lifts `ğŸ™ T`, then `Ï†  â‰« Ïˆ` lifts `f` -/
lemma comp_lift_id {p : ğ’³ â¥¤ ğ’®} {R S T : ğ’®} {a b c : ğ’³} {f : R âŸ¶ S}
    {Ï† : a âŸ¶ b} [hÏ† : IsHomLift p f Ï†] {Ïˆ : b âŸ¶ c} [hÏˆ : IsHomLift p (ğŸ™ T) Ïˆ] :
    IsHomLift p f (Ï† â‰« Ïˆ) where
  domain_eq := hÏ†.domain_eq
  codomain_eq := by rw [hÏˆ.codomain_eq, â† hÏˆ.domain_eq, hÏ†.codomain_eq]
  homlift := âŸ¨by simp [hÏˆ.hom_eq', hÏ†.3.1]âŸ©

@[simp]
lemma eqToHom_domain_lift_id {p : ğ’³ â¥¤ ğ’®} {a b : ğ’³} (hab : a = b) {R : ğ’®}
    (hR : p.obj a = R) : IsHomLift p (ğŸ™ R) (eqToHom hab) where
  domain_eq := hR
  codomain_eq := hab â–¸ hR
  homlift := âŸ¨by simp only [eqToHom_map, eqToHom_trans, comp_id]âŸ©

@[simp]
lemma eqToHom_codomain_lift_id {p : ğ’³ â¥¤ ğ’®} {a b : ğ’³} (hab : a = b) {S : ğ’®}
    (hS : p.obj b = S) : IsHomLift p (ğŸ™ S) (eqToHom hab) where
  domain_eq := hab â–¸ hS
  codomain_eq := hS
  homlift := âŸ¨by simp only [eqToHom_map, eqToHom_trans, comp_id]âŸ©

@[simp]
lemma id_lift_eqToHom_domain {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} (hRS : R = S)
    {a : ğ’³} (ha : p.obj a = R) : IsHomLift p (eqToHom hRS) (ğŸ™ a) where
  domain_eq := ha
  codomain_eq := hRS â–¸ ha
  homlift := âŸ¨by simp only [map_id, id_comp, eqToHom_trans]âŸ©

@[simp]
lemma id_lift_eqToHom_codomain {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} (hRS : R = S)
    {b : ğ’³} (hb : p.obj b = S) : IsHomLift p (eqToHom hRS) (ğŸ™ b) where
  domain_eq := hRS â–¸ hb
  codomain_eq := hb
  homlift := âŸ¨by simp only [map_id, id_comp, eqToHom_trans]âŸ©

instance comp_eqToHom_lift {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a' a b : ğ’³} {f : R âŸ¶ S}
    {Ï† : a âŸ¶ b} {h : a' = a} [hÏ† : IsHomLift p f Ï†] : IsHomLift p f (eqToHom h â‰« Ï†) :=
  id_comp f â–¸ (eqToHom_codomain_lift_id h hÏ†.domain_eq).comp

instance eqToHom_comp_lift {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b b' : ğ’³} {f : R âŸ¶ S}
    {Ï† : a âŸ¶ b} {h : b = b'} [hÏ† : IsHomLift p f Ï†] : IsHomLift p f (Ï† â‰« eqToHom h) :=
  comp_id f â–¸ hÏ†.comp (hÏˆ := eqToHom_domain_lift_id h hÏ†.codomain_eq)

instance lift_eqToHom_comp {p : ğ’³ â¥¤ ğ’®} {R' R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S}
    {Ï† : a âŸ¶ b} (h : R' = R) [hÏ† : IsHomLift p f Ï†] : IsHomLift p ((eqToHom h) â‰« f) Ï† :=
  id_comp Ï† â–¸ (IsHomLift.id_lift_eqToHom_codomain h hÏ†.domain_eq).comp

instance lift_comp_eqToHom {p : ğ’³ â¥¤ ğ’®} {R S S': ğ’®} {a b : ğ’³} {f : R âŸ¶ S}
    {Ï† : a âŸ¶ b} (h : S = S') [hÏ† : IsHomLift p f Ï†] : IsHomLift p (f â‰« (eqToHom h)) Ï† :=
  comp_id Ï† â–¸ hÏ†.comp (hÏˆ := IsHomLift.id_lift_eqToHom_domain h hÏ†.codomain_eq)

@[simp]
lemma comp_eqToHom_lift_iff {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a' a b : ğ’³} {f : R âŸ¶ S}
    {Ï† : a âŸ¶ b} {h : a' = a} : IsHomLift p f (eqToHom h â‰« Ï†) â†” IsHomLift p f Ï† where
  mp := by intro hÏ†'; subst h; simpa using hÏ†'
  mpr := fun hÏ† => inferInstance

@[simp]
lemma eqToHom_comp_lift_iff {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b b' : ğ’³} {f : R âŸ¶ S}
    {Ï† : a âŸ¶ b} {h : b = b'} : IsHomLift p f (Ï† â‰« eqToHom h) â†” IsHomLift p f Ï† where
  mp := by intro hÏ†'; subst h; simpa using hÏ†'
  mpr := fun hÏ† => inferInstance

@[simp]
lemma lift_eqToHom_comp_iff {p : ğ’³ â¥¤ ğ’®} {R' R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S}
    {Ï† : a âŸ¶ b} (h : R' = R) : IsHomLift p ((eqToHom h) â‰« f) Ï† â†” IsHomLift p f Ï† where
  mp := by intro hÏ†'; subst h; simpa using hÏ†'
  mpr := fun hÏ† => inferInstance

@[simp]
lemma lift_comp_eqToHom_iff {p : ğ’³ â¥¤ ğ’®} {R S S': ğ’®} {a b : ğ’³} {f : R âŸ¶ S}
    {Ï† : a âŸ¶ b} (h : S = S') : IsHomLift p (f â‰« (eqToHom h)) Ï† â†” IsHomLift p f Ï† where
  mp := by intro hÏ†'; subst h; simpa using hÏ†'
  mpr := fun hÏ† => inferInstance

/-- The isomorphism `R â‰… S` obtained from an isomorphism `Ï† : a â‰… b` lifting `f` -/
def Iso_of_Iso_lift {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a â‰… b}
    [hÏ† : IsHomLift p f Ï†.hom] : R â‰… S :=
  eqToIso hÏ†.domain_eq.symm â‰ªâ‰« p.mapIso Ï† â‰ªâ‰« eqToIso hÏ†.codomain_eq

@[simp]
lemma Iso_of_Iso_lift_hom {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a â‰… b}
    [hÏ† : IsHomLift p f Ï†.hom] : (hÏ†.Iso_of_Iso_lift).hom = f := by
  simp [Iso_of_Iso_lift, hÏ†.hom_eq]

@[simp]
lemma Iso_of_Iso_lift_comp {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a â‰… b}
    [hÏ† : IsHomLift p f Ï†.hom] : (hÏ†.Iso_of_Iso_lift).inv â‰« f = ğŸ™ S := by
  rw [CategoryTheory.Iso.inv_comp_eq]
  simp only [Iso_of_Iso_lift_hom, comp_id]

@[simp]
lemma comp_Iso_of_Iso_lift {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a â‰… b}
    [hÏ† : IsHomLift p f Ï†.hom] : f â‰« (hÏ†.Iso_of_Iso_lift).inv = ğŸ™ R := by
  rw [CategoryTheory.Iso.comp_inv_eq]
  simp only [Iso_of_Iso_lift_hom, id_comp]

/-- If `Ï† : a âŸ¶ b` lifts `f : R âŸ¶ S` and `Ï†` is an isomorphism, then so is `f`. -/
lemma IsIso_of_lift_IsIso {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a âŸ¶ b}
   [hÏ† : IsHomLift p f Ï†] [IsIso Ï†] : IsIso f :=
  hÏ†.hom_eq â–¸ inferInstance

/-- Given `Ï† : a â‰… b` and `f : R â‰… S`, such that `Ï†.hom` lifts `f.hom`, then `Ï†.inv` lifts
`f.inv`. -/
protected instance inv_lift_inv {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R â‰… S} {Ï† : a â‰… b}
    [hÏ† : IsHomLift p f.hom Ï†.hom] : IsHomLift p f.inv Ï†.inv where
  domain_eq := hÏ†.2
  codomain_eq := hÏ†.1
  homlift := CommSq.horiz_inv (f:=p.mapIso Ï†) (i:=f) hÏ†.3

/-- Given `Ï† : a â‰… b` and `f : R âŸ¶ S`, such that `Ï†.hom` lifts `f`, then `Ï†.inv` lifts the
inverse of `f` given by `Iso_of_Iso_lift`. -/
protected instance inv_lift {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a â‰… b}
    [hÏ† : IsHomLift p f Ï†.hom] : IsHomLift p (hÏ†.Iso_of_Iso_lift).inv Ï†.inv where
  domain_eq := hÏ†.2
  codomain_eq := hÏ†.1
  homlift := CommSq.horiz_inv (f:=p.mapIso Ï†) (i:=hÏ†.Iso_of_Iso_lift) (by simpa using hÏ†.3)

/-- If `Ï† : a âŸ¶ b` lifts `f : R âŸ¶ S` and both are isomorphisms, then `Ï†â»Â¹` lifts `fâ»Â¹`. -/
protected instance inv {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a âŸ¶ b}
    [hÏ† : IsHomLift p f Ï†] [IsIso Ï†] [IsIso f] : IsHomLift p (inv f) (inv Ï†) :=
  IsHomLift.inv_lift_inv (f := asIso f) (Ï† := asIso Ï†) (hÏ† := hÏ†)

/-- If `Ï† : a âŸ¶ b` is an isomorphism, and lifts `ğŸ™ S` for some `S : ğ’®`, then `Ï†â»Â¹` also
lifts `ğŸ™ S` -/
instance lift_id_inv {p : ğ’³ â¥¤ ğ’®} {S : ğ’®} {a b : ğ’³} {Ï† : a âŸ¶ b} [IsIso Ï†]
    [hÏ† : IsHomLift p (ğŸ™ S) Ï†] : IsHomLift p (ğŸ™ S) (inv Ï†) :=
  (IsIso.inv_id (X:=S)) â–¸ hÏ†.inv

end Functor.IsHomLift
