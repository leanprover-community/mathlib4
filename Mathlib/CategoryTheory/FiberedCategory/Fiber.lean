/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle SÃ¶nne, Paul Lezeau
-/

import Mathlib.CategoryTheory.FiberedCategory.Fibered
import Mathlib.CategoryTheory.Functor.Const

/-!

# Fibers of functors

In this file we develop the theory of fibers of functors. Given a functor `p : ğ’³ â¥¤ ğ’®`, we define
the fiber categories `Fiber p S` for every `S : ğ’®` as follows:
- An object in `Fiber p S` is a pair `(a, ha)` where `a : ğ’³` and `ha : p.obj a = S`.
- A morphism in `Fiber p S` is a morphism `Ï† : a âŸ¶ b` in ğ’³ such that `p.map Ï† = ğŸ™ S`.

We also introduce a typeclass `HasFibers` for a functor `p : ğ’³ â¥¤ ğ’®`, consisting of:
- A collection of categories `Fib S` for every `S` in `ğ’®` (the fiber categories)
- Functors `Î¹ : Fib S â¥¤ ğ’³` such that `Î¹ â‹™ p = const (Fib S) S
- The induced functor `Fib S â¥¤ Fiber p S` is an equivalence.

The reason for introducing this typeclass is that in practice, when working with fibered categories
one often already has a collection of categories `Fib S` for every `S` that are equivalent to the
fibers `Fiber p S`. One would then like to use these categories `Fib S` directly, instead of working
through this equivalence of categories. By developing an API for the `HasFibers` typeclass, this
will be possible.For example, we develop the following lemmas:
- `HasFibersEssSurj` any object `a : ğ’³` lying over some `S : ğ’®` is isomorphic to the image of some
  `a' : Fib S`
- `HasFibersPullback` allows one to take pullbacks such that the codomain lies in one of the fibers
  `Fib S`.
- `HasFibersFactorization` (TODO: maybe call it `HasFibersInducedMap`, and the next `HasFibersFactorization`)
- `fiber_factorization` any morphism in `ğ’³` can be factored as a morphism in some fiber `Fib S`
  followed by a pullback. (TODO: rename this lemma)

Here is an example of when this typeclass is useful. Suppose we have a presheaf of types
`F : ğ’®áµ’áµ– â¥¤ Type _`. The associated fibered category then has objects `(S, a)` where `S : ğ’®` and `a`
is an element of `F(S)`. The fiber category `Fiber p S` is then equivalent to the discrete category
`Fib S` with objects `a` in `F(S)`. In this case, the `HasFibers` instance is given by the
categories `F(S)` and the functor `Î¹` sends `a : F(S)` to `(S, a)` in the fibered category.
See `Presheaf.lean` for more details.
-/

-- TODO: u, v should be flipped here?
universe uâ‚ vâ‚ uâ‚‚ vâ‚‚ uâ‚ƒ vâ‚ƒ w

open CategoryTheory Functor Category IsCartesian IsHomLift

variable {ğ’® : Type uâ‚} {ğ’³ : Type uâ‚‚} [Category.{vâ‚} ğ’®] [Category.{vâ‚‚} ğ’³]

/-- Fiber p S is the type of elements of ğ’³ mapping to S via p  -/
-- TODO: coe?
def Fiber (p : ğ’³ â¥¤ ğ’®) (S : ğ’®) := {a : ğ’³ // p.obj a = S}

namespace Fiber

variable {p : ğ’³ â¥¤ ğ’®} {S : ğ’®}

-- TODO: bundled? Coe?
def Hom (a b : Fiber p S) := {Ï† : a.1 âŸ¶ b.1 // IsHomLift p (ğŸ™ S) Ï†}

instance (a b : Fiber p S) (Ï† : Hom a b) : IsHomLift p (ğŸ™ S) Ï†.1 := Ï†.2

/-- Fiber p S has the structure of a category by taking the morphisms to be those lying over ğŸ™ S -/
@[simps]
instance FiberCategory : Category (Fiber p S) where
  Hom a b := {Ï† : a.1 âŸ¶ b.1 // IsHomLift p (ğŸ™ S) Ï†}
  id a := âŸ¨ğŸ™ a.1, IsHomLift.id a.2âŸ©
  comp Ï† Ïˆ := âŸ¨Ï†.val â‰« Ïˆ.val, inferInstanceâŸ©

/-- The object .... -/
@[simps]
def mk_obj {a : ğ’³} (ha : p.obj a = S) : Fiber p S := âŸ¨a, haâŸ©

/-- The object ... -/
@[simps]
def mk_map {a b : ğ’³} (Ï† : a âŸ¶ b) [IsHomLift p (ğŸ™ S) Ï†] :
  mk_obj (domain_eq p (ğŸ™ S) Ï†) âŸ¶ mk_obj (codomain_eq p (ğŸ™ S) Ï†) := âŸ¨Ï†, inferInstanceâŸ©

@[simp]
lemma mk_map_id {a : ğ’³} [IsHomLift p (ğŸ™ S) (ğŸ™ a)] :
    mk_map (ğŸ™ a) = ğŸ™ (mk_obj (domain_eq p (ğŸ™ S) (ğŸ™ a))) :=
  rfl

/-- The functor including Fiber p S into ğ’³ -/
@[simps]
def FiberInclusion (p : ğ’³ â¥¤ ğ’®) (S : ğ’®) : (Fiber p S) â¥¤ ğ’³ where
  obj a := a.1
  map Ï† := Ï†.1

instance FiberInclusionFaithful (p : ğ’³ â¥¤ ğ’®) (S : ğ’®) : Functor.Faithful (FiberInclusion p S) where
  map_injective := Subtype.val_inj.1

@[ext]
lemma hom_ext {a b : Fiber p S} (Ï† Ïˆ : a âŸ¶ b) : Ï†.1 = Ïˆ.1 â†’ Ï† = Ïˆ :=
  Subtype.ext

@[simp]
lemma val_comp {a b c : Fiber p S} (Ï† : a âŸ¶ b)
    (Ïˆ : b âŸ¶ c) : (Ï† â‰« Ïˆ).1 = Ï†.1 â‰« Ïˆ.1 := rfl

lemma mk_map_com {a b c : ğ’³} (Ï† : a âŸ¶ b) (Ïˆ : b âŸ¶ c) [IsHomLift p (ğŸ™ S) Ï†]
    -- TODO: mk_map is annoying here, maybe make more variables explicit?
    [IsHomLift p (ğŸ™ S) Ïˆ] : mk_map (Ï† â‰« Ïˆ) = mk_map Ï† â‰« mk_map (p:=p) (S:=S) Ïˆ :=
  rfl

section

-- TODO: which parameters should be explicit here? Also F, S?
variable {p : ğ’³ â¥¤ ğ’®} {S : ğ’®} {C : Type uâ‚ƒ} [Category.{vâ‚ƒ} C] {F : C â¥¤ ğ’³}
  (hF : F â‹™ p = (const C).obj S)

/-- Given a functor F : C â¥¤ ğ’³ mapping constantly to some S in the base,
  we get an induced functor C â¥¤ Fiber p S -/
@[simps]
def FiberInducedFunctor : C â¥¤ Fiber p S where
    obj := fun x â†¦ âŸ¨F.obj x, by simp only [â† comp_obj, hF, const_obj_obj]âŸ©
    map := fun Ï† â†¦ âŸ¨F.map Ï†, by
      apply IsHomLift.of_commSq
      -- TODO: of_commsq lemma (which applies constructor automatically?)
      constructor; simpa using (eqToIso hF).hom.naturality Ï† âŸ©

/-- The natural transformation between F : C â¥¤ ğ’³ and .... -/
def FiberInducedFunctorNat : F â‰… (FiberInducedFunctor hF) â‹™ (FiberInclusion p S) where
    hom := { app := fun a â†¦ ğŸ™ (F.obj a) }
    inv := { app := fun a â†¦ ğŸ™ ((FiberInducedFunctor hF â‹™ FiberInclusion p S).obj a) }

-- TODO: simp lemma? If so should switch sides in the equality
lemma fiberInducedFunctor_comp : F = (FiberInducedFunctor hF) â‹™ (FiberInclusion p S) :=
  Functor.ext_of_iso (FiberInducedFunctorNat hF) (fun _ â†¦ rfl) (fun _ â†¦ rfl)

end

-- TODO: move earlier in this file?

/-- Now we define the standard/canonical fiber associated to a fibered category.
When the user does not wish to supply specific fiber categories, this will be the default choice. -/
def CompConstNat (p : ğ’³ â¥¤ ğ’®) (S : ğ’®) : (FiberInclusion p S) â‹™ p â‰… (const (Fiber p S)).obj S where
  hom := {
    app := fun x => eqToHom x.prop
    naturality := fun x y Ï† => by simpa using (commSq p (ğŸ™ S) Ï†.val).w}
  inv := {
    app := fun x => eqToHom (x.prop).symm
    naturality := fun x y Ï† =>  by
      -- TODO: add this have into API?
      have := by simpa [comp_eqToHom_iff] using (commSq p (ğŸ™ S) Ï†.val).w
      simp [this] }

lemma comp_const (p : ğ’³ â¥¤ ğ’®) (S : ğ’®) : (FiberInclusion p S) â‹™ p = (const (Fiber p S)).obj S := by
  apply Functor.ext_of_iso (CompConstNat p S)
  all_goals intro x; simp [CompConstNat, x.2]

end Fiber
