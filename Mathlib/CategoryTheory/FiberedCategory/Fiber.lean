/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle SÃ¶nne, Paul Lezeau
-/

import Mathlib.CategoryTheory.FiberedCategory.HomLift
import Mathlib.CategoryTheory.Functor.Const

/-!

# Fibers of a functors

In this file we define, for a functor `p : ğ’³ â¥¤ ğ’´`, the fiber categories `Fiber p S` for every
`S : ğ’®` as follows
- An object in `Fiber p S` is a pair `(a, ha)` where `a : ğ’³` and `ha : p.obj a = S`.
- A morphism in `Fiber p S` is a morphism `Ï† : a âŸ¶ b` in ğ’³ such that `p.map Ï† = ğŸ™ S`.

For any category `C` equipped with a functor `F : C â¥¤ ğ’³` such that `F â‹™ p` is constant at `S`,
we define a functor `inducedFunctor : C â¥¤ Fiber p S` that `F` factors through.
-/

universe vâ‚ uâ‚ vâ‚‚ uâ‚‚ vâ‚ƒ uâ‚ƒ

namespace CategoryTheory

open IsHomLift

namespace Functor

variable {ğ’® : Type uâ‚} {ğ’³ : Type uâ‚‚} [Category.{vâ‚} ğ’®] [Category.{vâ‚‚} ğ’³]

/-- `Fiber p S` is the type of elements of `ğ’³` mapping to `S` via `p`. -/
def Fiber (p : ğ’³ â¥¤ ğ’®) (S : ğ’®) := { a : ğ’³ // p.obj a = S }

namespace Fiber

variable {p : ğ’³ â¥¤ ğ’®} {S : ğ’®}

/-- `Fiber p S` has the structure of a category with morphisms being those lying over `ğŸ™ S`. -/
instance fiberCategory : Category (Fiber p S) where
  Hom a b := {Ï† : a.1 âŸ¶ b.1 // IsHomLift p (ğŸ™ S) Ï†}
  id a := âŸ¨ğŸ™ a.1, IsHomLift.id a.2âŸ©
  comp Ï† Ïˆ := âŸ¨Ï†.val â‰« Ïˆ.val, by have := Ï†.2; have := Ïˆ.2; infer_instanceâŸ©

/-- The functor including `Fiber p S` into `ğ’³`. -/
def fiberInclusion : Fiber p S â¥¤ ğ’³ where
  obj a := a.1
  map Ï† := Ï†.1

instance {a b : Fiber p S} (Ï† : a âŸ¶ b) : IsHomLift p (ğŸ™ S) (fiberInclusion.map Ï†) := Ï†.2

@[ext]
lemma hom_ext {a b : Fiber p S} {Ï† Ïˆ : a âŸ¶ b}
    (h : fiberInclusion.map Ï† = fiberInclusion.map Ïˆ) : Ï† = Ïˆ :=
  Subtype.ext h

instance : (fiberInclusion : Fiber p S â¥¤ _).Faithful where

lemma fiberInclusion_obj_inj : (fiberInclusion : Fiber p S â¥¤ _).obj.Injective :=
  fun _ _ f â†¦ Subtype.val_inj.1 f

/-- For fixed `S : ğ’®` this is the natural isomorphism between `fiberInclusion â‹™ p` and the constant
function valued at `S`. -/
@[simps!]
def fiberInclusionCompIsoConst : fiberInclusion â‹™ p â‰… (const (Fiber p S)).obj S :=
  NatIso.ofComponents (fun X â†¦ eqToIso X.2)
    (fun Ï† â†¦ by simp [IsHomLift.fac' p (ğŸ™ S) (fiberInclusion.map Ï†)])

lemma fiberInclusion_comp_eq_const : fiberInclusion â‹™ p = (const (Fiber p S)).obj S :=
  Functor.ext_of_iso fiberInclusionCompIsoConst (fun x â†¦ x.2)

/-- The object of the fiber over `S` corresponding to a `a : ğ’³` such that `p(a) = S`. -/
def mk {p : ğ’³ â¥¤ ğ’®} {S : ğ’®} {a : ğ’³} (ha : p.obj a = S) : Fiber p S := âŸ¨a, haâŸ©

@[simp]
lemma fiberInclusion_mk {p : ğ’³ â¥¤ ğ’®} {S : ğ’®} {a : ğ’³} (ha : p.obj a = S) :
    fiberInclusion.obj (mk ha) = a :=
  rfl

/-- The morphism in the fiber over `S` corresponding to a morphism in `ğ’³` lifting `ğŸ™ S`. -/
def homMk (p : ğ’³ â¥¤ ğ’®) (S : ğ’®) {a b : ğ’³} (Ï† : a âŸ¶ b) [IsHomLift p (ğŸ™ S) Ï†] :
    mk (domain_eq p (ğŸ™ S) Ï†) âŸ¶ mk (codomain_eq p (ğŸ™ S) Ï†) :=
  âŸ¨Ï†, inferInstanceâŸ©

@[simp]
lemma fiberInclusion_homMk (p : ğ’³ â¥¤ ğ’®) (S : ğ’®) {a b : ğ’³} (Ï† : a âŸ¶ b) [IsHomLift p (ğŸ™ S) Ï†] :
    fiberInclusion.map (homMk p S Ï†) = Ï† :=
  rfl

@[simp]
lemma homMk_id (p : ğ’³ â¥¤ ğ’®) (S : ğ’®) (a : ğ’³) [IsHomLift p (ğŸ™ S) (ğŸ™ a)] :
    homMk p S (ğŸ™ a) = ğŸ™ (mk (domain_eq p (ğŸ™ S) (ğŸ™ a))) :=
  rfl

@[simp]
lemma homMk_comp {a b c : ğ’³} (Ï† : a âŸ¶ b) (Ïˆ : b âŸ¶ c) [IsHomLift p (ğŸ™ S) Ï†]
    [IsHomLift p (ğŸ™ S) Ïˆ] : homMk p S Ï† â‰« homMk p S Ïˆ = homMk p S (Ï† â‰« Ïˆ) :=
  rfl

section

variable {p : ğ’³ â¥¤ ğ’®} {S : ğ’®} {C : Type uâ‚ƒ} [Category.{vâ‚ƒ} C] {F : C â¥¤ ğ’³}
  (hF : F â‹™ p = (const C).obj S)

/-- Given a functor `F : C â¥¤ ğ’³` such that `F â‹™ p` is constant at some `S : ğ’®`, then
we get an induced functor `C â¥¤ Fiber p S` that `F` factors through. -/
def inducedFunctor : C â¥¤ Fiber p S where
  obj x := âŸ¨F.obj x, by simp only [â† comp_obj, hF, const_obj_obj]âŸ©
  map Ï† := âŸ¨F.map Ï†, of_commsq _ _ _ (congr_obj hF _) (congr_obj hF _) <|
    by simpa using (eqToIso hF).hom.naturality Ï†âŸ©

/-- Given a functor `F : C â¥¤ ğ’³` such that `F â‹™ p` is constant at some `S : ğ’®`, then
we get a natural isomorphism between `inducedFunctor _ â‹™ fiberInclusion` and `F`. -/
@[simps!]
def inducedFunctorCompIsoSelf : (inducedFunctor hF) â‹™ fiberInclusion â‰… F := .refl _

lemma inducedFunctor_comp : (inducedFunctor hF) â‹™ fiberInclusion = F := rfl

@[simp]
lemma inducedFunctor_comp_obj (X : C) :
    fiberInclusion.obj ((inducedFunctor hF).obj X) = F.obj X := rfl

@[simp]
lemma inducedFunctor_comp_map {X Y : C} (f : X âŸ¶ Y) :
    fiberInclusion.map ((inducedFunctor hF).map f) = F.map f := rfl

end

end Fiber

end Functor

end CategoryTheory
