/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.CategoryTheory.Elementwise
import Mathlib.CategoryTheory.Adjunction.Evaluation
import Mathlib.Tactic.CategoryTheory.Elementwise
import Mathlib.CategoryTheory.Adhesive
import Mathlib.CategoryTheory.Sites.ConcreteSheafification

#align_import category_theory.sites.subsheaf from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!

# Subsheaf of types

We define the sub(pre)sheaf of a type valued presheaf.

## Main results

- `CategoryTheory.GrothendieckTopology.Subpresheaf` :
  A subpresheaf of a presheaf of types.
- `CategoryTheory.GrothendieckTopology.Subpresheaf.sheafify` :
  The sheafification of a subpresheaf as a subpresheaf. Note that this is a sheaf only when the
  whole sheaf is.
- `CategoryTheory.GrothendieckTopology.Subpresheaf.sheafify_isSheaf` :
  The sheafification is a sheaf
- `CategoryTheory.GrothendieckTopology.Subpresheaf.sheafifyLift` :
  The descent of a map into a sheaf to the sheafification.
- `CategoryTheory.GrothendieckTopology.imageSheaf` : The image sheaf of a morphism.
- `CategoryTheory.GrothendieckTopology.imageFactorization` : The image sheaf as a
  `Limits.imageFactorization`.
-/


universe w v u

open Opposite CategoryTheory

namespace CategoryTheory.GrothendieckTopology

variable {C : Type u} [Category.{v} C] (J : GrothendieckTopology C)

/-- A subpresheaf of a presheaf consists of a subset of `F.obj U` for every `U`,
compatible with the restriction maps `F.map i`. -/
@[ext]
structure Subpresheaf (F : C·µí·µñ ‚•§ Type w) where
  /-- If `G` is a sub-presheaf of `F`, then the sections of `G` on `U` forms a subset of sections of
    `F` on `U`. -/
  obj : ‚àÄ U, Set (F.obj U)
  /-- If `G` is a sub-presheaf of `F` and `i : U ‚ü∂ V`, then for each `G`-sections on `U` `x`,
    `F i x` is in `F(V)`. -/
  map : ‚àÄ {U V : C·µí·µñ} (i : U ‚ü∂ V), obj U ‚äÜ F.map i ‚Åª¬π' obj V
#align category_theory.grothendieck_topology.subpresheaf CategoryTheory.GrothendieckTopology.Subpresheaf

variable {F F' F'' : C·µí·µñ ‚•§ Type w} (G G' : Subpresheaf F)

instance : PartialOrder (Subpresheaf F) :=
  PartialOrder.lift Subpresheaf.obj Subpresheaf.ext

instance : Top (Subpresheaf F) :=
  ‚ü®‚ü®fun U => ‚ä§, @fun U V _ x _ => by aesop_cat‚ü©‚ü©

instance : Nonempty (Subpresheaf F) :=
  inferInstance

/-- The subpresheaf as a presheaf. -/
@[simps!]
def Subpresheaf.toPresheaf : C·µí·µñ ‚•§ Type w where
  obj U := G.obj U
  map := @fun U V i x => ‚ü®F.map i x, G.map i x.prop‚ü©
  map_id X := by
    ext ‚ü®x, _‚ü©
    dsimp
    simp only [FunctorToTypes.map_id_apply]
  map_comp := @fun X Y Z i j => by
    ext ‚ü®x, _‚ü©
    dsimp
    simp only [FunctorToTypes.map_comp_apply]
#align category_theory.grothendieck_topology.subpresheaf.to_presheaf CategoryTheory.GrothendieckTopology.Subpresheaf.toPresheaf

instance {U} : CoeHead (G.toPresheaf.obj U) (F.obj U) where
  coe := Subtype.val

/-- The inclusion of a subpresheaf to the original presheaf. -/
@[simps]
def Subpresheaf.Œπ : G.toPresheaf ‚ü∂ F where app U x := x
#align category_theory.grothendieck_topology.subpresheaf.Œπ CategoryTheory.GrothendieckTopology.Subpresheaf.Œπ

instance : Mono G.Œπ :=
  ‚ü®@fun _ f‚ÇÅ f‚ÇÇ e =>
    NatTrans.ext f‚ÇÅ f‚ÇÇ <|
      funext fun U => funext fun x => Subtype.ext <| congr_fun (congr_app e U) x‚ü©

/-- The inclusion of a subpresheaf to a larger subpresheaf -/
@[simps]
def Subpresheaf.homOfLe {G G' : Subpresheaf F} (h : G ‚â§ G') : G.toPresheaf ‚ü∂ G'.toPresheaf where
  app U x := ‚ü®x, h U x.prop‚ü©
#align category_theory.grothendieck_topology.subpresheaf.hom_of_le CategoryTheory.GrothendieckTopology.Subpresheaf.homOfLe

instance {G G' : Subpresheaf F} (h : G ‚â§ G') : Mono (Subpresheaf.homOfLe h) :=
  ‚ü®fun f‚ÇÅ f‚ÇÇ e =>
    NatTrans.ext f‚ÇÅ f‚ÇÇ <|
      funext fun U =>
        funext fun x =>
          Subtype.ext <| (congr_arg Subtype.val <| (congr_fun (congr_app e U) x : _) : _)‚ü©

@[reassoc (attr := simp)]
theorem Subpresheaf.homOfLe_Œπ {G G' : Subpresheaf F} (h : G ‚â§ G') :
    Subpresheaf.homOfLe h ‚â´ G'.Œπ = G.Œπ := by
  ext
  rfl
#align category_theory.grothendieck_topology.subpresheaf.hom_of_le_Œπ CategoryTheory.GrothendieckTopology.Subpresheaf.homOfLe_Œπ

instance : IsIso (Subpresheaf.Œπ (‚ä§ : Subpresheaf F)) := by
  refine @NatIso.isIso_of_isIso_app _ _ _ _ _ _ _ ?_
  intro X
  rw [isIso_iff_bijective]
  exact ‚ü®Subtype.coe_injective, fun x => ‚ü®‚ü®x, _root_.trivial‚ü©, rfl‚ü©‚ü©

theorem Subpresheaf.eq_top_iff_isIso : G = ‚ä§ ‚Üî IsIso G.Œπ := by
  constructor
  ¬∑ rintro rfl
    infer_instance
  ¬∑ intro H
    ext U x
    apply iff_true_iff.mpr
    rw [‚Üê IsIso.inv_hom_id_apply (G.Œπ.app U) x]
    exact ((inv (G.Œπ.app U)) x).2
#align category_theory.grothendieck_topology.subpresheaf.eq_top_iff_is_iso CategoryTheory.GrothendieckTopology.Subpresheaf.eq_top_iff_isIso

/-- If the image of a morphism falls in a subpresheaf, then the morphism factors through it. -/
@[simps!]
def Subpresheaf.lift (f : F' ‚ü∂ F) (hf : ‚àÄ U x, f.app U x ‚àà G.obj U) : F' ‚ü∂ G.toPresheaf where
  app U x := ‚ü®f.app U x, hf U x‚ü©
  naturality := by
    have := elementwise_of% f.naturality
    intros
    refine funext fun x => Subtype.ext ?_
    simp only [toPresheaf_obj, types_comp_apply]
    exact this _ _
#align category_theory.grothendieck_topology.subpresheaf.lift CategoryTheory.GrothendieckTopology.Subpresheaf.lift

@[reassoc (attr := simp)]
theorem Subpresheaf.lift_Œπ (f : F' ‚ü∂ F) (hf : ‚àÄ U x, f.app U x ‚àà G.obj U) :
    G.lift f hf ‚â´ G.Œπ = f := by
  ext
  rfl
#align category_theory.grothendieck_topology.subpresheaf.lift_Œπ CategoryTheory.GrothendieckTopology.Subpresheaf.lift_Œπ

/-- Given a subpresheaf `G` of `F`, an `F`-section `s` on `U`, we may define a sieve of `U`
consisting of all `f : V ‚ü∂ U` such that the restriction of `s` along `f` is in `G`. -/
@[simps]
def Subpresheaf.sieveOfSection {U : C·µí·µñ} (s : F.obj U) : Sieve (unop U) where
  arrows V f := F.map f.op s ‚àà G.obj (op V)
  downward_closed := @fun V W i hi j => by
    simp only [op_unop, op_comp, FunctorToTypes.map_comp_apply]
    exact G.map _ hi
#align category_theory.grothendieck_topology.subpresheaf.sieve_of_section CategoryTheory.GrothendieckTopology.Subpresheaf.sieveOfSection

/-- Given an `F`-section `s` on `U` and a subpresheaf `G`, we may define a family of elements in
`G` consisting of the restrictions of `s` -/
def Subpresheaf.familyOfElementsOfSection {U : C·µí·µñ} (s : F.obj U) :
    (G.sieveOfSection s).1.FamilyOfElements G.toPresheaf := fun _ i hi => ‚ü®F.map i.op s, hi‚ü©
#align category_theory.grothendieck_topology.subpresheaf.family_of_elements_of_section CategoryTheory.GrothendieckTopology.Subpresheaf.familyOfElementsOfSection

theorem Subpresheaf.family_of_elements_compatible {U : C·µí·µñ} (s : F.obj U) :
    (G.familyOfElementsOfSection s).Compatible := by
  intro Y‚ÇÅ Y‚ÇÇ Z g‚ÇÅ g‚ÇÇ f‚ÇÅ f‚ÇÇ h‚ÇÅ h‚ÇÇ e
  refine Subtype.ext ?_ -- Porting note: `ext1` does not work here
  change F.map g‚ÇÅ.op (F.map f‚ÇÅ.op s) = F.map g‚ÇÇ.op (F.map f‚ÇÇ.op s)
  rw [‚Üê FunctorToTypes.map_comp_apply, ‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, ‚Üê op_comp, e]
#align category_theory.grothendieck_topology.subpresheaf.family_of_elements_compatible CategoryTheory.GrothendieckTopology.Subpresheaf.family_of_elements_compatible

theorem Subpresheaf.nat_trans_naturality (f : F' ‚ü∂ G.toPresheaf) {U V : C·µí·µñ} (i : U ‚ü∂ V)
    (x : F'.obj U) : (f.app V (F'.map i x)).1 = F.map i (f.app U x).1 :=
  congr_arg Subtype.val (FunctorToTypes.naturality _ _ f i x)
#align category_theory.grothendieck_topology.subpresheaf.nat_trans_naturality CategoryTheory.GrothendieckTopology.Subpresheaf.nat_trans_naturality

/-- The sheafification of a subpresheaf as a subpresheaf.
Note that this is a sheaf only when the whole presheaf is a sheaf. -/
def Subpresheaf.sheafify : Subpresheaf F where
  obj U := { s | G.sieveOfSection s ‚àà J (unop U) }
  map := by
    rintro U V i s hs
    refine J.superset_covering ?_ (J.pullback_stable i.unop hs)
    intro _ _ h
    dsimp at h ‚ä¢
    rwa [‚Üê FunctorToTypes.map_comp_apply]
#align category_theory.grothendieck_topology.subpresheaf.sheafify CategoryTheory.GrothendieckTopology.Subpresheaf.sheafify

theorem Subpresheaf.le_sheafify : G ‚â§ G.sheafify J := by
  intro U s hs
  change _ ‚àà J _
  convert J.top_mem U.unop -- Porting note: `U.unop` can not be inferred now
  rw [eq_top_iff]
  rintro V i -
  exact G.map i.op hs
#align category_theory.grothendieck_topology.subpresheaf.le_sheafify CategoryTheory.GrothendieckTopology.Subpresheaf.le_sheafify

variable {J}

theorem Subpresheaf.eq_sheafify (h : Presieve.IsSheaf J F) (hG : Presieve.IsSheaf J G.toPresheaf) :
    G = G.sheafify J := by
  apply (G.le_sheafify J).antisymm
  intro U s hs
  suffices ((hG _ hs).amalgamate _ (G.family_of_elements_compatible s)).1 = s by
    rw [‚Üê this]
    exact ((hG _ hs).amalgamate _ (G.family_of_elements_compatible s)).2
  apply (h _ hs).isSeparatedFor.ext
  intro V i hi
  exact (congr_arg Subtype.val ((hG _ hs).valid_glue (G.family_of_elements_compatible s) _ hi) : _)
#align category_theory.grothendieck_topology.subpresheaf.eq_sheafify CategoryTheory.GrothendieckTopology.Subpresheaf.eq_sheafify

theorem Subpresheaf.sheafify_isSheaf (hF : Presieve.IsSheaf J F) :
    Presieve.IsSheaf J (G.sheafify J).toPresheaf := by
  intro U S hS x hx
  let S' := Sieve.bind S fun Y f hf => G.sieveOfSection (x f hf).1
  have := fun (V) (i : V ‚ü∂ U) (hi : S' i) => hi
  -- Porting note: change to explicit variable so that `choose` can find the correct
  -- dependent functions. Thus everything follows need two additional explicit variables.
  choose W i‚ÇÅ i‚ÇÇ hi‚ÇÇ h‚ÇÅ h‚ÇÇ using this
  dsimp [-Sieve.bind_apply] at *
  let x'' : Presieve.FamilyOfElements F S' := fun V i hi => F.map (i‚ÇÅ V i hi).op (x _ (hi‚ÇÇ V i hi))
  have H : ‚àÄ s, x.IsAmalgamation s ‚Üî x''.IsAmalgamation s.1 := by
    intro s
    constructor
    ¬∑ intro H V i hi
      dsimp only [x'', show x'' = fun V i hi => F.map (i‚ÇÅ V i hi).op (x _ (hi‚ÇÇ V i hi)) from rfl]
      conv_lhs => rw [‚Üê h‚ÇÇ _ _ hi]
      rw [‚Üê H _ (hi‚ÇÇ _ _ hi)]
      exact FunctorToTypes.map_comp_apply F (i‚ÇÇ _ _ hi).op (i‚ÇÅ _ _ hi).op _
    ¬∑ intro H V i hi
      refine Subtype.ext ?_
      apply (hF _ (x i hi).2).isSeparatedFor.ext
      intro V' i' hi'
      have hi'' : S' (i' ‚â´ i) := ‚ü®_, _, _, hi, hi', rfl‚ü©
      have := H _ hi''
      rw [op_comp, F.map_comp] at this
      exact this.trans (congr_arg Subtype.val (hx _ _ (hi‚ÇÇ _ _ hi'') hi (h‚ÇÇ _ _ hi'')))
  have : x''.Compatible := by
    intro V‚ÇÅ V‚ÇÇ V‚ÇÉ g‚ÇÅ g‚ÇÇ g‚ÇÉ g‚ÇÑ S‚ÇÅ S‚ÇÇ e
    rw [‚Üê FunctorToTypes.map_comp_apply, ‚Üê FunctorToTypes.map_comp_apply]
    exact
      congr_arg Subtype.val
        (hx (g‚ÇÅ ‚â´ i‚ÇÅ _ _ S‚ÇÅ) (g‚ÇÇ ‚â´ i‚ÇÅ _ _ S‚ÇÇ) (hi‚ÇÇ _ _ S‚ÇÅ) (hi‚ÇÇ _ _ S‚ÇÇ)
        (by simp only [Category.assoc, h‚ÇÇ, e]))
  obtain ‚ü®t, ht, ht'‚ü© := hF _ (J.bind_covering hS fun V i hi => (x i hi).2) _ this
  refine ‚ü®‚ü®t, _‚ü©, (H ‚ü®t, ?_‚ü©).mpr ht, fun y hy => Subtype.ext (ht' _ ((H _).mp hy))‚ü©
  refine J.superset_covering ?_ (J.bind_covering hS fun V i hi => (x i hi).2)
  intro V i hi
  dsimp
  rw [ht _ hi]
  exact h‚ÇÅ _ _ hi
#align category_theory.grothendieck_topology.subpresheaf.sheafify_is_sheaf CategoryTheory.GrothendieckTopology.Subpresheaf.sheafify_isSheaf

theorem Subpresheaf.eq_sheafify_iff (h : Presieve.IsSheaf J F) :
    G = G.sheafify J ‚Üî Presieve.IsSheaf J G.toPresheaf :=
  ‚ü®fun e => e.symm ‚ñ∏ G.sheafify_isSheaf h, G.eq_sheafify h‚ü©
#align category_theory.grothendieck_topology.subpresheaf.eq_sheafify_iff CategoryTheory.GrothendieckTopology.Subpresheaf.eq_sheafify_iff

theorem Subpresheaf.isSheaf_iff (h : Presieve.IsSheaf J F) :
    Presieve.IsSheaf J G.toPresheaf ‚Üî
      ‚àÄ (U) (s : F.obj U), G.sieveOfSection s ‚àà J (unop U) ‚Üí s ‚àà G.obj U := by
  rw [‚Üê G.eq_sheafify_iff h]
  change _ ‚Üî G.sheafify J ‚â§ G
  exact ‚ü®Eq.ge, (G.le_sheafify J).antisymm‚ü©
#align category_theory.grothendieck_topology.subpresheaf.is_sheaf_iff CategoryTheory.GrothendieckTopology.Subpresheaf.isSheaf_iff

theorem Subpresheaf.sheafify_sheafify (h : Presieve.IsSheaf J F) :
    (G.sheafify J).sheafify J = G.sheafify J :=
  ((Subpresheaf.eq_sheafify_iff _ h).mpr <| G.sheafify_isSheaf h).symm
#align category_theory.grothendieck_topology.subpresheaf.sheafify_sheafify CategoryTheory.GrothendieckTopology.Subpresheaf.sheafify_sheafify

/-- The lift of a presheaf morphism onto the sheafification subpresheaf.  -/
noncomputable def Subpresheaf.sheafifyLift (f : G.toPresheaf ‚ü∂ F') (h : Presieve.IsSheaf J F') :
    (G.sheafify J).toPresheaf ‚ü∂ F' where
  app U s := (h (G.sieveOfSection s.1) s.prop).amalgamate
    (_) ((G.family_of_elements_compatible s.1).compPresheafMap f)
  naturality := by
    intro U V i
    ext s
    apply (h _ ((Subpresheaf.sheafify J G).toPresheaf.map i s).prop).isSeparatedFor.ext
    intro W j hj
    refine (Presieve.IsSheafFor.valid_glue (h _ ((G.sheafify J).toPresheaf.map i s).2)
      ((G.family_of_elements_compatible _).compPresheafMap _) _ hj).trans ?_
    dsimp
    conv_rhs => rw [‚Üê FunctorToTypes.map_comp_apply]
    change _ = F'.map (j ‚â´ i.unop).op _
    refine Eq.trans ?_ (Presieve.IsSheafFor.valid_glue (h _ s.2)
      ((G.family_of_elements_compatible s.1).compPresheafMap f) (j ‚â´ i.unop) ?_).symm
    swap -- Porting note: need to swap two goals otherwise the first goal needs to be proven
    -- inside the second goal any way
    ¬∑ dsimp [Presieve.FamilyOfElements.compPresheafMap] at hj ‚ä¢
      rwa [FunctorToTypes.map_comp_apply]
    ¬∑ dsimp [Presieve.FamilyOfElements.compPresheafMap]
      exact congr_arg _ (Subtype.ext (FunctorToTypes.map_comp_apply _ _ _ _).symm)
#align category_theory.grothendieck_topology.subpresheaf.sheafify_lift CategoryTheory.GrothendieckTopology.Subpresheaf.sheafifyLift

theorem Subpresheaf.to_sheafifyLift (f : G.toPresheaf ‚ü∂ F') (h : Presieve.IsSheaf J F') :
    Subpresheaf.homOfLe (G.le_sheafify J) ‚â´ G.sheafifyLift f h = f := by
  ext U s
  apply (h _ ((Subpresheaf.homOfLe (G.le_sheafify J)).app U s).prop).isSeparatedFor.ext
  intro V i hi
  have := elementwise_of% f.naturality
  -- Porting note: filled in some underscores where Lean3 could automatically fill.
  exact (Presieve.IsSheafFor.valid_glue (h _ ((homOfLe (_ : G ‚â§ sheafify J G)).app U s).2)
    ((G.family_of_elements_compatible _).compPresheafMap _) _ hi).trans (this _ _)
#align category_theory.grothendieck_topology.subpresheaf.to_sheafify_lift CategoryTheory.GrothendieckTopology.Subpresheaf.to_sheafifyLift

theorem Subpresheaf.to_sheafify_lift_unique (h : Presieve.IsSheaf J F')
    (l‚ÇÅ l‚ÇÇ : (G.sheafify J).toPresheaf ‚ü∂ F')
    (e : Subpresheaf.homOfLe (G.le_sheafify J) ‚â´ l‚ÇÅ = Subpresheaf.homOfLe (G.le_sheafify J) ‚â´ l‚ÇÇ) :
    l‚ÇÅ = l‚ÇÇ := by
  ext U ‚ü®s, hs‚ü©
  apply (h _ hs).isSeparatedFor.ext
  rintro V i hi
  dsimp at hi
  erw [‚Üê FunctorToTypes.naturality, ‚Üê FunctorToTypes.naturality]
  exact (congr_fun (congr_app e <| op V) ‚ü®_, hi‚ü© : _)
#align category_theory.grothendieck_topology.subpresheaf.to_sheafify_lift_unique CategoryTheory.GrothendieckTopology.Subpresheaf.to_sheafify_lift_unique

theorem Subpresheaf.sheafify_le (h : G ‚â§ G') (hF : Presieve.IsSheaf J F)
    (hG' : Presieve.IsSheaf J G'.toPresheaf) : G.sheafify J ‚â§ G' := by
  intro U x hx
  convert ((G.sheafifyLift (Subpresheaf.homOfLe h) hG').app U ‚ü®x, hx‚ü©).2
  apply (hF _ hx).isSeparatedFor.ext
  intro V i hi
  have :=
    congr_arg (fun f : G.toPresheaf ‚ü∂ G'.toPresheaf => (NatTrans.app f (op V) ‚ü®_, hi‚ü©).1)
      (G.to_sheafifyLift (Subpresheaf.homOfLe h) hG')
  convert this.symm
  erw [‚Üê Subpresheaf.nat_trans_naturality]
  rfl
#align category_theory.grothendieck_topology.subpresheaf.sheafify_le CategoryTheory.GrothendieckTopology.Subpresheaf.sheafify_le

section Image

/-- The image presheaf of a morphism, whose components are the set-theoretic images. -/
@[simps]
def imagePresheaf (f : F' ‚ü∂ F) : Subpresheaf F where
  obj U := Set.range (f.app U)
  map := by
    rintro U V i _ ‚ü®x, rfl‚ü©
    have := elementwise_of% f.naturality
    exact ‚ü®_, this i x‚ü©
#align category_theory.grothendieck_topology.image_presheaf CategoryTheory.GrothendieckTopology.imagePresheaf

@[simp]
theorem top_subpresheaf_obj (U) : (‚ä§ : Subpresheaf F).obj U = ‚ä§ :=
  rfl
#align category_theory.grothendieck_topology.top_subpresheaf_obj CategoryTheory.GrothendieckTopology.top_subpresheaf_obj

@[simp]
theorem imagePresheaf_id : imagePresheaf (ùüô F) = ‚ä§ := by
  ext
  simp
#align category_theory.grothendieck_topology.image_presheaf_id CategoryTheory.GrothendieckTopology.imagePresheaf_id

/-- A morphism factors through the image presheaf. -/
@[simps!]
def toImagePresheaf (f : F' ‚ü∂ F) : F' ‚ü∂ (imagePresheaf f).toPresheaf :=
  (imagePresheaf f).lift f fun _ _ => Set.mem_range_self _
#align category_theory.grothendieck_topology.to_image_presheaf CategoryTheory.GrothendieckTopology.toImagePresheaf

variable (J)

/-- A morphism factors through the sheafification of the image presheaf. -/
@[simps!]
def toImagePresheafSheafify (f : F' ‚ü∂ F) : F' ‚ü∂ ((imagePresheaf f).sheafify J).toPresheaf :=
  toImagePresheaf f ‚â´ Subpresheaf.homOfLe ((imagePresheaf f).le_sheafify J)
#align category_theory.grothendieck_topology.to_image_presheaf_sheafify CategoryTheory.GrothendieckTopology.toImagePresheafSheafify

variable {J}

@[reassoc (attr := simp)]
theorem toImagePresheaf_Œπ (f : F' ‚ü∂ F) : toImagePresheaf f ‚â´ (imagePresheaf f).Œπ = f :=
  (imagePresheaf f).lift_Œπ _ _
#align category_theory.grothendieck_topology.to_image_presheaf_Œπ CategoryTheory.GrothendieckTopology.toImagePresheaf_Œπ

theorem imagePresheaf_comp_le (f‚ÇÅ : F ‚ü∂ F') (f‚ÇÇ : F' ‚ü∂ F'') :
    imagePresheaf (f‚ÇÅ ‚â´ f‚ÇÇ) ‚â§ imagePresheaf f‚ÇÇ := fun U _ hx => ‚ü®f‚ÇÅ.app U hx.choose, hx.choose_spec‚ü©
#align category_theory.grothendieck_topology.image_presheaf_comp_le CategoryTheory.GrothendieckTopology.imagePresheaf_comp_le

instance isIso_toImagePresheaf {F F' : C·µí·µñ ‚•§ TypeMax.{v, w}} (f : F ‚ü∂ F') [hf : Mono f] :
  IsIso (toImagePresheaf f) := by
  have : ‚àÄ (X : C·µí·µñ), IsIso ((toImagePresheaf f).app X) := by
    intro X
    rw [isIso_iff_bijective]
    constructor
    ¬∑ intro x y e
      have := (NatTrans.mono_iff_mono_app _ _).mp hf X
      rw [mono_iff_injective] at this
      exact this (congr_arg Subtype.val e : _)
    ¬∑ rintro ‚ü®_, ‚ü®x, rfl‚ü©‚ü©
      exact ‚ü®x, rfl‚ü©
  apply NatIso.isIso_of_isIso_app

/-- The image sheaf of a morphism between sheaves, defined to be the sheafification of
`image_presheaf`. -/
@[simps]
def imageSheaf {F F' : Sheaf J (Type w)} (f : F ‚ü∂ F') : Sheaf J (Type w) :=
  ‚ü®((imagePresheaf f.1).sheafify J).toPresheaf, by
    rw [isSheaf_iff_isSheaf_of_type]
    apply Subpresheaf.sheafify_isSheaf
    rw [‚Üê isSheaf_iff_isSheaf_of_type]
    exact F'.2‚ü©
#align category_theory.grothendieck_topology.image_sheaf CategoryTheory.GrothendieckTopology.imageSheaf

/-- A morphism factors through the image sheaf. -/
@[simps]
def toImageSheaf {F F' : Sheaf J (Type w)} (f : F ‚ü∂ F') : F ‚ü∂ imageSheaf f :=
  ‚ü®toImagePresheafSheafify J f.1‚ü©
#align category_theory.grothendieck_topology.to_image_sheaf CategoryTheory.GrothendieckTopology.toImageSheaf

/-- The inclusion of the image sheaf to the target. -/
@[simps]
def imageSheafŒπ {F F' : Sheaf J (Type w)} (f : F ‚ü∂ F') : imageSheaf f ‚ü∂ F' :=
  ‚ü®Subpresheaf.Œπ _‚ü©
#align category_theory.grothendieck_topology.image_sheaf_Œπ CategoryTheory.GrothendieckTopology.imageSheafŒπ

@[reassoc (attr := simp)]
theorem toImageSheaf_Œπ {F F' : Sheaf J (Type w)} (f : F ‚ü∂ F') :
    toImageSheaf f ‚â´ imageSheafŒπ f = f := by
  ext1
  simp [toImagePresheafSheafify]
#align category_theory.grothendieck_topology.to_image_sheaf_Œπ CategoryTheory.GrothendieckTopology.toImageSheaf_Œπ

instance {F F' : Sheaf J (Type w)} (f : F ‚ü∂ F') : Mono (imageSheafŒπ f) :=
  (sheafToPresheaf J _).mono_of_mono_map
    (by
      dsimp
      infer_instance)

instance {F F' : Sheaf J (Type w)} (f : F ‚ü∂ F') : Epi (toImageSheaf f) := by
  refine ‚ü®@fun G' g‚ÇÅ g‚ÇÇ e => ?_‚ü©
  ext U ‚ü®s, hx‚ü©
  apply ((isSheaf_iff_isSheaf_of_type J _).mp G'.2 _ hx).isSeparatedFor.ext
  rintro V i ‚ü®y, e'‚ü©
  change (g‚ÇÅ.val.app _ ‚â´ G'.val.map _) _ = (g‚ÇÇ.val.app _ ‚â´ G'.val.map _) _
  rw [‚Üê NatTrans.naturality, ‚Üê NatTrans.naturality]
  have E : (toImageSheaf f).val.app (op V) y = (imageSheaf f).val.map i.op ‚ü®s, hx‚ü© :=
    Subtype.ext e'
  have := congr_arg (fun f : F ‚ü∂ G' => (Sheaf.Hom.val f).app _ y) e
  dsimp at this ‚ä¢
  convert this <;> exact E.symm

/-- The mono factorization given by `image_sheaf` for a morphism. -/
def imageMonoFactorization {F F' : Sheaf J (Type w)} (f : F ‚ü∂ F') : Limits.MonoFactorisation f where
  I := imageSheaf f
  m := imageSheafŒπ f
  e := toImageSheaf f
#align category_theory.grothendieck_topology.image_mono_factorization CategoryTheory.GrothendieckTopology.imageMonoFactorization

/-- The mono factorization given by `image_sheaf` for a morphism is an image. -/
noncomputable def imageFactorization {F F' : Sheaf J TypeMax.{v, u}} (f : F ‚ü∂ F') :
    Limits.ImageFactorisation f where
  F := imageMonoFactorization f
  isImage :=
    { lift := fun I => by
        -- Porting note: need to specify the target category (TypeMax.{v, u}) for this to work.
        haveI M := (Sheaf.Hom.mono_iff_presheaf_mono J TypeMax.{v, u} _).mp I.m_mono
        haveI := isIso_toImagePresheaf I.m.1
        refine ‚ü®Subpresheaf.homOfLe ?_ ‚â´ inv (toImagePresheaf I.m.1)‚ü©
        apply Subpresheaf.sheafify_le
        ¬∑ conv_lhs => rw [‚Üê I.fac]
          apply imagePresheaf_comp_le
        ¬∑ rw [‚Üê isSheaf_iff_isSheaf_of_type]
          exact F'.2
        ¬∑ apply Presieve.isSheaf_iso J (asIso <| toImagePresheaf I.m.1)
          rw [‚Üê isSheaf_iff_isSheaf_of_type]
          exact I.I.2
      lift_fac := fun I => by
        ext1
        dsimp [imageMonoFactorization]
        generalize_proofs h
        rw [‚Üê Subpresheaf.homOfLe_Œπ h, Category.assoc]
        congr 1
        rw [IsIso.inv_comp_eq, toImagePresheaf_Œπ] }
#align category_theory.grothendieck_topology.image_factorization CategoryTheory.GrothendieckTopology.imageFactorization

instance : Limits.HasImages (Sheaf J (Type max v u)) :=
  ‚ü®@fun _ _ f => ‚ü®‚ü®imageFactorization f‚ü©‚ü©‚ü©

end Image

end CategoryTheory.GrothendieckTopology
