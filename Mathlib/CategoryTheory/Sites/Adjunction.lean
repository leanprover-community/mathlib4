/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz, Jo√´l Riou
-/
import Mathlib.CategoryTheory.Adjunction.Restrict
import Mathlib.CategoryTheory.Adjunction.Whiskering
import Mathlib.CategoryTheory.Sites.PreservesSheafification

/-!

In this file, we show that an adjunction `G ‚ä£ F` induces an adjunction between
categories of sheaves. We also show that `G` preserves sheafification.

-/


namespace CategoryTheory

open GrothendieckTopology CategoryTheory Limits Opposite

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] (J : GrothendieckTopology C)
variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
variable {E : Type*} [Category E]
variable {F : D ‚•§ E} {G : E ‚•§ D}

/-- The forgetful functor from `Sheaf J D` to sheaves of types, for a concrete category `D`
whose forgetful functor preserves the correct limits. -/
abbrev sheafForget [ConcreteCategory D] [HasSheafCompose J (forget D)] :
    Sheaf J D ‚•§ SheafOfTypes J :=
  sheafCompose J (forget D) ‚ãô (sheafEquivSheafOfTypes J).functor

namespace Sheaf

noncomputable section

/-- An adjunction `adj : G ‚ä£ F` with `F : D ‚•§ E` and `G : E ‚•§ D` induces an adjunction
between `Sheaf J D` and `Sheaf J E`, in contexts where one can sheafify `D`-valued presheaves,
and postcomposing with `F` preserves the property of being a sheaf. -/
def adjunction [HasWeakSheafify J D] [HasSheafCompose J F] (adj : G ‚ä£ F) :
    composeAndSheafify J G ‚ä£ sheafCompose J F :=
  Adjunction.restrictFullyFaithful ((adj.whiskerRight C·µí·µñ).comp (sheafificationAdjunction J D))
    (fullyFaithfulSheafToPresheaf J E) (Functor.FullyFaithful.id _) (Iso.refl _) (Iso.refl _)

@[simp]
lemma adjunction_unit_app_val [HasWeakSheafify J D] [HasSheafCompose J F] (adj : G ‚ä£ F)
    (X : Sheaf J E) : ((adjunction J adj).unit.app X).val =
      (adj.whiskerRight C·µí·µñ).unit.app _ ‚â´ whiskerRight (toSheafify J (X.val ‚ãô G)) F  := by
  change (sheafToPresheaf _ _).map ((adjunction J adj).unit.app X) = _
  simp only [Functor.id_obj, Functor.comp_obj, whiskeringRight_obj_obj, adjunction,
    Adjunction.map_restrictFullyFaithful_unit_app, Adjunction.comp_unit_app,
    sheafificationAdjunction_unit_app, whiskeringRight_obj_map, Iso.refl_hom, NatTrans.id_app,
    Functor.comp_map, Functor.map_id, whiskerRight_id', Category.comp_id]
  rfl

@[simp]
lemma adjunction_counit_app_val [HasWeakSheafify J D] [HasSheafCompose J F] (adj : G ‚ä£ F)
    (Y : Sheaf J D) : ((adjunction J adj).counit.app Y).val =
      sheafifyLift J (((adj.whiskerRight C·µí·µñ).counit.app Y.val)) Y.cond := by
  change ((ùü≠ (Sheaf _ _)).map ((adjunction J adj).counit.app Y)).val = _
  simp only [Functor.comp_obj, sheafToPresheaf_obj, sheafCompose_obj_val, whiskeringRight_obj_obj,
    adjunction, Adjunction.map_restrictFullyFaithful_counit_app, Iso.refl_inv, NatTrans.id_app,
    Functor.comp_map, whiskeringRight_obj_map, Adjunction.comp_counit_app,
    instCategorySheaf_comp_val, instCategorySheaf_id_val, sheafificationAdjunction_counit_app_val,
    sheafifyMap_sheafifyLift, Functor.id_obj, whiskerRight_id', Category.comp_id, Category.id_comp]


instance [HasWeakSheafify J D] [F.IsRightAdjoint] : (sheafCompose J F).IsRightAdjoint :=
  (adjunction J (Adjunction.ofIsRightAdjoint F)).isRightAdjoint

instance [HasWeakSheafify J D] [G.IsLeftAdjoint] : (composeAndSheafify J G).IsLeftAdjoint :=
  (adjunction J (Adjunction.ofIsLeftAdjoint G)).isLeftAdjoint

lemma preservesSheafification_of_adjunction (adj : G ‚ä£ F) :
    J.PreservesSheafification G where
  le P Q f hf := by
    have := adj.isRightAdjoint
    rw [MorphismProperty.inverseImage_iff]
    dsimp
    intro R hR
    rw [‚Üê ((adj.whiskerRight C·µí·µñ).homEquiv P R).comp_bijective]
    convert (((adj.whiskerRight C·µí·µñ).homEquiv Q R).trans
      (hf.homEquiv (R ‚ãô F) ((sheafCompose J F).obj ‚ü®R, hR‚ü©).cond)).bijective
    ext g X
    -- The rest of this proof was
    -- `dsimp [Adjunction.whiskerRight, Adjunction.mkOfUnitCounit]; simp` before #16317.
    dsimp
    rw [‚Üê NatTrans.comp_app]
    congr
    exact Adjunction.homEquiv_naturality_left _ _ _

instance [G.IsLeftAdjoint] : J.PreservesSheafification G :=
  preservesSheafification_of_adjunction J (Adjunction.ofIsLeftAdjoint G)

section ForgetToType

variable [HasWeakSheafify J D] [ConcreteCategory D] [HasSheafCompose J (forget D)]

/-- This is the functor sending a sheaf of types `X` to the sheafification of `X ‚ãô G`. -/
abbrev composeAndSheafifyFromTypes (G : Type max v‚ÇÅ u‚ÇÅ ‚•§ D) : SheafOfTypes J ‚•§ Sheaf J D :=
  (sheafEquivSheafOfTypes J).inverse ‚ãô composeAndSheafify _ G

/-- A variant of the adjunction between sheaf categories, in the case where the right adjoint
is the forgetful functor to sheaves of types. -/
def adjunctionToTypes {G : Type max v‚ÇÅ u‚ÇÅ ‚•§ D} (adj : G ‚ä£ forget D) :
    composeAndSheafifyFromTypes J G ‚ä£ sheafForget J :=
  (sheafEquivSheafOfTypes J).symm.toAdjunction.comp (adjunction J adj)

@[simp]
theorem adjunctionToTypes_unit_app_val {G : Type max v‚ÇÅ u‚ÇÅ ‚•§ D} (adj : G ‚ä£ forget D)
    (Y : SheafOfTypes J) :
    ((adjunctionToTypes J adj).unit.app Y).val =
      (adj.whiskerRight _).unit.app ((sheafOfTypesToPresheaf J).obj Y) ‚â´
        whiskerRight (toSheafify J _) (forget D) := by
  simp [adjunctionToTypes]
  rfl

@[simp]
theorem adjunctionToTypes_counit_app_val {G : Type max v‚ÇÅ u‚ÇÅ ‚•§ D} (adj : G ‚ä£ forget D)
    (X : Sheaf J D) :
    ((adjunctionToTypes J adj).counit.app X).val =
      sheafifyLift J ((Functor.associator _ _ _).hom ‚â´ (adj.whiskerRight _).counit.app _) X.2 := by
  apply sheafifyLift_unique
  ext
  simp [adjunctionToTypes, sheafEquivSheafOfTypes, Equivalence.symm]

instance [(forget D).IsRightAdjoint] :
    (sheafForget.{_, _, _, _, max u‚ÇÅ v‚ÇÅ} (D := D) J).IsRightAdjoint :=
  (adjunctionToTypes J (Adjunction.ofIsRightAdjoint (forget D))).isRightAdjoint

end ForgetToType

end

end Sheaf

end CategoryTheory
