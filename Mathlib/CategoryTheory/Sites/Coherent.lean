/-
Copyright (c) 2023 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/

import Mathlib.CategoryTheory.Sites.Canonical
import Mathlib.CategoryTheory.Sites.Coverage
import Mathlib.CategoryTheory.Sites.EffectiveEpimorphic
import Mathlib.Data.Fintype.Sigma
/-!

# The Coherent Grothendieck Topology

This file defines the coherent Grothendieck topology (and coverage) on a category `C`.
The category `C` must satisfy a `Precoherent C` condition, which is essentially the minimal
requirement for the coherent coverage to exist.
Given such a category, the coherent coverage is `coherentCoverage C` and the corresponding
Grothendieck topology is `coherentTopology C`.

In `isSheaf_coherent`, we characterize the sheaf condition for presheaves of types for the
coherent Grothendieck topology in terms of finite effective epimorphic families.

## References:
- [Elephant]: *Sketches of an Elephant*, P. T. Johnstone: C2.1, Example 2.1.12.
- [nLab, *Coherent Coverage*](https://ncatlab.org/nlab/show/coherent+coverage)

-/

namespace CategoryTheory

open Limits

variable (C : Type _) [Category C]

/--
The condition `Precoherent C` is essentially the minimal condition required to define the
coherent coverage on `C`.
-/
class Precoherent : Prop where
  /--
  Given an effective epi family `œÄ‚ÇÅ` over `B‚ÇÅ` and a morphism `f : B‚ÇÇ ‚ü∂ B‚ÇÅ`, there exists
  an effective epi family `œÄ‚ÇÇ` over `B‚ÇÇ`, such that `œÄ‚ÇÇ` factors through `œÄ‚ÇÅ`.
  -/
  pullback {B‚ÇÅ B‚ÇÇ : C} (f : B‚ÇÇ ‚ü∂ B‚ÇÅ) :
    ‚àÄ (Œ± : Type) [Fintype Œ±] (X‚ÇÅ : Œ± ‚Üí C) (œÄ‚ÇÅ : (a : Œ±) ‚Üí (X‚ÇÅ a ‚ü∂ B‚ÇÅ)),
      EffectiveEpiFamily X‚ÇÅ œÄ‚ÇÅ ‚Üí
    ‚àÉ (Œ≤ : Type) (_ : Fintype Œ≤) (X‚ÇÇ : Œ≤ ‚Üí C) (œÄ‚ÇÇ : (b : Œ≤) ‚Üí (X‚ÇÇ b ‚ü∂ B‚ÇÇ)),
      EffectiveEpiFamily X‚ÇÇ œÄ‚ÇÇ ‚àß
      ‚àÉ (i : Œ≤ ‚Üí Œ±) (Œπ : (b :  Œ≤) ‚Üí (X‚ÇÇ b ‚ü∂ X‚ÇÅ (i b))),
      ‚àÄ (b : Œ≤), Œπ b ‚â´ œÄ‚ÇÅ _ = œÄ‚ÇÇ _ ‚â´ f

/--
The coherent coverage on a precoherent category `C`.
-/
def coherentCoverage [Precoherent C] : Coverage C where
  covering B := { S | ‚àÉ (Œ± : Type) (_ : Fintype Œ±) (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B)),
    S = Presieve.ofArrows X œÄ ‚àß EffectiveEpiFamily X œÄ }
  pullback := by
    rintro B‚ÇÅ B‚ÇÇ f S ‚ü®Œ±, _, X‚ÇÅ, œÄ‚ÇÅ, rfl, hS‚ü©
    obtain ‚ü®Œ≤,_,X‚ÇÇ,œÄ‚ÇÇ,h,i,Œπ,hh‚ü© := Precoherent.pullback f Œ± X‚ÇÅ œÄ‚ÇÅ hS
    refine ‚ü®Presieve.ofArrows X‚ÇÇ œÄ‚ÇÇ, ‚ü®Œ≤, inferInstance, X‚ÇÇ, œÄ‚ÇÇ, rfl, h‚ü©, ?_‚ü©
    rintro _ _ ‚ü®b‚ü©
    refine ‚ü®(X‚ÇÅ (i b)), Œπ _, œÄ‚ÇÅ _, ‚ü®_‚ü©, hh _‚ü©

/--
The coherent Grothendieck topology on a precoherent category `C`.
-/
def coherentTopology [Precoherent C] : GrothendieckTopology C :=
  Coverage.toGrothendieck _ <| coherentCoverage C

lemma isSheaf_coherent [Precoherent C] (P : C·µí·µñ ‚•§ Type w) :
    Presieve.IsSheaf (coherentTopology C) P ‚Üî
    (‚àÄ (B : C) (Œ± : Type) [Fintype Œ±] (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B)),
      EffectiveEpiFamily X œÄ ‚Üí (Presieve.ofArrows X œÄ).IsSheafFor P) := by
  constructor
  ¬∑ intro hP B Œ± _ X œÄ h
    simp only [coherentTopology, Presieve.isSheaf_coverage] at hP
    apply hP
    refine ‚ü®Œ±, inferInstance, X, œÄ, rfl, h‚ü©
  ¬∑ intro h
    simp only [coherentTopology, Presieve.isSheaf_coverage]
    rintro B S ‚ü®Œ±, _, X, œÄ, rfl, hS‚ü©
    exact h _ _ _ _ hS


namespace coherentTopology

variable {C : Type _} [Category C] [Precoherent C]

variable {X : C}
/--
For a precoherent category, any sieve that contains an `EffectiveEpiFamily` is a sieve of the
coherent topology.
Note: This is one direction of `mem_sieves_iff_hasEffectiveEpiFamily`, but is needed for the proof.
-/
theorem mem_sieves_of_hasEffectiveEpiFamily (S : Sieve X) :
    (‚àÉ (Œ± : Type) (_ : Fintype Œ±) (Y : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (Y a ‚ü∂ X)),
        EffectiveEpiFamily Y œÄ ‚àß (‚àÄ a : Œ±, (S.arrows) (œÄ a)) ) ‚Üí
          (S ‚àà GrothendieckTopology.sieves (coherentTopology C) X) := by
  rintro ‚ü®Œ±, ‚ü®h, ‚ü®Y, ‚ü®œÄ, hœÄ‚ü©‚ü©‚ü©‚ü©
  have h_le : Sieve.generate (Presieve.ofArrows _ œÄ) ‚â§ S := by
      rw [Sieve.sets_iff_generate (Presieve.ofArrows _ œÄ) S]
      apply Presieve.le_of_factorsThru_sieve (Presieve.ofArrows (fun i => Y i) œÄ) S _
      intro Y g f
      use Y, ùüô Y
      rcases f with ‚ü®i‚ü©
      exact ‚ü®œÄ i, ‚ü®hœÄ.2 i,Category.id_comp (œÄ i) ‚ü©‚ü©
  apply Coverage.saturate_of_superset (coherentCoverage C) h_le
  exact Coverage.saturate.of X _ ‚ü®Œ±, inferInstance, Y, œÄ, ‚ü®rfl, hœÄ.1‚ü©‚ü©

/-- Every Yoneda-presheaf is a sheaf for the coherent topology. -/
theorem isSheaf_yoneda_obj (W : C) : Presieve.IsSheaf (coherentTopology C) (yoneda.obj W) := by
  rw [isSheaf_coherent]
  intro X Œ± _ Y œÄ H
  have h_colim := isColimitOfEffectiveEpiFamilyStruct Y œÄ H.effectiveEpiFamily.some
  rw [‚ÜêSieve.generateFamily_eq] at h_colim
  intro x hx
  let x_ext := Presieve.FamilyOfElements.sieveExtend x
  have hx_ext := Presieve.FamilyOfElements.Compatible.sieveExtend hx
  let S := Sieve.generate (Presieve.ofArrows Y œÄ)
  have := (Sieve.forallYonedaIsSheaf_iff_colimit S).mpr ‚ü®h_colim‚ü© W x_ext hx_ext
  rcases this with ‚ü®t, t_amalg, t_uniq‚ü©
  refine ‚ü®t, ?_, ?_‚ü©
  ¬∑ convert Presieve.isAmalgamation_restrict (Sieve.le_generate (Presieve.ofArrows Y œÄ)) _ _ t_amalg
    exact (Presieve.restrict_extend hx).symm
  ¬∑ exact fun y hy => t_uniq y <| Presieve.isAmalgamation_sieveExtend x y hy

/-- The coherent topology on a precoherent category is subcanonical. -/
theorem isSubcanonical : Sheaf.Subcanonical (coherentTopology C) :=
  Sheaf.Subcanonical.of_yoneda_isSheaf _ isSheaf_yoneda_obj

end coherentTopology

variable {C : Type _} [Category C] [Precoherent C]

variable {X : C}

/--
Effective epi families in a precoherent category are transitive, in the sense that an
`EffectiveEpiFamily` and an `EffectiveEpiFamily` over each member, the composition is an
`EffectiveEpiFamily`.
Note: The finiteness condition is an artifact of the proof and is probably unneccessary
-/
theorem EffectiveEpiFamily.transitive_of_finite {Œ± : Type} [Fintype Œ±] {Y : Œ± ‚Üí C}
    (œÄ : (a : Œ±) ‚Üí (Y a ‚ü∂ X)) (h : EffectiveEpiFamily Y œÄ) {Œ≤ : Œ± ‚Üí Type} [‚àÄ (a: Œ±), Fintype (Œ≤ a)]
    {Y_n : (a : Œ±) ‚Üí Œ≤ a ‚Üí C} (œÄ_n : (a : Œ±) ‚Üí (b : Œ≤ a) ‚Üí (Y_n a b ‚ü∂ Y a))
    (H : ‚àÄ a, EffectiveEpiFamily (Y_n a) (œÄ_n a)) :
EffectiveEpiFamily (fun (c : Œ£ a, Œ≤ a) => Y_n c.fst c.snd) (fun c => œÄ_n c.fst c.snd ‚â´ œÄ c.fst)
    := by
  rw [‚Üê Sieve.effectiveEpimorphic_family]
  suffices h‚ÇÇ : (Sieve.generate (Presieve.ofArrows (fun (‚ü®a, b‚ü© : Œ£ _, Œ≤ _) => Y_n a b)
        (fun ‚ü®a,b‚ü© => œÄ_n a b ‚â´ œÄ a))) ‚àà GrothendieckTopology.sieves (coherentTopology C) X by
    change Nonempty _
    rw [‚Üê Sieve.forallYonedaIsSheaf_iff_colimit]
    exact fun W => coherentTopology.isSheaf_yoneda_obj W _ h‚ÇÇ
  let h' := h
  rw [‚Üê Sieve.effectiveEpimorphic_family] at h'
  let H' := H
  conv at H' =>
    intro a
    rw [‚Üê Sieve.effectiveEpimorphic_family]
  -- Show that a covering sieve is a colimit, which implies the original set of arrows is regular
  -- epimorphic. We use the transitivity property of saturation
  apply Coverage.saturate.transitive X (Sieve.generate (Presieve.ofArrows Y œÄ))
  ¬∑ apply Coverage.saturate.of
    use Œ±, inferInstance, Y, œÄ
    simp only [true_and]
    exact Iff.mp (Sieve.effectiveEpimorphic_family Y œÄ) h'
  ¬∑ intro V f ‚ü®Y‚ÇÅ, h, g, ‚ü®hY, hf‚ü©‚ü©
    rw [‚Üê hf, Sieve.pullback_comp]
    apply (coherentTopology C).pullback_stable'
    apply coherentTopology.mem_sieves_of_hasEffectiveEpiFamily
    -- Need to show that the pullback of the family `œÄ_n` to a given `Y i` is effective epimorphic
    rcases hY with ‚ü®i‚ü©
    use Œ≤ i, inferInstance, Y_n i, œÄ_n i
    constructor
    ¬∑ exact H i
    ¬∑ intro b
      use Y_n i b, (ùüô _), œÄ_n i b ‚â´ œÄ i
      constructor
      ¬∑ exact ‚ü®(‚ü®i, b‚ü© : Œ£ (i : Œ±), Œ≤ i)‚ü©
      ¬∑ exact Category.id_comp (œÄ_n i b ‚â´ œÄ i)

/--
A sieve belongs to the coherent topology if and only if it contains a finite
`EffectiveEpiFamily`
-/
theorem coherentTopology.mem_sieves_iff_hasEffectiveEpiFamily (S : Sieve X) :
(S ‚àà GrothendieckTopology.sieves (coherentTopology C) X) ‚Üî
    (‚àÉ (Œ± : Type) (_ : Fintype Œ±) (Y : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (Y a ‚ü∂ X)),
        EffectiveEpiFamily Y œÄ ‚àß (‚àÄ a : Œ±, (S.arrows) (œÄ a)) )  := by
  constructor
  ¬∑ intro h
    induction' h with Y T hS  Y Y R S _ _ a b
    ¬∑ rcases hS with ‚ü®a, h, Y', œÄ, h'‚ü©
      use a, h, Y', œÄ
      constructor
      ¬∑ tauto
      ¬∑ intro a'
        cases' h' with h_left h_right
        simp only [Sieve.generate_apply]
        use Y' a', ùüô Y' a', œÄ a'
        constructor
        ¬∑ rw [h_left]
          exact Presieve.ofArrows.mk a'
        ¬∑ apply Category.id_comp
    ¬∑ use Unit, Unit.fintype, fun _ => Y, fun _ => (ùüô Y)
      cases' S with arrows downward_closed
      exact ‚ü®inferInstance, by simp only [Sieve.top_apply, forall_const]‚ü©
    ¬∑ rcases a with ‚ü®Œ±, w, Y‚ÇÅ, œÄ, ‚ü®h‚ÇÅ,h‚ÇÇ‚ü©‚ü©
      have H  := fun a => b (h‚ÇÇ a)
      rw [Classical.skolem] at H
      rcases H with ‚ü®Œ≤, H‚ü©
      rw [Classical.skolem] at H
      rcases H with ‚ü®_, H‚ü©
      rw [Classical.skolem] at H
      rcases H with ‚ü®Y_n, H‚ü©
      rw [Classical.skolem] at H
      rcases H with ‚ü®œÄ_n, H‚ü©
      use (Œ£ a, Œ≤ a), inferInstance, fun ‚ü®a,b‚ü© => Y_n a b, fun ‚ü®a, b‚ü© => (œÄ_n a b) ‚â´ (œÄ a)
      constructor
      ¬∑ apply EffectiveEpiFamily.transitive_of_finite
        ¬∑ exact h‚ÇÅ
        ¬∑ exact fun a => (H a).1
      ¬∑ exact fun c => (H c.fst).2 c.snd
  ¬∑ exact coherentTopology.mem_sieves_of_hasEffectiveEpiFamily S

end CategoryTheory
