/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import Mathlib.CategoryTheory.Sites.Sheaf

/-!

# The plus construction for presheaves.

This file contains the construction of `P‚Å∫`, for a presheaf `P : C·µí·µñ ‚•§ D`
where `C` is endowed with a grothendieck topology `J`.

See <https://stacks.math.columbia.edu/tag/00W1> for details.

-/


namespace CategoryTheory.GrothendieckTopology

open CategoryTheory

open CategoryTheory.Limits

open Opposite

universe w' w v u

variable {C : Type u} [Category.{v} C] (J : GrothendieckTopology C)
variable {D : Type w} [Category.{w'} D]

noncomputable section

variable [‚àÄ (P : C·µí·µñ ‚•§ D) (X : C) (S : J.Cover X), HasMultiequalizer (S.index P)]
variable (P : C·µí·µñ ‚•§ D)

/-- The diagram whose colimit defines the values of `plus`. -/
@[simps]
def diagram (X : C) : (J.Cover X)·µí·µñ ‚•§ D where
  obj S := multiequalizer (S.unop.index P)
  map {S _} f :=
    Multiequalizer.lift _ _ (fun I ‚Ü¶ Multiequalizer.Œπ (S.unop.index P) (I.map f.unop))
      (fun I ‚Ü¶ Multiequalizer.condition (S.unop.index P) (Cover.Relation.mk' (I.r.map f.unop)))

/-- A helper definition used to define the morphisms for `plus`. -/
@[simps]
def diagramPullback {X Y : C} (f : X ‚ü∂ Y) : J.diagram P Y ‚ü∂ (J.pullback f).op ‚ãô J.diagram P X where
  app S :=
    Multiequalizer.lift _ _ (fun I ‚Ü¶ Multiequalizer.Œπ (S.unop.index P) I.base) fun I ‚Ü¶
      Multiequalizer.condition (S.unop.index P) (Cover.Relation.mk' I.r.base)
  naturality S T f := Multiequalizer.hom_ext _ _ _ (fun I ‚Ü¶ by simp; rfl)

/-- A natural transformation `P ‚ü∂ Q` induces a natural transformation
between diagrams whose colimits define the values of `plus`. -/
@[simps]
def diagramNatTrans {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (X : C) : J.diagram P X ‚ü∂ J.diagram Q X where
  app W :=
    Multiequalizer.lift _ _ (fun _ ‚Ü¶ Multiequalizer.Œπ _ _ ‚â´ Œ∑.app _) (fun i ‚Ü¶ by
      dsimp only
      erw [Category.assoc, Category.assoc, ‚Üê Œ∑.naturality, ‚Üê Œ∑.naturality,
        Multiequalizer.condition_assoc]
      rfl)

@[simp]
theorem diagramNatTrans_id (X : C) (P : C·µí·µñ ‚•§ D) :
    J.diagramNatTrans (ùüô P) X = ùüô (J.diagram P X) := by
  ext : 2
  refine Multiequalizer.hom_ext _ _ _ (fun i ‚Ü¶ ?_)
  simp

@[simp]
theorem diagramNatTrans_zero [Preadditive D] (X : C) (P Q : C·µí·µñ ‚•§ D) :
    J.diagramNatTrans (0 : P ‚ü∂ Q) X = 0 := by
  ext : 2
  refine Multiequalizer.hom_ext _ _ _ (fun i ‚Ü¶ ?_)
  simp

@[simp]
theorem diagramNatTrans_comp {P Q R : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (Œ≥ : Q ‚ü∂ R) (X : C) :
    J.diagramNatTrans (Œ∑ ‚â´ Œ≥) X = J.diagramNatTrans Œ∑ X ‚â´ J.diagramNatTrans Œ≥ X := by
  ext : 2
  refine Multiequalizer.hom_ext _ _ _ (fun i ‚Ü¶ ?_)
  simp

variable (D) in
/-- `J.diagram P`, as a functor in `P`. -/
@[simps]
def diagramFunctor (X : C) : (C·µí·µñ ‚•§ D) ‚•§ (J.Cover X)·µí·µñ ‚•§ D where
  obj P := J.diagram P X
  map Œ∑ := J.diagramNatTrans Œ∑ X

variable [‚àÄ X : C, HasColimitsOfShape (J.Cover X)·µí·µñ D]

/-- The plus construction, associating a presheaf to any presheaf.
See `plusFunctor` below for a functorial version. -/
def plusObj : C·µí·µñ ‚•§ D where
  obj X := colimit (J.diagram P X.unop)
  map f := colimMap (J.diagramPullback P f.unop) ‚â´ colimit.pre _ _
  map_id := by
    intro X
    refine colimit.hom_ext (fun S ‚Ü¶ ?_)
    dsimp
    simp only [diagramPullback_app, colimit.Œπ_pre, Œπ_colimMap_assoc, Category.comp_id]
    let e := S.unop.pullbackId
    dsimp only [Functor.op, pullback_obj]
    rw [‚Üê colimit.w _ e.inv.op, ‚Üê Category.assoc]
    convert Category.id_comp (colimit.Œπ (diagram J P (unop X)) S)
    refine Multiequalizer.hom_ext _ _ _ (fun I ‚Ü¶ ?_)
    dsimp
    simp only [Multiequalizer.lift_Œπ, Category.id_comp, Category.assoc]
    dsimp [Cover.Arrow.map, Cover.Arrow.base]
    cases I
    congr
    simp
  map_comp := by
    intro X Y Z f g
    refine colimit.hom_ext (fun S ‚Ü¶ ?_)
    dsimp
    simp only [diagramPullback_app, colimit.Œπ_pre_assoc, colimit.Œπ_pre, Œπ_colimMap_assoc,
      Category.assoc]
    let e := S.unop.pullbackComp g.unop f.unop
    dsimp only [Functor.op, pullback_obj]
    rw [‚Üê colimit.w _ e.inv.op, ‚Üê Category.assoc, ‚Üê Category.assoc]
    congr 1
    refine Multiequalizer.hom_ext _ _ _ (fun I ‚Ü¶ ?_)
    dsimp
    simp only [Multiequalizer.lift_Œπ, Category.assoc]
    cases I
    dsimp only [Cover.Arrow.base, Cover.Arrow.map]
    congr 2
    simp

/-- An auxiliary definition used in `plus` below. -/
def plusMap {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) : J.plusObj P ‚ü∂ J.plusObj Q where
  app X := colimMap (J.diagramNatTrans Œ∑ X.unop)
  naturality := by
    intro X Y f
    dsimp [plusObj]
    ext
    simp only [diagramPullback_app, Œπ_colimMap, colimit.Œπ_pre_assoc, colimit.Œπ_pre,
      Œπ_colimMap_assoc, Category.assoc]
    simp_rw [‚Üê Category.assoc]
    congr 1
    exact Multiequalizer.hom_ext _ _ _ (fun I ‚Ü¶ by simp)

@[simp]
theorem plusMap_id (P : C·µí·µñ ‚•§ D) : J.plusMap (ùüô P) = ùüô _ := by
  ext : 2
  dsimp only [plusMap, plusObj]
  rw [J.diagramNatTrans_id, NatTrans.id_app]
  ext
  simp

@[simp]
theorem plusMap_zero [Preadditive D] (P Q : C·µí·µñ ‚•§ D) : J.plusMap (0 : P ‚ü∂ Q) = 0 := by
  ext : 2
  refine colimit.hom_ext (fun S ‚Ü¶ ?_)
  erw [comp_zero, colimit.Œπ_map, J.diagramNatTrans_zero, zero_comp]

@[simp, reassoc]
theorem plusMap_comp {P Q R : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (Œ≥ : Q ‚ü∂ R) :
    J.plusMap (Œ∑ ‚â´ Œ≥) = J.plusMap Œ∑ ‚â´ J.plusMap Œ≥ := by
  ext : 2
  refine colimit.hom_ext (fun S ‚Ü¶ ?_)
  simp [plusMap, J.diagramNatTrans_comp]

variable (D) in
/-- The plus construction, a functor sending `P` to `J.plusObj P`. -/
@[simps]
def plusFunctor : (C·µí·µñ ‚•§ D) ‚•§ C·µí·µñ ‚•§ D where
  obj P := J.plusObj P
  map Œ∑ := J.plusMap Œ∑

/-- The canonical map from `P` to `J.plusObj P`.
See `toPlusNatTrans` for a functorial version. -/
def toPlus : P ‚ü∂ J.plusObj P where
  app X := Cover.toMultiequalizer (‚ä§ : J.Cover X.unop) P ‚â´ colimit.Œπ (J.diagram P X.unop) (op ‚ä§)
  naturality := by
    intro X Y f
    dsimp [plusObj]
    delta Cover.toMultiequalizer
    simp only [diagramPullback_app, colimit.Œπ_pre, Œπ_colimMap_assoc, Category.assoc]
    dsimp only [Functor.op, unop_op]
    let e : (J.pullback f.unop).obj ‚ä§ ‚ü∂ ‚ä§ := homOfLE (OrderTop.le_top _)
    rw [‚Üê colimit.w _ e.op, ‚Üê Category.assoc, ‚Üê Category.assoc, ‚Üê Category.assoc]
    congr 1
    refine Multiequalizer.hom_ext _ _ _ (fun I ‚Ü¶ ?_)
    simp only [Category.assoc]
    dsimp [Cover.Arrow.base]
    simp

@[reassoc (attr := simp)]
theorem toPlus_naturality {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) :
    Œ∑ ‚â´ J.toPlus Q = J.toPlus _ ‚â´ J.plusMap Œ∑ := by
  ext
  dsimp [toPlus, plusMap]
  delta Cover.toMultiequalizer
  simp only [Œπ_colimMap, Category.assoc]
  simp_rw [‚Üê Category.assoc]
  congr 1
  exact Multiequalizer.hom_ext _ _ _ (fun I ‚Ü¶ by simp)

variable (D) in
/-- The natural transformation from the identity functor to `plus`. -/
@[simps]
def toPlusNatTrans : ùü≠ (C·µí·µñ ‚•§ D) ‚ü∂ J.plusFunctor D where
  app P := J.toPlus P

/-- `(P ‚ü∂ P‚Å∫)‚Å∫ = P‚Å∫ ‚ü∂ P‚Å∫‚Å∫` -/
@[simp]
theorem plusMap_toPlus : J.plusMap (J.toPlus P) = J.toPlus (J.plusObj P) := by
  ext X : 2
  refine colimit.hom_ext (fun S ‚Ü¶ ?_)
  dsimp only [plusMap, toPlus]
  let e : S.unop ‚ü∂ ‚ä§ := homOfLE (OrderTop.le_top _)
  rw [Œπ_colimMap, ‚Üê colimit.w _ e.op, ‚Üê Category.assoc, ‚Üê Category.assoc]
  congr 1
  refine Multiequalizer.hom_ext _ _ _ (fun I ‚Ü¶ ?_)
  erw [Multiequalizer.lift_Œπ]
  simp only [unop_op, op_unop, diagram_map, Category.assoc, limit.lift_œÄ,
    Multifork.ofŒπ_œÄ_app]
  let ee : (J.pullback (I.map e).f).obj S.unop ‚ü∂ ‚ä§ := homOfLE (OrderTop.le_top _)
  erw [‚Üê colimit.w _ ee.op, Œπ_colimMap_assoc, colimit.Œπ_pre, diagramPullback_app,
    ‚Üê Category.assoc, ‚Üê Category.assoc]
  congr 1
  refine Multiequalizer.hom_ext _ _ _ (fun II ‚Ü¶ ?_)
  convert Multiequalizer.condition (S.unop.index P)
    { fst := I, snd := II.base, r.Z := II.Y, r.g‚ÇÅ := II.f, r.g‚ÇÇ := ùüô II.Y } using 1
  all_goals simp

theorem isIso_toPlus_of_isSheaf (hP : Presheaf.IsSheaf J P) : IsIso (J.toPlus P) := by
  rw [Presheaf.isSheaf_iff_multiequalizer] at hP
  suffices ‚àÄ X, IsIso ((J.toPlus P).app X) from NatIso.isIso_of_isIso_app _
  intro X
  refine IsIso.comp_isIso' inferInstance ?_
  suffices ‚àÄ (S T : (J.Cover X.unop)·µí·µñ) (f : S ‚ü∂ T), IsIso ((J.diagram P X.unop).map f) from
    isIso_Œπ_of_isInitial (initialOpOfTerminal isTerminalTop) _
  intro S T e
  have : S.unop.toMultiequalizer P ‚â´ (J.diagram P X.unop).map e = T.unop.toMultiequalizer P :=
    Multiequalizer.hom_ext _ _ _ (fun II ‚Ü¶ by simp)
  have :
    (J.diagram P X.unop).map e = inv (S.unop.toMultiequalizer P) ‚â´ T.unop.toMultiequalizer P := by
    simp [‚Üê this]
  rw [this]
  infer_instance

/-- The natural isomorphism between `P` and `P‚Å∫` when `P` is a sheaf. -/
def isoToPlus (hP : Presheaf.IsSheaf J P) : P ‚âÖ J.plusObj P :=
  letI := isIso_toPlus_of_isSheaf J P hP
  asIso (J.toPlus P)

@[simp]
theorem isoToPlus_hom (hP : Presheaf.IsSheaf J P) : (J.isoToPlus P hP).hom = J.toPlus P :=
  rfl

/-- Lift a morphism `P ‚ü∂ Q` to `P‚Å∫ ‚ü∂ Q` when `Q` is a sheaf. -/
def plusLift {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (hQ : Presheaf.IsSheaf J Q) : J.plusObj P ‚ü∂ Q :=
  J.plusMap Œ∑ ‚â´ (J.isoToPlus Q hQ).inv

@[reassoc (attr := simp)]
theorem toPlus_plusLift {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (hQ : Presheaf.IsSheaf J Q) :
    J.toPlus P ‚â´ J.plusLift Œ∑ hQ = Œ∑ := by
  dsimp [plusLift]
  rw [‚Üê Category.assoc]
  rw [Iso.comp_inv_eq]
  dsimp only [isoToPlus, asIso]
  rw [toPlus_naturality]

theorem plusLift_unique {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (hQ : Presheaf.IsSheaf J Q)
    (Œ≥ : J.plusObj P ‚ü∂ Q) (hŒ≥ : J.toPlus P ‚â´ Œ≥ = Œ∑) : Œ≥ = J.plusLift Œ∑ hQ := by
  dsimp only [plusLift]
  rw [Iso.eq_comp_inv, ‚Üê hŒ≥, plusMap_comp]
  simp

theorem plus_hom_ext {P Q : C·µí·µñ ‚•§ D} (Œ∑ Œ≥ : J.plusObj P ‚ü∂ Q) (hQ : Presheaf.IsSheaf J Q)
    (h : J.toPlus P ‚â´ Œ∑ = J.toPlus P ‚â´ Œ≥) : Œ∑ = Œ≥ := by
  have : Œ≥ = J.plusLift (J.toPlus P ‚â´ Œ≥) hQ := by
    apply plusLift_unique
    rfl
  rw [this]
  apply plusLift_unique
  exact h

@[simp]
theorem isoToPlus_inv (hP : Presheaf.IsSheaf J P) :
    (J.isoToPlus P hP).inv = J.plusLift (ùüô _) hP := by
  apply J.plusLift_unique
  rw [Iso.comp_inv_eq, Category.id_comp]
  rfl

@[simp]
theorem plusMap_plusLift {P Q R : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (Œ≥ : Q ‚ü∂ R) (hR : Presheaf.IsSheaf J R) :
    J.plusMap Œ∑ ‚â´ J.plusLift Œ≥ hR = J.plusLift (Œ∑ ‚â´ Œ≥) hR := by
  apply J.plusLift_unique
  rw [‚Üê Category.assoc, ‚Üê J.toPlus_naturality, Category.assoc, J.toPlus_plusLift]

instance plusFunctor_preservesZeroMorphisms [Preadditive D] :
    (plusFunctor J D).PreservesZeroMorphisms where
  map_zero F G := by
    ext
    dsimp
    rw [J.plusMap_zero, NatTrans.app_zero]

end

end CategoryTheory.GrothendieckTopology
