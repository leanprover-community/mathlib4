/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.CategoryTheory.Sites.Sheafification
import Mathlib.CategoryTheory.Limits.KanExtension
import Mathlib.CategoryTheory.Sites.CoverPreserving

#align_import category_theory.sites.cover_lifting from "leanprover-community/mathlib"@"14b69e9f3c16630440a2cbd46f1ddad0d561dee7"

/-!
# Cover-lifting functors between sites.

We define cover-lifting functors between sites as functors that pull covering sieves back to
covering sieves. This concept is also known as *cocontinuous functors* or
*cover-reflecting functors*, but we have chosen this name following [MM92] in order to avoid
potential naming collision or confusion with the general definition of cocontinuous functors
between categories as functors preserving small colimits.

The definition given here seems stronger than the definition found elsewhere,
but they are actually equivalent via `CategoryTheory.GrothendieckTopology.superset_covering`.
(The precise statement is not formalized, but follows from it quite trivially).

## Main definitions

* `CategoryTheory.CoverLifting`: a functor between sites is cover-lifting if it
  pulls back covering sieves to covering sieves
* `CategoryTheory.Sites.copullback`: A cover-lifting functor `G : (C, J) ‚•§ (D, K)` induces a
  morphism of sites in the same direction as the functor.

## Main results
* `CategoryTheory.ran_isSheaf_of_coverLifting`: If `G : C ‚•§ D` is cover_lifting, then
  `Ran G.op` (`‚Çöu`) as a functor `(C·µí·µñ ‚•§ A) ‚•§ (D·µí·µñ ‚•§ A)` of presheaves maps sheaves to sheaves.
* `CategoryTheory.Sites.pullbackCopullbackAdjunction`: If `G : (C, J) ‚•§ (D, K)` is cover-lifting,
  cover-preserving, and compatible-preserving, then `pullback G` and `copullback G` are adjoint.

## References

* [Elephant]: *Sketches of an Elephant*, P. T. Johnstone: C2.3.
* [S. MacLane, I. Moerdijk, *Sheaves in Geometry and Logic*][MM92]
* https://stacks.math.columbia.edu/tag/00XI

-/


universe w v v‚ÇÅ v‚ÇÇ v‚ÇÉ u u‚ÇÅ u‚ÇÇ u‚ÇÉ

noncomputable section

open CategoryTheory

open Opposite

open CategoryTheory.Presieve.FamilyOfElements

open CategoryTheory.Presieve

open CategoryTheory.Limits

namespace CategoryTheory

section CoverLifting

variable {C : Type*} [Category C] {D : Type*} [Category D] {E : Type*} [Category E]

variable (J : GrothendieckTopology C) (K : GrothendieckTopology D)

variable {L : GrothendieckTopology E}

/-- A functor `G : (C, J) ‚•§ (D, K)` between sites is called to have the cover-lifting property
if for all covering sieves `R` in `D`, `R.pullback G` is a covering sieve in `C`.
-/
-- porting note: removed `@[nolint has_nonempty_instance]`
structure CoverLifting (G : C ‚•§ D) : Prop where
  cover_lift : ‚àÄ {U : C} {S : Sieve (G.obj U)} (_ : S ‚àà K (G.obj U)), S.functorPullback G ‚àà J U
#align category_theory.cover_lifting CategoryTheory.CoverLifting

/-- The identity functor on a site is cover-lifting. -/
theorem idCoverLifting : CoverLifting J J (ùü≠ _) :=
  ‚ü®fun h => by simpa using h‚ü©
#align category_theory.id_cover_lifting CategoryTheory.idCoverLifting

variable {J K}

/-- The composition of two cover-lifting functors are cover-lifting -/
theorem compCoverLifting {F : C ‚•§ D} (hu : CoverLifting J K F) {G : D ‚•§ E}
    (hv : CoverLifting K L G) : CoverLifting J L (F ‚ãô G) :=
  ‚ü®fun h => hu.cover_lift (hv.cover_lift h)‚ü©
#align category_theory.comp_cover_lifting CategoryTheory.compCoverLifting

end CoverLifting

/-!
We will now prove that `Ran G.op` (`‚Çöu`) maps sheaves to sheaves if `G` is cover-lifting. This can
be found in <https://stacks.math.columbia.edu/tag/00XK>. However, the proof given here uses the
amalgamation definition of sheaves, and thus does not require that `C` or `D` has categorical
pullbacks.

For the following proof sketch, `‚äÜ` denotes the homs on `C` and `D` as in the topological analogy.
By definition, the presheaf `ùí¢ : D·µí·µñ ‚•§ A` is a sheaf if for every sieve `S` of `U : D`, and every
compatible family of morphisms `X ‚ü∂ ùí¢(V)` for each `V ‚äÜ U : S` with a fixed source `X`,
we can glue them into a morphism `X ‚ü∂ ùí¢(U)`.

Since the presheaf `ùí¢ := (Ran G.op).obj ‚Ñ±.val` is defined via `ùí¢(U) = lim_{G(V) ‚äÜ U} ‚Ñ±(V)`, for
gluing the family `x` into a `X ‚ü∂ ùí¢(U)`, it suffices to provide a `X ‚ü∂ ‚Ñ±(Y)` for each
`G(Y) ‚äÜ U`. This can be done since `{ Y' ‚äÜ Y : G(Y') ‚äÜ U ‚àà S}` is a covering sieve for `Y` on
`C` (by the cover-lifting property of `G`). Thus the morphisms `X ‚ü∂ ùí¢(G(Y')) ‚ü∂ ‚Ñ±(Y')` can be
glued into a morphism `X ‚ü∂ ‚Ñ±(Y)`. This is done in `get_sections`.

In `glued_limit_cone`, we verify these obtained sections are indeed compatible, and thus we obtain
A `X ‚ü∂ ùí¢(U)`. The remaining work is to verify that this is indeed the amalgamation and is unique.
-/


variable {C D : Type u} [Category.{v} C] [Category.{v} D]

variable {A : Type w} [Category.{max u v} A] [HasLimits A]

variable {J : GrothendieckTopology C} {K : GrothendieckTopology D}

namespace RanIsSheafOfCoverLifting

variable {G : C ‚•§ D} (hu : CoverLifting J K G) (‚Ñ± : Sheaf J A)

variable {X : A} {U : D} (S : Sieve U) (hS : S ‚àà K U)

instance (X : D·µí·µñ) : HasLimitsOfShape (StructuredArrow X G.op) A :=
  haveI := Limits.hasLimitsOfSizeShrink.{v, max u v, max u v, max u v} A
  HasLimitsOfSize.has_limits_of_shape _

variable (x : S.arrows.FamilyOfElements ((ran G.op).obj ‚Ñ±.val ‚ãô coyoneda.obj (op X)))

variable (hx : x.Compatible)

/-- The family of morphisms `X ‚ü∂ ùí¢(G(Y')) ‚ü∂ ‚Ñ±(Y')` defined on `{ Y' ‚äÜ Y : G(Y') ‚äÜ U ‚àà S}`. -/
def pulledbackFamily (Y : StructuredArrow (op U) G.op) :=
  ((x.pullback Y.hom.unop).functorPullback G).compPresheafMap
    (show _ ‚ü∂ _ from whiskerRight ((Ran.adjunction A G.op).counit.app ‚Ñ±.val) (coyoneda.obj (op X)))
set_option linter.uppercaseLean3 false in
#align category_theory.Ran_is_sheaf_of_cover_lifting.pulledback_family CategoryTheory.RanIsSheafOfCoverLifting.pulledbackFamily

@[simp]
theorem pulledbackFamily_apply (Y : StructuredArrow (op U) G.op) {W} {f : W ‚ü∂ _} (Hf) :
    pulledbackFamily ‚Ñ± S x Y f Hf =
      x (G.map f ‚â´ Y.hom.unop) Hf ‚â´ ((Ran.adjunction A G.op).counit.app ‚Ñ±.val).app (op W) :=
  rfl
set_option linter.uppercaseLean3 false in
#align category_theory.Ran_is_sheaf_of_cover_lifting.pulledback_family_apply CategoryTheory.RanIsSheafOfCoverLifting.pulledbackFamily_apply

variable {x} {S}

-- porting note: no longer needed
-- include hu hS hx

/-- Given a `G(Y) ‚äÜ U`, we can find a unique section `X ‚ü∂ ‚Ñ±(Y)` that agrees with `x`. -/
def getSection (Y : StructuredArrow (op U) G.op) : X ‚ü∂ ‚Ñ±.val.obj Y.right := by
  letI hom_sh := whiskerRight ((Ran.adjunction A G.op).counit.app ‚Ñ±.val) (coyoneda.obj (op X))
  haveI S' := K.pullback_stable Y.hom.unop hS
  haveI hs' := ((hx.pullback Y.3.unop).functorPullback G).compPresheafMap hom_sh
  exact (‚Ñ±.2 X _ (hu.cover_lift S')).amalgamate _ hs'
set_option linter.uppercaseLean3 false in
#align category_theory.Ran_is_sheaf_of_cover_lifting.get_section CategoryTheory.RanIsSheafOfCoverLifting.getSection

theorem getSection_isAmalgamation (Y : StructuredArrow (op U) G.op) :
    (pulledbackFamily ‚Ñ± S x Y).IsAmalgamation (getSection hu ‚Ñ± hS hx Y) :=
  IsSheafFor.isAmalgamation _ _
set_option linter.uppercaseLean3 false in
#align category_theory.Ran_is_sheaf_of_cover_lifting.get_section_is_amalgamation CategoryTheory.RanIsSheafOfCoverLifting.getSection_isAmalgamation

theorem getSection_is_unique (Y : StructuredArrow (op U) G.op) {y}
    (H : (pulledbackFamily ‚Ñ± S x Y).IsAmalgamation y) : y = getSection hu ‚Ñ± hS hx Y := by
  apply IsSheafFor.isSeparatedFor _ (pulledbackFamily ‚Ñ± S x Y)
  ¬∑ exact H
  ¬∑ apply getSection_isAmalgamation
  ¬∑ exact ‚Ñ±.2 X _ (hu.cover_lift (K.pullback_stable Y.hom.unop hS))
set_option linter.uppercaseLean3 false in
#align category_theory.Ran_is_sheaf_of_cover_lifting.get_section_is_unique CategoryTheory.RanIsSheafOfCoverLifting.getSection_is_unique

@[simp]
theorem getSection_commute {Y Z : StructuredArrow (op U) G.op} (f : Y ‚ü∂ Z) :
    getSection hu ‚Ñ± hS hx Y ‚â´ ‚Ñ±.val.map f.right = getSection hu ‚Ñ± hS hx Z := by
  apply getSection_is_unique
  intro V' fV' hV'
  have eq : Z.hom = Y.hom ‚â´ (G.map f.right.unop).op := by
    convert f.w
    erw [Category.id_comp]
  rw [eq] at hV'
  convert getSection_isAmalgamation hu ‚Ñ± hS hx Y (fV' ‚â´ f.right.unop) _ using 1
  ¬∑ aesop_cat
  -- porting note: the below proof was mildly rewritten because `simp` changed behaviour
  -- slightly (a rewrite which seemed to work in Lean 3, didn't work in Lean 4 because of
  -- motive is not type correct issues)
  ¬∑ rw [pulledbackFamily_apply, pulledbackFamily_apply]
    ¬∑ congr 2
      simp [eq]
    ¬∑ change S (G.map _ ‚â´ Y.hom.unop)
      simpa only [Functor.map_comp, Category.assoc] using hV'
set_option linter.uppercaseLean3 false in
#align category_theory.Ran_is_sheaf_of_cover_lifting.get_section_commute CategoryTheory.RanIsSheafOfCoverLifting.getSection_commute

/-- The limit cone in order to glue the sections obtained via `get_section`. -/
def gluedLimitCone : Limits.Cone (Ran.diagram G.op ‚Ñ±.val (op U)) :=
  { pt := X -- porting note: autoporter got this wrong
    œÄ := { app := fun Y => getSection hu ‚Ñ± hS hx Y } }
set_option linter.uppercaseLean3 false in
#align category_theory.Ran_is_sheaf_of_cover_lifting.glued_limit_cone CategoryTheory.RanIsSheafOfCoverLifting.gluedLimitCone

@[simp]
theorem gluedLimitCone_œÄ_app (W) : (gluedLimitCone hu ‚Ñ± hS hx).œÄ.app W = getSection hu ‚Ñ± hS hx W :=
  rfl
set_option linter.uppercaseLean3 false in
#align category_theory.Ran_is_sheaf_of_cover_lifting.glued_limit_cone_œÄ_app CategoryTheory.RanIsSheafOfCoverLifting.gluedLimitCone_œÄ_app

/-- The section obtained by passing `glued_limit_cone` into `CategoryTheory.Limits.limit.lift`. -/
def gluedSection : X ‚ü∂ ((ran G.op).obj ‚Ñ±.val).obj (op U) :=
  limit.lift _ (gluedLimitCone hu ‚Ñ± hS hx)
set_option linter.uppercaseLean3 false in
#align category_theory.Ran_is_sheaf_of_cover_lifting.glued_section CategoryTheory.RanIsSheafOfCoverLifting.gluedSection

/--
A helper lemma for the following two lemmas. Basically stating that if the section `y : X ‚ü∂ ùí¢(V)`
coincides with `x` on `G(V')` for all `G(V') ‚äÜ V ‚àà S`, then `X ‚ü∂ ùí¢(V) ‚ü∂ ‚Ñ±(W)` is indeed the
section obtained in `get_sections`. That said, this is littered with some more categorical jargon
in order to be applied in the following lemmas easier.
-/
theorem helper {V} (f : V ‚ü∂ U) (y : X ‚ü∂ ((ran G.op).obj ‚Ñ±.val).obj (op V)) (W)
    (H : ‚àÄ {V'} {fV : G.obj V' ‚ü∂ V} (hV), y ‚â´ ((ran G.op).obj ‚Ñ±.val).map fV.op = x (fV ‚â´ f) hV) :
    y ‚â´ limit.œÄ (Ran.diagram G.op ‚Ñ±.val (op V)) W =
      (gluedLimitCone hu ‚Ñ± hS hx).œÄ.app ((StructuredArrow.map f.op).obj W) := by
  dsimp only [gluedLimitCone_œÄ_app]
  apply getSection_is_unique hu ‚Ñ± hS hx ((StructuredArrow.map f.op).obj W)
  intro V' fV' hV'
  dsimp only [Ran.adjunction, Ran.equiv, pulledbackFamily_apply]
  erw [Adjunction.adjunctionOfEquivRight_counit_app]
  have :
    y ‚â´ ((ran G.op).obj ‚Ñ±.val).map (G.map fV' ‚â´ W.hom.unop).op =
      x (G.map fV' ‚â´ W.hom.unop ‚â´ f) (by simpa only using hV') := by
    convert H (show S ((G.map fV' ‚â´ W.hom.unop) ‚â´ f) by simpa only [Category.assoc] using hV')
      using 2
    simp only [Category.assoc]
  simp only [Quiver.Hom.unop_op, Equiv.symm_symm, StructuredArrow.map_obj_hom, unop_comp,
    Equiv.coe_fn_mk, Functor.comp_map, coyoneda_obj_map, Category.assoc, ‚Üê this, op_comp,
    ran_obj_map, NatTrans.id_app]
  erw [Category.id_comp, limit.pre_œÄ]
  congr
  convert limit.w (Ran.diagram G.op ‚Ñ±.val (op V)) (StructuredArrow.homMk' W fV'.op)
  rw [StructuredArrow.map_mk]
  erw [Category.comp_id]
  simp only [Quiver.Hom.unop_op, Functor.op_map, Quiver.Hom.op_unop]
set_option linter.uppercaseLean3 false in
#align category_theory.Ran_is_sheaf_of_cover_lifting.helper CategoryTheory.RanIsSheafOfCoverLifting.helper

/-- Verify that the `glued_section` is an amalgamation of `x`. -/
theorem gluedSection_isAmalgamation : x.IsAmalgamation (gluedSection hu ‚Ñ± hS hx) := by
  intro V fV hV
  -- porting note: next line was `ext W`
  -- Now `ext` can't see that `ran` is defined as a limit.
  -- See https://github.com/leanprover-community/mathlib4/issues/5229
  refine limit.hom_ext (Œª (W : StructuredArrow (op V) G.op) => ?_)
  simp only [Functor.comp_map, limit.lift_pre, coyoneda_obj_map, ran_obj_map, gluedSection]
  erw [limit.lift_œÄ]
  symm
  convert helper hu ‚Ñ± hS hx _ (x fV hV) _ _ using 1
  intro V' fV' hV'
  convert hx fV' (ùüô _) hV hV' (by rw [Category.id_comp])
  simp only [op_id, FunctorToTypes.map_id_apply]
set_option linter.uppercaseLean3 false in
#align category_theory.Ran_is_sheaf_of_cover_lifting.glued_section_is_amalgamation CategoryTheory.RanIsSheafOfCoverLifting.gluedSection_isAmalgamation

/-- Verify that the amalgamation is indeed unique. -/
theorem gluedSection_is_unique (y) (hy : x.IsAmalgamation y) : y = gluedSection hu ‚Ñ± hS hx := by
  unfold gluedSection limit.lift
  -- porting note: next line was `ext W`
  -- Now `ext` can't see that `ran` is defined as a limit.
  -- See https://github.com/leanprover-community/mathlib4/issues/5229
  refine limit.hom_ext (Œª (W : StructuredArrow (op U) G.op) => ?_)
  erw [limit.lift_œÄ]
  convert helper hu ‚Ñ± hS hx (ùüô _) y W _
  ¬∑ simp only [op_id, StructuredArrow.map_id]
  ¬∑ intro V' fV' hV'
    convert hy fV' (by simpa only [Category.comp_id] using hV')
    erw [Category.comp_id]
set_option linter.uppercaseLean3 false in
#align category_theory.Ran_is_sheaf_of_cover_lifting.glued_section_is_unique CategoryTheory.RanIsSheafOfCoverLifting.gluedSection_is_unique

end RanIsSheafOfCoverLifting

/-- If `G` is cover_lifting, then `Ran G.op` pushes sheaves to sheaves.

This result is basically https://stacks.math.columbia.edu/tag/00XK,
but without the condition that `C` or `D` has pullbacks.
-/
theorem ran_isSheaf_of_coverLifting {G : C ‚•§ D} (hG : CoverLifting J K G) (‚Ñ± : Sheaf J A) :
    Presheaf.IsSheaf K ((ran G.op).obj ‚Ñ±.val) := by
  intro X U S hS x hx
  constructor; swap
  ¬∑ apply RanIsSheafOfCoverLifting.gluedSection hG ‚Ñ± hS hx
  constructor
  ¬∑ apply RanIsSheafOfCoverLifting.gluedSection_isAmalgamation
  ¬∑ apply RanIsSheafOfCoverLifting.gluedSection_is_unique
set_option linter.uppercaseLean3 false in
#align category_theory.Ran_is_sheaf_of_cover_lifting CategoryTheory.ran_isSheaf_of_coverLifting

variable (A)

/-- A cover-lifting functor induces a morphism of sites in the same direction as the functor. -/
def Sites.copullback {G : C ‚•§ D} (hG : CoverLifting J K G) : Sheaf J A ‚•§ Sheaf K A where
  obj ‚Ñ± := ‚ü®(ran G.op).obj ‚Ñ±.val, ran_isSheaf_of_coverLifting hG ‚Ñ±‚ü©
  map f := ‚ü®(ran G.op).map f.val‚ü©
  map_id ‚Ñ± := Sheaf.Hom.ext _ _ <| (ran G.op).map_id ‚Ñ±.val
  map_comp f g := Sheaf.Hom.ext _ _ <| (ran G.op).map_comp f.val g.val
#align category_theory.sites.copullback CategoryTheory.Sites.copullback

/--
Given a functor between sites that is cover-preserving, cover-lifting, and compatible-preserving,
the pullback and copullback along `G` are adjoint to each other
-/
@[simps unit_app_val counit_app_val]
noncomputable def Sites.pullbackCopullbackAdjunction {G : C ‚•§ D} (Hp : CoverPreserving J K G)
    (Hl : CoverLifting J K G) (Hc : CompatiblePreserving K G) :
    Sites.pullback A Hc Hp ‚ä£ Sites.copullback A Hl where
  homEquiv X Y :=
    { toFun := fun f => ‚ü®(Ran.adjunction A G.op).homEquiv X.val Y.val f.val‚ü©
      invFun := fun f => ‚ü®((Ran.adjunction A G.op).homEquiv X.val Y.val).symm f.val‚ü©
      left_inv := fun f => by
        ext1
        dsimp
        rw [Equiv.symm_apply_apply]
      right_inv := fun f => by
        ext1
        dsimp
        rw [Equiv.apply_symm_apply] }
  unit :=
    { app := fun X => ‚ü®(Ran.adjunction A G.op).unit.app X.val‚ü©
      naturality := fun _ _ f =>
        Sheaf.Hom.ext _ _ <| (Ran.adjunction A G.op).unit.naturality f.val }
  counit :=
    { app := fun X => ‚ü®(Ran.adjunction A G.op).counit.app X.val‚ü©
      naturality := fun _ _ f =>
        Sheaf.Hom.ext _ _ <| (Ran.adjunction A G.op).counit.naturality f.val }
  homEquiv_unit := by
    -- porting note: next line was `ext1`
    refine Sheaf.Hom.ext _ _ ?_
    apply (Ran.adjunction A G.op).homEquiv_unit
  homEquiv_counit := by
    -- porting note: next line was `ext1`
    refine Sheaf.Hom.ext _ _ ?_
    apply (Ran.adjunction A G.op).homEquiv_counit
#align category_theory.sites.pullback_copullback_adjunction CategoryTheory.Sites.pullbackCopullbackAdjunction

namespace Sites

variable
  [ConcreteCategory.{max v u} A]
  [PreservesLimits (forget A)]
  [ReflectsIsomorphisms (forget A)]
  [‚àÄ (X : C), PreservesColimitsOfShape (J.Cover X)·µí·µñ (forget A)]
  [‚àÄ (X : C), HasColimitsOfShape (J.Cover X)·µí·µñ A]
  [‚àÄ (X : D), PreservesColimitsOfShape (K.Cover X)·µí·µñ (forget A)]
  [‚àÄ (X : D), HasColimitsOfShape (K.Cover X)·µí·µñ A]

/-- The natural isomorphism exhibiting compatibility between pullback and sheafification. -/
def pullbackSheafificationCompatibility
    {G : C ‚•§ D} (Hp : CoverPreserving J K G)
    (Hl : CoverLifting J K G) (Hc : CompatiblePreserving K G) :
    (whiskeringLeft _ _ A).obj G.op ‚ãô presheafToSheaf J A ‚âÖ
    presheafToSheaf K A ‚ãô pullback A Hc Hp :=
  letI A1 : (whiskeringLeft _ _ A).obj G.op ‚ä£ _ := Ran.adjunction _ _
  letI A2 : presheafToSheaf J A ‚ä£ _ := sheafificationAdjunction _ _
  letI B1 : presheafToSheaf K A ‚ä£ _ := sheafificationAdjunction _ _
  letI B2 : pullback A Hc Hp ‚ä£ _ := pullbackCopullbackAdjunction _ _ Hl _
  letI A12 := A1.comp A2
  letI B12 := B1.comp B2
  A12.leftAdjointUniq B12

/- Implementation: This is primarily used to prove the lemma
`pullbackSheafificationCompatibility_hom_app_val`. -/
lemma toSheafify_pullbackSheafificationCompatibility
    {G : C ‚•§ D} (Hp : CoverPreserving J K G)
    (Hl : CoverLifting J K G) (Hc : CompatiblePreserving K G) (F) :
    J.toSheafify (G.op ‚ãô F) ‚â´
    ((pullbackSheafificationCompatibility.{w, v, u} A Hp Hl Hc).hom.app F).val =
    whiskerLeft _ (K.toSheafify _) := by
  dsimp [pullbackSheafificationCompatibility, Adjunction.leftAdjointUniq]
  apply Quiver.Hom.op_inj
  apply coyoneda.map_injective
  ext E : 2
  dsimp [Functor.preimage, Full.preimage, coyoneda, Adjunction.leftAdjointsCoyonedaEquiv]
  erw [Adjunction.homEquiv_unit, Adjunction.homEquiv_counit]
  dsimp [Adjunction.comp]
  simp only [sheafificationAdjunction_unit_app, Category.comp_id, Functor.map_id,
    whiskerLeft_id', GrothendieckTopology.sheafifyMap_comp,
    GrothendieckTopology.sheafifyMap_sheafifyLift, Category.id_comp,
    Category.assoc, GrothendieckTopology.toSheafify_sheafifyLift]
  ext t s : 3
  dsimp [pullbackSheaf]
  congr 1
  simp only [‚Üê Category.assoc]
  convert Category.id_comp (obj := A) _
  have := (Ran.adjunction A G.op).left_triangle
  apply_fun (fun e => (e.app (K.sheafify F)).app s) at this
  exact this

@[simp]
lemma pullbackSheafificationCompatibility_hom_app_val
    {G : C ‚•§ D} (Hp : CoverPreserving J K G)
    (Hl : CoverLifting J K G) (Hc : CompatiblePreserving K G) (F : D·µí·µñ ‚•§ A) :
    ((pullbackSheafificationCompatibility.{w, v, u} A Hp Hl Hc).hom.app F).val =
    J.sheafifyLift (whiskerLeft G.op <| K.toSheafify F)
      ((presheafToSheaf K A ‚ãô pullback A Hc Hp).obj F).cond := by
  apply J.sheafifyLift_unique
  apply toSheafify_pullbackSheafificationCompatibility

end Sites

end CategoryTheory
