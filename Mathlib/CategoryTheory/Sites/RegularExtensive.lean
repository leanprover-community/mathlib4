/-
Copyright (c) 2023 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson, Filippo A. E. Nuccio, Riccardo Brasca
-/
import Mathlib.CategoryTheory.Sites.Coherent
/-!

# The Regular and Extensive Coverages

This file defines two coverages on a category `C`.

The first one is called the *regular* coverage and for that to exist, the category `C` must satisfy
a condition called `Regular C`. This means that effective epimorphisms can be "pulled back". The
covering sieves of this coverage are generated by presieves consisting of a single effective
epimorphism.

The second one is called the *extensive* coverage and for that to exist, the category `C` must
satisfy a condition called `Extensive C`. This means `C` has finite coproducts and that those
are preserved by pullbacks. The covering sieves of this coverage are generated by presieves
consisting finitely many arrows that together induce an isomorphism from the coproduct to the
target.

In `extensive_union_regular_generates_coherent`, we prove that the union of these two coverages
generates the coherent topology on `C` if `C` is precoherent, extensive and regular.

TODO: figure out under what conditions `Regular` and `Extensive`¬†are implied by `Precoherent` and
vice versa.

-/

universe v u w

namespace CategoryTheory

open Limits

variable (C : Type u) [Category.{v} C]

/--
The condition `Regular C` is property that effective epis can be "pulled back" along any
morphism. This is satisfied e.g. by categories that have pullbacks that preserve effective
epimorphisms (like `Profinite` and `CompHaus`), and categories where every object is projective
(like  `Stonean`).
-/
class Regular : Prop where
  /--
  For `X`, `Y`, `Z`, `f`, `g` like in the diagram, where `g` is an effective epi, there exists
  an object `W`, a morphism `i : W ‚ü∂ Z` and an effective epi `h : W ‚ü∂ X` making the diagram
  commute.
  ```
  W --i-‚Üí Z
  |       |
  h       g
  ‚Üì       ‚Üì
  X --f-‚Üí Y
  ```
  -/
  exists_fac : ‚àÄ {X Y Z : C} (f : X ‚ü∂ Y) (g : Z ‚ü∂ Y) [EffectiveEpi g],
    (‚àÉ (W : C) (h : W ‚ü∂ X) (_ : EffectiveEpi h) (i : W ‚ü∂ Z), i ‚â´ g = h ‚â´ f)

/--
Describes the property of having pullbacks of morphsims into a finite coproduct, where one
of the morphisms is an inclusion map into the coproduct (up to isomorphism).
-/
class HasPullbacksOfInclusions : Prop where
    /-- For any morphism `f : X ‚ü∂ Z`, where `Z` is the coproduct of `i : (a : Œ±) ‚Üí Y a ‚ü∂ Z` with
    `Œ±` finite, the pullback of `f` and `i a` exists for every `a : Œ±`. -/
    has_pullback : ‚àÄ {X Z : C} {Œ± : Type w} (f : X ‚ü∂ Z) {Y : (a : Œ±) ‚Üí C}
    (i : (a : Œ±) ‚Üí Y a ‚ü∂ Z) [Fintype Œ±] [HasCoproduct Y] [IsIso (Sigma.desc i)] (a : Œ±),
    HasPullback f (i a)

instance {Œ± : Type w} (Y : (a : Œ±) ‚Üí C) [HasCoproduct Y] :
    IsIso (Sigma.desc (fun a ‚Ü¶ Sigma.Œπ Y a)) := by
  suffices (Sigma.desc fun a ‚Ü¶ Sigma.Œπ Y a) = ùüô _ by rw [this]; infer_instance
  ext
  simp only [colimit.Œπ_desc, Cofan.mk_pt, Cofan.mk_Œπ_app, Category.comp_id]

instance [HasPullbacksOfInclusions C] {X : C} {Œ± : Type w} (Y : (a : Œ±) ‚Üí C)
    [Fintype Œ±] [HasCoproduct Y] (f : X ‚ü∂ ‚àê Y) (a : Œ±) :
    HasPullback f (Sigma.Œπ Y a) := HasPullbacksOfInclusions.has_pullback f (fun a ‚Ü¶ Sigma.Œπ Y a) a

instance [HasPullbacksOfInclusions C] {X Z : C} {Œ± : Type w} (f : X ‚ü∂ Z) {Y : (a : Œ±) ‚Üí C}
    (i : (a : Œ±) ‚Üí Y a ‚ü∂ Z) [Fintype Œ±] [HasCoproduct Y] [IsIso (Sigma.desc i)] (a : Œ±) :
    HasPullback f (i a) := HasPullbacksOfInclusions.has_pullback f i a

/--
If `C` has pullbacks then it has the pullbacks relevant to `HasPullbacksOfInclusions`.
-/
instance (priority := 10) [HasPullbacks C] :
  HasPullbacksOfInclusions C := ‚ü®fun _ _ _ => inferInstance‚ü©

/--
A category is *extensive* if it has all finite coproducts and those coproducts are preserved
by pullbacks (we only require the relevant pullbacks to exist, via `HasPullbacksOfInclusions`).
-/
class Extensive extends HasFiniteCoproducts C, HasPullbacksOfInclusions C : Prop where
  /-- Pulling back an isomorphism from a coproduct yields an isomorphism. -/
  sigma_desc_iso : ‚àÄ {Œ± : Type} [Fintype Œ±] {X : C} {Z : Œ± ‚Üí C} (œÄ : (a : Œ±) ‚Üí Z a ‚ü∂ X)
    {Y : C} (f : Y ‚ü∂ X) (_ : IsIso (Sigma.desc œÄ)),
    IsIso (Sigma.desc ((fun _ ‚Ü¶ pullback.fst) : (a : Œ±) ‚Üí pullback f (œÄ a) ‚ü∂ _))

/--
The regular coverage on a regular category `C`.
-/
def regularCoverage [Regular C] : Coverage C where
  covering B := { S | ‚àÉ (X : C) (f : X ‚ü∂ B), S = Presieve.ofArrows (fun (_ : Unit) ‚Ü¶ X)
    (fun (_ : Unit) ‚Ü¶ f) ‚àß EffectiveEpi f }
  pullback := by
    intro X Y f S ‚ü®Z, œÄ, hœÄ, h_epi‚ü©
    have := Regular.exists_fac f œÄ
    obtain ‚ü®W, h, _, i, this‚ü© := this
    refine ‚ü®Presieve.singleton h, ‚ü®?_, ?_‚ü©‚ü©
    ¬∑ exact ‚ü®W, h, by {rw [Presieve.ofArrows_pUnit h]}, inferInstance‚ü©
    ¬∑ intro W g hg
      cases hg
      refine ‚ü®Z, i, œÄ, ‚ü®?_, this‚ü©‚ü©
      cases hœÄ
      rw [Presieve.ofArrows_pUnit]
      exact Presieve.singleton.mk

/--
The extensive coverage on an extensive category `C`
-/
def extensiveCoverage [Extensive C] : Coverage C where
  covering B := { S | ‚àÉ (Œ± : Type) (_ : Fintype Œ±) (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B)),
    S = Presieve.ofArrows X œÄ ‚àß IsIso (Sigma.desc œÄ) }
  pullback := by
    intro X Y f S ‚ü®Œ±, hŒ±, Z, œÄ, hS, h_iso‚ü©
    let Z' : Œ± ‚Üí C := fun a ‚Ü¶ pullback f (œÄ a)
    let œÄ' : (a : Œ±) ‚Üí Z' a ‚ü∂ Y := fun a ‚Ü¶ pullback.fst
    refine ‚ü®@Presieve.ofArrows C _ _ Œ± Z' œÄ', ‚ü®?_, ?_‚ü©‚ü©
    ¬∑ constructor
      exact ‚ü®hŒ±, Z', œÄ', ‚ü®by simp only, Extensive.sigma_desc_iso (fun x => œÄ x) f h_iso‚ü©‚ü©
    ¬∑ intro W g hg
      rcases hg with ‚ü®a‚ü©
      refine ‚ü®Z a, pullback.snd, œÄ a, ?_, by rw [CategoryTheory.Limits.pullback.condition]‚ü©
      rw [hS]
      refine Presieve.ofArrows.mk a

instance [Regular C] [Extensive C] : Precoherent C where
  pullback f Œ± _ X‚ÇÅ œÄ‚ÇÅ _ := by
    haveI : EffectiveEpi (Sigma.desc œÄ‚ÇÅ) := inferInstance
    obtain ‚ü®W, h, _, i, hh‚ü© := Regular.exists_fac f (Sigma.desc œÄ‚ÇÅ)
    have hi := Extensive.sigma_desc_iso (fun a ‚Ü¶ Sigma.Œπ X‚ÇÅ a) i inferInstance
    let P := fun a ‚Ü¶ pullback i (Sigma.Œπ X‚ÇÅ a)
    refine ‚ü®Œ±, inferInstance, P, fun a ‚Ü¶ pullback.fst ‚â´ h,
      ‚ü®inferInstance, ‚ü®id, fun b ‚Ü¶ pullback.snd, ?_‚ü©‚ü©‚ü©
    intro b
    simp only [id_eq, Category.assoc, ‚Üê hh]
    rw [‚Üê Category.assoc, pullback.condition]
    simp only [Category.assoc, colimit.Œπ_desc, Cofan.mk_pt, Cofan.mk_Œπ_app]

end CategoryTheory
