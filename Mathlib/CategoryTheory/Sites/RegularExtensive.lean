/-
Copyright (c) 2023 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson, Filippo A. E. Nuccio, Riccardo Brasca
-/
import Mathlib.CategoryTheory.Extensive
import Mathlib.CategoryTheory.Preadditive.Projective
import Mathlib.CategoryTheory.Sites.Coherent
import Mathlib.CategoryTheory.Sites.Preserves
/-!

# The Regular and Extensive Coverages

This file defines two coverages on a category `C`.

The first one is called the *regular* coverage and for that to exist, the category `C` must satisfy
a condition called `Preregular C`. This means that effective epimorphisms can be "pulled back". The
covering sieves of this coverage are generated by presieves consisting of a single effective
epimorphism.

The second one is called the *extensive* coverage and for that to exist, the category `C` must
satisfy a condition called `FinitaryPreExtensive C`. This means `C` has finite coproducts and that
those are preserved by pullbacks. The covering sieves of this coverage are generated by presieves
consisting finitely many arrows that together induce an isomorphism from the coproduct to the
target. This condition is weaker than `FinitaryExtensive`, where in addition finite coproducts are
disjoint.

## Main results

* `instance : Precoherent C` given `Preregular C` and `FinitaryPreExtensive C`.

* `extensive_union_regular_generates_coherent`: the union of the regular and extensive coverages
  generates the coherent topology on `C` if `C` is precoherent, preextensive and preregular.

* `isSheaf_iff_equalizerCondition`:¬†In a preregular category with pullbacks, the sheaves for the
  regular topology are precisely the presheaves satisfying an equaliser condition with respect to
  effective epimorphisms.

* `isSheaf_of_projective`: In a preregular category in which every object is projective, every
  presheaf is a sheaf for the regular topology.

* `isSheaf_iff_preservesFiniteProducts`: In a finitary extensive category, the sheaves for the
  extensive topology are precisely those preserving finite products.

-/

universe v u w

namespace CategoryTheory

open Limits

variable (C : Type u) [Category.{v} C]

/--
The condition `Preregular C` is property that effective epis can be "pulled back" along any
morphism. This is satisfied e.g. by categories that have pullbacks that preserve effective
epimorphisms (like `Profinite` and `CompHaus`), and categories where every object is projective
(like  `Stonean`).
-/
class Preregular : Prop where
  /--
  For `X`, `Y`, `Z`, `f`, `g` like in the diagram, where `g` is an effective epi, there exists
  an object `W`, an effective epi `h : W ‚ü∂ X` and a morphism `i : W ‚ü∂ Z` making the diagram
  commute.
  ```
  W --i-‚Üí Z
  |       |
  h       g
  ‚Üì       ‚Üì
  X --f-‚Üí Y
  ```
  -/
  exists_fac : ‚àÄ {X Y Z : C} (f : X ‚ü∂ Y) (g : Z ‚ü∂ Y) [EffectiveEpi g],
    (‚àÉ (W : C) (h : W ‚ü∂ X) (_ : EffectiveEpi h) (i : W ‚ü∂ Z), i ‚â´ g = h ‚â´ f)

instance [Precoherent C] [HasFiniteCoproducts C] : Preregular C where
  exists_fac {X Y Z} f g _ := by
    have hp := Precoherent.pullback f PUnit (fun () ‚Ü¶ Z) (fun () ‚Ü¶ g)
    simp only [exists_const] at hp
    rw [‚Üê effectiveEpi_iff_effectiveEpiFamily g] at hp
    obtain ‚ü®Œ≤, _, X‚ÇÇ, œÄ‚ÇÇ, h, Œπ, hŒπ‚ü© := hp inferInstance
    refine ‚ü®‚àê X‚ÇÇ, Sigma.desc œÄ‚ÇÇ, inferInstance, Sigma.desc Œπ, ?_‚ü©
    ext b
    simpa using hŒπ b

/--
The regular coverage on a regular category `C`.
-/
def regularCoverage [Preregular C] : Coverage C where
  covering B := { S | ‚àÉ (X : C) (f : X ‚ü∂ B), S = Presieve.ofArrows (fun (_ : Unit) ‚Ü¶ X)
    (fun (_ : Unit) ‚Ü¶ f) ‚àß EffectiveEpi f }
  pullback := by
    intro X Y f S ‚ü®Z, œÄ, hœÄ, h_epi‚ü©
    have := Preregular.exists_fac f œÄ
    obtain ‚ü®W, h, _, i, this‚ü© := this
    refine ‚ü®Presieve.singleton h, ‚ü®?_, ?_‚ü©‚ü©
    ¬∑ exact ‚ü®W, h, by {rw [Presieve.ofArrows_pUnit h]}, inferInstance‚ü©
    ¬∑ intro W g hg
      cases hg
      refine ‚ü®Z, i, œÄ, ‚ü®?_, this‚ü©‚ü©
      cases hœÄ
      rw [Presieve.ofArrows_pUnit]
      exact Presieve.singleton.mk

/--
The extensive coverage on an extensive category `C`

TODO: use general colimit API instead of `IsIso (Sigma.desc œÄ)`
-/
def extensiveCoverage [FinitaryPreExtensive C] : Coverage C where
  covering B := { S | ‚àÉ (Œ± : Type) (_ : Fintype Œ±) (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B)),
    S = Presieve.ofArrows X œÄ ‚àß IsIso (Sigma.desc œÄ) }
  pullback := by
    intro X Y f S ‚ü®Œ±, hŒ±, Z, œÄ, hS, h_iso‚ü©
    let Z' : Œ± ‚Üí C := fun a ‚Ü¶ pullback f (œÄ a)
    let œÄ' : (a : Œ±) ‚Üí Z' a ‚ü∂ Y := fun a ‚Ü¶ pullback.fst
    refine ‚ü®@Presieve.ofArrows C _ _ Œ± Z' œÄ', ‚ü®?_, ?_‚ü©‚ü©
    ¬∑ constructor
      exact ‚ü®hŒ±, Z', œÄ', ‚ü®by simp only, FinitaryPreExtensive.sigma_desc_iso (fun x => œÄ x) f h_iso‚ü©‚ü©
    ¬∑ intro W g hg
      rcases hg with ‚ü®a‚ü©
      refine ‚ü®Z a, pullback.snd, œÄ a, ?_, by rw [CategoryTheory.Limits.pullback.condition]‚ü©
      rw [hS]
      exact Presieve.ofArrows.mk a

theorem effectiveEpi_desc_iff_effectiveEpiFamily [FinitaryPreExtensive C] {Œ± : Type} [Fintype Œ±]
    {B : C} (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí X a ‚ü∂ B) :
    EffectiveEpi (Sigma.desc œÄ) ‚Üî EffectiveEpiFamily X œÄ := by
  exact ‚ü®fun h ‚Ü¶ ‚ü®‚ü®@effectiveEpiFamilyStructOfEffectiveEpiDesc _ _ _ _ X œÄ _ h _ _ (fun g ‚Ü¶
    (FinitaryPreExtensive.sigma_desc_iso (fun a ‚Ü¶ Sigma.Œπ X a) g inferInstance).epi_of_iso)‚ü©‚ü©,
    fun _ ‚Ü¶ inferInstance‚ü©

instance [FinitaryPreExtensive C] [Preregular C] : Precoherent C where
  pullback {B‚ÇÅ B‚ÇÇ} f Œ± _ X‚ÇÅ œÄ‚ÇÅ h := by
    refine ‚ü®Œ±, inferInstance, ?_‚ü©
    obtain ‚ü®Y, g, _, g', hg‚ü© := Preregular.exists_fac f (Sigma.desc œÄ‚ÇÅ)
    let X‚ÇÇ := fun a ‚Ü¶ pullback g' (Sigma.Œπ X‚ÇÅ a)
    let œÄ‚ÇÇ := fun a ‚Ü¶ pullback.fst (f := g') (g := Sigma.Œπ X‚ÇÅ a) ‚â´ g
    let œÄ' := fun a ‚Ü¶ pullback.fst (f := g') (g := Sigma.Œπ X‚ÇÅ a)
    have _ := FinitaryPreExtensive.sigma_desc_iso (fun a ‚Ü¶ Sigma.Œπ X‚ÇÅ a) g' inferInstance
    refine ‚ü®X‚ÇÇ, œÄ‚ÇÇ, ?_, ?_‚ü©
    ¬∑ have : (Sigma.desc œÄ' ‚â´ g) = Sigma.desc œÄ‚ÇÇ := by ext; simp
      rw [‚Üê effectiveEpi_desc_iff_effectiveEpiFamily, ‚Üê this]
      infer_instance
    ¬∑ refine ‚ü®id, fun b ‚Ü¶ pullback.snd, fun b ‚Ü¶ ?_‚ü©
      simp only [id_eq, Category.assoc, ‚Üê hg]
      rw [‚Üê Category.assoc, pullback.condition]
      simp

/-- The union of the extensive and regular coverages generates the coherent topology on `C`. -/
lemma extensive_regular_generate_coherent [Preregular C] [FinitaryPreExtensive C] :
    ((extensiveCoverage C) ‚äî (regularCoverage C)).toGrothendieck =
    (coherentTopology C) := by
  ext B S
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ induction h with
    | of Y T hT =>
      apply Coverage.saturate.of
      simp only [Coverage.sup_covering, Set.mem_union] at hT
      exact Or.elim hT
        (fun ‚ü®Œ±, x, X, œÄ, ‚ü®h, _‚ü©‚ü© ‚Ü¶ ‚ü®Œ±, x, X, œÄ, ‚ü®h, inferInstance‚ü©‚ü©)
        (fun ‚ü®Z, f, ‚ü®h, _‚ü©‚ü© ‚Ü¶ ‚ü®Unit, inferInstance, fun _ ‚Ü¶ Z, fun _ ‚Ü¶ f, ‚ü®h, inferInstance‚ü©‚ü©)
    | top => apply Coverage.saturate.top
    | transitive Y T => apply Coverage.saturate.transitive Y T<;> [assumption; assumption]
  ¬∑ induction h with
    | of Y T hT =>
      obtain ‚ü®I, hI, X, f, ‚ü®h, hT‚ü©‚ü© := hT
      let œÜ := fun (i : I) ‚Ü¶ Sigma.Œπ X i
      let F := Sigma.desc f
      let Z := Sieve.generate T
      let Xs := (‚àê fun (i : I) => X i)
      let Zf := Sieve.generate (Presieve.ofArrows (fun (_ : Unit) ‚Ü¶ Xs) (fun (_ : Unit) ‚Ü¶ F))
      apply Coverage.saturate.transitive Y Zf
      ¬∑ apply Coverage.saturate.of
        simp only [Coverage.sup_covering, extensiveCoverage, regularCoverage, Set.mem_union,
          Set.mem_setOf_eq]
        exact Or.inr ‚ü®Xs, F, ‚ü®rfl, inferInstance‚ü©‚ü©
      ¬∑ intro R g hZfg
        dsimp at hZfg
        rw [Presieve.ofArrows_pUnit] at hZfg
        obtain ‚ü®W, œà, œÉ, ‚ü®hW, hW'‚ü©‚ü© := hZfg
        induction hW
        rw [‚Üê hW', Sieve.pullback_comp Z]
        suffices Sieve.pullback œà ((Sieve.pullback F) Z) ‚àà GrothendieckTopology.sieves
          ((extensiveCoverage C) ‚äî (regularCoverage C)).toGrothendieck R by assumption
        apply GrothendieckTopology.pullback_stable'
        suffices Coverage.saturate ((extensiveCoverage C) ‚äî (regularCoverage C)) Xs
          (Z.pullback F) by assumption
        suffices : Sieve.generate (Presieve.ofArrows X œÜ) ‚â§ Z.pullback F
        ¬∑ apply Coverage.saturate_of_superset _ this
          apply Coverage.saturate.of
          simp only [Coverage.sup_covering, extensiveCoverage, regularCoverage, Set.mem_union,
            Set.mem_setOf_eq]
          refine Or.inl ‚ü®I, hI, X, œÜ, ‚ü®rfl, ?_‚ü©‚ü©
          suffices Sigma.desc œÜ = ùüô _ by rw [this]; infer_instance
          ext
          simp only [colimit.Œπ_desc, Cofan.mk_pt, Cofan.mk_Œπ_app, Category.comp_id]
        intro Q q hq
        simp only [Sieve.pullback_apply, Sieve.generate_apply]
        simp only [Sieve.generate_apply] at hq
        obtain ‚ü®E, e, r, hq‚ü© := hq
        refine' ‚ü®E, e, r ‚â´ F, ‚ü®_, _‚ü©‚ü©
        ¬∑ rw [h]
          induction hq.1
          simp only [colimit.Œπ_desc, Cofan.mk_pt, Cofan.mk_Œπ_app]
          exact Presieve.ofArrows.mk _
        ¬∑ rw [‚Üê hq.2]
          simp only [Category.assoc]
    | top => apply Coverage.saturate.top
    | transitive Y T => apply Coverage.saturate.transitive Y T<;> [assumption; assumption]

section RegularSheaves

variable {C}

open Opposite Presieve

/-- A presieve is *regular* if it consists of a single effective epimorphism. -/
class Presieve.regular {X : C} (R : Presieve X) : Prop where
  /-- `R` consists of a single epimorphism. -/
  single_epi : ‚àÉ (Y : C) (f : Y ‚ü∂ X), R = Presieve.ofArrows (fun (_ : Unit) ‚Ü¶ Y)
    (fun (_ : Unit) ‚Ü¶ f) ‚àß EffectiveEpi f

namespace regularCoverage

/--
The map to the explicit equalizer used in the sheaf condition.
-/
def MapToEqualizer (P : C·µí·µñ ‚•§ Type*) {W X B : C} (f : X ‚ü∂ B)
    (g‚ÇÅ g‚ÇÇ : W ‚ü∂ X) (w : g‚ÇÅ ‚â´ f = g‚ÇÇ ‚â´ f) :
    P.obj (op B) ‚Üí { x : P.obj (op X) | P.map g‚ÇÅ.op x = P.map g‚ÇÇ.op x } := fun t ‚Ü¶
  ‚ü®P.map f.op t, by simp only [Set.mem_setOf_eq, ‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, w]‚ü©

/--
The sheaf condition with respect to regular presieves, given the existence of the relavant pullback.
-/
def EqualizerCondition (P : C·µí·µñ ‚•§ Type*) : Prop :=
  ‚àÄ (X B : C) (œÄ : X ‚ü∂ B) [EffectiveEpi œÄ] [HasPullback œÄ œÄ], Function.Bijective
    (MapToEqualizer P œÄ (pullback.fst (f := œÄ) (g := œÄ)) (pullback.snd (f := œÄ) (g := œÄ))
    pullback.condition)

lemma EqualizerCondition.isSheafFor {B : C} {S : Presieve B} [S.regular] [S.hasPullbacks]
    {F : C·µí·µñ ‚•§ Type*}
    (hF : EqualizerCondition F) : S.IsSheafFor F := by
  obtain ‚ü®X, œÄ, hS, œÄsurj‚ü© := Presieve.regular.single_epi (R := S)
  subst hS
  rw [isSheafFor_arrows_iff_pullbacks]
  intro y h
  have : (Presieve.singleton œÄ).hasPullbacks := by rw [‚Üê ofArrows_pUnit]; infer_instance
  have : HasPullback œÄ œÄ := hasPullbacks.has_pullbacks Presieve.singleton.mk Presieve.singleton.mk
  specialize hF X B œÄ
  rw [Function.bijective_iff_existsUnique] at hF
  obtain ‚ü®t, ht, ht'‚ü© := hF ‚ü®y (), h () ()‚ü©
  refine ‚ü®t, fun _ ‚Ü¶ ?_, fun x h ‚Ü¶ ht' x ?_‚ü©
  ¬∑ simpa [MapToEqualizer] using ht
  ¬∑ simpa [MapToEqualizer] using h ()

lemma equalizerCondition_of_regular {F : C·µí·µñ ‚•§ Type*}
    (hSF : ‚àÄ {B : C} (S : Presieve B) [S.regular] [S.hasPullbacks], S.IsSheafFor F) :
    EqualizerCondition F := by
  intro X B œÄ _ _
  have : (ofArrows (fun _ ‚Ü¶ X) (fun _ ‚Ü¶ œÄ)).regular := ‚ü®X, œÄ, rfl, inferInstance‚ü©
  have : (ofArrows (fun () ‚Ü¶ X) (fun _ ‚Ü¶ œÄ)).hasPullbacks := ‚ü®
      fun hf _ hg ‚Ü¶ (by cases hf; cases hg; infer_instance)‚ü©
  specialize hSF (ofArrows (fun () ‚Ü¶ X) (fun _ ‚Ü¶ œÄ))
  rw [isSheafFor_arrows_iff_pullbacks] at hSF
  rw [Function.bijective_iff_existsUnique]
  intro ‚ü®x, hx‚ü©
  obtain ‚ü®t, ht, ht'‚ü© := hSF (fun _ ‚Ü¶ x) (fun _ _ ‚Ü¶ hx)
  refine ‚ü®t, ?_, fun y h ‚Ü¶ ht' y ?_‚ü©
  ¬∑ simpa [MapToEqualizer] using ht ()
  ¬∑ simpa [MapToEqualizer] using h

lemma isSheafFor_regular_of_projective {X : C} (S : Presieve X) [S.regular] [Projective X]
    (F : C·µí·µñ ‚•§ Type*) : S.IsSheafFor F := by
  obtain ‚ü®Y, f, rfl, hf‚ü© := Presieve.regular.single_epi (R := S)
  rw [isSheafFor_arrows_iff]
  refine fun x hx ‚Ü¶ ‚ü®F.map (Projective.factorThru (ùüô _) f).op <| x (), fun _ ‚Ü¶ ?_, fun y h ‚Ü¶ ?_‚ü©
  ¬∑ simpa using (hx () () Y (ùüô Y) (f ‚â´ (Projective.factorThru (ùüô _) f)) (by simp)).symm
  ¬∑ simp only [‚Üê h (), ‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, Projective.factorThru_comp,
      op_id, FunctorToTypes.map_id_apply]

lemma EqualizerCondition.isSheaf_iff (F : C·µí·µñ ‚•§ Type*)
    [‚àÄ ‚¶ÉX Y : C‚¶Ñ (œÄ : X ‚ü∂ Y) [EffectiveEpi œÄ], HasPullback œÄ œÄ] [Preregular C] :
    Presieve.IsSheaf (regularCoverage C).toGrothendieck F ‚Üî EqualizerCondition F := by
  rw [Presieve.isSheaf_coverage]
  refine ‚ü®fun h ‚Ü¶ equalizerCondition_of_regular fun S ‚ü®Y, f, hh‚ü© _ ‚Ü¶ h S ‚ü®Y, f, hh‚ü©, ?_‚ü©
  rintro h X S ‚ü®Y, f, rfl, hf‚ü©
  exact @isSheafFor _ _ _ _ ‚ü®Y, f, rfl, hf‚ü© ‚ü®fun g _ h ‚Ü¶ by cases g; cases h; infer_instance‚ü© _ h

lemma isSheaf_of_projective (F : C·µí·µñ ‚•§ Type*) [Preregular C] [‚àÄ (X : C), Projective X] :
    IsSheaf (regularCoverage C).toGrothendieck F :=
  (isSheaf_coverage _ _).mpr fun S ‚ü®_, h‚ü© ‚Ü¶ have : S.regular := ‚ü®_, h‚ü©
    isSheafFor_regular_of_projective _ _

/-- Every Yoneda-presheaf is a sheaf for the regular topology. -/
theorem isSheaf_yoneda_obj [Preregular C] (W : C)  :
    Presieve.IsSheaf (regularCoverage C).toGrothendieck (yoneda.obj W) := by
  rw [isSheaf_coverage]
  intro X S ‚ü®_, hS‚ü©
  have : S.regular := ‚ü®_, hS‚ü©
  obtain ‚ü®Y, f, rfl, hf‚ü© := Presieve.regular.single_epi (R := S)
  have h_colim := isColimitOfEffectiveEpiStruct f hf.effectiveEpi.some
  rw [‚Üê Sieve.generateSingleton_eq, ‚Üê Presieve.ofArrows_pUnit] at h_colim
  intro x hx
  let x_ext := Presieve.FamilyOfElements.sieveExtend x
  have hx_ext := Presieve.FamilyOfElements.Compatible.sieveExtend hx
  let S := Sieve.generate (Presieve.ofArrows (fun () ‚Ü¶ Y) (fun () ‚Ü¶ f))
  obtain ‚ü®t, t_amalg, t_uniq‚ü© :=
    (Sieve.forallYonedaIsSheaf_iff_colimit S).mpr ‚ü®h_colim‚ü© W x_ext hx_ext
  refine ‚ü®t, ?_, ?_‚ü©
  ¬∑ convert Presieve.isAmalgamation_restrict (Sieve.le_generate
      (Presieve.ofArrows (fun () ‚Ü¶ Y) (fun () ‚Ü¶ f))) _ _ t_amalg
    exact (Presieve.restrict_extend hx).symm
  ¬∑ exact fun y hy ‚Ü¶ t_uniq y <| Presieve.isAmalgamation_sieveExtend x y hy

/-- The regular topology on any preregular category is subcanonical. -/
theorem subcanonical [Preregular C] : Sheaf.Subcanonical (regularCoverage C).toGrothendieck :=
  Sheaf.Subcanonical.of_yoneda_isSheaf _ isSheaf_yoneda_obj

end regularCoverage

end RegularSheaves

section ExtensiveSheaves

variable [FinitaryPreExtensive C] {C}

/-- A presieve is *extensive* if it is finite and its arrows induce an isomorphism from the
coproduct to the target. -/
class Presieve.extensive {X : C} (R : Presieve X) :
    Prop where
  /-- `R` consists of a finite collection of arrows that together induce an isomorphism from the
  coproduct of their sources. -/
  arrows_nonempty_isColimit : ‚àÉ (Œ± : Type) (_ : Fintype Œ±) (Z : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (Z a ‚ü∂ X)),
    R = Presieve.ofArrows Z œÄ ‚àß Nonempty (IsColimit (Cofan.mk X œÄ))

instance {X : C} (S : Presieve X) [S.extensive] : S.hasPullbacks where
  has_pullbacks := by
    obtain ‚ü®_, _, _, _, rfl, ‚ü®hc‚ü©‚ü© := Presieve.extensive.arrows_nonempty_isColimit (R := S)
    intro _ _ _ _ _ hg
    cases hg
    apply FinitaryPreExtensive.hasPullbacks_of_is_coproduct hc

instance {Œ± : Type} [Fintype Œ±] {Z : Œ± ‚Üí C} {F : C ‚•§ Type w}
    [PreservesFiniteProducts F] : PreservesLimit (Discrete.functor fun a => (Z a)) F :=
  (PreservesFiniteProducts.preserves Œ±).preservesLimit

open Presieve Opposite

/--
A finite product preserving presheaf is a sheaf for the extensive topology on a category which is
`FinitaryPreExtensive`.
-/
theorem isSheafFor_extensive_of_preservesFiniteProducts {X : C} (S : Presieve X) [S.extensive]
    (F : C·µí·µñ ‚•§ Type max u v) [PreservesFiniteProducts F] : S.IsSheafFor F  := by
  obtain ‚ü®_, _, Z, œÄ, rfl, ‚ü®hc‚ü©‚ü© := extensive.arrows_nonempty_isColimit (R := S)
  have : (ofArrows Z (Cofan.mk X œÄ).inj).hasPullbacks :=
    (inferInstance : (ofArrows Z œÄ).hasPullbacks)
  exact isSheafFor_of_preservesProduct _ _ hc

instance {Œ± : Type} [Fintype Œ±] (Z : Œ± ‚Üí C) : (ofArrows Z (fun i ‚Ü¶ Sigma.Œπ Z i)).extensive :=
  ‚ü®‚ü®Œ±, inferInstance, Z, (fun i ‚Ü¶ Sigma.Œπ Z i), rfl, ‚ü®coproductIsCoproduct _‚ü©‚ü©‚ü©

/--
A presheaf on a category which is `FinitaryExtensive`¬†is a sheaf iff it preserves finite products.
-/
theorem isSheaf_iff_preservesFiniteProducts [FinitaryExtensive C] (F : C·µí·µñ ‚•§ Type max u v) :
    Presieve.IsSheaf (extensiveCoverage C).toGrothendieck F ‚Üî
    Nonempty (PreservesFiniteProducts F) := by
  refine ‚ü®fun hF ‚Ü¶ ‚ü®‚ü®fun Œ± _ ‚Ü¶ ‚ü®fun {K} ‚Ü¶ ?_‚ü©‚ü©‚ü©, fun hF ‚Ü¶ ?_‚ü©
  ¬∑ rw [Presieve.isSheaf_coverage] at hF
    let Z : Œ± ‚Üí C := fun i ‚Ü¶ unop (K.obj ‚ü®i‚ü©)
    have : (Presieve.ofArrows Z (Cofan.mk (‚àê Z) (Sigma.Œπ Z)).inj).hasPullbacks :=
      (inferInstance : (Presieve.ofArrows Z (Sigma.Œπ Z)).hasPullbacks)
    have : ‚àÄ (i : Œ±), Mono (Cofan.inj (Cofan.mk (‚àê Z) (Sigma.Œπ Z)) i) :=
      (inferInstance : ‚àÄ (i : Œ±), Mono (Sigma.Œπ Z i))
    let i : K ‚âÖ Discrete.functor (fun i ‚Ü¶ op (Z i)) := Discrete.natIsoFunctor
    let _ : PreservesLimit (Discrete.functor (fun i ‚Ü¶ op (Z i))) F :=
        Presieve.preservesProductOfIsSheafFor F ?_ initialIsInitial _ (coproductIsCoproduct Z)
        (FinitaryExtensive.isPullback_initial_to_sigma_Œπ Z)
        (hF (Presieve.ofArrows Z (fun i ‚Ü¶ Sigma.Œπ Z i)) ?_)
    ¬∑ exact preservesLimitOfIsoDiagram F i.symm
    ¬∑ apply hF
      refine ‚ü®Empty, inferInstance, Empty.elim, IsEmpty.elim inferInstance, rfl, ‚ü®default,?_, ?_‚ü©‚ü©
      ¬∑ ext b
        cases b
      ¬∑ simp only [eq_iff_true_of_subsingleton]
    ¬∑ refine ‚ü®Œ±, inferInstance, Z, (fun i ‚Ü¶ Sigma.Œπ Z i), rfl, ?_‚ü©
      suffices Sigma.desc (fun i ‚Ü¶ Sigma.Œπ Z i) = ùüô _ by rw [this]; infer_instance
      ext
      simp
  ¬∑ let _ := hF.some
    rw [Presieve.isSheaf_coverage]
    intro X R ‚ü®Y, Œ±, Z, œÄ, hR, hi‚ü©
    have : IsIso (Sigma.desc (Cofan.inj (Cofan.mk X œÄ))) := hi
    have : R.extensive := ‚ü®Y, Œ±, Z, œÄ, hR, ‚ü®Cofan.isColimitOfIsIsoSigmaDesc (Cofan.mk X œÄ)‚ü©‚ü©
    exact isSheafFor_extensive_of_preservesFiniteProducts R F

end ExtensiveSheaves

end CategoryTheory
