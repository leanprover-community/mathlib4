/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Limits.Opposites
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Products
import Mathlib.CategoryTheory.Limits.Types.Shapes
import Mathlib.CategoryTheory.Sites.IsSheafFor
import Mathlib.Tactic.ApplyFun

/-!
# The equalizer diagram sheaf condition for a presieve

In `Mathlib/CategoryTheory/Sites/IsSheafFor.lean` it is defined what it means for a presheaf to be a
sheaf *for* a particular presieve. In this file we provide equivalent conditions in terms of
equalizer diagrams.

* In `Equalizer.Presieve.sheaf_condition`, the sheaf condition at a presieve is shown to be
  equivalent to that of https://stacks.math.columbia.edu/tag/00VM (and combined with
  `isSheaf_pretopology`, this shows the notions of `IsSheaf` are exactly equivalent.)

* In `Equalizer.Sieve.equalizer_sheaf_condition`, the sheaf condition at a sieve is shown to be
  equivalent to that of Equation (3) p. 122 in Maclane-Moerdijk [MM92].

## References

* [MM92]: *Sheaves in geometry and logic*, Saunders MacLane, and Ieke Moerdijk:
  Chapter III, Section 4.
* https://stacks.math.columbia.edu/tag/00VL (sheaves on a pretopology or site)

-/


universe t w v u

namespace CategoryTheory

open Opposite CategoryTheory Category Limits Sieve

namespace Equalizer

variable {C : Type u} [Category.{v} C] (P : C·µí·µñ ‚•§ Type max v u) {X : C} (R : Presieve X)
  (S : Sieve X)

noncomputable section

/--
The middle object of the fork diagram given in Equation (3) of [MM92], as well as the fork diagram
of the Stacks entry.
-/
@[stacks 00VM "This is the middle object of the fork diagram there."]
def FirstObj : Type max v u :=
  ‚àè·∂ú fun f : Œ£Y, { f : Y ‚ü∂ X // R f } => P.obj (op f.1)

variable {P R}

-- Porting note (https://github.com/leanprover-community/mathlib4/issues/10688): added to ease automation
@[ext]
lemma FirstObj.ext (z‚ÇÅ z‚ÇÇ : FirstObj P R) (h : ‚àÄ (Y : C) (f : Y ‚ü∂ X)
    (hf : R f), (Pi.œÄ _ ‚ü®Y, f, hf‚ü© : FirstObj P R ‚ü∂ _) z‚ÇÅ =
      (Pi.œÄ _ ‚ü®Y, f, hf‚ü© : FirstObj P R ‚ü∂ _) z‚ÇÇ) : z‚ÇÅ = z‚ÇÇ := by
  apply Limits.Types.limit_ext
  rintro ‚ü®‚ü®Y, f, hf‚ü©‚ü©
  exact h Y f hf

variable (P R)

/-- Show that `FirstObj` is isomorphic to `FamilyOfElements`. -/
@[simps]
def firstObjEqFamily : FirstObj P R ‚âÖ R.FamilyOfElements P where
  hom t _ _ hf := Pi.œÄ (fun f : Œ£Y, { f : Y ‚ü∂ X // R f } => P.obj (op f.1)) ‚ü®_, _, hf‚ü© t
  inv := Pi.lift fun f x => x _ f.2.2

instance : Inhabited (FirstObj P (‚ä• : Presieve X)) :=
  (firstObjEqFamily P _).toEquiv.inhabited

-- Porting note: was not needed in mathlib
instance : Inhabited (FirstObj P ((‚ä• : Sieve X) : Presieve X)) :=
  (inferInstance : Inhabited (FirstObj P (‚ä• : Presieve X)))

/--
The left morphism of the fork diagram given in Equation (3) of [MM92], as well as the fork diagram
of the Stacks entry.
-/
@[stacks 00VM "This is the left morphism of the fork diagram there."]
def forkMap : P.obj (op X) ‚ü∂ FirstObj P R :=
  Pi.lift fun f => P.map f.2.1.op

/-!
This section establishes the equivalence between the sheaf condition of Equation (3) [MM92] and
the definition of `IsSheafFor`.
-/


namespace Sieve

/-- The rightmost object of the fork diagram of Equation (3) [MM92], which contains the data used
to check a family is compatible.
-/
def SecondObj : Type max v u :=
  ‚àè·∂ú fun f : Œ£ (Y Z : _) (_ : Z ‚ü∂ Y), { f' : Y ‚ü∂ X // S f' } => P.obj (op f.2.1)

variable {P S}

-- Porting note (https://github.com/leanprover-community/mathlib4/issues/10688): added to ease automation
@[ext]
lemma SecondObj.ext (z‚ÇÅ z‚ÇÇ : SecondObj P S) (h : ‚àÄ (Y Z : C) (g : Z ‚ü∂ Y) (f : Y ‚ü∂ X)
    (hf : S.arrows f), (Pi.œÄ _ ‚ü®Y, Z, g, f, hf‚ü© : SecondObj P S ‚ü∂ _) z‚ÇÅ =
      (Pi.œÄ _ ‚ü®Y, Z, g, f, hf‚ü© : SecondObj P S ‚ü∂ _) z‚ÇÇ) : z‚ÇÅ = z‚ÇÇ := by
  apply Limits.Types.limit_ext
  rintro ‚ü®‚ü®Y, Z, g, f, hf‚ü©‚ü©
  apply h

variable (P S)

/-- The map `p` of Equations (3,4) [MM92]. -/
def firstMap : FirstObj P (S : Presieve X) ‚ü∂ SecondObj P S :=
  Pi.lift fun fg =>
    Pi.œÄ _ (‚ü®_, _, S.downward_closed fg.2.2.2.2 fg.2.2.1‚ü© : Œ£Y, { f : Y ‚ü∂ X // S f })

instance : Inhabited (SecondObj P (‚ä• : Sieve X)) :=
  ‚ü®firstMap _ _ default‚ü©

/-- The map `a` of Equations (3,4) [MM92]. -/
def secondMap : FirstObj P (S : Presieve X) ‚ü∂ SecondObj P S :=
  Pi.lift fun fg => Pi.œÄ _ ‚ü®_, fg.2.2.2‚ü© ‚â´ P.map fg.2.2.1.op

theorem w : forkMap P (S : Presieve X) ‚â´ firstMap P S = forkMap P S ‚â´ secondMap P S := by
  ext
  simp [firstMap, secondMap, forkMap]

/--
The family of elements given by `x : FirstObj P S` is compatible iff `firstMap` and `secondMap`
map it to the same point.
-/
theorem compatible_iff (x : FirstObj P S.arrows) :
    ((firstObjEqFamily P S.arrows).hom x).Compatible ‚Üî firstMap P S x = secondMap P S x := by
  rw [Presieve.compatible_iff_sieveCompatible]
  constructor
  ¬∑ intro t
    apply SecondObj.ext
    intros Y Z g f hf
    simpa [firstMap, secondMap] using t _ g hf
  ¬∑ intro t Y Z f g hf
    rw [Types.limit_ext_iff'] at t
    simpa [firstMap, secondMap] using t ‚ü®‚ü®Y, Z, g, f, hf‚ü©‚ü©

/-- `P` is a sheaf for `S`, iff the fork given by `w` is an equalizer. -/
theorem equalizer_sheaf_condition :
    Presieve.IsSheafFor P (S : Presieve X) ‚Üî Nonempty (IsLimit (Fork.ofŒπ _ (w P S))) := by
  rw [Types.type_equalizer_iff_unique,
    ‚Üê Equiv.forall_congr_right (firstObjEqFamily P (S : Presieve X)).toEquiv.symm]
  simp_rw [‚Üê compatible_iff]
  simp only [inv_hom_id_apply, Iso.toEquiv_symm_fun]
  apply forall‚ÇÇ_congr
  intro x _
  apply existsUnique_congr
  intro t
  rw [‚Üê Iso.toEquiv_symm_fun]
  rw [Equiv.eq_symm_apply]
  constructor
  ¬∑ intro q
    funext Y f hf
    simpa [firstObjEqFamily, forkMap] using q _ _
  ¬∑ intro q Y f hf
    rw [‚Üê q]
    simp [firstObjEqFamily, forkMap]

end Sieve

/-!
This section establishes the equivalence between the sheaf condition of
https://stacks.math.columbia.edu/tag/00VM and the definition of `isSheafFor`.
-/


namespace Presieve

variable [R.hasPullbacks]

/--
The rightmost object of the fork diagram of the Stacks entry, which
contains the data used to check a family of elements for a presieve is compatible.
-/
@[simp, stacks 00VM "This is the rightmost object of the fork diagram there."]
def SecondObj : Type max v u :=
  ‚àè·∂ú fun fg : (Œ£Y, { f : Y ‚ü∂ X // R f }) √ó Œ£Z, { g : Z ‚ü∂ X // R g } =>
    haveI := Presieve.hasPullbacks.has_pullbacks fg.1.2.2 fg.2.2.2
    P.obj (op (pullback fg.1.2.1 fg.2.2.1))

/-- The map `pr‚ÇÄ*` of the Stacks entry. -/
@[stacks 00VM "This is the map `pr‚ÇÄ*` there."]
def firstMap : FirstObj P R ‚ü∂ SecondObj P R :=
  Pi.lift fun fg =>
    haveI := Presieve.hasPullbacks.has_pullbacks fg.1.2.2 fg.2.2.2
    Pi.œÄ _ _ ‚â´ P.map (pullback.fst _ _).op

instance [HasPullbacks C] : Inhabited (SecondObj P (‚ä• : Presieve X)) :=
  ‚ü®firstMap _ _ default‚ü©

/-- The map `pr‚ÇÅ*` of the Stacks entry. -/
@[stacks 00VM "This is the map `pr‚ÇÅ*` there."]
def secondMap : FirstObj P R ‚ü∂ SecondObj P R :=
  Pi.lift fun fg =>
    haveI := Presieve.hasPullbacks.has_pullbacks fg.1.2.2 fg.2.2.2
    Pi.œÄ _ _ ‚â´ P.map (pullback.snd _ _).op

theorem w : forkMap P R ‚â´ firstMap P R = forkMap P R ‚â´ secondMap P R := by
  dsimp
  ext fg
  simp only [firstMap, secondMap, forkMap]
  simp only [limit.lift_œÄ, limit.lift_œÄ_assoc, assoc, Fan.mk_œÄ_app]
  haveI := Presieve.hasPullbacks.has_pullbacks fg.1.2.2 fg.2.2.2
  rw [‚Üê P.map_comp, ‚Üê op_comp, pullback.condition]
  simp

/--
The family of elements given by `x : FirstObj P S` is compatible iff `firstMap` and `secondMap`
map it to the same point.
-/
theorem compatible_iff (x : FirstObj P R) :
    ((firstObjEqFamily P R).hom x).Compatible ‚Üî firstMap P R x = secondMap P R x := by
  rw [Presieve.pullbackCompatible_iff]
  constructor
  ¬∑ intro t
    apply Limits.Types.limit_ext
    rintro ‚ü®‚ü®Y, f, hf‚ü©, Z, g, hg‚ü©
    simpa [firstMap, secondMap] using t hf hg
  ¬∑ intro t Y Z f g hf hg
    rw [Types.limit_ext_iff'] at t
    simpa [firstMap, secondMap] using t ‚ü®‚ü®‚ü®Y, f, hf‚ü©, Z, g, hg‚ü©‚ü©

/-- `P` is a sheaf for `R`, iff the fork given by `w` is an equalizer. -/
@[stacks 00VM]
theorem sheaf_condition : R.IsSheafFor P ‚Üî Nonempty (IsLimit (Fork.ofŒπ _ (w P R))) := by
  rw [Types.type_equalizer_iff_unique,
    ‚Üê Equiv.forall_congr_right (firstObjEqFamily P R).toEquiv.symm]
  simp_rw [‚Üê compatible_iff, ‚Üê Iso.toEquiv_fun, Equiv.apply_symm_apply]
  apply forall‚ÇÇ_congr
  intro x _
  apply existsUnique_congr
  intro t
  rw [Equiv.eq_symm_apply]
  constructor
  ¬∑ intro q
    funext Y f hf
    simpa [forkMap] using q _ _
  ¬∑ intro q Y f hf
    rw [‚Üê q]
    simp [forkMap]

namespace Arrows

variable (P : C·µí·µñ ‚•§ Type w) {X : C} (R : Presieve X) (S : Sieve X)

open Presieve

variable {B : C} {I : Type t} [Small.{w} I] (X : I ‚Üí C) (œÄ : (i : I) ‚Üí X i ‚ü∂ B)
    [(Presieve.ofArrows X œÄ).hasPullbacks]

/--
The middle object of the fork diagram of the Stacks entry.
The difference between this and `Equalizer.FirstObj P (ofArrows X œÄ)` arises if the family of
arrows `œÄ` contains duplicates. The `Presieve.ofArrows` doesn't see those.
-/
@[stacks 00VM "The middle object of the fork diagram there."]
def FirstObj : Type w := ‚àè·∂ú (fun i ‚Ü¶ P.obj (op (X i)))

@[ext]
lemma FirstObj.ext (z‚ÇÅ z‚ÇÇ : FirstObj P X) (h : ‚àÄ i, (Pi.œÄ _ i : FirstObj P X ‚ü∂ _) z‚ÇÅ =
    (Pi.œÄ _ i : FirstObj P X ‚ü∂ _) z‚ÇÇ) : z‚ÇÅ = z‚ÇÇ := by
  apply Limits.Types.limit_ext
  rintro ‚ü®i‚ü©
  exact h i

/--
The rightmost object of the fork diagram of the Stacks entry.
The difference between this and `Equalizer.Presieve.SecondObj P (ofArrows X œÄ)` arises if the
family of arrows `œÄ` contains duplicates. The `Presieve.ofArrows` doesn't see those.
-/
@[stacks 00VM "The rightmost object of the fork diagram there."]
def SecondObj : Type w  :=
  ‚àè·∂ú (fun (ij : I √ó I) ‚Ü¶ P.obj (op (pullback (œÄ ij.1) (œÄ ij.2))))

@[ext]
lemma SecondObj.ext (z‚ÇÅ z‚ÇÇ : SecondObj P X œÄ) (h : ‚àÄ ij, (Pi.œÄ _ ij : SecondObj P X œÄ ‚ü∂ _) z‚ÇÅ =
    (Pi.œÄ _ ij : SecondObj P X œÄ ‚ü∂ _) z‚ÇÇ) : z‚ÇÅ = z‚ÇÇ := by
  apply Limits.Types.limit_ext
  rintro ‚ü®i‚ü©
  exact h i

/--
The left morphism of the fork diagram.
-/
def forkMap : P.obj (op B) ‚ü∂ FirstObj P X := Pi.lift (fun i ‚Ü¶ P.map (œÄ i).op)

/--
The first of the two parallel morphisms of the fork diagram, induced by the first projection in
each pullback.
-/
def firstMap : FirstObj P X ‚ü∂ SecondObj P X œÄ :=
  Pi.lift fun _ => Pi.œÄ _ _ ‚â´ P.map (pullback.fst _ _).op

/--
The second of the two parallel morphisms of the fork diagram, induced by the second projection in
each pullback.
-/
def secondMap : FirstObj P X ‚ü∂ SecondObj P X œÄ :=
  Pi.lift fun _ => Pi.œÄ _ _ ‚â´ P.map (pullback.snd _ _).op

theorem w : forkMap P X œÄ ‚â´ firstMap P X œÄ = forkMap P X œÄ ‚â´ secondMap P X œÄ := by
  ext x ij
  simp only [firstMap, secondMap, forkMap, types_comp_apply, Types.pi_lift_œÄ_apply,
    ‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, pullback.condition]

/--
The family of elements given by `x : FirstObj P S` is compatible iff `firstMap` and `secondMap`
map it to the same point.
See `CategoryTheory.Equalizer.Presieve.Arrows.compatible_iff_of_small` for a version with
less universe assumptions.
-/
theorem compatible_iff {I : Type w} (X : I ‚Üí C) (œÄ : (i : I) ‚Üí X i ‚ü∂ B)
    [(Presieve.ofArrows X œÄ).hasPullbacks] (x : FirstObj P X) :
    (Arrows.Compatible P œÄ ((Types.productIso _).hom x)) ‚Üî
      firstMap P X œÄ x = secondMap P X œÄ x := by
  rw [Arrows.pullbackCompatible_iff]
  constructor
  ¬∑ intro t
    ext ij
    simpa [firstMap, secondMap] using t ij.1 ij.2
  ¬∑ intro t i j
    apply_fun Pi.œÄ (fun (ij : I √ó I) ‚Ü¶ P.obj (op (pullback (œÄ ij.1) (œÄ ij.2)))) ‚ü®i, j‚ü© at t
    simpa [firstMap, secondMap] using t

/-- Version of `CategoryTheory.Equalizer.Presieve.Arrows.compatible_iff` for a small
indexing type. -/
lemma compatible_iff_of_small (x : FirstObj P X) :
    (Arrows.Compatible P œÄ ((equivShrink _).symm ((Types.Small.productIso _).hom x))) ‚Üî
      firstMap P X œÄ x = secondMap P X œÄ x := by
  rw [Arrows.pullbackCompatible_iff]
  refine ‚ü®fun t ‚Ü¶ ?_, fun t i j ‚Ü¶ ?_‚ü©
  ¬∑ ext ij
    simpa [firstMap, secondMap] using t ij.1 ij.2
  ¬∑ apply_fun Pi.œÄ (fun (ij : I √ó I) ‚Ü¶ P.obj (op (pullback (œÄ ij.1) (œÄ ij.2)))) ‚ü®i, j‚ü© at t
    simpa [firstMap, secondMap] using t

/-- `P` is a sheaf for `Presieve.ofArrows X œÄ`, iff the fork given by `w` is an equalizer. -/
@[stacks 00VM]
theorem sheaf_condition : (Presieve.ofArrows X œÄ).IsSheafFor P ‚Üî
    Nonempty (IsLimit (Fork.ofŒπ (forkMap P X œÄ) (w P X œÄ))) := by
  rw [Types.type_equalizer_iff_unique, isSheafFor_arrows_iff]
  simp only [FirstObj]
  rw [‚Üê Equiv.forall_congr_right ((equivShrink _).trans (Types.Small.productIso _).toEquiv.symm)]
  simp_rw [‚Üê compatible_iff_of_small, ‚Üê Iso.toEquiv_fun, Equiv.trans_apply, Equiv.apply_symm_apply,
    Equiv.symm_apply_apply]
  apply forall‚ÇÇ_congr
  intro x _
  apply existsUnique_congr
  intro t
  rw [Equiv.eq_symm_apply, ‚Üê Equiv.symm_apply_eq]
  constructor
  ¬∑ intro q
    funext i
    simpa [forkMap] using q i
  ¬∑ intro q i
    rw [‚Üê q]
    simp [forkMap]

end Arrows

/-- The sheaf condition for a single morphism is the same as the canonical fork diagram being
limiting. -/
lemma isSheafFor_singleton_iff {F : C·µí·µñ ‚•§ Type*} {X Y : C} {f : X ‚ü∂ Y}
    (c : PullbackCone f f) (hc : IsLimit c) :
    Presieve.IsSheafFor F (.singleton f) ‚Üî
      Nonempty
        (IsLimit (Fork.ofŒπ (F.map f.op) (f := F.map c.fst.op) (g := F.map c.snd.op)
          (by simp [‚Üê Functor.map_comp, ‚Üê op_comp, c.condition]))) := by
  have h (x : F.obj (op X)) : (‚àÄ {Z : C} (p‚ÇÅ p‚ÇÇ : Z ‚ü∂ X),
      p‚ÇÅ ‚â´ f = p‚ÇÇ ‚â´ f ‚Üí F.map p‚ÇÅ.op x = F.map p‚ÇÇ.op x) ‚Üî F.map c.fst.op x = F.map c.snd.op x := by
    refine ‚ü®fun H ‚Ü¶ H _ _ c.condition, fun H Z p‚ÇÅ p‚ÇÇ h ‚Ü¶ ?_‚ü©
    rw [‚Üê PullbackCone.IsLimit.lift_fst hc _ _ h, op_comp, FunctorToTypes.map_comp_apply, H]
    simp [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]
  rw [Types.type_equalizer_iff_unique, Presieve.isSheafFor_singleton]
  simp_rw [h]

/-- Special case of `isSheafFor_singleton_iff` with `c = pullback.cone f f`. -/
lemma isSheafFor_singleton_iff_of_hasPullback {F : C·µí·µñ ‚•§ Type*} {X Y : C} {f : X ‚ü∂ Y}
    [HasPullback f f] :
    Presieve.IsSheafFor F (.singleton f) ‚Üî
      Nonempty
        (IsLimit (Fork.ofŒπ (F.map f.op) (f := F.map (pullback.fst f f).op)
          (g := F.map (pullback.snd f f).op)
          (by simp [‚Üê Functor.map_comp, ‚Üê op_comp, pullback.condition]))) :=
  isSheafFor_singleton_iff (pullback.cone f f) (pullback.isLimit f f)

end Presieve

end

end Equalizer

variable {C : Type*} [Category C]

/--
If
- `F` contravariantly maps (suitable) coproducts to products,
- (suitable) coproducts exist in `C`, and
- (suitable) coproducts distribute over pullbacks, then:

`F` is a sheaf for the single object covering `{ ‚àê·µ¢ Y·µ¢ ‚ü∂ X }`
if and only if it is a presieve for `{ f·µ¢ : Y·µ¢ ‚ü∂ X }·µ¢`.

Note: The second two conditions are satisfied if `C` is (finitary) extensive.
-/
lemma Presieve.isSheafFor_sigmaDesc_iff {F : C·µí·µñ ‚•§ Type v} {X : C} {Œπ : Type*} [Small.{v} Œπ]
    {Y : Œπ ‚Üí C}
    (f : ‚àÄ i, Y i ‚ü∂ X) [(ofArrows Y f).hasPullbacks]
    [HasCoproduct Y] [HasCoproduct fun (ij : Œπ √ó Œπ) ‚Ü¶ pullback (f ij.1) (f ij.2)]
    [HasPullback (Limits.Sigma.desc f) (Limits.Sigma.desc f)]
    [PreservesLimit (Discrete.functor <| fun i ‚Ü¶ op (Y i)) F]
    [PreservesLimit (Discrete.functor fun (ij : Œπ √ó Œπ) ‚Ü¶ op (pullback (f ij.1) (f ij.2))) F]
    [IsIso (Limits.Sigma.desc fun (ij : Œπ √ó Œπ) ‚Ü¶ Limits.pullback.map (f ij.fst) (f ij.snd)
      (Limits.Sigma.desc f) (Limits.Sigma.desc f) (Limits.Sigma.Œπ _ _) (Limits.Sigma.Œπ _ _) (ùüô X)
      (by simp) (by simp))] :
    Presieve.IsSheafFor F (.singleton <| Limits.Sigma.desc f) ‚Üî
      Presieve.IsSheafFor F (.ofArrows Y f) := by
  let e : (‚àê fun (ij : Œπ √ó Œπ) ‚Ü¶ pullback (f ij.1) (f ij.2)) ‚ü∂
      pullback (Limits.Sigma.desc f) (Limits.Sigma.desc f) :=
    Limits.Sigma.desc fun ij ‚Ü¶
    pullback.map _ _ _ _ (Limits.Sigma.Œπ _ _) (Limits.Sigma.Œπ _ _) (ùüô X) (by simp) (by simp)
  rw [Equalizer.Presieve.isSheafFor_singleton_iff (pullback.cone _ _) (pullback.isLimit _ _),
    Equalizer.Presieve.Arrows.sheaf_condition]
  refine (Fork.isLimitEquivOfIsos _ _ ?_ ?_ ?_ ?_ ?_ ?_).nonempty_congr
  ¬∑ exact F.mapIso (opCoproductIsoProduct Y) ‚â™‚â´ PreservesProduct.iso F _
  ¬∑ exact F.mapIso (.op <| asIso e) ‚â™‚â´ F.mapIso (opCoproductIsoProduct _) ‚â™‚â´
      PreservesProduct.iso F _
  ¬∑ exact Iso.refl _
  ¬∑ refine Pi.hom_ext _ _ fun ij ‚Ü¶ ?_
    simp only [Iso.trans_hom, Functor.mapIso_hom, PreservesProduct.iso_hom, Category.assoc,
      limit.cone_x, PullbackCone.fst_limit_cone, Iso.op_hom, asIso_hom, e, piComparison_comp_œÄ,
      Equalizer.Presieve.Arrows.firstMap]
    rw [‚Üê F.map_comp, opCoproductIsoProduct_hom_comp_œÄ, ‚Üê F.map_comp, ‚Üê op_comp, Sigma.Œπ_desc,
      ‚Üê F.map_comp, ‚Üê op_comp, pullback.lift_fst, Pi.lift_œÄ, piComparison_comp_œÄ_assoc,
      ‚Üê F.map_comp, ‚Üê F.map_comp]
    simp
  ¬∑ refine Pi.hom_ext _ _ fun ij ‚Ü¶ ?_
    simp only [Iso.trans_hom, Functor.mapIso_hom, PreservesProduct.iso_hom, Category.assoc,
      limit.cone_x, PullbackCone.snd_limit_cone, Iso.op_hom, asIso_hom, e, piComparison_comp_œÄ,
      Equalizer.Presieve.Arrows.secondMap]
    rw [‚Üê F.map_comp, opCoproductIsoProduct_hom_comp_œÄ, ‚Üê F.map_comp, ‚Üê op_comp, Sigma.Œπ_desc,
      ‚Üê F.map_comp, ‚Üê op_comp, pullback.lift_snd, Pi.lift_œÄ, piComparison_comp_œÄ_assoc,
      ‚Üê F.map_comp, ‚Üê F.map_comp]
    simp
  ¬∑ refine Pi.hom_ext _ _ fun i ‚Ü¶ ?_
    simp only [Fork.ofŒπ_pt, Fork.Œπ_ofŒπ, Iso.trans_hom, Functor.mapIso_hom, PreservesProduct.iso_hom,
      Category.assoc, piComparison_comp_œÄ]
    rw [‚Üê F.map_comp, ‚Üê F.map_comp, opCoproductIsoProduct_hom_comp_œÄ, ‚Üê op_comp, Sigma.Œπ_desc]
    simp [Equalizer.Presieve.Arrows.forkMap]

end CategoryTheory
