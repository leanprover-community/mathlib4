/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import Mathlib.CategoryTheory.Sites.Plus
import Mathlib.CategoryTheory.Limits.Shapes.ConcreteCategory

#align_import category_theory.sites.sheafification from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!

# Sheafification

We construct the sheafification of a presheaf over a site `C` with values in `D` whenever
`D` is a concrete category for which the forgetful functor preserves the appropriate (co)limits
and reflects isomorphisms.

We generally follow the approach of https://stacks.math.columbia.edu/tag/00W1

-/


namespace CategoryTheory

open CategoryTheory.Limits Opposite

universe w v u

variable {C : Type u} [Category.{v} C] {J : GrothendieckTopology C}
variable {D : Type w} [Category.{max v u} D]

section

variable [ConcreteCategory.{max v u} D]

attribute [local instance] ConcreteCategory.hasCoeToSort ConcreteCategory.instFunLike

-- porting note (#5171): removed @[nolint has_nonempty_instance]
/-- A concrete version of the multiequalizer, to be used below. -/
def Meq {X : C} (P : C·µí·µñ ‚•§ D) (S : J.Cover X) :=
  { x : ‚àÄ I : S.Arrow, P.obj (op I.Y) //
    ‚àÄ I : S.Relation, P.map I.g‚ÇÅ.op (x I.fst) = P.map I.g‚ÇÇ.op (x I.snd) }
#align category_theory.meq CategoryTheory.Meq

end

namespace Meq

variable [ConcreteCategory.{max v u} D]

attribute [local instance] ConcreteCategory.hasCoeToSort ConcreteCategory.instFunLike

instance {X} (P : C·µí·µñ ‚•§ D) (S : J.Cover X) :
    CoeFun (Meq P S) fun _ => ‚àÄ I : S.Arrow, P.obj (op I.Y) :=
  ‚ü®fun x => x.1‚ü©

@[ext]
theorem ext {X} {P : C·µí·µñ ‚•§ D} {S : J.Cover X} (x y : Meq P S) (h : ‚àÄ I : S.Arrow, x I = y I) :
    x = y :=
  Subtype.ext <| funext <| h
#align category_theory.meq.ext CategoryTheory.Meq.ext

theorem condition {X} {P : C·µí·µñ ‚•§ D} {S : J.Cover X} (x : Meq P S) (I : S.Relation) :
    P.map I.g‚ÇÅ.op (x ((S.index P).fstTo I)) = P.map I.g‚ÇÇ.op (x ((S.index P).sndTo I)) :=
  x.2 _
#align category_theory.meq.condition CategoryTheory.Meq.condition

/-- Refine a term of `Meq P T` with respect to a refinement `S ‚ü∂ T` of covers. -/
def refine {X : C} {P : C·µí·µñ ‚•§ D} {S T : J.Cover X} (x : Meq P T) (e : S ‚ü∂ T) : Meq P S :=
  ‚ü®fun I => x ‚ü®I.Y, I.f, (leOfHom e) _ I.hf‚ü©, fun I =>
    x.condition
      ‚ü®I.Y‚ÇÅ, I.Y‚ÇÇ, I.Z, I.g‚ÇÅ, I.g‚ÇÇ, I.f‚ÇÅ, I.f‚ÇÇ, (leOfHom e) _ I.h‚ÇÅ, (leOfHom e) _ I.h‚ÇÇ, I.w‚ü©‚ü©
#align category_theory.meq.refine CategoryTheory.Meq.refine

@[simp]
theorem refine_apply {X : C} {P : C·µí·µñ ‚•§ D} {S T : J.Cover X} (x : Meq P T) (e : S ‚ü∂ T)
    (I : S.Arrow) : x.refine e I = x ‚ü®I.Y, I.f, (leOfHom e) _ I.hf‚ü© :=
  rfl
#align category_theory.meq.refine_apply CategoryTheory.Meq.refine_apply

/-- Pull back a term of `Meq P S` with respect to a morphism `f : Y ‚ü∂ X` in `C`. -/
def pullback {Y X : C} {P : C·µí·µñ ‚•§ D} {S : J.Cover X} (x : Meq P S) (f : Y ‚ü∂ X) :
    Meq P ((J.pullback f).obj S) :=
  ‚ü®fun I => x ‚ü®_, I.f ‚â´ f, I.hf‚ü©, fun I =>
    x.condition
      ‚ü®I.Y‚ÇÅ, I.Y‚ÇÇ, I.Z, I.g‚ÇÅ, I.g‚ÇÇ, I.f‚ÇÅ ‚â´ f, I.f‚ÇÇ ‚â´ f, I.h‚ÇÅ, I.h‚ÇÇ, by simp [I.w_assoc]‚ü©‚ü©
#align category_theory.meq.pullback CategoryTheory.Meq.pullback

@[simp]
theorem pullback_apply {Y X : C} {P : C·µí·µñ ‚•§ D} {S : J.Cover X} (x : Meq P S) (f : Y ‚ü∂ X)
    (I : ((J.pullback f).obj S).Arrow) : x.pullback f I = x ‚ü®_, I.f ‚â´ f, I.hf‚ü© :=
  rfl
#align category_theory.meq.pullback_apply CategoryTheory.Meq.pullback_apply

@[simp]
theorem pullback_refine {Y X : C} {P : C·µí·µñ ‚•§ D} {S T : J.Cover X} (h : S ‚ü∂ T) (f : Y ‚ü∂ X)
    (x : Meq P T) : (x.pullback f).refine ((J.pullback f).map h) = (refine x h).pullback _ :=
  rfl
#align category_theory.meq.pullback_refine CategoryTheory.Meq.pullback_refine

/-- Make a term of `Meq P S`. -/
def mk {X : C} {P : C·µí·µñ ‚•§ D} (S : J.Cover X) (x : P.obj (op X)) : Meq P S :=
  ‚ü®fun I => P.map I.f.op x, fun I => by
    dsimp
    simp only [‚Üê comp_apply, ‚Üê P.map_comp, ‚Üê op_comp, I.w]‚ü©
#align category_theory.meq.mk CategoryTheory.Meq.mk

theorem mk_apply {X : C} {P : C·µí·µñ ‚•§ D} (S : J.Cover X) (x : P.obj (op X)) (I : S.Arrow) :
    mk S x I = P.map I.f.op x :=
  rfl
#align category_theory.meq.mk_apply CategoryTheory.Meq.mk_apply

variable [PreservesLimits (forget D)]

/-- The equivalence between the type associated to `multiequalizer (S.index P)` and `Meq P S`. -/
noncomputable def equiv {X : C} (P : C·µí·µñ ‚•§ D) (S : J.Cover X) [HasMultiequalizer (S.index P)] :
    (multiequalizer (S.index P) : D) ‚âÉ Meq P S :=
  Limits.Concrete.multiequalizerEquiv _
#align category_theory.meq.equiv CategoryTheory.Meq.equiv

@[simp]
theorem equiv_apply {X : C} {P : C·µí·µñ ‚•§ D} {S : J.Cover X} [HasMultiequalizer (S.index P)]
    (x : (multiequalizer (S.index P) : D)) (I : S.Arrow) :
    equiv P S x I = Multiequalizer.Œπ (S.index P) I x :=
  rfl
#align category_theory.meq.equiv_apply CategoryTheory.Meq.equiv_apply

@[simp]
theorem equiv_symm_eq_apply {X : C} {P : C·µí·µñ ‚•§ D} {S : J.Cover X} [HasMultiequalizer (S.index P)]
    (x : Meq P S) (I : S.Arrow) :
    Multiequalizer.Œπ (S.index P) I ((Meq.equiv P S).symm x) = x I := by
  rw [‚Üê equiv_apply]
  simp
#align category_theory.meq.equiv_symm_eq_apply CategoryTheory.Meq.equiv_symm_eq_apply

end Meq

namespace GrothendieckTopology

namespace Plus

variable [ConcreteCategory.{max v u} D]

attribute [local instance] ConcreteCategory.hasCoeToSort ConcreteCategory.instFunLike

variable [PreservesLimits (forget D)]
variable [‚àÄ X : C, HasColimitsOfShape (J.Cover X)·µí·µñ D]
variable [‚àÄ (P : C·µí·µñ ‚•§ D) (X : C) (S : J.Cover X), HasMultiequalizer (S.index P)]

noncomputable section

/-- Make a term of `(J.plusObj P).obj (op X)` from `x : Meq P S`. -/
def mk {X : C} {P : C·µí·µñ ‚•§ D} {S : J.Cover X} (x : Meq P S) : (J.plusObj P).obj (op X) :=
  colimit.Œπ (J.diagram P X) (op S) ((Meq.equiv P S).symm x)
#align category_theory.grothendieck_topology.plus.mk CategoryTheory.GrothendieckTopology.Plus.mk

theorem res_mk_eq_mk_pullback {Y X : C} {P : C·µí·µñ ‚•§ D} {S : J.Cover X} (x : Meq P S) (f : Y ‚ü∂ X) :
    (J.plusObj P).map f.op (mk x) = mk (x.pullback f) := by
  dsimp [mk, plusObj]
  rw [‚Üê comp_apply (x := (Meq.equiv P S).symm x), Œπ_colimMap_assoc, colimit.Œπ_pre,
    comp_apply (x := (Meq.equiv P S).symm x)]
  apply congr_arg
  apply (Meq.equiv P _).injective
  erw [Equiv.apply_symm_apply]
  ext i
  simp only [Functor.op_obj, unop_op, pullback_obj, diagram_obj, Functor.comp_obj,
    diagramPullback_app, Meq.equiv_apply, Meq.pullback_apply]
  erw [‚Üê comp_apply, Multiequalizer.lift_Œπ, Meq.equiv_symm_eq_apply]
  cases i; rfl
#align category_theory.grothendieck_topology.plus.res_mk_eq_mk_pullback CategoryTheory.GrothendieckTopology.Plus.res_mk_eq_mk_pullback

theorem toPlus_mk {X : C} {P : C·µí·µñ ‚•§ D} (S : J.Cover X) (x : P.obj (op X)) :
    (J.toPlus P).app _ x = mk (Meq.mk S x) := by
  dsimp [mk, toPlus]
  let e : S ‚ü∂ ‚ä§ := homOfLE (OrderTop.le_top _)
  rw [‚Üê colimit.w _ e.op]
  delta Cover.toMultiequalizer
  erw [comp_apply, comp_apply]
  apply congr_arg
  dsimp [diagram]
  apply Concrete.multiequalizer_ext
  intro i
  simp only [‚Üê comp_apply, Category.assoc, Multiequalizer.lift_Œπ, Category.comp_id,
    Meq.equiv_symm_eq_apply]
  rfl
#align category_theory.grothendieck_topology.plus.to_plus_mk CategoryTheory.GrothendieckTopology.Plus.toPlus_mk

theorem toPlus_apply {X : C} {P : C·µí·µñ ‚•§ D} (S : J.Cover X) (x : Meq P S) (I : S.Arrow) :
    (J.toPlus P).app _ (x I) = (J.plusObj P).map I.f.op (mk x) := by
  dsimp only [toPlus, plusObj]
  delta Cover.toMultiequalizer
  dsimp [mk]
  erw [‚Üê comp_apply]
  rw [Œπ_colimMap_assoc, colimit.Œπ_pre, comp_apply, comp_apply]
  dsimp only [Functor.op]
  let e : (J.pullback I.f).obj (unop (op S)) ‚ü∂ ‚ä§ := homOfLE (OrderTop.le_top _)
  rw [‚Üê colimit.w _ e.op]
  erw [comp_apply]
  apply congr_arg
  apply Concrete.multiequalizer_ext
  intro i
  dsimp [diagram]
  rw [‚Üê comp_apply, ‚Üê comp_apply, ‚Üê comp_apply, Multiequalizer.lift_Œπ, Multiequalizer.lift_Œπ,
    Multiequalizer.lift_Œπ]
  erw [Meq.equiv_symm_eq_apply]
  let RR : S.Relation :=
    ‚ü®_, _, _, i.f, ùüô _, I.f, i.f ‚â´ I.f, I.hf, Sieve.downward_closed _ I.hf _, by simp‚ü©
  erw [x.condition RR]
  simp only [unop_op, pullback_obj, op_id, Functor.map_id, id_apply]
  rfl
#align category_theory.grothendieck_topology.plus.to_plus_apply CategoryTheory.GrothendieckTopology.Plus.toPlus_apply

theorem toPlus_eq_mk {X : C} {P : C·µí·µñ ‚•§ D} (x : P.obj (op X)) :
    (J.toPlus P).app _ x = mk (Meq.mk ‚ä§ x) := by
  dsimp [mk, toPlus]
  delta Cover.toMultiequalizer
  simp only [comp_apply]
  apply congr_arg
  apply (Meq.equiv P ‚ä§).injective
  ext i
  rw [Meq.equiv_apply, Equiv.apply_symm_apply, ‚Üê comp_apply, Multiequalizer.lift_Œπ]
  rfl
#align category_theory.grothendieck_topology.plus.to_plus_eq_mk CategoryTheory.GrothendieckTopology.Plus.toPlus_eq_mk

variable [‚àÄ X : C, PreservesColimitsOfShape (J.Cover X)·µí·µñ (forget D)]

theorem exists_rep {X : C} {P : C·µí·µñ ‚•§ D} (x : (J.plusObj P).obj (op X)) :
    ‚àÉ (S : J.Cover X) (y : Meq P S), x = mk y := by
  obtain ‚ü®S, y, h‚ü© := Concrete.colimit_exists_rep (J.diagram P X) x
  use S.unop, Meq.equiv _ _ y
  rw [‚Üê h]
  dsimp [mk]
  simp
#align category_theory.grothendieck_topology.plus.exists_rep CategoryTheory.GrothendieckTopology.Plus.exists_rep

theorem eq_mk_iff_exists {X : C} {P : C·µí·µñ ‚•§ D} {S T : J.Cover X} (x : Meq P S) (y : Meq P T) :
    mk x = mk y ‚Üî ‚àÉ (W : J.Cover X) (h1 : W ‚ü∂ S) (h2 : W ‚ü∂ T), x.refine h1 = y.refine h2 := by
  constructor
  ¬∑ intro h
    obtain ‚ü®W, h1, h2, hh‚ü© := Concrete.colimit_exists_of_rep_eq.{u} _ _ _ h
    use W.unop, h1.unop, h2.unop
    ext I
    apply_fun Multiequalizer.Œπ (W.unop.index P) I at hh
    convert hh
    all_goals
      dsimp [diagram]
      erw [‚Üê comp_apply, Multiequalizer.lift_Œπ, Meq.equiv_symm_eq_apply]
      cases I; rfl
  ¬∑ rintro ‚ü®S, h1, h2, e‚ü©
    apply Concrete.colimit_rep_eq_of_exists
    use op S, h1.op, h2.op
    apply Concrete.multiequalizer_ext
    intro i
    apply_fun fun ee => ee i at e
    convert e
    all_goals
      dsimp [diagram]
      rw [‚Üê comp_apply, Multiequalizer.lift_Œπ]
      erw [Meq.equiv_symm_eq_apply]
      cases i; rfl
#align category_theory.grothendieck_topology.plus.eq_mk_iff_exists CategoryTheory.GrothendieckTopology.Plus.eq_mk_iff_exists

/-- `P‚Å∫` is always separated. -/
theorem sep {X : C} (P : C·µí·µñ ‚•§ D) (S : J.Cover X) (x y : (J.plusObj P).obj (op X))
    (h : ‚àÄ I : S.Arrow, (J.plusObj P).map I.f.op x = (J.plusObj P).map I.f.op y) : x = y := by
  -- First, we choose representatives for x and y.
  obtain ‚ü®Sx, x, rfl‚ü© := exists_rep x
  obtain ‚ü®Sy, y, rfl‚ü© := exists_rep y
  simp only [res_mk_eq_mk_pullback] at h
  -- Next, using our assumption,
  -- choose covers over which the pullbacks of these representatives become equal.
  choose W h1 h2 hh using fun I : S.Arrow => (eq_mk_iff_exists _ _).mp (h I)
  -- To prove equality, it suffices to prove that there exists a cover over which
  -- the representatives become equal.
  rw [eq_mk_iff_exists]
  -- Construct the cover over which the representatives become equal by combining the various
  -- covers chosen above.
  let B : J.Cover X := S.bind W
  use B
  -- Prove that this cover refines the two covers over which our representatives are defined
  -- and use these proofs.
  let ex : B ‚ü∂ Sx :=
    homOfLE
      (by
        rintro Y f ‚ü®Z, e1, e2, he2, he1, hee‚ü©
        rw [‚Üê hee]
        apply leOfHom (h1 ‚ü®_, _, he2‚ü©)
        exact he1)
  let ey : B ‚ü∂ Sy :=
    homOfLE
      (by
        rintro Y f ‚ü®Z, e1, e2, he2, he1, hee‚ü©
        rw [‚Üê hee]
        apply leOfHom (h2 ‚ü®_, _, he2‚ü©)
        exact he1)
  use ex, ey
  -- Now prove that indeed the representatives become equal over `B`.
  -- This will follow by using the fact that our representatives become
  -- equal over the chosen covers.
  ext1 I
  let IS : S.Arrow := I.fromMiddle
  specialize hh IS
  let IW : (W IS).Arrow := I.toMiddle
  apply_fun fun e => e IW at hh
  convert hh using 1
  ¬∑ let Rx : Sx.Relation :=
      ‚ü®I.Y, I.Y, I.Y, ùüô _, ùüô _, I.f, I.toMiddleHom ‚â´ I.fromMiddleHom, leOfHom ex _ I.hf,
        by simpa only [I.middle_spec] using leOfHom ex _ I.hf, by simp [I.middle_spec]‚ü©
    simpa [id_apply] using x.condition Rx
  ¬∑ let Ry : Sy.Relation :=
      ‚ü®I.Y, I.Y, I.Y, ùüô _, ùüô _, I.f, I.toMiddleHom ‚â´ I.fromMiddleHom, leOfHom ey _ I.hf,
        by simpa only [I.middle_spec] using leOfHom ey _ I.hf, by simp [I.middle_spec]‚ü©
    simpa [id_apply] using y.condition Ry
#align category_theory.grothendieck_topology.plus.sep CategoryTheory.GrothendieckTopology.Plus.sep

theorem inj_of_sep (P : C·µí·µñ ‚•§ D)
    (hsep :
      ‚àÄ (X : C) (S : J.Cover X) (x y : P.obj (op X)),
        (‚àÄ I : S.Arrow, P.map I.f.op x = P.map I.f.op y) ‚Üí x = y)
    (X : C) : Function.Injective ((J.toPlus P).app (op X)) := by
  intro x y h
  simp only [toPlus_eq_mk] at h
  rw [eq_mk_iff_exists] at h
  obtain ‚ü®W, h1, h2, hh‚ü© := h
  apply hsep X W
  intro I
  apply_fun fun e => e I at hh
  exact hh
#align category_theory.grothendieck_topology.plus.inj_of_sep CategoryTheory.GrothendieckTopology.Plus.inj_of_sep

/-- An auxiliary definition to be used in the proof of `exists_of_sep` below.
  Given a compatible family of local sections for `P‚Å∫`, and representatives of said sections,
  construct a compatible family of local sections of `P` over the combination of the covers
  associated to the representatives.
  The separatedness condition is used to prove compatibility among these local sections of `P`. -/
def meqOfSep (P : C·µí·µñ ‚•§ D)
    (hsep :
      ‚àÄ (X : C) (S : J.Cover X) (x y : P.obj (op X)),
        (‚àÄ I : S.Arrow, P.map I.f.op x = P.map I.f.op y) ‚Üí x = y)
    (X : C) (S : J.Cover X) (s : Meq (J.plusObj P) S) (T : ‚àÄ I : S.Arrow, J.Cover I.Y)
    (t : ‚àÄ I : S.Arrow, Meq P (T I)) (ht : ‚àÄ I : S.Arrow, s I = mk (t I)) : Meq P (S.bind T) where
  val I := t I.fromMiddle I.toMiddle
  property := by
    intro II
    apply inj_of_sep P hsep
    rw [‚Üê comp_apply, ‚Üê comp_apply, (J.toPlus P).naturality, (J.toPlus P).naturality, comp_apply,
      comp_apply]
    erw [toPlus_apply (T II.fst.fromMiddle) (t II.fst.fromMiddle) II.fst.toMiddle,
      toPlus_apply (T II.snd.fromMiddle) (t II.snd.fromMiddle) II.snd.toMiddle, ‚Üê ht, ‚Üê ht, ‚Üê
      comp_apply, ‚Üê comp_apply, ‚Üê (J.plusObj P).map_comp, ‚Üê (J.plusObj P).map_comp]
    rw [‚Üê op_comp, ‚Üê op_comp]
    let IR : S.Relation :=
      ‚ü®_, _, _, II.g‚ÇÅ ‚â´ II.fst.toMiddleHom, II.g‚ÇÇ ‚â´ II.snd.toMiddleHom, II.fst.fromMiddleHom,
        II.snd.fromMiddleHom, II.fst.from_middle_condition, II.snd.from_middle_condition, by
          simpa only [Category.assoc, II.fst.middle_spec, II.snd.middle_spec] using II.w‚ü©
    exact s.condition IR
#align category_theory.grothendieck_topology.plus.meq_of_sep CategoryTheory.GrothendieckTopology.Plus.meqOfSep

theorem exists_of_sep (P : C·µí·µñ ‚•§ D)
    (hsep :
      ‚àÄ (X : C) (S : J.Cover X) (x y : P.obj (op X)),
        (‚àÄ I : S.Arrow, P.map I.f.op x = P.map I.f.op y) ‚Üí x = y)
    (X : C) (S : J.Cover X) (s : Meq (J.plusObj P) S) :
    ‚àÉ t : (J.plusObj P).obj (op X), Meq.mk S t = s := by
  have inj : ‚àÄ X : C, Function.Injective ((J.toPlus P).app (op X)) := inj_of_sep _ hsep
  -- Choose representatives for the given local sections.
  choose T t ht using fun I => exists_rep (s I)
  -- Construct a large cover over which we will define a representative that will
  -- provide the gluing of the given local sections.
  let B : J.Cover X := S.bind T
  choose Z e1 e2 he2 _ _ using fun I : B.Arrow => I.hf
  -- Construct a compatible system of local sections over this large cover, using the chosen
  -- representatives of our local sections.
  -- The compatibility here follows from the separatedness assumption.
  let w : Meq P B := meqOfSep P hsep X S s T t ht
  -- The associated gluing will be the candidate section.
  use mk w
  ext I
  dsimp [Meq.mk]
  erw [ht, res_mk_eq_mk_pullback]
  -- Use the separatedness of `P‚Å∫` to prove that this is indeed a gluing of our
  -- original local sections.
  apply sep P (T I)
  intro II
  simp only [res_mk_eq_mk_pullback, eq_mk_iff_exists]
  -- It suffices to prove equality for representatives over a
  -- convenient sufficiently large cover...
  use (J.pullback II.f).obj (T I)
  let e0 : (J.pullback II.f).obj (T I) ‚ü∂ (J.pullback II.f).obj ((J.pullback I.f).obj B) :=
    homOfLE
      (by
        intro Y f hf
        apply Sieve.le_pullback_bind _ _ _ I.hf
        ¬∑ cases I
          exact hf)
  use e0, ùüô _
  ext IV
  let IA : B.Arrow := ‚ü®_, (IV.f ‚â´ II.f) ‚â´ I.f,
    ‚ü®I.Y, _, _, I.hf, Sieve.downward_closed _ II.hf _, rfl‚ü©‚ü©
  let IB : S.Arrow := IA.fromMiddle
  let IC : (T IB).Arrow := IA.toMiddle
  let ID : (T I).Arrow := ‚ü®IV.Y, IV.f ‚â´ II.f, Sieve.downward_closed (T I).sieve II.hf IV.f‚ü©
  change t IB IC = t I ID
  apply inj IV.Y
  erw [toPlus_apply (T I) (t I) ID, toPlus_apply (T IB) (t IB) IC, ‚Üê ht, ‚Üê ht]
  -- Conclude by constructing the relation showing equality...
  let IR : S.Relation := ‚ü®_, _, IV.Y, IC.f, ID.f, IB.f, I.f, IB.hf, I.hf, IA.middle_spec‚ü©
  exact s.condition IR
#align category_theory.grothendieck_topology.plus.exists_of_sep CategoryTheory.GrothendieckTopology.Plus.exists_of_sep

variable [(forget D).ReflectsIsomorphisms]

/-- If `P` is separated, then `P‚Å∫` is a sheaf. -/
theorem isSheaf_of_sep (P : C·µí·µñ ‚•§ D)
    (hsep :
      ‚àÄ (X : C) (S : J.Cover X) (x y : P.obj (op X)),
        (‚àÄ I : S.Arrow, P.map I.f.op x = P.map I.f.op y) ‚Üí x = y) :
    Presheaf.IsSheaf J (J.plusObj P) := by
  rw [Presheaf.isSheaf_iff_multiequalizer]
  intro X S
  apply @isIso_of_reflects_iso _ _ _ _ _ _ _ (forget D) ?_
  rw [isIso_iff_bijective]
  constructor
  ¬∑ intro x y h
    apply sep P S _ _
    intro I
    apply_fun Meq.equiv _ _ at h
    apply_fun fun e => e I at h
    convert h <;> erw [Meq.equiv_apply, ‚Üê comp_apply, Multiequalizer.lift_Œπ] <;> rfl
  ¬∑ rintro (x : (multiequalizer (S.index _) : D))
    obtain ‚ü®t, ht‚ü© := exists_of_sep P hsep X S (Meq.equiv _ _ x)
    use t
    apply (Meq.equiv _ _).injective
    rw [‚Üê ht]
    ext i
    dsimp
    erw [‚Üê comp_apply]
    rw [Multiequalizer.lift_Œπ]
    rfl
#align category_theory.grothendieck_topology.plus.is_sheaf_of_sep CategoryTheory.GrothendieckTopology.Plus.isSheaf_of_sep

variable (J)

/-- `P‚Å∫‚Å∫` is always a sheaf. -/
theorem isSheaf_plus_plus (P : C·µí·µñ ‚•§ D) : Presheaf.IsSheaf J (J.plusObj (J.plusObj P)) := by
  apply isSheaf_of_sep
  intro X S x y
  apply sep
#align category_theory.grothendieck_topology.plus.is_sheaf_plus_plus CategoryTheory.GrothendieckTopology.Plus.isSheaf_plus_plus

end

end Plus

variable (J)
variable [‚àÄ (P : C·µí·µñ ‚•§ D) (X : C) (S : J.Cover X), HasMultiequalizer (S.index P)]
  [‚àÄ X : C, HasColimitsOfShape (J.Cover X)·µí·µñ D]

/-- The sheafification of a presheaf `P`.
*NOTE:* Additional hypotheses are needed to obtain a proof that this is a sheaf! -/
noncomputable def sheafify (P : C·µí·µñ ‚•§ D) : C·µí·µñ ‚•§ D :=
  J.plusObj (J.plusObj P)
#align category_theory.grothendieck_topology.sheafify CategoryTheory.GrothendieckTopology.sheafify

/-- The canonical map from `P` to its sheafification. -/
noncomputable def toSheafify (P : C·µí·µñ ‚•§ D) : P ‚ü∂ J.sheafify P :=
  J.toPlus P ‚â´ J.plusMap (J.toPlus P)
#align category_theory.grothendieck_topology.to_sheafify CategoryTheory.GrothendieckTopology.toSheafify

/-- The canonical map on sheafifications induced by a morphism. -/
noncomputable def sheafifyMap {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) : J.sheafify P ‚ü∂ J.sheafify Q :=
  J.plusMap <| J.plusMap Œ∑
#align category_theory.grothendieck_topology.sheafify_map CategoryTheory.GrothendieckTopology.sheafifyMap

@[simp]
theorem sheafifyMap_id (P : C·µí·µñ ‚•§ D) : J.sheafifyMap (ùüô P) = ùüô (J.sheafify P) := by
  dsimp [sheafifyMap, sheafify]
  simp
#align category_theory.grothendieck_topology.sheafify_map_id CategoryTheory.GrothendieckTopology.sheafifyMap_id

@[simp]
theorem sheafifyMap_comp {P Q R : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (Œ≥ : Q ‚ü∂ R) :
    J.sheafifyMap (Œ∑ ‚â´ Œ≥) = J.sheafifyMap Œ∑ ‚â´ J.sheafifyMap Œ≥ := by
  dsimp [sheafifyMap, sheafify]
  simp
#align category_theory.grothendieck_topology.sheafify_map_comp CategoryTheory.GrothendieckTopology.sheafifyMap_comp

@[reassoc (attr := simp)]
theorem toSheafify_naturality {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) :
    Œ∑ ‚â´ J.toSheafify _ = J.toSheafify _ ‚â´ J.sheafifyMap Œ∑ := by
  dsimp [sheafifyMap, sheafify, toSheafify]
  simp
#align category_theory.grothendieck_topology.to_sheafify_naturality CategoryTheory.GrothendieckTopology.toSheafify_naturality

variable (D)

/-- The sheafification of a presheaf `P`, as a functor.
*NOTE:* Additional hypotheses are needed to obtain a proof that this is a sheaf! -/
noncomputable def sheafification : (C·µí·µñ ‚•§ D) ‚•§ C·µí·µñ ‚•§ D :=
  J.plusFunctor D ‚ãô J.plusFunctor D
#align category_theory.grothendieck_topology.sheafification CategoryTheory.GrothendieckTopology.sheafification

@[simp]
theorem sheafification_obj (P : C·µí·µñ ‚•§ D) : (J.sheafification D).obj P = J.sheafify P :=
  rfl
#align category_theory.grothendieck_topology.sheafification_obj CategoryTheory.GrothendieckTopology.sheafification_obj

@[simp]
theorem sheafification_map {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) :
    (J.sheafification D).map Œ∑ = J.sheafifyMap Œ∑ :=
  rfl
#align category_theory.grothendieck_topology.sheafification_map CategoryTheory.GrothendieckTopology.sheafification_map

/-- The canonical map from `P` to its sheafification, as a natural transformation.
*Note:* We only show this is a sheaf under additional hypotheses on `D`. -/
noncomputable def toSheafification : ùü≠ _ ‚ü∂ sheafification J D :=
  J.toPlusNatTrans D ‚â´ whiskerRight (J.toPlusNatTrans D) (J.plusFunctor D)
#align category_theory.grothendieck_topology.to_sheafification CategoryTheory.GrothendieckTopology.toSheafification

@[simp]
theorem toSheafification_app (P : C·µí·µñ ‚•§ D) :
    (J.toSheafification D).app P = J.toSheafify P :=
  rfl
#align category_theory.grothendieck_topology.to_sheafification_app CategoryTheory.GrothendieckTopology.toSheafification_app

variable {D}

theorem isIso_toSheafify {P : C·µí·µñ ‚•§ D} (hP : Presheaf.IsSheaf J P) : IsIso (J.toSheafify P) := by
  dsimp [toSheafify]
  haveI := isIso_toPlus_of_isSheaf J P hP
  change (IsIso (toPlus J P ‚â´ (J.plusFunctor D).map (toPlus J P)))
  infer_instance
#align category_theory.grothendieck_topology.is_iso_to_sheafify CategoryTheory.GrothendieckTopology.isIso_toSheafify

/-- If `P` is a sheaf, then `P` is isomorphic to `J.sheafify P`. -/
noncomputable def isoSheafify {P : C·µí·µñ ‚•§ D} (hP : Presheaf.IsSheaf J P) : P ‚âÖ J.sheafify P :=
  letI := isIso_toSheafify J hP
  asIso (J.toSheafify P)
#align category_theory.grothendieck_topology.iso_sheafify CategoryTheory.GrothendieckTopology.isoSheafify

@[simp]
theorem isoSheafify_hom {P : C·µí·µñ ‚•§ D} (hP : Presheaf.IsSheaf J P) :
    (J.isoSheafify hP).hom = J.toSheafify P :=
  rfl
#align category_theory.grothendieck_topology.iso_sheafify_hom CategoryTheory.GrothendieckTopology.isoSheafify_hom

/-- Given a sheaf `Q` and a morphism `P ‚ü∂ Q`, construct a morphism from `J.sheafify P` to `Q`. -/
noncomputable def sheafifyLift {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (hQ : Presheaf.IsSheaf J Q) :
    J.sheafify P ‚ü∂ Q :=
  J.plusLift (J.plusLift Œ∑ hQ) hQ
#align category_theory.grothendieck_topology.sheafify_lift CategoryTheory.GrothendieckTopology.sheafifyLift

@[reassoc (attr := simp)]
theorem toSheafify_sheafifyLift {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (hQ : Presheaf.IsSheaf J Q) :
    J.toSheafify P ‚â´ sheafifyLift J Œ∑ hQ = Œ∑ := by
  dsimp only [sheafifyLift, toSheafify]
  simp
#align category_theory.grothendieck_topology.to_sheafify_sheafify_lift CategoryTheory.GrothendieckTopology.toSheafify_sheafifyLift

theorem sheafifyLift_unique {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (hQ : Presheaf.IsSheaf J Q)
    (Œ≥ : J.sheafify P ‚ü∂ Q) : J.toSheafify P ‚â´ Œ≥ = Œ∑ ‚Üí Œ≥ = sheafifyLift J Œ∑ hQ := by
  intro h
  apply plusLift_unique
  apply plusLift_unique
  rw [‚Üê Category.assoc, ‚Üê plusMap_toPlus]
  exact h
#align category_theory.grothendieck_topology.sheafify_lift_unique CategoryTheory.GrothendieckTopology.sheafifyLift_unique

@[simp]
theorem isoSheafify_inv {P : C·µí·µñ ‚•§ D} (hP : Presheaf.IsSheaf J P) :
    (J.isoSheafify hP).inv = J.sheafifyLift (ùüô _) hP := by
  apply J.sheafifyLift_unique
  simp [Iso.comp_inv_eq]
#align category_theory.grothendieck_topology.iso_sheafify_inv CategoryTheory.GrothendieckTopology.isoSheafify_inv

theorem sheafify_hom_ext {P Q : C·µí·µñ ‚•§ D} (Œ∑ Œ≥ : J.sheafify P ‚ü∂ Q) (hQ : Presheaf.IsSheaf J Q)
    (h : J.toSheafify P ‚â´ Œ∑ = J.toSheafify P ‚â´ Œ≥) : Œ∑ = Œ≥ := by
  apply J.plus_hom_ext _ _ hQ
  apply J.plus_hom_ext _ _ hQ
  rw [‚Üê Category.assoc, ‚Üê Category.assoc, ‚Üê plusMap_toPlus]
  exact h
#align category_theory.grothendieck_topology.sheafify_hom_ext CategoryTheory.GrothendieckTopology.sheafify_hom_ext

@[reassoc (attr := simp)]
theorem sheafifyMap_sheafifyLift {P Q R : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (Œ≥ : Q ‚ü∂ R)
    (hR : Presheaf.IsSheaf J R) :
    J.sheafifyMap Œ∑ ‚â´ J.sheafifyLift Œ≥ hR = J.sheafifyLift (Œ∑ ‚â´ Œ≥) hR := by
  apply J.sheafifyLift_unique
  rw [‚Üê Category.assoc, ‚Üê J.toSheafify_naturality, Category.assoc, toSheafify_sheafifyLift]
#align category_theory.grothendieck_topology.sheafify_map_sheafify_lift CategoryTheory.GrothendieckTopology.sheafifyMap_sheafifyLift

end GrothendieckTopology

variable (J)
variable [ConcreteCategory.{max v u} D] [PreservesLimits (forget D)]
  [‚àÄ (P : C·µí·µñ ‚•§ D) (X : C) (S : J.Cover X), HasMultiequalizer (S.index P)]
  [‚àÄ X : C, HasColimitsOfShape (J.Cover X)·µí·µñ D]
  [‚àÄ X : C, PreservesColimitsOfShape (J.Cover X)·µí·µñ (forget D)] [(forget D).ReflectsIsomorphisms]

theorem GrothendieckTopology.sheafify_isSheaf (P : C·µí·µñ ‚•§ D) : Presheaf.IsSheaf J (J.sheafify P) :=
  GrothendieckTopology.Plus.isSheaf_plus_plus _ _
#align category_theory.grothendieck_topology.sheafify_is_sheaf CategoryTheory.GrothendieckTopology.sheafify_isSheaf

variable (D)

/-- The sheafification functor, as a functor taking values in `Sheaf`. -/
@[simps]
noncomputable def plusPlusSheaf : (C·µí·µñ ‚•§ D) ‚•§ Sheaf J D where
  obj P := ‚ü®J.sheafify P, J.sheafify_isSheaf P‚ü©
  map Œ∑ := ‚ü®J.sheafifyMap Œ∑‚ü©
  map_id _ := Sheaf.Hom.ext _ _ <| J.sheafifyMap_id _
  map_comp _ _ := Sheaf.Hom.ext _ _ <| J.sheafifyMap_comp _ _
set_option linter.uppercaseLean3 false in
#align category_theory.presheaf_to_Sheaf CategoryTheory.plusPlusSheaf

instance plusPlusSheaf_preservesZeroMorphisms [Preadditive D] :
    (plusPlusSheaf J D).PreservesZeroMorphisms where
  map_zero F G := by
    ext : 3
    refine colimit.hom_ext (fun j => ?_)
    erw [colimit.Œπ_map, comp_zero, J.plusMap_zero, J.diagramNatTrans_zero, zero_comp]
set_option linter.uppercaseLean3 false in
#align category_theory.presheaf_to_Sheaf_preserves_zero_morphisms CategoryTheory.plusPlusSheaf_preservesZeroMorphisms

/-- The sheafification functor is left adjoint to the forgetful functor. -/
@[simps! unit_app counit_app_val]
noncomputable def plusPlusAdjunction : plusPlusSheaf J D ‚ä£ sheafToPresheaf J D :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun P Q =>
        { toFun := fun e => J.toSheafify P ‚â´ e.val
          invFun := fun e => ‚ü®J.sheafifyLift e Q.2‚ü©
          left_inv := fun e => Sheaf.Hom.ext _ _ <| (J.sheafifyLift_unique _ _ _ rfl).symm
          right_inv := fun e => J.toSheafify_sheafifyLift _ _ }
      homEquiv_naturality_left_symm := by
        intro P Q R Œ∑ Œ≥; ext1; dsimp; symm
        apply J.sheafifyMap_sheafifyLift
      homEquiv_naturality_right := fun Œ∑ Œ≥ => by
        dsimp
        rw [Category.assoc] }
#align category_theory.sheafification_adjunction CategoryTheory.plusPlusAdjunction

instance sheafToPresheaf_isRightAdjoint : (sheafToPresheaf J D).IsRightAdjoint  :=
  (plusPlusAdjunction J D).isRightAdjoint
set_option linter.uppercaseLean3 false in
#align category_theory.Sheaf_to_presheaf_is_right_adjoint CategoryTheory.sheafToPresheaf_isRightAdjoint

instance presheaf_mono_of_mono {F G : Sheaf J D} (f : F ‚ü∂ G) [Mono f] : Mono f.1 :=
  (sheafToPresheaf J D).map_mono _
#align category_theory.presheaf_mono_of_mono CategoryTheory.presheaf_mono_of_mono

theorem Sheaf.Hom.mono_iff_presheaf_mono {F G : Sheaf J D} (f : F ‚ü∂ G) : Mono f ‚Üî Mono f.1 :=
  ‚ü®fun m => by infer_instance, fun m => by exact Sheaf.Hom.mono_of_presheaf_mono J D f‚ü©
set_option linter.uppercaseLean3 false in
#align category_theory.Sheaf.hom.mono_iff_presheaf_mono CategoryTheory.Sheaf.Hom.mono_iff_presheaf_mono

-- Porting note: added to ease the port of CategoryTheory.Sites.LeftExact
-- in mathlib, this was `by refl`, but here it would timeout
@[simps! hom_app inv_app]
noncomputable
def GrothendieckTopology.sheafificationIsoPresheafToSheafCompSheafToPreasheaf :
    J.sheafification D ‚âÖ plusPlusSheaf J D ‚ãô sheafToPresheaf J D :=
  NatIso.ofComponents fun P => Iso.refl _

end CategoryTheory
