/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.CategoryTheory.Sites.OneHypercover
import Mathlib.CategoryTheory.Quotient

/-!

-/

universe w'' w' w v u

namespace CategoryTheory

open Limits

variable {C : Type u} [Category.{v} C] {A : Type*} [Category A]

@[simp]
lemma Sieve.ofArrows_pUnit {X Y : C} (f : X âŸ¶ Y) :
    Sieve.ofArrows _ (fun _ : PUnit â†¦ f) = Sieve.generate (Presieve.singleton f) := by
  rw [Sieve.ofArrows, Presieve.ofArrows_pUnit]

namespace PreZeroHypercover

variable {S : C} (E : PreZeroHypercover.{w} S) (F : PreZeroHypercover.{w'} S)
  [âˆ€ (i : E.Iâ‚€) (j : F.Iâ‚€), HasPullback (E.f i) (F.f j)]

/-- First projection from the intersection of two pre-`0`-hypercovers. -/
@[simps]
noncomputable
def interFst : Hom (inter E F) E where
  sâ‚€ i := i.1
  hâ‚€ _ := pullback.fst _ _

/-- Second projection from the intersection of two pre-`0`-hypercovers. -/
@[simps]
noncomputable
def interSnd : Hom (inter E F) F where
  sâ‚€ i := i.2
  hâ‚€ _ := pullback.snd _ _
  wâ‚€ i := by simp [â† pullback.condition]

variable {E F} in
/-- Universal property of the intersection of two pre-`0`-hypercovers. -/
@[simps]
noncomputable
def interLift {G : PreZeroHypercover.{w''} S} (f : G.Hom E) (g : G.Hom F) :
    G.Hom (E.inter F) where
  sâ‚€ i := âŸ¨f.sâ‚€ i, g.sâ‚€ iâŸ©
  hâ‚€ i := pullback.lift (f.hâ‚€ i) (g.hâ‚€ i) (by simp)

end PreZeroHypercover

namespace PreOneHypercover

variable {S : C}

attribute [local grind =] Category.assoc Category.id_comp

variable {S : C} {E F G : PreOneHypercover S}

section

/-- Intersection of two pre-`1`-hypercovers. -/
@[simps toPreZeroHypercover Iâ‚ Y pâ‚ pâ‚‚]
noncomputable
def inter (E F : PreOneHypercover S) [âˆ€ i j, HasPullback (E.f i) (F.f j)]
    [âˆ€ (i j : E.Iâ‚€) (k : E.Iâ‚ i j) (a b : F.Iâ‚€) (l : F.Iâ‚ a b),
      HasPullback (E.pâ‚ k â‰« E.f i) (F.pâ‚ l â‰« F.f a)] :
    PreOneHypercover S where
  __ := E.toPreZeroHypercover.inter F.toPreZeroHypercover
  Iâ‚ i j := E.Iâ‚ i.1 j.1 Ã— F.Iâ‚ i.2 j.2
  Y i j k := pullback (E.pâ‚ k.1 â‰« E.f _) (F.pâ‚ k.2 â‰« F.f _)
  pâ‚ i j k := pullback.map _ _ _ _ (E.pâ‚ _) (F.pâ‚ _) (ğŸ™ S) (by simp) (by simp)
  pâ‚‚ i j k := pullback.map _ _ _ _ (E.pâ‚‚ _) (F.pâ‚‚ _) (ğŸ™ S) (by simp [E.w]) (by simp [F.w])
  w := by simp [E.w]

variable (E F : PreOneHypercover S) [âˆ€ i j, HasPullback (E.f i) (F.f j)]
  [âˆ€ (i j : E.Iâ‚€) (k : E.Iâ‚ i j) (a b : F.Iâ‚€) (l : F.Iâ‚ a b),
    HasPullback (E.pâ‚ k â‰« E.f i) (F.pâ‚ l â‰« F.f a)]

lemma sieveâ‚_inter [HasPullbacks C] {i j : E.Iâ‚€ Ã— F.Iâ‚€} {W : C}
    (pâ‚ : W âŸ¶ pullback (E.f i.1) (F.f i.2))
    (pâ‚‚ : W âŸ¶ pullback (E.f j.1) (F.f j.2))
    (w : pâ‚ â‰« pullback.fst _ _ â‰« E.f _ = pâ‚‚ â‰« pullback.fst _ _ â‰« E.f _) :
    (inter E F).sieveâ‚ pâ‚ pâ‚‚ = Sieve.bind
      (E.sieveâ‚ (pâ‚ â‰« pullback.fst _ _) (pâ‚‚ â‰« pullback.fst _ _))
      (fun _ f _ â†¦ (F.sieveâ‚ (pâ‚ â‰« pullback.snd _ _) (pâ‚‚ â‰« pullback.snd _ _)).pullback f) := by
  ext Y f
  let p : W âŸ¶ pullback ((inter E F).f i) ((inter E F).f j) :=
    pullback.lift pâ‚ pâ‚‚ w
  refine âŸ¨fun âŸ¨k, a, hâ‚, hâ‚‚âŸ© â†¦ ?_, fun âŸ¨Z, a, b, âŸ¨k, e, hâ‚, hâ‚‚âŸ©, âŸ¨l, u, uâ‚, uâ‚‚âŸ©, habâŸ© â†¦ ?_âŸ©
  Â· refine âŸ¨pullback p ((E.inter F).toPullback k), pullback.lift f a ?_,
        pullback.fst _ _, ?_, ?_, ?_âŸ©
    Â· apply pullback.hom_ext
      Â· apply pullback.hom_ext <;> simp [p, hâ‚, toPullback]
      Â· apply pullback.hom_ext <;> simp [p, hâ‚‚, toPullback]
    Â· refine âŸ¨k.1, pullback.snd _ _ â‰« pullback.fst _ _, ?_, ?_âŸ©
      Â· have : pâ‚ â‰« pullback.fst (E.f i.1) (F.f i.2) = p â‰« pullback.fst _ _ â‰« pullback.fst _ _ := by
          simp [p]
        simp [this, pullback.condition_assoc, toPullback]
      Â· have : pâ‚‚ â‰« pullback.fst (E.f j.1) (F.f j.2) = p â‰« pullback.snd _ _ â‰« pullback.fst _ _ := by
          simp [p]
        simp [this, pullback.condition_assoc, toPullback]
    Â· exact âŸ¨k.2, a â‰« pullback.snd _ _, by simp [reassoc_of% hâ‚], by simp [reassoc_of% hâ‚‚]âŸ©
    Â· simp
  Â· subst hab
    refine âŸ¨(k, l), pullback.lift (a â‰« e) u ?_, ?_, ?_âŸ©
    Â· simp only [Category.assoc] at uâ‚
      simp [â† reassoc_of% hâ‚, w, â† reassoc_of% uâ‚, â† pullback.condition]
    Â· apply pullback.hom_ext
      Â· simp [hâ‚]
      Â· simpa using uâ‚
    Â· apply pullback.hom_ext
      Â· simp [hâ‚‚]
      Â· simpa using uâ‚‚

/-- First projection from the intersection of two pre-`1`-hypercovers. -/
@[simps toHom sâ‚]
noncomputable
def interFst : (E.inter F).Hom E where
  __ := E.toPreZeroHypercover.interFst F.toPreZeroHypercover
  sâ‚ {i j} k := k.1
  hâ‚ _ := pullback.fst _ _

/-- Second projection from the intersection of two pre-`1`-hypercovers. -/
@[simps toHom sâ‚]
noncomputable
def interSnd : (E.inter F).Hom F where
  __ := E.toPreZeroHypercover.interSnd F.toPreZeroHypercover
  sâ‚ {i j} k := k.2
  hâ‚ _ := pullback.snd _ _

/-- Universal property of the intersection of two pre-`1`-hypercovers. -/
noncomputable
def interLift {G : PreOneHypercover.{w''} S} (f : G.Hom E) (g : G.Hom F) :
    G.Hom (E.inter F) where
  __ := PreZeroHypercover.interLift f.toHom g.toHom
  sâ‚ {i j} k := âŸ¨f.sâ‚ k, g.sâ‚ kâŸ©
  hâ‚ k := pullback.lift (f.hâ‚ k) (g.hâ‚ k) <| by
    rw [f.wâ‚â‚_assoc k, g.wâ‚â‚_assoc k]
    simp
  wâ‚€ := by simp
  wâ‚â‚ k := by
    apply pullback.hom_ext
    Â· simpa using f.wâ‚â‚ k
    Â· simpa using g.wâ‚â‚ k
  wâ‚â‚‚ k := by
    apply pullback.hom_ext
    Â· simpa using f.wâ‚â‚‚ k
    Â· simpa using g.wâ‚â‚‚ k

end

/-- If `g` and `g'` are homotopic, also `f â‰« g` and `f â‰« g'` are homotopic. -/
@[simps]
def Homotopy.whiskerLeft (f : E.Hom F) (g g' : F.Hom G) (h : Homotopy g g') :
    Homotopy (f.comp g) (f.comp g') where
  H i := h.H (f.sâ‚€ i)
  a i := f.hâ‚€ i â‰« h.a (f.sâ‚€ i)
  wl i := by simp
  wr i := by simp

/-- If `f` and `f'` are homotopic, also `f â‰« g` and `f' â‰« g` are homotopic. -/
def Homotopy.whiskerRight (f f' : E.Hom F) (g : F.Hom G) (h : Homotopy f f') :
    Homotopy (f.comp g) (f'.comp g) where
  H i := g.sâ‚ (h.H i)
  a i := h.a i â‰« g.hâ‚ _
  wl i := by simp [Hom.wâ‚â‚]
  wr i := by simp [Hom.wâ‚â‚‚]

/-- The trivial pre-`1`-hypercover of `S` where a single component `S`. -/
@[simps toPreZeroHypercover Iâ‚ Y pâ‚ pâ‚‚]
def trivial (S : C) : PreOneHypercover.{w} S where
  __ := PreZeroHypercover.singleton (ğŸ™ S)
  Iâ‚ _ _ := PUnit
  Y _ _ _ := S
  pâ‚ _ _ _ := ğŸ™ _
  pâ‚‚ _ _ _ := ğŸ™ _
  w _ _ _ := by simp

lemma sieveâ‚€_trivial (S : C) : (trivial S).sieveâ‚€ = âŠ¤ := by
  rw [PreZeroHypercover.sieveâ‚€, Sieve.ofArrows, â† PreZeroHypercover.presieveâ‚€]
  simp

@[simp]
lemma sieveâ‚_trivial {S : C} {W : C} {p : W âŸ¶ S} :
    (trivial S).sieveâ‚ (iâ‚ := âŸ¨âŸ©) (iâ‚‚ := âŸ¨âŸ©) p p = âŠ¤ := by ext; simp

instance : Nonempty (PreOneHypercover.{w} S) := âŸ¨trivial SâŸ©

end PreOneHypercover

namespace GrothendieckTopology

open Limits
variable (J : GrothendieckTopology C)

namespace OneHypercover

variable {S : C}

/-- The trivial `1`-hypercover of `S` where a single component `S`. -/
@[simps toPreOneHypercover]
def trivial (S : C) : OneHypercover.{w} J S where
  __ := PreOneHypercover.trivial S
  memâ‚€ := by simp only [PreOneHypercover.sieveâ‚€_trivial, J.top_mem]
  memâ‚ _ _ _ _ _ h := by
    simp only [PreOneHypercover.trivial_toPreZeroHypercover, PreZeroHypercover.singleton_X,
      PreZeroHypercover.singleton_f, Category.comp_id] at h
    subst h
    simp

instance : Nonempty (J.OneHypercover S) := âŸ¨trivial J SâŸ©

/-- Forget the `1`-components of a `OneHypercover`. -/
@[simps toPreZeroHypercover]
def toZeroHypercover (E : OneHypercover.{w} J S) : J.toPrecoverage.ZeroHypercover S where
  __ := E.toPreZeroHypercover
  memâ‚€ := E.memâ‚€

variable {J} in
/-- Intersection of two `1`-hypercovers. -/
@[simps toPreOneHypercover]
noncomputable
def inter [HasPullbacks C] (E F : J.OneHypercover S)
    [âˆ€ (i : E.Iâ‚€) (j : F.Iâ‚€), HasPullback (E.f i) (F.f j)]
    [âˆ€ (i j : E.Iâ‚€) (k : E.Iâ‚ i j) (a b : F.Iâ‚€) (l : F.Iâ‚ a b),
      HasPullback (E.pâ‚ k â‰« E.f i) (F.pâ‚ l â‰« F.f a)] : J.OneHypercover S where
  __ := E.toPreOneHypercover.inter F.toPreOneHypercover
  memâ‚€ := (E.toZeroHypercover.inter F.toZeroHypercover).memâ‚€
  memâ‚ iâ‚ iâ‚‚ W pâ‚ pâ‚‚ h := by
    rw [PreOneHypercover.sieveâ‚_inter _ _ _ _ h]
    refine J.bind_covering (E.memâ‚ _ _ _ _ (by simpa using h)) fun _ _ _ â†¦ ?_
    exact J.pullback_stable _
      (F.memâ‚ _ _ _ _ (by simpa [Category.assoc, â† pullback.condition]))

variable (S) in
/--
Two refinement morphisms of `1`-hypercovers are homotopic if there exists a homotopy between
them.
Note: This is not an equivalence relation, it is not even reflexive!
-/
def homotopicRel : HomRel (J.OneHypercover S) :=
  fun _ _ f g â†¦ Nonempty (PreOneHypercover.Homotopy f g)

end OneHypercover

open PreOneHypercover OneHypercover

/-- The category of `1`-hypercovers with refinement morphisms up to homotopy. -/
abbrev HOneHypercover (S : C) := Quotient (OneHypercover.homotopicRel J S)

/-- The canonical projection from `1`-hypercovers to `1`-hypercovers up to homotopy. -/
abbrev OneHypercover.toHOneHypercover (S : C) : J.OneHypercover S â¥¤ J.HOneHypercover S :=
  Quotient.functor _

lemma _root_.CategoryTheory.PreOneHypercover.Homotopy.map_eq_map {S : C} {E F : J.OneHypercover S}
    {f g : E âŸ¶ F} (H : Homotopy f g) :
    (toHOneHypercover J S).map f = (toHOneHypercover J S).map g :=
  Quotient.sound _ âŸ¨HâŸ©

namespace HOneHypercover

variable {S : C}

instance : Nonempty (J.HOneHypercover S) := âŸ¨âŸ¨Nonempty.some inferInstanceâŸ©âŸ©

/-- If `C` has pullbacks, the category of `1`-hypercovers up to homotopy is cofiltered. -/
instance [HasPullbacks C] : IsCofiltered (J.HOneHypercover S) where
  cone_objs {E F} :=
    âŸ¨âŸ¨E.1.inter F.1âŸ©, Quot.mk _ (PreOneHypercover.interFst _ _),
      Quot.mk _ (PreOneHypercover.interSnd _ _), âŸ¨âŸ©âŸ©
  cone_maps {X Y} f g := by
    obtain âŸ¨(f : X.1 âŸ¶ Y.1), rflâŸ© := (toHOneHypercover J S).map_surjective f
    obtain âŸ¨(g : X.1 âŸ¶ Y.1), rflâŸ© := (toHOneHypercover J S).map_surjective g
    obtain âŸ¨W, h, âŸ¨HâŸ©âŸ© := OneHypercover.exists_nonempty_homotopy f g
    use (toHOneHypercover J S).obj W, (toHOneHypercover J S).map h
    rw [â† Functor.map_comp, â† Functor.map_comp]
    exact H.map_eq_map

end HOneHypercover

end GrothendieckTopology

end CategoryTheory
