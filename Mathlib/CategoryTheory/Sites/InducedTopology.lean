/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.CategoryTheory.Sites.DenseSubsite

#align_import category_theory.sites.induced_topology from "leanprover-community/mathlib"@"ba43124c37cfe0009bbfc57505f9503ae0e8c1af"

/-!
# Induced Topology

We say that a functor `G : C â¥¤ (D, K)` is locally dense if for each covering sieve `T` in `D` of
some `X : C`, `T âˆ© mor(C)` generates a covering sieve of `X` in `D`. A locally dense fully faithful
functor then induces a topology on `C` via `{ T âˆ© mor(C) | T âˆˆ K }`. Note that this is equal to
the collection of sieves on `C` whose image generates a covering sieve. This construction would
make `C` both cover-lifting and cover-preserving.

Some typical examples are full and cover-dense functors (for example the functor from a basis of a
topological space `X` into `Opens X`). The functor `Over X â¥¤ C` is also locally dense, and the
induced topology can then be used to construct the big sites associated to a scheme.

Given a fully faithful cover-dense functor `G : C â¥¤ (D, K)` between small sites, we then have
`Sheaf (H.inducedTopology) A â‰Œ Sheaf K A`. This is known as the comparison lemma.

## References

* [Elephant]: *Sketches of an Elephant*, P. T. Johnstone: C2.2.
* https://ncatlab.org/nlab/show/dense+sub-site
* https://ncatlab.org/nlab/show/comparison+lemma

-/


namespace CategoryTheory

universe v u

open Limits Opposite Presieve

section

variable {C : Type*} [Category C] {D : Type*} [Category D] {G : C â¥¤ D}

variable {J : GrothendieckTopology C} {K : GrothendieckTopology D}

variable (A : Type v) [Category.{u} A]

-- variables (A) [full G] [faithful G]
/-- We say that a functor `C â¥¤ D` into a site is "locally dense" if
for each covering sieve `T` in `D`, `T âˆ© mor(C)` generates a covering sieve in `D`.
-/
def LocallyCoverDense (K : GrothendieckTopology D) (G : C â¥¤ D) : Prop :=
  âˆ€ â¦ƒXâ¦„ (T : K (G.obj X)), (T.val.functorPullback G).functorPushforward G âˆˆ K (G.obj X)
#align category_theory.locally_cover_dense CategoryTheory.LocallyCoverDense

namespace LocallyCoverDense

variable [Full G] [Faithful G] (Hld : LocallyCoverDense K G)

theorem pushforward_cover_iff_cover_pullback {X : C} (S : Sieve X) :
    K _ (S.functorPushforward G) â†” âˆƒ T : K (G.obj X), T.val.functorPullback G = S := by
  constructor
  -- âŠ¢ GrothendieckTopology.sieves K (G.obj X) (Sieve.functorPushforward G S) â†’ âˆƒ T â€¦
  Â· intro hS
    -- âŠ¢ âˆƒ T, Sieve.functorPullback G â†‘T = S
    exact âŸ¨âŸ¨_, hSâŸ©, (Sieve.fullyFaithfulFunctorGaloisCoinsertion G X).u_l_eq SâŸ©
    -- ğŸ‰ no goals
  Â· rintro âŸ¨T, rflâŸ©
    -- âŠ¢ GrothendieckTopology.sieves K (G.obj X) (Sieve.functorPushforward G (Sieve.f â€¦
    exact Hld T
    -- ğŸ‰ no goals
#align category_theory.locally_cover_dense.pushforward_cover_iff_cover_pullback CategoryTheory.LocallyCoverDense.pushforward_cover_iff_cover_pullback

/-- If a functor `G : C â¥¤ (D, K)` is fully faithful and locally dense,
then the set `{ T âˆ© mor(C) | T âˆˆ K }` is a grothendieck topology of `C`.
-/
@[simps]
def inducedTopology : GrothendieckTopology C where
  sieves X S := K _ (S.functorPushforward G)
  top_mem' X := by
    change K _ _
    -- âŠ¢ GrothendieckTopology.sieves K (G.obj X) (Sieve.functorPushforward G âŠ¤)
    rw [Sieve.functorPushforward_top]
    -- âŠ¢ GrothendieckTopology.sieves K (G.obj X) âŠ¤
    exact K.top_mem _
    -- ğŸ‰ no goals
  pullback_stable' X Y S f hS := by
    have : S.pullback f = ((S.functorPushforward G).pullback (G.map f)).functorPullback G := by
      conv_lhs => rw [â† (Sieve.fullyFaithfulFunctorGaloisCoinsertion G X).u_l_eq S]
      ext
      change (S.functorPushforward G) _ â†” (S.functorPushforward G) _
      rw [G.map_comp]
    rw [this]
    -- âŠ¢ Sieve.functorPullback G (Sieve.pullback (G.map f) (Sieve.functorPushforward  â€¦
    change K _ _
    -- âŠ¢ GrothendieckTopology.sieves K (G.obj Y) (Sieve.functorPushforward G (Sieve.f â€¦
    apply Hld âŸ¨_, K.pullback_stable (G.map f) hSâŸ©
    -- ğŸ‰ no goals
  transitive' X S hS S' H' := by
    apply K.transitive hS
    -- âŠ¢ âˆ€ â¦ƒY : Dâ¦„ â¦ƒf : Y âŸ¶ G.obj Xâ¦„, (Sieve.functorPushforward G S).arrows f â†’ Sieve â€¦
    rintro Y _ âŸ¨Z, g, i, hg, rflâŸ©
    -- âŠ¢ Sieve.pullback (i â‰« G.map g) (Sieve.functorPushforward G S') âˆˆ GrothendieckT â€¦
    rw [Sieve.pullback_comp]
    -- âŠ¢ Sieve.pullback i (Sieve.pullback (G.map g) (Sieve.functorPushforward G S'))  â€¦
    apply K.pullback_stable i
    -- âŠ¢ Sieve.pullback (G.map g) (Sieve.functorPushforward G S') âˆˆ GrothendieckTopol â€¦
    refine' K.superset_covering _ (H' hg)
    -- âŠ¢ Sieve.functorPushforward G (Sieve.pullback g S') â‰¤ Sieve.pullback (G.map g)  â€¦
    rintro W _ âŸ¨Z', g', i', hg, rflâŸ©
    -- âŠ¢ (Sieve.pullback (G.map g) (Sieve.functorPushforward G S')).arrows (i' â‰« G.ma â€¦
    refine' âŸ¨Z', g' â‰« g , i', hg, _âŸ©
    -- âŠ¢ (i' â‰« G.map g') â‰« G.map g = i' â‰« G.map (g' â‰« g)
    simp
    -- ğŸ‰ no goals
#align category_theory.locally_cover_dense.induced_topology CategoryTheory.LocallyCoverDense.inducedTopology

/-- `G` is cover-lifting wrt the induced topology. -/
theorem inducedTopology_coverLifting : CoverLifting Hld.inducedTopology K G :=
  âŸ¨@fun _ S hS => Hld âŸ¨S, hSâŸ©âŸ©
#align category_theory.locally_cover_dense.induced_topology_cover_lifting CategoryTheory.LocallyCoverDense.inducedTopology_coverLifting

/-- `G` is cover-preserving wrt the induced topology. -/
theorem inducedTopology_coverPreserving : CoverPreserving Hld.inducedTopology K G :=
  âŸ¨@fun _ _ hS => hSâŸ©
#align category_theory.locally_cover_dense.induced_topology_cover_preserving CategoryTheory.LocallyCoverDense.inducedTopology_coverPreserving

end LocallyCoverDense

theorem CoverDense.locallyCoverDense [Full G] (H : CoverDense K G) : LocallyCoverDense K G := by
  intro X T
  -- âŠ¢ Sieve.functorPushforward G (Sieve.functorPullback G â†‘T) âˆˆ GrothendieckTopolo â€¦
  refine' K.superset_covering _ (K.bind_covering T.property fun Y f _ => H.is_cover Y)
  -- âŠ¢ (Sieve.bind (â†‘T).arrows fun Y f x => Sieve.coverByImage G Y) â‰¤ Sieve.functor â€¦
  rintro Y _ âŸ¨Z, _, f, hf, âŸ¨W, g, f', rfl : _ = _âŸ©, rflâŸ©
  -- âŠ¢ (Sieve.functorPushforward G (Sieve.functorPullback G â†‘T)).arrows ((g â‰« f') â‰« â€¦
  use W; use G.preimage (f' â‰« f); use g
  -- âŠ¢ âˆƒ g_1 h, (Sieve.functorPullback G â†‘T).arrows g_1 âˆ§ (g â‰« f') â‰« f = h â‰« G.map  â€¦
         -- âŠ¢ âˆƒ h, (Sieve.functorPullback G â†‘T).arrows (G.preimage (f' â‰« f)) âˆ§ (g â‰« f') â‰«  â€¦
                                  -- âŠ¢ (Sieve.functorPullback G â†‘T).arrows (G.preimage (f' â‰« f)) âˆ§ (g â‰« f') â‰« f = g â€¦
  constructor
  -- âŠ¢ (Sieve.functorPullback G â†‘T).arrows (G.preimage (f' â‰« f))
  simpa using T.val.downward_closed hf f'
  -- âŠ¢ (g â‰« f') â‰« f = g â‰« G.map (G.preimage (f' â‰« f))
  simp
  -- ğŸ‰ no goals
#align category_theory.cover_dense.locally_cover_dense CategoryTheory.CoverDense.locallyCoverDense

/-- Given a fully faithful cover-dense functor `G : C â¥¤ (D, K)`, we may induce a topology on `C`.
-/
abbrev CoverDense.inducedTopology [Full G] [Faithful G] (H : CoverDense K G) :
    GrothendieckTopology C :=
  H.locallyCoverDense.inducedTopology
#align category_theory.cover_dense.induced_topology CategoryTheory.CoverDense.inducedTopology

variable (J)

theorem over_forget_locallyCoverDense (X : C) : LocallyCoverDense J (Over.forget X) := by
  intro Y T
  -- âŠ¢ Sieve.functorPushforward (Over.forget X) (Sieve.functorPullback (Over.forget â€¦
  convert T.property
  -- âŠ¢ Sieve.functorPushforward (Over.forget X) (Sieve.functorPullback (Over.forget â€¦
  ext Z f
  -- âŠ¢ (Sieve.functorPushforward (Over.forget X) (Sieve.functorPullback (Over.forge â€¦
  constructor
  -- âŠ¢ (Sieve.functorPushforward (Over.forget X) (Sieve.functorPullback (Over.forge â€¦
  Â· rintro âŸ¨_, _, g', hg, rflâŸ©
    -- âŠ¢ (â†‘T).arrows (g' â‰« (Over.forget X).map wâœ)
    exact T.val.downward_closed hg g'
    -- ğŸ‰ no goals
  Â· intro hf
    -- âŠ¢ (Sieve.functorPushforward (Over.forget X) (Sieve.functorPullback (Over.forge â€¦
    exact âŸ¨Over.mk (f â‰« Y.hom), Over.homMk f, ğŸ™ _, hf, (Category.id_comp _).symmâŸ©
    -- ğŸ‰ no goals
#align category_theory.over_forget_locally_cover_dense CategoryTheory.over_forget_locallyCoverDense

end

section SmallSite

variable {C : Type v} [SmallCategory C] {D : Type v} [SmallCategory D] {G : C â¥¤ D}

variable {J : GrothendieckTopology C} {K : GrothendieckTopology D}

variable (A : Type u) [Category.{v} A]

/-- Cover-dense functors induces an equivalence of categories of sheaves.

This is known as the comparison lemma. It requires that the sites are small and the value category
is complete.
-/
noncomputable def CoverDense.sheafEquiv [Full G] [Faithful G] (H : CoverDense K G) [HasLimits A] :
    Sheaf H.inducedTopology A â‰Œ Sheaf K A :=
  H.sheafEquivOfCoverPreservingCoverLifting H.locallyCoverDense.inducedTopology_coverPreserving
    H.locallyCoverDense.inducedTopology_coverLifting
set_option linter.uppercaseLean3 false in
#align category_theory.cover_dense.Sheaf_equiv CategoryTheory.CoverDense.sheafEquiv

end SmallSite

end CategoryTheory
