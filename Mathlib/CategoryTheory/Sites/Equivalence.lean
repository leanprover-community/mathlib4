/-
Copyright (c) 2023 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.CategoryTheory.Sites.InducedTopology
import Mathlib.CategoryTheory.Sites.LocallyBijective
import Mathlib.CategoryTheory.Sites.PreservesLocallyBijective
import Mathlib.CategoryTheory.Sites.Whiskering
/-!
# Equivalences of sheaf categories

Given a site `(C, J)` and a category `D` which is equivalent to `C`, with `C` and `D` possibly large
and possibly in different universes, we transport the Grothendieck topology `J` on `C` to `D` and
prove that the sheaf categories are equivalent.

We also prove that sheafification and the property `HasSheafCompose` transport nicely over this
equivalence, and apply it to essentially small sites. We also provide instances for existence of
sufficiently small limits in the sheaf category on the essentially small site.

## Main definitions

* `CategoryTheory.Equivalence.sheafCongr` is the equivalence of sheaf categories.

* `CategoryTheory.Equivalence.transportAndSheafify` is the functor which takes a presheaf on `C`,
  transports it over the equivalence to `D`, sheafifies there and then transports back to `C`.

* `CategoryTheory.Equivalence.transportSheafificationAdjunction`: `transportAndSheafify` is
  left adjoint to the functor taking a sheaf to its underlying presheaf.

* `CategoryTheory.smallSheafify` is the functor which takes a presheaf on an essentially small site
  `(C, J)`, transports to a small model, sheafifies there and then transports back to `C`.

* `CategoryTheory.smallSheafificationAdjunction`: `smallSheafify` is left adjoint to the functor
  taking a sheaf to its underlying presheaf.

-/

universe u

namespace CategoryTheory

open Functor Limits GrothendieckTopology

variable {C : Type*} [Category C] (J : GrothendieckTopology C)
variable {D : Type*} [Category D] (K : GrothendieckTopology D) (e : C ‚âå D) (G : D ‚•§ C)
variable (A : Type*) [Category A]

namespace Equivalence

theorem locallyCoverDense : LocallyCoverDense J e.inverse := by
  intro X T
  convert T.prop
  ext Z f
  constructor
  ¬∑ rintro ‚ü®_, _, g', hg, rfl‚ü©
    exact T.val.downward_closed hg g'
  ¬∑ intro hf
    refine ‚ü®e.functor.obj Z, (Adjunction.homEquiv e.toAdjunction _ _).symm f, e.unit.app Z, ?_, ?_‚ü©
    ¬∑ simp only [Adjunction.homEquiv_counit, Functor.id_obj, Equivalence.toAdjunction_counit,
        Sieve.functorPullback_apply, Presieve.functorPullback_mem, Functor.map_comp,
        Equivalence.inv_fun_map, Functor.comp_obj, Category.assoc, Equivalence.unit_inverse_comp,
        Category.comp_id]
      exact T.val.downward_closed hf _
    ¬∑ simp

theorem coverPreserving : CoverPreserving J (e.locallyCoverDense J).inducedTopology e.functor where
  cover_preserve {U S} h := by
    change _ ‚àà J.sieves (e.inverse.obj (e.functor.obj U))
    convert J.pullback_stable (e.unitInv.app U) h
    ext Z f
    rw [‚Üê Sieve.functorPushforward_comp]
    simp only [Sieve.functorPushforward_apply, Presieve.functorPushforward, exists_and_left, id_obj,
      comp_obj, Sieve.pullback_apply]
    constructor
    ¬∑ rintro ‚ü®W, g, hg, x, rfl‚ü©
      rw [Category.assoc]
      apply S.downward_closed
      simpa using S.downward_closed hg _
    ¬∑ intro hf
      exact ‚ü®_, e.unitInv.app Z ‚â´ f ‚â´ e.unitInv.app U, S.downward_closed hf _,
        e.unit.app Z ‚â´ e.unit.app _, by simp‚ü©

instance : IsCoverDense e.functor (e.locallyCoverDense J).inducedTopology where
  is_cover U := by
    change _ ‚àà J.sieves _
    convert J.top_mem (e.inverse.obj U)
    ext Y f
    simp only [Sieve.functorPushforward_apply, Presieve.functorPushforward, exists_and_left,
      Sieve.top_apply, iff_true]
    exact ‚ü®e.functor.obj Y, (Adjunction.homEquiv e.toAdjunction _ _).symm f,
      Presieve.in_coverByImage _ _, e.unit.app _, by simp‚ü©

instance : IsCoverDense e.inverse J where
  is_cover U := by
    convert J.top_mem U
    ext Y f
    simp only [Sieve.functorPushforward_apply, Presieve.functorPushforward, exists_and_left,
      Sieve.top_apply, iff_true]
    let g : e.inverse.obj _ ‚ü∂ U := (e.unitInv.app Y) ‚â´ f
    have : (Sieve.coverByImage e.inverse U).arrows g := Presieve.in_coverByImage _ g
    replace := Sieve.downward_closed _ this (e.unit.app Y)
    simpa [g] using this

/-- A class saying that the equivalence `e`¬†transports the Grothendieck topology `J`¬†to `K`. -/
class TransportsGrothendieckTopology : Prop where
  /-- `K` is equal to the induced topology. -/
  eq_inducedTopology : K = (e.locallyCoverDense J).inducedTopology

instance : e.TransportsGrothendieckTopology J (e.locallyCoverDense J).inducedTopology := ‚ü®rfl‚ü©

variable [e.TransportsGrothendieckTopology J K]

theorem eq_inducedTopology_of_transports : K = (e.locallyCoverDense J).inducedTopology :=
  TransportsGrothendieckTopology.eq_inducedTopology

instance : IsContinuous e.functor J K := by
  rw [e.eq_inducedTopology_of_transports J K]
  exact IsCoverDense.isContinuous _ _ _ (e.coverPreserving J)

instance : IsContinuous e.inverse K J := by
  rw [eq_inducedTopology_of_transports J K e]
  exact IsCoverDense.isContinuous _ _ _ (e.locallyCoverDense J).inducedTopology_coverPreserving

/-- The functor in the equivalence of sheaf categories. -/
@[simps!]
def sheafCongr.functor : Sheaf J A ‚•§ Sheaf K A where
  obj F := ‚ü®e.inverse.op ‚ãô F.val, e.inverse.op_comp_isSheaf _ _ _‚ü©
  map f := ‚ü®whiskerLeft e.inverse.op f.val‚ü©

/-- The inverse in the equivalence of sheaf categories. -/
@[simps!]
def sheafCongr.inverse : Sheaf K A ‚•§ Sheaf J A where
  obj F := ‚ü®e.functor.op ‚ãô F.val, e.functor.op_comp_isSheaf _ _ _‚ü©
  map f := ‚ü®whiskerLeft e.functor.op f.val‚ü©

/-- The unit iso in the equivalence of sheaf categories. -/
@[simps!]
def sheafCongr.unitIso : ùü≠ (Sheaf J A) ‚âÖ functor J K e A ‚ãô inverse J K e A :=
  NatIso.ofComponents (fun F ‚Ü¶ ‚ü®‚ü®(isoWhiskerRight e.op.unitIso F.val).hom‚ü©,
    ‚ü®(isoWhiskerRight e.op.unitIso F.val).inv‚ü©,
    Sheaf.hom_ext _ _ (isoWhiskerRight e.op.unitIso F.val).hom_inv_id,
    Sheaf.hom_ext _ _ (isoWhiskerRight e.op.unitIso F.val).inv_hom_id‚ü© ) (by aesop)

/-- The counit iso in the equivalence of sheaf categories. -/
@[simps!]
def sheafCongr.counitIso : inverse J K e A ‚ãô functor J K e A ‚âÖ ùü≠ (Sheaf _ A) :=
  NatIso.ofComponents (fun F ‚Ü¶ ‚ü®‚ü®(isoWhiskerRight e.op.counitIso F.val).hom‚ü©,
    ‚ü®(isoWhiskerRight e.op.counitIso F.val).inv‚ü©,
    Sheaf.hom_ext _ _ (isoWhiskerRight e.op.counitIso F.val).hom_inv_id,
    Sheaf.hom_ext _ _ (isoWhiskerRight e.op.counitIso F.val).inv_hom_id‚ü© ) (by aesop)

/-- The equivalence of sheaf categories. -/
def sheafCongr : Sheaf J A ‚âå Sheaf K A where
  functor := sheafCongr.functor J K e A
  inverse := sheafCongr.inverse J K e A
  unitIso := sheafCongr.unitIso J K e A
  counitIso := sheafCongr.counitIso J K e A
  functor_unitIso_comp X := by
    ext
    simp only [id_obj, sheafCongr.functor_obj_val_obj, comp_obj,
      Sheaf.instCategorySheaf_comp_val, NatTrans.comp_app, sheafCongr.inverse_obj_val_obj,
      Opposite.unop_op, sheafCongr.functor_map_val_app,
      sheafCongr.unitIso_hom_app_val_app, sheafCongr.counitIso_hom_app_val_app,
      sheafCongr.functor_obj_val_map, Quiver.Hom.unop_op, Sheaf.instCategorySheaf_id_val,
      NatTrans.id_app]
    simp [‚Üê Functor.map_comp, ‚Üê op_comp]

-- /-- The equivalence of sheaf categories explicitly stated for the induced topology. -/
-- abbrev sheafCongrRight : Sheaf J A ‚âå Sheaf (e.locallyCoverDense J).inducedTopology A :=
--   sheafCongr e A rfl

variable [HasSheafify K A]

/-- Transport a presheaf to the equivalent category and sheafify there. -/
noncomputable
def transportAndSheafify : (C·µí·µñ ‚•§ A) ‚•§ Sheaf J A :=
  e.op.congrLeft.functor ‚ãô presheafToSheaf _ _ ‚ãô (e.sheafCongr J K A).inverse

/-- An auxiliary definition for the sheafification adjunction. -/
noncomputable
def transportIsoSheafToPresheaf : (e.sheafCongr J K A).functor ‚ãô
    sheafToPresheaf K A ‚ãô e.op.congrLeft.inverse ‚âÖ sheafToPresheaf J A :=
  NatIso.ofComponents (fun F ‚Ü¶ isoWhiskerRight e.op.unitIso.symm F.val)
    (by intros; ext; simp [Equivalence.sheafCongr])

/-- Transporting and sheafifying is left adjoint to taking the underlying presheaf. -/
noncomputable
def transportSheafificationAdjunction : transportAndSheafify J K e A ‚ä£ sheafToPresheaf J A :=
  ((e.op.congrLeft.toAdjunction.comp (sheafificationAdjunction _ _)).comp
    (e.sheafCongr J K A).symm.toAdjunction).ofNatIsoRight
    (transportIsoSheafToPresheaf _ _ _ _)

noncomputable instance : PreservesFiniteLimits <| transportAndSheafify J K e A where
  preservesFiniteLimits _ := compPreservesLimitsOfShape _ _

/-- Transport `HasSheafify` along an equivalence of sites. -/
theorem hasSheafify : HasSheafify J A :=
  HasSheafify.mk' J A (transportSheafificationAdjunction J K e A)

variable {A : Type*} [Category A] {B : Type*} [Category B] (F : A ‚•§ B)
  [K.HasSheafCompose F]

theorem hasSheafCompose : J.HasSheafCompose F where
  isSheaf P hP := by
    have hP' : Presheaf.IsSheaf K (e.inverse.op ‚ãô P ‚ãô F) := by
      change Presheaf.IsSheaf K ((_ ‚ãô _) ‚ãô _)
      apply HasSheafCompose.isSheaf
      exact e.inverse.op_comp_isSheaf K J ‚ü®P, hP‚ü©
    replace hP' : Presheaf.IsSheaf J (e.functor.op ‚ãô e.inverse.op ‚ãô P ‚ãô F) :=
      e.functor.op_comp_isSheaf _ _ ‚ü®_, hP'‚ü©
    exact (Presheaf.isSheaf_of_iso_iff ((isoWhiskerRight e.op.unitIso.symm (P ‚ãô F)))).mp hP'

end Equivalence

variable [EssentiallySmall C]
variable (B : Type*) [Category B] (F : A ‚•§ B)
variable [HasSheafify ((equivSmallModel C).locallyCoverDense J).inducedTopology A]
variable [((equivSmallModel C).locallyCoverDense J).inducedTopology.HasSheafCompose F]

/-- Transport to a small model and sheafify there. -/
noncomputable
def smallSheafify : (C·µí·µñ ‚•§ A) ‚•§ Sheaf J A := (equivSmallModel C).transportAndSheafify J
  ((equivSmallModel C).locallyCoverDense J).inducedTopology A

/--
Transporting to a small model and sheafifying there is left adjoint to the underlying presheaf
functor
-/
noncomputable
def smallSheafificationAdjunction : smallSheafify J A ‚ä£ sheafToPresheaf J A :=
  (equivSmallModel C).transportSheafificationAdjunction J _ A

noncomputable instance hasSheafifyEssentiallySmallSite : HasSheafify J A :=
  (equivSmallModel C).hasSheafify J ((equivSmallModel C).locallyCoverDense J).inducedTopology A

instance hasSheafComposeEssentiallySmallSite : HasSheafCompose J F :=
  (equivSmallModel C).hasSheafCompose J ((equivSmallModel C).locallyCoverDense J).inducedTopology F

instance hasLimitsEssentiallySmallSite
    [HasLimits <| Sheaf ((equivSmallModel C).locallyCoverDense J).inducedTopology A] :
    HasLimitsOfSize <| Sheaf J A :=
  Adjunction.has_limits_of_equivalence ((equivSmallModel C).sheafCongr J
    ((equivSmallModel C).locallyCoverDense J).inducedTopology A).functor

instance hasColimitsEssentiallySmallSite
    [HasColimits <| Sheaf ((equivSmallModel C).locallyCoverDense J).inducedTopology A] :
    HasColimitsOfSize <| Sheaf J A :=
  Adjunction.has_colimits_of_equivalence ((equivSmallModel C).sheafCongr J
    ((equivSmallModel C).locallyCoverDense J).inducedTopology A).functor

namespace GrothendieckTopology

variable {A}
variable [G.IsCoverDense J] [Functor.IsContinuous G K J] [G.Full]
  [(G.sheafPushforwardContinuous A K J).EssSurj]

open Localization

lemma W_inverseImage_whiskeringLeft :
    K.W.inverseImage ((whiskeringLeft D·µí·µñ C·µí·µñ A).obj G.op) = J.W := by
  ext P Q f
  have h‚ÇÅ : K.W (A := A) =
    Localization.LeftBousfield.W (¬∑ ‚àà Set.range (sheafToPresheaf J A ‚ãô
      ((whiskeringLeft D·µí·µñ C·µí·µñ A).obj G.op)).obj) := by
    rw [W_eq_W_range_sheafToPresheaf_obj, ‚Üê LeftBousfield.W_isoClosure]
    conv_rhs => rw [‚Üê LeftBousfield.W_isoClosure]
    apply congr_arg
    ext P
    constructor
    ¬∑ rintro ‚ü®_, ‚ü®R, rfl‚ü©, ‚ü®e‚ü©‚ü©
      exact ‚ü®_, ‚ü®_, rfl‚ü©, ‚ü®e.trans ((sheafToPresheaf _ _).mapIso
        ((G.sheafPushforwardContinuous A K J).objObjPreimageIso R).symm)‚ü©‚ü©
    ¬∑ rintro ‚ü®_, ‚ü®R, rfl‚ü©, ‚ü®e‚ü©‚ü©
      exact ‚ü®G.op ‚ãô R.val, ‚ü®(G.sheafPushforwardContinuous A K J).obj R, rfl‚ü©, ‚ü®e‚ü©‚ü©
  have h‚ÇÇ : ‚àÄ (R : Sheaf J A),
    Function.Bijective (fun (g : G.op ‚ãô Q ‚ü∂ G.op ‚ãô R.val) ‚Ü¶ whiskerLeft G.op f ‚â´ g) ‚Üî
      Function.Bijective (fun (g : Q ‚ü∂ R.val) ‚Ü¶ f ‚â´ g) := fun R ‚Ü¶ by
    rw [‚Üê Function.Bijective.of_comp_iff _
      (Functor.whiskerLeft_obj_map_bijective_of_isCoverDense J G Q R.val R.cond)]
    exact Function.Bijective.of_comp_iff'
      (Functor.whiskerLeft_obj_map_bijective_of_isCoverDense J G P R.val R.cond)
        (fun g ‚Ü¶ f ‚â´ g)
  rw [h‚ÇÅ, J.W_eq_W_range_sheafToPresheaf_obj, MorphismProperty.inverseImage_iff]
  constructor
  ¬∑ rintro h _ ‚ü®R, rfl‚ü©
    exact (h‚ÇÇ R).1 (h _ ‚ü®R, rfl‚ü©)
  ¬∑ rintro h _ ‚ü®R, rfl‚ü©
    exact (h‚ÇÇ R).2 (h _ ‚ü®R, rfl‚ü©)

lemma W_whiskerLeft_iff {P Q : C·µí·µñ ‚•§ A} (f : P ‚ü∂ Q) :
    K.W (whiskerLeft G.op f) ‚Üî J.W f := by
  rw [‚Üê W_inverseImage_whiskeringLeft J K G]
  rfl

variable [G.IsCocontinuous K J] (hG : CoverPreserving K J G) [ConcreteCategory A]
  [K.WEqualsLocallyBijective A]

lemma WEqualsLocallyBijective.transport : J.WEqualsLocallyBijective A where
  iff f := by
    rw [‚Üê W_whiskerLeft_iff J K G f, ‚Üê Presheaf.isLocallyInjective_whisker_iff K J G f hG,
      ‚Üê Presheaf.isLocallySurjective_whisker_iff K J G f hG, W_iff_isLocallyBijective]

end GrothendieckTopology

end CategoryTheory
