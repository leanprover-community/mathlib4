/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.CategoryTheory.Sites.Sheaf
import Mathlib.CategoryTheory.Sites.CoverLifting
import Mathlib.CategoryTheory.Sites.CoverPreserving
import Mathlib.CategoryTheory.Adjunction.FullyFaithful
import Mathlib.CategoryTheory.Sites.LocallyFullyFaithful

/-!
# Dense subsites

We define `IsCoverDense` functors into sites as functors such that there exists a covering sieve
that factors through images of the functor for each object in `D`.

## Main results

- `CategoryTheory.Functor.IsCoverDense.Types.presheafHom`: If `G : C ‚•§ (D, K)` is locally-full
  and cover-dense, then given any presheaf `‚Ñ±` and sheaf `‚Ñ±'` on `D`,
  and a morphism `Œ± : G ‚ãô ‚Ñ± ‚ü∂ G ‚ãô ‚Ñ±'`, we may glue them together to obtain
  a morphism of presheaves `‚Ñ± ‚ü∂ ‚Ñ±'`.
- `CategoryTheory.Functor.IsCoverDense.sheafIso`: If `‚Ñ±` above is a sheaf and `Œ±` is an iso,
  then the result is also an iso.
- `CategoryTheory.Functor.IsCoverDense.iso_of_restrict_iso`: If `G : C ‚•§ (D, K)` is locally-full
  and cover-dense, then given any sheaves `‚Ñ±, ‚Ñ±'` on `D`, and a morphism `Œ± : ‚Ñ± ‚ü∂ ‚Ñ±'`,
  then `Œ±` is an iso if `G ‚ãô ‚Ñ± ‚ü∂ G ‚ãô ‚Ñ±'` is iso.
- `CategoryTheory.Functor.IsDenseSubsite`:
  The functor `G : C ‚•§ D` exhibits `(C, J)` as a dense subsite of `(D, K)` if `G` is cover-dense,
  locally fully-faithful, and `S` is a cover of `C` iff the image of `S` in `D` is a cover.
- `CategoryTheory.Functor.IsDenseSubsite.sheafEquiv`:
  If `G : C ‚•§ D` exhibits `(C, J)` as a dense subsite of `(D, K)`,
  it induces an equivalence of category of sheaves valued in a category with suitable limits.

## References

* [Elephant]: *Sketches of an Elephant*, ‚Ñ±. T. Johnstone: C2.2.
* https://ncatlab.org/nlab/show/dense+sub-site
* https://ncatlab.org/nlab/show/comparison+lemma

-/


universe w v u

namespace CategoryTheory

variable {C : Type*} [Category C] {D : Type*} [Category D] {E : Type*} [Category E]
variable (J : GrothendieckTopology C) (K : GrothendieckTopology D)
variable {L : GrothendieckTopology E}

/-- An auxiliary structure that witnesses the fact that `f` factors through an image object of `G`.
-/
-- Porting note(#5171): removed `@[nolint has_nonempty_instance]`
structure Presieve.CoverByImageStructure (G : C ‚•§ D) {V U : D} (f : V ‚ü∂ U) where
  obj : C
  lift : V ‚ü∂ G.obj obj
  map : G.obj obj ‚ü∂ U
  fac : lift ‚â´ map = f := by aesop_cat
attribute [nolint docBlame] Presieve.CoverByImageStructure.obj Presieve.CoverByImageStructure.lift
  Presieve.CoverByImageStructure.map Presieve.CoverByImageStructure.fac

attribute [reassoc (attr := simp)] Presieve.CoverByImageStructure.fac

/-- For a functor `G : C ‚•§ D`, and an object `U : D`, `Presieve.coverByImage G U` is the presieve
of `U` consisting of those arrows that factor through images of `G`.
-/
def Presieve.coverByImage (G : C ‚•§ D) (U : D) : Presieve U := fun _ f =>
  Nonempty (Presieve.CoverByImageStructure G f)

/-- For a functor `G : C ‚•§ D`, and an object `U : D`, `Sieve.coverByImage G U` is the sieve of `U`
consisting of those arrows that factor through images of `G`.
-/
def Sieve.coverByImage (G : C ‚•§ D) (U : D) : Sieve U :=
  ‚ü®Presieve.coverByImage G U, fun ‚ü®‚ü®Z, f‚ÇÅ, f‚ÇÇ, (e : _ = _)‚ü©‚ü© g =>
    ‚ü®‚ü®Z, g ‚â´ f‚ÇÅ, f‚ÇÇ, show (g ‚â´ f‚ÇÅ) ‚â´ f‚ÇÇ = g ‚â´ _ by rw [Category.assoc, ‚Üê e]‚ü©‚ü©‚ü©

theorem Presieve.in_coverByImage (G : C ‚•§ D) {X : D} {Y : C} (f : G.obj Y ‚ü∂ X) :
    Presieve.coverByImage G X f :=
  ‚ü®‚ü®Y, ùüô _, f, by simp‚ü©‚ü©

/-- A functor `G : (C, J) ‚•§ (D, K)` is cover dense if for each object in `D`,
  there exists a covering sieve in `D` that factors through images of `G`.

This definition can be found in https://ncatlab.org/nlab/show/dense+sub-site Definition 2.2.
-/
class Functor.IsCoverDense (G : C ‚•§ D) (K : GrothendieckTopology D) : Prop where
  is_cover : ‚àÄ U : D, Sieve.coverByImage G U ‚àà K U

lemma Functor.is_cover_of_isCoverDense (G : C ‚•§ D) (K : GrothendieckTopology D)
    [G.IsCoverDense K] (U : D) : Sieve.coverByImage G U ‚àà K U := by
  apply Functor.IsCoverDense.is_cover

lemma Functor.isCoverDense_of_generate_singleton_functor_œÄ_mem (G : C ‚•§ D)
    (K : GrothendieckTopology D)
    (h : ‚àÄ B, ‚àÉ (X : C) (f : G.obj X ‚ü∂ B), Sieve.generate (Presieve.singleton f) ‚àà K B) :
    G.IsCoverDense K where
  is_cover B := by
    obtain ‚ü®X, f, h‚ü© := h B
    refine K.superset_covering ?_ h
    intro Y f ‚ü®Z, g, _, h, w‚ü©
    cases h
    exact ‚ü®‚ü®_, g, _, w‚ü©‚ü©

attribute [nolint docBlame] CategoryTheory.Functor.IsCoverDense.is_cover

open Presieve Opposite

namespace Functor

namespace IsCoverDense

variable {K}
variable {A : Type*} [Category A] (G : C ‚•§ D) [G.IsCoverDense K]

-- this is not marked with `@[ext]` because `H` can not be inferred from the type
theorem ext (‚Ñ± : SheafOfTypes K) (X : D) {s t : ‚Ñ±.val.obj (op X)}
    (h : ‚àÄ ‚¶ÉY : C‚¶Ñ (f : G.obj Y ‚ü∂ X), ‚Ñ±.val.map f.op s = ‚Ñ±.val.map f.op t) : s = t := by
  apply (‚Ñ±.cond (Sieve.coverByImage G X) (G.is_cover_of_isCoverDense K X)).isSeparatedFor.ext
  rintro Y _ ‚ü®Z, f‚ÇÅ, f‚ÇÇ, ‚ü®rfl‚ü©‚ü©
  simp [h f‚ÇÇ]

variable {G}

theorem functorPullback_pushforward_covering [G.IsLocallyFull K] {X : C}
    (T : K (G.obj X)) : (T.val.functorPullback G).functorPushforward G ‚àà K (G.obj X) := by
  refine K.transitive T.2 _ fun Y iYX hiYX ‚Ü¶ ?_
  apply K.transitive (G.is_cover_of_isCoverDense _ _) _
  rintro W _ ‚ü®Z, iWZ, iZY, rfl‚ü©
  rw [Sieve.pullback_comp]; apply K.pullback_stable; clear W iWZ
  apply K.superset_covering ?_ (G.functorPushforward_imageSieve_mem _ (iZY ‚â´ iYX))
  rintro W _ ‚ü®V, iVZ, iWV, ‚ü®iVX, e‚ü©, rfl‚ü©
  exact ‚ü®_, iVX, iWV, by simpa [e] using T.1.downward_closed hiYX (G.map iVZ ‚â´ iZY), by simp [e]‚ü©

/-- (Implementation). Given a hom between the pullbacks of two sheaves, we can whisker it with
`coyoneda` to obtain a hom between the pullbacks of the sheaves of maps from `X`.
-/
@[simps!]
def homOver {‚Ñ± : D·µí·µñ ‚•§ A} {‚Ñ±' : Sheaf K A} (Œ± : G.op ‚ãô ‚Ñ± ‚ü∂ G.op ‚ãô ‚Ñ±'.val) (X : A) :
    G.op ‚ãô ‚Ñ± ‚ãô coyoneda.obj (op X) ‚ü∂ G.op ‚ãô (sheafOver ‚Ñ±' X).val :=
  whiskerRight Œ± (coyoneda.obj (op X))

/-- (Implementation). Given an iso between the pullbacks of two sheaves, we can whisker it with
`coyoneda` to obtain an iso between the pullbacks of the sheaves of maps from `X`.
-/
@[simps!]
def isoOver {‚Ñ± ‚Ñ±' : Sheaf K A} (Œ± : G.op ‚ãô ‚Ñ±.val ‚âÖ G.op ‚ãô ‚Ñ±'.val) (X : A) :
    G.op ‚ãô (sheafOver ‚Ñ± X).val ‚âÖ G.op ‚ãô (sheafOver ‚Ñ±' X).val :=
  isoWhiskerRight Œ± (coyoneda.obj (op X))

theorem sheaf_eq_amalgamation (‚Ñ± : Sheaf K A) {X : A} {U : D} {T : Sieve U} (hT)
    (x : FamilyOfElements _ T) (hx) (t) (h : x.IsAmalgamation t) :
    t = (‚Ñ±.cond X T hT).amalgamate x hx :=
  (‚Ñ±.cond X T hT).isSeparatedFor x t _ h ((‚Ñ±.cond X T hT).isAmalgamation hx)

variable [G.IsLocallyFull K]

namespace Types

variable {‚Ñ± : D·µí·µñ ‚•§ Type v} {‚Ñ±' : SheafOfTypes.{v} K} (Œ± : G.op ‚ãô ‚Ñ± ‚ü∂ G.op ‚ãô ‚Ñ±'.val)

theorem naturality_apply {X Y : C} (i : G.obj X ‚ü∂ G.obj Y) (x) :
    ‚Ñ±'.1.map i.op (Œ±.app _ x) = Œ±.app _ (‚Ñ±.map i.op x) := by
  have {X Y} (i : X ‚ü∂ Y) (x) :
      ‚Ñ±'.1.map (G.map i).op (Œ±.app _ x) = Œ±.app _ (‚Ñ±.map (G.map i).op x) := by
    exact congr_fun (Œ±.naturality i.op).symm x
  refine IsLocallyFull.ext G _ i fun V iVX iVY e ‚Ü¶ ?_
  simp only [comp_obj, types_comp_apply, ‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, ‚Üê e, this]

@[reassoc]
theorem naturality {X Y : C} (i : G.obj X ‚ü∂ G.obj Y) :
    Œ±.app _ ‚â´ ‚Ñ±'.1.map i.op = ‚Ñ±.map i.op ‚â´ Œ±.app _ := types_ext _ _ (naturality_apply Œ± i)

/--
(Implementation). Given a section of `‚Ñ±` on `X`, we can obtain a family of elements valued in `‚Ñ±'`
that is defined on a cover generated by the images of `G`. -/
-- Porting note: removed `@[simp, nolint unused_arguments]`
noncomputable def pushforwardFamily {X} (x : ‚Ñ±.obj (op X)) :
    FamilyOfElements ‚Ñ±'.val (coverByImage G X) := fun _ _ hf =>
  ‚Ñ±'.val.map hf.some.lift.op <| Œ±.app (op _) (‚Ñ±.map hf.some.map.op x : _)

-- Porting note: there are various `include` and `omit`s in this file  (e.g. one is removed here),
-- none of which are needed in Lean 4.

-- Porting note: `pushforward_family` was tagged `@[simp]` in Lean 3 so we add the
-- equation lemma
@[simp] theorem pushforwardFamily_def {X} (x : ‚Ñ±.obj (op X)) :
    pushforwardFamily Œ± x = fun _ _ hf =>
  ‚Ñ±'.val.map hf.some.lift.op <| Œ±.app (op _) (‚Ñ±.map hf.some.map.op x : _) := rfl

/-- (Implementation). The `pushforwardFamily` defined is compatible. -/
theorem pushforwardFamily_compatible {X} (x : ‚Ñ±.obj (op X)) :
    (pushforwardFamily Œ± x).Compatible := by
  suffices ‚àÄ {Z W‚ÇÅ W‚ÇÇ} (iWX‚ÇÅ : G.obj W‚ÇÅ ‚ü∂ X) (iWX‚ÇÇ : G.obj W‚ÇÇ ‚ü∂ X) (iZW‚ÇÅ : Z ‚ü∂ G.obj W‚ÇÅ)
      (iZW‚ÇÇ : Z ‚ü∂ G.obj W‚ÇÇ), iZW‚ÇÅ ‚â´ iWX‚ÇÅ = iZW‚ÇÇ ‚â´ iWX‚ÇÇ ‚Üí
      ‚Ñ±'.1.map iZW‚ÇÅ.op (Œ±.app _ (‚Ñ±.map iWX‚ÇÅ.op x)) = ‚Ñ±'.1.map iZW‚ÇÇ.op (Œ±.app _ (‚Ñ±.map iWX‚ÇÇ.op x)) by
    rintro Y‚ÇÅ Y‚ÇÇ Z iZY‚ÇÅ iZY‚ÇÇ f‚ÇÅ f‚ÇÇ h‚ÇÅ h‚ÇÇ e
    simp only [pushforwardFamily, ‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]
    generalize Nonempty.some h‚ÇÅ = l‚ÇÅ
    generalize Nonempty.some h‚ÇÇ = l‚ÇÇ
    obtain ‚ü®W‚ÇÅ, iYW‚ÇÅ, iWX‚ÇÅ, rfl‚ü© := l‚ÇÅ
    obtain ‚ü®W‚ÇÇ, iYW‚ÇÇ, iWX‚ÇÇ, rfl‚ü© := l‚ÇÇ
    exact this _ _ _ _ (by simpa only [Category.assoc] using e)
  introv e
  refine ext G _ _ fun V iVZ ‚Ü¶ ?_
  simp only [‚Üê op_comp, ‚Üê FunctorToTypes.map_comp_apply, ‚Üê Functor.map_comp, naturality_apply,
    Category.assoc, e]

/-- (Implementation). The morphism `‚Ñ±(X) ‚ü∂ ‚Ñ±'(X)` given by gluing the `pushforwardFamily`. -/
noncomputable def appHom (X : D) : ‚Ñ±.obj (op X) ‚ü∂ ‚Ñ±'.val.obj (op X) := fun x =>
  (‚Ñ±'.cond _ (G.is_cover_of_isCoverDense _ X)).amalgamate (pushforwardFamily Œ± x)
    (pushforwardFamily_compatible Œ± x)

@[simp]
theorem pushforwardFamily_apply {X} (x : ‚Ñ±.obj (op X)) {Y : C} (f : G.obj Y ‚ü∂ X) :
    pushforwardFamily Œ± x f (Presieve.in_coverByImage G f) = Œ±.app (op Y) (‚Ñ±.map f.op x) := by
  simp only [pushforwardFamily_def, op_obj]
  generalize Nonempty.some (Presieve.in_coverByImage G f) = l
  obtain ‚ü®W, iYW, iWX, rfl‚ü© := l
  simp only [‚Üê op_comp, ‚Üê FunctorToTypes.map_comp_apply, naturality_apply]

@[simp]
theorem appHom_restrict {X : D} {Y : C} (f : op X ‚ü∂ op (G.obj Y)) (x) :
    ‚Ñ±'.val.map f (appHom Œ± X x) = Œ±.app (op Y) (‚Ñ±.map f x) :=
  ((‚Ñ±'.cond _ (G.is_cover_of_isCoverDense _ X)).valid_glue
      (pushforwardFamily_compatible Œ± x) f.unop
          (Presieve.in_coverByImage G f.unop)).trans (pushforwardFamily_apply _ _ _)

@[simp]
theorem appHom_valid_glue {X : D} {Y : C} (f : op X ‚ü∂ op (G.obj Y)) :
    appHom Œ± X ‚â´ ‚Ñ±'.val.map f = ‚Ñ±.map f ‚â´ Œ±.app (op Y) := by
  ext
  apply appHom_restrict

/--
(Implementation). The maps given in `appIso` is inverse to each other and gives a `‚Ñ±(X) ‚âÖ ‚Ñ±'(X)`.
-/
@[simps]
noncomputable def appIso {‚Ñ± ‚Ñ±' : SheafOfTypes.{v} K} (i : G.op ‚ãô ‚Ñ±.val ‚âÖ G.op ‚ãô ‚Ñ±'.val)
    (X : D) : ‚Ñ±.val.obj (op X) ‚âÖ ‚Ñ±'.val.obj (op X) where
  hom := appHom i.hom X
  inv := appHom i.inv X
  hom_inv_id := by
    ext x
    apply Functor.IsCoverDense.ext G
    intro Y f
    simp
  inv_hom_id := by
    ext x
    apply Functor.IsCoverDense.ext G
    intro Y f
    simp

/--
Given a natural transformation `G ‚ãô ‚Ñ± ‚ü∂ G ‚ãô ‚Ñ±'` between presheaves of types,
where `G` is locally-full and cover-dense, and `‚Ñ±'` is a sheaf,
we may obtain a natural transformation between sheaves.
-/
@[simps]
noncomputable def presheafHom (Œ± : G.op ‚ãô ‚Ñ± ‚ü∂ G.op ‚ãô ‚Ñ±'.val) : ‚Ñ± ‚ü∂ ‚Ñ±'.val where
  app X := appHom Œ± (unop X)
  naturality X Y f := by
    ext x
    apply Functor.IsCoverDense.ext G
    intro Y' f'
    simp only [appHom_restrict, types_comp_apply, ‚Üê FunctorToTypes.map_comp_apply]
    -- Porting note: Lean 3 proof continued with a rewrite but we're done here

/--
Given a natural isomorphism `G ‚ãô ‚Ñ± ‚âÖ G ‚ãô ‚Ñ±'` between presheaves of types,
where `G` is locally-full and cover-dense, and `‚Ñ±, ‚Ñ±'` are sheaves,
we may obtain a natural isomorphism between presheaves.
-/
@[simps!]
noncomputable def presheafIso {‚Ñ± ‚Ñ±' : SheafOfTypes.{v} K} (i : G.op ‚ãô ‚Ñ±.val ‚âÖ G.op ‚ãô ‚Ñ±'.val) :
    ‚Ñ±.val ‚âÖ ‚Ñ±'.val :=
  NatIso.ofComponents (fun X => appIso i (unop X)) @(presheafHom i.hom).naturality

/--
Given a natural isomorphism `G ‚ãô ‚Ñ± ‚âÖ G ‚ãô ‚Ñ±'` between presheaves of types,
where `G` is locally-full and cover-dense, and `‚Ñ±, ‚Ñ±'` are sheaves,
we may obtain a natural isomorphism between sheaves.
-/
@[simps]
noncomputable def sheafIso {‚Ñ± ‚Ñ±' : SheafOfTypes.{v} K} (i : G.op ‚ãô ‚Ñ±.val ‚âÖ G.op ‚ãô ‚Ñ±'.val) :
    ‚Ñ± ‚âÖ ‚Ñ±' where
  hom := ‚ü®(presheafIso i).hom‚ü©
  inv := ‚ü®(presheafIso i).inv‚ü©
  hom_inv_id := by
    ext1
    apply (presheafIso i).hom_inv_id
  inv_hom_id := by
    ext1
    apply (presheafIso i).inv_hom_id

end Types

open Types

variable {‚Ñ± : D·µí·µñ ‚•§ A} {‚Ñ±' : Sheaf K A}

/-- (Implementation). The sheaf map given in `types.sheaf_hom` is natural in terms of `X`. -/
@[simps]
noncomputable def sheafCoyonedaHom (Œ± : G.op ‚ãô ‚Ñ± ‚ü∂ G.op ‚ãô ‚Ñ±'.val) :
    coyoneda ‚ãô (whiskeringLeft D·µí·µñ A (Type _)).obj ‚Ñ± ‚ü∂
      coyoneda ‚ãô (whiskeringLeft D·µí·µñ A (Type _)).obj ‚Ñ±'.val where
  app X := presheafHom (homOver Œ± (unop X))
  naturality X Y f := by
    ext U x
    change
      appHom (homOver Œ± (unop Y)) (unop U) (f.unop ‚â´ x) =
        f.unop ‚â´ appHom (homOver Œ± (unop X)) (unop U) x
    symm
    apply sheaf_eq_amalgamation
    ¬∑ apply G.is_cover_of_isCoverDense
    -- Porting note: the following line closes a goal which didn't exist before reenableeta
    ¬∑ exact pushforwardFamily_compatible (homOver Œ± Y.unop) (f.unop ‚â´ x)
    intro Y' f' hf'
    change unop X ‚ü∂ ‚Ñ±.obj (op (unop _)) at x
    dsimp
    simp only [pushforwardFamily, Functor.comp_map, coyoneda_obj_map, homOver_app, Category.assoc]
    congr 1
    conv_lhs => rw [‚Üê hf'.some.fac]
    simp only [‚Üê Category.assoc, op_comp, Functor.map_comp]
    congr 1
    exact (appHom_restrict (homOver Œ± (unop X)) hf'.some.map.op x).trans (by simp)

/--
(Implementation). `sheafCoyonedaHom` but the order of the arguments of the functor are swapped.
-/
noncomputable def sheafYonedaHom (Œ± : G.op ‚ãô ‚Ñ± ‚ü∂ G.op ‚ãô ‚Ñ±'.val) :
    ‚Ñ± ‚ãô yoneda ‚ü∂ ‚Ñ±'.val ‚ãô yoneda where
  app U :=
    let Œ± := (sheafCoyonedaHom Œ±)
    { app := fun X => (Œ±.app X).app U
      naturality := fun X Y f => by simpa using congr_app (Œ±.naturality f) U }
  naturality U V i := by
    ext X x
    exact congr_fun (((sheafCoyonedaHom Œ±).app X).naturality i) x

/--
Given a natural transformation `G ‚ãô ‚Ñ± ‚ü∂ G ‚ãô ‚Ñ±'` between presheaves of arbitrary category,
where `G` is locally-full and cover-dense, and `‚Ñ±'` is a sheaf, we may obtain a natural
transformation between presheaves.
-/
noncomputable def sheafHom (Œ± : G.op ‚ãô ‚Ñ± ‚ü∂ G.op ‚ãô ‚Ñ±'.val) : ‚Ñ± ‚ü∂ ‚Ñ±'.val :=
  let Œ±' := sheafYonedaHom Œ±
  { app := fun X => yoneda.preimage (Œ±'.app X)
    naturality := fun X Y f => yoneda.map_injective (by simpa using Œ±'.naturality f) }

/--
Given a natural isomorphism `G ‚ãô ‚Ñ± ‚âÖ G ‚ãô ‚Ñ±'` between presheaves of arbitrary category,
where `G` is locally-full and cover-dense, and `‚Ñ±', ‚Ñ±` are sheaves,
we may obtain a natural isomorphism between presheaves.
-/
@[simps!]
noncomputable def presheafIso {‚Ñ± ‚Ñ±' : Sheaf K A} (i : G.op ‚ãô ‚Ñ±.val ‚âÖ G.op ‚ãô ‚Ñ±'.val) :
    ‚Ñ±.val ‚âÖ ‚Ñ±'.val := by
  have : ‚àÄ X : D·µí·µñ, IsIso ((sheafHom i.hom).app X) := by
    intro X
    -- Porting note: somehow `apply` in Lean 3 is leaving a typeclass goal,
    -- perhaps due to elaboration order. The corresponding `apply` in Lean 4 fails
    -- because the instance can't yet be synthesized. I hence reorder the proof.
    suffices IsIso (yoneda.map ((sheafHom i.hom).app X)) by
      apply isIso_of_reflects_iso _ yoneda
    use (sheafYonedaHom i.inv).app X
    constructor <;> ext x : 2 <;>
      simp only [sheafHom, NatTrans.comp_app, NatTrans.id_app, Functor.map_preimage]
    ¬∑ exact ((Types.presheafIso (isoOver i (unop x))).app X).hom_inv_id
    ¬∑ exact ((Types.presheafIso (isoOver i (unop x))).app X).inv_hom_id
    -- Porting note: Lean 4 proof is finished, Lean 3 needed `inferInstance`
  haveI : IsIso (sheafHom i.hom) := by apply NatIso.isIso_of_isIso_app
  apply asIso (sheafHom i.hom)

/--
Given a natural isomorphism `G ‚ãô ‚Ñ± ‚âÖ G ‚ãô ‚Ñ±'` between presheaves of arbitrary category,
where `G` is locally-full and cover-dense, and `‚Ñ±', ‚Ñ±` are sheaves,
we may obtain a natural isomorphism between presheaves.
-/
@[simps]
noncomputable def sheafIso {‚Ñ± ‚Ñ±' : Sheaf K A} (i : G.op ‚ãô ‚Ñ±.val ‚âÖ G.op ‚ãô ‚Ñ±'.val) : ‚Ñ± ‚âÖ ‚Ñ±' where
  hom := ‚ü®(presheafIso i).hom‚ü©
  inv := ‚ü®(presheafIso i).inv‚ü©
  hom_inv_id := by
    ext1
    apply (presheafIso i).hom_inv_id
  inv_hom_id := by
    ext1
    apply (presheafIso i).inv_hom_id

/-- The constructed `sheafHom Œ±` is equal to `Œ±` when restricted onto `C`. -/
theorem sheafHom_restrict_eq (Œ± : G.op ‚ãô ‚Ñ± ‚ü∂ G.op ‚ãô ‚Ñ±'.val) :
    whiskerLeft G.op (sheafHom Œ±) = Œ± := by
  ext X
  apply yoneda.map_injective
  ext U
  -- Porting note: didn't need to provide the input to `map_preimage` in Lean 3
  erw [yoneda.map_preimage ((sheafYonedaHom Œ±).app (G.op.obj X))]
  symm
  change (show (‚Ñ±'.val ‚ãô coyoneda.obj (op (unop U))).obj (op (G.obj (unop X))) from _) = _
  apply sheaf_eq_amalgamation ‚Ñ±' (G.is_cover_of_isCoverDense _ _)
  -- Porting note: next line was not needed in mathlib3
  ¬∑ exact (pushforwardFamily_compatible _ _)
  intro Y f hf
  conv_lhs => rw [‚Üê hf.some.fac]
  simp only [pushforwardFamily, Functor.comp_map, yoneda_map_app, coyoneda_obj_map, op_comp,
    FunctorToTypes.map_comp_apply, homOver_app, ‚Üê Category.assoc]
  congr 1
  simp only [Category.assoc]
  congr 1
  have := naturality_apply (G := G) (‚Ñ± := ‚Ñ± ‚ãô coyoneda.obj (op <| (G.op ‚ãô ‚Ñ±).obj X))
    (‚Ñ±' := ‚ü®_, ‚Ñ±'.2 ((G.op ‚ãô ‚Ñ±).obj X)‚ü©) (whiskerRight Œ± (coyoneda.obj _)) hf.some.map (ùüô _)
  simpa using this

variable (G)

/--
If the pullback map is obtained via whiskering,
then the result `sheaf_hom (whisker_left G.op Œ±)` is equal to `Œ±`.
-/
theorem sheafHom_eq (Œ± : ‚Ñ± ‚ü∂ ‚Ñ±'.val) : sheafHom (whiskerLeft G.op Œ±) = Œ± := by
  ext X
  apply yoneda.map_injective
  -- Porting note: deleted next line as it's not needed in Lean 4
  ext U
  -- Porting note: Lean 3 didn't need to be told the explicit input to map_preimage
  erw [yoneda.map_preimage ((sheafYonedaHom (whiskerLeft G.op Œ±)).app X)]
  symm
  change (show (‚Ñ±'.val ‚ãô coyoneda.obj (op (unop U))).obj (op (unop X)) from _) = _
  apply sheaf_eq_amalgamation ‚Ñ±' (G.is_cover_of_isCoverDense _ _)
  -- Porting note: next line was not needed in mathlib3
  ¬∑ exact (pushforwardFamily_compatible _ _)
  intro Y f hf
  conv_lhs => rw [‚Üê hf.some.fac]
  dsimp
  simp

variable {G}

/--
A locally-full and cover-dense functor `G` induces an equivalence between morphisms into a sheaf and
morphisms over the restrictions via `G`.
-/
noncomputable def restrictHomEquivHom : (G.op ‚ãô ‚Ñ± ‚ü∂ G.op ‚ãô ‚Ñ±'.val) ‚âÉ (‚Ñ± ‚ü∂ ‚Ñ±'.val) where
  toFun := sheafHom
  invFun := whiskerLeft G.op
  left_inv := sheafHom_restrict_eq
  right_inv := sheafHom_eq _

/-- Given a locally-full and cover-dense functor `G` and a natural transformation of sheaves
`Œ± : ‚Ñ± ‚ü∂ ‚Ñ±'`, if the pullback of `Œ±` along `G` is iso, then `Œ±` is also iso.
-/
theorem iso_of_restrict_iso {‚Ñ± ‚Ñ±' : Sheaf K A} (Œ± : ‚Ñ± ‚ü∂ ‚Ñ±') (i : IsIso (whiskerLeft G.op Œ±.val)) :
    IsIso Œ± := by
  convert (sheafIso (asIso (whiskerLeft G.op Œ±.val))).isIso_hom using 1
  ext1
  apply (sheafHom_eq _ _).symm

variable (G K)

/-- A locally-fully-faithful and cover-dense functor preserves compatible families. -/
lemma compatiblePreserving [G.IsLocallyFaithful K] : CompatiblePreserving K G := by
  constructor
  intro ‚Ñ± Z T x hx Y‚ÇÅ Y‚ÇÇ X f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ hg‚ÇÅ hg‚ÇÇ eq
  apply Functor.IsCoverDense.ext G
  intro W i
  refine IsLocallyFull.ext G _ (i ‚â´ f‚ÇÅ) fun V‚ÇÅ iVW iV‚ÇÅY‚ÇÅ e‚ÇÅ ‚Ü¶ ?_
  refine IsLocallyFull.ext G _ (G.map iVW ‚â´ i ‚â´ f‚ÇÇ) fun V‚ÇÇ iV‚ÇÇV‚ÇÅ iV‚ÇÇY‚ÇÇ e‚ÇÇ ‚Ü¶ ?_
  refine IsLocallyFaithful.ext G _ (iV‚ÇÇV‚ÇÅ ‚â´ iV‚ÇÅY‚ÇÅ ‚â´ g‚ÇÅ) (iV‚ÇÇY‚ÇÇ ‚â´ g‚ÇÇ) (by simp [e‚ÇÅ, e‚ÇÇ, eq]) ?_
  intro V‚ÇÉ iV‚ÇÉ e‚ÇÑ
  simp only [‚Üê op_comp, ‚Üê FunctorToTypes.map_comp_apply, ‚Üê e‚ÇÅ, ‚Üê e‚ÇÇ, ‚Üê Functor.map_comp]
  apply hx
  simpa using e‚ÇÑ

lemma isContinuous [G.IsLocallyFaithful K] (Hp : CoverPreserving J K G) : G.IsContinuous J K :=
  isContinuous_of_coverPreserving (compatiblePreserving K G) Hp

instance full_sheafPushforwardContinuous [G.IsContinuous J K] :
    Full (G.sheafPushforwardContinuous A J K) where
  map_surjective Œ± := ‚ü®‚ü®sheafHom Œ±.val‚ü©, Sheaf.Hom.ext _ _ <| sheafHom_restrict_eq Œ±.val‚ü©

instance faithful_sheafPushforwardContinuous [G.IsContinuous J K] :
    Faithful (G.sheafPushforwardContinuous A J K) where
  map_injective := by
    intro ‚Ñ± ‚Ñ±' Œ± Œ≤ e
    ext1
    apply_fun fun e => e.val at e
    dsimp [sheafPushforwardContinuous] at e
    rw [‚Üê sheafHom_eq G Œ±.val, ‚Üê sheafHom_eq G Œ≤.val, e]

end IsCoverDense

/-- If `G : C ‚•§ D` is cover dense and full, then the
map `(P ‚ü∂ Q) ‚Üí (G.op ‚ãô P ‚ü∂ G.op ‚ãô Q)` is bijective when `Q` is a sheaf`. -/
lemma whiskerLeft_obj_map_bijective_of_isCoverDense (G : C ‚•§ D)
    [G.IsCoverDense K] [G.IsLocallyFull K] {A : Type*} [Category A]
    (P Q : D·µí·µñ ‚•§ A) (hQ : Presheaf.IsSheaf K Q) :
    Function.Bijective (((whiskeringLeft C·µí·µñ D·µí·µñ A).obj G.op).map : (P ‚ü∂ Q) ‚Üí _) :=
  (IsCoverDense.restrictHomEquivHom (‚Ñ±' := ‚ü®Q, hQ‚ü©)).symm.bijective

variable {A : Type*} [Category A]
variable (J : GrothendieckTopology C) (K : GrothendieckTopology D) (G : C ‚•§ D)

/-- The functor `G : C ‚•§ D` exhibits `(C, J)` as a dense subsite of `(D, K)`
if `G` is cover-dense, locally fully-faithful,
and `S` is a cover of `C` if and only if the image of `S` in `D` is a cover. -/
class IsDenseSubsite : Prop where
  isCoverDense' : G.IsCoverDense K := by infer_instance
  isLocallyFull' : G.IsLocallyFull K := by infer_instance
  isLocallyFaithful' : G.IsLocallyFaithful K := by infer_instance
  functorPushforward_mem_iff : ‚àÄ {X : C} {S : Sieve X}, S.functorPushforward G ‚àà K _ ‚Üî S ‚àà J _

namespace IsDenseSubsite

variable [G.IsDenseSubsite J K]

lemma isCoverDense : G.IsCoverDense K := isCoverDense' J
lemma isLocallyFull : G.IsLocallyFull K := isLocallyFull' J
lemma isLocallyFaithful : G.IsLocallyFaithful K := isLocallyFaithful' J

lemma coverPreserving [G.IsDenseSubsite J K] : CoverPreserving J K G :=
  ‚ü®functorPushforward_mem_iff.mpr‚ü©

instance (priority := 900) [G.IsDenseSubsite J K] : G.IsContinuous J K :=
  letI := IsDenseSubsite.isCoverDense J K G
  letI := IsDenseSubsite.isLocallyFull J K G
  letI := IsDenseSubsite.isLocallyFaithful J K G
  IsCoverDense.isContinuous J K G (IsDenseSubsite.coverPreserving J K G)

instance (priority := 900) [G.IsDenseSubsite J K] : G.IsCocontinuous J K where
  cover_lift hS :=
    letI := IsDenseSubsite.isCoverDense J K G
    letI := IsDenseSubsite.isLocallyFull J K G
    IsDenseSubsite.functorPushforward_mem_iff.mp
      (IsCoverDense.functorPullback_pushforward_covering ‚ü®_, hS‚ü©)

instance full_sheafPushforwardContinuous [G.IsDenseSubsite J K] :
    Full (G.sheafPushforwardContinuous A J K) :=
  letI := IsDenseSubsite.isCoverDense J K G
  letI := IsDenseSubsite.isLocallyFull J K G
  inferInstance

instance faithful_sheafPushforwardContinuous [G.IsDenseSubsite J K] :
    Faithful (G.sheafPushforwardContinuous A J K) :=
  letI := IsDenseSubsite.isCoverDense J K G
  letI := IsDenseSubsite.isLocallyFull J K G
  inferInstance

lemma imageSieve_mem [G.IsDenseSubsite J K] {U V} (f : G.obj U ‚ü∂ G.obj V) :
    G.imageSieve f ‚àà J _ :=
  letI := IsDenseSubsite.isLocallyFull J K G
  IsDenseSubsite.functorPushforward_mem_iff.mp (G.functorPushforward_imageSieve_mem K f)

lemma equalizer_mem [G.IsDenseSubsite J K] {U V} (f‚ÇÅ f‚ÇÇ : U ‚ü∂ V) (e : G.map f‚ÇÅ = G.map f‚ÇÇ) :
    Sieve.equalizer f‚ÇÅ f‚ÇÇ ‚àà J _ :=
  letI := IsDenseSubsite.isLocallyFaithful J K G
  IsDenseSubsite.functorPushforward_mem_iff.mp (G.functorPushforward_equalizer_mem K f‚ÇÅ f‚ÇÇ e)

end IsDenseSubsite

end Functor

end CategoryTheory

namespace CategoryTheory.Functor.IsDenseSubsite

open CategoryTheory Opposite

universe w'
variable {C D : Type*} [Category C] [Category D]
variable (G : C ‚•§ D)
variable (J : GrothendieckTopology C) (K : GrothendieckTopology D)
variable {A : Type w} [Category.{w'} A] [‚àÄ X, Limits.HasLimitsOfShape (StructuredArrow X G.op) A]
variable [G.IsDenseSubsite J K]

lemma isIso_ranCounit_app_of_isDenseSubsite (Y : Sheaf J A) (U X) :
    IsIso ((yoneda.map ((G.op.ranCounit.app Y.val).app (op U))).app (op X)) := by
  rw [isIso_iff_bijective]
  constructor
  ¬∑ intro f‚ÇÅ f‚ÇÇ e
    apply (isPointwiseRightKanExtensionRanCounit G.op Y.1 (.op (G.obj U))).hom_ext
    rintro ‚ü®‚ü®‚ü®‚ü©‚ü©, ‚ü®W‚ü©, g‚ü©
    obtain ‚ü®g, rfl‚ü© : ‚àÉ g' : G.obj W ‚ü∂ G.obj U, g = g'.op := ‚ü®g.unop, rfl‚ü©
    simp only [id_obj, comp_obj, StructuredArrow.proj_obj, RightExtension.coneAt_pt,
      RightExtension.mk_left, RightExtension.coneAt_œÄ_app, const_obj_obj, op_obj,
      whiskeringLeft_obj_obj, RightExtension.mk_hom]
    apply (Y.2 X _ (IsDenseSubsite.imageSieve_mem J K G g)).isSeparatedFor.ext
    rintro V iVW ‚ü®iVU, e'‚ü©
    have := congr($e ‚â´ Y.1.map iVU.op)
    simp only [comp_obj, yoneda_map_app, Category.assoc, coyoneda_obj_obj, comp_map,
      coyoneda_obj_map, ‚Üê NatTrans.naturality, op_obj, op_map, Quiver.Hom.unop_op, ‚Üê map_comp_assoc,
      ‚Üê op_comp, ‚Üê e'] at this ‚ä¢
    erw [‚Üê NatTrans.naturality] at this
    exact this
  ¬∑ intro f
    have (X Y Z) (f : X ‚ü∂ Y) (g : G.obj Y ‚ü∂ G.obj Z) (hf : G.imageSieve g f) : Exists _ := hf
    choose l hl using this
    let c : Limits.Cone (StructuredArrow.proj (op (G.obj U)) G.op ‚ãô Y.val) := by
      refine ‚ü®X, ‚ü®fun g ‚Ü¶ ?_, ?_‚ü©‚ü©
      ¬∑ refine Y.2.amalgamate ‚ü®_, IsDenseSubsite.imageSieve_mem J K G g.hom.unop‚ü©
          (fun I ‚Ü¶ f ‚â´ Y.1.map (l _ _ _ _ _ I.hf).op) fun I‚ÇÅ I‚ÇÇ r ‚Ü¶ ?_
        apply (Y.2 X _ (IsDenseSubsite.equalizer_mem J K G (r.g‚ÇÅ ‚â´ l _ _ _ _ _ I‚ÇÅ.hf)
          (r.g‚ÇÇ ‚â´ l _ _ _ _ _ I‚ÇÇ.hf) ?_)).isSeparatedFor.ext fun V iUV (hiUV : _ = _) ‚Ü¶ ?_
        ¬∑ simp only [const_obj_obj, op_obj, map_comp, hl]
          simp only [‚Üê map_comp_assoc, r.w]
        ¬∑ simp [‚Üê map_comp, ‚Üê op_comp, hiUV]
      ¬∑ rintro ‚ü®‚ü®‚ü®‚ü©‚ü©, ‚ü®W‚ÇÅ‚ü©, g‚ÇÅ‚ü© ‚ü®‚ü®‚ü®‚ü©‚ü©, ‚ü®W‚ÇÇ‚ü©, g‚ÇÇ‚ü© ‚ü®‚ü®‚ü®‚ü®‚ü©‚ü©‚ü©, i, hi‚ü©
        dsimp at g‚ÇÅ g‚ÇÇ i hi
        obtain rfl : g‚ÇÇ = g‚ÇÅ ‚â´ (G.map i.unop).op := by simpa only [Category.id_comp] using hi
        obtain ‚ü®g, rfl‚ü© : ‚àÉ g' : G.obj W‚ÇÅ ‚ü∂ G.obj U, g‚ÇÅ = g'.op := ‚ü®g‚ÇÅ.unop, rfl‚ü©
        obtain ‚ü®i, rfl‚ü© : ‚àÉ i' : W‚ÇÇ ‚ü∂ W‚ÇÅ, i = i'.op := ‚ü®i.unop, rfl‚ü©
        simp only [const_obj_obj, id_obj, comp_obj, StructuredArrow.proj_obj, const_obj_map, op_obj,
          unop_comp, Quiver.Hom.unop_op, Category.id_comp, comp_map, StructuredArrow.proj_map]
        apply Y.2.hom_ext ‚ü®_, IsDenseSubsite.imageSieve_mem J K G (G.map i ‚â´ g)‚ü©
        intro I
        simp only [Presheaf.IsSheaf.amalgamate_map, Category.assoc, ‚Üê Functor.map_comp, ‚Üê op_comp]
        let I' : GrothendieckTopology.Cover.Arrow ‚ü®_, IsDenseSubsite.imageSieve_mem J K G g‚ü© :=
          ‚ü®_, I.f ‚â´ i, ‚ü®l _ _ _ _ _ I.hf, by simp [hl]‚ü©‚ü©
        refine Eq.trans ?_ (Y.2.amalgamate_map _ _ _ I').symm
        apply (Y.2 X _ (IsDenseSubsite.equalizer_mem J K G (l _ _ _ _ _ I.hf)
          (l _ _ _ _ _ I'.hf) (by simp [hl]))).isSeparatedFor.ext fun V iUV (hiUV : _ = _) ‚Ü¶ ?_
        simp [‚Üê Functor.map_comp, ‚Üê op_comp, hiUV]
    refine ‚ü®(isPointwiseRightKanExtensionRanCounit G.op Y.1 (.op (G.obj U))).lift c, ?_‚ü©
    ¬∑ have := (isPointwiseRightKanExtensionRanCounit G.op Y.1 (.op (G.obj U))).fac c (.mk (ùüô _))
      simp only [id_obj, comp_obj, StructuredArrow.proj_obj, StructuredArrow.mk_right,
        RightExtension.coneAt_pt, RightExtension.mk_left, RightExtension.coneAt_œÄ_app,
        const_obj_obj, op_obj, StructuredArrow.mk_hom_eq_self, map_id, whiskeringLeft_obj_obj,
        RightExtension.mk_hom, Category.id_comp, StructuredArrow.mk_left, unop_id] at this
      simp only [id_obj, yoneda_map_app, this]
      apply Y.2.hom_ext ‚ü®_, IsDenseSubsite.imageSieve_mem J K G (ùüô (G.obj U))‚ü© _ _ fun I ‚Ü¶ ?_
      apply (Y.2 X _ (IsDenseSubsite.equalizer_mem J K G (l _ _ _ _ _ I.hf)
        I.f (by simp [hl]))).isSeparatedFor.ext fun V iUV (hiUV : _ = _) ‚Ü¶ ?_
      simp [‚Üê Functor.map_comp, ‚Üê op_comp, hiUV]

instance (Y : Sheaf J A) : IsIso ((G.sheafAdjunctionCocontinuous A J K).counit.app Y) := by
  apply (config := { allowSynthFailures := true })
    ReflectsIsomorphisms.reflects (sheafToPresheaf J A)
  rw [NatTrans.isIso_iff_isIso_app]
  intro ‚ü®U‚ü©
  apply (config := { allowSynthFailures := true }) ReflectsIsomorphisms.reflects yoneda
  rw [NatTrans.isIso_iff_isIso_app]
  intro ‚ü®X‚ü©
  simp only [comp_obj, sheafToPresheaf_obj, sheafPushforwardContinuous_obj_val_obj, yoneda_obj_obj,
    id_obj, sheafToPresheaf_map, sheafAdjunctionCocontinuous_counit_app_val, ranAdjunction_counit]
  exact isIso_ranCounit_app_of_isDenseSubsite G J K Y U X

variable (A)

/--
If `G : C ‚•§ D` exhibits `(C, J)` as a dense subsite of `(D, K)`,
it induces an equivalence of category of sheaves valued in a category with suitable limits.
-/
@[simps! functor inverse]
noncomputable def sheafEquiv : Sheaf J A ‚âå Sheaf K A :=
  (G.sheafAdjunctionCocontinuous A J K).toEquivalence.symm

variable [HasWeakSheafify J A] [HasWeakSheafify K A]

/-- The natural isomorphism exhibiting the compatibility of
`IsDenseSubsite.sheafEquiv` with sheafification. -/
noncomputable
abbrev sheafEquivSheafificationCompatibility :
    (whiskeringLeft _ _ A).obj G.op ‚ãô presheafToSheaf _ _ ‚âÖ
      presheafToSheaf _ _ ‚ãô (sheafEquiv G J K A).inverse := by
  apply Functor.pushforwardContinuousSheafificationCompatibility

end IsDenseSubsite

@[deprecated (since := "2024-07-23")]
alias IsCoverDense.sheafEquivOfCoverPreservingCoverLifting := IsDenseSubsite.sheafEquiv
@[deprecated (since := "2024-07-23")]
alias IsCoverDense.sheafEquivOfCoverPreservingCoverLiftingSheafificationCompatibility :=
  IsDenseSubsite.sheafEquivSheafificationCompatibility

end CategoryTheory.Functor
