/-
Copyright (c) 2024 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
module

public import Mathlib.CategoryTheory.Limits.Preserves.Ulift
public import Mathlib.CategoryTheory.Sites.Canonical
public import Mathlib.CategoryTheory.Sites.Whiskering
public import Mathlib.CategoryTheory.Limits.Shapes.DisjointCoproduct
/-!

# Subcanonical Grothendieck topologies

This file provides some API for the Yoneda embedding into the category of sheaves for a
subcanonical Grothendieck topology.
-/

@[expose] public section

universe v' v u

namespace CategoryTheory.GrothendieckTopology

open Opposite Functor

variable {C : Type u} [Category.{v} C] (J : GrothendieckTopology C) [Subcanonical J]

/--
The equivalence between natural transformations from the yoneda embedding (to the sheaf category)
and elements of `F.val.obj X`.
-/
def yonedaEquiv {X : C} {F : Sheaf J (Type v)} : (J.yoneda.obj X ‚ü∂ F) ‚âÉ F.val.obj (op X) :=
  (fullyFaithfulSheafToPresheaf _ _).homEquiv.trans CategoryTheory.yonedaEquiv

theorem yonedaEquiv_apply {X : C} {F : Sheaf J (Type v)} (f : J.yoneda.obj X ‚ü∂ F) :
    yonedaEquiv J f = f.val.app (op X) (ùüô X) :=
  rfl

@[simp]
theorem yonedaEquiv_symm_app_apply {X : C} {F : Sheaf J (Type v)} (x : F.val.obj (op X)) (Y : C·µí·µñ)
    (f : Y.unop ‚ü∂ X) : (J.yonedaEquiv.symm x).val.app Y f = F.val.map f.op x :=
  rfl

/-- See also `yonedaEquiv_naturality'` for a more general version. -/
lemma yonedaEquiv_naturality {X Y : C} {F : Sheaf J (Type v)} (f : J.yoneda.obj X ‚ü∂ F)
    (g : Y ‚ü∂ X) : F.val.map g.op (J.yonedaEquiv f) = J.yonedaEquiv (J.yoneda.map g ‚â´ f) := by
  simp [yonedaEquiv, CategoryTheory.yonedaEquiv_naturality]
  rfl

/--
Variant of `yonedaEquiv_naturality` with general `g`. This is technically strictly more general
than `yonedaEquiv_naturality`, but `yonedaEquiv_naturality` is sometimes preferable because it
can avoid the "motive is not type correct" error.
-/
lemma yonedaEquiv_naturality' {X Y : C·µí·µñ} {F : Sheaf J (Type v)} (f : J.yoneda.obj (unop X) ‚ü∂ F)
    (g : X ‚ü∂ Y) : F.val.map g (J.yonedaEquiv f) = J.yonedaEquiv (J.yoneda.map g.unop ‚â´ f) :=
  J.yonedaEquiv_naturality _ _

lemma yonedaEquiv_comp {X : C} {F G : Sheaf J (Type v)} (Œ± : J.yoneda.obj X ‚ü∂ F) (Œ≤ : F ‚ü∂ G) :
    J.yonedaEquiv (Œ± ‚â´ Œ≤) = Œ≤.val.app _ (J.yonedaEquiv Œ±) :=
  rfl

lemma yonedaEquiv_yoneda_map {X Y : C} (f : X ‚ü∂ Y) : J.yonedaEquiv (J.yoneda.map f) = f := by
  rw [yonedaEquiv_apply]
  simp

lemma yonedaEquiv_symm_naturality_left {X X' : C} (f : X' ‚ü∂ X) (F : Sheaf J (Type v))
    (x : F.val.obj ‚ü®X‚ü©) : J.yoneda.map f ‚â´ J.yonedaEquiv.symm x = J.yonedaEquiv.symm
      ((F.val.map f.op) x) := by
  apply J.yonedaEquiv.injective
  simp only [yonedaEquiv_comp, yonedaEquiv_symm_app_apply, Equiv.apply_symm_apply]
  rw [yonedaEquiv_yoneda_map]

lemma yonedaEquiv_symm_naturality_right (X : C) {F F' : Sheaf J (Type v)} (f : F ‚ü∂ F')
    (x : F.val.obj ‚ü®X‚ü©) : J.yonedaEquiv.symm x ‚â´ f = J.yonedaEquiv.symm (f.val.app ‚ü®X‚ü© x) := by
  apply J.yonedaEquiv.injective
  simp [yonedaEquiv_comp]

/-- See also `map_yonedaEquiv'` for a more general version. -/
lemma map_yonedaEquiv {X Y : C} {F : Sheaf J (Type v)} (f : J.yoneda.obj X ‚ü∂ F)
    (g : Y ‚ü∂ X) : F.val.map g.op (J.yonedaEquiv f) = f.val.app (op Y) g := by
  rw [yonedaEquiv_naturality, yonedaEquiv_comp, yonedaEquiv_yoneda_map]

/--
Variant of `map_yonedaEquiv` with general `g`. This is technically strictly more general
than `map_yonedaEquiv`, but `map_yonedaEquiv` is sometimes preferable because it
can avoid the "motive is not type correct" error.
-/
lemma map_yonedaEquiv' {X Y : C·µí·µñ} {F : Sheaf J (Type v)} (f : J.yoneda.obj (unop X) ‚ü∂ F)
    (g : X ‚ü∂ Y) : F.val.map g (J.yonedaEquiv f) = f.val.app Y g.unop := by
  rw [yonedaEquiv_naturality', yonedaEquiv_comp, yonedaEquiv_yoneda_map]

lemma yonedaEquiv_symm_map {X Y : C·µí·µñ} (f : X ‚ü∂ Y) {F : Sheaf J (Type v)} (t : F.val.obj X) :
    J.yonedaEquiv.symm (F.val.map f t) = J.yoneda.map f.unop ‚â´ J.yonedaEquiv.symm t := by
  obtain ‚ü®u, rfl‚ü© := J.yonedaEquiv.surjective t
  rw [yonedaEquiv_naturality', Equiv.symm_apply_apply, Equiv.symm_apply_apply]

/--
Two morphisms of sheaves of types `P ‚ü∂ Q` coincide if the precompositions with morphisms
`yoneda.obj X ‚ü∂ P` agree.
-/
lemma hom_ext_yoneda {P Q : Sheaf J (Type v)} {f g : P ‚ü∂ Q}
    (h : ‚àÄ (X : C) (p : J.yoneda.obj X ‚ü∂ P), p ‚â´ f = p ‚â´ g) :
    f = g := by
  ext X x
  simpa only [yonedaEquiv_comp, Equiv.apply_symm_apply]
    using congr_arg (J.yonedaEquiv) (h _ (J.yonedaEquiv.symm x))

/-- The Yoneda lemma for sheaves. -/
@[simps! hom_app_app_down inv_app_app]
def yonedaOpCompCoyoneda :
    J.yoneda.op ‚ãô coyoneda ‚âÖ
      evaluation C·µí·µñ (Type v) ‚ãô (whiskeringRight _ _ _).obj uliftFunctor.{u} ‚ãô
      (whiskeringLeft _ _ _).obj (sheafToPresheaf _ _) :=
  ((isoWhiskerLeft _ sheafToPresheafCompCoyonedaCompWhiskeringLeftSheafToPresheaf.symm).trans
    (isoWhiskerRight (NatIso.op J.yonedaCompSheafToPresheaf.symm)
    (_ ‚ãô (whiskeringLeft _ _ _).obj _))).trans
    (isoWhiskerRight CategoryTheory.largeCurriedYonedaLemma ((whiskeringLeft _ _ _).obj _))

/-- A version of `yonedaEquiv` for `uliftYoneda`. -/
def uliftYonedaEquiv {X : C} {F : Sheaf J (Type (max v v'))} :
    ((uliftYoneda.{v'} J).obj X ‚ü∂ F) ‚âÉ F.val.obj (op X) :=
  (fullyFaithfulSheafToPresheaf _ _).homEquiv.trans CategoryTheory.uliftYonedaEquiv

@[deprecated (since := "2025-11-10")] alias yonedaULiftEquiv := uliftYonedaEquiv

theorem uliftYonedaEquiv_apply {X : C} {F : Sheaf J (Type (max v v'))}
    (f : J.uliftYoneda.obj X ‚ü∂ F) : uliftYonedaEquiv.{v'} J f = f.val.app (op X) ‚ü®ùüô X‚ü© :=
  rfl

@[deprecated (since := "2025-11-10")] alias yonedaULiftEquiv_apply := uliftYonedaEquiv_apply

@[simp]
theorem uliftYonedaEquiv_symm_app_apply {X : C} {F : Sheaf J (Type (max v v'))}
    (x : F.val.obj (op X)) (Y : C·µí·µñ) (f : Y.unop ‚ü∂ X) :
    (J.uliftYonedaEquiv.symm x).val.app Y ‚ü®f‚ü© = F.val.map f.op x :=
  rfl

@[deprecated (since := "2025-11-10")] alias yonedaULiftEquiv_symm_app_apply :=
  uliftYonedaEquiv_symm_app_apply

/-- See also `uliftYonedaEquiv_naturality'` for a more general version. -/
lemma uliftYonedaEquiv_naturality {X Y : C} {F : Sheaf J (Type (max v v'))}
    (f : J.uliftYoneda.obj X ‚ü∂ F) (g : Y ‚ü∂ X) :
      F.val.map g.op (J.uliftYonedaEquiv f) = J.uliftYonedaEquiv (J.uliftYoneda.map g ‚â´ f) := by
  change (f.val.app (op X) ‚â´ F.val.map g.op) ‚ü®ùüô X‚ü© = f.val.app (op Y) ‚ü®ùüô Y ‚â´ g‚ü©
  rw [‚Üê f.val.naturality]
  simp [uliftYoneda]

@[deprecated (since := "2025-11-10")] alias yonedaULiftEquiv_naturality :=
  uliftYonedaEquiv_naturality

/-- Variant of `uliftYonedaEquiv_naturality` with general `g`. This is technically strictly more
general than `uliftYonedaEquiv_naturality`, but `uliftYonedaEquiv_naturality` is sometimes
preferable because it can avoid the "motive is not type correct" error. -/
lemma uliftYonedaEquiv_naturality' {X Y : C·µí·µñ} {F : Sheaf J (Type (max v v'))}
    (f : J.uliftYoneda.obj (unop X) ‚ü∂ F) (g : X ‚ü∂ Y) :
    F.val.map g (J.uliftYonedaEquiv f) = J.uliftYonedaEquiv (J.uliftYoneda.map g.unop ‚â´ f) :=
  J.uliftYonedaEquiv_naturality _ _

@[deprecated (since := "2025-11-10")] alias yonedaULiftEquiv_naturality' :=
  uliftYonedaEquiv_naturality'

lemma uliftYonedaEquiv_comp {X : C} {F G : Sheaf J (Type (max v v'))} (Œ± : J.uliftYoneda.obj X ‚ü∂ F)
    (Œ≤ : F ‚ü∂ G) : J.uliftYonedaEquiv (Œ± ‚â´ Œ≤) = Œ≤.val.app _ (J.uliftYonedaEquiv Œ±) :=
  rfl

@[deprecated (since := "2025-11-10")] alias yonedaULiftEquiv_comp := uliftYonedaEquiv_comp

lemma uliftYonedaEquiv_uliftYoneda_map {X Y : C} (f : X ‚ü∂ Y) :
    (uliftYonedaEquiv.{v'} J) (J.uliftYoneda.map f) = ‚ü®f‚ü© := by
  rw [uliftYonedaEquiv_apply]
  simp [uliftYoneda]

@[deprecated (since := "2025-11-10")] alias yonedaULiftEquiv_yonedaULift_map :=
  uliftYonedaEquiv_uliftYoneda_map

lemma uliftYonedaEquiv_symm_naturality_left {X X' : C} (f : X' ‚ü∂ X) (F : Sheaf J (Type (max v v')))
    (x : F.val.obj ‚ü®X‚ü©) :
    J.uliftYoneda.map f ‚â´ J.uliftYonedaEquiv.symm x =
      J.uliftYonedaEquiv.symm ((F.val.map f.op) x) := by
  apply J.uliftYonedaEquiv.injective
  simp only [uliftYonedaEquiv_comp, Equiv.apply_symm_apply]
  rw [uliftYonedaEquiv_uliftYoneda_map]
  rfl

@[deprecated (since := "2025-11-10")] alias yonedaULiftEquiv_symm_naturality_left :=
  uliftYonedaEquiv_symm_naturality_left

lemma uliftYonedaEquiv_symm_naturality_right (X : C) {F F' : Sheaf J (Type (max v v'))}
    (f : F ‚ü∂ F') (x : F.val.obj ‚ü®X‚ü©) :
    J.uliftYonedaEquiv.symm x ‚â´ f = J.uliftYonedaEquiv.symm (f.val.app ‚ü®X‚ü© x) := by
  apply J.uliftYonedaEquiv.injective
  simp [uliftYonedaEquiv_comp]

@[deprecated (since := "2025-11-10")] alias yonedaULiftEquiv_symm_naturality_right :=
  uliftYonedaEquiv_symm_naturality_right

/-- See also `map_yonedaEquiv'` for a more general version. -/
lemma map_uliftYonedaEquiv {X Y : C} {F : Sheaf J (Type (max v v'))}
    (f : J.uliftYoneda.obj X ‚ü∂ F) (g : Y ‚ü∂ X) :
    F.val.map g.op (J.uliftYonedaEquiv f) = f.val.app (op Y) ‚ü®g‚ü© := by
  rw [uliftYonedaEquiv_naturality, uliftYonedaEquiv_comp, uliftYonedaEquiv_uliftYoneda_map]

@[deprecated (since := "2025-11-10")] alias map_yonedaULiftEquiv := map_uliftYonedaEquiv

/-- Variant of `map_uliftYonedaEquiv` with general `g`. This is technically strictly more general
than `map_uliftYonedaEquiv`, but `map_uliftYonedaEquiv` is sometimes preferable because it
can avoid the "motive is not type correct" error. -/
lemma map_uliftYonedaEquiv' {X Y : C·µí·µñ} {F : Sheaf J (Type (max v v'))}
    (f : J.uliftYoneda.obj (unop X) ‚ü∂ F) (g : X ‚ü∂ Y) :
    F.val.map g (J.uliftYonedaEquiv f) = f.val.app Y ‚ü®g.unop‚ü© := by
  rw [uliftYonedaEquiv_naturality', uliftYonedaEquiv_comp, uliftYonedaEquiv_uliftYoneda_map]

@[deprecated (since := "2025-11-10")] alias map_yonedaULiftEquiv' := map_uliftYonedaEquiv'

lemma uliftYonedaEquiv_symm_map {X Y : C·µí·µñ} (f : X ‚ü∂ Y) {F : Sheaf J (Type (max v v'))}
    (t : F.val.obj X) : J.uliftYonedaEquiv.symm (F.val.map f t) =
      J.uliftYoneda.map f.unop ‚â´ J.uliftYonedaEquiv.symm t := by
  obtain ‚ü®u, rfl‚ü© := J.uliftYonedaEquiv.surjective t
  rw [uliftYonedaEquiv_naturality', Equiv.symm_apply_apply, Equiv.symm_apply_apply]

@[deprecated (since := "2025-11-10")] alias yonedaULiftEquiv_symm_map := uliftYonedaEquiv_symm_map

/-- Two morphisms of sheaves of types `P ‚ü∂ Q` coincide if the precompositions
with morphisms `uliftYoneda.obj X ‚ü∂ P` agree. -/
lemma hom_ext_uliftYoneda {P Q : Sheaf J (Type (max v v'))} {f g : P ‚ü∂ Q}
    (h : ‚àÄ (X : C) (p : J.uliftYoneda.obj X ‚ü∂ P), p ‚â´ f = p ‚â´ g) :
    f = g := by
  ext X x
  simpa only [uliftYonedaEquiv_comp, Equiv.apply_symm_apply]
    using congr_arg (J.uliftYonedaEquiv) (h _ (J.uliftYonedaEquiv.symm x))

@[deprecated (since := "2025-11-10")] alias hom_ext_yonedaULift := hom_ext_uliftYoneda

/-- A variant of the Yoneda lemma for sheaves with a raise in the universe level. -/
@[simps! -isSimp]
def uliftYonedaOpCompCoyoneda :
    J.uliftYoneda.op ‚ãô coyoneda ‚âÖ
      evaluation C·µí·µñ (Type max v v') ‚ãô (whiskeringRight _ _ _).obj uliftFunctor.{u} ‚ãô
      (whiskeringLeft _ _ _).obj (sheafToPresheaf _ _) :=
  ((isoWhiskerLeft (J.yoneda.op ‚ãô (sheafCompose J _).op)
    sheafToPresheafCompCoyonedaCompWhiskeringLeftSheafToPresheaf.symm).trans
    (isoWhiskerRight (NatIso.op (J.uliftYonedaCompSheafToPresheaf.symm))
    (_ ‚ãô (whiskeringLeft _ _ _).obj _))).trans
    (isoWhiskerRight CategoryTheory.uliftYonedaOpCompCoyoneda
    ((whiskeringLeft _ _ _).obj _))

attribute [simp] uliftYonedaOpCompCoyoneda_hom_app_app_down

@[simp]
lemma uliftYonedaOpCompCoyoneda_inv_app_app (X : C·µí·µñ) (F : Sheaf J (Type max v v'))
    (s : ULift.{u} (F.val.obj X)) :
    (J.uliftYonedaOpCompCoyoneda.inv.app X).app F s = J.uliftYonedaEquiv.symm s.down :=
  rfl

lemma uliftYonedaOpCompCoyoneda_app_app (X : C·µí·µñ) (F : Sheaf J (Type (max v v'))) :
    (J.uliftYonedaOpCompCoyoneda.app X).app F = (J.uliftYonedaEquiv.trans Equiv.ulift.symm).toIso :=
  rfl

open Limits

instance preservesLimitsOfSize_yoneda : PreservesLimitsOfSize J.yoneda := by
  refine ‚ü®fun {I} _ ‚Ü¶ ?_‚ü©
  have : PreservesLimitsOfShape I (J.yoneda ‚ãô sheafToPresheaf J _) :=
    inferInstanceAs <| PreservesLimitsOfShape I CategoryTheory.yoneda
  exact preservesLimitsOfShape_of_reflects_of_preserves _ (sheafToPresheaf J _)

/--
Let `{ X·µ¢ ‚ü∂ Y }` be a family of pairwise disjoint maps that form a cover in `J`. Then its
image under the yoneda embedding to `J`-sheaves is a coproduct.
-/
noncomputable def isColimitCofanMkYoneda {Œπ : Type*} (X : Œπ ‚Üí C) {c : Cofan X}
    (H : (Sieve.ofArrows _ c.inj) ‚àà J c.pt) [‚àÄ (i : Œπ), Mono (c.inj i)]
    (hempty : (Y : C) ‚Üí IsInitial Y ‚Üí ‚ä• ‚àà J Y)
    (hdisj : ‚àÄ {i j : Œπ} (_ : i ‚â† j) {Y : C} (a : Y ‚ü∂ X i)
      (b : Y ‚ü∂ X j), a ‚â´ c.inj i = b ‚â´ c.inj j ‚Üí Nonempty (IsInitial Y)) :
    IsColimit (Cofan.mk _ fun i ‚Ü¶ J.yoneda.map (c.inj i)) := by
  have heq (s : Cofan fun i ‚Ü¶ J.yoneda.obj (X i))
      {Y : C} {i j : Œπ} (a : Y ‚ü∂ X i) (b : Y ‚ü∂ X j) (hab : a ‚â´ c.inj i = b ‚â´ c.inj j) :
      (s.inj i).val.app (op Y) a = (s.inj j).val.app (op Y) b := by
    by_cases h : i = j
    ¬∑ subst h
      rw [(cancel_mono _).mp hab]
    ¬∑ obtain ‚ü®h‚ü© := hdisj h a b hab
      have := Types.isTerminalEquivUnique _ (Sheaf.isTerminalOfBotCover s.pt _ (hempty Y h))
      exact Subsingleton.elim _ _
  refine mkCofanColimit _ (fun s ‚Ü¶ ‚ü®?_‚ü©) (fun s j ‚Ü¶ ?_) fun s m hm ‚Ü¶ ?_
  ¬∑ refine (s.pt.2.isSheafFor _ H).extend ?_
    refine ‚ü®fun Y g ‚Ü¶ ((s.inj (Sieve.ofArrows.i g.2)).val.app Y) (Sieve.ofArrows.h g.2), ?_‚ü©
    intro ‚ü®Y‚ü© ‚ü®Z‚ü© ‚ü®(g : Z ‚ü∂ Y)‚ü©
    ext u
    dsimp
    rw [‚Üê heq s (g ‚â´ Sieve.ofArrows.h u.2)
      (Sieve.ofArrows.h <| Sieve.downward_closed _ u.2 g) (by simp)]
    exact congrFun ((s.inj _).val.naturality g.op) _
  ¬∑ ext : 1
    let u (j : Œπ) : CategoryTheory.yoneda.obj (X j) ‚ü∂ (Sieve.ofArrows _ c.inj).functor :=
      (Sieve.ofArrows _ c.inj).toFunctor (c.inj j) (Sieve.ofArrows_mk _ _ j)
    have (j : Œπ) : u j ‚â´ (Sieve.ofArrows _ c.inj).functorInclusion =
      CategoryTheory.yoneda.map (c.inj j) := rfl
    dsimp
    simp only [‚Üê this, Category.assoc, Presieve.IsSheafFor.functorInclusion_comp_extend]
    ext Z (g : Z.unop ‚ü∂ X j)
    have h : Sieve.ofArrows X c.inj (g ‚â´ c.inj j) :=
      Sieve.downward_closed _ (Sieve.ofArrows_mk _ _ j) _
    exact heq s (Sieve.ofArrows.h h) g (by simp)
  ¬∑ ext : 1
    dsimp
    apply Presieve.IsSheafFor.unique_extend
    ext Y ‚ü®g, hg‚ü©
    simp [‚Üê hm (Sieve.ofArrows.i hg)]

/-- If the coproduct inclusions form a covering of `J` and coproducts are disjoint,
the yoneda embedding to `J`-sheaves preserves coproducts. -/
lemma preservesColimitsOfShape_yoneda_of_ofArrows_inj_mem {Œπ : Type*}
    [CoproductsOfShapeDisjoint C Œπ] [HasPullbacks C] [HasStrictInitialObjects C]
    (hcov : ‚àÄ {X : Œπ ‚Üí C} {c : Cofan X} (_ : IsColimit c), Sieve.ofArrows X c.inj ‚àà J c.pt)
    (htriv : ‚àÄ (Y : C), IsInitial Y ‚Üí ‚ä• ‚àà J Y) :
    PreservesColimitsOfShape (Discrete Œπ) J.yoneda := by
  apply (config := { allowSynthFailures := true }) preservesColimitsOfShape_of_discrete
  refine fun X ‚Ü¶ ‚ü®fun {c : Cofan X} hc ‚Ü¶ ‚ü®(Limits.Cofan.isColimitMapCoconeEquiv _ _ _).symm ?_‚ü©‚ü©
  have (i : Œπ) : Mono (c.inj i) := .of_coproductDisjoint hc _
  refine isColimitCofanMkYoneda _ _ (hcov hc) htriv fun hij Y a b hab ‚Ü¶ ‚ü®?_‚ü©
  exact .ofCoproductDisjointOfCommSq hij hc _ _ hab

end CategoryTheory.GrothendieckTopology
