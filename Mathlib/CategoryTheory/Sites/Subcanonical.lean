/-
Copyright (c) 2024 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.CategoryTheory.Limits.Preserves.Ulift
import Mathlib.CategoryTheory.Sites.Canonical
import Mathlib.CategoryTheory.Sites.Whiskering
/-!

# Subcanonical Grothendieck topologies

This file provides some API for the Yoneda embedding into the category of sheaves for a
subcanonical Grothendieck topology.
-/

universe v' v u

namespace CategoryTheory.GrothendieckTopology

open Opposite

variable {C : Type u} [Category.{v} C] (J : GrothendieckTopology C) [Subcanonical J]

/--
The equivalence between natural transformations from the yoneda embedding (to the sheaf category)
and elements of `F.val.obj X`.
-/
def yonedaEquiv {X : C} {F : Sheaf J (Type v)} : (J.yoneda.obj X ‚ü∂ F) ‚âÉ F.val.obj (op X) :=
  (fullyFaithfulSheafToPresheaf _ _).homEquiv.trans CategoryTheory.yonedaEquiv

theorem yonedaEquiv_apply {X : C} {F : Sheaf J (Type v)} (f : J.yoneda.obj X ‚ü∂ F) :
    yonedaEquiv J f = f.val.app (op X) (ùüô X) :=
  rfl

@[simp]
theorem yonedaEquiv_symm_app_apply {X : C} {F : Sheaf J (Type v)} (x : F.val.obj (op X)) (Y : C·µí·µñ)
    (f : Y.unop ‚ü∂ X) : (J.yonedaEquiv.symm x).val.app Y f = F.val.map f.op x :=
  rfl

/-- See also `yonedaEquiv_naturality'` for a more general version. -/
lemma yonedaEquiv_naturality {X Y : C} {F : Sheaf J (Type v)} (f : J.yoneda.obj X ‚ü∂ F)
    (g : Y ‚ü∂ X) : F.val.map g.op (J.yonedaEquiv f) = J.yonedaEquiv (J.yoneda.map g ‚â´ f) := by
  simp [yonedaEquiv, CategoryTheory.yonedaEquiv_naturality]
  rfl

/--
Variant of `yonedaEquiv_naturality` with general `g`. This is technically strictly more general
than `yonedaEquiv_naturality`, but `yonedaEquiv_naturality` is sometimes preferable because it
can avoid the "motive is not type correct" error.
-/
lemma yonedaEquiv_naturality' {X Y : C·µí·µñ} {F : Sheaf J (Type v)} (f : J.yoneda.obj (unop X) ‚ü∂ F)
    (g : X ‚ü∂ Y) : F.val.map g (J.yonedaEquiv f) = J.yonedaEquiv (J.yoneda.map g.unop ‚â´ f) :=
  J.yonedaEquiv_naturality _ _

lemma yonedaEquiv_comp {X : C} {F G : Sheaf J (Type v)} (Œ± : J.yoneda.obj X ‚ü∂ F) (Œ≤ : F ‚ü∂ G) :
    J.yonedaEquiv (Œ± ‚â´ Œ≤) = Œ≤.val.app _ (J.yonedaEquiv Œ±) :=
  rfl

lemma yonedaEquiv_yoneda_map {X Y : C} (f : X ‚ü∂ Y) : J.yonedaEquiv (J.yoneda.map f) = f := by
  rw [yonedaEquiv_apply]
  simp

lemma yonedaEquiv_symm_naturality_left {X X' : C} (f : X' ‚ü∂ X) (F : Sheaf J (Type v))
    (x : F.val.obj ‚ü®X‚ü©) : J.yoneda.map f ‚â´ J.yonedaEquiv.symm x = J.yonedaEquiv.symm
      ((F.val.map f.op) x) := by
  apply J.yonedaEquiv.injective
  simp only [yonedaEquiv_comp, yonedaEquiv_symm_app_apply, Equiv.apply_symm_apply]
  rw [yonedaEquiv_yoneda_map]

lemma yonedaEquiv_symm_naturality_right (X : C) {F F' : Sheaf J (Type v)} (f : F ‚ü∂ F')
    (x : F.val.obj ‚ü®X‚ü©) : J.yonedaEquiv.symm x ‚â´ f = J.yonedaEquiv.symm (f.val.app ‚ü®X‚ü© x) := by
  apply J.yonedaEquiv.injective
  simp [yonedaEquiv_comp]

/-- See also `map_yonedaEquiv'` for a more general version. -/
lemma map_yonedaEquiv {X Y : C} {F : Sheaf J (Type v)} (f : J.yoneda.obj X ‚ü∂ F)
    (g : Y ‚ü∂ X) : F.val.map g.op (J.yonedaEquiv f) = f.val.app (op Y) g := by
  rw [yonedaEquiv_naturality, yonedaEquiv_comp, yonedaEquiv_yoneda_map]

/--
Variant of `map_yonedaEquiv` with general `g`. This is technically strictly more general
than `map_yonedaEquiv`, but `map_yonedaEquiv` is sometimes preferable because it
can avoid the "motive is not type correct" error.
-/
lemma map_yonedaEquiv' {X Y : C·µí·µñ} {F : Sheaf J (Type v)} (f : J.yoneda.obj (unop X) ‚ü∂ F)
    (g : X ‚ü∂ Y) : F.val.map g (J.yonedaEquiv f) = f.val.app Y g.unop := by
  rw [yonedaEquiv_naturality', yonedaEquiv_comp, yonedaEquiv_yoneda_map]

lemma yonedaEquiv_symm_map {X Y : C·µí·µñ} (f : X ‚ü∂ Y) {F : Sheaf J (Type v)} (t : F.val.obj X) :
    J.yonedaEquiv.symm (F.val.map f t) = J.yoneda.map f.unop ‚â´ J.yonedaEquiv.symm t := by
  obtain ‚ü®u, rfl‚ü© := J.yonedaEquiv.surjective t
  rw [yonedaEquiv_naturality', Equiv.symm_apply_apply, Equiv.symm_apply_apply]

/--
Two morphisms of sheaves of types `P ‚ü∂ Q` coincide if the precompositions with morphisms
`yoneda.obj X ‚ü∂ P` agree.
-/
lemma hom_ext_yoneda {P Q : Sheaf J (Type v)} {f g : P ‚ü∂ Q}
    (h : ‚àÄ (X : C) (p : J.yoneda.obj X ‚ü∂ P), p ‚â´ f = p ‚â´ g) :
    f = g := by
  ext X x
  simpa only [yonedaEquiv_comp, Equiv.apply_symm_apply]
    using congr_arg (J.yonedaEquiv) (h _ (J.yonedaEquiv.symm x))

/--
The Yoneda embedding into a category of sheaves taking values in sets possibly larger than the
morphisms in the defining site.
-/
@[pp_with_univ]
def yonedaULift : C ‚•§ Sheaf J (Type (max v v')) := J.yoneda ‚ãô sheafCompose J uliftFunctor.{v'}

/-- A version of `yonedaEquiv` for `yonedaULift`. -/
def yonedaULiftEquiv {X : C} {F : Sheaf J (Type (max v v'))} :
    ((yonedaULift.{v'} J).obj X ‚ü∂ F) ‚âÉ F.val.obj (op X) :=
  (fullyFaithfulSheafToPresheaf _ _).homEquiv.trans uliftYonedaEquiv

theorem yonedaULiftEquiv_apply {X : C} {F : Sheaf J (Type (max v v'))}
    (f : J.yonedaULift.obj X ‚ü∂ F) : yonedaULiftEquiv.{v'} J f = f.val.app (op X) ‚ü®ùüô X‚ü© :=
  rfl

@[simp]
theorem yonedaULiftEquiv_symm_app_apply {X : C} {F : Sheaf J (Type (max v v'))}
    (x : F.val.obj (op X)) (Y : C·µí·µñ) (f : Y.unop ‚ü∂ X) :
      (J.yonedaULiftEquiv.symm x).val.app Y ‚ü®f‚ü© = F.val.map f.op x :=
  rfl

/-- See also `yonedaEquiv_naturality'` for a more general version. -/
lemma yonedaULiftEquiv_naturality {X Y : C} {F : Sheaf J (Type (max v v'))}
    (f : J.yonedaULift.obj X ‚ü∂ F) (g : Y ‚ü∂ X) :
      F.val.map g.op (J.yonedaULiftEquiv f) = J.yonedaULiftEquiv (J.yonedaULift.map g ‚â´ f) := by
  change (f.val.app (op X) ‚â´ F.val.map g.op) ‚ü®ùüô X‚ü© = f.val.app (op Y) ‚ü®ùüô Y ‚â´ g‚ü©
  rw [‚Üê f.val.naturality]
  simp [yonedaULift]

/-- Variant of `yonedaEquiv_naturality` with general `g`. This is technically strictly more general
than `yonedaEquiv_naturality`, but `yonedaEquiv_naturality` is sometimes preferable because it
can avoid the "motive is not type correct" error. -/
lemma yonedaULiftEquiv_naturality' {X Y : C·µí·µñ} {F : Sheaf J (Type (max v v'))}
    (f : J.yonedaULift.obj (unop X) ‚ü∂ F) (g : X ‚ü∂ Y) :
      F.val.map g (J.yonedaULiftEquiv f) = J.yonedaULiftEquiv (J.yonedaULift.map g.unop ‚â´ f) :=
  J.yonedaULiftEquiv_naturality _ _

lemma yonedaULiftEquiv_comp {X : C} {F G : Sheaf J (Type (max v v'))} (Œ± : J.yonedaULift.obj X ‚ü∂ F)
    (Œ≤ : F ‚ü∂ G) : J.yonedaULiftEquiv (Œ± ‚â´ Œ≤) = Œ≤.val.app _ (J.yonedaULiftEquiv Œ±) :=
  rfl

lemma yonedaULiftEquiv_yonedaULift_map {X Y : C} (f : X ‚ü∂ Y) :
    (yonedaULiftEquiv.{v'} J) (J.yonedaULift.map f) = ‚ü®f‚ü© := by
  rw [yonedaULiftEquiv_apply]
  simp [yonedaULift]

lemma yonedaULiftEquiv_symm_naturality_left {X X' : C} (f : X' ‚ü∂ X) (F : Sheaf J (Type (max v v')))
    (x : F.val.obj ‚ü®X‚ü©) : J.yonedaULift.map f ‚â´ J.yonedaULiftEquiv.symm x = J.yonedaULiftEquiv.symm
      ((F.val.map f.op) x) := by
  apply J.yonedaULiftEquiv.injective
  simp only [yonedaULiftEquiv_comp, Equiv.apply_symm_apply]
  rw [yonedaULiftEquiv_yonedaULift_map]
  rfl

lemma yonedaULiftEquiv_symm_naturality_right (X : C) {F F' : Sheaf J (Type (max v v'))}
    (f : F ‚ü∂ F') (x : F.val.obj ‚ü®X‚ü©) :
      J.yonedaULiftEquiv.symm x ‚â´ f = J.yonedaULiftEquiv.symm (f.val.app ‚ü®X‚ü© x) := by
  apply J.yonedaULiftEquiv.injective
  simp [yonedaULiftEquiv_comp]

/-- See also `map_yonedaEquiv'` for a more general version. -/
lemma map_yonedaULiftEquiv {X Y : C} {F : Sheaf J (Type (max v v'))}
    (f : J.yonedaULift.obj X ‚ü∂ F) (g : Y ‚ü∂ X) :
      F.val.map g.op (J.yonedaULiftEquiv f) = f.val.app (op Y) ‚ü®g‚ü© := by
  rw [yonedaULiftEquiv_naturality, yonedaULiftEquiv_comp, yonedaULiftEquiv_yonedaULift_map]

/-- Variant of `map_yonedaEquiv` with general `g`. This is technically strictly more general
than `map_yonedaEquiv`, but `map_yonedaEquiv` is sometimes preferable because it
can avoid the "motive is not type correct" error. -/
lemma map_yonedaULiftEquiv' {X Y : C·µí·µñ} {F : Sheaf J (Type (max v v'))}
    (f : J.yonedaULift.obj (unop X) ‚ü∂ F)
    (g : X ‚ü∂ Y) : F.val.map g (J.yonedaULiftEquiv f) = f.val.app Y ‚ü®g.unop‚ü© := by
  rw [yonedaULiftEquiv_naturality', yonedaULiftEquiv_comp, yonedaULiftEquiv_yonedaULift_map]

lemma yonedaULeftEquiv_symm_map {X Y : C·µí·µñ} (f : X ‚ü∂ Y) {F : Sheaf J (Type (max v v'))}
    (t : F.val.obj X) : J.yonedaULiftEquiv.symm (F.val.map f t) =
      J.yonedaULift.map f.unop ‚â´ J.yonedaULiftEquiv.symm t := by
  obtain ‚ü®u, rfl‚ü© := J.yonedaULiftEquiv.surjective t
  rw [yonedaULiftEquiv_naturality', Equiv.symm_apply_apply, Equiv.symm_apply_apply]

/-- Two morphisms of sheaves of types `P ‚ü∂ Q` coincide if the precompositions
with morphisms `yoneda.obj X ‚ü∂ P` agree. -/
lemma hom_ext_yonedaULift {P Q : Sheaf J (Type (max v v'))} {f g : P ‚ü∂ Q}
    (h : ‚àÄ (X : C) (p : J.yonedaULift.obj X ‚ü∂ P), p ‚â´ f = p ‚â´ g) :
    f = g := by
  ext X x
  simpa only [yonedaULiftEquiv_comp, Equiv.apply_symm_apply]
    using congr_arg (J.yonedaULiftEquiv) (h _ (J.yonedaULiftEquiv.symm x))

end CategoryTheory.GrothendieckTopology
