/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Sites.Grothendieck
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq
import Mathlib.CategoryTheory.Bicategory.Functor.Cat
import Mathlib.CategoryTheory.Bicategory.LocallyDiscrete

/-!
# Codescent data

-/

universe t t' w v v' u u'

namespace CategoryTheory

open Category Limits Bicategory

namespace Pseudofunctor

variable {C : Type u} [Bicategory.{w, v} C]
  (F : Pseudofunctor C Cat.{v', u'}) {Œπ : Type t} (X : Œπ ‚Üí C)

/-
Let us use `CodescentData` for a "covariant" pseudofunctor from `F` to `Cat`.
The "codescent" property for `F`, a family of objects `X : Œπ ‚Üí C` and
an initial object `X‚ÇÄ`, there is an equivalence of categories
(induced by `toCodescentDataOfIsInitial`) from `F.obj X‚ÇÄ` to
`F.CodescentData X`.

We shall use the name `DescentData` for the case of a pseudofunctor
from the locally discrete bicategory associated to the opposite category
of `C`, especially when `C` is endowed with a Grothendieck topology, and
for this we shall apply `CodescentData` to the restriction of the
pseudofunctor to `LocallyDiscrete (Over X)·µí·µñ` for `X : C`.

-/

structure CodescentData where
  obj (i : Œπ) : F.obj (X i)
  iso ‚¶ÉY : C‚¶Ñ ‚¶Éi‚ÇÅ i‚ÇÇ : Œπ‚¶Ñ (f‚ÇÅ : X i‚ÇÅ ‚ü∂ Y) (f‚ÇÇ : X i‚ÇÇ ‚ü∂ Y) :
      (F.map f‚ÇÅ).obj (obj i‚ÇÅ) ‚âÖ (F.map f‚ÇÇ).obj (obj i‚ÇÇ)
  iso_comp' ‚¶ÉY' Y : C‚¶Ñ (g : Y ‚ü∂ Y') ‚¶Éi‚ÇÅ i‚ÇÇ : Œπ‚¶Ñ (f‚ÇÅ : X i‚ÇÅ ‚ü∂ Y) (f‚ÇÇ : X i‚ÇÇ ‚ü∂ Y)
      (f‚ÇÅg : X i‚ÇÅ ‚ü∂ Y') (f‚ÇÇg : X i‚ÇÇ ‚ü∂ Y') (hf‚ÇÅg : f‚ÇÅ ‚â´ g = f‚ÇÅg) (hf‚ÇÇg : f‚ÇÇ ‚â´ g = f‚ÇÇg) :
      iso f‚ÇÅg f‚ÇÇg =
        (F.mapComp' f‚ÇÅ g f‚ÇÅg).app (obj i‚ÇÅ) ‚â™‚â´ Functor.mapIso (F.map g) (iso f‚ÇÅ f‚ÇÇ) ‚â™‚â´
          (F.mapComp' f‚ÇÇ g f‚ÇÇg).symm.app (obj i‚ÇÇ)
  iso_trans ‚¶ÉY : C‚¶Ñ ‚¶Éi‚ÇÅ i‚ÇÇ i‚ÇÉ : Œπ‚¶Ñ (f‚ÇÅ : X i‚ÇÅ ‚ü∂ Y) (f‚ÇÇ : X i‚ÇÇ ‚ü∂ Y) (f‚ÇÉ : X i‚ÇÉ ‚ü∂ Y) :
    iso f‚ÇÅ f‚ÇÇ ‚â™‚â´ iso f‚ÇÇ f‚ÇÉ = iso f‚ÇÅ f‚ÇÉ := by aesop_cat

namespace CodescentData

variable {F X}

@[simps]
def mk' (obj : ‚àÄ i, F.obj (X i))
    (hom : ‚àÄ ‚¶ÉY : C‚¶Ñ ‚¶Éi‚ÇÅ i‚ÇÇ : Œπ‚¶Ñ (f‚ÇÅ : X i‚ÇÅ ‚ü∂ Y) (f‚ÇÇ : X i‚ÇÇ ‚ü∂ Y),
      (F.map f‚ÇÅ).obj (obj i‚ÇÅ) ‚ü∂ (F.map f‚ÇÇ).obj (obj i‚ÇÇ))
    (hom_comp' : ‚àÄ ‚¶ÉY' Y : C‚¶Ñ (g : Y ‚ü∂ Y') ‚¶Éi‚ÇÅ i‚ÇÇ : Œπ‚¶Ñ (f‚ÇÅ : X i‚ÇÅ ‚ü∂ Y) (f‚ÇÇ : X i‚ÇÇ ‚ü∂ Y)
      (f‚ÇÅg : X i‚ÇÅ ‚ü∂ Y') (f‚ÇÇg : X i‚ÇÇ ‚ü∂ Y') (hf‚ÇÅg : f‚ÇÅ ‚â´ g = f‚ÇÅg) (hf‚ÇÇg : f‚ÇÇ ‚â´ g = f‚ÇÇg),
      hom f‚ÇÅg f‚ÇÇg =
        (F.mapComp' f‚ÇÅ g f‚ÇÅg).hom.app _ ‚â´
          (F.map g).map (hom f‚ÇÅ f‚ÇÇ) ‚â´
            (F.mapComp' f‚ÇÇ g f‚ÇÇg).inv.app _ := by aesop_cat)
    (hom_self : ‚àÄ ‚¶ÉY : C‚¶Ñ ‚¶Éi : Œπ‚¶Ñ (f : X i ‚ü∂ Y), hom f f = ùüô _ := by aesop_cat)
    (comp_hom : ‚àÄ ‚¶ÉY : C‚¶Ñ ‚¶Éi‚ÇÅ i‚ÇÇ i‚ÇÉ : Œπ‚¶Ñ (f‚ÇÅ : X i‚ÇÅ ‚ü∂ Y) (f‚ÇÇ : X i‚ÇÇ ‚ü∂ Y) (f‚ÇÉ : X i‚ÇÉ ‚ü∂ Y),
      hom f‚ÇÅ f‚ÇÇ ‚â´ hom f‚ÇÇ f‚ÇÉ = hom f‚ÇÅ f‚ÇÉ := by aesop_cat) : F.CodescentData X where
  obj := obj
  iso Y i‚ÇÅ i‚ÇÇ f‚ÇÅ f‚ÇÇ :=
    { hom := hom f‚ÇÅ f‚ÇÇ
      inv := hom f‚ÇÇ f‚ÇÅ }
  iso_comp' Y' Y g i‚ÇÅ i‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÅg f‚ÇÇg hf‚ÇÅg hf‚ÇÇg := by
    ext
    exact hom_comp' g f‚ÇÅ f‚ÇÇ f‚ÇÅg f‚ÇÇg hf‚ÇÅg hf‚ÇÇg

section

variable (D : F.CodescentData X)

@[reassoc (attr := simp)]
lemma iso_hom_iso_hom ‚¶ÉY : C‚¶Ñ ‚¶Éi‚ÇÅ i‚ÇÇ i‚ÇÉ : Œπ‚¶Ñ
    (f‚ÇÅ : X i‚ÇÅ ‚ü∂ Y) (f‚ÇÇ : X i‚ÇÇ ‚ü∂ Y) (f‚ÇÉ : X i‚ÇÉ ‚ü∂ Y) :
    (D.iso f‚ÇÅ f‚ÇÇ).hom ‚â´ (D.iso f‚ÇÇ f‚ÇÉ).hom = (D.iso f‚ÇÅ f‚ÇÉ).hom := by
  simp [‚Üê D.iso_trans f‚ÇÅ f‚ÇÇ f‚ÇÉ]

@[simp]
lemma iso_self ‚¶ÉY : C‚¶Ñ ‚¶Éi : Œπ‚¶Ñ (f : X i ‚ü∂ Y) :
    D.iso f f = Iso.refl _ := by
  ext
  simp [‚Üê cancel_epi (D.iso f f).hom]

@[simp]
lemma iso_symm ‚¶ÉY : C‚¶Ñ ‚¶Éi‚ÇÅ i‚ÇÇ : Œπ‚¶Ñ
    (f‚ÇÅ : X i‚ÇÅ ‚ü∂ Y) (f‚ÇÇ : X i‚ÇÇ ‚ü∂ Y) :
    (D.iso f‚ÇÅ f‚ÇÇ).symm = D.iso f‚ÇÇ f‚ÇÅ := by
  ext
  simp [‚Üê cancel_epi (D.iso f‚ÇÅ f‚ÇÇ).hom]

lemma iso_inv ‚¶ÉY : C‚¶Ñ ‚¶Éi‚ÇÅ i‚ÇÇ : Œπ‚¶Ñ
    (f‚ÇÅ : X i‚ÇÅ ‚ü∂ Y) (f‚ÇÇ : X i‚ÇÇ ‚ü∂ Y) :
    (D.iso f‚ÇÅ f‚ÇÇ).inv = (D.iso f‚ÇÇ f‚ÇÅ).hom :=
  congr_arg Iso.hom (D.iso_symm f‚ÇÅ f‚ÇÇ)

end

@[ext]
structure Hom (D‚ÇÅ D‚ÇÇ : F.CodescentData X) where
  hom (i : Œπ) : D‚ÇÅ.obj i ‚ü∂ D‚ÇÇ.obj i
  comm ‚¶ÉY : C‚¶Ñ ‚¶Éi‚ÇÅ i‚ÇÇ : Œπ‚¶Ñ (f‚ÇÅ : X i‚ÇÅ ‚ü∂ Y) (f‚ÇÇ : X i‚ÇÇ ‚ü∂ Y) :
    (F.map f‚ÇÅ).map (hom i‚ÇÅ) ‚â´ (D‚ÇÇ.iso f‚ÇÅ f‚ÇÇ).hom =
      (D‚ÇÅ.iso f‚ÇÅ f‚ÇÇ).hom ‚â´ (F.map f‚ÇÇ).map (hom i‚ÇÇ) := by aesop_cat

attribute [reassoc (attr := simp)] Hom.comm

instance : Category (F.CodescentData X) where
  Hom := Hom
  id D := { hom i := ùüô _ }
  comp {D‚ÇÅ D‚ÇÇ D‚ÇÉ} œÜ œà :=
    { hom i := œÜ.hom i ‚â´ œà.hom i
      comm Y i‚ÇÅ i‚ÇÇ f‚ÇÅ f‚ÇÇ := by
        simp only [Functor.map_comp, assoc]
        rw [œà.comm, œÜ.comm_assoc] }

@[ext]
lemma hom_ext {D‚ÇÅ D‚ÇÇ : F.CodescentData X} {f g : D‚ÇÅ ‚ü∂ D‚ÇÇ}
    (h : ‚àÄ i, f.hom i = g.hom i) : f = g :=
  Hom.ext (funext h)

@[simp]
lemma id_hom (D : F.CodescentData X) (i : Œπ) : Hom.hom (ùüô D) i = ùüô _ := rfl

@[simp, reassoc]
lemma comp_hom {D‚ÇÅ D‚ÇÇ D‚ÇÉ : F.CodescentData X} (f : D‚ÇÅ ‚ü∂ D‚ÇÇ) (g : D‚ÇÇ ‚ü∂ D‚ÇÉ) (i : Œπ) :
    (f ‚â´ g).hom i = f.hom i ‚â´ g.hom i := rfl


namespace Hom

variable {D‚ÇÅ D‚ÇÇ : F.CodescentData X} (f : D‚ÇÅ ‚ü∂ D‚ÇÇ)

@[reassoc]
lemma map_map ‚¶ÉY : C‚¶Ñ ‚¶Éi‚ÇÅ i‚ÇÇ : Œπ‚¶Ñ (f‚ÇÅ : X i‚ÇÅ ‚ü∂ Y) (f‚ÇÇ : X i‚ÇÇ ‚ü∂ Y) :
    (F.map f‚ÇÅ).map (f.hom i‚ÇÅ) =
      (D‚ÇÅ.iso f‚ÇÅ f‚ÇÇ).hom ‚â´ (F.map f‚ÇÇ).map (f.hom i‚ÇÇ) ‚â´ (D‚ÇÇ.iso f‚ÇÅ f‚ÇÇ).inv := by
  rw [‚Üê comm_assoc, Iso.hom_inv_id, comp_id]

@[reassoc]
lemma map_map' ‚¶ÉY : C‚¶Ñ ‚¶Éi‚ÇÅ i‚ÇÇ : Œπ‚¶Ñ (f‚ÇÅ : X i‚ÇÅ ‚ü∂ Y) (f‚ÇÇ : X i‚ÇÇ ‚ü∂ Y) :
    (F.map f‚ÇÇ).map (f.hom i‚ÇÇ) =
      (D‚ÇÅ.iso f‚ÇÅ f‚ÇÇ).inv ‚â´ (F.map f‚ÇÅ).map (f.hom i‚ÇÅ) ‚â´ (D‚ÇÇ.iso f‚ÇÅ f‚ÇÇ).hom := by
  simp

end Hom

variable {Œπ' : Type t'} {X' : Œπ' ‚Üí C} {p : Œπ' ‚Üí Œπ} (œÄ : ‚àÄ i', X (p i') ‚ü∂ X' i')

abbrev pullbackObjObj (D : F.CodescentData X) (i' : Œπ') : F.obj (X' i') :=
  (F.map (œÄ i')).obj (D.obj (p i'))

def pullbackObjIso
    (D : F.CodescentData X) ‚¶ÉY : C‚¶Ñ ‚¶Éi‚ÇÅ i‚ÇÇ : Œπ'‚¶Ñ (f‚ÇÅ : X' i‚ÇÅ ‚ü∂ Y) (f‚ÇÇ : X' i‚ÇÇ ‚ü∂ Y) :
    (F.map f‚ÇÅ).obj (pullbackObjObj œÄ D i‚ÇÅ) ‚âÖ (F.map f‚ÇÇ).obj (pullbackObjObj œÄ D i‚ÇÇ) :=
  (F.mapComp' (œÄ i‚ÇÅ) f‚ÇÅ _ rfl).symm.app _ ‚â™‚â´
      D.iso _ _ ‚â™‚â´ (F.mapComp' (œÄ i‚ÇÇ) f‚ÇÇ _ rfl).app _

def pullbackObjIso_eq
    (D : F.CodescentData X) ‚¶ÉY : C‚¶Ñ ‚¶Éi‚ÇÅ i‚ÇÇ : Œπ'‚¶Ñ (f‚ÇÅ : X' i‚ÇÅ ‚ü∂ Y) (f‚ÇÇ : X' i‚ÇÇ ‚ü∂ Y)
    (g‚ÇÅ : X (p i‚ÇÅ) ‚ü∂ Y) (g‚ÇÇ : X (p i‚ÇÇ) ‚ü∂ Y) (hg‚ÇÅ : g‚ÇÅ = œÄ i‚ÇÅ ‚â´ f‚ÇÅ) (hg‚ÇÇ : g‚ÇÇ = œÄ i‚ÇÇ ‚â´ f‚ÇÇ) :
    pullbackObjIso œÄ D f‚ÇÅ f‚ÇÇ = (F.mapComp' (œÄ i‚ÇÅ) f‚ÇÅ g‚ÇÅ).symm.app _ ‚â™‚â´
      D.iso g‚ÇÅ g‚ÇÇ ‚â™‚â´ (F.mapComp' (œÄ i‚ÇÇ) f‚ÇÇ g‚ÇÇ).app _ := by
  subst hg‚ÇÅ hg‚ÇÇ
  rfl

@[reassoc (attr := simp)]
lemma pullbackObjIso_hom_comp
    (D : F.CodescentData X) ‚¶ÉY : C‚¶Ñ ‚¶Éi‚ÇÅ i‚ÇÇ i‚ÇÉ : Œπ'‚¶Ñ
    (f‚ÇÅ : X' i‚ÇÅ ‚ü∂ Y) (f‚ÇÇ : X' i‚ÇÇ ‚ü∂ Y) (f‚ÇÉ : X' i‚ÇÉ ‚ü∂ Y)
    (g‚ÇÅ : X (p i‚ÇÅ) ‚ü∂ Y) (g‚ÇÇ : X (p i‚ÇÇ) ‚ü∂ Y) (g‚ÇÉ : X (p i‚ÇÉ) ‚ü∂ Y)
    (hg‚ÇÅ : g‚ÇÅ = œÄ i‚ÇÅ ‚â´ f‚ÇÅ) (hg‚ÇÇ : g‚ÇÇ = œÄ i‚ÇÇ ‚â´ f‚ÇÇ) (hg‚ÇÉ : g‚ÇÉ = œÄ i‚ÇÉ ‚â´ f‚ÇÉ) :
    (pullbackObjIso œÄ D f‚ÇÅ f‚ÇÇ).hom ‚â´ (pullbackObjIso œÄ D f‚ÇÇ f‚ÇÉ).hom =
      (pullbackObjIso œÄ D f‚ÇÅ f‚ÇÉ).hom := by
  simp [pullbackObjIso_eq œÄ D _ _ g‚ÇÅ g‚ÇÇ hg‚ÇÅ hg‚ÇÇ, pullbackObjIso_eq œÄ D _ _ g‚ÇÇ g‚ÇÉ hg‚ÇÇ hg‚ÇÉ,
    pullbackObjIso_eq œÄ D _ _ g‚ÇÅ g‚ÇÉ hg‚ÇÅ hg‚ÇÉ]

variable [Strict C]

@[simps]
def pullbackObj (D : F.CodescentData X) : F.CodescentData X' where
  obj := pullbackObjObj œÄ D
  iso := pullbackObjIso œÄ D
  iso_comp' Y' Y g i‚ÇÅ i‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÅg f‚ÇÇg hf‚ÇÅg hf‚ÇÇg := by
    ext
    dsimp
    rw [pullbackObjIso_eq œÄ D f‚ÇÅ f‚ÇÇ _ _ rfl rfl,
      pullbackObjIso_eq œÄ D f‚ÇÅg f‚ÇÇg _ _ rfl rfl,
      D.iso_comp' g (œÄ i‚ÇÅ ‚â´ f‚ÇÅ) (œÄ i‚ÇÇ ‚â´ f‚ÇÇ) (œÄ i‚ÇÅ ‚â´ f‚ÇÅg) (œÄ i‚ÇÇ ‚â´ f‚ÇÇg)
        (by aesop_cat) (by aesop_cat)]
    dsimp [pullbackObjObj]
    simp only [assoc, Functor.map_comp_assoc]
    rw [F.mapComp'_inv_app_comp_mapComp'_hom_app_assoc _ _ _ _ _ _ rfl hf‚ÇÅg rfl,
      F.mapComp'_inv_app_comp_mapComp'_hom_app' _ _ _ _ _ _ rfl hf‚ÇÇg rfl]
  iso_trans Y i‚ÇÅ i‚ÇÇ i‚ÇÉ f‚ÇÅ f‚ÇÇ f‚ÇÉ := by ext; simp

abbrev pullbackMapHom {D‚ÇÅ D‚ÇÇ : F.CodescentData X} (f : D‚ÇÅ ‚ü∂ D‚ÇÇ) (i' : Œπ'):
    pullbackObjObj œÄ D‚ÇÅ i' ‚ü∂ pullbackObjObj œÄ D‚ÇÇ i' :=
  (F.map (œÄ i')).map (f.hom (p i'))

attribute [local simp] pullbackObjIso pullbackMapHom

@[simps]
def pullbackMap {D‚ÇÅ D‚ÇÇ : F.CodescentData X} (f : D‚ÇÅ ‚ü∂ D‚ÇÇ) :
    pullbackObj œÄ D‚ÇÅ ‚ü∂ pullbackObj œÄ D‚ÇÇ where
  hom i' := pullbackMapHom œÄ f i'

-- note: up to a natural isomorphism, this should not depend on the choice of `p` or `œÄ`,
-- but only that any object `X' i'` is a target of a map from some `X i`
@[simps]
def pullback : F.CodescentData X ‚•§ F.CodescentData X' where
  obj := pullbackObj œÄ
  map f := pullbackMap œÄ f

end CodescentData

variable [Strict C]

def toCodescentDataOfIsInitial (X‚ÇÄ : C) (hX‚ÇÄ : IsInitial X‚ÇÄ) :
    F.obj X‚ÇÄ ‚•§ F.CodescentData X where
  obj A :=
    { obj i := (F.map (hX‚ÇÄ.to (X i))).obj A
      iso Y i‚ÇÅ i‚ÇÇ f‚ÇÅ f‚ÇÇ :=
        (F.mapComp' (hX‚ÇÄ.to (X i‚ÇÅ)) f‚ÇÅ (hX‚ÇÄ.to Y) (by simp)).symm.app A ‚â™‚â´
          (F.mapComp' (hX‚ÇÄ.to (X i‚ÇÇ)) f‚ÇÇ (hX‚ÇÄ.to Y) (by simp)).app A
      iso_comp' Y' Y g i‚ÇÅ i‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÅg f‚ÇÇg hf‚ÇÅg hf‚ÇÇg := by
        ext
        dsimp
        rw [Functor.map_comp, assoc, F.mapComp'‚ÇÄ‚ÇÅ‚ÇÉ_inv_app_assoc (hX‚ÇÄ.to (X i‚ÇÅ))
          f‚ÇÅ g (hX‚ÇÄ.to Y) f‚ÇÅg (hX‚ÇÄ.to Y') (by simp) hf‚ÇÅg (by simp) A,
          F.mapComp'‚ÇÄ‚ÇÅ‚ÇÉ_hom_app (hX‚ÇÄ.to (X i‚ÇÇ))
            f‚ÇÇ g (hX‚ÇÄ.to Y) f‚ÇÇg (hX‚ÇÄ.to Y') (by simp) hf‚ÇÇg (by simp) A,
            Iso.inv_hom_id_app_assoc]
      iso_trans := by
        intros
        ext
        dsimp
        rw [assoc, Iso.hom_inv_id_app_assoc] }
  map {A B} f :=
    { hom i := (F.map _).map f
      comm := by
        intros
        dsimp
        rw [mapComp'_inv_naturality_assoc, NatTrans.naturality, assoc, Cat.comp_map] }
  map_id := by intros; ext; dsimp; simp only [Functor.map_id]
  map_comp := by intros; ext; dsimp; simp only [Functor.map_comp]

end Pseudofunctor

end CategoryTheory
