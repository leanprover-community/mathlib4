/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Edward Ayers
-/
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.HasPullback
import Mathlib.Data.Set.BooleanAlgebra

/-!
# Theory of sieves

- For an object `X` of a category `C`, a `Sieve X` is a set of morphisms to `X`
  which is closed under left-composition.
- The complete lattice structure on sieves is given, as well as the Galois insertion
  given by downward-closing.
- A `Sieve X` (functorially) induces a presheaf on `C` together with a monomorphism to
  the yoneda embedding of `X`.

## Tags

sieve, pullback
-/


universe v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

namespace CategoryTheory

open Category Limits

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] (F : C ‚•§ D)
variable {X Y Z : C} (f : Y ‚ü∂ X)

/-- A set of arrows all with codomain `X`. -/
def Presieve (X : C) :=
  ‚àÄ ‚¶ÉY‚¶Ñ, Set (Y ‚ü∂ X)-- deriving CompleteLattice

instance : CompleteLattice (Presieve X) := by
  dsimp [Presieve]
  infer_instance

namespace Presieve

noncomputable instance : Inhabited (Presieve X) :=
  ‚ü®‚ä§‚ü©

/-- The full subcategory of the over category `C/X` consisting of arrows which belong to a
    presieve on `X`. -/
abbrev category {X : C} (P : Presieve X) :=
  ObjectProperty.FullSubcategory fun f : Over X => P f.hom

/-- Construct an object of `P.category`. -/
abbrev categoryMk {X : C} (P : Presieve X) {Y : C} (f : Y ‚ü∂ X) (hf : P f) : P.category :=
  ‚ü®Over.mk f, hf‚ü©

/-- Given a sieve `S` on `X : C`, its associated diagram `S.diagram` is defined to be
    the natural functor from the full subcategory of the over category `C/X` consisting
    of arrows in `S` to `C`. -/
abbrev diagram (S : Presieve X) : S.category ‚•§ C :=
  ObjectProperty.Œπ _ ‚ãô Over.forget X

/-- Given a sieve `S` on `X : C`, its associated cocone `S.cocone` is defined to be
    the natural cocone over the diagram defined above with cocone point `X`. -/
abbrev cocone (S : Presieve X) : Cocone S.diagram :=
  (Over.forgetCocone X).whisker (ObjectProperty.Œπ _)

/-- Given a set of arrows `S` all with codomain `X`, and a set of arrows with codomain `Y` for each
`f : Y ‚ü∂ X` in `S`, produce a set of arrows with codomain `X`:
`{ g ‚â´ f | (f : Y ‚ü∂ X) ‚àà S, (g : Z ‚ü∂ Y) ‚àà R f }`.
-/
def bind (S : Presieve X) (R : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ, S f ‚Üí Presieve Y) : Presieve X := fun Z h =>
  ‚àÉ (Y : C) (g : Z ‚ü∂ Y) (f : Y ‚ü∂ X) (H : S f), R H g ‚àß g ‚â´ f = h

/-- Structure which contains the data and properties for a morphism `h` satisfying
`Presieve.bind S R h`. -/
structure BindStruct (S : Presieve X) (R : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ, S f ‚Üí Presieve Y)
    {Z : C} (h : Z ‚ü∂ X) where
  /-- the intermediate object -/
  Y : C
  /-- a morphism in the family of presieves `R` -/
  g : Z ‚ü∂ Y
  /-- a morphism in the presieve `S` -/
  f : Y ‚ü∂ X
  hf : S f
  hg : R hf g
  fac : g ‚â´ f = h

attribute [reassoc (attr := simp)] BindStruct.fac

/-- If a morphism `h` satisfies `Presieve.bind S R h`, this is a choice of a structure
in `BindStruct S R h`. -/
noncomputable def bind.bindStruct {S : Presieve X} {R : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ, S f ‚Üí Presieve Y}
    {Z : C} {h : Z ‚ü∂ X} (H : bind S R h) : BindStruct S R h :=
  Nonempty.some (by
    obtain ‚ü®Y, g, f, hf, hg, fac‚ü© := H
    exact ‚ü®{ hf := hf, hg := hg, fac := fac, .. }‚ü©)

lemma BindStruct.bind {S : Presieve X} {R : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ, S f ‚Üí Presieve Y}
    {Z : C} {h : Z ‚ü∂ X} (b : BindStruct S R h) : bind S R h :=
  ‚ü®b.Y, b.g, b.f, b.hf, b.hg, b.fac‚ü©

@[simp]
theorem bind_comp {S : Presieve X} {R : ‚àÄ ‚¶ÉY : C‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ, S f ‚Üí Presieve Y} {g : Z ‚ü∂ Y}
    (h‚ÇÅ : S f) (h‚ÇÇ : R h‚ÇÅ g) : bind S R (g ‚â´ f) :=
  ‚ü®_, _, _, h‚ÇÅ, h‚ÇÇ, rfl‚ü©

-- Note we can't make this into `HasSingleton` because of the out-param.
/-- The singleton presieve. -/
inductive singleton : Presieve X
  | mk : singleton f

@[deprecated (since := "2025-08-22")] alias singleton' := singleton

@[simp]
theorem singleton_eq_iff_domain (f g : Y ‚ü∂ X) : singleton f g ‚Üî f = g := by
  constructor
  ¬∑ rintro ‚ü®a, rfl‚ü©
    rfl
  ¬∑ rintro rfl
    apply singleton.mk

theorem singleton_self : singleton f f :=
  singleton.mk

/-- A presieve `R` has pullbacks along `f` if for every `h` in `R`, the pullback
with `f` exists. -/
protected class HasPullbacks (R : Presieve X) {Y : C} (f : Y ‚ü∂ X) : Prop where
  hasPullback (f) {Z : C} {h : Z ‚ü∂ X} : R h ‚Üí Limits.HasPullback h f

protected alias hasPullback := HasPullbacks.hasPullback

instance [HasPullbacks C] (R : Presieve X) {Y : C} (f : Y ‚ü∂ X) : R.HasPullbacks f where
  hasPullback _ := inferInstance

instance (g : Z ‚ü∂ X) [HasPullback g f] : (singleton g).HasPullbacks f where
  hasPullback {Z} h := by
    intro ‚ü®‚ü©
    infer_instance

/-- Pullback a set of arrows with given codomain along a fixed map, by taking the pullback in the
category.
This is not the same as the arrow set of `Sieve.pullback`, but there is a relation between them
in `pullbackArrows_comm`.
-/
inductive pullbackArrows (R : Presieve X) [R.HasPullbacks f] : Presieve Y
  | mk (Z : C) (h : Z ‚ü∂ X) (hRh : R h) :
    haveI := R.hasPullback f hRh
    pullbackArrows _ (pullback.snd h f)

theorem pullback_singleton (g : Z ‚ü∂ X) [HasPullback g f] :
    pullbackArrows f (singleton g) = singleton (pullback.snd g f) := by
  funext W
  ext h
  constructor
  ¬∑ rintro ‚ü®W, _, _, _‚ü©
    exact singleton.mk
  ¬∑ rintro ‚ü®_‚ü©
    exact pullbackArrows.mk Z g singleton.mk

/-- Construct the presieve given by the family of arrows indexed by `Œπ`. -/
inductive ofArrows {Œπ : Type*} (Y : Œπ ‚Üí C) (f : ‚àÄ i, Y i ‚ü∂ X) : Presieve X
  | mk (i : Œπ) : ofArrows _ _ (f i)

lemma ofArrows.mk' {Œπ : Type*} {Y : Œπ ‚Üí C} {f : ‚àÄ i, Y i ‚ü∂ X} {Z : C} {g : Z ‚ü∂ X}
    (i : Œπ) (h : Z = Y i) (hg : g = eqToHom h ‚â´ f i) :
    ofArrows Y f g := by
  subst h
  simp only [eqToHom_refl, id_comp] at hg
  subst hg
  constructor

theorem ofArrows_pUnit : (ofArrows _ fun _ : PUnit => f) = singleton f := by
  funext Y
  ext g
  constructor
  ¬∑ rintro ‚ü®_‚ü©
    apply singleton.mk
  ¬∑ rintro ‚ü®_‚ü©
    exact ofArrows.mk PUnit.unit

instance {Œπ : Type*} (Z : Œπ ‚Üí C) (g : ‚àÄ i : Œπ, Z i ‚ü∂ X)
    [‚àÄ i, HasPullback (g i) f] : (ofArrows Z g).HasPullbacks f where
  hasPullback {_} _ := fun ‚ü®i‚ü© ‚Ü¶ inferInstance

theorem ofArrows_pullback {Œπ : Type*} (Z : Œπ ‚Üí C) (g : ‚àÄ i : Œπ, Z i ‚ü∂ X)
    [‚àÄ i, HasPullback (g i) f] :
    (ofArrows (fun i => pullback (g i) f) fun _ => pullback.snd _ _) =
      pullbackArrows f (ofArrows Z g) := by
  funext T
  ext h
  constructor
  ¬∑ rintro ‚ü®hk‚ü©
    exact pullbackArrows.mk _ _ (ofArrows.mk hk)
  ¬∑ rintro ‚ü®W, k, ‚ü®_‚ü©‚ü©
    apply ofArrows.mk

theorem ofArrows_bind {Œπ : Type*} (Z : Œπ ‚Üí C) (g : ‚àÄ i : Œπ, Z i ‚ü∂ X)
    (j : ‚àÄ ‚¶ÉY‚¶Ñ (f : Y ‚ü∂ X), ofArrows Z g f ‚Üí Type*) (W : ‚àÄ ‚¶ÉY‚¶Ñ (f : Y ‚ü∂ X) (H), j f H ‚Üí C)
    (k : ‚àÄ ‚¶ÉY‚¶Ñ (f : Y ‚ü∂ X) (H i), W f H i ‚ü∂ Y) :
    ((ofArrows Z g).bind fun _ f H => ofArrows (W f H) (k f H)) =
      ofArrows (fun i : Œ£ i, j _ (ofArrows.mk i) => W (g i.1) _ i.2) fun ij =>
        k (g ij.1) _ ij.2 ‚â´ g ij.1 := by
  funext Y
  ext f
  constructor
  ¬∑ rintro ‚ü®_, _, _, ‚ü®i‚ü©, ‚ü®i'‚ü©, rfl‚ü©
    exact ofArrows.mk (Sigma.mk _ _)
  ¬∑ rintro ‚ü®i‚ü©
    exact bind_comp _ (ofArrows.mk _) (ofArrows.mk _)

theorem ofArrows_surj {Œπ : Type*} {Y : Œπ ‚Üí C} (f : ‚àÄ i, Y i ‚ü∂ X) {Z : C} (g : Z ‚ü∂ X)
    (hg : ofArrows Y f g) : ‚àÉ (i : Œπ) (h : Y i = Z),
    g = eqToHom h.symm ‚â´ f i := by
  obtain ‚ü®i‚ü© := hg
  exact ‚ü®i, rfl, by simp only [eqToHom_refl, id_comp]‚ü©

lemma exists_eq_ofArrows (R : Presieve X) :
    ‚àÉ (Œπ : Type (max u‚ÇÅ v‚ÇÅ)) (Y : Œπ ‚Üí C) (f : ‚àÄ i, Y i ‚ü∂ X), R = .ofArrows Y f := by
  let Œπ := { x : Œ£ Z, (Z ‚ü∂ X) // R x.2 }
  use Œπ, fun x ‚Ü¶ x.1.1, fun x ‚Ü¶ x.1.2
  exact le_antisymm (fun Z g hg ‚Ü¶ .mk (‚ü®‚ü®_, _‚ü©, hg‚ü© : Œπ)) fun Z g ‚ü®x‚ü© ‚Ü¶ x.2

/-- A convenient constructor for a refinement of a presieve of the form `Presieve.ofArrows`.
This contains a sieve obtained by `Sieve.bind` and `Sieve.ofArrows`, see
`Presieve.bind_ofArrows_le_bindOfArrows`, but has better definitional properties. -/
inductive bindOfArrows {Œπ : Type*} {X : C} (Y : Œπ ‚Üí C)
    (f : ‚àÄ i, Y i ‚ü∂ X) (R : ‚àÄ i, Presieve (Y i)) : Presieve X
  | mk (i : Œπ) {Z : C} (g : Z ‚ü∂ Y i) (hg : R i g) : bindOfArrows Y f R (g ‚â´ f i)

/-- Given a presieve on `F(X)`, we can define a presieve on `X` by taking the preimage via `F`. -/
def functorPullback (R : Presieve (F.obj X)) : Presieve X := fun _ f => R (F.map f)

@[simp]
theorem functorPullback_mem (R : Presieve (F.obj X)) {Y} (f : Y ‚ü∂ X) :
    R.functorPullback F f ‚Üî R (F.map f) :=
  Iff.rfl

@[simp]
theorem functorPullback_id (R : Presieve X) : R.functorPullback (ùü≠ _) = R :=
  rfl

/-- Given a presieve `R` on `X`, the predicate `R.HasPairwisePullbacks` means that for all arrows
`f` and `g` in `R`, the pullback of `f` and `g` exists. -/
class HasPairwisePullbacks (R : Presieve X) : Prop where
  /-- For all arrows `f` and `g` in `R`, the pullback of `f` and `g` exists. -/
  has_pullbacks : ‚àÄ {Y Z} {f : Y ‚ü∂ X} (_ : R f) {g : Z ‚ü∂ X} (_ : R g), HasPullback f g

@[deprecated (since := "2025-08-28")]
alias hasPullbacks := HasPairwisePullbacks

instance (R : Presieve X) [HasPullbacks C] : R.HasPairwisePullbacks := ‚ü®fun _ _ ‚Ü¶ inferInstance‚ü©

instance {Œ± : Type v‚ÇÇ} {X : Œ± ‚Üí C} {B : C} (œÄ : (a : Œ±) ‚Üí X a ‚ü∂ B)
    [(Presieve.ofArrows X œÄ).HasPairwisePullbacks] (a b : Œ±) : HasPullback (œÄ a) (œÄ b) :=
  Presieve.HasPairwisePullbacks.has_pullbacks (Presieve.ofArrows.mk _) (Presieve.ofArrows.mk _)

section FunctorPushforward

variable {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E] (G : D ‚•§ E)

/-- Given a presieve on `X`, we can define a presieve on `F(X)` (which is actually a sieve)
by taking the sieve generated by the image via `F`.
-/
def functorPushforward (S : Presieve X) : Presieve (F.obj X) := fun Y f =>
  ‚àÉ (Z : C) (g : Z ‚ü∂ X) (h : Y ‚ü∂ F.obj Z), S g ‚àß f = h ‚â´ F.map g

/-- An auxiliary definition in order to fix the choice of the preimages between various definitions.
-/
structure FunctorPushforwardStructure (S : Presieve X) {Y} (f : Y ‚ü∂ F.obj X) where
  /-- an object in the source category -/
  preobj : C
  /-- a map in the source category which has to be in the presieve -/
  premap : preobj ‚ü∂ X
  /-- the morphism which appear in the factorisation -/
  lift : Y ‚ü∂ F.obj preobj
  /-- the condition that `premap` is in the presieve -/
  cover : S premap
  /-- the factorisation of the morphism -/
  fac : f = lift ‚â´ F.map premap

/-- The fixed choice of a preimage. -/
noncomputable def getFunctorPushforwardStructure {F : C ‚•§ D} {S : Presieve X} {Y : D}
    {f : Y ‚ü∂ F.obj X} (h : S.functorPushforward F f) : FunctorPushforwardStructure F S f := by
  choose Z f' g h‚ÇÅ h using h
  exact ‚ü®Z, f', g, h‚ÇÅ, h‚ü©

theorem functorPushforward_comp (R : Presieve X) :
    R.functorPushforward (F ‚ãô G) = (R.functorPushforward F).functorPushforward G := by
  funext x
  ext f
  constructor
  ¬∑ rintro ‚ü®X, f‚ÇÅ, g‚ÇÅ, h‚ÇÅ, rfl‚ü©
    exact ‚ü®F.obj X, F.map f‚ÇÅ, g‚ÇÅ, ‚ü®X, f‚ÇÅ, ùüô _, h‚ÇÅ, by simp‚ü©, rfl‚ü©
  ¬∑ rintro ‚ü®X, f‚ÇÅ, g‚ÇÅ, ‚ü®X', f‚ÇÇ, g‚ÇÇ, h‚ÇÅ, rfl‚ü©, rfl‚ü©
    exact ‚ü®X', f‚ÇÇ, g‚ÇÅ ‚â´ G.map g‚ÇÇ, h‚ÇÅ, by simp‚ü©

theorem image_mem_functorPushforward (R : Presieve X) {f : Y ‚ü∂ X} (h : R f) :
    R.functorPushforward F (F.map f) :=
  ‚ü®Y, f, ùüô _, h, by simp‚ü©

/-- This presieve generates `functorPushforward`.
See `arrows_generate_map_eq_functorPushforward`. -/
inductive map (s : Presieve X) : Presieve (F.obj X) where
  | of {Y : C} {u : Y ‚ü∂ X} (h : s u) : map s (F.map u)

section

variable {F}

@[grind ‚Üê]
lemma map_map {X Y : C} {f : Y ‚ü∂ X} {R : Presieve X} (hf : R f) : R.map F (F.map f) :=
  ‚ü®hf‚ü©

@[simp]
lemma map_ofArrows {X : C} {Œπ : Type*} {Y : Œπ ‚Üí C} (f : ‚àÄ i, Y i ‚ü∂ X) :
    (ofArrows Y f).map F = ofArrows _ (fun i ‚Ü¶ F.map (f i)) := by
  refine le_antisymm (fun Z g hg ‚Ü¶ ?_) fun _ _ ‚ü®i‚ü© ‚Ü¶ map_map ‚ü®i‚ü©
  obtain ‚ü®hu‚ü© := hg
  obtain ‚ü®i, rfl, rfl‚ü© := Presieve.ofArrows_surj _ _ hu
  simpa using ofArrows.mk i

@[simp]
lemma map_singleton {X Y : C} (f : X ‚ü∂ Y) : (singleton f).map F = singleton (F.map f) := by
  rw [‚Üê ofArrows_pUnit.{_, _, 0}, map_ofArrows, ofArrows_pUnit]

end

end FunctorPushforward

section uncurry

variable (s : Presieve X)

/-- Uncurry a presieve to one set over the sigma type. -/
def uncurry : Set (Œ£ Y, Y ‚ü∂ X) :=
  { u | s u.snd }

@[simp] theorem uncurry_singleton {Y : C} (u : Y ‚ü∂ X) : (singleton u).uncurry = { ‚ü®Y, u‚ü© } := by
  ext ‚ü®Z, v‚ü©; constructor
  ¬∑ rintro ‚ü®‚ü©; rfl
  ¬∑ intro h
    rw [Set.mem_singleton_iff, Sigma.ext_iff] at h
    obtain ‚ü®rfl, h‚ü© := h; subst h; constructor

@[simp] theorem uncurry_pullbackArrows [HasPullbacks C] {B : C} (b : B ‚ü∂ X) :
    (pullbackArrows b s).uncurry = (fun f ‚Ü¶ ‚ü®pullback f.2 b, pullback.snd _ _‚ü©) '' s.uncurry := by
  ext ‚ü®Z, v‚ü©; constructor
  ¬∑ rintro ‚ü®Y, u, hu‚ü©; exact ‚ü®‚ü®Y, u‚ü©, hu, rfl‚ü©
  ¬∑ rintro ‚ü®‚ü®Y, u‚ü©, hu, h‚ü©
    rw [Sigma.ext_iff] at h
    obtain ‚ü®rfl, h‚ü© := h
    rw [heq_iff_eq] at h; subst h
    exact ‚ü®Y, u, hu‚ü©

@[simp] theorem uncurry_bind (t : ‚¶ÉY : C‚¶Ñ ‚Üí (f : Y ‚ü∂ X) ‚Üí s f ‚Üí Presieve Y) :
    (s.bind t).uncurry = ‚ãÉ i ‚àà s.uncurry,
      Sigma.map id (fun Z g ‚Ü¶ (g ‚â´ i.2 : Z ‚ü∂ X)) '' (t _ ‚Äπ_‚Ä∫).uncurry := by
  ext ‚ü®Z, v‚ü©; simp only [Set.mem_iUnion, Set.mem_image]; constructor
  ¬∑ rintro ‚ü®Y, g, f, hf, ht, hv‚ü©
    exact ‚ü®‚ü®_, f‚ü©, hf, ‚ü®_, g‚ü©, ht, Sigma.ext rfl (heq_of_eq hv)‚ü©
  ¬∑ rintro ‚ü®‚ü®_, f‚ü©, hf, ‚ü®Y, g‚ü©, hg, h‚ü©
    rw [Sigma.ext_iff] at h
    obtain ‚ü®rfl, h‚ü© := h
    rw [heq_iff_eq] at h; subst h
    exact ‚ü®_, _, _, _, hg, rfl‚ü©

@[simp] theorem uncurry_ofArrows {Œπ : Type*} (Y : Œπ ‚Üí C) (f : (i : Œπ) ‚Üí Y i ‚ü∂ X) :
    (ofArrows Y f).uncurry = Set.range fun i : Œπ ‚Ü¶ ‚ü®_, f i‚ü© := by
  ext ‚ü®Z, v‚ü©; simp only [Set.mem_range, Sigma.mk.injEq]; constructor
  ¬∑ rintro ‚ü®i‚ü©; exact ‚ü®_, rfl, HEq.refl _‚ü©
  ¬∑ rintro ‚ü®i, rfl, h‚ü©; rw [‚Üê eq_of_heq h]; exact ‚ü®i‚ü©

end uncurry

end Presieve

/--
For an object `X` of a category `C`, a `Sieve X` is a set of morphisms to `X` which is closed under
left-composition.
-/
structure Sieve {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] (X : C) where
  /-- the underlying presieve -/
  arrows : Presieve X
  /-- stability by precomposition -/
  downward_closed : ‚àÄ {Y Z f} (_ : arrows f) (g : Z ‚ü∂ Y), arrows (g ‚â´ f)

namespace Sieve

instance : CoeFun (Sieve X) fun _ => Presieve X :=
  ‚ü®Sieve.arrows‚ü©

initialize_simps_projections Sieve (arrows ‚Üí apply)

variable {S R : Sieve X}

attribute [simp] downward_closed

theorem arrows_ext : ‚àÄ {R S : Sieve X}, R.arrows = S.arrows ‚Üí R = S := by
  rintro ‚ü®_, _‚ü© ‚ü®_, _‚ü© rfl
  rfl

@[ext]
protected theorem ext {R S : Sieve X} (h : ‚àÄ ‚¶ÉY‚¶Ñ (f : Y ‚ü∂ X), R f ‚Üî S f) : R = S :=
  arrows_ext <| funext fun _ => funext fun f => propext <| h f

open Lattice

/-- The supremum of a collection of sieves: the union of them all. -/
protected def sup (ùíÆ : Set (Sieve X)) : Sieve X where
  arrows _ := { f | ‚àÉ S ‚àà ùíÆ, Sieve.arrows S f }
  downward_closed {_ _ f} hf _ := by
    obtain ‚ü®S, hS, hf‚ü© := hf
    exact ‚ü®S, hS, S.downward_closed hf _‚ü©

/-- The infimum of a collection of sieves: the intersection of them all. -/
protected def inf (ùíÆ : Set (Sieve X)) : Sieve X where
  arrows _ := { f | ‚àÄ S ‚àà ùíÆ, Sieve.arrows S f }
  downward_closed {_ _ _} hf g S H := S.downward_closed (hf S H) g

/-- The union of two sieves is a sieve. -/
protected def union (S R : Sieve X) : Sieve X where
  arrows _ f := S f ‚à® R f
  downward_closed := by rintro _ _ _ (h | h) g <;> simp [h]

/-- The intersection of two sieves is a sieve. -/
protected def inter (S R : Sieve X) : Sieve X where
  arrows _ f := S f ‚àß R f
  downward_closed := by
    rintro _ _ _ ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© g
    simp [h‚ÇÅ, h‚ÇÇ]

/-- Sieves on an object `X` form a complete lattice.
We generate this directly rather than using the Galois insertion for nicer definitional properties.
-/
instance : CompleteLattice (Sieve X) where
  le S R := ‚àÄ ‚¶ÉY‚¶Ñ (f : Y ‚ü∂ X), S f ‚Üí R f
  le_refl _ _ _ := id
  le_trans _ _ _ S‚ÇÅ‚ÇÇ S‚ÇÇ‚ÇÉ _ _ h := S‚ÇÇ‚ÇÉ _ (S‚ÇÅ‚ÇÇ _ h)
  le_antisymm _ _ p q := Sieve.ext fun _ _ => ‚ü®p _, q _‚ü©
  top :=
    { arrows := fun _ => Set.univ
      downward_closed := fun _ _ => ‚ü®‚ü© }
  bot :=
    { arrows := fun _ => ‚àÖ
      downward_closed := False.elim }
  sup := Sieve.union
  inf := Sieve.inter
  sSup := Sieve.sup
  sInf := Sieve.inf
  le_sSup _ S hS _ _ hf := ‚ü®S, hS, hf‚ü©
  sSup_le := fun _ _ ha _ _ ‚ü®b, hb, hf‚ü© => (ha b hb) _ hf
  sInf_le _ _ hS _ _ h := h _ hS
  le_sInf _ _ hS _ _ hf _ hR := hS _ hR _ hf
  le_sup_left _ _ _ _ := Or.inl
  le_sup_right _ _ _ _ := Or.inr
  sup_le _ _ _ h‚ÇÅ h‚ÇÇ _ f := by--‚Ñ∞ S hS Y f := by
    rintro (hf | hf)
    ¬∑ exact h‚ÇÅ _ hf
    ¬∑ exact h‚ÇÇ _ hf
  inf_le_left _ _ _ _ := And.left
  inf_le_right _ _ _ _ := And.right
  le_inf _ _ _ p q _ _ z := ‚ü®p _ z, q _ z‚ü©
  le_top _ _ _ _ := trivial
  bot_le _ _ _ := False.elim

/-- The maximal sieve always exists. -/
instance sieveInhabited : Inhabited (Sieve X) :=
  ‚ü®‚ä§‚ü©

@[simp]
theorem sInf_apply {Ss : Set (Sieve X)} {Y} (f : Y ‚ü∂ X) :
    sInf Ss f ‚Üî ‚àÄ (S : Sieve X) (_ : S ‚àà Ss), S f :=
  Iff.rfl

@[simp]
theorem sSup_apply {Ss : Set (Sieve X)} {Y} (f : Y ‚ü∂ X) :
    sSup Ss f ‚Üî ‚àÉ (S : Sieve X) (_ : S ‚àà Ss), S f := by
  simp [sSup, Sieve.sup, setOf]

@[simp]
theorem inter_apply {R S : Sieve X} {Y} (f : Y ‚ü∂ X) : (R ‚äì S) f ‚Üî R f ‚àß S f :=
  Iff.rfl

@[simp]
theorem union_apply {R S : Sieve X} {Y} (f : Y ‚ü∂ X) : (R ‚äî S) f ‚Üî R f ‚à® S f :=
  Iff.rfl

@[simp]
theorem top_apply (f : Y ‚ü∂ X) : (‚ä§ : Sieve X) f :=
  trivial

/-- Generate the smallest sieve containing the given set of arrows. -/
@[simps]
def generate (R : Presieve X) : Sieve X where
  arrows Z f := ‚àÉ (Y : _) (h : Z ‚ü∂ Y) (g : Y ‚ü∂ X), R g ‚àß h ‚â´ g = f
  downward_closed := by
    rintro Y Z _ ‚ü®W, g, f, hf, rfl‚ü© h
    exact ‚ü®_, h ‚â´ g, _, hf, by simp‚ü©

theorem arrows_generate_map_eq_functorPushforward {s : Presieve X} :
    (generate (s.map F)).arrows = s.functorPushforward F := by
  refine funext fun Z ‚Ü¶ Set.ext fun u ‚Ü¶ ‚ü®?_, ?_‚ü©
  ¬∑ rintro ‚ü®_, _, _, ‚ü®hu‚ü©, rfl‚ü©; exact ‚ü®_, _, _, hu, rfl‚ü©
  ¬∑ rintro ‚ü®_, _, _, hu, rfl‚ü©; exact ‚ü®_, _, _, ‚ü®hu‚ü©, rfl‚ü©

/-- Given a presieve on `X`, and a sieve on each domain of an arrow in the presieve, we can bind to
produce a sieve on `X`.
-/
@[simps]
def bind (S : Presieve X) (R : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ, S f ‚Üí Sieve Y) : Sieve X where
  arrows := S.bind fun _ _ h => R h
  downward_closed := by
    rintro Y Z f ‚ü®W, f, h, hh, hf, rfl‚ü© g
    exact ‚ü®_, g ‚â´ f, _, hh, by simp [hf]‚ü©

/-- Structure which contains the data and properties for a morphism `h` satisfying
`Sieve.bind S R h`. -/
abbrev BindStruct (S : Presieve X) (R : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ, S f ‚Üí Sieve Y)
    {Z : C} (h : Z ‚ü∂ X) :=
  Presieve.BindStruct S (fun _ _ hf ‚Ü¶ R hf) h

open Order Lattice

theorem generate_le_iff (R : Presieve X) (S : Sieve X) : generate R ‚â§ S ‚Üî R ‚â§ S :=
  ‚ü®fun H _ _ hg => H _ ‚ü®_, ùüô _, _, hg, id_comp _‚ü©, fun ss Y f => by
    rintro ‚ü®Z, f, g, hg, rfl‚ü©
    exact S.downward_closed (ss Z hg) f‚ü©

/-- Show that there is a Galois insertion (generate, set_over). -/
def giGenerate : GaloisInsertion (generate : Presieve X ‚Üí Sieve X) arrows where
  gc := generate_le_iff
  choice ùí¢ _ := generate ùí¢
  choice_eq _ _ := rfl
  le_l_u _ _ _ hf := ‚ü®_, ùüô _, _, hf, id_comp _‚ü©

theorem le_generate (R : Presieve X) : R ‚â§ generate R :=
  giGenerate.gc.le_u_l R

@[simp]
theorem generate_sieve (S : Sieve X) : generate S = S :=
  giGenerate.l_u_eq S

/-- If the identity arrow is in a sieve, the sieve is maximal. -/
theorem id_mem_iff_eq_top : S (ùüô X) ‚Üî S = ‚ä§ :=
  ‚ü®fun h => top_unique fun Y f _ => by simpa using downward_closed _ h f, fun h => h.symm ‚ñ∏ trivial‚ü©

/-- If an arrow set contains a split epi, it generates the maximal sieve. -/
theorem generate_of_contains_isSplitEpi {R : Presieve X} (f : Y ‚ü∂ X) [IsSplitEpi f] (hf : R f) :
    generate R = ‚ä§ := by
  rw [‚Üê id_mem_iff_eq_top]
  exact ‚ü®_, section_ f, f, hf, by simp‚ü©

@[simp]
theorem generate_of_singleton_isSplitEpi (f : Y ‚ü∂ X) [IsSplitEpi f] :
    generate (Presieve.singleton f) = ‚ä§ :=
  generate_of_contains_isSplitEpi f (Presieve.singleton_self _)

@[simp]
theorem generate_top : generate (‚ä§ : Presieve X) = ‚ä§ :=
  generate_of_contains_isSplitEpi (ùüô _) ‚ü®‚ü©

@[simp]
lemma comp_mem_iff (i : X ‚ü∂ Y) (f : Y ‚ü∂ Z) [IsIso i] (S : Sieve Z) :
    S (i ‚â´ f) ‚Üî S f := by
  refine ‚ü®fun H ‚Ü¶ ?_, fun H ‚Ü¶ S.downward_closed H _‚ü©
  convert S.downward_closed H (inv i)
  simp

section

variable {I : Type*} {X : C} (Y : I ‚Üí C) (f : ‚àÄ i, Y i ‚ü∂ X)

/-- The sieve of `X` generated by family of morphisms `Y i ‚ü∂ X`. -/
abbrev ofArrows : Sieve X := generate (Presieve.ofArrows Y f)

lemma ofArrows_mk (i : I) : ofArrows Y f (f i) :=
  ‚ü®_, ùüô _, _, ‚ü®i‚ü©, by simp‚ü©

lemma mem_ofArrows_iff {W : C} (g : W ‚ü∂ X) :
    ofArrows Y f g ‚Üî ‚àÉ (i : I) (a : W ‚ü∂ Y i), g = a ‚â´ f i := by
  constructor
  ¬∑ rintro ‚ü®T, a, b, ‚ü®i‚ü©, rfl‚ü©
    exact ‚ü®i, a, rfl‚ü©
  ¬∑ rintro ‚ü®i, a, rfl‚ü©
    apply downward_closed _ (ofArrows_mk Y f i)

variable {Y f} {W : C} {g : W ‚ü∂ X} (hg : ofArrows Y f g)

include hg in
lemma ofArrows.exists : ‚àÉ (i : I) (h : W ‚ü∂ Y i), g = h ‚â´ f i := by
  obtain ‚ü®_, h, _, ‚ü®i‚ü©, rfl‚ü© := hg
  exact ‚ü®i, h, rfl‚ü©

/-- When `hg : Sieve.ofArrows Y f g`, this is a choice of `i` such that `g`
factors through `f i`. -/
noncomputable def ofArrows.i : I := (ofArrows.exists hg).choose

/-- When `hg : Sieve.ofArrows Y f g`, this is a morphism `h : W ‚ü∂ Y (i hg)` such
that `h ‚â´ f (i hg) = g`. -/
noncomputable def ofArrows.h : W ‚ü∂ Y (i hg) := (ofArrows.exists hg).choose_spec.choose

@[reassoc (attr := simp)]
lemma ofArrows.fac : h hg ‚â´ f (i hg) = g :=
  (ofArrows.exists hg).choose_spec.choose_spec.symm

end

/-- The sieve generated by two morphisms. -/
abbrev ofTwoArrows {U V X : C} (i : U ‚ü∂ X) (j : V ‚ü∂ X) : Sieve X :=
  Sieve.ofArrows (Y := pairFunction U V) (fun k ‚Ü¶ WalkingPair.casesOn k i j)

/-- The sieve of `X : C` that is generated by a family of objects `Y : I ‚Üí C`:
it consists of morphisms to `X` which factor through at least one of the `Y i`. -/
def ofObjects {I : Type*} (Y : I ‚Üí C) (X : C) : Sieve X where
  arrows Z _ := ‚àÉ (i : I), Nonempty (Z ‚ü∂ Y i)
  downward_closed := by
    rintro Z‚ÇÅ Z‚ÇÇ p ‚ü®i, ‚ü®f‚ü©‚ü© g
    exact ‚ü®i, ‚ü®g ‚â´ f‚ü©‚ü©

lemma mem_ofObjects_iff {I : Type*} (Y : I ‚Üí C) {Z X : C} (g : Z ‚ü∂ X) :
    ofObjects Y X g ‚Üî ‚àÉ (i : I), Nonempty (Z ‚ü∂ Y i) := by rfl

lemma ofArrows_le_ofObjects
    {I : Type*} (Y : I ‚Üí C) {X : C} (f : ‚àÄ i, Y i ‚ü∂ X) :
    Sieve.ofArrows Y f ‚â§ Sieve.ofObjects Y X := by
  intro W g hg
  rw [mem_ofArrows_iff] at hg
  obtain ‚ü®i, a, rfl‚ü© := hg
  exact ‚ü®i, ‚ü®a‚ü©‚ü©

lemma ofArrows_eq_ofObjects {X : C} (hX : IsTerminal X)
    {I : Type*} (Y : I ‚Üí C) (f : ‚àÄ i, Y i ‚ü∂ X) :
    ofArrows Y f = ofObjects Y X := by
  refine le_antisymm (ofArrows_le_ofObjects Y f) (fun W g => ?_)
  rw [mem_ofArrows_iff, mem_ofObjects_iff]
  rintro ‚ü®i, ‚ü®h‚ü©‚ü©
  exact ‚ü®i, h, hX.hom_ext _ _‚ü©

/-- Given a morphism `h : Y ‚ü∂ X`, send a sieve S on X to a sieve on Y
as the inverse image of S with `_ ‚â´ h`. That is, `Sieve.pullback S h := (‚â´ h) '‚Åª¬π S`. -/
@[simps]
def pullback (h : Y ‚ü∂ X) (S : Sieve X) : Sieve Y where
  arrows _ sl := S (sl ‚â´ h)
  downward_closed g := by simp [g]

@[simp]
theorem pullback_id : S.pullback (ùüô _) = S := by simp [Sieve.ext_iff]

@[simp]
theorem pullback_top {f : Y ‚ü∂ X} : (‚ä§ : Sieve X).pullback f = ‚ä§ :=
  top_unique fun _ _ => id

theorem pullback_comp {f : Y ‚ü∂ X} {g : Z ‚ü∂ Y} (S : Sieve X) :
    S.pullback (g ‚â´ f) = (S.pullback f).pullback g := by simp [Sieve.ext_iff]

@[simp]
theorem pullback_inter {f : Y ‚ü∂ X} (S R : Sieve X) :
    (S ‚äì R).pullback f = S.pullback f ‚äì R.pullback f := by simp [Sieve.ext_iff]

theorem mem_iff_pullback_eq_top (f : Y ‚ü∂ X) : S f ‚Üî S.pullback f = ‚ä§ := by
  rw [‚Üê id_mem_iff_eq_top, pullback_apply, id_comp]

theorem pullback_eq_top_of_mem (S : Sieve X) {f : Y ‚ü∂ X} : S f ‚Üí S.pullback f = ‚ä§ :=
  (mem_iff_pullback_eq_top f).1

lemma pullback_ofObjects_eq_top
    {I : Type*} (Y : I ‚Üí C) {X : C} {i : I} (g : X ‚ü∂ Y i) :
    ofObjects Y X = ‚ä§ := by
  ext Z h
  simp only [top_apply, iff_true]
  rw [mem_ofObjects_iff ]
  exact ‚ü®i, ‚ü®h ‚â´ g‚ü©‚ü©

/-- Push a sieve `R` on `Y` forward along an arrow `f : Y ‚ü∂ X`: `gf : Z ‚ü∂ X` is in the sieve if `gf`
factors through some `g : Z ‚ü∂ Y` which is in `R`.
-/
@[simps]
def pushforward (f : Y ‚ü∂ X) (R : Sieve Y) : Sieve X where
  arrows _ gf := ‚àÉ g, g ‚â´ f = gf ‚àß R g
  downward_closed := fun ‚ü®j, k, z‚ü© h => ‚ü®h ‚â´ j, by simp [k], by simp [z]‚ü©

theorem pushforward_apply_comp {R : Sieve Y} {Z : C} {g : Z ‚ü∂ Y} (hg : R g) (f : Y ‚ü∂ X) :
    R.pushforward f (g ‚â´ f) :=
  ‚ü®g, rfl, hg‚ü©

theorem pushforward_comp {f : Y ‚ü∂ X} {g : Z ‚ü∂ Y} (R : Sieve Z) :
    R.pushforward (g ‚â´ f) = (R.pushforward g).pushforward f :=
  Sieve.ext fun W h =>
    ‚ü®fun ‚ü®f‚ÇÅ, hq, hf‚ÇÅ‚ü© => ‚ü®f‚ÇÅ ‚â´ g, by simpa, f‚ÇÅ, rfl, hf‚ÇÅ‚ü©, fun ‚ü®y, hy, z, hR, hz‚ü© =>
      ‚ü®z, by rw [‚Üê Category.assoc, hR]; tauto‚ü©‚ü©

theorem galoisConnection (f : Y ‚ü∂ X) : GaloisConnection (Sieve.pushforward f) (Sieve.pullback f) :=
  fun _ _ => ‚ü®fun hR _ g hg => hR _ ‚ü®g, rfl, hg‚ü©, fun hS _ _ ‚ü®h, hg, hh‚ü© => hg ‚ñ∏ hS h hh‚ü©

theorem pullback_monotone (f : Y ‚ü∂ X) : Monotone (Sieve.pullback f) :=
  (galoisConnection f).monotone_u

theorem pushforward_monotone (f : Y ‚ü∂ X) : Monotone (Sieve.pushforward f) :=
  (galoisConnection f).monotone_l

theorem le_pushforward_pullback (f : Y ‚ü∂ X) (R : Sieve Y) : R ‚â§ (R.pushforward f).pullback f :=
  (galoisConnection f).le_u_l _

theorem pullback_pushforward_le (f : Y ‚ü∂ X) (R : Sieve X) : (R.pullback f).pushforward f ‚â§ R :=
  (galoisConnection f).l_u_le _

theorem pushforward_union {f : Y ‚ü∂ X} (S R : Sieve Y) :
    (S ‚äî R).pushforward f = S.pushforward f ‚äî R.pushforward f :=
  (galoisConnection f).l_sup

theorem pushforward_le_bind_of_mem (S : Presieve X) (R : ‚àÄ ‚¶ÉY : C‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ, S f ‚Üí Sieve Y)
    (f : Y ‚ü∂ X) (h : S f) : (R h).pushforward f ‚â§ bind S R := by
  rintro Z _ ‚ü®g, rfl, hg‚ü©
  exact ‚ü®_, g, f, h, hg, rfl‚ü©

theorem le_pullback_bind (S : Presieve X) (R : ‚àÄ ‚¶ÉY : C‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ, S f ‚Üí Sieve Y) (f : Y ‚ü∂ X)
    (h : S f) : R h ‚â§ (bind S R).pullback f := by
  rw [‚Üê galoisConnection f]
  apply pushforward_le_bind_of_mem

/-- If `f` is a monomorphism, the pushforward-pullback adjunction on sieves is coreflective. -/
def galoisCoinsertionOfMono (f : Y ‚ü∂ X) [Mono f] :
    GaloisCoinsertion (Sieve.pushforward f) (Sieve.pullback f) := by
  apply (galoisConnection f).toGaloisCoinsertion
  rintro S Z g ‚ü®g‚ÇÅ, hf, hg‚ÇÅ‚ü©
  rw [cancel_mono f] at hf
  rwa [‚Üê hf]

/-- If `f` is a split epi, the pushforward-pullback adjunction on sieves is reflective. -/
def galoisInsertionOfIsSplitEpi (f : Y ‚ü∂ X) [IsSplitEpi f] :
    GaloisInsertion (Sieve.pushforward f) (Sieve.pullback f) := by
  apply (galoisConnection f).toGaloisInsertion
  intro S Z g hg
  exact ‚ü®g ‚â´ section_ f, by simpa‚ü©

theorem pullbackArrows_comm {X Y : C} (f : Y ‚ü∂ X) (R : Presieve X) [R.HasPullbacks f] :
    Sieve.generate (R.pullbackArrows f) = (Sieve.generate R).pullback f := by
  ext W g
  constructor
  ¬∑ rintro ‚ü®_, h, k, ‚ü®W, g, hg‚ü©, rfl‚ü©
    have := R.hasPullback f hg
    rw [Sieve.pullback_apply, assoc, ‚Üê pullback.condition, ‚Üê assoc]
    exact Sieve.downward_closed _ (by exact Sieve.le_generate R W hg) (h ‚â´ pullback.fst g f)
  ¬∑ rintro ‚ü®W, h, k, hk, comm‚ü©
    have := R.hasPullback f hk
    exact ‚ü®_, _, _, Presieve.pullbackArrows.mk _ _ hk, pullback.lift_snd _ _ comm‚ü©

section Functor

variable {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E] (G : D ‚•§ E)

/--
If `R` is a sieve, then the `CategoryTheory.Presieve.functorPullback` of `R` is actually a sieve.
-/
@[simps]
def functorPullback (R : Sieve (F.obj X)) : Sieve X where
  arrows := Presieve.functorPullback F R
  downward_closed := by
    intro _ _ f hf g
    unfold Presieve.functorPullback
    rw [F.map_comp]
    exact R.downward_closed hf (F.map g)

@[simp]
theorem functorPullback_arrows (R : Sieve (F.obj X)) :
    (R.functorPullback F).arrows = R.arrows.functorPullback F :=
  rfl

@[simp]
theorem functorPullback_id (R : Sieve X) : R.functorPullback (ùü≠ _) = R := by
  ext
  rfl

theorem functorPullback_comp (R : Sieve ((F ‚ãô G).obj X)) :
    R.functorPullback (F ‚ãô G) = (R.functorPullback G).functorPullback F := by
  ext
  rfl

theorem functorPushforward_extend_eq {R : Presieve X} :
    (generate R).arrows.functorPushforward F = R.functorPushforward F := by
  funext Y
  ext f
  constructor
  ¬∑ rintro ‚ü®X', g, f', ‚ü®X'', g', f'', h‚ÇÅ, rfl‚ü©, rfl‚ü©
    exact ‚ü®X'', f'', f' ‚â´ F.map g', h‚ÇÅ, by simp‚ü©
  ¬∑ rintro ‚ü®X', g, f', h‚ÇÅ, h‚ÇÇ‚ü©
    exact ‚ü®X', g, f', le_generate R _ h‚ÇÅ, h‚ÇÇ‚ü©

/-- The sieve generated by the image of `R` under `F`. -/
@[simps]
def functorPushforward (R : Sieve X) : Sieve (F.obj X) where
  arrows := R.arrows.functorPushforward F
  downward_closed := by
    intro _ _ f h g
    obtain ‚ü®X, Œ±, Œ≤, hŒ±, rfl‚ü© := h
    exact ‚ü®X, Œ±, g ‚â´ Œ≤, hŒ±, by simp‚ü©

theorem generate_map_eq_functorPushforward {s : Presieve X} :
    generate (s.map F) = (generate s).functorPushforward F := by
  ext
  rw [arrows_generate_map_eq_functorPushforward]
  simp [functorPushforward_extend_eq]

@[simp]
theorem functorPushforward_id (R : Sieve X) : R.functorPushforward (ùü≠ _) = R := by
  ext X f
  constructor
  ¬∑ intro hf
    obtain ‚ü®X, g, h, hg, rfl‚ü© := hf
    exact R.downward_closed hg h
  ¬∑ intro hf
    exact ‚ü®X, f, ùüô _, hf, by simp‚ü©

theorem functorPushforward_comp (R : Sieve X) :
    R.functorPushforward (F ‚ãô G) = (R.functorPushforward F).functorPushforward G := by
  ext
  simp [R.arrows.functorPushforward_comp F G]

theorem functor_galoisConnection (X : C) :
    GaloisConnection (Sieve.functorPushforward F : Sieve X ‚Üí Sieve (F.obj X))
      (Sieve.functorPullback F) := by
  intro R S
  constructor
  ¬∑ intro hle X f hf
    apply hle
    refine ‚ü®X, f, ùüô _, hf, ?_‚ü©
    rw [id_comp]
  ¬∑ rintro hle Y f ‚ü®X, g, h, hg, rfl‚ü©
    apply Sieve.downward_closed S
    exact hle g hg

theorem functorPullback_monotone (X : C) :
    Monotone (Sieve.functorPullback F : Sieve (F.obj X) ‚Üí Sieve X) :=
  (functor_galoisConnection F X).monotone_u

theorem functorPushforward_monotone (X : C) :
    Monotone (Sieve.functorPushforward F : Sieve X ‚Üí Sieve (F.obj X)) :=
  (functor_galoisConnection F X).monotone_l

theorem le_functorPushforward_pullback (R : Sieve X) :
    R ‚â§ (R.functorPushforward F).functorPullback F :=
  (functor_galoisConnection F X).le_u_l _

theorem functorPullback_pushforward_le (R : Sieve (F.obj X)) :
    (R.functorPullback F).functorPushforward F ‚â§ R :=
  (functor_galoisConnection F X).l_u_le _

theorem functorPushforward_union (S R : Sieve X) :
    (S ‚äî R).functorPushforward F = S.functorPushforward F ‚äî R.functorPushforward F :=
  (functor_galoisConnection F X).l_sup

theorem functorPullback_union (S R : Sieve (F.obj X)) :
    (S ‚äî R).functorPullback F = S.functorPullback F ‚äî R.functorPullback F :=
  rfl

theorem functorPullback_inter (S R : Sieve (F.obj X)) :
    (S ‚äì R).functorPullback F = S.functorPullback F ‚äì R.functorPullback F :=
  rfl

@[simp]
theorem functorPushforward_bot (F : C ‚•§ D) (X : C) : (‚ä• : Sieve X).functorPushforward F = ‚ä• :=
  (functor_galoisConnection F X).l_bot

@[simp]
theorem functorPushforward_top (F : C ‚•§ D) (X : C) : (‚ä§ : Sieve X).functorPushforward F = ‚ä§ := by
  refine (generate_sieve _).symm.trans ?_
  apply generate_of_contains_isSplitEpi (ùüô (F.obj X))
  exact ‚ü®X, ùüô _, ùüô _, trivial, by simp‚ü©

@[simp]
theorem functorPullback_bot (F : C ‚•§ D) (X : C) : (‚ä• : Sieve (F.obj X)).functorPullback F = ‚ä• :=
  rfl

@[simp]
theorem functorPullback_top (F : C ‚•§ D) (X : C) : (‚ä§ : Sieve (F.obj X)).functorPullback F = ‚ä§ :=
  rfl

theorem image_mem_functorPushforward (R : Sieve X) {V} {f : V ‚ü∂ X} (h : R f) :
    R.functorPushforward F (F.map f) :=
  ‚ü®V, f, ùüô _, h, by simp‚ü©

/-- When `F` is essentially surjective and full, the Galois connection is a Galois insertion. -/
def essSurjFullFunctorGaloisInsertion [F.EssSurj] [F.Full] (X : C) :
    GaloisInsertion (Sieve.functorPushforward F : Sieve X ‚Üí Sieve (F.obj X))
      (Sieve.functorPullback F) := by
  apply (functor_galoisConnection F X).toGaloisInsertion
  intro S Y f hf
  refine ‚ü®_, F.preimage ((F.objObjPreimageIso Y).hom ‚â´ f), (F.objObjPreimageIso Y).inv, ?_‚ü©
  simpa using hf

/-- When `F` is fully faithful, the Galois connection is a Galois coinsertion. -/
def fullyFaithfulFunctorGaloisCoinsertion [F.Full] [F.Faithful] (X : C) :
    GaloisCoinsertion (Sieve.functorPushforward F : Sieve X ‚Üí Sieve (F.obj X))
      (Sieve.functorPullback F) := by
  apply (functor_galoisConnection F X).toGaloisCoinsertion
  rintro S Y f ‚ü®Z, g, h, h‚ÇÅ, h‚ÇÇ‚ü©
  rw [‚Üê F.map_preimage h, ‚Üê F.map_comp] at h‚ÇÇ
  rw [F.map_injective h‚ÇÇ]
  exact S.downward_closed h‚ÇÅ _

lemma functorPushforward_functor (S : Sieve X) (e : C ‚âå D) :
    S.functorPushforward e.functor = (S.pullback (e.unitInv.app X)).functorPullback e.inverse := by
  ext Y iYX
  constructor
  ¬∑ rintro ‚ü®Z, iZX, iYZ, hiZX, rfl‚ü©
    simpa using S.downward_closed hiZX (e.inverse.map iYZ ‚â´ e.unitInv.app Z)
  ¬∑ intro H
    exact ‚ü®_, e.inverse.map iYX ‚â´ e.unitInv.app X, e.counitInv.app Y, by simpa using H, by simp‚ü©

@[simp]
lemma mem_functorPushforward_functor {Y : D} {S : Sieve X} {e : C ‚âå D} {f : Y ‚ü∂ e.functor.obj X} :
    S.functorPushforward e.functor f ‚Üî S (e.inverse.map f ‚â´ e.unitInv.app X) :=
  congr($(S.functorPushforward_functor e).arrows f)

lemma functorPushforward_inverse {X : D} (S : Sieve X) (e : C ‚âå D) :
    S.functorPushforward e.inverse = (S.pullback (e.counit.app X)).functorPullback e.functor :=
  Sieve.functorPushforward_functor S e.symm

@[simp]
lemma mem_functorPushforward_inverse {X : D} {S : Sieve X} {e : C ‚âå D} {f : Y ‚ü∂ e.inverse.obj X} :
    S.functorPushforward e.inverse f ‚Üî S (e.functor.map f ‚â´ e.counit.app X) :=
  congr($(S.functorPushforward_inverse e).arrows f)

variable (e : C ‚âå D)

lemma functorPushforward_equivalence_eq_pullback {U : C} (S : Sieve U) :
    Sieve.functorPushforward e.inverse (Sieve.functorPushforward e.functor S) =
      Sieve.pullback (e.unitInv.app U) S := by ext; simp

lemma pullback_functorPushforward_equivalence_eq {X : C} (S : Sieve X) :
    Sieve.pullback (e.unit.app X) (Sieve.functorPushforward e.inverse
      (Sieve.functorPushforward e.functor S)) = S := by ext; simp

lemma mem_functorPushforward_iff_of_full [F.Full] {X Y : C} (R : Sieve X) (f : F.obj Y ‚ü∂ F.obj X) :
    (R.arrows.functorPushforward F) f ‚Üî ‚àÉ (g : Y ‚ü∂ X), F.map g = f ‚àß R g := by
  refine ‚ü®fun ‚ü®Z, g, h, hg, hcomp‚ü© ‚Ü¶ ?_, fun ‚ü®g, hcomp, hg‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®h', hh'‚ü© := F.map_surjective h
    use h' ‚â´ g
    simp only [Functor.map_comp, hh', hcomp, true_and]
    apply R.downward_closed hg
  ¬∑ use Y, g, ùüô _, hg
    simp [hcomp]

lemma mem_functorPushforward_iff_of_full_of_faithful [F.Full] [F.Faithful]
    {X Y : C} (R : Sieve X) (f : Y ‚ü∂ X) :
    (R.arrows.functorPushforward F) (F.map f) ‚Üî R f := by
  rw [Sieve.mem_functorPushforward_iff_of_full]
  refine ‚ü®fun ‚ü®g, hcomp, hg‚ü© ‚Ü¶ ?_, fun hf ‚Ü¶ ‚ü®f, rfl, hf‚ü©‚ü©
  rwa [‚Üê F.map_injective hcomp]

end Functor

/-- A sieve induces a presheaf. -/
@[simps]
def functor (S : Sieve X) : C·µí·µñ ‚•§ Type v‚ÇÅ where
  obj Y := { g : Y.unop ‚ü∂ X // S g }
  map f g := ‚ü®f.unop ‚â´ g.1, downward_closed _ g.2 _‚ü©

/-- If a sieve S is contained in a sieve T, then we have a morphism of presheaves on their induced
presheaves.
-/
@[simps]
def natTransOfLe {S T : Sieve X} (h : S ‚â§ T) : S.functor ‚ü∂ T.functor where app _ f := ‚ü®f.1, h _ f.2‚ü©

/-- The natural inclusion from the functor induced by a sieve to the yoneda embedding. -/
@[simps]
def functorInclusion (S : Sieve X) : S.functor ‚ü∂ yoneda.obj X where app _ f := f.1

theorem natTransOfLe_comm {S T : Sieve X} (h : S ‚â§ T) :
    natTransOfLe h ‚â´ functorInclusion _ = functorInclusion _ :=
  rfl

/-- The presheaf induced by a sieve is a subobject of the yoneda embedding. -/
instance functorInclusion_is_mono : Mono S.functorInclusion :=
  ‚ü®fun f g h => by
    ext Y y
    simpa [Subtype.ext_iff] using congr_fun (NatTrans.congr_app h Y) y‚ü©

-- TODO: Show that when `f` is mono, this is right inverse to `functorInclusion` up to isomorphism.
/-- A natural transformation to a representable functor induces a sieve. This is the left inverse of
`functorInclusion`, shown in `sieveOfSubfunctor_functorInclusion`.
-/
@[simps]
def sieveOfSubfunctor {R} (f : R ‚ü∂ yoneda.obj X) : Sieve X where
  arrows Y g := ‚àÉ t, f.app (Opposite.op Y) t = g
  downward_closed := by
    rintro Y Z _ ‚ü®t, rfl‚ü© g
    refine ‚ü®R.map g.op t, ?_‚ü©
    rw [FunctorToTypes.naturality _ _ f]
    simp

theorem sieveOfSubfunctor_functorInclusion : sieveOfSubfunctor S.functorInclusion = S := by
  ext
  simp only [functorInclusion_app, sieveOfSubfunctor_apply]
  constructor
  ¬∑ rintro ‚ü®‚ü®f, hf‚ü©, rfl‚ü©
    exact hf
  ¬∑ intro hf
    exact ‚ü®‚ü®_, hf‚ü©, rfl‚ü©

instance functorInclusion_top_isIso : IsIso (‚ä§ : Sieve X).functorInclusion :=
  ‚ü®‚ü®{ app := fun _ a => ‚ü®a, ‚ü®‚ü©‚ü© }, rfl, rfl‚ü©‚ü©

end Sieve

lemma Presieve.bind_ofArrows_le_bindOfArrows {Œπ : Type*} {X : C} (Z : Œπ ‚Üí C)
    (f : ‚àÄ i, Z i ‚ü∂ X) (R : ‚àÄ i, Presieve (Z i)) :
    Sieve.bind (Sieve.ofArrows Z f)
      (fun _ _ hg ‚Ü¶ Sieve.pullback
        (Sieve.ofArrows.h hg) (.generate <| R (Sieve.ofArrows.i hg))) ‚â§
    Sieve.generate (Presieve.bindOfArrows Z f R) := by
  rintro T g ‚ü®W, v, v', hv', ‚ü®S, u, u', h, hu‚ü©, rfl‚ü©
  rw [‚Üê Sieve.ofArrows.fac hv', ‚Üê reassoc_of% hu]
  exact ‚ü®S, u, u' ‚â´ f _, ‚ü®_, _, h‚ü©, rfl‚ü©

end CategoryTheory
