/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Limits.Shapes.Opposites.Products
public import Mathlib.CategoryTheory.Sites.Coverage
public import Mathlib.CategoryTheory.Sites.Sheaf
public import Mathlib.CategoryTheory.Sites.Hypercover.Zero

/-!
# 1-hypercovers

Given a Grothendieck topology `J` on a category `C`, we define the type of
`1`-hypercovers of an object `S : C`. They consist of a covering family
of morphisms `X i ‚ü∂ S` indexed by a type `I‚ÇÄ` and, for each tuple `(i‚ÇÅ, i‚ÇÇ)`
of elements of `I‚ÇÄ`, a "covering `Y j` of the fibre product of `X i‚ÇÅ` and
`X i‚ÇÇ` over `S`", a condition which is phrased here without assuming that
the fibre product actually exists.

The definition `OneHypercover.isLimitMultifork` shows that if `E` is a
`1`-hypercover of `S`, and `F` is a sheaf, then `F.obj (op S)`
identifies to the multiequalizer of suitable maps
`F.obj (op (E.X i)) ‚ü∂ F.obj (op (E.Y j))`.

-/

@[expose] public section

universe w'' w' w v u

namespace CategoryTheory

open Category Limits

variable {C : Type u} [Category.{v} C] {A : Type*} [Category* A]

/-- The categorical data that is involved in a `1`-hypercover of an object `S`. This
consists of a family of morphisms `f i : X i ‚ü∂ S` for `i : I‚ÇÄ`, and for each
tuple `(i‚ÇÅ, i‚ÇÇ)` of elements in `I‚ÇÄ`, a family of objects `Y j` indexed by
a type `I‚ÇÅ i‚ÇÅ i‚ÇÇ`, which are equipped with a map to the fibre product of `X i‚ÇÅ`
and `X i‚ÇÇ`, which is phrased here as the data of the two projections
`p‚ÇÅ : Y j ‚ü∂ X i‚ÇÅ`, `p‚ÇÇ : Y j ‚ü∂ X i‚ÇÇ` and the relation `p‚ÇÅ j ‚â´ f i‚ÇÅ = p‚ÇÇ j ‚â´ f i‚ÇÇ`.
(See `GrothendieckTopology.OneHypercover` for the topological conditions.) -/
structure PreOneHypercover (S : C) extends PreZeroHypercover.{w} S where
  /-- the index type of the coverings of the fibre products -/
  I‚ÇÅ (i‚ÇÅ i‚ÇÇ : I‚ÇÄ) : Type w
  /-- the objects in the coverings of the fibre products -/
  Y ‚¶Éi‚ÇÅ i‚ÇÇ : I‚ÇÄ‚¶Ñ (j : I‚ÇÅ i‚ÇÅ i‚ÇÇ) : C
  /-- the first projection `Y j ‚ü∂ X i‚ÇÅ` -/
  p‚ÇÅ ‚¶Éi‚ÇÅ i‚ÇÇ : I‚ÇÄ‚¶Ñ (j : I‚ÇÅ i‚ÇÅ i‚ÇÇ) : Y j ‚ü∂ X i‚ÇÅ
  /-- the second projection `Y j ‚ü∂ X i‚ÇÇ` -/
  p‚ÇÇ ‚¶Éi‚ÇÅ i‚ÇÇ : I‚ÇÄ‚¶Ñ (j : I‚ÇÅ i‚ÇÅ i‚ÇÇ) : Y j ‚ü∂ X i‚ÇÇ
  w ‚¶Éi‚ÇÅ i‚ÇÇ : I‚ÇÄ‚¶Ñ (j : I‚ÇÅ i‚ÇÅ i‚ÇÇ) : p‚ÇÅ j ‚â´ f i‚ÇÅ = p‚ÇÇ j ‚â´ f i‚ÇÇ

namespace PreOneHypercover

variable {S : C} (E : PreOneHypercover.{w} S)

/-- Given an object `W` equipped with morphisms `p‚ÇÅ : W ‚ü∂ E.X i‚ÇÅ`, `p‚ÇÇ : W ‚ü∂ E.X i‚ÇÇ`,
this is the sieve of `W` which consists of morphisms `g : Z ‚ü∂ W` such that there exists `j`
and `h : Z ‚ü∂ E.Y j` such that `g ‚â´ p‚ÇÅ = h ‚â´ E.p‚ÇÅ j` and `g ‚â´ p‚ÇÇ = h ‚â´ E.p‚ÇÇ j`.
See lemmas `sieve‚ÇÅ_eq_pullback_sieve‚ÇÅ'` and `sieve‚ÇÅ'_eq_sieve‚ÇÅ` for equational lemmas
regarding this sieve. -/
@[simps]
def sieve‚ÇÅ {i‚ÇÅ i‚ÇÇ : E.I‚ÇÄ} {W : C} (p‚ÇÅ : W ‚ü∂ E.X i‚ÇÅ) (p‚ÇÇ : W ‚ü∂ E.X i‚ÇÇ) : Sieve W where
  arrows Z g := ‚àÉ (j : E.I‚ÇÅ i‚ÇÅ i‚ÇÇ) (h : Z ‚ü∂ E.Y j), g ‚â´ p‚ÇÅ = h ‚â´ E.p‚ÇÅ j ‚àß g ‚â´ p‚ÇÇ = h ‚â´ E.p‚ÇÇ j
  downward_closed := by
    rintro Z Z' g ‚ü®j, h, fac‚ÇÅ, fac‚ÇÇ‚ü© œÜ
    exact ‚ü®j, œÜ ‚â´ h, by simpa using œÜ ‚â´= fac‚ÇÅ, by simpa using œÜ ‚â´= fac‚ÇÇ‚ü©

section

variable {i‚ÇÅ i‚ÇÇ : E.I‚ÇÄ} [HasPullback (E.f i‚ÇÅ) (E.f i‚ÇÇ)]

/-- The obvious morphism `E.Y j ‚ü∂ pullback (E.f i‚ÇÅ) (E.f i‚ÇÇ)` given by `E : PreOneHypercover S`. -/
noncomputable abbrev toPullback (j : E.I‚ÇÅ i‚ÇÅ i‚ÇÇ) [HasPullback (E.f i‚ÇÅ) (E.f i‚ÇÇ)] :
    E.Y j ‚ü∂ pullback (E.f i‚ÇÅ) (E.f i‚ÇÇ) :=
  pullback.lift (E.p‚ÇÅ j) (E.p‚ÇÇ j) (E.w j)

variable (i‚ÇÅ i‚ÇÇ) in
/-- The sieve of `pullback (E.f i‚ÇÅ) (E.f i‚ÇÇ)` given by `E : PreOneHypercover S`. -/
noncomputable def sieve‚ÇÅ' : Sieve (pullback (E.f i‚ÇÅ) (E.f i‚ÇÇ)) :=
  Sieve.ofArrows _ (fun (j : E.I‚ÇÅ i‚ÇÅ i‚ÇÇ) => E.toPullback j)

lemma sieve‚ÇÅ_eq_pullback_sieve‚ÇÅ' {W : C} (p‚ÇÅ : W ‚ü∂ E.X i‚ÇÅ) (p‚ÇÇ : W ‚ü∂ E.X i‚ÇÇ)
    (w : p‚ÇÅ ‚â´ E.f i‚ÇÅ = p‚ÇÇ ‚â´ E.f i‚ÇÇ) :
    E.sieve‚ÇÅ p‚ÇÅ p‚ÇÇ = (E.sieve‚ÇÅ' i‚ÇÅ i‚ÇÇ).pullback (pullback.lift _ _ w) := by
  ext Z g
  constructor
  ¬∑ rintro ‚ü®j, h, fac‚ÇÅ, fac‚ÇÇ‚ü©
    exact ‚ü®_, h, _, ‚ü®j‚ü©, by cat_disch‚ü©
  ¬∑ rintro ‚ü®_, h, w, ‚ü®j‚ü©, fac‚ü©
    exact ‚ü®j, h, by simpa using fac.symm =‚â´ pullback.fst _ _,
      by simpa using fac.symm =‚â´ pullback.snd _ _‚ü©

variable (i‚ÇÅ i‚ÇÇ) in
lemma sieve‚ÇÅ'_eq_sieve‚ÇÅ : E.sieve‚ÇÅ' i‚ÇÅ i‚ÇÇ = E.sieve‚ÇÅ (pullback.fst _ _) (pullback.snd _ _) := by
  rw [‚Üê Sieve.pullback_id (S := E.sieve‚ÇÅ' i‚ÇÅ i‚ÇÇ),
    sieve‚ÇÅ_eq_pullback_sieve‚ÇÅ' _ _ _ pullback.condition]
  congr
  cat_disch

end

/-- The sigma type of all `E.I‚ÇÅ i‚ÇÅ i‚ÇÇ` for `‚ü®i‚ÇÅ, i‚ÇÇ‚ü© : E.I‚ÇÄ √ó E.I‚ÇÄ`. -/
abbrev I‚ÇÅ' : Type w := Sigma (fun (i : E.I‚ÇÄ √ó E.I‚ÇÄ) => E.I‚ÇÅ i.1 i.2)

/-- The `1`-components as a function from the sigma type over `E.I‚ÇÅ i‚ÇÅ i‚ÇÇ`. -/
def Y' (i : E.I‚ÇÅ') : C := E.Y i.2

@[simp]
lemma Y'_apply (i : E.I‚ÇÅ') : E.Y' i = E.Y i.2 := rfl

/-- The shape of the multiforks attached to `E : PreOneHypercover S`. -/
@[simps]
def multicospanShape : MulticospanShape where
  L := E.I‚ÇÄ
  R := E.I‚ÇÅ'
  fst j := j.1.1
  snd j := j.1.2

/-- The diagram of the multifork attached to a presheaf
`F : C·µí·µñ ‚•§ A`, `S : C` and `E : PreOneHypercover S`. -/
@[simps]
def multicospanIndex (F : C·µí·µñ ‚•§ A) : MulticospanIndex E.multicospanShape A where
  left i := F.obj (Opposite.op (E.X i))
  right j := F.obj (Opposite.op (E.Y j.2))
  fst j := F.map ((E.p‚ÇÅ j.2).op)
  snd j := F.map ((E.p‚ÇÇ j.2).op)

/-- The multifork attached to a presheaf `F : C·µí·µñ ‚•§ A`, `S : C` and `E : PreOneHypercover S`. -/
def multifork (F : C·µí·µñ ‚•§ A) :
    Multifork (E.multicospanIndex F) :=
  Multifork.ofŒπ _ (F.obj (Opposite.op S)) (fun i‚ÇÄ => F.map (E.f i‚ÇÄ).op) (by
    rintro ‚ü®‚ü®i‚ÇÅ, i‚ÇÇ‚ü©, (j : E.I‚ÇÅ i‚ÇÅ i‚ÇÇ)‚ü©
    dsimp
    simp only [‚Üê F.map_comp, ‚Üê op_comp, E.w])

lemma multifork_Œπ (F : C·µí·µñ ‚•§ A) (i : E.I‚ÇÄ) : (E.multifork F).Œπ i = F.map (E.f i).op := rfl

/-- The fork associated to a pre-`0`-hypercover induced by taking the coproduct of the
components. -/
@[simps! pt]
def forkOfIsColimit {c : Cofan E.X} (hc : IsColimit c) {d : Cofan E.Y'} (hd : IsColimit d)
    (F : C·µí·µñ ‚•§ A) :
    Fork (F.map (Cofan.IsColimit.desc hd fun _ ‚Ü¶ E.p‚ÇÅ _ ‚â´ c.inj _).op)
      (F.map (Cofan.IsColimit.desc hd fun _ ‚Ü¶ E.p‚ÇÇ _ ‚â´ c.inj _).op) :=
  .ofŒπ (F.map (Cofan.IsColimit.desc hc E.f).op) <| by
    simp_rw [‚Üê Functor.map_comp, ‚Üê op_comp]
    congr 2
    exact Cofan.IsColimit.hom_ext hd _ _ (by simp [E.w])

@[reassoc (attr := simp)]
lemma forkOfIsColimit_Œπ_map_inj {c : Cofan E.X} (hc : IsColimit c) {d : Cofan E.Y'}
    (hd : IsColimit d) (F : C·µí·µñ ‚•§ A) (i : E.I‚ÇÄ) :
    (E.forkOfIsColimit hc hd F).Œπ ‚â´ F.map (c.inj i).op = F.map (E.f i).op := by
  simp [forkOfIsColimit, ‚Üê Functor.map_comp, ‚Üê op_comp]

open Opposite

/-- The multifork associated to a pre-`1`-hypercover is limiting if and only if
the fork induced by taking the coproduct of the components is limiting. -/
noncomputable def isLimitMultiforkEquivIsLimitFork
    {c : Cofan E.X} (hc : IsColimit c) {d : Cofan E.Y'} (hd : IsColimit d) (F : C·µí·µñ ‚•§ A)
    [PreservesLimit (Discrete.functor fun i ‚Ü¶ Opposite.op (E.X i)) F]
    [PreservesLimit (Discrete.functor fun i ‚Ü¶ Opposite.op (E.Y' i)) F] :
    IsLimit (E.multifork F) ‚âÉ IsLimit (E.forkOfIsColimit hc hd F) := by
  letI c' : Fan (E.multicospanIndex F).left := Fan.mk _ fun i ‚Ü¶ F.map (c.inj i).op
  letI hc' : IsLimit c' := isLimitFanMkObjOfIsLimit _ _ (fun i : E.I‚ÇÄ ‚Ü¶ _) (Cofan.IsColimit.op hc)
  letI d' : Fan (E.multicospanIndex F).right := Fan.mk _ fun i ‚Ü¶ F.map (d.inj i).op
  letI hd' : IsLimit d' := isLimitFanMkObjOfIsLimit _ _ (fun i : E.I‚ÇÅ' ‚Ü¶ _) (Cofan.IsColimit.op hd)
  refine (IsLimit.ofConeEquiv <|
    (E.multicospanIndex F).multiforkEquivPiForkOfIsLimit hc' hd').symm.trans ?_
  refine Fork.isLimitEquivOfIsos _ _ (Iso.refl _) (Iso.refl _) (Iso.refl _) ?_ ?_ ?_
  ¬∑ refine Fan.IsLimit.hom_ext hd' _ _ fun i ‚Ü¶ ?_
    simp only [multicospanShape_L, multicospanIndex_right, multicospanShape_R, Iso.refl_hom,
      Y'_apply, id_comp, comp_id]
    rw [MulticospanIndex.fstPiMapOfIsLimit_proj]
    simp [c', d', ‚Üê F.map_comp, ‚Üê op_comp]
  ¬∑ refine Fan.IsLimit.hom_ext hd' _ _ fun i ‚Ü¶ ?_
    simp only [multicospanShape_L, multicospanIndex_right, multicospanShape_R, Iso.refl_hom,
      Y'_apply, id_comp, comp_id]
    rw [MulticospanIndex.sndPiMapOfIsLimit_proj]
    simp [c', d', ‚Üê F.map_comp, ‚Üê op_comp]
  ¬∑ refine Fan.IsLimit.hom_ext hc' _ _ fun i ‚Ü¶ ?_
    simp
    simp [c', multifork_Œπ]

/-- The single object pre-`1`-hypercover obtained from taking coproducts of the components. -/
@[simps toPreZeroHypercover Y]
def sigmaOfIsColimit {c : Cofan E.X} (hc : IsColimit c) {d : Cofan E.Y'} (hd : IsColimit d) :
    PreOneHypercover.{w} S where
  __ := E.toPreZeroHypercover.sigmaOfIsColimit hc
  I‚ÇÅ _ _ := PUnit
  Y _ _ _ := d.pt
  p‚ÇÅ _ _ _ := Cofan.IsColimit.desc hd fun i ‚Ü¶ E.p‚ÇÅ _ ‚â´ c.inj _
  p‚ÇÇ _ _ _ := Cofan.IsColimit.desc hd fun i ‚Ü¶ E.p‚ÇÇ _ ‚â´ c.inj _
  w _ _ _ := Cofan.IsColimit.hom_ext hd _ _ (by simp [E.w])

@[reassoc (attr := simp)]
lemma p‚ÇÅ_sigmaOfIsColimit {c : Cofan E.X} (hc : IsColimit c) {d : Cofan E.Y'} (hd : IsColimit d)
    (i : E.I‚ÇÅ') {a b : PUnit} (r : (E.sigmaOfIsColimit hc hd).I‚ÇÅ a b) :
    d.inj i ‚â´ (E.sigmaOfIsColimit hc hd).p‚ÇÅ r = E.p‚ÇÅ _ ‚â´ c.inj _ := by
  simp [sigmaOfIsColimit]

@[reassoc (attr := simp)]
lemma p‚ÇÇ_sigmaOfIsColimit {c : Cofan E.X} (hc : IsColimit c) {d : Cofan E.Y'} (hd : IsColimit d)
    (i : E.I‚ÇÅ') {a b : PUnit} (r : (E.sigmaOfIsColimit hc hd).I‚ÇÅ a b) :
    d.inj i ‚â´ (E.sigmaOfIsColimit hc hd).p‚ÇÇ r = E.p‚ÇÇ _ ‚â´ c.inj _ := by
  simp [sigmaOfIsColimit]

instance {c : Cofan E.X} (hc : IsColimit c) {d : Cofan E.Y'} (hd : IsColimit d) :
    Unique (E.sigmaOfIsColimit hc hd).multicospanShape.L :=
  inferInstanceAs <| Unique PUnit

instance {c : Cofan E.X} (hc : IsColimit c) {d : Cofan E.Y'} (hd : IsColimit d) :
    Unique (E.sigmaOfIsColimit hc hd).multicospanShape.R where
  default := ‚ü®(‚ü®‚ü©, ‚ü®‚ü©), ‚ü®‚ü©‚ü©
  uniq _ := rfl

/-- If `E` is a pre-`1`-hypercover and `F` a presheaf, the induced equalizer of
the single object covering obtained from `E` by taking coproducts is limiting
if and only if the induced multiequalizer of `E` is limiting. -/
noncomputable
def isLimitSigmaOfIsColimitEquiv {c : Cofan E.X} (hc : IsColimit c) {d : Cofan E.Y'}
    (hd : IsColimit d) (F : C·µí·µñ ‚•§ A)
    [PreservesLimit (Discrete.functor fun i ‚Ü¶ Opposite.op (E.X i)) F]
    [PreservesLimit (Discrete.functor fun i ‚Ü¶ Opposite.op (E.Y' i)) F] :
    IsLimit ((E.sigmaOfIsColimit hc hd).multifork F) ‚âÉ IsLimit (E.multifork F) := by
  refine (Multifork.isLimitEquivOfIsos _ _ ?_ ?_ ?_ ?_ ?_ ?_).trans
    (IsLimit.ofConeEquiv <| (MulticospanIndex.multiforkOfParallelHomsEquivFork
      (E.sigmaOfIsColimit hc hd).multicospanShape _ _).symm) |>.trans
      (E.isLimitMultiforkEquivIsLimitFork hc hd F).symm
  ¬∑ exact .refl _
  ¬∑ exact fun _ ‚Ü¶ .refl _
  ¬∑ exact fun _ ‚Ü¶ .refl _
  all_goals cat_disch

/-- The trivial pre-`1`-hypercover of `S` with a single component `S`. -/
@[simps toPreZeroHypercover I‚ÇÅ Y p‚ÇÅ p‚ÇÇ]
def trivial (S : C) : PreOneHypercover.{w} S where
  __ := PreZeroHypercover.singleton (ùüô S)
  I‚ÇÅ _ _ := PUnit
  Y _ _ _ := S
  p‚ÇÅ _ _ _ := ùüô _
  p‚ÇÇ _ _ _ := ùüô _
  w _ _ _ := by simp

lemma sieve‚ÇÄ_trivial (S : C) : (trivial S).sieve‚ÇÄ = ‚ä§ := by
  rw [PreZeroHypercover.sieve‚ÇÄ, Sieve.ofArrows, ‚Üê PreZeroHypercover.presieve‚ÇÄ]
  simp

@[simp]
lemma sieve‚ÇÅ_trivial {S : C} {W : C} {p : W ‚ü∂ S} :
    (trivial S).sieve‚ÇÅ (i‚ÇÅ := ‚ü®‚ü©) (i‚ÇÇ := ‚ü®‚ü©) p p = ‚ä§ := by ext; simp

instance : Nonempty (PreOneHypercover.{w} S) := ‚ü®trivial S‚ü©

section

/-- Intersection of two pre-`1`-hypercovers. -/
@[simps toPreZeroHypercover I‚ÇÅ Y p‚ÇÅ p‚ÇÇ]
noncomputable
def inter (E F : PreOneHypercover S) [‚àÄ i j, HasPullback (E.f i) (F.f j)]
    [‚àÄ (i j : E.I‚ÇÄ) (k : E.I‚ÇÅ i j) (a b : F.I‚ÇÄ) (l : F.I‚ÇÅ a b),
      HasPullback (E.p‚ÇÅ k ‚â´ E.f i) (F.p‚ÇÅ l ‚â´ F.f a)] :
    PreOneHypercover S where
  __ := E.toPreZeroHypercover.inter F.toPreZeroHypercover
  I‚ÇÅ i j := E.I‚ÇÅ i.1 j.1 √ó F.I‚ÇÅ i.2 j.2
  Y i j k := pullback (E.p‚ÇÅ k.1 ‚â´ E.f _) (F.p‚ÇÅ k.2 ‚â´ F.f _)
  p‚ÇÅ i j k := pullback.map _ _ _ _ (E.p‚ÇÅ _) (F.p‚ÇÅ _) (ùüô S) (by simp) (by simp)
  p‚ÇÇ i j k := pullback.map _ _ _ _ (E.p‚ÇÇ _) (F.p‚ÇÇ _) (ùüô S) (by simp [E.w]) (by simp [F.w])
  w := by simp [E.w]

variable {E} {F : PreOneHypercover S}

lemma sieve‚ÇÅ_inter [HasPullbacks C] {i j : E.I‚ÇÄ √ó F.I‚ÇÄ} {W : C}
    {p‚ÇÅ : W ‚ü∂ pullback (E.f i.1) (F.f i.2)}
    {p‚ÇÇ : W ‚ü∂ pullback (E.f j.1) (F.f j.2)}
    (w : p‚ÇÅ ‚â´ pullback.fst _ _ ‚â´ E.f _ = p‚ÇÇ ‚â´ pullback.fst _ _ ‚â´ E.f _) :
    (inter E F).sieve‚ÇÅ p‚ÇÅ p‚ÇÇ = Sieve.bind
      (E.sieve‚ÇÅ (p‚ÇÅ ‚â´ pullback.fst _ _) (p‚ÇÇ ‚â´ pullback.fst _ _))
      (fun _ f _ ‚Ü¶ (F.sieve‚ÇÅ (p‚ÇÅ ‚â´ pullback.snd _ _) (p‚ÇÇ ‚â´ pullback.snd _ _)).pullback f) := by
  ext Y f
  let p : W ‚ü∂ pullback ((inter E F).f i) ((inter E F).f j) :=
    pullback.lift p‚ÇÅ p‚ÇÇ w
  refine ‚ü®fun ‚ü®k, a, h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ ?_, fun ‚ü®Z, a, b, ‚ü®k, e, h‚ÇÅ, h‚ÇÇ‚ü©, ‚ü®l, u, u‚ÇÅ, u‚ÇÇ‚ü©, hab‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ refine ‚ü®pullback p ((E.inter F).toPullback k), pullback.lift f a ?_,
        pullback.fst _ _, ?_, ?_, ?_‚ü©
    ¬∑ apply pullback.hom_ext
      ¬∑ apply pullback.hom_ext <;> simp [p, h‚ÇÅ, toPullback]
      ¬∑ apply pullback.hom_ext <;> simp [p, h‚ÇÇ, toPullback]
    ¬∑ refine ‚ü®k.1, pullback.snd _ _ ‚â´ pullback.fst _ _, ?_, ?_‚ü©
      ¬∑ have : p‚ÇÅ ‚â´ pullback.fst (E.f i.1) (F.f i.2) = p ‚â´ pullback.fst _ _ ‚â´ pullback.fst _ _ := by
          simp [p]
        simp [this, pullback.condition_assoc, toPullback]
      ¬∑ have : p‚ÇÇ ‚â´ pullback.fst (E.f j.1) (F.f j.2) = p ‚â´ pullback.snd _ _ ‚â´ pullback.fst _ _ := by
          simp [p]
        simp [this, pullback.condition_assoc, toPullback]
    ¬∑ exact ‚ü®k.2, a ‚â´ pullback.snd _ _, by simp [reassoc_of% h‚ÇÅ], by simp [reassoc_of% h‚ÇÇ]‚ü©
    ¬∑ simp
  ¬∑ subst hab
    refine ‚ü®(k, l), pullback.lift (a ‚â´ e) u ?_, ?_, ?_‚ü©
    ¬∑ simp only [Category.assoc] at u‚ÇÅ
      simp [‚Üê reassoc_of% h‚ÇÅ, w, ‚Üê reassoc_of% u‚ÇÅ, ‚Üê pullback.condition]
    ¬∑ apply pullback.hom_ext
      ¬∑ simp [h‚ÇÅ]
      ¬∑ simpa using u‚ÇÅ
    ¬∑ apply pullback.hom_ext
      ¬∑ simp [h‚ÇÇ]
      ¬∑ simpa using u‚ÇÇ

end

section Category

/-- A morphism of pre-`1`-hypercovers of `S` is a family of refinement morphisms commuting
with the structure morphisms of `E` and `F`. -/
@[ext]
structure Hom (E F : PreOneHypercover S) extends
    E.toPreZeroHypercover.Hom F.toPreZeroHypercover where
  /-- The map between indexing types of the coverings of the fibre products over `S`. -/
  s‚ÇÅ {i j : E.I‚ÇÄ} (k : E.I‚ÇÅ i j) : F.I‚ÇÅ (s‚ÇÄ i) (s‚ÇÄ j)
  /-- The refinement morphisms between objects in the coverings of the fibre products over `S`. -/
  h‚ÇÅ {i j : E.I‚ÇÄ} (k : E.I‚ÇÅ i j) : E.Y k ‚ü∂ F.Y (s‚ÇÅ k)
  w‚ÇÅ‚ÇÅ {i j : E.I‚ÇÄ} (k : E.I‚ÇÅ i j) : h‚ÇÅ k ‚â´ F.p‚ÇÅ (s‚ÇÅ k) = E.p‚ÇÅ k ‚â´ h‚ÇÄ i := by cat_disch
  w‚ÇÅ‚ÇÇ {i j : E.I‚ÇÄ} (k : E.I‚ÇÅ i j) : h‚ÇÅ k ‚â´ F.p‚ÇÇ (s‚ÇÅ k) = E.p‚ÇÇ k ‚â´ h‚ÇÄ j := by cat_disch

attribute [reassoc] Hom.w‚ÇÅ‚ÇÅ Hom.w‚ÇÅ‚ÇÇ

/-- The identity refinement of a pre-`1`-hypercover. -/
@[simps!]
def Hom.id (E : PreOneHypercover S) : Hom E E where
  __ := PreZeroHypercover.Hom.id _
  s‚ÇÅ := _root_.id
  h‚ÇÅ _ := ùüô _

variable {E : PreOneHypercover.{w} S} {F : PreOneHypercover.{w'} S}
  {G : PreOneHypercover S}

/-- Composition of refinement morphisms of pre-`1`-hypercovers. -/
@[simps!]
def Hom.comp (f : E.Hom F) (g : F.Hom G) : E.Hom G where
  __ := PreZeroHypercover.Hom.comp _ _
  s‚ÇÅ := g.s‚ÇÅ ‚àò f.s‚ÇÅ
  h‚ÇÅ i := f.h‚ÇÅ i ‚â´ g.h‚ÇÅ _
  w‚ÇÅ‚ÇÅ := by simp [w‚ÇÅ‚ÇÅ, w‚ÇÅ‚ÇÅ_assoc]
  w‚ÇÅ‚ÇÇ := by simp [w‚ÇÅ‚ÇÇ, w‚ÇÅ‚ÇÇ_assoc]

/-- The induced index map `E.I‚ÇÅ' ‚Üí F.I‚ÇÅ'` from a refinement morphism `E ‚ü∂ F`. -/
@[simp]
def Hom.s‚ÇÅ' (f : E.Hom F) (k : E.I‚ÇÅ') : F.I‚ÇÅ' :=
  ‚ü®‚ü®f.s‚ÇÄ k.1.1, f.s‚ÇÄ k.1.2‚ü©, f.s‚ÇÅ k.2‚ü©

@[simps! id_s‚ÇÄ id_s‚ÇÅ id_h‚ÇÄ id_h‚ÇÅ comp_s‚ÇÄ comp_s‚ÇÅ comp_h‚ÇÄ comp_h‚ÇÅ]
instance : Category (PreOneHypercover S) where
  Hom := Hom
  id E := Hom.id E
  comp f g := f.comp g

/-- The forgetful functor from pre-`1`-hypercovers to pre-`0`-hypercovers. -/
@[simps]
def oneToZero : PreOneHypercover.{w} S ‚•§ PreZeroHypercover.{w} S where
  obj f := f.1
  map f := f.1

/-- A refinement morphism `E ‚ü∂ F` induces a morphism on associated multiequalizers. -/
def Hom.mapMultiforkOfIsLimit (f : E.Hom F) (P : C·µí·µñ ‚•§ A) {c : Multifork (E.multicospanIndex P)}
    (hc : IsLimit c) (d : Multifork (F.multicospanIndex P)) :
    d.pt ‚ü∂ c.pt :=
  Multifork.IsLimit.lift hc (fun a ‚Ü¶ d.Œπ (f.s‚ÇÄ a) ‚â´ P.map (f.h‚ÇÄ a).op) <| by
    intro (k : E.I‚ÇÅ')
    simp only [multicospanIndex_right, multicospanShape_fst, multicospanIndex_left,
      multicospanIndex_fst, assoc, multicospanShape_snd, multicospanIndex_snd]
    have heq := d.condition (f.s‚ÇÅ' k)
    simp only [Hom.s‚ÇÅ', multicospanIndex_right, multicospanShape_fst, multicospanIndex_left,
      multicospanIndex_fst, multicospanShape_snd, multicospanIndex_snd] at heq
    rw [‚Üê Functor.map_comp, ‚Üê op_comp, ‚Üê Hom.w‚ÇÅ‚ÇÅ, ‚Üê Functor.map_comp, ‚Üê op_comp, ‚Üê Hom.w‚ÇÅ‚ÇÇ]
    rw [op_comp, Functor.map_comp, reassoc_of% heq, op_comp, Functor.map_comp]

@[reassoc (attr := simp)]
lemma Hom.mapMultiforkOfIsLimit_Œπ {E F : PreOneHypercover.{w} S}
    (f : E.Hom F) (P : C·µí·µñ ‚•§ A) {c : Multifork (E.multicospanIndex P)} (hc : IsLimit c)
    (d : Multifork (F.multicospanIndex P)) (a : E.I‚ÇÄ) :
    f.mapMultiforkOfIsLimit P hc d ‚â´ c.Œπ a = d.Œπ (f.s‚ÇÄ a) ‚â´ P.map (f.h‚ÇÄ a).op := by
  simp [mapMultiforkOfIsLimit]

end Category

section

variable (F : PreOneHypercover.{w'} S) {G : PreOneHypercover.{w''} S}
  [‚àÄ (i : E.I‚ÇÄ) (j : F.I‚ÇÄ), HasPullback (E.f i) (F.f j)]
  [‚àÄ (i j : E.I‚ÇÄ) (k : E.I‚ÇÅ i j) (a b : F.I‚ÇÄ) (l : F.I‚ÇÅ a b),
    HasPullback (E.p‚ÇÅ k ‚â´ E.f i) (F.p‚ÇÅ l ‚â´ F.f a)]

/-- First projection from the intersection of two pre-`1`-hypercovers. -/
@[simps toHom s‚ÇÅ]
noncomputable
def interFst : (E.inter F).Hom E where
  __ := E.toPreZeroHypercover.interFst F.toPreZeroHypercover
  s‚ÇÅ {i j} k := k.1
  h‚ÇÅ _ := pullback.fst _ _

/-- Second projection from the intersection of two pre-`1`-hypercovers. -/
@[simps toHom s‚ÇÅ]
noncomputable
def interSnd : (E.inter F).Hom F where
  __ := E.toPreZeroHypercover.interSnd F.toPreZeroHypercover
  s‚ÇÅ {i j} k := k.2
  h‚ÇÅ _ := pullback.snd _ _

variable {E F} in
/-- Universal property of the intersection of two pre-`1`-hypercovers. -/
noncomputable
def interLift {G : PreOneHypercover.{w''} S} (f : G.Hom E) (g : G.Hom F) :
    G.Hom (E.inter F) where
  __ := PreZeroHypercover.interLift f.toHom g.toHom
  s‚ÇÅ {i j} k := ‚ü®f.s‚ÇÅ k, g.s‚ÇÅ k‚ü©
  h‚ÇÅ k := pullback.lift (f.h‚ÇÅ k) (g.h‚ÇÅ k) <| by
    rw [f.w‚ÇÅ‚ÇÅ_assoc k, g.w‚ÇÅ‚ÇÅ_assoc k]
    simp
  w‚ÇÄ := by simp
  w‚ÇÅ‚ÇÅ k := by
    apply pullback.hom_ext
    ¬∑ simpa using f.w‚ÇÅ‚ÇÅ k
    ¬∑ simpa using g.w‚ÇÅ‚ÇÅ k
  w‚ÇÅ‚ÇÇ k := by
    apply pullback.hom_ext
    ¬∑ simpa using f.w‚ÇÅ‚ÇÇ k
    ¬∑ simpa using g.w‚ÇÅ‚ÇÇ k

end

end PreOneHypercover

namespace GrothendieckTopology

variable (J : GrothendieckTopology C)

/-- The type of `1`-hypercovers of an object `S : C` in a category equipped with a
Grothendieck topology `J`. This can be constructed from a covering of `S` and
a covering of the fibre products of the objects in this covering (see `OneHypercover.mk'`). -/
structure OneHypercover (S : C) extends PreOneHypercover.{w} S where
  mem‚ÇÄ : toPreOneHypercover.sieve‚ÇÄ ‚àà J S
  mem‚ÇÅ (i‚ÇÅ i‚ÇÇ : I‚ÇÄ) ‚¶ÉW : C‚¶Ñ (p‚ÇÅ : W ‚ü∂ X i‚ÇÅ) (p‚ÇÇ : W ‚ü∂ X i‚ÇÇ) (w : p‚ÇÅ ‚â´ f i‚ÇÅ = p‚ÇÇ ‚â´ f i‚ÇÇ) :
    toPreOneHypercover.sieve‚ÇÅ p‚ÇÅ p‚ÇÇ ‚àà J W

variable {J}

lemma OneHypercover.mem_sieve‚ÇÅ' {S : C} (E : J.OneHypercover S)
    (i‚ÇÅ i‚ÇÇ : E.I‚ÇÄ) [HasPullback (E.f i‚ÇÅ) (E.f i‚ÇÇ)] :
    E.sieve‚ÇÅ' i‚ÇÅ i‚ÇÇ ‚àà J _ := by
  rw [E.sieve‚ÇÅ'_eq_sieve‚ÇÅ]
  exact mem‚ÇÅ _ _ _ _ _ pullback.condition

namespace OneHypercover

/-- In order to check that a certain data is a `1`-hypercover of `S`, it suffices to
check that the data provides a covering of `S` and of the fibre products. -/
@[simps toPreOneHypercover]
def mk' {S : C} (E : PreOneHypercover S) [E.HasPullbacks]
    (mem‚ÇÄ : E.sieve‚ÇÄ ‚àà J S) (mem‚ÇÅ' : ‚àÄ (i‚ÇÅ i‚ÇÇ : E.I‚ÇÄ), E.sieve‚ÇÅ' i‚ÇÅ i‚ÇÇ ‚àà J _) :
    J.OneHypercover S where
  toPreOneHypercover := E
  mem‚ÇÄ := mem‚ÇÄ
  mem‚ÇÅ i‚ÇÅ i‚ÇÇ W p‚ÇÅ p‚ÇÇ w := by
    rw [E.sieve‚ÇÅ_eq_pullback_sieve‚ÇÅ' _ _ w]
    exact J.pullback_stable' _ (mem‚ÇÅ' i‚ÇÅ i‚ÇÇ)

section

variable {S : C} (E : J.OneHypercover S) (F : Sheaf J A)

section

variable {E F}
variable (c : Multifork (E.multicospanIndex F.val))

/-- Auxiliary definition of `isLimitMultifork`. -/
noncomputable def multiforkLift : c.pt ‚ü∂ F.val.obj (Opposite.op S) :=
  F.cond.amalgamateOfArrows _ E.mem‚ÇÄ c.Œπ (fun W i‚ÇÅ i‚ÇÇ p‚ÇÅ p‚ÇÇ w => by
    apply F.cond.hom_ext ‚ü®_, E.mem‚ÇÅ _ _ _ _ w‚ü©
    rintro ‚ü®T, g, j, h, fac‚ÇÅ, fac‚ÇÇ‚ü©
    dsimp
    simp only [assoc, ‚Üê Functor.map_comp, ‚Üê op_comp, fac‚ÇÅ, fac‚ÇÇ]
    simp only [op_comp, Functor.map_comp]
    simpa using c.condition ‚ü®‚ü®i‚ÇÅ, i‚ÇÇ‚ü©, j‚ü© =‚â´ F.val.map h.op)

@[reassoc]
lemma multiforkLift_map (i‚ÇÄ : E.I‚ÇÄ) : multiforkLift c ‚â´ F.val.map (E.f i‚ÇÄ).op = c.Œπ i‚ÇÄ := by
  simp [multiforkLift]

end

/-- If `E : J.OneHypercover S` and `F : Sheaf J A`, then `F.obj (op S)` is
a multiequalizer of suitable maps `F.obj (op (E.X i)) ‚ü∂ F.obj (op (E.Y j))`
induced by `E.p‚ÇÅ j` and `E.p‚ÇÇ j`. -/
noncomputable def isLimitMultifork : IsLimit (E.multifork F.1) :=
  Multifork.IsLimit.mk _ (fun c => multiforkLift c) (fun c => multiforkLift_map c) (by
    intro c m hm
    apply F.cond.hom_ext_ofArrows _ E.mem‚ÇÄ
    intro i‚ÇÄ
    dsimp only
    rw [multiforkLift_map]
    exact hm i‚ÇÄ)

end

section

variable {S : C}

/-- Forget the `1`-components of a `OneHypercover`. -/
@[simps toPreZeroHypercover]
def toZeroHypercover (E : OneHypercover.{w} J S) : J.toPrecoverage.ZeroHypercover S where
  __ := E.toPreZeroHypercover
  mem‚ÇÄ := E.mem‚ÇÄ

variable (J) in
/-- The trivial `1`-hypercover of `S` where a single component `S`. -/
@[simps toPreOneHypercover]
def trivial (S : C) : OneHypercover.{w} J S where
  __ := PreOneHypercover.trivial S
  mem‚ÇÄ := by simp only [PreOneHypercover.sieve‚ÇÄ_trivial, J.top_mem]
  mem‚ÇÅ _ _ _ _ _ h := by
    simp only [PreOneHypercover.trivial_toPreZeroHypercover, PreZeroHypercover.singleton_X,
      PreZeroHypercover.singleton_f, Category.comp_id] at h
    subst h
    simp

instance (S : C) : Nonempty (J.OneHypercover S) := ‚ü®trivial J S‚ü©

/-- Intersection of two `1`-hypercovers. -/
@[simps toPreOneHypercover]
noncomputable
def inter [HasPullbacks C] (E F : J.OneHypercover S)
    [‚àÄ (i : E.I‚ÇÄ) (j : F.I‚ÇÄ), HasPullback (E.f i) (F.f j)]
    [‚àÄ (i j : E.I‚ÇÄ) (k : E.I‚ÇÅ i j) (a b : F.I‚ÇÄ) (l : F.I‚ÇÅ a b),
      HasPullback (E.p‚ÇÅ k ‚â´ E.f i) (F.p‚ÇÅ l ‚â´ F.f a)] : J.OneHypercover S where
  __ := E.toPreOneHypercover.inter F.toPreOneHypercover
  mem‚ÇÄ := (E.toZeroHypercover.inter F.toZeroHypercover).mem‚ÇÄ
  mem‚ÇÅ i‚ÇÅ i‚ÇÇ W p‚ÇÅ p‚ÇÇ h := by
    rw [PreOneHypercover.sieve‚ÇÅ_inter h]
    refine J.bind_covering (E.mem‚ÇÅ _ _ _ _ (by simpa using h)) fun _ _ _ ‚Ü¶ ?_
    exact J.pullback_stable _
      (F.mem‚ÇÅ _ _ _ _ (by simpa [Category.assoc, ‚Üê pullback.condition]))

end

section Category

variable {S : C} {E : OneHypercover.{w} J S} {F : OneHypercover.{w'} J S}

/-- A morphism of `1`-hypercovers is a morphism of the underlying pre-`1`-hypercovers. -/
abbrev Hom (E : OneHypercover.{w} J S) (F : OneHypercover.{w'} J S) :=
  E.toPreOneHypercover.Hom F.toPreOneHypercover

@[simps! id_s‚ÇÄ id_s‚ÇÅ id_h‚ÇÄ id_h‚ÇÅ comp_s‚ÇÄ comp_s‚ÇÅ comp_h‚ÇÄ comp_h‚ÇÅ]
instance : Category (J.OneHypercover S) where
  Hom := Hom
  id E := PreOneHypercover.Hom.id E.toPreOneHypercover
  comp f g := f.comp g

/-- An isomorphism of `1`-hypercovers is an isomorphism of pre-`1`-hypercovers. -/
@[simps]
def isoMk {E F : J.OneHypercover S} (f : E.toPreOneHypercover ‚âÖ F.toPreOneHypercover) :
    E ‚âÖ F where
  __ := f

end Category

section

open Opposite
variable {C : Type*} [Category* C] {K : GrothendieckTopology C} {P : C·µí·µñ ‚•§ Type*}
   {S : C} (E : K.OneHypercover S)

lemma isSheafFor_presieve‚ÇÄ (h : Presieve.IsSheaf K P) : E.presieve‚ÇÄ.IsSheafFor P := by
  rw [Presieve.isSheafFor_iff_generate]
  exact h _ E.mem‚ÇÄ

lemma arrowsCompatible (h : Presieve.IsSeparated K P) (x : ‚àÄ i, P.obj (op <| E.X i))
    (hc : ‚àÄ ‚¶Éi j : E.I‚ÇÄ‚¶Ñ (k : E.I‚ÇÅ i j), P.map (E.p‚ÇÅ k).op (x i) = P.map (E.p‚ÇÇ k).op (x j)) :
    Presieve.Arrows.Compatible _ E.f x := by
  rintro i‚ÇÅ i‚ÇÇ Z g‚ÇÅ g‚ÇÇ heq
  refine (h _ (E.mem‚ÇÅ _ _ _ _ heq)).ext fun W f ‚ü®T, u, h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ ?_
  rw [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, h‚ÇÅ]
  conv_rhs => rw [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, h‚ÇÇ]
  simp [hc]

/-- Glue sections of a `Type`-valued sheaf over a `1`-hypercover. -/
noncomputable def amalgamate (h : Presieve.IsSheaf K P) (x : ‚àÄ i, P.obj (op <| E.X i))
    (hc : ‚àÄ ‚¶Éi j : E.I‚ÇÄ‚¶Ñ (k : E.I‚ÇÅ i j), P.map (E.p‚ÇÅ k).op (x i) = P.map (E.p‚ÇÇ k).op (x j)) :
    P.obj (op S) :=
  (E.isSheafFor_presieve‚ÇÄ h).amalgamate _
    ((E.arrowsCompatible h.isSeparated x hc).familyOfElements_compatible)

@[simp]
lemma map_amalgamate (h : Presieve.IsSheaf K P) (x : ‚àÄ i, P.obj (op <| E.X i))
    (hc : ‚àÄ ‚¶Éi j : E.I‚ÇÄ‚¶Ñ (k : E.I‚ÇÅ i j), P.map (E.p‚ÇÅ k).op (x i) = P.map (E.p‚ÇÇ k).op (x j))
    (i : E.I‚ÇÄ) :
    P.map (E.f i).op (E.amalgamate h x hc) = x i := by
  rw [amalgamate, Presieve.IsSheafFor.valid_glue _ _ _ ‚ü®i‚ü©]
  simp

end

end OneHypercover

namespace Cover

variable {X : C} (S : J.Cover X)

/-- The tautological 1-pre-hypercover induced by `S : J.Cover X`. Its index type `I‚ÇÄ`
is given by `S.Arrow` (i.e. all the morphisms in the sieve `S`), while `I‚ÇÅ` is given
by all possible pullback cones. -/
@[simps]
def preOneHypercover : PreOneHypercover.{max u v} X where
  I‚ÇÄ := S.Arrow
  X f := f.Y
  f f := f.f
  I‚ÇÅ f‚ÇÅ f‚ÇÇ := f‚ÇÅ.Relation f‚ÇÇ
  Y _ _ r := r.Z
  p‚ÇÅ _ _ r := r.g‚ÇÅ
  p‚ÇÇ _ _ r := r.g‚ÇÇ
  w _ _ r := r.w

@[simp]
lemma preOneHypercover_sieve‚ÇÄ : S.preOneHypercover.sieve‚ÇÄ = S.1 := by
  ext Y f
  constructor
  ¬∑ rintro ‚ü®_, _, _, ‚ü®g‚ü©, rfl‚ü©
    exact S.1.downward_closed g.hf _
  ¬∑ intro hf
    exact Sieve.ofArrows_mk _ _ ({ hf := hf, .. } : S.Arrow)

lemma preOneHypercover_sieve‚ÇÅ (f‚ÇÅ f‚ÇÇ : S.Arrow) {W : C} (p‚ÇÅ : W ‚ü∂ f‚ÇÅ.Y) (p‚ÇÇ : W ‚ü∂ f‚ÇÇ.Y)
    (w : p‚ÇÅ ‚â´ f‚ÇÅ.f = p‚ÇÇ ‚â´ f‚ÇÇ.f) :
    S.preOneHypercover.sieve‚ÇÅ p‚ÇÅ p‚ÇÇ = ‚ä§ := by
  ext Y f
  simp only [Sieve.top_apply, iff_true]
  exact ‚ü®{ w := w, .. }, f, rfl, rfl‚ü©

/-- The tautological 1-hypercover induced by `S : J.Cover X`. Its index type `I‚ÇÄ`
is given by `S.Arrow` (i.e. all the morphisms in the sieve `S`), while `I‚ÇÅ` is given
by all possible pullback cones. -/
@[simps toPreOneHypercover]
def oneHypercover : J.OneHypercover X where
  toPreOneHypercover := S.preOneHypercover
  mem‚ÇÄ := by simp
  mem‚ÇÅ f‚ÇÅ f‚ÇÇ _ p‚ÇÅ p‚ÇÇ w := by simp [S.preOneHypercover_sieve‚ÇÅ f‚ÇÅ f‚ÇÇ p‚ÇÅ p‚ÇÇ w]

end Cover

end GrothendieckTopology

lemma PreZeroHypercover.ext_of_isSeparatedFor {P : C·µí·µñ ‚•§ Type*} {S : C} (E : PreZeroHypercover S)
    (h : E.presieve‚ÇÄ.IsSeparatedFor P) {x y : P.obj (.op S)}
    (hi : ‚àÄ i, P.map (E.f i).op x = P.map (E.f i).op y) :
    x = y :=
  h.ext fun _ _ ‚ü®i‚ü© ‚Ü¶ hi i

/-- If the pairwise pullbacks exist, this is the pre-`1`-hypercover where the covers
by the pullbacks are given by the pullbacks themselves. -/
@[simps toPreZeroHypercover I‚ÇÅ Y p‚ÇÅ p‚ÇÇ]
noncomputable def PreZeroHypercover.toPreOneHypercover {S : C} (E : PreZeroHypercover S)
    [E.HasPullbacks] :
    PreOneHypercover S where
  __ := E
  I‚ÇÅ _ _ := PUnit
  Y i j _ := pullback (E.f i) (E.f j)
  p‚ÇÅ _ _ _ := pullback.fst _ _
  p‚ÇÇ _ _ _ := pullback.snd _ _
  w _ _ _ := pullback.condition

instance {S : C} (E : PreZeroHypercover S) [E.HasPullbacks] :
    E.toPreOneHypercover.HasPullbacks := by
  dsimp
  infer_instance

@[simp]
lemma sieve‚ÇÅ'_toPreOneHypercover_eq_top {S : C} (E : PreZeroHypercover S) [E.HasPullbacks]
    (i j : E.I‚ÇÄ) :
    E.toPreOneHypercover.sieve‚ÇÅ' i j = ‚ä§ := by
  rw [eq_top_iff]
  intro Y f _
  refine ‚ü®pullback (E.f i) (E.f j), f, ùüô _, ?_, by simp‚ü©
  refine Presieve.ofArrows.mk' ‚ü®‚ü© rfl ?_
  apply pullback.hom_ext <;> simp [PreOneHypercover.toPullback]

/-- If the pairwise pullbacks exist, this is the pre-`1`-hypercover where the covers
by the pullbacks are given by the pullbacks themselves. -/
@[simps! toPreOneHypercover]
noncomputable def Precoverage.ZeroHypercover.toOneHypercover {J : Precoverage C}
    {S : C} (E : J.ZeroHypercover S) [E.HasPullbacks] :
    (J.toGrothendieck).OneHypercover S :=
  .mk' E.toPreZeroHypercover.toPreOneHypercover (J.generate_mem_toGrothendieck E.mem‚ÇÄ) (by simp)

end CategoryTheory
