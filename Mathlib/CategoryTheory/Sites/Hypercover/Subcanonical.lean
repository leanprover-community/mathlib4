/-
Copyright (c) 2026 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
module

public import Mathlib.CategoryTheory.Sites.Canonical
public import Mathlib.CategoryTheory.Sites.Hypercover.One
public import Mathlib.CategoryTheory.MorphismProperty.Local

/-!
# Covers in subcanonical topologies

In this file we provide API related to covers in subcanonical topologies.
-/

@[expose] public section

universe v u

namespace CategoryTheory

open Limits

variable {C : Type u} [Category.{v} C]

namespace GrothendieckTopology.OneHypercover

variable {J : GrothendieckTopology C} [J.Subcanonical]

/-- Glue a family of morphisms along a `1`-hypercover for a subcanonical topology. -/
noncomputable def glueMorphisms {S T : C} (E : J.OneHypercover S) (f : âˆ€ i, E.X i âŸ¶ T)
    (h : âˆ€ â¦ƒi j : E.Iâ‚€â¦„ (k : E.Iâ‚ i j), E.pâ‚ k â‰« f i = E.pâ‚‚ k â‰« f j) :
    S âŸ¶ T :=
  E.amalgamate (Subcanonical.isSheaf_of_isRepresentable (CategoryTheory.yoneda.obj T)) f h

variable {S T : C} (E : J.OneHypercover S) (f : âˆ€ i, E.X i âŸ¶ T)
  (h : âˆ€ â¦ƒi j : E.Iâ‚€â¦„ (k : E.Iâ‚ i j), E.pâ‚ k â‰« f i = E.pâ‚‚ k â‰« f j)

@[reassoc (attr := simp)]
lemma f_glueMorphisms (i : E.Iâ‚€) : E.f i â‰« E.glueMorphisms f h = f i :=
  E.map_amalgamate (Subcanonical.isSheaf_of_isRepresentable (CategoryTheory.yoneda.obj T)) _ _ i

end GrothendieckTopology.OneHypercover

namespace Precoverage.ZeroHypercover

variable {J : Precoverage C} [J.toGrothendieck.Subcanonical]

lemma hom_ext {X Y : C} (ğ’° : J.ZeroHypercover X) {f g : X âŸ¶ Y}
    (h : âˆ€ i, ğ’°.f i â‰« f = ğ’°.f i â‰« g) : f = g := by
  have hs : ğ’°.presieveâ‚€.IsSheafFor (yoneda.obj Y) :=
    (GrothendieckTopology.Subcanonical.isSheaf_of_isRepresentable _).isSheafFor_of_mem_precoverage
      ğ’°.memâ‚€
  exact ğ’°.ext_of_isSeparatedFor hs.isSeparatedFor fun i â†¦ by simp [h i]

/-- Glue morphisms along a `0`-hypercover. -/
noncomputable def glueMorphisms {S T : C} (ğ’° : J.ZeroHypercover S) [ğ’°.HasPullbacks]
    (f : âˆ€ i, ğ’°.X i âŸ¶ T)
    (hf : âˆ€ i j, pullback.fst (ğ’°.f i) (ğ’°.f j) â‰« f i = pullback.snd (ğ’°.f i) (ğ’°.f j) â‰« f j) :
    S âŸ¶ T :=
  ğ’°.toOneHypercover.glueMorphisms f fun i j _ â†¦ hf i j

@[reassoc (attr := simp)]
lemma f_glueMorphisms {S T : C} (ğ’° : J.ZeroHypercover S) [ğ’°.HasPullbacks]
    (f : âˆ€ i, ğ’°.X i âŸ¶ T)
    (hf : âˆ€ i j, pullback.fst (ğ’°.f i) (ğ’°.f j) â‰« f i = pullback.snd (ğ’°.f i) (ğ’°.f j) â‰« f j)
    (i : ğ’°.Iâ‚€) :
    ğ’°.f i â‰« ğ’°.glueMorphisms f hf = f i :=
  ğ’°.toOneHypercover.f_glueMorphisms _ _ _

open MorphismProperty

variable [Limits.HasPullbacks C] [J.IsStableUnderBaseChange]

/-- If `J` is a subcanonical precoverage, isomorphisms are local on the target for `J`. -/
instance : (isomorphisms C).IsLocalAtTarget J := by
  refine .mk_of_isStableUnderBaseChange fun {X Y} f ğ’° (H : âˆ€ i, IsIso _) â†¦ âŸ¨?_, ?_, ?_âŸ©
  Â· refine ğ’°.glueMorphisms (fun i â†¦ inv (pullback.snd f (ğ’°.f i)) â‰« pullback.fst _ _) fun i j â†¦ ?_
    let f := pullback.map (pullback.fst f (ğ’°.f i) â‰« f) (ğ’°.f j) (ğ’°.f i) (ğ’°.f j) (pullback.snd _ _)
      (ğŸ™ _) (ğŸ™ _) (by simp [pullback.condition]) (by simp)
    rw [â† cancel_epi ((pullbackRightPullbackFstIso _ _ _).hom â‰« f)]
    simp [pullback.condition, f]
  Â· exact (ğ’°.pullbackâ‚ f).hom_ext fun i â†¦ by simp [pullback.condition_assoc]
  Â· exact ğ’°.hom_ext fun i â†¦ by simp [pullback.condition]

/--
To show that
```
P ---> X
|      |
v      v
Y ---> Z
```
is a pullback square, it suffices to check that
```
P Ã—[X] Uáµ¢ ---> Uáµ¢
   |           |
   v           v
   Y --------> Z
```
is a pullback square for all `Uáµ¢` in a cover of `X` for some subcanonical topology.
-/
lemma isPullback_of_forall_isPullback {P X Y Z : C} (fst : P âŸ¶ X) (snd : P âŸ¶ Y) (f : X âŸ¶ Z)
    (g : Y âŸ¶ Z)
    -- TODO: after refactoring `MorphismProperty.IsLocalAtTarget` to allow covers
    -- in an arbitrary universe, replace `v` by an arbitrary universe
    (ğ’° : Precoverage.ZeroHypercover.{v} J X)
    (H : âˆ€ i, IsPullback (pullback.snd fst _) (pullback.fst fst (ğ’°.f i) â‰« snd) (ğ’°.f i â‰« f) g) :
    IsPullback fst snd f g := by
  have h : fst â‰« f = snd â‰« g := (ğ’°.pullbackâ‚ fst).hom_ext fun i â†¦ by
    simpa [pullback.condition_assoc] using (H i).w
  suffices IsIso (pullback.lift fst snd h) from
    .of_iso_pullback âŸ¨hâŸ© (asIso (pullback.lift _ _ h)) (by simp) (by simp)
  simp_rw [â† isomorphisms.iff, IsLocalAtTarget.iff_of_zeroHypercover (P := isomorphisms C)
    (ğ’°.pullbackCoverOfLeft f g), isomorphisms.iff]
  intro i
  let m := pullback.map (ğ’°.f i â‰« f) g f g (ğ’°.f i) (ğŸ™ Y) (ğŸ™ Z) (by simp) (by simp)
  have : IsPullback (pullback.fst (ğ’°.f i â‰« f) g) m (ğ’°.f i) (pullback.fst _ _) := by
    simpa [â† IsPullback.paste_vert_iff (.of_hasPullback _ _), m] using .of_hasPullback _ _
  have H' : IsPullback (pullback.fst fst (ğ’°.f i))
      (pullback.lift (pullback.snd _ _) (pullback.fst _ _ â‰« snd)
        (by simp [â† h, pullback.condition_assoc]))
      (pullback.lift fst snd h) m := by
    rw [â† IsPullback.paste_vert_iff this.flip (by ext <;> simp [m, pullback.condition])]
    simpa using .of_hasPullback _ _
  have heq : pullback.snd (pullback.lift fst snd h) ((ğ’°.pullbackCoverOfLeft f g).f i) =
      H'.isoPullback.inv â‰« (H i).isoPullback.hom := by
    rw [Iso.eq_inv_comp]
    cat_disch
  rw [heq]
  infer_instance

end Precoverage.ZeroHypercover

end CategoryTheory
