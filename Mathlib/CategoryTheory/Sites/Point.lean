/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Functor.TypeFlat
import Mathlib.CategoryTheory.Limits.Preserves.Filtered
import Mathlib.CategoryTheory.Limits.Types.Filtered
import Mathlib.CategoryTheory.ObjectProperty.FunctorCategory.PreservesLimits
import Mathlib.CategoryTheory.Sites.Limits
import Mathlib.CategoryTheory.Sites.LocallyBijective
import Mathlib.CategoryTheory.Filtered.FinallySmall

/-!
# Points of a site

Let `C` be a category equipped with a Grothendieck topology `J`. In this file,
we define the notion of point of the site `(C, J)`, as a
structure `GrothendieckTopology.Point`. Such a `Œ¶ : J.Point` consists
in a functor `Œ¶.fiber : C ‚•§ Type w` such that the category `Œ¶.fiber.Elements`
is cofiltered (and initially small) and such that if `x : Œ¶.fiber.obj X`
and `R` is a covering sieve of `X`, then `x` belongs to the image
of some `y : Œ¶.fiber.obj Y` by a morphism `f : Y ‚ü∂ X` which belongs to `R`.
The fact that `Œ¶.fiber.Elements·µí·µñ` is filtered allows to define
`Œ¶.presheafFiber : (C·µí·µñ ‚•§ A) ‚•§ A` by taking the filtering colimit
of the evaluation functors at `op X` when `(X : C, x : F.obj X)` varies in
`Œ¶.fiber.Elements·µí·µñ`. We define `Œ¶.sheafFiber : Sheaf J A ‚•§ A` as the
restriction of `Œ¶.presheafFiber` to the full subcategory of sheaves.

Under certain assumptions, we show that if `A` is concrete and
`P ‚ü∂ Q` is a locally bijective morphism between presheaves,
then the induced morphism on fibers is a bijection. It follows
that `Œ¶.sheafFiber : Sheaf J A ‚•§ A` is not only the restriction of
`Œ¶.presheafFiber` but it may also be thought as a localization
of this functor with respect to the class of morphisms `J.W`.
In particular, the fiber of a presheaf identifies to the fiber of
its associated sheaf.

We show that both `Œ¶.presheafFiber` and `Œ¶.sheafFiber`
commute to finite limits and to arbitrary colimits.

-/

universe w' w v v' u u'

namespace CategoryTheory

open Limits Opposite

-- to be moved
instance {C D : Type*} [Category C] [Category D] [HasFiniteLimits D] (X : C) :
    PreservesFiniteLimits ((evaluation C D).obj X) where
  preservesFiniteLimits J _ _ := by
    infer_instance

-- to be moved
lemma HasExactColimitOfShape.of_final
    {J‚ÇÅ J‚ÇÇ : Type*} [Category J‚ÇÅ] [Category J‚ÇÇ] (F : J‚ÇÅ ‚•§ J‚ÇÇ) [F.Final]
    (C : Type*) [Category C] [HasFiniteLimits C]
    [HasColimitsOfShape J‚ÇÅ C] [HasExactColimitsOfShape J‚ÇÅ C] :
    letI : HasColimitsOfShape J‚ÇÇ C := Functor.Final.hasColimitsOfShape_of_final F
    HasExactColimitsOfShape J‚ÇÇ C := by
  letI : HasColimitsOfShape J‚ÇÇ C := Functor.Final.hasColimitsOfShape_of_final F
  constructor
  let œÜ : (Functor.whiskeringLeft J‚ÇÅ J‚ÇÇ C).obj F ‚ãô colim ‚ü∂ colim :=
    { app G := colimit.pre G F }
  have : IsIso œÜ := by
    rw [NatTrans.isIso_iff_isIso_app]
    intro
    dsimp [œÜ]
    infer_instance
  have : PreservesFiniteLimits ((Functor.whiskeringLeft J‚ÇÅ J‚ÇÇ C).obj F) :=
    ‚ü®fun J _ _ ‚Ü¶ ‚ü®fun {K} ‚Ü¶ ‚ü®fun {c} hc ‚Ü¶ ‚ü®evaluationJointlyReflectsLimits _
    (fun j ‚Ü¶ isLimitOfPreserves ((evaluation _ _).obj (F.obj j)) hc) ‚ü©‚ü©‚ü©‚ü©
  exact preservesFiniteLimits_of_natIso (asIso œÜ)

variable {C : Type u} [Category.{v} C]

namespace Functor

instance [LocallySmall.{w} C] (F : C ‚•§ Type w) : LocallySmall.{w} F.Elements where
  hom_small := by
    rintro ‚ü®X, _‚ü© ‚ü®Y, y‚ü©
    exact small_of_injective (f := fun g ‚Ü¶ g.val) (by cat_disch)

lemma isCofiltered_elements (F : C ‚•§ Type w) [HasFiniteLimits C] [PreservesFiniteLimits F] :
    IsCofiltered F.Elements where
  nonempty := ‚ü®‚ä§_ C, (terminalIsTerminal.isTerminalObj F).from PUnit .unit‚ü©
  cone_objs := by
    rintro ‚ü®X, x‚ü© ‚ü®Y, y‚ü©
    let h := mapIsLimitOfPreservesOfIsLimit F _ _ (prodIsProd X Y)
    let h' := Types.binaryProductLimit (F.obj X) (F.obj Y)
    exact ‚ü®‚ü®X ‚®Ø Y, (h'.conePointUniqueUpToIso h).hom ‚ü®x, y‚ü©‚ü©,
      ‚ü®prod.fst, congr_fun (h'.conePointUniqueUpToIso_hom_comp h (.mk .left)) _‚ü©,
      ‚ü®prod.snd, congr_fun (h'.conePointUniqueUpToIso_hom_comp h (.mk .right)) _‚ü©, by tauto‚ü©
  cone_maps := by
    rintro ‚ü®X, x‚ü© ‚ü®Y, y‚ü© ‚ü®f, hf‚ü© ‚ü®g, hg‚ü©
    dsimp at f g hf hg
    subst hg
    let h := isLimitForkMapOfIsLimit F _ (equalizerIsEqualizer f g)
    let h' := (Types.equalizerLimit (g := F.map f) (h := F.map g)).isLimit
    exact ‚ü®‚ü®equalizer f g, (h'.conePointUniqueUpToIso h).hom ‚ü®x, hf‚ü©‚ü©,
      ‚ü®equalizer.Œπ f g, congr_fun (h'.conePointUniqueUpToIso_hom_comp h .zero) ‚ü®x, hf‚ü©‚ü©,
      by ext; exact equalizer.condition f g‚ü©

end Functor

namespace GrothendieckTopology

variable (J : GrothendieckTopology C)

structure Point where
  fiber : C ‚•§ Type w
  isCofiltered : IsCofiltered fiber.Elements := by infer_instance
  initiallySmall : InitiallySmall.{w} fiber.Elements := by infer_instance
  jointly_surjective {X : C} (R : Sieve X) (h : R ‚àà J X) (x : fiber.obj X) :
    ‚àÉ (Y : C) (f : Y ‚ü∂ X) (_ : R f) (y : fiber.obj Y), fiber.map f y = x

namespace Point

attribute [instance] initiallySmall isCofiltered

variable {J} (Œ¶ : Point.{w} J) {A : Type u'} [Category.{v'} A]
  [HasColimitsOfSize.{w, w} A]

instance : HasColimitsOfShape Œ¶.fiber.Elements·µí·µñ A :=
    hasColimitsOfShape_of_finallySmall _ _

instance [LocallySmall.{w} C] [AB5OfSize.{w, w} A] [HasFiniteLimits A] :
    HasExactColimitsOfShape Œ¶.fiber.Elements·µí·µñ A := by
  obtain ‚ü®D, _, _, F, _‚ü© := FinallySmall.exists_of_isFiltered.{w} Œ¶.fiber.Elements·µí·µñ
  exact HasExactColimitOfShape.of_final F A

noncomputable def presheafFiber : (C·µí·µñ ‚•§ A) ‚•§ A :=
  (Functor.whiskeringLeft _ _ _).obj (CategoryOfElements.œÄ Œ¶.fiber).op ‚ãô colim

noncomputable def toPresheafFiberNatTrans (X : C) (x : Œ¶.fiber.obj X) :
    (evaluation C·µí·µñ A).obj (op X) ‚ü∂ Œ¶.presheafFiber where
  app P := colimit.Œπ ((CategoryOfElements.œÄ Œ¶.fiber).op ‚ãô P) (op ‚ü®X, x‚ü©)
  naturality _ _ f := by simp [presheafFiber]

noncomputable abbrev toPresheafFiber (X : C) (x : Œ¶.fiber.obj X) (P : C·µí·µñ ‚•§ A) :
    P.obj (op X) ‚ü∂ Œ¶.presheafFiber.obj P :=
  (Œ¶.toPresheafFiberNatTrans X x).app P

@[elementwise (attr := simp)]
lemma toPresheafFiber_w {X Y : C} (f : X ‚ü∂ Y) (x : Œ¶.fiber.obj X) (P : C·µí·µñ ‚•§ A) :
    P.map f.op ‚â´ Œ¶.toPresheafFiber X x P =
      Œ¶.toPresheafFiber Y (Œ¶.fiber.map f x) P :=
  colimit.w ((CategoryOfElements.œÄ Œ¶.fiber).op ‚ãô P)
      (CategoryOfElements.homMk ‚ü®X, x‚ü© ‚ü®Y, Œ¶.fiber.map f x‚ü© f rfl).op

@[reassoc]
lemma toPresheafFiber_naturality {P Q : C·µí·µñ ‚•§ A} (g : P ‚ü∂ Q) (X : C) (x : Œ¶.fiber.obj X) :
    Œ¶.toPresheafFiber X x P ‚â´ Œ¶.presheafFiber.map g =
      g.app (op X) ‚â´ Œ¶.toPresheafFiber X x Q :=
  ((Œ¶.toPresheafFiberNatTrans X x).naturality g).symm

variable {FC : A ‚Üí A ‚Üí Type*} {CC : A ‚Üí Type w'}
  [‚àÄ (X Y : A), FunLike (FC X Y) (CC X) (CC Y)]
  [ConcreteCategory.{w'} A FC]

section

variable {P Q : C·µí·µñ ‚•§ A}

@[simp]
lemma toPresheafFiber_naturality_apply {P Q : C·µí·µñ ‚•§ A} (g : P ‚ü∂ Q) (X : C) (x : Œ¶.fiber.obj X)
    (p : ToType (P.obj (op X))) :
    Œ¶.presheafFiber.map g (Œ¶.toPresheafFiber X x P p)  =
      Œ¶.toPresheafFiber X x Q (g.app (op X) p) := by
  rw [‚Üê ConcreteCategory.comp_apply, ‚Üê ConcreteCategory.comp_apply]
  exact congr_fun ((forget A).congr_map (Œ¶.toPresheafFiber_naturality g X x)) p

variable [PreservesFilteredColimitsOfSize.{w, w} (forget A)] [LocallySmall.{w} C]

instance : PreservesColimitsOfShape Œ¶.fiber.Elements·µí·µñ (forget A) :=
  Functor.Final.preservesColimitsOfShape_of_final (FinallySmall.fromFilteredFinalModel.{w} _) _

lemma toPresheafFiber_jointly_surjective (p : ToType (Œ¶.presheafFiber.obj P)) :
    ‚àÉ (X : C) (x : Œ¶.fiber.obj X) (z : ToType (P.obj (op X))),
      Œ¶.toPresheafFiber X x P z = p := by
  obtain ‚ü®‚ü®X, x‚ü©, z, rfl‚ü© := Types.jointly_surjective_of_isColimit
    (isColimitOfPreserves (forget A)
      (colimit.isColimit ((CategoryOfElements.œÄ Œ¶.fiber).op ‚ãô P))) p
  exact ‚ü®X, x, z, rfl‚ü©

lemma toPresheafFiber_jointly_surjective‚ÇÇ (p‚ÇÅ p‚ÇÇ : ToType (Œ¶.presheafFiber.obj P)) :
    ‚àÉ (X : C) (x : Œ¶.fiber.obj X) (z‚ÇÅ z‚ÇÇ : ToType (P.obj (op X))),
      Œ¶.toPresheafFiber X x P z‚ÇÅ = p‚ÇÅ ‚àß Œ¶.toPresheafFiber X x P z‚ÇÇ = p‚ÇÇ := by
  obtain ‚ü®‚ü®X, x‚ü©, z‚ÇÅ, z‚ÇÇ, rfl, rfl‚ü© := Types.FilteredColimit.jointly_surjective_of_isColimit‚ÇÇ
    (isColimitOfPreserves (forget A)
      (colimit.isColimit ((CategoryOfElements.œÄ Œ¶.fiber).op ‚ãô P))) p‚ÇÅ p‚ÇÇ
  exact ‚ü®X, x, z‚ÇÅ, z‚ÇÇ, rfl, rfl‚ü©

lemma toPresheafFiber_eq_iff' (X : C) (x : Œ¶.fiber.obj X) (z‚ÇÅ z‚ÇÇ : ToType (P.obj (op X))) :
    Œ¶.toPresheafFiber X x P z‚ÇÅ = Œ¶.toPresheafFiber X x P z‚ÇÇ ‚Üî
      ‚àÉ (Y : C) (f : Y ‚ü∂ X) (y : Œ¶.fiber.obj Y), Œ¶.fiber.map f y = x ‚àß
        P.map f.op z‚ÇÅ = P.map f.op z‚ÇÇ := by
  refine (Types.FilteredColimit.isColimit_eq_iff'
    (ht := isColimitOfPreserves (forget A)
      (colimit.isColimit ((CategoryOfElements.œÄ Œ¶.fiber).op ‚ãô P))) ..).trans ?_
  constructor
  ¬∑ rintro ‚ü®‚ü®Y, y‚ü©, ‚ü®f, hf‚ü©, hf'‚ü©
    exact ‚ü®Y, f, y, hf, hf'‚ü©
  ¬∑ rintro ‚ü®Y, f, y, hf, hf'‚ü©
    exact ‚ü®‚ü®Y, y‚ü©, ‚ü®f, hf‚ü©, hf'‚ü©

variable (f : P ‚ü∂ Q)

lemma toPresheafFiber_map_surjective [Presheaf.IsLocallySurjective J f] :
    Function.Surjective (Œ¶.presheafFiber.map f) := by
  intro p
  obtain ‚ü®X, x, z, rfl‚ü© := Œ¶.toPresheafFiber_jointly_surjective p
  obtain ‚ü®Y, g, ‚ü®t, ht‚ü©, y, rfl‚ü© := Œ¶.jointly_surjective _ (Presheaf.imageSieve_mem J f z) x
  exact ‚ü®Œ¶.toPresheafFiber Y y P t, by simp [‚Üê toPresheafFiber_w, ‚Üê ht]‚ü©

lemma toPresheafFiber_map_injective [Presheaf.IsLocallyInjective J f] :
    Function.Injective (Œ¶.presheafFiber.map f) := by
  suffices ‚àÄ (X : C) (x : Œ¶.fiber.obj X) (p‚ÇÅ p‚ÇÇ : ToType (P.obj (op X)))
      (hp : f.app _ p‚ÇÅ = f.app _ p‚ÇÇ), Œ¶.toPresheafFiber X x P p‚ÇÅ = Œ¶.toPresheafFiber X x P p‚ÇÇ by
    rintro q‚ÇÅ q‚ÇÇ h
    obtain ‚ü®X, x, p‚ÇÅ, p‚ÇÇ, rfl, rfl‚ü© := Œ¶.toPresheafFiber_jointly_surjective‚ÇÇ q‚ÇÅ q‚ÇÇ
    simp only [toPresheafFiber_naturality_apply, toPresheafFiber_eq_iff'] at h
    obtain ‚ü®Y, g, y, rfl, h‚ü© := h
    simp only [‚Üê NatTrans.naturality_apply] at h
    simpa using this _ y _ _ h
  intro X x p‚ÇÅ p‚ÇÇ h
  obtain ‚ü®Y, g, hg, y, rfl‚ü© := Œ¶.jointly_surjective _ (Presheaf.equalizerSieve_mem J f _ _ h) x
  simp only [Presheaf.equalizerSieve_apply] at hg
  simp only [‚Üê toPresheafFiber_w_apply, hg]

lemma toPresheafFiber_map_bijective
    [Presheaf.IsLocallyInjective J f] [Presheaf.IsLocallySurjective J f] :
    Function.Bijective (Œ¶.presheafFiber.map f) :=
  ‚ü®Œ¶.toPresheafFiber_map_injective f, Œ¶.toPresheafFiber_map_surjective f‚ü©

lemma W_isInvertedBy_presheafFiber
    [J.WEqualsLocallyBijective A] [(forget A).ReflectsIsomorphisms] :
    J.W.IsInvertedBy (Œ¶.presheafFiber (A := A)) := by
  intro P Q f hf
  obtain ‚ü®_, _‚ü© := (J.W_iff_isLocallyBijective f).1 hf
  rw [‚Üê isIso_iff_of_reflects_iso _ (forget A), isIso_iff_bijective]
  exact Œ¶.toPresheafFiber_map_bijective f

end

noncomputable def sheafFiber : Sheaf J A ‚•§ A :=
  sheafToPresheaf J A ‚ãô Œ¶.presheafFiber

variable (A) in
noncomputable def sheafToPresheafCompPresheafFiber :
    sheafToPresheaf J A ‚ãô Œ¶.presheafFiber ‚âÖ Œ¶.sheafFiber := Iso.refl _

instance (P : C·µí·µñ ‚•§ A) [HasWeakSheafify J A]
    [PreservesFilteredColimitsOfSize.{w, w} (forget A)] [LocallySmall.{w} C]
    [J.WEqualsLocallyBijective A] [(forget A).ReflectsIsomorphisms] :
    IsIso (Œ¶.presheafFiber.map (CategoryTheory.toSheafify J P)) :=
  W_isInvertedBy_presheafFiber _ _ (W_toSheafify J P)

variable (A) in
noncomputable def presheafToSheafCompSheafFiber [HasWeakSheafify J A]
    [PreservesFilteredColimitsOfSize.{w, w} (forget A)] [LocallySmall.{w} C]
    [J.WEqualsLocallyBijective A] [(forget A).ReflectsIsomorphisms] :
    presheafToSheaf J A ‚ãô Œ¶.sheafFiber ‚âÖ Œ¶.presheafFiber :=
  Functor.isoWhiskerLeft (presheafToSheaf J A) (Œ¶.sheafToPresheafCompPresheafFiber A).symm ‚â™‚â´
    (NatIso.ofComponents
      (fun P ‚Ü¶ asIso ((Œ¶.presheafFiber (A := A)).map (CategoryTheory.toSheafify J P) :))
        (by simp [‚Üê Functor.map_comp])).symm

instance [LocallySmall.{w} C] [HasFiniteLimits A] [AB5OfSize.{w, w} A] :
    PreservesFiniteLimits (Œ¶.presheafFiber (A := A)) := by
  dsimp [presheafFiber]
  have : PreservesFiniteLimits ((Functor.whiskeringLeft Œ¶.fiber.Elements·µí·µñ C·µí·µñ A).obj
      (CategoryOfElements.œÄ Œ¶.fiber).op) := by
    constructor
    intro _ _ _
    infer_instance
  apply comp_preservesFiniteLimits

instance [LocallySmall.{w} C] [HasFiniteLimits A] [AB5OfSize.{w, w} A] :
    PreservesFiniteLimits (Œ¶.sheafFiber (A := A)) := comp_preservesFiniteLimits _ _

instance : PreservesColimitsOfSize.{w, w} (Œ¶.presheafFiber (A := A)) where
  preservesColimitsOfShape := by
    dsimp [presheafFiber]
    infer_instance

instance [HasSheafify J A] [J.WEqualsLocallyBijective A] [(forget A).ReflectsIsomorphisms]
    [PreservesFilteredColimitsOfSize.{w, w} (forget A)] [LocallySmall.{w} C] :
    PreservesColimitsOfSize.{w, w} (Œ¶.sheafFiber (A := A)) where
  preservesColimitsOfShape {K _} := ‚ü®fun {F} ‚Ü¶
    preservesColimit_of_preserves_colimit_cocone
      (Sheaf.isColimitSheafifyCocone _ (colimit.isColimit _))
        (IsColimit.ofIsoColimit (isColimitOfPreserves Œ¶.presheafFiber
          (colimit.isColimit (F ‚ãô sheafToPresheaf J A))) (by
            let G := colimit (F ‚ãô sheafToPresheaf J A)
            let œÜ := (sheafificationAdjunction J A).unit.app G
            have : IsIso (Œ¶.presheafFiber.map œÜ) :=
              W_isInvertedBy_presheafFiber _ _ (W_toSheafify J _)
            refine Cocones.ext (asIso (Œ¶.presheafFiber.map œÜ)) (fun k ‚Ü¶ ?_)
            -- needs cleanup
            dsimp [sheafFiber, Sheaf.sheafifyCocone, œÜ]
            simp [‚Üê Functor.map_comp]
            congr 1
            have (G : Sheaf J A) :
                ((sheafificationAdjunction J A).counit.app G).val ‚â´
                  (sheafificationAdjunction J A).unit.app G.val = ùüô _ := by
              simp [‚Üê cancel_mono ((sheafToPresheaf _ _).map
                ((sheafificationAdjunction J A).counit.app G))]
            rw [‚Üê cancel_epi ((sheafToPresheaf _ _).map
              ((sheafificationAdjunction J A).counit.app (F.obj k))),
              sheafToPresheaf_map, ‚Üê Sheaf.comp_val_assoc,
              IsIso.hom_inv_id, Sheaf.id_val, Category.id_comp,
              ‚Üê (sheafificationAdjunction J A).unit_naturality, reassoc_of% this]
            dsimp))‚ü©

end Point

end GrothendieckTopology

end CategoryTheory
