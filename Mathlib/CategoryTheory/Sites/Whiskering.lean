/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import Mathlib.CategoryTheory.Sites.Sheaf

#align_import category_theory.sites.whiskering from "leanprover-community/mathlib"@"9f9015c645d85695581237cc761981036be8bd37"

/-!

In this file we construct the functor `Sheaf J A â¥¤ Sheaf J B` between sheaf categories
obtained by composition with a functor `F : A â¥¤ B`.

In order for the sheaf condition to be preserved, `F` must preserve the correct limits.
The lemma `Presheaf.IsSheaf.comp` says that composition with such an `F` indeed preserves the
sheaf condition.

The functor between sheaf categories is called `sheafCompose J F`.
Given a natural transformation `Î· : F âŸ¶ G`, we obtain a natural transformation
`sheafCompose J F âŸ¶ sheafCompose J G`, which we call `sheafCompose_map J Î·`.

-/


namespace CategoryTheory

open CategoryTheory.Limits

universe vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

variable {C : Type uâ‚} [Category.{vâ‚} C]

variable {A : Type uâ‚‚} [Category.{vâ‚‚} A]

variable {B : Type uâ‚ƒ} [Category.{vâ‚ƒ} B]

variable {J : GrothendieckTopology C}

variable {U : C} (R : Presieve U)

variable (F G H : A â¥¤ B) (Î· : F âŸ¶ G) (Î³ : G âŸ¶ H)

namespace GrothendieckTopology.Cover

variable (P : Cáµ’áµ– â¥¤ A) {X : C} (S : J.Cover X)

/-- The multicospan associated to a cover `S : J.Cover X` and a presheaf of the form `P â‹™ F`
is isomorphic to the composition of the multicospan associated to `S` and `P`,
composed with `F`. -/
def multicospanComp : (S.index (P â‹™ F)).multicospan â‰… (S.index P).multicospan â‹™ F :=
  NatIso.ofComponents
    (fun t =>
      match t with
      | WalkingMulticospan.left a => eqToIso rfl
      | WalkingMulticospan.right b => eqToIso rfl)
    (by
      rintro (a | b) (a | b) (f | f | f)
      all_goals aesop_cat)
#align category_theory.grothendieck_topology.cover.multicospan_comp CategoryTheory.GrothendieckTopology.Cover.multicospanComp

@[simp]
theorem multicospanComp_app_left (a) :
    (S.multicospanComp F P).app (WalkingMulticospan.left a) = eqToIso rfl :=
  rfl
#align category_theory.grothendieck_topology.cover.multicospan_comp_app_left CategoryTheory.GrothendieckTopology.Cover.multicospanComp_app_left

@[simp]
theorem multicospanComp_app_right (b) :
    (S.multicospanComp F P).app (WalkingMulticospan.right b) = eqToIso rfl :=
  rfl
#align category_theory.grothendieck_topology.cover.multicospan_comp_app_right CategoryTheory.GrothendieckTopology.Cover.multicospanComp_app_right

@[simp]
theorem multicospanComp_hom_app_left (a) :
    (S.multicospanComp F P).hom.app (WalkingMulticospan.left a) = eqToHom rfl :=
  rfl
#align category_theory.grothendieck_topology.cover.multicospan_comp_hom_app_left CategoryTheory.GrothendieckTopology.Cover.multicospanComp_hom_app_left

@[simp]
theorem multicospanComp_hom_app_right (b) :
    (S.multicospanComp F P).hom.app (WalkingMulticospan.right b) = eqToHom rfl :=
  rfl
#align category_theory.grothendieck_topology.cover.multicospan_comp_hom_app_right CategoryTheory.GrothendieckTopology.Cover.multicospanComp_hom_app_right

@[simp]
theorem multicospanComp_hom_inv_left (P : Cáµ’áµ– â¥¤ A) {X : C} (S : J.Cover X) (a) :
    (S.multicospanComp F P).inv.app (WalkingMulticospan.left a) = eqToHom rfl :=
  rfl
#align category_theory.grothendieck_topology.cover.multicospan_comp_hom_inv_left CategoryTheory.GrothendieckTopology.Cover.multicospanComp_hom_inv_left

@[simp]
theorem multicospanComp_hom_inv_right (P : Cáµ’áµ– â¥¤ A) {X : C} (S : J.Cover X) (b) :
    (S.multicospanComp F P).inv.app (WalkingMulticospan.right b) = eqToHom rfl :=
  rfl
#align category_theory.grothendieck_topology.cover.multicospan_comp_hom_inv_right CategoryTheory.GrothendieckTopology.Cover.multicospanComp_hom_inv_right

/-- Mapping the multifork associated to a cover `S : J.Cover X` and a presheaf `P` with
respect to a functor `F` is isomorphic (upto a natural isomorphism of the underlying functors)
to the multifork associated to `S` and `P â‹™ F`. -/
def mapMultifork :
    F.mapCone (S.multifork P) â‰…
      (Limits.Cones.postcompose (S.multicospanComp F P).hom).obj (S.multifork (P â‹™ F)) :=
  Cones.ext (eqToIso rfl)
    (by
      rintro (a | b)
      Â· dsimp
        erw [Category.id_comp, multicospanComp_hom_app_left, eqToHom_refl, Category.comp_id]
      Â· dsimp
        erw [Functor.map_comp, Category.assoc, Category.id_comp,
          multicospanComp_hom_app_right, eqToHom_refl, Category.comp_id]
        rfl)
#align category_theory.grothendieck_topology.cover.map_multifork CategoryTheory.GrothendieckTopology.Cover.mapMultifork

end GrothendieckTopology.Cover

variable [âˆ€ (X : C) (S : J.Cover X) (P : Cáµ’áµ– â¥¤ A), PreservesLimit (S.index P).multicospan F]
variable [âˆ€ (X : C) (S : J.Cover X) (P : Cáµ’áµ– â¥¤ A), PreservesLimit (S.index P).multicospan G]
variable [âˆ€ (X : C) (S : J.Cover X) (P : Cáµ’áµ– â¥¤ A), PreservesLimit (S.index P).multicospan H]

theorem Presheaf.IsSheaf.comp {P : Cáµ’áµ– â¥¤ A} (hP : Presheaf.IsSheaf J P) :
    Presheaf.IsSheaf J (P â‹™ F) := by
  rw [Presheaf.isSheaf_iff_multifork] at hP âŠ¢
  intro X S
  obtain âŸ¨hâŸ© := hP X S
  replace h := isLimitOfPreserves F h
  replace h := Limits.IsLimit.ofIsoLimit h (S.mapMultifork F P)
  exact âŸ¨Limits.IsLimit.postcomposeHomEquiv (S.multicospanComp F P) _ hâŸ©
#align category_theory.presheaf.is_sheaf.comp CategoryTheory.Presheaf.IsSheaf.comp

variable (J)

/-- Composing a sheaf with a functor preserving the appropriate limits yields a functor
between sheaf categories. -/
@[simps]
def sheafCompose : Sheaf J A â¥¤ Sheaf J B where
  obj G := âŸ¨G.val â‹™ F, Presheaf.IsSheaf.comp _ G.2âŸ©
  map Î· := âŸ¨whiskerRight Î·.val _âŸ©
  map_id _ := Sheaf.Hom.ext _ _ <| whiskerRight_id _
  map_comp _ _ := Sheaf.Hom.ext _ _ <| whiskerRight_comp _ _ _
set_option linter.uppercaseLean3 false in
#align category_theory.Sheaf_compose CategoryTheory.sheafCompose

variable {F G}

/--
If `Î· : F âŸ¶ G` is a natural transformation then we obtain a morphism of functors
`sheafCompose J F âŸ¶ sheafCompose J G` by whiskering with `Î·` on the level of presheaves.
-/
def sheafCompose_map : sheafCompose J F âŸ¶ sheafCompose J G where
  app := fun X => .mk <| whiskerLeft _ Î·

@[simp]
lemma sheafCompose_id : sheafCompose_map (F := F) J (ðŸ™ _) = ðŸ™ _ := rfl

@[simp]
lemma sheafCompose_comp :
    sheafCompose_map J (Î· â‰« Î³) = sheafCompose_map J Î· â‰« sheafCompose_map J Î³ := rfl

end CategoryTheory
