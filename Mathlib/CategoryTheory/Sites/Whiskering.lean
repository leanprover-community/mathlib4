/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import Mathlib.CategoryTheory.Sites.Sheaf

/-!

In this file we construct the functor `Sheaf J A ‚•§ Sheaf J B` between sheaf categories
obtained by composition with a functor `F : A ‚•§ B`.

In order for the sheaf condition to be preserved, `F` must preserve the correct limits.
The lemma `Presheaf.IsSheaf.comp` says that composition with such an `F` indeed preserves the
sheaf condition.

The functor between sheaf categories is called `sheafCompose J F`.
Given a natural transformation `Œ∑ : F ‚ü∂ G`, we obtain a natural transformation
`sheafCompose J F ‚ü∂ sheafCompose J G`, which we call `sheafCompose_map J Œ∑`.

-/


namespace CategoryTheory

open CategoryTheory.Limits Functor

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]
variable {A : Type u‚ÇÇ} [Category.{v‚ÇÇ} A]
variable {B : Type u‚ÇÉ} [Category.{v‚ÇÉ} B]
variable (J : GrothendieckTopology C)
variable {U : C} (R : Presieve U)
variable (F G H : A ‚•§ B) (Œ∑ : F ‚ü∂ G) (Œ≥ : G ‚ü∂ H)

/-- Describes the property of a functor to "preserve sheaves". -/
class GrothendieckTopology.HasSheafCompose : Prop where
  /-- For every sheaf `P`, `P ‚ãô F` is a sheaf. -/
  isSheaf (P : C·µí·µñ ‚•§ A) (hP : Presheaf.IsSheaf J P) : Presheaf.IsSheaf J (P ‚ãô F)

variable [J.HasSheafCompose F] [J.HasSheafCompose G] [J.HasSheafCompose H]

/-- Composing a functor which `HasSheafCompose`, yields a functor between sheaf categories. -/
@[simps]
def sheafCompose : Sheaf J A ‚•§ Sheaf J B where
  obj G := ‚ü®G.val ‚ãô F, GrothendieckTopology.HasSheafCompose.isSheaf G.val G.2‚ü©
  map Œ∑ := ‚ü®whiskerRight Œ∑.val _‚ü©
  map_id _ := Sheaf.Hom.ext <| whiskerRight_id _
  map_comp _ _ := Sheaf.Hom.ext <| whiskerRight_comp _ _ _

instance [F.Faithful] : (sheafCompose J F ‚ãô sheafToPresheaf _ _).Faithful :=
  show (sheafToPresheaf _ _ ‚ãô (whiskeringRight C·µí·µñ A B).obj F).Faithful from inferInstance

instance [F.Faithful] [F.Full] : (sheafCompose J F ‚ãô sheafToPresheaf _ _).Full :=
  show (sheafToPresheaf _ _ ‚ãô (whiskeringRight C·µí·µñ A B).obj F).Full from inferInstance

instance [F.Faithful] : (sheafCompose J F).Faithful :=
  Functor.Faithful.of_comp (sheafCompose J F) (sheafToPresheaf _ _)

instance [F.Full] [F.Faithful] : (sheafCompose J F).Full :=
  Functor.Full.of_comp_faithful (sheafCompose J F) (sheafToPresheaf _ _)

instance [F.ReflectsIsomorphisms] : (sheafCompose J F).ReflectsIsomorphisms where
  reflects {G‚ÇÅ G‚ÇÇ} f _ := by
    rw [‚Üê isIso_iff_of_reflects_iso _ (sheafToPresheaf _ _),
      ‚Üê isIso_iff_of_reflects_iso _ ((whiskeringRight C·µí·µñ A B).obj F)]
    change IsIso ((sheafToPresheaf _ _).map ((sheafCompose J F).map f))
    infer_instance

variable {F G}

/--
If `Œ∑ : F ‚ü∂ G` is a natural transformation then we obtain a morphism of functors
`sheafCompose J F ‚ü∂ sheafCompose J G` by whiskering with `Œ∑` on the level of presheaves.
-/
def sheafCompose_map : sheafCompose J F ‚ü∂ sheafCompose J G where
  app := fun _ => .mk <| whiskerLeft _ Œ∑

@[simp]
lemma sheafCompose_id : sheafCompose_map (F := F) J (ùüô _) = ùüô _ := rfl

@[simp]
lemma sheafCompose_comp :
    sheafCompose_map J (Œ∑ ‚â´ Œ≥) = sheafCompose_map J Œ∑ ‚â´ sheafCompose_map J Œ≥ := rfl

namespace GrothendieckTopology.Cover

variable (F G) {J}
variable (P : C·µí·µñ ‚•§ A) {X : C} (S : J.Cover X)

/-- The multicospan associated to a cover `S : J.Cover X` and a presheaf of the form `P ‚ãô F`
is isomorphic to the composition of the multicospan associated to `S` and `P`,
composed with `F`. -/
@[simps!]
def multicospanComp : (S.index (P ‚ãô F)).multicospan ‚âÖ (S.index P).multicospan ‚ãô F :=
  NatIso.ofComponents
    (fun t =>
      match t with
      | WalkingMulticospan.left _ => Iso.refl _
      | WalkingMulticospan.right _ => Iso.refl _)
    (by
      rintro (a | b) (a | b) (f | f | f)
      all_goals cat_disch)

/-- Mapping the multifork associated to a cover `S : J.Cover X` and a presheaf `P` with
respect to a functor `F` is isomorphic (upto a natural isomorphism of the underlying functors)
to the multifork associated to `S` and `P ‚ãô F`. -/
def mapMultifork :
    F.mapCone (S.multifork P) ‚âÖ
      (Limits.Cones.postcompose (S.multicospanComp F P).hom).obj (S.multifork (P ‚ãô F)) :=
  Cones.ext (Iso.refl _)

end GrothendieckTopology.Cover

/--
Composing a sheaf with a functor preserving the limit of `(S.index P).multicospan` yields a functor
between sheaf categories.
-/
instance hasSheafCompose_of_preservesMulticospan (F : A ‚•§ B)
    [‚àÄ (X : C) (S : J.Cover X) (P : C·µí·µñ ‚•§ A), PreservesLimit (S.index P).multicospan F] :
    J.HasSheafCompose F where
  isSheaf P hP := by
    rw [Presheaf.isSheaf_iff_multifork] at hP ‚ä¢
    intro X S
    obtain ‚ü®h‚ü© := hP X S
    replace h := isLimitOfPreserves F h
    replace h := Limits.IsLimit.ofIsoLimit h (S.mapMultifork F P)
    exact ‚ü®Limits.IsLimit.postcomposeHomEquiv (S.multicospanComp F P) _ h‚ü©

/--
Composing a sheaf with a functor preserving limits of the same size as the hom sets in `C` yields a
functor between sheaf categories.

Note: the size of the limit that `F` is required to preserve in
`hasSheafCompose_of_preservesMulticospan` is in general larger than this.
-/
instance hasSheafCompose_of_preservesLimitsOfSize [PreservesLimitsOfSize.{v‚ÇÅ, max u‚ÇÅ v‚ÇÅ} F] :
    J.HasSheafCompose F where
  isSheaf _ hP := Presheaf.isSheaf_comp_of_isSheaf J _ F hP

variable {J}

lemma Sheaf.isSeparated {FA : A ‚Üí A ‚Üí Type*} {CA : A ‚Üí Type*}
    [‚àÄ X Y, FunLike (FA X Y) (CA X) (CA Y)] [ConcreteCategory A FA] [J.HasSheafCompose (forget A)]
    (F : Sheaf J A) : Presheaf.IsSeparated J F.val := by
  rintro X S hS x y h
  exact (Presieve.isSeparated_of_isSheaf _ _ ((isSheaf_iff_isSheaf_of_type _ _).1
    ((sheafCompose J (forget A)).obj F).2) S hS).ext (fun _ _ hf => h _ _ hf)

lemma Presheaf.IsSheaf.isSeparated {F : C·µí·µñ ‚•§ A} {FA : A ‚Üí A ‚Üí Type*} {CA : A ‚Üí Type*}
    [‚àÄ X Y, FunLike (FA X Y) (CA X) (CA Y)] [ConcreteCategory A FA]
    [J.HasSheafCompose (forget A)] (hF : Presheaf.IsSheaf J F) :
    Presheaf.IsSeparated J F :=
  Sheaf.isSeparated ‚ü®F, hF‚ü©

end CategoryTheory
