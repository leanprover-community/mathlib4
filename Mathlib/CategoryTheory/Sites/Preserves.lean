/-
Copyright (c) 2023 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Products
import Mathlib.CategoryTheory.Limits.Shapes.CommSq
import Mathlib.CategoryTheory.Sites.EqualizerSheafCondition

/-!
# Sheaves preserve products
We prove that a presheaf which satisfies the sheaf condition with respect to certain presieves
preserve "the corresponding products".
More precisely, given a presheaf `F : C·µí·µñ ‚•§ Type*`, we have:
## Main results
* If `F` satisfies the sheaf condition with respect to the empty sieve on the initial object of `C`,
  then `F` preserves terminal objects.
See `preservesTerminalOfIsSheafForEmpty`.
* If `F` furthermore satisfies the sheaf condition with respect to the presieve consisting of the
  inclusion arrows in a coproduct in `C`, then `F` preserves the corresponding product.
See `preservesProductOfIsSheafFor`.
* If `F` preserves a product, then it satisfies the sheaf condition with respect to the
  corresponding presieve of arrows.
See `isSheafFor_of_preservesProduct`.
-/

universe v u

namespace CategoryTheory.Presieve

open Limits Opposite

variable {C : Type u} [Category.{v} C] {I : C} (hI : IsInitial I) (F : C·µí·µñ ‚•§ Type (max u v))
    (hF : (ofArrows (X := I) Empty.elim instIsEmptyEmpty.elim).IsSheafFor F) {Œ± : Type}
    (X : Œ± ‚Üí C) [HasCoproduct X] [(ofArrows X (fun i ‚Ü¶ Sigma.Œπ X i)).hasPullbacks]
    [HasInitial C]
    (hd : ‚àÄ i j, i ‚â† j ‚Üí IsPullback (initial.to _) (initial.to _) (Sigma.Œπ X i) (Sigma.Œπ X j))
    [‚àÄ i, Mono (Sigma.Œπ X i)]

namespace Preserves

variable (I) in
/--
If `F` is a presheaf which satisfies the sheaf condition with respect to the empty presieve on the
initial object, then `F` takes the initial object to the terminal object.
-/
noncomputable
def isTerminal_of_isSheafFor_empty_presieve : IsTerminal (F.obj (op I)) := by
  refine @IsTerminal.ofUnique _ _ _ fun Y ‚Ü¶ ?_
  choose t h using hF (by tauto) (by tauto)
  exact ‚ü®‚ü®fun _ ‚Ü¶ t‚ü©, fun a ‚Ü¶ by ext; exact h.2 _ (by tauto)‚ü©

/--
If `F` is a presheaf which satisfies the sheaf condition with respect to the empty presieve on the
initial object, then `F` preserves terminal objects.
-/
noncomputable
def preservesTerminalOfIsSheafForEmpty : PreservesLimit (Functor.empty C·µí·µñ) F :=
  haveI := hI.hasInitial
  (preservesTerminalOfIso F
    ((F.mapIso (terminalIsoIsTerminal (terminalOpOfInitial initialIsInitial)) ‚â™‚â´
    (F.mapIso (initialIsoIsInitial hI).symm.op) ‚â™‚â´
    (terminalIsoIsTerminal (isTerminal_of_isSheafFor_empty_presieve I F hF)).symm)))

theorem firstMap_eq_secondMap : Equalizer.Presieve.Arrows.firstMap F X (fun j ‚Ü¶ Sigma.Œπ X j) =
    Equalizer.Presieve.Arrows.secondMap F X (fun j ‚Ü¶ Sigma.Œπ X j) := by
  ext a ‚ü®i, j‚ü©
  simp only [Equalizer.Presieve.Arrows.firstMap, Types.pi_lift_œÄ_apply, types_comp_apply,
    Equalizer.Presieve.Arrows.secondMap]
  by_cases hi : i = j
  ¬∑ subst hi
    suffices pullback.fst (f := Sigma.Œπ X i) (g := Sigma.Œπ X i) =
      pullback.snd (f := Sigma.Œπ X i) (g := Sigma.Œπ X i) by rw [this]
    apply Mono.right_cancellation (f := Sigma.Œπ X i)
    exact pullback.condition
  ¬∑ haveI := preservesTerminalOfIsSheafForEmpty hI F hF
    let i‚ÇÅ : op (pullback (Sigma.Œπ X i) (Sigma.Œπ X j)) ‚âÖ op (‚ä•_ _) :=
      ((hd i j hi).isoPullback).op
    let i‚ÇÇ : op (‚ä•_ C) ‚âÖ (‚ä§_ C·µí·µñ) :=
      (terminalIsoIsTerminal (terminalOpOfInitial initialIsInitial)).symm
    apply_fun (F.mapIso i‚ÇÅ ‚â™‚â´ F.mapIso i‚ÇÇ ‚â™‚â´ (PreservesTerminal.iso F)).hom using
      injective_of_mono _
    simp

theorem piComparison_fac' {Z : C} (œÄ : (i : Œ±) ‚Üí X i ‚ü∂ Z) [IsIso (Sigma.desc œÄ)] :
    piComparison F (fun x ‚Ü¶ op (X x)) =
    F.map ((opCoproductIsoProduct X).inv ‚â´ (inv (Sigma.desc œÄ)).op) ‚â´
    Equalizer.Presieve.Arrows.forkMap F X œÄ := by
  have h‚ÇÅ : Pi.lift (fun i ‚Ü¶ F.map (œÄ i).op) =
      F.map (Pi.lift (fun i ‚Ü¶ (œÄ i).op)) ‚â´ piComparison F _ := by simp
  have h‚ÇÇ : (opCoproductIsoProduct X).inv ‚â´ (inv (Sigma.desc œÄ)).op =
      inv ((Sigma.desc œÄ).op ‚â´ (opCoproductIsoProduct X).hom) := by
    simp only [op_inv, IsIso.inv_comp, IsIso.Iso.inv_hom]
  simp only [Equalizer.Presieve.Arrows.forkMap, h‚ÇÇ, h‚ÇÅ,  ‚Üê desc_op_comp_opCoproductIsoProduct_hom,
    ‚Üê Category.assoc, ‚Üê Functor.map_comp, IsIso.inv_comp, IsIso.Iso.inv_hom,
    Category.assoc, IsIso.inv_hom_id, Category.comp_id, Iso.inv_hom_id, Functor.map_id,
    Category.id_comp]

theorem piComparison_fac : piComparison F (fun x ‚Ü¶ op (X x)) =
    F.map (opCoproductIsoProduct X).inv ‚â´
    Equalizer.Presieve.Arrows.forkMap F X (fun i ‚Ü¶ Sigma.Œπ X i) := by
  have : Sigma.desc (fun i ‚Ü¶ Sigma.Œπ X i) = ùüô _ := by ext; simp
  have _ : IsIso (Sigma.desc (fun i ‚Ü¶ Sigma.Œπ X i)) := by rw [this]; infer_instance
  rw [piComparison_fac' (œÄ := (fun i ‚Ü¶ Sigma.Œπ X i))]
  simp only [op_inv, Functor.map_comp, Functor.map_inv, Category.assoc, this]
  congr
  simp only [op_id, Functor.map_id, IsIso.inv_id]
  rfl

end Preserves

open CategoryTheory.Presieve.Preserves

/--
If `F` is a presheaf which `IsSheafFor` a presieve of arrows and the empty presieve, then it
preserves the product corresponding to the presieve of arrows.
-/
noncomputable
def preservesProductOfIsSheafFor (hF' : (ofArrows X (fun i ‚Ü¶ Sigma.Œπ X i)).IsSheafFor F) :
    PreservesLimit (Discrete.functor (fun x ‚Ü¶ op (X x))) F := by
  refine @PreservesProduct.ofIsoComparison _ _ _ _ F _ (fun x ‚Ü¶ op (X x)) _ _ ?_
  rw [piComparison_fac]
  refine @IsIso.comp_isIso _ _ _ _ _ _ _ inferInstance ?_
  rw [isIso_iff_bijective, Function.bijective_iff_existsUnique]
  rw [Equalizer.Presieve.Arrows.sheaf_condition, Limits.Types.type_equalizer_iff_unique] at hF'
  exact fun b ‚Ü¶ hF' b (congr_fun (firstMap_eq_secondMap hI F hF X hd) b)

theorem isSheafFor_of_preservesProduct {Z : C} (œÄ : (i : Œ±) ‚Üí X i ‚ü∂ Z) [IsIso (Sigma.desc œÄ)]
    [PreservesLimit (Discrete.functor (fun x ‚Ü¶ op (X x))) F] [(ofArrows X œÄ).hasPullbacks] :
    (ofArrows X œÄ).IsSheafFor F := by
  rw [Equalizer.Presieve.Arrows.sheaf_condition, Limits.Types.type_equalizer_iff_unique]
  have hc : IsIso (piComparison F (fun x ‚Ü¶ op (X x))) := inferInstance
  rw [piComparison_fac' (œÄ := œÄ), isIso_iff_bijective, Function.bijective_iff_existsUnique] at hc
  intro b _
  obtain ‚ü®t, ht‚ÇÅ, ht‚ÇÇ‚ü© := hc b
  refine ‚ü®F.map ((opCoproductIsoProduct X).inv ‚â´ (inv (Sigma.desc œÄ)).op ) t, ht‚ÇÅ, fun y hy ‚Ü¶ ?_‚ü©
  specialize ht‚ÇÇ (F.map ((Sigma.desc œÄ).op ‚â´ (opCoproductIsoProduct X).hom) y)
  apply_fun F.map ((Sigma.desc œÄ).op ‚â´ (opCoproductIsoProduct X).hom) using injective_of_mono _
  simp only [‚Üê FunctorToTypes.map_comp_apply]
  simp only [op_inv, Category.assoc, IsIso.inv_hom_id_assoc,
    Iso.inv_hom_id, FunctorToTypes.map_id_apply]
  apply ht‚ÇÇ
  rw [‚Üê hy]
  simp only [op_inv, Functor.map_comp, Functor.map_inv,
    types_comp_apply, FunctorToTypes.map_inv_map_hom_apply]
  congr
  simp only [‚Üê Functor.map_inv, ‚Üê FunctorToTypes.map_comp_apply, IsIso.hom_inv_id,
    FunctorToTypes.map_id_apply]

theorem isSheafFor_iff_preservesProduct : (ofArrows X (fun i ‚Ü¶ Sigma.Œπ X i)).IsSheafFor F ‚Üî
    Nonempty (PreservesLimit (Discrete.functor (fun x ‚Ü¶ op (X x))) F) := by
  refine ‚ü®fun hF' ‚Ü¶ ‚ü®preservesProductOfIsSheafFor hI _ hF X hd hF'‚ü©, fun hF' ‚Ü¶ ?_‚ü©
  let _ := hF'.some
  have : Sigma.desc (fun i ‚Ü¶ Sigma.Œπ X i) = ùüô _ := by ext; simp
  have _ : IsIso (Sigma.desc (fun i ‚Ü¶ Sigma.Œπ X i)) := by rw [this]; infer_instance
  exact isSheafFor_of_preservesProduct F X (fun i ‚Ü¶ Sigma.Œπ X i)

end CategoryTheory.Presieve
