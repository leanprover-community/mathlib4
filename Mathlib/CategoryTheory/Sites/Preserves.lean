/-
Copyright (c) 2023 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.CategoryTheory.Limits.Opposites
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Products
import Mathlib.CategoryTheory.Sites.SheafOfTypes
import Mathlib.Tactic.ApplyFun

/-!
# Sheaves preserve products

We prove that a presheaf which satisfies the sheaf condition with respect to certain presieves
preserve "the corresponding products".

More precisely, given a presheaf `F : Cáµ’áµ– â¥¤ Type*`, we have:

## Main results

* If `F` satisfies the sheaf condition with respect to the empty sieve on the initial object of `C`,
  then `F`Â preserves terminal objects.
See `preservesTerminalOfIsSheafForEmpty`.

* If `F`Â furthermore satisfies the sheaf condition with respect to the presieve consisting of the
  inclusion arrows in a coproduct in `C`, then `F`Â preserves the corresponding product.
See `preservesProductOfIsSheafFor`.
-/

universe v u

namespace CategoryTheory.Presieve

open Limits Opposite

variable {C : Type u} [Category.{v} C] (F : Cáµ’áµ– â¥¤ Type (max u v)) [HasInitial C]
    (hF : (ofArrows (X := âŠ¥_ C) Empty.elim instIsEmptyEmpty.elim).IsSheafFor F)

/--
If `F`Â is a presheaf which satisfies the sheaf condition with respect to the empty presieve on the
initial object, then `F`Â takes the initial object to the terminal object.
-/
noncomputable
def isTerminal_obj_initial_of_isSheafFor_empty_presieve : IsTerminal (F.obj (op (âŠ¥_ C))) := by
  refine @IsTerminal.ofUnique _ _ _ fun Y â†¦ ?_
  choose t h using hF (by tauto) (by tauto)
  exact âŸ¨âŸ¨fun _ â†¦ tâŸ©, fun a â†¦ by ext; exact h.2 _ (by tauto)âŸ©

/--
If `F`Â is a presheaf which satisfies the sheaf condition with respect to the empty presieve on the
initial object, then `F`Â preserves terminal objects.
-/
noncomputable
def preservesTerminalOfIsSheafForEmpty : PreservesLimit (Functor.empty Cáµ’áµ–) F :=
  preservesTerminalOfIso F
    (F.mapIso (terminalIsoIsTerminal (terminalOpOfInitial initialIsInitial)) â‰ªâ‰«
    (terminalIsoIsTerminal (isTerminal_obj_initial_of_isSheafFor_empty_presieve F hF)).symm)

variable {Î± : Type} {X : Î± â†’ C} [HasCoproduct X]
    [(ofArrows X (fun i â†¦ Sigma.Î¹ X i)).hasPullbacks]
    (hd : âˆ€ i j, i â‰  j â†’ IsInitial (pullback (Sigma.Î¹ X i) (Sigma.Î¹ X j)))
    [âˆ€ i, Mono (Sigma.Î¹ X i)]

variable (X)

namespace Preserves

/-- The canonical map from `Equalizer.FirstObj` to a product indexed by `Î±` -/
noncomputable
def prodMap (F : Cáµ’áµ– â¥¤ Type (max u v)) :
    (âˆ fun (f : (Î£(Y : C), { f : Y âŸ¶ âˆ X // ofArrows X (fun i â†¦ Sigma.Î¹ X i) f })) â†¦
    F.obj (op f.fst)) âŸ¶ âˆ fun a â†¦ F.obj (op (X a)) :=
  Pi.map' (fun a â†¦ âŸ¨X a, (fun i â†¦ Sigma.Î¹ X i) a, ofArrows.mk aâŸ©) (fun _ â†¦ ğŸ™ _)

/--
Remove the factors coming from `a : Î±` where `X a` is an initial object.
-/
noncomputable
def removeInitialâ‚ : (âˆ fun a â†¦ F.obj (op (X a))) âŸ¶
    âˆ fun (a : {i : Î± // Â¬ (Nonempty (IsInitial (X i))) }) â†¦ F.obj (op (X a.val)) :=
  Pi.map' (fun a â†¦ a.val) fun _ â†¦ ğŸ™ _

/--
Remove the factors coming from those `f` in the indexing set of `Equalizer.firstObj`
where `f.fst` is an initial object.
-/
noncomputable
def removeInitialâ‚‚ : (âˆ fun (f : Î£(Y : C), { f : Y âŸ¶ âˆ X // ofArrows X (fun i â†¦ Sigma.Î¹ X i) f }) â†¦
    F.obj (op f.fst)) âŸ¶ âˆ fun (f : {g : Î£(Y : C), { f : Y âŸ¶ âˆ X //
    ofArrows X (fun i â†¦ Sigma.Î¹ X i) f } // Â¬ (Nonempty (IsInitial g.fst)) }) â†¦
    F.obj (op f.val.fst) :=
  Pi.map' (fun a â†¦ a.val) fun _ â†¦ ğŸ™ _

theorem sigma_surjective :
    Function.Surjective (fun a â†¦ âŸ¨âŸ¨X a.val, Sigma.Î¹ X a.val, ofArrows.mk a.valâŸ©, a.propâŸ© :
    {i : Î± // Â¬ (Nonempty (IsInitial (X i))) } â†’ {g : Î£(Y : C), { f : Y âŸ¶ âˆ X //
    ofArrows X (fun i â†¦ Sigma.Î¹ X i) f } // Â¬ (Nonempty (IsInitial g.fst)) }) :=
  fun âŸ¨âŸ¨_, _, hgâŸ©, propâŸ© â†¦ by cases' hg with i; exact âŸ¨âŸ¨i, propâŸ©, rflâŸ©

theorem sigma_injective :
    Function.Injective (fun a â†¦ âŸ¨âŸ¨X a.val, Sigma.Î¹ X a.val, ofArrows.mk a.valâŸ©, a.propâŸ© :
    {i : Î± // Â¬ (Nonempty (IsInitial (X i))) } â†’ {g : Î£(Y : C), { f : Y âŸ¶ âˆ X //
    ofArrows X (fun i â†¦ Sigma.Î¹ X i) f } // Â¬ (Nonempty (IsInitial g.fst)) }) := by
  intro a b h
  simp only [Subtype.mk.injEq, Sigma.mk.inj_iff] at h
  ext
  by_contra hh
  specialize hd _ _ hh
  apply a.prop
  constructor
  refine IsInitial.ofIso hd âŸ¨pullback.fst, pullback.lift (ğŸ™ _) (eqToHom h.1) ?_, ?_, ?_âŸ©
  Â· refine eq_comp_of_heq h.1 ?_ ?_
    Â· rw [Subtype.heq_iff_coe_heq ?_ ?_] at h
      Â· exact h.2
      Â· rw [h.1]
      Â· rw [h.1]
    Â· simp
  Â· exact IsInitial.hom_ext hd _ _
  Â· simp

/--
After removing the factors that come from initial objects, the products are isomorphic.
-/
noncomputable
def prodIsoWithoutInitial : (âˆ fun (f : {g : Î£(Y : C), { f : Y âŸ¶ âˆ X //
    ofArrows X (fun i â†¦ Sigma.Î¹ X i) f } // Â¬ (Nonempty (IsInitial g.fst)) }) â†¦
    F.obj (op f.val.fst)) â‰…
    âˆ fun (a : {i : Î± // Â¬ (Nonempty (IsInitial (X i))) }) â†¦ F.obj (op (X a.val)) :=
  (Pi.whiskerEquiv (Equiv.ofBijective _ âŸ¨sigma_injective X hd, (sigma_surjective X)âŸ©)
    (fun _ â†¦ Iso.refl _)).symm

theorem prodMap_comp : prodMap X F â‰« removeInitialâ‚ F X = removeInitialâ‚‚ F X â‰«
    (prodIsoWithoutInitial F X hd).hom := by
  ext; simp [prodMap, removeInitialâ‚, removeInitialâ‚‚, prodIsoWithoutInitial, Pi.map']

theorem iso_prodMap_aux {Î² : Type v} {Z : Î² â†’ Type (max u v)} (p : Î² â†’ Prop)
    [âˆ€ b, Decidable (p b)] (h : âˆ€ b, p b â†’ Nonempty (Unique (Z b))) :
    IsIso (Pi.map' (fun a â†¦ a.val) fun _ â†¦ ğŸ™ _ :
    (âˆ Z) âŸ¶ âˆ fun (b : {a : Î² // Â¬ (p a)}) â†¦ Z b.val) := by
  rw [isIso_iff_bijective]
  refine âŸ¨?_, ?_âŸ©
  Â· intro a b hab
    ext âŸ¨jâŸ©
    simp only [Pi.map', Category.comp_id] at hab
    simp only [Discrete.functor_obj]
    by_cases hj : p j
    Â· obtain âŸ¨hj'âŸ© := h j hj
      replace hj' := hj'.instSubsingleton
      exact hj'.allEq  _ _
    Â· apply_fun Pi.Ï€ (fun (b : {a : Î² // Â¬ (p a)}) â†¦ Z b.val) âŸ¨j, hjâŸ© at hab
      simp only [Types.pi_lift_Ï€_apply] at hab
      exact hab
  Â· intro a
    let i : âˆ€ (Î³ : Type v) (Y : Î³ â†’ Type (max u v)), âˆ Y â‰… (x : Î³) â†’ Y x :=
      fun Î³ Y â†¦ Types.productIso.{v, max u v} _
    have : âˆ€ b, p b â†’ Inhabited (Z b) := fun b hb â†¦ (h b hb).some.instInhabited
    let a' : (b : Î²) â†’ Z b := fun b â†¦ if hb : p b then @default _ (this b hb)
      else (i {a : Î² // Â¬ (p a)} (fun c â†¦ Z c.val)).hom a âŸ¨b, hbâŸ©
    refine âŸ¨(i _ Z).inv a', ?_âŸ©
    apply_fun (i {a : Î² // Â¬ (p a)} (fun c â†¦ Z c.val)).hom using injective_of_mono _
    ext j
    simp only [Types.productIso_hom_comp_eval_apply]
    rw [â† types_comp_apply (g := Pi.Ï€ _ _)]
    simp only [Pi.map'_comp_Ï€, Category.comp_id]
    rw [â† types_comp_apply (g := Pi.Ï€ _ _)]
    simp only [Types.productIso_inv_comp_Ï€]
    exact dif_neg j.prop

open Classical in
theorem iso_prodMap : IsIso (prodMap X F) :=
  let _ := preservesTerminalOfIsSheafForEmpty F hF
  have _ : IsIso (removeInitialâ‚ F X) :=
    iso_prodMap_aux (fun b â†¦ Nonempty (IsInitial.{v, u} (X b))) fun b âŸ¨hbâŸ© â†¦
      âŸ¨(Types.isTerminalEquivUnique _).toFun <|
      IsTerminal.isTerminalObj F (op (X b)) (terminalOpOfInitial hb )âŸ©
  have _ : IsIso (removeInitialâ‚‚ F X) :=
    iso_prodMap_aux.{max u v, max u v} (fun (g : Î£(Y : C),
      { f : Y âŸ¶ âˆ X // ofArrows X (fun i â†¦ Sigma.Î¹ X i) f }) â†¦ Nonempty (IsInitial g.fst))
      fun b âŸ¨hbâŸ© â†¦ âŸ¨(Types.isTerminalEquivUnique _) <|
      IsTerminal.isTerminalObj F (op b.fst) (terminalOpOfInitial hb )âŸ©
  have _ : IsIso (prodMap X F â‰« removeInitialâ‚ F X) := by
    rw [prodMap_comp F X hd]
    exact IsIso.comp_isIso
  IsIso.of_isIso_comp_right (prodMap X F) (removeInitialâ‚ F X)

theorem piComparison_fac : F.map (opCoproductIsoProduct X).inv â‰«
    Equalizer.forkMap F (ofArrows X (fun j â†¦ Sigma.Î¹ X j)) â‰« prodMap X F =
    piComparison F (fun z â†¦ op (X z)) := by
  have : (Equalizer.forkMap F (ofArrows X (fun j â†¦ Sigma.Î¹ X j)) â‰«
      prodMap X F) = Pi.lift (fun j â†¦ F.map ((fun j â†¦ Sigma.Î¹ X j) j).op) := by
    ext; simp [prodMap, Pi.map', Equalizer.forkMap]
  rw [this]
  have t : Pi.lift (fun j â†¦ Pi.Ï€ (fun a â†¦ (op (X a))) j) = ğŸ™ _ := by ext; simp -- why not just simp?
  have hh : (fun j â†¦ (opCoproductIsoProduct X).inv â‰« (Sigma.Î¹ X j).op) =
      fun j â†¦ Pi.Ï€ (fun a â†¦ (op (X a))) j
  Â· ext j
    exact opCoproductIsoProduct_inv_comp_Î¹ _ _
  have : F.map (Pi.lift (fun j â†¦ (opCoproductIsoProduct X).inv â‰« (Sigma.Î¹ X j).op)) â‰«
      piComparison F (fun z â†¦ op (X z)) =
      (F.map (opCoproductIsoProduct X).inv â‰« Pi.lift fun j â†¦ F.map ((fun j â†¦ Sigma.Î¹ X j) j).op)
  Â· rw [hh, t]
    ext j x
    simp only [Functor.map_id, Category.id_comp, piComparison, types_comp_apply,
      Types.pi_lift_Ï€_apply, â† FunctorToTypes.map_comp_apply, congr_fun hh j]
  rw [â† this, hh]
  congr
  ext
  simp [t]

theorem firstMap_eq_secondMap : Equalizer.Presieve.firstMap F (ofArrows X (fun j â†¦ Sigma.Î¹ X j)) =
    Equalizer.Presieve.secondMap F (ofArrows X (fun j â†¦ Sigma.Î¹ X j)) := by
  ext a
  simp only [Equalizer.Presieve.SecondObj, Equalizer.Presieve.firstMap,
    Equalizer.Presieve.secondMap]
  ext âŸ¨jâŸ©
  simp only [Discrete.functor_obj, Types.pi_lift_Ï€_apply, types_comp_apply]
  obtain âŸ¨âŸ¨Y, f, hfâŸ©, âŸ¨Z, g, hgâŸ©âŸ© := j
  cases' hf with i
  cases' hg with j
  by_cases hi : i = j
  Â· subst hi
    suffices pullback.fst (f := Sigma.Î¹ X i) (g := Sigma.Î¹ X i) =
      pullback.snd (f := Sigma.Î¹ X i) (g := Sigma.Î¹ X i) by rw [this]
    apply Mono.right_cancellation (f := Sigma.Î¹ X i)
    exact pullback.condition
  Â· haveI := preservesTerminalOfIsSheafForEmpty F hF
    let iâ‚ : op (pullback (Sigma.Î¹ X i) (Sigma.Î¹ X j)) â‰… op (âŠ¥_ _) :=
      (initialIsoIsInitial (hd i j hi)).op
    let iâ‚‚ : op (âŠ¥_ C) â‰… (âŠ¤_ Cáµ’áµ–) :=
      (terminalIsoIsTerminal (terminalOpOfInitial initialIsInitial)).symm
    apply_fun (F.mapIso iâ‚ â‰ªâ‰« F.mapIso iâ‚‚ â‰ªâ‰« (PreservesTerminal.iso F)).hom using
      injective_of_mono _
    simp

end Preserves

open Preserves

/--
If `F` is a presheaf which `IsSheafFor` a presieve of arrows and the empty presieve, then it
preserves the product corresponding to the presieve of arrows.
-/
noncomputable
def preservesProductOfIsSheafFor (hF' : (ofArrows X (fun i â†¦ Sigma.Î¹ X i)).IsSheafFor F) :
    PreservesLimit (Discrete.functor (fun x â†¦ op (X x))) F := by
  refine @PreservesProduct.ofIsoComparison _ _ _ _ F _ (fun x â†¦ op (X x)) _ _ ?_
  rw [â† piComparison_fac F]
  refine @IsIso.comp_isIso _ _ _ _ _ _ _ inferInstance (@IsIso.comp_isIso _ _ _ _ _ _ _ ?_ ?_)
  Â· rw [isIso_iff_bijective, Function.bijective_iff_existsUnique]
    rw [Equalizer.Presieve.sheaf_condition, Limits.Types.type_equalizer_iff_unique] at hF'
    exact fun b â†¦ hF' b (congr_fun (firstMap_eq_secondMap F hF X hd) b)
  Â· exact iso_prodMap F hF X hd

end CategoryTheory.Presieve
