/-
Copyright (c) 2023 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/

import Mathlib.CategoryTheory.Sites.Sieves
import Mathlib.CategoryTheory.Limits.Shapes.KernelPair
import Mathlib.Tactic.ApplyFun

/-!

# Effective epimorphisms

We define the notion of effective epimorphic (pre)sieves, morphisms and family of morphisms
and provide some API for relating the three notions.

More precisely, if `f` is a morphism, then `f` is an effective epi if and only if the sieve
it generates is effective epimorphic; see `CategoryTheory.Sieve.effectiveEpimorphic_singleton`.
The analogous statement for a family of morphisms is in the theorem
`CategoryTheory.Sieve.effectiveEpimorphic_family`.

We have defined the notion of effective epi for morphisms and families of morphisms in such a
way that avoids requiring the existence of pullbacks. However, if the relevant pullbacks exist
then these definitions are equivalent, see `effectiveEpiStructOfRegularEpi`,
`regularEpiOfEffectiveEpi`, and `effectiveEpiOfKernelPair`.
See [nlab: *Effective Epimorphism*](https://ncatlab.org/nlab/show/effective+epimorphism) and
[Stacks 00WP](https://stacks.math.columbia.edu/tag/00WP) for the standard definitions. Note that
our notion of `EffectiveEpi`Â is often called "strict epi" in the literature.

## References
- [Elephant]: *Sketches of an Elephant*, P. T. Johnstone: C2.1, Example 2.1.12.
- [nlab: *Effective Epimorphism*](https://ncatlab.org/nlab/show/effective+epimorphism) and
- [Stacks 00WP](https://stacks.math.columbia.edu/tag/00WP) for the standard definitions.

## TODO
- Find sufficient conditions on functors to preserve/reflect effective epis.

-/

namespace CategoryTheory

open Limits

variable {C : Type*} [Category C]

/-- A sieve is effective epimorphic if the associated cocone is a colimit cocone. -/
def Sieve.EffectiveEpimorphic {X : C} (S : Sieve X) : Prop :=
  Nonempty (IsColimit (S : Presieve X).cocone)

/-- A presieve is effective epimorphic if the cocone associated to the sieve it generates
is a colimit cocone. -/
abbrev Presieve.EffectiveEpimorphic {X : C} (S : Presieve X) : Prop :=
  (Sieve.generate S).EffectiveEpimorphic

/--
The sieve of morphisms which factor through a given morphism `f`.
This is equal to `Sieve.generate (Presieve.singleton f)`, but has
more convenient definitional properties.
-/
def Sieve.generateSingleton {X Y : C} (f : Y âŸ¶ X) : Sieve X where
  arrows Z := { g | âˆƒ (e : Z âŸ¶ Y), e â‰« f = g }
  downward_closed := by
    rintro W Z g âŸ¨e,rflâŸ© q
    exact âŸ¨q â‰« e, by simpâŸ©

lemma Sieve.generateSingleton_eq {X Y : C} (f : Y âŸ¶ X) :
    Sieve.generate (Presieve.singleton f) = Sieve.generateSingleton f := by
  ext Z g
  constructor
  Â· rintro âŸ¨W,i,p,âŸ¨âŸ©,rflâŸ©
    exact âŸ¨i,rflâŸ©
  Â· rintro âŸ¨g,hâŸ©
    exact âŸ¨Y,g,f,âŸ¨âŸ©,hâŸ©

/--
This structure encodes the data required for a morphism to be an effective epimorphism.
-/
structure EffectiveEpiStruct {X Y : C} (f : Y âŸ¶ X) where
  desc : âˆ€ {W : C} (e : Y âŸ¶ W),
    (âˆ€ {Z : C} (gâ‚ gâ‚‚ : Z âŸ¶ Y), gâ‚ â‰« f = gâ‚‚ â‰« f â†’ gâ‚ â‰« e = gâ‚‚ â‰« e) â†’ (X âŸ¶ W)
  fac : âˆ€ {W : C} (e : Y âŸ¶ W)
    (h : âˆ€ {Z : C} (gâ‚ gâ‚‚ : Z âŸ¶ Y), gâ‚ â‰« f = gâ‚‚ â‰« f â†’ gâ‚ â‰« e = gâ‚‚ â‰« e),
    f â‰« desc e h = e
  uniq : âˆ€ {W : C} (e : Y âŸ¶ W)
    (h : âˆ€ {Z : C} (gâ‚ gâ‚‚ : Z âŸ¶ Y), gâ‚ â‰« f = gâ‚‚ â‰« f â†’ gâ‚ â‰« e = gâ‚‚ â‰« e)
    (m : X âŸ¶ W), f â‰« m = e â†’ m = desc e h

attribute [nolint docBlame]
  EffectiveEpiStruct.desc
  EffectiveEpiStruct.fac
  EffectiveEpiStruct.uniq

/--
A morphism `f : Y âŸ¶ X` is an effective epimorphism provided that `f` exhibits `X` as a colimit
of the diagram of all "relations" `R â‡‰ Y`.
If `f` has a kernel pair, then this is equivalent to showing that the corresponding cofork is
a colimit.
-/
class EffectiveEpi {X Y : C} (f : Y âŸ¶ X) : Prop where
  effectiveEpi : Nonempty (EffectiveEpiStruct f)

attribute [nolint docBlame] EffectiveEpi.effectiveEpi

/-- Some chosen `EffectiveEpiStruct` associated to an effective epi. -/
noncomputable
def EffectiveEpi.getStruct {X Y : C} (f : Y âŸ¶ X) [EffectiveEpi f] : EffectiveEpiStruct f :=
  EffectiveEpi.effectiveEpi.some

/-- Descend along an effective epi. -/
noncomputable
def EffectiveEpi.desc {X Y W : C} (f : Y âŸ¶ X) [EffectiveEpi f]
    (e : Y âŸ¶ W) (h : âˆ€ {Z : C} (gâ‚ gâ‚‚ : Z âŸ¶ Y), gâ‚ â‰« f = gâ‚‚ â‰« f â†’ gâ‚ â‰« e = gâ‚‚ â‰« e) :
    X âŸ¶ W := (EffectiveEpi.getStruct f).desc e h

@[reassoc (attr := simp)]
lemma EffectiveEpi.fac {X Y W : C} (f : Y âŸ¶ X) [EffectiveEpi f]
    (e : Y âŸ¶ W) (h : âˆ€ {Z : C} (gâ‚ gâ‚‚ : Z âŸ¶ Y), gâ‚ â‰« f = gâ‚‚ â‰« f â†’ gâ‚ â‰« e = gâ‚‚ â‰« e) :
    f â‰« EffectiveEpi.desc f e h = e :=
  (EffectiveEpi.getStruct f).fac e h

lemma EffectiveEpi.uniq {X Y W : C} (f : Y âŸ¶ X) [EffectiveEpi f]
    (e : Y âŸ¶ W) (h : âˆ€ {Z : C} (gâ‚ gâ‚‚ : Z âŸ¶ Y), gâ‚ â‰« f = gâ‚‚ â‰« f â†’ gâ‚ â‰« e = gâ‚‚ â‰« e)
    (m : X âŸ¶ W) (hm : f â‰« m = e) :
    m = EffectiveEpi.desc f e h :=
  (EffectiveEpi.getStruct f).uniq e h _ hm

instance epiOfEffectiveEpi {X Y : C} (f : Y âŸ¶ X) [EffectiveEpi f] : Epi f := by
  constructor
  intro W mâ‚ mâ‚‚ h
  have : mâ‚‚ = EffectiveEpi.desc f (f â‰« mâ‚‚)
    (fun {Z} gâ‚ gâ‚‚ h => by simp only [â† Category.assoc, h]) := EffectiveEpi.uniq _ _ _ _ rfl
  rw [this]
  exact EffectiveEpi.uniq _ _ _ _ h

/--
Implementation: This is a construction which will be used in the proof that
the sieve generated by a single arrow is effective epimorphic if and only if
the arrow is an effective epi.
-/
def isColimitOfEffectiveEpiStruct {X Y : C} (f : Y âŸ¶ X) (Hf : EffectiveEpiStruct f) :
    IsColimit (Sieve.generateSingleton f : Presieve X).cocone :=
  letI D := FullSubcategory fun T : Over X => Sieve.generateSingleton f T.hom
  letI F : D â¥¤ _ := (Sieve.generateSingleton f).arrows.diagram
  { desc := fun S => Hf.desc (S.Î¹.app âŸ¨Over.mk f, âŸ¨ğŸ™ _, by simpâŸ©âŸ©) <| by
      intro Z gâ‚ gâ‚‚ h
      let Y' : D := âŸ¨Over.mk f, ğŸ™ _, by simpâŸ©
      let Z' : D := âŸ¨Over.mk (gâ‚ â‰« f), gâ‚, rflâŸ©
      let gâ‚' : Z' âŸ¶ Y' := Over.homMk gâ‚
      let gâ‚‚' : Z' âŸ¶ Y' := Over.homMk gâ‚‚ (by simp [h])
      change F.map gâ‚' â‰« _ = F.map gâ‚‚' â‰« _
      simp only [S.w]
    fac := by
      rintro S âŸ¨T,g,hTâŸ©
      dsimp
      nth_rewrite 1 [â† hT, Category.assoc, Hf.fac]
      let y : D := âŸ¨Over.mk f, ğŸ™ _, by simpâŸ©
      let x : D := âŸ¨Over.mk T.hom, g, hTâŸ©
      let g' : x âŸ¶ y := Over.homMk g
      change F.map g' â‰« _ = _
      rw [S.w]
      rfl
    uniq := by
      intro S m hm
      dsimp
      generalize_proofs h1 h2
      apply Hf.uniq _ h2
      exact hm âŸ¨Over.mk f, ğŸ™ _, by simpâŸ© }

/--
Implementation: This is a construction which will be used in the proof that
the sieve generated by a single arrow is effective epimorphic if and only if
the arrow is an effective epi.
-/
noncomputable
def effectiveEpiStructOfIsColimit {X Y : C} (f : Y âŸ¶ X)
    (Hf : IsColimit (Sieve.generateSingleton f : Presieve X).cocone) :
    EffectiveEpiStruct f :=
  let aux {W : C} (e : Y âŸ¶ W)
    (h : âˆ€ {Z : C} (gâ‚ gâ‚‚ : Z âŸ¶ Y), gâ‚ â‰« f = gâ‚‚ â‰« f â†’ gâ‚ â‰« e = gâ‚‚ â‰« e) :
    Cocone (Sieve.generateSingleton f).arrows.diagram :=
    { pt := W
      Î¹ := {
        app := fun âŸ¨T,hTâŸ© => hT.choose â‰« e
        naturality := by
          rintro âŸ¨A,hAâŸ© âŸ¨B,hBâŸ© (q : A âŸ¶ B)
          dsimp; simp only [â† Category.assoc, Category.comp_id]
          apply h
          rw [Category.assoc, hB.choose_spec, hA.choose_spec, Over.w] } }
  { desc := fun {W} e h => Hf.desc (aux e h)
    fac := by
      intro W e h
      dsimp
      have := Hf.fac (aux e h) âŸ¨Over.mk f, ğŸ™ _, by simpâŸ©
      dsimp at this; rw [this]; clear this
      nth_rewrite 2 [â† Category.id_comp e]
      apply h
      generalize_proofs hh
      rw [hh.choose_spec, Category.id_comp]
    uniq := by
      intro W e h m hm
      dsimp
      apply Hf.uniq (aux e h)
      rintro âŸ¨A,g,hAâŸ©
      dsimp
      nth_rewrite 1 [â† hA, Category.assoc, hm]
      apply h
      generalize_proofs hh
      rwa [hh.choose_spec] }

theorem Sieve.effectiveEpimorphic_singleton {X Y : C} (f : Y âŸ¶ X) :
    (Presieve.singleton f).EffectiveEpimorphic â†” (EffectiveEpi f) := by
  constructor
  Â· intro (h : Nonempty _)
    rw [Sieve.generateSingleton_eq] at h
    constructor
    apply Nonempty.map (effectiveEpiStructOfIsColimit _) h
  Â· rintro âŸ¨hâŸ©
    show Nonempty _
    rw [Sieve.generateSingleton_eq]
    apply Nonempty.map (isColimitOfEffectiveEpiStruct _) h

/--
The sieve of morphisms which factor through a morphism in a given family.
This is equal to `Sieve.generate (Presieve.ofArrows X Ï€)`, but has
more convenient definitional properties.
-/
def Sieve.generateFamily {B : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) :
    Sieve B where
  arrows Y := { f | âˆƒ (a : Î±) (g : Y âŸ¶ X a), g â‰« Ï€ a = f }
  downward_closed := by
    rintro Yâ‚ Yâ‚‚ gâ‚ âŸ¨a,q,rflâŸ© e
    exact âŸ¨a, e â‰« q, by simpâŸ©

lemma Sieve.generateFamily_eq {B : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) :
    Sieve.generate (Presieve.ofArrows X Ï€) = Sieve.generateFamily X Ï€ := by
  ext Y g
  constructor
  Â· rintro âŸ¨W, g, f, âŸ¨aâŸ©, rflâŸ©
    exact âŸ¨a, g, rflâŸ©
  Â· rintro âŸ¨a, g, rflâŸ©
    exact âŸ¨_, g, Ï€ a, âŸ¨aâŸ©, rflâŸ©

/--
This structure encodes the data required for a family of morphisms to be effective epimorphic.
-/
structure EffectiveEpiFamilyStruct {B : C} {Î± : Type*}
    (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) where
  desc : âˆ€ {W} (e : (a : Î±) â†’ (X a âŸ¶ W)),
          (âˆ€ {Z : C} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ X aâ‚) (gâ‚‚ : Z âŸ¶ X aâ‚‚),
      gâ‚ â‰« Ï€ _ = gâ‚‚ â‰« Ï€ _ â†’ gâ‚ â‰« e _ = gâ‚‚ â‰« e _) â†’ (B âŸ¶ W)
  fac : âˆ€ {W} (e : (a : Î±) â†’ (X a âŸ¶ W))
          (h : âˆ€ {Z : C} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ X aâ‚) (gâ‚‚ : Z âŸ¶ X aâ‚‚),
            gâ‚ â‰« Ï€ _ = gâ‚‚ â‰« Ï€ _ â†’ gâ‚ â‰« e _ = gâ‚‚ â‰« e _)
          (a : Î±), Ï€ a â‰« desc e h = e a
  uniq : âˆ€ {W} (e : (a : Î±) â†’ (X a âŸ¶ W))
          (h : âˆ€ {Z : C} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ X aâ‚) (gâ‚‚ : Z âŸ¶ X aâ‚‚),
            gâ‚ â‰« Ï€ _ = gâ‚‚ â‰« Ï€ _ â†’ gâ‚ â‰« e _ = gâ‚‚ â‰« e _)
          (m : B âŸ¶ W), (âˆ€ (a : Î±), Ï€ a â‰« m = e a) â†’ m = desc e h

attribute [nolint docBlame]
  EffectiveEpiFamilyStruct.desc
  EffectiveEpiFamilyStruct.fac
  EffectiveEpiFamilyStruct.uniq

/--
A family of morphisms `f a : X a âŸ¶ B` indexed by `Î±` is effective epimorphic
provided that the `f a` exhibit `B` as a colimit of the diagram of all "relations"
`R â†’ X aâ‚`, `R âŸ¶ X aâ‚‚` for all `aâ‚ aâ‚‚ : Î±`.
-/
class EffectiveEpiFamily {B : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) : Prop where
  effectiveEpiFamily : Nonempty (EffectiveEpiFamilyStruct X Ï€)

attribute [nolint docBlame] EffectiveEpiFamily.effectiveEpiFamily

/-- Some chosen `EffectiveEpiFamilyStruct` associated to an effective epi family. -/
noncomputable
def EffectiveEpiFamily.getStruct {B : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B))
    [EffectiveEpiFamily X Ï€] : EffectiveEpiFamilyStruct X Ï€ :=
  EffectiveEpiFamily.effectiveEpiFamily.some

/-- Descend along an effective epi family. -/
noncomputable
def EffectiveEpiFamily.desc {B W : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B))
    [EffectiveEpiFamily X Ï€] (e : (a : Î±) â†’ (X a âŸ¶ W))
    (h : âˆ€ {Z : C} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ X aâ‚) (gâ‚‚ : Z âŸ¶ X aâ‚‚),
      gâ‚ â‰« Ï€ _ = gâ‚‚ â‰« Ï€ _ â†’ gâ‚ â‰« e _ = gâ‚‚ â‰« e _) : B âŸ¶ W :=
  (EffectiveEpiFamily.getStruct X Ï€).desc e h

@[reassoc (attr := simp)]
lemma EffectiveEpiFamily.fac {B W : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B))
    [EffectiveEpiFamily X Ï€] (e : (a : Î±) â†’ (X a âŸ¶ W))
    (h : âˆ€ {Z : C} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ X aâ‚) (gâ‚‚ : Z âŸ¶ X aâ‚‚),
      gâ‚ â‰« Ï€ _ = gâ‚‚ â‰« Ï€ _ â†’ gâ‚ â‰« e _ = gâ‚‚ â‰« e _) (a : Î±) :
    Ï€ a â‰« EffectiveEpiFamily.desc X Ï€ e h = e a :=
  (EffectiveEpiFamily.getStruct X Ï€).fac e h a

/-
NOTE: The `simpNF` linter complains for some reason. See the two examples below.
Zulip discussion:
https://leanprover.zulipchat.com/#narrow/stream/287929-mathlib4/topic/simpNF.20bug.3F
-/
attribute [nolint simpNF]
  EffectiveEpiFamily.fac
  EffectiveEpiFamily.fac_assoc

/-- The effective epi family structure on the identity -/
def effectiveEpiFamilyStructId {Î± : Unit â†’ C} : EffectiveEpiFamilyStruct Î± (fun _ => ğŸ™ (Î± ())) where
  desc := fun e _ => e ()
  fac := by aesop_cat
  uniq := by aesop_cat

instance {X : C} : EffectiveEpiFamily (fun _ => X : Unit â†’ C) (fun _ => ğŸ™ X) :=
  âŸ¨âŸ¨effectiveEpiFamilyStructIdâŸ©âŸ©

example {B W : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B))
    [EffectiveEpiFamily X Ï€] (e : (a : Î±) â†’ (X a âŸ¶ W))
    (h : âˆ€ {Z : C} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ X aâ‚) (gâ‚‚ : Z âŸ¶ X aâ‚‚),
      gâ‚ â‰« Ï€ _ = gâ‚‚ â‰« Ï€ _ â†’ gâ‚ â‰« e _ = gâ‚‚ â‰« e _) (a : Î±) :
    Ï€ a â‰« EffectiveEpiFamily.desc X Ï€ e h = e a :=
  by simp

example {B W Q : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B))
    [EffectiveEpiFamily X Ï€] (e : (a : Î±) â†’ (X a âŸ¶ W))
    (h : âˆ€ {Z : C} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ X aâ‚) (gâ‚‚ : Z âŸ¶ X aâ‚‚),
      gâ‚ â‰« Ï€ _ = gâ‚‚ â‰« Ï€ _ â†’ gâ‚ â‰« e _ = gâ‚‚ â‰« e _) (a : Î±)
    (q : W âŸ¶ Q) :
    Ï€ a â‰« EffectiveEpiFamily.desc X Ï€ e h â‰« q = e a â‰« q :=
  by simp

lemma EffectiveEpiFamily.uniq {B W : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B))
    [EffectiveEpiFamily X Ï€] (e : (a : Î±) â†’ (X a âŸ¶ W))
    (h : âˆ€ {Z : C} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ X aâ‚) (gâ‚‚ : Z âŸ¶ X aâ‚‚),
      gâ‚ â‰« Ï€ _ = gâ‚‚ â‰« Ï€ _ â†’ gâ‚ â‰« e _ = gâ‚‚ â‰« e _)
    (m : B âŸ¶ W) (hm : âˆ€ a, Ï€ a â‰« m = e a) :
    m = EffectiveEpiFamily.desc X Ï€ e h :=
  (EffectiveEpiFamily.getStruct X Ï€).uniq e h m hm

-- TODO: Once we have "jointly epimorphic families", we could rephrase this as such a property.
lemma EffectiveEpiFamily.hom_ext {B W : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B))
    [EffectiveEpiFamily X Ï€] (mâ‚ mâ‚‚ : B âŸ¶ W) (h : âˆ€ a, Ï€ a â‰« mâ‚ = Ï€ a â‰« mâ‚‚) :
    mâ‚ = mâ‚‚ := by
  have : mâ‚‚ = EffectiveEpiFamily.desc X Ï€ (fun a => Ï€ a â‰« mâ‚‚)
      (fun aâ‚ aâ‚‚ gâ‚ gâ‚‚ h => by simp only [â† Category.assoc, h]) := by
    apply EffectiveEpiFamily.uniq; intro; rfl
  rw [this]
  exact EffectiveEpiFamily.uniq _ _ _ _ _ h

instance epiCoproductDescOfEffectiveEpiFamily {B : C} {Î± : Type*}
    (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) [EffectiveEpiFamily X Ï€] [HasCoproduct X] :
    Epi (Sigma.desc Ï€) := by
  constructor
  intro Z g h H
  apply EffectiveEpiFamily.hom_ext X Ï€
  intro a
  suffices (Sigma.Î¹ _ a â‰« Sigma.desc Ï€) â‰« g = (Sigma.Î¹ _ a â‰« Sigma.desc Ï€) â‰« h by
    simpa only [colimit.Î¹_desc] using this
  simp only [Category.assoc, H]

/--
Implementation: This is a construction which will be used in the proof that
the sieve generated by a family of arrows is effective epimorphic if and only if
the family is an effective epi.
-/
def isColimitOfEffectiveEpiFamilyStruct {B : C} {Î± : Type*}
    (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) (H : EffectiveEpiFamilyStruct X Ï€) :
    IsColimit (Sieve.generateFamily X Ï€ : Presieve B).cocone :=
  letI D := FullSubcategory fun T : Over B => Sieve.generateFamily X Ï€ T.hom
  letI F : D â¥¤ _ := (Sieve.generateFamily X Ï€).arrows.diagram
  { desc := fun S => H.desc (fun a => S.Î¹.app âŸ¨Over.mk (Ï€ a), âŸ¨a,ğŸ™ _, by simpâŸ©âŸ©) <| by
      intro Z aâ‚ aâ‚‚ gâ‚ gâ‚‚ h
      dsimp
      let Aâ‚ : D := âŸ¨Over.mk (Ï€ aâ‚), aâ‚, ğŸ™ _, by simpâŸ©
      let Aâ‚‚ : D := âŸ¨Over.mk (Ï€ aâ‚‚), aâ‚‚, ğŸ™ _, by simpâŸ©
      let Z' : D := âŸ¨Over.mk (gâ‚ â‰« Ï€ aâ‚), aâ‚, gâ‚, rflâŸ©
      let iâ‚ : Z' âŸ¶ Aâ‚ := Over.homMk gâ‚
      let iâ‚‚ : Z' âŸ¶ Aâ‚‚ := Over.homMk gâ‚‚
      change F.map iâ‚ â‰« _ = F.map iâ‚‚ â‰« _
      simp only [S.w]
    fac := by
      intro S âŸ¨T, a, (g : T.left âŸ¶ X a), hTâŸ©
      dsimp
      nth_rewrite 1 [â† hT, Category.assoc, H.fac]
      let A : D := âŸ¨Over.mk (Ï€ a), a, ğŸ™ _, by simpâŸ©
      let B : D := âŸ¨Over.mk T.hom, a, g, hTâŸ©
      let i : B âŸ¶ A := Over.homMk g
      change F.map i â‰« _ = _
      rw [S.w]
      rfl
    uniq := by
      intro S m hm; dsimp
      apply H.uniq
      intro a
      exact hm âŸ¨Over.mk (Ï€ a), a, ğŸ™ _, by simpâŸ© }

/--
Implementation: This is a construction which will be used in the proof that
the sieve generated by a family of arrows is effective epimorphic if and only if
the family is an effective epi.
-/
noncomputable
def effectiveEpiFamilyStructOfIsColimit {B : C} {Î± : Type*}
    (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B))
    (H : IsColimit (Sieve.generateFamily X Ï€ : Presieve B).cocone) :
    EffectiveEpiFamilyStruct X Ï€ :=
  let aux {W : C} (e : (a : Î±) â†’ (X a âŸ¶ W))
    (h : âˆ€ {Z : C} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ X aâ‚) (gâ‚‚ : Z âŸ¶ X aâ‚‚),
      gâ‚ â‰« Ï€ _ = gâ‚‚ â‰« Ï€ _ â†’ gâ‚ â‰« e _ = gâ‚‚ â‰« e _) :
    Cocone (Sieve.generateFamily X Ï€).arrows.diagram := {
      pt := W
      Î¹ := {
        app := fun âŸ¨T,hTâŸ© => hT.choose_spec.choose â‰« e hT.choose
        naturality := by
          intro âŸ¨A,a,(gâ‚ : A.left âŸ¶ _),haâŸ© âŸ¨B,b,(gâ‚‚ : B.left âŸ¶ _),hbâŸ© (q : A âŸ¶ B)
          dsimp; rw [Category.comp_id, â† Category.assoc]
          apply h; rw [Category.assoc]
          generalize_proofs h1 h2 h3 h4
          rw [h2.choose_spec, h4.choose_spec, Over.w] } }
  { desc := fun {W} e h => H.desc (aux e h)
    fac := by
      intro W e h a
      dsimp
      have := H.fac (aux e h) âŸ¨Over.mk (Ï€ a), a, ğŸ™ _, by simpâŸ©
      dsimp at this; rw [this]; clear this
      conv_rhs => rw [â† Category.id_comp (e a)]
      apply h
      generalize_proofs h1 h2
      rw [h2.choose_spec, Category.id_comp]
    uniq := by
      intro W e h m hm
      apply H.uniq (aux e h)
      rintro âŸ¨T, a, (g : T.left âŸ¶ _), haâŸ©
      dsimp
      nth_rewrite 1 [â† ha, Category.assoc, hm]
      apply h
      generalize_proofs h1 h2
      rwa [h2.choose_spec] }

theorem Sieve.effectiveEpimorphic_family {B : C} {Î± : Type*}
    (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) :
    (Presieve.ofArrows X Ï€).EffectiveEpimorphic â†” EffectiveEpiFamily X Ï€ := by
  constructor
  Â· intro (h : Nonempty _)
    rw [Sieve.generateFamily_eq] at h
    constructor
    apply Nonempty.map (effectiveEpiFamilyStructOfIsColimit _ _) h
  Â· rintro âŸ¨hâŸ©
    show Nonempty _
    rw [Sieve.generateFamily_eq]
    apply Nonempty.map (isColimitOfEffectiveEpiFamilyStruct _ _) h


section instances

/--
Given an `EffectiveEpiFamily X Ï€` such that the coproduct of `X` exists, `Sigma.desc Ï€` is an
`EffectiveEpi`.
-/
noncomputable
def effectiveEpiStructDescOfEffectiveEpiFamily {B : C} {Î± : Type*} (X : Î± â†’ C)
    (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) [HasCoproduct X] [EffectiveEpiFamily X Ï€] :
    EffectiveEpiStruct (Sigma.desc Ï€) where
  desc e h := EffectiveEpiFamily.desc X Ï€ (fun a â†¦ Sigma.Î¹ X a â‰« e) (fun aâ‚ aâ‚‚ gâ‚ gâ‚‚ hg â†¦ by
    simp only [â† Category.assoc]
    apply h (gâ‚ â‰« Sigma.Î¹ X aâ‚) (gâ‚‚ â‰« Sigma.Î¹ X aâ‚‚)
    simpa only [Category.assoc, colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app])
  fac e h := by
    ext a
    simp only [colimit.Î¹_desc_assoc, Discrete.functor_obj, Cofan.mk_pt, Cofan.mk_Î¹_app,
      EffectiveEpiFamily.fac]
  uniq e _ m hm := by
    have := EffectiveEpiFamily.uniq X Ï€ (fun a â†¦ Sigma.Î¹ X a â‰« e) ?_ m
    Â· apply this
      intro a
      rw [â† hm]
      simp only [colimit.Î¹_desc_assoc, Discrete.functor_obj, Cofan.mk_pt, Cofan.mk_Î¹_app]
    Â· intro Z aâ‚ aâ‚‚ gâ‚ gâ‚‚ hg
      rw [â† hm]
      simp only [colimit.Î¹_desc_assoc, Discrete.functor_obj, Cofan.mk_pt, Cofan.mk_Î¹_app]
      rw [â† Category.assoc, hg, Category.assoc]

instance {B : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) [HasCoproduct X]
    [EffectiveEpiFamily X Ï€] : EffectiveEpi (Sigma.desc Ï€) :=
  âŸ¨âŸ¨effectiveEpiStructDescOfEffectiveEpiFamily X Ï€âŸ©âŸ©

/--
This is an auxiliary lemma used twice in the definition of  `EffectiveEpiFamilyOfEffectiveEpiDesc`.
It is the `h` hypothesis of `EffectiveEpi.desc`Â and `EffectiveEpi.fac`.Â 
-/
theorem effectiveEpiFamilyStructOfEffectiveEpiDesc_aux {B : C} {Î± : Type*} {X : Î± â†’ C}
    {Ï€ : (a : Î±) â†’ X a âŸ¶ B} [HasCoproduct X]
    [âˆ€ {Z : C} (g : Z âŸ¶ âˆ X) (a : Î±), HasPullback g (Sigma.Î¹ X a)]
    [âˆ€ {Z : C} (g : Z âŸ¶ âˆ X), HasCoproduct fun a â†¦ pullback g (Sigma.Î¹ X a)]
    [âˆ€ {Z : C} (g : Z âŸ¶ âˆ X), Epi (Sigma.desc fun a â†¦ pullback.fst (f := g) (g := (Sigma.Î¹ X a)))]
    {W : C} {e : (a : Î±) â†’ X a âŸ¶ W} (h : âˆ€ {Z : C} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ X aâ‚) (gâ‚‚ : Z âŸ¶ X aâ‚‚),
      gâ‚ â‰« Ï€ aâ‚ = gâ‚‚ â‰« Ï€ aâ‚‚ â†’ gâ‚ â‰« e aâ‚ = gâ‚‚ â‰« e aâ‚‚) {Z : C}
    {gâ‚ gâ‚‚ : Z âŸ¶ âˆ fun b â†¦ X b} (hg : gâ‚ â‰« Sigma.desc Ï€ = gâ‚‚ â‰« Sigma.desc Ï€) :
    gâ‚ â‰« Sigma.desc e = gâ‚‚ â‰« Sigma.desc e := by
  apply_fun ((Sigma.desc fun a â†¦ pullback.fst (f := gâ‚) (g := (Sigma.Î¹ X a))) â‰« Â·) using
    (fun a b â†¦ (cancel_epi _).mp)
  ext a
  simp only [colimit.Î¹_desc_assoc, Discrete.functor_obj, Cofan.mk_pt, Cofan.mk_Î¹_app]
  rw [â† Category.assoc, pullback.condition]
  simp only [Category.assoc, colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app]
  apply_fun ((Sigma.desc fun a â†¦ pullback.fst (f := pullback.fst â‰« gâ‚‚)
    (g := (Sigma.Î¹ X a))) â‰« Â·) using (fun a b â†¦ (cancel_epi _).mp)
  ext b
  simp only [colimit.Î¹_desc_assoc, Discrete.functor_obj, Cofan.mk_pt, Cofan.mk_Î¹_app]
  simp only [â† Category.assoc]
  rw [(Category.assoc _ _ gâ‚‚), pullback.condition]
  simp only [Category.assoc, colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app]
  rw [â† Category.assoc]
  apply h
  apply_fun (pullback.fst (f := gâ‚) (g := (Sigma.Î¹ X a)) â‰« Â·) at hg
  rw [â† Category.assoc, pullback.condition] at hg
  simp only [Category.assoc, colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app] at hg
  apply_fun ((Sigma.Î¹ (fun a â†¦ pullback _ _) b) â‰« (Sigma.desc fun a â†¦ pullback.fst
    (f := pullback.fst â‰« gâ‚‚) (g := (Sigma.Î¹ X a))) â‰« Â·) at hg
  simp only [colimit.Î¹_desc_assoc, Discrete.functor_obj, Cofan.mk_pt, Cofan.mk_Î¹_app] at hg
  simp only [â† Category.assoc] at hg
  rw [(Category.assoc _ _ gâ‚‚), pullback.condition] at hg
  simpa using hg

/--
If a coproduct interacts well enough with pullbacks, then a family whose domains are the terms of
the coproduct is effective epimorphic whenever `Sigma.desc` induces an effective epimorphism from
the coproduct itself.
-/
noncomputable
def effectiveEpiFamilyStructOfEffectiveEpiDesc {B : C} {Î± : Type*} (X : Î± â†’ C)
    (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) [HasCoproduct X] [EffectiveEpi (Sigma.desc Ï€)]
    [âˆ€ {Z : C} (g : Z âŸ¶ âˆ X) (a : Î±), HasPullback g (Sigma.Î¹ X a)]
    [âˆ€ {Z : C} (g : Z âŸ¶ âˆ X), HasCoproduct (fun a â†¦ pullback g (Sigma.Î¹ X a))]
    [âˆ€ {Z : C} (g : Z âŸ¶ âˆ X),
      Epi (Sigma.desc (fun a â†¦ pullback.fst (f := g) (g := (Sigma.Î¹ X a))))] :
    EffectiveEpiFamilyStruct X Ï€ where
  desc e h := EffectiveEpi.desc (Sigma.desc Ï€) (Sigma.desc e) fun _ _ hg â†¦
    effectiveEpiFamilyStructOfEffectiveEpiDesc_aux h hg
  fac e h a := by
    rw [(by simp : Ï€ a = Sigma.Î¹ X a â‰« Sigma.desc Ï€), (by simp : e a = Sigma.Î¹ X a â‰« Sigma.desc e),
      Category.assoc, EffectiveEpi.fac (Sigma.desc Ï€) (Sigma.desc e) (fun gâ‚ gâ‚‚ hg â†¦
      effectiveEpiFamilyStructOfEffectiveEpiDesc_aux h hg)]
  uniq _ _ _ hm := by
    apply EffectiveEpi.uniq (Sigma.desc Ï€)
    ext
    simpa using hm _

/--
An `EffectiveEpiFamily` consisting of a single `EffectiveEpi`
-/
noncomputable
def effectiveEpiFamilyStructSingletonOfEffectiveEpi {B X : C} (f : X âŸ¶ B) [EffectiveEpi f] :
    EffectiveEpiFamilyStruct (fun () â†¦ X) (fun () â†¦ f) where
  desc e h := EffectiveEpi.desc f (e ()) (fun gâ‚ gâ‚‚ hg â†¦ h () () gâ‚ gâ‚‚ hg)
  fac e h := fun _ â†¦ EffectiveEpi.fac f (e ()) (fun gâ‚ gâ‚‚ hg â†¦ h () () gâ‚ gâ‚‚ hg)
  uniq e h m hm := by apply EffectiveEpi.uniq f (e ()) (h () ()); exact hm ()

instance {B X : C} (f : X âŸ¶ B) [EffectiveEpi f] : EffectiveEpiFamily (fun () â†¦ X) (fun () â†¦ f) :=
  âŸ¨âŸ¨effectiveEpiFamilyStructSingletonOfEffectiveEpi fâŸ©âŸ©

/--
A single element `EffectiveEpiFamily`Â constists of an `EffectiveEpi`
-/
noncomputable
def effectiveEpiStructOfEffectiveEpiFamilySingleton {B X : C} (f : X âŸ¶ B)
    [EffectiveEpiFamily (fun () â†¦ X) (fun () â†¦ f)] :
    EffectiveEpiStruct f where
  desc e h := EffectiveEpiFamily.desc
    (fun () â†¦ X) (fun () â†¦ f) (fun () â†¦ e) (fun _ _ gâ‚ gâ‚‚ hg â†¦ h gâ‚ gâ‚‚ hg)
  fac e h := EffectiveEpiFamily.fac
    (fun () â†¦ X) (fun () â†¦ f) (fun () â†¦ e) (fun _ _ gâ‚ gâ‚‚ hg â†¦ h gâ‚ gâ‚‚ hg) ()
  uniq e h m hm := EffectiveEpiFamily.uniq
    (fun () â†¦ X) (fun () â†¦ f) (fun () â†¦ e) (fun _ _ gâ‚ gâ‚‚ hg â†¦ h gâ‚ gâ‚‚ hg) m (fun _ â†¦ hm)

instance {B X : C} (f : X âŸ¶ B) [EffectiveEpiFamily (fun () â†¦ X) (fun () â†¦ f)] :
    EffectiveEpi f :=
  âŸ¨âŸ¨effectiveEpiStructOfEffectiveEpiFamilySingleton fâŸ©âŸ©

theorem effectiveEpi_iff_effectiveEpiFamily {B X : C} (f : X âŸ¶ B) :
    EffectiveEpi f â†” EffectiveEpiFamily (fun () â†¦ X) (fun () â†¦ f) :=
  âŸ¨fun _ â†¦ inferInstance, fun _ â†¦ inferInstanceâŸ©

/--
A family of morphisms with the same target inducing an isomorphism from the coproduct to the target
is an `EffectiveEpiFamily`.
-/
noncomputable
def effectiveEpiFamilyStructOfIsIsoDesc {B : C} {Î± : Type*} (X : Î± â†’ C)
    (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) [HasCoproduct X] [IsIso (Sigma.desc Ï€)] :
    EffectiveEpiFamilyStruct X Ï€ where
  desc e _ := (asIso (Sigma.desc Ï€)).inv â‰« (Sigma.desc e)
  fac e h := by
    intro a
    have : Ï€ a = Sigma.Î¹ X a â‰« (asIso (Sigma.desc Ï€)).hom := by simp only [asIso_hom,
      colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app]
    rw [this, Category.assoc]
    simp only [asIso_hom, asIso_inv, IsIso.hom_inv_id_assoc, colimit.Î¹_desc, Cofan.mk_pt,
      Cofan.mk_Î¹_app]
  uniq e h m hm := by
    simp only [asIso_inv, IsIso.eq_inv_comp]
    ext a
    simp only [colimit.Î¹_desc_assoc, Discrete.functor_obj, Cofan.mk_pt, Cofan.mk_Î¹_app,
      colimit.Î¹_desc]
    exact hm a

instance {B : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) [HasCoproduct X]
    [IsIso (Sigma.desc Ï€)] : EffectiveEpiFamily X Ï€ :=
  âŸ¨âŸ¨effectiveEpiFamilyStructOfIsIsoDesc X Ï€âŸ©âŸ©

/-- The identity is an effective epi. -/
noncomputable
def effectiveEpiStructOfIsIso {X Y : C} (f : X âŸ¶ Y) [IsIso f] : EffectiveEpiStruct f where
  desc e _ := inv f â‰« e
  fac _ _ := by simp
  uniq _ _ _ h := by simpa using h

instance {X Y : C} (f : X âŸ¶ Y) [IsIso f] : EffectiveEpi f := âŸ¨âŸ¨effectiveEpiStructOfIsIso fâŸ©âŸ©

/--
A split epi followed by an effective epi is an effective epi. This version takes an explicit section
to the split epi, and is mainly used to define `effectiveEpiStructCompOfEffectiveEpiSplitEpi`,
which takes a `IsSplitEpi` instance instead.
-/
noncomputable
def effectiveEpiStructCompOfEffectiveEpiSplitEpi' {B X Y : C} (f : X âŸ¶ B) (g : Y âŸ¶ X) (i : X âŸ¶ Y)
    (hi : i â‰« g = ğŸ™ _) [EffectiveEpi f] : EffectiveEpiStruct (g â‰« f) where
  desc e w := EffectiveEpi.desc f (i â‰« e) fun gâ‚ gâ‚‚ _ â†¦ (by
    simp only [â† Category.assoc]
    apply w (gâ‚ â‰« i) (gâ‚‚ â‰« i)
    simpa [â† Category.assoc, hi])
  fac e w := by
    simp only [Category.assoc, EffectiveEpi.fac]
    rw [â† Category.id_comp e, â† Category.assoc, â† Category.assoc]
    apply w
    simp only [Category.comp_id, Category.id_comp, â† Category.assoc]
    aesop
  uniq _ _ _ hm := by
    apply EffectiveEpi.uniq f
    rw [â† hm, â† Category.assoc, â† Category.assoc, hi, Category.id_comp]

/-- A split epi followed by an effective epi is an effective epi. -/
noncomputable
def effectiveEpiStructCompOfEffectiveEpiSplitEpi {B X Y : C} (f : X âŸ¶ B) (g : Y âŸ¶ X) [IsSplitEpi g]
    [EffectiveEpi f] : EffectiveEpiStruct (g â‰« f) :=
  effectiveEpiStructCompOfEffectiveEpiSplitEpi' f g
    (IsSplitEpi.exists_splitEpi (f := g)).some.section_
    (IsSplitEpi.exists_splitEpi (f := g)).some.id

instance {B X Y : C} (f : X âŸ¶ B) (g : Y âŸ¶ X) [IsSplitEpi g] [EffectiveEpi f] :
    EffectiveEpi (g â‰« f) := âŸ¨âŸ¨effectiveEpiStructCompOfEffectiveEpiSplitEpi f gâŸ©âŸ©

end instances

section Regular

open RegularEpi in
/-- The data of an `EffectiveEpi`Â structure on a `RegularEpi`. -/
def effectiveEpiStructOfRegularEpi {B X : C} (f : X âŸ¶ B) [RegularEpi f] :
    EffectiveEpiStruct f where
  desc _ h := Cofork.IsColimit.desc isColimit _ (h _ _ w)
  fac _ _ := Cofork.IsColimit.Ï€_desc' isColimit _ _
  uniq _ _ _ hg := Cofork.IsColimit.hom_ext isColimit (hg.trans
    (Cofork.IsColimit.Ï€_desc' _ _ _).symm)

instance {B X : C} (f : X âŸ¶ B) [RegularEpi f] : EffectiveEpi f :=
  âŸ¨âŸ¨effectiveEpiStructOfRegularEpi fâŸ©âŸ©

/-- A morphism which is a coequalizer for its kernel pair is an effective epi. -/
theorem effectiveEpiOfKernelPair {B X : C} (f : X âŸ¶ B) [HasPullback f f]
    (hc : IsColimit (Cofork.ofÏ€ f pullback.condition)) : EffectiveEpi f :=
  let _ := regularEpiOfKernelPair f hc
  inferInstance

/-- An effective epi which has a kernel pair is a regular epi. -/
noncomputable instance regularEpiOfEffectiveEpi {B X : C} (f : X âŸ¶ B) [HasPullback f f]
    [EffectiveEpi f] : RegularEpi f where
  W := pullback f f
  left := pullback.fst
  right := pullback.snd
  w := pullback.condition
  isColimit := {
    desc := fun s â†¦ EffectiveEpi.desc f (s.Î¹.app WalkingParallelPair.one) fun gâ‚ gâ‚‚ hg â†¦ (by
      simp only [Cofork.app_one_eq_Ï€]
      rw [â† pullback.lift_snd gâ‚ gâ‚‚ hg, Category.assoc, â† Cofork.app_zero_eq_comp_Ï€_right]
      simp)
    fac := by
      intro s j
      have := EffectiveEpi.fac f (s.Î¹.app WalkingParallelPair.one) fun gâ‚ gâ‚‚ hg â†¦ (by
          simp only [Cofork.app_one_eq_Ï€]
          rw [â† pullback.lift_snd gâ‚ gâ‚‚ hg, Category.assoc, â† Cofork.app_zero_eq_comp_Ï€_right]
          simp)
      simp only [Functor.const_obj_obj, Cofork.app_one_eq_Ï€] at this
      cases j with
      | zero => simp [this]
      | one => simp [this]
    uniq := fun _ _ h â†¦ EffectiveEpi.uniq f _ _ _ (h WalkingParallelPair.one) }

end Regular

section Epi

variable [HasFiniteCoproducts C] (h : âˆ€ {Î± : Type} [Fintype Î±] {B : C}
    (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)), EffectiveEpiFamily X Ï€ â†” Epi (Sigma.desc Ï€ ))

lemma effectiveEpi_iff_epi {X Y : C} (f : X âŸ¶ Y) : EffectiveEpi f â†” Epi f := by
  rw [effectiveEpi_iff_effectiveEpiFamily, h]
  have w : f = (Limits.Sigma.Î¹ (fun () â†¦ X) ()) â‰« (Limits.Sigma.desc (fun () â†¦ f))
  Â· simp only [Limits.colimit.Î¹_desc, Limits.Cofan.mk_pt, Limits.Cofan.mk_Î¹_app]
  refine âŸ¨?_, fun _ â†¦ epi_of_epi_fac w.symmâŸ©
  intro
  rw [w]
  have : Epi (Limits.Sigma.Î¹ (fun () â†¦ X) ()) := âŸ¨fun _ _ h â†¦ by ext; exact hâŸ©
  exact epi_comp _ _

end Epi

end CategoryTheory
