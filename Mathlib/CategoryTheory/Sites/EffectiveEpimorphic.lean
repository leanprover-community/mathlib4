/-
Copyright (c) 2023 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/

import Mathlib.CategoryTheory.Sites.Sieves
import Mathlib.CategoryTheory.Limits.Shapes.KernelPair

/-!

# Effective epimorphisms

We define the notion of effective epimorphic (pre)sieves, morphisms and family of morphisms
and provide some API for relating the three notions.

More precisely, if `f` is a morphism, then `f` is an effective epi if and only if the sieve
it generates is effective epimorphic; see `CategoryTheory.Sieve.effectiveEpimorphic_singleton`.
The analogous statement for a family of morphisms is in the theorem
`CategoryTheory.Sieve.effectiveEpimorphic_family`.

We have defined the notion of effective epi for morphisms and families of morphisms in such a
way that avoids requiring the existence of pullbacks. However, if the relevant pullbacks exist
then these definitions should be equivalent (project: formalize this!).
See [nlab: *Effective Epimorphism*](https://ncatlab.org/nlab/show/effective+epimorphism) and
[Stacks 00WP](https://stacks.math.columbia.edu/tag/00WP) for the standard definitions.

## References
- [Elephant]: *Sketches of an Elephant*, P. T. Johnstone: C2.1, Example 2.1.12.
- [nlab: *Effective Epimorphism*](https://ncatlab.org/nlab/show/effective+epimorphism) and
- [Stacks 00WP](https://stacks.math.columbia.edu/tag/00WP) for the standard definitions.

-/

set_option autoImplicit true

namespace CategoryTheory

open Limits

variable {C : Type*} [Category C]

/-- A sieve is effective epimorphic if the associated cocone is a colimit cocone. -/
def Sieve.EffectiveEpimorphic {X : C} (S : Sieve X) : Prop :=
  Nonempty (IsColimit (S : Presieve X).cocone)

/-- A presieve is effective epimorphic if the cocone associated to the sieve it generates
is a colimit cocone. -/
abbrev Presieve.EffectiveEpimorphic {X : C} (S : Presieve X) : Prop :=
  (Sieve.generate S).EffectiveEpimorphic

/--
The sieve of morphisms which factor through a given morphism `f`.
This is equal to `Sieve.generate (Presieve.singleton f)`, but has
more convenient definitional properties.
-/
def Sieve.generateSingleton {X Y : C} (f : Y ‚ü∂ X) : Sieve X where
  arrows Z := { g | ‚àÉ (e : Z ‚ü∂ Y), e ‚â´ f = g }
  downward_closed := by
    rintro W Z g ‚ü®e,rfl‚ü© q
    exact ‚ü®q ‚â´ e, by simp‚ü©

lemma Sieve.generateSingleton_eq {X Y : C} (f : Y ‚ü∂ X) :
    Sieve.generate (Presieve.singleton f) = Sieve.generateSingleton f := by
  ext Z g
  constructor
  ¬∑ rintro ‚ü®W,i,p,‚ü®‚ü©,rfl‚ü©
    exact ‚ü®i,rfl‚ü©
  ¬∑ rintro ‚ü®g,h‚ü©
    exact ‚ü®Y,g,f,‚ü®‚ü©,h‚ü©

/--
This structure encodes the data required for a morphism to be an effective epimorphism.
-/
structure EffectiveEpiStruct {X Y : C} (f : Y ‚ü∂ X) where
  desc : ‚àÄ {W : C} (e : Y ‚ü∂ W),
    (‚àÄ {Z : C} (g‚ÇÅ g‚ÇÇ : Z ‚ü∂ Y), g‚ÇÅ ‚â´ f = g‚ÇÇ ‚â´ f ‚Üí g‚ÇÅ ‚â´ e = g‚ÇÇ ‚â´ e) ‚Üí (X ‚ü∂ W)
  fac : ‚àÄ {W : C} (e : Y ‚ü∂ W)
    (h : ‚àÄ {Z : C} (g‚ÇÅ g‚ÇÇ : Z ‚ü∂ Y), g‚ÇÅ ‚â´ f = g‚ÇÇ ‚â´ f ‚Üí g‚ÇÅ ‚â´ e = g‚ÇÇ ‚â´ e),
    f ‚â´ desc e h = e
  uniq : ‚àÄ {W : C} (e : Y ‚ü∂ W)
    (h : ‚àÄ {Z : C} (g‚ÇÅ g‚ÇÇ : Z ‚ü∂ Y), g‚ÇÅ ‚â´ f = g‚ÇÇ ‚â´ f ‚Üí g‚ÇÅ ‚â´ e = g‚ÇÇ ‚â´ e)
    (m : X ‚ü∂ W), f ‚â´ m = e ‚Üí m = desc e h

attribute [nolint docBlame]
  EffectiveEpiStruct.desc
  EffectiveEpiStruct.fac
  EffectiveEpiStruct.uniq

/--
A morphism `f : Y ‚ü∂ X` is an effective epimorphism provided that `f` exhibits `X` as a colimit
of the diagram of all "relations" `R ‚áâ Y`.
If `f` has a kernel pair, then this is equivalent to showing that the corresponding cofork is
a colimit.
-/
class EffectiveEpi {X Y : C} (f : Y ‚ü∂ X) : Prop where
  effectiveEpi : Nonempty (EffectiveEpiStruct f)

attribute [nolint docBlame] EffectiveEpi.effectiveEpi

/-- Some chosen `EffectiveEpiStruct` associated to an effective epi. -/
noncomputable
def EffectiveEpi.getStruct {X Y : C} (f : Y ‚ü∂ X) [EffectiveEpi f] : EffectiveEpiStruct f :=
  EffectiveEpi.effectiveEpi.some

/-- Descend along an effective epi. -/
noncomputable
def EffectiveEpi.desc {X Y W : C} (f : Y ‚ü∂ X) [EffectiveEpi f]
    (e : Y ‚ü∂ W) (h : ‚àÄ {Z : C} (g‚ÇÅ g‚ÇÇ : Z ‚ü∂ Y), g‚ÇÅ ‚â´ f = g‚ÇÇ ‚â´ f ‚Üí g‚ÇÅ ‚â´ e = g‚ÇÇ ‚â´ e) :
    X ‚ü∂ W := (EffectiveEpi.getStruct f).desc e h

@[reassoc (attr := simp)]
lemma EffectiveEpi.fac {X Y W : C} (f : Y ‚ü∂ X) [EffectiveEpi f]
    (e : Y ‚ü∂ W) (h : ‚àÄ {Z : C} (g‚ÇÅ g‚ÇÇ : Z ‚ü∂ Y), g‚ÇÅ ‚â´ f = g‚ÇÇ ‚â´ f ‚Üí g‚ÇÅ ‚â´ e = g‚ÇÇ ‚â´ e) :
    f ‚â´ EffectiveEpi.desc f e h = e :=
  (EffectiveEpi.getStruct f).fac e h

lemma EffectiveEpi.uniq {X Y W : C} (f : Y ‚ü∂ X) [EffectiveEpi f]
    (e : Y ‚ü∂ W) (h : ‚àÄ {Z : C} (g‚ÇÅ g‚ÇÇ : Z ‚ü∂ Y), g‚ÇÅ ‚â´ f = g‚ÇÇ ‚â´ f ‚Üí g‚ÇÅ ‚â´ e = g‚ÇÇ ‚â´ e)
    (m : X ‚ü∂ W) (hm : f ‚â´ m = e) :
    m = EffectiveEpi.desc f e h :=
  (EffectiveEpi.getStruct f).uniq e h _ hm

instance epiOfEffectiveEpi {X Y : C} (f : Y ‚ü∂ X) [EffectiveEpi f] : Epi f := by
  constructor
  intro W m‚ÇÅ m‚ÇÇ h
  have : m‚ÇÇ = EffectiveEpi.desc f (f ‚â´ m‚ÇÇ)
    (fun {Z} g‚ÇÅ g‚ÇÇ h => by simp only [‚Üê Category.assoc, h]) := EffectiveEpi.uniq _ _ _ _ rfl
  rw [this]
  exact EffectiveEpi.uniq _ _ _ _ h

/--
Implementation: This is a construction which will be used in the proof that
the sieve generated by a single arrow is effective epimorphic if and only if
the arrow is an effective epi.
-/
def isColimitOfEffectiveEpiStruct {X Y : C} (f : Y ‚ü∂ X) (Hf : EffectiveEpiStruct f) :
    IsColimit (Sieve.generateSingleton f : Presieve X).cocone :=
  letI D := FullSubcategory fun T : Over X => Sieve.generateSingleton f T.hom
  letI F : D ‚•§ _ := (Sieve.generateSingleton f).arrows.diagram
  { desc := fun S => Hf.desc (S.Œπ.app ‚ü®Over.mk f, ‚ü®ùüô _, by simp‚ü©‚ü©) <| by
      intro Z g‚ÇÅ g‚ÇÇ h
      let Y' : D := ‚ü®Over.mk f, ùüô _, by simp‚ü©
      let Z' : D := ‚ü®Over.mk (g‚ÇÅ ‚â´ f), g‚ÇÅ, rfl‚ü©
      let g‚ÇÅ' : Z' ‚ü∂ Y' := Over.homMk g‚ÇÅ
      let g‚ÇÇ' : Z' ‚ü∂ Y' := Over.homMk g‚ÇÇ (by simp [h])
      change F.map g‚ÇÅ' ‚â´ _ = F.map g‚ÇÇ' ‚â´ _
      simp only [S.w]
    fac := by
      rintro S ‚ü®T,g,hT‚ü©
      dsimp
      nth_rewrite 1 [‚Üê hT, Category.assoc, Hf.fac]
      let y : D := ‚ü®Over.mk f, ùüô _, by simp‚ü©
      let x : D := ‚ü®Over.mk T.hom, g, hT‚ü©
      let g' : x ‚ü∂ y := Over.homMk g
      change F.map g' ‚â´ _ = _
      rw [S.w]
      rfl
    uniq := by
      intro S m hm
      dsimp
      generalize_proofs h1 h2
      apply Hf.uniq _ h2
      exact hm ‚ü®Over.mk f, ùüô _, by simp‚ü© }

/--
Implementation: This is a construction which will be used in the proof that
the sieve generated by a single arrow is effective epimorphic if and only if
the arrow is an effective epi.
-/
noncomputable
def effectiveEpiStructOfIsColimit {X Y : C} (f : Y ‚ü∂ X)
    (Hf : IsColimit (Sieve.generateSingleton f : Presieve X).cocone) :
    EffectiveEpiStruct f :=
  let aux {W : C} (e : Y ‚ü∂ W)
    (h : ‚àÄ {Z : C} (g‚ÇÅ g‚ÇÇ : Z ‚ü∂ Y), g‚ÇÅ ‚â´ f = g‚ÇÇ ‚â´ f ‚Üí g‚ÇÅ ‚â´ e = g‚ÇÇ ‚â´ e) :
    Cocone (Sieve.generateSingleton f).arrows.diagram :=
    { pt := W
      Œπ := {
        app := fun ‚ü®T,hT‚ü© => hT.choose ‚â´ e
        naturality := by
          rintro ‚ü®A,hA‚ü© ‚ü®B,hB‚ü© (q : A ‚ü∂ B)
          dsimp; simp only [‚Üê Category.assoc, Category.comp_id]
          apply h
          rw [Category.assoc, hB.choose_spec, hA.choose_spec, Over.w] } }
  { desc := fun {W} e h => Hf.desc (aux e h)
    fac := by
      intro W e h
      dsimp
      have := Hf.fac (aux e h) ‚ü®Over.mk f, ùüô _, by simp‚ü©
      dsimp at this; rw [this]; clear this
      nth_rewrite 2 [‚Üê Category.id_comp e]
      apply h
      generalize_proofs hh
      rw [hh.choose_spec, Category.id_comp]
    uniq := by
      intro W e h m hm
      dsimp
      apply Hf.uniq (aux e h)
      rintro ‚ü®A,g,hA‚ü©
      dsimp
      nth_rewrite 1 [‚Üê hA, Category.assoc, hm]
      apply h
      generalize_proofs hh
      rwa [hh.choose_spec] }

theorem Sieve.effectiveEpimorphic_singleton {X Y : C} (f : Y ‚ü∂ X) :
    (Presieve.singleton f).EffectiveEpimorphic ‚Üî (EffectiveEpi f) := by
  constructor
  ¬∑ intro (h : Nonempty _)
    rw [Sieve.generateSingleton_eq] at h
    constructor
    apply Nonempty.map (effectiveEpiStructOfIsColimit _) h
  ¬∑ rintro ‚ü®h‚ü©
    show Nonempty _
    rw [Sieve.generateSingleton_eq]
    apply Nonempty.map (isColimitOfEffectiveEpiStruct _) h

/--
The sieve of morphisms which factor through a morphism in a given family.
This is equal to `Sieve.generate (Presieve.ofArrows X œÄ)`, but has
more convenient definitional properties.
-/
def Sieve.generateFamily {B : C} {Œ± : Type*} (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B)) :
    Sieve B where
  arrows Y := { f | ‚àÉ (a : Œ±) (g : Y ‚ü∂ X a), g ‚â´ œÄ a = f }
  downward_closed := by
    rintro Y‚ÇÅ Y‚ÇÇ g‚ÇÅ ‚ü®a,q,rfl‚ü© e
    exact ‚ü®a, e ‚â´ q, by simp‚ü©

lemma Sieve.generateFamily_eq {B : C} {Œ± : Type*} (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B)) :
    Sieve.generate (Presieve.ofArrows X œÄ) = Sieve.generateFamily X œÄ := by
  ext Y g
  constructor
  ¬∑ rintro ‚ü®W, g, f, ‚ü®a‚ü©, rfl‚ü©
    exact ‚ü®a, g, rfl‚ü©
  ¬∑ rintro ‚ü®a, g, rfl‚ü©
    exact ‚ü®_, g, œÄ a, ‚ü®a‚ü©, rfl‚ü©

/--
This structure encodes the data required for a family of morphisms to be effective epimorphic.
-/
structure EffectiveEpiFamilyStruct {B : C} {Œ± : Type*}
    (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B)) where
  desc : ‚àÄ {W} (e : (a : Œ±) ‚Üí (X a ‚ü∂ W)),
          (‚àÄ {Z : C} (a‚ÇÅ a‚ÇÇ : Œ±) (g‚ÇÅ : Z ‚ü∂ X a‚ÇÅ) (g‚ÇÇ : Z ‚ü∂ X a‚ÇÇ),
      g‚ÇÅ ‚â´ œÄ _ = g‚ÇÇ ‚â´ œÄ _ ‚Üí g‚ÇÅ ‚â´ e _ = g‚ÇÇ ‚â´ e _) ‚Üí (B ‚ü∂ W)
  fac : ‚àÄ {W} (e : (a : Œ±) ‚Üí (X a ‚ü∂ W))
          (h : ‚àÄ {Z : C} (a‚ÇÅ a‚ÇÇ : Œ±) (g‚ÇÅ : Z ‚ü∂ X a‚ÇÅ) (g‚ÇÇ : Z ‚ü∂ X a‚ÇÇ),
            g‚ÇÅ ‚â´ œÄ _ = g‚ÇÇ ‚â´ œÄ _ ‚Üí g‚ÇÅ ‚â´ e _ = g‚ÇÇ ‚â´ e _)
          (a : Œ±), œÄ a ‚â´ desc e h = e a
  uniq : ‚àÄ {W} (e : (a : Œ±) ‚Üí (X a ‚ü∂ W))
          (h : ‚àÄ {Z : C} (a‚ÇÅ a‚ÇÇ : Œ±) (g‚ÇÅ : Z ‚ü∂ X a‚ÇÅ) (g‚ÇÇ : Z ‚ü∂ X a‚ÇÇ),
            g‚ÇÅ ‚â´ œÄ _ = g‚ÇÇ ‚â´ œÄ _ ‚Üí g‚ÇÅ ‚â´ e _ = g‚ÇÇ ‚â´ e _)
          (m : B ‚ü∂ W), (‚àÄ (a : Œ±), œÄ a ‚â´ m = e a) ‚Üí m = desc e h

attribute [nolint docBlame]
  EffectiveEpiFamilyStruct.desc
  EffectiveEpiFamilyStruct.fac
  EffectiveEpiFamilyStruct.uniq

/--
A family of morphisms `f a : X a ‚ü∂ B` indexed by `Œ±` is effective epimorphic
provided that the `f a` exhibit `B` as a colimit of the diagram of all "relations"
`R ‚Üí X a‚ÇÅ`, `R ‚ü∂ X a‚ÇÇ` for all `a‚ÇÅ a‚ÇÇ : Œ±`.
-/
class EffectiveEpiFamily {B : C} {Œ± : Type*} (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B)) : Prop where
  effectiveEpiFamily : Nonempty (EffectiveEpiFamilyStruct X œÄ)

attribute [nolint docBlame] EffectiveEpiFamily.effectiveEpiFamily

/-- Some chosen `EffectiveEpiFamilyStruct` associated to an effective epi family. -/
noncomputable
def EffectiveEpiFamily.getStruct {B : C} {Œ± : Type*} (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B))
    [EffectiveEpiFamily X œÄ] : EffectiveEpiFamilyStruct X œÄ :=
  EffectiveEpiFamily.effectiveEpiFamily.some

/-- Descend along an effective epi family. -/
noncomputable
def EffectiveEpiFamily.desc {B W : C} {Œ± : Type*} (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B))
    [EffectiveEpiFamily X œÄ] (e : (a : Œ±) ‚Üí (X a ‚ü∂ W))
    (h : ‚àÄ {Z : C} (a‚ÇÅ a‚ÇÇ : Œ±) (g‚ÇÅ : Z ‚ü∂ X a‚ÇÅ) (g‚ÇÇ : Z ‚ü∂ X a‚ÇÇ),
      g‚ÇÅ ‚â´ œÄ _ = g‚ÇÇ ‚â´ œÄ _ ‚Üí g‚ÇÅ ‚â´ e _ = g‚ÇÇ ‚â´ e _) : B ‚ü∂ W :=
  (EffectiveEpiFamily.getStruct X œÄ).desc e h

@[reassoc (attr := simp)]
lemma EffectiveEpiFamily.fac {B W : C} {Œ± : Type*} (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B))
    [EffectiveEpiFamily X œÄ] (e : (a : Œ±) ‚Üí (X a ‚ü∂ W))
    (h : ‚àÄ {Z : C} (a‚ÇÅ a‚ÇÇ : Œ±) (g‚ÇÅ : Z ‚ü∂ X a‚ÇÅ) (g‚ÇÇ : Z ‚ü∂ X a‚ÇÇ),
      g‚ÇÅ ‚â´ œÄ _ = g‚ÇÇ ‚â´ œÄ _ ‚Üí g‚ÇÅ ‚â´ e _ = g‚ÇÇ ‚â´ e _) (a : Œ±) :
    œÄ a ‚â´ EffectiveEpiFamily.desc X œÄ e h = e a :=
  (EffectiveEpiFamily.getStruct X œÄ).fac e h a

/-
NOTE: The `simpNF` linter complains for some reason. See the two examples below.
Zulip discussion:
https://leanprover.zulipchat.com/#narrow/stream/287929-mathlib4/topic/simpNF.20bug.3F
-/
attribute [nolint simpNF]
  EffectiveEpiFamily.fac
  EffectiveEpiFamily.fac_assoc

/-- The effective epi family structure on the identity -/
def effectiveEpiFamilyStructId : EffectiveEpiFamilyStruct (Œ± : Unit ‚Üí C) (fun _ => ùüô (Œ± ())) where
  desc := fun e _ => e ()
  fac := by aesop_cat
  uniq := by aesop_cat

instance : EffectiveEpiFamily (fun _ => X : Unit ‚Üí C) (fun _ => ùüô X) :=
  ‚ü®‚ü®effectiveEpiFamilyStructId‚ü©‚ü©

example {B W : C} {Œ± : Type*} (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B))
    [EffectiveEpiFamily X œÄ] (e : (a : Œ±) ‚Üí (X a ‚ü∂ W))
    (h : ‚àÄ {Z : C} (a‚ÇÅ a‚ÇÇ : Œ±) (g‚ÇÅ : Z ‚ü∂ X a‚ÇÅ) (g‚ÇÇ : Z ‚ü∂ X a‚ÇÇ),
      g‚ÇÅ ‚â´ œÄ _ = g‚ÇÇ ‚â´ œÄ _ ‚Üí g‚ÇÅ ‚â´ e _ = g‚ÇÇ ‚â´ e _) (a : Œ±) :
    œÄ a ‚â´ EffectiveEpiFamily.desc X œÄ e h = e a :=
  by simp

example {B W Q : C} {Œ± : Type*} (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B))
    [EffectiveEpiFamily X œÄ] (e : (a : Œ±) ‚Üí (X a ‚ü∂ W))
    (h : ‚àÄ {Z : C} (a‚ÇÅ a‚ÇÇ : Œ±) (g‚ÇÅ : Z ‚ü∂ X a‚ÇÅ) (g‚ÇÇ : Z ‚ü∂ X a‚ÇÇ),
      g‚ÇÅ ‚â´ œÄ _ = g‚ÇÇ ‚â´ œÄ _ ‚Üí g‚ÇÅ ‚â´ e _ = g‚ÇÇ ‚â´ e _) (a : Œ±)
    (q : W ‚ü∂ Q) :
    œÄ a ‚â´ EffectiveEpiFamily.desc X œÄ e h ‚â´ q = e a ‚â´ q :=
  by simp

lemma EffectiveEpiFamily.uniq {B W : C} {Œ± : Type*} (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B))
    [EffectiveEpiFamily X œÄ] (e : (a : Œ±) ‚Üí (X a ‚ü∂ W))
    (h : ‚àÄ {Z : C} (a‚ÇÅ a‚ÇÇ : Œ±) (g‚ÇÅ : Z ‚ü∂ X a‚ÇÅ) (g‚ÇÇ : Z ‚ü∂ X a‚ÇÇ),
      g‚ÇÅ ‚â´ œÄ _ = g‚ÇÇ ‚â´ œÄ _ ‚Üí g‚ÇÅ ‚â´ e _ = g‚ÇÇ ‚â´ e _)
    (m : B ‚ü∂ W) (hm : ‚àÄ a, œÄ a ‚â´ m = e a) :
    m = EffectiveEpiFamily.desc X œÄ e h :=
  (EffectiveEpiFamily.getStruct X œÄ).uniq e h m hm

-- TODO: Once we have "jointly epimorphic families", we could rephrase this as such a property.
lemma EffectiveEpiFamily.hom_ext {B W : C} {Œ± : Type*} (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B))
    [EffectiveEpiFamily X œÄ] (m‚ÇÅ m‚ÇÇ : B ‚ü∂ W) (h : ‚àÄ a, œÄ a ‚â´ m‚ÇÅ = œÄ a ‚â´ m‚ÇÇ) :
    m‚ÇÅ = m‚ÇÇ := by
  have : m‚ÇÇ = EffectiveEpiFamily.desc X œÄ (fun a => œÄ a ‚â´ m‚ÇÇ)
      (fun a‚ÇÅ a‚ÇÇ g‚ÇÅ g‚ÇÇ h => by simp only [‚Üê Category.assoc, h]) := by
    apply EffectiveEpiFamily.uniq; intro; rfl
  rw [this]
  exact EffectiveEpiFamily.uniq _ _ _ _ _ h

instance epiCoproductDescOfEffectiveEpiFamily {B : C} {Œ± : Type*}
    (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B)) [EffectiveEpiFamily X œÄ] [HasCoproduct X] :
    Epi (Sigma.desc œÄ) := by
  constructor
  intro Z g h H
  apply EffectiveEpiFamily.hom_ext X œÄ
  intro a
  suffices (Sigma.Œπ _ a ‚â´ Sigma.desc œÄ) ‚â´ g = (Sigma.Œπ _ a ‚â´ Sigma.desc œÄ) ‚â´ h by
    simpa only [colimit.Œπ_desc] using this
  simp only [Category.assoc, H]

/--
Implementation: This is a construction which will be used in the proof that
the sieve generated by a family of arrows is effective epimorphic if and only if
the family is an effective epi.
-/
def isColimitOfEffectiveEpiFamilyStruct {B : C} {Œ± : Type*}
    (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B)) (H : EffectiveEpiFamilyStruct X œÄ) :
    IsColimit (Sieve.generateFamily X œÄ : Presieve B).cocone :=
  letI D := FullSubcategory fun T : Over B => Sieve.generateFamily X œÄ T.hom
  letI F : D ‚•§ _ := (Sieve.generateFamily X œÄ).arrows.diagram
  { desc := fun S => H.desc (fun a => S.Œπ.app ‚ü®Over.mk (œÄ a), ‚ü®a,ùüô _, by simp‚ü©‚ü©) <| by
      intro Z a‚ÇÅ a‚ÇÇ g‚ÇÅ g‚ÇÇ h
      dsimp
      let A‚ÇÅ : D := ‚ü®Over.mk (œÄ a‚ÇÅ), a‚ÇÅ, ùüô _, by simp‚ü©
      let A‚ÇÇ : D := ‚ü®Over.mk (œÄ a‚ÇÇ), a‚ÇÇ, ùüô _, by simp‚ü©
      let Z' : D := ‚ü®Over.mk (g‚ÇÅ ‚â´ œÄ a‚ÇÅ), a‚ÇÅ, g‚ÇÅ, rfl‚ü©
      let i‚ÇÅ : Z' ‚ü∂ A‚ÇÅ := Over.homMk g‚ÇÅ
      let i‚ÇÇ : Z' ‚ü∂ A‚ÇÇ := Over.homMk g‚ÇÇ
      change F.map i‚ÇÅ ‚â´ _ = F.map i‚ÇÇ ‚â´ _
      simp only [S.w]
    fac := by
      intro S ‚ü®T, a, (g : T.left ‚ü∂ X a), hT‚ü©
      dsimp
      nth_rewrite 1 [‚Üê hT, Category.assoc, H.fac]
      let A : D := ‚ü®Over.mk (œÄ a), a, ùüô _, by simp‚ü©
      let B : D := ‚ü®Over.mk T.hom, a, g, hT‚ü©
      let i : B ‚ü∂ A := Over.homMk g
      change F.map i ‚â´ _ = _
      rw [S.w]
      rfl
    uniq := by
      intro S m hm; dsimp
      apply H.uniq
      intro a
      exact hm ‚ü®Over.mk (œÄ a), a, ùüô _, by simp‚ü© }

/--
Implementation: This is a construction which will be used in the proof that
the sieve generated by a family of arrows is effective epimorphic if and only if
the family is an effective epi.
-/
noncomputable
def effectiveEpiFamilyStructOfIsColimit {B : C} {Œ± : Type*}
    (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B))
    (H : IsColimit (Sieve.generateFamily X œÄ : Presieve B).cocone) :
    EffectiveEpiFamilyStruct X œÄ :=
  let aux {W : C} (e : (a : Œ±) ‚Üí (X a ‚ü∂ W))
    (h : ‚àÄ {Z : C} (a‚ÇÅ a‚ÇÇ : Œ±) (g‚ÇÅ : Z ‚ü∂ X a‚ÇÅ) (g‚ÇÇ : Z ‚ü∂ X a‚ÇÇ),
      g‚ÇÅ ‚â´ œÄ _ = g‚ÇÇ ‚â´ œÄ _ ‚Üí g‚ÇÅ ‚â´ e _ = g‚ÇÇ ‚â´ e _) :
    Cocone (Sieve.generateFamily X œÄ).arrows.diagram := {
      pt := W
      Œπ := {
        app := fun ‚ü®T,hT‚ü© => hT.choose_spec.choose ‚â´ e hT.choose
        naturality := by
          intro ‚ü®A,a,(g‚ÇÅ : A.left ‚ü∂ _),ha‚ü© ‚ü®B,b,(g‚ÇÇ : B.left ‚ü∂ _),hb‚ü© (q : A ‚ü∂ B)
          dsimp; rw [Category.comp_id, ‚Üê Category.assoc]
          apply h; rw [Category.assoc]
          generalize_proofs h1 h2 h3 h4
          rw [h2.choose_spec, h4.choose_spec, Over.w] } }
  { desc := fun {W} e h => H.desc (aux e h)
    fac := by
      intro W e h a
      dsimp
      have := H.fac (aux e h) ‚ü®Over.mk (œÄ a), a, ùüô _, by simp‚ü©
      dsimp at this; rw [this]; clear this
      conv_rhs => rw [‚Üê Category.id_comp (e a)]
      apply h
      generalize_proofs h1 h2
      rw [h2.choose_spec, Category.id_comp]
    uniq := by
      intro W e h m hm
      apply H.uniq (aux e h)
      rintro ‚ü®T, a, (g : T.left ‚ü∂ _), ha‚ü©
      dsimp
      nth_rewrite 1 [‚Üê ha, Category.assoc, hm]
      apply h
      generalize_proofs h1 h2
      rwa [h2.choose_spec] }

theorem Sieve.effectiveEpimorphic_family {B : C} {Œ± : Type*}
    (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B)) :
    (Presieve.ofArrows X œÄ).EffectiveEpimorphic ‚Üî EffectiveEpiFamily X œÄ := by
  constructor
  ¬∑ intro (h : Nonempty _)
    rw [Sieve.generateFamily_eq] at h
    constructor
    apply Nonempty.map (effectiveEpiFamilyStructOfIsColimit _ _) h
  ¬∑ rintro ‚ü®h‚ü©
    show Nonempty _
    rw [Sieve.generateFamily_eq]
    apply Nonempty.map (isColimitOfEffectiveEpiFamilyStruct _ _) h


section instances

/--
Given an `EffectiveEpiFamily X œÄ` such that the coproduct of `X` exists, `Sigma.desc œÄ` is an
`EffectiveEpi`.
-/
noncomputable
def EffectiveEpiFamily_descStruct {B : C} {Œ± : Type*} (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B))
    [HasCoproduct X] [EffectiveEpiFamily X œÄ] : EffectiveEpiStruct (Sigma.desc œÄ) where
  desc e h := EffectiveEpiFamily.desc X œÄ (fun a ‚Ü¶ Sigma.Œπ X a ‚â´ e) (fun a‚ÇÅ a‚ÇÇ g‚ÇÅ g‚ÇÇ hg ‚Ü¶ by
    simp only [‚Üê Category.assoc]
    apply h (g‚ÇÅ ‚â´ Sigma.Œπ X a‚ÇÅ) (g‚ÇÇ ‚â´ Sigma.Œπ X a‚ÇÇ)
    simpa only [Category.assoc, colimit.Œπ_desc, Cofan.mk_pt, Cofan.mk_Œπ_app])
  fac e h := by
    ext a
    simp only [colimit.Œπ_desc_assoc, Discrete.functor_obj, Cofan.mk_pt, Cofan.mk_Œπ_app,
      EffectiveEpiFamily.fac]
  uniq e _ m hm := by
    have := EffectiveEpiFamily.uniq X œÄ (fun a ‚Ü¶ Sigma.Œπ X a ‚â´ e) ?_ m
    ¬∑ apply this
      intro a
      rw [‚Üê hm]
      simp only [colimit.Œπ_desc_assoc, Discrete.functor_obj, Cofan.mk_pt, Cofan.mk_Œπ_app]
    ¬∑ intro Z a‚ÇÅ a‚ÇÇ g‚ÇÅ g‚ÇÇ hg
      rw [‚Üê hm]
      simp only [colimit.Œπ_desc_assoc, Discrete.functor_obj, Cofan.mk_pt, Cofan.mk_Œπ_app]
      rw [‚Üê Category.assoc, hg, Category.assoc]

instance {B : C} {Œ± : Type*} (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B)) [HasCoproduct X]
    [EffectiveEpiFamily X œÄ] : EffectiveEpi (Sigma.desc œÄ) :=
  ‚ü®‚ü®EffectiveEpiFamily_descStruct X œÄ‚ü©‚ü©

/--
An `EffectiveEpiFamily` consisting of a single `EffectiveEpi`
-/
noncomputable
def EffectiveEpi_familyStruct {B X : C} (f : X ‚ü∂ B) [EffectiveEpi f] :
    EffectiveEpiFamilyStruct (fun () ‚Ü¶ X) (fun () ‚Ü¶ f) where
  desc e h := EffectiveEpi.desc f (e ()) (fun g‚ÇÅ g‚ÇÇ hg ‚Ü¶ h () () g‚ÇÅ g‚ÇÇ hg)
  fac e h := fun _ ‚Ü¶ EffectiveEpi.fac f (e ()) (fun g‚ÇÅ g‚ÇÇ hg ‚Ü¶ h () () g‚ÇÅ g‚ÇÇ hg)
  uniq e h m hm := by apply EffectiveEpi.uniq f (e ()) (h () ()); exact hm ()

instance {B X : C} (f : X ‚ü∂ B) [EffectiveEpi f] : EffectiveEpiFamily (fun () ‚Ü¶ X) (fun () ‚Ü¶ f) :=
  ‚ü®‚ü®EffectiveEpi_familyStruct f‚ü©‚ü©

/--
A single element `EffectiveEpiFamily`¬†constists of an `EffectiveEpi`
-/
noncomputable
def EffectiveEpiStruct_ofFamily {B X : C} (f : X ‚ü∂ B)
    [EffectiveEpiFamily (fun () ‚Ü¶ X) (fun () ‚Ü¶ f)] :
    EffectiveEpiStruct f where
  desc e h := EffectiveEpiFamily.desc
    (fun () ‚Ü¶ X) (fun () ‚Ü¶ f) (fun () ‚Ü¶ e) (fun _ _ g‚ÇÅ g‚ÇÇ hg ‚Ü¶ h g‚ÇÅ g‚ÇÇ hg)
  fac e h := EffectiveEpiFamily.fac
    (fun () ‚Ü¶ X) (fun () ‚Ü¶ f) (fun () ‚Ü¶ e) (fun _ _ g‚ÇÅ g‚ÇÇ hg ‚Ü¶ h g‚ÇÅ g‚ÇÇ hg) ()
  uniq e h m hm := EffectiveEpiFamily.uniq
    (fun () ‚Ü¶ X) (fun () ‚Ü¶ f) (fun () ‚Ü¶ e) (fun _ _ g‚ÇÅ g‚ÇÇ hg ‚Ü¶ h g‚ÇÅ g‚ÇÇ hg) m (fun _ ‚Ü¶ hm)

instance {B X : C} (f : X ‚ü∂ B) [EffectiveEpiFamily (fun () ‚Ü¶ X) (fun () ‚Ü¶ f)] :
    EffectiveEpi f :=
  ‚ü®‚ü®EffectiveEpiStruct_ofFamily f‚ü©‚ü©

lemma effectiveEpi_iff_effectiveEpiFamily {B X : C} (f : X ‚ü∂ B) :
    EffectiveEpi f ‚Üî EffectiveEpiFamily (fun () ‚Ü¶ X) (fun () ‚Ü¶ f) :=
  ‚ü®fun _ ‚Ü¶ inferInstance, fun _ ‚Ü¶ inferInstance‚ü©

/--
A family of morphisms with the same target inducing an isomorphism from the coproduct to the target
is an `EffectiveEpiFamily`.
-/
noncomputable
def EffectiveEpiFamilyStruct_of_isIso_desc {B : C} {Œ± : Type*} (X : Œ± ‚Üí C)
    (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B)) [HasCoproduct X] [IsIso (Sigma.desc œÄ)] :
    EffectiveEpiFamilyStruct X œÄ where
  desc e _ := (asIso (Sigma.desc œÄ)).inv ‚â´ (Sigma.desc e)
  fac e h := by
    intro a
    have : œÄ a = Sigma.Œπ X a ‚â´ (asIso (Sigma.desc œÄ)).hom := by simp only [asIso_hom,
      colimit.Œπ_desc, Cofan.mk_pt, Cofan.mk_Œπ_app]
    rw [this, Category.assoc]
    simp only [asIso_hom, asIso_inv, IsIso.hom_inv_id_assoc, colimit.Œπ_desc, Cofan.mk_pt,
      Cofan.mk_Œπ_app]
  uniq e h m hm := by
    simp only [asIso_inv, IsIso.eq_inv_comp]
    ext a
    simp only [colimit.Œπ_desc_assoc, Discrete.functor_obj, Cofan.mk_pt, Cofan.mk_Œπ_app,
      colimit.Œπ_desc]
    exact hm a

instance {B : C} {Œ± : Type*} (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B)) [HasCoproduct X]
    [IsIso (Sigma.desc œÄ)] : EffectiveEpiFamily X œÄ :=
  ‚ü®‚ü®EffectiveEpiFamilyStruct_of_isIso_desc X œÄ‚ü©‚ü©

/-- The identity is an effective epi. -/
def EffectiveEpiStructId {X : C} : EffectiveEpiStruct (ùüô X) where
  desc e _ := e
  fac _ _ := by simp only [Category.id_comp]
  uniq _ _ _ h := by simp only [Category.id_comp] at h; exact h

instance {X : C} : EffectiveEpi (ùüô X) := ‚ü®‚ü®EffectiveEpiStructId‚ü©‚ü©

end instances

section Epi

variable [HasFiniteCoproducts C] (h : ‚àÄ {Œ± : Type} [Fintype Œ±] {B : C}
    (X : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (X a ‚ü∂ B)), EffectiveEpiFamily X œÄ ‚Üî Epi (Sigma.desc œÄ ))

lemma effectiveEpi_iff_epi {X Y : C} (f : X ‚ü∂ Y) : EffectiveEpi f ‚Üî Epi f := by
  rw [effectiveEpi_iff_effectiveEpiFamily, h]
  have w : f = (Limits.Sigma.Œπ (fun () ‚Ü¶ X) ()) ‚â´ (Limits.Sigma.desc (fun () ‚Ü¶ f))
  ¬∑ simp only [Limits.colimit.Œπ_desc, Limits.Cofan.mk_pt, Limits.Cofan.mk_Œπ_app]
  refine ‚ü®?_, fun _ ‚Ü¶ epi_of_epi_fac w.symm‚ü©
  intro
  rw [w]
  have : Epi (Limits.Sigma.Œπ (fun () ‚Ü¶ X) ()) := ‚ü®fun _ _ h ‚Ü¶ by ext; exact h‚ü©
  exact epi_comp _ _

end Epi

end CategoryTheory
