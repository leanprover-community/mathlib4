/-
Copyright (c) 2025 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
module

public import Mathlib.CategoryTheory.Sites.Point.Basic

/-!
# The category of points of a site

We define the category structure on the points of a site `(C, J)`:
a morphism between `Î¦â‚ âŸ¶ Î¦â‚‚` between two points consists of a
morphism `Î¦â‚‚.fiber âŸ¶ Î¦â‚.fiber` (SGA 4 IV 3.2).

## References
* [Alexander Grothendieck and Jean-Louis Verdier, *ExposÃ© IV : Topos*,
  SGA 4 IV 3.2][sga-4-tome-1]

-/

@[expose] public section

universe w v v' u u'

namespace CategoryTheory

open Limits Opposite

variable {C : Type u} [Category.{v} C] {J : GrothendieckTopology C}

namespace GrothendieckTopology.Point

/-- A morphism between points of a site consists of a morphism
between the functors `Point.fiber`, in the opposite direction. -/
@[ext]
structure Hom (Î¦â‚ Î¦â‚‚ : Point.{w} J) where
  /-- a natural transformation, in the opposite direction -/
  hom : Î¦â‚‚.fiber âŸ¶ Î¦â‚.fiber

instance : Category (Point.{w} J) where
  Hom := Hom
  id _ := âŸ¨ğŸ™ _âŸ©
  comp f g := âŸ¨g.hom â‰« f.homâŸ©

@[ext]
lemma hom_ext {Î¦â‚ Î¦â‚‚ : Point.{w} J} {f g : Î¦â‚ âŸ¶ Î¦â‚‚} (h : f.hom = g.hom) : f = g :=
  Hom.ext h

@[simp]
lemma id_hom (Î¦ : Point.{w} J) : Hom.hom (ğŸ™ Î¦) = ğŸ™ _ := rfl

@[simp, reassoc]
lemma comp_hom {Î¦â‚ Î¦â‚‚ Î¦â‚ƒ : Point.{w} J} (f : Î¦â‚ âŸ¶ Î¦â‚‚) (g : Î¦â‚‚ âŸ¶ Î¦â‚ƒ) :
    (f â‰« g).hom = g.hom â‰« f.hom := rfl

variable {A : Type u'} [Category.{v'} A]
  [HasColimitsOfSize.{w, w} A]

namespace Hom

variable {Î¦â‚ Î¦â‚‚ Î¦â‚ƒ : Point.{w} J} (f : Î¦â‚ âŸ¶ Î¦â‚‚) (g : Î¦â‚‚ âŸ¶ Î¦â‚ƒ)

attribute [local simp] FunctorToTypes.naturality in
/-- The natural transformation on fibers of presheaves that is induced
by a morphism of points of a site. -/
@[simps]
noncomputable def presheafFiber :
    Î¦â‚‚.presheafFiber (A := A) âŸ¶ Î¦â‚.presheafFiber where
  app P := Î¦â‚‚.presheafFiberDesc (fun X x â†¦ Î¦â‚.toPresheafFiber X (f.hom.app X x) P)

@[simp]
lemma presheafFiber_id (Î¦ : Point.{w} J) :
    presheafFiber (ğŸ™ Î¦) (A := A) = ğŸ™ _ := by
  cat_disch

@[reassoc, simp]
lemma presheafFiber_comp :
    (f â‰« g).presheafFiber (A := A) = g.presheafFiber â‰« f.presheafFiber := by
  cat_disch

/-- The natural transformation on fibers of sheaves that is induced
by a morphism of points of a site. -/
noncomputable abbrev sheafFiber :
    Î¦â‚‚.sheafFiber (A := A) âŸ¶ Î¦â‚.sheafFiber :=
  Functor.whiskerLeft _ f.presheafFiber

@[simp]
lemma sheafFiber_id (Î¦ : Point.{w} J) :
    sheafFiber (ğŸ™ Î¦) (A := A) = ğŸ™ _ := by
  cat_disch

@[reassoc, simp]
lemma sheafFiber_comp :
    (f â‰« g).sheafFiber (A := A) = g.sheafFiber â‰« f.sheafFiber := by
  cat_disch

end Hom

end GrothendieckTopology.Point

end CategoryTheory
