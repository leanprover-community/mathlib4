/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Sites.CoverLifting
import Mathlib.CategoryTheory.Sites.CoverPreserving

/-! Localization

In this file, given a Grothendieck topology `J` on a category `C` and `X : C`, we construct
a Grothendieck topology `J.over X` on the category `Over X`. In order to do this,
we first construct a bijection `Sieve.overEquiv Y : Sieve Y ‚âÉ Sieve Y.left`
for all `Y : Over X`. Then, as it is stated in SGA 4 III 5.2.1, a sieve of `Y : Over X`
is covering for `J.over X` if and only if the corresponding sieve of `Y.left`
is covering for `J`. As a result, the forgetful functor
`Over.forget X : Over X ‚•§ X` is both cover-preserving and cover-lifting.

-/

universe v' v u' u

namespace CategoryTheory

open Category

variable {C : Type u} [Category.{v} C]

namespace Sieve

/-- The equivalence `Sieve Y ‚âÉ Sieve Y.left` for all `Y : Over X`. -/
def overEquiv {X : C} (Y : Over X) :
    Sieve Y ‚âÉ Sieve Y.left where
  toFun S := Sieve.functorPushforward (Over.forget X) S
  invFun S' := Sieve.functorPullback (Over.forget X) S'
  left_inv S := by
    ext Z g
    dsimp [Presieve.functorPullback, Presieve.functorPushforward]
    constructor
    ¬∑ rintro ‚ü®W, a, b, h, w‚ü©
      let c : Z ‚ü∂ W := Over.homMk b
        (by rw [‚Üê Over.w g, w, assoc, Over.w a])
      rw [show g = c ‚â´ a by ext; exact w]
      exact S.downward_closed h _
    ¬∑ intro h
      exact ‚ü®Z, g, ùüô _, h, by simp‚ü©
  right_inv S := by
    ext Z g
    dsimp [Presieve.functorPullback, Presieve.functorPushforward]
    constructor
    ¬∑ rintro ‚ü®W, a, b, h, rfl‚ü©
      exact S.downward_closed h _
    ¬∑ intro h
      exact ‚ü®Over.mk ((g ‚â´ Y.hom)), Over.homMk g, ùüô _, h, by simp‚ü©

@[simp]
lemma overEquiv_top {X : C} (Y : Over X) :
    overEquiv Y ‚ä§ = ‚ä§ := by
  ext Z g
  simp only [top_apply, iff_true]
  dsimp [overEquiv, Presieve.functorPushforward]
  exact ‚ü®Y, ùüô Y, g, by simp, by simp‚ü©

@[simp]
lemma overEquiv_symm_top {X : C} (Y : Over X) :
    (overEquiv Y).symm ‚ä§ = ‚ä§ :=
  (overEquiv Y).injective (by simp)

lemma overEquiv_le_overEquiv_iff {X : C} {Y : Over X} (R‚ÇÅ R‚ÇÇ : Sieve Y) :
    R‚ÇÅ.overEquiv Y ‚â§ R‚ÇÇ.overEquiv Y ‚Üî R‚ÇÅ ‚â§ R‚ÇÇ := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ Sieve.functorPushforward_monotone _ _ h‚ü©
  replace h : (overEquiv Y).symm (R‚ÇÅ.overEquiv Y) ‚â§ (overEquiv Y).symm (R‚ÇÇ.overEquiv Y) :=
    Sieve.functorPullback_monotone _ _ h
  simpa using h

lemma overEquiv_pullback {X : C} {Y‚ÇÅ Y‚ÇÇ : Over X} (f : Y‚ÇÅ ‚ü∂ Y‚ÇÇ) (S : Sieve Y‚ÇÇ) :
    overEquiv _ (S.pullback f) = (overEquiv _ S).pullback f.left := by
  ext Z g
  dsimp [overEquiv, Presieve.functorPushforward]
  constructor
  ¬∑ rintro ‚ü®W, a, b, h, rfl‚ü©
    exact ‚ü®W, a ‚â´ f, b, h, by simp‚ü©
  ¬∑ rintro ‚ü®W, a, b, h, w‚ü©
    let T := Over.mk (b ‚â´ W.hom)
    let c : T ‚ü∂ Y‚ÇÅ := Over.homMk g (by dsimp [T]; rw [‚Üê Over.w a, ‚Üê reassoc_of% w, Over.w f])
    let d : T ‚ü∂ W := Over.homMk b
    refine ‚ü®T, c, ùüô Z, ?_, by simp [T, c]‚ü©
    rw [show c ‚â´ f = d ‚â´ a by ext; exact w]
    exact S.downward_closed h _

@[simp]
lemma overEquiv_symm_iff {X : C} {Y : Over X} (S : Sieve Y.left) {Z : Over X} (f : Z ‚ü∂ Y) :
    (overEquiv Y).symm S f ‚Üî S f.left := by
  rfl

lemma overEquiv_iff {X : C} {Y : Over X} (S : Sieve Y) {Z : C} (f : Z ‚ü∂ Y.left) :
    overEquiv Y S f ‚Üî S (Over.homMk f : Over.mk (f ‚â´ Y.hom) ‚ü∂ Y) := by
  obtain ‚ü®S, rfl‚ü© := (overEquiv Y).symm.surjective S
  simp

@[simp]
lemma functorPushforward_over_map {X Y : C} (f : X ‚ü∂ Y) (Z : Over X) (S : Sieve Z.left) :
    Sieve.functorPushforward (Over.map f) ((Sieve.overEquiv Z).symm S) =
      (Sieve.overEquiv ((Over.map f).obj Z)).symm S := by
  ext W g
  constructor
  ¬∑ rintro ‚ü®T, a, b, ha, rfl‚ü©
    exact S.downward_closed ha _
  ¬∑ intro hg
    exact ‚ü®Over.mk (g.left ‚â´ Z.hom), Over.homMk g.left,
      Over.homMk (ùüô _) (by simpa using Over.w g), hg, by cat_disch‚ü©

end Sieve

variable (J : GrothendieckTopology C)

namespace GrothendieckTopology

/-- The Grothendieck topology on the category `Over X` for any `X : C` that is
induced by a Grothendieck topology on `C`. -/
def over (X : C) : GrothendieckTopology (Over X) where
  sieves Y S := Sieve.overEquiv Y S ‚àà J Y.left
  top_mem' Y := by
    change _ ‚àà J Y.left
    simp
  pullback_stable' Y‚ÇÅ Y‚ÇÇ S‚ÇÅ f h‚ÇÅ := by
    change _ ‚àà J _ at h‚ÇÅ ‚ä¢
    rw [Sieve.overEquiv_pullback]
    exact J.pullback_stable _ h‚ÇÅ
  transitive' Y S (hS : _ ‚àà J _) R hR := J.transitive hS _ (fun Z f hf => by
    have hf' : _ ‚àà J _ := hR ((Sieve.overEquiv_iff _ _).1 hf)
    rw [Sieve.overEquiv_pullback] at hf'
    exact hf')

lemma mem_over_iff {X : C} {Y : Over X} (S : Sieve Y) :
    S ‚àà (J.over X) Y ‚Üî Sieve.overEquiv _ S ‚àà J Y.left := by
  rfl

lemma overEquiv_symm_mem_over {X : C} (Y : Over X) (S : Sieve Y.left) (hS : S ‚àà J Y.left) :
    (Sieve.overEquiv Y).symm S ‚àà (J.over X) Y := by
  simpa only [mem_over_iff, Equiv.apply_symm_apply] using hS

lemma over_forget_coverPreserving (X : C) :
    CoverPreserving (J.over X) J (Over.forget X) where
  cover_preserve hS := hS

lemma over_forget_compatiblePreserving (X : C) :
    CompatiblePreserving J (Over.forget X) where
  compatible {_ Z _ _ hx Y‚ÇÅ Y‚ÇÇ W f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ hg‚ÇÅ hg‚ÇÇ h} := by
    let W' : Over X := Over.mk (f‚ÇÅ ‚â´ Y‚ÇÅ.hom)
    let g‚ÇÅ' : W' ‚ü∂ Y‚ÇÅ := Over.homMk f‚ÇÅ
    let g‚ÇÇ' : W' ‚ü∂ Y‚ÇÇ := Over.homMk f‚ÇÇ (by simpa using h.symm =‚â´ Z.hom)
    exact hx g‚ÇÅ' g‚ÇÇ' hg‚ÇÅ hg‚ÇÇ (by ext; exact h)

instance (X : C) : (Over.forget X).IsCocontinuous (J.over X) J where
  cover_lift hS := J.overEquiv_symm_mem_over _ _ hS

instance (X : C) : (Over.forget X).IsContinuous (J.over X) J :=
  Functor.isContinuous_of_coverPreserving
    (over_forget_compatiblePreserving J X)
    (over_forget_coverPreserving J X)

/-- The pullback functor `Sheaf J A ‚•§ Sheaf (J.over X) A` -/
abbrev overPullback (A : Type u') [Category.{v'} A] (X : C) :
    Sheaf J A ‚•§ Sheaf (J.over X) A :=
  (Over.forget X).sheafPushforwardContinuous _ _ _

lemma over_map_coverPreserving {X Y : C} (f : X ‚ü∂ Y) :
    CoverPreserving (J.over X) (J.over Y) (Over.map f) where
  cover_preserve {U S} hS := by
    obtain ‚ü®S, rfl‚ü© := (Sieve.overEquiv U).symm.surjective S
    rw [Sieve.functorPushforward_over_map]
    apply overEquiv_symm_mem_over
    simpa [mem_over_iff] using hS

lemma over_map_compatiblePreserving {X Y : C} (f : X ‚ü∂ Y) :
    CompatiblePreserving (J.over Y) (Over.map f) where
  compatible {F Z _ x hx Y‚ÇÅ Y‚ÇÇ W f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ hg‚ÇÅ hg‚ÇÇ h} := by
    let W' : Over X := Over.mk (f‚ÇÅ.left ‚â´ Y‚ÇÅ.hom)
    let g‚ÇÅ' : W' ‚ü∂ Y‚ÇÅ := Over.homMk f‚ÇÅ.left
    let g‚ÇÇ' : W' ‚ü∂ Y‚ÇÇ := Over.homMk f‚ÇÇ.left
      (by simpa using (Over.forget _).congr_map h.symm =‚â´ Z.hom)
    let e : (Over.map f).obj W' ‚âÖ W := Over.isoMk (Iso.refl _)
      (by simpa [W'] using (Over.w f‚ÇÅ).symm)
    convert congr_arg (F.val.map e.inv.op)
      (hx g‚ÇÅ' g‚ÇÇ' hg‚ÇÅ hg‚ÇÇ (by ext; exact (Over.forget _).congr_map h)) using 1
    all_goals
      dsimp [e, W', g‚ÇÅ', g‚ÇÇ']
      rw [‚Üê FunctorToTypes.map_comp_apply]
      apply congr_fun
      congr 1
      rw [‚Üê op_comp]
      congr 1
      ext
      simp

instance {X Y : C} (f : X ‚ü∂ Y) : (Over.map f).IsContinuous (J.over X) (J.over Y) :=
  Functor.isContinuous_of_coverPreserving
    (over_map_compatiblePreserving J f)
    (over_map_coverPreserving J f)

/-- The pullback functor `Sheaf (J.over Y) A ‚•§ Sheaf (J.over X) A` induced
by a morphism `f : X ‚ü∂ Y`. -/
abbrev overMapPullback (A : Type u') [Category.{v'} A] {X Y : C} (f : X ‚ü∂ Y) :
    Sheaf (J.over Y) A ‚•§ Sheaf (J.over X) A :=
  (Over.map f).sheafPushforwardContinuous _ _ _

end GrothendieckTopology

variable {J}

/-- Given `F : Sheaf J A` and `X : C`, this is the pullback of `F` on `J.over X`. -/
abbrev Sheaf.over {A : Type u'} [Category.{v'} A] (F : Sheaf J A) (X : C) :
    Sheaf (J.over X) A := (J.overPullback A X).obj F

end CategoryTheory
