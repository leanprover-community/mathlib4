/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Sites.Grothendieck

/-! Localization

In this file, given a Grothendieck topology `J` on a category `C` and `X : C`, we construct
a Grothendieck topology `J.over X` on the category `Over X`. In order to do this,
we first construct a bijection `Sieve.overEquiv Y : Sieve Y â‰ƒ Sieve Y.left`
for all `Y : Over X`. Then, as it is stated in SGA 4 III 5.2.1, a sieve of `Y : Over X`
is covering for `J.over X` if and only if the corresponding sieve of `Y.left`
is covering for `J`.

-/

universe v u

namespace CategoryTheory

open Category

variable {C : Type u} [Category.{v} C]

namespace Sieve

/-- The equivalence `Sieve Y â‰ƒ Sieve Y.left` for all `Y : Over X`. -/
def overEquiv {X : C} (Y : Over X) :
    Sieve Y â‰ƒ Sieve Y.left where
  toFun S := Sieve.functorPushforward (Over.forget X) S
  invFun S' := Sieve.functorPullback (Over.forget X) S'
  left_inv S := by
    ext Z g
    dsimp [Presieve.functorPullback, Presieve.functorPushforward]
    constructor
    Â· rintro âŸ¨W, a, b, h, wâŸ©
      let c : Z âŸ¶ W := Over.homMk b
        (by rw [â† Over.w g, w, assoc, Over.w a])
      rw [show g = c â‰« a by ext; exact w]
      exact S.downward_closed h _
    Â· intro h
      exact âŸ¨Z, g, ðŸ™ _, h, by simpâŸ©
  right_inv S := by
    ext Z g
    dsimp [Presieve.functorPullback, Presieve.functorPushforward]
    constructor
    Â· rintro âŸ¨W, a, b, h, rflâŸ©
      exact S.downward_closed h _
    Â· intro h
      exact âŸ¨Over.mk ((g â‰« Y.hom)), Over.homMk g, ðŸ™ _, h, by simpâŸ©

@[simp]
lemma overEquiv_top {X : C} (Y : Over X) :
    overEquiv Y âŠ¤ = âŠ¤ := by
  ext Z g
  simp only [top_apply, iff_true]
  dsimp [overEquiv, Presieve.functorPushforward]
  exact âŸ¨Y, ðŸ™ Y, g, by simp, by simpâŸ©

@[simp]
lemma overEquiv_symm_top {X : C} (Y : Over X) :
    (overEquiv Y).symm âŠ¤ = âŠ¤ :=
  (overEquiv Y).injective (by simp)

lemma overEquiv_pullback {X : C} {Yâ‚ Yâ‚‚ : Over X} (f : Yâ‚ âŸ¶ Yâ‚‚) (S : Sieve Yâ‚‚) :
    overEquiv _ (S.pullback f) = (overEquiv _ S).pullback f.left := by
  ext Z g
  dsimp [overEquiv, Presieve.functorPushforward]
  constructor
  Â· rintro âŸ¨W, a, b, h, rflâŸ©
    exact âŸ¨W, a â‰« f, b, h, by simpâŸ©
  Â· rintro âŸ¨W, a, b, h, wâŸ©
    let T := Over.mk (b â‰« W.hom)
    let c : T âŸ¶ Yâ‚ := Over.homMk g (by dsimp; rw [â† Over.w a, â† reassoc_of% w, Over.w f])
    let d : T âŸ¶ W := Over.homMk b
    refine' âŸ¨T, c, ðŸ™ Z, _, by simpâŸ©
    rw [show c â‰« f = d â‰« a by ext; exact w]
    exact S.downward_closed h _

@[simp]
lemma overEquiv_symm_iff {X : C} {Y : Over X} (S : Sieve Y.left) {Z : Over X} (f : Z âŸ¶ Y) :
    (overEquiv Y).symm S f â†” S f.left := by
  rfl

lemma overEquiv_iff {X : C} {Y : Over X} (S : Sieve Y) {Z : C} (f : Z âŸ¶ Y.left) :
    overEquiv Y S f â†” S (Over.homMk f : Over.mk (f â‰« Y.hom) âŸ¶ Y) := by
  obtain âŸ¨S, rflâŸ© := (overEquiv Y).symm.surjective S
  simp

end Sieve

variable (J : GrothendieckTopology C)

namespace GrothendieckTopology

/-- The Grothendieck topology on the category `Over X` for any `X : C` that is
induced by a Grothendieck topology on `C`. -/
def over (X : C) : GrothendieckTopology (Over X) where
  sieves Y S := Sieve.overEquiv Y S âˆˆ J Y.left
  top_mem' Y := by
    change _ âˆˆ J Y.left
    simp
  pullback_stable' Yâ‚ Yâ‚‚ Sâ‚ f hâ‚ := by
    change _ âˆˆ J _ at hâ‚ âŠ¢
    rw [Sieve.overEquiv_pullback]
    exact J.pullback_stable _ hâ‚
  transitive' Y S (hS : _ âˆˆ J _) R hR := J.transitive hS _ (fun Z f hf => by
    have hf' : _ âˆˆ J _ := hR ((Sieve.overEquiv_iff _ _).1 hf)
    rw [Sieve.overEquiv_pullback] at hf'
    exact hf')

lemma mem_over_iff {X : C} {Y : Over X} (S : Sieve Y) :
    S âˆˆ (J.over X) Y â†” Sieve.overEquiv _ S âˆˆ J Y.left := by
  rfl

lemma overEquiv_symm_mem_over {X : C} (Y : Over X) (S : Sieve Y.left) (hS : S âˆˆ J Y.left) :
    (Sieve.overEquiv Y).symm S âˆˆ (J.over X) Y := by
  simpa only [mem_over_iff, Equiv.apply_symm_apply] using hS

end GrothendieckTopology

end CategoryTheory
