/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import Mathlib.CategoryTheory.Sites.Over

/-! Internal hom of sheaves

In this file, given two sheaves `F` and `G` in `Sheaf J A`
(with `J : GrothendieckTopology C`), we shall define a sheaf of types
`Sheaf.internalHom F G` which sends `X : C` to the type of morphisms
between the restrictions of `F` and `G` to the categories `Over X`.

We first define `Presheaf.internalHom F G` when `F` and `G` are
presheaves `Cáµ’áµ– â¥¤ A` and show that it is a sheaf when `G` is a sheaf.

-/

universe v v' u u'

namespace CategoryTheory

open Category Opposite Limits

variable {C : Type u} [Category.{v} C] {J : GrothendieckTopology C}
  {A : Type u'} [Category.{v'} A]

namespace Presheaf

variable (F G : Cáµ’áµ– â¥¤ A)

/-- Given two presheaves `F` and `G` on a category `C` with values in a category `A`,
this `internalHom F G` is the presheaf of types which sends an object `X : C`
to the type of morphisms between the "restrictions" of `F` and `G` to the category `Over X`. -/
@[simps! obj]
def internalHom : Cáµ’áµ– â¥¤ Type _ where
  obj X := (Over.forget X.unop).op â‹™ F âŸ¶ (Over.forget X.unop).op â‹™ G
  map f := whiskerLeft (Over.map f.unop).op
  map_id := by
    rintro âŸ¨XâŸ©
    dsimp
    ext Ï† âŸ¨YâŸ©
    simpa [Over.mapId] using Ï†.naturality ((Over.mapId X).hom.app Y).op
  map_comp := by
    rintro âŸ¨XâŸ© âŸ¨YâŸ© âŸ¨ZâŸ© âŸ¨f : Y âŸ¶ XâŸ© âŸ¨g : Z âŸ¶ YâŸ©
    dsimp
    ext Ï† âŸ¨WâŸ©
    simpa [Over.mapComp] using Ï†.naturality ((Over.mapComp g f).hom.app W).op

variable {F G}

/-- Equational lemma for the presheaf structure on `Presheaf.internalHom`.
It is advisable to use this lemma rather than `dsimp [internalHom]` which may result
in the need to prove equalities of objects in an `Over` category. -/
lemma internalHom_map_app {X Y Z : C} (f : Z âŸ¶ Y) (g : Y âŸ¶ X) (h : Z âŸ¶ X) (w : f â‰« g = h)
    (Î± : (internalHom F G).obj (op X)) :
    ((internalHom F G).map g.op Î±).app (op (Over.mk f)) =
      Î±.app (op (Over.mk h)) := by
  subst w
  rfl

@[simp]
lemma internalHom_map_app_op_mk_id {X Y : C} (g : Y âŸ¶ X)
    (Î± : (internalHom F G).obj (op X)) :
    ((internalHom F G).map g.op Î±).app (op (Over.mk (ðŸ™ Y))) =
      Î±.app (op (Over.mk g)) :=
  internalHom_map_app (ðŸ™ Y) g g (by simp) Î±

variable (F G)

/-- The sections of the presheaf `internalHom F G` identify to morphisms `F âŸ¶ G`. -/
def internalHomSectionsEquiv : (internalHom F G).sections â‰ƒ (F âŸ¶ G) where
  toFun s :=
    { app := fun X => (s.1 X).app âŸ¨Over.mk (ðŸ™ _)âŸ©
      naturality := by
        rintro âŸ¨Xâ‚âŸ© âŸ¨Xâ‚‚âŸ© âŸ¨f : Xâ‚‚ âŸ¶ Xâ‚âŸ©
        dsimp
        refine' Eq.trans _ ((s.1 âŸ¨Xâ‚âŸ©).naturality
          (Over.homMk f : Over.mk f âŸ¶ Over.mk (ðŸ™ Xâ‚)).op)
        erw [â† s.2 f.op, internalHom_map_app_op_mk_id]
        rfl }
  invFun f := âŸ¨fun X => whiskerLeft _ f, fun _ => rflâŸ©
  left_inv s := by
    dsimp
    ext âŸ¨XâŸ© âŸ¨Y : Over XâŸ©
    have H := s.2 Y.hom.op
    dsimp at H âŠ¢
    rw [â† H]
    apply internalHom_map_app_op_mk_id
  right_inv f := rfl

variable {F G}

lemma InternalHom.isAmalgamation_iff {X : C} (S : Sieve X)
    (x : Presieve.FamilyOfElements (internalHom F G) S.arrows)
    (hx : x.Compatible) (y : (internalHom F G).obj (op X)) :
    x.IsAmalgamation y â†” âˆ€ (Y : C) (g : Y âŸ¶ X) (hg : S g),
      y.app (op (Over.mk g)) = (x g hg).app (op (Over.mk (ðŸ™ Y))) := by
  constructor
  Â· intro h Y g hg
    rw [â† h g hg, internalHom_map_app_op_mk_id]
  Â· intro h Y g hg
    dsimp
    ext âŸ¨W : Over YâŸ©
    refine (h W.left (W.hom â‰« g) (S.downward_closed hg _)).trans ?_
    have H := hx (ðŸ™ _) W.hom (S.downward_closed hg W.hom) hg (by simp)
    dsimp at H
    simp only [Functor.map_id, FunctorToTypes.map_id_apply] at H
    rw [H, internalHom_map_app_op_mk_id]
    rfl

variable (F G)

lemma internalHom_isSheafFor {X : C} (S : Sieve X)
    (hG : âˆ€ â¦ƒY : Câ¦„ (f : Y âŸ¶ X), IsLimit (G.mapCone (S.pullback f).arrows.cocone.op)) :
    Presieve.IsSheafFor (internalHom F G) S.arrows := by
  intro x hx
  apply exists_unique_of_exists_of_unique
  Â· have Î¦ : âˆ€ {Y : C} (g : Y âŸ¶ X), âˆƒ (Ï† : F.obj (op Y) âŸ¶ G.obj (op Y)),
      âˆ€ {Z : C} (p : Z âŸ¶ Y) (hp : S (p â‰« g)), Ï† â‰« G.map p.op =
        F.map p.op â‰« (x (p â‰« g) hp).app âŸ¨Over.mk (ðŸ™ Z)âŸ© := fun {Y} g => by
        let c : Cone ((Presieve.diagram (Sieve.pullback g S).arrows).op â‹™ G) :=
          { pt := F.obj (op Y)
            Ï€ :=
              { app := fun âŸ¨Z, hZâŸ© => F.map Z.hom.op â‰« (x _ hZ).app (op (Over.mk (ðŸ™ _)))
                naturality := by
                  rintro âŸ¨Zâ‚, hZâ‚âŸ© âŸ¨Zâ‚‚, hZâ‚‚âŸ© âŸ¨f : Zâ‚‚ âŸ¶ Zâ‚âŸ©
                  dsimp
                  rw [id_comp, assoc]
                  have H := hx f.left (ðŸ™ _) hZâ‚ hZâ‚‚ (by simp)
                  simp only [internalHom_obj, unop_op, Functor.id_obj, op_id,
                    FunctorToTypes.map_id_apply] at H
                  let Ï† : Over.mk f.left âŸ¶ Over.mk (ðŸ™ Zâ‚.left) := Over.homMk f.left
                  have H' := (x (Zâ‚.hom â‰« g) hZâ‚).naturality Ï†.op
                  dsimp at H H' âŠ¢
                  erw [â† H, â† H', internalHom_map_app_op_mk_id, â† F.map_comp_assoc,
                    â† op_comp, Over.w f] } }
        use (hG g).lift c
        intro Z p hp
        exact ((hG g).fac c âŸ¨Over.mk p, hpâŸ©)
    let app : âˆ€ {Y : C} (_ : Y âŸ¶ X), F.obj (op Y) âŸ¶ G.obj (op Y) := fun g => (Î¦ g).choose
    have happ : âˆ€ {Y : C} (g : Y âŸ¶ X) {Z : C} (p : Z âŸ¶ Y) (hp : S (p â‰« g)),
      app g â‰« G.map p.op = F.map p.op â‰« (x (p â‰« g) hp).app âŸ¨Over.mk (ðŸ™ Z)âŸ© :=
        fun g => (Î¦ g).choose_spec
    refine âŸ¨
      { app := fun Y => app Y.unop.hom
        naturality := by
          rintro âŸ¨Yâ‚ : Over XâŸ© âŸ¨Yâ‚‚ : Over XâŸ© âŸ¨Ï† : Yâ‚‚ âŸ¶ Yâ‚âŸ©
          apply (hG Yâ‚‚.hom).hom_ext
          rintro âŸ¨Z : Over Yâ‚‚.left, hZâŸ©
          change (F.map Ï†.left.op â‰« app Yâ‚‚.hom) â‰« G.map Z.hom.op =
            (app Yâ‚.hom â‰« G.map Ï†.left.op) â‰« G.map Z.hom.op
          rw [assoc, assoc, happ Yâ‚‚.hom Z.hom hZ, â† G.map_comp, â† op_comp,
            happ Yâ‚.hom (Z.hom â‰« Ï†.left) (by simpa using hZ), â† F.map_comp_assoc, op_comp]
          congr 3
          simp }, ?_âŸ©
    rw [InternalHom.isAmalgamation_iff _ _ hx]
    intro Y g hg
    dsimp
    have H := happ g (ðŸ™ _) (by simpa using hg)
    rw [op_id, G.map_id, comp_id, F.map_id, id_comp] at H
    exact H.trans (by congr; simp)
  Â· intro yâ‚ yâ‚‚ hyâ‚ hyâ‚‚
    rw [InternalHom.isAmalgamation_iff _ _ hx] at hyâ‚ hyâ‚‚
    apply NatTrans.ext
    ext âŸ¨Y : Over XâŸ©
    apply (hG Y.hom).hom_ext
    rintro âŸ¨Z : Over Y.left, hZâŸ©
    dsimp
    let Ï† : Over.mk (Z.hom â‰« Y.hom) âŸ¶ Y := Over.homMk Z.hom
    refine' (yâ‚.naturality Ï†.op).symm.trans (Eq.trans _ (yâ‚‚.naturality Ï†.op))
    rw [(hyâ‚ _ _ hZ), â† ((hyâ‚‚ _ _ hZ))]

lemma internalHom_isSheaf (hG : IsSheaf J G) :
    IsSheaf J (internalHom F G) := by
  rw [isSheaf_iff_isSheaf_of_type]
  intro X S hS
  exact internalHom_isSheafFor F G S
    (fun _ _ => ((isSheaf_iff_isLimit J G).1 hG _ (J.pullback_stable _ hS)).some)

end Presheaf

end CategoryTheory
