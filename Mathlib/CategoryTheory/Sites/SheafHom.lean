/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/

import Mathlib.CategoryTheory.Sites.Over

/-! Internal hom of sheaves

In this file, given two sheaves `F` and `G` on a site `(C, J)` with values
in a category `A`, we shall define a sheaf of types
`sheafHom F G` which sends `X : C` to the type of morphisms
between the restrictions of `F` and `G` to the categories `Over X`.

We first define `presheafHom F G` when `F` and `G` are
presheaves `C·µí·µñ ‚•§ A` and show that it is a sheaf when `G` is a sheaf.

-/

universe v v' u u'

namespace CategoryTheory

open Category Opposite Limits

variable {C : Type u} [Category.{v} C] {J : GrothendieckTopology C}
  {A : Type u'} [Category.{v'} A]

variable (F G : C·µí·µñ ‚•§ A)

/-- Given two presheaves `F` and `G` on a category `C` with values in a category `A`,
this `presheafHom F G` is the presheaf of types which sends an object `X : C`
to the type of morphisms between the "restrictions" of `F` and `G` to the category `Over X`. -/
@[simps! obj]
def presheafHom : C·µí·µñ ‚•§ Type _ where
  obj X := (Over.forget X.unop).op ‚ãô F ‚ü∂ (Over.forget X.unop).op ‚ãô G
  map f := whiskerLeft (Over.map f.unop).op
  map_id := by
    rintro ‚ü®X‚ü©
    ext œÜ ‚ü®Y‚ü©
    simpa [Over.mapId] using œÜ.naturality ((Over.mapId X).hom.app Y).op
  map_comp := by
    rintro ‚ü®X‚ü© ‚ü®Y‚ü© ‚ü®Z‚ü© ‚ü®f : Y ‚ü∂ X‚ü© ‚ü®g : Z ‚ü∂ Y‚ü©
    ext œÜ ‚ü®W‚ü©
    simpa [Over.mapComp] using œÜ.naturality ((Over.mapComp g f).hom.app W).op

variable {F G}

/-- Equational lemma for the presheaf structure on `presheafHom`.
It is advisable to use this lemma rather than `dsimp [presheafHom]` which may result
in the need to prove equalities of objects in an `Over` category. -/
lemma presheafHom_map_app {X Y Z : C} (f : Z ‚ü∂ Y) (g : Y ‚ü∂ X) (h : Z ‚ü∂ X) (w : f ‚â´ g = h)
    (Œ± : (presheafHom F G).obj (op X)) :
    ((presheafHom F G).map g.op Œ±).app (op (Over.mk f)) =
      Œ±.app (op (Over.mk h)) := by
  subst w
  rfl

@[simp]
lemma presheafHom_map_app_op_mk_id {X Y : C} (g : Y ‚ü∂ X)
    (Œ± : (presheafHom F G).obj (op X)) :
    ((presheafHom F G).map g.op Œ±).app (op (Over.mk (ùüô Y))) =
      Œ±.app (op (Over.mk g)) :=
  presheafHom_map_app (ùüô Y) g g (by simp) Œ±

variable (F G)

/-- The sections of the presheaf `presheafHom F G` identify to morphisms `F ‚ü∂ G`. -/
def presheafHomSectionsEquiv : (presheafHom F G).sections ‚âÉ (F ‚ü∂ G) where
  toFun s :=
    { app := fun X => (s.1 X).app ‚ü®Over.mk (ùüô _)‚ü©
      naturality := by
        rintro ‚ü®X‚ÇÅ‚ü© ‚ü®X‚ÇÇ‚ü© ‚ü®f : X‚ÇÇ ‚ü∂ X‚ÇÅ‚ü©
        dsimp
        refine' Eq.trans _ ((s.1 ‚ü®X‚ÇÅ‚ü©).naturality
          (Over.homMk f : Over.mk f ‚ü∂ Over.mk (ùüô X‚ÇÅ)).op)
        erw [‚Üê s.2 f.op, presheafHom_map_app_op_mk_id]
        rfl }
  invFun f := ‚ü®fun X => whiskerLeft _ f, fun _ => rfl‚ü©
  left_inv s := by
    dsimp
    ext ‚ü®X‚ü© ‚ü®Y : Over X‚ü©
    have H := s.2 Y.hom.op
    dsimp at H ‚ä¢
    rw [‚Üê H]
    apply presheafHom_map_app_op_mk_id
  right_inv f := rfl

variable {F G}

lemma PresheafHom.isAmalgamation_iff {X : C} (S : Sieve X)
    (x : Presieve.FamilyOfElements (presheafHom F G) S.arrows)
    (hx : x.Compatible) (y : (presheafHom F G).obj (op X)) :
    x.IsAmalgamation y ‚Üî ‚àÄ (Y : C) (g : Y ‚ü∂ X) (hg : S g),
      y.app (op (Over.mk g)) = (x g hg).app (op (Over.mk (ùüô Y))) := by
  constructor
  ¬∑ intro h Y g hg
    rw [‚Üê h g hg, presheafHom_map_app_op_mk_id]
  ¬∑ intro h Y g hg
    dsimp
    ext ‚ü®W : Over Y‚ü©
    refine (h W.left (W.hom ‚â´ g) (S.downward_closed hg _)).trans ?_
    have H := hx (ùüô _) W.hom (S.downward_closed hg W.hom) hg (by simp)
    dsimp at H
    simp only [Functor.map_id, FunctorToTypes.map_id_apply] at H
    rw [H, presheafHom_map_app_op_mk_id]
    rfl

section

variable {X : C} {S : Sieve X}
    (hG : ‚àÄ ‚¶ÉY : C‚¶Ñ (f : Y ‚ü∂ X), IsLimit (G.mapCone (S.pullback f).arrows.cocone.op))

namespace PresheafHom.IsSheafFor

variable (x : Presieve.FamilyOfElements (presheafHom F G) S.arrows) (hx : x.Compatible)
  {Y : C} (g : Y ‚ü∂ X)

lemma exists_app :
    ‚àÉ (œÜ : F.obj (op Y) ‚ü∂ G.obj (op Y)),
      ‚àÄ {Z : C} (p : Z ‚ü∂ Y) (hp : S (p ‚â´ g)), œÜ ‚â´ G.map p.op =
        F.map p.op ‚â´ (x (p ‚â´ g) hp).app ‚ü®Over.mk (ùüô Z)‚ü© := by
  let c : Cone ((Presieve.diagram (Sieve.pullback g S).arrows).op ‚ãô G) :=
    { pt := F.obj (op Y)
      œÄ :=
        { app := fun ‚ü®Z, hZ‚ü© => F.map Z.hom.op ‚â´ (x _ hZ).app (op (Over.mk (ùüô _)))
          naturality := by
            rintro ‚ü®Z‚ÇÅ, hZ‚ÇÅ‚ü© ‚ü®Z‚ÇÇ, hZ‚ÇÇ‚ü© ‚ü®f : Z‚ÇÇ ‚ü∂ Z‚ÇÅ‚ü©
            dsimp
            rw [id_comp, assoc]
            have H := hx f.left (ùüô _) hZ‚ÇÅ hZ‚ÇÇ (by simp)
            simp only [presheafHom_obj, unop_op, Functor.id_obj, op_id,
              FunctorToTypes.map_id_apply] at H
            let œÜ : Over.mk f.left ‚ü∂ Over.mk (ùüô Z‚ÇÅ.left) := Over.homMk f.left
            have H' := (x (Z‚ÇÅ.hom ‚â´ g) hZ‚ÇÅ).naturality œÜ.op
            dsimp at H H' ‚ä¢
            erw [‚Üê H, ‚Üê H', presheafHom_map_app_op_mk_id, ‚Üê F.map_comp_assoc,
              ‚Üê op_comp, Over.w f] } }
  use (hG g).lift c
  intro Z p hp
  exact ((hG g).fac c ‚ü®Over.mk p, hp‚ü©)

/-- Auxiliary definition for `presheafHom_isSheafFor`. -/
noncomputable def app : F.obj (op Y) ‚ü∂ G.obj (op Y) := (exists_app hG x hx g).choose

lemma app_cond {Z : C} (p : Z ‚ü∂ Y) (hp : S (p ‚â´ g)) :
    app hG x hx g ‚â´ G.map p.op = F.map p.op ‚â´ (x (p ‚â´ g) hp).app ‚ü®Over.mk (ùüô Z)‚ü© :=
  (exists_app hG x hx g).choose_spec p hp

end PresheafHom.IsSheafFor

variable (F G S)

open PresheafHom.IsSheafFor in
lemma presheafHom_isSheafFor  :
    Presieve.IsSheafFor (presheafHom F G) S.arrows := by
  intro x hx
  apply exists_unique_of_exists_of_unique
  ¬∑ refine' ‚ü®
      { app := fun Y => app hG x hx Y.unop.hom
        naturality := by
          rintro ‚ü®Y‚ÇÅ : Over X‚ü© ‚ü®Y‚ÇÇ : Over X‚ü© ‚ü®œÜ : Y‚ÇÇ ‚ü∂ Y‚ÇÅ‚ü©
          apply (hG Y‚ÇÇ.hom).hom_ext
          rintro ‚ü®Z : Over Y‚ÇÇ.left, hZ‚ü©
          dsimp
          rw [assoc, assoc, app_cond hG x hx Y‚ÇÇ.hom Z.hom hZ, ‚Üê G.map_comp, ‚Üê op_comp]
          erw [app_cond hG x hx Y‚ÇÅ.hom (Z.hom ‚â´ œÜ.left) (by simpa using hZ),
            ‚Üê F.map_comp_assoc, op_comp]
          congr 3
          simp }, _‚ü©
    rw [PresheafHom.isAmalgamation_iff _ _ hx]
    intro Y g hg
    dsimp
    have H := app_cond hG x hx g (ùüô _) (by simpa using hg)
    rw [op_id, G.map_id, comp_id, F.map_id, id_comp] at H
    exact H.trans (by congr; simp)
  ¬∑ intro y‚ÇÅ y‚ÇÇ hy‚ÇÅ hy‚ÇÇ
    rw [PresheafHom.isAmalgamation_iff _ _ hx] at hy‚ÇÅ hy‚ÇÇ
    apply NatTrans.ext
    ext ‚ü®Y : Over X‚ü©
    apply (hG Y.hom).hom_ext
    rintro ‚ü®Z : Over Y.left, hZ‚ü©
    dsimp
    let œÜ : Over.mk (Z.hom ‚â´ Y.hom) ‚ü∂ Y := Over.homMk Z.hom
    refine' (y‚ÇÅ.naturality œÜ.op).symm.trans (Eq.trans _ (y‚ÇÇ.naturality œÜ.op))
    rw [(hy‚ÇÅ _ _ hZ), ‚Üê ((hy‚ÇÇ _ _ hZ))]

end

variable (F G)

lemma Presheaf.IsSheaf.hom (hG : Presheaf.IsSheaf J G) :
    Presheaf.IsSheaf J (presheafHom F G) := by
  rw [isSheaf_iff_isSheaf_of_type]
  intro X S hS
  exact presheafHom_isSheafFor F G S
    (fun _ _ => ((Presheaf.isSheaf_iff_isLimit J G).1 hG _ (J.pullback_stable _ hS)).some)

/-- Given two sheaves `F` and `G`, this is the underlying presheaf of `sheafHom F G`:
it is isomorphic to `presheafHom F.1 G.1` (see `sheafHom'Iso`), but it is better
definitional properties. -/
def sheafHom' (F G : Sheaf J A) : C·µí·µñ ‚•§ Type _ where
  obj X := (J.overPullback A X.unop).obj F ‚ü∂ (J.overPullback A X.unop).obj G
  map f := fun œÜ => (J.overMapPullback A f.unop).map œÜ
  map_id X := by
    ext œÜ : 2
    exact congr_fun ((presheafHom F.1 G.1).map_id X) œÜ.1
  map_comp f g := by
    ext œÜ : 2
    exact congr_fun ((presheafHom F.1 G.1).map_comp f g) œÜ.1

/-- Auxiliary isomorphism for the definition of `sheafHom`. -/
def sheafHom'Iso (F G : Sheaf J A) :
    sheafHom' F G ‚âÖ presheafHom F.1 G.1 :=
  NatIso.ofComponents (fun _ => (equivOfFullyFaithful (sheafToPresheaf _ _)).toIso)
    (fun _ => rfl)

/-- The sheaf of morphisms `F ‚ü∂ G`: its sections on an object `X` are the morphisms
between the "restrictions" of `F` and `G` to the category `Over X`. -/
def sheafHom (F G : Sheaf J A) : Sheaf J (Type _) where
  val := sheafHom' F G
  cond := (Presheaf.isSheaf_of_iso_iff (sheafHom'Iso F G)).2
    (Presheaf.IsSheaf.hom F.1 G.1 G.2)

/-- The sections of the sheaf `sheafHom F G` identify to morphisms `F ‚ü∂ G`. -/
def sheafHomSectionsEquiv (F G : Sheaf J A) :
    (sheafHom F G).1.sections ‚âÉ (F ‚ü∂ G) :=
  ((Functor.sectionsFunctor C·µí·µñ).mapIso (sheafHom'Iso F G)).toEquiv.trans
    ((presheafHomSectionsEquiv F.1 G.1).trans (equivOfFullyFaithful (sheafToPresheaf J A)).symm)

@[simp]
lemma sheafHomSectionsEquiv_symm_apply_coe_apply {F G : Sheaf J A} (œÜ : F ‚ü∂ G) (X : C·µí·µñ):
    ((sheafHomSectionsEquiv F G).symm œÜ).1 X = (J.overPullback A X.unop).map œÜ := by
  rfl

@[simp]
lemma overPullback_map_sheafHomSectionsEquiv_apply {F G : Sheaf J A}
    (s : (sheafHom F G).1.sections) (X : C) :
    (J.overPullback A X).map (sheafHomSectionsEquiv F G s) = s.1 (Opposite.op X) := by
  obtain ‚ü®œÜ, rfl‚ü© := (sheafHomSectionsEquiv F G).symm.surjective s
  simp

end CategoryTheory
