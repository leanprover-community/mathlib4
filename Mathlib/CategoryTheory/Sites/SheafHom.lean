/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import Mathlib.CategoryTheory.Sites.Over

/-! Internal hom of sheaves

In this file, given two sheaves `F` and `G` on a site `(C, J)` with values
in a category `A`, we define a sheaf of types
`sheafHom F G` which sends `X : C` to the type of morphisms
between the restrictions of `F` and `G` to the categories `Over X`.

We first define `presheafHom F G` when `F` and `G` are
presheaves `Cáµ’áµ– â¥¤ A` and show that it is a sheaf when `G` is a sheaf.

TODO:
- turn both `presheafHom` and `sheafHom` into bifunctors
- for a sheaf of types `F`, the `sheafHom` functor from `F` is right-adjoint to
the product functor with `F`, i.e. for all `X` and `Y`, there is a
natural bijection `(X â¨¯ F âŸ¶ Y) â‰ƒ (X âŸ¶ sheafHom F Y)`.
- use these results in order to show that the category of sheaves of types is Cartesian closed

-/

universe v v' u u'

namespace CategoryTheory

open Category Opposite Limits

variable {C : Type u} [Category.{v} C] {J : GrothendieckTopology C}
  {A : Type u'} [Category.{v'} A]

variable (F G : Cáµ’áµ– â¥¤ A)

/-- Given two presheaves `F` and `G` on a category `C` with values in a category `A`,
this `presheafHom F G` is the presheaf of types which sends an object `X : C`
to the type of morphisms between the "restrictions" of `F` and `G` to the category `Over X`. -/
@[simps! obj]
def presheafHom : Cáµ’áµ– â¥¤ Type _ where
  obj X := (Over.forget X.unop).op â‹™ F âŸ¶ (Over.forget X.unop).op â‹™ G
  map f := Functor.whiskerLeft (Over.map f.unop).op
  map_id := by
    rintro âŸ¨XâŸ©
    ext Ï† âŸ¨YâŸ©
    simpa [Over.mapId] using Ï†.naturality ((Over.mapId X).hom.app Y).op
  map_comp := by
    rintro âŸ¨XâŸ© âŸ¨YâŸ© âŸ¨ZâŸ© âŸ¨f : Y âŸ¶ XâŸ© âŸ¨g : Z âŸ¶ YâŸ©
    ext Ï† âŸ¨WâŸ©
    simpa [Over.mapComp] using Ï†.naturality ((Over.mapComp g f).hom.app W).op

variable {F G}

/-- Equational lemma for the presheaf structure on `presheafHom`.
It is advisable to use this lemma rather than `dsimp [presheafHom]` which may result
in the need to prove equalities of objects in an `Over` category. -/
lemma presheafHom_map_app {X Y Z : C} (f : Z âŸ¶ Y) (g : Y âŸ¶ X) (h : Z âŸ¶ X) (w : f â‰« g = h)
    (Î± : (presheafHom F G).obj (op X)) :
    ((presheafHom F G).map g.op Î±).app (op (Over.mk f)) =
      Î±.app (op (Over.mk h)) := by
  subst w
  rfl

@[simp]
lemma presheafHom_map_app_op_mk_id {X Y : C} (g : Y âŸ¶ X)
    (Î± : (presheafHom F G).obj (op X)) :
    ((presheafHom F G).map g.op Î±).app (op (Over.mk (ðŸ™ Y))) =
      Î±.app (op (Over.mk g)) :=
  presheafHom_map_app (ðŸ™ Y) g g (by simp) Î±

variable (F G)

/-- The sections of the presheaf `presheafHom F G` identify to morphisms `F âŸ¶ G`. -/
def presheafHomSectionsEquiv : (presheafHom F G).sections â‰ƒ (F âŸ¶ G) where
  toFun s :=
    { app := fun X => (s.1 X).app âŸ¨Over.mk (ðŸ™ _)âŸ©
      naturality := by
        rintro âŸ¨Xâ‚âŸ© âŸ¨Xâ‚‚âŸ© âŸ¨f : Xâ‚‚ âŸ¶ Xâ‚âŸ©
        dsimp
        refine Eq.trans ?_ ((s.1 âŸ¨Xâ‚âŸ©).naturality
          (Over.homMk f : Over.mk f âŸ¶ Over.mk (ðŸ™ Xâ‚)).op)
        rw [â† s.2 f.op, presheafHom_map_app_op_mk_id]
        rfl }
  invFun f := âŸ¨fun _ => Functor.whiskerLeft _ f, fun _ => rflâŸ©
  left_inv s := by
    dsimp
    ext âŸ¨XâŸ© âŸ¨Y : Over XâŸ©
    have H := s.2 Y.hom.op
    dsimp at H âŠ¢
    rw [â† H]
    apply presheafHom_map_app_op_mk_id

variable {F G}

lemma PresheafHom.isAmalgamation_iff {X : C} (S : Sieve X)
    (x : Presieve.FamilyOfElements (presheafHom F G) S.arrows)
    (hx : x.Compatible) (y : (presheafHom F G).obj (op X)) :
    x.IsAmalgamation y â†” âˆ€ (Y : C) (g : Y âŸ¶ X) (hg : S g),
      y.app (op (Over.mk g)) = (x g hg).app (op (Over.mk (ðŸ™ Y))) := by
  constructor
  Â· intro h Y g hg
    rw [â† h g hg, presheafHom_map_app_op_mk_id]
  Â· intro h Y g hg
    dsimp
    ext âŸ¨W : Over YâŸ©
    refine (h W.left (W.hom â‰« g) (S.downward_closed hg _)).trans ?_
    have H := hx (ðŸ™ _) W.hom (S.downward_closed hg W.hom) hg (by simp)
    dsimp at H
    simp only [FunctorToTypes.map_id_apply] at H
    rw [H, presheafHom_map_app_op_mk_id]
    rfl

section

variable {X : C} {S : Sieve X}
    (hG : âˆ€ â¦ƒY : Câ¦„ (f : Y âŸ¶ X), IsLimit (G.mapCone (S.pullback f).arrows.cocone.op))

namespace PresheafHom.IsSheafFor

variable (x : Presieve.FamilyOfElements (presheafHom F G) S.arrows) {Y : C}

include hG in
lemma exists_app (hx : x.Compatible) (g : Y âŸ¶ X) :
    âˆƒ (Ï† : F.obj (op Y) âŸ¶ G.obj (op Y)),
      âˆ€ {Z : C} (p : Z âŸ¶ Y) (hp : S (p â‰« g)), Ï† â‰« G.map p.op =
        F.map p.op â‰« (x (p â‰« g) hp).app âŸ¨Over.mk (ðŸ™ Z)âŸ© := by
  let c : Cone ((Presieve.diagram (Sieve.pullback g S).arrows).op â‹™ G) :=
    { pt := F.obj (op Y)
      Ï€ :=
        { app := fun âŸ¨Z, hZâŸ© => F.map Z.hom.op â‰« (x _ hZ).app (op (Over.mk (ðŸ™ _)))
          naturality := by
            rintro âŸ¨Zâ‚, hZâ‚âŸ© âŸ¨Zâ‚‚, hZâ‚‚âŸ© âŸ¨f : Zâ‚‚ âŸ¶ Zâ‚âŸ©
            dsimp
            rw [id_comp, assoc]
            have H := hx f.left (ðŸ™ _) hZâ‚ hZâ‚‚ (by simp)
            simp only [presheafHom_obj, unop_op, Functor.id_obj, op_id,
              FunctorToTypes.map_id_apply] at H
            let Ï† : Over.mk f.left âŸ¶ Over.mk (ðŸ™ Zâ‚.left) := Over.homMk f.left
            have H' := (x (Zâ‚.hom â‰« g) hZâ‚).naturality Ï†.op
            dsimp at H H' âŠ¢
            erw [â† H, â† H', presheafHom_map_app_op_mk_id, â† F.map_comp_assoc,
              â† op_comp, Over.w f] } }
  use (hG g).lift c
  intro Z p hp
  exact ((hG g).fac c âŸ¨Over.mk p, hpâŸ©)

/-- Auxiliary definition for `presheafHom_isSheafFor`. -/
noncomputable def app (hx : x.Compatible) (g : Y âŸ¶ X) : F.obj (op Y) âŸ¶ G.obj (op Y) :=
  (exists_app hG x hx g).choose

lemma app_cond (hx : x.Compatible) (g : Y âŸ¶ X) {Z : C} (p : Z âŸ¶ Y) (hp : S (p â‰« g)) :
    app hG x hx g â‰« G.map p.op = F.map p.op â‰« (x (p â‰« g) hp).app âŸ¨Over.mk (ðŸ™ Z)âŸ© :=
  (exists_app hG x hx g).choose_spec p hp

end PresheafHom.IsSheafFor

variable (F G S)

include hG in
open PresheafHom.IsSheafFor in
lemma presheafHom_isSheafFor :
    Presieve.IsSheafFor (presheafHom F G) S.arrows := by
  intro x hx
  apply existsUnique_of_exists_of_unique
  Â· refine âŸ¨
      { app := fun Y => app hG x hx Y.unop.hom
        naturality := by
          rintro âŸ¨Yâ‚ : Over XâŸ© âŸ¨Yâ‚‚ : Over XâŸ© âŸ¨Ï† : Yâ‚‚ âŸ¶ Yâ‚âŸ©
          apply (hG Yâ‚‚.hom).hom_ext
          rintro âŸ¨Z : Over Yâ‚‚.left, hZâŸ©
          dsimp
          rw [assoc, assoc, app_cond hG x hx Yâ‚‚.hom Z.hom hZ, â† G.map_comp, â† op_comp]
          rw [app_cond hG x hx Yâ‚.hom (Z.hom â‰« Ï†.left) (by simpa using hZ),
            â† F.map_comp_assoc, op_comp]
          congr 3
          simp }, ?_âŸ©
    rw [PresheafHom.isAmalgamation_iff _ _ hx]
    intro Y g hg
    dsimp
    have H := app_cond hG x hx g (ðŸ™ _) (by simpa using hg)
    rw [op_id, G.map_id, comp_id, F.map_id, id_comp] at H
    exact H.trans (by congr; simp)
  Â· intro yâ‚ yâ‚‚ hyâ‚ hyâ‚‚
    rw [PresheafHom.isAmalgamation_iff _ _ hx] at hyâ‚ hyâ‚‚
    apply NatTrans.ext
    ext âŸ¨Y : Over XâŸ©
    apply (hG Y.hom).hom_ext
    rintro âŸ¨Z : Over Y.left, hZâŸ©
    dsimp
    let Ï† : Over.mk (Z.hom â‰« Y.hom) âŸ¶ Y := Over.homMk Z.hom
    refine (yâ‚.naturality Ï†.op).symm.trans (Eq.trans ?_ (yâ‚‚.naturality Ï†.op))
    rw [(hyâ‚ _ _ hZ), â† ((hyâ‚‚ _ _ hZ))]

end

variable (F G)

lemma Presheaf.IsSheaf.hom (hG : Presheaf.IsSheaf J G) :
    Presheaf.IsSheaf J (presheafHom F G) := by
  rw [isSheaf_iff_isSheaf_of_type]
  intro X S hS
  exact presheafHom_isSheafFor F G S
    (fun _ _ => ((Presheaf.isSheaf_iff_isLimit J G).1 hG _ (J.pullback_stable _ hS)).some)


/-- The underlying presheaf of `sheafHom F G`. It is isomorphic to `presheafHom F.1 G.1`
(see `sheafHom'Iso`), but has better definitional properties. -/
def sheafHom' (F G : Sheaf J A) : Cáµ’áµ– â¥¤ Type _ where
  obj X := (J.overPullback A X.unop).obj F âŸ¶ (J.overPullback A X.unop).obj G
  map f := fun Ï† => (J.overMapPullback A f.unop).map Ï†
  map_id X := by
    ext Ï† : 2
    exact congr_fun ((presheafHom F.1 G.1).map_id X) Ï†.1
  map_comp f g := by
    ext Ï† : 2
    exact congr_fun ((presheafHom F.1 G.1).map_comp f g) Ï†.1

/-- The canonical isomorphism `sheafHom' F G â‰… presheafHom F.1 G.1`. -/
def sheafHom'Iso (F G : Sheaf J A) :
    sheafHom' F G â‰… presheafHom F.1 G.1 :=
  NatIso.ofComponents
    (fun _ => Sheaf.homEquiv.toIso) (fun _ => rfl)

/-- Given two sheaves `F` and `G` on a site `(C, J)` with values in a category `A`,
this `sheafHom F G` is the sheaf of types which sends an object `X : C`
to the type of morphisms between the "restrictions" of `F` and `G` to the category `Over X`. -/
def sheafHom (F G : Sheaf J A) : Sheaf J (Type _) where
  val := sheafHom' F G
  cond := (Presheaf.isSheaf_of_iso_iff (sheafHom'Iso F G)).2 (G.2.hom F.1)

/-- The sections of the sheaf `sheafHom F G` identify to morphisms `F âŸ¶ G`. -/
def sheafHomSectionsEquiv (F G : Sheaf J A) :
    (sheafHom F G).1.sections â‰ƒ (F âŸ¶ G) :=
  ((Functor.sectionsFunctor Cáµ’áµ–).mapIso (sheafHom'Iso F G)).toEquiv.trans
    ((presheafHomSectionsEquiv F.1 G.1).trans Sheaf.homEquiv.symm)

@[simp]
lemma sheafHomSectionsEquiv_symm_apply_coe_apply {F G : Sheaf J A} (Ï† : F âŸ¶ G) (X : Cáµ’áµ–) :
    ((sheafHomSectionsEquiv F G).symm Ï†).1 X = (J.overPullback A X.unop).map Ï† := rfl

end CategoryTheory
