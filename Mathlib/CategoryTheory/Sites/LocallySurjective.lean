/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang, Jo√´l Riou
-/
import Mathlib.CategoryTheory.Sites.Subsheaf
import Mathlib.CategoryTheory.Sites.CompatibleSheafification
import Mathlib.CategoryTheory.Sites.LocallyInjective
/-!

# Locally surjective morphisms

## Main definitions

- `IsLocallySurjective` : A morphism of presheaves valued in a concrete category is locally
  surjective with respect to a Grothendieck topology if every section in the target is locally
  in the set-theoretic image, i.e. the image sheaf coincides with the target.

## Main results

- `Presheaf.isLocallySurjective_toSheafify`: `toSheafify` is locally surjective.
- `Sheaf.isLocallySurjective_iff_epi`: a morphism of sheaves of types is locally
  surjective iff it is epi

-/


universe v u w v' u' w'

open Opposite CategoryTheory CategoryTheory.GrothendieckTopology CategoryTheory.Functor

namespace CategoryTheory

variable {C : Type u} [Category.{v} C] (J : GrothendieckTopology C)

variable {A : Type u'} [Category.{v'} A] {FA : A ‚Üí A ‚Üí Type*} {CA : A ‚Üí Type w'}
variable [‚àÄ X Y, FunLike (FA X Y) (CA X) (CA Y)] [ConcreteCategory.{w'} A FA]

namespace Presheaf

/-- Given `f : F ‚ü∂ G`, a morphism between presieves, and `s : G.obj (op U)`, this is the sieve
of `U` consisting of the `i : V ‚ü∂ U` such that `s` restricted along `i` is in the image of `f`. -/
@[simps -isSimp]
def imageSieve {F G : C·µí·µñ ‚•§ A} (f : F ‚ü∂ G) {U : C} (s : ToType (G.obj (op U))) : Sieve U where
  arrows V i := ‚àÉ t : ToType (F.obj (op V)), f.app _ t = G.map i.op s
  downward_closed := by
    rintro V W i ‚ü®t, ht‚ü© j
    refine ‚ü®F.map j.op t, ?_‚ü©
    rw [op_comp, G.map_comp, ConcreteCategory.comp_apply, ‚Üê ht, NatTrans.naturality_apply f]

theorem imageSieve_eq_sieveOfSection {F G : C·µí·µñ ‚•§ A} (f : F ‚ü∂ G) {U : C}
    (s : ToType (G.obj (op U))) :
    imageSieve f s = (Subpresheaf.range (whiskerRight f (forget A))).sieveOfSection s :=
  rfl

attribute [local instance] Types.instFunLike Types.instConcreteCategory in
theorem imageSieve_whisker_forget {F G : C·µí·µñ ‚•§ A} (f : F ‚ü∂ G) {U : C} (s : ToType (G.obj (op U))) :
    imageSieve (whiskerRight f (forget A)) s = imageSieve f s :=
  rfl

theorem imageSieve_app {F G : C·µí·µñ ‚•§ A} (f : F ‚ü∂ G) {U : C} (s : ToType (F.obj (op U))) :
    imageSieve f (f.app _ s) = ‚ä§ := by
  ext V i
  simp only [Sieve.top_apply, iff_true, imageSieve_apply]
  exact ‚ü®F.map i.op s, NatTrans.naturality_apply f i.op s‚ü©

/-- If a morphism `g : V ‚ü∂ U.unop` belongs to the sieve `imageSieve f s g`, then
this is choice of a preimage of `G.map g.op s` in `F.obj (op V)`, see
`app_localPreimage`. -/
noncomputable def localPreimage {F G : C·µí·µñ ‚•§ A} (f : F ‚ü∂ G) {U : C·µí·µñ} (s : ToType (G.obj U))
    {V : C} (g : V ‚ü∂ U.unop) (hg : imageSieve f s g) :
    ToType (F.obj (op V)) :=
  hg.choose

@[simp]
lemma app_localPreimage {F G : C·µí·µñ ‚•§ A} (f : F ‚ü∂ G) {U : C·µí·µñ} (s : ToType (G.obj U))
    {V : C} (g : V ‚ü∂ U.unop) (hg : imageSieve f s g) :
    f.app _ (localPreimage f s g hg) = G.map g.op s :=
  hg.choose_spec

/-- A morphism of presheaves `f : F ‚ü∂ G` is locally surjective with respect to a grothendieck
topology if every section of `G` is locally in the image of `f`. -/
class IsLocallySurjective {F G : C·µí·µñ ‚•§ A} (f : F ‚ü∂ G) : Prop where
  imageSieve_mem {U : C} (s : ToType (G.obj (op U))) : imageSieve f s ‚àà J U

lemma imageSieve_mem {F G : C·µí·µñ ‚•§ A} (f : F ‚ü∂ G) [IsLocallySurjective J f] {U : C·µí·µñ}
    (s : ToType (G.obj U)) : imageSieve f s ‚àà J U.unop :=
  IsLocallySurjective.imageSieve_mem _

attribute [local instance] Types.instFunLike Types.instConcreteCategory in
instance {F G : C·µí·µñ ‚•§ A} (f : F ‚ü∂ G) [IsLocallySurjective J f] :
    IsLocallySurjective J (whiskerRight f (forget A)) where
  imageSieve_mem s := imageSieve_mem J f s

theorem isLocallySurjective_iff_range_sheafify_eq_top {F G : C·µí·µñ ‚•§ A} (f : F ‚ü∂ G) :
    IsLocallySurjective J f ‚Üî (Subpresheaf.range (whiskerRight f (forget A))).sheafify J = ‚ä§ := by
  simp only [Subpresheaf.ext_iff, funext_iff, Set.ext_iff, Subpresheaf.top_obj,
    Set.top_eq_univ, Set.mem_univ, iff_true]
  exact ‚ü®fun H _ => H.imageSieve_mem, fun H => ‚ü®H _‚ü©‚ü©

attribute [local instance] Types.instFunLike Types.instConcreteCategory in
theorem isLocallySurjective_iff_range_sheafify_eq_top' {F G : C·µí·µñ ‚•§ Type w} (f : F ‚ü∂ G) :
    IsLocallySurjective J f ‚Üî (Subpresheaf.range f).sheafify J = ‚ä§ := by
  apply isLocallySurjective_iff_range_sheafify_eq_top

attribute [local instance] Types.instFunLike Types.instConcreteCategory in
theorem isLocallySurjective_iff_whisker_forget {F G : C·µí·µñ ‚•§ A} (f : F ‚ü∂ G) :
    IsLocallySurjective J f ‚Üî IsLocallySurjective J (whiskerRight f (forget A)) := by
  simp only [isLocallySurjective_iff_range_sheafify_eq_top]
  rfl

theorem isLocallySurjective_of_surjective {F G : C·µí·µñ ‚•§ A} (f : F ‚ü∂ G)
    (H : ‚àÄ U, Function.Surjective (f.app U)) : IsLocallySurjective J f where
  imageSieve_mem {U} s := by
    obtain ‚ü®t, rfl‚ü© := H _ s
    rw [imageSieve_app]
    exact J.top_mem _

instance isLocallySurjective_of_iso {F G : C·µí·µñ ‚•§ A} (f : F ‚ü∂ G) [IsIso f] :
    IsLocallySurjective J f := by
  apply isLocallySurjective_of_surjective
  intro U
  apply Function.Bijective.surjective
  rw [‚Üê isIso_iff_bijective, ‚Üê ConcreteCategory.forget_map_eq_coe]
  infer_instance

instance isLocallySurjective_comp {F‚ÇÅ F‚ÇÇ F‚ÇÉ : C·µí·µñ ‚•§ A} (f‚ÇÅ : F‚ÇÅ ‚ü∂ F‚ÇÇ) (f‚ÇÇ : F‚ÇÇ ‚ü∂ F‚ÇÉ)
    [IsLocallySurjective J f‚ÇÅ] [IsLocallySurjective J f‚ÇÇ] :
    IsLocallySurjective J (f‚ÇÅ ‚â´ f‚ÇÇ) where
  imageSieve_mem s := by
    have : (Sieve.bind (imageSieve f‚ÇÇ s) fun _ _ h => imageSieve f‚ÇÅ h.choose) ‚â§
        imageSieve (f‚ÇÅ ‚â´ f‚ÇÇ) s := by
      rintro V i ‚ü®W, i, j, H, ‚ü®t', ht'‚ü©, rfl‚ü©
      refine ‚ü®t', ?_‚ü©
      rw [op_comp, F‚ÇÉ.map_comp, NatTrans.comp_app, ConcreteCategory.comp_apply,
        ConcreteCategory.comp_apply, ht', NatTrans.naturality_apply, H.choose_spec]
    apply J.superset_covering this
    apply J.bind_covering
    ¬∑ apply imageSieve_mem
    ¬∑ intros; apply imageSieve_mem

lemma isLocallySurjective_of_isLocallySurjective
    {F‚ÇÅ F‚ÇÇ F‚ÇÉ : C·µí·µñ ‚•§ A} (f‚ÇÅ : F‚ÇÅ ‚ü∂ F‚ÇÇ) (f‚ÇÇ : F‚ÇÇ ‚ü∂ F‚ÇÉ)
    [IsLocallySurjective J (f‚ÇÅ ‚â´ f‚ÇÇ)] :
    IsLocallySurjective J f‚ÇÇ where
  imageSieve_mem {X} x := by
    refine J.superset_covering ?_ (imageSieve_mem J (f‚ÇÅ ‚â´ f‚ÇÇ) x)
    intro Y g hg
    exact ‚ü®f‚ÇÅ.app _ (localPreimage (f‚ÇÅ ‚â´ f‚ÇÇ) x g hg),
      by simpa using app_localPreimage (f‚ÇÅ ‚â´ f‚ÇÇ) x g hg‚ü©

lemma isLocallySurjective_of_isLocallySurjective_fac
    {F‚ÇÅ F‚ÇÇ F‚ÇÉ : C·µí·µñ ‚•§ A} {f‚ÇÅ : F‚ÇÅ ‚ü∂ F‚ÇÇ} {f‚ÇÇ : F‚ÇÇ ‚ü∂ F‚ÇÉ} {f‚ÇÉ : F‚ÇÅ ‚ü∂ F‚ÇÉ} (fac : f‚ÇÅ ‚â´ f‚ÇÇ = f‚ÇÉ)
    [IsLocallySurjective J f‚ÇÉ] : IsLocallySurjective J f‚ÇÇ := by
  subst fac
  exact isLocallySurjective_of_isLocallySurjective J f‚ÇÅ f‚ÇÇ

lemma isLocallySurjective_iff_of_fac
    {F‚ÇÅ F‚ÇÇ F‚ÇÉ : C·µí·µñ ‚•§ A} {f‚ÇÅ : F‚ÇÅ ‚ü∂ F‚ÇÇ} {f‚ÇÇ : F‚ÇÇ ‚ü∂ F‚ÇÉ} {f‚ÇÉ : F‚ÇÅ ‚ü∂ F‚ÇÉ} (fac : f‚ÇÅ ‚â´ f‚ÇÇ = f‚ÇÉ)
    [IsLocallySurjective J f‚ÇÅ] :
    IsLocallySurjective J f‚ÇÉ ‚Üî IsLocallySurjective J f‚ÇÇ := by
  constructor
  ¬∑ intro
    exact isLocallySurjective_of_isLocallySurjective_fac J fac
  ¬∑ intro
    rw [‚Üê fac]
    infer_instance

lemma comp_isLocallySurjective_iff
    {F‚ÇÅ F‚ÇÇ F‚ÇÉ : C·µí·µñ ‚•§ A} (f‚ÇÅ : F‚ÇÅ ‚ü∂ F‚ÇÇ) (f‚ÇÇ : F‚ÇÇ ‚ü∂ F‚ÇÉ)
    [IsLocallySurjective J f‚ÇÅ] :
    IsLocallySurjective J (f‚ÇÅ ‚â´ f‚ÇÇ) ‚Üî IsLocallySurjective J f‚ÇÇ :=
  isLocallySurjective_iff_of_fac J rfl

variable {J} in
lemma isLocallySurjective_of_le {K : GrothendieckTopology C} (hJK : J ‚â§ K) {F G : C·µí·µñ ‚•§ A}
    (f : F ‚ü∂ G) (h : IsLocallySurjective J f) : IsLocallySurjective K f where
  imageSieve_mem s := by apply hJK; exact h.1 _

lemma isLocallyInjective_of_isLocallyInjective_of_isLocallySurjective
    {F‚ÇÅ F‚ÇÇ F‚ÇÉ : C·µí·µñ ‚•§ A} (f‚ÇÅ : F‚ÇÅ ‚ü∂ F‚ÇÇ) (f‚ÇÇ : F‚ÇÇ ‚ü∂ F‚ÇÉ)
    [IsLocallyInjective J (f‚ÇÅ ‚â´ f‚ÇÇ)] [IsLocallySurjective J f‚ÇÅ] :
    IsLocallyInjective J f‚ÇÇ where
  equalizerSieve_mem {X} x‚ÇÅ x‚ÇÇ h := by
    let S := imageSieve f‚ÇÅ x‚ÇÅ ‚äì imageSieve f‚ÇÅ x‚ÇÇ
    have hS : S ‚àà J X.unop := by
      apply J.intersection_covering
      all_goals apply imageSieve_mem
    let T : ‚àÄ ‚¶ÉY : C‚¶Ñ (f : Y ‚ü∂ X.unop) (_ : S f), Sieve Y := fun Y f hf =>
      equalizerSieve (localPreimage f‚ÇÅ x‚ÇÅ f hf.1) (localPreimage f‚ÇÅ x‚ÇÇ f hf.2)
    refine J.superset_covering ?_ (J.transitive hS (Sieve.bind S.1 T) ?_)
    ¬∑ rintro Y f ‚ü®Z, a, g, hg, ha, rfl‚ü©
      simpa using congr_arg (f‚ÇÅ.app _) ha
    ¬∑ intro Y f hf
      apply J.superset_covering (Sieve.le_pullback_bind _ _ _ hf)
      apply equalizerSieve_mem J (f‚ÇÅ ‚â´ f‚ÇÇ)
      dsimp
      rw [ConcreteCategory.comp_apply, ConcreteCategory.comp_apply, app_localPreimage,
        app_localPreimage, NatTrans.naturality_apply, NatTrans.naturality_apply, h]

lemma isLocallyInjective_of_isLocallyInjective_of_isLocallySurjective_fac
    {F‚ÇÅ F‚ÇÇ F‚ÇÉ : C·µí·µñ ‚•§ A} {f‚ÇÅ : F‚ÇÅ ‚ü∂ F‚ÇÇ} {f‚ÇÇ : F‚ÇÇ ‚ü∂ F‚ÇÉ} (f‚ÇÉ : F‚ÇÅ ‚ü∂ F‚ÇÉ) (fac : f‚ÇÅ ‚â´ f‚ÇÇ = f‚ÇÉ)
    [IsLocallyInjective J f‚ÇÉ] [IsLocallySurjective J f‚ÇÅ] :
    IsLocallyInjective J f‚ÇÇ := by
  subst fac
  exact isLocallyInjective_of_isLocallyInjective_of_isLocallySurjective J f‚ÇÅ f‚ÇÇ

lemma isLocallySurjective_of_isLocallySurjective_of_isLocallyInjective
    {F‚ÇÅ F‚ÇÇ F‚ÇÉ : C·µí·µñ ‚•§ A} (f‚ÇÅ : F‚ÇÅ ‚ü∂ F‚ÇÇ) (f‚ÇÇ : F‚ÇÇ ‚ü∂ F‚ÇÉ)
    [IsLocallySurjective J (f‚ÇÅ ‚â´ f‚ÇÇ)] [IsLocallyInjective J f‚ÇÇ] :
    IsLocallySurjective J f‚ÇÅ where
  imageSieve_mem {X} x := by
    let S := imageSieve (f‚ÇÅ ‚â´ f‚ÇÇ) (f‚ÇÇ.app _ x)
    let T : ‚àÄ ‚¶ÉY : C‚¶Ñ (f : Y ‚ü∂ X) (_ : S f), Sieve Y := fun Y f hf =>
      equalizerSieve (f‚ÇÅ.app _ (localPreimage (f‚ÇÅ ‚â´ f‚ÇÇ) (f‚ÇÇ.app _ x) f hf)) (F‚ÇÇ.map f.op x)
    refine J.superset_covering ?_ (J.transitive (imageSieve_mem J (f‚ÇÅ ‚â´ f‚ÇÇ) (f‚ÇÇ.app _ x))
      (Sieve.bind S.1 T) ?_)
    ¬∑ rintro Y _ ‚ü®Z, a, g, hg, ha, rfl‚ü©
      exact ‚ü®F‚ÇÅ.map a.op (localPreimage (f‚ÇÅ ‚â´ f‚ÇÇ) _ _ hg), by simpa using ha‚ü©
    ¬∑ intro Y f hf
      apply J.superset_covering (Sieve.le_pullback_bind _ _ _ hf)
      apply equalizerSieve_mem J f‚ÇÇ
      rw [NatTrans.naturality_apply, ‚Üê app_localPreimage (f‚ÇÅ ‚â´ f‚ÇÇ) _ _ hf,
        NatTrans.comp_app, ConcreteCategory.comp_apply]

lemma isLocallySurjective_of_isLocallySurjective_of_isLocallyInjective_fac
    {F‚ÇÅ F‚ÇÇ F‚ÇÉ : C·µí·µñ ‚•§ A} {f‚ÇÅ : F‚ÇÅ ‚ü∂ F‚ÇÇ} {f‚ÇÇ : F‚ÇÇ ‚ü∂ F‚ÇÉ} (f‚ÇÉ : F‚ÇÅ ‚ü∂ F‚ÇÉ) (fac : f‚ÇÅ ‚â´ f‚ÇÇ = f‚ÇÉ)
    [IsLocallySurjective J f‚ÇÉ] [IsLocallyInjective J f‚ÇÇ] :
    IsLocallySurjective J f‚ÇÅ := by
  subst fac
  exact isLocallySurjective_of_isLocallySurjective_of_isLocallyInjective J f‚ÇÅ f‚ÇÇ

lemma comp_isLocallyInjective_iff
    {F‚ÇÅ F‚ÇÇ F‚ÇÉ : C·µí·µñ ‚•§ A} (f‚ÇÅ : F‚ÇÅ ‚ü∂ F‚ÇÇ) (f‚ÇÇ : F‚ÇÇ ‚ü∂ F‚ÇÉ)
    [IsLocallyInjective J f‚ÇÅ] [IsLocallySurjective J f‚ÇÅ] :
    IsLocallyInjective J (f‚ÇÅ ‚â´ f‚ÇÇ) ‚Üî IsLocallyInjective J f‚ÇÇ := by
  constructor
  ¬∑ intro
    exact isLocallyInjective_of_isLocallyInjective_of_isLocallySurjective J f‚ÇÅ f‚ÇÇ
  ¬∑ intro
    infer_instance

lemma isLocallySurjective_comp_iff
    {F‚ÇÅ F‚ÇÇ F‚ÇÉ : C·µí·µñ ‚•§ A} (f‚ÇÅ : F‚ÇÅ ‚ü∂ F‚ÇÇ) (f‚ÇÇ : F‚ÇÇ ‚ü∂ F‚ÇÉ)
    [IsLocallyInjective J f‚ÇÇ] [IsLocallySurjective J f‚ÇÇ] :
    IsLocallySurjective J (f‚ÇÅ ‚â´ f‚ÇÇ) ‚Üî IsLocallySurjective J f‚ÇÅ := by
  constructor
  ¬∑ intro
    exact isLocallySurjective_of_isLocallySurjective_of_isLocallyInjective J f‚ÇÅ f‚ÇÇ
  ¬∑ intro
    infer_instance

attribute [local instance] Types.instFunLike Types.instConcreteCategory in
instance {F‚ÇÅ F‚ÇÇ : C·µí·µñ ‚•§ Type w} (f : F‚ÇÅ ‚ü∂ F‚ÇÇ) :
    IsLocallySurjective J (Subpresheaf.toRangeSheafify J f) where
  imageSieve_mem {X} := by
    rintro ‚ü®s, hs‚ü©
    refine J.superset_covering ?_ hs
    rintro Y g ‚ü®t, ht‚ü©
    exact ‚ü®t, Subtype.ext ht‚ü©

attribute [local instance] Types.instFunLike Types.instConcreteCategory in
/-- The image of `F` in `J.sheafify F` is isomorphic to the sheafification. -/
noncomputable def sheafificationIsoImagePresheaf (F : C·µí·µñ ‚•§ Type max u v) :
    J.sheafify F ‚âÖ ((Subpresheaf.range (J.toSheafify F)).sheafify J).toPresheaf where
  hom :=
    J.sheafifyLift (Subpresheaf.toRangeSheafify J _)
      ((isSheaf_iff_isSheaf_of_type J _).mpr <|
        Subpresheaf.sheafify_isSheaf _ <|
          (isSheaf_iff_isSheaf_of_type J _).mp <| GrothendieckTopology.sheafify_isSheaf J _)
  inv := Subpresheaf.Œπ _
  hom_inv_id :=
    J.sheafify_hom_ext _ _ (J.sheafify_isSheaf _) (by simp [Subpresheaf.toRangeSheafify])
  inv_hom_id := by
    rw [‚Üê cancel_mono (Subpresheaf.Œπ _), Category.id_comp, Category.assoc]
    refine Eq.trans ?_ (Category.comp_id _)
    congr 1
    exact J.sheafify_hom_ext _ _ (J.sheafify_isSheaf _) (by simp [Subpresheaf.toRangeSheafify])

section

open GrothendieckTopology.Plus

attribute [local instance] Types.instFunLike Types.instConcreteCategory in
instance isLocallySurjective_toPlus (P : C·µí·µñ ‚•§ Type max u v) :
    IsLocallySurjective J (J.toPlus P) where
  imageSieve_mem x := by
    obtain ‚ü®S, x, rfl‚ü© := exists_rep x
    refine J.superset_covering (fun Y f hf => ‚ü®x.1 ‚ü®Y, f, hf‚ü©, ?_‚ü©) S.2
    dsimp
    rw [toPlus_eq_mk, res_mk_eq_mk_pullback, eq_mk_iff_exists]
    refine ‚ü®S.pullback f, homOfLE le_top, ùüô _, ?_‚ü©
    ext ‚ü®Z, g, hg‚ü©
    simpa using x.2 { fst.hf := hf, snd.hf := S.1.downward_closed hf g, r.g‚ÇÅ := g, r.g‚ÇÇ := ùüô Z, .. }

attribute [local instance] Types.instFunLike Types.instConcreteCategory in
instance isLocallySurjective_toSheafify (P : C·µí·µñ ‚•§ Type max u v) :
    IsLocallySurjective J (J.toSheafify P) := by
  dsimp [GrothendieckTopology.toSheafify]
  rw [GrothendieckTopology.plusMap_toPlus]
  infer_instance

attribute [local instance] Types.instFunLike Types.instConcreteCategory in
instance isLocallySurjective_toSheafify' {D : Type*} [Category D] {FD : D ‚Üí D ‚Üí Type*}
    {CD : D ‚Üí Type (max u v)} [‚àÄ X Y, FunLike (FD X Y) (CD X) (CD Y)]
    [ConcreteCategory.{max u v} D FD]
    (P : C·µí·µñ ‚•§ D) [HasWeakSheafify J D] [J.HasSheafCompose (forget D)]
    [J.PreservesSheafification (forget D)] :
    IsLocallySurjective J (toSheafify J P) := by
  rw [isLocallySurjective_iff_whisker_forget,
    ‚Üê sheafComposeIso_hom_fac, ‚Üê toSheafify_plusPlusIsoSheafify_hom]
  infer_instance

end

end Presheaf

namespace Sheaf

variable {J}
variable {F‚ÇÅ F‚ÇÇ F‚ÇÉ : Sheaf J A} (œÜ : F‚ÇÅ ‚ü∂ F‚ÇÇ) (œà : F‚ÇÇ ‚ü∂ F‚ÇÉ)

/-- If `œÜ : F‚ÇÅ ‚ü∂ F‚ÇÇ` is a morphism of sheaves, this is an abbreviation for
`Presheaf.IsLocallySurjective J œÜ.val`. -/
abbrev IsLocallySurjective := Presheaf.IsLocallySurjective J œÜ.val

lemma isLocallySurjective_sheafToPresheaf_map_iff :
    Presheaf.IsLocallySurjective J ((sheafToPresheaf J A).map œÜ) ‚Üî IsLocallySurjective œÜ := by rfl

instance isLocallySurjective_comp [IsLocallySurjective œÜ] [IsLocallySurjective œà] :
    IsLocallySurjective (œÜ ‚â´ œà) :=
  Presheaf.isLocallySurjective_comp J œÜ.val œà.val

instance isLocallySurjective_of_iso [IsIso œÜ] : IsLocallySurjective œÜ := by
  have : IsIso œÜ.val := (inferInstance : IsIso ((sheafToPresheaf J A).map œÜ))
  infer_instance

attribute [local instance] Types.instFunLike Types.instConcreteCategory in
instance {F G : Sheaf J (Type w)} (f : F ‚ü∂ G) :
    IsLocallySurjective (Sheaf.toImage f) := by
  dsimp [Sheaf.toImage]
  infer_instance

variable [J.HasSheafCompose (forget A)]

attribute [local instance] Types.instFunLike Types.instConcreteCategory in
instance [IsLocallySurjective œÜ] :
    IsLocallySurjective ((sheafCompose J (forget A)).map œÜ) :=
  (Presheaf.isLocallySurjective_iff_whisker_forget J œÜ.val).1 inferInstance

attribute [local instance] Types.instFunLike Types.instConcreteCategory in
theorem isLocallySurjective_iff_isIso {F G : Sheaf J (Type w)} (f : F ‚ü∂ G) :
    IsLocallySurjective f ‚Üî IsIso (Sheaf.imageŒπ f) := by
  dsimp only [IsLocallySurjective]
  rw [Sheaf.imageŒπ, Presheaf.isLocallySurjective_iff_range_sheafify_eq_top',
    Subpresheaf.eq_top_iff_isIso]
  exact isIso_iff_of_reflects_iso (f := Sheaf.imageŒπ f) (F := sheafToPresheaf J (Type w))

attribute [local instance] Types.instFunLike Types.instConcreteCategory in
instance epi_of_isLocallySurjective' {F‚ÇÅ F‚ÇÇ : Sheaf J (Type w)} (œÜ : F‚ÇÅ ‚ü∂ F‚ÇÇ)
    [IsLocallySurjective œÜ] : Epi œÜ where
  left_cancellation {Z} f‚ÇÅ f‚ÇÇ h := by
    ext X x
    apply (Presieve.isSeparated_of_isSheaf J Z.1 ((isSheaf_iff_isSheaf_of_type _ _).1 Z.2) _
      (Presheaf.imageSieve_mem J œÜ.val x)).ext
    rintro Y f ‚ü®s : F‚ÇÅ.val.obj (op Y), hs : œÜ.val.app _ s = F‚ÇÇ.val.map f.op x‚ü©
    dsimp
    have h‚ÇÅ := congr_fun (f‚ÇÅ.val.naturality f.op) x
    have h‚ÇÇ := congr_fun (f‚ÇÇ.val.naturality f.op) x
    dsimp at h‚ÇÅ h‚ÇÇ
    rw [‚Üê h‚ÇÅ, ‚Üê h‚ÇÇ, ‚Üê hs]
    exact congr_fun (congr_app ((sheafToPresheaf J _).congr_map h) (op Y)) s

instance epi_of_isLocallySurjective [IsLocallySurjective œÜ] : Epi œÜ :=
  (sheafCompose J (forget A)).epi_of_epi_map inferInstance

attribute [local instance] Types.instFunLike Types.instConcreteCategory in
lemma isLocallySurjective_iff_epi {F G : Sheaf J (Type w)} (œÜ : F ‚ü∂ G)
    [HasSheafify J (Type w)] :
    IsLocallySurjective œÜ ‚Üî Epi œÜ := by
  constructor
  ¬∑ intro
    infer_instance
  ¬∑ intro
    have := epi_of_epi_fac (Sheaf.toImage_Œπ œÜ)
    rw [isLocallySurjective_iff_isIso œÜ]
    apply isIso_of_mono_of_epi

end Sheaf

namespace Presieve.FamilyOfElements

variable {R R' : C·µí·µñ ‚•§ Type w} (œÜ : R ‚ü∂ R') {X : C·µí·µñ} (r' : R'.obj X)

attribute [local instance] Types.instFunLike Types.instConcreteCategory in
/-- Given a morphism `œÜ : R ‚ü∂ R'` of presheaves of types and `r' : R'.obj X`,
this is the family of elements of `R` defined over the sieve `Presheaf.imageSieve œÜ r'`
which sends a map in this sieve to an arbitrary choice of a preimage of the
restriction of `r'`. -/
noncomputable def localPreimage :
    FamilyOfElements R (Presheaf.imageSieve œÜ r').arrows :=
  fun _ f hf => Presheaf.localPreimage œÜ r' f hf

attribute [local instance] Types.instFunLike Types.instConcreteCategory in
lemma isAmalgamation_map_localPreimage :
    ((localPreimage œÜ r').map œÜ).IsAmalgamation r' :=
  fun _ f hf => (Presheaf.app_localPreimage œÜ r' f hf).symm

end Presieve.FamilyOfElements

end CategoryTheory
