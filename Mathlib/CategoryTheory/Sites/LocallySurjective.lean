/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Sites.Whiskering
import Mathlib.CategoryTheory.Sites.LeftExact
/-!
# Locally injective and locally surjective morphisms of (pre)sheaves

Let `C` be a category equipped with a Grothendieck topology `J`,
and let `D` be a concrete category.

In this file, we introduce typeclasses `Presheaf.LocallyInjective J Ï†` and
`Presheaf.LocallySurjective J Ï†` for a morphism `Ï† : Fâ‚ âŸ¶ Fâ‚‚` in the category `Cáµ’áµ– â¥¤ D`:
they are given by local injectivity/surjectivity conditions.

When the suitable assumptions for the construction of the associated sheaves by the plus-plus
contruction are available, we show that the canonical morphism `P âŸ¶ toSheafify J P`
is both locally injective and locally surjective.

Then, one of the main results in this file is the lemma
`presheafToSheaf_map_locallySurjective_iff` which asserts that if `Ï†` is a
morphism of presheaves, that `Ï†` is locally surjective iff
`(presheafToSheaf J D).map Ï†` is locally surjective. (A similar result holds for
locally injective morphisms.)

For morphisms of sheaves, a locally injective morphism is a monomorphism
(`Sheaf.mono_of_locallyInjective`) and a locally surjective morphism is an epimorphism
(`Sheaf.epi_of_locallySurjective`). The converse statements also hold,
at least for sheaves of types: this is studied in the file `CategoryTheory.Sites.EpiMono`.

-/

universe w v' v u' u

namespace CategoryTheory

open Opposite Limits

variable {C : Type u} [Category.{v} C]
  {D : Type u'} [Category.{v'} D] [ConcreteCategory.{w} D]
  (J : GrothendieckTopology C)

attribute [local instance] ConcreteCategory.hasCoeToSort ConcreteCategory.instFunLike

@[simp]
lemma NatTrans.naturality_apply {F G : C â¥¤ D} (Ï† : F âŸ¶ G) {X Y : C}
    (f : X âŸ¶ Y) (x : F.obj X) :
    Ï†.app Y (F.map f x) = G.map f (Ï†.app X x) := by
  simpa only [Functor.map_comp] using congr_fun ((forget D).congr_map (Ï†.naturality f)) x

namespace Presheaf

variable {Fâ‚ Fâ‚‚ Fâ‚ƒ : Cáµ’áµ– â¥¤ D} (Ï† : Fâ‚ âŸ¶ Fâ‚‚) (Ïˆ : Fâ‚‚ âŸ¶ Fâ‚ƒ)

/-- A morphism `Ï† : Fâ‚ âŸ¶ Fâ‚‚` of presheaves `Cáµ’áµ– â¥¤ D` (with `D` a concrete category)
is locally injective for a Grothendieck topology `J` on `C` if
whenever two sections of `Fâ‚` are sent to the same section of `Fâ‚‚`, then these two
sections coincide locally. -/
class LocallyInjective : Prop where
  locally_injective {X : Cáµ’áµ–} (x y : Fâ‚.obj X) (h : Ï†.app X x = Ï†.app X y) :
    âˆƒ (S : Sieve X.unop) (_ : S âˆˆ J X.unop),
      âˆ€ {Y : C} (f : Y âŸ¶ X.unop) (_ : S f), Fâ‚.map f.op x = Fâ‚.map f.op y

lemma locallyInjective_of_injective (hÏ† : âˆ€ (X : Cáµ’áµ–), Function.Injective (Ï†.app X)) :
    LocallyInjective J Ï† where
  locally_injective {X} x y h := âŸ¨âŠ¤, J.top_mem _, fun f _ => hÏ† _ (by simp [h])âŸ©

instance [IsIso Ï†] :
    LocallyInjective J Ï† := locallyInjective_of_injective J Ï† (fun X => by
  apply Function.Bijective.injective
  rw [â† isIso_iff_bijective]
  change IsIso ((forget D).map (Ï†.app X))
  infer_instance)

section

variable [hÏ† : LocallyInjective J Ï†]
  {X : Cáµ’áµ–} (x y : Fâ‚.obj X) (h : Ï†.app X x = Ï†.app X y)

/-- When `Ï† : Fâ‚ âŸ¶ Fâ‚‚` is locally injective and `x` and `y` are two elements in `Fâ‚.obj X` such
that `Ï†.app X x = Ï†.app X y`, this is a covering sieve of `X.unop`
over which `x` and `y` coincide, see `map_apply_eq_of_locallyInjective`. -/
noncomputable def sieveOfLocallyInjective : Sieve X.unop :=
  (hÏ†.locally_injective x y h).choose

lemma sieveOfLocallyInjective_mem :
    sieveOfLocallyInjective J Ï† x y h âˆˆ J X.unop :=
  (hÏ†.locally_injective x y h).choose_spec.choose

lemma map_apply_eq_of_locallyInjective
    {Y : C} (f : Y âŸ¶ X.unop) (hf : sieveOfLocallyInjective J Ï† x y h f) :
    Fâ‚.map f.op x = Fâ‚.map f.op y :=
  (hÏ†.locally_injective x y h).choose_spec.choose_spec f hf

end

/-- A morphism `Ï† : Fâ‚ âŸ¶ Fâ‚‚` of presheaves `Cáµ’áµ– â¥¤ D` (with `D` a concrete category)
is locally surjective for a Grothendieck topology `J` on `C` if any section of `Fâ‚‚`
can be lifted locally to a section of `Fâ‚`. -/
class LocallySurjective : Prop where
  locally_surjective {X : Cáµ’áµ–} (x : Fâ‚‚.obj X) :
    âˆƒ (S : Sieve X.unop) (_ : S âˆˆ J X.unop),
      âˆ€ {Y : C} (f : Y âŸ¶ X.unop) (_ : S f), âˆƒ (y : Fâ‚.obj (op Y)),
        Ï†.app (op Y) y = Fâ‚‚.map f.op x

lemma locallySurjective_of_surjective (hÏ† : âˆ€ (X : Cáµ’áµ–), Function.Surjective (Ï†.app X)) :
    LocallySurjective J Ï† where
  locally_surjective _ := âŸ¨âŠ¤, J.top_mem _, fun _ _ => hÏ† _ _âŸ©

instance [IsIso Ï†] : LocallySurjective J Ï† := locallySurjective_of_surjective J Ï† (fun X => by
  apply Function.Bijective.surjective
  rw [â† isIso_iff_bijective]
  change IsIso ((forget D).map (Ï†.app X))
  infer_instance)

section

variable [hÏ† : LocallySurjective J Ï†]
  {X : Cáµ’áµ–} (x : Fâ‚‚.obj X)

/-- When `Ï† : Fâ‚ âŸ¶ Fâ‚‚` is locally surjective and `x : Fâ‚‚.obj X`, this is a covering
sieve of `X.unop` over which `x` has a preimage, which is given by `localPreimage`. -/
noncomputable def sieveOfLocallySurjective : Sieve X.unop :=
  (hÏ†.locally_surjective x).choose

lemma sieveOfLocallySurjective_mem : sieveOfLocallySurjective J Ï† x âˆˆ J X.unop :=
  (hÏ†.locally_surjective x).choose_spec.choose

variable {Y : C} (f : Y âŸ¶ X.unop) (hf : sieveOfLocallySurjective J Ï† x f)

/-- When `Ï† : Fâ‚ âŸ¶ Fâ‚‚` is locally surjective, `x : Fâ‚‚.obj X`, and `f : Y âŸ¶ X.unop`
belongs to the sieve `sieveOfLocallySurjective J Ï† x`, this is a preimage of `Fâ‚‚.map f.op x`. -/
noncomputable def localPreimage : Fâ‚.obj (op Y) :=
  ((hÏ†.locally_surjective x).choose_spec.choose_spec f hf).choose

@[simp]
lemma app_apply_localPreimage :
    Ï†.app _ (localPreimage J Ï† x f hf) = Fâ‚‚.map f.op x :=
  ((hÏ†.locally_surjective x).choose_spec.choose_spec f hf).choose_spec

end

instance locallyInjective_forget [LocallyInjective J Ï†] :
    LocallyInjective J (whiskerRight Ï† (forget D)) where
  locally_injective x y h :=
    âŸ¨_, sieveOfLocallyInjective_mem J Ï† x y h, map_apply_eq_of_locallyInjective J Ï† x y hâŸ©

instance locallySurjective_forget [LocallySurjective J Ï†] :
    LocallySurjective J (whiskerRight Ï† (forget D)) where
  locally_surjective x :=
    âŸ¨_, sieveOfLocallySurjective_mem J Ï† x,
      fun f hf => âŸ¨localPreimage J Ï† x f hf, app_apply_localPreimage J Ï† x f hfâŸ©âŸ©

instance locallyInjective_comp [LocallyInjective J Ï†] [LocallyInjective J Ïˆ] :
    LocallyInjective J (Ï† â‰« Ïˆ) where
  locally_injective {X} x y h := by
    let S := sieveOfLocallyInjective J Ïˆ (Ï†.app _ x) (Ï†.app _ y) (by simpa using h)
    have hS : S âˆˆ J X.unop :=
      sieveOfLocallyInjective_mem J Ïˆ (Ï†.app _ x) (Ï†.app _ y) (by simpa using h)
    have hS' : âˆ€ â¦ƒY : Câ¦„ â¦ƒf : Y âŸ¶ X.unopâ¦„ (_ : S f),
      Ï†.app _ (Fâ‚.map f.op x) = Ï†.app _ (Fâ‚.map f.op y) := fun Y f hf => by
        simpa using map_apply_eq_of_locallyInjective J Ïˆ (Ï†.app _ x) (Ï†.app _ y) _ f hf
    let T : âˆ€ â¦ƒY : Câ¦„ â¦ƒf : Y âŸ¶ X.unopâ¦„ (_ : S f), Sieve Y := fun Y f hf =>
      sieveOfLocallyInjective J Ï† (Fâ‚.map f.op x) (Fâ‚.map f.op y) (hS' hf)
    refine âŸ¨_, J.transitive hS (Sieve.bind S.1 T) ?_, ?_âŸ©
    Â· intro Y f hf
      exact J.superset_covering (Sieve.le_pullback_bind S.1 T _ hf)
        (sieveOfLocallyInjective_mem J Ï† (Fâ‚.map f.op x) (Fâ‚.map f.op y) (hS' hf))
    Â· intro Y f hf
      obtain âŸ¨Z, a, g, hg, ha, rflâŸ© := hf
      simpa using map_apply_eq_of_locallyInjective J Ï† _ _ (hS' hg) _ ha

lemma locallyInjective_of_locallyInjective [LocallyInjective J (Ï† â‰« Ïˆ)] :
    LocallyInjective J Ï† where
  locally_injective {X} x y h :=
      âŸ¨_, sieveOfLocallyInjective_mem J (Ï† â‰« Ïˆ) x y
        (by simpa using congr_arg (Ïˆ.app X) h),
        map_apply_eq_of_locallyInjective J (Ï† â‰« Ïˆ) x y _âŸ©

variable {Ï† Ïˆ}

lemma locallyInjective_of_locallyInjective_fac {Ï†Ïˆ : Fâ‚ âŸ¶ Fâ‚ƒ} (fac : Ï† â‰« Ïˆ = Ï†Ïˆ)
    [LocallyInjective J Ï†Ïˆ] : LocallyInjective J Ï† := by
  subst fac
  exact locallyInjective_of_locallyInjective J Ï† Ïˆ

lemma locallyInjective_iff_fac {Ï†Ïˆ : Fâ‚ âŸ¶ Fâ‚ƒ} (fac : Ï† â‰« Ïˆ = Ï†Ïˆ) [LocallyInjective J Ïˆ] :
    LocallyInjective J Ï†Ïˆ â†” LocallyInjective J Ï† := by
  constructor
  Â· intro
    exact locallyInjective_of_locallyInjective_fac J fac
  Â· intro
    rw [â† fac]
    infer_instance

variable (Ï† Ïˆ)

lemma locallyInjective_comp_iff [LocallyInjective J Ïˆ] :
    LocallyInjective J (Ï† â‰« Ïˆ) â†” LocallyInjective J Ï† :=
  locallyInjective_iff_fac J rfl

instance locallySurjective_comp [LocallySurjective J Ï†] [LocallySurjective J Ïˆ] :
    LocallySurjective J (Ï† â‰« Ïˆ) where
  locally_surjective {X} x := by
    let S := sieveOfLocallySurjective J Ïˆ x
    let hS : S âˆˆ J X.unop := sieveOfLocallySurjective_mem J Ïˆ x
    let T : âˆ€ â¦ƒY : Câ¦„ â¦ƒf : Y âŸ¶ X.unopâ¦„ (_ : S f), Sieve Y :=
      fun Y f hf => sieveOfLocallySurjective J Ï† (localPreimage J Ïˆ x f hf)
    refine âŸ¨_, J.transitive hS (Sieve.bind S.1 T) ?_, ?_âŸ©
    Â· intro Y f hf
      exact J.superset_covering (Sieve.le_pullback_bind _ _ _ hf)
        (by apply sieveOfLocallySurjective_mem)
    Â· intro Y f hf
      obtain âŸ¨Z, a, g, hg, ha, rflâŸ© := hf
      exact âŸ¨localPreimage J Ï† (localPreimage J Ïˆ x g hg) a ha, by simpâŸ©

lemma locallySurjective_of_locallySurjective [LocallySurjective J (Ï† â‰« Ïˆ)] :
    LocallySurjective J Ïˆ where
  locally_surjective {X} x :=
    âŸ¨_, sieveOfLocallySurjective_mem J (Ï† â‰« Ïˆ) x, fun f hf =>
      âŸ¨Ï†.app _ (localPreimage J (Ï† â‰« Ïˆ) x f hf),
        by simpa using app_apply_localPreimage J (Ï† â‰« Ïˆ) x f hfâŸ©âŸ©

lemma locallyInjective_of_locallyInjective_of_locallySurjective
    [LocallyInjective J (Ï† â‰« Ïˆ)] [LocallySurjective J Ï†] :
    LocallyInjective J Ïˆ where
  locally_injective {X} xâ‚ xâ‚‚ h := by
    let S := sieveOfLocallySurjective J Ï† xâ‚ âŠ“ sieveOfLocallySurjective J Ï† xâ‚‚
    have hS : S âˆˆ J X.unop := by
      apply J.intersection_covering
      all_goals apply sieveOfLocallySurjective_mem
    have hS' : âˆ€ â¦ƒY : Câ¦„ (f : Y âŸ¶ X.unop) (hf : S f),
      (Ï† â‰« Ïˆ).app (op Y) (localPreimage J Ï† xâ‚ f hf.1) =
        (Ï† â‰« Ïˆ).app (op Y) (localPreimage J Ï† xâ‚‚ f hf.2) := fun Y f hf => by simp [h]
    let T : âˆ€ â¦ƒY : Câ¦„ (f : Y âŸ¶ X.unop) (_ : S f), Sieve Y := fun Y f hf =>
      sieveOfLocallyInjective J (Ï† â‰« Ïˆ) _ _ (hS' f hf)
    refine âŸ¨_, J.transitive hS (Sieve.bind S.1 T) ?_, ?_âŸ©
    Â· intro Y f hf
      exact J.superset_covering (Sieve.le_pullback_bind _ _ _ hf)
        (by apply sieveOfLocallyInjective_mem)
    Â· intro Y f hf
      obtain âŸ¨Z, a, g, hg, ha, rflâŸ© := hf
      simp only [op_unop, op_comp, Functor.map_comp, comp_apply]
      erw [â† app_apply_localPreimage J Ï† xâ‚ g hg.1, â† app_apply_localPreimage J Ï† xâ‚‚ g hg.2,
        â† NatTrans.naturality_apply, â† NatTrans.naturality_apply,
        map_apply_eq_of_locallyInjective J (Ï† â‰« Ïˆ) _ _ (hS' _ hg) a ha]
      rfl

lemma locallySurjective_of_locallySurjective_of_locallyInjective
    [LocallySurjective J (Ï† â‰« Ïˆ)] [LocallyInjective J Ïˆ] :
    LocallySurjective J Ï† where
  locally_surjective {X} x := by
    let S := sieveOfLocallySurjective J (Ï† â‰« Ïˆ) (Ïˆ.app _ x)
    have hS : S âˆˆ J X.unop := by apply sieveOfLocallySurjective_mem
    have hS' : âˆ€ â¦ƒY : Câ¦„ (f : Y âŸ¶ X.unop) (hf : S f),
      (Ïˆ.app (op Y)) ((Ï†.app (op Y)) (localPreimage J (Ï† â‰« Ïˆ) ((Ïˆ.app X) x) f hf)) =
        (Ïˆ.app (op Y)) ((Fâ‚‚.map f.op) x) := fun Y f hf => by
          simpa using app_apply_localPreimage J (Ï† â‰« Ïˆ) (Ïˆ.app _ x) f hf
    let T : âˆ€ â¦ƒY : Câ¦„ (f : Y âŸ¶ X.unop) (_ : S f), Sieve Y := fun Y f hf =>
      sieveOfLocallyInjective J Ïˆ _ _ (hS' f hf)
    refine âŸ¨_, J.transitive hS (Sieve.bind S.1 T) ?_, ?_âŸ©
    Â· intro Y f hf
      exact J.superset_covering (Sieve.le_pullback_bind _ _ _ hf)
        (by apply sieveOfLocallyInjective_mem)
    Â· intro Y f hf
      obtain âŸ¨Z, a, g, hg, ha, rflâŸ© := hf
      exact âŸ¨Fâ‚.map a.op (localPreimage J (Ï† â‰« Ïˆ) _ g hg),
        by simpa using map_apply_eq_of_locallyInjective J Ïˆ _ _ (hS' g hg) a haâŸ©

variable {Ï† Ïˆ}

lemma locallySurjective_of_locallySurjective_fac {Ï†Ïˆ : Fâ‚ âŸ¶ Fâ‚ƒ} (fac : Ï† â‰« Ïˆ = Ï†Ïˆ)
    [LocallySurjective J Ï†Ïˆ] : LocallySurjective J Ïˆ := by
  subst fac
  exact locallySurjective_of_locallySurjective J Ï† Ïˆ

lemma locallySurjective_iff_fac {Ï†Ïˆ : Fâ‚ âŸ¶ Fâ‚ƒ} (fac : Ï† â‰« Ïˆ = Ï†Ïˆ) [LocallySurjective J Ï†] :
    LocallySurjective J Ï†Ïˆ â†” LocallySurjective J Ïˆ := by
  constructor
  Â· intro
    exact locallySurjective_of_locallySurjective_fac J fac
  Â· intro
    rw [â† fac]
    infer_instance

lemma locallyInjective_of_locallyInjective_of_locallySurjective_fac
    {Ï†Ïˆ : Fâ‚ âŸ¶ Fâ‚ƒ} (fac : Ï† â‰« Ïˆ = Ï†Ïˆ)
    [LocallyInjective J (Ï†Ïˆ)] [LocallySurjective J Ï†] :
    LocallyInjective J Ïˆ := by
  subst fac
  exact locallyInjective_of_locallyInjective_of_locallySurjective J Ï† Ïˆ

lemma locallySurjective_of_locallySurjective_of_locallyInjective_fac
    {Ï†Ïˆ : Fâ‚ âŸ¶ Fâ‚ƒ} (fac : Ï† â‰« Ïˆ = Ï†Ïˆ)
    [LocallySurjective J Ï†Ïˆ] [LocallyInjective J Ïˆ] :
    LocallySurjective J Ï† := by
  subst fac
  exact locallySurjective_of_locallySurjective_of_locallyInjective J Ï† Ïˆ

variable (Ï† Ïˆ)

lemma locallySurjective_comp_iff [LocallySurjective J Ï†] :
    LocallySurjective J (Ï† â‰« Ïˆ) â†” LocallySurjective J Ïˆ :=
  locallySurjective_iff_fac J rfl

section

variable {E : Type u'} [Category.{max u v} E] [ConcreteCategory E]
  [PreservesLimits (forget E)]
  [âˆ€ (P : Cáµ’áµ– â¥¤ E) (X : C) (S : J.Cover X),
    HasMultiequalizer (GrothendieckTopology.Cover.index S P)]
  [âˆ€ (X : C), HasColimitsOfShape (GrothendieckTopology.Cover J X)áµ’áµ– E]
  [âˆ€ X : C, PreservesColimitsOfShape (J.Cover X)áµ’áµ– (forget E)] [ReflectsIsomorphisms (forget E)]

variable (P : Cáµ’áµ– â¥¤ E)

section

open GrothendieckTopology Plus

instance locallyInjective_toPlus : LocallyInjective J (J.toPlus P) where
  locally_injective {X} x y h := by
    erw [toPlus_eq_mk, toPlus_eq_mk, eq_mk_iff_exists] at h
    obtain âŸ¨W, hâ‚, hâ‚‚, eqâŸ© := h
    exact âŸ¨W.1, W.2, fun {Y} f hf => congr_fun (congr_arg Subtype.val eq) âŸ¨Y, f, hfâŸ©âŸ©

instance locallySurjective_toPlus : LocallySurjective J (J.toPlus P) where
  locally_surjective {X} x := by
    obtain âŸ¨S, x, rflâŸ© := exists_rep x
    refine' âŸ¨S.1, S.2, fun {Y} f hf => âŸ¨x.1 âŸ¨Y, f, hfâŸ©, _âŸ©âŸ©
    dsimp
    erw [toPlus_eq_mk, res_mk_eq_mk_pullback, eq_mk_iff_exists]
    refine' âŸ¨S.pullback f, homOfLE le_top, ğŸ™ _, _âŸ©
    ext âŸ¨Z, g, hgâŸ©
    simpa using x.2 (Cover.Relation.mk _ _ _ g (ğŸ™ Z) f (g â‰« f) hf
      (S.1.downward_closed hf g) (by simp))

end

instance locallyInjective_toSheafify : LocallyInjective J (J.toSheafify P) := by
  dsimp [GrothendieckTopology.toSheafify]
  rw [GrothendieckTopology.plusMap_toPlus]
  infer_instance

instance locallySurjective_toSheafify : LocallySurjective J (J.toSheafify P) := by
  dsimp [GrothendieckTopology.toSheafify]
  rw [GrothendieckTopology.plusMap_toPlus]
  infer_instance

@[reassoc (attr := simp)]
lemma toSheafify_plusPlusIsoSheafify_hom :
    J.toSheafify P â‰« (plusPlusIsoSheafify J E P).hom = toSheafify J P := by
  convert Adjunction.unit_leftAdjointUniq_hom_app
    (plusPlusAdjunction J E) (sheafificationAdjunction J E) P
  ext1 P
  dsimp [GrothendieckTopology.toSheafify, plusPlusAdjunction]
  rw [Category.comp_id]

instance locallyInjective_toSheafify' : LocallyInjective J (toSheafify J P) := by
  rw [â† toSheafify_plusPlusIsoSheafify_hom]
  infer_instance

instance locallySurjective_toSheafify' : LocallySurjective J (toSheafify J P) := by
  rw [â† toSheafify_plusPlusIsoSheafify_hom]
  infer_instance

end

-- we should have a better API for "separated" to that it follows trivially from IsSheaf...
lemma locallyInjective_iff_injective_of_separated
    (hsep : âˆ€ (X : C) (S : J.Cover X) (x y : Fâ‚.obj (op X)),
        (âˆ€ I : S.Arrow, Fâ‚.map I.f.op x = Fâ‚.map I.f.op y) â†’ x = y) :
    LocallyInjective J Ï† â†” âˆ€ (X : Cáµ’áµ–), Function.Injective (Ï†.app X) := by
  constructor
  Â· intro _ X x y h
    apply hsep X.unop âŸ¨_, sieveOfLocallyInjective_mem J Ï† x y hâŸ©
    rintro âŸ¨Y, f, hfâŸ©
    exact map_apply_eq_of_locallyInjective J Ï† x y h f hf
  Â· apply locallyInjective_of_injective

end Presheaf

namespace Sheaf

variable {J}
variable {Fâ‚ Fâ‚‚ Fâ‚ƒ : Sheaf J D} (Ï† : Fâ‚ âŸ¶ Fâ‚‚) (Ïˆ : Fâ‚‚ âŸ¶ Fâ‚ƒ)

/-- A morphism of sheaves `Ï† : Fâ‚ âŸ¶ Fâ‚‚` with values in a concrete category `D` is
locally injective if the corresponding morphism of presheaves if locally injective. -/
abbrev LocallyInjective := Presheaf.LocallyInjective J Ï†.1

/-- A morphism of sheaves `Ï† : Fâ‚ âŸ¶ Fâ‚‚` with values in a concrete category `D` is
locally surjective if the corresponding morphism of presheaves if locally surjective. -/
abbrev LocallySurjective := Presheaf.LocallySurjective J Ï†.1

instance locallyInjective_comp [LocallyInjective Ï†] [LocallyInjective Ïˆ] :
    LocallyInjective (Ï† â‰« Ïˆ) :=
  Presheaf.locallyInjective_comp J Ï†.1 Ïˆ.1

instance locallySurjective_comp [LocallySurjective Ï†] [LocallySurjective Ïˆ] :
    LocallySurjective (Ï† â‰« Ïˆ) :=
  Presheaf.locallySurjective_comp J Ï†.1 Ïˆ.1

lemma locallyInjective_of_locallyInjective [LocallyInjective (Ï† â‰« Ïˆ)] :
    LocallyInjective Ï† :=
  Presheaf.locallyInjective_of_locallyInjective J Ï†.1 Ïˆ.1

lemma locallySurjective_of_locallySurjective [LocallySurjective (Ï† â‰« Ïˆ)] :
    LocallySurjective Ïˆ :=
  Presheaf.locallySurjective_of_locallySurjective J Ï†.1 Ïˆ.1

variable {Ï† Ïˆ}

lemma locallyInjective_of_locallyInjective_fac {Ï†Ïˆ : Fâ‚ âŸ¶ Fâ‚ƒ} (fac : Ï† â‰« Ïˆ = Ï†Ïˆ)
    [LocallyInjective Ï†Ïˆ] : LocallyInjective Ï† := by
  subst fac
  exact locallyInjective_of_locallyInjective Ï† Ïˆ

lemma locallySurjective_of_locallySurjective_fac {Ï†Ïˆ : Fâ‚ âŸ¶ Fâ‚ƒ} (fac : Ï† â‰« Ïˆ = Ï†Ïˆ)
    [LocallySurjective Ï†Ïˆ] : LocallySurjective Ïˆ := by
  subst fac
  exact locallySurjective_of_locallySurjective Ï† Ïˆ

section

variable (Ï†)
variable [J.HasSheafCompose (forget D)]

instance locallyInjective_forget [LocallyInjective Ï†] :
    LocallyInjective ((sheafCompose J (forget D)).map Ï†) :=
  Presheaf.locallyInjective_forget J Ï†.1

instance locallySurjective_forget [LocallySurjective Ï†] :
    LocallySurjective ((sheafCompose J (forget D)).map Ï†) :=
  Presheaf.locallySurjective_forget J Ï†.1

lemma mono_of_locallyInjective' {F G : Sheaf J (Type w)} (Ï† : F âŸ¶ G) [LocallyInjective Ï†] :
    Mono Ï† where
  right_cancellation {Z} fâ‚ fâ‚‚ h := by
    ext X x
    apply ((Presieve.isSeparated_of_isSheaf _ _ ((isSheaf_iff_isSheaf_of_type _ _).1 F.2)) _
      (Presheaf.sieveOfLocallyInjective_mem J Ï†.1 (fâ‚.1.app _ x) (fâ‚‚.1.app _ x)
      (congr_fun (congr_app (congr_arg Sheaf.Hom.val h) X) x))).ext
    intro Y f hf
    exact Presheaf.map_apply_eq_of_locallyInjective J Ï†.1 _ _ _ f hf

lemma epi_of_locallySurjective' {F G : Sheaf J (Type w)} (Ï† : F âŸ¶ G) [LocallySurjective Ï†] :
    Epi Ï† where
  left_cancellation := by
    intro H fâ‚ fâ‚‚ hâ‚â‚‚
    ext X x
    apply ((Presieve.isSeparated_of_isSheaf _ _ ((isSheaf_iff_isSheaf_of_type _ _).1 H.2)) _
      (Presheaf.sieveOfLocallySurjective_mem J Ï†.1 x)).ext
    intro Y f hf
    have hâ‚ := congr_fun (fâ‚.1.naturality f.op) x
    have hâ‚‚ := congr_fun (fâ‚‚.1.naturality f.op) x
    dsimp at hâ‚ hâ‚‚
    simp only [â† hâ‚, â† hâ‚‚]
    erw [congr_arg (fâ‚.val.app (op Y)) (Presheaf.app_apply_localPreimage J Ï†.1 x f hf).symm,
      congr_arg (fâ‚‚.val.app (op Y)) (Presheaf.app_apply_localPreimage J Ï†.1 x f hf).symm]
    exact congr_fun (congr_app (congr_arg Sheaf.Hom.val hâ‚â‚‚) (op Y)) _

instance : Faithful (sheafCompose J (forget D)) where
  map_injective {F G fâ‚ fâ‚‚} h := by
    ext X x
    exact congr_fun (congr_app ((sheafToPresheaf _ _).congr_map h) X) x

lemma mono_of_locallyInjective [LocallyInjective Ï†] : Mono Ï† :=
  (sheafCompose J (forget D)).mono_of_mono_map (mono_of_locallyInjective' _)

lemma epi_of_locallySurjective [LocallySurjective Ï†] : Epi Ï† :=
  (sheafCompose J (forget D)).epi_of_epi_map (epi_of_locallySurjective' _)

end

end Sheaf

namespace Presheaf

variable [HasWeakSheafify J D]
  [âˆ€ (P : Cáµ’áµ– â¥¤ D), Presheaf.LocallyInjective J (toSheafify J P)]
  [âˆ€ (P : Cáµ’áµ– â¥¤ D), Presheaf.LocallySurjective J (toSheafify J P)]
  {F G : Cáµ’áµ– â¥¤ D} (Ï† : F âŸ¶ G)

lemma sheafifyMap_locallyInjective_iff :
    LocallyInjective J (sheafifyMap J Ï†) â†”
      LocallyInjective J Ï† := by
  rw [â† locallyInjective_comp_iff J _ (toSheafify J G), toSheafify_naturality J Ï†]
  constructor
  Â· intro
    infer_instance
  Â· intro
    exact locallyInjective_of_locallyInjective_of_locallySurjective J
      (toSheafify J F) (sheafifyMap J Ï†)

lemma presheafToSheaf_map_locallyInjective_iff :
    Sheaf.LocallyInjective ((presheafToSheaf J D).map Ï†) â†”
      LocallyInjective J Ï† :=
  sheafifyMap_locallyInjective_iff J Ï†

lemma sheafifyMap_locallySurjective_iff :
    LocallySurjective J (sheafifyMap J Ï†) â†”
      LocallySurjective J Ï† := by
  rw [â† locallySurjective_comp_iff J (toSheafify J F) _, â† toSheafify_naturality J Ï†]
  constructor
  Â· intro
    exact locallySurjective_of_locallySurjective_of_locallyInjective J Ï† (toSheafify J G)
  Â· intro
    infer_instance

lemma presheafToSheaf_map_locallySurjective_iff :
    Sheaf.LocallySurjective ((presheafToSheaf J D).map Ï†) â†”
      LocallySurjective J Ï† :=
  sheafifyMap_locallySurjective_iff J Ï†

end Presheaf

end CategoryTheory
