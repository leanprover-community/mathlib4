/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Sites.Sheaf

/-! Objects which cover the terminal object

In this file, given a site `(C, J)`, we introduce the notion of a family
of objects `Y : I ‚Üí C` which "cover the final object": this means
that for all `X : C`, the sieve `Sieve.ofObjects Y X` is covering for `J`.
When there is a terminal object `X : C`, then `J.CoversTop Y`
holds iff `Sieve.ofObjects Y X` is covering for `J`.

We introduce a notion of compatible family of elements on objects `Y`
and obtain `Presheaf.FamilyOfElementsOnObjects.IsCompatible.existsUnique_section`
which asserts that if a presheaf of types is a sheaf, then any compatible
family of elements on objects `Y` which cover the final object extends as
a section of this presheaf.

-/

universe w v' v u' u

namespace CategoryTheory

open Limits

variable {C : Type u} [Category.{v} C] (J : GrothendieckTopology C)
  {A : Type u'} [Category.{v'} A]

namespace GrothendieckTopology

/-- A family of objects `Y : I ‚Üí C` "covers the final object"
if for all `X : C`, the sieve `ofObjects Y X` is a covering sieve. -/
def CoversTop {I : Type*} (Y : I ‚Üí C) : Prop :=
  ‚àÄ (X : C), Sieve.ofObjects Y X ‚àà J X

lemma coversTop_iff_of_isTerminal (X : C) (hX : IsTerminal X)
    {I : Type*} (Y : I ‚Üí C) :
    J.CoversTop Y ‚Üî Sieve.ofObjects Y X ‚àà J X := by
  constructor
  ¬∑ tauto
  ¬∑ intro h W
    apply J.superset_covering _ (J.pullback_stable (hX.from W) h)
    rintro T a ‚ü®i, ‚ü®b‚ü©‚ü©
    exact ‚ü®i, ‚ü®b‚ü©‚ü©

namespace CoversTop

variable {J}
variable {I : Type*} {Y : I ‚Üí C} (hY : J.CoversTop Y)
include hY

/-- The cover of any object `W : C` attached to a family of objects `Y` that satisfy
`J.CoversTop Y` -/
abbrev cover (W : C) : Cover J W := ‚ü®Sieve.ofObjects Y W, hY W‚ü©

lemma ext (F : Sheaf J A) {c : Cone F.1} (hc : IsLimit c) {X : A} {f g : X ‚ü∂ c.pt}
    (h : ‚àÄ (i : I), f ‚â´ c.œÄ.app (Opposite.op (Y i)) =
      g ‚â´ c.œÄ.app (Opposite.op (Y i))) :
    f = g := by
  refine hc.hom_ext (fun Z => F.2.hom_ext (hY.cover Z.unop) _ _ ?_)
  rintro ‚ü®W, a, ‚ü®i, ‚ü®b‚ü©‚ü©‚ü©
  simpa using h i =‚â´ F.1.map b.op

lemma sections_ext (F : Sheaf J (Type _)) {x y : F.1.sections}
    (h : ‚àÄ (i : I), x.1 (Opposite.op (Y i)) = y.1 (Opposite.op (Y i))) :
    x = y := by
  ext W
  apply (Presieve.isSeparated_of_isSheaf J F.1
    ((isSheaf_iff_isSheaf_of_type _ _).1 F.2) _ (hY W.unop)).ext
  rintro T a ‚ü®i, ‚ü®b‚ü©‚ü©
  simpa using congr_arg (F.1.map b.op) (h i)

end CoversTop

end GrothendieckTopology

namespace Presheaf

variable (F : C·µí·µñ ‚•§ Type w) {I : Type*} (Y : I ‚Üí C)

/-- A family of elements of a presheaf of types `F` indexed by a family of objects
`Y : I ‚Üí C` consists of the data of an element in `F.obj (Opposite.op (Y i))` for all `i`. -/
def FamilyOfElementsOnObjects := ‚àÄ (i : I), F.obj (Opposite.op (Y i))

namespace FamilyOfElementsOnObjects

variable {F Y}
variable (x : FamilyOfElementsOnObjects F Y)

/-- `x : FamilyOfElementsOnObjects F Y` is compatible if for any object `Z` such that
there exists a morphism `f : Z ‚Üí Y i`, then the pullback of `x i` by `f` is independent
of `f` and `i`. -/
def IsCompatible (x : FamilyOfElementsOnObjects F Y) : Prop :=
  ‚àÄ (Z : C) (i j : I) (f : Z ‚ü∂ Y i) (g : Z ‚ü∂ Y j),
    F.map f.op (x i) = F.map g.op (x j)

/-- A family of elements indexed by `Sieve.ofObjects Y X` that is induced by
`x : FamilyOfElementsOnObjects F Y`. See the equational lemma
`IsCompatible.familyOfElements_apply` which holds under the assumption `x.IsCompatible`. -/
noncomputable def familyOfElements (X : C) :
    Presieve.FamilyOfElements F (Sieve.ofObjects Y X).arrows :=
  fun _ _ hf => F.map hf.choose_spec.some.op (x _)

namespace IsCompatible

variable {x}

lemma familyOfElements_apply (hx : x.IsCompatible) {X Z : C} (f : Z ‚ü∂ X) (i : I) (œÜ : Z ‚ü∂ Y i) :
    familyOfElements x X f ‚ü®i, ‚ü®œÜ‚ü©‚ü© = F.map œÜ.op (x i) := by
  apply hx

lemma familyOfElements_isCompatible (hx : x.IsCompatible) (X : C) :
    (familyOfElements x X).Compatible := by
  intro Y‚ÇÅ Y‚ÇÇ Z g‚ÇÅ g‚ÇÇ f‚ÇÅ f‚ÇÇ ‚ü®i‚ÇÅ, ‚ü®œÜ‚ÇÅ‚ü©‚ü© ‚ü®i‚ÇÇ, ‚ü®œÜ‚ÇÇ‚ü©‚ü© _
  simpa [hx.familyOfElements_apply f‚ÇÅ i‚ÇÅ œÜ‚ÇÅ,
    hx.familyOfElements_apply f‚ÇÇ i‚ÇÇ œÜ‚ÇÇ] using hx Z i‚ÇÅ i‚ÇÇ (g‚ÇÅ ‚â´ œÜ‚ÇÅ) (g‚ÇÇ ‚â´ œÜ‚ÇÇ)

variable {J}

lemma existsUnique_section (hx : x.IsCompatible) (hY : J.CoversTop Y) (hF : IsSheaf J F) :
    ‚àÉ! (s : F.sections), ‚àÄ (i : I), s.1 (Opposite.op (Y i)) = x i := by
  have H := (isSheaf_iff_isSheaf_of_type _ _).1 hF
  apply existsUnique_of_exists_of_unique
  ¬∑ let s := fun (X : C) => (H _ (hY X)).amalgamate _
      (hx.familyOfElements_isCompatible X)
    have hs : ‚àÄ {X : C} (i : I) (f : X ‚ü∂ Y i), s X = F.map f.op (x i) := fun {X} i f => by
      have h := Presieve.IsSheafFor.valid_glue (H _ (hY X))
          (hx.familyOfElements_isCompatible _) (ùüô _) ‚ü®i, ‚ü®f‚ü©‚ü©
      simp only [op_id, F.map_id, types_id_apply] at h
      exact h.trans (hx.familyOfElements_apply _ _ _)
    have hs' : ‚àÄ {W X : C} (a : W ‚ü∂ X) (i : I) (_ : W ‚ü∂ Y i), F.map a.op (s X) = s W := by
      intro W X a i b
      rw [hs i b]
      exact (Presieve.IsSheafFor.valid_glue (H _ (hY X))
        (hx.familyOfElements_isCompatible _) a ‚ü®i, ‚ü®b‚ü©‚ü©).trans (familyOfElements_apply hx _ _ _)
    refine ‚ü®‚ü®fun X => s X.unop, ?_‚ü©, fun i => (hs i (ùüô (Y i))).trans (by simp)‚ü©
    rintro ‚ü®Y‚ÇÅ‚ü© ‚ü®Y‚ÇÇ‚ü© ‚ü®f : Y‚ÇÇ ‚ü∂ Y‚ÇÅ‚ü©
    change F.map f.op (s Y‚ÇÅ) = s Y‚ÇÇ
    apply (Presieve.isSeparated_of_isSheaf J F H _ (hY Y‚ÇÇ)).ext
    rintro Z œÜ ‚ü®i, ‚ü®g‚ü©‚ü©
    rw [hs' œÜ i g, ‚Üê hs' (œÜ ‚â´ f) i g, op_comp, F.map_comp]
    rfl
  ¬∑ intro y‚ÇÅ y‚ÇÇ hy‚ÇÅ hy‚ÇÇ
    exact hY.sections_ext ‚ü®F, hF‚ü© (fun i => by rw [hy‚ÇÅ, hy‚ÇÇ])

variable (hx : x.IsCompatible) (hY : J.CoversTop Y) (hF : IsSheaf J F)

/-- The section of a sheaf of types which lifts a compatible family of elements indexed
by objects which cover the terminal object. -/
noncomputable def section_ : F.sections := (hx.existsUnique_section hY hF).choose

@[simp]
lemma section_apply (i : I) : (hx.section_ hY hF).1 (Opposite.op (Y i)) = x i :=
  (hx.existsUnique_section hY hF).choose_spec.1 i

end IsCompatible

end FamilyOfElementsOnObjects

end Presheaf

end CategoryTheory
