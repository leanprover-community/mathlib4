/-
Copyright (c) 2025 Ben Eltschig. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ben Eltschig
-/
import Mathlib.CategoryTheory.Limits.FunctorCategory.Shapes.Products
import Mathlib.CategoryTheory.Sites.ConstantSheaf

/-!
# Global sections of sheaves
In this file we define a global sections functor `Sheaf.Î“ : Sheaf J A â¥¤ A` and show that it
is isomorphic to several other constructions when they exist, most notably evaluation of sheaves
on a terminal object and `Functor.sectionsFunctor`.

## Main definitions / results
* `HasGlobalSectionsFunctor J A`: typeclass stating that the constant sheaf functor `A â¥¤ Sheaf J A`
  has a right-adjoint.
* `Sheaf.Î“ J A`: the global sections functor `Sheaf J A â¥¤ A`, defined as the right-adjoint of the
  constant sheaf functor, whenever that exists.
* `constantSheafÎ“Adj J A`: the adjunction between the constant sheaf functor and `Sheaf.Î“ J A`.
* `Sheaf.Î“NatIsoSheafSections J A hT`: on sites with a terminal object `T`, `Sheaf.Î“ J A` exists and
  is isomorphic to the functor evaluating sheaves at `T`.
* `Sheaf.Î“NatIsoLim J A`: when `A` has limits of shape `Cáµ’áµ–`, `Sheaf.Î“ J A` exists and is isomorphic
  to the functor taking each sheaf to the limit of its underlying presheaf.
* `Sheaf.Î“NatIsoSectionsFunctor J`: for sheaves of types, `Sheaf.Î“ J A` is isomorphic to the
  functor taking each sheaf to the type of sections of its underlying presheaf in the sense of
  `Functor.sections`.
* `Sheaf.Î“NatIsoCoyonedaObj J`: for sheaves of types, `Sheaf.Î“ J A` is isomorphic to the
  coyoneda embedding of the terminal sheaf, i.e. the functor sending each sheaf `F` to the type
  of morphisms from the terminal sheaf to `F`.
-/

universe u v w uâ‚‚ vâ‚‚

open CategoryTheory Limits Sheaf Opposite GrothendieckTopology

namespace CategoryTheory

variable {C : Type u} [Category.{v} C] (J : GrothendieckTopology C)
  (A : Type uâ‚‚) [Category.{vâ‚‚} A] [HasWeakSheafify J A]

/-- Typeclass stating that the constant sheaf functor has a right adjoint. This right adjoint will
then be called the global sections functor and written `Sheaf.Î“`. -/
abbrev HasGlobalSectionsFunctor := (constantSheaf J A).IsLeftAdjoint

/-- We define the global sections functor as the right-adjoint of the constant sheaf functor
whenever it exists. -/
noncomputable def Sheaf.Î“ [HasGlobalSectionsFunctor J A] : Sheaf J A â¥¤ A :=
  (constantSheaf J A).rightAdjoint

/-- The constant sheaf functor is by definition left-adjoint to the global sections functor. -/
noncomputable def constantSheafÎ“Adj [HasGlobalSectionsFunctor J A] :
    constantSheaf J A âŠ£ Î“ J A :=
  Adjunction.ofIsLeftAdjoint (constantSheaf J A)

instance [HasGlobalSectionsFunctor J A] : (Î“ J A).IsRightAdjoint := by
  unfold Î“; infer_instance

/-- Sites with a terminal object admit a global sections functor. -/
instance hasGlobalSectionsFunctor_of_hasTerminal [HasTerminal C] :
    HasGlobalSectionsFunctor J A :=
  âŸ¨_, âŸ¨constantSheafAdj J A terminalIsTerminalâŸ©âŸ©

/-- On sites with a terminal object, the global sections functor is isomorphic to the functor
of sections on that object. -/
noncomputable def Sheaf.Î“NatIsoSheafSections [HasTerminal C] {T : C} (hT : IsTerminal T) :
    Î“ J A â‰… (sheafSections J A).obj (op T) :=
  (constantSheafÎ“Adj J A).rightAdjointUniq (constantSheafAdj J A hT)

/-- Every site `C` admits a global sections functor for `A`-valued sheaves when `A` has limits of
shape `Cáµ’áµ–`. -/
instance hasGlobalSectionsFunctor_of_hasLimitsOfShape [HasLimitsOfShape Cáµ’áµ– A] :
    HasGlobalSectionsFunctor J A :=
  âŸ¨sheafToPresheaf J A â‹™ lim, âŸ¨constLimAdj.comp (sheafificationAdjunction J A)âŸ©âŸ©

/-- Global sections of sheaves are naturally isomorphic to the limits of the underlying presheaves.
Note that while `HasLimitsOfShape Cáµ’áµ– A` is needed here to talk about `lim` as a functor, global
sections are always limits, it just has to be stated a little bit more carefully. -/
noncomputable def Sheaf.Î“NatIsoLim [HasLimitsOfShape Cáµ’áµ– A] :
    Î“ J A â‰… sheafToPresheaf J A â‹™ lim :=
  (constantSheafÎ“Adj J A).rightAdjointUniq (constLimAdj.comp (sheafificationAdjunction J A))

variable {J A}

/-- Natural transformations from a constant presheaf into a sheaf correspond to morphisms to its
global sections. -/
noncomputable def Sheaf.Î“HomEquiv [HasGlobalSectionsFunctor J A] {X : A} {F : Sheaf J A} :
    ((Functor.const _).obj X âŸ¶ F.val) â‰ƒ (X âŸ¶ (Î“ J A).obj F) :=
  ((sheafificationAdjunction J A).homEquiv _ _).symm.trans
    ((constantSheafÎ“Adj J A).homEquiv _ _)

/-- Naturality lemma for `Î“HomEquiv` analogous to `Adjunction.homEquiv_naturality_left`. -/
lemma Sheaf.Î“HomEquiv_naturality_left [HasGlobalSectionsFunctor J A] {X' X : A} {F : Sheaf J A}
    (f : X' âŸ¶ X) (g : (Functor.const _).obj X âŸ¶ F.val) :
    Î“HomEquiv ((Functor.const _).map f â‰« g) = f â‰« Î“HomEquiv g :=
  (congrArg _ ((sheafificationAdjunction J A).homEquiv_naturality_left_symm _ _)).trans
    ((constantSheafÎ“Adj J A).homEquiv_naturality_left _ _)

/-- Naturality lemma for `Î“HomEquiv` analogous to `Adjunction.homEquiv_naturality_left_symm`. -/
lemma Sheaf.Î“HomEquiv_naturality_left_symm [HasGlobalSectionsFunctor J A] {X' X : A} {F : Sheaf J A}
    (f : X' âŸ¶ X) (g : X âŸ¶ (Î“ J A).obj F) :
    Î“HomEquiv.symm (f â‰« g) = (Functor.const _).map f â‰« Î“HomEquiv.symm g :=
  (congrArg _ ((constantSheafÎ“Adj J A).homEquiv_naturality_left_symm _ _)).trans
    ((sheafificationAdjunction J A).homEquiv_naturality_left _ _)

/-- Naturality lemma for `Î“HomEquiv` analogous to `Adjunction.homEquiv_naturality_right`. -/
lemma Sheaf.Î“HomEquiv_naturality_right [HasGlobalSectionsFunctor J A] {X : A} {F F' : Sheaf J A}
    (f : (Functor.const _).obj X âŸ¶ F.val) (g : F âŸ¶ F') :
    Î“HomEquiv (f â‰« g.val) = Î“HomEquiv f â‰« (Î“ J A).map g :=
  (congrArg _ ((sheafificationAdjunction J A).homEquiv_naturality_right_symm _ _)).trans
    ((constantSheafÎ“Adj J A).homEquiv_naturality_right _ _)

/-- Naturality lemma for `Î“HomEquiv` analogous to `Adjunction.homEquiv_naturality_right_symm`. -/
lemma Sheaf.Î“HomEquiv_naturality_right_symm [HasGlobalSectionsFunctor J A] {X : A}
    {F F' : Sheaf J A} (f : X âŸ¶ (Î“ J A).obj F) (g : F âŸ¶ F') :
    Î“HomEquiv.symm (f â‰« (Î“ J A).map g) = Î“HomEquiv.symm f â‰« g.val :=
  (congrArg _ ((constantSheafÎ“Adj J A).homEquiv_naturality_right_symm _ _)).trans
    ((sheafificationAdjunction J A).homEquiv_naturality_right _ _)

/-- The cone over a given sheaf whose cone point are the global sections and whose components are
the restriction maps. -/
@[simps pt]
noncomputable def Sheaf.coneÎ“ [HasGlobalSectionsFunctor J A] (F : Sheaf J A) : Cone F.val where
  pt := (Î“ J A).obj F
  Ï€ := Î“HomEquiv.symm (ğŸ™ _)

/-- The global sections cone `Sheaf.coneÎ“` is limiting - that is, global sections are limits even
when not all limits of shape `Cáµ’áµ–` exist in `A`. -/
noncomputable def Sheaf.isLimitConeÎ“ [HasGlobalSectionsFunctor J A] (F : Sheaf J A) :
    IsLimit F.coneÎ“ where
  lift c := F.Î“HomEquiv c.Ï€
  fac c j := by
    suffices h : ((Functor.const Cáµ’áµ–).map (Î“HomEquiv c.Ï€)) â‰« F.coneÎ“.Ï€ = c.Ï€ from congr_app h j
    simp [coneÎ“, â† Î“HomEquiv_naturality_left_symm]
  uniq c f hf := by
    replace hf : ((Functor.const Cáµ’áµ–).map f) â‰« F.coneÎ“.Ï€ = c.Ï€ := by ext j; exact hf j
    simpa [coneÎ“, â† Î“HomEquiv_naturality_left_symm, Equiv.symm_apply_eq] using hf

/-- The restriction map from global sections of `F` to sections on `U`. -/
noncomputable def Sheaf.Î“res [HasGlobalSectionsFunctor J A] (F : Sheaf J A) (U : Cáµ’áµ–) :
    (Î“ J A).obj F âŸ¶ F.val.obj U :=
  F.coneÎ“.Ï€.app U

@[reassoc (attr := simp)]
lemma Sheaf.Î“res_map [HasGlobalSectionsFunctor J A] (F : Sheaf J A) {V U : Cáµ’áµ–} (f : U âŸ¶ V) :
    F.Î“res U â‰« F.val.map f = F.Î“res V :=
  F.coneÎ“.w f

@[simp]
lemma Sheaf.coneÎ“_Ï€_app [HasGlobalSectionsFunctor J A] (F : Sheaf J A) (U : Cáµ’áµ–) :
    F.coneÎ“.Ï€.app U = F.Î“res U := rfl

variable (J A)

-- this is currently needed to obtain the instance `HasSheafify J (Type max u v)`.
attribute [local instance] CategoryTheory.Types.instConcreteCategory
attribute [local instance] CategoryTheory.Types.instFunLike

/-- Sections of a functor correspond to morphisms from a terminal functor to it. We use the constant
functor on a given singleton type here as a specific choice of terminal functor. -/
@[simps apply_app]
def Functor.sectionsEquivHom (F : C â¥¤ Type w) (X : Type w) [Unique X] :
    F.sections â‰ƒ ((Functor.const _).obj X âŸ¶ F) where
  toFun s :=
    { app j x := s.1 j
      naturality _ _ _ := by ext x; simp }
  invFun Ï„ := âŸ¨fun j â†¦ Ï„.app _ (default : X), fun Ï† â†¦ (congr_fun (Ï„.naturality Ï†) _).symmâŸ©
  left_inv s := rfl
  right_inv Ï„ := by
    ext _ (x : X)
    rw [Unique.eq_default x]

lemma Functor.sectionsEquivHom_naturality {F G : C â¥¤ Type w} (f : F âŸ¶ G) (X : Type w) [Unique X]
    (x : F.sections) :
    (G.sectionsEquivHom X) ((sectionsFunctor C).map f x) = (F.sectionsEquivHom X) x â‰« f := by
  rfl

lemma Functor.sectionsEquivHom_naturality_symm {F G : C â¥¤ Type w} (f : F âŸ¶ G) (X : Type w)
    [Unique X] (Ï„ : (Functor.const C).obj X âŸ¶ F) : (G.sectionsEquivHom X).symm (Ï„ â‰« f) =
    (sectionsFunctor C).map f ((F.sectionsEquivHom X).symm Ï„) := by
  rfl

/-- For functors `F : C â¥¤ Type _`, `F.sections` is naturally isomorphic to the type `âŠ¤_ _ âŸ¶ F`
of natural transformations from the terminal functor to `F`. -/
@[simps!]
noncomputable def sectionsFunctorNatIsoCoyoneda (X : Type max u w) [Unique X] :
    Functor.sectionsFunctor.{v,max u w} C â‰… coyoneda.obj (op ((Functor.const C).obj X)) :=
  NatIso.ofComponents fun F â†¦ (F.sectionsEquivHom X).toIso

#find_home! sectionsFunctorNatIsoCoyoneda

/-- Global sections of a sheaf of types correspond to sections of the underlying presheaf. -/
noncomputable def Sheaf.Î“ObjEquivSections [HasWeakSheafify J (Type w)]
    [HasGlobalSectionsFunctor J (Type w)] (F : Sheaf J (Type w)) :
      (Î“ J (Type w)).obj F â‰ƒ F.val.sections :=
  (Equiv.trans (by exact (Equiv.funUnique PUnit _).symm) Î“HomEquiv.symm).trans
    (F.val.sectionsEquivHom PUnit).symm

/-- For sheaves of types, the global sections functor is isomorphic to the sections functor
on presheaves. -/
noncomputable def Sheaf.Î“NatIsoSectionsFunctor :
    Î“ J (Type max u v) â‰… sheafToPresheaf J _ â‹™ Functor.sectionsFunctor _ :=
  NatIso.ofComponents (fun F â†¦ (Î“ObjEquivSections J F).toIso) fun _ â†¦ by
    ext x
    dsimp [Î“ObjEquivSections]
    exact (congr_arg _ (Î“HomEquiv_naturality_right_symm _ _)).trans
      (Functor.sectionsEquivHom_naturality_symm _ _ _)

/-- Global sections of a sheaf of types `F` correspond to morphisms from a terminal sheaf to `F`.
We use the constant sheaf on a singleton type as a specific choice of terminal sheaf here. -/
noncomputable def Sheaf.Î“ObjEquivHom [HasWeakSheafify J (Type w)]
    [HasGlobalSectionsFunctor J (Type w)] (F : Sheaf J (Type w)) (X : Type w) [Unique X]:
      (Î“ J (Type w)).obj F â‰ƒ ((constantSheaf J (Type w)).obj X âŸ¶ F) :=
  (Equiv.funUnique X _).symm.trans ((constantSheafÎ“Adj J (Type w)).homEquiv _ _).symm

/-- For sheaves of types, the global sections functor is isomorphic to the covariant hom
functor of the terminal sheaf. -/
noncomputable def Sheaf.Î“NatIsoCoyoneda (X : Type max u v) [Unique X] :
    Î“ J (Type max u v) â‰… coyoneda.obj (op ((constantSheaf J _).obj X)) := by
  exact NatIso.ofComponents (fun F â†¦ (F.Î“ObjEquivHom J X).toIso) fun _ â†¦ by
    ext x
    dsimp
    exact (constantSheafÎ“Adj J _).homEquiv_naturality_right_symm _ _

end CategoryTheory
