/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Limits.EpiMono
import Mathlib.CategoryTheory.MorphismProperty
import Mathlib.CategoryTheory.Sites.Sheafification
/-!
# Charactezition of mono and epi in the category of sheaves of types

In this file, we obtain the lemmas `Sheaf.mono_iff_injective`, `Sheaf.isIso_iff_bijective`
and `Sheaf.epi_iff_locally_surjective` which are concrete characterizations of monomorphisms,
isomorphisms and epimorphisms in a category of sheaves of types for a Grothendieck
topology `J` on a category `C`.

Given a morphism `œÜ : F ‚ü∂ G` in `Sheaf J (Type _)`, it is easy to show that it is
a mono (resp. an iso) iff for all `X : C·µí·µñ`, the map `œÜ.val.app X : F.val.obj X ‚ü∂ G.val.obj X`
is injective (resp. bijective), similarly as for morphisms of presheaves. We also
obtain a characterization of epimorphism : `œÜ` is an epimorphism iff any section of
`G` can be *locally* lifted to a section of `F`.

The proof of the characterization of epimorphisms uses an epi/mono factorization of
`œÜ : F ‚ü∂ G` (see `Sheaf.EpiMonoFactorization.œÄ_Œπ`) in order to reduce to the particular
case when `œÜ` is also a monomorphism, and for this, we show that the category of
sheaves of types is balanced: `œÜ` is an isomorphism iff it is a mono and an epi.
The proof of this last fact is obtained following the argument in SGA 4 II 4.2.

-/

universe w v u

namespace CategoryTheory

open Opposite Limits

namespace Limits

variable {J : Type u} [Category J]
  {C D : Type*} [Category C] [Category D] (F : C ‚•§ D)

instance [IsLeftAdjoint F] : PreservesColimitsOfShape J F :=
  (Adjunction.ofLeftAdjoint F).leftAdjointPreservesColimits.preservesColimitsOfShape

instance [IsLeftAdjoint F] : PreservesColimits F where

instance [IsRightAdjoint F] : PreservesLimitsOfShape J F :=
  (Adjunction.ofRightAdjoint F).rightAdjointPreservesLimits.preservesLimitsOfShape

instance [IsRightAdjoint F] : PreservesLimits F where

end Limits

namespace Limits

-- to be moved to CategoryTheory.Limits.Shapes.Types
namespace Types

variable {S X‚ÇÅ X‚ÇÇ : Type w} (f : S ‚ü∂ X‚ÇÅ) (g : S ‚ü∂ X‚ÇÇ)

/-- The pushout of two maps `f : S ‚ü∂ X‚ÇÅ` and `g : S ‚ü∂ X‚ÇÇ` is the quotient
by the equivalence relation on `X‚ÇÅ ‚äï X‚ÇÇ` relation generated by this relation. -/
inductive PushoutRel (f : S ‚ü∂ X‚ÇÅ) (g : S ‚ü∂ X‚ÇÇ) :
    Sum X‚ÇÅ X‚ÇÇ ‚Üí Sum X‚ÇÅ X‚ÇÇ ‚Üí Prop
  | mk (s : S) : PushoutRel f g (Sum.inl (f s)) (Sum.inr (g s))

/-- Construction of the pushout in the category of types, as a quotient of `X‚ÇÅ ‚äï X‚ÇÇ`. -/
def Pushout : Type w := _root_.Quot (PushoutRel f g)

/-- The left inclusion in the constructed pushout `Pushout f g`. -/
@[simp]
def pushoutInl : X‚ÇÅ ‚ü∂ Pushout f g := fun x => Quot.mk _ (Sum.inl x)

/-- The right inclusion in the constructed pushout `Pushout f g`. -/
@[simp]
def pushoutInr : X‚ÇÇ ‚ü∂ Pushout f g := fun x => Quot.mk _ (Sum.inr x)

lemma pushout_condition : f ‚â´ pushoutInl f g = g ‚â´ pushoutInr f g := by
  ext x
  exact Quot.sound (PushoutRel.mk x)

/-- The constructed pushout cocone in the category of types. -/
@[simps!]
def pushoutCocone : PushoutCocone f g :=
    PushoutCocone.mk _ _ (pushout_condition f g)

/-- The cocone `pushoutCocone f g` is colimit. -/
def isColimitPushoutCocone : IsColimit (pushoutCocone f g) :=
  PushoutCocone.IsColimit.mk _ (fun s => Quot.lift (fun x => match x with
      | Sum.inl x‚ÇÅ => s.inl x‚ÇÅ
      | Sum.inr x‚ÇÇ => s.inr x‚ÇÇ) (by
    rintro _ _ ‚ü®t‚ü©
    exact congr_fun s.condition t)) (fun _ => rfl) (fun _ => rfl) (fun s m h‚ÇÅ h‚ÇÇ => by
      ext ‚ü®x‚ÇÅ|x‚ÇÇ‚ü©
      ¬∑ exact congr_fun h‚ÇÅ x‚ÇÅ
      ¬∑ exact congr_fun h‚ÇÇ x‚ÇÇ)

/-- In case `f : S ‚ü∂ X‚ÇÅ` is a monomorphism, this relation is the equivalence relation
generated by `PushoutRel f g`. -/
inductive PushoutRel' : Sum X‚ÇÅ X‚ÇÇ ‚Üí Sum X‚ÇÅ X‚ÇÇ ‚Üí Prop
  | refl' (x : Sum X‚ÇÅ X‚ÇÇ) : PushoutRel' x x
  | inl_inl (x‚ÇÄ y‚ÇÄ : S) (h : g x‚ÇÄ = g y‚ÇÄ) : PushoutRel' (Sum.inl (f x‚ÇÄ)) (Sum.inl (f y‚ÇÄ))
  | inl_inr (s : S) : PushoutRel' (Sum.inl (f s)) (Sum.inr (g s))
  | inr_inl (s : S) : PushoutRel' (Sum.inr (g s)) (Sum.inl (f s))

namespace PushoutRel'

lemma refl (x : Sum X‚ÇÅ X‚ÇÇ) : PushoutRel' f g x x :=
  PushoutRel'.refl' x

lemma inl_rel'_inl_iff (x‚ÇÅ y‚ÇÅ : X‚ÇÅ) :
    PushoutRel' f g (Sum.inl x‚ÇÅ) (Sum.inl y‚ÇÅ) ‚Üî x‚ÇÅ = y‚ÇÅ ‚à®
      ‚àÉ (x‚ÇÄ y‚ÇÄ : S) (_ : g x‚ÇÄ = g y‚ÇÄ), x‚ÇÅ = f x‚ÇÄ ‚àß y‚ÇÅ = f y‚ÇÄ := by
  constructor
  ¬∑ intro h
    obtain _|‚ü®_, _, h‚ü©  := h
    ¬∑ exact Or.inl rfl
    ¬∑ exact Or.inr ‚ü®_, _, h, rfl, rfl‚ü©
  ¬∑ intro h
    obtain rfl | ‚ü®_,_ , h, rfl, rfl‚ü© := h
    ¬∑ apply refl
    ¬∑ exact inl_inl _ _ h

lemma inl_rel'_inr_iff (x‚ÇÅ : X‚ÇÅ) (x‚ÇÇ : X‚ÇÇ) :
    PushoutRel' f g (Sum.inl x‚ÇÅ) (Sum.inr x‚ÇÇ) ‚Üî
      ‚àÉ (s : S), x‚ÇÅ = f s ‚àß x‚ÇÇ = g s := by
  constructor
  ¬∑ intro h
    cases h
    exact ‚ü®_, rfl, rfl‚ü©
  ¬∑ rintro ‚ü®s, rfl, rfl‚ü©
    exact PushoutRel'.inl_inr _

lemma inr_rel'_inr_iff (x‚ÇÇ y‚ÇÇ : X‚ÇÇ) :
    PushoutRel' f g (Sum.inr x‚ÇÇ) (Sum.inr y‚ÇÇ) ‚Üî x‚ÇÇ = y‚ÇÇ := by
  constructor
  ¬∑ intro h
    cases h
    rfl
  ¬∑ rintro rfl
    apply refl

variable {f g}

lemma symm {x y : Sum X‚ÇÅ X‚ÇÇ} (h : PushoutRel' f g x y) :
    PushoutRel' f g y x := by
  obtain _|‚ü®_, _, h‚ü©|_|_ := h
  ¬∑ apply refl
  ¬∑ exact PushoutRel'.inl_inl _ _ h.symm
  ¬∑ exact PushoutRel'.inr_inl _
  ¬∑ exact PushoutRel'.inl_inr _

variable [Mono f]

variable (f g)

lemma equivalence :
    _root_.Equivalence (PushoutRel' f g) where
  refl := PushoutRel'.refl _ _
  symm h := h.symm
  trans := by
    intro x y z hxy hyz
    obtain _|‚ü®_, _, h‚ü©|s|_ := hxy
    ¬∑ exact hyz
    ¬∑ obtain z‚ÇÅ|z‚ÇÇ := z
      ¬∑ rw [inl_rel'_inl_iff] at hyz
        obtain rfl|‚ü®_, _, h', h'', rfl‚ü©  := hyz
        ¬∑ exact inl_inl _ _ h
        ¬∑ obtain rfl := (mono_iff_injective f).1 inferInstance h''
          exact inl_inl _ _ (h.trans h')
      ¬∑ rw [inl_rel'_inr_iff] at hyz
        obtain ‚ü®s, hs, rfl‚ü© := hyz
        obtain rfl := (mono_iff_injective f).1 inferInstance hs
        rw [‚Üê h]
        apply inl_inr
    ¬∑ obtain z‚ÇÅ|z‚ÇÇ := z
      ¬∑ replace hyz := hyz.symm
        rw [inl_rel'_inr_iff] at hyz
        obtain ‚ü®s', rfl, hs'‚ü© := hyz
        exact inl_inl _ _ hs'
      ¬∑ rw [inr_rel'_inr_iff] at hyz
        subst hyz
        apply inl_inr
    ¬∑ obtain z‚ÇÅ|z‚ÇÇ := z
      ¬∑ rw [inl_rel'_inl_iff] at hyz
        obtain rfl|‚ü®_, _, h, h', rfl‚ü©  := hyz
        ¬∑ apply inr_inl
        ¬∑ obtain rfl := (mono_iff_injective f).1 inferInstance h'
          rw [h]
          apply inr_inl
      ¬∑ rw [inl_rel'_inr_iff] at hyz
        obtain ‚ü®s, hs, rfl‚ü© := hyz
        obtain rfl := (mono_iff_injective f).1 inferInstance hs
        apply refl

end PushoutRel'

/-- The quotient of `X‚ÇÅ ‚äï X‚ÇÇ` by the relation `PushoutRel' f g`. -/
def Pushout' : Type w := _root_.Quot (PushoutRel' f g)

/-- The obvious equivalence `Pushout f g ‚âÉ Pushout' f g`. -/
def pushoutEquiv : Pushout f g ‚âÉ Pushout' f g where
  toFun := Quot.lift (Quot.mk _) (by
    rintro _ _ ‚ü®‚ü©
    apply Quot.sound
    apply PushoutRel'.inl_inr)
  invFun := Quot.lift (Quot.mk _) (by
    rintro a b h
    obtain _|‚ü®x‚ÇÄ, y‚ÇÄ, h‚ü©|_|_ := h
    ¬∑ rfl
    ¬∑ have h‚ÇÄ : PushoutRel f g _ _ := PushoutRel.mk x‚ÇÄ
      rw [Quot.sound h‚ÇÄ, h]
      symm
      apply Quot.sound
      apply PushoutRel.mk
    ¬∑ apply Quot.sound
      apply PushoutRel.mk
    ¬∑ symm
      apply Quot.sound
      apply PushoutRel.mk)
  left_inv := by rintro ‚ü®x‚ü©; rfl
  right_inv := by rintro ‚ü®x‚ü©; rfl

section

variable [Mono f]

lemma Pushout.quot_mk_eq_iff [Mono f] (a b : Sum X‚ÇÅ X‚ÇÇ) :
    (Quot.mk _ a : Pushout f g) = Quot.mk _ b ‚Üî PushoutRel' f g a b := by
  rw [‚Üê (PushoutRel'.equivalence f g).quot_mk_eq_iff]
  constructor
  ¬∑ intro h
    exact (pushoutEquiv f g).symm.injective h
  ¬∑ intro h
    exact (pushoutEquiv f g).injective h

lemma pushoutInl_eq_pushoutInr_iff (x‚ÇÅ : X‚ÇÅ) (x‚ÇÇ : X‚ÇÇ) :
    (pushoutInl f g x‚ÇÅ = pushoutInr f g x‚ÇÇ) ‚Üî
      ‚àÉ (s : S), f s = x‚ÇÅ ‚àß g s = x‚ÇÇ := by
  refine' (Pushout.quot_mk_eq_iff f g (Sum.inl x‚ÇÅ) (Sum.inr x‚ÇÇ)).trans _
  constructor
  ¬∑ rintro ‚ü®‚ü©
    exact ‚ü®_, rfl, rfl‚ü©
  ¬∑ rintro ‚ü®s, rfl, rfl‚ü©
    apply PushoutRel'.inl_inr

lemma inl_eq_inr_imp_of_iso
    {S X‚ÇÅ X‚ÇÇ: Type w} {i‚ÇÅ : S ‚ü∂ X‚ÇÅ} {i‚ÇÇ : S ‚ü∂ X‚ÇÇ} (c c' : PushoutCocone i‚ÇÅ i‚ÇÇ)
    (e : c ‚âÖ c') (x‚ÇÅ : X‚ÇÅ) (x‚ÇÇ : X‚ÇÇ) (h : c.inl x‚ÇÅ = c.inr x‚ÇÇ) :
    c'.inl x‚ÇÅ = c'.inr x‚ÇÇ := by
  convert congr_arg e.hom.hom h
  ¬∑ exact congr_fun (e.hom.w WalkingSpan.left).symm x‚ÇÅ
  ¬∑ exact congr_fun (e.hom.w WalkingSpan.right).symm x‚ÇÇ

lemma inl_eq_inr_iff_of_iso
    {S X‚ÇÅ X‚ÇÇ: Type w} {i‚ÇÅ : S ‚ü∂ X‚ÇÅ} {i‚ÇÇ : S ‚ü∂ X‚ÇÇ} (c c' : PushoutCocone i‚ÇÅ i‚ÇÇ)
    (e : c ‚âÖ c') (x‚ÇÅ : X‚ÇÅ) (x‚ÇÇ : X‚ÇÇ) :
    c.inl x‚ÇÅ = c.inr x‚ÇÇ ‚Üî c'.inl x‚ÇÅ = c'.inr x‚ÇÇ := by
  constructor
  ¬∑ apply inl_eq_inr_imp_of_iso c c' e
  ¬∑ apply inl_eq_inr_imp_of_iso c' c e.symm

lemma inl_eq_inr_iff_of_isColimit {S X‚ÇÅ X‚ÇÇ: Type w} {i‚ÇÅ : S ‚ü∂ X‚ÇÅ} {i‚ÇÇ : S ‚ü∂ X‚ÇÇ}
    {c : PushoutCocone i‚ÇÅ i‚ÇÇ} (hc : IsColimit c)
    (h‚ÇÅ : Function.Injective i‚ÇÅ)
    (x‚ÇÅ : X‚ÇÅ) (x‚ÇÇ : X‚ÇÇ) :
    c.inl x‚ÇÅ = c.inr x‚ÇÇ ‚Üî ‚àÉ (s : S), i‚ÇÅ s = x‚ÇÅ ‚àß i‚ÇÇ s = x‚ÇÇ := by
  rw [inl_eq_inr_iff_of_iso c (pushoutCocone i‚ÇÅ i‚ÇÇ)
    (Cocones.ext (IsColimit.coconePointUniqueUpToIso hc (isColimitPushoutCocone i‚ÇÅ i‚ÇÇ))
    (by aesop_cat))]
  have := (mono_iff_injective i‚ÇÅ).2 h‚ÇÅ
  apply pushoutInl_eq_pushoutInr_iff

end

end Types

end Limits

namespace Sheaf

variable {C : Type u} [Category.{v} C] {J : GrothendieckTopology C}
  {F G : Sheaf J (Type w)} (œÜ : F ‚ü∂ G)

lemma mono_of_injective
    (hœÜ : ‚àÄ (X : C·µí·µñ), Function.Injective (fun (x : F.1.obj X) => œÜ.1.app _ x)) : Mono œÜ where
  right_cancellation := by
    intro H f‚ÇÅ f‚ÇÇ h
    ext Z x
    exact hœÜ Z (congr_fun (congr_app (congr_arg Sheaf.Hom.val h) Z) x)

lemma mono_iff_injective [HasWeakSheafify J (Type w)] :
    Mono œÜ ‚Üî ‚àÄ (X : C·µí·µñ), Function.Injective (fun (x : F.1.obj X) => œÜ.1.app _ x) := by
  constructor
  ¬∑ intro hœÜ X
    simp only [‚Üê CategoryTheory.mono_iff_injective]
    change Mono (((evaluation _ _).obj X).map ((sheafToPresheaf _ _).map œÜ))
    infer_instance
  ¬∑ intro hœÜ
    exact mono_of_injective œÜ hœÜ

lemma epi_of_locally_surjective (hœÜ : ‚àÄ (X : C·µí·µñ) (x : G.1.obj X),
    ‚àÉ (S : Sieve X.unop) (_ : S ‚àà J X.unop),
    ‚àÄ (Y : C) (f : Y ‚ü∂ X.unop) (_ : S f), ‚àÉ (y : F.1.obj (op Y)),
    œÜ.1.app _ y = G.1.map f.op x) : Epi œÜ where
  left_cancellation := by
    intro H f‚ÇÅ f‚ÇÇ h
    ext X x
    obtain ‚ü®S, hS, hS'‚ü© := hœÜ _ x
    apply ((Presieve.isSeparated_of_isSheaf _ _
      ((isSheaf_iff_isSheaf_of_type _ _).1 H.2)) S hS).ext
    intro Y f hf
    obtain ‚ü®y, hy‚ü© := hS' Y f hf
    have h‚ÇÅ := congr_fun (f‚ÇÅ.1.naturality f.op) x
    have h‚ÇÇ := congr_fun (f‚ÇÇ.1.naturality f.op) x
    dsimp at h‚ÇÅ h‚ÇÇ
    simp only [‚Üê h‚ÇÅ, ‚Üê h‚ÇÇ, ‚Üê hy]
    exact congr_fun (congr_app (congr_arg Sheaf.Hom.val h) (op Y)) y

lemma isIso_iff_bijective :
    IsIso œÜ ‚Üî ‚àÄ (X : C·µí·µñ), Function.Bijective (fun (x : F.1.obj X) => œÜ.1.app _ x) := by
  have : IsIso œÜ ‚Üî IsIso œÜ.1 := by
    change _ ‚Üî IsIso ((sheafToPresheaf _ _).map œÜ)
    constructor
    ¬∑ intro
      infer_instance
    ¬∑ intro
      exact isIso_of_reflects_iso œÜ (sheafToPresheaf _ _)
  rw [this]
  constructor
  ¬∑ intro _ X
    rw [‚Üê CategoryTheory.isIso_iff_bijective]
    change IsIso (((evaluation _ _).obj X).map œÜ.1)
    infer_instance
  ¬∑ intro hœÜ
    have : ‚àÄ (X : C·µí·µñ), IsIso (œÜ.1.app X) := by
      simpa only [CategoryTheory.isIso_iff_bijective] using hœÜ
    apply NatIso.isIso_of_isIso_app

namespace EpiMonoFactorization

/-- The underlying presheaf of the image of a sheaf of sets, which consists of sections
of the target sheaf which can be locally lifted to the source sheaf. -/
@[simps]
def presheafI : C·µí·µñ ‚•§ Type w where
  obj X := { x : G.1.obj X | ‚àÉ (S : Sieve X.unop) (_ : S ‚àà J X.unop),
    ‚àÄ (Y : C) (f : Y ‚ü∂ X.unop) (_ : S f), ‚àÉ (y : F.1.obj (op Y)),
      œÜ.1.app _ y = G.1.map f.op x }
  map {X X'} g a := ‚ü®G.1.map g a.1, by
    obtain ‚ü®S, hS, h‚ü© := a.2
    refine' ‚ü®S.pullback g.unop, J.pullback_stable _ hS, fun Y f hf => _‚ü©
    obtain ‚ü®y, hy‚ü© := h Y (f ‚â´ g.unop) hf
    exact ‚ü®y, by simp [hy]‚ü©‚ü©

/-- The inclusion of the image of a morphism of sheaves of sets, as a morpshim of presheaves. -/
@[simps]
def presheafŒπ : presheafI œÜ ‚ü∂ G.1 where
  app _ x := x.1
  naturality _ _ _ := rfl

/-- The image of a morphism of sheaves of sets. -/
@[simps]
def I : Sheaf J (Type w) := ‚ü®presheafI œÜ, by
  rw [isSheaf_iff_isSheaf_of_type]
  intro X S hS Œ± hŒ±
  have hS' := (((isSheaf_iff_isSheaf_of_type _ _).1 G.2) _ hS)
  refine' ‚ü®‚ü®hS'.amalgamate _
    (hŒ±.compPresheafMap (presheafŒπ œÜ)), _‚ü©, _, _‚ü©
  ¬∑ let U := fun ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ (hf : S.arrows f) => (Œ± f hf).2.choose
    have hU : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ (hf : S.arrows f), U hf ‚àà J _:= fun Y f hf =>
        (Œ± f hf).2.choose_spec.choose
    refine' ‚ü®_, J.bind_covering hS hU, fun Y f hf => _‚ü©
    obtain ‚ü®T, a, b, hb, ha : U hb a, fac‚ü© := hf
    obtain ‚ü®y, hy‚ü© := (Œ± _ hb).2.choose_spec.choose_spec _ a ha
    refine' ‚ü®y, _‚ü©
    have hf : S.arrows f := by
      rw [‚Üê fac]
      apply S.downward_closed hb
    rw [hy, Presieve.IsSheafFor.valid_glue hS' (hŒ±.compPresheafMap (presheafŒπ œÜ)) f hf]
    simpa using (hŒ±.compPresheafMap (presheafŒπ œÜ)) a (ùüô _) hb hf (by simpa using fac)
  ¬∑ intro Y f hf
    apply Subtype.ext
    apply Presieve.IsSheafFor.valid_glue hS' (hŒ±.compPresheafMap (presheafŒπ œÜ))
  ¬∑ rintro ‚ü®y, _‚ü© hy
    apply Subtype.ext
    apply ((Presieve.isSeparated_of_isSheaf _ _
      ((isSheaf_iff_isSheaf_of_type _ _).1 G.2)) S hS).ext
    intro Y f hf
    dsimp
    replace hy := hy f hf
    rw [Subtype.ext_iff] at hy
    dsimp at hy
    rw [hy]
    symm
    apply Presieve.IsSheafFor.valid_glue‚ü©

/-- The inclusion of the image of a morphism of sheaves of sets. -/
@[simps]
def Œπ : I œÜ ‚ü∂ G := Sheaf.Hom.mk (presheafŒπ œÜ)

/-- The projection to the image of a morphism of sheaves of sets. -/
@[simps]
def œÄ : F ‚ü∂ I œÜ where
  val :=
    { app := fun X x => ‚ü®œÜ.1.app X x, ‚ü®‚ä§, J.top_mem X.unop, fun Y f _ =>
        ‚ü®F.1.map f.op x, congr_fun (œÜ.val.naturality f.op) x‚ü©‚ü©‚ü©
      naturality := fun X X' g => by
        ext x
        exact Subtype.ext (congr_fun (œÜ.val.naturality g) x) }

instance : Epi (œÄ œÜ) := by
  apply epi_of_locally_surjective
  intro X x
  obtain ‚ü®S, hS, hS'‚ü© := x.2
  refine' ‚ü®S, hS, fun Y f hf => _‚ü©
  obtain ‚ü®y, hy‚ü© := hS' Y f hf
  exact ‚ü®y, Subtype.ext hy‚ü©

instance : Mono (Œπ œÜ) := by
  apply mono_of_injective
  intro X x‚ÇÅ x‚ÇÇ h
  exact Subtype.ext h

@[reassoc (attr := simp)]
lemma œÄ_Œπ : œÄ œÜ ‚â´ Œπ œÜ = œÜ := rfl

instance [Epi œÜ] : Epi (Œπ œÜ) := epi_of_epi_fac (œÄ_Œπ œÜ)

end EpiMonoFactorization

namespace BalancedAux

/-- If a commutative square in the category of sets is pushout square, and the top map
is injective, then the square is also pullback square,  -/
noncomputable def isLimit_of_isPushout_of_injective {X Y S : Type w} {f : X ‚ü∂ S} {g : Y ‚ü∂ S}
    (c : PullbackCone f g) (hc : IsPushout c.fst c.snd f g)
    (h‚ÇÅ : Function.Injective c.fst) :
        IsLimit c := by
  let œÜ : c.pt ‚ü∂ pullback f g := pullback.lift c.fst c.snd c.condition
  have hœÜ : IsIso œÜ := by
    rw [CategoryTheory.isIso_iff_bijective]
    constructor
    ¬∑ intro x‚ÇÅ x‚ÇÇ h
      apply h‚ÇÅ
      have : c.fst = œÜ ‚â´ pullback.fst := by simp
      rw [this, types_comp_apply, types_comp_apply, h]
    ¬∑ intro t
      obtain ‚ü®x, hx‚ÇÅ, hx‚ÇÇ‚ü© := (Types.inl_eq_inr_iff_of_isColimit hc.isColimit h‚ÇÅ
        (@pullback.fst _ _ _ _ _ f g _ t)
        (@pullback.snd _ _ _ _ _ f g _ t)).1 (by
          dsimp
          rw [‚Üê types_comp_apply (pullback.fst : pullback f g ‚ü∂ _) f,
            ‚Üê types_comp_apply (pullback.snd : pullback f g ‚ü∂ _) g, pullback.condition])
      refine' ‚ü®x, _‚ü©
      apply (Types.pullbackIsoPullback f g).toEquiv.injective
      ext
      ¬∑ rw [Iso.toEquiv_fun, Types.pullbackIsoPullback_hom_fst,
          ‚Üê types_comp_apply œÜ pullback.fst, pullback.lift_fst, hx‚ÇÅ,
          Types.pullbackIsoPullback_hom_fst]
      ¬∑ rw [Iso.toEquiv_fun, Types.pullbackIsoPullback_hom_snd,
          ‚Üê types_comp_apply œÜ pullback.snd, pullback.lift_snd, hx‚ÇÇ,
          Types.pullbackIsoPullback_hom_snd]
  exact IsLimit.ofIsoLimit (pullbackIsPullback _ _)
    (Iso.symm (PullbackCone.ext (asIso œÜ) (by simp) (by simp)))

end BalancedAux

-- SGA 4 II 4.2
instance [HasSheafify J (Type w)] : Balanced (Sheaf J (Type w)) where
  isIso_of_mono_of_epi {F G} œÜ _ _ := by
    have e : Arrow.mk œÜ ‚âÖ ((presheafToSheaf J _).map œÜ.1) :=
      Arrow.isoOfNatIso (sheafificationNatIso J (Type w)) (Arrow.mk œÜ)
    let c‚ÇÅ : PushoutCocone œÜ.1 œÜ.1 := PushoutCocone.mk _ _ pushout.condition
    have h‚ÇÅ : IsColimit c‚ÇÅ := pushoutIsPushout œÜ.1 œÜ.1
    let c‚ÇÇ := PullbackCone.mk _ _ c‚ÇÅ.condition
    have h‚ÇÇ : IsLimit c‚ÇÇ := by
      apply evaluationJointlyReflectsLimits
      intro X
      apply (isLimitMapConePullbackConeEquiv _ _).2
      apply BalancedAux.isLimit_of_isPushout_of_injective
      ¬∑ exact IsPushout.of_isColimit
          (isColimitPushoutCoconeMapOfIsColimit ((evaluation C·µí·µñ (Type w)).obj X) _ h‚ÇÅ)
      ¬∑ exact (mono_iff_injective œÜ).1 inferInstance X
    have fac := (presheafToSheaf J _).congr_map c‚ÇÅ.condition
    simp only [Functor.map_comp] at fac
    let c‚ÇÉ := PushoutCocone.mk _ _ fac
    have h‚ÇÉ : IsColimit c‚ÇÉ :=
      isColimitPushoutCoconeMapOfIsColimit (presheafToSheaf J (Type w)) _ h‚ÇÅ
    let c‚ÇÑ := PullbackCone.mk _ _ fac
    have h‚ÇÑ : IsLimit c‚ÇÑ := isLimitPullbackConeMapOfIsLimit (presheafToSheaf J (Type w)) _ h‚ÇÇ
    have : Epi ((presheafToSheaf J _).map œÜ.1) :=
      ((MorphismProperty.RespectsIso.epimorphisms _).arrow_mk_iso_iff e).1 (by simpa)
    have : IsIso ((presheafToSheaf J _).map œÜ.1) :=
      (MorphismProperty.StableUnderBaseChange.isomorphisms (Sheaf J (Type w)))
        (IsPullback.of_isLimit h‚ÇÑ) ((epi_iff_isIso_inl h‚ÇÉ).1 inferInstance)
    exact ((MorphismProperty.RespectsIso.isomorphisms _).arrow_mk_iso_iff e).2 this

lemma epi_iff_locally_surjective [HasSheafify J (Type w)] :
    Epi œÜ ‚Üî (‚àÄ (X : C·µí·µñ) (x : G.1.obj X),
    ‚àÉ (S : Sieve X.unop) (_ : S ‚àà J X.unop),
    ‚àÄ (Y : C) (f : Y ‚ü∂ X.unop) (_ : S f), ‚àÉ (y : F.1.obj (op Y)),
    œÜ.1.app _ y = G.1.map f.op x) := by
  constructor
  ¬∑ intro hœÜ
    have : IsIso (EpiMonoFactorization.Œπ œÜ) := isIso_of_mono_of_epi _
    intro X x
    obtain ‚ü®‚ü®y, hy‚ü©, rfl‚ü© :=
      ((isIso_iff_bijective (EpiMonoFactorization.Œπ œÜ)).1 inferInstance X).2 x
    exact hy
  ¬∑ intro hœÜ
    exact epi_of_locally_surjective œÜ hœÜ

end Sheaf

end CategoryTheory
