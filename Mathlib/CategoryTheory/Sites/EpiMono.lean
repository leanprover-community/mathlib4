/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Limits.EpiMono
import Mathlib.CategoryTheory.MorphismProperty
import Mathlib.CategoryTheory.Sites.Sheafification
import Mathlib.CategoryTheory.Sites.Whiskering
/-!
# Characterization of mono and epi in the category of sheaves of types

In this file, we obtain the lemmas `Sheaf.mono_iff_injective`, `Sheaf.isIso_iff_bijective`
and `Sheaf.epi_iff_locally_surjective` which are concrete characterizations of monomorphisms,
isomorphisms and epimorphisms in a category of sheaves of types for a Grothendieck
topology `J` on a category `C`.

Given a morphism `œÜ : F ‚ü∂ G` in `Sheaf J (Type _)`, it is easy to show that it is
a mono (resp. an iso) iff for all `X : C·µí·µñ`, the map `œÜ.val.app X : F.val.obj X ‚ü∂ G.val.obj X`
is injective (resp. bijective), similarly as for morphisms of presheaves. We also
obtain a characterization of epimorphism : `œÜ` is an epimorphism iff any section of
`G` can be *locally* lifted to a section of `F`.

The proof of the characterization of epimorphisms uses an epi/mono factorization of
`œÜ : F ‚ü∂ G` (see `Sheaf.EpiMonoFactorization.œÄ_Œπ`) in order to reduce to the particular
case when `œÜ` is also a monomorphism, and for this, we show that the category of
sheaves of types is balanced: `œÜ` is an isomorphism iff it is a mono and an epi.
The proof of this last fact is obtained following the argument in SGA 4 II 4.2.

-/

universe w v u

namespace CategoryTheory

open Opposite Limits

namespace Sheaf

variable {C : Type u} [Category.{v} C] {J : GrothendieckTopology C}

attribute [local instance] ConcreteCategory.hasCoeToSort ConcreteCategory.instFunLike

section

variable {D : Type*} [Category D] [ConcreteCategory D] {F G : Sheaf J D} (œÜ : F ‚ü∂ G)

structure LocallySurjective : Prop where
  exists_local_lifting {X : C·µí·µñ} (x : G.1.obj X) :
    ‚àÉ (S : Sieve X.unop) (_ : S ‚àà J X.unop),
      ‚àÄ {Y : C} (f : Y ‚ü∂ X.unop) (_ : S f), ‚àÉ (y : F.1.obj (op Y)),
        œÜ.1.app (op Y) y = G.1.map f.op x

namespace LocallySurjective

variable {œÜ}
variable (h : LocallySurjective œÜ) {X : C·µí·µñ} (x : G.1.obj X)

noncomputable def liftingSieve : Sieve X.unop := (h.exists_local_lifting x).choose

lemma liftingSieve_mem : h.liftingSieve x ‚àà J X.unop :=
  (h.exists_local_lifting x).choose_spec.choose

noncomputable def lifting {Y : C} (f : Y ‚ü∂ X.unop) (hf : h.liftingSieve x f) :
    F.1.obj (op Y) :=
  ((h.exists_local_lifting x).choose_spec.choose_spec f hf).choose

@[simp]
lemma app_apply_lifting {Y : C} (f : Y ‚ü∂ X.unop) (hf : h.liftingSieve x f) :
    œÜ.1.app (op Y) (h.lifting x f hf) = G.1.map f.op x :=
  ((h.exists_local_lifting x).choose_spec.choose_spec f hf).choose_spec

lemma epi' {F G : Sheaf J (Type w)} {œÜ : F ‚ü∂ G} (h : LocallySurjective œÜ) : Epi œÜ where
  left_cancellation := by
    intro H f‚ÇÅ f‚ÇÇ h‚ÇÅ‚ÇÇ
    ext X x
    apply ((Presieve.isSeparated_of_isSheaf _ _
      ((isSheaf_iff_isSheaf_of_type _ _).1 H.2)) _ (h.liftingSieve_mem x)).ext
    intro Y f hf
    have h‚ÇÅ := congr_fun (f‚ÇÅ.1.naturality f.op) x
    have h‚ÇÇ := congr_fun (f‚ÇÇ.1.naturality f.op) x
    dsimp at h‚ÇÅ h‚ÇÇ
    simp only [‚Üê h‚ÇÅ, ‚Üê h‚ÇÇ]
    erw [congr_arg (f‚ÇÅ.val.app (op Y)) (h.app_apply_lifting x f hf).symm,
      congr_arg (f‚ÇÇ.val.app (op Y)) (h.app_apply_lifting x f hf).symm]
    exact congr_fun (congr_app (congr_arg Sheaf.Hom.val h‚ÇÅ‚ÇÇ) (op Y)) _

variable [J.HasSheafCompose (forget D)]

lemma sheafCompose_forget : LocallySurjective ((sheafCompose J (forget D)).map œÜ) where
  exists_local_lifting x := ‚ü®h.liftingSieve x, h.liftingSieve_mem x, fun f hf =>
    ‚ü®h.lifting x f hf, h.app_apply_lifting x f hf‚ü©‚ü©

instance : Faithful (sheafCompose J (forget D)) where
  map_injective {F G f‚ÇÅ f‚ÇÇ} h := by
    ext X x
    exact congr_fun (congr_app ((sheafToPresheaf _ _).congr_map h) X) x

lemma epi : Epi œÜ :=
  (sheafCompose J (forget D)).epi_of_epi_map (h.sheafCompose_forget.epi')

end LocallySurjective

end

variable {F G : Sheaf J (Type w)} (œÜ : F ‚ü∂ G)

lemma mono_of_injective
    (hœÜ : ‚àÄ (X : C·µí·µñ), Function.Injective (fun (x : F.1.obj X) => œÜ.1.app _ x)) : Mono œÜ where
  right_cancellation := by
    intro H f‚ÇÅ f‚ÇÇ h
    ext Z x
    exact hœÜ Z (congr_fun (congr_app (congr_arg Sheaf.Hom.val h) Z) x)

lemma mono_iff_injective [HasWeakSheafify J (Type w)] :
    Mono œÜ ‚Üî ‚àÄ (X : C·µí·µñ), Function.Injective (fun (x : F.1.obj X) => œÜ.1.app _ x) := by
  constructor
  ¬∑ intro hœÜ X
    simp only [‚Üê CategoryTheory.mono_iff_injective]
    change Mono (((evaluation _ _).obj X).map ((sheafToPresheaf _ _).map œÜ))
    infer_instance
  ¬∑ intro hœÜ
    exact mono_of_injective œÜ hœÜ

lemma epi_of_locally_surjective (h : LocallySurjective œÜ) : Epi œÜ := h.epi

lemma isIso_iff_bijective :
    IsIso œÜ ‚Üî ‚àÄ (X : C·µí·µñ), Function.Bijective (fun (x : F.1.obj X) => œÜ.1.app _ x) := by
  have : IsIso œÜ ‚Üî IsIso œÜ.1 := by
    change _ ‚Üî IsIso ((sheafToPresheaf _ _).map œÜ)
    constructor
    ¬∑ intro
      infer_instance
    ¬∑ intro
      exact isIso_of_reflects_iso œÜ (sheafToPresheaf _ _)
  rw [this]
  constructor
  ¬∑ intro _ X
    rw [‚Üê CategoryTheory.isIso_iff_bijective]
    change IsIso (((evaluation _ _).obj X).map œÜ.1)
    infer_instance
  ¬∑ intro hœÜ
    have : ‚àÄ (X : C·µí·µñ), IsIso (œÜ.1.app X) := by
      simpa only [CategoryTheory.isIso_iff_bijective] using hœÜ
    apply NatIso.isIso_of_isIso_app

namespace EpiMonoFactorization

/-- The underlying presheaf of the image of a sheaf of sets, which consists of sections
of the target sheaf which can be locally lifted to the source sheaf. -/
@[simps]
def presheafI : C·µí·µñ ‚•§ Type w where
  obj X := { x : G.1.obj X | ‚àÉ (S : Sieve X.unop) (_ : S ‚àà J X.unop),
    ‚àÄ ‚¶ÉY : C‚¶Ñ (f : Y ‚ü∂ X.unop) (_ : S f), ‚àÉ (y : F.1.obj (op Y)),
      œÜ.1.app _ y = G.1.map f.op x }
  map {X X'} g a := ‚ü®G.1.map g a.1, by
    obtain ‚ü®S, hS, h‚ü© := a.2
    refine' ‚ü®S.pullback g.unop, J.pullback_stable _ hS, fun Y f hf => _‚ü©
    obtain ‚ü®y, hy‚ü© := h (f ‚â´ g.unop) hf
    exact ‚ü®y, by simp [hy]‚ü©‚ü©

/-- The inclusion of the image of a morphism of sheaves of sets, as a morpshim of presheaves. -/
@[simps]
def presheafŒπ : presheafI œÜ ‚ü∂ G.1 where
  app _ x := x.1
  naturality _ _ _ := rfl

/-- The image of a morphism of sheaves of sets. -/
@[simps]
def I : Sheaf J (Type w) := ‚ü®presheafI œÜ, by
  rw [isSheaf_iff_isSheaf_of_type]
  intro X S hS Œ± hŒ±
  have hS' := (((isSheaf_iff_isSheaf_of_type _ _).1 G.2) _ hS)
  refine' ‚ü®‚ü®hS'.amalgamate _
    (hŒ±.compPresheafMap (presheafŒπ œÜ)), _‚ü©, _, _‚ü©
  ¬∑ let U := fun ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ (hf : S.arrows f) => (Œ± f hf).2.choose
    have hU : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ (hf : S.arrows f), U hf ‚àà J _:= fun Y f hf =>
        (Œ± f hf).2.choose_spec.choose
    refine' ‚ü®_, J.bind_covering hS hU, fun Y f hf => _‚ü©
    obtain ‚ü®T, a, b, hb, ha : U hb a, fac‚ü© := hf
    obtain ‚ü®y, hy‚ü© := (Œ± _ hb).2.choose_spec.choose_spec _ ha
    refine' ‚ü®y, _‚ü©
    have hf : S.arrows f := by
      rw [‚Üê fac]
      apply S.downward_closed hb
    rw [hy, Presieve.IsSheafFor.valid_glue hS' (hŒ±.compPresheafMap (presheafŒπ œÜ)) f hf]
    simpa using (hŒ±.compPresheafMap (presheafŒπ œÜ)) a (ùüô _) hb hf (by simpa using fac)
  ¬∑ intro Y f hf
    apply Subtype.ext
    apply Presieve.IsSheafFor.valid_glue hS' (hŒ±.compPresheafMap (presheafŒπ œÜ))
  ¬∑ rintro ‚ü®y, _‚ü© hy
    apply Subtype.ext
    apply ((Presieve.isSeparated_of_isSheaf _ _
      ((isSheaf_iff_isSheaf_of_type _ _).1 G.2)) S hS).ext
    intro Y f hf
    dsimp
    replace hy := hy f hf
    rw [Subtype.ext_iff] at hy
    dsimp at hy
    rw [hy]
    symm
    apply Presieve.IsSheafFor.valid_glue‚ü©

/-- The inclusion of the image of a morphism of sheaves of sets. -/
@[simps]
def Œπ : I œÜ ‚ü∂ G := Sheaf.Hom.mk (presheafŒπ œÜ)

/-- The projection to the image of a morphism of sheaves of sets. -/
@[simps]
def œÄ : F ‚ü∂ I œÜ where
  val :=
    { app := fun X x => ‚ü®œÜ.1.app X x, ‚ü®‚ä§, J.top_mem X.unop, fun Y f _ =>
        ‚ü®F.1.map f.op x, congr_fun (œÜ.val.naturality f.op) x‚ü©‚ü©‚ü©
      naturality := fun X X' g => by
        ext x
        exact Subtype.ext (congr_fun (œÜ.val.naturality g) x) }

lemma locallySurjective_œÄ : LocallySurjective (œÄ œÜ) where
  exists_local_lifting  x := by
    obtain ‚ü®S, hS, hS'‚ü© := x.2
    refine ‚ü®S, hS, fun f hf => ?_‚ü©
    obtain ‚ü®y, hy‚ü© := hS' f hf
    exact ‚ü®y, Subtype.ext hy‚ü©

instance : Epi (œÄ œÜ) := (locallySurjective_œÄ œÜ).epi

instance : Mono (Œπ œÜ) := by
  apply mono_of_injective
  intro X x‚ÇÅ x‚ÇÇ h
  exact Subtype.ext h

@[reassoc (attr := simp)]
lemma œÄ_Œπ : œÄ œÜ ‚â´ Œπ œÜ = œÜ := rfl

instance [Epi œÜ] : Epi (Œπ œÜ) := epi_of_epi_fac (œÄ_Œπ œÜ)

end EpiMonoFactorization

namespace BalancedAux

/-- If a commutative square in the category of sets is pushout square, and the top map
is injective, then the square is also pullback square,  -/
noncomputable def isLimit_of_isPushout_of_injective {X Y S : Type w} {f : X ‚ü∂ S} {g : Y ‚ü∂ S}
    (c : PullbackCone f g) (hc : IsPushout c.fst c.snd f g)
    (h‚ÇÅ : Function.Injective c.fst) :
        IsLimit c := by
  let œÜ : c.pt ‚ü∂ pullback f g := pullback.lift c.fst c.snd c.condition
  have hœÜ : IsIso œÜ := by
    rw [CategoryTheory.isIso_iff_bijective]
    constructor
    ¬∑ intro x‚ÇÅ x‚ÇÇ h
      apply h‚ÇÅ
      have : c.fst = œÜ ‚â´ pullback.fst := by simp
      rw [this, types_comp_apply, types_comp_apply, h]
    ¬∑ intro t
      obtain ‚ü®x, hx‚ÇÅ, hx‚ÇÇ‚ü© := (Types.pushoutCocone_inl_eq_inr_iff_of_isColimit hc.isColimit h‚ÇÅ
        (@pullback.fst _ _ _ _ _ f g _ t)
        (@pullback.snd _ _ _ _ _ f g _ t)).1 (by
          dsimp
          rw [‚Üê types_comp_apply (pullback.fst : pullback f g ‚ü∂ _) f,
            ‚Üê types_comp_apply (pullback.snd : pullback f g ‚ü∂ _) g, pullback.condition])
      refine' ‚ü®x, _‚ü©
      apply (Types.pullbackIsoPullback f g).toEquiv.injective
      ext
      ¬∑ rw [Iso.toEquiv_fun, Types.pullbackIsoPullback_hom_fst,
          ‚Üê types_comp_apply œÜ pullback.fst, pullback.lift_fst, hx‚ÇÅ,
          Types.pullbackIsoPullback_hom_fst]
      ¬∑ rw [Iso.toEquiv_fun, Types.pullbackIsoPullback_hom_snd,
          ‚Üê types_comp_apply œÜ pullback.snd, pullback.lift_snd, hx‚ÇÇ,
          Types.pullbackIsoPullback_hom_snd]
  exact IsLimit.ofIsoLimit (pullbackIsPullback _ _)
    (Iso.symm (PullbackCone.ext (asIso œÜ) (by simp) (by simp)))

end BalancedAux

instance [HasSheafify J (Type w)] : Balanced (Sheaf J (Type w)) where
  isIso_of_mono_of_epi {F G} œÜ _ _ := by
    -- this is the argument from SGA 4 II 4.2
    let c‚ÇÅ : PushoutCocone œÜ.1 œÜ.1 := PushoutCocone.mk _ _ pushout.condition
    have h‚ÇÅ : IsColimit c‚ÇÅ := pushoutIsPushout œÜ.1 œÜ.1
    let c‚ÇÇ := PullbackCone.mk _ _ c‚ÇÅ.condition
    have h‚ÇÇ : IsLimit c‚ÇÇ := by
      apply evaluationJointlyReflectsLimits
      intro X
      apply (isLimitMapConePullbackConeEquiv _ _).2
      apply BalancedAux.isLimit_of_isPushout_of_injective
      ¬∑ exact IsPushout.of_isColimit
          (isColimitPushoutCoconeMapOfIsColimit ((evaluation C·µí·µñ (Type w)).obj X) _ h‚ÇÅ)
      ¬∑ exact (mono_iff_injective œÜ).1 inferInstance X
    have h‚ÇÅ' := isColimitPushoutCoconeMapOfIsColimit (presheafToSheaf J (Type w)) _ h‚ÇÅ
    have h‚ÇÇ' := isLimitPullbackConeMapOfIsLimit (presheafToSheaf J (Type w)) _ h‚ÇÇ
    have e : Arrow.mk œÜ ‚âÖ ((presheafToSheaf J _).map œÜ.1) :=
      Arrow.isoOfNatIso (sheafificationNatIso J (Type w)) (Arrow.mk œÜ)
    have : Epi ((presheafToSheaf J _).map œÜ.1) :=
      ((MorphismProperty.RespectsIso.epimorphisms _).arrow_mk_iso_iff e).1 (by simpa)
    have : IsIso ((presheafToSheaf J _).map œÜ.1) :=
      (MorphismProperty.StableUnderBaseChange.isomorphisms (Sheaf J (Type w)))
        (IsPullback.of_isLimit h‚ÇÇ') ((epi_iff_isIso_inl h‚ÇÅ').1 inferInstance)
    exact ((MorphismProperty.RespectsIso.isomorphisms _).arrow_mk_iso_iff e).2 this

lemma epi_iff_locally_surjective [HasSheafify J (Type w)] :
    Epi œÜ ‚Üî LocallySurjective œÜ := by
  constructor
  ¬∑ intro hœÜ
    constructor
    have : IsIso (EpiMonoFactorization.Œπ œÜ) := isIso_of_mono_of_epi _
    intro X x
    obtain ‚ü®‚ü®y, hy‚ü©, rfl‚ü© :=
      ((isIso_iff_bijective (EpiMonoFactorization.Œπ œÜ)).1 inferInstance X).2 x
    exact hy
  ¬∑ intro hœÜ
    exact epi_of_locally_surjective œÜ hœÜ

end Sheaf

end CategoryTheory
