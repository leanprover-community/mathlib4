/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Localization.Adjunction
import Mathlib.CategoryTheory.Sites.Sheafification

/-! The sheaf category as a localized category

In this file, it is shown that the category of sheaves `Sheaf J A` is a localization
of the category `Presheaf J A` with respect to the class `J.W` of morphisms
of presheaves which become isomorphisms after applying the sheafification functor.

-/

namespace CategoryTheory

open Category

variable {C : Type*} [Category C] (J : GrothendieckTopology C) {A : Type*} [Category A]

namespace GrothendieckTopology

/-- The class of morphisms of presheaves which become isomorphisms after sheafification.
(See `GrothendieckTopology.W_iff`.) -/
@[pp_dot]
def W : MorphismProperty (C·µí·µñ ‚•§ A) := fun _ P‚ÇÇ f =>
  ‚àÄ Q, Presheaf.IsSheaf J Q ‚Üí Function.Bijective (fun (g : P‚ÇÇ ‚ü∂ Q) => f ‚â´ g)

instance : (W (A := A) J).ContainsIdentities where
  id_mem' P Q _ := by
    simp only [id_comp]
    exact Function.bijective_id

instance : (W (A := A) J).IsMultiplicative where
  stableUnderComposition P‚ÇÅ P‚ÇÇ P‚ÇÉ f g hf hg Q hQ := by
    simpa using Function.Bijective.comp (hf Q hQ) (hg Q hQ)

lemma W_postcomp_iff {P‚ÇÅ P‚ÇÇ P‚ÇÉ : C·µí·µñ ‚•§ A} (f : P‚ÇÅ ‚ü∂ P‚ÇÇ) (g : P‚ÇÇ ‚ü∂ P‚ÇÉ) (hg : J.W g) :
    J.W (f ‚â´ g) ‚Üî J.W f := by
  constructor
  ¬∑ intro hfg Q hQ
    exact (Function.Bijective.of_comp_iff _ (hg Q hQ)).1 (by simpa using hfg Q hQ)
  ¬∑ intro hf
    exact J.W.comp_mem _ _ hf hg

lemma W_precomp_iff {P‚ÇÅ P‚ÇÇ P‚ÇÉ : C·µí·µñ ‚•§ A} (f : P‚ÇÅ ‚ü∂ P‚ÇÇ) (g : P‚ÇÇ ‚ü∂ P‚ÇÉ) (hf : J.W f) :
    J.W (f ‚â´ g) ‚Üî J.W g := by
  constructor
  ¬∑ intro hfg Q hQ
    exact (Function.Bijective.of_comp_iff' (hf Q hQ) _).1 (by simpa using hfg Q hQ)
  ¬∑ intro hg
    exact J.W.comp_mem _ _ hf hg

section

variable {P‚ÇÅ P‚ÇÇ : C·µí·µñ ‚•§ A} (f : P‚ÇÅ ‚ü∂ P‚ÇÇ)

lemma W_of_isIso [IsIso f] : J.W f := fun Q _ => by
  constructor
  ¬∑ intro _ _ h
    simpa only [‚Üê cancel_epi f] using h
  ¬∑ intro g
    exact ‚ü®inv f ‚â´ g, by simp‚ü©

lemma W_iff_isIso (hP‚ÇÅ : Presheaf.IsSheaf J P‚ÇÅ) (hP‚ÇÇ : Presheaf.IsSheaf J P‚ÇÇ) :
    J.W f ‚Üî IsIso f := by
  constructor
  ¬∑ intro hf
    obtain ‚ü®g, hg‚ü© := (hf _ hP‚ÇÅ).2 (ùüô _)
    dsimp at hg
    exact ‚ü®g, hg, (hf _ hP‚ÇÇ).1 (by simp only [reassoc_of% hg, comp_id])‚ü©
  ¬∑ intro
    exact W_of_isIso J f

end

section

variable {G : (C·µí·µñ ‚•§ A) ‚•§ Sheaf J A} (adj : G ‚ä£ sheafToPresheaf J A)

lemma W_adj_unit_app (P : C·µí·µñ ‚•§ A) : J.W (adj.unit.app P) := fun Q hQ => by
  let e := equivOfFullyFaithful (F := sheafToPresheaf J A).symm.trans (adj.homEquiv P ‚ü®Q, hQ‚ü©)
  convert e.bijective
  ext1 f
  dsimp [e]
  obtain ‚ü®g, rfl‚ü© : ‚àÉ (g : G.obj P ‚ü∂ ‚ü®Q, hQ‚ü©),
    f = (sheafToPresheaf J A).map g := ‚ü®‚ü®f‚ü©, rfl‚ü©
  erw [equivOfFullyFaithful_symm_apply]
  rw [Functor.preimage_map]
  erw [Adjunction.homEquiv_unit]

lemma W_iff_isIso_map_of_adjunction {P‚ÇÅ P‚ÇÇ : C·µí·µñ ‚•§ A} (f : P‚ÇÅ ‚ü∂ P‚ÇÇ) :
    J.W f ‚Üî IsIso (G.map f) := by
  rw [‚Üê W_postcomp_iff J f _ (J.W_adj_unit_app adj P‚ÇÇ)]
  erw [adj.unit.naturality f]
  rw [W_precomp_iff J _ _ (J.W_adj_unit_app adj P‚ÇÅ),
    J.W_iff_isIso _ (G.obj P‚ÇÅ).cond (G.obj P‚ÇÇ).cond]
  dsimp
  constructor
  ¬∑ intro (h : IsIso ((sheafToPresheaf J A).map (G.map f)))
    exact isIso_of_reflects_iso _ (sheafToPresheaf J A)
  ¬∑ intro
    change IsIso ((sheafToPresheaf J A).map (G.map f))
    infer_instance

lemma W_eq_inverseImage_isomorphisms_of_adjunction :
    J.W = (MorphismProperty.isomorphisms _).inverseImage G := by
  ext P‚ÇÅ P‚ÇÇ f
  rw [J.W_iff_isIso_map_of_adjunction adj]
  rfl

lemma isLocalization_of_adjunction : G.IsLocalization J.W := by
  rw [J.W_eq_inverseImage_isomorphisms_of_adjunction adj]
  exact adj.isLocalization

end

section

variable [HasWeakSheafify J A]

lemma W_toSheafify (P : C·µí·µñ ‚•§ A) : J.W (toSheafify J P) :=
  J.W_adj_unit_app (sheafificationAdjunction J A) P

lemma W_iff {P‚ÇÅ P‚ÇÇ : C·µí·µñ ‚•§ A} (f : P‚ÇÅ ‚ü∂ P‚ÇÇ) :
    J.W f ‚Üî IsIso ((presheafToSheaf J A).map f) :=
  J.W_iff_isIso_map_of_adjunction (sheafificationAdjunction J A) f

variable (A)

lemma W_eq_inverseImage_isomorphisms :
    J.W = (MorphismProperty.isomorphisms _).inverseImage (presheafToSheaf J A) := by
  ext P‚ÇÅ P‚ÇÇ f
  rw [W_iff]
  rfl

instance : (presheafToSheaf J A).IsLocalization J.W := by
  rw [W_eq_inverseImage_isomorphisms]
  exact (sheafificationAdjunction J A).isLocalization

end

end GrothendieckTopology

end CategoryTheory
