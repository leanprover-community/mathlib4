/-
Copyright (c) 2024 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.CategoryTheory.Sites.Coherent.ReflectsPrecoherent
import Mathlib.CategoryTheory.Sites.Coherent.ReflectsPreregular
import Mathlib.CategoryTheory.Sites.Equivalence
/-!

# Coherence and equivalence of categories

This file proves that the coherent and regular topologiesÂ transfer nicely along equivalences of
categories.
-/

namespace CategoryTheory

variable {C : Type*} [Category C]

open GrothendieckTopology

namespace Equivalence

variable {D : Type*} [Category D]
variable (e : C â‰Œ D)

section Coherent

variable [Precoherent C]

/-- `Precoherent`Â is preserved by equivalence of categories. -/
theorem precoherent : Precoherent D := e.inverse.reflects_precoherent

instance [EssentiallySmall C] :
    Precoherent (SmallModel C) := (equivSmallModel C).precoherent

/--
Transferring the coherent topology along an equivalence of categories gives the coherent topology.
-/
theorem precoherent_eq : haveI := precoherent e
    (e.locallyCoverDense (coherentTopology C)).inducedTopology =
    coherentTopology D := by
  ext _ S
  haveI := precoherent e
  simp only [LocallyCoverDense.inducedTopology]
  change (Sieve.functorPushforward e.inverse S) âˆˆ sieves _ _ â†” _
  simp only [coherentTopology.mem_sieves_iff_hasEffectiveEpiFamily]
  constructor
  Â· intro âŸ¨Î±, _, Y, Ï€, _, hâŸ©
    refine âŸ¨Î±, inferInstance, _, fun b â†¦ e.functor.map (Ï€ b) â‰« e.counit.app _, ?_, ?_âŸ©
    Â· have : EffectiveEpiFamily _ fun i â†¦ (e.functor.map (Ï€ i)) :=
        âŸ¨âŸ¨effectiveEpiFamilyStructOfEquivalence e Y Ï€âŸ©âŸ©
      infer_instance
    Â· intro a
      obtain âŸ¨_, _, _, hâ‚, hâ‚‚âŸ© := h a
      simp only [hâ‚‚, Functor.map_comp, fun_inv_map, Functor.comp_obj, Functor.id_obj,
        Category.assoc, Iso.inv_hom_id_app, Category.comp_id]
      rw [â† Category.assoc]
      exact Sieve.downward_closed S hâ‚ _
  Â· intro âŸ¨Î±, _, Y, Ï€, _, hâŸ©
    refine âŸ¨Î±, inferInstance, _, fun b â†¦ e.unitInv.app _ â‰« e.inverse.map (Ï€ b), ?_, ?_âŸ©
    Â· have : EffectiveEpiFamily (fun a â†¦ (ðŸ­ C).obj _) fun i â†¦ (e.inverse.map (Ï€ i)) :=
        âŸ¨âŸ¨effectiveEpiFamilyStructOfEquivalence e.symm Y Ï€âŸ©âŸ©
      infer_instance
    Â· exact fun a â†¦ âŸ¨Y a, Ï€ a, e.unitInv.app _, h a, rflâŸ©

instance : haveI := precoherent e
    e.TransportsGrothendieckTopology (coherentTopology C) (coherentTopology D) where
  eq_inducedTopology := e.precoherent_eq.symm

variable (A : Type*) [Category A]

/--
Equivalent precoherent categories give equivalent coherent toposes.
-/
@[simps!]
def sheafCongrPrecoherent : haveI := e.precoherent
    Sheaf (coherentTopology C) A â‰Œ Sheaf (coherentTopology D) A := e.sheafCongr _ _ _

open Presheaf

/--
The coherent sheaf condition can be checked after precomposing with the equivalence.
-/
theorem precoherent_isSheaf_iff (F : Cáµ’áµ– â¥¤ A) : haveI := e.precoherent
    IsSheaf (coherentTopology C) F â†” IsSheaf (coherentTopology D) (e.inverse.op â‹™ F) := by
  refine âŸ¨fun hF â†¦ ((e.sheafCongrPrecoherent A).functor.obj âŸ¨F, hFâŸ©).cond, fun hF â†¦ ?_âŸ©
  rw [isSheaf_of_iso_iff (P' := e.functor.op â‹™ e.inverse.op â‹™ F)]
  Â· exact (e.sheafCongrPrecoherent A).inverse.obj âŸ¨e.inverse.op â‹™ F, hFâŸ© |>.cond
  Â· exact isoWhiskerRight e.op.unitIso F

/--
The coherent sheaf condition on an essentially small site can be checked after precomposing with
the equivalence with a small category.
-/
theorem precoherent_isSheaf_iff_of_essentiallySmall [EssentiallySmall C] (F : Cáµ’áµ– â¥¤ A) :
    IsSheaf (coherentTopology C) F â†” IsSheaf (coherentTopology (SmallModel C))
    ((equivSmallModel C).inverse.op â‹™ F) := precoherent_isSheaf_iff _ _ _

end Coherent

section Regular

variable [Preregular C]

/-- `Preregular`Â is preserved by equivalence of categories. -/
theorem preregular : Preregular D := e.inverse.reflects_preregular

instance [EssentiallySmall C] :
    Preregular (SmallModel C) := (equivSmallModel C).preregular

/--
Transferring the regular topology along an equivalence of categories gives the regular topology.
-/
theorem preregular_eq : haveI := preregular e
    (e.locallyCoverDense (regularTopology C)).inducedTopology =
    regularTopology D := by
  ext _ S
  haveI := preregular e
  simp only [LocallyCoverDense.inducedTopology]
  change (Sieve.functorPushforward e.inverse S) âˆˆ sieves _ _ â†” _
  simp only [regularTopology.mem_sieves_iff_hasEffectiveEpi]
  constructor
  Â· intro âŸ¨Y, Ï€, _, hâŸ©
    refine âŸ¨_, e.functor.map Ï€ â‰« e.counit.app _, inferInstance, ?_âŸ©
    obtain âŸ¨_, _, _, hâ‚, hâ‚‚âŸ© := h
    simp only [hâ‚‚, Functor.map_comp, fun_inv_map, Functor.comp_obj, Functor.id_obj,
      Category.assoc, Iso.inv_hom_id_app, Category.comp_id]
    rw [â† Category.assoc]
    exact Sieve.downward_closed S hâ‚ _
  Â· intro âŸ¨Y, Ï€, _, hâŸ©
    exact âŸ¨_, e.unitInv.app _ â‰« e.inverse.map Ï€, inferInstance, Y, Ï€, e.unitInv.app _, h, rflâŸ©

instance : haveI := preregular e
    e.TransportsGrothendieckTopology (regularTopology C) (regularTopology D) where
  eq_inducedTopology := e.preregular_eq.symm

variable (A : Type*) [Category A]

/--
Equivalent preregular categories give equivalent regular toposes.
-/
@[simps!]
def sheafCongrPreregular : haveI := e.preregular
    Sheaf (regularTopology C) A â‰Œ Sheaf (regularTopology D) A := e.sheafCongr _ _ _

open Presheaf

/--
The regular sheaf condition can be checked after precomposing with the equivalence.
-/
theorem preregular_isSheaf_iff (F : Cáµ’áµ– â¥¤ A) : haveI := e.preregular
    IsSheaf (regularTopology C) F â†” IsSheaf (regularTopology D) (e.inverse.op â‹™ F) := by
  refine âŸ¨fun hF â†¦ ((e.sheafCongrPreregular A).functor.obj âŸ¨F, hFâŸ©).cond, fun hF â†¦ ?_âŸ©
  rw [isSheaf_of_iso_iff (P' := e.functor.op â‹™ e.inverse.op â‹™ F)]
  Â· exact (e.sheafCongrPreregular A).inverse.obj âŸ¨e.inverse.op â‹™ F, hFâŸ© |>.cond
  Â· exact isoWhiskerRight e.op.unitIso F

/--
The regular sheaf condition on an essentially small site can be checked after precomposing with
the equivalence with a small category.
-/
theorem preregular_isSheaf_iff_of_essentiallySmall [EssentiallySmall C] (F : Cáµ’áµ– â¥¤ A) :
    IsSheaf (regularTopology C) F â†” IsSheaf (regularTopology (SmallModel C))
    ((equivSmallModel C).inverse.op â‹™ F) := preregular_isSheaf_iff _ _ _

end Regular

end Equivalence
