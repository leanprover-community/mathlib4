/-
Copyright (c) 2023 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson, Filippo A. E. Nuccio, Riccardo Brasca
-/
import Mathlib.CategoryTheory.EffectiveEpi.Preserves
import Mathlib.CategoryTheory.Limits.Final.ParallelPair
import Mathlib.CategoryTheory.Preadditive.Projective.Basic
import Mathlib.CategoryTheory.Sites.Canonical
import Mathlib.CategoryTheory.Sites.Coherent.Basic
import Mathlib.CategoryTheory.Sites.EffectiveEpimorphic
/-!

# Sheaves for the regular topology

This file characterises sheaves for the regular topology.

## Main results

* `equalizerCondition_iff_isSheaf`: In a preregular category with pullbacks, the sheaves for the
  regular topology are precisely the presheaves satisfying an equaliser condition with respect to
  effective epimorphisms.

* `isSheaf_of_projective`: In a preregular category in which every object is projective, every
  presheaf is a sheaf for the regular topology.
-/

namespace CategoryTheory

open Limits

variable {C D E : Type*} [Category C] [Category D] [Category E]

open Opposite Presieve Functor

/-- A presieve is *regular* if it consists of a single effective epimorphism. -/
class Presieve.regular {X : C} (R : Presieve X) : Prop where
  /-- `R` consists of a single epimorphism. -/
  single_epi : âˆƒ (Y : C) (f : Y âŸ¶ X), R = Presieve.ofArrows (fun (_ : Unit) â†¦ Y)
    (fun (_ : Unit) â†¦ f) âˆ§ EffectiveEpi f

namespace regularTopology

lemma equalizerCondition_w (P : Cáµ’áµ– â¥¤ D) {X B : C} {Ï€ : X âŸ¶ B} (c : PullbackCone Ï€ Ï€) :
    P.map Ï€.op â‰« P.map c.fst.op = P.map Ï€.op â‰« P.map c.snd.op := by
  simp only [â† Functor.map_comp, â† op_comp, c.condition]

/--
A contravariant functor on `C` satisfies `SingleEqualizerCondition` with respect to a morphism `Ï€`
if it takes its kernel pair to an equalizer diagram.
-/
def SingleEqualizerCondition (P : Cáµ’áµ– â¥¤ D) â¦ƒX B : Câ¦„ (Ï€ : X âŸ¶ B) : Prop :=
  âˆ€ (c : PullbackCone Ï€ Ï€) (_ : IsLimit c),
    Nonempty (IsLimit (Fork.ofÎ¹ (P.map Ï€.op) (equalizerCondition_w P c)))

/--
A contravariant functor on `C` satisfies `EqualizerCondition` if it takes kernel pairs of effective
epimorphisms to equalizer diagrams.
-/
def EqualizerCondition (P : Cáµ’áµ– â¥¤ D) : Prop :=
  âˆ€ â¦ƒX B : Câ¦„ (Ï€ : X âŸ¶ B) [EffectiveEpi Ï€], SingleEqualizerCondition P Ï€

/-- The equalizer condition is preserved by natural isomorphism. -/
theorem equalizerCondition_of_natIso {P P' : Cáµ’áµ– â¥¤ D} (i : P â‰… P')
    (hP : EqualizerCondition P) : EqualizerCondition P' := fun X B Ï€ _ c hc â†¦
  âŸ¨Fork.isLimitOfIsos _ (hP Ï€ c hc).some _ (i.app _) (i.app _) (i.app _)âŸ©

/-- Precomposing with a pullback-preserving functor preserves the equalizer condition. -/
theorem equalizerCondition_precomp_of_preservesPullback (P : Cáµ’áµ– â¥¤ D) (F : E â¥¤ C)
    [âˆ€ {X B} (Ï€ : X âŸ¶ B) [EffectiveEpi Ï€], PreservesLimit (cospan Ï€ Ï€) F]
    [F.PreservesEffectiveEpis] (hP : EqualizerCondition P) : EqualizerCondition (F.op â‹™ P) := by
  intro X B Ï€ _ c hc
  have h : P.map (F.map Ï€).op = (F.op â‹™ P).map Ï€.op := by simp
  refine âŸ¨(IsLimit.equivIsoLimit (ForkOfÎ¹.ext ?_ _ h)) ?_âŸ©
  Â· simp only [Functor.comp_map, op_map, Quiver.Hom.unop_op, â† map_comp, â† op_comp, c.condition]
  Â· refine (hP (F.map Ï€) (PullbackCone.mk (F.map c.fst) (F.map c.snd) ?_) ?_).some
    Â· simp only [â† map_comp, c.condition]
    Â· exact (isLimitMapConePullbackConeEquiv F c.condition)
        (isLimitOfPreserves F (hc.ofIsoLimit (PullbackCone.ext (Iso.refl _) (by simp) (by simp))))

/-- The canonical map to the explicit equalizer. -/
def MapToEqualizer (P : Cáµ’áµ– â¥¤ Type*) {W X B : C} (f : X âŸ¶ B)
    (gâ‚ gâ‚‚ : W âŸ¶ X) (w : gâ‚ â‰« f = gâ‚‚ â‰« f) :
    P.obj (op B) â†’ { x : P.obj (op X) | P.map gâ‚.op x = P.map gâ‚‚.op x } := fun t â†¦
  âŸ¨P.map f.op t, by simp only [Set.mem_setOf_eq, â† FunctorToTypes.map_comp_apply, â† op_comp, w]âŸ©

theorem EqualizerCondition.bijective_mapToEqualizer_pullback (P : Cáµ’áµ– â¥¤ Type*)
    (hP : EqualizerCondition P) : âˆ€ (X B : C) (Ï€ : X âŸ¶ B) [EffectiveEpi Ï€] [HasPullback Ï€ Ï€],
    Function.Bijective
      (MapToEqualizer P Ï€ (pullback.fst Ï€ Ï€) (pullback.snd Ï€ Ï€) pullback.condition) := by
  intro X B Ï€ _ _
  specialize hP Ï€ _ (pullbackIsPullback Ï€ Ï€)
  rw [Types.type_equalizer_iff_unique] at hP
  rw [Function.bijective_iff_existsUnique]
  intro âŸ¨b, hbâŸ©
  obtain âŸ¨a, haâ‚, haâ‚‚âŸ© := hP b hb
  refine âŸ¨a, ?_, ?_âŸ©
  Â· simpa [MapToEqualizer] using haâ‚
  Â· simpa [MapToEqualizer] using haâ‚‚

theorem EqualizerCondition.mk (P : Cáµ’áµ– â¥¤ Type*)
    (hP : âˆ€ (X B : C) (Ï€ : X âŸ¶ B) [EffectiveEpi Ï€] [HasPullback Ï€ Ï€], Function.Bijective
    (MapToEqualizer P Ï€ (pullback.fst Ï€ Ï€) (pullback.snd Ï€ Ï€)
    pullback.condition)) : EqualizerCondition P := by
  intro X B Ï€ _ c hc
  have : HasPullback Ï€ Ï€ := âŸ¨c, hcâŸ©
  specialize hP X B Ï€
  rw [Types.type_equalizer_iff_unique]
  rw [Function.bijective_iff_existsUnique] at hP
  intro b hb
  have hâ‚ : ((pullbackIsPullback Ï€ Ï€).conePointUniqueUpToIso hc).hom â‰« c.fst =
    pullback.fst Ï€ Ï€ := by simp
  have hb' : P.map (pullback.fst Ï€ Ï€).op b = P.map (pullback.snd _ _).op b := by
    rw [â† hâ‚, op_comp, FunctorToTypes.map_comp_apply, hb]
    simp [â† FunctorToTypes.map_comp_apply, â† op_comp]
  obtain âŸ¨a, haâ‚, haâ‚‚âŸ© := hP âŸ¨b, hb'âŸ©
  refine âŸ¨a, ?_, ?_âŸ©
  Â· simpa [MapToEqualizer] using haâ‚
  Â· simpa [MapToEqualizer] using haâ‚‚

lemma equalizerCondition_w' (P : Cáµ’áµ– â¥¤ Type*) {X B : C} (Ï€ : X âŸ¶ B)
    [HasPullback Ï€ Ï€] : P.map Ï€.op â‰« P.map (pullback.fst Ï€ Ï€).op =
    P.map Ï€.op â‰« P.map (pullback.snd Ï€ Ï€).op := by
  simp only [â† Functor.map_comp, â† op_comp, pullback.condition]

lemma mapToEqualizer_eq_comp (P : Cáµ’áµ– â¥¤ Type*) {X B : C} (Ï€ : X âŸ¶ B) [HasPullback Ï€ Ï€] :
    MapToEqualizer P Ï€ (pullback.fst Ï€ Ï€) (pullback.snd Ï€ Ï€) pullback.condition =
    equalizer.lift (P.map Ï€.op) (equalizerCondition_w' P Ï€) â‰«
    (Types.equalizerIso _ _).hom := by
  rw [â† Iso.comp_inv_eq (Î± := Types.equalizerIso _ _)]
  apply equalizer.hom_ext
  aesop

/-- An alternative phrasing of the explicit equalizer condition, using more categorical language. -/
theorem equalizerCondition_iff_isIso_lift (P : Cáµ’áµ– â¥¤ Type*) : EqualizerCondition P â†”
    âˆ€ (X B : C) (Ï€ : X âŸ¶ B) [EffectiveEpi Ï€] [HasPullback Ï€ Ï€],
      IsIso (equalizer.lift (P.map Ï€.op) (equalizerCondition_w' P Ï€)) := by
  constructor
  Â· intro hP X B Ï€ _ _
    have h := hP.bijective_mapToEqualizer_pullback _ X B Ï€
    rw [â† isIso_iff_bijective, mapToEqualizer_eq_comp] at h
    exact IsIso.of_isIso_comp_right (equalizer.lift (P.map Ï€.op)
      (equalizerCondition_w' P Ï€))
      (Types.equalizerIso _ _).hom
  Â· intro hP
    apply EqualizerCondition.mk
    intro X B Ï€ _ _
    rw [mapToEqualizer_eq_comp, â† isIso_iff_bijective]
    infer_instance

/-- `P` satisfies the equalizer condition iff its precomposition by an equivalence does. -/
theorem equalizerCondition_iff_of_equivalence (P : Cáµ’áµ– â¥¤ D)
    (e : C â‰Œ E) : EqualizerCondition P â†” EqualizerCondition (e.op.inverse â‹™ P) :=
  âŸ¨fun h â†¦ equalizerCondition_precomp_of_preservesPullback P e.inverse h, fun h â†¦
    equalizerCondition_of_natIso (e.op.funInvIdAssoc P)
      (equalizerCondition_precomp_of_preservesPullback (e.op.inverse â‹™ P) e.functor h)âŸ©

open WalkingParallelPair WalkingParallelPairHom in
theorem parallelPair_pullback_initial {X B : C} (Ï€ : X âŸ¶ B)
    (c : PullbackCone Ï€ Ï€) (hc : IsLimit c) :
    (parallelPair (C := (Sieve.ofArrows (fun (_ : Unit) => X) (fun _ => Ï€)).arrows.categoryáµ’áµ–)
    (Y := op ((Presieve.categoryMk _ (c.fst â‰« Ï€) âŸ¨_, c.fst, Ï€, ofArrows.mk (), rflâŸ©)))
    (X := op ((Presieve.categoryMk _ Ï€ (Sieve.ofArrows_mk _ _ Unit.unit))))
    (Quiver.Hom.op (Over.homMk c.fst))
    (Quiver.Hom.op (Over.homMk c.snd c.condition.symm))).Initial := by
  apply Limits.parallelPair_initial_mk
  Â· intro âŸ¨ZâŸ©
    obtain âŸ¨_, f, g, âŸ¨âŸ©, hhâŸ© := Z.property
    let X' : (Presieve.ofArrows (fun () â†¦ X) (fun () â†¦ Ï€)).category :=
      Presieve.categoryMk _ Ï€ (ofArrows.mk ())
    let f' : Z.obj.left âŸ¶ X'.obj.left := f
    exact âŸ¨(Over.homMk f').opâŸ©
  Â· intro âŸ¨ZâŸ© âŸ¨iâŸ© âŸ¨jâŸ©
    let ij := PullbackCone.IsLimit.lift hc i.left j.left (by erw [i.w, j.w]; rfl)
    refine âŸ¨Quiver.Hom.op (Over.homMk ij (by simpa [ij] using i.w)), ?_, ?_âŸ©
    all_goals congr
    all_goals exact Comma.hom_ext _ _ (by erw [Over.comp_left]; simp [ij]) rfl

/--
Given a limiting pullback cone, the fork in `SingleEqualizerCondition` is limiting iff the diagram
in `Presheaf.isSheaf_iff_isLimit_coverage` is limiting.
-/
noncomputable def isLimit_forkOfÎ¹_equiv (P : Cáµ’áµ– â¥¤ D) {X B : C} (Ï€ : X âŸ¶ B)
    (c : PullbackCone Ï€ Ï€) (hc : IsLimit c) :
    IsLimit (Fork.ofÎ¹ (P.map Ï€.op) (equalizerCondition_w P c)) â‰ƒ
    IsLimit (P.mapCone (Sieve.ofArrows (fun (_ : Unit) â†¦ X) fun _ â†¦ Ï€).arrows.cocone.op) := by
  let S := (Sieve.ofArrows (fun (_ : Unit) => X) (fun _ => Ï€)).arrows
  let X' := S.categoryMk Ï€ âŸ¨_, ğŸ™ _, Ï€, ofArrows.mk (), Category.id_comp _âŸ©
  let P' := S.categoryMk (c.fst â‰« Ï€) âŸ¨_, c.fst, Ï€, ofArrows.mk (), rflâŸ©
  let fst : P' âŸ¶ X' := Over.homMk c.fst
  let snd : P' âŸ¶ X' := Over.homMk c.snd c.condition.symm
  let F : S.categoryáµ’áµ– â¥¤ D := S.diagram.op â‹™ P
  let G := parallelPair (P.map c.fst.op) (P.map c.snd.op)
  let H := parallelPair fst.op snd.op
  have : H.Initial := parallelPair_pullback_initial Ï€ c hc
  let i : H â‹™ F â‰… G := parallelPair.ext (Iso.refl _) (Iso.refl _) (by aesop) (by aesop)
  refine (IsLimit.equivOfNatIsoOfIso i.symm _ _ ?_).trans (Functor.Initial.isLimitWhiskerEquiv H _)
  refine Cones.ext (Iso.refl _) ?_
  rintro âŸ¨_ | _âŸ©
  all_goals aesop

lemma equalizerConditionMap_iff_nonempty_isLimit (P : Cáµ’áµ– â¥¤ D) â¦ƒX B : Câ¦„ (Ï€ : X âŸ¶ B)
    [HasPullback Ï€ Ï€] : SingleEqualizerCondition P Ï€ â†”
      Nonempty (IsLimit (P.mapCone
        (Sieve.ofArrows (fun (_ : Unit) => X) (fun _ => Ï€)).arrows.cocone.op)) := by
  constructor
  Â· intro h
    exact âŸ¨isLimit_forkOfÎ¹_equiv _ _ _ (pullbackIsPullback Ï€ Ï€) (h _ (pullbackIsPullback Ï€ Ï€)).someâŸ©
  Â· intro âŸ¨hâŸ©
    exact fun c hc â†¦ âŸ¨(isLimit_forkOfÎ¹_equiv _ _ _ hc).symm hâŸ©

lemma equalizerCondition_iff_isSheaf (F : Cáµ’áµ– â¥¤ D) [Preregular C]
    [âˆ€ {Y X : C} (f : Y âŸ¶ X) [EffectiveEpi f], HasPullback f f] :
    EqualizerCondition F â†” Presheaf.IsSheaf (regularTopology C) F := by
  dsimp [regularTopology]
  rw [Presheaf.isSheaf_iff_isLimit_coverage]
  constructor
  Â· rintro hF X _ âŸ¨Y, f, rfl, _âŸ©
    exact (equalizerConditionMap_iff_nonempty_isLimit F f).1 (hF f)
  Â· intro hF Y X f _
    exact (equalizerConditionMap_iff_nonempty_isLimit F f).2 (hF _ âŸ¨_, f, rfl, inferInstanceâŸ©)

lemma isSheafFor_regular_of_projective {X : C} (S : Presieve X) [S.regular] [Projective X]
    (F : Cáµ’áµ– â¥¤ Type*) : S.IsSheafFor F := by
  obtain âŸ¨Y, f, rfl, hfâŸ© := Presieve.regular.single_epi (R := S)
  rw [isSheafFor_arrows_iff]
  refine fun x hx â†¦ âŸ¨F.map (Projective.factorThru (ğŸ™ _) f).op <| x (), fun _ â†¦ ?_, fun y h â†¦ ?_âŸ©
  Â· simpa using (hx () () Y (ğŸ™ Y) (f â‰« (Projective.factorThru (ğŸ™ _) f)) (by simp)).symm
  Â· simp only [â† h (), â† FunctorToTypes.map_comp_apply, â† op_comp, Projective.factorThru_comp,
      op_id, FunctorToTypes.map_id_apply]

/-- Every presheaf is a sheaf for the regular topology if every object of `C` is projective. -/
theorem isSheaf_of_projective (F : Cáµ’áµ– â¥¤ D) [Preregular C] [âˆ€ (X : C), Projective X] :
    Presheaf.IsSheaf (regularTopology C) F :=
  fun _ â†¦ (isSheaf_coverage _ _).mpr fun S âŸ¨_, hâŸ© â†¦ have : S.regular := âŸ¨_, hâŸ©
    isSheafFor_regular_of_projective _ _

/-- Every Yoneda-presheaf is a sheaf for the regular topology. -/
lemma isSheaf_yoneda_obj [Preregular C] (W : C)  :
    Presieve.IsSheaf (regularTopology C) (yoneda.obj W) := by
  rw [regularTopology, isSheaf_coverage]
  intro X S âŸ¨_, hSâŸ©
  have : S.regular := âŸ¨_, hSâŸ©
  obtain âŸ¨Y, f, rfl, hfâŸ© := Presieve.regular.single_epi (R := S)
  have h_colim := isColimitOfEffectiveEpiStruct f hf.effectiveEpi.some
  rw [â† Sieve.generateSingleton_eq, â† Presieve.ofArrows_pUnit] at h_colim
  intro x hx
  let x_ext := Presieve.FamilyOfElements.sieveExtend x
  have hx_ext := Presieve.FamilyOfElements.Compatible.sieveExtend hx
  let S := Sieve.generate (Presieve.ofArrows (fun () â†¦ Y) (fun () â†¦ f))
  obtain âŸ¨t, t_amalg, t_uniqâŸ© :=
    (Sieve.forallYonedaIsSheaf_iff_colimit S).mpr âŸ¨h_colimâŸ© W x_ext hx_ext
  refine âŸ¨t, ?_, ?_âŸ©
  Â· convert Presieve.isAmalgamation_restrict (Sieve.le_generate
      (Presieve.ofArrows (fun () â†¦ Y) (fun () â†¦ f))) _ _ t_amalg
    exact (Presieve.restrict_extend hx).symm
  Â· exact fun y hy â†¦ t_uniq y <| Presieve.isAmalgamation_sieveExtend x y hy

/-- The regular topology on any preregular category is subcanonical. -/
instance subcanonical [Preregular C] : (regularTopology C).Subcanonical :=
  GrothendieckTopology.Subcanonical.of_isSheaf_yoneda_obj _ isSheaf_yoneda_obj

end regularTopology

end CategoryTheory
