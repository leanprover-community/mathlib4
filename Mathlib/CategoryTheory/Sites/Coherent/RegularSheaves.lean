/-
Copyright (c) 2023 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson, Filippo A. E. Nuccio, Riccardo Brasca
-/
import Mathlib.CategoryTheory.Preadditive.Projective
import Mathlib.CategoryTheory.Sites.Canonical
import Mathlib.CategoryTheory.Sites.Coherent.Basic
import Mathlib.CategoryTheory.Sites.EffectiveEpimorphic
/-!

# Sheaves for the regular topology

This file characterises sheaves for the regular topology.

## Main results

* `isSheaf_iff_equalizerCondition`:Â In a preregular category with pullbacks, the sheaves for the
  regular topology are precisely the presheaves satisfying an equaliser condition with respect to
  effective epimorphisms.

* `isSheaf_of_projective`: In a preregular category in which every object is projective, every
  presheaf is a sheaf for the regular topology.
-/

namespace CategoryTheory

open Limits

variable {C : Type*} [Category C]

open Opposite Presieve

/-- A presieve is *regular* if it consists of a single effective epimorphism. -/
class Presieve.regular {X : C} (R : Presieve X) : Prop where
  /-- `R` consists of a single epimorphism. -/
  single_epi : âˆƒ (Y : C) (f : Y âŸ¶ X), R = Presieve.ofArrows (fun (_ : Unit) â†¦ Y)
    (fun (_ : Unit) â†¦ f) âˆ§ EffectiveEpi f

namespace regularCoverage

/--
The map to the explicit equalizer used in the sheaf condition.
-/
def MapToEqualizer (P : Cáµ’áµ– â¥¤ Type*) {W X B : C} (f : X âŸ¶ B)
    (gâ‚ gâ‚‚ : W âŸ¶ X) (w : gâ‚ â‰« f = gâ‚‚ â‰« f) :
    P.obj (op B) â†’ { x : P.obj (op X) | P.map gâ‚.op x = P.map gâ‚‚.op x } := fun t â†¦
  âŸ¨P.map f.op t, by simp only [Set.mem_setOf_eq, â† FunctorToTypes.map_comp_apply, â† op_comp, w]âŸ©

/--
The sheaf condition with respect to regular presieves, given the existence of the relavant pullback.
-/
def EqualizerCondition (P : Cáµ’áµ– â¥¤ Type*) : Prop :=
  âˆ€ (X B : C) (Ï€ : X âŸ¶ B) [EffectiveEpi Ï€] [HasPullback Ï€ Ï€], Function.Bijective
    (MapToEqualizer P Ï€ (pullback.fst (f := Ï€) (g := Ï€)) (pullback.snd (f := Ï€) (g := Ï€))
    pullback.condition)

theorem equalizerCondition_iff_isIso_lift_w (P : Cáµ’áµ– â¥¤ Type*) {X B : C} (Ï€ : X âŸ¶ B)
    [HasPullback Ï€ Ï€] : P.map Ï€.op â‰« P.map (pullback.fst (f := Ï€) (g := Ï€)).op =
    P.map Ï€.op â‰« P.map (pullback.snd).op := by
  simp only [â† Functor.map_comp, â† op_comp, pullback.condition]

theorem equalizerCondition_iff_isIso_lift_aux_comp (P : Cáµ’áµ– â¥¤ Type*) {X B : C} (Ï€ : X âŸ¶ B)
    [HasPullback Ï€ Ï€] : MapToEqualizer P Ï€ pullback.fst pullback.snd pullback.condition =
    equalizer.lift (P.map Ï€.op) (equalizerCondition_iff_isIso_lift_w P Ï€) â‰«
    (Types.equalizerIso _ _).hom := by
  rw [â† Iso.comp_inv_eq (Î± := Types.equalizerIso _ _)]
  apply equalizer.hom_ext
  simp only [Category.assoc, Types.equalizerIso_inv_comp_Î¹, limit.lift_Ï€, Fork.ofÎ¹_pt,
    Fork.ofÎ¹_Ï€_app]
  ext
  rfl

theorem equalizerCondition_iff_isIso_lift (P : Cáµ’áµ– â¥¤ Type*) : EqualizerCondition P â†”
    âˆ€ (X B : C) (Ï€ : X âŸ¶ B) [EffectiveEpi Ï€] [HasPullback Ï€ Ï€], IsIso
    (equalizer.lift (P.map Ï€.op) (equalizerCondition_iff_isIso_lift_w P Ï€)) := by
  unfold EqualizerCondition
  refine âŸ¨fun h X B Ï€ _ _ â†¦ ?_, fun h X B Ï€ _ _ â†¦ ?_âŸ©
  Â· specialize h X B Ï€
    rw [â† isIso_iff_bijective, equalizerCondition_iff_isIso_lift_aux_comp] at h
    exact IsIso.of_isIso_comp_right (equalizer.lift (P.map Ï€.op)
      (equalizerCondition_iff_isIso_lift_w P Ï€))
      (Types.equalizerIso _ _).hom
  Â· specialize h X B Ï€
    rw [equalizerCondition_iff_isIso_lift_aux_comp, â† isIso_iff_bijective]
    infer_instance

/-- TODO: move to right place and correct generality -/
@[simps]
noncomputable
def preservesPullbackIso {D : Type*} [Category D] (P : Cáµ’áµ– â¥¤ Type*) (F : D â¥¤ C)
    {X B : D} (Ï€ : X âŸ¶ B) [HasPullback Ï€ Ï€] [PreservesLimit (cospan Ï€ Ï€) F] :
    haveI := hasPullback_of_preservesPullback F Ï€ Ï€
    (equalizer (P.map (pullback.fst (f := (F.map Ï€)) (g := (F.map Ï€))).op)
      (P.map (pullback.snd).op)) â‰…
      equalizer ((F.op â‹™ P).map (pullback.fst (f := Ï€) (g := Ï€)).op)
        ((F.op â‹™ P).map pullback.snd.op) where
  hom := equalizer.lift (equalizer.Î¹ _ _) (by
    simp only [Functor.comp_obj, Functor.op_obj, unop_op, Functor.comp_map, Functor.op_map,
      Quiver.Hom.unop_op]
    haveI := hasPullback_of_preservesPullback F Ï€ Ï€
    have h := equalizer.condition (P.map (pullback.fst (f := (F.map Ï€)) (g := (F.map Ï€))).op)
      (P.map (pullback.snd).op)
    have hâ‚ := PreservesPullback.iso_hom_fst F Ï€ Ï€
    have hâ‚‚ := PreservesPullback.iso_hom_snd F Ï€ Ï€
    simp only [â† hâ‚, â† hâ‚‚, op_comp, Functor.map_comp, â† Category.assoc, h])
  inv := equalizer.lift (equalizer.Î¹ _ _) (by
    simp only [Functor.comp_obj, Functor.op_obj, unop_op, Functor.comp_map, Functor.op_map,
      Quiver.Hom.unop_op]
    haveI := hasPullback_of_preservesPullback F Ï€ Ï€
    have hâ‚ := PreservesPullback.iso_inv_fst F Ï€ Ï€
    have hâ‚‚ := PreservesPullback.iso_inv_snd F Ï€ Ï€
    simp only [â† hâ‚, â† hâ‚‚, op_comp, Functor.map_comp]
    rw [â† Category.assoc, equalizer.condition]
    rfl)
  hom_inv_id := by
    apply equalizer.hom_ext
    simp only [Functor.comp_obj, Functor.op_obj, unop_op, Functor.comp_map, Functor.op_map,
      Quiver.Hom.unop_op, Category.assoc, limit.lift_Ï€, op_comp, id_eq, eq_mpr_eq_cast, cast_eq,
      Fork.ofÎ¹_pt, Fork.ofÎ¹_Ï€_app, Category.id_comp]
    exact equalizer.lift_Î¹ _ _
  inv_hom_id := by
    apply equalizer.hom_ext
    simp only [Functor.comp_obj, Functor.op_obj, unop_op, Functor.comp_map, Functor.op_map,
      Quiver.Hom.unop_op, Category.assoc, Category.id_comp]
    erw [equalizer.lift_Î¹, equalizer.lift_Î¹]

theorem mapToEqualizer_pullback_comp {D : Type*} [Category D] (P : Cáµ’áµ– â¥¤ Type*) (F : D â¥¤ C)
    {X B : D} (Ï€ : X âŸ¶ B) [HasPullback Ï€ Ï€] [PreservesLimit (cospan Ï€ Ï€) F] :
    haveI := hasPullback_of_preservesPullback F Ï€ Ï€
    (equalizer.lift ((F.op â‹™ P).map Ï€.op)
      (equalizerCondition_iff_isIso_lift_w (F.op â‹™ P) Ï€)) =
    (equalizer.lift (P.map (F.map Ï€).op)
      (equalizerCondition_iff_isIso_lift_w P (F.map Ï€))) â‰« (preservesPullbackIso P F Ï€).hom := by
  simp only [Functor.comp_obj, Functor.op_obj, unop_op, Functor.comp_map, Functor.op_map,
    Quiver.Hom.unop_op, preservesPullbackIso]
  apply equalizer.hom_ext
  simp only [Functor.comp_obj, Functor.op_obj, unop_op, Functor.comp_map, Functor.op_map,
    Quiver.Hom.unop_op, Category.assoc]
  erw [equalizer.lift_Î¹, equalizer.lift_Î¹, equalizer.lift_Î¹]

theorem equalizerCondition_precomp_of_preservesPullback {D : Type*} [Category D] (P : Cáµ’áµ– â¥¤ Type*)
    (F : D â¥¤ C)
    [âˆ€ {X B} (Ï€ : X âŸ¶ B) [EffectiveEpi Ï€], PreservesLimit (cospan Ï€ Ï€) F]
    [F.PreservesEffectiveEpis]
    (hP : EqualizerCondition P) : EqualizerCondition (F.op â‹™ P) := by
  rw [equalizerCondition_iff_isIso_lift] at hP âŠ¢
  intro X B Ï€ _ _
  have := hasPullback_of_preservesPullback F Ï€ Ï€
  have := hP (F.obj X) (F.obj B) (F.map Ï€)
  rw [mapToEqualizer_pullback_comp (F := F)]
  exact IsIso.comp_isIso

theorem equalizerCondition_of_natIso_aux
    {P : Cáµ’áµ– â¥¤ Type*} {P' : Cáµ’áµ– â¥¤ Type _} (i : P â‰… P') {X B : C} (Ï€ : X âŸ¶ B)
    [HasPullback Ï€ Ï€] :
    (equalizer.Î¹ (P.map (pullback.fst (f := Ï€) (g := Ï€)).op) (P.map pullback.snd.op) â‰«
      i.hom.app (op X)) â‰« P'.map pullback.fst.op =
      (equalizer.Î¹ (P.map pullback.fst.op) (P.map pullback.snd.op) â‰« i.hom.app (op X)) â‰«
      P'.map (pullback.snd  (f := Ï€) (g := Ï€)).op := by
  rw [Category.assoc, Category.assoc, â† i.hom.naturality (pullback.fst (f := Ï€) (g := Ï€)).op,
    â† i.hom.naturality (pullback.snd (f := Ï€) (g := Ï€)).op, â† Category.assoc, equalizer.condition]
  simp

/-- TODO: move to right place and correct generality -/
noncomputable
def equalizerIso_of_natIso {P : Cáµ’áµ– â¥¤ Type*} {P' : Cáµ’áµ– â¥¤ Type _} (i : P â‰… P') {X B : C}
    (Ï€ : X âŸ¶ B) [HasPullback Ï€ Ï€] :
    equalizer (P.map (pullback.fst (f := Ï€) (g := Ï€)).op) (P.map pullback.snd.op) â‰…
    equalizer (P'.map (pullback.fst (f := Ï€) (g := Ï€)).op) (P'.map pullback.snd.op) where
  hom := equalizer.lift (equalizer.Î¹ _ _ â‰« i.hom.app (op X)) (equalizerCondition_of_natIso_aux i Ï€)
  inv := equalizer.lift (equalizer.Î¹ _ _ â‰« i.inv.app (op X))
    (equalizerCondition_of_natIso_aux i.symm Ï€)
  hom_inv_id := by apply equalizer.hom_ext; simp
  inv_hom_id := by apply equalizer.hom_ext; simp

theorem equalizerCondition_of_natIso {P : Cáµ’áµ– â¥¤ Type*} {P' : Cáµ’áµ– â¥¤ Type _} (i : P â‰… P')
    (hP : EqualizerCondition P) :
    EqualizerCondition P' := by
  rw [equalizerCondition_iff_isIso_lift] at hP âŠ¢
  intro X B Ï€ _ _
  specialize hP X B Ï€
  have h : equalizer.lift (P'.map Ï€.op) (equalizerCondition_iff_isIso_lift_w P' Ï€) =
      i.inv.app (op B) â‰«
      equalizer.lift (P.map Ï€.op) (equalizerCondition_iff_isIso_lift_w P Ï€) â‰«
      (equalizerIso_of_natIso i Ï€).hom := by
    apply equalizer.hom_ext
    simp [equalizerIso_of_natIso]
  rw [h]
  infer_instance

theorem equalizerCondition_iff_of_equivalence {D : Type*} [Category D] (P : Cáµ’áµ– â¥¤ Type*)
    (e : C â‰Œ D) : EqualizerCondition P â†” EqualizerCondition (e.op.inverse â‹™ P) :=
  âŸ¨fun h â†¦ equalizerCondition_precomp_of_preservesPullback P e.inverse h, fun h â†¦
    equalizerCondition_of_natIso (e.op.funInvIdAssoc P)
      (equalizerCondition_precomp_of_preservesPullback (e.op.inverse â‹™ P) e.functor h)âŸ©

lemma EqualizerCondition.isSheafFor {B : C} {S : Presieve B} [S.regular] [S.hasPullbacks]
    {F : Cáµ’áµ– â¥¤ Type*}
    (hF : EqualizerCondition F) : S.IsSheafFor F := by
  obtain âŸ¨X, Ï€, hS, Ï€surjâŸ© := Presieve.regular.single_epi (R := S)
  subst hS
  rw [isSheafFor_arrows_iff_pullbacks]
  intro y h
  have : (Presieve.singleton Ï€).hasPullbacks := by rw [â† ofArrows_pUnit]; infer_instance
  have : HasPullback Ï€ Ï€ := hasPullbacks.has_pullbacks Presieve.singleton.mk Presieve.singleton.mk
  specialize hF X B Ï€
  rw [Function.bijective_iff_existsUnique] at hF
  obtain âŸ¨t, ht, ht'âŸ© := hF âŸ¨y (), h () ()âŸ©
  refine âŸ¨t, fun _ â†¦ ?_, fun x h â†¦ ht' x ?_âŸ©
  Â· simpa [MapToEqualizer] using ht
  Â· simpa [MapToEqualizer] using h ()

lemma equalizerCondition_of_regular {F : Cáµ’áµ– â¥¤ Type*}
    (hSF : âˆ€ {B : C} (S : Presieve B) [S.regular] [S.hasPullbacks], S.IsSheafFor F) :
    EqualizerCondition F := by
  intro X B Ï€ _ _
  have : (ofArrows (fun _ â†¦ X) (fun _ â†¦ Ï€)).regular := âŸ¨X, Ï€, rfl, inferInstanceâŸ©
  have : (ofArrows (fun () â†¦ X) (fun _ â†¦ Ï€)).hasPullbacks := âŸ¨
      fun hf _ hg â†¦ (by cases hf; cases hg; infer_instance)âŸ©
  specialize hSF (ofArrows (fun () â†¦ X) (fun _ â†¦ Ï€))
  rw [isSheafFor_arrows_iff_pullbacks] at hSF
  rw [Function.bijective_iff_existsUnique]
  intro âŸ¨x, hxâŸ©
  obtain âŸ¨t, ht, ht'âŸ© := hSF (fun _ â†¦ x) (fun _ _ â†¦ hx)
  refine âŸ¨t, ?_, fun y h â†¦ ht' y ?_âŸ©
  Â· simpa [MapToEqualizer] using ht ()
  Â· simpa [MapToEqualizer] using h

lemma isSheafFor_regular_of_projective {X : C} (S : Presieve X) [S.regular] [Projective X]
    (F : Cáµ’áµ– â¥¤ Type*) : S.IsSheafFor F := by
  obtain âŸ¨Y, f, rfl, hfâŸ© := Presieve.regular.single_epi (R := S)
  rw [isSheafFor_arrows_iff]
  refine fun x hx â†¦ âŸ¨F.map (Projective.factorThru (ğŸ™ _) f).op <| x (), fun _ â†¦ ?_, fun y h â†¦ ?_âŸ©
  Â· simpa using (hx () () Y (ğŸ™ Y) (f â‰« (Projective.factorThru (ğŸ™ _) f)) (by simp)).symm
  Â· simp only [â† h (), â† FunctorToTypes.map_comp_apply, â† op_comp, Projective.factorThru_comp,
      op_id, FunctorToTypes.map_id_apply]

lemma EqualizerCondition.isSheaf_iff (F : Cáµ’áµ– â¥¤ Type*)
    [âˆ€ â¦ƒX Y : Câ¦„ (Ï€ : X âŸ¶ Y) [EffectiveEpi Ï€], HasPullback Ï€ Ï€] [Preregular C] :
    Presieve.IsSheaf (regularCoverage C).toGrothendieck F â†” EqualizerCondition F := by
  rw [Presieve.isSheaf_coverage]
  refine âŸ¨fun h â†¦ equalizerCondition_of_regular fun S âŸ¨Y, f, hhâŸ© _ â†¦ h S âŸ¨Y, f, hhâŸ©, ?_âŸ©
  rintro h X S âŸ¨Y, f, rfl, hfâŸ©
  exact @isSheafFor _ _ _ _ âŸ¨Y, f, rfl, hfâŸ© âŸ¨fun g _ h â†¦ by cases g; cases h; infer_instanceâŸ© _ h

lemma isSheaf_of_projective (F : Cáµ’áµ– â¥¤ Type*) [Preregular C] [âˆ€ (X : C), Projective X] :
    IsSheaf (regularCoverage C).toGrothendieck F :=
  (isSheaf_coverage _ _).mpr fun S âŸ¨_, hâŸ© â†¦ have : S.regular := âŸ¨_, hâŸ©
    isSheafFor_regular_of_projective _ _

/-- Every Yoneda-presheaf is a sheaf for the regular topology. -/
theorem isSheaf_yoneda_obj [Preregular C] (W : C)  :
    Presieve.IsSheaf (regularCoverage C).toGrothendieck (yoneda.obj W) := by
  rw [isSheaf_coverage]
  intro X S âŸ¨_, hSâŸ©
  have : S.regular := âŸ¨_, hSâŸ©
  obtain âŸ¨Y, f, rfl, hfâŸ© := Presieve.regular.single_epi (R := S)
  have h_colim := isColimitOfEffectiveEpiStruct f hf.effectiveEpi.some
  rw [â† Sieve.generateSingleton_eq, â† Presieve.ofArrows_pUnit] at h_colim
  intro x hx
  let x_ext := Presieve.FamilyOfElements.sieveExtend x
  have hx_ext := Presieve.FamilyOfElements.Compatible.sieveExtend hx
  let S := Sieve.generate (Presieve.ofArrows (fun () â†¦ Y) (fun () â†¦ f))
  obtain âŸ¨t, t_amalg, t_uniqâŸ© :=
    (Sieve.forallYonedaIsSheaf_iff_colimit S).mpr âŸ¨h_colimâŸ© W x_ext hx_ext
  refine âŸ¨t, ?_, ?_âŸ©
  Â· convert Presieve.isAmalgamation_restrict (Sieve.le_generate
      (Presieve.ofArrows (fun () â†¦ Y) (fun () â†¦ f))) _ _ t_amalg
    exact (Presieve.restrict_extend hx).symm
  Â· exact fun y hy â†¦ t_uniq y <| Presieve.isAmalgamation_sieveExtend x y hy

/-- The regular topology on any preregular category is subcanonical. -/
theorem subcanonical [Preregular C] : Sheaf.Subcanonical (regularCoverage C).toGrothendieck :=
  Sheaf.Subcanonical.of_yoneda_isSheaf _ isSheaf_yoneda_obj

end regularCoverage

end CategoryTheory
