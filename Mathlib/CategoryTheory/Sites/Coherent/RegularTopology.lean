/-
Copyright (c) 2023 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.CategoryTheory.Sites.Coherent.RegularSheaves
/-!

# Description of the covering sieves of the regular topology

This file characterises the covering sieves of the regular topology.

## Main result

* `regularTopology.mem_sieves_iff_hasEffectiveEpi`: a sieve is a covering sieve for the
  regular topology if and only if it contains an effective epi.
-/

namespace CategoryTheory.regularTopology

open Limits

variable {C : Type*} [Category C] [Preregular C] {X : C}

/--
For a preregular category, any sieve that contains an `EffectiveEpi` is a covering sieve of the
regular topology.
Note: This is one direction of `mem_sieves_iff_hasEffectiveEpi`, but is needed for the proof.
-/
theorem mem_sieves_of_hasEffectiveEpi (S : Sieve X) :
    (âˆƒ (Y : C) (Ï€ : Y âŸ¶ X), EffectiveEpi Ï€ âˆ§ S.arrows Ï€) â†’ (S âˆˆ (regularTopology C).sieves X) := by
  rintro âŸ¨Y, Ï€, hâŸ©
  have h_le : Sieve.generate (Presieve.ofArrows (fun () â†¦ Y) (fun _ â†¦ Ï€)) â‰¤ S := by
    rw [Sieve.sets_iff_generate (Presieve.ofArrows _ _) S]
    apply Presieve.le_of_factorsThru_sieve (Presieve.ofArrows _ _) S _
    intro W g f
    refine âŸ¨W, ğŸ™ W, ?_âŸ©
    cases f
    exact âŸ¨Ï€, âŸ¨h.2, Category.id_comp Ï€âŸ©âŸ©
  apply Coverage.saturate_of_superset (regularCoverage C) h_le
  exact Coverage.saturate.of X _ âŸ¨Y, Ï€, rfl, h.1âŸ©

/-- Effective epis in a preregular category are stable under composition. -/
instance {Y Y' : C} (Ï€ : Y âŸ¶ X) [EffectiveEpi Ï€]
    (Ï€' : Y' âŸ¶ Y) [EffectiveEpi Ï€'] : EffectiveEpi (Ï€' â‰« Ï€) := by
  rw [effectiveEpi_iff_effectiveEpiFamily, â† Sieve.effectiveEpimorphic_family]
  suffices hâ‚‚ : (Sieve.generate (Presieve.ofArrows _ _)) âˆˆ
      GrothendieckTopology.sieves (regularTopology C) X by
    change Nonempty _
    rw [â† Sieve.forallYonedaIsSheaf_iff_colimit]
    exact fun W => regularTopology.isSheaf_yoneda_obj W _ hâ‚‚
  apply Coverage.saturate.transitive X (Sieve.generate (Presieve.ofArrows (fun () â†¦ Y)
      (fun () â†¦ Ï€)))
  Â· apply Coverage.saturate.of
    use Y, Ï€
  Â· intro V f âŸ¨Yâ‚, h, g, âŸ¨hY, hfâŸ©âŸ©
    rw [â† hf, Sieve.pullback_comp]
    apply (regularTopology C).pullback_stable'
    apply regularTopology.mem_sieves_of_hasEffectiveEpi
    cases hY
    exact âŸ¨Y', Ï€', inferInstance, Y', (ğŸ™ _), Ï€' â‰« Ï€, Presieve.ofArrows.mk (), (by simp)âŸ©

/-- A sieve is a cover for the regular topology if and only if it contains an `EffectiveEpi`. -/
theorem mem_sieves_iff_hasEffectiveEpi (S : Sieve X) :
    (S âˆˆ (regularTopology C).sieves  X) â†”
    âˆƒ (Y : C) (Ï€ : Y âŸ¶ X), EffectiveEpi Ï€ âˆ§ (S.arrows Ï€) := by
  constructor
  Â· intro h
    induction' h with Y T hS Y Y R S _ _ a b
    Â· rcases hS with âŸ¨Y', Ï€, h'âŸ©
      refine âŸ¨Y', Ï€, h'.2, ?_âŸ©
      rcases h' with âŸ¨rfl, _âŸ©
      exact âŸ¨Y', ğŸ™ Y', Ï€, Presieve.ofArrows.mk (), (by simp)âŸ©
    Â· exact âŸ¨Y, (ğŸ™ Y), inferInstance, by simp only [Sieve.top_apply, forall_const]âŸ©
    Â· rcases a with âŸ¨Yâ‚, Ï€, âŸ¨hâ‚,hâ‚‚âŸ©âŸ©
      choose Y' Ï€' _ H using b hâ‚‚
      exact âŸ¨Y', Ï€' â‰« Ï€, inferInstance, (by simpa using H)âŸ©
  Â· exact regularTopology.mem_sieves_of_hasEffectiveEpi S

end CategoryTheory.regularTopology
