/-
Copyright (c) 2023 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz, Nikolas Kuhn
-/
import Mathlib.CategoryTheory.Sites.Coherent.CoherentSheaves
import Mathlib.Data.Finite.Sigma

/-!
# Description of the covering sieves of the coherent topology

This file characterises the covering sieves of the coherent topology.

## Main result

* `coherentTopology.mem_sieves_iff_hasEffectiveEpiFamily`: a sieve is a covering sieve for the
  coherent topology if and only if it contains a finite effective epimorphic family.

-/

namespace CategoryTheory

variable {C : Type*} [Category C] [Precoherent C] {X : C}

/--
For a precoherent category, any sieve that contains an `EffectiveEpiFamily` is a sieve of the
coherent topology.
Note: This is one direction of `mem_sieves_iff_hasEffectiveEpiFamily`, but is needed for the proof.
-/
theorem coherentTopology.mem_sieves_of_hasEffectiveEpiFamily (S : Sieve X) :
    (âˆƒ (Î± : Type) (_ : Finite Î±) (Y : Î± â†’ C) (Ï€ : (a : Î±) â†’ (Y a âŸ¶ X)),
      EffectiveEpiFamily Y Ï€ âˆ§ (âˆ€ a : Î±, (S.arrows) (Ï€ a)) ) â†’
        (S âˆˆ (coherentTopology C) X) := by
  intro âŸ¨Î±, _, Y, Ï€, hÏ€âŸ©
  apply (coherentCoverage C).mem_toGrothendieck_sieves_of_superset (R := Presieve.ofArrows Y Ï€)
  Â· exact fun _ _ h â†¦ by cases h; exact hÏ€.2 _
  Â· exact âŸ¨_, inferInstance, Y, Ï€, rfl, hÏ€.1âŸ©

/--
Effective epi families in a precoherent category are transitive, in the sense that an
`EffectiveEpiFamily` and an `EffectiveEpiFamily` over each member, the composition is an
`EffectiveEpiFamily`.
Note: The finiteness condition is an artifact of the proof and is probably unnecessary.
-/
theorem EffectiveEpiFamily.transitive_of_finite {Î± : Type} [Finite Î±] {Y : Î± â†’ C}
    (Ï€ : (a : Î±) â†’ (Y a âŸ¶ X)) (h : EffectiveEpiFamily Y Ï€) {Î² : Î± â†’ Type} [âˆ€ (a : Î±), Finite (Î² a)]
    {Y_n : (a : Î±) â†’ Î² a â†’ C} (Ï€_n : (a : Î±) â†’ (b : Î² a) â†’ (Y_n a b âŸ¶ Y a))
    (H : âˆ€ a, EffectiveEpiFamily (Y_n a) (Ï€_n a)) :
    EffectiveEpiFamily
      (fun (c : Î£ a, Î² a) => Y_n c.fst c.snd) (fun c => Ï€_n c.fst c.snd â‰« Ï€ c.fst) := by
  rw [â† Sieve.effectiveEpimorphic_family]
  suffices hâ‚‚ : (Sieve.generate (Presieve.ofArrows (fun (âŸ¨a, bâŸ© : Î£ _, Î² _) => Y_n a b)
        (fun âŸ¨a,bâŸ© => Ï€_n a b â‰« Ï€ a))) âˆˆ (coherentTopology C) X by
    change Nonempty _
    rw [â† Sieve.forallYonedaIsSheaf_iff_colimit]
    exact fun W => coherentTopology.isSheaf_yoneda_obj W _ hâ‚‚
  -- Show that a covering sieve is a colimit, which implies the original set of arrows is regular
  -- epimorphic. We use the transitivity property of saturation
  apply Coverage.Saturate.transitive X (Sieve.generate (Presieve.ofArrows Y Ï€))
  Â· apply Coverage.Saturate.of
    use Î±, inferInstance, Y, Ï€
  Â· intro V f âŸ¨Yâ‚, h, g, âŸ¨hY, hfâŸ©âŸ©
    rw [â† hf, Sieve.pullback_comp]
    apply (coherentTopology C).pullback_stable'
    apply coherentTopology.mem_sieves_of_hasEffectiveEpiFamily
    -- Need to show that the pullback of the family `Ï€_n` to a given `Y i` is effective epimorphic
    obtain âŸ¨iâŸ© := hY
    exact âŸ¨Î² i, inferInstance, Y_n i, Ï€_n i, H i, fun b â†¦
      âŸ¨Y_n i b, (ðŸ™ _), Ï€_n i b â‰« Ï€ i, âŸ¨(âŸ¨i, bâŸ© : Î£ (i : Î±), Î² i)âŸ©, by simpâŸ©âŸ©

instance precoherentEffectiveEpiFamilyCompEffectiveEpis
    {Î± : Type} [Finite Î±] {Y Z : Î± â†’ C} (Ï€ : (a : Î±) â†’ (Y a âŸ¶ X)) [EffectiveEpiFamily Y Ï€]
    (f : (a : Î±) â†’ Z a âŸ¶ Y a) [h : âˆ€ a, EffectiveEpi (f a)] :
    EffectiveEpiFamily _ fun a â†¦ f a â‰« Ï€ a := by
  simp_rw [effectiveEpi_iff_effectiveEpiFamily] at h
  exact EffectiveEpiFamily.reindex (e := Equiv.sigmaPUnit Î±) _ _
    (EffectiveEpiFamily.transitive_of_finite (Î² := fun _ â†¦ Unit) _ inferInstance _ h)

/--
A sieve belongs to the coherent topology if and only if it contains a finite
`EffectiveEpiFamily`.
-/
theorem coherentTopology.mem_sieves_iff_hasEffectiveEpiFamily (S : Sieve X) :
    (S âˆˆ (coherentTopology C) X) â†”
    (âˆƒ (Î± : Type) (_ : Finite Î±) (Y : Î± â†’ C) (Ï€ : (a : Î±) â†’ (Y a âŸ¶ X)),
        EffectiveEpiFamily Y Ï€ âˆ§ (âˆ€ a : Î±, (S.arrows) (Ï€ a)) )  := by
  constructor
  Â· intro h
    induction h with
    | of Y T hS =>
      obtain âŸ¨a, h, Y', Ï€, h', _âŸ© := hS
      refine âŸ¨a, h, Y', Ï€, inferInstance, fun a' â†¦ ?_âŸ©
      obtain âŸ¨rfl, _âŸ© := h'
      exact âŸ¨Y' a', ðŸ™ Y' a', Ï€ a', Presieve.ofArrows.mk a', by simpâŸ©
    | top Y =>
      exact âŸ¨Unit, inferInstance, fun _ => Y, fun _ => (ðŸ™ Y), inferInstance, by simpâŸ©
    | transitive Y R S _ _ a b =>
      obtain âŸ¨Î±, w, Yâ‚, Ï€, âŸ¨hâ‚,hâ‚‚âŸ©âŸ© := a
      choose Î² _ Y_n Ï€_n H using fun a => b (hâ‚‚ a)
      exact âŸ¨(Î£ a, Î² a), inferInstance, fun âŸ¨a,bâŸ© => Y_n a b, fun âŸ¨a, bâŸ© => (Ï€_n a b) â‰« (Ï€ a),
        EffectiveEpiFamily.transitive_of_finite _ hâ‚ _ (fun a => (H a).1),
        fun c => (H c.fst).2 c.sndâŸ©
  Â· exact coherentTopology.mem_sieves_of_hasEffectiveEpiFamily S

end CategoryTheory
