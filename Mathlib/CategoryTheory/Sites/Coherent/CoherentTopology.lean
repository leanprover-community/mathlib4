/-
Copyright (c) 2023 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz, Nikolas Kuhn
-/
import Mathlib.CategoryTheory.Sites.Coherent.CoherentSheaves
/-!

# Description of the covering sieves of the coherent topology

This file characterises the covering sieves of the coherent topology.

## Main result

* `coherentTopology.mem_sieves_iff_hasEffectiveEpiFamily`: a sieve is a covering sieve for the
  coherent topology if and only if it contains a finite effective epimorphic family.

-/

namespace CategoryTheory

variable {C : Type*} [Category C] [Precoherent C] {X : C}

/--
For a precoherent category, any sieve that contains an `EffectiveEpiFamily` is a sieve of the
coherent topology.
Note: This is one direction of `mem_sieves_iff_hasEffectiveEpiFamily`, but is needed for the proof.
-/
theorem coherentTopology.mem_sieves_of_hasEffectiveEpiFamily (S : Sieve X) :
    (‚àÉ (Œ± : Type) (_ : Finite Œ±) (Y : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (Y a ‚ü∂ X)),
      EffectiveEpiFamily Y œÄ ‚àß (‚àÄ a : Œ±, (S.arrows) (œÄ a)) ) ‚Üí
        (S ‚àà (coherentTopology C) X) := by
  intro ‚ü®Œ±, _, Y, œÄ, hœÄ‚ü©
  apply (coherentCoverage C).mem_toGrothendieck_sieves_of_superset (R := Presieve.ofArrows Y œÄ)
  ¬∑ exact fun _ _ h ‚Ü¶ by cases h; exact hœÄ.2 _
  ¬∑ exact ‚ü®_, inferInstance, Y, œÄ, rfl, hœÄ.1‚ü©

/--
Effective epi families in a precoherent category are transitive, in the sense that an
`EffectiveEpiFamily` and an `EffectiveEpiFamily` over each member, the composition is an
`EffectiveEpiFamily`.
Note: The finiteness condition is an artifact of the proof and is probably unnecessary.
-/
theorem EffectiveEpiFamily.transitive_of_finite {Œ± : Type} [Finite Œ±] {Y : Œ± ‚Üí C}
    (œÄ : (a : Œ±) ‚Üí (Y a ‚ü∂ X)) (h : EffectiveEpiFamily Y œÄ) {Œ≤ : Œ± ‚Üí Type} [‚àÄ (a : Œ±), Finite (Œ≤ a)]
    {Y_n : (a : Œ±) ‚Üí Œ≤ a ‚Üí C} (œÄ_n : (a : Œ±) ‚Üí (b : Œ≤ a) ‚Üí (Y_n a b ‚ü∂ Y a))
    (H : ‚àÄ a, EffectiveEpiFamily (Y_n a) (œÄ_n a)) :
    EffectiveEpiFamily
      (fun (c : Œ£ a, Œ≤ a) ‚Ü¶ Y_n c.fst c.snd) (fun c ‚Ü¶ œÄ_n c.fst c.snd ‚â´ œÄ c.fst) := by
  rw [‚Üê Sieve.effectiveEpimorphic_family]
  suffices h‚ÇÇ : (Sieve.generate (Presieve.ofArrows (fun (‚ü®a, b‚ü© : Œ£ _, Œ≤ _) ‚Ü¶ Y_n a b)
        (fun ‚ü®a,b‚ü© ‚Ü¶ œÄ_n a b ‚â´ œÄ a))) ‚àà (coherentTopology C) X by
    change Nonempty _
    rw [‚Üê Sieve.forallYonedaIsSheaf_iff_colimit]
    exact fun W ‚Ü¶ coherentTopology.isSheaf_yoneda_obj W _ h‚ÇÇ
  -- Show that a covering sieve is a colimit, which implies the original set of arrows is regular
  -- epimorphic. We use the transitivity property of saturation
  apply Coverage.Saturate.transitive X (Sieve.generate (Presieve.ofArrows Y œÄ))
  ¬∑ apply Coverage.Saturate.of
    use Œ±, inferInstance, Y, œÄ
  ¬∑ intro V f ‚ü®Y‚ÇÅ, h, g, ‚ü®hY, hf‚ü©‚ü©
    rw [‚Üê hf, Sieve.pullback_comp]
    apply (coherentTopology C).pullback_stable'
    apply coherentTopology.mem_sieves_of_hasEffectiveEpiFamily
    -- Need to show that the pullback of the family `œÄ_n` to a given `Y i` is effective epimorphic
    obtain ‚ü®i‚ü© := hY
    exact ‚ü®Œ≤ i, inferInstance, Y_n i, œÄ_n i, H i, fun b ‚Ü¶
      ‚ü®Y_n i b, (ùüô _), œÄ_n i b ‚â´ œÄ i, ‚ü®(‚ü®i, b‚ü© : Œ£ (i : Œ±), Œ≤ i)‚ü©, by simp‚ü©‚ü©

instance precoherentEffectiveEpiFamilyCompEffectiveEpis
    {Œ± : Type} [Finite Œ±] {Y Z : Œ± ‚Üí C} (œÄ : (a : Œ±) ‚Üí (Y a ‚ü∂ X)) [EffectiveEpiFamily Y œÄ]
    (f : (a : Œ±) ‚Üí Z a ‚ü∂ Y a) [h : ‚àÄ a, EffectiveEpi (f a)] :
    EffectiveEpiFamily _ fun a ‚Ü¶ f a ‚â´ œÄ a := by
  simp_rw [effectiveEpi_iff_effectiveEpiFamily] at h
  exact EffectiveEpiFamily.reindex (e := Equiv.sigmaPUnit Œ±) _ _
    (EffectiveEpiFamily.transitive_of_finite (Œ≤ := fun _ ‚Ü¶ Unit) _ inferInstance _ h)

/--
A sieve belongs to the coherent topology if and only if it contains a finite
`EffectiveEpiFamily`.
-/
theorem coherentTopology.mem_sieves_iff_hasEffectiveEpiFamily (S : Sieve X) :
    (S ‚àà (coherentTopology C) X) ‚Üî
    (‚àÉ (Œ± : Type) (_ : Finite Œ±) (Y : Œ± ‚Üí C) (œÄ : (a : Œ±) ‚Üí (Y a ‚ü∂ X)),
        EffectiveEpiFamily Y œÄ ‚àß (‚àÄ a : Œ±, (S.arrows) (œÄ a)) )  := by
  constructor
  ¬∑ intro h
    induction h with
    | of Y T hS =>
      obtain ‚ü®a, h, Y', œÄ, h', _‚ü© := hS
      refine ‚ü®a, h, Y', œÄ, inferInstance, fun a' ‚Ü¶ ?_‚ü©
      obtain ‚ü®rfl, _‚ü© := h'
      exact ‚ü®Y' a', ùüô Y' a', œÄ a', Presieve.ofArrows.mk a', by simp‚ü©
    | top Y =>
      exact ‚ü®Unit, inferInstance, fun _ ‚Ü¶ Y, fun _ ‚Ü¶ (ùüô Y), inferInstance, by simp‚ü©
    | transitive Y R S _ _ a b =>
      obtain ‚ü®Œ±, w, Y‚ÇÅ, œÄ, ‚ü®h‚ÇÅ,h‚ÇÇ‚ü©‚ü© := a
      choose Œ≤ _ Y_n œÄ_n H using fun a ‚Ü¶ b (h‚ÇÇ a)
      exact ‚ü®(Œ£ a, Œ≤ a), inferInstance, fun ‚ü®a,b‚ü© ‚Ü¶ Y_n a b, fun ‚ü®a, b‚ü© ‚Ü¶ (œÄ_n a b) ‚â´ (œÄ a),
        EffectiveEpiFamily.transitive_of_finite _ h‚ÇÅ _ (fun a ‚Ü¶ (H a).1),
        fun c ‚Ü¶ (H c.fst).2 c.snd‚ü©
  ¬∑ exact coherentTopology.mem_sieves_of_hasEffectiveEpiFamily S

end CategoryTheory
