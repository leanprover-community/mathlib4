/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.CategoryTheory.Functor.Flat
import Mathlib.CategoryTheory.Sites.Sheaf
import Mathlib.Tactic.ApplyFun

#align_import category_theory.sites.cover_preserving from "leanprover-community/mathlib"@"f0c8bf9245297a541f468be517f1bde6195105e9"
/-!
# Cover-preserving functors between sites.

We define cover-preserving functors between sites as functors that push covering sieves to
covering sieves. A cover-preserving and compatible-preserving functor `G : C ‚•§ D` then pulls
sheaves on `D` back to sheaves on `C` via `G.op ‚ãô -`.

## Main definitions

* `CategoryTheory.CoverPreserving`: a functor between sites is cover-preserving if it
pushes covering sieves to covering sieves
* `CategoryTheory.CompatiblePreserving`: a functor between sites is compatible-preserving
if it pushes compatible families of elements to compatible families.
* `CategoryTheory.pullbackSheaf`: the pullback of a sheaf along a cover-preserving and
compatible-preserving functor.
* `CategoryTheory.Sites.pullback`: the induced functor `Sheaf K A ‚•§ Sheaf J A` for a
cover-preserving and compatible-preserving functor `G : (C, J) ‚•§ (D, K)`.

## Main results

- `CategoryTheory.pullback_isSheaf_of_coverPreserving`: If `G : C ‚•§ D` is
cover-preserving and compatible-preserving, then `G ‚ãô -` (`u·µñ`) as a functor
`(D·µí·µñ ‚•§ A) ‚•§ (C·µí·µñ ‚•§ A)` of presheaves maps sheaves to sheaves.

## References

* [Elephant]: *Sketches of an Elephant*, P. T. Johnstone: C2.3.
* https://stacks.math.columbia.edu/tag/00WW

-/


universe w v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

noncomputable section

open CategoryTheory Opposite CategoryTheory.Presieve.FamilyOfElements CategoryTheory.Presieve
  CategoryTheory.Limits

namespace CategoryTheory

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]

variable {A : Type u‚ÇÉ} [Category.{v‚ÇÉ} A]

variable (J : GrothendieckTopology C) (K : GrothendieckTopology D)

variable {L : GrothendieckTopology A}

/-- A functor `G : (C, J) ‚•§ (D, K)` between sites is *cover-preserving*
if for all covering sieves `R` in `C`, `R.functorPushforward G` is a covering sieve in `D`.
-/
-- porting note: removed `@[nolint has_nonempty_instance]`
structure CoverPreserving (G : C ‚•§ D) : Prop where
  cover_preserve : ‚àÄ {U : C} {S : Sieve U} (_ : S ‚àà J U), S.functorPushforward G ‚àà K (G.obj U)
#align category_theory.cover_preserving CategoryTheory.CoverPreserving

/-- The identity functor on a site is cover-preserving. -/
theorem idCoverPreserving : CoverPreserving J J (ùü≠ _) :=
  ‚ü®fun hS => by simpa using hS‚ü©
#align category_theory.id_cover_preserving CategoryTheory.idCoverPreserving

/-- The composition of two cover-preserving functors is cover-preserving. -/
theorem CoverPreserving.comp {F} (hF : CoverPreserving J K F) {G} (hG : CoverPreserving K L G) :
    CoverPreserving J L (F ‚ãô G) :=
  ‚ü®fun hS => by
    rw [Sieve.functorPushforward_comp]
    exact hG.cover_preserve (hF.cover_preserve hS)‚ü©
#align category_theory.cover_preserving.comp CategoryTheory.CoverPreserving.comp

/-- A functor `G : (C, J) ‚•§ (D, K)` between sites is called compatible preserving if for each
compatible family of elements at `C` and valued in `G.op ‚ãô ‚Ñ±`, and each commuting diagram
`f‚ÇÅ ‚â´ G.map g‚ÇÅ = f‚ÇÇ ‚â´ G.map g‚ÇÇ`, `x g‚ÇÅ` and `x g‚ÇÇ` coincide when restricted via `f·µ¢`.
This is actually stronger than merely preserving compatible families because of the definition of
`functorPushforward` used.
-/
-- porting note: this doesn't work yet @[nolint has_nonempty_instance]
structure CompatiblePreserving (K : GrothendieckTopology D) (G : C ‚•§ D) : Prop where
  Compatible :
    ‚àÄ (‚Ñ± : SheafOfTypes.{w} K) {Z} {T : Presieve Z} {x : FamilyOfElements (G.op ‚ãô ‚Ñ±.val) T}
      (_ : x.Compatible) {Y‚ÇÅ Y‚ÇÇ} {X} (f‚ÇÅ : X ‚ü∂ G.obj Y‚ÇÅ) (f‚ÇÇ : X ‚ü∂ G.obj Y‚ÇÇ) {g‚ÇÅ : Y‚ÇÅ ‚ü∂ Z}
      {g‚ÇÇ : Y‚ÇÇ ‚ü∂ Z} (hg‚ÇÅ : T g‚ÇÅ) (hg‚ÇÇ : T g‚ÇÇ) (_ : f‚ÇÅ ‚â´ G.map g‚ÇÅ = f‚ÇÇ ‚â´ G.map g‚ÇÇ),
      ‚Ñ±.val.map f‚ÇÅ.op (x g‚ÇÅ hg‚ÇÅ) = ‚Ñ±.val.map f‚ÇÇ.op (x g‚ÇÇ hg‚ÇÇ)
#align category_theory.compatible_preserving CategoryTheory.CompatiblePreserving

variable {J K} {G : C ‚•§ D} (hG : CompatiblePreserving.{w} K G) (‚Ñ± : SheafOfTypes.{w} K) {Z : C}

variable {T : Presieve Z} {x : FamilyOfElements (G.op ‚ãô ‚Ñ±.val) T} (h : x.Compatible)

/-- `CompatiblePreserving` functors indeed preserve compatible families. -/
theorem Presieve.FamilyOfElements.Compatible.functorPushforward :
    (x.functorPushforward G).Compatible := by
  rintro Z‚ÇÅ Z‚ÇÇ W g‚ÇÅ g‚ÇÇ f‚ÇÅ' f‚ÇÇ' H‚ÇÅ H‚ÇÇ eq
  unfold FamilyOfElements.functorPushforward
  rcases getFunctorPushforwardStructure H‚ÇÅ with ‚ü®X‚ÇÅ, f‚ÇÅ, h‚ÇÅ, hf‚ÇÅ, rfl‚ü©
  rcases getFunctorPushforwardStructure H‚ÇÇ with ‚ü®X‚ÇÇ, f‚ÇÇ, h‚ÇÇ, hf‚ÇÇ, rfl‚ü©
  suffices ‚Ñ±.val.map (g‚ÇÅ ‚â´ h‚ÇÅ).op (x f‚ÇÅ hf‚ÇÅ) = ‚Ñ±.val.map (g‚ÇÇ ‚â´ h‚ÇÇ).op (x f‚ÇÇ hf‚ÇÇ) by
    simpa using this
  apply hG.Compatible ‚Ñ± h _ _ hf‚ÇÅ hf‚ÇÇ
  simpa using eq
#align category_theory.presieve.family_of_elements.compatible.functor_pushforward CategoryTheory.Presieve.FamilyOfElements.Compatible.functorPushforward

@[simp]
theorem CompatiblePreserving.apply_map {Y : C} {f : Y ‚ü∂ Z} (hf : T f) :
    x.functorPushforward G (G.map f) (image_mem_functorPushforward G T hf) = x f hf := by
  unfold FamilyOfElements.functorPushforward
  rcases e‚ÇÅ : getFunctorPushforwardStructure (image_mem_functorPushforward G T hf) with
    ‚ü®X, g, f', hg, eq‚ü©
  simpa using hG.Compatible ‚Ñ± h f' (ùüô _) hg hf (by simp [eq])
#align category_theory.compatible_preserving.apply_map CategoryTheory.CompatiblePreserving.apply_map

open Limits.WalkingCospan

theorem compatiblePreservingOfFlat {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D]
    (K : GrothendieckTopology D) (G : C ‚•§ D) [RepresentablyFlat G] : CompatiblePreserving K G := by
  constructor
  intro ‚Ñ± Z T x hx Y‚ÇÅ Y‚ÇÇ X f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ hg‚ÇÅ hg‚ÇÇ e
  -- First, `f‚ÇÅ` and `f‚ÇÇ` form a cone over `cospan g‚ÇÅ g‚ÇÇ ‚ãô u`.
  let c : Cone (cospan g‚ÇÅ g‚ÇÇ ‚ãô G) :=
    (Cones.postcompose (diagramIsoCospan (cospan g‚ÇÅ g‚ÇÇ ‚ãô G)).inv).obj (PullbackCone.mk f‚ÇÅ f‚ÇÇ e)
  /-
    This can then be viewed as a cospan of structured arrows, and we may obtain an arbitrary cone
    over it since `StructuredArrow W u` is cofiltered.
    Then, it suffices to prove that it is compatible when restricted onto `u(c'.X.right)`.
    -/
  let c' := IsCofiltered.cone (c.toStructuredArrow ‚ãô StructuredArrow.pre _ _ _)
  have eq‚ÇÅ : f‚ÇÅ = (c'.pt.hom ‚â´ G.map (c'.œÄ.app left).right) ‚â´ eqToHom (by simp) := by
    erw [‚Üê (c'.œÄ.app left).w]
    dsimp
    simp
  have eq‚ÇÇ : f‚ÇÇ = (c'.pt.hom ‚â´ G.map (c'.œÄ.app right).right) ‚â´ eqToHom (by simp) := by
    erw [‚Üê (c'.œÄ.app right).w]
    dsimp
    simp
  conv_lhs => rw [eq‚ÇÅ]
  conv_rhs => rw [eq‚ÇÇ]
  simp only [op_comp, Functor.map_comp, types_comp_apply, eqToHom_op, eqToHom_map]
  apply congr_arg -- porting note: was `congr 1` which for some reason doesn't do anything here
  -- despite goal being of the form f a = f b, with f=`‚Ñ±.val.map (Quiver.Hom.op c'.pt.hom)`
  /-
    Since everything now falls in the image of `u`,
    the result follows from the compatibility of `x` in the image of `u`.
    -/
  injection c'.œÄ.naturality WalkingCospan.Hom.inl with _ e‚ÇÅ
  injection c'.œÄ.naturality WalkingCospan.Hom.inr with _ e‚ÇÇ
  exact hx (c'.œÄ.app left).right (c'.œÄ.app right).right hg‚ÇÅ hg‚ÇÇ (e‚ÇÅ.symm.trans e‚ÇÇ)
#align category_theory.compatible_preserving_of_flat CategoryTheory.compatiblePreservingOfFlat

theorem compatiblePreservingOfDownwardsClosed (F : C ‚•§ D) [Full F] [Faithful F]
    (hF : ‚àÄ {c : C} {d : D} (_ : d ‚ü∂ F.obj c), Œ£c', F.obj c' ‚âÖ d) : CompatiblePreserving K F := by
  constructor
  introv hx he
  obtain ‚ü®X', e‚ü© := hF f‚ÇÅ
  apply (‚Ñ±.1.mapIso e.op).toEquiv.injective
  simp only [Iso.op_hom, Iso.toEquiv_fun, ‚Ñ±.1.mapIso_hom, ‚Üê FunctorToTypes.map_comp_apply]
  simpa using
    hx (F.preimage <| e.hom ‚â´ f‚ÇÅ) (F.preimage <| e.hom ‚â´ f‚ÇÇ) hg‚ÇÅ hg‚ÇÇ
      (F.map_injective <| by simpa using he)
#align category_theory.compatible_preserving_of_downwards_closed CategoryTheory.compatiblePreservingOfDownwardsClosed

/-- If `G` is cover-preserving and compatible-preserving,
then `G.op ‚ãô _` pulls sheaves back to sheaves.

This result is basically <https://stacks.math.columbia.edu/tag/00WW>.
-/
theorem pullback_isSheaf_of_coverPreserving {G : C ‚•§ D} (hG‚ÇÅ : CompatiblePreserving.{v‚ÇÉ} K G)
    (hG‚ÇÇ : CoverPreserving J K G) (‚Ñ± : Sheaf K A) : Presheaf.IsSheaf J (G.op ‚ãô ‚Ñ±.val) := by
  intro X U S hS x hx
  change FamilyOfElements (G.op ‚ãô ‚Ñ±.val ‚ãô coyoneda.obj (op X)) _ at x
  let H := ‚Ñ±.2 X _ (hG‚ÇÇ.cover_preserve hS)
  let hx' := hx.functorPushforward hG‚ÇÅ (sheafOver ‚Ñ± X)
  constructor; swap
  ¬∑ apply H.amalgamate (x.functorPushforward G)
    exact hx'
  constructor
  ¬∑ intro V f hf
    convert H.isAmalgamation hx' (G.map f) (image_mem_functorPushforward G S hf)
    rw [hG‚ÇÅ.apply_map (sheafOver ‚Ñ± X) hx]
  ¬∑ intro y hy
    refine'
      H.isSeparatedFor _ y _ _ (H.isAmalgamation (hx.functorPushforward hG‚ÇÅ (sheafOver ‚Ñ± X)))
    rintro V f ‚ü®Z, f', g', h, rfl‚ü©
    -- porting note: didn't need coercion (S : Presieve U) in Lean 3
    erw [FamilyOfElements.comp_of_compatible (S.functorPushforward G) hx'
        (image_mem_functorPushforward G (S : Presieve U) h) g']
    dsimp
    simp [hG‚ÇÅ.apply_map (sheafOver ‚Ñ± X) hx h, ‚Üê hy f' h]
#align category_theory.pullback_is_sheaf_of_cover_preserving CategoryTheory.pullback_isSheaf_of_coverPreserving

/-- The pullback of a sheaf along a cover-preserving and compatible-preserving functor. -/
def pullbackSheaf {G : C ‚•§ D} (hG‚ÇÅ : CompatiblePreserving K G) (hG‚ÇÇ : CoverPreserving J K G)
    (‚Ñ± : Sheaf K A) : Sheaf J A :=
  ‚ü®G.op ‚ãô ‚Ñ±.val, pullback_isSheaf_of_coverPreserving hG‚ÇÅ hG‚ÇÇ ‚Ñ±‚ü©
#align category_theory.pullback_sheaf CategoryTheory.pullbackSheaf

variable (A)

/-- The induced functor from `Sheaf K A ‚•§ Sheaf J A` given by `G.op ‚ãô _`
if `G` is cover-preserving and compatible-preserving.
-/
@[simps]
def Sites.pullback {G : C ‚•§ D} (hG‚ÇÅ : CompatiblePreserving K G) (hG‚ÇÇ : CoverPreserving J K G) :
    Sheaf K A ‚•§ Sheaf J A where
  obj ‚Ñ± := pullbackSheaf hG‚ÇÅ hG‚ÇÇ ‚Ñ±
  map f := ‚ü®((whiskeringLeft _ _ _).obj G.op).map f.val‚ü©
  map_id ‚Ñ± := by
    ext1
    apply ((whiskeringLeft _ _ _).obj G.op).map_id
  map_comp f g := by
    ext1
    apply ((whiskeringLeft _ _ _).obj G.op).map_comp
#align category_theory.sites.pullback CategoryTheory.Sites.pullback

end CategoryTheory
