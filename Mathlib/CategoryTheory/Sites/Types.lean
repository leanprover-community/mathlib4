/-
Copyright (c) 2020 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/
import Mathlib.CategoryTheory.Sites.Canonical

/-!
# Grothendieck Topology and Sheaves on the Category of Types

In this file we define a Grothendieck topology on the category of types,
and construct the canonical functor that sends a type to a sheaf over
the category of types, and make this an equivalence of categories.

Then we prove that the topology defined is the canonical topology.
-/


universe u

namespace CategoryTheory

/-- A Grothendieck topology associated to the category of all types.
A sieve is a covering iff it is jointly surjective. -/
def typesGrothendieckTopology : GrothendieckTopology (Type u) where
  sieves Î± S := âˆ€ x : Î±, S fun _ : PUnit â†¦ x
  top_mem' _ _ := trivial
  pullback_stable' _ _ _ f hs x := hs (f x)
  transitive' _ _ hs _ hr x := hr (hs x) PUnit.unit

/-- The discrete sieve on a type, which only includes arrows whose image is a subsingleton. -/
@[simps]
def discreteSieve (Î± : Type u) : Sieve Î± where
  arrows _ f := âˆƒ x, âˆ€ y, f y = x
  downward_closed := fun âŸ¨x, hxâŸ© g â†¦ âŸ¨x, fun y â†¦ hx <| g yâŸ©

theorem discreteSieve_mem (Î± : Type u) : discreteSieve Î± âˆˆ typesGrothendieckTopology Î± :=
  fun x â†¦ âŸ¨x, fun _ â†¦ rflâŸ©

/-- The discrete presieve on a type, which only includes arrows whose domain is a singleton. -/
def discretePresieve (Î± : Type u) : Presieve Î± :=
  fun Î² _ â†¦ âˆƒ x : Î², âˆ€ y : Î², y = x

theorem generate_discretePresieve_mem (Î± : Type u) :
    Sieve.generate (discretePresieve Î±) âˆˆ typesGrothendieckTopology Î± :=
  fun x â†¦ âŸ¨PUnit, id, fun _ â†¦ x, âŸ¨PUnit.unit, fun _ â†¦ Subsingleton.elim _ _âŸ©, rflâŸ©

/-- The sheaf condition for `yoneda'`. -/
theorem Presieve.isSheaf_yoneda' {Î± : Type u} :
    Presieve.IsSheaf typesGrothendieckTopology (yoneda.obj Î±) :=
  fun Î² _ hs x hx â†¦
  âŸ¨fun y â†¦ x _ (hs y) PUnit.unit, fun Î³ f h â†¦
    funext fun z â†¦ by
      convert congr_fun (hx (ðŸ™ _) (fun _ â†¦ z) (hs <| f z) h rfl) PUnit.unit using 1,
    fun f hf â†¦ funext fun y â†¦ by convert congr_fun (hf _ (hs y)) PUnit.unitâŸ©

/-- The sheaf condition for `yoneda'`. -/
theorem Presheaf.isSheaf_yoneda' {Î± : Type u} :
    Presheaf.IsSheaf typesGrothendieckTopology (yoneda.obj Î±) := by
  rw [isSheaf_iff_isSheaf_of_type]
  exact Presieve.isSheaf_yoneda'

/-- The yoneda functor that sends a type to a sheaf over the category of types. -/
@[simps]
def yoneda' : Type u â¥¤ Sheaf typesGrothendieckTopology (Type u) where
  obj Î± := âŸ¨yoneda.obj Î±, Presheaf.isSheaf_yoneda'âŸ©
  map f := âŸ¨yoneda.map fâŸ©

@[simp]
theorem yoneda'_comp : yoneda'.{u} â‹™ sheafToPresheaf _ _ = yoneda :=
  rfl

open Opposite

/-- Given a presheaf `P` on the category of types, construct
a map `P(Î±) â†’ (Î± â†’ P(*))` for all type `Î±`. -/
def eval (P : Type uáµ’áµ– â¥¤ Type u) (Î± : Type u) (s : P.obj (op Î±)) (x : Î±) : P.obj (op PUnit) :=
  P.map (â†¾fun _ â†¦ x).op s

open Presieve

/-- Given a sheaf `S` on the category of types, construct a map
`(Î± â†’ S(*)) â†’ S(Î±)` that is inverse to `eval`. -/
noncomputable def typesGlue (S : Type uáµ’áµ– â¥¤ Type u) (hs : IsSheaf typesGrothendieckTopology S)
    (Î± : Type u) (f : Î± â†’ S.obj (op PUnit)) : S.obj (op Î±) :=
  (hs.isSheafFor _ _ (generate_discretePresieve_mem Î±)).amalgamate
    (fun _ g hg â†¦ S.map (â†¾fun _ â†¦ PUnit.unit).op <| f <| g <| Classical.choose hg)
    fun Î² Î³ Î´ gâ‚ gâ‚‚ fâ‚ fâ‚‚ hfâ‚ hfâ‚‚ h â†¦
    (hs.isSheafFor _ _ (generate_discretePresieve_mem Î´)).isSeparatedFor.ext fun Îµ g âŸ¨x, _âŸ© â†¦ by
      have : fâ‚ (Classical.choose hfâ‚) = fâ‚‚ (Classical.choose hfâ‚‚) :=
        Classical.choose_spec hfâ‚ (gâ‚ <| g x) â–¸
          Classical.choose_spec hfâ‚‚ (gâ‚‚ <| g x) â–¸ congr_fun h _
      simp_rw [â† FunctorToTypes.map_comp_apply, this, â† op_comp]
      rfl

theorem eval_typesGlue {S hs Î±} (f) : eval.{u} S Î± (typesGlue S hs Î± f) = f := by
  funext x
  apply (IsSheafFor.valid_glue _ _ _ <| âŸ¨PUnit.unit, fun _ â†¦ Subsingleton.elim _ _âŸ©).trans
  convert FunctorToTypes.map_id_apply S _

theorem typesGlue_eval {S hs Î±} (s) : typesGlue.{u} S hs Î± (eval S Î± s) = s := by
  apply (hs.isSheafFor _ _ (generate_discretePresieve_mem Î±)).isSeparatedFor.ext
  intro Î² f hf
  apply (IsSheafFor.valid_glue _ _ _ hf).trans
  apply (FunctorToTypes.map_comp_apply _ _ _ _).symm.trans
  rw [â† op_comp]
  --congr 2 -- Porting note: This tactic didn't work. Find an alternative.
  suffices ((â†¾fun _ â†¦ PUnit.unit) â‰« â†¾fun _ â†¦ f (Classical.choose hf)) = f by rw [this]
  funext x
  exact congr_arg f (Classical.choose_spec hf x).symm

/-- Given a sheaf `S`, construct an equivalence `S(Î±) â‰ƒ (Î± â†’ S(*))`. -/
@[simps]
noncomputable def evalEquiv (S : Type uáµ’áµ– â¥¤ Type u)
    (hs : Presheaf.IsSheaf typesGrothendieckTopology S)
    (Î± : Type u) : S.obj (op Î±) â‰ƒ (Î± â†’ S.obj (op PUnit)) where
  toFun := eval S Î±
  invFun := typesGlue S ((isSheaf_iff_isSheaf_of_type _ _ ).1 hs) Î±
  left_inv := typesGlue_eval
  right_inv := eval_typesGlue

theorem eval_map (S : Type uáµ’áµ– â¥¤ Type u) (Î± Î²) (f : Î² âŸ¶ Î±) (s x) :
    eval S Î² (S.map f.op s) x = eval S Î± s (f x) := by
  simp_rw [eval, â† FunctorToTypes.map_comp_apply, â† op_comp]; rfl

/-- Given a sheaf `S`, construct an isomorphism `S â‰… [-, S(*)]`. -/
@[simps!]
noncomputable def equivYoneda (S : Type uáµ’áµ– â¥¤ Type u)
    (hs : Presheaf.IsSheaf typesGrothendieckTopology S) :
    S â‰… yoneda.obj (S.obj (op PUnit)) :=
  NatIso.ofComponents (fun Î± â†¦ Equiv.toIso <| evalEquiv S hs <| unop Î±) fun {Î± Î²} f â†¦
    funext fun _ â†¦ funext fun _ â†¦ eval_map S (unop Î±) (unop Î²) f.unop _ _

/-- Given a sheaf `S`, construct an isomorphism `S â‰… [-, S(*)]`. -/
@[simps]
noncomputable def equivYoneda' (S : Sheaf typesGrothendieckTopology (Type u)) :
    S â‰… yoneda'.obj (S.1.obj (op PUnit)) where
  hom := âŸ¨(equivYoneda S.1 S.2).homâŸ©
  inv := âŸ¨(equivYoneda S.1 S.2).invâŸ©
  hom_inv_id := by ext1; apply (equivYoneda S.1 S.2).hom_inv_id
  inv_hom_id := by ext1; apply (equivYoneda S.1 S.2).inv_hom_id

theorem eval_app (Sâ‚ Sâ‚‚ : Sheaf typesGrothendieckTopology (Type u)) (f : Sâ‚ âŸ¶ Sâ‚‚) (Î± : Type u)
    (s : Sâ‚.1.obj (op Î±)) (x : Î±) :
    eval Sâ‚‚.1 Î± (f.val.app (op Î±) s) x = f.val.app (op PUnit) (eval Sâ‚.1 Î± s x) :=
  (congr_fun (f.val.naturality (â†¾fun _ : PUnit â†¦ x).op) s).symm

/-- `yoneda'` induces an equivalence of category between `Type u` and
`Sheaf typesGrothendieckTopology (Type u)`. -/
@[simps!]
noncomputable def typeEquiv : Type u â‰Œ Sheaf typesGrothendieckTopology (Type u) where
  functor := yoneda'
  inverse := sheafToPresheaf _ _ â‹™ (evaluation _ _).obj (op PUnit)
  unitIso := NatIso.ofComponents
      (fun _Î± â†¦ -- Î± â‰… PUnit âŸ¶ Î±
        { hom := fun x _ â†¦ x
          inv := fun f â†¦ f PUnit.unit
          hom_inv_id := funext fun _ â†¦ rfl
          inv_hom_id := funext fun _ â†¦ funext fun y â†¦ PUnit.casesOn y rfl })
      fun _ â†¦ rfl
  counitIso := Iso.symm <|
      NatIso.ofComponents (fun S â†¦ equivYoneda' S) (fun {Sâ‚ Sâ‚‚} f â†¦ by
        ext âŸ¨Î±âŸ© s
        dsimp at s âŠ¢
        ext x
        exact eval_app Sâ‚ Sâ‚‚ f Î± s x)
  functor_unitIso_comp X := by
    ext1
    apply yonedaEquiv.injective
    dsimp [yoneda', yonedaEquiv, evalEquiv]
    erw [typesGlue_eval]

instance subcanonical_typesGrothendieckTopology : typesGrothendieckTopology.{u}.Subcanonical :=
  GrothendieckTopology.Subcanonical.of_isSheaf_yoneda_obj _ fun _ â†¦ Presieve.isSheaf_yoneda'

theorem typesGrothendieckTopology_eq_canonical :
    typesGrothendieckTopology.{u} = Sheaf.canonicalTopology (Type u) := by
  refine le_antisymm typesGrothendieckTopology.le_canonical (sInf_le ?_)
  refine âŸ¨yoneda.obj (ULift Bool), âŸ¨_, rflâŸ©, GrothendieckTopology.ext ?_âŸ©
  funext Î±
  ext S
  refine âŸ¨fun hs x â†¦ ?_, fun hs Î² f â†¦ Presieve.isSheaf_yoneda' _ fun y â†¦ hs _âŸ©
  by_contra hsx
  have : (fun _ â†¦ ULift.up true) = fun _ â†¦ ULift.up false :=
    (hs PUnit fun _ â†¦ x).isSeparatedFor.ext
      fun Î² f hf â†¦ funext fun y â†¦ hsx.elim <| S.2 hf fun _ â†¦ y
  simp [funext_iff] at this

end CategoryTheory
