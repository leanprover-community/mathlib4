/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Sites.Sieves
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.Mono

/-!
# The sheaf condition for a presieve

We define what it means for a presheaf `P : C·µí·µñ ‚•§ Type v` to be a sheaf *for* a particular
presieve `R` on `X`:
* A *family of elements* `x` for `P` at `R` is an element `x_f` of `P Y` for every `f : Y ‚ü∂ X` in
  `R`. See `FamilyOfElements`.
* The family `x` is *compatible* if, for any `f‚ÇÅ : Y‚ÇÅ ‚ü∂ X` and `f‚ÇÇ : Y‚ÇÇ ‚ü∂ X` both in `R`,
  and any `g‚ÇÅ : Z ‚ü∂ Y‚ÇÅ` and `g‚ÇÇ : Z ‚ü∂ Y‚ÇÇ` such that `g‚ÇÅ ‚â´ f‚ÇÅ = g‚ÇÇ ‚â´ f‚ÇÇ`, the restriction of
  `x_f‚ÇÅ` along `g‚ÇÅ` agrees with the restriction of `x_f‚ÇÇ` along `g‚ÇÇ`.
  See `FamilyOfElements.Compatible`.
* An *amalgamation* `t` for the family is an element of `P X` such that for every `f : Y ‚ü∂ X` in
  `R`, the restriction of `t` on `f` is `x_f`.
  See `FamilyOfElements.IsAmalgamation`.
We then say `P` is *separated* for `R` if every compatible family has at most one amalgamation,
and it is a *sheaf* for `R` if every compatible family has a unique amalgamation.
See `IsSeparatedFor` and `IsSheafFor`.

In the special case where `R` is a sieve, the compatibility condition can be simplified:
* The family `x` is *compatible* if, for any `f : Y ‚ü∂ X` in `R` and `g : Z ‚ü∂ Y`, the restriction of
  `x_f` along `g` agrees with `x_(g ‚â´ f)` (which is well defined since `g ‚â´ f` is in `R`).
See `FamilyOfElements.SieveCompatible` and `compatible_iff_sieveCompatible`.

In the special case where `C` has pullbacks, the compatibility condition can be simplified:
* The family `x` is *compatible* if, for any `f : Y ‚ü∂ X` and `g : Z ‚ü∂ X` both in `R`,
  the restriction of `x_f` along `œÄ‚ÇÅ : pullback f g ‚ü∂ Y` agrees with the restriction of `x_g`
  along `œÄ‚ÇÇ : pullback f g ‚ü∂ Z`.
See `FamilyOfElements.PullbackCompatible` and `pullbackCompatible_iff`.

We also provide equivalent conditions to satisfy alternate definitions given in the literature.

* Stacks: The condition of https://stacks.math.columbia.edu/tag/00Z8 is virtually identical to the
  statement of `isSheafFor_iff_yonedaSheafCondition` (since the bijection described there carries
  the same information as the unique existence.)

* Maclane-Moerdijk [MM92]: Using `compatible_iff_sieveCompatible`, the definitions of `IsSheaf`
  are equivalent. There are also alternate definitions given:
  - Yoneda condition: Defined in `yonedaSheafCondition` and equivalence in
    `isSheafFor_iff_yonedaSheafCondition`.
  - Matching family for presieves with pullback: `pullbackCompatible_iff`.

## Implementation

The sheaf condition is given as a proposition, rather than a subsingleton in `Type (max u‚ÇÅ v)`.
This doesn't seem to make a big difference, other than making a couple of definitions noncomputable,
but it means that equivalent conditions can be given as `‚Üî` statements rather than `‚âÉ` statements,
which can be convenient.

## References

* [MM92]: *Sheaves in geometry and logic*, Saunders MacLane, and Ieke Moerdijk:
  Chapter III, Section 4.
* [Elephant]: *Sketches of an Elephant*, P. T. Johnstone: C2.1.
* https://stacks.math.columbia.edu/tag/00VL (sheaves on a pretopology or site)
* https://stacks.math.columbia.edu/tag/00ZB (sheaves on a topology)

-/


universe w w' v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory

open Opposite CategoryTheory Category Limits Sieve

namespace Presieve

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]
variable {P Q U : C·µí·µñ ‚•§ Type w}
variable {X Y : C} {S : Sieve X} {R : Presieve X}

/-- A family of elements for a presheaf `P` given a collection of arrows `R` with fixed codomain `X`
consists of an element of `P Y` for every `f : Y ‚ü∂ X` in `R`.
A presheaf is a sheaf (resp, separated) if every *compatible* family of elements has exactly one
(resp, at most one) amalgamation.

This data is referred to as a `family` in [MM92], Chapter III, Section 4. It is also a concrete
version of the elements of the middle object in the Stacks entry which is
more useful for direct calculations. It is also used implicitly in Definition C2.1.2 in [Elephant].
-/
@[stacks 00VM "This is a concrete version of the elements of the middle object there."]
def FamilyOfElements (P : C·µí·µñ ‚•§ Type w) (R : Presieve X) :=
  ‚àÄ ‚¶ÉY : C‚¶Ñ (f : Y ‚ü∂ X), R f ‚Üí P.obj (op Y)

instance : Inhabited (FamilyOfElements P (‚ä• : Presieve X)) :=
  ‚ü®fun _ _ => False.elim‚ü©

@[ext]
lemma FamilyOfElements.ext {R : Presieve X} {x y : R.FamilyOfElements P}
    (H : ‚àÄ {Y : C} (f : Y ‚ü∂ X) (hf : R f), x f hf = y f hf) :
    x = y := by
  funext Z f hf
  exact H f hf

/-- A family of elements for a presheaf on the presieve `R‚ÇÇ` can be restricted to a smaller presieve
`R‚ÇÅ`.
-/
def FamilyOfElements.restrict {R‚ÇÅ R‚ÇÇ : Presieve X} (h : R‚ÇÅ ‚â§ R‚ÇÇ) :
    FamilyOfElements P R‚ÇÇ ‚Üí FamilyOfElements P R‚ÇÅ := fun x _ f hf => x f (h _ hf)

/-- The image of a family of elements by a morphism of presheaves. -/
def FamilyOfElements.map (p : FamilyOfElements P R) (œÜ : P ‚ü∂ Q) :
    FamilyOfElements Q R :=
  fun _ f hf => œÜ.app _ (p f hf)

@[simp]
lemma FamilyOfElements.map_apply
    (p : FamilyOfElements P R) (œÜ : P ‚ü∂ Q) {Y : C} (f : Y ‚ü∂ X) (hf : R f) :
    p.map œÜ f hf = œÜ.app _ (p f hf) := rfl

lemma FamilyOfElements.restrict_map
    (p : FamilyOfElements P R) (œÜ : P ‚ü∂ Q) {R' : Presieve X} (h : R' ‚â§ R) :
    (p.restrict h).map œÜ = (p.map œÜ).restrict h := rfl

variable (P) in
/-- A family of elements on `{ f : X ‚ü∂ Y }` is an element of `F(X)`. -/
@[simps apply, simps -isSimp symm_apply]
def FamilyOfElements.singletonEquiv {X Y : C} (f : X ‚ü∂ Y) :
    (singleton f).FamilyOfElements P ‚âÉ P.obj (op X) where
  toFun x := x f (by simp)
  invFun x Z g hg := P.map (eqToHom <| by cases hg; rfl).op x
  left_inv x := by ext _ _ ‚ü®rfl‚ü©; simp
  right_inv x := by simp

@[simp]
lemma FamilyOfElements.singletonEquiv_symm_apply_self {X Y : C} (f : X ‚ü∂ Y) (x : P.obj (op X)) :
    (singletonEquiv P f).symm x f ‚ü®‚ü© = x := by
  simp [singletonEquiv_symm_apply]

/-- A family of elements for the arrow set `R` is *compatible* if for any `f‚ÇÅ : Y‚ÇÅ ‚ü∂ X` and
`f‚ÇÇ : Y‚ÇÇ ‚ü∂ X` in `R`, and any `g‚ÇÅ : Z ‚ü∂ Y‚ÇÅ` and `g‚ÇÇ : Z ‚ü∂ Y‚ÇÇ`, if the square `g‚ÇÅ ‚â´ f‚ÇÅ = g‚ÇÇ ‚â´ f‚ÇÇ`
commutes then the elements of `P Z` obtained by restricting the element of `P Y‚ÇÅ` along `g‚ÇÅ` and
restricting the element of `P Y‚ÇÇ` along `g‚ÇÇ` are the same.

In special cases, this condition can be simplified, see `pullbackCompatible_iff` and
`compatible_iff_sieveCompatible`.

This is referred to as a "compatible family" in Definition C2.1.2 of [Elephant], and on nlab:
https://ncatlab.org/nlab/show/sheaf#GeneralDefinitionInComponents

For a more explicit version in the case where `R` is of the form `Presieve.ofArrows`, see
`CategoryTheory.Presieve.Arrows.Compatible`.
-/
def FamilyOfElements.Compatible (x : FamilyOfElements P R) : Prop :=
  ‚àÄ ‚¶ÉY‚ÇÅ Y‚ÇÇ Z‚¶Ñ (g‚ÇÅ : Z ‚ü∂ Y‚ÇÅ) (g‚ÇÇ : Z ‚ü∂ Y‚ÇÇ) ‚¶Éf‚ÇÅ : Y‚ÇÅ ‚ü∂ X‚¶Ñ ‚¶Éf‚ÇÇ : Y‚ÇÇ ‚ü∂ X‚¶Ñ (h‚ÇÅ : R f‚ÇÅ) (h‚ÇÇ : R f‚ÇÇ),
    g‚ÇÅ ‚â´ f‚ÇÅ = g‚ÇÇ ‚â´ f‚ÇÇ ‚Üí P.map g‚ÇÅ.op (x f‚ÇÅ h‚ÇÅ) = P.map g‚ÇÇ.op (x f‚ÇÇ h‚ÇÇ)

/--
If the category `C` has pullbacks, this is an alternative condition for a family of elements to be
compatible: For any `f : Y ‚ü∂ X` and `g : Z ‚ü∂ X` in the presieve `R`, the restriction of the
given elements for `f` and `g` to the pullback agree.
This is equivalent to being compatible (provided `C` has pullbacks), shown in
`pullbackCompatible_iff`.

This is the definition for a "matching" family given in [MM92], Chapter III, Section 4,
Equation (5). Viewing the type `FamilyOfElements` as the middle object of the fork in
https://stacks.math.columbia.edu/tag/00VM, this condition expresses that `pr‚ÇÄ* (x) = pr‚ÇÅ* (x)`,
using the notation defined there.

For a more explicit version in the case where `R` is of the form `Presieve.ofArrows`, see
`CategoryTheory.Presieve.Arrows.PullbackCompatible`.
-/
def FamilyOfElements.PullbackCompatible (x : FamilyOfElements P R) [R.hasPullbacks] : Prop :=
  ‚àÄ ‚¶ÉY‚ÇÅ Y‚ÇÇ‚¶Ñ ‚¶Éf‚ÇÅ : Y‚ÇÅ ‚ü∂ X‚¶Ñ ‚¶Éf‚ÇÇ : Y‚ÇÇ ‚ü∂ X‚¶Ñ (h‚ÇÅ : R f‚ÇÅ) (h‚ÇÇ : R f‚ÇÇ),
    haveI := hasPullbacks.has_pullbacks h‚ÇÅ h‚ÇÇ
    P.map (pullback.fst f‚ÇÅ f‚ÇÇ).op (x f‚ÇÅ h‚ÇÅ) = P.map (pullback.snd f‚ÇÅ f‚ÇÇ).op (x f‚ÇÇ h‚ÇÇ)

theorem pullbackCompatible_iff (x : FamilyOfElements P R) [R.hasPullbacks] :
    x.Compatible ‚Üî x.PullbackCompatible := by
  constructor
  ¬∑ intro t Y‚ÇÅ Y‚ÇÇ f‚ÇÅ f‚ÇÇ hf‚ÇÅ hf‚ÇÇ
    apply t
    haveI := hasPullbacks.has_pullbacks hf‚ÇÅ hf‚ÇÇ
    apply pullback.condition
  ¬∑ intro t Y‚ÇÅ Y‚ÇÇ Z g‚ÇÅ g‚ÇÇ f‚ÇÅ f‚ÇÇ hf‚ÇÅ hf‚ÇÇ comm
    haveI := hasPullbacks.has_pullbacks hf‚ÇÅ hf‚ÇÇ
    rw [‚Üê pullback.lift_fst _ _ comm, op_comp, FunctorToTypes.map_comp_apply, t hf‚ÇÅ hf‚ÇÇ,
      ‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, pullback.lift_snd]

/-- The restriction of a compatible family is compatible. -/
theorem FamilyOfElements.Compatible.restrict {R‚ÇÅ R‚ÇÇ : Presieve X} (h : R‚ÇÅ ‚â§ R‚ÇÇ)
    {x : FamilyOfElements P R‚ÇÇ} : x.Compatible ‚Üí (x.restrict h).Compatible :=
  fun q _ _ _ g‚ÇÅ g‚ÇÇ _ _ h‚ÇÅ h‚ÇÇ comm => q g‚ÇÅ g‚ÇÇ (h _ h‚ÇÅ) (h _ h‚ÇÇ) comm

/-- Extend a family of elements to the sieve generated by an arrow set.
This is the construction described as "easy" in Lemma C2.1.3 of [Elephant].
-/
noncomputable def FamilyOfElements.sieveExtend (x : FamilyOfElements P R) :
    FamilyOfElements P (generate R : Presieve X) := fun _ _ hf =>
  P.map hf.choose_spec.choose.op (x _ hf.choose_spec.choose_spec.choose_spec.1)

/-- The extension of a compatible family to the generated sieve is compatible. -/
theorem FamilyOfElements.Compatible.sieveExtend {x : FamilyOfElements P R} (hx : x.Compatible) :
    x.sieveExtend.Compatible := by
  intro _ _ _ _ _ _ _ h‚ÇÅ h‚ÇÇ comm
  iterate 2 erw [‚Üê FunctorToTypes.map_comp_apply]; rw [‚Üê op_comp]
  apply hx
  simp [comm, h‚ÇÅ.choose_spec.choose_spec.choose_spec.2, h‚ÇÇ.choose_spec.choose_spec.choose_spec.2]

/-- The extension of a family agrees with the original family. -/
theorem extend_agrees {x : FamilyOfElements P R} (t : x.Compatible) {f : Y ‚ü∂ X} (hf : R f) :
    x.sieveExtend f (le_generate R Y hf) = x f hf := by
  have h := (le_generate R Y hf).choose_spec
  unfold FamilyOfElements.sieveExtend
  rw [t h.choose (ùüô _) _ hf _]
  ¬∑ simp
  ¬∑ rw [id_comp]
    exact h.choose_spec.choose_spec.2

/-- The restriction of an extension is the original. -/
@[simp]
theorem restrict_extend {x : FamilyOfElements P R} (t : x.Compatible) :
    x.sieveExtend.restrict (le_generate R) = x := by
  funext Y f hf
  exact extend_agrees t hf

/--
If the arrow set for a family of elements is actually a sieve (i.e. it is downward closed) then the
consistency condition can be simplified.
This is an equivalent condition, see `compatible_iff_sieveCompatible`.

This is the notion of "matching" given for families on sieves given in [MM92], Chapter III,
Section 4, Equation 1, and nlab: https://ncatlab.org/nlab/show/matching+family.
See also the discussion before Lemma C2.1.4 of [Elephant].
-/
def FamilyOfElements.SieveCompatible (x : FamilyOfElements P (S : Presieve X)) : Prop :=
  ‚àÄ ‚¶ÉY Z‚¶Ñ (f : Y ‚ü∂ X) (g : Z ‚ü∂ Y) (hf), x (g ‚â´ f) (S.downward_closed hf g) = P.map g.op (x f hf)

theorem compatible_iff_sieveCompatible (x : FamilyOfElements P (S : Presieve X)) :
    x.Compatible ‚Üî x.SieveCompatible := by
  constructor
  ¬∑ intro h Y Z f g hf
    simpa using h (ùüô _) g (S.downward_closed hf g) hf (id_comp _)
  ¬∑ intro h Y‚ÇÅ Y‚ÇÇ Z g‚ÇÅ g‚ÇÇ f‚ÇÅ f‚ÇÇ h‚ÇÅ h‚ÇÇ k
    simp_rw [‚Üê h f‚ÇÅ g‚ÇÅ h‚ÇÅ, ‚Üê h f‚ÇÇ g‚ÇÇ h‚ÇÇ]
    congr

theorem FamilyOfElements.Compatible.to_sieveCompatible {x : FamilyOfElements P (S : Presieve X)}
    (t : x.Compatible) : x.SieveCompatible :=
  (compatible_iff_sieveCompatible x).1 t

/--
Given a family of elements `x` for the sieve `S` generated by a presieve `R`, if `x` is restricted
to `R` and then extended back up to `S`, the resulting extension equals `x`.
-/
@[simp]
theorem extend_restrict {x : FamilyOfElements P (generate R).arrows} (t : x.Compatible) :
    (x.restrict (le_generate R)).sieveExtend = x := by
  rw [compatible_iff_sieveCompatible] at t
  funext _ _ h
  apply (t _ _ _).symm.trans
  congr
  exact h.choose_spec.choose_spec.choose_spec.2

/--
Two compatible families on the sieve generated by a presieve `R` are equal if and only if they are
equal when restricted to `R`.
-/
theorem restrict_inj {x‚ÇÅ x‚ÇÇ : FamilyOfElements P (generate R).arrows} (t‚ÇÅ : x‚ÇÅ.Compatible)
    (t‚ÇÇ : x‚ÇÇ.Compatible) : x‚ÇÅ.restrict (le_generate R) = x‚ÇÇ.restrict (le_generate R) ‚Üí x‚ÇÅ = x‚ÇÇ :=
  fun h => by
  rw [‚Üê extend_restrict t‚ÇÅ, ‚Üê extend_restrict t‚ÇÇ]
  -- Porting note: congr fails to make progress
  apply congr_arg
  exact h

/-- Compatible families of elements for a presheaf of types `P` and a presieve `R`
    are in 1-1 correspondence with compatible families for the same presheaf and
    the sieve generated by `R`, through extension and restriction. -/
@[simps]
noncomputable def compatibleEquivGenerateSieveCompatible :
    { x : FamilyOfElements P R // x.Compatible } ‚âÉ
      { x : FamilyOfElements P (generate R : Presieve X) // x.Compatible } where
  toFun x := ‚ü®x.1.sieveExtend, x.2.sieveExtend‚ü©
  invFun x := ‚ü®x.1.restrict (le_generate R), x.2.restrict _‚ü©
  left_inv x := Subtype.ext (restrict_extend x.2)
  right_inv x := Subtype.ext (extend_restrict x.2)

theorem FamilyOfElements.comp_of_compatible (S : Sieve X) {x : FamilyOfElements P S}
    (t : x.Compatible) {f : Y ‚ü∂ X} (hf : S f) {Z} (g : Z ‚ü∂ Y) :
    x (g ‚â´ f) (S.downward_closed hf g) = P.map g.op (x f hf) := by
  simpa using t (ùüô _) g (S.downward_closed hf g) hf (id_comp _)

lemma FamilyOfElements.compatible_singleton_iff
    {X Y : C} (f : X ‚ü∂ Y) (x : (singleton f).FamilyOfElements P) :
    x.Compatible ‚Üî ‚àÄ {Z : C} (p‚ÇÅ p‚ÇÇ : Z ‚ü∂ X), p‚ÇÅ ‚â´ f = p‚ÇÇ ‚â´ f ‚Üí
      P.map p‚ÇÅ.op (x f ‚ü®‚ü©) = P.map p‚ÇÇ.op (x f ‚ü®‚ü©) := by
  refine ‚ü®fun H Z p‚ÇÅ p‚ÇÇ h ‚Ü¶ H _ _ _ _ h, fun H Y‚ÇÅ Y‚ÇÇ Z g‚ÇÅ g‚ÇÇ f‚ÇÅ f‚ÇÇ ‚Ü¶ ?_‚ü©
  rintro ‚ü®rfl‚ü© ‚ü®rfl‚ü© h
  exact H _ _ h

section FunctorPullback

variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] (F : D ‚•§ C) {Z : D}
variable {T : Presieve (F.obj Z)} {x : FamilyOfElements P T}

/--
Given a family of elements of a sieve `S` on `F(X)`, we can realize it as a family of elements of
`S.functorPullback F`.
-/
def FamilyOfElements.functorPullback (x : FamilyOfElements P T) :
    FamilyOfElements (F.op ‚ãô P) (T.functorPullback F) := fun _ f hf => x (F.map f) hf

theorem FamilyOfElements.Compatible.functorPullback (h : x.Compatible) :
    (x.functorPullback F).Compatible := by
  intro Z‚ÇÅ Z‚ÇÇ W g‚ÇÅ g‚ÇÇ f‚ÇÅ f‚ÇÇ h‚ÇÅ h‚ÇÇ eq
  exact h (F.map g‚ÇÅ) (F.map g‚ÇÇ) h‚ÇÅ h‚ÇÇ (by simp only [‚Üê F.map_comp, eq])

end FunctorPullback

/-- Given a family of elements of a sieve `S` on `X` whose values factors through `F`, we can
realize it as a family of elements of `S.functorPushforward F`. Since the preimage is obtained by
choice, this is not well-defined generally.
-/
noncomputable def FamilyOfElements.functorPushforward {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] (F : D ‚•§ C)
    {X : D} {T : Presieve X} (x : FamilyOfElements (F.op ‚ãô P) T) :
    FamilyOfElements P (T.functorPushforward F) := fun Y f h => by
  obtain ‚ü®Z, g, h, h‚ÇÅ, _‚ü© := getFunctorPushforwardStructure h
  exact P.map h.op (x g h‚ÇÅ)

section Pullback

/-- Given a family of elements of a sieve `S` on `X`, and a map `Y ‚ü∂ X`, we can obtain a
family of elements of `S.pullback f` by taking the same elements.
-/
def FamilyOfElements.pullback (f : Y ‚ü∂ X) (x : FamilyOfElements P (S : Presieve X)) :
    FamilyOfElements P (S.pullback f : Presieve Y) := fun _ g hg => x (g ‚â´ f) hg

theorem FamilyOfElements.Compatible.pullback (f : Y ‚ü∂ X) {x : FamilyOfElements P S.arrows}
    (h : x.Compatible) : (x.pullback f).Compatible := by
  simp only [compatible_iff_sieveCompatible] at h ‚ä¢
  intro W Z f‚ÇÅ f‚ÇÇ hf
  unfold FamilyOfElements.pullback
  rw [‚Üê h (f‚ÇÅ ‚â´ f) f‚ÇÇ hf]
  congr 1
  simp only [assoc]

end Pullback

/-- Given a morphism of presheaves `f : P ‚ü∂ Q`, we can take a family of elements valued in `P` to a
family of elements valued in `Q` by composing with `f`.
-/
def FamilyOfElements.compPresheafMap (f : P ‚ü∂ Q) (x : FamilyOfElements P R) :
    FamilyOfElements Q R := fun Y g hg => f.app (op Y) (x g hg)

@[simp]
theorem FamilyOfElements.compPresheafMap_id (x : FamilyOfElements P R) :
    x.compPresheafMap (ùüô P) = x :=
  rfl

@[simp]
theorem FamilyOfElements.compPresheafMap_comp (x : FamilyOfElements P R) (f : P ‚ü∂ Q)
    (g : Q ‚ü∂ U) : (x.compPresheafMap f).compPresheafMap g = x.compPresheafMap (f ‚â´ g) :=
  rfl

theorem FamilyOfElements.Compatible.compPresheafMap (f : P ‚ü∂ Q) {x : FamilyOfElements P R}
    (h : x.Compatible) : (x.compPresheafMap f).Compatible := by
  intro Z‚ÇÅ Z‚ÇÇ W g‚ÇÅ g‚ÇÇ f‚ÇÅ f‚ÇÇ h‚ÇÅ h‚ÇÇ eq
  unfold FamilyOfElements.compPresheafMap
  rwa [‚Üê FunctorToTypes.naturality, ‚Üê FunctorToTypes.naturality, h]

/--
The given element `t` of `P.obj (op X)` is an *amalgamation* for the family of elements `x` if every
restriction `P.map f.op t = x_f` for every arrow `f` in the presieve `R`.

This is the definition given in https://ncatlab.org/nlab/show/sheaf#GeneralDefinitionInComponents,
and https://ncatlab.org/nlab/show/matching+family, as well as [MM92], Chapter III, Section 4,
equation (2).
-/
def FamilyOfElements.IsAmalgamation (x : FamilyOfElements P R) (t : P.obj (op X)) : Prop :=
  ‚àÄ ‚¶ÉY : C‚¶Ñ (f : Y ‚ü∂ X) (h : R f), P.map f.op t = x f h

theorem FamilyOfElements.IsAmalgamation.compPresheafMap {x : FamilyOfElements P R} {t} (f : P ‚ü∂ Q)
    (h : x.IsAmalgamation t) : (x.compPresheafMap f).IsAmalgamation (f.app (op X) t) := by
  intro Y g hg
  dsimp [FamilyOfElements.compPresheafMap]
  change (f.app _ ‚â´ Q.map _) _ = _
  rw [‚Üê f.naturality, types_comp_apply, h g hg]

theorem is_compatible_of_exists_amalgamation (x : FamilyOfElements P R)
    (h : ‚àÉ t, x.IsAmalgamation t) : x.Compatible := by
  obtain ‚ü®t, ht‚ü© := h
  intro Y‚ÇÅ Y‚ÇÇ Z g‚ÇÅ g‚ÇÇ f‚ÇÅ f‚ÇÇ h‚ÇÅ h‚ÇÇ comm
  rw [‚Üê ht _ h‚ÇÅ, ‚Üê ht _ h‚ÇÇ, ‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, comm]
  simp

theorem isAmalgamation_restrict {R‚ÇÅ R‚ÇÇ : Presieve X} (h : R‚ÇÅ ‚â§ R‚ÇÇ) (x : FamilyOfElements P R‚ÇÇ)
    (t : P.obj (op X)) (ht : x.IsAmalgamation t) : (x.restrict h).IsAmalgamation t := fun Y f hf =>
  ht f (h Y hf)

theorem isAmalgamation_sieveExtend {R : Presieve X} (x : FamilyOfElements P R) (t : P.obj (op X))
    (ht : x.IsAmalgamation t) : x.sieveExtend.IsAmalgamation t := by
  intro Y f hf
  dsimp [FamilyOfElements.sieveExtend]
  rw [‚Üê ht _, ‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, hf.choose_spec.choose_spec.choose_spec.2]

@[simp]
lemma FamilyOfElements.isAmalgamation_singleton_iff {X Y : C} (f : X ‚ü∂ Y)
    (x : (singleton f).FamilyOfElements P) (y : P.obj (op Y)) :
    x.IsAmalgamation y ‚Üî P.map f.op y = x f ‚ü®‚ü© := by
  refine ‚ü®fun H ‚Ü¶ H _ _, ?_‚ü©
  rintro H Y g ‚ü®rfl‚ü©
  exact H

/-- A presheaf is separated for a presieve if there is at most one amalgamation. -/
def IsSeparatedFor (P : C·µí·µñ ‚•§ Type w) (R : Presieve X) : Prop :=
  ‚àÄ (x : FamilyOfElements P R) (t‚ÇÅ t‚ÇÇ), x.IsAmalgamation t‚ÇÅ ‚Üí x.IsAmalgamation t‚ÇÇ ‚Üí t‚ÇÅ = t‚ÇÇ

theorem IsSeparatedFor.ext {R : Presieve X} (hR : IsSeparatedFor P R) {t‚ÇÅ t‚ÇÇ : P.obj (op X)}
    (h : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ (_ : R f), P.map f.op t‚ÇÅ = P.map f.op t‚ÇÇ) : t‚ÇÅ = t‚ÇÇ :=
  hR (fun _ f _ => P.map f.op t‚ÇÇ) t‚ÇÅ t‚ÇÇ (fun _ _ hf => h hf) fun _ _ _ => rfl

theorem isSeparatedFor_iff_generate :
    IsSeparatedFor P R ‚Üî IsSeparatedFor P (generate R : Presieve X) := by
  constructor
  ¬∑ intro h x t‚ÇÅ t‚ÇÇ ht‚ÇÅ ht‚ÇÇ
    apply h (x.restrict (le_generate R)) t‚ÇÅ t‚ÇÇ _ _
    ¬∑ exact isAmalgamation_restrict _ x t‚ÇÅ ht‚ÇÅ
    ¬∑ exact isAmalgamation_restrict _ x t‚ÇÇ ht‚ÇÇ
  ¬∑ intro h x t‚ÇÅ t‚ÇÇ ht‚ÇÅ ht‚ÇÇ
    apply h x.sieveExtend
    ¬∑ exact isAmalgamation_sieveExtend x t‚ÇÅ ht‚ÇÅ
    ¬∑ exact isAmalgamation_sieveExtend x t‚ÇÇ ht‚ÇÇ

theorem isSeparatedFor_top (P : C·µí·µñ ‚•§ Type w) : IsSeparatedFor P (‚ä§ : Presieve X) :=
  fun x t‚ÇÅ t‚ÇÇ h‚ÇÅ h‚ÇÇ => by
  have q‚ÇÅ := h‚ÇÅ (ùüô X) (by tauto)
  have q‚ÇÇ := h‚ÇÇ (ùüô X) (by tauto)
  simp only [op_id, FunctorToTypes.map_id_apply] at q‚ÇÅ q‚ÇÇ
  rw [q‚ÇÅ, q‚ÇÇ]

/-- We define `P` to be a sheaf for the presieve `R` if every compatible family has a unique
amalgamation.

This is the definition of a sheaf for the given presieve given in C2.1.2 of [Elephant], and
https://ncatlab.org/nlab/show/sheaf#GeneralDefinitionInComponents.
Using `compatible_iff_sieveCompatible`,
this is equivalent to the definition of a sheaf in [MM92], Chapter III, Section 4.
-/
def IsSheafFor (P : C·µí·µñ ‚•§ Type w) (R : Presieve X) : Prop :=
  ‚àÄ x : FamilyOfElements P R, x.Compatible ‚Üí ‚àÉ! t, x.IsAmalgamation t

/-- This is an equivalent condition to be a sheaf, which is useful for the abstraction to local
operators on elementary toposes. However this definition is defined only for sieves, not presieves.
The equivalence between this and `IsSheafFor` is given in `isSheafFor_iff_yonedaSheafCondition`.
This version is also useful to establish that being a sheaf is preserved under isomorphism of
presheaves.

See the discussion before Equation (3) of [MM92], Chapter III, Section 4. See also C2.1.4 of
[Elephant]. -/
@[stacks 00Z8 "Direct reformulation"]
def YonedaSheafCondition (P : C·µí·µñ ‚•§ Type v‚ÇÅ) (S : Sieve X) : Prop :=
  ‚àÄ f : S.functor ‚ü∂ P, ‚àÉ! g, S.functorInclusion ‚â´ g = f

-- TODO: We can generalize the universe parameter v‚ÇÅ above by composing with
-- appropriate `ulift_functor`s.
/-- (Implementation). This is a (primarily internal) equivalence between natural transformations
and compatible families.

Cf the discussion after Lemma 7.47.10 in <https://stacks.math.columbia.edu/tag/00YW>. See also
the proof of C2.1.4 of [Elephant], and the discussion in [MM92], Chapter III, Section 4.
-/
def natTransEquivCompatibleFamily {P : C·µí·µñ ‚•§ Type v‚ÇÅ} :
    (S.functor ‚ü∂ P) ‚âÉ { x : FamilyOfElements P (S : Presieve X) // x.Compatible } where
  toFun Œ± := by
    refine ‚ü®fun Y f hf => ?_, ?_‚ü©
    ¬∑ apply Œ±.app (op Y) ‚ü®_, hf‚ü©
    ¬∑ rw [compatible_iff_sieveCompatible]
      intro Y Z f g hf
      dsimp
      rw [‚Üê FunctorToTypes.naturality _ _ Œ± g.op]
      rfl
  invFun t :=
    { app := fun _ f => t.1 _ f.2
      naturality := fun Y Z g => by
        ext ‚ü®f, hf‚ü©
        apply t.2.to_sieveCompatible _ }
  left_inv Œ± := by
    ext X ‚ü®_, _‚ü©
    rfl
  right_inv := by
    rintro ‚ü®x, hx‚ü©
    rfl

/-- (Implementation). A lemma useful to prove `isSheafFor_iff_yonedaSheafCondition`. -/
theorem extension_iff_amalgamation {P : C·µí·µñ ‚•§ Type v‚ÇÅ} (x : S.functor ‚ü∂ P) (g : yoneda.obj X ‚ü∂ P) :
    S.functorInclusion ‚â´ g = x ‚Üî
      (natTransEquivCompatibleFamily x).1.IsAmalgamation (yonedaEquiv g) := by
  change _ ‚Üî ‚àÄ ‚¶ÉY : C‚¶Ñ (f : Y ‚ü∂ X) (h : S f), P.map f.op (yonedaEquiv g) = x.app (op Y) ‚ü®f, h‚ü©
  constructor
  ¬∑ rintro rfl Y f hf
    rw [yonedaEquiv_naturality]
    simp [yonedaEquiv_apply]
  ¬∑ intro h
    ext Y ‚ü®f, hf‚ü©
    convert h f hf
    rw [yonedaEquiv_naturality]
    simp [yonedaEquiv]

/-- The yoneda version of the sheaf condition is equivalent to the sheaf condition.

C2.1.4 of [Elephant].
-/
theorem isSheafFor_iff_yonedaSheafCondition {P : C·µí·µñ ‚•§ Type v‚ÇÅ} :
    IsSheafFor P (S : Presieve X) ‚Üî YonedaSheafCondition P S := by
  rw [IsSheafFor, YonedaSheafCondition]
  simp_rw [extension_iff_amalgamation]
  rw [Equiv.forall_congr_left natTransEquivCompatibleFamily]
  rw [Subtype.forall]
  exact forall‚ÇÇ_congr fun x hx ‚Ü¶ by simp [Equiv.existsUnique_congr_right]

/--
If `P` is a sheaf for the sieve `S` on `X`, a natural transformation from `S` (viewed as a functor)
to `P` can be (uniquely) extended to all of `yoneda.obj X`.

      f
   S  ‚Üí  P
   ‚Üì  ‚Üó
   yX
-/
noncomputable def IsSheafFor.extend {P : C·µí·µñ ‚•§ Type v‚ÇÅ} (h : IsSheafFor P (S : Presieve X))
    (f : S.functor ‚ü∂ P) : yoneda.obj X ‚ü∂ P :=
  (isSheafFor_iff_yonedaSheafCondition.1 h f).exists.choose

/--
Show that the extension of `f : S.functor ‚ü∂ P` to all of `yoneda.obj X` is in fact an extension, ie
that the triangle below commutes, provided `P` is a sheaf for `S`

      f
   S  ‚Üí  P
   ‚Üì  ‚Üó
   yX
-/
@[reassoc (attr := simp)]
theorem IsSheafFor.functorInclusion_comp_extend {P : C·µí·µñ ‚•§ Type v‚ÇÅ} (h : IsSheafFor P S.arrows)
    (f : S.functor ‚ü∂ P) : S.functorInclusion ‚â´ h.extend f = f :=
  (isSheafFor_iff_yonedaSheafCondition.1 h f).exists.choose_spec

/-- The extension of `f` to `yoneda.obj X` is unique. -/
theorem IsSheafFor.unique_extend {P : C·µí·µñ ‚•§ Type v‚ÇÅ} (h : IsSheafFor P S.arrows) {f : S.functor ‚ü∂ P}
    (t : yoneda.obj X ‚ü∂ P) (ht : S.functorInclusion ‚â´ t = f) : t = h.extend f :=
  (isSheafFor_iff_yonedaSheafCondition.1 h f).unique ht (h.functorInclusion_comp_extend f)

/--
If `P` is a sheaf for the sieve `S` on `X`, then if two natural transformations from `yoneda.obj X`
to `P` agree when restricted to the subfunctor given by `S`, they are equal.
-/
theorem IsSheafFor.hom_ext {P : C·µí·µñ ‚•§ Type v‚ÇÅ} (h : IsSheafFor P (S : Presieve X))
    (t‚ÇÅ t‚ÇÇ : yoneda.obj X ‚ü∂ P) (ht : S.functorInclusion ‚â´ t‚ÇÅ = S.functorInclusion ‚â´ t‚ÇÇ) :
    t‚ÇÅ = t‚ÇÇ :=
  (h.unique_extend t‚ÇÅ ht).trans (h.unique_extend t‚ÇÇ rfl).symm

/-- `P` is a sheaf for `R` iff it is separated for `R` and there exists an amalgamation. -/
theorem isSeparatedFor_and_exists_isAmalgamation_iff_isSheafFor :
    (IsSeparatedFor P R ‚àß ‚àÄ x : FamilyOfElements P R, x.Compatible ‚Üí ‚àÉ t, x.IsAmalgamation t) ‚Üî
      IsSheafFor P R := by
  rw [IsSeparatedFor, ‚Üê forall_and]
  apply forall_congr'
  intro x
  constructor
  ¬∑ intro z hx
    exact existsUnique_of_exists_of_unique (z.2 hx) z.1
  ¬∑ intro h
    refine ‚ü®?_, ExistsUnique.exists ‚àò h‚ü©
    intro t‚ÇÅ t‚ÇÇ ht‚ÇÅ ht‚ÇÇ
    apply (h _).unique ht‚ÇÅ ht‚ÇÇ
    exact is_compatible_of_exists_amalgamation x ‚ü®_, ht‚ÇÇ‚ü©

/-- If `P` is separated for `R` and every family has an amalgamation, then `P` is a sheaf for `R`.
-/
theorem IsSeparatedFor.isSheafFor (t : IsSeparatedFor P R) :
    (‚àÄ x : FamilyOfElements P R, x.Compatible ‚Üí ‚àÉ t, x.IsAmalgamation t) ‚Üí IsSheafFor P R := by
  rw [‚Üê isSeparatedFor_and_exists_isAmalgamation_iff_isSheafFor]
  exact And.intro t

/-- If `P` is a sheaf for `R`, it is separated for `R`. -/
theorem IsSheafFor.isSeparatedFor : IsSheafFor P R ‚Üí IsSeparatedFor P R := fun q =>
  (isSeparatedFor_and_exists_isAmalgamation_iff_isSheafFor.2 q).1

/-- Get the amalgamation of the given compatible family, provided we have a sheaf. -/
noncomputable def IsSheafFor.amalgamate (t : IsSheafFor P R) (x : FamilyOfElements P R)
    (hx : x.Compatible) : P.obj (op X) :=
  (t x hx).exists.choose

theorem IsSheafFor.isAmalgamation (t : IsSheafFor P R) {x : FamilyOfElements P R}
    (hx : x.Compatible) : x.IsAmalgamation (t.amalgamate x hx) :=
  (t x hx).exists.choose_spec

@[simp]
theorem IsSheafFor.valid_glue (t : IsSheafFor P R) {x : FamilyOfElements P R} (hx : x.Compatible)
    (f : Y ‚ü∂ X) (Hf : R f) : P.map f.op (t.amalgamate x hx) = x f Hf :=
  t.isAmalgamation hx f Hf

/-- C2.1.3 in [Elephant] -/
theorem isSheafFor_iff_generate (R : Presieve X) :
    IsSheafFor P R ‚Üî IsSheafFor P (generate R : Presieve X) := by
  rw [‚Üê isSeparatedFor_and_exists_isAmalgamation_iff_isSheafFor]
  rw [‚Üê isSeparatedFor_and_exists_isAmalgamation_iff_isSheafFor]
  rw [‚Üê isSeparatedFor_iff_generate]
  apply and_congr (Iff.refl _)
  constructor
  ¬∑ intro q x hx
    apply Exists.imp _ (q _ (hx.restrict (le_generate R)))
    intro t ht
    simpa [hx] using isAmalgamation_sieveExtend _ _ ht
  ¬∑ intro q x hx
    apply Exists.imp _ (q _ hx.sieveExtend)
    intro t ht
    simpa [hx] using isAmalgamation_restrict (le_generate R) _ _ ht

/-- Every presheaf is a sheaf for the family {ùüô X}.

[Elephant] C2.1.5(i)
-/
theorem isSheafFor_singleton_iso (P : C·µí·µñ ‚•§ Type w) : IsSheafFor P (Presieve.singleton (ùüô X)) := by
  intro x _
  refine ‚ü®x _ (Presieve.singleton_self _), ?_, ?_‚ü©
  ¬∑ rintro _ _ ‚ü®rfl, rfl‚ü©
    simp
  ¬∑ intro t ht
    simpa using ht _ (Presieve.singleton_self _)

/-- Every presheaf is a sheaf for the maximal sieve.

[Elephant] C2.1.5(ii)
-/
theorem isSheafFor_top_sieve (P : C·µí·µñ ‚•§ Type w) : IsSheafFor P ((‚ä§ : Sieve X) : Presieve X) := by
  rw [‚Üê generate_of_singleton_isSplitEpi (ùüô X)]
  rw [‚Üê isSheafFor_iff_generate]
  apply isSheafFor_singleton_iso

/-- If `P‚ÇÅ : C·µí·µñ ‚•§ Type w` and `P‚ÇÇ  : C·µí·µñ ‚•§ Type w` are two naturally equivalent
presheaves, and `P‚ÇÅ` is a sheaf for a presieve `R`, then `P‚ÇÇ` is also a sheaf for `R`. -/
lemma isSheafFor_of_nat_equiv {P‚ÇÅ : C·µí·µñ ‚•§ Type w} {P‚ÇÇ : C·µí·µñ ‚•§ Type w'}
    (e : ‚àÄ ‚¶ÉX : C‚¶Ñ, P‚ÇÅ.obj (op X) ‚âÉ P‚ÇÇ.obj (op X))
    (he : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y) (x : P‚ÇÅ.obj (op Y)),
      e (P‚ÇÅ.map f.op x) = P‚ÇÇ.map f.op (e x))
    {X : C} {R : Presieve X} (hP‚ÇÅ : IsSheafFor P‚ÇÅ R) :
    IsSheafFor P‚ÇÇ R := fun x‚ÇÇ hx‚ÇÇ ‚Ü¶ by
  have he' : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y) (x : P‚ÇÇ.obj (op Y)),
    e.symm (P‚ÇÇ.map f.op x) = P‚ÇÅ.map f.op (e.symm x) := fun X Y f x ‚Ü¶
      e.injective (by simp only [Equiv.apply_symm_apply, he])
  let x‚ÇÅ : FamilyOfElements P‚ÇÅ R := fun Y f hf ‚Ü¶ e.symm (x‚ÇÇ f hf)
  have hx‚ÇÅ : x‚ÇÅ.Compatible := fun Y‚ÇÅ Y‚ÇÇ Z g‚ÇÅ g‚ÇÇ f‚ÇÅ f‚ÇÇ h‚ÇÅ h‚ÇÇ fac ‚Ü¶ e.injective
    (by simp only [he, Equiv.apply_symm_apply, hx‚ÇÇ g‚ÇÅ g‚ÇÇ h‚ÇÅ h‚ÇÇ fac, x‚ÇÅ])
  have : ‚àÄ (t‚ÇÇ : P‚ÇÇ.obj (op X)),
      x‚ÇÇ.IsAmalgamation t‚ÇÇ ‚Üî x‚ÇÅ.IsAmalgamation (e.symm t‚ÇÇ) := fun t‚ÇÇ ‚Ü¶ by
    simp only [FamilyOfElements.IsAmalgamation, x‚ÇÅ,
      ‚Üê he', EmbeddingLike.apply_eq_iff_eq]
  refine ‚ü®e (hP‚ÇÅ.amalgamate x‚ÇÅ hx‚ÇÅ), ?_, ?_‚ü©
  ¬∑ dsimp
    simp only [this, Equiv.symm_apply_apply]
    exact IsSheafFor.isAmalgamation hP‚ÇÅ hx‚ÇÅ
  ¬∑ intro t‚ÇÇ ht‚ÇÇ
    refine e.symm.injective ?_
    simp only [Equiv.symm_apply_apply]
    exact hP‚ÇÅ.isSeparatedFor x‚ÇÅ _ _ (by simpa only [this] using ht‚ÇÇ)
      (IsSheafFor.isAmalgamation hP‚ÇÅ hx‚ÇÅ)

/-- If `P` is a sheaf for `S`, and it is iso to `P'`, then `P'` is a sheaf for `S`. This shows that
"being a sheaf for a presieve" is a mathematical or hygienic property.
-/
theorem isSheafFor_iso {P' : C·µí·µñ ‚•§ Type w} (i : P ‚âÖ P') (hP : IsSheafFor P R) :
    IsSheafFor P' R :=
  isSheafFor_of_nat_equiv (fun X ‚Ü¶ (i.app (op X)).toEquiv)
    (fun _ _ f x ‚Ü¶ congr_fun (i.hom.naturality f.op) x) hP

/-- If a presieve `R` on `X` has a subsieve `S` such that:

* `P` is a sheaf for `S`.
* For every `f` in `R`, `P` is separated for the pullback of `S` along `f`,

then `P` is a sheaf for `R`.

This is closely related to [Elephant] C2.1.6(i).
-/
theorem isSheafFor_subsieve_aux (P : C·µí·µñ ‚•§ Type w) {S : Sieve X} {R : Presieve X}
    (h : (S : Presieve X) ‚â§ R) (hS : IsSheafFor P (S : Presieve X))
    (trans : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ, R f ‚Üí IsSeparatedFor P (S.pullback f : Presieve Y)) :
    IsSheafFor P R := by
  rw [‚Üê isSeparatedFor_and_exists_isAmalgamation_iff_isSheafFor]
  constructor
  ¬∑ intro x t‚ÇÅ t‚ÇÇ ht‚ÇÅ ht‚ÇÇ
    exact
      hS.isSeparatedFor _ _ _ (isAmalgamation_restrict h x t‚ÇÅ ht‚ÇÅ)
        (isAmalgamation_restrict h x t‚ÇÇ ht‚ÇÇ)
  ¬∑ intro x hx
    use hS.amalgamate _ (hx.restrict h)
    intro W j hj
    apply (trans hj).ext
    intro Y f hf
    rw [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, hS.valid_glue (hx.restrict h) _ hf,
      FamilyOfElements.restrict, ‚Üê hx (ùüô _) f (h _ hf) _ (id_comp _)]
    simp

/--
If `P` is a sheaf for every pullback of the sieve `S`, then `P` is a sheaf for any presieve which
contains `S`.
This is closely related to [Elephant] C2.1.6.
-/
theorem isSheafFor_subsieve (P : C·µí·µñ ‚•§ Type w) {S : Sieve X} {R : Presieve X}
    (h : (S : Presieve X) ‚â§ R) (trans : ‚àÄ ‚¶ÉY‚¶Ñ (f : Y ‚ü∂ X),
      IsSheafFor P (S.pullback f : Presieve Y)) :
    IsSheafFor P R :=
  isSheafFor_subsieve_aux P h (by simpa using trans (ùüô _)) fun _ f _ => (trans f).isSeparatedFor

section Arrows

variable {B : C} {I : Type*} {X : I ‚Üí C} (œÄ : (i : I) ‚Üí X i ‚ü∂ B) (P)

/--
A more explicit version of `FamilyOfElements.Compatible` for a `Presieve.ofArrows`.
-/
def Arrows.Compatible (x : (i : I) ‚Üí P.obj (op (X i))) : Prop :=
  ‚àÄ i j Z (gi : Z ‚ü∂ X i) (gj : Z ‚ü∂ X j), gi ‚â´ œÄ i = gj ‚â´ œÄ j ‚Üí
    P.map gi.op (x i) = P.map gj.op (x j)

lemma FamilyOfElements.isAmalgamation_iff_ofArrows (x : FamilyOfElements P (ofArrows X œÄ))
    (t : P.obj (op B)) :
    x.IsAmalgamation t ‚Üî ‚àÄ (i : I), P.map (œÄ i).op t = x _ (ofArrows.mk i) :=
  ‚ü®fun h i ‚Ü¶ h _ (ofArrows.mk i), fun h _ f ‚ü®i‚ü© ‚Ü¶ h i‚ü©

namespace Arrows.Compatible

variable {x : (i : I) ‚Üí P.obj (op (X i))}
variable {P œÄ}

theorem exists_familyOfElements (hx : Compatible P œÄ x) :
    ‚àÉ (x' : FamilyOfElements P (ofArrows X œÄ)), ‚àÄ (i : I), x' _ (ofArrows.mk i) = x i := by
  choose i h h' using @ofArrows_surj _ _ _ _ _ œÄ
  exact ‚ü®fun Y f hf ‚Ü¶ P.map (eqToHom (h f hf).symm).op (x _),
    fun j ‚Ü¶ (hx _ j (X j) _ (ùüô _) <| by rw [‚Üê h', id_comp]).trans <| by simp‚ü©

variable (hx : Compatible P œÄ x)

/--
A `FamilyOfElements` associated to an explicit family of elements.
-/
noncomputable
def familyOfElements : FamilyOfElements P (ofArrows X œÄ) :=
  (exists_familyOfElements hx).choose

@[simp]
theorem familyOfElements_ofArrows_mk (i : I) :
    hx.familyOfElements _ (ofArrows.mk i) = x i :=
  (exists_familyOfElements hx).choose_spec _

theorem familyOfElements_compatible : hx.familyOfElements.Compatible := by
  rintro Y‚ÇÅ Y‚ÇÇ Z g‚ÇÅ g‚ÇÇ f‚ÇÅ f‚ÇÇ ‚ü®i‚ü© ‚ü®j‚ü© hgf
  simp [hx i j Z g‚ÇÅ g‚ÇÇ hgf]

end Arrows.Compatible

theorem isSheafFor_arrows_iff : (ofArrows X œÄ).IsSheafFor P ‚Üî
    (‚àÄ (x : (i : I) ‚Üí P.obj (op (X i))), Arrows.Compatible P œÄ x ‚Üí
    ‚àÉ! t, ‚àÄ i, P.map (œÄ i).op t = x i) := by
  refine ‚ü®fun h x hx ‚Ü¶ ?_, fun h x hx ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®t, ht‚ÇÅ, ht‚ÇÇ‚ü© := h _ hx.familyOfElements_compatible
    refine ‚ü®t, fun i ‚Ü¶ ?_, fun t' ht' ‚Ü¶ ht‚ÇÇ _ fun _ _ ‚ü®i‚ü© ‚Ü¶ ?_‚ü©
    ¬∑ rw [ht‚ÇÅ _ (ofArrows.mk i), hx.familyOfElements_ofArrows_mk]
    ¬∑ rw [ht', hx.familyOfElements_ofArrows_mk]
  ¬∑ obtain ‚ü®t, hA, ht‚ü© := h (fun i ‚Ü¶ x (œÄ i) (ofArrows.mk _))
      (fun i j Z gi gj ‚Ü¶ hx gi gj (ofArrows.mk _) (ofArrows.mk _))
    exact ‚ü®t, fun Y f ‚ü®i‚ü© ‚Ü¶ hA i, fun y hy ‚Ü¶ ht y (fun i ‚Ü¶ hy (œÄ i) (ofArrows.mk _))‚ü©

variable [(ofArrows X œÄ).hasPullbacks]

/--
A more explicit version of `FamilyOfElements.PullbackCompatible` for a `Presieve.ofArrows`.
-/
def Arrows.PullbackCompatible (x : (i : I) ‚Üí P.obj (op (X i))) : Prop :=
  ‚àÄ i j, P.map (pullback.fst (œÄ i) (œÄ j)).op (x i) =
    P.map (pullback.snd (œÄ i) (œÄ j)).op (x j)

theorem Arrows.pullbackCompatible_iff (x : (i : I) ‚Üí P.obj (op (X i))) :
    Compatible P œÄ x ‚Üî PullbackCompatible P œÄ x := by
  refine ‚ü®fun t i j ‚Ü¶ ?_, fun t i j Z gi gj comm ‚Ü¶ ?_‚ü©
  ¬∑ apply t
    exact pullback.condition
  ¬∑ rw [‚Üê pullback.lift_fst _ _ comm, op_comp, FunctorToTypes.map_comp_apply, t i j,
      ‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, pullback.lift_snd]

theorem isSheafFor_arrows_iff_pullbacks : (ofArrows X œÄ).IsSheafFor P ‚Üî
    (‚àÄ (x : (i : I) ‚Üí P.obj (op (X i))), Arrows.PullbackCompatible P œÄ x ‚Üí
    ‚àÉ! t, ‚àÄ i, P.map (œÄ i).op t = x i) := by
  simp_rw [‚Üê Arrows.pullbackCompatible_iff, isSheafFor_arrows_iff]

end Arrows

@[simp]
lemma isSeparatedFor_singleton {X Y : C} {f : X ‚ü∂ Y} :
    Presieve.IsSeparatedFor P (.singleton f) ‚Üî
      Function.Injective (P.map f.op) := by
  rw [IsSeparatedFor, Equiv.forall_congr_left (Presieve.FamilyOfElements.singletonEquiv P f)]
  simp_rw [FamilyOfElements.isAmalgamation_singleton_iff,
    FamilyOfElements.singletonEquiv_symm_apply_self, Function.Injective]
  aesop

lemma isSheafFor_singleton {X Y : C} {f : X ‚ü∂ Y} :
    Presieve.IsSheafFor P (.singleton f) ‚Üî
      ‚àÄ (x : P.obj (op X)),
        (‚àÄ {Z : C} (p‚ÇÅ p‚ÇÇ : Z ‚ü∂ X), p‚ÇÅ ‚â´ f = p‚ÇÇ ‚â´ f ‚Üí P.map p‚ÇÅ.op x = P.map p‚ÇÇ.op x) ‚Üí
        ‚àÉ! y, P.map f.op y = x := by
  rw [IsSheafFor, Equiv.forall_congr_left (Presieve.FamilyOfElements.singletonEquiv P f)]
  simp_rw [FamilyOfElements.compatible_singleton_iff,
    FamilyOfElements.isAmalgamation_singleton_iff, FamilyOfElements.singletonEquiv_symm_apply_self]

end Presieve

end CategoryTheory
