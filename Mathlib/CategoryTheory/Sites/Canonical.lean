/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Sites.Sheaf

/-!
# The canonical topology on a category

We define the finest (largest) Grothendieck topology for which a given presheaf `P` is a sheaf.
This is well defined since if `P` is a sheaf for a topology `J`, then it is a sheaf for any
coarser (smaller) topology. Nonetheless we define the topology explicitly by specifying its sieves:
A sieve `S` on `X` is covering for `finestTopologySingle P` iff
  for any `f : Y ‚ü∂ X`, `P` satisfies the sheaf axiom for `S.pullback f`.
Showing that this is a genuine Grothendieck topology (namely that it satisfies the transitivity
axiom) forms the bulk of this file.

This generalises to a set of presheaves, giving the topology `finestTopology Ps` which is the
finest topology for which every presheaf in `Ps` is a sheaf.
Using `Ps` as the set of representable presheaves defines the `canonicalTopology`: the finest
topology for which every representable is a sheaf.

A Grothendieck topology is called `Subcanonical` if it is smaller than the canonical topology,
equivalently it is subcanonical iff every representable presheaf is a sheaf.

## References
* https://ncatlab.org/nlab/show/canonical+topology
* https://ncatlab.org/nlab/show/subcanonical+coverage
* https://stacks.math.columbia.edu/tag/00Z9
* https://math.stackexchange.com/a/358709/
-/


universe v u

namespace CategoryTheory

open CategoryTheory Category Limits Sieve

variable {C : Type u} [Category.{v} C]

namespace Sheaf

variable {P : C·µí·µñ ‚•§ Type v} {X : C} (J : GrothendieckTopology C)

/--
To show `P` is a sheaf for the binding of `U` with `B`, it suffices to show that `P` is a sheaf for
`U`, that `P` is a sheaf for each sieve in `B`, and that it is separated for any pullback of any
sieve in `B`.

This is mostly an auxiliary lemma to show `isSheafFor_trans`.
Adapted from [Elephant], Lemma C2.1.7(i) with suggestions as mentioned in
https://math.stackexchange.com/a/358709/
-/
theorem isSheafFor_bind (P : C·µí·µñ ‚•§ Type v) (U : Sieve X) (B : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ, U f ‚Üí Sieve Y)
    (hU : Presieve.IsSheafFor P (U : Presieve X))
    (hB : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ (hf : U f), Presieve.IsSheafFor P (B hf : Presieve Y))
    (hB' : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ (h : U f) ‚¶ÉZ‚¶Ñ (g : Z ‚ü∂ Y),
      Presieve.IsSeparatedFor P (((B h).pullback g) : Presieve Z)) :
    Presieve.IsSheafFor P (Sieve.bind (U : Presieve X) B : Presieve X) := by
  intro s hs
  let y : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ (hf : U f), Presieve.FamilyOfElements P (B hf : Presieve Y) :=
    fun Y f hf Z g hg => s _ (Presieve.bind_comp _ _ hg)
  have hy : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ (hf : U f), (y hf).Compatible := by
    intro Y f H Y‚ÇÅ Y‚ÇÇ Z g‚ÇÅ g‚ÇÇ f‚ÇÅ f‚ÇÇ hf‚ÇÅ hf‚ÇÇ comm
    apply hs
    apply reassoc_of% comm
  let t : Presieve.FamilyOfElements P (U : Presieve X) :=
    fun Y f hf => (hB hf).amalgamate (y hf) (hy hf)
  have ht : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ (hf : U f), (y hf).IsAmalgamation (t f hf) := fun Y f hf =>
    (hB hf).isAmalgamation _
  have hT : t.Compatible := by
    rw [Presieve.compatible_iff_sieveCompatible]
    intro Z W f h hf
    apply (hB (U.downward_closed hf h)).isSeparatedFor.ext
    intro Y l hl
    apply (hB' hf (l ‚â´ h)).ext
    intro M m hm
    have : bind U B (m ‚â´ l ‚â´ h ‚â´ f) := by simpa using (Presieve.bind_comp f hf hm : bind U B _)
    trans s (m ‚â´ l ‚â´ h ‚â´ f) this
    ¬∑ have := ht (U.downward_closed hf h) _ ((B _).downward_closed hl m)
      rw [op_comp, FunctorToTypes.map_comp_apply] at this
      rw [this]
      change s _ _ = s _ _
      -- Porting note: the proof was `by simp`
      congr 1
      simp only [assoc]
    ¬∑ have h : s _ _ = _ := (ht hf _ hm).symm
      -- Porting note: this was done by `simp only [assoc] at`
      conv_lhs at h => congr; rw [assoc, assoc]
      rw [h]
      simp only [op_comp, assoc, FunctorToTypes.map_comp_apply]
  refine ‚ü®hU.amalgamate t hT, ?_, ?_‚ü©
  ¬∑ rintro Z _ ‚ü®Y, f, g, hg, hf, rfl‚ü©
    rw [op_comp, FunctorToTypes.map_comp_apply, Presieve.IsSheafFor.valid_glue _ _ _ hg]
    apply ht hg _ hf
  ¬∑ intro y hy
    apply hU.isSeparatedFor.ext
    intro Y f hf
    apply (hB hf).isSeparatedFor.ext
    intro Z g hg
    rw [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, hy _ (Presieve.bind_comp _ _ hg),
      hU.valid_glue _ _ hf, ht hf _ hg]

/-- Given two sieves `R` and `S`, to show that `P` is a sheaf for `S`, we can show:
* `P` is a sheaf for `R`
* `P` is a sheaf for the pullback of `S` along any arrow in `R`
* `P` is separated for the pullback of `R` along any arrow in `S`.

This is mostly an auxiliary lemma to construct `finestTopology`.
Adapted from [Elephant], Lemma C2.1.7(ii) with suggestions as mentioned in
https://math.stackexchange.com/a/358709
-/
theorem isSheafFor_trans (P : C·µí·µñ ‚•§ Type v) (R S : Sieve X)
    (hR : Presieve.IsSheafFor P (R : Presieve X))
    (hR' : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ (_ : S f), Presieve.IsSeparatedFor P (R.pullback f : Presieve Y))
    (hS : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ (_ : R f), Presieve.IsSheafFor P (S.pullback f : Presieve Y)) :
    Presieve.IsSheafFor P (S : Presieve X) := by
  have : (bind R fun Y f _ => S.pullback f : Presieve X) ‚â§ S := by
    rintro Z f ‚ü®W, f, g, hg, hf : S _, rfl‚ü©
    apply hf
  apply Presieve.isSheafFor_subsieve_aux P this
  ¬∑ apply isSheafFor_bind _ _ _ hR hS
    intro Y f hf Z g
    rw [‚Üê pullback_comp]
    apply (hS (R.downward_closed hf _)).isSeparatedFor
  ¬∑ intro Y f hf
    have : Sieve.pullback f (bind R fun T (k : T ‚ü∂ X) (_ : R k) => pullback k S) =
        R.pullback f := by
      ext Z g
      constructor
      ¬∑ rintro ‚ü®W, k, l, hl, _, comm‚ü©
        rw [pullback_apply, ‚Üê comm]
        simp [hl]
      ¬∑ intro a
        refine ‚ü®Z, ùüô Z, _, a, ?_‚ü©
        simp [hf]
    rw [this]
    apply hR' hf

/-- Construct the finest (largest) Grothendieck topology for which the given presheaf is a sheaf. -/
@[stacks 00Z9 "This is a special case of the Stacks entry, but following a different
proof (see the Stacks comments)."]
def finestTopologySingle (P : C·µí·µñ ‚•§ Type v) : GrothendieckTopology C where
  sieves X S := ‚àÄ (Y) (f : Y ‚ü∂ X), Presieve.IsSheafFor P (S.pullback f : Presieve Y)
  top_mem' X Y f := by
    rw [Sieve.pullback_top]
    exact Presieve.isSheafFor_top_sieve P
  pullback_stable' X Y S f hS Z g := by
    rw [‚Üê pullback_comp]
    apply hS
  transitive' X S hS R hR Z g := by
    -- This is the hard part of the construction, showing that the given set of sieves satisfies
    -- the transitivity axiom.
    refine isSheafFor_trans P (pullback g S) _ (hS Z g) ?_ ?_
    ¬∑ intro Y f _
      rw [‚Üê pullback_comp]
      apply (hS _ _).isSeparatedFor
    ¬∑ intro Y f hf
      have := hR hf _ (ùüô _)
      rw [pullback_id, pullback_comp] at this
      apply this

/-- Construct the finest (largest) Grothendieck topology for which all the given presheaves are
sheaves. -/
@[stacks 00Z9 "Equal to that Stacks construction"]
def finestTopology (Ps : Set (C·µí·µñ ‚•§ Type v)) : GrothendieckTopology C :=
  sInf (finestTopologySingle '' Ps)

/-- Check that if `P ‚àà Ps`, then `P` is indeed a sheaf for the finest topology on `Ps`. -/
theorem sheaf_for_finestTopology (Ps : Set (C·µí·µñ ‚•§ Type v)) (h : P ‚àà Ps) :
    Presieve.IsSheaf (finestTopology Ps) P := fun X S hS => by
  simpa using hS _ ‚ü®‚ü®_, _, ‚ü®_, h, rfl‚ü©, rfl‚ü©, rfl‚ü© _ (ùüô _)

/--
Check that if each `P ‚àà Ps` is a sheaf for `J`, then `J` is a subtopology of `finestTopology Ps`.
-/
theorem le_finestTopology (Ps : Set (C·µí·µñ ‚•§ Type v)) (J : GrothendieckTopology C)
    (hJ : ‚àÄ P ‚àà Ps, Presieve.IsSheaf J P) : J ‚â§ finestTopology Ps := by
  rintro X S hS _ ‚ü®‚ü®_, _, ‚ü®P, hP, rfl‚ü©, rfl‚ü©, rfl‚ü©
  intro Y f
  -- this can't be combined with the previous because the `subst` is applied at the end
  exact hJ P hP (S.pullback f) (J.pullback_stable f hS)

/-- The `canonicalTopology` on a category is the finest (largest) topology for which every
representable presheaf is a sheaf. -/
@[stacks 00ZA]
def canonicalTopology (C : Type u) [Category.{v} C] : GrothendieckTopology C :=
  finestTopology (Set.range yoneda.obj)

/-- `yoneda.obj X` is a sheaf for the canonical topology. -/
theorem isSheaf_yoneda_obj (X : C) : Presieve.IsSheaf (canonicalTopology C) (yoneda.obj X) :=
  fun _ _ hS => sheaf_for_finestTopology _ (Set.mem_range_self _) _ hS

/-- A representable functor is a sheaf for the canonical topology. -/
theorem isSheaf_of_isRepresentable (P : C·µí·µñ ‚•§ Type v) [P.IsRepresentable] :
    Presieve.IsSheaf (canonicalTopology C) P :=
  Presieve.isSheaf_iso (canonicalTopology C) P.reprW (isSheaf_yoneda_obj _)

end Sheaf

namespace GrothendieckTopology

open Sheaf

/-- A subcanonical topology is a topology which is smaller than the canonical topology.
Equivalently, a topology is subcanonical iff every representable is a sheaf.
-/
class Subcanonical (J : GrothendieckTopology C) : Prop where
  le_canonical : J ‚â§ canonicalTopology C

lemma le_canonical (J : GrothendieckTopology C) [Subcanonical J] : J ‚â§ canonicalTopology C :=
  Subcanonical.le_canonical

instance : (canonicalTopology C).Subcanonical where
  le_canonical := le_rfl

namespace Subcanonical

/-- If every functor `yoneda.obj X` is a `J`-sheaf, then `J` is subcanonical. -/
theorem of_isSheaf_yoneda_obj (J : GrothendieckTopology C)
    (h : ‚àÄ X, Presieve.IsSheaf J (yoneda.obj X)) : Subcanonical J where
  le_canonical := le_finestTopology _ _ (by rintro P ‚ü®X, rfl‚ü©; apply h)

/-- If `J` is subcanonical, then any representable is a `J`-sheaf. -/
theorem isSheaf_of_isRepresentable {J : GrothendieckTopology C} [Subcanonical J]
    (P : C·µí·µñ ‚•§ Type v) [P.IsRepresentable] : Presieve.IsSheaf J P :=
  Presieve.isSheaf_of_le _ J.le_canonical (Sheaf.isSheaf_of_isRepresentable P)

variable {J : GrothendieckTopology C}

end Subcanonical

variable (J : GrothendieckTopology C)

/--
If `J` is subcanonical, we obtain a "Yoneda" functor from the defining site
into the sheaf category.
-/
@[simps]
def yoneda [J.Subcanonical] : C ‚•§ Sheaf J (Type v) where
  obj X := ‚ü®CategoryTheory.yoneda.obj X, by
    rw [isSheaf_iff_isSheaf_of_type]
    apply Subcanonical.isSheaf_of_isRepresentable‚ü©
  map f := ‚ü®CategoryTheory.yoneda.map f‚ü©

variable [Subcanonical J]

/--
The yoneda embedding into the presheaf category factors through the one
to the sheaf category.
-/
def yonedaCompSheafToPresheaf :
    J.yoneda ‚ãô sheafToPresheaf J (Type v) ‚âÖ CategoryTheory.yoneda :=
  Iso.refl _

/-- The yoneda functor into the sheaf category is fully faithful -/
def yonedaFullyFaithful : (J.yoneda).FullyFaithful :=
  Functor.FullyFaithful.ofCompFaithful (G := sheafToPresheaf J (Type v)) Yoneda.fullyFaithful

instance : (J.yoneda).Full := (J.yonedaFullyFaithful).full

instance : (J.yoneda).Faithful := (J.yonedaFullyFaithful).faithful

end GrothendieckTopology

@[deprecated (since := "2024-10-29")] alias Sheaf.Subcanonical := GrothendieckTopology.Subcanonical
@[deprecated (since := "2024-10-29")] alias Sheaf.Subcanonical.of_isSheaf_yoneda_obj :=
  GrothendieckTopology.Subcanonical.of_isSheaf_yoneda_obj
@[deprecated (since := "2024-10-29")] alias Sheaf.Subcanonical.isSheaf_of_isRepresentable :=
  GrothendieckTopology.Subcanonical.isSheaf_of_isRepresentable
@[deprecated (since := "2024-10-29")] alias Sheaf.Subcanonical.yoneda :=
  GrothendieckTopology.yoneda
@[deprecated (since := "2024-10-29")] alias Sheaf.Subcanonical.yonedaCompSheafToPresheaf :=
  GrothendieckTopology.yonedaCompSheafToPresheaf
@[deprecated (since := "2024-10-29")] alias Sheaf.Subcanonical.yonedaFullyFaithful :=
  GrothendieckTopology.yonedaFullyFaithful

end CategoryTheory
