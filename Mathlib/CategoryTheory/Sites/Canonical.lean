/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Sites.SheafOfTypes

#align_import category_theory.sites.canonical from "leanprover-community/mathlib"@"9e7c80f638149bfb3504ba8ff48dfdbfc949fb1a"

/-!
# The canonical topology on a category

We define the finest (largest) Grothendieck topology for which a given presheaf `P` is a sheaf.
This is well defined since if `P` is a sheaf for a topology `J`, then it is a sheaf for any
coarser (smaller) topology. Nonetheless we define the topology explicitly by specifying its sieves:
A sieve `S` on `X` is covering for `finestTopologySingle P` iff
  for any `f : Y ‚ü∂ X`, `P` satisfies the sheaf axiom for `S.pullback f`.
Showing that this is a genuine Grothendieck topology (namely that it satisfies the transitivity
axiom) forms the bulk of this file.

This generalises to a set of presheaves, giving the topology `finestTopology Ps` which is the
finest topology for which every presheaf in `Ps` is a sheaf.
Using `Ps` as the set of representable presheaves defines the `canonicalTopology`: the finest
topology for which every representable is a sheaf.

A Grothendieck topology is called `Subcanonical` if it is smaller than the canonical topology,
equivalently it is subcanonical iff every representable presheaf is a sheaf.

## References
* https://ncatlab.org/nlab/show/canonical+topology
* https://ncatlab.org/nlab/show/subcanonical+coverage
* https://stacks.math.columbia.edu/tag/00Z9
* https://math.stackexchange.com/a/358709/
-/


universe v u

namespace CategoryTheory

open CategoryTheory Category Limits Sieve Classical

variable {C : Type u} [Category.{v} C]

namespace Sheaf

variable {P : C·µí·µñ ‚•§ Type v}

variable {X Y : C} {S : Sieve X} {R : Presieve X}

variable (J J‚ÇÇ : GrothendieckTopology C)

/--
To show `P` is a sheaf for the binding of `U` with `B`, it suffices to show that `P` is a sheaf for
`U`, that `P` is a sheaf for each sieve in `B`, and that it is separated for any pullback of any
sieve in `B`.

This is mostly an auxiliary lemma to show `isSheafFor_trans`.
Adapted from [Elephant], Lemma C2.1.7(i) with suggestions as mentioned in
https://math.stackexchange.com/a/358709/
-/
theorem isSheafFor_bind (P : C·µí·µñ ‚•§ Type v) (U : Sieve X) (B : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ, U f ‚Üí Sieve Y)
    (hU : Presieve.IsSheafFor P (U : Presieve X))
    (hB : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ (hf : U f), Presieve.IsSheafFor P (B hf : Presieve Y))
    (hB' : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ (h : U f) ‚¶ÉZ‚¶Ñ (g : Z ‚ü∂ Y),
      Presieve.IsSeparatedFor P (((B h).pullback g) : Presieve Z)) :
    Presieve.IsSheafFor P (Sieve.bind (U : Presieve X) B : Presieve X) := by
  intro s hs
  -- ‚ä¢ ‚àÉ! t, Presieve.FamilyOfElements.IsAmalgamation s t
  let y : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ (hf : U f), Presieve.FamilyOfElements P (B hf : Presieve Y) :=
    fun Y f hf Z g hg => s _ (Presieve.bind_comp _ _ hg)
  have hy : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ (hf : U f), (y hf).Compatible := by
    intro Y f H Y‚ÇÅ Y‚ÇÇ Z g‚ÇÅ g‚ÇÇ f‚ÇÅ f‚ÇÇ hf‚ÇÅ hf‚ÇÇ comm
    apply hs
    apply reassoc_of% comm
  let t : Presieve.FamilyOfElements P (U : Presieve X) :=
    fun Y f hf => (hB hf).amalgamate (y hf) (hy hf)
  have ht : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ (hf : U f), (y hf).IsAmalgamation (t f hf) := fun Y f hf =>
    (hB hf).isAmalgamation _
  have hT : t.Compatible := by
    rw [Presieve.compatible_iff_sieveCompatible]
    intro Z W f h hf
    apply (hB (U.downward_closed hf h)).isSeparatedFor.ext
    intro Y l hl
    apply (hB' hf (l ‚â´ h)).ext
    intro M m hm
    have : bind U B (m ‚â´ l ‚â´ h ‚â´ f) := by
      -- porting note: had to make explicit the parameter `((m ‚â´ l ‚â´ h) ‚â´ f)` and
      -- using `by exact`
      have : bind U B ((m ‚â´ l ‚â´ h) ‚â´ f) := by exact Presieve.bind_comp f hf hm
      simpa using this
    trans s (m ‚â´ l ‚â´ h ‚â´ f) this
    ¬∑ have := ht (U.downward_closed hf h) _ ((B _).downward_closed hl m)
      rw [op_comp, FunctorToTypes.map_comp_apply] at this
      rw [this]
      change s _ _ = s _ _
      -- porting note: the proof was `by simp`
      congr 1
      simp only [assoc]
    ¬∑ have h : s _ _ = _ := (ht hf _ hm).symm
      -- porting note: this was done by `simp only [assoc] at`
      conv_lhs at h => congr; rw [assoc, assoc]
      rw [h]
      simp only [op_comp, assoc, FunctorToTypes.map_comp_apply]
  refine' ‚ü®hU.amalgamate t hT, _, _‚ü©
  -- ‚ä¢ (fun t => Presieve.FamilyOfElements.IsAmalgamation s t) (Presieve.IsSheafFor ‚Ä¶
  ¬∑ rintro Z _ ‚ü®Y, f, g, hg, hf, rfl‚ü©
    -- ‚ä¢ P.map (f ‚â´ g).op (Presieve.IsSheafFor.amalgamate hU t hT) = s (f ‚â´ g) (_ : ‚àÉ ‚Ä¶
    rw [op_comp, FunctorToTypes.map_comp_apply, Presieve.IsSheafFor.valid_glue _ _ _ hg]
    -- ‚ä¢ P.map f.op (t g hg) = s (f ‚â´ g) (_ : ‚àÉ Y_1 g_1 f_1 H, (fun Y f h => (B h).ar ‚Ä¶
    apply ht hg _ hf
    -- üéâ no goals
  ¬∑ intro y hy
    -- ‚ä¢ y = Presieve.IsSheafFor.amalgamate hU t hT
    apply hU.isSeparatedFor.ext
    -- ‚ä¢ ‚àÄ ‚¶ÉY : C‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ, U.arrows f ‚Üí P.map f.op y = P.map f.op (Presieve.IsSh ‚Ä¶
    intro Y f hf
    -- ‚ä¢ P.map f.op y = P.map f.op (Presieve.IsSheafFor.amalgamate hU t hT)
    apply (hB hf).isSeparatedFor.ext
    -- ‚ä¢ ‚àÄ ‚¶ÉY_1 : C‚¶Ñ ‚¶Éf_1 : Y_1 ‚ü∂ Y‚¶Ñ, (B hf).arrows f_1 ‚Üí P.map f_1.op (P.map f.op y) ‚Ä¶
    intro Z g hg
    -- ‚ä¢ P.map g.op (P.map f.op y) = P.map g.op (P.map f.op (Presieve.IsSheafFor.amal ‚Ä¶
    rw [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, hy _ (Presieve.bind_comp _ _ hg),
      hU.valid_glue _ _ hf, ht hf _ hg]
#align category_theory.sheaf.is_sheaf_for_bind CategoryTheory.Sheaf.isSheafFor_bind

/-- Given two sieves `R` and `S`, to show that `P` is a sheaf for `S`, we can show:
* `P` is a sheaf for `R`
* `P` is a sheaf for the pullback of `S` along any arrow in `R`
* `P` is separated for the pullback of `R` along any arrow in `S`.

This is mostly an auxiliary lemma to construct `finestTopology`.
Adapted from [Elephant], Lemma C2.1.7(ii) with suggestions as mentioned in
https://math.stackexchange.com/a/358709
-/
theorem isSheafFor_trans (P : C·µí·µñ ‚•§ Type v) (R S : Sieve X)
    (hR : Presieve.IsSheafFor P (R : Presieve X))
    (hR' : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ (_ : S f), Presieve.IsSeparatedFor P (R.pullback f : Presieve Y))
    (hS : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ (_ : R f), Presieve.IsSheafFor P (S.pullback f : Presieve Y)) :
    Presieve.IsSheafFor P (S : Presieve X) := by
  have : (bind R fun Y f _ => S.pullback f : Presieve X) ‚â§ S := by
    rintro Z f ‚ü®W, f, g, hg, hf : S _, rfl‚ü©
    apply hf
  apply Presieve.isSheafFor_subsieve_aux P this
  -- ‚ä¢ Presieve.IsSheafFor P (Sieve.bind R.arrows fun Y f x => Sieve.pullback f S). ‚Ä¶
  apply isSheafFor_bind _ _ _ hR hS
  -- ‚ä¢ ‚àÄ ‚¶ÉY : C‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ, R.arrows f ‚Üí ‚àÄ ‚¶ÉZ : C‚¶Ñ (g : Z ‚ü∂ Y), Presieve.IsSepara ‚Ä¶
  ¬∑ intro Y f hf Z g
    -- ‚ä¢ Presieve.IsSeparatedFor P (Sieve.pullback g (Sieve.pullback f S)).arrows
    rw [‚Üê pullback_comp]
    -- ‚ä¢ Presieve.IsSeparatedFor P (Sieve.pullback (g ‚â´ f) S).arrows
    apply (hS (R.downward_closed hf _)).isSeparatedFor
    -- üéâ no goals
  ¬∑ intro Y f hf
    -- ‚ä¢ Presieve.IsSeparatedFor P (Sieve.pullback f (Sieve.bind R.arrows fun Y f x = ‚Ä¶
    have : Sieve.pullback f (bind R fun T (k : T ‚ü∂ X) (_ : R k) => pullback k S) =
        R.pullback f := by
      ext Z g
      constructor
      ¬∑ rintro ‚ü®W, k, l, hl, _, comm‚ü©
        rw [pullback_apply, ‚Üê comm]
        simp [hl]
      ¬∑ intro a
        refine' ‚ü®Z, ùüô Z, _, a, _‚ü©
        simp [hf]
    rw [this]
    -- ‚ä¢ Presieve.IsSeparatedFor P (Sieve.pullback f R).arrows
    apply hR' hf
    -- üéâ no goals
#align category_theory.sheaf.is_sheaf_for_trans CategoryTheory.Sheaf.isSheafFor_trans

/-- Construct the finest (largest) Grothendieck topology for which the given presheaf is a sheaf.

This is a special case of https://stacks.math.columbia.edu/tag/00Z9, but following a different
proof (see the comments there).
-/
def finestTopologySingle (P : C·µí·µñ ‚•§ Type v) : GrothendieckTopology C where
  sieves X S := ‚àÄ (Y) (f : Y ‚ü∂ X), Presieve.IsSheafFor P (S.pullback f : Presieve Y)
  top_mem' X Y f := by
    rw [Sieve.pullback_top]
    -- ‚ä¢ Presieve.IsSheafFor P ‚ä§.arrows
    exact Presieve.isSheafFor_top_sieve P
    -- üéâ no goals
  pullback_stable' X Y S f hS Z g := by
    rw [‚Üê pullback_comp]
    -- ‚ä¢ Presieve.IsSheafFor P (Sieve.pullback (g ‚â´ f) S).arrows
    apply hS
    -- üéâ no goals
  transitive' X S hS R hR Z g := by
    -- This is the hard part of the construction, showing that the given set of sieves satisfies
    -- the transitivity axiom.
    refine' isSheafFor_trans P (pullback g S) _ (hS Z g) _ _
    -- ‚ä¢ ‚àÄ ‚¶ÉY : C‚¶Ñ ‚¶Éf : Y ‚ü∂ Z‚¶Ñ, (Sieve.pullback g R).arrows f ‚Üí Presieve.IsSeparatedF ‚Ä¶
    ¬∑ intro Y f _
      -- ‚ä¢ Presieve.IsSeparatedFor P (Sieve.pullback f (Sieve.pullback g S)).arrows
      rw [‚Üê pullback_comp]
      -- ‚ä¢ Presieve.IsSeparatedFor P (Sieve.pullback (f ‚â´ g) S).arrows
      apply (hS _ _).isSeparatedFor
      -- üéâ no goals
    ¬∑ intro Y f hf
      -- ‚ä¢ Presieve.IsSheafFor P (Sieve.pullback f (Sieve.pullback g R)).arrows
      have := hR hf _ (ùüô _)
      -- ‚ä¢ Presieve.IsSheafFor P (Sieve.pullback f (Sieve.pullback g R)).arrows
      rw [pullback_id, pullback_comp] at this
      -- ‚ä¢ Presieve.IsSheafFor P (Sieve.pullback f (Sieve.pullback g R)).arrows
      apply this
      -- üéâ no goals
#align category_theory.sheaf.finest_topology_single CategoryTheory.Sheaf.finestTopologySingle

/--
Construct the finest (largest) Grothendieck topology for which all the given presheaves are sheaves.

This is equal to the construction of <https://stacks.math.columbia.edu/tag/00Z9>.
-/
def finestTopology (Ps : Set (C·µí·µñ ‚•§ Type v)) : GrothendieckTopology C :=
  sInf (finestTopologySingle '' Ps)
#align category_theory.sheaf.finest_topology CategoryTheory.Sheaf.finestTopology

/-- Check that if `P ‚àà Ps`, then `P` is indeed a sheaf for the finest topology on `Ps`. -/
theorem sheaf_for_finestTopology (Ps : Set (C·µí·µñ ‚•§ Type v)) (h : P ‚àà Ps) :
    Presieve.IsSheaf (finestTopology Ps) P := fun X S hS => by
  simpa using hS _ ‚ü®‚ü®_, _, ‚ü®_, h, rfl‚ü©, rfl‚ü©, rfl‚ü© _ (ùüô _)
  -- üéâ no goals
#align category_theory.sheaf.sheaf_for_finest_topology CategoryTheory.Sheaf.sheaf_for_finestTopology

/--
Check that if each `P ‚àà Ps` is a sheaf for `J`, then `J` is a subtopology of `finestTopology Ps`.
-/
theorem le_finestTopology (Ps : Set (C·µí·µñ ‚•§ Type v)) (J : GrothendieckTopology C)
    (hJ : ‚àÄ P ‚àà Ps, Presieve.IsSheaf J P) : J ‚â§ finestTopology Ps := by
  rintro X S hS _ ‚ü®‚ü®_, _, ‚ü®P, hP, rfl‚ü©, rfl‚ü©, rfl‚ü©
  -- ‚ä¢ S ‚àà (fun f => ‚Üëf X) { val := (finestTopologySingle P).sieves, property := (_ ‚Ä¶
  intro Y f
  -- ‚ä¢ Presieve.IsSheafFor P (Sieve.pullback f S).arrows
  -- this can't be combined with the previous because the `subst` is applied at the end
  exact hJ P hP (S.pullback f) (J.pullback_stable f hS)
  -- üéâ no goals
#align category_theory.sheaf.le_finest_topology CategoryTheory.Sheaf.le_finestTopology

/-- The `canonicalTopology` on a category is the finest (largest) topology for which every
representable presheaf is a sheaf.

See <https://stacks.math.columbia.edu/tag/00ZA>
-/
def canonicalTopology (C : Type u) [Category.{v} C] : GrothendieckTopology C :=
  finestTopology (Set.range yoneda.obj)
#align category_theory.sheaf.canonical_topology CategoryTheory.Sheaf.canonicalTopology

/-- `yoneda.obj X` is a sheaf for the canonical topology. -/
theorem isSheaf_yoneda_obj (X : C) : Presieve.IsSheaf (canonicalTopology C) (yoneda.obj X) :=
  fun _ _ hS => sheaf_for_finestTopology _ (Set.mem_range_self _) _ hS
#align category_theory.sheaf.is_sheaf_yoneda_obj CategoryTheory.Sheaf.isSheaf_yoneda_obj

/-- A representable functor is a sheaf for the canonical topology. -/
theorem isSheaf_of_representable (P : C·µí·µñ ‚•§ Type v) [P.Representable] :
    Presieve.IsSheaf (canonicalTopology C) P :=
  Presieve.isSheaf_iso (canonicalTopology C) P.reprW (isSheaf_yoneda_obj _)
#align category_theory.sheaf.is_sheaf_of_representable CategoryTheory.Sheaf.isSheaf_of_representable

/-- A subcanonical topology is a topology which is smaller than the canonical topology.
Equivalently, a topology is subcanonical iff every representable is a sheaf.
-/
def Subcanonical (J : GrothendieckTopology C) : Prop :=
  J ‚â§ canonicalTopology C
#align category_theory.sheaf.subcanonical CategoryTheory.Sheaf.Subcanonical

namespace Subcanonical

/-- If every functor `yoneda.obj X` is a `J`-sheaf, then `J` is subcanonical. -/
theorem of_yoneda_isSheaf (J : GrothendieckTopology C)
    (h : ‚àÄ X, Presieve.IsSheaf J (yoneda.obj X)) : Subcanonical J :=
  le_finestTopology _ _
    (by
      rintro P ‚ü®X, rfl‚ü©
      -- ‚ä¢ Presieve.IsSheaf J (yoneda.obj X)
      apply h)
      -- üéâ no goals
#align category_theory.sheaf.subcanonical.of_yoneda_is_sheaf CategoryTheory.Sheaf.Subcanonical.of_yoneda_isSheaf

/-- If `J` is subcanonical, then any representable is a `J`-sheaf. -/
theorem isSheaf_of_representable {J : GrothendieckTopology C} (hJ : Subcanonical J)
    (P : C·µí·µñ ‚•§ Type v) [P.Representable] : Presieve.IsSheaf J P :=
  Presieve.isSheaf_of_le _ hJ (Sheaf.isSheaf_of_representable P)
#align category_theory.sheaf.subcanonical.is_sheaf_of_representable CategoryTheory.Sheaf.Subcanonical.isSheaf_of_representable

end Subcanonical

end Sheaf

end CategoryTheory
