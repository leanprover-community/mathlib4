/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Sites.SheafOfTypes
import Mathlib.Order.Closure

#align_import category_theory.sites.closed from "leanprover-community/mathlib"@"4cfc30e317caad46858393f1a7a33f609296cc30"

/-!
# Closed sieves

A natural closure operator on sieves is a closure operator on `Sieve X` for each `X` which commutes
with pullback.
We show that a Grothendieck topology `J` induces a natural closure operator, and define what the
closed sieves are. The collection of `J`-closed sieves forms a presheaf which is a sheaf for `J`,
and further this presheaf can be used to determine the Grothendieck topology from the sheaf
predicate.
Finally we show that a natural closure operator on sieves induces a Grothendieck topology, and hence
that natural closure operators are in bijection with Grothendieck topologies.

## Main definitions

* `CategoryTheory.GrothendieckTopology.close`: Sends a sieve `S` on `X` to the set of arrows
  which it covers. This has all the usual properties of a closure operator, as well as commuting
  with pullback.
* `CategoryTheory.GrothendieckTopology.closureOperator`: The bundled `ClosureOperator` given
  by `CategoryTheory.GrothendieckTopology.close`.
* `CategoryTheory.GrothendieckTopology.IsClosed`: A sieve `S` on `X` is closed for the topology `J`
   if it contains every arrow it covers.
* `CategoryTheory.Functor.closedSieves`: The presheaf sending `X` to the collection of `J`-closed
  sieves on `X`. This is additionally shown to be a sheaf for `J`, and if this is a sheaf for a
  different topology `J'`, then `J' ‚â§ J`.
* `CategoryTheory.topologyOfClosureOperator`: A closure operator on the
  set of sieves on every object which commutes with pullback additionally induces a Grothendieck
  topology, giving a bijection with `CategoryTheory.GrothendieckTopology.closureOperator`.


## Tags

closed sieve, closure, Grothendieck topology

## References

* [S. MacLane, I. Moerdijk, *Sheaves in Geometry and Logic*][MM92]
-/


universe v u

namespace CategoryTheory

variable {C : Type u} [Category.{v} C]

variable (J‚ÇÅ J‚ÇÇ : GrothendieckTopology C)

namespace GrothendieckTopology

/-- The `J`-closure of a sieve is the collection of arrows which it covers. -/
@[simps]
def close {X : C} (S : Sieve X) : Sieve X where
  arrows _ f := J‚ÇÅ.Covers S f
  downward_closed hS := J‚ÇÅ.arrow_stable _ _ hS
#align category_theory.grothendieck_topology.close CategoryTheory.GrothendieckTopology.close

/-- Any sieve is smaller than its closure. -/
theorem le_close {X : C} (S : Sieve X) : S ‚â§ J‚ÇÅ.close S :=
  fun _ _ hg => J‚ÇÅ.covering_of_eq_top (S.pullback_eq_top_of_mem hg)
#align category_theory.grothendieck_topology.le_close CategoryTheory.GrothendieckTopology.le_close

/-- A sieve is closed for the Grothendieck topology if it contains every arrow it covers.
In the case of the usual topology on a topological space, this means that the open cover contains
every open set which it covers.

Note this has no relation to a closed subset of a topological space.
-/
def IsClosed {X : C} (S : Sieve X) : Prop :=
  ‚àÄ ‚¶ÉY : C‚¶Ñ (f : Y ‚ü∂ X), J‚ÇÅ.Covers S f ‚Üí S f
#align category_theory.grothendieck_topology.is_closed CategoryTheory.GrothendieckTopology.IsClosed

/-- If `S` is `J‚ÇÅ`-closed, then `S` covers exactly the arrows it contains. -/
theorem covers_iff_mem_of_isClosed {X : C} {S : Sieve X} (h : J‚ÇÅ.IsClosed S) {Y : C} (f : Y ‚ü∂ X) :
    J‚ÇÅ.Covers S f ‚Üî S f :=
  ‚ü®h _, J‚ÇÅ.arrow_max _ _‚ü©
#align category_theory.grothendieck_topology.covers_iff_mem_of_closed CategoryTheory.GrothendieckTopology.covers_iff_mem_of_isClosed

/-- Being `J`-closed is stable under pullback. -/
theorem isClosed_pullback {X Y : C} (f : Y ‚ü∂ X) (S : Sieve X) :
    J‚ÇÅ.IsClosed S ‚Üí J‚ÇÅ.IsClosed (S.pullback f) :=
  fun hS Z g hg => hS (g ‚â´ f) (by rwa [J‚ÇÅ.covers_iff, Sieve.pullback_comp])
                                  -- üéâ no goals
#align category_theory.grothendieck_topology.is_closed_pullback CategoryTheory.GrothendieckTopology.isClosed_pullback

/-- The closure of a sieve `S` is the largest closed sieve which contains `S` (justifying the name
"closure").
-/
theorem le_close_of_isClosed {X : C} {S T : Sieve X} (h : S ‚â§ T) (hT : J‚ÇÅ.IsClosed T) :
    J‚ÇÅ.close S ‚â§ T :=
  fun _ f hf => hT _ (J‚ÇÅ.superset_covering (Sieve.pullback_monotone f h) hf)
#align category_theory.grothendieck_topology.le_close_of_is_closed CategoryTheory.GrothendieckTopology.le_close_of_isClosed

/-- The closure of a sieve is closed. -/
theorem close_isClosed {X : C} (S : Sieve X) : J‚ÇÅ.IsClosed (J‚ÇÅ.close S) :=
  fun _ g hg => J‚ÇÅ.arrow_trans g _ S hg fun _ hS => hS
#align category_theory.grothendieck_topology.close_is_closed CategoryTheory.GrothendieckTopology.close_isClosed

/-- The sieve `S` is closed iff its closure is equal to itself. -/
theorem isClosed_iff_close_eq_self {X : C} (S : Sieve X) : J‚ÇÅ.IsClosed S ‚Üî J‚ÇÅ.close S = S := by
  constructor
  -- ‚ä¢ IsClosed J‚ÇÅ S ‚Üí close J‚ÇÅ S = S
  ¬∑ intro h
    -- ‚ä¢ close J‚ÇÅ S = S
    apply le_antisymm
    -- ‚ä¢ close J‚ÇÅ S ‚â§ S
    ¬∑ intro Y f hf
      -- ‚ä¢ S.arrows f
      rw [‚Üê J‚ÇÅ.covers_iff_mem_of_isClosed h]
      -- ‚ä¢ Covers J‚ÇÅ S f
      apply hf
      -- üéâ no goals
    ¬∑ apply J‚ÇÅ.le_close
      -- üéâ no goals
  ¬∑ intro e
    -- ‚ä¢ IsClosed J‚ÇÅ S
    rw [‚Üê e]
    -- ‚ä¢ IsClosed J‚ÇÅ (close J‚ÇÅ S)
    apply J‚ÇÅ.close_isClosed
    -- üéâ no goals
#align category_theory.grothendieck_topology.is_closed_iff_close_eq_self CategoryTheory.GrothendieckTopology.isClosed_iff_close_eq_self

theorem close_eq_self_of_isClosed {X : C} {S : Sieve X} (hS : J‚ÇÅ.IsClosed S) : J‚ÇÅ.close S = S :=
  (J‚ÇÅ.isClosed_iff_close_eq_self S).1 hS
#align category_theory.grothendieck_topology.close_eq_self_of_is_closed CategoryTheory.GrothendieckTopology.close_eq_self_of_isClosed

/-- Closing under `J` is stable under pullback. -/
theorem pullback_close {X Y : C} (f : Y ‚ü∂ X) (S : Sieve X) :
    J‚ÇÅ.close (S.pullback f) = (J‚ÇÅ.close S).pullback f := by
  apply le_antisymm
  -- ‚ä¢ close J‚ÇÅ (Sieve.pullback f S) ‚â§ Sieve.pullback f (close J‚ÇÅ S)
  ¬∑ refine' J‚ÇÅ.le_close_of_isClosed (Sieve.pullback_monotone _ (J‚ÇÅ.le_close S)) _
    -- ‚ä¢ IsClosed J‚ÇÅ (Sieve.pullback f (close J‚ÇÅ S))
    apply J‚ÇÅ.isClosed_pullback _ _ (J‚ÇÅ.close_isClosed _)
    -- üéâ no goals
  ¬∑ intro Z g hg
    -- ‚ä¢ (close J‚ÇÅ (Sieve.pullback f S)).arrows g
    change _ ‚àà J‚ÇÅ _
    -- ‚ä¢ Sieve.pullback g (Sieve.pullback f S) ‚àà sieves J‚ÇÅ Z
    rw [‚Üê Sieve.pullback_comp]
    -- ‚ä¢ Sieve.pullback (g ‚â´ f) S ‚àà sieves J‚ÇÅ Z
    apply hg
    -- üéâ no goals
#align category_theory.grothendieck_topology.pullback_close CategoryTheory.GrothendieckTopology.pullback_close

@[mono]
theorem monotone_close {X : C} : Monotone (J‚ÇÅ.close : Sieve X ‚Üí Sieve X) :=
  fun _ S‚ÇÇ h => J‚ÇÅ.le_close_of_isClosed (h.trans (J‚ÇÅ.le_close _)) (J‚ÇÅ.close_isClosed S‚ÇÇ)
#align category_theory.grothendieck_topology.monotone_close CategoryTheory.GrothendieckTopology.monotone_close

@[simp]
theorem close_close {X : C} (S : Sieve X) : J‚ÇÅ.close (J‚ÇÅ.close S) = J‚ÇÅ.close S :=
  le_antisymm (J‚ÇÅ.le_close_of_isClosed le_rfl (J‚ÇÅ.close_isClosed S))
    (J‚ÇÅ.monotone_close (J‚ÇÅ.le_close _))
#align category_theory.grothendieck_topology.close_close CategoryTheory.GrothendieckTopology.close_close

/--
The sieve `S` is in the topology iff its closure is the maximal sieve. This shows that the closure
operator determines the topology.
-/
theorem close_eq_top_iff_mem {X : C} (S : Sieve X) : J‚ÇÅ.close S = ‚ä§ ‚Üî S ‚àà J‚ÇÅ X := by
  constructor
  -- ‚ä¢ close J‚ÇÅ S = ‚ä§ ‚Üí S ‚àà sieves J‚ÇÅ X
  ¬∑ intro h
    -- ‚ä¢ S ‚àà sieves J‚ÇÅ X
    apply J‚ÇÅ.transitive (J‚ÇÅ.top_mem X)
    -- ‚ä¢ ‚àÄ ‚¶ÉY : C‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ, ‚ä§.arrows f ‚Üí Sieve.pullback f S ‚àà sieves J‚ÇÅ Y
    intro Y f hf
    -- ‚ä¢ Sieve.pullback f S ‚àà sieves J‚ÇÅ Y
    change J‚ÇÅ.close S f
    -- ‚ä¢ (close J‚ÇÅ S).arrows f
    rwa [h]
    -- üéâ no goals
  ¬∑ intro hS
    -- ‚ä¢ close J‚ÇÅ S = ‚ä§
    rw [eq_top_iff]
    -- ‚ä¢ ‚ä§ ‚â§ close J‚ÇÅ S
    intro Y f _
    -- ‚ä¢ (close J‚ÇÅ S).arrows f
    apply J‚ÇÅ.pullback_stable _ hS
    -- üéâ no goals
#align category_theory.grothendieck_topology.close_eq_top_iff_mem CategoryTheory.GrothendieckTopology.close_eq_top_iff_mem

/-- A Grothendieck topology induces a natural family of closure operators on sieves. -/
@[simps!]
def closureOperator (X : C) : ClosureOperator (Sieve X) :=
  ClosureOperator.mk' J‚ÇÅ.close
    (fun _ S‚ÇÇ h => J‚ÇÅ.le_close_of_isClosed (h.trans (J‚ÇÅ.le_close _)) (J‚ÇÅ.close_isClosed S‚ÇÇ))
    J‚ÇÅ.le_close fun S => J‚ÇÅ.le_close_of_isClosed le_rfl (J‚ÇÅ.close_isClosed S)
#align category_theory.grothendieck_topology.closure_operator CategoryTheory.GrothendieckTopology.closureOperator

@[simp]
theorem closed_iff_closed {X : C} (S : Sieve X) :
    S ‚àà (J‚ÇÅ.closureOperator X).closed ‚Üî J‚ÇÅ.IsClosed S :=
  (J‚ÇÅ.isClosed_iff_close_eq_self S).symm
#align category_theory.grothendieck_topology.closed_iff_closed CategoryTheory.GrothendieckTopology.closed_iff_closed

end GrothendieckTopology

/--
The presheaf sending each object to the set of `J`-closed sieves on it. This presheaf is a `J`-sheaf
(and will turn out to be a subobject classifier for the category of `J`-sheaves).
-/
@[simps]
def Functor.closedSieves : C·µí·µñ ‚•§ Type max v u where
  obj X := { S : Sieve X.unop // J‚ÇÅ.IsClosed S }
  map f S := ‚ü®S.1.pullback f.unop, J‚ÇÅ.isClosed_pullback f.unop _ S.2‚ü©
#align category_theory.functor.closed_sieves CategoryTheory.Functor.closedSieves

/-- The presheaf of `J`-closed sieves is a `J`-sheaf.
The proof of this is adapted from [MM92], Chapter III, Section 7, Lemma 1.
-/
theorem classifier_isSheaf : Presieve.IsSheaf J‚ÇÅ (Functor.closedSieves J‚ÇÅ) := by
  intro X S hS
  -- ‚ä¢ Presieve.IsSheafFor (Functor.closedSieves J‚ÇÅ) S.arrows
  rw [‚Üê Presieve.isSeparatedFor_and_exists_isAmalgamation_iff_isSheafFor]
  -- ‚ä¢ Presieve.IsSeparatedFor (Functor.closedSieves J‚ÇÅ) S.arrows ‚àß ‚àÄ (x : Presieve ‚Ä¶
  refine' ‚ü®_, _‚ü©
  -- ‚ä¢ Presieve.IsSeparatedFor (Functor.closedSieves J‚ÇÅ) S.arrows
  ¬∑ rintro x ‚ü®M, hM‚ü© ‚ü®N, hN‚ü© hM‚ÇÇ hN‚ÇÇ
    -- ‚ä¢ { val := M, property := hM } = { val := N, property := hN }
    simp only [Functor.closedSieves_obj]
    -- ‚ä¢ { val := M, property := hM } = { val := N, property := hN }
    ext Y f
    -- ‚ä¢ (‚Üë{ val := M, property := hM }).arrows f ‚Üî (‚Üë{ val := N, property := hN }).a ‚Ä¶
    dsimp only [Subtype.coe_mk]
    -- ‚ä¢ M.arrows f ‚Üî N.arrows f
    rw [‚Üê J‚ÇÅ.covers_iff_mem_of_isClosed hM, ‚Üê J‚ÇÅ.covers_iff_mem_of_isClosed hN]
    -- ‚ä¢ GrothendieckTopology.Covers J‚ÇÅ M f ‚Üî GrothendieckTopology.Covers J‚ÇÅ N f
    have q : ‚àÄ ‚¶ÉZ : C‚¶Ñ (g : Z ‚ü∂ X) (_ : S g), M.pullback g = N.pullback g :=
      fun Z g hg => congr_arg Subtype.val ((hM‚ÇÇ g hg).trans (hN‚ÇÇ g hg).symm)
    have MSNS : M ‚äì S = N ‚äì S := by
      ext Z g
      rw [Sieve.inter_apply, Sieve.inter_apply]
      simp only [and_comm]
      apply and_congr_right
      intro hg
      rw [Sieve.pullback_eq_top_iff_mem, Sieve.pullback_eq_top_iff_mem, q g hg]
    constructor
    -- ‚ä¢ GrothendieckTopology.Covers J‚ÇÅ M f ‚Üí GrothendieckTopology.Covers J‚ÇÅ N f
    ¬∑ intro hf
      -- ‚ä¢ GrothendieckTopology.Covers J‚ÇÅ N f
      rw [J‚ÇÅ.covers_iff]
      -- ‚ä¢ Sieve.pullback f N ‚àà GrothendieckTopology.sieves J‚ÇÅ Y
      apply J‚ÇÅ.superset_covering (Sieve.pullback_monotone f inf_le_left)
      -- ‚ä¢ Sieve.pullback f (N ‚äì ?m.20423) ‚àà GrothendieckTopology.sieves J‚ÇÅ Y
      rw [‚Üê MSNS]
      -- ‚ä¢ Sieve.pullback f (M ‚äì S) ‚àà GrothendieckTopology.sieves J‚ÇÅ Y
      apply J‚ÇÅ.arrow_intersect f M S hf (J‚ÇÅ.pullback_stable _ hS)
      -- üéâ no goals
    ¬∑ intro hf
      -- ‚ä¢ GrothendieckTopology.Covers J‚ÇÅ M f
      rw [J‚ÇÅ.covers_iff]
      -- ‚ä¢ Sieve.pullback f M ‚àà GrothendieckTopology.sieves J‚ÇÅ Y
      apply J‚ÇÅ.superset_covering (Sieve.pullback_monotone f inf_le_left)
      -- ‚ä¢ Sieve.pullback f (M ‚äì ?m.20633) ‚àà GrothendieckTopology.sieves J‚ÇÅ Y
      rw [MSNS]
      -- ‚ä¢ Sieve.pullback f (N ‚äì S) ‚àà GrothendieckTopology.sieves J‚ÇÅ Y
      apply J‚ÇÅ.arrow_intersect f N S hf (J‚ÇÅ.pullback_stable _ hS)
      -- üéâ no goals
  ¬∑ intro x hx
    -- ‚ä¢ ‚àÉ t, Presieve.FamilyOfElements.IsAmalgamation x t
    rw [Presieve.compatible_iff_sieveCompatible] at hx
    -- ‚ä¢ ‚àÉ t, Presieve.FamilyOfElements.IsAmalgamation x t
    let M := Sieve.bind S fun Y f hf => (x f hf).1
    -- ‚ä¢ ‚àÉ t, Presieve.FamilyOfElements.IsAmalgamation x t
    have : ‚àÄ ‚¶ÉY‚¶Ñ (f : Y ‚ü∂ X) (hf : S f), M.pullback f = (x f hf).1 := by
      intro Y f hf
      apply le_antisymm
      ¬∑ rintro Z u ‚ü®W, g, f', hf', hg : (x f' hf').1 _, c‚ü©
        rw [Sieve.pullback_eq_top_iff_mem,
          ‚Üê show (x (u ‚â´ f) _).1 = (x f hf).1.pullback u from congr_arg Subtype.val (hx f u hf)]
        conv_lhs => congr; congr; rw [‚Üê c] -- Porting note: Originally `simp_rw [‚Üê c]`
        rw [show (x (g ‚â´ f') _).1 = _ from congr_arg Subtype.val (hx f' g hf')]
        apply Sieve.pullback_eq_top_of_mem _ hg
      ¬∑ apply Sieve.le_pullback_bind S fun Y f hf => (x f hf).1
    refine' ‚ü®‚ü®_, J‚ÇÅ.close_isClosed M‚ü©, _‚ü©
    -- ‚ä¢ Presieve.FamilyOfElements.IsAmalgamation x { val := GrothendieckTopology.clo ‚Ä¶
    ¬∑ intro Y f hf
      -- ‚ä¢ (Functor.closedSieves J‚ÇÅ).map f.op { val := GrothendieckTopology.close J‚ÇÅ M, ‚Ä¶
      simp only [Functor.closedSieves_obj]
      -- ‚ä¢ (Functor.closedSieves J‚ÇÅ).map f.op { val := GrothendieckTopology.close J‚ÇÅ (S ‚Ä¶
      ext1
      -- ‚ä¢ ‚Üë((Functor.closedSieves J‚ÇÅ).map f.op { val := GrothendieckTopology.close J‚ÇÅ  ‚Ä¶
      dsimp
      -- ‚ä¢ Sieve.pullback f (GrothendieckTopology.close J‚ÇÅ (Sieve.bind S.arrows fun Y f ‚Ä¶
      rw [‚Üê J‚ÇÅ.pullback_close, this _ hf]
      -- ‚ä¢ GrothendieckTopology.close J‚ÇÅ ‚Üë(x f hf) = ‚Üë(x f hf)
      apply le_antisymm (J‚ÇÅ.le_close_of_isClosed le_rfl (x f hf).2) (J‚ÇÅ.le_close _)
      -- üéâ no goals
#align category_theory.classifier_is_sheaf CategoryTheory.classifier_isSheaf

/-- If presheaf of `J‚ÇÅ`-closed sieves is a `J‚ÇÇ`-sheaf then `J‚ÇÅ ‚â§ J‚ÇÇ`. Note the converse is true by
`classifier_isSheaf` and `isSheaf_of_le`.
-/
theorem le_topology_of_closedSieves_isSheaf {J‚ÇÅ J‚ÇÇ : GrothendieckTopology C}
    (h : Presieve.IsSheaf J‚ÇÅ (Functor.closedSieves J‚ÇÇ)) : J‚ÇÅ ‚â§ J‚ÇÇ := by
  intro X S hS
  -- ‚ä¢ S ‚àà GrothendieckTopology.sieves J‚ÇÇ X
  rw [‚Üê J‚ÇÇ.close_eq_top_iff_mem]
  -- ‚ä¢ GrothendieckTopology.close J‚ÇÇ S = ‚ä§
  have : J‚ÇÇ.IsClosed (‚ä§ : Sieve X) := by
    intro Y f _
    trivial
  suffices (‚ü®J‚ÇÇ.close S, J‚ÇÇ.close_isClosed S‚ü© : Subtype _) = ‚ü®‚ä§, this‚ü© by
    rw [Subtype.ext_iff] at this
    exact this
  apply (h S hS).isSeparatedFor.ext
  -- ‚ä¢ ‚àÄ ‚¶ÉY : C‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ, S.arrows f ‚Üí (Functor.closedSieves J‚ÇÇ).map f.op { val ‚Ä¶
  ¬∑ intro Y f hf
    -- ‚ä¢ (Functor.closedSieves J‚ÇÇ).map f.op { val := GrothendieckTopology.close J‚ÇÇ S, ‚Ä¶
    simp only [Functor.closedSieves_obj]
    -- ‚ä¢ (Functor.closedSieves J‚ÇÇ).map f.op { val := GrothendieckTopology.close J‚ÇÇ S, ‚Ä¶
    ext1
    -- ‚ä¢ ‚Üë((Functor.closedSieves J‚ÇÇ).map f.op { val := GrothendieckTopology.close J‚ÇÇ  ‚Ä¶
    dsimp
    -- ‚ä¢ Sieve.pullback f (GrothendieckTopology.close J‚ÇÇ S) = Sieve.pullback f ‚ä§
    rw [Sieve.pullback_top, ‚Üê J‚ÇÇ.pullback_close, S.pullback_eq_top_of_mem hf,
      J‚ÇÇ.close_eq_top_iff_mem]
    apply J‚ÇÇ.top_mem
    -- üéâ no goals
#align category_theory.le_topology_of_closed_sieves_is_sheaf CategoryTheory.le_topology_of_closedSieves_isSheaf

/-- If being a sheaf for `J‚ÇÅ` is equivalent to being a sheaf for `J‚ÇÇ`, then `J‚ÇÅ = J‚ÇÇ`. -/
theorem topology_eq_iff_same_sheaves {J‚ÇÅ J‚ÇÇ : GrothendieckTopology C} :
    J‚ÇÅ = J‚ÇÇ ‚Üî ‚àÄ P : C·µí·µñ ‚•§ Type max v u, Presieve.IsSheaf J‚ÇÅ P ‚Üî Presieve.IsSheaf J‚ÇÇ P := by
  constructor
  -- ‚ä¢ J‚ÇÅ = J‚ÇÇ ‚Üí ‚àÄ (P : C·µí·µñ ‚•§ Type (max v u)), Presieve.IsSheaf J‚ÇÅ P ‚Üî Presieve.IsS ‚Ä¶
  ¬∑ rintro rfl
    -- ‚ä¢ ‚àÄ (P : C·µí·µñ ‚•§ Type (max v u)), Presieve.IsSheaf J‚ÇÅ P ‚Üî Presieve.IsSheaf J‚ÇÅ P
    intro P
    -- ‚ä¢ Presieve.IsSheaf J‚ÇÅ P ‚Üî Presieve.IsSheaf J‚ÇÅ P
    rfl
    -- üéâ no goals
  ¬∑ intro h
    -- ‚ä¢ J‚ÇÅ = J‚ÇÇ
    apply le_antisymm
    -- ‚ä¢ J‚ÇÅ ‚â§ J‚ÇÇ
    ¬∑ apply le_topology_of_closedSieves_isSheaf
      -- ‚ä¢ Presieve.IsSheaf J‚ÇÅ (Functor.closedSieves J‚ÇÇ)
      rw [h]
      -- ‚ä¢ Presieve.IsSheaf J‚ÇÇ (Functor.closedSieves J‚ÇÇ)
      apply classifier_isSheaf
      -- üéâ no goals
    ¬∑ apply le_topology_of_closedSieves_isSheaf
      -- ‚ä¢ Presieve.IsSheaf J‚ÇÇ (Functor.closedSieves J‚ÇÅ)
      rw [‚Üê h]
      -- ‚ä¢ Presieve.IsSheaf J‚ÇÅ (Functor.closedSieves J‚ÇÅ)
      apply classifier_isSheaf
      -- üéâ no goals
#align category_theory.topology_eq_iff_same_sheaves CategoryTheory.topology_eq_iff_same_sheaves

/--
A closure (increasing, inflationary and idempotent) operation on sieves that commutes with pullback
induces a Grothendieck topology.
In fact, such operations are in bijection with Grothendieck topologies.
-/
@[simps]
def topologyOfClosureOperator (c : ‚àÄ X : C, ClosureOperator (Sieve X))
    (hc : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : Y ‚ü∂ X) (S : Sieve X), c _ (S.pullback f) = (c _ S).pullback f) :
    GrothendieckTopology C where
  sieves X := { S | c X S = ‚ä§ }
  top_mem' X := top_unique ((c X).le_closure _)
  pullback_stable' X Y S f hS := by
    rw [Set.mem_setOf_eq] at hS
    -- ‚ä¢ Sieve.pullback f S ‚àà (fun X => {S | ‚Üë(c X) S = ‚ä§}) Y
    rw [Set.mem_setOf_eq, hc, hS, Sieve.pullback_top]
    -- üéâ no goals
  transitive' X S hS R hR := by
    rw [Set.mem_setOf_eq] at hS
    -- ‚ä¢ R ‚àà (fun X => {S | ‚Üë(c X) S = ‚ä§}) X
    rw [Set.mem_setOf_eq, ‚Üê (c X).idempotent, eq_top_iff, ‚Üê hS]
    -- ‚ä¢ ‚Üë(c X) S ‚â§ ‚Üë(c X) (‚Üë(c X) R)
    apply (c X).monotone fun Y f hf => _
    -- ‚ä¢ ‚àÄ (Y : C) (f : Y ‚ü∂ X), S.arrows f ‚Üí (‚Üë(c X) R).arrows f
    intros Y f hf
    -- ‚ä¢ (‚Üë(c X) R).arrows f
    rw [Sieve.pullback_eq_top_iff_mem, ‚Üê hc]
    -- ‚ä¢ ‚Üë(c Y) (Sieve.pullback f R) = ‚ä§
    apply hR hf
    -- üéâ no goals
#align category_theory.topology_of_closure_operator CategoryTheory.topologyOfClosureOperator

/--
The topology given by the closure operator `J.close` on a Grothendieck topology is the same as `J`.
-/
theorem topologyOfClosureOperator_self :
    (topologyOfClosureOperator J‚ÇÅ.closureOperator fun X Y => J‚ÇÅ.pullback_close) = J‚ÇÅ := by
  ext X S
  -- ‚ä¢ S ‚àà GrothendieckTopology.sieves (topologyOfClosureOperator (GrothendieckTopo ‚Ä¶
  apply GrothendieckTopology.close_eq_top_iff_mem
  -- üéâ no goals
#align category_theory.topology_of_closure_operator_self CategoryTheory.topologyOfClosureOperator_self

theorem topologyOfClosureOperator_close (c : ‚àÄ X : C, ClosureOperator (Sieve X))
    (pb : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : Y ‚ü∂ X) (S : Sieve X), c Y (S.pullback f) = (c X S).pullback f) (X : C)
    (S : Sieve X) : (topologyOfClosureOperator c pb).close S = c X S := by
  ext Y f
  -- ‚ä¢ (GrothendieckTopology.close (topologyOfClosureOperator c pb) S).arrows f ‚Üî ( ‚Ä¶
  change c _ (Sieve.pullback f S) = ‚ä§ ‚Üî c _ S f
  -- ‚ä¢ ‚Üë(c Y) (Sieve.pullback f S) = ‚ä§ ‚Üî (‚Üë(c X) S).arrows f
  rw [pb, Sieve.pullback_eq_top_iff_mem]
  -- üéâ no goals
#align category_theory.topology_of_closure_operator_close CategoryTheory.topologyOfClosureOperator_close

end CategoryTheory
