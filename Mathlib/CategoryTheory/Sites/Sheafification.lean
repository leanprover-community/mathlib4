/-
Copyright (c) 2023 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.CategoryTheory.Adjunction.Opposites
import Mathlib.CategoryTheory.Adjunction.FullyFaithful
import Mathlib.CategoryTheory.Sites.Sheaf
import Mathlib.CategoryTheory.Limits.Preserves.Finite
/-!

# Sheafification

Given a site `(C, J)` we define a typeclass `HasSheafify J A` saying that the inclusion functor from
`A`-valued sheaves on `C` to presheaves admits a left exact left adjoint (sheafification).

Note: to access the `HasSheafify`¬†instance for suitable concrete categories, import the file
`Mathlib.CategoryTheory.Sites.LeftExact`.
-/

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory

open Limits

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] (J : GrothendieckTopology C)
variable (A : Type u‚ÇÇ) [Category.{v‚ÇÇ} A]

/--
A proposition saying that the inclusion functor from sheaves to presheaves admits a left adjoint.
-/
abbrev HasWeakSheafify := Nonempty (IsRightAdjoint (sheafToPresheaf J A))

/--
`HasSheafify` means that the inclusion functor from sheaves to presheaves admits a left exact
left adjiont (sheafification).

Given a finite limit preserving functor `F : (C·µí·µñ ‚•§ A) ‚•§ Sheaf J A` and an adjunction
`adj : F ‚ä£ sheafToPresheaf J A`, use `HasSheafify.mk'` to construct a `HasSheafify`¬†instance.
-/
class HasSheafify : Prop where
  isRightAdjoint : HasWeakSheafify J A
  isLeftExact : letI := isRightAdjoint.some
    Nonempty (PreservesFiniteLimits (leftAdjoint (sheafToPresheaf J A)))

instance [HasSheafify J A] : HasWeakSheafify J A := HasSheafify.isRightAdjoint

instance [IsRightAdjoint <| sheafToPresheaf J A] : HasWeakSheafify J A := ‚ü®inferInstance‚ü©

noncomputable section

instance [h : HasWeakSheafify J A] : IsRightAdjoint (sheafToPresheaf J A) := h.some

instance [HasSheafify J A] : PreservesFiniteLimits (leftAdjoint (sheafToPresheaf J A)) :=
  HasSheafify.isLeftExact.some

theorem HasSheafify.mk' {F : (C·µí·µñ ‚•§ A) ‚•§ Sheaf J A} (adj : F ‚ä£ sheafToPresheaf J A)
    [PreservesFiniteLimits F] : HasSheafify J A where
  isRightAdjoint := ‚ü®‚ü®F, adj‚ü©‚ü©
  isLeftExact :=
    let i : (h : IsRightAdjoint (sheafToPresheaf J A) := ‚ü®F, adj‚ü©) ‚Üí
      F ‚âÖ leftAdjoint (sheafToPresheaf J A) := fun _ ‚Ü¶
      adj.leftAdjointUniq (Adjunction.ofRightAdjoint (sheafToPresheaf J A))
    ‚ü®‚ü®fun _ ‚Ü¶ preservesLimitsOfShapeOfNatIso (i _)‚ü©‚ü©

/-- The sheafification functor, left adjoint to the inclusion. -/
def presheafToSheaf [HasWeakSheafify J A] : (C·µí·µñ ‚•§ A) ‚•§ Sheaf J A :=
  leftAdjoint (sheafToPresheaf J A)

instance [HasSheafify J A] : PreservesFiniteLimits (presheafToSheaf J A) :=
  HasSheafify.isLeftExact.some

/-- The sheafification-inclusion adjunction. -/
def sheafificationAdjunction [HasWeakSheafify J A] :
    presheafToSheaf J A ‚ä£ sheafToPresheaf J A := IsRightAdjoint.adj

instance [HasWeakSheafify J A] : IsLeftAdjoint <| presheafToSheaf J A where
  adj := sheafificationAdjunction J A

end

variable {D : Type*} [Category D] [HasWeakSheafify J D]

/-- The sheafification of a presheaf `P`. -/
noncomputable abbrev sheafify (P : C·µí·µñ ‚•§ D) : C·µí·µñ ‚•§ D :=
  presheafToSheaf J D |>.obj P |>.val

/-- The canonical map from `P` to its sheafification. -/
noncomputable abbrev toSheafify (P : C·µí·µñ ‚•§ D) : P ‚ü∂ sheafify J P :=
  sheafificationAdjunction J D |>.unit.app P

@[simp]
theorem sheafificationAdjunction_unit_app (P : C·µí·µñ ‚•§ D) :
    (sheafificationAdjunction J D).unit.app P = toSheafify J P := rfl

/-- The canonical map on sheafifications induced by a morphism. -/
noncomputable abbrev sheafifyMap {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) : sheafify J P ‚ü∂ sheafify J Q :=
  presheafToSheaf J D |>.map Œ∑ |>.val

@[simp]
theorem sheafifyMap_id (P : C·µí·µñ ‚•§ D) : sheafifyMap J (ùüô P) = ùüô (sheafify J P) := by
  simp [sheafifyMap, sheafify]

@[simp]
theorem sheafifyMap_comp {P Q R : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (Œ≥ : Q ‚ü∂ R) :
    sheafifyMap J (Œ∑ ‚â´ Œ≥) = sheafifyMap J Œ∑ ‚â´ sheafifyMap J Œ≥ := by
  simp [sheafifyMap, sheafify]

@[reassoc (attr := simp)]
theorem toSheafify_naturality {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) :
    Œ∑ ‚â´ toSheafify J _ = toSheafify J _ ‚â´ sheafifyMap J Œ∑ :=
  sheafificationAdjunction J D |>.unit.naturality Œ∑

variable (D)

/-- The sheafification of a presheaf `P`, as a functor. -/
noncomputable abbrev sheafification : (C·µí·µñ ‚•§ D) ‚•§ C·µí·µñ ‚•§ D :=
  presheafToSheaf J D ‚ãô sheafToPresheaf J D

theorem sheafification_obj (P : C·µí·µñ ‚•§ D) : (sheafification J D).obj P = sheafify J P :=
  rfl

theorem sheafification_map {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) :
    (sheafification J D).map Œ∑ = sheafifyMap J Œ∑ :=
  rfl

/-- The canonical map from `P` to its sheafification, as a natural transformation. -/
noncomputable abbrev toSheafification : ùü≠ _ ‚ü∂ sheafification J D :=
  sheafificationAdjunction J D |>.unit

theorem toSheafification_app (P : C·µí·µñ ‚•§ D) : (toSheafification J D).app P = toSheafify J P :=
  rfl

variable {D}

theorem isIso_toSheafify {P : C·µí·µñ ‚•§ D} (hP : Presheaf.IsSheaf J P) : IsIso (toSheafify J P) := by
  refine ‚ü®(sheafificationAdjunction J D |>.counit.app ‚ü®P, hP‚ü©).val, ?_, ?_‚ü©
  ¬∑ change _ = (ùüô (sheafToPresheaf J D ‚ãô ùü≠ (C·µí·µñ ‚•§ D)) : _).app ‚ü®P, hP‚ü©
    rw [‚Üê sheafificationAdjunction J D |>.right_triangle]
    rfl
  ¬∑ change (sheafToPresheaf _ _).map _ ‚â´ _ = _
    change _ ‚â´ (sheafificationAdjunction J D).unit.app ((sheafToPresheaf J D).obj ‚ü®P, hP‚ü©) = _
    erw [‚Üê inv_counit_map (sheafificationAdjunction J D) (X := ‚ü®P, hP‚ü©), comp_inv_eq_id]

/-- If `P` is a sheaf, then `P` is isomorphic to `sheafify J P`. -/
noncomputable def isoSheafify {P : C·µí·µñ ‚•§ D} (hP : Presheaf.IsSheaf J P) : P ‚âÖ sheafify J P :=
  letI := isIso_toSheafify J hP
  asIso (toSheafify J P)

@[simp]
theorem isoSheafify_hom {P : C·µí·µñ ‚•§ D} (hP : Presheaf.IsSheaf J P) :
    (isoSheafify J hP).hom = toSheafify J P :=
  rfl

/-- Given a sheaf `Q` and a morphism `P ‚ü∂ Q`, construct a morphism from `sheafify J P` to `Q`. -/
noncomputable def sheafifyLift {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (hQ : Presheaf.IsSheaf J Q) :
    sheafify J P ‚ü∂ Q :=
  (sheafificationAdjunction J D).homEquiv P ‚ü®Q, hQ‚ü© |>.symm Œ∑ |>.val

@[simp]
theorem sheafificationAdjunction_counit_app_val (P : Sheaf J D) :
    ((sheafificationAdjunction J D).counit.app P).val = sheafifyLift J (ùüô P.val) P.cond := by
  unfold sheafifyLift
  rw [Adjunction.homEquiv_counit]
  simp

@[reassoc (attr := simp)]
theorem toSheafify_sheafifyLift {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (hQ : Presheaf.IsSheaf J Q) :
    toSheafify J P ‚â´ sheafifyLift J Œ∑ hQ = Œ∑ := by
  rw [toSheafify, sheafifyLift, Adjunction.homEquiv_counit]
  change _ ‚â´ (sheafToPresheaf J D).map _ ‚â´ _ = _
  simp only [Adjunction.unit_naturality_assoc]
  change _ ‚â´ (sheafificationAdjunction J D).unit.app ((sheafToPresheaf J D).obj ‚ü®Q, hQ‚ü©) ‚â´ _ = _
  change _ ‚â´ _ ‚â´ (sheafToPresheaf J D).map _ = _
  rw [sheafificationAdjunction J D |>.right_triangle_components (Y := ‚ü®Q, hQ‚ü©)]
  simp

theorem sheafifyLift_unique {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (hQ : Presheaf.IsSheaf J Q)
    (Œ≥ : sheafify J P ‚ü∂ Q) : toSheafify J P ‚â´ Œ≥ = Œ∑ ‚Üí Œ≥ = sheafifyLift J Œ∑ hQ := by
  intro h
  rw [toSheafify] at h
  rw [sheafifyLift]
  let Œ≥' : (presheafToSheaf J D).obj P ‚ü∂ ‚ü®Q, hQ‚ü© := ‚ü®Œ≥‚ü©
  change Œ≥'.val = _
  rw [‚Üê Sheaf.Hom.ext_iff, ‚Üê Adjunction.homEquiv_apply_eq, Adjunction.homEquiv_unit]
  exact h

@[simp]
theorem isoSheafify_inv {P : C·µí·µñ ‚•§ D} (hP : Presheaf.IsSheaf J P) :
    (isoSheafify J hP).inv = sheafifyLift J (ùüô _) hP := by
  apply sheafifyLift_unique
  simp [Iso.comp_inv_eq]

theorem sheafify_hom_ext {P Q : C·µí·µñ ‚•§ D} (Œ∑ Œ≥ : sheafify J P ‚ü∂ Q) (hQ : Presheaf.IsSheaf J Q)
    (h : toSheafify J P ‚â´ Œ∑ = toSheafify J P ‚â´ Œ≥) : Œ∑ = Œ≥ := by
  rw [sheafifyLift_unique J _ hQ _ h, ‚Üê h]
  exact (sheafifyLift_unique J _ hQ _ h.symm).symm

@[reassoc (attr := simp)]
theorem sheafifyMap_sheafifyLift {P Q R : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (Œ≥ : Q ‚ü∂ R)
    (hR : Presheaf.IsSheaf J R) :
    sheafifyMap J Œ∑ ‚â´ sheafifyLift J Œ≥ hR = sheafifyLift J (Œ∑ ‚â´ Œ≥) hR := by
  apply sheafifyLift_unique
  rw [‚Üê Category.assoc, ‚Üê toSheafify_naturality, Category.assoc, toSheafify_sheafifyLift]

variable {J}

/-- A sheaf `P` is isomorphic to its own sheafification. -/
@[simps]
noncomputable def sheafificationIso (P : Sheaf J D) : P ‚âÖ (presheafToSheaf J D).obj P.val where
  hom := ‚ü®(isoSheafify J P.2).hom‚ü©
  inv := ‚ü®(isoSheafify J P.2).inv‚ü©
  hom_inv_id := by
    ext1
    apply (isoSheafify J P.2).hom_inv_id
  inv_hom_id := by
    ext1
    apply (isoSheafify J P.2).inv_hom_id
#align category_theory.sheafification_iso CategoryTheory.sheafificationIso

instance isIso_sheafificationAdjunction_counit (P : Sheaf J D) :
    IsIso ((sheafificationAdjunction J D).counit.app P) :=
  isIso_of_fully_faithful (sheafToPresheaf J D) _
#align category_theory.is_iso_sheafification_adjunction_counit CategoryTheory.isIso_sheafificationAdjunction_counit

instance sheafification_reflective : IsIso (sheafificationAdjunction J D).counit :=
  NatIso.isIso_of_isIso_app _
#align category_theory.sheafification_reflective CategoryTheory.sheafification_reflective

end CategoryTheory
