/-
Copyright (c) 2023 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.CategoryTheory.Adjunction.Unique
import Mathlib.CategoryTheory.Adjunction.Reflective
import Mathlib.CategoryTheory.Sites.Sheaf
import Mathlib.CategoryTheory.Limits.Preserves.Finite
/-!

# Sheafification

Given a site `(C, J)` we define a typeclass `HasSheafify J A` saying that the inclusion functor from
`A`-valued sheaves on `C` to presheaves admits a left exact left adjoint (sheafification).

Note: to access the `HasSheafify` instance for suitable concrete categories, import the file
`Mathlib/CategoryTheory/Sites/LeftExact.lean`.
-/

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

namespace CategoryTheory

open Limits

variable {C : Type uâ‚} [Category.{vâ‚} C] (J : GrothendieckTopology C)
variable (A : Type uâ‚‚) [Category.{vâ‚‚} A]

/--
A proposition saying that the inclusion functor from sheaves to presheaves admits a left adjoint.
-/
abbrev HasWeakSheafify : Prop := (sheafToPresheaf J A).IsRightAdjoint

/--
`HasSheafify` means that the inclusion functor from sheaves to presheaves admits a left exact
left adjoint (sheafification).

Given a functor, preserving finite limits, `F : (Cáµ’áµ– â¥¤ A) â¥¤ Sheaf J A` and an adjunction
`adj : F âŠ£ sheafToPresheaf J A`, use `HasSheafify.mk'` to construct a `HasSheafify` instance.
-/
class HasSheafify : Prop where
  isRightAdjoint : HasWeakSheafify J A
  isLeftExact : PreservesFiniteLimits ((sheafToPresheaf J A).leftAdjoint)

instance [HasSheafify J A] : HasWeakSheafify J A := HasSheafify.isRightAdjoint

noncomputable section

instance [HasSheafify J A] : PreservesFiniteLimits ((sheafToPresheaf J A).leftAdjoint) :=
  HasSheafify.isLeftExact

theorem HasSheafify.mk' {F : (Cáµ’áµ– â¥¤ A) â¥¤ Sheaf J A} (adj : F âŠ£ sheafToPresheaf J A)
    [PreservesFiniteLimits F] : HasSheafify J A where
  isRightAdjoint := âŸ¨F, âŸ¨adjâŸ©âŸ©
  isLeftExact := âŸ¨by
    have : (sheafToPresheaf J A).IsRightAdjoint := âŸ¨_, âŸ¨adjâŸ©âŸ©
    exact fun _ _ _ â†¦ preservesLimitsOfShape_of_natIso
      (adj.leftAdjointUniq (Adjunction.ofIsRightAdjoint (sheafToPresheaf J A)))âŸ©

/-- The sheafification functor, left adjoint to the inclusion. -/
def presheafToSheaf [HasWeakSheafify J A] : (Cáµ’áµ– â¥¤ A) â¥¤ Sheaf J A :=
  (sheafToPresheaf J A).leftAdjoint

instance [HasSheafify J A] : PreservesFiniteLimits (presheafToSheaf J A) :=
  HasSheafify.isLeftExact

/-- The sheafification-inclusion adjunction. -/
def sheafificationAdjunction [HasWeakSheafify J A] :
    presheafToSheaf J A âŠ£ sheafToPresheaf J A := Adjunction.ofIsRightAdjoint _

instance [HasWeakSheafify J A] : (presheafToSheaf J A).IsLeftAdjoint :=
  âŸ¨_, âŸ¨sheafificationAdjunction J AâŸ©âŸ©

instance [HasWeakSheafify J A] : Reflective (sheafToPresheaf J A) where
  L := presheafToSheaf J A
  adj := sheafificationAdjunction _ _

instance [HasSheafify J A] :  PreservesFiniteLimits (reflector (sheafToPresheaf J A)) :=
  inferInstanceAs (PreservesFiniteLimits (presheafToSheaf _ _))

end

variable {D : Type*} [Category D] [HasWeakSheafify J D]

/-- The sheafification of a presheaf `P`. -/
noncomputable abbrev sheafify (P : Cáµ’áµ– â¥¤ D) : Cáµ’áµ– â¥¤ D :=
  presheafToSheaf J D |>.obj P |>.val

/-- The canonical map from `P` to its sheafification. -/
noncomputable abbrev toSheafify (P : Cáµ’áµ– â¥¤ D) : P âŸ¶ sheafify J P :=
  sheafificationAdjunction J D |>.unit.app P

@[simp]
theorem sheafificationAdjunction_unit_app (P : Cáµ’áµ– â¥¤ D) :
    (sheafificationAdjunction J D).unit.app P = toSheafify J P := rfl

/-- The canonical map on sheafifications induced by a morphism. -/
noncomputable abbrev sheafifyMap {P Q : Cáµ’áµ– â¥¤ D} (Î· : P âŸ¶ Q) : sheafify J P âŸ¶ sheafify J Q :=
  presheafToSheaf J D |>.map Î· |>.val

@[simp]
theorem sheafifyMap_id (P : Cáµ’áµ– â¥¤ D) : sheafifyMap J (ðŸ™ P) = ðŸ™ (sheafify J P) := by
  simp [sheafifyMap, sheafify]

@[simp]
theorem sheafifyMap_comp {P Q R : Cáµ’áµ– â¥¤ D} (Î· : P âŸ¶ Q) (Î³ : Q âŸ¶ R) :
    sheafifyMap J (Î· â‰« Î³) = sheafifyMap J Î· â‰« sheafifyMap J Î³ := by
  simp [sheafifyMap, sheafify]

@[reassoc (attr := simp)]
theorem toSheafify_naturality {P Q : Cáµ’áµ– â¥¤ D} (Î· : P âŸ¶ Q) :
    Î· â‰« toSheafify J _ = toSheafify J _ â‰« sheafifyMap J Î· :=
  sheafificationAdjunction J D |>.unit.naturality Î·

variable (D)

/-- The sheafification of a presheaf `P`, as a functor. -/
noncomputable abbrev sheafification : (Cáµ’áµ– â¥¤ D) â¥¤ Cáµ’áµ– â¥¤ D :=
  presheafToSheaf J D â‹™ sheafToPresheaf J D

theorem sheafification_obj (P : Cáµ’áµ– â¥¤ D) : (sheafification J D).obj P = sheafify J P :=
  rfl

theorem sheafification_map {P Q : Cáµ’áµ– â¥¤ D} (Î· : P âŸ¶ Q) :
    (sheafification J D).map Î· = sheafifyMap J Î· :=
  rfl

/-- The canonical map from `P` to its sheafification, as a natural transformation. -/
noncomputable abbrev toSheafification : ðŸ­ _ âŸ¶ sheafification J D :=
  sheafificationAdjunction J D |>.unit

theorem toSheafification_app (P : Cáµ’áµ– â¥¤ D) : (toSheafification J D).app P = toSheafify J P :=
  rfl

variable {D}

theorem isIso_toSheafify {P : Cáµ’áµ– â¥¤ D} (hP : Presheaf.IsSheaf J P) : IsIso (toSheafify J P) := by
  refine âŸ¨(sheafificationAdjunction J D |>.counit.app âŸ¨P, hPâŸ©).val, ?_, ?_âŸ©
  Â· change _ = (ðŸ™ (sheafToPresheaf J D â‹™ ðŸ­ (Cáµ’áµ– â¥¤ D)) :).app âŸ¨P, hPâŸ©
    rw [â† sheafificationAdjunction J D |>.right_triangle]
    rfl
  Â· change (sheafToPresheaf _ _).map _ â‰« _ = _
    change _ â‰« (sheafificationAdjunction J D).unit.app ((sheafToPresheaf J D).obj âŸ¨P, hPâŸ©) = _
    rw [â† (sheafificationAdjunction J D).inv_counit_map (X := âŸ¨P, hPâŸ©)]
    simp

/-- If `P` is a sheaf, then `P` is isomorphic to `sheafify J P`. -/
noncomputable def isoSheafify {P : Cáµ’áµ– â¥¤ D} (hP : Presheaf.IsSheaf J P) : P â‰… sheafify J P :=
  letI := isIso_toSheafify J hP
  asIso (toSheafify J P)

@[simp]
theorem isoSheafify_hom {P : Cáµ’áµ– â¥¤ D} (hP : Presheaf.IsSheaf J P) :
    (isoSheafify J hP).hom = toSheafify J P :=
  rfl

/-- Given a sheaf `Q` and a morphism `P âŸ¶ Q`, construct a morphism from `sheafify J P` to `Q`. -/
noncomputable def sheafifyLift {P Q : Cáµ’áµ– â¥¤ D} (Î· : P âŸ¶ Q) (hQ : Presheaf.IsSheaf J Q) :
    sheafify J P âŸ¶ Q :=
  (sheafificationAdjunction J D).homEquiv P âŸ¨Q, hQâŸ© |>.symm Î· |>.val

@[simp]
theorem sheafificationAdjunction_counit_app_val (P : Sheaf J D) :
    ((sheafificationAdjunction J D).counit.app P).val = sheafifyLift J (ðŸ™ P.val) P.cond := by
  unfold sheafifyLift
  rw [Adjunction.homEquiv_counit]
  simp

@[reassoc (attr := simp)]
theorem toSheafify_sheafifyLift {P Q : Cáµ’áµ– â¥¤ D} (Î· : P âŸ¶ Q) (hQ : Presheaf.IsSheaf J Q) :
    toSheafify J P â‰« sheafifyLift J Î· hQ = Î· := by
  rw [toSheafify, sheafifyLift, Adjunction.homEquiv_counit]
  change _ â‰« (sheafToPresheaf J D).map _ â‰« _ = _
  simp only [Adjunction.unit_naturality_assoc]
  change _ â‰« (sheafificationAdjunction J D).unit.app ((sheafToPresheaf J D).obj âŸ¨Q, hQâŸ©) â‰« _ = _
  change _ â‰« _ â‰« (sheafToPresheaf J D).map _ = _
  rw [sheafificationAdjunction J D |>.right_triangle_components (Y := âŸ¨Q, hQâŸ©)]
  simp

theorem sheafifyLift_unique {P Q : Cáµ’áµ– â¥¤ D} (Î· : P âŸ¶ Q) (hQ : Presheaf.IsSheaf J Q)
    (Î³ : sheafify J P âŸ¶ Q) : toSheafify J P â‰« Î³ = Î· â†’ Î³ = sheafifyLift J Î· hQ := by
  intro h
  rw [toSheafify] at h
  rw [sheafifyLift]
  let Î³' : (presheafToSheaf J D).obj P âŸ¶ âŸ¨Q, hQâŸ© := âŸ¨Î³âŸ©
  change Î³'.val = _
  rw [â† Sheaf.Hom.ext_iff, â† Adjunction.homEquiv_apply_eq, Adjunction.homEquiv_unit]
  exact h

@[simp]
theorem isoSheafify_inv {P : Cáµ’áµ– â¥¤ D} (hP : Presheaf.IsSheaf J P) :
    (isoSheafify J hP).inv = sheafifyLift J (ðŸ™ _) hP := by
  apply sheafifyLift_unique
  simp [Iso.comp_inv_eq]

theorem sheafify_hom_ext {P Q : Cáµ’áµ– â¥¤ D} (Î· Î³ : sheafify J P âŸ¶ Q) (hQ : Presheaf.IsSheaf J Q)
    (h : toSheafify J P â‰« Î· = toSheafify J P â‰« Î³) : Î· = Î³ := by
  rw [sheafifyLift_unique J _ hQ _ h, â† h]
  exact (sheafifyLift_unique J _ hQ _ h.symm).symm

@[reassoc (attr := simp)]
theorem sheafifyMap_sheafifyLift {P Q R : Cáµ’áµ– â¥¤ D} (Î· : P âŸ¶ Q) (Î³ : Q âŸ¶ R)
    (hR : Presheaf.IsSheaf J R) :
    sheafifyMap J Î· â‰« sheafifyLift J Î³ hR = sheafifyLift J (Î· â‰« Î³) hR := by
  apply sheafifyLift_unique
  rw [â† Category.assoc, â† toSheafify_naturality, Category.assoc, toSheafify_sheafifyLift]

variable {J}

/-- A sheaf `P` is isomorphic to its own sheafification. -/
@[simps]
noncomputable def sheafificationIso (P : Sheaf J D) : P â‰… (presheafToSheaf J D).obj P.val where
  hom := âŸ¨(isoSheafify J P.2).homâŸ©
  inv := âŸ¨(isoSheafify J P.2).invâŸ©
  hom_inv_id := by
    ext1
    apply (isoSheafify J P.2).hom_inv_id
  inv_hom_id := by
    ext1
    apply (isoSheafify J P.2).inv_hom_id

instance isIso_sheafificationAdjunction_counit (P : Sheaf J D) :
    IsIso ((sheafificationAdjunction J D).counit.app P) :=
  isIso_of_fully_faithful (sheafToPresheaf J D) _

instance sheafification_reflective : IsIso (sheafificationAdjunction J D).counit :=
  NatIso.isIso_of_isIso_app _

variable (J D)

/-- The natural isomorphism `ðŸ­ (Sheaf J D) â‰… sheafToPresheaf J D â‹™ presheafToSheaf J D`. -/
@[simps!]
noncomputable def sheafificationNatIso :
    ðŸ­ (Sheaf J D) â‰… sheafToPresheaf J D â‹™ presheafToSheaf J D :=
  NatIso.ofComponents (fun P => sheafificationIso P) (by cat_disch)

end CategoryTheory
