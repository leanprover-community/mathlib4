/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Robin Carlier
-/
import Mathlib.CategoryTheory.Equivalence

/-!
# Binary disjoint unions of categories

We define the category instance on `C ‚äï D` when `C` and `D` are categories.

We define:
* `inl_`      : the functor `C ‚•§ C ‚äï D`
* `inr_`      : the functor `D ‚•§ C ‚äï D`
* `swap`      : the functor `C ‚äï D ‚•§ D ‚äï C`
    (and the fact this is an equivalence)

The sum of two functors `F : A ‚•§ C` and `G : B ‚•§ C` is a functor `A ‚äï B ‚•§ C`, written `F.sum' G`.
This construction should be preffered when defining functors out of a sum.

We provides natural isomorphisms `inlCompSum' : inl_ ‚ãô F.sum' G ‚âÖ F` and
`inrCompSum' : inl_ ‚ãô F.sum' G ‚âÖ G`.

Furthermore, we provide `Functor.sumIsoExt`, which
constructs a natural isomorphism of functors out of a sum out of natural isomorphism with
their precomposition with the inclusion. This construction sholud be preffered when trying
to construct isomorphisms between functors out of a sum.

We further define sums of functors and natural transformations, written `F.sum G` and `Œ±.sum Œ≤`.
-/


namespace CategoryTheory

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ v‚ÇÑ u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ

-- morphism levels before object levels. See note [category_theory universes].
open Sum

section

variable (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] (D : Type u‚ÇÇ) [Category.{v‚ÇÇ} D]

/- Porting note: `aesop_cat` not firing on `assoc` where autotac in Lean 3 did -/

/-- `sum C D` gives the direct sum of two categories.
-/
instance sum : Category.{max v‚ÇÅ v‚ÇÇ} (C ‚äï D) where
  Hom X Y :=
    match X, Y with
    | inl X, inl Y => ULift.{max v‚ÇÅ v‚ÇÇ} (X ‚ü∂ Y)
    | inl _, inr _ => PEmpty
    | inr _, inl _ => PEmpty
    | inr X, inr Y => ULift.{max v‚ÇÅ v‚ÇÇ} (X ‚ü∂ Y)
  id X :=
    match X with
    | inl X => ULift.up (ùüô X)
    | inr X => ULift.up (ùüô X)
  comp {X Y Z} f g :=
    match X, Y, Z, f, g with
    | inl _, inl _, inl _, f, g => ULift.up <|f.down ‚â´ g.down
    | inr _, inr _, inr _, f, g => ULift.up <| f.down ‚â´ g.down
  assoc {W X Y Z} f g h :=
    match X, Y, Z, W with
    | inl _, inl _, inl _, inl _ => by simp
    | inr _, inr _, inr _, inr _ => by simp

@[aesop norm -10 destruct (rule_sets := [CategoryTheory])]
theorem hom_inl_inr_false {X : C} {Y : D} (f : Sum.inl X ‚ü∂ Sum.inr Y) : False := by
  cases f

@[aesop norm -10 destruct (rule_sets := [CategoryTheory])]
theorem hom_inr_inl_false {X : C} {Y : D} (f : Sum.inr X ‚ü∂ Sum.inl Y) : False := by
  cases f

@[reassoc (attr := simp)]
theorem sum_comp_inl_down {P Q R : C} (f : (inl P : C ‚äï D) ‚ü∂ inl Q) (g : (inl Q : C ‚äï D) ‚ü∂ inl R) :
    (f ‚â´ g).down = f.down ‚â´ g.down :=
  rfl

@[reassoc (attr := simp)]
theorem sum_comp_inr_down {P Q R : D} (f : (inr P : C ‚äï D) ‚ü∂ inr Q) (g : (inr Q : C ‚äï D) ‚ü∂ inr R) :
    (f ‚â´ g).down = f.down ‚â´ g.down :=
  rfl

@[reassoc (attr := simp)]
theorem sum_comp_inl {P Q R : C} (f : (inl P : C ‚äï D) ‚ü∂ inl Q) (g : (inl Q : C ‚äï D) ‚ü∂ inl R) :
    (f ‚â´ g) = (ULift.up (f.down ‚â´ g.down) : inl P ‚ü∂ inl R) :=
  rfl

@[reassoc (attr := simp)]
theorem sum_comp_inr {P Q R : D} (f : (inr P : C ‚äï D) ‚ü∂ inr Q) (g : (inr Q : C ‚äï D) ‚ü∂ inr R) :
    f ‚â´ g = (ULift.up (f.down ‚â´ g.down) : inr P ‚ü∂ inr R) :=
  rfl

@[simp]
lemma id_down_left {P : C} : (ùüô (inl P) : (_ : C ‚äï D) ‚ü∂ _ ).down = ùüô P := rfl

@[simp]
lemma id_down_right {P : D} : (ùüô (inr P) : (_ : C ‚äï D) ‚ü∂ _ ).down = ùüô P := rfl

end

namespace Sum

variable (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] (D : Type u‚ÇÇ) [Category.{v‚ÇÇ} D]

-- Unfortunate naming here, suggestions welcome.
/-- `inl_` is the functor `X ‚Ü¶ inl X`. -/
@[simps! obj]
def inl_ : C ‚•§ C ‚äï D where
  obj X := inl X
  map {_ _} f := ULift.up f

@[simp]
lemma inl_map_down {c c' : C} (f : c ‚ü∂ c') : ((inl_ C D).map f).down = f := rfl

@[simp]
lemma inl_map_down' {c c' : C} (f : (inl c : C ‚äï D) ‚ü∂ inl c') : (inl_ C D).map f.down = f := rfl

/-- `inr_` is the functor `X ‚Ü¶ inr X`. -/
@[simps! obj]
def inr_ : D ‚•§ C ‚äï D where
  obj X := inr X
  map {_ _} f := ULift.up f

@[simp]
lemma inr_map_down {d d' : D} (f : d ‚ü∂ d') : ((inr_ C D).map f).down = f := rfl

@[simp]
lemma inr_map_down' {d d' : D} (f : (inr d : C ‚äï D) ‚ü∂ inr d') : (inr_ C D).map f.down = f := rfl

end Sum

variable {A : Type u‚ÇÅ} [Category.{v‚ÇÅ} A] {B : Type u‚ÇÇ} [Category.{v‚ÇÇ} B] {C : Type u‚ÇÉ}
  [Category.{v‚ÇÉ} C] {D : Type u‚ÇÑ} [Category.{v‚ÇÑ} D]

namespace Functor

section Sum'

variable (F : A ‚•§ C) (G : B ‚•§ C)

/-- The sum of two functors that lands in a given category `C`. -/
def sum' : A ‚äï B ‚•§ C where
  obj X :=
    match X with
    | inl X => F.obj X
    | inr X => G.obj X
  map {X Y} f :=
    match X, Y, f with
    | inl _, inl _, f => F.map f.down
    | inr _, inr _, f => G.map f.down

/-- The sum `F.sum' G` precomposed with the left inclusion functor is isomorphic to `F` -/
@[simps!]
def inlCompSum' : Sum.inl_ A B ‚ãô F.sum' G ‚âÖ F :=
  NatIso.ofComponents fun _ => Iso.refl _

/-- The sum `F.sum' G` precomposed with the right inclusion functor is isomorphic to `G` -/
@[simps!]
def inrCompSum' : Sum.inr_ A B ‚ãô F.sum' G ‚âÖ G :=
  NatIso.ofComponents fun _ => Iso.refl _

@[simp]
theorem sum'_obj_inl (a : A) : (F.sum' G).obj (inl a) = (F.obj a) :=
  rfl

@[simp]
theorem sum'_obj_inr (b : B) : (F.sum' G).obj (inr b) = (G.obj b) :=
  rfl

@[simp]
theorem sum'_map_inl {a a' : A} (f : inl a ‚ü∂ inl a') :
    (F.sum' G).map f = (F.map f.down) :=
  rfl

@[simp]
theorem sum'_map_inr {b b' : B} (f : inr b ‚ü∂ inr b') :
    (F.sum' G).map f = (G.map f.down) :=
  rfl

end Sum'

/-- The sum of two functors. -/
def sum (F : A ‚•§ B) (G : C ‚•§ D) : A ‚äï C ‚•§ B ‚äï D := (F ‚ãô Sum.inl_ _ _).sum' (G ‚ãô Sum.inr_ _ _)

@[simp]
theorem sum_obj_inl (F : A ‚•§ B) (G : C ‚•§ D) (a : A) : (F.sum G).obj (inl a) = inl (F.obj a) :=
  rfl

@[simp]
theorem sum_obj_inr (F : A ‚•§ B) (G : C ‚•§ D) (c : C) : (F.sum G).obj (inr c) = inr (G.obj c) :=
  rfl

@[simp]
theorem sum_map_inl (F : A ‚•§ B) (G : C ‚•§ D) {a a' : A} (f : inl a ‚ü∂ inl a') :
    (F.sum G).map f = (Sum.inl_ _ _).map (F.map f.down) :=
  rfl

@[simp]
theorem sum_map_inr (F : A ‚•§ B) (G : C ‚•§ D) {c c' : C} (f : inr c ‚ü∂ inr c') :
    (F.sum G).map f = (Sum.inr_ _ _).map (G.map f.down) :=
  rfl

section

variable {F G: A ‚äï B ‚•§ C}
  (e‚ÇÅ : Sum.inl_ _ _ ‚ãô F ‚âÖ Sum.inl_ _ _ ‚ãô G)
  (e‚ÇÇ : Sum.inr_ _ _ ‚ãô F ‚âÖ Sum.inr_ _ _ ‚ãô G)

/-- A functor out of a sum is uniquely characterized by its precompositions with `inl_` and `inr_`.
-/
def sumIsoExt : F ‚âÖ G :=
  NatIso.ofComponents (fun x ‚Ü¶
    match x with
    | inl x => e‚ÇÅ.app x
    | inr x => e‚ÇÇ.app x)
    (fun {x y} f ‚Ü¶
      match x, y, f with
      | inl x, inl y, f => by simpa using e‚ÇÅ.hom.naturality f.down
      | inr x, inr y, f => by simpa using e‚ÇÇ.hom.naturality f.down)

@[simp]
lemma sumIsoExt_hom_app_inl (a : A) : (sumIsoExt e‚ÇÅ e‚ÇÇ).hom.app (inl a) = e‚ÇÅ.hom.app a := rfl

@[simp]
lemma sumIsoExt_hom_app_inr (b : B) : (sumIsoExt e‚ÇÅ e‚ÇÇ).hom.app (inr b) = e‚ÇÇ.hom.app b := rfl

@[simp]
lemma sumIsoExt_inv_app_inl (a : A) : (sumIsoExt e‚ÇÅ e‚ÇÇ).inv.app (inl a) = e‚ÇÅ.inv.app a := rfl

@[simp]
lemma sumIsoExt_inv_app_inr (b : B) : (sumIsoExt e‚ÇÅ e‚ÇÇ).inv.app (inr b) = e‚ÇÇ.inv.app b := rfl

end

section

variable (F : A ‚äï B ‚•§ C) (a : A) (b : B)

/-- Any functor out of a sum is the sum of its precomposition with the inclusions. -/
def isoSum : F ‚âÖ (Sum.inl_ _ _ ‚ãô F).sum' (Sum.inr_ _ _ ‚ãô F) :=
    sumIsoExt (Iso.refl _) (Iso.refl _)

@[simp]
lemma isoSum_hom_app_inl : (isoSum F).hom.app (inl a) = ùüô (F.obj (inl a)) := rfl

@[simp]
lemma isoSum_hom_app_inr : (isoSum F).hom.app (inr b) = ùüô (F.obj (inr b)) := rfl

@[simp]
lemma isoSum_inv_app_inl : (isoSum F).inv.app (inl a) = ùüô (F.obj (inl a)) := rfl

@[simp]
lemma isoSum_inv_app_inr : (isoSum F).inv.app (inr b) = ùüô (F.obj (inr b)) := rfl

end

end Functor

namespace NatTrans

/-- The sum of two natural transformations. -/
def sum {F G : A ‚•§ B} {H I : C ‚•§ D} (Œ± : F ‚ü∂ G) (Œ≤ : H ‚ü∂ I) : F.sum H ‚ü∂ G.sum I where
  app X :=
    match X with
    | inl X => (Sum.inl_ _ _).map (Œ±.app X)
    | inr X => (Sum.inr_ _ _).map (Œ≤.app X)
  naturality X Y f :=
    match X, Y, f with
    | inl X, inl Y, f => by simp [‚Üê Functor.map_comp]
    | inr X, inr Y, f => by simp [‚Üê Functor.map_comp]

@[simp]
theorem sum_app_inl {F G : A ‚•§ B} {H I : C ‚•§ D} (Œ± : F ‚ü∂ G) (Œ≤ : H ‚ü∂ I) (a : A) :
    (sum Œ± Œ≤).app (inl a) = (Sum.inl_ _ _).map (Œ±.app a) :=
  rfl

@[simp]
theorem sum_app_inr {F G : A ‚•§ B} {H I : C ‚•§ D} (Œ± : F ‚ü∂ G) (Œ≤ : H ‚ü∂ I) (c : C) :
    (sum Œ± Œ≤).app (inr c) = (Sum.inr_ _ _).map (Œ≤.app c) :=
  rfl

end NatTrans

namespace Sum

variable (C : Type u‚ÇÅ) [Category.{v‚ÇÅ} C] (D : Type u‚ÇÇ) [Category.{v‚ÇÇ} D]

/-- The functor exchanging two direct summand categories. -/
def swap : C ‚äï D ‚•§ D ‚äï C := (inr_ _ _).sum' (inl_ _ _)

@[simp]
theorem swap_obj_inl (X : C) : (swap C D).obj (inl X) = inr X :=
  rfl

@[simp]
theorem swap_obj_inr (X : D) : (swap C D).obj (inr X) = inl X :=
  rfl

@[simp]
theorem swap_map_inl {X Y : C} {f : inl X ‚ü∂ inl Y} : (swap C D).map f = f :=
  rfl

@[simp]
theorem swap_map_inr {X Y : D} {f : inr X ‚ü∂ inr Y} : (swap C D).map f = f :=
  rfl

/-- Precomposing `swap` with the left inclusion gives the right inclusion. -/
@[simps!]
def swapCompInl : inl_ _ _ ‚ãô swap C D ‚âÖ inr_ _ _ := (Functor.inlCompSum' (inr_ _ _) (inl_ _ _)).symm

/-- Precomposing `swap` with the rightt inclusion gives the leftt inclusion. -/
@[simps!]
def swapCompInr : inr_ _ _ ‚ãô swap C D ‚âÖ inl_ _ _ := (Functor.inrCompSum' (inr_ _ _) (inl_ _ _)).symm

namespace Swap

/-- `swap` gives an equivalence between `C ‚äï D` and `D ‚äï C`. -/
@[simps functor inverse]
def equivalence : C ‚äï D ‚âå D ‚äï C where
  functor := swap C D
  inverse := swap D C
  unitIso := Functor.sumIsoExt (swapCompInr D C).symm (swapCompInl D C).symm
  counitIso := Functor.sumIsoExt (swapCompInr C D).symm (swapCompInl C D).symm

instance isEquivalence : (swap C D).IsEquivalence :=
  (by infer_instance : (equivalence C D).functor.IsEquivalence)

/-- The double swap on `C ‚äï D` is naturally isomorphic to the identity functor. -/
def symmetry : swap C D ‚ãô swap D C ‚âÖ ùü≠ (C ‚äï D) :=
  (equivalence C D).unitIso.symm

end Swap

end Sum

end CategoryTheory
