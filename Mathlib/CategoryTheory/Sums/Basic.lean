/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Robin Carlier
-/
import Mathlib.CategoryTheory.Equivalence

/-!
# Binary disjoint unions of categories

We define the category instance on `C âŠ• D` when `C` and `D` are categories.

We define:
* `inl_`      : the functor `C â¥¤ C âŠ• D`
* `inr_`      : the functor `D â¥¤ C âŠ• D`
* `swap`      : the functor `C âŠ• D â¥¤ D âŠ• C`
    (and the fact this is an equivalence)

We provide an induction principle `Sum.homInduction` to reason and work with morphisms in this
category.

The sum of two functors `F : A â¥¤ C` and `G : B â¥¤ C` is a functor `A âŠ• B â¥¤ C`, written `F.sum' G`.
This construction should be preferred when defining functors out of a sum.

We provide natural isomorphisms `inlCompSum' : inl_ â‹™ F.sum' G â‰… F` and
`inrCompSum' : inl_ â‹™ F.sum' G â‰… G`.

Furthermore, we provide `Functor.sumIsoExt`, which
constructs a natural isomorphism of functors out of a sum out of natural isomorphism with
their precomposition with the inclusion. This construction should be preferred when trying
to construct isomorphisms between functors out of a sum.

We further define sums of functors and natural transformations, written `F.sum G` and `Î±.sum Î²`.
-/


namespace CategoryTheory

universe vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ uâ‚ uâ‚‚ uâ‚ƒ uâ‚„

-- morphism levels before object levels. See note [category_theory universes].
open Sum Functor

section

variable (C : Type uâ‚) [Category.{vâ‚} C] (D : Type uâ‚‚) [Category.{vâ‚‚} D]

/-- `sum C D` gives the direct sum of two categories.
-/
instance sum : Category.{max vâ‚ vâ‚‚} (C âŠ• D) where
  Hom X Y :=
    match X, Y with
    | inl X, inl Y => ULift.{max vâ‚ vâ‚‚} (X âŸ¶ Y)
    | inl _, inr _ => PEmpty
    | inr _, inl _ => PEmpty
    | inr X, inr Y => ULift.{max vâ‚ vâ‚‚} (X âŸ¶ Y)
  id X :=
    match X with
    | inl X => ULift.up (ğŸ™ X)
    | inr X => ULift.up (ğŸ™ X)
  comp {X Y Z} f g :=
    match X, Y, Z, f, g with
    | inl _, inl _, inl _, f, g => ULift.up <|f.down â‰« g.down
    | inr _, inr _, inr _, f, g => ULift.up <| f.down â‰« g.down

@[aesop norm -10 destruct (rule_sets := [CategoryTheory])]
theorem hom_inl_inr_false {X : C} {Y : D} (f : Sum.inl X âŸ¶ Sum.inr Y) : False := by
  cases f

@[aesop norm -10 destruct (rule_sets := [CategoryTheory])]
theorem hom_inr_inl_false {X : C} {Y : D} (f : Sum.inr X âŸ¶ Sum.inl Y) : False := by
  cases f

end

namespace Sum

variable (C : Type uâ‚) [Category.{vâ‚} C] (D : Type uâ‚‚) [Category.{vâ‚‚} D]

-- Unfortunate naming here, suggestions welcome.
/-- `inl_` is the functor `X â†¦ inl X`. -/
@[simps! obj]
def inl_ : C â¥¤ C âŠ• D where
  obj X := inl X
  map f := ULift.up f

/-- `inr_` is the functor `X â†¦ inr X`. -/
@[simps! obj]
def inr_ : D â¥¤ C âŠ• D where
  obj X := inr X
  map f := ULift.up f

variable {C D}

/-- An induction principle for morphisms in a sum of category: a morphism is either of the form
`(inl_ _ _).map _` or of the form `(inr_ _ _).map _)`. -/
@[elab_as_elim, cases_eliminator, induction_eliminator]
def homInduction {P : {x y : C âŠ• D} â†’ (x âŸ¶ y) â†’ Sort*}
    (inl : âˆ€ x y : C, (f : x âŸ¶ y) â†’ P ((inl_ C D).map f))
    (inr : âˆ€ x y : D, (f : x âŸ¶ y) â†’ P ((inr_ C D).map f))
    {x y : C âŠ• D} (f : x âŸ¶ y) : P f :=
  match x, y, f with
  | .inl x, .inl y, f => inl x y f.down
  | .inr x, .inr y, f => inr x y f.down

@[simp]
lemma homInduction_left {P : {x y : C âŠ• D} â†’ (x âŸ¶ y) â†’ Sort*}
    (inl : âˆ€ x y : C, (f : x âŸ¶ y) â†’ P ((inl_ C D).map f))
    (inr : âˆ€ x y : D, (f : x âŸ¶ y) â†’ P ((inr_ C D).map f))
    {x y : C} (f : x âŸ¶ y) : homInduction inl inr ((inl_ C D).map f) = inl x y f :=
  rfl

@[simp]
lemma homInduction_right {P : {x y : C âŠ• D} â†’ (x âŸ¶ y) â†’ Sort*}
    (inl : âˆ€ x y : C, (f : x âŸ¶ y) â†’ P ((inl_ C D).map f))
    (inr : âˆ€ x y : D, (f : x âŸ¶ y) â†’ P ((inr_ C D).map f))
    {x y : D} (f : x âŸ¶ y) : homInduction inl inr ((inr_ C D).map f) = inr x y f :=
  rfl

end Sum

namespace Functor

variable {A : Type uâ‚} [Category.{vâ‚} A] {B : Type uâ‚‚} [Category.{vâ‚‚} B] {C : Type uâ‚ƒ}
  [Category.{vâ‚ƒ} C] {D : Type uâ‚„} [Category.{vâ‚„} D]

section Sum'

variable (F : A â¥¤ C) (G : B â¥¤ C)

/-- The sum of two functors that land in a given category `C`. -/
def sum' : A âŠ• B â¥¤ C where
  obj
  | inl X => F.obj X
  | inr X => G.obj X
  map {X Y} f := Sum.homInduction (inl := fun _ _ f â†¦ F.map f) (inr := fun _ _ g â†¦ G.map g) f
  map_comp {x y z} f g := by
    cases f <;> cases g <;> simp [â† Functor.map_comp]
  map_id x := by
    cases x <;> (simp only [â† map_id]; rfl)

/-- The sum `F.sum' G` precomposed with the left inclusion functor is isomorphic to `F` -/
@[simps!]
def inlCompSum' : Sum.inl_ A B â‹™ F.sum' G â‰… F :=
  NatIso.ofComponents fun _ => Iso.refl _

/-- The sum `F.sum' G` precomposed with the right inclusion functor is isomorphic to `G` -/
@[simps!]
def inrCompSum' : Sum.inr_ A B â‹™ F.sum' G â‰… G :=
  NatIso.ofComponents fun _ => Iso.refl _

@[simp]
theorem sum'_obj_inl (a : A) : (F.sum' G).obj (inl a) = (F.obj a) :=
  rfl

@[simp]
theorem sum'_obj_inr (b : B) : (F.sum' G).obj (inr b) = (G.obj b) :=
  rfl

@[simp]
theorem sum'_map_inl {a a' : A} (f : a âŸ¶ a') :
    (F.sum' G).map ((Sum.inl_ _ _).map f) = F.map f :=
  rfl

@[simp]
theorem sum'_map_inr {b b' : B} (f : b âŸ¶ b') :
    (F.sum' G).map ((Sum.inr_ _ _).map f) = G.map f :=
  rfl

end Sum'

/-- The sum of two functors. -/
def sum (F : A â¥¤ B) (G : C â¥¤ D) : A âŠ• C â¥¤ B âŠ• D := (F â‹™ Sum.inl_ _ _).sum' (G â‹™ Sum.inr_ _ _)

@[simp]
theorem sum_obj_inl (F : A â¥¤ B) (G : C â¥¤ D) (a : A) : (F.sum G).obj (inl a) = inl (F.obj a) :=
  rfl

@[simp]
theorem sum_obj_inr (F : A â¥¤ B) (G : C â¥¤ D) (c : C) : (F.sum G).obj (inr c) = inr (G.obj c) :=
  rfl

@[simp]
theorem sum_map_inl (F : A â¥¤ B) (G : C â¥¤ D) {a a' : A} (f : a âŸ¶ a') :
    (F.sum G).map ((Sum.inl_ _ _).map f) = (Sum.inl_ _ _).map (F.map f) := by
  simp [sum]

@[simp]
theorem sum_map_inr (F : A â¥¤ B) (G : C â¥¤ D) {c c' : C} (f : c âŸ¶ c') :
    (F.sum G).map ((Sum.inr_ _ _).map f) = (Sum.inr_ _ _).map (G.map f) := by
  simp [sum]

section

variable {F G : A âŠ• B â¥¤ C}
  (eâ‚ : Sum.inl_ A B â‹™ F â‰… Sum.inl_ A B â‹™ G)
  (eâ‚‚ : Sum.inr_ A B â‹™ F â‰… Sum.inr_ A B â‹™ G)

/-- A functor out of a sum is uniquely characterized by its precompositions with `inl_` and `inr_`.
-/
def sumIsoExt : F â‰… G :=
  NatIso.ofComponents (fun x â†¦
    match x with
    | inl x => eâ‚.app x
    | inr x => eâ‚‚.app x)
    (fun {x y} f â†¦ by
      cases f
      Â· simpa using eâ‚.hom.naturality _
      Â· simpa using eâ‚‚.hom.naturality _)

@[simp]
lemma sumIsoExt_hom_app_inl (a : A) : (sumIsoExt eâ‚ eâ‚‚).hom.app (inl a) = eâ‚.hom.app a := rfl

@[simp]
lemma sumIsoExt_hom_app_inr (b : B) : (sumIsoExt eâ‚ eâ‚‚).hom.app (inr b) = eâ‚‚.hom.app b := rfl

@[simp]
lemma sumIsoExt_inv_app_inl (a : A) : (sumIsoExt eâ‚ eâ‚‚).inv.app (inl a) = eâ‚.inv.app a := rfl

@[simp]
lemma sumIsoExt_inv_app_inr (b : B) : (sumIsoExt eâ‚ eâ‚‚).inv.app (inr b) = eâ‚‚.inv.app b := rfl

end

section

variable (F : A âŠ• B â¥¤ C)

/-- Any functor out of a sum is the sum of its precomposition with the inclusions. -/
def isoSum : F â‰… (Sum.inl_ A B â‹™ F).sum' (Sum.inr_ A B â‹™ F) :=
  sumIsoExt (inlCompSum' _ _).symm (inrCompSum' _ _).symm

variable (a : A) (b : B)

@[simp]
lemma isoSum_hom_app_inl : (isoSum F).hom.app (inl a) = ğŸ™ (F.obj (inl a)) := rfl

@[simp]
lemma isoSum_hom_app_inr : (isoSum F).hom.app (inr b) = ğŸ™ (F.obj (inr b)) := rfl

@[simp]
lemma isoSum_inv_app_inl : (isoSum F).inv.app (inl a) = ğŸ™ (F.obj (inl a)) := rfl

@[simp]
lemma isoSum_inv_app_inr : (isoSum F).inv.app (inr b) = ğŸ™ (F.obj (inr b)) := rfl

end

end Functor

namespace NatTrans

variable {A : Type uâ‚} [Category.{vâ‚} A] {B : Type uâ‚‚} [Category.{vâ‚‚} B] {C : Type uâ‚ƒ}
  [Category.{vâ‚ƒ} C] {D : Type uâ‚„} [Category.{vâ‚„} D]

/-- The sum of two natural transformations, where all functors have the same target category. -/
def sum' {F G : A â¥¤ C} {H I : B â¥¤ C} (Î± : F âŸ¶ G) (Î² : H âŸ¶ I) : F.sum' H âŸ¶ G.sum' I where
  app X :=
    match X with
    | inl X => Î±.app X
    | inr X => Î².app X
  naturality X Y f := by
    cases f <;> simp

@[simp]
theorem sum'_app_inl {F G : A â¥¤ C} {H I : B â¥¤ C} (Î± : F âŸ¶ G) (Î² : H âŸ¶ I) (a : A) :
    (sum' Î± Î²).app (inl a) = Î±.app a :=
  rfl

@[simp]
theorem sum'_app_inr {F G : A â¥¤ C} {H I : B â¥¤ C} (Î± : F âŸ¶ G) (Î² : H âŸ¶ I) (b : B) :
    (sum' Î± Î²).app (inr b) = Î².app b :=
  rfl

/-- The sum of two natural transformations. -/
def sum {F G : A â¥¤ B} {H I : C â¥¤ D} (Î± : F âŸ¶ G) (Î² : H âŸ¶ I) : F.sum H âŸ¶ G.sum I where
  app X :=
    match X with
    | inl X => (Sum.inl_ B D).map (Î±.app X)
    | inr X => (Sum.inr_ B D).map (Î².app X)
  naturality X Y f := by
    cases f <;> simp [â† Functor.map_comp]

@[simp]
theorem sum_app_inl {F G : A â¥¤ B} {H I : C â¥¤ D} (Î± : F âŸ¶ G) (Î² : H âŸ¶ I) (a : A) :
    (sum Î± Î²).app (inl a) = (Sum.inl_ _ _).map (Î±.app a) :=
  rfl

@[simp]
theorem sum_app_inr {F G : A â¥¤ B} {H I : C â¥¤ D} (Î± : F âŸ¶ G) (Î² : H âŸ¶ I) (c : C) :
    (sum Î± Î²).app (inr c) = (Sum.inr_ _ _).map (Î².app c) :=
  rfl

end NatTrans

namespace Sum

variable (C : Type uâ‚) [Category.{vâ‚} C] (D : Type uâ‚‚) [Category.{vâ‚‚} D]

/-- The functor exchanging two direct summand categories. -/
def swap : C âŠ• D â¥¤ D âŠ• C := (inr_ D C).sum' (inl_ D C)

@[simp]
theorem swap_obj_inl (X : C) : (swap C D).obj (inl X) = inr X :=
  rfl

@[simp]
theorem swap_obj_inr (X : D) : (swap C D).obj (inr X) = inl X :=
  rfl

@[simp]
theorem swap_map_inl {X Y : C} {f : inl X âŸ¶ inl Y} : (swap C D).map f = f :=
  rfl

@[simp]
theorem swap_map_inr {X Y : D} {f : inr X âŸ¶ inr Y} : (swap C D).map f = f :=
  rfl

/-- Precomposing `swap` with the left inclusion gives the right inclusion. -/
@[simps! hom_app inv_app]
def swapCompInl : inl_ C D â‹™ swap C D â‰… inr_ D C :=
  Functor.inlCompSum' (inr_ _ _) (inl_ _ _)

/-- Precomposing `swap` with the right inclusion gives the left inclusion. -/
@[simps! hom_app inv_app]
def swapCompInr : inr_ C D â‹™ swap C D â‰… inl_ D C :=
  Functor.inrCompSum' (inr_ _ _) (inl_ _ _)

namespace Swap

/-- `swap` gives an equivalence between `C âŠ• D` and `D âŠ• C`. -/
@[simps functor inverse]
def equivalence : C âŠ• D â‰Œ D âŠ• C where
  functor := swap C D
  inverse := swap D C
  unitIso := Functor.sumIsoExt
    (calc inl_ C D â‹™ ğŸ­ (C âŠ• D)
        â‰… inl_ C D := rightUnitor _
      _ â‰… inr_ D C â‹™ swap D C := (swapCompInr D C).symm
      _ â‰… (inl_ C D â‹™ swap C D) â‹™ swap D C := isoWhiskerRight (swapCompInl C D).symm _
      _ â‰… inl_ C D â‹™ swap C D â‹™ swap D C := associator _ _ _)
    (calc inr_ C D â‹™ ğŸ­ (C âŠ• D)
        â‰… inr_ C D := rightUnitor _
      _ â‰… inl_ D C â‹™ swap D C := (swapCompInl D C).symm
      _ â‰… (inr_ C D â‹™ swap C D) â‹™ swap D C := isoWhiskerRight (swapCompInr C D).symm _
      _ â‰… inr_ C D â‹™ swap C D â‹™ swap D C := associator _ _ _)
  counitIso := Functor.sumIsoExt
    (calc inl_ D C â‹™ swap D C â‹™ swap C D
        â‰… (inl_ D C â‹™ swap D C) â‹™ swap C D := (associator _ _ _).symm
      _ â‰… inr_ C D â‹™ swap C D := isoWhiskerRight (swapCompInl D C) _
      _ â‰… inl_ D C := swapCompInr C D
      _ â‰… inl_ D C â‹™ ğŸ­ (D âŠ• C) := (rightUnitor _).symm)
    (calc inr_ D C â‹™ swap D C â‹™ swap C D
        â‰… (inr_ D C â‹™ swap D C) â‹™ swap C D := (associator _ _ _).symm
      _ â‰… inl_ C D â‹™ swap C D := isoWhiskerRight (swapCompInr D C) _
      _ â‰… inr_ D C := swapCompInl C D
      _ â‰… inr_ D C â‹™ ğŸ­ (D âŠ• C) := (rightUnitor _).symm)

instance isEquivalence : (swap C D).IsEquivalence :=
  (by infer_instance : (equivalence C D).functor.IsEquivalence)

/-- The double swap on `C âŠ• D` is naturally isomorphic to the identity functor. -/
def symmetry : swap C D â‹™ swap D C â‰… ğŸ­ (C âŠ• D) :=
  (equivalence C D).unitIso.symm

end Swap

end Sum

end CategoryTheory
