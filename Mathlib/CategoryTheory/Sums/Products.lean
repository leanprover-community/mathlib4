/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.Sums.Associator
import Mathlib.CategoryTheory.Products.Associator

/-!
# Functors out of sums of categories.

This file records the universal property of sums of categories as an equivalence of
categories `Sum.functorEquiv : A âŠ• A' â¥¤ B â‰Œ (A â¥¤ B) Ã— (A' â¥¤ B)`, and characterizes its
precompositions with the left and right inclusion as corresponding to the projections on
the product side.

-/

namespace CategoryTheory

open Functor

universe v u

variable (A : Type*) [Category A] (A' : Type*) [Category A']
  (B : Type u) [Category.{v} B]

namespace Sum

/-- The equivalence between functors from a sum and the product of the functor categories. -/
@[simps]
def functorEquiv : A âŠ• A' â¥¤ B â‰Œ (A â¥¤ B) Ã— (A' â¥¤ B) where
  functor :=
    { obj F := âŸ¨inl_ A A' â‹™ F, inr_ A A' â‹™ FâŸ©
      map Î· := âŸ¨whiskerLeft (inl_ A A') Î·, whiskerLeft (inr_ A A') Î·âŸ© }
  inverse :=
    { obj F := Functor.sum' F.1 F.2
      map Î· := NatTrans.sum' Î·.1 Î·.2 }
  unitIso := NatIso.ofComponents <| fun F â†¦ F.isoSum
  counitIso := NatIso.ofComponents <| fun F â†¦
    (Functor.inlCompSum' _ _).prod (Functor.inrCompSum' _ _) â‰ªâ‰« prod.etaIso F

variable {A A' B}

@[simp]
lemma functorEquiv_unit_app_app_inl (X : A âŠ• A' â¥¤ B) (a : A) :
    ((functorEquiv A A' B).unit.app X).app (.inl a) = ðŸ™ (X.obj (.inl a)) :=
  rfl

@[simp]
lemma functorEquiv_unit_app_app_inr (X : A âŠ• A' â¥¤ B) (a' : A') :
    ((functorEquiv A A' B).unit.app X).app (.inr a') = ðŸ™ (X.obj (.inr a')) :=
  rfl

@[simp]
lemma functorEquiv_unitIso_inv_app_app_inl (X : A âŠ• A' â¥¤ B) (a : A) :
    ((functorEquiv A A' B).unitIso.inv.app X).app (.inl a) = ðŸ™ (X.obj (.inl a)) :=
  rfl

@[simp]
lemma functorEquiv_unitIso_inv_app_app_inr (X : A âŠ• A' â¥¤ B) (a' : A') :
    ((functorEquiv A A' B).unitIso.inv.app X).app (.inr a') = ðŸ™ (X.obj (.inr a')) :=
  rfl

/-- Composing the forward direction of `functorEquiv` with the first projection is the same as
precomposition with `inl_ A A'`. -/
@[simps!]
def functorEquivFunctorCompFstIso :
    (functorEquiv A A' B).functor â‹™ Prod.fst (A â¥¤ B) (A' â¥¤ B) â‰…
    (whiskeringLeft A (A âŠ• A') B).obj (inl_ A A') :=
  NatIso.ofComponents (fun _ â†¦ Iso.refl _)

/-- Composing the forward direction of `functorEquiv` with the second projection is the same as
precomposition with `inr_ A A'`. -/
@[simps!]
def functorEquivFunctorCompSndIso :
    (functorEquiv A A' B).functor â‹™ Prod.snd (A â¥¤ B) (A' â¥¤ B) â‰…
    (whiskeringLeft A' (A âŠ• A') B).obj (inr_ A A') :=
  NatIso.ofComponents (fun _ â†¦ Iso.refl _)

/-- Composing the backward direction of `functorEquiv` with precomposition with `inl_ A A'`.
is naturally isomorphic to the first projection. -/
@[simps!]
def functorEquivInverseCompWhiskeringLeftInlIso :
    (functorEquiv A A' B).inverse â‹™ (whiskeringLeft A (A âŠ• A') B).obj (inl_ A A') â‰…
    Prod.fst (A â¥¤ B) (A' â¥¤ B) :=
  NatIso.ofComponents (fun _ â†¦ Functor.inlCompSum' _ _)

/-- Composing the backward direction of `functorEquiv` with the second projection is the same as
precomposition with `inr_ A A'`. -/
@[simps!]
def functorEquivInverseCompWhiskeringLeftInrIso :
    (functorEquiv A A' B).inverse â‹™ (whiskeringLeft A' (A âŠ• A') B).obj (inr_ A A') â‰…
    Prod.snd (A â¥¤ B) (A' â¥¤ B) :=
  NatIso.ofComponents (fun _ â†¦ Functor.inrCompSum' _ _)

/-- A consequence of `functorEquiv`: we can construct a natural transformation of functors
`A âŠ• A' â¥¤ B` from the data of natural transformations of their whiskering with `inl_` and `inr_`. -/
@[simps!]
def natTransOfWhiskerLeftInlInr {F G : A âŠ• A' â¥¤ B}
    (Î·â‚ : Sum.inl_ A A' â‹™ F âŸ¶ Sum.inl_ A A' â‹™ G) (Î·â‚‚ : Sum.inr_ A A' â‹™ F âŸ¶ Sum.inr_ A A' â‹™ G) :
    F âŸ¶ G :=
  (Sum.functorEquiv A A' B).unit.app F â‰«
    (Sum.functorEquiv A A' B).inverse.map ((Î·â‚, Î·â‚‚) :) â‰«
      (Sum.functorEquiv A A' B).unitInv.app G

@[simp]
lemma natTransOfWhiskerLeftInlInr_id {F : A âŠ• A' â¥¤ B} :
    natTransOfWhiskerLeftInlInr (ðŸ™ (Sum.inl_ A A' â‹™ F)) (ðŸ™ (Sum.inr_ A A' â‹™ F)) = ðŸ™ F := by
  cat_disch

@[simp]
lemma natTransOfWhiskerLeftInlInr_comp {F G H : A âŠ• A' â¥¤ B}
    (Î·â‚ : Sum.inl_ A A' â‹™ F âŸ¶ Sum.inl_ A A' â‹™ G) (Î·â‚‚ : Sum.inr_ A A' â‹™ F âŸ¶ Sum.inr_ A A' â‹™ G)
    (Î½â‚ : Sum.inl_ A A' â‹™ G âŸ¶ Sum.inl_ A A' â‹™ H) (Î½â‚‚ : Sum.inr_ A A' â‹™ G âŸ¶ Sum.inr_ A A' â‹™ H) :
    natTransOfWhiskerLeftInlInr (Î·â‚ â‰« Î½â‚) (Î·â‚‚ â‰« Î½â‚‚) = natTransOfWhiskerLeftInlInr Î·â‚ Î·â‚‚ â‰«
      natTransOfWhiskerLeftInlInr Î½â‚ Î½â‚‚ := by
  cat_disch

/-- A consequence of `functorEquiv`: we can construct a natural isomorphism of functors
`A âŠ• A' â¥¤ B` from the data of natural isomorphisms of their whiskering with `inl_` and `inr_`. -/
@[simps]
def natIsoOfWhiskerLeftInlInr {F G : A âŠ• A' â¥¤ B}
    (Î·â‚ : Sum.inl_ A A' â‹™ F â‰… Sum.inl_ A A' â‹™ G) (Î·â‚‚ : Sum.inr_ A A' â‹™ F â‰… Sum.inr_ A A' â‹™ G) :
    F â‰… G where
  hom := natTransOfWhiskerLeftInlInr Î·â‚.hom Î·â‚‚.hom
  inv := natTransOfWhiskerLeftInlInr Î·â‚.inv Î·â‚‚.inv

lemma natIsoOfWhiskerLeftInlInr_eq {F G : A âŠ• A' â¥¤ B}
    (Î·â‚ : Sum.inl_ A A' â‹™ F â‰… Sum.inl_ A A' â‹™ G) (Î·â‚‚ : Sum.inr_ A A' â‹™ F â‰… Sum.inr_ A A' â‹™ G) :
    natIsoOfWhiskerLeftInlInr Î·â‚ Î·â‚‚ =
    (Sum.functorEquiv A A' B).unitIso.app _ â‰ªâ‰«
      (Sum.functorEquiv A A' B).inverse.mapIso (Iso.prod Î·â‚ Î·â‚‚) â‰ªâ‰«
      (Sum.functorEquiv A A' B).unitIso.symm.app _ := by
  cat_disch

namespace Swap

/-- `functorEquiv A A' B` transforms `Swap.equivalence` into `Prod.braiding`. -/
@[simps! hom_app_fst hom_app_snd inv_app_fst inv_app_snd]
def equivalenceFunctorEquivFunctorIso :
    ((equivalence A A').congrLeft.trans <| functorEquiv A' A B).functor â‰…
      ((functorEquiv A A' B).trans <| Prod.braiding (A â¥¤ B) (A' â¥¤ B)).functor :=
  NatIso.ofComponents (fun E â†¦
    Iso.prod
      ((Functor.associator _ _ E).symm â‰ªâ‰« isoWhiskerRight (Sum.swapCompInl A' A) _)
      ((Functor.associator _ _ _).symm â‰ªâ‰« isoWhiskerRight (Sum.swapCompInr A' A) _))

end Swap

section CompatibilityWithProductAssociator

variable (T : Type*) [Category T]

/-- The equivalence `Sum.functorEquiv` sends associativity of sums to associativity of products -/
@[simps! hom_app_fst hom_app_snd_fst hom_app_snd_snd inv_app_fst inv_app_snd_fst inv_app_snd_snd]
def associativityFunctorEquivNaturalityFunctorIso :
    ((sum.associativity A A' T).congrLeft.trans <| (Sum.functorEquiv A (A' âŠ• T) B).trans <|
      Equivalence.refl.prod <| Sum.functorEquiv _ _ B).functor â‰…
        (Sum.functorEquiv (A âŠ• A') T B).trans
          ((Sum.functorEquiv A A' B).prod Equivalence.refl)|>.trans
            (prod.associativity _ _ _)|>.functor :=
  NatIso.ofComponents (fun E â†¦ Iso.prod
    ((Functor.associator _ _ _).symm â‰ªâ‰«
      isoWhiskerRight (sum.inlCompInverseAssociator A A' T) E â‰ªâ‰« Functor.associator _ _ _)
    (Iso.prod
      (isoWhiskerLeft _ (Functor.associator _ _ E).symm â‰ªâ‰« (Functor.associator _ _ E).symm â‰ªâ‰«
        isoWhiskerRight (sum.inlCompInrCompInverseAssociator A A' T) E â‰ªâ‰« Functor.associator _ _ E)
      (isoWhiskerLeft _ (Functor.associator _ _ E).symm â‰ªâ‰« (Functor.associator _ _ E).symm â‰ªâ‰«
        isoWhiskerRight (sum.inrCompInrCompInverseAssociator A A' T) E))) (by
      intros
      ext
      all_goals
        dsimp
        simp only [Category.comp_id, Category.id_comp, NatTrans.naturality])

end CompatibilityWithProductAssociator

end Sum

end CategoryTheory
