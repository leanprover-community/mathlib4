/-
Copyright (c) 2023 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import Mathlib.CategoryTheory.Limits.Shapes.Products
/-!

# Effective epimorphisms

We define the notion of effective epimorphism and effective epimorphic family of morphisms.

A morphism is an *effective epi* if it is a joint coequalizer of all pairs of
morphisms which it coequalizes.

A family of morphisms with fixed target is *effective epimorphic* if it is initial among families
of morphisms with its sources and a general fixed target, coequalizing every pair of morphisms it
coequalizes (here, the pair of morphisms coequalized can have different targets among the sources
of the family).

We have defined the notion of effective epi for morphisms and families of morphisms in such a
way that avoids requiring the existence of pullbacks. However, if the relevant pullbacks exist
then these definitions are equivalent, see the file
`CategoryTheory/EffectiveEpi/RegularEpi.lean`
See [nlab: *Effective Epimorphism*](https://ncatlab.org/nlab/show/effective+epimorphism) and
[Stacks 00WP](https://stacks.math.columbia.edu/tag/00WP) for the standard definitions. Note that
our notion of `EffectiveEpi`Â is often called "strict epi" in the literature.

## References
- [Elephant]: *Sketches of an Elephant*, P. T. Johnstone: C2.1, Example 2.1.12.
- [nlab: *Effective Epimorphism*](https://ncatlab.org/nlab/show/effective+epimorphism) and
- [Stacks 00WP](https://stacks.math.columbia.edu/tag/00WP) for the standard definitions.

-/

namespace CategoryTheory

open Limits

variable {C : Type*} [Category C]

/--
This structure encodes the data required for a morphism to be an effective epimorphism.
-/
structure EffectiveEpiStruct {X Y : C} (f : Y âŸ¶ X) where
  /--
  For every `W` with a morphism `e : Y âŸ¶ W` that coequalizes every pair of morphisms
  `gâ‚ gâ‚‚ : Z âŸ¶ Y` which `f` coequalizes, `desc e h` is a morphism `X âŸ¶ W`...Â 
  -/
  desc : âˆ€ {W : C} (e : Y âŸ¶ W),
    (âˆ€ {Z : C} (gâ‚ gâ‚‚ : Z âŸ¶ Y), gâ‚ â‰« f = gâ‚‚ â‰« f â†’ gâ‚ â‰« e = gâ‚‚ â‰« e) â†’ (X âŸ¶ W)
  /-- ...factorizing `e` through `f`... -/
  fac : âˆ€ {W : C} (e : Y âŸ¶ W)
    (h : âˆ€ {Z : C} (gâ‚ gâ‚‚ : Z âŸ¶ Y), gâ‚ â‰« f = gâ‚‚ â‰« f â†’ gâ‚ â‰« e = gâ‚‚ â‰« e),
    f â‰« desc e h = e
  /-- ...and as such, unique. -/
  uniq : âˆ€ {W : C} (e : Y âŸ¶ W)
    (h : âˆ€ {Z : C} (gâ‚ gâ‚‚ : Z âŸ¶ Y), gâ‚ â‰« f = gâ‚‚ â‰« f â†’ gâ‚ â‰« e = gâ‚‚ â‰« e)
    (m : X âŸ¶ W), f â‰« m = e â†’ m = desc e h

/--
A morphism `f : Y âŸ¶ X` is an effective epimorphism provided that `f` exhibits `X` as a colimit
of the diagram of all "relations" `R â‡‰ Y`.
If `f` has a kernel pair, then this is equivalent to showing that the corresponding cofork is
a colimit.
-/
class EffectiveEpi {X Y : C} (f : Y âŸ¶ X) : Prop where
  /-- `f` is an effective epimorphism if there exists an `EffectiveEpiStruct`Â for `f`. -/
  effectiveEpi : Nonempty (EffectiveEpiStruct f)

/-- Some chosen `EffectiveEpiStruct` associated to an effective epi. -/
noncomputable
def EffectiveEpi.getStruct {X Y : C} (f : Y âŸ¶ X) [EffectiveEpi f] : EffectiveEpiStruct f :=
  EffectiveEpi.effectiveEpi.some

/-- Descend along an effective epi. -/
noncomputable
def EffectiveEpi.desc {X Y W : C} (f : Y âŸ¶ X) [EffectiveEpi f]
    (e : Y âŸ¶ W) (h : âˆ€ {Z : C} (gâ‚ gâ‚‚ : Z âŸ¶ Y), gâ‚ â‰« f = gâ‚‚ â‰« f â†’ gâ‚ â‰« e = gâ‚‚ â‰« e) :
    X âŸ¶ W := (EffectiveEpi.getStruct f).desc e h

@[reassoc (attr := simp)]
lemma EffectiveEpi.fac {X Y W : C} (f : Y âŸ¶ X) [EffectiveEpi f]
    (e : Y âŸ¶ W) (h : âˆ€ {Z : C} (gâ‚ gâ‚‚ : Z âŸ¶ Y), gâ‚ â‰« f = gâ‚‚ â‰« f â†’ gâ‚ â‰« e = gâ‚‚ â‰« e) :
    f â‰« EffectiveEpi.desc f e h = e :=
  (EffectiveEpi.getStruct f).fac e h

lemma EffectiveEpi.uniq {X Y W : C} (f : Y âŸ¶ X) [EffectiveEpi f]
    (e : Y âŸ¶ W) (h : âˆ€ {Z : C} (gâ‚ gâ‚‚ : Z âŸ¶ Y), gâ‚ â‰« f = gâ‚‚ â‰« f â†’ gâ‚ â‰« e = gâ‚‚ â‰« e)
    (m : X âŸ¶ W) (hm : f â‰« m = e) :
    m = EffectiveEpi.desc f e h :=
  (EffectiveEpi.getStruct f).uniq e h _ hm

instance epiOfEffectiveEpi {X Y : C} (f : Y âŸ¶ X) [EffectiveEpi f] : Epi f := by
  constructor
  intro W mâ‚ mâ‚‚ h
  have : mâ‚‚ = EffectiveEpi.desc f (f â‰« mâ‚‚)
    (fun {Z} gâ‚ gâ‚‚ h => by simp only [â† Category.assoc, h]) := EffectiveEpi.uniq _ _ _ _ rfl
  rw [this]
  exact EffectiveEpi.uniq _ _ _ _ h

/--
This structure encodes the data required for a family of morphisms to be effective epimorphic.
-/
structure EffectiveEpiFamilyStruct {B : C} {Î± : Type*}
    (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) where
  /--
  For every `W` with a family of morphisms `e a : Y a âŸ¶ W` that coequalizes every pair of morphisms
  `gâ‚ : Z âŸ¶ Y aâ‚`, `gâ‚‚ : Z âŸ¶ Y aâ‚‚` which the family `Ï€` coequalizes, `desc e h` is a morphism
  `X âŸ¶ W`...Â 
  -/
  desc : âˆ€ {W} (e : (a : Î±) â†’ (X a âŸ¶ W)),
          (âˆ€ {Z : C} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ X aâ‚) (gâ‚‚ : Z âŸ¶ X aâ‚‚),
      gâ‚ â‰« Ï€ _ = gâ‚‚ â‰« Ï€ _ â†’ gâ‚ â‰« e _ = gâ‚‚ â‰« e _) â†’ (B âŸ¶ W)
  /-- ...factorizing the components of `e` through the components of `Ï€`... -/
  fac : âˆ€ {W} (e : (a : Î±) â†’ (X a âŸ¶ W))
          (h : âˆ€ {Z : C} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ X aâ‚) (gâ‚‚ : Z âŸ¶ X aâ‚‚),
            gâ‚ â‰« Ï€ _ = gâ‚‚ â‰« Ï€ _ â†’ gâ‚ â‰« e _ = gâ‚‚ â‰« e _)
          (a : Î±), Ï€ a â‰« desc e h = e a
  /-- ...and as such, unique. -/
  uniq : âˆ€ {W} (e : (a : Î±) â†’ (X a âŸ¶ W))
          (h : âˆ€ {Z : C} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ X aâ‚) (gâ‚‚ : Z âŸ¶ X aâ‚‚),
            gâ‚ â‰« Ï€ _ = gâ‚‚ â‰« Ï€ _ â†’ gâ‚ â‰« e _ = gâ‚‚ â‰« e _)
          (m : B âŸ¶ W), (âˆ€ (a : Î±), Ï€ a â‰« m = e a) â†’ m = desc e h

/--
A family of morphisms `Ï€ a : X a âŸ¶ B` indexed by `Î±` is effective epimorphic
provided that the `Ï€ a` exhibit `B` as a colimit of the diagram of all "relations"
`R â†’ X aâ‚`, `R âŸ¶ X aâ‚‚` for all `aâ‚ aâ‚‚ : Î±`.
-/
class EffectiveEpiFamily {B : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) : Prop where
  /-- `Ï€` is an effective epimorphic family if there exists an `EffectiveEpiFamilyStruct`Â for `Ï€` -/
  effectiveEpiFamily : Nonempty (EffectiveEpiFamilyStruct X Ï€)

/-- Some chosen `EffectiveEpiFamilyStruct` associated to an effective epi family. -/
noncomputable
def EffectiveEpiFamily.getStruct {B : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B))
    [EffectiveEpiFamily X Ï€] : EffectiveEpiFamilyStruct X Ï€ :=
  EffectiveEpiFamily.effectiveEpiFamily.some

/-- Descend along an effective epi family. -/
noncomputable
def EffectiveEpiFamily.desc {B W : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B))
    [EffectiveEpiFamily X Ï€] (e : (a : Î±) â†’ (X a âŸ¶ W))
    (h : âˆ€ {Z : C} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ X aâ‚) (gâ‚‚ : Z âŸ¶ X aâ‚‚),
      gâ‚ â‰« Ï€ _ = gâ‚‚ â‰« Ï€ _ â†’ gâ‚ â‰« e _ = gâ‚‚ â‰« e _) : B âŸ¶ W :=
  (EffectiveEpiFamily.getStruct X Ï€).desc e h

@[reassoc (attr := simp)]
lemma EffectiveEpiFamily.fac {B W : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B))
    [EffectiveEpiFamily X Ï€] (e : (a : Î±) â†’ (X a âŸ¶ W))
    (h : âˆ€ {Z : C} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ X aâ‚) (gâ‚‚ : Z âŸ¶ X aâ‚‚),
      gâ‚ â‰« Ï€ _ = gâ‚‚ â‰« Ï€ _ â†’ gâ‚ â‰« e _ = gâ‚‚ â‰« e _) (a : Î±) :
    Ï€ a â‰« EffectiveEpiFamily.desc X Ï€ e h = e a :=
  (EffectiveEpiFamily.getStruct X Ï€).fac e h a

lemma EffectiveEpiFamily.uniq {B W : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B))
    [EffectiveEpiFamily X Ï€] (e : (a : Î±) â†’ (X a âŸ¶ W))
    (h : âˆ€ {Z : C} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ X aâ‚) (gâ‚‚ : Z âŸ¶ X aâ‚‚),
      gâ‚ â‰« Ï€ _ = gâ‚‚ â‰« Ï€ _ â†’ gâ‚ â‰« e _ = gâ‚‚ â‰« e _)
    (m : B âŸ¶ W) (hm : âˆ€ a, Ï€ a â‰« m = e a) :
    m = EffectiveEpiFamily.desc X Ï€ e h :=
  (EffectiveEpiFamily.getStruct X Ï€).uniq e h m hm

-- TODO: Once we have "jointly epimorphic families", we could rephrase this as such a property.
lemma EffectiveEpiFamily.hom_ext {B W : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B))
    [EffectiveEpiFamily X Ï€] (mâ‚ mâ‚‚ : B âŸ¶ W) (h : âˆ€ a, Ï€ a â‰« mâ‚ = Ï€ a â‰« mâ‚‚) :
    mâ‚ = mâ‚‚ := by
  have : mâ‚‚ = EffectiveEpiFamily.desc X Ï€ (fun a => Ï€ a â‰« mâ‚‚)
      (fun aâ‚ aâ‚‚ gâ‚ gâ‚‚ h => by simp only [â† Category.assoc, h]) := by
    apply EffectiveEpiFamily.uniq; intro; rfl
  rw [this]
  exact EffectiveEpiFamily.uniq _ _ _ _ _ h

/--
An `EffectiveEpiFamily` consisting of a single `EffectiveEpi`
-/
noncomputable
def effectiveEpiFamilyStructSingletonOfEffectiveEpi {B X : C} (f : X âŸ¶ B) [EffectiveEpi f] :
    EffectiveEpiFamilyStruct (fun () â†¦ X) (fun () â†¦ f) where
  desc e h := EffectiveEpi.desc f (e ()) (fun gâ‚ gâ‚‚ hg â†¦ h () () gâ‚ gâ‚‚ hg)
  fac e h := fun _ â†¦ EffectiveEpi.fac f (e ()) (fun gâ‚ gâ‚‚ hg â†¦ h () () gâ‚ gâ‚‚ hg)
  uniq e h m hm := by apply EffectiveEpi.uniq f (e ()) (h () ()); exact hm ()

instance {B X : C} (f : X âŸ¶ B) [EffectiveEpi f] : EffectiveEpiFamily (fun () â†¦ X) (fun () â†¦ f) :=
  âŸ¨âŸ¨effectiveEpiFamilyStructSingletonOfEffectiveEpi fâŸ©âŸ©

/--
A single element `EffectiveEpiFamily`Â constists of an `EffectiveEpi`
-/
noncomputable
def effectiveEpiStructOfEffectiveEpiFamilySingleton {B X : C} (f : X âŸ¶ B)
    [EffectiveEpiFamily (fun () â†¦ X) (fun () â†¦ f)] :
    EffectiveEpiStruct f where
  desc e h := EffectiveEpiFamily.desc
    (fun () â†¦ X) (fun () â†¦ f) (fun () â†¦ e) (fun _ _ gâ‚ gâ‚‚ hg â†¦ h gâ‚ gâ‚‚ hg)
  fac e h := EffectiveEpiFamily.fac
    (fun () â†¦ X) (fun () â†¦ f) (fun () â†¦ e) (fun _ _ gâ‚ gâ‚‚ hg â†¦ h gâ‚ gâ‚‚ hg) ()
  uniq e h m hm := EffectiveEpiFamily.uniq
    (fun () â†¦ X) (fun () â†¦ f) (fun () â†¦ e) (fun _ _ gâ‚ gâ‚‚ hg â†¦ h gâ‚ gâ‚‚ hg) m (fun _ â†¦ hm)

instance {B X : C} (f : X âŸ¶ B) [EffectiveEpiFamily (fun () â†¦ X) (fun () â†¦ f)] :
    EffectiveEpi f :=
  âŸ¨âŸ¨effectiveEpiStructOfEffectiveEpiFamilySingleton fâŸ©âŸ©

theorem effectiveEpi_iff_effectiveEpiFamily {B X : C} (f : X âŸ¶ B) :
    EffectiveEpi f â†” EffectiveEpiFamily (fun () â†¦ X) (fun () â†¦ f) :=
  âŸ¨fun _ â†¦ inferInstance, fun _ â†¦ inferInstanceâŸ©

/--
A family of morphisms with the same target inducing an isomorphism from the coproduct to the target
is an `EffectiveEpiFamily`.
-/
noncomputable
def effectiveEpiFamilyStructOfIsIsoDesc {B : C} {Î± : Type*} (X : Î± â†’ C)
    (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) [HasCoproduct X] [IsIso (Sigma.desc Ï€)] :
    EffectiveEpiFamilyStruct X Ï€ where
  desc e _ := (asIso (Sigma.desc Ï€)).inv â‰« (Sigma.desc e)
  fac e h := by
    intro a
    have : Ï€ a = Sigma.Î¹ X a â‰« (asIso (Sigma.desc Ï€)).hom := by simp only [asIso_hom,
      colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app]
    rw [this, Category.assoc]
    simp only [asIso_hom, asIso_inv, IsIso.hom_inv_id_assoc, colimit.Î¹_desc, Cofan.mk_pt,
      Cofan.mk_Î¹_app]
  uniq e h m hm := by
    simp only [asIso_inv, IsIso.eq_inv_comp]
    ext a
    simp only [colimit.Î¹_desc_assoc, Discrete.functor_obj, Cofan.mk_pt, Cofan.mk_Î¹_app,
      colimit.Î¹_desc]
    exact hm a

instance {B : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) [HasCoproduct X]
    [IsIso (Sigma.desc Ï€)] : EffectiveEpiFamily X Ï€ :=
  âŸ¨âŸ¨effectiveEpiFamilyStructOfIsIsoDesc X Ï€âŸ©âŸ©

/-- Any isomorphism is an effective epi. -/
noncomputable
def effectiveEpiStructOfIsIso {X Y : C} (f : X âŸ¶ Y) [IsIso f] : EffectiveEpiStruct f where
  desc e _ := inv f â‰« e
  fac _ _ := by simp
  uniq _ _ _ h := by simpa using h

instance {X Y : C} (f : X âŸ¶ Y) [IsIso f] : EffectiveEpi f := âŸ¨âŸ¨effectiveEpiStructOfIsIso fâŸ©âŸ©

example {X : C} : EffectiveEpiFamily (fun _ => X : Unit â†’ C) (fun _ => ðŸ™ X) := inferInstance

/--
Reindex the indexing type of an effective epi family struct.
-/
def EffectiveEpiFamilyStruct.reindex
    {B : C} {Î± Î±' : Type*}
    (X : Î± â†’ C)
    (Ï€ : (a : Î±) â†’ (X a âŸ¶ B))
    (e : Î±' â‰ƒ Î±)
    (P : EffectiveEpiFamilyStruct (fun a => X (e a)) (fun a => Ï€ (e a))) :
    EffectiveEpiFamilyStruct X Ï€ where
  desc := fun f h => P.desc (fun a => f _) (fun aâ‚ aâ‚‚ => h _ _)
  fac _ _ a := by
    obtain âŸ¨a,rflâŸ© := e.surjective a
    apply P.fac
  uniq _ _ m hm := P.uniq _ _ _ fun a => hm _

/--
Reindex the indexing type of an effective epi family.
-/
lemma EffectiveEpiFamily.reindex
    {B : C} {Î± Î±' : Type*}
    (X : Î± â†’ C)
    (Ï€ : (a : Î±) â†’ (X a âŸ¶ B))
    (e : Î±' â‰ƒ Î±)
    (h : EffectiveEpiFamily (fun a => X (e a)) (fun a => Ï€ (e a))) :
    EffectiveEpiFamily X Ï€ :=
  .mk <| .intro <| @EffectiveEpiFamily.getStruct _ _ _ _ _ _ h |>.reindex _ _ e
