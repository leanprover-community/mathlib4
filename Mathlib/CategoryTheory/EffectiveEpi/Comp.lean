/-
Copyright (c) 2023 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.CategoryTheory.EffectiveEpi.Basic
/-!

# Composition of effective epimorphisms

This file provides `EffectiveEpi`Â instances for certain compositions.

-/

namespace CategoryTheory

open Limits

variable {C : Type*} [Category C]

/--
An effective epi family precomposed by a family of split epis is effective epimorphic.
This version takes an explicit section to the split epis, and is mainly used to define
`effectiveEpiStructCompOfEffectiveEpiSplitEpi`,
which takes a `IsSplitEpi` instance instead.
-/
noncomputable
def effectiveEpiFamilyStructCompOfEffectiveEpiSplitEpi' {Î± : Type*} {B : C} {X Y : Î± â†’ C}
    (f : (a : Î±) â†’ X a âŸ¶ B) (g : (a : Î±) â†’ Y a âŸ¶ X a) (i : (a : Î±) â†’ X a âŸ¶ Y a)
    (hi : âˆ€ a, i a â‰« g a = ğŸ™ _) [EffectiveEpiFamily _ f] :
    EffectiveEpiFamilyStruct _ (fun a â†¦ g a â‰« f a) where
  desc e w := EffectiveEpiFamily.desc _ f (fun a â†¦ i a â‰« e a) fun aâ‚ aâ‚‚ gâ‚ gâ‚‚ _ â†¦ (by
    simp only [â† Category.assoc]
    apply w _ _ (gâ‚ â‰« i aâ‚) (gâ‚‚ â‰« i aâ‚‚)
    simpa [â† Category.assoc, hi])
  fac e w a := by
    simp only [Category.assoc, EffectiveEpiFamily.fac]
    rw [â† Category.id_comp (e a), â† Category.assoc, â† Category.assoc]
    apply w
    simp only [Category.comp_id, Category.id_comp, â† Category.assoc]
    aesop
  uniq _ _ _ hm := by
    apply EffectiveEpiFamily.uniq _ f
    intro a
    rw [â† hm a, â† Category.assoc, â† Category.assoc, hi, Category.id_comp]

/--
An effective epi family precomposed with a family of split epis is effective epimorphic.
-/
noncomputable
def effectiveEpiFamilyStructCompOfEffectiveEpiSplitEpi {Î± : Type*} {B : C} {X Y : Î± â†’ C}
    (f : (a : Î±) â†’ X a âŸ¶ B) (g : (a : Î±) â†’ Y a âŸ¶ X a) [âˆ€ a, IsSplitEpi (g a)]
    [EffectiveEpiFamily _ f] : EffectiveEpiFamilyStruct _ (fun a â†¦ g a â‰« f a) :=
  effectiveEpiFamilyStructCompOfEffectiveEpiSplitEpi' f g
    (fun a â†¦ section_ (g a))
    (fun a â†¦ IsSplitEpi.id (g a))

instance {Î± : Type*} {B : C} {X Y : Î± â†’ C}
    (f : (a : Î±) â†’ X a âŸ¶ B) (g : (a : Î±) â†’ Y a âŸ¶ X a) [âˆ€ a, IsSplitEpi (g a)]
    [EffectiveEpiFamily _ f] : EffectiveEpiFamily _ (fun a â†¦ g a â‰« f a) :=
  âŸ¨âŸ¨effectiveEpiFamilyStructCompOfEffectiveEpiSplitEpi f gâŸ©âŸ©

example {B X Y : C} (f : X âŸ¶ B) (g : Y âŸ¶ X) [IsSplitEpi g] [EffectiveEpi f] :
    EffectiveEpi (g â‰« f) := inferInstance

instance IsSplitEpi.EffectiveEpi {B X : C} (f : X âŸ¶ B) [IsSplitEpi f] : EffectiveEpi f := by
  rw [â† Category.comp_id f]
  infer_instance

/--
If a family of morphisms with fixed target, precomposed by a family of split epis is
effective epimorphic, then the original family is as well.
This version takes an explicit section to the split epis, and is mainly used to define
`effectiveEpiStructCompOfEffectiveEpiSplitEpi`,
which takes a `IsSplitEpi` instance instead.
-/
noncomputable
def effectiveEpiFamilyStructOfEffectiveEpiSplitEpiComp' {Î± : Type*} {B : C} {X Y : Î± â†’ C}
    (f : (a : Î±) â†’ X a âŸ¶ B) (g : (a : Î±) â†’ Y a âŸ¶ X a) (i : (a : Î±) â†’ X a âŸ¶ Y a)
    (hi : âˆ€ a, i a â‰« g a = ğŸ™ _) [EffectiveEpiFamily _ (fun a â†¦ g a â‰« f a)] :
    EffectiveEpiFamilyStruct _ f where
  desc e w := EffectiveEpiFamily.desc _ (fun a â†¦ g a â‰« f a) (fun a â†¦ g a â‰« e a) fun aâ‚ aâ‚‚ gâ‚ gâ‚‚ hg â†¦
    (by
      simp only [â† Category.assoc] at hg
      simpa using w aâ‚ aâ‚‚ (gâ‚ â‰« g aâ‚) (gâ‚‚ â‰« g aâ‚‚) hg)
  fac e w a := by
    have := EffectiveEpiFamily.fac _ (fun a â†¦ g a â‰« f a) (fun a â†¦ g a â‰« e a) fun aâ‚ aâ‚‚ gâ‚ gâ‚‚ hg â†¦
      (by
        simp only [â† Category.assoc] at hg
        simpa using w aâ‚ aâ‚‚ (gâ‚ â‰« g aâ‚) (gâ‚‚ â‰« g aâ‚‚) hg)
    have := congrArg (i a â‰« Â·) (this a)
    simp only [â† Category.assoc, hi a] at this
    simpa using this
  uniq _ _ _ hm := by
    apply EffectiveEpiFamily.uniq _ (fun a â†¦ g a â‰« f a)
    intro a
    rw [â† hm, â† Category.assoc]

/--
If a family of morphisms with fixed target, precomposed by a family of split epis is
effective epimorphic, then the original family is as well.
-/
noncomputable
def effectiveEpiFamilyStructOfEffectiveEpiSplitEpiComp {Î± : Type*} {B : C} {X Y : Î± â†’ C}
    (f : (a : Î±) â†’ X a âŸ¶ B) (g : (a : Î±) â†’ Y a âŸ¶ X a) [âˆ€ a, IsSplitEpi (g a)]
    [EffectiveEpiFamily _ (fun a â†¦ g a â‰« f a)] : EffectiveEpiFamilyStruct _ f :=
  effectiveEpiFamilyStructOfEffectiveEpiSplitEpiComp' f g
    (fun a â†¦ section_ (g a))
    (fun a â†¦ IsSplitEpi.id (g a))

lemma effectiveEpiFamily_of_effectiveEpi_splitEpi_comp {Î± : Type*} {B : C} {X Y : Î± â†’ C}
    (f : (a : Î±) â†’ X a âŸ¶ B) (g : (a : Î±) â†’ Y a âŸ¶ X a) [âˆ€ a, IsSplitEpi (g a)]
    [EffectiveEpiFamily _ (fun a â†¦ g a â‰« f a)] : EffectiveEpiFamily _ f :=
  âŸ¨âŸ¨effectiveEpiFamilyStructOfEffectiveEpiSplitEpiComp f gâŸ©âŸ©

lemma effectiveEpi_of_effectiveEpi_splitEpi_comp {B X Y : C} (f : X âŸ¶ B) (g : Y âŸ¶ X)
    [IsSplitEpi g] [EffectiveEpi (g â‰« f)] : EffectiveEpi f :=
  have := (effectiveEpi_iff_effectiveEpiFamily (g â‰« f)).mp inferInstance
  have := effectiveEpiFamily_of_effectiveEpi_splitEpi_comp
    (X := fun () â†¦ X) (Y := fun () â†¦ Y) (fun () â†¦ f) (fun () â†¦ g)
  inferInstance

section CompIso

variable {B B' : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) [EffectiveEpiFamily X Ï€]
  (i : B âŸ¶ B') [IsIso i]

theorem effectiveEpiFamilyStructCompIso_aux
    {W : C} (e : (a : Î±) â†’ X a âŸ¶ W)
    (h : âˆ€ {Z : C} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ X aâ‚) (gâ‚‚ : Z âŸ¶ X aâ‚‚),
      gâ‚ â‰« Ï€ aâ‚ â‰« i = gâ‚‚ â‰« Ï€ aâ‚‚ â‰« i â†’ gâ‚ â‰« e aâ‚ = gâ‚‚ â‰« e aâ‚‚)
    {Z : C} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ X aâ‚) (gâ‚‚ : Z âŸ¶ X aâ‚‚) (hg : gâ‚ â‰« Ï€ aâ‚ = gâ‚‚ â‰« Ï€ aâ‚‚) :
    gâ‚ â‰« e aâ‚ = gâ‚‚ â‰« e aâ‚‚ := by
  apply h
  rw [â† Category.assoc, hg]
  simp

/-- An effective epi family followed by an iso is an effective epi family. -/
noncomputable
def effectiveEpiFamilyStructCompIso : EffectiveEpiFamilyStruct X (fun a â†¦ Ï€ a â‰« i) where
  desc e h := inv i â‰« EffectiveEpiFamily.desc X Ï€ e (effectiveEpiFamilyStructCompIso_aux X Ï€ i e h)
  fac _ _ _ := by simp
  uniq e h m hm := by
    simp only [Category.assoc] at hm
    simp [â† EffectiveEpiFamily.uniq X Ï€ e
      (effectiveEpiFamilyStructCompIso_aux X Ï€ i e h) (i â‰« m) hm]

instance : EffectiveEpiFamily X (fun a â†¦ Ï€ a â‰« i) := âŸ¨âŸ¨effectiveEpiFamilyStructCompIso X Ï€ iâŸ©âŸ©

end CompIso

section IsoComp

variable {B : C} {Î± : Type*} (X Y : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) [EffectiveEpiFamily X Ï€]
  (i : (a : Î±) â†’ Y a âŸ¶ X a) [âˆ€ a, IsIso (i a)]

example : EffectiveEpiFamily Y (fun a â†¦ i a â‰« Ï€ a) :=
  inferInstance

end IsoComp
