/-
Copyright (c) 2021 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Category.ULift
import Mathlib.CategoryTheory.EqToHom
import Mathlib.CategoryTheory.Skeletal
import Mathlib.CategoryTheory.Comma.Arrow
import Mathlib.Logic.UnivLE
import Mathlib.Logic.Small.Basic

/-!
# Essentially small categories.

A category given by `(C : Type u) [Category.{v} C]` is `w`-essentially small
if there exists a `SmallModel C : Type w` equipped with `[SmallCategory (SmallModel C)]` and an
equivalence `C ‚âå SmallModel C`.

A category is `w`-locally small if every hom type is `w`-small.

The main theorem here is that a category is `w`-essentially small iff
the type `Skeleton C` is `w`-small, and `C` is `w`-locally small.
-/


universe w w' v v' u u'

open CategoryTheory

variable (C : Type u) [Category.{v} C]

namespace CategoryTheory

/-- A category is `EssentiallySmall.{w}` if there exists
an equivalence to some `S : Type w` with `[SmallCategory S]`. -/
@[pp_with_univ]
class EssentiallySmall (C : Type u) [Category.{v} C] : Prop where
  /-- An essentially small category is equivalent to some small category. -/
  equiv_smallCategory : ‚àÉ (S : Type w) (_ : SmallCategory S), Nonempty (C ‚âå S)

/-- Constructor for `EssentiallySmall C` from an explicit small category witness. -/
theorem EssentiallySmall.mk' {C : Type u} [Category.{v} C] {S : Type w} [SmallCategory S]
    (e : C ‚âå S) : EssentiallySmall.{w} C :=
  ‚ü®‚ü®S, _, ‚ü®e‚ü©‚ü©‚ü©

/-- An arbitrarily chosen small model for an essentially small category.
-/
@[pp_with_univ]
def SmallModel (C : Type u) [Category.{v} C] [EssentiallySmall.{w} C] : Type w :=
  Classical.choose (@EssentiallySmall.equiv_smallCategory C _ _)

noncomputable instance smallCategorySmallModel (C : Type u) [Category.{v} C]
    [EssentiallySmall.{w} C] : SmallCategory (SmallModel C) :=
  Classical.choose (Classical.choose_spec (@EssentiallySmall.equiv_smallCategory C _ _))

/-- The (noncomputable) categorical equivalence between
an essentially small category and its small model.
-/
noncomputable def equivSmallModel (C : Type u) [Category.{v} C] [EssentiallySmall.{w} C] :
    C ‚âå SmallModel C :=
  Nonempty.some
    (Classical.choose_spec (Classical.choose_spec (@EssentiallySmall.equiv_smallCategory C _ _)))

instance (C : Type u) [Category.{v} C] [EssentiallySmall.{w} C] : EssentiallySmall.{w} C·µí·µñ :=
  EssentiallySmall.mk' (equivSmallModel C).op

theorem essentiallySmall_congr {C : Type u} [Category.{v} C] {D : Type u'} [Category.{v'} D]
    (e : C ‚âå D) : EssentiallySmall.{w} C ‚Üî EssentiallySmall.{w} D := by
  fconstructor
  ¬∑ rintro ‚ü®S, ùíÆ, ‚ü®f‚ü©‚ü©
    exact EssentiallySmall.mk' (e.symm.trans f)
  ¬∑ rintro ‚ü®S, ùíÆ, ‚ü®f‚ü©‚ü©
    exact EssentiallySmall.mk' (e.trans f)

theorem Discrete.essentiallySmallOfSmall {Œ± : Type u} [Small.{w} Œ±] :
    EssentiallySmall.{w} (Discrete Œ±) :=
  ‚ü®‚ü®Discrete (Shrink Œ±), ‚ü®inferInstance, ‚ü®Discrete.equivalence (equivShrink _)‚ü©‚ü©‚ü©‚ü©

theorem essentiallySmallSelf : EssentiallySmall.{max w v u} C :=
  EssentiallySmall.mk' (AsSmall.equiv : C ‚âå AsSmall.{w} C)

/-- A category is `w`-locally small if every hom set is `w`-small.

See `ShrinkHoms C` for a category instance where every hom set has been replaced by a small model.
-/
@[pp_with_univ]
class LocallySmall (C : Type u) [Category.{v} C] : Prop where
  /-- A locally small category has small hom-types. -/
  hom_small : ‚àÄ X Y : C, Small.{w} (X ‚ü∂ Y) := by infer_instance

instance (C : Type u) [Category.{v} C] [LocallySmall.{w} C] (X Y : C) : Small.{w, v} (X ‚ü∂ Y) :=
  LocallySmall.hom_small X Y

instance (C : Type u) [Category.{v} C] [LocallySmall.{w} C] : LocallySmall.{w} C·µí·µñ where
  hom_small X Y := small_of_injective (opEquiv X Y).injective

theorem locallySmall_of_faithful {C : Type u} [Category.{v} C] {D : Type u'} [Category.{v'} D]
    (F : C ‚•§ D) [F.Faithful] [LocallySmall.{w} D] : LocallySmall.{w} C where
  hom_small {_ _} := small_of_injective F.map_injective

theorem locallySmall_congr {C : Type u} [Category.{v} C] {D : Type u'} [Category.{v'} D]
    (e : C ‚âå D) : LocallySmall.{w} C ‚Üî LocallySmall.{w} D :=
  ‚ü®fun _ => locallySmall_of_faithful e.inverse, fun _ => locallySmall_of_faithful e.functor‚ü©

instance (priority := 100) locallySmall_self (C : Type u) [Category.{v} C] :
    LocallySmall.{v} C where

instance (priority := 100) locallySmall_of_univLE (C : Type u) [Category.{v} C] [UnivLE.{v, w}] :
    LocallySmall.{w} C where

theorem locallySmall_max {C : Type u} [Category.{v} C] : LocallySmall.{max v w} C where
  hom_small _ _ := small_max.{w} _

instance (priority := 100) locallySmall_of_essentiallySmall (C : Type u) [Category.{v} C]
    [EssentiallySmall.{w} C] : LocallySmall.{w} C :=
  (locallySmall_congr (equivSmallModel C)).mpr (CategoryTheory.locallySmall_self _)

/-- We define a type alias `ShrinkHoms C` for `C`. When we have `LocallySmall.{w} C`,
we'll put a `Category.{w}` instance on `ShrinkHoms C`.
-/
@[pp_with_univ]
def ShrinkHoms (C : Type u) :=
  C

namespace ShrinkHoms

section

variable {C' : Type*}

-- a fresh variable with no category instance attached
/-- Help the typechecker by explicitly translating from `C` to `ShrinkHoms C`. -/
def toShrinkHoms {C' : Type*} (X : C') : ShrinkHoms C' :=
  X

/-- Help the typechecker by explicitly translating from `ShrinkHoms C` to `C`. -/
def fromShrinkHoms {C' : Type*} (X : ShrinkHoms C') : C' :=
  X

@[simp]
theorem to_from (X : C') : fromShrinkHoms (toShrinkHoms X) = X :=
  rfl

@[simp]
theorem from_to (X : ShrinkHoms C') : toShrinkHoms (fromShrinkHoms X) = X :=
  rfl

end

variable [LocallySmall.{w} C]

@[simps]
noncomputable instance : Category.{w} (ShrinkHoms C) where
  Hom X Y := Shrink (fromShrinkHoms X ‚ü∂ fromShrinkHoms Y)
  id X := equivShrink _ (ùüô (fromShrinkHoms X))
  comp f g := equivShrink _ ((equivShrink _).symm f ‚â´ (equivShrink _).symm g)

/-- Implementation of `ShrinkHoms.equivalence`. -/
@[simps]
noncomputable def functor : C ‚•§ ShrinkHoms C where
  obj X := toShrinkHoms X
  map {X Y} f := equivShrink (X ‚ü∂ Y) f

/-- Implementation of `ShrinkHoms.equivalence`. -/
@[simps]
noncomputable def inverse : ShrinkHoms C ‚•§ C where
  obj X := fromShrinkHoms X
  map {X Y} f := (equivShrink (fromShrinkHoms X ‚ü∂ fromShrinkHoms Y)).symm f

/-- The categorical equivalence between `C` and `ShrinkHoms C`, when `C` is locally small.
-/
@[simps]
noncomputable def equivalence : C ‚âå ShrinkHoms C where
  functor := functor C
  inverse := inverse C
  unitIso := NatIso.ofComponents (fun _ ‚Ü¶ Iso.refl _)
  counitIso := NatIso.ofComponents (fun _ ‚Ü¶ Iso.refl _)

instance : (functor C).IsEquivalence := (equivalence C).isEquivalence_functor
instance : (inverse C).IsEquivalence := (equivalence C).isEquivalence_inverse

instance {T : Type u} [Unique T] : Unique (ShrinkHoms.{u} T) where
  default := ShrinkHoms.toShrinkHoms (default : T)
  uniq _ := congr_arg ShrinkHoms.fromShrinkHoms (Unique.uniq _ _)

instance {T : Type u} [Category.{v} T] [IsDiscrete T] : IsDiscrete (ShrinkHoms.{u} T) where
  subsingleton _ _ := { allEq _ _ := Shrink.ext (Subsingleton.elim _ _) }
  eq_of_hom f := IsDiscrete.eq_of_hom  (C := T) ((equivShrink _).symm f)

end ShrinkHoms

namespace Shrink

noncomputable instance [Small.{w} C] : Category.{v} (Shrink.{w} C) :=
  InducedCategory.category (equivShrink C).symm

/-- The categorical equivalence between `C` and `Shrink C`, when `C` is small. -/
noncomputable def equivalence [Small.{w} C] : C ‚âå Shrink.{w} C :=
  (Equivalence.induced _).symm

instance [Small.{w'} C] [LocallySmall.{w} C] :
    LocallySmall.{w} (Shrink.{w'} C) :=
  locallySmall_of_faithful.{w} (equivalence.{w'} C).inverse

end Shrink

/-- A category is essentially small if and only if
the underlying type of its skeleton (i.e. the "set" of isomorphism classes) is small,
and it is locally small.
-/
theorem essentiallySmall_iff (C : Type u) [Category.{v} C] :
    EssentiallySmall.{w} C ‚Üî Small.{w} (Skeleton C) ‚àß LocallySmall.{w} C := by
  -- This theorem is the only bit of real work in this file.
  fconstructor
  ¬∑ intro h
    fconstructor
    ¬∑ rcases h with ‚ü®S, ùíÆ, ‚ü®e‚ü©‚ü©
      refine ‚ü®‚ü®Skeleton S, ‚ü®?_‚ü©‚ü©‚ü©
      exact e.skeletonEquiv
    ¬∑ infer_instance
  ¬∑ rintro ‚ü®‚ü®S, ‚ü®e‚ü©‚ü©, L‚ü©
    let e' := (ShrinkHoms.equivalence C).skeletonEquiv.symm
    letI : Category S := InducedCategory.category (e'.trans e).symm
    refine ‚ü®‚ü®S, this, ‚ü®?_‚ü©‚ü©‚ü©
    refine (ShrinkHoms.equivalence C).trans <|
      (skeletonEquivalence (ShrinkHoms C)).symm.trans
        ((inducedFunctor (e'.trans e).symm).asEquivalence.symm)

theorem essentiallySmall_of_small_of_locallySmall [Small.{w} C] [LocallySmall.{w} C] :
    EssentiallySmall.{w} C :=
  (essentiallySmall_iff C).2 ‚ü®small_of_surjective Quotient.exists_rep, by infer_instance‚ü©

instance small_skeleton_of_essentiallySmall [h : EssentiallySmall.{w} C] : Small.{w} (Skeleton C) :=
  essentiallySmall_iff C |>.1 h |>.1

variable {C} in
theorem essentiallySmall_of_fully_faithful {D : Type u'} [Category.{v'} D] (F : C ‚•§ D)
    [F.Full] [F.Faithful] [EssentiallySmall.{w} D] : EssentiallySmall.{w} C :=
  (essentiallySmall_iff C).2 ‚ü®small_of_injective F.mapSkeleton_injective,
    locallySmall_of_faithful F‚ü©

section FullSubcategory

instance locallySmall_fullSubcategory [LocallySmall.{w} C] (P : ObjectProperty C) :
    LocallySmall.{w} P.FullSubcategory :=
  locallySmall_of_faithful <| P.Œπ

instance essentiallySmall_fullSubcategory_mem (s : Set C) [Small.{w} s] [LocallySmall.{w} C] :
    EssentiallySmall.{w} (ObjectProperty.FullSubcategory (¬∑ ‚àà s)) :=
  suffices Small.{w} (ObjectProperty.FullSubcategory (¬∑ ‚àà s)) from
    essentiallySmall_of_small_of_locallySmall _
  small_of_injective (f := fun x => (‚ü®x.1, x.2‚ü© : s)) (by cat_disch)

end FullSubcategory

/-- Any thin category is locally small.
-/
instance (priority := 100) locallySmall_of_thin {C : Type u} [Category.{v} C] [Quiver.IsThin C] :
    LocallySmall.{w} C where

/--
A thin category is essentially small if and only if the underlying type of its skeleton is small.
-/
theorem essentiallySmall_iff_of_thin {C : Type u} [Category.{v} C] [Quiver.IsThin C] :
    EssentiallySmall.{w} C ‚Üî Small.{w} (Skeleton C) := by
  simp [essentiallySmall_iff, CategoryTheory.locallySmall_of_thin]

instance [Small.{w} C] : Small.{w} (Discrete C) := small_map discreteEquiv

instance [Small.{w} C] [LocallySmall.{w} C] :
    Small.{w} (Arrow C) := by
  let œÜ (f : Arrow C) : Œ£ (s t : C), s ‚ü∂ t := ‚ü®_, _, f.hom‚ü©
  refine small_of_injective (f := œÜ) ?_
  rintro ‚ü®s, t, f‚ü© ‚ü®s', t', f'‚ü© h
  obtain rfl : s = s' := congr_arg Sigma.fst h
  simp only [Functor.id_obj, Sigma.mk.injEq, heq_eq_eq, true_and, œÜ] at h
  obtain rfl : t = t' := h.1
  obtain rfl : f = f' := by simpa using h
  rfl

instance [Small.{w} C] [LocallySmall.{w} C]
    {D : Type u'} [Category.{v'} D] [Small.{w} D] [LocallySmall.{w} D] :
    Small.{w} (C ‚•§ D) := by
  refine small_of_injective (f := fun F (f : Arrow C) ‚Ü¶ Arrow.mk (F.map f.hom))
    (fun F G h ‚Ü¶ Functor.ext (fun X ‚Ü¶ ?_) (fun X Y f ‚Ü¶ ?_))
  ¬∑ exact congr_arg Comma.left (congr_fun h (Arrow.mk (ùüô X)))
  ¬∑ have : Arrow.mk (F.map f) = Arrow.mk (G.map f) := congr_fun h (Arrow.mk f)
    rw [Arrow.mk_eq_mk_iff] at this
    tauto

end CategoryTheory
