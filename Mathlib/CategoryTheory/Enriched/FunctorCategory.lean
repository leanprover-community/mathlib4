/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Monoidal.FunctorCategory
import Mathlib.CategoryTheory.Enriched.Ordinary.Basic
import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.Limits.Shapes.End

/-!
# Functor categories are enriched

If `C` is a `V`-enriched ordinary category, then `J ‚•§ C` is also
both a `V`-enriched ordinary category and a `J ‚•§ V`-enriched
ordinary category, provided `C` has suitable limits.

We first define the `V`-enriched structure on `J ‚•§ C` by saying
that if `F‚ÇÅ` and `F‚ÇÇ` are in `J ‚•§ C`, then `enrichedHom V F‚ÇÅ F‚ÇÇ : V`
is a suitable limit involving `F‚ÇÅ.obj j ‚ü∂[V] F‚ÇÇ.obj j` for all `j : C`.
The `J ‚•§ V` object of morphisms `functorEnrichedHom V F‚ÇÅ F‚ÇÇ : J ‚•§ V`
is defined by sending `j : J` to the previously defined `enrichedHom`
for the "restriction" of `F‚ÇÅ` and `F‚ÇÇ` to the category `Under j`.
The definition `isLimitConeFunctorEnrichedHom` shows that
`enriched V F‚ÇÅ F‚ÇÇ` is the limit of the functor `functorEnrichedHom V F‚ÇÅ F‚ÇÇ`.

-/

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ v‚ÇÑ u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ

namespace CategoryTheory.Enriched.FunctorCategory

open Category MonoidalCategory Limits Functor

variable (V : Type u‚ÇÅ) [Category.{v‚ÇÅ} V] [MonoidalCategory V]
  {C : Type u‚ÇÇ} [Category.{v‚ÇÇ} C] {J : Type u‚ÇÉ} [Category.{v‚ÇÉ} J]
  {K : Type u‚ÇÑ} [Category.{v‚ÇÑ} K] [EnrichedOrdinaryCategory V C]

variable (F‚ÇÅ F‚ÇÇ F‚ÇÉ F‚ÇÑ : J ‚•§ C)

/-- Given two functors `F‚ÇÅ` and `F‚ÇÇ` from a category `J` to a `V`-enriched
ordinary category `C`, this is the diagram `J·µí·µñ ‚•§ J ‚•§ V` whose end shall be
the `V`-morphisms in `J ‚•§ V` from `F‚ÇÅ` to `F‚ÇÇ`. -/
@[simps!]
def diagram : J·µí·µñ ‚•§ J ‚•§ V := F‚ÇÅ.op ‚ãô eHomFunctor V C ‚ãô (whiskeringLeft J C V).obj F‚ÇÇ

/-- The condition that the end `diagram V F‚ÇÅ F‚ÇÇ` exists, see `enrichedHom`. -/
abbrev HasEnrichedHom := HasEnd (diagram V F‚ÇÅ F‚ÇÇ)

section

variable [HasEnrichedHom V F‚ÇÅ F‚ÇÇ]

/-- The `V`-enriched hom from `F‚ÇÅ` to `F‚ÇÇ` when `F‚ÇÅ` and `F‚ÇÇ` are functors `J ‚•§ C`
and `C` is a `V`-enriched category. -/
noncomputable abbrev enrichedHom : V := end_ (diagram V F‚ÇÅ F‚ÇÇ)

/-- The projection `enrichedHom V F‚ÇÅ F‚ÇÇ ‚ü∂ F‚ÇÅ.obj j ‚ü∂[V] F‚ÇÇ.obj j` in the category `V`
for any `j : J` when `F‚ÇÅ` and `F‚ÇÇ` are functors `J ‚•§ C` and `C` is a `V`-enriched category. -/
noncomputable abbrev enrichedHomœÄ (j : J) : enrichedHom V F‚ÇÅ F‚ÇÇ ‚ü∂ F‚ÇÅ.obj j ‚ü∂[V] F‚ÇÇ.obj j :=
  end_.œÄ _ j

@[reassoc]
lemma enrichedHom_condition {i j : J} (f : i ‚ü∂ j) :
    enrichedHomœÄ V F‚ÇÅ F‚ÇÇ i ‚â´ eHomWhiskerLeft V (F‚ÇÅ.obj i) (F‚ÇÇ.map f) =
    enrichedHomœÄ V F‚ÇÅ F‚ÇÇ j ‚â´ eHomWhiskerRight V (F‚ÇÅ.map f) (F‚ÇÇ.obj j) :=
  end_.condition (diagram V F‚ÇÅ F‚ÇÇ) f

@[reassoc]
lemma enrichedHom_condition' {i j : J} (f : i ‚ü∂ j) :
    enrichedHomœÄ V F‚ÇÅ F‚ÇÇ i ‚â´ (œÅ_ _).inv ‚â´
      _ ‚óÅ (eHomEquiv V) (F‚ÇÇ.map f) ‚â´ eComp V _ _ _  =
    enrichedHomœÄ V F‚ÇÅ F‚ÇÇ j ‚â´ (Œª_ _).inv ‚â´
      (eHomEquiv V) (F‚ÇÅ.map f) ‚ñ∑ _ ‚â´ eComp V _ _ _ :=
  end_.condition (diagram V F‚ÇÅ F‚ÇÇ) f

variable {F‚ÇÅ F‚ÇÇ}

/-- Given functors `F‚ÇÅ` and `F‚ÇÇ` in `J ‚•§ C`, where `C` is a `V`-enriched ordinary category,
this is the bijection `(F‚ÇÅ ‚ü∂ F‚ÇÇ) ‚âÉ (ùüô_ V ‚ü∂ enrichedHom V F‚ÇÅ F‚ÇÇ)`. -/
noncomputable def homEquiv : (F‚ÇÅ ‚ü∂ F‚ÇÇ) ‚âÉ (ùüô_ V ‚ü∂ enrichedHom V F‚ÇÅ F‚ÇÇ) where
  toFun œÑ := end_.lift (fun j ‚Ü¶ eHomEquiv V (œÑ.app j)) (fun i j f ‚Ü¶ by
    trans eHomEquiv V (œÑ.app i ‚â´ F‚ÇÇ.map f)
    ¬∑ dsimp
      simp only [eHomEquiv_comp, tensorHom_def_assoc, MonoidalCategory.whiskerRight_id,
        ‚Üê unitors_equal, assoc, Iso.inv_hom_id_assoc, eHomWhiskerLeft]
    ¬∑ dsimp
      simp only [‚Üê NatTrans.naturality, eHomEquiv_comp, tensorHom_def', id_whiskerLeft,
        assoc, Iso.inv_hom_id_assoc, eHomWhiskerRight])
  invFun g :=
    { app := fun j ‚Ü¶ (eHomEquiv V).symm (g ‚â´ end_.œÄ _ j)
      naturality := fun i j f ‚Ü¶ (eHomEquiv V).injective (by
        simp only [eHomEquiv_comp, Equiv.apply_symm_apply, Iso.cancel_iso_inv_left]
        conv_rhs =>
          rw [tensorHom_def_assoc, MonoidalCategory.whiskerRight_id_assoc, assoc,
            enrichedHom_condition' V F‚ÇÅ F‚ÇÇ f]
        conv_lhs =>
          rw [tensorHom_def'_assoc, MonoidalCategory.whiskerLeft_comp_assoc,
            id_whiskerLeft_assoc, id_whiskerLeft_assoc, Iso.inv_hom_id_assoc, unitors_equal]) }
  left_inv œÑ := by aesop
  right_inv g := by aesop

@[reassoc (attr := simp)]
lemma homEquiv_apply_œÄ (œÑ : F‚ÇÅ ‚ü∂ F‚ÇÇ) (j : J) :
    homEquiv V œÑ ‚â´ enrichedHomœÄ V _ _ j = eHomEquiv V (œÑ.app j) := by
  simp [homEquiv]

end

section

variable [HasEnrichedHom V F‚ÇÅ F‚ÇÅ]

/-- The identity for the `V`-enrichment of the category `J ‚•§ C`. -/
noncomputable def enrichedId : ùüô_ V ‚ü∂ enrichedHom V F‚ÇÅ F‚ÇÅ := homEquiv _ (ùüô F‚ÇÅ)

@[reassoc (attr := simp)]
lemma enrichedId_œÄ (j : J) : enrichedId V F‚ÇÅ ‚â´ end_.œÄ _ j = eId V (F‚ÇÅ.obj j) := by
  simp [enrichedId]

@[simp]
lemma homEquiv_id : homEquiv V (ùüô F‚ÇÅ) = enrichedId V F‚ÇÅ := rfl

end

section

variable [HasEnrichedHom V F‚ÇÅ F‚ÇÇ] [HasEnrichedHom V F‚ÇÇ F‚ÇÉ] [HasEnrichedHom V F‚ÇÅ F‚ÇÉ]

/-- The composition for the `V`-enrichment of the category `J ‚•§ C`. -/
noncomputable def enrichedComp : enrichedHom V F‚ÇÅ F‚ÇÇ ‚äó enrichedHom V F‚ÇÇ F‚ÇÉ ‚ü∂ enrichedHom V F‚ÇÅ F‚ÇÉ :=
  end_.lift (fun j ‚Ü¶ (end_.œÄ _ j ‚äó‚Çò end_.œÄ _ j) ‚â´ eComp V _ _ _) (fun i j f ‚Ü¶ by
    dsimp
    trans (end_.œÄ (diagram V F‚ÇÅ F‚ÇÇ) i ‚äó‚Çò end_.œÄ (diagram V F‚ÇÇ F‚ÇÉ) j) ‚â´
      (œÅ_ _).inv ‚ñ∑ _ ‚â´ (_ ‚óÅ (eHomEquiv V (F‚ÇÇ.map f))) ‚ñ∑ _ ‚â´ eComp V _ (F‚ÇÇ.obj i) _ ‚ñ∑ _ ‚â´
        eComp V _ (F‚ÇÇ.obj j) _
    ¬∑ have := end_.condition (diagram V F‚ÇÇ F‚ÇÉ) f
      dsimp [eHomWhiskerLeft, eHomWhiskerRight] at this ‚ä¢
      conv_lhs => rw [assoc, tensorHom_def_assoc]
      conv_rhs =>
        rw [tensorHom_def_assoc, whisker_assoc_assoc, e_assoc,
          triangle_assoc_comp_right_inv_assoc, ‚Üê MonoidalCategory.whiskerLeft_comp_assoc,
          ‚Üê MonoidalCategory.whiskerLeft_comp_assoc, ‚Üê MonoidalCategory.whiskerLeft_comp_assoc,
          assoc, assoc, ‚Üê this, MonoidalCategory.whiskerLeft_comp_assoc,
          MonoidalCategory.whiskerLeft_comp_assoc, MonoidalCategory.whiskerLeft_comp_assoc,
          ‚Üê e_assoc, whiskerLeft_rightUnitor_inv_assoc, associator_inv_naturality_right_assoc,
          Iso.hom_inv_id_assoc, whisker_exchange_assoc, MonoidalCategory.whiskerRight_id_assoc,
          Iso.inv_hom_id_assoc]
    ¬∑ have := end_.condition (diagram V F‚ÇÅ F‚ÇÇ) f
      dsimp [eHomWhiskerLeft, eHomWhiskerRight] at this ‚ä¢
      conv_lhs =>
        rw [tensorHom_def'_assoc, ‚Üê comp_whiskerRight_assoc,
          ‚Üê comp_whiskerRight_assoc, ‚Üê comp_whiskerRight_assoc,
          assoc, assoc, this, comp_whiskerRight_assoc, comp_whiskerRight_assoc,
          comp_whiskerRight_assoc, leftUnitor_inv_whiskerRight_assoc,
          ‚Üê associator_inv_naturality_left_assoc, ‚Üê e_assoc',
          Iso.inv_hom_id_assoc, ‚Üê whisker_exchange_assoc, id_whiskerLeft_assoc,
          Iso.inv_hom_id_assoc]
      conv_rhs => rw [assoc, tensorHom_def'_assoc])

@[reassoc (attr := simp)]
lemma enrichedComp_œÄ (j : J) :
    enrichedComp V F‚ÇÅ F‚ÇÇ F‚ÇÉ ‚â´ end_.œÄ _ j =
      (end_.œÄ (diagram V F‚ÇÅ F‚ÇÇ) j ‚äó‚Çò end_.œÄ (diagram V F‚ÇÇ F‚ÇÉ) j) ‚â´ eComp V _ _ _ := by
  simp [enrichedComp]

variable {F‚ÇÅ F‚ÇÇ F‚ÇÉ}

@[reassoc]
lemma homEquiv_comp (f : F‚ÇÅ ‚ü∂ F‚ÇÇ) (g : F‚ÇÇ ‚ü∂ F‚ÇÉ) :
    (homEquiv V) (f ‚â´ g) = (Œª_ (ùüô_ V)).inv ‚â´ ((homEquiv V) f ‚äó‚Çò (homEquiv V) g) ‚â´
    enrichedComp V F‚ÇÅ F‚ÇÇ F‚ÇÉ := by
  ext j
  simp only [homEquiv_apply_œÄ, NatTrans.comp_app, eHomEquiv_comp, assoc,
    enrichedComp_œÄ, Functor.op_obj, tensorHom_comp_tensorHom_assoc]

end

@[reassoc (attr := simp)]
lemma enriched_id_comp [HasEnrichedHom V F‚ÇÅ F‚ÇÅ] [HasEnrichedHom V F‚ÇÅ F‚ÇÇ] :
    (Œª_ (enrichedHom V F‚ÇÅ F‚ÇÇ)).inv ‚â´ enrichedId V F‚ÇÅ ‚ñ∑ enrichedHom V F‚ÇÅ F‚ÇÇ ‚â´
      enrichedComp V F‚ÇÅ F‚ÇÅ F‚ÇÇ = ùüô _ := by
  ext j
  rw [assoc, assoc, enrichedComp_œÄ, id_comp, tensorHom_def, assoc,
    ‚Üê comp_whiskerRight_assoc, enrichedId_œÄ, ‚Üê whisker_exchange_assoc,
    id_whiskerLeft, assoc, assoc, Iso.inv_hom_id_assoc]
  dsimp
  rw [e_id_comp, comp_id]

@[reassoc (attr := simp)]
lemma enriched_comp_id [HasEnrichedHom V F‚ÇÅ F‚ÇÇ] [HasEnrichedHom V F‚ÇÇ F‚ÇÇ] :
    (œÅ_ (enrichedHom V F‚ÇÅ F‚ÇÇ)).inv ‚â´ enrichedHom V F‚ÇÅ F‚ÇÇ ‚óÅ enrichedId V F‚ÇÇ ‚â´
      enrichedComp V F‚ÇÅ F‚ÇÇ F‚ÇÇ = ùüô _ := by
  ext j
  rw [assoc, assoc, enrichedComp_œÄ, id_comp, tensorHom_def', assoc,
    ‚Üê MonoidalCategory.whiskerLeft_comp_assoc, enrichedId_œÄ,
    whisker_exchange_assoc, MonoidalCategory.whiskerRight_id, assoc, assoc,
    Iso.inv_hom_id_assoc]
  dsimp
  rw [e_comp_id, comp_id]

@[reassoc]
lemma enriched_assoc [HasEnrichedHom V F‚ÇÅ F‚ÇÇ] [HasEnrichedHom V F‚ÇÅ F‚ÇÉ] [HasEnrichedHom V F‚ÇÅ F‚ÇÑ]
    [HasEnrichedHom V F‚ÇÇ F‚ÇÉ] [HasEnrichedHom V F‚ÇÇ F‚ÇÑ] [HasEnrichedHom V F‚ÇÉ F‚ÇÑ] :
    (Œ±_ (enrichedHom V F‚ÇÅ F‚ÇÇ) (enrichedHom V F‚ÇÇ F‚ÇÉ) (enrichedHom V F‚ÇÉ F‚ÇÑ)).inv ‚â´
      enrichedComp V F‚ÇÅ F‚ÇÇ F‚ÇÉ ‚ñ∑ enrichedHom V F‚ÇÉ F‚ÇÑ ‚â´ enrichedComp V F‚ÇÅ F‚ÇÉ F‚ÇÑ =
      enrichedHom V F‚ÇÅ F‚ÇÇ ‚óÅ enrichedComp V F‚ÇÇ F‚ÇÉ F‚ÇÑ ‚â´ enrichedComp V F‚ÇÅ F‚ÇÇ F‚ÇÑ := by
  ext j
  conv_lhs =>
    rw [assoc, assoc, enrichedComp_œÄ,
      tensorHom_def_assoc, ‚Üê comp_whiskerRight_assoc, enrichedComp_œÄ,
      comp_whiskerRight_assoc, ‚Üê whisker_exchange_assoc,
      ‚Üê whisker_exchange_assoc, ‚Üê tensorHom_def'_assoc, ‚Üê associator_inv_naturality_assoc]
  conv_rhs =>
    rw [assoc, enrichedComp_œÄ, tensorHom_def'_assoc, ‚Üê MonoidalCategory.whiskerLeft_comp_assoc,
      enrichedComp_œÄ, MonoidalCategory.whiskerLeft_comp_assoc, whisker_exchange_assoc,
      whisker_exchange_assoc, ‚Üê tensorHom_def_assoc]
  dsimp
  rw [e_assoc]

variable (J C)

/-- If `C` is a `V`-enriched ordinary category, and `C` has suitable limits,
then `J ‚•§ C` is also a `V`-enriched ordinary category. -/
noncomputable def enrichedOrdinaryCategory [‚àÄ (F‚ÇÅ F‚ÇÇ : J ‚•§ C), HasEnrichedHom V F‚ÇÅ F‚ÇÇ] :
    EnrichedOrdinaryCategory V (J ‚•§ C) where
  Hom F‚ÇÅ F‚ÇÇ := enrichedHom V F‚ÇÅ F‚ÇÇ
  id F := enrichedId V F
  comp F‚ÇÅ F‚ÇÇ F‚ÇÉ := enrichedComp V F‚ÇÅ F‚ÇÇ F‚ÇÉ
  assoc _ _ _ _ := enriched_assoc _ _ _ _ _
  homEquiv := homEquiv V
  homEquiv_id _ := homEquiv_id V _
  homEquiv_comp f g := homEquiv_comp V f g

variable {J C}

section

variable (G : K ‚•§ J) [HasEnrichedHom V F‚ÇÅ F‚ÇÇ]

variable {F‚ÇÅ F‚ÇÇ} in
/-- If `F‚ÇÅ` and `F‚ÇÇ` are functors `J ‚•§ C`, `G : K ‚•§ J`, and
`F‚ÇÅ'` and `F‚ÇÇ'` are functors `K ‚•§ C` that are respectively
isomorphic to `G ‚ãô F‚ÇÅ` and `G ‚ãô F‚ÇÇ`, then this is the
induced morphism `enrichedHom V F‚ÇÅ F‚ÇÇ ‚ü∂ enrichedHom V F‚ÇÅ' F‚ÇÇ'` in `V`
when `C` is a category enriched in `V`. -/
noncomputable abbrev precompEnrichedHom' {F‚ÇÅ' F‚ÇÇ' : K ‚•§ C}
    [HasEnrichedHom V F‚ÇÅ' F‚ÇÇ'] (e‚ÇÅ : G ‚ãô F‚ÇÅ ‚âÖ F‚ÇÅ') (e‚ÇÇ : G ‚ãô F‚ÇÇ ‚âÖ F‚ÇÇ') :
    enrichedHom V F‚ÇÅ F‚ÇÇ ‚ü∂ enrichedHom V F‚ÇÅ' F‚ÇÇ' :=
  end_.lift (fun x ‚Ü¶ enrichedHomœÄ V F‚ÇÅ F‚ÇÇ (G.obj x) ‚â´
    (eHomWhiskerRight _ (e‚ÇÅ.inv.app x) _ ‚â´ eHomWhiskerLeft _ _ (e‚ÇÇ.hom.app x)))
    (fun i j f ‚Ü¶ by
      dsimp
      rw [assoc, assoc, assoc, assoc, ‚Üê eHomWhiskerLeft_comp,
        ‚Üê eHom_whisker_exchange, ‚Üê e‚ÇÇ.hom.naturality f,
        eHomWhiskerLeft_comp_assoc]
      dsimp
      rw [enrichedHom_condition_assoc, eHom_whisker_exchange,
        eHom_whisker_exchange, ‚Üê eHomWhiskerRight_comp_assoc,
        ‚Üê eHomWhiskerRight_comp_assoc, NatTrans.naturality]
      dsimp)

/-- If `F‚ÇÅ` and `F‚ÇÇ` are functors `J ‚•§ C`, and `G : K ‚•§ J`,
then this is the induced morphism
`enrichedHom V F‚ÇÅ F‚ÇÇ ‚ü∂ enrichedHom V (G ‚ãô F‚ÇÅ) (G ‚ãô F‚ÇÇ)` in `V`
when `C` is a category enriched in `V`. -/
noncomputable abbrev precompEnrichedHom
    [HasEnrichedHom V (G ‚ãô F‚ÇÅ) (G ‚ãô F‚ÇÇ)] :
    enrichedHom V F‚ÇÅ F‚ÇÇ ‚ü∂ enrichedHom V (G ‚ãô F‚ÇÅ) (G ‚ãô F‚ÇÇ) :=
  precompEnrichedHom' V G (Iso.refl _) (Iso.refl _)

end


section

/-- Given functors `F‚ÇÅ` and `F‚ÇÇ` in `J ‚•§ C`, where `C` is a category enriched in `V`,
this condition allows the definition of `functorEnrichedHom V F‚ÇÅ F‚ÇÇ : J ‚•§ V`. -/
abbrev HasFunctorEnrichedHom :=
  ‚àÄ (j : J), HasEnrichedHom V (Under.forget j ‚ãô F‚ÇÅ) (Under.forget j ‚ãô F‚ÇÇ)

variable [HasFunctorEnrichedHom V F‚ÇÅ F‚ÇÇ]

instance {j j' : J} (f : j ‚ü∂ j') :
    HasEnrichedHom V (Under.map f ‚ãô Under.forget j ‚ãô F‚ÇÅ)
      (Under.map f ‚ãô Under.forget j ‚ãô F‚ÇÇ) :=
  inferInstanceAs (HasEnrichedHom V (Under.forget j' ‚ãô F‚ÇÅ) (Under.forget j' ‚ãô F‚ÇÇ))

/-- Given functors `F‚ÇÅ` and `F‚ÇÇ` in `J ‚•§ C`, where `C` is a category enriched in `V`,
this is the enriched hom functor from `F‚ÇÅ` to `F‚ÇÇ` in `J ‚•§ V`. -/
@[simps!]
noncomputable def functorEnrichedHom : J ‚•§ V where
  obj j := enrichedHom V (Under.forget j ‚ãô F‚ÇÅ) (Under.forget j ‚ãô F‚ÇÇ)
  map f := precompEnrichedHom' V (Under.map f) (Iso.refl _) (Iso.refl _)
  map_id X := by
    ext j
    -- this was produced by `simp?`
    simp only [diagram_obj_obj, Functor.comp_obj, Under.forget_obj, end_.lift_œÄ,
      Under.map_obj_right, Iso.refl_inv, NatTrans.id_app, eHomWhiskerRight_id, Iso.refl_hom,
      eHomWhiskerLeft_id, comp_id, id_comp]
    congr 1
    simp [Under.map, Comma.mapLeft]
    rfl
  map_comp f g := by
    ext j
    -- this was produced by `simp?`
    simp only [diagram_obj_obj, Functor.comp_obj, Under.forget_obj, end_.lift_œÄ,
      Under.map_obj_right, Iso.refl_inv, NatTrans.id_app, eHomWhiskerRight_id, Iso.refl_hom,
      eHomWhiskerLeft_id, comp_id, assoc]
    congr 1
    simp [Under.map, Comma.mapLeft]

variable [HasEnrichedHom V F‚ÇÅ F‚ÇÇ]

/-- The (limit) cone expressing that the limit of `functorEnrichedHom V F‚ÇÅ F‚ÇÇ`
is `enrichedHom V F‚ÇÅ F‚ÇÇ`. -/
@[simps]
noncomputable def coneFunctorEnrichedHom : Cone (functorEnrichedHom V F‚ÇÅ F‚ÇÇ) where
  pt := enrichedHom V F‚ÇÅ F‚ÇÇ
  œÄ := { app := fun j ‚Ü¶ precompEnrichedHom V F‚ÇÅ F‚ÇÇ (Under.forget j) }

namespace isLimitConeFunctorEnrichedHom

variable {V F‚ÇÅ F‚ÇÇ} (s : Cone (functorEnrichedHom V F‚ÇÅ F‚ÇÇ))

/-- Auxiliary definition for `Enriched.FunctorCategory.isLimitConeFunctorEnrichedHom`. -/
noncomputable def lift : s.pt ‚ü∂ enrichedHom V F‚ÇÅ F‚ÇÇ :=
  end_.lift (fun j ‚Ü¶ s.œÄ.app j ‚â´ enrichedHomœÄ V _ _ (Under.mk (ùüô j))) (fun j j' f ‚Ü¶ by
    dsimp
    rw [‚Üê s.w f, assoc, assoc, assoc]
    -- this was produced by `simp?`
    simp only [functorEnrichedHom_obj, functorEnrichedHom_map, end_.lift_œÄ_assoc, diagram_obj_obj,
      Functor.comp_obj, Under.forget_obj, Under.mk_right, Under.map_obj_right, Iso.refl_inv,
      NatTrans.id_app, eHomWhiskerRight_id, Iso.refl_hom, eHomWhiskerLeft_id, comp_id]
    have := enrichedHom_condition V (Under.forget j ‚ãô F‚ÇÅ) (Under.forget j ‚ãô F‚ÇÇ)
      (Under.homMk f : Under.mk (ùüô j) ‚ü∂ Under.mk f)
    dsimp at this
    rw [this]
    congr 3
    simp [Under.map, Comma.mapLeft]
    rfl)

lemma fac (j : J) : lift s ‚â´ (coneFunctorEnrichedHom V F‚ÇÅ F‚ÇÇ).œÄ.app j = s.œÄ.app j := by
  dsimp [coneFunctorEnrichedHom]
  ext k
  have := s.w k.hom
  dsimp at this
  -- this was produced by `simp? [lift, ‚Üê this]`
  simp only [diagram_obj_obj, Functor.comp_obj, Under.forget_obj, lift, functorEnrichedHom_obj,
    assoc, end_.lift_œÄ, Iso.refl_inv, NatTrans.id_app, eHomWhiskerRight_id, Iso.refl_hom,
    eHomWhiskerLeft_id, comp_id, ‚Üê this, Under.map_obj_right, Under.mk_right]
  congr
  simp [Under.map, Comma.mapLeft]
  rfl

end isLimitConeFunctorEnrichedHom

open isLimitConeFunctorEnrichedHom in
/-- The limit of `functorEnrichedHom V F‚ÇÅ F‚ÇÇ` is `enrichedHom V F‚ÇÅ F‚ÇÇ`. -/
noncomputable def isLimitConeFunctorEnrichedHom :
    IsLimit (coneFunctorEnrichedHom V F‚ÇÅ F‚ÇÇ) where
  lift := lift
  fac := fac
  uniq s m hm := by
    dsimp
    ext j
    simpa using ((hm j).trans (fac s j).symm) =‚â´ enrichedHomœÄ V _ _ (Under.mk (ùüô j))

end

/-- The identity for the `J ‚•§ V`-enrichment of the category `J ‚•§ C`. -/
@[simps]
noncomputable def functorEnrichedId [HasFunctorEnrichedHom V F‚ÇÅ F‚ÇÅ] :
    ùüô_ (J ‚•§ V) ‚ü∂ functorEnrichedHom V F‚ÇÅ F‚ÇÅ where
  app j := enrichedId V _

/-- The composition for the `J ‚•§ V`-enrichment of the category `J ‚•§ C`. -/
@[simps]
noncomputable def functorEnrichedComp [HasFunctorEnrichedHom V F‚ÇÅ F‚ÇÇ]
    [HasFunctorEnrichedHom V F‚ÇÇ F‚ÇÉ] [HasFunctorEnrichedHom V F‚ÇÅ F‚ÇÉ] :
    functorEnrichedHom V F‚ÇÅ F‚ÇÇ ‚äó functorEnrichedHom V F‚ÇÇ F‚ÇÉ ‚ü∂ functorEnrichedHom V F‚ÇÅ F‚ÇÉ where
  app j := enrichedComp V _ _ _
  naturality j j' f := by
    dsimp
    ext k
    dsimp
    rw [assoc, assoc, enrichedComp_œÄ]
    dsimp
    rw [tensorHom_comp_tensorHom_assoc]
    simp

@[reassoc (attr := simp)]
lemma functorEnriched_id_comp [HasFunctorEnrichedHom V F‚ÇÅ F‚ÇÇ] [HasFunctorEnrichedHom V F‚ÇÅ F‚ÇÅ] :
    (Œª_ (functorEnrichedHom V F‚ÇÅ F‚ÇÇ)).inv ‚â´
      functorEnrichedId V F‚ÇÅ ‚ñ∑ functorEnrichedHom V F‚ÇÅ F‚ÇÇ ‚â´
        functorEnrichedComp V F‚ÇÅ F‚ÇÅ F‚ÇÇ = ùüô (functorEnrichedHom V F‚ÇÅ F‚ÇÇ) := by cat_disch

@[reassoc (attr := simp)]
lemma functorEnriched_comp_id [HasFunctorEnrichedHom V F‚ÇÅ F‚ÇÇ] [HasFunctorEnrichedHom V F‚ÇÇ F‚ÇÇ] :
    (œÅ_ (functorEnrichedHom V F‚ÇÅ F‚ÇÇ)).inv ‚â´
      functorEnrichedHom V F‚ÇÅ F‚ÇÇ ‚óÅ functorEnrichedId V F‚ÇÇ ‚â´
        functorEnrichedComp V F‚ÇÅ F‚ÇÇ F‚ÇÇ = ùüô (functorEnrichedHom V F‚ÇÅ F‚ÇÇ) := by cat_disch

@[reassoc]
lemma functorEnriched_assoc [HasFunctorEnrichedHom V F‚ÇÅ F‚ÇÇ] [HasFunctorEnrichedHom V F‚ÇÇ F‚ÇÉ]
    [HasFunctorEnrichedHom V F‚ÇÉ F‚ÇÑ] [HasFunctorEnrichedHom V F‚ÇÅ F‚ÇÉ]
    [HasFunctorEnrichedHom V F‚ÇÇ F‚ÇÑ] [HasFunctorEnrichedHom V F‚ÇÅ F‚ÇÑ] :
    (Œ±_ _ _ _).inv ‚â´ functorEnrichedComp V F‚ÇÅ F‚ÇÇ F‚ÇÉ ‚ñ∑ functorEnrichedHom V F‚ÇÉ F‚ÇÑ ‚â´
      functorEnrichedComp V F‚ÇÅ F‚ÇÉ F‚ÇÑ =
        functorEnrichedHom V F‚ÇÅ F‚ÇÇ ‚óÅ functorEnrichedComp V F‚ÇÇ F‚ÇÉ F‚ÇÑ ‚â´
          functorEnrichedComp V F‚ÇÅ F‚ÇÇ F‚ÇÑ := by
  ext j
  dsimp
  rw [enriched_assoc]

variable (J C) in
/-- If `C` is a `V`-enriched ordinary category, and `C` has suitable limits,
then `J ‚•§ C` is also a `J ‚•§ V`-enriched ordinary category. -/
noncomputable def functorEnrichedCategory
    [‚àÄ (F‚ÇÅ F‚ÇÇ : J ‚•§ C), HasFunctorEnrichedHom V F‚ÇÅ F‚ÇÇ] :
    EnrichedCategory (J ‚•§ V) (J ‚•§ C) where
  Hom F‚ÇÅ F‚ÇÇ := functorEnrichedHom V F‚ÇÅ F‚ÇÇ
  id F := functorEnrichedId V F
  comp F‚ÇÅ F‚ÇÇ F‚ÇÉ := functorEnrichedComp V F‚ÇÅ F‚ÇÇ F‚ÇÉ
  assoc F‚ÇÅ F‚ÇÇ F‚ÇÉ F‚ÇÑ := functorEnriched_assoc V F‚ÇÅ F‚ÇÇ F‚ÇÉ F‚ÇÑ

variable {F‚ÇÅ F‚ÇÇ} in
/-- Given functors `F‚ÇÅ` and `F‚ÇÇ` in `J ‚•§ C`, where `C` is a `V`-enriched ordinary category,
this is the bijection `(F‚ÇÅ ‚ü∂ F‚ÇÇ) ‚âÉ (ùüô_ (J ‚•§ V) ‚ü∂ functorEnrichedHom V F‚ÇÅ F‚ÇÇ)`. -/
@[simps! apply_app]
noncomputable def functorHomEquiv [HasFunctorEnrichedHom V F‚ÇÅ F‚ÇÇ] [HasEnrichedHom V F‚ÇÅ F‚ÇÇ] :
    (F‚ÇÅ ‚ü∂ F‚ÇÇ) ‚âÉ (ùüô_ (J ‚•§ V) ‚ü∂ functorEnrichedHom V F‚ÇÅ F‚ÇÇ) :=
  (homEquiv V).trans (isLimitConeFunctorEnrichedHom V F‚ÇÅ F‚ÇÇ).homEquiv

lemma functorHomEquiv_id [HasFunctorEnrichedHom V F‚ÇÅ F‚ÇÅ] [HasEnrichedHom V F‚ÇÅ F‚ÇÅ] :
    (functorHomEquiv V) (ùüô F‚ÇÅ) = functorEnrichedId V F‚ÇÅ := by cat_disch

variable {F‚ÇÅ F‚ÇÇ F‚ÇÉ} in
lemma functorHomEquiv_comp [HasFunctorEnrichedHom V F‚ÇÅ F‚ÇÇ] [HasEnrichedHom V F‚ÇÅ F‚ÇÇ]
    [HasFunctorEnrichedHom V F‚ÇÇ F‚ÇÉ] [HasEnrichedHom V F‚ÇÇ F‚ÇÉ]
    [HasFunctorEnrichedHom V F‚ÇÅ F‚ÇÉ] [HasEnrichedHom V F‚ÇÅ F‚ÇÉ]
    (f : F‚ÇÅ ‚ü∂ F‚ÇÇ) (g : F‚ÇÇ ‚ü∂ F‚ÇÉ) :
    (functorHomEquiv V) (f ‚â´ g) = (Œª_ (ùüô_ (J ‚•§ V))).inv ‚â´
      ((functorHomEquiv V) f ‚äó‚Çò (functorHomEquiv V) g) ‚â´ functorEnrichedComp V F‚ÇÅ F‚ÇÇ F‚ÇÉ := by
  ext j
  dsimp
  ext k
  rw [homEquiv_comp, assoc, assoc, assoc, assoc, assoc, end_.lift_œÄ, enrichedComp_œÄ]
  simp [tensorHom_comp_tensorHom_assoc]

attribute [local instance] functorEnrichedCategory

variable (J C) in
/-- If `C` is a `V`-enriched ordinary category, and `C` has suitable limits,
then `J ‚•§ C` is also a `J ‚•§ V`-enriched ordinary category. -/
noncomputable def functorEnrichedOrdinaryCategory
    [‚àÄ (F‚ÇÅ F‚ÇÇ : J ‚•§ C), HasFunctorEnrichedHom V F‚ÇÅ F‚ÇÇ]
    [‚àÄ (F‚ÇÅ F‚ÇÇ : J ‚•§ C), HasEnrichedHom V F‚ÇÅ F‚ÇÇ] :
    EnrichedOrdinaryCategory (J ‚•§ V) (J ‚•§ C) where
  homEquiv := functorHomEquiv V
  homEquiv_id F := functorHomEquiv_id V F
  homEquiv_comp f g := functorHomEquiv_comp V f g

end CategoryTheory.Enriched.FunctorCategory
