/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Enriched.Ordinary
import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.Limits.Shapes.End

/-!
# Functor categories are enriched

If `C` is a `V`-enriched ordinary category, then `J â¥¤ C` is also
a `V`-enriched ordinary category, provided `C` has suitable limits.

-/

universe vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ uâ‚ uâ‚‚ uâ‚ƒ uâ‚„

namespace CategoryTheory.Enriched.FunctorCategory

open Category MonoidalCategory Limits

variable (V : Type uâ‚) [Category.{vâ‚} V] [MonoidalCategory V]
  {C : Type uâ‚‚} [Category.{vâ‚‚} C]
  {J : Type uâ‚ƒ} [Category.{vâ‚ƒ} J]
  {K : Type uâ‚„} [Category.{vâ‚„} K]
  [EnrichedOrdinaryCategory V C]

variable (Fâ‚ Fâ‚‚ Fâ‚ƒ Fâ‚„ : J â¥¤ C)

/-- Given two functors `Fâ‚` and `Fâ‚‚` from a category `J` to a `V`-enriched
ordinary category `C`, this is the diagram `Jáµ’áµ– â¥¤ J â¥¤ V` whose end shall be
the `V`-morphisms in `J â¥¤ V` from `Fâ‚` to `Fâ‚‚`. -/
@[simps!]
def diagram : Jáµ’áµ– â¥¤ J â¥¤ V := Fâ‚.op â‹™ eHomFunctor V C â‹™ (whiskeringLeft J C V).obj Fâ‚‚

/-- The condition that the end `diagram V Fâ‚ Fâ‚‚` exists, see `enrichedHom`. -/
abbrev HasEnrichedHom := HasEnd (diagram V Fâ‚ Fâ‚‚)

section

variable [HasEnrichedHom V Fâ‚ Fâ‚‚]

/-- The `V`-enriched hom from `Fâ‚` to `Fâ‚‚` when `Fâ‚` and `Fâ‚‚` are functors `J â¥¤ C`
and `C` is a `V`-enriched category. -/
noncomputable abbrev enrichedHom : V := end_ (diagram V Fâ‚ Fâ‚‚)

/-- The projection `enrichedHom V Fâ‚ Fâ‚‚ âŸ¶ Fâ‚.obj j âŸ¶[V] Fâ‚‚.obj j` in the category `V`
for any `j : J` when `Fâ‚` and `Fâ‚‚` are functors `J â¥¤ C` and `C` is a `V`-enriched category. -/
noncomputable abbrev enrichedHomÏ€ (j : J) : enrichedHom V Fâ‚ Fâ‚‚ âŸ¶ Fâ‚.obj j âŸ¶[V] Fâ‚‚.obj j :=
  end_.Ï€ _ j

@[reassoc]
lemma enrichedHom_condition {i j : J} (f : i âŸ¶ j) :
    enrichedHomÏ€ V Fâ‚ Fâ‚‚ i â‰« (Ï_ _).inv â‰«
      _ â— (eHomEquiv V) (Fâ‚‚.map f) â‰« eComp V _ _ _  =
    enrichedHomÏ€ V Fâ‚ Fâ‚‚ j â‰« (Î»_ _).inv â‰«
      (eHomEquiv V) (Fâ‚.map f) â–· _ â‰« eComp V _ _ _ :=
  end_.condition (diagram V Fâ‚ Fâ‚‚) f

variable {Fâ‚ Fâ‚‚}

/-- Given functors `Fâ‚` and `Fâ‚‚` in `J â¥¤ C`, where `C` is a `V`-enriched ordinary category,
this is the isomorphism `(Fâ‚ âŸ¶ Fâ‚‚) â‰ƒ (ğŸ™_ V âŸ¶ enrichedHom V Fâ‚ Fâ‚‚)` in the category `V`. -/
noncomputable def homEquiv : (Fâ‚ âŸ¶ Fâ‚‚) â‰ƒ (ğŸ™_ V âŸ¶ enrichedHom V Fâ‚ Fâ‚‚) where
  toFun Ï„ := end_.lift (fun j â†¦ eHomEquiv V (Ï„.app j)) (fun i j f â†¦ by
    trans eHomEquiv V (Ï„.app i â‰« Fâ‚‚.map f)
    Â· dsimp
      simp only [eHomEquiv_comp, tensorHom_def_assoc, MonoidalCategory.whiskerRight_id,
        â† unitors_equal, assoc, Iso.inv_hom_id_assoc, eHomWhiskerLeft]
    Â· dsimp
      simp only [â† NatTrans.naturality, eHomEquiv_comp, tensorHom_def', id_whiskerLeft,
        assoc, Iso.inv_hom_id_assoc, eHomWhiskerRight])
  invFun g :=
    { app := fun j â†¦ (eHomEquiv V).symm (g â‰« end_.Ï€ _ j)
      naturality := fun i j f â†¦ (eHomEquiv V).injective (by
        dsimp
        simp only [eHomEquiv_comp, Equiv.apply_symm_apply, Iso.cancel_iso_inv_left]
        conv_rhs =>
          rw [tensorHom_def_assoc, MonoidalCategory.whiskerRight_id_assoc, assoc,
            enrichedHom_condition V Fâ‚ Fâ‚‚ f]
        conv_lhs =>
          rw [tensorHom_def'_assoc, MonoidalCategory.whiskerLeft_comp_assoc,
            id_whiskerLeft_assoc, id_whiskerLeft_assoc, Iso.inv_hom_id_assoc, unitors_equal]) }
  left_inv Ï„ := by aesop
  right_inv g := by aesop

@[reassoc (attr := simp)]
lemma homEquiv_apply_Ï€ (Ï„ : Fâ‚ âŸ¶ Fâ‚‚) (j : J) :
    homEquiv V Ï„ â‰« enrichedHomÏ€ V _ _ j = eHomEquiv V (Ï„.app j) := by
  simp [homEquiv]

end

section

variable [HasEnrichedHom V Fâ‚ Fâ‚]

/-- The identity for the `V`-enrichment of the category `J â¥¤ C` over `V`. -/
noncomputable def enrichedId : ğŸ™_ V âŸ¶ enrichedHom V Fâ‚ Fâ‚ := homEquiv _ (ğŸ™ Fâ‚)

@[reassoc (attr := simp)]
lemma enrichedId_Ï€ (j : J) : enrichedId V Fâ‚ â‰« end_.Ï€ _ j = eId V (Fâ‚.obj j) := by
  simp [enrichedId]

@[simp]
lemma homEquiv_id : homEquiv V (ğŸ™ Fâ‚) = enrichedId V Fâ‚ := rfl

end

section

variable [HasEnrichedHom V Fâ‚ Fâ‚‚] [HasEnrichedHom V Fâ‚‚ Fâ‚ƒ] [HasEnrichedHom V Fâ‚ Fâ‚ƒ]

/-- The composition for the `V`-enrichment of the category `J â¥¤ C` over `V`. -/
noncomputable def enrichedComp : enrichedHom V Fâ‚ Fâ‚‚ âŠ— enrichedHom V Fâ‚‚ Fâ‚ƒ âŸ¶ enrichedHom V Fâ‚ Fâ‚ƒ :=
  end_.lift (fun j â†¦ (end_.Ï€ _ j âŠ— end_.Ï€ _ j) â‰« eComp V _ _ _) (fun i j f â†¦ by
    dsimp
    trans (end_.Ï€ (diagram V Fâ‚ Fâ‚‚) i âŠ— end_.Ï€ (diagram V Fâ‚‚ Fâ‚ƒ) j) â‰«
      (Ï_ _).inv â–· _ â‰« (_ â— (eHomEquiv V (Fâ‚‚.map f))) â–· _ â‰« eComp V _ (Fâ‚‚.obj i) _ â–· _ â‰«
        eComp V _ (Fâ‚‚.obj j) _
    Â· have := end_.condition (diagram V Fâ‚‚ Fâ‚ƒ) f
      dsimp [eHomWhiskerLeft, eHomWhiskerRight] at this âŠ¢
      conv_lhs => rw [assoc, tensorHom_def_assoc]
      conv_rhs =>
        rw [tensorHom_def_assoc, whisker_assoc_assoc, e_assoc,
          triangle_assoc_comp_right_inv_assoc, â† MonoidalCategory.whiskerLeft_comp_assoc,
          â† MonoidalCategory.whiskerLeft_comp_assoc, â† MonoidalCategory.whiskerLeft_comp_assoc,
          assoc, assoc, â† this, MonoidalCategory.whiskerLeft_comp_assoc,
          MonoidalCategory.whiskerLeft_comp_assoc, MonoidalCategory.whiskerLeft_comp_assoc,
          â† e_assoc, whiskerLeft_rightUnitor_inv_assoc, associator_inv_naturality_right_assoc,
          Iso.hom_inv_id_assoc, whisker_exchange_assoc, MonoidalCategory.whiskerRight_id_assoc,
          Iso.inv_hom_id_assoc]
    Â· have := end_.condition (diagram V Fâ‚ Fâ‚‚) f
      dsimp [eHomWhiskerLeft, eHomWhiskerRight] at this âŠ¢
      conv_lhs =>
        rw [tensorHom_def'_assoc, â† comp_whiskerRight_assoc,
          â† comp_whiskerRight_assoc, â† comp_whiskerRight_assoc,
          assoc, assoc, this, comp_whiskerRight_assoc, comp_whiskerRight_assoc,
          comp_whiskerRight_assoc, leftUnitor_inv_whiskerRight_assoc,
          â† associator_inv_naturality_left_assoc, â† e_assoc',
          Iso.inv_hom_id_assoc, â† whisker_exchange_assoc, id_whiskerLeft_assoc,
          Iso.inv_hom_id_assoc]
      conv_rhs => rw [assoc, tensorHom_def'_assoc])

@[reassoc (attr := simp)]
lemma enrichedComp_Ï€ (j : J) :
    enrichedComp V Fâ‚ Fâ‚‚ Fâ‚ƒ â‰« end_.Ï€ _ j =
      (end_.Ï€ (diagram V Fâ‚ Fâ‚‚) j âŠ— end_.Ï€ (diagram V Fâ‚‚ Fâ‚ƒ) j) â‰« eComp V _ _ _ := by
  simp [enrichedComp]

variable {Fâ‚ Fâ‚‚ Fâ‚ƒ}

@[reassoc]
lemma homEquiv_comp (f : Fâ‚ âŸ¶ Fâ‚‚) (g : Fâ‚‚ âŸ¶ Fâ‚ƒ) :
    (homEquiv V) (f â‰« g) = (Î»_ (ğŸ™_ V)).inv â‰« ((homEquiv V) f âŠ— (homEquiv V) g) â‰«
    enrichedComp V Fâ‚ Fâ‚‚ Fâ‚ƒ := by
  ext j
  simp only [homEquiv_apply_Ï€, NatTrans.comp_app, eHomEquiv_comp, assoc,
    enrichedComp_Ï€, Functor.op_obj, â† tensor_comp_assoc]

end

@[reassoc (attr := simp)]
lemma enriched_id_comp [HasEnrichedHom V Fâ‚ Fâ‚] [HasEnrichedHom V Fâ‚ Fâ‚‚] :
    (Î»_ (enrichedHom V Fâ‚ Fâ‚‚)).inv â‰« enrichedId V Fâ‚ â–· enrichedHom V Fâ‚ Fâ‚‚ â‰«
      enrichedComp V Fâ‚ Fâ‚ Fâ‚‚ = ğŸ™ _ := by
  ext j
  rw [assoc, assoc, enrichedComp_Ï€, id_comp, tensorHom_def, assoc,
    â† comp_whiskerRight_assoc, enrichedId_Ï€, â† whisker_exchange_assoc,
    id_whiskerLeft, assoc, assoc, Iso.inv_hom_id_assoc]
  dsimp
  rw [e_id_comp, comp_id]

@[reassoc (attr := simp)]
lemma enriched_comp_id [HasEnrichedHom V Fâ‚ Fâ‚‚] [HasEnrichedHom V Fâ‚‚ Fâ‚‚] :
    (Ï_ (enrichedHom V Fâ‚ Fâ‚‚)).inv â‰« enrichedHom V Fâ‚ Fâ‚‚ â— enrichedId V Fâ‚‚ â‰«
      enrichedComp V Fâ‚ Fâ‚‚ Fâ‚‚ = ğŸ™ _ := by
  ext j
  rw [assoc, assoc, enrichedComp_Ï€, id_comp, tensorHom_def', assoc,
    â† MonoidalCategory.whiskerLeft_comp_assoc, enrichedId_Ï€,
    whisker_exchange_assoc, MonoidalCategory.whiskerRight_id, assoc, assoc,
    Iso.inv_hom_id_assoc]
  dsimp
  rw [e_comp_id, comp_id]

@[reassoc]
lemma enriched_assoc [HasEnrichedHom V Fâ‚ Fâ‚‚] [HasEnrichedHom V Fâ‚ Fâ‚ƒ] [HasEnrichedHom V Fâ‚ Fâ‚„]
    [HasEnrichedHom V Fâ‚‚ Fâ‚ƒ] [HasEnrichedHom V Fâ‚‚ Fâ‚„] [HasEnrichedHom V Fâ‚ƒ Fâ‚„] :
    (Î±_ (enrichedHom V Fâ‚ Fâ‚‚) (enrichedHom V Fâ‚‚ Fâ‚ƒ) (enrichedHom V Fâ‚ƒ Fâ‚„)).inv â‰«
      enrichedComp V Fâ‚ Fâ‚‚ Fâ‚ƒ â–· enrichedHom V Fâ‚ƒ Fâ‚„ â‰« enrichedComp V Fâ‚ Fâ‚ƒ Fâ‚„ =
      enrichedHom V Fâ‚ Fâ‚‚ â— enrichedComp V Fâ‚‚ Fâ‚ƒ Fâ‚„ â‰« enrichedComp V Fâ‚ Fâ‚‚ Fâ‚„ := by
  ext j
  conv_lhs =>
    rw [assoc, assoc, enrichedComp_Ï€,
      tensorHom_def_assoc, â† comp_whiskerRight_assoc, enrichedComp_Ï€,
      comp_whiskerRight_assoc, â† whisker_exchange_assoc,
      â† whisker_exchange_assoc, â† tensorHom_def'_assoc, â† associator_inv_naturality_assoc]
  conv_rhs =>
    rw [assoc, enrichedComp_Ï€, tensorHom_def'_assoc, â† MonoidalCategory.whiskerLeft_comp_assoc,
      enrichedComp_Ï€, MonoidalCategory.whiskerLeft_comp_assoc, whisker_exchange_assoc,
      whisker_exchange_assoc, â† tensorHom_def_assoc]
  dsimp
  rw [e_assoc]

variable (J C)

/-- If `C` is a `V`-enriched ordinary category, and `C` has suitable limits,
then `J â¥¤ C` is also a `V`-enriched ordinary category. -/
noncomputable def enrichedOrdinaryCategory [âˆ€ (Fâ‚ Fâ‚‚ : J â¥¤ C), HasEnrichedHom V Fâ‚ Fâ‚‚] :
    EnrichedOrdinaryCategory V (J â¥¤ C) where
  Hom Fâ‚ Fâ‚‚ := enrichedHom V Fâ‚ Fâ‚‚
  id F := enrichedId V F
  comp Fâ‚ Fâ‚‚ Fâ‚ƒ := enrichedComp V Fâ‚ Fâ‚‚ Fâ‚ƒ
  assoc _ _ _ _ := enriched_assoc _ _ _ _ _
  homEquiv := homEquiv V
  homEquiv_id _ := homEquiv_id V _
  homEquiv_comp f g := homEquiv_comp V f g

variable {J C}

section

variable (G : K â¥¤ J) [HasEnrichedHom V Fâ‚ Fâ‚‚]
  [HasEnrichedHom V (G â‹™ Fâ‚) (G â‹™ Fâ‚‚)]

noncomputable def precompEnrichedHom :
    enrichedHom V Fâ‚ Fâ‚‚ âŸ¶ enrichedHom V (G â‹™ Fâ‚) (G â‹™ Fâ‚‚) :=
  end_.lift (fun _ â†¦ enrichedHomÏ€ _ _ _ _)
    (fun _ _ f â†¦ enrichedHom_condition V Fâ‚ Fâ‚‚ (G.map f))

@[reassoc (attr := simp)]
lemma precompEnrichedHom_Ï€ (k : K) :
    precompEnrichedHom V Fâ‚ Fâ‚‚ G â‰« enrichedHomÏ€ _ _ _ k = enrichedHomÏ€ _ _ _ (G.obj k) := by
  apply end_.lift_Ï€

end

section

abbrev HasPresheafEnrichedHom :=
  âˆ€ (j : J), HasEnrichedHom V (Under.forget j â‹™ Fâ‚) (Under.forget j â‹™ Fâ‚‚)

variable [HasPresheafEnrichedHom V Fâ‚ Fâ‚‚]

instance {j j' : J} (f : j âŸ¶ j') :
    HasEnrichedHom V (Under.map f â‹™ Under.forget j â‹™ Fâ‚)
      (Under.map f â‹™ Under.forget j â‹™ Fâ‚‚) :=
  inferInstanceAs (HasEnrichedHom V (Under.forget j' â‹™ Fâ‚) (Under.forget j' â‹™ Fâ‚‚))

@[simps!]
noncomputable def presheafEnrichedHom : J â¥¤ V where
  obj j := enrichedHom V (Under.forget j â‹™ Fâ‚) (Under.forget j â‹™ Fâ‚‚)
  map f := precompEnrichedHom V (Under.forget _ â‹™ Fâ‚) (Under.forget _ â‹™ Fâ‚‚) (Under.map f)
  map_id X := by
    dsimp
    ext j
    rw [precompEnrichedHom_Ï€, id_comp]
    congr 1
    simp [Under.map, Comma.mapLeft]
    rfl
  map_comp f g := by
    dsimp
    ext j
    rw [assoc, precompEnrichedHom_Ï€]
    erw [precompEnrichedHom_Ï€, precompEnrichedHom_Ï€]
    congr 1
    simp [Under.map, Comma.mapLeft]

variable [HasEnrichedHom V Fâ‚ Fâ‚‚]

@[simps pt]
noncomputable def conePresheafEnrichedHom : Cone (presheafEnrichedHom V Fâ‚ Fâ‚‚) where
  pt := enrichedHom V Fâ‚ Fâ‚‚
  Ï€ :=
    { app := fun j â†¦ precompEnrichedHom V Fâ‚ Fâ‚‚ (Under.forget j)
      naturality := fun j j' f â†¦ by
        dsimp
        rw [id_comp]
        ext k
        rw [assoc, precompEnrichedHom_Ï€]
        erw [precompEnrichedHom_Ï€]
        rw [precompEnrichedHom_Ï€]
        rfl }

namespace isLimitConePresheafEnrichedHom

variable {V Fâ‚ Fâ‚‚} (s : Cone (presheafEnrichedHom V Fâ‚ Fâ‚‚))

noncomputable def lift : s.pt âŸ¶ enrichedHom V Fâ‚ Fâ‚‚ :=
  end_.lift (fun j â†¦ s.Ï€.app j â‰« enrichedHomÏ€ V _ _ (Under.mk (ğŸ™ j))) (fun j j' f â†¦ by
    dsimp
    rw [â† s.w f, assoc, assoc, assoc]
    dsimp [presheafEnrichedHom]
    erw [precompEnrichedHom_Ï€_assoc,
      enrichedHom_condition V (Under.forget j â‹™ Fâ‚) (Under.forget j â‹™ Fâ‚‚)
      (Under.homMk f : Under.mk (ğŸ™ j) âŸ¶ Under.mk f)]
    congr 3
    simp [Under.map, Comma.mapLeft]
    rfl)

lemma fac (j : J) : lift s â‰« (conePresheafEnrichedHom V Fâ‚ Fâ‚‚).Ï€.app j = s.Ï€.app j := by
  dsimp [conePresheafEnrichedHom]
  ext k
  rw [assoc]
  erw [end_.lift_Ï€, end_.lift_Ï€, â† s.w k.hom]
  rw [assoc]
  erw [precompEnrichedHom_Ï€]
  congr
  simp [Under.map, Comma.mapLeft]
  rfl

end isLimitConePresheafEnrichedHom

open isLimitConePresheafEnrichedHom in
noncomputable def isLimitConePresheafEnrichedHom :
    IsLimit (conePresheafEnrichedHom V Fâ‚ Fâ‚‚) where
  lift := lift
  fac := fac
  uniq s m hm := by
    dsimp
    ext j
    have := ((hm j).trans (fac s j).symm) =â‰« enrichedHomÏ€ V _ _ (Under.mk (ğŸ™ j))
    dsimp [conePresheafEnrichedHom] at this
    rw [assoc, assoc, precompEnrichedHom_Ï€] at this
    exact this

end

end CategoryTheory.Enriched.FunctorCategory
