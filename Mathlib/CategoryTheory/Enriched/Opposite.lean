/-
Copyright (c) 2024 Daniel Carranza. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Daniel Carranza
-/
import Mathlib.CategoryTheory.Enriched.Ordinary.Basic
import Mathlib.CategoryTheory.Monoidal.Braided.Basic

/-!

# The opposite category of an enriched category

When a monoidal category `V` is braided, we may define the opposite `V`-category of a
`V`-category. The symmetry map is required to define the composition morphism.

This file constructs the opposite `V`-category as an instance on the type `C·µí·µñ` and constructs an
equivalence between
* `ForgetEnrichment V (C·µí·µñ)`, the underlying category of the `V`-category `C·µí·µñ`; and
* `(ForgetEnrichment V C)·µí·µñ`, the opposite category of the underlying category of `C`.
We also show that if `C` is an enriched ordinary category (i.e. a category enriched in `V`
equipped with an identification `(X ‚ü∂ Y) ‚âÉ (ùüô_ V ‚ü∂ (X ‚ü∂[V] Y))`) then `C·µí·µñ` is again
an enriched ordinary category.

-/

universe v‚ÇÅ u‚ÇÅ v u

namespace CategoryTheory

open MonoidalCategory BraidedCategory

variable (V : Type u‚ÇÅ) [Category.{v‚ÇÅ} V] [MonoidalCategory V] [BraidedCategory V]

section

variable (C : Type u) [EnrichedCategory V C]

/-- For a `V`-category `C`, construct the opposite `V`-category structure on the type `C·µí·µñ`
using the braiding in `V`. -/
instance EnrichedCategory.opposite : EnrichedCategory V C·µí·µñ where
  Hom y x := EnrichedCategory.Hom x.unop y.unop
  id x := EnrichedCategory.id x.unop
  comp z y x := (Œ≤_ _ _).hom ‚â´ EnrichedCategory.comp (x.unop) (y.unop) (z.unop)
  id_comp _ _ := by
    simp only [braiding_naturality_left_assoc, braiding_tensorUnit_left,
      Category.assoc, Iso.inv_hom_id_assoc]
    exact EnrichedCategory.comp_id _ _
  comp_id _ _ := by
    simp only [braiding_naturality_right_assoc, braiding_tensorUnit_right,
      Category.assoc, Iso.inv_hom_id_assoc]
    exact EnrichedCategory.id_comp _ _
  assoc _ _ _ _ := by
    simp only [braiding_naturality_left_assoc,
      MonoidalCategory.whiskerLeft_comp, Category.assoc]
    rw [‚Üê EnrichedCategory.assoc]
    simp only [braiding_tensor_left_hom, Category.assoc, Iso.inv_hom_id_assoc,
      braiding_naturality_right_assoc, braiding_tensor_right_hom]

end

/-- Unfold the definition of composition in the enriched opposite category. -/
@[reassoc]
lemma eComp_op_eq {C : Type u} [EnrichedCategory V C] (x y z : C·µí·µñ) :
    eComp V z y x = (Œ≤_ _ _).hom ‚â´ eComp V x.unop y.unop z.unop :=
  rfl

/-- When composing a tensor product of morphisms with the `V`-composition morphism in `C·µí·µñ`,
this re-writes the `V`-composition to be in `C` and moves the braiding to the left. -/
@[reassoc]
lemma tensorHom_eComp_op_eq {C : Type u} [EnrichedCategory V C] {x y z : C·µí·µñ} {v w : V}
    (f : v ‚ü∂ EnrichedCategory.Hom z y) (g : w ‚ü∂ EnrichedCategory.Hom y x) :
    (f ‚äó‚Çò g) ‚â´ eComp V z y x = (Œ≤_ v w).hom ‚â´ (g ‚äó‚Çò f) ‚â´ eComp V x.unop y.unop z.unop := by
  rw [eComp_op_eq]
  exact braiding_naturality_assoc f g _

-- This section establishes the equivalence on underlying categories
section

open ForgetEnrichment

variable (C : Type u) [EnrichedCategory V C]

/-- The functor going from the underlying category of the enriched category `C·µí·µñ`
to the opposite of the underlying category of the enriched category `C`. -/
def forgetEnrichmentOppositeEquivalence.functor :
    ForgetEnrichment V C·µí·µñ ‚•§ (ForgetEnrichment V C)·µí·µñ where
  obj x := x
  map {x y} f := f.op
  map_comp {x y z} f g := by
    have : (f ‚â´ g) = homTo V (f ‚â´ g) := rfl
    rw [this, forgetEnrichment_comp, Category.assoc, tensorHom_eComp_op_eq,
      leftUnitor_inv_braiding_assoc, ‚Üê unitors_inv_equal, ‚Üê Category.assoc]
    congr 1

/-- The functor going from the opposite of the underlying category of the enriched category `C`
to the underlying category of the enriched category `C·µí·µñ`. -/
def forgetEnrichmentOppositeEquivalence.inverse :
    (ForgetEnrichment V C)·µí·µñ ‚•§ ForgetEnrichment V C·µí·µñ where
  obj x := x
  map {x y} f := f.unop
  map_comp {x y z} f g := by
    have : g.unop ‚â´ f.unop = homTo V (g.unop ‚â´ f.unop) := rfl
    dsimp
    rw [this, forgetEnrichment_comp, Category.assoc, unitors_inv_equal,
      ‚Üê leftUnitor_inv_braiding_assoc]
    have : (Œ≤_ _ _).hom ‚â´ (homTo V g.unop ‚äó‚Çò homTo V f.unop) ‚â´
      eComp V (¬´to¬ª V z.unop) (¬´to¬ª V y.unop) (¬´to¬ª V x.unop) =
      ((homTo V f.unop) ‚äó‚Çò (homTo V g.unop)) ‚â´ eComp V x y z := (tensorHom_eComp_op_eq V _ _).symm
    rw [this, ‚Üê Category.assoc]
    congr 1

/-- The equivalence between the underlying category of the enriched category `C·µí·µñ` and
the opposite of the underlying category of the enriched category `C`. -/
@[simps]
def forgetEnrichmentOppositeEquivalence : ForgetEnrichment V C·µí·µñ ‚âå (ForgetEnrichment V C)·µí·µñ where
  functor := forgetEnrichmentOppositeEquivalence.functor V C
  inverse := forgetEnrichmentOppositeEquivalence.inverse V C
  unitIso := NatIso.ofComponents (fun _ ‚Ü¶ Iso.refl _)
  counitIso := NatIso.ofComponents (fun _ ‚Ü¶ Iso.refl _)

/-- If `D` is an enriched ordinary category then `D·µí·µñ` is an enriched ordinary category. -/
instance EnrichedOrdinaryCategory.opposite {D : Type u} [Category.{v} D]
    [EnrichedOrdinaryCategory V D] : EnrichedOrdinaryCategory V D·µí·µñ where
  homEquiv := Quiver.Hom.opEquiv.symm.trans homEquiv
  homEquiv_id x := homEquiv_id (x.unop)
  homEquiv_comp f g := by
    simp only [tensorHom_eComp_op_eq, leftUnitor_inv_braiding_assoc, ‚Üê unitors_inv_equal]
    exact homEquiv_comp g.unop f.unop

end

end CategoryTheory
