/-
Copyright (c) 2024 Nick Ward. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nick Ward
-/
import Mathlib.CategoryTheory.Enriched.Ordinary

/-!
# Congruence of enriched homs

In a `V`-enriched ordinary category `C`, isomorphisms in `C` induce
isomorphisms between hom-objects in `V`.

The treatment here parallels that for unenriched categories in
`Mathlib/CategoryTheory/HomCongr.lean` and that for sorts in
`Mathlib/Logic/Equiv/Defs.lean` (cf. `Equiv.arrowCongr`). Note, however, that
they construct equivalences between `Type`s and `Sort`s, respectively, while
in this file we construct isomorphisms between objects in `V`.
-/

universe v' v u u'

namespace CategoryTheory
namespace Iso

open Category MonoidalCategory

variable (V : Type u') [Category.{v'} V] [MonoidalCategory V]
  {C : Type u} [Category.{v} C] [EnrichedOrdinaryCategory V C]

/-- If we have isomorphisms `Î± : X â‰… Xâ‚` and `Î² : Y â‰… Yâ‚` in `C`, then we can
construct an isomorphism between `V` objects `X âŸ¶[V] Y` and `Xâ‚ âŸ¶[V] Yâ‚`. -/
@[simp]
def eHomCongr {X Y Xâ‚ Yâ‚ : C} (Î± : X â‰… Xâ‚) (Î² : Y â‰… Yâ‚) :
    (X âŸ¶[V] Y) â‰… (Xâ‚ âŸ¶[V] Yâ‚) where
  hom := eHomWhiskerRight V Î±.inv Y â‰« eHomWhiskerLeft V Xâ‚ Î².hom
  inv := eHomWhiskerRight V Î±.hom Yâ‚ â‰« eHomWhiskerLeft V X Î².inv
  hom_inv_id := by
    rw [â† eHom_whisker_exchange, assoc]
    rw [â† eHomWhiskerRight_comp_assoc, hom_inv_id, eHomWhiskerRight_id, id_comp]
    rw [â† eHomWhiskerLeft_comp, hom_inv_id, eHomWhiskerLeft_id]
  inv_hom_id := by
    rw [â† eHom_whisker_exchange, assoc]
    rw [â† eHomWhiskerRight_comp_assoc, inv_hom_id, eHomWhiskerRight_id, id_comp]
    rw [â† eHomWhiskerLeft_comp, inv_hom_id, eHomWhiskerLeft_id]

@[simp]
lemma eHomCongr_refl {X Y : C} :
    eHomCongr V (Iso.refl X) (Iso.refl Y) = Iso.refl _ := by aesop

@[simp]
lemma eHomCongr_trans {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ Xâ‚ƒ Yâ‚ƒ : C} (Î±â‚ : Xâ‚ â‰… Xâ‚‚) (Î²â‚ : Yâ‚ â‰… Yâ‚‚)
    (Î±â‚‚ : Xâ‚‚ â‰… Xâ‚ƒ) (Î²â‚‚ : Yâ‚‚ â‰… Yâ‚ƒ) :
    eHomCongr V (Î±â‚ â‰ªâ‰« Î±â‚‚) (Î²â‚ â‰ªâ‰« Î²â‚‚) =
      eHomCongr V Î±â‚ Î²â‚ â‰ªâ‰« eHomCongr V Î±â‚‚ Î²â‚‚ := by
  ext; simp [eHom_whisker_exchange_assoc]

@[simp]
lemma eHomCongr_symm {X Y Xâ‚ Yâ‚ : C} (Î± : X â‰… Xâ‚) (Î² : Y â‰… Yâ‚) :
    (eHomCongr V Î± Î²).symm = eHomCongr V Î±.symm Î².symm := rfl

/-- `eHomCongr` respects composition of morphisms. Recall that for any
pair of composable arrows `f : X âŸ¶ Y` and `g : Y âŸ¶ Z` in `C`, the composite
`f â‰« g` in `C` defines a morphism `ğŸ™_ V âŸ¶ (X âŸ¶[V] Z)` in `V`. Composing with
the isomorphism `eHomCongr V Î± Î³` yields a morphism in `V` that can be factored
through the enriched composition map as shown:
`ğŸ™_ V âŸ¶ ğŸ™_ V âŠ— ğŸ™_ V âŸ¶ (Xâ‚ âŸ¶[V] Yâ‚) âŠ— (Yâ‚ âŸ¶[V] Zâ‚) âŸ¶ (Xâ‚ âŸ¶[V] Zâ‚)`. -/
lemma eHomCongr_comp {X Y Z Xâ‚ Yâ‚ Zâ‚ : C} (Î± : X â‰… Xâ‚) (Î² : Y â‰… Yâ‚) (Î³ : Z â‰… Zâ‚)
    (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    eHomEquiv V (f â‰« g) â‰« (eHomCongr V Î± Î³).hom =
      (Î»_ _).inv â‰« (eHomEquiv V f â‰« (eHomCongr V Î± Î²).hom) â–· _ â‰«
        _ â— (eHomEquiv V g â‰« (eHomCongr V Î² Î³).hom) â‰« eComp V Xâ‚ Yâ‚ Zâ‚ := by
  dsimp only [eHomCongr]
  simp only [assoc, MonoidalCategory.whiskerRight_id,
    MonoidalCategory.whiskerLeft_comp]
  rw [rightUnitor_inv_naturality_assoc, rightUnitor_inv_naturality_assoc,
    rightUnitor_inv_naturality_assoc, hom_inv_id_assoc]
  rw [â† whisker_exchange_assoc, â† whisker_exchange_assoc, â† eComp_eHomWhiskerLeft]
  rw [eHom_whisker_cancel_assoc]
  rw [â† eComp_eHomWhiskerRight_assoc, â† tensorHom_def_assoc,
    â† eHomEquiv_comp_assoc]

/-- The inverse map defined by `eHomCongr` respects composition of morphisms. -/
lemma eHomCongr_inv_comp {X Y Z Xâ‚ Yâ‚ Zâ‚ : C} (Î± : X â‰… Xâ‚) (Î² : Y â‰… Yâ‚) (Î³ : Z â‰… Zâ‚)
    (f : Xâ‚ âŸ¶ Yâ‚) (g : Yâ‚ âŸ¶ Zâ‚) :
    eHomEquiv V (f â‰« g) â‰« (eHomCongr V Î± Î³).inv =
      (Î»_ _).inv â‰« (eHomEquiv V f â‰« (eHomCongr V Î± Î²).inv) â–· _ â‰«
        _ â— (eHomEquiv V g â‰« (eHomCongr V Î² Î³).inv) â‰« eComp V X Y Z :=
  eHomCongr_comp V Î±.symm Î².symm Î³.symm f g

end Iso
end CategoryTheory
