/-
Copyright (c) 2021 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Monoidal.Types.Coyoneda
import Mathlib.CategoryTheory.Monoidal.Center
import Mathlib.Tactic.ApplyFun

/-!
# Enriched categories

We set up the basic theory of `V`-enriched categories,
for `V` an arbitrary monoidal category.

We do not assume here that `V` is a concrete category,
so there does not need to be an "honest" underlying category!

Use `X ‚ü∂[V] Y` to obtain the `V` object of morphisms from `X` to `Y`.

This file contains the definitions of `V`-enriched categories and
`V`-functors.

We don't yet define the `V`-object of natural transformations
between a pair of `V`-functors (this requires limits in `V`),
but we do provide a presheaf isomorphic to the Yoneda embedding of this object.

We verify that when `V = Type v`, all these notion reduce to the usual ones.
-/


universe w w' v v' u‚ÇÅ u‚ÇÇ u‚ÇÉ

noncomputable section

namespace CategoryTheory

open Opposite

open MonoidalCategory

variable (V : Type v) [Category.{w} V] [MonoidalCategory V]

/-- A `V`-category is a category enriched in a monoidal category `V`.

Note that we do not assume that `V` is a concrete category,
so there may not be an "honest" underlying category at all!
-/
class EnrichedCategory (C : Type u‚ÇÅ) where
  /-- `X ‚ü∂[V] Y` is the `V` object of morphisms from `X` to `Y`. -/
  Hom : C ‚Üí C ‚Üí V
  /-- The identity morphism of this catgeory -/
  id (X : C) : ùüô_ V ‚ü∂ Hom X X
  /-- Composition of two morphisms in this category -/
  comp (X Y Z : C) : Hom X Y ‚äó Hom Y Z ‚ü∂ Hom X Z
  id_comp (X Y : C) : (Œª_ (Hom X Y)).inv ‚â´ id X ‚ñ∑ _ ‚â´ comp X X Y = ùüô _ := by aesop_cat
  comp_id (X Y : C) : (œÅ_ (Hom X Y)).inv ‚â´ _ ‚óÅ id Y ‚â´ comp X Y Y = ùüô _ := by aesop_cat
  assoc (W X Y Z : C) : (Œ±_ _ _ _).inv ‚â´ comp W X Y ‚ñ∑ _ ‚â´ comp W Y Z =
    _ ‚óÅ comp X Y Z ‚â´ comp W X Z := by aesop_cat

@[inherit_doc EnrichedCategory.Hom] notation X " ‚ü∂[" V "] " Y:10 => (EnrichedCategory.Hom X Y : V)

variable {C : Type u‚ÇÅ} [EnrichedCategory V C]

/-- The `ùüô_ V`-shaped generalized element giving the identity in a `V`-enriched category.
-/
def eId (X : C) : ùüô_ V ‚ü∂ X ‚ü∂[V] X :=
  EnrichedCategory.id X

/-- The composition `V`-morphism for a `V`-enriched category.
-/
def eComp (X Y Z : C) : ((X ‚ü∂[V] Y) ‚äó Y ‚ü∂[V] Z) ‚ü∂ X ‚ü∂[V] Z :=
  EnrichedCategory.comp X Y Z

@[reassoc (attr := simp)]
theorem e_id_comp (X Y : C) :
    (Œª_ (X ‚ü∂[V] Y)).inv ‚â´ eId V X ‚ñ∑ _ ‚â´ eComp V X X Y = ùüô (X ‚ü∂[V] Y) :=
  EnrichedCategory.id_comp X Y

@[reassoc (attr := simp)]
theorem e_comp_id (X Y : C) :
    (œÅ_ (X ‚ü∂[V] Y)).inv ‚â´ _ ‚óÅ eId V Y ‚â´ eComp V X Y Y = ùüô (X ‚ü∂[V] Y) :=
  EnrichedCategory.comp_id X Y

@[reassoc (attr := simp)]
theorem e_assoc (W X Y Z : C) :
    (Œ±_ _ _ _).inv ‚â´ eComp V W X Y ‚ñ∑ _ ‚â´ eComp V W Y Z =
      _ ‚óÅ eComp V X Y Z ‚â´ eComp V W X Z :=
  EnrichedCategory.assoc W X Y Z

@[reassoc]
theorem e_assoc' (W X Y Z : C) :
    (Œ±_ _ _ _).hom ‚â´ _ ‚óÅ eComp V X Y Z ‚â´ eComp V W X Z =
      eComp V W X Y ‚ñ∑ _ ‚â´ eComp V W Y Z := by
  rw [‚Üê e_assoc V W X Y Z, Iso.hom_inv_id_assoc]

section

variable {V} {W : Type v'} [Category.{w'} W] [MonoidalCategory W]

/-- A type synonym for `C`, which should come equipped with a `V`-enriched category structure.
In a moment we will equip this with the `W`-enriched category structure
obtained by applying the functor `F : LaxMonoidalFunctor V W` to each hom object.
-/
@[nolint unusedArguments]
def TransportEnrichment (F : V ‚•§ W) [F.LaxMonoidal] (C : Type u‚ÇÅ) :=
  C

variable (F : V ‚•§ W) [F.LaxMonoidal]

open Functor.LaxMonoidal

instance : EnrichedCategory W (TransportEnrichment F C) where
  Hom := fun X Y : C => F.obj (X ‚ü∂[V] Y)
  id := fun X : C => Œµ F ‚â´ F.map (eId V X)
  comp := fun X Y Z : C => Œº F _ _ ‚â´ F.map (eComp V X Y Z)
  id_comp X Y := by
    simp only [comp_whiskerRight, Category.assoc, Functor.LaxMonoidal.Œº_natural_left_assoc,
      Functor.LaxMonoidal.left_unitality_inv_assoc]
    simp_rw [‚Üê F.map_comp]
    convert F.map_id _
    simp
  comp_id X Y := by
    simp only [MonoidalCategory.whiskerLeft_comp, Category.assoc,
      Functor.LaxMonoidal.Œº_natural_right_assoc,
      Functor.LaxMonoidal.right_unitality_inv_assoc]
    simp_rw [‚Üê F.map_comp]
    convert F.map_id _
    simp
  assoc P Q R S := by
    rw [comp_whiskerRight, Category.assoc, Œº_natural_left_assoc,
      ‚Üê associativity_inv_assoc, ‚Üê F.map_comp, ‚Üê F.map_comp, e_assoc,
      F.map_comp, MonoidalCategory.whiskerLeft_comp, Category.assoc,
      Functor.LaxMonoidal.Œº_natural_right_assoc]

end

/-- Construct an honest category from a `Type v`-enriched category.
-/
def categoryOfEnrichedCategoryType (C : Type u‚ÇÅ) [ùíû : EnrichedCategory (Type v) C] :
    Category.{v} C where
  Hom := ùíû.Hom
  id X := eId (Type v) X PUnit.unit
  comp f g := eComp (Type v) _ _ _ ‚ü®f, g‚ü©
  id_comp f := congr_fun (e_id_comp (Type v) _ _) f
  comp_id f := congr_fun (e_comp_id (Type v) _ _) f
  assoc f g h := (congr_fun (e_assoc (Type v) _ _ _ _) ‚ü®f, g, h‚ü© :)

/-- Construct a `Type v`-enriched category from an honest category.
-/
def enrichedCategoryTypeOfCategory (C : Type u‚ÇÅ) [ùíû : Category.{v} C] :
    EnrichedCategory (Type v) C where
  Hom := ùíû.Hom
  id X _ := ùüô X
  comp _ _ _ p := p.1 ‚â´ p.2
  id_comp X Y := by ext; simp
  comp_id X Y := by ext; simp
  assoc W X Y Z := by ext ‚ü®f, g, h‚ü©; simp

/-- We verify that an enriched category in `Type u` is just the same thing as an honest category.
-/
def enrichedCategoryTypeEquivCategory (C : Type u‚ÇÅ) :
    EnrichedCategory (Type v) C ‚âÉ Category.{v} C where
  toFun _ := categoryOfEnrichedCategoryType C
  invFun _ := enrichedCategoryTypeOfCategory C

section

variable {W : Type v} [Category.{w} W] [MonoidalCategory W] [EnrichedCategory W C]

/-- A type synonym for `C`, which should come equipped with a `V`-enriched category structure.
In a moment we will equip this with the (honest) category structure
so that `X ‚ü∂ Y` is `(ùüô_ W) ‚ü∂ (X ‚ü∂[W] Y)`.

We obtain this category by
transporting the enrichment in `V` along the lax monoidal functor `coyonedaTensorUnit`,
then using the equivalence of `Type`-enriched categories with honest categories.

This is sometimes called the "underlying" category of an enriched category,
although some care is needed as the functor `coyonedaTensorUnit`,
which always exists, does not necessarily coincide with
"the forgetful functor" from `V` to `Type`, if such exists.
When `V` is any of `Type`, `Top`, `AddCommGroup`, or `Module R`,
`coyonedaTensorUnit` is just the usual forgetful functor, however.
For `V = Algebra R`, the usual forgetful functor is coyoneda of `R[X]`, not of `R`.
(Perhaps we should have a typeclass for this situation: `ConcreteMonoidal`?)
-/
@[nolint unusedArguments]
def ForgetEnrichment (W : Type v) [Category.{w} W] [MonoidalCategory W] (C : Type u‚ÇÅ)
    [EnrichedCategory W C] :=
  C

variable (W)

/-- Typecheck an object of `C` as an object of `ForgetEnrichment W C`. -/
def ForgetEnrichment.of (X : C) : ForgetEnrichment W C :=
  X

/-- Typecheck an object of `ForgetEnrichment W C` as an object of `C`. -/
def ForgetEnrichment.to (X : ForgetEnrichment W C) : C :=
  X

@[simp]
theorem ForgetEnrichment.to_of (X : C) : ForgetEnrichment.to W (ForgetEnrichment.of W X) = X :=
  rfl

@[simp]
theorem ForgetEnrichment.of_to (X : ForgetEnrichment W C) :
    ForgetEnrichment.of W (ForgetEnrichment.to W X) = X :=
  rfl

instance categoryForgetEnrichment : Category (ForgetEnrichment W C) :=
  enrichedCategoryTypeEquivCategory C (inferInstanceAs (EnrichedCategory (Type w)
      (TransportEnrichment (coyoneda.obj (op (ùüô_ W))) C)))

/-- We verify that the morphism types in `ForgetEnrichment W C` are `(ùüô_ W) ‚ü∂ (X ‚ü∂[W] Y)`.
-/
example (X Y : ForgetEnrichment W C) :
    (X ‚ü∂ Y) = (ùüô_ W ‚ü∂ ForgetEnrichment.to W X ‚ü∂[W] ForgetEnrichment.to W Y) :=
  rfl

/-- Typecheck a `(ùüô_ W)`-shaped `W`-morphism as a morphism in `ForgetEnrichment W C`. -/
def ForgetEnrichment.homOf {X Y : C} (f : ùüô_ W ‚ü∂ X ‚ü∂[W] Y) :
    ForgetEnrichment.of W X ‚ü∂ ForgetEnrichment.of W Y :=
  f

/-- Typecheck a morphism in `ForgetEnrichment W C` as a `(ùüô_ W)`-shaped `W`-morphism. -/
def ForgetEnrichment.homTo {X Y : ForgetEnrichment W C} (f : X ‚ü∂ Y) :
    ùüô_ W ‚ü∂ ForgetEnrichment.to W X ‚ü∂[W] ForgetEnrichment.to W Y :=
  f

@[simp]
theorem ForgetEnrichment.homTo_homOf {X Y : C} (f : ùüô_ W ‚ü∂ X ‚ü∂[W] Y) :
    ForgetEnrichment.homTo W (ForgetEnrichment.homOf W f) = f :=
  rfl

@[simp]
theorem ForgetEnrichment.homOf_homTo {X Y : ForgetEnrichment W C} (f : X ‚ü∂ Y) :
    ForgetEnrichment.homOf W (ForgetEnrichment.homTo W f) = f :=
  rfl

/-- The identity in the "underlying" category of an enriched category. -/
@[simp]
theorem forgetEnrichment_id (X : ForgetEnrichment W C) :
    ForgetEnrichment.homTo W (ùüô X) = eId W (ForgetEnrichment.to W X : C) :=
  Category.id_comp _

@[simp]
theorem forgetEnrichment_id' (X : C) :
    ForgetEnrichment.homOf W (eId W X) = ùüô (ForgetEnrichment.of W X : C) :=
  (forgetEnrichment_id W (ForgetEnrichment.of W X)).symm

/-- Composition in the "underlying" category of an enriched category. -/
@[simp]
theorem forgetEnrichment_comp {X Y Z : ForgetEnrichment W C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    ForgetEnrichment.homTo W (f ‚â´ g) =
      ((Œª_ (ùüô_ W)).inv ‚â´ (ForgetEnrichment.homTo W f ‚äó‚Çò ForgetEnrichment.homTo W g)) ‚â´
        eComp W _ _ _ :=
  rfl

end

/-- A `V`-functor `F` between `V`-enriched categories
has a `V`-morphism from `X ‚ü∂[V] Y` to `F.obj X ‚ü∂[V] F.obj Y`,
satisfying the usual axioms.
-/
structure EnrichedFunctor (C : Type u‚ÇÅ) [EnrichedCategory V C] (D : Type u‚ÇÇ)
    [EnrichedCategory V D] where
  /-- The application of this functor to an object -/
  obj : C ‚Üí D
  /-- The `V`-morphism from `X ‚ü∂[V] Y` to `F.obj X ‚ü∂[V] F.obj Y`, for all `X Y : C` -/
  map : ‚àÄ X Y : C, (X ‚ü∂[V] Y) ‚ü∂ obj X ‚ü∂[V] obj Y
  map_id : ‚àÄ X : C, eId V X ‚â´ map X X = eId V (obj X) := by aesop_cat
  map_comp :
    ‚àÄ X Y Z : C,
      eComp V X Y Z ‚â´ map X Z = (map X Y ‚äó‚Çò map Y Z) ‚â´ eComp V (obj X) (obj Y) (obj Z) := by
    aesop_cat

attribute [reassoc (attr := simp)] EnrichedFunctor.map_id

attribute [reassoc (attr := simp)] EnrichedFunctor.map_comp

/-- The identity enriched functor. -/
@[simps]
def EnrichedFunctor.id (C : Type u‚ÇÅ) [EnrichedCategory V C] : EnrichedFunctor V C C where
  obj X := X
  map _ _ := ùüô _

instance : Inhabited (EnrichedFunctor V C C) :=
  ‚ü®EnrichedFunctor.id V C‚ü©

/-- Composition of enriched functors. -/
@[simps]
def EnrichedFunctor.comp {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} {E : Type u‚ÇÉ} [EnrichedCategory V C]
    [EnrichedCategory V D] [EnrichedCategory V E] (F : EnrichedFunctor V C D)
    (G : EnrichedFunctor V D E) : EnrichedFunctor V C E where
  obj X := G.obj (F.obj X)
  map _ _ := F.map _ _ ‚â´ G.map _ _

lemma EnrichedFunctor.ext {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} [EnrichedCategory V C]
    [EnrichedCategory V D] {F G : EnrichedFunctor V C D} (h_obj : ‚àÄ X, F.obj X = G.obj X)
    (h_map : ‚àÄ (X Y : C), F.map X Y ‚â´ eqToHom (by rw [h_obj, h_obj]) = G.map X Y) : F = G := by
  match F, G with
  | mk F_obj F_map _ _, mk G_obj G_map _ _ =>
    obtain rfl : F_obj = G_obj := funext fun X ‚Ü¶ h_obj X
    congr
    ext X Y
    simpa using h_map X Y

section

variable {W : Type (v + 1)} [Category.{v} W] [MonoidalCategory W]

/-- An enriched functor induces an honest functor of the underlying categories,
by mapping the `(ùüô_ W)`-shaped morphisms.
-/
def EnrichedFunctor.forget {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} [EnrichedCategory W C] [EnrichedCategory W D]
    (F : EnrichedFunctor W C D) : ForgetEnrichment W C ‚•§ ForgetEnrichment W D where
  obj X := ForgetEnrichment.of W (F.obj (ForgetEnrichment.to W X))
  map f :=
    ForgetEnrichment.homOf W
      (ForgetEnrichment.homTo W f ‚â´ F.map (ForgetEnrichment.to W _) (ForgetEnrichment.to W _))
  map_comp f g := by
    dsimp
    apply_fun ForgetEnrichment.homTo W
    ¬∑ simp only [Iso.cancel_iso_inv_left, Category.assoc, tensor_comp,
        ForgetEnrichment.homTo_homOf, EnrichedFunctor.map_comp, forgetEnrichment_comp]
      rfl
    ¬∑ intro f g w; apply_fun ForgetEnrichment.homOf W at w; simpa using w

end

section

variable {V}
variable {D : Type u‚ÇÇ} [EnrichedCategory V D]

/-!
We now turn to natural transformations between `V`-functors.

The mostly commonly encountered definition of an enriched natural transformation
is a collection of morphisms
```
(ùüô_ W) ‚ü∂ (F.obj X ‚ü∂[V] G.obj X)
```
satisfying an appropriate analogue of the naturality square.
(c.f. https://ncatlab.org/nlab/show/enriched+natural+transformation)

This is the same thing as a natural transformation `F.forget ‚ü∂ G.forget`.

We formalize this as `EnrichedNatTrans F G`, which is a `Type`.

However, there's also something much nicer: with appropriate additional hypotheses,
there is a `V`-object `EnrichedNatTransObj F G` which contains more information,
and from which one can recover `EnrichedNatTrans F G ‚âÉ (ùüô_ V) ‚ü∂ EnrichedNatTransObj F G`.

Using these as the hom-objects, we can build a `V`-enriched category
with objects the `V`-functors.

For `EnrichedNatTransObj` to exist, it suffices to have `V` braided and complete.

Before assuming `V` is complete, we assume it is braided and
define a presheaf `enrichedNatTransYoneda F G`
which is isomorphic to the Yoneda embedding of `EnrichedNatTransObj F G`
whether or not that object actually exists.

This presheaf has components `(enrichedNatTransYoneda F G).obj A`
what we call the `A`-graded enriched natural transformations,
which are collections of morphisms
```
A ‚ü∂ (F.obj X ‚ü∂[V] G.obj X)
```
satisfying a similar analogue of the naturality square,
this time incorporating a half-braiding on `A`.

(We actually define `EnrichedNatTrans F G`
as the special case `A := ùüô_ V` with the trivial half-braiding,
and when defining `enrichedNatTransYoneda F G` we use the half-braidings
coming from the ambient braiding on `V`.)
-/


/-- The type of `A`-graded natural transformations between `V`-functors `F` and `G`.
This is the type of morphisms in `V` from `A` to the `V`-object of natural transformations.
-/
@[ext]
structure GradedNatTrans (A : Center V) (F G : EnrichedFunctor V C D) where
  /-- The `A`-graded transformation from `F` to `G` -/
  app : ‚àÄ X : C, A.1 ‚ü∂ F.obj X ‚ü∂[V] G.obj X
  /-- `app` is a natural transformation. -/
  naturality :
    ‚àÄ X Y : C,
      (A.2.Œ≤ (X ‚ü∂[V] Y)).hom ‚â´ (F.map X Y ‚äó‚Çò app Y) ‚â´ eComp V _ _ _ =
        (app X ‚äó‚Çò G.map X Y) ‚â´ eComp V _ _ _

variable [BraidedCategory V]

open BraidedCategory

/-- A presheaf isomorphic to the Yoneda embedding of
the `V`-object of natural transformations from `F` to `G`.
-/
@[simps]
def enrichedNatTransYoneda (F G : EnrichedFunctor V C D) : V·µí·µñ ‚•§ Type max u‚ÇÅ w where
  obj A := GradedNatTrans ((Center.ofBraided V).obj (unop A)) F G
  map f œÉ :=
    { app := fun X => f.unop ‚â´ œÉ.app X
      naturality := fun X Y => by
        have p := œÉ.naturality X Y
        dsimp at p ‚ä¢
        rw [‚Üê id_tensor_comp_tensor_id (f.unop ‚â´ œÉ.app Y) _, id_tensor_comp, Category.assoc,
          Category.assoc, ‚Üê braiding_naturality_assoc, id_tensor_comp_tensor_id_assoc, p, ‚Üê
          tensor_comp_assoc, Category.id_comp] }

-- TODO assuming `[HasLimits C]` construct the actual object of natural transformations
-- and show that the functor category is `V`-enriched.
end

section

attribute [local instance] categoryOfEnrichedCategoryType

/-- We verify that an enriched functor between `Type v` enriched categories
is just the same thing as an honest functor.
-/
@[simps]
def enrichedFunctorTypeEquivFunctor {C : Type u‚ÇÅ} [ùíû : EnrichedCategory (Type v) C] {D : Type u‚ÇÇ}
    [ùíü : EnrichedCategory (Type v) D] : EnrichedFunctor (Type v) C D ‚âÉ C ‚•§ D where
  toFun F :=
    { obj := fun X => F.obj X
      map := fun f => F.map _ _ f
      map_id := fun X => congr_fun (F.map_id X) PUnit.unit
      map_comp := fun f g => congr_fun (F.map_comp _ _ _) ‚ü®f, g‚ü© }
  invFun F :=
    { obj := fun X => F.obj X
      map := fun _ _ f => F.map f
      map_id := fun X => by ext ‚ü®‚ü©; exact F.map_id X
      map_comp := fun X Y Z => by ext ‚ü®f, g‚ü©; exact F.map_comp f g }

/-- We verify that the presheaf representing natural transformations
between `Type v`-enriched functors is actually represented by
the usual type of natural transformations!
-/
def enrichedNatTransYonedaTypeIsoYonedaNatTrans {C : Type v} [EnrichedCategory (Type v) C]
    {D : Type v} [EnrichedCategory (Type v) D] (F G : EnrichedFunctor (Type v) C D) :
    enrichedNatTransYoneda F G ‚âÖ
      yoneda.obj (enrichedFunctorTypeEquivFunctor F ‚ü∂ enrichedFunctorTypeEquivFunctor G) :=
  NatIso.ofComponents
    (fun Œ± =>
      { hom := fun œÉ x =>
          { app := fun X => œÉ.app X x
            naturality := fun X Y f => congr_fun (œÉ.naturality X Y) ‚ü®x, f‚ü© }
        inv := fun œÉ =>
          { app := fun X x => (œÉ x).app X
            naturality := fun X Y => by ext ‚ü®x, f‚ü©; exact (œÉ x).naturality f } })
    (by aesop_cat)

end

end CategoryTheory
