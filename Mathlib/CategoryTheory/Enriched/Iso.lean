/-
Copyright (c) 2025 Jakob von Raumer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jakob von Raumer
-/
import Mathlib.CategoryTheory.Enriched.Basic

universe w v uâ‚

/-!
# Isomorphisms in ordinary categories

We define the notion of `V`-enriched isomorphisms in `V`-enriched categories and relate them
to the isomorphisms in the category `ForgetEnrichment V C`.
-/


namespace CategoryTheory

open MonoidalCategory

variable (V : Type v) [Category.{w} V] [MonoidalCategory V]

variable {C : Type uâ‚} [EnrichedCategory V C]

/-- Isomorphisms in a `V`-enriched category `C`consist of a morphism
`ğŸ™_ V âŸ¶ X âŸ¶[V] Y`, an inverse `ğŸ™_ V âŸ¶ Y âŸ¶[V] X`, and proofs that these compose to the identity
morphism. -/
@[ext]
structure EnrichedIso (X Y : C) where
  /-- The forward direction of an isomorphism. -/
  hom : ğŸ™_ V âŸ¶ X âŸ¶[V] Y
  /-- The backward direction of an isomorphism. -/
  inv : ğŸ™_ V âŸ¶ Y âŸ¶[V] X
  hom_inv : (Î»_ _).inv â‰« (hom âŠ—â‚˜ inv) â‰« eComp V X Y X = eId V X := by aesop_cat
  inv_hom : (Î»_ _).inv â‰« (inv âŠ—â‚˜ hom) â‰« eComp V Y X Y = eId V Y := by aesop_cat

@[inherit_doc EnrichedIso]
notation X " â‰…[" V "] " Y:10 => EnrichedIso V X Y

namespace EnrichedIso

variable {V}

/-- The identity isomorphism in a `V`-enriched category. -/
@[refl, simps]
def refl (X : C) : X â‰…[V] X where
  hom := eId V X
  inv := eId V X
  hom_inv := by simp [tensorHom_def']
  inv_hom := by simp [tensorHom_def']

/-- The inverse isomorphism of an isomorphism in a `V`-enriched category. -/
@[symm, simps]
def symm {X Y : C} (I : X â‰…[V] Y) : Y â‰…[V] X where
  hom := I.inv
  inv := I.hom
  hom_inv := I.inv_hom
  inv_hom := I.hom_inv

open EnrichedCategory

lemma trans_hom_inv {X Y Z : C} (I : X â‰…[V] Y) (J : Y â‰…[V] Z) :
    (Î»_ (ğŸ™_ V)).inv â‰« ((Î»_ (ğŸ™_ V)).inv â‰« (I.hom âŠ—â‚˜ J.hom) â‰« eComp V X Y Z âŠ—â‚˜ (Î»_ (ğŸ™_ V)).inv â‰«
      (J.inv âŠ—â‚˜ I.inv) â‰« eComp V Z Y X) â‰« eComp V X Z X = eId V X := by
  rw [tensor_comp, tensor_comp, tensorHom_def (eComp V X Y Z), Category.assoc, Category.assoc,
    Category.assoc, â† e_assoc, associator_inv_naturality_left_assoc,
    â† comp_whiskerRight_assoc, â† e_assoc', associator_inv_naturality_assoc,
    tensorHom_def' (g := I.inv), Category.assoc, â† comp_whiskerRight_assoc,
    associator_naturality_assoc, tensorHom_def (f := I.hom), Category.assoc,
    â† whiskerLeft_comp_assoc, (Iso.inv_comp_eq _).mp J.hom_inv, â† I.hom_inv,
    tensorHom_def' I.hom]
  simp only [whiskerLeft_comp, Category.comp_id, Category.assoc, Iso.inv_hom_id_assoc,
    whiskerRight_tensor, whiskerRight_id, triangle_assoc, e_comp_id]
  monoidal

/-- The composition of to isomorphisms in a `V`-enriched category. -/
@[trans, simps]
def trans {X Y Z : C} (I : X â‰…[V] Y) (J : Y â‰…[V] Z) : X â‰…[V] Z where
  hom := (Î»_ _).inv â‰« (I.hom âŠ—â‚˜ J.hom) â‰« eComp V X Y Z
  inv := (Î»_ _).inv â‰« (J.inv âŠ—â‚˜ I.inv) â‰« eComp V Z Y X
  hom_inv := trans_hom_inv I J
  inv_hom := trans_hom_inv J.symm I.symm

end EnrichedIso

/-- The isomorphism in `ForgetEnrichment W C` induced by a `W`-enriched iso in `C`. -/
@[simps]
def ForgetEnrichment.isoOf {X Y : C} (I : X â‰…[V] Y) :
    ForgetEnrichment.of V X â‰… ForgetEnrichment.of V Y where
  hom := homOf V I.hom
  inv := homOf V I.inv
  hom_inv_id := by simp [â† homOf_comp, I.hom_inv]
  inv_hom_id := by simp [â† homOf_comp, I.inv_hom]

@[simp]
lemma ForgetEnrichment.isoOf_refl (X : C) : isoOf V (.refl X) = Iso.refl (of V X) := by
  ext; simp

@[simp]
lemma ForgetEnrichment.isoOf_symm {X Y : C} (I : X â‰…[V] Y) :
    isoOf V (.symm I) = (isoOf V I).symm := by
  rfl

@[simp]
lemma ForgetEnrichment.isoOf_trans {X Y Z : C} (I : X â‰…[V] Y) (J : Y â‰…[V] Z) :
    isoOf V (I.trans J) = (isoOf V I).trans (isoOf V J) := by
  ext; simp [â† Category.assoc, homOf_comp]

/-- The `V`-enriched isomorphism in `C` associated to an iso `X â‰… Y` in `ForgetEnrichment V C`. -/
@[simps]
def ForgetEnrichment.isoTo {X Y : ForgetEnrichment V C} (I : X â‰… Y) :
    EnrichedIso V (ForgetEnrichment.to V X) (ForgetEnrichment.to V Y) where
  hom := homTo V I.hom
  inv := homTo V I.inv
  hom_inv := by rw [â† Category.assoc, â† homTo_comp, I.hom_inv_id, homTo_id]
  inv_hom := by rw [â† Category.assoc, â† homTo_comp, I.inv_hom_id, homTo_id]

@[simp]
lemma ForgetEnrichment.isoTo_rfl {X : ForgetEnrichment V C} :
    isoTo V (.refl X) = .refl (ForgetEnrichment.to V X) := by
  ext <;> simp

@[simp]
lemma ForgetEnrichment.isoTo_symm {X Y : ForgetEnrichment V C} (I : X â‰… Y) :
    isoTo V I.symm = .symm (isoTo V I) := by
  ext <;> simp

@[simp]
lemma ForgetEnrichment.isoTo_trans {X Y Z : ForgetEnrichment V C} (I : X â‰… Y) (J : Y â‰… Z) :
    isoTo V (I.trans J) = .trans (isoTo V I) (isoTo V J) := by
  ext <;> simp

/-- The type equivalence between isos in `ForgetEnrichment V C` and `V`-enriched isos in `C`. -/
def ForgetEnrichment.equivIsoEnrichedIso (X Y : ForgetEnrichment V C) :
    (X â‰… Y) â‰ƒ (ForgetEnrichment.to V X) â‰…[V] (ForgetEnrichment.to V Y) where
  toFun := ForgetEnrichment.isoTo V
  invFun := ForgetEnrichment.isoOf V

end CategoryTheory
