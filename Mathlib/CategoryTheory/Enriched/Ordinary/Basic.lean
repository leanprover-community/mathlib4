/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Enriched.Basic
import Mathlib.CategoryTheory.Monoidal.Types.Coyoneda

/-!
# Enriched ordinary categories

If `V` is a monoidal category, a `V`-enriched category `C` does not need
to be a category. However, when we have both `Category C` and `EnrichedCategory V C`,
we may require that the type of morphisms `X ‚ü∂ Y` in `C` identify to
`ùüô_ V ‚ü∂ EnrichedCategory.Hom X Y`. This data shall be packaged in the
typeclass `EnrichedOrdinaryCategory V C`.

In particular, if `C` is a `V`-enriched category, it is shown that
the "underlying" category `ForgetEnrichment V C` is equipped with a
`EnrichedOrdinaryCategory V C` instance.

Simplicial categories are implemented in `AlgebraicTopology.SimplicialCategory.Basic`
using an abbreviation for `EnrichedOrdinaryCategory SSet C`.

-/

universe v' v v'' u u' u''

open CategoryTheory Category MonoidalCategory Opposite

namespace CategoryTheory

variable (V : Type u') [Category.{v'} V] [MonoidalCategory V]
  (C : Type u) [Category.{v} C]

/-- An enriched ordinary category is a category `C` that is also enriched
over a category `V` in such a way that morphisms `X ‚ü∂ Y` in `C` identify
to morphisms `ùüô_ V ‚ü∂ (X ‚ü∂[V] Y)` in `V`. -/
class EnrichedOrdinaryCategory extends EnrichedCategory V C where
  /-- morphisms `X ‚ü∂ Y` in the category identify morphisms `ùüô_ V ‚ü∂ (X ‚ü∂[V] Y)` in `V` -/
  homEquiv {X Y : C} : (X ‚ü∂ Y) ‚âÉ (ùüô_ V ‚ü∂ (X ‚ü∂[V] Y))
  homEquiv_id (X : C) : homEquiv (ùüô X) = eId V X := by cat_disch
  homEquiv_comp {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    homEquiv (f ‚â´ g) = (Œª_ _).inv ‚â´ (homEquiv f ‚äó‚Çò homEquiv g) ‚â´
      eComp V X Y Z := by cat_disch

variable [EnrichedOrdinaryCategory V C] {C}

/-- The bijection `(X ‚ü∂ Y) ‚âÉ (ùüô_ V ‚ü∂ (X ‚ü∂[V] Y))` given by a
`EnrichedOrdinaryCategory` instance. -/
def eHomEquiv {X Y : C} : (X ‚ü∂ Y) ‚âÉ (ùüô_ V ‚ü∂ (X ‚ü∂[V] Y)) :=
  EnrichedOrdinaryCategory.homEquiv

@[simp]
lemma eHomEquiv_id (X : C) : eHomEquiv V (ùüô X) = eId V X :=
  EnrichedOrdinaryCategory.homEquiv_id _

@[reassoc]
lemma eHomEquiv_comp {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    eHomEquiv V (f ‚â´ g) = (Œª_ _).inv ‚â´ (eHomEquiv V f ‚äó‚Çò eHomEquiv V g) ‚â´ eComp V X Y Z :=
  EnrichedOrdinaryCategory.homEquiv_comp _ _

/-- The morphism `(X' ‚ü∂[V] Y) ‚ü∂ (X ‚ü∂[V] Y)` induced by a morphism `X ‚ü∂ X'`. -/
def eHomWhiskerRight {X X' : C} (f : X ‚ü∂ X') (Y : C) :
    (X' ‚ü∂[V] Y) ‚ü∂ (X ‚ü∂[V] Y) :=
  (Œª_ _).inv ‚â´ eHomEquiv V f ‚ñ∑ _ ‚â´ eComp V X X' Y

@[simp]
lemma eHomWhiskerRight_id (X Y : C) : eHomWhiskerRight V (ùüô X) Y = ùüô _ := by
  simp [eHomWhiskerRight]

@[simp, reassoc]
lemma eHomWhiskerRight_comp {X X' X'' : C} (f : X ‚ü∂ X') (f' : X' ‚ü∂ X'') (Y : C) :
    eHomWhiskerRight V (f ‚â´ f') Y = eHomWhiskerRight V f' Y ‚â´ eHomWhiskerRight V f Y := by
  dsimp [eHomWhiskerRight]
  rw [assoc, assoc, eHomEquiv_comp, comp_whiskerRight_assoc, comp_whiskerRight_assoc, ‚Üê e_assoc',
    tensorHom_def', comp_whiskerRight_assoc, id_whiskerLeft, comp_whiskerRight_assoc,
    ‚Üê comp_whiskerRight_assoc, Iso.inv_hom_id, id_whiskerRight_assoc,
    comp_whiskerRight_assoc, leftUnitor_inv_whiskerRight_assoc,
    ‚Üê associator_inv_naturality_left_assoc, Iso.inv_hom_id_assoc,
    ‚Üê whisker_exchange_assoc, id_whiskerLeft_assoc, Iso.inv_hom_id_assoc]

/-- Whiskering commutes with the enriched composition. -/
@[reassoc]
lemma eComp_eHomWhiskerRight {X X' : C} (f : X ‚ü∂ X') (Y Z : C) :
    eComp V X' Y Z ‚â´ eHomWhiskerRight V f Z =
      eHomWhiskerRight V f Y ‚ñ∑ _ ‚â´ eComp V X Y Z := by
  dsimp [eHomWhiskerRight]
  rw [leftUnitor_inv_naturality_assoc, whisker_exchange_assoc]
  simp [e_assoc']

/-- The morphism `(X ‚ü∂[V] Y) ‚ü∂ (X ‚ü∂[V] Y')` induced by a morphism `Y ‚ü∂ Y'`. -/
def eHomWhiskerLeft (X : C) {Y Y' : C} (g : Y ‚ü∂ Y') :
    (X ‚ü∂[V] Y) ‚ü∂ (X ‚ü∂[V] Y') :=
  (œÅ_ _).inv ‚â´ _ ‚óÅ eHomEquiv V g ‚â´ eComp V X Y Y'

@[simp]
lemma eHomWhiskerLeft_id (X Y : C) : eHomWhiskerLeft V X (ùüô Y) = ùüô _ := by
  simp [eHomWhiskerLeft]

@[simp, reassoc]
lemma eHomWhiskerLeft_comp (X : C) {Y Y' Y'' : C} (g : Y ‚ü∂ Y') (g' : Y' ‚ü∂ Y'') :
    eHomWhiskerLeft V X (g ‚â´ g') = eHomWhiskerLeft V X g ‚â´ eHomWhiskerLeft V X g' := by
  dsimp [eHomWhiskerLeft]
  rw [assoc, assoc, eHomEquiv_comp, MonoidalCategory.whiskerLeft_comp_assoc,
    MonoidalCategory.whiskerLeft_comp_assoc, ‚Üê e_assoc, tensorHom_def,
    MonoidalCategory.whiskerRight_id_assoc, MonoidalCategory.whiskerLeft_comp_assoc,
    MonoidalCategory.whiskerLeft_comp_assoc, MonoidalCategory.whiskerLeft_comp_assoc,
    whiskerLeft_rightUnitor_assoc, whiskerLeft_rightUnitor_inv_assoc,
    triangle_assoc_comp_left_inv_assoc, MonoidalCategory.whiskerRight_id_assoc,
    Iso.hom_inv_id_assoc, Iso.inv_hom_id_assoc,
    associator_inv_naturality_right_assoc, Iso.hom_inv_id_assoc,
    whisker_exchange_assoc, MonoidalCategory.whiskerRight_id_assoc, Iso.inv_hom_id_assoc]

/-- Whiskering commutes with the enriched composition. -/
@[reassoc]
lemma eComp_eHomWhiskerLeft (X Y : C) {Z Z' : C} (g : Z ‚ü∂ Z') :
    eComp V X Y Z ‚â´ eHomWhiskerLeft V X g =
      _ ‚óÅ eHomWhiskerLeft V Y g ‚â´ eComp V X Y Z' := by
  dsimp [eHomWhiskerLeft]
  rw [rightUnitor_inv_naturality_assoc, ‚Üê whisker_exchange_assoc]
  simp

/-- Given an isomorphism `Œ± : Y ‚âÖ Y‚ÇÅ` in C, the enriched composition map
`eComp V X Y Z : (X ‚ü∂[V] Y) ‚äó (Y ‚ü∂[V] Z) ‚ü∂ (X ‚ü∂[V] Z)` factors through the `V`
object `(X ‚ü∂[V] Y‚ÇÅ) ‚äó (Y‚ÇÅ ‚ü∂[V] Z)` via the map defined by whiskering in the
middle with `Œ±.hom` and `Œ±.inv`. -/
@[reassoc]
lemma eHom_whisker_cancel {X Y Y‚ÇÅ Z : C} (Œ± : Y ‚âÖ Y‚ÇÅ) :
    eHomWhiskerLeft V X Œ±.hom ‚ñ∑ _ ‚â´ _ ‚óÅ eHomWhiskerRight V Œ±.inv Z ‚â´
      eComp V X Y‚ÇÅ Z = eComp V X Y Z := by
  dsimp [eHomWhiskerLeft, eHomWhiskerRight]
  simp only [MonoidalCategory.whiskerLeft_comp_assoc, whisker_assoc_symm,
    triangle_assoc_comp_left_inv_assoc, e_assoc', assoc]
  simp only [‚Üê comp_whiskerRight_assoc]
  change (eHomWhiskerLeft V X Œ±.hom ‚â´ eHomWhiskerLeft V X Œ±.inv) ‚ñ∑ _ ‚â´ _ = _
  simp [‚Üê eHomWhiskerLeft_comp]

@[reassoc]
lemma eHom_whisker_cancel_inv {X Y Y‚ÇÅ Z : C} (Œ± : Y ‚âÖ Y‚ÇÅ) :
    eHomWhiskerLeft V X Œ±.inv ‚ñ∑ _ ‚â´ _ ‚óÅ eHomWhiskerRight V Œ±.hom Z ‚â´
      eComp V X Y Z = eComp V X Y‚ÇÅ Z := eHom_whisker_cancel V Œ±.symm

@[reassoc]
lemma eHom_whisker_exchange {X X' Y Y' : C} (f : X ‚ü∂ X') (g : Y ‚ü∂ Y') :
    eHomWhiskerLeft V X' g ‚â´ eHomWhiskerRight V f Y' =
      eHomWhiskerRight V f Y ‚â´ eHomWhiskerLeft V X g := by
  dsimp [eHomWhiskerLeft, eHomWhiskerRight]
  rw [assoc, assoc, assoc, assoc, leftUnitor_inv_naturality_assoc,
    whisker_exchange_assoc, ‚Üê e_assoc, leftUnitor_tensor_inv_assoc,
    associator_inv_naturality_left_assoc, Iso.hom_inv_id_assoc,
    ‚Üê comp_whiskerRight_assoc, whisker_exchange_assoc,
    MonoidalCategory.whiskerRight_id_assoc, assoc, Iso.inv_hom_id_assoc,
    whisker_exchange_assoc, MonoidalCategory.whiskerRight_id_assoc, Iso.inv_hom_id_assoc]

attribute [local simp] eHom_whisker_exchange

variable (C) in
/-- The bifunctor `C·µí·µñ ‚•§ C ‚•§ V` which sends `X : C·µí·µñ` and `Y : C` to `X ‚ü∂[V] Y`. -/
@[simps]
def eHomFunctor : C·µí·µñ ‚•§ C ‚•§ V where
  obj X :=
    { obj := fun Y => X.unop ‚ü∂[V] Y
      map := fun œÜ => eHomWhiskerLeft V X.unop œÜ }
  map œÜ :=
    { app := fun Y => eHomWhiskerRight V œÜ.unop Y }

instance ForgetEnrichment.enrichedOrdinaryCategory {D : Type*} [EnrichedCategory V D] :
    EnrichedOrdinaryCategory V (ForgetEnrichment V D) where
  toEnrichedCategory := inferInstanceAs (EnrichedCategory V D)
  homEquiv := Equiv.refl _
  homEquiv_id _ := Category.id_comp _
  homEquiv_comp _ _ := Category.assoc _ _ _

/-- If `D` is already an enriched ordinary category, there is a canonical functor from `D` to
`ForgetEnrichment V D`. -/
@[simps]
def ForgetEnrichment.equivInverse (D : Type u') [Category.{v'} D] [EnrichedOrdinaryCategory V D] :
    D ‚•§ ForgetEnrichment V D where
  obj X := .of V X
  map f := ForgetEnrichment.homOf V (eHomEquiv V f)
  map_comp f g := by simp [eHomEquiv_comp]

/-- If `D` is already an enriched ordinary category, there is a canonical functor from
`ForgetEnrichment V D` to `D`. -/
@[simps]
def ForgetEnrichment.equivFunctor (D : Type u') [Category.{v'} D] [EnrichedOrdinaryCategory V D] :
    ForgetEnrichment V D ‚•§ D where
  obj X := ForgetEnrichment.to V X
  map f := (eHomEquiv V).symm (ForgetEnrichment.homTo V f)
  map_id X := by rw [ForgetEnrichment.homTo_id, ‚Üê eHomEquiv_id, Equiv.symm_apply_apply]
  map_comp {X} {Y} {Z} f g :=  Equiv.injective
    (eHomEquiv V (X := ForgetEnrichment.to V X) (Y := ForgetEnrichment.to V Z))
    (by simp [eHomEquiv_comp])

/-- If `D` is already an enriched ordinary category, it is equivalent to `ForgetEnrichment V D`. -/
@[simps]
def ForgetEnrichment.equiv {D : Type u'} [Category.{v'} D] [EnrichedOrdinaryCategory V D] :
    ForgetEnrichment V D ‚âå D where
  functor := equivFunctor V D
  inverse := equivInverse V D
  unitIso := NatIso.ofComponents (fun X => Iso.refl _)
  counitIso := NatIso.ofComponents (fun X => Iso.refl _)
  functor_unitIso_comp X := Equiv.injective
    (eHomEquiv V (X := ForgetEnrichment.to V X) (Y := ForgetEnrichment.to V X)) (by simp)

/-- enriched coyoneda functor `(X ‚ü∂[V] _) : C ‚•§ V`. -/
abbrev eCoyoneda (X : C) := (eHomFunctor V C).obj (op X)

section TransportEnrichment

variable {V} {W : Type u''} [Category.{v''} W] [MonoidalCategory W]
  (F : V ‚•§ W) [F.LaxMonoidal]
  (C)

instance : Category (TransportEnrichment F C) := inferInstanceAs (Category C)

/-- If `C` is an ordinary enriched category, the category structure on `TransportEnrichment F C`
is trivially equivalent to the one on `C` itself. -/
def TransportEnrichment.ofOrdinaryEnrichedCategoryEquiv : TransportEnrichment F C ‚âå C :=
  Equivalence.refl

open EnrichedCategory

/-- If for a lax monoidal functor `F : V ‚•§ W` the canonical function
`(ùüô_ V ‚ü∂ v) ‚Üí (ùüô_ W ‚ü∂ F.obj v)` is bijective, and `C` is an enriched ordinary category on `V`,
then `F` induces the structure of a `W`-enriched ordinary category on `TransportEnrichment F C`,
i.e. on the same underlying category `C`. -/
noncomputable def TransportEnrichment.enrichedOrdinaryCategory
    (h : ‚àÄ v : V, Function.Bijective fun (f : ùüô_ V ‚ü∂ v) => Functor.LaxMonoidal.Œµ F ‚â´ F.map f) :
    EnrichedOrdinaryCategory W (TransportEnrichment F C) where
  homEquiv {X Y} := (eHomEquiv V (C := C)).trans <| Equiv.ofBijective _ (h (Hom (C := C) X Y))
  homEquiv_comp f g := by
    simp [eHomEquiv_comp, eComp_eq, tensorHom_def (Functor.LaxMonoidal.Œµ F), unitors_inv_equal]

section Equiv

variable {W : Type u''} [Category.{v''} W] [MonoidalCategory W]
  (F : V ‚•§ W) [F.LaxMonoidal]
  (D : Type u) [EnrichedCategory V D]
  (e : ‚àÄ v : V, (ùüô_ V ‚ü∂ v) ‚âÉ (ùüô_ W ‚ü∂ F.obj v))
  (h : ‚àÄ (v : V) (f : ùüô_ V ‚ü∂ v), (e v) f = Functor.LaxMonoidal.Œµ F ‚â´ F.map f)

/-- The functor that makes up `TransportEnrichment.forgetEnrichmentEquiv`. -/
@[simps]
def TransportEnrichment.forgetEnrichmentEquivFunctor :
    TransportEnrichment F (ForgetEnrichment V D) ‚•§
      ForgetEnrichment W (TransportEnrichment F D) where
  obj X := ForgetEnrichment.of W X
  map {X} {Y} f := ForgetEnrichment.homOf W <| (e (Hom (C := ForgetEnrichment V D) X Y)) <|
    ForgetEnrichment.homTo V f
  map_id X := by
    rw [h, ForgetEnrichment.homTo_id, ‚Üê TransportEnrichment.eId_eq]
    simp [ForgetEnrichment.to]
  map_comp f g := by
    rw [h, h, h, ForgetEnrichment.homTo_comp, F.map_comp, F.map_comp, ‚Üê Category.assoc,
      ‚Üê Functor.LaxMonoidal.left_unitality_inv, Category.assoc, Category.assoc, Category.assoc,
      Category.assoc, ‚Üê Functor.LaxMonoidal.Œº_natural_assoc, ‚Üê TransportEnrichment.eComp_eq,
      ‚Üê ForgetEnrichment.homOf_comp, leftUnitor_inv_naturality_assoc, ‚Üê tensorHom_def'_assoc,
      ‚Üê tensor_comp_assoc]
    rfl

/-- The inverse functor that makes up `TransportEnrichment.forgetEnrichmentEquiv`. -/
@[simps]
def TransportEnrichment.forgetEnrichmentEquivInverse :
    ForgetEnrichment W (TransportEnrichment F D) ‚•§ TransportEnrichment F (ForgetEnrichment V D)
      where
  obj X := ForgetEnrichment.of V (ForgetEnrichment.to (C := TransportEnrichment F D) W X)
  map f := ForgetEnrichment.homOf V ((e _).symm (ForgetEnrichment.homTo W f))
  map_id X := by
    rw [‚Üê ForgetEnrichment.homOf_eId]
    congr 1
    apply Equiv.injective (e _)
    rw [ForgetEnrichment.homTo_id, Equiv.apply_symm_apply, h, TransportEnrichment.eId_eq]
  map_comp f g := by
    rw [‚Üê ForgetEnrichment.homOf_comp]
    congr
    apply Equiv.injective (e _)
    rw [Equiv.apply_symm_apply, h]
    simp only [ForgetEnrichment.homTo_comp, eComp_eq, Category.assoc, Functor.map_comp]
    slice_rhs 1 3 =>
      rw [‚Üê Functor.LaxMonoidal.left_unitality_inv, Category.assoc, Category.assoc,
        ‚Üê Functor.LaxMonoidal.Œº_natural, ‚Üê leftUnitor_inv_comp_tensorHom_assoc,
        ‚Üê tensor_comp_assoc]
    simp [‚Üê h]

/-- If `D` is a `V`-enriched category, then forgetting the enrichment and transporting the resulting
enriched ordinary category along a functor `F : V ‚•§ W`, for which
`f ‚Ü¶ Functor.LaxMonoidal.Œµ F ‚â´ F.map f` has an inverse, results in a category equivalent to
transporting along `F` and then forgetting about the resulting `W`-enrichment. -/
@[simps]
def TransportEnrichment.forgetEnrichmentEquiv : TransportEnrichment F (ForgetEnrichment V D) ‚âå
    ForgetEnrichment W (TransportEnrichment F D) where
  functor := forgetEnrichmentEquivFunctor _ _ e h
  inverse := forgetEnrichmentEquivInverse _ _ e h
  unitIso := NatIso.ofComponents (fun _ => Iso.refl _) (by simp)
  counitIso := NatIso.ofComponents (fun _ => Iso.refl _) fun f => by
    simp [ForgetEnrichment.to, ForgetEnrichment.of]
  functor_unitIso_comp X := by
    simp only [Functor.id_obj, forgetEnrichmentEquivFunctor_obj, Functor.comp_obj,
      forgetEnrichmentEquivInverse_obj, ForgetEnrichment.to_of, NatIso.ofComponents_hom_app,
      Iso.refl_hom, forgetEnrichmentEquivFunctor_map, h, Category.comp_id]
    rw [‚Üê ForgetEnrichment.homOf_eId, TransportEnrichment.eId_eq, ForgetEnrichment.homTo_id]
    rfl

end Equiv

end TransportEnrichment

end CategoryTheory
