/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Enriched.Basic
import Mathlib.CategoryTheory.Monoidal.Types.Coyoneda

/-!
# Enriched ordinary categories

If `V` is a monoidal category, a `V`-enriched category `C` does not need
to be a category. However, when we have both `Category C` and `EnrichedCategory V C`,
we may require that the type of morphisms `X âŸ¶ Y` in `C` identify to
`ğŸ™_ V âŸ¶ EnrichedCategory.Hom X Y`. This data shall be packaged in the
typeclass `EnrichedOrdinaryCategory V C`.

In particular, if `C` is a `V`-enriched category, it is shown that
the "underlying" category `ForgetEnrichment V C` is equipped with a
`EnrichedOrdinaryCategory V C` instance.

Simplicial categories are implemented in `AlgebraicTopology.SimplicialCategory.Basic`
using an abbreviation for `EnrichedOrdinaryCategory SSet C`.

-/

universe v' v u u'

open CategoryTheory Category MonoidalCategory Opposite

namespace CategoryTheory

variable (V : Type u') [Category.{v'} V] [MonoidalCategory V]
  (C : Type u) [Category.{v} C]

/-- An enriched ordinary category is a category `C` that is also enriched
over a category `V` in such a way that morphisms `X âŸ¶ Y` in `C` identify
to morphisms `ğŸ™_ V âŸ¶ (X âŸ¶[V] Y)` in `V`. -/
class EnrichedOrdinaryCategory extends EnrichedCategory V C where
  /-- morphisms `X âŸ¶ Y` in the category identify morphisms
    `ğŸ™_ V âŸ¶ (X âŸ¶[V] Y)` in `V` -/
  homEquiv {X Y : C} : (X âŸ¶ Y) â‰ƒ (ğŸ™_ V âŸ¶ (X âŸ¶[V] Y))
  homEquiv_id (X : C) : homEquiv (ğŸ™ X) = eId V X := by aesop_cat
  homEquiv_comp {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    homEquiv (f â‰« g) = (Î»_ _).inv â‰« (homEquiv f âŠ—â‚˜ homEquiv g) â‰«
      eComp V X Y Z := by aesop_cat

variable [EnrichedOrdinaryCategory V C] {C}

/-- The bijection `(X âŸ¶ Y) â‰ƒ (ğŸ™_ V âŸ¶ (X âŸ¶[V] Y))` given by a
`EnrichedOrdinaryCategory` instance. -/
def eHomEquiv {X Y : C} : (X âŸ¶ Y) â‰ƒ (ğŸ™_ V âŸ¶ (X âŸ¶[V] Y)) :=
  EnrichedOrdinaryCategory.homEquiv

@[simp]
lemma eHomEquiv_id (X : C) : eHomEquiv V (ğŸ™ X) = eId V X :=
  EnrichedOrdinaryCategory.homEquiv_id _

@[simp]
lemma eHomEquiv_symm_id (X : C) : (eHomEquiv V).symm (eId V X) = ğŸ™ X := by
  rw [â† eHomEquiv_id, (eHomEquiv V).symm_apply_apply]

@[reassoc]
lemma eHomEquiv_comp {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    eHomEquiv V (f â‰« g) = (Î»_ _).inv â‰« (eHomEquiv V f âŠ—â‚˜ eHomEquiv V g) â‰« eComp V X Y Z :=
  EnrichedOrdinaryCategory.homEquiv_comp _ _

lemma eHomEquiv_symm_comp {X Y Z : C} (f : ğŸ™_ V âŸ¶ (X âŸ¶[V] Y)) (g : ğŸ™_ V âŸ¶ (Y âŸ¶[V] Z)) :
    (eHomEquiv V).symm ((Î»_ _).inv â‰« (f âŠ—â‚˜ g) â‰« eComp V X Y Z) =
    (eHomEquiv V).symm f â‰« (eHomEquiv V).symm g := by
  apply (eHomEquiv V).injective
  simp [eHomEquiv_comp]

/-- The morphism `(X' âŸ¶[V] Y) âŸ¶ (X âŸ¶[V] Y)` induced by a morphism `X âŸ¶ X'`. -/
def eHomWhiskerRight {X X' : C} (f : X âŸ¶ X') (Y : C) :
    (X' âŸ¶[V] Y) âŸ¶ (X âŸ¶[V] Y) :=
  (Î»_ _).inv â‰« eHomEquiv V f â–· _ â‰« eComp V X X' Y

@[simp]
lemma eHomWhiskerRight_id (X Y : C) : eHomWhiskerRight V (ğŸ™ X) Y = ğŸ™ _ := by
  simp [eHomWhiskerRight]

@[simp, reassoc]
lemma eHomWhiskerRight_comp {X X' X'' : C} (f : X âŸ¶ X') (f' : X' âŸ¶ X'') (Y : C) :
    eHomWhiskerRight V (f â‰« f') Y = eHomWhiskerRight V f' Y â‰« eHomWhiskerRight V f Y := by
  dsimp [eHomWhiskerRight]
  rw [assoc, assoc, eHomEquiv_comp, comp_whiskerRight_assoc, comp_whiskerRight_assoc, â† e_assoc',
    tensorHom_def', comp_whiskerRight_assoc, id_whiskerLeft, comp_whiskerRight_assoc,
    â† comp_whiskerRight_assoc, Iso.inv_hom_id, id_whiskerRight_assoc,
    comp_whiskerRight_assoc, leftUnitor_inv_whiskerRight_assoc,
    â† associator_inv_naturality_left_assoc, Iso.inv_hom_id_assoc,
    â† whisker_exchange_assoc, id_whiskerLeft_assoc, Iso.inv_hom_id_assoc]

/-- Whiskering commutes with the enriched composition. -/
@[reassoc]
lemma eComp_eHomWhiskerRight {X X' : C} (f : X âŸ¶ X') (Y Z : C) :
    eComp V X' Y Z â‰« eHomWhiskerRight V f Z =
      eHomWhiskerRight V f Y â–· _ â‰« eComp V X Y Z := by
  dsimp [eHomWhiskerRight]
  rw [leftUnitor_inv_naturality_assoc, whisker_exchange_assoc]
  simp [e_assoc']

/-- The morphism `(X âŸ¶[V] Y) âŸ¶ (X âŸ¶[V] Y')` induced by a morphism `Y âŸ¶ Y'`. -/
def eHomWhiskerLeft (X : C) {Y Y' : C} (g : Y âŸ¶ Y') :
    (X âŸ¶[V] Y) âŸ¶ (X âŸ¶[V] Y') :=
  (Ï_ _).inv â‰« _ â— eHomEquiv V g â‰« eComp V X Y Y'

@[simp]
lemma eHomWhiskerLeft_id (X Y : C) : eHomWhiskerLeft V X (ğŸ™ Y) = ğŸ™ _ := by
  simp [eHomWhiskerLeft]

@[simp, reassoc]
lemma eHomWhiskerLeft_comp (X : C) {Y Y' Y'' : C} (g : Y âŸ¶ Y') (g' : Y' âŸ¶ Y'') :
    eHomWhiskerLeft V X (g â‰« g') = eHomWhiskerLeft V X g â‰« eHomWhiskerLeft V X g' := by
  dsimp [eHomWhiskerLeft]
  rw [assoc, assoc, eHomEquiv_comp, MonoidalCategory.whiskerLeft_comp_assoc,
    MonoidalCategory.whiskerLeft_comp_assoc, â† e_assoc, tensorHom_def,
    MonoidalCategory.whiskerRight_id_assoc, MonoidalCategory.whiskerLeft_comp_assoc,
    MonoidalCategory.whiskerLeft_comp_assoc, MonoidalCategory.whiskerLeft_comp_assoc,
    whiskerLeft_rightUnitor_assoc, whiskerLeft_rightUnitor_inv_assoc,
    triangle_assoc_comp_left_inv_assoc, MonoidalCategory.whiskerRight_id_assoc,
    Iso.hom_inv_id_assoc, Iso.inv_hom_id_assoc,
    associator_inv_naturality_right_assoc, Iso.hom_inv_id_assoc,
    whisker_exchange_assoc, MonoidalCategory.whiskerRight_id_assoc, Iso.inv_hom_id_assoc]

/-- Whiskering commutes with the enriched composition. -/
@[reassoc]
lemma eComp_eHomWhiskerLeft (X Y : C) {Z Z' : C} (g : Z âŸ¶ Z') :
    eComp V X Y Z â‰« eHomWhiskerLeft V X g =
      _ â— eHomWhiskerLeft V Y g â‰« eComp V X Y Z' := by
  dsimp [eHomWhiskerLeft]
  rw [rightUnitor_inv_naturality_assoc, â† whisker_exchange_assoc]
  simp

/-- Given an isomorphism `Î± : Y â‰… Yâ‚` in C, the enriched composition map
`eComp V X Y Z : (X âŸ¶[V] Y) âŠ— (Y âŸ¶[V] Z) âŸ¶ (X âŸ¶[V] Z)` factors through the `V`
object `(X âŸ¶[V] Yâ‚) âŠ— (Yâ‚ âŸ¶[V] Z)` via the map defined by whiskering in the
middle with `Î±.hom` and `Î±.inv`. -/
@[reassoc]
lemma eHom_whisker_cancel {X Y Yâ‚ Z : C} (Î± : Y â‰… Yâ‚) :
    eHomWhiskerLeft V X Î±.hom â–· _ â‰« _ â— eHomWhiskerRight V Î±.inv Z â‰«
      eComp V X Yâ‚ Z = eComp V X Y Z := by
  dsimp [eHomWhiskerLeft, eHomWhiskerRight]
  simp only [MonoidalCategory.whiskerLeft_comp_assoc, whisker_assoc_symm,
    triangle_assoc_comp_left_inv_assoc, e_assoc', assoc]
  simp only [â† comp_whiskerRight_assoc]
  change (eHomWhiskerLeft V X Î±.hom â‰« eHomWhiskerLeft V X Î±.inv) â–· _ â‰« _ = _
  simp [â† eHomWhiskerLeft_comp]

@[reassoc]
lemma eHom_whisker_cancel_inv {X Y Yâ‚ Z : C} (Î± : Y â‰… Yâ‚) :
    eHomWhiskerLeft V X Î±.inv â–· _ â‰« _ â— eHomWhiskerRight V Î±.hom Z â‰«
      eComp V X Y Z = eComp V X Yâ‚ Z := eHom_whisker_cancel V Î±.symm

@[reassoc]
lemma eHom_whisker_exchange {X X' Y Y' : C} (f : X âŸ¶ X') (g : Y âŸ¶ Y') :
    eHomWhiskerLeft V X' g â‰« eHomWhiskerRight V f Y' =
      eHomWhiskerRight V f Y â‰« eHomWhiskerLeft V X g := by
  dsimp [eHomWhiskerLeft, eHomWhiskerRight]
  rw [assoc, assoc, assoc, assoc, leftUnitor_inv_naturality_assoc,
    whisker_exchange_assoc, â† e_assoc, leftUnitor_tensor_inv_assoc,
    associator_inv_naturality_left_assoc, Iso.hom_inv_id_assoc,
    â† comp_whiskerRight_assoc, whisker_exchange_assoc,
    MonoidalCategory.whiskerRight_id_assoc, assoc, Iso.inv_hom_id_assoc,
    whisker_exchange_assoc, MonoidalCategory.whiskerRight_id_assoc, Iso.inv_hom_id_assoc]

/-- Obtain an `V`-enriched isomorphism from an isomorphism in a `V`-enriched ordinary category. -/
@[simps]
def EnrichedIso.ofIso {X Y : C} (I : X â‰… Y) : EnrichedIso V X Y where
  hom := eHomEquiv V I.hom
  inv := eHomEquiv V I.inv
  hom_inv := by simp [â† eHomEquiv_comp]
  inv_hom := by simp [â† eHomEquiv_comp]

@[simp]
lemma EnrichedIso.ofIso_refl {X : C} : EnrichedIso.ofIso V (Iso.refl X) = .refl X := by
  ext <;> simp [â† eHomEquiv_id]

@[simp]
lemma EnrichedIso.ofIso_symm {X Y : C} (I : X â‰… Y) :
    (EnrichedIso.ofIso V I).symm = .ofIso _ I.symm := by
  ext <;> simp

@[simp]
lemma EnrichedIso.ofIso_trans {X Y Z : C} (I : X â‰… Y) (J : Y â‰… Z) :
    (EnrichedIso.ofIso V I).trans (.ofIso V J) = .ofIso _ (I.trans J) := by
  ext <;> simp [â† eHomEquiv_comp]

/-- Obtain an iso in a `V`-enriched ordinary category from a `V`-enriched isomorphism. -/
@[simps]
def EnrichedIso.iso {X Y : C} (I : EnrichedIso V X Y) : X â‰… Y where
  hom := (eHomEquiv V).symm I.hom
  inv := (eHomEquiv V).symm I.inv
  hom_inv_id := by rw [â† eHomEquiv_symm_comp V I.hom I.inv, I.hom_inv, eHomEquiv_symm_id]
  inv_hom_id := by rw [â† eHomEquiv_symm_comp V I.inv I.hom, I.inv_hom, eHomEquiv_symm_id]

/-- The type of `V`-enriched isomorphisms is equivalent to the type of isomorphisms in a
`V`-enriched ordinary category. -/
@[simps]
def EnrichedIso.equivIso (X Y : C) : EnrichedIso V X Y â‰ƒ (X â‰… Y) where
  toFun := iso V
  invFun := ofIso V
  left_inv I := by ext <;> simp
  right_inv I := by ext; simp

@[simp]
lemma EnrichedIso.iso_refl {X : C} : EnrichedIso.iso V (.refl X) = .refl X := by
  ext; simp

@[simp]
lemma EnrichedIso.iso_symm {X Y : C} (I : EnrichedIso V X Y) :
    EnrichedIso.iso V I.symm = (EnrichedIso.iso V I).symm := by
  ext; simp

@[simp]
lemma EnrichedIso.iso_tranx {X Y Z : C} (I : EnrichedIso V X Y) (J : EnrichedIso V Y Z) :
    EnrichedIso.iso V (I.trans J) = (EnrichedIso.iso V I).trans (EnrichedIso.iso V J) := by
  ext; simp [eHomEquiv_symm_comp]

attribute [local simp] eHom_whisker_exchange

variable (C) in
/-- The bifunctor `Cáµ’áµ– â¥¤ C â¥¤ V` which sends `X : Cáµ’áµ–` and `Y : C` to `X âŸ¶[V] Y`. -/
@[simps]
def eHomFunctor : Cáµ’áµ– â¥¤ C â¥¤ V where
  obj X :=
    { obj := fun Y => X.unop âŸ¶[V] Y
      map := fun Ï† => eHomWhiskerLeft V X.unop Ï† }
  map Ï† :=
    { app := fun Y => eHomWhiskerRight V Ï†.unop Y }

instance ForgetEnrichment.EnrichedOrdinaryCategory {D : Type*} [EnrichedCategory V D] :
    EnrichedOrdinaryCategory V (ForgetEnrichment V D) where
  toEnrichedCategory := inferInstanceAs (EnrichedCategory V D)
  homEquiv := Equiv.refl _
  homEquiv_id _ := Category.id_comp _
  homEquiv_comp _ _ := Category.assoc _ _ _

/-- enriched coyoneda functor `(X âŸ¶[V] _) : C â¥¤ V`. -/
abbrev eCoyoneda (X : C) := (eHomFunctor V C).obj (op X)

end CategoryTheory
