/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.Algebra.Group.Basic
import Mathlib.Data.Int.Cast.Defs
import Mathlib.CategoryTheory.Shift.Basic
import Mathlib.CategoryTheory.ConcreteCategory.Basic

/-!
# Differential objects in a category.

A differential object in a category with zero morphisms and a shift is
an object `X` equipped with
a morphism `d : obj âŸ¶ objâŸ¦1âŸ§`, such that `d^2 = 0`.

We build the category of differential objects, and some basic constructions
such as the forgetful functor, zero morphisms and zero objects, and the shift functor
on differential objects.
-/


open CategoryTheory.Limits

universe v u

namespace CategoryTheory

variable (S : Type*) [AddMonoidWithOne S] (C : Type u) [Category.{v} C]
variable [HasZeroMorphisms C] [HasShift C S]

/-- A differential object in a category with zero morphisms and a shift is
an object `obj` equipped with
a morphism `d : obj âŸ¶ objâŸ¦1âŸ§`, such that `d^2 = 0`. -/
structure DifferentialObject where
  /-- The underlying object of a differential object. -/
  obj : C
  /-- The differential of a differential object. -/
  d : obj âŸ¶ objâŸ¦(1 : S)âŸ§
  /-- The differential `d` satisfies that `dÂ² = 0`. -/
  d_squared : d â‰« dâŸ¦(1 : S)âŸ§' = 0 := by cat_disch

attribute [reassoc (attr := simp)] DifferentialObject.d_squared

variable {S C}

namespace DifferentialObject

/-- A morphism of differential objects is a morphism commuting with the differentials. -/
@[ext]
structure Hom (X Y : DifferentialObject S C) where
  /-- The morphism between underlying objects of the two differentiable objects. -/
  f : X.obj âŸ¶ Y.obj
  comm : X.d â‰« fâŸ¦1âŸ§' = f â‰« Y.d := by cat_disch

attribute [reassoc (attr := simp)] Hom.comm

namespace Hom

/-- The identity morphism of a differential object. -/
@[simps]
def id (X : DifferentialObject S C) : Hom X X where
  f := ðŸ™ X.obj

/-- The composition of morphisms of differential objects. -/
@[simps]
def comp {X Y Z : DifferentialObject S C} (f : Hom X Y) (g : Hom Y Z) : Hom X Z where
  f := f.f â‰« g.f

end Hom

instance categoryOfDifferentialObjects : Category (DifferentialObject S C) where
  Hom := Hom
  id := Hom.id
  comp f g := Hom.comp f g

@[ext]
theorem ext {A B : DifferentialObject S C} {f g : A âŸ¶ B} (w : f.f = g.f := by cat_disch) : f = g :=
  Hom.ext w

@[simp]
theorem id_f (X : DifferentialObject S C) : (ðŸ™ X : X âŸ¶ X).f = ðŸ™ X.obj := rfl

@[simp]
theorem comp_f {X Y Z : DifferentialObject S C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    (f â‰« g).f = f.f â‰« g.f :=
  rfl

@[simp]
theorem eqToHom_f {X Y : DifferentialObject S C} (h : X = Y) :
    Hom.f (eqToHom h) = eqToHom (congr_arg _ h) := by
  subst h
  rw [eqToHom_refl, eqToHom_refl]
  rfl

variable (S C)

/-- The forgetful functor taking a differential object to its underlying object. -/
def forget : DifferentialObject S C â¥¤ C where
  obj X := X.obj
  map f := f.f

instance forget_faithful : (forget S C).Faithful where

variable {S C}

section
variable [(shiftFunctor C (1 : S)).PreservesZeroMorphisms]

instance {X Y : DifferentialObject S C} : Zero (X âŸ¶ Y) := âŸ¨{f := 0}âŸ©

@[simp]
theorem zero_f (P Q : DifferentialObject S C) : (0 : P âŸ¶ Q).f = 0 := rfl

instance hasZeroMorphisms : HasZeroMorphisms (DifferentialObject S C) where

end

/-- An isomorphism of differential objects gives an isomorphism of the underlying objects. -/
@[simps]
def isoApp {X Y : DifferentialObject S C} (f : X â‰… Y) : X.obj â‰… Y.obj where
  hom := f.hom.f
  inv := f.inv.f
  hom_inv_id := by rw [â† comp_f, Iso.hom_inv_id, id_f]
  inv_hom_id := by rw [â† comp_f, Iso.inv_hom_id, id_f]

@[simp]
theorem isoApp_refl (X : DifferentialObject S C) : isoApp (Iso.refl X) = Iso.refl X.obj := rfl

@[simp]
theorem isoApp_symm {X Y : DifferentialObject S C} (f : X â‰… Y) : isoApp f.symm = (isoApp f).symm :=
  rfl

@[simp]
theorem isoApp_trans {X Y Z : DifferentialObject S C} (f : X â‰… Y) (g : Y â‰… Z) :
    isoApp (f â‰ªâ‰« g) = isoApp f â‰ªâ‰« isoApp g := rfl

/-- An isomorphism of differential objects can be constructed
from an isomorphism of the underlying objects that commutes with the differentials. -/
@[simps]
def mkIso {X Y : DifferentialObject S C} (f : X.obj â‰… Y.obj) (hf : X.d â‰« f.homâŸ¦1âŸ§' = f.hom â‰« Y.d) :
    X â‰… Y where
  hom := âŸ¨f.hom, hfâŸ©
  inv := âŸ¨f.inv, by
    rw [â† Functor.mapIso_inv, Iso.comp_inv_eq, Category.assoc, Iso.eq_inv_comp, Functor.mapIso_hom,
      hf]âŸ©
  hom_inv_id := by ext1; dsimp; exact f.hom_inv_id
  inv_hom_id := by ext1; dsimp; exact f.inv_hom_id

end DifferentialObject

namespace Functor

universe v' u'

variable (D : Type u') [Category.{v'} D]
variable [HasZeroMorphisms D] [HasShift D S]

/-- A functor `F : C â¥¤ D` which commutes with shift functors on `C` and `D` and preserves zero
morphisms can be lifted to a functor `DifferentialObject S C â¥¤ DifferentialObject S D`. -/
@[simps]
def mapDifferentialObject (F : C â¥¤ D)
    (Î· : (shiftFunctor C (1 : S)).comp F âŸ¶ F.comp (shiftFunctor D (1 : S)))
    (hF : âˆ€ c c', F.map (0 : c âŸ¶ c') = 0) : DifferentialObject S C â¥¤ DifferentialObject S D where
  obj X :=
    { obj := F.obj X.obj
      d := F.map X.d â‰« Î·.app X.obj
      d_squared := by
        rw [Functor.map_comp, â† Functor.comp_map F (shiftFunctor D (1 : S))]
        slice_lhs 2 3 => rw [â† Î·.naturality X.d]
        rw [Functor.comp_map]
        slice_lhs 1 2 => rw [â† F.map_comp, X.d_squared, hF]
        rw [zero_comp, zero_comp] }
  map f :=
    { f := F.map f.f
      comm := by
        dsimp
        slice_lhs 2 3 => rw [â† Functor.comp_map F (shiftFunctor D (1 : S)), â† Î·.naturality f.f]
        slice_lhs 1 2 => rw [Functor.comp_map, â† F.map_comp, f.comm, F.map_comp]
        rw [Category.assoc] }
  map_id := by intros; ext; simp
  map_comp := by intros; ext; simp

end Functor

end CategoryTheory

namespace CategoryTheory

namespace DifferentialObject

variable (S : Type*) [AddMonoidWithOne S] (C : Type u) [Category.{v} C]
variable [HasZeroObject C] [HasZeroMorphisms C] [HasShift C S]
variable [(shiftFunctor C (1 : S)).PreservesZeroMorphisms]

open scoped ZeroObject

instance hasZeroObject : HasZeroObject (DifferentialObject S C) where
  zero := âŸ¨{ obj := 0, d := 0 },
    { unique_to := fun X => âŸ¨âŸ¨âŸ¨{ f := 0 }âŸ©, fun f => by extâŸ©âŸ©,
      unique_from := fun X => âŸ¨âŸ¨âŸ¨{ f := 0 }âŸ©, fun f => by extâŸ©âŸ© }âŸ©

end DifferentialObject

namespace DifferentialObject

section HasForget

variable (S : Type*) [AddMonoidWithOne S]
variable (C : Type (u + 1)) [LargeCategory C] [HasForget C] [HasZeroMorphisms C]
variable [HasShift C S]

instance hasForgetOfDifferentialObjects : HasForget (DifferentialObject S C) where
  forget := forget S C â‹™ CategoryTheory.forget C

instance : HasForgetâ‚‚ (DifferentialObject S C) C where
  forgetâ‚‚ := forget S C

end HasForget

section ConcreteCategory

variable (S : Type*) [AddMonoidWithOne S]
variable (C : Type (u + 1)) [LargeCategory C] [HasZeroMorphisms C]
variable {FC : C â†’ C â†’ Type*} {CC : C â†’ Type*} [âˆ€ X Y, FunLike (FC X Y) (CC X) (CC Y)]
variable [ConcreteCategory C FC] [HasShift C S]

/--
The type of `C`-morphisms that can be lifted back to morphisms in the category `DifferentialObject`.
-/
abbrev HomSubtype (X Y : DifferentialObject S C) :=
  { f : FC X.obj Y.obj // X.d â‰« (ConcreteCategory.ofHom f)âŸ¦1âŸ§' = (ConcreteCategory.ofHom f) â‰« Y.d }

instance (X Y : DifferentialObject S C) :
    FunLike (HomSubtype S C X Y) (CC X.obj) (CC Y.obj) where
  coe f := f.1
  coe_injective' _ _ h := Subtype.ext (DFunLike.coe_injective h)

instance concreteCategoryOfDifferentialObjects :
    ConcreteCategory (DifferentialObject S C) (HomSubtype S C) where
  hom f := âŸ¨ConcreteCategory.hom (C := C) f.1, by simp [ConcreteCategory.ofHom_hom]âŸ©
  ofHom f := âŸ¨ConcreteCategory.ofHom (C := C) f, by simpa [ConcreteCategory.hom_ofHom] using f.2âŸ©
  hom_ofHom _ := by dsimp; ext; simp [ConcreteCategory.hom_ofHom]
  ofHom_hom _ := by ext; simp [ConcreteCategory.ofHom_hom]
  id_apply := ConcreteCategory.id_apply (C := C)
  comp_apply _ _ := ConcreteCategory.comp_apply (C := C) _ _

end ConcreteCategory

end DifferentialObject

/-! The category of differential objects itself has a shift functor. -/


namespace DifferentialObject

variable {S : Type*} [AddCommGroupWithOne S] (C : Type u) [Category.{v} C]
variable [HasZeroMorphisms C] [HasShift C S]

noncomputable section

/-- The shift functor on `DifferentialObject S C`. -/
@[simps]
def shiftFunctor (n : S) : DifferentialObject S C â¥¤ DifferentialObject S C where
  obj X :=
    { obj := X.objâŸ¦nâŸ§
      d := X.dâŸ¦nâŸ§' â‰« (shiftComm _ _ _).hom
      d_squared := by
        rw [Functor.map_comp, Category.assoc, shiftComm_hom_comp_assoc, â† Functor.map_comp_assoc,
          X.d_squared, Functor.map_zero, zero_comp] }
  map f :=
    { f := f.fâŸ¦nâŸ§'
      comm := by
        dsimp
        rw [Category.assoc]
        erw [shiftComm_hom_comp]
        rw [â† Functor.map_comp_assoc, f.comm, Functor.map_comp_assoc]
        rfl }
  map_id X := by ext1; dsimp; rw [Functor.map_id]
  map_comp f g := by ext1; dsimp; rw [Functor.map_comp]

/-- The shift functor on `DifferentialObject S C` is additive. -/
@[simps!]
nonrec def shiftFunctorAdd (m n : S) :
    shiftFunctor C (m + n) â‰… shiftFunctor C m â‹™ shiftFunctor C n := by
  refine NatIso.ofComponents (fun X => mkIso (shiftAdd X.obj _ _) ?_) (fun f => ?_)
  Â· dsimp
    rw [â† cancel_epi ((shiftFunctorAdd C m n).inv.app X.obj)]
    simp only [Category.assoc, Iso.inv_hom_id_app_assoc]
    rw [â† NatTrans.naturality_assoc]
    dsimp
    simp only [Functor.map_comp, Category.assoc,
      shiftFunctorComm_hom_app_comp_shift_shiftFunctorAdd_hom_app 1 m n X.obj,
      Iso.inv_hom_id_app_assoc]
  Â· ext; dsimp; exact NatTrans.naturality _ _

section

/-- The shift by zero is naturally isomorphic to the identity. -/
@[simps!]
def shiftZero : shiftFunctor C (0 : S) â‰… ðŸ­ (DifferentialObject S C) := by
  refine NatIso.ofComponents (fun X => mkIso ((shiftFunctorZero C S).app X.obj) ?_) (fun f => ?_)
  Â· erw [â† NatTrans.naturality]
    dsimp
    simp only [shiftFunctorZero_hom_app_shift, Category.assoc]
  Â· cat_disch

end

instance : HasShift (DifferentialObject S C) S :=
  hasShiftMk _ _
    { F := shiftFunctor C
      zero := shiftZero C
      add := shiftFunctorAdd C
      assoc_hom_app := fun mâ‚ mâ‚‚ mâ‚ƒ X => by
        ext1
        convert shiftFunctorAdd_assoc_hom_app mâ‚ mâ‚‚ mâ‚ƒ X.obj
        dsimp [shiftFunctorAdd']
        simp
      zero_add_hom_app := fun n X => by
        ext1
        convert shiftFunctorAdd_zero_add_hom_app n X.obj
        simp
      add_zero_hom_app := fun n X => by
        ext1
        convert shiftFunctorAdd_add_zero_hom_app n X.obj
        simp }

end

end DifferentialObject

end CategoryTheory
