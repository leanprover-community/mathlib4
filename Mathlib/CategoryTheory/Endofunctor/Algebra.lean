/-
Copyright (c) 2022 Joseph Hua. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Bhavik Mehta, Johan Commelin, Reid Barton, Robert Y. Lewis, Joseph Hua
-/
import Mathlib.CategoryTheory.Limits.Shapes.IsTerminal
import Mathlib.CategoryTheory.Functor.EpiMono

/-!

# Algebras of endofunctors

This file defines (co)algebras of an endofunctor, and provides the category instance for them.
It also defines the forgetful functor from the category of (co)algebras. It is shown that the
structure map of the initial algebra of an endofunctor is an isomorphism. Furthermore, it is shown
that for an adjunction `F âŠ£ G` the category of algebras over `F` is equivalent to the category of
coalgebras over `G`.

## TODO

* Prove that if the countable infinite product over the powers of the endofunctor exists, then
  algebras over the endofunctor coincide with algebras over the free monad on the endofunctor.
-/


universe v u

namespace CategoryTheory

namespace Endofunctor

variable {C : Type u} [Category.{v} C]

/-- An algebra of an endofunctor; `str` stands for "structure morphism" -/
structure Algebra (F : C â¥¤ C) where
  /-- carrier of the algebra -/
  a : C
  /-- structure morphism of the algebra -/
  str : F.obj a âŸ¶ a

instance [Inhabited C] : Inhabited (Algebra (ğŸ­ C)) :=
  âŸ¨âŸ¨default, ğŸ™ _âŸ©âŸ©

namespace Algebra

variable {F : C â¥¤ C} (A : Algebra F) {Aâ‚€ Aâ‚ Aâ‚‚ : Algebra F}

/-
```
        str
   F Aâ‚€ -----> Aâ‚€
    |          |
F f |          | f
    V          V
   F Aâ‚ -----> Aâ‚
        str
```
-/
/-- A morphism between algebras of endofunctor `F` -/
@[ext]
structure Hom (Aâ‚€ Aâ‚ : Algebra F) where
  /-- underlying morphism between the carriers -/
  f : Aâ‚€.1 âŸ¶ Aâ‚.1
  /-- compatibility condition -/
  h : F.map f â‰« Aâ‚.str = Aâ‚€.str â‰« f := by cat_disch

attribute [reassoc (attr := simp)] Hom.h

namespace Hom

/-- The identity morphism of an algebra of endofunctor `F` -/
def id : Hom A A where f := ğŸ™ _

instance : Inhabited (Hom A A) :=
  âŸ¨{ f := ğŸ™ _ }âŸ©

/-- The composition of morphisms between algebras of endofunctor `F` -/
def comp (f : Hom Aâ‚€ Aâ‚) (g : Hom Aâ‚ Aâ‚‚) : Hom Aâ‚€ Aâ‚‚ where f := f.1 â‰« g.1

end Hom

instance (F : C â¥¤ C) : CategoryStruct (Algebra F) where
  Hom := Hom
  id := Hom.id
  comp := @Hom.comp _ _ _

@[ext]
lemma ext {A B : Algebra F} {f g : A âŸ¶ B} (w : f.f = g.f := by cat_disch) : f = g :=
  Hom.ext w

@[simp]
theorem id_eq_id : Algebra.Hom.id A = ğŸ™ A :=
  rfl

@[simp]
theorem id_f : (ğŸ™ _ : A âŸ¶ A).1 = ğŸ™ A.1 :=
  rfl

variable (f : Aâ‚€ âŸ¶ Aâ‚) (g : Aâ‚ âŸ¶ Aâ‚‚)

@[simp]
theorem comp_eq_comp : Algebra.Hom.comp f g = f â‰« g :=
  rfl

@[simp]
theorem comp_f : (f â‰« g).1 = f.1 â‰« g.1 :=
  rfl

/-- Algebras of an endofunctor `F` form a category -/
instance (F : C â¥¤ C) : Category (Algebra F) := { }

/-- To construct an isomorphism of algebras, it suffices to give an isomorphism of the As which
commutes with the structure morphisms.
-/
@[simps!]
def isoMk (h : Aâ‚€.1 â‰… Aâ‚.1) (w : F.map h.hom â‰« Aâ‚.str = Aâ‚€.str â‰« h.hom := by cat_disch) :
    Aâ‚€ â‰… Aâ‚ where
  hom := { f := h.hom }
  inv :=
    { f := h.inv
      h := by
        rw [h.eq_comp_inv, Category.assoc, â† w, â† Functor.map_comp_assoc]
        simp }

/-- The forgetful functor from the category of algebras, forgetting the algebraic structure. -/
@[simps]
def forget (F : C â¥¤ C) : Algebra F â¥¤ C where
  obj A := A.1
  map := Hom.f

/-- An algebra morphism with an underlying isomorphism hom in `C` is an algebra isomorphism. -/
theorem iso_of_iso (f : Aâ‚€ âŸ¶ Aâ‚) [IsIso f.1] : IsIso f :=
  âŸ¨âŸ¨{ f := inv f.1
      h := by simp }, by cat_disch, by cat_dischâŸ©âŸ©

instance forget_reflects_iso : (forget F).ReflectsIsomorphisms where reflects := iso_of_iso

instance forget_faithful : (forget F).Faithful := { }

/-- An algebra morphism with an underlying epimorphism hom in `C` is an algebra epimorphism. -/
theorem epi_of_epi {X Y : Algebra F} (f : X âŸ¶ Y) [h : Epi f.1] : Epi f :=
  (forget F).epi_of_epi_map h

/-- An algebra morphism with an underlying monomorphism hom in `C` is an algebra monomorphism. -/
theorem mono_of_mono {X Y : Algebra F} (f : X âŸ¶ Y) [h : Mono f.1] : Mono f :=
  (forget F).mono_of_mono_map h

/-- From a natural transformation `Î± : G â†’ F` we get a functor from
algebras of `F` to algebras of `G`.
-/
@[simps]
def functorOfNatTrans {F G : C â¥¤ C} (Î± : G âŸ¶ F) : Algebra F â¥¤ Algebra G where
  obj A :=
    { a := A.1
      str := Î±.app _ â‰« A.str }
  map f := { f := f.1 }

/-- The identity transformation induces the identity endofunctor on the category of algebras. -/
@[simps!]
def functorOfNatTransId : functorOfNatTrans (ğŸ™ F) â‰… ğŸ­ _ :=
  NatIso.ofComponents fun X => isoMk (Iso.refl _)

/-- A composition of natural transformations gives the composition of corresponding functors. -/
@[simps!]
def functorOfNatTransComp {Fâ‚€ Fâ‚ Fâ‚‚ : C â¥¤ C} (Î± : Fâ‚€ âŸ¶ Fâ‚) (Î² : Fâ‚ âŸ¶ Fâ‚‚) :
    functorOfNatTrans (Î± â‰« Î²) â‰… functorOfNatTrans Î² â‹™ functorOfNatTrans Î± :=
  NatIso.ofComponents fun X => isoMk (Iso.refl _)

/--
If `Î±` and `Î²` are two equal natural transformations, then the functors of algebras induced by them
are isomorphic.
We define it like this as opposed to using `eq_to_iso` so that the components are nicer to prove
lemmas about.
-/
@[simps!]
def functorOfNatTransEq {F G : C â¥¤ C} {Î± Î² : F âŸ¶ G} (h : Î± = Î²) :
    functorOfNatTrans Î± â‰… functorOfNatTrans Î² :=
  NatIso.ofComponents fun X => isoMk (Iso.refl _)

/-- Naturally isomorphic endofunctors give equivalent categories of algebras.
Furthermore, they are equivalent as categories over `C`, that is,
we have `equiv_of_nat_iso h â‹™ forget = forget`.
-/
@[simps]
def equivOfNatIso {F G : C â¥¤ C} (Î± : F â‰… G) : Algebra F â‰Œ Algebra G where
  functor := functorOfNatTrans Î±.inv
  inverse := functorOfNatTrans Î±.hom
  unitIso := functorOfNatTransId.symm â‰ªâ‰« functorOfNatTransEq (by simp) â‰ªâ‰« functorOfNatTransComp _ _
  counitIso :=
    (functorOfNatTransComp _ _).symm â‰ªâ‰« functorOfNatTransEq (by simp) â‰ªâ‰« functorOfNatTransId

namespace Initial

variable {A : Algebra F} (h : Limits.IsInitial A)
/-- The inverse of the structure map of an initial algebra -/
@[simp]
def strInv : A.1 âŸ¶ F.obj A.1 :=
  (h.to âŸ¨F.obj A.a, F.map A.strâŸ©).f

theorem left_inv' :
    âŸ¨strInv h â‰« A.str, by rw [â† Category.assoc, F.map_comp, strInv, â† Hom.h]âŸ© = ğŸ™ A :=
  Limits.IsInitial.hom_ext h _ (ğŸ™ A)

theorem left_inv : strInv h â‰« A.str = ğŸ™ _ :=
  congr_arg Hom.f (left_inv' h)

theorem right_inv : A.str â‰« strInv h = ğŸ™ _ := by
  rw [strInv, â† (h.to âŸ¨F.obj A.1, F.map A.strâŸ©).h, â† F.map_id, â† F.map_comp]
  congr
  exact left_inv h

/-- The structure map of the initial algebra is an isomorphism,
hence endofunctors preserve their initial algebras
-/
theorem str_isIso (h : Limits.IsInitial A) : IsIso A.str :=
  { out := âŸ¨strInv h, right_inv _, left_inv _âŸ© }

end Initial

end Algebra

/-- A coalgebra of an endofunctor; `str` stands for "structure morphism" -/
structure Coalgebra (F : C â¥¤ C) where
  /-- carrier of the coalgebra -/
  V : C
  /-- structure morphism of the coalgebra -/
  str : V âŸ¶ F.obj V

instance [Inhabited C] : Inhabited (Coalgebra (ğŸ­ C)) :=
  âŸ¨âŸ¨default, ğŸ™ _âŸ©âŸ©

namespace Coalgebra

variable {F : C â¥¤ C} (V : Coalgebra F) {Vâ‚€ Vâ‚ Vâ‚‚ : Coalgebra F}

/-
```
        str
    Vâ‚€ -----> F Vâ‚€
    |          |
  f |          | F f
    V          V
    Vâ‚ -----> F Vâ‚
        str
```
-/
/-- A morphism between coalgebras of an endofunctor `F` -/
@[ext]
structure Hom (Vâ‚€ Vâ‚ : Coalgebra F) where
  /-- underlying morphism between two carriers -/
  f : Vâ‚€.1 âŸ¶ Vâ‚.1
  /-- compatibility condition -/
  h : Vâ‚€.str â‰« F.map f = f â‰« Vâ‚.str := by cat_disch

attribute [reassoc (attr := simp)] Hom.h

namespace Hom

/-- The identity morphism of an algebra of endofunctor `F` -/
def id : Hom V V where f := ğŸ™ _

instance : Inhabited (Hom V V) :=
  âŸ¨{ f := ğŸ™ _ }âŸ©

/-- The composition of morphisms between algebras of endofunctor `F` -/
def comp (f : Hom Vâ‚€ Vâ‚) (g : Hom Vâ‚ Vâ‚‚) : Hom Vâ‚€ Vâ‚‚ where f := f.1 â‰« g.1

end Hom

instance (F : C â¥¤ C) : CategoryStruct (Coalgebra F) where
  Hom := Hom
  id := Hom.id
  comp := @Hom.comp _ _ _

@[ext]
lemma ext {A B : Coalgebra F} {f g : A âŸ¶ B} (w : f.f = g.f := by cat_disch) : f = g :=
  Hom.ext w

@[simp]
theorem id_eq_id : Coalgebra.Hom.id V = ğŸ™ V :=
  rfl

@[simp]
theorem id_f : (ğŸ™ _ : V âŸ¶ V).1 = ğŸ™ V.1 :=
  rfl

variable (f : Vâ‚€ âŸ¶ Vâ‚) (g : Vâ‚ âŸ¶ Vâ‚‚)

@[simp]
theorem comp_eq_comp : Coalgebra.Hom.comp f g = f â‰« g :=
  rfl

@[simp]
theorem comp_f : (f â‰« g).1 = f.1 â‰« g.1 :=
  rfl

/-- Coalgebras of an endofunctor `F` form a category -/
instance (F : C â¥¤ C) : Category (Coalgebra F) := { }

/-- To construct an isomorphism of coalgebras, it suffices to give an isomorphism of the Vs which
commutes with the structure morphisms.
-/
@[simps]
def isoMk (h : Vâ‚€.1 â‰… Vâ‚.1) (w : Vâ‚€.str â‰« F.map h.hom = h.hom â‰« Vâ‚.str := by cat_disch) :
    Vâ‚€ â‰… Vâ‚ where
  hom := { f := h.hom }
  inv :=
    { f := h.inv
      h := by
        rw [h.eq_inv_comp, â† Category.assoc, â† w, Category.assoc, â† F.map_comp]
        simp only [Iso.hom_inv_id, Functor.map_id, Category.comp_id] }

/-- The forgetful functor from the category of coalgebras, forgetting the coalgebraic structure. -/
@[simps]
def forget (F : C â¥¤ C) : Coalgebra F â¥¤ C where
  obj A := A.1
  map f := f.1

/-- A coalgebra morphism with an underlying isomorphism hom in `C` is a coalgebra isomorphism. -/
theorem iso_of_iso (f : Vâ‚€ âŸ¶ Vâ‚) [IsIso f.1] : IsIso f :=
  âŸ¨âŸ¨{ f := inv f.1
      h := by
        rw [IsIso.eq_inv_comp f.1, â† Category.assoc, â† f.h, Category.assoc]
        simp }, by cat_disch, by cat_dischâŸ©âŸ©

instance forget_reflects_iso : (forget F).ReflectsIsomorphisms where reflects := iso_of_iso

instance forget_faithful : (forget F).Faithful := { }

/-- An algebra morphism with an underlying epimorphism hom in `C` is an algebra epimorphism. -/
theorem epi_of_epi {X Y : Coalgebra F} (f : X âŸ¶ Y) [h : Epi f.1] : Epi f :=
  (forget F).epi_of_epi_map h

/-- An algebra morphism with an underlying monomorphism hom in `C` is an algebra monomorphism. -/
theorem mono_of_mono {X Y : Coalgebra F} (f : X âŸ¶ Y) [h : Mono f.1] : Mono f :=
  (forget F).mono_of_mono_map h

/-- From a natural transformation `Î± : F â†’ G` we get a functor from
coalgebras of `F` to coalgebras of `G`.
-/
@[simps]
def functorOfNatTrans {F G : C â¥¤ C} (Î± : F âŸ¶ G) : Coalgebra F â¥¤ Coalgebra G where
  obj V :=
    { V := V.1
      str := V.str â‰« Î±.app V.1 }
  map f :=
    { f := f.1
      h := by rw [Category.assoc, â† Î±.naturality, â† Category.assoc, f.h, Category.assoc] }

/-- The identity transformation induces the identity endofunctor on the category of coalgebras. -/
@[simps!]
def functorOfNatTransId : functorOfNatTrans (ğŸ™ F) â‰… ğŸ­ _ :=
  NatIso.ofComponents fun X => isoMk (Iso.refl _)

/-- A composition of natural transformations gives the composition of corresponding functors. -/
@[simps!]
def functorOfNatTransComp {Fâ‚€ Fâ‚ Fâ‚‚ : C â¥¤ C} (Î± : Fâ‚€ âŸ¶ Fâ‚) (Î² : Fâ‚ âŸ¶ Fâ‚‚) :
    functorOfNatTrans (Î± â‰« Î²) â‰… functorOfNatTrans Î± â‹™ functorOfNatTrans Î² :=
  NatIso.ofComponents fun X => isoMk (Iso.refl _)

/-- If `Î±` and `Î²` are two equal natural transformations, then the functors of coalgebras induced by
them are isomorphic.
We define it like this as opposed to using `eq_to_iso` so that the components are nicer to prove
lemmas about.
-/
@[simps!]
def functorOfNatTransEq {F G : C â¥¤ C} {Î± Î² : F âŸ¶ G} (h : Î± = Î²) :
    functorOfNatTrans Î± â‰… functorOfNatTrans Î² :=
  NatIso.ofComponents fun X => isoMk (Iso.refl _)

/-- Naturally isomorphic endofunctors give equivalent categories of coalgebras.
Furthermore, they are equivalent as categories over `C`, that is,
we have `equiv_of_nat_iso h â‹™ forget = forget`.
-/
@[simps]
def equivOfNatIso {F G : C â¥¤ C} (Î± : F â‰… G) : Coalgebra F â‰Œ Coalgebra G where
  functor := functorOfNatTrans Î±.hom
  inverse := functorOfNatTrans Î±.inv
  unitIso := functorOfNatTransId.symm â‰ªâ‰« functorOfNatTransEq (by simp) â‰ªâ‰« functorOfNatTransComp _ _
  counitIso :=
    (functorOfNatTransComp _ _).symm â‰ªâ‰« functorOfNatTransEq (by simp) â‰ªâ‰« functorOfNatTransId

namespace Terminal

variable {A : Coalgebra F} (h : Limits.IsTerminal A)

/-- The inverse of the structure map of an terminal coalgebra -/
@[simp]
def strInv : F.obj A.1 âŸ¶ A.1 :=
  (h.from âŸ¨F.obj A.V, F.map A.strâŸ©).f

theorem right_inv' :
    âŸ¨A.str â‰« strInv h, by rw [Category.assoc, F.map_comp, strInv, â† Hom.h] âŸ© = ğŸ™ A :=
  Limits.IsTerminal.hom_ext h _ (ğŸ™ A)

theorem right_inv : A.str â‰« strInv h = ğŸ™ _ :=
  congr_arg Hom.f (right_inv' h)

theorem left_inv : strInv h â‰« A.str = ğŸ™ _ := by
  rw [strInv, â† (h.from âŸ¨F.obj A.V, F.map A.strâŸ©).h, â† F.map_id, â† F.map_comp]
  congr
  exact right_inv h

/-- The structure map of the terminal coalgebra is an isomorphism,
hence endofunctors preserve their terminal coalgebras
-/
theorem str_isIso (h : Limits.IsTerminal A) : IsIso A.str :=
  { out := âŸ¨strInv h, right_inv _, left_inv _âŸ©  }

end Terminal

end Coalgebra

namespace Adjunction

variable {F : C â¥¤ C} {G : C â¥¤ C}

theorem Algebra.homEquiv_naturality_str (adj : F âŠ£ G) (Aâ‚ Aâ‚‚ : Algebra F) (f : Aâ‚ âŸ¶ Aâ‚‚) :
    (adj.homEquiv Aâ‚.a Aâ‚.a) Aâ‚.str â‰« G.map f.f = f.f â‰« (adj.homEquiv Aâ‚‚.a Aâ‚‚.a) Aâ‚‚.str := by
  rw [â† Adjunction.homEquiv_naturality_right, â† Adjunction.homEquiv_naturality_left, f.h]

theorem Coalgebra.homEquiv_naturality_str_symm (adj : F âŠ£ G) (Vâ‚ Vâ‚‚ : Coalgebra G) (f : Vâ‚ âŸ¶ Vâ‚‚) :
    F.map f.f â‰« (adj.homEquiv Vâ‚‚.V Vâ‚‚.V).symm Vâ‚‚.str =
    (adj.homEquiv Vâ‚.V Vâ‚.V).symm Vâ‚.str â‰« f.f := by
  rw [â† Adjunction.homEquiv_naturality_left_symm, â† Adjunction.homEquiv_naturality_right_symm,
    f.h]

/-- Given an adjunction `F âŠ£ G`, the functor that associates to an algebra over `F` a
coalgebra over `G` defined via adjunction applied to the structure map. -/
@[simps!]
def Algebra.toCoalgebraOf (adj : F âŠ£ G) : Algebra F â¥¤ Coalgebra G where
  obj A :=
    { V := A.1
      str := (adj.homEquiv A.1 A.1).toFun A.2 }
  map f :=
    { f := f.1
      h := Algebra.homEquiv_naturality_str adj _ _ f }

/-- Given an adjunction `F âŠ£ G`, the functor that associates to a coalgebra over `G` an algebra over
`F` defined via adjunction applied to the structure map. -/
@[simps!]
def Coalgebra.toAlgebraOf (adj : F âŠ£ G) : Coalgebra G â¥¤ Algebra F where
  obj V :=
    { a := V.1
      str := (adj.homEquiv V.1 V.1).invFun V.2 }
  map f :=
    { f := f.1
      h := Coalgebra.homEquiv_naturality_str_symm adj _ _ f }

/-- Given an adjunction, assigning to an algebra over the left adjoint a coalgebra over its right
adjoint and going back is isomorphic to the identity functor. -/
@[simps!]
def AlgCoalgEquiv.unitIso (adj : F âŠ£ G) :
    ğŸ­ (Algebra F) â‰… Algebra.toCoalgebraOf adj â‹™ Coalgebra.toAlgebraOf adj :=
  NatIso.ofComponents (fun _ â†¦ Algebra.isoMk <| Iso.refl _)

/-- Given an adjunction, assigning to a coalgebra over the right adjoint an algebra over the left
adjoint and going back is isomorphic to the identity functor. -/
@[simps!]
def AlgCoalgEquiv.counitIso (adj : F âŠ£ G) :
    Coalgebra.toAlgebraOf adj â‹™ Algebra.toCoalgebraOf adj â‰… ğŸ­ (Coalgebra G) :=
  NatIso.ofComponents (fun _ â†¦ Coalgebra.isoMk <| Iso.refl _)

/-- If `F` is left adjoint to `G`, then the category of algebras over `F` is equivalent to the
category of coalgebras over `G`. -/
@[simps!]
def algebraCoalgebraEquiv (adj : F âŠ£ G) : Algebra F â‰Œ Coalgebra G where
  functor := Algebra.toCoalgebraOf adj
  inverse := Coalgebra.toAlgebraOf adj
  unitIso := AlgCoalgEquiv.unitIso adj
  counitIso := AlgCoalgEquiv.counitIso adj
  functor_unitIso_comp A := by
    ext
    simp

end Adjunction

end Endofunctor

end CategoryTheory
