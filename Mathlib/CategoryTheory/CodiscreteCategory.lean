/-
Copyright (c) 2024 Alvaro Belmonte. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alvaro Belmonte, Jo√´l Riou
-/
import Mathlib.CategoryTheory.EqToHom
import Mathlib.CategoryTheory.Pi.Basic
import Mathlib.Data.ULift
import Mathlib.CategoryTheory.Category.Cat
import Mathlib.CategoryTheory.Adjunction.Basic

/-!
# Codiscrete categories

We define `Codiscrete A` as an alias for the type `A` ,
and use this type alias to provide a `Category` instance
whose Hom type are Unit types.

`Codiscrete.functor` promotes a function `f : C ‚Üí A` (for any category `C`) to a functor
 `f : C ‚•§ Codiscrete A`.

Similarly, `Codiscrete.natTrans` and `Codiscrete.natIso` promote `I`-indexed families of morphisms,
or `I`-indexed families of isomorphisms to natural transformations or natural isomorphism.

We define `functorToCat : Type u ‚•§ Cat.{0,u}` which sends a type to the codiscrete category and show
it is right adjoint to `Cat.objects.`
-/
namespace CategoryTheory

universe u v w

-- This is intentionally a structure rather than a type synonym
-- to enforce using `CodiscreteEquiv` (or `Codiscrete.mk` and `Codiscrete.as`) to move between
-- `Codiscrete Œ±` and `Œ±`. Otherwise there is too much API leakage.
/-- A wrapper for promoting any type to a category,
with a unique morphisms between any two objects of the category.
-/
@[ext, aesop safe cases (rule_sets := [CategoryTheory])]
structure Codiscrete (Œ± : Type u) where
  /-- A wrapper for promoting any type to a category,
  with a unique morphisms between any two objects of the category. -/
  as : Œ±

@[simp]
theorem Codiscrete.mk_as {Œ± : Type u} (X : Codiscrete Œ±) : Codiscrete.mk X.as = X := rfl

/-- `Codiscrete Œ±` is equivalent to the original type `Œ±`. -/
@[simps]
def codiscreteEquiv {Œ± : Type u} : Codiscrete Œ± ‚âÉ Œ± where
  toFun := Codiscrete.as
  invFun := Codiscrete.mk
  left_inv := by aesop_cat
  right_inv := by aesop_cat

instance {Œ± : Type u} [DecidableEq Œ±] : DecidableEq (Codiscrete Œ±) :=
  codiscreteEquiv.decidableEq

namespace Codiscrete

instance (A : Type*) : Category (Codiscrete A) where
  Hom _ _ := Unit
  id _ := ‚ü®‚ü©
  comp _ _ := ‚ü®‚ü©

section
variable {C : Type u} [Category.{v} C] {A : Type w}

/-- Any function `C ‚Üí A` lifts to a functor `C ‚•§ Codiscrete A`.-/
def functor (F : C ‚Üí A) : C ‚•§ Codiscrete A where
  obj := Codiscrete.mk ‚àò F
  map _ := ‚ü®‚ü©

/-- The underlying function `C ‚Üí A` of a functor `C ‚•§ Codiscrete A`. -/
def invFunctor (F : C ‚•§ Codiscrete A) : C ‚Üí A := Codiscrete.as ‚àò F.obj

/-- Given two functors to a codiscrete category, there is a trivial natural transformation.-/
def natTrans {F G : C ‚•§ Codiscrete A} : F ‚ü∂ G where
  app _ := ‚ü®‚ü©

/-- Given two functors into a codiscrete category, the trivial natural transformation is an
natural isomorphism.-/
def natIso {F G : C ‚•§ Codiscrete A} : F ‚âÖ G where
  hom := natTrans
  inv := natTrans

/-- Every functor `F` to a codiscrete category is naturally isomorphic {(actually, equal)} to
  `Codiscrete.as ‚àò F.obj`. -/
@[simps!]
def natIsoFunctor {F : C ‚•§ Codiscrete A} : F ‚âÖ functor (Codiscrete.as ‚àò F.obj) := Iso.refl _

end

/-- A function induces a functor between codiscrete categories.-/
def functorOfFun {A B : Type*} (f : A ‚Üí B) : Codiscrete A ‚•§ Codiscrete B :=
  functor (f ‚àò Codiscrete.as)

open Opposite

/-- A codiscrete category is equivalent to its opposite category. -/
def oppositeEquivalence (A : Type*) : (Codiscrete A)·µí·µñ ‚âå Codiscrete A where
  functor := functor (fun x ‚Ü¶ Codiscrete.as x.unop)
  inverse := (functor (fun x ‚Ü¶ Codiscrete.as x.unop)).rightOp
  unitIso := NatIso.ofComponents (fun _ => by exact Iso.refl _)
  counitIso := natIso

/-- `Codiscrete.functorToCat` turns a type into a codiscrete category-/
def functorToCat : Type u ‚•§ Cat.{0,u} where
  obj A := Cat.of (Codiscrete A)
  map := functorOfFun

open Adjunction Cat

/-- For a category `C` and type `A`, there is an equivalence between functions `objects.obj C ‚ü∂ A`
and functors `C ‚•§ Codiscrete A`.-/
def equivFunctorToCodiscrete {C : Type u} [Category.{v} C] {A : Type w} :
    (C ‚Üí A) ‚âÉ (C ‚•§ Codiscrete A) where
  toFun := functor
  invFun := invFunctor
  left_inv _ := rfl
  right_inv _ := rfl

/-- The functor that turns a type into a codiscrete category is right adjoint to the objects
functor.-/
def adj : objects ‚ä£ functorToCat := mkOfHomEquiv {
  homEquiv := fun _ _ => equivFunctorToCodiscrete
  homEquiv_naturality_left_symm := fun _ _ => rfl
  homEquiv_naturality_right := fun _ _ => rfl }

/-- Components of the unit of the adjunction `Cat.objects ‚ä£ Codiscrete.functorToCat`. -/
def unitApp (C : Type u) [Category.{v} C] : C ‚•§ Codiscrete C := functor id

/-- Components of the counit of the adjunction `Cat.objects ‚ä£ Codiscrete.functorToCat` -/
def counitApp (A : Type u) : Codiscrete A ‚Üí A := Codiscrete.as

lemma adj_unit_app (X : Cat.{0, u}) :
    adj.unit.app X = unitApp X := rfl

lemma adj_counit_app (A : Type u) :
    adj.counit.app A = counitApp A := rfl

/-- Left triangle equality of the adjunction `Cat.objects ‚ä£ Codiscrete.functorToCat`,
as a universe polymorphic statement. -/
lemma left_triangle_components (C : Type u) [Category.{v} C] :
    (counitApp C).comp (unitApp C).obj = id :=
  rfl

/-- Right triangle equality of the adjunction `Cat.objects ‚ä£ Codiscrete.functorToCat`,
stated using a composition of functors. -/
lemma right_triangle_components (X : Type u) :
    unitApp (Codiscrete X) ‚ãô functorOfFun (counitApp X) = ùü≠ (Codiscrete X) :=
  rfl

end Codiscrete

end CategoryTheory
