/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.Tactic.CategoryTheory.Elementwise
import Mathlib.CategoryTheory.Limits.Shapes.Multiequalizer
import Mathlib.CategoryTheory.Limits.Constructions.EpiMono
import Mathlib.CategoryTheory.Limits.Preserves.Limits
import Mathlib.CategoryTheory.Limits.Shapes.Types

#align_import category_theory.glue_data from "leanprover-community/mathlib"@"14b69e9f3c16630440a2cbd46f1ddad0d561dee7"

/-!
# Gluing data

We define `GlueData` as a family of data needed to glue topological spaces, schemes, etc. We
provide the API to realize it as a multispan diagram, and also state lemmas about its
interaction with a functor that preserves certain pullbacks.

-/


noncomputable section

open CategoryTheory.Limits

namespace CategoryTheory

universe v uâ‚ uâ‚‚

variable (C : Type uâ‚) [Category.{v} C] {C' : Type uâ‚‚} [Category.{v} C']

/-- A gluing datum consists of
1. An index type `J`
2. An object `U i` for each `i : J`.
3. An object `V i j` for each `i j : J`.
4. A monomorphism `f i j : V i j âŸ¶ U i` for each `i j : J`.
5. A transition map `t i j : V i j âŸ¶ V j i` for each `i j : J`.
such that
6. `f i i` is an isomorphism.
7. `t i i` is the identity.
8. The pullback for `f i j` and `f i k` exists.
9. `V i j Ã—[U i] V i k âŸ¶ V i j âŸ¶ V j i` factors through `V j k Ã—[U j] V j i âŸ¶ V j i` via some
    `t' : V i j Ã—[U i] V i k âŸ¶ V j k Ã—[U j] V j i`.
10. `t' i j k â‰« t' j k i â‰« t' k i j = ğŸ™ _`.
-/
-- Porting note(#5171): linter not ported yet
-- @[nolint has_nonempty_instance]
structure GlueData where
  J : Type v
  U : J â†’ C
  V : J Ã— J â†’ C
  f : âˆ€ i j, V (i, j) âŸ¶ U i
  f_mono : âˆ€ i j, Mono (f i j) := by infer_instance
  f_hasPullback : âˆ€ i j k, HasPullback (f i j) (f i k) := by infer_instance
  f_id : âˆ€ i, IsIso (f i i) := by infer_instance
  t : âˆ€ i j, V (i, j) âŸ¶ V (j, i)
  t_id : âˆ€ i, t i i = ğŸ™ _
  t' : âˆ€ i j k, pullback (f i j) (f i k) âŸ¶ pullback (f j k) (f j i)
  t_fac : âˆ€ i j k, t' i j k â‰« pullback.snd = pullback.fst â‰« t i j
  cocycle : âˆ€ i j k, t' i j k â‰« t' j k i â‰« t' k i j = ğŸ™ _
#align category_theory.glue_data CategoryTheory.GlueData

attribute [simp] GlueData.t_id

attribute [instance] GlueData.f_id GlueData.f_mono GlueData.f_hasPullback

attribute [reassoc] GlueData.t_fac GlueData.cocycle

namespace GlueData

variable {C}
variable (D : GlueData C)

@[simp]
theorem t'_iij (i j : D.J) : D.t' i i j = (pullbackSymmetry _ _).hom := by
  have eqâ‚ := D.t_fac i i j
  have eqâ‚‚ := (IsIso.eq_comp_inv (D.f i i)).mpr (@pullback.condition _ _ _ _ _ _ (D.f i j) _)
  rw [D.t_id, Category.comp_id, eqâ‚‚] at eqâ‚
  have eqâ‚ƒ := (IsIso.eq_comp_inv (D.f i i)).mp eqâ‚
  rw [Category.assoc, â† pullback.condition, â† Category.assoc] at eqâ‚ƒ
  exact
    Mono.right_cancellation _ _
      ((Mono.right_cancellation _ _ eqâ‚ƒ).trans (pullbackSymmetry_hom_comp_fst _ _).symm)
#align category_theory.glue_data.t'_iij CategoryTheory.GlueData.t'_iij

theorem t'_jii (i j : D.J) : D.t' j i i = pullback.fst â‰« D.t j i â‰« inv pullback.snd := by
  rw [â† Category.assoc, â† D.t_fac]
  simp
#align category_theory.glue_data.t'_jii CategoryTheory.GlueData.t'_jii

theorem t'_iji (i j : D.J) : D.t' i j i = pullback.fst â‰« D.t i j â‰« inv pullback.snd := by
  rw [â† Category.assoc, â† D.t_fac]
  simp
#align category_theory.glue_data.t'_iji CategoryTheory.GlueData.t'_iji

@[reassoc, elementwise (attr := simp)]
theorem t_inv (i j : D.J) : D.t i j â‰« D.t j i = ğŸ™ _ := by
  have eq : (pullbackSymmetry (D.f i i) (D.f i j)).hom = pullback.snd â‰« inv pullback.fst := by simp
  have := D.cocycle i j i
  rw [D.t'_iij, D.t'_jii, D.t'_iji, fst_eq_snd_of_mono_eq, eq] at this
  simp only [Category.assoc, IsIso.inv_hom_id_assoc] at this
  rw [â† IsIso.eq_inv_comp, â† Category.assoc, IsIso.comp_inv_eq] at this
  simpa using this
#align category_theory.glue_data.t_inv CategoryTheory.GlueData.t_inv

theorem t'_inv (i j k : D.J) :
    D.t' i j k â‰« (pullbackSymmetry _ _).hom â‰« D.t' j i k â‰« (pullbackSymmetry _ _).hom = ğŸ™ _ := by
  rw [â† cancel_mono (pullback.fst : pullback (D.f i j) (D.f i k) âŸ¶ _)]
  simp [t_fac, t_fac_assoc]
#align category_theory.glue_data.t'_inv CategoryTheory.GlueData.t'_inv

instance t_isIso (i j : D.J) : IsIso (D.t i j) :=
  âŸ¨âŸ¨D.t j i, D.t_inv _ _, D.t_inv _ _âŸ©âŸ©
#align category_theory.glue_data.t_is_iso CategoryTheory.GlueData.t_isIso

instance t'_isIso (i j k : D.J) : IsIso (D.t' i j k) :=
  âŸ¨âŸ¨D.t' j k i â‰« D.t' k i j, D.cocycle _ _ _, by simpa using D.cocycle _ _ _âŸ©âŸ©
#align category_theory.glue_data.t'_is_iso CategoryTheory.GlueData.t'_isIso

@[reassoc]
theorem t'_comp_eq_pullbackSymmetry (i j k : D.J) :
    D.t' j k i â‰« D.t' k i j =
      (pullbackSymmetry _ _).hom â‰« D.t' j i k â‰« (pullbackSymmetry _ _).hom := by
  trans inv (D.t' i j k)
  Â· exact IsIso.eq_inv_of_hom_inv_id (D.cocycle _ _ _)
  Â· rw [â† cancel_mono (pullback.fst : pullback (D.f i j) (D.f i k) âŸ¶ _)]
    simp [t_fac, t_fac_assoc]
#align category_theory.glue_data.t'_comp_eq_pullback_symmetry CategoryTheory.GlueData.t'_comp_eq_pullbackSymmetry

/-- (Implementation) The disjoint union of `U i`. -/
def sigmaOpens [HasCoproduct D.U] : C :=
  âˆ D.U
#align category_theory.glue_data.sigma_opens CategoryTheory.GlueData.sigmaOpens

/-- (Implementation) The diagram to take colimit of. -/
def diagram : MultispanIndex C where
  L := D.J Ã— D.J
  R := D.J
  fstFrom := _root_.Prod.fst
  sndFrom := _root_.Prod.snd
  left := D.V
  right := D.U
  fst := fun âŸ¨i, jâŸ© => D.f i j
  snd := fun âŸ¨i, jâŸ© => D.t i j â‰« D.f j i
#align category_theory.glue_data.diagram CategoryTheory.GlueData.diagram

@[simp]
theorem diagram_l : D.diagram.L = (D.J Ã— D.J) :=
  rfl
set_option linter.uppercaseLean3 false in
#align category_theory.glue_data.diagram_L CategoryTheory.GlueData.diagram_l

@[simp]
theorem diagram_r : D.diagram.R = D.J :=
  rfl
set_option linter.uppercaseLean3 false in
#align category_theory.glue_data.diagram_R CategoryTheory.GlueData.diagram_r

@[simp]
theorem diagram_fstFrom (i j : D.J) : D.diagram.fstFrom âŸ¨i, jâŸ© = i :=
  rfl
#align category_theory.glue_data.diagram_fst_from CategoryTheory.GlueData.diagram_fstFrom

@[simp]
theorem diagram_sndFrom (i j : D.J) : D.diagram.sndFrom âŸ¨i, jâŸ© = j :=
  rfl
#align category_theory.glue_data.diagram_snd_from CategoryTheory.GlueData.diagram_sndFrom

@[simp]
theorem diagram_fst (i j : D.J) : D.diagram.fst âŸ¨i, jâŸ© = D.f i j :=
  rfl
#align category_theory.glue_data.diagram_fst CategoryTheory.GlueData.diagram_fst

@[simp]
theorem diagram_snd (i j : D.J) : D.diagram.snd âŸ¨i, jâŸ© = D.t i j â‰« D.f j i :=
  rfl
#align category_theory.glue_data.diagram_snd CategoryTheory.GlueData.diagram_snd

@[simp]
theorem diagram_left : D.diagram.left = D.V :=
  rfl
#align category_theory.glue_data.diagram_left CategoryTheory.GlueData.diagram_left

@[simp]
theorem diagram_right : D.diagram.right = D.U :=
  rfl
#align category_theory.glue_data.diagram_right CategoryTheory.GlueData.diagram_right

section

variable [HasMulticoequalizer D.diagram]

/-- The glued object given a family of gluing data. -/
def glued : C :=
  multicoequalizer D.diagram
#align category_theory.glue_data.glued CategoryTheory.GlueData.glued

/-- The map `D.U i âŸ¶ D.glued` for each `i`. -/
def Î¹ (i : D.J) : D.U i âŸ¶ D.glued :=
  Multicoequalizer.Ï€ D.diagram i
#align category_theory.glue_data.Î¹ CategoryTheory.GlueData.Î¹

@[elementwise (attr := simp)]
theorem glue_condition (i j : D.J) : D.t i j â‰« D.f j i â‰« D.Î¹ j = D.f i j â‰« D.Î¹ i :=
  (Category.assoc _ _ _).symm.trans (Multicoequalizer.condition D.diagram âŸ¨i, jâŸ©).symm
#align category_theory.glue_data.glue_condition CategoryTheory.GlueData.glue_condition

/-- The pullback cone spanned by `V i j âŸ¶ U i` and `V i j âŸ¶ U j`.
This will often be a pullback diagram. -/
def vPullbackCone (i j : D.J) : PullbackCone (D.Î¹ i) (D.Î¹ j) :=
  PullbackCone.mk (D.f i j) (D.t i j â‰« D.f j i) (by simp)
set_option linter.uppercaseLean3 false in
#align category_theory.glue_data.V_pullback_cone CategoryTheory.GlueData.vPullbackCone

variable [HasColimits C]

/-- The projection `âˆ D.U âŸ¶ D.glued` given by the colimit. -/
def Ï€ : D.sigmaOpens âŸ¶ D.glued :=
  Multicoequalizer.sigmaÏ€ D.diagram
#align category_theory.glue_data.Ï€ CategoryTheory.GlueData.Ï€

instance Ï€_epi : Epi D.Ï€ := by
  unfold Ï€
  infer_instance
#align category_theory.glue_data.Ï€_epi CategoryTheory.GlueData.Ï€_epi

end

theorem types_Ï€_surjective (D : GlueData Type*) : Function.Surjective D.Ï€ :=
  (epi_iff_surjective _).mp inferInstance
#align category_theory.glue_data.types_Ï€_surjective CategoryTheory.GlueData.types_Ï€_surjective

theorem types_Î¹_jointly_surjective (D : GlueData (Type v)) (x : D.glued) :
    âˆƒ (i : _) (y : D.U i), D.Î¹ i y = x := by
  delta CategoryTheory.GlueData.Î¹
  simp_rw [â† Multicoequalizer.Î¹_sigmaÏ€ D.diagram]
  rcases D.types_Ï€_surjective x with âŸ¨x', rflâŸ©
  --have := colimit.isoColimitCocone (Types.coproductColimitCocone _)
  rw [â† show (colimit.isoColimitCocone (Types.coproductColimitCocone.{v, v} _)).inv _ = x' from
      ConcreteCategory.congr_hom
        (colimit.isoColimitCocone (Types.coproductColimitCocone _)).hom_inv_id x']
  rcases (colimit.isoColimitCocone (Types.coproductColimitCocone _)).hom x' with âŸ¨i, yâŸ©
  exact âŸ¨i, y, by
    simp [â† Multicoequalizer.Î¹_sigmaÏ€, -Multicoequalizer.Î¹_sigmaÏ€]
    rfl âŸ©
#align category_theory.glue_data.types_Î¹_jointly_surjective CategoryTheory.GlueData.types_Î¹_jointly_surjective

variable (F : C â¥¤ C') [H : âˆ€ i j k, PreservesLimit (cospan (D.f i j) (D.f i k)) F]

instance (i j k : D.J) : HasPullback (F.map (D.f i j)) (F.map (D.f i k)) :=
  âŸ¨âŸ¨âŸ¨_, isLimitOfHasPullbackOfPreservesLimit F (D.f i j) (D.f i k)âŸ©âŸ©âŸ©

/-- A functor that preserves the pullbacks of `f i j` and `f i k` can map a family of glue data. -/
@[simps]
def mapGlueData : GlueData C' where
  J := D.J
  U i := F.obj (D.U i)
  V i := F.obj (D.V i)
  f i j := F.map (D.f i j)
  f_mono i j := preserves_mono_of_preservesLimit _ _
  f_id i := inferInstance
  t i j := F.map (D.t i j)
  t_id i := by
    simp [D.t_id i]
  t' i j k :=
    (PreservesPullback.iso F (D.f i j) (D.f i k)).inv â‰«
      F.map (D.t' i j k) â‰« (PreservesPullback.iso F (D.f j k) (D.f j i)).hom
  t_fac i j k := by simpa [Iso.inv_comp_eq] using congr_arg (fun f => F.map f) (D.t_fac i j k)
  cocycle i j k := by
    simp only [Category.assoc, Iso.hom_inv_id_assoc, â† Functor.map_comp_assoc, D.cocycle,
      Iso.inv_hom_id, CategoryTheory.Functor.map_id, Category.id_comp]
#align category_theory.glue_data.map_glue_data CategoryTheory.GlueData.mapGlueData

/-- The diagram of the image of a `GlueData` under a functor `F` is naturally isomorphic to the
original diagram of the `GlueData` via `F`.
-/
def diagramIso : D.diagram.multispan â‹™ F â‰… (D.mapGlueData F).diagram.multispan :=
  NatIso.ofComponents
    (fun x =>
      match x with
      | WalkingMultispan.left a => Iso.refl _
      | WalkingMultispan.right b => Iso.refl _)
    (by
      rintro (âŸ¨_, _âŸ© | _) _ (_ | _ | _)
      Â· erw [Category.comp_id, Category.id_comp, Functor.map_id]
        rfl
      Â· erw [Category.comp_id, Category.id_comp]
        rfl
      Â· erw [Category.comp_id, Category.id_comp, Functor.map_comp]
        rfl
      Â· erw [Category.comp_id, Category.id_comp, Functor.map_id]
        rfl)
#align category_theory.glue_data.diagram_iso CategoryTheory.GlueData.diagramIso

@[simp]
theorem diagramIso_app_left (i : D.J Ã— D.J) :
    (D.diagramIso F).app (WalkingMultispan.left i) = Iso.refl _ :=
  rfl
#align category_theory.glue_data.diagram_iso_app_left CategoryTheory.GlueData.diagramIso_app_left

@[simp]
theorem diagramIso_app_right (i : D.J) :
    (D.diagramIso F).app (WalkingMultispan.right i) = Iso.refl _ :=
  rfl
#align category_theory.glue_data.diagram_iso_app_right CategoryTheory.GlueData.diagramIso_app_right

@[simp]
theorem diagramIso_hom_app_left (i : D.J Ã— D.J) :
    (D.diagramIso F).hom.app (WalkingMultispan.left i) = ğŸ™ _ :=
  rfl
#align category_theory.glue_data.diagram_iso_hom_app_left CategoryTheory.GlueData.diagramIso_hom_app_left

@[simp]
theorem diagramIso_hom_app_right (i : D.J) :
    (D.diagramIso F).hom.app (WalkingMultispan.right i) = ğŸ™ _ :=
  rfl
#align category_theory.glue_data.diagram_iso_hom_app_right CategoryTheory.GlueData.diagramIso_hom_app_right

@[simp]
theorem diagramIso_inv_app_left (i : D.J Ã— D.J) :
    (D.diagramIso F).inv.app (WalkingMultispan.left i) = ğŸ™ _ :=
  rfl
#align category_theory.glue_data.diagram_iso_inv_app_left CategoryTheory.GlueData.diagramIso_inv_app_left

@[simp]
theorem diagramIso_inv_app_right (i : D.J) :
    (D.diagramIso F).inv.app (WalkingMultispan.right i) = ğŸ™ _ :=
  rfl
#align category_theory.glue_data.diagram_iso_inv_app_right CategoryTheory.GlueData.diagramIso_inv_app_right

variable [HasMulticoequalizer D.diagram] [PreservesColimit D.diagram.multispan F]

theorem hasColimit_multispan_comp : HasColimit (D.diagram.multispan â‹™ F) :=
  âŸ¨âŸ¨âŸ¨_, PreservesColimit.preserves (colimit.isColimit _)âŸ©âŸ©âŸ©
#align category_theory.glue_data.has_colimit_multispan_comp CategoryTheory.GlueData.hasColimit_multispan_comp

attribute [local instance] hasColimit_multispan_comp

theorem hasColimit_mapGlueData_diagram : HasMulticoequalizer (D.mapGlueData F).diagram :=
  hasColimitOfIso (D.diagramIso F).symm
#align category_theory.glue_data.has_colimit_map_glue_data_diagram CategoryTheory.GlueData.hasColimit_mapGlueData_diagram

attribute [local instance] hasColimit_mapGlueData_diagram

/-- If `F` preserves the gluing, we obtain an iso between the glued objects. -/
def gluedIso : F.obj D.glued â‰… (D.mapGlueData F).glued :=
  haveI : HasColimit (MultispanIndex.multispan (diagram (mapGlueData D F))) := inferInstance
  preservesColimitIso F D.diagram.multispan â‰ªâ‰« Limits.HasColimit.isoOfNatIso (D.diagramIso F)
#align category_theory.glue_data.glued_iso CategoryTheory.GlueData.gluedIso

@[reassoc (attr := simp)]
theorem Î¹_gluedIso_hom (i : D.J) : F.map (D.Î¹ i) â‰« (D.gluedIso F).hom = (D.mapGlueData F).Î¹ i := by
  haveI : HasColimit (MultispanIndex.multispan (diagram (mapGlueData D F))) := inferInstance
  erw [Î¹_preservesColimitsIso_hom_assoc]
  rw [HasColimit.isoOfNatIso_Î¹_hom]
  erw [Category.id_comp]
  rfl
#align category_theory.glue_data.Î¹_glued_iso_hom CategoryTheory.GlueData.Î¹_gluedIso_hom

@[reassoc (attr := simp)]
theorem Î¹_gluedIso_inv (i : D.J) : (D.mapGlueData F).Î¹ i â‰« (D.gluedIso F).inv = F.map (D.Î¹ i) := by
  rw [Iso.comp_inv_eq, Î¹_gluedIso_hom]
#align category_theory.glue_data.Î¹_glued_iso_inv CategoryTheory.GlueData.Î¹_gluedIso_inv

/-- If `F` preserves the gluing, and reflects the pullback of `U i âŸ¶ glued` and `U j âŸ¶ glued`,
then `F` reflects the fact that `V_pullback_cone` is a pullback. -/
def vPullbackConeIsLimitOfMap (i j : D.J) [ReflectsLimit (cospan (D.Î¹ i) (D.Î¹ j)) F]
    (hc : IsLimit ((D.mapGlueData F).vPullbackCone i j)) : IsLimit (D.vPullbackCone i j) := by
  apply isLimitOfReflects F
  apply (isLimitMapConePullbackConeEquiv _ _).symm _
  let e : cospan (F.map (D.Î¹ i)) (F.map (D.Î¹ j)) â‰…
      cospan ((D.mapGlueData F).Î¹ i) ((D.mapGlueData F).Î¹ j) :=
    NatIso.ofComponents
      (fun x => by
        cases x
        exacts [D.gluedIso F, Iso.refl _])
      (by rintro (_ | _) (_ | _) (_ | _ | _) <;> simp)
  apply IsLimit.postcomposeHomEquiv e _ _
  apply hc.ofIsoLimit
  refine Cones.ext (Iso.refl _) ?_
  rintro (_ | _ | _)
  on_goal 1 => change _ = _ â‰« (_ â‰« _) â‰« _
  all_goals change _ = ğŸ™ _ â‰« _ â‰« _; aesop_cat
set_option linter.uppercaseLean3 false in
#align category_theory.glue_data.V_pullback_cone_is_limit_of_map CategoryTheory.GlueData.vPullbackConeIsLimitOfMap

/-- If there is a forgetful functor into `Type` that preserves enough (co)limits, then `D.Î¹` will
be jointly surjective. -/
theorem Î¹_jointly_surjective (F : C â¥¤ Type v) [PreservesColimit D.diagram.multispan F]
    [âˆ€ i j k : D.J, PreservesLimit (cospan (D.f i j) (D.f i k)) F] (x : F.obj D.glued) :
    âˆƒ (i : _) (y : F.obj (D.U i)), F.map (D.Î¹ i) y = x := by
  let e := D.gluedIso F
  obtain âŸ¨i, y, eqâŸ© := (D.mapGlueData F).types_Î¹_jointly_surjective (e.hom x)
  replace eq := congr_arg e.inv eq
  change ((D.mapGlueData F).Î¹ i â‰« e.inv) y = (e.hom â‰« e.inv) x at eq
  rw [e.hom_inv_id, D.Î¹_gluedIso_inv] at eq
  exact âŸ¨i, y, eqâŸ©
#align category_theory.glue_data.Î¹_jointly_surjective CategoryTheory.GlueData.Î¹_jointly_surjective

end GlueData

end CategoryTheory
