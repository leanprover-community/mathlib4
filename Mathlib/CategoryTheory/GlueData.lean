/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.Tactic.CategoryTheory.Elementwise
import Mathlib.CategoryTheory.Limits.Shapes.Multiequalizer
import Mathlib.CategoryTheory.Limits.Constructions.EpiMono
import Mathlib.CategoryTheory.Limits.Preserves.Limits
import Mathlib.CategoryTheory.Limits.Shapes.Types

#align_import category_theory.glue_data from "leanprover-community/mathlib"@"14b69e9f3c16630440a2cbd46f1ddad0d561dee7"

/-!
# Gluing data

We define `GlueData` as a family of data needed to glue topological spaces, schemes, etc. We
provide the API to realize it as a multispan diagram, and also state lemmas about its
interaction with a functor that preserves certain pullbacks.

-/


noncomputable section

open CategoryTheory.Limits

namespace CategoryTheory

universe v u‚ÇÅ u‚ÇÇ

variable (C : Type u‚ÇÅ) [Category.{v} C] {C' : Type u‚ÇÇ} [Category.{v} C']

/-- A gluing datum consists of
1. An index type `J`
2. An object `U i` for each `i : J`.
3. An object `V i j` for each `i j : J`.
4. A monomorphism `f i j : V i j ‚ü∂ U i` for each `i j : J`.
5. A transition map `t i j : V i j ‚ü∂ V j i` for each `i j : J`.
such that
6. `f i i` is an isomorphism.
7. `t i i` is the identity.
8. The pullback for `f i j` and `f i k` exists.
9. `V i j √ó[U i] V i k ‚ü∂ V i j ‚ü∂ V j i` factors through `V j k √ó[U j] V j i ‚ü∂ V j i` via some
    `t' : V i j √ó[U i] V i k ‚ü∂ V j k √ó[U j] V j i`.
10. `t' i j k ‚â´ t' j k i ‚â´ t' k i j = ùüô _`.
-/
-- Porting note: This linter does not exist yet
-- @[nolint has_nonempty_instance]
structure GlueData where
  J : Type v
  U : J ‚Üí C
  V : J √ó J ‚Üí C
  f : ‚àÄ i j, V (i, j) ‚ü∂ U i
  f_mono : ‚àÄ i j, Mono (f i j) := by infer_instance
  f_hasPullback : ‚àÄ i j k, HasPullback (f i j) (f i k) := by infer_instance
  f_id : ‚àÄ i, IsIso (f i i) := by infer_instance
  t : ‚àÄ i j, V (i, j) ‚ü∂ V (j, i)
  t_id : ‚àÄ i, t i i = ùüô _
  t' : ‚àÄ i j k, pullback (f i j) (f i k) ‚ü∂ pullback (f j k) (f j i)
  t_fac : ‚àÄ i j k, t' i j k ‚â´ pullback.snd = pullback.fst ‚â´ t i j
  cocycle : ‚àÄ i j k, t' i j k ‚â´ t' j k i ‚â´ t' k i j = ùüô _
#align category_theory.glue_data CategoryTheory.GlueData

attribute [simp] GlueData.t_id

attribute [instance] GlueData.f_id GlueData.f_mono GlueData.f_hasPullback

attribute [reassoc] GlueData.t_fac GlueData.cocycle

namespace GlueData

variable {C}
variable (D : GlueData C)

@[simp]
theorem t'_iij (i j : D.J) : D.t' i i j = (pullbackSymmetry _ _).hom := by
  have eq‚ÇÅ := D.t_fac i i j
  have eq‚ÇÇ := (IsIso.eq_comp_inv (D.f i i)).mpr (@pullback.condition _ _ _ _ _ _ (D.f i j) _)
  rw [D.t_id, Category.comp_id, eq‚ÇÇ] at eq‚ÇÅ
  have eq‚ÇÉ := (IsIso.eq_comp_inv (D.f i i)).mp eq‚ÇÅ
  rw [Category.assoc, ‚Üê pullback.condition, ‚Üê Category.assoc] at eq‚ÇÉ
  exact
    Mono.right_cancellation _ _
      ((Mono.right_cancellation _ _ eq‚ÇÉ).trans (pullbackSymmetry_hom_comp_fst _ _).symm)
#align category_theory.glue_data.t'_iij CategoryTheory.GlueData.t'_iij

theorem t'_jii (i j : D.J) : D.t' j i i = pullback.fst ‚â´ D.t j i ‚â´ inv pullback.snd := by
  rw [‚Üê Category.assoc, ‚Üê D.t_fac]
  simp
#align category_theory.glue_data.t'_jii CategoryTheory.GlueData.t'_jii

theorem t'_iji (i j : D.J) : D.t' i j i = pullback.fst ‚â´ D.t i j ‚â´ inv pullback.snd := by
  rw [‚Üê Category.assoc, ‚Üê D.t_fac]
  simp
#align category_theory.glue_data.t'_iji CategoryTheory.GlueData.t'_iji

@[reassoc, elementwise (attr := simp)]
theorem t_inv (i j : D.J) : D.t i j ‚â´ D.t j i = ùüô _ := by
  have eq : (pullbackSymmetry (D.f i i) (D.f i j)).hom = pullback.snd ‚â´ inv pullback.fst := by simp
  have := D.cocycle i j i
  rw [D.t'_iij, D.t'_jii, D.t'_iji, fst_eq_snd_of_mono_eq, eq] at this
  simp only [Category.assoc, IsIso.inv_hom_id_assoc] at this
  rw [‚Üê IsIso.eq_inv_comp, ‚Üê Category.assoc, IsIso.comp_inv_eq] at this
  simpa using this
#align category_theory.glue_data.t_inv CategoryTheory.GlueData.t_inv

theorem t'_inv (i j k : D.J) :
    D.t' i j k ‚â´ (pullbackSymmetry _ _).hom ‚â´ D.t' j i k ‚â´ (pullbackSymmetry _ _).hom = ùüô _ := by
  rw [‚Üê cancel_mono (pullback.fst : pullback (D.f i j) (D.f i k) ‚ü∂ _)]
  simp [t_fac, t_fac_assoc]
#align category_theory.glue_data.t'_inv CategoryTheory.GlueData.t'_inv

instance t_isIso (i j : D.J) : IsIso (D.t i j) :=
  ‚ü®‚ü®D.t j i, D.t_inv _ _, D.t_inv _ _‚ü©‚ü©
#align category_theory.glue_data.t_is_iso CategoryTheory.GlueData.t_isIso

instance t'_isIso (i j k : D.J) : IsIso (D.t' i j k) :=
  ‚ü®‚ü®D.t' j k i ‚â´ D.t' k i j, D.cocycle _ _ _, by simpa using D.cocycle _ _ _‚ü©‚ü©
#align category_theory.glue_data.t'_is_iso CategoryTheory.GlueData.t'_isIso

@[reassoc]
theorem t'_comp_eq_pullbackSymmetry (i j k : D.J) :
    D.t' j k i ‚â´ D.t' k i j =
      (pullbackSymmetry _ _).hom ‚â´ D.t' j i k ‚â´ (pullbackSymmetry _ _).hom := by
  trans inv (D.t' i j k)
  ¬∑ exact IsIso.eq_inv_of_hom_inv_id (D.cocycle _ _ _)
  ¬∑ rw [‚Üê cancel_mono (pullback.fst : pullback (D.f i j) (D.f i k) ‚ü∂ _)]
    simp [t_fac, t_fac_assoc]
#align category_theory.glue_data.t'_comp_eq_pullback_symmetry CategoryTheory.GlueData.t'_comp_eq_pullbackSymmetry

/-- (Implementation) The disjoint union of `U i`. -/
def sigmaOpens [HasCoproduct D.U] : C :=
  ‚àê D.U
#align category_theory.glue_data.sigma_opens CategoryTheory.GlueData.sigmaOpens

/-- (Implementation) The diagram to take colimit of. -/
def diagram : MultispanIndex C where
  L := D.J √ó D.J
  R := D.J
  fstFrom := _root_.Prod.fst
  sndFrom := _root_.Prod.snd
  left := D.V
  right := D.U
  fst := fun ‚ü®i, j‚ü© => D.f i j
  snd := fun ‚ü®i, j‚ü© => D.t i j ‚â´ D.f j i
#align category_theory.glue_data.diagram CategoryTheory.GlueData.diagram

@[simp]
theorem diagram_l : D.diagram.L = (D.J √ó D.J) :=
  rfl
set_option linter.uppercaseLean3 false in
#align category_theory.glue_data.diagram_L CategoryTheory.GlueData.diagram_l

@[simp]
theorem diagram_r : D.diagram.R = D.J :=
  rfl
set_option linter.uppercaseLean3 false in
#align category_theory.glue_data.diagram_R CategoryTheory.GlueData.diagram_r

@[simp]
theorem diagram_fstFrom (i j : D.J) : D.diagram.fstFrom ‚ü®i, j‚ü© = i :=
  rfl
#align category_theory.glue_data.diagram_fst_from CategoryTheory.GlueData.diagram_fstFrom

@[simp]
theorem diagram_sndFrom (i j : D.J) : D.diagram.sndFrom ‚ü®i, j‚ü© = j :=
  rfl
#align category_theory.glue_data.diagram_snd_from CategoryTheory.GlueData.diagram_sndFrom

@[simp]
theorem diagram_fst (i j : D.J) : D.diagram.fst ‚ü®i, j‚ü© = D.f i j :=
  rfl
#align category_theory.glue_data.diagram_fst CategoryTheory.GlueData.diagram_fst

@[simp]
theorem diagram_snd (i j : D.J) : D.diagram.snd ‚ü®i, j‚ü© = D.t i j ‚â´ D.f j i :=
  rfl
#align category_theory.glue_data.diagram_snd CategoryTheory.GlueData.diagram_snd

@[simp]
theorem diagram_left : D.diagram.left = D.V :=
  rfl
#align category_theory.glue_data.diagram_left CategoryTheory.GlueData.diagram_left

@[simp]
theorem diagram_right : D.diagram.right = D.U :=
  rfl
#align category_theory.glue_data.diagram_right CategoryTheory.GlueData.diagram_right

section

variable [HasMulticoequalizer D.diagram]

/-- The glued object given a family of gluing data. -/
def glued : C :=
  multicoequalizer D.diagram
#align category_theory.glue_data.glued CategoryTheory.GlueData.glued

/-- The map `D.U i ‚ü∂ D.glued` for each `i`. -/
def Œπ (i : D.J) : D.U i ‚ü∂ D.glued :=
  Multicoequalizer.œÄ D.diagram i
#align category_theory.glue_data.Œπ CategoryTheory.GlueData.Œπ

@[elementwise (attr := simp)]
theorem glue_condition (i j : D.J) : D.t i j ‚â´ D.f j i ‚â´ D.Œπ j = D.f i j ‚â´ D.Œπ i :=
  (Category.assoc _ _ _).symm.trans (Multicoequalizer.condition D.diagram ‚ü®i, j‚ü©).symm
#align category_theory.glue_data.glue_condition CategoryTheory.GlueData.glue_condition

/-- The pullback cone spanned by `V i j ‚ü∂ U i` and `V i j ‚ü∂ U j`.
This will often be a pullback diagram. -/
def vPullbackCone (i j : D.J) : PullbackCone (D.Œπ i) (D.Œπ j) :=
  PullbackCone.mk (D.f i j) (D.t i j ‚â´ D.f j i) (by simp)
set_option linter.uppercaseLean3 false in
#align category_theory.glue_data.V_pullback_cone CategoryTheory.GlueData.vPullbackCone

variable [HasColimits C]

/-- The projection `‚àê D.U ‚ü∂ D.glued` given by the colimit. -/
def œÄ : D.sigmaOpens ‚ü∂ D.glued :=
  Multicoequalizer.sigmaœÄ D.diagram
#align category_theory.glue_data.œÄ CategoryTheory.GlueData.œÄ

instance œÄ_epi : Epi D.œÄ := by
  unfold œÄ
  infer_instance
#align category_theory.glue_data.œÄ_epi CategoryTheory.GlueData.œÄ_epi

end

theorem types_œÄ_surjective (D : GlueData (Type*)) : Function.Surjective D.œÄ :=
  (epi_iff_surjective _).mp inferInstance
#align category_theory.glue_data.types_œÄ_surjective CategoryTheory.GlueData.types_œÄ_surjective

theorem types_Œπ_jointly_surjective (D : GlueData (Type v)) (x : D.glued) :
    ‚àÉ (i : _) (y : D.U i), D.Œπ i y = x := by
  delta CategoryTheory.GlueData.Œπ
  simp_rw [‚Üê Multicoequalizer.Œπ_sigmaœÄ D.diagram]
  rcases D.types_œÄ_surjective x with ‚ü®x', rfl‚ü©
  --have := colimit.isoColimitCocone (Types.coproductColimitCocone _)
  rw [‚Üê show (colimit.isoColimitCocone (Types.coproductColimitCocone.{v, v} _)).inv _ = x' from
      ConcreteCategory.congr_hom
        (colimit.isoColimitCocone (Types.coproductColimitCocone _)).hom_inv_id x']
  rcases (colimit.isoColimitCocone (Types.coproductColimitCocone _)).hom x' with ‚ü®i, y‚ü©
  exact ‚ü®i, y, by
    simp [‚Üê Multicoequalizer.Œπ_sigmaœÄ, -Multicoequalizer.Œπ_sigmaœÄ]
    rfl ‚ü©
#align category_theory.glue_data.types_Œπ_jointly_surjective CategoryTheory.GlueData.types_Œπ_jointly_surjective

variable (F : C ‚•§ C') [H : ‚àÄ i j k, PreservesLimit (cospan (D.f i j) (D.f i k)) F]

instance (i j k : D.J) : HasPullback (F.map (D.f i j)) (F.map (D.f i k)) :=
  ‚ü®‚ü®‚ü®_, isLimitOfHasPullbackOfPreservesLimit F (D.f i j) (D.f i k)‚ü©‚ü©‚ü©

/-- A functor that preserves the pullbacks of `f i j` and `f i k` can map a family of glue data. -/
@[simps]
def mapGlueData : GlueData C' where
  J := D.J
  U i := F.obj (D.U i)
  V i := F.obj (D.V i)
  f i j := F.map (D.f i j)
  f_mono i j := preserves_mono_of_preservesLimit _ _
  f_id i := inferInstance
  t i j := F.map (D.t i j)
  t_id i := by
    simp [D.t_id i]
  t' i j k :=
    (PreservesPullback.iso F (D.f i j) (D.f i k)).inv ‚â´
      F.map (D.t' i j k) ‚â´ (PreservesPullback.iso F (D.f j k) (D.f j i)).hom
  t_fac i j k := by simpa [Iso.inv_comp_eq] using congr_arg (fun f => F.map f) (D.t_fac i j k)
  cocycle i j k := by
    simp only [Category.assoc, Iso.hom_inv_id_assoc, ‚Üê Functor.map_comp_assoc, D.cocycle,
      Iso.inv_hom_id, CategoryTheory.Functor.map_id, Category.id_comp]
#align category_theory.glue_data.map_glue_data CategoryTheory.GlueData.mapGlueData

/-- The diagram of the image of a `GlueData` under a functor `F` is naturally isomorphic to the
original diagram of the `GlueData` via `F`.
-/
def diagramIso : D.diagram.multispan ‚ãô F ‚âÖ (D.mapGlueData F).diagram.multispan :=
  NatIso.ofComponents
    (fun x =>
      match x with
      | WalkingMultispan.left a => Iso.refl _
      | WalkingMultispan.right b => Iso.refl _)
    (by
      rintro (‚ü®_, _‚ü© | _) _ (_ | _ | _)
      ¬∑ erw [Category.comp_id, Category.id_comp, Functor.map_id]
        rfl
      ¬∑ erw [Category.comp_id, Category.id_comp]
        rfl
      ¬∑ erw [Category.comp_id, Category.id_comp, Functor.map_comp]
        rfl
      ¬∑ erw [Category.comp_id, Category.id_comp, Functor.map_id]
        rfl)
#align category_theory.glue_data.diagram_iso CategoryTheory.GlueData.diagramIso

@[simp]
theorem diagramIso_app_left (i : D.J √ó D.J) :
    (D.diagramIso F).app (WalkingMultispan.left i) = Iso.refl _ :=
  rfl
#align category_theory.glue_data.diagram_iso_app_left CategoryTheory.GlueData.diagramIso_app_left

@[simp]
theorem diagramIso_app_right (i : D.J) :
    (D.diagramIso F).app (WalkingMultispan.right i) = Iso.refl _ :=
  rfl
#align category_theory.glue_data.diagram_iso_app_right CategoryTheory.GlueData.diagramIso_app_right

@[simp]
theorem diagramIso_hom_app_left (i : D.J √ó D.J) :
    (D.diagramIso F).hom.app (WalkingMultispan.left i) = ùüô _ :=
  rfl
#align category_theory.glue_data.diagram_iso_hom_app_left CategoryTheory.GlueData.diagramIso_hom_app_left

@[simp]
theorem diagramIso_hom_app_right (i : D.J) :
    (D.diagramIso F).hom.app (WalkingMultispan.right i) = ùüô _ :=
  rfl
#align category_theory.glue_data.diagram_iso_hom_app_right CategoryTheory.GlueData.diagramIso_hom_app_right

@[simp]
theorem diagramIso_inv_app_left (i : D.J √ó D.J) :
    (D.diagramIso F).inv.app (WalkingMultispan.left i) = ùüô _ :=
  rfl
#align category_theory.glue_data.diagram_iso_inv_app_left CategoryTheory.GlueData.diagramIso_inv_app_left

@[simp]
theorem diagramIso_inv_app_right (i : D.J) :
    (D.diagramIso F).inv.app (WalkingMultispan.right i) = ùüô _ :=
  rfl
#align category_theory.glue_data.diagram_iso_inv_app_right CategoryTheory.GlueData.diagramIso_inv_app_right

variable [HasMulticoequalizer D.diagram] [PreservesColimit D.diagram.multispan F]

theorem hasColimit_multispan_comp : HasColimit (D.diagram.multispan ‚ãô F) :=
  ‚ü®‚ü®‚ü®_, PreservesColimit.preserves (colimit.isColimit _)‚ü©‚ü©‚ü©
#align category_theory.glue_data.has_colimit_multispan_comp CategoryTheory.GlueData.hasColimit_multispan_comp

attribute [local instance] hasColimit_multispan_comp

theorem hasColimit_mapGlueData_diagram : HasMulticoequalizer (D.mapGlueData F).diagram :=
  hasColimitOfIso (D.diagramIso F).symm
#align category_theory.glue_data.has_colimit_map_glue_data_diagram CategoryTheory.GlueData.hasColimit_mapGlueData_diagram

attribute [local instance] hasColimit_mapGlueData_diagram

/-- If `F` preserves the gluing, we obtain an iso between the glued objects. -/
def gluedIso : F.obj D.glued ‚âÖ (D.mapGlueData F).glued :=
  haveI : HasColimit (MultispanIndex.multispan (diagram (mapGlueData D F))) := inferInstance
  preservesColimitIso F D.diagram.multispan ‚â™‚â´ Limits.HasColimit.isoOfNatIso (D.diagramIso F)
#align category_theory.glue_data.glued_iso CategoryTheory.GlueData.gluedIso

@[reassoc (attr := simp)]
theorem Œπ_gluedIso_hom (i : D.J) : F.map (D.Œπ i) ‚â´ (D.gluedIso F).hom = (D.mapGlueData F).Œπ i := by
  haveI : HasColimit (MultispanIndex.multispan (diagram (mapGlueData D F))) := inferInstance
  erw [Œπ_preservesColimitsIso_hom_assoc]
  rw [HasColimit.isoOfNatIso_Œπ_hom]
  erw [Category.id_comp]
  rfl
#align category_theory.glue_data.Œπ_glued_iso_hom CategoryTheory.GlueData.Œπ_gluedIso_hom

@[reassoc (attr := simp)]
theorem Œπ_gluedIso_inv (i : D.J) : (D.mapGlueData F).Œπ i ‚â´ (D.gluedIso F).inv = F.map (D.Œπ i) := by
  rw [Iso.comp_inv_eq, Œπ_gluedIso_hom]
#align category_theory.glue_data.Œπ_glued_iso_inv CategoryTheory.GlueData.Œπ_gluedIso_inv

/-- If `F` preserves the gluing, and reflects the pullback of `U i ‚ü∂ glued` and `U j ‚ü∂ glued`,
then `F` reflects the fact that `V_pullback_cone` is a pullback. -/
def vPullbackConeIsLimitOfMap (i j : D.J) [ReflectsLimit (cospan (D.Œπ i) (D.Œπ j)) F]
    (hc : IsLimit ((D.mapGlueData F).vPullbackCone i j)) : IsLimit (D.vPullbackCone i j) := by
  apply isLimitOfReflects F
  apply (isLimitMapConePullbackConeEquiv _ _).symm _
  let e : cospan (F.map (D.Œπ i)) (F.map (D.Œπ j)) ‚âÖ
      cospan ((D.mapGlueData F).Œπ i) ((D.mapGlueData F).Œπ j) :=
    NatIso.ofComponents
      (fun x => by
        cases x
        exacts [D.gluedIso F, Iso.refl _])
      (by rintro (_ | _) (_ | _) (_ | _ | _) <;> simp)
  apply IsLimit.postcomposeHomEquiv e _ _
  apply hc.ofIsoLimit
  refine Cones.ext (Iso.refl _) ?_
  rintro (_ | _ | _)
  change _ = _ ‚â´ (_ ‚â´ _) ‚â´ _
  all_goals change _ = ùüô _ ‚â´ _ ‚â´ _; aesop_cat
set_option linter.uppercaseLean3 false in
#align category_theory.glue_data.V_pullback_cone_is_limit_of_map CategoryTheory.GlueData.vPullbackConeIsLimitOfMap

/-- If there is a forgetful functor into `Type` that preserves enough (co)limits, then `D.Œπ` will
be jointly surjective. -/
theorem Œπ_jointly_surjective (F : C ‚•§ Type v) [PreservesColimit D.diagram.multispan F]
    [‚àÄ i j k : D.J, PreservesLimit (cospan (D.f i j) (D.f i k)) F] (x : F.obj D.glued) :
    ‚àÉ (i : _) (y : F.obj (D.U i)), F.map (D.Œπ i) y = x := by
  let e := D.gluedIso F
  obtain ‚ü®i, y, eq‚ü© := (D.mapGlueData F).types_Œπ_jointly_surjective (e.hom x)
  replace eq := congr_arg e.inv eq
  change ((D.mapGlueData F).Œπ i ‚â´ e.inv) y = (e.hom ‚â´ e.inv) x at eq
  rw [e.hom_inv_id, D.Œπ_gluedIso_inv] at eq
  exact ‚ü®i, y, eq‚ü©
#align category_theory.glue_data.Œπ_jointly_surjective CategoryTheory.GlueData.Œπ_jointly_surjective

end GlueData

end CategoryTheory
