/-
Copyright (c) 2019 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Kim Morrison, Simon Hudon
-/
import Mathlib.Algebra.Group.Action.Defs
import Mathlib.Algebra.Group.Equiv.Basic
import Mathlib.Algebra.Group.Units.Basic
import Mathlib.Algebra.Group.Units.Hom
import Mathlib.CategoryTheory.Groupoid
import Mathlib.CategoryTheory.Opposites

/-!
# Endomorphisms

Definition and basic properties of endomorphisms and automorphisms of an object in a category.

For each `X : C`, we provide `CategoryTheory.End X := X âŸ¶ X` with a monoid structure,
and `CategoryTheory.Aut X := X â‰… X` with a group structure.
-/


universe v v' u u'

namespace CategoryTheory

/-- Endomorphisms of an object in a category. Arguments order in multiplication agrees with
`Function.comp`, not with `CategoryTheory.CategoryStruct.comp`. -/
def End {C : Type u} [CategoryStruct.{v} C] (X : C) := X âŸ¶ X

namespace End

section Struct

variable {C : Type u} [CategoryStruct.{v} C] (X : C)

protected instance one : One (End X) := âŸ¨ğŸ™ XâŸ©

protected instance inhabited : Inhabited (End X) := âŸ¨ğŸ™ XâŸ©

/-- Multiplication of endomorphisms agrees with `Function.comp`, not with
`CategoryTheory.CategoryStruct.comp`. -/
protected instance mul : Mul (End X) := âŸ¨fun x y => y â‰« xâŸ©

variable {X}

/-- Assist the typechecker by expressing a morphism `X âŸ¶ X` as a term of `CategoryTheory.End X`. -/
def of (f : X âŸ¶ X) : End X := f

/-- Assist the typechecker by expressing an endomorphism `f : CategoryTheory.End X` as a term of
`X âŸ¶ X`. -/
def asHom (f : End X) : X âŸ¶ X := f

-- dsimp loops when applying this lemma to its LHS,
-- probably https://github.com/leanprover/lean4/pull/2867
@[simp, nolint simpNF] -- Porting note (https://github.com/leanprover-community/mathlib4/issues/11215): TODO: use `of`/`asHom`?
theorem one_def : (1 : End X) = ğŸ™ X := rfl

@[simp] -- Porting note (https://github.com/leanprover-community/mathlib4/issues/11215): TODO: use `of`/`asHom`?
theorem mul_def (xs ys : End X) : xs * ys = ys â‰« xs := rfl

end Struct

/-- Endomorphisms of an object form a monoid -/
instance monoid {C : Type u} [Category.{v} C] {X : C} : Monoid (End X) where
  mul_one := Category.id_comp
  one_mul := Category.comp_id
  mul_assoc := fun x y z => (Category.assoc z y x).symm

section MulAction

variable {C : Type u} [Category.{v} C]

open Opposite

instance mulActionRight {X Y : C} : MulAction (End Y) (X âŸ¶ Y) where
  smul r f := f â‰« r
  one_smul := Category.comp_id
  mul_smul _ _ _ := Eq.symm <| Category.assoc _ _ _

instance mulActionLeft {X : Cáµ’áµ–} {Y : C} : MulAction (End X) (unop X âŸ¶ Y) where
  smul r f := r.unop â‰« f
  one_smul := Category.id_comp
  mul_smul _ _ _ := Category.assoc _ _ _

theorem smul_right {X Y : C} {r : End Y} {f : X âŸ¶ Y} : r â€¢ f = f â‰« r :=
  rfl

theorem smul_left {X : Cáµ’áµ–} {Y : C} {r : End X} {f : unop X âŸ¶ Y} : r â€¢ f = r.unop â‰« f :=
  rfl

end MulAction

/-- In a groupoid, endomorphisms form a group -/
instance group {C : Type u} [Groupoid.{v} C] (X : C) : Group (End X) where
  inv_mul_cancel := Groupoid.comp_inv
  inv := Groupoid.inv

end End

theorem isUnit_iff_isIso {C : Type u} [Category.{v} C] {X : C} (f : End X) :
    IsUnit (f : End X) â†” IsIso f :=
  âŸ¨fun h => { out := âŸ¨h.unit.inv, âŸ¨h.unit.inv_val, h.unit.val_invâŸ©âŸ© }, fun h =>
    âŸ¨âŸ¨f, inv f, by simp, by simpâŸ©, rflâŸ©âŸ©

variable {C : Type u} [Category.{v} C] (X : C)

/-- Automorphisms of an object in a category.

The order of arguments in multiplication agrees with
`Function.comp`, not with `CategoryTheory.CategoryStruct.comp`.
-/
def Aut (X : C) := X â‰… X

namespace Aut

@[ext]
lemma ext {X : C} {Ï†â‚ Ï†â‚‚ : Aut X} (h : Ï†â‚.hom = Ï†â‚‚.hom) : Ï†â‚ = Ï†â‚‚ :=
  Iso.ext h

protected instance inhabited : Inhabited (Aut X) := âŸ¨Iso.refl XâŸ©

instance : Group (Aut X) where
  one := Iso.refl X
  inv := Iso.symm
  mul x y := Iso.trans y x
  mul_assoc _ _ _ := (Iso.trans_assoc _ _ _).symm
  one_mul := Iso.trans_refl
  mul_one := Iso.refl_trans
  inv_mul_cancel := Iso.self_symm_id

theorem Aut_mul_def (f g : Aut X) : f * g = g.trans f := rfl

theorem Aut_inv_def (f : Aut X) : fâ»Â¹ = f.symm := rfl

/-- Units in the monoid of endomorphisms of an object
are (multiplicatively) equivalent to automorphisms of that object.
-/
def unitsEndEquivAut : (End X)Ë£ â‰ƒ* Aut X where
  toFun f := âŸ¨f.1, f.2, f.4, f.3âŸ©
  invFun f := âŸ¨f.1, f.2, f.4, f.3âŸ©
  left_inv := fun âŸ¨_, _, _, _âŸ© => rfl
  right_inv := fun âŸ¨_, _, _, _âŸ© => rfl
  map_mul' f g := by cases f; cases g; rfl

/-- The inclusion of `Aut X` to `End X` as a monoid homomorphism. -/
@[simps!]
def toEnd (X : C) : Aut X â†’* End X := (Units.coeHom (End X)).comp (Aut.unitsEndEquivAut X).symm

/-- Isomorphisms induce isomorphisms of the automorphism group -/
def autMulEquivOfIso {X Y : C} (h : X â‰… Y) : Aut X â‰ƒ* Aut Y where
  toFun x := { hom := h.inv â‰« x.hom â‰« h.hom, inv := h.inv â‰« x.inv â‰« h.hom }
  invFun y := { hom := h.hom â‰« y.hom â‰« h.inv, inv := h.hom â‰« y.inv â‰« h.inv }
  left_inv _ := by aesop_cat
  right_inv _ := by aesop_cat
  map_mul' := by simp [Aut_mul_def]

end Aut

namespace Functor

variable {D : Type u'} [Category.{v'} D] (f : C â¥¤ D)

/-- `f.map` as a monoid hom between endomorphism monoids. -/
@[simps]
def mapEnd : End X â†’* End (f.obj X) where
  toFun := f.map
  map_mul' x y := f.map_comp y x
  map_one' := f.map_id X

/-- `f.mapIso` as a group hom between automorphism groups. -/
def mapAut : Aut X â†’* Aut (f.obj X) where
  toFun := f.mapIso
  map_mul' x y := f.mapIso_trans y x
  map_one' := f.mapIso_refl X

namespace FullyFaithful

variable {f}
variable (hf : FullyFaithful f)

/-- `mulEquivEnd` as an isomorphism between endomorphism monoids. -/
@[simps!]
noncomputable def mulEquivEnd (X : C) :
    End X â‰ƒ* End (f.obj X) where
  toEquiv := hf.homEquiv
  __ := mapEnd X f

/-- `mulEquivAut` as an isomorphism between automorphism groups. -/
@[simps!]
noncomputable def autMulEquivOfFullyFaithful (X : C) :
    Aut X â‰ƒ* Aut (f.obj X) where
  toEquiv := hf.isoEquiv
  __ := mapAut X f

end FullyFaithful

end Functor

namespace Equivalence

universe uC uC' vC vC' uD uD' vD vD'

variable {C : Type uC} [Category.{vC} C]
variable {C' : Type uC'} [Category.{vC'} C']
variable {D : Type uD} [Category.{vD} D]
variable {D' : Type uD'} [Category.{vD'} D']
variable {f : C â¥¤ D}  {g : C' â¥¤ D'}
variable {e : C â‰Œ C'} {e' : D â‰Œ D'}

/--
Suppose we have categories `C, C', D, D'` such that the diagram of functors
```
C ===== f =====> D
||e            ||e'
||             ||
C' ==== g ====> D'
```
commutes where `e` and `e'` are equivalence of categories.

Then we have an isomorphism of endomorphism monoids `End f â‰ƒ* End g'` and
-/
@[simps]
def endMonoidEquiv
    (sqâ‚€ : f.comp e'.functor â‰… e.functor.comp g) : End f â‰ƒ* End g :=
  let sqâ‚ : e.inverse.comp (f.comp e'.functor) â‰… g := sqâ‚€.inverseCompIso
  let sqâ‚‚ : f â‰… e.functor.comp (g.comp e'.inverse) := Iso.isoCompInverse sqâ‚€
  { toFun Î± :=
    { app c :=
        sqâ‚.inv.app c â‰«
        e'.functor.map (Î±.app (e.inverse.obj c)) â‰«
        sqâ‚.hom.app c
      naturality c c' h := by
        simp only [Functor.comp_obj, NatTrans.naturality_assoc, Functor.comp_map, Category.assoc,
          NatIso.cancel_natIso_inv_left]
        rw [â† sqâ‚.hom.naturality h, â† Category.assoc, â† e'.functor.map_comp, â† Category.assoc,
          Functor.comp_map, Functor.comp_map, â† e'.functor.map_comp, â† Î±.naturality] }
    invFun Î± :=
    { app c := sqâ‚‚.hom.app c â‰« e'.inverse.map (Î±.app (e.functor.obj c)) â‰« sqâ‚‚.inv.app c
      naturality c c' h := by
        simp only [Functor.comp_obj, NatTrans.naturality_assoc, Functor.comp_map, Category.assoc,
          NatIso.cancel_natIso_hom_left]
        rw [â† sqâ‚‚.inv.naturality h, â† Category.assoc, â† e'.inverse.map_comp, â† Category.assoc,
          Functor.comp_map, Functor.comp_map, â† e'.inverse.map_comp, â† Î±.naturality] }
    left_inv Î± := NatTrans.ext <| funext fun c â†¦ by
      simp only [Functor.comp_obj, Iso.isoCompInverse_hom_app, Iso.inverseCompIso_inv_app,
        Iso.inverseCompIso_hom_app, Category.assoc, Functor.map_comp, inv_fun_map, Functor.id_obj,
        Iso.isoCompInverse_inv_app, sqâ‚‚, sqâ‚]
      have eqâ‚€ := sqâ‚€.hom.naturality (e.unit.app c)
      simp only [Functor.id_obj, Functor.comp_obj, Functor.comp_map, sqâ‚, sqâ‚‚] at eqâ‚€
      slice_lhs 2 4 =>
        rw [â† e'.inverse.map_comp, â† e'.inverse.map_comp, e.counitInv_app_functor c,
          â† Category.assoc, â† eqâ‚€]
      simp only [Category.assoc, Iso.hom_inv_id_app, Functor.comp_obj, Category.comp_id,
        inv_fun_map, Functor.id_obj, NatTrans.naturality, Iso.hom_inv_id_app_assoc, sqâ‚, sqâ‚‚]
      have eqâ‚ := sqâ‚€.hom.naturality (e.unitInv.app c)
      simp only [Functor.comp_obj, Functor.id_obj, Functor.comp_map, sqâ‚, sqâ‚‚] at eqâ‚
      slice_lhs 4 6 =>
        rw [â† e'.inverse.map_comp, â† e'.inverse.map_comp, e.counit_app_functor c,
          â† Category.assoc, â† eqâ‚]
      simp only [Category.assoc, Iso.hom_inv_id_app, Functor.comp_obj, Category.comp_id,
        inv_fun_map, Functor.id_obj, Iso.hom_inv_id_app_assoc, sqâ‚, sqâ‚‚]
      simp only [â† Functor.map_comp, Iso.hom_inv_id_app, Functor.id_obj, Functor.map_id,
        Category.comp_id, sqâ‚, sqâ‚‚]
    right_inv Î± := NatTrans.ext <| funext fun c â†¦ by
      simp only [Functor.comp_obj, Iso.inverseCompIso_inv_app, Iso.isoCompInverse_hom_app,
        Iso.isoCompInverse_inv_app, Category.assoc, Functor.map_comp, fun_inv_map, Functor.id_obj,
        counitInv_functor_comp, Category.comp_id, Iso.inv_hom_id_app_assoc, functor_unit_comp_assoc,
        Iso.inverseCompIso_hom_app, NatTrans.naturality_assoc, sqâ‚, sqâ‚‚]
      simp only [â† Functor.map_comp, Iso.inv_hom_id_app, Functor.id_obj, Functor.map_id,
        Category.comp_id, sqâ‚, sqâ‚‚]
    map_mul' Î± Î² := by
      simp only [Functor.comp_obj, Iso.inverseCompIso_inv_app, End.mul_def, NatTrans.comp_app,
        Functor.map_comp, Iso.inverseCompIso_hom_app, Category.assoc, sqâ‚, sqâ‚‚]
      refine NatTrans.ext <| funext fun c â†¦ show _ = _ â‰« _ from ?_
      simp only [Category.assoc, sqâ‚, sqâ‚‚]
      congr 2
      rw [â† e'.functor.map_comp_assoc, â† g.map_comp_assoc, Iso.hom_inv_id_app]
      simp only [Functor.map_comp, Category.assoc, Functor.comp_obj, Functor.map_id,
        Category.id_comp, Iso.hom_inv_id_app_assoc, sqâ‚, sqâ‚‚] }

end Equivalence

end CategoryTheory
