/-
Copyright (c) 2019 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Kim Morrison, Simon Hudon
-/
import Mathlib.Algebra.Group.Action.Defs
import Mathlib.Algebra.Group.Equiv.Defs
import Mathlib.Algebra.Group.Opposite
import Mathlib.Algebra.Group.Units.Hom
import Mathlib.CategoryTheory.Groupoid

/-!
# Endomorphisms

Definition and basic properties of endomorphisms and automorphisms of an object in a category.

For each `X : C`, we provide `CategoryTheory.End X := X ‚ü∂ X` with a monoid structure,
and `CategoryTheory.Aut X := X ‚âÖ X` with a group structure.
-/


universe v v' u u'

namespace CategoryTheory

/-- Endomorphisms of an object in a category. Arguments order in multiplication agrees with
`Function.comp`, not with `CategoryTheory.CategoryStruct.comp`. -/
def End {C : Type u} [CategoryStruct.{v} C] (X : C) := X ‚ü∂ X

namespace End

section Struct

variable {C : Type u} [CategoryStruct.{v} C] (X : C)

protected instance one : One (End X) := ‚ü®ùüô X‚ü©

protected instance inhabited : Inhabited (End X) := ‚ü®ùüô X‚ü©

/-- Multiplication of endomorphisms agrees with `Function.comp`, not with
`CategoryTheory.CategoryStruct.comp`. -/
protected instance mul : Mul (End X) := ‚ü®fun x y => y ‚â´ x‚ü©

variable {X}

/-- Assist the typechecker by expressing a morphism `X ‚ü∂ X` as a term of `CategoryTheory.End X`. -/
def of (f : X ‚ü∂ X) : End X := f

/-- Assist the typechecker by expressing an endomorphism `f : CategoryTheory.End X` as a term of
`X ‚ü∂ X`. -/
def asHom (f : End X) : X ‚ü∂ X := f

@[simp] -- Porting note (https://github.com/leanprover-community/mathlib4/issues/11215): TODO: use `of`/`asHom`?
theorem one_def : (1 : End X) = ùüô X := rfl

@[simp] -- Porting note (https://github.com/leanprover-community/mathlib4/issues/11215): TODO: use `of`/`asHom`?
theorem mul_def (xs ys : End X) : xs * ys = ys ‚â´ xs := rfl

end Struct

/-- Endomorphisms of an object form a monoid -/
instance monoid {C : Type u} [Category.{v} C] {X : C} : Monoid (End X) where
  mul_one := Category.id_comp
  one_mul := Category.comp_id
  mul_assoc := fun x y z => (Category.assoc z y x).symm

section MulAction

variable {C : Type u} [Category.{v} C]

open Opposite

instance mulActionRight {X Y : C} : MulAction (End Y) (X ‚ü∂ Y) where
  smul r f := f ‚â´ r
  one_smul := Category.comp_id
  mul_smul _ _ _ := Eq.symm <| Category.assoc _ _ _

instance mulActionLeft {X Y : C} : MulAction (End X)·µê·µí·µñ (X ‚ü∂ Y) where
  smul r f := r.unop ‚â´ f
  one_smul := Category.id_comp
  mul_smul _ _ _ := Category.assoc _ _ _

theorem smul_right {X Y : C} {r : End Y} {f : X ‚ü∂ Y} : r ‚Ä¢ f = f ‚â´ r :=
  rfl

theorem smul_left {X Y : C} {r : (End X)·µê·µí·µñ} {f : X ‚ü∂ Y} : r ‚Ä¢ f = r.unop ‚â´ f :=
  rfl

end MulAction

/-- In a groupoid, endomorphisms form a group -/
instance group {C : Type u} [Groupoid.{v} C] (X : C) : Group (End X) where
  inv_mul_cancel := Groupoid.comp_inv
  inv := Groupoid.inv

end End

theorem isUnit_iff_isIso {C : Type u} [Category.{v} C] {X : C} (f : End X) :
    IsUnit (f : End X) ‚Üî IsIso f :=
  ‚ü®fun h => { out := ‚ü®h.unit.inv, ‚ü®h.unit.inv_val, h.unit.val_inv‚ü©‚ü© }, fun h =>
    ‚ü®‚ü®f, inv f, by simp, by simp‚ü©, rfl‚ü©‚ü©

variable {C : Type u} [Category.{v} C] (X : C)

/-- Automorphisms of an object in a category.

The order of arguments in multiplication agrees with
`Function.comp`, not with `CategoryTheory.CategoryStruct.comp`.
-/
def Aut (X : C) := X ‚âÖ X

namespace Aut

@[ext]
lemma ext {X : C} {œÜ‚ÇÅ œÜ‚ÇÇ : Aut X} (h : œÜ‚ÇÅ.hom = œÜ‚ÇÇ.hom) : œÜ‚ÇÅ = œÜ‚ÇÇ :=
  Iso.ext h

protected instance inhabited : Inhabited (Aut X) := ‚ü®Iso.refl X‚ü©

instance : Group (Aut X) where
  one := Iso.refl X
  inv := Iso.symm
  mul x y := Iso.trans y x
  mul_assoc _ _ _ := (Iso.trans_assoc _ _ _).symm
  one_mul := Iso.trans_refl
  mul_one := Iso.refl_trans
  inv_mul_cancel := Iso.self_symm_id

theorem Aut_mul_def (f g : Aut X) : f * g = g.trans f := rfl

theorem Aut_inv_def (f : Aut X) : f‚Åª¬π = f.symm := rfl

/-- Units in the monoid of endomorphisms of an object
are (multiplicatively) equivalent to automorphisms of that object.
-/
def unitsEndEquivAut : (End X)À£ ‚âÉ* Aut X where
  toFun f := ‚ü®f.1, f.2, f.4, f.3‚ü©
  invFun f := ‚ü®f.1, f.2, f.4, f.3‚ü©
  map_mul' f g := by cases f; cases g; rfl

/-- The inclusion of `Aut X` to `End X` as a monoid homomorphism. -/
@[simps!]
def toEnd (X : C) : Aut X ‚Üí* End X := (Units.coeHom (End X)).comp (Aut.unitsEndEquivAut X).symm

/-- Isomorphisms induce isomorphisms of the automorphism group -/
def autMulEquivOfIso {X Y : C} (h : X ‚âÖ Y) : Aut X ‚âÉ* Aut Y where
  toFun x := { hom := h.inv ‚â´ x.hom ‚â´ h.hom, inv := h.inv ‚â´ x.inv ‚â´ h.hom }
  invFun y := { hom := h.hom ‚â´ y.hom ‚â´ h.inv, inv := h.hom ‚â´ y.inv ‚â´ h.inv }
  left_inv _ := by aesop_cat
  right_inv _ := by aesop_cat
  map_mul' := by simp [Aut_mul_def]

end Aut

namespace Functor

variable {D : Type u'} [Category.{v'} D] (f : C ‚•§ D)

/-- `f.map` as a monoid hom between endomorphism monoids. -/
@[simps]
def mapEnd : End X ‚Üí* End (f.obj X) where
  toFun := f.map
  map_mul' x y := f.map_comp y x
  map_one' := f.map_id X

/-- `f.mapIso` as a group hom between automorphism groups. -/
def mapAut : Aut X ‚Üí* Aut (f.obj X) where
  toFun := f.mapIso
  map_mul' x y := f.mapIso_trans y x
  map_one' := f.mapIso_refl X

namespace FullyFaithful

variable {f}
variable (hf : FullyFaithful f)

/-- `mulEquivEnd` as an isomorphism between endomorphism monoids. -/
@[simps!]
noncomputable def mulEquivEnd (X : C) :
    End X ‚âÉ* End (f.obj X) where
  toEquiv := hf.homEquiv
  __ := mapEnd X f

/-- `mulEquivAut` as an isomorphism between automorphism groups. -/
@[simps!]
noncomputable def autMulEquivOfFullyFaithful (X : C) :
    Aut X ‚âÉ* Aut (f.obj X) where
  toEquiv := hf.isoEquiv
  __ := mapAut X f

end FullyFaithful

end Functor

end CategoryTheory
