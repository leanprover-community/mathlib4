/-
Copyright (c) 2017 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Tim Baumann, Stephen Morgan, Kim Morrison, Floris van Doorn
-/
import Mathlib.Tactic.CategoryTheory.Reassoc

/-!
# Natural transformations

Defines natural transformations between functors.

A natural transformation `Î± : NatTrans F G` consists of morphisms `Î±.app X : F.obj X âŸ¶ G.obj X`,
and the naturality squares `Î±.naturality f : F.map f â‰« Î±.app Y = Î±.app X â‰« G.map f`,
where `f : X âŸ¶ Y`.

Note that we make `NatTrans.naturality` a simp lemma, with the preferred simp normal form
pushing components of natural transformations to the left.

See also `CategoryTheory.FunctorCat`, where we provide the category structure on
functors and natural transformations.

Introduces notations
* `Ï„.app X` for the components of natural transformations,
* `F âŸ¶ G` for the type of natural transformations between functors `F` and `G`
  (this and the next require `CategoryTheory.FunctorCat`),
* `Ïƒ â‰« Ï„` for vertical compositions, and
* `Ïƒ â—« Ï„` for horizontal compositions.

-/

set_option mathlib.tactic.category.grind true

namespace CategoryTheory

-- declare the `v`'s first; see note [category theory universes].
universe vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ uâ‚ uâ‚‚ uâ‚ƒ uâ‚„

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D]

/-- `NatTrans F G` represents a natural transformation between functors `F` and `G`.

The field `app` provides the components of the natural transformation.

Naturality is expressed by `Î±.naturality`.
-/
@[ext]
structure NatTrans (F G : C â¥¤ D) : Type max uâ‚ vâ‚‚ where
  /-- The component of a natural transformation. -/
  app : âˆ€ X : C, F.obj X âŸ¶ G.obj X
  /-- The naturality square for a given morphism. -/
  naturality : âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), F.map f â‰« app Y = app X â‰« G.map f := by cat_disch

-- Rather arbitrarily, we say that the 'simpler' form is
-- components of natural transformations moving earlier.
attribute [reassoc (attr := simp)] NatTrans.naturality

attribute [grind _=_] NatTrans.naturality

theorem congr_app {F G : C â¥¤ D} {Î± Î² : NatTrans F G} (h : Î± = Î²) (X : C) : Î±.app X = Î².app X := by
  cat_disch

namespace NatTrans

/-- `NatTrans.id F` is the identity natural transformation on a functor `F`. -/
protected def id (F : C â¥¤ D) : NatTrans F F where app X := ðŸ™ (F.obj X)

@[simp]
theorem id_app' (F : C â¥¤ D) (X : C) : (NatTrans.id F).app X = ðŸ™ (F.obj X) := rfl

instance (F : C â¥¤ D) : Inhabited (NatTrans F F) := âŸ¨NatTrans.id FâŸ©

open Category

open CategoryTheory.Functor

section

variable {F G H : C â¥¤ D}

/-- `vcomp Î± Î²` is the vertical compositions of natural transformations. -/
def vcomp (Î± : NatTrans F G) (Î² : NatTrans G H) : NatTrans F H where
  app X := Î±.app X â‰« Î².app X

-- functor_category will rewrite (vcomp Î± Î²) to (Î± â‰« Î²), so this is not a
-- suitable simp lemma.  We will declare the variant vcomp_app' there.
theorem vcomp_app (Î± : NatTrans F G) (Î² : NatTrans G H) (X : C) :
    (vcomp Î± Î²).app X = Î±.app X â‰« Î².app X := rfl

attribute [grind =] vcomp_app

end

/-- The diagram
```
   F(f)      F(g)      F(h)
F X ----> F Y ----> F U ----> F U
 |         |         |         |
 | Î±(X)    | Î±(Y)    | Î±(U)    | Î±(V)
 v         v         v         v
G X ----> G Y ----> G U ----> G V
    G(f)      G(g)      G(h)
```
commutes.
-/
example {F G : C â¥¤ D} (Î± : NatTrans F G) {X Y U V : C} (f : X âŸ¶ Y) (g : Y âŸ¶ U) (h : U âŸ¶ V) :
    Î±.app X â‰« G.map f â‰« G.map g â‰« G.map h = F.map f â‰« F.map g â‰« F.map h â‰« Î±.app V := by
  grind

end NatTrans

end CategoryTheory
