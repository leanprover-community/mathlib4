/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Whiskering
import Mathlib.CategoryTheory.Functor.FullyFaithful
import Mathlib.CategoryTheory.NatIso

#align_import category_theory.sigma.basic from "leanprover-community/mathlib"@"ba2245edf0c8bb155f1569fd9b9492a9b384cde6"

/-!
# Disjoint union of categories

We define the category structure on a sigma-type (disjoint union) of categories.
-/


namespace CategoryTheory

namespace Sigma

universe w‚ÇÅ w‚ÇÇ w‚ÇÉ v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

variable {I : Type w‚ÇÅ} {C : I ‚Üí Type u‚ÇÅ} [‚àÄ i, Category.{v‚ÇÅ} (C i)]

/-- The type of morphisms of a disjoint union of categories: for `X : C i` and `Y : C j`, a morphism
`(i, X) ‚ü∂ (j, Y)` if `i = j` is just a morphism `X ‚ü∂ Y`, and if `i ‚â† j` there are no such morphisms.
-/
inductive SigmaHom : (Œ£i, C i) ‚Üí (Œ£i, C i) ‚Üí Type max w‚ÇÅ v‚ÇÅ u‚ÇÅ
  | mk : ‚àÄ {i : I} {X Y : C i}, (X ‚ü∂ Y) ‚Üí SigmaHom ‚ü®i, X‚ü© ‚ü®i, Y‚ü©
#align category_theory.sigma.sigma_hom CategoryTheory.Sigma.SigmaHom

namespace SigmaHom

/-- The identity morphism on an object. -/
def id : ‚àÄ X : Œ£i, C i, SigmaHom X X
  | ‚ü®_, _‚ü© => mk (ùüô _)
-- Porting note: reordered universes
#align category_theory.sigma.sigma_hom.id CategoryTheory.Sigma.SigmaHom.id‚Çì

instance (X : Œ£i, C i) : Inhabited (SigmaHom X X) :=
  ‚ü®id X‚ü©

/-- Composition of sigma homomorphisms. -/
def comp : ‚àÄ {X Y Z : Œ£i, C i}, SigmaHom X Y ‚Üí SigmaHom Y Z ‚Üí SigmaHom X Z
  | _, _, _, mk f, mk g => mk (f ‚â´ g)
-- Porting note: reordered universes
#align category_theory.sigma.sigma_hom.comp CategoryTheory.Sigma.SigmaHom.comp‚Çì

instance : CategoryStruct (Œ£i, C i) where
  Hom := SigmaHom
  id := id
  comp f g := comp f g

@[simp]
lemma comp_def (i : I) (X Y Z : C i) (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : comp (mk f) (mk g) = mk (f ‚â´ g) :=
  rfl
#align category_theory.sigma.sigma_hom.comp_def CategoryTheory.Sigma.SigmaHom.comp_def

lemma assoc : ‚àÄ {X Y Z W : Œ£i, C i} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (h : Z ‚ü∂ W), (f ‚â´ g) ‚â´ h = f ‚â´ g ‚â´ h
  | _, _, _, _, mk _, mk _, mk _ => congr_arg mk (Category.assoc _ _ _)
#align category_theory.sigma.sigma_hom.assoc CategoryTheory.Sigma.SigmaHom.assoc

lemma id_comp : ‚àÄ {X Y : Œ£i, C i} (f : X ‚ü∂ Y), ùüô X ‚â´ f = f
  | _, _, mk _ => congr_arg mk (Category.id_comp _)
#align category_theory.sigma.sigma_hom.id_comp CategoryTheory.Sigma.SigmaHom.id_comp

lemma comp_id : ‚àÄ {X Y : Œ£i, C i} (f : X ‚ü∂ Y), f ‚â´ ùüô Y = f
  | _, _, mk _ => congr_arg mk (Category.comp_id _)
#align category_theory.sigma.sigma_hom.comp_id CategoryTheory.Sigma.SigmaHom.comp_id

end SigmaHom

instance sigma : Category (Œ£i, C i) where
  id_comp := SigmaHom.id_comp
  comp_id := SigmaHom.comp_id
  assoc := SigmaHom.assoc
#align category_theory.sigma.sigma CategoryTheory.Sigma.sigma

/-- The inclusion functor into the disjoint union of categories. -/
@[simps map]
def incl (i : I) : C i ‚•§ Œ£i, C i where
  obj X := ‚ü®i, X‚ü©
  map := SigmaHom.mk
#align category_theory.sigma.incl CategoryTheory.Sigma.incl

@[simp]
lemma incl_obj {i : I} (X : C i) : (incl i).obj X = ‚ü®i, X‚ü© :=
  rfl
#align category_theory.sigma.incl_obj CategoryTheory.Sigma.incl_obj

instance (i : I) : Full (incl i : C i ‚•§ Œ£i, C i) where
  preimage := fun ‚ü®f‚ü© => f
  witness := fun ‚ü®_‚ü© => rfl

instance (i : I) : Faithful (incl i : C i ‚•§ Œ£i, C i) where
  -- Porting note: was `tidy`
  map_injective {_ _ _ _} h := by injection h

section

variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] (F : ‚àÄ i, C i ‚•§ D)

/--
To build a natural transformation over the sigma category, it suffices to specify it restricted to
each subcategory.
-/
def natTrans {F G : (Œ£i, C i) ‚•§ D} (h : ‚àÄ i : I, incl i ‚ãô F ‚ü∂ incl i ‚ãô G) : F ‚ü∂ G where
  app := fun ‚ü®j, X‚ü© => (h j).app X
  naturality := by
    rintro ‚ü®j, X‚ü© ‚ü®_, _‚ü© ‚ü®f‚ü©
    apply (h j).naturality
#align category_theory.sigma.nat_trans CategoryTheory.Sigma.natTrans

@[simp]
lemma natTrans_app {F G : (Œ£i, C i) ‚•§ D} (h : ‚àÄ i : I, incl i ‚ãô F ‚ü∂ incl i ‚ãô G) (i : I)
    (X : C i) : (natTrans h).app ‚ü®i, X‚ü© = (h i).app X :=
  rfl
#align category_theory.sigma.nat_trans_app CategoryTheory.Sigma.natTrans_app

/-- (Implementation). An auxiliary definition to build the functor `desc`. -/
def descMap : ‚àÄ X Y : Œ£i, C i, (X ‚ü∂ Y) ‚Üí ((F X.1).obj X.2 ‚ü∂ (F Y.1).obj Y.2)
  | _, _, SigmaHom.mk g => (F _).map g
-- Porting note: reordered universes
#align category_theory.sigma.desc_map CategoryTheory.Sigma.descMap‚Çì

/-- Given a collection of functors `F i : C i ‚•§ D`, we can produce a functor `(Œ£ i, C i) ‚•§ D`.

The produced functor `desc F` satisfies: `incl i ‚ãô desc F ‚âÖ F i`, i.e. restricted to just the
subcategory `C i`, `desc F` agrees with `F i`, and it is unique (up to natural isomorphism) with
this property.

This witnesses that the sigma-type is the coproduct in Cat.
-/
@[simps obj]
def desc : (Œ£i, C i) ‚•§ D where
  obj X := (F X.1).obj X.2
  map g := descMap F _ _ g
  map_id := by
    rintro ‚ü®i, X‚ü©
    apply (F i).map_id
  map_comp := by
    rintro ‚ü®i, X‚ü© ‚ü®_, Y‚ü© ‚ü®_, Z‚ü© ‚ü®f‚ü© ‚ü®g‚ü©
    apply (F i).map_comp
#align category_theory.sigma.desc CategoryTheory.Sigma.desc

@[simp]
lemma desc_map_mk {i : I} (X Y : C i) (f : X ‚ü∂ Y) : (desc F).map (SigmaHom.mk f) = (F i).map f :=
  rfl
#align category_theory.sigma.desc_map_mk CategoryTheory.Sigma.desc_map_mk

-- We hand-generate the simp lemmas about this since they come out cleaner.
/-- This shows that when `desc F` is restricted to just the subcategory `C i`, `desc F` agrees with
`F i`.
-/
def inclDesc (i : I) : incl i ‚ãô desc F ‚âÖ F i :=
  NatIso.ofComponents fun X => Iso.refl _
#align category_theory.sigma.incl_desc CategoryTheory.Sigma.inclDesc

@[simp]
lemma inclDesc_hom_app (i : I) (X : C i) : (inclDesc F i).hom.app X = ùüô ((F i).obj X) :=
  rfl
#align category_theory.sigma.incl_desc_hom_app CategoryTheory.Sigma.inclDesc_hom_app

@[simp]
lemma inclDesc_inv_app (i : I) (X : C i) : (inclDesc F i).inv.app X = ùüô ((F i).obj X) :=
  rfl
#align category_theory.sigma.incl_desc_inv_app CategoryTheory.Sigma.inclDesc_inv_app

/-- If `q` when restricted to each subcategory `C i` agrees with `F i`, then `q` is isomorphic to
`desc F`.
-/
def descUniq (q : (Œ£i, C i) ‚•§ D) (h : ‚àÄ i, incl i ‚ãô q ‚âÖ F i) : q ‚âÖ desc F :=
  NatIso.ofComponents (fun ‚ü®i, X‚ü© => (h i).app X) <| by
    rintro ‚ü®i, X‚ü© ‚ü®_, _‚ü© ‚ü®f‚ü©
    apply (h i).hom.naturality f
#align category_theory.sigma.desc_uniq CategoryTheory.Sigma.descUniq

@[simp]
lemma descUniq_hom_app (q : (Œ£i, C i) ‚•§ D) (h : ‚àÄ i, incl i ‚ãô q ‚âÖ F i) (i : I) (X : C i) :
    (descUniq F q h).hom.app ‚ü®i, X‚ü© = (h i).hom.app X :=
  rfl
#align category_theory.sigma.desc_uniq_hom_app CategoryTheory.Sigma.descUniq_hom_app

@[simp]
lemma descUniq_inv_app (q : (Œ£i, C i) ‚•§ D) (h : ‚àÄ i, incl i ‚ãô q ‚âÖ F i) (i : I) (X : C i) :
    (descUniq F q h).inv.app ‚ü®i, X‚ü© = (h i).inv.app X :=
  rfl
#align category_theory.sigma.desc_uniq_inv_app CategoryTheory.Sigma.descUniq_inv_app

/--
If `q‚ÇÅ` and `q‚ÇÇ` when restricted to each subcategory `C i` agree, then `q‚ÇÅ` and `q‚ÇÇ` are isomorphic.
-/
@[simps]
def natIso {q‚ÇÅ q‚ÇÇ : (Œ£i, C i) ‚•§ D} (h : ‚àÄ i, incl i ‚ãô q‚ÇÅ ‚âÖ incl i ‚ãô q‚ÇÇ) : q‚ÇÅ ‚âÖ q‚ÇÇ where
  hom := natTrans fun i => (h i).hom
  inv := natTrans fun i => (h i).inv
#align category_theory.sigma.nat_iso CategoryTheory.Sigma.natIso

end

section

variable (C) {J : Type w‚ÇÇ} (g : J ‚Üí I)

/-- A function `J ‚Üí I` induces a functor `Œ£ j, C (g j) ‚•§ Œ£ i, C i`. -/
def map : (Œ£j : J, C (g j)) ‚•§ Œ£i : I, C i :=
  desc fun j => incl (g j)
#align category_theory.sigma.map CategoryTheory.Sigma.map

@[simp]
lemma map_obj (j : J) (X : C (g j)) : (Sigma.map C g).obj ‚ü®j, X‚ü© = ‚ü®g j, X‚ü© :=
  rfl
#align category_theory.sigma.map_obj CategoryTheory.Sigma.map_obj

@[simp]
lemma map_map {j : J} {X Y : C (g j)} (f : X ‚ü∂ Y) :
    (Sigma.map C g).map (SigmaHom.mk f) = SigmaHom.mk f :=
  rfl
#align category_theory.sigma.map_map CategoryTheory.Sigma.map_map

/-- The functor `Sigma.map C g` restricted to the subcategory `C j` acts as the inclusion of `g j`.
-/
@[simps!]
def inclCompMap (j : J) : incl j ‚ãô map C g ‚âÖ incl (g j) :=
  Iso.refl _
#align category_theory.sigma.incl_comp_map CategoryTheory.Sigma.inclCompMap

variable (I)

/-- The functor `Sigma.map` applied to the identity function is just the identity functor. -/
@[simps!]
def mapId : map C (id : I ‚Üí I) ‚âÖ ùü≠ (Œ£i, C i) :=
  natIso fun i => NatIso.ofComponents fun X => Iso.refl _
#align category_theory.sigma.map_id CategoryTheory.Sigma.mapId

variable {I} {K : Type w‚ÇÉ}

-- Porting note: Had to expand (G ‚àò g) to (fun i => C (g i)) in lemma statement
-- so that the suitable category instances could be found
/-- The functor `Sigma.map` applied to a composition is a composition of functors. -/
@[simps!]
def mapComp (f : K ‚Üí J) (g : J ‚Üí I) : map (fun x => C (g x)) f ‚ãô (map C g : _) ‚âÖ map C (g ‚àò f) :=
  (descUniq _ _) fun k =>
    (isoWhiskerRight (inclCompMap (fun i => C (g i)) f k) (map C g : _) : _) ‚â™‚â´ inclCompMap _ _ _
#align category_theory.sigma.map_comp CategoryTheory.Sigma.mapComp

end

namespace Functor

-- variable {C}

variable {D : I ‚Üí Type u‚ÇÅ} [‚àÄ i, Category.{v‚ÇÅ} (D i)]

/-- Assemble an `I`-indexed family of functors into a functor between the sigma types.
-/
def sigma (F : ‚àÄ i, C i ‚•§ D i) : (Œ£i, C i) ‚•§ Œ£i, D i :=
  desc fun i => F i ‚ãô incl i
#align category_theory.sigma.functor.sigma CategoryTheory.Sigma.Functor.sigma

end Functor

namespace natTrans

variable {D : I ‚Üí Type u‚ÇÅ} [‚àÄ i, Category.{v‚ÇÅ} (D i)]

variable {F G : ‚àÄ i, C i ‚•§ D i}

/-- Assemble an `I`-indexed family of natural transformations into a single natural transformation.
-/
def sigma (Œ± : ‚àÄ i, F i ‚ü∂ G i) : Functor.sigma F ‚ü∂ Functor.sigma G where
  app f := SigmaHom.mk ((Œ± f.1).app _)
  naturality := by
    rintro ‚ü®i, X‚ü© ‚ü®_, _‚ü© ‚ü®f‚ü©
    change SigmaHom.mk _ = SigmaHom.mk _
    rw [(Œ± i).naturality]
#align category_theory.sigma.nat_trans.sigma CategoryTheory.Sigma.natTrans.sigma

end natTrans

end Sigma

end CategoryTheory
