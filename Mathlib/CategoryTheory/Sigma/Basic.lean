/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Whiskering
import Mathlib.CategoryTheory.Functor.FullyFaithful
import Mathlib.CategoryTheory.NatIso

/-!
# Disjoint union of categories

We define the category structure on a sigma-type (disjoint union) of categories.
-/


namespace CategoryTheory

namespace Sigma

universe wâ‚ wâ‚‚ wâ‚ƒ vâ‚ vâ‚‚ uâ‚ uâ‚‚

variable {I : Type wâ‚} {C : I â†’ Type uâ‚} [âˆ€ i, Category.{vâ‚} (C i)]

/-- The type of morphisms of a disjoint union of categories: for `X : C i` and `Y : C j`, a morphism
`(i, X) âŸ¶ (j, Y)` if `i = j` is just a morphism `X âŸ¶ Y`, and if `i â‰  j` there are no such morphisms.
-/
inductive SigmaHom : (Î£i, C i) â†’ (Î£i, C i) â†’ Type max wâ‚ vâ‚ uâ‚
  | mk : âˆ€ {i : I} {X Y : C i}, (X âŸ¶ Y) â†’ SigmaHom âŸ¨i, XâŸ© âŸ¨i, YâŸ©

namespace SigmaHom

/-- The identity morphism on an object. -/
def id : âˆ€ X : Î£i, C i, SigmaHom X X
  | âŸ¨_, _âŸ© => mk (ğŸ™ _)
-- Porting note: reordered universes

instance (X : Î£i, C i) : Inhabited (SigmaHom X X) :=
  âŸ¨id XâŸ©

/-- Composition of sigma homomorphisms. -/
def comp : âˆ€ {X Y Z : Î£i, C i}, SigmaHom X Y â†’ SigmaHom Y Z â†’ SigmaHom X Z
  | _, _, _, mk f, mk g => mk (f â‰« g)
-- Porting note: reordered universes

instance : CategoryStruct (Î£i, C i) where
  Hom := SigmaHom
  id := id
  comp f g := comp f g

@[simp]
lemma comp_def (i : I) (X Y Z : C i) (f : X âŸ¶ Y) (g : Y âŸ¶ Z) : comp (mk f) (mk g) = mk (f â‰« g) :=
  rfl

lemma assoc : âˆ€ {X Y Z W : Î£i, C i} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (h : Z âŸ¶ W), (f â‰« g) â‰« h = f â‰« g â‰« h
  | _, _, _, _, mk _, mk _, mk _ => congr_arg mk (Category.assoc _ _ _)

lemma id_comp : âˆ€ {X Y : Î£i, C i} (f : X âŸ¶ Y), ğŸ™ X â‰« f = f
  | _, _, mk _ => congr_arg mk (Category.id_comp _)

lemma comp_id : âˆ€ {X Y : Î£i, C i} (f : X âŸ¶ Y), f â‰« ğŸ™ Y = f
  | _, _, mk _ => congr_arg mk (Category.comp_id _)

end SigmaHom

instance sigma : Category (Î£i, C i) where
  id_comp := SigmaHom.id_comp
  comp_id := SigmaHom.comp_id
  assoc := SigmaHom.assoc

/-- The inclusion functor into the disjoint union of categories. -/
@[simps map]
def incl (i : I) : C i â¥¤ Î£i, C i where
  obj X := âŸ¨i, XâŸ©
  map := SigmaHom.mk

@[simp]
lemma incl_obj {i : I} (X : C i) : (incl i).obj X = âŸ¨i, XâŸ© :=
  rfl

instance (i : I) : Functor.Full (incl i : C i â¥¤ Î£i, C i) where
  map_surjective := fun âŸ¨fâŸ© => âŸ¨f, rflâŸ©

instance (i : I) : Functor.Faithful (incl i : C i â¥¤ Î£i, C i) where
  map_injective {_ _ _ _} h := by injection h

section

variable {D : Type uâ‚‚} [Category.{vâ‚‚} D] (F : âˆ€ i, C i â¥¤ D)

/--
To build a natural transformation over the sigma category, it suffices to specify it restricted to
each subcategory.
-/
def natTrans {F G : (Î£i, C i) â¥¤ D} (h : âˆ€ i : I, incl i â‹™ F âŸ¶ incl i â‹™ G) : F âŸ¶ G where
  app := fun âŸ¨j, XâŸ© => (h j).app X
  naturality := by
    rintro âŸ¨j, XâŸ© âŸ¨_, _âŸ© âŸ¨fâŸ©
    apply (h j).naturality

@[simp]
lemma natTrans_app {F G : (Î£i, C i) â¥¤ D} (h : âˆ€ i : I, incl i â‹™ F âŸ¶ incl i â‹™ G) (i : I)
    (X : C i) : (natTrans h).app âŸ¨i, XâŸ© = (h i).app X :=
  rfl

/-- (Implementation). An auxiliary definition to build the functor `desc`. -/
def descMap : âˆ€ X Y : Î£i, C i, (X âŸ¶ Y) â†’ ((F X.1).obj X.2 âŸ¶ (F Y.1).obj Y.2)
  | _, _, SigmaHom.mk g => (F _).map g
-- Porting note: reordered universes

/-- Given a collection of functors `F i : C i â¥¤ D`, we can produce a functor `(Î£ i, C i) â¥¤ D`.

The produced functor `desc F` satisfies: `incl i â‹™ desc F â‰… F i`, i.e. restricted to just the
subcategory `C i`, `desc F` agrees with `F i`, and it is unique (up to natural isomorphism) with
this property.

This witnesses that the sigma-type is the coproduct in Cat.
-/
@[simps obj]
def desc : (Î£i, C i) â¥¤ D where
  obj X := (F X.1).obj X.2
  map g := descMap F _ _ g
  map_id := by
    rintro âŸ¨i, XâŸ©
    apply (F i).map_id
  map_comp := by
    rintro âŸ¨i, XâŸ© âŸ¨_, YâŸ© âŸ¨_, ZâŸ© âŸ¨fâŸ© âŸ¨gâŸ©
    apply (F i).map_comp

@[simp]
lemma desc_map_mk {i : I} (X Y : C i) (f : X âŸ¶ Y) : (desc F).map (SigmaHom.mk f) = (F i).map f :=
  rfl

-- We hand-generate the simp lemmas about this since they come out cleaner.
/-- This shows that when `desc F` is restricted to just the subcategory `C i`, `desc F` agrees with
`F i`.
-/
def inclDesc (i : I) : incl i â‹™ desc F â‰… F i :=
  NatIso.ofComponents fun _ => Iso.refl _

@[simp]
lemma inclDesc_hom_app (i : I) (X : C i) : (inclDesc F i).hom.app X = ğŸ™ ((F i).obj X) :=
  rfl

@[simp]
lemma inclDesc_inv_app (i : I) (X : C i) : (inclDesc F i).inv.app X = ğŸ™ ((F i).obj X) :=
  rfl

/-- If `q` when restricted to each subcategory `C i` agrees with `F i`, then `q` is isomorphic to
`desc F`.
-/
def descUniq (q : (Î£i, C i) â¥¤ D) (h : âˆ€ i, incl i â‹™ q â‰… F i) : q â‰… desc F :=
  NatIso.ofComponents (fun âŸ¨i, XâŸ© => (h i).app X) <| by
    rintro âŸ¨i, XâŸ© âŸ¨_, _âŸ© âŸ¨fâŸ©
    apply (h i).hom.naturality f

@[simp]
lemma descUniq_hom_app (q : (Î£i, C i) â¥¤ D) (h : âˆ€ i, incl i â‹™ q â‰… F i) (i : I) (X : C i) :
    (descUniq F q h).hom.app âŸ¨i, XâŸ© = (h i).hom.app X :=
  rfl

@[simp]
lemma descUniq_inv_app (q : (Î£i, C i) â¥¤ D) (h : âˆ€ i, incl i â‹™ q â‰… F i) (i : I) (X : C i) :
    (descUniq F q h).inv.app âŸ¨i, XâŸ© = (h i).inv.app X :=
  rfl

/--
If `qâ‚` and `qâ‚‚` when restricted to each subcategory `C i` agree, then `qâ‚` and `qâ‚‚` are isomorphic.
-/
@[simps]
def natIso {qâ‚ qâ‚‚ : (Î£i, C i) â¥¤ D} (h : âˆ€ i, incl i â‹™ qâ‚ â‰… incl i â‹™ qâ‚‚) : qâ‚ â‰… qâ‚‚ where
  hom := natTrans fun i â†¦ (h i).hom
  inv := natTrans fun i â†¦ (h i).inv

end

section

variable (C) {J : Type wâ‚‚} (g : J â†’ I)

/-- A function `J â†’ I` induces a functor `Î£ j, C (g j) â¥¤ Î£ i, C i`. -/
def map : (Î£j : J, C (g j)) â¥¤ Î£i : I, C i :=
  desc fun j â†¦ incl (g j)

@[simp]
lemma map_obj (j : J) (X : C (g j)) : (Sigma.map C g).obj âŸ¨j, XâŸ© = âŸ¨g j, XâŸ© :=
  rfl

@[simp]
lemma map_map {j : J} {X Y : C (g j)} (f : X âŸ¶ Y) :
    (Sigma.map C g).map (SigmaHom.mk f) = SigmaHom.mk f :=
  rfl

/-- The functor `Sigma.map C g` restricted to the subcategory `C j` acts as the inclusion of `g j`.
-/
@[simps!]
def inclCompMap (j : J) : incl j â‹™ map C g â‰… incl (g j) :=
  Iso.refl _

variable (I)

/-- The functor `Sigma.map` applied to the identity function is just the identity functor. -/
@[simps!]
def mapId : map C (id : I â†’ I) â‰… ğŸ­ (Î£i, C i) :=
  natIso fun i â†¦ NatIso.ofComponents fun _ => Iso.refl _

variable {I} {K : Type wâ‚ƒ}

-- Porting note: Had to expand (C âˆ˜ g) to (fun x â†¦ C (g x)) in lemma statement
-- so that the suitable category instances could be found
/-- The functor `Sigma.map` applied to a composition is a composition of functors. -/
@[simps!]
def mapComp (f : K â†’ J) (g : J â†’ I) : map (fun x â†¦ C (g x)) f â‹™ (map C g : _) â‰… map C (g âˆ˜ f) :=
  (descUniq _ _) fun k =>
    (isoWhiskerRight (inclCompMap (fun i â†¦ C (g i)) f k) (map C g : _) : _) â‰ªâ‰« inclCompMap _ _ _

end

namespace Functor

-- variable {C}
variable {D : I â†’ Type uâ‚} [âˆ€ i, Category.{vâ‚} (D i)]

/-- Assemble an `I`-indexed family of functors into a functor between the sigma types.
-/
def sigma (F : âˆ€ i, C i â¥¤ D i) : (Î£i, C i) â¥¤ Î£i, D i :=
  desc fun i â†¦ F i â‹™ incl i

end Functor

namespace natTrans

variable {D : I â†’ Type uâ‚} [âˆ€ i, Category.{vâ‚} (D i)]
variable {F G : âˆ€ i, C i â¥¤ D i}

/-- Assemble an `I`-indexed family of natural transformations into a single natural transformation.
-/
def sigma (Î± : âˆ€ i, F i âŸ¶ G i) : Functor.sigma F âŸ¶ Functor.sigma G where
  app f := SigmaHom.mk ((Î± f.1).app _)
  naturality := by
    rintro âŸ¨i, XâŸ© âŸ¨_, _âŸ© âŸ¨fâŸ©
    change SigmaHom.mk _ = SigmaHom.mk _
    rw [(Î± i).naturality]

end natTrans

end Sigma

end CategoryTheory
