/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.Algebra.BigOperators.Fin
import Mathlib.CategoryTheory.Monoidal.Linear
import Mathlib.CategoryTheory.Monoidal.Rigid.FunctorCategory
import Mathlib.CategoryTheory.Monoidal.Rigid.OfEquivalence
import Mathlib.CategoryTheory.Monoidal.Transport
import Mathlib.CategoryTheory.Monoidal.Types.Basic
import Mathlib.CategoryTheory.Action.Concrete
import Mathlib.CategoryTheory.Action.Limits

/-!
# Induced monoidal structure on `Action V G`

We show:

* When `V` is monoidal, braided, or symmetric, so is `Action V G`.
-/

universe u

open CategoryTheory Limits MonoidalCategory

variable {V : Type*} [Category V] {G : Type*} [Monoid G]

namespace Action

section Monoidal

open MonoidalCategory

variable [MonoidalCategory V]

@[simps! tensorUnit_V tensorObj_V tensorHom_hom whiskerLeft_hom whiskerRight_hom
  associator_hom_hom associator_inv_hom leftUnitor_hom_hom leftUnitor_inv_hom
  rightUnitor_hom_hom rightUnitor_inv_hom]
instance instMonoidalCategory : MonoidalCategory (Action V G) :=
  Monoidal.transport (Action.functorCategoryEquivalence _ _).symm

@[simp]
theorem tensorUnit_œÅ {g : G} :
    @DFunLike.coe (G ‚Üí* End (ùüô_ V)) _ _ _ (ùüô_ (Action V G)).œÅ g = ùüô (ùüô_ V) :=
  rfl

@[simp]
theorem tensor_œÅ {X Y : Action V G} {g : G} :
    @DFunLike.coe (G ‚Üí* End (X.V ‚äó Y.V)) _ _ _ (X ‚äó Y).œÅ g = X.œÅ g ‚äó‚Çò Y.œÅ g :=
  rfl

/-- Given an object `X` isomorphic to the tensor unit of `V`, `X` equipped with the trivial action
is isomorphic to the tensor unit of `Action V G`. -/
def tensorUnitIso {X : V} (f : ùüô_ V ‚âÖ X) : ùüô_ (Action V G) ‚âÖ Action.mk X 1 :=
  Action.mkIso f

variable (V G)

instance : (Action.forget V G).Monoidal :=
  Functor.CoreMonoidal.toMonoidal
    { ŒµIso := Iso.refl _
      ŒºIso := fun _ _ ‚Ü¶ Iso.refl _ }

open Functor.LaxMonoidal Functor.OplaxMonoidal

@[simp] lemma forget_Œµ : Œµ (Action.forget V G) = ùüô _ := rfl
@[simp] lemma forget_Œ∑ : Œµ (Action.forget V G) = ùüô _ := rfl

variable {V G}

@[simp] lemma forget_Œº (X Y : Action V G) : Œº (Action.forget V G) X Y = ùüô _ := rfl
@[simp] lemma forget_Œ¥ (X Y : Action V G) : Œ¥ (Action.forget V G) X Y = ùüô _ := rfl

variable (V G)

section

variable [BraidedCategory V]

instance : BraidedCategory (Action V G) :=
  .ofFaithful (Action.forget V G) fun X Y ‚Ü¶ mkIso (Œ≤_ _ _) fun g ‚Ü¶ by simp

@[simp]
theorem Œ≤_hom_hom {X Y : Action V G} : (Œ≤_ X Y).hom.hom = (Œ≤_ X.V Y.V).hom := rfl

@[simp]
theorem Œ≤_inv_hom {X Y : Action V G} : (Œ≤_ X Y).inv.hom = (Œ≤_ X.V Y.V).inv := rfl

/-- When `V` is braided the forgetful functor `Action V G` to `V` is braided. -/
instance : (Action.forget V G).Braided where

end

instance [SymmetricCategory V] : SymmetricCategory (Action V G) :=
  symmetricCategoryOfFaithful (Action.forget V G)

section

variable [Preadditive V] [MonoidalPreadditive V]

attribute [local simp] MonoidalPreadditive.whiskerLeft_add MonoidalPreadditive.add_whiskerRight

instance : MonoidalPreadditive (Action V G) where

variable {R : Type*} [Semiring R] [Linear R V] [MonoidalLinear R V]

instance : MonoidalLinear R (Action V G) where

end

noncomputable section

/-- Upgrading the functor `Action V G ‚•§ (SingleObj G ‚•§ V)` to a monoidal functor. -/
instance FunctorCategoryEquivalence.functorMonoidal :
    (FunctorCategoryEquivalence.functor (V := V) (G := G)).Monoidal :=
  inferInstanceAs (Monoidal.equivalenceTransported
    (Action.functorCategoryEquivalence V G).symm).inverse.Monoidal

instance functorCategoryEquivalenceFunctorMonoidal :
    (functorCategoryEquivalence V G).functor.Monoidal := by
  dsimp only [functorCategoryEquivalence_functor]; infer_instance

/-- Upgrading the functor `(SingleObj G ‚•§ V) ‚•§ Action V G` to a monoidal functor. -/
instance FunctorCategoryEquivalence.inverseMonoidal :
    (FunctorCategoryEquivalence.inverse (V := V) (G := G)).Monoidal :=
  inferInstanceAs (Monoidal.equivalenceTransported
    (Action.functorCategoryEquivalence V G).symm).functor.Monoidal

instance functorCategoryEquivalenceInverseMonoidal :
    (functorCategoryEquivalence V G).inverse.Monoidal := by
  dsimp only [functorCategoryEquivalence_inverse]; infer_instance

@[simp]
lemma FunctorCategoryEquivalence.functor_Œµ :
    Œµ (FunctorCategoryEquivalence.functor (V := V) (G := G)) = ùüô _ := rfl

@[simp]
lemma FunctorCategoryEquivalence.functor_Œ∑ :
    Œ∑ (FunctorCategoryEquivalence.functor (V := V) (G := G)) = ùüô _ := rfl

@[simp]
lemma FunctorCategoryEquivalence.functor_Œº (A B : Action V G) :
    Œº FunctorCategoryEquivalence.functor A B = ùüô _ := rfl

@[simp]
lemma FunctorCategoryEquivalence.functor_Œ¥ (A B : Action V G) :
    Œ¥ FunctorCategoryEquivalence.functor A B = ùüô _ := rfl


variable (H : Type*) [Group H]

instance [RightRigidCategory V] : RightRigidCategory (SingleObj H ‚•§ V) := by
  infer_instance

/-- If `V` is right rigid, so is `Action V G`. -/
instance [RightRigidCategory V] : RightRigidCategory (Action V H) :=
  rightRigidCategoryOfEquivalence
    (functorCategoryEquivalence V H).toAdjunction

instance [LeftRigidCategory V] : LeftRigidCategory (SingleObj H ‚•§ V) := by
  infer_instance

/-- If `V` is left rigid, so is `Action V G`. -/
instance [LeftRigidCategory V] : LeftRigidCategory (Action V H) :=
  leftRigidCategoryOfEquivalence (functorCategoryEquivalence V H).toAdjunction

instance [RigidCategory V] : RigidCategory (SingleObj H ‚•§ V) := by
  infer_instance

/-- If `V` is rigid, so is `Action V G`. -/
instance [RigidCategory V] : RigidCategory (Action V H) :=
  rigidCategoryOfEquivalence (functorCategoryEquivalence V H).toAdjunction

variable {V H}
variable (X : Action V H)

@[simp]
theorem rightDual_v [RightRigidCategory V] : X·òÅ.V = X.V·òÅ :=
  rfl

@[simp]
theorem leftDual_v [LeftRigidCategory V] : (·òÅX).V = ·òÅX.V :=
  rfl

theorem rightDual_œÅ [RightRigidCategory V] (h : H) : X·òÅ.œÅ h = (X.œÅ (h‚Åª¬π : H))·òÅ := by
  rw [‚Üê SingleObj.inv_as_inv]; rfl

theorem leftDual_œÅ [LeftRigidCategory V] (h : H) : (·òÅX).œÅ h = ·òÅX.œÅ (h‚Åª¬π : H) := by
  rw [‚Üê SingleObj.inv_as_inv]; rfl

end

end Monoidal

section

open MonoidalCategory

variable (G : Type u)

/-- The natural isomorphism of `G`-sets `G‚Åø‚Å∫¬π ‚âÖ G √ó G‚Åø`, where `G` acts by left multiplication on
each factor. -/
@[simps!]
noncomputable def diagonalSuccIsoTensorDiagonal [Monoid G] (n : ‚Ñï) :
    diagonal G (n + 1) ‚âÖ leftRegular G ‚äó diagonal G n :=
  mkIso (Fin.consEquiv _).symm.toIso fun _ => rfl

@[deprecated (since := "2025-06-02")] alias diagonalSucc := diagonalSuccIsoTensorDiagonal

variable [Group G]

/-- Given `X : Action (Type u) G` for `G` a group, then `G √ó X` (with `G` acting as left
multiplication on the first factor and by `X.œÅ` on the second) is isomorphic as a `G`-set to
`G √ó X` (with `G` acting as left multiplication on the first factor and trivially on the second).
The isomorphism is given by `(g, x) ‚Ü¶ (g, g‚Åª¬π ‚Ä¢ x)`. -/
@[simps!]
noncomputable def leftRegularTensorIso (X : Action (Type u) G) :
    leftRegular G ‚äó X ‚âÖ leftRegular G ‚äó trivial G X.V :=
  mkIso (Equiv.toIso {
    toFun g := ‚ü®g.1, (X.œÅ (g.1‚Åª¬π : G) g.2 : X.V)‚ü©
    invFun g := ‚ü®g.1, X.œÅ g.1 g.2‚ü©
    left_inv _ := Prod.ext rfl <| by simp
    right_inv _ := Prod.ext rfl <| by simp }) <| fun _ => by
      ext _
      simp only [tensorObj_V, tensor_œÅ, types_comp_apply, tensor_apply, ofMulAction_apply]
      simp

/-- An isomorphism of `G`-sets `G‚Åø‚Å∫¬π ‚âÖ G √ó G‚Åø`, where `G` acts by left multiplication on `G‚Åø‚Å∫¬π` and
`G` but trivially on `G‚Åø`. The map sends `(g‚ÇÄ, ..., g‚Çô) ‚Ü¶ (g‚ÇÄ, (g‚ÇÄ‚Åª¬πg‚ÇÅ, g‚ÇÅ‚Åª¬πg‚ÇÇ, ..., g‚Çô‚Çã‚ÇÅ‚Åª¬πg‚Çô))`,
and the inverse is `(g‚ÇÄ, (g‚ÇÅ, ..., g‚Çô)) ‚Ü¶ (g‚ÇÄ, g‚ÇÄg‚ÇÅ, g‚ÇÄg‚ÇÅg‚ÇÇ, ..., g‚ÇÄg‚ÇÅ...g‚Çô).` -/
noncomputable def diagonalSuccIsoTensorTrivial :
    ‚àÄ n : ‚Ñï, diagonal G (n + 1) ‚âÖ leftRegular G ‚äó trivial G (Fin n ‚Üí G)
  | 0 =>
    diagonalOneIsoLeftRegular G ‚â™‚â´
      (œÅ_ _).symm ‚â™‚â´ tensorIso (Iso.refl _) (tensorUnitIso (Equiv.ofUnique PUnit _).toIso)
  | n + 1 =>
    diagonalSuccIsoTensorDiagonal _ _ ‚â™‚â´
      tensorIso (Iso.refl _) (diagonalSuccIsoTensorTrivial n) ‚â™‚â´
        leftRegularTensorIso _ _ ‚â™‚â´
          tensorIso (Iso.refl _)
            (mkIso (Fin.insertNthEquiv (fun _ => G) 0).toIso fun _ => rfl)

variable {G}

@[simp]
theorem diagonalSuccIsoTensorTrivial_hom_hom_apply {n : ‚Ñï} (f : Fin (n + 1) ‚Üí G) :
    (diagonalSuccIsoTensorTrivial G n).hom.hom f =
      (f 0, fun i => (f (Fin.castSucc i))‚Åª¬π * f i.succ) := by
  induction n with
  | zero => exact Prod.ext rfl (funext fun x => Fin.elim0 x)
  | succ n hn =>
    refine Prod.ext rfl (funext fun x => ?_)
    induction x using Fin.cases
    <;> simp_all only [tensorObj_V, diagonalSuccIsoTensorTrivial, Iso.trans_hom, tensorIso_hom,
      Iso.refl_hom, id_tensorHom, comp_hom, whiskerLeft_hom, types_comp_apply, whiskerLeft_apply,
      leftRegularTensorIso_hom_hom, tensor_œÅ, tensor_apply, ofMulAction_apply]
    <;> simp [ofMulAction_V, types_tensorObj_def, Fin.tail]

@[simp]
theorem diagonalSuccIsoTensorTrivial_inv_hom_apply {n : ‚Ñï} (g : G) (f : Fin n ‚Üí G) :
    (diagonalSuccIsoTensorTrivial G n).inv.hom (g, f) =
      (g ‚Ä¢ Fin.partialProd f : Fin (n + 1) ‚Üí G) := by
  induction n generalizing g with
  | zero =>
    funext (x : Fin 1)
    simp [diagonalSuccIsoTensorTrivial, diagonalOneIsoLeftRegular, Subsingleton.elim x 0,
      ofMulAction_V]
  | succ n hn =>
    funext x
    induction x using Fin.cases
    <;> simp_all only [diagonalSuccIsoTensorTrivial, Iso.trans_inv, comp_hom,
        tensorObj_V, types_comp_apply, leftRegularTensorIso_inv_hom, tensor_œÅ, tensor_apply,
        ofMulAction_apply]
    <;> simp_all [types_tensorObj_def, mul_assoc, Fin.partialProd_succ', ofMulAction_V]

end

end Action

namespace CategoryTheory.Functor

open Action

variable {W : Type*} [Category W] [MonoidalCategory V] [MonoidalCategory W]
  (F : V ‚•§ W)

open Functor.LaxMonoidal Functor.OplaxMonoidal Functor.Monoidal

/-- A lax monoidal functor induces a lax monoidal functor between
the categories of `G`-actions within those categories. -/
instance [F.LaxMonoidal] : (F.mapAction G).LaxMonoidal where
  Œµ :=
    { hom := Œµ F
      comm := fun g => by
        dsimp [FunctorCategoryEquivalence.inverse, Functor.mapAction]
        rw [Category.id_comp, F.map_id, Category.comp_id] }
  Œº X Y :=
    { hom := Œº F X.V Y.V
      comm := fun g => Œº_natural F (X.œÅ g) (Y.œÅ g) }
  Œº_natural_left _ _ := by ext; simp
  Œº_natural_right _ _ := by ext; simp
  associativity _ _ _ := by ext; simp
  left_unitality _ := by ext; simp
  right_unitality _ := by ext; simp

@[simp]
lemma mapAction_Œµ_hom [F.LaxMonoidal] : (Œµ (F.mapAction G)).hom = Œµ F := rfl

@[simp]
lemma mapAction_Œº_hom [F.LaxMonoidal] (X Y : Action V G) :
    (Œº (F.mapAction G) X Y).hom = Œº F X.V Y.V := rfl

/-- An oplax monoidal functor induces an oplax monoidal functor between
the categories of `G`-actions within those categories. -/
instance [F.OplaxMonoidal] : (F.mapAction G).OplaxMonoidal where
  Œ∑ :=
    { hom := Œ∑ F
      comm := fun g => by
        dsimp [FunctorCategoryEquivalence.inverse, Functor.mapAction]
        rw [map_id, Category.id_comp, Category.comp_id] }
  Œ¥ X Y :=
    { hom := Œ¥ F X.V Y.V
      comm := fun g => (Œ¥_natural F (X.œÅ g) (Y.œÅ g)).symm }
  Œ¥_natural_left _ _ := by ext; simp
  Œ¥_natural_right _ _ := by ext; simp
  oplax_associativity _ _ _ := by ext; simp
  oplax_left_unitality _ := by ext; simp
  oplax_right_unitality _ := by ext; simp

@[simp]
lemma mapAction_Œ∑_hom [F.OplaxMonoidal] : (Œ∑ (F.mapAction G)).hom = Œ∑ F := rfl

@[simp]
lemma mapAction_Œ¥_hom [F.OplaxMonoidal] (X Y : Action V G) :
    (Œ¥ (F.mapAction G) X Y).hom = Œ¥ F X.V Y.V := rfl

/-- A monoidal functor induces a monoidal functor between
the categories of `G`-actions within those categories. -/
instance [F.Monoidal] : (F.mapAction G).Monoidal where
  Œ∑_Œµ := by ext; dsimp; rw [Œ∑_Œµ]
  Œµ_Œ∑ := by ext; dsimp; rw [Œµ_Œ∑]
  Œº_Œ¥ _ _ := by ext; dsimp; rw [Œº_Œ¥]
  Œ¥_Œº _ _ := by ext; dsimp; rw [Œ¥_Œº]

end CategoryTheory.Functor
