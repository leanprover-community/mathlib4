/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.Algebra.Group.Action.Pi
import Mathlib.CategoryTheory.FintypeCat
import Mathlib.GroupTheory.GroupAction.Quotient
import Mathlib.GroupTheory.QuotientGroup.Defs
import Mathlib.CategoryTheory.Action.Basic

/-!
# Constructors for `Action V G` for some concrete categories

We construct `Action (Type u) G` from a `[MulAction G X]` instance and give some applications.
-/

assert_not_exists Field

universe u v

open CategoryTheory Limits

namespace Action

/-- Bundles a type `H` with a multiplicative action of `G` as an `Action`. -/
def ofMulAction (G H : Type u) [Monoid G] [MulAction G H] : Action (Type u) (MonCat.of G) where
  V := H
  œÅ := MonCat.ofHom <| @MulAction.toEndHom _ _ _ (by assumption)

@[simp]
theorem ofMulAction_apply {G H : Type u} [Monoid G] [MulAction G H] (g : G) (x : H) :
    (ofMulAction G H).œÅ g x = (g ‚Ä¢ x : H) :=
  rfl

/-- Given a family `F` of types with `G`-actions, this is the limit cone demonstrating that the
product of `F` as types is a product in the category of `G`-sets. -/
def ofMulActionLimitCone {Œπ : Type v} (G : Type max v u) [Monoid G] (F : Œπ ‚Üí Type max v u)
    [‚àÄ i : Œπ, MulAction G (F i)] :
    LimitCone (Discrete.functor fun i : Œπ => Action.ofMulAction G (F i)) where
  cone :=
    { pt := Action.ofMulAction G (‚àÄ i : Œπ, F i)
      œÄ := Discrete.natTrans (fun i => ‚ü®fun x => x i.as, fun _ => rfl‚ü©) }
  isLimit :=
    { lift := fun s =>
        { hom := fun x i => (s.œÄ.app ‚ü®i‚ü©).hom x
          comm := fun g => by
            ext x
            funext j
            exact congr_fun ((s.œÄ.app ‚ü®j‚ü©).comm g) x }
      fac := fun _ _ => rfl
      uniq := fun s f h => by
        ext x
        funext j
        dsimp at *
        rw [‚Üê h ‚ü®j‚ü©]
        rfl }

/-- The `G`-set `G`, acting on itself by left multiplication. -/
@[simps!]
def leftRegular (G : Type u) [Monoid G] : Action (Type u) (MonCat.of G) :=
  Action.ofMulAction G G

/-- The `G`-set `G‚Åø`, acting on itself by left multiplication. -/
@[simps!]
def diagonal (G : Type u) [Monoid G] (n : ‚Ñï) : Action (Type u) (MonCat.of G) :=
  Action.ofMulAction G (Fin n ‚Üí G)

/-- We have `Fin 1 ‚Üí G ‚âÖ G` as `G`-sets, with `G` acting by left multiplication. -/
def diagonalOneIsoLeftRegular (G : Type u) [Monoid G] : diagonal G 1 ‚âÖ leftRegular G :=
  Action.mkIso (Equiv.funUnique _ _).toIso fun _ => rfl

namespace FintypeCat

/-- If `X` is a type with `[Fintype X]` and `G` acts on `X`, then `G` also acts on
`FintypeCat.of X`. -/
instance (G : Type*) (X : Type*) [Monoid G] [MulAction G X] [Fintype X] :
    MulAction G (FintypeCat.of X) :=
  inferInstanceAs <| MulAction G X

/-- Bundles a finite type `H` with a multiplicative action of `G` as an `Action`. -/
def ofMulAction (G : Type u) (H : FintypeCat.{u}) [Monoid G] [MulAction G H] :
    Action FintypeCat (MonCat.of G) where
  V := H
  œÅ := MonCat.ofHom <| @MulAction.toEndHom _ _ _ (by assumption)

@[simp]
theorem ofMulAction_apply {G : Type u} {H : FintypeCat.{u}} [Monoid G] [MulAction G H]
    (g : G) (x : H) : (FintypeCat.ofMulAction G H).œÅ g x = (g ‚Ä¢ x : H) :=
  rfl

section

/-- Shorthand notation for the quotient of `G` by `H` as a finite `G`-set. -/
notation:10 G:10 " ‚ß∏‚Çê " H:10 => Action.FintypeCat.ofMulAction G (FintypeCat.of <| G ‚ß∏ H)

variable {G : Type*} [Group G] (H N : Subgroup G) [Fintype (G ‚ß∏ N)]

/-- If `N` is a normal subgroup of `G`, then this is the group homomorphism
sending an element `g` of `G` to the `G`-endomorphism of `G ‚ß∏‚Çê N` given by
multiplication with `g‚Åª¬π` on the right. -/
def toEndHom [N.Normal] : G ‚Üí* End (G ‚ß∏‚Çê N) where
  toFun v := {
    hom := Quotient.lift (fun œÉ ‚Ü¶ ‚ü¶œÉ * v‚Åª¬π‚üß) <| fun a b h ‚Ü¶ Quotient.sound <| by
      apply (QuotientGroup.leftRel_apply).mpr
      -- We avoid `group` here to minimize imports while low in the hierarchy;
      -- typically it would be better to invoke the tactic.
      simpa [mul_assoc] using Subgroup.Normal.conj_mem ‚Äπ_‚Ä∫ _ (QuotientGroup.leftRel_apply.mp h) _
    comm := fun (g : G) ‚Ü¶ by
      ext (x : G ‚ß∏ N)
      induction' x using Quotient.inductionOn with x
      simp only [FintypeCat.comp_apply, Action.FintypeCat.ofMulAction_apply, Quotient.lift_mk]
      show Quotient.lift (fun œÉ ‚Ü¶ ‚ü¶œÉ * v‚Åª¬π‚üß) _ (‚ü¶g ‚Ä¢ x‚üß) = _
      simp only [smul_eq_mul, Quotient.lift_mk, mul_assoc]
      rfl
  }
  map_one' := by
    apply Action.hom_ext
    ext (x : G ‚ß∏ N)
    induction' x using Quotient.inductionOn with x
    simp
  map_mul' œÉ œÑ := by
    apply Action.hom_ext
    ext (x : G ‚ß∏ N)
    induction' x using Quotient.inductionOn with x
    show ‚ü¶x * (œÉ * œÑ)‚Åª¬π‚üß = ‚ü¶x * œÑ‚Åª¬π * œÉ‚Åª¬π‚üß
    rw [mul_inv_rev, mul_assoc]

@[simp]
lemma toEndHom_apply [N.Normal] (g h : G) : (toEndHom N g).hom ‚ü¶h‚üß = ‚ü¶h * g‚Åª¬π‚üß := rfl

variable {N} in
lemma toEndHom_trivial_of_mem [N.Normal] {n : G} (hn : n ‚àà N) : toEndHom N n = ùüô (G ‚ß∏‚Çê N) := by
  apply Action.hom_ext
  ext (x : G ‚ß∏ N)
  induction' x using Quotient.inductionOn with Œº
  exact Quotient.sound ((QuotientGroup.leftRel_apply).mpr <| by simpa)

/-- If `H` and `N` are subgroups of a group `G` with `N` normal, there is a canonical
group homomorphism `H ‚ß∏ N ‚äì H` to the `G`-endomorphisms of `G ‚ß∏ N`. -/
def quotientToEndHom [N.Normal] : H ‚ß∏ Subgroup.subgroupOf N H ‚Üí* End (G ‚ß∏‚Çê N) :=
  QuotientGroup.lift (Subgroup.subgroupOf N H) ((toEndHom N).comp H.subtype) <| fun _ uinU' ‚Ü¶
    toEndHom_trivial_of_mem uinU'

@[simp]
lemma quotientToEndHom_mk [N.Normal] (x : H) (g : G) :
    (quotientToEndHom H N ‚ü¶x‚üß).hom ‚ü¶g‚üß = ‚ü¶g * x‚Åª¬π‚üß :=
  rfl

/-- If `N` and `H` are subgroups of a group `G` with `N ‚â§ H`, this is the canonical
`G`-morphism `G ‚ß∏ N ‚ü∂ G ‚ß∏ H`. -/
def quotientToQuotientOfLE [Fintype (G ‚ß∏ H)] (h : N ‚â§ H) : (G ‚ß∏‚Çê N) ‚ü∂ (G ‚ß∏‚Çê H) where
  hom := Quotient.lift _ <| fun _ _ hab ‚Ü¶ Quotient.sound <|
    (QuotientGroup.leftRel_apply).mpr (h <| (QuotientGroup.leftRel_apply).mp hab)
  comm g := by
    ext (x : G ‚ß∏ N)
    induction' x using Quotient.inductionOn with Œº
    rfl

@[simp]
lemma quotientToQuotientOfLE_hom_mk [Fintype (G ‚ß∏ H)] (h : N ‚â§ H) (x : G) :
    (quotientToQuotientOfLE H N h).hom ‚ü¶x‚üß = ‚ü¶x‚üß :=
  rfl

end

end FintypeCat

section ToMulAction

variable {V : Type (u + 1)} [LargeCategory V] {FV : V ‚Üí V ‚Üí Type*} {CV : V ‚Üí Type*}
variable [‚àÄ X Y, FunLike (FV X Y) (CV X) (CV Y)] [ConcreteCategory V FV]

instance instMulAction {G : MonCat.{u}} (X : Action V G) :
    MulAction G (ToType X) where
  smul g x := ConcreteCategory.hom (X.œÅ g) x
  one_smul x := by
    show ConcreteCategory.hom (X.œÅ 1) x = x
    simp
  mul_smul g h x := by
    show ConcreteCategory.hom (X.œÅ (g * h)) x =
      ConcreteCategory.hom (X.œÅ g) ((ConcreteCategory.hom (X.œÅ h)) x)
    simp

/- Specialize `instMulAction` to assist typeclass inference. -/
instance {G : MonCat.{u}} (X : Action FintypeCat G) : MulAction G X.V := Action.instMulAction X
instance {G : Type u} [Group G] (X : Action FintypeCat (MonCat.of G)) : MulAction G X.V :=
  Action.instMulAction X

end ToMulAction

end Action
