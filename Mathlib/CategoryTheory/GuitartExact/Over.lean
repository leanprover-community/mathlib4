/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.GuitartExact.Basic
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.BinaryProducts

/-!
# Guitart exact squares involving `Over` categories

Let `F : C ‚•§ D` be a functor and `X : C`. One may consider the
commutative square of categories where vertical functors are `Over.forget`:
```
    Over.post F
Over X ‚•§ Over (F.obj X)
 |          |
 v          v
 C     ‚•§    D
       F
```

We show that this square is Guitart exact if for all `Y : C`, the binary product `X ‚®Ø Y`
exists and `F` commutes with it.

-/

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory

open Limits

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
  (F : C ‚•§ D) (X : C)

/-- Given `F : C ‚•§ D` and `X : C`, this is the `2`-square
```
    Over.post F
Over X ‚•§ Over (F.obj X)
 |          |
 v          v
 C     ‚•§    D
       F
```
with `Over.forget` as vertical functors. -/
abbrev TwoSquare.overPost :
    TwoSquare (Over.post F) (Over.forget X) (Over.forget (F.obj X)) F :=
  TwoSquare.mk _ _ _ _ (ùüô _)

instance [‚àÄ (Y : C), HasBinaryProduct X Y] [‚àÄ (Y : C), PreservesLimit (pair X Y) F] :
    (TwoSquare.overPost F X).GuitartExact where
  isConnected_rightwards {W Z} g := by
    let P : (TwoSquare.overPost F X).StructuredArrowRightwards g :=
      TwoSquare.StructuredArrowRightwards.mk _ _ (Over.mk (Y := X ‚®Ø Z) prod.fst)
        (Over.homMk (prod.lift (show W.left ‚ü∂ F.obj X from W.hom) g ‚â´ inv (prodComparison F X Z))
            (by simp [inv_prodComparison_map_fst])) prod.snd
            (by simp [inv_prodComparison_map_snd])
    have := Nonempty.intro P
    let œÜ (Q) : Q ‚ü∂ P := StructuredArrow.homMk (CostructuredArrow.homMk
      (Over.homMk (prod.lift Q.right.left.hom Q.right.hom))) (by
        ext
        dsimp
        rw [‚Üê cancel_mono (prodComparison F X _)]
        ext
        ¬∑ simpa [‚Üê Functor.map_comp, P] using Over.w Q.hom.left
        ¬∑ simpa [‚Üê Functor.map_comp, P] using CostructuredArrow.w Q.hom)
    exact zigzag_isConnected (fun Q‚ÇÅ Q‚ÇÇ ‚Ü¶ (Zigzag.of_hom (œÜ Q‚ÇÅ)).trans (Zigzag.of_inv (œÜ Q‚ÇÇ)))

end CategoryTheory
