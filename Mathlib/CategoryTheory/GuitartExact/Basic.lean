/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Limits.Final
import Mathlib.CategoryTheory.Functor.TwoSquare

/-!
# Guitart exact squares

Given four functors `T`, `L`, `R` and `B`, a 2-square `TwoSquare T L R B` consists of
a natural transformation `w : T ‚ãô R ‚ü∂ L ‚ãô B`:
```
     T
  C‚ÇÅ ‚•§ C‚ÇÇ
L |     | R
  v     v
  C‚ÇÉ ‚•§ C‚ÇÑ
     B
```

In this file, we define a typeclass `w.GuitartExact` which expresses
that this square is exact in the sense of Guitart. This means that
for any `X‚ÇÉ : C‚ÇÉ`, the induced functor
`CostructuredArrow L X‚ÇÉ ‚•§ CostructuredArrow R (B.obj X‚ÇÉ)` is final.
It is also equivalent to the fact that for any `X‚ÇÇ : C‚ÇÇ`, the
induced functor `StructuredArrow X‚ÇÇ T ‚•§ StructuredArrow (R.obj X‚ÇÇ) B`
is initial.

Various categorical notions (fully faithful functors, adjunctions, etc.) can
be characterized in terms of Guitart exact squares. Their particular role
in pointwise Kan extensions shall also be used in the construction of
derived functors.

## TODO

* Define the notion of derivability structure from
[the paper by Kahn and Maltsiniotis][KahnMaltsiniotis2008] using Guitart exact squares
and construct (pointwise) derived functors using this notion

## References
* https://ncatlab.org/nlab/show/exact+square
* [Ren√© Guitart, *Relations et carr√©s exacts*][Guitart1980]
* [Bruno Kahn and Georges Maltsiniotis, *Structures de d√©rivabilit√©*][KahnMaltsiniotis2008]

-/

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ v‚ÇÑ u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ

namespace CategoryTheory

open Category

variable {C‚ÇÅ : Type u‚ÇÅ} {C‚ÇÇ : Type u‚ÇÇ} {C‚ÇÉ : Type u‚ÇÉ} {C‚ÇÑ : Type u‚ÇÑ}
  [Category.{v‚ÇÅ} C‚ÇÅ] [Category.{v‚ÇÇ} C‚ÇÇ] [Category.{v‚ÇÉ} C‚ÇÉ] [Category.{v‚ÇÑ} C‚ÇÑ]
  (T : C‚ÇÅ ‚•§ C‚ÇÇ) (L : C‚ÇÅ ‚•§ C‚ÇÉ) (R : C‚ÇÇ ‚•§ C‚ÇÑ) (B : C‚ÇÉ ‚•§ C‚ÇÑ)

namespace TwoSquare

variable {T L R B} (w : TwoSquare T L R B)

/-- Given `w : TwoSquare T L R B` and `X‚ÇÉ : C‚ÇÉ`, this is the obvious functor
`CostructuredArrow L X‚ÇÉ ‚•§ CostructuredArrow R (B.obj X‚ÇÉ)`. -/
@[simps! obj map]
def costructuredArrowRightwards (X‚ÇÉ : C‚ÇÉ) :
    CostructuredArrow L X‚ÇÉ ‚•§ CostructuredArrow R (B.obj X‚ÇÉ) :=
  CostructuredArrow.post L B X‚ÇÉ ‚ãô Comma.mapLeft _ w ‚ãô
    CostructuredArrow.pre T R (B.obj X‚ÇÉ)

/-- Given `w : TwoSquare T L R B` and `X‚ÇÇ : C‚ÇÇ`, this is the obvious functor
`StructuredArrow X‚ÇÇ T ‚•§ StructuredArrow (R.obj X‚ÇÇ) B`. -/
@[simps! obj map]
def structuredArrowDownwards (X‚ÇÇ : C‚ÇÇ) :
    StructuredArrow X‚ÇÇ T ‚•§ StructuredArrow (R.obj X‚ÇÇ) B :=
  StructuredArrow.post X‚ÇÇ T R ‚ãô Comma.mapRight _ w ‚ãô
    StructuredArrow.pre (R.obj X‚ÇÇ) L B

section

variable {X‚ÇÇ : C‚ÇÇ} {X‚ÇÉ : C‚ÇÉ} (g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ)

/- In [the paper by Kahn and Maltsiniotis, ¬ß4.3][KahnMaltsiniotis2008], given
`w : TwoSquare T L R B` and `g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ`, a category `J` is introduced
and it is observed that it is equivalent to the two categories
`w.StructuredArrowRightwards g` and `w.CostructuredArrowDownwards g`. We shall show below
that there is an equivalence
`w.equivalenceJ g : w.StructuredArrowRightwards g ‚âå w.CostructuredArrowDownwards g`. -/

/-- Given `w : TwoSquare T L R B` and a morphism `g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ`, this is the
category `StructuredArrow (CostructuredArrow.mk g) (w.costructuredArrowRightwards X‚ÇÉ)`,
see the constructor `StructuredArrowRightwards.mk` for the data that is involved. -/
abbrev StructuredArrowRightwards :=
  StructuredArrow (CostructuredArrow.mk g) (w.costructuredArrowRightwards X‚ÇÉ)

/-- Given `w : TwoSquare T L R B` and a morphism `g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ`, this is the
category `CostructuredArrow (w.structuredArrowDownwards X‚ÇÇ) (StructuredArrow.mk g)`,
see the constructor `CostructuredArrowDownwards.mk` for the data that is involved. -/
abbrev CostructuredArrowDownwards :=
  CostructuredArrow (w.structuredArrowDownwards X‚ÇÇ) (StructuredArrow.mk g)

section

variable (X‚ÇÅ : C‚ÇÅ) (a : X‚ÇÇ ‚ü∂ T.obj X‚ÇÅ) (b : L.obj X‚ÇÅ ‚ü∂ X‚ÇÉ)

/-- Constructor for objects in `w.StructuredArrowRightwards g`. -/
abbrev StructuredArrowRightwards.mk (comm : R.map a ‚â´ w.app X‚ÇÅ ‚â´ B.map b = g) :
    w.StructuredArrowRightwards g :=
  StructuredArrow.mk (Y := CostructuredArrow.mk b) (CostructuredArrow.homMk a comm)

/-- Constructor for objects in `w.CostructuredArrowDownwards g`. -/
abbrev CostructuredArrowDownwards.mk (comm : R.map a ‚â´ w.app X‚ÇÅ ‚â´ B.map b = g) :
    w.CostructuredArrowDownwards g :=
  CostructuredArrow.mk (Y := StructuredArrow.mk a)
    (StructuredArrow.homMk b (by simpa using comm))

variable {w g}

lemma StructuredArrowRightwards.mk_surjective
    (f : w.StructuredArrowRightwards g) :
    ‚àÉ (X‚ÇÅ : C‚ÇÅ) (a : X‚ÇÇ ‚ü∂ T.obj X‚ÇÅ) (b : L.obj X‚ÇÅ ‚ü∂ X‚ÇÉ)
      (comm : R.map a ‚â´ w.app X‚ÇÅ ‚â´ B.map b = g), f = mk w g X‚ÇÅ a b comm := by
  obtain ‚ü®g, œÜ, rfl‚ü© := StructuredArrow.mk_surjective f
  obtain ‚ü®X‚ÇÅ, b, rfl‚ü© := g.mk_surjective
  obtain ‚ü®a, ha, rfl‚ü© := CostructuredArrow.homMk_surjective œÜ
  exact ‚ü®X‚ÇÅ, a, b, by simpa using ha, rfl‚ü©

lemma CostructuredArrowDownwards.mk_surjective
    (f : w.CostructuredArrowDownwards g) :
    ‚àÉ (X‚ÇÅ : C‚ÇÅ) (a : X‚ÇÇ ‚ü∂ T.obj X‚ÇÅ) (b : L.obj X‚ÇÅ ‚ü∂ X‚ÇÉ)
      (comm : R.map a ‚â´ w.app X‚ÇÅ ‚â´ B.map b = g), f = mk w g X‚ÇÅ a b comm := by
  obtain ‚ü®g, œÜ, rfl‚ü© := CostructuredArrow.mk_surjective f
  obtain ‚ü®X‚ÇÅ, a, rfl‚ü© := g.mk_surjective
  obtain ‚ü®b, hb, rfl‚ü© := StructuredArrow.homMk_surjective œÜ
  exact ‚ü®X‚ÇÅ, a, b, by simpa using hb, rfl‚ü©

end

namespace EquivalenceJ

/-- Given `w : TwoSquare T L R B` and a morphism `g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ`, this is
the obvious functor `w.StructuredArrowRightwards g ‚•§ w.CostructuredArrowDownwards g`. -/
@[simps]
def functor : w.StructuredArrowRightwards g ‚•§ w.CostructuredArrowDownwards g where
  obj f := CostructuredArrow.mk (Y := StructuredArrow.mk f.hom.left)
      (StructuredArrow.homMk f.right.hom (by simpa using CostructuredArrow.w f.hom))
  map {f‚ÇÅ f‚ÇÇ} œÜ :=
    CostructuredArrow.homMk (StructuredArrow.homMk œÜ.right.left
      (by dsimp; rw [‚Üê StructuredArrow.w œÜ]; rfl))
      (by ext; exact CostructuredArrow.w œÜ.right)
  map_id _ := rfl
  map_comp _ _ := rfl

/-- Given `w : TwoSquare T L R B` and a morphism `g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ`, this is
the obvious functor `w.CostructuredArrowDownwards g ‚•§ w.StructuredArrowRightwards g`. -/
@[simps]
def inverse : w.CostructuredArrowDownwards g ‚•§ w.StructuredArrowRightwards g where
  obj f := StructuredArrow.mk (Y := CostructuredArrow.mk f.hom.right)
      (CostructuredArrow.homMk f.left.hom (by simpa using StructuredArrow.w f.hom))
  map {f‚ÇÅ f‚ÇÇ} œÜ :=
    StructuredArrow.homMk (CostructuredArrow.homMk œÜ.left.right
      (by dsimp; rw [‚Üê CostructuredArrow.w œÜ]; rfl))
      (by ext; exact StructuredArrow.w œÜ.left)
  map_id _ := rfl
  map_comp _ _ := rfl

end EquivalenceJ

/-- Given `w : TwoSquare T L R B` and a morphism `g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ`, this is
the obvious equivalence of categories
`w.StructuredArrowRightwards g ‚âå w.CostructuredArrowDownwards g`. -/
@[simps functor inverse unitIso counitIso]
def equivalenceJ : w.StructuredArrowRightwards g ‚âå w.CostructuredArrowDownwards g where
  functor := EquivalenceJ.functor w g
  inverse := EquivalenceJ.inverse w g
  unitIso := Iso.refl _
  counitIso := Iso.refl _

lemma isConnected_rightwards_iff_downwards :
    IsConnected (w.StructuredArrowRightwards g) ‚Üî IsConnected (w.CostructuredArrowDownwards g) :=
  isConnected_iff_of_equivalence (w.equivalenceJ g)

end

section

/-- The functor `w.CostructuredArrowDownwards g ‚•§ w.CostructuredArrowDownwards g'` induced
by a morphism `Œ≥` such that `R.map Œ≥ ‚â´ g = g'`. -/
@[simps]
def costructuredArrowDownwardsPrecomp
    {X‚ÇÇ X‚ÇÇ' : C‚ÇÇ} {X‚ÇÉ : C‚ÇÉ} (g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ) (g' : R.obj X‚ÇÇ' ‚ü∂ B.obj X‚ÇÉ)
    (Œ≥ : X‚ÇÇ' ‚ü∂ X‚ÇÇ) (hŒ≥ : R.map Œ≥ ‚â´ g = g') :
    w.CostructuredArrowDownwards g ‚•§ w.CostructuredArrowDownwards g' where
  obj A := CostructuredArrowDownwards.mk _ _ A.left.right (Œ≥ ‚â´ A.left.hom) A.hom.right
    (by simpa [‚Üê hŒ≥] using R.map Œ≥ ‚â´= StructuredArrow.w A.hom)
  map {A A'} œÜ := CostructuredArrow.homMk (StructuredArrow.homMk œÜ.left.right (by
      dsimp
      rw [assoc, StructuredArrow.w])) (by
    ext
    dsimp
    rw [‚Üê CostructuredArrow.w œÜ, structuredArrowDownwards_map]
    rfl)
  map_id _ := rfl
  map_comp _ _ := rfl

end

/-- Condition on `w : TwoSquare T L R B` expressing that it is a Guitart exact square.
It is equivalent to saying that for any `X‚ÇÉ : C‚ÇÉ`, the induced functor
`CostructuredArrow L X‚ÇÉ ‚•§ CostructuredArrow R (B.obj X‚ÇÉ)` is final (see `guitartExact_iff_final`)
or equivalently that for any `X‚ÇÇ : C‚ÇÇ`, the induced functor
`StructuredArrow X‚ÇÇ T ‚•§ StructuredArrow (R.obj X‚ÇÇ) B` is initial (see `guitartExact_iff_initial`).
See also  `guitartExact_iff_isConnected_rightwards`, `guitartExact_iff_isConnected_downwards`
for characterizations in terms of the connectedness of auxiliary categories. -/
class GuitartExact : Prop where
  isConnected_rightwards {X‚ÇÇ : C‚ÇÇ} {X‚ÇÉ : C‚ÇÉ} (g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ) :
    IsConnected (w.StructuredArrowRightwards g)

lemma guitartExact_iff_isConnected_rightwards :
    w.GuitartExact ‚Üî ‚àÄ {X‚ÇÇ : C‚ÇÇ} {X‚ÇÉ : C‚ÇÉ} (g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ),
      IsConnected (w.StructuredArrowRightwards g) :=
  ‚ü®fun h ‚Ü¶ h.isConnected_rightwards, fun h ‚Ü¶ ‚ü®h‚ü©‚ü©

lemma guitartExact_iff_isConnected_downwards :
    w.GuitartExact ‚Üî ‚àÄ {X‚ÇÇ : C‚ÇÇ} {X‚ÇÉ : C‚ÇÉ} (g : R.obj X‚ÇÇ ‚ü∂ B.obj X‚ÇÉ),
      IsConnected (w.CostructuredArrowDownwards g) := by
  simp only [guitartExact_iff_isConnected_rightwards,
    isConnected_rightwards_iff_downwards]

instance [hw : w.GuitartExact] {X‚ÇÉ : C‚ÇÉ} (g : CostructuredArrow R (B.obj X‚ÇÉ)) :
    IsConnected (StructuredArrow g (w.costructuredArrowRightwards X‚ÇÉ)) := by
  rw [guitartExact_iff_isConnected_rightwards] at hw
  apply hw

instance [hw : w.GuitartExact] {X‚ÇÇ : C‚ÇÇ} (g : StructuredArrow (R.obj X‚ÇÇ) B) :
    IsConnected (CostructuredArrow (w.structuredArrowDownwards X‚ÇÇ) g) := by
  rw [guitartExact_iff_isConnected_downwards] at hw
  apply hw

lemma guitartExact_iff_final :
    w.GuitartExact ‚Üî ‚àÄ (X‚ÇÉ : C‚ÇÉ), (w.costructuredArrowRightwards X‚ÇÉ).Final :=
  ‚ü®fun _ _ ‚Ü¶ ‚ü®fun _ ‚Ü¶ inferInstance‚ü©, fun _ ‚Ü¶ ‚ü®fun _ ‚Ü¶ inferInstance‚ü©‚ü©

instance [hw : w.GuitartExact] (X‚ÇÉ : C‚ÇÉ) :
    (w.costructuredArrowRightwards X‚ÇÉ).Final := by
  rw [guitartExact_iff_final] at hw
  apply hw

lemma guitartExact_iff_initial :
    w.GuitartExact ‚Üî ‚àÄ (X‚ÇÇ : C‚ÇÇ), (w.structuredArrowDownwards X‚ÇÇ).Initial :=
  ‚ü®fun _ _ ‚Ü¶ ‚ü®fun _ ‚Ü¶ inferInstance‚ü©, by
    rw [guitartExact_iff_isConnected_downwards]
    intros
    infer_instance‚ü©

instance [hw : w.GuitartExact] (X‚ÇÇ : C‚ÇÇ) :
    (w.structuredArrowDownwards X‚ÇÇ).Initial := by
  rw [guitartExact_iff_initial] at hw
  apply hw

/-- When the left and right functors of a 2-square are equivalences, and the natural
transformation of the 2-square is an isomorphism, then the 2-square is Guitart exact. -/
instance (priority := 100) guitartExact_of_isEquivalence_of_isIso
    [L.IsEquivalence] [R.IsEquivalence] [IsIso w] : GuitartExact w := by
  rw [guitartExact_iff_initial]
  intro X‚ÇÇ
  have := StructuredArrow.isEquivalence_post X‚ÇÇ T R
  have : (Comma.mapRight _ w : StructuredArrow (R.obj X‚ÇÇ) _ ‚•§ _).IsEquivalence :=
    (Comma.mapRightIso _ (asIso w)).isEquivalence_functor
  have := StructuredArrow.isEquivalence_pre (R.obj X‚ÇÇ) L B
  dsimp only [structuredArrowDownwards]
  infer_instance

instance guitartExact_id (F : C‚ÇÅ ‚•§ C‚ÇÇ) :
    GuitartExact (TwoSquare.mk (ùü≠ C‚ÇÅ) F F (ùü≠ C‚ÇÇ) (ùüô F)) := by
  rw [guitartExact_iff_isConnected_rightwards]
  intro X‚ÇÇ X‚ÇÉ (g : F.obj X‚ÇÇ ‚ü∂ X‚ÇÉ)
  let Z := StructuredArrowRightwards (TwoSquare.mk (ùü≠ C‚ÇÅ) F F (ùü≠ C‚ÇÇ) (ùüô F)) g
  let X‚ÇÄ : Z := StructuredArrow.mk (Y := CostructuredArrow.mk g) (CostructuredArrow.homMk (ùüô _))
  have œÜ : ‚àÄ (X : Z), X‚ÇÄ ‚ü∂ X := fun X ‚Ü¶
    StructuredArrow.homMk (CostructuredArrow.homMk X.hom.left
      (by simpa using CostructuredArrow.w X.hom))
  have : Nonempty Z := ‚ü®X‚ÇÄ‚ü©
  apply zigzag_isConnected
  intro X Y
  exact Zigzag.of_inv_hom (œÜ X) (œÜ Y)

end TwoSquare

end CategoryTheory
