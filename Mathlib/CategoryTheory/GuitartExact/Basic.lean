/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Limits.Final
import Mathlib.CategoryTheory.Functor.TwoSquare

/-!
# Guitart exact squares

Given four functors `T`, `L`, `R` and `B`, a 2-square `TwoSquare T L R B` consists of
a natural transformation `w : T â‹™ R âŸ¶ L â‹™ B`:
```
     T
  Câ‚ â¥¤ Câ‚‚
L |     | R
  v     v
  Câ‚ƒ â¥¤ Câ‚„
     B
```

In this file, we define a typeclass `w.GuitartExact` which expresses
that this square is exact in the sense of Guitart. This means that
for any `Xâ‚ƒ : Câ‚ƒ`, the induced functor
`CostructuredArrow L Xâ‚ƒ â¥¤ CostructuredArrow R (B.obj Xâ‚ƒ)` is final.
It is also equivalent to the fact that for any `Xâ‚‚ : Câ‚‚`, the
induced functor `StructuredArrow Xâ‚‚ T â¥¤ StructuredArrow (R.obj Xâ‚‚) B`
is initial.

Various categorical notions (fully faithful functors, adjunctions, etc.) can
be characterized in terms of Guitart exact squares. Their particular role
in pointwise Kan extensions shall also be used in the construction of
derived functors.

## TODO

* Define the notion of derivability structure from
[the paper by Kahn and Maltsiniotis][KahnMaltsiniotis2008] using Guitart exact squares
and construct (pointwise) derived functors using this notion

## References
* https://ncatlab.org/nlab/show/exact+square
* [RenÃ© Guitart, *Relations et carrÃ©s exacts*][Guitart1980]
* [Bruno Kahn and Georges Maltsiniotis, *Structures de dÃ©rivabilitÃ©*][KahnMaltsiniotis2008]

-/

universe vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ vâ‚' vâ‚‚' vâ‚ƒ' vâ‚„' uâ‚ uâ‚‚ uâ‚ƒ uâ‚„ uâ‚' uâ‚‚' uâ‚ƒ' uâ‚„'

namespace CategoryTheory

open Limits

namespace IsConnected

variable {C D : Type*} [Category C] [Category D]

instance [IsConnected C] [IsConnected D] : IsConnected (C Ã— D) := by
  apply zigzag_isConnected
  intro âŸ¨Xâ‚, Yâ‚âŸ© âŸ¨Xâ‚‚, Yâ‚‚âŸ©
  exact (zigzag_obj_of_zigzag (Functor.prod' (ðŸ­ C) ((Functor.const C).obj Yâ‚))
      (isPreconnected_zigzag Xâ‚ Xâ‚‚)).trans
    (zigzag_obj_of_zigzag (Functor.prod' ((Functor.const D).obj Xâ‚‚) (ðŸ­ D))
      (isPreconnected_zigzag Yâ‚ Yâ‚‚))

end IsConnected

open Category

variable {Câ‚ : Type uâ‚} {Câ‚‚ : Type uâ‚‚} {Câ‚ƒ : Type uâ‚ƒ} {Câ‚„ : Type uâ‚„}
  [Category.{vâ‚} Câ‚] [Category.{vâ‚‚} Câ‚‚] [Category.{vâ‚ƒ} Câ‚ƒ] [Category.{vâ‚„} Câ‚„]
  (T : Câ‚ â¥¤ Câ‚‚) (L : Câ‚ â¥¤ Câ‚ƒ) (R : Câ‚‚ â¥¤ Câ‚„) (B : Câ‚ƒ â¥¤ Câ‚„)

namespace TwoSquare

variable {T L R B} (w : TwoSquare T L R B)

/-- Given `w : TwoSquare T L R B` and `Xâ‚ƒ : Câ‚ƒ`, this is the obvious functor
`CostructuredArrow L Xâ‚ƒ â¥¤ CostructuredArrow R (B.obj Xâ‚ƒ)`. -/
@[simps! obj map]
def costructuredArrowRightwards (Xâ‚ƒ : Câ‚ƒ) :
    CostructuredArrow L Xâ‚ƒ â¥¤ CostructuredArrow R (B.obj Xâ‚ƒ) :=
  CostructuredArrow.post L B Xâ‚ƒ â‹™ Comma.mapLeft _ w â‹™
    CostructuredArrow.pre T R (B.obj Xâ‚ƒ)

/-- Given `w : TwoSquare T L R B` and `Xâ‚‚ : Câ‚‚`, this is the obvious functor
`StructuredArrow Xâ‚‚ T â¥¤ StructuredArrow (R.obj Xâ‚‚) B`. -/
@[simps! obj map]
def structuredArrowDownwards (Xâ‚‚ : Câ‚‚) :
    StructuredArrow Xâ‚‚ T â¥¤ StructuredArrow (R.obj Xâ‚‚) B :=
  StructuredArrow.post Xâ‚‚ T R â‹™ Comma.mapRight _ w â‹™
    StructuredArrow.pre (R.obj Xâ‚‚) L B

section

variable {Xâ‚‚ : Câ‚‚} {Xâ‚ƒ : Câ‚ƒ} (g : R.obj Xâ‚‚ âŸ¶ B.obj Xâ‚ƒ)

/- In [the paper by Kahn and Maltsiniotis, Â§4.3][KahnMaltsiniotis2008], given
`w : TwoSquare T L R B` and `g : R.obj Xâ‚‚ âŸ¶ B.obj Xâ‚ƒ`, a category `J` is introduced
and it is observed that it is equivalent to the two categories
`w.StructuredArrowRightwards g` and `w.CostructuredArrowDownwards g`. We shall show below
that there is an equivalence
`w.equivalenceJ g : w.StructuredArrowRightwards g â‰Œ w.CostructuredArrowDownwards g`. -/

/-- Given `w : TwoSquare T L R B` and a morphism `g : R.obj Xâ‚‚ âŸ¶ B.obj Xâ‚ƒ`, this is the
category `StructuredArrow (CostructuredArrow.mk g) (w.costructuredArrowRightwards Xâ‚ƒ)`,
see the constructor `StructuredArrowRightwards.mk` for the data that is involved. -/
abbrev StructuredArrowRightwards :=
  StructuredArrow (CostructuredArrow.mk g) (w.costructuredArrowRightwards Xâ‚ƒ)

/-- Given `w : TwoSquare T L R B` and a morphism `g : R.obj Xâ‚‚ âŸ¶ B.obj Xâ‚ƒ`, this is the
category `CostructuredArrow (w.structuredArrowDownwards Xâ‚‚) (StructuredArrow.mk g)`,
see the constructor `CostructuredArrowDownwards.mk` for the data that is involved. -/
abbrev CostructuredArrowDownwards :=
  CostructuredArrow (w.structuredArrowDownwards Xâ‚‚) (StructuredArrow.mk g)

section

variable (Xâ‚ : Câ‚) (a : Xâ‚‚ âŸ¶ T.obj Xâ‚) (b : L.obj Xâ‚ âŸ¶ Xâ‚ƒ)

/-- Constructor for objects in `w.StructuredArrowRightwards g`. -/
abbrev StructuredArrowRightwards.mk (comm : R.map a â‰« w.app Xâ‚ â‰« B.map b = g) :
    w.StructuredArrowRightwards g :=
  StructuredArrow.mk (Y := CostructuredArrow.mk b) (CostructuredArrow.homMk a comm)

/-- Constructor for objects in `w.CostructuredArrowDownwards g`. -/
abbrev CostructuredArrowDownwards.mk (comm : R.map a â‰« w.app Xâ‚ â‰« B.map b = g) :
    w.CostructuredArrowDownwards g :=
  CostructuredArrow.mk (Y := StructuredArrow.mk a)
    (StructuredArrow.homMk b (by simpa using comm))

variable {w g}

lemma StructuredArrowRightwards.mk_surjective
    (f : w.StructuredArrowRightwards g) :
    âˆƒ (Xâ‚ : Câ‚) (a : Xâ‚‚ âŸ¶ T.obj Xâ‚) (b : L.obj Xâ‚ âŸ¶ Xâ‚ƒ)
      (comm : R.map a â‰« w.app Xâ‚ â‰« B.map b = g), f = mk w g Xâ‚ a b comm := by
  obtain âŸ¨g, Ï†, rflâŸ© := StructuredArrow.mk_surjective f
  obtain âŸ¨Xâ‚, b, rflâŸ© := g.mk_surjective
  obtain âŸ¨a, ha, rflâŸ© := CostructuredArrow.homMk_surjective Ï†
  exact âŸ¨Xâ‚, a, b, by simpa using ha, rflâŸ©

lemma CostructuredArrowDownwards.mk_surjective
    (f : w.CostructuredArrowDownwards g) :
    âˆƒ (Xâ‚ : Câ‚) (a : Xâ‚‚ âŸ¶ T.obj Xâ‚) (b : L.obj Xâ‚ âŸ¶ Xâ‚ƒ)
      (comm : R.map a â‰« w.app Xâ‚ â‰« B.map b = g), f = mk w g Xâ‚ a b comm := by
  obtain âŸ¨g, Ï†, rflâŸ© := CostructuredArrow.mk_surjective f
  obtain âŸ¨Xâ‚, a, rflâŸ© := g.mk_surjective
  obtain âŸ¨b, hb, rflâŸ© := StructuredArrow.homMk_surjective Ï†
  exact âŸ¨Xâ‚, a, b, by simpa using hb, rflâŸ©

end

namespace EquivalenceJ

/-- Given `w : TwoSquare T L R B` and a morphism `g : R.obj Xâ‚‚ âŸ¶ B.obj Xâ‚ƒ`, this is
the obvious functor `w.StructuredArrowRightwards g â¥¤ w.CostructuredArrowDownwards g`. -/
@[simps]
def functor : w.StructuredArrowRightwards g â¥¤ w.CostructuredArrowDownwards g where
  obj f := CostructuredArrow.mk (Y := StructuredArrow.mk f.hom.left)
      (StructuredArrow.homMk f.right.hom (by simpa using CostructuredArrow.w f.hom))
  map {fâ‚ fâ‚‚} Ï† :=
    CostructuredArrow.homMk (StructuredArrow.homMk Ï†.right.left
      (by dsimp; rw [â† StructuredArrow.w Ï†]; rfl))
      (by ext; exact CostructuredArrow.w Ï†.right)
  map_id _ := rfl
  map_comp _ _ := rfl

/-- Given `w : TwoSquare T L R B` and a morphism `g : R.obj Xâ‚‚ âŸ¶ B.obj Xâ‚ƒ`, this is
the obvious functor `w.CostructuredArrowDownwards g â¥¤ w.StructuredArrowRightwards g`. -/
@[simps]
def inverse : w.CostructuredArrowDownwards g â¥¤ w.StructuredArrowRightwards g where
  obj f := StructuredArrow.mk (Y := CostructuredArrow.mk f.hom.right)
      (CostructuredArrow.homMk f.left.hom (by simpa using StructuredArrow.w f.hom))
  map {fâ‚ fâ‚‚} Ï† :=
    StructuredArrow.homMk (CostructuredArrow.homMk Ï†.left.right
      (by dsimp; rw [â† CostructuredArrow.w Ï†]; rfl))
      (by ext; exact StructuredArrow.w Ï†.left)
  map_id _ := rfl
  map_comp _ _ := rfl

end EquivalenceJ

/-- Given `w : TwoSquare T L R B` and a morphism `g : R.obj Xâ‚‚ âŸ¶ B.obj Xâ‚ƒ`, this is
the obvious equivalence of categories
`w.StructuredArrowRightwards g â‰Œ w.CostructuredArrowDownwards g`. -/
@[simps functor inverse unitIso counitIso]
def equivalenceJ : w.StructuredArrowRightwards g â‰Œ w.CostructuredArrowDownwards g where
  functor := EquivalenceJ.functor w g
  inverse := EquivalenceJ.inverse w g
  unitIso := Iso.refl _
  counitIso := Iso.refl _

lemma isConnected_rightwards_iff_downwards :
    IsConnected (w.StructuredArrowRightwards g) â†” IsConnected (w.CostructuredArrowDownwards g) :=
  isConnected_iff_of_equivalence (w.equivalenceJ g)

end

section

/-- The functor `w.CostructuredArrowDownwards g â¥¤ w.CostructuredArrowDownwards g'` induced
by a morphism `Î³` such that `R.map Î³ â‰« g = g'`. -/
@[simps]
def costructuredArrowDownwardsPrecomp
    {Xâ‚‚ Xâ‚‚' : Câ‚‚} {Xâ‚ƒ : Câ‚ƒ} (g : R.obj Xâ‚‚ âŸ¶ B.obj Xâ‚ƒ) (g' : R.obj Xâ‚‚' âŸ¶ B.obj Xâ‚ƒ)
    (Î³ : Xâ‚‚' âŸ¶ Xâ‚‚) (hÎ³ : R.map Î³ â‰« g = g') :
    w.CostructuredArrowDownwards g â¥¤ w.CostructuredArrowDownwards g' where
  obj A := CostructuredArrowDownwards.mk _ _ A.left.right (Î³ â‰« A.left.hom) A.hom.right
    (by simpa [â† hÎ³] using R.map Î³ â‰«= StructuredArrow.w A.hom)
  map {A A'} Ï† := CostructuredArrow.homMk (StructuredArrow.homMk Ï†.left.right (by
      dsimp
      rw [assoc, StructuredArrow.w])) (by
    ext
    dsimp
    rw [â† CostructuredArrow.w Ï†, structuredArrowDownwards_map]
    rfl)
  map_id _ := rfl
  map_comp _ _ := rfl

end

/-- Condition on `w : TwoSquare T L R B` expressing that it is a Guitart exact square.
It is equivalent to saying that for any `Xâ‚ƒ : Câ‚ƒ`, the induced functor
`CostructuredArrow L Xâ‚ƒ â¥¤ CostructuredArrow R (B.obj Xâ‚ƒ)` is final (see `guitartExact_iff_final`)
or equivalently that for any `Xâ‚‚ : Câ‚‚`, the induced functor
`StructuredArrow Xâ‚‚ T â¥¤ StructuredArrow (R.obj Xâ‚‚) B` is initial (see `guitartExact_iff_initial`).
See also  `guitartExact_iff_isConnected_rightwards`, `guitartExact_iff_isConnected_downwards`
for characterizations in terms of the connectedness of auxiliary categories. -/
class GuitartExact : Prop where
  isConnected_rightwards {Xâ‚‚ : Câ‚‚} {Xâ‚ƒ : Câ‚ƒ} (g : R.obj Xâ‚‚ âŸ¶ B.obj Xâ‚ƒ) :
    IsConnected (w.StructuredArrowRightwards g)

lemma guitartExact_iff_isConnected_rightwards :
    w.GuitartExact â†” âˆ€ {Xâ‚‚ : Câ‚‚} {Xâ‚ƒ : Câ‚ƒ} (g : R.obj Xâ‚‚ âŸ¶ B.obj Xâ‚ƒ),
      IsConnected (w.StructuredArrowRightwards g) :=
  âŸ¨fun h => h.isConnected_rightwards, fun h => âŸ¨hâŸ©âŸ©

lemma guitartExact_iff_isConnected_downwards :
    w.GuitartExact â†” âˆ€ {Xâ‚‚ : Câ‚‚} {Xâ‚ƒ : Câ‚ƒ} (g : R.obj Xâ‚‚ âŸ¶ B.obj Xâ‚ƒ),
      IsConnected (w.CostructuredArrowDownwards g) := by
  simp only [guitartExact_iff_isConnected_rightwards,
    isConnected_rightwards_iff_downwards]

instance [hw : w.GuitartExact] {Xâ‚ƒ : Câ‚ƒ} (g : CostructuredArrow R (B.obj Xâ‚ƒ)) :
    IsConnected (StructuredArrow g (w.costructuredArrowRightwards Xâ‚ƒ)) := by
  rw [guitartExact_iff_isConnected_rightwards] at hw
  apply hw

instance [hw : w.GuitartExact] {Xâ‚‚ : Câ‚‚} (g : StructuredArrow (R.obj Xâ‚‚) B) :
    IsConnected (CostructuredArrow (w.structuredArrowDownwards Xâ‚‚) g) := by
  rw [guitartExact_iff_isConnected_downwards] at hw
  apply hw

lemma costructuredArrowRightWards_final_iff_of_iso {Xâ‚ƒ Xâ‚ƒ' : Câ‚ƒ} (e : Xâ‚ƒ â‰… Xâ‚ƒ') :
    (w.costructuredArrowRightwards Xâ‚ƒ).Final â†”
      (w.costructuredArrowRightwards Xâ‚ƒ').Final := by
  rw [Functor.final_iff_comp_equivalence _ (CostructuredArrow.mapIso (B.mapIso e)).functor,
    Functor.final_iff_equivalence_comp (CostructuredArrow.mapIso e).functor]
  exact Functor.final_natIso_iff
    (NatIso.ofComponents (fun A â†¦ CostructuredArrow.isoMk (Iso.refl _)))

lemma guitartExact_iff_final :
    w.GuitartExact â†” âˆ€ (Xâ‚ƒ : Câ‚ƒ), (w.costructuredArrowRightwards Xâ‚ƒ).Final :=
  âŸ¨fun _ _ => âŸ¨fun _ => inferInstanceâŸ©, fun _ => âŸ¨fun _ => inferInstanceâŸ©âŸ©

instance [hw : w.GuitartExact] (Xâ‚ƒ : Câ‚ƒ) :
    (w.costructuredArrowRightwards Xâ‚ƒ).Final := by
  rw [guitartExact_iff_final] at hw
  apply hw

lemma guitartExact_iff_initial :
    w.GuitartExact â†” âˆ€ (Xâ‚‚ : Câ‚‚), (w.structuredArrowDownwards Xâ‚‚).Initial :=
  âŸ¨fun _ _ => âŸ¨fun _ => inferInstanceâŸ©, by
    rw [guitartExact_iff_isConnected_downwards]
    intros
    infer_instanceâŸ©

instance [hw : w.GuitartExact] (Xâ‚‚ : Câ‚‚) :
    (w.structuredArrowDownwards Xâ‚‚).Initial := by
  rw [guitartExact_iff_initial] at hw
  apply hw

instance [L.IsEquivalence] [R.IsEquivalence] [IsIso w] : GuitartExact w := by
  rw [guitartExact_iff_initial]
  intro Xâ‚‚
  have := StructuredArrow.isEquivalence_post Xâ‚‚ T R
  have : (Comma.mapRight _ w : StructuredArrow (R.obj Xâ‚‚) _ â¥¤
    StructuredArrow (R.obj Xâ‚‚) _).IsEquivalence :=
    (Comma.mapRightIso _ (asIso w)).isEquivalence_functor
  have := StructuredArrow.isEquivalence_pre (R.obj Xâ‚‚) L B
  dsimp only [structuredArrowDownwards]
  infer_instance

section prod

variable {Câ‚' : Type uâ‚'} {Câ‚‚' : Type uâ‚‚'} {Câ‚ƒ' : Type uâ‚ƒ'} {Câ‚„' : Type uâ‚„'}
  [Category.{vâ‚'} Câ‚'] [Category.{vâ‚‚'} Câ‚‚'] [Category.{vâ‚ƒ'} Câ‚ƒ'] [Category.{vâ‚„'} Câ‚„']
  {T' : Câ‚' â¥¤ Câ‚‚'} {L' : Câ‚' â¥¤ Câ‚ƒ'} {R' : Câ‚‚' â¥¤ Câ‚„'} {B' : Câ‚ƒ' â¥¤ Câ‚„'}
  (w' : TwoSquare T' L' R' B')

def prod : TwoSquare (T.prod T') (L.prod L') (R.prod R') (B.prod B') := NatTrans.prod w w'

section

variable {Yâ‚‚ : Câ‚‚ Ã— Câ‚‚'} {Yâ‚ƒ : Câ‚ƒ Ã— Câ‚ƒ'} (g : (R.prod R').obj Yâ‚‚ âŸ¶ (B.prod B').obj Yâ‚ƒ)

namespace JRightwardsProdEquivalence

@[simp]
def functorObj (X : StructuredArrowRightwards (w.prod w') g) :
    (StructuredArrowRightwards w g.1) Ã— (StructuredArrowRightwards w' g.2) :=
  âŸ¨StructuredArrowRightwards.mk w g.1 _ X.hom.left.1 X.right.hom.1
      (by simpa using congr_arg _root_.Prod.fst X.hom.w),
    StructuredArrowRightwards.mk w' g.2 _ X.hom.left.2 X.right.hom.2
      (by simpa using congr_arg _root_.Prod.snd X.hom.w)âŸ©

@[simps]
def functor : StructuredArrowRightwards (w.prod w') g â¥¤
    (StructuredArrowRightwards w g.1) Ã— (StructuredArrowRightwards w' g.2) where
  obj X := functorObj w w' g X
  map {X Y} f :=
    âŸ¨StructuredArrow.homMk (CostructuredArrow.homMk f.right.left.1) (by
          ext
          have eq := StructuredArrow.w f
          dsimp at eq âŠ¢
          rw [â† eq]
          rfl),
      StructuredArrow.homMk (CostructuredArrow.homMk f.right.left.2) (by
          ext
          have eq := StructuredArrow.w f
          dsimp at eq âŠ¢
          rw [â† eq]
          rfl)âŸ©
  map_id _ := rfl
  map_comp f g := rfl

@[simp]
def inverseObj (X : (StructuredArrowRightwards w g.1) Ã— (StructuredArrowRightwards w' g.2)) :
  StructuredArrowRightwards (w.prod w') g :=
  StructuredArrowRightwards.mk _ _ âŸ¨X.1.right.left, X.2.right.leftâŸ©
    âŸ¨X.1.hom.left, X.2.hom.leftâŸ© âŸ¨X.1.right.hom, X.2.right.homâŸ© (by
      dsimp
      ext
      Â· simpa using X.1.hom.w
      Â· simpa using X.2.hom.w)

@[simps]
def inverse : (StructuredArrowRightwards w g.1) Ã— (StructuredArrowRightwards w' g.2) â¥¤
    StructuredArrowRightwards (w.prod w') g where
  obj X := inverseObj w w' g X
  map {X Y} f := StructuredArrow.homMk
    (CostructuredArrow.homMk âŸ¨f.1.right.left, f.2.right.leftâŸ© (by
      dsimp
      ext
      Â· exact CostructuredArrow.w f.1.right
      Â· exact CostructuredArrow.w f.2.right)) (by
      dsimp
      ext
      Â· have eq := StructuredArrow.w f.1
        dsimp at eq âŠ¢
        rw [â† eq]
        rfl
      Â· have eq := StructuredArrow.w f.2
        dsimp at eq âŠ¢
        rw [â† eq]
        rfl)
  map_id _ := rfl
  map_comp _ _ := rfl

end JRightwardsProdEquivalence

@[simps]
def StructuredArrowRightwardsProdEquivalence :
    StructuredArrowRightwards (w.prod w') g â‰Œ
      (StructuredArrowRightwards w g.1) Ã— (StructuredArrowRightwards w' g.2) where
  functor := JRightwardsProdEquivalence.functor w w' g
  inverse := JRightwardsProdEquivalence.inverse w w' g
  unitIso := Iso.refl _
  counitIso := Iso.refl _
  functor_unitIso_comp X := by
    dsimp
    erw [comp_id, comp_id]
    rfl

end

instance GuitartExact.prod [w.GuitartExact] [w'.GuitartExact] :
    (w.prod w').GuitartExact := by
  rw [guitartExact_iff_isConnected_rightwards]
  rintro Yâ‚‚ Yâ‚ƒ g
  exact isConnected_of_equivalent (StructuredArrowRightwardsProdEquivalence w w' g).symm

end prod

/-- When the left and right functors of a 2-square are equivalences, and the natural
transformation of the 2-square is an isomorphism, then the 2-square is Guitart exact. -/
instance (priority := 100) guitartExact_of_isEquivalence_of_isIso
    [L.IsEquivalence] [R.IsEquivalence] [IsIso w] : GuitartExact w := by
  rw [guitartExact_iff_initial]
  intro Xâ‚‚
  have := StructuredArrow.isEquivalence_post Xâ‚‚ T R
  have : (Comma.mapRight _ w : StructuredArrow (R.obj Xâ‚‚) _ â¥¤ _).IsEquivalence :=
    (Comma.mapRightIso _ (asIso w)).isEquivalence_functor
  have := StructuredArrow.isEquivalence_pre (R.obj Xâ‚‚) L B
  dsimp only [structuredArrowDownwards]
  infer_instance

instance guitartExact_id (F : Câ‚ â¥¤ Câ‚‚) :
    GuitartExact (TwoSquare.mk (ðŸ­ Câ‚) F F (ðŸ­ Câ‚‚) (ðŸ™ F)) := by
  rw [guitartExact_iff_isConnected_rightwards]
  intro Xâ‚‚ Xâ‚ƒ (g : F.obj Xâ‚‚ âŸ¶ Xâ‚ƒ)
  let Z := StructuredArrowRightwards (TwoSquare.mk (ðŸ­ Câ‚) F F (ðŸ­ Câ‚‚) (ðŸ™ F)) g
  let Xâ‚€ : Z := StructuredArrow.mk (Y := CostructuredArrow.mk g) (CostructuredArrow.homMk (ðŸ™ _))
  have Ï† : âˆ€ (X : Z), Xâ‚€ âŸ¶ X := fun X =>
    StructuredArrow.homMk (CostructuredArrow.homMk X.hom.left
      (by simpa using CostructuredArrow.w X.hom))
  have : Nonempty Z := âŸ¨Xâ‚€âŸ©
  apply zigzag_isConnected
  intro X Y
  exact Zigzag.of_inv_hom (Ï† X) (Ï† Y)

end TwoSquare

end CategoryTheory
