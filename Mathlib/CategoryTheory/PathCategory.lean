/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.EqToHom
import Mathlib.CategoryTheory.Quotient
import Mathlib.Combinatorics.Quiver.Path

#align_import category_theory.path_category from "leanprover-community/mathlib"@"c6dd521ebdce53bb372c527569dd7c25de53a08b"

/-!
# The category paths on a quiver.
When `C` is a quiver, `paths C` is the category of paths.

## When the quiver is itself a category
We provide `path_composition : paths C â¥¤ C`.

We check that the quotient of the path category of a category by the canonical relation
(paths are related if they compose to the same path) is equivalent to the original category.
-/


universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

namespace CategoryTheory

section

/-- A type synonym for the category of paths in a quiver.
-/
def Paths (V : Type uâ‚) : Type uâ‚ := V
#align category_theory.paths CategoryTheory.Paths

instance (V : Type uâ‚) [Inhabited V] : Inhabited (Paths V) := âŸ¨(default : V)âŸ©

variable (V : Type uâ‚) [Quiver.{vâ‚ + 1} V]

namespace Paths

instance categoryPaths : Category.{max uâ‚ vâ‚} (Paths V) where
  Hom := fun X Y : V => Quiver.Path X Y
  id X := Quiver.Path.nil
  comp f g := Quiver.Path.comp f g
#align category_theory.paths.category_paths CategoryTheory.Paths.categoryPaths

variable {V}

/-- The inclusion of a quiver `V` into its path category, as a prefunctor.
-/
@[simps]
def of : V â¥¤q Paths V where
  obj X := X
  map f := f.toPath
#align category_theory.paths.of CategoryTheory.Paths.of

attribute [local ext] Functor.ext

/-- Any prefunctor from `V` lifts to a functor from `paths V` -/
def lift {C} [Category C] (Ï† : V â¥¤q C) : Paths V â¥¤ C where
  obj := Ï†.obj
  map {X} {Y} f :=
    @Quiver.Path.rec V _ X (fun Y _ => Ï†.obj X âŸ¶ Ï†.obj Y) (ğŸ™ <| Ï†.obj X)
      (fun _ f ihp => ihp â‰« Ï†.map f) Y f
  map_id X := by rfl
                 -- ğŸ‰ no goals
  map_comp f g := by
    induction' g with _ _ g' p ih _ _ _
    -- âŠ¢ { obj := Ï†.obj, map := fun {X Y} f => Quiver.Path.rec (ğŸ™ (Ï†.obj X)) (fun {b  â€¦
    Â· rw [Category.comp_id]
      -- âŠ¢ { obj := Ï†.obj, map := fun {X Y} f => Quiver.Path.rec (ğŸ™ (Ï†.obj X)) (fun {b  â€¦
      rfl
      -- ğŸ‰ no goals
    Â· have : f â‰« Quiver.Path.cons g' p = (f â‰« g').cons p := by apply Quiver.Path.comp_cons
      -- âŠ¢ { obj := Ï†.obj, map := fun {X Y} f => Quiver.Path.rec (ğŸ™ (Ï†.obj X)) (fun {b  â€¦
      rw [this]
      -- âŠ¢ { obj := Ï†.obj, map := fun {X Y} f => Quiver.Path.rec (ğŸ™ (Ï†.obj X)) (fun {b  â€¦
      simp only at ih âŠ¢
      -- âŠ¢ Quiver.Path.rec (ğŸ™ (Ï†.obj Xâœ)) (fun {b c} x f ihp => ihp â‰« Ï†.map f) (f â‰« g') â€¦
      rw [ih, Category.assoc]
      -- ğŸ‰ no goals
#align category_theory.paths.lift CategoryTheory.Paths.lift

@[simp]
theorem lift_nil {C} [Category C] (Ï† : V â¥¤q C) (X : V) :
    (lift Ï†).map Quiver.Path.nil = ğŸ™ (Ï†.obj X) := rfl
#align category_theory.paths.lift_nil CategoryTheory.Paths.lift_nil

@[simp]
theorem lift_cons {C} [Category C] (Ï† : V â¥¤q C) {X Y Z : V} (p : Quiver.Path X Y) (f : Y âŸ¶ Z) :
    (lift Ï†).map (p.cons f) = (lift Ï†).map p â‰« Ï†.map f := rfl
#align category_theory.paths.lift_cons CategoryTheory.Paths.lift_cons

@[simp]
theorem lift_toPath {C} [Category C] (Ï† : V â¥¤q C) {X Y : V} (f : X âŸ¶ Y) :
    (lift Ï†).map f.toPath = Ï†.map f := by
  dsimp [Quiver.Hom.toPath, lift]
  -- âŠ¢ ğŸ™ (Ï†.obj X) â‰« Ï†.map f = Ï†.map f
  simp
  -- ğŸ‰ no goals
#align category_theory.paths.lift_to_path CategoryTheory.Paths.lift_toPath

theorem lift_spec {C} [Category C] (Ï† : V â¥¤q C) : of â‹™q (lift Ï†).toPrefunctor = Ï† := by
  fapply Prefunctor.ext
  -- âŠ¢ âˆ€ (X : V), (of â‹™q (lift Ï†).toPrefunctor).obj X = Ï†.obj X
  Â· rintro X
    -- âŠ¢ (of â‹™q (lift Ï†).toPrefunctor).obj X = Ï†.obj X
    rfl
    -- ğŸ‰ no goals
  Â· rintro X Y f
    -- âŠ¢ (of â‹™q (lift Ï†).toPrefunctor).map f = Eq.recOn (_ : Ï†.obj Y = (of â‹™q (lift Ï† â€¦
    rcases Ï† with âŸ¨Ï†o, Ï†mâŸ©
    -- âŠ¢ (of â‹™q (lift { obj := Ï†o, map := Ï†m }).toPrefunctor).map f = Eq.recOn (_ : { â€¦
    dsimp [lift, Quiver.Hom.toPath]
    -- âŠ¢ ğŸ™ (Ï†o X) â‰« Ï†m f = Ï†m f
    simp only [Category.id_comp]
    -- ğŸ‰ no goals
#align category_theory.paths.lift_spec CategoryTheory.Paths.lift_spec

theorem lift_unique {C} [Category C] (Ï† : V â¥¤q C) (Î¦ : Paths V â¥¤ C)
    (hÎ¦ : of â‹™q Î¦.toPrefunctor = Ï†) : Î¦ = lift Ï† := by
  subst_vars
  -- âŠ¢ Î¦ = lift (of â‹™q Î¦.toPrefunctor)
  fapply Functor.ext
  -- âŠ¢ âˆ€ (X : Paths V), Î¦.obj X = (lift (of â‹™q Î¦.toPrefunctor)).obj X
  Â· rintro X
    -- âŠ¢ Î¦.obj X = (lift (of â‹™q Î¦.toPrefunctor)).obj X
    rfl
    -- ğŸ‰ no goals
  Â· rintro X Y f
    -- âŠ¢ Î¦.map f = eqToHom (_ : Î¦.obj X = Î¦.obj X) â‰« (lift (of â‹™q Î¦.toPrefunctor)).ma â€¦
    dsimp [lift]
    -- âŠ¢ Î¦.map f = ğŸ™ (Î¦.obj X) â‰« Quiver.Path.rec (ğŸ™ (Î¦.obj X)) (fun {b c} x f ihp =>  â€¦
    induction' f with _ _ p f' ih
    -- âŠ¢ Î¦.map Quiver.Path.nil = ğŸ™ (Î¦.obj X) â‰« Quiver.Path.rec (ğŸ™ (Î¦.obj X)) (fun {b  â€¦
    Â· simp only [Category.comp_id]
      -- âŠ¢ Î¦.map Quiver.Path.nil = ğŸ™ (Î¦.obj X)
      apply Functor.map_id
      -- ğŸ‰ no goals
    Â· simp only [Category.comp_id, Category.id_comp] at ih âŠ¢
      -- âŠ¢ Î¦.map (Quiver.Path.cons p f') = Quiver.Path.rec (ğŸ™ (Î¦.obj X)) (fun {b c} x f â€¦
      -- porting note: Had to do substitute `p.cons f'` and `f'.toPath` by their fully qualified
      -- versions in this `have` clause (elsewhere too).
      have : Î¦.map (Quiver.Path.cons p f') = Î¦.map p â‰« Î¦.map (Quiver.Hom.toPath f') := by
        convert Functor.map_comp Î¦ p (Quiver.Hom.toPath f')
      rw [this, ih]
      -- ğŸ‰ no goals
#align category_theory.paths.lift_unique CategoryTheory.Paths.lift_unique

/-- Two functors out of a path category are equal when they agree on singleton paths. -/
@[ext]
theorem ext_functor {C} [Category C] {F G : Paths V â¥¤ C} (h_obj : F.obj = G.obj)
    (h : âˆ€ (a b : V) (e : a âŸ¶ b), F.map e.toPath =
        eqToHom (congr_fun h_obj a) â‰« G.map e.toPath â‰« eqToHom (congr_fun h_obj.symm b)) :
    F = G := by
  fapply Functor.ext
  -- âŠ¢ âˆ€ (X : Paths V), F.obj X = G.obj X
  Â· intro X
    -- âŠ¢ F.obj X = G.obj X
    rw [h_obj]
    -- ğŸ‰ no goals
  Â· intro X Y f
    -- âŠ¢ F.map f = eqToHom (_ : F.obj X = G.obj X) â‰« G.map f â‰« eqToHom (_ : G.obj Y = â€¦
    induction' f with Y' Z' g e ih
    -- âŠ¢ F.map Quiver.Path.nil = eqToHom (_ : F.obj X = G.obj X) â‰« G.map Quiver.Path. â€¦
    Â· erw [F.map_id, G.map_id, Category.id_comp, eqToHom_trans, eqToHom_refl]
      -- ğŸ‰ no goals
    Â· erw [F.map_comp g (Quiver.Hom.toPath e), G.map_comp g (Quiver.Hom.toPath e), ih, h]
      -- âŠ¢ (eqToHom (_ : F.obj X = G.obj X) â‰« G.map g â‰« eqToHom (_ : G.obj Y' = F.obj Y â€¦
      simp only [Category.id_comp, eqToHom_refl, eqToHom_trans_assoc, Category.assoc]
      -- ğŸ‰ no goals
#align category_theory.paths.ext_functor CategoryTheory.Paths.ext_functor

end Paths

variable (W : Type uâ‚‚) [Quiver.{vâ‚‚ + 1} W]

-- A restatement of `Prefunctor.mapPath_comp` using `f â‰« g` instead of `f.comp g`.
@[simp]
theorem Prefunctor.mapPath_comp' (F : V â¥¤q W) {X Y Z : Paths V} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    F.mapPath (f â‰« g) = (F.mapPath f).comp (F.mapPath g) :=
  Prefunctor.mapPath_comp _ _ _
#align category_theory.prefunctor.map_path_comp' CategoryTheory.Prefunctor.mapPath_comp'

end

section

variable {C : Type uâ‚} [Category.{vâ‚} C]

open Quiver

-- porting note:
-- This def was originally marked `@[simp]`, but the meaning is different in lean4: lean4#2042
-- So, the `@[simp]` was removed, and the two equational lemmas below added instead.
/-- A path in a category can be composed to a single morphism. -/
def composePath {X : C} : âˆ€ {Y : C} (_ : Path X Y), X âŸ¶ Y
  | _, .nil => ğŸ™ X
  | _, .cons p e => composePath p â‰« e
#align category_theory.compose_path CategoryTheory.composePath

@[simp] lemma composePath_nil {X : C} : composePath (Path.nil : Path X X) = ğŸ™ X := rfl

@[simp] lemma composePath_cons {X Y Z : C} (p : Path X Y) (e : Y âŸ¶ Z) :
  composePath (p.cons e) = composePath p â‰« e := rfl

@[simp]
theorem composePath_toPath {X Y : C} (f : X âŸ¶ Y) : composePath f.toPath = f := Category.id_comp _
#align category_theory.compose_path_to_path CategoryTheory.composePath_toPath

@[simp]
theorem composePath_comp {X Y Z : C} (f : Path X Y) (g : Path Y Z) :
    composePath (f.comp g) = composePath f â‰« composePath g := by
  induction' g with Y' Z' g e ih
  -- âŠ¢ composePath (Path.comp f Path.nil) = composePath f â‰« composePath Path.nil
  Â· simp
    -- ğŸ‰ no goals
  Â· simp [ih]
    -- ğŸ‰ no goals
#align category_theory.compose_path_comp CategoryTheory.composePath_comp

@[simp]
-- porting note: TODO get rid of `(id X : C)` somehow?
theorem composePath_id {X : Paths C} : composePath (ğŸ™ X) = ğŸ™ (id X : C) := rfl
#align category_theory.compose_path_id CategoryTheory.composePath_id

@[simp]
theorem composePath_comp' {X Y Z : Paths C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    composePath (f â‰« g) = composePath f â‰« composePath g :=
  composePath_comp f g
#align category_theory.compose_path_comp' CategoryTheory.composePath_comp'

variable (C)

/-- Composition of paths as functor from the path category of a category to the category. -/
@[simps]
def pathComposition : Paths C â¥¤ C where
  obj X := X
  map f := composePath f
#align category_theory.path_composition CategoryTheory.pathComposition

-- TODO: This, and what follows, should be generalized to
-- the `HomRel` for the kernel of any functor.
-- Indeed, this should be part of an equivalence between congruence relations on a category `C`
-- and full, essentially surjective functors out of `C`.
/-- The canonical relation on the path category of a category:
two paths are related if they compose to the same morphism. -/
@[simp]
def pathsHomRel : HomRel (Paths C) := fun _ _ p q =>
  (pathComposition C).map p = (pathComposition C).map q
#align category_theory.paths_hom_rel CategoryTheory.pathsHomRel

/-- The functor from a category to the canonical quotient of its path category. -/
@[simps]
def toQuotientPaths : C â¥¤ Quotient (pathsHomRel C) where
  obj X := Quotient.mk X
  map f := Quot.mk _ f.toPath
  map_id X := Quot.sound (Quotient.CompClosure.of _ _ _ (by simp))
                                                            -- ğŸ‰ no goals
  map_comp f g := Quot.sound (Quotient.CompClosure.of _ _ _ (by simp))
                                                                -- ğŸ‰ no goals
#align category_theory.to_quotient_paths CategoryTheory.toQuotientPaths

/-- The functor from the canonical quotient of a path category of a category
to the original category. -/
@[simps!]
def quotientPathsTo : Quotient (pathsHomRel C) â¥¤ C :=
  Quotient.lift _ (pathComposition C) fun _ _ _ _ w => w
#align category_theory.quotient_paths_to CategoryTheory.quotientPathsTo

/-- The canonical quotient of the path category of a category
is equivalent to the original category. -/
def quotientPathsEquiv : Quotient (pathsHomRel C) â‰Œ C where
  functor := quotientPathsTo C
  inverse := toQuotientPaths C
  unitIso :=
    NatIso.ofComponents
      (fun X => by cases X; rfl)
                   -- âŠ¢ (ğŸ­ (Quotient (pathsHomRel C))).obj { as := asâœ } â‰… (quotientPathsTo C â‹™ toQu â€¦
                            -- ğŸ‰ no goals
      (Quot.ind $ fun f => by
        apply Quot.sound
        -- âŠ¢ Quotient.CompClosure (pathsHomRel C) (f â‰« ğŸ™ ((ğŸ­ (Quotient (pathsHomRel C))). â€¦
        apply Quotient.CompClosure.of
        -- âŠ¢ pathsHomRel C (f â‰« ğŸ™ ((ğŸ­ (Quotient (pathsHomRel C))).obj { as := Yâœ.as }).as â€¦
        simp [Category.comp_id, Category.id_comp, pathsHomRel])
        -- ğŸ‰ no goals
  counitIso := NatIso.ofComponents (fun X => Iso.refl _) (fun f => by simp [Quot.liftOn_mk])
                                                                      -- ğŸ‰ no goals
  functor_unitIso_comp X := by
    cases X
    -- âŠ¢ (quotientPathsTo C).map (NatTrans.app (NatIso.ofComponents fun X => Quotient â€¦
    simp only [pathsHomRel, pathComposition_obj, pathComposition_map, Functor.id_obj,
               quotientPathsTo_obj, Functor.comp_obj, toQuotientPaths_obj_as,
               NatIso.ofComponents_hom_app, Iso.refl_hom, quotientPathsTo_map, Category.comp_id]
    rfl
    -- ğŸ‰ no goals
#align category_theory.quotient_paths_equiv CategoryTheory.quotientPathsEquiv

end

end CategoryTheory
