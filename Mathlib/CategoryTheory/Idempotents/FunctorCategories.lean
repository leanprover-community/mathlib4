/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Idempotents.Karoubi

/-!
# Idempotent completeness and functor categories

In this file we define an instance `functor_category_isIdempotentComplete` expressing
that a functor category `J â¥¤ C` is idempotent complete when the target category `C` is.

We also provide a fully faithful functor
`karoubiFunctorCategoryEmbedding : Karoubi (J â¥¤ C)) : J â¥¤ Karoubi C` for all categories
`J` and `C`.

-/


open CategoryTheory

open CategoryTheory.Category

open CategoryTheory.Idempotents.Karoubi

open CategoryTheory.Limits

namespace CategoryTheory

namespace Idempotents

variable {J C : Type*} [Category J] [Category C] (P Q : Karoubi (J â¥¤ C)) (f : P âŸ¶ Q) (X : J)

@[reassoc (attr := simp)]
theorem app_idem : P.p.app X â‰« P.p.app X = P.p.app X :=
  congr_app P.idem X

variable {P Q}

@[reassoc (attr := simp)]
theorem app_p_comp : P.p.app X â‰« f.f.app X = f.f.app X :=
  congr_app (p_comp f) X

@[reassoc (attr := simp)]
theorem app_comp_p : f.f.app X â‰« Q.p.app X = f.f.app X :=
  congr_app (comp_p f) X

@[reassoc]
theorem app_p_comm : P.p.app X â‰« f.f.app X = f.f.app X â‰« Q.p.app X :=
  congr_app (p_comm f) X

variable (J C)

instance functor_category_isIdempotentComplete [IsIdempotentComplete C] :
    IsIdempotentComplete (J â¥¤ C) := by
  refine âŸ¨fun F p hp => ?_âŸ©
  have hC := (isIdempotentComplete_iff_hasEqualizer_of_id_and_idempotent C).mp inferInstance
  haveI : âˆ€ j : J, HasEqualizer (ğŸ™ _) (p.app j) := fun j => hC _ _ (congr_app hp j)
  /- We construct the direct factor `Y` associated to `p : F âŸ¶ F` by computing
      the equalizer of the identity and `p.app j` on each object `(j : J)`. -/
  let Y : J â¥¤ C :=
    { obj := fun j => Limits.equalizer (ğŸ™ _) (p.app j)
      map := fun {j j'} Ï† =>
        equalizer.lift (Limits.equalizer.Î¹ (ğŸ™ _) (p.app j) â‰« F.map Ï†)
          (by rw [comp_id, assoc, p.naturality Ï†, â† assoc, â† Limits.equalizer.condition, comp_id]) }
  let i : Y âŸ¶ F :=
    { app := fun j => equalizer.Î¹ _ _
      naturality := fun _ _ _ => by rw [equalizer.lift_Î¹] }
  let e : F âŸ¶ Y :=
    { app := fun j =>
        equalizer.lift (p.app j) (by simpa only [comp_id] using (congr_app hp j).symm)
      naturality := fun j j' Ï† => equalizer.hom_ext (by simp [Y]) }
  use Y, i, e
  constructor
  Â· ext j
    dsimp
    rw [assoc, equalizer.lift_Î¹, â† equalizer.condition, id_comp, comp_id]
  Â· ext j
    simp [Y, i, e]
namespace KaroubiFunctorCategoryEmbedding

variable {J C}

/-- On objects, the functor which sends a formal direct factor `P` of a
functor `F : J â¥¤ C` to the functor `J â¥¤ Karoubi C` which sends `(j : J)` to
the corresponding direct factor of `F.obj j`. -/
@[simps]
def obj (P : Karoubi (J â¥¤ C)) : J â¥¤ Karoubi C where
  obj j := âŸ¨P.X.obj j, P.p.app j, congr_app P.idem jâŸ©
  map {j j'} Ï† :=
    { f := P.p.app j â‰« P.X.map Ï†
      comm := by
        simp only [NatTrans.naturality, assoc]
        have h := congr_app P.idem j
        rw [NatTrans.comp_app] at h
        rw [reassoc_of% h, reassoc_of% h] }

/-- Tautological action on maps of the functor `Karoubi (J â¥¤ C) â¥¤ (J â¥¤ Karoubi C)`. -/
@[simps]
def map {P Q : Karoubi (J â¥¤ C)} (f : P âŸ¶ Q) : obj P âŸ¶ obj Q where
  app j := âŸ¨f.f.app j, congr_app f.comm jâŸ©

end KaroubiFunctorCategoryEmbedding

/-- The tautological fully faithful functor `Karoubi (J â¥¤ C) â¥¤ (J â¥¤ Karoubi C)`. -/
@[simps]
def karoubiFunctorCategoryEmbedding : Karoubi (J â¥¤ C) â¥¤ J â¥¤ Karoubi C where
  obj := KaroubiFunctorCategoryEmbedding.obj
  map := KaroubiFunctorCategoryEmbedding.map

instance : (karoubiFunctorCategoryEmbedding J C).Full where
  map_surjective {P Q} f :=
   âŸ¨{ f :=
        { app := fun j => (f.app j).f
          naturality := fun j j' Ï† => by
            rw [â† Karoubi.comp_p_assoc]
            have h := hom_ext_iff.mp (f.naturality Ï†)
            simp only [comp_f] at h
            dsimp [karoubiFunctorCategoryEmbedding] at h
            erw [â† h, assoc, â† P.p.naturality_assoc Ï†, p_comp (f.app j')] }
      comm := by
        ext j
        exact (f.app j).comm }, rflâŸ©

instance : (karoubiFunctorCategoryEmbedding J C).Faithful where
  map_injective h := by
    ext j
    exact hom_ext_iff.mp (congr_app h j)

/-- The composition of `(J â¥¤ C) â¥¤ Karoubi (J â¥¤ C)` and `Karoubi (J â¥¤ C) â¥¤ (J â¥¤ Karoubi C)`
equals the functor `(J â¥¤ C) â¥¤ (J â¥¤ Karoubi C)` given by the composition with
`toKaroubi C : C â¥¤ Karoubi C`. -/
theorem toKaroubi_comp_karoubiFunctorCategoryEmbedding :
    toKaroubi _ â‹™ karoubiFunctorCategoryEmbedding J C =
      (whiskeringRight J _ _).obj (toKaroubi C) := by
  apply Functor.ext
  Â· intro X Y f
    ext j
    simp
  Â· intro X
    apply Functor.ext
    Â· intro j j' Ï†
      ext
      simp
    Â· intro j
      rfl

end Idempotents

end CategoryTheory
