/-
Copyright (c) 2022 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Abelian.Basic

/-!
# Idempotent complete categories

In this file, we define the notion of idempotent complete categories
(also known as Karoubian categories, or pseudoabelian in the case of
preadditive categories).

## Main definitions

- `IsIdempotentComplete C` expresses that `C` is idempotent complete, i.e.
all idempotents in `C` split. Other characterisations of idempotent completeness are given
by `isIdempotentComplete_iff_hasEqualizer_of_id_and_idempotent` and
`isIdempotentComplete_iff_idempotents_have_kernels`.
- `isIdempotentComplete_of_abelian` expresses that abelian categories are
idempotent complete.
- `isIdempotentComplete_iff_ofEquivalence` expresses that if two categories `C` and `D`
are equivalent, then `C` is idempotent complete iff `D` is.
- `isIdempotentComplete_iff_opposite` expresses that `C·µí·µñ` is idempotent complete
iff `C` is.

## References
* [Stacks: Karoubian categories] https://stacks.math.columbia.edu/tag/09SF

-/


open CategoryTheory

open CategoryTheory.Category

open CategoryTheory.Limits

open CategoryTheory.Preadditive

open Opposite

namespace CategoryTheory

variable (C : Type*) [Category C]

/-- A category is idempotent complete iff all idempotent endomorphisms `p`
split as a composition `p = e ‚â´ i` with `i ‚â´ e = ùüô _` -/
class IsIdempotentComplete : Prop where
  /-- A category is idempotent complete iff all idempotent endomorphisms `p`
    split as a composition `p = e ‚â´ i` with `i ‚â´ e = ùüô _` -/
  idempotents_split :
    ‚àÄ (X : C) (p : X ‚ü∂ X), p ‚â´ p = p ‚Üí ‚àÉ (Y : C) (i : Y ‚ü∂ X) (e : X ‚ü∂ Y), i ‚â´ e = ùüô Y ‚àß e ‚â´ i = p

namespace Idempotents

/-- A category is idempotent complete iff for all idempotent endomorphisms,
the equalizer of the identity and this idempotent exists. -/
theorem isIdempotentComplete_iff_hasEqualizer_of_id_and_idempotent :
    IsIdempotentComplete C ‚Üî ‚àÄ (X : C) (p : X ‚ü∂ X), p ‚â´ p = p ‚Üí HasEqualizer (ùüô X) p := by
  constructor
  ¬∑ intro
    intro X p hp
    rcases IsIdempotentComplete.idempotents_split X p hp with ‚ü®Y, i, e, ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©‚ü©
    exact
      ‚ü®Nonempty.intro
          { cone := Fork.ofŒπ i (show i ‚â´ ùüô X = i ‚â´ p by rw [comp_id, ‚Üê h‚ÇÇ, ‚Üê assoc, h‚ÇÅ, id_comp])
            isLimit := by
              apply Fork.IsLimit.mk'
              intro s
              refine ‚ü®s.Œπ ‚â´ e, ?_‚ü©
              constructor
              ¬∑ erw [assoc, h‚ÇÇ, ‚Üê Limits.Fork.condition s, comp_id]
              ¬∑ intro m hm
                rw [Fork.Œπ_ofŒπ] at hm
                rw [‚Üê hm]
                simp only [assoc, h‚ÇÅ]
                exact (comp_id m).symm }‚ü©
  ¬∑ intro h
    refine ‚ü®?_‚ü©
    intro X p hp
    haveI : HasEqualizer (ùüô X) p := h X p hp
    refine ‚ü®equalizer (ùüô X) p, equalizer.Œπ (ùüô X) p,
      equalizer.lift p (show p ‚â´ ùüô X = p ‚â´ p by rw [hp, comp_id]), ?_, equalizer.lift_Œπ _ _‚ü©
    ext
    simp only [assoc, limit.lift_œÄ, Fork.ofŒπ_pt,
      Fork.ofŒπ_œÄ_app, id_comp]
    rw [‚Üê equalizer.condition, comp_id]

variable {C} in
/-- In a preadditive category, when `p : X ‚ü∂ X` is idempotent,
then `ùüô X - p` is also idempotent. -/
theorem idem_of_id_sub_idem [Preadditive C] {X : C} (p : X ‚ü∂ X) (hp : p ‚â´ p = p) :
    (ùüô _ - p) ‚â´ (ùüô _ - p) = ùüô _ - p := by
  simp only [comp_sub, sub_comp, id_comp, comp_id, hp, sub_self, sub_zero]

/-- A preadditive category is pseudoabelian iff all idempotent endomorphisms have a kernel. -/
theorem isIdempotentComplete_iff_idempotents_have_kernels [Preadditive C] :
    IsIdempotentComplete C ‚Üî ‚àÄ (X : C) (p : X ‚ü∂ X), p ‚â´ p = p ‚Üí HasKernel p := by
  rw [isIdempotentComplete_iff_hasEqualizer_of_id_and_idempotent]
  constructor
  ¬∑ intro h X p hp
    haveI : HasEqualizer (ùüô X) (ùüô X - p) := h X (ùüô _ - p) (idem_of_id_sub_idem p hp)
    convert hasKernel_of_hasEqualizer (ùüô X) (ùüô X - p)
    rw [sub_sub_cancel]
  ¬∑ intro h X p hp
    haveI : HasKernel (ùüô _ - p) := h X (ùüô _ - p) (idem_of_id_sub_idem p hp)
    apply Preadditive.hasEqualizer_of_hasKernel

/-- An abelian category is idempotent complete. -/
instance (priority := 100) isIdempotentComplete_of_abelian (D : Type*) [Category D] [Abelian D] :
    IsIdempotentComplete D := by
  rw [isIdempotentComplete_iff_idempotents_have_kernels]
  intros
  infer_instance

variable {C}

theorem split_imp_of_iso {X X' : C} (œÜ : X ‚âÖ X') (p : X ‚ü∂ X) (p' : X' ‚ü∂ X')
    (hpp' : p ‚â´ œÜ.hom = œÜ.hom ‚â´ p')
    (h : ‚àÉ (Y : C) (i : Y ‚ü∂ X) (e : X ‚ü∂ Y), i ‚â´ e = ùüô Y ‚àß e ‚â´ i = p) :
    ‚àÉ (Y' : C) (i' : Y' ‚ü∂ X') (e' : X' ‚ü∂ Y'), i' ‚â´ e' = ùüô Y' ‚àß e' ‚â´ i' = p' := by
  rcases h with ‚ü®Y, i, e, ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©‚ü©
  use Y, i ‚â´ œÜ.hom, œÜ.inv ‚â´ e
  constructor
  ¬∑ slice_lhs 2 3 => rw [œÜ.hom_inv_id]
    rw [id_comp, h‚ÇÅ]
  ¬∑ slice_lhs 2 3 => rw [h‚ÇÇ]
    rw [hpp', ‚Üê assoc, œÜ.inv_hom_id, id_comp]

theorem split_iff_of_iso {X X' : C} (œÜ : X ‚âÖ X') (p : X ‚ü∂ X) (p' : X' ‚ü∂ X')
    (hpp' : p ‚â´ œÜ.hom = œÜ.hom ‚â´ p') :
    (‚àÉ (Y : C) (i : Y ‚ü∂ X) (e : X ‚ü∂ Y), i ‚â´ e = ùüô Y ‚àß e ‚â´ i = p) ‚Üî
      ‚àÉ (Y' : C) (i' : Y' ‚ü∂ X') (e' : X' ‚ü∂ Y'), i' ‚â´ e' = ùüô Y' ‚àß e' ‚â´ i' = p' := by
  constructor
  ¬∑ exact split_imp_of_iso œÜ p p' hpp'
  ¬∑ apply split_imp_of_iso œÜ.symm p' p
    rw [‚Üê comp_id p, ‚Üê œÜ.hom_inv_id]
    slice_rhs 2 3 => rw [hpp']
    simp

theorem Equivalence.isIdempotentComplete {D : Type*} [Category D] (Œµ : C ‚âå D)
    (h : IsIdempotentComplete C) : IsIdempotentComplete D := by
  refine ‚ü®?_‚ü©
  intro X' p hp
  let œÜ := Œµ.counitIso.symm.app X'
  erw [split_iff_of_iso œÜ p (œÜ.inv ‚â´ p ‚â´ œÜ.hom)
      (by
        slice_rhs 1 2 => rw [œÜ.hom_inv_id]
        rw [id_comp])]
  rcases IsIdempotentComplete.idempotents_split (Œµ.inverse.obj X') (Œµ.inverse.map p)
      (by rw [‚Üê Œµ.inverse.map_comp, hp]) with
    ‚ü®Y, i, e, ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©‚ü©
  use Œµ.functor.obj Y, Œµ.functor.map i, Œµ.functor.map e
  constructor
  ¬∑ rw [‚Üê Œµ.functor.map_comp, h‚ÇÅ, Œµ.functor.map_id]
  ¬∑ simp only [‚Üê Œµ.functor.map_comp, h‚ÇÇ, Equivalence.fun_inv_map]
    rfl

/-- If `C` and `D` are equivalent categories, that `C` is idempotent complete iff `D` is. -/
theorem isIdempotentComplete_iff_of_equivalence {D : Type*} [Category D] (Œµ : C ‚âå D) :
    IsIdempotentComplete C ‚Üî IsIdempotentComplete D := by
  constructor
  ¬∑ exact Equivalence.isIdempotentComplete Œµ
  ¬∑ exact Equivalence.isIdempotentComplete Œµ.symm

theorem isIdempotentComplete_of_isIdempotentComplete_opposite (h : IsIdempotentComplete C·µí·µñ) :
    IsIdempotentComplete C := by
  refine ‚ü®?_‚ü©
  intro X p hp
  rcases IsIdempotentComplete.idempotents_split (op X) p.op (by rw [‚Üê op_comp, hp]) with
    ‚ü®Y, i, e, ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©‚ü©
  use Y.unop, e.unop, i.unop
  constructor
  ¬∑ simp only [‚Üê unop_comp, h‚ÇÅ]
    rfl
  ¬∑ simp only [‚Üê unop_comp, h‚ÇÇ]
    rfl

theorem isIdempotentComplete_iff_opposite : IsIdempotentComplete C·µí·µñ ‚Üî IsIdempotentComplete C := by
  constructor
  ¬∑ exact isIdempotentComplete_of_isIdempotentComplete_opposite
  ¬∑ intro h
    apply isIdempotentComplete_of_isIdempotentComplete_opposite
    rw [isIdempotentComplete_iff_of_equivalence (opOpEquivalence C)]
    exact h

instance [IsIdempotentComplete C] : IsIdempotentComplete C·µí·µñ := by
  rwa [isIdempotentComplete_iff_opposite]

end Idempotents

end CategoryTheory
