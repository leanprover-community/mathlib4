/-
Copyright (c) 2022 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Idempotents.Karoubi

/-!
# Extension of functors to the idempotent completion

In this file, we construct an extension `functorExtension‚ÇÅ`
of functors `C ‚•§ Karoubi D` to functors `Karoubi C ‚•§ Karoubi D`. This results in an
equivalence `karoubiUniversal‚ÇÅ C D : (C ‚•§ Karoubi D) ‚âå (Karoubi C ‚•§ Karoubi D)`.

We also construct an extension `functorExtension‚ÇÇ` of functors
`(C ‚•§ D) ‚•§ (Karoubi C ‚•§ Karoubi D)`. Moreover,
when `D` is idempotent complete, we get equivalences
`karoubiUniversal‚ÇÇ C D : C ‚•§ D ‚âå Karoubi C ‚•§ Karoubi D`
and `karoubiUniversal C D : C ‚•§ D ‚âå Karoubi C ‚•§ D`.

-/

namespace CategoryTheory

namespace Idempotents

open Category Karoubi

variable {C D E : Type*} [Category C] [Category D] [Category E]

/-- A natural transformation between functors `Karoubi C ‚•§ D` is determined
by its value on objects coming from `C`. -/
theorem natTrans_eq {F G : Karoubi C ‚•§ D} (œÜ : F ‚ü∂ G) (P : Karoubi C) :
    œÜ.app P = F.map (decompId_i P) ‚â´ œÜ.app P.X ‚â´ G.map (decompId_p P) := by
  rw [‚Üê œÜ.naturality, ‚Üê assoc, ‚Üê F.map_comp]
  conv_lhs => rw [‚Üê id_comp (œÜ.app P), ‚Üê F.map_id]
  congr
  apply decompId

namespace FunctorExtension‚ÇÅ

/-- The canonical extension of a functor `C ‚•§ Karoubi D` to a functor
`Karoubi C ‚•§ Karoubi D` -/
@[simps]
def obj (F : C ‚•§ Karoubi D) : Karoubi C ‚•§ Karoubi D where
  obj P :=
    ‚ü®(F.obj P.X).X, (F.map P.p).f, by simpa only [F.map_comp, hom_ext_iff] using F.congr_map P.idem‚ü©
  map f := ‚ü®(F.map f.f).f, by simpa only [F.map_comp, hom_ext_iff] using F.congr_map f.comm‚ü©

/-- Extension of a natural transformation `œÜ` between functors
`C ‚•§ karoubi D` to a natural transformation between the
extension of these functors to `karoubi C ‚•§ karoubi D` -/
@[simps]
def map {F G : C ‚•§ Karoubi D} (œÜ : F ‚ü∂ G) : obj F ‚ü∂ obj G where
  app P :=
    { f := (F.map P.p).f ‚â´ (œÜ.app P.X).f
      comm := by
        have h := œÜ.naturality P.p
        have h' := F.congr_map P.idem
        simp only [hom_ext_iff, Karoubi.comp_f, F.map_comp] at h h'
        simp only [obj_obj_p, assoc, ‚Üê h]
        slice_lhs 1 3 => rw [h', h'] }
  naturality _ _ f := by
    ext
    dsimp [obj]
    have h := œÜ.naturality f.f
    have h' := F.congr_map (comp_p f)
    have h'' := F.congr_map (p_comp f)
    simp only [hom_ext_iff, Functor.map_comp, comp_f] at h h' h'' ‚ä¢
    slice_rhs 2 3 => rw [‚Üê h]
    slice_lhs 1 2 => rw [h']
    slice_rhs 1 2 => rw [h'']

end FunctorExtension‚ÇÅ

variable (C D E)

/-- The canonical functor `(C ‚•§ Karoubi D) ‚•§ (Karoubi C ‚•§ Karoubi D)` -/
@[simps]
def functorExtension‚ÇÅ : (C ‚•§ Karoubi D) ‚•§ Karoubi C ‚•§ Karoubi D where
  obj := FunctorExtension‚ÇÅ.obj
  map := FunctorExtension‚ÇÅ.map
  map_id F := by
    ext P
    exact comp_p (F.map P.p)
  map_comp {F G H} œÜ œÜ' := by
    ext P
    simp only [comp_f, FunctorExtension‚ÇÅ.map_app_f, NatTrans.comp_app, assoc]
    have h := œÜ.naturality P.p
    have h' := F.congr_map P.idem
    simp only [hom_ext_iff, comp_f, F.map_comp] at h h'
    slice_rhs 2 3 => rw [‚Üê h]
    slice_rhs 1 2 => rw [h']
    simp only [assoc]

/-- The natural isomorphism expressing that functors `Karoubi C ‚•§ Karoubi D` obtained
using `functorExtension‚ÇÅ` actually extends the original functors `C ‚•§ Karoubi D`. -/
@[simps!]
def functorExtension‚ÇÅCompWhiskeringLeftToKaroubiIso :
    functorExtension‚ÇÅ C D ‚ãô (whiskeringLeft C (Karoubi C) (Karoubi D)).obj (toKaroubi C) ‚âÖ ùü≠ _ :=
  NatIso.ofComponents
    (fun F => NatIso.ofComponents
      (fun X =>
        { hom := { f := (F.obj X).p }
          inv := { f := (F.obj X).p } })
      (fun {X Y} f => by simp))
    (by aesop_cat)

/-- The counit isomorphism of the equivalence `(C ‚•§ Karoubi D) ‚âå (Karoubi C ‚•§ Karoubi D)`. -/
def KaroubiUniversal‚ÇÅ.counitIso :
    (whiskeringLeft C (Karoubi C) (Karoubi D)).obj (toKaroubi C) ‚ãô functorExtension‚ÇÅ C D ‚âÖ ùü≠ _ :=
  NatIso.ofComponents
    (fun G =>
      { hom :=
          { app := fun P =>
              { f := (G.map (decompId_p P)).f
                comm := by
                  simpa only [hom_ext_iff, G.map_comp, G.map_id] using
                    G.congr_map
                      (show (toKaroubi C).map P.p ‚â´ P.decompId_p ‚â´ ùüô _ = P.decompId_p by simp) }
            naturality := fun P Q f => by
              simpa only [hom_ext_iff, G.map_comp]
                using (G.congr_map (decompId_p_naturality f)).symm }
        inv :=
          { app := fun P =>
              { f := (G.map (decompId_i P)).f
                comm := by
                  simpa only [hom_ext_iff, G.map_comp, G.map_id] using
                    G.congr_map
                      (show ùüô _ ‚â´ P.decompId_i ‚â´ (toKaroubi C).map P.p = P.decompId_i by simp) }
            naturality := fun P Q f => by
              simpa only [hom_ext_iff, G.map_comp] using G.congr_map (decompId_i_naturality f) }
        hom_inv_id := by
          ext P
          simpa only [hom_ext_iff, G.map_comp, G.map_id] using G.congr_map P.decomp_p.symm
        inv_hom_id := by
          ext P
          simpa only [hom_ext_iff, G.map_comp, G.map_id] using G.congr_map P.decompId.symm })
    (fun {X Y} œÜ => by
      ext P
      dsimp
      rw [natTrans_eq œÜ P, P.decomp_p]
      simp only [Functor.map_comp, comp_f, assoc]
      rfl)

attribute [simps!] KaroubiUniversal‚ÇÅ.counitIso

/-- The equivalence of categories `(C ‚•§ Karoubi D) ‚âå (Karoubi C ‚•§ Karoubi D)`. -/
@[simps]
def karoubiUniversal‚ÇÅ : C ‚•§ Karoubi D ‚âå Karoubi C ‚•§ Karoubi D where
  functor := functorExtension‚ÇÅ C D
  inverse := (whiskeringLeft C (Karoubi C) (Karoubi D)).obj (toKaroubi C)
  unitIso := (functorExtension‚ÇÅCompWhiskeringLeftToKaroubiIso C D).symm
  counitIso := KaroubiUniversal‚ÇÅ.counitIso C D
  functor_unitIso_comp F := by
    ext P
    dsimp
    rw [comp_p, ‚Üê comp_f, ‚Üê F.map_comp, P.idem]

/-- Compatibility isomorphisms of `functorExtension‚ÇÅ` with respect to the
composition of functors. -/
def functorExtension‚ÇÅComp (F : C ‚•§ Karoubi D) (G : D ‚•§ Karoubi E) :
    (functorExtension‚ÇÅ C E).obj (F ‚ãô (functorExtension‚ÇÅ D E).obj G) ‚âÖ
      (functorExtension‚ÇÅ C D).obj F ‚ãô (functorExtension‚ÇÅ D E).obj G :=
  Iso.refl _

/-- The canonical functor `(C ‚•§ D) ‚•§ (Karoubi C ‚•§ Karoubi D)` -/
@[simps!]
def functorExtension‚ÇÇ : (C ‚•§ D) ‚•§ Karoubi C ‚•§ Karoubi D :=
  (whiskeringRight C D (Karoubi D)).obj (toKaroubi D) ‚ãô functorExtension‚ÇÅ C D

/-- The natural isomorphism expressing that functors `Karoubi C ‚•§ Karoubi D` obtained
using `functorExtension‚ÇÇ` actually extends the original functors `C ‚•§ D`. -/
@[simps!]
def functorExtension‚ÇÇCompWhiskeringLeftToKaroubiIso :
    functorExtension‚ÇÇ C D ‚ãô (whiskeringLeft C (Karoubi C) (Karoubi D)).obj (toKaroubi C) ‚âÖ
      (whiskeringRight C D (Karoubi D)).obj (toKaroubi D) :=
  NatIso.ofComponents
    (fun F => NatIso.ofComponents
      (fun X =>
        { hom := { f := ùüô _ }
          inv := { f := ùüô _ } })
      (by simp))
    (by aesop_cat)

section IsIdempotentComplete

variable [IsIdempotentComplete D]

/-- The equivalence of categories `(C ‚•§ D) ‚âå (Karoubi C ‚•§ Karoubi D)` when `D`
is idempotent complete. -/
@[simp]
noncomputable def karoubiUniversal‚ÇÇ : C ‚•§ D ‚âå Karoubi C ‚•§ Karoubi D :=
  (Equivalence.congrRight (toKaroubi D).asEquivalence).trans (karoubiUniversal‚ÇÅ C D)

theorem karoubiUniversal‚ÇÇ_functor_eq : (karoubiUniversal‚ÇÇ C D).functor = functorExtension‚ÇÇ C D :=
  rfl

noncomputable instance : (functorExtension‚ÇÇ C D).IsEquivalence := by
  rw [‚Üê karoubiUniversal‚ÇÇ_functor_eq]
  infer_instance

/-- The extension of functors functor `(C ‚•§ D) ‚•§ (Karoubi C ‚•§ D)`
when `D` is idempotent complete. -/
@[simps!]
noncomputable def functorExtension : (C ‚•§ D) ‚•§ Karoubi C ‚•§ D :=
  functorExtension‚ÇÇ C D ‚ãô
    (whiskeringRight (Karoubi C) (Karoubi D) D).obj (toKaroubiEquivalence D).inverse

/-- The equivalence `(C ‚•§ D) ‚âå (Karoubi C ‚•§ D)` when `D` is idempotent complete. -/
@[simp]
noncomputable def karoubiUniversal : C ‚•§ D ‚âå Karoubi C ‚•§ D :=
  (karoubiUniversal‚ÇÇ C D).trans (Equivalence.congrRight (toKaroubi D).asEquivalence.symm)

theorem karoubiUniversal_functor_eq : (karoubiUniversal C D).functor = functorExtension C D :=
  rfl

noncomputable instance : (functorExtension C D).IsEquivalence := by
  rw [‚Üê karoubiUniversal_functor_eq]
  infer_instance

instance : ((whiskeringLeft C (Karoubi C) D).obj (toKaroubi C)).IsEquivalence := by
  have : ((whiskeringLeft C (Karoubi C) D).obj (toKaroubi C) ‚ãô
    (whiskeringRight C D (Karoubi D)).obj (toKaroubi D) ‚ãô
    (whiskeringRight C (Karoubi D) D).obj (Functor.inv (toKaroubi D))).IsEquivalence := by
    change (karoubiUniversal C D).inverse.IsEquivalence
    infer_instance
  exact Functor.isEquivalence_of_comp_right _
    ((whiskeringRight C _ _).obj (toKaroubi D) ‚ãô
      (whiskeringRight C (Karoubi D) D).obj (Functor.inv (toKaroubi D)))

variable {C D}

theorem whiskeringLeft_obj_preimage_app {F G : Karoubi C ‚•§ D}
    (œÑ : toKaroubi _ ‚ãô F ‚ü∂ toKaroubi _ ‚ãô G) (P : Karoubi C) :
    (((whiskeringLeft _ _ _).obj (toKaroubi _)).preimage œÑ).app P =
      F.map P.decompId_i ‚â´ œÑ.app P.X ‚â´ G.map P.decompId_p := by
  rw [natTrans_eq]
  congr 2
  rw [‚Üê congr_app (((whiskeringLeft _ _ _).obj (toKaroubi _)).map_preimage œÑ) P.X]
  dsimp
  congr

end IsIdempotentComplete

end Idempotents

end CategoryTheory
