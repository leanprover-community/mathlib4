/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Idempotents.Karoubi

/-!
# Extension of functors to the idempotent completion

In this file, we construct an extension `functorExtensionâ‚`
of functors `C â¥¤ Karoubi D` to functors `Karoubi C â¥¤ Karoubi D`. This results in an
equivalence `karoubiUniversalâ‚ C D : (C â¥¤ Karoubi D) â‰Œ (Karoubi C â¥¤ Karoubi D)`.

We also construct an extension `functorExtensionâ‚‚` of functors
`(C â¥¤ D) â¥¤ (Karoubi C â¥¤ Karoubi D)`. Moreover,
when `D` is idempotent complete, we get equivalences
`karoubiUniversalâ‚‚ C D : C â¥¤ D â‰Œ Karoubi C â¥¤ Karoubi D`
and `karoubiUniversal C D : C â¥¤ D â‰Œ Karoubi C â¥¤ D`.

-/

namespace CategoryTheory

namespace Idempotents

open Category Karoubi Functor

variable {C D E : Type*} [Category C] [Category D] [Category E]

/-- A natural transformation between functors `Karoubi C â¥¤ D` is determined
by its value on objects coming from `C`. -/
theorem natTrans_eq {F G : Karoubi C â¥¤ D} (Ï† : F âŸ¶ G) (P : Karoubi C) :
    Ï†.app P = F.map (decompId_i P) â‰« Ï†.app P.X â‰« G.map (decompId_p P) := by
  rw [â† Ï†.naturality, â† assoc, â† F.map_comp]
  conv_lhs => rw [â† id_comp (Ï†.app P), â† F.map_id]
  congr
  apply decompId

namespace FunctorExtensionâ‚

/-- The canonical extension of a functor `C â¥¤ Karoubi D` to a functor
`Karoubi C â¥¤ Karoubi D` -/
@[simps]
def obj (F : C â¥¤ Karoubi D) : Karoubi C â¥¤ Karoubi D where
  obj P :=
    âŸ¨(F.obj P.X).X, (F.map P.p).f, by simpa only [F.map_comp, hom_ext_iff] using F.congr_map P.idemâŸ©
  map f := âŸ¨(F.map f.f).f, by simpa only [F.map_comp, hom_ext_iff] using F.congr_map f.commâŸ©

/-- Extension of a natural transformation `Ï†` between functors
`C â¥¤ karoubi D` to a natural transformation between the
extension of these functors to `karoubi C â¥¤ karoubi D` -/
@[simps]
def map {F G : C â¥¤ Karoubi D} (Ï† : F âŸ¶ G) : obj F âŸ¶ obj G where
  app P :=
    { f := (F.map P.p).f â‰« (Ï†.app P.X).f
      comm := by
        have h := Ï†.naturality P.p
        have h' := F.congr_map P.idem
        simp only [hom_ext_iff, Karoubi.comp_f, F.map_comp] at h h'
        simp only [obj_obj_p, assoc, â† h]
        slice_lhs 1 3 => rw [h', h'] }
  naturality _ _ f := by
    ext
    dsimp [obj]
    have h := Ï†.naturality f.f
    have h' := F.congr_map (comp_p f)
    have h'' := F.congr_map (p_comp f)
    simp only [hom_ext_iff, Functor.map_comp, comp_f] at h h' h'' âŠ¢
    slice_rhs 2 3 => rw [â† h]
    slice_lhs 1 2 => rw [h']
    slice_rhs 1 2 => rw [h'']

end FunctorExtensionâ‚

variable (C D E)

/-- The canonical functor `(C â¥¤ Karoubi D) â¥¤ (Karoubi C â¥¤ Karoubi D)` -/
@[simps]
def functorExtensionâ‚ : (C â¥¤ Karoubi D) â¥¤ Karoubi C â¥¤ Karoubi D where
  obj := FunctorExtensionâ‚.obj
  map := FunctorExtensionâ‚.map
  map_id F := by
    ext P
    exact comp_p (F.map P.p)
  map_comp {F G H} Ï† Ï†' := by
    ext P
    simp only [comp_f, FunctorExtensionâ‚.map_app_f, NatTrans.comp_app, assoc]
    have h := Ï†.naturality P.p
    have h' := F.congr_map P.idem
    simp only [hom_ext_iff, comp_f, F.map_comp] at h h'
    slice_rhs 2 3 => rw [â† h]
    slice_rhs 1 2 => rw [h']
    simp only [assoc]

/-- The natural isomorphism expressing that functors `Karoubi C â¥¤ Karoubi D` obtained
using `functorExtensionâ‚` actually extends the original functors `C â¥¤ Karoubi D`. -/
@[simps!]
def functorExtensionâ‚CompWhiskeringLeftToKaroubiIso :
    functorExtensionâ‚ C D â‹™ (whiskeringLeft C (Karoubi C) (Karoubi D)).obj (toKaroubi C) â‰… ğŸ­ _ :=
  NatIso.ofComponents
    (fun F => NatIso.ofComponents
      (fun X =>
        { hom := { f := (F.obj X).p }
          inv := { f := (F.obj X).p } })
      (fun {X Y} f => by simp))
    (by cat_disch)

/-- The counit isomorphism of the equivalence `(C â¥¤ Karoubi D) â‰Œ (Karoubi C â¥¤ Karoubi D)`. -/
def KaroubiUniversalâ‚.counitIso :
    (whiskeringLeft C (Karoubi C) (Karoubi D)).obj (toKaroubi C) â‹™ functorExtensionâ‚ C D â‰… ğŸ­ _ :=
  NatIso.ofComponents
    (fun G =>
      { hom :=
          { app := fun P =>
              { f := (G.map (decompId_p P)).f
                comm := by
                  simpa only [hom_ext_iff, G.map_comp, G.map_id] using
                    G.congr_map
                      (show (toKaroubi C).map P.p â‰« P.decompId_p â‰« ğŸ™ _ = P.decompId_p by simp) }
            naturality := fun P Q f => by
              simpa only [hom_ext_iff, G.map_comp]
                using (G.congr_map (decompId_p_naturality f)).symm }
        inv :=
          { app := fun P =>
              { f := (G.map (decompId_i P)).f
                comm := by
                  simpa only [hom_ext_iff, G.map_comp, G.map_id] using
                    G.congr_map
                      (show ğŸ™ _ â‰« P.decompId_i â‰« (toKaroubi C).map P.p = P.decompId_i by simp) }
            naturality := fun P Q f => by
              simpa only [hom_ext_iff, G.map_comp] using G.congr_map (decompId_i_naturality f) }
        hom_inv_id := by
          ext P
          simpa only [hom_ext_iff, G.map_comp, G.map_id] using G.congr_map P.decomp_p.symm
        inv_hom_id := by
          ext P
          simpa only [hom_ext_iff, G.map_comp, G.map_id] using G.congr_map P.decompId.symm })
    (fun {X Y} Ï† => by
      ext P
      dsimp
      rw [natTrans_eq Ï† P, P.decomp_p]
      simp only [Functor.map_comp, comp_f, assoc]
      rfl)

attribute [simps!] KaroubiUniversalâ‚.counitIso

/-- The equivalence of categories `(C â¥¤ Karoubi D) â‰Œ (Karoubi C â¥¤ Karoubi D)`. -/
@[simps]
def karoubiUniversalâ‚ : C â¥¤ Karoubi D â‰Œ Karoubi C â¥¤ Karoubi D where
  functor := functorExtensionâ‚ C D
  inverse := (whiskeringLeft C (Karoubi C) (Karoubi D)).obj (toKaroubi C)
  unitIso := (functorExtensionâ‚CompWhiskeringLeftToKaroubiIso C D).symm
  counitIso := KaroubiUniversalâ‚.counitIso C D
  functor_unitIso_comp F := by
    ext P
    dsimp
    rw [comp_p, â† comp_f, â† F.map_comp, P.idem]

/-- Compatibility isomorphisms of `functorExtensionâ‚` with respect to the
composition of functors. -/
def functorExtensionâ‚Comp (F : C â¥¤ Karoubi D) (G : D â¥¤ Karoubi E) :
    (functorExtensionâ‚ C E).obj (F â‹™ (functorExtensionâ‚ D E).obj G) â‰…
      (functorExtensionâ‚ C D).obj F â‹™ (functorExtensionâ‚ D E).obj G :=
  Iso.refl _

/-- The canonical functor `(C â¥¤ D) â¥¤ (Karoubi C â¥¤ Karoubi D)` -/
@[simps!]
def functorExtensionâ‚‚ : (C â¥¤ D) â¥¤ Karoubi C â¥¤ Karoubi D :=
  (whiskeringRight C D (Karoubi D)).obj (toKaroubi D) â‹™ functorExtensionâ‚ C D

/-- The natural isomorphism expressing that functors `Karoubi C â¥¤ Karoubi D` obtained
using `functorExtensionâ‚‚` actually extends the original functors `C â¥¤ D`. -/
@[simps!]
def functorExtensionâ‚‚CompWhiskeringLeftToKaroubiIso :
    functorExtensionâ‚‚ C D â‹™ (whiskeringLeft C (Karoubi C) (Karoubi D)).obj (toKaroubi C) â‰…
      (whiskeringRight C D (Karoubi D)).obj (toKaroubi D) :=
  NatIso.ofComponents
    (fun F => NatIso.ofComponents
      (fun X =>
        { hom := { f := ğŸ™ _ }
          inv := { f := ğŸ™ _ } })
      (by simp))
    (by cat_disch)

section IsIdempotentComplete

variable [IsIdempotentComplete D]

/-- The equivalence of categories `(C â¥¤ D) â‰Œ (Karoubi C â¥¤ Karoubi D)` when `D`
is idempotent complete. -/
@[simp]
noncomputable def karoubiUniversalâ‚‚ : C â¥¤ D â‰Œ Karoubi C â¥¤ Karoubi D :=
  (Equivalence.congrRight (toKaroubi D).asEquivalence).trans (karoubiUniversalâ‚ C D)

theorem karoubiUniversalâ‚‚_functor_eq : (karoubiUniversalâ‚‚ C D).functor = functorExtensionâ‚‚ C D :=
  rfl

noncomputable instance : (functorExtensionâ‚‚ C D).IsEquivalence := by
  rw [â† karoubiUniversalâ‚‚_functor_eq]
  infer_instance

/-- The extension of functors functor `(C â¥¤ D) â¥¤ (Karoubi C â¥¤ D)`
when `D` is idempotent complete. -/
@[simps!]
noncomputable def functorExtension : (C â¥¤ D) â¥¤ Karoubi C â¥¤ D :=
  functorExtensionâ‚‚ C D â‹™
    (whiskeringRight (Karoubi C) (Karoubi D) D).obj (toKaroubiEquivalence D).inverse

/-- The equivalence `(C â¥¤ D) â‰Œ (Karoubi C â¥¤ D)` when `D` is idempotent complete. -/
@[simp]
noncomputable def karoubiUniversal : C â¥¤ D â‰Œ Karoubi C â¥¤ D :=
  (karoubiUniversalâ‚‚ C D).trans (Equivalence.congrRight (toKaroubi D).asEquivalence.symm)

theorem karoubiUniversal_functor_eq : (karoubiUniversal C D).functor = functorExtension C D :=
  rfl

noncomputable instance : (functorExtension C D).IsEquivalence := by
  rw [â† karoubiUniversal_functor_eq]
  infer_instance

instance : ((whiskeringLeft C (Karoubi C) D).obj (toKaroubi C)).IsEquivalence := by
  have : ((whiskeringLeft C (Karoubi C) D).obj (toKaroubi C) â‹™
    (whiskeringRight C D (Karoubi D)).obj (toKaroubi D) â‹™
    (whiskeringRight C (Karoubi D) D).obj (Functor.inv (toKaroubi D))).IsEquivalence := by
    change (karoubiUniversal C D).inverse.IsEquivalence
    infer_instance
  exact Functor.isEquivalence_of_comp_right _
    ((whiskeringRight C _ _).obj (toKaroubi D) â‹™
      (whiskeringRight C (Karoubi D) D).obj (Functor.inv (toKaroubi D)))

variable {C D}

theorem whiskeringLeft_obj_preimage_app {F G : Karoubi C â¥¤ D}
    (Ï„ : toKaroubi _ â‹™ F âŸ¶ toKaroubi _ â‹™ G) (P : Karoubi C) :
    (((whiskeringLeft _ _ _).obj (toKaroubi _)).preimage Ï„).app P =
      F.map P.decompId_i â‰« Ï„.app P.X â‰« G.map P.decompId_p := by
  rw [natTrans_eq]
  congr 2
  rw [â† congr_app (((whiskeringLeft _ _ _).obj (toKaroubi _)).map_preimage Ï„) P.X]
  dsimp
  congr

end IsIdempotentComplete

variable {C D} in
/-- The precomposition of functors with `toKaroubi C` is fully faithful. -/
def whiskeringLeftObjToKaroubiFullyFaithful :
    ((Functor.whiskeringLeft C (Karoubi C) D).obj (toKaroubi C)).FullyFaithful where
  preimage {F G} Ï„ :=
    { app P := F.map P.decompId_i â‰« Ï„.app P.X â‰« G.map P.decompId_p
      naturality X Y f := by
        dsimp at Ï„ âŠ¢
        have hâ‚ : f â‰« Y.decompId_i = X.decompId_i â‰« (toKaroubi C).map f.f := by simp
        have hâ‚‚ := Ï„.naturality f.f
        have hâ‚ƒ : X.decompId_p â‰« f = (toKaroubi C).map f.f â‰« Y.decompId_p := by simp
        dsimp at hâ‚‚
        rw [Category.assoc, Category.assoc, â† F.map_comp_assoc,
          hâ‚, F.map_comp_assoc, reassoc_of% hâ‚‚, â† G.map_comp, â† hâ‚ƒ, G.map_comp] }
  preimage_map {F G} Ï„ := by ext X; exact (natTrans_eq _ _).symm
  map_preimage {F G} Ï„ := by
    ext X
    dsimp
    rw [Karoubi.decompId_i_toKaroubi, Karoubi.decompId_p_toKaroubi,
      Functor.map_id, Category.id_comp]
    change _ â‰« G.map (ğŸ™ _) = _
    simp

end Idempotents

end CategoryTheory
