/-
Copyright (c) 2022 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Idempotents.Basic
import Mathlib.CategoryTheory.Preadditive.AdditiveFunctor
import Mathlib.CategoryTheory.Equivalence

/-!
# The Karoubi envelope of a category

In this file, we define the Karoubi envelope `Karoubi C` of a category `C`.

## Main constructions and definitions

- `Karoubi C` is the Karoubi envelope of a category `C`: it is an idempotent
complete category. It is also preadditive when `C` is preadditive.
- `toKaroubi C : C ‚•§ Karoubi C` is a fully faithful functor, which is an equivalence
(`toKaroubiIsEquivalence`) when `C` is idempotent complete.

-/

noncomputable section

open CategoryTheory.Category CategoryTheory.Preadditive CategoryTheory.Limits

namespace CategoryTheory

variable (C : Type*) [Category C]

namespace Idempotents

/-- In a preadditive category `C`, when an object `X` decomposes as `X ‚âÖ P ‚®ø Q`, one may
consider `P` as a direct factor of `X` and up to unique isomorphism, it is determined by the
obvious idempotent `X ‚ü∂ P ‚ü∂ X` which is the projection onto `P` with kernel `Q`. More generally,
one may define a formal direct factor of an object `X : C` : it consists of an idempotent
`p : X ‚ü∂ X` which is thought as the "formal image" of `p`. The type `Karoubi C` shall be the
type of the objects of the karoubi envelope of `C`. It makes sense for any category `C`. -/
structure Karoubi where
  /-- an object of the underlying category -/
  X : C
  /-- an endomorphism of the object -/
  p : X ‚ü∂ X
  /-- the condition that the given endomorphism is an idempotent -/
  idem : p ‚â´ p = p := by cat_disch

namespace Karoubi

variable {C}

attribute [reassoc (attr := simp)] idem

@[ext (iff := false)]
theorem ext {P Q : Karoubi C} (h_X : P.X = Q.X) (h_p : P.p ‚â´ eqToHom h_X = eqToHom h_X ‚â´ Q.p) :
    P = Q := by
  cases P
  cases Q
  dsimp at h_X h_p
  subst h_X
  simpa only [mk.injEq, heq_eq_eq, true_and, eqToHom_refl, comp_id, id_comp] using h_p

/-- A morphism `P ‚ü∂ Q` in the category `Karoubi C` is a morphism in the underlying category
`C` which satisfies a relation, which in the preadditive case, expresses that it induces a
map between the corresponding "formal direct factors" and that it vanishes on the complement
formal direct factor. -/
@[ext]
structure Hom (P Q : Karoubi C) where
  /-- a morphism between the underlying objects -/
  f : P.X ‚ü∂ Q.X
  /-- compatibility of the given morphism with the given idempotents -/
  comm : P.p ‚â´ f ‚â´ Q.p = f := by cat_disch

instance [Preadditive C] (P Q : Karoubi C) : Inhabited (Hom P Q) :=
  ‚ü®‚ü®0, by rw [zero_comp, comp_zero]‚ü©‚ü©

@[reassoc (attr := simp)]
theorem p_comp {P Q : Karoubi C} (f : Hom P Q) : P.p ‚â´ f.f = f.f := by
  rw [‚Üê f.comm, ‚Üê assoc, P.idem]

@[reassoc (attr := simp)]
theorem comp_p {P Q : Karoubi C} (f : Hom P Q) : f.f ‚â´ Q.p = f.f := by
  rw [‚Üê f.comm, assoc, assoc, Q.idem]

@[reassoc]
theorem p_comm {P Q : Karoubi C} (f : Hom P Q) : P.p ‚â´ f.f = f.f ‚â´ Q.p := by rw [p_comp, comp_p]

theorem comp_proof {P Q R : Karoubi C} (g : Hom Q R) (f : Hom P Q) :
    P.p ‚â´ (f.f ‚â´ g.f) ‚â´ R.p = f.f ‚â´ g.f := by simp

/-- The category structure on the karoubi envelope of a category. -/
instance : Category (Karoubi C) where
  Hom := Karoubi.Hom
  id P := ‚ü®P.p, by repeat' rw [P.idem]‚ü©
  comp f g := ‚ü®f.f ‚â´ g.f, Karoubi.comp_proof g f‚ü©

@[simp]
theorem hom_ext_iff {P Q : Karoubi C} {f g : P ‚ü∂ Q} : f = g ‚Üî f.f = g.f := by
  constructor
  ¬∑ intro h
    rw [h]
  ¬∑ apply Hom.ext

@[ext]
theorem hom_ext {P Q : Karoubi C} (f g : P ‚ü∂ Q) (h : f.f = g.f) : f = g := by
  simpa [hom_ext_iff] using h

@[simp]
theorem comp_f {P Q R : Karoubi C} (f : P ‚ü∂ Q) (g : Q ‚ü∂ R) : (f ‚â´ g).f = f.f ‚â´ g.f := rfl

@[simp]
theorem id_f {P : Karoubi C} : Hom.f (ùüô P) = P.p := rfl

/-- It is possible to coerce an object of `C` into an object of `Karoubi C`.
See also the functor `toKaroubi`. -/
instance coe : CoeTC C (Karoubi C) :=
  ‚ü®fun X => ‚ü®X, ùüô X, by rw [comp_id]‚ü©‚ü©

theorem coe_X (X : C) : (X : Karoubi C).X = X := by simp

@[simp]
theorem coe_p (X : C) : (X : Karoubi C).p = ùüô X := rfl

@[simp]
theorem eqToHom_f {P Q : Karoubi C} (h : P = Q) :
    Karoubi.Hom.f (eqToHom h) = P.p ‚â´ eqToHom (congr_arg Karoubi.X h) := by
  subst h
  simp only [eqToHom_refl, Karoubi.id_f, comp_id]

end Karoubi

/-- The obvious fully faithful functor `toKaroubi` sends an object `X : C` to the obvious
formal direct factor of `X` given by `ùüô X`. -/
@[simps]
def toKaroubi : C ‚•§ Karoubi C where
  obj X := ‚ü®X, ùüô X, by rw [comp_id]‚ü©
  map f := ‚ü®f, by simp only [comp_id, id_comp]‚ü©

instance : (toKaroubi C).Full where map_surjective f := ‚ü®f.f, rfl‚ü©

instance : (toKaroubi C).Faithful where
  map_injective := fun h => congr_arg Karoubi.Hom.f h

variable {C}

@[simps add]
instance instAdd [Preadditive C] {P Q : Karoubi C} : Add (P ‚ü∂ Q) where
  add f g := ‚ü®f.f + g.f, by rw [add_comp, comp_add, f.comm, g.comm]‚ü©

@[simps neg]
instance instNeg [Preadditive C] {P Q : Karoubi C} : Neg (P ‚ü∂ Q) where
  neg f := ‚ü®-f.f, by simpa only [neg_comp, comp_neg, neg_inj] using f.comm‚ü©

@[simps zero]
instance instZero [Preadditive C] {P Q : Karoubi C} : Zero (P ‚ü∂ Q) where
  zero := ‚ü®0, by simp only [comp_zero, zero_comp]‚ü©

instance instAddCommGroupHom [Preadditive C] {P Q : Karoubi C} : AddCommGroup (P ‚ü∂ Q) where
  zero_add f := by
    ext
    apply zero_add
  add_zero f := by
    ext
    apply add_zero
  add_assoc f g h' := by
    ext
    apply add_assoc
  add_comm f g := by
    ext
    apply add_comm
  neg_add_cancel f := by
    ext
    apply neg_add_cancel
  zsmul := zsmulRec
  nsmul := nsmulRec

namespace Karoubi

theorem hom_eq_zero_iff [Preadditive C] {P Q : Karoubi C} {f : P ‚ü∂ Q} : f = 0 ‚Üî f.f = 0 :=
  hom_ext_iff

/-- The map sending `f : P ‚ü∂ Q` to `f.f : P.X ‚ü∂ Q.X` is additive. -/
@[simps]
def inclusionHom [Preadditive C] (P Q : Karoubi C) : AddMonoidHom (P ‚ü∂ Q) (P.X ‚ü∂ Q.X) where
  toFun f := f.f
  map_zero' := rfl
  map_add' _ _ := rfl

@[simp]
theorem sum_hom [Preadditive C] {P Q : Karoubi C} {Œ± : Type*} (s : Finset Œ±) (f : Œ± ‚Üí (P ‚ü∂ Q)) :
    (‚àë x ‚àà s, f x).f = ‚àë x ‚àà s, (f x).f :=
  map_sum (inclusionHom P Q) f s

end Karoubi

/-- The category `Karoubi C` is preadditive if `C` is. -/
instance [Preadditive C] : Preadditive (Karoubi C) where
  homGroup P Q := by infer_instance

instance [Preadditive C] : Functor.Additive (toKaroubi C) where

open Karoubi

variable (C)

instance : IsIdempotentComplete (Karoubi C) := by
  refine ‚ü®?_‚ü©
  intro P p hp
  simp only [hom_ext_iff, comp_f] at hp
  use ‚ü®P.X, p.f, hp‚ü©
  use ‚ü®p.f, by rw [comp_p p, hp]‚ü©
  use ‚ü®p.f, by rw [hp, p_comp p]‚ü©
  simp [hp]

instance [IsIdempotentComplete C] : (toKaroubi C).EssSurj :=
  ‚ü®fun P => by
    rcases IsIdempotentComplete.idempotents_split P.X P.p P.idem with ‚ü®Y, i, e, ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©‚ü©
    use Y
    exact
      Nonempty.intro
        { hom := ‚ü®i, by erw [id_comp, ‚Üê h‚ÇÇ, ‚Üê assoc, h‚ÇÅ, id_comp]‚ü©
          inv := ‚ü®e, by erw [comp_id, ‚Üê h‚ÇÇ, assoc, h‚ÇÅ, comp_id]‚ü© }‚ü©

/-- If `C` is idempotent complete, the functor `toKaroubi : C ‚•§ Karoubi C` is an equivalence. -/
instance toKaroubi_isEquivalence [IsIdempotentComplete C] : (toKaroubi C).IsEquivalence where

/-- The equivalence `C ‚âÖ Karoubi C` when `C` is idempotent complete. -/
def toKaroubiEquivalence [IsIdempotentComplete C] : C ‚âå Karoubi C :=
  (toKaroubi C).asEquivalence

instance toKaroubiEquivalence_functor_additive [Preadditive C] [IsIdempotentComplete C] :
    (toKaroubiEquivalence C).functor.Additive :=
  (inferInstance : (toKaroubi C).Additive)

namespace Karoubi

variable {C}

/-- The split mono which appears in the factorisation `decompId P`. -/
@[simps]
def decompId_i (P : Karoubi C) : P ‚ü∂ P.X :=
  ‚ü®P.p, by rw [coe_p, comp_id, P.idem]‚ü©

/-- The split epi which appears in the factorisation `decompId P`. -/
@[simps]
def decompId_p (P : Karoubi C) : (P.X : Karoubi C) ‚ü∂ P :=
  ‚ü®P.p, by rw [coe_p, id_comp, P.idem]‚ü©

/-- The formal direct factor of `P.X` given by the idempotent `P.p` in the category `C`
is actually a direct factor in the category `Karoubi C`. -/
@[reassoc]
theorem decompId (P : Karoubi C) : ùüô P = decompId_i P ‚â´ decompId_p P := by
  ext
  simp only [comp_f, id_f, P.idem, decompId_i, decompId_p]

theorem decomp_p (P : Karoubi C) : (toKaroubi C).map P.p = decompId_p P ‚â´ decompId_i P := by
  ext
  simp only [comp_f, decompId_p_f, decompId_i_f, P.idem, toKaroubi_map_f]

theorem decompId_i_toKaroubi (X : C) : decompId_i ((toKaroubi C).obj X) = ùüô _ :=
  rfl

theorem decompId_p_toKaroubi (X : C) : decompId_p ((toKaroubi C).obj X) = ùüô _ :=
  rfl

theorem decompId_i_naturality {P Q : Karoubi C} (f : P ‚ü∂ Q) :
    f ‚â´ decompId_i Q = decompId_i P ‚â´ (by exact Hom.mk f.f (by simp)) := by
  simp

theorem decompId_p_naturality {P Q : Karoubi C} (f : P ‚ü∂ Q) :
    decompId_p P ‚â´ f = (by exact Hom.mk f.f (by simp)) ‚â´ decompId_p Q := by
  simp

@[simp]
theorem zsmul_hom [Preadditive C] {P Q : Karoubi C} (n : ‚Ñ§) (f : P ‚ü∂ Q) : (n ‚Ä¢ f).f = n ‚Ä¢ f.f :=
  map_zsmul (inclusionHom P Q) n f

end Karoubi

end Idempotents

end CategoryTheory
