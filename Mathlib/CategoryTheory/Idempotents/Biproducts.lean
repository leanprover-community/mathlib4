/-
Copyright (c) 2022 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Idempotents.Karoubi

/-!

# Biproducts in the idempotent completion of a preadditive category

In this file, we define an instance expressing that if `C` is an additive category
(i.e. is preadditive and has finite biproducts), then `Karoubi C` is also an additive category.

We also obtain that for all `P : Karoubi C` where `C` is a preadditive category `C`, there
is a canonical isomorphism `P ‚äû P.complement ‚âÖ (toKaroubi C).obj P.X` in the category
`Karoubi C` where `P.complement` is the formal direct factor of `P.X` corresponding to
the idempotent endomorphism `ùüô P.X - P.p`.

-/


noncomputable section

open CategoryTheory.Category

open CategoryTheory.Limits

open CategoryTheory.Preadditive

universe v

namespace CategoryTheory

namespace Idempotents

namespace Karoubi

variable {C : Type*} [Category.{v} C] [Preadditive C]

namespace Biproducts

/-- The `Bicone` used in order to obtain the existence of
the biproduct of a functor `J ‚•§ Karoubi C` when the category `C` is additive. -/
@[simps]
def bicone [HasFiniteBiproducts C] {J : Type} [Finite J] (F : J ‚Üí Karoubi C) : Bicone F where
  pt :=
    { X := biproduct fun j => (F j).X
      p := biproduct.map fun j => (F j).p
      idem := by
        ext
        simp only [assoc, biproduct.map_œÄ, biproduct.map_œÄ_assoc, idem] }
  œÄ j :=
    { f := (biproduct.map fun j => (F j).p) ‚â´ Bicone.œÄ _ j
      comm := by
        simp only [assoc, biproduct.bicone_œÄ, biproduct.map_œÄ, biproduct.map_œÄ_assoc, (F j).idem] }
  Œπ j :=
    { f := biproduct.Œπ (fun j => (F j).X) j ‚â´ biproduct.map fun j => (F j).p
      comm := by simp only [biproduct.Œπ_map, assoc, idem_assoc] }
  Œπ_œÄ j j' := by
    split_ifs with h
    ¬∑ subst h
      simp only [biproduct.Œπ_map, biproduct.bicone_œÄ, biproduct.map_œÄ, eqToHom_refl,
        id_f, hom_ext_iff, comp_f, assoc, bicone_Œπ_œÄ_self_assoc, idem]
    ¬∑ dsimp
      simp only [biproduct.Œπ_map, biproduct.map_œÄ, hom_ext_iff, comp_f,
        assoc, biproduct.Œπ_œÄ_ne_assoc _ h, zero_comp, comp_zero, zero_def]

end Biproducts

theorem karoubi_hasFiniteBiproducts [HasFiniteBiproducts C] : HasFiniteBiproducts (Karoubi C) :=
  { out := fun n =>
      { has_biproduct := fun F => by
          apply hasBiproduct_of_total (Biproducts.bicone F)
          simp only [hom_ext_iff]
          refine biproduct.hom_ext' _ _ (fun j => ?_)
          simp only [Biproducts.bicone_pt_X, sum_hom, comp_f, Biproducts.bicone_œÄ_f,
            biproduct.bicone_œÄ, biproduct.map_œÄ, Biproducts.bicone_Œπ_f, biproduct.Œπ_map, assoc,
            idem_assoc, id_f, Biproducts.bicone_pt_p, comp_sum]
          rw [Finset.sum_eq_single j]
          ¬∑ simp only [bicone_Œπ_œÄ_self_assoc]
          ¬∑ intro b _ hb
            simp only [biproduct.Œπ_œÄ_ne_assoc _ hb.symm, zero_comp]
          ¬∑ intro hj
            simp only [Finset.mem_univ, not_true] at hj } }

attribute [instance] karoubi_hasFiniteBiproducts

/-- `P.complement` is the formal direct factor of `P.X` given by the idempotent
endomorphism `ùüô P.X - P.p` -/
@[simps]
def complement (P : Karoubi C) : Karoubi C where
  X := P.X
  p := ùüô _ - P.p
  idem := idem_of_id_sub_idem P.p P.idem

instance (P : Karoubi C) : HasBinaryBiproduct P P.complement :=
  hasBinaryBiproduct_of_total
    { pt := P.X
      fst := P.decompId_p
      snd := P.complement.decompId_p
      inl := P.decompId_i
      inr := P.complement.decompId_i
      inl_fst := P.decompId.symm
      inl_snd := by
        simp only [zero_def, hom_ext_iff, complement_X, comp_f,
          decompId_i_f, decompId_p_f, complement_p, comp_sub, comp_id, idem, sub_self]
      inr_fst := by
        simp only [zero_def, hom_ext_iff, complement_X, comp_f,
          decompId_i_f, complement_p, decompId_p_f, sub_comp, id_comp, idem, sub_self]
      inr_snd := P.complement.decompId.symm }
    (by
      ext
      simp only [complement_X, comp_f, decompId_i_f, decompId_p_f, complement_p, add_def, idem,
        comp_sub, comp_id, sub_comp, id_comp, sub_self, sub_zero, add_sub_cancel, id_f])

attribute [-simp] hom_ext_iff

/-- A formal direct factor `P : Karoubi C` of an object `P.X : C` in a
preadditive category is actually a direct factor of the image `(toKaroubi C).obj P.X`
of `P.X` in the category `Karoubi C` -/
def decomposition (P : Karoubi C) : P ‚äû P.complement ‚âÖ (toKaroubi _).obj P.X where
  hom := biprod.desc P.decompId_i P.complement.decompId_i
  inv := biprod.lift P.decompId_p P.complement.decompId_p
  hom_inv_id := by
    apply biprod.hom_ext'
    ¬∑ rw [biprod.inl_desc_assoc, comp_id, biprod.lift_eq, comp_add, ‚Üê decompId_assoc,
        add_eq_left, ‚Üê assoc]
      refine (?_ =‚â´ _).trans zero_comp
      ext
      simp only [comp_f, toKaroubi_obj_X, decompId_i_f, decompId_p_f,
        complement_p, comp_sub, comp_id, idem, sub_self, zero_def]
    ¬∑ rw [biprod.inr_desc_assoc, comp_id, biprod.lift_eq, comp_add, ‚Üê decompId_assoc,
        add_eq_right, ‚Üê assoc]
      refine (?_ =‚â´ _).trans zero_comp
      ext
      simp only [complement_X, comp_f, decompId_i_f, complement_p,
        decompId_p_f, sub_comp, id_comp, idem, sub_self, zero_def]
  inv_hom_id := by
    ext
    simp only [toKaroubi_obj_X, biprod.lift_desc, add_def, comp_f, decompId_p_f, decompId_i_f,
      idem, complement_X, complement_p, comp_sub, comp_id, sub_comp, id_comp, sub_self, sub_zero,
      add_sub_cancel, id_f, toKaroubi_obj_p]

end Karoubi

end Idempotents

end CategoryTheory
