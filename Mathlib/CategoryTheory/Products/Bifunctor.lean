/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stephen Morgan, Scott Morrison, Andrea Laretto
-/
import Mathlib.CategoryTheory.Products.Basic
import Mathlib.Tactic.CategoryTheory.Reassoc
import Mathlib.CategoryTheory.Opposites

#align_import category_theory.products.bifunctor from "leanprover-community/mathlib"@"dc6c365e751e34d100e80fe6e314c3c3e0fd2988"

/-!
# Lemmas about functors out of product categories.
-/

open CategoryTheory

universe vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ uâ‚ uâ‚‚ uâ‚ƒ uâ‚„

variable {C : Type uâ‚} {D : Type uâ‚‚} {E : Type uâ‚ƒ} {F : Type uâ‚„}

variable [Category.{vâ‚} C] [Category.{vâ‚‚} D] [Category.{vâ‚ƒ} E] [Category.{vâ‚„} F]

namespace CategoryTheory.Bifunctor

@[simp]
theorem map_id (F : C Ã— D â¥¤ E) (X : C) (Y : D) :
    F.map ((ğŸ™ X, ğŸ™ Y) : (X, Y) âŸ¶ (X, Y)) = ğŸ™ (F.obj (X, Y)) :=
  F.map_id (X, Y)
#align category_theory.bifunctor.map_id CategoryTheory.Bifunctor.map_id

@[simp]
theorem map_id_comp (F : C Ã— D â¥¤ E) (W : C) {X Y Z : D} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    F.map ((ğŸ™ W, f â‰« g) : (W, X) âŸ¶ (W, Z)) =
      F.map ((ğŸ™ W, f) : (W, X) âŸ¶ (W, Y)) â‰« F.map ((ğŸ™ W, g) : (W, Y) âŸ¶ (W, Z)) := by
  rw [â† Functor.map_comp, prod_comp, Category.comp_id]
#align category_theory.bifunctor.map_id_comp CategoryTheory.Bifunctor.map_id_comp

@[simp]
theorem map_comp_id (F : C Ã— D â¥¤ E) (X Y Z : C) (W : D) (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    F.map ((f â‰« g, ğŸ™ W) : (X, W) âŸ¶ (Z, W)) =
      F.map ((f, ğŸ™ W) : (X, W) âŸ¶ (Y, W)) â‰« F.map ((g, ğŸ™ W) : (Y, W) âŸ¶ (Z, W)) := by
  rw [â† Functor.map_comp, prod_comp, Category.comp_id]
#align category_theory.bifunctor.map_comp_id CategoryTheory.Bifunctor.map_comp_id

@[simp]
theorem diagonal (F : C Ã— D â¥¤ E) (X X' : C) (f : X âŸ¶ X') (Y Y' : D) (g : Y âŸ¶ Y') :
    F.map ((ğŸ™ X, g) : (X, Y) âŸ¶ (X, Y')) â‰« F.map ((f, ğŸ™ Y') : (X, Y') âŸ¶ (X', Y')) =
      F.map ((f, g) : (X, Y) âŸ¶ (X', Y')) := by
  rw [â† Functor.map_comp, prod_comp, Category.id_comp, Category.comp_id]
#align category_theory.bifunctor.diagonal CategoryTheory.Bifunctor.diagonal

@[simp]
theorem diagonal' (F : C Ã— D â¥¤ E) (X X' : C) (f : X âŸ¶ X') (Y Y' : D) (g : Y âŸ¶ Y') :
    F.map ((f, ğŸ™ Y) : (X, Y) âŸ¶ (X', Y)) â‰« F.map ((ğŸ™ X', g) : (X', Y) âŸ¶ (X', Y')) =
      F.map ((f, g) : (X, Y) âŸ¶ (X', Y')) := by
  rw [â† Functor.map_comp, prod_comp, Category.id_comp, Category.comp_id]
#align category_theory.bifunctor.diagonal' CategoryTheory.Bifunctor.diagonal'

end CategoryTheory.Bifunctor

namespace CategoryTheory.Functor

/-- Opposite of a bifunctor.
-/
@[simps!]
def biop (F : Cáµ’áµ– â¥¤ D â¥¤ E) : C â¥¤ Dáµ’áµ– â¥¤ Eáµ’áµ– := F.rightOp â‹™ Functor.opHom _ _

end CategoryTheory.Functor

/- Utilities to apply functors into functor categories. -/
namespace CategoryTheory.Functor

-- We would like to keep these opaque in order to avoid leaking implementation details.

/- Action of two-variable functors on objects. -/
abbrev objâ‚‚ (H : C â¥¤ D â¥¤ E) (A : C) (B : D) : E := (H.obj A).obj B

/- Action of three-variable functors on objects. -/
abbrev objâ‚ƒ (H : C â¥¤ D â¥¤ E â¥¤ F) (A : C) (B : D) (C : E) : F := ((H.obj A).obj B).obj C

variable {Dâ‚ Dâ‚‚ Câ‚ Câ‚‚ Eâ‚ Eâ‚‚}

/- Action of two-variable functors on morphisms. -/
abbrev mapâ‚‚ (H : C â¥¤ D â¥¤ E) (f : Câ‚ âŸ¶ Câ‚‚) (g : Dâ‚ âŸ¶ Dâ‚‚) :
    (H.objâ‚‚ Câ‚ Dâ‚ âŸ¶ H.objâ‚‚ Câ‚‚ Dâ‚‚) :=
  (H.map f).app _ â‰« (H.obj Câ‚‚).map g

/- Action of three-variable functors on morphisms. -/
abbrev mapâ‚ƒ (H : C â¥¤ D â¥¤ E â¥¤ F) (f : Câ‚ âŸ¶ Câ‚‚) (g : Dâ‚ âŸ¶ Dâ‚‚) (h : Eâ‚ âŸ¶ Eâ‚‚) :
    (H.objâ‚ƒ Câ‚ Dâ‚ Eâ‚ âŸ¶ H.objâ‚ƒ Câ‚‚ Dâ‚‚ Eâ‚‚) :=
  (H.mapâ‚‚ f g).app _ â‰« (H.objâ‚‚ Câ‚‚ Dâ‚‚).map h

end CategoryTheory.Functor

/- Natural transformations between functors with many variables. -/
namespace CategoryTheory.NatTrans

/- Apply a natural transformation between bifunctors to two objects. -/
abbrev appâ‚‚ {F G : C â¥¤ D â¥¤ E} (Î± : NatTrans F G) (X : C) (Y : D) :
    F.objâ‚‚ X Y âŸ¶ G.objâ‚‚ X Y :=
  (Î±.app X).app Y

/- Apply a natural transformation between bifunctors in three variables to three objects. -/
abbrev appâ‚ƒ {H G : C â¥¤ D â¥¤ E â¥¤ F} (Î± : NatTrans H G) (X : C) (Y : D) (Z : E) :
    H.objâ‚ƒ X Y Z âŸ¶ G.objâ‚ƒ X Y Z :=
  ((Î±.app X).app Y).app Z

/- Naturality for natural transformations in two variables. -/
@[reassoc (attr := simp)]
lemma naturalityâ‚‚ {H G : C â¥¤ D â¥¤ E} (Î± : NatTrans H G) {X Y X' Y'} (f : X âŸ¶ X')
    (g : Y âŸ¶ Y') : H.mapâ‚‚ f g â‰« Î±.appâ‚‚ X' Y' = Î±.appâ‚‚ X Y â‰« G.mapâ‚‚ f g := by
  rw [Category.assoc, naturality, reassoc_of% naturality_app Î±]

/- Naturality for natural transformations in three variables. -/
@[reassoc (attr := simp)]
lemma naturalityâ‚ƒ {H G : C â¥¤ D â¥¤ E â¥¤ F} (Î± : H âŸ¶ G) {X Y Z X' Y' Z'}
    (f : X âŸ¶ X') (g : Y âŸ¶ Y') (h : Z âŸ¶ Z') :
    H.mapâ‚ƒ f g h â‰« Î±.appâ‚ƒ X' Y' Z' = Î±.appâ‚ƒ X Y Z â‰« G.mapâ‚ƒ f g h := by
  slice_lhs 1 2 => exact Category.assoc _ _ _
  slice_rhs 2 3 => exact Category.assoc _ _ _
  rw [Category.assoc, naturalityâ‚‚]
  slice_lhs 1 2 => exact congrArg (appâ‚‚ Â· Y Z) (Î±.naturality f)
  rw [â† Category.assoc, â† Category.assoc]
  rfl

end NatTrans
