/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stephen Morgan, Scott Morrison, Andrea Laretto
-/
import Mathlib.CategoryTheory.Products.Basic
import Mathlib.Tactic.CategoryTheory.Reassoc
import Mathlib.CategoryTheory.Opposites

/-!
# Lemmas about functors out of product categories.
-/

open CategoryTheory

universe vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ uâ‚ uâ‚‚ uâ‚ƒ uâ‚„

variable {C : Type uâ‚} {D : Type uâ‚‚} {E : Type uâ‚ƒ} {F : Type uâ‚„}

variable [Category.{vâ‚} C] [Category.{vâ‚‚} D] [Category.{vâ‚ƒ} E] [Category.{vâ‚„} F]

namespace CategoryTheory.Bifunctor

@[simp]
theorem map_id (F : C Ã— D â¥¤ E) (X : C) (Y : D) :
    F.map ((ğŸ™ X, ğŸ™ Y) : (X, Y) âŸ¶ (X, Y)) = ğŸ™ (F.obj (X, Y)) :=
  F.map_id (X, Y)

@[simp]
theorem map_id_comp (F : C Ã— D â¥¤ E) (W : C) {X Y Z : D} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    F.map ((ğŸ™ W, f â‰« g) : (W, X) âŸ¶ (W, Z)) =
      F.map ((ğŸ™ W, f) : (W, X) âŸ¶ (W, Y)) â‰« F.map ((ğŸ™ W, g) : (W, Y) âŸ¶ (W, Z)) := by
  rw [â† Functor.map_comp, prod_comp, Category.comp_id]

@[simp]
theorem map_comp_id (F : C Ã— D â¥¤ E) (X Y Z : C) (W : D) (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    F.map ((f â‰« g, ğŸ™ W) : (X, W) âŸ¶ (Z, W)) =
      F.map ((f, ğŸ™ W) : (X, W) âŸ¶ (Y, W)) â‰« F.map ((g, ğŸ™ W) : (Y, W) âŸ¶ (Z, W)) := by
  rw [â† Functor.map_comp, prod_comp, Category.comp_id]

@[simp]
theorem diagonal (F : C Ã— D â¥¤ E) (X X' : C) (f : X âŸ¶ X') (Y Y' : D) (g : Y âŸ¶ Y') :
    F.map ((ğŸ™ X, g) : (X, Y) âŸ¶ (X, Y')) â‰« F.map ((f, ğŸ™ Y') : (X, Y') âŸ¶ (X', Y')) =
      F.map ((f, g) : (X, Y) âŸ¶ (X', Y')) := by
  rw [â† Functor.map_comp, prod_comp, Category.id_comp, Category.comp_id]

@[simp]
theorem diagonal' (F : C Ã— D â¥¤ E) (X X' : C) (f : X âŸ¶ X') (Y Y' : D) (g : Y âŸ¶ Y') :
    F.map ((f, ğŸ™ Y) : (X, Y) âŸ¶ (X', Y)) â‰« F.map ((ğŸ™ X', g) : (X', Y) âŸ¶ (X', Y')) =
      F.map ((f, g) : (X, Y) âŸ¶ (X', Y')) := by
  rw [â† Functor.map_comp, prod_comp, Category.id_comp, Category.comp_id]

end CategoryTheory.Bifunctor

namespace CategoryTheory.Functor

/-- Opposite of a bifunctor.
-/
@[simps!]
def biop (F : Cáµ’áµ– â¥¤ D â¥¤ E) : C â¥¤ Dáµ’áµ– â¥¤ Eáµ’áµ– := F.rightOp â‹™ Functor.opHom _ _

end CategoryTheory.Functor

namespace CategoryTheory

variable {Câ‚ Câ‚‚ : C} {Dâ‚ Dâ‚‚ : D} {Eâ‚ Eâ‚‚ : E}

/-- Action of two-variable functors on objects. -/
abbrev Functor.objâ‚‚ (H : C â¥¤ D â¥¤ E) (A : C) (B : D) : E := (H.obj A).obj B

/-- Action of three-variable functors on objects. -/
abbrev Functor.objâ‚ƒ (H : C â¥¤ D â¥¤ E â¥¤ F) (A : C) (B : D) (C : E) : F :=
  ((H.obj A).obj B).obj C

/-- Apply a natural transformation between bifunctors to two objects. -/
abbrev NatTrans.appâ‚‚ {F G : C â¥¤ D â¥¤ E} (Î± : NatTrans F G) (X : C) (Y : D) :
    F.objâ‚‚ X Y âŸ¶ G.objâ‚‚ X Y :=
  (Î±.app X).app Y

/-- Apply a natural transformation between bifunctors in three variables to three objects. -/
abbrev NatTrans.appâ‚ƒ {H G : C â¥¤ D â¥¤ E â¥¤ F} (Î± : NatTrans H G) (X : C) (Y : D) (Z : E) :
    H.objâ‚ƒ X Y Z âŸ¶ G.objâ‚ƒ X Y Z :=
  ((Î±.app X).app Y).app Z

/- Natural transformations between functors with many variables. -/
namespace NatTrans

@[reassoc, simp]
lemma comp_appâ‚‚ {H G K : C â¥¤ D â¥¤ E} (Î± : H âŸ¶ G) (Î² : G âŸ¶ K) (X : C) (Y : D) :
    (Î± â‰« Î²).appâ‚‚ X Y = Î±.appâ‚‚ X Y â‰« Î².appâ‚‚ X Y := rfl

@[reassoc, simp]
lemma comp_appâ‚ƒ {H G K : C â¥¤ D â¥¤ E â¥¤ F} (Î± : H âŸ¶ G) (Î² : G âŸ¶ K) (X : C) (Y : D)
    (Z : E) : (Î± â‰« Î²).appâ‚ƒ X Y Z = Î±.appâ‚ƒ X Y Z â‰« Î².appâ‚ƒ X Y Z := rfl

@[reassoc (attr := simp)]
lemma naturality_app_app
    {G H : C â¥¤ D â¥¤ E â¥¤ F} (T : NatTrans G H) (Y : D) (Z : E) {X X' : C} (f : X âŸ¶ X') :
    ((G.map f).app Y).app Z â‰« ((T.app X').app Y).app Z =
      ((T.app X).app Y).app Z â‰« ((H.map f).app Y).app Z :=
  congr_app (NatTrans.naturality_app T Y f) Z

end NatTrans

end CategoryTheory
