/-
Copyright (c) 2017 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stephen Morgan, Kim Morrison
-/
import Mathlib.CategoryTheory.Products.Basic

/-!
The associator functor `((C Ã— D) Ã— E) â¥¤ (C Ã— (D Ã— E))` and its inverse form an equivalence.
-/


universe vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ uâ‚ uâ‚‚ uâ‚ƒ uâ‚„

open CategoryTheory

namespace CategoryTheory.prod

variable (C : Type uâ‚) [Category.{vâ‚} C] (D : Type uâ‚‚) [Category.{vâ‚‚} D] (E : Type uâ‚ƒ)
  [Category.{vâ‚ƒ} E]

/-- The associator functor `(C Ã— D) Ã— E â¥¤ C Ã— (D Ã— E)`.
-/
@[simps]
def associator : (C Ã— D) Ã— E â¥¤ C Ã— D Ã— E where
  obj X := (X.1.1, (X.1.2, X.2))
  map := @fun _ _ f => (f.1.1, (f.1.2, f.2))

/-- The inverse associator functor `C Ã— (D Ã— E) â¥¤ (C Ã— D) Ã— E `.
-/
@[simps]
def inverseAssociator : C Ã— D Ã— E â¥¤ (C Ã— D) Ã— E where
  obj X := ((X.1, X.2.1), X.2.2)
  map := @fun _ _ f => ((f.1, f.2.1), f.2.2)

/-- The equivalence of categories expressing associativity of products of categories.
-/
@[simps]
def associativity : (C Ã— D) Ã— E â‰Œ C Ã— D Ã— E where
  functor := associator C D E
  inverse := inverseAssociator C D E
  unitIso := NatIso.ofComponents fun _ => Iso.refl _
  counitIso := NatIso.ofComponents fun _ => Iso.refl _

instance associatorIsEquivalence : (associator C D E).IsEquivalence :=
  (by infer_instance : (associativity C D E).functor.IsEquivalence)

instance inverseAssociatorIsEquivalence : (inverseAssociator C D E).IsEquivalence :=
  (by infer_instance : (associativity C D E).inverse.IsEquivalence)

-- TODO pentagon natural transformation? ...satisfying?

variable (A : Type uâ‚„) [Category.{vâ‚„} A]

/-- The associator isomorphism is compatible with `prodFunctorToFunctorProd`. -/
@[simps!]
def prodFunctorToFunctorProdAssociator :
    (associativity _ _ _).functor â‹™ ((ðŸ­ _).prod (prodFunctorToFunctorProd A D E) â‹™
      (prodFunctorToFunctorProd A C (D Ã— E))) â‰…
        (prodFunctorToFunctorProd A C D).prod (ðŸ­ _) â‹™ (prodFunctorToFunctorProd A (C Ã— D) E) â‹™
          (associativity C D E).congrRight.functor :=
  NatIso.ofComponents (fun _ => NatIso.ofComponents fun _ => Iso.refl _)

/-- The associator isomorphism is compatible with `functorProdToProdFunctor`. -/
@[simps!]
def functorProdToProdFunctorAssociator :
    (associativity _ _ _).congrRight.functor â‹™ functorProdToProdFunctor A C (D Ã— E) â‹™
      (ðŸ­ _).prod (functorProdToProdFunctor A D E) â‰…
        functorProdToProdFunctor A (C Ã— D) E â‹™ (functorProdToProdFunctor A C D).prod (ðŸ­ _) â‹™
          (associativity _ _ _).functor :=
  NatIso.ofComponents (fun F => Iso.prod
    (NatIso.ofComponents fun _ => Iso.refl _)
    (Iso.prod (NatIso.ofComponents fun _ => Iso.refl _) (NatIso.ofComponents fun _ => Iso.refl _)))

end CategoryTheory.prod
