/-
Copyright (c) 2022 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Limits.Shapes.Biproducts
import Mathlib.GroupTheory.EckmannHilton
import Mathlib.Tactic.CategoryTheory.Reassoc

#align_import category_theory.preadditive.of_biproducts from "leanprover-community/mathlib"@"061ea99a5610cfc72c286aa930d3c1f47f74f3d0"
/-!
# Constructing a semiadditive structure from binary biproducts

We show that any category with zero morphisms and binary biproducts is enriched over the category
of commutative monoids.

-/


noncomputable section

universe v u

open CategoryTheory

open CategoryTheory.Limits

namespace CategoryTheory.SemiadditiveOfBinaryBiproducts

variable {C : Type u} [Category.{v} C] [HasZeroMorphisms C] [HasBinaryBiproducts C]

section

variable (X Y : C)

/-- `f +â‚— g` is the composite `X âŸ¶ Y âŠ Y âŸ¶ Y`, where the first map is `(f, g)` and the second map
    is `(ğŸ™ ğŸ™)`. -/
@[simp]
def leftAdd (f g : X âŸ¶ Y) : X âŸ¶ Y :=
  biprod.lift f g â‰« biprod.desc (ğŸ™ Y) (ğŸ™ Y)
#align category_theory.semiadditive_of_binary_biproducts.left_add CategoryTheory.SemiadditiveOfBinaryBiproducts.leftAdd

/-- `f +áµ£ g` is the composite `X âŸ¶ X âŠ X âŸ¶ Y`, where the first map is `(ğŸ™, ğŸ™)` and the second map
    is `(f g)`. -/
@[simp]
def rightAdd (f g : X âŸ¶ Y) : X âŸ¶ Y :=
  biprod.lift (ğŸ™ X) (ğŸ™ X) â‰« biprod.desc f g
#align category_theory.semiadditive_of_binary_biproducts.right_add CategoryTheory.SemiadditiveOfBinaryBiproducts.rightAdd

local infixr:65 " +â‚— " => leftAdd X Y

local infixr:65 " +áµ£ " => rightAdd X Y

theorem isUnital_leftAdd : EckmannHilton.IsUnital (Â· +â‚— Â·) 0 := by
  have hr : âˆ€ f : X âŸ¶ Y, biprod.lift (0 : X âŸ¶ Y) f = f â‰« biprod.inr := by
    intro f
    ext
    Â· aesop_cat
    Â· simp [biprod.lift_fst, Category.assoc, biprod.inr_fst, comp_zero]
  have hl : âˆ€ f : X âŸ¶ Y, biprod.lift f (0 : X âŸ¶ Y) = f â‰« biprod.inl := by
    intro f
    ext
    Â· aesop_cat
    Â· simp [biprod.lift_snd, Category.assoc, biprod.inl_snd, comp_zero]
  exact {
    left_id := fun f => by simp [hr f, leftAdd, Category.assoc, Category.comp_id, biprod.inr_desc],
    right_id := fun f => by simp [hl f, leftAdd, Category.assoc, Category.comp_id, biprod.inl_desc]
  }
#align category_theory.semiadditive_of_binary_biproducts.is_unital_left_add CategoryTheory.SemiadditiveOfBinaryBiproducts.isUnital_leftAdd

theorem isUnital_rightAdd : EckmannHilton.IsUnital (Â· +áµ£ Â·) 0 := by
  have hâ‚‚ : âˆ€ f : X âŸ¶ Y, biprod.desc (0 : X âŸ¶ Y) f = biprod.snd â‰« f := by
    intro f
    ext
    Â· aesop_cat
    Â· simp only [biprod.inr_desc, BinaryBicone.inr_snd_assoc]
  have hâ‚ : âˆ€ f : X âŸ¶ Y, biprod.desc f (0 : X âŸ¶ Y) = biprod.fst â‰« f := by
    intro f
    ext
    Â· aesop_cat
    Â· simp only [biprod.inr_desc, BinaryBicone.inr_fst_assoc, zero_comp]
  exact {
    left_id := fun f => by simp [hâ‚‚ f, rightAdd, biprod.lift_snd_assoc, Category.id_comp],
    right_id := fun f => by simp [hâ‚ f, rightAdd, biprod.lift_fst_assoc, Category.id_comp]
  }
#align category_theory.semiadditive_of_binary_biproducts.is_unital_right_add CategoryTheory.SemiadditiveOfBinaryBiproducts.isUnital_rightAdd

theorem distrib (f g h k : X âŸ¶ Y) : (f +áµ£ g) +â‚— h +áµ£ k = (f +â‚— h) +áµ£ g +â‚— k := by
  let diag : X âŠ X âŸ¶ Y âŠ Y := biprod.lift (biprod.desc f g) (biprod.desc h k)
  have hdâ‚ : biprod.inl â‰« diag = biprod.lift f h := by ext <;> simp [diag]
  have hdâ‚‚ : biprod.inr â‰« diag = biprod.lift g k := by ext <;> simp [diag]
  have hâ‚ : biprod.lift (f +áµ£ g) (h +áµ£ k) = biprod.lift (ğŸ™ X) (ğŸ™ X) â‰« diag := by
    ext <;> aesop_cat
  have hâ‚‚ : diag â‰« biprod.desc (ğŸ™ Y) (ğŸ™ Y) = biprod.desc (f +â‚— h) (g +â‚— k) := by
    ext <;> simp [reassoc_of% hdâ‚, reassoc_of% hdâ‚‚]
  rw [leftAdd, hâ‚, Category.assoc, hâ‚‚, rightAdd]
#align category_theory.semiadditive_of_binary_biproducts.distrib CategoryTheory.SemiadditiveOfBinaryBiproducts.distrib

/-- In a category with binary biproducts, the morphisms form a commutative monoid. -/
def addCommMonoidHomOfHasBinaryBiproducts : AddCommMonoid (X âŸ¶ Y) where
  add := (Â· +áµ£ Â·)
  add_assoc :=
    (EckmannHilton.mul_assoc (isUnital_leftAdd X Y) (isUnital_rightAdd X Y) (distrib X Y)).assoc
  zero := 0
  zero_add := (isUnital_rightAdd X Y).left_id
  add_zero := (isUnital_rightAdd X Y).right_id
  add_comm :=
    (EckmannHilton.mul_comm (isUnital_leftAdd X Y) (isUnital_rightAdd X Y) (distrib X Y)).comm
  nsmul := letI : Add (X âŸ¶ Y) := âŸ¨(Â· +áµ£ Â·)âŸ©; nsmulRec
#align category_theory.semiadditive_of_binary_biproducts.add_comm_monoid_hom_of_has_binary_biproducts CategoryTheory.SemiadditiveOfBinaryBiproducts.addCommMonoidHomOfHasBinaryBiproducts

end

section

variable {X Y Z : C}

attribute [local instance] addCommMonoidHomOfHasBinaryBiproducts

theorem add_eq_right_addition (f g : X âŸ¶ Y) : f + g = biprod.lift (ğŸ™ X) (ğŸ™ X) â‰« biprod.desc f g :=
  rfl
#align category_theory.semiadditive_of_binary_biproducts.add_eq_right_addition CategoryTheory.SemiadditiveOfBinaryBiproducts.add_eq_right_addition

theorem add_eq_left_addition (f g : X âŸ¶ Y) : f + g = biprod.lift f g â‰« biprod.desc (ğŸ™ Y) (ğŸ™ Y) :=
  congr_funâ‚‚ (EckmannHilton.mul (isUnital_leftAdd X Y) (isUnital_rightAdd X Y) (distrib X Y)).symm f
    g
#align category_theory.semiadditive_of_binary_biproducts.add_eq_left_addition CategoryTheory.SemiadditiveOfBinaryBiproducts.add_eq_left_addition

theorem add_comp (f g : X âŸ¶ Y) (h : Y âŸ¶ Z) : (f + g) â‰« h = f â‰« h + g â‰« h := by
  simp only [add_eq_right_addition, Category.assoc]
  congr
  ext <;> simp
#align category_theory.semiadditive_of_binary_biproducts.add_comp CategoryTheory.SemiadditiveOfBinaryBiproducts.add_comp

theorem comp_add (f : X âŸ¶ Y) (g h : Y âŸ¶ Z) : f â‰« (g + h) = f â‰« g + f â‰« h := by
  simp only [add_eq_left_addition, â† Category.assoc]
  congr
  ext <;> simp
#align category_theory.semiadditive_of_binary_biproducts.comp_add CategoryTheory.SemiadditiveOfBinaryBiproducts.comp_add

end

end CategoryTheory.SemiadditiveOfBinaryBiproducts
