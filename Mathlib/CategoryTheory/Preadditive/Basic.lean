/-
Copyright (c) 2020 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel, Jakob von Raumer
-/
import Mathlib.Algebra.BigOperators.Basic
import Mathlib.Algebra.Hom.Group
import Mathlib.Algebra.Module.Basic
import Mathlib.CategoryTheory.Endomorphism
import Mathlib.CategoryTheory.Limits.Shapes.Kernels

#align_import category_theory.preadditive.basic from "leanprover-community/mathlib"@"829895f162a1f29d0133f4b3538f4cd1fb5bffd3"

/-!
# Preadditive categories

A preadditive category is a category in which `X ‚ü∂ Y` is an abelian group in such a way that
composition of morphisms is linear in both variables.

This file contains a definition of preadditive category that directly encodes the definition given
above. The definition could also be phrased as follows: A preadditive category is a category
enriched over the category of Abelian groups. Once the general framework to state this in Lean is
available, the contents of this file should become obsolete.

## Main results

* Definition of preadditive categories and basic properties
* In a preadditive category, `f : Q ‚ü∂ R` is mono if and only if `g ‚â´ f = 0 ‚Üí g = 0` for all
  composable `g`.
* A preadditive category with kernels has equalizers.

## Implementation notes

The simp normal form for negation and composition is to push negations as far as possible to
the outside. For example, `f ‚â´ (-g)` and `(-f) ‚â´ g` both become `-(f ‚â´ g)`, and `(-f) ‚â´ (-g)`
is simplified to `f ‚â´ g`.

## References

* [F. Borceux, *Handbook of Categorical Algebra 2*][borceux-vol2]

## Tags

additive, preadditive, Hom group, Ab-category, Ab-enriched
-/


universe v u

open CategoryTheory.Limits

open BigOperators

namespace CategoryTheory

variable (C : Type u) [Category.{v} C]

/-- A category is called preadditive if `P ‚ü∂ Q` is an abelian group such that composition is
    linear in both variables. -/
class Preadditive where
  homGroup : ‚àÄ P Q : C, AddCommGroup (P ‚ü∂ Q) := by infer_instance
  add_comp : ‚àÄ (P Q R : C) (f f' : P ‚ü∂ Q) (g : Q ‚ü∂ R), (f + f') ‚â´ g = f ‚â´ g + f' ‚â´ g := by
    aesop_cat
  comp_add : ‚àÄ (P Q R : C) (f : P ‚ü∂ Q) (g g' : Q ‚ü∂ R), f ‚â´ (g + g') = f ‚â´ g + f ‚â´ g' := by
    aesop_cat
#align category_theory.preadditive CategoryTheory.Preadditive
#align category_theory.preadditive.add_comp' CategoryTheory.Preadditive.add_comp
#align category_theory.preadditive.comp_add' CategoryTheory.Preadditive.comp_add

attribute [inherit_doc Preadditive] Preadditive.homGroup Preadditive.add_comp Preadditive.comp_add

attribute [instance] Preadditive.homGroup

-- Porting note: simp can prove reassoc version
attribute [reassoc, simp] Preadditive.add_comp

attribute [reassoc] Preadditive.comp_add

-- (the linter doesn't like `simp` on this lemma)
attribute [simp] Preadditive.comp_add

end CategoryTheory

open CategoryTheory

namespace CategoryTheory

namespace Preadditive

section Preadditive

open AddMonoidHom

variable {C : Type u} [Category.{v} C] [Preadditive C]

section InducedCategory

universe u'

variable {D : Type u'} (F : D ‚Üí C)

instance inducedCategory : Preadditive.{v} (InducedCategory C F) where
  homGroup P Q := @Preadditive.homGroup C _ _ (F P) (F Q)
  add_comp _ _ _ _ _ _ := add_comp _ _ _ _ _ _
  comp_add _ _ _ _ _ _ := comp_add _ _ _ _ _ _
#align category_theory.preadditive.induced_category CategoryTheory.Preadditive.inducedCategory

end InducedCategory

instance fullSubcategory (Z : C ‚Üí Prop) : Preadditive.{v} (FullSubcategory Z) where
  homGroup P Q := @Preadditive.homGroup C _ _ P.obj Q.obj
  add_comp _ _ _ _ _ _ := add_comp _ _ _ _ _ _
  comp_add _ _ _ _ _ _ := comp_add _ _ _ _ _ _
#align category_theory.preadditive.full_subcategory CategoryTheory.Preadditive.fullSubcategory

instance (X : C) : AddCommGroup (End X) := by
  dsimp [End]
  -- ‚ä¢ AddCommGroup (X ‚ü∂ X)
  infer_instance
  -- üéâ no goals

/-- Composition by a fixed left argument as a group homomorphism -/
def leftComp {P Q : C} (R : C) (f : P ‚ü∂ Q) : (Q ‚ü∂ R) ‚Üí+ (P ‚ü∂ R) :=
  mk' (fun g => f ‚â´ g) fun g g' => by simp
                                      -- üéâ no goals
#align category_theory.preadditive.left_comp CategoryTheory.Preadditive.leftComp

/-- Composition by a fixed right argument as a group homomorphism -/
def rightComp (P : C) {Q R : C} (g : Q ‚ü∂ R) : (P ‚ü∂ Q) ‚Üí+ (P ‚ü∂ R) :=
  mk' (fun f => f ‚â´ g) fun f f' => by simp
                                      -- üéâ no goals
#align category_theory.preadditive.right_comp CategoryTheory.Preadditive.rightComp

variable {P Q R : C} (f f' : P ‚ü∂ Q) (g g' : Q ‚ü∂ R)

/-- Composition as a bilinear group homomorphism -/
def compHom : (P ‚ü∂ Q) ‚Üí+ (Q ‚ü∂ R) ‚Üí+ (P ‚ü∂ R) :=
  AddMonoidHom.mk' (fun f => leftComp _ f) fun f‚ÇÅ f‚ÇÇ =>
    AddMonoidHom.ext fun g => (rightComp _ g).map_add f‚ÇÅ f‚ÇÇ
#align category_theory.preadditive.comp_hom CategoryTheory.Preadditive.compHom

-- Porting note: simp can prove the reassoc version
@[reassoc, simp]
theorem sub_comp : (f - f') ‚â´ g = f ‚â´ g - f' ‚â´ g :=
  map_sub (rightComp P g) f f'
#align category_theory.preadditive.sub_comp CategoryTheory.Preadditive.sub_comp

-- The redundant simp lemma linter says that simp can prove the reassoc version of this lemma.
@[reassoc, simp]
theorem comp_sub : f ‚â´ (g - g') = f ‚â´ g - f ‚â´ g' :=
  map_sub (leftComp R f) g g'
#align category_theory.preadditive.comp_sub CategoryTheory.Preadditive.comp_sub

-- Porting note: simp can prove the reassoc version
@[reassoc, simp]
theorem neg_comp : (-f) ‚â´ g = -f ‚â´ g :=
  map_neg (rightComp P g) f
#align category_theory.preadditive.neg_comp CategoryTheory.Preadditive.neg_comp

-- The redundant simp lemma linter says that simp can prove the reassoc version of this lemma.
@[reassoc, simp]
theorem comp_neg : f ‚â´ (-g) = -f ‚â´ g :=
  map_neg (leftComp R f) g
#align category_theory.preadditive.comp_neg CategoryTheory.Preadditive.comp_neg

@[reassoc]
theorem neg_comp_neg : (-f) ‚â´ (-g) = f ‚â´ g := by simp
                                                 -- üéâ no goals
#align category_theory.preadditive.neg_comp_neg CategoryTheory.Preadditive.neg_comp_neg

theorem nsmul_comp (n : ‚Ñï) : (n ‚Ä¢ f) ‚â´ g = n ‚Ä¢ f ‚â´ g :=
  map_nsmul (rightComp P g) n f
#align category_theory.preadditive.nsmul_comp CategoryTheory.Preadditive.nsmul_comp

theorem comp_nsmul (n : ‚Ñï) : f ‚â´ (n ‚Ä¢ g) = n ‚Ä¢ f ‚â´ g :=
  map_nsmul (leftComp R f) n g
#align category_theory.preadditive.comp_nsmul CategoryTheory.Preadditive.comp_nsmul

theorem zsmul_comp (n : ‚Ñ§) : (n ‚Ä¢ f) ‚â´ g = n ‚Ä¢ f ‚â´ g :=
  map_zsmul (rightComp P g) n f
#align category_theory.preadditive.zsmul_comp CategoryTheory.Preadditive.zsmul_comp

theorem comp_zsmul (n : ‚Ñ§) : f ‚â´ (n ‚Ä¢ g) = n ‚Ä¢ f ‚â´ g :=
  map_zsmul (leftComp R f) n g
#align category_theory.preadditive.comp_zsmul CategoryTheory.Preadditive.comp_zsmul

@[reassoc]
theorem comp_sum {P Q R : C} {J : Type*} (s : Finset J) (f : P ‚ü∂ Q) (g : J ‚Üí (Q ‚ü∂ R)) :
    (f ‚â´ ‚àë j in s, g j) = ‚àë j in s, f ‚â´ g j :=
  map_sum (leftComp R f) _ _
#align category_theory.preadditive.comp_sum CategoryTheory.Preadditive.comp_sum

@[reassoc]
theorem sum_comp {P Q R : C} {J : Type*} (s : Finset J) (f : J ‚Üí (P ‚ü∂ Q)) (g : Q ‚ü∂ R) :
    (‚àë j in s, f j) ‚â´ g = ‚àë j in s, f j ‚â´ g :=
  map_sum (rightComp P g) _ _
#align category_theory.preadditive.sum_comp CategoryTheory.Preadditive.sum_comp

instance {P Q : C} {f : P ‚ü∂ Q} [Epi f] : Epi (-f) :=
  ‚ü®fun g g' H => by rwa [neg_comp, neg_comp, ‚Üê comp_neg, ‚Üê comp_neg, cancel_epi, neg_inj] at H‚ü©
                    -- üéâ no goals

instance {P Q : C} {f : P ‚ü∂ Q} [Mono f] : Mono (-f) :=
  ‚ü®fun g g' H => by rwa [comp_neg, comp_neg, ‚Üê neg_comp, ‚Üê neg_comp, cancel_mono, neg_inj] at H‚ü©
                    -- üéâ no goals

instance (priority := 100) preadditiveHasZeroMorphisms : HasZeroMorphisms C where
  Zero := inferInstance
  comp_zero f R := show leftComp R f 0 = 0 from map_zero _
  zero_comp P _ _ f := show rightComp P f 0 = 0 from map_zero _
#align category_theory.preadditive.preadditive_has_zero_morphisms CategoryTheory.Preadditive.preadditiveHasZeroMorphisms

/--Porting note: adding this before the ring instance allowed moduleEndRight to find
the correct Monoid structure on End. Moved both down after preadditiveHasZeroMorphisms
to make use of them -/
instance {X : C} : Semiring (End X) :=
  { End.monoid with
    zero_mul := fun f => by dsimp [mul]; exact HasZeroMorphisms.comp_zero f _
                            -- ‚ä¢ f ‚â´ 0 = 0
                                         -- üéâ no goals
    mul_zero := fun f => by dsimp [mul]; exact HasZeroMorphisms.zero_comp _ f
                            -- ‚ä¢ 0 ‚â´ f = 0
                                         -- üéâ no goals
    left_distrib := fun f g h => Preadditive.add_comp X X X g h f
    right_distrib := fun f g h => Preadditive.comp_add X X X h f g }

/-- Porting note: It looks like Ring's parent classes changed in
Lean 4 so the previous instance needed modification. Was following my nose here. -/
instance {X : C} : Ring (End X) :=
  { (inferInstance : Semiring (End X)),
    (inferInstance : AddCommGroup (End X)) with
    add_left_neg := add_left_neg }

instance moduleEndRight {X Y : C} : Module (End Y) (X ‚ü∂ Y) where
  smul_add _ _ _ := add_comp _ _ _ _ _ _
  smul_zero _ := zero_comp
  add_smul _ _ _ := comp_add _ _ _ _ _ _
  zero_smul _ := comp_zero
#align category_theory.preadditive.module_End_right CategoryTheory.Preadditive.moduleEndRight

theorem mono_of_cancel_zero {Q R : C} (f : Q ‚ü∂ R) (h : ‚àÄ {P : C} (g : P ‚ü∂ Q), g ‚â´ f = 0 ‚Üí g = 0) :
    Mono f where
  right_cancellation := fun {Z} g‚ÇÅ g‚ÇÇ hg =>
    sub_eq_zero.1 <| h _ <| (map_sub (rightComp Z f) g‚ÇÅ g‚ÇÇ).trans <| sub_eq_zero.2 hg
#align category_theory.preadditive.mono_of_cancel_zero CategoryTheory.Preadditive.mono_of_cancel_zero

theorem mono_iff_cancel_zero {Q R : C} (f : Q ‚ü∂ R) :
    Mono f ‚Üî ‚àÄ (P : C) (g : P ‚ü∂ Q), g ‚â´ f = 0 ‚Üí g = 0 :=
  ‚ü®fun _ _ _ => zero_of_comp_mono _, mono_of_cancel_zero f‚ü©
#align category_theory.preadditive.mono_iff_cancel_zero CategoryTheory.Preadditive.mono_iff_cancel_zero

theorem mono_of_kernel_zero {X Y : C} {f : X ‚ü∂ Y} [HasLimit (parallelPair f 0)]
    (w : kernel.Œπ f = 0) : Mono f :=
  mono_of_cancel_zero f fun g h => by rw [‚Üê kernel.lift_Œπ f g h, w, Limits.comp_zero]
                                      -- üéâ no goals
#align category_theory.preadditive.mono_of_kernel_zero CategoryTheory.Preadditive.mono_of_kernel_zero

theorem epi_of_cancel_zero {P Q : C} (f : P ‚ü∂ Q) (h : ‚àÄ {R : C} (g : Q ‚ü∂ R), f ‚â´ g = 0 ‚Üí g = 0) :
    Epi f :=
  ‚ü®fun {Z} g g' hg =>
    sub_eq_zero.1 <| h _ <| (map_sub (leftComp Z f) g g').trans <| sub_eq_zero.2 hg‚ü©
#align category_theory.preadditive.epi_of_cancel_zero CategoryTheory.Preadditive.epi_of_cancel_zero

theorem epi_iff_cancel_zero {P Q : C} (f : P ‚ü∂ Q) :
    Epi f ‚Üî ‚àÄ (R : C) (g : Q ‚ü∂ R), f ‚â´ g = 0 ‚Üí g = 0 :=
  ‚ü®fun _ _ _ => zero_of_epi_comp _, epi_of_cancel_zero f‚ü©
#align category_theory.preadditive.epi_iff_cancel_zero CategoryTheory.Preadditive.epi_iff_cancel_zero

theorem epi_of_cokernel_zero {X Y : C} {f : X ‚ü∂ Y} [HasColimit (parallelPair f 0)]
    (w : cokernel.œÄ f = 0) : Epi f :=
  epi_of_cancel_zero f fun g h => by rw [‚Üê cokernel.œÄ_desc f g h, w, Limits.zero_comp]
                                     -- üéâ no goals
#align category_theory.preadditive.epi_of_cokernel_zero CategoryTheory.Preadditive.epi_of_cokernel_zero

namespace IsIso

@[simp]
theorem comp_left_eq_zero [IsIso f] : f ‚â´ g = 0 ‚Üî g = 0 := by
  rw [‚Üê IsIso.eq_inv_comp, Limits.comp_zero]
  -- üéâ no goals
#align category_theory.preadditive.is_iso.comp_left_eq_zero CategoryTheory.Preadditive.IsIso.comp_left_eq_zero

@[simp]
theorem comp_right_eq_zero [IsIso g] : f ‚â´ g = 0 ‚Üî f = 0 := by
  rw [‚Üê IsIso.eq_comp_inv, Limits.zero_comp]
  -- üéâ no goals
#align category_theory.preadditive.is_iso.comp_right_eq_zero CategoryTheory.Preadditive.IsIso.comp_right_eq_zero

end IsIso

open ZeroObject

variable [HasZeroObject C]

theorem mono_of_kernel_iso_zero {X Y : C} {f : X ‚ü∂ Y} [HasLimit (parallelPair f 0)]
    (w : kernel f ‚âÖ 0) : Mono f :=
  mono_of_kernel_zero (zero_of_source_iso_zero _ w)
#align category_theory.preadditive.mono_of_kernel_iso_zero CategoryTheory.Preadditive.mono_of_kernel_iso_zero

theorem epi_of_cokernel_iso_zero {X Y : C} {f : X ‚ü∂ Y} [HasColimit (parallelPair f 0)]
    (w : cokernel f ‚âÖ 0) : Epi f :=
  epi_of_cokernel_zero (zero_of_target_iso_zero _ w)
#align category_theory.preadditive.epi_of_cokernel_iso_zero CategoryTheory.Preadditive.epi_of_cokernel_iso_zero

end Preadditive

section Equalizers

variable {C : Type u} [Category.{v} C] [Preadditive C]

section

variable {X Y : C} {f : X ‚ü∂ Y} {g : X ‚ü∂ Y}

/-- Map a kernel cone on the difference of two morphisms to the equalizer fork. -/
@[simps! pt]
def forkOfKernelFork (c : KernelFork (f - g)) : Fork f g :=
  Fork.ofŒπ c.Œπ <| by rw [‚Üê sub_eq_zero, ‚Üê comp_sub, c.condition]
                     -- üéâ no goals
#align category_theory.preadditive.fork_of_kernel_fork CategoryTheory.Preadditive.forkOfKernelFork

@[simp]
theorem forkOfKernelFork_Œπ (c : KernelFork (f - g)) : (forkOfKernelFork c).Œπ = c.Œπ :=
  rfl
#align category_theory.preadditive.fork_of_kernel_fork_Œπ CategoryTheory.Preadditive.forkOfKernelFork_Œπ

/-- Map any equalizer fork to a cone on the difference of the two morphisms. -/
def kernelForkOfFork (c : Fork f g) : KernelFork (f - g) :=
  Fork.ofŒπ c.Œπ <| by rw [comp_sub, comp_zero, sub_eq_zero, c.condition]
                     -- üéâ no goals
#align category_theory.preadditive.kernel_fork_of_fork CategoryTheory.Preadditive.kernelForkOfFork

@[simp]
theorem kernelForkOfFork_Œπ (c : Fork f g) : (kernelForkOfFork c).Œπ = c.Œπ :=
  rfl
#align category_theory.preadditive.kernel_fork_of_fork_Œπ CategoryTheory.Preadditive.kernelForkOfFork_Œπ

@[simp]
theorem kernelForkOfFork_ofŒπ {P : C} (Œπ : P ‚ü∂ X) (w : Œπ ‚â´ f = Œπ ‚â´ g) :
    kernelForkOfFork (Fork.ofŒπ Œπ w) = KernelFork.ofŒπ Œπ (by simp [w]) :=
                                                           -- üéâ no goals
  rfl
#align category_theory.preadditive.kernel_fork_of_fork_of_Œπ CategoryTheory.Preadditive.kernelForkOfFork_ofŒπ

/-- A kernel of `f - g` is an equalizer of `f` and `g`. -/
def isLimitForkOfKernelFork {c : KernelFork (f - g)} (i : IsLimit c) :
    IsLimit (forkOfKernelFork c) :=
  Fork.IsLimit.mk' _ fun s =>
    ‚ü®i.lift (kernelForkOfFork s), i.fac _ _, fun h => by apply Fork.IsLimit.hom_ext i; aesop_cat‚ü©
                                                         -- ‚ä¢ m‚úù ‚â´ Fork.Œπ c = IsLimit.lift i (kernelForkOfFork s) ‚â´ Fork.Œπ c
                                                                                       -- üéâ no goals
#align category_theory.preadditive.is_limit_fork_of_kernel_fork CategoryTheory.Preadditive.isLimitForkOfKernelFork

@[simp]
theorem isLimitForkOfKernelFork_lift {c : KernelFork (f - g)} (i : IsLimit c) (s : Fork f g) :
    (isLimitForkOfKernelFork i).lift s = i.lift (kernelForkOfFork s) :=
  rfl
#align category_theory.preadditive.is_limit_fork_of_kernel_fork_lift CategoryTheory.Preadditive.isLimitForkOfKernelFork_lift

/-- An equalizer of `f` and `g` is a kernel of `f - g`. -/
def isLimitKernelForkOfFork {c : Fork f g} (i : IsLimit c) : IsLimit (kernelForkOfFork c) :=
  Fork.IsLimit.mk' _ fun s =>
    ‚ü®i.lift (forkOfKernelFork s), i.fac _ _, fun h => by apply Fork.IsLimit.hom_ext i; aesop_cat‚ü©
                                                         -- ‚ä¢ m‚úù ‚â´ Fork.Œπ c = IsLimit.lift i (forkOfKernelFork s) ‚â´ Fork.Œπ c
                                                                                       -- üéâ no goals
#align category_theory.preadditive.is_limit_kernel_fork_of_fork CategoryTheory.Preadditive.isLimitKernelForkOfFork

variable (f g)

/-- A preadditive category has an equalizer for `f` and `g` if it has a kernel for `f - g`. -/
theorem hasEqualizer_of_hasKernel [HasKernel (f - g)] : HasEqualizer f g :=
  HasLimit.mk
    { cone := forkOfKernelFork _
      isLimit := isLimitForkOfKernelFork (equalizerIsEqualizer (f - g) 0) }
#align category_theory.preadditive.has_equalizer_of_has_kernel CategoryTheory.Preadditive.hasEqualizer_of_hasKernel

/-- A preadditive category has a kernel for `f - g` if it has an equalizer for `f` and `g`. -/
theorem hasKernel_of_hasEqualizer [HasEqualizer f g] : HasKernel (f - g) :=
  HasLimit.mk
    { cone := kernelForkOfFork (equalizer.fork f g)
      isLimit := isLimitKernelForkOfFork (limit.isLimit (parallelPair f g)) }
#align category_theory.preadditive.has_kernel_of_has_equalizer CategoryTheory.Preadditive.hasKernel_of_hasEqualizer

variable {f g}

/-- Map a cokernel cocone on the difference of two morphisms to the coequalizer cofork. -/
@[simps! pt]
def coforkOfCokernelCofork (c : CokernelCofork (f - g)) : Cofork f g :=
  Cofork.ofœÄ c.œÄ <| by rw [‚Üê sub_eq_zero, ‚Üê sub_comp, c.condition]
                       -- üéâ no goals
#align category_theory.preadditive.cofork_of_cokernel_cofork CategoryTheory.Preadditive.coforkOfCokernelCofork

@[simp]
theorem coforkOfCokernelCofork_œÄ (c : CokernelCofork (f - g)) :
    (coforkOfCokernelCofork c).œÄ = c.œÄ :=
  rfl
#align category_theory.preadditive.cofork_of_cokernel_cofork_œÄ CategoryTheory.Preadditive.coforkOfCokernelCofork_œÄ

/-- Map any coequalizer cofork to a cocone on the difference of the two morphisms. -/
def cokernelCoforkOfCofork (c : Cofork f g) : CokernelCofork (f - g) :=
  Cofork.ofœÄ c.œÄ <| by rw [sub_comp, zero_comp, sub_eq_zero, c.condition]
                       -- üéâ no goals
#align category_theory.preadditive.cokernel_cofork_of_cofork CategoryTheory.Preadditive.cokernelCoforkOfCofork

@[simp]
theorem cokernelCoforkOfCofork_œÄ (c : Cofork f g) : (cokernelCoforkOfCofork c).œÄ = c.œÄ :=
  rfl
#align category_theory.preadditive.cokernel_cofork_of_cofork_œÄ CategoryTheory.Preadditive.cokernelCoforkOfCofork_œÄ

@[simp]
theorem cokernelCoforkOfCofork_ofœÄ {P : C} (œÄ : Y ‚ü∂ P) (w : f ‚â´ œÄ = g ‚â´ œÄ) :
    cokernelCoforkOfCofork (Cofork.ofœÄ œÄ w) = CokernelCofork.ofœÄ œÄ (by simp [w]) :=
                                                                       -- üéâ no goals
  rfl
#align category_theory.preadditive.cokernel_cofork_of_cofork_of_œÄ CategoryTheory.Preadditive.cokernelCoforkOfCofork_ofœÄ

/-- A cokernel of `f - g` is a coequalizer of `f` and `g`. -/
def isColimitCoforkOfCokernelCofork {c : CokernelCofork (f - g)} (i : IsColimit c) :
    IsColimit (coforkOfCokernelCofork c) :=
  Cofork.IsColimit.mk' _ fun s =>
    ‚ü®i.desc (cokernelCoforkOfCofork s), i.fac _ _, fun h => by
      apply Cofork.IsColimit.hom_ext i; aesop_cat‚ü©
      -- ‚ä¢ Cofork.œÄ c ‚â´ m‚úù = Cofork.œÄ c ‚â´ IsColimit.desc i (cokernelCoforkOfCofork s)
                                        -- üéâ no goals
#align category_theory.preadditive.is_colimit_cofork_of_cokernel_cofork CategoryTheory.Preadditive.isColimitCoforkOfCokernelCofork

@[simp]
theorem isColimitCoforkOfCokernelCofork_desc {c : CokernelCofork (f - g)} (i : IsColimit c)
    (s : Cofork f g) :
    (isColimitCoforkOfCokernelCofork i).desc s = i.desc (cokernelCoforkOfCofork s) :=
  rfl
#align category_theory.preadditive.is_colimit_cofork_of_cokernel_cofork_desc CategoryTheory.Preadditive.isColimitCoforkOfCokernelCofork_desc

/-- A coequalizer of `f` and `g` is a cokernel of `f - g`. -/
def isColimitCokernelCoforkOfCofork {c : Cofork f g} (i : IsColimit c) :
    IsColimit (cokernelCoforkOfCofork c) :=
  Cofork.IsColimit.mk' _ fun s =>
    ‚ü®i.desc (coforkOfCokernelCofork s), i.fac _ _, fun h => by
      apply Cofork.IsColimit.hom_ext i; aesop_cat‚ü©
      -- ‚ä¢ Cofork.œÄ c ‚â´ m‚úù = Cofork.œÄ c ‚â´ IsColimit.desc i (coforkOfCokernelCofork s)
                                        -- üéâ no goals
#align category_theory.preadditive.is_colimit_cokernel_cofork_of_cofork CategoryTheory.Preadditive.isColimitCokernelCoforkOfCofork

variable (f g)

/-- A preadditive category has a coequalizer for `f` and `g` if it has a cokernel for `f - g`. -/
theorem hasCoequalizer_of_hasCokernel [HasCokernel (f - g)] : HasCoequalizer f g :=
  HasColimit.mk
    { cocone := coforkOfCokernelCofork _
      isColimit := isColimitCoforkOfCokernelCofork (coequalizerIsCoequalizer (f - g) 0) }
#align category_theory.preadditive.has_coequalizer_of_has_cokernel CategoryTheory.Preadditive.hasCoequalizer_of_hasCokernel

/-- A preadditive category has a cokernel for `f - g` if it has a coequalizer for `f` and `g`. -/
theorem hasCokernel_of_hasCoequalizer [HasCoequalizer f g] : HasCokernel (f - g) :=
  HasColimit.mk
    { cocone := cokernelCoforkOfCofork (coequalizer.cofork f g)
      isColimit := isColimitCokernelCoforkOfCofork (colimit.isColimit (parallelPair f g)) }
#align category_theory.preadditive.has_cokernel_of_has_coequalizer CategoryTheory.Preadditive.hasCokernel_of_hasCoequalizer

end

/-- If a preadditive category has all kernels, then it also has all equalizers. -/
theorem hasEqualizers_of_hasKernels [HasKernels C] : HasEqualizers C :=
  @hasEqualizers_of_hasLimit_parallelPair _ _ fun {_} {_} f g => hasEqualizer_of_hasKernel f g
#align category_theory.preadditive.has_equalizers_of_has_kernels CategoryTheory.Preadditive.hasEqualizers_of_hasKernels

/-- If a preadditive category has all cokernels, then it also has all coequalizers. -/
theorem hasCoequalizers_of_hasCokernels [HasCokernels C] : HasCoequalizers C :=
  @hasCoequalizers_of_hasColimit_parallelPair _ _ fun {_} {_} f g =>
    hasCoequalizer_of_hasCokernel f g
#align category_theory.preadditive.has_coequalizers_of_has_cokernels CategoryTheory.Preadditive.hasCoequalizers_of_hasCokernels

end Equalizers

end Preadditive

end CategoryTheory
