/-
Copyright (c) 2020 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel, Scott Morrison
-/
import Mathlib.Algebra.Homology.Exact
import Mathlib.CategoryTheory.Limits.Shapes.Biproducts
import Mathlib.CategoryTheory.Adjunction.Limits
import Mathlib.CategoryTheory.Limits.Preserves.Finite

#align_import category_theory.preadditive.projective from "leanprover-community/mathlib"@"3974a774a707e2e06046a14c0eaef4654584fada"

/-!
# Projective objects and categories with enough projectives

An object `P` is called *projective* if every morphism out of `P` factors through every epimorphism.

A category `C` *has enough projectives* if every object admits an epimorphism from some
projective object.

`CategoryTheory.Projective.over X` picks an arbitrary such projective object, and
`CategoryTheory.Projective.œÄ X : CategoryTheory.Projective.over X ‚ü∂ X` is the corresponding
epimorphism.

Given a morphism `f : X ‚ü∂ Y`, `CategoryTheory.Projective.left f` is a projective object over
`CategoryTheory.Limits.kernel f`, and `Projective.d f : Projective.left f ‚ü∂ X` is the morphism
`œÄ (kernel f) ‚â´ kernel.Œπ f`.

-/


noncomputable section

open CategoryTheory Limits Opposite

universe v u v' u'

namespace CategoryTheory

variable {C : Type u} [Category.{v} C]

/--
An object `P` is called *projective* if every morphism out of `P` factors through every epimorphism.
-/
class Projective (P : C) : Prop where
  factors : ‚àÄ {E X : C} (f : P ‚ü∂ X) (e : E ‚ü∂ X) [Epi e], ‚àÉ f', f' ‚â´ e = f
#align category_theory.projective CategoryTheory.Projective

lemma Limits.IsZero.projective {X : C} (h : IsZero X) : Projective X where
  factors _ _ _ := ‚ü®h.to_ _, h.eq_of_src _ _‚ü©

section

/-- A projective presentation of an object `X` consists of an epimorphism `f : P ‚ü∂ X`
from some projective object `P`.
-/
-- Porting note(#5171): was @[nolint has_nonempty_instance]
structure ProjectivePresentation (X : C) where
  p : C
  [projective : Projective p]
  f : p ‚ü∂ X
  [epi : Epi f]
#align category_theory.projective_presentation CategoryTheory.ProjectivePresentation

attribute [instance] ProjectivePresentation.projective ProjectivePresentation.epi

variable (C)

/-- A category "has enough projectives" if for every object `X` there is a projective object `P` and
    an epimorphism `P ‚Ü† X`. -/
class EnoughProjectives : Prop where
  presentation : ‚àÄ X : C, Nonempty (ProjectivePresentation X)
#align category_theory.enough_projectives CategoryTheory.EnoughProjectives

end

namespace Projective

/--
An arbitrarily chosen factorisation of a morphism out of a projective object through an epimorphism.
-/
def factorThru {P X E : C} [Projective P] (f : P ‚ü∂ X) (e : E ‚ü∂ X) [Epi e] : P ‚ü∂ E :=
  (Projective.factors f e).choose
#align category_theory.projective.factor_thru CategoryTheory.Projective.factorThru

@[reassoc (attr := simp)]
theorem factorThru_comp {P X E : C} [Projective P] (f : P ‚ü∂ X) (e : E ‚ü∂ X) [Epi e] :
    factorThru f e ‚â´ e = f :=
  (Projective.factors f e).choose_spec
#align category_theory.projective.factor_thru_comp CategoryTheory.Projective.factorThru_comp

section

open ZeroObject

instance zero_projective [HasZeroObject C] : Projective (0 : C) :=
  (isZero_zero C).projective
#align category_theory.projective.zero_projective CategoryTheory.Projective.zero_projective

end

theorem of_iso {P Q : C} (i : P ‚âÖ Q) (hP : Projective P) : Projective Q where
  factors f e e_epi :=
    let ‚ü®f', hf'‚ü© := Projective.factors (i.hom ‚â´ f) e
    ‚ü®i.inv ‚â´ f', by simp [hf']‚ü©
#align category_theory.projective.of_iso CategoryTheory.Projective.of_iso

theorem iso_iff {P Q : C} (i : P ‚âÖ Q) : Projective P ‚Üî Projective Q :=
  ‚ü®of_iso i, of_iso i.symm‚ü©
#align category_theory.projective.iso_iff CategoryTheory.Projective.iso_iff

/-- The axiom of choice says that every type is a projective object in `Type`. -/
instance (X : Type u) : Projective X where
  factors f e _ :=
    have he : Function.Surjective e := surjective_of_epi e
    ‚ü®fun x => (he (f x)).choose, funext fun x ‚Ü¶ (he (f x)).choose_spec‚ü©

instance Type.enoughProjectives : EnoughProjectives (Type u) where
  presentation X := ‚ü®‚ü®X, ùüô X‚ü©‚ü©
#align category_theory.projective.Type.enough_projectives CategoryTheory.Projective.Type.enoughProjectives

instance {P Q : C} [HasBinaryCoproduct P Q] [Projective P] [Projective Q] : Projective (P ‚®ø Q) where
  factors f e epi := ‚ü®coprod.desc (factorThru (coprod.inl ‚â´ f) e) (factorThru (coprod.inr ‚â´ f) e),
    by aesop_cat‚ü©

instance {Œ≤ : Type v} (g : Œ≤ ‚Üí C) [HasCoproduct g] [‚àÄ b, Projective (g b)] : Projective (‚àê g) where
  factors f e epi := ‚ü®Sigma.desc fun b => factorThru (Sigma.Œπ g b ‚â´ f) e, by aesop_cat‚ü©

instance {P Q : C} [HasZeroMorphisms C] [HasBinaryBiproduct P Q] [Projective P] [Projective Q] :
    Projective (P ‚äû Q) where
  factors f e epi := ‚ü®biprod.desc (factorThru (biprod.inl ‚â´ f) e) (factorThru (biprod.inr ‚â´ f) e),
    by aesop_cat‚ü©

instance {Œ≤ : Type v} (g : Œ≤ ‚Üí C) [HasZeroMorphisms C] [HasBiproduct g] [‚àÄ b, Projective (g b)] :
    Projective (‚®Å g) where
  factors f e epi := ‚ü®biproduct.desc fun b => factorThru (biproduct.Œπ g b ‚â´ f) e, by aesop_cat‚ü©

theorem projective_iff_preservesEpimorphisms_coyoneda_obj (P : C) :
    Projective P ‚Üî (coyoneda.obj (op P)).PreservesEpimorphisms :=
  ‚ü®fun hP =>
    ‚ü®fun f _ =>
      (epi_iff_surjective _).2 fun g =>
        have : Projective (unop (op P)) := hP
        ‚ü®factorThru g f, factorThru_comp _ _‚ü©‚ü©,
    fun _ =>
    ‚ü®fun f e _ =>
      (epi_iff_surjective _).1 (inferInstance : Epi ((coyoneda.obj (op P)).map e)) f‚ü©‚ü©
#align category_theory.projective.projective_iff_preserves_epimorphisms_coyoneda_obj CategoryTheory.Projective.projective_iff_preservesEpimorphisms_coyoneda_obj

section EnoughProjectives

variable [EnoughProjectives C]

/-- `Projective.over X` provides an arbitrarily chosen projective object equipped with
an epimorphism `Projective.œÄ : Projective.over X ‚ü∂ X`.
-/
def over (X : C) : C :=
  (EnoughProjectives.presentation X).some.p
#align category_theory.projective.over CategoryTheory.Projective.over

instance projective_over (X : C) : Projective (over X) :=
  (EnoughProjectives.presentation X).some.projective
#align category_theory.projective.projective_over CategoryTheory.Projective.projective_over

/-- The epimorphism `projective.œÄ : projective.over X ‚ü∂ X`
from the arbitrarily chosen projective object over `X`.
-/
def œÄ (X : C) : over X ‚ü∂ X :=
  (EnoughProjectives.presentation X).some.f
#align category_theory.projective.œÄ CategoryTheory.Projective.œÄ

instance œÄ_epi (X : C) : Epi (œÄ X) :=
  (EnoughProjectives.presentation X).some.epi
#align category_theory.projective.œÄ_epi CategoryTheory.Projective.œÄ_epi

section

variable [HasZeroMorphisms C] {X Y : C} (f : X ‚ü∂ Y) [HasKernel f]

/-- When `C` has enough projectives, the object `Projective.syzygies f` is
an arbitrarily chosen projective object over `kernel f`.
-/
def syzygies : C := over (kernel f)
#align category_theory.projective.syzygies CategoryTheory.Projective.syzygies

instance : Projective (syzygies f) := inferInstanceAs (Projective (over _))

/-- When `C` has enough projectives,
`Projective.d f : Projective.syzygies f ‚ü∂ X` is the composition
`œÄ (kernel f) ‚â´ kernel.Œπ f`.

(When `C` is abelian, we have `exact (projective.d f) f`.)
-/
abbrev d : syzygies f ‚ü∂ X :=
  œÄ (kernel f) ‚â´ kernel.Œπ f
#align category_theory.projective.d CategoryTheory.Projective.d

end

end EnoughProjectives

end Projective

namespace Adjunction

variable {D : Type u'} [Category.{v'} D] {F : C ‚•§ D} {G : D ‚•§ C}

theorem map_projective (adj : F ‚ä£ G) [G.PreservesEpimorphisms] (P : C) (hP : Projective P) :
    Projective (F.obj P) where
  factors f g _ := by
    rcases hP.factors (adj.unit.app P ‚â´ G.map f) (G.map g) with ‚ü®f', hf'‚ü©
    use F.map f' ‚â´ adj.counit.app _
    rw [Category.assoc, ‚Üê Adjunction.counit_naturality, ‚Üê Category.assoc, ‚Üê F.map_comp, hf']
    simp
#align category_theory.adjunction.map_projective CategoryTheory.Adjunction.map_projective

theorem projective_of_map_projective (adj : F ‚ä£ G) [F.Full] [F.Faithful] (P : C)
    (hP : Projective (F.obj P)) : Projective P where
  factors f g _ := by
    haveI := Adjunction.leftAdjointPreservesColimits.{0, 0} adj
    rcases (@hP).1 (F.map f) (F.map g) with ‚ü®f', hf'‚ü©
    use adj.unit.app _ ‚â´ G.map f' ‚â´ (inv <| adj.unit.app _)
    exact F.map_injective (by simpa)
#align category_theory.adjunction.projective_of_map_projective CategoryTheory.Adjunction.projective_of_map_projective

/-- Given an adjunction `F ‚ä£ G` such that `G` preserves epis, `F` maps a projective presentation of
`X` to a projective presentation of `F(X)`. -/
def mapProjectivePresentation (adj : F ‚ä£ G) [G.PreservesEpimorphisms] (X : C)
    (Y : ProjectivePresentation X) : ProjectivePresentation (F.obj X) where
  p := F.obj Y.p
  projective := adj.map_projective _ Y.projective
  f := F.map Y.f
  epi := have := Adjunction.leftAdjointPreservesColimits.{0, 0} adj; inferInstance
#align category_theory.adjunction.map_projective_presentation CategoryTheory.Adjunction.mapProjectivePresentation

end Adjunction

namespace Equivalence

variable {D : Type u'} [Category.{v'} D] (F : C ‚âå D)

theorem map_projective_iff (P : C) : Projective (F.functor.obj P) ‚Üî Projective P :=
  ‚ü®F.toAdjunction.projective_of_map_projective P, F.toAdjunction.map_projective P‚ü©

/-- Given an equivalence of categories `F`, a projective presentation of `F(X)` induces a
projective presentation of `X.` -/
def projectivePresentationOfMapProjectivePresentation (X : C)
    (Y : ProjectivePresentation (F.functor.obj X)) : ProjectivePresentation X where
  p := F.inverse.obj Y.p
  projective := Adjunction.map_projective F.symm.toAdjunction Y.p Y.projective
  f := F.inverse.map Y.f ‚â´ F.unitInv.app _
  epi := epi_comp _ _
#align category_theory.equivalence.projective_presentation_of_map_projective_presentation CategoryTheory.Equivalence.projectivePresentationOfMapProjectivePresentation

theorem enoughProjectives_iff (F : C ‚âå D) : EnoughProjectives C ‚Üî EnoughProjectives D := by
  constructor
  all_goals intro H; constructor; intro X; constructor
  ¬∑ exact F.symm.projectivePresentationOfMapProjectivePresentation _
      (Nonempty.some (H.presentation (F.inverse.obj X)))
  ¬∑ exact F.projectivePresentationOfMapProjectivePresentation X
      (Nonempty.some (H.presentation (F.functor.obj X)))
#align category_theory.equivalence.enough_projectives_iff CategoryTheory.Equivalence.enoughProjectives_iff

end Equivalence

open Projective

section

variable [HasZeroMorphisms C] [HasEqualizers C] [HasImages C]

/-- Given a projective object `P` mapping via `h` into
the middle object `R` of a pair of exact morphisms `f : Q ‚ü∂ R` and `g : R ‚ü∂ S`,
such that `h ‚â´ g = 0`, there is a lift of `h` to `Q`.
-/
def Exact.lift {P Q R S : C} [Projective P] (h : P ‚ü∂ R) (f : Q ‚ü∂ R) (g : R ‚ü∂ S) (hfg : Exact f g)
    (w : h ‚â´ g = 0) : P ‚ü∂ Q :=
  -- See the porting note on `Exact.epi`.
  haveI := hfg.epi
  factorThru (factorThru (factorThruKernelSubobject g h w) (imageToKernel f g hfg.w))
    (factorThruImageSubobject f)
#align category_theory.exact.lift CategoryTheory.Exact.lift

@[simp]
theorem Exact.lift_comp {P Q R S : C} [Projective P] (h : P ‚ü∂ R) (f : Q ‚ü∂ R) (g : R ‚ü∂ S)
    (hfg : Exact f g) (w : h ‚â´ g = 0) : Exact.lift h f g hfg w ‚â´ f = h := by
  simp only [Exact.lift]
  conv_lhs =>
    congr
    rfl
    rw [‚Üê imageSubobject_arrow_comp f]
  -- See the porting note on `Exact.epi`.
  haveI := hfg.epi
  rw [‚Üê Category.assoc, factorThru_comp, ‚Üê imageToKernel_arrow f g, ‚Üê Category.assoc,
    CategoryTheory.Projective.factorThru_comp, factorThruKernelSubobject_comp_arrow]
#align category_theory.exact.lift_comp CategoryTheory.Exact.lift_comp

end

end CategoryTheory
