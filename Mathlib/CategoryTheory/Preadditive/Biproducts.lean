/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Algebra.Group.Ext
import Mathlib.CategoryTheory.Limits.Shapes.Biproducts
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Biproducts
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Products
import Mathlib.CategoryTheory.Preadditive.Basic
import Mathlib.Tactic.Abel

#align_import category_theory.preadditive.biproducts from "leanprover-community/mathlib"@"a176cb1219e300e85793d44583dede42377b51af"

/-!
# Basic facts about biproducts in preadditive categories.

In (or between) preadditive categories,

* Any biproduct satisfies the equality
  `total : âˆ‘ j : J, biproduct.Ï€ f j â‰« biproduct.Î¹ f j = ğŸ™ (â¨ f)`,
  or, in the binary case, `total : fst â‰« inl + snd â‰« inr = ğŸ™ X`.

* Any (binary) `product` or (binary) `coproduct` is a (binary) `biproduct`.

* In any category (with zero morphisms), if `biprod.map f g` is an isomorphism,
  then both `f` and `g` are isomorphisms.

* If `f` is a morphism `Xâ‚ âŠ Xâ‚‚ âŸ¶ Yâ‚ âŠ Yâ‚‚` whose `Xâ‚ âŸ¶ Yâ‚` entry is an isomorphism,
  then we can construct isomorphisms `L : Xâ‚ âŠ Xâ‚‚ â‰… Xâ‚ âŠ Xâ‚‚` and `R : Yâ‚ âŠ Yâ‚‚ â‰… Yâ‚ âŠ Yâ‚‚`
  so that `L.hom â‰« g â‰« R.hom` is diagonal (with `Xâ‚ âŸ¶ Yâ‚` component still `f`),
  via Gaussian elimination.

* As a corollary of the previous two facts,
  if we have an isomorphism `Xâ‚ âŠ Xâ‚‚ â‰… Yâ‚ âŠ Yâ‚‚` whose `Xâ‚ âŸ¶ Yâ‚` entry is an isomorphism,
  we can construct an isomorphism `Xâ‚‚ â‰… Yâ‚‚`.

* If `f : W âŠ X âŸ¶ Y âŠ Z` is an isomorphism, either `ğŸ™ W = 0`,
  or at least one of the component maps `W âŸ¶ Y` and `W âŸ¶ Z` is nonzero.

* If `f : â¨ S âŸ¶ â¨ T` is an isomorphism,
  then every column (corresponding to a nonzero summand in the domain)
  has some nonzero matrix entry.

* A functor preserves a biproduct if and only if it preserves
  the corresponding product if and only if it preserves the corresponding coproduct.

There are connections between this material and the special case of the category whose morphisms are
matrices over a ring, in particular the Schur complement (see
`Mathlib.LinearAlgebra.Matrix.SchurComplement`). In particular, the declarations
`CategoryTheory.Biprod.isoElim`, `CategoryTheory.Biprod.gaussian`
and `Matrix.invertibleOfFromBlocksâ‚â‚Invertible` are all closely related.

-/


open CategoryTheory

open CategoryTheory.Preadditive

open CategoryTheory.Limits

open CategoryTheory.Functor

open CategoryTheory.Preadditive

open scoped Classical

universe v v' u u'

noncomputable section

namespace CategoryTheory

variable {C : Type u} [Category.{v} C] [Preadditive C]

namespace Limits

section Fintype

variable {J : Type} [Fintype J]

/-- In a preadditive category, we can construct a biproduct for `f : J â†’ C` from
any bicone `b` for `f` satisfying `total : âˆ‘ j : J, b.Ï€ j â‰« b.Î¹ j = ğŸ™ b.X`.

(That is, such a bicone is a limit cone and a colimit cocone.)
-/
def isBilimitOfTotal {f : J â†’ C} (b : Bicone f) (total : âˆ‘ j : J, b.Ï€ j â‰« b.Î¹ j = ğŸ™ b.pt) :
    b.IsBilimit where
  isLimit :=
    { lift := fun s => âˆ‘ j : J, s.Ï€.app âŸ¨jâŸ© â‰« b.Î¹ j
      uniq := fun s m h => by
        erw [â† Category.comp_id m, â† total, comp_sum]
        apply Finset.sum_congr rfl
        intro j _
        have reassoced : m â‰« Bicone.Ï€ b j â‰« Bicone.Î¹ b j = s.Ï€.app âŸ¨jâŸ© â‰« Bicone.Î¹ b j := by
          erw [â† Category.assoc, eq_whisker (h âŸ¨jâŸ©)]
        rw [reassoced]
      fac := fun s j => by
        cases j
        simp only [sum_comp, Category.assoc, Bicone.toCone_Ï€_app, b.Î¹_Ï€, comp_dite]
        -- See note [dsimp, simp].
        dsimp;
        simp }
  isColimit :=
    { desc := fun s => âˆ‘ j : J, b.Ï€ j â‰« s.Î¹.app âŸ¨jâŸ©
      uniq := fun s m h => by
        erw [â† Category.id_comp m, â† total, sum_comp]
        apply Finset.sum_congr rfl
        intro j _
        erw [Category.assoc, h âŸ¨jâŸ©]
      fac := fun s j => by
        cases j
        simp only [comp_sum, â† Category.assoc, Bicone.toCocone_Î¹_app, b.Î¹_Ï€, dite_comp]
        dsimp; simp }
#align category_theory.limits.is_bilimit_of_total CategoryTheory.Limits.isBilimitOfTotal

theorem IsBilimit.total {f : J â†’ C} {b : Bicone f} (i : b.IsBilimit) :
    âˆ‘ j : J, b.Ï€ j â‰« b.Î¹ j = ğŸ™ b.pt :=
  i.isLimit.hom_ext fun j => by
    cases j
    simp [sum_comp, b.Î¹_Ï€, comp_dite]
#align category_theory.limits.is_bilimit.total CategoryTheory.Limits.IsBilimit.total

/-- In a preadditive category, we can construct a biproduct for `f : J â†’ C` from
any bicone `b` for `f` satisfying `total : âˆ‘ j : J, b.Ï€ j â‰« b.Î¹ j = ğŸ™ b.X`.

(That is, such a bicone is a limit cone and a colimit cocone.)
-/
theorem hasBiproduct_of_total {f : J â†’ C} (b : Bicone f)
    (total : âˆ‘ j : J, b.Ï€ j â‰« b.Î¹ j = ğŸ™ b.pt) : HasBiproduct f :=
  HasBiproduct.mk
    { bicone := b
      isBilimit := isBilimitOfTotal b total }
#align category_theory.limits.has_biproduct_of_total CategoryTheory.Limits.hasBiproduct_of_total

/-- In a preadditive category, any finite bicone which is a limit cone is in fact a bilimit
    bicone. -/
def isBilimitOfIsLimit {f : J â†’ C} (t : Bicone f) (ht : IsLimit t.toCone) : t.IsBilimit :=
  isBilimitOfTotal _ <|
    ht.hom_ext fun j => by
      cases j
      simp [sum_comp, t.Î¹_Ï€, dite_comp, comp_dite]
#align category_theory.limits.is_bilimit_of_is_limit CategoryTheory.Limits.isBilimitOfIsLimit

/-- We can turn any limit cone over a pair into a bilimit bicone. -/
def biconeIsBilimitOfLimitConeOfIsLimit {f : J â†’ C} {t : Cone (Discrete.functor f)}
    (ht : IsLimit t) : (Bicone.ofLimitCone ht).IsBilimit :=
  isBilimitOfIsLimit _ <|
    IsLimit.ofIsoLimit ht <|
      Cones.ext (Iso.refl _)
        (by
          rintro âŸ¨jâŸ©
          aesop_cat)
#align category_theory.limits.bicone_is_bilimit_of_limit_cone_of_is_limit CategoryTheory.Limits.biconeIsBilimitOfLimitConeOfIsLimit

/-- In a preadditive category, any finite bicone which is a colimit cocone is in fact a bilimit
    bicone. -/
def isBilimitOfIsColimit {f : J â†’ C} (t : Bicone f) (ht : IsColimit t.toCocone) : t.IsBilimit :=
  isBilimitOfTotal _ <|
    ht.hom_ext fun j => by
      cases j
      simp_rw [Bicone.toCocone_Î¹_app, comp_sum, â† Category.assoc, t.Î¹_Ï€, dite_comp]
      simp
#align category_theory.limits.is_bilimit_of_is_colimit CategoryTheory.Limits.isBilimitOfIsColimit

/-- We can turn any limit cone over a pair into a bilimit bicone. -/
def biconeIsBilimitOfColimitCoconeOfIsColimit {f : J â†’ C} {t : Cocone (Discrete.functor f)}
    (ht : IsColimit t) : (Bicone.ofColimitCocone ht).IsBilimit :=
  isBilimitOfIsColimit _ <| IsColimit.ofIsoColimit ht <| Cocones.ext (Iso.refl _) <| by
    rintro âŸ¨jâŸ©; simp
#align category_theory.limits.bicone_is_bilimit_of_colimit_cocone_of_is_colimit CategoryTheory.Limits.biconeIsBilimitOfColimitCoconeOfIsColimit

end Fintype

section Finite

variable {J : Type} [Finite J]

/-- In a preadditive category, if the product over `f : J â†’ C` exists,
    then the biproduct over `f` exists. -/
theorem HasBiproduct.of_hasProduct (f : J â†’ C) [HasProduct f] : HasBiproduct f := by
  cases nonempty_fintype J
  exact HasBiproduct.mk
    { bicone := _
      isBilimit := biconeIsBilimitOfLimitConeOfIsLimit (limit.isLimit _) }
#align category_theory.limits.has_biproduct.of_has_product CategoryTheory.Limits.HasBiproduct.of_hasProduct

/-- In a preadditive category, if the coproduct over `f : J â†’ C` exists,
    then the biproduct over `f` exists. -/
theorem HasBiproduct.of_hasCoproduct (f : J â†’ C) [HasCoproduct f] : HasBiproduct f := by
  cases nonempty_fintype J
  exact HasBiproduct.mk
    { bicone := _
      isBilimit := biconeIsBilimitOfColimitCoconeOfIsColimit (colimit.isColimit _) }
#align category_theory.limits.has_biproduct.of_has_coproduct CategoryTheory.Limits.HasBiproduct.of_hasCoproduct

end Finite

/-- A preadditive category with finite products has finite biproducts. -/
theorem HasFiniteBiproducts.of_hasFiniteProducts [HasFiniteProducts C] : HasFiniteBiproducts C :=
  âŸ¨fun _ => { has_biproduct := fun _ => HasBiproduct.of_hasProduct _ }âŸ©
#align category_theory.limits.has_finite_biproducts.of_has_finite_products CategoryTheory.Limits.HasFiniteBiproducts.of_hasFiniteProducts

/-- A preadditive category with finite coproducts has finite biproducts. -/
theorem HasFiniteBiproducts.of_hasFiniteCoproducts [HasFiniteCoproducts C] :
    HasFiniteBiproducts C :=
  âŸ¨fun _ => { has_biproduct := fun _ => HasBiproduct.of_hasCoproduct _ }âŸ©
#align category_theory.limits.has_finite_biproducts.of_has_finite_coproducts CategoryTheory.Limits.HasFiniteBiproducts.of_hasFiniteCoproducts

section HasBiproduct

variable {J : Type} [Fintype J] {f : J â†’ C} [HasBiproduct f]

/-- In any preadditive category, any biproduct satsifies
`âˆ‘ j : J, biproduct.Ï€ f j â‰« biproduct.Î¹ f j = ğŸ™ (â¨ f)`
-/
@[simp]
theorem biproduct.total : âˆ‘ j : J, biproduct.Ï€ f j â‰« biproduct.Î¹ f j = ğŸ™ (â¨ f) :=
  IsBilimit.total (biproduct.isBilimit _)
#align category_theory.limits.biproduct.total CategoryTheory.Limits.biproduct.total

theorem biproduct.lift_eq {T : C} {g : âˆ€ j, T âŸ¶ f j} :
    biproduct.lift g = âˆ‘ j, g j â‰« biproduct.Î¹ f j := by
  ext j
  simp only [sum_comp, biproduct.Î¹_Ï€, comp_dite, biproduct.lift_Ï€, Category.assoc, comp_zero,
    Finset.sum_dite_eq', Finset.mem_univ, eqToHom_refl, Category.comp_id, if_true]
#align category_theory.limits.biproduct.lift_eq CategoryTheory.Limits.biproduct.lift_eq

theorem biproduct.desc_eq {T : C} {g : âˆ€ j, f j âŸ¶ T} :
    biproduct.desc g = âˆ‘ j, biproduct.Ï€ f j â‰« g j := by
  ext j
  simp [comp_sum, biproduct.Î¹_Ï€_assoc, dite_comp]
#align category_theory.limits.biproduct.desc_eq CategoryTheory.Limits.biproduct.desc_eq

@[reassoc]
theorem biproduct.lift_desc {T U : C} {g : âˆ€ j, T âŸ¶ f j} {h : âˆ€ j, f j âŸ¶ U} :
    biproduct.lift g â‰« biproduct.desc h = âˆ‘ j : J, g j â‰« h j := by
  simp [biproduct.lift_eq, biproduct.desc_eq, comp_sum, sum_comp, biproduct.Î¹_Ï€_assoc, comp_dite,
    dite_comp]
#align category_theory.limits.biproduct.lift_desc CategoryTheory.Limits.biproduct.lift_desc

theorem biproduct.map_eq [HasFiniteBiproducts C] {f g : J â†’ C} {h : âˆ€ j, f j âŸ¶ g j} :
    biproduct.map h = âˆ‘ j : J, biproduct.Ï€ f j â‰« h j â‰« biproduct.Î¹ g j := by
  ext
  simp [biproduct.Î¹_Ï€, biproduct.Î¹_Ï€_assoc, comp_sum, sum_comp, comp_dite, dite_comp]
#align category_theory.limits.biproduct.map_eq CategoryTheory.Limits.biproduct.map_eq

@[reassoc]
theorem biproduct.lift_matrix {K : Type} [Finite K] [HasFiniteBiproducts C] {f : J â†’ C} {g : K â†’ C}
    {P} (x : âˆ€ j, P âŸ¶ f j) (m : âˆ€ j k, f j âŸ¶ g k) :
    biproduct.lift x â‰« biproduct.matrix m = biproduct.lift fun k => âˆ‘ j, x j â‰« m j k := by
  ext
  simp [biproduct.lift_desc]
#align category_theory.limits.biproduct.lift_matrix CategoryTheory.Limits.biproduct.lift_matrix

end HasBiproduct

section HasFiniteBiproducts

variable {J K : Type} [Finite J] {f : J â†’ C} [HasFiniteBiproducts C]

@[reassoc]
theorem biproduct.matrix_desc [Fintype K] {f : J â†’ C} {g : K â†’ C}
    (m : âˆ€ j k, f j âŸ¶ g k) {P} (x : âˆ€ k, g k âŸ¶ P) :
    biproduct.matrix m â‰« biproduct.desc x = biproduct.desc fun j => âˆ‘ k, m j k â‰« x k := by
  ext
  simp [lift_desc]
#align category_theory.limits.biproduct.matrix_desc CategoryTheory.Limits.biproduct.matrix_desc

variable [Finite K]

@[reassoc]
theorem biproduct.matrix_map {f : J â†’ C} {g : K â†’ C} {h : K â†’ C} (m : âˆ€ j k, f j âŸ¶ g k)
    (n : âˆ€ k, g k âŸ¶ h k) :
    biproduct.matrix m â‰« biproduct.map n = biproduct.matrix fun j k => m j k â‰« n k := by
  ext
  simp
#align category_theory.limits.biproduct.matrix_map CategoryTheory.Limits.biproduct.matrix_map

@[reassoc]
theorem biproduct.map_matrix {f : J â†’ C} {g : J â†’ C} {h : K â†’ C} (m : âˆ€ k, f k âŸ¶ g k)
    (n : âˆ€ j k, g j âŸ¶ h k) :
    biproduct.map m â‰« biproduct.matrix n = biproduct.matrix fun j k => m j â‰« n j k := by
  ext
  simp
#align category_theory.limits.biproduct.map_matrix CategoryTheory.Limits.biproduct.map_matrix

end HasFiniteBiproducts

/-- Reindex a categorical biproduct via an equivalence of the index types. -/
@[simps]
def biproduct.reindex {Î² Î³ : Type} [Finite Î²] (Îµ : Î² â‰ƒ Î³)
    (f : Î³ â†’ C) [HasBiproduct f] [HasBiproduct (f âˆ˜ Îµ)] : â¨ f âˆ˜ Îµ â‰… â¨ f where
  hom := biproduct.desc fun b => biproduct.Î¹ f (Îµ b)
  inv := biproduct.lift fun b => biproduct.Ï€ f (Îµ b)
  hom_inv_id := by
    ext b b'
    by_cases h : b' = b
    Â· subst h; simp
    Â· have : Îµ b' â‰  Îµ b := by simp [h]
      simp [biproduct.Î¹_Ï€_ne _ h, biproduct.Î¹_Ï€_ne _ this]
  inv_hom_id := by
    cases nonempty_fintype Î²
    ext g g'
    by_cases h : g' = g <;>
      simp [Preadditive.sum_comp, Preadditive.comp_sum, biproduct.lift_desc,
        biproduct.Î¹_Ï€, biproduct.Î¹_Ï€_assoc, comp_dite, Equiv.apply_eq_iff_eq_symm_apply,
        Finset.sum_dite_eq' Finset.univ (Îµ.symm g') _, h]
#align category_theory.limits.biproduct.reindex CategoryTheory.Limits.biproduct.reindex

/-- In a preadditive category, we can construct a binary biproduct for `X Y : C` from
any binary bicone `b` satisfying `total : b.fst â‰« b.inl + b.snd â‰« b.inr = ğŸ™ b.X`.

(That is, such a bicone is a limit cone and a colimit cocone.)
-/
def isBinaryBilimitOfTotal {X Y : C} (b : BinaryBicone X Y)
    (total : b.fst â‰« b.inl + b.snd â‰« b.inr = ğŸ™ b.pt) : b.IsBilimit where
  isLimit :=
    { lift := fun s =>
      (BinaryFan.fst s â‰« b.inl : s.pt âŸ¶ b.pt) + (BinaryFan.snd s â‰« b.inr : s.pt âŸ¶ b.pt)
      uniq := fun s m h => by
        have reassoced (j : WalkingPair) {W : C} (h' : _ âŸ¶ W) :
          m â‰« b.toCone.Ï€.app âŸ¨jâŸ© â‰« h' = s.Ï€.app âŸ¨jâŸ© â‰« h' := by
            rw [â† Category.assoc, eq_whisker (h âŸ¨jâŸ©)]
        erw [â† Category.comp_id m, â† total, comp_add, reassoced WalkingPair.left,
          reassoced WalkingPair.right]
      fac := fun s j => by rcases j with âŸ¨âŸ¨âŸ©âŸ© <;> simp }
  isColimit :=
    { desc := fun s =>
        (b.fst â‰« BinaryCofan.inl s : b.pt âŸ¶ s.pt) + (b.snd â‰« BinaryCofan.inr s : b.pt âŸ¶ s.pt)
      uniq := fun s m h => by
        erw [â† Category.id_comp m, â† total, add_comp, Category.assoc, Category.assoc,
          h âŸ¨WalkingPair.leftâŸ©, h âŸ¨WalkingPair.rightâŸ©]
      fac := fun s j => by rcases j with âŸ¨âŸ¨âŸ©âŸ© <;> simp }
#align category_theory.limits.is_binary_bilimit_of_total CategoryTheory.Limits.isBinaryBilimitOfTotal

theorem IsBilimit.binary_total {X Y : C} {b : BinaryBicone X Y} (i : b.IsBilimit) :
    b.fst â‰« b.inl + b.snd â‰« b.inr = ğŸ™ b.pt :=
  i.isLimit.hom_ext fun j => by rcases j with âŸ¨âŸ¨âŸ©âŸ© <;> simp
#align category_theory.limits.is_bilimit.binary_total CategoryTheory.Limits.IsBilimit.binary_total

/-- In a preadditive category, we can construct a binary biproduct for `X Y : C` from
any binary bicone `b` satisfying `total : b.fst â‰« b.inl + b.snd â‰« b.inr = ğŸ™ b.X`.

(That is, such a bicone is a limit cone and a colimit cocone.)
-/
theorem hasBinaryBiproduct_of_total {X Y : C} (b : BinaryBicone X Y)
    (total : b.fst â‰« b.inl + b.snd â‰« b.inr = ğŸ™ b.pt) : HasBinaryBiproduct X Y :=
  HasBinaryBiproduct.mk
    { bicone := b
      isBilimit := isBinaryBilimitOfTotal b total }
#align category_theory.limits.has_binary_biproduct_of_total CategoryTheory.Limits.hasBinaryBiproduct_of_total

/-- We can turn any limit cone over a pair into a bicone. -/
@[simps]
def BinaryBicone.ofLimitCone {X Y : C} {t : Cone (pair X Y)} (ht : IsLimit t) :
    BinaryBicone X Y where
  pt := t.pt
  fst := t.Ï€.app âŸ¨WalkingPair.leftâŸ©
  snd := t.Ï€.app âŸ¨WalkingPair.rightâŸ©
  inl := ht.lift (BinaryFan.mk (ğŸ™ X) 0)
  inr := ht.lift (BinaryFan.mk 0 (ğŸ™ Y))
#align category_theory.limits.binary_bicone.of_limit_cone CategoryTheory.Limits.BinaryBicone.ofLimitCone

theorem inl_of_isLimit {X Y : C} {t : BinaryBicone X Y} (ht : IsLimit t.toCone) :
    t.inl = ht.lift (BinaryFan.mk (ğŸ™ X) 0) := by
  apply ht.uniq (BinaryFan.mk (ğŸ™ X) 0); rintro âŸ¨âŸ¨âŸ©âŸ© <;> dsimp <;> simp
#align category_theory.limits.inl_of_is_limit CategoryTheory.Limits.inl_of_isLimit

theorem inr_of_isLimit {X Y : C} {t : BinaryBicone X Y} (ht : IsLimit t.toCone) :
    t.inr = ht.lift (BinaryFan.mk 0 (ğŸ™ Y)) := by
  apply ht.uniq (BinaryFan.mk 0 (ğŸ™ Y)); rintro âŸ¨âŸ¨âŸ©âŸ© <;> dsimp <;> simp
#align category_theory.limits.inr_of_is_limit CategoryTheory.Limits.inr_of_isLimit

/-- In a preadditive category, any binary bicone which is a limit cone is in fact a bilimit
    bicone. -/
def isBinaryBilimitOfIsLimit {X Y : C} (t : BinaryBicone X Y) (ht : IsLimit t.toCone) :
    t.IsBilimit :=
  isBinaryBilimitOfTotal _ (by refine BinaryFan.IsLimit.hom_ext ht ?_ ?_ <;> simp)
#align category_theory.limits.is_binary_bilimit_of_is_limit CategoryTheory.Limits.isBinaryBilimitOfIsLimit

/-- We can turn any limit cone over a pair into a bilimit bicone. -/
def binaryBiconeIsBilimitOfLimitConeOfIsLimit {X Y : C} {t : Cone (pair X Y)} (ht : IsLimit t) :
    (BinaryBicone.ofLimitCone ht).IsBilimit :=
  isBinaryBilimitOfTotal _ <| BinaryFan.IsLimit.hom_ext ht (by simp) (by simp)
#align category_theory.limits.binary_bicone_is_bilimit_of_limit_cone_of_is_limit CategoryTheory.Limits.binaryBiconeIsBilimitOfLimitConeOfIsLimit

/-- In a preadditive category, if the product of `X` and `Y` exists, then the
    binary biproduct of `X` and `Y` exists. -/
theorem HasBinaryBiproduct.of_hasBinaryProduct (X Y : C) [HasBinaryProduct X Y] :
    HasBinaryBiproduct X Y :=
  HasBinaryBiproduct.mk
    { bicone := _
      isBilimit := binaryBiconeIsBilimitOfLimitConeOfIsLimit (limit.isLimit _) }
#align category_theory.limits.has_binary_biproduct.of_has_binary_product CategoryTheory.Limits.HasBinaryBiproduct.of_hasBinaryProduct

/-- In a preadditive category, if all binary products exist, then all binary biproducts exist. -/
theorem HasBinaryBiproducts.of_hasBinaryProducts [HasBinaryProducts C] : HasBinaryBiproducts C :=
  { has_binary_biproduct := fun X Y => HasBinaryBiproduct.of_hasBinaryProduct X Y }
#align category_theory.limits.has_binary_biproducts.of_has_binary_products CategoryTheory.Limits.HasBinaryBiproducts.of_hasBinaryProducts

/-- We can turn any colimit cocone over a pair into a bicone. -/
@[simps]
def BinaryBicone.ofColimitCocone {X Y : C} {t : Cocone (pair X Y)} (ht : IsColimit t) :
    BinaryBicone X Y where
  pt := t.pt
  fst := ht.desc (BinaryCofan.mk (ğŸ™ X) 0)
  snd := ht.desc (BinaryCofan.mk 0 (ğŸ™ Y))
  inl := t.Î¹.app âŸ¨WalkingPair.leftâŸ©
  inr := t.Î¹.app âŸ¨WalkingPair.rightâŸ©
#align category_theory.limits.binary_bicone.of_colimit_cocone CategoryTheory.Limits.BinaryBicone.ofColimitCocone

theorem fst_of_isColimit {X Y : C} {t : BinaryBicone X Y} (ht : IsColimit t.toCocone) :
    t.fst = ht.desc (BinaryCofan.mk (ğŸ™ X) 0) := by
  apply ht.uniq (BinaryCofan.mk (ğŸ™ X) 0)
  rintro âŸ¨âŸ¨âŸ©âŸ© <;> dsimp <;> simp
#align category_theory.limits.fst_of_is_colimit CategoryTheory.Limits.fst_of_isColimit

theorem snd_of_isColimit {X Y : C} {t : BinaryBicone X Y} (ht : IsColimit t.toCocone) :
    t.snd = ht.desc (BinaryCofan.mk 0 (ğŸ™ Y)) := by
  apply ht.uniq (BinaryCofan.mk 0 (ğŸ™ Y))
  rintro âŸ¨âŸ¨âŸ©âŸ© <;> dsimp <;> simp
#align category_theory.limits.snd_of_is_colimit CategoryTheory.Limits.snd_of_isColimit

/-- In a preadditive category, any binary bicone which is a colimit cocone is in fact a
    bilimit bicone. -/
def isBinaryBilimitOfIsColimit {X Y : C} (t : BinaryBicone X Y) (ht : IsColimit t.toCocone) :
    t.IsBilimit :=
  isBinaryBilimitOfTotal _ <| by
    refine BinaryCofan.IsColimit.hom_ext ht ?_ ?_ <;> simp
#align category_theory.limits.is_binary_bilimit_of_is_colimit CategoryTheory.Limits.isBinaryBilimitOfIsColimit

/-- We can turn any colimit cocone over a pair into a bilimit bicone. -/
def binaryBiconeIsBilimitOfColimitCoconeOfIsColimit {X Y : C} {t : Cocone (pair X Y)}
    (ht : IsColimit t) : (BinaryBicone.ofColimitCocone ht).IsBilimit :=
  isBinaryBilimitOfIsColimit (BinaryBicone.ofColimitCocone ht) <|
    IsColimit.ofIsoColimit ht <|
      Cocones.ext (Iso.refl _) fun j => by
        rcases j with âŸ¨âŸ¨âŸ©âŸ© <;> simp
#align category_theory.limits.binary_bicone_is_bilimit_of_colimit_cocone_of_is_colimit CategoryTheory.Limits.binaryBiconeIsBilimitOfColimitCoconeOfIsColimit

/-- In a preadditive category, if the coproduct of `X` and `Y` exists, then the
    binary biproduct of `X` and `Y` exists. -/
theorem HasBinaryBiproduct.of_hasBinaryCoproduct (X Y : C) [HasBinaryCoproduct X Y] :
    HasBinaryBiproduct X Y :=
  HasBinaryBiproduct.mk
    { bicone := _
      isBilimit := binaryBiconeIsBilimitOfColimitCoconeOfIsColimit (colimit.isColimit _) }
#align category_theory.limits.has_binary_biproduct.of_has_binary_coproduct CategoryTheory.Limits.HasBinaryBiproduct.of_hasBinaryCoproduct

/-- In a preadditive category, if all binary coproducts exist, then all binary biproducts exist. -/
theorem HasBinaryBiproducts.of_hasBinaryCoproducts [HasBinaryCoproducts C] :
    HasBinaryBiproducts C :=
  { has_binary_biproduct := fun X Y => HasBinaryBiproduct.of_hasBinaryCoproduct X Y }
#align category_theory.limits.has_binary_biproducts.of_has_binary_coproducts CategoryTheory.Limits.HasBinaryBiproducts.of_hasBinaryCoproducts

section

variable {X Y : C} [HasBinaryBiproduct X Y]

/-- In any preadditive category, any binary biproduct satsifies
`biprod.fst â‰« biprod.inl + biprod.snd â‰« biprod.inr = ğŸ™ (X âŠ Y)`.
-/
@[simp]
theorem biprod.total : biprod.fst â‰« biprod.inl + biprod.snd â‰« biprod.inr = ğŸ™ (X âŠ Y) := by
  ext <;> simp [add_comp]
#align category_theory.limits.biprod.total CategoryTheory.Limits.biprod.total

theorem biprod.lift_eq {T : C} {f : T âŸ¶ X} {g : T âŸ¶ Y} :
    biprod.lift f g = f â‰« biprod.inl + g â‰« biprod.inr := by ext <;> simp [add_comp]
#align category_theory.limits.biprod.lift_eq CategoryTheory.Limits.biprod.lift_eq

theorem biprod.desc_eq {T : C} {f : X âŸ¶ T} {g : Y âŸ¶ T} :
    biprod.desc f g = biprod.fst â‰« f + biprod.snd â‰« g := by ext <;> simp [add_comp]
#align category_theory.limits.biprod.desc_eq CategoryTheory.Limits.biprod.desc_eq

@[reassoc (attr := simp)]
theorem biprod.lift_desc {T U : C} {f : T âŸ¶ X} {g : T âŸ¶ Y} {h : X âŸ¶ U} {i : Y âŸ¶ U} :
    biprod.lift f g â‰« biprod.desc h i = f â‰« h + g â‰« i := by simp [biprod.lift_eq, biprod.desc_eq]
#align category_theory.limits.biprod.lift_desc CategoryTheory.Limits.biprod.lift_desc

theorem biprod.map_eq [HasBinaryBiproducts C] {W X Y Z : C} {f : W âŸ¶ Y} {g : X âŸ¶ Z} :
    biprod.map f g = biprod.fst â‰« f â‰« biprod.inl + biprod.snd â‰« g â‰« biprod.inr := by
  ext <;> simp
#align category_theory.limits.biprod.map_eq CategoryTheory.Limits.biprod.map_eq

/-- Every split mono `f` with a cokernel induces a binary bicone with `f` as its `inl` and
the cokernel map as its `snd`.
We will show in `is_bilimit_binary_bicone_of_split_mono_of_cokernel` that this binary bicone is in
fact already a biproduct. -/
@[simps]
def binaryBiconeOfIsSplitMonoOfCokernel {X Y : C} {f : X âŸ¶ Y} [IsSplitMono f] {c : CokernelCofork f}
    (i : IsColimit c) : BinaryBicone X c.pt where
  pt := Y
  fst := retraction f
  snd := c.Ï€
  inl := f
  inr :=
    let c' : CokernelCofork (ğŸ™ Y - (ğŸ™ Y - retraction f â‰« f)) :=
      CokernelCofork.ofÏ€ (Cofork.Ï€ c) (by simp)
    let i' : IsColimit c' := isCokernelEpiComp i (retraction f) (by simp)
    let i'' := isColimitCoforkOfCokernelCofork i'
    (splitEpiOfIdempotentOfIsColimitCofork C (by simp) i'').section_
  inl_fst := by simp
  inl_snd := by simp
  inr_fst := by
    dsimp only
    rw [splitEpiOfIdempotentOfIsColimitCofork_section_,
      isColimitCoforkOfCokernelCofork_desc, isCokernelEpiComp_desc]
    dsimp only [cokernelCoforkOfCofork_ofÏ€]
    letI := epi_of_isColimit_cofork i
    apply zero_of_epi_comp c.Ï€
    simp only [sub_comp, comp_sub, Category.comp_id, Category.assoc, IsSplitMono.id, sub_self,
      Cofork.IsColimit.Ï€_desc_assoc, CokernelCofork.Ï€_ofÏ€, IsSplitMono.id_assoc]
    apply sub_eq_zero_of_eq
    apply Category.id_comp
  inr_snd := by apply SplitEpi.id
#align category_theory.limits.binary_bicone_of_is_split_mono_of_cokernel CategoryTheory.Limits.binaryBiconeOfIsSplitMonoOfCokernel

/-- The bicone constructed in `binaryBiconeOfSplitMonoOfCokernel` is a bilimit.
This is a version of the splitting lemma that holds in all preadditive categories. -/
def isBilimitBinaryBiconeOfIsSplitMonoOfCokernel {X Y : C} {f : X âŸ¶ Y} [IsSplitMono f]
    {c : CokernelCofork f} (i : IsColimit c) : (binaryBiconeOfIsSplitMonoOfCokernel i).IsBilimit :=
  isBinaryBilimitOfTotal _
    (by
      simp only [binaryBiconeOfIsSplitMonoOfCokernel_fst,
        binaryBiconeOfIsSplitMonoOfCokernel_inr,
        binaryBiconeOfIsSplitMonoOfCokernel_snd,
        splitEpiOfIdempotentOfIsColimitCofork_section_]
      dsimp only [binaryBiconeOfIsSplitMonoOfCokernel_pt]
      rw [isColimitCoforkOfCokernelCofork_desc, isCokernelEpiComp_desc]
      simp only [binaryBiconeOfIsSplitMonoOfCokernel_inl, Cofork.IsColimit.Ï€_desc,
        cokernelCoforkOfCofork_Ï€, Cofork.Ï€_ofÏ€, add_sub_cancel])
#align category_theory.limits.is_bilimit_binary_bicone_of_is_split_mono_of_cokernel CategoryTheory.Limits.isBilimitBinaryBiconeOfIsSplitMonoOfCokernel

/-- If `b` is a binary bicone such that `b.inl` is a kernel of `b.snd`, then `b` is a bilimit
    bicone. -/
def BinaryBicone.isBilimitOfKernelInl {X Y : C} (b : BinaryBicone X Y)
    (hb : IsLimit b.sndKernelFork) : b.IsBilimit :=
  isBinaryBilimitOfIsLimit _ <|
    BinaryFan.IsLimit.mk _ (fun f g => f â‰« b.inl + g â‰« b.inr) (fun f g => by simp)
      (fun f g => by simp) fun {T} f g m hâ‚ hâ‚‚ => by
      dsimp at m
      have hâ‚' : ((m : T âŸ¶ b.pt) - (f â‰« b.inl + g â‰« b.inr)) â‰« b.fst = 0 := by
        simpa using sub_eq_zero.2 hâ‚
      have hâ‚‚' : (m - (f â‰« b.inl + g â‰« b.inr)) â‰« b.snd = 0 := by simpa using sub_eq_zero.2 hâ‚‚
      obtain âŸ¨q : T âŸ¶ X, hq : q â‰« b.inl = m - (f â‰« b.inl + g â‰« b.inr)âŸ© :=
        KernelFork.IsLimit.lift' hb _ hâ‚‚'
      rw [â† sub_eq_zero, â† hq, â† Category.comp_id q, â† b.inl_fst, â† Category.assoc, hq, hâ‚',
        zero_comp]
#align category_theory.limits.binary_bicone.is_bilimit_of_kernel_inl CategoryTheory.Limits.BinaryBicone.isBilimitOfKernelInl

/-- If `b` is a binary bicone such that `b.inr` is a kernel of `b.fst`, then `b` is a bilimit
    bicone. -/
def BinaryBicone.isBilimitOfKernelInr {X Y : C} (b : BinaryBicone X Y)
    (hb : IsLimit b.fstKernelFork) : b.IsBilimit :=
  isBinaryBilimitOfIsLimit _ <|
    BinaryFan.IsLimit.mk _ (fun f g => f â‰« b.inl + g â‰« b.inr) (fun f g => by simp)
    (fun f g => by simp) fun {T} f g m hâ‚ hâ‚‚ => by
      dsimp at m
      have hâ‚' : (m - (f â‰« b.inl + g â‰« b.inr)) â‰« b.fst = 0 := by simpa using sub_eq_zero.2 hâ‚
      have hâ‚‚' : (m - (f â‰« b.inl + g â‰« b.inr)) â‰« b.snd = 0 := by simpa using sub_eq_zero.2 hâ‚‚
      obtain âŸ¨q : T âŸ¶ Y, hq : q â‰« b.inr = m - (f â‰« b.inl + g â‰« b.inr)âŸ© :=
        KernelFork.IsLimit.lift' hb _ hâ‚'
      rw [â† sub_eq_zero, â† hq, â† Category.comp_id q, â† b.inr_snd, â† Category.assoc, hq, hâ‚‚',
        zero_comp]
#align category_theory.limits.binary_bicone.is_bilimit_of_kernel_inr CategoryTheory.Limits.BinaryBicone.isBilimitOfKernelInr

/-- If `b` is a binary bicone such that `b.fst` is a cokernel of `b.inr`, then `b` is a bilimit
    bicone. -/
def BinaryBicone.isBilimitOfCokernelFst {X Y : C} (b : BinaryBicone X Y)
    (hb : IsColimit b.inrCokernelCofork) : b.IsBilimit :=
  isBinaryBilimitOfIsColimit _ <|
    BinaryCofan.IsColimit.mk _ (fun f g => b.fst â‰« f + b.snd â‰« g) (fun f g => by simp)
      (fun f g => by simp) fun {T} f g m hâ‚ hâ‚‚ => by
      dsimp at m
      have hâ‚' : b.inl â‰« (m - (b.fst â‰« f + b.snd â‰« g)) = 0 := by simpa using sub_eq_zero.2 hâ‚
      have hâ‚‚' : b.inr â‰« (m - (b.fst â‰« f + b.snd â‰« g)) = 0 := by simpa using sub_eq_zero.2 hâ‚‚
      obtain âŸ¨q : X âŸ¶ T, hq : b.fst â‰« q = m - (b.fst â‰« f + b.snd â‰« g)âŸ© :=
        CokernelCofork.IsColimit.desc' hb _ hâ‚‚'
      rw [â† sub_eq_zero, â† hq, â† Category.id_comp q, â† b.inl_fst, Category.assoc, hq, hâ‚',
        comp_zero]
#align category_theory.limits.binary_bicone.is_bilimit_of_cokernel_fst CategoryTheory.Limits.BinaryBicone.isBilimitOfCokernelFst

/-- If `b` is a binary bicone such that `b.snd` is a cokernel of `b.inl`, then `b` is a bilimit
    bicone. -/
def BinaryBicone.isBilimitOfCokernelSnd {X Y : C} (b : BinaryBicone X Y)
    (hb : IsColimit b.inlCokernelCofork) : b.IsBilimit :=
  isBinaryBilimitOfIsColimit _ <|
    BinaryCofan.IsColimit.mk _ (fun f g => b.fst â‰« f + b.snd â‰« g) (fun f g => by simp)
      (fun f g => by simp) fun {T} f g m hâ‚ hâ‚‚ => by
      dsimp at m
      have hâ‚' : b.inl â‰« (m - (b.fst â‰« f + b.snd â‰« g)) = 0 := by simpa using sub_eq_zero.2 hâ‚
      have hâ‚‚' : b.inr â‰« (m - (b.fst â‰« f + b.snd â‰« g)) = 0 := by simpa using sub_eq_zero.2 hâ‚‚
      obtain âŸ¨q : Y âŸ¶ T, hq : b.snd â‰« q = m - (b.fst â‰« f + b.snd â‰« g)âŸ© :=
        CokernelCofork.IsColimit.desc' hb _ hâ‚'
      rw [â† sub_eq_zero, â† hq, â† Category.id_comp q, â† b.inr_snd, Category.assoc, hq, hâ‚‚',
        comp_zero]
#align category_theory.limits.binary_bicone.is_bilimit_of_cokernel_snd CategoryTheory.Limits.BinaryBicone.isBilimitOfCokernelSnd

/-- Every split epi `f` with a kernel induces a binary bicone with `f` as its `snd` and
the kernel map as its `inl`.
We will show in `binary_bicone_of_is_split_mono_of_cokernel` that this binary bicone is in fact
already a biproduct. -/
@[simps]
def binaryBiconeOfIsSplitEpiOfKernel {X Y : C} {f : X âŸ¶ Y} [IsSplitEpi f] {c : KernelFork f}
    (i : IsLimit c) : BinaryBicone c.pt Y :=
  { pt := X
    fst :=
      let c' : KernelFork (ğŸ™ X - (ğŸ™ X - f â‰« section_ f)) := KernelFork.ofÎ¹ (Fork.Î¹ c) (by simp)
      let i' : IsLimit c' := isKernelCompMono i (section_ f) (by simp)
      let i'' := isLimitForkOfKernelFork i'
      (splitMonoOfIdempotentOfIsLimitFork C (by simp) i'').retraction
    snd := f
    inl := c.Î¹
    inr := section_ f
    inl_fst := by apply SplitMono.id
    inl_snd := by simp
    inr_fst := by
      dsimp only
      rw [splitMonoOfIdempotentOfIsLimitFork_retraction, isLimitForkOfKernelFork_lift,
        isKernelCompMono_lift]
      dsimp only [kernelForkOfFork_Î¹]
      letI := mono_of_isLimit_fork i
      apply zero_of_comp_mono c.Î¹
      simp only [comp_sub, Category.comp_id, Category.assoc, sub_self, Fork.IsLimit.lift_Î¹,
        Fork.Î¹_ofÎ¹, IsSplitEpi.id_assoc]
    inr_snd := by simp }
#align category_theory.limits.binary_bicone_of_is_split_epi_of_kernel CategoryTheory.Limits.binaryBiconeOfIsSplitEpiOfKernel

/-- The bicone constructed in `binaryBiconeOfIsSplitEpiOfKernel` is a bilimit.
This is a version of the splitting lemma that holds in all preadditive categories. -/
def isBilimitBinaryBiconeOfIsSplitEpiOfKernel {X Y : C} {f : X âŸ¶ Y} [IsSplitEpi f]
    {c : KernelFork f} (i : IsLimit c) : (binaryBiconeOfIsSplitEpiOfKernel i).IsBilimit :=
  BinaryBicone.isBilimitOfKernelInl _ <| i.ofIsoLimit <| Fork.ext (Iso.refl _) (by simp)
#align category_theory.limits.is_bilimit_binary_bicone_of_is_split_epi_of_kernel CategoryTheory.Limits.isBilimitBinaryBiconeOfIsSplitEpiOfKernel

end

section

variable {X Y : C} (f g : X âŸ¶ Y)

/-- The existence of binary biproducts implies that there is at most one preadditive structure. -/
theorem biprod.add_eq_lift_id_desc [HasBinaryBiproduct X X] :
    f + g = biprod.lift (ğŸ™ X) (ğŸ™ X) â‰« biprod.desc f g := by simp
#align category_theory.limits.biprod.add_eq_lift_id_desc CategoryTheory.Limits.biprod.add_eq_lift_id_desc

/-- The existence of binary biproducts implies that there is at most one preadditive structure. -/
theorem biprod.add_eq_lift_desc_id [HasBinaryBiproduct Y Y] :
    f + g = biprod.lift f g â‰« biprod.desc (ğŸ™ Y) (ğŸ™ Y) := by simp
#align category_theory.limits.biprod.add_eq_lift_desc_id CategoryTheory.Limits.biprod.add_eq_lift_desc_id

end

end Limits

open CategoryTheory.Limits

section

attribute [local ext] Preadditive

/-- The existence of binary biproducts implies that there is at most one preadditive structure. -/
instance subsingleton_preadditive_of_hasBinaryBiproducts {C : Type u} [Category.{v} C]
    [HasZeroMorphisms C] [HasBinaryBiproducts C] : Subsingleton (Preadditive C) where
  allEq := fun a b => by
    apply Preadditive.ext; funext X Y; apply AddCommGroup.ext; funext f g
    have hâ‚ := @biprod.add_eq_lift_id_desc _ _ a _ _ f g
      (by convert (inferInstance : HasBinaryBiproduct X X); apply Subsingleton.elim)
    have hâ‚‚ := @biprod.add_eq_lift_id_desc _ _ b _ _ f g
      (by convert (inferInstance : HasBinaryBiproduct X X); apply Subsingleton.elim)
    refine hâ‚.trans (Eq.trans ?_ hâ‚‚.symm)
    congr! 2 <;> apply Subsingleton.elim
#align category_theory.subsingleton_preadditive_of_has_binary_biproducts CategoryTheory.subsingleton_preadditive_of_hasBinaryBiproducts

end

section

variable [HasBinaryBiproducts.{v} C]
variable {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : C}
variable (fâ‚â‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚â‚‚ : Xâ‚ âŸ¶ Yâ‚‚) (fâ‚‚â‚ : Xâ‚‚ âŸ¶ Yâ‚) (fâ‚‚â‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚)

/-- The "matrix" morphism `Xâ‚ âŠ Xâ‚‚ âŸ¶ Yâ‚ âŠ Yâ‚‚` with specified components.
-/
def Biprod.ofComponents : Xâ‚ âŠ Xâ‚‚ âŸ¶ Yâ‚ âŠ Yâ‚‚ :=
  biprod.fst â‰« fâ‚â‚ â‰« biprod.inl + biprod.fst â‰« fâ‚â‚‚ â‰« biprod.inr + biprod.snd â‰« fâ‚‚â‚ â‰« biprod.inl +
    biprod.snd â‰« fâ‚‚â‚‚ â‰« biprod.inr
#align category_theory.biprod.of_components CategoryTheory.Biprod.ofComponents

@[simp]
theorem Biprod.inl_ofComponents :
    biprod.inl â‰« Biprod.ofComponents fâ‚â‚ fâ‚â‚‚ fâ‚‚â‚ fâ‚‚â‚‚ = fâ‚â‚ â‰« biprod.inl + fâ‚â‚‚ â‰« biprod.inr := by
  simp [Biprod.ofComponents]
#align category_theory.biprod.inl_of_components CategoryTheory.Biprod.inl_ofComponents

@[simp]
theorem Biprod.inr_ofComponents :
    biprod.inr â‰« Biprod.ofComponents fâ‚â‚ fâ‚â‚‚ fâ‚‚â‚ fâ‚‚â‚‚ = fâ‚‚â‚ â‰« biprod.inl + fâ‚‚â‚‚ â‰« biprod.inr := by
  simp [Biprod.ofComponents]
#align category_theory.biprod.inr_of_components CategoryTheory.Biprod.inr_ofComponents

@[simp]
theorem Biprod.ofComponents_fst :
    Biprod.ofComponents fâ‚â‚ fâ‚â‚‚ fâ‚‚â‚ fâ‚‚â‚‚ â‰« biprod.fst = biprod.fst â‰« fâ‚â‚ + biprod.snd â‰« fâ‚‚â‚ := by
  simp [Biprod.ofComponents]
#align category_theory.biprod.of_components_fst CategoryTheory.Biprod.ofComponents_fst

@[simp]
theorem Biprod.ofComponents_snd :
    Biprod.ofComponents fâ‚â‚ fâ‚â‚‚ fâ‚‚â‚ fâ‚‚â‚‚ â‰« biprod.snd = biprod.fst â‰« fâ‚â‚‚ + biprod.snd â‰« fâ‚‚â‚‚ := by
  simp [Biprod.ofComponents]
#align category_theory.biprod.of_components_snd CategoryTheory.Biprod.ofComponents_snd

@[simp]
theorem Biprod.ofComponents_eq (f : Xâ‚ âŠ Xâ‚‚ âŸ¶ Yâ‚ âŠ Yâ‚‚) :
    Biprod.ofComponents (biprod.inl â‰« f â‰« biprod.fst) (biprod.inl â‰« f â‰« biprod.snd)
        (biprod.inr â‰« f â‰« biprod.fst) (biprod.inr â‰« f â‰« biprod.snd) =
      f := by
  ext <;>
    simp only [Category.comp_id, biprod.inr_fst, biprod.inr_snd, biprod.inl_snd, add_zero, zero_add,
      Biprod.inl_ofComponents, Biprod.inr_ofComponents, eq_self_iff_true, Category.assoc,
      comp_zero, biprod.inl_fst, Preadditive.add_comp]
#align category_theory.biprod.of_components_eq CategoryTheory.Biprod.ofComponents_eq

@[simp]
theorem Biprod.ofComponents_comp {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ Zâ‚ Zâ‚‚ : C} (fâ‚â‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚â‚‚ : Xâ‚ âŸ¶ Yâ‚‚)
    (fâ‚‚â‚ : Xâ‚‚ âŸ¶ Yâ‚) (fâ‚‚â‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (gâ‚â‚ : Yâ‚ âŸ¶ Zâ‚) (gâ‚â‚‚ : Yâ‚ âŸ¶ Zâ‚‚) (gâ‚‚â‚ : Yâ‚‚ âŸ¶ Zâ‚)
    (gâ‚‚â‚‚ : Yâ‚‚ âŸ¶ Zâ‚‚) :
    Biprod.ofComponents fâ‚â‚ fâ‚â‚‚ fâ‚‚â‚ fâ‚‚â‚‚ â‰« Biprod.ofComponents gâ‚â‚ gâ‚â‚‚ gâ‚‚â‚ gâ‚‚â‚‚ =
      Biprod.ofComponents (fâ‚â‚ â‰« gâ‚â‚ + fâ‚â‚‚ â‰« gâ‚‚â‚) (fâ‚â‚ â‰« gâ‚â‚‚ + fâ‚â‚‚ â‰« gâ‚‚â‚‚) (fâ‚‚â‚ â‰« gâ‚â‚ + fâ‚‚â‚‚ â‰« gâ‚‚â‚)
        (fâ‚‚â‚ â‰« gâ‚â‚‚ + fâ‚‚â‚‚ â‰« gâ‚‚â‚‚) := by
  dsimp [Biprod.ofComponents]
  ext <;>
    simp only [add_comp, comp_add, add_comp_assoc, add_zero, zero_add, biprod.inl_fst,
      biprod.inl_snd, biprod.inr_fst, biprod.inr_snd, biprod.inl_fst_assoc, biprod.inl_snd_assoc,
      biprod.inr_fst_assoc, biprod.inr_snd_assoc, comp_zero, zero_comp, Category.assoc]
#align category_theory.biprod.of_components_comp CategoryTheory.Biprod.ofComponents_comp

/-- The unipotent upper triangular matrix
```
(1 r)
(0 1)
```
as an isomorphism.
-/
@[simps]
def Biprod.unipotentUpper {Xâ‚ Xâ‚‚ : C} (r : Xâ‚ âŸ¶ Xâ‚‚) : Xâ‚ âŠ Xâ‚‚ â‰… Xâ‚ âŠ Xâ‚‚ where
  hom := Biprod.ofComponents (ğŸ™ _) r 0 (ğŸ™ _)
  inv := Biprod.ofComponents (ğŸ™ _) (-r) 0 (ğŸ™ _)
#align category_theory.biprod.unipotent_upper CategoryTheory.Biprod.unipotentUpper

/-- The unipotent lower triangular matrix
```
(1 0)
(r 1)
```
as an isomorphism.
-/
@[simps]
def Biprod.unipotentLower {Xâ‚ Xâ‚‚ : C} (r : Xâ‚‚ âŸ¶ Xâ‚) : Xâ‚ âŠ Xâ‚‚ â‰… Xâ‚ âŠ Xâ‚‚ where
  hom := Biprod.ofComponents (ğŸ™ _) 0 r (ğŸ™ _)
  inv := Biprod.ofComponents (ğŸ™ _) 0 (-r) (ğŸ™ _)
#align category_theory.biprod.unipotent_lower CategoryTheory.Biprod.unipotentLower

/-- If `f` is a morphism `Xâ‚ âŠ Xâ‚‚ âŸ¶ Yâ‚ âŠ Yâ‚‚` whose `Xâ‚ âŸ¶ Yâ‚` entry is an isomorphism,
then we can construct isomorphisms `L : Xâ‚ âŠ Xâ‚‚ â‰… Xâ‚ âŠ Xâ‚‚` and `R : Yâ‚ âŠ Yâ‚‚ â‰… Yâ‚ âŠ Yâ‚‚`
so that `L.hom â‰« g â‰« R.hom` is diagonal (with `Xâ‚ âŸ¶ Yâ‚` component still `f`),
via Gaussian elimination.

(This is the version of `Biprod.gaussian` written in terms of components.)
-/
def Biprod.gaussian' [IsIso fâ‚â‚] :
    Î£' (L : Xâ‚ âŠ Xâ‚‚ â‰… Xâ‚ âŠ Xâ‚‚) (R : Yâ‚ âŠ Yâ‚‚ â‰… Yâ‚ âŠ Yâ‚‚) (gâ‚‚â‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚),
      L.hom â‰« Biprod.ofComponents fâ‚â‚ fâ‚â‚‚ fâ‚‚â‚ fâ‚‚â‚‚ â‰« R.hom = biprod.map fâ‚â‚ gâ‚‚â‚‚ :=
  âŸ¨Biprod.unipotentLower (-fâ‚‚â‚ â‰« inv fâ‚â‚), Biprod.unipotentUpper (-inv fâ‚â‚ â‰« fâ‚â‚‚),
    fâ‚‚â‚‚ - fâ‚‚â‚ â‰« inv fâ‚â‚ â‰« fâ‚â‚‚, by ext <;> simp; abelâŸ©
#align category_theory.biprod.gaussian' CategoryTheory.Biprod.gaussian'

/-- If `f` is a morphism `Xâ‚ âŠ Xâ‚‚ âŸ¶ Yâ‚ âŠ Yâ‚‚` whose `Xâ‚ âŸ¶ Yâ‚` entry is an isomorphism,
then we can construct isomorphisms `L : Xâ‚ âŠ Xâ‚‚ â‰… Xâ‚ âŠ Xâ‚‚` and `R : Yâ‚ âŠ Yâ‚‚ â‰… Yâ‚ âŠ Yâ‚‚`
so that `L.hom â‰« g â‰« R.hom` is diagonal (with `Xâ‚ âŸ¶ Yâ‚` component still `f`),
via Gaussian elimination.
-/
def Biprod.gaussian (f : Xâ‚ âŠ Xâ‚‚ âŸ¶ Yâ‚ âŠ Yâ‚‚) [IsIso (biprod.inl â‰« f â‰« biprod.fst)] :
    Î£' (L : Xâ‚ âŠ Xâ‚‚ â‰… Xâ‚ âŠ Xâ‚‚) (R : Yâ‚ âŠ Yâ‚‚ â‰… Yâ‚ âŠ Yâ‚‚) (gâ‚‚â‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚),
      L.hom â‰« f â‰« R.hom = biprod.map (biprod.inl â‰« f â‰« biprod.fst) gâ‚‚â‚‚ := by
  let this :=
    Biprod.gaussian' (biprod.inl â‰« f â‰« biprod.fst) (biprod.inl â‰« f â‰« biprod.snd)
      (biprod.inr â‰« f â‰« biprod.fst) (biprod.inr â‰« f â‰« biprod.snd)
  rwa [Biprod.ofComponents_eq] at this
#align category_theory.biprod.gaussian CategoryTheory.Biprod.gaussian

/-- If `Xâ‚ âŠ Xâ‚‚ â‰… Yâ‚ âŠ Yâ‚‚` via a two-by-two matrix whose `Xâ‚ âŸ¶ Yâ‚` entry is an isomorphism,
then we can construct an isomorphism `Xâ‚‚ â‰… Yâ‚‚`, via Gaussian elimination.
-/
def Biprod.isoElim' [IsIso fâ‚â‚] [IsIso (Biprod.ofComponents fâ‚â‚ fâ‚â‚‚ fâ‚‚â‚ fâ‚‚â‚‚)] : Xâ‚‚ â‰… Yâ‚‚ := by
  obtain âŸ¨L, R, g, wâŸ© := Biprod.gaussian' fâ‚â‚ fâ‚â‚‚ fâ‚‚â‚ fâ‚‚â‚‚
  letI : IsIso (biprod.map fâ‚â‚ g) := by
    rw [â† w]
    infer_instance
  letI : IsIso g := isIso_right_of_isIso_biprod_map fâ‚â‚ g
  exact asIso g
#align category_theory.biprod.iso_elim' CategoryTheory.Biprod.isoElim'

/-- If `f` is an isomorphism `Xâ‚ âŠ Xâ‚‚ â‰… Yâ‚ âŠ Yâ‚‚` whose `Xâ‚ âŸ¶ Yâ‚` entry is an isomorphism,
then we can construct an isomorphism `Xâ‚‚ â‰… Yâ‚‚`, via Gaussian elimination.
-/
def Biprod.isoElim (f : Xâ‚ âŠ Xâ‚‚ â‰… Yâ‚ âŠ Yâ‚‚) [IsIso (biprod.inl â‰« f.hom â‰« biprod.fst)] : Xâ‚‚ â‰… Yâ‚‚ :=
  letI :
    IsIso
      (Biprod.ofComponents (biprod.inl â‰« f.hom â‰« biprod.fst) (biprod.inl â‰« f.hom â‰« biprod.snd)
        (biprod.inr â‰« f.hom â‰« biprod.fst) (biprod.inr â‰« f.hom â‰« biprod.snd)) := by
    simp only [Biprod.ofComponents_eq]
    infer_instance
  Biprod.isoElim' (biprod.inl â‰« f.hom â‰« biprod.fst) (biprod.inl â‰« f.hom â‰« biprod.snd)
    (biprod.inr â‰« f.hom â‰« biprod.fst) (biprod.inr â‰« f.hom â‰« biprod.snd)
#align category_theory.biprod.iso_elim CategoryTheory.Biprod.isoElim

theorem Biprod.column_nonzero_of_iso {W X Y Z : C} (f : W âŠ X âŸ¶ Y âŠ Z) [IsIso f] :
    ğŸ™ W = 0 âˆ¨ biprod.inl â‰« f â‰« biprod.fst â‰  0 âˆ¨ biprod.inl â‰« f â‰« biprod.snd â‰  0 := by
  by_contra! h
  rcases h with âŸ¨nz, aâ‚, aâ‚‚âŸ©
  set x := biprod.inl â‰« f â‰« inv f â‰« biprod.fst
  have hâ‚ : x = ğŸ™ W := by simp [x]
  have hâ‚€ : x = 0 := by
    dsimp [x]
    rw [â† Category.id_comp (inv f), Category.assoc, â† biprod.total]
    conv_lhs =>
      slice 2 3
      rw [comp_add]
    simp only [Category.assoc]
    rw [comp_add_assoc, add_comp]
    conv_lhs =>
      congr
      next => skip
      slice 1 3
      rw [aâ‚‚]
    simp only [zero_comp, add_zero]
    conv_lhs =>
      slice 1 3
      rw [aâ‚]
    simp only [zero_comp]
  exact nz (hâ‚.symm.trans hâ‚€)
#align category_theory.biprod.column_nonzero_of_iso CategoryTheory.Biprod.column_nonzero_of_iso

end

theorem Biproduct.column_nonzero_of_iso' {Ïƒ Ï„ : Type} [Finite Ï„] {S : Ïƒ â†’ C} [HasBiproduct S]
    {T : Ï„ â†’ C} [HasBiproduct T] (s : Ïƒ) (f : â¨ S âŸ¶ â¨ T) [IsIso f] :
    (âˆ€ t : Ï„, biproduct.Î¹ S s â‰« f â‰« biproduct.Ï€ T t = 0) â†’ ğŸ™ (S s) = 0 := by
  cases nonempty_fintype Ï„
  intro z
  have reassoced {t : Ï„} {W : C} (h : _ âŸ¶ W) :
    biproduct.Î¹ S s â‰« f â‰« biproduct.Ï€ T t â‰« h = 0 â‰« h := by
    simp only [â† Category.assoc]
    apply eq_whisker
    simp only [Category.assoc]
    apply z
  set x := biproduct.Î¹ S s â‰« f â‰« inv f â‰« biproduct.Ï€ S s
  have hâ‚ : x = ğŸ™ (S s) := by simp [x]
  have hâ‚€ : x = 0 := by
    dsimp [x]
    rw [â† Category.id_comp (inv f), Category.assoc, â† biproduct.total]
    simp only [comp_sum_assoc]
    conv_lhs =>
      congr
      congr
      next => skip
      intro j; simp only [reassoced]
    simp
  exact hâ‚.symm.trans hâ‚€
#align category_theory.biproduct.column_nonzero_of_iso' CategoryTheory.Biproduct.column_nonzero_of_iso'

/-- If `f : â¨ S âŸ¶ â¨ T` is an isomorphism, and `s` is a non-trivial summand of the source,
then there is some `t` in the target so that the `s, t` matrix entry of `f` is nonzero.
-/
def Biproduct.columnNonzeroOfIso {Ïƒ Ï„ : Type} [Fintype Ï„] {S : Ïƒ â†’ C} [HasBiproduct S] {T : Ï„ â†’ C}
    [HasBiproduct T] (s : Ïƒ) (nz : ğŸ™ (S s) â‰  0) (f : â¨ S âŸ¶ â¨ T) [IsIso f] :
    Trunc (Î£'t : Ï„, biproduct.Î¹ S s â‰« f â‰« biproduct.Ï€ T t â‰  0) := by
  classical
    apply truncSigmaOfExists
    have t := Biproduct.column_nonzero_of_iso'.{v} s f
    by_contra h
    simp only [not_exists_not] at h
    exact nz (t h)
#align category_theory.biproduct.column_nonzero_of_iso CategoryTheory.Biproduct.columnNonzeroOfIso

section Preadditive

variable {D : Type u'} [Category.{v'} D] [Preadditive.{v'} D]
variable (F : C â¥¤ D) [PreservesZeroMorphisms F]

namespace Limits

section Fintype

variable {J : Type} [Fintype J]

/-- A functor between preadditive categories that preserves (zero morphisms and) finite biproducts
    preserves finite products. -/
def preservesProductOfPreservesBiproduct {f : J â†’ C} [PreservesBiproduct f F] :
    PreservesLimit (Discrete.functor f) F where
  preserves hc :=
    IsLimit.ofIsoLimit
        ((IsLimit.postcomposeInvEquiv (Discrete.compNatIsoDiscrete _ _) _).symm
          (isBilimitOfPreserves F (biconeIsBilimitOfLimitConeOfIsLimit hc)).isLimit) <|
      Cones.ext (Iso.refl _) (by rintro âŸ¨âŸ©; simp)
#align category_theory.limits.preserves_product_of_preserves_biproduct CategoryTheory.Limits.preservesProductOfPreservesBiproduct

section

attribute [local instance] preservesProductOfPreservesBiproduct

/-- A functor between preadditive categories that preserves (zero morphisms and) finite biproducts
    preserves finite products. -/
def preservesProductsOfShapeOfPreservesBiproductsOfShape [PreservesBiproductsOfShape J F] :
    PreservesLimitsOfShape (Discrete J) F where
  preservesLimit {_} := preservesLimitOfIsoDiagram _ Discrete.natIsoFunctor.symm
#align category_theory.limits.preserves_products_of_shape_of_preserves_biproducts_of_shape CategoryTheory.Limits.preservesProductsOfShapeOfPreservesBiproductsOfShape

end

/-- A functor between preadditive categories that preserves (zero morphisms and) finite products
    preserves finite biproducts. -/
def preservesBiproductOfPreservesProduct {f : J â†’ C} [PreservesLimit (Discrete.functor f) F] :
    PreservesBiproduct f F where
  preserves {b} hb :=
    isBilimitOfIsLimit _ <|
      IsLimit.ofIsoLimit
          ((IsLimit.postcomposeHomEquiv (Discrete.compNatIsoDiscrete _ _) (F.mapCone b.toCone)).symm
            (isLimitOfPreserves F hb.isLimit)) <|
        Cones.ext (Iso.refl _) (by rintro âŸ¨âŸ©; simp)
#align category_theory.limits.preserves_biproduct_of_preserves_product CategoryTheory.Limits.preservesBiproductOfPreservesProduct

/-- If the (product-like) biproduct comparison for `F` and `f` is a monomorphism, then `F`
    preserves the biproduct of `f`. For the converse, see `mapBiproduct`. -/
def preservesBiproductOfMonoBiproductComparison {f : J â†’ C} [HasBiproduct f]
    [HasBiproduct (F.obj âˆ˜ f)] [Mono (biproductComparison F f)] : PreservesBiproduct f F := by
  haveI : HasProduct fun b => F.obj (f b) := by
    change HasProduct (F.obj âˆ˜ f)
    infer_instance
  have that : piComparison F f =
      (F.mapIso (biproduct.isoProduct f)).inv â‰«
        biproductComparison F f â‰« (biproduct.isoProduct _).hom := by
    ext j
    convert piComparison_comp_Ï€ F f j; simp [â† Functor.map_comp]
  haveI : IsIso (biproductComparison F f) := isIso_of_mono_of_isSplitEpi _
  haveI : IsIso (piComparison F f) := by
    rw [that]
    infer_instance
  haveI := PreservesProduct.ofIsoComparison F f
  apply preservesBiproductOfPreservesProduct
#align category_theory.limits.preserves_biproduct_of_mono_biproduct_comparison CategoryTheory.Limits.preservesBiproductOfMonoBiproductComparison

/-- If the (coproduct-like) biproduct comparison for `F` and `f` is an epimorphism, then `F`
    preserves the biproduct of `F` and `f`. For the converse, see `mapBiproduct`. -/
def preservesBiproductOfEpiBiproductComparison' {f : J â†’ C} [HasBiproduct f]
    [HasBiproduct (F.obj âˆ˜ f)] [Epi (biproductComparison' F f)] : PreservesBiproduct f F := by
  haveI : Epi (splitEpiBiproductComparison F f).section_ := by simpa
  haveI : IsIso (biproductComparison F f) :=
    IsIso.of_epi_section' (splitEpiBiproductComparison F f)
  apply preservesBiproductOfMonoBiproductComparison
#align category_theory.limits.preserves_biproduct_of_epi_biproduct_comparison' CategoryTheory.Limits.preservesBiproductOfEpiBiproductComparison'

/-- A functor between preadditive categories that preserves (zero morphisms and) finite products
    preserves finite biproducts. -/
def preservesBiproductsOfShapeOfPreservesProductsOfShape [PreservesLimitsOfShape (Discrete J) F] :
    PreservesBiproductsOfShape J F where
  preserves {_} := preservesBiproductOfPreservesProduct F
#align category_theory.limits.preserves_biproducts_of_shape_of_preserves_products_of_shape CategoryTheory.Limits.preservesBiproductsOfShapeOfPreservesProductsOfShape

/-- A functor between preadditive categories that preserves (zero morphisms and) finite biproducts
    preserves finite coproducts. -/
def preservesCoproductOfPreservesBiproduct {f : J â†’ C} [PreservesBiproduct f F] :
    PreservesColimit (Discrete.functor f) F where
  preserves {c} hc :=
    IsColimit.ofIsoColimit
        ((IsColimit.precomposeHomEquiv (Discrete.compNatIsoDiscrete _ _) _).symm
          (isBilimitOfPreserves F (biconeIsBilimitOfColimitCoconeOfIsColimit hc)).isColimit) <|
      Cocones.ext (Iso.refl _) (by rintro âŸ¨âŸ©; simp)
#align category_theory.limits.preserves_coproduct_of_preserves_biproduct CategoryTheory.Limits.preservesCoproductOfPreservesBiproduct

section

attribute [local instance] preservesCoproductOfPreservesBiproduct

/-- A functor between preadditive categories that preserves (zero morphisms and) finite biproducts
    preserves finite coproducts. -/
def preservesCoproductsOfShapeOfPreservesBiproductsOfShape [PreservesBiproductsOfShape J F] :
    PreservesColimitsOfShape (Discrete J) F where
  preservesColimit {_} := preservesColimitOfIsoDiagram _ Discrete.natIsoFunctor.symm
#align category_theory.limits.preserves_coproducts_of_shape_of_preserves_biproducts_of_shape CategoryTheory.Limits.preservesCoproductsOfShapeOfPreservesBiproductsOfShape

end

/-- A functor between preadditive categories that preserves (zero morphisms and) finite coproducts
    preserves finite biproducts. -/
def preservesBiproductOfPreservesCoproduct {f : J â†’ C} [PreservesColimit (Discrete.functor f) F] :
    PreservesBiproduct f F where
  preserves {b} hb :=
    isBilimitOfIsColimit _ <|
      IsColimit.ofIsoColimit
          ((IsColimit.precomposeInvEquiv (Discrete.compNatIsoDiscrete _ _)
                (F.mapCocone b.toCocone)).symm
            (isColimitOfPreserves F hb.isColimit)) <|
        Cocones.ext (Iso.refl _) (by rintro âŸ¨âŸ©; simp)
#align category_theory.limits.preserves_biproduct_of_preserves_coproduct CategoryTheory.Limits.preservesBiproductOfPreservesCoproduct

/-- A functor between preadditive categories that preserves (zero morphisms and) finite coproducts
    preserves finite biproducts. -/
def preservesBiproductsOfShapeOfPreservesCoproductsOfShape
    [PreservesColimitsOfShape (Discrete J) F] : PreservesBiproductsOfShape J F where
  preserves {_} := preservesBiproductOfPreservesCoproduct F
#align category_theory.limits.preserves_biproducts_of_shape_of_preserves_coproducts_of_shape CategoryTheory.Limits.preservesBiproductsOfShapeOfPreservesCoproductsOfShape

end Fintype

/-- A functor between preadditive categories that preserves (zero morphisms and) binary biproducts
    preserves binary products. -/
def preservesBinaryProductOfPreservesBinaryBiproduct {X Y : C} [PreservesBinaryBiproduct X Y F] :
    PreservesLimit (pair X Y) F where
  preserves {c} hc := IsLimit.ofIsoLimit
        ((IsLimit.postcomposeInvEquiv (diagramIsoPair _) _).symm
          (isBinaryBilimitOfPreserves F (binaryBiconeIsBilimitOfLimitConeOfIsLimit hc)).isLimit) <|
      Cones.ext (by dsimp; rfl) fun j => by
        rcases j with âŸ¨âŸ¨âŸ©âŸ© <;> simp
#align category_theory.limits.preserves_binary_product_of_preserves_binary_biproduct CategoryTheory.Limits.preservesBinaryProductOfPreservesBinaryBiproduct

section

attribute [local instance] preservesBinaryProductOfPreservesBinaryBiproduct

/-- A functor between preadditive categories that preserves (zero morphisms and) binary biproducts
    preserves binary products. -/
def preservesBinaryProductsOfPreservesBinaryBiproducts [PreservesBinaryBiproducts F] :
    PreservesLimitsOfShape (Discrete WalkingPair) F where
  preservesLimit {_} := preservesLimitOfIsoDiagram _ (diagramIsoPair _).symm
#align category_theory.limits.preserves_binary_products_of_preserves_binary_biproducts CategoryTheory.Limits.preservesBinaryProductsOfPreservesBinaryBiproducts

end

/-- A functor between preadditive categories that preserves (zero morphisms and) binary products
    preserves binary biproducts. -/
def preservesBinaryBiproductOfPreservesBinaryProduct {X Y : C} [PreservesLimit (pair X Y) F] :
    PreservesBinaryBiproduct X Y F where
  preserves {b} hb := isBinaryBilimitOfIsLimit _ <| IsLimit.ofIsoLimit
          ((IsLimit.postcomposeHomEquiv (diagramIsoPair _) (F.mapCone b.toCone)).symm
            (isLimitOfPreserves F hb.isLimit)) <|
        Cones.ext (by dsimp; rfl) fun j => by
          rcases j with âŸ¨âŸ¨âŸ©âŸ© <;> simp
#align category_theory.limits.preserves_binary_biproduct_of_preserves_binary_product CategoryTheory.Limits.preservesBinaryBiproductOfPreservesBinaryProduct

/-- If the (product-like) biproduct comparison for `F`, `X` and `Y` is a monomorphism, then
    `F` preserves the biproduct of `X` and `Y`. For the converse, see `map_biprod`. -/
def preservesBinaryBiproductOfMonoBiprodComparison {X Y : C} [HasBinaryBiproduct X Y]
    [HasBinaryBiproduct (F.obj X) (F.obj Y)] [Mono (biprodComparison F X Y)] :
    PreservesBinaryBiproduct X Y F := by
  have that :
    prodComparison F X Y =
      (F.mapIso (biprod.isoProd X Y)).inv â‰« biprodComparison F X Y â‰« (biprod.isoProd _ _).hom := by
    ext <;> simp [â† Functor.map_comp]
  haveI : IsIso (biprodComparison F X Y) := isIso_of_mono_of_isSplitEpi _
  haveI : IsIso (prodComparison F X Y) := by
    rw [that]
    infer_instance
  haveI := PreservesLimitPair.ofIsoProdComparison F X Y
  apply preservesBinaryBiproductOfPreservesBinaryProduct
#align category_theory.limits.preserves_binary_biproduct_of_mono_biprod_comparison CategoryTheory.Limits.preservesBinaryBiproductOfMonoBiprodComparison

/-- If the (coproduct-like) biproduct comparison for `F`, `X` and `Y` is an epimorphism, then
    `F` preserves the biproduct of `X` and `Y`. For the converse, see `mapBiprod`. -/
def preservesBinaryBiproductOfEpiBiprodComparison' {X Y : C} [HasBinaryBiproduct X Y]
    [HasBinaryBiproduct (F.obj X) (F.obj Y)] [Epi (biprodComparison' F X Y)] :
    PreservesBinaryBiproduct X Y F := by
  haveI : Epi (splitEpiBiprodComparison F X Y).section_ := by simpa
  haveI : IsIso (biprodComparison F X Y) :=
    IsIso.of_epi_section' (splitEpiBiprodComparison F X Y)
  apply preservesBinaryBiproductOfMonoBiprodComparison
#align category_theory.limits.preserves_binary_biproduct_of_epi_biprod_comparison' CategoryTheory.Limits.preservesBinaryBiproductOfEpiBiprodComparison'

/-- A functor between preadditive categories that preserves (zero morphisms and) binary products
    preserves binary biproducts. -/
def preservesBinaryBiproductsOfPreservesBinaryProducts
    [PreservesLimitsOfShape (Discrete WalkingPair) F] : PreservesBinaryBiproducts F where
  preserves {_} {_} := preservesBinaryBiproductOfPreservesBinaryProduct F
#align category_theory.limits.preserves_binary_biproducts_of_preserves_binary_products CategoryTheory.Limits.preservesBinaryBiproductsOfPreservesBinaryProducts

/-- A functor between preadditive categories that preserves (zero morphisms and) binary biproducts
    preserves binary coproducts. -/
def preservesBinaryCoproductOfPreservesBinaryBiproduct {X Y : C} [PreservesBinaryBiproduct X Y F] :
    PreservesColimit (pair X Y) F where
  preserves {c} hc :=
    IsColimit.ofIsoColimit
        ((IsColimit.precomposeHomEquiv (diagramIsoPair _) _).symm
          (isBinaryBilimitOfPreserves F
              (binaryBiconeIsBilimitOfColimitCoconeOfIsColimit hc)).isColimit) <|
      Cocones.ext (by dsimp; rfl) fun j => by
        rcases j with âŸ¨âŸ¨âŸ©âŸ© <;> simp
#align category_theory.limits.preserves_binary_coproduct_of_preserves_binary_biproduct CategoryTheory.Limits.preservesBinaryCoproductOfPreservesBinaryBiproduct

section

attribute [local instance] preservesBinaryCoproductOfPreservesBinaryBiproduct

/-- A functor between preadditive categories that preserves (zero morphisms and) binary biproducts
    preserves binary coproducts. -/
def preservesBinaryCoproductsOfPreservesBinaryBiproducts [PreservesBinaryBiproducts F] :
    PreservesColimitsOfShape (Discrete WalkingPair) F where
  preservesColimit {_} := preservesColimitOfIsoDiagram _ (diagramIsoPair _).symm
#align category_theory.limits.preserves_binary_coproducts_of_preserves_binary_biproducts CategoryTheory.Limits.preservesBinaryCoproductsOfPreservesBinaryBiproducts

end

/-- A functor between preadditive categories that preserves (zero morphisms and) binary coproducts
    preserves binary biproducts. -/
def preservesBinaryBiproductOfPreservesBinaryCoproduct {X Y : C} [PreservesColimit (pair X Y) F] :
    PreservesBinaryBiproduct X Y F where
  preserves {b} hb :=
    isBinaryBilimitOfIsColimit _ <|
      IsColimit.ofIsoColimit
          ((IsColimit.precomposeInvEquiv (diagramIsoPair _) (F.mapCocone b.toCocone)).symm
            (isColimitOfPreserves F hb.isColimit)) <|
        Cocones.ext (Iso.refl _) fun j => by
          rcases j with âŸ¨âŸ¨âŸ©âŸ© <;> simp
#align category_theory.limits.preserves_binary_biproduct_of_preserves_binary_coproduct CategoryTheory.Limits.preservesBinaryBiproductOfPreservesBinaryCoproduct

/-- A functor between preadditive categories that preserves (zero morphisms and) binary coproducts
    preserves binary biproducts. -/
def preservesBinaryBiproductsOfPreservesBinaryCoproducts
    [PreservesColimitsOfShape (Discrete WalkingPair) F] : PreservesBinaryBiproducts F where
  preserves {_} {_} := preservesBinaryBiproductOfPreservesBinaryCoproduct F
#align category_theory.limits.preserves_binary_biproducts_of_preserves_binary_coproducts CategoryTheory.Limits.preservesBinaryBiproductsOfPreservesBinaryCoproducts

end Limits

end Preadditive

end CategoryTheory
