/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel, Scott Morrison
-/
import Mathlib.Algebra.Group.Ext
import Mathlib.CategoryTheory.Simple
import Mathlib.CategoryTheory.Linear.Basic
import Mathlib.CategoryTheory.Endomorphism
import Mathlib.FieldTheory.IsAlgClosed.Spectrum

#align_import category_theory.preadditive.schur from "leanprover-community/mathlib"@"58a272265b5e05f258161260dd2c5d247213cbd3"

/-!
# Schur's lemma
We first prove the part of Schur's Lemma that holds in any preadditive category with kernels,
that any nonzero morphism between simple objects
is an isomorphism.

Second, we prove Schur's lemma for `ğ•œ`-linear categories with finite dimensional hom spaces,
over an algebraically closed field `ğ•œ`:
the hom space `X âŸ¶ Y` between simple objects `X` and `Y` is at most one dimensional,
and is 1-dimensional iff `X` and `Y` are isomorphic.
-/


namespace CategoryTheory

open CategoryTheory.Limits

variable {C : Type*} [Category C]
variable [Preadditive C]

-- See also `epi_of_nonzero_to_simple`, which does not require `Preadditive C`.
theorem mono_of_nonzero_from_simple [HasKernels C] {X Y : C} [Simple X] {f : X âŸ¶ Y} (w : f â‰  0) :
    Mono f :=
  Preadditive.mono_of_kernel_zero (kernel_zero_of_nonzero_from_simple w)
#align category_theory.mono_of_nonzero_from_simple CategoryTheory.mono_of_nonzero_from_simple

/-- The part of **Schur's lemma** that holds in any preadditive category with kernels:
that a nonzero morphism between simple objects is an isomorphism.
-/
theorem isIso_of_hom_simple
    [HasKernels C] {X Y : C} [Simple X] [Simple Y] {f : X âŸ¶ Y} (w : f â‰  0) : IsIso f :=
  haveI := mono_of_nonzero_from_simple w
  isIso_of_mono_of_nonzero w
#align category_theory.is_iso_of_hom_simple CategoryTheory.isIso_of_hom_simple

/-- As a corollary of Schur's lemma for preadditive categories,
any morphism between simple objects is (exclusively) either an isomorphism or zero.
-/
theorem isIso_iff_nonzero [HasKernels C] {X Y : C} [Simple X] [Simple Y] (f : X âŸ¶ Y) :
    IsIso f â†” f â‰  0 :=
  âŸ¨fun I => by
    intro h
    apply id_nonzero X
    simp only [â† IsIso.hom_inv_id f, h, zero_comp],
   fun w => isIso_of_hom_simple wâŸ©
#align category_theory.is_iso_iff_nonzero CategoryTheory.isIso_iff_nonzero

open scoped Classical in
/-- In any preadditive category with kernels,
the endomorphisms of a simple object form a division ring. -/
noncomputable instance [HasKernels C] {X : C} [Simple X] : DivisionRing (End X) where
  inv f := if h : f = 0 then 0 else haveI := isIso_of_hom_simple h; inv f
  exists_pair_ne := âŸ¨ğŸ™ X, 0, id_nonzero _âŸ©
  inv_zero := dif_pos rfl
  mul_inv_cancel f hf := by
    dsimp
    rw [dif_neg hf]
    haveI := isIso_of_hom_simple hf
    exact IsIso.inv_hom_id f
  nnqsmul := _
  qsmul := _

open FiniteDimensional

section

variable (ğ•œ : Type*) [DivisionRing ğ•œ]

/-- Part of **Schur's lemma** for `ğ•œ`-linear categories:
the hom space between two non-isomorphic simple objects is 0-dimensional.
-/
theorem finrank_hom_simple_simple_eq_zero_of_not_iso [HasKernels C] [Linear ğ•œ C] {X Y : C}
    [Simple X] [Simple Y] (h : (X â‰… Y) â†’ False) : finrank ğ•œ (X âŸ¶ Y) = 0 :=
  haveI :=
    subsingleton_of_forall_eq (0 : X âŸ¶ Y) fun f => by
      have p := not_congr (isIso_iff_nonzero f)
      simp only [Classical.not_not, Ne] at p
      exact p.mp fun _ => h (asIso f)
  finrank_zero_of_subsingleton
#align category_theory.finrank_hom_simple_simple_eq_zero_of_not_iso CategoryTheory.finrank_hom_simple_simple_eq_zero_of_not_iso

end

variable (ğ•œ : Type*) [Field ğ•œ]
variable [IsAlgClosed ğ•œ] [Linear ğ•œ C]

-- Porting note: the defeq issue in lean3 described below is no longer a problem in Lean4.
-- In the proof below we have some difficulty using `I : FiniteDimensional ğ•œ (X âŸ¶ X)`
-- where we need a `FiniteDimensional ğ•œ (End X)`.
-- These are definitionally equal, but without eta reduction Lean can't see this.
-- To get around this, we use `convert I`,
-- then check the various instances agree field-by-field,
-- We prove this with the explicit `isIso_iff_nonzero` assumption,
-- rather than just `[Simple X]`, as this form is useful for
-- MÃ¼ger's formulation of semisimplicity.
/-- An auxiliary lemma for Schur's lemma.

If `X âŸ¶ X` is finite dimensional, and every nonzero endomorphism is invertible,
then `X âŸ¶ X` is 1-dimensional.
-/
theorem finrank_endomorphism_eq_one {X : C} (isIso_iff_nonzero : âˆ€ f : X âŸ¶ X, IsIso f â†” f â‰  0)
    [I : FiniteDimensional ğ•œ (X âŸ¶ X)] : finrank ğ•œ (X âŸ¶ X) = 1 := by
  have id_nonzero := (isIso_iff_nonzero (ğŸ™ X)).mp (by infer_instance)
  refine finrank_eq_one (ğŸ™ X) id_nonzero ?_
  intro f
  have : Nontrivial (End X) := nontrivial_of_ne _ _ id_nonzero
  have : FiniteDimensional ğ•œ (End X) := I
  obtain âŸ¨c, nuâŸ© := spectrum.nonempty_of_isAlgClosed_of_finiteDimensional ğ•œ (End.of f)
  use c
  rw [spectrum.mem_iff, IsUnit.sub_iff, isUnit_iff_isIso, isIso_iff_nonzero, Ne,
    Classical.not_not, sub_eq_zero, Algebra.algebraMap_eq_smul_one] at nu
  exact nu.symm
#align category_theory.finrank_endomorphism_eq_one CategoryTheory.finrank_endomorphism_eq_one

variable [HasKernels C]

/-- **Schur's lemma** for endomorphisms in `ğ•œ`-linear categories.
-/
theorem finrank_endomorphism_simple_eq_one (X : C) [Simple X] [FiniteDimensional ğ•œ (X âŸ¶ X)] :
    finrank ğ•œ (X âŸ¶ X) = 1 :=
  finrank_endomorphism_eq_one ğ•œ isIso_iff_nonzero
#align category_theory.finrank_endomorphism_simple_eq_one CategoryTheory.finrank_endomorphism_simple_eq_one

theorem endomorphism_simple_eq_smul_id {X : C} [Simple X] [FiniteDimensional ğ•œ (X âŸ¶ X)]
    (f : X âŸ¶ X) : âˆƒ c : ğ•œ, c â€¢ ğŸ™ X = f :=
  (finrank_eq_one_iff_of_nonzero' (ğŸ™ X) (id_nonzero X)).mp (finrank_endomorphism_simple_eq_one ğ•œ X)
    f
#align category_theory.endomorphism_simple_eq_smul_id CategoryTheory.endomorphism_simple_eq_smul_id

/-- Endomorphisms of a simple object form a field if they are finite dimensional.
This can't be an instance as `ğ•œ` would be undetermined.
-/
noncomputable def fieldEndOfFiniteDimensional (X : C) [Simple X] [I : FiniteDimensional ğ•œ (X âŸ¶ X)] :
    Field (End X) := by
  classical exact
    { (inferInstance : DivisionRing (End X)) with
      mul_comm := fun f g => by
        obtain âŸ¨c, rflâŸ© := endomorphism_simple_eq_smul_id ğ•œ f
        obtain âŸ¨d, rflâŸ© := endomorphism_simple_eq_smul_id ğ•œ g
        simp [â† mul_smul, mul_comm c d] }
#align category_theory.field_End_of_finite_dimensional CategoryTheory.fieldEndOfFiniteDimensional

-- There is a symmetric argument that uses `[FiniteDimensional ğ•œ (Y âŸ¶ Y)]` instead,
-- but we don't bother proving that here.
/-- **Schur's lemma** for `ğ•œ`-linear categories:
if hom spaces are finite dimensional, then the hom space between simples is at most 1-dimensional.

See `finrank_hom_simple_simple_eq_one_iff` and `finrank_hom_simple_simple_eq_zero_iff` below
for the refinements when we know whether or not the simples are isomorphic.
-/
theorem finrank_hom_simple_simple_le_one (X Y : C) [FiniteDimensional ğ•œ (X âŸ¶ X)] [Simple X]
    [Simple Y] : finrank ğ•œ (X âŸ¶ Y) â‰¤ 1 := by
  obtain (h|h) := subsingleton_or_nontrivial (X âŸ¶ Y)
  Â· rw [finrank_zero_of_subsingleton]
    exact zero_le_one
  Â· obtain âŸ¨f, nzâŸ© := (nontrivial_iff_exists_ne 0).mp h
    haveI fi := (isIso_iff_nonzero f).mpr nz
    refine finrank_le_one f ?_
    intro g
    obtain âŸ¨c, wâŸ© := endomorphism_simple_eq_smul_id ğ•œ (g â‰« inv f)
    exact âŸ¨c, by simpa using w =â‰« fâŸ©
#align category_theory.finrank_hom_simple_simple_le_one CategoryTheory.finrank_hom_simple_simple_le_one

theorem finrank_hom_simple_simple_eq_one_iff (X Y : C) [FiniteDimensional ğ•œ (X âŸ¶ X)]
    [FiniteDimensional ğ•œ (X âŸ¶ Y)] [Simple X] [Simple Y] :
    finrank ğ•œ (X âŸ¶ Y) = 1 â†” Nonempty (X â‰… Y) := by
  fconstructor
  Â· intro h
    rw [finrank_eq_one_iff'] at h
    obtain âŸ¨f, nz, -âŸ© := h
    rw [â† isIso_iff_nonzero] at nz
    exact âŸ¨asIso fâŸ©
  Â· rintro âŸ¨fâŸ©
    have le_one := finrank_hom_simple_simple_le_one ğ•œ X Y
    have zero_lt : 0 < finrank ğ•œ (X âŸ¶ Y) :=
      finrank_pos_iff_exists_ne_zero.mpr âŸ¨f.hom, (isIso_iff_nonzero f.hom).mp inferInstanceâŸ©
    omega
#align category_theory.finrank_hom_simple_simple_eq_one_iff CategoryTheory.finrank_hom_simple_simple_eq_one_iff

theorem finrank_hom_simple_simple_eq_zero_iff (X Y : C) [FiniteDimensional ğ•œ (X âŸ¶ X)]
    [FiniteDimensional ğ•œ (X âŸ¶ Y)] [Simple X] [Simple Y] :
    finrank ğ•œ (X âŸ¶ Y) = 0 â†” IsEmpty (X â‰… Y) := by
  rw [â† not_nonempty_iff, â† not_congr (finrank_hom_simple_simple_eq_one_iff ğ•œ X Y)]
  refine âŸ¨fun h => by rw [h]; simp, fun h => ?_âŸ©
  have := finrank_hom_simple_simple_le_one ğ•œ X Y
  interval_cases finrank ğ•œ (X âŸ¶ Y)
  Â· rfl
  Â· exact False.elim (h rfl)
#align category_theory.finrank_hom_simple_simple_eq_zero_iff CategoryTheory.finrank_hom_simple_simple_eq_zero_iff

open scoped Classical

theorem finrank_hom_simple_simple (X Y : C) [âˆ€ X Y : C, FiniteDimensional ğ•œ (X âŸ¶ Y)] [Simple X]
    [Simple Y] : finrank ğ•œ (X âŸ¶ Y) = if Nonempty (X â‰… Y) then 1 else 0 := by
  split_ifs with h
  Â· exact (finrank_hom_simple_simple_eq_one_iff ğ•œ X Y).2 h
  Â· exact (finrank_hom_simple_simple_eq_zero_iff ğ•œ X Y).2 (not_nonempty_iff.mp h)
#align category_theory.finrank_hom_simple_simple CategoryTheory.finrank_hom_simple_simple

end CategoryTheory
