/-
Copyright (c) 2022 Jakob von Raumer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel, Jakob von Raumer
-/
import Mathlib.CategoryTheory.Limits.Constructions.FiniteProductsOfBinaryProducts
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Kernels
import Mathlib.CategoryTheory.Limits.Constructions.LimitsOfProductsAndEqualizers
import Mathlib.CategoryTheory.Preadditive.AdditiveFunctor

#align_import category_theory.preadditive.left_exact from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# Left exactness of functors between preadditive categories

We show that a functor is left exact in the sense that it preserves finite limits, if it
preserves kernels. The dual result holds for right exact functors and cokernels.
## Main results
* We first derive preservation of binary product in the lemma
  `preservesBinaryProductsOfPreservesKernels`,
* then show the preservation of equalizers in `preservesEqualizerOfPreservesKernels`,
* and then derive the preservation of all finite limits with the usual construction.

-/


universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

noncomputable section

open CategoryTheory

open CategoryTheory.Limits

open CategoryTheory.Preadditive

namespace CategoryTheory

namespace Functor

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] [Preadditive C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
  [Preadditive D] (F : C ‚•§ D) [PreservesZeroMorphisms F]

section FiniteLimits

/-- A functor between preadditive categories which preserves kernels preserves that an
arbitrary binary fan is a limit.
-/
def isLimitMapConeBinaryFanOfPreservesKernels {X Y Z : C} (œÄ‚ÇÅ : Z ‚ü∂ X) (œÄ‚ÇÇ : Z ‚ü∂ Y)
    [PreservesLimit (parallelPair œÄ‚ÇÇ 0) F] (i : IsLimit (BinaryFan.mk œÄ‚ÇÅ œÄ‚ÇÇ)) :
    IsLimit (F.mapCone (BinaryFan.mk œÄ‚ÇÅ œÄ‚ÇÇ)) := by
  let bc := BinaryBicone.ofLimitCone i
  -- ‚ä¢ IsLimit (F.mapCone (BinaryFan.mk œÄ‚ÇÅ œÄ‚ÇÇ))
  let presf : PreservesLimit (parallelPair bc.snd 0) F := by simpa
  -- ‚ä¢ IsLimit (F.mapCone (BinaryFan.mk œÄ‚ÇÅ œÄ‚ÇÇ))
  let hf : IsLimit bc.sndKernelFork := BinaryBicone.isLimitSndKernelFork i
  -- ‚ä¢ IsLimit (F.mapCone (BinaryFan.mk œÄ‚ÇÅ œÄ‚ÇÇ))
  exact (isLimitMapConeBinaryFanEquiv F œÄ‚ÇÅ œÄ‚ÇÇ).invFun
    (BinaryBicone.isBilimitOfKernelInl (F.mapBinaryBicone bc)
    (isLimitMapConeForkEquiv' F bc.inl_snd (isLimitOfPreserves F hf))).isLimit
#align category_theory.functor.is_limit_map_cone_binary_fan_of_preserves_kernels CategoryTheory.Functor.isLimitMapConeBinaryFanOfPreservesKernels

/-- A kernel preserving functor between preadditive categories preserves any pair being a limit. -/
def preservesBinaryProductOfPreservesKernels
    [‚àÄ {X Y} (f : X ‚ü∂ Y), PreservesLimit (parallelPair f 0) F] {X Y : C} :
    PreservesLimit (pair X Y) F where
  preserves {c} hc :=
    IsLimit.ofIsoLimit
      (isLimitMapConeBinaryFanOfPreservesKernels F _ _ (IsLimit.ofIsoLimit hc (isoBinaryFanMk c)))
      ((Cones.functoriality _ F).mapIso (isoBinaryFanMk c).symm)
#align category_theory.functor.preserves_binary_product_of_preserves_kernels CategoryTheory.Functor.preservesBinaryProductOfPreservesKernels

attribute [local instance] preservesBinaryProductOfPreservesKernels

/-- A kernel preserving functor between preadditive categories preserves binary products. -/
def preservesBinaryProductsOfPreservesKernels
    [‚àÄ {X Y} (f : X ‚ü∂ Y), PreservesLimit (parallelPair f 0) F] :
  PreservesLimitsOfShape (Discrete WalkingPair) F where
    preservesLimit := preservesLimitOfIsoDiagram F (diagramIsoPair _).symm
#align category_theory.functor.preserves_binary_products_of_preserves_kernels CategoryTheory.Functor.preservesBinaryProductsOfPreservesKernels

attribute [local instance] preservesBinaryProductsOfPreservesKernels

variable [HasBinaryBiproducts C]

/-- A functor between preadditive categories preserves the equalizer of two
morphisms if it preserves all kernels. -/
def preservesEqualizerOfPreservesKernels [‚àÄ {X Y} (f : X ‚ü∂ Y), PreservesLimit (parallelPair f 0) F]
    {X Y : C} (f g : X ‚ü∂ Y) : PreservesLimit (parallelPair f g) F := by
  letI := preservesBinaryBiproductsOfPreservesBinaryProducts F
  -- ‚ä¢ PreservesLimit (parallelPair f g) F
  haveI := additive_of_preservesBinaryBiproducts F
  -- ‚ä¢ PreservesLimit (parallelPair f g) F
  constructor; intro c i
  -- ‚ä¢ {c : Cone (parallelPair f g)} ‚Üí IsLimit c ‚Üí IsLimit (F.mapCone c)
               -- ‚ä¢ IsLimit (F.mapCone c)
  let c' := isLimitKernelForkOfFork (i.ofIsoLimit (Fork.isoForkOfŒπ c))
  -- ‚ä¢ IsLimit (F.mapCone c)
  dsimp only [kernelForkOfFork_ofŒπ] at c'
  -- ‚ä¢ IsLimit (F.mapCone c)
  let iFc := isLimitForkMapOfIsLimit' F _ c'
  -- ‚ä¢ IsLimit (F.mapCone c)
  apply IsLimit.ofIsoLimit _ ((Cones.functoriality _ F).mapIso (Fork.isoForkOfŒπ c).symm)
  -- ‚ä¢ IsLimit ((Cones.functoriality (parallelPair f g) F).obj (Fork.ofŒπ (Fork.Œπ c) ‚Ä¶
  apply (isLimitMapConeForkEquiv F (Fork.condition c)).invFun
  -- ‚ä¢ IsLimit (Fork.ofŒπ (F.map (Fork.Œπ c)) (_ : F.map (Fork.Œπ c) ‚â´ F.map f = F.map ‚Ä¶
  let p : parallelPair (F.map (f - g)) 0 ‚âÖ parallelPair (F.map f - F.map g) 0 :=
    parallelPair.eqOfHomEq F.map_sub rfl
  exact
    IsLimit.ofIsoLimit
      (isLimitForkOfKernelFork ((IsLimit.postcomposeHomEquiv p _).symm iFc))
      (Fork.ext (Iso.refl _) (by simp))
#align category_theory.functor.preserves_equalizer_of_preserves_kernels CategoryTheory.Functor.preservesEqualizerOfPreservesKernels

/-- A functor between preadditive categories preserves all equalizers if it preserves all kernels.
-/
def preservesEqualizersOfPreservesKernels
    [‚àÄ {X Y} (f : X ‚ü∂ Y), PreservesLimit (parallelPair f 0) F] :
    PreservesLimitsOfShape WalkingParallelPair F where
  preservesLimit {K} := by
    letI := preservesEqualizerOfPreservesKernels F (K.map WalkingParallelPairHom.left)
        (K.map WalkingParallelPairHom.right)
    apply preservesLimitOfIsoDiagram F (diagramIsoParallelPair K).symm
    -- üéâ no goals
#align category_theory.functor.preserves_equalizers_of_preserves_kernels CategoryTheory.Functor.preservesEqualizersOfPreservesKernels

/-- A functor between preadditive categories which preserves kernels preserves all finite limits.
-/
def preservesFiniteLimitsOfPreservesKernels [HasFiniteProducts C] [HasEqualizers C]
    [HasZeroObject C] [HasZeroObject D] [‚àÄ {X Y} (f : X ‚ü∂ Y), PreservesLimit (parallelPair f 0) F] :
    PreservesFiniteLimits F := by
  letI := preservesEqualizersOfPreservesKernels F
  -- ‚ä¢ PreservesFiniteLimits F
  letI := preservesTerminalObjectOfPreservesZeroMorphisms F
  -- ‚ä¢ PreservesFiniteLimits F
  letI := preservesLimitsOfShapePemptyOfPreservesTerminal F
  -- ‚ä¢ PreservesFiniteLimits F
  letI : PreservesFiniteProducts F := ‚ü®preservesFiniteProductsOfPreservesBinaryAndTerminal F‚ü©
  -- ‚ä¢ PreservesFiniteLimits F
  exact preservesFiniteLimitsOfPreservesEqualizersAndFiniteProducts F
  -- üéâ no goals
#align category_theory.functor.preserves_finite_limits_of_preserves_kernels CategoryTheory.Functor.preservesFiniteLimitsOfPreservesKernels

end FiniteLimits

section FiniteColimits

/-- A functor between preadditive categories which preserves cokernels preserves finite coproducts.
-/
def isColimitMapCoconeBinaryCofanOfPreservesCokernels {X Y Z : C} (Œπ‚ÇÅ : X ‚ü∂ Z) (Œπ‚ÇÇ : Y ‚ü∂ Z)
    [PreservesColimit (parallelPair Œπ‚ÇÇ 0) F] (i : IsColimit (BinaryCofan.mk Œπ‚ÇÅ Œπ‚ÇÇ)) :
    IsColimit (F.mapCocone (BinaryCofan.mk Œπ‚ÇÅ Œπ‚ÇÇ)) := by
  let bc := BinaryBicone.ofColimitCocone i
  -- ‚ä¢ IsColimit (F.mapCocone (BinaryCofan.mk Œπ‚ÇÅ Œπ‚ÇÇ))
  let presf : PreservesColimit (parallelPair bc.inr 0) F := by simpa
  -- ‚ä¢ IsColimit (F.mapCocone (BinaryCofan.mk Œπ‚ÇÅ Œπ‚ÇÇ))
  let hf : IsColimit bc.inrCokernelCofork := BinaryBicone.isColimitInrCokernelCofork i
  -- ‚ä¢ IsColimit (F.mapCocone (BinaryCofan.mk Œπ‚ÇÅ Œπ‚ÇÇ))
  exact
    (isColimitMapCoconeBinaryCofanEquiv F Œπ‚ÇÅ Œπ‚ÇÇ).invFun
      (BinaryBicone.isBilimitOfCokernelFst (F.mapBinaryBicone bc)
          (isColimitMapCoconeCoforkEquiv' F bc.inr_fst (isColimitOfPreserves F hf))).isColimit
#align category_theory.functor.is_colimit_map_cocone_binary_cofan_of_preserves_cokernels CategoryTheory.Functor.isColimitMapCoconeBinaryCofanOfPreservesCokernels

/-- A cokernel preserving functor between preadditive categories preserves any pair being
a colimit. -/
def preservesCoproductOfPreservesCokernels
    [‚àÄ {X Y} (f : X ‚ü∂ Y), PreservesColimit (parallelPair f 0) F] {X Y : C} :
    PreservesColimit (pair X Y) F where
  preserves {c} hc :=
    IsColimit.ofIsoColimit
      (isColimitMapCoconeBinaryCofanOfPreservesCokernels F _ _
        (IsColimit.ofIsoColimit hc (isoBinaryCofanMk c)))
      ((Cocones.functoriality _ F).mapIso (isoBinaryCofanMk c).symm)
#align category_theory.functor.preserves_coproduct_of_preserves_cokernels CategoryTheory.Functor.preservesCoproductOfPreservesCokernels

attribute [local instance] preservesCoproductOfPreservesCokernels

/-- A cokernel preserving functor between preadditive categories preserves binary coproducts. -/
def preservesBinaryCoproductsOfPreservesCokernels
    [‚àÄ {X Y} (f : X ‚ü∂ Y), PreservesColimit (parallelPair f 0) F] :
    PreservesColimitsOfShape (Discrete WalkingPair) F where
  preservesColimit := preservesColimitOfIsoDiagram F (diagramIsoPair _).symm
#align category_theory.functor.preserves_binary_coproducts_of_preserves_cokernels CategoryTheory.Functor.preservesBinaryCoproductsOfPreservesCokernels

attribute [local instance] preservesBinaryCoproductsOfPreservesCokernels

variable [HasBinaryBiproducts C]

/-- A functor between preadditive categories preserves the coequalizer of two
morphisms if it preserves all cokernels. -/
def preservesCoequalizerOfPreservesCokernels
    [‚àÄ {X Y} (f : X ‚ü∂ Y), PreservesColimit (parallelPair f 0) F] {X Y : C} (f g : X ‚ü∂ Y) :
    PreservesColimit (parallelPair f g) F := by
  letI := preservesBinaryBiproductsOfPreservesBinaryCoproducts F
  -- ‚ä¢ PreservesColimit (parallelPair f g) F
  haveI := additive_of_preservesBinaryBiproducts F
  -- ‚ä¢ PreservesColimit (parallelPair f g) F
  constructor
  -- ‚ä¢ {c : Cocone (parallelPair f g)} ‚Üí IsColimit c ‚Üí IsColimit (F.mapCocone c)
  intro c i
  -- ‚ä¢ IsColimit (F.mapCocone c)
  let c' := isColimitCokernelCoforkOfCofork (i.ofIsoColimit (Cofork.isoCoforkOfœÄ c))
  -- ‚ä¢ IsColimit (F.mapCocone c)
  dsimp only [cokernelCoforkOfCofork_ofœÄ] at c'
  -- ‚ä¢ IsColimit (F.mapCocone c)
  let iFc := isColimitCoforkMapOfIsColimit' F _ c'
  -- ‚ä¢ IsColimit (F.mapCocone c)
  apply
    IsColimit.ofIsoColimit _ ((Cocones.functoriality _ F).mapIso (Cofork.isoCoforkOfœÄ c).symm)
  apply (isColimitMapCoconeCoforkEquiv F (Cofork.condition c)).invFun
  -- ‚ä¢ IsColimit (Cofork.ofœÄ (F.map (Cofork.œÄ c)) (_ : F.map f ‚â´ F.map (Cofork.œÄ c) ‚Ä¶
  let p : parallelPair (F.map (f - g)) 0 ‚âÖ parallelPair (F.map f - F.map g) 0 :=
    parallelPair.ext (Iso.refl _) (Iso.refl _) (by simp) (by simp)
  exact
    IsColimit.ofIsoColimit
      (isColimitCoforkOfCokernelCofork ((IsColimit.precomposeHomEquiv p.symm _).symm iFc))
      (Cofork.ext (Iso.refl _) (by simp))
#align category_theory.functor.preserves_coequalizer_of_preserves_cokernels CategoryTheory.Functor.preservesCoequalizerOfPreservesCokernels

/-- A functor between preadditive categories preserves all coequalizers if it preserves all kernels.
-/
def preservesCoequalizersOfPreservesCokernels
    [‚àÄ {X Y} (f : X ‚ü∂ Y), PreservesColimit (parallelPair f 0) F] :
    PreservesColimitsOfShape WalkingParallelPair F where
  preservesColimit {K} := by
    letI := preservesCoequalizerOfPreservesCokernels F (K.map Limits.WalkingParallelPairHom.left)
        (K.map Limits.WalkingParallelPairHom.right)
    apply preservesColimitOfIsoDiagram F (diagramIsoParallelPair K).symm
    -- üéâ no goals
#align category_theory.functor.preserves_coequalizers_of_preserves_cokernels CategoryTheory.Functor.preservesCoequalizersOfPreservesCokernels

/-- A functor between preadditive categories which preserves kernels preserves all finite limits.
-/
def preservesFiniteColimitsOfPreservesCokernels [HasFiniteCoproducts C] [HasCoequalizers C]
    [HasZeroObject C] [HasZeroObject D]
    [‚àÄ {X Y} (f : X ‚ü∂ Y), PreservesColimit (parallelPair f 0) F] : PreservesFiniteColimits F := by
  letI := preservesCoequalizersOfPreservesCokernels F
  -- ‚ä¢ PreservesFiniteColimits F
  letI := preservesInitialObjectOfPreservesZeroMorphisms F
  -- ‚ä¢ PreservesFiniteColimits F
  letI := preservesColimitsOfShapePemptyOfPreservesInitial F
  -- ‚ä¢ PreservesFiniteColimits F
  letI : PreservesFiniteCoproducts F := ‚ü®preservesFiniteCoproductsOfPreservesBinaryAndInitial F‚ü©
  -- ‚ä¢ PreservesFiniteColimits F
  exact preservesFiniteColimitsOfPreservesCoequalizersAndFiniteCoproducts F
  -- üéâ no goals
#align category_theory.functor.preserves_finite_colimits_of_preserves_cokernels CategoryTheory.Functor.preservesFiniteColimitsOfPreservesCokernels

end FiniteColimits

end Functor

end CategoryTheory
