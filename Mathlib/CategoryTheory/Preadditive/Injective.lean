/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Kevin Buzzard
-/
import Mathlib.CategoryTheory.Preadditive.Projective

#align_import category_theory.preadditive.injective from "leanprover-community/mathlib"@"3974a774a707e2e06046a14c0eaef4654584fada"

/-!
# Injective objects and categories with enough injectives

An object `J` is injective iff every morphism into `J` can be obtained by extending a monomorphism.
-/


noncomputable section

open CategoryTheory

open CategoryTheory.Limits

open Opposite

universe v v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]

/--
An object `J` is injective iff every morphism into `J` can be obtained by extending a monomorphism.
-/
class Injective (J : C) : Prop where
  factors : ‚àÄ {X Y : C} (g : X ‚ü∂ J) (f : X ‚ü∂ Y) [Mono f], ‚àÉ h : Y ‚ü∂ J, f ‚â´ h = g
#align category_theory.injective CategoryTheory.Injective

attribute [inherit_doc Injective] Injective.factors

lemma Limits.IsZero.injective {X : C} (h : IsZero X) : Injective X where
  factors _ _ _ := ‚ü®h.from_ _, h.eq_of_tgt _ _‚ü©

section

/-- An injective presentation of an object `X` consists of a monomorphism `f : X ‚ü∂ J`
to some injective object `J`.
-/
structure InjectivePresentation (X : C) where
  J : C
  injective : Injective J := by infer_instance
  f : X ‚ü∂ J
  mono : Mono f := by infer_instance
#align category_theory.injective_presentation CategoryTheory.InjectivePresentation

open InjectivePresentation in
attribute [inherit_doc InjectivePresentation] J injective f mono

attribute [instance] InjectivePresentation.injective InjectivePresentation.mono

variable (C)

/-- A category "has enough injectives" if every object has an injective presentation,
i.e. if for every object `X` there is an injective object `J` and a monomorphism `X ‚Ü™ J`. -/
class EnoughInjectives : Prop where
  presentation : ‚àÄ X : C, Nonempty (InjectivePresentation X)
#align category_theory.enough_injectives CategoryTheory.EnoughInjectives

attribute [inherit_doc EnoughInjectives] EnoughInjectives.presentation

end

namespace Injective

/--
Let `J` be injective and `g` a morphism into `J`, then `g` can be factored through any monomorphism.
-/
def factorThru {J X Y : C} [Injective J] (g : X ‚ü∂ J) (f : X ‚ü∂ Y) [Mono f] : Y ‚ü∂ J :=
  (Injective.factors g f).choose
#align category_theory.injective.factor_thru CategoryTheory.Injective.factorThru

@[reassoc (attr := simp)]
theorem comp_factorThru {J X Y : C} [Injective J] (g : X ‚ü∂ J) (f : X ‚ü∂ Y) [Mono f] :
    f ‚â´ factorThru g f = g :=
  (Injective.factors g f).choose_spec
#align category_theory.injective.comp_factor_thru CategoryTheory.Injective.comp_factorThru

section

open ZeroObject

lemma injective_of_isZero (X : C) (hX : IsZero X) : Injective X where
  factors _ _ _ := ‚ü®hX.from_ _, hX.eq_of_tgt _ _‚ü©

instance zero_injective [HasZeroObject C] : Injective (0 : C) :=
  (isZero_zero C).injective
#align category_theory.injective.zero_injective CategoryTheory.Injective.zero_injective

end

theorem of_iso {P Q : C} (i : P ‚âÖ Q) (hP : Injective P) : Injective Q :=
  {
    factors := fun g f mono => by
      obtain ‚ü®h, h_eq‚ü© := @Injective.factors C _ P _ _ _ (g ‚â´ i.inv) f mono
      refine' ‚ü®h ‚â´ i.hom, _‚ü©
      rw [‚Üê Category.assoc, h_eq, Category.assoc, Iso.inv_hom_id, Category.comp_id] }
#align category_theory.injective.of_iso CategoryTheory.Injective.of_iso

theorem iso_iff {P Q : C} (i : P ‚âÖ Q) : Injective P ‚Üî Injective Q :=
  ‚ü®of_iso i, of_iso i.symm‚ü©
#align category_theory.injective.iso_iff CategoryTheory.Injective.iso_iff

/-- The axiom of choice says that every nonempty type is an injective object in `Type`. -/
instance (X : Type u‚ÇÅ) [Nonempty X] : Injective X where
  factors g f mono :=
    ‚ü®fun z => by
      classical
      exact
          if h : z ‚àà Set.range f then g (Classical.choose h) else Nonempty.some inferInstance, by
      ext y
      classical
      change dite (f y ‚àà Set.range f) (fun h => g (Classical.choose h)) _ = _
      split_ifs <;> rename_i h
      ¬∑ rw [mono_iff_injective] at mono
        erw [mono (Classical.choose_spec h)]
      ¬∑ exact False.elim (h ‚ü®y, rfl‚ü©)‚ü©

instance Type.enoughInjectives : EnoughInjectives (Type u‚ÇÅ) where
  presentation X :=
    Nonempty.intro
      { J := WithBot X
        injective := inferInstance
        f := Option.some
        mono := by
          rw [mono_iff_injective]
          exact Option.some_injective X }
#align category_theory.injective.Type.enough_injectives CategoryTheory.Injective.Type.enoughInjectives

instance {P Q : C} [HasBinaryProduct P Q] [Injective P] [Injective Q] : Injective (P ‚®Ø Q) where
  factors g f mono := by
    use Limits.prod.lift (factorThru (g ‚â´ Limits.prod.fst) f) (factorThru (g ‚â´ Limits.prod.snd) f)
    simp only [prod.comp_lift, comp_factorThru]
    ext
    ¬∑ simp only [prod.lift_fst]
    ¬∑ simp only [prod.lift_snd]

instance {Œ≤ : Type v} (c : Œ≤ ‚Üí C) [HasProduct c] [‚àÄ b, Injective (c b)] : Injective (‚àè c) where
  factors g f mono := by
    refine' ‚ü®Pi.lift fun b => factorThru (g ‚â´ Pi.œÄ c _) f, _‚ü©
    ext b
    simp only [Category.assoc, limit.lift_œÄ, Fan.mk_œÄ_app, comp_factorThru]

instance {P Q : C} [HasZeroMorphisms C] [HasBinaryBiproduct P Q] [Injective P] [Injective Q] :
    Injective (P ‚äû Q) where
  factors g f mono := by
    refine' ‚ü®biprod.lift (factorThru (g ‚â´ biprod.fst) f) (factorThru (g ‚â´ biprod.snd) f), _‚ü©
    ext
    ¬∑ simp only [Category.assoc, biprod.lift_fst, comp_factorThru]
    ¬∑ simp only [Category.assoc, biprod.lift_snd, comp_factorThru]

instance {Œ≤ : Type v} (c : Œ≤ ‚Üí C) [HasZeroMorphisms C] [HasBiproduct c] [‚àÄ b, Injective (c b)] :
    Injective (‚®Å c) where
  factors g f mono := by
    refine' ‚ü®biproduct.lift fun b => factorThru (g ‚â´ biproduct.œÄ _ _) f, _‚ü©
    ext
    simp only [Category.assoc, biproduct.lift_œÄ, comp_factorThru]

instance {P : C·µí·µñ} [Projective P] : Injective (unop P) where
  factors g f mono :=
    ‚ü®(@Projective.factorThru C·µí·µñ _ P _ _ _ g.op f.op _).unop, Quiver.Hom.op_inj (by simp)‚ü©

instance {J : C·µí·µñ} [Injective J] : Projective (unop J) where
  factors f e he :=
    ‚ü®(@factorThru C·µí·µñ _ J _ _ _ f.op e.op _).unop, Quiver.Hom.op_inj (by simp)‚ü©

instance {J : C} [Injective J] : Projective (op J) where
  factors f e epi :=
    ‚ü®(@factorThru C _ J _ _ _ f.unop e.unop _).op, Quiver.Hom.unop_inj (by simp)‚ü©

instance {P : C} [Projective P] : Injective (op P) where
  factors g f mono :=
    ‚ü®(@Projective.factorThru C _ P _ _ _ g.unop f.unop _).op, Quiver.Hom.unop_inj (by simp)‚ü©

theorem injective_iff_projective_op {J : C} : Injective J ‚Üî Projective (op J) :=
  ‚ü®fun _ => inferInstance, fun _ => show Injective (unop (op J)) from inferInstance‚ü©
#align category_theory.injective.injective_iff_projective_op CategoryTheory.Injective.injective_iff_projective_op

theorem projective_iff_injective_op {P : C} : Projective P ‚Üî Injective (op P) :=
  ‚ü®fun _ => inferInstance, fun _ => show Projective (unop (op P)) from inferInstance‚ü©
#align category_theory.injective.projective_iff_injective_op CategoryTheory.Injective.projective_iff_injective_op

theorem injective_iff_preservesEpimorphisms_yoneda_obj (J : C) :
    Injective J ‚Üî (yoneda.obj J).PreservesEpimorphisms := by
  rw [injective_iff_projective_op, Projective.projective_iff_preservesEpimorphisms_coyoneda_obj]
  exact Functor.preservesEpimorphisms.iso_iff (Coyoneda.objOpOp _)
#align category_theory.injective.injective_iff_preserves_epimorphisms_yoneda_obj CategoryTheory.Injective.injective_iff_preservesEpimorphisms_yoneda_obj

section Adjunction

open CategoryTheory.Functor

variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
variable {L : C ‚•§ D} {R : D ‚•§ C} [PreservesMonomorphisms L]

theorem injective_of_adjoint (adj : L ‚ä£ R) (J : D) [Injective J] : Injective <| R.obj J :=
  ‚ü®fun {A} {_} g f im =>
    ‚ü®adj.homEquiv _ _ (factorThru ((adj.homEquiv A J).symm g) (L.map f)),
      (adj.homEquiv _ _).symm.injective (by simp)‚ü©‚ü©
#align category_theory.injective.injective_of_adjoint CategoryTheory.Injective.injective_of_adjoint

end Adjunction

section EnoughInjectives

variable [EnoughInjectives C]

lemma exists_presentation' (X : C) : ‚àÉ (I : InjectivePresentation X), IsZero X ‚Üí IsZero I.J := by
  by_cases h : IsZero X
  ¬∑ have := injective_of_isZero _ h
    exact ‚ü®{ J := X, f := ùüô X}, by tauto‚ü©
  ¬∑ exact ‚ü®(EnoughInjectives.presentation X).some, by tauto‚ü©

/-- `Injective.under X` provides an arbitrarily chosen injective object equipped with
a monomorphism `Injective.Œπ : X ‚ü∂ Injective.under X`.
-/
def under (X : C) : C :=
  (exists_presentation' X).choose.J
#align category_theory.injective.under CategoryTheory.Injective.under

instance injective_under (X : C) : Injective (under X) :=
  (exists_presentation' X).choose.injective
#align category_theory.injective.injective_under CategoryTheory.Injective.injective_under

/-- The monomorphism `Injective.Œπ : X ‚ü∂ Injective.under X`
from the arbitrarily chosen injective object under `X`.
-/
def Œπ (X : C) : X ‚ü∂ under X :=
  (exists_presentation' X).choose.f
#align category_theory.injective.Œπ CategoryTheory.Injective.Œπ

instance Œπ_mono (X : C) : Mono (Œπ X) :=
  (exists_presentation' X).choose.mono
#align category_theory.injective.Œπ_mono CategoryTheory.Injective.Œπ_mono

lemma isZero_under (X : C) (hX : IsZero X) :
    IsZero (under X) :=
  (exists_presentation' X).choose_spec hX

section

variable [HasZeroMorphisms C] {X Y : C} (f : X ‚ü∂ Y) [HasCokernel f]

/-- When `C` has enough injectives, the object `Injective.syzygies f` is
an arbitrarily chosen injective object under `cokernel f`.
-/
def syzygies : C :=
  under (cokernel f) -- Porting note: no deriving Injective
#align category_theory.injective.syzygies CategoryTheory.Injective.syzygies

instance : Injective <| syzygies f := injective_under (cokernel f)

/-- When `C` has enough injective,
`Injective.d f : Y ‚ü∂ syzygies f` is the composition
`cokernel.œÄ f ‚â´ Œπ (cokernel f)`.

(When `C` is abelian, we have `exact f (injective.d f)`.)
-/
abbrev d : Y ‚ü∂ syzygies f :=
  cokernel.œÄ f ‚â´ Œπ (cokernel f)
#align category_theory.injective.d CategoryTheory.Injective.d

end

end EnoughInjectives

instance [EnoughInjectives C] : EnoughProjectives C·µí·µñ :=
  ‚ü®fun X => ‚ü®{ p := _, f := (Injective.Œπ (unop X)).op}‚ü©‚ü©

instance [EnoughProjectives C] : EnoughInjectives C·µí·µñ :=
  ‚ü®fun X => ‚ü®‚ü®_, inferInstance, (Projective.œÄ (unop X)).op, inferInstance‚ü©‚ü©‚ü©

theorem enoughProjectives_of_enoughInjectives_op [EnoughInjectives C·µí·µñ] : EnoughProjectives C :=
  ‚ü®fun X => ‚ü®{ p := _, f := (Injective.Œπ (op X)).unop} ‚ü©‚ü©
#align category_theory.injective.enough_projectives_of_enough_injectives_op CategoryTheory.Injective.enoughProjectives_of_enoughInjectives_op

theorem enoughInjectives_of_enoughProjectives_op [EnoughProjectives C·µí·µñ] : EnoughInjectives C :=
  ‚ü®fun X => ‚ü®‚ü®_, inferInstance, (Projective.œÄ (op X)).unop, inferInstance‚ü©‚ü©‚ü©
#align category_theory.injective.enough_injectives_of_enough_projectives_op CategoryTheory.Injective.enoughInjectives_of_enoughProjectives_op

open Injective

/-

redundant with Exact.descToInjective in CategoryTheory.Abelian.InjectiveResolution

section

variable [HasZeroMorphisms C] [HasImages C·µí·µñ] [HasEqualizers C·µí·µñ]

/-- Given a pair of exact morphism `f : Q ‚ü∂ R` and `g : R ‚ü∂ S` and a map `h : R ‚ü∂ J` to an injective
object `J` such that `f ‚â´ h = 0`, then `g` descents to a map `S ‚ü∂ J`. See below:

```
Q --- f --> R --- g --> S
            |
            | h
            v
            J
```
-/
def Exact.desc {J Q R S : C} [Injective J] (h : R ‚ü∂ J) (f : Q ‚ü∂ R) (g : R ‚ü∂ S)
    (hgf : Exact g.op f.op) (w : f ‚â´ h = 0) : S ‚ü∂ J :=
  (Exact.lift h.op g.op f.op hgf (congr_arg Quiver.Hom.op w)).unop
#align category_theory.injective.exact.desc CategoryTheory.Injective.Exact.desc

@[simp]
theorem Exact.comp_desc {J Q R S : C} [Injective J] (h : R ‚ü∂ J) (f : Q ‚ü∂ R) (g : R ‚ü∂ S)
    (hgf : Exact g.op f.op) (w : f ‚â´ h = 0) : g ‚â´ Exact.desc h f g hgf w = h := by
  convert congr_arg Quiver.Hom.unop (Exact.lift_comp h.op g.op f.op hgf (congrArg Quiver.Hom.op w))
#align category_theory.injective.exact.comp_desc CategoryTheory.Injective.Exact.comp_desc

end-/

end Injective

namespace Adjunction

variable {D : Type*} [Category D] {F : C ‚•§ D} {G : D ‚•§ C}

theorem map_injective (adj : F ‚ä£ G) [F.PreservesMonomorphisms] (I : D) (hI : Injective I) :
    Injective (G.obj I) :=
  ‚ü®fun {X} {Y} f g => by
    intro
    rcases hI.factors (F.map f ‚â´ adj.counit.app _) (F.map g) with ‚ü®w,h‚ü©
    use adj.unit.app Y ‚â´ G.map w
    rw [‚Üê unit_naturality_assoc, ‚Üê G.map_comp, h]
    simp‚ü©
#align category_theory.adjunction.map_injective CategoryTheory.Adjunction.map_injective

theorem injective_of_map_injective (adj : F ‚ä£ G) [Full G] [Faithful G] (I : D)
    (hI : Injective (G.obj I)) : Injective I :=
  ‚ü®fun {X} {Y} f g => by
    intro
    haveI : PreservesLimitsOfSize.{0, 0} G := adj.rightAdjointPreservesLimits
    rcases hI.factors (G.map f) (G.map g) with ‚ü®w,h‚ü©
    use inv (adj.counit.app _) ‚â´ F.map w ‚â´ adj.counit.app _
    refine' Faithful.map_injective (F := G) _
    simpa‚ü©
#align category_theory.adjunction.injective_of_map_injective CategoryTheory.Adjunction.injective_of_map_injective

/-- Given an adjunction `F ‚ä£ G` such that `F` preserves monos, `G` maps an injective presentation
of `X` to an injective presentation of `G(X)`. -/
def mapInjectivePresentation (adj : F ‚ä£ G) [F.PreservesMonomorphisms] (X : D)
    (I : InjectivePresentation X) : InjectivePresentation (G.obj X) where
  J := G.obj I.J
  injective := adj.map_injective _ I.injective
  f := G.map I.f
  mono := by
    haveI : PreservesLimitsOfSize.{0, 0} G := adj.rightAdjointPreservesLimits; infer_instance
#align category_theory.adjunction.map_injective_presentation CategoryTheory.Adjunction.mapInjectivePresentation

/-- Given an adjunction `F ‚ä£ G` such that `F` preserves monomorphisms and is faithful,
  then any injective presentation of `F(X)` can be pulled back to an injective presentation of `X`.
  This is similar to `mapInjectivePresentation`. -/
def injectivePresentationOfMap (adj : F ‚ä£ G)
    [F.PreservesMonomorphisms] [F.ReflectsMonomorphisms] (X : C)
    (I : InjectivePresentation <| F.obj X) :
    InjectivePresentation X where
  J := G.obj I.J
  injective := Injective.injective_of_adjoint adj _
  f := adj.homEquiv _ _ I.f

end Adjunction

/--
[Lemma 3.8](https://ncatlab.org/nlab/show/injective+object#preservation_of_injective_objects)
-/
lemma EnoughInjectives.of_adjunction {C : Type u‚ÇÅ} {D : Type u‚ÇÇ}
    [Category.{v‚ÇÅ} C] [Category.{v‚ÇÇ} D]
    {L : C ‚•§ D} {R : D ‚•§ C} (adj : L ‚ä£ R) [L.PreservesMonomorphisms] [L.ReflectsMonomorphisms]
    [EnoughInjectives D] : EnoughInjectives C where
  presentation _ :=
    ‚ü®adj.injectivePresentationOfMap _ (EnoughInjectives.presentation _).some‚ü©

/-- An equivalence of categories transfers enough injectives. -/
lemma EnoughInjectives.of_equivalence {C : Type u‚ÇÅ} {D : Type u‚ÇÇ}
    [Category.{v‚ÇÅ} C] [Category.{v‚ÇÇ} D]
    (e : C ‚•§ D) [IsEquivalence e] [EnoughInjectives D] : EnoughInjectives C :=
  EnoughInjectives.of_adjunction (adj := e.asEquivalence.toAdjunction)

namespace Equivalence

variable {D : Type*} [Category D] (F : C ‚âå D)

theorem map_injective_iff (P : C) : Injective (F.functor.obj P) ‚Üî Injective P :=
  ‚ü®F.symm.toAdjunction.injective_of_map_injective P, F.symm.toAdjunction.map_injective P‚ü©

/-- Given an equivalence of categories `F`, an injective presentation of `F(X)` induces an
injective presentation of `X.` -/
def injectivePresentationOfMapInjectivePresentation (X : C)
    (I : InjectivePresentation (F.functor.obj X)) : InjectivePresentation X :=
  F.toAdjunction.injectivePresentationOfMap _ I
#align category_theory.equivalence.injective_presentation_of_map_injective_presentation CategoryTheory.Equivalence.injectivePresentationOfMapInjectivePresentation

theorem enoughInjectives_iff (F : C ‚âå D) : EnoughInjectives C ‚Üî EnoughInjectives D :=
  ‚ü®fun h => h.of_adjunction F.symm.toAdjunction, fun h => h.of_adjunction F.toAdjunction‚ü©
#align category_theory.equivalence.enough_injectives_iff CategoryTheory.Equivalence.enoughInjectives_iff

end Equivalence

end CategoryTheory
