/-
Copyright (c) 2022 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Limits.Yoneda
import Mathlib.CategoryTheory.Preadditive.Opposite
import Mathlib.Algebra.Category.ModuleCat.Basic
import Mathlib.Algebra.Category.GroupCat.Preadditive

#align_import category_theory.preadditive.yoneda.basic from "leanprover-community/mathlib"@"09f981f72d43749f1fa072deade828d9c1e185bb"

/-!
# The Yoneda embedding for preadditive categories

The Yoneda embedding for preadditive categories sends an object `Y` to the presheaf sending an
object `X` to the group of morphisms `X ‚ü∂ Y`. At each point, we get an additional `End Y`-module
structure.

We also show that this presheaf is additive and that it is compatible with the normal Yoneda
embedding in the expected way and deduce that the preadditive Yoneda embedding is fully faithful.

## TODO
* The Yoneda embedding is additive itself

-/


universe v u

open CategoryTheory.Preadditive Opposite CategoryTheory.Limits

noncomputable section

namespace CategoryTheory

variable {C : Type u} [Category.{v} C] [Preadditive C]

/-- The Yoneda embedding for preadditive categories sends an object `Y` to the presheaf sending an
object `X` to the `End Y`-module of morphisms `X ‚ü∂ Y`.
-/
@[simps]
def preadditiveYonedaObj (Y : C) : C·µí·µñ ‚•§ ModuleCat.{v} (End Y) where
  obj X := ModuleCat.of _ (X.unop ‚ü∂ Y)
  map f := ModuleCat.ofHom
    { toFun := fun g => f.unop ‚â´ g
      map_add' := fun g g' => comp_add _ _ _ _ _ _
      map_smul' := fun r g => Eq.symm <| Category.assoc _ _ _ }
#align category_theory.preadditive_yoneda_obj CategoryTheory.preadditiveYonedaObj

/-- The Yoneda embedding for preadditive categories sends an object `Y` to the presheaf sending an
object `X` to the group of morphisms `X ‚ü∂ Y`. At each point, we get an additional `End Y`-module
structure, see `preadditiveYonedaObj`.
-/
@[simps]
def preadditiveYoneda : C ‚•§ C·µí·µñ ‚•§ AddCommGroupCat.{v} where
  obj Y := preadditiveYonedaObj Y ‚ãô forget‚ÇÇ _ _
  map f :=
    { app := fun X =>
        { toFun := fun g => g ‚â´ f
          map_zero' := Limits.zero_comp
          map_add' := fun g g' => add_comp _ _ _ _ _ _ }
      naturality := fun X X' g => AddCommGroupCat.ext fun x => Category.assoc _ _ _ }
  map_id _ := by ext; dsimp; simp
                 -- ‚ä¢ ‚Üë(NatTrans.app ({ obj := fun Y => preadditiveYonedaObj Y ‚ãô forget‚ÇÇ (ModuleCa ‚Ä¶
                      -- ‚ä¢ x‚úù ‚â´ ùüô x‚úù¬≤ = x‚úù
                             -- üéâ no goals
  map_comp f g := by ext; dsimp; simp
                     -- ‚ä¢ ‚Üë(NatTrans.app ({ obj := fun Y => preadditiveYonedaObj Y ‚ãô forget‚ÇÇ (ModuleCa ‚Ä¶
                          -- ‚ä¢ x‚úù ‚â´ f ‚â´ g = (x‚úù ‚â´ f) ‚â´ g
                                 -- üéâ no goals
#align category_theory.preadditive_yoneda CategoryTheory.preadditiveYoneda

/-- The Yoneda embedding for preadditive categories sends an object `X` to the copresheaf sending an
object `Y` to the `End X`-module of morphisms `X ‚ü∂ Y`.
-/
@[simps]
def preadditiveCoyonedaObj (X : C·µí·µñ) : C ‚•§ ModuleCat.{v} (End X) where
  obj Y := ModuleCat.of _ (unop X ‚ü∂ Y)
  map f := ModuleCat.ofHom
    { toFun := fun g => g ‚â´ f
      map_add' := fun g g' => add_comp _ _ _ _ _ _
      map_smul' := fun r g => Category.assoc _ _ _ }
#align category_theory.preadditive_coyoneda_obj CategoryTheory.preadditiveCoyonedaObj

/-- The Yoneda embedding for preadditive categories sends an object `X` to the copresheaf sending an
object `Y` to the group of morphisms `X ‚ü∂ Y`. At each point, we get an additional `End X`-module
structure, see `preadditiveCoyonedaObj`.
-/
@[simps]
def preadditiveCoyoneda : C·µí·µñ ‚•§ C ‚•§ AddCommGroupCat.{v} where
  obj X := preadditiveCoyonedaObj X ‚ãô forget‚ÇÇ _ _
  map f :=
    { app := fun Y =>
        { toFun := fun g => f.unop ‚â´ g
          map_zero' := Limits.comp_zero
          map_add' := fun g g' => comp_add _ _ _ _ _ _ }
      naturality := fun Y Y' g =>
        AddCommGroupCat.ext fun x => Eq.symm <| Category.assoc _ _ _ }
  map_id _ := by ext; dsimp; simp
                 -- ‚ä¢ ‚Üë(NatTrans.app ({ obj := fun X => preadditiveCoyonedaObj X ‚ãô forget‚ÇÇ (Module ‚Ä¶
                      -- ‚ä¢ ùüô x‚úù¬≤.unop ‚â´ x‚úù = x‚úù
                             -- üéâ no goals
  map_comp f g := by ext; dsimp; simp
                     -- ‚ä¢ ‚Üë(NatTrans.app ({ obj := fun X => preadditiveCoyonedaObj X ‚ãô forget‚ÇÇ (Module ‚Ä¶
                          -- ‚ä¢ (g.unop ‚â´ f.unop) ‚â´ x‚úù = g.unop ‚â´ f.unop ‚â´ x‚úù
                                 -- üéâ no goals
#align category_theory.preadditive_coyoneda CategoryTheory.preadditiveCoyoneda

instance additive_yonedaObj (X : C) : Functor.Additive (preadditiveYonedaObj X) where
#align category_theory.additive_yoneda_obj CategoryTheory.additive_yonedaObj

instance additive_yonedaObj' (X : C) : Functor.Additive (preadditiveYoneda.obj X) where
#align category_theory.additive_yoneda_obj' CategoryTheory.additive_yonedaObj'

instance additive_coyonedaObj (X : C·µí·µñ) : Functor.Additive (preadditiveCoyonedaObj X) where
#align category_theory.additive_coyoneda_obj CategoryTheory.additive_coyonedaObj

instance additive_coyonedaObj' (X : C·µí·µñ) : Functor.Additive (preadditiveCoyoneda.obj X) where
#align category_theory.additive_coyoneda_obj' CategoryTheory.additive_coyonedaObj'

/-- Composing the preadditive yoneda embedding with the forgetful functor yields the regular
Yoneda embedding.
-/
@[simp]
theorem whiskering_preadditiveYoneda :
    preadditiveYoneda ‚ãô
        (whiskeringRight C·µí·µñ AddCommGroupCat (Type v)).obj (forget AddCommGroupCat) =
      yoneda :=
  rfl
#align category_theory.whiskering_preadditive_yoneda CategoryTheory.whiskering_preadditiveYoneda

/-- Composing the preadditive yoneda embedding with the forgetful functor yields the regular
Yoneda embedding.
-/
@[simp]
theorem whiskering_preadditiveCoyoneda :
    preadditiveCoyoneda ‚ãô
        (whiskeringRight C AddCommGroupCat (Type v)).obj (forget AddCommGroupCat) =
      coyoneda :=
  rfl
#align category_theory.whiskering_preadditive_coyoneda CategoryTheory.whiskering_preadditiveCoyoneda

instance full_preadditiveYoneda : Full (preadditiveYoneda : C ‚•§ C·µí·µñ ‚•§ AddCommGroupCat) :=
  let _ : Full (preadditiveYoneda ‚ãô
      (whiskeringRight C·µí·µñ AddCommGroupCat (Type v)).obj (forget AddCommGroupCat)) :=
    Yoneda.yonedaFull
  Full.ofCompFaithful preadditiveYoneda
    ((whiskeringRight C·µí·µñ AddCommGroupCat (Type v)).obj (forget AddCommGroupCat))
#align category_theory.preadditive_yoneda_full CategoryTheory.full_preadditiveYoneda

instance full_preadditiveCoyoneda : Full (preadditiveCoyoneda : C·µí·µñ ‚•§ C ‚•§ AddCommGroupCat) :=
  let _ : Full (preadditiveCoyoneda ‚ãô
      (whiskeringRight C AddCommGroupCat (Type v)).obj (forget AddCommGroupCat)) :=
    Coyoneda.coyonedaFull
  Full.ofCompFaithful preadditiveCoyoneda
    ((whiskeringRight C AddCommGroupCat (Type v)).obj (forget AddCommGroupCat))
#align category_theory.preadditive_coyoneda_full CategoryTheory.full_preadditiveCoyoneda

instance faithful_preadditiveYoneda : Faithful (preadditiveYoneda : C ‚•§ C·µí·µñ ‚•§ AddCommGroupCat) :=
  Faithful.of_comp_eq whiskering_preadditiveYoneda
#align category_theory.preadditive_yoneda_faithful CategoryTheory.faithful_preadditiveYoneda

instance faithful_preadditiveCoyoneda :
    Faithful (preadditiveCoyoneda : C·µí·µñ ‚•§ C ‚•§ AddCommGroupCat) :=
  Faithful.of_comp_eq whiskering_preadditiveCoyoneda
#align category_theory.preadditive_coyoneda_faithful CategoryTheory.faithful_preadditiveCoyoneda

end CategoryTheory
