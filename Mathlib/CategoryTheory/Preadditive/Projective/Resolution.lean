/-
Copyright (c) 2021 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Jo√´l Riou
-/
import Mathlib.Algebra.Homology.QuasiIso
import Mathlib.Algebra.Homology.SingleHomology
import Mathlib.CategoryTheory.Preadditive.Projective.Preserves

/-!
# Projective resolutions

A projective resolution `P : ProjectiveResolution Z` of an object `Z : C` consists of
an `‚Ñï`-indexed chain complex `P.complex` of projective objects,
along with a quasi-isomorphism `P.œÄ` from `C` to the chain complex consisting just
of `Z` in degree zero.

-/


universe v u v' u'

namespace CategoryTheory

open Category Limits ChainComplex HomologicalComplex

variable {C : Type u} [Category.{v} C]

open Projective

variable [HasZeroObject C] [HasZeroMorphisms C]

/--
A `ProjectiveResolution Z` consists of a bundled `‚Ñï`-indexed chain complex of projective objects,
along with a quasi-isomorphism to the complex consisting of just `Z` supported in degree `0`.
-/
structure ProjectiveResolution (Z : C) where
  /-- the chain complex involved in the resolution -/
  complex : ChainComplex C ‚Ñï
  /-- the chain complex must be degreewise projective -/
  projective : ‚àÄ n, Projective (complex.X n) := by infer_instance
  /-- the chain complex must have homology -/
  [hasHomology : ‚àÄ i, complex.HasHomology i]
  /-- the morphism to the single chain complex with `Z` in degree `0` -/
  œÄ : complex ‚ü∂ (ChainComplex.single‚ÇÄ C).obj Z
  /-- the morphism to the single chain complex with `Z` in degree `0` is a quasi-isomorphism -/
  quasiIso : QuasiIso œÄ := by infer_instance

open ProjectiveResolution in
attribute [instance] projective hasHomology ProjectiveResolution.quasiIso

/-- An object admits a projective resolution.
-/
class HasProjectiveResolution (Z : C) : Prop where
  out : Nonempty (ProjectiveResolution Z)

variable (C)

/-- You will rarely use this typeclass directly: it is implied by the combination
`[EnoughProjectives C]` and `[Abelian C]`.
By itself it's enough to set up the basic theory of derived functors.
-/
class HasProjectiveResolutions : Prop where
  out : ‚àÄ Z : C, HasProjectiveResolution Z

attribute [instance 100] HasProjectiveResolutions.out

namespace ProjectiveResolution

variable {C}
variable {Z : C} (P : ProjectiveResolution Z)

lemma complex_exactAt_succ (n : ‚Ñï) :
    P.complex.ExactAt (n + 1) := by
  rw [‚Üê quasiIsoAt_iff_exactAt' P.œÄ (n + 1) (exactAt_succ_single_obj _ _)]
  infer_instance

lemma exact_succ (n : ‚Ñï) :
    (ShortComplex.mk _ _ (P.complex.d_comp_d (n + 2) (n + 1) n)).Exact :=
  ((HomologicalComplex.exactAt_iff' _ (n + 2) (n + 1) n) (by simp only [prev]; rfl)
    (by simp)).1 (P.complex_exactAt_succ n)

@[simp]
theorem œÄ_f_succ (n : ‚Ñï) : P.œÄ.f (n + 1) = 0 :=
  (isZero_single_obj_X _ _ _ _ (by simp)).eq_of_tgt _ _

@[reassoc (attr := simp)]
theorem complex_d_comp_œÄ_f_zero :
    P.complex.d 1 0 ‚â´ P.œÄ.f 0 = 0 := by
  rw [‚Üê P.œÄ.comm 1 0, single_obj_d, comp_zero]

theorem complex_d_succ_comp (n : ‚Ñï) :
    P.complex.d n (n + 1) ‚â´ P.complex.d (n + 1) (n + 2) = 0 := by
  simp

/-- The (limit) cokernel cofork given by the composition
`P.complex.X 1 ‚ü∂ P.complex.X 0 ‚ü∂ Z` when `P : ProjectiveResolution Z`. -/
@[simp]
noncomputable def cokernelCofork : CokernelCofork (P.complex.d 1 0) :=
  CokernelCofork.ofœÄ _ P.complex_d_comp_œÄ_f_zero

/-- `Z` is the cokernel of `P.complex.X 1 ‚ü∂ P.complex.X 0` when `P : ProjectiveResolution Z`. -/
noncomputable def isColimitCokernelCofork : IsColimit (P.cokernelCofork) := by
  refine IsColimit.ofIsoColimit (P.complex.opcyclesIsCokernel 1 0 (by simp)) ?_
  refine Cofork.ext (P.complex.isoHomologyŒπ‚ÇÄ.symm ‚â™‚â´ isoOfQuasiIsoAt P.œÄ 0 ‚â™‚â´
    singleObjHomologySelfIso _ _ _) ?_
  rw [‚Üê cancel_mono (singleObjHomologySelfIso (ComplexShape.down ‚Ñï) 0 _).inv,
    ‚Üê cancel_mono (isoHomologyŒπ‚ÇÄ _).hom]
  dsimp
  simp only [isoHomologyŒπ‚ÇÄ_inv_naturality_assoc, p_opcyclesMap_assoc, single‚ÇÄ_obj_zero, assoc,
    Iso.hom_inv_id, comp_id, isoHomologyŒπ_inv_hom_id, singleObjHomologySelfIso_inv_homologyŒπ,
    singleObjOpcyclesSelfIso_hom, single‚ÇÄObjXSelf, Iso.refl_inv, id_comp]

instance (n : ‚Ñï) : Epi (P.œÄ.f n) := by
  cases n
  ¬∑ exact epi_of_isColimit_cofork P.isColimitCokernelCofork
  ¬∑ rw [œÄ_f_succ]; infer_instance

variable (Z)

/-- A projective object admits a trivial projective resolution: itself in degree 0. -/
@[simps]
noncomputable def self [Projective Z] : ProjectiveResolution Z where
  complex := (ChainComplex.single‚ÇÄ C).obj Z
  œÄ := ùüô ((ChainComplex.single‚ÇÄ C).obj Z)
  projective n := by
    cases n
    ¬∑ simpa
    ¬∑ apply IsZero.projective
      apply HomologicalComplex.isZero_single_obj_X
      simp

end ProjectiveResolution

namespace Functor

open Limits

variable {C : Type u} [Category C] [HasZeroObject C] [Preadditive C]
  {D : Type u'} [Category.{v'} D] [HasZeroObject D] [Preadditive D] [CategoryWithHomology D]

/-- An additive functor `F` which preserves homology and sends projective objects to projective
objects sends a projective resolution of `Z` to a projective resolution of `F.obj Z`. -/
@[simps complex œÄ]
noncomputable def mapProjectiveResolution (F : C ‚•§ D) [F.Additive]
    [F.PreservesProjectiveObjects] [F.PreservesHomology] {Z : C} (P : ProjectiveResolution Z) :
    ProjectiveResolution (F.obj Z) where
  complex := (F.mapHomologicalComplex _).obj P.complex
  projective n := PreservesProjectiveObjects.projective_obj (P.projective n)
  œÄ := (F.mapHomologicalComplex _).map P.œÄ ‚â´
    (HomologicalComplex.singleMapHomologicalComplex _ _ _).hom.app _
  quasiIso := inferInstance

end CategoryTheory.Functor
