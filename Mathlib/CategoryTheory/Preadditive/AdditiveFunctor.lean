/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz, Kim Morrison
-/
import Mathlib.CategoryTheory.Limits.ExactFunctor
import Mathlib.CategoryTheory.Limits.Preserves.Finite
import Mathlib.CategoryTheory.Preadditive.Biproducts
import Mathlib.CategoryTheory.Preadditive.FunctorCategory

/-!
# Additive Functors

A functor between two preadditive categories is called *additive*
provided that the induced map on hom types is a morphism of abelian
groups.

An additive functor between preadditive categories creates and preserves biproducts.
Conversely, if `F : C ‚•§ D` is a functor between preadditive categories, where `C` has binary
biproducts, and if `F` preserves binary biproducts, then `F` is additive.

We also define the category of bundled additive functors.

# Implementation details

`Functor.Additive` is a `Prop`-valued class, defined by saying that for every two objects `X` and
`Y`, the map `F.map : (X ‚ü∂ Y) ‚Üí (F.obj X ‚ü∂ F.obj Y)` is a morphism of abelian groups.

-/


universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory

/-- A functor `F` is additive provided `F.map` is an additive homomorphism. -/
@[stacks 00ZY]
class Functor.Additive {C D : Type*} [Category C] [Category D] [Preadditive C] [Preadditive D]
  (F : C ‚•§ D) : Prop where
  /-- the addition of two morphisms is mapped to the sum of their images -/
  map_add : ‚àÄ {X Y : C} {f g : X ‚ü∂ Y}, F.map (f + g) = F.map f + F.map g := by cat_disch

section Preadditive

namespace Functor

section

variable {C D E : Type*} [Category C] [Category D] [Category E]
  [Preadditive C] [Preadditive D] [Preadditive E] (F : C ‚•§ D) [Functor.Additive F]

@[simp]
theorem map_add {X Y : C} {f g : X ‚ü∂ Y} : F.map (f + g) = F.map f + F.map g :=
  Functor.Additive.map_add

/-- `F.mapAddHom` is an additive homomorphism whose underlying function is `F.map`. -/
@[simps!]
def mapAddHom {X Y : C} : (X ‚ü∂ Y) ‚Üí+ (F.obj X ‚ü∂ F.obj Y) :=
  AddMonoidHom.mk' (fun f => F.map f) fun _ _ => F.map_add

theorem coe_mapAddHom {X Y : C} : ‚áë(F.mapAddHom : (X ‚ü∂ Y) ‚Üí+ _) = F.map :=
  rfl

instance (priority := 100) preservesZeroMorphisms_of_additive : PreservesZeroMorphisms F where
  map_zero _ _ := F.mapAddHom.map_zero

instance : Additive (ùü≠ C) where

instance {E : Type*} [Category E] [Preadditive E] (G : D ‚•§ E) [Functor.Additive G] :
    Additive (F ‚ãô G) where

instance {J : Type*} [Category J] (j : J) : ((evaluation J C).obj j).Additive where

@[simp]
theorem map_neg {X Y : C} {f : X ‚ü∂ Y} : F.map (-f) = -F.map f :=
  (F.mapAddHom : (X ‚ü∂ Y) ‚Üí+ (F.obj X ‚ü∂ F.obj Y)).map_neg _

@[simp]
theorem map_sub {X Y : C} {f g : X ‚ü∂ Y} : F.map (f - g) = F.map f - F.map g :=
  (F.mapAddHom : (X ‚ü∂ Y) ‚Üí+ (F.obj X ‚ü∂ F.obj Y)).map_sub _ _

theorem map_nsmul {X Y : C} {f : X ‚ü∂ Y} {n : ‚Ñï} : F.map (n ‚Ä¢ f) = n ‚Ä¢ F.map f :=
  (F.mapAddHom : (X ‚ü∂ Y) ‚Üí+ (F.obj X ‚ü∂ F.obj Y)).map_nsmul _ _

-- You can alternatively just use `Functor.map_smul` here, with an explicit `(r : ‚Ñ§)` argument.
theorem map_zsmul {X Y : C} {f : X ‚ü∂ Y} {r : ‚Ñ§} : F.map (r ‚Ä¢ f) = r ‚Ä¢ F.map f :=
  (F.mapAddHom : (X ‚ü∂ Y) ‚Üí+ (F.obj X ‚ü∂ F.obj Y)).map_zsmul _ _

@[simp]
nonrec theorem map_sum {X Y : C} {Œ± : Type*} (f : Œ± ‚Üí (X ‚ü∂ Y)) (s : Finset Œ±) :
    F.map (‚àë a ‚àà s, f a) = ‚àë a ‚àà s, F.map (f a) :=
  map_sum F.mapAddHom f s

variable {F}

lemma additive_of_iso {G : C ‚•§ D} (e : F ‚âÖ G) : G.Additive := by
  constructor
  intro X Y f g
  simp only [‚Üê NatIso.naturality_1 e (f + g), map_add, Preadditive.add_comp,
    NatTrans.naturality, Preadditive.comp_add, Iso.inv_hom_id_app_assoc]

variable (F)

lemma additive_of_full_essSurj_comp [Full F] [EssSurj F] (G : D ‚•§ E)
    [(F ‚ãô G).Additive] : G.Additive where
  map_add {X Y f g} := by
    obtain ‚ü®f', hf'‚ü© := F.map_surjective ((F.objObjPreimageIso X).hom ‚â´ f ‚â´
      (F.objObjPreimageIso Y).inv)
    obtain ‚ü®g', hg'‚ü© := F.map_surjective ((F.objObjPreimageIso X).hom ‚â´ g ‚â´
      (F.objObjPreimageIso Y).inv)
    simp only [‚Üê cancel_mono (G.map (F.objObjPreimageIso Y).inv),
      ‚Üê cancel_epi (G.map (F.objObjPreimageIso X).hom),
      Preadditive.add_comp, Preadditive.comp_add, ‚Üê Functor.map_comp]
    erw [‚Üê hf', ‚Üê hg', ‚Üê (F ‚ãô G).map_add]
    dsimp
    rw [F.map_add]

lemma additive_of_comp_faithful
    (F : C ‚•§ D) (G : D ‚•§ E) [G.Additive] [(F ‚ãô G).Additive] [Faithful G] :
    F.Additive where
  map_add {_ _ f‚ÇÅ f‚ÇÇ} := G.map_injective (by
    rw [‚Üê Functor.comp_map, G.map_add, (F ‚ãô G).map_add, Functor.comp_map, Functor.comp_map])

open ZeroObject Limits in
include F in
lemma hasZeroObject_of_additive [HasZeroObject C] :
    HasZeroObject D where
  zero := ‚ü®F.obj 0, by rw [IsZero.iff_id_eq_zero, ‚Üê F.map_id, id_zero, F.map_zero]‚ü©

end

section InducedCategory

variable {C : Type*} {D : Type*} [Category D] [Preadditive D] (F : C ‚Üí D)

instance inducedFunctor_additive : Functor.Additive (inducedFunctor F) where

end InducedCategory

instance fullSubcategoryInclusion_additive {C : Type*} [Category C] [Preadditive C]
    (Z : ObjectProperty C) : Z.Œπ.Additive where

section

-- To talk about preservation of biproducts we need to specify universes explicitly.
noncomputable section

variable {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} [Category.{v‚ÇÅ} C] [Category.{v‚ÇÇ} D] [Preadditive C]
  [Preadditive D] (F : C ‚•§ D)

open CategoryTheory.Limits

open CategoryTheory.Preadditive

instance (priority := 100) preservesFiniteBiproductsOfAdditive [Additive F] :
    PreservesFiniteBiproducts F where
  preserves := fun {J} _ =>
    let ‚ü®_‚ü© := nonempty_fintype J
    { preserves :=
      { preserves := fun hb =>
          ‚ü®isBilimitOfTotal _ (by
            simp_rw [F.mapBicone_œÄ, F.mapBicone_Œπ, ‚Üê F.map_comp]
            erw [‚Üê F.map_sum, ‚Üê F.map_id, IsBilimit.total hb])‚ü© } }

instance (priority := 100) preservesFiniteCoproductsOfAdditive [Additive F] :
    PreservesFiniteCoproducts F where
  preserves _ := preservesCoproductsOfShape_of_preservesBiproductsOfShape F

instance (priority := 100) preservesFiniteProductsOfAdditive [Additive F] :
    PreservesFiniteProducts F where
  preserves _ := preservesProductsOfShape_of_preservesBiproductsOfShape F

theorem additive_of_preservesBinaryBiproducts [HasBinaryBiproducts C] [PreservesZeroMorphisms F]
    [PreservesBinaryBiproducts F] : Additive F where
  map_add {X Y f g} := by
    rw [biprod.add_eq_lift_id_desc, F.map_comp, ‚Üê biprod.lift_mapBiprod,
      ‚Üê biprod.mapBiprod_hom_desc, Category.assoc, Iso.inv_hom_id_assoc, F.map_id,
      biprod.add_eq_lift_id_desc]

lemma additive_of_preserves_binary_products
    [HasBinaryProducts C] [PreservesLimitsOfShape (Discrete WalkingPair) F]
    [F.PreservesZeroMorphisms] : F.Additive := by
  have : HasBinaryBiproducts C := HasBinaryBiproducts.of_hasBinaryProducts
  have := preservesBinaryBiproducts_of_preservesBinaryProducts F
  exact Functor.additive_of_preservesBinaryBiproducts F

end

end

end Functor

namespace Equivalence

variable {C D : Type*} [Category C] [Category D] [Preadditive C] [Preadditive D]

instance inverse_additive (e : C ‚âå D) [e.functor.Additive] : e.inverse.Additive where
  map_add {f g} := e.functor.map_injective (by simp)

end Equivalence

section

variable (C D : Type*) [Category C] [Category D] [Preadditive C] [Preadditive D]

/-- Bundled additive functors. -/
def AdditiveFunctor :=
  ObjectProperty.FullSubcategory fun F : C ‚•§ D => F.Additive

instance : Category (AdditiveFunctor C D) :=
  ObjectProperty.FullSubcategory.category _

/-- the category of additive functors is denoted `C ‚•§+ D` -/
infixr:26 " ‚•§+ " => AdditiveFunctor

instance : Preadditive (C ‚•§+ D) :=
  Preadditive.inducedCategory _

/-- An additive functor is in particular a functor. -/
def AdditiveFunctor.forget : (C ‚•§+ D) ‚•§ C ‚•§ D :=
  ObjectProperty.Œπ _

instance : (AdditiveFunctor.forget C D).Full :=
  ObjectProperty.full_Œπ _

instance : (AdditiveFunctor.forget C D).Faithful :=
  ObjectProperty.faithful_Œπ _

variable {C D}

/-- Turn an additive functor into an object of the category `AdditiveFunctor C D`. -/
def AdditiveFunctor.of (F : C ‚•§ D) [F.Additive] : C ‚•§+ D :=
  ‚ü®F, inferInstance‚ü©

@[simp]
theorem AdditiveFunctor.of_fst (F : C ‚•§ D) [F.Additive] : (AdditiveFunctor.of F).1 = F :=
  rfl

@[simp]
theorem AdditiveFunctor.forget_obj (F : C ‚•§+ D) : (AdditiveFunctor.forget C D).obj F = F.1 :=
  rfl

theorem AdditiveFunctor.forget_obj_of (F : C ‚•§ D) [F.Additive] :
    (AdditiveFunctor.forget C D).obj (AdditiveFunctor.of F) = F :=
  rfl

@[simp]
theorem AdditiveFunctor.forget_map (F G : C ‚•§+ D) (Œ± : F ‚ü∂ G) :
    (AdditiveFunctor.forget C D).map Œ± = Œ± :=
  rfl

instance : Functor.Additive (AdditiveFunctor.forget C D) where map_add := rfl

instance (F : C ‚•§+ D) : Functor.Additive F.1 :=
  F.2

end

section Exact

open CategoryTheory.Limits

variable (C : Type u‚ÇÅ) (D : Type u‚ÇÇ) [Category.{v‚ÇÅ} C] [Category.{v‚ÇÇ} D] [Preadditive C]
variable [Preadditive D] [HasZeroObject C] [HasZeroObject D] [HasBinaryBiproducts C]

section

attribute [local instance] preservesBinaryBiproducts_of_preservesBinaryProducts

attribute [local instance] preservesBinaryBiproducts_of_preservesBinaryCoproducts

/-- Turn a left exact functor into an additive functor. -/
def AdditiveFunctor.ofLeftExact : (C ‚•§‚Çó D) ‚•§ C ‚•§+ D :=
  ObjectProperty.ŒπOfLE fun F ‚ü®_‚ü© =>
    Functor.additive_of_preservesBinaryBiproducts F

instance : (AdditiveFunctor.ofLeftExact C D).Full := ObjectProperty.full_ŒπOfLE _
instance : (AdditiveFunctor.ofLeftExact C D).Faithful := ObjectProperty.faithful_ŒπOfLE _

/-- Turn a right exact functor into an additive functor. -/
def AdditiveFunctor.ofRightExact : (C ‚•§·µ£ D) ‚•§ C ‚•§+ D :=
  ObjectProperty.ŒπOfLE fun F ‚ü®_‚ü© =>
    Functor.additive_of_preservesBinaryBiproducts F

instance : (AdditiveFunctor.ofRightExact C D).Full := ObjectProperty.full_ŒπOfLE _
instance : (AdditiveFunctor.ofRightExact C D).Faithful := ObjectProperty.faithful_ŒπOfLE _

/-- Turn an exact functor into an additive functor. -/
def AdditiveFunctor.ofExact : (C ‚•§‚Çë D) ‚•§ C ‚•§+ D :=
  ObjectProperty.ŒπOfLE fun F ‚ü®‚ü®_‚ü©, _‚ü© =>
    Functor.additive_of_preservesBinaryBiproducts F

instance : (AdditiveFunctor.ofExact C D).Full := ObjectProperty.full_ŒπOfLE _
instance : (AdditiveFunctor.ofExact C D).Faithful := ObjectProperty.faithful_ŒπOfLE _

end

variable {C D}

@[simp]
theorem AdditiveFunctor.ofLeftExact_obj_fst (F : C ‚•§‚Çó D) :
    ((AdditiveFunctor.ofLeftExact C D).obj F).obj = F.obj :=
  rfl

@[simp]
theorem AdditiveFunctor.ofRightExact_obj_fst (F : C ‚•§·µ£ D) :
    ((AdditiveFunctor.ofRightExact C D).obj F).obj = F.obj :=
  rfl

@[simp]
theorem AdditiveFunctor.ofExact_obj_fst (F : C ‚•§‚Çë D) :
    ((AdditiveFunctor.ofExact C D).obj F).obj = F.obj :=
  rfl

@[simp]
theorem AdditiveFunctor.ofLeftExact_map {F G : C ‚•§‚Çó D} (Œ± : F ‚ü∂ G) :
    (AdditiveFunctor.ofLeftExact C D).map Œ± = Œ± :=
  rfl

@[simp]
theorem AdditiveFunctor.ofRightExact_map {F G : C ‚•§·µ£ D} (Œ± : F ‚ü∂ G) :
    (AdditiveFunctor.ofRightExact C D).map Œ± = Œ± :=
  rfl

@[simp]
theorem AdditiveFunctor.ofExact_map {F G : C ‚•§‚Çë D} (Œ± : F ‚ü∂ G) :
    (AdditiveFunctor.ofExact C D).map Œ± = Œ± :=
  rfl

end Exact

end Preadditive

end CategoryTheory
