/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz, Kim Morrison
-/
import Mathlib.CategoryTheory.Limits.ExactFunctor
import Mathlib.CategoryTheory.Limits.Preserves.Finite
import Mathlib.CategoryTheory.Preadditive.Biproducts
import Mathlib.CategoryTheory.Preadditive.FunctorCategory
import Mathlib.CategoryTheory.Conj
import Mathlib.Algebra.Ring.Equiv

/-!
# Additive Functors

A functor between two preadditive categories is called *additive*
provided that the induced map on hom types is a morphism of abelian
groups.

An additive functor between preadditive categories creates and preserves biproducts.
Conversely, if `F : C â¥¤ D` is a functor between preadditive categories, where `C` has binary
biproducts, and if `F` preserves binary biproducts, then `F` is additive.

We also define the category of bundled additive functors.

# Implementation details

`Functor.Additive` is a `Prop`-valued class, defined by saying that for every two objects `X` and
`Y`, the map `F.map : (X âŸ¶ Y) â†’ (F.obj X âŸ¶ F.obj Y)` is a morphism of abelian groups.

-/


universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

namespace CategoryTheory

/-- A functor `F` is additive provided `F.map` is an additive homomorphism. -/
class Functor.Additive {C D : Type*} [Category C] [Category D] [Preadditive C] [Preadditive D]
  (F : C â¥¤ D) : Prop where
  /-- the addition of two morphisms is mapped to the sum of their images -/
  map_add : âˆ€ {X Y : C} {f g : X âŸ¶ Y}, F.map (f + g) = F.map f + F.map g := by aesop_cat

section Preadditive

namespace Functor

section

variable {C D E : Type*} [Category C] [Category D] [Category E]
  [Preadditive C] [Preadditive D] [Preadditive E] (F : C â¥¤ D) [Functor.Additive F]

@[simp]
theorem map_add {X Y : C} {f g : X âŸ¶ Y} : F.map (f + g) = F.map f + F.map g :=
  Functor.Additive.map_add

-- Porting note: it was originally @[simps (config := .asFn)]
/-- `F.mapAddHom` is an additive homomorphism whose underlying function is `F.map`. -/
@[simps!]
def mapAddHom {X Y : C} : (X âŸ¶ Y) â†’+ (F.obj X âŸ¶ F.obj Y) :=
  AddMonoidHom.mk' (fun f => F.map f) fun _ _ => F.map_add

theorem coe_mapAddHom {X Y : C} : â‡‘(F.mapAddHom : (X âŸ¶ Y) â†’+ _) = F.map :=
  rfl

instance (priority := 100) preservesZeroMorphisms_of_additive : PreservesZeroMorphisms F where
  map_zero _ _ := F.mapAddHom.map_zero

instance : Additive (ðŸ­ C) where

instance {E : Type*} [Category E] [Preadditive E] (G : D â¥¤ E) [Functor.Additive G] :
    Additive (F â‹™ G) where

instance {J : Type*} [Category J] (j : J) : ((evaluation J C).obj j).Additive where

@[simp]
theorem map_neg {X Y : C} {f : X âŸ¶ Y} : F.map (-f) = -F.map f :=
  (F.mapAddHom : (X âŸ¶ Y) â†’+ (F.obj X âŸ¶ F.obj Y)).map_neg _

@[simp]
theorem map_sub {X Y : C} {f g : X âŸ¶ Y} : F.map (f - g) = F.map f - F.map g :=
  (F.mapAddHom : (X âŸ¶ Y) â†’+ (F.obj X âŸ¶ F.obj Y)).map_sub _ _

theorem map_nsmul {X Y : C} {f : X âŸ¶ Y} {n : â„•} : F.map (n â€¢ f) = n â€¢ F.map f :=
  (F.mapAddHom : (X âŸ¶ Y) â†’+ (F.obj X âŸ¶ F.obj Y)).map_nsmul _ _

-- You can alternatively just use `Functor.map_smul` here, with an explicit `(r : â„¤)` argument.
theorem map_zsmul {X Y : C} {f : X âŸ¶ Y} {r : â„¤} : F.map (r â€¢ f) = r â€¢ F.map f :=
  (F.mapAddHom : (X âŸ¶ Y) â†’+ (F.obj X âŸ¶ F.obj Y)).map_zsmul _ _

@[simp]
nonrec theorem map_sum {X Y : C} {Î± : Type*} (f : Î± â†’ (X âŸ¶ Y)) (s : Finset Î±) :
    F.map (âˆ‘ a âˆˆ s, f a) = âˆ‘ a âˆˆ s, F.map (f a) :=
  map_sum F.mapAddHom f s

variable {F}

lemma additive_of_iso {G : C â¥¤ D} (e : F â‰… G) : G.Additive := by
  constructor
  intro X Y f g
  simp only [â† NatIso.naturality_1 e (f + g), map_add, Preadditive.add_comp,
    NatTrans.naturality, Preadditive.comp_add, Iso.inv_hom_id_app_assoc]

variable (F)

lemma additive_of_full_essSurj_comp [Full F] [EssSurj F] (G : D â¥¤ E)
    [(F â‹™ G).Additive] : G.Additive where
  map_add {X Y f g} := by
    obtain âŸ¨f', hf'âŸ© := F.map_surjective ((F.objObjPreimageIso X).hom â‰« f â‰«
      (F.objObjPreimageIso Y).inv)
    obtain âŸ¨g', hg'âŸ© := F.map_surjective ((F.objObjPreimageIso X).hom â‰« g â‰«
      (F.objObjPreimageIso Y).inv)
    simp only [â† cancel_mono (G.map (F.objObjPreimageIso Y).inv),
      â† cancel_epi (G.map (F.objObjPreimageIso X).hom),
      Preadditive.add_comp, Preadditive.comp_add, â† Functor.map_comp]
    erw [â† hf', â† hg', â† (F â‹™ G).map_add]
    dsimp
    rw [F.map_add]

lemma additive_of_comp_faithful
    (F : C â¥¤ D) (G : D â¥¤ E) [G.Additive] [(F â‹™ G).Additive] [Faithful G] :
    F.Additive where
  map_add {_ _ fâ‚ fâ‚‚} := G.map_injective (by
    rw [â† Functor.comp_map, G.map_add, (F â‹™ G).map_add, Functor.comp_map, Functor.comp_map])

open ZeroObject Limits in
include F in
lemma hasZeroObject_of_additive [HasZeroObject C] :
    HasZeroObject D where
  zero := âŸ¨F.obj 0, by rw [IsZero.iff_id_eq_zero, â† F.map_id, id_zero, F.map_zero]âŸ©

end

section InducedCategory

variable {C : Type*} {D : Type*} [Category D] [Preadditive D] (F : C â†’ D)

instance inducedFunctor_additive : Functor.Additive (inducedFunctor F) where

end InducedCategory

instance fullSubcategoryInclusion_additive {C : Type*} [Category C] [Preadditive C]
    (Z : C â†’ Prop) : (fullSubcategoryInclusion Z).Additive where

section

-- To talk about preservation of biproducts we need to specify universes explicitly.
noncomputable section

variable {C : Type uâ‚} {D : Type uâ‚‚} [Category.{vâ‚} C] [Category.{vâ‚‚} D] [Preadditive C]
  [Preadditive D] (F : C â¥¤ D)

open CategoryTheory.Limits

open CategoryTheory.Preadditive

instance (priority := 100) preservesFiniteBiproductsOfAdditive [Additive F] :
    PreservesFiniteBiproducts F where
  preserves :=
    { preserves :=
      { preserves := fun hb =>
          âŸ¨isBilimitOfTotal _ (by
            simp_rw [F.mapBicone_Ï€, F.mapBicone_Î¹, â† F.map_comp]
            erw [â† F.map_sum, â† F.map_id, IsBilimit.total hb])âŸ© } }

theorem additive_of_preservesBinaryBiproducts [HasBinaryBiproducts C] [PreservesZeroMorphisms F]
    [PreservesBinaryBiproducts F] : Additive F where
  map_add {X Y f g} := by
    rw [biprod.add_eq_lift_id_desc, F.map_comp, â† biprod.lift_mapBiprod,
      â† biprod.mapBiprod_hom_desc, Category.assoc, Iso.inv_hom_id_assoc, F.map_id,
      biprod.add_eq_lift_id_desc]

lemma additive_of_preserves_binary_products
    [HasBinaryProducts C] [PreservesLimitsOfShape (Discrete WalkingPair) F]
    [F.PreservesZeroMorphisms] : F.Additive := by
  have : HasBinaryBiproducts C := HasBinaryBiproducts.of_hasBinaryProducts
  have := preservesBinaryBiproducts_of_preservesBinaryProducts F
  exact Functor.additive_of_preservesBinaryBiproducts F

end

end

end Functor

namespace Equivalence

variable {C D : Type*} [Category C] [Category D] [Preadditive C] [Preadditive D]

instance inverse_additive (e : C â‰Œ D) [e.functor.Additive] : e.inverse.Additive where
  map_add {f g} := e.functor.map_injective (by simp)

end Equivalence

section

variable (C D : Type*) [Category C] [Category D] [Preadditive C] [Preadditive D]

-- Porting note (https://github.com/leanprover-community/mathlib4/issues/5171): removed @[nolint has_nonempty_instance]
/-- Bundled additive functors. -/
def AdditiveFunctor :=
  FullSubcategory fun F : C â¥¤ D => F.Additive

instance : Category (AdditiveFunctor C D) :=
  FullSubcategory.category _

/-- the category of additive functors is denoted `C â¥¤+ D` -/
infixr:26 " â¥¤+ " => AdditiveFunctor

instance : Preadditive (C â¥¤+ D) :=
  Preadditive.inducedCategory _

/-- An additive functor is in particular a functor. -/
def AdditiveFunctor.forget : (C â¥¤+ D) â¥¤ C â¥¤ D :=
  fullSubcategoryInclusion _

instance : (AdditiveFunctor.forget C D).Full :=
  FullSubcategory.full _

variable {C D}

/-- Turn an additive functor into an object of the category `AdditiveFunctor C D`. -/
def AdditiveFunctor.of (F : C â¥¤ D) [F.Additive] : C â¥¤+ D :=
  âŸ¨F, inferInstanceâŸ©

@[simp]
theorem AdditiveFunctor.of_fst (F : C â¥¤ D) [F.Additive] : (AdditiveFunctor.of F).1 = F :=
  rfl

@[simp]
theorem AdditiveFunctor.forget_obj (F : C â¥¤+ D) : (AdditiveFunctor.forget C D).obj F = F.1 :=
  rfl

theorem AdditiveFunctor.forget_obj_of (F : C â¥¤ D) [F.Additive] :
    (AdditiveFunctor.forget C D).obj (AdditiveFunctor.of F) = F :=
  rfl

@[simp]
theorem AdditiveFunctor.forget_map (F G : C â¥¤+ D) (Î± : F âŸ¶ G) :
    (AdditiveFunctor.forget C D).map Î± = Î± :=
  rfl

instance : Functor.Additive (AdditiveFunctor.forget C D) where map_add := rfl

instance (F : C â¥¤+ D) : Functor.Additive F.1 :=
  F.2

end

section Exact

open CategoryTheory.Limits

variable (C : Type uâ‚) (D : Type uâ‚‚) [Category.{vâ‚} C] [Category.{vâ‚‚} D] [Preadditive C]
variable [Preadditive D] [HasZeroObject C] [HasZeroObject D] [HasBinaryBiproducts C]

section

attribute [local instance] preservesBinaryBiproducts_of_preservesBinaryProducts

attribute [local instance] preservesBinaryBiproducts_of_preservesBinaryCoproducts

/-- Turn a left exact functor into an additive functor. -/
def AdditiveFunctor.ofLeftExact : (C â¥¤â‚— D) â¥¤ C â¥¤+ D :=
  FullSubcategory.map fun F âŸ¨_âŸ© =>
    Functor.additive_of_preservesBinaryBiproducts F

instance : (AdditiveFunctor.ofLeftExact C D).Full := FullSubcategory.full_map _
instance : (AdditiveFunctor.ofLeftExact C D).Faithful := FullSubcategory.faithful_map _

/-- Turn a right exact functor into an additive functor. -/
def AdditiveFunctor.ofRightExact : (C â¥¤áµ£ D) â¥¤ C â¥¤+ D :=
  FullSubcategory.map fun F âŸ¨_âŸ© =>
    Functor.additive_of_preservesBinaryBiproducts F

instance : (AdditiveFunctor.ofRightExact C D).Full := FullSubcategory.full_map _
instance : (AdditiveFunctor.ofRightExact C D).Faithful := FullSubcategory.faithful_map _

/-- Turn an exact functor into an additive functor. -/
def AdditiveFunctor.ofExact : (C â¥¤â‚‘ D) â¥¤ C â¥¤+ D :=
  FullSubcategory.map fun F âŸ¨âŸ¨_âŸ©, _âŸ© =>
    Functor.additive_of_preservesBinaryBiproducts F

instance : (AdditiveFunctor.ofExact C D).Full := FullSubcategory.full_map _
instance : (AdditiveFunctor.ofExact C D).Faithful := FullSubcategory.faithful_map _

end

variable {C D}

@[simp]
theorem AdditiveFunctor.ofLeftExact_obj_fst (F : C â¥¤â‚— D) :
    ((AdditiveFunctor.ofLeftExact C D).obj F).obj = F.obj :=
  rfl

@[simp]
theorem AdditiveFunctor.ofRightExact_obj_fst (F : C â¥¤áµ£ D) :
    ((AdditiveFunctor.ofRightExact C D).obj F).obj = F.obj :=
  rfl

@[simp]
theorem AdditiveFunctor.ofExact_obj_fst (F : C â¥¤â‚‘ D) :
    ((AdditiveFunctor.ofExact C D).obj F).obj = F.obj :=
  rfl

@[simp]
theorem AdditiveFunctor.ofLeftExact_map {F G : C â¥¤â‚— D} (Î± : F âŸ¶ G) :
    (AdditiveFunctor.ofLeftExact C D).map Î± = Î± :=
  rfl

@[simp]
theorem AdditiveFunctor.ofRightExact_map {F G : C â¥¤áµ£ D} (Î± : F âŸ¶ G) :
    (AdditiveFunctor.ofRightExact C D).map Î± = Î± :=
  rfl

@[simp]
theorem AdditiveFunctor.ofExact_map {F G : C â¥¤â‚‘ D} (Î± : F âŸ¶ G) :
    (AdditiveFunctor.ofExact C D).map Î± = Î± :=
  rfl

end Exact

end Preadditive

namespace Equivalence

universe uC vC uC' vC' uD vD uD' vD'

variable {C : Type uC} [Category.{vC} C] [Preadditive C]
variable {C' : Type uC'} [Category.{vC'} C'] [Preadditive C']
variable {D : Type uD} [Category.{vD} D] [Preadditive D]
variable {D' : Type uD'} [Category.{vD'} D'] [Preadditive D']
variable {f : C â¥¤ D}  {g : C' â¥¤ D'}
variable {e : C â‰Œ C'} {e' : D â‰Œ D'} [e.functor.Additive] [e'.functor.Additive]

/--
Suppose we have categories `C, C', D, D'` such that the diagram of functors
```
C ===== f =====> D
||e            ||e'
||             ||
C' ==== g ====> D'
```
commutes where `e` and `e'` are additive equivalence of categories.

Then we have an isomorphism of endomorphism rings `End f â‰ƒ+* End g'` and
-/
noncomputable def endRingEquiv
    (sqâ‚€ : e.congrLeft.functor.obj f â‰… e'.congrRight.inverse.obj g) : End f â‰ƒ+* End g where
  __ := endMonoidEquiv sqâ‚€
  map_add' Î± Î² := by
    simp only [MulEquiv.toEquiv_eq_coe, Equiv.toFun_as_coe, EquivLike.coe_coe]
    refine NatTrans.ext <| funext fun c â†¦ show _ = _ + _ from ?_
    rw [endMonoidEquiv_apply_app, Iso.conj_apply, NatTrans.comp_app, NatTrans.comp_app]
    erw [Functor.FullyFaithful.mulEquivEnd_apply]
    rw [whiskeringLeft_obj_map, whiskerLeft_app, NatTrans.app_add]
    simp only [Functor.comp_obj, Preadditive.add_comp, Preadditive.comp_add, Functor.map_add,
      Functor.map_comp, Category.assoc, endMonoidEquiv_apply_app, Iso.conj, Iso.homCongr,
      MulEquiv.coe_mk]
    rw [â† e'.functor.map_comp_assoc, â† e'.functor.map_comp_assoc, â† e'.functor.map_comp_assoc,
      â† e'.functor.map_comp_assoc, Category.assoc, Category.assoc]
    rfl

end Equivalence

end CategoryTheory
