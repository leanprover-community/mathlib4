/-
Copyright (c) 2025 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Monoidal.CommGrp_
import Mathlib.CategoryTheory.Preadditive.Biproducts

/-!
# Commutative group objects in additive categories.

We construct an inverse of the forgetful functor `CommGrp_ C â¥¤ C` if `C` is an additive category.

This looks slightly strange because the additive structure of `C` maps to the multiplicative
structure of the commutative group objects.
-/

universe v u

namespace CategoryTheory.Preadditive

open CategoryTheory Limits MonoidalCategory ChosenFiniteProducts

variable {C : Type u} [Category.{v} C] [Preadditive C] [ChosenFiniteProducts C]

variable (C) in
/-- The canonical functor from an additive category into its commutative group objects. This is
always an equivalence, see `commGrpEquivalence`. -/
@[simps]
def toCommGrp : C â¥¤ CommGrp_ C where
  obj X :=
    { X := X
      one := 0
      mul := fst _ _ + snd _ _
      inv := -ðŸ™ X
      mul_assoc := by simp [add_assoc]
      mul_comm := by simp [add_comm] }
  map {X Y} f := { hom := f }

-- PROJECT: develop `ChosenFiniteCoproducts`, and construct `ChosenFiniteCoproducts` from
-- `ChosenFiniteProducts` in preadditive categories, to give this lemma a proper home.
private theorem monoidal_hom_ext {X Y Z : C} {f g : X âŠ— Y âŸ¶ Z}
    (hâ‚ : lift (ðŸ™ X) 0 â‰« f = lift (ðŸ™ X) 0 â‰« g) (hâ‚‚ : lift 0 (ðŸ™ Y) â‰« f = lift 0 (ðŸ™ Y) â‰« g) :
    f = g :=
  BinaryCofan.IsColimit.hom_ext
    (binaryBiconeIsBilimitOfLimitConeOfIsLimit (product X Y).isLimit).isColimit hâ‚ hâ‚‚

/-- Auxiliary definition for `commGrpEquivalence`. -/
@[simps!]
def commGrpEquivalenceAux : CommGrp_.forget C â‹™ toCommGrp C â‰…
      ðŸ­ (CommGrp_ C) := by
  refine NatIso.ofComponents (fun _ => CommGrp_.mkIso (Iso.refl _) ?_ ?_) ?_
  Â· exact ((IsZero.iff_id_eq_zero _).2 (Subsingleton.elim _ _)).eq_of_src _ _
  Â· simp only [Functor.comp_obj, Mon_.forget_obj, toCommGrp_obj_X, Functor.id_obj,
      toCommGrp_obj_mul, Iso.refl_hom, Category.comp_id, tensorHom_id, id_whiskerRight,
      Category.id_comp]
    apply monoidal_hom_ext
    Â· simp only [comp_add, lift_fst, lift_snd, add_zero]
      convert (Mon_.lift_comp_one_right _ 0).symm
      Â· simp
      Â· infer_instance
    Â· simp only [comp_add, lift_fst, lift_snd, zero_add]
      convert (Mon_.lift_comp_one_left 0 _).symm
      Â· simp
      Â· infer_instance
  Â· aesop_cat

/-- An additive category is equivalent to its category of commutative group objects. -/
@[simps!]
def commGrpEquivalence : C â‰Œ CommGrp_ C where
  functor := toCommGrp C
  inverse := CommGrp_.forget C
  unitIso := Iso.refl _
  counitIso := commGrpEquivalenceAux

end CategoryTheory.Preadditive
