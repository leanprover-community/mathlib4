/-
Copyright (c) 2022 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Linear.Basic
import Mathlib.CategoryTheory.Preadditive.Biproducts
import Mathlib.LinearAlgebra.Matrix.InvariantBasisNumber
import Mathlib.Data.Set.Subsingleton

#align_import category_theory.preadditive.hom_orthogonal from "leanprover-community/mathlib"@"829895f162a1f29d0133f4b3538f4cd1fb5bffd3"

/-!
# Hom orthogonal families.

A family of objects in a category with zero morphisms is "hom orthogonal" if the only
morphism between distinct objects is the zero morphism.

We show that in any category with zero morphisms and finite biproducts,
a morphism between biproducts drawn from a hom orthogonal family `s : Œπ ‚Üí C`
can be decomposed into a block diagonal matrix with entries in the endomorphism rings of the `s i`.

When the category is preadditive, this decomposition is an additive equivalence,
and intertwines composition and matrix multiplication.
When the category is `R`-linear, the decomposition is an `R`-linear equivalence.

If every object in the hom orthogonal family has an endomorphism ring with invariant basis number
(e.g. if each object in the family is simple, so its endomorphism ring is a division ring,
or otherwise if each endomorphism ring is commutative),
then decompositions of an object as a biproduct of the family have uniquely defined multiplicities.
We state this as:
```
theorem HomOrthogonal.equiv_of_iso (o : HomOrthogonal s) {f : Œ± ‚Üí Œπ} {g : Œ≤ ‚Üí Œπ}
    (i : (‚®Å fun a => s (f a)) ‚âÖ ‚®Å fun b => s (g b)) : ‚àÉ e : Œ± ‚âÉ Œ≤, ‚àÄ a, g (e a) = f a
```

This is preliminary to defining semisimple categories.
-/


open scoped Classical
open Matrix CategoryTheory.Limits

universe v u

namespace CategoryTheory

variable {C : Type u} [Category.{v} C]

/-- A family of objects is "hom orthogonal" if
there is at most one morphism between distinct objects.

(In a category with zero morphisms, that must be the zero morphism.) -/
def HomOrthogonal {Œπ : Type*} (s : Œπ ‚Üí C) : Prop :=
  Pairwise fun i j => Subsingleton (s i ‚ü∂ s j)
#align category_theory.hom_orthogonal CategoryTheory.HomOrthogonal

namespace HomOrthogonal

variable {Œπ : Type*} {s : Œπ ‚Üí C}

theorem eq_zero [HasZeroMorphisms C] (o : HomOrthogonal s) {i j : Œπ} (w : i ‚â† j) (f : s i ‚ü∂ s j) :
    f = 0 :=
  (o w).elim _ _
#align category_theory.hom_orthogonal.eq_zero CategoryTheory.HomOrthogonal.eq_zero

section

variable [HasZeroMorphisms C] [HasFiniteBiproducts C]

/-- Morphisms between two direct sums over a hom orthogonal family `s : Œπ ‚Üí C`
are equivalent to block diagonal matrices,
with blocks indexed by `Œπ`,
and matrix entries in `i`-th block living in the endomorphisms of `s i`. -/
@[simps]
noncomputable def matrixDecomposition (o : HomOrthogonal s) {Œ± Œ≤ : Type} [Finite Œ±] [Finite Œ≤]
    {f : Œ± ‚Üí Œπ} {g : Œ≤ ‚Üí Œπ} :
    ((‚®Å fun a => s (f a)) ‚ü∂ ‚®Å fun b => s (g b)) ‚âÉ
      ‚àÄ i : Œπ, Matrix (g ‚Åª¬π' {i}) (f ‚Åª¬π' {i}) (End (s i)) where
  toFun z i j k :=
    eqToHom
        (by
          rcases k with ‚ü®k, ‚ü®‚ü©‚ü©
          simp) ‚â´
      biproduct.components z k j ‚â´
        eqToHom
          (by
            rcases j with ‚ü®j, ‚ü®‚ü©‚ü©
            simp)
  invFun z :=
    biproduct.matrix fun j k =>
      if h : f j = g k then z (f j) ‚ü®k, by simp [h]‚ü© ‚ü®j, by simp‚ü© ‚â´ eqToHom (by simp [h]) else 0
  left_inv z := by
    ext j k
    simp only [biproduct.matrix_œÄ, biproduct.Œπ_desc]
    split_ifs with h
    ¬∑ simp
      rfl
    ¬∑ symm
      apply o.eq_zero h
  right_inv z := by
    ext i ‚ü®j, w‚ü© ‚ü®k, ‚ü®‚ü©‚ü©
    simp only [eqToHom_refl, biproduct.matrix_components, Category.id_comp]
    split_ifs with h
    ¬∑ simp
    ¬∑ exfalso
      exact h w.symm
#align category_theory.hom_orthogonal.matrix_decomposition CategoryTheory.HomOrthogonal.matrixDecomposition

end

section

variable [Preadditive C] [HasFiniteBiproducts C]

/-- `HomOrthogonal.matrixDecomposition` as an additive equivalence. -/
@[simps!]
noncomputable def matrixDecompositionAddEquiv (o : HomOrthogonal s) {Œ± Œ≤ : Type} [Finite Œ±]
    [Finite Œ≤] {f : Œ± ‚Üí Œπ} {g : Œ≤ ‚Üí Œπ} :
    ((‚®Å fun a => s (f a)) ‚ü∂ ‚®Å fun b => s (g b)) ‚âÉ+
      ‚àÄ i : Œπ, Matrix (g ‚Åª¬π' {i}) (f ‚Åª¬π' {i}) (End (s i)) :=
  { o.matrixDecomposition with
    map_add' := fun w z => by
      ext
      dsimp [biproduct.components]
      simp }
#align category_theory.hom_orthogonal.matrix_decomposition_add_equiv CategoryTheory.HomOrthogonal.matrixDecompositionAddEquiv

@[simp]
theorem matrixDecomposition_id (o : HomOrthogonal s) {Œ± : Type} [Finite Œ±] {f : Œ± ‚Üí Œπ} (i : Œπ) :
    o.matrixDecomposition (ùüô (‚®Å fun a => s (f a))) i = 1 := by
  ext ‚ü®b, ‚ü®‚ü©‚ü© ‚ü®a, j_property‚ü©
  simp only [Set.mem_preimage, Set.mem_singleton_iff] at j_property
  simp only [Category.comp_id, Category.id_comp, Category.assoc, End.one_def, eqToHom_refl,
    Matrix.one_apply, HomOrthogonal.matrixDecomposition_apply, biproduct.components]
  split_ifs with h
  ¬∑ cases h
    simp
  ¬∑ simp at h
    -- Porting note: used to be `convert comp_zero`, but that does not work anymore
    have : biproduct.Œπ (fun a ‚Ü¶ s (f a)) a ‚â´ biproduct.œÄ (fun b ‚Ü¶ s (f b)) b = 0 := by
      simpa using biproduct.Œπ_œÄ_ne _ (Ne.symm h)
    rw [this, comp_zero]
#align category_theory.hom_orthogonal.matrix_decomposition_id CategoryTheory.HomOrthogonal.matrixDecomposition_id

theorem matrixDecomposition_comp (o : HomOrthogonal s) {Œ± Œ≤ Œ≥ : Type} [Finite Œ±] [Fintype Œ≤]
    [Finite Œ≥] {f : Œ± ‚Üí Œπ} {g : Œ≤ ‚Üí Œπ} {h : Œ≥ ‚Üí Œπ} (z : (‚®Å fun a => s (f a)) ‚ü∂ ‚®Å fun b => s (g b))
    (w : (‚®Å fun b => s (g b)) ‚ü∂ ‚®Å fun c => s (h c)) (i : Œπ) :
    o.matrixDecomposition (z ‚â´ w) i = o.matrixDecomposition w i * o.matrixDecomposition z i := by
  ext ‚ü®c, ‚ü®‚ü©‚ü© ‚ü®a, j_property‚ü©
  simp only [Set.mem_preimage, Set.mem_singleton_iff] at j_property
  simp only [Matrix.mul_apply, Limits.biproduct.components,
    HomOrthogonal.matrixDecomposition_apply, Category.comp_id, Category.id_comp, Category.assoc,
    End.mul_def, eqToHom_refl, eqToHom_trans_assoc, Finset.sum_congr]
  conv_lhs => rw [‚Üê Category.id_comp w, ‚Üê biproduct.total]
  simp only [Preadditive.sum_comp, Preadditive.comp_sum]
  apply Finset.sum_congr_set
  ¬∑ intros
    simp
  ¬∑ intro b nm
    simp only [Set.mem_preimage, Set.mem_singleton_iff] at nm
    simp only [Category.assoc]
    -- Porting note: this used to be 4 times `convert comp_zero`
    have : biproduct.Œπ (fun b ‚Ü¶ s (g b)) b ‚â´ w ‚â´ biproduct.œÄ (fun b ‚Ü¶ s (h b)) c = 0 := by
      apply o.eq_zero nm
    simp only [this, comp_zero]
#align category_theory.hom_orthogonal.matrix_decomposition_comp CategoryTheory.HomOrthogonal.matrixDecomposition_comp

section

variable {R : Type*} [Semiring R] [Linear R C]

/-- `HomOrthogonal.MatrixDecomposition` as an `R`-linear equivalence. -/
@[simps]
noncomputable def matrixDecompositionLinearEquiv (o : HomOrthogonal s) {Œ± Œ≤ : Type} [Finite Œ±]
    [Finite Œ≤] {f : Œ± ‚Üí Œπ} {g : Œ≤ ‚Üí Œπ} :
    ((‚®Å fun a => s (f a)) ‚ü∂ ‚®Å fun b => s (g b)) ‚âÉ‚Çó[R]
      ‚àÄ i : Œπ, Matrix (g ‚Åª¬π' {i}) (f ‚Åª¬π' {i}) (End (s i)) :=
  { o.matrixDecompositionAddEquiv with
    map_smul' := fun w z => by
      ext
      dsimp [biproduct.components]
      simp }
#align category_theory.hom_orthogonal.matrix_decomposition_linear_equiv CategoryTheory.HomOrthogonal.matrixDecompositionLinearEquiv

end

/-!
The hypothesis that `End (s i)` has invariant basis number is automatically satisfied
if `s i` is simple (as then `End (s i)` is a division ring).
-/


variable [‚àÄ i, InvariantBasisNumber (End (s i))]

/-- Given a hom orthogonal family `s : Œπ ‚Üí C`
for which each `End (s i)` is a ring with invariant basis number (e.g. if each `s i` is simple),
if two direct sums over `s` are isomorphic, then they have the same multiplicities.
-/
theorem equiv_of_iso (o : HomOrthogonal s) {Œ± Œ≤ : Type} [Finite Œ±] [Finite Œ≤] {f : Œ± ‚Üí Œπ}
    {g : Œ≤ ‚Üí Œπ} (i : (‚®Å fun a => s (f a)) ‚âÖ ‚®Å fun b => s (g b)) :
    ‚àÉ e : Œ± ‚âÉ Œ≤, ‚àÄ a, g (e a) = f a := by
  refine ‚ü®Equiv.ofPreimageEquiv ?_, fun a => Equiv.ofPreimageEquiv_map _ _‚ü©
  intro c
  apply Nonempty.some
  apply Cardinal.eq.1
  cases nonempty_fintype Œ±; cases nonempty_fintype Œ≤
  simp only [Cardinal.mk_fintype, Nat.cast_inj]
  exact
    Matrix.square_of_invertible (o.matrixDecomposition i.inv c) (o.matrixDecomposition i.hom c)
      (by
        rw [‚Üê o.matrixDecomposition_comp]
        simp)
      (by
        rw [‚Üê o.matrixDecomposition_comp]
        simp)
#align category_theory.hom_orthogonal.equiv_of_iso CategoryTheory.HomOrthogonal.equiv_of_iso

end

end HomOrthogonal

end CategoryTheory
