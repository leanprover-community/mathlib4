/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Kevin Buzzard
-/
module

public import Mathlib.CategoryTheory.Preadditive.Projective.Basic

/-!
# Injective objects and categories with enough injectives

An object `J` is injective iff every morphism into `J` can be obtained by extending a monomorphism.
-/

@[expose] public section


noncomputable section

open CategoryTheory Limits Opposite

universe v v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]

/--
An object `J` is injective iff every morphism into `J` can be obtained by extending a monomorphism.
-/
class Injective (J : C) : Prop where
  factors : ‚àÄ {X Y : C} (g : X ‚ü∂ J) (f : X ‚ü∂ Y) [Mono f], ‚àÉ h : Y ‚ü∂ J, f ‚â´ h = g

attribute [inherit_doc Injective] Injective.factors

variable (C) in
/-- The `ObjectProperty C` corresponding to the notion of injective objects in `C`. -/
abbrev isInjective : ObjectProperty C := Injective

lemma Limits.IsZero.injective {X : C} (h : IsZero X) : Injective X where
  factors _ _ _ := ‚ü®h.from_ _, h.eq_of_tgt _ _‚ü©

section

/-- An injective presentation of an object `X` consists of a monomorphism `f : X ‚ü∂ J`
to some injective object `J`.
-/
structure InjectivePresentation (X : C) where
  J : C
  injective : Injective J := by infer_instance
  f : X ‚ü∂ J
  mono : Mono f := by infer_instance

open InjectivePresentation in
attribute [inherit_doc InjectivePresentation] J injective f mono

attribute [instance] InjectivePresentation.injective InjectivePresentation.mono

variable (C)

/-- A category "has enough injectives" if every object has an injective presentation,
i.e. if for every object `X` there is an injective object `J` and a monomorphism `X ‚Ü™ J`. -/
class EnoughInjectives : Prop where
  presentation : ‚àÄ X : C, Nonempty (InjectivePresentation X)

attribute [inherit_doc EnoughInjectives] EnoughInjectives.presentation

end

namespace Injective

/--
Let `J` be injective and `g` a morphism into `J`, then `g` can be factored through any monomorphism.
-/
def factorThru {J X Y : C} [Injective J] (g : X ‚ü∂ J) (f : X ‚ü∂ Y) [Mono f] : Y ‚ü∂ J :=
  (Injective.factors g f).choose

@[reassoc (attr := simp)]
theorem comp_factorThru {J X Y : C} [Injective J] (g : X ‚ü∂ J) (f : X ‚ü∂ Y) [Mono f] :
    f ‚â´ factorThru g f = g :=
  (Injective.factors g f).choose_spec

section

open ZeroObject

instance zero_injective [HasZeroObject C] : Injective (0 : C) :=
  (isZero_zero C).injective

end

theorem of_iso {P Q : C} (i : P ‚âÖ Q) (hP : Injective P) : Injective Q :=
  {
    factors := fun g f mono => by
      obtain ‚ü®h, h_eq‚ü© := @Injective.factors C _ P _ _ _ (g ‚â´ i.inv) f mono
      refine ‚ü®h ‚â´ i.hom, ?_‚ü©
      rw [‚Üê Category.assoc, h_eq, Category.assoc, Iso.inv_hom_id, Category.comp_id] }

theorem iso_iff {P Q : C} (i : P ‚âÖ Q) : Injective P ‚Üî Injective Q :=
  ‚ü®of_iso i, of_iso i.symm‚ü©

/-- The axiom of choice says that every nonempty type is an injective object in `Type`. -/
instance (X : Type u‚ÇÅ) [Nonempty X] : Injective X where
  factors g f mono :=
    ‚ü®fun z => by
      classical
      exact
          if h : z ‚àà Set.range f then g (Classical.choose h) else Nonempty.some inferInstance, by
      ext y
      classical
      change dite (f y ‚àà Set.range f) (fun h => g (Classical.choose h)) _ = _
      split_ifs <;> rename_i h
      ¬∑ rw [mono_iff_injective] at mono
        rw [mono (Classical.choose_spec h)]
      ¬∑ exact False.elim (h ‚ü®y, rfl‚ü©)‚ü©

instance Type.enoughInjectives : EnoughInjectives (Type u‚ÇÅ) where
  presentation X :=
    Nonempty.intro
      { J := WithBot X
        injective := inferInstance
        f := WithBot.some
        mono := by
          rw [mono_iff_injective]
          exact WithBot.coe_injective }

instance {P Q : C} [HasBinaryProduct P Q] [Injective P] [Injective Q] : Injective (P ‚®Ø Q) where
  factors g f mono := by
    use Limits.prod.lift (factorThru (g ‚â´ Limits.prod.fst) f) (factorThru (g ‚â´ Limits.prod.snd) f)
    simp only [prod.comp_lift, comp_factorThru]
    ext
    ¬∑ simp only [prod.lift_fst]
    ¬∑ simp only [prod.lift_snd]

instance {Œ≤ : Type v} (c : Œ≤ ‚Üí C) [HasProduct c] [‚àÄ b, Injective (c b)] : Injective (‚àè·∂ú c) where
  factors g f mono := by
    refine ‚ü®Pi.lift fun b => factorThru (g ‚â´ Pi.œÄ c _) f, ?_‚ü©
    ext b
    simp only [Category.assoc, limit.lift_œÄ, Fan.mk_œÄ_app, comp_factorThru]

instance {P Q : C} [HasZeroMorphisms C] [HasBinaryBiproduct P Q] [Injective P] [Injective Q] :
    Injective (P ‚äû Q) where
  factors g f mono := by
    refine ‚ü®biprod.lift (factorThru (g ‚â´ biprod.fst) f) (factorThru (g ‚â´ biprod.snd) f), ?_‚ü©
    ext
    ¬∑ simp only [Category.assoc, biprod.lift_fst, comp_factorThru]
    ¬∑ simp only [Category.assoc, biprod.lift_snd, comp_factorThru]

instance {Œ≤ : Type v} (c : Œ≤ ‚Üí C) [HasZeroMorphisms C] [HasBiproduct c] [‚àÄ b, Injective (c b)] :
    Injective (‚®Å c) where
  factors g f mono := by
    refine ‚ü®biproduct.lift fun b => factorThru (g ‚â´ biproduct.œÄ _ _) f, ?_‚ü©
    ext
    simp only [Category.assoc, biproduct.lift_œÄ, comp_factorThru]

instance {P : C·µí·µñ} [Projective P] : Injective no_index (unop P) where
  factors g f mono :=
    ‚ü®(@Projective.factorThru C·µí·µñ _ P _ _ _ g.op f.op _).unop, Quiver.Hom.op_inj (by simp)‚ü©

instance {J : C·µí·µñ} [Injective J] : Projective no_index (unop J) where
  factors f e he :=
    ‚ü®(@factorThru C·µí·µñ _ J _ _ _ f.op e.op _).unop, Quiver.Hom.op_inj (by simp)‚ü©

instance {J : C} [Injective J] : Projective (op J) where
  factors f e epi :=
    ‚ü®(@factorThru C _ J _ _ _ f.unop e.unop _).op, Quiver.Hom.unop_inj (by simp)‚ü©

instance {P : C} [Projective P] : Injective (op P) where
  factors g f mono :=
    ‚ü®(@Projective.factorThru C _ P _ _ _ g.unop f.unop _).op, Quiver.Hom.unop_inj (by simp)‚ü©

theorem injective_iff_projective_op {J : C} : Injective J ‚Üî Projective (op J) :=
  ‚ü®fun _ => inferInstance, fun _ => show Injective (unop (op J)) from inferInstance‚ü©

theorem projective_iff_injective_op {P : C} : Projective P ‚Üî Injective (op P) :=
  ‚ü®fun _ => inferInstance, fun _ => show Projective (unop (op P)) from inferInstance‚ü©

theorem injective_iff_preservesEpimorphisms_yoneda_obj (J : C) :
    Injective J ‚Üî (yoneda.obj J).PreservesEpimorphisms := by
  rw [injective_iff_projective_op, Projective.projective_iff_preservesEpimorphisms_coyoneda_obj]
  exact Functor.preservesEpimorphisms.iso_iff (Coyoneda.objOpOp _)

section Adjunction

open CategoryTheory.Functor

variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
variable {L : C ‚•§ D} {R : D ‚•§ C} [PreservesMonomorphisms L]

theorem injective_of_adjoint (adj : L ‚ä£ R) (J : D) [Injective J] : Injective <| R.obj J :=
  ‚ü®fun {A} {_} g f im =>
    ‚ü®adj.homEquiv _ _ (factorThru ((adj.homEquiv A J).symm g) (L.map f)),
      (adj.homEquiv _ _).symm.injective
        (by simp [Adjunction.homEquiv_unit, Adjunction.homEquiv_counit])‚ü©‚ü©

end Adjunction

section EnoughInjectives

variable [EnoughInjectives C]

/-- If `C` has enough injectives, we may choose an injective presentation of `X : C`
which is given by a zero object when `X` is a zero object. -/
lemma exists_presentation (X : C) : ‚àÉ (p : InjectivePresentation X), IsZero X ‚Üí IsZero p.J := by
  by_cases h : IsZero X
  ¬∑ have := h.injective
    exact ‚ü®{ J := X, f := ùüô X}, by tauto‚ü©
  ¬∑ exact ‚ü®(EnoughInjectives.presentation X).some, by tauto‚ü©

/-- `Injective.under X` provides an arbitrarily chosen injective object equipped with
a monomorphism `Injective.Œπ : X ‚ü∂ Injective.under X`.
-/
def under (X : C) : C :=
  (exists_presentation X).choose.J

instance injective_under (X : C) : Injective (under X) :=
  (exists_presentation X).choose.injective

/-- The monomorphism `Injective.Œπ : X ‚ü∂ Injective.under X`
from the arbitrarily chosen injective object under `X`.
-/
def Œπ (X : C) : X ‚ü∂ under X :=
  (exists_presentation X).choose.f

instance Œπ_mono (X : C) : Mono (Œπ X) :=
  (exists_presentation X).choose.mono

lemma isZero_under (X : C) (hX : IsZero X) :
    IsZero (under X) :=
  (exists_presentation X).choose_spec hX

section

variable [HasZeroMorphisms C] {X Y : C} (f : X ‚ü∂ Y) [HasCokernel f]

/-- When `C` has enough injectives, the object `Injective.syzygies f` is
an arbitrarily chosen injective object under `cokernel f`.
-/
def syzygies : C :=
  under (cokernel f)
deriving Injective

/-- When `C` has enough injective,
`Injective.d f : Y ‚ü∂ syzygies f` is the composition
`cokernel.œÄ f ‚â´ Œπ (cokernel f)`.

(When `C` is abelian, we have `exact f (injective.d f)`.)
-/
abbrev d : Y ‚ü∂ syzygies f :=
  cokernel.œÄ f ‚â´ Œπ (cokernel f)

end

end EnoughInjectives

instance [EnoughInjectives C] : EnoughProjectives C·µí·µñ :=
  ‚ü®fun X => ‚ü®{ p := _, f := (Injective.Œπ (unop X)).op}‚ü©‚ü©

instance [EnoughProjectives C] : EnoughInjectives C·µí·µñ :=
  ‚ü®fun X => ‚ü®‚ü®_, inferInstance, (Projective.œÄ (unop X)).op, inferInstance‚ü©‚ü©‚ü©

theorem enoughProjectives_of_enoughInjectives_op [EnoughInjectives C·µí·µñ] : EnoughProjectives C :=
  ‚ü®fun X => ‚ü®{ p := _, f := (Injective.Œπ (op X)).unop} ‚ü©‚ü©

theorem enoughInjectives_of_enoughProjectives_op [EnoughProjectives C·µí·µñ] : EnoughInjectives C :=
  ‚ü®fun X => ‚ü®‚ü®_, inferInstance, (Projective.œÄ (op X)).unop, inferInstance‚ü©‚ü©‚ü©

end Injective

namespace Adjunction

variable {D : Type*} [Category* D] {F : C ‚•§ D} {G : D ‚•§ C}

theorem map_injective (adj : F ‚ä£ G) [F.PreservesMonomorphisms] (I : D) (hI : Injective I) :
    Injective (G.obj I) :=
  ‚ü®fun {X} {Y} f g => by
    intro
    rcases hI.factors (F.map f ‚â´ adj.counit.app _) (F.map g) with ‚ü®w,h‚ü©
    use adj.unit.app Y ‚â´ G.map w
    rw [‚Üê unit_naturality_assoc, ‚Üê G.map_comp, h]
    simp‚ü©

theorem injective_of_map_injective (adj : F ‚ä£ G) [G.Full] [G.Faithful] (I : D)
    (hI : Injective (G.obj I)) : Injective I :=
  ‚ü®fun {X} {Y} f g => by
    intro
    haveI : PreservesLimitsOfSize.{0, 0} G := adj.rightAdjoint_preservesLimits
    rcases hI.factors (G.map f) (G.map g) with ‚ü®w,h‚ü©
    use inv (adj.counit.app _) ‚â´ F.map w ‚â´ adj.counit.app _
    exact G.map_injective (by simpa)‚ü©

/-- Given an adjunction `F ‚ä£ G` such that `F` preserves monos, `G` maps an injective presentation
of `X` to an injective presentation of `G(X)`. -/
def mapInjectivePresentation (adj : F ‚ä£ G) [F.PreservesMonomorphisms] (X : D)
    (I : InjectivePresentation X) : InjectivePresentation (G.obj X) where
  J := G.obj I.J
  injective := adj.map_injective _ I.injective
  f := G.map I.f
  mono := by
    haveI : PreservesLimitsOfSize.{0, 0} G := adj.rightAdjoint_preservesLimits; infer_instance

/-- Given an adjunction `F ‚ä£ G` such that `F` preserves monomorphisms and is faithful,
  then any injective presentation of `F(X)` can be pulled back to an injective presentation of `X`.
  This is similar to `mapInjectivePresentation`. -/
def injectivePresentationOfMap (adj : F ‚ä£ G)
    [F.PreservesMonomorphisms] [F.ReflectsMonomorphisms] (X : C)
    (I : InjectivePresentation <| F.obj X) :
    InjectivePresentation X where
  J := G.obj I.J
  injective := Injective.injective_of_adjoint adj _
  f := adj.homEquiv _ _ I.f

end Adjunction

namespace Functor

variable {D : Type*} [Category* D] (F : C ‚•§ D)

theorem injective_of_map_injective [F.Full] [F.Faithful]
    [F.PreservesMonomorphisms] {I : C} (hI : Injective (F.obj I)) : Injective I where
  factors g f _ := by
    obtain ‚ü®h, fac‚ü© := hI.factors (F.map g) (F.map f)
    exact ‚ü®F.preimage h, F.map_injective (by simp [fac])‚ü©

end Functor

/--
[Lemma 3.8](https://ncatlab.org/nlab/show/injective+object#preservation_of_injective_objects)
-/
lemma EnoughInjectives.of_adjunction {C : Type u‚ÇÅ} {D : Type u‚ÇÇ}
    [Category.{v‚ÇÅ} C] [Category.{v‚ÇÇ} D]
    {L : C ‚•§ D} {R : D ‚•§ C} (adj : L ‚ä£ R) [L.PreservesMonomorphisms] [L.ReflectsMonomorphisms]
    [EnoughInjectives D] : EnoughInjectives C where
  presentation _ :=
    ‚ü®adj.injectivePresentationOfMap _ (EnoughInjectives.presentation _).some‚ü©

/-- An equivalence of categories transfers enough injectives. -/
lemma EnoughInjectives.of_equivalence {C : Type u‚ÇÅ} {D : Type u‚ÇÇ}
    [Category.{v‚ÇÅ} C] [Category.{v‚ÇÇ} D]
    (e : C ‚•§ D) [e.IsEquivalence] [EnoughInjectives D] : EnoughInjectives C :=
  EnoughInjectives.of_adjunction (adj := e.asEquivalence.toAdjunction)

namespace Equivalence

variable {D : Type*} [Category* D] (F : C ‚âå D)

theorem map_injective_iff (P : C) : Injective (F.functor.obj P) ‚Üî Injective P :=
  ‚ü®F.symm.toAdjunction.injective_of_map_injective P, F.symm.toAdjunction.map_injective P‚ü©

/-- Given an equivalence of categories `F`, an injective presentation of `F(X)` induces an
injective presentation of `X.` -/
def injectivePresentationOfMapInjectivePresentation (X : C)
    (I : InjectivePresentation (F.functor.obj X)) : InjectivePresentation X :=
  F.toAdjunction.injectivePresentationOfMap _ I

theorem enoughInjectives_iff (F : C ‚âå D) : EnoughInjectives C ‚Üî EnoughInjectives D :=
  ‚ü®fun h => h.of_adjunction F.symm.toAdjunction, fun h => h.of_adjunction F.toAdjunction‚ü©

end Equivalence

end CategoryTheory
