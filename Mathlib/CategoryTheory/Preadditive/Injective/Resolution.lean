/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Kim Morrison, Jo√´l Riou
-/
import Mathlib.Algebra.Homology.QuasiIso
import Mathlib.Algebra.Homology.ShortComplex.HomologicalComplex
import Mathlib.Algebra.Homology.SingleHomology
import Mathlib.CategoryTheory.Preadditive.Injective.Basic

/-!
# Injective resolutions

An injective resolution `I : InjectiveResolution Z` of an object `Z : C` consists of
an `‚Ñï`-indexed cochain complex `I.cocomplex` of injective objects,
along with a quasi-isomorphism `I.Œπ` from the cochain complex consisting just of `Z`
in degree zero to `I.cocomplex`.
```
Z ----> 0 ----> ... ----> 0 ----> ...
|       |                 |
|       |                 |
v       v                 v
I‚Å∞ ---> I¬π ---> ... ----> I‚Åø ---> ...
```
-/


noncomputable section

universe v u

namespace CategoryTheory

open Limits HomologicalComplex CochainComplex

variable {C : Type u} [Category.{v} C] [HasZeroObject C] [HasZeroMorphisms C]
/--
An `InjectiveResolution Z` consists of a bundled `‚Ñï`-indexed cochain complex of injective objects,
along with a quasi-isomorphism from the complex consisting of just `Z` supported in degree `0`.
-/
structure InjectiveResolution (Z : C) where
  /-- the cochain complex involved in the resolution -/
  cocomplex : CochainComplex C ‚Ñï
  /-- the cochain complex must be degreewise injective -/
  injective : ‚àÄ n, Injective (cocomplex.X n) := by infer_instance
  /-- the cochain complex must have homology -/
  [hasHomology : ‚àÄ i, cocomplex.HasHomology i]
  /-- the morphism from the single cochain complex with `Z` in degree `0` -/
  Œπ : (single‚ÇÄ C).obj Z ‚ü∂ cocomplex
  /-- the morphism from the single cochain complex with `Z` in degree `0` is a quasi-isomorphism -/
  quasiIso : QuasiIso Œπ := by infer_instance

open InjectiveResolution in
attribute [instance] injective hasHomology InjectiveResolution.quasiIso

/-- An object admits an injective resolution. -/
class HasInjectiveResolution (Z : C) : Prop where
  out : Nonempty (InjectiveResolution Z)

attribute [inherit_doc HasInjectiveResolution] HasInjectiveResolution.out

section

variable (C)

/-- You will rarely use this typeclass directly: it is implied by the combination
`[EnoughInjectives C]` and `[Abelian C]`. -/
class HasInjectiveResolutions : Prop where
  out : ‚àÄ Z : C, HasInjectiveResolution Z

attribute [instance 100] HasInjectiveResolutions.out

end

namespace InjectiveResolution

variable {Z : C} (I : InjectiveResolution Z)

lemma cocomplex_exactAt_succ (n : ‚Ñï) :
    I.cocomplex.ExactAt (n + 1) := by
  rw [‚Üê quasiIsoAt_iff_exactAt I.Œπ (n + 1) (exactAt_succ_single_obj _ _)]
  infer_instance

lemma exact_succ (n : ‚Ñï) :
    (ShortComplex.mk _ _ (I.cocomplex.d_comp_d n (n + 1) (n + 2))).Exact :=
  (HomologicalComplex.exactAt_iff' _ n (n + 1) (n + 2) (by simp)
    (by simp only [CochainComplex.next]; rfl)).1 (I.cocomplex_exactAt_succ n)

@[simp]
theorem Œπ_f_succ (n : ‚Ñï) : I.Œπ.f (n + 1) = 0 :=
  (isZero_single_obj_X _ _ _ _ (by simp)).eq_of_src _ _

@[reassoc]
theorem Œπ_f_zero_comp_complex_d :
    I.Œπ.f 0 ‚â´ I.cocomplex.d 0 1 = 0 := by
  simp

theorem complex_d_comp (n : ‚Ñï) :
    I.cocomplex.d n (n + 1) ‚â´ I.cocomplex.d (n + 1) (n + 2) = 0 := by
  simp

/-- The (limit) kernel fork given by the composition
`Z ‚ü∂ I.cocomplex.X 0 ‚ü∂ I.cocomplex.X 1` when `I : InjectiveResolution Z`. -/
@[simp]
def kernelFork : KernelFork (I.cocomplex.d 0 1) :=
  KernelFork.ofŒπ _ I.Œπ_f_zero_comp_complex_d

/-- `Z` is the kernel of `I.cocomplex.X 0 ‚ü∂ I.cocomplex.X 1` when `I : InjectiveResolution Z`. -/
def isLimitKernelFork : IsLimit (I.kernelFork) := by
  refine IsLimit.ofIsoLimit (I.cocomplex.cyclesIsKernel 0 1 (by simp)) (Iso.symm ?_)
  refine Fork.ext ((singleObjHomologySelfIso _ _ _).symm ‚â™‚â´
    isoOfQuasiIsoAt I.Œπ 0 ‚â™‚â´ I.cocomplex.isoHomologyœÄ‚ÇÄ.symm) ?_
  rw [‚Üê cancel_epi (singleObjHomologySelfIso (ComplexShape.up ‚Ñï) _ _).hom,
    ‚Üê cancel_epi (isoHomologyœÄ‚ÇÄ _).hom,
    ‚Üê cancel_epi (singleObjCyclesSelfIso (ComplexShape.up ‚Ñï) _ _).inv]
  simp

instance (n : ‚Ñï) : Mono (I.Œπ.f n) := by
  cases n
  ¬∑ exact mono_of_isLimit_fork I.isLimitKernelFork
  ¬∑ rw [Œπ_f_succ]; infer_instance

variable (Z)

/-- An injective object admits a trivial injective resolution: itself in degree 0. -/
@[simps]
def self [Injective Z] : InjectiveResolution Z where
  cocomplex := (CochainComplex.single‚ÇÄ C).obj Z
  Œπ := ùüô ((CochainComplex.single‚ÇÄ C).obj Z)
  injective n := by
    cases n
    ¬∑ simpa
    ¬∑ apply IsZero.injective
      apply HomologicalComplex.isZero_single_obj_X
      simp

end InjectiveResolution

end CategoryTheory
