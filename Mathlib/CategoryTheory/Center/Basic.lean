/-
Copyright (c) 2025 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
module

public import Mathlib.CategoryTheory.Endomorphism

/-!
# The center of a category

Given a category `C`, we introduce an abbreviation `CatCenter C` for
the center of the category `C`, which is `End (ğŸ­ C)`, the
type of endomorphisms of the identity functor of `C`.

## References
* https://ncatlab.org/nlab/show/center+of+a+category

-/

@[expose] public section
universe v u

namespace CategoryTheory

open Category

variable (C : Type u) [Category.{v} C]

/-- The center of a category `C` is the type `End (ğŸ­ C)` of the endomorphisms
of the identify functor of `C`. -/
abbrev CatCenter := End (ğŸ­ C)

namespace CatCenter

variable {C}

/-- The action of the center of a category on an object. (This is necessary as
`NatTrans.app x X` is syntactically an endomorphism of `(ğŸ­ C).obj X`
rather than of `X`.) -/
abbrev app (x : CatCenter C) (X : C) : X âŸ¶ X := NatTrans.app x X

@[ext]
lemma ext (x y : CatCenter C) (h : âˆ€ (X : C), x.app X = y.app X) : x = y :=
  NatTrans.ext (funext h)

@[reassoc]
lemma naturality (z : CatCenter C) {X Y : C} (f : X âŸ¶ Y) :
    f â‰« z.app Y = z.app X â‰« f := NatTrans.naturality z f

@[reassoc]
lemma mul_app' (x y : CatCenter C) (X : C) : (x * y).app X = y.app X â‰« x.app X := rfl

@[reassoc]
lemma mul_app (x y : CatCenter C) (X : C) : (x * y).app X = x.app X â‰« y.app X := by
  rw [mul_app']
  exact x.naturality (y.app X)

instance : IsMulCommutative (CatCenter C) where
  is_comm := âŸ¨fun x y â†¦ by
    ext X
    rw [mul_app' x y, mul_app y x]âŸ©

instance {X Y : C} : SMul (CatCenter C) (X âŸ¶ Y) where
  smul z f := f â‰« z.app Y

lemma smul_eq (z : CatCenter C) {X Y : C} (f : X âŸ¶ Y) : z â€¢ f = f â‰« z.app Y := rfl

lemma smul_eq' (z : CatCenter C) {X Y : C} (f : X âŸ¶ Y) : z â€¢ f = z.app X â‰« f :=
  z.naturality f

instance {X Y : C} : SMul (CatCenter C)Ë£ (X â‰… Y) where
  smul z e :=
    { hom := z.1 â€¢ e.hom
      inv := (zâ»Â¹).1 â€¢ e.inv
      hom_inv_id := by
        rw [smul_eq, smul_eq', Category.assoc, â† mul_app_assoc]
        simp
      inv_hom_id := by
        rw [smul_eq, smul_eq', Category.assoc, â† mul_app_assoc]
        simp }

@[reassoc]
lemma smul_iso_hom_eq (z : (CatCenter C)Ë£) {X Y : C} (f : X â‰… Y) :
    (z â€¢ f).hom = f.hom â‰« z.1.app Y := rfl

@[reassoc]
lemma smul_iso_hom_eq' (z : (CatCenter C)Ë£) {X Y : C} (f : X â‰… Y) :
    (z â€¢ f).hom = z.1.app X â‰« f.hom :=
  z.1.naturality f.hom

@[reassoc]
lemma smul_iso_inv_eq (z : (CatCenter C)Ë£) {X Y : C} (f : X â‰… Y) :
    (z â€¢ f).inv = f.inv â‰« (zâ»Â¹.1).app X := rfl

@[reassoc]
lemma smul_iso_inv_eq' (z : (CatCenter C)Ë£) {X Y : C} (f : X â‰… Y) :
    (z â€¢ f).inv = (zâ»Â¹.1).app Y â‰« f.inv :=
  z.2.naturality f.inv

end CatCenter

end CategoryTheory
