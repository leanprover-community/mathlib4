/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Iso
import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.Functor.FullyFaithful

/-!
# Whiskering

Given a functor `F : C â¥¤ D` and functors `G H : D â¥¤ E` and a natural transformation `Î± : G âŸ¶ H`,
we can construct a new natural transformation `F â‹™ G âŸ¶ F â‹™ H`,
called `whiskerLeft F Î±`. This is the same as the horizontal composition of `ğŸ™ F` with `Î±`.

This operation is functorial in `F`, and we package this as `whiskeringLeft`. Here
`(whiskeringLeft.obj F).obj G` is `F â‹™ G`, and
`(whiskeringLeft.obj F).map Î±` is `whiskerLeft F Î±`.
(That is, we might have alternatively named this as the "left composition functor".)

We also provide analogues for composition on the right, and for these operations on isomorphisms.

At the end of the file, we provide the left and right unitors, and the associator,
for functor composition.
(In fact functor composition is definitionally associative, but very often relying on this causes
extremely slow elaboration, so it is better to insert it explicitly.)
We also show these natural isomorphisms satisfy the triangle and pentagon identities.
-/


namespace CategoryTheory

universe uâ‚ vâ‚ uâ‚‚ vâ‚‚ uâ‚ƒ vâ‚ƒ uâ‚„ vâ‚„

section

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D] {E : Type uâ‚ƒ}
  [Category.{vâ‚ƒ} E]

/-- If `Î± : G âŸ¶ H` then
`whiskerLeft F Î± : (F â‹™ G) âŸ¶ (F â‹™ H)` has components `Î±.app (F.obj X)`.
-/
@[simps]
def whiskerLeft (F : C â¥¤ D) {G H : D â¥¤ E} (Î± : G âŸ¶ H) :
    F â‹™ G âŸ¶ F â‹™ H where
  app X := Î±.app (F.obj X)
  naturality X Y f := by rw [Functor.comp_map, Functor.comp_map, Î±.naturality]

/-- If `Î± : G âŸ¶ H` then
`whisker_right Î± F : (G â‹™ F) âŸ¶ (G â‹™ F)` has components `F.map (Î±.app X)`.
-/
@[simps]
def whiskerRight {G H : C â¥¤ D} (Î± : G âŸ¶ H) (F : D â¥¤ E) :
    G â‹™ F âŸ¶ H â‹™ F where
  app X := F.map (Î±.app X)
  naturality X Y f := by
    rw [Functor.comp_map, Functor.comp_map, â† F.map_comp, â† F.map_comp, Î±.naturality]

variable (C D E)

/-- Left-composition gives a functor `(C â¥¤ D) â¥¤ ((D â¥¤ E) â¥¤ (C â¥¤ E))`.

`(whiskeringLeft.obj F).obj G` is `F â‹™ G`, and
`(whiskeringLeft.obj F).map Î±` is `whiskerLeft F Î±`.
-/
@[simps]
def whiskeringLeft : (C â¥¤ D) â¥¤ (D â¥¤ E) â¥¤ C â¥¤ E where
  obj F :=
    { obj := fun G => F â‹™ G
      map := fun Î± => whiskerLeft F Î± }
  map Ï„ :=
    { app := fun H =>
        { app := fun c => H.map (Ï„.app c)
          naturality := fun X Y f => by dsimp; rw [â† H.map_comp, â† H.map_comp, â† Ï„.naturality] }
      naturality := fun X Y f => by ext; dsimp; rw [f.naturality] }

/-- Right-composition gives a functor `(D â¥¤ E) â¥¤ ((C â¥¤ D) â¥¤ (C â¥¤ E))`.

`(whiskeringRight.obj H).obj F` is `F â‹™ H`, and
`(whiskeringRight.obj H).map Î±` is `whiskerRight Î± H`.
-/
@[simps]
def whiskeringRight : (D â¥¤ E) â¥¤ (C â¥¤ D) â¥¤ C â¥¤ E where
  obj H :=
    { obj := fun F => F â‹™ H
      map := fun Î± => whiskerRight Î± H }
  map Ï„ :=
    { app := fun F =>
        { app := fun c => Ï„.app (F.obj c)
          naturality := fun X Y f => by dsimp; rw [Ï„.naturality] }
      naturality := fun X Y f => by ext; dsimp; rw [â† NatTrans.naturality] }

variable {C} {D} {E}

instance faithful_whiskeringRight_obj {F : D â¥¤ E} [F.Faithful] :
    ((whiskeringRight C D E).obj F).Faithful where
  map_injective hÎ±Î² := by
    ext X
    exact F.map_injective <| congr_fun (congr_arg NatTrans.app hÎ±Î²) X

/-- If `F : D â¥¤ E` is fully faithful, then so is
`(whiskeringRight C D E).obj F : (C â¥¤ D) â¥¤ C â¥¤ E`. -/
@[simps]
def Functor.FullyFaithful.whiskeringRight {F : D â¥¤ E} (hF : F.FullyFaithful)
    (C : Type*) [Category C] :
    ((whiskeringRight C D E).obj F).FullyFaithful where
  preimage f :=
    { app := fun X => hF.preimage (f.app X)
      naturality := fun _ _ g => by
        apply hF.map_injective
        dsimp
        simp only [map_comp, map_preimage]
        apply f.naturality }

instance full_whiskeringRight_obj {F : D â¥¤ E} [F.Faithful] [F.Full] :
    ((whiskeringRight C D E).obj F).Full :=
  ((Functor.FullyFaithful.ofFullyFaithful F).whiskeringRight C).full

@[simp]
theorem whiskerLeft_id (F : C â¥¤ D) {G : D â¥¤ E} :
    whiskerLeft F (NatTrans.id G) = NatTrans.id (F.comp G) :=
  rfl

@[simp]
theorem whiskerLeft_id' (F : C â¥¤ D) {G : D â¥¤ E} : whiskerLeft F (ğŸ™ G) = ğŸ™ (F.comp G) :=
  rfl

@[simp]
theorem whiskerRight_id {G : C â¥¤ D} (F : D â¥¤ E) :
    whiskerRight (NatTrans.id G) F = NatTrans.id (G.comp F) :=
  ((whiskeringRight C D E).obj F).map_id _

@[simp]
theorem whiskerRight_id' {G : C â¥¤ D} (F : D â¥¤ E) : whiskerRight (ğŸ™ G) F = ğŸ™ (G.comp F) :=
  ((whiskeringRight C D E).obj F).map_id _

@[simp, reassoc]
theorem whiskerLeft_comp (F : C â¥¤ D) {G H K : D â¥¤ E} (Î± : G âŸ¶ H) (Î² : H âŸ¶ K) :
    whiskerLeft F (Î± â‰« Î²) = whiskerLeft F Î± â‰« whiskerLeft F Î² :=
  rfl

@[simp, reassoc]
theorem whiskerRight_comp {G H K : C â¥¤ D} (Î± : G âŸ¶ H) (Î² : H âŸ¶ K) (F : D â¥¤ E) :
    whiskerRight (Î± â‰« Î²) F = whiskerRight Î± F â‰« whiskerRight Î² F :=
  ((whiskeringRight C D E).obj F).map_comp Î± Î²

/-- If `Î± : G â‰… H` is a natural isomorphism then
`iso_whisker_left F Î± : (F â‹™ G) â‰… (F â‹™ H)` has components `Î±.app (F.obj X)`.
-/
def isoWhiskerLeft (F : C â¥¤ D) {G H : D â¥¤ E} (Î± : G â‰… H) : F â‹™ G â‰… F â‹™ H :=
  ((whiskeringLeft C D E).obj F).mapIso Î±

@[simp]
theorem isoWhiskerLeft_hom (F : C â¥¤ D) {G H : D â¥¤ E} (Î± : G â‰… H) :
    (isoWhiskerLeft F Î±).hom = whiskerLeft F Î±.hom :=
  rfl

@[simp]
theorem isoWhiskerLeft_inv (F : C â¥¤ D) {G H : D â¥¤ E} (Î± : G â‰… H) :
    (isoWhiskerLeft F Î±).inv = whiskerLeft F Î±.inv :=
  rfl

/-- If `Î± : G â‰… H` then
`iso_whisker_right Î± F : (G â‹™ F) â‰… (H â‹™ F)` has components `F.map_iso (Î±.app X)`.
-/
def isoWhiskerRight {G H : C â¥¤ D} (Î± : G â‰… H) (F : D â¥¤ E) : G â‹™ F â‰… H â‹™ F :=
  ((whiskeringRight C D E).obj F).mapIso Î±

@[simp]
theorem isoWhiskerRight_hom {G H : C â¥¤ D} (Î± : G â‰… H) (F : D â¥¤ E) :
    (isoWhiskerRight Î± F).hom = whiskerRight Î±.hom F :=
  rfl

@[simp]
theorem isoWhiskerRight_inv {G H : C â¥¤ D} (Î± : G â‰… H) (F : D â¥¤ E) :
    (isoWhiskerRight Î± F).inv = whiskerRight Î±.inv F :=
  rfl

instance isIso_whiskerLeft (F : C â¥¤ D) {G H : D â¥¤ E} (Î± : G âŸ¶ H) [IsIso Î±] :
    IsIso (whiskerLeft F Î±) :=
  (isoWhiskerLeft F (asIso Î±)).isIso_hom

instance isIso_whiskerRight {G H : C â¥¤ D} (Î± : G âŸ¶ H) (F : D â¥¤ E) [IsIso Î±] :
    IsIso (whiskerRight Î± F) :=
  (isoWhiskerRight (asIso Î±) F).isIso_hom

variable {B : Type uâ‚„} [Category.{vâ‚„} B]

-- Porting note: it was `attribute [local elab_without_expected_type]`,
-- but now `elab_without_expected-type` must be global
attribute [elab_without_expected_type] whiskerLeft whiskerRight

@[simp]
theorem whiskerLeft_twice (F : B â¥¤ C) (G : C â¥¤ D) {H K : D â¥¤ E} (Î± : H âŸ¶ K) :
    whiskerLeft F (whiskerLeft G Î±) = whiskerLeft (F â‹™ G) Î± :=
  rfl

@[simp]
theorem whiskerRight_twice {H K : B â¥¤ C} (F : C â¥¤ D) (G : D â¥¤ E) (Î± : H âŸ¶ K) :
    whiskerRight (whiskerRight Î± F) G = whiskerRight Î± (F â‹™ G) :=
  rfl

theorem whiskerRight_left (F : B â¥¤ C) {G H : C â¥¤ D} (Î± : G âŸ¶ H) (K : D â¥¤ E) :
    whiskerRight (whiskerLeft F Î±) K = whiskerLeft F (whiskerRight Î± K) :=
  rfl

end

namespace Functor

universe uâ‚… vâ‚…

variable {A : Type uâ‚} [Category.{vâ‚} A]
variable {B : Type uâ‚‚} [Category.{vâ‚‚} B]

/-- The left unitor, a natural isomorphism `((ğŸ­ _) â‹™ F) â‰… F`.
-/
@[simps]
def leftUnitor (F : A â¥¤ B) :
    ğŸ­ A â‹™ F â‰… F where
  hom := { app := fun X => ğŸ™ (F.obj X) }
  inv := { app := fun X => ğŸ™ (F.obj X) }

/-- The right unitor, a natural isomorphism `(F â‹™ (ğŸ­ B)) â‰… F`.
-/
@[simps]
def rightUnitor (F : A â¥¤ B) :
    F â‹™ ğŸ­ B â‰… F where
  hom := { app := fun X => ğŸ™ (F.obj X) }
  inv := { app := fun X => ğŸ™ (F.obj X) }

variable {C : Type uâ‚ƒ} [Category.{vâ‚ƒ} C]
variable {D : Type uâ‚„} [Category.{vâ‚„} D]

/-- The associator for functors, a natural isomorphism `((F â‹™ G) â‹™ H) â‰… (F â‹™ (G â‹™ H))`.

(In fact, `iso.refl _` will work here, but it tends to make Lean slow later,
and it's usually best to insert explicit associators.)
-/
@[simps]
def associator (F : A â¥¤ B) (G : B â¥¤ C) (H : C â¥¤ D) :
    (F â‹™ G) â‹™ H â‰… F â‹™ G â‹™ H where
  hom := { app := fun _ => ğŸ™ _ }
  inv := { app := fun _ => ğŸ™ _ }

protected theorem assoc (F : A â¥¤ B) (G : B â¥¤ C) (H : C â¥¤ D) : (F â‹™ G) â‹™ H = F â‹™ G â‹™ H :=
  rfl

theorem triangle (F : A â¥¤ B) (G : B â¥¤ C) :
    (associator F (ğŸ­ B) G).hom â‰« whiskerLeft F (leftUnitor G).hom =
      whiskerRight (rightUnitor F).hom G := by aesop_cat

-- See note [dsimp, simp].
variable {E : Type uâ‚…} [Category.{vâ‚…} E]
variable (F : A â¥¤ B) (G : B â¥¤ C) (H : C â¥¤ D) (K : D â¥¤ E)

theorem pentagon :
    whiskerRight (associator F G H).hom K â‰«
        (associator F (G â‹™ H) K).hom â‰« whiskerLeft F (associator G H K).hom =
      (associator (F â‹™ G) H K).hom â‰« (associator F G (H â‹™ K)).hom := by aesop_cat

end Functor

end CategoryTheory
