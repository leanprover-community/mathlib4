/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Iso
import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.Functor.FullyFaithful

#align_import category_theory.whiskering from "leanprover-community/mathlib"@"d012cd09a9b256d870751284dd6a29882b0be105"

/-!
# Whiskering

Given a functor `F : C ‚•§ D` and functors `G H : D ‚•§ E` and a natural transformation `Œ± : G ‚ü∂ H`,
we can construct a new natural transformation `F ‚ãô G ‚ü∂ F ‚ãô H`,
called `whiskerLeft F Œ±`. This is the same as the horizontal composition of `ùüô F` with `Œ±`.

This operation is functorial in `F`, and we package this as `whiskeringLeft`. Here
`(whiskeringLeft.obj F).obj G` is `F ‚ãô G`, and
`(whiskeringLeft.obj F).map Œ±` is `whiskerLeft F Œ±`.
(That is, we might have alternatively named this as the "left composition functor".)

We also provide analogues for composition on the right, and for these operations on isomorphisms.

At the end of the file, we provide the left and right unitors, and the associator,
for functor composition.
(In fact functor composition is definitionally associative, but very often relying on this causes
extremely slow elaboration, so it is better to insert it explicitly.)
We also show these natural isomorphisms satisfy the triangle and pentagon identities.
-/


namespace CategoryTheory

universe u‚ÇÅ v‚ÇÅ u‚ÇÇ v‚ÇÇ u‚ÇÉ v‚ÇÉ u‚ÇÑ v‚ÇÑ

section

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] {E : Type u‚ÇÉ}
  [Category.{v‚ÇÉ} E]

/-- If `Œ± : G ‚ü∂ H` then
`whiskerLeft F Œ± : (F ‚ãô G) ‚ü∂ (F ‚ãô H)` has components `Œ±.app (F.obj X)`.
-/
@[simps]
def whiskerLeft (F : C ‚•§ D) {G H : D ‚•§ E} (Œ± : G ‚ü∂ H) :
    F ‚ãô G ‚ü∂ F ‚ãô H where
  app X := Œ±.app (F.obj X)
  naturality X Y f := by rw [Functor.comp_map, Functor.comp_map, Œ±.naturality]
#align category_theory.whisker_left CategoryTheory.whiskerLeft
#align category_theory.whisker_left_app CategoryTheory.whiskerLeft_app

/-- If `Œ± : G ‚ü∂ H` then
`whisker_right Œ± F : (G ‚ãô F) ‚ü∂ (G ‚ãô F)` has components `F.map (Œ±.app X)`.
-/
@[simps]
def whiskerRight {G H : C ‚•§ D} (Œ± : G ‚ü∂ H) (F : D ‚•§ E) :
    G ‚ãô F ‚ü∂ H ‚ãô F where
  app X := F.map (Œ±.app X)
  naturality X Y f := by
    rw [Functor.comp_map, Functor.comp_map, ‚Üê F.map_comp, ‚Üê F.map_comp, Œ±.naturality]
#align category_theory.whisker_right CategoryTheory.whiskerRight
#align category_theory.whisker_right_app CategoryTheory.whiskerRight_app

variable (C D E)

/-- Left-composition gives a functor `(C ‚•§ D) ‚•§ ((D ‚•§ E) ‚•§ (C ‚•§ E))`.

`(whiskeringLeft.obj F).obj G` is `F ‚ãô G`, and
`(whiskeringLeft.obj F).map Œ±` is `whiskerLeft F Œ±`.
-/
@[simps]
def whiskeringLeft : (C ‚•§ D) ‚•§ (D ‚•§ E) ‚•§ C ‚•§ E where
  obj F :=
    { obj := fun G => F ‚ãô G
      map := fun Œ± => whiskerLeft F Œ± }
  map œÑ :=
    { app := fun H =>
        { app := fun c => H.map (œÑ.app c)
          naturality := fun X Y f => by dsimp; rw [‚Üê H.map_comp, ‚Üê H.map_comp, ‚Üê œÑ.naturality] }
      naturality := fun X Y f => by ext; dsimp; rw [f.naturality] }
#align category_theory.whiskering_left CategoryTheory.whiskeringLeft
#align category_theory.whiskering_left_obj_map CategoryTheory.whiskeringLeft_obj_map
#align category_theory.whiskering_left_obj_obj CategoryTheory.whiskeringLeft_obj_obj
#align category_theory.whiskering_left_map_app_app CategoryTheory.whiskeringLeft_map_app_app

/-- Right-composition gives a functor `(D ‚•§ E) ‚•§ ((C ‚•§ D) ‚•§ (C ‚•§ E))`.

`(whiskeringRight.obj H).obj F` is `F ‚ãô H`, and
`(whiskeringRight.obj H).map Œ±` is `whiskerRight Œ± H`.
-/
@[simps]
def whiskeringRight : (D ‚•§ E) ‚•§ (C ‚•§ D) ‚•§ C ‚•§ E where
  obj H :=
    { obj := fun F => F ‚ãô H
      map := fun Œ± => whiskerRight Œ± H }
  map œÑ :=
    { app := fun F =>
        { app := fun c => œÑ.app (F.obj c)
          naturality := fun X Y f => by dsimp; rw [œÑ.naturality] }
      naturality := fun X Y f => by ext; dsimp; rw [‚Üê NatTrans.naturality] }
#align category_theory.whiskering_right CategoryTheory.whiskeringRight
#align category_theory.whiskering_right_map_app_app CategoryTheory.whiskeringRight_map_app_app
#align category_theory.whiskering_right_obj_obj CategoryTheory.whiskeringRight_obj_obj
#align category_theory.whiskering_right_obj_map CategoryTheory.whiskeringRight_obj_map

variable {C} {D} {E}

instance faithful_whiskeringRight_obj {F : D ‚•§ E} [F.Faithful] :
    ((whiskeringRight C D E).obj F).Faithful where
  map_injective hŒ±Œ≤ := by
    ext X
    exact F.map_injective <| congr_fun (congr_arg NatTrans.app hŒ±Œ≤) X
#align category_theory.faithful_whiskering_right_obj CategoryTheory.faithful_whiskeringRight_obj

/-- If `F : D ‚•§ E` is fully faithful, then so is
`(whiskeringRight C D E).obj F : (C ‚•§ D) ‚•§ C ‚•§ E`. -/
@[simps]
def Functor.FullyFaithful.whiskeringRight {F : D ‚•§ E} (hF : F.FullyFaithful)
    (C : Type*) [Category C] :
    ((whiskeringRight C D E).obj F).FullyFaithful where
  preimage f :=
    { app := fun X => hF.preimage (f.app X)
      naturality := fun _ _ g => by
        apply hF.map_injective
        dsimp
        simp only [map_comp, map_preimage]
        apply f.naturality }

instance full_whiskeringRight_obj {F : D ‚•§ E} [F.Faithful] [F.Full] :
    ((whiskeringRight C D E).obj F).Full :=
  ((Functor.FullyFaithful.ofFullyFaithful F).whiskeringRight C).full

@[simp]
theorem whiskerLeft_id (F : C ‚•§ D) {G : D ‚•§ E} :
    whiskerLeft F (NatTrans.id G) = NatTrans.id (F.comp G) :=
  rfl
#align category_theory.whisker_left_id CategoryTheory.whiskerLeft_id

@[simp]
theorem whiskerLeft_id' (F : C ‚•§ D) {G : D ‚•§ E} : whiskerLeft F (ùüô G) = ùüô (F.comp G) :=
  rfl
#align category_theory.whisker_left_id' CategoryTheory.whiskerLeft_id'

@[simp]
theorem whiskerRight_id {G : C ‚•§ D} (F : D ‚•§ E) :
    whiskerRight (NatTrans.id G) F = NatTrans.id (G.comp F) :=
  ((whiskeringRight C D E).obj F).map_id _
#align category_theory.whisker_right_id CategoryTheory.whiskerRight_id

@[simp]
theorem whiskerRight_id' {G : C ‚•§ D} (F : D ‚•§ E) : whiskerRight (ùüô G) F = ùüô (G.comp F) :=
  ((whiskeringRight C D E).obj F).map_id _
#align category_theory.whisker_right_id' CategoryTheory.whiskerRight_id'

@[simp, reassoc]
theorem whiskerLeft_comp (F : C ‚•§ D) {G H K : D ‚•§ E} (Œ± : G ‚ü∂ H) (Œ≤ : H ‚ü∂ K) :
    whiskerLeft F (Œ± ‚â´ Œ≤) = whiskerLeft F Œ± ‚â´ whiskerLeft F Œ≤ :=
  rfl
#align category_theory.whisker_left_comp CategoryTheory.whiskerLeft_comp

@[simp, reassoc]
theorem whiskerRight_comp {G H K : C ‚•§ D} (Œ± : G ‚ü∂ H) (Œ≤ : H ‚ü∂ K) (F : D ‚•§ E) :
    whiskerRight (Œ± ‚â´ Œ≤) F = whiskerRight Œ± F ‚â´ whiskerRight Œ≤ F :=
  ((whiskeringRight C D E).obj F).map_comp Œ± Œ≤
#align category_theory.whisker_right_comp CategoryTheory.whiskerRight_comp

/-- If `Œ± : G ‚âÖ H` is a natural isomorphism then
`iso_whisker_left F Œ± : (F ‚ãô G) ‚âÖ (F ‚ãô H)` has components `Œ±.app (F.obj X)`.
-/
def isoWhiskerLeft (F : C ‚•§ D) {G H : D ‚•§ E} (Œ± : G ‚âÖ H) : F ‚ãô G ‚âÖ F ‚ãô H :=
  ((whiskeringLeft C D E).obj F).mapIso Œ±
#align category_theory.iso_whisker_left CategoryTheory.isoWhiskerLeft

@[simp]
theorem isoWhiskerLeft_hom (F : C ‚•§ D) {G H : D ‚•§ E} (Œ± : G ‚âÖ H) :
    (isoWhiskerLeft F Œ±).hom = whiskerLeft F Œ±.hom :=
  rfl
#align category_theory.iso_whisker_left_hom CategoryTheory.isoWhiskerLeft_hom

@[simp]
theorem isoWhiskerLeft_inv (F : C ‚•§ D) {G H : D ‚•§ E} (Œ± : G ‚âÖ H) :
    (isoWhiskerLeft F Œ±).inv = whiskerLeft F Œ±.inv :=
  rfl
#align category_theory.iso_whisker_left_inv CategoryTheory.isoWhiskerLeft_inv

/-- If `Œ± : G ‚âÖ H` then
`iso_whisker_right Œ± F : (G ‚ãô F) ‚âÖ (H ‚ãô F)` has components `F.map_iso (Œ±.app X)`.
-/
def isoWhiskerRight {G H : C ‚•§ D} (Œ± : G ‚âÖ H) (F : D ‚•§ E) : G ‚ãô F ‚âÖ H ‚ãô F :=
  ((whiskeringRight C D E).obj F).mapIso Œ±
#align category_theory.iso_whisker_right CategoryTheory.isoWhiskerRight

@[simp]
theorem isoWhiskerRight_hom {G H : C ‚•§ D} (Œ± : G ‚âÖ H) (F : D ‚•§ E) :
    (isoWhiskerRight Œ± F).hom = whiskerRight Œ±.hom F :=
  rfl
#align category_theory.iso_whisker_right_hom CategoryTheory.isoWhiskerRight_hom

@[simp]
theorem isoWhiskerRight_inv {G H : C ‚•§ D} (Œ± : G ‚âÖ H) (F : D ‚•§ E) :
    (isoWhiskerRight Œ± F).inv = whiskerRight Œ±.inv F :=
  rfl
#align category_theory.iso_whisker_right_inv CategoryTheory.isoWhiskerRight_inv

instance isIso_whiskerLeft (F : C ‚•§ D) {G H : D ‚•§ E} (Œ± : G ‚ü∂ H) [IsIso Œ±] :
    IsIso (whiskerLeft F Œ±) :=
  (isoWhiskerLeft F (asIso Œ±)).isIso_hom
#align category_theory.is_iso_whisker_left CategoryTheory.isIso_whiskerLeft

instance isIso_whiskerRight {G H : C ‚•§ D} (Œ± : G ‚ü∂ H) (F : D ‚•§ E) [IsIso Œ±] :
    IsIso (whiskerRight Œ± F) :=
  (isoWhiskerRight (asIso Œ±) F).isIso_hom
#align category_theory.is_iso_whisker_right CategoryTheory.isIso_whiskerRight

variable {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B]

-- Porting note: it was `attribute [local elab_without_expected_type]`,
-- but now `elab_without_expected-type` must be global
attribute [elab_without_expected_type] whiskerLeft whiskerRight

@[simp]
theorem whiskerLeft_twice (F : B ‚•§ C) (G : C ‚•§ D) {H K : D ‚•§ E} (Œ± : H ‚ü∂ K) :
    whiskerLeft F (whiskerLeft G Œ±) = whiskerLeft (F ‚ãô G) Œ± :=
  rfl
#align category_theory.whisker_left_twice CategoryTheory.whiskerLeft_twice

@[simp]
theorem whiskerRight_twice {H K : B ‚•§ C} (F : C ‚•§ D) (G : D ‚•§ E) (Œ± : H ‚ü∂ K) :
    whiskerRight (whiskerRight Œ± F) G = whiskerRight Œ± (F ‚ãô G) :=
  rfl
#align category_theory.whisker_right_twice CategoryTheory.whiskerRight_twice

theorem whiskerRight_left (F : B ‚•§ C) {G H : C ‚•§ D} (Œ± : G ‚ü∂ H) (K : D ‚•§ E) :
    whiskerRight (whiskerLeft F Œ±) K = whiskerLeft F (whiskerRight Œ± K) :=
  rfl
#align category_theory.whisker_right_left CategoryTheory.whiskerRight_left

end

namespace Functor

universe u‚ÇÖ v‚ÇÖ

variable {A : Type u‚ÇÅ} [Category.{v‚ÇÅ} A]
variable {B : Type u‚ÇÇ} [Category.{v‚ÇÇ} B]

/-- The left unitor, a natural isomorphism `((ùü≠ _) ‚ãô F) ‚âÖ F`.
-/
@[simps]
def leftUnitor (F : A ‚•§ B) :
    ùü≠ A ‚ãô F ‚âÖ F where
  hom := { app := fun X => ùüô (F.obj X) }
  inv := { app := fun X => ùüô (F.obj X) }
#align category_theory.functor.left_unitor CategoryTheory.Functor.leftUnitor
#align category_theory.functor.left_unitor_inv_app CategoryTheory.Functor.leftUnitor_inv_app
#align category_theory.functor.left_unitor_hom_app CategoryTheory.Functor.leftUnitor_hom_app

/-- The right unitor, a natural isomorphism `(F ‚ãô (ùü≠ B)) ‚âÖ F`.
-/
@[simps]
def rightUnitor (F : A ‚•§ B) :
    F ‚ãô ùü≠ B ‚âÖ F where
  hom := { app := fun X => ùüô (F.obj X) }
  inv := { app := fun X => ùüô (F.obj X) }
#align category_theory.functor.right_unitor CategoryTheory.Functor.rightUnitor
#align category_theory.functor.right_unitor_hom_app CategoryTheory.Functor.rightUnitor_hom_app
#align category_theory.functor.right_unitor_inv_app CategoryTheory.Functor.rightUnitor_inv_app

variable {C : Type u‚ÇÉ} [Category.{v‚ÇÉ} C]
variable {D : Type u‚ÇÑ} [Category.{v‚ÇÑ} D]

/-- The associator for functors, a natural isomorphism `((F ‚ãô G) ‚ãô H) ‚âÖ (F ‚ãô (G ‚ãô H))`.

(In fact, `iso.refl _` will work here, but it tends to make Lean slow later,
and it's usually best to insert explicit associators.)
-/
@[simps]
def associator (F : A ‚•§ B) (G : B ‚•§ C) (H : C ‚•§ D) :
    (F ‚ãô G) ‚ãô H ‚âÖ F ‚ãô G ‚ãô H where
  hom := { app := fun _ => ùüô _ }
  inv := { app := fun _ => ùüô _ }
#align category_theory.functor.associator CategoryTheory.Functor.associator
#align category_theory.functor.associator_inv_app CategoryTheory.Functor.associator_inv_app
#align category_theory.functor.associator_hom_app CategoryTheory.Functor.associator_hom_app

protected theorem assoc (F : A ‚•§ B) (G : B ‚•§ C) (H : C ‚•§ D) : (F ‚ãô G) ‚ãô H = F ‚ãô G ‚ãô H :=
  rfl
#align category_theory.functor.assoc CategoryTheory.Functor.assoc

theorem triangle (F : A ‚•§ B) (G : B ‚•§ C) :
    (associator F (ùü≠ B) G).hom ‚â´ whiskerLeft F (leftUnitor G).hom =
      whiskerRight (rightUnitor F).hom G := by aesop_cat
#align category_theory.functor.triangle CategoryTheory.Functor.triangle

-- See note [dsimp, simp].
variable {E : Type u‚ÇÖ} [Category.{v‚ÇÖ} E]
variable (F : A ‚•§ B) (G : B ‚•§ C) (H : C ‚•§ D) (K : D ‚•§ E)

theorem pentagon :
    whiskerRight (associator F G H).hom K ‚â´
        (associator F (G ‚ãô H) K).hom ‚â´ whiskerLeft F (associator G H K).hom =
      (associator (F ‚ãô G) H K).hom ‚â´ (associator F G (H ‚ãô K)).hom := by aesop_cat
#align category_theory.functor.pentagon CategoryTheory.Functor.pentagon

end Functor

end CategoryTheory
