/-
Copyright (c) 2024 Sina Hazratpour. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sina Hazratpour
-/

import Mathlib.CategoryTheory.Category.Preorder
import Mathlib.CategoryTheory.Displayed.Fiber

/-!
# Displayed category

Given a type family `F : C ‚Üí Type*` on a category `C` we define the type class `Display F`
of displayed categories over `F`. A displayed category structure associates to each morphism `f`
in `C`  and terms `X : F I` and `Y : F J` a type `HomOver f X Y`.
We think of `F I` as the Fiber over `I`, and we think of `HomOver f X Y` as the type of morphisms
lying over `f` starting from `X` and ending at `Y`. The data of a displayed category structure
also provides the dependent operations of identity and composition for `HomOver`.
Finally, the modified laws of associativity and unitality hold dependently over the associativity
and unitality equalities in `C`.

## Main declarations

Our main construction is the displayed category of a functor. Given a functor `P : E ‚•§ C`, the
associated displayed category on the fiber family `fun I => P‚Åª¬π I` is provided by the instance
`Functor.display`. Here `HomOver f X Y ` is given by the type `BasedLift f src tgt` carrying data
witnessing morphisms in `E` starting from `src` and ending at `tgt` and are mapped to `f` under `P`.

There is another displayed structure `EBasedLift` associated to a functor `P : E ‚•§ C` which is
defined in terms of the displayed family of "fat" fibers, namely `fun I => P‚Åª¬π·µâ I` where `P‚Åª¬π·µâ I`
is the fibers of `P` at `J` for all `J` isomorphic to `I`. The type `EBasedLift f src tgt` is the
type of morphisms in `E` starting from `src` and ending at `tgt` and are mapped, up to the specified
isomorphisms of `src` and `tgt`, to `f` under `P`.

We also provide various useful constructors for based-lifts:

* `BasedLift.tauto` regards a morphism `g` of the domain category `E` as a
  tautological based-lift of its image `P.map g`.

* `BasedLift.id` and `BasedLift.comp` provide the identity and composition of
  based-lifts, respectively.

* `BasedLift.cast` casts a based-lift along an equality of the base morphisms using the equivalence
`BasedLift.cast`.

### Notation

We provide the following notations:

* `X ‚ü∂[f] Y` for `DisplayStruct.HomOver f x y`

* `f ‚â´‚Çí g` for `DisplayStruct.comp_over f g`

* `ùüô‚Çí X` for `DisplayStruct.id_over`

#### References

Benedikt Ahrens, Peter LeFanu Lumsdaine, Displayed Categories, Logical Methods in Computer Science
15 (1).
-/


namespace CategoryTheory

open Category CategoryTheory

universe u‚ÇÅ v‚ÇÅ u‚ÇÇ v‚ÇÇ

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] (F : C ‚Üí Type u‚ÇÇ)

namespace eqToHom

/-- Transporting a morphism `f : I ‚ü∂ J` along equalities `w : I = I'` and  `w' : J = J'`.
Note: It might be a good idea to add this to eqToHom file. -/
@[simp]
def map {I I' J J' : C} (w : I = I') (w' : J = J') (f : I ‚ü∂ J) : I' ‚ü∂ J' :=
  w' ‚ñ∏ (w ‚ñ∏ f) --eqToHom (w.symm) ‚â´ f ‚â´ eqToHom w'

@[simp]
theorem map_id {I I' : C} (w : I = I') : w ‚ñ∏ ùüô I = ùüô I' := by
  subst w
  rfl

/--
The diagram below commutes:
```
    I --eqToHom w -->  J
    |                  |
  f |                  | eqToHom.map w w' f
    v                  v
    I' --eqToHom w'-> J'
```
-/
@[simp]
lemma map_naturality {I I' J J' : C} {w : I = I'} {w' : J = J'} (f : I ‚ü∂ J) :
    eqToHom w ‚â´ map w w' f = f ‚â´ eqToHom w' := by
  subst w' w
  simp

end eqToHom

namespace Fiber

/-- Cast an element of a Fiber along an equality of the base objects. -/
def cast {I I' : C} (w : I = I') (X : F I)  : F I' :=
  w ‚ñ∏ X

@[simp]
lemma cast_trans {I I' I'': C} (X : F I) {w : I = I'} {w' : I' = I''} {w'' : I = I''} :
    w' ‚ñ∏ (w ‚ñ∏ X) = w'' ‚ñ∏ X := by
  subst w'
  rfl

lemma cast_cast {I I' : C} (X : F I) {w : I = I'} {w' : I' = I} : w' ‚ñ∏ w ‚ñ∏ X = X := by
  simp only [cast_trans]

end Fiber

class DisplayStruct where
  /-- The type of morphisms indexed over morphisms of `C`. -/
  HomOver : ‚àÄ {I J : C}, (I ‚ü∂ J) ‚Üí F I ‚Üí F J ‚Üí Type v‚ÇÇ
  /-- The identity morphism overlying the identity morphism of `C`. -/
  id_over : ‚àÄ {I : C} (X : F I), HomOver (ùüô I) X X
  /-- Composition of morphisms overlying composition of morphisms of `C`. -/
  comp_over : ‚àÄ {I J K : C} {f‚ÇÅ : I ‚ü∂ J} {f‚ÇÇ : J ‚ü∂ K} {X : F I} {Y : F J}
  {Z : F K}, HomOver f‚ÇÅ X Y ‚Üí HomOver f‚ÇÇ Y Z ‚Üí HomOver (f‚ÇÅ ‚â´ f‚ÇÇ) X Z

notation X " ‚ü∂[" f "] " Y => DisplayStruct.HomOver f X Y
notation "ùüô‚Çí" => DisplayStruct.id_over
scoped infixr:80 " ‚â´‚Çí "  => DisplayStruct.comp_over

class Display extends DisplayStruct F where
  id_comp_cast {I J : C} {f : I ‚ü∂ J} {X : F I} {Y : F J}
  (g : X ‚ü∂[f] Y) : (ùüô‚Çí X) ‚â´‚Çí g = (id_comp f).symm ‚ñ∏ g := by aesop_cat
  comp_id_cast {I J : C} {f : I ‚ü∂ J} {X : F I} {Y : F J}
  (g : X ‚ü∂[f] Y) : g ‚â´‚Çí (ùüô‚Çí Y) = ((comp_id f).symm ‚ñ∏ g) := by aesop_cat
  assoc_cast {I J K L : C} {f‚ÇÅ : I ‚ü∂ J} {f‚ÇÇ : J ‚ü∂ K} {f‚ÇÉ : K ‚ü∂ L} {X : F I}
  {Y : F J} {Z : F K} {W : F L} (g‚ÇÅ : X ‚ü∂[f‚ÇÅ] Y)
  (g‚ÇÇ : Y ‚ü∂[f‚ÇÇ] Z) (g‚ÇÉ : Z ‚ü∂[f‚ÇÉ] W) :
  (g‚ÇÅ ‚â´‚Çí g‚ÇÇ) ‚â´‚Çí g‚ÇÉ = (assoc f‚ÇÅ f‚ÇÇ f‚ÇÉ).symm ‚ñ∏ (g‚ÇÅ ‚â´‚Çí (g‚ÇÇ ‚â´‚Çí g‚ÇÉ)) := by aesop_cat

attribute [simp] Display.id_comp_cast Display.comp_id_cast Display.assoc_cast
attribute [trans] Display.assoc_cast

namespace Display

variable {F}
variable [Display F]


@[simp]
def cast {I J : C} {f f' : I ‚ü∂ J} {X : F I} {Y : F J} (w : f = f') (g : X ‚ü∂[f] Y) :
    X ‚ü∂[f'] Y :=
  w ‚ñ∏ g

@[simp]
lemma cast_symm {I J : C} {f f' : I ‚ü∂ J} {X : F I} {Y : F J}
    (w : f = f') (g : X ‚ü∂[f] Y) (g' : X ‚ü∂[f'] Y) : (w ‚ñ∏ g = g') ‚Üî g = w.symm ‚ñ∏ g' := by
  subst w
  rfl

lemma cast_assoc_symm {I J K L : C} {f‚ÇÅ : I ‚ü∂ J} {f‚ÇÇ : J ‚ü∂ K} {f‚ÇÉ : K ‚ü∂ L}
    {X : F I} {Y : F J} {Z : F K} {W : F L} (g‚ÇÅ : X ‚ü∂[f‚ÇÅ] Y)
    (g‚ÇÇ : Y ‚ü∂[f‚ÇÇ] Z) (g‚ÇÉ : Z ‚ü∂[f‚ÇÉ] W) :
    (assoc f‚ÇÅ f‚ÇÇ f‚ÇÉ) ‚ñ∏ ((g‚ÇÅ ‚â´‚Çí g‚ÇÇ) ‚â´‚Çí g‚ÇÉ) = (g‚ÇÅ ‚â´‚Çí (g‚ÇÇ ‚â´‚Çí g‚ÇÉ)) := by
  simp only [cast_symm, assoc_cast]

@[simp]
lemma cast_trans {I J : C} {f f' f'' : I ‚ü∂ J} {X : F I} {Y : F J} {w : f = f'}
    {w' : f' = f''} (g : X ‚ü∂[f] Y) : w' ‚ñ∏ w ‚ñ∏ g = (w.trans w') ‚ñ∏ g := by
  subst w'
  rfl

lemma cast_eq {I J : C} {f f' : I ‚ü∂ J} {X : F I} {Y : F J} {w w' : f = f'} (g : X ‚ü∂[f] Y) :
    w ‚ñ∏ g = w' ‚ñ∏ g := by
  rfl

@[simp]
lemma cast_cast {I J : C} {f f' : I ‚ü∂ J} {X : F I} {Y : F J} (w : f = f') (w' : f' = f)
    (g : X ‚ü∂[f'] Y) :
    w' ‚ñ∏ w ‚ñ∏ g = g := by
  simp only [cast_trans]

@[simp]
lemma comp_id_eq_cast_id_comp {I J : C} {f : I ‚ü∂ J} {X : F I} {Y : F J} (g : X ‚ü∂[f] Y) :
    g ‚â´‚Çí ùüô‚Çí Y = (by simp : (ùüô I ‚â´ f = f ‚â´ ùüô J)) ‚ñ∏ (ùüô‚Çí X  ‚â´‚Çí g) := by 
  simp only [comp_id_cast, cast, id_comp_cast, comp_id, cast_trans]

/-- `castToHom w X` is a morphism from `X` to `w ‚ñ∏ X` over `eqToHom w`. -/
def castToHom {I I' : C} (w : I = I') (X : F I) : X ‚ü∂[eqToHom w] (w ‚ñ∏ X) := by
  subst w
  exact ùüô‚Çí X

def castToHomInv {I I' : C} (w : I = I') (X : F I) :
    (w ‚ñ∏ X) ‚ü∂[CategoryTheory.eqToHom w.symm] X := by
  subst w
  exact ùüô‚Çí X

@[simp]
def castToHomMap {I I' J J' : C} (w : I = I') (w' : J = J') {f : I ‚ü∂ J} {X : F I } {Y : F J}
    (g : X ‚ü∂[f] Y) :
    (w ‚ñ∏ X) ‚ü∂[eqToHom.map w w' f] (w' ‚ñ∏ Y) := by
  subst w
  subst w'
  exact g

@[simp]
def castToHomMapId {I I' : C} (w : I = I') {X : F I } {Y : F I} (g : X ‚ü∂[ùüô I] Y) :
    (w ‚ñ∏ X) ‚ü∂[ùüô I'] (w ‚ñ∏ Y) := by
  subst w
  exact g

/--
The displayed diagram
```
              X --------g--------> Y
              |                    |
eqToHom w X   |                    | eqToHom w' Y
              v                    v
           w ‚ñ∏ X ------------->  w ‚ñ∏ Y
                eqToHom.map w w' g

```
commutes.
-/
lemma eqToHom_naturality {I I' J J': C} {X : F I} {Y : F J} (w : I = I') (w' : J = J')
    (f : I ‚ü∂ J) (g : X ‚ü∂[f] Y) : g ‚â´‚Çí castToHom w' Y = cast (eqToHom.map_naturality f)
    (castToHom w X ‚â´‚Çí castToHomMap w w' g) := by
  subst w' w
  simp only [castToHom, comp_id_eq_cast_id_comp, cast]
  rfl

@[simps!]
def castEquiv {I J : C} {f f' : I ‚ü∂ J} {X : F I} {Y : F J} (w : f = f') :
    (X ‚ü∂[f] Y) ‚âÉ (X ‚ü∂[f'] Y) where
  toFun := fun g ‚Ü¶ w ‚ñ∏ g
  invFun := fun g ‚Ü¶ w.symm ‚ñ∏ g
  left_inv := by aesop_cat
  right_inv := by aesop_cat

variable (F)

/-- The total space of a displayed category consists of pairs `(I, X)` where `I` is an object of `C` and `X` is an object of the Fiber `F I`. -/
def Total := Œ£ I : C, F I

prefix:75 " ‚à´ "  => Total

namespace Total

variable {F}

@[simp]
def mk {I : C} (X : F I) : ‚à´ F := ‚ü®I, X‚ü©

abbrev Hom (X Y : ‚à´ F) := Œ£ (f : X.1 ‚ü∂ Y.1), X.2 ‚ü∂[f] Y.2

@[simps!]
def Hom.mk {I J : C} {X : F I} {Y : F J} {f : I ‚ü∂ J} (g : X ‚ü∂[f] Y) :
    Total.Hom (.mk X) (.mk Y) :=
  ‚ü®f, g‚ü©

@[simp]
instance categoryStruct : CategoryStruct (‚à´ F) where
  Hom := Total.Hom
  id X := ‚ü®ùüô X.1, ùüô‚Çí X.2‚ü©
  comp u u' := ‚ü®u.1 ‚â´ u'.1, u.2 ‚â´‚Çí u'.2‚ü©

-- @[simp]
-- lemma cast_exchange_comp {I J K : C} {f f' : I ‚ü∂ J} {h h' : J ‚ü∂ K} {X : F I} {Y : F J} {Z : F K}
--     (g : X ‚ü∂[f] Y) (k : Y ‚ü∂[h] Z) (w : f = f') (w' : h = h') :
--     w' ‚ñ∏ (g ‚â´‚Çí k) = (w ‚ñ∏ g) ‚â´‚Çí (w' ‚ñ∏ k) := by
--   subst w w'
--   rfl

@[simp]
lemma whisker_left_cast_comp {I J K : C} {f : I ‚ü∂ J} {h h' : J ‚ü∂ K} {X : F I} {Y : F J} {Z : F K}
    (g : X ‚ü∂[f] Y) (k : Y ‚ü∂[h] Z) (w : h = h') : (f ‚â´= w) ‚ñ∏ (g ‚â´‚Çí k) = g ‚â´‚Çí (w ‚ñ∏ k) := by
  subst w
  rfl

@[simp]
lemma whisker_right_cast_comp {I J K : C} {f f' : I ‚ü∂ J} {h : J ‚ü∂ K} {X : F I} {Y : F J} {Z : F K}
    (g : X ‚ü∂[f] Y) (k : Y ‚ü∂[h] Z) (w : f = f') : (w =‚â´ h) ‚ñ∏ (g ‚â´‚Çí k) = (w ‚ñ∏ g) ‚â´‚Çí k := by
  subst w
  rfl

instance category : Category (‚à´ F) where
  id_comp := by
    rintro ‚ü®I, X‚ü© ‚ü®J, Y‚ü© ‚ü®f, g‚ü©
    dsimp
    refine Sigma.eq ?_ ?_
    simp only [id_comp]
    simp only [id_comp_cast, cast_trans]
  comp_id := by
    rintro ‚ü®I, X‚ü© ‚ü®J, Y‚ü© ‚ü®f, g‚ü©
    dsimp
    refine Sigma.eq ?_ ?_
    simp only [comp_id]
    simp only [comp_id_cast, cast_trans]
  assoc := by
    rintro ‚ü®I, X‚ü© ‚ü®J, Y‚ü© ‚ü®K, Z‚ü© ‚ü®L, W‚ü© ‚ü®f, g‚ü© ‚ü®h, k‚ü© ‚ü®l, m‚ü©
    dsimp
    refine Sigma.eq ?_ ?_
    simp only [assoc]
    simp only [assoc_cast, cast_trans]

def forget : (‚à´ F) ‚•§ C where
  obj X := X.1
  map {X Y} f := f.1


end Total

end Display

variable {E : Type*} [Category E] {P : E ‚•§ C}

/-- The type of lifts of a given morphism in the base
with fixed source and target in the Fibers of the domain and codomain respectively.-/
structure BasedLift {I J : C} (f : I ‚ü∂ J) (X : P‚Åª¬π I) (Y : P‚Åª¬π J) where
  hom : (X : E) ‚ü∂ (Y : E)
  over_eq : (P.map hom) ‚â´ eqToHom (Y.2) = eqToHom (X.2) ‚â´ f

def BasedLift' {I J : C} (f : I ‚ü∂ J) (X : P‚Åª¬π I) (Y : P‚Åª¬π J) :=
{ hom : (X : E) ‚ü∂ (Y : E) // (P.map hom) ‚â´ eqToHom (Y.2) = eqToHom (X.2) ‚â´ f }


/--
The structure of based-lifts up to an isomorphism of the domain objects in the base.
```                   g
     X -------------------------------> Y
     _                                  -
     |                                  |
     |                                  |
     v                                  v
P.obj X --------> I ------> J ----> P.obj Y
            ‚âÖ           f       ‚âÖ
```
-/
structure EBasedLift {I J : C} (f : I ‚ü∂ J) (X : P‚Åª¬π·µâ I) (Y : P‚Åª¬π·µâ J) where
  hom : X.obj ‚ü∂ Y.obj
  over_eq : (P.map hom) ‚â´ Y.iso.hom = X.iso.hom ‚â´ f := by aesop_cat

attribute [reassoc] EBasedLift.over_eq

namespace BasedLift

variable {E : Type*} [Category E] {P : E ‚•§ C}

@[simp]
lemma over_eq' {I J : C} {f : I ‚ü∂ J} {X : P‚Åª¬π I} {Y : P‚Åª¬π J} (g : BasedLift f X Y) :
    P.map g.hom = eqToHom (X.2) ‚â´ f ‚â´ (eqToHom (Y.2).symm)  := by
  simp only [‚Üê Category.assoc _ _ _, ‚Üê g.over_eq, assoc, eqToHom_trans, eqToHom_refl, comp_id]

/-- The identity based-lift. -/
@[simps!]
def id {I : C} (X : P‚Åª¬π I) : BasedLift (ùüô I) X X := ‚ü®ùüô _, by simp‚ü©

/-- The composition of based-lifts -/
@[simps]
def comp {I J K : C} {f‚ÇÅ : I ‚ü∂ J} {f‚ÇÇ : J ‚ü∂ K} {X : P‚Åª¬π I} {Y : P‚Åª¬π J} {Z : P‚Åª¬π K}
    (g‚ÇÅ : BasedLift f‚ÇÅ X Y) (g‚ÇÇ : BasedLift f‚ÇÇ Y Z) :
    BasedLift (f‚ÇÅ ‚â´ f‚ÇÇ) X Z :=
  ‚ü®g‚ÇÅ.hom ‚â´ g‚ÇÇ.hom, by simp only [P.map_comp]; rw [assoc, over_eq' g‚ÇÅ, over_eq' g‚ÇÇ]; simp‚ü©

@[simps!]
def cast {I J : C} {f f' : I ‚ü∂ J} {X : P‚Åª¬π I} {Y : P‚Åª¬π J} (w : f = f')
  (g : BasedLift f X Y) : BasedLift f' X Y := ‚ü®g.hom, by rw [‚Üêw, g.over_eq]‚ü©

end BasedLift

namespace EBasedLift

@[simp]
lemma over_eq' {I J : C} {f : I ‚ü∂ J} {X : P‚Åª¬π·µâ I} {Y : P‚Åª¬π·µâ J} (g : EBasedLift f X Y) :
    P.map g.hom = X.iso.hom ‚â´ f ‚â´ Y.iso.inv := by
  simpa using g.over_eq_assoc (Y.iso.inv)

@[simps!]
def id {I : C} (X : P‚Åª¬π·µâ I) : EBasedLift (ùüô I) X X where
  hom := ùüô _

@[simps!]
def comp {I J K : C} {f‚ÇÅ : I ‚ü∂ J} {f‚ÇÇ : J ‚ü∂ K} {X : P‚Åª¬π·µâ I} {Y : P‚Åª¬π·µâ J} {Z : P‚Åª¬π·µâ K}
    (g‚ÇÅ : EBasedLift f‚ÇÅ X Y) (g‚ÇÇ : EBasedLift f‚ÇÇ Y Z) :
    EBasedLift (f‚ÇÅ ‚â´ f‚ÇÇ) X Z := by
  refine ‚ü®g‚ÇÅ.hom ‚â´ g‚ÇÇ.hom, ?_‚ü©
  have := by
    calc
      P.map (g‚ÇÅ.hom ‚â´ g‚ÇÇ.hom) = P.map (g‚ÇÅ.hom) ‚â´ P.map (g‚ÇÇ.hom) := by
        rw [P.map_comp]
      _   = (X.iso.hom ‚â´ f‚ÇÅ ‚â´ Y.iso.inv) ‚â´ P.map (g‚ÇÇ.hom) := by
        rw [g‚ÇÅ.over_eq']
      _   = X.iso.hom ‚â´ f‚ÇÅ ‚â´ (Y.iso.inv ‚â´ P.map (g‚ÇÇ.hom)) := by
        simp only [over_eq', assoc, Iso.inv_hom_id_assoc]
      _   = X.iso.hom ‚â´ f‚ÇÅ ‚â´ (Y.iso.inv ‚â´ Y.iso.hom ‚â´ f‚ÇÇ ‚â´ Z.iso.inv) := by
        rw [g‚ÇÇ.over_eq']
      _   = X.iso.hom ‚â´ f‚ÇÅ ‚â´ (ùüô J ‚â´ f‚ÇÇ ‚â´ Z.iso.inv) := by
        simp
      _   = X.iso.hom ‚â´ f‚ÇÅ ‚â´ f‚ÇÇ ‚â´ Z.iso.inv := by
        simp
  simp [this]

@[simps!]
def cast {I J : C} {f f' : I ‚ü∂ J} {X : P‚Åª¬π·µâ I} {Y : P‚Åª¬π·µâ J}
    (w : f = f') (g : EBasedLift f X Y) : EBasedLift f' X Y where
  hom := g.hom
  over_eq := by rw [‚Üêw, g.over_eq]

end EBasedLift

variable (P)

/-- The display structure `DisplayStruct P` associated to a functor `P : E ‚•§ C`.
This instance makes the displayed notations `_ ‚ü∂[f] _`, `_ ‚â´‚Çí _` and `ùüô‚Çí` available for based-lifts.   -/
instance Functor.displayStruct : DisplayStruct (fun I => P‚Åª¬π I) where
  HomOver := fun f X Y => BasedLift f X Y
  id_over X := BasedLift.id X
  comp_over := fun g‚ÇÅ g‚ÇÇ => BasedLift.comp g‚ÇÅ g‚ÇÇ

instance Functor.isodisplay : DisplayStruct (fun I => P‚Åª¬π·µâ I) where
  HomOver := fun f X Y => EBasedLift f X Y
  id_over := fun X => EBasedLift.id X
  comp_over := fun g‚ÇÅ g‚ÇÇ => EBasedLift.comp g‚ÇÅ g‚ÇÇ

namespace BasedLift

variable {P}

@[ext]
theorem ext {I J : C} {f : I ‚ü∂ J} {X : P‚Åª¬π I} {Y : P‚Åª¬π J} (g g' : X ‚ü∂[f] Y) (w : g.hom = g'.hom) :
    g = g' := by
  cases' g with g hg
  cases' g' with g' hg'
  congr

@[simp]
lemma cast_rec {I J : C} {f f' : I ‚ü∂ J} {X : P‚Åª¬π I} {Y : P‚Åª¬π J} {w : f = f'} (g : X ‚ü∂[f] Y) :
    g.cast w  = w ‚ñ∏ g := by
  subst w
  rfl

/-- `BasedLift.tauto` regards a morphism `g` of the domain category `E` as a
based-lift of its image `P g` under functor `P`. -/
@[simps!]
def tauto {X Y : E} (g : X ‚ü∂ Y) : (Fiber.tauto X) ‚ü∂[P.map g] (Fiber.tauto Y) :=
  ‚ü®g, by simp only [Fiber.tauto, eqToHom_refl, id_comp, comp_id]‚ü©

lemma tauto_over_base {X Y : E} (f : (P.obj X) ‚ü∂ (P.obj Y)) (g : (Fiber.tauto X) ‚ü∂[f] (Fiber.tauto Y)) : P.map g.hom = f := by
  simp only [Fiber.coe_mk, over_eq', eqToHom_refl, comp_id, id_comp]

lemma tauto_comp_hom {X Y Z : E} {g : X ‚ü∂ Y} {g' : Y ‚ü∂ Z} :
    (tauto (P:= P) g ‚â´‚Çí tauto g').hom = g ‚â´ g' := rfl

lemma comp_tauto_hom {X Y Z : E} {f : P.obj X ‚ü∂ P.obj Y} {f' : Fiber.tauto X ‚ü∂[f] (Fiber.tauto Y)}
    {g : Y ‚ü∂ Z} : (f' ‚â´‚Çí tauto g).hom = f'.hom ‚â´ g := rfl

/-- A morphism of `E` coerced as a tautological based-lift. -/
@[simps]
 instance instCoeTautoBasedLift {X Y : E} {g : X ‚ü∂ Y} :
    CoeDep (X ‚ü∂ Y) (g : X ‚ü∂ Y) (Fiber.tauto X ‚ü∂[P.map g] Fiber.tauto Y) := ‚ü®tauto g‚ü©

lemma eq_id_of_hom_eq_id {I : C} {X : P‚Åª¬π I} {g : X ‚ü∂[ùüô I] X} :
    (g.hom = ùüô X.1) ‚Üî (g = id X) := by
  aesop

@[simp]
lemma id_comp_cast {I J : C} {f : I ‚ü∂ J} {X : P‚Åª¬π I} {Y : P‚Åª¬π J}
    {g : X ‚ü∂[f] Y} : ùüô‚Çí X  ‚â´‚Çí g = g.cast (id_comp f).symm := by
  ext
  simp only [cast_hom, DisplayStruct.comp_over, DisplayStruct.id_over, comp_hom, id_hom, id_comp]

@[simp]
lemma comp_id_cast {I J : C} {f : I ‚ü∂ J} {X : P‚Åª¬π I} {Y : P‚Åª¬π J} {g : X ‚ü∂[f] Y} :
    g ‚â´‚Çí ùüô‚Çí Y = g.cast (comp_id f).symm := by
  ext
  simp only [cast_hom, DisplayStruct.comp_over, DisplayStruct.id_over, comp_hom, id_hom, comp_id]

@[simp]
lemma assoc {I J K L : C} {f : I ‚ü∂ J} {h : J ‚ü∂ K} {l : K ‚ü∂ L} {W : P‚Åª¬π I} {X : P‚Åª¬π J} {Y : P‚Åª¬π K} {Z : P‚Åª¬π L} (g : W ‚ü∂[f] X) (k : X ‚ü∂[h] Y) (m : Y ‚ü∂[l] Z) : (g ‚â´‚Çí k) ‚â´‚Çí m = (g ‚â´‚Çí (k ‚â´‚Çí m)).cast (assoc f h l).symm := by
  ext
  simp only [cast_hom, DisplayStruct.comp_over, comp_hom, Category.assoc]

def eqToHom {I : C} {X Y : P‚Åª¬π I} (w : X = Y) : X ‚ü∂[ùüô I] Y := by
  subst w
  exact id X

def eqToHom' {I : C} {X Y : P‚Åª¬π I} (w : X.1 = Y.1) : X ‚ü∂[ùüô I] Y := by
  apply Fiber.ext at w
  subst w
  exact id X

end BasedLift

namespace EBasedLift

@[ext]
theorem ext {I J : C} {f : I ‚ü∂ J} {X : P‚Åª¬π·µâ I} {Y : P‚Åª¬π·µâ J} (g g' : X ‚ü∂[f] Y)
    (w : g.hom = g'.hom) : g = g' := by
  cases' g with g hg
  cases' g' with g' hg'
  congr

@[simp]
lemma cast_rec {I J : C} {f f' : I ‚ü∂ J} {X : P‚Åª¬π·µâ I} {Y : P‚Åª¬π·µâ J}
    {w : f = f'} (g : X ‚ü∂[f] Y) :
    g.cast w  = w ‚ñ∏ g := by
  subst w
  rfl

variable {P}

/-- `EBasedLift.tauto` regards a morphism `g` of the domain category `E` as a
based-lift of its image `P g` under functor `P`. -/
@[simps!]
def tauto {X Y : E} (g : X ‚ü∂ Y) : (EFiber.tauto X) ‚ü∂[P.map g] (EFiber.tauto Y) where
  hom := g

lemma tauto_over_base {X Y : E} (f : (P.obj X) ‚ü∂ (P.obj Y))
    (g : (Fiber.tauto X) ‚ü∂[f] (Fiber.tauto Y)) :
    P.map g.hom = f := by
  aesop_cat

lemma tauto_comp_hom {X Y Z : E} {g : X ‚ü∂ Y} {g' : Y ‚ü∂ Z} :
    (tauto (P:= P) g ‚â´‚Çí tauto g').hom = g ‚â´ g' := rfl

lemma comp_tauto_hom {X Y Z : E} {f : P.obj X ‚ü∂ P.obj Y} {f' : EFiber.tauto X ‚ü∂[f] (EFiber.tauto Y)}
    {g : Y ‚ü∂ Z} : (f' ‚â´‚Çí tauto g).hom = f'.hom ‚â´ g := rfl

/-- A morphism of `E` coerced as a tautological based-lift. -/
@[simps]
 instance instCoeTautoBasedLift {X Y : E} {g : X ‚ü∂ Y} :
    CoeDep (X ‚ü∂ Y) (g : X ‚ü∂ Y) (EFiber.tauto X ‚ü∂[P.map g] EFiber.tauto Y) := ‚ü®tauto g‚ü©

lemma eq_id_of_hom_eq_id {I : C} {X : P‚Åª¬π·µâ I} {g : X ‚ü∂[ùüô I] X} :
    (g.hom = ùüô X.1) ‚Üî (g = id X) := by
  aesop

@[simp]
lemma id_comp_cast {I J : C} {f : I ‚ü∂ J} {X : P‚Åª¬π·µâ I} {Y : P‚Åª¬π·µâ J}
    {g : X ‚ü∂[f] Y} : ùüô‚Çí X  ‚â´‚Çí g = g.cast (id_comp f).symm := by
  ext
  simp only [cast_hom, DisplayStruct.comp_over, DisplayStruct.id_over, comp_hom, id_hom, id_comp]

@[simp]
lemma comp_id_cast {I J : C} {f : I ‚ü∂ J} {X : P‚Åª¬π·µâ I} {Y : P‚Åª¬π·µâ J} {g : X ‚ü∂[f] Y} :
    g ‚â´‚Çí ùüô‚Çí Y = g.cast (comp_id f).symm := by
  ext
  simp only [cast_hom, DisplayStruct.comp_over, DisplayStruct.id_over, comp_hom, id_hom, comp_id]

@[simp]
lemma assoc {I J K L : C} {f : I ‚ü∂ J} {h : J ‚ü∂ K} {l : K ‚ü∂ L}
    {W : P‚Åª¬π·µâ I} {X : P‚Åª¬π·µâ J} {Y : P‚Åª¬π·µâ K} {Z : P‚Åª¬π·µâ L}
    (g : W ‚ü∂[f] X) (k : X ‚ü∂[h] Y) (m : Y ‚ü∂[l] Z) :
    (g ‚â´‚Çí k) ‚â´‚Çí m = (g ‚â´‚Çí (k ‚â´‚Çí m)).cast (assoc f h l).symm := by
  ext
  simp only [cast_hom, DisplayStruct.comp_over, comp_hom, Category.assoc]

end EBasedLift

/-- The displayed category of a functor `P : E ‚•§ C`. -/
instance Functor.display : Display (fun I => P‚Åª¬π I) where
  id_comp_cast := by simp
  comp_id_cast := by simp
  assoc_cast := by simp

instance Functor.edisplay : Display (fun I => P‚Åª¬π·µâ I) where
  id_comp_cast := by simp
  comp_id_cast := by simp
  assoc_cast := by simp

namespace Display

variable {F}
variable [Display F]

/-- The type `Lift f tgt` of a lift of `f` with the target `tgt` consists of an object `src` in
the Fiber of the domain of `f` and a based-lift of `f` starting at `src` and ending at `tgt`. -/
structure Lift {I J : C} (f : I ‚ü∂ J) (tgt : F J) where
  src : F I
  homOver : src ‚ü∂[f] tgt

theorem Lift.ext {I J : C} {f : I ‚ü∂ J} {tgt : F J} {g g' : Lift f tgt}
    (w_src : g.src = g'.src) (w_homOver : g.homOver = w_src ‚ñ∏ g'.homOver) :
    g = g' := by
  cases' g with X g
  cases' g' with X' g'
  cases w_src
  cases w_homOver
  rfl

/-- The type `CoLift f src` of a colift of `f` with the source `src` consists of an object `tgt` in
the Fiber of the codomain of `f` and a based-lift of `f` starting at `src` and ending at `tgt`. -/
@[ext]
structure CoLift {I J : C} (f : I ‚ü∂ J) (src : F I) where
  tgt : F J
  homOver : src ‚ü∂[f] tgt

end Display

end CategoryTheory
