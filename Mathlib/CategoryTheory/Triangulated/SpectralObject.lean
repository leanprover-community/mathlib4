/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.ComposableArrows.One
public import Mathlib.CategoryTheory.ComposableArrows.Two
public import Mathlib.CategoryTheory.Triangulated.Functor

/-!
# Spectral objects in triangulated categories

In this file, we introduce the category `SpectralObject C Œπ` of spectral
objects in a pretriangulated category `C` indexed by the category `Œπ`.

## TODO (@joelriou)
* construct the spectral object indexed by `WithTop (WithBot ‚Ñ§)` consisting
of all truncations of an object of a triangulated category equipped with a t-structure
* define a similar notion of spectral objects in abelian categories, show that
by applying a homological functor `C ‚•§ A` to a spectral object in the
triangulated category `C`, we obtain a spectral object in the abelian category `A`
* construct the spectral sequence attached to a spectral object in an abelian category

## References
* [Jean-Louis Verdier, *Des cat√©gories d√©riv√©es des cat√©gories ab√©liennes*, II.4][verdier1996]

-/

@[expose] public section

namespace CategoryTheory

open Limits Pretriangulated ComposableArrows

variable (C Œπ : Type*) [Category* C] [Category* Œπ] [HasZeroObject C]
  [HasShift C ‚Ñ§] [Preadditive C] [‚àÄ (n : ‚Ñ§), (shiftFunctor C n).Additive] [Pretriangulated C]
  {D : Type*} [Category* D] [HasZeroObject D] [HasShift D ‚Ñ§] [Preadditive D]
  [‚àÄ (n : ‚Ñ§), (shiftFunctor D n).Additive] [Pretriangulated D]

namespace Triangulated

/-- A spectral object in a pretriangulated category `C` indexed by a category `Œπ` consists
of a functor `œâ‚ÇÅ : ComposableArrows Œπ 1 ‚•§ C`, and a functorial distinguished triangle
from the category `ComposableArrows Œπ 2`, which must be of the form
`œâ‚ÇÅ.obj (mk‚ÇÅ f) ‚ü∂ œâ‚ÇÅ.obj (mk‚ÇÅ (f ‚â´ g)) ‚ü∂ œâ‚ÇÅ.obj (mk‚ÇÅ g) ‚ü∂ ...` when evaluated
on `mk‚ÇÇ f g : ComposableArrows Œπ 2`. -/
structure SpectralObject where
  /-- A functor from `ComposableArrows Œπ 1` to the pretriangulated category. -/
  œâ‚ÇÅ : ComposableArrows Œπ 1 ‚•§ C
  /-- The connecting homomorphism of the spectral object. -/
  Œ¥' : functorArrows Œπ 1 2 2 ‚ãô œâ‚ÇÅ ‚ü∂ functorArrows Œπ 0 1 2 ‚ãô œâ‚ÇÅ ‚ãô shiftFunctor C (1 : ‚Ñ§)
  distinguished' (D : ComposableArrows Œπ 2) :
    Triangle.mk (œâ‚ÇÅ.map ((mapFunctorArrows Œπ 0 1 0 2 2).app D))
      (œâ‚ÇÅ.map ((mapFunctorArrows Œπ 0 2 1 2 2).app D)) (Œ¥'.app D) ‚àà distTriang C

namespace SpectralObject

variable {C Œπ} (X : SpectralObject C Œπ)

/-- The functorial (distinguished) triangle attached to a spectral object in
a pretriangulated category. -/
@[simps!]
noncomputable def œâ‚ÇÇ : ComposableArrows Œπ 2 ‚•§ Triangle C :=
  Triangle.functorMk (Functor.whiskerRight (mapFunctorArrows Œπ 0 1 0 2 2) X.œâ‚ÇÅ)
    (Functor.whiskerRight (mapFunctorArrows Œπ 0 2 1 2 2) X.œâ‚ÇÅ) X.Œ¥'

lemma œâ‚ÇÇ_obj_distinguished (D : ComposableArrows Œπ 2) :
    X.œâ‚ÇÇ.obj D ‚àà distTriang C :=
  X.distinguished' D

section

variable {i j k : Œπ} (f : i ‚ü∂ j) (g : j ‚ü∂ k)

/-- The connecting homomorphism `X.œâ‚ÇÅ.obj (mk‚ÇÅ g) ‚ü∂ (X.œâ‚ÇÅ.obj (mk‚ÇÅ f))‚ü¶(1 : ‚Ñ§)‚üß`
of a spectral object `X` in a pretriangulated category when `f : i ‚ü∂ j` and `g : j ‚ü∂ k`
are composable. -/
def Œ¥ : X.œâ‚ÇÅ.obj (mk‚ÇÅ g) ‚ü∂ (X.œâ‚ÇÅ.obj (mk‚ÇÅ f))‚ü¶(1 : ‚Ñ§)‚üß :=
  X.Œ¥'.app (mk‚ÇÇ f g)

/-- The distinguished triangle attached to a spectral object `E : SpectralObjet C Œπ`
and composable morphisms `f : i ‚ü∂ j` and `g : j ‚ü∂ k` in `Œπ`. -/
@[simps!]
def triangle : Triangle C :=
  Triangle.mk (X.œâ‚ÇÅ.map (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f g _ rfl))
    (X.œâ‚ÇÅ.map (twoŒ¥‚ÇÅToŒ¥‚ÇÄ f g _ rfl)) (X.Œ¥ f g)

lemma triangle_distinguished : X.triangle f g ‚àà distTriang C :=
  X.œâ‚ÇÇ_obj_distinguished (mk‚ÇÇ f g)

end

section

variable {Œπ' : Type*} [Category Œπ'] (F : Œπ' ‚•§ Œπ)

attribute [local simp] Precomp.map Precomp.obj Œ¥ in
/-- The precomposition of a spectral object with a functor. -/
def precomp : SpectralObject C Œπ' where
  œâ‚ÇÅ := F.mapComposableArrows 1 ‚ãô X.œâ‚ÇÅ
  Œ¥'.app D := X.œâ‚ÇÅ.map (F.mapComposableArrowsObjMk‚ÇÅIso _).hom ‚â´
      X.Œ¥'.app ((F.mapComposableArrows 2).obj D) ‚â´
      (X.œâ‚ÇÅ.map (F.mapComposableArrowsObjMk‚ÇÅIso _).inv)‚ü¶1‚üß'
  Œ¥'.naturality D‚ÇÅ D‚ÇÇ f := by
    have := X.Œ¥'.naturality ((F.mapComposableArrows 2).map f)
    rw [‚Üê cancel_epi (X.œâ‚ÇÅ.map (F.mapComposableArrowsObjMk‚ÇÅIso _).hom)] at this
    rw [‚Üê cancel_mono ((X.œâ‚ÇÅ.map (F.mapComposableArrowsObjMk‚ÇÅIso _).hom)‚ü¶(1 : ‚Ñ§)‚üß')]
    dsimp at this ‚ä¢
    simp only [‚Üê Functor.map_comp_assoc, ‚Üê Functor.map_comp, Category.assoc,
      Iso.inv_hom_id, Functor.map_id, Category.comp_id] at this ‚ä¢
    convert this using 3
    ¬∑ cat_disch
    ¬∑ congr 2; cat_disch
  distinguished' D := by
    obtain ‚ü®_, _, _, f, g, rfl‚ü© := ComposableArrows.mk‚ÇÇ_surjective D
    refine isomorphic_distinguished _ (X.triangle_distinguished (F.map f) (F.map g)) _ ?_
    refine Triangle.isoMk _ _ (X.œâ‚ÇÅ.mapIso (ComposableArrows.isoMk‚ÇÅ (Iso.refl _) (Iso.refl _)))
      (X.œâ‚ÇÅ.mapIso (ComposableArrows.isoMk‚ÇÅ (Iso.refl _) (Iso.refl _)))
      (X.œâ‚ÇÅ.mapIso (ComposableArrows.isoMk‚ÇÅ (Iso.refl _) (Iso.refl _))) ?_ ?_ ?_
    ¬∑ dsimp
      simp only [‚Üê Functor.map_comp]
      congr 1
      cat_disch
    ¬∑ dsimp
      simp only [‚Üê Functor.map_comp]
      congr 1
      cat_disch
    ¬∑ have := X.Œ¥'.naturality (F.mapComposableArrowsObjMk‚ÇÇIso f g).hom
      dsimp at this ‚ä¢
      rw [‚Üê cancel_epi (X.œâ‚ÇÅ.map (F.mapComposableArrowsObjMk‚ÇÅIso _).inv)]
      simp only [‚Üê Functor.map_comp_assoc, ‚Üê Functor.map_comp, Category.assoc,
        Iso.inv_hom_id, Functor.map_id, Category.id_comp] at this ‚ä¢
      convert this.symm using 3
      ¬∑ congr; cat_disch
      ¬∑ cat_disch

end

section

variable (F : C ‚•§ D) [F.CommShift ‚Ñ§] [F.IsTriangulated]

/-- The image of a spectral by a triangulated functor. -/
@[simps]
def mapTriangulatedFunctor :
    SpectralObject D Œπ where
  œâ‚ÇÅ := X.œâ‚ÇÅ ‚ãô F
  Œ¥' := Functor.whiskerRight X.Œ¥' F ‚â´
      Functor.whiskerLeft (functorArrows Œπ 0 1 2 ‚ãô X.œâ‚ÇÅ) (F.commShiftIso (1 : ‚Ñ§)).hom
  distinguished' D := F.map_distinguished _ (X.distinguished' D)

@[simp]
lemma mapTriangulatedFunctor_Œ¥ {i j k : Œπ} (f : i ‚ü∂ j) (g : j ‚ü∂ k) :
    (X.mapTriangulatedFunctor F).Œ¥ f g = F.map (X.Œ¥ f g) ‚â´ (F.commShiftIso 1).hom.app _ := rfl

end

/-- The type of morphisms between spectral objects in pretriangulated categories. -/
@[ext]
structure Hom (Y : SpectralObject C Œπ) where
  /-- The natural transformation that is part of a morphism between spectral objects. -/
  hom : X.œâ‚ÇÅ ‚ü∂ Y.œâ‚ÇÅ
  comm {i j k : Œπ} (f : i ‚ü∂ j) (g : j ‚ü∂ k) :
    X.Œ¥ f g ‚â´ (hom.app (mk‚ÇÅ f))‚ü¶(1 : ‚Ñ§)‚üß' = hom.app (mk‚ÇÅ g) ‚â´ Y.Œ¥ f g := by cat_disch

attribute [reassoc (attr := simp)] Hom.comm

instance : Category (SpectralObject C Œπ) where
  Hom := Hom
  id X := { hom := ùüô _ }
  comp f g :=
    { hom := f.hom ‚â´ g.hom }

section

variable {X} {Y Z : SpectralObject C Œπ}

@[ext]
lemma hom_ext {Œ± Œ≤ : X ‚ü∂ Y} (h : Œ±.hom = Œ≤.hom) : Œ± = Œ≤ := Hom.ext h

variable (X) in
@[simp]
lemma id_hom : Hom.hom (ùüô X) = ùüô _ := rfl

@[simp, reassoc]
lemma comp_hom (Œ± : X ‚ü∂ Y) (Œ≤ : Y ‚ü∂ Z) :
    (Œ± ‚â´ Œ≤).hom = Œ±.hom ‚â´ Œ≤.hom := rfl

end

end SpectralObject

end Triangulated

namespace Functor

variable {C}

/-- The functor between categories of spectral objects that is induced by
a triangulated functor. -/
def mapTriangulatedSpectralObject (F : C ‚•§ D) [F.CommShift ‚Ñ§] [F.IsTriangulated]
    (Œπ : Type*) [Category* Œπ] :
    Triangulated.SpectralObject C Œπ ‚•§ Triangulated.SpectralObject D Œπ where
  obj X := X.mapTriangulatedFunctor F
  map Œ± :=
    { hom := Functor.whiskerRight Œ±.hom _
      comm f g := by
        have hf := (F.commShiftIso (1 : ‚Ñ§)).hom.naturality (Œ±.hom.app (mk‚ÇÅ f))
        dsimp at hf ‚ä¢
        rw [Category.assoc, ‚Üê hf, ‚Üê F.map_comp_assoc, Œ±.comm, F.map_comp_assoc] }

end Functor

end CategoryTheory
