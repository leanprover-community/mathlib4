/-
Copyright (c) 2021 Luke Kershaw. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Luke Kershaw
-/
import Mathlib.Data.Int.Basic
import Mathlib.CategoryTheory.Shift.Basic

#align_import category_theory.triangulated.basic from "leanprover-community/mathlib"@"6876fa15e3158ff3e4a4e2af1fb6e1945c6e8803"

/-!
# Triangles

This file contains the definition of triangles in an additive category with an additive shift.
It also defines morphisms between these triangles.

TODO: generalise this to n-angles in n-angulated categories as in https://arxiv.org/abs/1006.4592
-/


noncomputable section

open CategoryTheory Limits

universe v v‚ÇÄ v‚ÇÅ v‚ÇÇ u u‚ÇÄ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory.Pretriangulated

open CategoryTheory.Category

/-
We work in a category `C` equipped with a shift.
-/
variable (C : Type u) [Category.{v} C] [HasShift C ‚Ñ§]

/-- A triangle in `C` is a sextuple `(X,Y,Z,f,g,h)` where `X,Y,Z` are objects of `C`,
and `f : X ‚ü∂ Y`, `g : Y ‚ü∂ Z`, `h : Z ‚ü∂ X‚ü¶1‚üß` are morphisms in `C`.
See <https://stacks.math.columbia.edu/tag/0144>.
-/
structure Triangle where mk' ::
  /-- the first object of a triangle -/
  obj‚ÇÅ : C
  /-- the second object of a triangle -/
  obj‚ÇÇ : C
  /-- the third object of a triangle -/
  obj‚ÇÉ : C
  /-- the first morphism of a triangle -/
  mor‚ÇÅ : obj‚ÇÅ ‚ü∂ obj‚ÇÇ
  /-- the second morphism of a triangle -/
  mor‚ÇÇ : obj‚ÇÇ ‚ü∂ obj‚ÇÉ
  /-- the third morphism of a triangle -/
  mor‚ÇÉ : obj‚ÇÉ ‚ü∂ obj‚ÇÅ‚ü¶(1 : ‚Ñ§)‚üß
#align category_theory.pretriangulated.triangle CategoryTheory.Pretriangulated.Triangle

variable {C}

/-- A triangle `(X,Y,Z,f,g,h)` in `C` is defined by the morphisms `f : X ‚ü∂ Y`, `g : Y ‚ü∂ Z`
and `h : Z ‚ü∂ X‚ü¶1‚üß`.
-/
@[simps]
def Triangle.mk {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (h : Z ‚ü∂ X‚ü¶(1 : ‚Ñ§)‚üß) : Triangle C
    where
  obj‚ÇÅ := X
  obj‚ÇÇ := Y
  obj‚ÇÉ := Z
  mor‚ÇÅ := f
  mor‚ÇÇ := g
  mor‚ÇÉ := h
#align category_theory.pretriangulated.triangle.mk CategoryTheory.Pretriangulated.Triangle.mk

section

variable [HasZeroObject C] [HasZeroMorphisms C]

open ZeroObject

instance : Inhabited (Triangle C) :=
  ‚ü®‚ü®0, 0, 0, 0, 0, 0‚ü©‚ü©

/-- For each object in `C`, there is a triangle of the form `(X,X,0,ùüô X,0,0)`
-/
@[simps!]
def contractibleTriangle (X : C) : Triangle C :=
  Triangle.mk (ùüô X) (0 : X ‚ü∂ 0) 0
#align category_theory.pretriangulated.contractible_triangle CategoryTheory.Pretriangulated.contractibleTriangle

end

/-- A morphism of triangles `(X,Y,Z,f,g,h) ‚ü∂ (X',Y',Z',f',g',h')` in `C` is a triple of morphisms
`a : X ‚ü∂ X'`, `b : Y ‚ü∂ Y'`, `c : Z ‚ü∂ Z'` such that
`a ‚â´ f' = f ‚â´ b`, `b ‚â´ g' = g ‚â´ c`, and `a‚ü¶1‚üß' ‚â´ h = h' ‚â´ c`.
In other words, we have a commutative diagram:
```
     f      g      h
  X  ‚îÄ‚îÄ‚îÄ> Y  ‚îÄ‚îÄ‚îÄ> Z  ‚îÄ‚îÄ‚îÄ> X‚ü¶1‚üß
  ‚îÇ       ‚îÇ       ‚îÇ        ‚îÇ
  ‚îÇa      ‚îÇb      ‚îÇc       ‚îÇa‚ü¶1‚üß'
  V       V       V        V
  X' ‚îÄ‚îÄ‚îÄ> Y' ‚îÄ‚îÄ‚îÄ> Z' ‚îÄ‚îÄ‚îÄ> X'‚ü¶1‚üß
     f'     g'     h'
```
See <https://stacks.math.columbia.edu/tag/0144>.
-/
@[ext]
structure TriangleMorphism (T‚ÇÅ : Triangle C) (T‚ÇÇ : Triangle C) where
  /-- the first morphism in a triangle morphism -/
  hom‚ÇÅ : T‚ÇÅ.obj‚ÇÅ ‚ü∂ T‚ÇÇ.obj‚ÇÅ
  /-- the second morphism in a triangle morphism -/
  hom‚ÇÇ : T‚ÇÅ.obj‚ÇÇ ‚ü∂ T‚ÇÇ.obj‚ÇÇ
  /-- the third morphism in a triangle morphism -/
  hom‚ÇÉ : T‚ÇÅ.obj‚ÇÉ ‚ü∂ T‚ÇÇ.obj‚ÇÉ
  /-- the first commutative square of a triangle morphism -/
  comm‚ÇÅ : T‚ÇÅ.mor‚ÇÅ ‚â´ hom‚ÇÇ = hom‚ÇÅ ‚â´ T‚ÇÇ.mor‚ÇÅ := by aesop_cat
  /-- the second commutative square of a triangle morphism -/
  comm‚ÇÇ : T‚ÇÅ.mor‚ÇÇ ‚â´ hom‚ÇÉ = hom‚ÇÇ ‚â´ T‚ÇÇ.mor‚ÇÇ := by aesop_cat
  /-- the third commutative square of a triangle morphism -/
  comm‚ÇÉ : T‚ÇÅ.mor‚ÇÉ ‚â´ hom‚ÇÅ‚ü¶1‚üß' = hom‚ÇÉ ‚â´ T‚ÇÇ.mor‚ÇÉ := by aesop_cat
#align category_theory.pretriangulated.triangle_morphism CategoryTheory.Pretriangulated.TriangleMorphism

attribute [reassoc (attr := simp)] TriangleMorphism.comm‚ÇÅ TriangleMorphism.comm‚ÇÇ
  TriangleMorphism.comm‚ÇÉ

/-- The identity triangle morphism.
-/
@[simps]
def triangleMorphismId (T : Triangle C) : TriangleMorphism T T
    where
  hom‚ÇÅ := ùüô T.obj‚ÇÅ
  hom‚ÇÇ := ùüô T.obj‚ÇÇ
  hom‚ÇÉ := ùüô T.obj‚ÇÉ
#align category_theory.pretriangulated.triangle_morphism_id CategoryTheory.Pretriangulated.triangleMorphismId

instance (T : Triangle C) : Inhabited (TriangleMorphism T T) :=
  ‚ü®triangleMorphismId T‚ü©

variable {T‚ÇÅ T‚ÇÇ T‚ÇÉ : Triangle C}

/-- Composition of triangle morphisms gives a triangle morphism.
-/
@[simps]
def TriangleMorphism.comp (f : TriangleMorphism T‚ÇÅ T‚ÇÇ) (g : TriangleMorphism T‚ÇÇ T‚ÇÉ) :
    TriangleMorphism T‚ÇÅ T‚ÇÉ where
  hom‚ÇÅ := f.hom‚ÇÅ ‚â´ g.hom‚ÇÅ
  hom‚ÇÇ := f.hom‚ÇÇ ‚â´ g.hom‚ÇÇ
  hom‚ÇÉ := f.hom‚ÇÉ ‚â´ g.hom‚ÇÉ
#align category_theory.pretriangulated.triangle_morphism.comp CategoryTheory.Pretriangulated.TriangleMorphism.comp

/-- Triangles with triangle morphisms form a category.
-/
@[simps]
instance triangleCategory : Category (Triangle C)
    where
  Hom A B := TriangleMorphism A B
  id A := triangleMorphismId A
  comp f g := f.comp g
#align category_theory.pretriangulated.triangle_category CategoryTheory.Pretriangulated.triangleCategory

@[ext]
lemma Triangle.hom_ext {A B : Triangle C} (f g : A ‚ü∂ B)
  (h‚ÇÅ : f.hom‚ÇÅ = g.hom‚ÇÅ) (h‚ÇÇ : f.hom‚ÇÇ = g.hom‚ÇÇ) (h‚ÇÉ : f.hom‚ÇÉ = g.hom‚ÇÉ) : f = g :=
  TriangleMorphism.ext _ _ h‚ÇÅ h‚ÇÇ h‚ÇÉ

@[simps]
def Triangle.homMk (A B : Triangle C)
    (hom‚ÇÅ : A.obj‚ÇÅ ‚ü∂ B.obj‚ÇÅ) (hom‚ÇÇ : A.obj‚ÇÇ ‚ü∂ B.obj‚ÇÇ) (hom‚ÇÉ : A.obj‚ÇÉ ‚ü∂ B.obj‚ÇÉ)
    (comm‚ÇÅ : A.mor‚ÇÅ ‚â´ hom‚ÇÇ = hom‚ÇÅ ‚â´ B.mor‚ÇÅ := by aesop_cat)
    (comm‚ÇÇ : A.mor‚ÇÇ ‚â´ hom‚ÇÉ = hom‚ÇÇ ‚â´ B.mor‚ÇÇ := by aesop_cat)
    (comm‚ÇÉ : A.mor‚ÇÉ ‚â´ hom‚ÇÅ‚ü¶1‚üß' = hom‚ÇÉ ‚â´ B.mor‚ÇÉ := by aesop_cat) :
    A ‚ü∂ B where
  hom‚ÇÅ := hom‚ÇÅ
  hom‚ÇÇ := hom‚ÇÇ
  hom‚ÇÉ := hom‚ÇÉ
  comm‚ÇÅ := comm‚ÇÅ
  comm‚ÇÇ := comm‚ÇÇ
  comm‚ÇÉ := comm‚ÇÉ
#align category_theory.pretriangulated.triangle.hom_mk CategoryTheory.Pretriangulated.Triangle.homMk

@[simps]
def Triangle.isoMk (A B : Triangle C)
    (iso‚ÇÅ : A.obj‚ÇÅ ‚âÖ B.obj‚ÇÅ) (iso‚ÇÇ : A.obj‚ÇÇ ‚âÖ B.obj‚ÇÇ) (iso‚ÇÉ : A.obj‚ÇÉ ‚âÖ B.obj‚ÇÉ)
    (comm‚ÇÅ : A.mor‚ÇÅ ‚â´ iso‚ÇÇ.hom = iso‚ÇÅ.hom ‚â´ B.mor‚ÇÅ := by aesop_cat)
    (comm‚ÇÇ : A.mor‚ÇÇ ‚â´ iso‚ÇÉ.hom = iso‚ÇÇ.hom ‚â´ B.mor‚ÇÇ := by aesop_cat)
    (comm‚ÇÉ : A.mor‚ÇÉ ‚â´ iso‚ÇÅ.hom‚ü¶1‚üß' = iso‚ÇÉ.hom ‚â´ B.mor‚ÇÉ := by aesop_cat) : A ‚âÖ B where
  hom := Triangle.homMk _ _ iso‚ÇÅ.hom iso‚ÇÇ.hom iso‚ÇÉ.hom comm‚ÇÅ comm‚ÇÇ comm‚ÇÉ
  inv := Triangle.homMk _ _ iso‚ÇÅ.inv iso‚ÇÇ.inv iso‚ÇÉ.inv
    (by simp only [‚Üê cancel_mono iso‚ÇÇ.hom, assoc, Iso.inv_hom_id, comp_id,
      comm‚ÇÅ, Iso.inv_hom_id_assoc])
    (by simp only [‚Üê cancel_mono iso‚ÇÉ.hom, assoc, Iso.inv_hom_id, comp_id,
      comm‚ÇÇ, Iso.inv_hom_id_assoc])
    (by simp only [‚Üê cancel_mono (iso‚ÇÅ.hom‚ü¶(1 : ‚Ñ§)‚üß'), Category.assoc, comm‚ÇÉ,
      Iso.inv_hom_id_assoc, ‚Üê Functor.map_comp, Iso.inv_hom_id,
      Functor.map_id, Category.comp_id])
#align category_theory.pretriangulated.triangle.iso_mk CategoryTheory.Pretriangulated.Triangle.isoMk

lemma Triangle.eqToHom_hom‚ÇÅ {A B : Triangle C} (h : A = B) :
    (eqToHom h).hom‚ÇÅ = eqToHom (by subst h; rfl) := by subst h; rfl
lemma Triangle.eqToHom_hom‚ÇÇ {A B : Triangle C} (h : A = B) :
    (eqToHom h).hom‚ÇÇ = eqToHom (by subst h; rfl) := by subst h; rfl
lemma Triangle.eqToHom_hom‚ÇÉ {A B : Triangle C} (h : A = B) :
    (eqToHom h).hom‚ÇÉ = eqToHom (by subst h; rfl) := by subst h; rfl

end CategoryTheory.Pretriangulated
