/-
Copyright (c) 2021 Luke Kershaw. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Luke Kershaw
-/
import Mathlib.CategoryTheory.Preadditive.AdditiveFunctor
import Mathlib.CategoryTheory.Triangulated.Basic

/-!
# Rotate

This file adds the ability to rotate triangles and triangle morphisms.
It also shows that rotation gives an equivalence on the category of triangles.

-/


noncomputable section

open CategoryTheory

open CategoryTheory.Preadditive

open CategoryTheory.Limits

universe v v‚ÇÄ v‚ÇÅ v‚ÇÇ u u‚ÇÄ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory.Pretriangulated

open CategoryTheory.Category

variable {C : Type u} [Category.{v} C] [Preadditive C]
variable [HasShift C ‚Ñ§]
variable (X : C)

/-- If you rotate a triangle, you get another triangle.
Given a triangle of the form:
```
      f       g       h
  X  ‚îÄ‚îÄ‚îÄ> Y  ‚îÄ‚îÄ‚îÄ> Z  ‚îÄ‚îÄ‚îÄ> X‚ü¶1‚üß
```
applying `rotate` gives a triangle of the form:
```
      g       h        -f‚ü¶1‚üß'
  Y  ‚îÄ‚îÄ‚îÄ> Z  ‚îÄ‚îÄ‚îÄ>  X‚ü¶1‚üß ‚îÄ‚îÄ‚îÄ> Y‚ü¶1‚üß
```
-/
@[simps!]
def Triangle.rotate (T : Triangle C) : Triangle C :=
  Triangle.mk T.mor‚ÇÇ T.mor‚ÇÉ (-T.mor‚ÇÅ‚ü¶1‚üß')

section

/-- Given a triangle of the form:
```
      f       g       h
  X  ‚îÄ‚îÄ‚îÄ> Y  ‚îÄ‚îÄ‚îÄ> Z  ‚îÄ‚îÄ‚îÄ> X‚ü¶1‚üß
```
applying `invRotate` gives a triangle that can be thought of as:
```
        -h‚ü¶-1‚üß'     f       g
  Z‚ü¶-1‚üß  ‚îÄ‚îÄ‚îÄ>  X  ‚îÄ‚îÄ‚îÄ> Y  ‚îÄ‚îÄ‚îÄ> Z
```
(note that this diagram doesn't technically fit the definition of triangle, as `Z‚ü¶-1‚üß‚ü¶1‚üß` is
not necessarily equal to `Z`, but it is isomorphic, by the `counitIso` of `shiftEquiv C 1`)
-/
@[simps!]
def Triangle.invRotate (T : Triangle C) : Triangle C :=
  Triangle.mk (-T.mor‚ÇÉ‚ü¶(-1 : ‚Ñ§)‚üß' ‚â´ (shiftEquiv C (1 : ‚Ñ§)).unitIso.inv.app _) (T.mor‚ÇÅ)
    (T.mor‚ÇÇ ‚â´ (shiftEquiv C (1 : ‚Ñ§)).counitIso.inv.app _ )

end

attribute [local simp] shift_shift_neg' shift_neg_shift'
  shift_shiftFunctorCompIsoId_add_neg_cancel_inv_app
  shift_shiftFunctorCompIsoId_add_neg_cancel_hom_app

variable (C)

/-- Rotating triangles gives an endofunctor on the category of triangles in `C`.
-/
@[simps]
def rotate : Triangle C ‚•§ Triangle C where
  obj := Triangle.rotate
  map f :=
  { hom‚ÇÅ := f.hom‚ÇÇ
    hom‚ÇÇ := f.hom‚ÇÉ
    hom‚ÇÉ := f.hom‚ÇÅ‚ü¶1‚üß'
    comm‚ÇÉ := by
      dsimp
      simp only [comp_neg, neg_comp, ‚Üê Functor.map_comp, f.comm‚ÇÅ] }

/-- The inverse rotation of triangles gives an endofunctor on the category of triangles in `C`.
-/
@[simps]
def invRotate : Triangle C ‚•§ Triangle C where
  obj := Triangle.invRotate
  map f :=
  { hom‚ÇÅ := f.hom‚ÇÉ‚ü¶-1‚üß'
    hom‚ÇÇ := f.hom‚ÇÅ
    hom‚ÇÉ := f.hom‚ÇÇ
    comm‚ÇÅ := by
      dsimp
      simp only [comp_neg, ‚Üê Functor.map_comp_assoc, ‚Üê f.comm‚ÇÉ]
      rw [Functor.map_comp]
      simp }

variable {C}
variable [‚àÄ n : ‚Ñ§, Functor.Additive (shiftFunctor C n)]

/-- The unit isomorphism of the auto-equivalence of categories `triangleRotation C` of
`Triangle C` given by the rotation of triangles. -/
@[simps!]
def rotCompInvRot : ùü≠ (Triangle C) ‚âÖ rotate C ‚ãô invRotate C :=
  NatIso.ofComponents fun T ‚Ü¶ Triangle.isoMk _ _
    ((shiftEquiv C (1 : ‚Ñ§)).unitIso.app T.obj‚ÇÅ) (Iso.refl _) (Iso.refl _)

/-- The counit isomorphism of the auto-equivalence of categories `triangleRotation C` of
`Triangle C` given by the rotation of triangles. -/
@[simps!]
def invRotCompRot : invRotate C ‚ãô rotate C ‚âÖ ùü≠ (Triangle C) :=
  NatIso.ofComponents fun T ‚Ü¶ Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _)
    ((shiftEquiv C (1 : ‚Ñ§)).counitIso.app T.obj‚ÇÉ)

variable (C) in
/-- Rotating triangles gives an auto-equivalence on the category of triangles in `C`.
-/
@[simps]
def triangleRotation : Equivalence (Triangle C) (Triangle C) where
  functor := rotate C
  inverse := invRotate C
  unitIso := rotCompInvRot
  counitIso := invRotCompRot

instance : (rotate C).IsEquivalence := by
  change (triangleRotation C).functor.IsEquivalence
  infer_instance

instance : (invRotate C).IsEquivalence := by
  change (triangleRotation C).inverse.IsEquivalence
  infer_instance

end CategoryTheory.Pretriangulated
