/-
Copyright (c) 2021 Luke Kershaw. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Luke Kershaw
-/
import Mathlib.CategoryTheory.Preadditive.AdditiveFunctor
import Mathlib.CategoryTheory.Triangulated.Basic

#align_import category_theory.triangulated.rotate from "leanprover-community/mathlib"@"94d4e70e97c36c896cb70fb42821acfed040de60"

/-!
# Rotate

This file adds the ability to rotate triangles and triangle morphisms.
It also shows that rotation gives an equivalence on the category of triangles.

-/


noncomputable section

open CategoryTheory

open CategoryTheory.Preadditive

open CategoryTheory.Limits

universe v vâ‚€ vâ‚ vâ‚‚ u uâ‚€ uâ‚ uâ‚‚

namespace CategoryTheory.Pretriangulated

open CategoryTheory.Category

variable {C : Type u} [Category.{v} C] [Preadditive C]
variable [HasShift C â„¤]
variable (X : C)

/-- If you rotate a triangle, you get another triangle.
Given a triangle of the form:
```
      f       g       h
  X  â”€â”€â”€> Y  â”€â”€â”€> Z  â”€â”€â”€> XâŸ¦1âŸ§
```
applying `rotate` gives a triangle of the form:
```
      g       h        -fâŸ¦1âŸ§'
  Y  â”€â”€â”€> Z  â”€â”€â”€>  XâŸ¦1âŸ§ â”€â”€â”€> YâŸ¦1âŸ§
```
-/
@[simps!]
def Triangle.rotate (T : Triangle C) : Triangle C :=
  Triangle.mk T.morâ‚‚ T.morâ‚ƒ (-T.morâ‚âŸ¦1âŸ§')
#align category_theory.pretriangulated.triangle.rotate CategoryTheory.Pretriangulated.Triangle.rotate

section

/-- Given a triangle of the form:
```
      f       g       h
  X  â”€â”€â”€> Y  â”€â”€â”€> Z  â”€â”€â”€> XâŸ¦1âŸ§
```
applying `invRotate` gives a triangle that can be thought of as:
```
        -hâŸ¦-1âŸ§'     f       g
  ZâŸ¦-1âŸ§  â”€â”€â”€>  X  â”€â”€â”€> Y  â”€â”€â”€> Z
```
(note that this diagram doesn't technically fit the definition of triangle, as `ZâŸ¦-1âŸ§âŸ¦1âŸ§` is
not necessarily equal to `Z`, but it is isomorphic, by the `counitIso` of `shiftEquiv C 1`)
-/
@[simps!]
def Triangle.invRotate (T : Triangle C) : Triangle C :=
  Triangle.mk (-T.morâ‚ƒâŸ¦(-1 : â„¤)âŸ§' â‰« (shiftEquiv C (1 : â„¤)).unitIso.inv.app _) (T.morâ‚)
    (T.morâ‚‚ â‰« (shiftEquiv C (1 : â„¤)).counitIso.inv.app _ )
#align category_theory.pretriangulated.triangle.inv_rotate CategoryTheory.Pretriangulated.Triangle.invRotate

end

attribute [local simp] shift_shift_neg' shift_neg_shift'
  shift_shiftFunctorCompIsoId_add_neg_self_inv_app
  shift_shiftFunctorCompIsoId_add_neg_self_hom_app

variable (C)

/-- Rotating triangles gives an endofunctor on the category of triangles in `C`.
-/
@[simps]
def rotate : Triangle C â¥¤ Triangle C where
  obj := Triangle.rotate
  map f :=
  { homâ‚ := f.homâ‚‚
    homâ‚‚ := f.homâ‚ƒ
    homâ‚ƒ := f.homâ‚âŸ¦1âŸ§'
    commâ‚ƒ := by
      dsimp
      simp only [comp_neg, neg_comp, â† Functor.map_comp, f.commâ‚] }
#align category_theory.pretriangulated.rotate CategoryTheory.Pretriangulated.rotate

/-- The inverse rotation of triangles gives an endofunctor on the category of triangles in `C`.
-/
@[simps]
def invRotate : Triangle C â¥¤ Triangle C where
  obj := Triangle.invRotate
  map f :=
  { homâ‚ := f.homâ‚ƒâŸ¦-1âŸ§'
    homâ‚‚ := f.homâ‚
    homâ‚ƒ := f.homâ‚‚
    commâ‚ := by
      dsimp
      simp only [neg_comp, assoc, comp_neg, neg_inj, â† Functor.map_comp_assoc, â† f.commâ‚ƒ]
      rw [Functor.map_comp, assoc]
      erw [â† NatTrans.naturality]
      rfl
    commâ‚ƒ := by
      erw [â† reassoc_of% f.commâ‚‚, Category.assoc, â† NatTrans.naturality]
      rfl }
#align category_theory.pretriangulated.inv_rotate CategoryTheory.Pretriangulated.invRotate

variable {C}
variable [âˆ€ n : â„¤, Functor.Additive (shiftFunctor C n)]

/-- The unit isomorphism of the auto-equivalence of categories `triangleRotation C` of
`Triangle C` given by the rotation of triangles. -/
@[simps!]
def rotCompInvRot : ðŸ­ (Triangle C) â‰… rotate C â‹™ invRotate C :=
  NatIso.ofComponents fun T => Triangle.isoMk _ _
    ((shiftEquiv C (1 : â„¤)).unitIso.app T.objâ‚) (Iso.refl _) (Iso.refl _)
#align category_theory.pretriangulated.rot_comp_inv_rot CategoryTheory.Pretriangulated.rotCompInvRot

/-- The counit isomorphism of the auto-equivalence of categories `triangleRotation C` of
`Triangle C` given by the rotation of triangles. -/
@[simps!]
def invRotCompRot : invRotate C â‹™ rotate C â‰… ðŸ­ (Triangle C) :=
  NatIso.ofComponents fun T => Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _)
    ((shiftEquiv C (1 : â„¤)).counitIso.app T.objâ‚ƒ)
#align category_theory.pretriangulated.inv_rot_comp_rot CategoryTheory.Pretriangulated.invRotCompRot

variable (C)

/-- Rotating triangles gives an auto-equivalence on the category of triangles in `C`.
-/
@[simps]
def triangleRotation : Equivalence (Triangle C) (Triangle C) where
  functor := rotate C
  inverse := invRotate C
  unitIso := rotCompInvRot
  counitIso := invRotCompRot
#align category_theory.pretriangulated.triangle_rotation CategoryTheory.Pretriangulated.triangleRotation

variable {C}

instance : (rotate C).IsEquivalence := by
  change (triangleRotation C).functor.IsEquivalence
  infer_instance

instance : (invRotate C).IsEquivalence := by
  change (triangleRotation C).inverse.IsEquivalence
  infer_instance

end CategoryTheory.Pretriangulated
