/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Limits.Opposites
import Mathlib.CategoryTheory.Shift.Opposite
import Mathlib.CategoryTheory.Shift.Pullback
import Mathlib.CategoryTheory.Triangulated.Triangulated
import Mathlib.CategoryTheory.Triangulated.Adjunction
import Mathlib.Tactic.Linarith

/-!
# The (pre)triangulated structure on the opposite category

In this file, we shall construct the (pre)triangulated structure
on the opposite category `Cáµ’áµ–` of a (pre)triangulated category `C`.

The shift on `Cáµ’áµ–` is obtained by combining the constructions in the files
`CategoryTheory.Shift.Opposite` and `CategoryTheory.Shift.Pullback`.
When the user opens `CategoryTheory.Pretriangulated.Opposite`, the
category `Cáµ’áµ–` is equipped with the shift by `â„¤` such that
shifting by `n : â„¤` on `Cáµ’áµ–` corresponds to the shift by
`-n` on `C`. This is actually a definitional equality, but the user
should not rely on this, and instead use the isomorphism
`shiftFunctorOpIso C n m hnm : shiftFunctor Cáµ’áµ– n â‰… (shiftFunctor C m).op`
where `hnm : n + m = 0`.

Some compatibilities between the shifts on `C` and `Cáµ’áµ–` are also expressed through
the equivalence of categories `opShiftFunctorEquivalence C n : Cáµ’áµ– â‰Œ Cáµ’áµ–` whose
functor is `shiftFunctor Cáµ’áµ– n` and whose inverse functor is `(shiftFunctor C n).op`.

If `X âŸ¶ Y âŸ¶ Z âŸ¶ XâŸ¦1âŸ§` is a distinguished triangle in `C`, then the triangle
`op Z âŸ¶ op Y âŸ¶ op X âŸ¶ (op Z)âŸ¦1âŸ§` that is deduced *without introducing signs*
shall be a distinguished triangle in `Cáµ’áµ–`. This is equivalent to the definition
in [Verdiers's thesis, p. 96][verdier1996] which would require that the triangle
`(op X)âŸ¦-1âŸ§ âŸ¶ op Z âŸ¶ op Y âŸ¶ op X` (without signs) is *antidistinguished*.

## References
* [Jean-Louis Verdier, *Des catÃ©gories dÃ©rivÃ©es des catÃ©gories abÃ©liennes*][verdier1996]

-/

namespace CategoryTheory

open Category Limits Preadditive ZeroObject

variable (C : Type*) [Category C]

section

variable {C}
variable {D : Type*} [Category D] {F G : C â¥¤ D} (e : F â‰… G) (X : C)

@[reassoc (attr := simp)]
lemma Iso.op_hom_inv_id_app : (e.hom.app X).op â‰« (e.inv.app X).op = ðŸ™ _ := by
  rw [â† op_comp, e.inv_hom_id_app, op_id]

@[reassoc (attr := simp)]
lemma Iso.op_inv_hom_id_app : (e.inv.app X).op â‰« (e.hom.app X).op = ðŸ™ _ := by
  rw [â† op_comp, e.hom_inv_id_app, op_id]

end

namespace Pretriangulated

variable [HasShift C â„¤]

namespace Opposite

/-- As it is unclear whether the opposite category `Cáµ’áµ–` should always be equipped
with the shift by `â„¤` such that shifting by `n` on `Cáµ’áµ–` corresponds to shifting
by `-n` on `C`, the user shall have to do `open CategoryTheory.Pretriangulated.Opposite`
in order to get this shift and the (pre)triangulated structure on `Cáµ’áµ–`. -/

private abbrev OppositeShiftAux :=
  PullbackShift (OppositeShift C â„¤)
    (AddMonoidHom.mk' (fun (n : â„¤) => -n) (by intros; dsimp; linarith))

/-- The category `Cáµ’áµ–` is equipped with the shift such that the shift by `n` on `Cáµ’áµ–`
corresponds to the shift by `-n` on `C`. -/
noncomputable scoped instance : HasShift Cáµ’áµ– â„¤ :=
  (inferInstance : HasShift (OppositeShiftAux C) â„¤)

instance [Preadditive C] [âˆ€ (n : â„¤), (shiftFunctor C n).Additive] (n : â„¤) :
    (shiftFunctor Cáµ’áµ– n).Additive :=
  (inferInstance : (shiftFunctor (OppositeShiftAux C) n).Additive)

end Opposite

open Opposite

/-- The shift functor on the opposite category identifies to the opposite functor
of a shift functor on the original category. -/
noncomputable def shiftFunctorOpIso (n m : â„¤) (hnm : n + m = 0) :
    shiftFunctor Cáµ’áµ– n â‰… (shiftFunctor C m).op := eqToIso (by
  obtain rfl : m = -n := by linarith
  rfl)

variable {C}

lemma shiftFunctorZero_op_hom_app (X : Cáµ’áµ–) :
    (shiftFunctorZero Cáµ’áµ– â„¤).hom.app X = (shiftFunctorOpIso C 0 0 (zero_add 0)).hom.app X â‰«
      ((shiftFunctorZero C â„¤).inv.app X.unop).op := by
  erw [@pullbackShiftFunctorZero_hom_app (OppositeShift C â„¤), oppositeShiftFunctorZero_hom_app]
  rfl

lemma shiftFunctorZero_op_inv_app (X : Cáµ’áµ–) :
    (shiftFunctorZero Cáµ’áµ– â„¤).inv.app X =
      ((shiftFunctorZero C â„¤).hom.app X.unop).op â‰«
      (shiftFunctorOpIso C 0 0 (zero_add 0)).inv.app X := by
  rw [â† cancel_epi ((shiftFunctorZero Cáµ’áµ– â„¤).hom.app X), Iso.hom_inv_id_app,
    shiftFunctorZero_op_hom_app, assoc, â† op_comp_assoc, Iso.hom_inv_id_app, op_id,
    id_comp, Iso.hom_inv_id_app]

lemma shiftFunctorAdd'_op_hom_app (X : Cáµ’áµ–) (aâ‚ aâ‚‚ aâ‚ƒ : â„¤) (h : aâ‚ + aâ‚‚ = aâ‚ƒ)
    (bâ‚ bâ‚‚ bâ‚ƒ : â„¤) (hâ‚ : aâ‚ + bâ‚ = 0) (hâ‚‚ : aâ‚‚ + bâ‚‚ = 0) (hâ‚ƒ : aâ‚ƒ + bâ‚ƒ = 0) :
    (shiftFunctorAdd' Cáµ’áµ– aâ‚ aâ‚‚ aâ‚ƒ h).hom.app X =
      (shiftFunctorOpIso C _ _ hâ‚ƒ).hom.app X â‰«
        ((shiftFunctorAdd' C bâ‚ bâ‚‚ bâ‚ƒ (by linarith)).inv.app X.unop).op â‰«
        (shiftFunctorOpIso C _ _ hâ‚‚).inv.app _ â‰«
        (shiftFunctor Cáµ’áµ– aâ‚‚).map ((shiftFunctorOpIso C _ _ hâ‚).inv.app X) := by
  erw [@pullbackShiftFunctorAdd'_hom_app (OppositeShift C â„¤) _ _ _ _ _ _ _ X
    aâ‚ aâ‚‚ aâ‚ƒ h bâ‚ bâ‚‚ bâ‚ƒ (by dsimp; linarith) (by dsimp; linarith) (by dsimp; linarith)]
  erw [oppositeShiftFunctorAdd'_hom_app]
  obtain rfl : bâ‚ = -aâ‚ := by linarith
  obtain rfl : bâ‚‚ = -aâ‚‚ := by linarith
  obtain rfl : bâ‚ƒ = -aâ‚ƒ := by linarith
  rfl

lemma shiftFunctorAdd'_op_inv_app (X : Cáµ’áµ–) (aâ‚ aâ‚‚ aâ‚ƒ : â„¤) (h : aâ‚ + aâ‚‚ = aâ‚ƒ)
    (bâ‚ bâ‚‚ bâ‚ƒ : â„¤) (hâ‚ : aâ‚ + bâ‚ = 0) (hâ‚‚ : aâ‚‚ + bâ‚‚ = 0) (hâ‚ƒ : aâ‚ƒ + bâ‚ƒ = 0) :
    (shiftFunctorAdd' Cáµ’áµ– aâ‚ aâ‚‚ aâ‚ƒ h).inv.app X =
      (shiftFunctor Cáµ’áµ– aâ‚‚).map ((shiftFunctorOpIso C _ _ hâ‚).hom.app X) â‰«
      (shiftFunctorOpIso C _ _ hâ‚‚).hom.app _ â‰«
      ((shiftFunctorAdd' C bâ‚ bâ‚‚ bâ‚ƒ (by linarith)).hom.app X.unop).op â‰«
      (shiftFunctorOpIso C _ _ hâ‚ƒ).inv.app X := by
  rw [â† cancel_epi ((shiftFunctorAdd' Cáµ’áµ– aâ‚ aâ‚‚ aâ‚ƒ h).hom.app X), Iso.hom_inv_id_app,
    shiftFunctorAdd'_op_hom_app X aâ‚ aâ‚‚ aâ‚ƒ h bâ‚ bâ‚‚ bâ‚ƒ hâ‚ hâ‚‚ hâ‚ƒ,
    assoc, assoc, assoc, â† Functor.map_comp_assoc, Iso.inv_hom_id_app]
  erw [Functor.map_id, id_comp, Iso.inv_hom_id_app_assoc]
  rw [â† op_comp_assoc, Iso.hom_inv_id_app, op_id, id_comp, Iso.hom_inv_id_app]

lemma shiftFunctor_op_map (n m : â„¤) (hnm : n + m = 0) {K L : Cáµ’áµ–} (Ï† : K âŸ¶ L) :
    (shiftFunctor Cáµ’áµ– n).map Ï† =
      (shiftFunctorOpIso C n m hnm).hom.app K â‰« ((shiftFunctor C m).map Ï†.unop).op â‰«
        (shiftFunctorOpIso C n m hnm).inv.app L :=
  (NatIso.naturality_2 (shiftFunctorOpIso C n m hnm) Ï†).symm

variable (C)

/-- The autoequivalence `Cáµ’áµ– â‰Œ Cáµ’áµ–` whose functor is `shiftFunctor Cáµ’áµ– n` and whose inverse
functor is `(shiftFunctor C n).op`. Do not unfold the definitions of the unit and counit
isomorphisms: the compatibilities they satisfy are stated as separate lemmas. -/
@[simps functor inverse]
noncomputable def opShiftFunctorEquivalence (n : â„¤) : Cáµ’áµ– â‰Œ Cáµ’áµ– where
  functor := shiftFunctor Cáµ’áµ– n
  inverse := (shiftFunctor C n).op
  unitIso := NatIso.op (shiftFunctorCompIsoId C (-n) n n.add_left_neg) â‰ªâ‰«
    isoWhiskerRight (shiftFunctorOpIso C n (-n) n.add_right_neg).symm (shiftFunctor C n).op
  counitIso := isoWhiskerLeft _ (shiftFunctorOpIso C n (-n) n.add_right_neg) â‰ªâ‰«
    NatIso.op (shiftFunctorCompIsoId C n (-n) n.add_right_neg).symm
  functor_unitIso_comp X := Quiver.Hom.unop_inj (by
    dsimp [shiftFunctorOpIso]
    erw [comp_id, Functor.map_id, comp_id]
    change (shiftFunctorCompIsoId C n (-n) (add_neg_self n)).inv.app (X.unopâŸ¦-nâŸ§) â‰«
      ((shiftFunctorCompIsoId C (-n) n (neg_add_self n)).hom.app X.unop)âŸ¦-nâŸ§' = ðŸ™ _
    rw [shift_shiftFunctorCompIsoId_neg_add_self_hom_app n X.unop, Iso.inv_hom_id_app])

/-! The naturality of the unit and counit isomorphisms are restated in the following
lemmas so as to mitigate the need for `erw`. -/

@[reassoc (attr := simp)]
lemma opShiftFunctorEquivalence_unitIso_hom_naturality (n : â„¤) {X Y : Cáµ’áµ–} (f : X âŸ¶ Y) :
    f â‰« (opShiftFunctorEquivalence C n).unitIso.hom.app Y =
      (opShiftFunctorEquivalence C n).unitIso.hom.app X â‰« (fâŸ¦nâŸ§').unopâŸ¦nâŸ§'.op :=
  (opShiftFunctorEquivalence C n).unitIso.hom.naturality f

@[reassoc (attr := simp)]
lemma opShiftFunctorEquivalence_unitIso_inv_naturality (n : â„¤) {X Y : Cáµ’áµ–} (f : X âŸ¶ Y) :
    (fâŸ¦nâŸ§').unopâŸ¦nâŸ§'.op â‰« (opShiftFunctorEquivalence C n).unitIso.inv.app Y =
      (opShiftFunctorEquivalence C n).unitIso.inv.app X â‰« f :=
  (opShiftFunctorEquivalence C n).unitIso.inv.naturality f

@[reassoc (attr := simp)]
lemma opShiftFunctorEquivalence_counitIso_hom_naturality (n : â„¤) {X Y : Cáµ’áµ–} (f : X âŸ¶ Y) :
    f.unopâŸ¦nâŸ§'.opâŸ¦nâŸ§' â‰« (opShiftFunctorEquivalence C n).counitIso.hom.app Y =
      (opShiftFunctorEquivalence C n).counitIso.hom.app X â‰« f :=
  (opShiftFunctorEquivalence C n).counitIso.hom.naturality f

@[reassoc (attr := simp)]
lemma opShiftFunctorEquivalence_counitIso_inv_naturality (n : â„¤) {X Y : Cáµ’áµ–} (f : X âŸ¶ Y) :
    f â‰« (opShiftFunctorEquivalence C n).counitIso.inv.app Y =
      (opShiftFunctorEquivalence C n).counitIso.inv.app X â‰« f.unopâŸ¦nâŸ§'.opâŸ¦nâŸ§' :=
  (opShiftFunctorEquivalence C n).counitIso.inv.naturality f

variable {C}

lemma opShiftFunctorEquivalence_unitIso_hom_app (X : Cáµ’áµ–) (n m : â„¤) (hnm : n + m = 0) :
    (opShiftFunctorEquivalence C n).unitIso.hom.app X =
      ((shiftFunctorCompIsoId C m n (by linarith)).hom.app X.unop).op â‰«
        ((shiftFunctorOpIso C n m hnm).inv.app X).unopâŸ¦nâŸ§'.op := by
  obtain rfl : m = -n := by linarith
  rfl

lemma opShiftFunctorEquivalence_unitIso_inv_app (X : Cáµ’áµ–) (n m : â„¤) (hnm : n + m = 0) :
    (opShiftFunctorEquivalence C n).unitIso.inv.app X =
      ((shiftFunctorOpIso C n m hnm).hom.app X).unopâŸ¦nâŸ§'.op â‰«
      ((shiftFunctorCompIsoId C m n (by linarith)).inv.app X.unop).op := by
  obtain rfl : m = -n := by linarith
  rfl

lemma opShiftFunctorEquivalence_counitIso_hom_app (X : Cáµ’áµ–) (n m : â„¤) (hnm : n + m = 0) :
    (opShiftFunctorEquivalence C n).counitIso.hom.app X =
      ((shiftFunctorOpIso C n m hnm).hom.app (Opposite.op (X.unopâŸ¦nâŸ§))) â‰«
        ((shiftFunctorCompIsoId C n m hnm).inv.app X.unop).op := by
  obtain rfl : m = -n := by linarith
  rfl

lemma opShiftFunctorEquivalence_counitIso_inv_app (X : Cáµ’áµ–) (n m : â„¤) (hnm : n + m = 0) :
    (opShiftFunctorEquivalence C n).counitIso.inv.app X =
      ((shiftFunctorCompIsoId C n m hnm).hom.app X.unop).op â‰«
      ((shiftFunctorOpIso C n m hnm).inv.app (Opposite.op (X.unopâŸ¦nâŸ§))) := by
  obtain rfl : m = -n := by linarith
  rfl

lemma shift_unop_opShiftFunctorEquivalence_counitIso_inv_app (X : Cáµ’áµ–) (n : â„¤) :
    ((opShiftFunctorEquivalence C n).counitIso.inv.app X).unopâŸ¦nâŸ§' =
      ((opShiftFunctorEquivalence C n).unitIso.hom.app ((Opposite.op ((X.unop)âŸ¦nâŸ§)))).unop :=
  Quiver.Hom.op_inj ((opShiftFunctorEquivalence C n).unit_app_inverse X).symm

lemma shift_unop_opShiftFunctorEquivalence_counitIso_hom_app (X : Cáµ’áµ–) (n : â„¤) :
    ((opShiftFunctorEquivalence C n).counitIso.hom.app X).unopâŸ¦nâŸ§' =
      ((opShiftFunctorEquivalence C n).unitIso.inv.app ((Opposite.op (X.unopâŸ¦nâŸ§)))).unop :=
  Quiver.Hom.op_inj ((opShiftFunctorEquivalence C n).unitInv_app_inverse X).symm

lemma opShiftFunctorEquivalence_counitIso_inv_app_shift (X : Cáµ’áµ–) (n : â„¤) :
    (opShiftFunctorEquivalence C n).counitIso.inv.app (XâŸ¦nâŸ§) =
      ((opShiftFunctorEquivalence C n).unitIso.hom.app X)âŸ¦nâŸ§' :=
  (opShiftFunctorEquivalence C n).counitInv_app_functor X

lemma opShiftFunctorEquivalence_counitIso_hom_app_shift (X : Cáµ’áµ–) (n : â„¤) :
    (opShiftFunctorEquivalence C n).counitIso.hom.app (XâŸ¦nâŸ§) =
      ((opShiftFunctorEquivalence C n).unitIso.inv.app X)âŸ¦nâŸ§' :=
  ((opShiftFunctorEquivalence C n).counit_app_functor X)

lemma shiftFunctorCompIsoId_op_hom_app (X : Cáµ’áµ–) (n m : â„¤) (hnm : n + m = 0) :
    (shiftFunctorCompIsoId Cáµ’áµ– n m hnm).hom.app X =
      ((shiftFunctorOpIso C n m hnm).hom.app X)âŸ¦mâŸ§' â‰«
        (shiftFunctorOpIso C m n (by linarith)).hom.app (Opposite.op (X.unopâŸ¦mâŸ§)) â‰«
          ((shiftFunctorCompIsoId C m n (by linarith)).inv.app X.unop).op := by
  dsimp [shiftFunctorCompIsoId]
  simp only [shiftFunctorAdd'_op_inv_app X n m 0 hnm m n 0 hnm (by linarith) (add_zero 0),
    shiftFunctorZero_op_hom_app X]
  simp only [Functor.op_obj, Opposite.unop_op, Functor.comp_obj,
    NatTrans.naturality_assoc, Functor.op_map, Functor.id_obj,
    Opposite.op_unop, assoc, Iso.inv_hom_id_app_assoc]

lemma shiftFunctorCompIsoId_op_inv_app (X : Cáµ’áµ–) (n m : â„¤) (hnm : n + m = 0) :
    (shiftFunctorCompIsoId Cáµ’áµ– n m hnm).inv.app X =
      ((shiftFunctorCompIsoId C m n (by linarith)).hom.app X.unop).op â‰«
      (shiftFunctorOpIso C m n (by linarith)).inv.app (Opposite.op (X.unopâŸ¦mâŸ§)) â‰«
      ((shiftFunctorOpIso C n m hnm).inv.app X)âŸ¦mâŸ§' := by
  dsimp [shiftFunctorCompIsoId]
  simp only [shiftFunctorAdd'_op_hom_app X n m 0 hnm m n 0 hnm (by linarith) (add_zero 0),
    shiftFunctorZero_op_inv_app X]
  simp only [Functor.id_obj, Opposite.op_unop, Functor.op_obj, Functor.comp_obj, assoc,
    Iso.inv_hom_id_app_assoc]
  rfl

lemma opShiftFunctorEquivalence_inv_app_shift (X : Cáµ’áµ–) (n m : â„¤) (hnm : n + m = 0) :
    ((opShiftFunctorEquivalence C m).unitIso.inv.app (XâŸ¦nâŸ§)) =
      ((shiftFunctorCompIsoId Cáµ’áµ– n m hnm).hom.app X).unopâŸ¦mâŸ§'.op â‰«
      ((shiftFunctorOpIso C n m hnm).inv.app X) := by
  rw [shiftFunctorCompIsoId_op_hom_app,
    opShiftFunctorEquivalence_unitIso_inv_app _ m n (by linarith)]
  simp only [opShiftFunctorEquivalence_functor, opShiftFunctorEquivalence_inverse, Functor.comp_obj, Functor.op_obj,
    Functor.id_obj, Opposite.unop_op, Opposite.op_unop, NatTrans.naturality_assoc, Functor.op_map, unop_comp,
    Quiver.Hom.unop_op, assoc, Functor.map_comp, op_comp]
  apply Quiver.Hom.unop_inj
  simp only [Opposite.op_unop, Opposite.unop_op, Quiver.Hom.unop_op, unop_comp, assoc]
  rw [shift_shiftFunctorCompIsoId_inv_app m n (by linarith) X.unop]
  erw [â† NatTrans.naturality_assoc]
  dsimp
  rw [â† unop_comp_assoc, Iso.hom_inv_id_app, unop_id, id_comp]

lemma natTrans_app_op_shift {D : Type*} [Category D] {F G : Cáµ’áµ– â¥¤ D} (Î± : F âŸ¶ G)
    (X : Cáµ’áµ–) (n m : â„¤) (hnm : n + m = 0) : Î±.app (XâŸ¦nâŸ§) =
      F.map ((shiftFunctorOpIso C n m hnm).hom.app X) â‰« Î±.app (Opposite.op (X.unopâŸ¦mâŸ§)) â‰«
        G.map ((shiftFunctorOpIso C n m hnm).inv.app X) := by
  rw [â† Î±.naturality, â† F.map_comp_assoc, Iso.hom_inv_id_app, F.map_id, id_comp]

noncomputable def opShiftFunctorEquivalence_symm_homEquiv (n : â„¤) (X Y : Cáµ’áµ–) :
    (Opposite.op (X.unopâŸ¦nâŸ§) âŸ¶ Y) â‰ƒ (X âŸ¶ YâŸ¦nâŸ§) :=
  (opShiftFunctorEquivalence C n).symm.toAdjunction.homEquiv X Y

lemma opShiftFunctorEquivalence_symm_homEquiv_apply {n : â„¤} {X Y : Cáµ’áµ–}
    (f : Opposite.op (X.unopâŸ¦nâŸ§) âŸ¶ Y) :
    (opShiftFunctorEquivalence_symm_homEquiv n X Y f) =
      (opShiftFunctorEquivalence C n).counitIso.inv.app X â‰« (shiftFunctor Cáµ’áµ– n).map f := rfl

lemma opShiftFunctorEquivalence_symm_homEquiv_left_inv
    {n : â„¤} {X Y : Cáµ’áµ–} (f : Opposite.op (X.unopâŸ¦nâŸ§) âŸ¶ Y) :
    ((opShiftFunctorEquivalence C n).unitIso.inv.app Y).unop â‰«
      (opShiftFunctorEquivalence_symm_homEquiv n X Y f).unopâŸ¦nâŸ§' = f.unop :=
  Quiver.Hom.op_inj ((opShiftFunctorEquivalence_symm_homEquiv n X Y).left_inv f)

variable (C)

namespace TriangleOpEquivalence

/-- The functor which sends a triangle `X âŸ¶ Y âŸ¶ Z âŸ¶ XâŸ¦1âŸ§` in `C` to the triangle
`op Z âŸ¶ op Y âŸ¶ op X âŸ¶ (op Z)âŸ¦1âŸ§` in `Cáµ’áµ–` (without introducing signs). -/
@[simps]
noncomputable def functor : (Triangle C)áµ’áµ– â¥¤ Triangle Cáµ’áµ– where
  obj T := Triangle.mk T.unop.morâ‚‚.op T.unop.morâ‚.op
      ((opShiftFunctorEquivalence C 1).counitIso.inv.app (Opposite.op T.unop.objâ‚) â‰«
        T.unop.morâ‚ƒ.opâŸ¦(1 : â„¤)âŸ§')
  map {Tâ‚ Tâ‚‚} Ï† :=
    { homâ‚ := Ï†.unop.homâ‚ƒ.op
      homâ‚‚ := Ï†.unop.homâ‚‚.op
      homâ‚ƒ := Ï†.unop.homâ‚.op
      commâ‚ := Quiver.Hom.unop_inj Ï†.unop.commâ‚‚.symm
      commâ‚‚ := Quiver.Hom.unop_inj Ï†.unop.commâ‚.symm
      commâ‚ƒ := by
        dsimp
        rw [assoc, â† Functor.map_comp, â† op_comp, â† Ï†.unop.commâ‚ƒ, op_comp, Functor.map_comp,
          opShiftFunctorEquivalence_counitIso_inv_naturality_assoc]
        rfl }

/-- The functor which sends a triangle `X âŸ¶ Y âŸ¶ Z âŸ¶ XâŸ¦1âŸ§` in `Cáµ’áµ–` to the triangle
`Z.unop âŸ¶ Y.unop âŸ¶ X.unop âŸ¶ Z.unopâŸ¦1âŸ§` in `C` (without introducing signs). -/
@[simps]
noncomputable def inverse : Triangle Cáµ’áµ– â¥¤ (Triangle C)áµ’áµ– where
  obj T := Opposite.op (Triangle.mk T.morâ‚‚.unop T.morâ‚.unop
      (((opShiftFunctorEquivalence C 1).unitIso.inv.app T.objâ‚).unop â‰« T.morâ‚ƒ.unopâŸ¦(1 : â„¤)âŸ§'))
  map {Tâ‚ Tâ‚‚} Ï† := Quiver.Hom.op
    { homâ‚ := Ï†.homâ‚ƒ.unop
      homâ‚‚ := Ï†.homâ‚‚.unop
      homâ‚ƒ := Ï†.homâ‚.unop
      commâ‚ := Quiver.Hom.op_inj Ï†.commâ‚‚.symm
      commâ‚‚ := Quiver.Hom.op_inj Ï†.commâ‚.symm
      commâ‚ƒ := Quiver.Hom.op_inj (by
        dsimp
        rw [assoc, â† opShiftFunctorEquivalence_unitIso_inv_naturality,
          â† op_comp_assoc, â† Functor.map_comp, â† unop_comp, â† Ï†.commâ‚ƒ,
          unop_comp, Functor.map_comp, op_comp, assoc]) }

/-- The unit isomorphism of the
equivalence `triangleOpEquivalence C : (Triangle C)áµ’áµ– â‰Œ Triangle Cáµ’áµ–` .-/
@[simps!]
noncomputable def unitIso : ðŸ­ _ â‰… functor C â‹™ inverse C :=
  NatIso.ofComponents (fun T => Iso.op
    (Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) (Iso.refl _) (by aesop_cat) (by aesop_cat)
      (Quiver.Hom.op_inj
        (by simp [shift_unop_opShiftFunctorEquivalence_counitIso_inv_app]))))
    (fun {Tâ‚ Tâ‚‚} f => Quiver.Hom.unop_inj (by aesop_cat))

/-- The counit isomorphism of the
equivalence `triangleOpEquivalence C : (Triangle C)áµ’áµ– â‰Œ Triangle Cáµ’áµ–` .-/
@[simps!]
noncomputable def counitIso : inverse C â‹™ functor C â‰… ðŸ­ _ :=
  NatIso.ofComponents (fun T => by
    refine' Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) (Iso.refl _) _ _ _
    Â· aesop_cat
    Â· aesop_cat
    Â· dsimp
      rw [Functor.map_id, comp_id, id_comp, Functor.map_comp,
        â† opShiftFunctorEquivalence_counitIso_inv_naturality_assoc,
        opShiftFunctorEquivalence_counitIso_inv_app_shift, â† Functor.map_comp,
        Iso.hom_inv_id_app, Functor.map_id]
      simp only [Functor.id_obj, comp_id])
    (by aesop_cat)

end TriangleOpEquivalence

/-- An anti-equivalence between the categories of triangles in `C` and in `Cáµ’áµ–`.
A triangle in `Cáµ’áµ–` shall be distinguished iff it correspond to a distinguished
triangle in `C` via this equivalence. -/
@[simps]
noncomputable def triangleOpEquivalence :
    (Triangle C)áµ’áµ– â‰Œ Triangle Cáµ’áµ– where
  functor := TriangleOpEquivalence.functor C
  inverse := TriangleOpEquivalence.inverse C
  unitIso := TriangleOpEquivalence.unitIso C
  counitIso := TriangleOpEquivalence.counitIso C

variable [HasZeroObject C] [Preadditive C] [âˆ€ (n : â„¤), (shiftFunctor C n).Additive]
  [Pretriangulated C]

namespace Opposite

/-- A triangle in `Cáµ’áµ–` shall be distinguished iff it corresponds to a distinguished
triangle in `C` via the equivalence `triangleOpEquivalence C : (Triangle C)áµ’áµ– â‰Œ Triangle Cáµ’áµ–`. -/
def distinguishedTriangles : Set (Triangle Cáµ’áµ–) :=
  fun T => ((triangleOpEquivalence C).inverse.obj T).unop âˆˆ distTriang C

variable {C}

lemma mem_distinguishedTriangles_iff (T : Triangle Cáµ’áµ–) :
    T âˆˆ distinguishedTriangles C â†”
      ((triangleOpEquivalence C).inverse.obj T).unop âˆˆ distTriang C := by
  rfl

lemma mem_distinguishedTriangles_iff' (T : Triangle Cáµ’áµ–) :
    T âˆˆ distinguishedTriangles C â†”
      âˆƒ (T' : Triangle C) (_ : T' âˆˆ distTriang C),
        Nonempty (T â‰… (triangleOpEquivalence C).functor.obj (Opposite.op T')) := by
  rw [mem_distinguishedTriangles_iff]
  constructor
  Â· intro hT
    exact âŸ¨_ ,hT, âŸ¨(triangleOpEquivalence C).counitIso.symm.app TâŸ©âŸ©
  Â· rintro âŸ¨T', hT', âŸ¨eâŸ©âŸ©
    refine' isomorphic_distinguished _ hT' _ _
    exact Iso.unop ((triangleOpEquivalence C).unitIso.app (Opposite.op T') â‰ªâ‰«
      (triangleOpEquivalence C).inverse.mapIso e.symm)

lemma isomorphic_distinguished (Tâ‚ : Triangle Cáµ’áµ–)
    (hTâ‚ : Tâ‚ âˆˆ distinguishedTriangles C) (Tâ‚‚ : Triangle Cáµ’áµ–)
    (e : Tâ‚‚ â‰… Tâ‚) :
    Tâ‚‚ âˆˆ distinguishedTriangles C := by
  simp only [mem_distinguishedTriangles_iff] at hTâ‚ âŠ¢
  exact Pretriangulated.isomorphic_distinguished _ hTâ‚ _
    ((triangleOpEquivalence C).inverse.mapIso e).unop.symm

/-- Up to rotation, the contractible triangle `X âŸ¶ X âŸ¶ 0 âŸ¶ XâŸ¦1âŸ§` for `X : Cáµ’áµ–` corresponds
to the contractible triangle for `X.unop` in `C`. -/
@[simps!]
noncomputable def contractibleTriangleIso (X : Cáµ’áµ–) :
    contractibleTriangle X â‰… (triangleOpEquivalence C).functor.obj
      (Opposite.op (contractibleTriangle X.unop).invRotate) :=
  Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _)
    (IsZero.iso (isZero_zero _) (by
      dsimp
      rw [IsZero.iff_id_eq_zero]
      change (ðŸ™ ((0 : C)âŸ¦(-1 : â„¤)âŸ§)).op = 0
      rw [â† Functor.map_id, id_zero, Functor.map_zero, op_zero]))
    (by aesop_cat) (by aesop_cat) (by aesop_cat)

lemma contractible_distinguished (X : Cáµ’áµ–) :
    contractibleTriangle X âˆˆ distinguishedTriangles C := by
  rw [mem_distinguishedTriangles_iff']
  exact âŸ¨_, inv_rot_of_distTriang _ (Pretriangulated.contractible_distinguished X.unop),
    âŸ¨contractibleTriangleIso XâŸ©âŸ©

/-- Isomorphism expressing a compatibility of the equivalence `triangleOpEquivalence C`
with the rotation of triangles. -/
noncomputable def rotateTriangleOpEquivalenceInverseObjRotateUnopIso (T : Triangle Cáµ’áµ–) :
    ((triangleOpEquivalence C).inverse.obj T.rotate).unop.rotate â‰…
      ((triangleOpEquivalence C).inverse.obj T).unop :=
  Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _)
      (-((opShiftFunctorEquivalence C 1).unitIso.app T.objâ‚).unop) (by simp)
        (Quiver.Hom.op_inj (by aesop_cat)) (by aesop_cat)

lemma rotate_distinguished_triangle (T : Triangle Cáµ’áµ–) :
    T âˆˆ distinguishedTriangles C â†” T.rotate âˆˆ distinguishedTriangles C := by
  simp only [mem_distinguishedTriangles_iff, Pretriangulated.rotate_distinguished_triangle
    ((triangleOpEquivalence C).inverse.obj (T.rotate)).unop]
  exact distinguished_iff_of_iso (rotateTriangleOpEquivalenceInverseObjRotateUnopIso T).symm

lemma distinguished_cocone_triangle {X Y : Cáµ’áµ–} (f : X âŸ¶ Y) :
    âˆƒ (Z : Cáµ’áµ–) (g : Y âŸ¶ Z) (h : Z âŸ¶ XâŸ¦(1 : â„¤)âŸ§),
      Triangle.mk f g h âˆˆ distinguishedTriangles C := by
  obtain âŸ¨Z, g, h, HâŸ© := Pretriangulated.distinguished_cocone_triangleâ‚ f.unop
  refine' âŸ¨_, g.op, (opShiftFunctorEquivalence C 1).counitIso.inv.app (Opposite.op Z) â‰«
    (shiftFunctor Cáµ’áµ– (1 : â„¤)).map h.op, _âŸ©
  simp only [mem_distinguishedTriangles_iff]
  refine' Pretriangulated.isomorphic_distinguished _ H _ _
  exact Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) (Iso.refl _) (by aesop_cat) (by aesop_cat)
    (Quiver.Hom.op_inj (by simp [shift_unop_opShiftFunctorEquivalence_counitIso_inv_app]))

lemma complete_distinguished_triangle_morphism (Tâ‚ Tâ‚‚ : Triangle Cáµ’áµ–)
    (hTâ‚ : Tâ‚ âˆˆ distinguishedTriangles C) (hTâ‚‚ : Tâ‚‚ âˆˆ distinguishedTriangles C)
    (a : Tâ‚.objâ‚ âŸ¶ Tâ‚‚.objâ‚) (b : Tâ‚.objâ‚‚ âŸ¶ Tâ‚‚.objâ‚‚) (comm : Tâ‚.morâ‚ â‰« b = a â‰« Tâ‚‚.morâ‚) :
    âˆƒ (c : Tâ‚.objâ‚ƒ âŸ¶ Tâ‚‚.objâ‚ƒ), Tâ‚.morâ‚‚ â‰« c = b â‰« Tâ‚‚.morâ‚‚ âˆ§
      Tâ‚.morâ‚ƒ â‰« aâŸ¦1âŸ§' = c â‰« Tâ‚‚.morâ‚ƒ := by
  rw [mem_distinguishedTriangles_iff] at hTâ‚ hTâ‚‚
  obtain âŸ¨c, hcâ‚, hcâ‚‚âŸ© :=
    Pretriangulated.complete_distinguished_triangle_morphismâ‚ _ _ hTâ‚‚ hTâ‚
      b.unop a.unop (Quiver.Hom.op_inj comm.symm)
  dsimp at c hcâ‚ hcâ‚‚
  replace hcâ‚‚ := ((opShiftFunctorEquivalence C 1).unitIso.hom.app Tâ‚‚.objâ‚).unop â‰«= hcâ‚‚
  dsimp at hcâ‚‚
  simp only [assoc, Iso.unop_hom_inv_id_app_assoc] at hcâ‚‚
  refine' âŸ¨c.op, Quiver.Hom.unop_inj hcâ‚.symm, Quiver.Hom.unop_inj _âŸ©
  apply (shiftFunctor C (1 : â„¤)).map_injective
  rw [unop_comp, unop_comp, Functor.map_comp, Functor.map_comp,
    Quiver.Hom.unop_op, hcâ‚‚, â† unop_comp_assoc, â† unop_comp_assoc,
    â† opShiftFunctorEquivalence_unitIso_inv_naturality]
  simp

/-- The pretriangulated structure on the opposite category of
a pretriangulated category. It is a scoped instance, so that we need to
`open CategoryTheory.Pretriangulated.Opposite` in order to be able
to use it: the reason is that it relies on the definition of the shift
on the opposite category `Cáµ’áµ–`, for which it is unclear whether it should
be a global instance or not. -/
scoped instance : Pretriangulated Cáµ’áµ– where
  distinguishedTriangles := distinguishedTriangles C
  isomorphic_distinguished := isomorphic_distinguished
  contractible_distinguished := contractible_distinguished
  distinguished_cocone_triangle := distinguished_cocone_triangle
  rotate_distinguished_triangle := rotate_distinguished_triangle
  complete_distinguished_triangle_morphism := complete_distinguished_triangle_morphism

end Opposite

variable {C}

lemma mem_distTriang_op_iff (T : Triangle Cáµ’áµ–) :
    (T âˆˆ distTriang Cáµ’áµ–) â†” ((triangleOpEquivalence C).inverse.obj T).unop âˆˆ distTriang C := by
  rfl

lemma mem_distTriang_op_iff' (T : Triangle Cáµ’áµ–) :
    (T âˆˆ distTriang Cáµ’áµ–) â†” âˆƒ (T' : Triangle C) (_ : T' âˆˆ distTriang C),
      Nonempty (T â‰… (triangleOpEquivalence C).functor.obj (Opposite.op T')) :=
  Opposite.mem_distinguishedTriangles_iff' T

lemma op_distinguished (T : Triangle C) (hT : T âˆˆ distTriang C) :
    ((triangleOpEquivalence C).functor.obj (Opposite.op T)) âˆˆ distTriang Cáµ’áµ– := by
  rw [mem_distTriang_op_iff']
  exact âŸ¨T, hT, âŸ¨Iso.refl _âŸ©âŸ©

lemma unop_distinguished (T : Triangle Cáµ’áµ–) (hT : T âˆˆ distTriang Cáµ’áµ–) :
    ((triangleOpEquivalence C).inverse.obj T).unop âˆˆ distTriang C := hT

lemma distinguished_iff_op (T : Triangle C) :
    (T âˆˆ distTriang C) â†”
      ((triangleOpEquivalence C).functor.obj (Opposite.op T)) âˆˆ distTriang Cáµ’áµ– := by
  constructor
  Â· intro hT
    exact op_distinguished _ hT
  Â· intro hT'
    exact isomorphic_distinguished _ (unop_distinguished _ hT') _
      (((triangleOpEquivalence C).unitIso.app (Opposite.op T)).unop.symm)

namespace Opposite

set_option maxHeartbeats 400000 in
scoped instance [IsTriangulated C] : IsTriangulated Cáµ’áµ– := by
  have : âˆ€ â¦ƒXâ‚ Xâ‚‚ Xâ‚ƒ : Câ¦„ (uâ‚â‚‚ : Xâ‚ âŸ¶ Xâ‚‚) (uâ‚‚â‚ƒ : Xâ‚‚ âŸ¶ Xâ‚ƒ),
    âˆƒ (Zâ‚â‚‚ Zâ‚‚â‚ƒ Zâ‚â‚ƒ : C)
      (vâ‚â‚‚ : Zâ‚â‚‚ âŸ¶ Xâ‚) (wâ‚â‚‚ : Xâ‚‚ âŸ¶ Zâ‚â‚‚âŸ¦(1 : â„¤)âŸ§) (hâ‚â‚‚ : Triangle.mk vâ‚â‚‚ uâ‚â‚‚ wâ‚â‚‚ âˆˆ distTriang C)
      (vâ‚‚â‚ƒ : Zâ‚‚â‚ƒ âŸ¶ Xâ‚‚) (wâ‚‚â‚ƒ : Xâ‚ƒ âŸ¶ Zâ‚‚â‚ƒâŸ¦(1 : â„¤)âŸ§) (hâ‚‚â‚ƒ : Triangle.mk vâ‚‚â‚ƒ uâ‚‚â‚ƒ wâ‚‚â‚ƒ âˆˆ distTriang C)
      (vâ‚â‚ƒ : Zâ‚â‚ƒ âŸ¶ Xâ‚) (wâ‚â‚ƒ : Xâ‚ƒ âŸ¶ Zâ‚â‚ƒâŸ¦(1 : â„¤)âŸ§)
        (hâ‚â‚ƒ : Triangle.mk vâ‚â‚ƒ (uâ‚â‚‚ â‰« uâ‚‚â‚ƒ) wâ‚â‚ƒ âˆˆ distTriang C),
        Nonempty (Triangulated.Octahedron rfl (rot_of_distTriang _ hâ‚â‚‚)
          (rot_of_distTriang _ hâ‚‚â‚ƒ) (rot_of_distTriang _ hâ‚â‚ƒ)) := by
    intro Xâ‚ Xâ‚‚ Xâ‚ƒ uâ‚â‚‚ uâ‚‚â‚ƒ
    obtain âŸ¨Zâ‚â‚‚, vâ‚â‚‚, wâ‚â‚‚, hâ‚â‚‚âŸ© := distinguished_cocone_triangleâ‚ uâ‚â‚‚
    obtain âŸ¨Zâ‚‚â‚ƒ, vâ‚‚â‚ƒ, wâ‚‚â‚ƒ, hâ‚‚â‚ƒâŸ© := distinguished_cocone_triangleâ‚ uâ‚‚â‚ƒ
    obtain âŸ¨Zâ‚â‚ƒ, vâ‚â‚ƒ, wâ‚â‚ƒ, hâ‚â‚ƒâŸ© := distinguished_cocone_triangleâ‚ (uâ‚â‚‚ â‰« uâ‚‚â‚ƒ)
    exact âŸ¨_, _, _, _, _, hâ‚â‚‚, _, _, hâ‚‚â‚ƒ, _, _, hâ‚â‚ƒ, âŸ¨Triangulated.someOctahedron _ _ _ _âŸ©âŸ©
  apply IsTriangulated.mk'
  intros Xâ‚ Xâ‚‚ Xâ‚ƒ uâ‚â‚‚ uâ‚‚â‚ƒ
  obtain âŸ¨Zâ‚â‚‚, Zâ‚‚â‚ƒ, Zâ‚â‚ƒ, vâ‚â‚‚, wâ‚â‚‚, hâ‚â‚‚, vâ‚‚â‚ƒ, wâ‚‚â‚ƒ, hâ‚‚â‚ƒ, vâ‚â‚ƒ, wâ‚â‚ƒ, hâ‚â‚ƒ, âŸ¨HâŸ©âŸ© :=
    this uâ‚‚â‚ƒ.unop uâ‚â‚‚.unop
  refine' âŸ¨Xâ‚, Xâ‚‚, Xâ‚ƒ, _, _, _, uâ‚â‚‚, uâ‚‚â‚ƒ, Iso.refl _, Iso.refl _, Iso.refl _, by simp, by simp,
    vâ‚‚â‚ƒ.op, opShiftFunctorEquivalence_symm_homEquiv 1 _ _ wâ‚‚â‚ƒ.op, _,
    vâ‚â‚‚.op, opShiftFunctorEquivalence_symm_homEquiv 1 _ _ wâ‚â‚‚.op, _,
    vâ‚â‚ƒ.op, opShiftFunctorEquivalence_symm_homEquiv 1 _ _ wâ‚â‚ƒ.op, _, _âŸ©
  Â· rw [mem_distTriang_op_iff]
    refine' Pretriangulated.isomorphic_distinguished _ hâ‚‚â‚ƒ _ _
    refine' Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) (Iso.refl _) (by simp) (by simp) _
    simpa using opShiftFunctorEquivalence_symm_homEquiv_left_inv wâ‚‚â‚ƒ.op
  Â· rw [mem_distTriang_op_iff]
    refine' Pretriangulated.isomorphic_distinguished _ hâ‚â‚‚ _ _
    refine' Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) (Iso.refl _) (by simp) (by simp) _
    simpa using opShiftFunctorEquivalence_symm_homEquiv_left_inv wâ‚â‚‚.op
  Â· rw [mem_distTriang_op_iff]
    refine' Pretriangulated.isomorphic_distinguished _ hâ‚â‚ƒ _ _
    refine' Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) (Iso.refl _) (by simp) (by simp) _
    simpa using opShiftFunctorEquivalence_symm_homEquiv_left_inv wâ‚â‚ƒ.op
  Â· obtain âŸ¨mâ‚, hmâ‚âŸ© := (shiftFunctor C (1 : â„¤)).map_surjective H.mâ‚ƒ
    obtain âŸ¨mâ‚ƒ, hmâ‚ƒâŸ© := (shiftFunctor C (1 : â„¤)).map_surjective H.mâ‚
    exact
     âŸ¨{ mâ‚ := mâ‚.op
        mâ‚ƒ := mâ‚ƒ.op
        commâ‚ := by
          apply Quiver.Hom.unop_inj
          apply (shiftFunctor C (1 : â„¤)).map_injective
          simpa [â† hmâ‚] using H.commâ‚„.symm
        commâ‚‚ := by
          have eq := H.commâ‚ƒ
          dsimp at eq
          rw [â† eq, â† hmâ‚, op_comp, opShiftFunctorEquivalence_symm_homEquiv_apply,
            opShiftFunctorEquivalence_symm_homEquiv_apply]
          simp only [Functor.id_obj, opShiftFunctorEquivalence_inverse,
            opShiftFunctorEquivalence_functor,
            Functor.comp_obj, Functor.op_obj, Functor.map_comp]
          erw [â† NatTrans.naturality_assoc]
          rfl
        commâ‚ƒ := by
          apply Quiver.Hom.unop_inj
          apply (shiftFunctor C (1 : â„¤)).map_injective
          simpa [â† hmâ‚ƒ] using H.commâ‚‚
        commâ‚„ := by
          have eq := congr_arg Quiver.Hom.op H.commâ‚
          dsimp at eq
          simp only [Opposite.op_unop, Triangle.mk_objâ‚]
          rw [opShiftFunctorEquivalence_symm_homEquiv_apply,
            opShiftFunctorEquivalence_symm_homEquiv_apply, assoc, â† Functor.map_comp,
            â† eq, â† hmâ‚ƒ, Functor.map_comp]
          erw [â† NatTrans.naturality_assoc]
          rfl
        mem := by
          rw [â† Triangle.shift_distinguished_iff _ (-1), mem_distTriang_op_iff']
          refine' âŸ¨_, H.mem, âŸ¨_âŸ©âŸ©
          refine' Triangle.isoMk _ _
            ((shiftFunctorOpIso C _ _ (neg_add_self 1)).app _)
            (-((shiftFunctorOpIso C _ _ (neg_add_self 1)).app _))
            ((shiftFunctorOpIso C _ _ (neg_add_self 1)).app _) _ _ _
          Â· dsimp
            simp [â† hmâ‚]
          Â· dsimp
            simp [â† hmâ‚ƒ]
          Â· dsimp
            simp only [Int.negOnePow_neg, Int.negOnePow_one, Functor.map_comp, assoc,
              neg_smul, one_smul, neg_comp, comp_neg, Functor.map_neg, neg_inj]
            erw [(shiftFunctorComm Cáµ’áµ– 1 (-1)).hom.naturality_assoc vâ‚‚â‚ƒ.op]
            dsimp
            rw [shiftFunctor_op_map _ _ (neg_add_self 1) vâ‚‚â‚ƒ.op]
            rw [opShiftFunctorEquivalence_symm_homEquiv_apply]
            simp
            nth_rewrite 1 [â† Functor.map_comp]
            rw [Iso.inv_hom_id_app]
            simp
            have eq := (shiftFunctorComm Cáµ’áµ– 1 (-1)).hom.naturality wâ‚â‚‚.op
            dsimp at eq
            rw [reassoc_of% eq]
            rw [shiftFunctor_op_map _ _ (neg_add_self 1) wâ‚â‚‚.op]
            simp only [â† Functor.map_comp_assoc, â† Functor.map_comp, assoc]
            erw [Iso.inv_hom_id_app_assoc]
            simp only [Functor.op_obj, Opposite.unop_op, Opposite.op_unop, Quiver.Hom.unop_op, Functor.map_comp, â† assoc]
            congr 2
            simp only [assoc]
            rw [shiftFunctorComm_hom_app_of_add_eq_zero _ _ (add_neg_self 1)]
            simp only [Functor.comp_obj, Functor.id_obj, assoc]
            rw [shiftFunctorCompIsoId_op_hom_app]
            rw [shiftFunctorCompIsoId_op_inv_app]
            simp only [shiftFunctor_op_map _ _ (neg_add_self 1)]
            simp only [shiftFunctor_op_map _ _ (add_neg_self 1)]
            simp
            rw [opShiftFunctorEquivalence_counitIso_inv_app _ _ _ (add_neg_self 1)]
            rw [opShiftFunctorEquivalence_counitIso_inv_app _ _ _ (add_neg_self 1)]
            simp only [Functor.id_obj, Functor.comp_obj, unop_comp, Opposite.unop_op, Quiver.Hom.unop_op,
              Functor.map_comp, op_comp, assoc]
            simp only [â† op_comp, â† op_comp_assoc, assoc, â† Functor.map_comp, â† Functor.map_comp_assoc,
              â† unop_comp, â† unop_comp_assoc]
            rw [Iso.inv_hom_id_app]
            rw [Iso.inv_hom_id_app]
            simp only [Functor.op_obj, Opposite.unop_op, unop_id, Functor.map_id, id_comp, op_comp, assoc]
            simp only [â† assoc];congr 1; simp only [assoc]
            rw [shift_shiftFunctorCompIsoId_add_neg_self_hom_app]
            simp only [â† op_comp_assoc, â† op_comp, assoc, Iso.inv_hom_id_app, Functor.id_obj, comp_id]
            simp
            rw [â† op_comp]
            erw [(shiftFunctorCompIsoId C (1 : â„¤) (-1) (by linarith)).hom.naturality]
            rfl }âŸ©

variable (C)

namespace OpOpCommShift

noncomputable def iso (n : â„¤) :
    shiftFunctor C n â‹™ opOp C â‰… opOp C â‹™ shiftFunctor Cáµ’áµ–áµ’áµ– n :=
  NatIso.ofComponents
    (fun X => ((shiftFunctorOpIso C _ _ (neg_add_self n)).app (Opposite.op X)).op â‰ªâ‰«
      (shiftFunctorOpIso Cáµ’áµ– _ _ (add_neg_self n)).symm.app (Opposite.op (Opposite.op X))) (by
      intros X Y f
      dsimp
      rw [assoc, â† (shiftFunctorOpIso Cáµ’áµ– _ _ (add_neg_self n)).inv.naturality f.op.op]
      dsimp
      rw [â† op_comp_assoc]
      erw [â† (shiftFunctorOpIso C _ _ (neg_add_self n)).hom.naturality f.op]
      rw [op_comp, assoc])

variable {C}

lemma iso_hom_app (X : C) (n m : â„¤) (hnm : n + m = 0) :
    (iso C n).hom.app X =
      ((shiftFunctorOpIso C m n (by linarith)).hom.app (Opposite.op X)).op â‰«
        (shiftFunctorOpIso Cáµ’áµ– _ _ hnm).inv.app (Opposite.op (Opposite.op X)) := by
  obtain rfl : m = -n := by linarith
  rfl

lemma iso_inv_app (X : C) (n m : â„¤) (hnm : n + m = 0) :
    (iso C n).inv.app X =
      (shiftFunctorOpIso Cáµ’áµ– _ _ hnm).hom.app (Opposite.op (Opposite.op X)) â‰«
        ((shiftFunctorOpIso C m n (by linarith)).inv.app (Opposite.op X)).op := by
  obtain rfl : m = -n := by linarith
  rfl

end OpOpCommShift

noncomputable instance : (opOp C).CommShift â„¤ where
  iso n := OpOpCommShift.iso C n
  zero := by
    ext X
    dsimp
    rw [OpOpCommShift.iso_hom_app _ 0 0 (zero_add 0)]
    dsimp
    simp only [Functor.CommShift.isoZero_hom_app, unopUnop_obj, unopUnop_map]
    rw [shiftFunctorZero_op_inv_app, shiftFunctorZero_op_hom_app]
    dsimp
    rw [assoc, â† op_comp_assoc, â† op_comp, Iso.hom_inv_id_app, op_id, op_id, id_comp]
  add a b := by
    ext X
    dsimp
    simp only [Functor.CommShift.isoAdd_hom_app, opOp_obj, Functor.comp_obj, opOp_map,
      OpOpCommShift.iso_hom_app X _ _ (add_neg_self (a + b)),
      OpOpCommShift.iso_hom_app _ _ _ (add_neg_self a),
      OpOpCommShift.iso_hom_app _ _ _ (add_neg_self b),
      shiftFunctor_op_map _ _ (add_neg_self b),
      shiftFunctor_op_map _ _ (neg_add_self b), assoc,
      â† shiftFunctorAdd'_eq_shiftFunctorAdd,
      shiftFunctorAdd'_op_inv_app (Opposite.op (Opposite.op X))
      a b (a + b) rfl _ _ _ (add_neg_self a) (add_neg_self b)
      (add_neg_self (a+b))]
    simp only [Functor.op_obj, Opposite.unop_op, unop_comp, Quiver.Hom.unop_op,
      Functor.map_comp, op_comp, assoc, Iso.inv_hom_id_app_assoc,
      Iso.op_hom_inv_id_app_assoc]
    simp only [â† op_comp_assoc, â† op_comp, assoc, â† Functor.map_comp, â† unop_comp, Iso.inv_hom_id_app]
    simp only [Functor.op_obj, Opposite.unop_op, unop_id, id_comp, op_comp, assoc]
    rw [shiftFunctorAdd'_op_hom_app (Opposite.op X) (-a) (-b) (-(a+b)) (by linarith)
      _ _ _ (neg_add_self a) (neg_add_self b) (neg_add_self (a + b))]
    simp only [Functor.op_obj, Opposite.unop_op, Functor.comp_obj, op_comp, assoc]
    simp only [â† op_comp_assoc, â† op_comp, assoc]
    erw [â† NatTrans.naturality_assoc, Iso.inv_hom_id_app_assoc]
    simp only [Functor.op_obj, Functor.op_map, op_comp, assoc]
    simp only [â† op_comp_assoc, assoc, â† Functor.map_comp_assoc, â† unop_comp,
      Iso.inv_hom_id_app]
    simp only [Functor.op_obj, Opposite.unop_op, unop_id_op, Functor.map_id, id_comp,
      Iso.op_inv_hom_id_app, comp_id]

variable {C}

lemma opOp_commShiftIso_hom_app (X : C) (n m : â„¤) (hnm : n + m = 0) :
    ((opOp C).commShiftIso n).hom.app X =
      ((shiftFunctorOpIso C m n (by linarith)).hom.app (Opposite.op X)).op â‰«
        (shiftFunctorOpIso Cáµ’áµ– _ _ hnm).inv.app (Opposite.op (Opposite.op X)) :=
  OpOpCommShift.iso_hom_app _ _ _ hnm

lemma opOp_commShiftIso_inv_app (X : C) (n m : â„¤) (hnm : n + m = 0) :
    ((opOp C).commShiftIso n).inv.app X =
      (shiftFunctorOpIso Cáµ’áµ– _ _ hnm).hom.app (Opposite.op (Opposite.op X)) â‰«
        ((shiftFunctorOpIso C m n (by linarith)).inv.app (Opposite.op X)).op :=
  OpOpCommShift.iso_inv_app _ _ _ hnm

instance : (opOp C).IsTriangulated where
  map_distinguished T hT := by
    rw [mem_distTriang_op_iff']
    refine' âŸ¨_, op_distinguished T hT, âŸ¨_âŸ©âŸ©
    refine' Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) (Iso.refl _) _ _ _
    Â· aesop_cat
    Â· aesop_cat
    Â· dsimp
      simp only [Functor.map_id, comp_id, id_comp,
        opOp_commShiftIso_hom_app T.objâ‚ _ _ (add_neg_self 1),
        opShiftFunctorEquivalence_counitIso_inv_app _ _ _ (add_neg_self 1),
        shiftFunctorCompIsoId_op_hom_app _ _ _ (add_neg_self 1),
        shiftFunctor_op_map _ _ (add_neg_self 1),
        shiftFunctor_op_map _ _ (neg_add_self 1)]
      simp only [Functor.op_obj, Opposite.unop_op, unop_id, Functor.map_id, op_id, id_comp, Iso.hom_inv_id_app, comp_id,
        Functor.id_obj, Functor.comp_obj, assoc, Iso.inv_hom_id_app_assoc, op_comp, Quiver.Hom.unop_op,
        Iso.op_hom_inv_id_app_assoc, unop_comp, Functor.map_comp]
      slice_rhs 2 3 =>
        rw [â† op_comp, â† op_comp, â† Functor.map_comp, â† unop_comp, Iso.inv_hom_id_app]
      simp only [Functor.op_obj, Opposite.unop_op, unop_id, Functor.map_id, op_id, id_comp, assoc]
      slice_rhs 1 2 =>
        rw [â† op_comp, â† op_comp]
        erw [â† NatTrans.naturality]
      dsimp
      simp only [assoc, shift_shiftFunctorCompIsoId_add_neg_self_hom_app]
      slice_rhs 2 3 =>
        rw [â† op_comp, â† op_comp, Iso.inv_hom_id_app]
      simp

noncomputable instance : (opOpEquivalence C).inverse.CommShift â„¤ :=
  (inferInstance : (opOp C).CommShift â„¤)

noncomputable instance : (opOpEquivalence C).functor.CommShift â„¤ :=
  (opOpEquivalence C).commShiftFunctor â„¤

noncomputable instance : (unopUnop C).CommShift â„¤ :=
  (inferInstance : (opOpEquivalence C).functor.CommShift â„¤)

instance : (opOpEquivalence C).CommShift â„¤ := (opOpEquivalence C).commShift_of_inverse â„¤

instance : (opOpEquivalence C).IsTriangulated :=
  Equivalence.IsTriangulated.mk'' _ (inferInstance : (opOp C).IsTriangulated)

instance : (opOp C).IsTriangulated := inferInstance

instance : (unopUnop C).IsTriangulated :=
  (inferInstance : (opOpEquivalence C).functor.IsTriangulated)

end Opposite

section

variable {J : Type*} (T : J â†’ Triangle C)
  [HasCoproduct (fun j => (T j).objâ‚)] [HasCoproduct (fun j => (T j).objâ‚‚)]
  [HasCoproduct (fun j => (T j).objâ‚ƒ)]
  [HasCoproduct (fun j => (T j).objâ‚âŸ¦(1 : â„¤)âŸ§)]

@[simps!]
noncomputable def coproductTriangle : Triangle C :=
  Triangle.mk (Limits.Sigma.map (fun j => (T j).morâ‚))
    (Limits.Sigma.map (fun j => (T j).morâ‚‚))
    (Limits.Sigma.map (fun j => (T j).morâ‚ƒ) â‰« sigmaComparison _ _)

noncomputable def opCoproductTriangleIsoProductTriangle
  [HasProduct (fun j => ((triangleOpEquivalence C).functor.obj (Opposite.op (T j))).objâ‚)]
  [HasProduct (fun j => ((triangleOpEquivalence C).functor.obj (Opposite.op (T j))).objâ‚‚)]
  [HasProduct (fun j => ((triangleOpEquivalence C).functor.obj (Opposite.op (T j))).objâ‚ƒ)]
  [HasProduct (fun j => (((triangleOpEquivalence C).functor.obj
    (Opposite.op (T j))).objâ‚)âŸ¦(1 : â„¤)âŸ§)] :
    productTriangle (fun j => (triangleOpEquivalence C).functor.obj (Opposite.op (T j))) â‰…
    (triangleOpEquivalence C).functor.obj (Opposite.op (coproductTriangle T)) :=
  Triangle.isoMk _ _ (opCoproductIsoProduct (fun j => (T j).objâ‚ƒ)).symm
    (opCoproductIsoProduct (fun j => (T j).objâ‚‚)).symm
    (opCoproductIsoProduct (fun j => (T j).objâ‚)).symm (by
      dsimp [productTriangle]
      simp only [opCoproductIsoProduct_inv_comp_map]) (by
      dsimp [productTriangle]
      simp only [opCoproductIsoProduct_inv_comp_map]) (by
      dsimp [productTriangle]
      have : HasProduct (fun j => (shiftFunctor Cáµ’áµ– (1 : â„¤)).obj (Opposite.op (T j).objâ‚ƒ)) :=
        âŸ¨_, isLimitFanMkObjOfIsLimit (shiftFunctor Cáµ’áµ– (1 : â„¤)) _ _
          (productIsProduct (fun j => (Opposite.op (T j).objâ‚ƒ)))âŸ©
      rw [assoc, â† cancel_mono ((shiftFunctor Cáµ’áµ– (1 : â„¤)).map
        (opCoproductIsoProduct (fun j â†¦ (T j).objâ‚ƒ)).hom), assoc, assoc, assoc, assoc,
        â† Functor.map_comp, Iso.inv_hom_id, Functor.map_id, comp_id,
        â† cancel_mono (piComparison (shiftFunctor Cáµ’áµ– (1 : â„¤)) (fun j â†¦ Opposite.op (T j).objâ‚ƒ)),
        assoc, assoc, assoc, assoc, IsIso.inv_hom_id, comp_id]
      ext j
      rw [limMap_Ï€, Discrete.natTrans_app, assoc, assoc, assoc, assoc, piComparison_comp_Ï€,
        â† Functor.map_comp, â† Functor.map_comp, assoc,
        opCoproductIsoProduct_hom_comm_Ï€, â† op_comp_assoc, â† op_comp, Î¹_colimMap_assoc,
        Discrete.natTrans_app, Î¹_comp_sigmaComparison]
      dsimp
      rw [Functor.map_comp]
      erw [â† (opShiftFunctorEquivalence C 1).counitIso.inv.naturality_assoc
        ((Sigma.Î¹ (fun j â†¦ (T j).objâ‚) j).op)]
      dsimp
      rw [opCoproductIsoProduct_inv_comp_Î¹_assoc])

lemma coproductTriangle_distinguished (hT : âˆ€ j, T j âˆˆ distTriang C) :
    coproductTriangle T âˆˆ distTriang C := by
  rw [distinguished_iff_op]
  let T' := fun j => (triangleOpEquivalence C).functor.obj (Opposite.op (T j))
  have : HasProduct (fun j â†¦ (T' j).objâ‚) := by dsimp [triangleOpEquivalence]; infer_instance
  have : HasProduct (fun j â†¦ (T' j).objâ‚‚) := by dsimp [triangleOpEquivalence]; infer_instance
  have : HasProduct (fun j â†¦ (T' j).objâ‚ƒ) := by dsimp [triangleOpEquivalence]; infer_instance
  have : HasProduct (fun j â†¦ ((T' j).objâ‚)âŸ¦(1 : â„¤)âŸ§) :=
    âŸ¨_, isLimitFanMkObjOfIsLimit (shiftFunctor Cáµ’áµ– (1 : â„¤)) _ _
      (productIsProduct (fun j => (T' j).objâ‚))âŸ©
  exact isomorphic_distinguished _
    (productTriangle_distinguished T' (fun j => op_distinguished _ (hT j))) _
    (opCoproductTriangleIsoProductTriangle T).symm

end

end Pretriangulated

end CategoryTheory
