/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Shift.Opposite
import Mathlib.CategoryTheory.Shift.Pullback
import Mathlib.CategoryTheory.Triangulated.Triangulated
import Mathlib.Tactic.Linarith

/-!
# The (pre)triangulated structure on the opposite category

In this file, we shall construct the (pre)triangulated structure
on the opposite category `C·µí·µñ` of a (pre)triangulated category `C`.

The shift on `C·µí·µñ` is obtained by combining the constructions in the files
`CategoryTheory.Shift.Opposite` and `CategoryTheory.Shift.Pullback`.
When the user opens `CategoryTheory.Pretriangulated.Opposite`, the
category `C·µí·µñ` is equipped with the shift by `‚Ñ§` such that
shifting by `n : ‚Ñ§` on `C·µí·µñ` corresponds to the shift by
`-n` on `C`. This is actually a definitional equality, but the user
should not rely on this, and instead use the isomorphism
`shiftFunctorOpIso C n m hnm : shiftFunctor C·µí·µñ n ‚âÖ (shiftFunctor C m).op`
where `hnm : n + m = 0`.

Some compatibilities between the shifts on `C` and `C·µí·µñ` are also expressed through
the equivalence of categories `opShiftFunctorEquivalence C n : C·µí·µñ ‚âå C·µí·µñ` whose
functor is `shiftFunctor C·µí·µñ n` and whose inverse functor is `(shiftFunctor C n).op`.

If `X ‚ü∂ Y ‚ü∂ Z ‚ü∂ X‚ü¶1‚üß` is a distinguished triangle in `C`, then the triangle
`op Z ‚ü∂ op Y ‚ü∂ op X ‚ü∂ (op Z)‚ü¶1‚üß` that is deduced *without introducing signs*
shall be a distinguished triangle in `C·µí·µñ`. This is equivalent to the definition
in [Verdiers's thesis, p. 96][verdier1996] which would require that the triangle
`(op X)‚ü¶-1‚üß ‚ü∂ op Z ‚ü∂ op Y ‚ü∂ op X` (without signs) is *antidistinguished*.

## References
* [Jean-Louis Verdier, *Des cat√©gories d√©riv√©es des cat√©gories ab√©liennes*][verdier1996]

-/

namespace CategoryTheory

open Category Limits Preadditive ZeroObject

variable (C : Type*) [Category C]

section

-- trying to improve automation in this file

variable {C}
variable {D : Type*} [Category D] {F G : C ‚•§ D·µí·µñ} (e : F ‚âÖ G) (X : C)

@[reassoc (attr := simp)]
lemma Iso.unop_hom_inv_id_app : (e.hom.app X).unop ‚â´ (e.inv.app X).unop = ùüô _ := by
  rw [‚Üê unop_comp, inv_hom_id_app, unop_id]

@[reassoc (attr := simp)]
lemma Iso.unop_inv_hom_id_app : (e.inv.app X).unop ‚â´ (e.hom.app X).unop = ùüô _ := by
  rw [‚Üê unop_comp, hom_inv_id_app, unop_id]

end

namespace Pretriangulated

variable [HasShift C ‚Ñ§]

namespace Opposite

/-- As it is unclear whether the opposite category `C·µí·µñ` should always be equipped
with the shift by `‚Ñ§` such that shifting by `n` on `C·µí·µñ` corresponds to shifting
by `-n` on `C`, the user shall have to do `open CategoryTheory.Pretriangulated.Opposite`
in order to get this shift and the (pre)triangulated structure on `C·µí·µñ`. -/

private abbrev OppositeShiftAux :=
  PullbackShift (OppositeShift C ‚Ñ§)
    (AddMonoidHom.mk' (fun (n : ‚Ñ§) => -n) (by intros; dsimp; linarith))

/-- The category `C·µí·µñ` is equipped with the shift such that the shift by `n` on `C·µí·µñ`
corresponds to the shift by `-n` on `C`. -/
noncomputable scoped instance : HasShift C·µí·µñ ‚Ñ§ :=
  (inferInstance : HasShift (OppositeShiftAux C) ‚Ñ§)

instance [Preadditive C] [‚àÄ (n : ‚Ñ§), (shiftFunctor C n).Additive] (n : ‚Ñ§) :
    (shiftFunctor C·µí·µñ n).Additive :=
  (inferInstance : (shiftFunctor (OppositeShiftAux C) n).Additive)

end Opposite

open Opposite

/-- The shift functor on the opposite category identifies to the opposite functor
of a shift functor on the original category. -/
noncomputable def shiftFunctorOpIso (n m : ‚Ñ§) (hnm : n + m = 0) :
    shiftFunctor C·µí·µñ n ‚âÖ (shiftFunctor C m).op := eqToIso (by
  obtain rfl : m = -n := by linarith
  rfl)

variable {C}

lemma shiftFunctorZero_op_hom_app (X : C·µí·µñ) :
    (shiftFunctorZero C·µí·µñ ‚Ñ§).hom.app X = (shiftFunctorOpIso C 0 0 (zero_add 0)).hom.app X ‚â´
      ((shiftFunctorZero C ‚Ñ§).inv.app X.unop).op := by
  erw [@pullbackShiftFunctorZero_hom_app (OppositeShift C ‚Ñ§), oppositeShiftFunctorZero_hom_app]
  rfl

lemma shiftFunctorZero_op_inv_app (X : C·µí·µñ) :
    (shiftFunctorZero C·µí·µñ ‚Ñ§).inv.app X =
      ((shiftFunctorZero C ‚Ñ§).hom.app X.unop).op ‚â´
      (shiftFunctorOpIso C 0 0 (zero_add 0)).inv.app X := by
  rw [‚Üê cancel_epi ((shiftFunctorZero C·µí·µñ ‚Ñ§).hom.app X), Iso.hom_inv_id_app,
    shiftFunctorZero_op_hom_app, assoc, ‚Üê op_comp_assoc, Iso.hom_inv_id_app, op_id,
    id_comp, Iso.hom_inv_id_app]

lemma shiftFunctorAdd'_op_hom_app (X : C·µí·µñ) (a‚ÇÅ a‚ÇÇ a‚ÇÉ : ‚Ñ§) (h : a‚ÇÅ + a‚ÇÇ = a‚ÇÉ)
    (b‚ÇÅ b‚ÇÇ b‚ÇÉ : ‚Ñ§) (h‚ÇÅ : a‚ÇÅ + b‚ÇÅ = 0) (h‚ÇÇ : a‚ÇÇ + b‚ÇÇ = 0) (h‚ÇÉ : a‚ÇÉ + b‚ÇÉ = 0) :
    (shiftFunctorAdd' C·µí·µñ a‚ÇÅ a‚ÇÇ a‚ÇÉ h).hom.app X =
      (shiftFunctorOpIso C _ _ h‚ÇÉ).hom.app X ‚â´
        ((shiftFunctorAdd' C b‚ÇÅ b‚ÇÇ b‚ÇÉ (by linarith)).inv.app X.unop).op ‚â´
        (shiftFunctorOpIso C _ _ h‚ÇÇ).inv.app _ ‚â´
        (shiftFunctor C·µí·µñ a‚ÇÇ).map ((shiftFunctorOpIso C _ _ h‚ÇÅ).inv.app X) := by
  erw [@pullbackShiftFunctorAdd'_hom_app (OppositeShift C ‚Ñ§) _ _ _ _ _ _ _ X
    a‚ÇÅ a‚ÇÇ a‚ÇÉ h b‚ÇÅ b‚ÇÇ b‚ÇÉ (by dsimp; linarith) (by dsimp; linarith) (by dsimp; linarith)]
  erw [oppositeShiftFunctorAdd'_hom_app]
  obtain rfl : b‚ÇÅ = -a‚ÇÅ := by linarith
  obtain rfl : b‚ÇÇ = -a‚ÇÇ := by linarith
  obtain rfl : b‚ÇÉ = -a‚ÇÉ := by linarith
  rfl

lemma shiftFunctorAdd'_op_inv_app (X : C·µí·µñ) (a‚ÇÅ a‚ÇÇ a‚ÇÉ : ‚Ñ§) (h : a‚ÇÅ + a‚ÇÇ = a‚ÇÉ)
    (b‚ÇÅ b‚ÇÇ b‚ÇÉ : ‚Ñ§) (h‚ÇÅ : a‚ÇÅ + b‚ÇÅ = 0) (h‚ÇÇ : a‚ÇÇ + b‚ÇÇ = 0) (h‚ÇÉ : a‚ÇÉ + b‚ÇÉ = 0) :
    (shiftFunctorAdd' C·µí·µñ a‚ÇÅ a‚ÇÇ a‚ÇÉ h).inv.app X =
      (shiftFunctor C·µí·µñ a‚ÇÇ).map ((shiftFunctorOpIso C _ _ h‚ÇÅ).hom.app X) ‚â´
      (shiftFunctorOpIso C _ _ h‚ÇÇ).hom.app _ ‚â´
      ((shiftFunctorAdd' C b‚ÇÅ b‚ÇÇ b‚ÇÉ (by linarith)).hom.app X.unop).op ‚â´
      (shiftFunctorOpIso C _ _ h‚ÇÉ).inv.app X := by
  rw [‚Üê cancel_epi ((shiftFunctorAdd' C·µí·µñ a‚ÇÅ a‚ÇÇ a‚ÇÉ h).hom.app X), Iso.hom_inv_id_app,
    shiftFunctorAdd'_op_hom_app X a‚ÇÅ a‚ÇÇ a‚ÇÉ h b‚ÇÅ b‚ÇÇ b‚ÇÉ h‚ÇÅ h‚ÇÇ h‚ÇÉ,
    assoc, assoc, assoc, ‚Üê Functor.map_comp_assoc, Iso.inv_hom_id_app]
  erw [Functor.map_id, id_comp, Iso.inv_hom_id_app_assoc]
  rw [‚Üê op_comp_assoc, Iso.hom_inv_id_app, op_id, id_comp, Iso.hom_inv_id_app]

lemma shiftFunctor_op_map (n m : ‚Ñ§) (hnm : n + m = 0) {K L : C·µí·µñ} (œÜ : K ‚ü∂ L) :
    (shiftFunctor C·µí·µñ n).map œÜ =
      (shiftFunctorOpIso C n m hnm).hom.app K ‚â´ ((shiftFunctor C m).map œÜ.unop).op ‚â´
        (shiftFunctorOpIso C n m hnm).inv.app L :=
  (NatIso.naturality_2 (shiftFunctorOpIso C n m hnm) œÜ).symm

variable (C)

/-- The autoequivalence `C·µí·µñ ‚âå C·µí·µñ` whose functor is `shiftFunctor C·µí·µñ n` and whose inverse
functor is `(shiftFunctor C n).op`. Do not unfold the definitions of the unit and counit
isomorphisms: the compatibilities they satisfy are stated as separate lemmas. -/
@[simps functor inverse]
noncomputable def opShiftFunctorEquivalence (n : ‚Ñ§) : C·µí·µñ ‚âå C·µí·µñ where
  functor := shiftFunctor C·µí·µñ n
  inverse := (shiftFunctor C n).op
  unitIso := NatIso.op (shiftFunctorCompIsoId C (-n) n n.add_left_neg) ‚â™‚â´
    isoWhiskerRight (shiftFunctorOpIso C n (-n) n.add_right_neg).symm (shiftFunctor C n).op
  counitIso := isoWhiskerLeft _ (shiftFunctorOpIso C n (-n) n.add_right_neg) ‚â™‚â´
    NatIso.op (shiftFunctorCompIsoId C n (-n) n.add_right_neg).symm
  functor_unitIso_comp X := Quiver.Hom.unop_inj (by
    dsimp [shiftFunctorOpIso]
    erw [comp_id, Functor.map_id, comp_id]
    change (shiftFunctorCompIsoId C n (-n) (add_neg_self n)).inv.app (X.unop‚ü¶-n‚üß) ‚â´
      ((shiftFunctorCompIsoId C (-n) n (neg_add_self n)).hom.app X.unop)‚ü¶-n‚üß' = ùüô _
    rw [shift_shiftFunctorCompIsoId_neg_add_self_hom_app n X.unop, Iso.inv_hom_id_app])

/-! The naturality of the unit and counit isomorphisms are restated in the following
lemmas so as to mitigate the need of `erw`. -/

@[reassoc (attr := simp)]
lemma opShiftFunctorEquivalence_unitIso_inv_naturality (n : ‚Ñ§) {X Y : C·µí·µñ} (f : X ‚ü∂ Y) :
    ((shiftFunctor C n).map ((shiftFunctor C·µí·µñ n).map f).unop).op ‚â´
        (opShiftFunctorEquivalence C n).unitIso.inv.app Y =
      (opShiftFunctorEquivalence C n).unitIso.inv.app X ‚â´ f :=
  (opShiftFunctorEquivalence C n).unitIso.inv.naturality f

variable {C}

lemma shift_unop_opShiftFunctorEquivalence_counitIso_inv_app (X : C·µí·µñ) (n : ‚Ñ§) :
    ((opShiftFunctorEquivalence C n).counitIso.inv.app X).unop‚ü¶n‚üß' =
      ((opShiftFunctorEquivalence C n).unitIso.hom.app ((Opposite.op ((X.unop)‚ü¶n‚üß)))).unop :=
  Quiver.Hom.op_inj ((opShiftFunctorEquivalence C n).unit_app_inverse X).symm

lemma shift_unop_opShiftFunctorEquivalence_counitIso_hom_app (X : C·µí·µñ) (n : ‚Ñ§) :
    ((opShiftFunctorEquivalence C n).counitIso.hom.app X).unop‚ü¶n‚üß' =
      ((opShiftFunctorEquivalence C n).unitIso.inv.app ((Opposite.op (X.unop‚ü¶n‚üß)))).unop :=
  Quiver.Hom.op_inj ((opShiftFunctorEquivalence C n).unitInv_app_inverse X).symm

lemma opShiftFunctorEquivalence_counitIso_inv_app_shift (X : C·µí·µñ) (n : ‚Ñ§) :
    (opShiftFunctorEquivalence C n).counitIso.inv.app (X‚ü¶n‚üß) =
      ((opShiftFunctorEquivalence C n).unitIso.hom.app X)‚ü¶n‚üß' :=
  (opShiftFunctorEquivalence C n).counitInv_app_functor X

lemma opShiftFunctorEquivalence_counitIso_hom_app_shift (X : C·µí·µñ) (n : ‚Ñ§) :
    (opShiftFunctorEquivalence C n).counitIso.hom.app (X‚ü¶n‚üß) =
      ((opShiftFunctorEquivalence C n).unitIso.inv.app X)‚ü¶n‚üß' :=
  (opShiftFunctorEquivalence C n).counit_app_functor X

variable (C)

namespace TriangleOpEquivalence

/-- The functor which sends a triangle `X ‚ü∂ Y ‚ü∂ Z ‚ü∂ X‚ü¶1‚üß` in `C` to the triangle
`op Z ‚ü∂ op Y ‚ü∂ op X ‚ü∂ (op Z)‚ü¶1‚üß` in `C·µí·µñ` (without introducing signs). -/
@[simps]
noncomputable def functor : (Triangle C)·µí·µñ ‚•§ Triangle C·µí·µñ where
  obj T := Triangle.mk T.unop.mor‚ÇÇ.op T.unop.mor‚ÇÅ.op
      ((opShiftFunctorEquivalence C 1).counitIso.inv.app (Opposite.op T.unop.obj‚ÇÅ) ‚â´
        T.unop.mor‚ÇÉ.op‚ü¶(1 : ‚Ñ§)‚üß')
  map {T‚ÇÅ T‚ÇÇ} œÜ :=
    { hom‚ÇÅ := œÜ.unop.hom‚ÇÉ.op
      hom‚ÇÇ := œÜ.unop.hom‚ÇÇ.op
      hom‚ÇÉ := œÜ.unop.hom‚ÇÅ.op
      comm‚ÇÅ := Quiver.Hom.unop_inj œÜ.unop.comm‚ÇÇ.symm
      comm‚ÇÇ := Quiver.Hom.unop_inj œÜ.unop.comm‚ÇÅ.symm
      comm‚ÇÉ := by
        dsimp
        rw [assoc, ‚Üê Functor.map_comp, ‚Üê op_comp, ‚Üê œÜ.unop.comm‚ÇÉ, op_comp, Functor.map_comp]
        erw [(opShiftFunctorEquivalence C 1).counitIso.inv.naturality_assoc œÜ.unop.hom‚ÇÅ.op]
        rfl }

/-- The functor which sends a triangle `X ‚ü∂ Y ‚ü∂ Z ‚ü∂ X‚ü¶1‚üß` in `C·µí·µñ` to the triangle
`Z.unop ‚ü∂ Y.unop ‚ü∂ X.unop ‚ü∂ Z.unop‚ü¶1‚üß` in `C` (without introducing signs). -/
@[simps]
noncomputable def inverse : Triangle C·µí·µñ ‚•§ (Triangle C)·µí·µñ where
  obj T := Opposite.op (Triangle.mk T.mor‚ÇÇ.unop T.mor‚ÇÅ.unop
      (((opShiftFunctorEquivalence C 1).unitIso.inv.app T.obj‚ÇÅ).unop ‚â´ T.mor‚ÇÉ.unop‚ü¶(1 : ‚Ñ§)‚üß'))
  map {T‚ÇÅ T‚ÇÇ} œÜ := Quiver.Hom.op
    { hom‚ÇÅ := œÜ.hom‚ÇÉ.unop
      hom‚ÇÇ := œÜ.hom‚ÇÇ.unop
      hom‚ÇÉ := œÜ.hom‚ÇÅ.unop
      comm‚ÇÅ := Opposite.op_injective œÜ.comm‚ÇÇ.symm
      comm‚ÇÇ := Opposite.op_injective œÜ.comm‚ÇÅ.symm
      comm‚ÇÉ := by
        dsimp
        rw [assoc, ‚Üê Functor.map_comp, ‚Üê unop_comp, ‚Üê œÜ.comm‚ÇÉ, unop_comp, Functor.map_comp,
          ‚Üê unop_comp_assoc]
        apply Quiver.Hom.op_inj
        simp only [Opposite.op_unop, op_comp, Quiver.Hom.op_unop, assoc,
          Opposite.unop_op, unop_comp]
        erw [‚Üê (opShiftFunctorEquivalence C 1).unitIso.inv.naturality œÜ.hom‚ÇÅ]
        rfl }

/-- The unit isomorphism of the
equivalence `triangleOpEquivalence C : (Triangle C)·µí·µñ ‚âå Triangle C·µí·µñ` .-/
@[simps!]
noncomputable def unitIso : ùü≠ _ ‚âÖ functor C ‚ãô inverse C :=
  NatIso.ofComponents (fun T => Iso.op (by
    refine' Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) (Iso.refl _) _ _ _
    ¬∑ aesop_cat
    ¬∑ aesop_cat
    ¬∑ dsimp
      simp only [Functor.map_comp, Functor.map_id, comp_id, id_comp]
      erw [‚Üê (NatIso.unop (opShiftFunctorEquivalence C 1).unitIso).inv.naturality_assoc]
      rw [shift_unop_opShiftFunctorEquivalence_counitIso_inv_app (Opposite.op T.unop.obj‚ÇÅ) 1]
      dsimp
      rw [‚Üê unop_comp, Iso.hom_inv_id_app]
      dsimp
      rw [comp_id]))
    (fun {T‚ÇÅ T‚ÇÇ} f => Quiver.Hom.unop_inj (by aesop_cat))

/-- The counit isomorphism of the
equivalence `triangleOpEquivalence C : (Triangle C)·µí·µñ ‚âå Triangle C·µí·µñ` .-/
@[simps!]
noncomputable def counitIso : inverse C ‚ãô functor C ‚âÖ ùü≠ _ :=
  NatIso.ofComponents (fun T => by
    refine' Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) (Iso.refl _) _ _ _
    ¬∑ aesop_cat
    ¬∑ aesop_cat
    ¬∑ dsimp
      rw [Functor.map_id, comp_id, id_comp, Functor.map_comp]
      erw [‚Üê (opShiftFunctorEquivalence C 1).counitIso.inv.naturality_assoc T.mor‚ÇÉ]
      simp only [opShiftFunctorEquivalence_counitIso_inv_app_shift, ‚Üê Functor.map_comp,
        Iso.hom_inv_id_app, Functor.map_id, Functor.id_obj, comp_id, Functor.id_map])
    (by aesop_cat)

end TriangleOpEquivalence

/-- An anti-equivalence between the categories of triangles in `C` and in `C·µí·µñ`.
A triangle in `C·µí·µñ` shall be distinguished iff it correspond to a distinguished
triangle in `C` via this equivalence. -/
@[simps]
noncomputable def triangleOpEquivalence :
    (Triangle C)·µí·µñ ‚âå Triangle C·µí·µñ where
  functor := TriangleOpEquivalence.functor C
  inverse := TriangleOpEquivalence.inverse C
  unitIso := TriangleOpEquivalence.unitIso C
  counitIso := TriangleOpEquivalence.counitIso C

variable [HasZeroObject C] [Preadditive C] [‚àÄ (n : ‚Ñ§), (shiftFunctor C n).Additive]
  [Pretriangulated C]

namespace Opposite

/-- A triangle in `C·µí·µñ` shall be distinguished iff it corresponds to a distinguished
triangle in `C` via the equivalence `triangleOpEquivalence C : (Triangle C)·µí·µñ ‚âå Triangle C·µí·µñ`. -/
def distinguishedTriangles : Set (Triangle C·µí·µñ) :=
  fun T => ((triangleOpEquivalence C).inverse.obj T).unop ‚àà distTriang C

variable {C}

lemma mem_distinguishedTriangles_iff (T : Triangle C·µí·µñ) :
    T ‚àà distinguishedTriangles C ‚Üî
      ((triangleOpEquivalence C).inverse.obj T).unop ‚àà distTriang C := by
  rfl

lemma mem_distinguishedTriangles_iff' (T : Triangle C·µí·µñ) :
    T ‚àà distinguishedTriangles C ‚Üî
      ‚àÉ (T' : Triangle C) (_ : T' ‚àà distTriang C),
        Nonempty (T ‚âÖ (triangleOpEquivalence C).functor.obj (Opposite.op T')) := by
  rw [mem_distinguishedTriangles_iff]
  constructor
  ¬∑ intro hT
    exact ‚ü®_ ,hT, ‚ü®(triangleOpEquivalence C).counitIso.symm.app T‚ü©‚ü©
  ¬∑ rintro ‚ü®T', hT', ‚ü®e‚ü©‚ü©
    refine' isomorphic_distinguished _ hT' _ _
    exact Iso.unop ((triangleOpEquivalence C).unitIso.app (Opposite.op T') ‚â™‚â´
      (triangleOpEquivalence C).inverse.mapIso e.symm)

lemma isomorphic_distinguished (T‚ÇÅ : Triangle C·µí·µñ)
    (hT‚ÇÅ : T‚ÇÅ ‚àà distinguishedTriangles C) (T‚ÇÇ : Triangle C·µí·µñ) (e : T‚ÇÇ ‚âÖ T‚ÇÅ) :
    T‚ÇÇ ‚àà distinguishedTriangles C := by
  simp only [mem_distinguishedTriangles_iff] at hT‚ÇÅ ‚ä¢
  exact Pretriangulated.isomorphic_distinguished _ hT‚ÇÅ _
    ((triangleOpEquivalence C).inverse.mapIso e).unop.symm

/-- Up to rotation, the contractible triangle `X ‚ü∂ X ‚ü∂ 0 ‚ü∂ X‚ü¶1‚üß` for `X : C·µí·µñ` corresponds
to the contractible triangle for `X.unop` in `C`. -/
@[simps!]
noncomputable def contractibleTriangleIso (X : C·µí·µñ) :
    contractibleTriangle X ‚âÖ (triangleOpEquivalence C).functor.obj
      (Opposite.op (contractibleTriangle X.unop).invRotate) :=
  Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _)
    (IsZero.iso (isZero_zero _) (by
      dsimp
      rw [IsZero.iff_id_eq_zero]
      change (ùüô ((0 : C)‚ü¶(-1 : ‚Ñ§)‚üß)).op = 0
      rw [‚Üê Functor.map_id, id_zero, Functor.map_zero, op_zero]))
    (by aesop_cat) (by aesop_cat) (by aesop_cat)

lemma contractible_distinguished (X : C·µí·µñ) :
    contractibleTriangle X ‚àà distinguishedTriangles C := by
  rw [mem_distinguishedTriangles_iff']
  exact ‚ü®_, inv_rot_of_dist_triangle _ (Pretriangulated.contractible_distinguished X.unop),
    ‚ü®contractibleTriangleIso X‚ü©‚ü©

/-- Isomorphism expressing a compatibility of the equivalence `triangleOpEquivalence C`
with the rotation of triangles. -/
noncomputable def rotateTriangleOpEquivalenceInverseObjRotateUnopIso (T : Triangle C·µí·µñ) :
    Triangle.rotate ((triangleOpEquivalence C).inverse.obj (Triangle.rotate T)).unop ‚âÖ
      ((triangleOpEquivalence C).inverse.obj T).unop :=
  Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _)
      (-((opShiftFunctorEquivalence C 1).unitIso.app T.obj‚ÇÅ).unop) (by simp)
        (Quiver.Hom.op_inj (by aesop_cat)) (by aesop_cat)

lemma rotate_distinguished_triangle (T : Triangle C·µí·µñ) :
    T ‚àà distinguishedTriangles C ‚Üî T.rotate ‚àà distinguishedTriangles C := by
  simp only [mem_distinguishedTriangles_iff, Pretriangulated.rotate_distinguished_triangle
    ((triangleOpEquivalence C).inverse.obj (T.rotate)).unop]
  exact distinguished_iff_of_iso (rotateTriangleOpEquivalenceInverseObjRotateUnopIso T).symm

lemma distinguished_cocone_triangle {X Y : C·µí·µñ} (f : X ‚ü∂ Y) :
    ‚àÉ (Z : C·µí·µñ) (g : Y ‚ü∂ Z) (h : Z ‚ü∂ X‚ü¶(1 : ‚Ñ§)‚üß),
      Triangle.mk f g h ‚àà distinguishedTriangles C := by
  obtain ‚ü®Z, g, h, H‚ü© := Pretriangulated.distinguished_cocone_triangle‚ÇÅ f.unop
  simp only [mem_distinguishedTriangles_iff]
  refine' ‚ü®_, g.op, (opShiftFunctorEquivalence C 1).counitIso.inv.app (Opposite.op Z) ‚â´
    (shiftFunctor C·µí·µñ (1 : ‚Ñ§)).map h.op, _‚ü©
  dsimp
  convert H using 2
  dsimp
  rw [Functor.map_comp]
  apply Quiver.Hom.op_inj
  rw [op_comp, op_comp, shift_unop_opShiftFunctorEquivalence_counitIso_inv_app (Opposite.op Z) 1]
  erw [‚Üê (opShiftFunctorEquivalence C 1).unitIso.hom.naturality h.op,
    assoc, Iso.hom_inv_id_app, comp_id]
  rfl

lemma complete_distinguished_triangle_morphism (T‚ÇÅ T‚ÇÇ : Triangle C·µí·µñ)
    (hT‚ÇÅ : T‚ÇÅ ‚àà distinguishedTriangles C) (hT‚ÇÇ : T‚ÇÇ ‚àà distinguishedTriangles C)
    (a : T‚ÇÅ.obj‚ÇÅ ‚ü∂ T‚ÇÇ.obj‚ÇÅ) (b : T‚ÇÅ.obj‚ÇÇ ‚ü∂ T‚ÇÇ.obj‚ÇÇ) (comm : T‚ÇÅ.mor‚ÇÅ ‚â´ b = a ‚â´ T‚ÇÇ.mor‚ÇÅ) :
    ‚àÉ (c : T‚ÇÅ.obj‚ÇÉ ‚ü∂ T‚ÇÇ.obj‚ÇÉ), T‚ÇÅ.mor‚ÇÇ ‚â´ c = b ‚â´ T‚ÇÇ.mor‚ÇÇ ‚àß
      T‚ÇÅ.mor‚ÇÉ ‚â´ a‚ü¶1‚üß' = c ‚â´ T‚ÇÇ.mor‚ÇÉ := by
  rw [mem_distinguishedTriangles_iff] at hT‚ÇÅ hT‚ÇÇ
  obtain ‚ü®c, hc‚ÇÅ, hc‚ÇÇ‚ü© := Pretriangulated.complete_distinguished_triangle_morphism‚ÇÅ _ _ hT‚ÇÇ hT‚ÇÅ b.unop
    a.unop (Quiver.Hom.op_inj comm.symm)
  dsimp at c hc‚ÇÅ hc‚ÇÇ
  simp only [neg_comp, assoc, comp_neg, neg_inj] at hc‚ÇÇ
  refine' ‚ü®c.op, Quiver.Hom.unop_inj hc‚ÇÅ.symm, Quiver.Hom.unop_inj _‚ü©
  apply (shiftFunctor C (1 : ‚Ñ§)).map_injective
  rw [unop_comp, unop_comp, Functor.map_comp, Functor.map_comp, Quiver.Hom.unop_op,
      ‚Üê cancel_epi ((opShiftFunctorEquivalence C 1).unitIso.inv.app T‚ÇÇ.obj‚ÇÅ).unop, hc‚ÇÇ]
  apply Quiver.Hom.op_inj
  simp only [op_comp, Functor.id_obj, Opposite.op_unop, Functor.comp_obj, Functor.op_obj,
    Opposite.unop_op, Quiver.Hom.op_unop, assoc]
  congr 1
  exact (opShiftFunctorEquivalence C 1).unitIso.inv.naturality a

scoped instance : Pretriangulated C·µí·µñ where
  distinguishedTriangles := distinguishedTriangles C
  isomorphic_distinguished := isomorphic_distinguished
  contractible_distinguished := contractible_distinguished
  distinguished_cocone_triangle := distinguished_cocone_triangle
  rotate_distinguished_triangle := rotate_distinguished_triangle
  complete_distinguished_triangle_morphism := complete_distinguished_triangle_morphism

end Opposite

variable {C}

lemma mem_distTriang_op_iff (T : Triangle C·µí·µñ) :
    (T ‚àà distTriang C·µí·µñ) ‚Üî ((triangleOpEquivalence C).inverse.obj T).unop ‚àà distTriang C := by
  rfl

lemma mem_distTriang_op_iff' (T : Triangle C·µí·µñ) :
    (T ‚àà distTriang C·µí·µñ) ‚Üî ‚àÉ (T' : Triangle C) (_ : T' ‚àà distTriang C),
      Nonempty (T ‚âÖ (triangleOpEquivalence C).functor.obj (Opposite.op T')) :=
  Opposite.mem_distinguishedTriangles_iff' T

lemma op_distinguished (T : Triangle C) (hT : T ‚àà distTriang C) :
    ((triangleOpEquivalence C).functor.obj (Opposite.op T)) ‚àà distTriang C·µí·µñ := by
  rw [mem_distTriang_op_iff']
  exact ‚ü®T, hT, ‚ü®Iso.refl _‚ü©‚ü©

lemma unop_distinguished (T : Triangle C·µí·µñ) (hT : T ‚àà distTriang C·µí·µñ) :
    ((triangleOpEquivalence C).inverse.obj T).unop ‚àà distTriang C := hT

end Pretriangulated

end CategoryTheory
