import Mathlib.CategoryTheory.Triangulated.Filtered.ForgetHalf

namespace CategoryTheory

open Category Limits Pretriangulated ZeroObject Preadditive

namespace Triangulated

namespace FilteredTriangulated

variable {C : Type _} [Category C] [HasZeroObject C]  [Preadditive C] [HasShift C (‚Ñ§ √ó ‚Ñ§)]
  [‚àÄ p : ‚Ñ§ √ó ‚Ñ§, Functor.Additive (CategoryTheory.shiftFunctor C p)]
  [hC : Pretriangulated C] [hP : FilteredTriangulated C] [IsTriangulated C]

variable [‚àÄ (X : C) (n : ‚Ñ§), Decidable (IsZero ((Gr'' n).obj X))]

noncomputable def HalfForgetObj (X : C) : hP.Core' := by
  by_cases IsLE X 0
  ¬∑ exact (existence_omega X).choose
  ¬∑ exact 0

noncomputable def IdToHalfForgetApp (X : C) : X ‚ü∂ (HalfForgetObj X).1 := by
  dsimp [HalfForgetObj]
  by_cases h : IsLE X 0
  ¬∑ simp only [h, ‚ÜìreduceDIte]
    exact (existence_omega X).choose_spec.choose
  ¬∑ simp only [h, ‚ÜìreduceDIte]
    exact 0

lemma HalfForgetObj_prop (X : C) [IsLE X 0] (Y : C) [IsGE Y 0] :
    Function.Bijective (fun (f : (HalfForgetObj X).1 ‚ü∂ Y) ‚Ü¶ (IdToHalfForgetApp X) ‚â´ f) := by
  dsimp [HalfForgetObj, IdToHalfForgetApp]
  have h : IsLE X 0 := inferInstance
  simp only [h, ‚ÜìreduceDIte, congrArg_cast_hom_right, assoc]
  refine Function.Bijective.comp ?_ (IsIso.comp_left_bijective _)
  exact (existence_omega X).choose_spec.choose_spec Y inferInstance

noncomputable def HalfForgetMap {X Y : C} (f : X ‚ü∂ Y) : HalfForgetObj X ‚ü∂ HalfForgetObj Y := by
  by_cases IsLE X 0
  ¬∑ by_cases IsLE Y 0
    ¬∑ exact ((HalfForgetObj_prop X (HalfForgetObj Y).1).2 (f ‚â´ IdToHalfForgetApp Y)).choose
    ¬∑ exact 0
  ¬∑ exact  0

@[simp]
lemma HalfForgetMap_prop {X Y : C} (f : X ‚ü∂ Y) [IsLE X 0] [IsLE Y 0] :
    IdToHalfForgetApp X ‚â´ HalfForgetMap f = f ‚â´ IdToHalfForgetApp Y := by
  dsimp [HalfForgetMap]
  have hX : IsLE X 0 := inferInstance
  have hY : IsLE Y 0 := inferInstance
  simp only [hX, ‚ÜìreduceDIte, hY, ‚ÜìreduceIte]
  exact ((HalfForgetObj_prop X (HalfForgetObj Y).1).2 (f ‚â´ IdToHalfForgetApp Y)).choose_spec

lemma HalfForgetMapId {X : C} : HalfForgetMap (ùüô X) = ùüô (HalfForgetObj X) := by
  by_cases h : IsLE X 0
  ¬∑ apply (HalfForgetObj_prop X (HalfForgetObj X).1).1
    simp only
    rw [HalfForgetMap_prop (ùüô X), id_comp]; erw [comp_id]
  ¬∑ simp only [HalfForgetObj, h, HalfForgetMap, ‚ÜìreduceDIte]
    have : IsZero (HalfForgetObj X) := by
      simp only [HalfForgetObj, h, ‚ÜìreduceDIte]
      exact Limits.isZero_zero _
    exact (Limits.IsZero.eq_zero_of_src this _).symm

lemma HalfForgetMapComp {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsLE Y 0] :
    HalfForgetMap (f ‚â´ g) = HalfForgetMap f ‚â´ HalfForgetMap g := by
  have hY : IsLE Y 0 := inferInstance
  by_cases hX : IsLE X 0
  ¬∑ by_cases hZ : IsLE Z 0
    ¬∑ apply (HalfForgetObj_prop X (HalfForgetObj Z).1).1
      simp only
      rw [HalfForgetMap_prop (f ‚â´ g)]
      conv_rhs => erw [‚Üê assoc]; rw [HalfForgetMap_prop f, assoc, HalfForgetMap_prop g, ‚Üê assoc]
    ¬∑ simp only [HalfForgetObj, hX, hZ, HalfForgetMap, ‚ÜìreduceDIte, hY, comp_zero]
  ¬∑ simp only [HalfForgetObj, hX, HalfForgetMap, ‚ÜìreduceDIte, hY, dite_eq_ite, zero_comp]

lemma HalfForgetMapComp' {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (hZ : ¬¨ (IsLE Z 0)) :
    HalfForgetMap (f ‚â´ g) = HalfForgetMap f ‚â´ HalfForgetMap g := by
  simp only [HalfForgetMap, hZ, ‚ÜìreduceDIte, dite_eq_ite, ite_self, comp_zero]

/- We construct `Forget X` as the colimit of `HalfForget X‚ü™n‚ü´` as `n` varies over `‚Ñ§` (seen
as a poset category), with the transition maps given by `power_of_alpha`.-/

@[simp]
noncomputable def ForgetInductiveSystem_aux (X : C) : ‚Ñ§ ‚•§ C where
  obj a := (@shiftFunctor C _ _ _ Shift‚ÇÇ a).obj X
  map := by
    intro a b f
    exact power_of_alpha X a b (b - a).natAbs
      (by rw [‚Üê Int.eq_natAbs_of_zero_le (sub_nonneg.mpr (leOfHom f)), add_sub_cancel])
  map_id a := by
    simp only [id_eq, eq_mpr_eq_cast, sub_self, Int.natAbs_zero]
    rw [power_of_alpha_zero', Iso.refl_hom]
  map_comp := by
    intro a b c f g
    simp only [id_eq, eq_mpr_eq_cast]
    have hab : (b - a).natAbs = b - a :=
      (Int.eq_natAbs_of_zero_le (sub_nonneg.mpr (leOfHom f))).symm
    have hbc : (c - b).natAbs = c - b :=
      (Int.eq_natAbs_of_zero_le (sub_nonneg.mpr (leOfHom g))).symm
    have hac : (c - a).natAbs = c - a :=
      (Int.eq_natAbs_of_zero_le (sub_nonneg.mpr (leOfHom (f ‚â´ g)))).symm
    rw [power_of_alpha_change_exponent X (c - a).natAbs ((b - a).natAbs + (c - b).natAbs)
      (by rw [‚Üê Nat.cast_inj (R := ‚Ñ§), Nat.cast_add, hab, hbc, hac, sub_add_sub_cancel'])
      a c (by rw [hac, add_sub_cancel])]
    exact (power_of_alpha_assoc X a b c (b - a).natAbs (c - b).natAbs
      (by rw [hab, add_sub_cancel]) (by rw [hbc, add_sub_cancel])).symm

lemma ForgetInductiveSystem_aux_prop (X : C) (a : ‚Ñ§) [IsLE X a] {b c : Set.Iic (-a)}
    (u : b ‚ü∂ c) (Y : C) [IsGE Y 0] : Function.Bijective
    (fun (f : (ForgetInductiveSystem_aux X).obj c ‚ü∂ Y) ‚Ü¶
    ((ForgetInductiveSystem_aux X).map u ‚â´ f)) := by
  have : IsLE X (a + b.1 - b.1) := by rw [add_sub_cancel_right]; infer_instance
  have : IsGE Y (a + b.1 + ‚Üë(c.1 - b.1).natAbs) := by
    rw [‚Üê Int.eq_natAbs_of_zero_le (sub_nonneg.mpr (leOfHom u)), add_assoc, add_sub_cancel]
    refine isGE_of_GE Y _ 0 ?_
    have := Set.mem_Iic.mp c.2; linarith
  refine adj_left_extended X Y b.1 c.1 (a + b) (c.1 - b.1).natAbs
    (by rw [‚Üê Int.eq_natAbs_of_zero_le (sub_nonneg.mpr (leOfHom u)), add_sub_cancel])

noncomputable def ForgetInductiveSystem (X : C) : ‚Ñ§ ‚•§ hP.Core' where
  obj a := HalfForgetObj ((ForgetInductiveSystem_aux X).obj a)
  map f := HalfForgetMap ((ForgetInductiveSystem_aux X).map f)
  map_id a := by
    simp only
    rw [Functor.map_id, HalfForgetMapId]
  map_comp := by
    intro a b c f g
    simp only
    rw [Functor.map_comp]
    by_cases h : IsLE X (-b)
    ¬∑ have : IsLE ((ForgetInductiveSystem_aux X).obj b) 0 := by
        dsimp [ForgetInductiveSystem_aux]
        exact isLE_shift X (-b) b 0 (by linarith)
      exact HalfForgetMapComp ((ForgetInductiveSystem_aux X).map f)
        ((ForgetInductiveSystem_aux X).map g)
    ¬∑ have : ¬¨ IsLE ((ForgetInductiveSystem_aux X).obj c) 0 := by
        dsimp [ForgetInductiveSystem_aux]
        intro habs
        have : IsLE X (-c) := isLE_of_shift X (-c) c 0 (by linarith)
        have : IsLE X (-b ) := isLE_of_LE X (- c) (- b) (by have := leOfHom g; linarith)
        exact h this
      exact HalfForgetMapComp' ((ForgetInductiveSystem_aux X).map f)
        ((ForgetInductiveSystem_aux X).map g) this

lemma ForgetInductiveSystem_prop (X : C) (a : ‚Ñ§) [IsLE X a] {b c : Set.Iic (-a)}
    (u : b ‚ü∂ c) (Y : C) [IsGE Y 0] : Function.Bijective
    (fun (f : ((ForgetInductiveSystem X).obj c).1 ‚ü∂ Y) ‚Ü¶
    ((fullSubcategoryInclusion _).map ((ForgetInductiveSystem X).map u) ‚â´ f)) := by
  simp only [ForgetInductiveSystem, fullSubcategoryInclusion.obj, fullSubcategoryInclusion.map]
  have : IsLE ((ForgetInductiveSystem_aux X).obj b.1) 0 := by
    simp only [ForgetInductiveSystem_aux]
    have : IsLE X (-b.1) := isLE_of_LE X a (-b) (by have := Set.mem_Iic.mp b.2; linarith)
    refine isLE_shift X (-b) b.1 0 (by linarith)
  have : IsLE ((ForgetInductiveSystem_aux X).obj c.1) 0 := by
    simp only [ForgetInductiveSystem_aux]
    have : IsLE X (-c.1) := isLE_of_LE X a (-c) (by have := Set.mem_Iic.mp c.2; linarith)
    refine isLE_shift X (-c) c.1 0 (by linarith)
  rw [‚Üê Function.Bijective.of_comp_iff'
    (HalfForgetObj_prop ((ForgetInductiveSystem_aux X).obj b) Y) _]
  have heq : (fun (f : (HalfForgetObj ((ForgetInductiveSystem_aux X).obj b.1)).obj ‚ü∂ Y) ‚Ü¶
      IdToHalfForgetApp ((ForgetInductiveSystem_aux X).obj b.1) ‚â´ f) ‚àò
      (fun f ‚Ü¶ HalfForgetMap ((ForgetInductiveSystem_aux X).map u) ‚â´ f) =
      (fun (f : (ForgetInductiveSystem_aux X).obj c ‚ü∂ Y) ‚Ü¶
      ((ForgetInductiveSystem_aux X).map u ‚â´ f)) ‚àò
      (fun (f : (HalfForgetObj ((ForgetInductiveSystem_aux X).obj c.1)).obj ‚ü∂ Y) ‚Ü¶
      IdToHalfForgetApp ((ForgetInductiveSystem_aux X).obj c.1) ‚â´ f) := by
    ext g
    simp only [Function.comp_apply]
    conv_lhs => rw [‚Üê assoc, HalfForgetMap_prop, assoc]
  rw [heq]
  apply Function.Bijective.comp
  ¬∑ exact ForgetInductiveSystem_aux_prop X a u Y
  ¬∑ exact HalfForgetObj_prop ((ForgetInductiveSystem_aux X).obj c) Y

lemma ForgetInductiveSystem_iso_of_le (X : C) (a : ‚Ñ§) [IsLE X a] {b c : Set.Iic (-a)}
    (u : b ‚ü∂ c) : IsIso ((ForgetInductiveSystem X).map u) := by
  apply IsIso.mk
  have bij := ForgetInductiveSystem_prop X a u ((ForgetInductiveSystem X).obj b).1
  obtain ‚ü®f, hf‚ü© := bij.2 (ùüô ((ForgetInductiveSystem X).obj b))
  use f
  constructor
  ¬∑ exact hf
  ¬∑ have bij' := ForgetInductiveSystem_prop X a u ((ForgetInductiveSystem X).obj c).1
    apply bij'.1
    simp only [fullSubcategoryInclusion.obj, fullSubcategoryInclusion.map] at hf ‚ä¢
    conv_lhs => erw [‚Üê assoc]; rw [hf]; erw [id_comp]
    erw [comp_id]

noncomputable abbrev ForgetInductiveSystemMap {X Y : C} (f : X ‚ü∂ Y) (a : ‚Ñ§) :
    (ForgetInductiveSystem X).obj a ‚ü∂ (ForgetInductiveSystem Y).obj a :=
  HalfForgetMap ((@shiftFunctor C _ _ _ Shift‚ÇÇ a).map f)

lemma ForgetInductiveSystemMap_naturality {X Y : C} (f : X ‚ü∂ Y) {a b : ‚Ñ§} (u : a ‚ü∂ b)
    [IsLE X (-b)] [IsLE Y (-b)] :
    (ForgetInductiveSystem X).map u ‚â´ ForgetInductiveSystemMap f b =
    ForgetInductiveSystemMap f a ‚â´ (ForgetInductiveSystem Y).map u := by
  dsimp [ForgetInductiveSystem, ForgetInductiveSystemMap]
  have : IsLE ((@shiftFunctor C _ _ _ Shift‚ÇÇ b).obj X) 0 := isLE_shift X (-b) b 0 (by linarith)
  have : IsLE ((@shiftFunctor C _ _ _ Shift‚ÇÇ a).obj Y) 0 := by
    have := isLE_shift Y (-b) a (a - b) (by linarith)
    exact isLE_of_LE _ (a - b) 0 (by have := leOfHom u; linarith)
  conv_lhs => rw [‚Üê HalfForgetMapComp]
  conv_rhs => rw [‚Üê HalfForgetMapComp]
  rw [power_of_alpha_naturality f]

lemma ForgetInductiveSystemMap_naturality' {X Y : C} (f : X ‚ü∂ Y) (a : ‚Ñ§) [IsLE X a] [IsLE Y a]
    {b c : Set.Iic (-a)} (u : b ‚ü∂ c) :
    (ForgetInductiveSystem X).map u ‚â´ ForgetInductiveSystemMap f c.1 =
    ForgetInductiveSystemMap f b.1 ‚â´ (ForgetInductiveSystem Y).map u := by
  have : IsLE X (- c.1) := isLE_of_LE X a (-c) (by have := Set.mem_Iic.mp c.2; linarith)
  have : IsLE Y (- c.1) := isLE_of_LE Y a (-c) (by have := Set.mem_Iic.mp c.2; linarith)
  exact ForgetInductiveSystemMap_naturality f u

lemma ForgetInductiveSystem_hasLimit (X : C) : HasLimit (ForgetInductiveSystem X) := by
  set a := (hP.LE_exhaustive X).choose
  have : IsLE X a := {le := (hP.LE_exhaustive X).choose_spec}
  exact HasLimit_of_transition_eventually_iso
    (ForgetInductiveSystem X) (a := -a) (fun _ _ u ‚Ü¶ ForgetInductiveSystem_iso_of_le X a u)

/- The definition of the functor `Forget`.-/

@[simp]
noncomputable def ForgetObj (X : C) : hP.Core' := by
  have := ForgetInductiveSystem_hasLimit X
  exact Limits.limit (ForgetInductiveSystem X)

@[simp]
noncomputable def ForgetMap {X Y : C} (f : X ‚ü∂ Y) : ForgetObj X ‚ü∂ ForgetObj Y := by
  have := ForgetInductiveSystem_hasLimit X
  have := ForgetInductiveSystem_hasLimit Y
  set a := (hP.LE_exhaustive X).choose
  have : IsLE X a := {le := (hP.LE_exhaustive X).choose_spec}
  set b := (hP.LE_exhaustive Y).choose
  have : IsLE Y b := {le := (hP.LE_exhaustive Y).choose_spec}
  refine Hom_of_almost_NatTrans _ _ (ForgetInductiveSystemMap f) ?_
  use -max a b
  have : IsLE X (max a b) := isLE_of_LE X a (max a b) (le_max_left _ _)
  have : IsLE Y (max a b) := isLE_of_LE Y b (max a b) (le_max_right _ _)
  exact (fun _ _ u ‚Ü¶ ForgetInductiveSystemMap_naturality' f (max a b) u)

noncomputable def Forget : C ‚•§ hP.Core' where
  obj X := ForgetObj X
  map f := ForgetMap f
  map_id X := by
    have := ForgetInductiveSystem_hasLimit X
    refine Hom_of_almost_NatTrans_id _ _ ?_
    use 0
    simp only [ForgetInductiveSystemMap, ForgetInductiveSystem_aux, Functor.map_id, Subtype.forall,
      Set.mem_Iic]
    exact fun _ _ ‚Ü¶ HalfForgetMapId
  map_comp := by
    intro X Y Z f g
    have := ForgetInductiveSystem_hasLimit X
    have := ForgetInductiveSystem_hasLimit Y
    have := ForgetInductiveSystem_hasLimit Z
    simp only [ForgetObj, ForgetMap]
    rw [Hom_of_almost_NatTrans_comp]
    set a := (hP.LE_exhaustive Y).choose
    have : IsLE Y a := {le := (hP.LE_exhaustive Y).choose_spec}
    use (-a)
    simp only [ForgetInductiveSystemMap, ForgetInductiveSystem_aux, Functor.map_comp]
    intro b
    have : IsLE ((@shiftFunctor C _ _ _ Shift‚ÇÇ b.1).obj Y) 0 := by
      have : IsLE Y (-b.1) := isLE_of_LE Y a (-b.1) (by have := Set.mem_Iic.mp b.2; linarith)
      exact isLE_shift Y (-b.1) b 0 (by linarith)
    rw [HalfForgetMapComp]

-- TODO:
-- (1) `Forget` is a triangulated functor.
-- (2) If `X` is `‚â§ 0`, then `Hom(Forget(X),Y) = Hom(X,Y)` for every `Y ‚â• 0`. (In particular,
-- the restriction of `Forget` to `C(‚â§ 0)` is left adjoint to the inclusion.)
-- (3) If `X` is `‚â• 0`, then `Hom(Y,Forget(X)) = Hom(Y,X)` for every `Y ‚â§ 0`. (In particular,
-- the restriction of `Forget` to `C(‚â• 0)` is right adjoint to the inclusion.)
-- (just redo the whole construction dually, and prove isomorphism of functors?)
-- (4) `Hom(X,Y) = Hom(Forget(X),Forget(X))` is `X ‚â§ 0` and `Y ‚â• 0`. (Should follow fron (2) and (3).)
-- (5) `Forget(Œ±)` is an isomorphism (`Œ± : X ‚ü∂ X‚ü™1‚ü´`). In fact `Forget` sends all
-- `power_of_alpha` to isomorphisms.

end FilteredTriangulated

end Triangulated

end CategoryTheory
