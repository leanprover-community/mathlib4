/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.ClosedUnderIsomorphisms
import Mathlib.CategoryTheory.Localization.CalculusOfFractions
import Mathlib.CategoryTheory.Localization.Triangulated
import Mathlib.CategoryTheory.Shift.Localization

/-! # Triangulated subcategories

In this file, we introduce the notion of triangulated subcategory of
a pretriangulated category `C`. If `S : Subcategory W`, we define the
class of morphisms `S.W : MorphismProperty C` consisting of morphisms
whose "cone" belongs to `S` (up to isomorphisms). We show that `S.W`
has both calculus of left and right fractions.

## TODO

* obtain (pre)triangulated instances on the localized category with respect to `S.W`
* define the type `S.category` as `Fullsubcategory S.set` and show that it
is a pretriangulated category.

## Implementation notes

In the definition of `Triangulated.Subcategory`, we do not assume that the predicate
on objects is closed under isomorphisms (i.e. that the subcategory is "strictly full").
Part of the theory would be more convenient under this stronger assumption
(e.g. `Subcategory C` would be a lattice), but some applications require this:
for example, the subcategory of bounded below complexes in the homotopy category
of an additive category is not closed under isomorphisms.

## References
* [Jean-Louis Verdier, *Des catÃ©gories dÃ©rivÃ©es des catÃ©gories abÃ©liennes*][verdier1996]

-/

namespace CategoryTheory

open Category Limits Preadditive ZeroObject

namespace Limits

variable {C Jâ‚ Jâ‚‚ : Type _} [Category C]
  (X : Jâ‚‚ â†’ C) (e : Jâ‚ â‰ƒ Jâ‚‚) [HasProduct X]

noncomputable def fanOfEquiv : Fan (X âˆ˜ e) := Fan.mk (âˆ X) (fun _ => Pi.Ï€ _ _)

@[simp]
lemma fanOfEquiv_proj (j : Jâ‚) : (fanOfEquiv X e).proj j = Pi.Ï€ _ (e j) := rfl

@[reassoc]
lemma Fan.congr_proj {J : Type _} {F : J â†’ C} (s : Fan F)
    {jâ‚ jâ‚‚ : J} (h : jâ‚ = jâ‚‚) : s.proj jâ‚ â‰« eqToHom (by rw [h]) = s.proj jâ‚‚ := by
  subst h
  simp

@[reassoc]
lemma Pi.congr_Ï€ {J : Type _} (F : J â†’ C) [HasProduct F] {jâ‚ jâ‚‚ : J} (h : jâ‚ = jâ‚‚) :
    Pi.Ï€ F jâ‚ â‰« eqToHom (by rw [h]) = Pi.Ï€ F jâ‚‚ := by
  subst h
  simp

noncomputable def isLimitFanOfEquiv : IsLimit (fanOfEquiv X e) :=
  mkFanLimit _ (fun s => Pi.lift (fun jâ‚‚ => s.proj (e.symm jâ‚‚) â‰« eqToHom (by simp) ))
    (fun s j => by simp [Fan.congr_proj _ (e.symm_apply_apply j)])
    (fun s m hm => Pi.hom_ext _ _ (fun j => by
      dsimp
      simp only [limit.lift_Ï€, Fan.mk_pt, Fan.mk_Ï€_app, â† hm,
        Function.comp_apply, fanOfEquiv_proj, assoc]
      rw [Pi.congr_Ï€]
      simp))

lemma hasProductOfEquiv : HasProduct (X âˆ˜ e) :=
  âŸ¨âŸ¨_, isLimitFanOfEquiv X eâŸ©âŸ©

noncomputable def productIsoOfEquiv [HasProduct (X âˆ˜ e)] :  âˆ (X âˆ˜ e) â‰… âˆ X :=
  IsLimit.conePointUniqueUpToIso (limit.isLimit _) (isLimitFanOfEquiv X e)

noncomputable def productOptionIso {C J : Type _} [Category C]
    (X : Option J â†’ C) [HasProduct X] [HasProduct (fun j => X (some j))]
    [HasBinaryProduct (âˆ (fun j => X (some j))) (X none)] :
    (âˆ X) â‰… (âˆ (fun j => X (some j))) â¨¯ (X none) where
  hom := prod.lift (Pi.lift (fun j => Pi.Ï€ _ (some j))) (Pi.Ï€ _ none)
  inv := Pi.lift (fun b => match b with
    | some j => prod.fst â‰« Pi.Ï€ _ j
    | none => prod.snd)

end Limits

namespace Triangulated

open Pretriangulated

variable (C : Type*) [Category C] [HasZeroObject C] [HasShift C â„¤]
  [Preadditive C] [âˆ€ (n : â„¤), (shiftFunctor C n).Additive] [Pretriangulated C]

/-- A triangulated subcategory of a pretriangulated category `C` consists of
a predicate `P : C â†’ Prop` which contains a zero object, is stable by shifts, and such that
if `Xâ‚ âŸ¶ Xâ‚‚ âŸ¶ Xâ‚ƒ âŸ¶ Xâ‚âŸ¦1âŸ§` is a distinguished triangle such that if `Xâ‚` and `Xâ‚ƒ` satisfy
`P` then `Xâ‚‚` is isomorphic to an object satisfying `P`. -/
structure Subcategory where
  /-- the underlying predicate on objects of a triangulated subcategory -/
  P : C â†’ Prop
  zero' : âˆƒ (Z : C) (_ : IsZero Z), P Z
  shift (X : C) (n : â„¤) : P X â†’ P (XâŸ¦nâŸ§)
  extâ‚‚' (T : Triangle C) (_ : T âˆˆ distTriang C) : P T.objâ‚ â†’ P T.objâ‚ƒ â†’ isoClosure P T.objâ‚‚

namespace Subcategory

variable {C}
variable (S : Subcategory C)

lemma zero [ClosedUnderIsomorphisms S.P] : S.P 0 := by
  obtain âŸ¨X, hX, memâŸ© := S.zero'
  exact mem_of_iso _ hX.isoZero mem

/-- The closure under isomorphisms of a triangulated subcategory. -/
def isoClosure : Subcategory C where
  P := CategoryTheory.isoClosure S.P
  zero' := by
    obtain âŸ¨Z, hZ, hZ'âŸ© := S.zero'
    exact âŸ¨Z, hZ, Z, hZ', âŸ¨Iso.refl _âŸ©âŸ©
  shift X n := by
    rintro âŸ¨Y, hY, âŸ¨eâŸ©âŸ©
    exact âŸ¨YâŸ¦nâŸ§, S.shift Y n hY, âŸ¨(shiftFunctor C n).mapIso eâŸ©âŸ©
  extâ‚‚' := by
    rintro T hT âŸ¨Xâ‚, hâ‚, âŸ¨eâ‚âŸ©âŸ© âŸ¨Xâ‚ƒ, hâ‚ƒ, âŸ¨eâ‚ƒâŸ©âŸ©
    exact subset_isoClosure _ _
      (S.extâ‚‚' (Triangle.mk (eâ‚.inv â‰« T.morâ‚) (T.morâ‚‚ â‰« eâ‚ƒ.hom) (eâ‚ƒ.inv â‰« T.morâ‚ƒ â‰« eâ‚.homâŸ¦1âŸ§'))
      (isomorphic_distinguished _ hT _
        (Triangle.isoMk _ _ eâ‚.symm (Iso.refl _) eâ‚ƒ.symm (by aesop_cat) (by aesop_cat) (by
          dsimp
          simp only [assoc, Iso.cancel_iso_inv_left, â† Functor.map_comp, eâ‚.hom_inv_id,
            Functor.map_id, comp_id]))) hâ‚ hâ‚ƒ)

instance : ClosedUnderIsomorphisms S.isoClosure.P := by
  dsimp only [isoClosure]
  infer_instance

section

variable (P : C â†’ Prop) (zero : P 0)
  (shift : âˆ€ (X : C) (n : â„¤), P X â†’ P (XâŸ¦nâŸ§))
  (extâ‚‚ : âˆ€ (T : Triangle C) (_ : T âˆˆ distTriang C), P T.objâ‚ â†’ P T.objâ‚ƒ â†’ P T.objâ‚‚)

/-- An alternative constructor for "strictly full" triangulated subcategory. -/
def mk' : Subcategory C where
  P := P
  zero' := âŸ¨0, isZero_zero _, zeroâŸ©
  shift := shift
  extâ‚‚' T hT hâ‚ hâ‚ƒ := subset_isoClosure P _ (extâ‚‚ T hT hâ‚ hâ‚ƒ)

instance : ClosedUnderIsomorphisms (mk' P zero shift extâ‚‚).P where
  mem_of_iso {X Y} e hX := by
    refine' extâ‚‚ (Triangle.mk e.hom (0 : Y âŸ¶ 0) 0) _ hX zero
    refine' isomorphic_distinguished _ (contractible_distinguished X) _ _
    exact Triangle.isoMk _ _ (Iso.refl _) e.symm (Iso.refl _)

end

@[simp]
lemma shift_iff [ClosedUnderIsomorphisms S.P] (X : C) (n : â„¤) :
    S.P (XâŸ¦nâŸ§) â†” S.P X := by
  constructor
  Â· intro h
    exact mem_of_iso _ ((shiftEquiv C n).unitIso.symm.app X) (S.shift _ (-n) h)
  Â· exact S.shift X n

lemma extâ‚‚ [ClosedUnderIsomorphisms S.P]
    (T : Triangle C) (hT : T âˆˆ distTriang C) (hâ‚ : S.P T.objâ‚)
    (hâ‚ƒ : S.P T.objâ‚ƒ) : S.P T.objâ‚‚ := by
  simpa only [isoClosure_eq_self] using S.extâ‚‚' T hT hâ‚ hâ‚ƒ

/-- Given `S : Triangulated.Subcategory C`, this is the class of morphisms on `C` which
consists of morphisms whose cone satisfies `S.P`. -/
def W : MorphismProperty C := fun X Y f => âˆƒ (Z : C) (g : Y âŸ¶ Z) (h : Z âŸ¶ XâŸ¦(1 : â„¤)âŸ§)
  (_ : Triangle.mk f g h âˆˆ distTriang C), S.P Z

lemma W_iff {X Y : C} (f : X âŸ¶ Y) :
    S.W f â†” âˆƒ (Z : C) (g : Y âŸ¶ Z) (h : Z âŸ¶ XâŸ¦(1 : â„¤)âŸ§)
      (_ : Triangle.mk f g h âˆˆ distTriang C), S.P Z := by rfl

lemma W_iff' {Y Z : C} (g : Y âŸ¶ Z) :
    S.W g â†” âˆƒ (X : C) (f : X âŸ¶ Y) (h : Z âŸ¶ XâŸ¦(1 : â„¤)âŸ§)
      (_ : Triangle.mk f g h âˆˆ distTriang C), S.P X := by
  rw [S.W_iff]
  constructor
  Â· rintro âŸ¨Z, g, h, H, memâŸ©
    exact âŸ¨_, _, _, inv_rot_of_distTriang _ H, S.shift _ (-1) memâŸ©
  Â· rintro âŸ¨Z, g, h, H, memâŸ©
    exact âŸ¨_, _, _, rot_of_distTriang _ H, S.shift _ 1 memâŸ©

lemma W.mk {T : Triangle C} (hT : T âˆˆ distTriang C) (h : S.P T.objâ‚ƒ) : S.W T.morâ‚ :=
  âŸ¨_, _, _, hT, hâŸ©

lemma W.mk' {T : Triangle C} (hT : T âˆˆ distTriang C) (h : S.P T.objâ‚) : S.W T.morâ‚‚ := by
  rw [W_iff']
  exact âŸ¨_, _, _, hT, hâŸ©

lemma isoClosure_W : S.isoClosure.W = S.W := by
  ext X Y f
  constructor
  Â· rintro âŸ¨Z, g, h, mem, âŸ¨Z', hZ', âŸ¨eâŸ©âŸ©âŸ©
    refine' âŸ¨Z', g â‰« e.hom, e.inv â‰« h, isomorphic_distinguished _ mem _ _, hZ'âŸ©
    exact Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) e.symm
  Â· rintro âŸ¨Z, g, h, mem, hZâŸ©
    exact âŸ¨Z, g, h, mem, subset_isoClosure _ _ hZâŸ©

lemma respectsIso_W : S.W.RespectsIso where
  left := by
    rintro X' X Y e f âŸ¨Z, g, h, mem, mem'âŸ©
    refine' âŸ¨Z, g, h â‰« e.invâŸ¦(1 : â„¤)âŸ§', isomorphic_distinguished _ mem _ _, mem'âŸ©
    refine' Triangle.isoMk _ _ e (Iso.refl _) (Iso.refl _) (by aesop_cat) (by aesop_cat) _
    dsimp
    simp only [assoc, â† Functor.map_comp, e.inv_hom_id, Functor.map_id, comp_id, id_comp]
  right := by
    rintro X Y Y' e f âŸ¨Z, g, h, mem, mem'âŸ©
    refine' âŸ¨Z, e.inv â‰« g, h, isomorphic_distinguished _ mem _ _, mem'âŸ©
    exact Triangle.isoMk _ _ (Iso.refl _) e.symm (Iso.refl _)

instance : S.W.ContainsIdentities := by
  rw [â† isoClosure_W]
  exact âŸ¨fun X => âŸ¨_, _, _, contractible_distinguished X, zero _âŸ©âŸ©

lemma W_of_isIso {X Y : C} (f : X âŸ¶ Y) [IsIso f] : S.W f := by
  refine (S.respectsIso_W.arrow_mk_iso_iff ?_).1 (MorphismProperty.id_mem _ X)
  exact Arrow.isoMk (Iso.refl _) (asIso f)

lemma smul_mem_W_iff {X Y : C} (f : X âŸ¶ Y) (n : â„¤Ë£) :
    S.W (n â€¢ f) â†” S.W f :=
  S.respectsIso_W.arrow_mk_iso_iff (Arrow.isoMk (n â€¢ (Iso.refl _)) (Iso.refl _))

variable {S}

lemma W.shift {Xâ‚ Xâ‚‚ : C} {f : Xâ‚ âŸ¶ Xâ‚‚} (hf : S.W f) (n : â„¤) : S.W (fâŸ¦nâŸ§') := by
  rw [â† smul_mem_W_iff _ _ (n.negOnePow)]
  obtain âŸ¨Xâ‚ƒ, g, h, hT, memâŸ© := hf
  exact âŸ¨_, _, _, Pretriangulated.Triangle.shift_distinguished _ hT n, S.shift _ _ memâŸ©

lemma W.unshift {Xâ‚ Xâ‚‚ : C} {f : Xâ‚ âŸ¶ Xâ‚‚} {n : â„¤} (hf : S.W (fâŸ¦nâŸ§')) : S.W f :=
  (S.respectsIso_W.arrow_mk_iso_iff
     (Arrow.isoOfNatIso (shiftEquiv C n).unitIso (Arrow.mk f))).2 (hf.shift (-n))

instance : S.W.IsCompatibleWithShift â„¤ where
  condition n := by
    ext K L f
    exact âŸ¨fun hf => hf.unshift, fun hf => hf.shift nâŸ©

instance [IsTriangulated C] : S.W.IsMultiplicative where
  stableUnderComposition := by
    rw [â† isoClosure_W]
    rintro Xâ‚ Xâ‚‚ Xâ‚ƒ uâ‚â‚‚ uâ‚‚â‚ƒ âŸ¨Zâ‚â‚‚, vâ‚â‚‚, wâ‚â‚‚, Hâ‚â‚‚, memâ‚â‚‚âŸ© âŸ¨Zâ‚‚â‚ƒ, vâ‚‚â‚ƒ, wâ‚‚â‚ƒ, Hâ‚‚â‚ƒ, memâ‚‚â‚ƒâŸ©
    obtain âŸ¨Zâ‚â‚ƒ, vâ‚â‚ƒ, wâ‚â‚‚, Hâ‚â‚ƒâŸ© := distinguished_cocone_triangle (uâ‚â‚‚ â‰« uâ‚‚â‚ƒ)
    exact âŸ¨_, _, _, Hâ‚â‚ƒ, S.isoClosure.extâ‚‚ _ (someOctahedron rfl Hâ‚â‚‚ Hâ‚‚â‚ƒ Hâ‚â‚ƒ).mem memâ‚â‚‚ memâ‚‚â‚ƒâŸ©

variable (S)

lemma mem_W_iff_of_distinguished
    [ClosedUnderIsomorphisms S.P] (T : Triangle C) (hT : T âˆˆ distTriang C) :
    S.W T.morâ‚ â†” S.P T.objâ‚ƒ := by
  constructor
  Â· rintro âŸ¨Z, g, h, hT', memâŸ©
    obtain âŸ¨e, _âŸ© := exists_iso_of_arrow_iso _ _ hT' hT (Iso.refl _)
    exact mem_of_iso S.P (Triangle.Ï€â‚ƒ.mapIso e) mem
  Â· intro h
    exact âŸ¨_, _, _, hT, hâŸ©

lemma mem_W_iff_of_distinguished'
    [ClosedUnderIsomorphisms S.P] (T : Triangle C) (hT : T âˆˆ distTriang C) :
    S.W T.morâ‚‚ â†” S.P T.objâ‚ := by
  have := S.mem_W_iff_of_distinguished _ (rot_of_distTriang _ hT)
  dsimp at this
  rw [this, shift_iff]

instance [IsTriangulated C] : S.W.HasLeftCalculusOfFractions where
  exists_leftFraction X Y Ï† := by
    obtain âŸ¨Z, f, g, H, memâŸ© := Ï†.hs
    obtain âŸ¨Y', s', f', mem'âŸ© := distinguished_cocone_triangleâ‚‚ (g â‰« Ï†.fâŸ¦1âŸ§')
    obtain âŸ¨b, âŸ¨hbâ‚, _âŸ©âŸ© :=
      complete_distinguished_triangle_morphismâ‚‚ _ _ H mem' Ï†.f (ğŸ™ Z) (by simp)
    exact âŸ¨MorphismProperty.LeftFraction.mk b s' âŸ¨_, _, _, mem', memâŸ©, hbâ‚.symmâŸ©
  ext := by
    rintro X' X Y fâ‚ fâ‚‚ s âŸ¨Z, g, h, H, memâŸ© hfâ‚
    have hfâ‚‚ : s â‰« (fâ‚ - fâ‚‚) = 0 := by rw [comp_sub, hfâ‚, sub_self]
    obtain âŸ¨q, hqâŸ© := Triangle.yoneda_exactâ‚‚ _ H _ hfâ‚‚
    obtain âŸ¨Y', r, t, mem'âŸ© := distinguished_cocone_triangle q
    refine' âŸ¨Y', r, _, _âŸ©
    Â· exact âŸ¨_, _, _, rot_of_distTriang _ mem', S.shift _ _ memâŸ©
    Â· have eq := comp_distTriang_mor_zeroâ‚â‚‚ _ mem'
      dsimp at eq
      rw [â† sub_eq_zero, â† sub_comp, hq, assoc, eq, comp_zero]

instance [IsTriangulated C] : S.W.HasRightCalculusOfFractions where
  exists_rightFraction X Y Ï† := by
    obtain âŸ¨Z, f, g, H, memâŸ© := Ï†.hs
    obtain âŸ¨X', f', h', mem'âŸ© := distinguished_cocone_triangleâ‚ (Ï†.f â‰« f)
    obtain âŸ¨a, âŸ¨haâ‚, _âŸ©âŸ© := complete_distinguished_triangle_morphismâ‚ _ _
      mem' H Ï†.f (ğŸ™ Z) (by simp)
    exact âŸ¨MorphismProperty.RightFraction.mk f' âŸ¨_, _, _, mem', memâŸ© a, haâ‚âŸ©
  ext Y Z Z' fâ‚ fâ‚‚ s hs hfâ‚ := by
    rw [S.W_iff'] at hs
    obtain âŸ¨Z, g, h, H, memâŸ© := hs
    have hfâ‚‚ : (fâ‚ - fâ‚‚) â‰« s = 0 := by rw [sub_comp, hfâ‚, sub_self]
    obtain âŸ¨q, hqâŸ© := Triangle.coyoneda_exactâ‚‚ _ H _ hfâ‚‚
    obtain âŸ¨Y', r, t, mem'âŸ© := distinguished_cocone_triangleâ‚ q
    refine' âŸ¨Y', r, _, _âŸ©
    Â· exact âŸ¨_, _, _, mem', memâŸ©
    Â· have eq := comp_distTriang_mor_zeroâ‚â‚‚ _ mem'
      dsimp at eq
      rw [â† sub_eq_zero, â† comp_sub, hq, reassoc_of% eq, zero_comp]

instance [IsTriangulated C] : S.W.IsCompatibleWithTriangulation := âŸ¨by
  rintro Tâ‚ Tâ‚ƒ memâ‚ memâ‚ƒ a b âŸ¨Zâ‚…, gâ‚…, hâ‚…, memâ‚…, memâ‚…'âŸ© âŸ¨Zâ‚„, gâ‚„, hâ‚„, memâ‚„, memâ‚„'âŸ© comm
  obtain âŸ¨Zâ‚‚, gâ‚‚, hâ‚‚, memâ‚‚âŸ© := distinguished_cocone_triangle (Tâ‚.morâ‚ â‰« b)
  have H := someOctahedron rfl memâ‚ memâ‚„ memâ‚‚
  have H' := someOctahedron comm.symm memâ‚… memâ‚ƒ memâ‚‚
  let Ï† : Tâ‚ âŸ¶ Tâ‚ƒ := H.triangleMorphismâ‚ â‰« H'.triangleMorphismâ‚‚
  exact âŸ¨Ï†.homâ‚ƒ, S.W.comp_mem _ _ (W.mk S H.mem memâ‚„') (W.mk' S H'.mem memâ‚…'),
    by simpa [Ï†] using Ï†.commâ‚‚, by simpa [Ï†] using Ï†.commâ‚ƒâŸ©âŸ©

section

variable (T : Triangle C) (hT : T âˆˆ distTriang C)

lemma extâ‚ [ClosedUnderIsomorphisms S.P] (hâ‚‚ : S.P T.objâ‚‚) (hâ‚ƒ : S.P T.objâ‚ƒ) :
    S.P T.objâ‚ :=
  S.extâ‚‚ _ (inv_rot_of_distTriang _ hT) (S.shift _ _ hâ‚ƒ) hâ‚‚

lemma extâ‚ƒ [ClosedUnderIsomorphisms S.P] (hâ‚ : S.P T.objâ‚) (hâ‚‚ : S.P T.objâ‚‚) :
    S.P T.objâ‚ƒ :=
  S.extâ‚‚ _ (rot_of_distTriang _ hT) hâ‚‚ (S.shift _ _ hâ‚)

lemma extâ‚' (hâ‚‚ : S.P T.objâ‚‚) (hâ‚ƒ : S.P T.objâ‚ƒ) :
    CategoryTheory.isoClosure S.P T.objâ‚ :=
  S.extâ‚‚' _ (inv_rot_of_distTriang _ hT) (S.shift _ _ hâ‚ƒ) hâ‚‚

lemma extâ‚ƒ' (hâ‚ : S.P T.objâ‚) (hâ‚‚ : S.P T.objâ‚‚) :
    CategoryTheory.isoClosure S.P T.objâ‚ƒ :=
  S.extâ‚‚' _ (rot_of_distTriang _ hT) hâ‚‚ (S.shift _ _ hâ‚)

lemma binary_product_stable [ClosedUnderIsomorphisms S.P] (Xâ‚ Xâ‚‚ : C) (hXâ‚ : S.P Xâ‚) (hXâ‚‚ : S.P Xâ‚‚) :
    S.P (Xâ‚ â¨¯ Xâ‚‚)  :=
  S.extâ‚‚ _ (binaryProductTriangle_distinguished Xâ‚ Xâ‚‚) hXâ‚ hXâ‚‚

lemma pi_finite_stable [ClosedUnderIsomorphisms S.P] {J : Type} [Finite J] (X : J â†’ C) (hX : âˆ€ j, S.P (X j)) :
    S.P (âˆ X) := by
  revert hX X
  let P : Type â†’ Prop := fun J =>
    âˆ€ [hJ : Finite J] (X : J â†’ C) (_ : âˆ€ j, S.P (X j)), S.P (âˆ X)
  change P J
  apply @Finite.induction_empty_option
  Â· intro Jâ‚ Jâ‚‚ e hJâ‚ _ X hX
    have : Finite Jâ‚ := Finite.of_equiv _ e.symm
    exact mem_of_iso _ (productIsoOfEquiv X e) (hJâ‚ (fun jâ‚ => X (e jâ‚)) (fun jâ‚ => hX _))
  Â· intro _ X _
    refine' mem_of_iso _ (IsZero.isoZero _).symm S.zero
    rw [IsZero.iff_id_eq_zero]
    ext âŸ¨âŸ©
  Â· intro J _ hJ _ X hX
    exact mem_of_iso _ (productOptionIso  X).symm
      (S.binary_product_stable _ _ (hJ (fun j => X (some j)) (fun j => hX _)) (hX none))

instance : S.W.IsStableUnderFiniteProducts := by
  rw [â† isoClosure_W]
  exact âŸ¨fun J _ => by
    refine' MorphismProperty.IsStableUnderProductsOfShape.mk _ _ (S.isoClosure.respectsIso_W) _
    intro Xâ‚ Xâ‚‚ f hf
    exact W.mk _ (productTriangle_distinguished _
      (fun j => (hf j).choose_spec.choose_spec.choose_spec.choose))
      (pi_finite_stable _ _ (fun j => (hf j).choose_spec.choose_spec.choose_spec.choose_spec))âŸ©

section

variable [IsTriangulated C]
example : Pretriangulated (S.W.Localization) := inferInstance
example : IsTriangulated (S.W.Localization) := inferInstance
example : S.W.Q.IsTriangulated := inferInstance

end

end

def category := FullSubcategory S.P

instance : Category S.category := FullSubcategory.category _

def Î¹ : S.category â¥¤ C := fullSubcategoryInclusion _

instance : Full S.Î¹ := FullSubcategory.full _
instance : Faithful S.Î¹ := FullSubcategory.faithful _

instance : Preadditive S.category := by
  dsimp [category]
  infer_instance

instance : S.Î¹.Additive := by
  dsimp [Î¹, category]
  infer_instance

noncomputable instance hasShift : HasShift S.category â„¤ :=
  hasShiftOfFullyFaithful S.Î¹ (fun n => FullSubcategory.lift _ (S.Î¹ â‹™ shiftFunctor C n)
    (fun X => S.shift _ _ X.2)) (fun _ => FullSubcategory.lift_comp_inclusion _ _ _)

instance commShiftÎ¹ : S.Î¹.CommShift â„¤ :=
  Functor.CommShift.of_hasShiftOfFullyFaithful _ _ _

-- these definitions are made irreducible to prevent (at least temporarily) any abuse of defeq
attribute [irreducible] hasShift commShiftÎ¹

instance (n : â„¤) : (shiftFunctor S.category n).Additive := by
  have := Functor.additive_of_iso (S.Î¹.commShiftIso n).symm
  apply Functor.additive_of_comp_faithful _ S.Î¹

instance : HasZeroObject S.category where
  zero := by
    obtain âŸ¨Z, hZ, memâŸ© := S.zero'
    refine' âŸ¨âŸ¨Z, memâŸ©, _âŸ©
    rw [IsZero.iff_id_eq_zero]
    apply hZ.eq_of_src

instance : Pretriangulated S.category where
  distinguishedTriangles := fun T => S.Î¹.mapTriangle.obj T âˆˆ distTriang C
  isomorphic_distinguished := fun Tâ‚ hTâ‚ Tâ‚‚ e =>
    isomorphic_distinguished _ hTâ‚ _ (S.Î¹.mapTriangle.mapIso e)
  contractible_distinguished X := by
    refine' isomorphic_distinguished _ (contractible_distinguished (S.Î¹.obj X)) _ _
    exact Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) S.Î¹.mapZeroObject
      (by aesop_cat) (by aesop_cat) (by aesop_cat)
  distinguished_cocone_triangle {X Y} f := by
    obtain âŸ¨Z', g', h', memâŸ© := distinguished_cocone_triangle (S.Î¹.map f)
    obtain âŸ¨Z'', hZ'', âŸ¨eâŸ©âŸ© := S.extâ‚ƒ' _ mem X.2 Y.2
    let Z : S.category := âŸ¨Z'', hZ''âŸ©
    refine' âŸ¨Z, S.Î¹.preimage (g' â‰« e.hom),
      S.Î¹.preimage (e.inv â‰« h' â‰« (S.Î¹.commShiftIso (1 : â„¤)).inv.app X),
      isomorphic_distinguished _ mem _ _âŸ©
    exact Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) e.symm
      (by aesop_cat) (by aesop_cat) (by aesop_cat)
  rotate_distinguished_triangle T :=
    (rotate_distinguished_triangle (S.Î¹.mapTriangle.obj T)).trans
      (distinguished_iff_of_iso (S.Î¹.mapTriangleRotateIso.app T))
  complete_distinguished_triangle_morphism Tâ‚ Tâ‚‚ hTâ‚ hTâ‚‚ a b comm := by
    obtain âŸ¨c, âŸ¨hcâ‚, hcâ‚‚âŸ©âŸ© := complete_distinguished_triangle_morphism (S.Î¹.mapTriangle.obj Tâ‚)
      (S.Î¹.mapTriangle.obj Tâ‚‚) hTâ‚ hTâ‚‚ (S.Î¹.map a) (S.Î¹.map b)
      (by simpa using S.Î¹.congr_map comm)
    have âŸ¨c', hc'âŸ© : âˆƒ (c' : Tâ‚.objâ‚ƒ âŸ¶ Tâ‚‚.objâ‚ƒ), c = S.Î¹.map c' := âŸ¨S.Î¹.preimage c, by simpâŸ©
    dsimp at hcâ‚ hcâ‚‚
    rw [hc'] at hcâ‚
    rw [hc', assoc, â† Functor.commShiftIso_hom_naturality] at hcâ‚‚
    refine' âŸ¨c', âŸ¨S.Î¹.map_injective _, S.Î¹.map_injective _âŸ©âŸ©
    Â· simpa using hcâ‚
    Â· rw [â† cancel_mono ((Functor.commShiftIso (Î¹ S) (1 : â„¤)).hom.app Tâ‚‚.objâ‚),
        S.Î¹.map_comp, S.Î¹.map_comp, assoc, assoc, hcâ‚‚]

instance : S.Î¹.IsTriangulated := âŸ¨fun _ hT => hTâŸ©

instance [IsTriangulated C] : IsTriangulated S.category :=
  IsTriangulated.of_fully_faithful_triangulated_functor S.Î¹


section

variable {D : Type*} [Category D] [HasZeroObject D] [Preadditive D]
    [HasShift D â„¤] [âˆ€ (n : â„¤), (shiftFunctor D n).Additive] [Pretriangulated D]
    (F : C â¥¤ D) [F.CommShift â„¤] [F.IsTriangulated] [Full F]

-- note: does not require `[Faithful F]` !

def essImage : Subcategory D :=
  Subcategory.mk' F.essImage âŸ¨0, âŸ¨F.mapZeroObjectâŸ©âŸ©
    (fun X n âŸ¨Y, âŸ¨eâŸ©âŸ© => âŸ¨(shiftFunctor C n).obj Y,
      âŸ¨(F.commShiftIso n).app Y â‰ªâ‰« (shiftFunctor D n).mapIso eâŸ©âŸ©)
    (fun T hT âŸ¨Xâ‚, âŸ¨eâ‚âŸ©âŸ© âŸ¨Xâ‚ƒ, âŸ¨eâ‚ƒâŸ©âŸ© => by
      have âŸ¨h, hhâŸ© := F.map_surjective (eâ‚ƒ.hom â‰« T.morâ‚ƒ â‰« eâ‚.invâŸ¦1âŸ§' â‰«
        (F.commShiftIso (1 : â„¤)).inv.app Xâ‚)
      obtain âŸ¨Xâ‚‚, f, g, HâŸ© := distinguished_cocone_triangleâ‚‚ h
      exact âŸ¨Xâ‚‚, âŸ¨Triangle.Ï€â‚‚.mapIso
        (isoTriangleOfIsoâ‚â‚ƒ _ _ (F.map_distinguished _ H) hT eâ‚ eâ‚ƒ (by
          dsimp
          simp only [hh, assoc, Iso.inv_hom_id_app, Functor.comp_obj,
            comp_id, Iso.cancel_iso_hom_left, â† Functor.map_comp,
            Iso.inv_hom_id, Functor.map_id]))âŸ©âŸ©)

instance : ClosedUnderIsomorphisms (essImage F).P  := by
  dsimp only [essImage]
  infer_instance

end

section

variable {D : Type*} [Category D] (F : D â¥¤ C) (hF : âˆ€ (X : D), S.P (F.obj X))

def lift : D â¥¤ S.category := FullSubcategory.lift S.P F hF

lemma lift_comp_inclusion_eq : S.lift F hF â‹™ S.Î¹ = F :=
  FullSubcategory.lift_comp_inclusion_eq _ _ _

def liftCompInclusion : S.lift F hF â‹™ S.Î¹ â‰… F := Iso.refl _

instance [Faithful F] : Faithful (S.lift F hF) :=
  Faithful.of_comp_iso (S.liftCompInclusion F hF)

instance [Full F] : Full (S.lift F hF) :=
  Full.ofCompFaithfulIso (S.liftCompInclusion F hF)

-- should be generalized
instance [Preadditive D] [F.Additive] : (S.lift F hF).Additive where
  map_add {X Y f g} := by
    apply S.Î¹.map_injective
    apply F.map_add

noncomputable instance [HasShift D â„¤] [F.CommShift â„¤] : (S.lift F hF).CommShift â„¤ :=
  Functor.CommShift.ofComp (S.liftCompInclusion F hF) â„¤

noncomputable instance [HasShift D â„¤] [F.CommShift â„¤] :
  NatTrans.CommShift (S.liftCompInclusion F hF).hom â„¤ :=
    Functor.CommShift.ofComp_compatibility _ _

instance isTriangulated_lift [HasShift D â„¤] [Preadditive D] [F.CommShift â„¤] [HasZeroObject D]
    [âˆ€ (n : â„¤), (shiftFunctor D n).Additive] [Pretriangulated D] [F.IsTriangulated]:
    (S.lift F hF).IsTriangulated := by
  rw [Functor.isTriangulated_iff_comp_right (S.liftCompInclusion F hF)]
  infer_instance

end

section

variable {D : Type*} [Category D] [Preadditive D] [HasZeroObject D] [HasShift D â„¤]
  [âˆ€ (n : â„¤), (shiftFunctor D n).Additive] [Pretriangulated D]
  (F : D â¥¤ C) [F.CommShift â„¤] [F.IsTriangulated]
  [ClosedUnderIsomorphisms S.P]

def inverseImage : Subcategory D :=
  Subcategory.mk' (fun X => S.P (F.obj X))
    (mem_of_iso _ F.mapZeroObject.symm S.zero)
    (fun X n hX => mem_of_iso _ ((F.commShiftIso n).symm.app X) (S.shift _ n hX))
    (fun _ hT hâ‚ hâ‚ƒ => S.extâ‚‚ _ (F.map_distinguished _ hT) hâ‚ hâ‚ƒ)

lemma mem_inverseImage_iff (X : D) :
    (S.inverseImage F).P X â†” S.P (F.obj X) := by rfl

instance : ClosedUnderIsomorphisms (S.inverseImage F).P where
  mem_of_iso {X Y} e hX := by
    rw [mem_inverseImage_iff] at hX âŠ¢
    exact mem_of_iso _ (F.mapIso e) hX

lemma mem_inverseImage_W_iff {X Y : D} (s : X âŸ¶ Y) :
    (S.inverseImage F).W s â†” S.W (F.map s) := by
  obtain âŸ¨Z, g, h, hTâŸ© := distinguished_cocone_triangle s
  have eqâ‚ := (S.inverseImage F).mem_W_iff_of_distinguished _ hT
  have eqâ‚‚ := S.mem_W_iff_of_distinguished _ (F.map_distinguished _ hT)
  dsimp at eqâ‚ eqâ‚‚
  rw [eqâ‚, mem_inverseImage_iff, eqâ‚‚]

lemma inverseImage_W_isInverted {E : Type*} [Category E]
    (L : C â¥¤ E) [L.IsLocalization S.W] :
    (S.inverseImage F).W.IsInvertedBy (F â‹™ L) :=
  fun X Y f hf => Localization.inverts L S.W (F.map f)
    (by simpa only [mem_inverseImage_W_iff] using hf)

end

section

variable {D : Type*} [Category D] [Preadditive D] [HasZeroObject D] [HasShift D â„¤]
  [âˆ€ (n : â„¤), (shiftFunctor D n).Additive] [Pretriangulated D]
  {F G : C â¥¤ D} [F.CommShift â„¤] [G.CommShift â„¤] [F.IsTriangulated]
  [G.IsTriangulated] (Ï„ : F âŸ¶ G) [NatTrans.CommShift Ï„ â„¤]

def ofNatTrans : Subcategory C :=
  Subcategory.mk' (fun X => IsIso (Ï„.app X))
    âŸ¨0, by rw [comp_zero, â† F.map_id, id_zero, F.map_zero],
        by rw [zero_comp, â† G.map_id, id_zero, G.map_zero]âŸ©
    (fun X n (_ : IsIso (Ï„.app X)) => by
      dsimp
      rw [NatTrans.CommShift.app_shift Ï„ n]
      infer_instance)
    (fun T hT hâ‚ hâ‚ƒ => by
      exact Pretriangulated.isIsoâ‚‚_of_isIsoâ‚â‚ƒ (by
        refine' (Pretriangulated.Triangle.homMk _ _ (Ï„.app _) (Ï„.app _) (Ï„.app _) (by simp) (by simp)
          (by simp [NatTrans.CommShift.comm_app Ï„])))
        (F.map_distinguished _ hT) (G.map_distinguished _ hT) (by exact hâ‚) (by exact hâ‚ƒ))

instance : ClosedUnderIsomorphisms (ofNatTrans Ï„).P := by
  dsimp [ofNatTrans]
  infer_instance

end

end Subcategory

end Triangulated

end CategoryTheory
