import Mathlib.CategoryTheory.Localization.Triangulated
import Mathlib.CategoryTheory.RespectsIso

namespace CategoryTheory

open Category Limits Preadditive ZeroObject

namespace Limits

variable {C Jâ‚ Jâ‚‚ : Type _} [Category C]
  (X : Jâ‚‚ â†’ C) (e : Jâ‚ â‰ƒ Jâ‚‚) [HasProduct X]

noncomputable def fanOfEquiv : Fan (X âˆ˜ e) := Fan.mk (âˆ X) (fun _ => Pi.Ï€ _ _)

@[simp]
lemma fanOfEquiv_proj (j : Jâ‚) : (fanOfEquiv X e).proj j = Pi.Ï€ _ (e j) := rfl

@[reassoc]
lemma Fan.congr_proj {J : Type _} {F : J â†’ C} (s : Fan F)
    {jâ‚ jâ‚‚ : J} (h : jâ‚ = jâ‚‚) : s.proj jâ‚ â‰« eqToHom (by rw [h]) = s.proj jâ‚‚ := by
  subst h
  simp

@[reassoc]
lemma Pi.congr_Ï€ {J : Type _} (F : J â†’ C) [HasProduct F] {jâ‚ jâ‚‚ : J} (h : jâ‚ = jâ‚‚) :
    Pi.Ï€ F jâ‚ â‰« eqToHom (by rw [h]) = Pi.Ï€ F jâ‚‚ := by
  subst h
  simp

noncomputable def isLimitFanOfEquiv : IsLimit (fanOfEquiv X e) :=
  mkFanLimit _ (fun s => Pi.lift (fun jâ‚‚ => s.proj (e.symm jâ‚‚) â‰« eqToHom (by simp) ))
    (fun s j => by simp [Fan.congr_proj _ (e.symm_apply_apply j)])
    (fun s m hm => Pi.hom_ext _ _ (fun j => by
      dsimp
      simp only [limit.lift_Ï€, Fan.mk_pt, Fan.mk_Ï€_app, â† hm,
        Function.comp_apply, fanOfEquiv_proj, assoc]
      rw [Pi.congr_Ï€]
      simp))

lemma hasProductOfEquiv : HasProduct (X âˆ˜ e) :=
  âŸ¨âŸ¨_, isLimitFanOfEquiv X eâŸ©âŸ©

noncomputable def productIsoOfEquiv [HasProduct (X âˆ˜ e)] :  âˆ (X âˆ˜ e) â‰… âˆ X :=
  IsLimit.conePointUniqueUpToIso (limit.isLimit _) (isLimitFanOfEquiv X e)

noncomputable def productOptionIso {C J : Type _} [Category C]
    (X : Option J â†’ C) [HasProduct X] [HasProduct (fun j => X (some j))]
    [HasBinaryProduct (âˆ (fun j => X (some j))) (X none)] :
    (âˆ X) â‰… (âˆ (fun j => X (some j))) â¨¯ (X none) where
  hom := prod.lift (Pi.lift (fun j => Pi.Ï€ _ (some j))) (Pi.Ï€ _ none)
  inv := Pi.lift (fun b => match b with
    | some j => prod.fst â‰« Pi.Ï€ _ j
    | none => prod.snd)

end Limits

namespace Arrow

-- should be moved to Arrow.lean

variable {C D : Type _} [Category C] [Category D]

@[simps]
def mapArrowNatTransOfNatTrans {F G : C â¥¤ D} (Ï„ : F âŸ¶ G) : F.mapArrow âŸ¶ G.mapArrow where
  app f :=
    { left := Ï„.app _
      right := Ï„.app _}

@[simps]
def mapArrowNatIsoOfNatIso {F G : C â¥¤ D} (e : F â‰… G) : F.mapArrow â‰… G.mapArrow where
  hom := mapArrowNatTransOfNatTrans e.hom
  inv := mapArrowNatTransOfNatTrans e.inv

end Arrow

namespace Triangulated

open Pretriangulated

variable (C : Type _) [Category C] [HasZeroObject C] [HasShift C â„¤]
  [Preadditive C] [âˆ€ (n : â„¤), (shiftFunctor C n).Additive] [Pretriangulated C]

structure Subcategory where
  set : Set C
  zero : 0 âˆˆ set
  shift : âˆ€ (X : C) (n : â„¤) (_ : X âˆˆ set), XâŸ¦nâŸ§ âˆˆ set
  extâ‚‚ : âˆ€ (T : Triangle C) (_ : T âˆˆ distTriang C), T.objâ‚ âˆˆ set â†’ T.objâ‚ƒ âˆˆ set â†’ T.objâ‚‚ âˆˆ set

namespace Subcategory

variable {C}
variable (S : Subcategory C)

instance : S.set.RespectsIso := âŸ¨fun X Y e hX => by
  refine' S.extâ‚‚ (Triangle.mk e.hom (0 : Y âŸ¶ 0) 0) _ hX S.zero
  refine' isomorphic_distinguished _ (contractible_distinguished X) _ _
  exact Triangle.isoMk _ _ (Iso.refl _) e.symm (Iso.refl _)
    (by aesop_cat) (by aesop_cat) (by aesop_cat)âŸ©

lemma zero' (X : C) (hX : IsZero X) : X âˆˆ S.set :=
  Set.mem_of_iso S.set hX.isoZero.symm S.zero

def W : MorphismProperty C := fun X Y f => âˆƒ (Z : C) (g : Y âŸ¶ Z) (h : Z âŸ¶ XâŸ¦(1 : â„¤)âŸ§)
  (_ : Triangle.mk f g h âˆˆ distTriang C), Z âˆˆ S.set

def W' : MorphismProperty C := fun Y Z g => âˆƒ (X : C) (f : X âŸ¶ Y) (h : Z âŸ¶ XâŸ¦(1 : â„¤)âŸ§)
  (_ : Triangle.mk f g h âˆˆ distTriang C), X âˆˆ S.set

variable {S}

lemma W.mk {T : Triangle C} (hT : T âˆˆ distTriang C) (h : T.objâ‚ƒ âˆˆ S.set) : S.W T.morâ‚ :=
  âŸ¨T.objâ‚ƒ, T.morâ‚‚, T.morâ‚ƒ, hT, hâŸ©

lemma W'.mk {T : Triangle C} (hT : T âˆˆ distTriang C) (h : T.objâ‚ âˆˆ S.set) : S.W' T.morâ‚‚ :=
  âŸ¨T.objâ‚, T.morâ‚, T.morâ‚ƒ, hT, hâŸ©

noncomputable def W.triangle {X Y : C} (f : X âŸ¶ Y) (hf : S.W f) : Triangle C :=
  Triangle.mk f hf.choose_spec.choose hf.choose_spec.choose_spec.choose

lemma W.triangle_distinguished {X Y : C} (f : X âŸ¶ Y) (hf : S.W f) :
   (W.triangle f hf) âˆˆ distTriang C :=
  hf.choose_spec.choose_spec.choose_spec.choose

lemma W.triangle_objâ‚ƒ_mem {X Y : C} (f : X âŸ¶ Y) (hf : S.W f) :
  (W.triangle f hf).objâ‚ƒ âˆˆ S.set :=
  hf.choose_spec.choose_spec.choose_spec.choose_spec

variable (S)

lemma W_eq_W' : S.W = S.W' := by
  apply MorphismProperty.ext
  intro X Y f
  constructor
  . rintro âŸ¨Z, g, h, H, memâŸ©
    exact âŸ¨_, _, _, inv_rot_of_dist_triangle _ H, S.shift _ (-1) memâŸ©
  . rintro âŸ¨Z, g, h, H, memâŸ©
    exact âŸ¨_, _, _, rot_of_dist_triangle _ H, S.shift _ 1 memâŸ©

variable {S}

lemma W.mk' {T : Triangle C} (hT : T âˆˆ distTriang C) (h : T.objâ‚ âˆˆ S.set) : S.W T.morâ‚‚ := by
  simpa only [W_eq_W'] using W'.mk hT h

variable (S)


instance instContainsIdentitiesW : S.W.ContainsIdentities :=
  âŸ¨fun X => âŸ¨_, _, _, contractible_distinguished X, S.zeroâŸ©âŸ©

lemma stableUnderCompositionW [IsTriangulated C] : (W S).StableUnderComposition := by
  rintro Xâ‚ Xâ‚‚ Xâ‚ƒ uâ‚â‚‚ uâ‚‚â‚ƒ âŸ¨Zâ‚â‚‚, vâ‚â‚‚, wâ‚â‚‚, Hâ‚â‚‚, memâ‚â‚‚âŸ© âŸ¨Zâ‚‚â‚ƒ, vâ‚‚â‚ƒ, wâ‚‚â‚ƒ, Hâ‚‚â‚ƒ, memâ‚‚â‚ƒâŸ©
  obtain âŸ¨Zâ‚â‚ƒ, vâ‚â‚ƒ, wâ‚â‚‚, Hâ‚â‚ƒâŸ© := distinguished_cocone_triangle (uâ‚â‚‚ â‰« uâ‚‚â‚ƒ)
  refine' âŸ¨_, _, _, Hâ‚â‚ƒ, S.extâ‚‚ _ (someOctahedron rfl Hâ‚â‚‚ Hâ‚‚â‚ƒ Hâ‚â‚ƒ).mem memâ‚â‚‚ memâ‚‚â‚ƒâŸ©

instance multiplicativeW [IsTriangulated C] : S.W.IsMultiplicative where
  comp' := S.stableUnderCompositionW

lemma respectsIsoW : S.W.RespectsIso where
  left := by
    rintro X' X Y e f âŸ¨Z, g, h, mem, mem'âŸ©
    refine' âŸ¨Z, g, h â‰« e.invâŸ¦(1 : â„¤)âŸ§', isomorphic_distinguished _ mem _ _, mem'âŸ©
    refine' Triangle.isoMk _ _ e (Iso.refl _) (Iso.refl _) (by aesop_cat) (by aesop_cat) _
    dsimp
    simp only [assoc, â† Functor.map_comp, e.inv_hom_id, Functor.map_id, comp_id, id_comp]
  right := by
    rintro X Y Y' e f âŸ¨Z, g, h, mem, mem'âŸ©
    refine' âŸ¨Z, e.inv â‰« g, h, isomorphic_distinguished _ mem _ _, mem'âŸ©
    exact Triangle.isoMk _ _ (Iso.refl _) e.symm (Iso.refl _)
      (by aesop_cat) (by aesop_cat) (by aesop_cat)


instance [IsTriangulated C] : S.W.HasLeftCalculusOfFractions where
  nonempty_toSq := by
    rintro X' X Y s âŸ¨Z, f, g, H, memâŸ© u
    obtain âŸ¨Y', s', f', mem'âŸ© := distinguished_cocone_triangleâ‚‚ (g â‰« uâŸ¦1âŸ§')
    obtain âŸ¨b, âŸ¨hbâ‚, _âŸ©âŸ© :=
      complete_distinguished_triangle_morphismâ‚‚ _ _ H mem' u (ðŸ™ Z) (by simp)
    exact âŸ¨âŸ¨Y', b, s', âŸ¨Z, f', g â‰« uâŸ¦1âŸ§', mem', memâŸ©, hbâ‚.symmâŸ©âŸ©
  ext := by
    rintro X' X Y fâ‚ fâ‚‚ s âŸ¨Z, g, h, H, memâŸ© hfâ‚
    have hfâ‚‚ : s â‰« (fâ‚ - fâ‚‚) = 0 := by rw [comp_sub, hfâ‚, sub_self]
    obtain âŸ¨q, hqâŸ© := contravariant_yoneda_exactâ‚‚ _ H _ hfâ‚‚
    obtain âŸ¨Y', r, t, mem'âŸ© := distinguished_cocone_triangle q
    refine' âŸ¨Y', r, _, _âŸ©
    . exact âŸ¨_, _, _, rot_of_dist_triangle _ mem', S.shift _ _ memâŸ©
    . have eq := comp_dist_triangle_mor_zeroâ‚â‚‚ _ mem'
      dsimp at eq
      rw [â† sub_eq_zero, â† sub_comp, hq, assoc, eq, comp_zero]

instance [IsTriangulated C] : S.W.HasRightCalculusOfFractions where
  nonempty_toSq := by
    rintro X Y Y' s âŸ¨Z, f, g, H, memâŸ© u
    obtain âŸ¨X', f', h', mem'âŸ© := distinguished_cocone_triangleâ‚ (u â‰« f)
    obtain âŸ¨a, âŸ¨haâ‚, _âŸ©âŸ© := complete_distinguished_triangle_morphismâ‚ _ _ mem' H u (ðŸ™ Z) (by simp)
    exact âŸ¨âŸ¨X', a, f', âŸ¨Z, u â‰« f, h', mem', memâŸ©, haâ‚âŸ©âŸ©
  ext := by
    rintro Y Z Z' fâ‚ fâ‚‚ s hs hfâ‚
    have hfâ‚‚ : (fâ‚ - fâ‚‚) â‰« s = 0 := by rw [sub_comp, hfâ‚, sub_self]
    rw [W_eq_W'] at hs
    obtain âŸ¨Z, g, h, H, memâŸ© := hs
    obtain âŸ¨q, hqâŸ© := covariant_yoneda_exactâ‚‚ _ H _ hfâ‚‚
    obtain âŸ¨Y', r, t, mem'âŸ© := distinguished_cocone_triangleâ‚ q
    refine' âŸ¨Y', r, _, _âŸ©
    . exact âŸ¨_, _, _, mem', memâŸ©
    . have eq := comp_dist_triangle_mor_zeroâ‚â‚‚ _ mem'
      dsimp at eq
      rw [â† sub_eq_zero, â† comp_sub, hq, reassoc_of% eq, zero_comp]

lemma W_of_isIso {X Y : C} (f : X âŸ¶ Y) [IsIso f] : S.W f := by
  refine' âŸ¨0, 0, 0, isomorphic_distinguished _ (contractible_distinguished Y) _ _, S.zeroâŸ©
  exact Triangle.isoMk _ _ (asIso f) (Iso.refl _) (Iso.refl _)
    (by aesop_cat) (by aesop_cat) (by aesop_cat)

lemma mul_mem_W_iff {X Y : C} (f : X âŸ¶ Y) (n : â„¤) :
    S.W ((â†‘((-1 : Units â„¤)^n) : â„¤)  â€¢ f) â†” S.W f :=
  S.respectsIsoW.arrow_mk_iso_iff
    (Arrow.isoMk (Preadditive.mulIso ((-1 : Units â„¤)^n) (Iso.refl _)) (Iso.refl _)
      (by dsimp ; simp only [Preadditive.zsmul_comp, id_comp, comp_id]))

instance WIsCompatibleWIthShift : S.W.IsCompatibleWithShift â„¤ := âŸ¨by
  have : âˆ€ {X Y : C} (f : X âŸ¶ Y) (hf : S.W f) (n : â„¤), S.W (fâŸ¦nâŸ§') := by
    rintro X Y f âŸ¨Z, g, h, H, memâŸ© n
    rw [â† mul_mem_W_iff S _ n]
    exact âŸ¨_, _, _, shift_distinguished _ H n, S.shift _ n memâŸ©
  intro n
  apply MorphismProperty.ext
  intro X Y f
  constructor
  . intro hf
    have H := S.respectsIsoW.arrow_mk_iso_iff
     ((Arrow.mapArrowNatIsoOfNatIso (shiftEquiv C n).unitIso).app (Arrow.mk f))
    dsimp at H
    rw [H]
    exact this _ hf (-n)
  . intro hf
    exact this _ hf nâŸ©

variable {S}

lemma W.shift {Xâ‚ Xâ‚‚ : C} {f : Xâ‚ âŸ¶ Xâ‚‚} (hf : S.W f) (n : â„¤) : S.W (fâŸ¦nâŸ§') := by
  simpa only [MorphismProperty.IsCompatibleWithShift.iff S.W f n] using hf

lemma W.unshift {Xâ‚ Xâ‚‚ : C} {f : Xâ‚ âŸ¶ Xâ‚‚} {n : â„¤} (hf : S.W (fâŸ¦nâŸ§')) : S.W f := by
  simpa only [â† MorphismProperty.IsCompatibleWithShift.iff S.W f n] using hf

variable (S)

lemma binary_product_stable (Xâ‚ Xâ‚‚ : C) (hXâ‚ : Xâ‚ âˆˆ S.set) (hXâ‚‚ : Xâ‚‚ âˆˆ S.set) :
    (Xâ‚ â¨¯ Xâ‚‚) âˆˆ S.set :=
  S.extâ‚‚ _ (binaryProductTriangle_distinguished Xâ‚ Xâ‚‚) hXâ‚ hXâ‚‚

lemma pi_finite_stable {J : Type} [Finite J] (X : J â†’ C) (hX : âˆ€ j, X j âˆˆ S.set) :
    (âˆ X) âˆˆ S.set := by
  revert hX X
  let P : Type â†’ Prop := fun J =>
    âˆ€ [hJ : Finite J] (X : J â†’ C) (_ : âˆ€ j, X j âˆˆ S.set), (âˆ X) âˆˆ S.set
  change P J
  apply @Finite.induction_empty_option
  . intro Jâ‚ Jâ‚‚ e hJâ‚ _ X hX
    have : Finite Jâ‚ := Finite.of_equiv _ e.symm
    exact Set.mem_of_iso _ (productIsoOfEquiv X e) (hJâ‚ (fun jâ‚ => X (e jâ‚)) (fun jâ‚ => hX _))
  . intro _ X _
    refine' Set.mem_of_iso _ (IsZero.isoZero _).symm S.zero
    rw [IsZero.iff_id_eq_zero]
    ext âŸ¨âŸ©
  . intro J _ hJ _ X hX
    exact Set.mem_of_iso _ (productOptionIso  X).symm
      (S.binary_product_stable _ _ (hJ (fun j => X (some j)) (fun j => hX _)) (hX none))

instance : S.W.IsStableUnderFiniteProducts := âŸ¨fun J _ => by
  refine' MorphismProperty.IsStableUnderProductsOfShape.mk _ _ (S.respectsIsoW) _
  intro Xâ‚ Xâ‚‚ f hf
  exact W.mk (productTriangle_distinguished _ (fun j => W.triangle_distinguished _ (hf j)))
    (pi_finite_stable _ _ (fun j => W.triangle_objâ‚ƒ_mem _ _))âŸ©

instance [IsTriangulated C] : S.W.IsCompatibleWithTriangulation := âŸ¨by
  rintro Tâ‚ Tâ‚ƒ memâ‚ memâ‚ƒ a b âŸ¨Zâ‚…, gâ‚…, hâ‚…, memâ‚…, memâ‚…'âŸ© âŸ¨Zâ‚„, gâ‚„, hâ‚„, memâ‚„, memâ‚„'âŸ© comm
  obtain âŸ¨Zâ‚‚, gâ‚‚, hâ‚‚, memâ‚‚âŸ© := distinguished_cocone_triangle (Tâ‚.morâ‚ â‰« b)
  have H := someOctahedron rfl memâ‚ memâ‚„ memâ‚‚
  have H' := someOctahedron comm.symm memâ‚… memâ‚ƒ memâ‚‚
  let Ï† : Tâ‚ âŸ¶ Tâ‚ƒ := H.triangleMorphismâ‚ â‰« H'.triangleMorphismâ‚‚
  exact âŸ¨Ï†.homâ‚ƒ,
    MorphismProperty.IsMultiplicative.comp S.W _ _ (W.mk H.mem memâ‚„') (W.mk' H'.mem memâ‚…'),
    âŸ¨by simpa using Ï†.commâ‚‚, by simpa using Ï†.commâ‚ƒâŸ©âŸ©âŸ©

lemma extâ‚ (T : Triangle C) (hT : T âˆˆ distTriang C) (hâ‚‚ : T.objâ‚‚ âˆˆ S.set)
    (hâ‚ƒ : T.objâ‚ƒ âˆˆ S.set) : T.objâ‚ âˆˆ S.set :=
  S.extâ‚‚ _ (inv_rot_of_dist_triangle _ hT) (S.shift _ _ hâ‚ƒ) hâ‚‚

lemma extâ‚ƒ (T : Triangle C) (hT : T âˆˆ distTriang C) (hâ‚ : T.objâ‚ âˆˆ S.set)
    (hâ‚‚ : T.objâ‚‚ âˆˆ S.set) : T.objâ‚ƒ âˆˆ S.set :=
  S.extâ‚‚ _ (rot_of_dist_triangle _ hT) hâ‚‚ (S.shift _ _ hâ‚)

noncomputable example [IsTriangulated C] : Pretriangulated (S.W.Localization) := inferInstance

def category := FullSubcategory S.set

instance : Category S.category := FullSubcategory.category _

def Î¹ : S.category â¥¤ C := fullSubcategoryInclusion _

instance : Full S.Î¹ := FullSubcategory.full _
instance : Faithful S.Î¹ := FullSubcategory.faithful _

instance : Preadditive S.category := by
  dsimp [category]
  infer_instance

instance : S.Î¹.Additive := by
  dsimp [Î¹, category]
  infer_instance


noncomputable instance hasShift : HasShift S.category â„¤ :=
  hasShiftOfFullyFaithful S.Î¹ (fun n => FullSubcategory.lift _ (S.Î¹ â‹™ shiftFunctor C n)
    (fun X => S.shift _ _ X.2)) (fun _ => FullSubcategory.lift_comp_inclusion _ _ _)

instance commShiftÎ¹ : S.Î¹.CommShift â„¤ :=
  Functor.CommShift.of_hasShiftOfFullyFaithful _ _ _

-- these definitions are made irreducible to prevent (at least temporarily) any abuse of defeq
attribute [irreducible] hasShift commShiftÎ¹

instance (n : â„¤) : (shiftFunctor S.category n).Additive := by
  have := Functor.additive_of_iso (S.Î¹.commShiftIso n).symm
  apply Functor.additive_of_comp_faithful _ S.Î¹

instance : HasZeroObject S.category where
  zero := by
    refine' âŸ¨âŸ¨0, S.zeroâŸ©, _âŸ©
    rw [IsZero.iff_id_eq_zero]
    apply S.Î¹.map_injective
    simpa only [Functor.map_zero] using id_zero

instance : Pretriangulated S.category where
  distinguishedTriangles := fun T => S.Î¹.mapTriangle.obj T âˆˆ distTriang C
  isomorphic_distinguished := fun Tâ‚ hTâ‚ Tâ‚‚ e =>
    isomorphic_distinguished _ hTâ‚ _ (S.Î¹.mapTriangle.mapIso e)
  contractible_distinguished X := by
    refine' isomorphic_distinguished _ (contractible_distinguished (S.Î¹.obj X)) _ _
    exact Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) S.Î¹.mapZeroObject
      (by aesop_cat) (by aesop_cat) (by aesop_cat)
  distinguished_cocone_triangle {X Y} f := by
    obtain âŸ¨Z', g', h', memâŸ© := distinguished_cocone_triangle (S.Î¹.map f)
    let Z : S.category := âŸ¨Z', S.extâ‚ƒ _ mem X.2 Y.2âŸ©
    refine' âŸ¨Z, S.Î¹.preimage g', S.Î¹.preimage (h' â‰« (S.Î¹.commShiftIso (1 : â„¤)).inv.app X),
      isomorphic_distinguished _ mem _ _âŸ©
    exact Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) (Iso.refl _)
      (by aesop_cat) (by aesop_cat) (by aesop_cat)
  rotate_distinguished_triangle T :=
    (rotate_distinguished_triangle (S.Î¹.mapTriangle.obj T)).trans
      (distinguished_iff_of_iso (S.Î¹.mapTriangleRotateIso.app T))
  complete_distinguished_triangle_morphism Tâ‚ Tâ‚‚ hTâ‚ hTâ‚‚ a b comm := by
    obtain âŸ¨c, âŸ¨hcâ‚, hcâ‚‚âŸ©âŸ© := complete_distinguished_triangle_morphism (S.Î¹.mapTriangle.obj Tâ‚)
      (S.Î¹.mapTriangle.obj Tâ‚‚) hTâ‚ hTâ‚‚ (S.Î¹.map a) (S.Î¹.map b)
      (by simpa using S.Î¹.congr_map comm)
    have âŸ¨c', hc'âŸ© : âˆƒ (c' : Tâ‚.objâ‚ƒ âŸ¶ Tâ‚‚.objâ‚ƒ), c = S.Î¹.map c' := âŸ¨S.Î¹.preimage c, by simpâŸ©
    dsimp at hcâ‚ hcâ‚‚
    rw [hc'] at hcâ‚
    rw [hc', assoc, â† Functor.commShiftIso_hom_naturality] at hcâ‚‚
    refine' âŸ¨c', âŸ¨S.Î¹.map_injective _, S.Î¹.map_injective _âŸ©âŸ©
    . simpa using hcâ‚
    . rw [â† cancel_mono ((Functor.commShiftIso (Î¹ S) (1 : â„¤)).hom.app Tâ‚‚.objâ‚),
        S.Î¹.map_comp, S.Î¹.map_comp, assoc, assoc, hcâ‚‚]

--instance [IsTriangulated C] : IsTriangulated S.category := sorry

instance : S.Î¹.IsTriangulated := âŸ¨fun _ hT => hTâŸ©

inductive setSpan (S : Set C) : C â†’ Prop
  | subset (X : C) (hX : X âˆˆ S) : setSpan S X
  | zero : setSpan S 0
  | shift (X : C) (n : â„¤) (hX : setSpan S X) : setSpan S (XâŸ¦nâŸ§)
  | extâ‚‚ (T : Triangle C) (hT : T âˆˆ distTriang C) (hâ‚ : setSpan S T.objâ‚) (hâ‚ƒ : setSpan S T.objâ‚ƒ) :
      setSpan S T.objâ‚‚

def span (S : Set C) : Subcategory C where
  set := setSpan S
  zero := setSpan.zero
  shift X n hX := setSpan.shift X n hX
  extâ‚‚ T hT hâ‚ hâ‚ƒ := setSpan.extâ‚‚ T hT hâ‚ hâ‚ƒ

lemma subset_span_set (S : Set C) : S âŠ† (span S).set :=
  setSpan.subset

instance : PartialOrder (Subcategory C) where
  le Sâ‚ Sâ‚‚ := Sâ‚.set âŠ† Sâ‚‚.set
  le_refl S := (by rfl : S.set âŠ† S.set)
  le_trans := by
    intro Sâ‚ Sâ‚‚ Sâ‚ƒ (hâ‚â‚‚ : Sâ‚.set âŠ† Sâ‚‚.set) (hâ‚‚â‚ƒ : Sâ‚‚.set âŠ† Sâ‚ƒ.set)
    exact hâ‚â‚‚.trans hâ‚‚â‚ƒ
  le_antisymm := by
    rintro Sâ‚ Sâ‚‚ (hâ‚â‚‚ : Sâ‚.set âŠ† Sâ‚‚.set) (hâ‚‚â‚ : Sâ‚‚.set âŠ† Sâ‚.set)
    have := le_antisymm hâ‚â‚‚ hâ‚‚â‚
    cases Sâ‚
    cases Sâ‚‚
    congr

lemma span_LE (S : Set C) (A : Subcategory C) (hA : S âŠ† A.set ) :
    span S â‰¤ A := by
  intro X (hX : setSpan S X)
  induction' hX with Y hY Y n _ hY T hT _ _ hâ‚ hâ‚ƒ
  . exact hA hY
  . exact A.zero
  . exact A.shift Y n hY
  . exact A.extâ‚‚ T hT hâ‚ hâ‚ƒ

def iInf {Î¹ : Type _} (S : Î¹ â†’ Subcategory C) : Subcategory C where
  set := Set.iInter (fun i => (S i).set)
  zero := by
    rw [Set.mem_iInter]
    intro i
    exact (S i).zero
  shift X n hX := by
    simp only [Set.mem_iInter] at hX âŠ¢
    intro i
    exact (S i).shift X n (hX i)
  extâ‚‚ T hT hâ‚ hâ‚ƒ := by
    simp only [Set.mem_iInter] at hâ‚ hâ‚ƒ âŠ¢
    intro i
    exact (S i).extâ‚‚ T hT (hâ‚ i) (hâ‚ƒ i)

lemma mem_iInf_set_iff {Î¹ : Type _} (S : Î¹ â†’ Subcategory C) (X : C) :
    X âˆˆ (iInf S).set â†” âˆ€ (i : Î¹), X âˆˆ (S i).set := by
  dsimp [iInf]
  rw [Set.mem_iInter]

def sInf (S : Set (Subcategory C)) : Subcategory C :=
  iInf (Subtype.val : S â†’ _)

lemma mem_sInf_set_iff (S : Set (Subcategory C)) (X : C) :
    X âˆˆ (sInf S).set â†” âˆ€ (A : Subcategory C) (_ : A âˆˆ S), X âˆˆ A.set := by
  dsimp [sInf]
  rw [mem_iInf_set_iff]
  constructor
  . intro hX A hA
    exact hX âŸ¨_, hAâŸ©
  . intro hX A
    exact hX A.1 A.2

instance : InfSet (Subcategory C) where
  sInf := sInf

instance : CompleteSemilatticeInf (Subcategory C) where
  sInf_le := by
    intro S A hA X hX
    erw [mem_sInf_set_iff] at hX
    exact hX _ hA
  le_sInf := by
    intro B A hA X hX
    erw [mem_sInf_set_iff]
    intro A' hA'
    exact hA _ hA' hX

instance : SupSet (Subcategory C) where
  sSup S := span (sSup (Subcategory.set '' S))

instance : CompleteSemilatticeSup (Subcategory C) where
  le_sSup := by
    intro S A hA X hX
    refine' subset_span_set _ _
    simp only [Set.sSup_eq_sUnion, Set.sUnion_image,
      Set.mem_iUnion, exists_prop]
    exact âŸ¨A, hA, hXâŸ©
  sSup_le := by
    intro S A hA
    apply span_LE
    rintro X âŸ¨_, âŸ¨B, hB, rflâŸ©, hXâŸ©
    exact hA B hB hX

instance : Lattice (Subcategory C) where
  sup Sâ‚ Sâ‚‚ := sSup {Sâ‚, Sâ‚‚}
  le_sup_left Sâ‚ Sâ‚‚ := le_sSup (Set.mem_insert _ _ )
  le_sup_right Sâ‚ Sâ‚‚ := le_sSup (Set.mem_insert_of_mem _ rfl)
  sup_le := by
    rintro Sâ‚ Sâ‚‚ Sâ‚ƒ (hâ‚â‚ƒ : Sâ‚.set âŠ† Sâ‚ƒ.set) (hâ‚‚â‚ƒ : Sâ‚‚.set âŠ† Sâ‚ƒ.set)
    apply span_LE
    rintro X âŸ¨_, âŸ¨B, hB, rflâŸ©, hXâŸ©
    simp only [Set.mem_singleton_iff, Set.mem_insert_iff] at hB
    obtain (rfl|rfl) := hB
    . exact hâ‚â‚ƒ hX
    . exact hâ‚‚â‚ƒ hX
  inf Sâ‚ Sâ‚‚ :=
    { set := Sâ‚.set âˆ© Sâ‚‚.set
      zero := âŸ¨Sâ‚.zero, Sâ‚‚.zeroâŸ©
      shift := fun X n hX => âŸ¨Sâ‚.shift X n hX.1, Sâ‚‚.shift X n hX.2âŸ©
      extâ‚‚ := fun T hT hâ‚ hâ‚ƒ => âŸ¨Sâ‚.extâ‚‚ T hT hâ‚.1 hâ‚ƒ.1,
        Sâ‚‚.extâ‚‚ T hT hâ‚.2 hâ‚ƒ.2âŸ© }
  inf_le_left := fun Sâ‚ Sâ‚‚ X hX => hX.1
  inf_le_right := fun Sâ‚ Sâ‚‚ X hX => hX.2
  le_inf := fun Sâ‚ Sâ‚‚ Sâ‚ƒ hâ‚â‚‚ hâ‚‚â‚ƒ X hX => âŸ¨hâ‚â‚‚ hX, hâ‚‚â‚ƒ hXâŸ©

variable (C)

def top : Subcategory C where
  set := âŠ¤
  zero := by tauto
  shift := by tauto
  extâ‚‚ := by tauto


variable {C}

instance : CompleteLattice (Subcategory C) where
  le_sSup := CompleteSemilatticeSup.le_sSup
  sSup_le := CompleteSemilatticeSup.sSup_le
  le_sInf := CompleteSemilatticeInf.le_sInf
  sInf_le := CompleteSemilatticeInf.sInf_le
  top :=
    { set := âŠ¤
      zero := by tauto
      shift := by tauto
      extâ‚‚ := by tauto }
  bot :=
    { set := IsZero
      zero := isZero_zero _
      shift := fun X n (hX : IsZero X) => by
        change IsZero _
        simp only [IsZero.iff_id_eq_zero] at hX âŠ¢
        rw [â† (shiftFunctor C n).map_id, hX, Functor.map_zero]
      extâ‚‚ := fun T hT hâ‚ hâ‚ƒ => isZeroâ‚‚_of_isZeroâ‚‚â‚ƒ _ hT hâ‚ hâ‚ƒ }
  le_top _ _ _ := Set.mem_univ _
  bot_le := fun A X (hX : IsZero X) => A.zero' _ hX

end Subcategory

end Triangulated

end CategoryTheory

namespace CategoryTheory

open Category Limits

variable {C : Type _} [Category C] [HasZeroObject C] [HasShift C â„¤] [Preadditive C]
  [âˆ€ (n : â„¤), (shiftFunctor C n).Additive] [Pretriangulated C] [IsTriangulated C]
  (S : Triangulated.Subcategory C)

example : MorphismProperty C := S.W

noncomputable example : Pretriangulated S.W.Localization := inferInstance
noncomputable example : IsTriangulated S.W.Localization := inferInstance
noncomputable example : S.W.Q.IsTriangulated := inferInstance

end CategoryTheory
