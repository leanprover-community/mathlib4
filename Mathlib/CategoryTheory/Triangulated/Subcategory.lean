/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.ObjectProperty.ClosedUnderIsomorphisms
import Mathlib.CategoryTheory.Localization.CalculusOfFractions
import Mathlib.CategoryTheory.Localization.Triangulated
import Mathlib.CategoryTheory.Shift.Localization

/-! # Triangulated subcategories

In this file, we introduce the notion of triangulated subcategory of
a pretriangulated category `C`. If `S : Subcategory W`, we define the
class of morphisms `S.W : MorphismProperty C` consisting of morphisms
whose "cone" belongs to `S` (up to isomorphisms). We show that `S.W`
has both calculus of left and right fractions.

## TODO

* obtain (pre)triangulated instances on the localized category with respect to `S.W`
* define the type `S.category` as `Fullsubcategory S.set` and show that it
  is a pretriangulated category.

## Implementation notes

In the definition of `Triangulated.Subcategory`, we do not assume that the predicate
on objects is closed under isomorphisms (i.e. that the subcategory is "strictly full").
Part of the theory would be more convenient under this stronger assumption
(e.g. `Subcategory C` would be a lattice), but some applications require this:
for example, the subcategory of bounded below complexes in the homotopy category
of an additive category is not closed under isomorphisms.

## References
* [Jean-Louis Verdier, *Des cat√©gories d√©riv√©es des cat√©gories ab√©liennes*][verdier1996]

-/

assert_not_exists TwoSidedIdeal

namespace CategoryTheory

open Category Limits Preadditive ZeroObject

namespace Triangulated

open Pretriangulated

variable (C : Type*) [Category C] [HasZeroObject C] [HasShift C ‚Ñ§]
  [Preadditive C] [‚àÄ (n : ‚Ñ§), (shiftFunctor C n).Additive] [Pretriangulated C]

/-- A triangulated subcategory of a pretriangulated category `C` consists of
a predicate `P : C ‚Üí Prop` which contains a zero object, is stable by shifts, and such that
if `X‚ÇÅ ‚ü∂ X‚ÇÇ ‚ü∂ X‚ÇÉ ‚ü∂ X‚ÇÅ‚ü¶1‚üß` is a distinguished triangle such that if `X‚ÇÅ` and `X‚ÇÉ` satisfy
`P` then `X‚ÇÇ` is isomorphic to an object satisfying `P`. -/
structure Subcategory where
  /-- the underlying predicate on objects of a triangulated subcategory -/
  P : ObjectProperty C
  zero' : ‚àÉ (Z : C) (_ : IsZero Z), P Z
  shift (X : C) (n : ‚Ñ§) : P X ‚Üí P (X‚ü¶n‚üß)
  ext‚ÇÇ' (T : Triangle C) (_ : T ‚àà distTriang C) : P T.obj‚ÇÅ ‚Üí P T.obj‚ÇÉ ‚Üí P.isoClosure T.obj‚ÇÇ

namespace Subcategory

variable {C}
variable (S : Subcategory C)

lemma zero [S.P.IsClosedUnderIsomorphisms] : S.P 0 := by
  obtain ‚ü®X, hX, mem‚ü© := S.zero'
  exact S.P.prop_of_iso hX.isoZero mem

/-- The closure under isomorphisms of a triangulated subcategory. -/
def isoClosure : Subcategory C where
  P := S.P.isoClosure
  zero' := by
    obtain ‚ü®Z, hZ, hZ'‚ü© := S.zero'
    exact ‚ü®Z, hZ, Z, hZ', ‚ü®Iso.refl _‚ü©‚ü©
  shift X n := by
    rintro ‚ü®Y, hY, ‚ü®e‚ü©‚ü©
    exact ‚ü®Y‚ü¶n‚üß, S.shift Y n hY, ‚ü®(shiftFunctor C n).mapIso e‚ü©‚ü©
  ext‚ÇÇ' := by
    rintro T hT ‚ü®X‚ÇÅ, h‚ÇÅ, ‚ü®e‚ÇÅ‚ü©‚ü© ‚ü®X‚ÇÉ, h‚ÇÉ, ‚ü®e‚ÇÉ‚ü©‚ü©
    exact ObjectProperty.le_isoClosure _ _
      (S.ext‚ÇÇ' (Triangle.mk (e‚ÇÅ.inv ‚â´ T.mor‚ÇÅ) (T.mor‚ÇÇ ‚â´ e‚ÇÉ.hom) (e‚ÇÉ.inv ‚â´ T.mor‚ÇÉ ‚â´ e‚ÇÅ.hom‚ü¶1‚üß'))
      (isomorphic_distinguished _ hT _
        (Triangle.isoMk _ _ e‚ÇÅ.symm (Iso.refl _) e‚ÇÉ.symm (by simp) (by simp) (by
          dsimp
          simp only [assoc, Iso.cancel_iso_inv_left, ‚Üê Functor.map_comp, e‚ÇÅ.hom_inv_id,
            Functor.map_id, comp_id]))) h‚ÇÅ h‚ÇÉ)

instance : S.isoClosure.P.IsClosedUnderIsomorphisms := by
  dsimp only [isoClosure]
  infer_instance

section

variable (P : ObjectProperty C) (zero : P 0)
  (shift : ‚àÄ (X : C) (n : ‚Ñ§), P X ‚Üí P (X‚ü¶n‚üß))
  (ext‚ÇÇ : ‚àÄ (T : Triangle C) (_ : T ‚àà distTriang C), P T.obj‚ÇÅ ‚Üí P T.obj‚ÇÉ ‚Üí P T.obj‚ÇÇ)

/-- An alternative constructor for "strictly full" triangulated subcategory. -/
def mk' : Subcategory C where
  P := P
  zero' := ‚ü®0, isZero_zero _, zero‚ü©
  shift := shift
  ext‚ÇÇ' T hT h‚ÇÅ h‚ÇÉ := P.le_isoClosure _ (ext‚ÇÇ T hT h‚ÇÅ h‚ÇÉ)

instance : (mk' P zero shift ext‚ÇÇ).P.IsClosedUnderIsomorphisms where
  of_iso {X Y} e hX := by
    refine ext‚ÇÇ (Triangle.mk e.hom (0 : Y ‚ü∂ 0) 0) ?_ hX zero
    refine isomorphic_distinguished _ (contractible_distinguished X) _ ?_
    exact Triangle.isoMk _ _ (Iso.refl _) e.symm (Iso.refl _)

end

lemma ext‚ÇÇ [S.P.IsClosedUnderIsomorphisms]
    (T : Triangle C) (hT : T ‚àà distTriang C) (h‚ÇÅ : S.P T.obj‚ÇÅ)
    (h‚ÇÉ : S.P T.obj‚ÇÉ) : S.P T.obj‚ÇÇ := by
  simpa only [ObjectProperty.isoClosure_eq_self] using S.ext‚ÇÇ' T hT h‚ÇÅ h‚ÇÉ

/-- Given `S : Triangulated.Subcategory C`, this is the class of morphisms on `C` which
consists of morphisms whose cone satisfies `S.P`. -/
def W : MorphismProperty C := fun X Y f => ‚àÉ (Z : C) (g : Y ‚ü∂ Z) (h : Z ‚ü∂ X‚ü¶(1 : ‚Ñ§)‚üß)
  (_ : Triangle.mk f g h ‚àà distTriang C), S.P Z

lemma W_iff {X Y : C} (f : X ‚ü∂ Y) :
    S.W f ‚Üî ‚àÉ (Z : C) (g : Y ‚ü∂ Z) (h : Z ‚ü∂ X‚ü¶(1 : ‚Ñ§)‚üß)
      (_ : Triangle.mk f g h ‚àà distTriang C), S.P Z := by rfl

lemma W_iff' {Y Z : C} (g : Y ‚ü∂ Z) :
    S.W g ‚Üî ‚àÉ (X : C) (f : X ‚ü∂ Y) (h : Z ‚ü∂ X‚ü¶(1 : ‚Ñ§)‚üß)
      (_ : Triangle.mk f g h ‚àà distTriang C), S.P X := by
  rw [S.W_iff]
  constructor
  ¬∑ rintro ‚ü®Z, g, h, H, mem‚ü©
    exact ‚ü®_, _, _, inv_rot_of_distTriang _ H, S.shift _ (-1) mem‚ü©
  ¬∑ rintro ‚ü®Z, g, h, H, mem‚ü©
    exact ‚ü®_, _, _, rot_of_distTriang _ H, S.shift _ 1 mem‚ü©

lemma W.mk {T : Triangle C} (hT : T ‚àà distTriang C) (h : S.P T.obj‚ÇÉ) : S.W T.mor‚ÇÅ :=
  ‚ü®_, _, _, hT, h‚ü©

lemma W.mk' {T : Triangle C} (hT : T ‚àà distTriang C) (h : S.P T.obj‚ÇÅ) : S.W T.mor‚ÇÇ := by
  rw [W_iff']
  exact ‚ü®_, _, _, hT, h‚ü©

lemma isoClosure_W : S.isoClosure.W = S.W := by
  ext X Y f
  constructor
  ¬∑ rintro ‚ü®Z, g, h, mem, ‚ü®Z', hZ', ‚ü®e‚ü©‚ü©‚ü©
    refine ‚ü®Z', g ‚â´ e.hom, e.inv ‚â´ h, isomorphic_distinguished _ mem _ ?_, hZ'‚ü©
    exact Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) e.symm
  ¬∑ rintro ‚ü®Z, g, h, mem, hZ‚ü©
    exact ‚ü®Z, g, h, mem, ObjectProperty.le_isoClosure _ _ hZ‚ü©

instance respectsIso_W : S.W.RespectsIso where
  precomp {X' X Y} e (he : IsIso e) := by
    rintro f ‚ü®Z, g, h, mem, mem'‚ü©
    refine ‚ü®Z, g, h ‚â´ inv e‚ü¶(1 : ‚Ñ§)‚üß', isomorphic_distinguished _ mem _ ?_, mem'‚ü©
    refine Triangle.isoMk _ _ (asIso e) (Iso.refl _) (Iso.refl _) (by simp) (by simp) ?_
    dsimp
    simp only [Functor.map_inv, assoc, IsIso.inv_hom_id, comp_id, id_comp]
  postcomp {X Y Y'} e (he : IsIso e) := by
    rintro f ‚ü®Z, g, h, mem, mem'‚ü©
    refine ‚ü®Z, inv e ‚â´ g, h, isomorphic_distinguished _ mem _ ?_, mem'‚ü©
    exact Triangle.isoMk _ _ (Iso.refl _) (asIso e).symm (Iso.refl _)

instance : S.W.ContainsIdentities := by
  rw [‚Üê isoClosure_W]
  exact ‚ü®fun X => ‚ü®_, _, _, contractible_distinguished X, zero _‚ü©‚ü©

lemma W_of_isIso {X Y : C} (f : X ‚ü∂ Y) [IsIso f] : S.W f := by
  refine (S.W.arrow_mk_iso_iff ?_).1 (MorphismProperty.id_mem _ X)
  exact Arrow.isoMk (Iso.refl _) (asIso f)

lemma smul_mem_W_iff {X Y : C} (f : X ‚ü∂ Y) (n : ‚Ñ§À£) :
    S.W (n ‚Ä¢ f) ‚Üî S.W f :=
  S.W.arrow_mk_iso_iff (Arrow.isoMk (n ‚Ä¢ (Iso.refl _)) (Iso.refl _))

variable {S}

lemma W.shift {X‚ÇÅ X‚ÇÇ : C} {f : X‚ÇÅ ‚ü∂ X‚ÇÇ} (hf : S.W f) (n : ‚Ñ§) : S.W (f‚ü¶n‚üß') := by
  rw [‚Üê smul_mem_W_iff _ _ (n.negOnePow)]
  obtain ‚ü®X‚ÇÉ, g, h, hT, mem‚ü© := hf
  exact ‚ü®_, _, _, Pretriangulated.Triangle.shift_distinguished _ hT n, S.shift _ _ mem‚ü©

lemma W.unshift {X‚ÇÅ X‚ÇÇ : C} {f : X‚ÇÅ ‚ü∂ X‚ÇÇ} {n : ‚Ñ§} (hf : S.W (f‚ü¶n‚üß')) : S.W f :=
  (S.W.arrow_mk_iso_iff
     (Arrow.isoOfNatIso (shiftEquiv C n).unitIso (Arrow.mk f))).2 (hf.shift (-n))

instance : S.W.IsCompatibleWithShift ‚Ñ§ where
  condition n := by
    ext K L f
    exact ‚ü®fun hf => hf.unshift, fun hf => hf.shift n‚ü©

instance [IsTriangulated C] : S.W.IsMultiplicative where
  comp_mem := by
    rw [‚Üê isoClosure_W]
    rintro X‚ÇÅ X‚ÇÇ X‚ÇÉ u‚ÇÅ‚ÇÇ u‚ÇÇ‚ÇÉ ‚ü®Z‚ÇÅ‚ÇÇ, v‚ÇÅ‚ÇÇ, w‚ÇÅ‚ÇÇ, H‚ÇÅ‚ÇÇ, mem‚ÇÅ‚ÇÇ‚ü© ‚ü®Z‚ÇÇ‚ÇÉ, v‚ÇÇ‚ÇÉ, w‚ÇÇ‚ÇÉ, H‚ÇÇ‚ÇÉ, mem‚ÇÇ‚ÇÉ‚ü©
    obtain ‚ü®Z‚ÇÅ‚ÇÉ, v‚ÇÅ‚ÇÉ, w‚ÇÅ‚ÇÇ, H‚ÇÅ‚ÇÉ‚ü© := distinguished_cocone_triangle (u‚ÇÅ‚ÇÇ ‚â´ u‚ÇÇ‚ÇÉ)
    exact ‚ü®_, _, _, H‚ÇÅ‚ÇÉ, S.isoClosure.ext‚ÇÇ _ (someOctahedron rfl H‚ÇÅ‚ÇÇ H‚ÇÇ‚ÇÉ H‚ÇÅ‚ÇÉ).mem mem‚ÇÅ‚ÇÇ mem‚ÇÇ‚ÇÉ‚ü©

variable (S)

lemma mem_W_iff_of_distinguished
    [S.P.IsClosedUnderIsomorphisms] (T : Triangle C) (hT : T ‚àà distTriang C) :
    S.W T.mor‚ÇÅ ‚Üî S.P T.obj‚ÇÉ := by
  constructor
  ¬∑ rintro ‚ü®Z, g, h, hT', mem‚ü©
    obtain ‚ü®e, _‚ü© := exists_iso_of_arrow_iso _ _ hT' hT (Iso.refl _)
    exact S.P.prop_of_iso (Triangle.œÄ‚ÇÉ.mapIso e) mem
  ¬∑ intro h
    exact ‚ü®_, _, _, hT, h‚ü©

instance [IsTriangulated C] : S.W.HasLeftCalculusOfFractions where
  exists_leftFraction X Y œÜ := by
    obtain ‚ü®Z, f, g, H, mem‚ü© := œÜ.hs
    obtain ‚ü®Y', s', f', mem'‚ü© := distinguished_cocone_triangle‚ÇÇ (g ‚â´ œÜ.f‚ü¶1‚üß')
    obtain ‚ü®b, ‚ü®hb‚ÇÅ, _‚ü©‚ü© :=
      complete_distinguished_triangle_morphism‚ÇÇ _ _ H mem' œÜ.f (ùüô Z) (by simp)
    exact ‚ü®MorphismProperty.LeftFraction.mk b s' ‚ü®_, _, _, mem', mem‚ü©, hb‚ÇÅ.symm‚ü©
  ext := by
    rintro X' X Y f‚ÇÅ f‚ÇÇ s ‚ü®Z, g, h, H, mem‚ü© hf‚ÇÅ
    have hf‚ÇÇ : s ‚â´ (f‚ÇÅ - f‚ÇÇ) = 0 := by rw [comp_sub, hf‚ÇÅ, sub_self]
    obtain ‚ü®q, hq‚ü© := Triangle.yoneda_exact‚ÇÇ _ H _ hf‚ÇÇ
    obtain ‚ü®Y', r, t, mem'‚ü© := distinguished_cocone_triangle q
    refine ‚ü®Y', r, ?_, ?_‚ü©
    ¬∑ exact ‚ü®_, _, _, rot_of_distTriang _ mem', S.shift _ _ mem‚ü©
    ¬∑ have eq := comp_distTriang_mor_zero‚ÇÅ‚ÇÇ _ mem'
      dsimp at eq
      rw [‚Üê sub_eq_zero, ‚Üê sub_comp, hq, assoc, eq, comp_zero]

instance [IsTriangulated C] : S.W.HasRightCalculusOfFractions where
  exists_rightFraction X Y œÜ := by
    obtain ‚ü®Z, f, g, H, mem‚ü© := œÜ.hs
    obtain ‚ü®X', f', h', mem'‚ü© := distinguished_cocone_triangle‚ÇÅ (œÜ.f ‚â´ f)
    obtain ‚ü®a, ‚ü®ha‚ÇÅ, _‚ü©‚ü© := complete_distinguished_triangle_morphism‚ÇÅ _ _
      mem' H œÜ.f (ùüô Z) (by simp)
    exact ‚ü®MorphismProperty.RightFraction.mk f' ‚ü®_, _, _, mem', mem‚ü© a, ha‚ÇÅ‚ü©
  ext Y Z Z' f‚ÇÅ f‚ÇÇ s hs hf‚ÇÅ := by
    rw [S.W_iff'] at hs
    obtain ‚ü®Z, g, h, H, mem‚ü© := hs
    have hf‚ÇÇ : (f‚ÇÅ - f‚ÇÇ) ‚â´ s = 0 := by rw [sub_comp, hf‚ÇÅ, sub_self]
    obtain ‚ü®q, hq‚ü© := Triangle.coyoneda_exact‚ÇÇ _ H _ hf‚ÇÇ
    obtain ‚ü®Y', r, t, mem'‚ü© := distinguished_cocone_triangle‚ÇÅ q
    refine ‚ü®Y', r, ?_, ?_‚ü©
    ¬∑ exact ‚ü®_, _, _, mem', mem‚ü©
    ¬∑ have eq := comp_distTriang_mor_zero‚ÇÅ‚ÇÇ _ mem'
      dsimp at eq
      rw [‚Üê sub_eq_zero, ‚Üê comp_sub, hq, reassoc_of% eq, zero_comp]

instance [IsTriangulated C] : S.W.IsCompatibleWithTriangulation := ‚ü®by
  rintro T‚ÇÅ T‚ÇÉ mem‚ÇÅ mem‚ÇÉ a b ‚ü®Z‚ÇÖ, g‚ÇÖ, h‚ÇÖ, mem‚ÇÖ, mem‚ÇÖ'‚ü© ‚ü®Z‚ÇÑ, g‚ÇÑ, h‚ÇÑ, mem‚ÇÑ, mem‚ÇÑ'‚ü© comm
  obtain ‚ü®Z‚ÇÇ, g‚ÇÇ, h‚ÇÇ, mem‚ÇÇ‚ü© := distinguished_cocone_triangle (T‚ÇÅ.mor‚ÇÅ ‚â´ b)
  have H := someOctahedron rfl mem‚ÇÅ mem‚ÇÑ mem‚ÇÇ
  have H' := someOctahedron comm.symm mem‚ÇÖ mem‚ÇÉ mem‚ÇÇ
  let œÜ : T‚ÇÅ ‚ü∂ T‚ÇÉ := H.triangleMorphism‚ÇÅ ‚â´ H'.triangleMorphism‚ÇÇ
  exact ‚ü®œÜ.hom‚ÇÉ, S.W.comp_mem _ _ (W.mk S H.mem mem‚ÇÑ') (W.mk' S H'.mem mem‚ÇÖ'),
    by simpa [œÜ] using œÜ.comm‚ÇÇ, by simpa [œÜ] using œÜ.comm‚ÇÉ‚ü©‚ü©

section

variable (T : Triangle C) (hT : T ‚àà distTriang C)

include hT

lemma ext‚ÇÅ [S.P.IsClosedUnderIsomorphisms] (h‚ÇÇ : S.P T.obj‚ÇÇ) (h‚ÇÉ : S.P T.obj‚ÇÉ) :
    S.P T.obj‚ÇÅ :=
  S.ext‚ÇÇ _ (inv_rot_of_distTriang _ hT) (S.shift _ _ h‚ÇÉ) h‚ÇÇ

lemma ext‚ÇÉ [S.P.IsClosedUnderIsomorphisms] (h‚ÇÅ : S.P T.obj‚ÇÅ) (h‚ÇÇ : S.P T.obj‚ÇÇ) :
    S.P T.obj‚ÇÉ :=
  S.ext‚ÇÇ _ (rot_of_distTriang _ hT) h‚ÇÇ (S.shift _ _ h‚ÇÅ)

lemma ext‚ÇÅ' (h‚ÇÇ : S.P T.obj‚ÇÇ) (h‚ÇÉ : S.P T.obj‚ÇÉ) :
    S.P.isoClosure T.obj‚ÇÅ :=
  S.ext‚ÇÇ' _ (inv_rot_of_distTriang _ hT) (S.shift _ _ h‚ÇÉ) h‚ÇÇ

lemma ext‚ÇÉ' (h‚ÇÅ : S.P T.obj‚ÇÅ) (h‚ÇÇ : S.P T.obj‚ÇÇ) :
    S.P.isoClosure T.obj‚ÇÉ :=
  S.ext‚ÇÇ' _ (rot_of_distTriang _ hT) h‚ÇÇ (S.shift _ _ h‚ÇÅ)

end

end Subcategory

end Triangulated

end CategoryTheory
