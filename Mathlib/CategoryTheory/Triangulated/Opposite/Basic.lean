/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Limits.Shapes.RegularMono
public import Mathlib.CategoryTheory.Shift.Opposite
public import Mathlib.CategoryTheory.Shift.Pullback

/-!
# The shift on the opposite category of a pretriangulated category

Let `C` be a (pre)triangulated category. We already have a shift on `C·µí·µñ` given
by `CategoryTheory.Shift.Opposite`, but this is not the shift that we want to
make `C·µí·µñ` into a (pre)triangulated category.
The correct shift on `C·µí·µñ` is obtained by combining the constructions in the files
`CategoryTheory.Shift.Opposite` and `CategoryTheory.Shift.Pullback`.
When the user opens `CategoryTheory.Pretriangulated.Opposite`, the
category `C·µí·µñ` is equipped with the shift by `‚Ñ§` such that
shifting by `n : ‚Ñ§` on `C·µí·µñ` corresponds to the shift by
`-n` on `C`. This is actually a definitional equality, but the user
should not rely on this, and instead use the isomorphism
`shiftFunctorOpIso C n m hnm : shiftFunctor C·µí·µñ n ‚âÖ (shiftFunctor C m).op`
where `hnm : n + m = 0`.

Some compatibilities between the shifts on `C` and `C·µí·µñ` are also expressed through
the equivalence of categories `opShiftFunctorEquivalence C n : C·µí·µñ ‚âå C·µí·µñ` whose
functor is `shiftFunctor C·µí·µñ n` and whose inverse functor is `(shiftFunctor C n).op`.

## References
* [Jean-Louis Verdier, *Des cat√©gories d√©riv√©es des cat√©gories ab√©liennes*][verdier1996]

-/

@[expose] public section

namespace CategoryTheory

open Category Limits Preadditive ZeroObject

variable (C : Type*) [Category* C]

namespace Pretriangulated

variable [HasShift C ‚Ñ§]

namespace Opposite

/-- As it is unclear whether the opposite category `C·µí·µñ` should always be equipped
with the shift by `‚Ñ§` such that shifting by `n` on `C·µí·µñ` corresponds to shifting
by `-n` on `C`, the user shall have to do `open CategoryTheory.Pretriangulated.Opposite`
in order to get this shift and the (pre)triangulated structure on `C·µí·µñ`. -/
private abbrev OppositeShiftAux :=
  PullbackShift (OppositeShift C ‚Ñ§)
    (AddMonoidHom.mk' (fun (n : ‚Ñ§) => -n) (by intros; dsimp; lia))

/-- The category `C·µí·µñ` is equipped with the shift such that the shift by `n` on `C·µí·µñ`
corresponds to the shift by `-n` on `C`. -/
scoped instance : HasShift C·µí·µñ ‚Ñ§ :=
  (inferInstance : HasShift (OppositeShiftAux C) ‚Ñ§)

instance [Preadditive C] [‚àÄ (n : ‚Ñ§), (shiftFunctor C n).Additive] (n : ‚Ñ§) :
    (shiftFunctor C·µí·µñ n).Additive :=
  (inferInstance : (shiftFunctor (OppositeShiftAux C) n).Additive)

end Opposite

open Opposite

/-- The shift functor on the opposite category identifies to the opposite functor
of a shift functor on the original category. -/
def shiftFunctorOpIso (n m : ‚Ñ§) (hnm : n + m = 0) :
    shiftFunctor C·µí·µñ n ‚âÖ (shiftFunctor C m).op := eqToIso (by
  obtain rfl : m = -n := by lia
  rfl)

variable {C}

lemma shiftFunctorZero_op_hom_app (X : C·µí·µñ) :
    (shiftFunctorZero C·µí·µñ ‚Ñ§).hom.app X = (shiftFunctorOpIso C 0 0 (zero_add 0)).hom.app X ‚â´
      ((shiftFunctorZero C ‚Ñ§).inv.app X.unop).op := rfl

lemma shiftFunctorZero_op_inv_app (X : C·µí·µñ) :
    (shiftFunctorZero C·µí·µñ ‚Ñ§).inv.app X =
      ((shiftFunctorZero C ‚Ñ§).hom.app X.unop).op ‚â´
      (shiftFunctorOpIso C 0 0 (zero_add 0)).inv.app X := by
  rw [‚Üê cancel_epi ((shiftFunctorZero C·µí·µñ ‚Ñ§).hom.app X), Iso.hom_inv_id_app,
    shiftFunctorZero_op_hom_app, assoc, ‚Üê op_comp_assoc, Iso.hom_inv_id_app, op_id,
    id_comp, Iso.hom_inv_id_app]

lemma shiftFunctorAdd'_op_hom_app (X : C·µí·µñ) (a‚ÇÅ a‚ÇÇ a‚ÇÉ : ‚Ñ§) (h : a‚ÇÅ + a‚ÇÇ = a‚ÇÉ)
    (b‚ÇÅ b‚ÇÇ b‚ÇÉ : ‚Ñ§) (h‚ÇÅ : a‚ÇÅ + b‚ÇÅ = 0) (h‚ÇÇ : a‚ÇÇ + b‚ÇÇ = 0) (h‚ÇÉ : a‚ÇÉ + b‚ÇÉ = 0) :
    (shiftFunctorAdd' C·µí·µñ a‚ÇÅ a‚ÇÇ a‚ÇÉ h).hom.app X =
      (shiftFunctorOpIso C _ _ h‚ÇÉ).hom.app X ‚â´
        ((shiftFunctorAdd' C b‚ÇÅ b‚ÇÇ b‚ÇÉ (by lia)).inv.app X.unop).op ‚â´
        (shiftFunctorOpIso C _ _ h‚ÇÇ).inv.app _ ‚â´
        (shiftFunctor C·µí·µñ a‚ÇÇ).map ((shiftFunctorOpIso C _ _ h‚ÇÅ).inv.app X) := by
  erw [@pullbackShiftFunctorAdd'_hom_app (OppositeShift C ‚Ñ§) _ _ _ _ _ _ _ X
    a‚ÇÅ a‚ÇÇ a‚ÇÉ h b‚ÇÅ b‚ÇÇ b‚ÇÉ (by dsimp; lia) (by dsimp; lia) (by dsimp; lia)]
  rw [oppositeShiftFunctorAdd'_hom_app]
  rfl

lemma shiftFunctorAdd'_op_inv_app (X : C·µí·µñ) (a‚ÇÅ a‚ÇÇ a‚ÇÉ : ‚Ñ§) (h : a‚ÇÅ + a‚ÇÇ = a‚ÇÉ)
    (b‚ÇÅ b‚ÇÇ b‚ÇÉ : ‚Ñ§) (h‚ÇÅ : a‚ÇÅ + b‚ÇÅ = 0) (h‚ÇÇ : a‚ÇÇ + b‚ÇÇ = 0) (h‚ÇÉ : a‚ÇÉ + b‚ÇÉ = 0) :
    (shiftFunctorAdd' C·µí·µñ a‚ÇÅ a‚ÇÇ a‚ÇÉ h).inv.app X =
      (shiftFunctor C·µí·µñ a‚ÇÇ).map ((shiftFunctorOpIso C _ _ h‚ÇÅ).hom.app X) ‚â´
      (shiftFunctorOpIso C _ _ h‚ÇÇ).hom.app _ ‚â´
      ((shiftFunctorAdd' C b‚ÇÅ b‚ÇÇ b‚ÇÉ (by lia)).hom.app X.unop).op ‚â´
      (shiftFunctorOpIso C _ _ h‚ÇÉ).inv.app X := by
  rw [‚Üê cancel_epi ((shiftFunctorAdd' C·µí·µñ a‚ÇÅ a‚ÇÇ a‚ÇÉ h).hom.app X), Iso.hom_inv_id_app,
    shiftFunctorAdd'_op_hom_app X a‚ÇÅ a‚ÇÇ a‚ÇÉ h b‚ÇÅ b‚ÇÇ b‚ÇÉ h‚ÇÅ h‚ÇÇ h‚ÇÉ,
    assoc, assoc, assoc, ‚Üê Functor.map_comp_assoc, Iso.inv_hom_id_app]
  erw [Functor.map_id, id_comp, Iso.inv_hom_id_app_assoc]
  rw [‚Üê op_comp_assoc, Iso.hom_inv_id_app, op_id, id_comp, Iso.hom_inv_id_app]

lemma shiftFunctor_op_map {K L : C·µí·µñ} (œÜ : K ‚ü∂ L) (n m : ‚Ñ§) (hnm : n + m = 0 := by lia) :
    (shiftFunctor C·µí·µñ n).map œÜ =
      (shiftFunctorOpIso C n m hnm).hom.app K ‚â´ ((shiftFunctor C m).map œÜ.unop).op ‚â´
        (shiftFunctorOpIso C n m hnm).inv.app L :=
  (NatIso.naturality_2 (shiftFunctorOpIso C n m hnm) œÜ).symm

variable (C) in
/-- The autoequivalence `C·µí·µñ ‚âå C·µí·µñ` whose functor is `shiftFunctor C·µí·µñ n` and whose inverse
functor is `(shiftFunctor C n).op`. In most cases, it is not necessary to unfold the
definitions of the unit and counit isomorphisms: the compatibilities they satisfy
are stated as separate lemmas. -/
@[simps functor inverse]
def opShiftFunctorEquivalence (n : ‚Ñ§) : C·µí·µñ ‚âå C·µí·µñ where
  functor := shiftFunctor C·µí·µñ n
  inverse := (shiftFunctor C n).op
  unitIso := NatIso.op (shiftFunctorCompIsoId C (-n) n n.add_left_neg) ‚â™‚â´
    Functor.isoWhiskerRight (shiftFunctorOpIso C n (-n) n.add_right_neg).symm (shiftFunctor C n).op
  counitIso := Functor.isoWhiskerLeft _ (shiftFunctorOpIso C n (-n) n.add_right_neg) ‚â™‚â´
    NatIso.op (shiftFunctorCompIsoId C n (-n) n.add_right_neg).symm
  functor_unitIso_comp X := Quiver.Hom.unop_inj (by
    dsimp [shiftFunctorOpIso]
    erw [comp_id, Functor.map_id, comp_id]
    change (shiftFunctorCompIsoId C n (-n) (add_neg_cancel n)).inv.app (X.unop‚ü¶-n‚üß) ‚â´
      ((shiftFunctorCompIsoId C (-n) n (neg_add_cancel n)).hom.app X.unop)‚ü¶-n‚üß' = ùüô _
    rw [shift_shiftFunctorCompIsoId_neg_add_cancel_hom_app n X.unop, Iso.inv_hom_id_app])

@[reassoc]
lemma opShiftFunctorEquivalence_unitIso_hom_app (X : C·µí·µñ) (n m : ‚Ñ§) (hnm : n + m = 0 := by lia) :
    (opShiftFunctorEquivalence C n).unitIso.hom.app X =
      ((shiftFunctorCompIsoId C m n (by lia)).hom.app X.unop).op ‚â´
        (((shiftFunctorOpIso C n m hnm).inv.app (X)).unop‚ü¶n‚üß').op := by
  obtain rfl : m = -n := by lia
  rfl

@[reassoc]
lemma opShiftFunctorEquivalence_unitIso_inv_app (X : C·µí·µñ) (n m : ‚Ñ§) (hnm : n + m = 0 := by lia) :
    (opShiftFunctorEquivalence C n).unitIso.inv.app X =
      (((shiftFunctorOpIso C n m hnm).hom.app (X)).unop‚ü¶n‚üß').op ‚â´
      ((shiftFunctorCompIsoId C m n (by lia)).inv.app X.unop).op := by
  obtain rfl : m = -n := by lia
  rfl

@[reassoc]
lemma opShiftFunctorEquivalence_counitIso_hom_app (X : C·µí·µñ) (n m : ‚Ñ§) (hnm : n + m = 0 := by lia) :
    (opShiftFunctorEquivalence C n).counitIso.hom.app X =
      (shiftFunctorOpIso C n m hnm).hom.app (Opposite.op (X.unop‚ü¶n‚üß)) ‚â´
        ((shiftFunctorCompIsoId C n m hnm).inv.app X.unop).op
        := by
  obtain rfl : m = -n := by lia
  rfl

@[reassoc]
lemma opShiftFunctorEquivalence_counitIso_inv_app (X : C·µí·µñ) (n m : ‚Ñ§) (hnm : n + m = 0 := by lia) :
    (opShiftFunctorEquivalence C n).counitIso.inv.app X =
      ((shiftFunctorCompIsoId C n m hnm).hom.app X.unop).op ‚â´
        (shiftFunctorOpIso C n m hnm).inv.app (Opposite.op (X.unop‚ü¶n‚üß)) := by
  obtain rfl : m = -n := by lia
  rfl

/-! The naturality of the unit and counit isomorphisms are restated in the following
lemmas so as to mitigate the need for `erw`. -/

@[reassoc (attr := simp)]
lemma opShiftFunctorEquivalence_unitIso_hom_naturality (n : ‚Ñ§) {X Y : C·µí·µñ} (f : X ‚ü∂ Y) :
    f ‚â´ (opShiftFunctorEquivalence C n).unitIso.hom.app Y =
      (opShiftFunctorEquivalence C n).unitIso.hom.app X ‚â´ (f‚ü¶n‚üß').unop‚ü¶n‚üß'.op :=
  (opShiftFunctorEquivalence C n).unitIso.hom.naturality f

@[reassoc (attr := simp)]
lemma opShiftFunctorEquivalence_unitIso_inv_naturality (n : ‚Ñ§) {X Y : C·µí·µñ} (f : X ‚ü∂ Y) :
    (f‚ü¶n‚üß').unop‚ü¶n‚üß'.op ‚â´ (opShiftFunctorEquivalence C n).unitIso.inv.app Y =
      (opShiftFunctorEquivalence C n).unitIso.inv.app X ‚â´ f :=
  (opShiftFunctorEquivalence C n).unitIso.inv.naturality f

@[reassoc (attr := simp)]
lemma opShiftFunctorEquivalence_counitIso_hom_naturality (n : ‚Ñ§) {X Y : C·µí·µñ} (f : X ‚ü∂ Y) :
    f.unop‚ü¶n‚üß'.op‚ü¶n‚üß' ‚â´ (opShiftFunctorEquivalence C n).counitIso.hom.app Y =
      (opShiftFunctorEquivalence C n).counitIso.hom.app X ‚â´ f :=
  (opShiftFunctorEquivalence C n).counitIso.hom.naturality f

@[reassoc (attr := simp)]
lemma opShiftFunctorEquivalence_counitIso_inv_naturality (n : ‚Ñ§) {X Y : C·µí·µñ} (f : X ‚ü∂ Y) :
    f ‚â´ (opShiftFunctorEquivalence C n).counitIso.inv.app Y =
      (opShiftFunctorEquivalence C n).counitIso.inv.app X ‚â´ f.unop‚ü¶n‚üß'.op‚ü¶n‚üß' :=
  (opShiftFunctorEquivalence C n).counitIso.inv.naturality f

lemma opShiftFunctorEquivalence_zero_unitIso_hom_app (X : C·µí·µñ) :
    (opShiftFunctorEquivalence C 0).unitIso.hom.app X =
      ((shiftFunctorZero C ‚Ñ§).hom.app X.unop).op ‚â´
      (((shiftFunctorZero C·µí·µñ ‚Ñ§).inv.app X).unop‚ü¶(0 : ‚Ñ§)‚üß').op := by
  apply Quiver.Hom.unop_inj
  dsimp [opShiftFunctorEquivalence]
  rw [shiftFunctorZero_op_inv_app, unop_comp, Quiver.Hom.unop_op, Functor.map_comp,
    shiftFunctorCompIsoId_zero_zero_hom_app, assoc]

lemma opShiftFunctorEquivalence_zero_unitIso_inv_app (X : C·µí·µñ) :
    (opShiftFunctorEquivalence C 0).unitIso.inv.app X =
      (((shiftFunctorZero C·µí·µñ ‚Ñ§).hom.app X).unop‚ü¶(0 : ‚Ñ§)‚üß').op ‚â´
        ((shiftFunctorZero C ‚Ñ§).inv.app X.unop).op := by
  apply Quiver.Hom.unop_inj
  dsimp [opShiftFunctorEquivalence]
  rw [shiftFunctorZero_op_hom_app, unop_comp, Quiver.Hom.unop_op, Functor.map_comp,
    shiftFunctorCompIsoId_zero_zero_inv_app, assoc]

lemma opShiftFunctorEquivalence_add_unitIso_hom_app_eq
    (X : C·µí·µñ) (m n p : ‚Ñ§) (h : m + n = p := by lia) :
    (opShiftFunctorEquivalence C p).unitIso.hom.app X =
      (opShiftFunctorEquivalence C n).unitIso.hom.app X ‚â´
      (((opShiftFunctorEquivalence C m).unitIso.hom.app (X‚ü¶n‚üß)).unop‚ü¶n‚üß').op ‚â´
      ((shiftFunctorAdd' C m n p h).hom.app _).op ‚â´
      (((shiftFunctorAdd' C·µí·µñ n m p (by lia)).inv.app X).unop‚ü¶p‚üß').op := by
  dsimp [opShiftFunctorEquivalence]
  simp only [shiftFunctorAdd'_op_inv_app _ n m p (by lia) _ _ _ (add_neg_cancel n)
    (add_neg_cancel m) (add_neg_cancel p), shiftFunctor_op_map _ m (-m),
    Category.assoc, Iso.inv_hom_id_app_assoc]
  erw [Functor.map_id, Functor.map_id, Functor.map_id, Functor.map_id,
    id_comp, id_comp, id_comp, comp_id, comp_id]
  dsimp
  rw [comp_id, shiftFunctorCompIsoId_add'_hom_app _ _ _ _ _ _
    (neg_add_cancel m) (neg_add_cancel n) (neg_add_cancel p) h]
  dsimp
  rw [Category.assoc, Category.assoc]
  rfl

lemma opShiftFunctorEquivalence_add_unitIso_inv_app_eq
    (X : C·µí·µñ) (m n p : ‚Ñ§) (h : m + n = p := by lia) :
    (opShiftFunctorEquivalence C p).unitIso.inv.app X =
      (((shiftFunctorAdd' C·µí·µñ n m p (by lia)).hom.app X).unop‚ü¶p‚üß').op ‚â´
      ((shiftFunctorAdd' C m n p h).inv.app _).op ‚â´
      (((opShiftFunctorEquivalence C m).unitIso.inv.app (X‚ü¶n‚üß)).unop‚ü¶n‚üß').op ‚â´
      (opShiftFunctorEquivalence C n).unitIso.inv.app X := by
  rw [‚Üê cancel_mono ((opShiftFunctorEquivalence C p).unitIso.hom.app X), Iso.inv_hom_id_app,
    opShiftFunctorEquivalence_add_unitIso_hom_app_eq _ _ _ _ h,
    Category.assoc, Category.assoc, Category.assoc, Iso.inv_hom_id_app_assoc]
  apply Quiver.Hom.unop_inj
  dsimp
  simp only [Category.assoc,
    ‚Üê unop_comp, Iso.inv_hom_id_app, Functor.comp_obj, Functor.op_obj, unop_id,
    Functor.map_id, id_comp, ‚Üê Functor.map_comp, Iso.hom_inv_id_app]

@[deprecated (since := "2025-12-08")] alias opShiftFunctorEquivalence_unitIso_hom_app_eq :=
  opShiftFunctorEquivalence_add_unitIso_hom_app_eq
@[deprecated (since := "2025-12-08")] alias opShiftFunctorEquivalence_unitIso_inv_app_eq :=
  opShiftFunctorEquivalence_add_unitIso_inv_app_eq

lemma shift_unop_opShiftFunctorEquivalence_counitIso_inv_app (X : C·µí·µñ) (n : ‚Ñ§) :
    ((opShiftFunctorEquivalence C n).counitIso.inv.app X).unop‚ü¶n‚üß' =
      ((opShiftFunctorEquivalence C n).unitIso.hom.app ((Opposite.op ((X.unop)‚ü¶n‚üß)))).unop :=
  Quiver.Hom.op_inj ((opShiftFunctorEquivalence C n).unit_app_inverse X).symm

lemma shift_unop_opShiftFunctorEquivalence_counitIso_hom_app (X : C·µí·µñ) (n : ‚Ñ§) :
    ((opShiftFunctorEquivalence C n).counitIso.hom.app X).unop‚ü¶n‚üß' =
      ((opShiftFunctorEquivalence C n).unitIso.inv.app ((Opposite.op (X.unop‚ü¶n‚üß)))).unop :=
  Quiver.Hom.op_inj ((opShiftFunctorEquivalence C n).unitInv_app_inverse X).symm

lemma opShiftFunctorEquivalence_counitIso_inv_app_shift (X : C·µí·µñ) (n : ‚Ñ§) :
    (opShiftFunctorEquivalence C n).counitIso.inv.app (X‚ü¶n‚üß) =
      ((opShiftFunctorEquivalence C n).unitIso.hom.app X)‚ü¶n‚üß' :=
  (opShiftFunctorEquivalence C n).counitInv_app_functor X

lemma opShiftFunctorEquivalence_counitIso_hom_app_shift (X : C·µí·µñ) (n : ‚Ñ§) :
    (opShiftFunctorEquivalence C n).counitIso.hom.app (X‚ü¶n‚üß) =
      ((opShiftFunctorEquivalence C n).unitIso.inv.app X)‚ü¶n‚üß' :=
  (opShiftFunctorEquivalence C n).counit_app_functor X

@[reassoc]
lemma shiftFunctorCompIsoId_op_hom_app (X : C·µí·µñ) (n m : ‚Ñ§) (hnm : n + m = 0 := by lia) :
    (shiftFunctorCompIsoId C·µí·µñ n m hnm).hom.app X =
      ((shiftFunctorOpIso C n m hnm).hom.app X)‚ü¶m‚üß' ‚â´
        (shiftFunctorOpIso C m n (by lia)).hom.app (Opposite.op (X.unop‚ü¶m‚üß)) ‚â´
          ((shiftFunctorCompIsoId C m n (by lia)).inv.app X.unop).op := by
  simp [shiftFunctorCompIsoId, shiftFunctorZero_op_hom_app X,
    shiftFunctorAdd'_op_inv_app X n m 0 hnm m n 0 hnm (by lia) (add_zero 0)]

@[reassoc]
lemma shiftFunctorCompIsoId_op_inv_app (X : C·µí·µñ) (n m : ‚Ñ§) (hnm : n + m = 0 := by lia) :
    (shiftFunctorCompIsoId C·µí·µñ n m hnm).inv.app X =
      ((shiftFunctorCompIsoId C m n (by omega)).hom.app X.unop).op ‚â´
        (shiftFunctorOpIso C m n (by omega)).inv.app (Opposite.op (X.unop‚ü¶m‚üß)) ‚â´
          ((shiftFunctorOpIso C n m hnm).inv.app X)‚ü¶m‚üß' := by
  simp [shiftFunctorCompIsoId, shiftFunctorZero_op_inv_app X,
    shiftFunctorAdd'_op_hom_app X n m 0 hnm m n 0 hnm (by omega) (add_zero 0)]

@[reassoc]
lemma shift_opShiftFunctorEquivalence_counitIso_inv_app
    (X : C) (m n : ‚Ñ§) (hmn : m + n = 0 := by lia) :
    ((opShiftFunctorEquivalence C n).counitIso.inv.app (Opposite.op X))‚ü¶m‚üß' =
      (opShiftFunctorEquivalence C n).counitIso.inv.app ((Opposite.op X)‚ü¶m‚üß) ‚â´
        (((shiftFunctorOpIso C m n hmn).hom.app (Opposite.op X)).unop‚ü¶n‚üß').op‚ü¶n‚üß' ‚â´
          ((shiftFunctorOpIso C m n hmn).inv.app (Opposite.op (X‚ü¶n‚üß)))‚ü¶n‚üß' ‚â´
            (shiftFunctorComm C·µí·µñ n m).inv.app (Opposite.op (X‚ü¶n‚üß)) := by
  obtain rfl : m = -n := by lia
  dsimp [opShiftFunctorEquivalence]
  simp only [shiftFunctor_op_map _ (-n) n, shiftFunctor_op_map _ n (-n),
    shiftFunctorComm_inv_app_of_add_eq_zero n (-n) (by lia), assoc,
    shiftFunctorCompIsoId_op_inv_app, shiftFunctorCompIsoId_op_hom_app,
    shift_shiftFunctorCompIsoId_hom_app, op_comp, unop_comp, Quiver.Hom.unop_op,
    Functor.map_comp, Iso.inv_hom_id_app_assoc, Functor.op_obj]
  apply Quiver.Hom.unop_inj
  dsimp
  simp only [Category.assoc, ‚Üê Functor.map_comp_assoc, Iso.unop_hom_inv_id_app_assoc]
  congr 3
  exact (NatIso.naturality_1 (shiftFunctorCompIsoId C n (-n) (by lia)) _).symm

/-- Given objects `X` and `Y` in `C·µí·µñ`, this is the bijection
`(op (X.unop‚ü¶n‚üß) ‚ü∂ Y) ‚âÉ (X ‚ü∂ Y‚ü¶n‚üß)` for any `n : ‚Ñ§`. -/
def opShiftFunctorEquivalenceSymmHomEquiv {n : ‚Ñ§} {X Y : C·µí·µñ} :
    (Opposite.op (X.unop‚ü¶n‚üß) ‚ü∂ Y) ‚âÉ (X ‚ü∂ Y‚ü¶n‚üß) :=
  (opShiftFunctorEquivalence C n).symm.toAdjunction.homEquiv X Y

@[reassoc]
lemma opShiftFunctorEquivalenceSymmHomEquiv_apply {n : ‚Ñ§} {X Y : C·µí·µñ}
    (f : Opposite.op (X.unop‚ü¶n‚üß) ‚ü∂ Y) :
    opShiftFunctorEquivalenceSymmHomEquiv f =
      (opShiftFunctorEquivalence C n).counitIso.inv.app X ‚â´ (shiftFunctor C·µí·µñ n).map f := rfl

@[reassoc]
lemma opShiftFunctorEquivalenceSymmHomEquiv_left_inv
    {n : ‚Ñ§} {X Y : C·µí·µñ} (f : Opposite.op (X.unop‚ü¶n‚üß) ‚ü∂ Y) :
    ((opShiftFunctorEquivalence C n).unitIso.inv.app Y).unop ‚â´
      (opShiftFunctorEquivalenceSymmHomEquiv f).unop‚ü¶n‚üß' = f.unop :=
  Quiver.Hom.op_inj (opShiftFunctorEquivalenceSymmHomEquiv.left_inv f)

@[reassoc]
lemma shift_opShiftFunctorEquivalenceSymmHomEquiv_unop
    {n : ‚Ñ§} {X Y : C·µí·µñ} (f : Opposite.op (X.unop‚ü¶n‚üß) ‚ü∂ Y) :
    (opShiftFunctorEquivalenceSymmHomEquiv f).unop‚ü¶n‚üß' =
      ((opShiftFunctorEquivalence C n).unitIso.hom.app Y).unop ‚â´ f.unop := by
  rw [‚Üê opShiftFunctorEquivalenceSymmHomEquiv_left_inv]
  simp

end Pretriangulated

end CategoryTheory
