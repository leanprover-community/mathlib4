import Mathlib.CategoryTheory.Triangulated.TStructure.TExact
import Mathlib.CategoryTheory.Triangulated.TStructure.AbelianSubcategory
import Mathlib.CategoryTheory.Triangulated.TStructure.Homology
import Mathlib.CategoryTheory.Abelian.Images
import Mathlib.Algebra.Homology.ShortComplex.Exact
import Mathlib.Algebra.Homology.ShortComplex.HomologicalComplex
import Mathlib.CategoryTheory.Triangulated.TStructure.AbelianCategoryLemmas
import Mathlib.Algebra.Homology.ShortComplex.HomologicalComplex
import Mathlib.CategoryTheory.Limits.Shapes.Equalizers
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.AbelianImages

namespace CategoryTheory

open Category Limits Triangulated Pretriangulated TStructure ObjectProperty

variable {C D : Type*} [Category C] [Category D] [Preadditive C] [Preadditive D]
  [HasZeroObject C] [HasZeroObject D] [HasShift C ‚Ñ§] [HasShift D ‚Ñ§]
  [‚àÄ (n : ‚Ñ§), (shiftFunctor C n).Additive] [‚àÄ (n : ‚Ñ§), (shiftFunctor D n).Additive]
  [Pretriangulated C] [Pretriangulated D]

scoped [ZeroObject] attribute [instance] CategoryTheory.Limits.HasZeroObject.zero'

open ZeroObject Limits Preadditive Pretriangulated CategoryTheory.Functor

variable (F : C ‚•§ D) (t‚ÇÅ : TStructure C) (t‚ÇÇ : TStructure D)

local instance : t‚ÇÅ.HasHeart := hasHeartFullSubcategory t‚ÇÅ
local instance : t‚ÇÇ.HasHeart := hasHeartFullSubcategory t‚ÇÇ

/--
An object of `t‚ÇÅ.heart` is called `F`-acyclic if its image by `F` is concentrated in degree
`0` (for `t‚ÇÇ`).
-/
def Acyclic : ObjectProperty t‚ÇÅ.Heart := fun X ‚Ü¶ t‚ÇÇ.heart (F.obj X.1)

namespace Functor

/--
The functor induced by `F` from the full subcategort of acyclic objects of `t‚ÇÅ.heart` to `t‚ÇÇ.heart`.
-/
abbrev fromAcyclic : (Acyclic F t‚ÇÅ t‚ÇÇ).FullSubcategory ‚•§ t‚ÇÇ.Heart := by
  refine t‚ÇÇ.heart.lift ((Acyclic F t‚ÇÅ t‚ÇÇ).Œπ ‚ãô t‚ÇÅ.ŒπHeart ‚ãô F) ?_
  intro ‚ü®_, h‚ü©
  simp only [comp_obj, Œπ_obj]
  exact h

instance [F.Additive] : Functor.Additive (t‚ÇÅ.ŒπHeart ‚ãô F) where
  map_add := by
    intro X Y f g
    simp only [comp_obj, comp_map, map_add]

end Functor

namespace AcyclicCategory

/--
Any object isomorphic to an acyclic object is acyclic.
-/
instance : (Acyclic F t‚ÇÅ t‚ÇÇ).IsClosedUnderIsomorphisms where
  of_iso e := t‚ÇÇ.heart.prop_of_iso ((t‚ÇÅ.ŒπHeart ‚ãô F).mapIso e)

variable (X Y : t‚ÇÅ.Heart)

/--
Any zero object is acyclic.
-/
lemma zero [F.Additive] {X : t‚ÇÅ.Heart} (hX : IsZero X) : Acyclic F t‚ÇÅ t‚ÇÇ X := by
  dsimp [Acyclic]
  exact IsClosedUnderIsomorphisms.of_iso (((t‚ÇÅ.ŒπHeart ‚ãô F).mapIso hX.isoZero).trans
    (t‚ÇÅ.ŒπHeart ‚ãô F).mapZeroObject).symm t‚ÇÇ.zero_mem_heart

instance : PreservesBinaryBiproducts t‚ÇÅ.ŒπHeart :=
  preservesBinaryBiproducts_of_preservesBiproducts _

/--
The product of two acyclic objects is acyclic.
-/
lemma prod [F.Additive] {X Y : t‚ÇÅ.Heart} (hX : Acyclic F t‚ÇÅ t‚ÇÇ X) (hY : Acyclic F t‚ÇÅ t‚ÇÇ Y) :
    Acyclic F t‚ÇÅ t‚ÇÇ (X ‚®Ø Y) := by
  dsimp [Acyclic]
  have : PreservesLimit (pair X Y) t‚ÇÅ.ŒπHeart :=
    preservesBinaryProduct_of_preservesBinaryBiproduct _
  have := PreservesLimitPair.iso t‚ÇÅ.ŒπHeart X Y
  exact IsClosedUnderIsomorphisms.of_iso (PreservesLimitPair.iso (t‚ÇÅ.ŒπHeart ‚ãô F) X Y).symm
      (prod_mem_heart t‚ÇÇ _ _ hX hY)

instance [F.Additive] : HasTerminal (Acyclic F t‚ÇÅ t‚ÇÇ).FullSubcategory := by
  let Z : (Acyclic F t‚ÇÅ t‚ÇÇ).FullSubcategory := ‚ü®0, zero F t‚ÇÅ t‚ÇÇ (isZero_zero t‚ÇÅ.Heart)‚ü©
  have : ‚àÄ X, Inhabited (X ‚ü∂ Z) := fun X => ‚ü®0‚ü©
  have : ‚àÄ X, Unique (X ‚ü∂ Z) := fun X =>
    { uniq := fun f => (ObjectProperty.Œπ (Acyclic F t‚ÇÅ t‚ÇÇ)).map_injective
          ((isZero_zero t‚ÇÅ.Heart).eq_of_tgt _ _) }
  exact hasTerminal_of_unique Z

instance [F.Additive] : HasBinaryProducts (Acyclic F t‚ÇÅ t‚ÇÇ).FullSubcategory := by
  apply hasLimitsOfShape_of_closedUnderLimits
  intro P c hc H
  exact prop_of_iso (Acyclic F t‚ÇÅ t‚ÇÇ)
    (limit.isoLimitCone ‚ü®_, (IsLimit.postcomposeHomEquiv (diagramIsoPair P) _).symm hc‚ü©)
    (prod F t‚ÇÅ t‚ÇÇ (H _) (H _))

instance [F.Additive] : HasFiniteProducts (Acyclic F t‚ÇÅ t‚ÇÇ).FullSubcategory :=
  hasFiniteProducts_of_has_binary_and_terminal

instance [F.Additive] : HasFiniteBiproducts (Acyclic F t‚ÇÅ t‚ÇÇ).FullSubcategory :=
  HasFiniteBiproducts.of_hasFiniteProducts

instance [F.Additive] : HasBinaryBiproducts (Acyclic F t‚ÇÅ t‚ÇÇ).FullSubcategory :=
  hasBinaryBiproducts_of_finite_biproducts _

end AcyclicCategory

instance [F.Additive] : Functor.Additive (F.fromAcyclic t‚ÇÅ t‚ÇÇ) where
  map_add := by
    dsimp [fromAcyclic]
    intro X Y f g
    simp only [lift_map, Functor.comp_map, Œπ_obj, Œπ_map, Functor.map_add]

instance : Functor.Additive (Acyclic F t‚ÇÅ t‚ÇÇ).Œπ where
  map_add := by
    intro X Y f g
    simp

section Triangulated

variable [IsTriangulated C] [F.CommShift ‚Ñ§] [F.IsTriangulated]

/--
An extension (in `t‚ÇÅ.heart`) of acyclic objects is acyclic.
-/
lemma extension_acyclic_of_acyclic {S : ShortComplex t‚ÇÅ.Heart} (SE : S.ShortExact)
    (hS‚ÇÅ : Acyclic F t‚ÇÅ t‚ÇÇ S.X‚ÇÅ) (hS‚ÇÉ : Acyclic F t‚ÇÅ t‚ÇÇ S.X‚ÇÉ) :
    Acyclic F t‚ÇÅ t‚ÇÇ S.X‚ÇÇ := by
  set DT' := F.map_distinguished _ (heartShortExactTriangle_distinguished t‚ÇÅ S SE)
  dsimp [Acyclic] at hS‚ÇÅ hS‚ÇÉ ‚ä¢
  rw [t‚ÇÇ.mem_heart_iff] at hS‚ÇÅ hS‚ÇÉ ‚ä¢
  exact ‚ü®t‚ÇÇ.isLE‚ÇÇ _ DT' 0 hS‚ÇÅ.1 hS‚ÇÉ.1, t‚ÇÇ.isGE‚ÇÇ _ DT' 0 hS‚ÇÅ.2 hS‚ÇÉ.2‚ü©

/--
A short exact complex in `t‚ÇÅ.heart` can be seen as an exact triangle in `C`, hence its
image by `F` is an exact triangle in `D`.
-/
noncomputable abbrev shortExactComplex_to_triangle {S : ShortComplex t‚ÇÅ.Heart}
    (he : S.ShortExact) : Pretriangulated.Triangle D :=
  F.mapTriangle.obj (heartShortExactTriangle t‚ÇÅ _ he)

lemma shortExactComplex_to_triangle_distinguished {S : ShortComplex t‚ÇÅ.Heart}
    (he : S.ShortExact) : shortExactComplex_to_triangle F t‚ÇÅ he ‚àà distinguishedTriangles :=
  F.map_distinguished _ (heartShortExactTriangle_distinguished t‚ÇÅ _ he)

end Triangulated

section Triangulated

variable [CategoryTheory.IsTriangulated D]

noncomputable local instance : t‚ÇÇ.HasHomology‚ÇÄ := t‚ÇÇ.hasHomology‚ÇÄ

noncomputable local instance : t‚ÇÇ.homology‚ÇÄ.ShiftSequence ‚Ñ§ :=
  Functor.ShiftSequence.tautological _ _

namespace Functor

/--
The functor `t‚ÇÅ.heatr.Œπ ‚ãô F ‚ãô t‚ÇÇ.homology 0` from the heart of `t‚ÇÅ` to the heat of `t‚ÇÇ`.
-/
noncomputable abbrev fromHeartToHeart : t‚ÇÅ.Heart ‚•§ t‚ÇÇ.Heart :=
  t‚ÇÅ.ŒπHeart ‚ãô F ‚ãô t‚ÇÇ.homology 0

end Functor

/--
If `X : t‚ÇÅ.heart` is acyclic, then the homology of `F.obj X` in nonzero degrees is zero.
-/
lemma isZero_homology_of_acyclic {X : t‚ÇÅ.Heart} (hX : Acyclic F t‚ÇÅ t‚ÇÇ X) (n : ‚Ñ§)
    (hn : n ‚â† 0) : IsZero ((t‚ÇÇ.homology n).obj (F.obj X.1)) := by
  simp only [Acyclic, mem_heart_iff] at hX
  by_cases h : n ‚â• 0
  ¬∑ have := hX.1
    exact t‚ÇÇ.isZero_homology_of_isLE (F.obj X.1) n 0 (lt_iff_le_and_ne.mpr ‚ü®h, Ne.symm hn‚ü©)
  ¬∑ have := hX.2
    exact t‚ÇÇ.isZero_homology_of_isGE (F.obj X.1) n 0 (lt_iff_not_ge.mpr h)

variable [IsTriangulated C] [F.CommShift ‚Ñ§] [F.IsTriangulated ]

/--
If `S` is a short exact triangle in `t‚ÇÅ.heart`, then its image by `F.fromHeartToHeart`
is isomorphic to the `0`th homology functor of `t‚ÇÇ` applied to the exact triangle
obtained by applying `shortExactComplex_to_triangle` to `S`.
-/
noncomputable abbrev map_shortExactComplex_iso_homology_triangle
    {S : ShortComplex t‚ÇÅ.Heart} (he : S.ShortExact) :
    (F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).mapShortComplex.obj S ‚âÖ
    ShortComplex.mk ((t‚ÇÇ.homology 0).map (shortExactComplex_to_triangle F t‚ÇÅ he).mor‚ÇÅ)
    ((t‚ÇÇ.homology 0).map (shortExactComplex_to_triangle F t‚ÇÅ he).mor‚ÇÇ)
    (by rw [‚Üê Functor.map_comp, comp_distTriang_mor_zero‚ÇÅ‚ÇÇ _
    (shortExactComplex_to_triangle_distinguished F t‚ÇÅ he), Functor.map_zero]) :=
  ShortComplex.isoMk (Iso.refl _) (Iso.refl _) (Iso.refl _) (by simp) (by simp)

/--
The functor `F ‚ãô t‚ÇÇ.homology 0` sends a short exact complex of acyclic objects to an exact complex.
-/
lemma map_shortExact_exact {S : ShortComplex t‚ÇÅ.Heart} (he : S.ShortExact) :
    ((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).mapShortComplex.obj S).Exact :=
  ShortComplex.exact_of_iso (map_shortExactComplex_iso_homology_triangle F t‚ÇÅ t‚ÇÇ he).symm
  (t‚ÇÇ.homology_exact‚ÇÇ _ (shortExactComplex_to_triangle_distinguished F t‚ÇÅ he) 0)

/--
The image by `F` of a mono with an acyclic cokernel is a mono.
-/
lemma mono_map_of_mono_and_acyclicCokernel {X Y : t‚ÇÅ.Heart} (f : X ‚ü∂ Y) [Mono f]
    (hv : IsZero ((t‚ÇÇ.homology (-1 : ‚Ñ§)).obj (F.obj (cokernel f).1))) :
    Mono ((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).map f) :=
  (ShortComplex.exact_iff_mono _ (IsZero.eq_zero_of_src hv _)).mp (t‚ÇÇ.homology_exact‚ÇÅ _
  (shortExactComplex_to_triangle_distinguished F t‚ÇÅ (monoCokernelComplexShortExact f))
  (-1 : ‚Ñ§) 0 (by simp))

/--
The image by `F` of an epi with an acyclic kernel is an epi.
-/
lemma epi_map_of_epi_and_acyclicKernel {X Y : t‚ÇÅ.Heart} (f : X ‚ü∂ Y) [Epi f]
    (hv : IsZero ((t‚ÇÇ.homology (1 : ‚Ñ§)).obj (F.obj (kernel f).1))) :
    Epi ((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).map f) :=
  (ShortComplex.exact_iff_epi _ (IsZero.eq_zero_of_tgt hv _)).mp (t‚ÇÇ.homology_exact‚ÇÉ _
  (shortExactComplex_to_triangle_distinguished F t‚ÇÅ (epiKernelComplexShortExact f))
  (0 : ‚Ñ§) 1 (by simp))

/--
The functor `F ‚ãô t‚ÇÇ.homology 0` sends a short exact complex `S` to a short exact
complex if `F.obj S.X‚ÇÅ` has no homology in degree `1` and `F.obj S.X‚ÇÉ` has no homology
in degree `-1`.
-/
lemma map_shortExact_of_shortExact_and_homology {S : ShortComplex t‚ÇÅ.Heart} (he : S.ShortExact)
    (hv‚ÇÅ : IsZero ((t‚ÇÇ.homology (1 : ‚Ñ§)).obj (F.obj S.X‚ÇÅ.1)))
    (hv‚ÇÇ : IsZero ((t‚ÇÇ.homology (-1 : ‚Ñ§)).obj (F.obj S.X‚ÇÉ.1))) :
    ((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).mapShortComplex.obj S).ShortExact where
  exact := map_shortExact_exact F t‚ÇÅ t‚ÇÇ he
  mono_f :=
    have := he.mono_f
    mono_map_of_mono_and_acyclicCokernel F t‚ÇÅ t‚ÇÇ S.f (IsZero.of_iso hv‚ÇÇ
    ((t‚ÇÇ.homology (-1 : ‚Ñ§)).mapIso (F.mapIso ((ObjectProperty.Œπ _).mapIso
    (IsColimit.coconePointUniqueUpToIso (cokernelIsCokernel S.f) he.gIsCokernel)))))
  epi_g :=
    have := he.epi_g
    epi_map_of_epi_and_acyclicKernel F t‚ÇÅ t‚ÇÇ S.g (IsZero.of_iso hv‚ÇÅ ((t‚ÇÇ.homology (1 : ‚Ñ§)).mapIso
    (F.mapIso ((ObjectProperty.Œπ _).mapIso (IsLimit.conePointUniqueUpToIso (kernelIsKernel S.g)
    he.fIsKernel)))))

/--
The functor `F ‚ãô t‚ÇÇ.homology 0` sends a short exact complex of acyclic objects to a short exact
complex.
-/
lemma map_shortExact_of_shortExact_and_acyclic {S : ShortComplex t‚ÇÅ.Heart} (he : S.ShortExact)
    (hv‚ÇÅ : Acyclic F t‚ÇÅ t‚ÇÇ S.X‚ÇÅ) (hv‚ÇÇ : Acyclic F t‚ÇÅ t‚ÇÇ S.X‚ÇÉ) :
    ((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).mapShortComplex.obj S).ShortExact :=
  map_shortExact_of_shortExact_and_homology F t‚ÇÅ t‚ÇÇ he
  (isZero_homology_of_acyclic F t‚ÇÅ t‚ÇÇ hv‚ÇÅ (1 : ‚Ñ§) (by simp))
  (isZero_homology_of_acyclic F t‚ÇÅ t‚ÇÇ hv‚ÇÇ (-1 : ‚Ñ§) (by simp))

/--
If a map `f` has an acyclic kernel and an acyclic cokernel, then its image by `F` has an image
factorization, whose image is `F.obj (Abelian.image f)`.
-/
@[simps!]
noncomputable def imageFactorisation_map_of_acyclic_ker_coker {X Y : t‚ÇÅ.Heart} (f : X ‚ü∂ Y)
    (h‚ÇÅ : Acyclic F t‚ÇÅ t‚ÇÇ (cokernel f)) (h‚ÇÇ : Acyclic F t‚ÇÅ t‚ÇÇ (kernel f)) :
    ImageFactorisation ((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).map f) := by
  refine imageFactorisationOfNormalEpi (C := t‚ÇÇ.Heart) _ ?_ ?_
  ¬∑ refine {I := (F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).obj (Abelian.image f),
            e := (F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).map (Abelian.factorThruImage _),
            m := (F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).map (Abelian.image.Œπ _),
            m_mono := ?_, fac := ?_}
    ¬∑ refine mono_map_of_mono_and_acyclicCokernel F t‚ÇÅ t‚ÇÇ (Abelian.image.Œπ f)
        (@isZero_homology_of_isGE _ _ _ _ _ _ _ t‚ÇÇ _ _ _ _ (-1 : ‚Ñ§) 0 (by simp only [Int.reduceNeg,
          Left.neg_neg_iff, zero_lt_one]) ?_)
      have := Limits.IsColimit.coconePointUniqueUpToIso (cokernelIsCokernel f)
       (Limits.isCokernelEpiComp (cokernelIsCokernel (Abelian.image.Œπ f))
        (Abelian.factorThruImage f) (Abelian.image.fac f).symm)
      have := IsClosedUnderIsomorphisms.of_iso this h‚ÇÅ
      simp only [Acyclic, mem_heart_iff] at this
      exact this.2
    ¬∑ rw [‚Üê map_comp, Abelian.image.fac]
  ¬∑ refine @normalEpiOfEpi (C := t‚ÇÇ.Heart) _ _ _ _ _ _  ?_
    refine epi_map_of_epi_and_acyclicKernel F t‚ÇÅ t‚ÇÇ (Abelian.factorThruImage f)
      (@isZero_homology_of_isLE _ _ _ _ _ _ _ t‚ÇÇ _ _ _ _ _ (1 : ‚Ñ§) 0 zero_lt_one ?_)
    have := Limits.IsLimit.conePointUniqueUpToIso (kernelIsKernel f) (Limits.isKernelCompMono
      (kernelIsKernel (Abelian.factorThruImage f)) (Abelian.image.Œπ f) (Abelian.image.fac f).symm)
    have := IsClosedUnderIsomorphisms.of_iso this h‚ÇÇ
    simp only [Acyclic, mem_heart_iff] at this
    exact this.1

/--
If a map `f` in `t‚ÇÅ.heart` has an acyclic kernel and an acyclic cokernel, then
`t‚ÇÅ.heart.Œπ ‚ãô F ‚ãô t‚ÇÇ.homology 0` preserves the image of `f`.
-/
noncomputable def isoImageOfAcyclic {X Y : t‚ÇÅ.Heart} (f : X ‚ü∂ Y)
    (h‚ÇÅ : Acyclic F t‚ÇÅ t‚ÇÇ (cokernel f)) (h‚ÇÇ : Acyclic F t‚ÇÅ t‚ÇÇ (kernel f)) :
    (F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).obj (Abelian.image f) ‚âÖ
    Abelian.image ((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).map f) :=
  (IsImage.isoExt (imageFactorisation_map_of_acyclic_ker_coker F t‚ÇÅ t‚ÇÇ f h‚ÇÅ h‚ÇÇ).isImage
  (Limits.Image.isImage ((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).map f))).trans (Abelian.imageIsoImage _).symm

@[simp]
lemma isoImageOfAcyclic_comp_Œπ {X Y : t‚ÇÅ.Heart} (f : X ‚ü∂ Y)
    (h‚ÇÅ : Acyclic F t‚ÇÅ t‚ÇÇ (cokernel f)) (h‚ÇÇ : Acyclic F t‚ÇÅ t‚ÇÇ (kernel f)) :
    (isoImageOfAcyclic F t‚ÇÅ t‚ÇÇ f h‚ÇÅ h‚ÇÇ).hom ‚â´ Abelian.image.Œπ ((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).map f) =
    (F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).map (Abelian.image.Œπ f) := by
  simp only [isoImageOfAcyclic]
  rw [Iso.trans_hom, Iso.symm_hom, assoc, image_compat]
  erw [IsImage.isoExt_hom_m]
  rfl

lemma factorThruImage_comp_isoImageOfAcyclic {X Y : t‚ÇÅ.Heart} (f : X ‚ü∂ Y)
    (h‚ÇÅ : Acyclic F t‚ÇÅ t‚ÇÇ (cokernel f)) (h‚ÇÇ : Acyclic F t‚ÇÅ t‚ÇÇ (kernel f)) :
    (F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).map (Abelian.factorThruImage f) ‚â´
    (isoImageOfAcyclic F t‚ÇÅ t‚ÇÇ f h‚ÇÅ h‚ÇÇ).hom
    = Abelian.factorThruImage ((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).map f) := by
  rw [‚Üê cancel_mono (Abelian.image.Œπ ((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).map f)), assoc,
  isoImageOfAcyclic_comp_Œπ, ‚Üê map_comp, Abelian.image.fac, Abelian.image.fac]

/--
If `f` is a morphism in `t‚ÇÅ.heart` with an acyclic image, then its kernel comparison morphism
for the functor `F.fromHeartToHeart` is a mono.
-/
lemma mono_kernelComparison_of_acyclic_image {X Y : t‚ÇÅ.Heart} (f : X ‚ü∂ Y)
    (h‚ÇÉ : Acyclic F t‚ÇÅ t‚ÇÇ (Abelian.image f)) :
    Mono (kernelComparison f (F.fromHeartToHeart t‚ÇÅ t‚ÇÇ)) := by
  refine @mono_of_mono_fac _ _ _ _ _ _ (kernel.Œπ _) ((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).map (kernel.Œπ f))
    ?_ (by rw [kernelComparison_comp_Œπ])
  refine mono_map_of_mono_and_acyclicCokernel F t‚ÇÅ t‚ÇÇ (kernel.Œπ f) (@isZero_homology_of_isGE
    _ _ _ _ _ _ _ t‚ÇÇ _ _ _ _ (-1 : ‚Ñ§) 0 (by simp only [Int.reduceNeg, Left.neg_neg_iff,
    zero_lt_one]) ?_)
  have := IsClosedUnderIsomorphisms.of_iso (Abelian.coimageIsoImage _).symm h‚ÇÉ
  simp only [Acyclic, mem_heart_iff] at this
  exact this.2

/--
If `f` is a morphism in `t‚ÇÅ.heart` with an acyclic image, an acyclic kernel and an acyclic cokernel,
then its kernel comparison morphism for the functor `F.fromHeartToHeart` is an epi.
-/
lemma epi_kernelComparison_of_acyclic_homology {X Y : t‚ÇÅ.Heart} (f : X ‚ü∂ Y)
    (h‚ÇÅ : Acyclic F t‚ÇÅ t‚ÇÇ (cokernel f)) (h‚ÇÇ : Acyclic F t‚ÇÅ t‚ÇÇ (kernel f))
    (h‚ÇÉ : Acyclic F t‚ÇÅ t‚ÇÇ (Abelian.image f)) :
    Epi (kernelComparison f (F.fromHeartToHeart t‚ÇÅ t‚ÇÇ)) := by
  set R‚ÇÅ := ((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).mapShortComplex.obj (ShortComplex.mk (kernel.Œπ f)
    (Abelian.factorThruImage f)
    (by rw [‚Üê cancel_mono (Abelian.image.Œπ f), assoc, Abelian.image.fac, zero_comp,
      kernel.condition f]))).toComposableArrows
  set R‚ÇÇ := (ShortComplex.mk (kernel.Œπ ((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).map f))
    (Abelian.factorThruImage ((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).map f))
    (by rw [‚Üê cancel_mono (Abelian.image.Œπ _), zero_comp, assoc, Abelian.image.fac,
    kernel.condition])).toComposableArrows
  have hR‚ÇÅ : R‚ÇÅ.Exact := (map_shortExact_of_shortExact_and_acyclic F t‚ÇÅ t‚ÇÇ
    (kernelImageComplexShortExact f) h‚ÇÇ h‚ÇÉ).exact.exact_toComposableArrows
  have hR‚ÇÇ : R‚ÇÇ.Exact := (kernelImageComplexShortExact _).exact.exact_toComposableArrows
  set œÜ : R‚ÇÅ ‚ü∂ R‚ÇÇ := by
    refine ComposableArrows.homMk ?_ ?_
    ¬∑ intro i
      match i with
      | 0 => exact kernelComparison f (F.fromHeartToHeart t‚ÇÅ t‚ÇÇ)
      | 1 => exact ùüô _
      | 2 => exact (isoImageOfAcyclic F t‚ÇÅ t‚ÇÇ f h‚ÇÅ h‚ÇÇ).hom
    ¬∑ intro i _
      match i with
      | 0 => erw [kernelComparison_comp_Œπ, comp_id]; rfl
      | 1 => erw [factorThruImage_comp_isoImageOfAcyclic, id_comp]; rfl
  refine Abelian.epi_of_mono_of_epi_of_mono œÜ hR‚ÇÅ hR‚ÇÇ ?_ ?_ ?_
  ¬∑ change Mono (kernel.Œπ _); exact inferInstance
  ¬∑ change Epi (ùüô _); exact inferInstance
  ¬∑ change Mono (isoImageOfAcyclic F t‚ÇÅ t‚ÇÇ f h‚ÇÅ h‚ÇÇ).hom; exact inferInstance

/--
If `f` is a morphism in `t‚ÇÅ.heart` with an acyclic image, an acyclic kernel and an acyclic cokernel,
then its kernel comparison morphism for the functor `F.fromHeartToHeart` is an iso.
-/
noncomputable def isIso_kernelComparison_of_acyclic_homology {X Y : t‚ÇÅ.Heart} (f : X ‚ü∂ Y)
    (h‚ÇÅ : Acyclic F t‚ÇÅ t‚ÇÇ (cokernel f)) (h‚ÇÇ : Acyclic F t‚ÇÅ t‚ÇÇ (kernel f))
    (h‚ÇÉ : Acyclic F t‚ÇÅ t‚ÇÇ (Abelian.image f)) :
    IsIso (kernelComparison f (F.fromHeartToHeart t‚ÇÅ t‚ÇÇ)) :=
  @isIso_of_mono_of_epi _ _ _ _ _ _ (mono_kernelComparison_of_acyclic_image F t‚ÇÅ t‚ÇÇ f h‚ÇÉ)
  (epi_kernelComparison_of_acyclic_homology F t‚ÇÅ t‚ÇÇ f h‚ÇÅ h‚ÇÇ h‚ÇÉ)

/--
If `f` is a morphism in `t‚ÇÅ.heart` with an acyclic image, then its cokernel comparison morphism
for the functor `F.fromHeartToHeart` is an epi.
-/
lemma epi_cokernelComparison_of_acyclic_image {X Y : t‚ÇÅ.Heart} (f : X ‚ü∂ Y)
    (h‚ÇÉ : Acyclic F t‚ÇÅ t‚ÇÇ (Abelian.image f)) :
    Epi (cokernelComparison f (F.fromHeartToHeart t‚ÇÅ t‚ÇÇ)) := by
  simp only [Acyclic, mem_heart_iff] at h‚ÇÉ
  exact @epi_of_epi_fac _ _ _ _ _ (cokernel.œÄ _) _ ((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).map (cokernel.œÄ f))
    (epi_map_of_epi_and_acyclicKernel F t‚ÇÅ t‚ÇÇ (cokernel.œÄ f) (@isZero_homology_of_isLE
    _ _ _ _ _ _ _ t‚ÇÇ _ _ _ _ _ (1 : ‚Ñ§) 0 zero_lt_one h‚ÇÉ.1)) (by rw [œÄ_comp_cokernelComparison])

/--
If `f` is a morphism in `t‚ÇÅ.heart` with an acyclic image, an acyclic kernel and an acyclic cokernel,
then its cokernel comparison morphism for the functor `F.fromHeartToHeart` is a mono.
-/
lemma mono_cokernelComparison_of_acyclic_homology {X Y : t‚ÇÅ.Heart} (f : X ‚ü∂ Y)
    (h‚ÇÅ : Acyclic F t‚ÇÅ t‚ÇÇ (cokernel f)) (h‚ÇÇ : Acyclic F t‚ÇÅ t‚ÇÇ (kernel f))
    (h‚ÇÉ : Acyclic F t‚ÇÅ t‚ÇÇ (Abelian.image f)) :
    Mono (cokernelComparison f (F.fromHeartToHeart t‚ÇÅ t‚ÇÇ)) := by
  set R‚ÇÇ := ((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).mapShortComplex.obj (ShortComplex.mk (Abelian.image.Œπ f)
    (Limits.cokernel.œÄ f)
    (by simp only [equalizer_as_kernel, kernel.condition]))).toComposableArrows
  set R‚ÇÅ := (ShortComplex.mk (Abelian.image.Œπ ((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).map f))
    (cokernel.œÄ ((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).map f))
    (by simp only [comp_obj, Functor.comp_map, equalizer_as_kernel,
    kernel.condition])).toComposableArrows
  have hR‚ÇÇ : R‚ÇÇ.Exact := (map_shortExact_of_shortExact_and_acyclic F t‚ÇÅ t‚ÇÇ
    (epiKernelComplexShortExact (cokernel.œÄ f)) h‚ÇÉ h‚ÇÅ).exact.exact_toComposableArrows
  have hR‚ÇÅ : R‚ÇÅ.Exact := (ShortComplex.exact_of_f_is_kernel _
    (kernelIsKernel _)).exact_toComposableArrows
  set œÜ : R‚ÇÅ ‚ü∂ R‚ÇÇ := by
    refine ComposableArrows.homMk ?_ ?_
    ¬∑ intro i
      match i with
      | 0 => exact (isoImageOfAcyclic F t‚ÇÅ t‚ÇÇ f h‚ÇÅ h‚ÇÇ).inv
      | 1 => exact ùüô _
      | 2 => exact cokernelComparison _ _
    ¬∑ intro i _
      match i with
      | 0 => rw [‚Üê cancel_epi (isoImageOfAcyclic F t‚ÇÅ t‚ÇÇ f h‚ÇÅ h‚ÇÇ).hom]; erw [comp_id]
             rw [‚Üê assoc, Iso.hom_inv_id, id_comp]
             erw [isoImageOfAcyclic_comp_Œπ]; rfl
      | 1 => erw [id_comp, œÄ_comp_cokernelComparison]; rfl
  refine Abelian.mono_of_epi_of_epi_of_mono œÜ hR‚ÇÅ hR‚ÇÇ ?_ ?_ ?_
  ¬∑ change Epi (cokernel.œÄ _); exact inferInstance
  ¬∑ change Epi (isoImageOfAcyclic F t‚ÇÅ t‚ÇÇ f h‚ÇÅ h‚ÇÇ).inv; exact inferInstance
  ¬∑ change Mono (ùüô _); exact inferInstance

/--
If `f` is a morphism in `t‚ÇÅ.heart` with an acyclic image, an acyclic kernel and an acyclic cokernel,
then its cokernel comparison morphism for the functor `F.fromHeartToHeart` is an iso.
-/
noncomputable def isIso_cokernelComparison_of_acyclic_homology {X Y : t‚ÇÅ.Heart} (f : X ‚ü∂ Y)
    (h‚ÇÅ : Acyclic F t‚ÇÅ t‚ÇÇ (cokernel f)) (h‚ÇÇ : Acyclic F t‚ÇÅ t‚ÇÇ (kernel f))
    (h‚ÇÉ : Acyclic F t‚ÇÅ t‚ÇÇ (Abelian.image f)) :
    IsIso (cokernelComparison f (F.fromHeartToHeart t‚ÇÅ t‚ÇÇ)) :=
  @isIso_of_mono_of_epi _ _ _ _ _ _ (mono_cokernelComparison_of_acyclic_homology F t‚ÇÅ t‚ÇÇ f h‚ÇÅ h‚ÇÇ h‚ÇÉ)
  (epi_cokernelComparison_of_acyclic_image F t‚ÇÅ t‚ÇÇ f h‚ÇÉ)

/--
Let `S` be a short exact sequence in `t‚ÇÅ.heart` such that `S.X‚ÇÇ` is acyclic. Then the connecting
morphism in the long exact sequence of homology of the exact triangle in `D` "image of `S` by `F`"
(i.e. given by `shortExactComplex_to_triangle`) is an isomorphism from `n`th homology of
`F.obj S.X‚ÇÉ` to the `(n + 1)`st homology of `F.obj S.X‚ÇÅ`.
-/
noncomputable def shortExact_connecting_iso {S : ShortComplex t‚ÇÅ.Heart} (hS : S.ShortExact)
    (hS‚ÇÅ : Acyclic F t‚ÇÅ t‚ÇÇ S.X‚ÇÇ) {n : ‚Ñ§} (hn : n ‚â† -1 ‚àß n ‚â† 0) :
    (t‚ÇÇ.homology n).obj (F.obj S.X‚ÇÉ.1) ‚âÖ (t‚ÇÇ.homology (n + 1)).obj (F.obj S.X‚ÇÅ.1) := by
  set T := shortExactComplex_to_triangle F t‚ÇÅ hS
  have hT : T ‚àà distinguishedTriangles := shortExactComplex_to_triangle_distinguished F t‚ÇÅ hS
  set f := t‚ÇÇ.homologyŒ¥ (shortExactComplex_to_triangle F t‚ÇÅ hS) n (n + 1) rfl
  have h‚ÇÅ : Mono f := by
    refine (ShortComplex.exact_iff_mono _ (Limits.zero_of_source_iso_zero _ ?_)).mp
      (t‚ÇÇ.homology_exact‚ÇÉ _ hT n (n + 1) rfl)
    change (t‚ÇÇ.homology n).obj (F.obj S.X‚ÇÇ.1) ‚âÖ 0
    refine Limits.IsZero.isoZero ?_
    by_cases hn' : 0 ‚â§ n
    ¬∑ letI : t‚ÇÇ.IsLE (F.obj S.X‚ÇÇ.1) 0 := {le := hS‚ÇÅ.1}
      exact t‚ÇÇ.isZero_homology_of_isLE _ n 0 (lt_iff_le_and_ne.mpr ‚ü®hn', Ne.symm hn.2‚ü©)
    ¬∑ letI : t‚ÇÇ.IsGE (F.obj S.X‚ÇÇ.1) 0 := {ge := hS‚ÇÅ.2}
      exact t‚ÇÇ.isZero_homology_of_isGE _ n 0 (lt_iff_not_ge.mpr hn')
  have h‚ÇÇ : Epi f := by
    refine (ShortComplex.exact_iff_epi _ (Limits.zero_of_target_iso_zero _ ?_)).mp
      (t‚ÇÇ.homology_exact‚ÇÅ _ hT n (n + 1) rfl)
    change (t‚ÇÇ.homology (n + 1)).obj (F.obj S.X‚ÇÇ.1) ‚âÖ 0
    refine Limits.IsZero.isoZero ?_
    by_cases hn' : 0 ‚â§ n
    ¬∑ letI : t‚ÇÇ.IsLE (F.obj S.X‚ÇÇ.1) 0 := {le := hS‚ÇÅ.1}
      exact t‚ÇÇ.isZero_homology_of_isLE _ (n + 1) 0 (Int.lt_add_one_iff.mpr hn')
    ¬∑ letI : t‚ÇÇ.IsGE (F.obj S.X‚ÇÇ.1) 0 := {ge := hS‚ÇÅ.2}
      refine t‚ÇÇ.isZero_homology_of_isGE _ (n + 1) 0 ?_
      rw [lt_iff_le_and_ne, Int.add_one_le_iff, and_iff_right (lt_iff_not_ge.mpr hn'), ne_eq,
          ‚Üê eq_neg_iff_add_eq_zero]
      exact hn.1
  exact @asIso _ _ _ _ f ((isIso_iff_mono_and_epi f).mpr ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©)

/--
Let `S` ba a cochain complex in `t‚ÇÅ.heart`. Suppose that `S` is exact in degree `r + 1`, and that
the `r`th entry of `S` is acyclic. Then, for every `n` different from `0` and `-1`, we have
an isomorphism betweem the `n`th cohomology of `F.obj (kernel (S.d (r + 1) (r + 2)))` and the
`(n + 1)`st homology of `F.obj (kernel (S.d r (r + 1)))`.
-/
noncomputable def iso_cohomology_of_acyclic_and_exact (S : CochainComplex t‚ÇÅ.Heart ‚Ñ§) (r k l m : ‚Ñ§)
    (hrk : r + 1 = k) (hkl : k = l) (hlm : l + 1 = m) (h‚ÇÅ : Acyclic F t‚ÇÅ t‚ÇÇ (S.X r))
    (h‚ÇÇ : S.ExactAt l) {n : ‚Ñ§} (hn : n ‚â† -1 ‚àß n ‚â† 0) :
    (t‚ÇÇ.homology n).obj (F.obj (Limits.kernel (S.d l m)).1) ‚âÖ (t‚ÇÇ.homology (n + 1)).obj
    (F.obj (Limits.kernel (S.d r k)).1) :=
  (((t‚ÇÇ.homology n).mapIso (F.mapIso ((ObjectProperty.Œπ _).mapIso
  ((S.sc' r l m).isoAbelianImageToKernelOfExact ((S.exactAt_iff' r l m
  (by simp only [CochainComplex.prev]; linarith [hrk, hkl])
  (by simp only [CochainComplex.next, hlm])).mp h‚ÇÇ))))).symm.trans
  (shortExact_connecting_iso F t‚ÇÅ t‚ÇÇ (kernelImageComplexShortExact (S.d r l)) h‚ÇÅ hn)).trans
  ((t‚ÇÇ.homology (n + 1)).mapIso (F.mapIso ((ObjectProperty.Œπ _).mapIso
  (kernel.mapIso (S.d r l) (S.d r k) (Iso.refl _) (S.XIsoOfEq hkl.symm)
  (by simp only [HomologicalComplex.d_comp_XIsoOfEq_hom, Iso.refl_hom, id_comp])))))

noncomputable def rightAcyclic_ker_aux (S : CochainComplex t‚ÇÅ.Heart ‚Ñ§) {r k l : ‚Ñ§} (hkl : k + 1 = l)
    (hr : r > 0) (hk1 : ‚àÄ (i : ‚Ñ§), i ‚â§ k ‚Üí S.ExactAt i)
    (hk2 : ‚àÄ (i : ‚Ñ§), i ‚â§ k ‚Üí Acyclic F t‚ÇÅ t‚ÇÇ (S.X i)) (n : ‚Ñï) :
    (t‚ÇÇ.homology r).obj (F.obj (Limits.kernel (S.d k l)).1) ‚âÖ (t‚ÇÇ.homology (r + n)).obj
    (F.obj (Limits.kernel (S.d (k - n) (l - n))).1) := by
  induction' n with n hn
  ¬∑ simp only [CharP.cast_eq_zero, add_zero, Int.cast_ofNat_Int]
    erw [sub_zero, sub_zero]
  ¬∑ have : r + ‚Üë(n + 1) = (r + n) + 1 := by simp only [Nat.cast_add, Nat.cast_one]; ring
    rw [this]
    have : k - ‚Üë(n + 1) = (k - n) - 1 := by simp only [Nat.cast_add, Nat.cast_one]; ring
    rw [this]
    have : l - ‚Üë(n + 1) = (l - n) - 1 := by simp only [Nat.cast_add, Nat.cast_one]; ring
    rw [this]
    refine hn.trans (iso_cohomology_of_acyclic_and_exact F t‚ÇÅ t‚ÇÇ S (k - n - 1) (l - n - 1) (k - n)
      (l - n) (by linarith) (by linarith [hkl]) (by linarith) (hk2 (k - n - 1) (by linarith))
      (hk1 (k - n) (by linarith)) (n := r + n) ‚ü®by linarith [hr], by linarith [hr]‚ü©)

/--
Let `S` be a cochain complex in `t‚ÇÅ.heart`. Suppose that `S` is exact in degree `‚â§ k`,
and that the entries of `S` are acyclic in degree `‚â§ k` and zero in small enough degree.
Then the homology of `F.obj (kernel (S.d k (k + 1)))` is zero in positive degree.
-/
lemma rightAcyclic_ker_of_bounded_complex (S : CochainComplex t‚ÇÅ.Heart ‚Ñ§) {r k l : ‚Ñ§}
    (hkl : k + 1 = l) (hr : r > 0) (hk1 : ‚àÄ (i : ‚Ñ§), i ‚â§ k ‚Üí S.ExactAt i)
    (hk2 : ‚àÄ (i : ‚Ñ§), i ‚â§ k ‚Üí Acyclic F t‚ÇÅ t‚ÇÇ (S.X i)) {a : ‚Ñ§}
    (ha : ‚àÄ (j : ‚Ñ§), j ‚â§ a ‚Üí IsZero (S.X j)) :
    IsZero ((t‚ÇÇ.homology r).obj (F.obj (Limits.kernel (S.d k l)).1)) := by
  refine IsZero.of_iso ?_ (rightAcyclic_ker_aux F t‚ÇÅ t‚ÇÇ S hkl hr hk1 hk2 (k - a).natAbs)
  suffices h : IsZero (kernel (S.d (k - ‚Üë(k - a).natAbs) (l - ‚Üë(k - a).natAbs))) by
    refine Functor.map_isZero _ (Functor.map_isZero _ ?_)
    change IsZero ((ObjectProperty.Œπ _).obj _)
    refine Functor.map_isZero _ h
  refine IsZero.of_mono (kernel.Œπ (S.d (k - (k - a).natAbs) (l - (k - a).natAbs))) (ha _ ?_)
  rw [tsub_le_iff_right, ‚Üê tsub_le_iff_left]; exact Int.le_natAbs

/--
Suppose that `F` has finite cohomological degree (relative to `t‚ÇÅ` and `t‚ÇÇ`).
Let `S` be a cochain complex in `t‚ÇÅ.heart`. Suppose that `S` is exact in degree `‚â§ k`
and that the entries of `S` in degree `‚â§ k` are acyclic.
Then the homology of `F.obj (kernel (S.d k (k + 1)))` is zero in positive degree.
-/
lemma rightAcyclic_ker_of_bounded_functor (S : CochainComplex t‚ÇÅ.Heart ‚Ñ§) {r k l : ‚Ñ§}
    (hkl : k + 1 = l) (hr : r > 0) (hk1 : ‚àÄ (i : ‚Ñ§), i ‚â§ k ‚Üí S.ExactAt i)
    (hk2 : ‚àÄ (i : ‚Ñ§), i ‚â§ k ‚Üí Acyclic F t‚ÇÅ t‚ÇÇ (S.X i)) {d : ‚Ñ§}
    (hd : ‚àÄ (X : t‚ÇÅ.Heart) (j : ‚Ñ§), d ‚â§ j ‚Üí IsZero ((t‚ÇÇ.homology j).obj (F.obj X.1))) :
    IsZero ((t‚ÇÇ.homology r).obj (F.obj (Limits.kernel (S.d k l)).1)) := by
  refine IsZero.of_iso (hd _ _ ?_) (rightAcyclic_ker_aux F t‚ÇÅ t‚ÇÇ S hkl hr hk1 hk2 (d - r).natAbs)
  rw [‚Üê tsub_le_iff_left]; exact Int.le_natAbs

noncomputable def leftAcyclic_ker_aux (S : CochainComplex t‚ÇÅ.Heart ‚Ñ§) {r k l : ‚Ñ§}
    (hkl : k + 1 = l) (hr : r < 0) (hk1 : ‚àÄ (i : ‚Ñ§), k < i ‚Üí S.ExactAt i)
    (hk2 : ‚àÄ (i : ‚Ñ§), k ‚â§ i ‚Üí Acyclic F t‚ÇÅ t‚ÇÇ (S.X i)) (n : ‚Ñï) :
    (t‚ÇÇ.homology r).obj (F.obj (Limits.kernel (S.d k l)).1) ‚âÖ (t‚ÇÇ.homology (r - n)).obj
    (F.obj (Limits.kernel (S.d (k + n) (l + n))).1) := by
  induction' n with n hn
  ¬∑ simp only [CharP.cast_eq_zero, sub_zero, Int.cast_ofNat_Int]
    erw [add_zero, add_zero]
  ¬∑ refine hn.trans ?_
    have : r - n = r - (n + 1) + 1 := by ring
    erw [this]
    exact (iso_cohomology_of_acyclic_and_exact F t‚ÇÅ t‚ÇÇ S (k + n) (l + n) (k + (n + 1))
      (l + (n + 1)) (by linarith) (by linarith) (by linarith)
      (hk2 (k + n) (by linarith)) (hk1 (k + (n + 1)) (by linarith)) (n := r - (n + 1))
      ‚ü®by linarith [hr], by linarith [hr]‚ü©).symm

/--
Let `S` be a cochain complex in `t‚ÇÅ.heart`. Suppose that `S` is exact in degree `> k`,
and that the entries of `S` are acyclic in degree `‚â§ k` and zero in large enough degree.
Then the homology of `F.obj (kernel (S.d k (k + 1)))` is zero in negative degree.
-/
lemma leftAcyclic_ker_of_boundedComplex (S : CochainComplex t‚ÇÅ.Heart ‚Ñ§) {r k l : ‚Ñ§}
    (hkl : k + 1 = l) (hr : r < 0) (hk1 : ‚àÄ (i : ‚Ñ§), k < i ‚Üí S.ExactAt i)
    (hk2 : ‚àÄ (i : ‚Ñ§), k ‚â§ i ‚Üí Acyclic F t‚ÇÅ t‚ÇÇ (S.X i)) {b : ‚Ñ§}
    (hb : ‚àÄ (j : ‚Ñ§), b ‚â§ j ‚Üí IsZero (S.X j)) :
    IsZero ((t‚ÇÇ.homology r).obj (F.obj (Limits.kernel (S.d k l)).1)) := by
  refine IsZero.of_iso ?_ (leftAcyclic_ker_aux F t‚ÇÅ t‚ÇÇ S hkl hr hk1 hk2 (b - k).natAbs)
  suffices h : IsZero (kernel (S.d (k + ‚Üë(b - k).natAbs) (l + ‚Üë(b - k).natAbs))) by
    refine Functor.map_isZero _ (Functor.map_isZero _ ?_)
    change IsZero ((ObjectProperty.Œπ _).obj _)
    refine Functor.map_isZero _ h
  refine IsZero.of_mono (kernel.Œπ (S.d (k + (b - k).natAbs) (l + (b - k).natAbs))) (hb _ ?_)
  rw [‚Üê tsub_le_iff_left]; exact Int.le_natAbs

/--
Suppose that `F` has finite cohomological degree (relative to `t‚ÇÅ` and `t‚ÇÇ`).
Let `S` be a cochain complex in `t‚ÇÅ.heart`. Suppose that `S` is exact in degree `> k`
and that the entries of `S` in degree `‚â• k` are acyclic.
Then the homology of `F.obj (kernel (S.d k (k + 1)))` is zero in negative degree.
-/
lemma leftAcyclic_ker_of_bounded_functor (S : CochainComplex t‚ÇÅ.Heart ‚Ñ§) {r k l : ‚Ñ§}
    (hkl : k + 1 = l) (hr : r < 0) (hk1 : ‚àÄ (i : ‚Ñ§), k < i ‚Üí S.ExactAt i)
    (hk2 : ‚àÄ (i : ‚Ñ§), k ‚â§ i ‚Üí Acyclic F t‚ÇÅ t‚ÇÇ (S.X i)) {c : ‚Ñ§}
    (hc : ‚àÄ (X : t‚ÇÅ.Heart) (j : ‚Ñ§), j ‚â§ c ‚Üí IsZero ((t‚ÇÇ.homology j).obj (F.obj X.1))) :
    IsZero ((t‚ÇÇ.homology r).obj (F.obj (Limits.kernel (S.d k l)).1)) := by
  refine IsZero.of_iso (hc _ _ ?_) (leftAcyclic_ker_aux F t‚ÇÅ t‚ÇÇ S hkl hr hk1 hk2 (r - c).natAbs)
  rw [tsub_le_iff_left, ‚Üê tsub_le_iff_right]; exact Int.le_natAbs

variable [NonDegenerate t‚ÇÇ]

/--
Let `S` be a cochain complex in `t‚ÇÅ.heart`. Suppose that `S` is exact, and that the entries
of `S` are acyclic in every degree and zero outside of a bounded interval.
Then `kernel (S.d k (k + 1))` is acyclic for every `k`.
-/
lemma acyclic_ker_of_bounded_exact_acyclic_complex (S : CochainComplex t‚ÇÅ.Heart ‚Ñ§) {a b : ‚Ñ§}
    (hexact : ‚àÄ (i : ‚Ñ§), S.ExactAt i)
    (hacy : ‚àÄ (i : ‚Ñ§), Acyclic F t‚ÇÅ t‚ÇÇ (S.X i))
    (ha : ‚àÄ (j : ‚Ñ§), j ‚â§ a ‚Üí IsZero (S.X j))
    (hb : ‚àÄ (j : ‚Ñ§), b ‚â§ j ‚Üí IsZero (S.X j)) (k l : ‚Ñ§) (hkl : k + 1 = l) :
    Acyclic F t‚ÇÅ t‚ÇÇ (Limits.kernel (S.d k l)) := by
  simp only [Acyclic]
  refine isHeart_of_isZero_homology t‚ÇÇ _ ?_
  intro j hj
  rw [ne_iff_lt_or_gt] at hj
  cases hj with
  | inl h => exact leftAcyclic_ker_of_boundedComplex F t‚ÇÅ t‚ÇÇ S hkl h (fun _ _ ‚Ü¶ hexact _)
               (fun _ _ ‚Ü¶ hacy _) hb
  | inr h => exact rightAcyclic_ker_of_bounded_complex F t‚ÇÅ t‚ÇÇ S hkl h (fun _ _ ‚Ü¶ hexact _)
               (fun _ _ ‚Ü¶ hacy _) ha

/--
Suppose that `F` has finite cohomological degree (relative to `t‚ÇÅ` and `t‚ÇÇ`).
Let `S` be a cochain complex in `t‚ÇÅ.heart`. Suppose that `S` is exact and that the entries
of `S` are acyclic.
Then `kernel (S.d k (k + 1))` is acyclic for every `k`.
-/
lemma acyclic_ker_of_exact_acyclic_complex_and_bounded_functor (S : CochainComplex t‚ÇÅ.Heart ‚Ñ§)
    {a b: ‚Ñ§} (hexact : ‚àÄ (i : ‚Ñ§), S.ExactAt i)
    (hacy : ‚àÄ (i : ‚Ñ§), Acyclic F t‚ÇÅ t‚ÇÇ (S.X i))
    (ha : ‚àÄ (X : t‚ÇÅ.Heart) (j : ‚Ñ§), j ‚â§ a ‚Üí IsZero ((t‚ÇÇ.homology j).obj (F.obj X.1)))
    (hb : ‚àÄ (X : t‚ÇÅ.Heart) (j : ‚Ñ§), b ‚â§ j ‚Üí IsZero ((t‚ÇÇ.homology j).obj (F.obj X.1)))
    (k l : ‚Ñ§) (hkl : k + 1 = l) :
    Acyclic F t‚ÇÅ t‚ÇÇ (Limits.kernel (S.d k l)) := by
  simp only [Acyclic]
  refine isHeart_of_isZero_homology t‚ÇÇ _ ?_
  intro j hj
  rw [ne_iff_lt_or_gt] at hj
  cases hj with
  | inl h => exact leftAcyclic_ker_of_bounded_functor F t‚ÇÅ t‚ÇÇ S hkl h (fun _ _ ‚Ü¶ hexact _)
               (fun _ _ ‚Ü¶ hacy _) ha
  | inr h => exact rightAcyclic_ker_of_bounded_functor F t‚ÇÅ t‚ÇÇ S hkl h (fun _ _ ‚Ü¶ hexact _)
               (fun _ _ ‚Ü¶ hacy _) hb

/--
Let `S` be a cochain complex in `t‚ÇÅ.heart`. Suppose that `S` is exact, and that the entries
of `S` are acyclic in every degree and zero outside of a bounded interval.
Then `Abelian.image (S.d k (k + 1))` is acyclic for every `k`.
-/
lemma acyclic_image_of_bounded_exact_acyclic_complex (S : CochainComplex t‚ÇÅ.Heart ‚Ñ§) {a b: ‚Ñ§}
    (hexact : ‚àÄ (i : ‚Ñ§), S.ExactAt i)
    (hacy : ‚àÄ (i : ‚Ñ§), Acyclic F t‚ÇÅ t‚ÇÇ (S.X i))
    (ha : ‚àÄ (j : ‚Ñ§), j ‚â§ a ‚Üí IsZero (S.X j))
    (hb : ‚àÄ (j : ‚Ñ§), b ‚â§ j ‚Üí IsZero (S.X j)) (k l : ‚Ñ§) (hkl : k + 1 = l) :
    Acyclic F t‚ÇÅ t‚ÇÇ (Abelian.image (S.d k l)) := by
  refine IsClosedUnderIsomorphisms.of_iso ?_ (acyclic_ker_of_bounded_exact_acyclic_complex F t‚ÇÅ t‚ÇÇ
    S hexact hacy ha hb (k + 1) (l + 1) (by linarith))
  set e : S.sc l ‚âÖ S.sc' k l (l + 1) :=
    S.isoSc' k l (l + 1) (by simp only [hkl.symm, CochainComplex.prev, add_sub_cancel_right])
    (by simp only [CochainComplex.next])
  have := ShortComplex.imageToKernelIsIsoOfExact (IsZero.of_iso
    ((S.exactAt_iff_isZero_homology _).mp (hexact l)) (ShortComplex.homologyMapIso e).symm)
  exact ((asIso (S.sc' k l (l + 1)).abelianImageToKernel).trans (kernel.mapIso (S.d l (l + 1))
    (S.d (k + 1) (l + 1)) (S.XIsoOfEq hkl.symm) (Iso.refl _)
    (by simp only [Iso.refl_hom, comp_id, HomologicalComplex.XIsoOfEq_hom_comp_d]))).symm

/--
Suppose that `F` has finite cohomological degree (relative to `t‚ÇÅ` and `t‚ÇÇ`).
Let `S` be a cochain complex in `t‚ÇÅ.heart`. Suppose that `S` is exact and that the entries
of `S` are acyclic.
Then `Abelian.image (S.d k (k + 1))` is acyclic for every `k`.
-/
lemma acyclic_image_of_exact_acyclic_complex_and_bounded_functor (S : CochainComplex t‚ÇÅ.Heart ‚Ñ§)
    {a b : ‚Ñ§} (hexact : ‚àÄ (i : ‚Ñ§), S.ExactAt i)
    (hacy : ‚àÄ (i : ‚Ñ§), Acyclic F t‚ÇÅ t‚ÇÇ (S.X i))
    (ha : ‚àÄ (X : t‚ÇÅ.Heart) (j : ‚Ñ§), j ‚â§ a ‚Üí IsZero ((t‚ÇÇ.homology j).obj (F.obj X.1)))
    (hb : ‚àÄ (X : t‚ÇÅ.Heart) (j : ‚Ñ§), b ‚â§ j ‚Üí IsZero ((t‚ÇÇ.homology j).obj (F.obj X.1))) (k l : ‚Ñ§)
    (hkl : k + 1 = l) :
    Acyclic F t‚ÇÅ t‚ÇÇ (Abelian.image (S.d k l)) := by
  refine IsClosedUnderIsomorphisms.of_iso ?_
    (acyclic_ker_of_exact_acyclic_complex_and_bounded_functor F t‚ÇÅ t‚ÇÇ
    S hexact hacy ha hb (k + 1) (l + 1) (by linarith))
  set e : S.sc l ‚âÖ S.sc' k l (l + 1) :=
    S.isoSc' k l (l + 1) (by simp only [hkl.symm, CochainComplex.prev, add_sub_cancel_right])
    (by simp only [CochainComplex.next])
  have := ShortComplex.imageToKernelIsIsoOfExact (IsZero.of_iso
    ((S.exactAt_iff_isZero_homology _).mp (hexact l)) (ShortComplex.homologyMapIso e).symm)
  exact ((asIso (S.sc' k l (l + 1)).abelianImageToKernel).trans (kernel.mapIso (S.d l (l + 1))
    (S.d (k + 1) (l + 1)) (S.XIsoOfEq hkl.symm) (Iso.refl _)
    (by simp only [Iso.refl_hom, comp_id, HomologicalComplex.XIsoOfEq_hom_comp_d]))).symm

/--
Let `S` be a cochain complex in `t‚ÇÅ.heart`. Suppose that `S` is exact, and that the entries
of `S` are acyclic in every degree and zero outside of a bounded interval.
Then `Abelian.coimage (S.d k (k + 1))` is acyclic for every `k`.
-/
lemma acyclic_coimage_of_bounded_exact_acyclic_complex (S : CochainComplex t‚ÇÅ.Heart ‚Ñ§) {a b : ‚Ñ§}
    (hexact : ‚àÄ (i : ‚Ñ§), S.ExactAt i)
    (hacy : ‚àÄ (i : ‚Ñ§), Acyclic F t‚ÇÅ t‚ÇÇ (S.X i))
    (ha : ‚àÄ (j : ‚Ñ§), j ‚â§ a ‚Üí IsZero (S.X j))
    (hb : ‚àÄ (j : ‚Ñ§), b ‚â§ j ‚Üí IsZero (S.X j)) (k l : ‚Ñ§) (hkl : k + 1 = l) :
    Acyclic F t‚ÇÅ t‚ÇÇ (Abelian.coimage (S.d k l)) :=
  IsClosedUnderIsomorphisms.of_iso (asIso (Abelian.coimageImageComparison (S.d k l))).symm
  (acyclic_image_of_bounded_exact_acyclic_complex F t‚ÇÅ t‚ÇÇ S hexact hacy ha hb k l hkl)

/--
Suppose that `F` has finite cohomological degree (relative to `t‚ÇÅ` and `t‚ÇÇ`).
Let `S` be a cochain complex in `t‚ÇÅ.heart`. Suppose that `S` is exact and that the entries
of `S` are acyclic.
Then `Abelian.coimage (S.d k (k + 1))` is acyclic for every `k`.
-/
lemma acyclic_coimage_of_exact_acyclic_complex_and_bounded_functor (S : CochainComplex t‚ÇÅ.Heart ‚Ñ§)
    {a b : ‚Ñ§} (hexact : ‚àÄ (i : ‚Ñ§), S.ExactAt i)
    (hacy : ‚àÄ (i : ‚Ñ§), Acyclic F t‚ÇÅ t‚ÇÇ (S.X i))
    (ha : ‚àÄ (X : t‚ÇÅ.Heart) (j : ‚Ñ§), j ‚â§ a ‚Üí IsZero ((t‚ÇÇ.homology j).obj (F.obj X.1)))
    (hb : ‚àÄ (X : t‚ÇÅ.Heart) (j : ‚Ñ§), b ‚â§ j ‚Üí IsZero ((t‚ÇÇ.homology j).obj (F.obj X.1))) (k l : ‚Ñ§)
    (hkl : k + 1 = l) :
    Acyclic F t‚ÇÅ t‚ÇÇ (Abelian.coimage (S.d k l)) :=
  IsClosedUnderIsomorphisms.of_iso (asIso (Abelian.coimageImageComparison (S.d k l))).symm
  (acyclic_image_of_exact_acyclic_complex_and_bounded_functor F t‚ÇÅ t‚ÇÇ S hexact hacy ha hb k l hkl)

/--
Let `S` be a cochain complex in `t‚ÇÅ.heart`. Suppose that `S` is exact, and that the entries
of `S` are acyclic in every degree and zero outside of a bounded interval.
Then `cokernel (S.d k (k + 1))` is acyclic for every `k`.
-/
lemma acyclic_coker_of_bounded_exact_acyclic_complex (S : CochainComplex t‚ÇÅ.Heart ‚Ñ§) {a b : ‚Ñ§}
    (hexact : ‚àÄ (i : ‚Ñ§), S.ExactAt i)
    (hacy : ‚àÄ (i : ‚Ñ§), Acyclic F t‚ÇÅ t‚ÇÇ (S.X i))
    (ha : ‚àÄ (j : ‚Ñ§), j ‚â§ a ‚Üí IsZero (S.X j))
    (hb : ‚àÄ (j : ‚Ñ§), b ‚â§ j ‚Üí IsZero (S.X j)) (k l : ‚Ñ§) (hkl : k + 1 = l) :
    Acyclic F t‚ÇÅ t‚ÇÇ (Limits.cokernel (S.d k l)) := by
  refine IsClosedUnderIsomorphisms.of_iso ?_ (acyclic_coimage_of_bounded_exact_acyclic_complex
    F t‚ÇÅ t‚ÇÇ S hexact hacy ha hb (k + 1) (l + 1) (by linarith))
  set e : S.sc (k + 1) ‚âÖ S.sc' k (k + 1) (l+ 1) :=
    S.isoSc' k (k + 1) (l + 1) (by simp only [CochainComplex.prev, add_sub_cancel_right])
    (by simp only [CochainComplex.next, hkl.symm])
  have := ShortComplex.cokernelToAbelianCoimageIsIsoOfExact (IsZero.of_iso
    ((S.exactAt_iff_isZero_homology _).mp (hexact (k + 1))) (ShortComplex.homologyMapIso e).symm)
  exact (asIso (S.sc' k (k + 1) (l + 1)).cokernelToAbelianCoimage).symm.trans
    (cokernel.mapIso (S.d k (k + 1)) (S.d k l) (Iso.refl _) (S.XIsoOfEq hkl)
    (by simp only [HomologicalComplex.d_comp_XIsoOfEq_hom, Iso.refl_hom, id_comp]))

/--
Suppose that `F` has finite cohomological degree (relative to `t‚ÇÅ` and `t‚ÇÇ`).
Let `S` be a cochain complex in `t‚ÇÅ.heart`. Suppose that `S` is exact and that the entries
of `S` are acyclic.
Then `cokernel (S.d k (k + 1))` is acyclic for every `k`.
-/
lemma acyclic_coker_of_exact_acyclic_complex_and_bounded_functor (S : CochainComplex t‚ÇÅ.Heart ‚Ñ§)
    {a b: ‚Ñ§} (hexact : ‚àÄ (i : ‚Ñ§), S.ExactAt i)
    (hacy : ‚àÄ (i : ‚Ñ§), Acyclic F t‚ÇÅ t‚ÇÇ (S.X i))
    (ha : ‚àÄ (X : t‚ÇÅ.Heart) (j : ‚Ñ§), j ‚â§ a ‚Üí IsZero ((t‚ÇÇ.homology j).obj (F.obj X.1)))
    (hb : ‚àÄ (X : t‚ÇÅ.Heart) (j : ‚Ñ§), b ‚â§ j ‚Üí IsZero ((t‚ÇÇ.homology j).obj (F.obj X.1))) (k l : ‚Ñ§)
    (hkl : k + 1 = l) :
    Acyclic F t‚ÇÅ t‚ÇÇ (Limits.cokernel (S.d k l)) := by
  refine IsClosedUnderIsomorphisms.of_iso ?_
    (acyclic_coimage_of_exact_acyclic_complex_and_bounded_functor F t‚ÇÅ t‚ÇÇ
    S hexact hacy ha hb (k + 1) (l + 1) (by linarith))
  set e : S.sc (k + 1) ‚âÖ S.sc' k (k + 1) (l+ 1) :=
    S.isoSc' k (k + 1) (l + 1) (by simp only [CochainComplex.prev, add_sub_cancel_right])
    (by simp only [CochainComplex.next, hkl.symm])
  have := ShortComplex.cokernelToAbelianCoimageIsIsoOfExact (IsZero.of_iso
    ((S.exactAt_iff_isZero_homology _).mp (hexact (k + 1))) (ShortComplex.homologyMapIso e).symm)
  exact (asIso (S.sc' k (k + 1) (l + 1)).cokernelToAbelianCoimage).symm.trans
    (cokernel.mapIso (S.d k (k + 1)) (S.d k l) (Iso.refl _) (S.XIsoOfEq hkl)
    (by simp only [HomologicalComplex.d_comp_XIsoOfEq_hom, Iso.refl_hom, id_comp]))

/--
Let `S` be a short complex in `t‚ÇÅ.heart`.  If `S` is exact, `S.f` has an acyclic kernel,
and `S.g` has an acyclic kernel, an acyclic cokernel and an acyclic image, then
`F.fromToHeartHeart` preserves the left homology data of `S`.
-/
noncomputable def preserves_leftHomologyData_of_acyclic (S : ShortComplex t‚ÇÅ.Heart)
    (he : S.Exact) (h‚ÇÄ : Acyclic F t‚ÇÅ t‚ÇÇ (kernel S.f))
    (h‚ÇÅ : Acyclic F t‚ÇÅ t‚ÇÇ (cokernel S.g)) (h‚ÇÇ : Acyclic F t‚ÇÅ t‚ÇÇ (kernel S.g))
    (h‚ÇÉ : Acyclic F t‚ÇÅ t‚ÇÇ (Abelian.image S.g)) :
    (ShortComplex.LeftHomologyData.ofAbelian S).IsPreservedBy (F.fromHeartToHeart t‚ÇÅ t‚ÇÇ) where
  g := by
    have := isIso_kernelComparison_of_acyclic_homology F t‚ÇÅ t‚ÇÇ S.g h‚ÇÅ h‚ÇÇ h‚ÇÉ
    exact PreservesKernel.of_iso_comparison _ _
  f' := by
    have := isIso_cokernelComparison_of_acyclic_homology F t‚ÇÅ t‚ÇÇ
      (ShortComplex.LeftHomologyData.ofAbelian S).f' ?_ ?_ ?_
    ¬∑ exact PreservesCokernel.of_iso_comparison _ _
    ¬∑ exact IsClosedUnderIsomorphisms.of_iso S.homologyIsoCokernelLift (AcyclicCategory.zero F t‚ÇÅ t‚ÇÇ
        (S.exact_iff_isZero_homology.mp he))
    ¬∑ exact IsClosedUnderIsomorphisms.of_iso (S.LeftHomologyData_ker_f' _).symm h‚ÇÄ
    ¬∑ refine IsClosedUnderIsomorphisms.of_iso (S.LeftHomologyData_image_f' _).symm ?_
      rw [S.exact_iff_isIso_abelianImageToKernel] at he
      exact IsClosedUnderIsomorphisms.of_iso (asIso S.abelianImageToKernel).symm h‚ÇÇ

/--
Let `S` be a short complex in `t‚ÇÅ.heart`.  If `S` is exact, `S.f` has an acyclic kernel,
and `S.g` has an acyclic kernel, an acyclic cokernel and an acyclic image, then
`F.fromToHeartHeart` preserves the left homology of `S`.
-/
noncomputable def preservesLeftHomology_of_acyclic (S : ShortComplex t‚ÇÅ.Heart)
    (he : S.Exact) (h‚ÇÄ : Acyclic F t‚ÇÅ t‚ÇÇ (kernel S.f))
    (h‚ÇÅ : Acyclic F t‚ÇÅ t‚ÇÇ (cokernel S.g)) (h‚ÇÇ : Acyclic F t‚ÇÅ t‚ÇÇ (kernel S.g))
    (h‚ÇÉ : Acyclic F t‚ÇÅ t‚ÇÇ (Abelian.image S.g)) :
    PreservesLeftHomologyOf (F.fromHeartToHeart t‚ÇÅ t‚ÇÇ) S := by
  have := preserves_leftHomologyData_of_acyclic F t‚ÇÅ t‚ÇÇ S he h‚ÇÄ h‚ÇÅ h‚ÇÇ h‚ÇÉ
  refine Functor.PreservesLeftHomologyOf.mk' (F.fromHeartToHeart t‚ÇÅ t‚ÇÇ)
    (ShortComplex.LeftHomologyData.ofAbelian S)

namespace ShortComplex

omit [t‚ÇÇ.NonDegenerate] in
/--
Let `S` be a short complex in `t‚ÇÅ.heart`.  If `S` is exact, `S.f` has an acyclic kernel,
and `S.g` has an acyclic kernel, an acyclic cokernel and an acyclic image, then
the image of `S` by `F.fromToHeartHeart` is exact.
-/
lemma exact_map_of_acyclic (S : ShortComplex t‚ÇÅ.Heart)
    (he : S.Exact) (h‚ÇÄ : Acyclic F t‚ÇÅ t‚ÇÇ (kernel S.f))
    (h‚ÇÅ : Acyclic F t‚ÇÅ t‚ÇÇ (cokernel S.g)) (h‚ÇÇ : Acyclic F t‚ÇÅ t‚ÇÇ (kernel S.g))
    (h‚ÇÉ : Acyclic F t‚ÇÅ t‚ÇÇ (Abelian.image S.g)) :
    (S.map (F.fromHeartToHeart t‚ÇÅ t‚ÇÇ)).Exact := by
  have := preservesLeftHomology_of_acyclic F t‚ÇÅ t‚ÇÇ S he h‚ÇÄ h‚ÇÅ h‚ÇÇ h‚ÇÉ
  exact he.map_of_preservesLeftHomologyOf _

end ShortComplex

namespace CochainComplex

/--
Let `S` be a cochain complex in `t‚ÇÅ.heart`. Suppose that `S` is exact and that the entries
of `S` are acyclic and zero outside of a bounded interval.
Then the image of `S` by `F.fromHeartToHeart` is exact.
-/
lemma exact_map_of_exact_bounded_acyclic_complex (S : CochainComplex t‚ÇÅ.Heart ‚Ñ§) {a b : ‚Ñ§}
    (hexact : ‚àÄ (i : ‚Ñ§), S.ExactAt i)
    (hacy : ‚àÄ (i : ‚Ñ§), Acyclic F t‚ÇÅ t‚ÇÇ (S.X i))
    (ha : ‚àÄ (j : ‚Ñ§), j ‚â§ a ‚Üí IsZero (S.X j))
    (hb : ‚àÄ (j : ‚Ñ§), b ‚â§ j ‚Üí IsZero (S.X j)) (i : ‚Ñ§):
    (((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).mapHomologicalComplex ((ComplexShape.up ‚Ñ§))).obj S).ExactAt i := by
  rw [HomologicalComplex.exactAt_iff]
  refine (S.sc i).exact_map_of_acyclic F t‚ÇÅ t‚ÇÇ ?_ ?_ ?_ ?_ ?_
  ¬∑ rw [‚Üê HomologicalComplex.exactAt_iff]; exact hexact i
  ¬∑ simp only [HomologicalComplex.shortComplexFunctor_obj_X‚ÇÅ,
    HomologicalComplex.shortComplexFunctor_obj_X‚ÇÇ, HomologicalComplex.shortComplexFunctor_obj_f]
    exact acyclic_ker_of_bounded_exact_acyclic_complex F t‚ÇÅ t‚ÇÇ S hexact hacy ha hb _ i
      (by simp only [CochainComplex.prev, sub_add_cancel])
  ¬∑ exact acyclic_coker_of_bounded_exact_acyclic_complex F t‚ÇÅ t‚ÇÇ S hexact hacy ha hb i _
      (by simp only [CochainComplex.next])
  ¬∑ exact acyclic_ker_of_bounded_exact_acyclic_complex F t‚ÇÅ t‚ÇÇ S hexact hacy ha hb i _
      (by simp only [CochainComplex.next])
  ¬∑ exact acyclic_image_of_bounded_exact_acyclic_complex F t‚ÇÅ t‚ÇÇ S hexact hacy ha hb i _
      (by simp only [CochainComplex.next])

/--
Suppose that `F` has finite cohomological degree (relative to `t‚ÇÅ` and `t‚ÇÇ`).
Let `S` be a cochain complex in `t‚ÇÅ.heart`. Suppose that `S` is exact and that the entries
of `S` are acyclic.
Then the image of `S` by `F.fromHeartToHeart` is exact.
-/
lemma exact_map_of_exact_acyclic_complex_and_bounded_functor (S : CochainComplex t‚ÇÅ.Heart ‚Ñ§)
    {a b: ‚Ñ§} (hexact : ‚àÄ (i : ‚Ñ§), S.ExactAt i)
    (hacy : ‚àÄ (i : ‚Ñ§), Acyclic F t‚ÇÅ t‚ÇÇ (S.X i))
    (ha : ‚àÄ (X : t‚ÇÅ.Heart) (j : ‚Ñ§), j ‚â§ a ‚Üí IsZero ((t‚ÇÇ.homology j).obj (F.obj X.1)))
    (hb : ‚àÄ (X : t‚ÇÅ.Heart) (j : ‚Ñ§), b ‚â§ j ‚Üí IsZero ((t‚ÇÇ.homology j).obj (F.obj X.1))) (i : ‚Ñ§) :
    (((F.fromHeartToHeart t‚ÇÅ t‚ÇÇ).mapHomologicalComplex ((ComplexShape.up ‚Ñ§))).obj S).ExactAt i := by
  rw [HomologicalComplex.exactAt_iff]
  refine (S.sc i).exact_map_of_acyclic F t‚ÇÅ t‚ÇÇ ?_ ?_ ?_ ?_ ?_
  ¬∑ rw [‚Üê HomologicalComplex.exactAt_iff]; exact hexact i
  ¬∑ simp only [HomologicalComplex.shortComplexFunctor_obj_X‚ÇÅ,
    HomologicalComplex.shortComplexFunctor_obj_X‚ÇÇ, HomologicalComplex.shortComplexFunctor_obj_f]
    exact acyclic_ker_of_exact_acyclic_complex_and_bounded_functor F t‚ÇÅ t‚ÇÇ S hexact hacy ha hb _ i
      (by simp only [CochainComplex.prev, sub_add_cancel])
  ¬∑ exact acyclic_coker_of_exact_acyclic_complex_and_bounded_functor F t‚ÇÅ t‚ÇÇ S hexact hacy ha hb i _
      (by simp only [CochainComplex.next])
  ¬∑ exact acyclic_ker_of_exact_acyclic_complex_and_bounded_functor F t‚ÇÅ t‚ÇÇ S hexact hacy ha hb i _
      (by simp only [CochainComplex.next])
  ¬∑ exact acyclic_image_of_exact_acyclic_complex_and_bounded_functor F t‚ÇÅ t‚ÇÇ S hexact hacy ha hb i _
      (by simp only [CochainComplex.next])

end CochainComplex

end Triangulated

end CategoryTheory
