/-
Copyright (c) 2025 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
module

public import Mathlib.CategoryTheory.Triangulated.TStructure.Basic

/-!
# Truncations for a t-structure

Let `t` be a t-structure on a (pre)triangulated category `C`.
In this file, for any `n : â„¤`, we construct truncation functors `t.truncLT n : C â¥¤ C`,
`t.truncGE n : C â¥¤ C` and natural transformations `t.truncLTÎ¹ n : t.truncLT n âŸ¶ ğŸ­ C`,
`t.truncGEÏ€ n : ğŸ­ C âŸ¶ t.truncGE n` and
`t.truncGEÎ´LT n : t.truncGE n âŸ¶ t.truncLT n â‹™ shiftFunctor C (1 : â„¤)` which are
part of a distinguished triangle
`(t.truncLT n).obj X âŸ¶ X âŸ¶ (t.truncGE n).obj X âŸ¶ ((t.truncLT n).obj X)âŸ¦1âŸ§` for any `X : C`,
with `(t.truncLT n).obj X < n` and `(t.truncGE n).obj X â‰¥ n`.

We obtain various properties of these truncation functors.
Variants `truncGT` and `truncLE` are introduced in the file
`Mathlib/CategoryTheory/Triangulated/TStucture/TruncLEGT.lean`.
Extensions to indices in `EInt` instead of `â„¤` are introduced in the file
`Mathlib/CategoryTheory/Triangulated/TStucture/ETrunc.lean`.
The spectral object attached to an object `X : C` is constructed in the file
`Mathlib/CategoryTheory/Triangulated/TStucture/SpectralObject.lean`.

-/

universe v u

namespace CategoryTheory

open Limits Pretriangulated ZeroObject

variable {C : Type u} [Category.{v} C] [Preadditive C] [HasZeroObject C] [HasShift C â„¤]
  [âˆ€ (n : â„¤), (shiftFunctor C n).Additive] [Pretriangulated C]

namespace Triangulated

namespace TStructure

variable (t : TStructure C)

/-- Two morphisms `T âŸ¶ T'` between distinguished triangles must coincide when
they coincide on the middle object, and there are integers `a â‰¤ b` such that
for a t-structure, we have `T.objâ‚ â‰¤ a` and `T'.objâ‚ƒ â‰¥ b`. -/
public lemma triangle_map_ext {T T' : Triangle C} {fâ‚ fâ‚‚ : T âŸ¶ T'}
    (hT : T âˆˆ distTriang C) (hT' : T' âˆˆ distTriang C) (a b : â„¤)
    (hâ‚€ : t.IsLE T.objâ‚ a) (hâ‚ : t.IsGE T'.objâ‚ƒ b)
    (H : fâ‚.homâ‚‚ = fâ‚‚.homâ‚‚ := by cat_disch)
    (hab : a â‰¤ b := by lia) : fâ‚ = fâ‚‚ := by
  suffices âˆ€ (f : T âŸ¶ T'), f.homâ‚‚ = 0 â†’ f = 0 by rw [â† sub_eq_zero]; cat_disch
  intro f hf
  ext
  Â· obtain âŸ¨g, hgâŸ© := Triangle.coyoneda_exactâ‚‚ _ (inv_rot_of_distTriang _ hT')
      f.homâ‚ (by simp [â† f.commâ‚, hf])
    simp [hg, t.zero_of_isLE_of_isGE g a (b + 1) (by lia)
      hâ‚€ (t.isGE_shift _ b (-1) (b + 1))]
  Â· simp [hf]
  Â· obtain âŸ¨g, hgâŸ© := T.yoneda_exactâ‚ƒ hT f.homâ‚ƒ (by cat_disch)
    simp [hg, t.zero_of_isLE_of_isGE g (a - 1) b (by lia)
      (t.isLE_shift _ a 1 (a - 1)) inferInstance]

/-- If `a < b`, then a morphism `T.objâ‚‚ âŸ¶ T'.objâ‚‚` extends to a morphism `T âŸ¶ T'`
of distinguished triangles when for a t-structure `T.objâ‚ â‰¤ a` and `T'.objâ‚ƒ â‰¥ b`. -/
public lemma triangle_map_exists {T T' : Triangle C}
    (hT : T âˆˆ distTriang C) (hT' : T' âˆˆ distTriang C)
    (Ï† : T.objâ‚‚ âŸ¶ T'.objâ‚‚) (a b : â„¤)
    (hâ‚€ : t.IsLE T.objâ‚ a) (hâ‚' : t.IsGE T'.objâ‚ƒ b) (h : a < b := by lia) :
    âˆƒ (f : T âŸ¶ T'), f.homâ‚‚ = Ï† := by
  obtain âŸ¨a, commâ‚âŸ© := T'.coyoneda_exactâ‚‚ hT' (T.morâ‚ â‰« Ï†) (t.zero _ a b)
  obtain âŸ¨c, commâ‚‚, commâ‚ƒâŸ© := complete_distinguished_triangle_morphism _ _ hT hT' a Ï† commâ‚
  exact âŸ¨{ homâ‚ := a, homâ‚‚ := Ï†, homâ‚ƒ := c }, rflâŸ©

/-- If `a < b`, then an isomorphism `T.objâ‚‚ â‰… T'.objâ‚‚` extends to an isomorphism `T â‰… T'`
of distinguished triangles when for a t-structure, both `T.objâ‚` and `T'.objâ‚` are `â‰¤ a` and
both `T.objâ‚ƒ` and `T'.objâ‚ƒ` are `â‰¥ b`. -/
public lemma triangle_iso_exists {T T' : Triangle C}
    (hT : T âˆˆ distTriang C) (hT' : T' âˆˆ distTriang C) (e : T.objâ‚‚ â‰… T'.objâ‚‚)
    (a b : â„¤) (hâ‚€ : t.IsLE T.objâ‚ a) (hâ‚ : t.IsGE T.objâ‚ƒ b)
    (hâ‚€' : t.IsLE T'.objâ‚ a) (hâ‚' : t.IsGE T'.objâ‚ƒ b) (h : a < b := by lia) :
    âˆƒ (e' : T â‰… T'), e'.hom.homâ‚‚ = e.hom := by
  obtain âŸ¨hom, hhomâŸ© := triangle_map_exists t hT hT' e.hom _ _ hâ‚€ hâ‚'
  obtain âŸ¨inv, _âŸ© := triangle_map_exists t hT' hT e.inv _ _ hâ‚€' hâ‚
  exact
    âŸ¨{hom := hom
      inv := inv
      hom_inv_id := triangle_map_ext t hT hT a b hâ‚€ hâ‚
      inv_hom_id := triangle_map_ext t hT' hT' a b hâ‚€' hâ‚' }, hhomâŸ©

namespace TruncAux
/-! The private definitions in the namespace `TStructure.TruncAux` are part of the
implementation of the truncation functors `truncLT`, `truncGE` and the
distinguished triangles they fit in. -/

variable (n : â„¤) (X : C)

/-- Given a t-structure `t` on `C`, `X : C` and `n : â„¤`, this is a distinguished
triangle `objâ‚ âŸ¶ X âŸ¶ objâ‚ƒ âŸ¶ objâ‚âŸ¦1âŸ§` where `objâ‚` is `< n` and `objâ‚ƒ` is `â‰¥ n`.
(This should not be used directly: use `truncLT` and `truncGE` instead.) -/
@[simps! objâ‚‚]
noncomputable def triangle : Triangle C :=
  Triangle.mk
    (t.exists_triangle X (n - 1) n
      (by lia)).choose_spec.choose_spec.choose_spec.choose_spec.choose
    (t.exists_triangle X (n - 1) n
      (by lia)).choose_spec.choose_spec.choose_spec.choose_spec.choose_spec.choose
    (t.exists_triangle X (n - 1) n
      (by lia)).choose_spec.choose_spec.choose_spec.choose_spec.choose_spec.choose_spec.choose

lemma triangle_distinguished :
    triangle t n X âˆˆ distTriang C :=
  (t.exists_triangle X (n - 1) n
    (by lia)).choose_spec.choose_spec.choose_spec.choose_spec.choose_spec.choose_spec.choose_spec

instance triangle_objâ‚_isLE (n : â„¤) :
    t.IsLE (triangle t n X).objâ‚ (n - 1) :=
  âŸ¨(t.exists_triangle X (n - 1) n (by lia)).choose_spec.choose_spec.chooseâŸ©

instance triangle_objâ‚ƒ_isGE :
    t.IsGE (triangle t n X).objâ‚ƒ n :=
  âŸ¨(t.exists_triangle X (n - 1) n (by lia)).choose_spec.choose_spec.choose_spec.chooseâŸ©

variable {X} {Y : C} (Ï† : X âŸ¶ Y)

/-- Version of `TStructure.triangle_map_ext` that is specialized for the auxiliary
definition `TruncAux.triangle`. -/
@[ext]
lemma triangle_map_ext' (fâ‚ fâ‚‚ : triangle t n X âŸ¶ triangle t n Y)
    (H : fâ‚.homâ‚‚ = fâ‚‚.homâ‚‚ := by cat_disch) : fâ‚ = fâ‚‚ :=
  triangle_map_ext t (triangle_distinguished t n X) (triangle_distinguished t n Y) (n - 1) n
    inferInstance inferInstance H (by lia)

/-- Auxiliary definition for `triangleFunctor`. -/
@[simps homâ‚‚]
noncomputable def triangleMap : triangle t n X âŸ¶ triangle t n Y :=
  have H := triangle_map_exists t (triangle_distinguished t n X)
    (triangle_distinguished t n Y) Ï† (n - 1) n inferInstance inferInstance (by lia)
  { homâ‚ := H.choose.homâ‚
    homâ‚‚ := Ï†
    homâ‚ƒ := H.choose.homâ‚ƒ
    commâ‚ := by rw [â† H.choose.commâ‚, H.choose_spec]
    commâ‚‚ := by rw [H.choose.commâ‚‚, H.choose_spec]
    commâ‚ƒ := H.choose.commâ‚ƒ }

/-- Given a t-structure `t` on `C` and `n : â„¤`, this is the
functorial (distinguished) triangle `objâ‚ âŸ¶ X âŸ¶ objâ‚ƒ âŸ¶ objâ‚âŸ¦1âŸ§` for any `X : C`,
where `objâ‚` is `< n` and `objâ‚ƒ` is `â‰¥ n`.
(This should not be used directly: use `triangleLTGE` instead.) -/
@[simps]
noncomputable def triangleFunctor : C â¥¤ Triangle C where
  obj := triangle t n
  map Ï† := triangleMap t n Ï†

variable (A)

lemma triangleFunctor_obj_distinguished :
    (triangleFunctor t n).obj A âˆˆ distTriang C :=
  triangle_distinguished t n A

instance isLE_triangleFunctor_obj_objâ‚ :
    t.IsLE ((triangleFunctor t n).obj A).objâ‚ (n - 1) := by
  dsimp [triangleFunctor]
  infer_instance

instance isGE_triangleFunctor_obj_objâ‚ƒ :
    t.IsGE ((triangleFunctor t n).obj A).objâ‚ƒ n := by
  dsimp [triangleFunctor]
  infer_instance

noncomputable def triangleMapOfLE (a b : â„¤) (h : a â‰¤ b) : triangle t a A âŸ¶ triangle t b A :=
  have H := triangle_map_exists t (triangle_distinguished t a A)
    (triangle_distinguished t b A) (ğŸ™ _) (a-1) b inferInstance inferInstance
  { homâ‚ := H.choose.homâ‚
    homâ‚‚ := ğŸ™ _
    homâ‚ƒ := H.choose.homâ‚ƒ
    commâ‚ := by rw [â† H.choose.commâ‚, H.choose_spec]
    commâ‚‚ := by rw [H.choose.commâ‚‚, H.choose_spec]
    commâ‚ƒ := H.choose.commâ‚ƒ }

noncomputable def triangleFunctorNatTransOfLE (a b : â„¤) (h : a â‰¤ b) :
    triangleFunctor t a âŸ¶ triangleFunctor t b where
  app X := triangleMapOfLE t X a b h
  naturality {Xâ‚ Xâ‚‚} Ï† :=
    triangle_map_ext t (triangleFunctor_obj_distinguished _ _ _)
      (triangleFunctor_obj_distinguished _ _ _) (a - 1) b inferInstance inferInstance
        (by simp [triangleMapOfLE])

@[simp]
lemma triangleFunctorNatTransOfLE_app_homâ‚‚ (a b : â„¤) (h : a â‰¤ b) (X : C) :
    ((triangleFunctorNatTransOfLE t a b h).app X).homâ‚‚ = ğŸ™ X := rfl

lemma triangleFunctorNatTransOfLE_trans (a b c : â„¤) (hab : a â‰¤ b) (hbc : b â‰¤ c) :
    triangleFunctorNatTransOfLE t a b hab â‰« triangleFunctorNatTransOfLE t b c hbc =
      triangleFunctorNatTransOfLE t a c (hab.trans hbc) := by
  apply NatTrans.ext
  ext1 X
  exact triangle_map_ext t (triangleFunctor_obj_distinguished _ _ _)
    (triangleFunctor_obj_distinguished _ _ _) (a - 1) c inferInstance inferInstance (by simp)

lemma triangleFunctorNatTransOfLE_refl (a : â„¤) :
    triangleFunctorNatTransOfLE t a a (by rfl) = ğŸ™ _ := by
  apply NatTrans.ext
  ext1 X
  exact triangle_map_ext t (triangleFunctor_obj_distinguished _ _ _)
    (triangleFunctor_obj_distinguished _ _ _) (a - 1) a inferInstance inferInstance (by simp)

instance : (triangleFunctor t n).Additive where

end TruncAux

public section

/-- Given a t-structure `t` on a pretriangulated category `C` and `n : â„¤`, this
is the `< n`-truncation functor. See also the natural transformation `truncLTÎ¹`. -/
noncomputable def truncLT (n : â„¤) : C â¥¤ C :=
  TruncAux.triangleFunctor t n â‹™ Triangle.Ï€â‚

instance (n : â„¤) : (t.truncLT n).Additive where
  map_add {_ _ _ _} := by
    dsimp only [truncLT, Functor.comp_map]
    rw [Functor.map_add]
    dsimp

/-- The natural transformation `t.truncLT n âŸ¶ ğŸ­ C` when `t` is a t-structure
on a category `C` and `n : â„¤`. -/
noncomputable def truncLTÎ¹ (n : â„¤) : t.truncLT n âŸ¶ ğŸ­ _ :=
  Functor.whiskerLeft (TruncAux.triangleFunctor t n) Triangle.Ï€â‚ToÏ€â‚‚

/-- Given a t-structure `t` on a pretriangulated category `C` and `n : â„¤`, this
is the `â‰¥ n`-truncation functor. See also the natural transformation `truncGEÏ€`. -/
noncomputable def truncGE (n : â„¤) : C â¥¤ C :=
  TruncAux.triangleFunctor t n â‹™ Triangle.Ï€â‚ƒ

instance (n : â„¤) : (t.truncGE n).Additive where
  map_add {_ _ _ _} := by
    dsimp only [truncGE, Functor.comp_map]
    rw [Functor.map_add]
    dsimp

/-- The natural transformation `ğŸ­ C âŸ¶ t.truncGE n` when `t` is a t-structure
on a category `C` and `n : â„¤`. -/
noncomputable def truncGEÏ€ (n : â„¤) : ğŸ­ _ âŸ¶ t.truncGE n :=
  Functor.whiskerLeft (TruncAux.triangleFunctor t n) Triangle.Ï€â‚‚ToÏ€â‚ƒ

@[reassoc (attr := simp)]
lemma truncGEÏ€_naturality (n : â„¤) {X Y : C} (f : X âŸ¶ Y) :
    (t.truncGEÏ€ n).app X â‰« (t.truncGE n).map f = f â‰« (t.truncGEÏ€ n).app Y :=
  ((t.truncGEÏ€ n).naturality f).symm

lemma isLE_truncLT_obj (X : C) (a b : â„¤) (hn : a â‰¤ b + 1 := by lia) :
    t.IsLE ((t.truncLT a).obj X) b := by
  have : t.IsLE ((t.truncLT a).obj X) (a - 1) := by dsimp [truncLT]; infer_instance
  exact t.isLE_of_LE _ (a - 1) _ (by lia)

instance (X : C) (n : â„¤) : t.IsLE ((t.truncLT n).obj X) (n - 1) :=
  t.isLE_truncLT_obj ..

instance (X : C) (n : â„¤) : t.IsLE ((t.truncLT (n + 1)).obj X) n :=
  t.isLE_truncLT_obj ..

lemma isGE_truncGE_obj (X : C) (a b : â„¤) (hn : b â‰¤ a := by lia) :
    t.IsGE ((t.truncGE a).obj X) b := by
  have : t.IsGE ((t.truncGE a).obj X) a := by dsimp [truncGE]; infer_instance
  exact t.isGE_of_GE _ _ a (by lia)

instance (X : C) (n : â„¤) : t.IsGE ((t.truncGE n).obj X) n :=
  t.isGE_truncGE_obj ..

/-- The connecting morphism `t.truncGE n âŸ¶ t.truncLT n â‹™ shiftFunctor C (1 : â„¤)`
when `t` is a t-structure on a pretriangulated category and `n : â„¤`. -/
noncomputable def truncGEÎ´LT (n : â„¤) :
    t.truncGE n âŸ¶ t.truncLT n â‹™ shiftFunctor C (1 : â„¤) :=
  Functor.whiskerLeft (TruncAux.triangleFunctor t n) Triangle.Ï€â‚ƒToÏ€â‚

/-- The distinguished triangle `(t.truncLT n).obj A âŸ¶ A âŸ¶ (t.truncGE n).obj A âŸ¶ ...`
as a functor `C â¥¤ Triangle C` when `t` is a `t`-structure on a pretriangulated
category `C` and `n : â„¤`. -/
@[expose, simps!]
noncomputable def triangleLTGE (n : â„¤) : C â¥¤ Triangle C :=
  Triangle.functorMk (t.truncLTÎ¹ n) (t.truncGEÏ€ n) (t.truncGEÎ´LT n)

lemma triangleLTGE_distinguished (n : â„¤) (X : C) :
    (t.triangleLTGE n).obj X âˆˆ distTriang C :=
  TruncAux.triangleFunctor_obj_distinguished t n X

instance (X : C) (n : â„¤) : t.IsLE ((t.triangleLTGE n).obj X).objâ‚ (n - 1) := by
  dsimp
  infer_instance

instance (X : C) (n : â„¤) : t.IsGE ((t.triangleLTGE n).obj X).objâ‚ƒ n := by
  dsimp
  infer_instance

@[reassoc (attr := simp)]
lemma truncLTÎ¹_comp_truncGEÏ€_app (n : â„¤) (X : C) :
    (t.truncLTÎ¹ n).app X â‰« (t.truncGEÏ€ n).app X = 0 :=
  comp_distTriang_mor_zeroâ‚â‚‚ _ (t.triangleLTGE_distinguished n X)

@[reassoc (attr := simp)]
lemma truncGEÏ€_comp_truncGEÎ´LT_app (n : â„¤) (X : C) :
    (t.truncGEÏ€ n).app X â‰« (t.truncGEÎ´LT n).app X = 0 :=
  comp_distTriang_mor_zeroâ‚‚â‚ƒ _ (t.triangleLTGE_distinguished n X)

@[reassoc (attr := simp)]
lemma truncGEÎ´LT_comp_truncLTÎ¹_app (n : â„¤) (X : C) :
    (t.truncGEÎ´LT n).app X â‰« ((t.truncLTÎ¹ n).app X)âŸ¦(1 : â„¤)âŸ§' = 0 :=
  comp_distTriang_mor_zeroâ‚ƒâ‚ _ (t.triangleLTGE_distinguished n X)

@[reassoc (attr := simp)]
lemma truncLTÎ¹_comp_truncGEÏ€ (n : â„¤) :
    t.truncLTÎ¹ n â‰« t.truncGEÏ€ n = 0 := by cat_disch

@[reassoc (attr := simp)]
lemma truncGEÏ€_comp_truncGEÎ´LT (n : â„¤) :
    t.truncGEÏ€ n â‰« t.truncGEÎ´LT n = 0 := by cat_disch

@[reassoc (attr := simp)]
lemma truncGEÎ´LT_comp_truncLTÎ¹ (n : â„¤) :
    t.truncGEÎ´LT n â‰« Functor.whiskerRight (t.truncLTÎ¹ n) (shiftFunctor C (1 : â„¤)) = 0 := by
  cat_disch

/-- The natural transformation `t.truncLT a âŸ¶ t.truncLT b` when `a â‰¤ b`. -/
noncomputable def natTransTruncLTOfLE (a b : â„¤) (h : a â‰¤ b) :
    t.truncLT a âŸ¶ t.truncLT b :=
  Functor.whiskerRight (TruncAux.triangleFunctorNatTransOfLE t a b h) Triangle.Ï€â‚

/-- The natural transformation `t.truncGE a âŸ¶ t.truncGE b` when `a â‰¤ b`. -/
noncomputable def natTransTruncGEOfLE (a b : â„¤) (h : a â‰¤ b) :
    t.truncGE a âŸ¶ t.truncGE b :=
  Functor.whiskerRight (TruncAux.triangleFunctorNatTransOfLE t a b h) Triangle.Ï€â‚ƒ

@[reassoc (attr := simp)]
lemma natTransTruncLTOfLE_Î¹_app (a b : â„¤) (h : a â‰¤ b) (X : C) :
    (t.natTransTruncLTOfLE a b h).app X â‰« (t.truncLTÎ¹ b).app X = (t.truncLTÎ¹ a).app X := by
  simpa using ((TruncAux.triangleFunctorNatTransOfLE t a b h).app X).commâ‚.symm

@[reassoc (attr := simp)]
lemma natTransTruncLTOfLE_Î¹ (a b : â„¤) (h : a â‰¤ b) :
    t.natTransTruncLTOfLE a b h â‰« t.truncLTÎ¹ b = t.truncLTÎ¹ a := by cat_disch

@[reassoc (attr := simp)]
lemma Ï€_natTransTruncGEOfLE_app (a b : â„¤) (h : a â‰¤ b) (X : C) :
    (t.truncGEÏ€ a).app X â‰« (t.natTransTruncGEOfLE a b h).app X = (t.truncGEÏ€ b).app X := by
  simpa only [TruncAux.triangleFunctor_obj, TruncAux.triangle_objâ‚‚,
    TruncAux.triangleFunctorNatTransOfLE_app_homâ‚‚, Category.id_comp] using
    ((TruncAux.triangleFunctorNatTransOfLE t a b h).app X).commâ‚‚

@[reassoc]
lemma truncGEÎ´LT_comp_natTransTruncLTOfLE_app (a b : â„¤) (h : a â‰¤ b) (X : C) :
  (t.truncGEÎ´LT a).app X â‰« ((natTransTruncLTOfLE t a b h).app X)âŸ¦(1 :â„¤)âŸ§' =
    (t.natTransTruncGEOfLE a b h).app X â‰« (t.truncGEÎ´LT b).app X :=
  ((TruncAux.triangleFunctorNatTransOfLE t a b h).app X).commâ‚ƒ

@[reassoc]
lemma truncGEÎ´LT_comp_whiskerRight_natTransTruncLTOfLE (a b : â„¤) (h : a â‰¤ b) :
  t.truncGEÎ´LT a â‰« Functor.whiskerRight (natTransTruncLTOfLE t a b h) (shiftFunctor C (1 : â„¤)) =
    t.natTransTruncGEOfLE a b h â‰« t.truncGEÎ´LT b := by
  ext X
  exact t.truncGEÎ´LT_comp_natTransTruncLTOfLE_app a b h X

@[reassoc (attr := simp)]
lemma Ï€_natTransTruncGEOfLE (a b : â„¤) (h : a â‰¤ b) :
    t.truncGEÏ€ a â‰« t.natTransTruncGEOfLE a b h = t.truncGEÏ€ b := by cat_disch

/-- The natural transformation `t.triangleLTGE a âŸ¶ t.triangleLTGE b`
when `a â‰¤ b`. -/
noncomputable def natTransTriangleLTGEOfLE (a b : â„¤) (h : a â‰¤ b) :
    t.triangleLTGE a âŸ¶ t.triangleLTGE b := by
  refine Triangle.functorHomMk' (t.natTransTruncLTOfLE a b h) (ğŸ™ _)
    ((t.natTransTruncGEOfLE a b h)) ?_ ?_ ?_
  Â· simp
  Â· simp
  Â· exact t.truncGEÎ´LT_comp_whiskerRight_natTransTruncLTOfLE a b h

@[simp]
lemma natTransTriangleLTGEOfLE_refl (a : â„¤) :
    t.natTransTriangleLTGEOfLE a a (by rfl) = ğŸ™ _ :=
  TruncAux.triangleFunctorNatTransOfLE_refl t a

lemma natTransTriangleLTGEOfLE_trans (a b c : â„¤) (hab : a â‰¤ b) (hbc : b â‰¤ c) :
    t.natTransTriangleLTGEOfLE a b hab â‰« t.natTransTriangleLTGEOfLE b c hbc =
      t.natTransTriangleLTGEOfLE a c (hab.trans hbc) :=
  TruncAux.triangleFunctorNatTransOfLE_trans t a b c hab hbc

@[simp]
lemma natTransTruncLTOfLE_refl (a : â„¤) :
    t.natTransTruncLTOfLE a a (by rfl) = ğŸ™ _ :=
  congr_arg (fun x â†¦ Functor.whiskerRight x (Triangle.Ï€â‚)) (t.natTransTriangleLTGEOfLE_refl a)

@[simp]
lemma natTransTruncLTOfLE_trans (a b c : â„¤) (hab : a â‰¤ b) (hbc : b â‰¤ c) :
    t.natTransTruncLTOfLE a b hab â‰« t.natTransTruncLTOfLE b c hbc =
      t.natTransTruncLTOfLE a c (hab.trans hbc) :=
  congr_arg (fun x â†¦ Functor.whiskerRight x Triangle.Ï€â‚)
    (t.natTransTriangleLTGEOfLE_trans a b c hab hbc)

@[simp]
lemma natTransTruncGEOfLE_refl (a : â„¤) :
    t.natTransTruncGEOfLE a a (by rfl) = ğŸ™ _ :=
  congr_arg (fun x â†¦ Functor.whiskerRight x (Triangle.Ï€â‚ƒ)) (t.natTransTriangleLTGEOfLE_refl a)

@[simp]
lemma natTransTruncGEOfLE_trans (a b c : â„¤) (hab : a â‰¤ b) (hbc : b â‰¤ c) :
    t.natTransTruncGEOfLE a b hab â‰« t.natTransTruncGEOfLE b c hbc =
      t.natTransTruncGEOfLE a c (hab.trans hbc) :=
  congr_arg (fun x â†¦ Functor.whiskerRight x Triangle.Ï€â‚ƒ)
    (t.natTransTriangleLTGEOfLE_trans a b c hab hbc)

lemma natTransTruncLTOfLE_refl_app (a : â„¤) (X : C) :
    (t.natTransTruncLTOfLE a a (by rfl)).app X = ğŸ™ _ :=
  congr_app (t.natTransTruncLTOfLE_refl a) X

lemma natTransTruncLTOfLE_trans_app (a b c : â„¤) (hab : a â‰¤ b) (hbc : b â‰¤ c) (X : C) :
    (t.natTransTruncLTOfLE a b hab).app X â‰« (t.natTransTruncLTOfLE b c hbc).app X =
      (t.natTransTruncLTOfLE a c (hab.trans hbc)).app X :=
  congr_app (t.natTransTruncLTOfLE_trans a b c hab hbc) X

lemma natTransTruncGEOfLE_refl_app (a : â„¤) (X : C) :
    (t.natTransTruncGEOfLE a a (by rfl)).app X = ğŸ™ _ :=
  congr_app (t.natTransTruncGEOfLE_refl a) X

lemma natTransTruncGEOfLE_trans_app (a b c : â„¤) (hab : a â‰¤ b) (hbc : b â‰¤ c) (X : C) :
    (t.natTransTruncGEOfLE a b hab).app X â‰« (t.natTransTruncGEOfLE b c hbc).app X =
      (t.natTransTruncGEOfLE a c (hab.trans hbc)).app X :=
  congr_app (t.natTransTruncGEOfLE_trans a b c hab hbc) X

lemma isLE_of_isZero {X : C} (hX : IsZero X) (n : â„¤) : t.IsLE X n :=
  t.isLE_of_iso (((t.truncLT (n + 1)).map_isZero hX).isoZero â‰ªâ‰« hX.isoZero.symm) n

lemma isGE_of_isZero {X : C} (hX : IsZero X) (n : â„¤) : t.IsGE X n :=
  t.isGE_of_iso (((t.truncGE n).map_isZero hX).isoZero â‰ªâ‰« hX.isoZero.symm) n

instance (n : â„¤) : t.IsLE (0 : C) n := t.isLE_of_isZero (isZero_zero C) n

instance (n : â„¤) : t.IsGE (0 : C) n := t.isGE_of_isZero (isZero_zero C) n

lemma isLE_iff_isIso_truncLTÎ¹_app (nâ‚€ nâ‚ : â„¤) (h : nâ‚€ + 1 = nâ‚) (X : C) :
    t.IsLE X nâ‚€ â†” IsIso (((t.truncLTÎ¹ nâ‚)).app X) := by
  subst h
  refine âŸ¨fun _ â†¦ ?_,
    fun _ â†¦ t.isLE_of_iso (asIso (((t.truncLTÎ¹ (nâ‚€ + 1))).app X)) nâ‚€âŸ©
  obtain âŸ¨e, heâŸ© := t.triangle_iso_exists
    (contractible_distinguished X) (t.triangleLTGE_distinguished (nâ‚€ + 1) X)
    (Iso.refl X) nâ‚€ (nâ‚€ + 1)
    (by dsimp; infer_instance) (by dsimp; infer_instance)
    (by dsimp; infer_instance) (by dsimp; infer_instance)
  have he' : e.inv.homâ‚‚ = ğŸ™ X := by
    rw [â† cancel_mono e.hom.homâ‚‚, â† comp_homâ‚‚, e.inv_hom_id, he]
    simp
  have : (t.truncLTÎ¹ (nâ‚€ + 1)).app X = e.inv.homâ‚ := by
    simpa [he'] using e.inv.commâ‚
  rw [this]
  infer_instance

lemma isGE_iff_isIso_truncGEÏ€_app (n : â„¤) (X : C) :
    t.IsGE X n â†” IsIso ((t.truncGEÏ€ n).app X) := by
  constructor
  Â· intro h
    obtain âŸ¨e, heâŸ© := t.triangle_iso_exists
      (inv_rot_of_distTriang _ (contractible_distinguished X))
      (t.triangleLTGE_distinguished n X) (Iso.refl X) (n - 1) n
      (t.isLE_of_iso (shiftFunctor C (-1 : â„¤)).mapZeroObject.symm _)
      (by dsimp; infer_instance) (by dsimp; infer_instance) (by dsimp; infer_instance)
    dsimp at he
    have : (truncGEÏ€ t n).app X = e.hom.homâ‚ƒ := by
      have := e.hom.commâ‚‚
      dsimp at this
      rw [â† cancel_epi e.hom.homâ‚‚, â† this, he]
    rw [this]
    infer_instance
  Â· intro
    exact t.isGE_of_iso (asIso ((truncGEÏ€ t n).app X)).symm n

instance (X : C) (n : â„¤) [t.IsGE X n] : IsIso ((t.truncGEÏ€ n).app X) := by
  rw [â† isGE_iff_isIso_truncGEÏ€_app]
  infer_instance

lemma isGE_iff_isZero_truncLT_obj (n : â„¤) (X : C) :
    t.IsGE X n â†” IsZero ((t.truncLT n).obj X) := by
  rw [t.isGE_iff_isIso_truncGEÏ€_app n X]
  exact (Triangle.isZeroâ‚_iff_isIsoâ‚‚ _ (t.triangleLTGE_distinguished n X)).symm

lemma isLE_iff_isZero_truncGE_obj (nâ‚€ nâ‚ : â„¤) (h : nâ‚€ + 1 = nâ‚) (X : C) :
    t.IsLE X nâ‚€ â†” IsZero ((t.truncGE nâ‚).obj X) := by
  rw [t.isLE_iff_isIso_truncLTÎ¹_app nâ‚€ nâ‚ h X]
  exact (Triangle.isZeroâ‚ƒ_iff_isIsoâ‚ _ (t.triangleLTGE_distinguished nâ‚ X)).symm

lemma isZero_truncLT_obj_of_isGE (n : â„¤) (X : C) [t.IsGE X n] :
    IsZero ((t.truncLT n).obj X) := by
  rw [â† isGE_iff_isZero_truncLT_obj]
  infer_instance

lemma isZero_truncGE_obj_of_isLE (nâ‚€ nâ‚ : â„¤) (h : nâ‚€ + 1 = nâ‚) (X : C) [t.IsLE X nâ‚€] :
    IsZero ((t.truncGE nâ‚).obj X) := by
  rw [â† t.isLE_iff_isZero_truncGE_obj _ _ h X]
  infer_instance

lemma from_truncGE_obj_ext {n : â„¤} {X : C} {Y : C}
    {fâ‚ fâ‚‚ : (t.truncGE n).obj X âŸ¶ Y} (h : (t.truncGEÏ€ n).app X â‰« fâ‚ = (t.truncGEÏ€ n).app X â‰« fâ‚‚)
    [t.IsGE Y n] :
    fâ‚ = fâ‚‚ := by
  suffices âˆ€ (f : (t.truncGE n).obj X âŸ¶ Y), (t.truncGEÏ€ n).app X â‰« f = 0 â†’ f = 0 by
    rw [â† sub_eq_zero, this (fâ‚ - fâ‚‚) (by cat_disch)]
  intro f hf
  obtain âŸ¨g, hgâŸ© := Triangle.yoneda_exactâ‚ƒ _
    (t.triangleLTGE_distinguished n X) f hf
  have hg' := t.zero_of_isLE_of_isGE g (n-2) n (by lia)
    (by exact t.isLE_shift _ (n-1) 1 (n-2) (by lia)) inferInstance
  rw [hg, hg', comp_zero]

lemma to_truncLT_obj_ext {n : â„¤} {Y : C} {X : C}
    {fâ‚ fâ‚‚ : Y âŸ¶ (t.truncLT n).obj X}
    (h : fâ‚ â‰« (t.truncLTÎ¹ n).app X = fâ‚‚ â‰« (t.truncLTÎ¹ n).app X)
    [t.IsLE Y (n - 1)] :
    fâ‚ = fâ‚‚ := by
  suffices âˆ€ (f : Y âŸ¶ (t.truncLT n).obj X) (_ : f â‰« (t.truncLTÎ¹ n).app X = 0), f = 0 by
    rw [â† sub_eq_zero, this (fâ‚ - fâ‚‚) (by cat_disch)]
  intro f hf
  obtain âŸ¨g, hgâŸ© := Triangle.coyoneda_exactâ‚‚ _ (inv_rot_of_distTriang _
    (t.triangleLTGE_distinguished n X)) f hf
  have hg' := t.zero_of_isLE_of_isGE g (n - 1) (n + 1) (by lia) inferInstance
    (by dsimp; apply (t.isGE_shift _ n (-1) (n + 1) (by lia)))
  rw [hg, hg', zero_comp]

@[reassoc]
lemma truncLT_map_truncLTÎ¹_app (n : â„¤) (X : C) :
    (t.truncLT n).map ((t.truncLTÎ¹ n).app X) = (t.truncLTÎ¹ n).app ((t.truncLT n).obj X) :=
  t.to_truncLT_obj_ext (by simp)

@[reassoc]
lemma truncGE_map_truncGEÏ€_app (n : â„¤) (X : C) :
    (t.truncGE n).map ((t.truncGEÏ€ n).app X) = (t.truncGEÏ€ n).app ((t.truncGE n).obj X) :=
  t.from_truncGE_obj_ext (by simp)

section

variable {X Y : C} (f : X âŸ¶ Y) (nâ‚€ nâ‚ : â„¤) (h : nâ‚€ + 1 = nâ‚) [t.IsLE X nâ‚€]

include h in
lemma liftTruncLT_aux :
    âˆƒ (f' : X âŸ¶ (t.truncLT nâ‚).obj Y), f = f' â‰« (t.truncLTÎ¹ nâ‚).app Y :=
  Triangle.coyoneda_exactâ‚‚ _ (t.triangleLTGE_distinguished nâ‚ Y) f
    (t.zero_of_isLE_of_isGE _ nâ‚€ nâ‚ (by lia) inferInstance (by dsimp; infer_instance))

/-- Constructor for morphisms to `(t.truncLT nâ‚).obj Y`. -/
noncomputable def liftTruncLT {X Y : C} (f : X âŸ¶ Y) (nâ‚€ nâ‚ : â„¤) (h : nâ‚€ + 1 = nâ‚) [t.IsLE X nâ‚€] :
    X âŸ¶ (t.truncLT nâ‚).obj Y :=
  (t.liftTruncLT_aux f nâ‚€ nâ‚ h).choose

@[reassoc (attr := simp)]
lemma liftTruncLT_Î¹ {X Y : C} (f : X âŸ¶ Y) (nâ‚€ nâ‚ : â„¤) (h : nâ‚€ + 1 = nâ‚) [t.IsLE X nâ‚€] :
    t.liftTruncLT f nâ‚€ nâ‚ h â‰« (t.truncLTÎ¹ nâ‚).app Y = f :=
  (t.liftTruncLT_aux f nâ‚€ nâ‚ h).choose_spec.symm

end

section

variable {X Y : C} (f : X âŸ¶ Y) (n : â„¤) [t.IsGE Y n]

lemma descTruncGE_aux :
  âˆƒ (f' : (t.truncGE n).obj X âŸ¶ Y), f = (t.truncGEÏ€ n).app X â‰« f' :=
  Triangle.yoneda_exactâ‚‚ _ (t.triangleLTGE_distinguished n X) f
    (t.zero_of_isLE_of_isGE _ (n-1)  n (by lia) (by dsimp; infer_instance) inferInstance)

/-- Constructor for morphisms from `(t.truncGE n).obj X`. -/
noncomputable def descTruncGE :
    (t.truncGE n).obj X âŸ¶ Y :=
  (t.descTruncGE_aux f n).choose

@[reassoc (attr := simp)]
lemma Ï€_descTruncGE {X Y : C} (f : X âŸ¶ Y) (n : â„¤) [t.IsGE Y n] :
    (t.truncGEÏ€ n).app X â‰« t.descTruncGE f n  = f :=
  (t.descTruncGE_aux f n).choose_spec.symm

end

lemma isLE_iff_orthogonal (nâ‚€ nâ‚ : â„¤) (h : nâ‚€ + 1 = nâ‚) (X : C) :
    t.IsLE X nâ‚€ â†” âˆ€ (Y : C) (f : X âŸ¶ Y) (_ : t.IsGE Y nâ‚), f = 0 := by
  refine âŸ¨fun _ Y f _ â†¦ t.zero f nâ‚€ nâ‚ (by lia), fun hX â†¦ ?_âŸ©
  rw [t.isLE_iff_isZero_truncGE_obj nâ‚€ nâ‚ h, IsZero.iff_id_eq_zero]
  exact t.from_truncGE_obj_ext (by simpa using hX _ _ inferInstance)

lemma isGE_iff_orthogonal (nâ‚€ nâ‚ : â„¤) (h : nâ‚€ + 1 = nâ‚) (X : C) :
    t.IsGE X nâ‚ â†” âˆ€ (Y : C) (f : Y âŸ¶ X) (_ : t.IsLE Y nâ‚€), f = 0 := by
  refine âŸ¨fun _ Y f _ â†¦ t.zero f nâ‚€ nâ‚ (by lia), fun hX â†¦ ?_âŸ©
  rw [t.isGE_iff_isZero_truncLT_obj nâ‚ X, IsZero.iff_id_eq_zero]
  exact t.to_truncLT_obj_ext (by simpa using hX _ _ (by rw [â† h]; infer_instance))

lemma isLEâ‚‚ (T : Triangle C) (hT : T âˆˆ distTriang C) (n : â„¤) (hâ‚ : t.IsLE T.objâ‚ n)
    (hâ‚ƒ : t.IsLE T.objâ‚ƒ n) : t.IsLE T.objâ‚‚ n := by
  rw [t.isLE_iff_orthogonal n (n+1) rfl]
  intro Y f hY
  obtain âŸ¨f', hf'âŸ© := Triangle.yoneda_exactâ‚‚ _ hT f
    (t.zero _ n (n + 1) (by lia) )
  rw [hf', t.zero f' n (n + 1) (by lia), comp_zero]

lemma isGEâ‚‚ (T : Triangle C) (hT : T âˆˆ distTriang C) (n : â„¤) (hâ‚ : t.IsGE T.objâ‚ n)
    (hâ‚ƒ : t.IsGE T.objâ‚ƒ n) : t.IsGE T.objâ‚‚ n := by
  rw [t.isGE_iff_orthogonal (n-1) n (by lia)]
  intro Y f hY
  obtain âŸ¨f', hf'âŸ© := Triangle.coyoneda_exactâ‚‚ _ hT f (t.zero _ (n-1) n (by lia))
  rw [hf', t.zero f' (n-1) n (by lia), zero_comp]

instance : t.minus.IsTriangulated where
  exists_zero := âŸ¨0, isZero_zero C, 0, inferInstanceâŸ©
  toIsTriangulatedClosedâ‚‚ := .mk' (fun T hT â†¦ by
    rintro âŸ¨iâ‚, hiâ‚âŸ© âŸ¨iâ‚ƒ, hiâ‚ƒâŸ©
    exact âŸ¨max iâ‚ iâ‚ƒ, t.isLEâ‚‚ T hT _ (t.isLE_of_LE _ _ _ (le_max_left iâ‚ iâ‚ƒ))
      (t.isLE_of_LE _ _ _ (le_max_right iâ‚ iâ‚ƒ))âŸ©)

instance : t.plus.IsTriangulated where
  exists_zero := âŸ¨0, isZero_zero C, 0, inferInstanceâŸ©
  toIsTriangulatedClosedâ‚‚ := .mk' (fun T hT â†¦ by
    rintro âŸ¨iâ‚, hiâ‚âŸ© âŸ¨iâ‚ƒ, hiâ‚ƒâŸ©
    exact âŸ¨min iâ‚ iâ‚ƒ, t.isGEâ‚‚ T hT _ (t.isGE_of_GE _ _ _ (min_le_left iâ‚ iâ‚ƒ))
      (t.isGE_of_GE _ _ _ (min_le_right iâ‚ iâ‚ƒ))âŸ©)

instance : t.bounded.IsTriangulated := by
  dsimp [bounded]
  infer_instance

lemma isIso_truncLT_map_iff {X Y : C} (f : X âŸ¶ Y) (n : â„¤) :
    IsIso ((t.truncLT n).map f) â†”
      âˆƒ (Z : C) (g : Y âŸ¶ Z) (h : Z âŸ¶ ((t.truncLT n).obj X)âŸ¦1âŸ§)
        (_ : Triangle.mk ((t.truncLTÎ¹ n).app X â‰« f) g h âˆˆ distTriang _), t.IsGE Z n := by
  refine âŸ¨fun hf â†¦ ?_, fun âŸ¨Z, g, h, mem, _âŸ© â†¦ ?_âŸ©
  Â· refine âŸ¨(t.truncGE n).obj Y, (t.truncGEÏ€ n).app Y,
      (t.truncGEÎ´LT n).app Y â‰« (inv ((t.truncLT n).map f))âŸ¦1âŸ§',
      isomorphic_distinguished _ (t.triangleLTGE_distinguished n Y) _ ?_, inferInstanceâŸ©
    exact Triangle.isoMk _ _ (asIso ((t.truncLT n).map f)) (Iso.refl _) (Iso.refl _)
  Â· obtain âŸ¨e, heâŸ© := t.triangle_iso_exists
      mem (t.triangleLTGE_distinguished n Y) (Iso.refl _) (n - 1) n
      (by dsimp; infer_instance) (by dsimp; infer_instance)
      (by dsimp; infer_instance) (by dsimp; infer_instance)
    suffices ((t.truncLT n).map f) = e.hom.homâ‚ by rw [this]; infer_instance
    exact t.to_truncLT_obj_ext (Eq.trans (by cat_disch) e.hom.commâ‚)

lemma isIso_truncGE_map_iff {Y Z : C} (g : Y âŸ¶ Z) (nâ‚€ nâ‚ : â„¤) (hn : nâ‚€ + 1 = nâ‚) :
    IsIso ((t.truncGE nâ‚).map g) â†”
      âˆƒ (X : C) (f : X âŸ¶ Y) (h : ((t.truncGE nâ‚).obj Z) âŸ¶ XâŸ¦(1 : â„¤)âŸ§)
        (_ : Triangle.mk f (g â‰« (t.truncGEÏ€ nâ‚).app Z) h âˆˆ distTriang _), t.IsLE X nâ‚€ := by
  refine âŸ¨fun hf â†¦ ?_, fun âŸ¨X, f, h, mem, _âŸ© â†¦ ?_âŸ©
  Â· refine âŸ¨_, (t.truncLTÎ¹ nâ‚).app Y, inv ((t.truncGE nâ‚).map g) â‰« (t.truncGEÎ´LT nâ‚).app Y,
      isomorphic_distinguished _ (t.triangleLTGE_distinguished nâ‚ Y) _ ?_,
      by subst hn; infer_instanceâŸ©
    exact Iso.symm (Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _)
      (asIso ((t.truncGE nâ‚).map g)) (by simp) (by simp) (by simp))
  Â· obtain âŸ¨e, heâŸ© :=
      t.triangle_iso_exists (t.triangleLTGE_distinguished nâ‚ Y) mem (Iso.refl _) nâ‚€ nâ‚
        (by dsimp; rw [â† hn]; infer_instance) (by dsimp; infer_instance)
        (by dsimp; infer_instance) (by dsimp; infer_instance)
    suffices ((t.truncGE nâ‚).map g) = e.hom.homâ‚ƒ by rw [this]; infer_instance
    exact t.from_truncGE_obj_ext (Eq.trans (by cat_disch) e.hom.commâ‚‚.symm)

instance (X : C) (a b : â„¤) [t.IsLE X b] : t.IsLE ((t.truncLT a).obj X) b := by
  by_cases h : a â‰¤ b + 1
  Â· exact t.isLE_truncLT_obj ..
  Â· have := (t.isLE_iff_isIso_truncLTÎ¹_app (a - 1) a (by lia) X).1 (t.isLE_of_LE _ b _ (by lia))
    exact t.isLE_of_iso (show X â‰… _ from (asIso ((t.truncLTÎ¹ a).app X)).symm) _

instance (X : C) (a b : â„¤) [t.IsGE X a] : t.IsGE ((t.truncGE b).obj X) a := by
  by_cases h : a â‰¤ b
  Â· exact t.isGE_truncGE_obj ..
  Â· have : t.IsGE X b := t.isGE_of_GE X b a (by lia)
    exact t.isGE_of_iso (show X â‰… _ from asIso ((t.truncGEÏ€ b).app X)) _

/-- The composition `t.truncLT b â‹™ t.truncGE a`. -/
noncomputable abbrev truncGELT (a b : â„¤) : C â¥¤ C := t.truncLT b â‹™ t.truncGE a

/-- The composition `t.truncGE b â‹™ t.truncLT a`. -/
noncomputable abbrev truncLTGE (a b : â„¤) : C â¥¤ C := t.truncGE a â‹™ t.truncLT b

instance (X : C) (a b : â„¤) : t.IsGE ((t.truncGELT a b).obj X) a := by
  dsimp; infer_instance

instance (X : C) (a b : â„¤) : t.IsLE ((t.truncLTGE a b).obj X) (b - 1) := by
  dsimp; infer_instance

section

variable [IsTriangulated C]

lemma isIsoâ‚_truncLT_map_of_isGE (T : Triangle C) (hT : T âˆˆ distTriang C)
    (n : â„¤) (hâ‚ƒ : t.IsGE T.objâ‚ƒ n) :
    IsIso ((t.truncLT n).map T.morâ‚) := by
  rw [isIso_truncLT_map_iff]
  obtain âŸ¨Z, g, k, memâŸ© := distinguished_cocone_triangle ((t.truncLTÎ¹ n).app T.objâ‚ â‰« T.morâ‚)
  refine âŸ¨_, _, _, mem, ?_âŸ©
  let H := someOctahedron rfl (t.triangleLTGE_distinguished n T.objâ‚) hT mem
  exact t.isGEâ‚‚ _ H.mem n (by dsimp; infer_instance) (by dsimp; infer_instance)

lemma isIsoâ‚‚_truncGE_map_of_isLE (T : Triangle C) (hT : T âˆˆ distTriang C)
    (nâ‚€ nâ‚ : â„¤) (h : nâ‚€ + 1 = nâ‚) (hâ‚ : t.IsLE T.objâ‚ nâ‚€) :
    IsIso ((t.truncGE nâ‚).map T.morâ‚‚) := by
  rw [isIso_truncGE_map_iff _ _ _ _ h]
  obtain âŸ¨X, f, k, memâŸ© := distinguished_cocone_triangleâ‚ (T.morâ‚‚ â‰« (t.truncGEÏ€ nâ‚).app T.objâ‚ƒ)
  refine âŸ¨_, _, _, mem, ?_âŸ©
  subst h
  have H := someOctahedron rfl (rot_of_distTriang _ hT)
    (rot_of_distTriang _ (t.triangleLTGE_distinguished (nâ‚€ + 1) T.objâ‚ƒ))
    (rot_of_distTriang _ mem)
  have : t.IsLE (XâŸ¦(1 : â„¤)âŸ§) (nâ‚€ - 1) :=
    t.isLEâ‚‚ _ H.mem (nâ‚€ - 1) (t.isLE_shift T.objâ‚ nâ‚€ 1 (nâ‚€ - 1) (by lia))
      (t.isLE_shift ((t.truncLT (nâ‚€ + 1)).obj T.objâ‚ƒ) nâ‚€ 1 (nâ‚€-1) (by lia))
  exact t.isLE_of_shift X nâ‚€ 1 (nâ‚€ - 1) (by lia)

instance (X : C) (a b : â„¤) [t.IsGE X a] :
    t.IsGE ((t.truncLT b).obj X) a := by
  rw [t.isGE_iff_isZero_truncLT_obj]
  have := t.isIsoâ‚_truncLT_map_of_isGE _ ((t.triangleLTGE_distinguished b X)) a
    (by dsimp; infer_instance)
  dsimp at this
  refine IsZero.of_iso ?_ (asIso ((t.truncLT a).map ((t.truncLTÎ¹ b).app X)))
  rwa [â† isGE_iff_isZero_truncLT_obj]

instance (X : C) (a b : â„¤) [t.IsLE X b] : t.IsLE ((t.truncGE a).obj X) b := by
  rw [t.isLE_iff_isZero_truncGE_obj b (b + 1) rfl]
  have := t.isIsoâ‚‚_truncGE_map_of_isLE _ (t.triangleLTGE_distinguished a X) b _ rfl
    (by dsimp; infer_instance)
  dsimp at this
  refine IsZero.of_iso ?_ (asIso ((t.truncGE (b + 1)).map ((t.truncGEÏ€ a).app X))).symm
  rwa [â† isLE_iff_isZero_truncGE_obj _ _ _ rfl]

instance (X : C) (a b : â„¤) :
    t.IsLE ((t.truncGELT a b).obj X) (b - 1) := by
  dsimp; infer_instance

instance (X : C) (a b : â„¤) :
    t.IsGE ((t.truncLTGE a b).obj X) a := by
  dsimp; infer_instance

lemma isIso_truncGE_map_truncGEÏ€_app (a b : â„¤) (h : b â‰¤ a) (X : C) :
    IsIso ((t.truncGE a).map ((t.truncGEÏ€ b).app X)) :=
  t.isIsoâ‚‚_truncGE_map_of_isLE _ (t.triangleLTGE_distinguished b X)
    (a - 1) a (by lia) (t.isLE_truncLT_obj _ _ _ (by simpa))

lemma isIso_truncLT_map_truncLTÎ¹_app (a b : â„¤) (h : a â‰¤ b) (X : C) :
    IsIso ((t.truncLT a).map ((t.truncLTÎ¹ b).app X)) :=
  t.isIsoâ‚_truncLT_map_of_isGE _ (t.triangleLTGE_distinguished b X) a
    (t.isGE_of_GE ((t.truncGE b).obj X) a b (by lia))

instance (X : C) (n : â„¤) : IsIso ((t.truncLT n).map ((t.truncLTÎ¹ n).app X)) :=
  isIso_truncLT_map_truncLTÎ¹_app t _ _ (by rfl) X

instance (X : C) (n : â„¤) : IsIso ((t.truncGE n).map ((t.truncGEÏ€ n).app X)) :=
  t.isIso_truncGE_map_truncGEÏ€_app _ _ (by rfl) _

instance (a b : â„¤) (X : C) :
    IsIso ((t.truncLTÎ¹ b).app ((t.truncGE a).obj ((t.truncLT b).obj X))) := by
  rw [â† t.isLE_iff_isIso_truncLTÎ¹_app (b - 1) b (by lia)]
  infer_instance

/-- The natural transformation `t.truncGELT a b âŸ¶ t.truncLTGE a b`
(which is an isomorphism, see `truncGELTIsoLTGE`.) -/
noncomputable def truncGELTToLTGE (a b : â„¤) :
    t.truncGELT a b âŸ¶ t.truncLTGE a b where
  app X := t.liftTruncLT (t.descTruncGE
    ((t.truncLTÎ¹ b).app X â‰« (t.truncGEÏ€ a).app X) a) (b - 1) b (by lia)
  naturality _ _ _ :=
    t.to_truncLT_obj_ext (by dsimp; exact t.from_truncGE_obj_ext (by simp))

@[reassoc (attr := simp)]
lemma truncGELTToLTGE_app_pentagon (a b : â„¤) (X : C) :
    (t.truncGEÏ€ a).app _ â‰« (t.truncGELTToLTGE a b).app X â‰« (t.truncLTÎ¹ b).app _ =
      (t.truncLTÎ¹ b).app X â‰« (t.truncGEÏ€ a).app X := by
  simp [truncGELTToLTGE]

lemma truncGELTToLTGE_app_pentagon_uniqueness {a b : â„¤} {X : C}
    (Ï† : (t.truncGELT a b).obj X âŸ¶ (t.truncLTGE a b).obj X)
    (hÏ† : (t.truncGEÏ€ a).app _ â‰« Ï† â‰« (t.truncLTÎ¹ b).app _ =
      (t.truncLTÎ¹ b).app X â‰« (t.truncGEÏ€ a).app X) :
    (t.truncGELTToLTGE a b).app X = Ï† :=
  t.to_truncLT_obj_ext (by dsimp; exact t.from_truncGE_obj_ext (by cat_disch))

@[reassoc]
lemma truncLT_map_truncGE_map_truncLTÎ¹_app_fac (a b : â„¤) (X : C) :
    (t.truncLTÎ¹ b).app ((t.truncGE a).obj ((t.truncLT b).obj X)) â‰«
        (t.truncGELTToLTGE a b).app X =
    (t.truncLT b).map ((t.truncGE a).map ((t.truncLTÎ¹ b).app X)) := by
  rw [â† cancel_epi (inv ((t.truncLTÎ¹ b).app ((t.truncGE a).obj ((t.truncLT b).obj X)))),
    IsIso.inv_hom_id_assoc]
  exact t.truncGELTToLTGE_app_pentagon_uniqueness _ (by simp)

/-- The connecting homomorphism
`(t.truncGELT a b).obj X âŸ¶ ((t.truncLT a).obj X)âŸ¦1âŸ§`,
as a natural transformation. -/
@[expose, simps!]
noncomputable def truncGELTÎ´LT (a b : â„¤) :
    t.truncGELT a b âŸ¶ t.truncLT a â‹™ shiftFunctor C (1 : â„¤) :=
  Functor.whiskerLeft (t.truncLT b) (t.truncGEÎ´LT a) â‰«
    Functor.whiskerRight (t.truncLTÎ¹ b) (t.truncLT a â‹™ shiftFunctor C (1 : â„¤))

/-- The functorial (distinguished) triangle
`(t.truncLT a).obj X âŸ¶ (t.truncLT b).obj X âŸ¶ (t.truncGELT a b).obj X âŸ¶ ...`
when `a â‰¤ b`. -/
@[expose, simps!]
noncomputable def triangleLTLTGELT (a b : â„¤) (h : a â‰¤ b) : C â¥¤ Triangle C :=
  Triangle.functorMk (t.natTransTruncLTOfLE a b h)
    (Functor.whiskerLeft (t.truncLT b) (t.truncGEÏ€ a)) (t.truncGELTÎ´LT a b)

lemma triangleLTLTGELT_distinguished (a b : â„¤) (h : a â‰¤ b) (X : C) :
    (t.triangleLTLTGELT a b h).obj X âˆˆ distTriang C := by
  have := t.isIso_truncLT_map_truncLTÎ¹_app a b h X
  refine isomorphic_distinguished _ (t.triangleLTGE_distinguished a ((t.truncLT b).obj X)) _ ?_
  refine Triangle.isoMk _ _ ((asIso ((t.truncLT a).map ((t.truncLTÎ¹ b).app X))).symm)
    (Iso.refl _) (Iso.refl _) ?_ ?_ ?_
  Â· dsimp
    simp only [Category.comp_id, IsIso.eq_inv_comp]
    exact t.to_truncLT_obj_ext (by simp)
  Â· simp
  Â· simp

instance (a b : â„¤) : IsIso (t.truncGELTToLTGE a b) := by
  rw [NatTrans.isIso_iff_isIso_app]
  intro X
  by_cases h : a â‰¤ b
  Â· let uâ‚â‚‚ := (t.natTransTruncLTOfLE a b h).app X
    let uâ‚‚â‚ƒ : (t.truncLT b).obj X âŸ¶ X := (t.truncLTÎ¹ b).app X
    let uâ‚â‚ƒ : _ âŸ¶ X := (t.truncLTÎ¹ a).app X
    have eq : uâ‚â‚‚ â‰« uâ‚‚â‚ƒ = uâ‚â‚ƒ := by simp [uâ‚â‚‚, uâ‚‚â‚ƒ, uâ‚â‚ƒ]
    have H := someOctahedron eq (t.triangleLTLTGELT_distinguished a b h X)
      (t.triangleLTGE_distinguished b X) (t.triangleLTGE_distinguished a X)
    let mâ‚ : (t.truncGELT a b).obj X âŸ¶  _ := H.mâ‚
    have : IsIso ((t.truncLT b).map H.mâ‚) :=
      t.isIsoâ‚_truncLT_map_of_isGE _ H.mem b (by dsimp; infer_instance)
    have eq' : t.liftTruncLT mâ‚ (b - 1) b (by lia) = (t.truncGELTToLTGE a b).app X :=
      t.to_truncLT_obj_ext
        (by dsimp; exact t.from_truncGE_obj_ext (by simpa using H.commâ‚))
    rw [â† eq']
    have fac : (t.truncLTÎ¹ b).app ((t.truncGE a).obj ((t.truncLT b).obj X)) â‰«
        t.liftTruncLT mâ‚ (b - 1) b (by lia) = (t.truncLT b).map mâ‚ :=
      t.to_truncLT_obj_ext (by simp [truncGELT])
    exact IsIso.of_isIso_fac_left fac
  Â· simp at h
    refine âŸ¨0, ?_, ?_âŸ©
    all_goals exact IsZero.eq_of_src (t.isZero _ (b-1) a (by lia)) _ _

instance (a b : â„¤) (X : C) :
    IsIso ((t.truncLT b).map ((t.truncGE a).map ((t.truncLTÎ¹ b).app X))) := by
  rw [â† t.truncLT_map_truncGE_map_truncLTÎ¹_app_fac a b X]
  infer_instance

/-- The natural transformation `t.truncGELT a b â‰… t.truncLTGE a b`. -/
noncomputable def truncGELTIsoLTGE (a b : â„¤) : t.truncGELT a b â‰… t.truncLTGE a b :=
  asIso (t.truncGELTToLTGE a b)

end

end

end TStructure

end Triangulated

end CategoryTheory
