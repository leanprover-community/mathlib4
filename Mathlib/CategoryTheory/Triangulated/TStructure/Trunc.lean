/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
module

public import Mathlib.CategoryTheory.Triangulated.TStructure.ETrunc
public import Mathlib.CategoryTheory.Triangulated.TStructure.Induced
public import Mathlib.CategoryTheory.Triangulated.TStructure.AbstractSpectralObject

/-!
# Truncations for a t-structure

-/

@[expose] public section

namespace CategoryTheory

open Category Limits Pretriangulated ZeroObject Preadditive

variable {C : Type*} [Category* C] [Preadditive C] [HasZeroObject C] [HasShift C â„¤]
  [âˆ€ (n : â„¤), (shiftFunctor C n).Additive] [Pretriangulated C]

namespace Triangulated

namespace TStructure

variable (t : TStructure C)

@[simps]
noncomputable def abstractSpectralObject : SpectralObject.AbstractSpectralObject C EInt where
  truncLT := t.eTruncLT
  truncGE := t.eTruncGE
  truncLTObjTopIso' := Iso.refl _
  truncGEObjBotIso' := Iso.refl _
  truncGEÎ´LT := t.eTruncGEÎ´LTt

namespace AbstractSpectralObject

open SpectralObject

@[simp]
lemma truncGELT_eq (g : Arrow EInt) :
  (abstractSpectralObject t).truncGELT.obj g =
    t.eTruncLT.obj g.right â‹™ t.eTruncGE.obj g.left := rfl

lemma isZero_truncGE_obj_top_obj (X : C) :
    IsZero ((t.abstractSpectralObject.truncGE.obj âŠ¤).obj X) :=
  IsZero.obj (isZero_zero _) _

lemma isZero_truncLT_obj_bot_obj (X : C) :
    IsZero ((t.abstractSpectralObject.truncLT.obj âŠ¥).obj X) :=
  IsZero.obj (isZero_zero _) _

@[simp]
lemma truncLEÎ¹_mk (n : â„¤) :
    t.abstractSpectralObject.truncLTÎ¹ (EInt.mk n) = t.truncLTÎ¹ n :=
  comp_id _

@[simp]
lemma truncGEÏ€_mk (n : â„¤) :
    t.abstractSpectralObject.truncGEÏ€ (EInt.mk n) = t.truncGEÏ€ n :=
  id_comp _

@[simp]
lemma truncGEÎ´LT_mk (n : â„¤) :
    t.abstractSpectralObject.truncGEÎ´LT.app (EInt.mk n) =
      t.truncGEÎ´LT n := rfl

noncomputable def triangleLTGEIso (n : â„¤) (X : C) :
    (t.abstractSpectralObject.triangleLTGE.obj (EInt.mk n)).obj X â‰…
      (t.triangleLTGE n).obj X :=
  Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) (Iso.refl _)

@[simp]
lemma truncLTObjTopIso : t.abstractSpectralObject.truncLTObjTopIso = Iso.refl _ := rfl

@[simp]
lemma truncGEObjBotIso : t.abstractSpectralObject.truncGEObjBotIso = Iso.refl _ := rfl

@[simp]
lemma truncLTÎ¹_top_app (X : C) :
    (t.abstractSpectralObject.truncLTÎ¹ âŠ¤).app X = ðŸ™ X := by
  dsimp [AbstractSpectralObject.truncLTÎ¹]
  erw [Functor.map_id]
  simp only [eTruncLT_obj_top, NatTrans.id_app, Functor.id_obj, comp_id]

@[simp]
lemma truncGEÏ€_bot_app (X : C) :
    (t.abstractSpectralObject.truncGEÏ€ âŠ¥).app X = ðŸ™ X := by
  dsimp [AbstractSpectralObject.truncGEÏ€]
  erw [Functor.map_id]
  simp only [eTruncGE_obj_bot, NatTrans.id_app, Functor.id_obj, comp_id]

noncomputable def triangleLTGETopIso (X : C) :
  (t.abstractSpectralObject.triangleLTGE.obj âŠ¤).obj X â‰…
    Pretriangulated.contractibleTriangle X := by
  refine Triangle.isoMk _ _ (((abstractSpectralObject t).truncLTObjTopIso).app X)
    (Iso.refl _) (isZero_truncLT_obj_bot_obj t X).isoZero ?_ ?_ ?_
  Â· dsimp
    rw [truncLTÎ¹_top_app]
  Â· exact IsZero.eq_of_tgt (isZero_zero _) _ _
  Â· refine IsZero.eq_of_src ?_ _ _
    exact IsZero.obj (isZero_zero _) _

noncomputable def triangleLTGEBotIso (X : C) :
  (t.abstractSpectralObject.triangleLTGE.obj âŠ¥).obj X â‰…
    (Pretriangulated.contractibleTriangle X).invRotate := by
  refine Triangle.isoMk _ _ ((isZero_truncLT_obj_bot_obj t X).isoZero â‰ªâ‰«
    (shiftFunctor C (-1 : â„¤)).mapZeroObject.symm)
    (((abstractSpectralObject t).truncLTObjTopIso).app X) (Iso.refl _) ?_ ?_ ?_
  Â· apply IsZero.eq_of_src
    apply isZero_truncLT_obj_bot_obj
  Â· dsimp
    rw [truncGEÏ€_bot_app]
  Â· apply IsZero.eq_of_tgt _
    dsimp
    rw [IsZero.iff_id_eq_zero, â† Functor.map_id, â† Functor.map_id, id_zero,
      Functor.map_zero, Functor.map_zero]

lemma distinguished (n : EInt) (X : C) :
  (t.abstractSpectralObject.triangleLTGE.obj n).obj X âˆˆ distTriang C := by
  obtain (_|_|n) := n
  Â· exact isomorphic_distinguished _
      (inv_rot_of_distTriang _ (contractible_distinguished X)) _
      (triangleLTGEBotIso t X)
  Â· exact isomorphic_distinguished _ (contractible_distinguished X) _
      (triangleLTGETopIso t X)
  Â· exact isomorphic_distinguished _ (t.triangleLTGE_distinguished n X) _
      (triangleLTGEIso t n X)

end AbstractSpectralObject


/- Now, we need the octahedron axiom -/

variable [IsTriangulated C]

-- to be removed
lemma isIso_eTruncGE_obj_map_truncGEÏ€_app' (a b : EInt) (h : a â‰¤ b) (X : C) :
    IsIso ((t.eTruncGE.obj b).map ((t.abstractSpectralObject.truncGEÏ€ a).app X)) := by
  induction b with
  | bot =>
    obtain rfl : a = âŠ¥ := by simpa using h
    dsimp
    simp only [AbstractSpectralObject.truncGEÏ€_bot_app]
    infer_instance
  | coe b =>
    induction a with
    | bot => dsimp; infer_instance
    | coe a =>
      simp only [EInt.coe_le_coe_iff] at h
      simp only [AbstractSpectralObject.truncGEÏ€_mk]
      exact t.isIso_truncGE_map_truncGEÏ€_app b a h X
    | top => simp at h
  | top => exact âŸ¨0, IsZero.eq_of_src (by simp) _ _, IsZero.eq_of_src (by simp) _ _âŸ©

-- to be removed
lemma isIso_eTruncLT_obj_map_truncLTÏ€_app' (a b : EInt) (h : a â‰¤ b) (X : C) :
    IsIso ((t.eTruncLT.obj a).map ((t.abstractSpectralObject.truncLTÎ¹ b).app X)) := by
  induction a with
  | bot => exact âŸ¨0, IsZero.eq_of_src (by simp) _ _, IsZero.eq_of_src (by simp) _ _âŸ©
  | coe a =>
    induction b with
    | bot => simp at h
    | coe b =>
      simp only [EInt.coe_le_coe_iff] at h
      simp only [AbstractSpectralObject.truncLEÎ¹_mk]
      exact t.isIso_truncLT_map_truncLTÎ¹_app a b h X
    | top => dsimp; infer_instance
  | top =>
    obtain rfl : b = âŠ¤ := by simpa using h
    dsimp [AbstractSpectralObject.truncLTÎ¹_top_app]
    infer_instance

instance (D : Arrow EInt) (X : C) :
  IsIso ((t.abstractSpectralObject.truncGEToTruncGEGE.app D).app X) :=
    t.isIso_eTruncGE_obj_map_truncGEÏ€_app' _ _ (leOfHom D.hom) X

instance (D : Arrow EInt) (X : C) :
  IsIso ((t.abstractSpectralObject.truncLTLTToTruncLT.app D).app X) :=
    t.isIso_eTruncLT_obj_map_truncLTÏ€_app' _ _ (leOfHom D.hom) X

instance (D : Arrow EInt) : IsIso (t.abstractSpectralObject.truncGEToTruncGEGE.app D) :=
  NatIso.isIso_of_isIso_app _

instance (D : Arrow EInt) : IsIso (t.abstractSpectralObject.truncLTLTToTruncLT.app D) :=
  NatIso.isIso_of_isIso_app _

instance : IsIso (t.abstractSpectralObject.truncGEToTruncGEGE) := NatIso.isIso_of_isIso_app _

instance : IsIso (t.abstractSpectralObject.truncLTLTToTruncLT) := NatIso.isIso_of_isIso_app _

omit [IsTriangulated C] in
lemma truncGEÏ€_compatibility (a : EInt) (X : C) :
  (t.abstractSpectralObject.truncGE.obj a).map ((t.abstractSpectralObject.truncGEÏ€ a).app X) =
    (t.abstractSpectralObject.truncGEÏ€ a).app
      ((t.abstractSpectralObject.truncGE.obj a).obj X) := by
  induction a with
  | bot => simp
  | coe a =>
    simp only [abstractSpectralObject_truncGE, eTruncGE_obj_mk, Functor.id_obj,
      AbstractSpectralObject.truncGEÏ€_mk]
    exact t.from_truncGE_obj_ext ((t.truncGEÏ€ a).naturality ((t.truncGEÏ€ a).app X)).symm
  | top => exact IsZero.eq_of_src (by simp) _ _

omit [IsTriangulated C] in
lemma truncLTÎ¹_compatibility (a : EInt) (X : C) :
    (t.abstractSpectralObject.truncLT.obj a).map ((t.abstractSpectralObject.truncLTÎ¹ a).app X) =
      (t.abstractSpectralObject.truncLTÎ¹ a).app
        ((t.abstractSpectralObject.truncLT.obj a).obj X) := by
  induction a with
  | bot => exact IsZero.eq_of_src (by simp) _ _
  | coe a =>
    simp only [abstractSpectralObject_truncLT, eTruncLT_obj_mk, Functor.id_obj,
      AbstractSpectralObject.truncLEÎ¹_mk]
    exact t.to_truncLT_obj_ext ((t.truncLTÎ¹ a).naturality ((t.truncLTÎ¹ a).app X))
  | top => simp

lemma isIso_truncLTÎ¹_app_truncGELT_obj (a b : EInt) (h : a â‰¤ b) (X : C) :
    IsIso ((t.abstractSpectralObject.truncLTÎ¹ b).app
      ((t.eTruncLT.obj b â‹™ t.eTruncGE.obj a).obj X)) := by
  induction b with
  | bot =>
    refine âŸ¨0, IsZero.eq_of_src (by simp) _ _, IsZero.eq_of_src ?_ _ _âŸ©
    dsimp
    exact IsZero.of_iso (isZero_zero _)
        (Functor.mapIso _ (IsZero.isoZero (Functor.zero_obj _)) â‰ªâ‰«
          (t.eTruncGE.obj a).mapZeroObject)
  | coe b =>
    simp only [abstractSpectralObject_truncLT, eTruncLT_obj_mk, Functor.comp_obj,
      Functor.id_obj, AbstractSpectralObject.truncLEÎ¹_mk]
    rw [â† t.isLE_iff_isIso_truncLTÎ¹_app (b-1) b (by lia)]
    induction a with
    | bot => dsimp; infer_instance
    | coe a => dsimp; infer_instance
    | top => exact t.isLE_of_isZero (by simp) _
  | top =>
    simp only [abstractSpectralObject_truncLT, eTruncLT_obj_top, Functor.comp_obj,
      Functor.id_obj, AbstractSpectralObject.truncLTÎ¹_top_app]
    infer_instance

instance (D : Arrow EInt) (X : C) :
    IsIso ((t.abstractSpectralObject.truncLTGELTSelfToTruncGELT.app D).app X) :=
  t.isIso_truncLTÎ¹_app_truncGELT_obj D.left D.right (leOfHom D.hom) X

instance (D : Arrow EInt) : IsIso (t.abstractSpectralObject.truncLTGELTSelfToTruncGELT.app D) :=
  NatIso.isIso_of_isIso_app _

instance : IsIso (t.abstractSpectralObject.truncLTGELTSelfToTruncGELT) :=
  NatIso.isIso_of_isIso_app _

instance (a b : â„¤) (X : C) : t.IsLE ((t.truncGELT a b).obj X) (b-1) := by
  dsimp [truncGELT]
  infer_instance

noncomputable def natTransTruncGELTTruncLTGE (a b : â„¤) :
    t.truncGELT a b âŸ¶ t.truncLTGE a b where
  app X := t.liftTruncLT (t.descTruncGE
    ((t.truncLTÎ¹ b).app X â‰« (t.truncGEÏ€ a).app X) a) (b-1) b (by lia)
  naturality X Y f := by
    dsimp [truncGELT, truncLTGE]
    apply t.to_truncLT_obj_ext
    dsimp
    apply t.from_truncGE_obj_ext
    simp only [Functor.id_obj, assoc, liftTruncLT_Î¹, NatTrans.naturality,
      Functor.id_map, liftTruncLT_Î¹_assoc, Ï€_descTruncGE_assoc,
      â† NatTrans.naturality_assoc, Ï€_descTruncGE]
    rw [â† NatTrans.naturality, NatTrans.naturality_assoc]

@[reassoc (attr := simp)]
lemma natTransTruncGELETruncLEGE_app_pentagon (a b : â„¤) (X : C) :
    (t.truncGEÏ€ a).app _ â‰« (t.natTransTruncGELTTruncLTGE a b).app X â‰« (t.truncLTÎ¹ b).app _ =
      (t.truncLTÎ¹ b).app X â‰« (t.truncGEÏ€ a).app X := by simp [natTransTruncGELTTruncLTGE]

lemma natTransTruncGELETruncLEGE_app_pentagon_uniqueness (a b : â„¤) (X : C)
    (Ï† : (t.truncGELT a b).obj X âŸ¶ (t.truncLTGE a b).obj X)
    (hÏ† : (t.truncGEÏ€ a).app _ â‰« Ï† â‰« (t.truncLTÎ¹ b).app _ =
      (t.truncLTÎ¹ b).app X â‰« (t.truncGEÏ€ a).app X) :
    Ï† = (t.natTransTruncGELTTruncLTGE a b).app X := by
  apply t.to_truncLT_obj_ext
  dsimp
  apply t.from_truncGE_obj_ext
  rw [hÏ†, natTransTruncGELETruncLEGE_app_pentagon]

noncomputable def truncGELTÎ´LT (a b : â„¤) :
    t.truncGELT a b âŸ¶ t.truncLT a â‹™ shiftFunctor C (1 : â„¤) :=
  Functor.whiskerLeft (t.truncLT b) (t.truncGEÎ´LT a) â‰«
    Functor.whiskerRight (t.truncLTÎ¹ b) (t.truncLT a â‹™ shiftFunctor C (1 : â„¤))

@[simps!]
noncomputable def triangleLTLTGELT (a b : â„¤) (h : a â‰¤ b) : C â¥¤ Triangle C :=
  Triangle.functorMk (t.natTransTruncLTOfLE a b h)
    (Functor.whiskerLeft (t.truncLT b) (t.truncGEÏ€ a)) (t.truncGELTÎ´LT a b)

lemma triangleLTLTGELT_distinguished (a b : â„¤) (h : a â‰¤ b) (X : C) :
    (t.triangleLTLTGELT a b h).obj X âˆˆ distTriang C := by
  have := t.isIso_truncLT_map_truncLTÎ¹_app a b h X
  refine isomorphic_distinguished _ (t.triangleLTGE_distinguished a ((t.truncLT b).obj X)) _ ?_
  refine Triangle.isoMk _ _ ((asIso ((t.truncLT a).map ((t.truncLTÎ¹ b).app X))).symm)
    (Iso.refl _) (Iso.refl _) ?_ ?_ ?_
  Â· dsimp
    simp only [comp_id, IsIso.eq_inv_comp]
    apply t.to_truncLT_obj_ext
    simp only [Functor.id_obj, NatTrans.naturality, assoc, Functor.id_map,
      natTransTruncLTOfLE_Î¹_app_assoc]
  Â· dsimp
    simp only [comp_id, id_comp]
  Â· dsimp [truncGELTÎ´LT]
    simp only [Functor.map_inv, assoc, IsIso.hom_inv_id, comp_id, id_comp]

instance (a b : â„¤) (X : C) : IsIso ((t.natTransTruncGELTTruncLTGE a b).app X) := by
  by_cases h : a â‰¤ b
  Â· let uâ‚â‚‚ := (t.natTransTruncLTOfLE a b h).app X
    let uâ‚‚â‚ƒ : (t.truncLT b).obj X âŸ¶ X := (t.truncLTÎ¹ b).app X
    let uâ‚â‚ƒ : _ âŸ¶ X := (t.truncLTÎ¹ a).app X
    have eq : uâ‚â‚‚ â‰« uâ‚‚â‚ƒ = uâ‚â‚ƒ := by simp [uâ‚â‚‚, uâ‚‚â‚ƒ, uâ‚â‚ƒ]
    have H := someOctahedron eq (t.triangleLTLTGELT_distinguished a b h X)
      (t.triangleLTGE_distinguished b X) (t.triangleLTGE_distinguished a X)
    let mâ‚ : (t.truncGELT a b).obj X âŸ¶  _ := H.mâ‚
    have := t.isIsoâ‚_truncLT_map_of_isGE _ H.mem b (by dsimp; infer_instance)
    dsimp at this
    have eq' : t.liftTruncLT mâ‚ (b-1) b (by lia) =
        (t.natTransTruncGELTTruncLTGE a b).app X := by
      apply t.to_truncLT_obj_ext
      dsimp
      apply t.from_truncGE_obj_ext
      simp_rw [natTransTruncGELETruncLEGE_app_pentagon, liftTruncLT_Î¹]
      exact H.commâ‚
    rw [â† eq']
    have fac : (t.truncLTÎ¹ b).app ((t.truncGE a).obj ((t.truncLT b).obj X)) â‰«
        t.liftTruncLT mâ‚ (b-1) b (by lia) = (t.truncLT b).map mâ‚ :=
      t.to_truncLT_obj_ext (by simp [truncGELT])
    have : IsIso ((t.truncLTÎ¹ b).app ((t.truncGE a).obj ((t.truncLT b).obj X))) := by
      rw [â† t.isLE_iff_isIso_truncLTÎ¹_app (b-1) b (by lia)]
      infer_instance
    exact IsIso.of_isIso_fac_left fac
  Â· refine âŸ¨0, ?_, ?_âŸ©
    all_goals
      apply IsZero.eq_of_src
      exact t.isZero _ (b-1) a (by lia)

instance (a b : â„¤) : IsIso (t.natTransTruncGELTTruncLTGE a b) :=
  NatIso.isIso_of_isIso_app _

noncomputable def truncGELTIsoLTGE (a b : â„¤) : t.truncGELT a b â‰… t.truncLTGE a b :=
  asIso (t.natTransTruncGELTTruncLTGE a b)

noncomputable def truncGELEIsoLEGE (a b : â„¤) : t.truncGELE a b â‰… t.truncLEGE a b :=
  t.truncGELTIsoLTGE a (b + 1)

instance (a b : â„¤) (X : C) :
  IsIso ((t.truncLTÎ¹ b).app ((t.truncGE a).obj ((t.truncLT b).obj X))) := by
    rw [â† t.isLE_iff_isIso_truncLTÎ¹_app (b-1) b (by lia)]
    infer_instance

lemma truncLT_map_truncGE_map_truncLTÎ¹_app_fac (a b : â„¤) (X : C) :
    (t.truncLT b).map ((t.truncGE a).map ((t.truncLTÎ¹ b).app X)) =
      (t.truncLTÎ¹ b).app ((t.truncGE a).obj ((t.truncLT b).obj X)) â‰«
        (t.natTransTruncGELTTruncLTGE a b).app X := by
  rw [â† cancel_epi (inv ((t.truncLTÎ¹ b).app ((t.truncGE a).obj ((t.truncLT b).obj X)))),
    IsIso.inv_hom_id_assoc]
  apply t.natTransTruncGELETruncLEGE_app_pentagon_uniqueness
  simp only [Functor.id_obj, assoc, NatTrans.naturality, Functor.id_map, IsIso.inv_hom_id_assoc]
  exact ((t.truncGEÏ€ a).naturality ((t.truncLTÎ¹ b).app X)).symm

lemma isIso_truncLT_map_truncGE_map_truncLTÎ¹_app (a b : â„¤) (X : C) :
    IsIso ((t.truncLT b).map ((t.truncGE a).map ((t.truncLTÎ¹ b).app X))) := by
  rw [t.truncLT_map_truncGE_map_truncLTÎ¹_app_fac a b X]
  infer_instance

instance (D : Arrow EInt) (X : C) :
    IsIso ((t.abstractSpectralObject.truncLTGELTSelfToTruncLTGE.app D).app X) := by
  obtain âŸ¨a, b, f, rflâŸ© := Arrow.mk_surjective D
  have h : a â‰¤ b := leOfHom f
  induction b with
  | bot =>
    obtain rfl : a = âŠ¥ := by simpa using h
    exact âŸ¨0, IsZero.eq_of_src (Functor.zero_obj _) _ _,
      IsZero.eq_of_src (Functor.zero_obj _) _ _âŸ©
  | coe b =>
    dsimp [SpectralObject.AbstractSpectralObject.truncLTGELTSelfToTruncLTGE,
      SpectralObject.AbstractSpectralObject.truncLTGE]
    induction a with
    | bot =>
      simp only [AbstractSpectralObject.truncLEÎ¹_mk]
      exact t.isIso_truncLT_map_truncLTÎ¹_app b b (by rfl) X
    | coe a =>
      simp only [EInt.coe_le_coe_iff] at h
      simp only [eTruncGE_obj_mk, AbstractSpectralObject.truncLEÎ¹_mk]
      exact t.isIso_truncLT_map_truncGE_map_truncLTÎ¹_app a b X
    | top =>
      refine âŸ¨0, IsZero.eq_of_src ?_ _ _, IsZero.eq_of_src ?_ _ _âŸ©
      all_goals
        exact (isZero_zero _).of_iso
          ((t.truncLT b).mapIso ((Functor.zero_obj _).isoZero) â‰ªâ‰«
            (t.truncLT b).mapZeroObject)
  | top =>
  Â· dsimp [SpectralObject.AbstractSpectralObject.truncLTGELTSelfToTruncLTGE]
    simp only [AbstractSpectralObject.truncLTÎ¹_top_app, Functor.map_id]
    infer_instance

instance (D : Arrow EInt) : IsIso (t.abstractSpectralObject.truncLTGELTSelfToTruncLTGE.app D) :=
  NatIso.isIso_of_isIso_app _

instance : IsIso (t.abstractSpectralObject.truncLTGELTSelfToTruncLTGE) :=
  NatIso.isIso_of_isIso_app _

instance : t.abstractSpectralObject.IsCompatible where
  distinguished := AbstractSpectralObject.distinguished t
  truncGEÏ€_compatibility' := t.truncGEÏ€_compatibility
  truncLTÎ¹_compatibility' := t.truncLTÎ¹_compatibility

@[simps!]
noncomputable def spectralObject (X : C) : SpectralObject C EInt :=
  t.abstractSpectralObject.spectralObject X

end TStructure

end Triangulated

end CategoryTheory
