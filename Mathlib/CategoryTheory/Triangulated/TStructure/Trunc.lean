/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Triangulated.TStructure.ETrunc
public import Mathlib.CategoryTheory.Triangulated.TStructure.Induced
public import Mathlib.CategoryTheory.Triangulated.TStructure.AbstractSpectralObject

/-!
# Truncations for a t-structure

-/

@[expose] public section

namespace CategoryTheory

open Category Limits Pretriangulated ZeroObject Preadditive

variable {C : Type*} [Category* C] [Preadditive C] [HasZeroObject C] [HasShift C ‚Ñ§]
  [‚àÄ (n : ‚Ñ§), (shiftFunctor C n).Additive] [Pretriangulated C]

namespace Triangulated

namespace TStructure

variable (t : TStructure C)

@[simps]
noncomputable def abstractSpectralObject : SpectralObject.AbstractSpectralObject C EInt where
  truncLT := t.eTruncLT
  truncGE := t.eTruncGE
  truncLTObjTopIso' := Iso.refl _
  truncGEObjBotIso' := Iso.refl _
  truncGEŒ¥LT := t.eTruncGEŒ¥LTt

namespace AbstractSpectralObject

open SpectralObject

@[simp]
lemma truncGELT_eq (g : Arrow EInt) :
  (abstractSpectralObject t).truncGELT.obj g =
    t.eTruncLT.obj g.right ‚ãô t.eTruncGE.obj g.left := rfl

lemma isZero_truncGE_obj_top_obj (X : C) :
    IsZero ((t.abstractSpectralObject.truncGE.obj ‚ä§).obj X) :=
  IsZero.obj (isZero_zero _) _

lemma isZero_truncLT_obj_bot_obj (X : C) :
    IsZero ((t.abstractSpectralObject.truncLT.obj ‚ä•).obj X) :=
  IsZero.obj (isZero_zero _) _

@[simp]
lemma truncLEŒπ_mk (n : ‚Ñ§) :
    t.abstractSpectralObject.truncLTŒπ (EInt.mk n) = t.truncLTŒπ n :=
  comp_id _

@[simp]
lemma truncGEœÄ_mk (n : ‚Ñ§) :
    t.abstractSpectralObject.truncGEœÄ (EInt.mk n) = t.truncGEœÄ n :=
  id_comp _

@[simp]
lemma truncGEŒ¥LT_mk (n : ‚Ñ§) :
    t.abstractSpectralObject.truncGEŒ¥LT.app (EInt.mk n) =
      t.truncGEŒ¥LT n := rfl

noncomputable def triangleLTGEIso (n : ‚Ñ§) (X : C) :
    (t.abstractSpectralObject.triangleLTGE.obj (EInt.mk n)).obj X ‚âÖ
      (t.triangleLTGE n).obj X :=
  Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) (Iso.refl _)

@[simp]
lemma truncLTObjTopIso : t.abstractSpectralObject.truncLTObjTopIso = Iso.refl _ := rfl

@[simp]
lemma truncGEObjBotIso : t.abstractSpectralObject.truncGEObjBotIso = Iso.refl _ := rfl

@[simp]
lemma truncLTŒπ_top_app (X : C) :
    (t.abstractSpectralObject.truncLTŒπ ‚ä§).app X = ùüô X := by
  dsimp [AbstractSpectralObject.truncLTŒπ]
  erw [Functor.map_id]
  simp only [eTruncLT_obj_top, NatTrans.id_app, Functor.id_obj, comp_id]

@[simp]
lemma truncGEœÄ_bot_app (X : C) :
    (t.abstractSpectralObject.truncGEœÄ ‚ä•).app X = ùüô X := by
  dsimp [AbstractSpectralObject.truncGEœÄ]
  erw [Functor.map_id]
  simp only [eTruncGE_obj_bot, NatTrans.id_app, Functor.id_obj, comp_id]

noncomputable def triangleLTGETopIso (X : C) :
  (t.abstractSpectralObject.triangleLTGE.obj ‚ä§).obj X ‚âÖ
    Pretriangulated.contractibleTriangle X := by
  refine Triangle.isoMk _ _ (((abstractSpectralObject t).truncLTObjTopIso).app X)
    (Iso.refl _) (isZero_truncLT_obj_bot_obj t X).isoZero ?_ ?_ ?_
  ¬∑ dsimp
    rw [truncLTŒπ_top_app]
  ¬∑ exact IsZero.eq_of_tgt (isZero_zero _) _ _
  ¬∑ refine IsZero.eq_of_src ?_ _ _
    exact IsZero.obj (isZero_zero _) _

noncomputable def triangleLTGEBotIso (X : C) :
  (t.abstractSpectralObject.triangleLTGE.obj ‚ä•).obj X ‚âÖ
    (Pretriangulated.contractibleTriangle X).invRotate := by
  refine Triangle.isoMk _ _ ((isZero_truncLT_obj_bot_obj t X).isoZero ‚â™‚â´
    (shiftFunctor C (-1 : ‚Ñ§)).mapZeroObject.symm)
    (((abstractSpectralObject t).truncLTObjTopIso).app X) (Iso.refl _) ?_ ?_ ?_
  ¬∑ apply IsZero.eq_of_src
    apply isZero_truncLT_obj_bot_obj
  ¬∑ dsimp
    rw [truncGEœÄ_bot_app]
  ¬∑ apply IsZero.eq_of_tgt _
    dsimp
    rw [IsZero.iff_id_eq_zero, ‚Üê Functor.map_id, ‚Üê Functor.map_id, id_zero,
      Functor.map_zero, Functor.map_zero]

lemma distinguished (n : EInt) (X : C) :
  (t.abstractSpectralObject.triangleLTGE.obj n).obj X ‚àà distTriang C := by
  obtain (_|_|n) := n
  ¬∑ exact isomorphic_distinguished _
      (inv_rot_of_distTriang _ (contractible_distinguished X)) _
      (triangleLTGEBotIso t X)
  ¬∑ exact isomorphic_distinguished _ (contractible_distinguished X) _
      (triangleLTGETopIso t X)
  ¬∑ exact isomorphic_distinguished _ (t.triangleLTGE_distinguished n X) _
      (triangleLTGEIso t n X)

end AbstractSpectralObject


/- Now, we need the octahedron axiom -/

variable [IsTriangulated C]

lemma isIso‚ÇÅ_truncLE_map_of_isGE (T : Triangle C) (hT : T ‚àà distTriang C)
    (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (h : n‚ÇÄ + 1 = n‚ÇÅ) (h‚ÇÉ : t.IsGE T.obj‚ÇÉ n‚ÇÅ) :
    IsIso ((t.truncLE n‚ÇÄ).map T.mor‚ÇÅ) := by
  rw [isIso_truncLEmap_iff _ _ _ _ h]
  obtain ‚ü®Z, g, k, mem‚ü© := distinguished_cocone_triangle ((t.truncLEŒπ n‚ÇÄ).app T.obj‚ÇÅ ‚â´ T.mor‚ÇÅ)
  refine ‚ü®_, _, _, mem, ?_‚ü©
  have H := someOctahedron rfl (t.triangleLEGE_distinguished n‚ÇÄ n‚ÇÅ h T.obj‚ÇÅ) hT mem
  exact t.isGE‚ÇÇ _ H.mem n‚ÇÅ (by dsimp; infer_instance) (by dsimp; infer_instance)

lemma isIso‚ÇÅ_truncLT_map_of_isGE (T : Triangle C) (hT : T ‚àà distTriang C)
    (n : ‚Ñ§) (h‚ÇÉ : t.IsGE T.obj‚ÇÉ n) : IsIso ((t.truncLT n).map T.mor‚ÇÅ) := by
  rw [‚Üê NatIso.isIso_map_iff (t.truncLEIsoTruncLT (n-1) n (by lia))]
  exact t.isIso‚ÇÅ_truncLE_map_of_isGE T hT (n-1) n (by lia) h‚ÇÉ

lemma isIso‚ÇÇ_truncGE_map_of_LE (T : Triangle C) (hT : T ‚àà distTriang C)
    (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (h : n‚ÇÄ + 1 = n‚ÇÅ) (h‚ÇÅ : t.IsLE T.obj‚ÇÅ n‚ÇÄ) :
    IsIso ((t.truncGE n‚ÇÅ).map T.mor‚ÇÇ) := by
  rw [isIso_truncGEmap_iff _ _ _ _ h]
  obtain ‚ü®X, f, k, mem‚ü© := distinguished_cocone_triangle‚ÇÅ (T.mor‚ÇÇ ‚â´ (t.truncGEœÄ n‚ÇÅ).app T.obj‚ÇÉ)
  refine ‚ü®_, _, _, mem, ?_‚ü©
  have H := someOctahedron rfl (rot_of_distTriang _ hT)
    (rot_of_distTriang _ (t.triangleLEGE_distinguished n‚ÇÄ n‚ÇÅ h T.obj‚ÇÉ))
    (rot_of_distTriang _ mem)
  have : t.IsLE (X‚ü¶(1 : ‚Ñ§)‚üß) (n‚ÇÄ-1) := t.isLE‚ÇÇ _ H.mem (n‚ÇÄ-1)
    (t.isLE_shift T.obj‚ÇÅ n‚ÇÄ 1 (n‚ÇÄ-1) (by lia))
    (t.isLE_shift ((t.truncLE n‚ÇÄ).obj T.obj‚ÇÉ) n‚ÇÄ 1 (n‚ÇÄ-1) (by lia))
  exact t.isLE_of_shift X n‚ÇÄ 1 (n‚ÇÄ-1) (by lia)

instance (X : C) (a b : ‚Ñ§) [t.IsGE X a] : t.IsGE ((t.truncLE b).obj X) a := by
  rw [t.isGE_iff_isZero_truncLE_obj (a-1) a (by lia)]
  have := t.isIso‚ÇÅ_truncLE_map_of_isGE _ ((t.triangleLEGE_distinguished b (b+1) rfl X))
    (a-1) a (by lia) (by dsimp; infer_instance)
  dsimp at this
  exact IsZero.of_iso (t.isZero_truncLE_obj_of_isGE (a-1) a (by lia) X)
    (asIso ((t.truncLE (a - 1)).map ((t.truncLEŒπ b).app X)))

instance (X : C) (a b : ‚Ñ§) [t.IsGE X a] : t.IsGE ((t.truncLT b).obj X) a :=
  t.isGE_of_iso ((t.truncLEIsoTruncLT (b-1) b (by lia)).app X) a

instance (X : C) (a b : ‚Ñ§) [t.IsLE X b] : t.IsLE ((t.truncGE a).obj X) b := by
  rw [t.isLE_iff_isZero_truncGE_obj b (b+1) rfl]
  have := t.isIso‚ÇÇ_truncGE_map_of_LE _ ((t.triangleLEGE_distinguished (a-1) a (by lia) X))
    b (b+1) rfl (by dsimp; infer_instance)
  dsimp at this
  exact IsZero.of_iso (t.isZero_truncGE_obj_of_isLE b (b+1) rfl X)
    (asIso ((t.truncGE (b+1)).map ((t.truncGEœÄ  a).app X))).symm

instance (X : C) (a b : ‚Ñ§) : t.IsGE ((t.truncGELE a b).obj X) a := by
  dsimp [truncGELE]
  infer_instance

instance (X : C) (a b : ‚Ñ§) : t.IsLE ((t.truncGELE a b).obj X) b := by
  dsimp [truncGELE]
  infer_instance

instance (X : C) (a b : ‚Ñ§) : t.IsGE ((t.truncGELT a b).obj X) a := by
  dsimp [truncGELT]
  infer_instance

instance (X : C) (a b : ‚Ñ§) : t.IsLE ((t.truncGELT a b).obj X) (b-1) := by
  dsimp [truncGELT]
  infer_instance

instance (X : C) (a b : ‚Ñ§) : t.IsGE ((t.truncLTGE a b).obj X) a := by
  dsimp [truncLTGE]
  infer_instance

instance (X : C) (a b : ‚Ñ§) : t.IsLE ((t.truncLTGE a b).obj X) (b-1) := by
  dsimp [truncLTGE]
  infer_instance

instance (a b : ‚Ñ§) : (t.truncGELT a b).Additive := by
  dsimp only [truncGELT]
  infer_instance

instance (a b : ‚Ñ§) : (t.truncGELE a b).Additive := by
  dsimp only [truncGELE]
  infer_instance

instance (i : EInt) : (t.eTruncGE.obj i).Additive := by
  induction i <;> constructor <;> cat_disch

instance (i : EInt) : (t.eTruncLT.obj i).Additive := by
  induction i <;> constructor <;> cat_disch

omit [IsTriangulated C] in
lemma isZero_eTruncLT_obj_obj (X : C) (n : ‚Ñ§) [t.IsGE X n] (j : EInt) (hj : j ‚â§ EInt.mk n) :
    IsZero ((t.eTruncLT.obj j).obj X) := by
  induction j with
  | bot => simp
  | coe j =>
    dsimp
    rw [‚Üê t.isGE_iff_isZero_truncLT_obj]
    exact t.isGE_of_GE  _ _ _ (by simpa using hj)
  | top => simp at hj

omit [IsTriangulated C] in
lemma isZero_eTruncGE_obj_obj (X : C) (n : ‚Ñ§) [t.IsLE X n] (j : EInt) (hj : EInt.mk n < j) :
    IsZero ((t.eTruncGE.obj j).obj X) := by
  induction j with
  | bot => simp at hj
  | coe j =>
    simp only [EInt.coe_lt_coe_iff] at hj
    dsimp
    rw [‚Üê t.isLE_iff_isZero_truncGE_obj (j - 1) j (by simp)]
    exact t.isLE_of_LE X n (j - 1) (by lia)
  | top => simp

omit [IsTriangulated C] in
lemma eTruncGE_obj_obj_isGE (n : ‚Ñ§) (i : EInt) (h : EInt.mk n ‚â§ i) (X : C) :
    t.IsGE ((t.eTruncGE.obj i).obj X) n := by
  induction i with
  | bot => simp at h
  | coe i =>
    dsimp
    exact t.isGE_of_GE  _ _ _ (by simpa using h)
  | top => exact t.isGE_of_isZero (Functor.zero_obj _) _

omit [IsTriangulated C] in
lemma eTruncLT_obj_obj_isLE (n : ‚Ñ§) (i : EInt) (h : i ‚â§ EInt.mk (n + 1)) (X : C) :
    t.IsLE (((t.eTruncLT.obj i)).obj X) n := by
  induction i with
  | bot => exact t.isLE_of_isZero (by simp) _
  | coe i =>
    simp only [EInt.coe_le_coe_iff] at h
    dsimp
    exact t.isLE_of_LE _ (i - 1) n (by lia)
  | top => simp at h

lemma isIso_truncGE_map_truncGEœÄ_app (a b : ‚Ñ§) (h : a ‚â§ b) (X : C) :
    IsIso ((t.truncGE b).map ((t.truncGEœÄ a).app X)) :=
  t.isIso‚ÇÇ_truncGE_map_of_LE _
    (t.triangleLEGE_distinguished (a-1) a (by lia) X) (b-1) b (by lia)
      (t.isLE_of_LE ((t.truncLE (a-1)).obj X) (a-1) (b-1) (by lia))

lemma isIso_truncLT_map_truncLTŒπ_app (a b : ‚Ñ§) (h : a ‚â§ b) (X : C) :
    IsIso ((t.truncLT a).map ((t.truncLTŒπ b).app X)) :=
  t.isIso‚ÇÅ_truncLT_map_of_isGE _ (t.triangleLTGE_distinguished b X) a
    (t.isGE_of_GE ((t.truncGE b).obj X) a b (by lia))

lemma isIso_truncLE_map_truncLEŒπ_app (a b : ‚Ñ§) (h : a ‚â§ b) (X : C) :
    IsIso ((t.truncLE a).map ((t.truncLEŒπ b).app X)) := by
  apply isIso_truncLT_map_truncLTŒπ_app
  lia

instance (X : C) (n : ‚Ñ§) : IsIso ((t.truncLE n).map ((t.truncLEŒπ n).app X)) :=
  t.isIso_truncLE_map_truncLEŒπ_app _ _ (by rfl) _

instance (X : C) (n : ‚Ñ§) : IsIso ((t.truncGE n).map ((t.truncGEœÄ n).app X)) :=
  t.isIso_truncGE_map_truncGEœÄ_app _ _ (by rfl) _

lemma isIso_eTruncGE_obj_map_truncGEœÄ_app (a b : EInt) (h : a ‚â§ b) (X : C) :
    IsIso ((t.eTruncGE.obj b).map ((t.abstractSpectralObject.truncGEœÄ a).app X)) := by
  induction b with
  | bot =>
    obtain rfl : a = ‚ä• := by simpa using h
    dsimp
    simp only [AbstractSpectralObject.truncGEœÄ_bot_app]
    infer_instance
  | coe b =>
    induction a with
    | bot => dsimp; infer_instance
    | coe a =>
      simp only [EInt.coe_le_coe_iff] at h
      simp only [AbstractSpectralObject.truncGEœÄ_mk]
      exact t.isIso_truncGE_map_truncGEœÄ_app a b h X
    | top => simp at h
  | top => exact ‚ü®0, IsZero.eq_of_src (by simp) _ _, IsZero.eq_of_src (by simp) _ _‚ü©

lemma isIso_eTruncLT_obj_map_truncLTœÄ_app (a b : EInt) (h : a ‚â§ b) (X : C) :
    IsIso ((t.eTruncLT.obj a).map ((t.abstractSpectralObject.truncLTŒπ b).app X)) := by
  induction a with
  | bot => exact ‚ü®0, IsZero.eq_of_src (by simp) _ _, IsZero.eq_of_src (by simp) _ _‚ü©
  | coe a =>
    induction b with
    | bot => simp at h
    | coe b =>
      simp only [EInt.coe_le_coe_iff] at h
      simp only [AbstractSpectralObject.truncLEŒπ_mk]
      exact t.isIso_truncLT_map_truncLTŒπ_app a b h X
    | top => dsimp; infer_instance
  | top =>
    obtain rfl : b = ‚ä§ := by simpa using h
    dsimp [AbstractSpectralObject.truncLTŒπ_top_app]
    infer_instance

instance (D : Arrow EInt) (X : C) :
  IsIso ((t.abstractSpectralObject.truncGEToTruncGEGE.app D).app X) :=
    t.isIso_eTruncGE_obj_map_truncGEœÄ_app _ _ (leOfHom D.hom) X

instance (D : Arrow EInt) (X : C) :
  IsIso ((t.abstractSpectralObject.truncLTLTToTruncLT.app D).app X) :=
    t.isIso_eTruncLT_obj_map_truncLTœÄ_app _ _ (leOfHom D.hom) X

instance (D : Arrow EInt) : IsIso (t.abstractSpectralObject.truncGEToTruncGEGE.app D) :=
  NatIso.isIso_of_isIso_app _

instance (D : Arrow EInt) : IsIso (t.abstractSpectralObject.truncLTLTToTruncLT.app D) :=
  NatIso.isIso_of_isIso_app _

instance : IsIso (t.abstractSpectralObject.truncGEToTruncGEGE) := NatIso.isIso_of_isIso_app _

instance : IsIso (t.abstractSpectralObject.truncLTLTToTruncLT) := NatIso.isIso_of_isIso_app _

omit [IsTriangulated C] in
lemma truncGEœÄ_compatibility (a : EInt) (X : C) :
  (t.abstractSpectralObject.truncGE.obj a).map ((t.abstractSpectralObject.truncGEœÄ a).app X) =
    (t.abstractSpectralObject.truncGEœÄ a).app
      ((t.abstractSpectralObject.truncGE.obj a).obj X) := by
  induction a with
  | bot => simp
  | coe a =>
    simp only [abstractSpectralObject_truncGE, eTruncGE_obj_mk, Functor.id_obj,
      AbstractSpectralObject.truncGEœÄ_mk]
    exact t.from_truncGE_obj_ext ((t.truncGEœÄ a).naturality ((t.truncGEœÄ a).app X)).symm
  | top => exact IsZero.eq_of_src (by simp) _ _

omit [IsTriangulated C] in
lemma truncLTŒπ_compatibility (a : EInt) (X : C) :
    (t.abstractSpectralObject.truncLT.obj a).map ((t.abstractSpectralObject.truncLTŒπ a).app X) =
      (t.abstractSpectralObject.truncLTŒπ a).app
        ((t.abstractSpectralObject.truncLT.obj a).obj X) := by
  induction a with
  | bot => exact IsZero.eq_of_src (by simp) _ _
  | coe a =>
    simp only [abstractSpectralObject_truncLT, eTruncLT_obj_mk, Functor.id_obj,
      AbstractSpectralObject.truncLEŒπ_mk]
    exact t.to_truncLT_obj_ext ((t.truncLTŒπ a).naturality ((t.truncLTŒπ a).app X))
  | top => simp

lemma isIso_truncLTŒπ_app_truncGELT_obj (a b : EInt) (h : a ‚â§ b) (X : C) :
    IsIso ((t.abstractSpectralObject.truncLTŒπ b).app
      ((t.eTruncLT.obj b ‚ãô t.eTruncGE.obj a).obj X)) := by
  induction b with
  | bot =>
    refine ‚ü®0, IsZero.eq_of_src (by simp) _ _, IsZero.eq_of_src ?_ _ _‚ü©
    dsimp
    exact IsZero.of_iso (isZero_zero _)
        (Functor.mapIso _ (IsZero.isoZero (Functor.zero_obj _)) ‚â™‚â´
          (t.eTruncGE.obj a).mapZeroObject)
  | coe b =>
    simp only [abstractSpectralObject_truncLT, eTruncLT_obj_mk, Functor.comp_obj,
      Functor.id_obj, AbstractSpectralObject.truncLEŒπ_mk]
    rw [‚Üê t.isLE_iff_isIso_truncLTŒπ_app (b-1) b (by lia)]
    induction a with
    | bot => dsimp; infer_instance
    | coe a => dsimp; infer_instance
    | top => exact t.isLE_of_isZero (by simp) _
  | top =>
    simp only [abstractSpectralObject_truncLT, eTruncLT_obj_top, Functor.comp_obj,
      Functor.id_obj, AbstractSpectralObject.truncLTŒπ_top_app]
    infer_instance

instance (D : Arrow EInt) (X : C) :
    IsIso ((t.abstractSpectralObject.truncLTGELTSelfToTruncGELT.app D).app X) :=
  t.isIso_truncLTŒπ_app_truncGELT_obj D.left D.right (leOfHom D.hom) X

instance (D : Arrow EInt) : IsIso (t.abstractSpectralObject.truncLTGELTSelfToTruncGELT.app D) :=
  NatIso.isIso_of_isIso_app _

instance : IsIso (t.abstractSpectralObject.truncLTGELTSelfToTruncGELT) :=
  NatIso.isIso_of_isIso_app _

instance (a b : ‚Ñ§) (X : C) : t.IsLE ((t.truncGELT a b).obj X) (b-1) := by
  dsimp [truncGELT]
  infer_instance

noncomputable def natTransTruncGELTTruncLTGE (a b : ‚Ñ§) :
    t.truncGELT a b ‚ü∂ t.truncLTGE a b where
  app X := t.liftTruncLT (t.descTruncGE
    ((t.truncLTŒπ b).app X ‚â´ (t.truncGEœÄ a).app X) a) (b-1) b (by lia)
  naturality X Y f := by
    dsimp [truncGELT, truncLTGE]
    apply t.to_truncLT_obj_ext
    dsimp
    apply t.from_truncGE_obj_ext
    simp only [Functor.id_obj, assoc, liftTruncLT_Œπ, NatTrans.naturality,
      Functor.id_map, liftTruncLT_Œπ_assoc, œÄ_descTruncGE_assoc,
      ‚Üê NatTrans.naturality_assoc, œÄ_descTruncGE]
    rw [‚Üê NatTrans.naturality, NatTrans.naturality_assoc]

@[reassoc (attr := simp)]
lemma natTransTruncGELETruncLEGE_app_pentagon (a b : ‚Ñ§) (X : C) :
    (t.truncGEœÄ a).app _ ‚â´ (t.natTransTruncGELTTruncLTGE a b).app X ‚â´ (t.truncLTŒπ b).app _ =
      (t.truncLTŒπ b).app X ‚â´ (t.truncGEœÄ a).app X := by simp [natTransTruncGELTTruncLTGE]

lemma natTransTruncGELETruncLEGE_app_pentagon_uniqueness (a b : ‚Ñ§) (X : C)
    (œÜ : (t.truncGELT a b).obj X ‚ü∂ (t.truncLTGE a b).obj X)
    (hœÜ : (t.truncGEœÄ a).app _ ‚â´ œÜ ‚â´ (t.truncLTŒπ b).app _ =
      (t.truncLTŒπ b).app X ‚â´ (t.truncGEœÄ a).app X) :
    œÜ = (t.natTransTruncGELTTruncLTGE a b).app X := by
  apply t.to_truncLT_obj_ext
  dsimp
  apply t.from_truncGE_obj_ext
  rw [hœÜ, natTransTruncGELETruncLEGE_app_pentagon]

noncomputable def truncGELTŒ¥LT (a b : ‚Ñ§) :
    t.truncGELT a b ‚ü∂ t.truncLT a ‚ãô shiftFunctor C (1 : ‚Ñ§) :=
  Functor.whiskerLeft (t.truncLT b) (t.truncGEŒ¥LT a) ‚â´
    Functor.whiskerRight (t.truncLTŒπ b) (t.truncLT a ‚ãô shiftFunctor C (1 : ‚Ñ§))

@[simps!]
noncomputable def triangleLTLTGELT (a b : ‚Ñ§) (h : a ‚â§ b) : C ‚•§ Triangle C :=
  Triangle.functorMk (t.natTransTruncLTOfLE a b h)
    (Functor.whiskerLeft (t.truncLT b) (t.truncGEœÄ a)) (t.truncGELTŒ¥LT a b)

lemma triangleLTLTGELT_distinguished (a b : ‚Ñ§) (h : a ‚â§ b) (X : C) :
    (t.triangleLTLTGELT a b h).obj X ‚àà distTriang C := by
  have := t.isIso_truncLT_map_truncLTŒπ_app a b h X
  refine isomorphic_distinguished _ (t.triangleLTGE_distinguished a ((t.truncLT b).obj X)) _ ?_
  refine Triangle.isoMk _ _ ((asIso ((t.truncLT a).map ((t.truncLTŒπ b).app X))).symm)
    (Iso.refl _) (Iso.refl _) ?_ ?_ ?_
  ¬∑ dsimp
    simp only [comp_id, IsIso.eq_inv_comp]
    apply t.to_truncLT_obj_ext
    simp only [Functor.id_obj, NatTrans.naturality, assoc, Functor.id_map,
      natTransTruncLTOfLE_Œπ_app_assoc]
  ¬∑ dsimp
    simp only [comp_id, id_comp]
  ¬∑ dsimp [truncGELTŒ¥LT]
    simp only [Functor.map_inv, assoc, IsIso.hom_inv_id, comp_id, id_comp]

instance (a b : ‚Ñ§) (X : C) : IsIso ((t.natTransTruncGELTTruncLTGE a b).app X) := by
  by_cases h : a ‚â§ b
  ¬∑ let u‚ÇÅ‚ÇÇ := (t.natTransTruncLTOfLE a b h).app X
    let u‚ÇÇ‚ÇÉ : (t.truncLT b).obj X ‚ü∂ X := (t.truncLTŒπ b).app X
    let u‚ÇÅ‚ÇÉ : _ ‚ü∂ X := (t.truncLTŒπ a).app X
    have eq : u‚ÇÅ‚ÇÇ ‚â´ u‚ÇÇ‚ÇÉ = u‚ÇÅ‚ÇÉ := by simp [u‚ÇÅ‚ÇÇ, u‚ÇÇ‚ÇÉ, u‚ÇÅ‚ÇÉ]
    have H := someOctahedron eq (t.triangleLTLTGELT_distinguished a b h X)
      (t.triangleLTGE_distinguished b X) (t.triangleLTGE_distinguished a X)
    let m‚ÇÅ : (t.truncGELT a b).obj X ‚ü∂  _ := H.m‚ÇÅ
    have := t.isIso‚ÇÅ_truncLT_map_of_isGE _ H.mem b (by dsimp; infer_instance)
    dsimp at this
    have eq' : t.liftTruncLT m‚ÇÅ (b-1) b (by lia) =
        (t.natTransTruncGELTTruncLTGE a b).app X := by
      apply t.to_truncLT_obj_ext
      dsimp
      apply t.from_truncGE_obj_ext
      simp_rw [natTransTruncGELETruncLEGE_app_pentagon, liftTruncLT_Œπ]
      exact H.comm‚ÇÅ
    rw [‚Üê eq']
    have fac : (t.truncLTŒπ b).app ((t.truncGE a).obj ((t.truncLT b).obj X)) ‚â´
        t.liftTruncLT m‚ÇÅ (b-1) b (by lia) = (t.truncLT b).map m‚ÇÅ :=
      t.to_truncLT_obj_ext (by simp [truncGELT])
    have : IsIso ((t.truncLTŒπ b).app ((t.truncGE a).obj ((t.truncLT b).obj X))) := by
      rw [‚Üê t.isLE_iff_isIso_truncLTŒπ_app (b-1) b (by lia)]
      infer_instance
    exact IsIso.of_isIso_fac_left fac
  ¬∑ refine ‚ü®0, ?_, ?_‚ü©
    all_goals
      apply IsZero.eq_of_src
      exact t.isZero _ (b-1) a (by lia)

instance (a b : ‚Ñ§) : IsIso (t.natTransTruncGELTTruncLTGE a b) :=
  NatIso.isIso_of_isIso_app _

noncomputable def truncGELTIsoLTGE (a b : ‚Ñ§) : t.truncGELT a b ‚âÖ t.truncLTGE a b :=
  asIso (t.natTransTruncGELTTruncLTGE a b)

noncomputable def truncGELEIsoLEGE (a b : ‚Ñ§) : t.truncGELE a b ‚âÖ t.truncLEGE a b :=
  t.truncGELTIsoLTGE a (b + 1)

instance (a b : ‚Ñ§) (X : C) :
  IsIso ((t.truncLTŒπ b).app ((t.truncGE a).obj ((t.truncLT b).obj X))) := by
    rw [‚Üê t.isLE_iff_isIso_truncLTŒπ_app (b-1) b (by lia)]
    infer_instance

lemma truncLT_map_truncGE_map_truncLTŒπ_app_fac (a b : ‚Ñ§) (X : C) :
    (t.truncLT b).map ((t.truncGE a).map ((t.truncLTŒπ b).app X)) =
      (t.truncLTŒπ b).app ((t.truncGE a).obj ((t.truncLT b).obj X)) ‚â´
        (t.natTransTruncGELTTruncLTGE a b).app X := by
  rw [‚Üê cancel_epi (inv ((t.truncLTŒπ b).app ((t.truncGE a).obj ((t.truncLT b).obj X)))),
    IsIso.inv_hom_id_assoc]
  apply t.natTransTruncGELETruncLEGE_app_pentagon_uniqueness
  simp only [Functor.id_obj, assoc, NatTrans.naturality, Functor.id_map, IsIso.inv_hom_id_assoc]
  exact ((t.truncGEœÄ a).naturality ((t.truncLTŒπ b).app X)).symm

lemma isIso_truncLT_map_truncGE_map_truncLTŒπ_app (a b : ‚Ñ§) (X : C) :
    IsIso ((t.truncLT b).map ((t.truncGE a).map ((t.truncLTŒπ b).app X))) := by
  rw [t.truncLT_map_truncGE_map_truncLTŒπ_app_fac a b X]
  infer_instance

instance (D : Arrow EInt) (X : C) :
    IsIso ((t.abstractSpectralObject.truncLTGELTSelfToTruncLTGE.app D).app X) := by
  obtain ‚ü®a, b, f, rfl‚ü© := Arrow.mk_surjective D
  have h : a ‚â§ b := leOfHom f
  induction b with
  | bot =>
    obtain rfl : a = ‚ä• := by simpa using h
    exact ‚ü®0, IsZero.eq_of_src (Functor.zero_obj _) _ _,
      IsZero.eq_of_src (Functor.zero_obj _) _ _‚ü©
  | coe b =>
    dsimp [SpectralObject.AbstractSpectralObject.truncLTGELTSelfToTruncLTGE,
      SpectralObject.AbstractSpectralObject.truncLTGE]
    induction a with
    | bot =>
      simp only [AbstractSpectralObject.truncLEŒπ_mk]
      exact t.isIso_truncLT_map_truncLTŒπ_app b b (by rfl) X
    | coe a =>
      simp only [EInt.coe_le_coe_iff] at h
      simp only [eTruncGE_obj_mk, AbstractSpectralObject.truncLEŒπ_mk]
      exact t.isIso_truncLT_map_truncGE_map_truncLTŒπ_app a b X
    | top =>
      refine ‚ü®0, IsZero.eq_of_src ?_ _ _, IsZero.eq_of_src ?_ _ _‚ü©
      all_goals
        exact (isZero_zero _).of_iso
          ((t.truncLT b).mapIso ((Functor.zero_obj _).isoZero) ‚â™‚â´
            (t.truncLT b).mapZeroObject)
  | top =>
  ¬∑ dsimp [SpectralObject.AbstractSpectralObject.truncLTGELTSelfToTruncLTGE]
    simp only [AbstractSpectralObject.truncLTŒπ_top_app, Functor.map_id]
    infer_instance

instance (D : Arrow EInt) : IsIso (t.abstractSpectralObject.truncLTGELTSelfToTruncLTGE.app D) :=
  NatIso.isIso_of_isIso_app _

instance : IsIso (t.abstractSpectralObject.truncLTGELTSelfToTruncLTGE) :=
  NatIso.isIso_of_isIso_app _

instance : t.abstractSpectralObject.IsCompatible where
  distinguished := AbstractSpectralObject.distinguished t
  truncGEœÄ_compatibility' := t.truncGEœÄ_compatibility
  truncLTŒπ_compatibility' := t.truncLTŒπ_compatibility

@[simps!]
noncomputable def spectralObject (X : C) : SpectralObject C EInt :=
  t.abstractSpectralObject.spectralObject X

end TStructure

end Triangulated

namespace Triangulated

variable (t : TStructure C)

instance [IsTriangulated C] : t.plus.HasInducedTStructure t :=
  .mk' (by
    rintro X ‚ü®a, _‚ü© n
    exact ‚ü®‚ü®a, inferInstance‚ü©, ‚ü®a, inferInstance‚ü©‚ü©)

instance [IsTriangulated C] : t.minus.HasInducedTStructure t :=
  .mk' (by
    rintro X ‚ü®a, _‚ü© n
    exact ‚ü®‚ü®a, inferInstance‚ü©, ‚ü®a, inferInstance‚ü©‚ü©)

instance [IsTriangulated C] : t.bounded.HasInducedTStructure t := by
  dsimp [TStructure.bounded]
  infer_instance

namespace TStructure

instance [IsTriangulated C] (X : C) (n : ‚Ñ§) [t.IsGE X n] (i : EInt) :
    t.IsGE ((t.eTruncLT.obj i).obj X) n := by
  induction i with
  | bot => exact isGE_of_isZero _ (by simp) _
  | coe _ => dsimp; infer_instance
  | top => dsimp; infer_instance

instance [IsTriangulated C] (X : C) (n : ‚Ñ§) [t.IsLE X n] (i : EInt) :
    t.IsLE ((t.eTruncGE.obj i).obj X) n := by
  induction i with
  | bot => dsimp; infer_instance
  | coe _ => dsimp; infer_instance
  | top => exact isLE_of_isZero _ (by simp) _

end TStructure

end Triangulated

end CategoryTheory
