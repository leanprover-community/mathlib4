/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Triangulated.TStructure.TruncLTGE

/-!
# Truncations for a t-structure

Let `t` be a t-structure on a (pre)triangulated category `C`.
In this file, for any `n : ‚Ñ§`, we introduce the truncation functors
`t.truncLE n : C ‚•§ C` and `t.truncGT n : C ‚•§ C`, as variants of the functors
`t.truncLT n : C ‚•§ C` and `t.truncGE n : C ‚•§ C` introduced in the file
`Mathlib/CategoryTheory/Triangulated/TStucture/TruncLTGE.lean`.

-/

@[expose] public section

namespace CategoryTheory

open Limits Pretriangulated

variable {C : Type*} [Category* C] [Preadditive C] [HasZeroObject C] [HasShift C ‚Ñ§]
  [‚àÄ (n : ‚Ñ§), (shiftFunctor C n).Additive] [Pretriangulated C]

namespace Triangulated

namespace TStructure

variable (t : TStructure C)

/-- Given a t-structure `t` on a pretriangulated category `C` and `n : ‚Ñ§`, this
is the `‚â§ n`-truncation functor. See also the natural transformation `truncLEŒπ`. -/
noncomputable def truncLE (n : ‚Ñ§) : C ‚•§ C := t.truncLT (n + 1)

instance (n : ‚Ñ§) : (t.truncLE n).Additive := by
  dsimp only [truncLE]
  infer_instance

lemma isLE_truncLE_obj (X : C) (a b : ‚Ñ§) (hn : a ‚â§ b := by lia) :
    t.IsLE ((t.truncLE a).obj X) b :=
  t.isLE_truncLT_obj ..

instance (n : ‚Ñ§) (X : C) : t.IsLE ((t.truncLE n).obj X) n :=
  t.isLE_truncLE_obj ..

/-- Given a t-structure `t` on a pretriangulated category `C` and `n : ‚Ñ§`, this
is the `> n`-truncation functor. See also the natural transformation `truncGTœÄ`. -/
noncomputable def truncGT (n : ‚Ñ§) : C ‚•§ C := t.truncGE (n + 1)

instance (n : ‚Ñ§) : (t.truncGT n).Additive := by
  dsimp only [truncGT]
  infer_instance

lemma isGE_truncGT_obj (X : C) (a b : ‚Ñ§) (hn : b ‚â§ a + 1 := by lia) :
    t.IsGE ((t.truncGT a).obj X) b :=
  t.isGE_truncGE_obj ..

instance (n : ‚Ñ§) (X : C) : t.IsGE ((t.truncGT n).obj X) (n + 1) :=
  t.isGE_truncGT_obj ..

instance (n : ‚Ñ§) (X : C) : t.IsGE ((t.truncGT (n - 1)).obj X) n :=
  t.isGE_truncGT_obj ..

/-- The isomorphism `t.truncLE a ‚âÖ t.truncLT b` when `a + 1 = b`. -/
noncomputable def truncLEIsoTruncLT (a b : ‚Ñ§) (h : a + 1 = b) :
    t.truncLE a ‚âÖ t.truncLT b :=
  eqToIso (by rw [‚Üê h]; rfl)

/-- The isomorphism `t.truncGT a ‚âÖ t.truncGE b` when `a + 1 = b`. -/
noncomputable def truncGTIsoTruncGE (a b : ‚Ñ§) (h : a + 1 = b) :
    t.truncGT a ‚âÖ t.truncGE b :=
  eqToIso (by rw [‚Üê h]; rfl)

/-- The natural transformation `t.truncLE n ‚ü∂ ùü≠ C` when `t` is a t-structure
on a category `C` and `n : ‚Ñ§`. -/
noncomputable def truncLEŒπ (n : ‚Ñ§) : t.truncLE n ‚ü∂ ùü≠ C := t.truncLTŒπ (n + 1)

@[reassoc (attr := simp)]
lemma truncLEIsoTruncLT_hom_Œπ (a b : ‚Ñ§) (h : a + 1 = b) :
    (t.truncLEIsoTruncLT a b h).hom ‚â´ t.truncLTŒπ b = t.truncLEŒπ a := by
  subst h
  dsimp [truncLEIsoTruncLT, truncLEŒπ]
  rw [Category.id_comp]

@[reassoc (attr := simp)]
lemma truncLEIsoTruncLT_hom_Œπ_app (a b : ‚Ñ§) (h : a + 1 = b) (X : C) :
    (t.truncLEIsoTruncLT a b h).hom.app X ‚â´ (t.truncLTŒπ b).app X = (t.truncLEŒπ a).app X :=
  congr_app (t.truncLEIsoTruncLT_hom_Œπ a b h) X

@[reassoc (attr := simp)]
lemma truncLEIsoTruncLT_inv_Œπ (a b : ‚Ñ§) (h : a + 1 = b) :
    (t.truncLEIsoTruncLT a b h).inv ‚â´ t.truncLEŒπ a = t.truncLTŒπ b := by
  subst h
  dsimp [truncLEIsoTruncLT, truncLEŒπ, truncLE]
  rw [Category.id_comp]

@[reassoc (attr := simp)]
lemma truncLEIsoTruncLT_inv_Œπ_app (a b : ‚Ñ§) (h : a + 1 = b) (X : C) :
    (t.truncLEIsoTruncLT a b h).inv.app X ‚â´ (t.truncLEŒπ a).app X = (t.truncLTŒπ b).app X :=
  congr_app (t.truncLEIsoTruncLT_inv_Œπ a b h) X

/-- The natural transformation `t.truncLE a ‚ü∂ t.truncLE b` when `a ‚â§ b`. -/
noncomputable def natTransTruncLEOfLE (a b : ‚Ñ§) (h : a ‚â§ b) :
    t.truncLE a ‚ü∂ t.truncLE b :=
  t.natTransTruncLTOfLE (a+1) (b+1) (by lia)

@[reassoc (attr := simp)]
lemma natTransTruncLEOfLE_Œπ_app (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (h : n‚ÇÄ ‚â§ n‚ÇÅ) (X : C) :
    (t.natTransTruncLEOfLE n‚ÇÄ n‚ÇÅ h).app X ‚â´ (t.truncLEŒπ n‚ÇÅ).app X =
      (t.truncLEŒπ n‚ÇÄ).app X :=
  t.natTransTruncLTOfLE_Œπ_app _ _ _ _

@[reassoc (attr := simp)]
lemma natTransTruncLEOfLE_Œπ (a b : ‚Ñ§) (h : a ‚â§ b) :
    t.natTransTruncLEOfLE a b h ‚â´ t.truncLEŒπ b = t.truncLEŒπ a := by cat_disch

@[simp]
lemma natTransTruncLEOfLE_refl (a : ‚Ñ§) :
    t.natTransTruncLEOfLE a a (by rfl) = ùüô _ :=
  t.natTransTruncLTOfLE_refl _

@[simp]
lemma natTransTruncLEOfLE_trans (a b c : ‚Ñ§) (hab : a ‚â§ b) (hbc : b ‚â§ c) :
    t.natTransTruncLEOfLE a b hab ‚â´ t.natTransTruncLEOfLE b c hbc =
      t.natTransTruncLEOfLE a c (hab.trans hbc) :=
  t.natTransTruncLTOfLE_trans _ _ _ _ _

lemma natTransTruncLEOfLE_refl_app (a : ‚Ñ§) (X : C) :
    (t.natTransTruncLEOfLE a a (by rfl)).app X = ùüô _ :=
  congr_app (t.natTransTruncLEOfLE_refl a) X

@[reassoc (attr := simp)]
lemma natTransTruncLEOfLE_trans_app (a b c : ‚Ñ§) (hab : a ‚â§ b) (hbc : b ‚â§ c) (X : C) :
    (t.natTransTruncLEOfLE a b hab).app X ‚â´ (t.natTransTruncLEOfLE b c hbc).app X =
      (t.natTransTruncLEOfLE a c (hab.trans hbc)).app X :=
  congr_app (t.natTransTruncLEOfLE_trans a b c hab hbc) X

/-- The natural transformation `ùü≠ C ‚ü∂ t.truncGT n` when `t` is a t-structure
on a category `C` and `n : ‚Ñ§`. -/
noncomputable def truncGTœÄ (n : ‚Ñ§) : ùü≠ C ‚ü∂ t.truncGT n := t.truncGEœÄ (n + 1)

@[reassoc (attr := simp)]
lemma œÄ_truncGTIsoTruncGE_hom (a b : ‚Ñ§) (h : a + 1 = b) :
    t.truncGTœÄ a ‚â´ (t.truncGTIsoTruncGE a b h).hom = t.truncGEœÄ b := by
  subst h
  dsimp [truncGTIsoTruncGE, truncGTœÄ]
  rw [Category.comp_id]

@[reassoc (attr := simp)]
lemma œÄ_truncGTIsoTruncGE_hom_Œπ_app (a b : ‚Ñ§) (h : a + 1 = b) (X : C) :
    (t.truncGTœÄ a).app X ‚â´ (t.truncGTIsoTruncGE a b h).hom.app X = (t.truncGEœÄ b).app X :=
  congr_app (t.œÄ_truncGTIsoTruncGE_hom a b h) X

@[reassoc (attr := simp)]
lemma œÄ_truncGTIsoTruncGE_inv (a b : ‚Ñ§) (h : a + 1 = b) :
    t.truncGEœÄ b ‚â´ (t.truncGTIsoTruncGE a b h).inv = t.truncGTœÄ a := by
  subst h
  dsimp [truncGTIsoTruncGE, truncGTœÄ, truncGT]
  rw [Category.comp_id]

@[reassoc (attr := simp)]
lemma œÄ_truncGTIsoTruncGE_inv_Œπ_app (a b : ‚Ñ§) (h : a + 1 = b) (X : C) :
    (t.truncGEœÄ b).app X ‚â´ (t.truncGTIsoTruncGE a b h).inv.app X = (t.truncGTœÄ a).app X :=
  congr_app (t.œÄ_truncGTIsoTruncGE_inv a b h) X

/-- The connecting homomorphism `(t.truncGE b).obj X ‚ü∂ ((t.truncLE a).obj X)‚ü¶1‚üß`
when `a + 1 = b`, as a natural transformation. -/
noncomputable def truncGEŒ¥LE (a b : ‚Ñ§) (h : a + 1 = b) :
    t.truncGE b ‚ü∂ t.truncLE a ‚ãô shiftFunctor C (1 : ‚Ñ§) :=
  t.truncGEŒ¥LT b ‚â´ Functor.whiskerRight (t.truncLEIsoTruncLT a b h).inv (shiftFunctor C (1 : ‚Ñ§))

/-- The distinguished triangle `(t.truncLE a).obj A ‚ü∂ A ‚ü∂ (t.truncGE b).obj A ‚ü∂ ...`
as a functor `C ‚•§ Triangle C` when `t` is a `t`-structure on a pretriangulated
category `C` and `a + 1 = b`. -/
@[simps!]
noncomputable def triangleLEGE (a b : ‚Ñ§) (h : a + 1 = b) : C ‚•§ Triangle C :=
  Triangle.functorMk (t.truncLEŒπ a) (t.truncGEœÄ b) (t.truncGEŒ¥LE a b h)

/-- The natural isomorphism of triangles `t.triangleLEGE a b h ‚âÖ t.triangleLTGE b`
when `a + 1 = b`. -/
noncomputable def triangleLEGEIsoTriangleLTGE (a b : ‚Ñ§) (h : a + 1 = b) :
    t.triangleLEGE a b h ‚âÖ t.triangleLTGE b := by
  refine Triangle.functorIsoMk _ _ (t.truncLEIsoTruncLT a b h) (Iso.refl _) (Iso.refl _) ?_ ?_ ?_
  ¬∑ cat_disch
  ¬∑ cat_disch
  ¬∑ ext
    dsimp [truncGEŒ¥LE]
    simp only [Category.assoc, Category.id_comp, ‚Üê Functor.map_comp,
      Iso.inv_hom_id_app, Functor.map_id, Category.comp_id]

lemma triangleLEGE_distinguished (a b : ‚Ñ§) (h : a + 1 = b) (X : C) :
    (t.triangleLEGE a b h).obj X ‚àà distTriang C :=
  isomorphic_distinguished _ (t.triangleLTGE_distinguished b X) _
    ((t.triangleLEGEIsoTriangleLTGE a b h).app X)

/-- The connecting homomorphism `(t.truncGT n).obj X ‚ü∂ ((t.truncLE n).obj X)‚ü¶1‚üß`
for `n : ‚Ñ§`, as a natural transformation. -/
noncomputable def truncGTŒ¥LE (n : ‚Ñ§) :
    t.truncGT n ‚ü∂ t.truncLE n ‚ãô shiftFunctor C (1 : ‚Ñ§) :=
  (t.truncGTIsoTruncGE n (n+1) rfl).hom ‚â´ t.truncGEŒ¥LE n (n + 1) (by lia)

/-- The distinguished triangle `(t.truncLE n).obj A ‚ü∂ A ‚ü∂ (t.truncGT n).obj A ‚ü∂ ...`
as a functor `C ‚•§ Triangle C` when `t` is a t-structure on a pretriangulated
category `C` and `n : ‚Ñ§`. -/
@[simps!]
noncomputable def triangleLEGT (n : ‚Ñ§) : C ‚•§ Triangle C :=
  Triangle.functorMk (t.truncLEŒπ n) (t.truncGTœÄ n) (t.truncGTŒ¥LE n)

/-- The natural isomorphism `t.triangleLEGT a ‚âÖ t.triangleLEGE a b h`
when `a + 1 = b`. -/
noncomputable def triangleLEGTIsoTriangleLEGE (a b : ‚Ñ§) (h : a + 1 = b) :
    t.triangleLEGT a ‚âÖ t.triangleLEGE a b h :=
  Triangle.functorIsoMk _ _ (Iso.refl _) (Iso.refl _) (t.truncGTIsoTruncGE a b h)
    (by cat_disch) (by cat_disch) (by
      ext
      dsimp [truncGTŒ¥LE]
      subst h
      simp only [Functor.map_id, Category.comp_id])

lemma triangleLEGT_distinguished (n : ‚Ñ§) (X : C) :
    (t.triangleLEGT n).obj X ‚àà distTriang C :=
  isomorphic_distinguished _ (t.triangleLEGE_distinguished n (n+1) rfl X) _
    ((t.triangleLEGTIsoTriangleLEGE n (n+1) rfl).app X)

lemma isLE_iff_isIso_truncLEŒπ_app (n : ‚Ñ§) (X : C) :
    t.IsLE X n ‚Üî IsIso ((t.truncLEŒπ n).app X) :=
  t.isLE_iff_isIso_truncLTŒπ_app n (n + 1) rfl X

lemma isGE_iff_isIso_truncGTœÄ_app (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ) (X : C) :
    t.IsGE X n‚ÇÅ ‚Üî IsIso ((t.truncGTœÄ n‚ÇÄ).app X) := by
  rw [t.isGE_iff_isIso_truncGEœÄ_app n‚ÇÅ X]
  exact (MorphismProperty.isomorphisms _).arrow_mk_iso_iff
    (Arrow.isoMk (Iso.refl _) ((t.truncGTIsoTruncGE _ _ hn‚ÇÅ).symm.app X))

instance (X : C) (n : ‚Ñ§) [t.IsLE X n] : IsIso ((t.truncLEŒπ n).app X) := by
  rw [‚Üê isLE_iff_isIso_truncLEŒπ_app ]
  infer_instance

lemma isLE_iff_isZero_truncGT_obj (n : ‚Ñ§) (X : C) :
    t.IsLE X n ‚Üî IsZero ((t.truncGT n).obj X) := by
  rw [t.isLE_iff_isIso_truncLEŒπ_app n X]
  exact (Triangle.isZero‚ÇÉ_iff_isIso‚ÇÅ _ (t.triangleLEGT_distinguished n X)).symm

lemma isGE_iff_isZero_truncLE_obj (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (h : n‚ÇÄ + 1 = n‚ÇÅ) (X : C) :
    t.IsGE X n‚ÇÅ ‚Üî IsZero ((t.truncLE n‚ÇÄ).obj X) := by
  rw [t.isGE_iff_isIso_truncGEœÄ_app n‚ÇÅ X]
  exact (Triangle.isZero‚ÇÅ_iff_isIso‚ÇÇ _ (t.triangleLEGE_distinguished n‚ÇÄ n‚ÇÅ h X)).symm

lemma isZero_truncLE_obj_of_isGE (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (h : n‚ÇÄ + 1 = n‚ÇÅ) (X : C) [t.IsGE X n‚ÇÅ] :
    IsZero ((t.truncLE n‚ÇÄ).obj X) := by
  rw [‚Üê t.isGE_iff_isZero_truncLE_obj _ _ h X]
  infer_instance

lemma to_truncLE_obj_ext {n : ‚Ñ§} {Y : C} {X : C}
    {f‚ÇÅ f‚ÇÇ : Y ‚ü∂ (t.truncLE n).obj X} (h : f‚ÇÅ ‚â´ (t.truncLEŒπ n).app X = f‚ÇÇ ‚â´ (t.truncLEŒπ n).app X)
    [t.IsLE Y n] :
    f‚ÇÅ = f‚ÇÇ := by
  have : t.IsLE Y (n + 1 - 1) := by simpa
  rw [‚Üê cancel_mono ((t.truncLEIsoTruncLT n (n + 1) rfl).hom.app _)]
  exact t.to_truncLT_obj_ext (by simpa)

section

variable {X Y : C} (f : X ‚ü∂ Y) (n : ‚Ñ§) [t.IsLE X n]

lemma liftTruncLE_aux :
    ‚àÉ (f' : X ‚ü∂ (t.truncLE n).obj Y), f = f' ‚â´ (t.truncLEŒπ n).app Y :=
  Triangle.coyoneda_exact‚ÇÇ _ (t.triangleLEGT_distinguished n Y) f
    (t.zero_of_isLE_of_isGE  _ n (n + 1) (by lia) inferInstance (by dsimp; infer_instance))

/-- Constructor for morphisms to `(t.truncLE n).obj Y`. -/
noncomputable def liftTruncLE :
    X ‚ü∂ (t.truncLE n).obj Y := (t.liftTruncLE_aux f n).choose

@[reassoc (attr := simp)]
lemma liftTruncLE_Œπ :
    t.liftTruncLE f n ‚â´ (t.truncLEŒπ n).app Y = f :=
  (t.liftTruncLE_aux f n).choose_spec.symm

end

section

variable {X Y : C} (f : X ‚ü∂ Y) (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (h : n‚ÇÄ + 1 = n‚ÇÅ) [t.IsGE Y n‚ÇÅ]

include h in
lemma descTruncGT_aux :
  ‚àÉ (f' : (t.truncGT n‚ÇÄ).obj X ‚ü∂ Y), f = (t.truncGTœÄ n‚ÇÄ).app X ‚â´ f' :=
  Triangle.yoneda_exact‚ÇÇ _ (t.triangleLEGT_distinguished n‚ÇÄ X) f
    (t.zero_of_isLE_of_isGE _ n‚ÇÄ n‚ÇÅ (by lia) (by dsimp; infer_instance) inferInstance)

/-- Constructor for morphisms from `(t.truncGT n‚ÇÄ).obj Y`. -/
noncomputable def descTruncGT :
    (t.truncGT n‚ÇÄ).obj X ‚ü∂ Y :=
  (t.descTruncGT_aux f n‚ÇÄ n‚ÇÅ h).choose

@[reassoc (attr := simp)]
lemma œÄ_descTruncGT :
    (t.truncGTœÄ n‚ÇÄ).app X ‚â´ t.descTruncGT f n‚ÇÄ n‚ÇÅ h = f :=
  (t.descTruncGT_aux f n‚ÇÄ n‚ÇÅ h).choose_spec.symm

end

lemma isIso_truncLE_map_iff {X Y : C} (f : X ‚ü∂ Y) (a b : ‚Ñ§) (h : a + 1 = b) :
    IsIso ((t.truncLE a).map f) ‚Üî
      ‚àÉ (Z : C) (g : Y ‚ü∂ Z) (h : Z ‚ü∂ ((t.truncLE a).obj X)‚ü¶1‚üß)
        (_ : Triangle.mk ((t.truncLEŒπ a).app X ‚â´ f) g h ‚àà distTriang _), t.IsGE Z b := by
  subst h
  apply isIso_truncLT_map_iff

lemma isIso_truncGT_map_iff {Y Z : C} (g : Y ‚ü∂ Z) (n : ‚Ñ§) :
    IsIso ((t.truncGT n).map g) ‚Üî
      ‚àÉ (X : C) (f : X ‚ü∂ Y) (h : ((t.truncGT n).obj Z) ‚ü∂ X‚ü¶(1 : ‚Ñ§)‚üß)
        (_ : Triangle.mk f (g ‚â´ (t.truncGTœÄ n).app Z) h ‚àà distTriang _), t.IsLE X n :=
  t.isIso_truncGE_map_iff g n (n + 1) rfl

instance (X : C) (a b : ‚Ñ§) [t.IsLE X b] : t.IsLE ((t.truncLE a).obj X) b := by
  dsimp [truncLE]
  infer_instance

instance (X : C) (a b : ‚Ñ§) [t.IsGE X a] : t.IsGE ((t.truncGT b).obj X) a := by
  dsimp [truncGT]
  infer_instance

/-- The composition `t.truncGE a ‚ãô t.truncGE b`. -/
noncomputable abbrev truncLEGE (a b : ‚Ñ§) : C ‚•§ C := t.truncGE a ‚ãô t.truncLE b

/-- The composition `t.truncLE b ‚ãô t.truncGE a`. -/
noncomputable abbrev truncGELE (a b : ‚Ñ§) : C ‚•§ C := t.truncLE b ‚ãô t.truncGE a

instance (X : C) (a b : ‚Ñ§) : t.IsGE ((t.truncGELE a b).obj X) a := by
  dsimp; infer_instance

/-- The natural isomorphism `t.truncGELE a b ‚âÖ t.truncGELT a b'` when `b + 1 = b'`. -/
noncomputable def truncGELEIsoTruncGELT (a b b' : ‚Ñ§) (hb' : b + 1 = b') :
    t.truncGELE a b ‚âÖ t.truncGELT a b' :=
  Functor.isoWhiskerRight (t.truncLEIsoTruncLT b b' hb') _

section

variable [IsTriangulated C]

lemma isIso‚ÇÅ_truncLE_map_of_isGE (T : Triangle C) (hT : T ‚àà distTriang C)
    (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (h : n‚ÇÄ + 1 = n‚ÇÅ) (h‚ÇÉ : t.IsGE T.obj‚ÇÉ n‚ÇÅ) :
    IsIso ((t.truncLE n‚ÇÄ).map T.mor‚ÇÅ) := by
  subst h
  exact t.isIso‚ÇÅ_truncLT_map_of_isGE _ hT _ h‚ÇÉ

lemma isIso‚ÇÇ_truncGT_map_of_isLE (T : Triangle C) (hT : T ‚àà distTriang C)
    (n‚ÇÄ : ‚Ñ§) (h‚ÇÅ : t.IsLE T.obj‚ÇÅ n‚ÇÄ) :
    IsIso ((t.truncGT n‚ÇÄ).map T.mor‚ÇÇ) :=
  t.isIso‚ÇÇ_truncGE_map_of_isLE _ hT _ _ rfl h‚ÇÅ

instance (X : C) (a b : ‚Ñ§) [t.IsGE X a] :
    t.IsGE ((t.truncLE b).obj X) a := by
  dsimp [truncLE]; infer_instance

instance (X : C) (a b : ‚Ñ§) [t.IsLE X b] :
    t.IsLE ((t.truncGT a).obj X) b := by
  dsimp [truncGT]; infer_instance

instance (X : C) (a b : ‚Ñ§) [t.IsGE X a] :
    t.IsGE ((t.truncLE b).obj X) a := by
  dsimp [truncLE]; infer_instance

instance (X : C) (a b : ‚Ñ§) :
    t.IsLE ((t.truncGELE a b).obj X) b := by
  dsimp; infer_instance

lemma isIso_truncLE_map_truncLEŒπ_app (a b : ‚Ñ§) (h : a ‚â§ b) (X : C) :
    IsIso ((t.truncLE a).map ((t.truncLEŒπ b).app X)) :=
  t.isIso_truncLT_map_truncLTŒπ_app _ _ (by lia) _

lemma isIso_truncGT_map_truncGTœÄ_app (a b : ‚Ñ§) (h : b ‚â§ a) (X : C) :
    IsIso ((t.truncGT a).map ((t.truncGTœÄ b).app X)) :=
  isIso_truncGE_map_truncGEœÄ_app _ _ _ (by lia) _

instance (X : C) (n : ‚Ñ§) : IsIso ((t.truncLE n).map ((t.truncLEŒπ n).app X)) :=
  t.isIso_truncLE_map_truncLEŒπ_app _ _ (by lia) _

/-- The natural isomorphism `t.truncGELE a b ‚âÖ t.truncLEGE a b`. -/
noncomputable def truncGELEIsoLEGE (a b : ‚Ñ§) : t.truncGELE a b ‚âÖ t.truncLEGE a b :=
  t.truncGELTIsoLTGE a (b + 1)

end

end TStructure

end Triangulated

end CategoryTheory
