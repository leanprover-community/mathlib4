/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Triangulated.TStructure.TruncLEGT
public import Mathlib.Algebra.Homology.SpectralSequence.EInt

/-!
# Truncations for a t-structure

-/

@[expose] public section

namespace CategoryTheory

open Category Limits Pretriangulated ZeroObject Preadditive Functor

variable {C : Type*} [Category* C] [Preadditive C] [HasZeroObject C] [HasShift C ‚Ñ§]
  [‚àÄ (n : ‚Ñ§), (shiftFunctor C n).Additive] [Pretriangulated C]

namespace Triangulated

namespace TStructure

variable (t : TStructure C)

noncomputable def eTruncLT : EInt ‚•§ C ‚•§ C where
  obj n := by
    induction n with
    | bot => exact 0
    | coe a => exact t.truncLT a
    | top => exact ùü≠ C
  map {x y} f := by
    induction x with
    | bot =>
      induction y with
      | bot => exact ùüô _
      | coe b => exact 0
      | top => exact 0
    | coe a =>
      induction y with
      | bot => exact 0
      | coe b => exact t.natTransTruncLTOfLE a b (by simpa using leOfHom f)
      | top => exact t.truncLTŒπ a
    | top =>
      induction y with
      | bot => exact 0
      | coe b => exact 0
      | top => exact ùüô _
  map_id n := by induction n <;> simp
  map_comp {x y z} f g := by
    have f' := leOfHom f
    have g' := leOfHom g
    induction x <;> induction y <;> induction z <;> cat_disch

@[simp]
lemma eTruncLT_obj_top : t.eTruncLT.obj ‚ä§ = ùü≠ _ := rfl

@[simp]
lemma eTruncLT_obj_bot : t.eTruncLT.obj ‚ä• = 0 := rfl

@[simp]
lemma eTruncLT_obj_mk (n : ‚Ñ§) : t.eTruncLT.obj (EInt.mk n) = t.truncLT n := rfl

@[simp]
lemma eTruncLT_map_eq_truncLTŒπ (n : ‚Ñ§) :
    t.eTruncLT.map (homOfLE (show EInt.mk n ‚â§ ‚ä§ by simp)) = t.truncLTŒπ n := rfl

noncomputable def eTruncGE : EInt ‚•§ C ‚•§ C where
  obj n := by
    induction n with
    | bot => exact ùü≠ C
    | coe a => exact t.truncGE a
    | top => exact 0
  map {x y} f := by
    induction x with
    | bot =>
      induction y with
      | bot => exact ùüô _
      | coe b => exact t.truncGEœÄ b
      | top => exact 0
    | coe a =>
      induction y with
      | bot => exact 0
      | coe b => exact t.natTransTruncGEOfLE a b (by simpa using leOfHom f)
      | top => exact 0
    | top =>
      induction y with
      | bot => exact 0
      | coe b => exact 0
      | top => exact ùüô _
  map_id n := by induction n <;> simp
  map_comp {x y z} f g := by
    have f' := leOfHom f
    have g' := leOfHom g
    induction x <;> induction y <;> induction z <;> cat_disch

@[simp]
lemma eTruncGE_obj_bot :
    t.eTruncGE.obj ‚ä• = ùü≠ _ := rfl

@[simp]
lemma eTruncGE_obj_top :
    t.eTruncGE.obj ‚ä§ = 0 := rfl

@[simp]
lemma eTruncGE_obj_mk (n : ‚Ñ§) : t.eTruncGE.obj (EInt.mk n) = t.truncGE n := rfl

noncomputable def eTruncGEŒ¥LTt :
    t.eTruncGE ‚ü∂ t.eTruncLT ‚ãô ((whiskeringRight C C C).obj (shiftFunctor C (1 : ‚Ñ§))) where
  app a := by
    induction a with
    | bot => exact 0
    | coe a => exact t.truncGEŒ¥LT a
    | top => exact 0
  naturality {a b} hab := by
    replace hab := leOfHom hab
    induction a; rotate_right
    ¬∑ apply (isZero_zero _).eq_of_src
    all_goals
      induction b <;> simp at hab <;>
        dsimp [eTruncGE, eTruncLT] <;>
        simp [t.truncGEŒ¥LT_comp_whiskerRight_natTransTruncLTOfLE]

@[simp]
lemma eTruncGEŒ¥LTt_mk (n : ‚Ñ§) :
    t.eTruncGEŒ¥LTt.app (EInt.mk n) = t.truncGEŒ¥LT n := rfl


end TStructure

end Triangulated

end CategoryTheory
