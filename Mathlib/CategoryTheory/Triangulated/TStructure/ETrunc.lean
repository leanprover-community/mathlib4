/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Triangulated.TStructure.TruncLEGT
public import Mathlib.Data.EInt.Basic

/-!
# Truncations for a t-structure

Let `t` be a t-structure on a triangulated category `C`.
In this file, we extend the definition of the truncation functors
`truncLT` and `truncGE` for indices in `‚Ñ§` to `EInt`,
as `t.eTruncLT : EInt ‚•§ C ‚•§ C` and `t.eTruncGE : EInt ‚•§ C ‚•§ C`.

-/

@[expose] public section

namespace CategoryTheory

open Category Limits Pretriangulated ZeroObject Preadditive

variable {C : Type*} [Category* C] [Preadditive C] [HasZeroObject C] [HasShift C ‚Ñ§]
  [‚àÄ (n : ‚Ñ§), (shiftFunctor C n).Additive] [Pretriangulated C]

namespace Triangulated

namespace TStructure

variable (t : TStructure C)

/-- The functor `EInt ‚•§ C ‚•§ C` which sends `‚ä•` to the zero functor,
`n : ‚Ñ§` to `t.truncLT n` and `‚ä§` to `ùü≠ C`. -/
noncomputable def eTruncLT : EInt ‚•§ C ‚•§ C where
  obj n := by
    induction n with
    | bot => exact 0
    | coe a => exact t.truncLT a
    | top => exact ùü≠ C
  map {x y} f := by
    induction x with
    | bot =>
      induction y with
      | bot => exact ùüô _
      | coe b => exact 0
      | top => exact 0
    | coe a =>
      induction y with
      | bot => exact 0
      | coe b => exact t.natTransTruncLTOfLE a b (by simpa using leOfHom f)
      | top => exact t.truncLTŒπ a
    | top =>
      induction y with
      | bot => exact 0
      | coe b => exact 0
      | top => exact ùüô _
  map_id n := by induction n <;> simp
  map_comp {x y z} f g := by
    have f' := leOfHom f
    have g' := leOfHom g
    induction x <;> induction y <;> induction z <;> cat_disch

@[simp]
lemma eTruncLT_obj_top : t.eTruncLT.obj ‚ä§ = ùü≠ _ := rfl

@[simp]
lemma eTruncLT_obj_bot : t.eTruncLT.obj ‚ä• = 0 := rfl

@[simp]
lemma eTruncLT_obj_coe (n : ‚Ñ§) : t.eTruncLT.obj n = t.truncLT n := rfl

@[simp]
lemma eTruncLT_map_eq_truncLTŒπ (n : ‚Ñ§) :
    t.eTruncLT.map (homOfLE (show (n : EInt) ‚â§ ‚ä§ by simp)) = t.truncLTŒπ n := rfl

instance (i : EInt) : (t.eTruncLT.obj i).Additive := by
  induction i <;> constructor <;> cat_disch

/-- The functor `EInt ‚•§ C ‚•§ C` which sends `‚ä•` to `ùü≠ C`,
`n : ‚Ñ§` to `t.truncGE n` and `‚ä§` to the zero functor. -/
noncomputable def eTruncGE : EInt ‚•§ C ‚•§ C where
  obj n := by
    induction n with
    | bot => exact ùü≠ C
    | coe a => exact t.truncGE a
    | top => exact 0
  map {x y} f := by
    induction x with
    | bot =>
      induction y with
      | bot => exact ùüô _
      | coe b => exact t.truncGEœÄ b
      | top => exact 0
    | coe a =>
      induction y with
      | bot => exact 0
      | coe b => exact t.natTransTruncGEOfLE a b (by simpa using leOfHom f)
      | top => exact 0
    | top =>
      induction y with
      | bot => exact 0
      | coe b => exact 0
      | top => exact ùüô _
  map_id n := by induction n <;> simp
  map_comp {x y z} f g := by
    have f' := leOfHom f
    have g' := leOfHom g
    induction x <;> induction y <;> induction z <;> cat_disch

@[simp]
lemma eTruncGE_obj_bot :
    t.eTruncGE.obj ‚ä• = ùü≠ _ := rfl

@[simp]
lemma eTruncGE_obj_top :
    t.eTruncGE.obj ‚ä§ = 0 := rfl

@[simp]
lemma eTruncGE_obj_coe (n : ‚Ñ§) : t.eTruncGE.obj n = t.truncGE n := rfl

instance (i : EInt) : (t.eTruncGE.obj i).Additive := by
  induction i <;> constructor <;> cat_disch

/-- The connecting homomorphism from `t.eTruncGE` to the
shift by `1` of `t.eTruncLT`. -/
noncomputable def eTruncGEŒ¥LT :
    t.eTruncGE ‚ü∂ t.eTruncLT ‚ãô ((Functor.whiskeringRight C C C).obj (shiftFunctor C (1 : ‚Ñ§))) where
  app a := by
    induction a with
    | bot => exact 0
    | coe a => exact t.truncGEŒ¥LT a
    | top => exact 0
  naturality {a b} hab := by
    replace hab := leOfHom hab
    induction a; rotate_right
    ¬∑ apply (isZero_zero _).eq_of_src
    all_goals
      induction b <;> simp at hab <;>
        dsimp [eTruncGE, eTruncLT] <;>
        simp [t.truncGEŒ¥LT_comp_whiskerRight_natTransTruncLTOfLE]

@[simp]
lemma eTruncGEŒ¥LT_coe (n : ‚Ñ§) :
    t.eTruncGEŒ¥LT.app n = t.truncGEŒ¥LT n := rfl

/-- The natural transformation `t.eTruncLT.obj i ‚ü∂ ùü≠ C` for all `i : EInt`. -/
noncomputable abbrev eTruncLTŒπ (i : EInt) : t.eTruncLT.obj i ‚ü∂ ùü≠ _ :=
  t.eTruncLT.map (homOfLE (le_top))

@[simp] lemma eTruncLT_Œπ_bot : t.eTruncLTŒπ ‚ä• = 0 := rfl
@[simp] lemma eTruncLT_Œπ_coe (n : ‚Ñ§) : t.eTruncLTŒπ n = t.truncLTŒπ n := rfl
@[simp] lemma eTruncLT_Œπ_top : t.eTruncLTŒπ ‚ä§ = ùüô _ := rfl

@[reassoc]
lemma eTruncLTŒπ_naturality (i : EInt) {X Y : C} (f : X ‚ü∂ Y) :
    (t.eTruncLT.obj i).map f ‚â´ (t.eTruncLTŒπ i).app Y = (t.eTruncLTŒπ i).app X ‚â´ f :=
  (t.eTruncLTŒπ i).naturality f

instance : IsIso (t.eTruncLTŒπ ‚ä§) := by
  dsimp [eTruncLTŒπ]
  infer_instance

@[reassoc (attr := simp)]
lemma eTruncLT_map_app_eTruncLTŒπ_app {i j : EInt} (f : i ‚ü∂ j) (X : C) :
    (t.eTruncLT.map f).app X ‚â´ (t.eTruncLTŒπ j).app X = (t.eTruncLTŒπ i).app X := by
  simp only [‚Üê NatTrans.comp_app, ‚Üê Functor.map_comp]
  rfl

@[reassoc]
lemma eTruncLT_obj_map_eTruncLTŒπ_app (i : EInt) (X : C) :
    (t.eTruncLT.obj i).map ((t.eTruncLTŒπ i).app X) =
    (t.eTruncLTŒπ i).app ((t.eTruncLT.obj i).obj X) := by
  induction i with
  | bot => simp
  | coe n => simp [truncLT_map_truncLTŒπ_app]
  | top => simp

/-- The natural transformation `ùü≠ C ‚ü∂ t.eTruncGE.obj i` for all `i : EInt`. -/
noncomputable abbrev eTruncGEœÄ (i : EInt) : ùü≠ C ‚ü∂ t.eTruncGE.obj i :=
  t.eTruncGE.map (homOfLE (bot_le))

@[simp] lemma eTruncGEœÄ_bot : t.eTruncGEœÄ ‚ä• = ùüô _ := rfl
@[simp] lemma eTruncGEœÄ_coe (n : ‚Ñ§) : t.eTruncGEœÄ n = t.truncGEœÄ n := rfl
@[simp] lemma eTruncGEœÄ_top : t.eTruncGEœÄ ‚ä§ = 0 := rfl

@[reassoc (attr := simp)]
lemma eTruncGEœÄ_naturality (i : EInt) {X Y : C} (f : X ‚ü∂ Y) :
    (t.eTruncGEœÄ i).app X ‚â´ (t.eTruncGE.obj i).map f = f ‚â´ (t.eTruncGEœÄ i).app Y :=
  ((t.eTruncGEœÄ i).naturality f).symm

instance : IsIso (t.eTruncGEœÄ ‚ä•) := by
  dsimp [eTruncGEœÄ]
  infer_instance

@[reassoc (attr := simp)]
lemma eTruncGEœÄ_app_eTruncGE_map_app {i j : EInt} (f : i ‚ü∂ j) (X : C) :
    (t.eTruncGEœÄ i).app X ‚â´ (t.eTruncGE.map f).app X = (t.eTruncGEœÄ j).app X := by
  simp only [‚Üê NatTrans.comp_app, ‚Üê Functor.map_comp]
  rfl

@[reassoc]
lemma eTruncGE_obj_map_eTruncGEœÄ_app (i : EInt) (X : C) :
    (t.eTruncGE.obj i).map ((t.eTruncGEœÄ i).app X) =
    (t.eTruncGEœÄ i).app ((t.eTruncGE.obj i).obj X) := by
  induction i with
  | bot => simp
  | coe n => simp [truncGE_map_truncGEœÄ_app]
  | top => simp

/-- The (distinguished) triangles given by the natural transformations
`t.eTruncLT.obj i ‚ü∂ ùü≠ C ‚ü∂ t.eTruncGE.obj i ‚ü∂ ...` for all `i : EInt`. -/
@[simps!]
noncomputable def eTriangleLTGE : EInt ‚•§ C ‚•§ Triangle C where
  obj i := Triangle.functorMk (t.eTruncLTŒπ i) (t.eTruncGEœÄ i) (t.eTruncGEŒ¥LT.app i)
  map f := Triangle.functorHomMk _ _ (t.eTruncLT.map f) (ùüô _) (t.eTruncGE.map f)

lemma eTriangleLTGE_distinguished (i : EInt) (X : C) :
    (t.eTriangleLTGE.obj i).obj X ‚àà distTriang _ := by
  induction i with
  | bot =>
    rw [Triangle.distinguished_iff_of_isZero‚ÇÅ _ (Functor.zero_obj X)]
    dsimp
    infer_instance
  | coe n => exact t.triangleLTGE_distinguished n X
  | top =>
    rw [Triangle.distinguished_iff_of_isZero‚ÇÉ _ (Functor.zero_obj X)]
    dsimp
    infer_instance

instance (X : C) (n : ‚Ñ§) [t.IsLE X n] (i : EInt) :
    t.IsLE ((t.eTruncLT.obj i).obj X) n := by
  induction i with
  | bot => exact isLE_of_isZero _ (by simp) _
  | coe _ => dsimp; infer_instance
  | top => dsimp; infer_instance

instance (X : C) (n : ‚Ñ§) [t.IsGE X n] (i : EInt) :
    t.IsGE ((t.eTruncGE.obj i).obj X) n := by
  induction i with
  | bot => dsimp; infer_instance
  | coe _ => dsimp; infer_instance
  | top => exact isGE_of_isZero _ (by simp) _

lemma isGE_eTruncGE_obj_obj (n : ‚Ñ§) (i : EInt) (h : n ‚â§ i) (X : C) :
    t.IsGE ((t.eTruncGE.obj i).obj X) n := by
  induction i with
  | bot => simp at h
  | coe i =>
    dsimp
    exact t.isGE_of_GE  _ _ _ (by simpa using h)
  | top => exact t.isGE_of_isZero (Functor.zero_obj _) _

lemma isLE_eTruncLT_obj_obj (n : ‚Ñ§) (i : EInt) (h : i ‚â§ (n + 1 :)) (X : C) :
    t.IsLE (((t.eTruncLT.obj i)).obj X) n := by
  induction i with
  | bot => exact t.isLE_of_isZero (by simp) _
  | coe i =>
    simp only [EInt.coe_le_coe_iff] at h
    dsimp
    exact t.isLE_of_LE _ (i - 1) n (by lia)
  | top => simp at h

lemma isZero_eTruncLT_obj_obj (X : C) (n : ‚Ñ§) [t.IsGE X n] (j : EInt) (hj : j ‚â§ n) :
    IsZero ((t.eTruncLT.obj j).obj X) := by
  induction j with
  | bot => simp
  | coe j =>
    have := t.isGE_of_GE X j n (by simpa using hj)
    exact t.isZero_truncLT_obj_of_isGE _ _
  | top => simp at hj

lemma isZero_eTruncGE_obj_obj (X : C) (n : ‚Ñ§) [t.IsLE X n] (j : EInt) (hj : n < j) :
    IsZero ((t.eTruncGE.obj j).obj X) := by
  induction j with
  | bot => simp at hj
  | coe j =>
    simp only [EInt.coe_lt_coe_iff] at hj
    have := t.isLE_of_LE X n (j - 1) (by lia)
    exact t.isZero_truncGE_obj_of_isLE (j - 1) j (by lia) _
  | top => simp

section

variable [IsTriangulated C]

lemma isIso_eTruncGE_obj_map_truncGEœÄ_app (a b : EInt) (h : a ‚â§ b) (X : C) :
    IsIso ((t.eTruncGE.obj b).map ((t.eTruncGEœÄ a).app X)) := by
  induction b with
  | bot =>
    obtain rfl : a = ‚ä• := by simpa using h
    infer_instance
  | coe b =>
    induction a with
    | bot => dsimp; infer_instance
    | coe a => exact t.isIso_truncGE_map_truncGEœÄ_app b a (by simpa using h) X
    | top => simp at h
  | top => exact ‚ü®0, IsZero.eq_of_src (by simp) _ _, IsZero.eq_of_src (by simp) _ _‚ü©

lemma isIso_eTruncLT_obj_map_truncLTœÄ_app (a b : EInt) (h : a ‚â§ b) (X : C) :
    IsIso ((t.eTruncLT.obj a).map ((t.eTruncLTŒπ b).app X)) := by
  induction a with
  | bot => exact ‚ü®0, IsZero.eq_of_src (by simp) _ _, IsZero.eq_of_src (by simp) _ _‚ü©
  | coe a =>
    induction b with
    | bot => simp at h
    | coe b =>
      exact t.isIso_truncLT_map_truncLTŒπ_app a b (by simpa using h) X
    | top => dsimp; infer_instance
  | top =>
    obtain rfl : b = ‚ä§ := by simpa using h
    infer_instance

instance (a : EInt) (X : C) : IsIso ((t.eTruncLT.obj a).map ((t.eTruncLTŒπ a).app X)) :=
  isIso_eTruncLT_obj_map_truncLTœÄ_app t a a (by rfl) X

instance (a : EInt) (X : C) : IsIso ((t.eTruncLTŒπ a).app ((t.eTruncLT.obj a).obj X)) := by
  rw [‚Üê eTruncLT_obj_map_eTruncLTŒπ_app]
  infer_instance

instance (X : C) (n : ‚Ñ§) [t.IsGE X n] (i : EInt) :
    t.IsGE ((t.eTruncLT.obj i).obj X) n := by
  induction i with
  | bot => exact isGE_of_isZero _ (by simp) _
  | coe _ => dsimp; infer_instance
  | top => dsimp; infer_instance

instance (X : C) (n : ‚Ñ§) [t.IsLE X n] (i : EInt) :
    t.IsLE ((t.eTruncGE.obj i).obj X) n := by
  induction i with
  | bot => dsimp; infer_instance
  | coe _ => dsimp; infer_instance
  | top => exact isLE_of_isZero _ (by simp) _

/-- The natural transformation `t.eTruncGE.obj b ‚ü∂ t.eTruncGE.obj a ‚ãô t.eTruncGE.obj b`
for all `a` and `b` in `EInt`. -/
@[simps!]
noncomputable def eTruncGEToGEGE (a b : EInt) :
    t.eTruncGE.obj b ‚ü∂ t.eTruncGE.obj a ‚ãô t.eTruncGE.obj b :=
  (Functor.leftUnitor _).inv ‚â´ Functor.whiskerRight (t.eTruncGEœÄ a) _

lemma isIso_eTruncGEIsoGEGE (a b : EInt) (hab : a ‚â§ b) :
    IsIso (t.eTruncGEToGEGE a b) := by
  rw [NatTrans.isIso_iff_isIso_app]
  intro X
  simp only [Functor.comp_obj, eTruncGEToGEGE_app]
  exact t.isIso_eTruncGE_obj_map_truncGEœÄ_app _ _ hab _

section

variable (a b : EInt) (hab : a ‚â§ b)

/-- The natural isomorphism `t.eTruncGE.obj b ‚âÖ t.eTruncGE.obj a ‚ãô t.eTruncGE.obj b`
when `a` and `b` in `EInt` satisfy `a ‚â§ b`. -/
@[simps! hom]
noncomputable def eTruncGEIsoGEGE :
    t.eTruncGE.obj b ‚âÖ t.eTruncGE.obj a ‚ãô t.eTruncGE.obj b :=
  haveI := t.isIso_eTruncGEIsoGEGE a b hab
  asIso (t.eTruncGEToGEGE a b)

@[reassoc (attr := simp)]
lemma eTruncGEIsoGEGE_hom_inv_id_app (X : C) :
    (t.eTruncGE.obj b).map ((t.eTruncGEœÄ a).app X) ‚â´ (t.eTruncGEIsoGEGE a b hab).inv.app X =
      ùüô _ := by
  simpa using (t.eTruncGEIsoGEGE a b hab).hom_inv_id_app X

@[reassoc (attr := simp)]
lemma eTruncGEIsoGEGE_inv_hom_id_app (X : C) :
    (t.eTruncGEIsoGEGE a b hab).inv.app X ‚â´ (t.eTruncGE.obj b).map ((t.eTruncGEœÄ a).app X) =
      ùüô _ := by
  simpa using (t.eTruncGEIsoGEGE a b hab).inv_hom_id_app X

end

/-- The natural transformation `t.eTruncLT.obj a ‚ãô t.eTruncLT.obj b ‚ü∂ t.eTruncLT.obj b`
for all `a` and `b` in `EInt`. -/
@[simps!]
noncomputable def eTruncLTLTToLT (a b : EInt) :
    t.eTruncLT.obj a ‚ãô t.eTruncLT.obj b ‚ü∂ t.eTruncLT.obj b :=
  Functor.whiskerRight (t.eTruncLTŒπ a) _ ‚â´ (Functor.leftUnitor _).hom

lemma isIso_eTruncLTLTIsoLT (a b : EInt) (hab : b ‚â§ a) :
    IsIso (t.eTruncLTLTToLT a b) := by
  rw [NatTrans.isIso_iff_isIso_app]
  intro X
  simp only [Functor.comp_obj, eTruncLTLTToLT_app]
  exact t.isIso_eTruncLT_obj_map_truncLTœÄ_app _ _ hab _

section

variable (a b : EInt) (hab : b ‚â§ a)

/-- The natural isomorphism `t.eTruncLT.obj a ‚ãô t.eTruncLT.obj b ‚ü∂ t.eTruncLT.obj b`
when `a` and `b` in `EInt` satisfy `b ‚â§ a`. -/
@[simps! hom]
noncomputable def eTruncLTLTIsoLT :
    t.eTruncLT.obj a ‚ãô t.eTruncLT.obj b ‚âÖ t.eTruncLT.obj b :=
  haveI := t.isIso_eTruncLTLTIsoLT a b hab
  asIso (t.eTruncLTLTToLT a b)

@[reassoc]
lemma eTruncLTLTIsoLT_hom_inv_id_app (X : C) :
    (t.eTruncLT.obj b).map ((t.eTruncLTŒπ a).app X) ‚â´
      (t.eTruncLTLTIsoLT a b hab).inv.app X = ùüô _ := by
  simpa using (t.eTruncLTLTIsoLT a b hab).hom_inv_id_app X

@[reassoc (attr := simp)]
lemma eTruncLTLTIsoLT_inv_hom_id_app (X : C) :
    (t.eTruncLTLTIsoLT a b hab).inv.app X ‚â´
    (t.eTruncLT.obj b).map ((t.eTruncLTŒπ a).app X) = ùüô _ := by
  simpa using (t.eTruncLTLTIsoLT a b hab).inv_hom_id_app X

@[reassoc (attr := simp)]
lemma eTruncLTLTIsoLT_inv_hom_id_app_eTruncLT_obj (X : C) :
    (t.eTruncLTLTIsoLT a b hab).inv.app ((t.eTruncLT.obj a).obj X) ‚â´
      (t.eTruncLT.obj b).map ((t.eTruncLT.obj a).map ((t.eTruncLTŒπ a).app X)) = ùüô _ := by
  simp [eTruncLT_obj_map_eTruncLTŒπ_app]

end


section

variable (a b : EInt)

/-- The natural transformation from
`t.eTruncLT.obj b ‚ãô t.eTruncGE.obj a ‚ãô t.eTruncLT.obj b` to
`t.eTruncGE.obj a ‚ãô t.eTruncLT.obj b`. (This is an isomorphism.) -/
@[simps!]
noncomputable def eTruncLTGELTSelfToLTGE :
    t.eTruncLT.obj b ‚ãô t.eTruncGE.obj a ‚ãô t.eTruncLT.obj b ‚ü∂
      t.eTruncGE.obj a ‚ãô t.eTruncLT.obj b :=
  Functor.whiskerRight (t.eTruncLTŒπ b) _ ‚â´ (Functor.leftUnitor _).hom

/-- The natural transformation from
`t.eTruncLT.obj b ‚ãô t.eTruncGE.obj a ‚ãô t.eTruncLT.obj b` to
`t.eTruncLT.obj b ‚ãô t.eTruncGE.obj a`. (This is an isomorphism.) -/
@[simps!]
noncomputable def eTruncLTGELTSelfToGELT :
    t.eTruncLT.obj b ‚ãô t.eTruncGE.obj a ‚ãô t.eTruncLT.obj b ‚ü∂
      t.eTruncLT.obj b ‚ãô t.eTruncGE.obj a :=
  (Functor.associator _ _ _).inv ‚â´ Functor.whiskerLeft _ (t.eTruncLTŒπ b) ‚â´
    (Functor.rightUnitor _).hom

instance : IsIso (t.eTruncLTGELTSelfToLTGE a b) := by
  rw [NatTrans.isIso_iff_isIso_app]
  intro X
  induction b with
  | bot => simp [isIsoZero_iff_source_target_isZero]
  | coe b =>
    induction a with
    | bot => simpa using inferInstanceAs (IsIso ((t.truncLT b).map ((t.truncLTŒπ b).app X)))
    | coe a =>
      simp only [eTruncLT_obj_coe, eTruncGE_obj_coe, Functor.comp_obj, eTruncLTGELTSelfToLTGE_app,
        eTruncLT_map_eq_truncLTŒπ]
      infer_instance
    | top =>
      simp only [eTruncLT_obj_coe, eTruncGE_obj_top, Functor.comp_obj, eTruncLTGELTSelfToLTGE_app,
        eTruncLT_map_eq_truncLTŒπ, zero_map, Functor.map_zero, isIsoZero_iff_source_target_isZero]
      constructor
      all_goals exact Functor.map_isZero _ (Functor.zero_obj _)
  | top => simpa using inferInstanceAs (IsIso (ùüô _))

variable (b : EInt) (X : C)

instance : IsIso (t.eTruncLTGELTSelfToGELT a b) := by
  rw [NatTrans.isIso_iff_isIso_app]
  intro X
  induction a with
  | bot => simpa using inferInstanceAs (IsIso ((t.eTruncLTŒπ b).app ((t.eTruncLT.obj b).obj X)))
  | coe a =>
    induction b with
    | bot => simpa [isIsoZero_iff_source_target_isZero] using
        (t.eTruncGE.obj a).map_isZero (Functor.zero_obj _)
    | coe b =>
      simp only [eTruncLT_obj_coe, eTruncGE_obj_coe, Functor.comp_obj, eTruncLTGELTSelfToGELT_app,
        eTruncLT_map_eq_truncLTŒπ]
      infer_instance
    | top => simpa using inferInstanceAs (IsIso (ùüô _))
  | top =>
    exact ‚ü®0, ((t.eTruncLT.obj b).map_isZero (by simp)).eq_of_src _ _,
      IsZero.eq_of_src (by simp) _ _‚ü©

end

/-- The commutation natural isomorphism
`t.eTruncGE.obj a ‚ãô t.eTruncLT.obj b ‚âÖ t.eTruncLT.obj b ‚ãô t.eTruncGE.obj a`
for all `a` and `b` in `EInt`. -/
noncomputable def eTruncLTGEIsoLEGT (a b : EInt) :
    t.eTruncGE.obj a ‚ãô t.eTruncLT.obj b ‚âÖ t.eTruncLT.obj b ‚ãô t.eTruncGE.obj a :=
  (asIso (t.eTruncLTGELTSelfToLTGE a b)).symm ‚â™‚â´ asIso (t.eTruncLTGELTSelfToGELT a b)

@[reassoc (attr := simp)]
lemma eTruncLTGEIsoLEGT_hom_naturality (a b : EInt) {X Y : C} (f : X ‚ü∂ Y) :
    (t.eTruncLT.obj b).map ((t.eTruncGE.obj a).map f) ‚â´ (t.eTruncLTGEIsoLEGT a b).hom.app Y =
      (t.eTruncLTGEIsoLEGT a b).hom.app X ‚â´ (t.eTruncGE.obj a).map ((t.eTruncLT.obj b).map f) :=
  (t.eTruncLTGEIsoLEGT a b).hom.naturality f

@[reassoc]
lemma eTruncLTGEIsoLEGT_hom_app_fac (a b : EInt) (X : C) :
    (t.eTruncLT.obj b).map ((t.eTruncGE.obj a).map ((t.eTruncLTŒπ b).app X)) ‚â´
      (t.eTruncLTGEIsoLEGT a b).hom.app X =
    (t.eTruncLTŒπ b).app ((t.eTruncGE.obj a).obj ((t.eTruncLT.obj b).obj X)):= by
  simp [eTruncLTGEIsoLEGT]

@[reassoc (attr := simp)]
lemma eTruncLTGEIsoLEGT_hom_app_fac' (a b : EInt) (X : C) :
    (t.eTruncLTGEIsoLEGT a b).hom.app X ‚â´ (t.eTruncGE.obj a).map ((t.eTruncLTŒπ b).app X) =
      (t.eTruncLTŒπ b).app ((t.eTruncGE.obj a).obj X) := by
  simp [eTruncLTGEIsoLEGT]

open ComposableArrows in
@[reassoc]
lemma eTruncLTGEIsoLEGT_naturality_app (a b : EInt) (hab : a ‚â§ b)
    (a' b' : EInt) (hab' : a' ‚â§ b') (œÜ : mk‚ÇÅ (homOfLE hab) ‚ü∂ mk‚ÇÅ (homOfLE hab')) (X : C) :
      (t.eTruncLT.map (œÜ.app 1)).app ((t.eTruncGE.obj a).obj X) ‚â´
        (t.eTruncLT.obj b').map ((t.eTruncGE.map (œÜ.app 0)).app X) ‚â´
        (t.eTruncLTGEIsoLEGT a' b').hom.app X =
    (t.eTruncLTGEIsoLEGT a b).hom.app X ‚â´ (t.eTruncGE.map (œÜ.app 0)).app _ ‚â´
      (t.eTruncGE.obj a').map ((t.eTruncLT.map (œÜ.app 1)).app X) := by
  rw [‚Üê cancel_epi ((t.eTruncLTGELTSelfToLTGE a b).app X)]
  dsimp
  rw [eTruncLTGELTSelfToLTGE_app, eTruncLTGEIsoLEGT_hom_app_fac_assoc,
    NatTrans.naturality_assoc, ‚Üê Functor.map_comp_assoc, NatTrans.naturality,
    Functor.map_comp_assoc, ‚Üê t.eTruncLT_map_app_eTruncLTŒπ_app (œÜ.app 1) X,
    Functor.map_comp, Functor.map_comp, Category.assoc,
    t.eTruncLTGEIsoLEGT_hom_app_fac]
  simp

end

end TStructure

end Triangulated

end CategoryTheory
