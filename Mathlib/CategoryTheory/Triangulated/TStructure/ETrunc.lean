/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
module

public import Mathlib.CategoryTheory.Triangulated.TStructure.TruncLEGT
public import Mathlib.Algebra.Homology.SpectralSequence.EInt

/-!
# Truncations for a t-structure

-/

@[expose] public section

namespace CategoryTheory

open Category Limits Pretriangulated ZeroObject Preadditive

variable {C : Type*} [Category* C] [Preadditive C] [HasZeroObject C] [HasShift C â„¤]
  [âˆ€ (n : â„¤), (shiftFunctor C n).Additive] [Pretriangulated C]

namespace Triangulated

namespace TStructure

variable (t : TStructure C)

noncomputable def eTruncLT : EInt â¥¤ C â¥¤ C where
  obj n := by
    induction n with
    | bot => exact 0
    | coe a => exact t.truncLT a
    | top => exact ğŸ­ C
  map {x y} f := by
    induction x with
    | bot =>
      induction y with
      | bot => exact ğŸ™ _
      | coe b => exact 0
      | top => exact 0
    | coe a =>
      induction y with
      | bot => exact 0
      | coe b => exact t.natTransTruncLTOfLE a b (by simpa using leOfHom f)
      | top => exact t.truncLTÎ¹ a
    | top =>
      induction y with
      | bot => exact 0
      | coe b => exact 0
      | top => exact ğŸ™ _
  map_id n := by induction n <;> simp
  map_comp {x y z} f g := by
    have f' := leOfHom f
    have g' := leOfHom g
    induction x <;> induction y <;> induction z <;> cat_disch

@[simp]
lemma eTruncLT_obj_top : t.eTruncLT.obj âŠ¤ = ğŸ­ _ := rfl

@[simp]
lemma eTruncLT_obj_bot : t.eTruncLT.obj âŠ¥ = 0 := rfl

@[simp]
lemma eTruncLT_obj_mk (n : â„¤) : t.eTruncLT.obj (EInt.mk n) = t.truncLT n := rfl

@[simp]
lemma eTruncLT_map_eq_truncLTÎ¹ (n : â„¤) :
    t.eTruncLT.map (homOfLE (show EInt.mk n â‰¤ âŠ¤ by simp)) = t.truncLTÎ¹ n := rfl

noncomputable def eTruncGE : EInt â¥¤ C â¥¤ C where
  obj n := by
    induction n with
    | bot => exact ğŸ­ C
    | coe a => exact t.truncGE a
    | top => exact 0
  map {x y} f := by
    induction x with
    | bot =>
      induction y with
      | bot => exact ğŸ™ _
      | coe b => exact t.truncGEÏ€ b
      | top => exact 0
    | coe a =>
      induction y with
      | bot => exact 0
      | coe b => exact t.natTransTruncGEOfLE a b (by simpa using leOfHom f)
      | top => exact 0
    | top =>
      induction y with
      | bot => exact 0
      | coe b => exact 0
      | top => exact ğŸ™ _
  map_id n := by induction n <;> simp
  map_comp {x y z} f g := by
    have f' := leOfHom f
    have g' := leOfHom g
    induction x <;> induction y <;> induction z <;> cat_disch

@[simp]
lemma eTruncGE_obj_bot :
    t.eTruncGE.obj âŠ¥ = ğŸ­ _ := rfl

@[simp]
lemma eTruncGE_obj_top :
    t.eTruncGE.obj âŠ¤ = 0 := rfl

@[simp]
lemma eTruncGE_obj_mk (n : â„¤) : t.eTruncGE.obj (EInt.mk n) = t.truncGE n := rfl

noncomputable def eTruncGEÎ´LTt :
    t.eTruncGE âŸ¶ t.eTruncLT â‹™ ((Functor.whiskeringRight C C C).obj (shiftFunctor C (1 : â„¤))) where
  app a := by
    induction a with
    | bot => exact 0
    | coe a => exact t.truncGEÎ´LT a
    | top => exact 0
  naturality {a b} hab := by
    replace hab := leOfHom hab
    induction a; rotate_right
    Â· apply (isZero_zero _).eq_of_src
    all_goals
      induction b <;> simp at hab <;>
        dsimp [eTruncGE, eTruncLT] <;>
        simp [t.truncGEÎ´LT_comp_whiskerRight_natTransTruncLTOfLE]

@[simp]
lemma eTruncGEÎ´LTt_mk (n : â„¤) :
    t.eTruncGEÎ´LTt.app (EInt.mk n) = t.truncGEÎ´LT n := rfl

noncomputable abbrev eTruncLTÎ¹ (i : EInt) : t.eTruncLT.obj i âŸ¶ ğŸ­ _ :=
  t.eTruncLT.map (homOfLE (le_top))

instance : IsIso (t.eTruncLTÎ¹ âŠ¤) := by
  dsimp [eTruncLTÎ¹]
  infer_instance

@[reassoc (attr := simp)]
lemma eTruncLT_map_app_eTruncLTÎ¹_app {i j : EInt} (f : i âŸ¶ j) (X : C) :
    (t.eTruncLT.map f).app X â‰« (t.eTruncLTÎ¹ j).app X = (t.eTruncLTÎ¹ i).app X := by
  simp only [â† NatTrans.comp_app, â† Functor.map_comp]
  rfl

noncomputable abbrev eTruncGEÏ€ (i : EInt) : ğŸ­ _ âŸ¶ t.eTruncGE.obj i :=
  t.eTruncGE.map (homOfLE (bot_le))

instance : IsIso (t.eTruncGEÏ€ âŠ¥) := by
  dsimp [eTruncGEÏ€]
  infer_instance

@[reassoc (attr := simp)]
lemma eTruncGEÏ€_app_eTruncGE_map_app {i j : EInt} (f : i âŸ¶ j) (X : C) :
    (t.eTruncGEÏ€ i).app X â‰« (t.eTruncGE.map f).app X = (t.eTruncGEÏ€ j).app X := by
  simp only [â† NatTrans.comp_app, â† Functor.map_comp]
  rfl

@[simps!]
noncomputable def eTriangleLTGE : EInt â¥¤ C â¥¤ Triangle C where
  obj i := Triangle.functorMk (t.eTruncLTÎ¹ i) (t.eTruncGEÏ€ i) (t.eTruncGEÎ´LTt.app i)
  map f := Triangle.functorHomMk _ _ (t.eTruncLT.map f) (ğŸ™ _) (t.eTruncGE.map f)

lemma eTriangleLTGE_distinguished (i : EInt) (X : C) :
    (t.eTriangleLTGE.obj i).obj X âˆˆ distTriang _ := by
  induction i with
  | bot =>
    rw [Triangle.distinguished_iff_of_isZeroâ‚ _ (Functor.zero_obj X)]
    dsimp
    infer_instance
  | coe n => exact t.triangleLTGE_distinguished n X
  | top =>
    rw [Triangle.distinguished_iff_of_isZeroâ‚ƒ _ (Functor.zero_obj X)]
    dsimp
    infer_instance

instance (X : C) (n : â„¤) [t.IsLE X n] (i : EInt) :
    t.IsLE ((t.eTruncLT.obj i).obj X) n := by
  induction i with
  | bot => exact isLE_of_isZero _ (by simp) _
  | coe _ => dsimp; infer_instance
  | top => dsimp; infer_instance

instance (X : C) (n : â„¤) [t.IsGE X n] (i : EInt) :
    t.IsGE ((t.eTruncGE.obj i).obj X) n := by
  induction i with
  | bot => dsimp; infer_instance
  | coe _ => dsimp; infer_instance
  | top => exact isGE_of_isZero _ (by simp) _

end TStructure

end Triangulated

end CategoryTheory
