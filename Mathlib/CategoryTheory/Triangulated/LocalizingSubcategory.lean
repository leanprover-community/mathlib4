/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Triangulated.Opposite.Subcategory
import Mathlib.CategoryTheory.Triangulated.Opposite.Functor
import Mathlib.CategoryTheory.Triangulated.Opposite.Triangulated
import Mathlib.CategoryTheory.Localization.Triangulated
import Mathlib.CategoryTheory.Localization.CalculusOfFractions.Lemmas

/-!
# Localizing subcategories

-/

namespace CategoryTheory

open Category Limits Pretriangulated Triangulated Pretriangulated.Opposite

namespace ObjectProperty

variable {A C D D' : Type*} [Category A] [Category C] [Category D] [Category D']
  [HasZeroObject C]
  [HasShift C ‚Ñ§]
  [Preadditive C]
  [‚àÄ (n : ‚Ñ§), (shiftFunctor C n).Additive]
  [Pretriangulated C]
  (B : ObjectProperty C) (F : A ‚•§ C)

class IsRightLocalizing [B.IsTriangulated] : Prop where
  fac {Y : C} {X : A} (œÜ : Y ‚ü∂ F.obj X) (hY : B Y) :
    ‚àÉ (Y' : A) (_ : B (F.obj Y')) (a : Y ‚ü∂ F.obj Y') (b : Y' ‚ü∂ X),
      a ‚â´ F.map b = œÜ

class IsLeftLocalizing [B.IsTriangulated]: Prop where
  fac {X : A} {Y : C} (œÜ : F.obj X ‚ü∂ Y) (hY : B Y) :
    ‚àÉ (Y' : A) (_ : B (F.obj Y')) (a : F.obj Y' ‚ü∂ Y) (b : X ‚ü∂ Y'),
      F.map b ‚â´ a = œÜ

variable [B.IsTriangulated]

lemma fac_of_isRightLocalizing [B.IsRightLocalizing F]
    {Y : C} {X : A} (œÜ : Y ‚ü∂ F.obj X) (hY : B Y) :
    ‚àÉ (Y' : A) (_ : B (F.obj Y')) (a : Y ‚ü∂ F.obj Y') (b : Y' ‚ü∂ X),
      a ‚â´ F.map b = œÜ :=
  IsRightLocalizing.fac œÜ hY

lemma fac_of_isLeftLocalizing [B.IsLeftLocalizing F]
    {X : A} {Y : C} (œÜ : F.obj X ‚ü∂ Y) (hY : B Y) :
    ‚àÉ (Y' : A) (_ : B (F.obj Y')) (a : F.obj Y' ‚ü∂ Y) (b : X ‚ü∂ Y'),
      F.map b ‚â´ a = œÜ :=
  IsLeftLocalizing.fac œÜ hY

open CategoryTheory.Pretriangulated.Opposite

instance [B.IsLeftLocalizing F] : B.op.IsRightLocalizing F.op where
  fac {Y X} œÜ hY := by
    obtain ‚ü®Y', hY', a, b, fac‚ü© := B.fac_of_isLeftLocalizing F œÜ.unop hY
    exact ‚ü®Opposite.op Y', hY', a.op, b.op, Quiver.Hom.unop_inj fac‚ü©

lemma isLeftLocalizing_of_op [B.op.IsRightLocalizing F.op] : B.IsLeftLocalizing F where
  fac {X Y} œÜ hY := by
    obtain ‚ü®Y', hY', a, b, fac‚ü© := B.op.fac_of_isRightLocalizing F.op œÜ.op hY
    exact ‚ü®Y'.unop, hY', a.unop, b.unop, Quiver.Hom.op_inj fac‚ü©

variable [HasZeroObject A]
  [HasShift A ‚Ñ§]
  [Preadditive A]
  [‚àÄ (n : ‚Ñ§), (shiftFunctor A n).Additive]
  [Pretriangulated A]
  [F.CommShift ‚Ñ§] [F.IsTriangulated]

section

variable [B.IsClosedUnderIsomorphisms]

/-- Factorization property of right localizing subcategories. -/
lemma fac_of_isRightLocalizing' [B.IsRightLocalizing F]
    {X : A} {Y : C} (s : F.obj X ‚ü∂ Y) (hs : B.trW s) :
    ‚àÉ (X' : A) (s' : X ‚ü∂ X') (_ : (B.inverseImage F).trW s') (b : Y ‚ü∂ F.obj X'),
      s ‚â´ b = F.map s' := by
  rw [ObjectProperty.trW_iff'] at hs
  obtain ‚ü®W, a, b, hT, hW‚ü© := hs
  obtain ‚ü®W', hW', c, d, fac‚ü© := B.fac_of_isRightLocalizing F a hW
  obtain ‚ü®U, e, f, hT'‚ü© := Pretriangulated.distinguished_cocone_triangle d
  obtain ‚ü®Œ≤, hŒ≤, _‚ü© := complete_distinguished_triangle_morphism _ _ hT (F.map_distinguished _ hT')
    c (ùüô _) (by simpa using fac.symm)
  dsimp at Œ≤ hŒ≤
  refine ‚ü®U, e, ?_, Œ≤, by simpa using hŒ≤‚ü©
  rw [ObjectProperty.trW_iff']
  exact ‚ü®_, _, _, hT', hW'‚ü©

/-- Factorization property of left localizing subcategories. -/
lemma fac_of_isLeftLocalizing' [B.IsLeftLocalizing F]
    {X : A} {Y : C} (s : Y ‚ü∂ F.obj X) (hs : B.trW s) :
    ‚àÉ (X' : A) (s' : X' ‚ü∂ X) (_ : (B.inverseImage F).trW s') (b : F.obj X' ‚ü∂ Y),
      b ‚â´ s = F.map s' := by
  obtain ‚ü®X', s', hs', b, fac‚ü© := B.op.fac_of_isRightLocalizing' F.op s.op
    (by simpa only [ObjectProperty.trW_op_iff] using hs)
  refine ‚ü®X'.unop, s'.unop, ?_, b.unop, Quiver.Hom.op_inj fac‚ü©
  rw [‚Üê trW_op_iff]
  exact hs'

variable {B F} in
/-- Constructor for right localizing categories. -/
lemma IsRightLocalizing.mk'
    (h : ‚àÄ ‚¶ÉX : A‚¶Ñ ‚¶ÉY : C‚¶Ñ (s : F.obj X ‚ü∂ Y) (_ : B.trW s),
      ‚àÉ (X' : A) (s' : X ‚ü∂ X') (_ : (B.inverseImage F).trW s')
        (b : Y ‚ü∂ F.obj X'), s ‚â´ b = F.map s') :
    B.IsRightLocalizing F where
  fac {Y X} œÜ hY := by
    obtain ‚ü®Z, s, b, hT‚ü© := Pretriangulated.distinguished_cocone_triangle œÜ
    have hs : B.trW s := by
      rw [trW_iff']
      exact ‚ü®_, _, _, hT, hY‚ü©
    obtain ‚ü®W, s', hs', c, fac‚ü© := h s hs
    obtain ‚ü®U, d, e, hT'‚ü© := distinguished_cocone_triangle‚ÇÅ s'
    obtain ‚ü®Œ≤, hŒ≤, _‚ü© := complete_distinguished_triangle_morphism‚ÇÅ _ _ hT
      (F.map_distinguished _ hT') (ùüô _) c (by simpa using fac)
    dsimp at Œ≤ hŒ≤
    refine ‚ü®U, (B.trW_iff_of_distinguished' _ (F.map_distinguished _ hT')).1 ?_,
      Œ≤, d, by simpa using hŒ≤.symm‚ü©
    rw [trW_iff] at hs' ‚ä¢
    obtain ‚ü®_, _, _, hT'', hV‚ü© := hs'
    exact ‚ü®_, _, _, F.map_distinguished _ hT'', hV‚ü©

variable {B F} in
/-- Constructor for left localizing categories. -/
lemma IsLeftLocalizing.mk'
    (h : ‚àÄ ‚¶ÉY : C‚¶Ñ ‚¶ÉX : A‚¶Ñ (s : Y ‚ü∂ F.obj X) (_ : B.trW s),
      ‚àÉ (X' : A) (s' : X' ‚ü∂ X) (_ : (B.inverseImage F).trW s')
        (b : F.obj X' ‚ü∂ Y), b ‚â´ s = F.map s') :
    B.IsLeftLocalizing F := by
  have : B.op.IsRightLocalizing F.op := IsRightLocalizing.mk' (fun X Y s hs => by
    obtain ‚ü®X', s', h, b, fac‚ü© := h s.unop (trW_of_op _ _ hs)
    exact ‚ü®Opposite.op X', s'.op, trW_of_unop _ _ h, b.op, Quiver.Hom.unop_inj fac‚ü©)
  exact B.isLeftLocalizing_of_op F

end

variable (L : C ‚•§ D) [L.IsLocalization B.trW]

section

variable [B.IsClosedUnderIsomorphisms]
  (L' : A ‚•§ D') [L'.IsLocalization (B.inverseImage F).trW]
  (F' : D' ‚•§ D) [Localization.Lifting L' (B.inverseImage F).trW (F ‚ãô L) F']
  [IsTriangulated C]

noncomputable def full_of_isRightLocalizing [B.IsRightLocalizing F] [F.Full] : F'.Full := by
  have := Localization.essSurj L' (B.inverseImage F).trW
  apply F'.full_of_precomp_essSurj L'
  intro X‚ÇÅ X‚ÇÇ œÜ
  have e := Localization.Lifting.iso L' (B.inverseImage F).trW (F ‚ãô L) F'
  obtain ‚ü®œÜ', hœÜ'‚ü© : ‚àÉ œÜ', œÜ = e.hom.app X‚ÇÅ ‚â´ œÜ' ‚â´ e.inv.app X‚ÇÇ :=
    ‚ü®e.inv.app X‚ÇÅ ‚â´ œÜ ‚â´ e.hom.app X‚ÇÇ, by simp‚ü©
  obtain ‚ü®f, hf‚ü© := Localization.exists_leftFraction L B.trW œÜ'
  obtain ‚ü®X‚ÇÉ, s', hs', t, fac‚ü© := B.fac_of_isRightLocalizing' F f.s f.hs
  let f' : (B.inverseImage F).trW.LeftFraction X‚ÇÅ X‚ÇÇ :=
    { f := F.preimage (f.f ‚â´ t)
      s := F.preimage (f.s ‚â´ t)
      hs := by
        rw [B.inverseImage_trW_iff, F.map_preimage, fac, ‚Üê B.inverseImage_trW_iff F]
        exact hs' }
  have hf' : œÜ' ‚â´ L.map (F.map f'.s) = L.map (F.map f'.f) := by
    replace hf := hf =‚â´ L.map (f.s)
    rw [f.map_comp_map_s] at hf
    dsimp
    rw [F.map_preimage, F.map_preimage, L.map_comp, L.map_comp, reassoc_of% hf]
  have : IsIso (F'.map (L'.map f'.s)) :=
    ((MorphismProperty.isomorphisms D).arrow_mk_iso_iff
      ((Arrow.isoOfNatIso e) (Arrow.mk f'.s))).2
        (Localization.inverts _ B.trW _
          (by simpa only [‚Üê B.inverseImage_trW_iff F] using f'.hs))
  refine ‚ü®f'.map L' (Localization.inverts _ _), ?_‚ü©
  rw [hœÜ', ‚Üê cancel_mono (F'.map (L'.map f'.s)), ‚Üê F'.map_comp, f'.map_comp_map_s,
    assoc, assoc]
  erw [‚Üê e.inv.naturality]
  rw [Functor.comp_map, reassoc_of% hf']
  erw [e.inv.naturality, e.hom_inv_id_app_assoc]
  rfl

include L L' in
lemma faithful_of_isRightLocalizing [B.IsRightLocalizing F] [F.Full] [F.Faithful] :
    F'.Faithful := by
  have e := Localization.Lifting.iso L' (B.inverseImage F).trW (F ‚ãô L) F'
  have := IsTriangulated.of_fully_faithful_triangulated_functor F
  letI := Localization.preadditive L' (B.inverseImage F).trW
  letI := Localization.functor_additive L' (B.inverseImage F).trW
  letI := Localization.preadditive L B.trW
  letI := Localization.functor_additive L B.trW
  have : (B.inverseImage F).trW.HasLeftCalculusOfFractions := inferInstance
  have : F'.Additive := by
    rw [Localization.functor_additive_iff L' (B.inverseImage F).trW]
    exact Functor.additive_of_iso e.symm
  apply F'.faithful_of_precomp_cancel_zero_of_hasLeftCalculusOfFractions L' (B.inverseImage F).trW
  intro X‚ÇÅ X‚ÇÇ f hf
  replace hf : L.map (F.map f) = L.map 0 := by
    erw [L.map_zero, ‚Üê NatIso.naturality_1 e f, hf, zero_comp, comp_zero]
  rw [MorphismProperty.map_eq_iff_postcomp L B.trW] at hf
  obtain ‚ü®Z, s, hs, fac‚ü© := hf
  rw [zero_comp] at fac
  obtain ‚ü®W, s', hs', t, fac'‚ü© := B.fac_of_isRightLocalizing' F s hs
  have hfs' : f ‚â´ s' = 0 := F.map_injective (by
    rw [F.map_zero, F.map_comp, ‚Üê fac', reassoc_of% fac, zero_comp])
  have := Localization.inverts L' (B.inverseImage F).trW s' hs'
  rw [‚Üê cancel_mono (L'.map s'), zero_comp, ‚Üê L'.map_comp, hfs', L'.map_zero]

end

variable {L : C ‚•§ D} {L' : A ‚•§ D'} {H : D' ‚•§ D} (e : L' ‚ãô H ‚âÖ F ‚ãô L)
  [L'.EssSurj] [H.Full] [H.Faithful] [L.IsLocalization B.trW]
  [IsTriangulated C] [B.IsClosedUnderIsomorphisms]

include e in
lemma isLocalization_of_isRightLocalizing [B.IsRightLocalizing F] [F.Full] [F.Faithful]:
    L'.IsLocalization (B.inverseImage F).trW := by
  have hL' : (B.inverseImage F).trW.IsInvertedBy L' := fun X‚ÇÅ X‚ÇÇ f hf => by
    rw [B.inverseImage_trW_iff] at hf
    have : IsIso (H.map (L'.map f)) :=
      ((MorphismProperty.isomorphisms D).arrow_mk_iso_iff
        (Arrow.isoOfNatIso e f)).2 (Localization.inverts L B.trW _ hf)
    apply isIso_of_fully_faithful H
  let G := Localization.lift _ hL' (B.inverseImage F).trW.Q
  have eG : (B.inverseImage F).trW.Q ‚ãô G ‚âÖ L' :=
    Localization.Lifting.iso _ (B.inverseImage F).trW _ _
  have : Localization.Lifting (B.inverseImage F).trW.Q (B.inverseImage F).trW
    (F ‚ãô L) (G ‚ãô H) :=
    ‚ü®(Functor.associator _ _ _).symm ‚â™‚â´ isoWhiskerRight eG H ‚â™‚â´ e‚ü©
  have := full_of_isRightLocalizing B F L (B.inverseImage F).trW.Q (G ‚ãô H)
  have := faithful_of_isRightLocalizing B F L (B.inverseImage F).trW.Q (G ‚ãô H)
  have : G.EssSurj :=
    { mem_essImage := fun X =>
        ‚ü®_, ‚ü®eG.app (L'.objPreimage X) ‚â™‚â´ L'.objObjPreimageIso X‚ü©‚ü© }
  have : G.Full := Functor.Full.of_comp_faithful G H
  have : G.Faithful := Functor.Faithful.of_comp_iso (Iso.refl (G ‚ãô H))
  have : G.IsEquivalence := { }
  exact Functor.IsLocalization.of_equivalence_target (B.inverseImage F).trW.Q _ _
    G.asEquivalence eG

include e in
lemma isLocalization_of_isLeftLocalizing [B.IsLeftLocalizing F] [F.Full] [F.Faithful] :
    L'.IsLocalization (B.inverseImage F).trW := by
  rw [Functor.isLocalization_iff_op, ‚Üê trW_op]
  have : Functor.IsLocalization L.op (B.op.trW) := by
    rw [trW_op]
    infer_instance
  let e' : L'.op ‚ãô H.op ‚âÖ F.op ‚ãô L.op := NatIso.op e.symm
  exact B.op.isLocalization_of_isRightLocalizing F.op e'

end ObjectProperty

end CategoryTheory
