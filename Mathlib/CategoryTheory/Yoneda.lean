/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Functor.Hom
import Mathlib.CategoryTheory.Functor.Currying
import Mathlib.CategoryTheory.Products.Basic

#align_import category_theory.yoneda from "leanprover-community/mathlib"@"369525b73f229ccd76a6ec0e0e0bf2be57599768"

/-!
# The Yoneda embedding

The Yoneda embedding as a functor `yoneda : C ‚•§ (C·µí·µñ ‚•§ Type v‚ÇÅ)`,
along with an instance that it is `FullyFaithful`.

Also the Yoneda lemma, `yonedaLemma : (yoneda_pairing C) ‚âÖ (yoneda_evaluation C)`.

## References
* [Stacks: Opposite Categories and the Yoneda Lemma](https://stacks.math.columbia.edu/tag/001L)
-/


namespace CategoryTheory

open Opposite

universe v‚ÇÅ u‚ÇÅ u‚ÇÇ

-- morphism levels before object levels. See note [CategoryTheory universes].
variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]

/-- The Yoneda embedding, as a functor from `C` into presheaves on `C`.

See <https://stacks.math.columbia.edu/tag/001O>.
-/
@[simps]
def yoneda : C ‚•§ C·µí·µñ ‚•§ Type v‚ÇÅ where
  obj X :=
    { obj := fun Y => unop Y ‚ü∂ X
      map := fun f g => f.unop ‚â´ g }
  map f :=
    { app := fun Y g => g ‚â´ f }
#align category_theory.yoneda CategoryTheory.yoneda

/-- The co-Yoneda embedding, as a functor from `C·µí·µñ` into co-presheaves on `C`.
-/
@[simps]
def coyoneda : C·µí·µñ ‚•§ C ‚•§ Type v‚ÇÅ where
  obj X :=
    { obj := fun Y => unop X ‚ü∂ Y
      map := fun f g => g ‚â´ f }
  map f :=
    { app := fun Y g => f.unop ‚â´ g }
#align category_theory.coyoneda CategoryTheory.coyoneda

namespace Yoneda

theorem obj_map_id {X Y : C} (f : op X ‚ü∂ op Y) :
    (yoneda.obj X).map f (ùüô X) = (yoneda.map f.unop).app (op Y) (ùüô Y) := by
  dsimp
  simp
#align category_theory.yoneda.obj_map_id CategoryTheory.Yoneda.obj_map_id

@[simp]
theorem naturality {X Y : C} (Œ± : yoneda.obj X ‚ü∂ yoneda.obj Y) {Z Z' : C} (f : Z ‚ü∂ Z')
    (h : Z' ‚ü∂ X) : f ‚â´ Œ±.app (op Z') h = Œ±.app (op Z) (f ‚â´ h) :=
  (FunctorToTypes.naturality _ _ Œ± f.op h).symm
#align category_theory.yoneda.naturality CategoryTheory.Yoneda.naturality

/-- The Yoneda embedding is full.

See <https://stacks.math.columbia.edu/tag/001P>.
-/
instance yonedaFull : Full (yoneda : C ‚•§ C·µí·µñ ‚•§ Type v‚ÇÅ) where
  preimage {X} {Y} f := f.app (op X) (ùüô X)
#align category_theory.yoneda.yoneda_full CategoryTheory.Yoneda.yonedaFull

/-- The Yoneda embedding is faithful.

See <https://stacks.math.columbia.edu/tag/001P>.
-/
instance yoneda_faithful : Faithful (yoneda : C ‚•§ C·µí·µñ ‚•§ Type v‚ÇÅ) where
  map_injective {X} {Y} f g p := by
    convert congr_fun (congr_app p (op X)) (ùüô X) using 1 <;> dsimp <;> simp
#align category_theory.yoneda.yoneda_faithful CategoryTheory.Yoneda.yoneda_faithful

/-- Extensionality via Yoneda. The typical usage would be
```
-- Goal is `X ‚âÖ Y`
apply yoneda.ext,
-- Goals are now functions `(Z ‚ü∂ X) ‚Üí (Z ‚ü∂ Y)`, `(Z ‚ü∂ Y) ‚Üí (Z ‚ü∂ X)`, and the fact that these
-- functions are inverses and natural in `Z`.
```
-/
def ext (X Y : C) (p : ‚àÄ {Z : C}, (Z ‚ü∂ X) ‚Üí (Z ‚ü∂ Y)) (q : ‚àÄ {Z : C}, (Z ‚ü∂ Y) ‚Üí (Z ‚ü∂ X))
    (h‚ÇÅ : ‚àÄ {Z : C} (f : Z ‚ü∂ X), q (p f) = f) (h‚ÇÇ : ‚àÄ {Z : C} (f : Z ‚ü∂ Y), p (q f) = f)
    (n : ‚àÄ {Z Z' : C} (f : Z' ‚ü∂ Z) (g : Z ‚ü∂ X), p (f ‚â´ g) = f ‚â´ p g) : X ‚âÖ Y :=
  yoneda.preimageIso
    (NatIso.ofComponents fun Z =>
      { hom := p
        inv := q })
#align category_theory.yoneda.ext CategoryTheory.Yoneda.ext

/-- If `yoneda.map f` is an isomorphism, so was `f`.
-/
theorem isIso {X Y : C} (f : X ‚ü∂ Y) [IsIso (yoneda.map f)] : IsIso f :=
  isIso_of_fully_faithful yoneda f
#align category_theory.yoneda.is_iso CategoryTheory.Yoneda.isIso

end Yoneda

namespace Coyoneda

@[simp]
theorem naturality {X Y : C·µí·µñ} (Œ± : coyoneda.obj X ‚ü∂ coyoneda.obj Y) {Z Z' : C} (f : Z' ‚ü∂ Z)
    (h : unop X ‚ü∂ Z') : Œ±.app Z' h ‚â´ f = Œ±.app Z (h ‚â´ f) :=
  (FunctorToTypes.naturality _ _ Œ± f h).symm
#align category_theory.coyoneda.naturality CategoryTheory.Coyoneda.naturality

instance coyonedaFull : Full (coyoneda : C·µí·µñ ‚•§ C ‚•§ Type v‚ÇÅ) where
  preimage {X} _ f := (f.app _ (ùüô X.unop)).op
  witness {X} {Y} f := by simp only [coyoneda]; aesop_cat
#align category_theory.coyoneda.coyoneda_full CategoryTheory.Coyoneda.coyonedaFull

instance coyoneda_faithful : Faithful (coyoneda : C·µí·µñ ‚•§ C ‚•§ Type v‚ÇÅ) where
  map_injective {X} _ _ _ p := by
    have t := congr_fun (congr_app p X.unop) (ùüô _)
    simpa using congr_arg Quiver.Hom.op t
#align category_theory.coyoneda.coyoneda_faithful CategoryTheory.Coyoneda.coyoneda_faithful

/-- If `coyoneda.map f` is an isomorphism, so was `f`.
-/
theorem isIso {X Y : C·µí·µñ} (f : X ‚ü∂ Y) [IsIso (coyoneda.map f)] : IsIso f :=
  isIso_of_fully_faithful coyoneda f
#align category_theory.coyoneda.is_iso CategoryTheory.Coyoneda.isIso

/-- The identity functor on `Type` is isomorphic to the coyoneda functor coming from `punit`. -/
def punitIso : coyoneda.obj (Opposite.op PUnit) ‚âÖ ùü≠ (Type v‚ÇÅ) :=
  NatIso.ofComponents fun X =>
    { hom := fun f => f ‚ü®‚ü©
      inv := fun x _ => x }
#align category_theory.coyoneda.punit_iso CategoryTheory.Coyoneda.punitIso

/-- Taking the `unop` of morphisms is a natural isomorphism. -/
@[simps!]
def objOpOp (X : C) : coyoneda.obj (op (op X)) ‚âÖ yoneda.obj X :=
  NatIso.ofComponents fun _ => (opEquiv _ _).toIso
#align category_theory.coyoneda.obj_op_op CategoryTheory.Coyoneda.objOpOp

end Coyoneda

namespace Functor

/-- A functor `F : C·µí·µñ ‚•§ Type v‚ÇÅ` is representable if there is object `X` so `F ‚âÖ yoneda.obj X`.

See <https://stacks.math.columbia.edu/tag/001Q>.
-/
class Representable (F : C·µí·µñ ‚•§ Type v‚ÇÅ) : Prop where
  /-- `Hom(-,X) ‚âÖ F` via `f` -/
  has_representation : ‚àÉ (X : _) (f : yoneda.obj X ‚ü∂ F), IsIso f
#align category_theory.functor.representable CategoryTheory.Functor.Representable

instance {X : C} : Representable (yoneda.obj X) where has_representation := ‚ü®X, ùüô _, inferInstance‚ü©

/-- A functor `F : C ‚•§ Type v‚ÇÅ` is corepresentable if there is object `X` so `F ‚âÖ coyoneda.obj X`.

See <https://stacks.math.columbia.edu/tag/001Q>.
-/
class Corepresentable (F : C ‚•§ Type v‚ÇÅ) : Prop where
  /-- `Hom(X,-) ‚âÖ F` via `f` -/
  has_corepresentation : ‚àÉ (X : _) (f : coyoneda.obj X ‚ü∂ F), IsIso f
#align category_theory.functor.corepresentable CategoryTheory.Functor.Corepresentable

instance {X : C·µí·µñ} : Corepresentable (coyoneda.obj X) where
  has_corepresentation := ‚ü®X, ùüô _, inferInstance‚ü©

-- instance : corepresentable (ùü≠ (Type v‚ÇÅ)) :=
-- corepresentable_of_nat_iso (op punit) coyoneda.punit_iso
section Representable

variable (F : C·µí·µñ ‚•§ Type v‚ÇÅ)

variable [F.Representable]

/-- The representing object for the representable functor `F`. -/
noncomputable def reprX : C :=
  (Representable.has_representation : ‚àÉ (_ : _) (_ : _ ‚ü∂ F), _).choose
set_option linter.uppercaseLean3 false
#align category_theory.functor.repr_X CategoryTheory.Functor.reprX

/-- The (forward direction of the) isomorphism witnessing `F` is representable. -/
noncomputable def reprF : yoneda.obj F.reprX ‚ü∂ F :=
  Representable.has_representation.choose_spec.choose
#align category_theory.functor.repr_f CategoryTheory.Functor.reprF

/-- The representing element for the representable functor `F`, sometimes called the universal
element of the functor.
-/
noncomputable def reprx : F.obj (op F.reprX) :=
  F.reprF.app (op F.reprX) (ùüô F.reprX)
#align category_theory.functor.repr_x CategoryTheory.Functor.reprx

instance : IsIso F.reprF :=
  Representable.has_representation.choose_spec.choose_spec

/-- An isomorphism between `F` and a functor of the form `C(-, F.repr_X)`.  Note the components
`F.repr_w.app X` definitionally have type `(X.unop ‚ü∂ F.repr_X) ‚âÖ F.obj X`.
-/
noncomputable def reprW : yoneda.obj F.reprX ‚âÖ F :=
  asIso F.reprF
#align category_theory.functor.repr_w CategoryTheory.Functor.reprW

@[simp]
theorem reprW_hom : F.reprW.hom = F.reprF :=
  rfl
#align category_theory.functor.repr_w_hom CategoryTheory.Functor.reprW_hom

theorem reprW_app_hom (X : C·µí·µñ) (f : unop X ‚ü∂ F.reprX) :
    (F.reprW.app X).hom f = F.map f.op F.reprx := by
  change F.reprF.app X f = (F.reprF.app (op F.reprX) ‚â´ F.map f.op) (ùüô F.reprX)
  rw [‚Üê F.reprF.naturality]
  dsimp
  simp
#align category_theory.functor.repr_w_app_hom CategoryTheory.Functor.reprW_app_hom

end Representable

section Corepresentable

variable (F : C ‚•§ Type v‚ÇÅ)

variable [F.Corepresentable]

/-- The representing object for the corepresentable functor `F`. -/
noncomputable def coreprX : C :=
  (Corepresentable.has_corepresentation : ‚àÉ (_ : _) (_ : _ ‚ü∂ F), _).choose.unop
set_option linter.uppercaseLean3 false
#align category_theory.functor.corepr_X CategoryTheory.Functor.coreprX

/-- The (forward direction of the) isomorphism witnessing `F` is corepresentable. -/
noncomputable def coreprF : coyoneda.obj (op F.coreprX) ‚ü∂ F :=
  Corepresentable.has_corepresentation.choose_spec.choose
#align category_theory.functor.corepr_f CategoryTheory.Functor.coreprF

/-- The representing element for the corepresentable functor `F`, sometimes called the universal
element of the functor.
-/
noncomputable def coreprx : F.obj F.coreprX :=
  F.coreprF.app F.coreprX (ùüô F.coreprX)
#align category_theory.functor.corepr_x CategoryTheory.Functor.coreprx

instance : IsIso F.coreprF :=
  Corepresentable.has_corepresentation.choose_spec.choose_spec

/-- An isomorphism between `F` and a functor of the form `C(F.corepr X, -)`. Note the components
`F.corepr_w.app X` definitionally have type `F.corepr_X ‚ü∂ X ‚âÖ F.obj X`.
-/
noncomputable def coreprW : coyoneda.obj (op F.coreprX) ‚âÖ F :=
  asIso F.coreprF
#align category_theory.functor.corepr_w CategoryTheory.Functor.coreprW

theorem coreprW_app_hom (X : C) (f : F.coreprX ‚ü∂ X) :
    (F.coreprW.app X).hom f = F.map f F.coreprx := by
  change F.coreprF.app X f = (F.coreprF.app F.coreprX ‚â´ F.map f) (ùüô F.coreprX)
  rw [‚Üê F.coreprF.naturality]
  dsimp
  simp
#align category_theory.functor.corepr_w_app_hom CategoryTheory.Functor.coreprW_app_hom

end Corepresentable

end Functor

theorem representable_of_nat_iso (F : C·µí·µñ ‚•§ Type v‚ÇÅ) {G} (i : F ‚âÖ G) [F.Representable] :
    G.Representable :=
  { has_representation := ‚ü®F.reprX, F.reprF ‚â´ i.hom, inferInstance‚ü© }
#align category_theory.representable_of_nat_iso CategoryTheory.representable_of_nat_iso

theorem corepresentable_of_nat_iso (F : C ‚•§ Type v‚ÇÅ) {G} (i : F ‚âÖ G) [F.Corepresentable] :
    G.Corepresentable :=
  { has_corepresentation := ‚ü®op F.coreprX, F.coreprF ‚â´ i.hom, inferInstance‚ü© }
#align category_theory.corepresentable_of_nat_iso CategoryTheory.corepresentable_of_nat_iso

instance : Functor.Corepresentable (ùü≠ (Type v‚ÇÅ)) :=
  corepresentable_of_nat_iso (coyoneda.obj (op PUnit)) Coyoneda.punitIso

open Opposite

variable (C)

-- We need to help typeclass inference with some awkward universe levels here.
instance prodCategoryInstance1 : Category ((C·µí·µñ ‚•§ Type v‚ÇÅ) √ó C·µí·µñ) :=
  CategoryTheory.prod.{max u‚ÇÅ v‚ÇÅ, v‚ÇÅ} (C·µí·µñ ‚•§ Type v‚ÇÅ) C·µí·µñ
#align category_theory.prod_category_instance_1 CategoryTheory.prodCategoryInstance1

instance prodCategoryInstance2 : Category (C·µí·µñ √ó (C·µí·µñ ‚•§ Type v‚ÇÅ)) :=
  CategoryTheory.prod.{v‚ÇÅ, max u‚ÇÅ v‚ÇÅ} C·µí·µñ (C·µí·µñ ‚•§ Type v‚ÇÅ)
#align category_theory.prod_category_instance_2 CategoryTheory.prodCategoryInstance2

open Yoneda

/-- The "Yoneda evaluation" functor, which sends `X : C·µí·µñ` and `F : C·µí·µñ ‚•§ Type`
to `F.obj X`, functorially in both `X` and `F`.
-/
def yonedaEvaluation : C·µí·µñ √ó (C·µí·µñ ‚•§ Type v‚ÇÅ) ‚•§ Type max u‚ÇÅ v‚ÇÅ :=
  evaluationUncurried C·µí·µñ (Type v‚ÇÅ) ‚ãô uliftFunctor.{u‚ÇÅ}
#align category_theory.yoneda_evaluation CategoryTheory.yonedaEvaluation

@[simp]
theorem yonedaEvaluation_map_down (P Q : C·µí·µñ √ó (C·µí·µñ ‚•§ Type v‚ÇÅ)) (Œ± : P ‚ü∂ Q)
    (x : (yonedaEvaluation C).obj P) :
    ((yonedaEvaluation C).map Œ± x).down = Œ±.2.app Q.1 (P.2.map Œ±.1 x.down) :=
  rfl
#align category_theory.yoneda_evaluation_map_down CategoryTheory.yonedaEvaluation_map_down

/-- The "Yoneda pairing" functor, which sends `X : C·µí·µñ` and `F : C·µí·µñ ‚•§ Type`
to `yoneda.op.obj X ‚ü∂ F`, functorially in both `X` and `F`.
-/
def yonedaPairing : C·µí·µñ √ó (C·µí·µñ ‚•§ Type v‚ÇÅ) ‚•§ Type max u‚ÇÅ v‚ÇÅ :=
  Functor.prod yoneda.op (ùü≠ (C·µí·µñ ‚•§ Type v‚ÇÅ)) ‚ãô Functor.hom (C·µí·µñ ‚•§ Type v‚ÇÅ)
#align category_theory.yoneda_pairing CategoryTheory.yonedaPairing

-- Porting note: we need to provide this `@[ext]` lemma separately,
-- as `ext` will not look through the definition.
-- See https://github.com/leanprover-community/mathlib4/issues/5229
@[ext]
lemma yonedaPairingExt {x y : (yonedaPairing C).obj X} (w : ‚àÄ Y, x.app Y = y.app Y) : x = y :=
  NatTrans.ext _ _ (funext w)

@[simp]
theorem yonedaPairing_map (P Q : C·µí·µñ √ó (C·µí·µñ ‚•§ Type v‚ÇÅ)) (Œ± : P ‚ü∂ Q) (Œ≤ : (yonedaPairing C).obj P) :
    (yonedaPairing C).map Œ± Œ≤ = yoneda.map Œ±.1.unop ‚â´ Œ≤ ‚â´ Œ±.2 :=
  rfl
#align category_theory.yoneda_pairing_map CategoryTheory.yonedaPairing_map

/-- The Yoneda lemma asserts that the Yoneda pairing
`(X : C·µí·µñ, F : C·µí·µñ ‚•§ Type) ‚Ü¶ (yoneda.obj (unop X) ‚ü∂ F)`
is naturally isomorphic to the evaluation `(X, F) ‚Ü¶ F.obj X`.

See <https://stacks.math.columbia.edu/tag/001P>.
-/
def yonedaLemma : yonedaPairing C ‚âÖ yonedaEvaluation C where
  hom :=
    { app := fun F x => ULift.up ((x.app F.1) (ùüô (unop F.1)))
      naturality := by
        intro X Y f
        simp only [yonedaEvaluation]
        ext
        dsimp
        erw [Category.id_comp, ‚ÜêFunctorToTypes.naturality]
        simp only [Category.comp_id, yoneda_obj_map] }
  inv :=
    { app := fun F x =>
        { app := fun X a => (F.2.map a.op) x.down
          naturality := by
            intro X Y f
            ext
            dsimp
            rw [FunctorToTypes.map_comp_apply] }
      naturality := by
        intro X Y f
        simp only [yoneda]
        ext
        dsimp
        rw [‚ÜêFunctorToTypes.naturality X.snd Y.snd f.snd, FunctorToTypes.map_comp_apply] }
  hom_inv_id := by
    ext
    dsimp
    erw [‚Üê FunctorToTypes.naturality, obj_map_id]
    simp only [yoneda_map_app, Quiver.Hom.unop_op]
    erw [Category.id_comp]
  inv_hom_id := by
    ext
    dsimp
    rw [FunctorToTypes.map_id_apply, ULift.up_down]
#align category_theory.yoneda_lemma CategoryTheory.yonedaLemma

variable {C}

/-- The isomorphism between `yoneda.obj X ‚ü∂ F` and `F.obj (op X)`
(we need to insert a `ulift` to get the universes right!)
given by the Yoneda lemma.
-/
@[simps!]
def yonedaSections (X : C) (F : C·µí·µñ ‚•§ Type v‚ÇÅ) : (yoneda.obj X ‚ü∂ F) ‚âÖ ULift.{u‚ÇÅ} (F.obj (op X)) :=
  (yonedaLemma C).app (op X, F)
#align category_theory.yoneda_sections CategoryTheory.yonedaSections

/-- We have a type-level equivalence between natural transformations from the yoneda embedding
and elements of `F.obj X`, without any universe switching.
-/
def yonedaEquiv {X : C} {F : C·µí·µñ ‚•§ Type v‚ÇÅ} : (yoneda.obj X ‚ü∂ F) ‚âÉ F.obj (op X) :=
  (yonedaSections X F).toEquiv.trans Equiv.ulift
#align category_theory.yoneda_equiv CategoryTheory.yonedaEquiv

@[simp]
theorem yonedaEquiv_apply {X : C} {F : C·µí·µñ ‚•§ Type v‚ÇÅ} (f : yoneda.obj X ‚ü∂ F) :
    yonedaEquiv f = f.app (op X) (ùüô X) :=
  rfl
#align category_theory.yoneda_equiv_apply CategoryTheory.yonedaEquiv_apply

@[simp]
theorem yonedaEquiv_symm_app_apply {X : C} {F : C·µí·µñ ‚•§ Type v‚ÇÅ} (x : F.obj (op X)) (Y : C·µí·µñ)
    (f : Y.unop ‚ü∂ X) : (yonedaEquiv.symm x).app Y f = F.map f.op x :=
  rfl
#align category_theory.yoneda_equiv_symm_app_apply CategoryTheory.yonedaEquiv_symm_app_apply

theorem yonedaEquiv_naturality {X Y : C} {F : C·µí·µñ ‚•§ Type v‚ÇÅ} (f : yoneda.obj X ‚ü∂ F) (g : Y ‚ü∂ X) :
    F.map g.op (yonedaEquiv f) = yonedaEquiv (yoneda.map g ‚â´ f) := by
  change (f.app (op X) ‚â´ F.map g.op) (ùüô X) = f.app (op Y) (ùüô Y ‚â´ g)
  rw [‚Üê f.naturality]
  dsimp
  simp
#align category_theory.yoneda_equiv_naturality CategoryTheory.yonedaEquiv_naturality

lemma yonedaEquiv_naturality' {X Y : C·µí·µñ} {F : C·µí·µñ ‚•§ Type v‚ÇÅ} (f : yoneda.obj (unop X) ‚ü∂ F)
    (g : X ‚ü∂ Y) : F.map g (yonedaEquiv f) = yonedaEquiv (yoneda.map g.unop ‚â´ f) :=
  yonedaEquiv_naturality _ _

lemma yonedaEquiv_comp {X : C} {F G : C·µí·µñ ‚•§ Type v‚ÇÅ} (Œ± : yoneda.obj X ‚ü∂ F) (Œ≤ : F ‚ü∂ G)  :
    yonedaEquiv (Œ± ‚â´ Œ≤) = Œ≤.app _ (yonedaEquiv Œ±) :=
  rfl

lemma yonedaEquiv_comp' {X : C·µí·µñ} {F G : C·µí·µñ ‚•§ Type v‚ÇÅ} (Œ± : yoneda.obj (unop X) ‚ü∂ F) (Œ≤ : F ‚ü∂ G)  :
    yonedaEquiv (Œ± ‚â´ Œ≤) = Œ≤.app X (yonedaEquiv Œ±) :=
  rfl

@[simp]
lemma yonedaEquiv_yoneda_map {X Y : C} (f : X ‚ü∂ Y) : yonedaEquiv (yoneda.map f) = f := by
  rw [yonedaEquiv_apply]
  simp

lemma yonedaEquiv_symm_map {X Y : C·µí·µñ} (f : X ‚ü∂ Y) {F : C·µí·µñ ‚•§ Type v‚ÇÅ} (t : F.obj X) :
    yonedaEquiv.symm (F.map f t) = yoneda.map f.unop ‚â´ yonedaEquiv.symm t := by
  obtain ‚ü®u, rfl‚ü© := yonedaEquiv.surjective t
  rw [yonedaEquiv_naturality', Equiv.symm_apply_apply, Equiv.symm_apply_apply]

/-- When `C` is a small category, we can restate the isomorphism from `yoneda_sections`
without having to change universes.
-/
def yonedaSectionsSmall {C : Type u‚ÇÅ} [SmallCategory C] (X : C) (F : C·µí·µñ ‚•§ Type u‚ÇÅ) :
    (yoneda.obj X ‚ü∂ F) ‚âÖ F.obj (op X) :=
  yonedaSections X F ‚â™‚â´ uliftTrivial _
#align category_theory.yoneda_sections_small CategoryTheory.yonedaSectionsSmall

@[simp]
theorem yonedaSectionsSmall_hom {C : Type u‚ÇÅ} [SmallCategory C] (X : C) (F : C·µí·µñ ‚•§ Type u‚ÇÅ)
    (f : yoneda.obj X ‚ü∂ F) : (yonedaSectionsSmall X F).hom f = f.app _ (ùüô _) :=
  rfl
#align category_theory.yoneda_sections_small_hom CategoryTheory.yonedaSectionsSmall_hom

@[simp]
theorem yonedaSectionsSmall_inv_app_apply {C : Type u‚ÇÅ} [SmallCategory C] (X : C)
    (F : C·µí·µñ ‚•§ Type u‚ÇÅ) (t : F.obj (op X)) (Y : C·µí·µñ) (f : Y.unop ‚ü∂ X) :
    ((yonedaSectionsSmall X F).inv t).app Y f = F.map f.op t :=
  rfl
#align category_theory.yoneda_sections_small_inv_app_apply CategoryTheory.yonedaSectionsSmall_inv_app_apply

attribute [local ext] Functor.ext

/- Porting note: this used to be two calls to `tidy` -/
/-- The curried version of yoneda lemma when `C` is small. -/
def curriedYonedaLemma {C : Type u‚ÇÅ} [SmallCategory C] :
    (yoneda.op ‚ãô coyoneda : C·µí·µñ ‚•§ (C·µí·µñ ‚•§ Type u‚ÇÅ) ‚•§ Type u‚ÇÅ) ‚âÖ evaluation C·µí·µñ (Type u‚ÇÅ) := by
  refine eqToIso ?_ ‚â™‚â´ curry.mapIso
    (yonedaLemma C ‚â™‚â´ isoWhiskerLeft (evaluationUncurried C·µí·µñ (Type u‚ÇÅ)) uliftFunctorTrivial) ‚â™‚â´
    eqToIso ?_
  ¬∑ apply Functor.ext
    ¬∑ intro X Y f
      ext
      simp
    ¬∑ aesop_cat
  ¬∑ apply Functor.ext
    ¬∑ intro X Y f
      ext
      simp
    ¬∑ intro X
      simp only [curry, yoneda, coyoneda, curryObj, yonedaPairing]
      aesop_cat
#align category_theory.curried_yoneda_lemma CategoryTheory.curriedYonedaLemma

/-- The curried version of yoneda lemma when `C` is small. -/
def curriedYonedaLemma' {C : Type u‚ÇÅ} [SmallCategory C] :
    yoneda ‚ãô (whiskeringLeft C·µí·µñ (C·µí·µñ ‚•§ Type u‚ÇÅ)·µí·µñ (Type u‚ÇÅ)).obj yoneda.op ‚âÖ ùü≠ (C·µí·µñ ‚•§ Type u‚ÇÅ)
    := by
  refine eqToIso ?_ ‚â™‚â´ curry.mapIso (isoWhiskerLeft (Prod.swap _ _)
    (yonedaLemma C ‚â™‚â´ isoWhiskerLeft (evaluationUncurried C·µí·µñ (Type u‚ÇÅ)) uliftFunctorTrivial :_))
    ‚â™‚â´ eqToIso ?_
  ¬∑ apply Functor.ext
    ¬∑ intro X Y f
      aesop_cat
  ¬∑ apply Functor.ext
    ¬∑ aesop_cat
#align category_theory.curried_yoneda_lemma' CategoryTheory.curriedYonedaLemma'

end CategoryTheory
