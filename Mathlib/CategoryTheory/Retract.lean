/-
Copyright (c) 2024 Jack McKoen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jack McKoen
-/
import Mathlib.CategoryTheory.Comma.Arrow
import Mathlib.CategoryTheory.EpiMono

/-!
# Retracts

Defines retracts of objects and morphisms.

-/

universe v v' u u'

namespace CategoryTheory

variable {C : Type u} [Category.{v} C] {D : Type u'} [Category.{v'} D]

/-- An object `X` is a retract of `Y` if there are morphisms `i : X âŸ¶ Y` and `r : Y âŸ¶ X` such
that `i â‰« r = ğŸ™ X`. -/
structure Retract (X Y : C) where
  /-- the split monomorphism -/
  i : X âŸ¶ Y
  /-- the split epimorphism -/
  r : Y âŸ¶ X
  retract : i â‰« r = ğŸ™ X := by cat_disch

namespace Retract

attribute [reassoc (attr := simp)] retract

variable {X Y : C} (h : Retract X Y)

open Opposite

/-- Retracts are preserved when passing to the opposite category. -/
@[simps]
def op : Retract (op X) (op Y) where
  i := h.r.op
  r := h.i.op
  retract := by simp [â† op_comp, h.retract]

/-- If `X` is a retract of `Y`, then `F.obj X` is a retract of `F.obj Y`. -/
@[simps]
def map (F : C â¥¤ D) : Retract (F.obj X) (F.obj Y) where
  i := F.map h.i
  r := F.map h.r
  retract := by rw [â† F.map_comp h.i h.r, h.retract, F.map_id]

/-- a retract determines a split epimorphism. -/
@[simps] def splitEpi : SplitEpi h.r where
  section_ := h.i

/-- a retract determines a split monomorphism. -/
@[simps] def splitMono : SplitMono h.i where
  retraction := h.r

instance : IsSplitEpi h.r := âŸ¨âŸ¨h.splitEpiâŸ©âŸ©

instance : IsSplitMono h.i := âŸ¨âŸ¨h.splitMonoâŸ©âŸ©

variable (X) in
/-- Any object is a retract of itself. -/
@[simps]
def refl : Retract X X where
  i := ğŸ™ X
  r := ğŸ™ X

/-- A retract of a retract is a retract. -/
@[simps]
def trans {Z : C} (h' : Retract Y Z) : Retract X Z where
  i := h.i â‰« h'.i
  r := h'.r â‰« h.r

/-- If `e : X â‰… Y`, then `X` is a retract of `Y`. -/
def ofIso (e : X â‰… Y) : Retract X Y where
  i := e.hom
  r := e.inv

end Retract

/--
```
  X -------> Z -------> X
  |          |          |
  f          g          f
  |          |          |
  v          v          v
  Y -------> W -------> Y

```
A morphism `f : X âŸ¶ Y` is a retract of `g : Z âŸ¶ W` if there are morphisms `i : f âŸ¶ g`
and `r : g âŸ¶ f` in the arrow category such that `i â‰« r = ğŸ™ f`. -/
abbrev RetractArrow {X Y Z W : C} (f : X âŸ¶ Y) (g : Z âŸ¶ W) := Retract (Arrow.mk f) (Arrow.mk g)

namespace RetractArrow

variable {X Y Z W : C} {f : X âŸ¶ Y} {g : Z âŸ¶ W} (h : RetractArrow f g)

@[reassoc]
lemma i_w : h.i.left â‰« g = f â‰« h.i.right := h.i.w

@[reassoc]
lemma r_w : h.r.left â‰« f = g â‰« h.r.right := h.r.w

/-- The top of a retract diagram of morphisms determines a retract of objects. -/
@[simps!]
def left : Retract X Z := h.map Arrow.leftFunc

/-- The bottom of a retract diagram of morphisms determines a retract of objects. -/
@[simps!]
def right : Retract Y W := h.map Arrow.rightFunc

@[reassoc (attr := simp)]
lemma retract_left : h.i.left â‰« h.r.left = ğŸ™ X := h.left.retract

@[reassoc (attr := simp)]
lemma retract_right : h.i.right â‰« h.r.right = ğŸ™ Y := h.right.retract

instance : IsSplitEpi h.r.left := âŸ¨âŸ¨h.left.splitEpiâŸ©âŸ©

instance : IsSplitEpi h.r.right := âŸ¨âŸ¨h.right.splitEpiâŸ©âŸ©

instance : IsSplitMono h.i.left := âŸ¨âŸ¨h.left.splitMonoâŸ©âŸ©

instance : IsSplitMono h.i.right := âŸ¨âŸ¨h.right.splitMonoâŸ©âŸ©

end RetractArrow

namespace Iso

/-- If `X` is isomorphic to `Y`, then `X` is a retract of `Y`. -/
@[simps]
def retract {X Y : C} (e : X â‰… Y) : Retract X Y where
  i := e.hom
  r := e.inv

end Iso

end CategoryTheory
