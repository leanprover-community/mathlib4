/-
Copyright (c) 2024 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Filtered.Connected
import Mathlib.CategoryTheory.Limits.Final.Connected
import Mathlib.CategoryTheory.Limits.Types.Filtered
import Mathlib.CategoryTheory.Limits.Sifted

/-!
# Final functors with filtered (co)domain

If `C` is a filtered category, then the usual equivalent conditions for a functor `F : C â¥¤ D` to be
final can be restated. We show:

* `final_iff_of_isFiltered`: a concrete description of finality which is sometimes a convenient way
  to show that a functor is final.
* `final_iff_isFiltered_structuredArrow`: `F` is final if and only if `StructuredArrow d F` is
  filtered for all `d : D`, which strengthens the usual statement that `F` is final if and only
  if `StructuredArrow d F` is connected for all `d : D`.
* Under categories of objects of filtered categories are filtered and their forgetful functors
  are final.
* If `D` is a filtered category and `F : C â¥¤ D` is fully faithful and satisfies the additional
  condition that for every `d : D` there is an object `c : D` and a morphism `d âŸ¶ F.obj c`, then
  `C` is filtered and `F` is final.
* Finality and initiality of diagonal functors `diag : C â¥¤ C Ã— C` and of projection functors
  of (co)structured arrow categories.
* Finality of `StructuredArrow.post`, given the finality of its arguments.

## References

* [M. Kashiwara, P. Schapira, *Categories and Sheaves*][Kashiwara2006], Section 3.2

-/

universe vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

namespace CategoryTheory

open CategoryTheory.Limits CategoryTheory.Functor Opposite

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D] (F : C â¥¤ D)

/-- If `StructuredArrow d F` is filtered for any `d : D`, then `F : C â¥¤ D` is final. This is
simply because filtered categories are connected. More profoundly, the converse is also true if
`C` is filtered, see `final_iff_isFiltered_structuredArrow`. -/
theorem Functor.final_of_isFiltered_structuredArrow [âˆ€ d, IsFiltered (StructuredArrow d F)] :
    Final F where
  out _ := IsFiltered.isConnected _

/-- If `CostructuredArrow F d` is filtered for any `d : D`, then `F : C â¥¤ D` is initial. This is
simply because cofiltered categories are connected. More profoundly, the converse is also true
if `C` is cofiltered, see `initial_iff_isCofiltered_costructuredArrow`. -/
theorem Functor.initial_of_isCofiltered_costructuredArrow
    [âˆ€ d, IsCofiltered (CostructuredArrow F d)] : Initial F where
  out _ := IsCofiltered.isConnected _

theorem isFiltered_structuredArrow_of_isFiltered_of_exists [IsFilteredOrEmpty C]
    (hâ‚ : âˆ€ d, âˆƒ c, Nonempty (d âŸ¶ F.obj c)) (hâ‚‚ : âˆ€ {d : D} {c : C} (s s' : d âŸ¶ F.obj c),
      âˆƒ (c' : C) (t : c âŸ¶ c'), s â‰« F.map t = s' â‰« F.map t) (d : D) :
    IsFiltered (StructuredArrow d F) := by
  have : Nonempty (StructuredArrow d F) := by
    obtain âŸ¨c, âŸ¨fâŸ©âŸ© := hâ‚ d
    exact âŸ¨.mk fâŸ©
  suffices IsFilteredOrEmpty (StructuredArrow d F) from IsFiltered.mk
  refine âŸ¨fun f g => ?_, fun f g Î· Î¼ => ?_âŸ©
  Â· obtain âŸ¨c, âŸ¨t, htâŸ©âŸ© := hâ‚‚ (f.hom â‰« F.map (IsFiltered.leftToMax f.right g.right))
        (g.hom â‰« F.map (IsFiltered.rightToMax f.right g.right))
    refine âŸ¨.mk (f.hom â‰« F.map (IsFiltered.leftToMax f.right g.right â‰« t)), ?_, ?_, trivialâŸ©
    Â· exact StructuredArrow.homMk (IsFiltered.leftToMax _ _ â‰« t) rfl
    Â· exact StructuredArrow.homMk (IsFiltered.rightToMax _ _ â‰« t) (by simpa using ht.symm)
  Â· refine âŸ¨.mk (f.hom â‰« F.map (Î·.right â‰« IsFiltered.coeqHom Î·.right Î¼.right)),
      StructuredArrow.homMk (IsFiltered.coeqHom Î·.right Î¼.right) (by simp), ?_âŸ©
    simpa using IsFiltered.coeq_condition _ _

theorem isCofiltered_costructuredArrow_of_isCofiltered_of_exists [IsCofilteredOrEmpty C]
    (hâ‚ : âˆ€ d, âˆƒ c, Nonempty (F.obj c âŸ¶ d)) (hâ‚‚ : âˆ€ {d : D} {c : C} (s s' : F.obj c âŸ¶ d),
      âˆƒ (c' : C) (t : c' âŸ¶ c), F.map t â‰« s = F.map t â‰« s') (d : D) :
    IsCofiltered (CostructuredArrow F d) := by
  suffices IsFiltered (CostructuredArrow F d)áµ’áµ– from isCofiltered_of_isFiltered_op _
  suffices IsFiltered (StructuredArrow (op d) F.op) from
    IsFiltered.of_equivalence (costructuredArrowOpEquivalence _ _).symm
  apply isFiltered_structuredArrow_of_isFiltered_of_exists
  Â· intro d
    obtain âŸ¨c, âŸ¨tâŸ©âŸ© := hâ‚ d.unop
    exact âŸ¨op c, âŸ¨Quiver.Hom.op tâŸ©âŸ©
  Â· intro d c s s'
    obtain âŸ¨c', t, htâŸ© := hâ‚‚ s.unop s'.unop
    exact âŸ¨op c', Quiver.Hom.op t, Quiver.Hom.unop_inj htâŸ©

/-- If `C` is filtered, then we can give an explicit condition for a functor `F : C â¥¤ D` to
be final. The converse is also true, see `final_iff_of_isFiltered`. -/
theorem Functor.final_of_exists_of_isFiltered [IsFilteredOrEmpty C]
    (hâ‚ : âˆ€ d, âˆƒ c, Nonempty (d âŸ¶ F.obj c)) (hâ‚‚ : âˆ€ {d : D} {c : C} (s s' : d âŸ¶ F.obj c),
      âˆƒ (c' : C) (t : c âŸ¶ c'), s â‰« F.map t = s' â‰« F.map t) : Functor.Final F := by
  suffices âˆ€ d, IsFiltered (StructuredArrow d F) from final_of_isFiltered_structuredArrow F
  exact isFiltered_structuredArrow_of_isFiltered_of_exists F hâ‚ hâ‚‚

/-- The inclusion of a terminal object is final. -/
theorem Functor.final_const_of_isTerminal [IsFiltered C] {X : D} (hX : IsTerminal X) :
    ((Functor.const C).obj X).Final :=
  Functor.final_of_exists_of_isFiltered _ (fun _ => âŸ¨IsFiltered.nonempty.some, âŸ¨hX.from _âŸ©âŸ©)
    (fun {_ c} _ _ => âŸ¨c, ğŸ™ _, hX.hom_ext _ _âŸ©)

/-- The inclusion of the terminal object is final. -/
theorem Functor.final_const_terminal [IsFiltered C] [HasTerminal D] :
    ((Functor.const C).obj (âŠ¤_ D)).Final :=
  Functor.final_const_of_isTerminal terminalIsTerminal

/-- If `C` is cofiltered, then we can give an explicit condition for a functor `F : C â¥¤ D` to
be final. The converse is also true, see `initial_iff_of_isCofiltered`. -/
theorem Functor.initial_of_exists_of_isCofiltered [IsCofilteredOrEmpty C]
    (hâ‚ : âˆ€ d, âˆƒ c, Nonempty (F.obj c âŸ¶ d)) (hâ‚‚ : âˆ€ {d : D} {c : C} (s s' : F.obj c âŸ¶ d),
      âˆƒ (c' : C) (t : c' âŸ¶ c), F.map t â‰« s = F.map t â‰« s') : Functor.Initial F := by
  suffices âˆ€ d, IsCofiltered (CostructuredArrow F d) from
    initial_of_isCofiltered_costructuredArrow F
  exact isCofiltered_costructuredArrow_of_isCofiltered_of_exists F hâ‚ hâ‚‚

/-- The inclusion of an initial object is initial. -/
theorem Functor.initial_const_of_isInitial [IsCofiltered C] {X : D} (hX : IsInitial X) :
    ((Functor.const C).obj X).Initial :=
  Functor.initial_of_exists_of_isCofiltered _ (fun _ => âŸ¨IsCofiltered.nonempty.some, âŸ¨hX.to _âŸ©âŸ©)
    (fun {_ c} _ _ => âŸ¨c, ğŸ™ _, hX.hom_ext _ _âŸ©)

/-- The inclusion of the initial object is initial. -/
theorem Functor.initial_const_initial [IsCofiltered C] [HasInitial D] :
    ((Functor.const C).obj (âŠ¥_ D)).Initial :=
  Functor.initial_const_of_isInitial initialIsInitial

/-- In this situation, `F` is also final, see
`Functor.final_of_exists_of_isFiltered_of_fullyFaithful`. -/
theorem IsFilteredOrEmpty.of_exists_of_isFiltered_of_fullyFaithful [IsFilteredOrEmpty D] [F.Full]
    [F.Faithful] (h : âˆ€ d, âˆƒ c, Nonempty (d âŸ¶ F.obj c)) : IsFilteredOrEmpty C where
  cocone_objs c c' := by
    obtain âŸ¨câ‚€, âŸ¨fâŸ©âŸ© := h (IsFiltered.max (F.obj c) (F.obj c'))
    exact âŸ¨câ‚€, F.preimage (IsFiltered.leftToMax _ _ â‰« f),
      F.preimage (IsFiltered.rightToMax _ _ â‰« f), trivialâŸ©
  cocone_maps {c c'} f g := by
    obtain âŸ¨câ‚€, âŸ¨fâ‚€âŸ©âŸ© := h (IsFiltered.coeq (F.map f) (F.map g))
    refine âŸ¨_, F.preimage (IsFiltered.coeqHom (F.map f) (F.map g) â‰« fâ‚€), F.map_injective ?_âŸ©
    simp [reassoc_of% (IsFiltered.coeq_condition (F.map f) (F.map g))]

/-- In this situation, `F` is also initial, see
`Functor.initial_of_exists_of_isCofiltered_of_fullyFaithful`. -/
theorem IsCofilteredOrEmpty.of_exists_of_isCofiltered_of_fullyFaithful [IsCofilteredOrEmpty D]
    [F.Full] [F.Faithful] (h : âˆ€ d, âˆƒ c, Nonempty (F.obj c âŸ¶ d)) : IsCofilteredOrEmpty C := by
  suffices IsFilteredOrEmpty Cáµ’áµ– from isCofilteredOrEmpty_of_isFilteredOrEmpty_op _
  refine IsFilteredOrEmpty.of_exists_of_isFiltered_of_fullyFaithful F.op (fun d => ?_)
  obtain âŸ¨c, âŸ¨fâŸ©âŸ© := h d.unop
  exact âŸ¨op c, âŸ¨f.opâŸ©âŸ©

/-- In this situation, `F` is also final, see
`Functor.final_of_exists_of_isFiltered_of_fullyFaithful`. -/
theorem IsFiltered.of_exists_of_isFiltered_of_fullyFaithful [IsFiltered D] [F.Full] [F.Faithful]
    (h : âˆ€ d, âˆƒ c, Nonempty (d âŸ¶ F.obj c)) : IsFiltered C :=
  { IsFilteredOrEmpty.of_exists_of_isFiltered_of_fullyFaithful F h with
    nonempty := by
      have : Nonempty D := IsFiltered.nonempty
      obtain âŸ¨c, -âŸ© := h (Classical.arbitrary D)
      exact âŸ¨câŸ© }

/-- In this situation, `F` is also initial, see
`Functor.initial_of_exists_of_isCofiltered_of_fullyFaithful`. -/
theorem IsCofiltered.of_exists_of_isCofiltered_of_fullyFaithful [IsCofiltered D] [F.Full]
    [F.Faithful] (h : âˆ€ d, âˆƒ c, Nonempty (F.obj c âŸ¶ d)) : IsCofiltered C :=
  { IsCofilteredOrEmpty.of_exists_of_isCofiltered_of_fullyFaithful F h with
    nonempty := by
      have : Nonempty D := IsCofiltered.nonempty
      obtain âŸ¨c, -âŸ© := h (Classical.arbitrary D)
      exact âŸ¨câŸ© }

/-- In this situation, `C` is also filtered, see
`IsFilteredOrEmpty.of_exists_of_isFiltered_of_fullyFaithful`. -/
theorem Functor.final_of_exists_of_isFiltered_of_fullyFaithful [IsFilteredOrEmpty D] [F.Full]
    [F.Faithful] (h : âˆ€ d, âˆƒ c, Nonempty (d âŸ¶ F.obj c)) : Final F := by
  have := IsFilteredOrEmpty.of_exists_of_isFiltered_of_fullyFaithful F h
  refine Functor.final_of_exists_of_isFiltered F h (fun {d c} s s' => ?_)
  obtain âŸ¨câ‚€, âŸ¨fâŸ©âŸ© := h (IsFiltered.coeq s s')
  refine âŸ¨câ‚€, F.preimage (IsFiltered.coeqHom s s' â‰« f), ?_âŸ©
  simp [reassoc_of% (IsFiltered.coeq_condition s s')]

/-- In this situation, `C` is also cofiltered, see
`IsCofilteredOrEmpty.of_exists_of_isCofiltered_of_fullyFaithful`. -/
theorem Functor.initial_of_exists_of_isCofiltered_of_fullyFaithful [IsCofilteredOrEmpty D] [F.Full]
    [Faithful F] (h : âˆ€ d, âˆƒ c, Nonempty (F.obj c âŸ¶ d)) : Initial F := by
  suffices Final F.op from initial_of_final_op _
  refine Functor.final_of_exists_of_isFiltered_of_fullyFaithful F.op (fun d => ?_)
  obtain âŸ¨c, âŸ¨fâŸ©âŸ© := h d.unop
  exact âŸ¨op c, âŸ¨f.opâŸ©âŸ©

/-- Any under category on a filtered or empty category is filtered.
(Note that under categories are always cofiltered since they have an initial object.) -/
instance IsFiltered.under [IsFilteredOrEmpty C] (c : C) : IsFiltered (Under c) :=
  isFiltered_structuredArrow_of_isFiltered_of_exists _
    (fun c' => âŸ¨c', âŸ¨ğŸ™ _âŸ©âŸ©)
    (fun s s' => IsFilteredOrEmpty.cocone_maps s s') c

/-- Any over category on a cofiltered or empty category is cofiltered.
(Note that over categories are always filtered since they have a terminal object.) -/
instance IsCofiltered.over [IsCofilteredOrEmpty C] (c : C) : IsCofiltered (Over c) :=
  isCofiltered_costructuredArrow_of_isCofiltered_of_exists _
    (fun c' => âŸ¨c', âŸ¨ğŸ™ _âŸ©âŸ©)
    (fun s s' => IsCofilteredOrEmpty.cone_maps s s') c

/-- The forgetful functor of the under category on any filtered or empty category is final. -/
instance Under.final_forget [IsFilteredOrEmpty C] (c : C) : Final (Under.forget c) :=
  final_of_exists_of_isFiltered _
    (fun c' => âŸ¨mk (IsFiltered.leftToMax c c'), âŸ¨IsFiltered.rightToMax c c'âŸ©âŸ©)
    (fun {_} {x} s s' => by
      use mk (x.hom â‰« IsFiltered.coeqHom s s')
      use homMk (IsFiltered.coeqHom s s') (by simp)
      simp only [forget_obj, id_obj, mk_right, forget_map, homMk_right]
      rw [IsFiltered.coeq_condition])

/-- The forgetful functor of the over category on any cofiltered or empty category is initial. -/
instance Over.initial_forget [IsCofilteredOrEmpty C] (c : C) : Initial (Over.forget c) :=
  initial_of_exists_of_isCofiltered _
    (fun c' => âŸ¨mk (IsCofiltered.minToLeft c c'), âŸ¨IsCofiltered.minToRight c c'âŸ©âŸ©)
    (fun {_} {x} s s' => by
      use mk (IsCofiltered.eqHom s s' â‰« x.hom)
      use homMk (IsCofiltered.eqHom s s') (by simp)
      simp only [forget_obj, mk_left, forget_map, homMk_left]
      rw [IsCofiltered.eq_condition])

section LocallySmall

variable {C : Type vâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚} D] (F : C â¥¤ D)

/-- Implementation; use `Functor.Final.exists_coeq instead`. -/
theorem Functor.Final.exists_coeq_of_locally_small [IsFilteredOrEmpty C] [Final F] {d : D} {c : C}
    (s s' : d âŸ¶ F.obj c) : âˆƒ (c' : C) (t : c âŸ¶ c'), s â‰« F.map t = s' â‰« F.map t := by
  have : colimit.Î¹ (F â‹™ coyoneda.obj (op d)) c s = colimit.Î¹ (F â‹™ coyoneda.obj (op d)) c s' := by
    apply (Final.colimitCompCoyonedaIso F d).toEquiv.injective
    subsingleton
  obtain âŸ¨c', tâ‚, tâ‚‚, hâŸ© := (Types.FilteredColimit.colimit_eq_iff.{vâ‚, vâ‚, vâ‚} _).mp this
  refine âŸ¨IsFiltered.coeq tâ‚ tâ‚‚, tâ‚ â‰« IsFiltered.coeqHom tâ‚ tâ‚‚, ?_âŸ©
  conv_rhs => rw [IsFiltered.coeq_condition tâ‚ tâ‚‚]
  dsimp only [comp_obj, coyoneda_obj_obj, unop_op, Functor.comp_map, coyoneda_obj_map] at h
  simp [reassoc_of% h]

end LocallySmall

/-- If `C` is filtered, then we can give an explicit condition for a functor `F : C â¥¤ D` to
be final. -/
theorem Functor.final_iff_of_isFiltered [IsFilteredOrEmpty C] :
    Final F â†” (âˆ€ d, âˆƒ c, Nonempty (d âŸ¶ F.obj c)) âˆ§ (âˆ€ {d : D} {c : C} (s s' : d âŸ¶ F.obj c),
      âˆƒ (c' : C) (t : c âŸ¶ c'), s â‰« F.map t = s' â‰« F.map t) := by
  refine âŸ¨fun hF => âŸ¨?_, ?_âŸ©, fun h => final_of_exists_of_isFiltered F h.1 h.2âŸ©
  Â· intro d
    obtain âŸ¨fâŸ© : Nonempty (StructuredArrow d F) := IsConnected.is_nonempty
    exact âŸ¨_, âŸ¨f.homâŸ©âŸ©
  Â· let sâ‚ : C â‰Œ AsSmall.{max uâ‚ vâ‚ uâ‚‚ vâ‚‚} C := AsSmall.equiv
    let sâ‚‚ : D â‰Œ AsSmall.{max uâ‚ vâ‚ uâ‚‚ vâ‚‚} D := AsSmall.equiv
    have : IsFilteredOrEmpty (AsSmall.{max uâ‚ vâ‚ uâ‚‚ vâ‚‚} C) := .of_equivalence sâ‚
    intro d c s s'
    obtain âŸ¨c', t, htâŸ© := Functor.Final.exists_coeq_of_locally_small (sâ‚.inverse â‹™ F â‹™ sâ‚‚.functor)
      (AsSmall.up.map s) (AsSmall.up.map s')
    exact âŸ¨AsSmall.down.obj c', AsSmall.down.map t, sâ‚‚.functor.map_injective (by simp_all [sâ‚, sâ‚‚])âŸ©

/-- If `C` is cofiltered, then we can give an explicit condition for a functor `F : C â¥¤ D` to
be initial. -/
theorem Functor.initial_iff_of_isCofiltered [IsCofilteredOrEmpty C] :
    Initial F â†” (âˆ€ d, âˆƒ c, Nonempty (F.obj c âŸ¶ d)) âˆ§ (âˆ€ {d : D} {c : C} (s s' : F.obj c âŸ¶ d),
      âˆƒ (c' : C) (t : c' âŸ¶ c), F.map t â‰« s = F.map t â‰« s') := by
  refine âŸ¨fun hF => ?_, fun h => initial_of_exists_of_isCofiltered F h.1 h.2âŸ©
  obtain âŸ¨hâ‚, hâ‚‚âŸ© := F.op.final_iff_of_isFiltered.mp inferInstance
  refine âŸ¨?_, ?_âŸ©
  Â· intro d
    obtain âŸ¨c, âŸ¨tâŸ©âŸ© := hâ‚ (op d)
    exact âŸ¨c.unop, âŸ¨t.unopâŸ©âŸ©
  Â· intro d c s s'
    obtain âŸ¨c', t, htâŸ© := hâ‚‚ (Quiver.Hom.op s) (Quiver.Hom.op s')
    exact âŸ¨c'.unop, t.unop, Quiver.Hom.op_inj htâŸ©

theorem Functor.Final.exists_coeq [IsFilteredOrEmpty C] [Final F] {d : D} {c : C}
    (s s' : d âŸ¶ F.obj c) : âˆƒ (c' : C) (t : c âŸ¶ c'), s â‰« F.map t = s' â‰« F.map t :=
  ((final_iff_of_isFiltered F).1 inferInstance).2 s s'

theorem Functor.Initial.exists_eq [IsCofilteredOrEmpty C] [Initial F] {d : D} {c : C}
    (s s' : F.obj c âŸ¶ d) : âˆƒ (c' : C) (t : c' âŸ¶ c), F.map t â‰« s = F.map t â‰« s' :=
  ((initial_iff_of_isCofiltered F).1 inferInstance).2 s s'

/-- If `C` is filtered, then `F : C â¥¤ D` is final if and only if `StructuredArrow d F` is filtered
for all `d : D`. -/
theorem Functor.final_iff_isFiltered_structuredArrow [IsFilteredOrEmpty C] :
    Final F â†” âˆ€ d, IsFiltered (StructuredArrow d F) := by
  refine âŸ¨?_, fun h => final_of_isFiltered_structuredArrow FâŸ©
  rw [final_iff_of_isFiltered]
  exact fun h => isFiltered_structuredArrow_of_isFiltered_of_exists F h.1 h.2

/-- If `C` is cofiltered, then `F : C â¥¤ D` is initial if and only if `CostructuredArrow F d` is
cofiltered for all `d : D`. -/
theorem Functor.initial_iff_isCofiltered_costructuredArrow [IsCofilteredOrEmpty C] :
    Initial F â†” âˆ€ d, IsCofiltered (CostructuredArrow F d) := by
  refine âŸ¨?_, fun h => initial_of_isCofiltered_costructuredArrow FâŸ©
  rw [initial_iff_of_isCofiltered]
  exact fun h => isCofiltered_costructuredArrow_of_isCofiltered_of_exists F h.1 h.2

/-- If `C` is filtered, then the structured arrow category on the diagonal functor `C â¥¤ C Ã— C`
is filtered as well. -/
instance [IsFilteredOrEmpty C] (X : C Ã— C) : IsFiltered (StructuredArrow X (diag C)) := by
  haveI : âˆ€ Y, IsFiltered (StructuredArrow Y (Under.forget X.1)) := by
    rw [â† final_iff_isFiltered_structuredArrow (Under.forget X.1)]
    infer_instance
  apply IsFiltered.of_equivalence (StructuredArrow.ofDiagEquivalence X).symm

/-- The diagonal functor on any filtered category is final. -/
instance Functor.final_diag_of_isFiltered [IsFilteredOrEmpty C] : Final (Functor.diag C) :=
  final_of_isFiltered_structuredArrow _

-- Adding this instance causes performance problems elsewhere, even with low priority
theorem IsFilteredOrEmpty.isSiftedOrEmpty [IsFilteredOrEmpty C] : IsSiftedOrEmpty C :=
  Functor.final_diag_of_isFiltered

-- Adding this instance causes performance problems elsewhere, even with low priority
attribute [local instance] IsFiltered.nonempty in
theorem IsFiltered.isSifted [IsFiltered C] : IsSifted C where

/-- If `C` is cofiltered, then the costructured arrow category on the diagonal functor `C â¥¤ C Ã— C`
is cofiltered as well. -/
instance [IsCofilteredOrEmpty C] (X : C Ã— C) : IsCofiltered (CostructuredArrow (diag C) X) := by
  haveI : âˆ€ Y, IsCofiltered (CostructuredArrow (Over.forget X.1) Y) := by
    rw [â† initial_iff_isCofiltered_costructuredArrow (Over.forget X.1)]
    infer_instance
  apply IsCofiltered.of_equivalence (CostructuredArrow.ofDiagEquivalence X).symm

/-- The diagonal functor on any cofiltered category is initial. -/
instance Functor.initial_diag_of_isFiltered [IsCofilteredOrEmpty C] : Initial (Functor.diag C) :=
  initial_of_isCofiltered_costructuredArrow _

/-- If `C` is filtered, then every functor `F : C â¥¤ Discrete PUnit` is final. -/
theorem Functor.final_of_isFiltered_of_pUnit [IsFiltered C] (F : C â¥¤ Discrete PUnit) :
    Final F := by
  refine final_of_exists_of_isFiltered F (fun _ => ?_) (fun {_} {c} _ _ => ?_)
  Â· use Classical.choice IsFiltered.nonempty
    exact âŸ¨Discrete.eqToHom (by simp)âŸ©
  Â· use c; use ğŸ™ c
    apply Subsingleton.elim

/-- If `C` is cofiltered, then every functor `F : C â¥¤ Discrete PUnit` is initial. -/
theorem Functor.initial_of_isCofiltered_pUnit [IsCofiltered C] (F : C â¥¤ Discrete PUnit) :
    Initial F := by
  refine initial_of_exists_of_isCofiltered F (fun _ => ?_) (fun {_} {c} _ _ => ?_)
  Â· use Classical.choice IsCofiltered.nonempty
    exact âŸ¨Discrete.eqToHom (by simp)âŸ©
  Â· use c; use ğŸ™ c
    apply Subsingleton.elim

/-- The functor `StructuredArrow.proj : StructuredArrow Y T â¥¤ C` is final if `T : C â¥¤ D` is final
and `C` is filtered. -/
instance StructuredArrow.final_proj_of_isFiltered [IsFilteredOrEmpty C]
    (T : C â¥¤ D) [Final T] (Y : D) : Final (StructuredArrow.proj Y T) := by
  refine âŸ¨fun X => ?_âŸ©
  rw [isConnected_iff_of_equivalence (ofStructuredArrowProjEquivalence T Y X)]
  exact (final_comp (Under.forget X) T).out _

/-- The functor `CostructuredArrow.proj : CostructuredArrow Y T â¥¤ C` is initial if `T : C â¥¤ D` is
initial and `C` is cofiltered. -/
instance CostructuredArrow.initial_proj_of_isCofiltered [IsCofilteredOrEmpty C]
    (T : C â¥¤ D) [Initial T] (Y : D) : Initial (CostructuredArrow.proj T Y) := by
  refine âŸ¨fun X => ?_âŸ©
  rw [isConnected_iff_of_equivalence (ofCostructuredArrowProjEquivalence T Y X)]
  exact (initial_comp (Over.forget X) T).out _

/-- The functor `StructuredArrow d T â¥¤ StructuredArrow e (T â‹™ S)` that `u : e âŸ¶ S.obj d`
induces via `StructuredArrow.mapâ‚‚` is final, if `T` and `S` are final and the domain of `T` is
filtered. -/
instance StructuredArrow.final_mapâ‚‚_id [IsFiltered C] {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E]
    {T : C â¥¤ D} [T.Final] {S : D â¥¤ E} [S.Final] {T' : C â¥¤ E}
    {d : D} {e : E} (u : e âŸ¶ S.obj d) (Î± : T â‹™ S âŸ¶ T') [IsIso Î±] :
    Final (mapâ‚‚ (F := ğŸ­ _) u Î±) := by
  haveI : IsFiltered (StructuredArrow e (T â‹™ S)) :=
    (T â‹™ S).final_iff_isFiltered_structuredArrow.mp inferInstance e
  apply final_of_natIso (mapâ‚‚IsoPreEquivalenceInverseCompProj d e u Î±).symm

/-- `StructuredArrow.map` is final if the functor `T` is final` and its domain is filtered. -/
instance StructuredArrow.final_map [IsFiltered C] {S S' : D} (f : S âŸ¶ S') (T : C â¥¤ D) [T.Final] :
    Final (map (T := T) f) := by
  haveI := NatIso.isIso_of_isIso_app (ğŸ™ T)
  have : (mapâ‚‚ (F := ğŸ­ C) (G := ğŸ­ D) f (ğŸ™ T)).Final := by
    apply StructuredArrow.final_mapâ‚‚_id (S := ğŸ­ D) (T := T) (T' := T) f (ğŸ™ T)
  apply final_of_natIso (mapIsoMapâ‚‚ f).symm

/-- `StructuredArrow.post X T S` is final if `T` and `S` are final and the domain of `T` is
filtered. -/
instance StructuredArrow.final_post [IsFiltered C] {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E] (X : D)
    (T : C â¥¤ D) [T.Final] (S : D â¥¤ E) [S.Final] : Final (post X T S) := by
  apply final_of_natIso (postIsoMapâ‚‚ X T S).symm

/-- The functor `CostructuredArrow T d â¥¤ CostructuredArrow (T â‹™ S) e` that `u : S.obj d âŸ¶ e`
induces via `CostructuredArrow.mapâ‚‚` is initial, if `T` and `S` are initial and the domain of `T` is
filtered. -/
instance CostructuredArrow.initial_mapâ‚‚_id [IsCofiltered C] {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E]
    (T : C â¥¤ D) [T.Initial] (S : D â¥¤ E) [S.Initial] (d : D) (e : E)
    (u : S.obj d âŸ¶ e) : Initial (mapâ‚‚ (F := ğŸ­ _) (U := T â‹™ S) (ğŸ™ (T â‹™ S)) u) := by
  have := (T â‹™ S).initial_iff_isCofiltered_costructuredArrow.mp inferInstance e
  apply initial_of_natIso (mapâ‚‚IsoPreEquivalenceInverseCompProj T S d e u).symm

/-- `CostructuredArrow.post T S X` is initial if `T` and `S` are initial and the domain of `T` is
cofiltered. -/
instance CostructuredArrow.initial_post [IsCofiltered C] {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E] (X : D)
    (T : C â¥¤ D) [T.Initial] (S : D â¥¤ E) [S.Initial] : Initial (post T S X) := by
  apply initial_of_natIso (postIsoMapâ‚‚ X T S).symm

section Pi

variable {Î± : Type uâ‚} {I : Î± â†’ Type uâ‚‚} [âˆ€ s, Category.{vâ‚‚} (I s)]

open IsFiltered in
instance final_eval [âˆ€ s, IsFiltered (I s)] (s : Î±) : (Pi.eval I s).Final := by
  classical
  apply Functor.final_of_exists_of_isFiltered
  Â· exact fun i => âŸ¨Function.update (fun t => nonempty.some) s i, âŸ¨by simpa using ğŸ™ _âŸ©âŸ©
  Â· intro d c f g
    let c't : (âˆ€ s, (c' : I s) Ã— (c s âŸ¶ c')) := Function.update (fun t => âŸ¨c t, ğŸ™ (c t)âŸ©)
      s âŸ¨coeq f g, coeqHom f gâŸ©
    refine âŸ¨fun t => (c't t).1, fun t => (c't t).2, ?_âŸ©
    dsimp only [Pi.eval_obj, Pi.eval_map, c't]
    rw [Function.update_self]
    simpa using coeq_condition _ _

open IsCofiltered in
instance initial_eval [âˆ€ s, IsCofiltered (I s)] (s : Î±) : (Pi.eval I s).Initial := by
  classical
  apply Functor.initial_of_exists_of_isCofiltered
  Â· exact fun i => âŸ¨Function.update (fun t => nonempty.some) s i, âŸ¨by simpa using ğŸ™ _âŸ©âŸ©
  Â· intro d c f g
    let c't : (âˆ€ s, (c' : I s) Ã— (c' âŸ¶ c s)) := Function.update (fun t => âŸ¨c t, ğŸ™ (c t)âŸ©)
      s âŸ¨eq f g, eqHom f gâŸ©
    refine âŸ¨fun t => (c't t).1, fun t => (c't t).2, ?_âŸ©
    dsimp only [Pi.eval_obj, Pi.eval_map, c't]
    rw [Function.update_self]
    simpa using eq_condition _ _

end Pi

section Prod

namespace IsFiltered

attribute [local instance] IsFiltered.isConnected IsCofiltered.isConnected

instance final_fst [IsFiltered D] : (Prod.fst C D).Final := inferInstance

instance final_snd [IsFiltered C] : (Prod.snd C D).Final := inferInstance

instance initial_fst [IsCofiltered D] : (Prod.fst C D).Initial := inferInstance

instance initial_snd [IsCofiltered C] : (Prod.snd C D).Initial := inferInstance

end IsFiltered

end Prod

end CategoryTheory

open CategoryTheory

lemma Monotone.final_functor_iff {Jâ‚ Jâ‚‚ : Type*} [Preorder Jâ‚] [Preorder Jâ‚‚]
    [IsDirected Jâ‚ (Â· â‰¤ Â·)] {f : Jâ‚ â†’ Jâ‚‚} (hf : Monotone f) :
    hf.functor.Final â†” âˆ€ (jâ‚‚ : Jâ‚‚), âˆƒ (jâ‚ : Jâ‚), jâ‚‚ â‰¤ f jâ‚ := by
  rw [Functor.final_iff_of_isFiltered]
  constructor
  Â· rintro âŸ¨h, _âŸ© jâ‚‚
    obtain âŸ¨jâ‚, âŸ¨Ï†âŸ©âŸ© := h jâ‚‚
    exact âŸ¨jâ‚, leOfHom Ï†âŸ©
  Â· intro h
    constructor
    Â· intro jâ‚‚
      obtain âŸ¨jâ‚, hâ‚âŸ© := h jâ‚‚
      exact âŸ¨jâ‚, âŸ¨homOfLE hâ‚âŸ©âŸ©
    Â· intro _ c _ _
      exact âŸ¨c, ğŸ™ _, rflâŸ©
