/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Reid Barton, Scott Morrison
-/
import Mathlib.CategoryTheory.FinCategory.Basic
import Mathlib.CategoryTheory.Limits.Cones
import Mathlib.CategoryTheory.Limits.Shapes.FiniteLimits
import Mathlib.CategoryTheory.Adjunction.Basic
import Mathlib.CategoryTheory.Category.Preorder
import Mathlib.CategoryTheory.Category.ULift
import Mathlib.CategoryTheory.PEmpty

#align_import category_theory.filtered from "leanprover-community/mathlib"@"14e80e85cbca5872a329fbfd3d1f3fd64e306934"

/-!
# Filtered categories

A category is filtered if every finite diagram admits a cocone.
We give a simple characterisation of this condition as
1. for every pair of objects there exists another object "to the right",
2. for every pair of parallel morphisms there exists a morphism to the right so the compositions
   are equal, and
3. there exists some object.

Filtered colimits are often better behaved than arbitrary colimits.
See `CategoryTheory/Limits/Types` for some details.

Filtered categories are nice because colimits indexed by filtered categories tend to be
easier to describe than general colimits (and more often preserved by functors).

In this file we show that any functor from a finite category to a filtered category admits a cocone:
* `cocone_nonempty [FinCategory J] [IsFiltered C] (F : J ‚•§ C) : Nonempty (Cocone F)`
More generally,
for any finite collection of objects and morphisms between them in a filtered category
(even if not closed under composition) there exists some object `Z` receiving maps from all of them,
so that all the triangles (one edge from the finite set, two from morphisms to `Z`) commute.
This formulation is often more useful in practice and is available via `sup_exists`,
which takes a finset of objects, and an indexed family (indexed by source and target)
of finsets of morphisms.

We also prove the converse of `cocone_nonempty` as `of_cocone_nonempty`.

Furthermore, we give special support for two diagram categories: The `bowtie` and the `tulip`.
This is because these shapes show up in the proofs that forgetful functors of algebraic categories
(e.g. `MonCat`, `CommRingCat`, ...) preserve filtered colimits.

All of the above API, except for the `bowtie` and the `tulip`, is also provided for cofiltered
categories.

## See also
In `CategoryTheory.Limits.FilteredColimitCommutesFiniteLimit` we show that filtered colimits
commute with finite limits.

There is another characterization of filtered categories, namely that whenever `F : J ‚•§ C` is a
functor from a finite category, there is `X : C` such that `Nonempty (limit (F.op ‚ãô yoneda.obj X))`.
This is shown in `CategoryTheory.Limits.Filtered`.

-/


open Function

-- declare the `v`'s first; see `CategoryTheory.Category` for an explanation
universe w v v‚ÇÅ u u‚ÇÅ u‚ÇÇ

namespace CategoryTheory

variable (C : Type u) [Category.{v} C]

/-- A category `IsFilteredOrEmpty` if
1. for every pair of objects there exists another object "to the right", and
2. for every pair of parallel morphisms there exists a morphism to the right so the compositions
   are equal.
-/
class IsFilteredOrEmpty : Prop where
  /-- for every pair of objects there exists another object "to the right" -/
  cocone_objs : ‚àÄ X Y : C, ‚àÉ (Z : _) (_ : X ‚ü∂ Z) (_ : Y ‚ü∂ Z), True
  /-- for every pair of parallel morphisms there exists a morphism to the right
    so the compositions are equal -/
  cocone_maps : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f g : X ‚ü∂ Y), ‚àÉ (Z : _) (h : Y ‚ü∂ Z), f ‚â´ h = g ‚â´ h
#align category_theory.is_filtered_or_empty CategoryTheory.IsFilteredOrEmpty

/-- A category `IsFiltered` if
1. for every pair of objects there exists another object "to the right",
2. for every pair of parallel morphisms there exists a morphism to the right so the compositions
   are equal, and
3. there exists some object.

See <https://stacks.math.columbia.edu/tag/002V>. (They also define a diagram being filtered.)
-/
class IsFiltered extends IsFilteredOrEmpty C : Prop where
  /-- a filtered category must be non empty -/
  [nonempty : Nonempty C]
#align category_theory.is_filtered CategoryTheory.IsFiltered

instance (priority := 100) isFilteredOrEmpty_of_semilatticeSup (Œ± : Type u) [SemilatticeSup Œ±] :
    IsFilteredOrEmpty Œ± where
  cocone_objs X Y := ‚ü®X ‚äî Y, homOfLE le_sup_left, homOfLE le_sup_right, trivial‚ü©
  cocone_maps X Y f g := ‚ü®Y, ùüô _, by
    apply ULift.ext
    apply Subsingleton.elim‚ü©
#align category_theory.is_filtered_or_empty_of_semilattice_sup CategoryTheory.isFilteredOrEmpty_of_semilatticeSup

instance (priority := 100) isFiltered_of_semilatticeSup_nonempty (Œ± : Type u) [SemilatticeSup Œ±]
    [Nonempty Œ±] : IsFiltered Œ± where
#align category_theory.is_filtered_of_semilattice_sup_nonempty CategoryTheory.isFiltered_of_semilatticeSup_nonempty

instance (priority := 100) isFilteredOrEmpty_of_directed_le (Œ± : Type u) [Preorder Œ±]
    [IsDirected Œ± (¬∑ ‚â§ ¬∑)] : IsFilteredOrEmpty Œ± where
  cocone_objs X Y :=
    let ‚ü®Z, h1, h2‚ü© := exists_ge_ge X Y
    ‚ü®Z, homOfLE h1, homOfLE h2, trivial‚ü©
  cocone_maps X Y f g := ‚ü®Y, ùüô _, by
    apply ULift.ext
    apply Subsingleton.elim‚ü©
#align category_theory.is_filtered_or_empty_of_directed_le CategoryTheory.isFilteredOrEmpty_of_directed_le

instance (priority := 100) isFiltered_of_directed_le_nonempty (Œ± : Type u) [Preorder Œ±]
    [IsDirected Œ± (¬∑ ‚â§ ¬∑)] [Nonempty Œ±] : IsFiltered Œ± where
#align category_theory.is_filtered_of_directed_le_nonempty CategoryTheory.isFiltered_of_directed_le_nonempty

-- Sanity checks
example (Œ± : Type u) [SemilatticeSup Œ±] [OrderBot Œ±] : IsFiltered Œ± := by infer_instance

example (Œ± : Type u) [SemilatticeSup Œ±] [OrderTop Œ±] : IsFiltered Œ± := by infer_instance

instance : IsFiltered (Discrete PUnit) where
  cocone_objs X Y := ‚ü®‚ü®PUnit.unit‚ü©, ‚ü®‚ü®by trivial‚ü©‚ü©, ‚ü®‚ü®Subsingleton.elim _ _‚ü©‚ü©, trivial‚ü©
  cocone_maps X Y f g := ‚ü®‚ü®PUnit.unit‚ü©, ‚ü®‚ü®by trivial‚ü©‚ü©, by
    apply ULift.ext
    apply Subsingleton.elim‚ü©

namespace IsFiltered

section AllowEmpty

variable {C}
variable [IsFilteredOrEmpty C]

-- Porting note: the following definitions were removed because the names are invalid,
-- direct references to `IsFilteredOrEmpty` have been added instead
--
-- theorem cocone_objs : ‚àÄ X Y : C, ‚àÉ (Z : _) (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z), True :=
--  IsFilteredOrEmpty.cocone_objs
-- #align category_theory.is_filtered.cocone_objs CategoryTheory.IsFiltered.cocone_objs
--
--theorem cocone_maps : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f g : X ‚ü∂ Y), ‚àÉ (Z : _) (h : Y ‚ü∂ Z), f ‚â´ h = g ‚â´ h :=
--  IsFilteredOrEmpty.cocone_maps
--#align category_theory.is_filtered.cocone_maps CategoryTheory.IsFiltered.cocone_maps

/-- `max j j'` is an arbitrary choice of object to the right of both `j` and `j'`,
whose existence is ensured by `IsFiltered`.
-/
noncomputable def max (j j' : C) : C :=
  (IsFilteredOrEmpty.cocone_objs j j').choose
#align category_theory.is_filtered.max CategoryTheory.IsFiltered.max

/-- `leftToMax j j'` is an arbitrary choice of morphism from `j` to `max j j'`,
whose existence is ensured by `IsFiltered`.
-/
noncomputable def leftToMax (j j' : C) : j ‚ü∂ max j j' :=
  (IsFilteredOrEmpty.cocone_objs j j').choose_spec.choose
#align category_theory.is_filtered.left_to_max CategoryTheory.IsFiltered.leftToMax

/-- `rightToMax j j'` is an arbitrary choice of morphism from `j'` to `max j j'`,
whose existence is ensured by `IsFiltered`.
-/
noncomputable def rightToMax (j j' : C) : j' ‚ü∂ max j j' :=
  (IsFilteredOrEmpty.cocone_objs j j').choose_spec.choose_spec.choose
#align category_theory.is_filtered.right_to_max CategoryTheory.IsFiltered.rightToMax

/-- `coeq f f'`, for morphisms `f f' : j ‚ü∂ j'`, is an arbitrary choice of object
which admits a morphism `coeqHom f f' : j' ‚ü∂ coeq f f'` such that
`coeq_condition : f ‚â´ coeqHom f f' = f' ‚â´ coeqHom f f'`.
Its existence is ensured by `IsFiltered`.
-/
noncomputable def coeq {j j' : C} (f f' : j ‚ü∂ j') : C :=
  (IsFilteredOrEmpty.cocone_maps f f').choose
#align category_theory.is_filtered.coeq CategoryTheory.IsFiltered.coeq

/-- `coeqHom f f'`, for morphisms `f f' : j ‚ü∂ j'`, is an arbitrary choice of morphism
`coeqHom f f' : j' ‚ü∂ coeq f f'` such that
`coeq_condition : f ‚â´ coeqHom f f' = f' ‚â´ coeqHom f f'`.
Its existence is ensured by `IsFiltered`.
-/
noncomputable def coeqHom {j j' : C} (f f' : j ‚ü∂ j') : j' ‚ü∂ coeq f f' :=
  (IsFilteredOrEmpty.cocone_maps f f').choose_spec.choose
#align category_theory.is_filtered.coeq_hom CategoryTheory.IsFiltered.coeqHom

-- Porting note: the simp tag has been removed as the linter complained
/-- `coeq_condition f f'`, for morphisms `f f' : j ‚ü∂ j'`, is the proof that
`f ‚â´ coeqHom f f' = f' ‚â´ coeqHom f f'`.
-/
@[reassoc]
theorem coeq_condition {j j' : C} (f f' : j ‚ü∂ j') : f ‚â´ coeqHom f f' = f' ‚â´ coeqHom f f' :=
  (IsFilteredOrEmpty.cocone_maps f f').choose_spec.choose_spec
#align category_theory.is_filtered.coeq_condition CategoryTheory.IsFiltered.coeq_condition

end AllowEmpty

end IsFiltered

namespace IsFilteredOrEmpty
open IsFiltered

variable {C}
variable [IsFilteredOrEmpty C]
variable {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D]

/-- If `C` is filtered or emtpy, and we have a functor `R : C ‚•§ D` with a left adjoint, then `D` is
filtered or empty.
-/
theorem of_right_adjoint {L : D ‚•§ C} {R : C ‚•§ D} (h : L ‚ä£ R) : IsFilteredOrEmpty D :=
  { cocone_objs := fun X Y =>
      ‚ü®_, h.homEquiv _ _ (leftToMax _ _), h.homEquiv _ _ (rightToMax _ _), ‚ü®‚ü©‚ü©
    cocone_maps := fun X Y f g =>
      ‚ü®_, h.homEquiv _ _ (coeqHom _ _), by
        rw [‚Üê h.homEquiv_naturality_left, ‚Üê h.homEquiv_naturality_left, coeq_condition]‚ü© }

/-- If `C` is filtered or empty, and we have a right adjoint functor `R : C ‚•§ D`, then `D` is
filtered or empty. -/
theorem of_isRightAdjoint (R : C ‚•§ D) [R.IsRightAdjoint] : IsFilteredOrEmpty D :=
  of_right_adjoint (Adjunction.ofIsRightAdjoint R)

/-- Being filtered or empty is preserved by equivalence of categories. -/
theorem of_equivalence (h : C ‚âå D) : IsFilteredOrEmpty D :=
  of_right_adjoint h.symm.toAdjunction

end IsFilteredOrEmpty

namespace IsFiltered

section Nonempty

open CategoryTheory.Limits

variable {C}
variable [IsFiltered C]

/-- Any finite collection of objects in a filtered category has an object "to the right".
-/
theorem sup_objs_exists (O : Finset C) : ‚àÉ S : C, ‚àÄ {X}, X ‚àà O ‚Üí Nonempty (X ‚ü∂ S) := by
  classical
  induction' O using Finset.induction with X O' nm h
  ¬∑ exact ‚ü®Classical.choice IsFiltered.nonempty, by intro; simp‚ü©
  ¬∑ obtain ‚ü®S', w'‚ü© := h
    use max X S'
    rintro Y mY
    obtain rfl | h := eq_or_ne Y X
    ¬∑ exact ‚ü®leftToMax _ _‚ü©
    ¬∑ exact ‚ü®(w' (Finset.mem_of_mem_insert_of_ne mY h)).some ‚â´ rightToMax _ _‚ü©
#align category_theory.is_filtered.sup_objs_exists CategoryTheory.IsFiltered.sup_objs_exists

variable (O : Finset C) (H : Finset (Œ£' (X Y : C) (_ : X ‚àà O) (_ : Y ‚àà O), X ‚ü∂ Y))

/-- Given any `Finset` of objects `{X, ...}` and
indexed collection of `Finset`s of morphisms `{f, ...}` in `C`,
there exists an object `S`, with a morphism `T X : X ‚ü∂ S` from each `X`,
such that the triangles commute: `f ‚â´ T Y = T X`, for `f : X ‚ü∂ Y` in the `Finset`.
-/
theorem sup_exists :
    ‚àÉ (S : C) (T : ‚àÄ {X : C}, X ‚àà O ‚Üí (X ‚ü∂ S)),
      ‚àÄ {X Y : C} (mX : X ‚àà O) (mY : Y ‚àà O) {f : X ‚ü∂ Y},
        (‚ü®X, Y, mX, mY, f‚ü© : Œ£' (X Y : C) (_ : X ‚àà O) (_ : Y ‚àà O), X ‚ü∂ Y) ‚àà H ‚Üí
          f ‚â´ T mY = T mX := by
  classical
  induction' H using Finset.induction with h' H' nmf h''
  ¬∑ obtain ‚ü®S, f‚ü© := sup_objs_exists O
    exact ‚ü®S, fun mX => (f mX).some, by rintro - - - - - ‚ü®‚ü©‚ü©
  ¬∑ obtain ‚ü®X, Y, mX, mY, f‚ü© := h'
    obtain ‚ü®S', T', w'‚ü© := h''
    refine ‚ü®coeq (f ‚â´ T' mY) (T' mX), fun mZ => T' mZ ‚â´ coeqHom (f ‚â´ T' mY) (T' mX), ?_‚ü©
    intro X' Y' mX' mY' f' mf'
    rw [‚Üê Category.assoc]
    by_cases h : X = X' ‚àß Y = Y'
    ¬∑ rcases h with ‚ü®rfl, rfl‚ü©
      by_cases hf : f = f'
      ¬∑ subst hf
        apply coeq_condition
      ¬∑ rw [@w' _ _ mX mY f']
        simp only [Finset.mem_insert, PSigma.mk.injEq, heq_eq_eq, true_and] at mf'
        rcases mf' with mf' | mf'
        ¬∑ exfalso
          exact hf mf'.symm
        ¬∑ exact mf'
    ¬∑ rw [@w' _ _ mX' mY' f' _]
      apply Finset.mem_of_mem_insert_of_ne mf'
      contrapose! h
      obtain ‚ü®rfl, h‚ü© := h
      trivial
#align category_theory.is_filtered.sup_exists CategoryTheory.IsFiltered.sup_exists

/-- An arbitrary choice of object "to the right"
of a finite collection of objects `O` and morphisms `H`,
making all the triangles commute.
-/
noncomputable def sup : C :=
  (sup_exists O H).choose
#align category_theory.is_filtered.sup CategoryTheory.IsFiltered.sup

/-- The morphisms to `sup O H`.
-/
noncomputable def toSup {X : C} (m : X ‚àà O) : X ‚ü∂ sup O H :=
  (sup_exists O H).choose_spec.choose m
#align category_theory.is_filtered.to_sup CategoryTheory.IsFiltered.toSup

/-- The triangles of consisting of a morphism in `H` and the maps to `sup O H` commute.
-/
theorem toSup_commutes {X Y : C} (mX : X ‚àà O) (mY : Y ‚àà O) {f : X ‚ü∂ Y}
    (mf : (‚ü®X, Y, mX, mY, f‚ü© : Œ£' (X Y : C) (_ : X ‚àà O) (_ : Y ‚àà O), X ‚ü∂ Y) ‚àà H) :
    f ‚â´ toSup O H mY = toSup O H mX :=
  (sup_exists O H).choose_spec.choose_spec mX mY mf
#align category_theory.is_filtered.to_sup_commutes CategoryTheory.IsFiltered.toSup_commutes

variable {J : Type w} [SmallCategory J] [FinCategory J]

/-- If we have `IsFiltered C`, then for any functor `F : J ‚•§ C` with `FinCategory J`,
there exists a cocone over `F`.
-/
theorem cocone_nonempty (F : J ‚•§ C) : Nonempty (Cocone F) := by
  classical
  let O := Finset.univ.image F.obj
  let H : Finset (Œ£' (X Y : C) (_ : X ‚àà O) (_ : Y ‚àà O), X ‚ü∂ Y) :=
    Finset.univ.biUnion   fun X : J =>
      Finset.univ.biUnion fun Y : J =>
        Finset.univ.image fun f : X ‚ü∂ Y => ‚ü®F.obj X, F.obj Y, by simp [O], by simp [O], F.map f‚ü©
  obtain ‚ü®Z, f, w‚ü© := sup_exists O H
  refine ‚ü®‚ü®Z, ‚ü®fun X => f (by simp [O]), ?_‚ü©‚ü©‚ü©
  intro j j' g
  dsimp
  simp only [Category.comp_id]
  apply w
  simp only [O, H, Finset.mem_biUnion, Finset.mem_univ, Finset.mem_image, PSigma.mk.injEq,
    true_and, exists_and_left]
  exact ‚ü®j, rfl, j', g, by simp‚ü©
#align category_theory.is_filtered.cocone_nonempty CategoryTheory.IsFiltered.cocone_nonempty

/-- An arbitrary choice of cocone over `F : J ‚•§ C`, for `FinCategory J` and `IsFiltered C`.
-/
noncomputable def cocone (F : J ‚•§ C) : Cocone F :=
  (cocone_nonempty F).some
#align category_theory.is_filtered.cocone CategoryTheory.IsFiltered.cocone

variable {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D]

/-- If `C` is filtered, and we have a functor `R : C ‚•§ D` with a left adjoint, then `D` is filtered.
-/
theorem of_right_adjoint {L : D ‚•§ C} {R : C ‚•§ D} (h : L ‚ä£ R) : IsFiltered D :=
  { IsFilteredOrEmpty.of_right_adjoint h with
    nonempty := IsFiltered.nonempty.map R.obj }
#align category_theory.is_filtered.of_right_adjoint CategoryTheory.IsFiltered.of_right_adjoint

/-- If `C` is filtered, and we have a right adjoint functor `R : C ‚•§ D`, then `D` is filtered. -/
theorem of_isRightAdjoint (R : C ‚•§ D) [R.IsRightAdjoint] : IsFiltered D :=
  of_right_adjoint (Adjunction.ofIsRightAdjoint R)
#align category_theory.is_filtered.of_is_right_adjoint CategoryTheory.IsFiltered.of_isRightAdjoint

/-- Being filtered is preserved by equivalence of categories. -/
theorem of_equivalence (h : C ‚âå D) : IsFiltered D :=
  of_right_adjoint h.symm.toAdjunction
#align category_theory.is_filtered.of_equivalence CategoryTheory.IsFiltered.of_equivalence

end Nonempty

section OfCocone

open CategoryTheory.Limits

/-- If every finite diagram in `C` admits a cocone, then `C` is filtered. It is sufficient to verify
    this for diagrams whose shape lives in any one fixed universe. -/
theorem of_cocone_nonempty (h : ‚àÄ {J : Type w} [SmallCategory J] [FinCategory J] (F : J ‚•§ C),
    Nonempty (Cocone F)) : IsFiltered C := by
  have : Nonempty C := by
    obtain ‚ü®c‚ü© := h (Functor.empty _)
    exact ‚ü®c.pt‚ü©
  have : IsFilteredOrEmpty C := by
    refine ‚ü®?_, ?_‚ü©
    ¬∑ intros X Y
      obtain ‚ü®c‚ü© := h (ULiftHom.down ‚ãô ULift.downFunctor ‚ãô pair X Y)
      exact ‚ü®c.pt, c.Œπ.app ‚ü®‚ü®WalkingPair.left‚ü©‚ü©, c.Œπ.app ‚ü®‚ü®WalkingPair.right‚ü©‚ü©, trivial‚ü©
    ¬∑ intros X Y f g
      obtain ‚ü®c‚ü© := h (ULiftHom.down ‚ãô ULift.downFunctor ‚ãô parallelPair f g)
      refine ‚ü®c.pt, c.Œπ.app ‚ü®WalkingParallelPair.one‚ü©, ?_‚ü©
      have h‚ÇÅ := c.Œπ.naturality ‚ü®WalkingParallelPairHom.left‚ü©
      have h‚ÇÇ := c.Œπ.naturality ‚ü®WalkingParallelPairHom.right‚ü©
      simp_all
  apply IsFiltered.mk

theorem of_hasFiniteColimits [HasFiniteColimits C] : IsFiltered C :=
  of_cocone_nonempty.{v} C fun F => ‚ü®colimit.cocone F‚ü©

theorem of_isTerminal {X : C} (h : IsTerminal X) : IsFiltered C :=
  of_cocone_nonempty.{v} _ fun {_} _ _ _ => ‚ü®‚ü®X, ‚ü®fun _ => h.from _, fun _ _ _ => h.hom_ext _ _‚ü©‚ü©‚ü©

instance (priority := 100) of_hasTerminal [HasTerminal C] : IsFiltered C :=
  of_isTerminal _ terminalIsTerminal

/-- For every universe `w`, `C` is filtered if and only if every finite diagram in `C` with shape
    in `w` admits a cocone. -/
theorem iff_cocone_nonempty : IsFiltered C ‚Üî
    ‚àÄ {J : Type w} [SmallCategory J] [FinCategory J] (F : J ‚•§ C), Nonempty (Cocone F) :=
  ‚ü®fun _ _ _ _ F => cocone_nonempty F, of_cocone_nonempty C‚ü©

end OfCocone

section SpecialShapes

variable {C}
variable [IsFilteredOrEmpty C]

/-- `max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ` is an arbitrary choice of object to the right of `j‚ÇÅ`, `j‚ÇÇ` and `j‚ÇÉ`,
whose existence is ensured by `IsFiltered`.
-/
noncomputable def max‚ÇÉ (j‚ÇÅ j‚ÇÇ j‚ÇÉ : C) : C :=
  max (max j‚ÇÅ j‚ÇÇ) j‚ÇÉ
#align category_theory.is_filtered.max‚ÇÉ CategoryTheory.IsFiltered.max‚ÇÉ

/-- `firstToMax‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ` is an arbitrary choice of morphism from `j‚ÇÅ` to `max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ`,
whose existence is ensured by `IsFiltered`.
-/
noncomputable def firstToMax‚ÇÉ (j‚ÇÅ j‚ÇÇ j‚ÇÉ : C) : j‚ÇÅ ‚ü∂ max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ :=
  leftToMax j‚ÇÅ j‚ÇÇ ‚â´ leftToMax (max j‚ÇÅ j‚ÇÇ) j‚ÇÉ
#align category_theory.is_filtered.first_to_max‚ÇÉ CategoryTheory.IsFiltered.firstToMax‚ÇÉ

/-- `secondToMax‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ` is an arbitrary choice of morphism from `j‚ÇÇ` to `max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ`,
whose existence is ensured by `IsFiltered`.
-/
noncomputable def secondToMax‚ÇÉ (j‚ÇÅ j‚ÇÇ j‚ÇÉ : C) : j‚ÇÇ ‚ü∂ max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ :=
  rightToMax j‚ÇÅ j‚ÇÇ ‚â´ leftToMax (max j‚ÇÅ j‚ÇÇ) j‚ÇÉ
#align category_theory.is_filtered.second_to_max‚ÇÉ CategoryTheory.IsFiltered.secondToMax‚ÇÉ

/-- `thirdToMax‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ` is an arbitrary choice of morphism from `j‚ÇÉ` to `max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ`,
whose existence is ensured by `IsFiltered`.
-/
noncomputable def thirdToMax‚ÇÉ (j‚ÇÅ j‚ÇÇ j‚ÇÉ : C) : j‚ÇÉ ‚ü∂ max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ :=
  rightToMax (max j‚ÇÅ j‚ÇÇ) j‚ÇÉ
#align category_theory.is_filtered.third_to_max‚ÇÉ CategoryTheory.IsFiltered.thirdToMax‚ÇÉ

/-- `coeq‚ÇÉ f g h`, for morphisms `f g h : j‚ÇÅ ‚ü∂ j‚ÇÇ`, is an arbitrary choice of object
which admits a morphism `coeq‚ÇÉHom f g h : j‚ÇÇ ‚ü∂ coeq‚ÇÉ f g h` such that
`coeq‚ÇÉ_condition‚ÇÅ`, `coeq‚ÇÉ_condition‚ÇÇ` and `coeq‚ÇÉ_condition‚ÇÉ` are satisfied.
Its existence is ensured by `IsFiltered`.
-/
noncomputable def coeq‚ÇÉ {j‚ÇÅ j‚ÇÇ : C} (f g h : j‚ÇÅ ‚ü∂ j‚ÇÇ) : C :=
  coeq (coeqHom f g ‚â´ leftToMax (coeq f g) (coeq g h))
    (coeqHom g h ‚â´ rightToMax (coeq f g) (coeq g h))
#align category_theory.is_filtered.coeq‚ÇÉ CategoryTheory.IsFiltered.coeq‚ÇÉ

/-- `coeq‚ÇÉHom f g h`, for morphisms `f g h : j‚ÇÅ ‚ü∂ j‚ÇÇ`, is an arbitrary choice of morphism
`j‚ÇÇ ‚ü∂ coeq‚ÇÉ f g h` such that `coeq‚ÇÉ_condition‚ÇÅ`, `coeq‚ÇÉ_condition‚ÇÇ` and `coeq‚ÇÉ_condition‚ÇÉ`
are satisfied. Its existence is ensured by `IsFiltered`.
-/
noncomputable def coeq‚ÇÉHom {j‚ÇÅ j‚ÇÇ : C} (f g h : j‚ÇÅ ‚ü∂ j‚ÇÇ) : j‚ÇÇ ‚ü∂ coeq‚ÇÉ f g h :=
  coeqHom f g ‚â´
    leftToMax (coeq f g) (coeq g h) ‚â´
      coeqHom (coeqHom f g ‚â´ leftToMax (coeq f g) (coeq g h))
        (coeqHom g h ‚â´ rightToMax (coeq f g) (coeq g h))
#align category_theory.is_filtered.coeq‚ÇÉ_hom CategoryTheory.IsFiltered.coeq‚ÇÉHom

theorem coeq‚ÇÉ_condition‚ÇÅ {j‚ÇÅ j‚ÇÇ : C} (f g h : j‚ÇÅ ‚ü∂ j‚ÇÇ) :
    f ‚â´ coeq‚ÇÉHom f g h = g ‚â´ coeq‚ÇÉHom f g h := by
  simp only [coeq‚ÇÉHom, ‚Üê Category.assoc, coeq_condition f g]
#align category_theory.is_filtered.coeq‚ÇÉ_condition‚ÇÅ CategoryTheory.IsFiltered.coeq‚ÇÉ_condition‚ÇÅ

theorem coeq‚ÇÉ_condition‚ÇÇ {j‚ÇÅ j‚ÇÇ : C} (f g h : j‚ÇÅ ‚ü∂ j‚ÇÇ) :
    g ‚â´ coeq‚ÇÉHom f g h = h ‚â´ coeq‚ÇÉHom f g h := by
  dsimp [coeq‚ÇÉHom]
  slice_lhs 2 4 => rw [‚Üê Category.assoc, coeq_condition _ _]
  slice_rhs 2 4 => rw [‚Üê Category.assoc, coeq_condition _ _]
  slice_lhs 1 3 => rw [‚Üê Category.assoc, coeq_condition _ _]
  simp only [Category.assoc]
#align category_theory.is_filtered.coeq‚ÇÉ_condition‚ÇÇ CategoryTheory.IsFiltered.coeq‚ÇÉ_condition‚ÇÇ

theorem coeq‚ÇÉ_condition‚ÇÉ {j‚ÇÅ j‚ÇÇ : C} (f g h : j‚ÇÅ ‚ü∂ j‚ÇÇ) : f ‚â´ coeq‚ÇÉHom f g h = h ‚â´ coeq‚ÇÉHom f g h :=
  Eq.trans (coeq‚ÇÉ_condition‚ÇÅ f g h) (coeq‚ÇÉ_condition‚ÇÇ f g h)
#align category_theory.is_filtered.coeq‚ÇÉ_condition‚ÇÉ CategoryTheory.IsFiltered.coeq‚ÇÉ_condition‚ÇÉ

/-- For every span `j ‚üµ i ‚ü∂ j'`, there
   exists a cocone `j ‚ü∂ k ‚üµ j'` such that the square commutes. -/
theorem span {i j j' : C} (f : i ‚ü∂ j) (f' : i ‚ü∂ j') :
    ‚àÉ (k : C) (g : j ‚ü∂ k) (g' : j' ‚ü∂ k), f ‚â´ g = f' ‚â´ g' :=
  let ‚ü®K, G, G', _‚ü© := IsFilteredOrEmpty.cocone_objs j j'
  let ‚ü®k, e, he‚ü© := IsFilteredOrEmpty.cocone_maps (f ‚â´ G) (f' ‚â´ G')
  ‚ü®k, G ‚â´ e, G' ‚â´ e, by simpa only [‚Üê Category.assoc] ‚ü©
#align category_theory.is_filtered.span CategoryTheory.IsFiltered.span

/-- Given a "bowtie" of morphisms
```
 j‚ÇÅ   j‚ÇÇ
 |\  /|
 | \/ |
 | /\ |
 |/  \‚à£
 vv  vv
 k‚ÇÅ  k‚ÇÇ
```
in a filtered category, we can construct an object `s` and two morphisms from `k‚ÇÅ` and `k‚ÇÇ` to `s`,
making the resulting squares commute.
-/
theorem bowtie {j‚ÇÅ j‚ÇÇ k‚ÇÅ k‚ÇÇ : C} (f‚ÇÅ : j‚ÇÅ ‚ü∂ k‚ÇÅ) (g‚ÇÅ : j‚ÇÅ ‚ü∂ k‚ÇÇ) (f‚ÇÇ : j‚ÇÇ ‚ü∂ k‚ÇÅ) (g‚ÇÇ : j‚ÇÇ ‚ü∂ k‚ÇÇ) :
    ‚àÉ (s : C) (Œ± : k‚ÇÅ ‚ü∂ s) (Œ≤ : k‚ÇÇ ‚ü∂ s), f‚ÇÅ ‚â´ Œ± = g‚ÇÅ ‚â´ Œ≤ ‚àß f‚ÇÇ ‚â´ Œ± = g‚ÇÇ ‚â´ Œ≤ := by
  obtain ‚ü®t, k‚ÇÅt, k‚ÇÇt, ht‚ü© := span f‚ÇÅ g‚ÇÅ
  obtain ‚ü®s, ts, hs‚ü© := IsFilteredOrEmpty.cocone_maps (f‚ÇÇ ‚â´ k‚ÇÅt) (g‚ÇÇ ‚â´ k‚ÇÇt)
  simp_rw [Category.assoc] at hs
  exact ‚ü®s, k‚ÇÅt ‚â´ ts, k‚ÇÇt ‚â´ ts, by simp only [‚Üê Category.assoc, ht], hs‚ü©
#align category_theory.is_filtered.bowtie CategoryTheory.IsFiltered.bowtie

/-- Given a "tulip" of morphisms
```
 j‚ÇÅ    j‚ÇÇ    j‚ÇÉ
 |\   / \   / |
 | \ /   \ /  |
 |  vv    vv  |
 \  k‚ÇÅ    k‚ÇÇ /
  \         /
   \       /
    \     /
     \   /
      v v
       l
```
in a filtered category, we can construct an object `s` and three morphisms from `k‚ÇÅ`, `k‚ÇÇ` and `l`
to `s`, making the resulting squares commute.
-/
theorem tulip {j‚ÇÅ j‚ÇÇ j‚ÇÉ k‚ÇÅ k‚ÇÇ l : C} (f‚ÇÅ : j‚ÇÅ ‚ü∂ k‚ÇÅ) (f‚ÇÇ : j‚ÇÇ ‚ü∂ k‚ÇÅ) (f‚ÇÉ : j‚ÇÇ ‚ü∂ k‚ÇÇ) (f‚ÇÑ : j‚ÇÉ ‚ü∂ k‚ÇÇ)
    (g‚ÇÅ : j‚ÇÅ ‚ü∂ l) (g‚ÇÇ : j‚ÇÉ ‚ü∂ l) :
    ‚àÉ (s : C) (Œ± : k‚ÇÅ ‚ü∂ s) (Œ≤ : l ‚ü∂ s) (Œ≥ : k‚ÇÇ ‚ü∂ s),
      f‚ÇÅ ‚â´ Œ± = g‚ÇÅ ‚â´ Œ≤ ‚àß f‚ÇÇ ‚â´ Œ± = f‚ÇÉ ‚â´ Œ≥ ‚àß f‚ÇÑ ‚â´ Œ≥ = g‚ÇÇ ‚â´ Œ≤ := by
  obtain ‚ü®l', k‚ÇÅl, k‚ÇÇl, hl‚ü© := span f‚ÇÇ f‚ÇÉ
  obtain ‚ü®s, ls, l's, hs‚ÇÅ, hs‚ÇÇ‚ü© := bowtie g‚ÇÅ (f‚ÇÅ ‚â´ k‚ÇÅl) g‚ÇÇ (f‚ÇÑ ‚â´ k‚ÇÇl)
  refine ‚ü®s, k‚ÇÅl ‚â´ l's, ls, k‚ÇÇl ‚â´ l's, ?_, by simp only [‚Üê Category.assoc, hl], ?_‚ü© <;>
    simp only [hs‚ÇÅ, hs‚ÇÇ, Category.assoc]
#align category_theory.is_filtered.tulip CategoryTheory.IsFiltered.tulip

end SpecialShapes

end IsFiltered

/-- A category `IsCofilteredOrEmpty` if
1. for every pair of objects there exists another object "to the left", and
2. for every pair of parallel morphisms there exists a morphism to the left so the compositions
   are equal.
-/
class IsCofilteredOrEmpty : Prop where
  /-- for every pair of objects there exists another object "to the left" -/
  cone_objs : ‚àÄ X Y : C, ‚àÉ (W : _) (_ : W ‚ü∂ X) (_ : W ‚ü∂ Y), True
  /-- for every pair of parallel morphisms there exists a morphism to the left
    so the compositions are equal -/
  cone_maps : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f g : X ‚ü∂ Y), ‚àÉ (W : _) (h : W ‚ü∂ X), h ‚â´ f = h ‚â´ g
#align category_theory.is_cofiltered_or_empty CategoryTheory.IsCofilteredOrEmpty

/-- A category `IsCofiltered` if
1. for every pair of objects there exists another object "to the left",
2. for every pair of parallel morphisms there exists a morphism to the left so the compositions
   are equal, and
3. there exists some object.

See <https://stacks.math.columbia.edu/tag/04AZ>.
-/
class IsCofiltered extends IsCofilteredOrEmpty C : Prop where
  /-- a cofiltered category must be non empty -/
  [nonempty : Nonempty C]
#align category_theory.is_cofiltered CategoryTheory.IsCofiltered

instance (priority := 100) isCofilteredOrEmpty_of_semilatticeInf (Œ± : Type u) [SemilatticeInf Œ±] :
    IsCofilteredOrEmpty Œ± where
  cone_objs X Y := ‚ü®X ‚äì Y, homOfLE inf_le_left, homOfLE inf_le_right, trivial‚ü©
  cone_maps X Y f g := ‚ü®X, ùüô _, by
    apply ULift.ext
    apply Subsingleton.elim‚ü©
#align category_theory.is_cofiltered_or_empty_of_semilattice_inf CategoryTheory.isCofilteredOrEmpty_of_semilatticeInf

instance (priority := 100) isCofiltered_of_semilatticeInf_nonempty (Œ± : Type u) [SemilatticeInf Œ±]
    [Nonempty Œ±] : IsCofiltered Œ± where
#align category_theory.is_cofiltered_of_semilattice_inf_nonempty CategoryTheory.isCofiltered_of_semilatticeInf_nonempty

instance (priority := 100) isCofilteredOrEmpty_of_directed_ge (Œ± : Type u) [Preorder Œ±]
    [IsDirected Œ± (¬∑ ‚â• ¬∑)] : IsCofilteredOrEmpty Œ± where
  cone_objs X Y :=
    let ‚ü®Z, hX, hY‚ü© := exists_le_le X Y
    ‚ü®Z, homOfLE hX, homOfLE hY, trivial‚ü©
  cone_maps X Y f g := ‚ü®X, ùüô _, by
    apply ULift.ext
    apply Subsingleton.elim‚ü©
#align category_theory.is_cofiltered_or_empty_of_directed_ge CategoryTheory.isCofilteredOrEmpty_of_directed_ge

instance (priority := 100) isCofiltered_of_directed_ge_nonempty (Œ± : Type u) [Preorder Œ±]
    [IsDirected Œ± (¬∑ ‚â• ¬∑)] [Nonempty Œ±] : IsCofiltered Œ± where
#align category_theory.is_cofiltered_of_directed_ge_nonempty CategoryTheory.isCofiltered_of_directed_ge_nonempty

-- Sanity checks
example (Œ± : Type u) [SemilatticeInf Œ±] [OrderBot Œ±] : IsCofiltered Œ± := by infer_instance

example (Œ± : Type u) [SemilatticeInf Œ±] [OrderTop Œ±] : IsCofiltered Œ± := by infer_instance

instance : IsCofiltered (Discrete PUnit) where
  cone_objs X Y := ‚ü®‚ü®PUnit.unit‚ü©, ‚ü®‚ü®by trivial‚ü©‚ü©, ‚ü®‚ü®Subsingleton.elim _ _‚ü©‚ü©, trivial‚ü©
  cone_maps X Y f g := ‚ü®‚ü®PUnit.unit‚ü©, ‚ü®‚ü®by trivial‚ü©‚ü©, by
    apply ULift.ext
    apply Subsingleton.elim‚ü©

namespace IsCofiltered

section AllowEmpty

variable {C}
variable [IsCofilteredOrEmpty C]

-- Porting note: the following definitions were removed because the names are invalid,
-- direct references to `IsCofilteredOrEmpty` have been added instead
--
--theorem cone_objs : ‚àÄ X Y : C, ‚àÉ (W : _) (f : W ‚ü∂ X) (g : W ‚ü∂ Y), True :=
--  IsCofilteredOrEmpty.cone_objs
--#align category_theory.is_cofiltered.cone_objs CategoryTheory.IsCofiltered.cone_objs
--
--theorem cone_maps : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f g : X ‚ü∂ Y), ‚àÉ (W : _) (h : W ‚ü∂ X), h ‚â´ f = h ‚â´ g :=
--  IsCofilteredOrEmpty.cone_maps
--#align category_theory.is_cofiltered.cone_maps CategoryTheory.IsCofiltered.cone_maps

/-- `min j j'` is an arbitrary choice of object to the left of both `j` and `j'`,
whose existence is ensured by `IsCofiltered`.
-/
noncomputable def min (j j' : C) : C :=
  (IsCofilteredOrEmpty.cone_objs j j').choose
#align category_theory.is_cofiltered.min CategoryTheory.IsCofiltered.min

/-- `minToLeft j j'` is an arbitrary choice of morphism from `min j j'` to `j`,
whose existence is ensured by `IsCofiltered`.
-/
noncomputable def minToLeft (j j' : C) : min j j' ‚ü∂ j :=
  (IsCofilteredOrEmpty.cone_objs j j').choose_spec.choose
#align category_theory.is_cofiltered.min_to_left CategoryTheory.IsCofiltered.minToLeft

/-- `minToRight j j'` is an arbitrary choice of morphism from `min j j'` to `j'`,
whose existence is ensured by `IsCofiltered`.
-/
noncomputable def minToRight (j j' : C) : min j j' ‚ü∂ j' :=
  (IsCofilteredOrEmpty.cone_objs j j').choose_spec.choose_spec.choose
#align category_theory.is_cofiltered.min_to_right CategoryTheory.IsCofiltered.minToRight

/-- `eq f f'`, for morphisms `f f' : j ‚ü∂ j'`, is an arbitrary choice of object
which admits a morphism `eqHom f f' : eq f f' ‚ü∂ j` such that
`eq_condition : eqHom f f' ‚â´ f = eqHom f f' ‚â´ f'`.
Its existence is ensured by `IsCofiltered`.
-/
noncomputable def eq {j j' : C} (f f' : j ‚ü∂ j') : C :=
  (IsCofilteredOrEmpty.cone_maps f f').choose
#align category_theory.is_cofiltered.eq CategoryTheory.IsCofiltered.eq

/-- `eqHom f f'`, for morphisms `f f' : j ‚ü∂ j'`, is an arbitrary choice of morphism
`eqHom f f' : eq f f' ‚ü∂ j` such that
`eq_condition : eqHom f f' ‚â´ f = eqHom f f' ‚â´ f'`.
Its existence is ensured by `IsCofiltered`.
-/
noncomputable def eqHom {j j' : C} (f f' : j ‚ü∂ j') : eq f f' ‚ü∂ j :=
  (IsCofilteredOrEmpty.cone_maps f f').choose_spec.choose
#align category_theory.is_cofiltered.eq_hom CategoryTheory.IsCofiltered.eqHom

-- Porting note: the simp tag has been removed as the linter complained
/-- `eq_condition f f'`, for morphisms `f f' : j ‚ü∂ j'`, is the proof that
`eqHom f f' ‚â´ f = eqHom f f' ‚â´ f'`.
-/
@[reassoc]
theorem eq_condition {j j' : C} (f f' : j ‚ü∂ j') : eqHom f f' ‚â´ f = eqHom f f' ‚â´ f' :=
  (IsCofilteredOrEmpty.cone_maps f f').choose_spec.choose_spec
#align category_theory.is_cofiltered.eq_condition CategoryTheory.IsCofiltered.eq_condition

/-- For every cospan `j ‚ü∂ i ‚üµ j'`,
 there exists a cone `j ‚üµ k ‚ü∂ j'` such that the square commutes. -/
theorem cospan {i j j' : C} (f : j ‚ü∂ i) (f' : j' ‚ü∂ i) :
    ‚àÉ (k : C) (g : k ‚ü∂ j) (g' : k ‚ü∂ j'), g ‚â´ f = g' ‚â´ f' :=
  let ‚ü®K, G, G', _‚ü© := IsCofilteredOrEmpty.cone_objs j j'
  let ‚ü®k, e, he‚ü© := IsCofilteredOrEmpty.cone_maps (G ‚â´ f) (G' ‚â´ f')
  ‚ü®k, e ‚â´ G, e ‚â´ G', by simpa only [Category.assoc] using he‚ü©
#align category_theory.is_cofiltered.cospan CategoryTheory.IsCofiltered.cospan

theorem _root_.CategoryTheory.Functor.ranges_directed (F : C ‚•§ Type*) (j : C) :
    Directed (¬∑ ‚äá ¬∑) fun f : Œ£'i, i ‚ü∂ j => Set.range (F.map f.2) := fun ‚ü®i, ij‚ü© ‚ü®k, kj‚ü© => by
  let ‚ü®l, li, lk, e‚ü© := cospan ij kj
  refine ‚ü®‚ü®l, lk ‚â´ kj‚ü©, e ‚ñ∏ ?_, ?_‚ü© <;> simp_rw [F.map_comp] <;> apply Set.range_comp_subset_range
#align category_theory.functor.ranges_directed CategoryTheory.Functor.ranges_directed

end AllowEmpty

end IsCofiltered

namespace IsCofilteredOrEmpty
open IsCofiltered

variable {C}
variable [IsCofilteredOrEmpty C]
variable {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D]

/-- If `C` is cofiltered or empty, and we have a functor `L : C ‚•§ D` with a right adjoint,
then `D` is cofiltered or empty.
-/
theorem of_left_adjoint {L : C ‚•§ D} {R : D ‚•§ C} (h : L ‚ä£ R) : IsCofilteredOrEmpty D :=
  { cone_objs := fun X Y =>
      ‚ü®L.obj (min (R.obj X) (R.obj Y)), (h.homEquiv _ X).symm (minToLeft _ _),
        (h.homEquiv _ Y).symm (minToRight _ _), ‚ü®‚ü©‚ü©
    cone_maps := fun X Y f g =>
      ‚ü®L.obj (eq (R.map f) (R.map g)), (h.homEquiv _ _).symm (eqHom _ _), by
        rw [‚Üê h.homEquiv_naturality_right_symm, ‚Üê h.homEquiv_naturality_right_symm, eq_condition]‚ü© }

/-- If `C` is cofiltered or empty, and we have a left adjoint functor `L : C ‚•§ D`, then `D` is
cofiltered or empty. -/
theorem of_isLeftAdjoint (L : C ‚•§ D) [L.IsLeftAdjoint] : IsCofilteredOrEmpty D :=
  of_left_adjoint (Adjunction.ofIsLeftAdjoint L)

/-- Being cofiltered or empty is preserved by equivalence of categories. -/
theorem of_equivalence (h : C ‚âå D) : IsCofilteredOrEmpty D :=
  of_left_adjoint h.toAdjunction

end IsCofilteredOrEmpty

namespace IsCofiltered

section Nonempty

open CategoryTheory.Limits

variable {C}
variable [IsCofiltered C]

/-- Any finite collection of objects in a cofiltered category has an object "to the left".
-/
theorem inf_objs_exists (O : Finset C) : ‚àÉ S : C, ‚àÄ {X}, X ‚àà O ‚Üí Nonempty (S ‚ü∂ X) := by
  classical
  induction' O using Finset.induction with X O' nm h
  ¬∑ exact ‚ü®Classical.choice IsCofiltered.nonempty, by intro; simp‚ü©
  ¬∑ obtain ‚ü®S', w'‚ü© := h
    use min X S'
    rintro Y mY
    obtain rfl | h := eq_or_ne Y X
    ¬∑ exact ‚ü®minToLeft _ _‚ü©
    ¬∑ exact ‚ü®minToRight _ _ ‚â´ (w' (Finset.mem_of_mem_insert_of_ne mY h)).some‚ü©
#align category_theory.is_cofiltered.inf_objs_exists CategoryTheory.IsCofiltered.inf_objs_exists

variable (O : Finset C) (H : Finset (Œ£' (X Y : C) (_ : X ‚àà O) (_ : Y ‚àà O), X ‚ü∂ Y))

/-- Given any `Finset` of objects `{X, ...}` and
indexed collection of `Finset`s of morphisms `{f, ...}` in `C`,
there exists an object `S`, with a morphism `T X : S ‚ü∂ X` from each `X`,
such that the triangles commute: `T X ‚â´ f = T Y`, for `f : X ‚ü∂ Y` in the `Finset`.
-/
theorem inf_exists :
    ‚àÉ (S : C) (T : ‚àÄ {X : C}, X ‚àà O ‚Üí (S ‚ü∂ X)),
      ‚àÄ {X Y : C} (mX : X ‚àà O) (mY : Y ‚àà O) {f : X ‚ü∂ Y},
        (‚ü®X, Y, mX, mY, f‚ü© : Œ£' (X Y : C) (_ : X ‚àà O) (_ : Y ‚àà O), X ‚ü∂ Y) ‚àà H ‚Üí
          T mX ‚â´ f = T mY := by
  classical
  induction' H using Finset.induction with h' H' nmf h''
  ¬∑ obtain ‚ü®S, f‚ü© := inf_objs_exists O
    exact ‚ü®S, fun mX => (f mX).some, by rintro - - - - - ‚ü®‚ü©‚ü©
  ¬∑ obtain ‚ü®X, Y, mX, mY, f‚ü© := h'
    obtain ‚ü®S', T', w'‚ü© := h''
    refine ‚ü®eq (T' mX ‚â´ f) (T' mY), fun mZ => eqHom (T' mX ‚â´ f) (T' mY) ‚â´ T' mZ, ?_‚ü©
    intro X' Y' mX' mY' f' mf'
    rw [Category.assoc]
    by_cases h : X = X' ‚àß Y = Y'
    ¬∑ rcases h with ‚ü®rfl, rfl‚ü©
      by_cases hf : f = f'
      ¬∑ subst hf
        apply eq_condition
      ¬∑ rw [@w' _ _ mX mY f']
        simp only [Finset.mem_insert, PSigma.mk.injEq, heq_eq_eq, true_and] at mf'
        rcases mf' with mf' | mf'
        ¬∑ exfalso
          exact hf mf'.symm
        ¬∑ exact mf'
    ¬∑ rw [@w' _ _ mX' mY' f' _]
      apply Finset.mem_of_mem_insert_of_ne mf'
      contrapose! h
      obtain ‚ü®rfl, h‚ü© := h
      trivial
#align category_theory.is_cofiltered.inf_exists CategoryTheory.IsCofiltered.inf_exists

/-- An arbitrary choice of object "to the left"
of a finite collection of objects `O` and morphisms `H`,
making all the triangles commute.
-/
noncomputable def inf : C :=
  (inf_exists O H).choose
#align category_theory.is_cofiltered.inf CategoryTheory.IsCofiltered.inf

/-- The morphisms from `inf O H`.
-/
noncomputable def infTo {X : C} (m : X ‚àà O) : inf O H ‚ü∂ X :=
  (inf_exists O H).choose_spec.choose m
#align category_theory.is_cofiltered.inf_to CategoryTheory.IsCofiltered.infTo

/-- The triangles consisting of a morphism in `H` and the maps from `inf O H` commute.
-/
theorem infTo_commutes {X Y : C} (mX : X ‚àà O) (mY : Y ‚àà O) {f : X ‚ü∂ Y}
    (mf : (‚ü®X, Y, mX, mY, f‚ü© : Œ£' (X Y : C) (_ : X ‚àà O) (_ : Y ‚àà O), X ‚ü∂ Y) ‚àà H) :
    infTo O H mX ‚â´ f = infTo O H mY :=
  (inf_exists O H).choose_spec.choose_spec mX mY mf
#align category_theory.is_cofiltered.inf_to_commutes CategoryTheory.IsCofiltered.infTo_commutes

variable {J : Type w} [SmallCategory J] [FinCategory J]

/-- If we have `IsCofiltered C`, then for any functor `F : J ‚•§ C` with `FinCategory J`,
there exists a cone over `F`.
-/
theorem cone_nonempty (F : J ‚•§ C) : Nonempty (Cone F) := by
  classical
  let O := Finset.univ.image F.obj
  let H : Finset (Œ£' (X Y : C) (_ : X ‚àà O) (_ : Y ‚àà O), X ‚ü∂ Y) :=
    Finset.univ.biUnion fun X : J =>
      Finset.univ.biUnion fun Y : J =>
        Finset.univ.image fun f : X ‚ü∂ Y => ‚ü®F.obj X, F.obj Y, by simp [O], by simp [O], F.map f‚ü©
  obtain ‚ü®Z, f, w‚ü© := inf_exists O H
  refine ‚ü®‚ü®Z, ‚ü®fun X => f (by simp [O]), ?_‚ü©‚ü©‚ü©
  intro j j' g
  dsimp
  simp only [Category.id_comp]
  symm
  apply w
  simp only [O, H, Finset.mem_biUnion, Finset.mem_univ, Finset.mem_image,
    PSigma.mk.injEq, true_and, exists_and_left]
  exact ‚ü®j, rfl, j', g, by simp‚ü©
#align category_theory.is_cofiltered.cone_nonempty CategoryTheory.IsCofiltered.cone_nonempty

/-- An arbitrary choice of cone over `F : J ‚•§ C`, for `FinCategory J` and `IsCofiltered C`.
-/
noncomputable def cone (F : J ‚•§ C) : Cone F :=
  (cone_nonempty F).some
#align category_theory.is_cofiltered.cone CategoryTheory.IsCofiltered.cone

variable {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D]

/-- If `C` is cofiltered, and we have a functor `L : C ‚•§ D` with a right adjoint,
then `D` is cofiltered.
-/
theorem of_left_adjoint {L : C ‚•§ D} {R : D ‚•§ C} (h : L ‚ä£ R) : IsCofiltered D :=
  { IsCofilteredOrEmpty.of_left_adjoint h with
    nonempty := IsCofiltered.nonempty.map L.obj }
#align category_theory.is_cofiltered.of_left_adjoint CategoryTheory.IsCofiltered.of_left_adjoint

/-- If `C` is cofiltered, and we have a left adjoint functor `L : C ‚•§ D`, then `D` is cofiltered. -/
theorem of_isLeftAdjoint (L : C ‚•§ D) [L.IsLeftAdjoint] : IsCofiltered D :=
  of_left_adjoint (Adjunction.ofIsLeftAdjoint L)
#align category_theory.is_cofiltered.of_is_left_adjoint CategoryTheory.IsCofiltered.of_isLeftAdjoint

/-- Being cofiltered is preserved by equivalence of categories. -/
theorem of_equivalence (h : C ‚âå D) : IsCofiltered D :=
  of_left_adjoint h.toAdjunction
#align category_theory.is_cofiltered.of_equivalence CategoryTheory.IsCofiltered.of_equivalence

end Nonempty


section OfCone

open CategoryTheory.Limits

/-- If every finite diagram in `C` admits a cone, then `C` is cofiltered. It is sufficient to
    verify this for diagrams whose shape lives in any one fixed universe. -/
theorem of_cone_nonempty (h : ‚àÄ {J : Type w} [SmallCategory J] [FinCategory J] (F : J ‚•§ C),
    Nonempty (Cone F)) : IsCofiltered C := by
  have : Nonempty C := by
    obtain ‚ü®c‚ü© := h (Functor.empty _)
    exact ‚ü®c.pt‚ü©
  have : IsCofilteredOrEmpty C := by
    refine ‚ü®?_, ?_‚ü©
    ¬∑ intros X Y
      obtain ‚ü®c‚ü© := h (ULiftHom.down ‚ãô ULift.downFunctor ‚ãô pair X Y)
      exact ‚ü®c.pt, c.œÄ.app ‚ü®‚ü®WalkingPair.left‚ü©‚ü©, c.œÄ.app ‚ü®‚ü®WalkingPair.right‚ü©‚ü©, trivial‚ü©
    ¬∑ intros X Y f g
      obtain ‚ü®c‚ü© := h (ULiftHom.down ‚ãô ULift.downFunctor ‚ãô parallelPair f g)
      refine ‚ü®c.pt, c.œÄ.app ‚ü®WalkingParallelPair.zero‚ü©, ?_‚ü©
      have h‚ÇÅ := c.œÄ.naturality ‚ü®WalkingParallelPairHom.left‚ü©
      have h‚ÇÇ := c.œÄ.naturality ‚ü®WalkingParallelPairHom.right‚ü©
      simp_all
  apply IsCofiltered.mk

theorem of_hasFiniteLimits [HasFiniteLimits C] : IsCofiltered C :=
  of_cone_nonempty.{v} C fun F => ‚ü®limit.cone F‚ü©
#align category_theory.cofiltered_of_has_finite_limits CategoryTheory.IsCofiltered.of_hasFiniteLimits

theorem of_isInitial {X : C} (h : IsInitial X) : IsCofiltered C :=
  of_cone_nonempty.{v} _ fun {_} _ _ _ => ‚ü®‚ü®X, ‚ü®fun _ => h.to _, fun _ _ _ => h.hom_ext _ _‚ü©‚ü©‚ü©

instance (priority := 100) of_hasInitial [HasInitial C] : IsCofiltered C :=
  of_isInitial _ initialIsInitial

@[deprecated] -- 2024-03-11
alias _root_.CategoryTheory.cofiltered_of_hasFiniteLimits := of_hasFiniteLimits

/-- For every universe `w`, `C` is filtered if and only if every finite diagram in `C` with shape
    in `w` admits a cocone. -/
theorem iff_cone_nonempty : IsCofiltered C ‚Üî
    ‚àÄ {J : Type w} [SmallCategory J] [FinCategory J] (F : J ‚•§ C), Nonempty (Cone F) :=
  ‚ü®fun _ _ _ _ F => cone_nonempty F, of_cone_nonempty C‚ü©

end OfCone

end IsCofiltered

section Opposite

open Opposite

instance isCofilteredOrEmpty_op_of_isFilteredOrEmpty [IsFilteredOrEmpty C] :
    IsCofilteredOrEmpty C·µí·µñ where
  cone_objs X Y :=
    ‚ü®op (IsFiltered.max X.unop Y.unop), (IsFiltered.leftToMax _ _).op,
      (IsFiltered.rightToMax _ _).op, trivial‚ü©
  cone_maps X Y f g :=
    ‚ü®op (IsFiltered.coeq f.unop g.unop), (IsFiltered.coeqHom _ _).op, by
      rw [show f = f.unop.op by simp, show g = g.unop.op by simp, ‚Üê op_comp, ‚Üê op_comp]
      congr 1
      exact IsFiltered.coeq_condition f.unop g.unop‚ü©

instance isCofiltered_op_of_isFiltered [IsFiltered C] : IsCofiltered C·µí·µñ where
  nonempty := letI : Nonempty C := IsFiltered.nonempty; inferInstance
#align category_theory.is_cofiltered_op_of_is_filtered CategoryTheory.isCofiltered_op_of_isFiltered

instance isFilteredOrEmpty_op_of_isCofilteredOrEmpty [IsCofilteredOrEmpty C] :
    IsFilteredOrEmpty C·µí·µñ where
  cocone_objs X Y :=
    ‚ü®op (IsCofiltered.min X.unop Y.unop), (IsCofiltered.minToLeft X.unop Y.unop).op,
      (IsCofiltered.minToRight X.unop Y.unop).op, trivial‚ü©
  cocone_maps X Y f g :=
    ‚ü®op (IsCofiltered.eq f.unop g.unop), (IsCofiltered.eqHom f.unop g.unop).op, by
      rw [show f = f.unop.op by simp, show g = g.unop.op by simp, ‚Üê op_comp, ‚Üê op_comp]
      congr 1
      exact IsCofiltered.eq_condition f.unop g.unop‚ü©

instance isFiltered_op_of_isCofiltered [IsCofiltered C] : IsFiltered C·µí·µñ where
  nonempty := letI : Nonempty C := IsCofiltered.nonempty; inferInstance
#align category_theory.is_filtered_op_of_is_cofiltered CategoryTheory.isFiltered_op_of_isCofiltered

/-- If C·µí·µñ is filtered or empty, then C is cofiltered or empty. -/
lemma isCofilteredOrEmpty_of_isFilteredOrEmpty_op [IsFilteredOrEmpty C·µí·µñ] : IsCofilteredOrEmpty C :=
  IsCofilteredOrEmpty.of_equivalence (opOpEquivalence _)

/-- If C·µí·µñ is cofiltered or empty, then C is filtered or empty. -/
lemma isFilteredOrEmpty_of_isCofilteredOrEmpty_op [IsCofilteredOrEmpty C·µí·µñ] : IsFilteredOrEmpty C :=
  IsFilteredOrEmpty.of_equivalence (opOpEquivalence _)

/-- If C·µí·µñ is filtered, then C is cofiltered. -/
lemma isCofiltered_of_isFiltered_op [IsFiltered C·µí·µñ] : IsCofiltered C :=
  IsCofiltered.of_equivalence (opOpEquivalence _)

/-- If C·µí·µñ is cofiltered, then C is filtered. -/
lemma isFiltered_of_isCofiltered_op [IsCofiltered C·µí·µñ] : IsFiltered C :=
  IsFiltered.of_equivalence (opOpEquivalence _)

end Opposite

section ULift

instance [IsFiltered C] : IsFiltered (ULift.{u‚ÇÇ} C) :=
  IsFiltered.of_equivalence ULift.equivalence

instance [IsCofiltered C] : IsCofiltered (ULift.{u‚ÇÇ} C) :=
  IsCofiltered.of_equivalence ULift.equivalence

instance [IsFiltered C] : IsFiltered (ULiftHom C) :=
  IsFiltered.of_equivalence ULiftHom.equiv

instance [IsCofiltered C] : IsCofiltered (ULiftHom C) :=
  IsCofiltered.of_equivalence ULiftHom.equiv

instance [IsFiltered C] : IsFiltered (AsSmall C) :=
  IsFiltered.of_equivalence AsSmall.equiv

instance [IsCofiltered C] : IsCofiltered (AsSmall C) :=
  IsCofiltered.of_equivalence AsSmall.equiv

end ULift

end CategoryTheory
