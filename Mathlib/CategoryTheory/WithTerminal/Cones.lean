/-
Copyright (c) 2025 MoisÃ©s HerradÃ³n Cueto. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: MoisÃ©s HerradÃ³n Cueto
-/
import Mathlib.CategoryTheory.Comma.Over.Basic
import Mathlib.CategoryTheory.WithTerminal.Basic

/-!
# Relations between `Cone`, `WithTerminal` and `Over`

Given categories `C` and `J`, an object `X : C` and a functor `K : J â¥¤ Over X`,
it has an obvious lift `liftFromOver K : WithTerminal J â¥¤ C`, namely, send the terminal
object to `X`. These two functors have equivalent categories of cones (`coneEquiv`).
As a corollary, the limit of `K` is the limit of `liftFromOver K`, and viceversa.
-/

open CategoryTheory CategoryTheory.Limits CategoryTheory.WithTerminal

universe w w' vâ‚ vâ‚‚ uâ‚ uâ‚‚
variable {C : Type uâ‚} [Category.{vâ‚} C]
variable {D : Type uâ‚‚} [Category.{vâ‚‚} D]
variable {J : Type w} [Category.{w'} J]

namespace CategoryTheory.Limits.WithTerminal

/-- The category of functors `J â¥¤ Over X` can be seen as part of a comma category,
namely the comma category constructed from the identity of the category of functors
`J â¥¤ C` and the functor that maps `X : C` to the constant functor `J â¥¤ C`.

Given a functor `K : J â¥¤ Over X`, it is mapped to a natural transformation from the
obvious functor `J â¥¤ C` to the constant functor `X`. -/
@[simps]
def commaFromFunctorToOver {X : C} : (J â¥¤ Over X) â¥¤ Comma (ğŸ­ (J â¥¤ C)) (Functor.const J) where
  obj K := {
    left := K â‹™ Over.forget X
    right := X
    hom.app a := (K.obj a).hom
  }
  map f := {
    left := whiskerRight f (Over.forget X)
    right := ğŸ™ X
  }

/-- For any functor `K : J â¥¤ Over X`, there is a canonical extension
`WithTerminal J â¥¤ C`, that sends `star` to `X`. -/
@[simps!]
def liftFromOver {X : C} : (J â¥¤ Over X) â¥¤ (WithTerminal J â¥¤ C) :=
  commaFromFunctorToOver â‹™ equivComma.inverse

/-- The extension of a functor to over categories behaves well with compositions. -/
@[simps]
def extendCompose {X : C} (K : J â¥¤ Over X) (F : C â¥¤ D) :
    (liftFromOver.obj K â‹™ F) â‰… liftFromOver.obj (K â‹™ (Over.post F)) where
  hom.app
  | star => ğŸ™ _
  | of a => ğŸ™ _
  inv.app
  | star => ğŸ™ _
  | of a => ğŸ™ _

/-- A cone of a functor `K : J â¥¤ Over X` consists of an object of `Over X`, together
with morphisms. This same object is a cone of the extended functor
`liftFromOver.obj K : WithTerminal J â¥¤ C`. -/
@[simps]
def coneLift {X : C} {K : J â¥¤ Over X} : Cone K â¥¤ Cone (liftFromOver.obj K) where
  obj t := {
    pt := t.pt.left
    Ï€.app
    | of a => CommaMorphism.left (t.Ï€.app a)
    | star => t.pt.hom
    Ï€.naturality
    | star , star , _
    | of a, star, _ => by aesop
    | star, of _, _ => by contradiction
    | of a, of b , f => by
      have := by
        calc
          (t.Ï€.app b).left = (t.Ï€.app a â‰« K.map f).left := by
            simp only [Functor.const_obj_obj, Cone.w]
          _ = (t.Ï€.app a).left â‰« (K.map f).left := rfl
      simpa
  }
  map {tâ‚ tâ‚‚} f := {
    hom := f.hom.left
    w
    | star => by aesop_cat
    | of a => by
      have := by calc
        f.hom.left â‰« (tâ‚‚.Ï€.app a).left = (f.hom â‰« tâ‚‚.Ï€.app a).left := by rfl_cat
        _ = (tâ‚.Ï€.app a).left := by simp_all only [ConeMorphism.w, Functor.const_obj_obj]
      simpa
  }

/-- This is the inverse of the previous construction: a cone of an extended functor
`liftFromOver.obj K : WithTerminal J â¥¤ C` consists of an object of `C`, together
with morphisms. This same object is a cone of the original functor `K : J â¥¤ Over X`. -/
@[simps map obj_Ï€]
def coneBack {X : C} {K : J â¥¤ Over X} : Cone (liftFromOver.obj K) â¥¤ Cone K where
  obj t := {
    pt := Over.mk (t.Ï€.app star)
    Ï€.app a := {
      left := t.Ï€.app (of a)
      right := ğŸ™ _
      w := by
        have := by
          calc
            t.Ï€.app (of a) â‰« (K.obj a).hom = t.Ï€.app (of a) â‰«
              (liftFromOver.obj K).map (homFrom a) := rfl
            _ = t.Ï€.app star := by simp only [Functor.const_obj_obj, Cone.w]
        simpa
    }
    Ï€.naturality a b f := by
      ext
      let fâ‚‚ := incl.map f
      have eq_after_K: (K.map fâ‚‚).left = (K.map f).left := by aesop
      have nat : t.Ï€.app (of b) =
        t.Ï€.app (of a) â‰« (K.map fâ‚‚).left := by simpa using t.Ï€.naturality fâ‚‚
      simp [nat, eq_after_K]
  }
  map {tâ‚ tâ‚‚ f} := {
    hom := Over.homMk f.hom
  }

@[simp]
theorem coneBack_obj_pt {X : C} {K : J â¥¤ Over X} (t : Cone (liftFromOver.obj K)) :
    (coneBack.obj t).pt  = Over.mk (t.Ï€.app star) := rfl

/-- The isomorphism between `coneLift â‹™ coneBack` and the identity, at the level of objects. -/
@[simps]
def coneLiftBack {X : C} {K : J â¥¤ Over X} (t : Cone K) : coneBack.obj (coneLift.obj t) â‰… t where
  hom.hom := ğŸ™ t.pt
  inv.hom := ğŸ™ t.pt

/-- The isomorphism between `coneBack â‹™ coneLift` and the identity, at the level of objects. -/
@[simps]
def coneBackLift {X : C} {K : J â¥¤ Over X} (t : Cone (liftFromOver.obj K)) :
coneLift.obj (coneBack.obj t) â‰… t where
  hom.hom := ğŸ™ t.pt
  inv.hom := ğŸ™ t.pt

/-- The equivalence made up of `coneBack` and `coneLift`. -/
def coneEquiv {X : C} (K : J â¥¤ Over X) : Cone K â‰Œ Cone (liftFromOver.obj K) where
  functor := coneLift
  inverse := coneBack
  unitIso := NatIso.ofComponents coneLiftBack
  counitIso := NatIso.ofComponents coneBackLift

/-- A cone `t` of `K : J â¥¤ Over X` is a limit if and only if the corresponding cone
`coneLift t` of `liftFromOver.obj K : WithTerminal K â¥¤ C` is a limit. -/
def limitEquiv {X : C} {K : J â¥¤ Over X} {t : Cone K} :
  IsLimit (coneLift.obj t) â‰ƒ IsLimit t := IsLimit.ofConeEquiv (coneEquiv K)

end CategoryTheory.Limits.WithTerminal
