/-
Copyright (c) 2025 Mois√©s Herrad√≥n Cueto. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mois√©s Herrad√≥n Cueto
-/
import Mathlib.CategoryTheory.Comma.Over.Basic
import Mathlib.CategoryTheory.WithTerminal.Basic

/-!
# Relations between `Cone`, `WithTerminal` and `Over`

Given categories `C` and `J`, an object `X : C` and a functor `K : J ‚•§ Over X`,
it has an obvious lift `liftFromOver K : WithTerminal J ‚•§ C`, namely, send the terminal
object to `X`. These two functors have equivalent categories of cones (`coneEquiv`).
As a corollary, the limit of `K` is the limit of `liftFromOver K`, and viceversa.
-/

open CategoryTheory CategoryTheory.Limits CategoryTheory.WithTerminal

universe w w' v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ
variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]
variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
variable {J : Type w} [Category.{w'} J]

namespace CategoryTheory.Limits.WithTerminal

/-- The category of functors `J ‚•§ Over X` can be seen as part of a comma category,
namely the comma category constructed from the identity of the category of functors
`J ‚•§ C` and the functor that maps `X : C` to the constant functor `J ‚•§ C`.

Given a functor `K : J ‚•§ Over X`, it is mapped to a natural transformation from the
obvious functor `J ‚•§ C` to the constant functor `X`. -/
@[simps]
def commaFromFunctorToOver {X : C} : (J ‚•§ Over X) ‚•§ Comma (ùü≠ (J ‚•§ C)) (Functor.const J) where
  obj K := {
    left := K ‚ãô Over.forget X
    right := X
    hom.app a := (K.obj a).hom
  }
  map f := {
    left := whiskerRight f (Over.forget X)
    right := ùüô X
  }

/-- For any functor `K : J ‚•§ Over X`, there is a canonical extension
`WithTerminal J ‚•§ C`, that sends `star` to `X`. -/
@[simps!]
def liftFromOverToWithTerminal {X : C} : (J ‚•§ Over X) ‚•§ WithTerminal J ‚•§ C :=
  commaFromFunctorToOver ‚ãô equivComma.inverse

/-- The extension of a functor to over categories behaves well with compositions. -/
@[simps]
def extendCompose {X : C} (K : J ‚•§ Over X) (F : C ‚•§ D) :
    liftFromOverToWithTerminal.obj K ‚ãô F ‚âÖ
    liftFromOverToWithTerminal.obj (K ‚ãô Over.post F) where
  hom.app
  | star => ùüô _
  | of a => ùüô _
  inv.app
  | star => ùüô _
  | of a => ùüô _

/-- A cone of a functor `K : J ‚•§ Over X` consists of an object of `Over X`, together
with morphisms. This same object is a cone of the extended functor
`liftFromOver.obj K : WithTerminal J ‚•§ C`. -/
@[simps]
private def coneLift {X : C} {K : J ‚•§ Over X} :
    Cone K ‚•§ Cone (liftFromOverToWithTerminal.obj K) where
  obj t := {
    pt := t.pt.left
    œÄ.app
    | of a => (t.œÄ.app a).left
    | star => t.pt.hom
    œÄ.naturality
    | star, star, _
    | of a, star, _ => by aesop
    | star, of _, _ => by contradiction
    | of a, of b, f => by simp [‚Üê Comma.comp_left]
  }
  map {t‚ÇÅ t‚ÇÇ} f := {
    hom := f.hom.left
    w
    | star => by aesop_cat
    | of a => by simp [‚Üê Comma.comp_left]
  }

/-- This is the inverse of the previous construction: a cone of an extended functor
`liftFromOver.obj K : WithTerminal J ‚•§ C` consists of an object of `C`, together
with morphisms. This same object is a cone of the original functor `K : J ‚•§ Over X`. -/
@[simps]
private def coneBack {X : C} {K : J ‚•§ Over X} :
    Cone (liftFromOverToWithTerminal.obj K) ‚•§ Cone K where
  obj t := {
    pt := .mk (t.œÄ.app star)
    œÄ.app a := {
      left := t.œÄ.app (of a)
      right := ùüô _
      w := by simpa using t.w (homFrom a)
    }
    œÄ.naturality a b f := by
      ext; simpa using t.œÄ.naturality (incl.map f)
  }
  map {t‚ÇÅ t‚ÇÇ f} := {
    hom := Over.homMk f.hom
  }

/-- Given a functor `K : J ‚•§ Over X` and its extension `liftFromOver K : WithTerminal J ‚•§ C`,
there is an obvious equivalence between cones of these two functors.
A cone of `K` is an object of `Over X`, so it has the form `t ‚ü∂ X`.
Equivalently, a cone of `WithTerminal K` is an object `t : C`,
and we can recover the structure morphism as `œÄ.app X : t ‚ü∂ X`. -/
@[simps!]
def coneEquiv {X : C} (K : J ‚•§ Over X) : Cone K ‚âå Cone (liftFromOverToWithTerminal.obj K) where
  functor := coneLift
  inverse := coneBack
  unitIso := .refl _
  counitIso := NatIso.ofComponents fun t ‚Ü¶ Cones.ext <| .refl _

end CategoryTheory.Limits.WithTerminal

-- TODO The analogous theorems for WithInitial and Cocone
