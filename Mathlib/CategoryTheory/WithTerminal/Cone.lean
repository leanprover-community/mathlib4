/-
Copyright (c) 2025 Mois√©s Herrad√≥n Cueto. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mois√©s Herrad√≥n Cueto
-/
import Mathlib.CategoryTheory.Comma.Over.Basic
import Mathlib.CategoryTheory.WithTerminal.Basic

/-!
# Relations between `Cone`, `WithTerminal` and `Over`

Given categories `C` and `J`, an object `X : C` and a functor `K : J ‚•§ Over X`,
it has an obvious lift `liftFromOver K : WithTerminal J ‚•§ C`, namely, send the terminal
object to `X`. These two functors have equivalent categories of cones (`coneEquiv`).
As a corollary, the limit of `K` is the limit of `liftFromOver K`, and viceversa.
-/

open CategoryTheory Limits

universe w w' v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ
variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]
variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
variable {J : Type w} [Category.{w'} J]

namespace CategoryTheory.WithTerminal
variable {X : C} {K : J ‚•§ Over X} {F : C ‚•§ D} {t : Cone K}

/-- The category of functors `J ‚•§ Over X` can be seen as part of a comma category,
namely the comma category constructed from the identity of the category of functors
`J ‚•§ C` and the functor that maps `X : C` to the constant functor `J ‚•§ C`.

Given a functor `K : J ‚•§ Over X`, it is mapped to a natural transformation from the
obvious functor `J ‚•§ C` to the constant functor `X`. -/
@[simps]
def commaFromOver : (J ‚•§ Over X) ‚•§ Comma (ùü≠ (J ‚•§ C)) (Functor.const J) where
  obj K := {
    left := K ‚ãô Over.forget X
    right := X
    hom.app a := (K.obj a).hom
  }
  map f := {
    left := Functor.whiskerRight f (Over.forget X)
    right := ùüô X
  }

/-- For any functor `K : J ‚•§ Over X`, there is a canonical extension
`WithTerminal J ‚•§ C`, that sends `star` to `X`. -/
@[simps!]
def liftFromOver : (J ‚•§ Over X) ‚•§ WithTerminal J ‚•§ C := commaFromOver ‚ãô equivComma.inverse

/-- The extension of a functor to over categories behaves well with compositions. -/
@[simps]
def liftFromOverComp : liftFromOver.obj (K ‚ãô Over.post F) ‚âÖ liftFromOver.obj K ‚ãô F where
  hom.app | star | of a => ùüô _
  inv.app | star | of a => ùüô _

/-- A cone of a functor `K : J ‚•§ Over X` consists of an object of `Over X`, together
with morphisms. This same object is a cone of the extended functor
`liftFromOver.obj K : WithTerminal J ‚•§ C`. -/
@[simps]
private def coneLift : Cone K ‚•§ Cone (liftFromOver.obj K) where
  obj t := {
    pt := t.pt.left
    œÄ.app
    | of a => (t.œÄ.app a).left
    | star => t.pt.hom
    œÄ.naturality
    | star, star, _
    | of a, star, _ => by aesop
    | of a, of b, f => by simp [‚Üê Comma.comp_left]
  }
  map {t‚ÇÅ t‚ÇÇ} f := {
    hom := f.hom.left
    w
    | star => by aesop_cat
    | of a => by simp [‚Üê Comma.comp_left]
  }

/-- This is the inverse of the previous construction: a cone of an extended functor
`liftFromOver.obj K : WithTerminal J ‚•§ C` consists of an object of `C`, together
with morphisms. This same object is a cone of the original functor `K : J ‚•§ Over X`. -/
@[simps]
private def coneBack : Cone (liftFromOver.obj K) ‚•§ Cone K where
  obj t := {
    pt := .mk (t.œÄ.app star)
    œÄ.app a := {
      left := t.œÄ.app (of a)
      right := ùüô _
      w := by simpa using t.w (homFrom a)
    }
    œÄ.naturality a b f := by ext; simpa using t.œÄ.naturality (incl.map f)
  }
  map {t‚ÇÅ t‚ÇÇ f} := {
    hom := Over.homMk f.hom
  }

/-- Given a functor `K : J ‚•§ Over X` and its extension `liftFromOver K : WithTerminal J ‚•§ C`,
there is an obvious equivalence between cones of these two functors.
A cone of `K` is an object of `Over X`, so it has the form `t ‚ü∂ X`.
Equivalently, a cone of `WithTerminal K` is an object `t : C`,
and we can recover the structure morphism as `œÄ.app X : t ‚ü∂ X`. -/
@[simps! functor_obj_pt functor_map_hom inverse_obj_pt_left inverse_obj_pt_right_as
  inverse_obj_pt_hom inverse_obj_œÄ_app_left inverse_map_hom_left unitIso_hom_app_hom_left
  unitIso_inv_app_hom_left counitIso_hom_app_hom counitIso_inv_app_hom]
def coneEquiv : Cone K ‚âå Cone (liftFromOver.obj K) where
  functor := coneLift
  inverse := coneBack
  unitIso := .refl _
  counitIso := NatIso.ofComponents fun t ‚Ü¶ Cones.ext <| .refl _

@[simp]
lemma coneEquiv_functor_obj_œÄ_app_star : (coneEquiv.functor.obj t).œÄ.app star = t.pt.hom := rfl

@[simp]
lemma coneEquiv_functor_obj_œÄ_app_of (Y : J) :
    (coneEquiv.functor.obj t).œÄ.app (of Y) = (t.œÄ.app Y).left := rfl

/-- A cone `t` of `K : J ‚•§ Over X` is a limit if and only if the corresponding cone
`coneLift t` of `liftFromOver.obj K : WithTerminal K ‚•§ C` is a limit. -/
@[simps!]
def isLimitEquiv : IsLimit (coneEquiv.functor.obj t) ‚âÉ IsLimit t := IsLimit.ofConeEquiv coneEquiv

end WithTerminal

open WithTerminal in
lemma Over.hasLimit_of_hasLimit_liftFromOver {X : C} (F : J ‚•§ Over X)
    [HasLimit (liftFromOver.obj F)] : HasLimit F :=
  ‚ü®_, isLimitEquiv <| .ofIsoLimit
    (limit.isLimit (liftFromOver.obj F)) (coneEquiv.counitIso.app _).symm‚ü©

instance (X : C) [HasLimitsOfShape (WithTerminal J) C] :
    HasLimitsOfShape J (Over X) where
  has_limit _ := Over.hasLimit_of_hasLimit_liftFromOver ..

instance (X : C) [HasLimitsOfSize.{w, w'} C] : HasLimitsOfSize.{w, w'} (Over X) where

namespace WithInitial
variable {X : C} {K : J ‚•§ Under X} {F : C ‚•§ D} {t : Cocone K}

/-- The category of functors `J ‚•§ Under X` can be seen as part of a comma category,
namely the comma category constructed from the identity of the category of functors
`J ‚•§ C` and the functor that maps `X : C` to the constant functor `J ‚•§ C`.

Given a functor `K : J ‚•§ Under X`, it is mapped to a natural transformation to the
obvious functor `J ‚•§ C` from the constant functor `X`. -/
@[simps]
def commaFromUnder : (J ‚•§ Under X) ‚•§ Comma (Functor.const J) (ùü≠ (J ‚•§ C)) where
  obj K := {
    left := X
    right := K ‚ãô Under.forget X
    hom.app a := (K.obj a).hom
  }
  map f := {
    left := ùüô X
    right := Functor.whiskerRight f (Under.forget X)
  }

/-- For any functor `K : J ‚•§ Under X`, there is a canonical extension
`WithInitial J ‚•§ C`, that sends `star` to `X`. -/
@[simps!]
def liftFromUnder : (J ‚•§ Under X) ‚•§ WithInitial J ‚•§ C := commaFromUnder ‚ãô equivComma.inverse

/-- The extension of a functor to under categories behaves well with compositions. -/
@[simps]
def liftFromUnderComp : liftFromUnder.obj (K ‚ãô Under.post F) ‚âÖ liftFromUnder.obj K ‚ãô F where
  hom.app | star | of a => ùüô _
  inv.app | star | of a => ùüô _

/-- A cocone of a functor `K : J ‚•§ Under X` consists of an object of `Under X`, together
with morphisms. This same object is a cocone of the extended functor
`liftFromUnder.obj K : WithInitial J ‚•§ C`. -/
@[simps]
private def coconeLift : Cocone K ‚•§ Cocone (liftFromUnder.obj K) where
  obj t := {
    pt := t.pt.right
    Œπ.app
    | of a => (t.Œπ.app a).right
    | star => t.pt.hom
    Œπ.naturality
    | star, star, _
    | star, of b, _ => by aesop
    | of a, of b, f => by simp [‚Üê Comma.comp_right]
  }
  map {t‚ÇÅ t‚ÇÇ} f := {
    hom := f.hom.right
    w
    | star => by aesop_cat
    | of a => by simp [‚Üê Comma.comp_right]
  }

/-- This is the inverse of the previous construction: a cocone of an extended functor
`liftFromUnder.obj K : WithInitial J ‚•§ C` consists of an object of `C`, together
with morphisms. This same object is a cocone of the original functor `K : J ‚•§ Under X`. -/
@[simps]
private def coconeBack : Cocone (liftFromUnder.obj K) ‚•§ Cocone K where
  obj t := {
    pt := .mk (t.Œπ.app star)
    Œπ.app a := {
      left := ùüô _
      right := t.Œπ.app (of a)
      w := by simpa using (t.w (homTo a)).symm
    }
    Œπ.naturality a b f := by ext; simpa using t.Œπ.naturality (incl.map f)
  }
  map {t‚ÇÅ t‚ÇÇ f} := {
    hom := Under.homMk f.hom
  }

/-- Given a functor `K : J ‚•§ Under X` and its extension `liftFromUnder K : WithInitial J ‚•§ C`,
there is an obvious equivalence between cocones of these two functors.
A cocone of `K` is an object of `Under X`, so it has the form `X ‚ü∂ t`.
Equivalently, a cocone of `WithInitial K` is an object `t : C`,
and we can recover the structure morphism as `Œπ.app X : X ‚ü∂ t`. -/
@[simps! functor_obj_pt functor_map_hom inverse_obj_pt_right inverse_obj_pt_left_as
  inverse_obj_pt_hom inverse_obj_Œπ_app_right inverse_map_hom_right unitIso_hom_app_hom_right
  unitIso_inv_app_hom_right counitIso_hom_app_hom counitIso_inv_app_hom]
def coconeEquiv : Cocone K ‚âå Cocone (liftFromUnder.obj K) where
  functor := coconeLift
  inverse := coconeBack
  unitIso := .refl _
  counitIso := NatIso.ofComponents fun t ‚Ü¶ Cocones.ext <| .refl _

@[simp]
lemma coconeEquiv_functor_obj_Œπ_app_star : (coconeEquiv.functor.obj t).Œπ.app star = t.pt.hom := rfl

@[simp]
lemma coconeEquiv_functor_obj_Œπ_app_of (Y : J) :
    (coconeEquiv.functor.obj t).Œπ.app (of Y) = (t.Œπ.app Y).right := rfl

/-- A cocone `t` of `K : J ‚•§ Under X` is a colimit if and only if the corresponding cocone
`coconeLift t` of `liftFromUnder.obj K : WithInitial K ‚•§ C` is a colimit. -/
@[simps!]
def isColimitEquiv : IsColimit (coconeEquiv.functor.obj t) ‚âÉ IsColimit t :=
  IsColimit.ofCoconeEquiv coconeEquiv

end CategoryTheory.WithInitial

open WithInitial in
lemma Under.hasColimit_of_hasColimit_liftFromUnder {X : C} (F : J ‚•§ Under X)
    [HasColimit (liftFromUnder.obj F)] : HasColimit F :=
  ‚ü®_, isColimitEquiv <| .ofIsoColimit
    (colimit.isColimit (liftFromUnder.obj F)) (coconeEquiv.counitIso.app _).symm‚ü©

instance (X : C) [HasColimitsOfShape (WithInitial J) C] :
    HasColimitsOfShape J (Under X) where
  has_colimit _ := Under.hasColimit_of_hasColimit_liftFromUnder ..

instance (X : C) [HasColimitsOfSize.{w, w'} C] : HasColimitsOfSize.{w, w'} (Under X) where
