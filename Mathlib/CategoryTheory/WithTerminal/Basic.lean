/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith, Adam Topaz
-/
import Mathlib.CategoryTheory.Limits.Shapes.IsTerminal
import Mathlib.CategoryTheory.Limits.Shapes.Terminal
import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor

/-!

# `WithInitial` and `WithTerminal`

Given a category `C`, this file constructs two objects:
1. `WithTerminal C`, the category built from `C` by formally adjoining a terminal object.
2. `WithInitial C`, the category built from `C` by formally adjoining an initial object.

The terminal resp. initial object is `WithTerminal.star` resp. `WithInitial.star`, and
the proofs that these are terminal resp. initial are in `WithTerminal.star_terminal`
and `WithInitial.star_initial`.

The inclusion from `C` into `WithTerminal C` resp. `WithInitial C` is denoted
`WithTerminal.incl` resp. `WithInitial.incl`.

The relevant constructions needed for the universal properties of these constructions are:
1. `lift`, which lifts `F : C ‚•§ D` to a functor from `WithTerminal C` resp. `WithInitial C` in
  the case where an object `Z : D` is provided satisfying some additional conditions.
2. `inclLift` shows that the composition of `lift` with `incl` is isomorphic to the
  functor which was lifted.
3. `liftUnique` provides the uniqueness property of `lift`.

In addition to this, we provide `WithTerminal.map` and `WithInitial.map` providing the
functoriality of these constructions with respect to functors on the base categories.

We define corresponding pseudofunctors `WithTerminal.pseudofunctor` and `WithInitial.pseudofunctor`
from `Cat` to `Cat`.

-/


namespace CategoryTheory

universe v u

variable (C : Type u) [Category.{v} C]

/-- Formally adjoin a terminal object to a category. -/
inductive WithTerminal : Type u
  | of : C ‚Üí WithTerminal
  | star : WithTerminal
  deriving Inhabited

attribute [local aesop safe cases (rule_sets := [CategoryTheory])] WithTerminal

/-- Formally adjoin an initial object to a category. -/
inductive WithInitial : Type u
  | of : C ‚Üí WithInitial
  | star : WithInitial
  deriving Inhabited

attribute [local aesop safe cases (rule_sets := [CategoryTheory])] WithInitial

namespace WithTerminal

variable {C}

/-- Morphisms for `WithTerminal C`. -/
@[simp]
def Hom : WithTerminal C ‚Üí WithTerminal C ‚Üí Type v
  | of X, of Y => X ‚ü∂ Y
  | star, of _ => PEmpty
  | _, star => PUnit
attribute [nolint simpNF] Hom.eq_3

/-- Identity morphisms for `WithTerminal C`. -/
@[simp]
def id : ‚àÄ X : WithTerminal C, Hom X X
  | of _ => ùüô _
  | star => PUnit.unit

/-- Composition of morphisms for `WithTerminal C`. -/
@[simp]
def comp : ‚àÄ {X Y Z : WithTerminal C}, Hom X Y ‚Üí Hom Y Z ‚Üí Hom X Z
  | of _X, of _Y, of _Z => fun f g => f ‚â´ g
  | of _X, _, star => fun _f _g => PUnit.unit
  | star, of _X, _ => fun f _g => PEmpty.elim f
  | _, star, of _Y => fun _f g => PEmpty.elim g
  | star, star, star => fun _ _ => PUnit.unit
attribute [nolint simpNF] comp.eq_4

instance : Category.{v} (WithTerminal C) where
  Hom X Y := Hom X Y
  id _ := id _
  comp := comp
  assoc {a b c d} f g h := by
    -- Porting note: it would be nice to automate this away as well.
    -- I tried splitting this into separate `Quiver` and `Category` instances,
    -- so the `false_of_from_star` destruct rule below can be used here.
    -- That works, but causes mysterious failures of `aesop_cat` in `map`.
    cases a <;> cases b <;> cases c <;> cases d <;> try aesop_cat
    ¬∑ exact (h : PEmpty).elim
    ¬∑ exact (g : PEmpty).elim
    ¬∑ exact (h : PEmpty).elim

/-- Helper function for typechecking. -/
def down {X Y : C} (f : of X ‚ü∂ of Y) : X ‚ü∂ Y := f

@[simp] lemma down_id {X : C} : down (ùüô (of X)) = ùüô X := rfl
@[simp] lemma down_comp {X Y Z : C} (f : of X ‚ü∂ of Y) (g : of Y ‚ü∂ of Z) :
    down (f ‚â´ g) = down f ‚â´ down g :=
  rfl

@[aesop safe destruct (rule_sets := [CategoryTheory])]
lemma false_of_from_star {X : C} (f : star ‚ü∂ of X) : False := (f : PEmpty).elim

/-- The inclusion from `C` into `WithTerminal C`. -/
def incl : C ‚•§ WithTerminal C where
  obj := of
  map f := f

instance : (incl : C ‚•§ _).Full where
  map_surjective f := ‚ü®f, rfl‚ü©

instance : (incl : C ‚•§ _).Faithful where

/-- Map `WithTerminal` with respect to a functor `F : C ‚•§ D`. -/
@[simps]
def map {D : Type*} [Category D] (F : C ‚•§ D) : WithTerminal C ‚•§ WithTerminal D where
  obj X :=
    match X with
    | of x => of <| F.obj x
    | star => star
  map {X Y} f :=
    match X, Y, f with
    | of _, of _, f => F.map (down f)
    | of _, star, _ => PUnit.unit
    | star, star, _ => PUnit.unit

/-- A natural isomorphism between the functor `map (ùü≠ C)` and `ùü≠ (WithTerminal C)`. -/
@[simps!]
def mapId (C : Type*) [Category C] : map (ùü≠ C) ‚âÖ ùü≠ (WithTerminal C) :=
  NatIso.ofComponents (fun X => match X with
    | of _ => Iso.refl _
    | star => Iso.refl _) (by aesop_cat)

/-- A natural isomorphism between the functor `map (F ‚ãô G) ` and `map F ‚ãô map G `. -/
@[simps!]
def mapComp {D E : Type*} [Category D] [Category E] (F : C ‚•§ D) (G : D ‚•§ E) :
    map (F ‚ãô G) ‚âÖ map F ‚ãô map G :=
  NatIso.ofComponents (fun X => match X with
    | of _ => Iso.refl _
    | star => Iso.refl _) (by aesop_cat)

/-- From a natural transformation of functors `C ‚•§ D`, the induced natural transformation
of functors `WithTerminal C ‚•§ WithTerminal D`. -/
@[simps]
def map‚ÇÇ {D : Type*} [Category D] {F G : C ‚•§ D} (Œ∑ : F ‚ü∂ G) : map F ‚ü∂ map G where
  app := fun X => match X with
    | of x => Œ∑.app x
    | star => ùüô star
  naturality := by
    intro X Y f
    match X, Y, f with
    | of x, of y, f => exact Œ∑.naturality f
    | of x, star, _ => rfl
    | star, star, _ => rfl

-- Note: ...
/-- The prelax functor from `Cat` to `Cat` defined with `WithTerminal`. -/
@[simps]
def prelaxfunctor : PrelaxFunctor Cat Cat where
  obj C := Cat.of (WithTerminal C)
  map := map
  map‚ÇÇ := map‚ÇÇ
  map‚ÇÇ_id := by
    intros
    apply NatTrans.ext
    funext X
    cases X <;> rfl
  map‚ÇÇ_comp := by
    intros
    apply NatTrans.ext
    funext X
    cases X <;> rfl

/-- The pseudofunctor from `Cat` to `Cat` defined with `WithTerminal`. -/
@[simps]
def pseudofunctor : Pseudofunctor Cat Cat where
  toPrelaxFunctor := prelaxfunctor
  mapId C := mapId C
  mapComp := mapComp
  map‚ÇÇ_whisker_left := by
    intros
    apply NatTrans.ext
    funext X
    cases X
    ¬∑ rw [NatTrans.comp_app, NatTrans.comp_app]
      simp only [prelaxfunctor_toPrelaxFunctorStruct_toPrefunctor_obj,
        prelaxfunctor_toPrelaxFunctorStruct_toPrefunctor_map, map_obj, Cat.comp_obj,
        prelaxfunctor_toPrelaxFunctorStruct_map‚ÇÇ, map‚ÇÇ_app, Cat.whiskerLeft_app, mapComp_hom_app,
        Iso.refl_hom, mapComp_inv_app, Iso.refl_inv, Category.comp_id, Category.id_comp]
    ¬∑ rfl
  map‚ÇÇ_whisker_right := by
    intros
    apply NatTrans.ext
    funext X
    cases X
    ¬∑ rw [NatTrans.comp_app, NatTrans.comp_app]
      simp only [prelaxfunctor_toPrelaxFunctorStruct_toPrefunctor_obj,
        prelaxfunctor_toPrelaxFunctorStruct_toPrefunctor_map, map_obj, Cat.comp_obj,
        prelaxfunctor_toPrelaxFunctorStruct_map‚ÇÇ, map‚ÇÇ_app, Cat.whiskerRight_app, mapComp_hom_app,
        Iso.refl_hom, map_map, mapComp_inv_app, Iso.refl_inv, Category.comp_id, Category.id_comp]
      rfl
    ¬∑ rfl
  map‚ÇÇ_associator := by
    intros
    dsimp
    apply NatTrans.ext
    funext X
    cases X
    ¬∑ rw [NatTrans.comp_app,NatTrans.comp_app,NatTrans.comp_app,NatTrans.comp_app]
      simp only [prelaxfunctor_toPrelaxFunctorStruct_toPrefunctor_obj,
        prelaxfunctor_toPrelaxFunctorStruct_toPrefunctor_map, map_obj, Cat.comp_obj,
        Bicategory.Strict.associator_eqToIso, eqToIso_refl, Iso.refl_hom,
        prelaxfunctor_toPrelaxFunctorStruct_map‚ÇÇ, map‚ÇÇ_app, mapComp_hom_app, Cat.whiskerRight_app,
        map_map, down_id, Functor.map_id, Cat.whiskerLeft_app, mapComp_inv_app, Iso.refl_inv,
        Category.comp_id, Category.id_comp]
      rw [NatTrans.id_app, NatTrans.id_app]
      simp only [Cat.comp_obj, Bicategory.whiskerRight, whiskerRight_app, map_obj, mapComp_hom_app,
        Iso.refl_hom, map_map, down_id, Functor.map_id, Bicategory.whiskerLeft, whiskerLeft_app,
        mapComp_inv_app, Iso.refl_inv, Category.comp_id]
    ¬∑ rfl
  map‚ÇÇ_left_unitor := by
    intros
    apply NatTrans.ext
    funext X
    cases X
    ¬∑ rw [NatTrans.comp_app, NatTrans.comp_app]
      simp only [prelaxfunctor_toPrelaxFunctorStruct_toPrefunctor_obj,
        prelaxfunctor_toPrelaxFunctorStruct_toPrefunctor_map, map_obj, Cat.comp_obj,
        Bicategory.Strict.leftUnitor_eqToIso, eqToIso_refl, Iso.refl_hom,
        prelaxfunctor_toPrelaxFunctorStruct_map‚ÇÇ, map‚ÇÇ_app, mapComp_hom_app, Cat.whiskerRight_app,
        mapId_hom_app, map_map, Category.id_comp]
      rw [NatTrans.id_app, NatTrans.id_app]
      simp only [Cat.comp_obj, map_obj, Category.comp_id]
      rw [‚Üê Functor.map_id]
      rfl
    ¬∑ rfl
  map‚ÇÇ_right_unitor := by
    intros
    apply NatTrans.ext
    funext X
    cases X
    ¬∑ rw [NatTrans.comp_app, NatTrans.comp_app]
      simp only [prelaxfunctor_toPrelaxFunctorStruct_toPrefunctor_obj,
        prelaxfunctor_toPrelaxFunctorStruct_toPrefunctor_map, map_obj, Cat.comp_obj,
        Bicategory.Strict.rightUnitor_eqToIso, eqToIso_refl, Iso.refl_hom,
        prelaxfunctor_toPrelaxFunctorStruct_map‚ÇÇ, map‚ÇÇ_app, mapComp_hom_app, Cat.whiskerLeft_app,
        mapId_hom_app, Category.id_comp]
      rw [NatTrans.id_app, NatTrans.id_app]
      simp only [Cat.comp_obj, map_obj, Category.comp_id]
      rw [‚Üê Functor.map_id]
      rfl
    ¬∑ rfl

instance {X : WithTerminal C} : Unique (X ‚ü∂ star) where
  default :=
    match X with
    | of _ => PUnit.unit
    | star => PUnit.unit
  uniq := by aesop_cat

/-- `WithTerminal.star` is terminal. -/
def starTerminal : Limits.IsTerminal (star : WithTerminal C) :=
  Limits.IsTerminal.ofUnique _

instance : Limits.HasTerminal (WithTerminal C) := Limits.hasTerminal_of_unique star

/-- The isomorphism between star and an abstract terminal object of `WithTerminal C` -/
@[simps!]
noncomputable def starIsoTerminal : star ‚âÖ ‚ä§_ (WithTerminal C) :=
  starTerminal.uniqueUpToIso (Limits.terminalIsTerminal)

/-- Lift a functor `F : C ‚•§ D` to `WithTerminal C ‚•§ D`. -/
@[simps]
def lift {D : Type*} [Category D] {Z : D} (F : C ‚•§ D) (M : ‚àÄ x : C, F.obj x ‚ü∂ Z)
    (hM : ‚àÄ (x y : C) (f : x ‚ü∂ y), F.map f ‚â´ M y = M x) : WithTerminal C ‚•§ D where
  obj X :=
    match X with
    | of x => F.obj x
    | star => Z
  map {X Y} f :=
    match X, Y, f with
    | of _, of _, f => F.map (down f)
    | of x, star, _ => M x
    | star, star, _ => ùüô Z

/-- The isomorphism between `incl ‚ãô lift F _ _` with `F`. -/
@[simps!]
def inclLift {D : Type*} [Category D] {Z : D} (F : C ‚•§ D) (M : ‚àÄ x : C, F.obj x ‚ü∂ Z)
    (hM : ‚àÄ (x y : C) (f : x ‚ü∂ y), F.map f ‚â´ M y = M x) : incl ‚ãô lift F M hM ‚âÖ F where
  hom := { app := fun _ => ùüô _ }
  inv := { app := fun _ => ùüô _ }

/-- The isomorphism between `(lift F _ _).obj WithTerminal.star` with `Z`. -/
@[simps!]
def liftStar {D : Type*} [Category D] {Z : D} (F : C ‚•§ D) (M : ‚àÄ x : C, F.obj x ‚ü∂ Z)
    (hM : ‚àÄ (x y : C) (f : x ‚ü∂ y), F.map f ‚â´ M y = M x) : (lift F M hM).obj star ‚âÖ Z :=
  eqToIso rfl

theorem lift_map_liftStar {D : Type*} [Category D] {Z : D} (F : C ‚•§ D) (M : ‚àÄ x : C, F.obj x ‚ü∂ Z)
    (hM : ‚àÄ (x y : C) (f : x ‚ü∂ y), F.map f ‚â´ M y = M x) (x : C) :
    (lift F M hM).map (starTerminal.from (incl.obj x)) ‚â´ (liftStar F M hM).hom =
      (inclLift F M hM).hom.app x ‚â´ M x := by
  simp
  rfl

/-- The uniqueness of `lift`. -/
@[simp]
def liftUnique {D : Type*} [Category D] {Z : D} (F : C ‚•§ D) (M : ‚àÄ x : C, F.obj x ‚ü∂ Z)
    (hM : ‚àÄ (x y : C) (f : x ‚ü∂ y), F.map f ‚â´ M y = M x)
    (G : WithTerminal C ‚•§ D) (h : incl ‚ãô G ‚âÖ F)
    (hG : G.obj star ‚âÖ Z)
    (hh : ‚àÄ x : C, G.map (starTerminal.from (incl.obj x)) ‚â´ hG.hom = h.hom.app x ‚â´ M x) :
    G ‚âÖ lift F M hM :=
  NatIso.ofComponents
    (fun X =>
      match X with
      | of x => h.app x
      | star => hG)
    (by
      rintro (X | X) (Y | Y) f
      ¬∑ apply h.hom.naturality
      ¬∑ cases f
        exact hh _
      ¬∑ cases f
      ¬∑ cases f
        change G.map (ùüô _) ‚â´ hG.hom = hG.hom ‚â´ ùüô _
        simp)

/-- A variant of `lift` with `Z` a terminal object. -/
@[simps!]
def liftToTerminal {D : Type*} [Category D] {Z : D} (F : C ‚•§ D) (hZ : Limits.IsTerminal Z) :
    WithTerminal C ‚•§ D :=
  lift F (fun _x => hZ.from _) fun _x _y _f => hZ.hom_ext _ _

/-- A variant of `incl_lift` with `Z` a terminal object. -/
@[simps!]
def inclLiftToTerminal {D : Type*} [Category D] {Z : D} (F : C ‚•§ D) (hZ : Limits.IsTerminal Z) :
    incl ‚ãô liftToTerminal F hZ ‚âÖ F :=
  inclLift _ _ _

/-- A variant of `lift_unique` with `Z` a terminal object. -/
@[simps!]
def liftToTerminalUnique {D : Type*} [Category D] {Z : D} (F : C ‚•§ D) (hZ : Limits.IsTerminal Z)
    (G : WithTerminal C ‚•§ D) (h : incl ‚ãô G ‚âÖ F) (hG : G.obj star ‚âÖ Z) : G ‚âÖ liftToTerminal F hZ :=
  liftUnique F (fun _z => hZ.from _) (fun _x _y _f => hZ.hom_ext _ _) G h hG fun _x =>
    hZ.hom_ext _ _

/-- Constructs a morphism to `star` from `of X`. -/
@[simp]
def homFrom (X : C) : incl.obj X ‚ü∂ star :=
  starTerminal.from _

instance isIso_of_from_star {X : WithTerminal C} (f : star ‚ü∂ X) : IsIso f :=
  match X with
  | of _X => f.elim
  | star => ‚ü®f, rfl, rfl‚ü©

section

variable {D : Type*} [Category D]

/-- A functor `WithTerminal C ‚•§ D` can be seen as an element of the comma category
`Comma (ùü≠ (C ‚•§ D)) (const C)`. -/
@[simps!]
def mkCommaObject (F : WithTerminal C ‚•§ D) : Comma (ùü≠ (C ‚•§ D)) (Functor.const C) where
  right := F.obj .star
  left := (incl ‚ãô F)
  hom :=
    { app x := F.map (starTerminal.from (.of x))
      naturality x y f := by
        dsimp
        rw [Category.comp_id, ‚Üê F.map_comp]
        congr 1}

/-- A morphism of functors `WithTerminal C ‚•§ D` gives a morphism between the associated comma
objects. -/
@[simps!]
def mkCommaMorphism {F G : WithTerminal C ‚•§ D} (Œ∑ : F ‚ü∂ G) : mkCommaObject F ‚ü∂ mkCommaObject G where
  right := Œ∑.app .star
  left := whiskerLeft incl Œ∑

/-- An element of the comma category `Comma (ùü≠ (C ‚•§ D)) (Functor.const C)` can be seen as a
functor `WithTerminal C ‚•§ D`. -/
@[simps!]
def ofCommaObject (c : Comma (ùü≠ (C ‚•§ D)) (Functor.const C)) : WithTerminal C ‚•§ D :=
  lift (Z := c.right) c.left (fun x ‚Ü¶ c.hom.app x) (fun x y f ‚Ü¶ by simp)

/-- A morphism in `Comma (ùü≠ (C ‚•§ D)) (Functor.const C)` gives a morphism between the associated
functors `WithTerminal C ‚•§ D`. -/
@[simps!]
def ofCommaMorphism {c c' : Comma (ùü≠ (C ‚•§ D)) (Functor.const C)} (œÜ : c ‚ü∂ c') :
    ofCommaObject c ‚ü∂ ofCommaObject c' where
  app x :=
    match x with
    | of x => œÜ.left.app x
    | star => œÜ.right
  naturality x y f :=
    match x, y, f with
    | of _, of _, f => by simp
    | of a, star, _ => by simp; simpa [-CommaMorphism.w] using (congrArg (fun f ‚Ü¶ f.app a) œÜ.w).symm
    | star, star, _ => by simp

/-- The category of functors `WithTerminal C ‚•§ D` is equivalent to the category
`Comma (ùü≠ (C ‚•§ D)) (const C) `. -/
@[simps!]
def equivComma : (WithTerminal C ‚•§ D) ‚âå Comma (ùü≠ (C ‚•§ D)) (Functor.const C) where
  functor :=
    { obj := mkCommaObject
      map := mkCommaMorphism }
  inverse :=
    { obj := ofCommaObject
      map := ofCommaMorphism }
  unitIso :=
    NatIso.ofComponents
      (fun F ‚Ü¶ liftUnique
        (incl ‚ãô F)
        (fun x ‚Ü¶ F.map (starTerminal.from (of x)))
        (fun x y f ‚Ü¶ by
          simp only [Functor.comp_obj, Functor.comp_map]
          rw [‚Üê F.map_comp]
          congr 1)
        F (Iso.refl _) (Iso.refl _)
        (fun x ‚Ü¶ by
          simp only [Iso.refl_symm, Iso.refl_hom, Category.id_comp, Functor.comp_obj,
            NatTrans.id_app, Category.comp_id]; rfl))
      (fun {x y} f ‚Ü¶ by ext t; cases t <;> simp [incl])
  counitIso := NatIso.ofComponents (fun F ‚Ü¶ Iso.refl _)
  functor_unitIso_comp x := by
    simp only [id_eq, Functor.id_obj, ofCommaObject_obj, ofCommaMorphism_app, Comma.id_right,
      NatTrans.id_app, Comma.id_left, Comma.comp_right, NatTrans.comp_app, Comma.comp_left,
      Functor.comp_obj, liftUnique, Functor.comp_map, eq_mpr_eq_cast, lift_obj,
      NatIso.ofComponents_hom_app, Iso.refl_hom, Category.comp_id]
    ext <;> rfl

end

end WithTerminal

namespace WithInitial

variable {C}

/-- Morphisms for `WithInitial C`. -/
@[simp]
def Hom : WithInitial C ‚Üí WithInitial C ‚Üí Type v
  | of X, of Y => X ‚ü∂ Y
  | of _, _ => PEmpty
  | star, _ => PUnit
attribute [nolint simpNF] Hom.eq_2

/-- Identity morphisms for `WithInitial C`. -/
@[simp]
def id : ‚àÄ X : WithInitial C, Hom X X
  | of _ => ùüô _
  | star => PUnit.unit

/-- Composition of morphisms for `WithInitial C`. -/
@[simp]
def comp : ‚àÄ {X Y Z : WithInitial C}, Hom X Y ‚Üí Hom Y Z ‚Üí Hom X Z
  | of _X, of _Y, of _Z => fun f g => f ‚â´ g
  | star, _, of _X => fun _f _g => PUnit.unit
  | _, of _X, star => fun _f g => PEmpty.elim g
  | of _Y, star, _ => fun f _g => PEmpty.elim f
  | star, star, star => fun _ _ => PUnit.unit
attribute [nolint simpNF] comp.eq_3

instance : Category.{v} (WithInitial C) where
  Hom X Y := Hom X Y
  id X := id X
  comp f g := comp f g
  assoc {a b c d} f g h := by
    -- Porting note: it would be nice to automate this away as well.
    -- See the note on `Category (WithTerminal C)`
    cases a <;> cases b <;> cases c <;> cases d <;> try aesop_cat
    ¬∑ exact (g : PEmpty).elim
    ¬∑ exact (f : PEmpty).elim
    ¬∑ exact (f : PEmpty).elim

/-- Helper function for typechecking. -/
def down {X Y : C} (f : of X ‚ü∂ of Y) : X ‚ü∂ Y := f

@[simp] lemma down_id {X : C} : down (ùüô (of X)) = ùüô X := rfl
@[simp] lemma down_comp {X Y Z : C} (f : of X ‚ü∂ of Y) (g : of Y ‚ü∂ of Z) :
    down (f ‚â´ g) = down f ‚â´ down g :=
  rfl

@[aesop safe destruct (rule_sets := [CategoryTheory])]
lemma false_of_to_star {X : C} (f : of X ‚ü∂ star) : False := (f : PEmpty).elim

/-- The inclusion of `C` into `WithInitial C`. -/
def incl : C ‚•§ WithInitial C where
  obj := of
  map f := f

instance : (incl : C ‚•§ _).Full where
  map_surjective f := ‚ü®f, rfl‚ü©

instance : (incl : C ‚•§ _).Faithful where

/-- Map `WithInitial` with respect to a functor `F : C ‚•§ D`. -/
@[simps]
def map {D : Type*} [Category D] (F : C ‚•§ D) : WithInitial C ‚•§ WithInitial D where
  obj X :=
    match X with
    | of x => of <| F.obj x
    | star => star
  map {X Y} f :=
    match X, Y, f with
    | of _, of _, f => F.map (down f)
    | star, of _, _ => PUnit.unit
    | star, star, _ => PUnit.unit

/-- A natural isomorphism between the functor `map (ùü≠ C)` and `ùü≠ (WithInitial C)`. -/
@[simps!]
def mapId (C : Type*) [Category C] : map (ùü≠ C) ‚âÖ ùü≠ (WithInitial C) :=
  NatIso.ofComponents (fun X => match X with
    | of _ => Iso.refl _
    | star => Iso.refl _) (by aesop_cat)

/-- A natural isomorphism between the functor `map (F ‚ãô G) ` and `map F ‚ãô map G `. -/
@[simps!]
def mapComp {D E : Type*} [Category D] [Category E] (F : C ‚•§ D) (G : D ‚•§ E) :
    map (F ‚ãô G) ‚âÖ map F ‚ãô map G :=
  NatIso.ofComponents (fun X => match X with
    | of _ => Iso.refl _
    | star => Iso.refl _) (by aesop_cat)

/-- From a natural transformation of functors `C ‚•§ D`, the induced natural transformation
of functors `WithInitial C ‚•§ WithInitial D`. -/
@[simps]
def map‚ÇÇ {D : Type*} [Category D] {F G : C ‚•§ D} (Œ∑ : F ‚ü∂ G) : map F ‚ü∂ map G where
  app := fun X => match X with
    | of x => Œ∑.app x
    | star => ùüô star
  naturality := by
    intro X Y f
    match X, Y, f with
    | of x, of y, f => exact Œ∑.naturality f
    | star, of x, _ => rfl
    | star, star, _ => rfl

/-- The prelax functor from `Cat` to `Cat` defined with `WithInitial`. -/
@[simps]
def prelaxfunctor : PrelaxFunctor Cat Cat where
  obj C := Cat.of (WithInitial C)
  map := map
  map‚ÇÇ := map‚ÇÇ
  map‚ÇÇ_id := by
    intros
    apply NatTrans.ext
    funext X
    cases X <;> rfl
  map‚ÇÇ_comp := by
    intros
    apply NatTrans.ext
    funext X
    cases X <;> rfl

/-- The pseudofunctor from `Cat` to `Cat` defined with `WithInitial`. -/
@[simps]
def pseudofunctor : Pseudofunctor Cat Cat where
  toPrelaxFunctor := prelaxfunctor
  mapId C := mapId C
  mapComp := mapComp
  map‚ÇÇ_whisker_left := by
    intros
    apply NatTrans.ext
    funext X
    cases X
    ¬∑ rw [NatTrans.comp_app, NatTrans.comp_app]
      simp only [prelaxfunctor_toPrelaxFunctorStruct_toPrefunctor_obj,
        prelaxfunctor_toPrelaxFunctorStruct_toPrefunctor_map, map_obj, Cat.comp_obj,
        prelaxfunctor_toPrelaxFunctorStruct_map‚ÇÇ, map‚ÇÇ_app, Cat.whiskerLeft_app, mapComp_hom_app,
        Iso.refl_hom, mapComp_inv_app, Iso.refl_inv, Category.comp_id, Category.id_comp]
    ¬∑ rfl
  map‚ÇÇ_whisker_right := by
    intros
    apply NatTrans.ext
    funext X
    cases X
    ¬∑ rw [NatTrans.comp_app, NatTrans.comp_app]
      simp only [prelaxfunctor_toPrelaxFunctorStruct_toPrefunctor_obj,
        prelaxfunctor_toPrelaxFunctorStruct_toPrefunctor_map, map_obj, Cat.comp_obj,
        prelaxfunctor_toPrelaxFunctorStruct_map‚ÇÇ, map‚ÇÇ_app, Cat.whiskerRight_app, mapComp_hom_app,
        Iso.refl_hom, map_map, mapComp_inv_app, Iso.refl_inv, Category.comp_id, Category.id_comp]
      rfl
    ¬∑ rfl
  map‚ÇÇ_associator := by
    intros
    apply NatTrans.ext
    funext X
    cases X
    ¬∑ rw [NatTrans.comp_app,NatTrans.comp_app,NatTrans.comp_app,NatTrans.comp_app]
      simp only [prelaxfunctor_toPrelaxFunctorStruct_toPrefunctor_obj,
        prelaxfunctor_toPrelaxFunctorStruct_toPrefunctor_map, map_obj, Cat.comp_obj,
        Bicategory.Strict.associator_eqToIso, eqToIso_refl, Iso.refl_hom,
        prelaxfunctor_toPrelaxFunctorStruct_map‚ÇÇ, map‚ÇÇ_app, mapComp_hom_app, Cat.whiskerRight_app,
        map_map, down_id, Functor.map_id, Cat.whiskerLeft_app, mapComp_inv_app, Iso.refl_inv,
        Category.comp_id, Category.id_comp]
      rw [NatTrans.id_app, NatTrans.id_app]
      simp only [Cat.comp_obj, map_obj, Category.comp_id]
    ¬∑ rfl
  map‚ÇÇ_left_unitor := by
    intros
    apply NatTrans.ext
    funext X
    cases X
    ¬∑ rw [NatTrans.comp_app, NatTrans.comp_app]
      simp only [prelaxfunctor_toPrelaxFunctorStruct_toPrefunctor_obj,
        prelaxfunctor_toPrelaxFunctorStruct_toPrefunctor_map, map_obj, Cat.comp_obj,
        Bicategory.Strict.leftUnitor_eqToIso, eqToIso_refl, Iso.refl_hom,
        prelaxfunctor_toPrelaxFunctorStruct_map‚ÇÇ, map‚ÇÇ_app, mapComp_hom_app, Cat.whiskerRight_app,
        mapId_hom_app, map_map, Category.id_comp]
      rw [NatTrans.id_app, NatTrans.id_app]
      simp only [Cat.comp_obj, map_obj, Category.comp_id]
      rw [‚Üê Functor.map_id]
      rfl
    ¬∑ rfl
  map‚ÇÇ_right_unitor := by
    intros
    apply NatTrans.ext
    funext X
    cases X
    ¬∑ rw [NatTrans.comp_app, NatTrans.comp_app]
      simp only [prelaxfunctor_toPrelaxFunctorStruct_toPrefunctor_obj,
        prelaxfunctor_toPrelaxFunctorStruct_toPrefunctor_map, map_obj, Cat.comp_obj,
        Bicategory.Strict.rightUnitor_eqToIso, eqToIso_refl, Iso.refl_hom,
        prelaxfunctor_toPrelaxFunctorStruct_map‚ÇÇ, map‚ÇÇ_app, mapComp_hom_app, Cat.whiskerLeft_app,
        mapId_hom_app, Category.id_comp]
      rw [NatTrans.id_app, NatTrans.id_app]
      simp only [Cat.comp_obj, map_obj, Category.comp_id]
      rw [‚Üê Functor.map_id, Cat.id_map]
      rfl
    ¬∑ rfl

instance {X : WithInitial C} : Unique (star ‚ü∂ X) where
  default :=
    match X with
    | of _x => PUnit.unit
    | star => PUnit.unit
  uniq := by aesop_cat

/-- `WithInitial.star` is initial. -/
def starInitial : Limits.IsInitial (star : WithInitial C) :=
  Limits.IsInitial.ofUnique _

instance : Limits.HasInitial (WithInitial C) := Limits.hasInitial_of_unique star

/-- The isomorphism between star and an abstract initial object of `WithInitial C` -/
@[simps!]
noncomputable def starIsoInitial : star ‚âÖ ‚ä•_ (WithInitial C) :=
  starInitial.uniqueUpToIso (Limits.initialIsInitial)

/-- Lift a functor `F : C ‚•§ D` to `WithInitial C ‚•§ D`. -/
@[simps]
def lift {D : Type*} [Category D] {Z : D} (F : C ‚•§ D) (M : ‚àÄ x : C, Z ‚ü∂ F.obj x)
    (hM : ‚àÄ (x y : C) (f : x ‚ü∂ y), M x ‚â´ F.map f = M y) : WithInitial C ‚•§ D where
  obj X :=
    match X with
    | of x => F.obj x
    | star => Z
  map {X Y} f :=
    match X, Y, f with
    | of _, of _, f => F.map (down f)
    | star, of _, _ => M _
    | star, star, _ => ùüô _

/-- The isomorphism between `incl ‚ãô lift F _ _` with `F`. -/
@[simps!]
def inclLift {D : Type*} [Category D] {Z : D} (F : C ‚•§ D) (M : ‚àÄ x : C, Z ‚ü∂ F.obj x)
    (hM : ‚àÄ (x y : C) (f : x ‚ü∂ y), M x ‚â´ F.map f = M y) : incl ‚ãô lift F M hM ‚âÖ F where
  hom := { app := fun _ => ùüô _ }
  inv := { app := fun _ => ùüô _ }

/-- The isomorphism between `(lift F _ _).obj WithInitial.star` with `Z`. -/
@[simps!]
def liftStar {D : Type*} [Category D] {Z : D} (F : C ‚•§ D) (M : ‚àÄ x : C, Z ‚ü∂ F.obj x)
    (hM : ‚àÄ (x y : C) (f : x ‚ü∂ y), M x ‚â´ F.map f = M y) : (lift F M hM).obj star ‚âÖ Z :=
  eqToIso rfl

theorem liftStar_lift_map {D : Type*} [Category D] {Z : D} (F : C ‚•§ D) (M : ‚àÄ x : C, Z ‚ü∂ F.obj x)
    (hM : ‚àÄ (x y : C) (f : x ‚ü∂ y), M x ‚â´ F.map f = M y) (x : C) :
    (liftStar F M hM).hom ‚â´ (lift F M hM).map (starInitial.to (incl.obj x)) =
      M x ‚â´ (inclLift F M hM).hom.app x := by
  erw [Category.id_comp, Category.comp_id]
  rfl

/-- The uniqueness of `lift`. -/
@[simp]
def liftUnique {D : Type*} [Category D] {Z : D} (F : C ‚•§ D) (M : ‚àÄ x : C, Z ‚ü∂ F.obj x)
    (hM : ‚àÄ (x y : C) (f : x ‚ü∂ y), M x ‚â´ F.map f = M y)
    (G : WithInitial C ‚•§ D) (h : incl ‚ãô G ‚âÖ F)
    (hG : G.obj star ‚âÖ Z)
    (hh : ‚àÄ x : C, hG.symm.hom ‚â´ G.map (starInitial.to (incl.obj x)) = M x ‚â´ h.symm.hom.app x) :
    G ‚âÖ lift F M hM :=
  NatIso.ofComponents
    (fun X =>
      match X with
      | of x => h.app x
      | star => hG)
    (by
      rintro (X | X) (Y | Y) f
      ¬∑ apply h.hom.naturality
      ¬∑ cases f
      ¬∑ cases f
        change G.map _ ‚â´ h.hom.app _ = hG.hom ‚â´ _
        symm
        erw [‚Üê Iso.eq_inv_comp, ‚Üê Category.assoc, hh]
        simp
      ¬∑ cases f
        change G.map (ùüô _) ‚â´ hG.hom = hG.hom ‚â´ ùüô _
        simp)

/-- A variant of `lift` with `Z` an initial object. -/
@[simps!]
def liftToInitial {D : Type*} [Category D] {Z : D} (F : C ‚•§ D) (hZ : Limits.IsInitial Z) :
    WithInitial C ‚•§ D :=
  lift F (fun _x => hZ.to _) fun _x _y _f => hZ.hom_ext _ _

/-- A variant of `incl_lift` with `Z` an initial object. -/
@[simps!]
def inclLiftToInitial {D : Type*} [Category D] {Z : D} (F : C ‚•§ D) (hZ : Limits.IsInitial Z) :
    incl ‚ãô liftToInitial F hZ ‚âÖ F :=
  inclLift _ _ _

/-- A variant of `lift_unique` with `Z` an initial object. -/
@[simps!]
def liftToInitialUnique {D : Type*} [Category D] {Z : D} (F : C ‚•§ D) (hZ : Limits.IsInitial Z)
    (G : WithInitial C ‚•§ D) (h : incl ‚ãô G ‚âÖ F) (hG : G.obj star ‚âÖ Z) : G ‚âÖ liftToInitial F hZ :=
  liftUnique F (fun _z => hZ.to _) (fun _x _y _f => hZ.hom_ext _ _) G h hG fun _x => hZ.hom_ext _ _

/-- Constructs a morphism from `star` to `of X`. -/
@[simp]
def homTo (X : C) : star ‚ü∂ incl.obj X :=
  starInitial.to _

instance isIso_of_to_star {X : WithInitial C} (f : X ‚ü∂ star) : IsIso f :=
  match X with
  | of _ => f.elim
  | star => ‚ü®f, rfl, rfl‚ü©

section

variable {D : Type*} [Category D]

/-- A functor `WithInitial C ‚•§ D` can be seen as an element of the comma category
`Comma (const C) (ùü≠ (C ‚•§ D))`. -/
@[simps!]
def mkCommaObject (F : WithInitial C ‚•§ D) : Comma (Functor.const C) (ùü≠ (C ‚•§ D)) where
  left := F.obj .star
  right := (incl ‚ãô F)
  hom :=
    { app x := F.map (starInitial.to (.of x))
      naturality x y f := by
        dsimp
        rw [Category.id_comp, ‚Üê F.map_comp]
        congr 1}

/-- A morphism of functors `WithInitial C ‚•§ D` gives a morphism between the associated comma
objects. -/
@[simps!]
def mkCommaMorphism {F G : WithInitial C ‚•§ D} (Œ∑ : F ‚ü∂ G) : mkCommaObject F ‚ü∂ mkCommaObject G where
  left := Œ∑.app .star
  right := whiskerLeft incl Œ∑

/-- An element of the comma category `Comma (Functor.const C) (ùü≠ (C ‚•§ D))` can be seen as a
functor `WithInitial C ‚•§ D`. -/
@[simps!]
def ofCommaObject (c : Comma (Functor.const C) (ùü≠ (C ‚•§ D))) : WithInitial C ‚•§ D :=
  lift (Z := c.left) c.right (fun x ‚Ü¶ c.hom.app x)
    (fun x y f ‚Ü¶ by simpa using (c.hom.naturality f).symm)

/-- A morphism in `Comma (Functor.const C) (ùü≠ (C ‚•§ D))` gives a morphism between the associated
functors `WithInitial C ‚•§ D`. -/
@[simps!]
def ofCommaMorphism {c c' : Comma (Functor.const C) (ùü≠ (C ‚•§ D))} (œÜ : c ‚ü∂ c') :
    ofCommaObject c ‚ü∂ ofCommaObject c' where
  app x :=
    match x with
    | of x => œÜ.right.app x
    | star => œÜ.left
  naturality x y f :=
    match x, y, f with
    | of _, of _, f => by simp
    | star, of a, _ => by simpa [-CommaMorphism.w] using (congrArg (fun f ‚Ü¶ f.app a) œÜ.w).symm
    | star, star, _ => by simp

/-- The category of functors `WithInitial C ‚•§ D` is equivalent to the category
`Comma (const C) (ùü≠ (C ‚•§ D))`. -/
@[simps!]
def equivComma : (WithInitial C ‚•§ D) ‚âå Comma (Functor.const C) (ùü≠ (C ‚•§ D)) where
  functor :=
    { obj := mkCommaObject
      map := mkCommaMorphism }
  inverse :=
    { obj := ofCommaObject
      map := ofCommaMorphism }
  unitIso :=
    NatIso.ofComponents
      (fun F ‚Ü¶ liftUnique
        (incl ‚ãô F)
        (fun x ‚Ü¶ F.map (starInitial.to (of x)))
        (fun x y f ‚Ü¶ by
          simp only [Functor.comp_obj, Functor.comp_map]
          rw [‚Üê F.map_comp]
          congr 1)
        F (Iso.refl _) (Iso.refl _)
        (fun x ‚Ü¶ by
          simp only [Iso.refl_symm, Iso.refl_hom, Category.id_comp, Functor.comp_obj,
            NatTrans.id_app, Category.comp_id]; rfl))
      (fun {x y} f ‚Ü¶ by ext t; cases t <;> simp [incl])
  counitIso := NatIso.ofComponents (fun F ‚Ü¶ Iso.refl _)
  functor_unitIso_comp x := by
    simp only [id_eq, Functor.id_obj, ofCommaObject_obj, ofCommaMorphism_app, Comma.id_right,
      NatTrans.id_app, Comma.id_left, Comma.comp_right, NatTrans.comp_app, Comma.comp_left,
      Functor.comp_obj, liftUnique, Functor.comp_map, eq_mpr_eq_cast, lift_obj,
      NatIso.ofComponents_hom_app, Iso.refl_hom, Category.comp_id]
    ext <;> rfl

end

end WithInitial

open Opposite in
/-- The opposite category of `WithTerminal C` is equivalent to `WithInitial C·µí·µñ`. -/
@[simps!]
def WithTerminal.opEquiv : (WithTerminal C)·µí·µñ ‚âå WithInitial C·µí·µñ where
  functor :=
    { obj := fun ‚ü®x‚ü© ‚Ü¶ match x with
      | of x => .of <| op x
      | star => .star
      map := fun {x y} ‚ü®f‚ü© ‚Ü¶
        match x, y, f with
        | op (of x), op (of y), f => (WithTerminal.down f).op
        | op star, op (of _), _ => WithInitial.starInitial.to _
        | op star, op star, _  => ùüô _
      map_id := fun ‚ü®x‚ü© ‚Ü¶ by cases x <;> rfl
      map_comp := fun {x y z} ‚ü®f‚ü© ‚ü®g‚ü© ‚Ü¶
        match x, y, z, f, g with
        | op (of x), op (of y), op (of z), f, g => rfl
        | _, op (of y), op star, f, g => (g : PEmpty).elim
        | op (of x), op star, _, f, _ => (f : PEmpty).elim
        | op star, _, _, f, g => rfl }
  inverse :=
    { obj := fun x ‚Ü¶
      match x with
        | .of x => op <| .of <| x.unop
        | .star => op .star
      map := fun {x y} f ‚Ü¶
        match x, y, f with
        | .of (op x), .of (op y), f => WithInitial.down f
        | .star, .of (op _), _ => op <| WithTerminal.starTerminal.from _
        | .star, .star, _  => ùüô _
      map_id := fun x ‚Ü¶ by cases x <;> rfl
      map_comp := fun {x y z} f g ‚Ü¶
        match x, y, z, f, g with
        | .of (op x), .of (op y), .of (op z), f, g => rfl
        | _, .of (op y), .star, f, g => (g : PEmpty).elim
        | .of (op x), .star, _, f, _ => (f : PEmpty).elim
        | .star, _, _, f, g => by subsingleton }
  unitIso :=
    NatIso.ofComponents
      (fun ‚ü®x‚ü© ‚Ü¶ match x with
        | .of x => Iso.refl _
        | .star => Iso.refl _)
      (fun {x y} ‚ü®f‚ü© ‚Ü¶ match x, y, f with
        | op (of x), op (of y), f => by
            simp only [Functor.id_obj, op_unop, Functor.comp_obj,
              Functor.id_map, Iso.refl_hom, Category.comp_id, Functor.comp_map, Category.id_comp]
            rfl
        | op star, op (of _), _ => rfl
        | op star, op star, _  => rfl)
  counitIso :=
    NatIso.ofComponents
      (fun x ‚Ü¶ match x with
        | .of x => Iso.refl _
        | .star => Iso.refl _)
  functor_unitIso_comp := fun ‚ü®x‚ü© ‚Ü¶
    match x with
    | .of x => by
        simp only [op_unop, Functor.id_obj, Functor.comp_obj, NatIso.ofComponents_hom_app,
          Iso.refl_hom, Category.comp_id]
        rfl
    | .star => rfl

open Opposite in
/-- The opposite category of `WithInitial C` is equivalent to `WithTerminal C·µí·µñ`. -/
@[simps!]
def WithInitial.opEquiv : (WithInitial C)·µí·µñ ‚âå WithTerminal C·µí·µñ where
  functor :=
    { obj := fun ‚ü®x‚ü© ‚Ü¶
        match x with
        | of x => .of <| op x
        | star => .star
      map := fun {x y} ‚ü®f‚ü© ‚Ü¶
        match x, y, f with
        | op (of x), op (of y), f => (WithTerminal.down f).op
        | op (of _), op star, _ => WithTerminal.starTerminal.from _
        | op star, op star, _  => ùüô _
      map_id := fun ‚ü®x‚ü© ‚Ü¶ by cases x <;> rfl
      map_comp := fun {x y z} ‚ü®f‚ü© ‚ü®g‚ü© ‚Ü¶
        match x, y, z, f, g with
        | op (of x), op (of y), op (of z), f, g => rfl
        | _, op star, op (of y), f, g => (g : PEmpty).elim
        | op star, op (of x), _, f, _ => (f : PEmpty).elim
        | _, _, op star, f, g => by subsingleton }
  inverse :=
    { obj := fun x ‚Ü¶
        match x with
        | .of x => op <| .of <| x.unop
        | .star => op .star
      map := fun {x y} f ‚Ü¶
        match x, y, f with
        | .of (op x), .of (op y), f => WithInitial.down f
        | .of (op _), .star, _ => op <| WithInitial.starInitial.to _
        | .star, .star, _  => ùüô _
      map_id := fun x ‚Ü¶ by cases x <;> rfl
      map_comp := fun {x y z} f g ‚Ü¶
        match x, y, z, f, g with
        | .of (op x), .of (op y), .of (op z), f, g => rfl
        | _, .star, .of (op y), f, g => (g : PEmpty).elim
        | .star, .of (op x), _, f, _ => (f : PEmpty).elim
        | _, _, .star, f, g => by rfl }
  unitIso :=
    NatIso.ofComponents
      (fun ‚ü®x‚ü© ‚Ü¶ match x with
        | .of x => Iso.refl _
        | .star => Iso.refl _)
      (fun {x y} f ‚Ü¶ match x, y, f with
        | op (of x), op (of y), f => by
            simp only [Functor.id_obj, op_unop, Functor.comp_obj,
              Functor.id_map, Iso.refl_hom, Category.comp_id, Functor.comp_map, Category.id_comp]
            rfl
        | op (of _), op star, _ => rfl
        | _, op star, _ => rfl)
  counitIso :=
    NatIso.ofComponents
      (fun x ‚Ü¶ match x with
        | .of x => Iso.refl _
        | .star => Iso.refl _)
  functor_unitIso_comp := fun ‚ü®x‚ü© ‚Ü¶
    match x with
    | .of x => by
        simp only [op_unop, Functor.id_obj, Functor.comp_obj, NatIso.ofComponents_hom_app,
          Iso.refl_hom, Category.comp_id]
        rfl
    | .star => rfl

end CategoryTheory
