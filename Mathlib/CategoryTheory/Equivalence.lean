/-
Copyright (c) 2017 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Tim Baumann, Stephen Morgan, Kim Morrison, Floris van Doorn
-/
import Mathlib.CategoryTheory.Functor.FullyFaithful
import Mathlib.CategoryTheory.ObjectProperty.FullSubcategory
import Mathlib.CategoryTheory.Whiskering
import Mathlib.CategoryTheory.EssentialImage
import Mathlib.Tactic.CategoryTheory.Slice
/-!
# Equivalence of categories

An equivalence of categories `C` and `D` is a pair of functors `F : C â¥¤ D` and `G : D â¥¤ C` such
that `Î· : ğŸ­ C â‰… F â‹™ G` and `Îµ : G â‹™ F â‰… ğŸ­ D`. In many situations, equivalences are a better
notion of "sameness" of categories than the stricter isomorphism of categories.

Recall that one way to express that two functors `F : C â¥¤ D` and `G : D â¥¤ C` are adjoint is using
two natural transformations `Î· : ğŸ­ C âŸ¶ F â‹™ G` and `Îµ : G â‹™ F âŸ¶ ğŸ­ D`, called the unit and the
counit, such that the compositions `F âŸ¶ FGF âŸ¶ F` and `G âŸ¶ GFG âŸ¶ G` are the identity. Unfortunately,
it is not the case that the natural isomorphisms `Î·` and `Îµ` in the definition of an equivalence
automatically give an adjunction. However, it is true that
* if one of the two compositions is the identity, then so is the other, and
* given an equivalence of categories, it is always possible to refine `Î·` in such a way that the
  identities are satisfied.

For this reason, in mathlib we define an equivalence to be a "half-adjoint equivalence", which is
a tuple `(F, G, Î·, Îµ)` as in the first paragraph such that the composite `F âŸ¶ FGF âŸ¶ F` is the
identity. By the remark above, this already implies that the tuple is an "adjoint equivalence",
i.e., that the composite `G âŸ¶ GFG âŸ¶ G` is also the identity.

We also define essentially surjective functors and show that a functor is an equivalence if and only
if it is full, faithful and essentially surjective.

## Main definitions

* `Equivalence`: bundled (half-)adjoint equivalences of categories
* `Functor.EssSurj`: type class on a functor `F` containing the data of the preimages
  and the isomorphisms `F.obj (preimage d) â‰… d`.
* `Functor.IsEquivalence`: type class on a functor `F` which is full, faithful and
  essentially surjective.

## Main results

* `Equivalence.mk`: upgrade an equivalence to a (half-)adjoint equivalence
* `isEquivalence_iff_of_iso`: when `F` and `G` are isomorphic functors,
  `F` is an equivalence iff `G` is.
* `Functor.asEquivalenceFunctor`: construction of an equivalence of categories from
  a functor `F` which satisfies the property `F.IsEquivalence` (i.e. `F` is full, faithful
  and essentially surjective).

## Notation

We write `C â‰Œ D` (`\backcong`, not to be confused with `â‰…`/`\cong`) for a bundled equivalence.

-/

namespace CategoryTheory

open CategoryTheory.Functor NatIso Category

-- declare the `v`'s first; see `CategoryTheory.Category` for an explanation
universe vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

/-- An equivalence of categories.

We define an equivalence between `C` and `D`, with notation `C â‰Œ D`, as a half-adjoint equivalence:
a pair of functors `F : C â¥¤ D` and `G : D â¥¤ C` with a unit `Î· : ğŸ­ C â‰… F â‹™ G` and counit
`Îµ : G â‹™ F â‰… ğŸ­ D`, such that the natural isomorphisms `Î·` and `Îµ` satisfy the triangle law for
`F`: namely, `FÎ· â‰« ÎµF = ğŸ™ F`. Or, in other words, the composite `F` âŸ¶ `F â‹™ G â‹™ F` âŸ¶ `F` is the
identity.

In `unit_inverse_comp`, we show that this is sufficient to establish a full adjoint
equivalence. I.e., the composite `G` âŸ¶ `G â‹™ F â‹™ G` âŸ¶ `G` is also the identity.

The triangle equation `functor_unitIso_comp` is written as a family of equalities between
morphisms. It is more complicated if we write it as an equality of natural transformations, because
then we would either have to insert natural transformations like `F âŸ¶ FğŸ­` or abuse defeq. -/
@[ext, stacks 001J]
structure Equivalence (C : Type uâ‚) (D : Type uâ‚‚) [Category.{vâ‚} C] [Category.{vâ‚‚} D] where mk' ::
  /-- The forwards direction of an equivalence. -/
  functor : C â¥¤ D
  /-- The backwards direction of an equivalence. -/
  inverse : D â¥¤ C
  /-- The composition `functor â‹™ inverse` is isomorphic to the identity. -/
  unitIso : ğŸ­ C â‰… functor â‹™ inverse
  /-- The composition `inverse â‹™ functor` is isomorphic to the identity. -/
  counitIso : inverse â‹™ functor â‰… ğŸ­ D
  /-- The triangle law for the forwards direction of an equivalence: the unit and counit compose
  to the identity when whiskered along the forwards direction.

  We state this as a family of equalities among morphisms instead of an equality of natural
  transformations to avoid abusing defeq or inserting natural transformations like `F âŸ¶ FğŸ­`. -/
  functor_unitIso_comp :
    âˆ€ X : C, functor.map (unitIso.hom.app X) â‰« counitIso.hom.app (functor.obj X) =
      ğŸ™ (functor.obj X) := by cat_disch

@[inherit_doc Equivalence]
infixr:10 " â‰Œ " => Equivalence

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D]
         {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E]

namespace Equivalence

/-- The unit of an equivalence of categories. -/
abbrev unit (e : C â‰Œ D) : ğŸ­ C âŸ¶ e.functor â‹™ e.inverse :=
  e.unitIso.hom

/-- The counit of an equivalence of categories. -/
abbrev counit (e : C â‰Œ D) : e.inverse â‹™ e.functor âŸ¶ ğŸ­ D :=
  e.counitIso.hom

/-- The inverse of the unit of an equivalence of categories. -/
abbrev unitInv (e : C â‰Œ D) : e.functor â‹™ e.inverse âŸ¶ ğŸ­ C :=
  e.unitIso.inv

/-- The inverse of the counit of an equivalence of categories. -/
abbrev counitInv (e : C â‰Œ D) : ğŸ­ D âŸ¶ e.inverse â‹™ e.functor :=
  e.counitIso.inv

section CategoryStructure

instance : Category (C â‰Œ D) where
  Hom e f := e.functor âŸ¶ f.functor
  id e := ğŸ™ e.functor
  comp {a b c} f g := (f â‰« g : a.functor âŸ¶ _)

/-- Promote a natural transformation `e.functor âŸ¶ f.functor` to a morphism in `C â‰Œ D`. -/
def mkHom {e f : C â‰Œ D} (Î· : e.functor âŸ¶ f.functor) : e âŸ¶ f := Î·

/-- Recover a natural transformation between `e.functor` and `f.functor` from the data of
a morphism `e âŸ¶ f`. -/
def asNatTrans {e f : C â‰Œ D} (Î· : e âŸ¶ f) : e.functor âŸ¶ f.functor := Î·

@[ext]
lemma hom_ext {e f : C â‰Œ D} {Î± Î² : e âŸ¶ f} (h : asNatTrans Î± = asNatTrans Î²) : Î± = Î² := h

@[simp]
lemma mkHom_asNatTrans {e f : C â‰Œ D} (Î· : e.functor âŸ¶ f.functor) :
    mkHom (asNatTrans Î·) = Î· :=
  rfl

@[simp]
lemma asNatTrans_mkHom {e f : C â‰Œ D} (Î· : e âŸ¶ f) :
    asNatTrans (mkHom Î·) = Î· :=
  rfl

@[simp]
lemma id_asNatTrans {e : C â‰Œ D} : asNatTrans (ğŸ™ e) = ğŸ™ _ := rfl

@[simp, reassoc]
lemma comp_asNatTrans {e f g : C â‰Œ D} (Î± : e âŸ¶ f) (Î² : f âŸ¶ g) :
    asNatTrans (Î± â‰« Î²) = asNatTrans Î± â‰« asNatTrans Î² :=
  rfl

@[simp]
lemma mkHom_id_functor {e : C â‰Œ D} : mkHom (ğŸ™ e.functor) = ğŸ™ e := rfl

@[simp, reassoc]
lemma mkHom_comp {e f g : C â‰Œ D} (Î± : e.functor âŸ¶ f.functor) (Î² : f.functor âŸ¶ g.functor) :
    mkHom (Î± â‰« Î²) = mkHom Î± â‰« mkHom Î² :=
  rfl

/-- Construct an isomorphism in `C â‰Œ D` from a natural isomorphism between the functors
of the equivalences. -/
@[simps]
def mkIso {e f : C â‰Œ D} (Î· : e.functor â‰… f.functor) : e â‰… f where
  hom := mkHom Î·.hom
  inv := mkHom Î·.inv

variable (C D) in
/-- The `functor` functor that sends an equivalence of categories to its functor. -/
@[simps!]
def functorFunctor : (C â‰Œ D) â¥¤ C â¥¤ D where
  obj f := f.functor
  map Î± := asNatTrans Î±

end CategoryStructure

/- While these abbreviations are convenient, they also cause some trouble,
preventing structure projections from unfolding. -/
@[simp]
theorem Equivalence_mk'_unit (functor inverse unit_iso counit_iso f) :
    (âŸ¨functor, inverse, unit_iso, counit_iso, fâŸ© : C â‰Œ D).unit = unit_iso.hom :=
  rfl

@[simp]
theorem Equivalence_mk'_counit (functor inverse unit_iso counit_iso f) :
    (âŸ¨functor, inverse, unit_iso, counit_iso, fâŸ© : C â‰Œ D).counit = counit_iso.hom :=
  rfl

@[simp]
theorem Equivalence_mk'_unitInv (functor inverse unit_iso counit_iso f) :
    (âŸ¨functor, inverse, unit_iso, counit_iso, fâŸ© : C â‰Œ D).unitInv = unit_iso.inv :=
  rfl

@[simp]
theorem Equivalence_mk'_counitInv (functor inverse unit_iso counit_iso f) :
    (âŸ¨functor, inverse, unit_iso, counit_iso, fâŸ© : C â‰Œ D).counitInv = counit_iso.inv :=
  rfl

@[reassoc]
theorem counit_naturality (e : C â‰Œ D) {X Y : D} (f : X âŸ¶ Y) :
    e.functor.map (e.inverse.map f) â‰« e.counit.app Y = e.counit.app X â‰« f :=
  e.counit.naturality f

@[reassoc]
theorem unit_naturality (e : C â‰Œ D) {X Y : C} (f : X âŸ¶ Y) :
    e.unit.app X â‰« e.inverse.map (e.functor.map f) = f â‰« e.unit.app Y :=
  (e.unit.naturality f).symm

@[reassoc]
theorem counitInv_naturality (e : C â‰Œ D) {X Y : D} (f : X âŸ¶ Y) :
    e.counitInv.app X â‰« e.functor.map (e.inverse.map f) = f â‰« e.counitInv.app Y :=
  (e.counitInv.naturality f).symm

@[reassoc]
theorem unitInv_naturality (e : C â‰Œ D) {X Y : C} (f : X âŸ¶ Y) :
    e.inverse.map (e.functor.map f) â‰« e.unitInv.app Y = e.unitInv.app X â‰« f :=
  e.unitInv.naturality f

@[reassoc (attr := simp)]
theorem functor_unit_comp (e : C â‰Œ D) (X : C) :
    e.functor.map (e.unit.app X) â‰« e.counit.app (e.functor.obj X) = ğŸ™ (e.functor.obj X) :=
  e.functor_unitIso_comp X

@[reassoc (attr := simp)]
theorem counitInv_functor_comp (e : C â‰Œ D) (X : C) :
    e.counitInv.app (e.functor.obj X) â‰« e.functor.map (e.unitInv.app X) = ğŸ™ (e.functor.obj X) := by
  simpa using Iso.inv_eq_inv
    (e.functor.mapIso (e.unitIso.app X) â‰ªâ‰« e.counitIso.app (e.functor.obj X)) (Iso.refl _)

theorem counitInv_app_functor (e : C â‰Œ D) (X : C) :
    e.counitInv.app (e.functor.obj X) = e.functor.map (e.unit.app X) := by
  symm
  simp only [id_obj, comp_obj, counitInv]
  rw [â† Iso.app_inv, â† Iso.comp_hom_eq_id (e.counitIso.app _), Iso.app_hom, functor_unit_comp]
  rfl

theorem counit_app_functor (e : C â‰Œ D) (X : C) :
    e.counit.app (e.functor.obj X) = e.functor.map (e.unitInv.app X) := by
  simpa using Iso.hom_comp_eq_id (e.functor.mapIso (e.unitIso.app X)) (f := e.counit.app _)

/-- The other triangle equality. The proof follows the following proof in Globular:
  http://globular.science/1905.001 -/
@[reassoc (attr := simp)]
theorem unit_inverse_comp (e : C â‰Œ D) (Y : D) :
    e.unit.app (e.inverse.obj Y) â‰« e.inverse.map (e.counit.app Y) = ğŸ™ (e.inverse.obj Y) := by
  rw [â† id_comp (e.inverse.map _), â† map_id e.inverse, â† counitInv_functor_comp, map_comp]
  dsimp
  rw [â† Iso.hom_inv_id_assoc (e.unitIso.app _) (e.inverse.map (e.functor.map _)), Iso.app_hom,
    Iso.app_inv]
  slice_lhs 2 3 => rw [â† e.unit_naturality]
  slice_lhs 1 2 => rw [â† e.unit_naturality]
  slice_lhs 4 4 =>
    rw [â† Iso.hom_inv_id_assoc (e.inverse.mapIso (e.counitIso.app _)) (e.unitInv.app _)]
  slice_lhs 3 4 =>
    dsimp only [Functor.mapIso_hom, Iso.app_hom]
    rw [â† map_comp e.inverse, e.counit_naturality, e.counitIso.hom_inv_id_app]
    dsimp only [Functor.comp_obj]
    rw [map_id]
  dsimp only [comp_obj, id_obj]
  rw [id_comp]
  slice_lhs 2 3 =>
    dsimp only [Functor.mapIso_inv, Iso.app_inv]
    rw [â† map_comp e.inverse, â† e.counitInv_naturality, map_comp]
  slice_lhs 3 4 => rw [e.unitInv_naturality]
  slice_lhs 4 5 =>
    rw [â† map_comp e.inverse, â† map_comp e.functor, e.unitIso.hom_inv_id_app]
    dsimp only [Functor.id_obj]
    rw [map_id, map_id]
  dsimp only [comp_obj, id_obj]
  rw [id_comp]
  slice_lhs 3 4 => rw [â† e.unitInv_naturality]
  slice_lhs 2 3 =>
    rw [â† map_comp e.inverse, e.counitInv_naturality, e.counitIso.hom_inv_id_app]
  dsimp only [Functor.comp_obj]
  simp

@[reassoc (attr := simp)]
theorem inverse_counitInv_comp (e : C â‰Œ D) (Y : D) :
    e.inverse.map (e.counitInv.app Y) â‰« e.unitInv.app (e.inverse.obj Y) = ğŸ™ (e.inverse.obj Y) := by
  simpa using Iso.inv_eq_inv
    (e.unitIso.app (e.inverse.obj Y) â‰ªâ‰« e.inverse.mapIso (e.counitIso.app Y)) (Iso.refl _)

theorem unit_app_inverse (e : C â‰Œ D) (Y : D) :
    e.unit.app (e.inverse.obj Y) = e.inverse.map (e.counitInv.app Y) := by
  simpa using Iso.comp_hom_eq_id (e.inverse.mapIso (e.counitIso.app Y)) (f := e.unit.app _)

theorem unitInv_app_inverse (e : C â‰Œ D) (Y : D) :
    e.unitInv.app (e.inverse.obj Y) = e.inverse.map (e.counit.app Y) := by
  rw [â† Iso.app_inv, â† Iso.app_hom, â† mapIso_hom, Eq.comm, â† Iso.hom_eq_inv]
  simpa using unit_app_inverse e Y

@[reassoc, simp]
theorem fun_inv_map (e : C â‰Œ D) (X Y : D) (f : X âŸ¶ Y) :
    e.functor.map (e.inverse.map f) = e.counit.app X â‰« f â‰« e.counitInv.app Y :=
  (NatIso.naturality_2 e.counitIso f).symm

@[reassoc, simp]
theorem inv_fun_map (e : C â‰Œ D) (X Y : C) (f : X âŸ¶ Y) :
    e.inverse.map (e.functor.map f) = e.unitInv.app X â‰« f â‰« e.unit.app Y :=
  (NatIso.naturality_1 e.unitIso f).symm

section

-- In this section we convert an arbitrary equivalence to a half-adjoint equivalence.
variable {F : C â¥¤ D} {G : D â¥¤ C} (Î· : ğŸ­ C â‰… F â‹™ G) (Îµ : G â‹™ F â‰… ğŸ­ D)

/-- If `Î· : ğŸ­ C â‰… F â‹™ G` is part of a (not necessarily half-adjoint) equivalence, we can upgrade it
to a refined natural isomorphism `adjointifyÎ· Î· : ğŸ­ C â‰… F â‹™ G` which exhibits the properties
required for a half-adjoint equivalence. See `Equivalence.mk`. -/
def adjointifyÎ· : ğŸ­ C â‰… F â‹™ G := by
  calc
    ğŸ­ C â‰… F â‹™ G := Î·
    _ â‰… F â‹™ ğŸ­ D â‹™ G := isoWhiskerLeft F (leftUnitor G).symm
    _ â‰… F â‹™ (G â‹™ F) â‹™ G := isoWhiskerLeft F (isoWhiskerRight Îµ.symm G)
    _ â‰… F â‹™ G â‹™ F â‹™ G := isoWhiskerLeft F (associator G F G)
    _ â‰… (F â‹™ G) â‹™ F â‹™ G := (associator F G (F â‹™ G)).symm
    _ â‰… ğŸ­ C â‹™ F â‹™ G := isoWhiskerRight Î·.symm (F â‹™ G)
    _ â‰… F â‹™ G := leftUnitor (F â‹™ G)

@[reassoc]
theorem adjointify_Î·_Îµ (X : C) :
    F.map ((adjointifyÎ· Î· Îµ).hom.app X) â‰« Îµ.hom.app (F.obj X) = ğŸ™ (F.obj X) := by
  dsimp [adjointifyÎ·, Trans.trans]
  simp only [comp_id, assoc, map_comp]
  have := Îµ.hom.naturality (F.map (Î·.inv.app X)); dsimp at this; rw [this]; clear this
  rw [â† assoc _ _ (F.map _)]
  have := Îµ.hom.naturality (Îµ.inv.app <| F.obj X); dsimp at this; rw [this]; clear this
  have := (Îµ.app <| F.obj X).hom_inv_id; dsimp at this; rw [this]; clear this
  rw [id_comp]; have := (F.mapIso <| Î·.app X).hom_inv_id; dsimp at this; rw [this]

end

/-- Every equivalence of categories consisting of functors `F` and `G` such that `F â‹™ G` and
    `G â‹™ F` are naturally isomorphic to identity functors can be transformed into a half-adjoint
    equivalence without changing `F` or `G`. -/
protected def mk (F : C â¥¤ D) (G : D â¥¤ C) (Î· : ğŸ­ C â‰… F â‹™ G) (Îµ : G â‹™ F â‰… ğŸ­ D) : C â‰Œ D :=
  âŸ¨F, G, adjointifyÎ· Î· Îµ, Îµ, adjointify_Î·_Îµ Î· ÎµâŸ©

/-- Equivalence of categories is reflexive. -/
@[refl, simps]
def refl : C â‰Œ C :=
  âŸ¨ğŸ­ C, ğŸ­ C, Iso.refl _, Iso.refl _, fun _ => Category.id_comp _âŸ©

instance : Inhabited (C â‰Œ C) :=
  âŸ¨reflâŸ©

/-- Equivalence of categories is symmetric. -/
@[symm, simps]
def symm (e : C â‰Œ D) : D â‰Œ C :=
  âŸ¨e.inverse, e.functor, e.counitIso.symm, e.unitIso.symm, e.inverse_counitInv_compâŸ©

@[simp]
lemma mkHom_id_inverse {e : C â‰Œ D} : mkHom (ğŸ™ e.inverse) = ğŸ™ e.symm := rfl

variable {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E]

/-- Equivalence of categories is transitive. -/
@[trans, simps]
def trans (e : C â‰Œ D) (f : D â‰Œ E) : C â‰Œ E where
  functor := e.functor â‹™ f.functor
  inverse := f.inverse â‹™ e.inverse
  unitIso := e.unitIso â‰ªâ‰« isoWhiskerRight (e.functor.rightUnitor.symm â‰ªâ‰«
    isoWhiskerLeft _ f.unitIso â‰ªâ‰« (Functor.associator _ _ _ ).symm) _ â‰ªâ‰« Functor.associator _ _ _
  counitIso := (Functor.associator _ _ _ ).symm â‰ªâ‰« isoWhiskerRight ((Functor.associator _ _ _ ) â‰ªâ‰«
      isoWhiskerLeft _ e.counitIso â‰ªâ‰« f.inverse.rightUnitor) _ â‰ªâ‰« f.counitIso
  -- We wouldn't have needed to give this proof if we'd used `Equivalence.mk`,
  -- but we choose to avoid using that here, for the sake of good structure projection `simp`
  -- lemmas.
  functor_unitIso_comp X := by
    dsimp
    simp only [comp_id, id_comp, map_comp, fun_inv_map, comp_obj, id_obj, counitInv,
      functor_unit_comp_assoc, assoc]
    slice_lhs 2 3 => rw [â† Functor.map_comp, Iso.inv_hom_id_app]
    simp

/-- Composing a functor with both functors of an equivalence yields a naturally isomorphic
functor. -/
def funInvIdAssoc (e : C â‰Œ D) (F : C â¥¤ E) : e.functor â‹™ e.inverse â‹™ F â‰… F :=
  (Functor.associator _ _ _).symm â‰ªâ‰« isoWhiskerRight e.unitIso.symm F â‰ªâ‰« F.leftUnitor

@[simp]
theorem funInvIdAssoc_hom_app (e : C â‰Œ D) (F : C â¥¤ E) (X : C) :
    (funInvIdAssoc e F).hom.app X = F.map (e.unitInv.app X) := by
  dsimp [funInvIdAssoc]
  simp

@[simp]
theorem funInvIdAssoc_inv_app (e : C â‰Œ D) (F : C â¥¤ E) (X : C) :
    (funInvIdAssoc e F).inv.app X = F.map (e.unit.app X) := by
  dsimp [funInvIdAssoc]
  simp

/-- Composing a functor with both functors of an equivalence yields a naturally isomorphic
functor. -/
def invFunIdAssoc (e : C â‰Œ D) (F : D â¥¤ E) : e.inverse â‹™ e.functor â‹™ F â‰… F :=
  (Functor.associator _ _ _).symm â‰ªâ‰« isoWhiskerRight e.counitIso F â‰ªâ‰« F.leftUnitor

@[simp]
theorem invFunIdAssoc_hom_app (e : C â‰Œ D) (F : D â¥¤ E) (X : D) :
    (invFunIdAssoc e F).hom.app X = F.map (e.counit.app X) := by
  dsimp [invFunIdAssoc]
  simp

@[simp]
theorem invFunIdAssoc_inv_app (e : C â‰Œ D) (F : D â¥¤ E) (X : D) :
    (invFunIdAssoc e F).inv.app X = F.map (e.counitInv.app X) := by
  dsimp [invFunIdAssoc]
  simp

/-- If `C` is equivalent to `D`, then `C â¥¤ E` is equivalent to `D â¥¤ E`. -/
@[simps! functor inverse unitIso counitIso]
def congrLeft (e : C â‰Œ D) : C â¥¤ E â‰Œ D â¥¤ E where
  functor := (whiskeringLeft _ _ _).obj e.inverse
  inverse := (whiskeringLeft _ _ _).obj e.functor
  unitIso := (NatIso.ofComponents fun F => (e.funInvIdAssoc F).symm)
  counitIso := (NatIso.ofComponents fun F => e.invFunIdAssoc F)
  functor_unitIso_comp F := by
    ext X
    dsimp
    simp only [funInvIdAssoc_inv_app, id_obj, comp_obj, invFunIdAssoc_hom_app,
      Functor.comp_map, â† F.map_comp, unit_inverse_comp, map_id]

/-- If `C` is equivalent to `D`, then `E â¥¤ C` is equivalent to `E â¥¤ D`. -/
@[simps! functor inverse unitIso counitIso]
def congrRight (e : C â‰Œ D) : E â¥¤ C â‰Œ E â¥¤ D where
  functor := (whiskeringRight _ _ _).obj e.functor
  inverse := (whiskeringRight _ _ _).obj e.inverse
  unitIso := NatIso.ofComponents
      fun F => F.rightUnitor.symm â‰ªâ‰« isoWhiskerLeft F e.unitIso â‰ªâ‰« Functor.associator _ _ _
  counitIso := NatIso.ofComponents
      fun F => Functor.associator _ _ _ â‰ªâ‰« isoWhiskerLeft F e.counitIso â‰ªâ‰« F.rightUnitor

variable (E) in
/-- Promoting `Equivalence.congrRight` to a functor. -/
@[simps]
def congrRightFunctor : (C â‰Œ D) â¥¤ ((E â¥¤ C) â‰Œ (E â¥¤ D)) where
  obj e := e.congrRight
  map {e f} Î± := mkHom <| (whiskeringRight _ _ _).map <| asNatTrans Î±

section CancellationLemmas

variable (e : C â‰Œ D)

/- We need special forms of `cancel_natIso_hom_right(_assoc)` and
`cancel_natIso_inv_right(_assoc)` for units and counits, because neither `simp` or `rw` will apply
those lemmas in this setting without providing `e.unitIso` (or similar) as an explicit argument.
We also provide the lemmas for length four compositions, since they're occasionally useful.
(e.g. in proving that equivalences take monos to monos) -/
@[simp]
theorem cancel_unit_right {X Y : C} (f f' : X âŸ¶ Y) :
    f â‰« e.unit.app Y = f' â‰« e.unit.app Y â†” f = f' := by simp only [cancel_mono]

@[simp]
theorem cancel_unitInv_right {X Y : C} (f f' : X âŸ¶ e.inverse.obj (e.functor.obj Y)) :
    f â‰« e.unitInv.app Y = f' â‰« e.unitInv.app Y â†” f = f' := by simp only [cancel_mono]

@[simp]
theorem cancel_counit_right {X Y : D} (f f' : X âŸ¶ e.functor.obj (e.inverse.obj Y)) :
    f â‰« e.counit.app Y = f' â‰« e.counit.app Y â†” f = f' := by simp only [cancel_mono]

@[simp]
theorem cancel_counitInv_right {X Y : D} (f f' : X âŸ¶ Y) :
    f â‰« e.counitInv.app Y = f' â‰« e.counitInv.app Y â†” f = f' := by simp only [cancel_mono]

@[simp]
theorem cancel_unit_right_assoc {W X X' Y : C} (f : W âŸ¶ X) (g : X âŸ¶ Y) (f' : W âŸ¶ X') (g' : X' âŸ¶ Y) :
    f â‰« g â‰« e.unit.app Y = f' â‰« g' â‰« e.unit.app Y â†” f â‰« g = f' â‰« g' := by
  simp only [â† Category.assoc, cancel_mono]

@[simp]
theorem cancel_counitInv_right_assoc {W X X' Y : D} (f : W âŸ¶ X) (g : X âŸ¶ Y) (f' : W âŸ¶ X')
    (g' : X' âŸ¶ Y) : f â‰« g â‰« e.counitInv.app Y = f' â‰« g' â‰« e.counitInv.app Y â†” f â‰« g = f' â‰« g' := by
  simp only [â† Category.assoc, cancel_mono]

@[simp]
theorem cancel_unit_right_assoc' {W X X' Y Y' Z : C} (f : W âŸ¶ X) (g : X âŸ¶ Y) (h : Y âŸ¶ Z)
    (f' : W âŸ¶ X') (g' : X' âŸ¶ Y') (h' : Y' âŸ¶ Z) :
    f â‰« g â‰« h â‰« e.unit.app Z = f' â‰« g' â‰« h' â‰« e.unit.app Z â†” f â‰« g â‰« h = f' â‰« g' â‰« h' := by
  simp only [â† Category.assoc, cancel_mono]

@[simp]
theorem cancel_counitInv_right_assoc' {W X X' Y Y' Z : D} (f : W âŸ¶ X) (g : X âŸ¶ Y) (h : Y âŸ¶ Z)
    (f' : W âŸ¶ X') (g' : X' âŸ¶ Y') (h' : Y' âŸ¶ Z) :
    f â‰« g â‰« h â‰« e.counitInv.app Z = f' â‰« g' â‰« h' â‰« e.counitInv.app Z â†”
    f â‰« g â‰« h = f' â‰« g' â‰« h' := by simp only [â† Category.assoc, cancel_mono]

end CancellationLemmas

section

-- There's of course a monoid structure on `C â‰Œ C`,
-- but let's not encourage using it.
-- The power structure is nevertheless useful.
/-- Natural number powers of an auto-equivalence.  Use `(^)` instead. -/
def powNat (e : C â‰Œ C) : â„• â†’ (C â‰Œ C)
  | 0 => Equivalence.refl
  | 1 => e
  | n + 2 => e.trans (powNat e (n + 1))

/-- Powers of an auto-equivalence.  Use `(^)` instead. -/
def pow (e : C â‰Œ C) : â„¤ â†’ (C â‰Œ C)
  | Int.ofNat n => e.powNat n
  | Int.negSucc n => e.symm.powNat (n + 1)

instance : Pow (C â‰Œ C) â„¤ :=
  âŸ¨powâŸ©

@[simp]
theorem pow_zero (e : C â‰Œ C) : e ^ (0 : â„¤) = Equivalence.refl :=
  rfl

@[simp]
theorem pow_one (e : C â‰Œ C) : e ^ (1 : â„¤) = e :=
  rfl

@[simp]
theorem pow_neg_one (e : C â‰Œ C) : e ^ (-1 : â„¤) = e.symm :=
  rfl

-- TODO as necessary, add the natural isomorphisms `(e^a).trans e^b â‰… e^(a+b)`.
-- At this point, we haven't even defined the category of equivalences.
-- Note: the better formulation of this would involve `HasShift`.
end

/-- The functor of an equivalence of categories is essentially surjective. -/
@[stacks 02C3]
instance essSurj_functor (e : C â‰Œ E) : e.functor.EssSurj :=
  âŸ¨fun Y => âŸ¨e.inverse.obj Y, âŸ¨e.counitIso.app YâŸ©âŸ©âŸ©

instance essSurj_inverse (e : C â‰Œ E) : e.inverse.EssSurj :=
  e.symm.essSurj_functor

/-- The functor of an equivalence of categories is fully faithful. -/
def fullyFaithfulFunctor (e : C â‰Œ E) : e.functor.FullyFaithful where
  preimage {X Y} f := e.unitIso.hom.app X â‰« e.inverse.map f â‰« e.unitIso.inv.app Y

/-- The inverse of an equivalence of categories is fully faithful. -/
def fullyFaithfulInverse (e : C â‰Œ E) : e.inverse.FullyFaithful where
  preimage {X Y} f := e.counitIso.inv.app X â‰« e.functor.map f â‰« e.counitIso.hom.app Y

/-- The functor of an equivalence of categories is faithful. -/
@[stacks 02C3]
instance faithful_functor (e : C â‰Œ E) : e.functor.Faithful :=
  e.fullyFaithfulFunctor.faithful

instance faithful_inverse (e : C â‰Œ E) : e.inverse.Faithful :=
  e.fullyFaithfulInverse.faithful

/-- The functor of an equivalence of categories is full. -/
@[stacks 02C3]
instance full_functor (e : C â‰Œ E) : e.functor.Full :=
  e.fullyFaithfulFunctor.full

instance full_inverse (e : C â‰Œ E) : e.inverse.Full :=
  e.fullyFaithfulInverse.full

/-- If `e : C â‰Œ D` is an equivalence of categories, and `iso : e.functor â‰… G` is
an isomorphism, then there is an equivalence of categories whose functor is `G`. -/
@[simps!]
def changeFunctor (e : C â‰Œ D) {G : C â¥¤ D} (iso : e.functor â‰… G) : C â‰Œ D where
  functor := G
  inverse := e.inverse
  unitIso := e.unitIso â‰ªâ‰« isoWhiskerRight iso _
  counitIso := isoWhiskerLeft _ iso.symm â‰ªâ‰« e.counitIso

/-- Compatibility of `changeFunctor` with identity isomorphisms of functors -/
theorem changeFunctor_refl (e : C â‰Œ D) : e.changeFunctor (Iso.refl _) = e := by cat_disch

/-- Compatibility of `changeFunctor` with the composition of isomorphisms of functors -/
theorem changeFunctor_trans (e : C â‰Œ D) {G G' : C â¥¤ D} (isoâ‚ : e.functor â‰… G) (isoâ‚‚ : G â‰… G') :
    (e.changeFunctor isoâ‚).changeFunctor isoâ‚‚ = e.changeFunctor (isoâ‚ â‰ªâ‰« isoâ‚‚) := by cat_disch

/-- If `e : C â‰Œ D` is an equivalence of categories, and `iso : e.functor â‰… G` is
an isomorphism, then there is an equivalence of categories whose inverse is `G`. -/
@[simps!]
def changeInverse (e : C â‰Œ D) {G : D â¥¤ C} (iso : e.inverse â‰… G) : C â‰Œ D where
  functor := e.functor
  inverse := G
  unitIso := e.unitIso â‰ªâ‰« isoWhiskerLeft _ iso
  counitIso := isoWhiskerRight iso.symm _ â‰ªâ‰« e.counitIso
  functor_unitIso_comp X := by
    dsimp
    rw [â† map_comp_assoc, assoc, iso.hom_inv_id_app, comp_id, functor_unit_comp]

end Equivalence

/-- A functor is an equivalence of categories if it is faithful, full and
essentially surjective. -/
class Functor.IsEquivalence (F : C â¥¤ D) : Prop where
  faithful : F.Faithful := by infer_instance
  full : F.Full := by infer_instance
  essSurj : F.EssSurj := by infer_instance

instance Equivalence.isEquivalence_functor (F : C â‰Œ D) : IsEquivalence F.functor where

instance Equivalence.isEquivalence_inverse (F : C â‰Œ D) : IsEquivalence F.inverse :=
  F.symm.isEquivalence_functor

namespace Functor

namespace IsEquivalence

attribute [instance] faithful full essSurj

/-- To see that a functor is an equivalence, it suffices to provide an inverse functor `G` such that
    `F â‹™ G` and `G â‹™ F` are naturally isomorphic to identity functors. -/
protected lemma mk' {F : C â¥¤ D} (G : D â¥¤ C) (Î· : ğŸ­ C â‰… F â‹™ G) (Îµ : G â‹™ F â‰… ğŸ­ D) :
    IsEquivalence F :=
  inferInstanceAs (IsEquivalence (Equivalence.mk F G Î· Îµ).functor)

end IsEquivalence

/-- A quasi-inverse `D â¥¤ C` to a functor that `F : C â¥¤ D` that is an equivalence,
i.e. faithful, full, and essentially surjective. -/
noncomputable def inv (F : C â¥¤ D) [F.IsEquivalence] : D â¥¤ C where
  obj X := F.objPreimage X
  map {X Y} f := F.preimage ((F.objObjPreimageIso X).hom â‰« f â‰« (F.objObjPreimageIso Y).inv)
  map_id X := by apply F.map_injective; simp
  map_comp {X Y Z} f g := by apply F.map_injective; simp

/-- Interpret a functor that is an equivalence as an equivalence. -/
@[simps functor, stacks 02C3]
noncomputable def asEquivalence (F : C â¥¤ D) [F.IsEquivalence] : C â‰Œ D where
  functor := F
  inverse := F.inv
  unitIso := NatIso.ofComponents
    (fun X => (F.preimageIso <| F.objObjPreimageIso <| F.obj X).symm)
      (fun f => F.map_injective (by simp [inv]))
  counitIso := NatIso.ofComponents F.objObjPreimageIso (by simp [inv])

instance isEquivalence_refl : IsEquivalence (ğŸ­ C) :=
  Equivalence.refl.isEquivalence_functor

instance isEquivalence_inv (F : C â¥¤ D) [IsEquivalence F] : IsEquivalence F.inv :=
  F.asEquivalence.symm.isEquivalence_functor

variable {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E]

instance isEquivalence_trans (F : C â¥¤ D) (G : D â¥¤ E) [IsEquivalence F] [IsEquivalence G] :
    IsEquivalence (F â‹™ G) where

instance (F : C â¥¤ D) [IsEquivalence F] : IsEquivalence ((whiskeringLeft C D E).obj F) :=
  (inferInstance : IsEquivalence (Equivalence.congrLeft F.asEquivalence).inverse)

instance (F : C â¥¤ D) [IsEquivalence F] : IsEquivalence ((whiskeringRight E C D).obj F) :=
  (inferInstance : IsEquivalence (Equivalence.congrRight F.asEquivalence).functor)

end Functor

namespace Functor


@[simp]
theorem fun_inv_map (F : C â¥¤ D) [IsEquivalence F] (X Y : D) (f : X âŸ¶ Y) :
    F.map (F.inv.map f) = F.asEquivalence.counit.app X â‰« f â‰« F.asEquivalence.counitInv.app Y := by
  simpa using (NatIso.naturality_2 (Î± := F.asEquivalence.counitIso) (f := f)).symm

@[simp]
theorem inv_fun_map (F : C â¥¤ D) [IsEquivalence F] (X Y : C) (f : X âŸ¶ Y) :
    F.inv.map (F.map f) = F.asEquivalence.unitInv.app X â‰« f â‰« F.asEquivalence.unit.app Y := by
  simpa using (NatIso.naturality_1 (Î± := F.asEquivalence.unitIso) (f := f)).symm

lemma isEquivalence_of_iso {F G : C â¥¤ D} (e : F â‰… G) [F.IsEquivalence] : G.IsEquivalence :=
  ((asEquivalence F).changeFunctor e).isEquivalence_functor

lemma isEquivalence_iff_of_iso {F G : C â¥¤ D} (e : F â‰… G) :
    F.IsEquivalence â†” G.IsEquivalence :=
  âŸ¨fun _ => isEquivalence_of_iso e, fun _ => isEquivalence_of_iso e.symmâŸ©

/-- If `G` and `F â‹™ G` are equivalence of categories, then `F` is also an equivalence. -/
lemma isEquivalence_of_comp_right {E : Type*} [Category E] (F : C â¥¤ D) (G : D â¥¤ E)
    [IsEquivalence G] [IsEquivalence (F â‹™ G)] : IsEquivalence F := by
  rw [isEquivalence_iff_of_iso (F.rightUnitor.symm â‰ªâ‰« isoWhiskerLeft F (G.asEquivalence.unitIso))]
  exact ((F â‹™ G).asEquivalence.trans G.asEquivalence.symm).isEquivalence_functor

/-- If `F` and `F â‹™ G` are equivalence of categories, then `G` is also an equivalence. -/
lemma isEquivalence_of_comp_left {E : Type*} [Category E] (F : C â¥¤ D) (G : D â¥¤ E)
    [IsEquivalence F] [IsEquivalence (F â‹™ G)] : IsEquivalence G := by
  rw [isEquivalence_iff_of_iso (G.leftUnitor.symm â‰ªâ‰«
    isoWhiskerRight F.asEquivalence.counitIso.symm G)]
  exact (F.asEquivalence.symm.trans (F â‹™ G).asEquivalence).isEquivalence_functor

end Functor

namespace Equivalence

instance essSurjInducedFunctor {C' : Type*} (e : C' â‰ƒ D) : (inducedFunctor e).EssSurj where
  mem_essImage Y := âŸ¨e.symm Y, by simpa using âŸ¨defaultâŸ©âŸ©

noncomputable instance inducedFunctorOfEquiv {C' : Type*} (e : C' â‰ƒ D) :
    IsEquivalence (inducedFunctor e) where

noncomputable instance fullyFaithfulToEssImage (F : C â¥¤ D) [F.Full] [F.Faithful] :
    IsEquivalence F.toEssImage where

end Equivalence

/-- An equality of properties of objects of a category `C` induces an equivalence of the
respective induced full subcategories of `C`. -/
@[simps]
def ObjectProperty.fullSubcategoryCongr {P P' : ObjectProperty C} (h : P = P') :
    P.FullSubcategory â‰Œ P'.FullSubcategory where
  functor := ObjectProperty.Î¹OfLE h.le
  inverse := ObjectProperty.Î¹OfLE h.symm.le
  unitIso := Iso.refl _
  counitIso := Iso.refl _

@[deprecated (since := "2025-03-04")]
alias Equivalence.ofFullSubcategory := ObjectProperty.fullSubcategoryCongr

/-- The essential image of a composition of functors is equivalent to the essential image of
the right functor restricted to the essential image of the left functor. -/
@[simps!]
def essImageInclusionComp (F : C â¥¤ D) (G : D â¥¤ E) :
    (F.essImage.Î¹ â‹™ G).EssImageSubcategory â‰Œ (F â‹™ G).EssImageSubcategory :=
  ObjectProperty.fullSubcategoryCongr <| by
    ext e
    constructor <;> intro h
    Â· use (Functor.essImage.witness h).2.choose
      exact âŸ¨G.mapIso (Functor.essImage.witness h).2.choose_spec.some |>.trans <|
          Functor.essImage.getIso hâŸ©
    Â· let out := (Functor.essImage.witness h)
      use âŸ¨F.obj (Functor.essImage.witness h), âŸ¨Functor.essImage.witness h, âŸ¨Iso.refl _âŸ©âŸ©âŸ©
      exact âŸ¨Functor.essImage.getIso hâŸ©

/-- The essential image of a composition with a fully faithful functor is equivalent to the
essential image of the left functor. -/
@[simps!]
noncomputable def Functor.EssImage.compEquivOfFullyFaithful
    (F : C â¥¤ D) (G : D â¥¤ E) [G.Full] [G.Faithful] :
    (F â‹™ G).EssImageSubcategory â‰Œ F.EssImageSubcategory :=
  letI inverse : F.EssImageSubcategory â¥¤ (F â‹™ G).EssImageSubcategory :=
    ObjectProperty.lift _ (F.essImage.Î¹ â‹™ G)
      (âŸ¨_, âŸ¨G.mapIso Â·.property.choose_spec.someâŸ©âŸ©)
  have : inverse.EssSurj := by
    constructor
    rintro âŸ¨Y, âŸ¨X, âŸ¨Î¹âŸ©âŸ©âŸ©
    exact âŸ¨âŸ¨F.obj X, âŸ¨X, âŸ¨Iso.refl _âŸ©âŸ©âŸ©, âŸ¨InducedCategory.isoMk Î¹âŸ©âŸ©
  have : inverse.IsEquivalence := âŸ¨inferInstance, inferInstance, â€¹_â€ºâŸ©
  inverse.asEquivalence.symm

-- end Equivalence

namespace Iso

variable {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E] {F : C â¥¤ E} {G : C â¥¤ D} {H : D â¥¤ E}

/-- Construct an isomorphism `F â‹™ H.inverse â‰… G` from an isomorphism `F â‰… G â‹™ H.functor`. -/
@[simps!]
def compInverseIso {H : D â‰Œ E} (i : F â‰… G â‹™ H.functor) : F â‹™ H.inverse â‰… G :=
  isoWhiskerRight i H.inverse â‰ªâ‰«
    associator G _ H.inverse â‰ªâ‰« isoWhiskerLeft G H.unitIso.symm â‰ªâ‰« G.rightUnitor

/-- Construct an isomorphism `G â‰… F â‹™ H.inverse` from an isomorphism `G â‹™ H.functor â‰… F`. -/
@[simps!]
def isoCompInverse {H : D â‰Œ E} (i : G â‹™ H.functor â‰… F) : G â‰… F â‹™ H.inverse :=
  G.rightUnitor.symm â‰ªâ‰« isoWhiskerLeft G H.unitIso â‰ªâ‰« (associator _ _ _).symm â‰ªâ‰«
    isoWhiskerRight i H.inverse

/-- Construct an isomorphism `G.inverse â‹™ F â‰… H` from an isomorphism `F â‰… G.functor â‹™ H`. -/
@[simps!]
def inverseCompIso {G : C â‰Œ D} (i : F â‰… G.functor â‹™ H) : G.inverse â‹™ F â‰… H :=
  isoWhiskerLeft G.inverse i â‰ªâ‰« (associator _ _ _).symm â‰ªâ‰«
    isoWhiskerRight G.counitIso H â‰ªâ‰« H.leftUnitor

/-- Construct an isomorphism `H â‰… G.inverse â‹™ F` from an isomorphism `G.functor â‹™ H â‰… F`. -/
@[simps!]
def isoInverseComp {G : C â‰Œ D} (i : G.functor â‹™ H â‰… F) : H â‰… G.inverse â‹™ F :=
  H.leftUnitor.symm â‰ªâ‰« isoWhiskerRight G.counitIso.symm H â‰ªâ‰« associator _ _ _
    â‰ªâ‰« isoWhiskerLeft G.inverse i

/-- As a special case, given two equivalences `G` and `G'` between the same categories,
construct an isomorphism `G.inverse â‰… G.inverse` from an isomorphism `G.functor â‰… G.functor`. -/
@[simps!]
def isoInverseOfIsoFunctor {G G' : C â‰Œ D} (i : G.functor â‰… G'.functor) : G.inverse â‰… G'.inverse :=
  isoCompInverse ((isoWhiskerLeft G.inverse i).symm â‰ªâ‰« G.counitIso) â‰ªâ‰« leftUnitor G'.inverse

/-- As a special case, given two equivalences `G` and `G'` between the same categories,
construct an isomorphism `G.functor â‰… G.functor` from an isomorphism `G.inverse â‰… G.inverse`. -/
@[simps!]
def isoFunctorOfIsoInverse {G G' : C â‰Œ D} (i : G.inverse â‰… G'.inverse) : G.functor â‰… G'.functor :=
  isoInverseOfIsoFunctor (G := G.symm) (G' := G'.symm) i

/-- Sanity check: `isoFunctorOfIsoInverse (isoInverseOfIsoFunctor i)` is just `i`. -/
@[simp]
lemma isoFunctorOfIsoInverse_isoInverseOfIsoFunctor {G G' : C â‰Œ D} (i : G.functor â‰… G'.functor) :
    isoFunctorOfIsoInverse (isoInverseOfIsoFunctor i) = i := by
  ext X
  simp [â† NatTrans.naturality]

@[simp]
lemma isoInverseOfIsoFunctor_isoFunctorOfIsoInverse {G G' : C â‰Œ D} (i : G.inverse â‰… G'.inverse) :
    isoInverseOfIsoFunctor (isoFunctorOfIsoInverse i) = i :=
  isoFunctorOfIsoInverse_isoInverseOfIsoFunctor (G := G.symm) (G' := G'.symm) i

end Iso

end CategoryTheory
