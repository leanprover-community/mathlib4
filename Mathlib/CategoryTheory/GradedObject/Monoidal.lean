/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou, Kim Morrison
-/
import Mathlib.CategoryTheory.GradedObject.Unitor

/-!
# The monoidal category structures on graded objects

Assuming that `C` is a monoidal category and that `I` is an additive monoid,
we introduce a partially defined tensor product on the category `GradedObject I C`:
given `Xâ‚` and `Xâ‚‚` two objects in `GradedObject I C`, we define
`GradedObject.Monoidal.tensorObj Xâ‚ Xâ‚‚` under the assumption `HasTensor Xâ‚ Xâ‚‚`
that the coproduct of `Xâ‚ i âŠ— Xâ‚‚ j` for `i + j = n` exists for any `n : I`.

-/

universe u

namespace CategoryTheory

open Limits MonoidalCategory Category

variable {I : Type u} [AddMonoid I] {C : Type*} [Category C] [MonoidalCategory C]

namespace GradedObject

/-- The tensor product of two graded objects `Xâ‚` and `Xâ‚‚` exists if for any `n`,
the coproduct of the objects `Xâ‚ i âŠ— Xâ‚‚ j` for `i + j = n` exists. -/
abbrev HasTensor (Xâ‚ Xâ‚‚ : GradedObject I C) : Prop :=
  HasMap (((mapBifunctor (curriedTensor C) I I).obj Xâ‚).obj Xâ‚‚) (fun âŸ¨i, jâŸ© => i + j)

namespace Monoidal

/-- The tensor product of two graded objects. -/
noncomputable abbrev tensorObj (Xâ‚ Xâ‚‚ : GradedObject I C) [HasTensor Xâ‚ Xâ‚‚] :
    GradedObject I C :=
  mapBifunctorMapObj (curriedTensor C) (fun âŸ¨i, jâŸ© => i + j) Xâ‚ Xâ‚‚

section

variable (Xâ‚ Xâ‚‚ : GradedObject I C) [HasTensor Xâ‚ Xâ‚‚]

/-- The inclusion of a summand in a tensor product of two graded objects. -/
noncomputable def Î¹TensorObj (iâ‚ iâ‚‚ iâ‚â‚‚ : I) (h : iâ‚ + iâ‚‚ = iâ‚â‚‚) :
  Xâ‚ iâ‚ âŠ— Xâ‚‚ iâ‚‚ âŸ¶ tensorObj Xâ‚ Xâ‚‚ iâ‚â‚‚ :=
    Î¹MapBifunctorMapObj (curriedTensor C) _ _ _ _ _ _ h

variable {Xâ‚ Xâ‚‚}

@[ext]
lemma tensorObj_ext {A : C} {j : I} (f g : tensorObj Xâ‚ Xâ‚‚ j âŸ¶ A)
    (h : âˆ€ (iâ‚ iâ‚‚ : I) (hi : iâ‚ + iâ‚‚ = j),
      Î¹TensorObj Xâ‚ Xâ‚‚ iâ‚ iâ‚‚ j hi â‰« f = Î¹TensorObj Xâ‚ Xâ‚‚ iâ‚ iâ‚‚ j hi â‰« g) : f = g := by
  apply mapObj_ext
  rintro âŸ¨iâ‚, iâ‚‚âŸ© hi
  exact h iâ‚ iâ‚‚ hi

/-- Constructor for morphisms from a tensor product of two graded objects. -/
noncomputable def tensorObjDesc {A : C} {k : I}
    (f : âˆ€ (iâ‚ iâ‚‚ : I) (_ : iâ‚ + iâ‚‚ = k), Xâ‚ iâ‚ âŠ— Xâ‚‚ iâ‚‚ âŸ¶ A) : tensorObj Xâ‚ Xâ‚‚ k âŸ¶ A :=
  mapBifunctorMapObjDesc f

@[reassoc (attr := simp)]
lemma Î¹_tensorObjDesc {A : C} {k : I}
    (f : âˆ€ (iâ‚ iâ‚‚ : I) (_ : iâ‚ + iâ‚‚ = k), Xâ‚ iâ‚ âŠ— Xâ‚‚ iâ‚‚ âŸ¶ A) (iâ‚ iâ‚‚ : I) (hi : iâ‚ + iâ‚‚ = k) :
    Î¹TensorObj Xâ‚ Xâ‚‚ iâ‚ iâ‚‚ k hi â‰« tensorObjDesc f = f iâ‚ iâ‚‚ hi := by
  apply Î¹_mapBifunctorMapObjDesc

end

/-- The morphism `tensorObj Xâ‚ Yâ‚ âŸ¶ tensorObj Xâ‚‚ Yâ‚‚` induced by morphisms of graded
objects `f : Xâ‚ âŸ¶ Xâ‚‚` and `g : Yâ‚ âŸ¶ Yâ‚‚`. -/
noncomputable def tensorHom {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : GradedObject I C} (f : Xâ‚ âŸ¶ Xâ‚‚) (g : Yâ‚ âŸ¶ Yâ‚‚)
    [HasTensor Xâ‚ Yâ‚] [HasTensor Xâ‚‚ Yâ‚‚] :
    tensorObj Xâ‚ Yâ‚ âŸ¶ tensorObj Xâ‚‚ Yâ‚‚ :=
  mapBifunctorMapMap _ _ f g

@[reassoc (attr := simp)]
lemma Î¹_tensorHom {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : GradedObject I C} (f : Xâ‚ âŸ¶ Xâ‚‚) (g : Yâ‚ âŸ¶ Yâ‚‚)
    [HasTensor Xâ‚ Yâ‚] [HasTensor Xâ‚‚ Yâ‚‚] (iâ‚ iâ‚‚ iâ‚â‚‚ : I) (h : iâ‚ + iâ‚‚ = iâ‚â‚‚) :
    Î¹TensorObj Xâ‚ Yâ‚ iâ‚ iâ‚‚ iâ‚â‚‚ h â‰« tensorHom f g iâ‚â‚‚ =
      (f iâ‚ âŠ— g iâ‚‚) â‰« Î¹TensorObj Xâ‚‚ Yâ‚‚ iâ‚ iâ‚‚ iâ‚â‚‚ h := by
  rw [MonoidalCategory.tensorHom_def, assoc]
  apply Î¹_mapBifunctorMapMap

/-- The morphism `tensorObj X Yâ‚ âŸ¶ tensorObj X Yâ‚‚` induced by a morphism of graded objects
`Ï† : Yâ‚ âŸ¶ Yâ‚‚`. -/
noncomputable abbrev whiskerLeft (X : GradedObject I C) {Yâ‚ Yâ‚‚ : GradedObject I C} (Ï† : Yâ‚ âŸ¶ Yâ‚‚)
    [HasTensor X Yâ‚] [HasTensor X Yâ‚‚] : tensorObj X Yâ‚ âŸ¶ tensorObj X Yâ‚‚ :=
  tensorHom (ğŸ™ X) Ï†

/-- The morphism `tensorObj Xâ‚ Y âŸ¶ tensorObj Xâ‚‚ Y` induced by a morphism of graded objects
`Ï† : Xâ‚ âŸ¶ Xâ‚‚`. -/
noncomputable abbrev whiskerRight {Xâ‚ Xâ‚‚ : GradedObject I C} (Ï† : Xâ‚ âŸ¶ Xâ‚‚) (Y : GradedObject I C)
    [HasTensor Xâ‚ Y] [HasTensor Xâ‚‚ Y] : tensorObj Xâ‚ Y âŸ¶ tensorObj Xâ‚‚ Y :=
  tensorHom Ï† (ğŸ™ Y)

@[simp]
lemma tensor_id (X Y : GradedObject I C) [HasTensor X Y] :
    tensorHom (ğŸ™ X) (ğŸ™ Y) = ğŸ™ _ := by
  dsimp [tensorHom, mapBifunctorMapMap]
  simp only [Functor.map_id, NatTrans.id_app, comp_id, mapMap_id]
  rfl

@[reassoc]
lemma tensor_comp {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ : GradedObject I C} (fâ‚ : Xâ‚ âŸ¶ Xâ‚‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Xâ‚ƒ)
    (gâ‚ : Yâ‚ âŸ¶ Yâ‚‚) (gâ‚‚ : Yâ‚‚ âŸ¶ Yâ‚ƒ) [HasTensor Xâ‚ Yâ‚] [HasTensor Xâ‚‚ Yâ‚‚] [HasTensor Xâ‚ƒ Yâ‚ƒ] :
    tensorHom (fâ‚ â‰« fâ‚‚) (gâ‚ â‰« gâ‚‚) = tensorHom fâ‚ gâ‚ â‰« tensorHom fâ‚‚ gâ‚‚ := by
  dsimp only [tensorHom, mapBifunctorMapMap]
  rw [â† mapMap_comp]
  apply congr_mapMap
  simp

lemma tensorHom_def {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : GradedObject I C} (f : Xâ‚ âŸ¶ Xâ‚‚) (g : Yâ‚ âŸ¶ Yâ‚‚)
    [HasTensor Xâ‚ Yâ‚] [HasTensor Xâ‚‚ Yâ‚‚] [HasTensor Xâ‚‚ Yâ‚] :
    tensorHom f g = whiskerRight f Yâ‚ â‰« whiskerLeft Xâ‚‚ g := by
  rw [â† tensor_comp, id_comp, comp_id]

/-- This is the addition map `I Ã— I Ã— I â†’ I` for an additive monoid `I`. -/
def râ‚â‚‚â‚ƒ : I Ã— I Ã— I â†’ I := fun âŸ¨i, j, kâŸ© => i + j + k

/-- Auxiliary definition for `associator`. -/
@[reducible] def Ïâ‚â‚‚ : BifunctorCompâ‚â‚‚IndexData (râ‚â‚‚â‚ƒ : _ â†’ I) where
  Iâ‚â‚‚ := I
  p := fun âŸ¨iâ‚, iâ‚‚âŸ© => iâ‚ + iâ‚‚
  q := fun âŸ¨iâ‚â‚‚, iâ‚ƒâŸ© => iâ‚â‚‚ + iâ‚ƒ
  hpq := fun _ => rfl

/-- Auxiliary definition for `associator`. -/
@[reducible] def Ïâ‚‚â‚ƒ : BifunctorCompâ‚‚â‚ƒIndexData (râ‚â‚‚â‚ƒ : _ â†’ I) where
  Iâ‚‚â‚ƒ := I
  p := fun âŸ¨iâ‚‚, iâ‚ƒâŸ© => iâ‚‚ + iâ‚ƒ
  q := fun âŸ¨iâ‚â‚‚, iâ‚ƒâŸ© => iâ‚â‚‚ + iâ‚ƒ
  hpq _ := (add_assoc _ _ _).symm

variable (I) in
/-- Auxiliary definition for `associator`. -/
@[reducible]
def triangleIndexData : TriangleIndexData (râ‚â‚‚â‚ƒ : _ â†’ I) (fun âŸ¨iâ‚, iâ‚ƒâŸ© => iâ‚ + iâ‚ƒ) where
  pâ‚â‚‚ := fun âŸ¨iâ‚, iâ‚‚âŸ© => iâ‚ + iâ‚‚
  pâ‚‚â‚ƒ := fun âŸ¨iâ‚‚, iâ‚ƒâŸ© => iâ‚‚ + iâ‚ƒ
  hpâ‚â‚‚ := fun _ => rfl
  hpâ‚‚â‚ƒ := fun _ => (add_assoc _ _ _).symm
  hâ‚ := add_zero
  hâ‚ƒ := zero_add

/-- Given three graded objects `Xâ‚`, `Xâ‚‚`, `Xâ‚ƒ` in `GradedObject I C`, this is the
assumption that for all `iâ‚â‚‚ : I` and `iâ‚ƒ : I`, the tensor product functor `- âŠ— Xâ‚ƒ iâ‚ƒ`
commutes with the coproduct of the objects `Xâ‚ iâ‚ âŠ— Xâ‚‚ iâ‚‚` such that `iâ‚ + iâ‚‚ = iâ‚â‚‚`. -/
abbrev _root_.CategoryTheory.GradedObject.HasGoodTensorâ‚â‚‚Tensor (Xâ‚ Xâ‚‚ Xâ‚ƒ : GradedObject I C) :=
  HasGoodTrifunctorâ‚â‚‚Obj (curriedTensor C) (curriedTensor C) Ïâ‚â‚‚ Xâ‚ Xâ‚‚ Xâ‚ƒ

/-- Given three graded objects `Xâ‚`, `Xâ‚‚`, `Xâ‚ƒ` in `GradedObject I C`, this is the
assumption that for all `iâ‚ : I` and `iâ‚‚â‚ƒ : I`, the tensor product functor `Xâ‚ iâ‚ âŠ— -`
commutes with the coproduct of the objects `Xâ‚‚ iâ‚‚ âŠ— Xâ‚ƒ iâ‚ƒ` such that `iâ‚‚ + iâ‚ƒ = iâ‚‚â‚ƒ`. -/
abbrev _root_.CategoryTheory.GradedObject.HasGoodTensorTensorâ‚‚â‚ƒ (Xâ‚ Xâ‚‚ Xâ‚ƒ : GradedObject I C) :=
  HasGoodTrifunctorâ‚‚â‚ƒObj (curriedTensor C) (curriedTensor C) Ïâ‚‚â‚ƒ Xâ‚ Xâ‚‚ Xâ‚ƒ

section

variable (Z : C) (Xâ‚ Xâ‚‚ Xâ‚ƒ : GradedObject I C) [HasTensor Xâ‚ Xâ‚‚] [HasTensor Xâ‚‚ Xâ‚ƒ]
  [HasTensor (tensorObj Xâ‚ Xâ‚‚) Xâ‚ƒ] [HasTensor Xâ‚ (tensorObj Xâ‚‚ Xâ‚ƒ)]
  {Yâ‚ Yâ‚‚ Yâ‚ƒ : GradedObject I C} [HasTensor Yâ‚ Yâ‚‚] [HasTensor Yâ‚‚ Yâ‚ƒ]
  [HasTensor (tensorObj Yâ‚ Yâ‚‚) Yâ‚ƒ] [HasTensor Yâ‚ (tensorObj Yâ‚‚ Yâ‚ƒ)]

/-- The associator isomorphism for graded objects. -/
noncomputable def associator [HasGoodTensorâ‚â‚‚Tensor Xâ‚ Xâ‚‚ Xâ‚ƒ] [HasGoodTensorTensorâ‚‚â‚ƒ Xâ‚ Xâ‚‚ Xâ‚ƒ] :
  tensorObj (tensorObj Xâ‚ Xâ‚‚) Xâ‚ƒ â‰… tensorObj Xâ‚ (tensorObj Xâ‚‚ Xâ‚ƒ) :=
    mapBifunctorAssociator (MonoidalCategory.curriedAssociatorNatIso C) Ïâ‚â‚‚ Ïâ‚‚â‚ƒ Xâ‚ Xâ‚‚ Xâ‚ƒ

/-- The inclusion `Xâ‚ iâ‚ âŠ— Xâ‚‚ iâ‚‚ âŠ— Xâ‚ƒ iâ‚ƒ âŸ¶ tensorObj Xâ‚ (tensorObj Xâ‚‚ Xâ‚ƒ) j`
when `iâ‚ + iâ‚‚ + iâ‚ƒ = j`. -/
noncomputable def Î¹TensorObjâ‚ƒ (iâ‚ iâ‚‚ iâ‚ƒ j : I) (h : iâ‚ + iâ‚‚ + iâ‚ƒ = j) :
    Xâ‚ iâ‚ âŠ— Xâ‚‚ iâ‚‚ âŠ— Xâ‚ƒ iâ‚ƒ âŸ¶ tensorObj Xâ‚ (tensorObj Xâ‚‚ Xâ‚ƒ) j :=
  Xâ‚ iâ‚ â— Î¹TensorObj Xâ‚‚ Xâ‚ƒ iâ‚‚ iâ‚ƒ _ rfl â‰« Î¹TensorObj Xâ‚ (tensorObj Xâ‚‚ Xâ‚ƒ) iâ‚ (iâ‚‚ + iâ‚ƒ) j
    (by rw [â† add_assoc, h])

@[reassoc]
lemma Î¹TensorObjâ‚ƒ_eq (iâ‚ iâ‚‚ iâ‚ƒ j : I) (h : iâ‚ + iâ‚‚ + iâ‚ƒ = j) (iâ‚‚â‚ƒ : I) (h' : iâ‚‚ + iâ‚ƒ = iâ‚‚â‚ƒ) :
    Î¹TensorObjâ‚ƒ Xâ‚ Xâ‚‚ Xâ‚ƒ iâ‚ iâ‚‚ iâ‚ƒ j h =
      (Xâ‚ iâ‚ â— Î¹TensorObj Xâ‚‚ Xâ‚ƒ iâ‚‚ iâ‚ƒ iâ‚‚â‚ƒ h') â‰«
        Î¹TensorObj Xâ‚ (tensorObj Xâ‚‚ Xâ‚ƒ) iâ‚ iâ‚‚â‚ƒ j (by rw [â† h', â† add_assoc, h]) := by
  subst h'
  rfl

/-- The inclusion `Xâ‚ iâ‚ âŠ— Xâ‚‚ iâ‚‚ âŠ— Xâ‚ƒ iâ‚ƒ âŸ¶ tensorObj (tensorObj Xâ‚ Xâ‚‚) Xâ‚ƒ j`
when `iâ‚ + iâ‚‚ + iâ‚ƒ = j`. -/
noncomputable def Î¹TensorObjâ‚ƒ' (iâ‚ iâ‚‚ iâ‚ƒ j : I) (h : iâ‚ + iâ‚‚ + iâ‚ƒ = j) :
    (Xâ‚ iâ‚ âŠ— Xâ‚‚ iâ‚‚) âŠ— Xâ‚ƒ iâ‚ƒ âŸ¶ tensorObj (tensorObj Xâ‚ Xâ‚‚) Xâ‚ƒ j :=
  (Î¹TensorObj Xâ‚ Xâ‚‚ iâ‚ iâ‚‚ (iâ‚ + iâ‚‚) rfl â–· Xâ‚ƒ iâ‚ƒ) â‰«
    Î¹TensorObj (tensorObj Xâ‚ Xâ‚‚) Xâ‚ƒ (iâ‚ + iâ‚‚) iâ‚ƒ j h

@[reassoc]
lemma Î¹TensorObjâ‚ƒ'_eq (iâ‚ iâ‚‚ iâ‚ƒ j : I) (h : iâ‚ + iâ‚‚ + iâ‚ƒ = j) (iâ‚â‚‚ : I)
    (h' : iâ‚ + iâ‚‚ = iâ‚â‚‚) :
    Î¹TensorObjâ‚ƒ' Xâ‚ Xâ‚‚ Xâ‚ƒ iâ‚ iâ‚‚ iâ‚ƒ j h =
      (Î¹TensorObj Xâ‚ Xâ‚‚ iâ‚ iâ‚‚ iâ‚â‚‚ h' â–· Xâ‚ƒ iâ‚ƒ) â‰«
        Î¹TensorObj (tensorObj Xâ‚ Xâ‚‚) Xâ‚ƒ iâ‚â‚‚ iâ‚ƒ j (by rw [â† h', h]) := by
  subst h'
  rfl

variable {Xâ‚ Xâ‚‚ Xâ‚ƒ}

@[reassoc (attr := simp)]
lemma Î¹TensorObjâ‚ƒ_tensorHom (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ)
    (iâ‚ iâ‚‚ iâ‚ƒ j : I) (h : iâ‚ + iâ‚‚ + iâ‚ƒ = j) :
    Î¹TensorObjâ‚ƒ Xâ‚ Xâ‚‚ Xâ‚ƒ iâ‚ iâ‚‚ iâ‚ƒ j h â‰« tensorHom fâ‚ (tensorHom fâ‚‚ fâ‚ƒ) j =
      (fâ‚ iâ‚ âŠ— fâ‚‚ iâ‚‚ âŠ— fâ‚ƒ iâ‚ƒ) â‰« Î¹TensorObjâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ iâ‚ iâ‚‚ iâ‚ƒ j h := by
  rw [Î¹TensorObjâ‚ƒ_eq _ _ _ iâ‚ iâ‚‚ iâ‚ƒ j h _  rfl,
    Î¹TensorObjâ‚ƒ_eq _ _ _ iâ‚ iâ‚‚ iâ‚ƒ j h _  rfl, assoc, Î¹_tensorHom,
    â† id_tensorHom, â† id_tensorHom, â† MonoidalCategory.tensor_comp_assoc, Î¹_tensorHom,
    â† MonoidalCategory.tensor_comp_assoc, id_comp, comp_id]

@[reassoc (attr := simp)]
lemma Î¹TensorObjâ‚ƒ'_tensorHom (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ)
    (iâ‚ iâ‚‚ iâ‚ƒ j : I) (h : iâ‚ + iâ‚‚ + iâ‚ƒ = j) :
    Î¹TensorObjâ‚ƒ' Xâ‚ Xâ‚‚ Xâ‚ƒ iâ‚ iâ‚‚ iâ‚ƒ j h â‰« tensorHom (tensorHom fâ‚ fâ‚‚) fâ‚ƒ j =
      ((fâ‚ iâ‚ âŠ— fâ‚‚ iâ‚‚) âŠ— fâ‚ƒ iâ‚ƒ) â‰« Î¹TensorObjâ‚ƒ' Yâ‚ Yâ‚‚ Yâ‚ƒ iâ‚ iâ‚‚ iâ‚ƒ j h := by
  rw [Î¹TensorObjâ‚ƒ'_eq _ _ _ iâ‚ iâ‚‚ iâ‚ƒ j h _  rfl,
    Î¹TensorObjâ‚ƒ'_eq _ _ _ iâ‚ iâ‚‚ iâ‚ƒ j h _  rfl, assoc, Î¹_tensorHom,
    â† tensorHom_id, â† tensorHom_id, â† MonoidalCategory.tensor_comp_assoc, id_comp,
    Î¹_tensorHom, â† MonoidalCategory.tensor_comp_assoc, comp_id]

@[ext]
lemma tensorObjâ‚ƒ_ext {j : I} {A : C} (f g : tensorObj Xâ‚ (tensorObj Xâ‚‚ Xâ‚ƒ) j âŸ¶ A)
    [H : HasGoodTensorTensorâ‚‚â‚ƒ Xâ‚ Xâ‚‚ Xâ‚ƒ]
    (h : âˆ€ (iâ‚ iâ‚‚ iâ‚ƒ : I) (hi : iâ‚ + iâ‚‚ + iâ‚ƒ = j),
      Î¹TensorObjâ‚ƒ Xâ‚ Xâ‚‚ Xâ‚ƒ iâ‚ iâ‚‚ iâ‚ƒ j hi â‰« f = Î¹TensorObjâ‚ƒ Xâ‚ Xâ‚‚ Xâ‚ƒ iâ‚ iâ‚‚ iâ‚ƒ j hi â‰« g) :
      f = g := by
  apply mapBifunctorBifunctorâ‚‚â‚ƒMapObj_ext (H := H)
  intro iâ‚ iâ‚‚ iâ‚ƒ hi
  exact h iâ‚ iâ‚‚ iâ‚ƒ hi

@[ext]
lemma tensorObjâ‚ƒ'_ext {j : I} {A : C} (f g : tensorObj (tensorObj Xâ‚ Xâ‚‚) Xâ‚ƒ j âŸ¶ A)
    [H : HasGoodTensorâ‚â‚‚Tensor Xâ‚ Xâ‚‚ Xâ‚ƒ]
    (h : âˆ€ (iâ‚ iâ‚‚ iâ‚ƒ : I) (h : iâ‚ + iâ‚‚ + iâ‚ƒ = j),
      Î¹TensorObjâ‚ƒ' Xâ‚ Xâ‚‚ Xâ‚ƒ iâ‚ iâ‚‚ iâ‚ƒ j h â‰« f = Î¹TensorObjâ‚ƒ' Xâ‚ Xâ‚‚ Xâ‚ƒ iâ‚ iâ‚‚ iâ‚ƒ j h â‰« g) :
      f = g := by
  apply mapBifunctorâ‚â‚‚BifunctorMapObj_ext (H := H)
  intro iâ‚ iâ‚‚ iâ‚ƒ hi
  exact h iâ‚ iâ‚‚ iâ‚ƒ hi

variable (Xâ‚ Xâ‚‚ Xâ‚ƒ)

@[reassoc (attr := simp)]
lemma Î¹TensorObjâ‚ƒ'_associator_hom
    [HasGoodTensorâ‚â‚‚Tensor Xâ‚ Xâ‚‚ Xâ‚ƒ] [HasGoodTensorTensorâ‚‚â‚ƒ Xâ‚ Xâ‚‚ Xâ‚ƒ]
    (iâ‚ iâ‚‚ iâ‚ƒ j : I) (h : iâ‚ + iâ‚‚ + iâ‚ƒ = j) :
    Î¹TensorObjâ‚ƒ' Xâ‚ Xâ‚‚ Xâ‚ƒ iâ‚ iâ‚‚ iâ‚ƒ j h â‰« (associator Xâ‚ Xâ‚‚ Xâ‚ƒ).hom j =
      (Î±_ _ _ _).hom â‰« Î¹TensorObjâ‚ƒ Xâ‚ Xâ‚‚ Xâ‚ƒ iâ‚ iâ‚‚ iâ‚ƒ j h :=
  Î¹_mapBifunctorAssociator_hom (MonoidalCategory.curriedAssociatorNatIso C)
    Ïâ‚â‚‚ Ïâ‚‚â‚ƒ Xâ‚ Xâ‚‚ Xâ‚ƒ iâ‚ iâ‚‚ iâ‚ƒ j h

@[reassoc (attr := simp)]
lemma Î¹TensorObjâ‚ƒ_associator_inv
    [HasGoodTensorâ‚â‚‚Tensor Xâ‚ Xâ‚‚ Xâ‚ƒ] [HasGoodTensorTensorâ‚‚â‚ƒ Xâ‚ Xâ‚‚ Xâ‚ƒ]
    (iâ‚ iâ‚‚ iâ‚ƒ j : I) (h : iâ‚ + iâ‚‚ + iâ‚ƒ = j) :
    Î¹TensorObjâ‚ƒ Xâ‚ Xâ‚‚ Xâ‚ƒ iâ‚ iâ‚‚ iâ‚ƒ j h â‰« (associator Xâ‚ Xâ‚‚ Xâ‚ƒ).inv j =
      (Î±_ _ _ _).inv â‰« Î¹TensorObjâ‚ƒ' Xâ‚ Xâ‚‚ Xâ‚ƒ iâ‚ iâ‚‚ iâ‚ƒ j h :=
  Î¹_mapBifunctorAssociator_inv (MonoidalCategory.curriedAssociatorNatIso C)
    Ïâ‚â‚‚ Ïâ‚‚â‚ƒ Xâ‚ Xâ‚‚ Xâ‚ƒ iâ‚ iâ‚‚ iâ‚ƒ j h

variable {Xâ‚ Xâ‚‚ Xâ‚ƒ}

lemma associator_naturality (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ)
    [HasGoodTensorâ‚â‚‚Tensor Xâ‚ Xâ‚‚ Xâ‚ƒ] [HasGoodTensorTensorâ‚‚â‚ƒ Xâ‚ Xâ‚‚ Xâ‚ƒ]
    [HasGoodTensorâ‚â‚‚Tensor Yâ‚ Yâ‚‚ Yâ‚ƒ] [HasGoodTensorTensorâ‚‚â‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ] :
    tensorHom (tensorHom fâ‚ fâ‚‚) fâ‚ƒ â‰« (associator Yâ‚ Yâ‚‚ Yâ‚ƒ).hom =
      (associator Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« tensorHom fâ‚ (tensorHom fâ‚‚ fâ‚ƒ) := by aesop_cat

end

section TensorUnit

variable [DecidableEq I] [HasInitial C]

/-- The unit of the tensor product on graded objects is `(singleâ‚€ I).obj (ğŸ™_ C)`. -/
noncomputable def tensorUnit : GradedObject I C := (singleâ‚€ I).obj (ğŸ™_ C)

/-- The canonical isomorphism `tensorUnit 0 â‰… ğŸ™_ C` -/
noncomputable def tensorUnitâ‚€ : (tensorUnit : GradedObject I C) 0 â‰… ğŸ™_ C :=
  singleObjApplyIso (0 : I) (ğŸ™_ C)

/-- `tensorUnit i` is an initial object when `i â‰  0`. -/
noncomputable def isInitialTensorUnitApply (i : I) (hi : i â‰  0) :
    IsInitial ((tensorUnit : GradedObject I C) i) :=
  isInitialSingleObjApply _ _ _ hi

end TensorUnit

section LeftUnitor

variable [DecidableEq I] [HasInitial C]
  [âˆ€ Xâ‚‚, PreservesColimit (Functor.empty.{0} C) ((curriedTensor C).flip.obj Xâ‚‚)]
  (X X' : GradedObject I C)

instance : HasTensor tensorUnit X :=
  mapBifunctorLeftUnitor_hasMap _ _ (leftUnitorNatIso C) _ zero_add _

instance : HasMap (((mapBifunctor (curriedTensor C) I I).obj
    ((singleâ‚€ I).obj (ğŸ™_ C))).obj X) (fun âŸ¨iâ‚, iâ‚‚âŸ© => iâ‚ + iâ‚‚) :=
  (inferInstance : HasTensor tensorUnit X)

/-- The left unitor isomorphism for graded objects. -/
noncomputable def leftUnitor : tensorObj tensorUnit X â‰… X :=
    mapBifunctorLeftUnitor (curriedTensor C) (ğŸ™_ C)
      (leftUnitorNatIso C) (fun (âŸ¨iâ‚, iâ‚‚âŸ© : I Ã— I) => iâ‚ + iâ‚‚) zero_add X

lemma leftUnitor_inv_apply (i : I) :
    (leftUnitor X).inv i = (Î»_ (X i)).inv â‰« tensorUnitâ‚€.inv â–· (X i) â‰«
      Î¹TensorObj tensorUnit X 0 i i (zero_add i) := rfl

variable {X X'}

@[reassoc (attr := simp)]
lemma leftUnitor_naturality (Ï† : X âŸ¶ X') :
    tensorHom (ğŸ™ (tensorUnit)) Ï† â‰« (leftUnitor X').hom =
      (leftUnitor X).hom â‰« Ï† := by
  apply mapBifunctorLeftUnitor_naturality

end LeftUnitor

section RightUnitor

variable [DecidableEq I] [HasInitial C]
  [âˆ€ Xâ‚, PreservesColimit (Functor.empty.{0} C) ((curriedTensor C).obj Xâ‚)]
  (X X' : GradedObject I C)

instance : HasTensor X tensorUnit :=
  mapBifunctorRightUnitor_hasMap (curriedTensor C) _
    (rightUnitorNatIso C) _ add_zero _

instance : HasMap (((mapBifunctor (curriedTensor C) I I).obj X).obj
    ((singleâ‚€ I).obj (ğŸ™_ C))) (fun âŸ¨iâ‚, iâ‚‚âŸ© => iâ‚ + iâ‚‚) :=
  (inferInstance : HasTensor X tensorUnit)

/-- The right unitor isomorphism for graded objects. -/
noncomputable def rightUnitor : tensorObj X tensorUnit â‰… X :=
    mapBifunctorRightUnitor (curriedTensor C) (ğŸ™_ C)
      (rightUnitorNatIso C) (fun (âŸ¨iâ‚, iâ‚‚âŸ© : I Ã— I) => iâ‚ + iâ‚‚) add_zero X

lemma rightUnitor_inv_apply (i : I) :
    (rightUnitor X).inv i = (Ï_ (X i)).inv â‰« (X i) â— tensorUnitâ‚€.inv â‰«
      Î¹TensorObj X tensorUnit i 0 i (add_zero i) := rfl

variable {X X'}

@[reassoc (attr := simp)]
lemma rightUnitor_naturality (Ï† : X âŸ¶ X') :
    tensorHom Ï† (ğŸ™ (tensorUnit)) â‰« (rightUnitor X').hom =
      (rightUnitor X).hom â‰« Ï† := by
  apply mapBifunctorRightUnitor_naturality

end RightUnitor

section Triangle

variable [DecidableEq I] [HasInitial C]
  [âˆ€ Xâ‚, PreservesColimit (Functor.empty.{0} C) ((curriedTensor C).obj Xâ‚)]
  [âˆ€ Xâ‚‚, PreservesColimit (Functor.empty.{0} C)
    ((curriedTensor C).flip.obj Xâ‚‚)]
  (Xâ‚ Xâ‚ƒ : GradedObject I C) [HasTensor Xâ‚ Xâ‚ƒ]
  [HasTensor (tensorObj Xâ‚ tensorUnit) Xâ‚ƒ] [HasTensor Xâ‚ (tensorObj tensorUnit Xâ‚ƒ)]
  [HasGoodTensorâ‚â‚‚Tensor Xâ‚ tensorUnit Xâ‚ƒ] [HasGoodTensorTensorâ‚‚â‚ƒ Xâ‚ tensorUnit Xâ‚ƒ]

lemma triangle :
    (associator Xâ‚ tensorUnit Xâ‚ƒ).hom â‰« tensorHom (ğŸ™ Xâ‚) (leftUnitor Xâ‚ƒ).hom =
      tensorHom (rightUnitor Xâ‚).hom (ğŸ™ Xâ‚ƒ) := by
  convert mapBifunctor_triangle (curriedAssociatorNatIso C) (ğŸ™_ C)
    (rightUnitorNatIso C) (leftUnitorNatIso C) (triangleIndexData I) Xâ‚ Xâ‚ƒ (by simp)

end Triangle

end Monoidal

end GradedObject

end CategoryTheory
