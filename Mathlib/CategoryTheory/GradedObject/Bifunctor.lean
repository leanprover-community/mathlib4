/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.GradedObject
/-!
# The action of bifunctors on graded objects

Given a bifunctor `F : Câ‚ â¥¤ Câ‚‚ â¥¤ Câ‚ƒ` and types `I` and `J`, we construct an obvious functor
`mapBifunctor F I J : GradedObject I Câ‚ â¥¤ GradedObject J Câ‚‚ â¥¤ GradedObject (I Ã— J) Câ‚ƒ`.
When we have a map `p : I Ã— J â†’ K` and that suitable coproducts exists, we also get
a functor
`mapBifunctorMap F p : GradedObject I Câ‚ â¥¤ GradedObject J Câ‚‚ â¥¤ GradedObject K Câ‚ƒ`.

In case `p : I Ã— I â†’ I` is the addition on a monoid and `F` is the tensor product on a monoidal
category `C`, these definitions shall be used in order to construct a monoidal structure
on `GradedObject I C` (TODO @joelriou).

-/

namespace CategoryTheory

open Category

variable {Câ‚ Câ‚‚ Câ‚ƒ : Type*} [Category Câ‚] [Category Câ‚‚] [Category Câ‚ƒ]
  (F : Câ‚ â¥¤ Câ‚‚ â¥¤ Câ‚ƒ)

namespace GradedObject

/-- Given a bifunctor `F : Câ‚ â¥¤ Câ‚‚ â¥¤ Câ‚ƒ` and types `I` and `J`, this is the obvious
functor `GradedObject I Câ‚ â¥¤ GradedObject J Câ‚‚ â¥¤ GradedObject (I Ã— J) Câ‚ƒ`. -/
@[simps]
def mapBifunctor (I J : Type*) :
    GradedObject I Câ‚ â¥¤ GradedObject J Câ‚‚ â¥¤ GradedObject (I Ã— J) Câ‚ƒ where
  obj X :=
    { obj := fun Y ij => (F.obj (X ij.1)).obj (Y ij.2)
      map := fun Ï† ij => (F.obj (X ij.1)).map (Ï† ij.2) }
  map Ï† :=
    { app := fun Y ij => (F.map (Ï† ij.1)).app (Y ij.2) }

section

variable {I J K : Type*} (p : I Ã— J â†’ K)

/-- Given a bifunctor `F : Câ‚ â¥¤ Câ‚‚ â¥¤ Câ‚ƒ`, graded objects `X : GradedObject I Câ‚` and
`Y : GradedObject J Câ‚‚` and a map `p : I Ã— J â†’ K`, this is the `K`-graded object sending
`k` to the coproduct of `(F.obj (X i)).obj (Y j)` for `p âŸ¨i, jâŸ© = k`. -/
noncomputable def mapBifunctorMapObj (X : GradedObject I Câ‚) (Y : GradedObject J Câ‚‚)
    [HasMap (((mapBifunctor F I J).obj X).obj Y) p] : GradedObject K Câ‚ƒ :=
  (((mapBifunctor F I J).obj X).obj Y).mapObj p

/-- The inclusion of `(F.obj (X i)).obj (Y j)` in `mapBifunctorMapObj F p X Y k`
when `i + j = k`. -/
noncomputable def Î¹MapBifunctorMapObj
    (X : GradedObject I Câ‚) (Y : GradedObject J Câ‚‚)
    [HasMap (((mapBifunctor F I J).obj X).obj Y) p]
    (i : I) (j : J) (k : K) (h : p âŸ¨i, jâŸ© = k) :
    (F.obj (X i)).obj (Y j) âŸ¶ mapBifunctorMapObj F p X Y k :=
  (((mapBifunctor F I J).obj X).obj Y).Î¹MapObj p âŸ¨i, jâŸ© k h

/-- The maps `mapBifunctorMapObj F p Xâ‚ Yâ‚ âŸ¶ mapBifunctorMapObj F p Xâ‚‚ Yâ‚‚` which express
the functoriality of `mapBifunctorMapObj`, see `mapBifunctorMap`. -/
noncomputable def mapBifunctorMapMap {Xâ‚ Xâ‚‚ : GradedObject I Câ‚} (f : Xâ‚ âŸ¶ Xâ‚‚)
    {Yâ‚ Yâ‚‚ : GradedObject J Câ‚‚} (g : Yâ‚ âŸ¶ Yâ‚‚)
    [HasMap (((mapBifunctor F I J).obj Xâ‚).obj Yâ‚) p]
    [HasMap (((mapBifunctor F I J).obj Xâ‚‚).obj Yâ‚‚) p] :
    mapBifunctorMapObj F p Xâ‚ Yâ‚ âŸ¶ mapBifunctorMapObj F p Xâ‚‚ Yâ‚‚ :=
  GradedObject.mapMap (((mapBifunctor F I J).map f).app Yâ‚ â‰«
    ((mapBifunctor F I J).obj Xâ‚‚).map g) p

@[reassoc (attr := simp)]
lemma Î¹_mapBifunctorMapMap {Xâ‚ Xâ‚‚ : GradedObject I Câ‚} (f : Xâ‚ âŸ¶ Xâ‚‚)
    {Yâ‚ Yâ‚‚ : GradedObject J Câ‚‚} (g : Yâ‚ âŸ¶ Yâ‚‚)
    [HasMap (((mapBifunctor F I J).obj Xâ‚).obj Yâ‚) p]
    [HasMap (((mapBifunctor F I J).obj Xâ‚‚).obj Yâ‚‚) p]
    (i : I) (j : J) (k : K) (h : p âŸ¨i, jâŸ© = k) :
    Î¹MapBifunctorMapObj F p Xâ‚ Yâ‚ i j k h â‰« mapBifunctorMapMap F p f g k =
      (F.map (f i)).app (Yâ‚ j) â‰« (F.obj (Xâ‚‚ i)).map (g j) â‰«
        Î¹MapBifunctorMapObj F p Xâ‚‚ Yâ‚‚ i j k h := by
  simp [Î¹MapBifunctorMapObj, mapBifunctorMapMap]

@[ext]
lemma mapBifunctorMapObj_ext {X : GradedObject I Câ‚} {Y : GradedObject J Câ‚‚} {A : Câ‚ƒ} {k : K}
    [HasMap (((mapBifunctor F I J).obj X).obj Y) p]
    {f g : mapBifunctorMapObj F p X Y k âŸ¶ A}
    (h : âˆ€ (i : I) (j : J) (hij : p âŸ¨i, jâŸ© = k),
      Î¹MapBifunctorMapObj F p X Y i j k hij â‰« f = Î¹MapBifunctorMapObj F p X Y i j k hij â‰« g) :
    f = g := by
  apply mapObj_ext
  rintro âŸ¨i, jâŸ© hij
  exact h i j hij

variable {F p} in
/-- Constructor for morphisms from `mapBifunctorMapObj F p X Y k`. -/
noncomputable def mapBifunctorMapObjDesc
    {X : GradedObject I Câ‚} {Y : GradedObject J Câ‚‚} {A : Câ‚ƒ} {k : K}
    [HasMap (((mapBifunctor F I J).obj X).obj Y) p]
    (f : âˆ€ (i : I) (j : J) (_ : p âŸ¨i, jâŸ© = k), (F.obj (X i)).obj (Y j) âŸ¶ A) :
    mapBifunctorMapObj F p X Y k âŸ¶ A :=
  descMapObj _ _ (fun âŸ¨i, jâŸ© hij => f i j hij)

@[reassoc (attr := simp)]
lemma Î¹_mapBifunctorMapObjDesc {X : GradedObject I Câ‚} {Y : GradedObject J Câ‚‚} {A : Câ‚ƒ} {k : K}
    [HasMap (((mapBifunctor F I J).obj X).obj Y) p]
    (f : âˆ€ (i : I) (j : J) (_ : p âŸ¨i, jâŸ© = k), (F.obj (X i)).obj (Y j) âŸ¶ A)
    (i : I) (j : J) (hij : p âŸ¨i, jâŸ© = k) :
    Î¹MapBifunctorMapObj F p X Y i j k hij â‰« mapBifunctorMapObjDesc f = f i j hij := by
  apply Î¹_descMapObj

section

variable {Xâ‚ Xâ‚‚ : GradedObject I Câ‚} {Yâ‚ Yâ‚‚ : GradedObject J Câ‚‚}
    [HasMap (((mapBifunctor F I J).obj Xâ‚).obj Yâ‚) p]
    [HasMap (((mapBifunctor F I J).obj Xâ‚‚).obj Yâ‚‚) p]

/-- The isomorphism `mapBifunctorMapObj F p Xâ‚ Yâ‚ â‰… mapBifunctorMapObj F p Xâ‚‚ Yâ‚‚`
induced by isomorphisms `Xâ‚ â‰… Xâ‚‚` and `Yâ‚ â‰… Yâ‚‚`. -/
@[simps]
noncomputable def mapBifunctorMapMapIso (e : Xâ‚ â‰… Xâ‚‚) (e' : Yâ‚ â‰… Yâ‚‚) :
    mapBifunctorMapObj F p Xâ‚ Yâ‚ â‰… mapBifunctorMapObj F p Xâ‚‚ Yâ‚‚ where
  hom := mapBifunctorMapMap F p e.hom e'.hom
  inv := mapBifunctorMapMap F p e.inv e'.inv
  hom_inv_id := by ext; simp
  inv_hom_id := by ext; simp

instance (f : Xâ‚ âŸ¶ Xâ‚‚) (g : Yâ‚ âŸ¶ Yâ‚‚) [IsIso f] [IsIso g] :
    IsIso (mapBifunctorMapMap F p f g) :=
  (inferInstance : IsIso (mapBifunctorMapMapIso F p (asIso f) (asIso g)).hom)

end

attribute [local simp] mapBifunctorMapMap

/-- Given a bifunctor `F : Câ‚ â¥¤ Câ‚‚ â¥¤ Câ‚ƒ` and a map `p : I Ã— J â†’ K`, this is the
functor `GradedObject I Câ‚ â¥¤ GradedObject J Câ‚‚ â¥¤ GradedObject K Câ‚ƒ` sending
`X : GradedObject I Câ‚` and `Y : GradedObject J Câ‚‚` to the `K`-graded object sending
`k` to the coproduct of `(F.obj (X i)).obj (Y j)` for `p âŸ¨i, jâŸ© = k`. -/
@[simps]
noncomputable def mapBifunctorMap [âˆ€ X Y, HasMap (((mapBifunctor F I J).obj X).obj Y) p] :
    GradedObject I Câ‚ â¥¤ GradedObject J Câ‚‚ â¥¤ GradedObject K Câ‚ƒ where
  obj X :=
    { obj := fun Y => mapBifunctorMapObj F p X Y
      map := fun Ïˆ => mapBifunctorMapMap F p (ğŸ™ X) Ïˆ }
  map {Xâ‚ Xâ‚‚} Ï† :=
    { app := fun Y => mapBifunctorMapMap F p Ï† (ğŸ™ Y)
      naturality := fun {Yâ‚ Yâ‚‚} Ïˆ => by
        dsimp
        simp only [Functor.map_id, NatTrans.id_app, id_comp, comp_id,
          â† mapMap_comp, NatTrans.naturality] }

end

end GradedObject

end CategoryTheory
