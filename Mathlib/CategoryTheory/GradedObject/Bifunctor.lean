/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.GradedObject
/-!
# The action of bifunctors on graded objects

Given a bifunctor `F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ` and types `I` and `J`, we construct an obvious functor
`mapBifunctor F I J : GradedObject I C‚ÇÅ ‚•§ GradedObject J C‚ÇÇ ‚•§ GradedObject (I √ó J) C‚ÇÉ`.
When we have a map `p : I √ó J ‚Üí K` and that suitable coproducts exists, we also get
a functor
`mapBifunctorMap F p : GradedObject I C‚ÇÅ ‚•§ GradedObject J C‚ÇÇ ‚•§ GradedObject K C‚ÇÉ`.

In case `p : I √ó I ‚Üí I` is the addition on a monoid and `F` is the tensor product on a monoidal
category `C`, these definitions shall be used in order to construct a monoidal structure
on `GradedObject I C` (TODO @joelriou).

-/

namespace CategoryTheory

open Category

variable {C‚ÇÅ C‚ÇÇ C‚ÇÉ : Type*} [Category C‚ÇÅ] [Category C‚ÇÇ] [Category C‚ÇÉ]
  (F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ)

namespace GradedObject

/-- Given a bifunctor `F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ` and types `I` and `J`, this is the obvious
functor `GradedObject I C‚ÇÅ ‚•§ GradedObject J C‚ÇÇ ‚•§ GradedObject (I √ó J) C‚ÇÉ`. -/
@[simps]
def mapBifunctor (I J : Type*) :
    GradedObject I C‚ÇÅ ‚•§ GradedObject J C‚ÇÇ ‚•§ GradedObject (I √ó J) C‚ÇÉ where
  obj X :=
    { obj := fun Y ij ‚Ü¶ (F.obj (X ij.1)).obj (Y ij.2)
      map := fun œÜ ij ‚Ü¶ (F.obj (X ij.1)).map (œÜ ij.2) }
  map œÜ :=
    { app := fun Y ij ‚Ü¶ (F.map (œÜ ij.1)).app (Y ij.2) }

section

variable {I J K : Type*} (p : I √ó J ‚Üí K)

/-- Given a bifunctor `F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ`, graded objects `X : GradedObject I C‚ÇÅ` and
`Y : GradedObject J C‚ÇÇ` and a map `p : I √ó J ‚Üí K`, this is the `K`-graded object sending
`k` to the coproduct of `(F.obj (X i)).obj (Y j)` for `p ‚ü®i, j‚ü© = k`. -/
noncomputable def mapBifunctorMapObj (X : GradedObject I C‚ÇÅ) (Y : GradedObject J C‚ÇÇ)
    [HasMap (((mapBifunctor F I J).obj X).obj Y) p] : GradedObject K C‚ÇÉ :=
  (((mapBifunctor F I J).obj X).obj Y).mapObj p

/-- The inclusion of `(F.obj (X i)).obj (Y j)` in `mapBifunctorMapObj F p X Y k`
when `i + j = k`. -/
noncomputable def ŒπMapBifunctorMapObj
    (X : GradedObject I C‚ÇÅ) (Y : GradedObject J C‚ÇÇ)
    [HasMap (((mapBifunctor F I J).obj X).obj Y) p]
    (i : I) (j : J) (k : K) (h : p ‚ü®i, j‚ü© = k) :
    (F.obj (X i)).obj (Y j) ‚ü∂ mapBifunctorMapObj F p X Y k :=
  (((mapBifunctor F I J).obj X).obj Y).ŒπMapObj p ‚ü®i, j‚ü© k h

/-- The maps `mapBifunctorMapObj F p X‚ÇÅ Y‚ÇÅ ‚ü∂ mapBifunctorMapObj F p X‚ÇÇ Y‚ÇÇ` which express
the functoriality of `mapBifunctorMapObj`, see `mapBifunctorMap`. -/
noncomputable def mapBifunctorMapMap {X‚ÇÅ X‚ÇÇ : GradedObject I C‚ÇÅ} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ)
    {Y‚ÇÅ Y‚ÇÇ : GradedObject J C‚ÇÇ} (g : Y‚ÇÅ ‚ü∂ Y‚ÇÇ)
    [HasMap (((mapBifunctor F I J).obj X‚ÇÅ).obj Y‚ÇÅ) p]
    [HasMap (((mapBifunctor F I J).obj X‚ÇÇ).obj Y‚ÇÇ) p] :
    mapBifunctorMapObj F p X‚ÇÅ Y‚ÇÅ ‚ü∂ mapBifunctorMapObj F p X‚ÇÇ Y‚ÇÇ :=
  GradedObject.mapMap (((mapBifunctor F I J).map f).app Y‚ÇÅ ‚â´
    ((mapBifunctor F I J).obj X‚ÇÇ).map g) p

@[reassoc (attr := simp)]
lemma Œπ_mapBifunctorMapMap {X‚ÇÅ X‚ÇÇ : GradedObject I C‚ÇÅ} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ)
    {Y‚ÇÅ Y‚ÇÇ : GradedObject J C‚ÇÇ} (g : Y‚ÇÅ ‚ü∂ Y‚ÇÇ)
    [HasMap (((mapBifunctor F I J).obj X‚ÇÅ).obj Y‚ÇÅ) p]
    [HasMap (((mapBifunctor F I J).obj X‚ÇÇ).obj Y‚ÇÇ) p]
    (i : I) (j : J) (k : K) (h : p ‚ü®i, j‚ü© = k) :
    ŒπMapBifunctorMapObj F p X‚ÇÅ Y‚ÇÅ i j k h ‚â´ mapBifunctorMapMap F p f g k =
      (F.map (f i)).app (Y‚ÇÅ j) ‚â´ (F.obj (X‚ÇÇ i)).map (g j) ‚â´
        ŒπMapBifunctorMapObj F p X‚ÇÇ Y‚ÇÇ i j k h := by
  simp [ŒπMapBifunctorMapObj, mapBifunctorMapMap]

@[ext]
lemma mapBifunctorMapObj_ext {X : GradedObject I C‚ÇÅ} {Y : GradedObject J C‚ÇÇ} {A : C‚ÇÉ} {k : K}
    [HasMap (((mapBifunctor F I J).obj X).obj Y) p]
    {f g : mapBifunctorMapObj F p X Y k ‚ü∂ A}
    (h : ‚àÄ (i : I) (j : J) (hij : p ‚ü®i, j‚ü© = k),
      ŒπMapBifunctorMapObj F p X Y i j k hij ‚â´ f = ŒπMapBifunctorMapObj F p X Y i j k hij ‚â´ g) :
    f = g := by
  apply mapObj_ext
  rintro ‚ü®i, j‚ü© hij
  exact h i j hij

variable {F p} in
/-- Constructor for morphisms from `mapBifunctorMapObj F p X Y k`. -/
noncomputable def mapBifunctorMapObjDesc
    {X : GradedObject I C‚ÇÅ} {Y : GradedObject J C‚ÇÇ} {A : C‚ÇÉ} {k : K}
    [HasMap (((mapBifunctor F I J).obj X).obj Y) p]
    (f : ‚àÄ (i : I) (j : J) (_ : p ‚ü®i, j‚ü© = k), (F.obj (X i)).obj (Y j) ‚ü∂ A) :
    mapBifunctorMapObj F p X Y k ‚ü∂ A :=
  descMapObj _ _ (fun ‚ü®i, j‚ü© hij ‚Ü¶ f i j hij)

@[reassoc (attr := simp)]
lemma Œπ_mapBifunctorMapObjDesc {X : GradedObject I C‚ÇÅ} {Y : GradedObject J C‚ÇÇ} {A : C‚ÇÉ} {k : K}
    [HasMap (((mapBifunctor F I J).obj X).obj Y) p]
    (f : ‚àÄ (i : I) (j : J) (_ : p ‚ü®i, j‚ü© = k), (F.obj (X i)).obj (Y j) ‚ü∂ A)
    (i : I) (j : J) (hij : p ‚ü®i, j‚ü© = k) :
    ŒπMapBifunctorMapObj F p X Y i j k hij ‚â´ mapBifunctorMapObjDesc f = f i j hij := by
  apply Œπ_descMapObj

section

variable {X‚ÇÅ X‚ÇÇ : GradedObject I C‚ÇÅ} {Y‚ÇÅ Y‚ÇÇ : GradedObject J C‚ÇÇ}
    [HasMap (((mapBifunctor F I J).obj X‚ÇÅ).obj Y‚ÇÅ) p]
    [HasMap (((mapBifunctor F I J).obj X‚ÇÇ).obj Y‚ÇÇ) p]

/-- The isomorphism `mapBifunctorMapObj F p X‚ÇÅ Y‚ÇÅ ‚âÖ mapBifunctorMapObj F p X‚ÇÇ Y‚ÇÇ`
induced by isomorphisms `X‚ÇÅ ‚âÖ X‚ÇÇ` and `Y‚ÇÅ ‚âÖ Y‚ÇÇ`. -/
@[simps]
noncomputable def mapBifunctorMapMapIso (e : X‚ÇÅ ‚âÖ X‚ÇÇ) (e' : Y‚ÇÅ ‚âÖ Y‚ÇÇ) :
    mapBifunctorMapObj F p X‚ÇÅ Y‚ÇÅ ‚âÖ mapBifunctorMapObj F p X‚ÇÇ Y‚ÇÇ where
  hom := mapBifunctorMapMap F p e.hom e'.hom
  inv := mapBifunctorMapMap F p e.inv e'.inv
  hom_inv_id := by ext; simp
  inv_hom_id := by ext; simp

instance (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (g : Y‚ÇÅ ‚ü∂ Y‚ÇÇ) [IsIso f] [IsIso g] :
    IsIso (mapBifunctorMapMap F p f g) :=
  (inferInstance : IsIso (mapBifunctorMapMapIso F p (asIso f) (asIso g)).hom)

end

attribute [local simp] mapBifunctorMapMap

/-- Given a bifunctor `F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ C‚ÇÉ` and a map `p : I √ó J ‚Üí K`, this is the
functor `GradedObject I C‚ÇÅ ‚•§ GradedObject J C‚ÇÇ ‚•§ GradedObject K C‚ÇÉ` sending
`X : GradedObject I C‚ÇÅ` and `Y : GradedObject J C‚ÇÇ` to the `K`-graded object sending
`k` to the coproduct of `(F.obj (X i)).obj (Y j)` for `p ‚ü®i, j‚ü© = k`. -/
@[simps]
noncomputable def mapBifunctorMap [‚àÄ X Y, HasMap (((mapBifunctor F I J).obj X).obj Y) p] :
    GradedObject I C‚ÇÅ ‚•§ GradedObject J C‚ÇÇ ‚•§ GradedObject K C‚ÇÉ where
  obj X :=
    { obj := fun Y ‚Ü¶ mapBifunctorMapObj F p X Y
      map := fun œà ‚Ü¶ mapBifunctorMapMap F p (ùüô X) œà }
  map {X‚ÇÅ X‚ÇÇ} œÜ :=
    { app := fun Y ‚Ü¶ mapBifunctorMapMap F p œÜ (ùüô Y)
      naturality := fun {Y‚ÇÅ Y‚ÇÇ} œà ‚Ü¶ by
        dsimp
        simp only [Functor.map_id, NatTrans.id_app, id_comp, comp_id,
          ‚Üê mapMap_comp, NatTrans.naturality] }

end

end GradedObject

end CategoryTheory
