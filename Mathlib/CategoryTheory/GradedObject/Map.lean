import Mathlib.CategoryTheory.GradedObject

namespace CategoryTheory

open Category Limits

variable {C D : Type*} [Category C] [Category D]

def Cofan.IsColimit.desc {I : Type*} {F : I â†’ C} {c : Cofan F} (hc : IsColimit c) {A : C}
    (f : âˆ€ i, F i âŸ¶ A) : c.pt âŸ¶ A :=
  hc.desc (Cofan.mk A f)

@[reassoc (attr := simp)]
lemma Cofan.IsColimit.fac {I : Type*} {F : I â†’ C} {c : Cofan F} (hc : IsColimit c) {A : C}
    (f : âˆ€ i, F i âŸ¶ A) (i : I) :
    c.proj i â‰« Cofan.IsColimit.desc hc f = f i :=
  hc.fac (Cofan.mk A f) âŸ¨iâŸ©

lemma Cofan.IsColimit.hom_ext {I : Type*} {F : I â†’ C} {c : Cofan F} (hc : IsColimit c) {A : C}
    (f g : c.pt âŸ¶ A) (h : âˆ€ i, c.proj i â‰« f = c.proj i â‰« g): f = g :=
  hc.hom_ext (fun âŸ¨iâŸ© => h i)

namespace GradedObject

section

@[simps]
def isoMk {I : Type*} (X Y : GradedObject I C) (e : âˆ€ i, X i â‰… Y i) : X â‰… Y where
  hom i := (e i).hom
  inv i := (e i).inv

lemma isIso_of_isIso_apply {I : Type*} {X Y : GradedObject I C} (f : X âŸ¶ Y)
    (h : âˆ€ i, IsIso (f i)) : IsIso f := by
  change IsIso (isoMk X Y (fun i => asIso (f i))).hom
  infer_instance

@[reassoc (attr := simp)]
lemma iso_hom_inv_id_apply {I : Type*} {X Y : GradedObject I C} (e : X â‰… Y) (i : I) :
    e.hom i â‰« e.inv i = ğŸ™ _ :=
  congr_fun e.hom_inv_id i

@[reassoc (attr := simp)]
lemma iso_inv_hom_id_apply {I : Type*} {X Y : GradedObject I C} (e : X â‰… Y) (i : I) :
    e.inv i â‰« e.hom i = ğŸ™ _ :=
  congr_fun e.inv_hom_id i

instance {I : Type*} {X Y : GradedObject I C} (f : X âŸ¶ Y) [IsIso f] (i : I) : IsIso (f i) := by
  change IsIso ((eval i).map f)
  infer_instance

end

section

variable {I J : Type*} (X Y Z : GradedObject I C) (Ï† : X âŸ¶ Y) (e : X â‰… Y) (Ïˆ : Y âŸ¶ Z) (p : I â†’ J)

@[simp]
abbrev mapObjFun (j : J) := (fun (i : (p â»Â¹' {j})) => X i)

variable (j : J)

abbrev HasMap : Prop := âˆ€ (j : J), HasCoproduct (X.mapObjFun p j)

variable [X.HasMap p] [Y.HasMap p] [Z.HasMap p]

noncomputable def mapObj : GradedObject J C := fun j => âˆ (X.mapObjFun p j)

noncomputable def Î¹MapObj (i : I) (j : J) (hij : p i = j) : X i âŸ¶ X.mapObj p j :=
  Sigma.Î¹ (fun (i' : (p â»Â¹' {j})) => X i') âŸ¨i, hijâŸ©

abbrev CofanMapObjFun (j : J) := Cofan (X.mapObjFun p j)

@[simp]
def CofanMapObjFun.mk (j : J) (pt : C) (Î¹' : âˆ€ (i : I) (_ : p i = j), X i âŸ¶ pt) : CofanMapObjFun X p j :=
  Cofan.mk pt (fun âŸ¨i, hiâŸ© => Î¹' i hi)

@[simps!]
noncomputable def cofanMapObj (j : J) : CofanMapObjFun X p j :=
  CofanMapObjFun.mk X p j (X.mapObj p j) (fun i hi => X.Î¹MapObj p i j hi)

@[ext]
lemma mapObj_ext {A : C} {j : J} (f g : X.mapObj p j âŸ¶ A)
    (hfg : âˆ€ (i : I) (hij : p i = j), X.Î¹MapObj p i j hij â‰« f = X.Î¹MapObj p i j hij â‰« g) :
    f = g := by
  apply Limits.Sigma.hom_ext
  rintro âŸ¨i, hijâŸ©
  exact hfg i hij

attribute [local ext] mapObj_ext

noncomputable def descMapObj {A : C} {j : J} (Ï† : âˆ€ (i : I) (_ : p i = j), X i âŸ¶ A) :
    X.mapObj p j âŸ¶ A :=
  Limits.Sigma.desc (fun x => Ï† x.1 x.2)

@[reassoc (attr := simp)]
lemma Î¹_descMapObj {A : C} {j : J} (Ï† : âˆ€ (i : I) (_ : p i = j), X i âŸ¶ A) (i : I) (hi : p i = j) :
    X.Î¹MapObj p i j hi â‰« X.descMapObj p Ï† = Ï† i hi := by
  simp [descMapObj, Î¹MapObj]

@[simps]
noncomputable def isColimitCofanMapObj (j : J) : IsColimit (X.cofanMapObj p j) where
  desc s := descMapObj _ _ (fun i hi => s.Î¹.app âŸ¨âŸ¨i, hiâŸ©âŸ©)
  fac s := fun âŸ¨i, hiâŸ© => by simp
  uniq s m hm := by
    apply mapObj_ext
    intro i hi
    simpa using hm âŸ¨i, hiâŸ©

namespace CofanMapObjFun

lemma hasMap (c : âˆ€ j, CofanMapObjFun X p j) (hc : âˆ€ j, IsColimit (c j)) :
    X.HasMap p := fun j => âŸ¨_, hc jâŸ©

variable {j X p}
  {c : CofanMapObjFun X p j} (hc : IsColimit c) [X.HasMap p]

noncomputable def iso : c.pt â‰… X.mapObj p j :=
  IsColimit.coconePointUniqueUpToIso hc (X.isColimitCofanMapObj p j)

@[reassoc (attr := simp)]
lemma proj_iso_hom (i : I) (hi : p i = j) :
    c.proj âŸ¨i, hiâŸ© â‰« (c.iso hc).hom = X.Î¹MapObj p i j hi := by
  apply IsColimit.comp_coconePointUniqueUpToIso_hom

@[reassoc (attr := simp)]
lemma Î¹MapObj_iso_inv (i : I) (hi : p i = j) :
    X.Î¹MapObj p i j hi â‰« (c.iso hc).inv = c.proj âŸ¨i, hiâŸ© := by
  apply IsColimit.comp_coconePointUniqueUpToIso_inv

end CofanMapObjFun

variable {X Y}

noncomputable def mapMap : X.mapObj p âŸ¶ Y.mapObj p := fun _ => Limits.Sigma.map (fun i => Ï† i)

@[reassoc (attr := simp)]
lemma Î¹_mapMap (i : I) (j : J) (hij : p i = j) :
    X.Î¹MapObj p i j hij â‰« mapMap Ï† p j = Ï† i â‰« Y.Î¹MapObj p i j hij := by
  simp [Î¹MapObj, mapMap]

lemma congr_mapMap (Ï†â‚ Ï†â‚‚ : X âŸ¶ Y) (h : Ï†â‚ = Ï†â‚‚) : mapMap Ï†â‚ p = mapMap Ï†â‚‚ p := by
  subst h
  rfl

variable (X)

@[simp]
lemma mapMap_id : mapMap (ğŸ™ X) p = ğŸ™ _ := by aesop_cat

variable {X Z}

@[simp]
lemma mapMap_comp : mapMap (Ï† â‰« Ïˆ) p = mapMap Ï† p â‰« mapMap Ïˆ p := by aesop_cat

@[simps]
noncomputable def mapIso : X.mapObj p â‰… Y.mapObj p where
  hom := mapMap e.hom p
  inv := mapMap e.inv p
  hom_inv_id := by rw [â† mapMap_comp, e.hom_inv_id, mapMap_id]
  inv_hom_id := by rw [â† mapMap_comp, e.inv_hom_id, mapMap_id]

variable (C)

@[simps]
noncomputable def map [âˆ€ (j : J), HasColimitsOfShape (Discrete (p â»Â¹' {j})) C] :
    GradedObject I C â¥¤ GradedObject J C where
  obj X := X.mapObj p
  map Ï† := mapMap Ï† p

end

section

variable {I J K : Type*} (X Y : GradedObject I C) (p : I â†’ J) (q : J â†’ K) (r : I â†’ K)
  (hpqr : âˆ€ i, r i = q (p i))

section

variable (k : K) (c : âˆ€ (j : J) (_ : q j = k), X.CofanMapObjFun p j)
  (hc : âˆ€ j hj, IsColimit (c j hj))
  (c' : Cofan (fun (j : q â»Â¹' {k}) => (c j.1 j.2).pt))
  (hc' : IsColimit c')

@[simp]
def cofanMapObjComp : X.CofanMapObjFun r k :=
  CofanMapObjFun.mk _ _ _ c'.pt (fun i hi =>
    (c (p i) (by rw [â† hpqr, hi])).proj âŸ¨i, rflâŸ© â‰« c'.proj (âŸ¨p i, by
      rw [Set.mem_preimage, Set.mem_singleton_iff, â† hpqr, hi]âŸ©))

@[simp]
def isColimitCofanMapObjComp :
    IsColimit (cofanMapObjComp X p q r hpqr k c c') :=
  mkCofanColimit _
    (fun s => Cofan.IsColimit.desc hc'
      (fun âŸ¨j, (hj : q j = k)âŸ© => Cofan.IsColimit.desc (hc j hj)
        (fun âŸ¨i, (hi : p i = j)âŸ© => s.proj âŸ¨i, by
          simp only [Set.mem_preimage, Set.mem_singleton_iff, hpqr, hi, hj]âŸ©)))
    (fun s âŸ¨i, (hi : r i = k)âŸ© => by simp)
    (fun s m hm => by
      apply Cofan.IsColimit.hom_ext hc'
      rintro âŸ¨j, rfl : q j = kâŸ©
      apply Cofan.IsColimit.hom_ext (hc j rfl)
      rintro âŸ¨i, rfl : p i = jâŸ©
      dsimp
      rw [Cofan.IsColimit.fac, Cofan.IsColimit.fac, â† hm]
      dsimp
      rw [assoc])

lemma hasMap_comp [X.HasMap p] [(X.mapObj p).HasMap q] : X.HasMap r :=
  fun k => âŸ¨_, isColimitCofanMapObjComp X p q r hpqr k _
    (fun j _ => X.isColimitCofanMapObj p j) _ ((X.mapObj p).isColimitCofanMapObj q k)âŸ©

end

variable [X.HasMap p] [(X.mapObj p).HasMap q] [X.HasMap r]

noncomputable def mapObjMapObjIso : (X.mapObj p).mapObj q â‰… X.mapObj r :=
  isoMk _ _ (fun k => CofanMapObjFun.iso (isColimitCofanMapObjComp X p q r hpqr k _
      (fun j _ => X.isColimitCofanMapObj p j) _ ((X.mapObj p).isColimitCofanMapObj q k)))

@[simp]
lemma mapObjMapObjIso_hom (k : K) :
    (mapObjMapObjIso X p q r hpqr).hom k =
      descMapObj _ _ (fun j hj =>
        descMapObj _ _ (fun i hi => X.Î¹MapObj r i k (by rw [hpqr, hi, hj]))) := rfl

@[simp]
lemma mapObjMapObjIso_inv (k : K) :
    (mapObjMapObjIso X p q r hpqr).inv k =
      descMapObj _ _ (fun i hi => X.Î¹MapObj p i (p i) rfl â‰«
        (X.mapObj p).Î¹MapObj q (p i) k (by rw [â† hi, hpqr])) := rfl

end

end GradedObject

end CategoryTheory
