/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.GradedObject.Bifunctor
import Mathlib.CategoryTheory.GradedObject.Single
/-!
# The left and right unitors

Given a bifunctor `F : C ‚•§ D ‚•§ D`, an object `X : C` such that `F.obj X ‚âÖ ùü≠ D` and a
map `p : I √ó J ‚Üí J` such that `hp : ‚àÄ (j : J), p ‚ü®0, j‚ü© = j`,
we define an isomorphism of `J`-graded objects for any `Y : GradedObject J D`.
`mapBifunctorLeftUnitor F X e p hp Y : mapBifunctorMapObj F p ((single‚ÇÄ I).obj X) Y ‚âÖ Y`.

TODO (@joelriou): define similarly a right unitor isomorphism and get the triangle identity.

-/

namespace CategoryTheory

open Category Limits

namespace GradedObject

section LeftUnitor

variable {C D I J : Type*} [Category C] [Category D]
  [Zero I] [DecidableEq I] [HasInitial C]
  (F : C ‚•§ D ‚•§ D) (X : C) (e : F.obj X ‚âÖ ùü≠ D)
  [‚àÄ (Y : D), PreservesColimit (Functor.empty.{0} C) (F.flip.obj Y)]
  (p : I √ó J ‚Üí J) (hp : ‚àÄ (j : J), p ‚ü®0, j‚ü© = j)
  (Y Y' : GradedObject J D) (œÜ : Y ‚ü∂ Y')

/-- Given `F : C ‚•§ D ‚•§ D`, `X : C`, `e : F.obj X ‚âÖ ùü≠ D` and `Y : GradedObject J D`,
this is the isomorphism `((mapBifunctor F I J).obj ((single‚ÇÄ I).obj X)).obj Y a ‚âÖ Y a.2`
when `a : I √ó J` is such that `a.1 = 0`. -/
@[simps!]
noncomputable def mapBifunctorObjSingle‚ÇÄObjIso (a : I √ó J) (ha : a.1 = 0) :
    ((mapBifunctor F I J).obj ((single‚ÇÄ I).obj X)).obj Y a ‚âÖ Y a.2 :=
  (F.mapIso (singleObjApplyIsoOfEq _ X _ ha)).app _ ‚â™‚â´ e.app (Y a.2)

/-- Given `F : C ‚•§ D ‚•§ D`, `X : C` and `Y : GradedObject J D`,
`((mapBifunctor F I J).obj ((single‚ÇÄ I).obj X)).obj Y a` is an initial when `a : I √ó J`
is such that `a.1 ‚â† 0`. -/
noncomputable def mapBifunctorObjSingle‚ÇÄObjIsInitial (a : I √ó J) (ha : a.1 ‚â† 0) :
    IsInitial (((mapBifunctor F I J).obj ((single‚ÇÄ I).obj X)).obj Y a) :=
  IsInitial.isInitialObj (F.flip.obj (Y a.2)) _ (isInitialSingleObjApply _ _ _ ha)

/-- Given `F : C ‚•§ D ‚•§ D`, `X : C`, `e : F.obj X ‚âÖ ùü≠ D`, `Y : GradedObject J D` and
`p : I √ó J ‚Üí J` such that `p ‚ü®0, j‚ü© = j` for all `j`,
this is the (colimit) cofan which shall be used to construct the isomorphism
`mapBifunctorMapObj F p ((single‚ÇÄ I).obj X) Y ‚âÖ Y`, see `mapBifunctorLeftUnitor`. -/
noncomputable def mapBifunctorLeftUnitorCofan (j : J) :
    (((mapBifunctor F I J).obj ((single‚ÇÄ I).obj X)).obj Y).CofanMapObjFun p j :=
  CofanMapObjFun.mk _ _ _ (Y j) (fun a ha =>
    if ha : a.1 = 0 then
      (mapBifunctorObjSingle‚ÇÄObjIso F X e Y a ha).hom ‚â´ eqToHom (by aesop)
    else
      (mapBifunctorObjSingle‚ÇÄObjIsInitial F X Y a ha).to _)

@[simp, reassoc]
lemma mapBifunctorLeftUnitorCofan_inj (j : J) :
    (mapBifunctorLeftUnitorCofan F X e p hp Y j).inj ‚ü®‚ü®0, j‚ü©, hp j‚ü© =
      (F.map (singleObjApplyIso (0 : I) X).hom).app (Y j) ‚â´ e.hom.app (Y j) := by
  simp [mapBifunctorLeftUnitorCofan]

/-- The cofan `mapBifunctorLeftUnitorCofan F X e p hp Y j` is a colimit. -/
noncomputable def mapBifunctorLeftUnitorCofanIsColimit (j : J) :
    IsColimit (mapBifunctorLeftUnitorCofan F X e p hp Y j) :=
  mkCofanColimit _
    (fun s => e.inv.app (Y j) ‚â´
      (F.map (singleObjApplyIso (0 : I) X).inv).app (Y j) ‚â´ s.inj ‚ü®‚ü®0, j‚ü©, hp j‚ü©)
    (fun s => by
      rintro ‚ü®‚ü®i, j'‚ü©, h‚ü©
      by_cases hi : i = 0
      ¬∑ subst hi
        simp only [Set.mem_preimage, hp, Set.mem_singleton_iff] at h
        subst h
        simp
      ¬∑ apply IsInitial.hom_ext
        exact mapBifunctorObjSingle‚ÇÄObjIsInitial _ _ _ _ hi)
    (fun s m hm => by simp [‚Üê hm ‚ü®‚ü®0, j‚ü©, hp j‚ü©])

lemma mapBifunctorLeftUnitor_hasMap :
    HasMap (((mapBifunctor F I J).obj ((single‚ÇÄ I).obj X)).obj Y) p :=
  CofanMapObjFun.hasMap _ _ _ (mapBifunctorLeftUnitorCofanIsColimit F X e p hp Y)

variable [HasMap (((mapBifunctor F I J).obj ((single‚ÇÄ I).obj X)).obj Y) p]
  [HasMap (((mapBifunctor F I J).obj ((single‚ÇÄ I).obj X)).obj Y') p]

/-- Given `F : C ‚•§ D ‚•§ D`, `X : C`, `e : F.obj X ‚âÖ ùü≠ D`, `Y : GradedObject J D` and
`p : I √ó J ‚Üí J` such that `p ‚ü®0, j‚ü© = j` for all `j`,
this is the left unitor isomorphism `mapBifunctorMapObj F p ((single‚ÇÄ I).obj X) Y ‚âÖ Y`. -/
noncomputable def mapBifunctorLeftUnitor : mapBifunctorMapObj F p ((single‚ÇÄ I).obj X) Y ‚âÖ Y :=
  isoMk _ _ (fun j => (CofanMapObjFun.iso
    (mapBifunctorLeftUnitorCofanIsColimit F X e p hp Y j)).symm)

@[reassoc (attr := simp)]
lemma Œπ_mapBifunctorLeftUnitor_hom (j : J) :
    ŒπMapBifunctorMapObj F p ((single‚ÇÄ I).obj X) Y 0 j j (hp j) ‚â´
      (mapBifunctorLeftUnitor F X e p hp Y).hom j =
      (F.map (singleObjApplyIso (0 : I) X).hom).app _ ‚â´ e.hom.app (Y j) := by
  dsimp [mapBifunctorLeftUnitor]
  erw [CofanMapObjFun.ŒπMapObj_iso_inv]
  rw [mapBifunctorLeftUnitorCofan_inj]

lemma mapBifunctorLeftUnitor_inv_apply (j : J) :
    (mapBifunctorLeftUnitor F X e p hp Y).inv j =
      e.inv.app (Y j) ‚â´ (F.map (singleObjApplyIso (0 : I) X).inv).app (Y j) ‚â´
      ŒπMapBifunctorMapObj F p ((single‚ÇÄ I).obj X) Y 0 j j (hp j) := rfl

variable {Y Y'}

@[reassoc]
lemma mapBifunctorLeftUnitor_inv_naturality :
    œÜ ‚â´ (mapBifunctorLeftUnitor F X e p hp Y').inv =
      (mapBifunctorLeftUnitor F X e p hp Y).inv ‚â´ mapBifunctorMapMap F p (ùüô _) œÜ := by
  ext j
  dsimp
  rw [mapBifunctorLeftUnitor_inv_apply, mapBifunctorLeftUnitor_inv_apply, assoc, assoc,
    Œπ_mapBifunctorMapMap]
  dsimp
  rw [Functor.map_id, NatTrans.id_app, id_comp]
  erw [‚Üê NatTrans.naturality_assoc, ‚Üê NatTrans.naturality_assoc]
  rfl

@[reassoc]
lemma mapBifunctorLeftUnitor_naturality :
    mapBifunctorMapMap F p (ùüô _) œÜ ‚â´ (mapBifunctorLeftUnitor F X e p hp Y').hom =
      (mapBifunctorLeftUnitor F X e p hp Y).hom ‚â´ œÜ := by
  rw [‚Üê cancel_mono (mapBifunctorLeftUnitor F X e p hp Y').inv, assoc, assoc, Iso.hom_inv_id,
    comp_id, mapBifunctorLeftUnitor_inv_naturality, Iso.hom_inv_id_assoc]

end LeftUnitor

end GradedObject

end CategoryTheory
