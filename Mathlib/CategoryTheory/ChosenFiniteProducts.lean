/-
Copyright (c) 2024 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz, Robin Carlier
-/
import Mathlib.CategoryTheory.Limits.Constructions.FiniteProductsOfBinaryProducts
import Mathlib.CategoryTheory.Limits.Preserves.Finite
import Mathlib.CategoryTheory.Monoidal.OfChosenFiniteProducts.Symmetric

/-!
# Categories with chosen finite products

We introduce a class, `ChosenFiniteProducts`, which bundles explicit choices
for a terminal object and binary products in a category `C`.
This is primarily useful for categories which have finite products with good
definitional properties, such as the category of types.

Given a category with such an instance, we also provide the associated
symmetric monoidal structure so that one can write `X âŠ— Y` for the explicit
binary product and `ğŸ™_ C` for the explicit terminal object.

# Projects

- Construct an instance of chosen finite products in the category of affine scheme, using
  the tensor product.
- Construct chosen finite products in other categories appearing "in nature".

-/

namespace CategoryTheory

universe v vâ‚ vâ‚‚ u uâ‚ uâ‚‚

/--
An instance of `ChosenFiniteProducts C` bundles an explicit choice of a binary
product of two objects of `C`, and a terminal object in `C`.

Users should use the monoidal notation: `X âŠ— Y` for the product and `ğŸ™_ C` for
the terminal object.
-/
class ChosenFiniteProducts (C : Type u) [Category.{v} C] where
  /-- A choice of a limit binary fan for any two objects of the category. -/
  product : (X Y : C) â†’ Limits.LimitCone (Limits.pair X Y)
  /-- A choice of a terminal object. -/
  terminal : Limits.LimitCone (Functor.empty.{0} C)

/--
An instance of `ChosenFiniteCoproducts C` bundles an explicit choice of a binary
product of two objects of `C`, and a terminal object in `C`.

Users should use the monoidal notation: `X âŠ•â‚’ Y` for the product and `ğŸ˜_ C` for
the initial object.
-/
class ChosenFiniteCoproducts (C : Type u) [Category.{v} C] where
  /-- A choice of a colimit binary cofan for any two objects of the category. -/
  coproduct : (X Y : C) â†’ Limits.ColimitCocone (Limits.pair X Y)
  /-- A choice of an initial object. -/
  initial : Limits.ColimitCocone (Functor.empty.{0} C)

namespace ChosenFiniteProducts

instance (priority := 100) (C : Type u) [Category.{v} C] [ChosenFiniteProducts C] :
    MonoidalCategory C :=
  monoidalOfChosenFiniteProducts terminal product

instance (priority := 100) (C : Type u) [Category.{v} C] [ChosenFiniteProducts C] :
    SymmetricCategory C :=
  symmetricOfChosenFiniteProducts _ _

variable {C : Type u} [Category.{v} C] [ChosenFiniteProducts C]

open MonoidalCategory

theorem braiding_eq_braiding (X Y : C) :
  (Î²_ X Y) = Limits.BinaryFan.braiding (product X Y).isLimit (product Y X).isLimit := rfl

/--
The unique map to the terminal object.
-/
def toUnit (X : C) : X âŸ¶ ğŸ™_ C :=
  terminal.isLimit.lift <| .mk _ <| .mk (fun x => x.as.elim) fun x => x.as.elim

instance (X : C) : Unique (X âŸ¶ ğŸ™_ C) where
  default := toUnit _
  uniq _ := terminal.isLimit.hom_ext fun âŸ¨jâŸ© => j.elim

/--
This lemma follows from the preexisting `Unique` instance, but
it is often convenient to use it directly as `apply toUnit_unique` forcing
lean to do the necessary elaboration.
-/
lemma toUnit_unique {X : C} (f g : X âŸ¶ ğŸ™_ _) : f = g :=
  Subsingleton.elim _ _

@[reassoc (attr := simp)]
theorem comp_toUnit {X Y : C} (f : X âŸ¶ Y) : f â‰« toUnit Y = toUnit X :=
  toUnit_unique _ _

/--
Construct a morphism to the product given its two components.
-/
def lift {T X Y : C} (f : T âŸ¶ X) (g : T âŸ¶ Y) : T âŸ¶ X âŠ— Y :=
  (product X Y).isLimit.lift <| Limits.BinaryFan.mk f g

/--
The first projection from the product.
-/
def fst (X Y : C) : X âŠ— Y âŸ¶ X :=
  letI F : Limits.BinaryFan X Y := (product X Y).cone
  F.fst

/--
The second projection from the product.
-/
def snd (X Y : C) : X âŠ— Y âŸ¶ Y :=
  letI F : Limits.BinaryFan X Y := (product X Y).cone
  F.snd

@[reassoc (attr := simp)]
lemma lift_fst {T X Y : C} (f : T âŸ¶ X) (g : T âŸ¶ Y) : lift f g â‰« fst _ _ = f := by
  simp [lift, fst]

@[reassoc (attr := simp)]
lemma lift_snd {T X Y : C} (f : T âŸ¶ X) (g : T âŸ¶ Y) : lift f g â‰« snd _ _ = g := by
  simp [lift, snd]

instance mono_lift_of_mono_left {W X Y : C} (f : W âŸ¶ X) (g : W âŸ¶ Y)
    [Mono f] : Mono (lift f g) :=
  mono_of_mono_fac <| lift_fst _ _

instance mono_lift_of_mono_right {W X Y : C} (f : W âŸ¶ X) (g : W âŸ¶ Y)
    [Mono g] : Mono (lift f g) :=
  mono_of_mono_fac <| lift_snd _ _

@[ext 1050]
lemma hom_ext {T X Y : C} (f g : T âŸ¶ X âŠ— Y)
    (h_fst : f â‰« fst _ _ = g â‰« fst _ _)
    (h_snd : f â‰« snd _ _ = g â‰« snd _ _) :
    f = g :=
  (product X Y).isLimit.hom_ext fun âŸ¨jâŸ© => j.recOn h_fst h_snd

-- Similarly to `CategoryTheory.Limits.prod.comp_lift`, we do not make the `assoc` version a simp
-- lemma
@[reassoc, simp]
lemma comp_lift {V W X Y : C} (f : V âŸ¶ W) (g : W âŸ¶ X) (h : W âŸ¶ Y) :
    f â‰« lift g h = lift (f â‰« g) (f â‰« h) := by ext <;> simp

@[simp]
lemma lift_fst_snd {X Y : C} : lift (fst X Y) (snd X Y) = ğŸ™ (X âŠ— Y) := by ext <;> simp

@[simp]
lemma lift_comp_fst_snd {X Y Z : C} (f : X âŸ¶ Y âŠ— Z) :
    lift (f â‰« fst _ _) (f â‰« snd _ _) = f := by
  aesop_cat

@[reassoc (attr := simp)]
lemma tensorHom_fst {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : C} (f : Xâ‚ âŸ¶ Xâ‚‚) (g : Yâ‚ âŸ¶ Yâ‚‚) :
    (f âŠ— g) â‰« fst _ _ = fst _ _ â‰« f := lift_fst _ _

@[reassoc (attr := simp)]
lemma tensorHom_snd {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : C} (f : Xâ‚ âŸ¶ Xâ‚‚) (g : Yâ‚ âŸ¶ Yâ‚‚) :
    (f âŠ— g) â‰« snd _ _ = snd _ _ â‰« g := lift_snd _ _

@[reassoc]
lemma lift_map {V W X Y Z : C} (f : V âŸ¶ W) (g : V âŸ¶ X) (h : W âŸ¶ Y) (k : X âŸ¶ Z) :
    lift f g â‰« (h âŠ— k) = lift (f â‰« h) (g â‰« k) := by ext <;> simp

@[deprecated lift_map (since := "2025-03-04"), reassoc (attr := simp)]
lemma lift_tensorHom {V W X Y Z : C} (f : V âŸ¶ W) (g : V âŸ¶ X) (h : W âŸ¶ Y) (k : X âŸ¶ Z) :
    lift f g â‰« (h âŠ— k) = lift (f â‰« h) (g â‰« k) := by ext <;> simp

@[simp]
lemma lift_fst_comp_snd_comp {W X Y Z : C} (g : W âŸ¶ X) (g' : Y âŸ¶ Z) :
    lift (fst _ _ â‰« g) (snd _ _ â‰« g') = g âŠ— g' := by ext <;> simp

@[reassoc (attr := simp)]
lemma whiskerLeft_fst (X : C) {Yâ‚ Yâ‚‚ : C} (g : Yâ‚ âŸ¶ Yâ‚‚) :
    (X â— g) â‰« fst _ _ = fst _ _ :=
  (tensorHom_fst _ _).trans (by simp)

@[reassoc (attr := simp)]
lemma whiskerLeft_snd (X : C) {Yâ‚ Yâ‚‚ : C} (g : Yâ‚ âŸ¶ Yâ‚‚) :
    (X â— g) â‰« snd _ _ = snd _ _ â‰« g :=
  tensorHom_snd _ _

@[reassoc (attr := simp)]
lemma whiskerRight_fst {Xâ‚ Xâ‚‚ : C} (f : Xâ‚ âŸ¶ Xâ‚‚) (Y : C) :
    (f â–· Y) â‰« fst _ _ = fst _ _ â‰« f :=
  tensorHom_fst _ _

@[reassoc (attr := simp)]
lemma whiskerRight_snd {Xâ‚ Xâ‚‚ : C} (f : Xâ‚ âŸ¶ Xâ‚‚) (Y : C) :
    (f â–· Y) â‰« snd _ _ = snd _ _ :=
  (tensorHom_snd _ _).trans (by simp)

@[reassoc (attr := simp)]
lemma lift_whiskerRight {X Y Z W : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) (h : Y âŸ¶ W) :
    lift f g â‰« (h â–· Z) = lift (f â‰« h) g := by
  aesop_cat

@[reassoc (attr := simp)]
lemma lift_whiskerLeft {X Y Z W : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) (h : Z âŸ¶ W) :
    lift f g â‰« (Y â— h) = lift f (g â‰« h) := by
  aesop_cat

@[reassoc (attr := simp)]
lemma associator_hom_fst (X Y Z : C) :
    (Î±_ X Y Z).hom â‰« fst _ _ = fst _ _ â‰« fst _ _ := lift_fst _ _

@[reassoc (attr := simp)]
lemma associator_hom_snd_fst (X Y Z : C) :
    (Î±_ X Y Z).hom â‰« snd _ _ â‰« fst _ _ = fst _ _ â‰« snd _ _  := by
  erw [lift_snd_assoc]
  erw [lift_fst]
  rfl

@[reassoc (attr := simp)]
lemma associator_hom_snd_snd (X Y Z : C) :
    (Î±_ X Y Z).hom â‰« snd _ _ â‰« snd _ _ = snd _ _  := by
  erw [lift_snd_assoc]
  erw [lift_snd]
  rfl

@[reassoc (attr := simp)]
lemma associator_inv_fst (X Y Z : C) :
    (Î±_ X Y Z).inv â‰« fst _ _ â‰« fst _ _ = fst _ _ := by
  erw [lift_fst_assoc]
  erw [lift_fst]
  rfl

@[reassoc (attr := simp)]
lemma associator_inv_fst_snd (X Y Z : C) :
    (Î±_ X Y Z).inv â‰« fst _ _ â‰« snd _ _ = snd _ _ â‰« fst _ _ := by
  erw [lift_fst_assoc]
  erw [lift_snd]
  rfl

@[reassoc (attr := simp)]
lemma associator_inv_snd (X Y Z : C) :
    (Î±_ X Y Z).inv â‰« snd _ _ = snd _ _ â‰« snd _ _ := lift_snd _ _

@[reassoc (attr := simp)]
lemma lift_lift_associator_hom {X Y Z W : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) (h : X âŸ¶ W) :
    lift (lift f g) h â‰« (Î±_ Y Z W).hom = lift f (lift g h) := by
  aesop_cat

@[reassoc (attr := simp)]
lemma lift_lift_associator_inv {X Y Z W : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) (h : X âŸ¶ W) :
    lift f (lift g h) â‰« (Î±_ Y Z W).inv = lift (lift f g) h := by
  aesop_cat

@[reassoc (attr := simp)]
lemma leftUnitor_inv_fst (X : C) :
    (Î»_ X).inv â‰« fst _ _ = toUnit _ := toUnit_unique _ _

@[reassoc (attr := simp)]
lemma leftUnitor_inv_snd (X : C) :
    (Î»_ X).inv â‰« snd _ _ = ğŸ™ X := lift_snd _ _

@[reassoc (attr := simp)]
lemma rightUnitor_inv_fst (X : C) :
    (Ï_ X).inv â‰« fst _ _ = ğŸ™ X := lift_fst _ _

@[reassoc (attr := simp)]
lemma rightUnitor_inv_snd (X : C) :
    (Ï_ X).inv â‰« snd _ _ = toUnit _ := toUnit_unique _ _

@[reassoc (attr := simp)]
lemma lift_leftUnitor_hom {X Y : C} (f : X âŸ¶ ğŸ™_ C) (g : X âŸ¶ Y) :
    lift f g â‰« (Î»_ Y).hom = g := by
  rw [â† Iso.eq_comp_inv]
  aesop_cat

@[reassoc (attr := simp)]
lemma lift_rightUnitor_hom {X Y : C} (f : X âŸ¶ Y) (g : X âŸ¶ ğŸ™_ C) :
    lift f g â‰« (Ï_ Y).hom = f := by
  rw [â† Iso.eq_comp_inv]
  aesop_cat

@[reassoc (attr := simp)]
theorem braiding_hom_fst {X Y : C} : (Î²_ X Y).hom â‰« fst _ _ = snd _ _ := by
  simp [braiding_eq_braiding, fst, snd]

@[reassoc (attr := simp)]
theorem braiding_hom_snd {X Y : C} : (Î²_ X Y).hom â‰« snd _ _ = fst _ _ := by
  simp [braiding_eq_braiding, fst, snd]

@[reassoc (attr := simp)]
theorem braiding_inv_fst {X Y : C} : (Î²_ X Y).inv â‰« fst _ _ = snd _ _ := by
  simp [braiding_eq_braiding, fst, snd]

@[reassoc (attr := simp)]
theorem braiding_inv_snd {X Y : C} : (Î²_ X Y).inv â‰« snd _ _ = fst _ _ := by
  simp [braiding_eq_braiding, fst, snd]

theorem lift_snd_fst {X Y : C} : lift (snd X Y) (fst X Y) = (Î²_ X Y).hom := rfl

@[simp, reassoc]
lemma lift_snd_comp_fst_comp {W X Y Z : C} (g : W âŸ¶ X) (g' : Y âŸ¶ Z) :
    lift (snd _ _ â‰« g') (fst _ _ â‰« g) = (Î²_ _ _).hom â‰« (g' âŠ— g) := by ext <;> simp

@[reassoc (attr := simp)]
lemma lift_braiding_hom {T X Y : C} (f : T âŸ¶ X) (g : T âŸ¶ Y) :
    lift f g â‰« (Î²_ X Y).hom = lift g f := by aesop

@[reassoc (attr := simp)]
lemma lift_braiding_inv {T X Y : C} (f : T âŸ¶ X) (g : T âŸ¶ Y) :
    lift f g â‰« (Î²_ Y X).inv = lift g f := by aesop

/--
Construct an instance of `ChosenFiniteProducts C` given an instance of `HasFiniteProducts C`.
-/
noncomputable
def ofFiniteProducts
    (C : Type u) [Category.{v} C] [Limits.HasFiniteProducts C] :
    ChosenFiniteProducts C where
  product X Y := Limits.getLimitCone (Limits.pair X Y)
  terminal := Limits.getLimitCone (Functor.empty C)

instance (priority := 100) : Limits.HasFiniteProducts C :=
  letI : âˆ€ (X Y : C), Limits.HasLimit (Limits.pair X Y) := fun _ _ =>
    .mk <| ChosenFiniteProducts.product _ _
  letI : Limits.HasBinaryProducts C := Limits.hasBinaryProducts_of_hasLimit_pair _
  letI : Limits.HasTerminal C := Limits.hasTerminal_of_unique (ğŸ™_ C)
  hasFiniteProducts_of_has_binary_and_terminal

section ChosenFiniteProductsComparison

variable {D : Type uâ‚} [Category.{vâ‚} D] [ChosenFiniteProducts D] (F : C â¥¤ D)

section terminalComparison

/-- When `C` and `D` have chosen finite products and `F : C â¥¤ D` is any functor,
`terminalComparison F` is the unique map `F (ğŸ™_ C) âŸ¶ ğŸ™_ D`. -/
abbrev terminalComparison : F.obj (ğŸ™_ C) âŸ¶ ğŸ™_ D := toUnit _

@[reassoc]
lemma map_toUnit_comp_terminalCompariso (A : C) :
    F.map (toUnit A) â‰« terminalComparison F = toUnit _ := toUnit_unique _ _

open Limits

/-- If `terminalComparison F` is an Iso, then `F` preserves terminal objects. -/
lemma preservesLimit_empty_of_isIso_terminalComparison [IsIso (terminalComparison F)] :
    PreservesLimit (Functor.empty.{0} C) F := by
  apply preservesLimit_of_preserves_limit_cone terminal.isLimit
  apply isLimitChangeEmptyCone D terminal.isLimit
  exact asIso (terminalComparison F)|>.symm

/-- If `F` preserves terminal objects, then `terminalComparison F` is an isomorphism. -/
noncomputable def preservesTerminalIso [h : PreservesLimit (Functor.empty.{0} C) F] :
    F.obj (ğŸ™_ C) â‰… ğŸ™_ D :=
  (isLimitChangeEmptyCone D (isLimitOfPreserves _ terminal.isLimit) (asEmptyCone (F.obj (ğŸ™_ C)))
    (Iso.refl _)).conePointUniqueUpToIso terminal.isLimit

@[simp]
lemma preservesTerminalIso_hom [PreservesLimit (Functor.empty.{0} C) F] :
    (preservesTerminalIso F).hom = terminalComparison F := toUnit_unique _ _

instance terminalComparison_isIso_of_preservesLimits [PreservesLimit (Functor.empty.{0} C) F] :
    IsIso (terminalComparison F) := by
  rw [â† preservesTerminalIso_hom]
  infer_instance

end terminalComparison

section prodComparison

variable (A B : C)

/-- When `C` and `D` have chosen finite products and `F : C â¥¤ D` is any functor,
`prodComparison F A B` is the canonical comparison morphism from `F (A âŠ— B)` to `F(A) âŠ— F(B)`. -/
def prodComparison (A B : C) : F.obj (A âŠ— B) âŸ¶ F.obj A âŠ— F.obj B :=
  lift (F.map (fst A B)) (F.map (snd A B))

@[reassoc (attr := simp)]
theorem prodComparison_fst : prodComparison F A B â‰« fst _ _ = F.map (fst A B) :=
  lift_fst _ _

@[reassoc (attr := simp)]
theorem prodComparison_snd : prodComparison F A B â‰« snd _ _ = F.map (snd A B) :=
  lift_snd _ _

@[reassoc (attr := simp)]
theorem inv_prodComparison_map_fst [IsIso (prodComparison F A B)] :
    inv (prodComparison F A B) â‰« F.map (fst _ _) = fst _ _ := by simp [IsIso.inv_comp_eq]

@[reassoc (attr := simp)]
theorem inv_prodComparison_map_snd [IsIso (prodComparison F A B)] :
    inv (prodComparison F A B) â‰« F.map (snd _ _) = snd _ _ := by simp [IsIso.inv_comp_eq]

variable {A B} {A' B' : C}

/-- Naturality of the `prodComparison` morphism in both arguments. -/
@[reassoc]
theorem prodComparison_natural (f : A âŸ¶ A') (g : B âŸ¶ B') :
    F.map (f âŠ— g) â‰« prodComparison F A' B' =
      prodComparison F A B â‰« (F.map f âŠ— F.map g) := by
  apply hom_ext <;>
  simp only [Category.assoc, prodComparison_fst, tensorHom_fst, prodComparison_fst_assoc,
    prodComparison_snd, tensorHom_snd, prodComparison_snd_assoc, â† F.map_comp]

/-- Naturality of the `prodComparison` morphism in the right argument. -/
@[reassoc]
theorem prodComparison_natural_whiskerLeft (g : B âŸ¶ B') :
    F.map (A â— g) â‰« prodComparison F A B' =
      prodComparison F A B â‰« (F.obj A â— F.map g) := by
  rw [â† id_tensorHom, prodComparison_natural, Functor.map_id]
  rfl

/-- Naturality of the `prodComparison` morphism in the left argument. -/
@[reassoc]
theorem prodComparison_natural_whiskerRight (f : A âŸ¶ A') :
    F.map (f â–· B) â‰« prodComparison F A' B =
      prodComparison F A B â‰« (F.map f â–· F.obj B) := by
  rw [â† tensorHom_id, prodComparison_natural, Functor.map_id]
  rfl

section
variable [IsIso (prodComparison F A B)]

/-- If the product comparison morphism is an iso, its inverse is natural in both arguments. -/
@[reassoc]
theorem prodComparison_inv_natural (f : A âŸ¶ A') (g : B âŸ¶ B') [IsIso (prodComparison F A' B')] :
    inv (prodComparison F A B) â‰« F.map (f âŠ— g) =
      (F.map f âŠ— F.map g) â‰« inv (prodComparison F A' B') := by
  rw [IsIso.eq_comp_inv, Category.assoc, IsIso.inv_comp_eq, prodComparison_natural]

/-- If the product comparison morphism is an iso, its inverse is natural in the right argument. -/
@[reassoc]
theorem prodComparison_inv_natural_whiskerLeft (g : B âŸ¶ B') [IsIso (prodComparison F A B')] :
    inv (prodComparison F A B) â‰« F.map (A â— g) =
      (F.obj A â— F.map g) â‰« inv (prodComparison F A B') := by
  rw [IsIso.eq_comp_inv, Category.assoc, IsIso.inv_comp_eq, prodComparison_natural_whiskerLeft]

/-- If the product comparison morphism is an iso, its inverse is natural in the left argument. -/
@[reassoc]
theorem prodComparison_inv_natural_whiskerRight (f : A âŸ¶ A') [IsIso (prodComparison F A' B)] :
    inv (prodComparison F A B) â‰« F.map (f â–· B) =
      (F.map f â–· F.obj B) â‰« inv (prodComparison F A' B) := by
  rw [IsIso.eq_comp_inv, Category.assoc, IsIso.inv_comp_eq, prodComparison_natural_whiskerRight]

end

theorem prodComparison_comp {E : Type uâ‚‚} [Category.{vâ‚‚} E] [ChosenFiniteProducts E] (G : D â¥¤ E) :
    prodComparison (F â‹™ G) A B =
      G.map (prodComparison F A B) â‰« prodComparison G (F.obj A) (F.obj B) := by
  unfold prodComparison
  ext <;> simp [â† G.map_comp]

@[simp]
lemma prodComparison_id :
    prodComparison (ğŸ­ C) A B = ğŸ™ (A âŠ— B) := lift_fst_snd

/-- The product comparison morphism from `F(A âŠ— -)` to `FA âŠ— F-`, whose components are given by
`prodComparison`. -/
@[simps]
def prodComparisonNatTrans (A : C) :
    (curriedTensor C).obj A â‹™ F âŸ¶ F â‹™ (curriedTensor D).obj (F.obj A) where
  app B := prodComparison F A B
  naturality x y f := by
    apply hom_ext <;>
    simp only [Functor.comp_obj, curriedTensor_obj_obj,
      Functor.comp_map, curriedTensor_obj_map, Category.assoc, prodComparison_fst, whiskerLeft_fst,
      prodComparison_snd, prodComparison_snd_assoc, whiskerLeft_snd, â† F.map_comp]

theorem prodComparisonNatTrans_comp {E : Type uâ‚‚} [Category.{vâ‚‚} E] [ChosenFiniteProducts E]
    (G : D â¥¤ E) : prodComparisonNatTrans (F â‹™ G) A =
      whiskerRight (prodComparisonNatTrans F A) G â‰«
        whiskerLeft F (prodComparisonNatTrans G (F.obj A)) := by ext; simp [prodComparison_comp]

@[simp]
lemma prodComparisonNatTrans_id :
    prodComparisonNatTrans (ğŸ­ C) A = ğŸ™ _ := by ext; simp

/-- The product comparison morphism from `F(- âŠ— -)` to `F- âŠ— F-`, whose components are given by
`prodComparison`. -/
@[simps]
def prodComparisonBifunctorNatTrans :
    curriedTensor C â‹™ (whiskeringRight _ _ _).obj F âŸ¶
      F â‹™ curriedTensor D â‹™ (whiskeringLeft _ _ _).obj F where
  app A := prodComparisonNatTrans F A
  naturality x y f := by
    ext z
    apply hom_ext <;> simp [â† Functor.map_comp]

variable {E : Type uâ‚‚} [Category.{vâ‚‚} E]
    [ChosenFiniteProducts E] (G : D â¥¤ E)

theorem prodComparisonBifunctorNatTrans_comp {E : Type uâ‚‚} [Category.{vâ‚‚} E]
    [ChosenFiniteProducts E] (G : D â¥¤ E) : prodComparisonBifunctorNatTrans (F â‹™ G) =
      whiskerRight (prodComparisonBifunctorNatTrans F) ((whiskeringRight _ _ _).obj G) â‰«
        whiskerLeft F (whiskerRight (prodComparisonBifunctorNatTrans G)
          ((whiskeringLeft _ _ _).obj F)) := by ext; simp [prodComparison_comp]

instance (A : C) [âˆ€ B, IsIso (prodComparison F A B)] : IsIso (prodComparisonNatTrans F A) := by
  letI : âˆ€ X, IsIso ((prodComparisonNatTrans F A).app X) := by assumption
  apply NatIso.isIso_of_isIso_app

instance [âˆ€ A B, IsIso (prodComparison F A B)] : IsIso (prodComparisonBifunctorNatTrans F) := by
  letI : âˆ€ X, IsIso ((prodComparisonBifunctorNatTrans F).app X) :=
    fun _ â†¦ by dsimp; apply NatIso.isIso_of_isIso_app
  apply NatIso.isIso_of_isIso_app

open Limits
section PreservesLimitPairs

section
variable (A B)
variable [PreservesLimit (pair A B) F]

/-- If `F` preserves the limit of the pair `(A, B)`, then the binary fan given by
`(F.map fst A B, F.map (snd A B))` is a limit cone. -/
noncomputable def isLimitChosenFiniteProductsOfPreservesLimits :
    IsLimit <| BinaryFan.mk (F.map (fst A B)) (F.map (snd A B)) :=
  mapIsLimitOfPreservesOfIsLimit F (fst _ _) (snd _ _) <|
    (product A B).isLimit.ofIsoLimit <| isoBinaryFanMk (product A B).cone

/-- If `F` preserves the limit of the pair `(A, B)`, then `prodComparison F A B` is an isomorphism.
-/
noncomputable def prodComparisonIso : F.obj (A âŠ— B) â‰… F.obj A âŠ— F.obj B :=
  IsLimit.conePointUniqueUpToIso (isLimitChosenFiniteProductsOfPreservesLimits F A B)
    (product _ _).isLimit

@[simp]
lemma prodComparisonIso_hom : (prodComparisonIso F A B).hom = prodComparison F A B := by
  rfl

instance isIso_prodComparison_of_preservesLimit_pair : IsIso (prodComparison F A B) := by
  rw [â† prodComparisonIso_hom]
  infer_instance

end

/-- The natural isomorphism `F(A âŠ— -) â‰… FA âŠ— F-`, provided each `prodComparison F A B` is an
isomorphism (as `B` changes). -/
@[simps! hom inv]
noncomputable def prodComparisonNatIso (A : C) [âˆ€ B, PreservesLimit (pair A B) F] :
    (curriedTensor C).obj A â‹™ F â‰… F â‹™ (curriedTensor D).obj (F.obj A) :=
  asIso (prodComparisonNatTrans F A)

/-- The natural isomorphism of bifunctors `F(- âŠ— -) â‰… F- âŠ— F-`, provided each
`prodComparison F A B` is an isomorphism. -/
@[simps! hom inv]
noncomputable def prodComparisonBifunctorNatIso [âˆ€ A B, PreservesLimit (pair A B) F] :
    curriedTensor C â‹™ (whiskeringRight _ _ _).obj F â‰…
      F â‹™ curriedTensor D â‹™ (whiskeringLeft _ _ _).obj F :=
  asIso (prodComparisonBifunctorNatTrans F)

end PreservesLimitPairs

section ProdComparisonIso

/-- If `prodComparison F A B` is an isomorphism, then `F` preserves the limit of `pair A B`. -/
lemma preservesLimit_pair_of_isIso_prodComparison (A B : C)
    [IsIso (prodComparison F A B)] :
    PreservesLimit (pair A B) F := by
 apply preservesLimit_of_preserves_limit_cone (product A B).isLimit
 refine IsLimit.equivOfNatIsoOfIso (pairComp A B F) _
    ((product (F.obj A) (F.obj B)).cone.extend (prodComparison F A B))
      (BinaryFan.ext (by exact Iso.refl _) ?_ ?_) |>.invFun
      (IsLimit.extendIso _ (product (F.obj A) (F.obj B)).isLimit)
 Â· dsimp only [BinaryFan.fst]
   simp [pairComp, prodComparison, lift, fst]
 Â· dsimp only [BinaryFan.snd]
   simp [pairComp, prodComparison, lift, snd]

/-- If `prodComparison F A B` is an isomorphism for all `A B` then `F` preserves limits of shape
`Discrete (WalkingPair)`. -/
lemma preservesLimitsOfShape_discrete_walkingPair_of_isIso_prodComparison
    [âˆ€ A B, IsIso (prodComparison F A B)] : PreservesLimitsOfShape (Discrete WalkingPair) F := by
  constructor
  intro K
  refine @preservesLimit_of_iso_diagram _ _ _ _ _ _ _ _ _ (diagramIsoPair K).symm ?_
  apply preservesLimit_pair_of_isIso_prodComparison

end ProdComparisonIso

end prodComparison

end ChosenFiniteProductsComparison

end ChosenFiniteProducts

namespace ChosenFiniteCoproducts

/-- The chosen initial object -/
abbrev initialObj (C : Type u) [Category.{v} C] [ChosenFiniteCoproducts C] : C := initial.cocone.pt

-- TODO: if `AddMonoidalCategory` is added, switch to use notation from there, analogously to how
-- `ChosenFiniteProducts` uses notation from `MonoidalCategory`

/-- Notation for the chosen initial object -/
scoped notation "ğŸ˜_" => initialObj

variable {C : Type u} [Category.{v} C] [ChosenFiniteCoproducts C]

/-- The chosen coproduct of two objects -/
abbrev coprodObj (X Y : C) : C := (coproduct X Y).cocone.pt

/-- Notation for the chosen coproduct of two objects -/
scoped infixr:70 " âŠ•â‚’ " => coprodObj

/--
The unique map from the initial object.
-/
def fromZero (X : C) : ğŸ˜_ C âŸ¶ X :=
  initial.isColimit.desc <| .mk _ <| .mk (fun x => x.as.elim) fun x => x.as.elim

instance (X : C) : Unique (ğŸ˜_ C âŸ¶ X) where
  default := fromZero _
  uniq _ := initial.isColimit.hom_ext fun âŸ¨jâŸ© => j.elim

/--
This lemma follows from the preexisting `Unique` instance, but
it is often convenient to use it directly as `apply fromZero_unique` forcing
lean to do the necessary elaboration.
-/
lemma fromZero_unique {X : C} (f g : ğŸ˜_ _ âŸ¶ X) : f = g :=
  Subsingleton.elim _ _

/--
Construct a morphism from the coproduct given its two components.
-/
def desc {T X Y : C} (f : X âŸ¶ T) (g : Y âŸ¶ T) : X âŠ•â‚’ Y âŸ¶ T :=
  (coproduct X Y).isColimit.desc <| Limits.BinaryCofan.mk f g

/--
The first injection into the coproduct
-/
def inl (X Y : C) : X âŸ¶ X âŠ•â‚’ Y :=
  letI F : Limits.BinaryCofan X Y := (coproduct X Y).cocone
  F.inl

/--
The second injection into the coproduct
-/
def inr (X Y : C) : Y âŸ¶ X âŠ•â‚’ Y :=
  letI F : Limits.BinaryCofan X Y := (coproduct X Y).cocone
  F.inr

@[reassoc (attr := simp)]
lemma inl_desc {T X Y : C} (f : X âŸ¶ T) (g : Y âŸ¶ T) : inl X Y â‰« desc f g = f := by
  simp [inl, desc]

@[simp, reassoc]
lemma inr_desc {T X Y : C} (f : X âŸ¶ T) (g : Y âŸ¶ T) : inr X Y â‰« desc f g = g := by
  simp [inr, desc]

instance epi_desc_of_epi_left {T X Y : C} (f : X âŸ¶ T) (g : Y âŸ¶ T)
    [Epi f] : Epi (desc f g) :=
  epi_of_epi_fac <| inl_desc _ _

instance epi_desc_of_epi_right {T X Y : C} (f : X âŸ¶ T) (g : Y âŸ¶ T)
    [Epi g] : Epi (desc f g) :=
  epi_of_epi_fac <| inr_desc _ _

@[ext 1050]
lemma hom_ext {T X Y : C} (f g : X âŠ•â‚’ Y âŸ¶ T)
    (h_inl : inl X Y â‰« f = inl X Y â‰« g)
    (h_inr : inr X Y â‰« f = inr X Y â‰« g) :
    f = g :=
  (coproduct X Y).isColimit.hom_ext fun âŸ¨jâŸ© => j.recOn h_inl h_inr

-- Similarly to `CategoryTheory.Limits.coprod.desc_comp`, we do not make the `assoc` version a simp
-- lemma
@[reassoc, simp]
lemma desc_comp {V W X Y : C} (f : V âŸ¶ W) (g : X âŸ¶ V) (h : Y âŸ¶ V) :
    desc g h â‰« f = desc (g â‰« f) (h â‰« f) := by ext <;> simp [inl_desc_assoc, inr_desc_assoc]

@[simp]
lemma desc_inl_inr {X Y : C} : desc (inl X Y) (inr X Y) = ğŸ™ (X âŠ•â‚’ Y) := by ext <;> simp

@[simp]
lemma desc_inl_comp_inr_comp {X Y Z : C} (f : Y âŠ•â‚’ Z âŸ¶ X) :
    desc (inl Y Z â‰« f) (inr Y Z â‰« f) = f := by ext <;> simp

-- TODO: if `AddMonoidalCategory` is added, switch to using `addHom` from there, analogously to how
-- `ChosenFiniteProducts` uses `tensorHom` from `MonoidalCategory`

/-- The coproduct of two morphisms -/
def addHom {X Y X' Y' : C} (f : X âŸ¶ Y) (g : X' âŸ¶ Y') : X âŠ•â‚’ X' âŸ¶ Y âŠ•â‚’ Y' :=
  desc (f â‰« inl Y Y') (g â‰« inr Y Y')

/-- Notation for the chosen coproduct of two morphisms -/
scoped infixr:70 " âŠ•â‚• " => addHom

@[reassoc]
lemma inl_addHom {X Y X' Y' : C} (f : X âŸ¶ Y) (g : X' âŸ¶ Y') :
    inl X X' â‰« (f âŠ•â‚• g) = f â‰« inl Y Y' := by simp [addHom]

@[reassoc]
lemma inr_addHom {X Y X' Y' : C} (f : X âŸ¶ Y) (g : X' âŸ¶ Y') :
    inr X X' â‰« (f âŠ•â‚• g) = g â‰« inr Y Y' := by simp [addHom]

@[reassoc]
lemma addHom_desc {S T U V W : C} (f : U âŸ¶ S) (g : W âŸ¶ S) (h : T âŸ¶ U) (k : V âŸ¶ W) :
    (h âŠ•â‚• k) â‰« desc f g = desc (h â‰« f) (k â‰« g) := by simp [addHom]

@[simp]
lemma desc_comp_inl_inr {W X Y Z : C} (g : X âŸ¶ W) (g' : Z âŸ¶ Y) :
    desc (g â‰« inl _ _) (g' â‰« inr _ _) = g âŠ•â‚• g' := rfl

/--
Construct an instance of `ChosenFiniteCoproducts C` given an instance of `HasFiniteCoproducts C`.
-/
noncomputable
def ofFiniteCoproducts
    (C : Type u) [Category.{v} C] [Limits.HasFiniteCoproducts C] :
    ChosenFiniteCoproducts C where
  coproduct X Y := Limits.getColimitCocone (Limits.pair X Y)
  initial := Limits.getColimitCocone (Functor.empty C)

instance (priority := 100) : Limits.HasFiniteCoproducts C :=
  letI : âˆ€ (X Y : C), Limits.HasColimit (Limits.pair X Y) := fun _ _ =>
    .mk <| ChosenFiniteCoproducts.coproduct _ _
  letI : Limits.HasBinaryCoproducts C := Limits.hasBinaryCoproducts_of_hasColimit_pair _
  letI : Limits.HasInitial C := Limits.hasInitial_of_unique (ğŸ˜_ C)
  hasFiniteCoproducts_of_has_binary_and_initial

section ChosenFiniteCoproductsComparison

variable {D : Type uâ‚} [Category.{vâ‚} D] [ChosenFiniteCoproducts D] (F : C â¥¤ D)

section initialComparison

/-- When `C` and `D` have chosen finite coproducts and `F : C â¥¤ D` is any functor,
`initialComparison F` is the unique map `ğŸ˜_ D âŸ¶ F (ğŸ˜_ C)`. -/
abbrev initialComparison : ğŸ˜_ D âŸ¶ F.obj (ğŸ˜_ C) := fromZero _

@[reassoc (attr := simp)]
lemma initialComparison_comp_map_fromZero (A : C) :
    initialComparison F â‰« F.map (fromZero A) = fromZero _ := fromZero_unique _ _

open Limits

/-- If `initialComparison F` is an Iso, then `F` preserves initial objects. -/
lemma preservesColimit_empty_of_isIso_initialComparison [IsIso (initialComparison F)] :
    PreservesColimit (Functor.empty.{0} C) F := by
  apply preservesColimit_of_preserves_colimit_cocone initial.isColimit
  apply isColimitChangeEmptyCocone D initial.isColimit
  exact asIso (initialComparison F)

/-- If `F` preserves initial objects, then `initialComparison F` is an isomorphism. -/
noncomputable def preservesInitialIso [h : PreservesColimit (Functor.empty.{0} C) F] :
    F.obj (ğŸ˜_ C) â‰… ğŸ˜_ D :=
  (isColimitChangeEmptyCocone D (isColimitOfPreserves _ initial.isColimit)
    (asEmptyCocone (F.obj (ğŸ˜_ C))) (Iso.refl _)).coconePointUniqueUpToIso initial.isColimit

@[simp]
lemma preservesInitialIso_inv [PreservesColimit (Functor.empty.{0} C) F] :
    (preservesInitialIso F).inv = initialComparison F := fromZero_unique _ _

instance initialComparison_isIso_of_preservesColimits [PreservesColimit (Functor.empty.{0} C) F] :
    IsIso (initialComparison F) := by
  rw [â† preservesInitialIso_inv]
  infer_instance

end initialComparison

section coprodComparison

variable (A B : C)

/-- When `C` and `D` have chosen finite products and `F : C â¥¤ D` is any functor,
`coprodComparison F A B` is the canonical comparison morphism from `F(A) âŠ•â‚’ F(B)` to `F (A âŠ•â‚’ B)`.
-/
def coprodComparison (A B : C) : F.obj A âŠ•â‚’ F.obj B âŸ¶ F.obj (A âŠ•â‚’ B) :=
  desc (F.map (inl A B)) (F.map (inr A B))

@[reassoc (attr := simp)]
theorem inl_coprodComparison : inl _ _ â‰« coprodComparison F A B = F.map (inl A B) :=
  inl_desc _ _

@[reassoc (attr := simp)]
theorem inr_coprodComparison : inr _ _ â‰« coprodComparison F A B = F.map (inr A B) :=
  inr_desc _ _

@[reassoc (attr := simp)]
theorem map_inl_inv_coprodComparison [IsIso (coprodComparison F A B)] :
    F.map (inl _ _) â‰« inv (coprodComparison F A B) = inl _ _ := by simp [IsIso.inv_comp_eq]

@[reassoc (attr := simp)]
theorem map_inr_inv_coprodComparison [IsIso (coprodComparison F A B)] :
    F.map (inr _ _) â‰« inv (coprodComparison F A B) = inr _ _ := by simp [IsIso.inv_comp_eq]

variable {A B} {A' B' : C}

/-- Naturality of the `coprodComparison` morphism in both arguments. -/
@[reassoc]
theorem coprodComparison_natural (f : A âŸ¶ A') (g : B âŸ¶ B') :
    coprodComparison F A B â‰« F.map (f âŠ•â‚• g) =
      (F.map f âŠ•â‚• F.map g) â‰« coprodComparison F A' B' := by
  apply hom_ext <;>
  simp only [Category.assoc, addHom, coprodComparison, inl_desc_assoc, inl_desc, inr_desc_assoc,
    inr_desc, â† F.map_comp]

section
variable [IsIso (coprodComparison F A B)]

/-- If the coproduct comparison morphism is an iso, its inverse is natural in both arguments. -/
@[reassoc]
theorem coprodComparison_inv_natural (f : A âŸ¶ A') (g : B âŸ¶ B') [IsIso (coprodComparison F A' B')] :
    inv (coprodComparison F A B) â‰« (F.map f âŠ•â‚• F.map g) =
      F.map (f âŠ•â‚• g) â‰« inv (coprodComparison F A' B') := by
  rw [IsIso.eq_comp_inv, Category.assoc, IsIso.inv_comp_eq, coprodComparison_natural]

end

theorem coprodComparison_comp {E : Type uâ‚‚} [Category.{vâ‚‚} E] [ChosenFiniteCoproducts E] (G : D â¥¤ E) :
    coprodComparison (F â‹™ G) A B =
      coprodComparison G (F.obj A) (F.obj B) â‰« G.map (coprodComparison F A B) := by
  unfold coprodComparison
  ext <;> simp [â† G.map_comp]

@[simp]
lemma coprodComparison_id :
    coprodComparison (ğŸ­ C) A B = ğŸ™ (A âŠ•â‚’ B) := desc_inl_inr

end coprodComparison

end ChosenFiniteCoproductsComparison

end ChosenFiniteCoproducts

open MonoidalCategory ChosenFiniteProducts

namespace Functor

variable {C : Type u} [Category.{v} C] [ChosenFiniteProducts C]
  {D : Type uâ‚} [Category.{vâ‚} D] [ChosenFiniteProducts D] (F : C â¥¤ D)

open Functor.OplaxMonoidal

/- The definitions `oplaxMonoidalOfChosenFiniteProducts` and
`monoidalOfChosenFiniteProducts` are not made instances because it would
create a diamond for the (oplax) monoidal structure on a composition
`F â‹™ G` of functors between categories with chosen finite products. -/

/-- Any functor between categories with chosen finite products induces an oplax monoial functor. -/
def oplaxMonoidalOfChosenFiniteProducts : F.OplaxMonoidal where
  Î·' := terminalComparison F
  Î´' X Y := prodComparison F X Y
  Î´'_natural_left f X' := by simpa using (prodComparison_natural F f (ğŸ™ X')).symm
  Î´'_natural_right X g := by simpa using (prodComparison_natural F (ğŸ™ X) g).symm
  oplax_associativity' _ _ _ := by
    apply hom_ext
    case' h_snd => apply hom_ext
    all_goals simp [â† Functor.map_comp]
  oplax_left_unitality' _ := by
    apply hom_ext
    Â· exact toUnit_unique _ _
    Â· simp only [leftUnitor_inv_snd, Category.assoc, whiskerRight_snd,
        prodComparison_snd, â† F.map_comp, F.map_id]
  oplax_right_unitality' _ := by
    apply hom_ext
    Â· simp only [rightUnitor_inv_fst, Category.assoc, whiskerLeft_fst,
        prodComparison_fst, â† F.map_comp, F.map_id]
    Â· exact toUnit_unique _ _


attribute [local instance] oplaxMonoidalOfChosenFiniteProducts

lemma Î·_of_chosenFiniteProducts : Î· F = terminalComparison F := rfl

lemma Î´_of_chosenFiniteProducts (X Y : C) : Î´ F X Y = prodComparison F X Y := rfl

open Limits

variable [PreservesFiniteProducts F]

instance : IsIso (Î· F) :=
  terminalComparison_isIso_of_preservesLimits F

instance (A B : C) : IsIso (Î´ F A B) :=
  isIso_prodComparison_of_preservesLimit_pair F A B

/-- If `F : C â¥¤ D` is a functor between categories with chosen finite products
that preserves finite products, then it is a monoidal functor. -/
noncomputable def monoidalOfChosenFiniteProducts : F.Monoidal :=
  Functor.Monoidal.ofOplaxMonoidal F

end Functor

namespace Functor.Monoidal

variable {C : Type u} [Category.{v} C] [ChosenFiniteProducts C]
  {D : Type uâ‚} [Category.{vâ‚} D] [ChosenFiniteProducts D] (F : C â¥¤ D)

section

attribute [local instance] oplaxMonoidalOfChosenFiniteProducts

@[reassoc (attr := simp)]
lemma Î´_fst (X Y : C) : OplaxMonoidal.Î´ F X Y â‰« fst _ _ = F.map (fst _ _) := by
  simp [Î´_of_chosenFiniteProducts]

@[reassoc (attr := simp)]
lemma Î´_snd (X Y : C) : OplaxMonoidal.Î´ F X Y â‰« snd _ _ = F.map (snd _ _) := by
  simp [Î´_of_chosenFiniteProducts]

@[reassoc (attr := simp)]
lemma lift_Î´ {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) :
    F.map (lift f g) â‰« OplaxMonoidal.Î´ F _ _ = lift (F.map f) (F.map g) := by
  apply hom_ext <;> simp [â† F.map_comp]

end

section

open Limits

variable [PreservesFiniteProducts F]

attribute [local instance] monoidalOfChosenFiniteProducts

@[reassoc (attr := simp)]
lemma toUnit_Îµ {X : C} : toUnit (F.obj X) â‰« LaxMonoidal.Îµ F = F.map (toUnit X) :=
  (cancel_mono (ÎµIso _).inv).1 (toUnit_unique _ _)

@[reassoc (attr := simp)]
lemma lift_Î¼ {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) :
    lift (F.map f) (F.map g) â‰« LaxMonoidal.Î¼ F _ _ = F.map (lift f g) :=
  (cancel_mono (Î¼Iso _ _ _).inv).1 (by simp)

@[reassoc (attr := simp)]
lemma Î¼_fst (X Y : C) : LaxMonoidal.Î¼ F X Y â‰« F.map (fst X Y) = fst (F.obj X) (F.obj Y) :=
  (cancel_epi (Î¼Iso _ _ _).inv).1 (by simp)

@[reassoc (attr := simp)]
lemma Î¼_snd (X Y : C) : LaxMonoidal.Î¼ F X Y â‰« F.map (snd X Y) = snd (F.obj X) (F.obj Y) :=
  (cancel_epi (Î¼Iso _ _ _).inv).1 (by simp)

section

variable {F} {E : Type uâ‚‚} [Category.{vâ‚‚} E] [ChosenFiniteProducts E] {G : D â¥¤ E}
  [PreservesFiniteProducts G]

attribute [-instance] Functor.LaxMonoidal.comp Functor.Monoidal.instComp in
@[reassoc]
lemma Î¼_comp (X Y : C) :
    LaxMonoidal.Î¼ (F â‹™ G) X Y = LaxMonoidal.Î¼ G _ _ â‰« G.map (LaxMonoidal.Î¼ F X Y) := by
  apply (cancel_mono (Î¼Iso _ _ _).inv).1
  apply ChosenFiniteProducts.hom_ext <;> simp [â† Functor.comp_obj, â† Functor.map_comp]

end

end

end Functor.Monoidal

namespace Functor

variable {C : Type u} [Category.{v} C] [ChosenFiniteProducts C]
  {D : Type uâ‚} [Category.{vâ‚} D] [ChosenFiniteProducts D] (F : C â¥¤ D)

attribute [local instance] monoidalOfChosenFiniteProducts

/-- A finite-product-preserving functor between categories with chosen finite products is
braided. -/
noncomputable def braidedOfChosenFiniteProducts [Limits.PreservesFiniteProducts F] : F.Braided :=
  { monoidalOfChosenFiniteProducts F with
    braided X Y := by
      rw [â† cancel_mono (Monoidal.Î¼Iso _ _ _).inv]
      apply ChosenFiniteProducts.hom_ext <;> simp [â† Functor.map_comp] }

end Functor

namespace NatTrans

variable {C : Type u} [Category.{v} C] [ChosenFiniteProducts C]
  {D : Type uâ‚} [Category.{vâ‚} D] [ChosenFiniteProducts D] (F G : C â¥¤ D)
  [Limits.PreservesFiniteProducts F] [Limits.PreservesFiniteProducts G]

attribute [local instance] Functor.monoidalOfChosenFiniteProducts in
theorem monoidal_of_preservesFiniteProducts (Î± : F âŸ¶ G) :
    NatTrans.IsMonoidal Î± where
  unit := (cancel_mono (Functor.Monoidal.ÎµIso _).inv).1 (toUnit_unique _ _)
  tensor {X Y} := by
    rw [â† cancel_mono (Functor.Monoidal.Î¼Iso _ _ _).inv]
    rw [â† cancel_epi (Functor.Monoidal.Î¼Iso _ _ _).inv]
    apply ChosenFiniteProducts.hom_ext <;> simp

end NatTrans

end CategoryTheory
