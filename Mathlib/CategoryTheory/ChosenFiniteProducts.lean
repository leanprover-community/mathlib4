/-
Copyright (c) 2024 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz, Robin Carlier
-/
import Mathlib.CategoryTheory.Limits.Constructions.FiniteProductsOfBinaryProducts
import Mathlib.CategoryTheory.Limits.Preserves.Finite
import Mathlib.CategoryTheory.Monoidal.OfChosenFiniteProducts.Symmetric

/-!
# Categories with chosen finite products

We introduce a class, `ChosenFiniteProducts`, which bundles explicit choices
for a terminal object and binary products in a category `C`.
This is primarily useful for categories which have finite products with good
definitional properties, such as the category of types.

Given a category with such an instance, we also provide the associated
symmetric monoidal structure so that one can write `X ‚äó Y` for the explicit
binary product and `ùüô_ C` for the explicit terminal object.

# Projects

- Construct an instance of chosen finite products in the category of affine scheme, using
  the tensor product.
- Construct chosen finite products in other categories appearing "in nature".

-/

namespace CategoryTheory

universe v v‚ÇÅ v‚ÇÇ u u‚ÇÅ u‚ÇÇ

/--
An instance of `ChosenFiniteProducts C` bundles an explicit choice of a binary
product of two objects of `C`, and a terminal object in `C`.

Users should use the monoidal notation: `X ‚äó Y` for the product and `ùüô_ C` for
the terminal object.
-/
class ChosenFiniteProducts (C : Type u) [Category.{v} C] where
  /-- A choice of a limit binary fan for any two objects of the category. -/
  product : (X Y : C) ‚Üí Limits.LimitCone (Limits.pair X Y)
  /-- A choice of a terminal object. -/
  terminal : Limits.LimitCone (Functor.empty.{0} C)

namespace ChosenFiniteProducts

instance (priority := 100) (C : Type u) [Category.{v} C] [ChosenFiniteProducts C] :
    MonoidalCategory C :=
  monoidalOfChosenFiniteProducts terminal product

instance (priority := 100) (C : Type u) [Category.{v} C] [ChosenFiniteProducts C] :
    SymmetricCategory C :=
  symmetricOfChosenFiniteProducts _ _

variable {C : Type u} [Category.{v} C] [ChosenFiniteProducts C]

open MonoidalCategory

theorem braiding_eq_braiding (X Y : C) :
  (Œ≤_ X Y) = Limits.BinaryFan.braiding (product X Y).isLimit (product Y X).isLimit := rfl

/--
The unique map to the terminal object.
-/
def toUnit (X : C) : X ‚ü∂ ùüô_ C :=
  terminal.isLimit.lift <| .mk _ <| .mk (fun x => x.as.elim) fun x => x.as.elim

instance (X : C) : Unique (X ‚ü∂ ùüô_ C) where
  default := toUnit _
  uniq _ := terminal.isLimit.hom_ext fun ‚ü®j‚ü© => j.elim

/--
This lemma follows from the preexisting `Unique` instance, but
it is often convenient to use it directly as `apply toUnit_unique` forcing
lean to do the necessary elaboration.
-/
lemma toUnit_unique {X : C} (f g : X ‚ü∂ ùüô_ _) : f = g :=
  Subsingleton.elim _ _

/--
Construct a morphism to the product given its two components.
-/
def lift {T X Y : C} (f : T ‚ü∂ X) (g : T ‚ü∂ Y) : T ‚ü∂ X ‚äó Y :=
  (product X Y).isLimit.lift <| Limits.BinaryFan.mk f g

/--
The first projection from the product.
-/
def fst (X Y : C) : X ‚äó Y ‚ü∂ X :=
  letI F : Limits.BinaryFan X Y := (product X Y).cone
  F.fst

/--
The second projection from the product.
-/
def snd (X Y : C) : X ‚äó Y ‚ü∂ Y :=
  letI F : Limits.BinaryFan X Y := (product X Y).cone
  F.snd

@[reassoc (attr := simp)]
lemma lift_fst {T X Y : C} (f : T ‚ü∂ X) (g : T ‚ü∂ Y) : lift f g ‚â´ fst _ _ = f := by
  simp [lift, fst]

@[reassoc (attr := simp)]
lemma lift_snd {T X Y : C} (f : T ‚ü∂ X) (g : T ‚ü∂ Y) : lift f g ‚â´ snd _ _ = g := by
  simp [lift, snd]

instance mono_lift_of_mono_left {W X Y : C} (f : W ‚ü∂ X) (g : W ‚ü∂ Y)
    [Mono f] : Mono (lift f g) :=
  mono_of_mono_fac <| lift_fst _ _

instance mono_lift_of_mono_right {W X Y : C} (f : W ‚ü∂ X) (g : W ‚ü∂ Y)
    [Mono g] : Mono (lift f g) :=
  mono_of_mono_fac <| lift_snd _ _

@[ext 1050]
lemma hom_ext {T X Y : C} (f g : T ‚ü∂ X ‚äó Y)
    (h_fst : f ‚â´ fst _ _ = g ‚â´ fst _ _)
    (h_snd : f ‚â´ snd _ _ = g ‚â´ snd _ _) :
    f = g :=
  (product X Y).isLimit.hom_ext fun ‚ü®j‚ü© => j.recOn h_fst h_snd

-- Similarly to `CategoryTheory.Limits.prod.comp_lift`, we do not make the `assoc` version a simp
-- lemma
@[reassoc, simp]
lemma comp_lift {V W X Y : C} (f : V ‚ü∂ W) (g : W ‚ü∂ X) (h : W ‚ü∂ Y) :
    f ‚â´ lift g h = lift (f ‚â´ g) (f ‚â´ h) := by ext <;> simp

@[simp]
lemma lift_fst_snd {X Y : C} : lift (fst X Y) (snd X Y) = ùüô (X ‚äó Y) := by ext <;> simp

@[simp]
lemma lift_comp_fst_snd {X Y Z : C} (f : X ‚ü∂ Y ‚äó Z) :
    lift (f ‚â´ fst _ _) (f ‚â´ snd _ _) = f := by
  aesop_cat

@[reassoc (attr := simp)]
lemma tensorHom_fst {X‚ÇÅ X‚ÇÇ Y‚ÇÅ Y‚ÇÇ : C} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (g : Y‚ÇÅ ‚ü∂ Y‚ÇÇ) :
    (f ‚äó g) ‚â´ fst _ _ = fst _ _ ‚â´ f := lift_fst _ _

@[reassoc (attr := simp)]
lemma tensorHom_snd {X‚ÇÅ X‚ÇÇ Y‚ÇÅ Y‚ÇÇ : C} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (g : Y‚ÇÅ ‚ü∂ Y‚ÇÇ) :
    (f ‚äó g) ‚â´ snd _ _ = snd _ _ ‚â´ g := lift_snd _ _

@[reassoc (attr := simp)]
lemma lift_map {V W X Y Z : C} (f : V ‚ü∂ W) (g : V ‚ü∂ X) (h : W ‚ü∂ Y) (k : X ‚ü∂ Z) :
    lift f g ‚â´ (h ‚äó k) = lift (f ‚â´ h) (g ‚â´ k) := by ext <;> simp

@[simp]
lemma lift_fst_comp_snd_comp {W X Y Z : C} (g : W ‚ü∂ X) (g' : Y ‚ü∂ Z) :
    lift (fst _ _ ‚â´ g) (snd _ _ ‚â´ g') = g ‚äó g' := by ext <;> simp

@[reassoc (attr := simp)]
lemma whiskerLeft_fst (X : C) {Y‚ÇÅ Y‚ÇÇ : C} (g : Y‚ÇÅ ‚ü∂ Y‚ÇÇ) :
    (X ‚óÅ g) ‚â´ fst _ _ = fst _ _ :=
  (tensorHom_fst _ _).trans (by simp)

@[reassoc (attr := simp)]
lemma whiskerLeft_snd (X : C) {Y‚ÇÅ Y‚ÇÇ : C} (g : Y‚ÇÅ ‚ü∂ Y‚ÇÇ) :
    (X ‚óÅ g) ‚â´ snd _ _ = snd _ _ ‚â´ g :=
  tensorHom_snd _ _

@[reassoc (attr := simp)]
lemma whiskerRight_fst {X‚ÇÅ X‚ÇÇ : C} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (Y : C) :
    (f ‚ñ∑ Y) ‚â´ fst _ _ = fst _ _ ‚â´ f :=
  tensorHom_fst _ _

@[reassoc (attr := simp)]
lemma whiskerRight_snd {X‚ÇÅ X‚ÇÇ : C} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (Y : C) :
    (f ‚ñ∑ Y) ‚â´ snd _ _ = snd _ _ :=
  (tensorHom_snd _ _).trans (by simp)

@[reassoc (attr := simp)]
lemma lift_whiskerRight {X Y Z W : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) (h : Y ‚ü∂ W) :
    lift f g ‚â´ (h ‚ñ∑ Z) = lift (f ‚â´ h) g := by
  aesop_cat

@[reassoc (attr := simp)]
lemma lift_whiskerLeft {X Y Z W : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) (h : Z ‚ü∂ W) :
    lift f g ‚â´ (Y ‚óÅ h) = lift f (g ‚â´ h) := by
  aesop_cat

@[reassoc (attr := simp)]
lemma associator_hom_fst (X Y Z : C) :
    (Œ±_ X Y Z).hom ‚â´ fst _ _ = fst _ _ ‚â´ fst _ _ := lift_fst _ _

@[reassoc (attr := simp)]
lemma associator_hom_snd_fst (X Y Z : C) :
    (Œ±_ X Y Z).hom ‚â´ snd _ _ ‚â´ fst _ _ = fst _ _ ‚â´ snd _ _  := by
  erw [lift_snd_assoc, lift_fst]
  rfl

@[reassoc (attr := simp)]
lemma associator_hom_snd_snd (X Y Z : C) :
    (Œ±_ X Y Z).hom ‚â´ snd _ _ ‚â´ snd _ _ = snd _ _  := by
  erw [lift_snd_assoc, lift_snd]
  rfl

@[reassoc (attr := simp)]
lemma associator_inv_fst (X Y Z : C) :
    (Œ±_ X Y Z).inv ‚â´ fst _ _ ‚â´ fst _ _ = fst _ _ := by
  erw [lift_fst_assoc, lift_fst]
  rfl

@[reassoc (attr := simp)]
lemma associator_inv_fst_snd (X Y Z : C) :
    (Œ±_ X Y Z).inv ‚â´ fst _ _ ‚â´ snd _ _ = snd _ _ ‚â´ fst _ _ := by
  erw [lift_fst_assoc, lift_snd]
  rfl

@[reassoc (attr := simp)]
lemma associator_inv_snd (X Y Z : C) :
    (Œ±_ X Y Z).inv ‚â´ snd _ _ = snd _ _ ‚â´ snd _ _ := lift_snd _ _

@[reassoc (attr := simp)]
lemma lift_lift_associator_hom {X Y Z W : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) (h : X ‚ü∂ W) :
    lift (lift f g) h ‚â´ (Œ±_ Y Z W).hom = lift f (lift g h) := by
  aesop_cat

@[reassoc (attr := simp)]
lemma lift_lift_associator_inv {X Y Z W : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) (h : X ‚ü∂ W) :
    lift f (lift g h) ‚â´ (Œ±_ Y Z W).inv = lift (lift f g) h := by
  aesop_cat

@[reassoc (attr := simp)]
lemma leftUnitor_inv_fst (X : C) :
    (Œª_ X).inv ‚â´ fst _ _ = toUnit _ := toUnit_unique _ _

@[reassoc (attr := simp)]
lemma leftUnitor_inv_snd (X : C) :
    (Œª_ X).inv ‚â´ snd _ _ = ùüô X := lift_snd _ _

@[reassoc (attr := simp)]
lemma rightUnitor_inv_fst (X : C) :
    (œÅ_ X).inv ‚â´ fst _ _ = ùüô X := lift_fst _ _

@[reassoc (attr := simp)]
lemma rightUnitor_inv_snd (X : C) :
    (œÅ_ X).inv ‚â´ snd _ _ = toUnit _ := toUnit_unique _ _

@[reassoc (attr := simp)]
lemma lift_leftUnitor_hom {X Y : C} (f : X ‚ü∂ ùüô_ C) (g : X ‚ü∂ Y) :
    lift f g ‚â´ (Œª_ Y).hom = g := by
  rw [‚Üê Iso.eq_comp_inv]
  aesop_cat

@[reassoc (attr := simp)]
lemma lift_rightUnitor_hom {X Y : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ ùüô_ C) :
    lift f g ‚â´ (œÅ_ Y).hom = f := by
  rw [‚Üê Iso.eq_comp_inv]
  aesop_cat

@[reassoc (attr := simp)]
theorem braiding_hom_fst {X Y : C} : (Œ≤_ X Y).hom ‚â´ fst _ _ = snd _ _ := by
  simp [braiding_eq_braiding, fst, snd]

@[reassoc (attr := simp)]
theorem braiding_hom_snd {X Y : C} : (Œ≤_ X Y).hom ‚â´ snd _ _ = fst _ _ := by
  simp [braiding_eq_braiding, fst, snd]

@[reassoc (attr := simp)]
theorem braiding_inv_fst {X Y : C} : (Œ≤_ X Y).inv ‚â´ fst _ _ = snd _ _ := by
  simp [braiding_eq_braiding, fst, snd]

@[reassoc (attr := simp)]
theorem braiding_inv_snd {X Y : C} : (Œ≤_ X Y).inv ‚â´ snd _ _ = fst _ _ := by
  simp [braiding_eq_braiding, fst, snd]

/--
Construct an instance of `ChosenFiniteProducts C` given an instance of `HasFiniteProducts C`.
-/
noncomputable
def ofFiniteProducts
    (C : Type u) [Category.{v} C] [Limits.HasFiniteProducts C] :
    ChosenFiniteProducts C where
  product X Y := Limits.getLimitCone (Limits.pair X Y)
  terminal := Limits.getLimitCone (Functor.empty C)

instance (priority := 100) : Limits.HasFiniteProducts C :=
  letI : ‚àÄ (X Y : C), Limits.HasLimit (Limits.pair X Y) := fun _ _ =>
    .mk <| ChosenFiniteProducts.product _ _
  letI : Limits.HasBinaryProducts C := Limits.hasBinaryProducts_of_hasLimit_pair _
  letI : Limits.HasTerminal C := Limits.hasTerminal_of_unique (ùüô_ C)
  hasFiniteProducts_of_has_binary_and_terminal

section ChosenFiniteProductsComparison

variable {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D] [ChosenFiniteProducts D] (F : C ‚•§ D)

section terminalComparison

/-- When `C` and `D` have chosen finite products and `F : C ‚•§ D` is any functor,
`terminalComparison F` is the unique map `F (ùüô_ C) ‚ü∂ ùüô_ D`. -/
abbrev terminalComparison : F.obj (ùüô_ C) ‚ü∂ ùüô_ D := toUnit _

@[reassoc (attr := simp)]
lemma map_toUnit_comp_terminalCompariso (A : C) :
    F.map (toUnit A) ‚â´ terminalComparison F = toUnit _ := toUnit_unique _ _

open Limits

/-- If `terminalComparison F` is an Iso, then `F` preserves terminal objects. -/
lemma preservesLimit_empty_of_isIso_terminalComparison [IsIso (terminalComparison F)] :
    PreservesLimit (Functor.empty.{0} C) F := by
  apply preservesLimit_of_preserves_limit_cone terminal.isLimit
  apply isLimitChangeEmptyCone D terminal.isLimit
  exact asIso (terminalComparison F)|>.symm

/-- If `F` preserves terminal objects, then `terminalComparison F` is an isomorphism. -/
noncomputable def preservesTerminalIso [h : PreservesLimit (Functor.empty.{0} C) F] :
    F.obj (ùüô_ C) ‚âÖ ùüô_ D :=
  (isLimitChangeEmptyCone D (isLimitOfPreserves _ terminal.isLimit) (asEmptyCone (F.obj (ùüô_ C)))
    (Iso.refl _)).conePointUniqueUpToIso terminal.isLimit

@[simp]
lemma preservesTerminalIso_hom [PreservesLimit (Functor.empty.{0} C) F] :
    (preservesTerminalIso F).hom = terminalComparison F := toUnit_unique _ _

instance terminalComparison_isIso_of_preservesLimits [PreservesLimit (Functor.empty.{0} C) F] :
    IsIso (terminalComparison F) := by
  rw [‚Üê preservesTerminalIso_hom]
  infer_instance

end terminalComparison

section prodComparison

variable (A B : C)

/-- When `C` and `D` have chosen finite products and `F : C ‚•§ D` is any functor,
`prodComparison F A B` is the canonical comparison morphism from `F (A ‚äó B)` to `F(A) ‚äó F(B)`. -/
def prodComparison (A B : C) : F.obj (A ‚äó B) ‚ü∂ F.obj A ‚äó F.obj B :=
  lift (F.map (fst A B)) (F.map (snd A B))

@[reassoc (attr := simp)]
theorem prodComparison_fst : prodComparison F A B ‚â´ fst _ _ = F.map (fst A B) :=
  lift_fst _ _

@[reassoc (attr := simp)]
theorem prodComparison_snd : prodComparison F A B ‚â´ snd _ _ = F.map (snd A B) :=
  lift_snd _ _

@[reassoc (attr := simp)]
theorem inv_prodComparison_map_fst [IsIso (prodComparison F A B)] :
    inv (prodComparison F A B) ‚â´ F.map (fst _ _) = fst _ _ := by simp [IsIso.inv_comp_eq]

@[reassoc (attr := simp)]
theorem inv_prodComparison_map_snd [IsIso (prodComparison F A B)] :
    inv (prodComparison F A B) ‚â´ F.map (snd _ _) = snd _ _ := by simp [IsIso.inv_comp_eq]

variable {A B} {A' B' : C}

/-- Naturality of the `prodComparison` morphism in both arguments. -/
@[reassoc]
theorem prodComparison_natural (f : A ‚ü∂ A') (g : B ‚ü∂ B') :
    F.map (f ‚äó g) ‚â´ prodComparison F A' B' =
      prodComparison F A B ‚â´ (F.map f ‚äó F.map g) := by
  apply hom_ext <;>
  simp only [Category.assoc, prodComparison_fst, tensorHom_fst, prodComparison_fst_assoc,
    prodComparison_snd, tensorHom_snd, prodComparison_snd_assoc, ‚Üê F.map_comp]

/-- Naturality of the `prodComparison` morphism in the right argument. -/
@[reassoc]
theorem prodComparison_natural_whiskerLeft (g : B ‚ü∂ B') :
    F.map (A ‚óÅ g) ‚â´ prodComparison F A B' =
      prodComparison F A B ‚â´ (F.obj A ‚óÅ F.map g) := by
  rw [‚Üê id_tensorHom, prodComparison_natural, Functor.map_id]
  rfl

/-- Naturality of the `prodComparison` morphism in the left argument. -/
@[reassoc]
theorem prodComparison_natural_whiskerRight (f : A ‚ü∂ A') :
    F.map (f ‚ñ∑ B) ‚â´ prodComparison F A' B =
      prodComparison F A B ‚â´ (F.map f ‚ñ∑ F.obj B) := by
  rw [‚Üê tensorHom_id, prodComparison_natural, Functor.map_id]
  rfl

section
variable [IsIso (prodComparison F A B)]

/-- If the product comparison morphism is an iso, its inverse is natural in both argument. -/
@[reassoc]
theorem prodComparison_inv_natural (f : A ‚ü∂ A') (g : B ‚ü∂ B') [IsIso (prodComparison F A' B')] :
    inv (prodComparison F A B) ‚â´ F.map (f ‚äó g) =
      (F.map f ‚äó F.map g) ‚â´ inv (prodComparison F A' B') := by
  rw [IsIso.eq_comp_inv, Category.assoc, IsIso.inv_comp_eq, prodComparison_natural]

/-- If the product comparison morphism is an iso, its inverse is natural in the right argument. -/
@[reassoc]
theorem prodComparison_inv_natural_whiskerLeft (g : B ‚ü∂ B') [IsIso (prodComparison F A B')] :
    inv (prodComparison F A B) ‚â´ F.map (A ‚óÅ g) =
      (F.obj A ‚óÅ F.map g) ‚â´ inv (prodComparison F A B') := by
  rw [IsIso.eq_comp_inv, Category.assoc, IsIso.inv_comp_eq, prodComparison_natural_whiskerLeft]

/-- If the product comparison morphism is an iso, its inverse is natural in the left argument. -/
@[reassoc]
theorem prodComparison_inv_natural_whiskerRight (f : A ‚ü∂ A') [IsIso (prodComparison F A' B)] :
    inv (prodComparison F A B) ‚â´ F.map (f ‚ñ∑ B) =
      (F.map f ‚ñ∑ F.obj B) ‚â´ inv (prodComparison F A' B) := by
  rw [IsIso.eq_comp_inv, Category.assoc, IsIso.inv_comp_eq, prodComparison_natural_whiskerRight]

end

theorem prodComparison_comp {E : Type u‚ÇÇ} [Category.{v‚ÇÇ} E] [ChosenFiniteProducts E] (G : D ‚•§ E) :
    prodComparison (F ‚ãô G) A B =
      G.map (prodComparison F A B) ‚â´ prodComparison G (F.obj A) (F.obj B) := by
  unfold prodComparison
  ext <;> simp [‚Üê G.map_comp]

@[simp]
lemma prodComparison_id :
    prodComparison (ùü≠ C) A B = ùüô (A ‚äó B) := lift_fst_snd

/-- The product comparison morphism from `F(A ‚äó -)` to `FA ‚äó F-`, whose components are given by
`prodComparison`. -/
@[simps]
def prodComparisonNatTrans (A : C) :
    (curriedTensor C).obj A ‚ãô F ‚ü∂ F ‚ãô (curriedTensor D).obj (F.obj A) where
  app B := prodComparison F A B
  naturality x y f := by
    apply hom_ext <;>
    simp only [Functor.comp_obj, curriedTensor_obj_obj,
      Functor.comp_map, curriedTensor_obj_map, Category.assoc, prodComparison_fst, whiskerLeft_fst,
      prodComparison_snd, prodComparison_snd_assoc, whiskerLeft_snd, ‚Üê F.map_comp]

theorem prodComparisonNatTrans_comp {E : Type u‚ÇÇ} [Category.{v‚ÇÇ} E] [ChosenFiniteProducts E]
    (G : D ‚•§ E) : prodComparisonNatTrans (F ‚ãô G) A =
      whiskerRight (prodComparisonNatTrans F A) G ‚â´
        whiskerLeft F (prodComparisonNatTrans G (F.obj A)) := by ext; simp [prodComparison_comp]

@[simp]
lemma prodComparisonNatTrans_id :
    prodComparisonNatTrans (ùü≠ C) A = ùüô _ := by ext; simp

/-- The product comparison morphism from `F(- ‚äó -)` to `F- ‚äó F-`, whose components are given by
`prodComparison`. -/
@[simps]
def prodComparisonBifunctorNatTrans :
    curriedTensor C ‚ãô (whiskeringRight _ _ _).obj F ‚ü∂
      F ‚ãô curriedTensor D ‚ãô (whiskeringLeft _ _ _).obj F where
  app A := prodComparisonNatTrans F A
  naturality x y f := by
    ext z
    apply hom_ext <;> simp [‚Üê Functor.map_comp]

variable {E : Type u‚ÇÇ} [Category.{v‚ÇÇ} E]
    [ChosenFiniteProducts E] (G : D ‚•§ E)

theorem prodComparisonBifunctorNatTrans_comp {E : Type u‚ÇÇ} [Category.{v‚ÇÇ} E]
    [ChosenFiniteProducts E] (G : D ‚•§ E) : prodComparisonBifunctorNatTrans (F ‚ãô G) =
      whiskerRight (prodComparisonBifunctorNatTrans F) ((whiskeringRight _ _ _).obj G) ‚â´
        whiskerLeft F (whiskerRight (prodComparisonBifunctorNatTrans G)
          ((whiskeringLeft _ _ _).obj F)) := by ext; simp [prodComparison_comp]

instance (A : C) [‚àÄ B, IsIso (prodComparison F A B)] : IsIso (prodComparisonNatTrans F A) := by
  letI : ‚àÄ X, IsIso ((prodComparisonNatTrans F A).app X) := by assumption
  apply NatIso.isIso_of_isIso_app

instance [‚àÄ A B, IsIso (prodComparison F A B)] : IsIso (prodComparisonBifunctorNatTrans F) := by
  letI : ‚àÄ X, IsIso ((prodComparisonBifunctorNatTrans F).app X) :=
    fun _ ‚Ü¶ by dsimp; apply NatIso.isIso_of_isIso_app
  apply NatIso.isIso_of_isIso_app

open Limits
section PreservesLimitPairs

section
variable (A B)
variable [PreservesLimit (pair A B) F]

/-- If `F` preserves the limit of the pair `(A, B)`, then the binary fan given by
`(F.map fst A B, F.map (snd A B))` is a limit cone. -/
noncomputable def isLimitChosenFiniteProductsOfPreservesLimits :
    IsLimit <| BinaryFan.mk (F.map (fst A B)) (F.map (snd A B)) :=
  mapIsLimitOfPreservesOfIsLimit F (fst _ _) (snd _ _) <|
    (product A B).isLimit.ofIsoLimit <| isoBinaryFanMk (product A B).cone

/-- If `F` preserves the limit of the pair `(A, B)`, then `prodComparison F A B` is an isomorphism.
-/
noncomputable def prodComparisonIso : F.obj (A ‚äó B) ‚âÖ F.obj A ‚äó F.obj B :=
  IsLimit.conePointUniqueUpToIso (isLimitChosenFiniteProductsOfPreservesLimits F A B)
    (product _ _).isLimit

@[simp]
lemma prodComparisonIso_hom : (prodComparisonIso F A B).hom = prodComparison F A B := by
  rfl

instance isIso_prodComparison_of_preservesLimit_pair : IsIso (prodComparison F A B) := by
  rw [‚Üê prodComparisonIso_hom]
  infer_instance

end

/-- The natural isomorphism `F(A ‚äó -) ‚âÖ FA ‚äó F-`, provided each `prodComparison F A B` is an
isomorphism (as `B` changes). -/
@[simps! hom inv]
noncomputable def prodComparisonNatIso (A : C) [‚àÄ B, PreservesLimit (pair A B) F] :
    (curriedTensor C).obj A ‚ãô F ‚âÖ F ‚ãô (curriedTensor D).obj (F.obj A) :=
  asIso (prodComparisonNatTrans F A)

/-- The natural isomorphism of bifunctors `F(- ‚äó -) ‚âÖ F- ‚äó F-`, provided each
`prodComparison F A B` is an isomorphism. -/
@[simps! hom inv]
noncomputable def prodComparisonBifunctorNatIso [‚àÄ A B, PreservesLimit (pair A B) F] :
    curriedTensor C ‚ãô (whiskeringRight _ _ _).obj F ‚âÖ
      F ‚ãô curriedTensor D ‚ãô (whiskeringLeft _ _ _).obj F :=
  asIso (prodComparisonBifunctorNatTrans F)

end PreservesLimitPairs

section ProdComparisonIso

/-- If `prodComparison F A B` is an isomorphism, then `F` preserves the limit of `pair A B`. -/
lemma preservesLimit_pair_of_isIso_prodComparison (A B : C)
    [IsIso (prodComparison F A B)] :
    PreservesLimit (pair A B) F := by
 apply preservesLimit_of_preserves_limit_cone (product A B).isLimit
 refine IsLimit.equivOfNatIsoOfIso (pairComp A B F) _
    ((product (F.obj A) (F.obj B)).cone.extend (prodComparison F A B))
      (BinaryFan.ext (by exact Iso.refl _) ?_ ?_) |>.invFun
      (IsLimit.extendIso _ (product (F.obj A) (F.obj B)).isLimit)
 ¬∑ dsimp only [BinaryFan.fst]
   simp [pairComp, prodComparison, lift, fst]
 ¬∑ dsimp only [BinaryFan.snd]
   simp [pairComp, prodComparison, lift, snd]

  /-- If `prodComparison F A B` is an isomorphism for all `A B` then `F` preserves limits of shape
`Discrete (WalkingPair)`. -/
lemma preservesLimitsOfShape_discrete_walkingPair_of_isIso_prodComparison
    [‚àÄ A B, IsIso (prodComparison F A B)] : PreservesLimitsOfShape (Discrete WalkingPair) F := by
  constructor
  intro K
  refine @preservesLimit_of_iso_diagram _ _ _ _ _ _ _ _ _ (diagramIsoPair K).symm ?_
  apply preservesLimit_pair_of_isIso_prodComparison

end ProdComparisonIso

end prodComparison

end ChosenFiniteProductsComparison

end ChosenFiniteProducts

open MonoidalCategory ChosenFiniteProducts

namespace Functor

variable {C : Type u} [Category.{v} C] [ChosenFiniteProducts C]
  {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D] [ChosenFiniteProducts D] (F : C ‚•§ D)

open Functor.OplaxMonoidal

/- The definitions `oplaxMonoidalOfChosenFiniteProducts` and
`monoidalOfChosenFiniteProducts` are not made instances because it would
create a diamond for the (oplax) monoidal structure on a composition
`F ‚ãô G` of functors between categories with chosen finite products. -/

/-- Any functor between categories with chosen finite products induces an oplax monoial functor. -/
def oplaxMonoidalOfChosenFiniteProducts : F.OplaxMonoidal where
  Œ∑' := terminalComparison F
  Œ¥' X Y := prodComparison F X Y
  Œ¥'_natural_left f X' := by simpa using (prodComparison_natural F f (ùüô X')).symm
  Œ¥'_natural_right X g := by simpa using (prodComparison_natural F (ùüô X) g).symm
  oplax_associativity' _ _ _ := by
    apply hom_ext
    case' h_snd => apply hom_ext
    all_goals simp [‚Üê Functor.map_comp]
  oplax_left_unitality' _ := by
    apply hom_ext
    ¬∑ exact toUnit_unique _ _
    ¬∑ simp only [leftUnitor_inv_snd, Category.assoc, whiskerRight_snd,
        prodComparison_snd, ‚Üê F.map_comp, F.map_id]
  oplax_right_unitality' _ := by
    apply hom_ext
    ¬∑ simp only [rightUnitor_inv_fst, Category.assoc, whiskerLeft_fst,
        prodComparison_fst, ‚Üê F.map_comp, F.map_id]
    ¬∑ exact toUnit_unique _ _


attribute [local instance] oplaxMonoidalOfChosenFiniteProducts

lemma Œ∑_of_chosenFiniteProducts : Œ∑ F = terminalComparison F := rfl

lemma Œ¥_of_chosenFiniteProducts (X Y : C) : Œ¥ F X Y = prodComparison F X Y := rfl

open Limits

variable [PreservesFiniteProducts F]

instance : IsIso (Œ∑ F) :=
  terminalComparison_isIso_of_preservesLimits F

instance (A B : C) : IsIso (Œ¥ F A B) :=
  isIso_prodComparison_of_preservesLimit_pair F A B

/-- If `F : C ‚•§ D` is a functor between categories with chosen finite products
that preserves finite products, then it is a monoidal functor. -/
noncomputable def monoidalOfChosenFiniteProducts : F.Monoidal :=
  Functor.Monoidal.ofOplaxMonoidal F

end Functor

namespace Functor.Monoidal

variable {C : Type u} [Category.{v} C] [ChosenFiniteProducts C]
  {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D] [ChosenFiniteProducts D] (F : C ‚•§ D)

section

attribute [local instance] oplaxMonoidalOfChosenFiniteProducts

@[reassoc (attr := simp)]
lemma Œ¥_fst (X Y : C) : OplaxMonoidal.Œ¥ F X Y ‚â´ fst _ _ = F.map (fst _ _) := by
  simp [Œ¥_of_chosenFiniteProducts]

@[reassoc (attr := simp)]
lemma Œ¥_snd (X Y : C) : OplaxMonoidal.Œ¥ F X Y ‚â´ snd _ _ = F.map (snd _ _) := by
  simp [Œ¥_of_chosenFiniteProducts]

@[reassoc (attr := simp)]
lemma lift_Œ¥ {X Y Z : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) :
    F.map (lift f g) ‚â´ OplaxMonoidal.Œ¥ F _ _ = lift (F.map f) (F.map g) := by
  apply hom_ext <;> simp [‚Üê F.map_comp]

end

section

open Limits

variable [PreservesFiniteProducts F]

attribute [local instance] monoidalOfChosenFiniteProducts

@[reassoc (attr := simp)]
lemma toUnit_Œµ {X : C} : toUnit (F.obj X) ‚â´ LaxMonoidal.Œµ F = F.map (toUnit X) :=
  (cancel_mono (ŒµIso _).inv).1 (toUnit_unique _ _)

@[reassoc (attr := simp)]
lemma lift_Œº {X Y Z : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) :
    lift (F.map f) (F.map g) ‚â´ LaxMonoidal.Œº F _ _ = F.map (lift f g) :=
  (cancel_mono (ŒºIso _ _ _).inv).1 (by simp)

@[reassoc (attr := simp)]
lemma Œº_fst (X Y : C) : LaxMonoidal.Œº F X Y ‚â´ F.map (fst X Y) = fst (F.obj X) (F.obj Y) :=
  (cancel_epi (ŒºIso _ _ _).inv).1 (by simp)

@[reassoc (attr := simp)]
lemma Œº_snd (X Y : C) : LaxMonoidal.Œº F X Y ‚â´ F.map (snd X Y) = snd (F.obj X) (F.obj Y) :=
  (cancel_epi (ŒºIso _ _ _).inv).1 (by simp)


end

end Functor.Monoidal

namespace Functor

variable {C : Type u} [Category.{v} C] [ChosenFiniteProducts C]
  {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D] [ChosenFiniteProducts D] (F : C ‚•§ D)

attribute [local instance] monoidalOfChosenFiniteProducts

/-- A finite-product-preserving functor between categories with chosen finite products is
braided. -/
noncomputable def braidedOfChosenFiniteProducts [Limits.PreservesFiniteProducts F] : F.Braided :=
  { monoidalOfChosenFiniteProducts F with
    braided X Y := by
      rw [‚Üê cancel_mono (Monoidal.ŒºIso _ _ _).inv]
      apply ChosenFiniteProducts.hom_ext <;> simp [‚Üê Functor.map_comp] }

end Functor

namespace NatTrans

variable {C : Type u} [Category.{v} C] [ChosenFiniteProducts C]
  {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D] [ChosenFiniteProducts D] (F G : C ‚•§ D)
  [Limits.PreservesFiniteProducts F] [Limits.PreservesFiniteProducts G]

attribute [local instance] Functor.monoidalOfChosenFiniteProducts in
theorem monoidal_of_preservesFiniteProducts (Œ± : F ‚ü∂ G) :
    NatTrans.IsMonoidal Œ± where
  unit := (cancel_mono (Functor.Monoidal.ŒµIso _).inv).1 (toUnit_unique _ _)
  tensor {X Y} := by
    rw [‚Üê cancel_mono (Functor.Monoidal.ŒºIso _ _ _).inv]
    rw [‚Üê cancel_epi (Functor.Monoidal.ŒºIso _ _ _).inv]
    apply ChosenFiniteProducts.hom_ext <;> simp

end NatTrans

end CategoryTheory
