/-
Copyright (c) 2024 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz, Robin Carlier
-/
import Mathlib.CategoryTheory.Limits.Constructions.FiniteProductsOfBinaryProducts
import Mathlib.CategoryTheory.Limits.FullSubcategory
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Terminal
import Mathlib.CategoryTheory.Monoidal.Braided.Basic
import Mathlib.CategoryTheory.Monoidal.OfChosenFiniteProducts.Basic

/-!
# Categories with chosen finite products

We introduce a class, `ChosenFiniteProducts`, which bundles explicit choices
for a terminal object and binary products in a category `C`.
This is primarily useful for categories which have finite products with good
definitional properties, such as the category of types.

For better defeqs, we also extend `MonoidalCategory`.

## Implementation notes

For cartesian monoidal categories, the oplax-monoidal/monoidal/braided structure of a functor `F`
preserving finite products is uniquely determined. See the `ofChosenFiniteProducts` declarations.

We however develop the theory for any `F.OplaxMonoidal`/`F.Monoidal`/`F.Braided` instance instead of
requiring it to be the `ofChosenFiniteProducts` one. This is to avoid diamonds: Consider
eg `ùü≠ C` and `F ‚ãô G`.

In applications requiring a finite preserving functor to be oplax-monoidal/monoidal/braided,
avoid `attribute [local instance] ofChosenFiniteProducts` but instead turn on the corresponding
`ofChosenFiniteProducts` declaration for that functor only.

# Projects

- Construct an instance of chosen finite products in the category of affine scheme, using
  the tensor product.
- Construct chosen finite products in other categories appearing "in nature".

-/

namespace CategoryTheory

universe v v‚ÇÅ v‚ÇÇ v‚ÇÉ u u‚ÇÅ u‚ÇÇ u‚ÇÉ

open MonoidalCategory Limits

variable (C) in
/--
An instance of `ChosenFiniteProducts C` bundles an explicit choice of a binary
product of two objects of `C`, and a terminal object in `C`.

Users should use the monoidal notation: `X ‚äó Y` for the product and `ùüô_ C` for
the terminal object.
-/
class ChosenFiniteProducts (C : Type u) [Category.{v} C] extends MonoidalCategory C where
  /-- The tensor unit is a terminal object. -/
  isTerminalTensorUnit : IsTerminal (ùüô_ C)
  /-- The first projection from the product. -/
  fst (X Y : C) : X ‚äó Y ‚ü∂ X
  /-- The second projection from the product. -/
  snd (X Y : C) : X ‚äó Y ‚ü∂ Y
  /-- The monoidal product is the categorical product. -/
  tensorProductIsBinaryProduct (X Y : C) : IsLimit <| BinaryFan.mk (fst X Y) (snd X Y)
  fst_def (X Y : C) : fst X Y = X ‚óÅ isTerminalTensorUnit.from Y ‚â´ (œÅ_ X).hom := by aesop_cat
  snd_def (X Y : C) : snd X Y = isTerminalTensorUnit.from X ‚ñ∑ Y ‚â´ (Œª_ Y).hom := by aesop_cat

namespace ChosenFiniteProducts

variable {C : Type u} [Category.{v} C] [ChosenFiniteProducts C]

open MonoidalCategory

/--
The unique map to the terminal object.
-/
def toUnit (X : C) : X ‚ü∂ ùüô_ C := isTerminalTensorUnit.from _

instance (X : C) : Unique (X ‚ü∂ ùüô_ C) := isTerminalEquivUnique _ _ isTerminalTensorUnit _

lemma default_eq_toUnit (X : C) : default = toUnit X := rfl

/--
This lemma follows from the preexisting `Unique` instance, but
it is often convenient to use it directly as `apply toUnit_unique` forcing
lean to do the necessary elaboration.
-/
@[ext]
lemma toUnit_unique {X : C} (f g : X ‚ü∂ ùüô_ _) : f = g :=
  Subsingleton.elim _ _

@[reassoc (attr := simp)]
theorem comp_toUnit {X Y : C} (f : X ‚ü∂ Y) : f ‚â´ toUnit Y = toUnit X :=
  toUnit_unique _ _

/--
Construct a morphism to the product given its two components.
-/
def lift {T X Y : C} (f : T ‚ü∂ X) (g : T ‚ü∂ Y) : T ‚ü∂ X ‚äó Y :=
  (BinaryFan.IsLimit.lift' (tensorProductIsBinaryProduct X Y) f g).1

@[reassoc (attr := simp)]
lemma lift_fst {T X Y : C} (f : T ‚ü∂ X) (g : T ‚ü∂ Y) : lift f g ‚â´ fst _ _ = f :=
  (BinaryFan.IsLimit.lift' (tensorProductIsBinaryProduct X Y) f g).2.1

@[reassoc (attr := simp)]
lemma lift_snd {T X Y : C} (f : T ‚ü∂ X) (g : T ‚ü∂ Y) : lift f g ‚â´ snd _ _ = g :=
  (BinaryFan.IsLimit.lift' (tensorProductIsBinaryProduct X Y) f g).2.2

instance mono_lift_of_mono_left {W X Y : C} (f : W ‚ü∂ X) (g : W ‚ü∂ Y)
    [Mono f] : Mono (lift f g) :=
  mono_of_mono_fac <| lift_fst _ _

instance mono_lift_of_mono_right {W X Y : C} (f : W ‚ü∂ X) (g : W ‚ü∂ Y)
    [Mono g] : Mono (lift f g) :=
  mono_of_mono_fac <| lift_snd _ _

@[ext 1050]
lemma hom_ext {T X Y : C} (f g : T ‚ü∂ X ‚äó Y)
    (h_fst : f ‚â´ fst _ _ = g ‚â´ fst _ _)
    (h_snd : f ‚â´ snd _ _ = g ‚â´ snd _ _) :
    f = g :=
  BinaryFan.IsLimit.hom_ext (tensorProductIsBinaryProduct X Y) h_fst h_snd

-- Similarly to `CategoryTheory.Limits.prod.comp_lift`, we do not make the `assoc` version a simp
-- lemma
@[reassoc, simp]
lemma comp_lift {V W X Y : C} (f : V ‚ü∂ W) (g : W ‚ü∂ X) (h : W ‚ü∂ Y) :
    f ‚â´ lift g h = lift (f ‚â´ g) (f ‚â´ h) := by ext <;> simp

@[simp]
lemma lift_fst_snd {X Y : C} : lift (fst X Y) (snd X Y) = ùüô (X ‚äó Y) := by ext <;> simp

@[simp]
lemma lift_comp_fst_snd {X Y Z : C} (f : X ‚ü∂ Y ‚äó Z) :
    lift (f ‚â´ fst _ _) (f ‚â´ snd _ _) = f := by
  aesop_cat

@[reassoc (attr := simp)]
lemma whiskerLeft_fst (X : C) {Y Z : C} (f : Y ‚ü∂ Z) : X ‚óÅ f ‚â´ fst _ _ = fst _ _ := by
  simp [fst_def, ‚Üê MonoidalCategory.whiskerLeft_comp_assoc]

@[reassoc (attr := simp)]
lemma whiskerLeft_snd (X : C) {Y Z : C} (f : Y ‚ü∂ Z) : X ‚óÅ f ‚â´ snd _ _ = snd _ _ ‚â´ f := by
  simp [snd_def, whisker_exchange_assoc]

@[reassoc (attr := simp)]
lemma whiskerRight_fst {X Y : C} (f : X ‚ü∂ Y) (Z : C) : f ‚ñ∑ Z ‚â´ fst _ _ = fst _ _ ‚â´ f := by
  simp [fst_def, ‚Üê whisker_exchange_assoc]

@[reassoc (attr := simp)]
lemma whiskerRight_snd {X Y : C} (f : X ‚ü∂ Y) (Z : C) : f ‚ñ∑ Z ‚â´ snd _ _ = snd _ _ := by
  simp [snd_def, ‚Üê MonoidalCategory.comp_whiskerRight_assoc]

@[reassoc (attr := simp)]
lemma tensorHom_fst {X‚ÇÅ X‚ÇÇ Y‚ÇÅ Y‚ÇÇ : C} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (g : Y‚ÇÅ ‚ü∂ Y‚ÇÇ) :
    (f ‚äó g) ‚â´ fst _ _ = fst _ _ ‚â´ f := by simp [tensorHom_def]

@[reassoc (attr := simp)]
lemma tensorHom_snd {X‚ÇÅ X‚ÇÇ Y‚ÇÅ Y‚ÇÇ : C} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (g : Y‚ÇÅ ‚ü∂ Y‚ÇÇ) :
    (f ‚äó g) ‚â´ snd _ _ = snd _ _ ‚â´ g := by simp [tensorHom_def]

@[reassoc (attr := simp)]
lemma lift_map {V W X Y Z : C} (f : V ‚ü∂ W) (g : V ‚ü∂ X) (h : W ‚ü∂ Y) (k : X ‚ü∂ Z) :
    lift f g ‚â´ (h ‚äó k) = lift (f ‚â´ h) (g ‚â´ k) := by ext <;> simp

@[simp]
lemma lift_fst_comp_snd_comp {W X Y Z : C} (g : W ‚ü∂ X) (g' : Y ‚ü∂ Z) :
    lift (fst _ _ ‚â´ g) (snd _ _ ‚â´ g') = g ‚äó g' := by ext <;> simp

@[reassoc (attr := simp)]
lemma lift_whiskerRight {X Y Z W : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) (h : Y ‚ü∂ W) :
    lift f g ‚â´ (h ‚ñ∑ Z) = lift (f ‚â´ h) g := by
  aesop_cat

@[reassoc (attr := simp)]
lemma lift_whiskerLeft {X Y Z W : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) (h : Z ‚ü∂ W) :
    lift f g ‚â´ (Y ‚óÅ h) = lift f (g ‚â´ h) := by
  aesop_cat

@[reassoc (attr := simp)]
lemma associator_hom_fst (X Y Z : C) :
    (Œ±_ X Y Z).hom ‚â´ fst _ _ = fst _ _ ‚â´ fst _ _ := by
  simp [fst_def, ‚Üê whiskerLeft_rightUnitor_assoc, -whiskerLeft_rightUnitor,
    ‚Üê MonoidalCategory.whiskerLeft_comp_assoc]

@[reassoc (attr := simp)]
lemma associator_hom_snd_fst (X Y Z : C) :
    (Œ±_ X Y Z).hom ‚â´ snd _ _ ‚â´ fst _ _ = fst _ _ ‚â´ snd _ _ := by
  simp [fst_def, ‚Üê whiskerLeft_rightUnitor_assoc, -whiskerLeft_rightUnitor]

@[reassoc (attr := simp)]
lemma associator_hom_snd_snd (X Y Z : C) :
    (Œ±_ X Y Z).hom ‚â´ snd _ _ ‚â´ snd _ _ = snd _ _ := by
  simp [snd_def, ‚Üê leftUnitor_whiskerRight_assoc, -leftUnitor_whiskerRight,
    ‚Üê MonoidalCategory.comp_whiskerRight_assoc]

@[reassoc (attr := simp)]
lemma associator_inv_fst_fst (X Y Z : C) :
    (Œ±_ X Y Z).inv ‚â´ fst _ _ ‚â´ fst _ _ = fst _ _ := by
  simp [fst_def, ‚Üê whiskerLeft_rightUnitor_assoc, -whiskerLeft_rightUnitor,
    ‚Üê MonoidalCategory.whiskerLeft_comp_assoc]

@[reassoc (attr := simp)]
lemma associator_inv_fst_snd (X Y Z : C) :
    (Œ±_ X Y Z).inv ‚â´ fst _ _ ‚â´ snd _ _ = snd _ _ ‚â´ fst _ _ := by
  simp [fst_def, ‚Üê whiskerLeft_rightUnitor_assoc, -whiskerLeft_rightUnitor]

@[deprecated (since := "2025-04-01")] alias associator_inv_fst := associator_inv_fst_fst
@[deprecated (since := "2025-04-01")] alias associator_inv_fst_assoc := associator_inv_fst_fst_assoc

@[reassoc (attr := simp)]
lemma associator_inv_snd (X Y Z : C) :
    (Œ±_ X Y Z).inv ‚â´ snd _ _ = snd _ _ ‚â´ snd _ _ := by
  simp [snd_def, ‚Üê leftUnitor_whiskerRight_assoc, -leftUnitor_whiskerRight,
    ‚Üê MonoidalCategory.comp_whiskerRight_assoc]

@[reassoc (attr := simp)]
lemma lift_lift_associator_hom {X Y Z W : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) (h : X ‚ü∂ W) :
    lift (lift f g) h ‚â´ (Œ±_ Y Z W).hom = lift f (lift g h) := by
  aesop_cat

@[reassoc (attr := simp)]
lemma lift_lift_associator_inv {X Y Z W : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) (h : X ‚ü∂ W) :
    lift f (lift g h) ‚â´ (Œ±_ Y Z W).inv = lift (lift f g) h := by
  aesop_cat

lemma leftUnitor_hom (X : C) : (Œª_ X).hom = snd _ _ := by simp [snd_def]
lemma rightUnitor_hom (X : C) : (œÅ_ X).hom = fst _ _ := by simp [fst_def]

@[reassoc (attr := simp)]
lemma leftUnitor_inv_fst (X : C) :
    (Œª_ X).inv ‚â´ fst _ _ = toUnit _ := toUnit_unique _ _

@[reassoc (attr := simp)]
lemma leftUnitor_inv_snd (X : C) :
    (Œª_ X).inv ‚â´ snd _ _ = ùüô X := by simp [snd_def]

@[reassoc (attr := simp)]
lemma rightUnitor_inv_fst (X : C) :
    (œÅ_ X).inv ‚â´ fst _ _ = ùüô X := by simp [fst_def]

@[reassoc (attr := simp)]
lemma rightUnitor_inv_snd (X : C) :
    (œÅ_ X).inv ‚â´ snd _ _ = toUnit _ := toUnit_unique _ _

@[reassoc]
lemma whiskerLeft_toUnit_comp_rightUnitor_hom (X Y : C) : X ‚óÅ toUnit Y ‚â´ (œÅ_ X).hom = fst X Y := by
  rw [‚Üê cancel_mono (œÅ_ X).inv]; aesop

@[reassoc]
lemma whiskerRight_toUnit_comp_leftUnitor_hom (X Y : C) : toUnit X ‚ñ∑ Y ‚â´ (Œª_ Y).hom = snd X Y := by
  rw [‚Üê cancel_mono (Œª_ Y).inv]; aesop

@[reassoc (attr := simp)]
lemma lift_leftUnitor_hom {X Y : C} (f : X ‚ü∂ ùüô_ C) (g : X ‚ü∂ Y) :
    lift f g ‚â´ (Œª_ Y).hom = g := by
  rw [‚Üê Iso.eq_comp_inv]
  aesop_cat

@[reassoc (attr := simp)]
lemma lift_rightUnitor_hom {X Y : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ ùüô_ C) :
    lift f g ‚â´ (œÅ_ Y).hom = f := by
  rw [‚Üê Iso.eq_comp_inv]
  aesop_cat

section BraidedCategory

variable [BraidedCategory C]

@[reassoc (attr := simp)]
theorem braiding_hom_fst (X Y : C) : (Œ≤_ X Y).hom ‚â´ fst _ _ = snd _ _ := by
  simp [fst_def, snd_def, ‚Üê BraidedCategory.braiding_naturality_left_assoc]

@[reassoc (attr := simp)]
theorem braiding_hom_snd (X Y : C) : (Œ≤_ X Y).hom ‚â´ snd _ _ = fst _ _ := by
  simp [fst_def, snd_def, ‚Üê BraidedCategory.braiding_naturality_right_assoc]

@[reassoc (attr := simp)]
theorem braiding_inv_fst (X Y : C) : (Œ≤_ X Y).inv ‚â´ fst _ _ = snd _ _ := by
  simp [fst_def, snd_def, ‚Üê BraidedCategory.braiding_inv_naturality_left_assoc]

@[reassoc (attr := simp)]
theorem braiding_inv_snd (X Y : C) : (Œ≤_ X Y).inv ‚â´ snd _ _ = fst _ _ := by
  simp [fst_def, snd_def, ‚Üê BraidedCategory.braiding_inv_naturality_right_assoc]

theorem lift_snd_fst {X Y : C} : lift (snd X Y) (fst X Y) = (Œ≤_ X Y).hom := by aesop_cat

@[simp, reassoc]
lemma lift_snd_comp_fst_comp {W X Y Z : C} (g : W ‚ü∂ X) (g' : Y ‚ü∂ Z) :
    lift (snd _ _ ‚â´ g') (fst _ _ ‚â´ g) = (Œ≤_ _ _).hom ‚â´ (g' ‚äó g) := by aesop_cat

@[reassoc (attr := simp)]
lemma lift_braiding_hom {T X Y : C} (f : T ‚ü∂ X) (g : T ‚ü∂ Y) :
    lift f g ‚â´ (Œ≤_ X Y).hom = lift g f := by aesop

@[reassoc (attr := simp)]
lemma lift_braiding_inv {T X Y : C} (f : T ‚ü∂ X) (g : T ‚ü∂ Y) :
    lift f g ‚â´ (Œ≤_ Y X).inv = lift g f := by aesop

instance (priority := low) [BraidedCategory C] : SymmetricCategory C where

/-- `ChosenFiniteProducts` implies `BraidedCategory`.
This is not an instance to prevent diamonds. -/
def _root_.CategoryTheory.BraidedCategory.ofChosenFiniteProducts : BraidedCategory C where
  braiding X Y := { hom := lift (snd _ _) (fst _ _), inv := lift (snd _ _) (fst _ _) }

instance : Nonempty (BraidedCategory C) :=
  ‚ü®.ofChosenFiniteProducts‚ü©

instance : Subsingleton (BraidedCategory C) where
  allEq
  | ‚ü®e‚ÇÅ, a‚ÇÅ, b‚ÇÅ, c‚ÇÅ, d‚ÇÅ‚ü©, ‚ü®e‚ÇÇ, a‚ÇÇ, b‚ÇÇ, c‚ÇÇ, d‚ÇÇ‚ü© => by
      congr
      ext
      ¬∑ exact (@braiding_hom_fst C _ ‚Äπ_‚Ä∫ ‚ü®e‚ÇÅ, a‚ÇÅ, b‚ÇÅ, c‚ÇÅ, d‚ÇÅ‚ü© ..).trans
          (@braiding_hom_fst C _ ‚Äπ_‚Ä∫ ‚ü®e‚ÇÇ, a‚ÇÇ, b‚ÇÇ, c‚ÇÇ, d‚ÇÇ‚ü© ..).symm
      ¬∑ exact (@braiding_hom_snd C _ ‚Äπ_‚Ä∫ ‚ü®e‚ÇÅ, a‚ÇÅ, b‚ÇÅ, c‚ÇÅ, d‚ÇÅ‚ü© ..).trans
          (@braiding_hom_snd C _ ‚Äπ_‚Ä∫ ‚ü®e‚ÇÇ, a‚ÇÇ, b‚ÇÇ, c‚ÇÇ, d‚ÇÇ‚ü© ..).symm

instance : Subsingleton (SymmetricCategory C) where
  allEq := by rintro ‚ü®_‚ü© ‚ü®_‚ü©; congr; exact Subsingleton.elim _ _

end BraidedCategory

/--
Construct an instance of `ChosenFiniteProducts C` given
a terminal object and limit cones over arbitrary pairs of objects.
-/
abbrev ofChosenFiniteProducts
    (ùíØ : LimitCone (Functor.empty C)) (‚Ñ¨ : (X Y : C) ‚Üí LimitCone (pair X Y)) :
    ChosenFiniteProducts C where
  __ := monoidalOfChosenFiniteProducts ùíØ ‚Ñ¨
  isTerminalTensorUnit :=
    .ofUniqueHom (ùíØ.isLimit.lift <| asEmptyCone ¬∑) fun _ _ ‚Ü¶ ùíØ.isLimit.hom_ext (by simp)
  fst X Y := BinaryFan.fst (‚Ñ¨ X Y).cone
  snd X Y := BinaryFan.snd (‚Ñ¨ X Y).cone
  tensorProductIsBinaryProduct X Y := BinaryFan.IsLimit.mk _
    (fun f g ‚Ü¶ (BinaryFan.IsLimit.lift' (‚Ñ¨ X Y).isLimit f g).1)
    (fun f g ‚Ü¶ (BinaryFan.IsLimit.lift' (‚Ñ¨ X Y).isLimit f g).2.1)
    (fun f g ‚Ü¶ (BinaryFan.IsLimit.lift' (‚Ñ¨ X Y).isLimit f g).2.2)
    (fun f g m hf hg ‚Ü¶
      BinaryFan.IsLimit.hom_ext (‚Ñ¨ X Y).isLimit (by simpa using hf) (by simpa using hg))
  fst_def X Y := (((‚Ñ¨ X ùíØ.cone.pt).isLimit.fac
    (BinaryFan.mk _ _) ‚ü®.left‚ü©).trans (Category.comp_id _)).symm
  snd_def X Y := (((‚Ñ¨ ùíØ.cone.pt Y).isLimit.fac
    (BinaryFan.mk _ _) ‚ü®.right‚ü©).trans (Category.comp_id _)).symm

/--
Construct an instance of `ChosenFiniteProducts C` given an instance of `HasFiniteProducts C`.
-/
noncomputable abbrev ofFiniteProducts (C : Type u) [Category.{v} C] [HasFiniteProducts C] :
    ChosenFiniteProducts C :=
  .ofChosenFiniteProducts
    (getLimitCone (Functor.empty C)) (getLimitCone <| pair ¬∑ ¬∑)

instance (priority := 100) : Limits.HasFiniteProducts C :=
  letI : ‚àÄ (X Y : C), Limits.HasLimit (Limits.pair X Y) := fun _ _ =>
    .mk ‚ü®_, tensorProductIsBinaryProduct _ _‚ü©
  letI : Limits.HasBinaryProducts C := Limits.hasBinaryProducts_of_hasLimit_pair _
  letI : Limits.HasTerminal C := Limits.hasTerminal_of_unique (ùüô_ C)
  hasFiniteProducts_of_has_binary_and_terminal

section ChosenFiniteProductsComparison

variable {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D] [ChosenFiniteProducts D] (F : C ‚•§ D)
variable {E : Type u‚ÇÇ} [Category.{v‚ÇÇ} E] [ChosenFiniteProducts E] (G : D ‚•§ E)

section terminalComparison

/-- When `C` and `D` have chosen finite products and `F : C ‚•§ D` is any functor,
`terminalComparison F` is the unique map `F (ùüô_ C) ‚ü∂ ùüô_ D`. -/
abbrev terminalComparison : F.obj (ùüô_ C) ‚ü∂ ùüô_ D := toUnit _

@[reassoc]
lemma map_toUnit_comp_terminalComparison (A : C) :
    F.map (toUnit A) ‚â´ terminalComparison F = toUnit _ := toUnit_unique _ _

@[deprecated (since := "2025-04-09")]
alias map_toUnit_comp_terminalCompariso := map_toUnit_comp_terminalComparison

@[deprecated (since := "2025-04-09")]
alias map_toUnit_comp_terminalCompariso_assoc := map_toUnit_comp_terminalComparison_assoc

open Limits

/-- If `terminalComparison F` is an Iso, then `F` preserves terminal objects. -/
lemma preservesLimit_empty_of_isIso_terminalComparison [IsIso (terminalComparison F)] :
    PreservesLimit (Functor.empty.{0} C) F := by
  apply preservesLimit_of_preserves_limit_cone isTerminalTensorUnit
  apply isLimitChangeEmptyCone D isTerminalTensorUnit
  exact asIso (terminalComparison F)|>.symm

/-- If `F` preserves terminal objects, then `terminalComparison F` is an isomorphism. -/
noncomputable def preservesTerminalIso [h : PreservesLimit (Functor.empty.{0} C) F] :
    F.obj (ùüô_ C) ‚âÖ ùüô_ D :=
  (isLimitChangeEmptyCone D (isLimitOfPreserves _ isTerminalTensorUnit) (asEmptyCone (F.obj (ùüô_ C)))
    (Iso.refl _)).conePointUniqueUpToIso isTerminalTensorUnit

@[simp]
lemma preservesTerminalIso_hom [PreservesLimit (Functor.empty.{0} C) F] :
    (preservesTerminalIso F).hom = terminalComparison F := toUnit_unique _ _

instance terminalComparison_isIso_of_preservesLimits [PreservesLimit (Functor.empty.{0} C) F] :
    IsIso (terminalComparison F) := by
  rw [‚Üê preservesTerminalIso_hom]
  infer_instance

@[simp]
lemma preservesTerminalIso_id : preservesTerminalIso (ùü≠ C) = .refl _ := by
  aesop_cat

@[simp]
lemma preservesTerminalIso_comp [PreservesLimit (Functor.empty.{0} C) F]
    [PreservesLimit (Functor.empty.{0} D) G] [PreservesLimit (Functor.empty.{0} C) (F ‚ãô G)] :
    preservesTerminalIso (F ‚ãô G) =
      G.mapIso (preservesTerminalIso F) ‚â™‚â´ preservesTerminalIso G := by
  aesop_cat

end terminalComparison

section prodComparison

variable (A B : C)

/-- When `C` and `D` have chosen finite products and `F : C ‚•§ D` is any functor,
`prodComparison F A B` is the canonical comparison morphism from `F (A ‚äó B)` to `F(A) ‚äó F(B)`. -/
def prodComparison (A B : C) : F.obj (A ‚äó B) ‚ü∂ F.obj A ‚äó F.obj B :=
  lift (F.map (fst A B)) (F.map (snd A B))

@[reassoc (attr := simp)]
theorem prodComparison_fst : prodComparison F A B ‚â´ fst _ _ = F.map (fst A B) :=
  lift_fst _ _

@[reassoc (attr := simp)]
theorem prodComparison_snd : prodComparison F A B ‚â´ snd _ _ = F.map (snd A B) :=
  lift_snd _ _

@[reassoc (attr := simp)]
theorem inv_prodComparison_map_fst [IsIso (prodComparison F A B)] :
    inv (prodComparison F A B) ‚â´ F.map (fst _ _) = fst _ _ := by simp [IsIso.inv_comp_eq]

@[reassoc (attr := simp)]
theorem inv_prodComparison_map_snd [IsIso (prodComparison F A B)] :
    inv (prodComparison F A B) ‚â´ F.map (snd _ _) = snd _ _ := by simp [IsIso.inv_comp_eq]

variable {A B} {A' B' : C}

/-- Naturality of the `prodComparison` morphism in both arguments. -/
@[reassoc]
theorem prodComparison_natural (f : A ‚ü∂ A') (g : B ‚ü∂ B') :
    F.map (f ‚äó g) ‚â´ prodComparison F A' B' =
      prodComparison F A B ‚â´ (F.map f ‚äó F.map g) := by
  apply hom_ext <;>
  simp only [Category.assoc, prodComparison_fst, tensorHom_fst, prodComparison_fst_assoc,
    prodComparison_snd, tensorHom_snd, prodComparison_snd_assoc, ‚Üê F.map_comp]

/-- Naturality of the `prodComparison` morphism in the right argument. -/
@[reassoc]
theorem prodComparison_natural_whiskerLeft (g : B ‚ü∂ B') :
    F.map (A ‚óÅ g) ‚â´ prodComparison F A B' =
      prodComparison F A B ‚â´ (F.obj A ‚óÅ F.map g) := by
  ext <;> simp [‚Üê Functor.map_comp]

/-- Naturality of the `prodComparison` morphism in the left argument. -/
@[reassoc]
theorem prodComparison_natural_whiskerRight (f : A ‚ü∂ A') :
    F.map (f ‚ñ∑ B) ‚â´ prodComparison F A' B =
      prodComparison F A B ‚â´ (F.map f ‚ñ∑ F.obj B) := by
  ext <;> simp [‚Üê Functor.map_comp]

section
variable [IsIso (prodComparison F A B)]

/-- If the product comparison morphism is an iso, its inverse is natural in both argument. -/
@[reassoc]
theorem prodComparison_inv_natural (f : A ‚ü∂ A') (g : B ‚ü∂ B') [IsIso (prodComparison F A' B')] :
    inv (prodComparison F A B) ‚â´ F.map (f ‚äó g) =
      (F.map f ‚äó F.map g) ‚â´ inv (prodComparison F A' B') := by
  rw [IsIso.eq_comp_inv, Category.assoc, IsIso.inv_comp_eq, prodComparison_natural]

/-- If the product comparison morphism is an iso, its inverse is natural in the right argument. -/
@[reassoc]
theorem prodComparison_inv_natural_whiskerLeft (g : B ‚ü∂ B') [IsIso (prodComparison F A B')] :
    inv (prodComparison F A B) ‚â´ F.map (A ‚óÅ g) =
      (F.obj A ‚óÅ F.map g) ‚â´ inv (prodComparison F A B') := by
  rw [IsIso.eq_comp_inv, Category.assoc, IsIso.inv_comp_eq, prodComparison_natural_whiskerLeft]

/-- If the product comparison morphism is an iso, its inverse is natural in the left argument. -/
@[reassoc]
theorem prodComparison_inv_natural_whiskerRight (f : A ‚ü∂ A') [IsIso (prodComparison F A' B)] :
    inv (prodComparison F A B) ‚â´ F.map (f ‚ñ∑ B) =
      (F.map f ‚ñ∑ F.obj B) ‚â´ inv (prodComparison F A' B) := by
  rw [IsIso.eq_comp_inv, Category.assoc, IsIso.inv_comp_eq, prodComparison_natural_whiskerRight]

end

theorem prodComparison_comp {E : Type u‚ÇÇ} [Category.{v‚ÇÇ} E] [ChosenFiniteProducts E] (G : D ‚•§ E) :
    prodComparison (F ‚ãô G) A B =
      G.map (prodComparison F A B) ‚â´ prodComparison G (F.obj A) (F.obj B) := by
  unfold prodComparison
  ext <;> simp [‚Üê G.map_comp]

@[simp]
lemma prodComparison_id :
    prodComparison (ùü≠ C) A B = ùüô (A ‚äó B) := lift_fst_snd

/-- The product comparison morphism from `F(A ‚äó -)` to `FA ‚äó F-`, whose components are given by
`prodComparison`. -/
@[simps]
def prodComparisonNatTrans (A : C) :
    (curriedTensor C).obj A ‚ãô F ‚ü∂ F ‚ãô (curriedTensor D).obj (F.obj A) where
  app B := prodComparison F A B
  naturality x y f := by
    apply hom_ext <;>
    simp only [Functor.comp_obj, curriedTensor_obj_obj,
      Functor.comp_map, curriedTensor_obj_map, Category.assoc, prodComparison_fst, whiskerLeft_fst,
      prodComparison_snd, prodComparison_snd_assoc, whiskerLeft_snd, ‚Üê F.map_comp]

theorem prodComparisonNatTrans_comp {E : Type u‚ÇÇ} [Category.{v‚ÇÇ} E] [ChosenFiniteProducts E]
    (G : D ‚•§ E) : prodComparisonNatTrans (F ‚ãô G) A =
      whiskerRight (prodComparisonNatTrans F A) G ‚â´
        whiskerLeft F (prodComparisonNatTrans G (F.obj A)) := by ext; simp [prodComparison_comp]

@[simp]
lemma prodComparisonNatTrans_id :
    prodComparisonNatTrans (ùü≠ C) A = ùüô _ := by ext; simp

/-- The product comparison morphism from `F(- ‚äó -)` to `F- ‚äó F-`, whose components are given by
`prodComparison`. -/
@[simps]
def prodComparisonBifunctorNatTrans :
    curriedTensor C ‚ãô (whiskeringRight _ _ _).obj F ‚ü∂
      F ‚ãô curriedTensor D ‚ãô (whiskeringLeft _ _ _).obj F where
  app A := prodComparisonNatTrans F A
  naturality x y f := by
    ext z
    apply hom_ext <;> simp [‚Üê Functor.map_comp]

variable {E : Type u‚ÇÇ} [Category.{v‚ÇÇ} E]
    [ChosenFiniteProducts E] (G : D ‚•§ E)

theorem prodComparisonBifunctorNatTrans_comp {E : Type u‚ÇÇ} [Category.{v‚ÇÇ} E]
    [ChosenFiniteProducts E] (G : D ‚•§ E) : prodComparisonBifunctorNatTrans (F ‚ãô G) =
      whiskerRight (prodComparisonBifunctorNatTrans F) ((whiskeringRight _ _ _).obj G) ‚â´
        whiskerLeft F (whiskerRight (prodComparisonBifunctorNatTrans G)
          ((whiskeringLeft _ _ _).obj F)) := by ext; simp [prodComparison_comp]

instance (A : C) [‚àÄ B, IsIso (prodComparison F A B)] : IsIso (prodComparisonNatTrans F A) := by
  letI : ‚àÄ X, IsIso ((prodComparisonNatTrans F A).app X) := by assumption
  apply NatIso.isIso_of_isIso_app

instance [‚àÄ A B, IsIso (prodComparison F A B)] : IsIso (prodComparisonBifunctorNatTrans F) := by
  letI : ‚àÄ X, IsIso ((prodComparisonBifunctorNatTrans F).app X) :=
    fun _ ‚Ü¶ by dsimp; apply NatIso.isIso_of_isIso_app
  apply NatIso.isIso_of_isIso_app

open Limits
section PreservesLimitPairs

section
variable (A B)
variable [PreservesLimit (pair A B) F]

/-- If `F` preserves the limit of the pair `(A, B)`, then the binary fan given by
`(F.map fst A B, F.map (snd A B))` is a limit cone. -/
noncomputable def isLimitChosenFiniteProductsOfPreservesLimits :
    IsLimit <| BinaryFan.mk (F.map (fst A B)) (F.map (snd A B)) :=
  mapIsLimitOfPreservesOfIsLimit F (fst _ _) (snd _ _) <|
    (tensorProductIsBinaryProduct A B).ofIsoLimit <|
      isoBinaryFanMk (BinaryFan.mk (fst A B) (snd A B))

/-- If `F` preserves the limit of the pair `(A, B)`, then `prodComparison F A B` is an isomorphism.
-/
noncomputable def prodComparisonIso : F.obj (A ‚äó B) ‚âÖ F.obj A ‚äó F.obj B :=
  IsLimit.conePointUniqueUpToIso (isLimitChosenFiniteProductsOfPreservesLimits F A B)
    (tensorProductIsBinaryProduct _ _)

@[simp]
lemma prodComparisonIso_hom : (prodComparisonIso F A B).hom = prodComparison F A B := by
  rfl

instance isIso_prodComparison_of_preservesLimit_pair : IsIso (prodComparison F A B) := by
  rw [‚Üê prodComparisonIso_hom]
  infer_instance

@[simp] lemma prodComparisonIso_id  : prodComparisonIso (ùü≠ C) A B = .refl _ := by ext <;> simp

@[simp]
lemma prodComparisonIso_comp [PreservesLimit (pair A B) (F ‚ãô G)]
    [PreservesLimit (pair (F.obj A) (F.obj B)) G] :
    prodComparisonIso (F ‚ãô G) A B =
      G.mapIso (prodComparisonIso F A B) ‚â™‚â´ prodComparisonIso G (F.obj A) (F.obj B) := by
  ext <;> simp [ChosenFiniteProducts.prodComparison, ‚Üê G.map_comp]

end

/-- The natural isomorphism `F(A ‚äó -) ‚âÖ FA ‚äó F-`, provided each `prodComparison F A B` is an
isomorphism (as `B` changes). -/
@[simps! hom inv]
noncomputable def prodComparisonNatIso (A : C) [‚àÄ B, PreservesLimit (pair A B) F] :
    (curriedTensor C).obj A ‚ãô F ‚âÖ F ‚ãô (curriedTensor D).obj (F.obj A) :=
  asIso (prodComparisonNatTrans F A)

/-- The natural isomorphism of bifunctors `F(- ‚äó -) ‚âÖ F- ‚äó F-`, provided each
`prodComparison F A B` is an isomorphism. -/
@[simps! hom inv]
noncomputable def prodComparisonBifunctorNatIso [‚àÄ A B, PreservesLimit (pair A B) F] :
    curriedTensor C ‚ãô (whiskeringRight _ _ _).obj F ‚âÖ
      F ‚ãô curriedTensor D ‚ãô (whiskeringLeft _ _ _).obj F :=
  asIso (prodComparisonBifunctorNatTrans F)

end PreservesLimitPairs

section ProdComparisonIso

/-- If `prodComparison F A B` is an isomorphism, then `F` preserves the limit of `pair A B`. -/
lemma preservesLimit_pair_of_isIso_prodComparison (A B : C)
    [IsIso (prodComparison F A B)] :
    PreservesLimit (pair A B) F := by
  apply preservesLimit_of_preserves_limit_cone (tensorProductIsBinaryProduct A B)
  refine IsLimit.equivOfNatIsoOfIso (pairComp A B F) _
    ((BinaryFan.mk (fst (F.obj A) (F.obj B)) (snd _ _)).extend (prodComparison F A B))
      (BinaryFan.ext (by exact Iso.refl _) ?_ ?_) |>.invFun
      (IsLimit.extendIso _ (tensorProductIsBinaryProduct (F.obj A) (F.obj B)))
  ¬∑ dsimp only [BinaryFan.fst]
    simp [pairComp]
  ¬∑ dsimp only [BinaryFan.snd]
    simp [pairComp]

/-- If `prodComparison F A B` is an isomorphism for all `A B` then `F` preserves limits of shape
`Discrete (WalkingPair)`. -/
lemma preservesLimitsOfShape_discrete_walkingPair_of_isIso_prodComparison
    [‚àÄ A B, IsIso (prodComparison F A B)] : PreservesLimitsOfShape (Discrete WalkingPair) F := by
  constructor
  intro K
  refine @preservesLimit_of_iso_diagram _ _ _ _ _ _ _ _ _ (diagramIsoPair K).symm ?_
  apply preservesLimit_pair_of_isIso_prodComparison

end ProdComparisonIso

end prodComparison

end ChosenFiniteProductsComparison

open Limits

variable {P : ObjectProperty C}

-- TODO: Introduce `ClosedUnderFiniteProducts`?
/-- The restriction of a cartesian-monoidal category along an object property that's closed under
finite products is cartesian-monoidal. -/
noncomputable def fullSubcategory (hP‚ÇÄ : ClosedUnderLimitsOfShape (Discrete PEmpty) P)
    (hP‚ÇÇ : ClosedUnderLimitsOfShape (Discrete WalkingPair) P) :
    ChosenFiniteProducts P.FullSubcategory where
  __ := MonoidalCategory.fullSubcategory P (hP‚ÇÄ isTerminalTensorUnit <| by simp)
    fun X Y hX hY ‚Ü¶ hP‚ÇÇ (tensorProductIsBinaryProduct X Y) (by rintro ‚ü®_ | _‚ü© <;> simp [hX, hY])
  isTerminalTensorUnit := .ofUniqueHom (fun X ‚Ü¶ toUnit X.1) fun _ _ ‚Ü¶ by ext
  fst X Y := fst X.1 Y.1
  snd X Y := snd X.1 Y.1
  tensorProductIsBinaryProduct X Y :=
    BinaryFan.IsLimit.mk _ (lift (C := C)) (lift_fst (C := C)) (lift_snd (C := C))
      (by rintro T f g m rfl rfl; symm; exact lift_comp_fst_snd _)
  fst_def X Y := fst_def X.1 Y.1
  snd_def X Y := snd_def X.1 Y.1

end ChosenFiniteProducts

open MonoidalCategory ChosenFiniteProducts

variable
  {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] [ChosenFiniteProducts C]
  {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] [ChosenFiniteProducts D]
  {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E] [ChosenFiniteProducts E]
  (F : C ‚•§ D) (G : D ‚•§ E) {X Y Z : C}

open Functor.LaxMonoidal Functor.OplaxMonoidal
open Limits (PreservesFiniteProducts)

namespace Functor.OplaxMonoidal
variable [F.OplaxMonoidal]

lemma Œ∑_of_chosenFiniteProducts :
    Œ∑ F = ChosenFiniteProducts.terminalComparison F := toUnit_unique ..

@[reassoc (attr := simp)]
lemma Œ¥_fst (X Y : C) :
    Œ¥ F X Y ‚â´ fst _ _ = F.map (fst _ _) := by
  trans F.map (X ‚óÅ toUnit Y) ‚â´ F.map (œÅ_ X).hom
  ¬∑ rw [‚Üê whiskerLeft_fst _ (F.map (toUnit Y)), Œ¥_natural_right_assoc]
    simp [‚Üê OplaxMonoidal.right_unitality_hom, rightUnitor_hom (F.obj X)]
  ¬∑ simp [‚Üê Functor.map_comp, rightUnitor_hom]

@[reassoc (attr := simp)]
lemma Œ¥_snd (X Y : C) :
    Œ¥ F X Y ‚â´ snd _ _ = F.map (snd _ _) := by
  trans F.map (toUnit X ‚ñ∑ Y) ‚â´ F.map (Œª_ Y).hom
  ¬∑ rw [‚Üê whiskerRight_snd (F.map (toUnit X)), Œ¥_natural_left_assoc]
    simp [‚Üê OplaxMonoidal.left_unitality_hom, leftUnitor_hom (F.obj Y)]
  ¬∑ simp [‚Üê Functor.map_comp, leftUnitor_hom]

@[reassoc (attr := simp)]
lemma lift_Œ¥ (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) : F.map (lift f g) ‚â´ Œ¥ F _ _ = lift (F.map f) (F.map g) := by
  ext <;> simp [‚Üê map_comp]

lemma Œ¥_of_chosenFiniteProducts (X Y : C) :
    Œ¥ F X Y = ChosenFiniteProducts.prodComparison F X Y := by aesop_cat

variable [PreservesFiniteProducts F]

instance : IsIso (Œ∑ F) :=
  Œ∑_of_chosenFiniteProducts F ‚ñ∏ terminalComparison_isIso_of_preservesLimits F

instance (X Y : C) : IsIso (Œ¥ F X Y) :=
  Œ¥_of_chosenFiniteProducts F X Y ‚ñ∏ isIso_prodComparison_of_preservesLimit_pair F X Y

omit [F.OplaxMonoidal] in
/-- Any functor between cartesian-monoidal categories is oplax monoidal.

This is not made an instance because it would create a diamond for the oplax monoidal structure on
the identity and composition of functors. -/
def ofChosenFiniteProducts (F : C ‚•§ D) : F.OplaxMonoidal where
  Œ∑' := terminalComparison F
  Œ¥' X Y := prodComparison F X Y
  Œ¥'_natural_left f X' := by ext <;> simp [‚Üê Functor.map_comp]
  Œ¥'_natural_right X g := by ext <;> simp [‚Üê Functor.map_comp]
  oplax_associativity' _ _ _ := by ext <;> simp [‚Üê Functor.map_comp]
  oplax_left_unitality' _ := by ext; simp [‚Üê Functor.map_comp]
  oplax_right_unitality' _ := by ext; simp [‚Üê Functor.map_comp]

omit [F.OplaxMonoidal] in
/-- Any functor between cartesian-monoidal categories is oplax monoidal in a unique way. -/
instance : Subsingleton F.OplaxMonoidal where
  allEq a b := by
    ext1
    ¬∑ exact toUnit_unique _ _
    ¬∑ ext1; ext1; rw [‚Üê Œ¥, ‚Üê Œ¥, Œ¥_of_chosenFiniteProducts, Œ¥_of_chosenFiniteProducts]

end OplaxMonoidal

namespace Monoidal
variable [F.Monoidal] [G.Monoidal]

@[reassoc (attr := simp)]
lemma toUnit_Œµ (X : C) : toUnit (F.obj X) ‚â´ Œµ F = F.map (toUnit X) := by
  rw [‚Üê cancel_mono (ŒµIso F).inv]; exact toUnit_unique ..

@[reassoc (attr := simp)]
lemma lift_Œº (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) : lift (F.map f) (F.map g) ‚â´ Œº F _ _ = F.map (lift f g) :=
  (cancel_mono (ŒºIso _ _ _).inv).1 (by simp)

@[reassoc (attr := simp)]
lemma Œº_fst (X Y : C) : Œº F X Y ‚â´ F.map (fst X Y) = fst (F.obj X) (F.obj Y) :=
  (cancel_epi (ŒºIso _ _ _).inv).1 (by simp)

@[reassoc (attr := simp)]
lemma Œº_snd (X Y : C) : Œº F X Y ‚â´ F.map (snd X Y) = snd (F.obj X) (F.obj Y) :=
  (cancel_epi (ŒºIso _ _ _).inv).1 (by simp)

attribute [-instance] Functor.LaxMonoidal.comp Functor.Monoidal.instComp in
@[reassoc]
lemma Œº_comp [(F ‚ãô G).Monoidal] (X Y : C) : Œº (F ‚ãô G) X Y = Œº G _ _ ‚â´ G.map (Œº F X Y) := by
  rw [‚Üê cancel_mono (ŒºIso _ _ _).inv]; ext <;> simp [‚Üê Functor.comp_obj, ‚Üê Functor.map_comp]

variable [PreservesFiniteProducts F]

lemma Œµ_of_chosenFiniteProducts : Œµ F = (preservesTerminalIso F).inv := by
  change (ŒµIso F).symm.inv = _; congr; ext

lemma Œº_of_chosenFiniteProducts (X Y : C) : Œº F X Y = (prodComparisonIso F X Y).inv := by
  change (ŒºIso F X Y).symm.inv = _; congr; ext : 1; simpa using Œ¥_of_chosenFiniteProducts F X Y

omit [F.Monoidal] in
/-- A finite-product-preserving functor between cartesian monoidal categories is monoidal.

This is the variant where `F.OplaxMonoidal` is an argument instead.

This is not made an instance because it would create a diamond for the monoidal structure on
the identity and composition of functors. -/
noncomputable def ofChosenFiniteProducts'
    (F : C ‚•§ D) [PreservesFiniteProducts F] [F.OplaxMonoidal] : F.Monoidal :=
  .ofOplaxMonoidal F

attribute [local instance] Functor.OplaxMonoidal.ofChosenFiniteProducts in
omit [F.Monoidal] in
/-- A finite-product-preserving functor between cartesian monoidal categories is monoidal.

This is not made an instance because it would create a diamond for the monoidal structure on
the identity and composition of functors. -/
noncomputable def ofChosenFiniteProducts (F : C ‚•§ D) [PreservesFiniteProducts F] : F.Monoidal :=
  .ofOplaxMonoidal F

instance : Subsingleton F.Monoidal := (toOplaxMonoidal_injective F).subsingleton

end Monoidal

namespace Monoidal

instance [F.Monoidal] : PreservesFiniteProducts F :=
  have (A B) : IsIso (ChosenFiniteProducts.prodComparison F A B) :=
    Œ¥_of_chosenFiniteProducts F A B ‚ñ∏ inferInstance
  have : IsIso (ChosenFiniteProducts.terminalComparison F) :=
    Œ∑_of_chosenFiniteProducts F ‚ñ∏ inferInstance
  have := preservesLimitsOfShape_discrete_walkingPair_of_isIso_prodComparison F
  have := preservesLimit_empty_of_isIso_terminalComparison F
  have := Limits.preservesLimitsOfShape_pempty_of_preservesTerminal F
  .of_preserves_binary_and_terminal _

attribute [local instance] OplaxMonoidal.ofChosenFiniteProducts in
/--
A functor between cartesian monoidal categories is monoidal iff it preserves finite products.
-/
lemma nonempty_monoidal_iff_preservesFiniteProducts :
    Nonempty F.Monoidal ‚Üî PreservesFiniteProducts F :=
  ‚ü®fun ‚ü®_‚ü© ‚Ü¶ inferInstance, fun _ ‚Ü¶ ‚ü®ofChosenFiniteProducts F‚ü©‚ü©

end Monoidal

namespace Braided
variable [BraidedCategory C] [BraidedCategory D]

/-- A finite-product-preserving functor between cartesian monoidal categories is braided.

This is the variant where `F.Monoidal` is an argument instead.

This is not made an instance because it would create a diamond for the braided structure on
the identity and composition of functors. -/
noncomputable def ofChosenFiniteProducts' [F.Monoidal] : F.Braided where
  braided X Y := by rw [‚Üê cancel_mono (Monoidal.ŒºIso _ _ _).inv]; ext <;> simp [‚Üê F.map_comp]

attribute [local instance] Functor.Monoidal.ofChosenFiniteProducts in
/-- A finite-product-preserving functor between cartesian monoidal categories is braided.

This is not made an instance because it would create a diamond for the monoidal structure on
the identity and composition of functors. -/
noncomputable def ofChosenFiniteProducts (F : C ‚•§ D) [PreservesFiniteProducts F] : F.Braided :=
  .ofChosenFiniteProducts' F

instance : Subsingleton F.Braided := (Braided.toMonoidal_injective F).subsingleton

end Braided

@[deprecated (since := "2025-04-24")]
alias oplaxMonoidalOfChosenFiniteProducts := OplaxMonoidal.ofChosenFiniteProducts

@[deprecated (since := "2025-04-24")]
alias monoidalOfChosenFiniteProducts := Monoidal.ofChosenFiniteProducts

@[deprecated (since := "2025-04-24")]
alias braidedOfChosenFiniteProducts := Braided.ofChosenFiniteProducts

namespace EssImageSubcategory
variable [F.Full] [F.Faithful] [PreservesFiniteProducts F] {T X Y Z : F.EssImageSubcategory}

@[simps!]
noncomputable instance instChosenFiniteProducts :
    ChosenFiniteProducts F.EssImageSubcategory :=
  .fullSubcategory (.essImage _) (.essImage _)

lemma tensor_obj (X Y : F.EssImageSubcategory) : (X ‚äó Y).obj = X.obj ‚äó Y.obj := rfl

lemma fst_def (X Y : F.EssImageSubcategory) : fst X Y = fst X.obj Y.obj := rfl
lemma snd_def (X Y : F.EssImageSubcategory) : snd X Y = snd X.obj Y.obj := rfl
lemma lift_def (f : T ‚ü∂ X) (g : T ‚ü∂ Y) : lift f g = lift (T := T.1) f g := rfl

lemma whiskerLeft_def (X : F.EssImageSubcategory) (f : Y ‚ü∂ Z) : X ‚óÅ f = X.obj ‚óÅ f := rfl
lemma whiskerRight_def (f : Y ‚ü∂ Z) (X : F.EssImageSubcategory) :
    f ‚ñ∑ X = MonoidalCategoryStruct.whiskerRight (C := D) f X.obj := rfl

lemma associator_hom_def (X Y Z : F.EssImageSubcategory) :
    (Œ±_ X Y Z).hom = (Œ±_ X.obj Y.obj Z.obj).hom := rfl

lemma associator_inv_def (X Y Z : F.EssImageSubcategory) :
    (Œ±_ X Y Z).inv = (Œ±_ X.obj Y.obj Z.obj).inv := rfl

lemma toUnit_def (X : F.EssImageSubcategory) : toUnit X = toUnit X.obj := toUnit_unique ..

end Functor.EssImageSubcategory

namespace NatTrans
variable (F G : C ‚•§ D) [F.Monoidal] [G.Monoidal]

instance isMonoidal_of_chosenFiniteProducts (Œ± : F ‚ü∂ G) : IsMonoidal Œ± where
  unit := (cancel_mono (Functor.Monoidal.ŒµIso _).inv).1 (toUnit_unique _ _)
  tensor {X Y} := by
    rw [‚Üê cancel_mono (Functor.Monoidal.ŒºIso _ _ _).inv]
    rw [‚Üê cancel_epi (Functor.Monoidal.ŒºIso _ _ _).inv]
    apply ChosenFiniteProducts.hom_ext <;> simp

end NatTrans

end CategoryTheory
