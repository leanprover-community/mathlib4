/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Limits.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Limits.Shapes.Terminal
import Mathlib.CategoryTheory.Subobject.MonoOver

#align_import category_theory.subterminal from "leanprover-community/mathlib"@"bb103f356534a9a7d3596a672097e375290a4c3a"

/-!
# Subterminal objects

Subterminal objects are the objects which can be thought of as subobjects of the terminal object.
In fact, the definition can be constructed to not require a terminal object, by defining `A` to be
subterminal iff for any `Z`, there is at most one morphism `Z âŸ¶ A`.
An alternate definition is that the diagonal morphism `A âŸ¶ A â¨¯ A` is an isomorphism.
In this file we define subterminal objects and show the equivalence of these three definitions.

We also construct the subcategory of subterminal objects.

## TODO

* Define exponential ideals, and show this subcategory is an exponential ideal.
* Use the above to show that in a locally cartesian closed category, every subobject lattice
  is cartesian closed (equivalently, a Heyting algebra).

-/


universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

noncomputable section

namespace CategoryTheory

open Limits Category

variable {C : Type uâ‚} [Category.{vâ‚} C] {A : C}

/-- An object `A` is subterminal iff for any `Z`, there is at most one morphism `Z âŸ¶ A`. -/
def IsSubterminal (A : C) : Prop :=
  âˆ€ â¦ƒZ : Câ¦„ (f g : Z âŸ¶ A), f = g
#align category_theory.is_subterminal CategoryTheory.IsSubterminal

theorem IsSubterminal.def : IsSubterminal A â†” âˆ€ â¦ƒZ : Câ¦„ (f g : Z âŸ¶ A), f = g :=
  Iff.rfl
#align category_theory.is_subterminal.def CategoryTheory.IsSubterminal.def

/-- If `A` is subterminal, the unique morphism from it to a terminal object is a monomorphism.
The converse of `isSubterminal_of_mono_isTerminal_from`.
-/
theorem IsSubterminal.mono_isTerminal_from (hA : IsSubterminal A) {T : C} (hT : IsTerminal T) :
    Mono (hT.from A) :=
  { right_cancellation := fun _ _ _ => hA _ _ }
#align category_theory.is_subterminal.mono_is_terminal_from CategoryTheory.IsSubterminal.mono_isTerminal_from

/-- If `A` is subterminal, the unique morphism from it to the terminal object is a monomorphism.
The converse of `isSubterminal_of_mono_terminal_from`.
-/
theorem IsSubterminal.mono_terminal_from [HasTerminal C] (hA : IsSubterminal A) :
    Mono (terminal.from A) :=
  hA.mono_isTerminal_from terminalIsTerminal
#align category_theory.is_subterminal.mono_terminal_from CategoryTheory.IsSubterminal.mono_terminal_from

/-- If the unique morphism from `A` to a terminal object is a monomorphism, `A` is subterminal.
The converse of `IsSubterminal.mono_isTerminal_from`.
-/
theorem isSubterminal_of_mono_isTerminal_from {T : C} (hT : IsTerminal T) [Mono (hT.from A)] :
    IsSubterminal A := fun Z f g => by
  rw [â† cancel_mono (hT.from A)]
  -- âŠ¢ f â‰« IsTerminal.from hT A = g â‰« IsTerminal.from hT A
  apply hT.hom_ext
  -- ðŸŽ‰ no goals
#align category_theory.is_subterminal_of_mono_is_terminal_from CategoryTheory.isSubterminal_of_mono_isTerminal_from

/-- If the unique morphism from `A` to the terminal object is a monomorphism, `A` is subterminal.
The converse of `IsSubterminal.mono_terminal_from`.
-/
theorem isSubterminal_of_mono_terminal_from [HasTerminal C] [Mono (terminal.from A)] :
    IsSubterminal A := fun Z f g => by
  rw [â† cancel_mono (terminal.from A)]
  -- âŠ¢ f â‰« terminal.from A = g â‰« terminal.from A
  apply Subsingleton.elim
  -- ðŸŽ‰ no goals
#align category_theory.is_subterminal_of_mono_terminal_from CategoryTheory.isSubterminal_of_mono_terminal_from

theorem isSubterminal_of_isTerminal {T : C} (hT : IsTerminal T) : IsSubterminal T := fun _ _ _ =>
  hT.hom_ext _ _
#align category_theory.is_subterminal_of_is_terminal CategoryTheory.isSubterminal_of_isTerminal

theorem isSubterminal_of_terminal [HasTerminal C] : IsSubterminal (âŠ¤_ C) := fun _ _ _ =>
  Subsingleton.elim _ _
#align category_theory.is_subterminal_of_terminal CategoryTheory.isSubterminal_of_terminal

/-- If `A` is subterminal, its diagonal morphism is an isomorphism.
The converse of `isSubterminal_of_isIso_diag`.
-/
theorem IsSubterminal.isIso_diag (hA : IsSubterminal A) [HasBinaryProduct A A] : IsIso (diag A) :=
  âŸ¨âŸ¨Limits.prod.fst,
      âŸ¨by simp, by
          -- ðŸŽ‰ no goals
        rw [IsSubterminal.def] at hA
        -- âŠ¢ prod.fst â‰« diag A = ðŸ™ (A â¨¯ A)
        aesop_catâŸ©âŸ©âŸ©
        -- ðŸŽ‰ no goals
#align category_theory.is_subterminal.is_iso_diag CategoryTheory.IsSubterminal.isIso_diag

/-- If the diagonal morphism of `A` is an isomorphism, then it is subterminal.
The converse of `isSubterminal.isIso_diag`.
-/
theorem isSubterminal_of_isIso_diag [HasBinaryProduct A A] [IsIso (diag A)] : IsSubterminal A :=
  fun Z f g => by
  have : (Limits.prod.fst : A â¨¯ A âŸ¶ _) = Limits.prod.snd := by simp [â† cancel_epi (diag A)]
  -- âŠ¢ f = g
  rw [â† prod.lift_fst f g, this, prod.lift_snd]
  -- ðŸŽ‰ no goals
#align category_theory.is_subterminal_of_is_iso_diag CategoryTheory.isSubterminal_of_isIso_diag

/-- If `A` is subterminal, it is isomorphic to `A â¨¯ A`. -/
@[simps!]
def IsSubterminal.isoDiag (hA : IsSubterminal A) [HasBinaryProduct A A] : A â¨¯ A â‰… A := by
  letI := IsSubterminal.isIso_diag hA
  -- âŠ¢ A â¨¯ A â‰… A
  apply (asIso (diag A)).symm
  -- ðŸŽ‰ no goals
#align category_theory.is_subterminal.iso_diag CategoryTheory.IsSubterminal.isoDiag

variable (C)

/-- The (full sub)category of subterminal objects.
TODO: If `C` is the category of sheaves on a topological space `X`, this category is equivalent
to the lattice of open subsets of `X`. More generally, if `C` is a topos, this is the lattice of
"external truth values".
-/
def Subterminals (C : Type uâ‚) [Category.{vâ‚} C] :=
  FullSubcategory fun A : C => IsSubterminal A
#align category_theory.subterminals CategoryTheory.Subterminals

instance (C : Type uâ‚) [Category.{vâ‚} C] :
  Category (Subterminals C) := FullSubcategory.category _

instance [HasTerminal C] : Inhabited (Subterminals C) :=
  âŸ¨âŸ¨âŠ¤_ C, isSubterminal_of_terminalâŸ©âŸ©

/-- The inclusion of the subterminal objects into the original category. -/
@[simps!]
def subterminalInclusion : Subterminals C â¥¤ C :=
  fullSubcategoryInclusion _
#align category_theory.subterminal_inclusion CategoryTheory.subterminalInclusion

instance (C : Type uâ‚) [Category.{vâ‚} C] : Full (subterminalInclusion C) :=
  FullSubcategory.full _

instance (C : Type uâ‚) [Category.{vâ‚} C] : Faithful (subterminalInclusion C) :=
  FullSubcategory.faithful _

instance subterminals_thin (X Y : Subterminals C) : Subsingleton (X âŸ¶ Y) :=
  âŸ¨fun f g => Y.2 f gâŸ©
#align category_theory.subterminals_thin CategoryTheory.subterminals_thin

/--
The category of subterminal objects is equivalent to the category of monomorphisms to the terminal
object (which is in turn equivalent to the subobjects of the terminal object).
-/
@[simps]
def subterminalsEquivMonoOverTerminal [HasTerminal C] : Subterminals C â‰Œ MonoOver (âŠ¤_ C) where
  functor :=
    { obj := fun X => âŸ¨Over.mk (terminal.from X.1), X.2.mono_terminal_fromâŸ©
      map := fun f => MonoOver.homMk f (by ext1 âŸ¨âŸ¨âŸ©âŸ©) }
                                           -- ðŸŽ‰ no goals
  inverse :=
    { obj := fun X =>
        âŸ¨X.obj.left, fun Z f g => by
          rw [â† cancel_mono X.arrow]
          -- âŠ¢ f â‰« MonoOver.arrow X = g â‰« MonoOver.arrow X
          apply Subsingleton.elimâŸ©
          -- ðŸŽ‰ no goals
      map := fun f => f.1 }
  -- porting note: the original definition was triggering a timeout, using `NatIso.ofComponents`
  -- in the definition of the natural isomorphisms makes the situation slightly better
  unitIso := NatIso.ofComponents fun X => Iso.refl _
  counitIso := NatIso.ofComponents fun X => MonoOver.isoMk (Iso.refl _)
#align category_theory.subterminals_equiv_mono_over_terminal CategoryTheory.subterminalsEquivMonoOverTerminal

@[simp]
theorem subterminals_to_monoOver_terminal_comp_forget [HasTerminal C] :
    (subterminalsEquivMonoOverTerminal C).functor â‹™ MonoOver.forget _ â‹™ Over.forget _ =
      subterminalInclusion C :=
  rfl
#align category_theory.subterminals_to_mono_over_terminal_comp_forget CategoryTheory.subterminals_to_monoOver_terminal_comp_forget

@[simp]
theorem monoOver_terminal_to_subterminals_comp [HasTerminal C] :
    (subterminalsEquivMonoOverTerminal C).inverse â‹™ subterminalInclusion C =
      MonoOver.forget _ â‹™ Over.forget _ :=
  rfl
#align category_theory.mono_over_terminal_to_subterminals_comp CategoryTheory.monoOver_terminal_to_subterminals_comp

end CategoryTheory
