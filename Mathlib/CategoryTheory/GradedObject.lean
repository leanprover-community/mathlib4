/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Jo√´l Riou
-/
import Mathlib.Algebra.GroupPower.Lemmas
import Mathlib.CategoryTheory.Pi.Basic
import Mathlib.CategoryTheory.Shift.Basic
import Mathlib.CategoryTheory.ConcreteCategory.Basic

#align_import category_theory.graded_object from "leanprover-community/mathlib"@"6876fa15e3158ff3e4a4e2af1fb6e1945c6e8803"

/-!
# The category of graded objects

For any type `Œ≤`, a `Œ≤`-graded object over some category `C` is just
a function `Œ≤ ‚Üí C` into the objects of `C`.
We put the "pointwise" category structure on these, as the non-dependent specialization of
`CategoryTheory.Pi`.

We describe the `comap` functors obtained by precomposing with functions `Œ≤ ‚Üí Œ≥`.

As a consequence a fixed element (e.g. `1`) in an additive group `Œ≤` provides a shift
functor on `Œ≤`-graded objects

When `C` has coproducts we construct the `total` functor `GradedObject Œ≤ C ‚•§ C`,
show that it is faithful, and deduce that when `C` is concrete so is `GradedObject Œ≤ C`.

A covariant functoriality of `GradedObject Œ≤ C` with respect to the index set `Œ≤` is also
introduced: if `p : I ‚Üí J` is a map such that `C` has coproducts indexed by `p ‚Åª¬π' {j}`, we
have a functor `map : GradedObject I C ‚•§ GradedObject J C`.

-/

set_option autoImplicit true

namespace CategoryTheory

open Category Limits

universe w v u

/-- A type synonym for `Œ≤ ‚Üí C`, used for `Œ≤`-graded objects in a category `C`. -/
def GradedObject (Œ≤ : Type w) (C : Type u) : Type max w u :=
  Œ≤ ‚Üí C
#align category_theory.graded_object CategoryTheory.GradedObject

-- Satisfying the inhabited linter...
instance inhabitedGradedObject (Œ≤ : Type w) (C : Type u) [Inhabited C] :
    Inhabited (GradedObject Œ≤ C) :=
  ‚ü®fun _ => Inhabited.default‚ü©
#align category_theory.inhabited_graded_object CategoryTheory.inhabitedGradedObject

-- `s` is here to distinguish type synonyms asking for different shifts
/-- A type synonym for `Œ≤ ‚Üí C`, used for `Œ≤`-graded objects in a category `C`
with a shift functor given by translation by `s`.
-/
@[nolint unusedArguments]
abbrev GradedObjectWithShift {Œ≤ : Type w} [AddCommGroup Œ≤] (_ : Œ≤) (C : Type u) : Type max w u :=
  GradedObject Œ≤ C
#align category_theory.graded_object_with_shift CategoryTheory.GradedObjectWithShift

namespace GradedObject

variable {C : Type u} [Category.{v} C]

@[simps!]
instance categoryOfGradedObjects (Œ≤ : Type w) : Category.{max w v} (GradedObject Œ≤ C) :=
  CategoryTheory.pi fun _ => C
#align category_theory.graded_object.category_of_graded_objects CategoryTheory.GradedObject.categoryOfGradedObjects

-- porting note: added to ease automation
@[ext]
lemma hom_ext {X Y : GradedObject Œ≤ C} (f g : X ‚ü∂ Y) (h : ‚àÄ x, f x = g x) : f = g := by
  funext
  apply h

/-- The projection of a graded object to its `i`-th component. -/
@[simps]
def eval {Œ≤ : Type w} (b : Œ≤) : GradedObject Œ≤ C ‚•§ C where
  obj X := X b
  map f := f b
#align category_theory.graded_object.eval CategoryTheory.GradedObject.eval

section

variable {Œ≤ : Type*} (X Y : GradedObject Œ≤ C)

/-- Constructor for isomorphisms in `GradedObject` -/
@[simps]
def isoMk (e : ‚àÄ i, X i ‚âÖ Y i) : X ‚âÖ Y where
  hom i := (e i).hom
  inv i := (e i).inv

variable {X Y}

-- this lemma is not an instance as it may create a loop with `isIso_apply_of_isIso`
lemma isIso_of_isIso_apply (f : X ‚ü∂ Y) [hf : ‚àÄ i, IsIso (f i)] :
    IsIso f := by
  change IsIso (isoMk X Y (fun i => asIso (f i))).hom
  infer_instance

@[reassoc (attr := simp)]
lemma iso_hom_inv_id_apply (e : X ‚âÖ Y) (i : Œ≤) :
    e.hom i ‚â´ e.inv i = ùüô _ :=
  congr_fun e.hom_inv_id i

@[reassoc (attr := simp)]
lemma iso_inv_hom_id_apply (e : X ‚âÖ Y) (i : Œ≤) :
    e.inv i ‚â´ e.hom i = ùüô _ :=
  congr_fun e.inv_hom_id i

instance isIso_apply_of_isIso (f : X ‚ü∂ Y) [IsIso f] (i : Œ≤) : IsIso (f i) := by
  change IsIso ((eval i).map f)
  infer_instance

end

section

variable (C)

-- porting note: added to ease the port
/-- Pull back an `I`-graded object in `C` to a `J`-graded object along a function `J ‚Üí I`. -/
abbrev comap {I J : Type*} (h : J ‚Üí I) : GradedObject I C ‚•§ GradedObject J C :=
  Pi.comap (fun _ => C) h

-- porting note: added to ease the port, this is a special case of `Functor.eqToHom_proj`
@[simp]
theorem eqToHom_proj {x x' : GradedObject I C} (h : x = x') (i : I) :
    (eqToHom h : x ‚ü∂ x') i = eqToHom (Function.funext_iff.mp h i) := by
  subst h
  rfl

/-- The natural isomorphism comparing between
pulling back along two propositionally equal functions.
-/
@[simps]
def comapEq {Œ≤ Œ≥ : Type w} {f g : Œ≤ ‚Üí Œ≥} (h : f = g) : comap C f ‚âÖ comap C g where
  hom := { app := fun X b => eqToHom (by dsimp; simp only [h]) }
  inv := { app := fun X b => eqToHom (by dsimp; simp only [h]) }
#align category_theory.graded_object.comap_eq CategoryTheory.GradedObject.comapEq

theorem comapEq_symm {Œ≤ Œ≥ : Type w} {f g : Œ≤ ‚Üí Œ≥} (h : f = g) :
    comapEq C h.symm = (comapEq C h).symm := by aesop_cat
#align category_theory.graded_object.comap_eq_symm CategoryTheory.GradedObject.comapEq_symm

theorem comapEq_trans {Œ≤ Œ≥ : Type w} {f g h : Œ≤ ‚Üí Œ≥} (k : f = g) (l : g = h) :
    comapEq C (k.trans l) = comapEq C k ‚â™‚â´ comapEq C l := by aesop_cat
#align category_theory.graded_object.comap_eq_trans CategoryTheory.GradedObject.comapEq_trans

@[simp]
theorem eqToHom_apply {Œ≤ : Type w} {X Y : ‚àÄ _ : Œ≤, C} (h : X = Y) (b : Œ≤) :
    (eqToHom h : X ‚ü∂ Y) b = eqToHom (by rw [h]) := by
  subst h
  rfl
#align category_theory.graded_object.eq_to_hom_apply CategoryTheory.GradedObject.eqToHom_apply

/-- The equivalence between Œ≤-graded objects and Œ≥-graded objects,
given an equivalence between Œ≤ and Œ≥.
-/
@[simps]
def comapEquiv {Œ≤ Œ≥ : Type w} (e : Œ≤ ‚âÉ Œ≥) : GradedObject Œ≤ C ‚âå GradedObject Œ≥ C where
  functor := comap C (e.symm : Œ≥ ‚Üí Œ≤)
  inverse := comap C (e : Œ≤ ‚Üí Œ≥)
  counitIso :=
    (Pi.comapComp (fun _ => C) _ _).trans (comapEq C (by ext; simp))
  unitIso :=
    (comapEq C (by ext; simp)).trans (Pi.comapComp _ _ _).symm
#align category_theory.graded_object.comap_equiv CategoryTheory.GradedObject.comapEquiv

-- See note [dsimp, simp].
end

instance hasShift {Œ≤ : Type*} [AddCommGroup Œ≤] (s : Œ≤) : HasShift (GradedObjectWithShift s C) ‚Ñ§ :=
  hasShiftMk _ _
    { F := fun n => comap C fun b : Œ≤ => b + n ‚Ä¢ s
      zero := comapEq C (by aesop_cat) ‚â™‚â´ Pi.comapId Œ≤ fun _ => C
      add := fun m n => comapEq C (by ext; dsimp; rw [add_comm m n, add_zsmul, add_assoc]) ‚â™‚â´
          (Pi.comapComp _ _ _).symm }
#align category_theory.graded_object.has_shift CategoryTheory.GradedObject.hasShift

@[simp]
theorem shiftFunctor_obj_apply {Œ≤ : Type*} [AddCommGroup Œ≤] (s : Œ≤) (X : Œ≤ ‚Üí C) (t : Œ≤) (n : ‚Ñ§) :
    (shiftFunctor (GradedObjectWithShift s C) n).obj X t = X (t + n ‚Ä¢ s) :=
  rfl
#align category_theory.graded_object.shift_functor_obj_apply CategoryTheory.GradedObject.shiftFunctor_obj_apply

@[simp]
theorem shiftFunctor_map_apply {Œ≤ : Type*} [AddCommGroup Œ≤] (s : Œ≤)
    {X Y : GradedObjectWithShift s C} (f : X ‚ü∂ Y) (t : Œ≤) (n : ‚Ñ§) :
    (shiftFunctor (GradedObjectWithShift s C) n).map f t = f (t + n ‚Ä¢ s) :=
  rfl
#align category_theory.graded_object.shift_functor_map_apply CategoryTheory.GradedObject.shiftFunctor_map_apply

instance [HasZeroMorphisms C] (Œ≤ : Type w) (X Y : GradedObject Œ≤ C) :
  Zero (X ‚ü∂ Y) := ‚ü®fun _ => 0‚ü©

@[simp]
theorem zero_apply [HasZeroMorphisms C] (Œ≤ : Type w) (X Y : GradedObject Œ≤ C) (b : Œ≤) :
    (0 : X ‚ü∂ Y) b = 0 :=
  rfl
#align category_theory.graded_object.zero_apply CategoryTheory.GradedObject.zero_apply

instance hasZeroMorphisms [HasZeroMorphisms C] (Œ≤ : Type w) :
    HasZeroMorphisms.{max w v} (GradedObject Œ≤ C) where
#align category_theory.graded_object.has_zero_morphisms CategoryTheory.GradedObject.hasZeroMorphisms

section

open ZeroObject

instance hasZeroObject [HasZeroObject C] [HasZeroMorphisms C] (Œ≤ : Type w) :
    HasZeroObject.{max w v} (GradedObject Œ≤ C) := by
  refine' ‚ü®‚ü®fun _ => 0, fun X => ‚ü®‚ü®‚ü®fun b => 0‚ü©, fun f => _‚ü©‚ü©, fun X =>
    ‚ü®‚ü®‚ü®fun b => 0‚ü©, fun f => _‚ü©‚ü©‚ü©‚ü© <;> aesop_cat
#align category_theory.graded_object.has_zero_object CategoryTheory.GradedObject.hasZeroObject

end

end GradedObject

namespace GradedObject

-- The universes get a little hairy here, so we restrict the universe level for the grading to 0.
-- Since we're typically interested in grading by ‚Ñ§ or a finite group, this should be okay.
-- If you're grading by things in higher universes, have fun!
variable (Œ≤ : Type)

variable (C : Type u) [Category.{v} C]

variable [HasCoproducts.{0} C]

section

/-- The total object of a graded object is the coproduct of the graded components.
-/
noncomputable def total : GradedObject Œ≤ C ‚•§ C where
  obj X := ‚àê fun i : Œ≤ => X i
  map f := Limits.Sigma.map fun i => f i
#align category_theory.graded_object.total CategoryTheory.GradedObject.total

end

variable [HasZeroMorphisms C]

/--
The `total` functor taking a graded object to the coproduct of its graded components is faithful.
To prove this, we need to know that the coprojections into the coproduct are monomorphisms,
which follows from the fact we have zero morphisms and decidable equality for the grading.
-/
instance : Faithful (total Œ≤ C) where
  map_injective {X Y} f g w := by
    ext i
    replace w := Sigma.Œπ (fun i : Œ≤ => X i) i ‚â´= w
    erw [colimit.Œπ_map, colimit.Œπ_map] at w
    simp? at * says simp only [Discrete.functor_obj, Discrete.natTrans_app] at *
    exact Mono.right_cancellation _ _ w

end GradedObject

namespace GradedObject

noncomputable section

variable (Œ≤ : Type)

variable (C : Type (u + 1)) [LargeCategory C] [ConcreteCategory C] [HasCoproducts.{0} C]
  [HasZeroMorphisms C]

instance : ConcreteCategory (GradedObject Œ≤ C) where forget := total Œ≤ C ‚ãô forget C

instance : HasForget‚ÇÇ (GradedObject Œ≤ C) C where forget‚ÇÇ := total Œ≤ C

end

end GradedObject

namespace GradedObject

variable {I J K : Type*} {C : Type*} [Category C]
  (X Y Z : GradedObject I C) (œÜ : X ‚ü∂ Y) (e : X ‚âÖ Y) (œà : Y ‚ü∂ Z) (p : I ‚Üí J)

/-- If `X : GradedObject I C` and `p : I ‚Üí J`, `X.mapObjFun p j` is the family of objects `X i`
for `i : I` such that `p i = j`. -/
abbrev mapObjFun (j : J) (i : p ‚Åª¬π' {j}) : C := X i

variable (j : J)

/-- Given `X : GradedObject I C` and `p : I ‚Üí J`, `X.HasMap p` is the condition that
for all `j : J`, the coproduct of all `X i` such `p i = j` exists. -/
abbrev HasMap : Prop := ‚àÄ (j : J), HasCoproduct (X.mapObjFun p j)

variable [X.HasMap p] [Y.HasMap p] [Z.HasMap p]

/-- Given `X : GradedObject I C` and `p : I ‚Üí J`, `X.mapObj p` is the graded object by `J`
which in degree `j` consists of the coproduct of the `X i` such that `p i = j`. -/
noncomputable def mapObj : GradedObject J C := fun j => ‚àê (X.mapObjFun p j)

/-- The canonical inclusion `X i ‚ü∂ X.mapObj p j` when `i : I` and `j : J` are such
that `p i = j`. -/
noncomputable def ŒπMapObj (i : I) (j : J) (hij : p i = j) : X i ‚ü∂ X.mapObj p j :=
  Sigma.Œπ (X.mapObjFun p j) ‚ü®i, hij‚ü©

/-- Given `X : GradedObject I C`, `p : I ‚Üí J` and `j : J`,
`CofanMapObjFun X p j` is the type `Cofan (X.mapObjFun p j)`. The point object of
such colimits cofans are isomorphic to `X.mapObj p j`, see `CofanMapObjFun.iso`. -/
abbrev CofanMapObjFun (j : J) : Type _ := Cofan (X.mapObjFun p j)

-- in order to use the cofan API, some definitions below
-- have a `simp` attribute rather than `simps`
/-- Constructor for `CofanMapObjFun X p j`. -/
@[simp]
def CofanMapObjFun.mk (j : J) (pt : C) (Œπ' : ‚àÄ (i : I) (_ : p i = j), X i ‚ü∂ pt) :
    CofanMapObjFun X p j :=
  Cofan.mk pt (fun ‚ü®i, hi‚ü© => Œπ' i hi)

/-- The tautological cofan corresponding to the coproduct decomposition of `X.mapObj p j`. -/
@[simp]
noncomputable def cofanMapObj (j : J) : CofanMapObjFun X p j :=
  CofanMapObjFun.mk X p j (X.mapObj p j) (fun i hi => X.ŒπMapObj p i j hi)

/-- Given `X : GradedObject I C`, `p : I ‚Üí J` and `j : J`, `X.mapObj p j` satisfies
the universal property of the coproduct of those `X i` such that `p i = j`. -/
noncomputable def isColimitCofanMapObj (j : J) : IsColimit (X.cofanMapObj p j) :=
  colimit.isColimit _

@[ext]
lemma mapObj_ext {A : C} {j : J} (f g : X.mapObj p j ‚ü∂ A)
    (hfg : ‚àÄ (i : I) (hij : p i = j), X.ŒπMapObj p i j hij ‚â´ f = X.ŒπMapObj p i j hij ‚â´ g) :
    f = g :=
  Cofan.IsColimit.hom_ext (X.isColimitCofanMapObj p j) _ _ (fun ‚ü®i, hij‚ü© => hfg i hij)

/-- This is the morphism `X.mapObj p j ‚ü∂ A` constructed from a family of
morphisms `X i ‚ü∂ A` for all `i : I` such that `p i = j`. -/
noncomputable def descMapObj {A : C} {j : J} (œÜ : ‚àÄ (i : I) (_ : p i = j), X i ‚ü∂ A) :
    X.mapObj p j ‚ü∂ A :=
  Cofan.IsColimit.desc (X.isColimitCofanMapObj p j) (fun ‚ü®i, hi‚ü© => œÜ i hi)

@[reassoc (attr := simp)]
lemma Œπ_descMapObj {A : C} {j : J}
    (œÜ : ‚àÄ (i : I) (_ : p i = j), X i ‚ü∂ A) (i : I) (hi : p i = j) :
    X.ŒπMapObj p i j hi ‚â´ X.descMapObj p œÜ = œÜ i hi := by
  apply Cofan.IsColimit.fac

namespace CofanMapObjFun

lemma hasMap (c : ‚àÄ j, CofanMapObjFun X p j) (hc : ‚àÄ j, IsColimit (c j)) :
    X.HasMap p := fun j => ‚ü®_, hc j‚ü©

variable {j X p}
  {c : CofanMapObjFun X p j} (hc : IsColimit c) [X.HasMap p]

/-- If `c : CofanMapObjFun X p j` is a colimit cofan, this is the induced
isomorphism `c.pt ‚âÖ X.mapObj p j`. -/
noncomputable def iso : c.pt ‚âÖ X.mapObj p j :=
  IsColimit.coconePointUniqueUpToIso hc (X.isColimitCofanMapObj p j)

@[reassoc (attr := simp)]
lemma inj_iso_hom (i : I) (hi : p i = j) :
    c.inj ‚ü®i, hi‚ü© ‚â´ (c.iso hc).hom = X.ŒπMapObj p i j hi := by
  apply IsColimit.comp_coconePointUniqueUpToIso_hom

@[reassoc (attr := simp)]
lemma ŒπMapObj_iso_inv (i : I) (hi : p i = j) :
    X.ŒπMapObj p i j hi ‚â´ (c.iso hc).inv = c.inj ‚ü®i, hi‚ü© := by
  apply IsColimit.comp_coconePointUniqueUpToIso_inv

end CofanMapObjFun

variable {X Y}

/-- The canonical morphism of `J`-graded objects `X.mapObj p ‚ü∂ Y.mapObj p` induced by
a morphism `X ‚ü∂ Y` of `I`-graded objects and a map `p : I ‚Üí J`. -/
noncomputable def mapMap : X.mapObj p ‚ü∂ Y.mapObj p := fun j =>
  X.descMapObj p (fun i hi => œÜ i ‚â´ Y.ŒπMapObj p i j hi)

@[reassoc (attr := simp)]
lemma Œπ_mapMap (i : I) (j : J) (hij : p i = j) :
    X.ŒπMapObj p i j hij ‚â´ mapMap œÜ p j = œÜ i ‚â´ Y.ŒπMapObj p i j hij := by
  simp only [mapMap, Œπ_descMapObj]

lemma congr_mapMap (œÜ‚ÇÅ œÜ‚ÇÇ : X ‚ü∂ Y) (h : œÜ‚ÇÅ = œÜ‚ÇÇ) : mapMap œÜ‚ÇÅ p = mapMap œÜ‚ÇÇ p := by
  subst h
  rfl

variable (X)

@[simp]
lemma mapMap_id : mapMap (ùüô X) p = ùüô _ := by aesop_cat

variable {X Z}

@[simp, reassoc]
lemma mapMap_comp : mapMap (œÜ ‚â´ œà) p = mapMap œÜ p ‚â´ mapMap œà p := by aesop_cat

/-- The isomorphism of `J`-graded objects `X.mapObj p ‚âÖ Y.mapObj p` induced by an
isomorphism `X ‚âÖ Y` of graded objects and a map `p : I ‚Üí J`. -/
@[simps]
noncomputable def mapIso : X.mapObj p ‚âÖ Y.mapObj p where
  hom := mapMap e.hom p
  inv := mapMap e.inv p

variable (C)

/-- Given a map `p : I ‚Üí J`, this is the functor `GradedObject I C ‚•§ GradedObject J C` which
sends an `I`-object `X` to the graded object `X.mapObj p` which in degree `j : J` is given
by the coproduct of those `X i` such that `p i = j`. -/
@[simps]
noncomputable def map [‚àÄ (j : J), HasColimitsOfShape (Discrete (p ‚Åª¬π' {j})) C] :
    GradedObject I C ‚•§ GradedObject J C where
  obj X := X.mapObj p
  map œÜ := mapMap œÜ p

variable {C} (X Y)
variable (q : J ‚Üí K) (r : I ‚Üí K) (hpqr : ‚àÄ i, q (p i) = r i)

section

variable (k : K) (c : ‚àÄ (j : J), q j = k ‚Üí X.CofanMapObjFun p j)
  (hc : ‚àÄ j hj, IsColimit (c j hj))
  (c' : Cofan (fun (j : q ‚Åª¬π' {k}) => (c j.1 j.2).pt)) (hc' : IsColimit c')

/-- Given maps `p : I ‚Üí J`, `q : J ‚Üí K` and `r : I ‚Üí K` such that `q.comp p = r`,
`X : GradedObject I C`, `k : K`, the datum of cofans `X.CofanMapObjFun p j` for all
`j : J` and of a cofan for all the points of these cofans, this is a cofan of
type `X.CofanMapObjFun r k`, which is a colimit (see `isColimitCofanMapObjComp`) if the
given cofans are. -/
@[simp]
def cofanMapObjComp : X.CofanMapObjFun r k :=
  CofanMapObjFun.mk _ _ _ c'.pt (fun i hi =>
    (c (p i) (by rw [hpqr, hi])).inj ‚ü®i, rfl‚ü© ‚â´ c'.inj (‚ü®p i, by
      rw [Set.mem_preimage, Set.mem_singleton_iff, hpqr, hi]‚ü©))

/-- Given maps `p : I ‚Üí J`, `q : J ‚Üí K` and `r : I ‚Üí K` such that `q.comp p = r`,
`X : GradedObject I C`, `k : K`, the cofan constructed by `cofanMapObjComp` is a colimit.
In other words, if we have, for all `j : J` such that `hj : q j = k`,
a colimit cofan `c j hj` which computes the coproduct of the `X i` such that `p i = j`,
and also a colimit cofan which computes the coproduct of the points of these `c j hj`, then
the point of this latter cofan computes the coproduct of the `X i` such that `r i = k`.
.-/
@[simp]
def isColimitCofanMapObjComp :
    IsColimit (cofanMapObjComp X p q r hpqr k c c') :=
  mkCofanColimit _
    (fun s => Cofan.IsColimit.desc hc'
      (fun ‚ü®j, (hj : q j = k)‚ü© => Cofan.IsColimit.desc (hc j hj)
        (fun ‚ü®i, (hi : p i = j)‚ü© => s.inj ‚ü®i, by
          simp only [Set.mem_preimage, Set.mem_singleton_iff, ‚Üê hpqr, hi, hj]‚ü©)))
    (fun s ‚ü®i, (hi : r i = k)‚ü© => by simp)
    (fun s m hm => by
      apply Cofan.IsColimit.hom_ext hc'
      rintro ‚ü®j, rfl : q j = k‚ü©
      apply Cofan.IsColimit.hom_ext (hc j rfl)
      rintro ‚ü®i, rfl : p i = j‚ü©
      dsimp
      rw [Cofan.IsColimit.fac, Cofan.IsColimit.fac, ‚Üê hm]
      dsimp
      rw [assoc])

lemma hasMap_comp [X.HasMap p] [(X.mapObj p).HasMap q] : X.HasMap r :=
  fun k => ‚ü®_, isColimitCofanMapObjComp X p q r hpqr k _
    (fun j _ => X.isColimitCofanMapObj p j) _ ((X.mapObj p).isColimitCofanMapObj q k)‚ü©

end

end GradedObject

end CategoryTheory
