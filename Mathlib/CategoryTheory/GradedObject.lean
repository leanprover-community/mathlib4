/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, JoÃ«l Riou
-/
import Mathlib.Algebra.Group.Int
import Mathlib.CategoryTheory.ConcreteCategory.Basic
import Mathlib.CategoryTheory.Shift.Basic
import Mathlib.Data.Set.Subsingleton

#align_import category_theory.graded_object from "leanprover-community/mathlib"@"6876fa15e3158ff3e4a4e2af1fb6e1945c6e8803"

/-!
# The category of graded objects

For any type `Î²`, a `Î²`-graded object over some category `C` is just
a function `Î² â†’ C` into the objects of `C`.
We put the "pointwise" category structure on these, as the non-dependent specialization of
`CategoryTheory.Pi`.

We describe the `comap` functors obtained by precomposing with functions `Î² â†’ Î³`.

As a consequence a fixed element (e.g. `1`) in an additive group `Î²` provides a shift
functor on `Î²`-graded objects

When `C` has coproducts we construct the `total` functor `GradedObject Î² C â¥¤ C`,
show that it is faithful, and deduce that when `C` is concrete so is `GradedObject Î² C`.

A covariant functoriality of `GradedObject Î² C` with respect to the index set `Î²` is also
introduced: if `p : I â†’ J` is a map such that `C` has coproducts indexed by `p â»Â¹' {j}`, we
have a functor `map : GradedObject I C â¥¤ GradedObject J C`.

-/

namespace CategoryTheory

open Category Limits

universe w v u

/-- A type synonym for `Î² â†’ C`, used for `Î²`-graded objects in a category `C`. -/
def GradedObject (Î² : Type w) (C : Type u) : Type max w u :=
  Î² â†’ C
#align category_theory.graded_object CategoryTheory.GradedObject

-- Satisfying the inhabited linter...
instance inhabitedGradedObject (Î² : Type w) (C : Type u) [Inhabited C] :
    Inhabited (GradedObject Î² C) :=
  âŸ¨fun _ => Inhabited.defaultâŸ©
#align category_theory.inhabited_graded_object CategoryTheory.inhabitedGradedObject

-- `s` is here to distinguish type synonyms asking for different shifts
/-- A type synonym for `Î² â†’ C`, used for `Î²`-graded objects in a category `C`
with a shift functor given by translation by `s`.
-/
@[nolint unusedArguments]
abbrev GradedObjectWithShift {Î² : Type w} [AddCommGroup Î²] (_ : Î²) (C : Type u) : Type max w u :=
  GradedObject Î² C
#align category_theory.graded_object_with_shift CategoryTheory.GradedObjectWithShift

namespace GradedObject

variable {C : Type u} [Category.{v} C]

@[simps!]
instance categoryOfGradedObjects (Î² : Type w) : Category.{max w v} (GradedObject Î² C) :=
  CategoryTheory.pi fun _ => C
#align category_theory.graded_object.category_of_graded_objects CategoryTheory.GradedObject.categoryOfGradedObjects

-- Porting note (#10688): added to ease automation
@[ext]
lemma hom_ext {Î² : Type*} {X Y : GradedObject Î² C} (f g : X âŸ¶ Y) (h : âˆ€ x, f x = g x) : f = g := by
  funext
  apply h

/-- The projection of a graded object to its `i`-th component. -/
@[simps]
def eval {Î² : Type w} (b : Î²) : GradedObject Î² C â¥¤ C where
  obj X := X b
  map f := f b
#align category_theory.graded_object.eval CategoryTheory.GradedObject.eval

section

variable {Î² : Type*} (X Y : GradedObject Î² C)

/-- Constructor for isomorphisms in `GradedObject` -/
@[simps]
def isoMk (e : âˆ€ i, X i â‰… Y i) : X â‰… Y where
  hom i := (e i).hom
  inv i := (e i).inv

variable {X Y}

-- this lemma is not an instance as it may create a loop with `isIso_apply_of_isIso`
lemma isIso_of_isIso_apply (f : X âŸ¶ Y) [hf : âˆ€ i, IsIso (f i)] :
    IsIso f := by
  change IsIso (isoMk X Y (fun i => asIso (f i))).hom
  infer_instance

instance isIso_apply_of_isIso (f : X âŸ¶ Y) [IsIso f] (i : Î²) : IsIso (f i) := by
  change IsIso ((eval i).map f)
  infer_instance

end

end GradedObject

namespace Iso

variable {C D E J : Type*} [Category C] [Category D] [Category E]
  {X Y : GradedObject J C}

@[reassoc (attr := simp)]
lemma hom_inv_id_eval (e : X â‰… Y) (j : J) :
    e.hom j â‰« e.inv j = ðŸ™ _ := by
  rw [â† GradedObject.categoryOfGradedObjects_comp, e.hom_inv_id,
    GradedObject.categoryOfGradedObjects_id]

@[reassoc (attr := simp)]
lemma inv_hom_id_eval (e : X â‰… Y) (j : J) :
    e.inv j â‰« e.hom j = ðŸ™ _ := by
  rw [â† GradedObject.categoryOfGradedObjects_comp, e.inv_hom_id,
    GradedObject.categoryOfGradedObjects_id]

@[reassoc (attr := simp)]
lemma map_hom_inv_id_eval (e : X â‰… Y) (F : C â¥¤ D) (j : J) :
    F.map (e.hom j) â‰« F.map (e.inv j) = ðŸ™ _ := by
  rw [â† F.map_comp, â† GradedObject.categoryOfGradedObjects_comp, e.hom_inv_id,
    GradedObject.categoryOfGradedObjects_id, Functor.map_id]

@[reassoc (attr := simp)]
lemma map_inv_hom_id_eval (e : X â‰… Y) (F : C â¥¤ D) (j : J) :
    F.map (e.inv j) â‰« F.map (e.hom j) = ðŸ™ _ := by
  rw [â† F.map_comp, â† GradedObject.categoryOfGradedObjects_comp, e.inv_hom_id,
    GradedObject.categoryOfGradedObjects_id, Functor.map_id]

@[reassoc (attr := simp)]
lemma map_hom_inv_id_eval_app (e : X â‰… Y) (F : C â¥¤ D â¥¤ E) (j : J) (Y : D) :
    (F.map (e.hom j)).app Y â‰« (F.map (e.inv j)).app Y = ðŸ™ _ := by
  rw [â† NatTrans.comp_app, â† F.map_comp, hom_inv_id_eval,
    Functor.map_id, NatTrans.id_app]

@[reassoc (attr := simp)]
lemma map_inv_hom_id_eval_app (e : X â‰… Y) (F : C â¥¤ D â¥¤ E) (j : J) (Y : D) :
    (F.map (e.inv j)).app Y â‰« (F.map (e.hom j)).app Y = ðŸ™ _ := by
  rw [â† NatTrans.comp_app, â† F.map_comp, inv_hom_id_eval,
    Functor.map_id, NatTrans.id_app]

end Iso

namespace GradedObject

variable {C : Type u} [Category.{v} C]

section

variable (C)

-- Porting note: added to ease the port
/-- Pull back an `I`-graded object in `C` to a `J`-graded object along a function `J â†’ I`. -/
abbrev comap {I J : Type*} (h : J â†’ I) : GradedObject I C â¥¤ GradedObject J C :=
  Pi.comap (fun _ => C) h

-- Porting note: added to ease the port, this is a special case of `Functor.eqToHom_proj`
@[simp]
theorem eqToHom_proj {I : Type*} {x x' : GradedObject I C} (h : x = x') (i : I) :
    (eqToHom h : x âŸ¶ x') i = eqToHom (Function.funext_iff.mp h i) := by
  subst h
  rfl

/-- The natural isomorphism comparing between
pulling back along two propositionally equal functions.
-/
@[simps]
def comapEq {Î² Î³ : Type w} {f g : Î² â†’ Î³} (h : f = g) : comap C f â‰… comap C g where
  hom := { app := fun X b => eqToHom (by dsimp; simp only [h]) }
  inv := { app := fun X b => eqToHom (by dsimp; simp only [h]) }
#align category_theory.graded_object.comap_eq CategoryTheory.GradedObject.comapEq

theorem comapEq_symm {Î² Î³ : Type w} {f g : Î² â†’ Î³} (h : f = g) :
    comapEq C h.symm = (comapEq C h).symm := by aesop_cat
#align category_theory.graded_object.comap_eq_symm CategoryTheory.GradedObject.comapEq_symm

theorem comapEq_trans {Î² Î³ : Type w} {f g h : Î² â†’ Î³} (k : f = g) (l : g = h) :
    comapEq C (k.trans l) = comapEq C k â‰ªâ‰« comapEq C l := by aesop_cat
#align category_theory.graded_object.comap_eq_trans CategoryTheory.GradedObject.comapEq_trans

theorem eqToHom_apply {Î² : Type w} {X Y : Î² â†’ C} (h : X = Y) (b : Î²) :
    (eqToHom h : X âŸ¶ Y) b = eqToHom (by rw [h]) := by
  subst h
  rfl
#align category_theory.graded_object.eq_to_hom_apply CategoryTheory.GradedObject.eqToHom_apply

/-- The equivalence between Î²-graded objects and Î³-graded objects,
given an equivalence between Î² and Î³.
-/
@[simps]
def comapEquiv {Î² Î³ : Type w} (e : Î² â‰ƒ Î³) : GradedObject Î² C â‰Œ GradedObject Î³ C where
  functor := comap C (e.symm : Î³ â†’ Î²)
  inverse := comap C (e : Î² â†’ Î³)
  counitIso :=
    (Pi.comapComp (fun _ => C) _ _).trans (comapEq C (by ext; simp))
  unitIso :=
    (comapEq C (by ext; simp)).trans (Pi.comapComp _ _ _).symm
#align category_theory.graded_object.comap_equiv CategoryTheory.GradedObject.comapEquiv

end

instance hasShift {Î² : Type*} [AddCommGroup Î²] (s : Î²) : HasShift (GradedObjectWithShift s C) â„¤ :=
  hasShiftMk _ _
    { F := fun n => comap C fun b : Î² => b + n â€¢ s
      zero := comapEq C (by aesop_cat) â‰ªâ‰« Pi.comapId Î² fun _ => C
      add := fun m n => comapEq C (by ext; dsimp; rw [add_comm m n, add_zsmul, add_assoc]) â‰ªâ‰«
          (Pi.comapComp _ _ _).symm }
#align category_theory.graded_object.has_shift CategoryTheory.GradedObject.hasShift

@[simp]
theorem shiftFunctor_obj_apply {Î² : Type*} [AddCommGroup Î²] (s : Î²) (X : Î² â†’ C) (t : Î²) (n : â„¤) :
    (shiftFunctor (GradedObjectWithShift s C) n).obj X t = X (t + n â€¢ s) :=
  rfl
#align category_theory.graded_object.shift_functor_obj_apply CategoryTheory.GradedObject.shiftFunctor_obj_apply

@[simp]
theorem shiftFunctor_map_apply {Î² : Type*} [AddCommGroup Î²] (s : Î²)
    {X Y : GradedObjectWithShift s C} (f : X âŸ¶ Y) (t : Î²) (n : â„¤) :
    (shiftFunctor (GradedObjectWithShift s C) n).map f t = f (t + n â€¢ s) :=
  rfl
#align category_theory.graded_object.shift_functor_map_apply CategoryTheory.GradedObject.shiftFunctor_map_apply

instance [HasZeroMorphisms C] (Î² : Type w) (X Y : GradedObject Î² C) :
  Zero (X âŸ¶ Y) := âŸ¨fun _ => 0âŸ©

@[simp]
theorem zero_apply [HasZeroMorphisms C] (Î² : Type w) (X Y : GradedObject Î² C) (b : Î²) :
    (0 : X âŸ¶ Y) b = 0 :=
  rfl
#align category_theory.graded_object.zero_apply CategoryTheory.GradedObject.zero_apply

instance hasZeroMorphisms [HasZeroMorphisms C] (Î² : Type w) :
    HasZeroMorphisms.{max w v} (GradedObject Î² C) where
#align category_theory.graded_object.has_zero_morphisms CategoryTheory.GradedObject.hasZeroMorphisms

section

open ZeroObject

instance hasZeroObject [HasZeroObject C] [HasZeroMorphisms C] (Î² : Type w) :
    HasZeroObject.{max w v} (GradedObject Î² C) := by
  refine âŸ¨âŸ¨fun _ => 0, fun X => âŸ¨âŸ¨âŸ¨fun b => 0âŸ©, fun f => ?_âŸ©âŸ©, fun X =>
    âŸ¨âŸ¨âŸ¨fun b => 0âŸ©, fun f => ?_âŸ©âŸ©âŸ©âŸ© <;> aesop_cat
#align category_theory.graded_object.has_zero_object CategoryTheory.GradedObject.hasZeroObject

end

end GradedObject

namespace GradedObject

-- The universes get a little hairy here, so we restrict the universe level for the grading to 0.
-- Since we're typically interested in grading by â„¤ or a finite group, this should be okay.
-- If you're grading by things in higher universes, have fun!
variable (Î² : Type)
variable (C : Type u) [Category.{v} C]
variable [HasCoproducts.{0} C]

section

/-- The total object of a graded object is the coproduct of the graded components.
-/
noncomputable def total : GradedObject Î² C â¥¤ C where
  obj X := âˆ fun i : Î² => X i
  map f := Limits.Sigma.map fun i => f i
#align category_theory.graded_object.total CategoryTheory.GradedObject.total

end

variable [HasZeroMorphisms C]

/--
The `total` functor taking a graded object to the coproduct of its graded components is faithful.
To prove this, we need to know that the coprojections into the coproduct are monomorphisms,
which follows from the fact we have zero morphisms and decidable equality for the grading.
-/
instance : (total Î² C).Faithful where
  map_injective {X Y} f g w := by
    ext i
    replace w := Sigma.Î¹ (fun i : Î² => X i) i â‰«= w
    erw [colimit.Î¹_map, colimit.Î¹_map] at w
    simp? at * says simp only [Discrete.functor_obj, Discrete.natTrans_app] at *
    exact Mono.right_cancellation _ _ w

end GradedObject

namespace GradedObject

noncomputable section

variable (Î² : Type)
variable (C : Type (u + 1)) [LargeCategory C] [ConcreteCategory C] [HasCoproducts.{0} C]
  [HasZeroMorphisms C]

instance : ConcreteCategory (GradedObject Î² C) where forget := total Î² C â‹™ forget C

instance : HasForgetâ‚‚ (GradedObject Î² C) C where forgetâ‚‚ := total Î² C

end

end GradedObject

namespace GradedObject

variable {I J K : Type*} {C : Type*} [Category C]
  (X Y Z : GradedObject I C) (Ï† : X âŸ¶ Y) (e : X â‰… Y) (Ïˆ : Y âŸ¶ Z) (p : I â†’ J)

/-- If `X : GradedObject I C` and `p : I â†’ J`, `X.mapObjFun p j` is the family of objects `X i`
for `i : I` such that `p i = j`. -/
abbrev mapObjFun (j : J) (i : p â»Â¹' {j}) : C := X i

variable (j : J)

/-- Given `X : GradedObject I C` and `p : I â†’ J`, `X.HasMap p` is the condition that
for all `j : J`, the coproduct of all `X i` such `p i = j` exists. -/
abbrev HasMap : Prop := âˆ€ (j : J), HasCoproduct (X.mapObjFun p j)

variable {X Y} in
lemma hasMap_of_iso [HasMap X p] : HasMap Y p := fun j => by
  have Î± : Discrete.functor (X.mapObjFun p j) â‰… Discrete.functor (Y.mapObjFun p j) :=
    Discrete.natIso (fun âŸ¨i, _âŸ© => (GradedObject.eval i).mapIso e)
  exact hasColimitOfIso Î±.symm

variable [X.HasMap p] [Y.HasMap p] [Z.HasMap p]

/-- Given `X : GradedObject I C` and `p : I â†’ J`, `X.mapObj p` is the graded object by `J`
which in degree `j` consists of the coproduct of the `X i` such that `p i = j`. -/
noncomputable def mapObj : GradedObject J C := fun j => âˆ (X.mapObjFun p j)

/-- The canonical inclusion `X i âŸ¶ X.mapObj p j` when `i : I` and `j : J` are such
that `p i = j`. -/
noncomputable def Î¹MapObj (i : I) (j : J) (hij : p i = j) : X i âŸ¶ X.mapObj p j :=
  Sigma.Î¹ (X.mapObjFun p j) âŸ¨i, hijâŸ©

/-- Given `X : GradedObject I C`, `p : I â†’ J` and `j : J`,
`CofanMapObjFun X p j` is the type `Cofan (X.mapObjFun p j)`. The point object of
such colimits cofans are isomorphic to `X.mapObj p j`, see `CofanMapObjFun.iso`. -/
abbrev CofanMapObjFun (j : J) : Type _ := Cofan (X.mapObjFun p j)

-- in order to use the cofan API, some definitions below
-- have a `simp` attribute rather than `simps`
/-- Constructor for `CofanMapObjFun X p j`. -/
@[simp]
def CofanMapObjFun.mk (j : J) (pt : C) (Î¹' : âˆ€ (i : I) (_ : p i = j), X i âŸ¶ pt) :
    CofanMapObjFun X p j :=
  Cofan.mk pt (fun âŸ¨i, hiâŸ© => Î¹' i hi)

/-- The tautological cofan corresponding to the coproduct decomposition of `X.mapObj p j`. -/
@[simp]
noncomputable def cofanMapObj (j : J) : CofanMapObjFun X p j :=
  CofanMapObjFun.mk X p j (X.mapObj p j) (fun i hi => X.Î¹MapObj p i j hi)

/-- Given `X : GradedObject I C`, `p : I â†’ J` and `j : J`, `X.mapObj p j` satisfies
the universal property of the coproduct of those `X i` such that `p i = j`. -/
noncomputable def isColimitCofanMapObj (j : J) : IsColimit (X.cofanMapObj p j) :=
  colimit.isColimit _

@[ext]
lemma mapObj_ext {A : C} {j : J} (f g : X.mapObj p j âŸ¶ A)
    (hfg : âˆ€ (i : I) (hij : p i = j), X.Î¹MapObj p i j hij â‰« f = X.Î¹MapObj p i j hij â‰« g) :
    f = g :=
  Cofan.IsColimit.hom_ext (X.isColimitCofanMapObj p j) _ _ (fun âŸ¨i, hijâŸ© => hfg i hij)

/-- This is the morphism `X.mapObj p j âŸ¶ A` constructed from a family of
morphisms `X i âŸ¶ A` for all `i : I` such that `p i = j`. -/
noncomputable def descMapObj {A : C} {j : J} (Ï† : âˆ€ (i : I) (_ : p i = j), X i âŸ¶ A) :
    X.mapObj p j âŸ¶ A :=
  Cofan.IsColimit.desc (X.isColimitCofanMapObj p j) (fun âŸ¨i, hiâŸ© => Ï† i hi)

@[reassoc (attr := simp)]
lemma Î¹_descMapObj {A : C} {j : J}
    (Ï† : âˆ€ (i : I) (_ : p i = j), X i âŸ¶ A) (i : I) (hi : p i = j) :
    X.Î¹MapObj p i j hi â‰« X.descMapObj p Ï† = Ï† i hi := by
  apply Cofan.IsColimit.fac

namespace CofanMapObjFun

lemma hasMap (c : âˆ€ j, CofanMapObjFun X p j) (hc : âˆ€ j, IsColimit (c j)) :
    X.HasMap p := fun j => âŸ¨_, hc jâŸ©

variable {j X p}
variable {c : CofanMapObjFun X p j} (hc : IsColimit c) [X.HasMap p]

/-- If `c : CofanMapObjFun X p j` is a colimit cofan, this is the induced
isomorphism `c.pt â‰… X.mapObj p j`. -/
noncomputable def iso : c.pt â‰… X.mapObj p j :=
  IsColimit.coconePointUniqueUpToIso hc (X.isColimitCofanMapObj p j)

@[reassoc (attr := simp)]
lemma inj_iso_hom (i : I) (hi : p i = j) :
    c.inj âŸ¨i, hiâŸ© â‰« (c.iso hc).hom = X.Î¹MapObj p i j hi := by
  apply IsColimit.comp_coconePointUniqueUpToIso_hom

@[reassoc (attr := simp)]
lemma Î¹MapObj_iso_inv (i : I) (hi : p i = j) :
    X.Î¹MapObj p i j hi â‰« (c.iso hc).inv = c.inj âŸ¨i, hiâŸ© := by
  apply IsColimit.comp_coconePointUniqueUpToIso_inv

end CofanMapObjFun

variable {X Y}

/-- The canonical morphism of `J`-graded objects `X.mapObj p âŸ¶ Y.mapObj p` induced by
a morphism `X âŸ¶ Y` of `I`-graded objects and a map `p : I â†’ J`. -/
noncomputable def mapMap : X.mapObj p âŸ¶ Y.mapObj p := fun j =>
  X.descMapObj p (fun i hi => Ï† i â‰« Y.Î¹MapObj p i j hi)

@[reassoc (attr := simp)]
lemma Î¹_mapMap (i : I) (j : J) (hij : p i = j) :
    X.Î¹MapObj p i j hij â‰« mapMap Ï† p j = Ï† i â‰« Y.Î¹MapObj p i j hij := by
  simp only [mapMap, Î¹_descMapObj]

lemma congr_mapMap (Ï†â‚ Ï†â‚‚ : X âŸ¶ Y) (h : Ï†â‚ = Ï†â‚‚) : mapMap Ï†â‚ p = mapMap Ï†â‚‚ p := by
  subst h
  rfl

variable (X)

@[simp]
lemma mapMap_id : mapMap (ðŸ™ X) p = ðŸ™ _ := by aesop_cat

variable {X Z}

@[simp, reassoc]
lemma mapMap_comp : mapMap (Ï† â‰« Ïˆ) p = mapMap Ï† p â‰« mapMap Ïˆ p := by aesop_cat

/-- The isomorphism of `J`-graded objects `X.mapObj p â‰… Y.mapObj p` induced by an
isomorphism `X â‰… Y` of graded objects and a map `p : I â†’ J`. -/
@[simps]
noncomputable def mapIso : X.mapObj p â‰… Y.mapObj p where
  hom := mapMap e.hom p
  inv := mapMap e.inv p

variable (C)

/-- Given a map `p : I â†’ J`, this is the functor `GradedObject I C â¥¤ GradedObject J C` which
sends an `I`-object `X` to the graded object `X.mapObj p` which in degree `j : J` is given
by the coproduct of those `X i` such that `p i = j`. -/
@[simps]
noncomputable def map [âˆ€ (j : J), HasColimitsOfShape (Discrete (p â»Â¹' {j})) C] :
    GradedObject I C â¥¤ GradedObject J C where
  obj X := X.mapObj p
  map Ï† := mapMap Ï† p

variable {C} (X Y)
variable (q : J â†’ K) (r : I â†’ K) (hpqr : âˆ€ i, q (p i) = r i)

section

variable (k : K) (c : âˆ€ (j : J), q j = k â†’ X.CofanMapObjFun p j)
  (hc : âˆ€ j hj, IsColimit (c j hj))
  (c' : Cofan (fun (j : q â»Â¹' {k}) => (c j.1 j.2).pt)) (hc' : IsColimit c')

/-- Given maps `p : I â†’ J`, `q : J â†’ K` and `r : I â†’ K` such that `q.comp p = r`,
`X : GradedObject I C`, `k : K`, the datum of cofans `X.CofanMapObjFun p j` for all
`j : J` and of a cofan for all the points of these cofans, this is a cofan of
type `X.CofanMapObjFun r k`, which is a colimit (see `isColimitCofanMapObjComp`) if the
given cofans are. -/
@[simp]
def cofanMapObjComp : X.CofanMapObjFun r k :=
  CofanMapObjFun.mk _ _ _ c'.pt (fun i hi =>
    (c (p i) (by rw [hpqr, hi])).inj âŸ¨i, rflâŸ© â‰« c'.inj (âŸ¨p i, by
      rw [Set.mem_preimage, Set.mem_singleton_iff, hpqr, hi]âŸ©))

/-- Given maps `p : I â†’ J`, `q : J â†’ K` and `r : I â†’ K` such that `q.comp p = r`,
`X : GradedObject I C`, `k : K`, the cofan constructed by `cofanMapObjComp` is a colimit.
In other words, if we have, for all `j : J` such that `hj : q j = k`,
a colimit cofan `c j hj` which computes the coproduct of the `X i` such that `p i = j`,
and also a colimit cofan which computes the coproduct of the points of these `c j hj`, then
the point of this latter cofan computes the coproduct of the `X i` such that `r i = k`. -/
@[simp]
def isColimitCofanMapObjComp :
    IsColimit (cofanMapObjComp X p q r hpqr k c c') :=
  mkCofanColimit _
    (fun s => Cofan.IsColimit.desc hc'
      (fun âŸ¨j, (hj : q j = k)âŸ© => Cofan.IsColimit.desc (hc j hj)
        (fun âŸ¨i, (hi : p i = j)âŸ© => s.inj âŸ¨i, by
          simp only [Set.mem_preimage, Set.mem_singleton_iff, â† hpqr, hi, hj]âŸ©)))
    (fun s âŸ¨i, (hi : r i = k)âŸ© => by simp)
    (fun s m hm => by
      apply Cofan.IsColimit.hom_ext hc'
      rintro âŸ¨j, rfl : q j = kâŸ©
      apply Cofan.IsColimit.hom_ext (hc j rfl)
      rintro âŸ¨i, rfl : p i = jâŸ©
      dsimp
      rw [Cofan.IsColimit.fac, Cofan.IsColimit.fac, â† hm]
      dsimp
      rw [assoc])

lemma hasMap_comp [X.HasMap p] [(X.mapObj p).HasMap q] : X.HasMap r :=
  fun k => âŸ¨_, isColimitCofanMapObjComp X p q r hpqr k _
    (fun j _ => X.isColimitCofanMapObj p j) _ ((X.mapObj p).isColimitCofanMapObj q k)âŸ©

end

section HasZeroMorphisms

end HasZeroMorphisms

variable [HasZeroMorphisms C] [DecidableEq J] (i : I) (j : J)

/-- The canonical inclusion `X i âŸ¶ X.mapObj p j` when `p i = j`, the zero morphism otherwise. -/
noncomputable def Î¹MapObjOrZero : X i âŸ¶ X.mapObj p j :=
  if h : p i = j
    then X.Î¹MapObj p i j h
    else 0

lemma Î¹MapObjOrZero_eq (h : p i = j) : X.Î¹MapObjOrZero p i j = X.Î¹MapObj p i j h := dif_pos h

lemma Î¹MapObjOrZero_eq_zero (h : p i â‰  j) : X.Î¹MapObjOrZero p i j = 0 := dif_neg h

variable {X Y} in
@[reassoc (attr := simp)]
lemma Î¹MapObjOrZero_mapMap :
    X.Î¹MapObjOrZero p i j â‰« mapMap Ï† p j = Ï† i â‰« Y.Î¹MapObjOrZero p i j := by
  by_cases h : p i = j
  Â· simp only [Î¹MapObjOrZero_eq _ _ _ _ h, Î¹_mapMap]
  Â· simp only [Î¹MapObjOrZero_eq_zero _ _ _ _ h, zero_comp, comp_zero]

end GradedObject

end CategoryTheory
