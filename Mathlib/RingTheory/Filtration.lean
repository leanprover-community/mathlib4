/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.RingTheory.Ideal.LocalRing
import Mathlib.RingTheory.Noetherian
import Mathlib.RingTheory.ReesAlgebra
import Mathlib.RingTheory.Finiteness
import Mathlib.Data.Polynomial.Module
import Mathlib.Order.Hom.Lattice

#align_import ring_theory.filtration from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!

# `I`-filtrations of modules

This file contains the definitions and basic results around (stable) `I`-filtrations of modules.

## Main results

- `Ideal.Filtration`: An `I`-filtration on the module `M` is a sequence of decreasing submodules
  `N i` such that `I â€¢ N â‰¤ I (i + 1)`. Note that we do not require the filtration to start from `âŠ¤`.
- `Ideal.Filtration.Stable`: An `I`-filtration is stable if `I â€¢ (N i) = N (i + 1)` for large
  enough `i`.
- `Ideal.Filtration.submodule`: The associated module `â¨ Náµ¢` of a filtration, implemented as a
  submodule of `M[X]`.
- `Ideal.Filtration.submodule_fg_iff_stable`: If `F.N i` are all finitely generated, then
  `F.Stable` iff `F.submodule.FG`.
- `Ideal.Filtration.Stable.of_le`: In a finite module over a noetherian ring,
  if `F' â‰¤ F`, then `F.Stable â†’ F'.Stable`.
- `Ideal.exists_pow_inf_eq_pow_smul`: **Artin-Rees lemma**.
  given `N â‰¤ M`, there exists a `k` such that `Iâ¿M âŠ“ N = Iâ¿â»áµ(IáµM âŠ“ N)` for all `n â‰¥ k`.
- `Ideal.iInf_pow_eq_bot_of_localRing`:
  **Krull's intersection theorem** (`â¨… i, I ^ i = âŠ¥`) for noetherian local rings.
- `Ideal.iInf_pow_eq_bot_of_isDomain`:
  **Krull's intersection theorem** (`â¨… i, I ^ i = âŠ¥`) for noetherian domains.

-/


universe u v

variable {R M : Type u} [CommRing R] [AddCommGroup M] [Module R M] (I : Ideal R)

open Polynomial

open scoped Polynomial BigOperators

/-- An `I`-filtration on the module `M` is a sequence of decreasing submodules `N i` such that
`I â€¢ (N i) â‰¤ N (i + 1)`. Note that we do not require the filtration to start from `âŠ¤`. -/
@[ext]
structure Ideal.Filtration (M : Type u) [AddCommGroup M] [Module R M] where
  N : â„• â†’ Submodule R M
  mono : âˆ€ i, N (i + 1) â‰¤ N i
  smul_le : âˆ€ i, I â€¢ N i â‰¤ N (i + 1)
#align ideal.filtration Ideal.Filtration

variable (F F' : I.Filtration M) {I}

namespace Ideal.Filtration

theorem pow_smul_le (i j : â„•) : I ^ i â€¢ F.N j â‰¤ F.N (i + j) := by
  induction' i with _ ih
  -- âŠ¢ I ^ Nat.zero â€¢ N F j â‰¤ N F (Nat.zero + j)
  Â· simp
    -- ğŸ‰ no goals
  Â· rw [pow_succ, mul_smul, Nat.succ_eq_add_one, add_assoc, add_comm 1, â† add_assoc]
    -- âŠ¢ I â€¢ I ^ nâœ â€¢ N F j â‰¤ N F (nâœ + j + 1)
    exact (Submodule.smul_mono_right ih).trans (F.smul_le _)
    -- ğŸ‰ no goals
#align ideal.filtration.pow_smul_le Ideal.Filtration.pow_smul_le

theorem pow_smul_le_pow_smul (i j k : â„•) : I ^ (i + k) â€¢ F.N j â‰¤ I ^ k â€¢ F.N (i + j) := by
  rw [add_comm, pow_add, mul_smul]
  -- âŠ¢ I ^ k â€¢ I ^ i â€¢ N F j â‰¤ I ^ k â€¢ N F (i + j)
  exact Submodule.smul_mono_right (F.pow_smul_le i j)
  -- ğŸ‰ no goals
#align ideal.filtration.pow_smul_le_pow_smul Ideal.Filtration.pow_smul_le_pow_smul

protected theorem antitone : Antitone F.N :=
  antitone_nat_of_succ_le F.mono
#align ideal.filtration.antitone Ideal.Filtration.antitone

/-- The trivial `I`-filtration of `N`. -/
@[simps]
def _root_.Ideal.trivialFiltration (I : Ideal R) (N : Submodule R M) : I.Filtration M where
  N _ := N
  mono _ := le_of_eq rfl
  smul_le _ := Submodule.smul_le_right
#align ideal.trivial_filtration Ideal.trivialFiltration

/-- The `sup` of two `I.Filtration`s is an `I.Filtration`. -/
instance : Sup (I.Filtration M) :=
  âŸ¨fun F F' =>
    âŸ¨F.N âŠ” F'.N, fun i => sup_le_sup (F.mono i) (F'.mono i), fun i =>
      (le_of_eq (Submodule.smul_sup _ _ _)).trans <| sup_le_sup (F.smul_le i) (F'.smul_le i)âŸ©âŸ©

/-- The `sSup` of a family of `I.Filtration`s is an `I.Filtration`. -/
instance : SupSet (I.Filtration M) :=
  âŸ¨fun S =>
    { N := sSup (Ideal.Filtration.N '' S)
      mono := fun i => by
        apply sSup_le_sSup_of_forall_exists_le _
        -- âŠ¢ âˆ€ (x : Submodule R M), (x âˆˆ Set.range fun f => â†‘f (i + 1)) â†’ âˆƒ y, (y âˆˆ Set.r â€¦
        rintro _ âŸ¨âŸ¨_, F, hF, rflâŸ©, rflâŸ©
        -- âŠ¢ âˆƒ y, (y âˆˆ Set.range fun f => â†‘f i) âˆ§ (fun f => â†‘f (i + 1)) { val := F.N, pro â€¦
        exact âŸ¨_, âŸ¨âŸ¨_, F, hF, rflâŸ©, rflâŸ©, F.mono iâŸ©
        -- ğŸ‰ no goals
      smul_le := fun i => by
        rw [sSup_eq_iSup', iSup_apply, Submodule.smul_iSup, iSup_apply]
        -- âŠ¢ â¨† (i_1 : â†‘(N '' S)), I â€¢ â†‘i_1 i â‰¤ â¨† (i_1 : â†‘(N '' S)), â†‘i_1 (i + 1)
        apply iSup_mono _
        -- âŠ¢ âˆ€ (i_1 : â†‘(N '' S)), I â€¢ â†‘i_1 i â‰¤ â†‘i_1 (i + 1)
        rintro âŸ¨_, F, hF, rflâŸ©
        -- âŠ¢ I â€¢ â†‘{ val := F.N, property := (_ : âˆƒ a, a âˆˆ S âˆ§ a.N = F.N) } i â‰¤ â†‘{ val :=  â€¦
        exact F.smul_le i }âŸ©
        -- ğŸ‰ no goals

/-- The `inf` of two `I.Filtration`s is an `I.Filtration`. -/
instance : Inf (I.Filtration M) :=
  âŸ¨fun F F' =>
    âŸ¨F.N âŠ“ F'.N, fun i => inf_le_inf (F.mono i) (F'.mono i), fun i =>
      (Submodule.smul_inf_le _ _ _).trans <| inf_le_inf (F.smul_le i) (F'.smul_le i)âŸ©âŸ©

/-- The `sInf` of a family of `I.Filtration`s is an `I.Filtration`. -/
instance : InfSet (I.Filtration M) :=
  âŸ¨fun S =>
    { N := sInf (Ideal.Filtration.N '' S)
      mono := fun i => by
        apply sInf_le_sInf_of_forall_exists_le _
        -- âŠ¢ âˆ€ (x : Submodule R M), (x âˆˆ Set.range fun f => â†‘f i) â†’ âˆƒ y, (y âˆˆ Set.range f â€¦
        rintro _ âŸ¨âŸ¨_, F, hF, rflâŸ©, rflâŸ©
        -- âŠ¢ âˆƒ y, (y âˆˆ Set.range fun f => â†‘f (i + 1)) âˆ§ y â‰¤ (fun f => â†‘f i) { val := F.N, â€¦
        exact âŸ¨_, âŸ¨âŸ¨_, F, hF, rflâŸ©, rflâŸ©, F.mono iâŸ©
        -- ğŸ‰ no goals
      smul_le := fun i => by
        rw [sInf_eq_iInf', iInf_apply, iInf_apply]
        -- âŠ¢ I â€¢ â¨… (i_1 : â†‘(N '' S)), â†‘i_1 i â‰¤ â¨… (i_1 : â†‘(N '' S)), â†‘i_1 (i + 1)
        refine' Submodule.smul_iInf_le.trans _
        -- âŠ¢ â¨… (i_1 : â†‘(N '' S)), I â€¢ â†‘i_1 i â‰¤ â¨… (i_1 : â†‘(N '' S)), â†‘i_1 (i + 1)
        apply iInf_mono _
        -- âŠ¢ âˆ€ (i_1 : â†‘(N '' S)), I â€¢ â†‘i_1 i â‰¤ â†‘i_1 (i + 1)
        rintro âŸ¨_, F, hF, rflâŸ©
        -- âŠ¢ I â€¢ â†‘{ val := F.N, property := (_ : âˆƒ a, a âˆˆ S âˆ§ a.N = F.N) } i â‰¤ â†‘{ val :=  â€¦
        exact F.smul_le i }âŸ©
        -- ğŸ‰ no goals

instance : Top (I.Filtration M) :=
  âŸ¨I.trivialFiltration âŠ¤âŸ©

instance : Bot (I.Filtration M) :=
  âŸ¨I.trivialFiltration âŠ¥âŸ©

@[simp]
theorem sup_N : (F âŠ” F').N = F.N âŠ” F'.N :=
  rfl
set_option linter.uppercaseLean3 false in
#align ideal.filtration.sup_N Ideal.Filtration.sup_N

@[simp]
theorem sSup_N (S : Set (I.Filtration M)) : (sSup S).N = sSup (Ideal.Filtration.N '' S) :=
  rfl
set_option linter.uppercaseLean3 false in
#align ideal.filtration.Sup_N Ideal.Filtration.sSup_N

@[simp]
theorem inf_N : (F âŠ“ F').N = F.N âŠ“ F'.N :=
  rfl
set_option linter.uppercaseLean3 false in
#align ideal.filtration.inf_N Ideal.Filtration.inf_N

@[simp]
theorem sInf_N (S : Set (I.Filtration M)) : (sInf S).N = sInf (Ideal.Filtration.N '' S) :=
  rfl
set_option linter.uppercaseLean3 false in
#align ideal.filtration.Inf_N Ideal.Filtration.sInf_N

@[simp]
theorem top_N : (âŠ¤ : I.Filtration M).N = âŠ¤ :=
  rfl
set_option linter.uppercaseLean3 false in
#align ideal.filtration.top_N Ideal.Filtration.top_N

@[simp]
theorem bot_N : (âŠ¥ : I.Filtration M).N = âŠ¥ :=
  rfl
set_option linter.uppercaseLean3 false in
#align ideal.filtration.bot_N Ideal.Filtration.bot_N

@[simp]
theorem iSup_N {Î¹ : Sort*} (f : Î¹ â†’ I.Filtration M) : (iSup f).N = â¨† i, (f i).N :=
  congr_arg sSup (Set.range_comp _ _).symm
set_option linter.uppercaseLean3 false in
#align ideal.filtration.supr_N Ideal.Filtration.iSup_N

@[simp]
theorem iInf_N {Î¹ : Sort*} (f : Î¹ â†’ I.Filtration M) : (iInf f).N = â¨… i, (f i).N :=
  congr_arg sInf (Set.range_comp _ _).symm
set_option linter.uppercaseLean3 false in
#align ideal.filtration.infi_N Ideal.Filtration.iInf_N

instance : CompleteLattice (I.Filtration M) :=
  Function.Injective.completeLattice Ideal.Filtration.N Ideal.Filtration.ext sup_N inf_N
    (fun _ => sSup_image) (fun _ => sInf_image) top_N bot_N

instance : Inhabited (I.Filtration M) :=
  âŸ¨âŠ¥âŸ©

/-- An `I` filtration is stable if `I â€¢ F.N n = F.N (n+1)` for large enough `n`. -/
def Stable : Prop :=
  âˆƒ nâ‚€, âˆ€ n â‰¥ nâ‚€, I â€¢ F.N n = F.N (n + 1)
#align ideal.filtration.stable Ideal.Filtration.Stable

/-- The trivial stable `I`-filtration of `N`. -/
@[simps]
def _root_.Ideal.stableFiltration (I : Ideal R) (N : Submodule R M) : I.Filtration M where
  N i := I ^ i â€¢ N
  mono i := by dsimp only; rw [add_comm, pow_add, mul_smul]; exact Submodule.smul_le_right
               -- âŠ¢ I ^ (i + 1) â€¢ N â‰¤ I ^ i â€¢ N
                           -- âŠ¢ I ^ 1 â€¢ I ^ i â€¢ N â‰¤ I ^ i â€¢ N
                                                             -- ğŸ‰ no goals
  smul_le i := by dsimp only; rw [add_comm, pow_add, mul_smul, pow_one]
                  -- âŠ¢ I â€¢ I ^ i â€¢ N â‰¤ I ^ (i + 1) â€¢ N
                              -- ğŸ‰ no goals
#align ideal.stable_filtration Ideal.stableFiltration

theorem _root_.Ideal.stableFiltration_stable (I : Ideal R) (N : Submodule R M) :
    (I.stableFiltration N).Stable := by
  use 0
  -- âŠ¢ âˆ€ (n : â„•), n â‰¥ 0 â†’ I â€¢ Ideal.Filtration.N (stableFiltration I N) n = Ideal.F â€¦
  intro n _
  -- âŠ¢ I â€¢ Ideal.Filtration.N (stableFiltration I N) n = Ideal.Filtration.N (stable â€¦
  dsimp
  -- âŠ¢ I â€¢ I ^ n â€¢ N = I ^ (n + 1) â€¢ N
  rw [add_comm, pow_add, mul_smul, pow_one]
  -- ğŸ‰ no goals
#align ideal.stable_filtration_stable Ideal.stableFiltration_stable

variable {F F'} (h : F.Stable)

theorem Stable.exists_pow_smul_eq : âˆƒ nâ‚€, âˆ€ k, F.N (nâ‚€ + k) = I ^ k â€¢ F.N nâ‚€ := by
  obtain âŸ¨nâ‚€, hnâŸ© := h
  -- âŠ¢ âˆƒ nâ‚€, âˆ€ (k : â„•), N F (nâ‚€ + k) = I ^ k â€¢ N F nâ‚€
  use nâ‚€
  -- âŠ¢ âˆ€ (k : â„•), N F (nâ‚€ + k) = I ^ k â€¢ N F nâ‚€
  intro k
  -- âŠ¢ N F (nâ‚€ + k) = I ^ k â€¢ N F nâ‚€
  induction' k with _ ih
  -- âŠ¢ N F (nâ‚€ + Nat.zero) = I ^ Nat.zero â€¢ N F nâ‚€
  Â· simp
    -- ğŸ‰ no goals
  Â· rw [Nat.succ_eq_add_one, â† add_assoc, â† hn, ih, add_comm, pow_add, mul_smul, pow_one]
    -- âŠ¢ nâ‚€ + nâœ â‰¥ nâ‚€
    linarith
    -- ğŸ‰ no goals
#align ideal.filtration.stable.exists_pow_smul_eq Ideal.Filtration.Stable.exists_pow_smul_eq

theorem Stable.exists_pow_smul_eq_of_ge : âˆƒ nâ‚€, âˆ€ n â‰¥ nâ‚€, F.N n = I ^ (n - nâ‚€) â€¢ F.N nâ‚€ := by
  obtain âŸ¨nâ‚€, hnâ‚€âŸ© := h.exists_pow_smul_eq
  -- âŠ¢ âˆƒ nâ‚€, âˆ€ (n : â„•), n â‰¥ nâ‚€ â†’ N F n = I ^ (n - nâ‚€) â€¢ N F nâ‚€
  use nâ‚€
  -- âŠ¢ âˆ€ (n : â„•), n â‰¥ nâ‚€ â†’ N F n = I ^ (n - nâ‚€) â€¢ N F nâ‚€
  intro n hn
  -- âŠ¢ N F n = I ^ (n - nâ‚€) â€¢ N F nâ‚€
  convert hnâ‚€ (n - nâ‚€)
  -- âŠ¢ n = nâ‚€ + (n - nâ‚€)
  rw [add_comm, tsub_add_cancel_of_le hn]
  -- ğŸ‰ no goals
#align ideal.filtration.stable.exists_pow_smul_eq_of_ge Ideal.Filtration.Stable.exists_pow_smul_eq_of_ge

theorem stable_iff_exists_pow_smul_eq_of_ge :
    F.Stable â†” âˆƒ nâ‚€, âˆ€ n â‰¥ nâ‚€, F.N n = I ^ (n - nâ‚€) â€¢ F.N nâ‚€ := by
  refine' âŸ¨Stable.exists_pow_smul_eq_of_ge, fun h => âŸ¨h.choose, fun n hn => _âŸ©âŸ©
  -- âŠ¢ I â€¢ N F n = N F (n + 1)
  rw [h.choose_spec n hn, h.choose_spec (n + 1) (by linarith), smul_smul, â† pow_succ,
    tsub_add_eq_add_tsub hn]
#align ideal.filtration.stable_iff_exists_pow_smul_eq_of_ge Ideal.Filtration.stable_iff_exists_pow_smul_eq_of_ge

theorem Stable.exists_forall_le (h : F.Stable) (e : F.N 0 â‰¤ F'.N 0) :
    âˆƒ nâ‚€, âˆ€ n, F.N (n + nâ‚€) â‰¤ F'.N n := by
  obtain âŸ¨nâ‚€, hFâŸ© := h
  -- âŠ¢ âˆƒ nâ‚€, âˆ€ (n : â„•), N F (n + nâ‚€) â‰¤ N F' n
  use nâ‚€
  -- âŠ¢ âˆ€ (n : â„•), N F (n + nâ‚€) â‰¤ N F' n
  intro n
  -- âŠ¢ N F (n + nâ‚€) â‰¤ N F' n
  induction' n with n hn
  -- âŠ¢ N F (Nat.zero + nâ‚€) â‰¤ N F' Nat.zero
  Â· refine' (F.antitone _).trans e; simp
    -- âŠ¢ 0 â‰¤ Nat.zero + nâ‚€
                                    -- ğŸ‰ no goals
  Â· rw [Nat.succ_eq_one_add, add_assoc, add_comm, add_comm 1 n, â† hF]
    -- âŠ¢ I â€¢ N F (n + nâ‚€) â‰¤ N F' (n + 1)
    exact (Submodule.smul_mono_right hn).trans (F'.smul_le _)
    -- âŠ¢ n + nâ‚€ â‰¥ nâ‚€
    simp
    -- ğŸ‰ no goals
#align ideal.filtration.stable.exists_forall_le Ideal.Filtration.Stable.exists_forall_le

theorem Stable.bounded_difference (h : F.Stable) (h' : F'.Stable) (e : F.N 0 = F'.N 0) :
    âˆƒ nâ‚€, âˆ€ n, F.N (n + nâ‚€) â‰¤ F'.N n âˆ§ F'.N (n + nâ‚€) â‰¤ F.N n := by
  obtain âŸ¨nâ‚, hâ‚âŸ© := h.exists_forall_le (le_of_eq e)
  -- âŠ¢ âˆƒ nâ‚€, âˆ€ (n : â„•), N F (n + nâ‚€) â‰¤ N F' n âˆ§ N F' (n + nâ‚€) â‰¤ N F n
  obtain âŸ¨nâ‚‚, hâ‚‚âŸ© := h'.exists_forall_le (le_of_eq e.symm)
  -- âŠ¢ âˆƒ nâ‚€, âˆ€ (n : â„•), N F (n + nâ‚€) â‰¤ N F' n âˆ§ N F' (n + nâ‚€) â‰¤ N F n
  use max nâ‚ nâ‚‚
  -- âŠ¢ âˆ€ (n : â„•), N F (n + max nâ‚ nâ‚‚) â‰¤ N F' n âˆ§ N F' (n + max nâ‚ nâ‚‚) â‰¤ N F n
  intro n
  -- âŠ¢ N F (n + max nâ‚ nâ‚‚) â‰¤ N F' n âˆ§ N F' (n + max nâ‚ nâ‚‚) â‰¤ N F n
  refine' âŸ¨(F.antitone _).trans (hâ‚ n), (F'.antitone _).trans (hâ‚‚ n)âŸ© <;> simp
  -- âŠ¢ n + nâ‚ â‰¤ n + max nâ‚ nâ‚‚
                                                                          -- ğŸ‰ no goals
                                                                          -- ğŸ‰ no goals
#align ideal.filtration.stable.bounded_difference Ideal.Filtration.Stable.bounded_difference

open PolynomialModule

variable (F F')

/-- The `R[IX]`-submodule of `M[X]` associated with an `I`-filtration. -/
protected def submodule : Submodule (reesAlgebra I) (PolynomialModule R M) where
  carrier := { f | âˆ€ i, f i âˆˆ F.N i }
  add_mem' hf hg i := Submodule.add_mem _ (hf i) (hg i)
  zero_mem' i := Submodule.zero_mem _
  smul_mem' r f hf i := by
    rw [Subalgebra.smul_def, PolynomialModule.smul_apply]
    -- âŠ¢ âˆ‘ x in Finset.Nat.antidiagonal i, coeff (â†‘r) x.fst â€¢ â†‘f x.snd âˆˆ N F i
    apply Submodule.sum_mem
    -- âŠ¢ âˆ€ (c : â„• Ã— â„•), c âˆˆ Finset.Nat.antidiagonal i â†’ coeff (â†‘r) c.fst â€¢ â†‘f c.snd âˆˆ â€¦
    rintro âŸ¨j, kâŸ© e
    -- âŠ¢ coeff â†‘r (j, k).fst â€¢ â†‘f (j, k).snd âˆˆ N F i
    rw [Finset.Nat.mem_antidiagonal] at e
    -- âŠ¢ coeff â†‘r (j, k).fst â€¢ â†‘f (j, k).snd âˆˆ N F i
    subst e
    -- âŠ¢ coeff â†‘r (j, k).fst â€¢ â†‘f (j, k).snd âˆˆ N F ((j, k).fst + (j, k).snd)
    exact F.pow_smul_le j k (Submodule.smul_mem_smul (r.2 j) (hf k))
    -- ğŸ‰ no goals
#align ideal.filtration.submodule Ideal.Filtration.submodule

@[simp]
theorem mem_submodule (f : PolynomialModule R M) : f âˆˆ F.submodule â†” âˆ€ i, f i âˆˆ F.N i :=
  Iff.rfl
#align ideal.filtration.mem_submodule Ideal.Filtration.mem_submodule

theorem inf_submodule : (F âŠ“ F').submodule = F.submodule âŠ“ F'.submodule := by
  ext
  -- âŠ¢ xâœ âˆˆ Filtration.submodule (F âŠ“ F') â†” xâœ âˆˆ Filtration.submodule F âŠ“ Filtratio â€¦
  exact forall_and
  -- ğŸ‰ no goals
#align ideal.filtration.inf_submodule Ideal.Filtration.inf_submodule

variable (I M)

/-- `Ideal.Filtration.submodule` as an `InfHom`. -/
def submoduleInfHom :
    InfHom (I.Filtration M) (Submodule (reesAlgebra I) (PolynomialModule R M)) where
  toFun := Ideal.Filtration.submodule
  map_inf' := inf_submodule
#align ideal.filtration.submodule_inf_hom Ideal.Filtration.submoduleInfHom

variable {I M}

theorem submodule_closure_single :
    AddSubmonoid.closure (â‹ƒ i, single R i '' (F.N i : Set M)) = F.submodule.toAddSubmonoid := by
  apply le_antisymm
  -- âŠ¢ AddSubmonoid.closure (â‹ƒ (i : â„•), â†‘(single R i) '' â†‘(N F i)) â‰¤ (Filtration.su â€¦
  Â· rw [AddSubmonoid.closure_le, Set.iUnion_subset_iff]
    -- âŠ¢ âˆ€ (i : â„•), â†‘(single R i) '' â†‘(N F i) âŠ† â†‘(Filtration.submodule F).toAddSubmon â€¦
    rintro i _ âŸ¨m, hm, rflâŸ© j
    -- âŠ¢ â†‘(â†‘(single R i) m) j âˆˆ N F j
    rw [single_apply]
    -- âŠ¢ (if i = j then m else 0) âˆˆ N F j
    split_ifs with h
    -- âŠ¢ m âˆˆ N F j
    Â· rwa [â† h]
      -- ğŸ‰ no goals
    Â· exact (F.N j).zero_mem
      -- ğŸ‰ no goals
  Â· intro f hf
    -- âŠ¢ f âˆˆ AddSubmonoid.closure (â‹ƒ (i : â„•), â†‘(single R i) '' â†‘(N F i))
    rw [â† f.sum_single]
    -- âŠ¢ Finsupp.sum f Finsupp.single âˆˆ AddSubmonoid.closure (â‹ƒ (i : â„•), â†‘(single R i â€¦
    apply AddSubmonoid.sum_mem _ _
    -- âŠ¢ âˆ€ (c : â„•), c âˆˆ f.support â†’ Finsupp.single c (â†‘f c) âˆˆ AddSubmonoid.closure (â‹ƒ â€¦
    rintro c -
    -- âŠ¢ Finsupp.single c (â†‘f c) âˆˆ AddSubmonoid.closure (â‹ƒ (i : â„•), â†‘(single R i) ''  â€¦
    exact AddSubmonoid.subset_closure (Set.subset_iUnion _ c <| Set.mem_image_of_mem _ (hf c))
    -- ğŸ‰ no goals
#align ideal.filtration.submodule_closure_single Ideal.Filtration.submodule_closure_single

theorem submodule_span_single :
    Submodule.span (reesAlgebra I) (â‹ƒ i, single R i '' (F.N i : Set M)) = F.submodule := by
  rw [â† Submodule.span_closure, submodule_closure_single, Submodule.coe_toAddSubmonoid]
  -- âŠ¢ Submodule.span { x // x âˆˆ reesAlgebra I } â†‘(Filtration.submodule F) = Filtra â€¦
  exact Submodule.span_eq (Filtration.submodule F)
  -- ğŸ‰ no goals
#align ideal.filtration.submodule_span_single Ideal.Filtration.submodule_span_single

theorem submodule_eq_span_le_iff_stable_ge (nâ‚€ : â„•) :
    F.submodule = Submodule.span _ (â‹ƒ i â‰¤ nâ‚€, single R i '' (F.N i : Set M)) â†”
      âˆ€ n â‰¥ nâ‚€, I â€¢ F.N n = F.N (n + 1) := by
  rw [â† submodule_span_single, â† LE.le.le_iff_eq, Submodule.span_le, Set.iUnion_subset_iff]
  -- âŠ¢ (âˆ€ (i : â„•), â†‘(single R i) '' â†‘(N F i) âŠ† â†‘(Submodule.span { x // x âˆˆ reesAlge â€¦
  swap; Â· exact Submodule.span_mono (Set.iUnionâ‚‚_subset_iUnion _ _)
  -- âŠ¢ Submodule.span { x // x âˆˆ reesAlgebra I } (â‹ƒ (i : â„•) (_ : i â‰¤ nâ‚€), â†‘(single  â€¦
          -- ğŸ‰ no goals
  constructor
  -- âŠ¢ (âˆ€ (i : â„•), â†‘(single R i) '' â†‘(N F i) âŠ† â†‘(Submodule.span { x // x âˆˆ reesAlge â€¦
  Â· intro H n hn
    -- âŠ¢ I â€¢ N F n = N F (n + 1)
    refine' (F.smul_le n).antisymm _
    -- âŠ¢ N F (n + 1) â‰¤ I â€¢ N F n
    intro x hx
    -- âŠ¢ x âˆˆ I â€¢ N F n
    obtain âŸ¨l, hlâŸ© := (Finsupp.mem_span_iff_total _ _ _).mp (H _ âŸ¨x, hx, rflâŸ©)
    -- âŠ¢ x âˆˆ I â€¢ N F n
    replace hl := congr_arg (fun f : â„• â†’â‚€ M => f (n + 1)) hl
    -- âŠ¢ x âˆˆ I â€¢ N F n
    dsimp only at hl
    -- âŠ¢ x âˆˆ I â€¢ N F n
    erw [Finsupp.single_eq_same] at hl
    -- âŠ¢ x âˆˆ I â€¢ N F n
    rw [â† hl, Finsupp.total_apply, Finsupp.sum_apply]
    -- âŠ¢ (Finsupp.sum l fun aâ‚ b => â†‘(b â€¢ â†‘aâ‚) (n + 1)) âˆˆ I â€¢ N F n
    apply Submodule.sum_mem _ _
    -- âŠ¢ âˆ€ (c : â†‘(â‹ƒ (i : â„•) (_ : i â‰¤ nâ‚€), â†‘(single R i) '' â†‘(N F i))), c âˆˆ l.support  â€¦
    rintro âŸ¨_, _, âŸ¨n', rflâŸ©, _, âŸ¨hn', rflâŸ©, m, hm, rflâŸ© -
    -- âŠ¢ (fun aâ‚ b => â†‘(b â€¢ â†‘aâ‚) (n + 1)) { val := â†‘(single R n') m, property := (_ : â€¦
    dsimp only [Subtype.coe_mk]
    -- âŠ¢ â†‘(â†‘l { val := â†‘(single R n') m, property := (_ : âˆƒ t, (t âˆˆ Set.range fun i = â€¦
    rw [Subalgebra.smul_def, smul_single_apply, if_pos (show n' â‰¤ n + 1 by linarith)]
    -- âŠ¢ coeff (â†‘(â†‘l { val := â†‘(single R n') m, property := (_ : âˆƒ t, (t âˆˆ Set.range  â€¦
    have e : n' â‰¤ n := by linarith
    -- âŠ¢ coeff (â†‘(â†‘l { val := â†‘(single R n') m, property := (_ : âˆƒ t, (t âˆˆ Set.range  â€¦
    have := F.pow_smul_le_pow_smul (n - n') n' 1
    -- âŠ¢ coeff (â†‘(â†‘l { val := â†‘(single R n') m, property := (_ : âˆƒ t, (t âˆˆ Set.range  â€¦
    rw [tsub_add_cancel_of_le e, pow_one, add_comm _ 1, â† add_tsub_assoc_of_le e, add_comm] at this
    -- âŠ¢ coeff (â†‘(â†‘l { val := â†‘(single R n') m, property := (_ : âˆƒ t, (t âˆˆ Set.range  â€¦
    exact this (Submodule.smul_mem_smul ((l _).2 <| n + 1 - n') hm)
    -- ğŸ‰ no goals
  Â· let F' := Submodule.span (reesAlgebra I) (â‹ƒ i â‰¤ nâ‚€, single R i '' (F.N i : Set M))
    -- âŠ¢ (âˆ€ (n : â„•), n â‰¥ nâ‚€ â†’ I â€¢ N F n = N F (n + 1)) â†’ âˆ€ (i : â„•), â†‘(single R i) ''  â€¦
    intro hF i
    -- âŠ¢ â†‘(single R i) '' â†‘(N F i) âŠ† â†‘(Submodule.span { x // x âˆˆ reesAlgebra I } (â‹ƒ ( â€¦
    have : âˆ€ i â‰¤ nâ‚€, single R i '' (F.N i : Set M) âŠ† F' := by
      -- Porting note: Original proof was
      -- `fun i hi => Set.Subset.trans (Set.subset_iUnionâ‚‚ i hi) Submodule.subset_span`
      intro i hi
      refine Set.Subset.trans ?_ Submodule.subset_span
      refine @Set.subset_iUnionâ‚‚ _ _ _ (fun i => fun _ => â†‘((single R i) '' ((N F i) : Set M))) i ?_
      exact hi
    induction' i with j hj
    -- âŠ¢ â†‘(single R Nat.zero) '' â†‘(N F Nat.zero) âŠ† â†‘(Submodule.span { x // x âˆˆ reesAl â€¦
    Â· exact this _ (zero_le _)
      -- ğŸ‰ no goals
    by_cases hj' : j.succ â‰¤ nâ‚€
    -- âŠ¢ â†‘(single R (Nat.succ j)) '' â†‘(N F (Nat.succ j)) âŠ† â†‘(Submodule.span { x // x  â€¦
    Â· exact this _ hj'
      -- ğŸ‰ no goals
    simp only [not_le, Nat.lt_succ_iff] at hj'
    -- âŠ¢ â†‘(single R (Nat.succ j)) '' â†‘(N F (Nat.succ j)) âŠ† â†‘(Submodule.span { x // x  â€¦
    rw [Nat.succ_eq_add_one, â† hF _ hj']
    -- âŠ¢ â†‘(single R (j + 1)) '' â†‘(I â€¢ N F j) âŠ† â†‘(Submodule.span { x // x âˆˆ reesAlgebr â€¦
    rintro _ âŸ¨m, hm, rflâŸ©
    -- âŠ¢ â†‘(single R (j + 1)) m âˆˆ â†‘(Submodule.span { x // x âˆˆ reesAlgebra I } (â‹ƒ (i :  â€¦
    refine' Submodule.smul_induction_on hm (fun r hr m' hm' => _) (fun x y hx hy => _)
    -- âŠ¢ â†‘(single R (j + 1)) (r â€¢ m') âˆˆ â†‘(Submodule.span { x // x âˆˆ reesAlgebra I } ( â€¦
    Â· rw [add_comm, â† monomial_smul_single]
      -- âŠ¢ â†‘(monomial 1) r â€¢ â†‘(single R j) m' âˆˆ â†‘(Submodule.span { x // x âˆˆ reesAlgebra â€¦
      exact F'.smul_mem
        âŸ¨_, reesAlgebra.monomial_mem.mpr (by rwa [pow_one])âŸ© (hj <| Set.mem_image_of_mem _ hm')
    Â· rw [map_add]
      -- âŠ¢ â†‘(single R (j + 1)) x + â†‘(single R (j + 1)) y âˆˆ â†‘(Submodule.span { x // x âˆˆ  â€¦
      exact F'.add_mem hx hy
      -- ğŸ‰ no goals
#align ideal.filtration.submodule_eq_span_le_iff_stable_ge Ideal.Filtration.submodule_eq_span_le_iff_stable_ge

/-- If the components of a filtration are finitely generated, then the filtration is stable iff
its associated submodule of is finitely generated. -/
theorem submodule_fg_iff_stable (hF' : âˆ€ i, (F.N i).FG) : F.submodule.FG â†” F.Stable := by
  classical
  delta Ideal.Filtration.Stable
  simp_rw [â† F.submodule_eq_span_le_iff_stable_ge]
  constructor
  Â· rintro H
    refine H.stablizes_of_iSup_eq
        âŸ¨fun nâ‚€ => Submodule.span _ (â‹ƒ (i : â„•) (_ : i â‰¤ nâ‚€), single R i '' â†‘(F.N i)), ?_âŸ© ?_
    Â· intro n m e
      rw [Submodule.span_le, Set.iUnionâ‚‚_subset_iff]
      intro i hi
      refine Set.Subset.trans ?_ Submodule.subset_span
      refine @Set.subset_iUnionâ‚‚ _ _ _ (fun i => fun _ => â†‘((single R i) '' ((N F i) : Set M))) i ?_
      exact hi.trans e
    Â· dsimp
      rw [â† Submodule.span_iUnion, â† submodule_span_single]
      congr 1
      ext
      simp only [Set.mem_iUnion, Set.mem_image, SetLike.mem_coe, exists_prop]
      constructor
      Â· rintro âŸ¨-, i, -, eâŸ©; exact âŸ¨i, eâŸ©
      Â· rintro âŸ¨i, eâŸ©; exact âŸ¨i, i, le_refl i, eâŸ©
  Â· rintro âŸ¨n, hnâŸ©
    rw [hn]
    simp_rw [Submodule.span_iUnionâ‚‚, â† Finset.mem_range_succ_iff, iSup_subtype']
    apply Submodule.fg_iSup
    rintro âŸ¨i, hiâŸ©
    obtain âŸ¨s, hsâŸ© := hF' i
    have : Submodule.span (reesAlgebra I) (s.image (lsingle R i) : Set (PolynomialModule R M)) =
        Submodule.span _ (single R i '' (F.N i : Set M)) := by
      rw [Finset.coe_image, â† Submodule.span_span_of_tower R, â† Submodule.map_span, hs]; rfl
    rw [Subtype.coe_mk, â† this]
    exact âŸ¨_, rflâŸ©
#align ideal.filtration.submodule_fg_iff_stable Ideal.Filtration.submodule_fg_iff_stable

variable {F}

theorem Stable.of_le [IsNoetherianRing R] [Module.Finite R M] (hF : F.Stable)
    {F' : I.Filtration M} (hf : F' â‰¤ F) : F'.Stable := by
  have := isNoetherian_of_isNoetherianRing_of_finite R M
  -- âŠ¢ Stable F'
  rw [â† submodule_fg_iff_stable] at hF âŠ¢
  any_goals intro i; exact IsNoetherian.noetherian _
  -- âŠ¢ Submodule.FG (Filtration.submodule F')
  have := isNoetherian_of_fg_of_noetherian _ hF
  -- âŠ¢ Submodule.FG (Filtration.submodule F')
  rw [isNoetherian_submodule] at this
  -- âŠ¢ Submodule.FG (Filtration.submodule F')
  exact this _ (OrderHomClass.mono (submoduleInfHom M I) hf)
  -- ğŸ‰ no goals
#align ideal.filtration.stable.of_le Ideal.Filtration.Stable.of_le

theorem Stable.inter_right [IsNoetherianRing R] [Module.Finite R M] (hF : F.Stable) :
    (F âŠ“ F').Stable :=
  hF.of_le inf_le_left
#align ideal.filtration.stable.inter_right Ideal.Filtration.Stable.inter_right

theorem Stable.inter_left [IsNoetherianRing R] [Module.Finite R M] (hF : F.Stable) :
    (F' âŠ“ F).Stable :=
  hF.of_le inf_le_right
#align ideal.filtration.stable.inter_left Ideal.Filtration.Stable.inter_left

end Ideal.Filtration

variable (I)

/-- **Artin-Rees lemma** -/
theorem Ideal.exists_pow_inf_eq_pow_smul [IsNoetherianRing R] [Module.Finite R M]
    (N : Submodule R M) : âˆƒ k : â„•, âˆ€ n â‰¥ k, I ^ n â€¢ âŠ¤ âŠ“ N = I ^ (n - k) â€¢ (I ^ k â€¢ âŠ¤ âŠ“ N) :=
  ((I.stableFiltration_stable âŠ¤).inter_right (I.trivialFiltration N)).exists_pow_smul_eq_of_ge
#align ideal.exists_pow_inf_eq_pow_smul Ideal.exists_pow_inf_eq_pow_smul

theorem Ideal.mem_iInf_smul_pow_eq_bot_iff [IsNoetherianRing R] [Module.Finite R M] (x : M) :
    x âˆˆ (â¨… i : â„•, I ^ i â€¢ âŠ¤ : Submodule R M) â†” âˆƒ r : I, (r : R) â€¢ x = x := by
  let N := (â¨… i : â„•, I ^ i â€¢ âŠ¤ : Submodule R M)
  -- âŠ¢ x âˆˆ â¨… (i : â„•), I ^ i â€¢ âŠ¤ â†” âˆƒ r, â†‘r â€¢ x = x
  have hN : âˆ€ k, (I.stableFiltration âŠ¤ âŠ“ I.trivialFiltration N).N k = N :=
    fun k => inf_eq_right.mpr ((iInf_le _ k).trans <| le_of_eq <| by simp)
  constructor
  -- âŠ¢ x âˆˆ â¨… (i : â„•), I ^ i â€¢ âŠ¤ â†’ âˆƒ r, â†‘r â€¢ x = x
  Â· have := isNoetherian_of_isNoetherianRing_of_finite R M
    -- âŠ¢ x âˆˆ â¨… (i : â„•), I ^ i â€¢ âŠ¤ â†’ âˆƒ r, â†‘r â€¢ x = x
    obtain âŸ¨r, hrâ‚, hrâ‚‚âŸ© :=
      Submodule.exists_mem_and_smul_eq_self_of_fg_of_le_smul I N (IsNoetherian.noetherian N) (by
        obtain âŸ¨k, hkâŸ© := (I.stableFiltration_stable âŠ¤).inter_right (I.trivialFiltration N)
        have := hk k (le_refl _)
        rw [hN, hN] at this
        exact le_of_eq this.symm)
    intro H
    -- âŠ¢ âˆƒ r, â†‘r â€¢ x = x
    exact âŸ¨âŸ¨r, hrâ‚âŸ©, hrâ‚‚ _ HâŸ©
    -- ğŸ‰ no goals
  Â· rintro âŸ¨r, eqâŸ©
    -- âŠ¢ x âˆˆ â¨… (i : â„•), I ^ i â€¢ âŠ¤
    rw [Submodule.mem_iInf]
    -- âŠ¢ âˆ€ (i : â„•), x âˆˆ I ^ i â€¢ âŠ¤
    intro i
    -- âŠ¢ x âˆˆ I ^ i â€¢ âŠ¤
    induction' i with i hi
    -- âŠ¢ x âˆˆ I ^ Nat.zero â€¢ âŠ¤
    Â· simp
      -- ğŸ‰ no goals
    Â· rw [Nat.succ_eq_one_add, pow_add, â† smul_smul, pow_one, â† eq]
      -- âŠ¢ â†‘r â€¢ x âˆˆ I â€¢ I ^ i â€¢ âŠ¤
      exact Submodule.smul_mem_smul r.prop hi
      -- ğŸ‰ no goals
#align ideal.mem_infi_smul_pow_eq_bot_iff Ideal.mem_iInf_smul_pow_eq_bot_iff

theorem Ideal.iInf_pow_smul_eq_bot_of_localRing [IsNoetherianRing R] [LocalRing R]
    [Module.Finite R M] (h : I â‰  âŠ¤) : (â¨… i : â„•, I ^ i â€¢ âŠ¤ : Submodule R M) = âŠ¥ := by
  rw [eq_bot_iff]
  -- âŠ¢ â¨… (i : â„•), I ^ i â€¢ âŠ¤ â‰¤ âŠ¥
  intro x hx
  -- âŠ¢ x âˆˆ âŠ¥
  obtain âŸ¨r, hrâŸ© := (I.mem_iInf_smul_pow_eq_bot_iff x).mp hx
  -- âŠ¢ x âˆˆ âŠ¥
  have := LocalRing.isUnit_one_sub_self_of_mem_nonunits _ (LocalRing.le_maximalIdeal h r.prop)
  -- âŠ¢ x âˆˆ âŠ¥
  apply this.smul_left_cancel.mp
  -- âŠ¢ (1 - â†‘r) â€¢ x = (1 - â†‘r) â€¢ 0
  simp [sub_smul, hr]
  -- ğŸ‰ no goals
#align ideal.infi_pow_smul_eq_bot_of_local_ring Ideal.iInf_pow_smul_eq_bot_of_localRing

/-- **Krull's intersection theorem** for noetherian local rings. -/
theorem Ideal.iInf_pow_eq_bot_of_localRing [IsNoetherianRing R] [LocalRing R] (h : I â‰  âŠ¤) :
    â¨… i : â„•, I ^ i = âŠ¥ := by
  convert I.iInf_pow_smul_eq_bot_of_localRing (M := R) h
  -- âŠ¢ I ^ xâœ = I ^ xâœ â€¢ âŠ¤
  ext i
  -- âŠ¢ i âˆˆ I ^ xâœ â†” i âˆˆ I ^ xâœ â€¢ âŠ¤
  rw [smul_eq_mul, â† Ideal.one_eq_top, mul_one]
  -- ğŸ‰ no goals
#align ideal.infi_pow_eq_bot_of_local_ring Ideal.iInf_pow_eq_bot_of_localRing

/-- **Krull's intersection theorem** for noetherian domains. -/
theorem Ideal.iInf_pow_eq_bot_of_isDomain [IsNoetherianRing R] [IsDomain R] (h : I â‰  âŠ¤) :
    â¨… i : â„•, I ^ i = âŠ¥ := by
  rw [eq_bot_iff]
  -- âŠ¢ â¨… (i : â„•), I ^ i â‰¤ âŠ¥
  intro x hx
  -- âŠ¢ x âˆˆ âŠ¥
  by_contra hx'
  -- âŠ¢ False
  have := Ideal.mem_iInf_smul_pow_eq_bot_iff I x
  -- âŠ¢ False
  simp_rw [smul_eq_mul, â† Ideal.one_eq_top, mul_one] at this
  -- âŠ¢ False
  obtain âŸ¨r, hrâŸ© := this.mp hx
  -- âŠ¢ False
  have := mul_right_cancelâ‚€ hx' (hr.trans (one_mul x).symm)
  -- âŠ¢ False
  exact I.eq_top_iff_one.not.mp h (this â–¸ r.prop)
  -- ğŸ‰ no goals
#align ideal.infi_pow_eq_bot_of_is_domain Ideal.iInf_pow_eq_bot_of_isDomain
