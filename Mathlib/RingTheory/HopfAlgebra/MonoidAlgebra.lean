/-
Copyright (c) 2025 Amelia Livingston. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Amelia Livingston
-/
import Mathlib.RingTheory.Bialgebra.MonoidAlgebra
import Mathlib.RingTheory.HopfAlgebra.Basic

/-!
# The Hopf algebra structure on group algebras

Given a group `G`, a commutative semiring `R` and an `R`-Hopf algebra `A`, this file collects
results about the `R`-Hopf algebra instance on `A[G]`, building upon results in
`Mathlib/RingTheory/Bialgebra/MonoidAlgebra.lean` about the bialgebra structure.

## Main definitions

* `(Add)MonoidAlgebra.instHopfAlgebra`: the `R`-Hopf algebra structure on `A[G]` when `G` is an
  (add) group and `A` is an `R`-Hopf algebra.
* `LaurentPolynomial.instHopfAlgebra`: the `R`-Hopf algebra structure on the Laurent polynomials
  `A[T;T‚Åª¬π]` when `A` is an `R`-Hopf algebra. When `A = R` this corresponds to the fact that `ùîæ‚Çò/R`
  is a group scheme.
-/

suppress_compilation

open HopfAlgebra

namespace MonoidAlgebra

variable {R A : Type*} [CommSemiring R] [Semiring A] [HopfAlgebra R A]
variable {G : Type*} [Group G]

variable (R A G) in
instance instHopfAlgebraStruct : HopfAlgebraStruct R (MonoidAlgebra A G) where
  antipode := Finsupp.lsum R fun g => Finsupp.lsingle g‚Åª¬π ‚àò‚Çó antipode R

@[simp]
lemma antipode_single (g : G) (a : A) :
    antipode R (single g a) = single g‚Åª¬π (antipode R a) := by
  simp [MonoidAlgebra, antipode]

open Coalgebra in
instance instHopfAlgebra : HopfAlgebra R (MonoidAlgebra A G) where
  mul_antipode_rTensor_comul := by
    ext a b : 2
    simpa [‚Üê (‚Ñõ R b).eq, -sum_antipode_mul_eq] using congr(lsingle (R := R) (1 : G)
      $(sum_antipode_mul_eq (Coalgebra.Repr.arbitrary R b)))
  mul_antipode_lTensor_comul := by
    ext a b : 2
    simpa [‚Üê (‚Ñõ R b).eq, -sum_mul_antipode_eq] using congr(lsingle (R := R) (1 : G)
      $(sum_mul_antipode_eq (Coalgebra.Repr.arbitrary R b)))

end MonoidAlgebra

namespace AddMonoidAlgebra

variable {R A : Type*} [CommSemiring R] [Semiring A] [HopfAlgebra R A]
variable {G : Type*} [AddGroup G]

variable (R A G) in
instance instHopfAlgebraStruct : HopfAlgebraStruct R A[G] where
  antipode := Finsupp.lsum R fun g => Finsupp.lsingle (-g) ‚àò‚Çó antipode R

@[simp]
lemma antipode_single (g : G) (a : A) :
    antipode R (single g a) = single (-g) (antipode R a) := by
  simp [AddMonoidAlgebra, antipode]

open Coalgebra in
instance instHopfAlgebra : HopfAlgebra R A[G] where
  mul_antipode_rTensor_comul := by
    ext a b : 2
    simpa [‚Üê (‚Ñõ R b).eq, single_mul_single, -sum_antipode_mul_eq] using
      congr(lsingle (R := R) (0 : G) $(sum_antipode_mul_eq (Coalgebra.Repr.arbitrary R b)))
  mul_antipode_lTensor_comul := by
    ext a b : 2
    simpa [‚Üê (‚Ñõ R b).eq, single_mul_single, -sum_mul_antipode_eq] using
      congr(lsingle (R := R) (0 : G) $(sum_mul_antipode_eq (Coalgebra.Repr.arbitrary R b)))

end AddMonoidAlgebra

namespace LaurentPolynomial

open Finsupp

variable (R A : Type*) [CommSemiring R] [Semiring A] [HopfAlgebra R A]

instance instHopfAlgebra : HopfAlgebra R A[T;T‚Åª¬π] :=
  inferInstanceAs (HopfAlgebra R <| AddMonoidAlgebra A ‚Ñ§)

variable {R A}

@[simp]
theorem antipode_C (a : A) :
    HopfAlgebra.antipode R (C a) = C (HopfAlgebra.antipode R a) := by
  rw [‚Üê single_eq_C, AddMonoidAlgebra.antipode_single]
  simp

@[simp]
theorem antipode_T (n : ‚Ñ§) :
    HopfAlgebra.antipode R (T n : A[T;T‚Åª¬π]) = T (-n) := by
  unfold T
  rw [AddMonoidAlgebra.antipode_single]
  simp only [HopfAlgebra.antipode_one, single_eq_C_mul_T, map_one, one_mul]

@[simp]
theorem antipode_C_mul_T (a : A) (n : ‚Ñ§) :
    HopfAlgebra.antipode R (C a * T n) = C (HopfAlgebra.antipode R a) * T (-n) := by
  simp [‚Üê single_eq_C_mul_T]

end LaurentPolynomial
