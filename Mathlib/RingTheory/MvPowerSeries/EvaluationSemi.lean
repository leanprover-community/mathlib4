/-
Copyright (c) 2024 Antoine Chambert-Loir, MarÃ­a InÃ©s de Frutos FernÃ¡ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Antoine Chambert-Loir, MarÃ­a InÃ©s de Frutos-FernÃ¡ndez
-/

import Mathlib.Algebra.MvPolynomial.CommRing
import Mathlib.RingTheory.Congruence.BigOperators
import Mathlib.RingTheory.Ideal.BigOperators
import Mathlib.RingTheory.MvPowerSeries.PiTopology
import Mathlib.RingTheory.MvPowerSeries.Trunc
import Mathlib.Topology.Algebra.Algebra
import Mathlib.Topology.Algebra.TopologicallyNilpotent
import Mathlib.Topology.Algebra.LinearUniformity

/-! # Evaluation of multivariate power series

Let `Ïƒ`, `R`, `S` be types, with `CommRing R`, `CommRing S`.
One assumes that `IsTopologicalRing R` and `IsUniformAddGroup R`,
and that `S` is a complete and separated topological `R`-algebra,
with `IsLinearTopology S S`, which means there is a basis of neighborhoods of 0
consisting of ideals.

Given `Ï† : R â†’+* S`, `a : Ïƒ â†’ S`, and `f : MvPowerSeries Ïƒ R`,
`MvPowerSeries.evalâ‚‚ f Ï† a` is the evaluation of the multivariate power series `f` at `a`.
It `f` is (the coercion of) a polynomial, it coincides with the evaluation of that polynomial.
Otherwise, it is defined by density from polynomials;
its values are irrelevant unless `Ï†` is continuous and `a` satisfies two conditions
bundled in `MvPowerSeries.HasEval a` :
  - for all `s : Ïƒ`, `a s` is topologically nilpotent,
    meaning that `(a s) ^ n` tends to 0 when `n` tends to infinity
  - when `a s` tends to zero for the filter of cofinite subsets of `Ïƒ`.

Under `Continuous Ï†` and `HasEval a`, the following lemmas furnish the properties of evaluation:

* `MvPowerSeries.evalâ‚‚Hom`: the evaluation of multivariate power series, as a ring morphism,
* `MvPowerSeries.aeval`: the evaluation map as an algebra morphism
* `MvPowerSeries.uniformContinuous_evalâ‚‚`: uniform continuity of the evaluation
* `MvPowerSeries.continuous_evalâ‚‚`: continuity of the evaluation
* `MvPowerSeries.evalâ‚‚_eq_tsum`: the evaluation is given by the sum of its monomials, evaluated.

-/

namespace MvPowerSeries

open Topology Uniformity

open Filter MvPolynomial RingHom Set TopologicalSpace UniformSpace

/- ## Necessary conditions -/

section

variable {Ïƒ : Type*}
variable {R : Type*} [CommSemiring R] [TopologicalSpace R]
variable {S : Type*} [CommSemiring S] [TopologicalSpace S]
variable {Ï† : R â†’+* S}

-- We endow MvPowerSeries Ïƒ R with the Pi topology
open WithPiTopology

/-- Families at which power series can be consistently evaluated -/
@[mk_iff hasEval_def]
structure HasEval (a : Ïƒ â†’ S) : Prop where
  hpow : âˆ€ s, IsTopologicallyNilpotent (a s)
  tendsto_zero : Tendsto a cofinite (ğ“ 0)

theorem HasEval.mono {S : Type*} [CommRing S] {a : Ïƒ â†’ S}
    {t u : TopologicalSpace S} (h : t â‰¤ u) (ha : @HasEval _ _ _ t a) :
    @HasEval _ _ _ u a :=
  âŸ¨fun s â†¦ Filter.Tendsto.mono_right (@HasEval.hpow _ _ _ t a ha s) (nhds_mono h),
   Filter.Tendsto.mono_right (@HasEval.tendsto_zero Ïƒ _ _ t a ha) (nhds_mono h)âŸ©

theorem HasEval.zero : HasEval (0 : Ïƒ â†’ S) where
  hpow _ := .zero
  tendsto_zero := tendsto_const_nhds

theorem HasEval.add [ContinuousAdd S] [IsLinearTopology S S]
    {a b : Ïƒ â†’ S} (ha : HasEval a) (hb : HasEval b) : HasEval (a + b) where
  hpow s := (ha.hpow s).add (hb.hpow s)
  tendsto_zero := by rw [â† add_zero 0]; exact ha.tendsto_zero.add hb.tendsto_zero

theorem HasEval.mul_left [IsLinearTopology S S]
    (c : Ïƒ â†’ S) {x : Ïƒ â†’ S} (hx : HasEval x) : HasEval (c * x) where
  hpow s := (hx.hpow s).mul_left (c s)
  tendsto_zero := IsLinearTopology.tendsto_mul_zero_of_right _ _ hx.tendsto_zero

theorem HasEval.mul_right [IsLinearTopology S S]
    (c : Ïƒ â†’ S) {x : Ïƒ â†’ S} (hx : HasEval x) : HasEval (x * c) :=
  mul_comm x c â–¸ HasEval.mul_left c hx

/-- [Bourbaki, *Algebra*, chap. 4, Â§4, nÂ°3, Prop. 4 (i) (a & b)][bourbaki1981]. -/
theorem HasEval.map (hÏ† : Continuous Ï†) {a : Ïƒ â†’ R} (ha : HasEval a) :
    HasEval (fun s â†¦ Ï† (a s)) where
  hpow s := (ha.hpow s).map hÏ†
  tendsto_zero := (map_zero Ï† â–¸ hÏ†.tendsto 0).comp ha.tendsto_zero

protected theorem HasEval.X :
    HasEval (fun s â†¦ (MvPowerSeries.X s : MvPowerSeries Ïƒ R)) where
  hpow s := isTopologicallyNilpotent_of_constantCoeff_zero (constantCoeff_X s)
  tendsto_zero := variables_tendsto_zero

variable [ContinuousAdd S] [IsLinearTopology S S]

/-- The domain of evaluation of `MvPowerSeries`, as an ideal -/
@[simps]
def hasEvalIdeal : Ideal (Ïƒ â†’ S) where
  carrier := {a | HasEval a}
  add_mem' := HasEval.add
  zero_mem' := HasEval.zero
  smul_mem' := HasEval.mul_left

theorem mem_hasEvalIdeal_iff {a : Ïƒ â†’ S} :
    a âˆˆ hasEvalIdeal â†” HasEval a := by
  simp [hasEvalIdeal]

end

/- ## Construction of an evaluation morphism for power series -/

section Evaluation

section Prelim

variable {Ïƒ R S : Type*} [CommSemiring R] [CommSemiring S]

-- TODO: move
theorem _root_.MvPolynomial.evalâ‚‚_eq_of_support_subset
    (g : R â†’+* S) (X : Ïƒ â†’ S) (f : MvPolynomial Ïƒ R) (t : Finset (Ïƒ â†’â‚€ â„•)) (hft : f.support âŠ† t) :
    f.evalâ‚‚ g X = âˆ‘ d âˆˆ t, g (f.coeff d) * âˆ i âˆˆ d.support, X i ^ d i := by
  rw [evalâ‚‚_eq]
  refine Finset.sum_congr_of_eq_on_inter ?_ ?_ (fun _ _ _ â†¦ rfl)
  Â· exact fun _ hâ‚ hâ‚‚ â†¦ (hâ‚‚ (hft hâ‚)).elim
  Â· intro a _ ha
    simp [MvPolynomial.notMem_support_iff.mp ha]

end Prelim

open WithPiTopology

variable {Ïƒ : Type*}
variable {R : Type*} [CommSemiring R] [UniformSpace R]
variable {S : Type*} [CommSemiring S] [UniformSpace S]
variable {Ï† : R â†’+* S}

-- We endow MvPowerSeries Ïƒ R with the product uniform structure
private instance : UniformSpace (MvPolynomial Ïƒ R) :=
  comap toMvPowerSeries (Pi.uniformSpace _)

theorem _root_.MvPolynomial.toMvPowerSeries_isUniformInducing :
    IsUniformInducing (toMvPowerSeries (Ïƒ := Ïƒ) (R := R)) :=
  (isUniformInducing_iff toMvPowerSeries).mpr rfl

theorem _root_.MvPolynomial.toMvPowerSeries_isDenseInducing :
    IsDenseInducing (toMvPowerSeries (Ïƒ := Ïƒ) (R := R)) :=
  toMvPowerSeries_isUniformInducing.isDenseInducing denseRange_toMvPowerSeries

variable {a : Ïƒ â†’ S}

/- The evaluation map on multivariate polynomials is uniformly continuous
for the uniform structure induced by that on multivariate power series. -/
theorem _root_.MvPolynomial.toMvPowerSeries_uniformContinuous
    [IsLinearUniformity S]
    (hÏ† : UniformContinuous Ï†) (ha : HasEval a) :
    UniformContinuous (MvPolynomial.evalâ‚‚Hom Ï† a) := by
  classical
  rw [UniformContinuous, IsLinearUniformity.hasBasis_ringCon.tendsto_right_iff]
  intro ğ“¡ hğ“¡
  let I : Ideal S := ğ“¡.ideal
  let hI : (I : Set S) âˆˆ ğ“ 0 := by
    rw [nhds_eq_comap_uniformity']
    exact preimage_mem_comap hğ“¡
  let n : Ïƒ â†’ â„• := fun s â†¦ sInf {n : â„• | (a s) ^ n.succ âˆˆ I}
  have hn_ne : âˆ€ s, Set.Nonempty {n : â„• | (a s) ^ n.succ âˆˆ I} := fun s â†¦ by
    rcases ha.hpow s |>.eventually_mem hI |>.exists_forall_of_atTop with âŸ¨n, hnâŸ©
    use n
    simpa using hn n.succ n.le_succ
  have hn : Set.Finite (n.support) := by
    change n =á¶ [cofinite] 0
    filter_upwards [ha.tendsto_zero.eventually_mem hI] with s has
    simpa [n, Pi.zero_apply, Nat.sInf_eq_zero, or_iff_left (hn_ne s).ne_empty] using has
  let nâ‚€ : Ïƒ â†’â‚€ â„• := .ofSupportFinite n hn
  let D := Iic nâ‚€
  have hD : Set.Finite D := finite_Iic _
  have : âˆ€ d âˆˆ D, âˆ€á¶  pq in ğ“¤ (MvPolynomial Ïƒ R), ğ“¡ (Ï† (pq.1.coeff d)) (Ï† (pq.2.coeff d)) := by
    intro d hd
    have : UniformContinuous (Ï† âˆ˜ coeff d âˆ˜ toMvPowerSeries) :=
      hÏ†.comp (uniformContinuous_coeff R d) |>.comp uniformContinuous_comap
    filter_upwards [this.eventually_mem hğ“¡] with f hf
    simpa using hf
  rw [â† hD.eventually_all] at this
  filter_upwards [this] with âŸ¨p, qâŸ© hpq
  set t : Finset (Ïƒ â†’â‚€ â„•) := p.support âˆª q.support
  have hpt : p.support âŠ† t := Finset.subset_union_left
  have hqt : q.support âŠ† t := Finset.subset_union_right
  rw [coe_evalâ‚‚Hom, evalâ‚‚_eq_of_support_subset _ _ _ _ hpt,
      evalâ‚‚_eq_of_support_subset _ _ _ _ hqt]
  apply ğ“¡.finsetSum
  intro d _
  by_cases hd : d âˆˆ D
  Â· exact ğ“¡.mul (hpq d hd) (ğ“¡.refl _)
  Â· have (r : MvPolynomial Ïƒ R) : Ï† (r.coeff d) * âˆ i âˆˆ d.support, a i ^ d i âˆˆ I := by
      apply Ideal.mul_mem_left
      simp only [mem_Iic, D, Finsupp.le_iff] at hd
      push_neg at hd
      rcases hd with âŸ¨s, hs', hsâŸ©
      exact I.prod_mem hs' (I.pow_mem_of_pow_mem (Nat.sInf_mem (hn_ne s)) hs)
    exact ğ“¡.trans (this p) (ğ“¡.symm (this q))

variable (Ï† a)
open scoped Classical in
/-- Evaluation of a multivariate power series at `f` at a point `a : Ïƒ â†’ S`.

It coincides with the evaluation of `f` as a polynomial if `f` is the coercion of a polynomial.
Otherwise, it is only relevant if `Ï†` is continuous and `HasEval a`. -/
noncomputable def evalâ‚‚ (f : MvPowerSeries Ïƒ R) : S :=
  if H : âˆƒ p : MvPolynomial Ïƒ R, p = f then (MvPolynomial.evalâ‚‚ Ï† a H.choose)
  else IsDenseInducing.extend toMvPowerSeries_isDenseInducing (MvPolynomial.evalâ‚‚ Ï† a) f

@[simp, norm_cast]
theorem evalâ‚‚_coe (f : MvPolynomial Ïƒ R) :
    MvPowerSeries.evalâ‚‚ Ï† a f = MvPolynomial.evalâ‚‚ Ï† a f := by
  have : âˆƒ p : MvPolynomial Ïƒ R, (p : MvPowerSeries Ïƒ R) = f := âŸ¨f, rflâŸ©
  rw [evalâ‚‚, dif_pos this]
  congr
  rw [â† MvPolynomial.coe_inj, this.choose_spec]

@[simp]
theorem evalâ‚‚_C (r : R) : evalâ‚‚ Ï† a (C r) = Ï† r := by
  rw [â† coe_C, evalâ‚‚_coe, MvPolynomial.evalâ‚‚_C]

@[simp]
theorem evalâ‚‚_X (s : Ïƒ) : evalâ‚‚ Ï† a (X s) = a s := by
  rw [â† coe_X, evalâ‚‚_coe, MvPolynomial.evalâ‚‚_X]

variable [IsTopologicalSemiring R] [CompleteSpace S] [T2Space S]
    [IsTopologicalSemiring S] [IsLinearUniformity S]

variable {Ï† a}

/-- Evaluation of power series at adequate elements, as a `RingHom` -/
noncomputable def evalâ‚‚Hom (hÏ† : UniformContinuous Ï†) (ha : HasEval a) :
    MvPowerSeries Ïƒ R â†’+* S :=
  IsDenseInducing.extendRingHom (i := coeToMvPowerSeries.ringHom)
    toMvPowerSeries_isUniformInducing
    denseRange_toMvPowerSeries
    (toMvPowerSeries_uniformContinuous hÏ† ha)

theorem evalâ‚‚Hom_eq_extend (hÏ† : UniformContinuous Ï†) (ha : HasEval a) (f : MvPowerSeries Ïƒ R) :
    evalâ‚‚Hom hÏ† ha f =
      toMvPowerSeries_isDenseInducing.extend (MvPolynomial.evalâ‚‚ Ï† a) f :=
  rfl

theorem coe_evalâ‚‚Hom (hÏ† : UniformContinuous Ï†) (ha : HasEval a) :
    â‡‘(evalâ‚‚Hom hÏ† ha) = evalâ‚‚ Ï† a := by
  ext f
  simp only [evalâ‚‚Hom_eq_extend, evalâ‚‚]
  split_ifs with h
  Â· obtain âŸ¨p, rflâŸ© := h
    simpa [MvPolynomial.coe_evalâ‚‚Hom] using
      toMvPowerSeries_isDenseInducing.extend_eq
        (toMvPowerSeries_uniformContinuous hÏ† ha).continuous p
  Â· rw [â† evalâ‚‚Hom_eq_extend hÏ† ha]

-- Note: this is still true without the `T2Space` hypothesis, by arguing that the case
-- disjunction in the definition of `evalâ‚‚` only replaces some values by topologically
-- inseparable ones.
theorem uniformContinuous_evalâ‚‚ (hÏ† : UniformContinuous Ï†) (ha : HasEval a) :
    UniformContinuous (evalâ‚‚ Ï† a) := by
  rw [â† coe_evalâ‚‚Hom hÏ† ha]
  exact uniformContinuous_uniformly_extend
    toMvPowerSeries_isUniformInducing
    denseRange_toMvPowerSeries
    (toMvPowerSeries_uniformContinuous hÏ† ha)

theorem continuous_evalâ‚‚ (hÏ† : UniformContinuous Ï†) (ha : HasEval a) :
    Continuous (evalâ‚‚ Ï† a : MvPowerSeries Ïƒ R â†’ S) :=
  (uniformContinuous_evalâ‚‚ hÏ† ha).continuous

theorem hasSum_evalâ‚‚ (hÏ† : UniformContinuous Ï†) (ha : HasEval a) (f : MvPowerSeries Ïƒ R) :
    HasSum
    (fun (d : Ïƒ â†’â‚€ â„•) â†¦ Ï† (coeff d f) * (d.prod fun s e => (a s) ^ e))
    (MvPowerSeries.evalâ‚‚ Ï† a f) := by
  rw [â† coe_evalâ‚‚Hom hÏ† ha, evalâ‚‚Hom_eq_extend hÏ† ha]
  convert (hasSum_of_monomials_self f).map (evalâ‚‚Hom hÏ† ha) (?_) with d
  Â· simp only [Function.comp_apply, coe_evalâ‚‚Hom, â† MvPolynomial.coe_monomial,
      evalâ‚‚_coe, evalâ‚‚_monomial]
  Â· rw [coe_evalâ‚‚Hom]; exact continuous_evalâ‚‚ hÏ† ha

theorem evalâ‚‚_eq_tsum (hÏ† : UniformContinuous Ï†) (ha : HasEval a) (f : MvPowerSeries Ïƒ R) :
    MvPowerSeries.evalâ‚‚ Ï† a f =
      âˆ‘' (d : Ïƒ â†’â‚€ â„•), Ï† (coeff d f) * (d.prod fun s e => (a s) ^ e) :=
  (hasSum_evalâ‚‚ hÏ† ha f).tsum_eq.symm

theorem evalâ‚‚_unique (hÏ† : UniformContinuous Ï†) (ha : HasEval a)
    {Îµ : MvPowerSeries Ïƒ R â†’ S} (hÎµ : Continuous Îµ)
    (h : âˆ€ p : MvPolynomial Ïƒ R, Îµ p = MvPolynomial.evalâ‚‚ Ï† a p) :
    Îµ = evalâ‚‚ Ï† a := by
  rw [â† coe_evalâ‚‚Hom hÏ† ha]
  exact (toMvPowerSeries_isDenseInducing.extend_unique h hÎµ).symm

theorem comp_evalâ‚‚ (hÏ† : UniformContinuous Ï†) (ha : HasEval a)
    {T : Type*} [UniformSpace T] [CompleteSpace T] [T2Space T]
    [CommSemiring T] [IsTopologicalSemiring T] [IsLinearUniformity T]
    {Îµ : S â†’+* T} (hÎµ : UniformContinuous Îµ) :
    Îµ âˆ˜ evalâ‚‚ Ï† a = evalâ‚‚ (Îµ.comp Ï†) (Îµ âˆ˜ a) := by
  apply evalâ‚‚_unique _ (ha.map hÎµ.continuous)
  Â· exact Continuous.comp hÎµ.continuous (continuous_evalâ‚‚ hÏ† ha)
  Â· intro p
    simp only [Function.comp_apply, evalâ‚‚_coe]
    rw [â† MvPolynomial.coe_evalâ‚‚Hom, â† comp_apply, MvPolynomial.comp_evalâ‚‚Hom,
      MvPolynomial.coe_evalâ‚‚Hom]
  Â· simp only [coe_comp, UniformContinuous.comp hÎµ hÏ†]

end Evaluation

end MvPowerSeries
