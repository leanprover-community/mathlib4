/-
Copyright (c) 2024 Antoine Chambert-Loir, MarÃ­a InÃ©s de Frutos-FernÃ¡ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Antoine Chambert-Loir, MarÃ­a InÃ©s de Frutos-FernÃ¡ndez
-/
import Mathlib.Data.Finsupp.Interval
import Mathlib.RingTheory.MvPowerSeries.PiTopology
import Mathlib.Topology.Algebra.LinearTopology
import Mathlib.Topology.Algebra.Nonarchimedean.Bases
import Mathlib.RingTheory.TwoSidedIdeal.Operations

/-! # Linear topology on the ring of multivariate power series

- `MvPowerSeries.basis`: the ideals of the ring of multivariate power series
all coefficients the exponent of which is smaller than some bound vanish.

## Instances :

- `MvPowerSeries.isLinearTopology` : if `R` has a linear topology,
then the product topology on `MvPowerSeries Ïƒ R` is a linear topology.
This applies in particular when `R` has a discrete topology.

-/

namespace MvPowerSeries

namespace LinearTopology

open scoped Topology

open Set SetLike

/-- The underlying family for the basis of ideals in a multivariate power series ring. -/
def basis (Ïƒ : Type*) (R : Type*) [Ring R] (Jd : TwoSidedIdeal R Ã— (Ïƒ â†’â‚€ â„•)) :
    TwoSidedIdeal (MvPowerSeries Ïƒ R) := by
  apply TwoSidedIdeal.mk' {f | âˆ€ e â‰¤ Jd.2, coeff R e f âˆˆ Jd.1}
  Â· simp [coeff_zero]
  Â· exact fun hf hg e he â†¦ by rw [map_add]; exact add_mem (hf e he) (hg e he)
  Â· exact fun {f} hf e he â†¦ by simp only [map_neg, neg_mem, hf e he]
  Â· exact fun {f g} hg e he â†¦ by
      classical
      rw [coeff_mul]
      apply sum_mem
      rintro uv huv
      apply TwoSidedIdeal.mul_mem_left
      exact hg _ (le_trans (le_iff_exists_add'.mpr
        âŸ¨uv.fst, (Finset.mem_antidiagonal.mp huv).symmâŸ©) he)
  Â· exact fun {f g} hf e he â†¦ by
      classical
      rw [coeff_mul]
      apply sum_mem
      rintro uv huv
      apply TwoSidedIdeal.mul_mem_right
      exact hf _ (le_trans (le_iff_exists_add.mpr âŸ¨uv.2, (Finset.mem_antidiagonal.mp huv).symmâŸ©) he)

variable {Ïƒ : Type*} {R : Type*} [Ring R]

/-- A power series `f` belongs to the twosided ideal `basis Ïƒ R âŸ¨J, dâŸ©`
if and only if `coeff R e f âˆˆ J` for all `e â‰¤ d`. -/
theorem mem_basis_iff {f : MvPowerSeries Ïƒ R} {Jd : TwoSidedIdeal R Ã— (Ïƒ â†’â‚€ â„•)} :
    f âˆˆ basis Ïƒ R Jd â†” âˆ€ e â‰¤ Jd.2, coeff R e f âˆˆ Jd.1 := by
  simp [basis]

/-- If `J â‰¤ K` and `e â‰¤ d`, then we have the inclusion of twosided ideals
`basis Ïƒ R âŸ¨J, dâŸ© â‰¤ basis Ïƒ R âŸ¨K, e,>`. -/
theorem basis_le {Jd Ke : TwoSidedIdeal R Ã— (Ïƒ â†’â‚€ â„•)} (hJK : Jd.1 â‰¤ Ke.1) (hed : Ke.2 â‰¤ Jd.2) :
    basis Ïƒ R Jd â‰¤ basis Ïƒ R Ke :=
  fun _ â†¦ forall_imp (fun _ h hue â†¦ hJK (h (le_trans hue hed)))

/-- `basis Ïƒ R âŸ¨J, dâŸ© â‰¤ basis Ïƒ R âŸ¨K, eâŸ©` if and only if `J â‰¤ K` and `e â‰¤ d`. -/
theorem basis_le_iff {J K : TwoSidedIdeal R} {d e : Ïƒ â†’â‚€ â„•} (hK : K â‰  âŠ¤) :
    basis Ïƒ R âŸ¨J, dâŸ© â‰¤ basis Ïƒ R âŸ¨K, eâŸ© â†” J â‰¤ K âˆ§ e â‰¤ d := by
  constructor
  Â· simp only [basis, TwoSidedIdeal.le_iff, TwoSidedIdeal.coe_mk', setOf_subset_setOf]
    intro h
    by_contra h'
    simp only [not_and_or] at h'
    rcases h' with h' | h'
    Â· simp only [â† coe_subset_coe, Set.not_subset] at h'
      obtain âŸ¨a, haJ, haKâŸ© := h'
      apply haK
      specialize h (monomial R e a) _ e (le_refl e)
      Â· intro e' he'
        classical
        rw [coeff_monomial]
        split_ifs
        Â· exact haJ
        Â· apply zero_mem
      rwa [coeff_monomial_same] at h
    Â· simp only [â† inf_eq_right] at h'
      apply hK
      rw [eq_top_iff]
      intro a _
      specialize h (monomial R e a) _
      Â· intro e' he'
        convert zero_mem J
        apply coeff_monomial_ne
        rintro âŸ¨rflâŸ©
        exact h' (right_eq_inf.mpr he').symm
      Â· specialize h e (le_refl e)
        rwa [coeff_monomial_same] at h
  Â· rintro âŸ¨hJK, hedâŸ©
    exact basis_le hJK hed

variable [TopologicalSpace R]

-- We endow MvPowerSeries Ïƒ R with the product topology.
open WithPiTopology

variable (Ïƒ R) in
theorem ringSubgroupsBasis :
    RingSubgroupsBasis (fun (Jd : {J : TwoSidedIdeal R | (J : Set R) âˆˆ ğ“ 0} Ã— (Ïƒ â†’â‚€ â„•))
        â†¦ (basis Ïƒ R âŸ¨Jd.1, Jd.2âŸ©).asIdeal.toAddSubgroup) where
  inter Jd Ke := âŸ¨âŸ¨âŸ¨Jd.1 âŠ“ Ke.1, Filter.inter_mem Jd.1.prop Ke.1.propâŸ©, Jd.2 âŠ” Ke.2âŸ©, by
    simp only [le_inf_iff]
    exact âŸ¨basis_le inf_le_left le_sup_left, basis_le inf_le_right le_sup_rightâŸ©âŸ©
  mul Jd := âŸ¨Jd, fun f â†¦ by
    simp only [Submodule.coe_toAddSubgroup, mem_mul]
    rintro âŸ¨x, hx, y, hy, rflâŸ©
    exact Ideal.mul_mem_left _ _ hyâŸ©
  leftMul f Jd := âŸ¨Jd, fun g hg â†¦ (basis Ïƒ R âŸ¨Jd.1, Jd.2âŸ©).mul_mem_left f g hgâŸ©
  rightMul f Jd := âŸ¨Jd, fun g hg â†¦ by
    intro e he
    simp only [Submodule.coe_toAddSubgroup, TwoSidedIdeal.coe_asIdeal,
      mem_coe, sub_zero, mem_basis_iff] at hg âŠ¢
    classical
    rw [coeff_mul]
    apply sum_mem
    rintro âŸ¨i, jâŸ© h
    apply TwoSidedIdeal.mul_mem_right
    apply hg i (le_trans ?_ he)
    simp only [â† Finset.mem_antidiagonal.mp h, le_self_add]âŸ©

/-- If the coefficient ring `R` is endowed with the discrete topology, then for every `d : Ïƒ â†’â‚€ â„•`,
`â†‘(basis Ïƒ R d) âˆˆ ğ“ (0 : MvPowerSeries Ïƒ R)`. -/
theorem basis_mem_nhds_zero (Jd : {J : TwoSidedIdeal R | (J : Set R) âˆˆ ğ“ 0} Ã— (Ïƒ â†’â‚€ â„•)) :
    (basis Ïƒ R âŸ¨Jd.1, Jd.2âŸ© : Set (MvPowerSeries Ïƒ R)) âˆˆ ğ“ 0 := by
  classical
  rw [nhds_pi, Filter.mem_pi]
  use Finset.Iic Jd.2, Finset.finite_toSet _, (fun e => if e â‰¤ Jd.2 then Jd.1 else univ)
  constructor
  Â· intro e
    split_ifs
    Â· exact Jd.1.prop
    Â· simp only [Filter.univ_mem]
  Â· intro f
    simp only [Finset.coe_Iic, mem_pi, mem_Iic, mem_ite_univ_right, mem_singleton_iff, mem_coe]
    rw [mem_basis_iff]
    exact forall_imp (fun e h he => h he he)

lemma mem_nhds_zero_iff [IsLinearTopology R R] [IsLinearTopology Ráµáµ’áµ– R]
    {U : Set (MvPowerSeries Ïƒ R)} :
    U âˆˆ ğ“ 0 â†” âˆƒ Jd, ((Jd.1 : Set R) âˆˆ ğ“ 0) âˆ§ (basis Ïƒ R Jd : Set (MvPowerSeries Ïƒ R)) âŠ† U := by
  constructor
  Â· rw [nhds_pi, Filter.mem_pi]
    rintro âŸ¨D, hD, t, ht, ht'âŸ©
    suffices âˆƒ J : TwoSidedIdeal R, (J : Set R) âˆˆ ğ“ 0 âˆ§ (J : Set R) âŠ† â‹‚ i âˆˆ D, t i by
      obtain âŸ¨J, hJ, hJDâŸ© := this
      use âŸ¨J, Finset.sup hD.toFinset idâŸ©
      constructor
      Â· exact hJ
      Â· apply subset_trans _ ht'
        intros f hf e he
        simp only [â† coeff_apply R f e]
        apply biInter_subset_of_mem he
        apply hJD
        rw [mem_coe, mem_basis_iff] at hf
        exact hf e (Finset.le_sup (f := id) (hD.mem_toFinset.mpr he))
    set s := â‹‚ i âˆˆ D, t i
    rw [â† (IsLinearTopology.hasBasis_twoSidedIdeal (R := R)).mem_iff']
    exact (Filter.biInter_mem hD).mpr fun i a â†¦ ht i
  Â· rintro âŸ¨Jd, hJd_mem_nhds, hJdâŸ©
    exact Filter.sets_of_superset _ (basis_mem_nhds_zero âŸ¨âŸ¨Jd.1, hJd_mem_nhdsâŸ©,Jd.2âŸ©) hJd

/-- The topology on `MvPowerSeries` is a (left and right) linear topology
  when the ring of coefficients has the discrete topology. -/
theorem hasBasis_twoSidedIdeal [IsLinearTopology R R] [IsLinearTopology Ráµáµ’áµ– R] :
  (ğ“ 0).HasBasis (fun I : TwoSidedIdeal (MvPowerSeries Ïƒ R) â†¦ (I : Set (MvPowerSeries Ïƒ R)) âˆˆ ğ“ 0)
    fun I : TwoSidedIdeal (MvPowerSeries Ïƒ R) â†¦ (I : Set (MvPowerSeries Ïƒ R)) := by
  constructor
  intro U
  simp only [mem_nhds_zero_iff]
  constructor
  Â· rintro âŸ¨Jd, hJ, hdâŸ©
    exact âŸ¨basis Ïƒ R Jd, âŸ¨Jd, âŸ¨hJ, by simpâŸ©âŸ©, hdâŸ©
  Â· rintro âŸ¨J, âŸ¨âŸ¨Id, hI, hJâŸ©, hUâŸ©âŸ©
    exact âŸ¨Id, hI, fun â¦ƒaâ¦„ a_1 â†¦ hU (hJ a_1)âŸ©

/-- The topology on `MvPowerSeries` is a left linear topology
  when the ring of coefficients has the discrete topology. -/
instance [IsLinearTopology R R] [IsLinearTopology Ráµáµ’áµ– R] :
    IsLinearTopology (MvPowerSeries Ïƒ R) (MvPowerSeries Ïƒ R) :=
  (isLinearTopology_iff_hasBasis_twoSidedIdeal.mpr (hasBasis_twoSidedIdeal)).1

/-- The topology on `MvPowerSeries` is a right linear topology
  when the ring of coefficients has the discrete topology. -/
instance [IsLinearTopology R R] [IsLinearTopology Ráµáµ’áµ– R] :
    IsLinearTopology (MvPowerSeries Ïƒ R)áµáµ’áµ– (MvPowerSeries Ïƒ R) :=
  (isLinearTopology_iff_hasBasis_twoSidedIdeal.mpr (hasBasis_twoSidedIdeal)).2

end LinearTopology

end MvPowerSeries
