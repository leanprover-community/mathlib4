/-
Copyright (c) 2024 Antoine Chambert-Loir, Mar√≠a In√©s de Frutos Fern√°ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Antoine Chambert-Loir, Mar√≠a In√©s de Frutos-Fern√°ndez
-/

import Mathlib.Algebra.MvPolynomial.CommRing
import Mathlib.RingTheory.Ideal.BigOperators
import Mathlib.RingTheory.MvPowerSeries.PiTopology
import Mathlib.RingTheory.MvPowerSeries.Trunc
import Mathlib.Topology.Algebra.Algebra
import Mathlib.Topology.Algebra.TopologicallyNilpotent
import Mathlib.Topology.Algebra.LinearTopology
import Mathlib.Topology.Algebra.UniformRing

/-! # Evaluation of multivariate power series

Let `œÉ`, `R` `S` be types, with `CommRing R`, `CommRing S`.
One assumes that `TopologicalRing R` and `UniformAddGroup R`,
and that `S` is a complete and separated topological `R`-algebra,
with `LinearTopology R`, which means there is a basis of neighborhoods of 0
consisting of ideals.

* `MvPowerSeries.eval‚ÇÇ` : Given `œÜ : R ‚Üí+* S` and `a : œÉ ‚Üí S`,
this file defines an evaluation of `f : MvPowerSeries œÉ R`,
that extends the evaluation of polynomials at `a`, by density,
provided `œÜ` and `a` satisfy certain conditions of which
the following lemmas assert the necessity

* `Continuous.on_scalars` : The map `œÜ` is continuous
* `Continuous.tendsto_apply_pow_zero_of_constantCoeff_zero` :
  for all `s : œÉ`, `(a s) ^ n` tends to 0 when `n` tends to infinity
* `Continuous.tendsto_apply_variables_zero_of_cofinite`:
  when `a s` tends to  zero for the filter of cofinite subsets of `œÉ`.

* `MvPowerSeries.eval‚ÇÇ_domain` : the `Prop`-valued structure
that is required to evaluate power series

* `MvPowerSeries.uniformContinuous_eval‚ÇÇ` : uniform continuity of the evaluation

* `MvPowerSeries.continuous_eval‚ÇÇ` : continuity of the evaluation

* `MvPowerSeries.aeval` : the evaluation map as an algebra map

-/

namespace MvPowerSeries

open Topology

open Filter MvPolynomial RingHom Set TopologicalSpace UniformSpace

/- ## Necessary conditions -/

section

variable {œÉ : Type*}
variable {R : Type*} [CommRing R] [TopologicalSpace R]
variable {S : Type*} [CommRing S] [TopologicalSpace S]
variable {œÜ : R ‚Üí+* S}

-- We endow MvPowerSeries œÉ R with the Pi topology
open WithPiTopology

/-- Families at which power series can be evaluated -/
structure EvalDomain (a : œÉ ‚Üí S) : Prop where
  hpow : ‚àÄ s, IsTopologicallyNilpotent (a s)
  tendsto_zero : Tendsto a cofinite (ùìù 0)

/-- The domain of evaluation of `MvPowerSeries`, as an ideal -/
def EvalDomain_ideal [IsTopologicalRing S] [IsLinearTopology S S] :
    Ideal (œÉ ‚Üí S) where
  carrier := setOf EvalDomain
  add_mem' {a} {b} ha hb := {
    hpow := fun s ‚Ü¶ IsTopologicallyNilpotent.add (ha.hpow s) (hb.hpow s)
    tendsto_zero := by
      rw [‚Üê add_zero 0]
      exact ha.tendsto_zero.add hb.tendsto_zero }
  zero_mem' := {
    hpow := fun s ‚Ü¶ by
      simp only [Pi.zero_apply]
      apply tendsto_atTop_of_eventually_const (i‚ÇÄ := 1)
      intro i hi
      rw [zero_pow (Nat.ne_zero_iff_zero_lt.mpr hi)]
    tendsto_zero := tendsto_const_nhds }
  smul_mem' c {x} hx := {
    hpow := fun s ‚Ü¶ by
      simp only [IsTopologicallyNilpotent, Pi.smul_apply', smul_eq_mul, mul_pow]
      exact IsLinearTopology.tendsto_mul_zero_of_right _ _ (hx.hpow s)
    tendsto_zero := IsLinearTopology.tendsto_mul_zero_of_right _ _ hx.tendsto_zero }

theorem EvalDomain.comp {a : œÉ ‚Üí R} (ha : EvalDomain a) {Œµ : R ‚Üí+* S} (hŒµ : Continuous Œµ) :
    EvalDomain (Œµ ‚àò a) := by
  apply EvalDomain.mk _ ((map_zero Œµ ‚ñ∏ Continuous.tendsto' hŒµ 0 (Œµ 0) rfl).comp ha.tendsto_zero)
  ¬∑ intro s
    unfold IsTopologicallyNilpotent
    convert (Continuous.tendsto' hŒµ 0 (Œµ 0) rfl).comp (ha.hpow s)
    ¬∑ ext n; simp only [Function.comp_apply, map_pow]
    ¬∑ rw [map_zero]

/-- Bourbaki, Alg√®bre, chap. 4, ¬ß4, n¬∞3, Prop. 4 (i) (a & b) -/
theorem EvalDomain.map (hœÜ : Continuous œÜ) {a : œÉ ‚Üí R} (ha : EvalDomain a) :
    EvalDomain (fun s ‚Ü¶ œÜ (a s)) where
  hpow := fun s ‚Ü¶ IsTopologicallyNilpotent.map hœÜ (ha.hpow s)
  tendsto_zero := (map_zero œÜ ‚ñ∏ hœÜ.tendsto 0).comp ha.tendsto_zero

theorem EvalDomain.evalDomain_X :
    EvalDomain (fun s ‚Ü¶ (MvPowerSeries.X s : MvPowerSeries œÉ R)) where
  hpow := fun s ‚Ü¶ tendsto_pow_zero_of_constantCoeff_zero (constantCoeff_X s)
  tendsto_zero := by
    classical
    exact variables_tendsto_zero

theorem Continuous.on_scalars {Œµ : MvPowerSeries œÉ R ‚Üí+* S} (hŒµ : Continuous Œµ) :
    Continuous (Œµ.comp (C œÉ R)) := by
  rw [coe_comp]
  exact Continuous.comp hŒµ MvPowerSeries.WithPiTopology.continuous_C

/-- The inclusion of polynomials into power series has dense image -/
theorem _root_.MvPolynomial.coeToMvPowerSeries_denseRange :
    DenseRange (coeToMvPowerSeries.ringHom (R := R) (œÉ := œÉ)) := fun f => by
  have : Tendsto (fun d ‚Ü¶ (trunc' R d f : MvPowerSeries œÉ R)) atTop (ùìù f) := by
    rw [tendsto_iff_coeff_tendsto]
    refine fun d ‚Ü¶ tendsto_atTop_of_eventually_const fun n (hdn : d ‚â§ n) ‚Ü¶ ?_
    simp [coeff_trunc', hdn]
  exact mem_closure_of_tendsto this <| .of_forall fun _ ‚Ü¶ mem_range_self _

end

/- ## Construction of an evaluation morphism for power series -/

section Evaluation

open WithPiTopology

variable {œÉ : Type*} -- [DecidableEq œÉ]
variable {R : Type*} [CommRing R] [UniformSpace R]
variable {S : Type*} [CommRing S] [UniformSpace S]
variable {œÜ : R ‚Üí+* S} -- (hœÜ : Continuous œÜ)

-- We endow MvPowerSeries œÉ R with the product uniform structure
private instance : UniformSpace (MvPolynomial œÉ R) :=
  comap toMvPowerSeries (Pi.uniformSpace _)

/-- The induced uniform structure of MvPolynomial œÉ R is an add group uniform structure -/
private instance [UniformAddGroup R] : UniformAddGroup (MvPolynomial œÉ R) :=
  UniformAddGroup.comap coeToMvPowerSeries.ringHom

theorem _root_.MvPolynomial.coeToMvPowerSeries_uniformInducing :
    IsUniformInducing (coeToMvPowerSeries.ringHom (œÉ := œÉ) (R := R)) :=
  (isUniformInducing_iff ‚áëcoeToMvPowerSeries.ringHom).mpr rfl

theorem _root_.MvPolynomial.coeToMvPowerSeries_denseInducing :
    IsDenseInducing (coeToMvPowerSeries.ringHom (œÉ := œÉ) (R := R)) :=
  coeToMvPowerSeries_uniformInducing.isDenseInducing coeToMvPowerSeries_denseRange

variable {a : œÉ ‚Üí S}

/- The evaluation map on multivariate polynomials is uniformly continuous
for the uniform structure induced by that on multivariate power series. -/
theorem _root_.MvPolynomial.coeToMvPowerSeries_uniformContinuous
    [UniformAddGroup R] [UniformAddGroup S] [IsLinearTopology S S]
    (hœÜ : Continuous œÜ) (ha : EvalDomain a) :
    UniformContinuous (MvPolynomial.eval‚ÇÇHom œÜ a) := by
  classical
  apply uniformContinuous_of_continuousAt_zero
  intro u hu
  simp only [coe_eval‚ÇÇHom, (induced_iff_nhds_eq _).mp rfl, coe_zero, mem_map, mem_comap]
  rw [map_zero, IsLinearTopology.hasBasis_ideal.mem_iff] at hu
  rcases hu with ‚ü®I, hI, hI'‚ü©
  let tendsto_zero := ha.tendsto_zero
  let hpow := ha.hpow
  simp only [tendsto_def] at tendsto_zero hpow
  specialize tendsto_zero I hI
  simp only [mem_cofinite] at tendsto_zero
  let hpow' := fun s ‚Ü¶ hpow s hI
  simp only [mem_map, mem_atTop_sets, ge_iff_le, mem_preimage, SetLike.mem_coe] at hpow'
  let n : œÉ ‚Üí ‚Ñï := fun s ‚Ü¶ sInf {n : ‚Ñï | (a s) ^ n.succ ‚àà I}
  have hn_ne : ‚àÄ s, Set.Nonempty {n : ‚Ñï | (a s) ^ n.succ ‚àà I} := fun s ‚Ü¶ by
    rcases hpow' s with ‚ü®n, hn‚ü©
    use n
    simp only [mem_setOf_eq, hn n.succ (Nat.le_succ n)]
  have hn : Set.Finite (n.support) := by
    apply @Finite.Set.subset  _ _ _ tendsto_zero
    intro s
    simp only [Function.mem_support, ne_eq, mem_compl_iff, mem_preimage, SetLike.mem_coe, not_not,
      not_imp_comm, imp_or, n, Nat.sInf_eq_zero, mem_setOf_eq, zero_add, pow_one, imp_self, true_or]
  let n‚ÇÄ : œÉ ‚Üí‚ÇÄ ‚Ñï := {
    toFun := n
    support := hn.toFinset
    mem_support_toFun := fun (s : œÉ) ‚Ü¶ by simp }
  let D := Iic n‚ÇÄ
  have hD : Set.Finite D := finite_Iic _
  use iInter (fun (d : D) ‚Ü¶ { p | œÜ (p d.val) ‚àà I})
  rw [nhds_pi, Filter.mem_pi]
  constructor
  ¬∑ use D, hD
    use fun d ‚Ü¶ if d ‚àà D then œÜ ‚Åª¬π' I else univ
    constructor
    ¬∑ intro d
      split_ifs with hd
      ¬∑ exact hœÜ.continuousAt.preimage_mem_nhds (map_zero œÜ ‚ñ∏ hI)
      ¬∑ exact univ_mem
    ¬∑ intro p
      simp only [Set.mem_pi, mem_ite_univ_right, mem_preimage, SetLike.mem_coe,
        iInter_coe_set, mem_iInter]
      exact fun hp i hi ‚Ü¶ hp i hi hi
  ¬∑ intro p hp
    simp only [iInter_coe_set, mem_preimage, coeToMvPowerSeries.ringHom_apply,
      mem_iInter, mem_setOf_eq] at hp
    simp only [mem_preimage]
    apply hI'
    simp only [coe_eval‚ÇÇHom, SetLike.mem_coe]
    rw [eval‚ÇÇ_eq]
    apply Ideal.sum_mem
    intro d _
    by_cases hd : d ‚àà D
    ¬∑ exact Ideal.mul_mem_right _ _ (hp d hd)
    ¬∑ apply Ideal.mul_mem_left
      simp only [mem_Iic, D, Finsupp.le_iff] at hd
      push_neg at hd
      rcases hd with ‚ü®s, hs', hs‚ü©
      exact I.mem_prod_of_mem hs' (I.pow_mem_of_pow_mem (Nat.sInf_mem (hn_ne s)) hs)

variable (œÜ a)
/-- Evaluation of power series. Meaningful on adequate elements or on `MvPolynomial`)  -/
noncomputable def eval‚ÇÇ (f : MvPowerSeries œÉ R) : S := by
  let hp := fun (p : MvPolynomial œÉ R) ‚Ü¶ p = f
  classical
  exact if (Classical.epsilon hp = f) then (MvPolynomial.eval‚ÇÇ œÜ a (Classical.epsilon hp))
    else IsDenseInducing.extend coeToMvPowerSeries_denseInducing (MvPolynomial.eval‚ÇÇ œÜ a) f

theorem eval‚ÇÇ_coe (f : MvPolynomial œÉ R) :
    MvPowerSeries.eval‚ÇÇ œÜ a f = MvPolynomial.eval‚ÇÇ œÜ a f := by
  have hf := Classical.epsilon_spec
    (p := fun (p : MvPolynomial œÉ R) ‚Ü¶ p = (f : MvPowerSeries œÉ R)) ‚ü®f, rfl‚ü©
  rw [eval‚ÇÇ, if_pos hf]
  apply _root_.congr_arg
  rw [‚Üê MvPolynomial.coe_inj, hf]

theorem eval‚ÇÇ_C (r : R) :
    eval‚ÇÇ œÜ a (C œÉ R r) = œÜ r := by
  rw [‚Üê coe_C, eval‚ÇÇ_coe, MvPolynomial.eval‚ÇÇ_C]

theorem eval‚ÇÇ_X (s : œÉ) :
    eval‚ÇÇ œÜ a (X s) = a s := by
  rw [‚Üê coe_X, eval‚ÇÇ_coe, MvPolynomial.eval‚ÇÇ_X]

variable [IsTopologicalSemiring R] [UniformAddGroup R]
    [UniformAddGroup S] [CompleteSpace S] [T2Space S]
    [IsTopologicalRing S] [IsLinearTopology S S]

variable {œÜ a}

/-- Evaluation of power series at adequate elements, as a `RingHom` -/
noncomputable def eval‚ÇÇHom (hœÜ : Continuous œÜ) (ha : EvalDomain a) :
    MvPowerSeries œÉ R ‚Üí+* S :=
  IsDenseInducing.extendRingHom
    coeToMvPowerSeries_uniformInducing
    coeToMvPowerSeries_denseRange
    (coeToMvPowerSeries_uniformContinuous hœÜ ha)

theorem eval‚ÇÇHom_apply (hœÜ : Continuous œÜ) (ha : EvalDomain a) (f : MvPowerSeries œÉ R) :
    eval‚ÇÇHom hœÜ ha f =
      coeToMvPowerSeries_denseInducing.extend (MvPolynomial.eval‚ÇÇ œÜ a) f :=
  rfl

theorem coe_eval‚ÇÇHom (hœÜ : Continuous œÜ) (ha : EvalDomain a) :
    ‚áë(eval‚ÇÇHom hœÜ ha) = eval‚ÇÇ œÜ a := by
  ext f
  let hf := fun (p : MvPolynomial œÉ R) ‚Ü¶ p = f
  simp only [eval‚ÇÇHom_apply, eval‚ÇÇ]
  split_ifs with h
  ¬∑ conv_lhs => rw [‚Üê h]
    simpa only [MvPolynomial.coe_eval‚ÇÇHom, coeToMvPowerSeries.ringHom_apply]
      using IsDenseInducing.extend_eq coeToMvPowerSeries_denseInducing
        (coeToMvPowerSeries_uniformContinuous hœÜ ha).continuous (Classical.epsilon hf)
  ¬∑ rfl

theorem uniformContinuous_eval‚ÇÇ (hœÜ : Continuous œÜ) (ha : EvalDomain a) :
    UniformContinuous (eval‚ÇÇ œÜ a) := by
  rw [‚Üê coe_eval‚ÇÇHom hœÜ ha]
  exact uniformContinuous_uniformly_extend
    coeToMvPowerSeries_uniformInducing
    coeToMvPowerSeries_denseRange
    (coeToMvPowerSeries_uniformContinuous hœÜ ha)

theorem continuous_eval‚ÇÇ (hœÜ : Continuous œÜ) (ha : EvalDomain a) :
    Continuous (eval‚ÇÇ œÜ a : MvPowerSeries œÉ R ‚Üí S) :=
  (uniformContinuous_eval‚ÇÇ hœÜ ha).continuous

theorem hasSum_eval‚ÇÇ (hœÜ : Continuous œÜ) (ha : EvalDomain a)(f : MvPowerSeries œÉ R) :
    HasSum
    (fun (d : œÉ ‚Üí‚ÇÄ ‚Ñï) ‚Ü¶ œÜ (coeff R d f) * (d.prod fun s e => (a s) ^ e))
    (MvPowerSeries.eval‚ÇÇ œÜ a f) := by
  rw [‚Üê coe_eval‚ÇÇHom hœÜ ha, eval‚ÇÇHom_apply hœÜ ha]
  convert (hasSum_of_monomials_self f).map (eval‚ÇÇHom hœÜ ha) (?_) with d
  ¬∑ simp only [Function.comp_apply, coe_eval‚ÇÇHom, ‚Üê MvPolynomial.coe_monomial,
    eval‚ÇÇ_coe, eval‚ÇÇ_monomial]
  ¬∑ rw [coe_eval‚ÇÇHom]; exact continuous_eval‚ÇÇ hœÜ ha

theorem eval‚ÇÇ_eq_sum (hœÜ : Continuous œÜ) (ha : EvalDomain a)(f : MvPowerSeries œÉ R) :
    MvPowerSeries.eval‚ÇÇ œÜ a f =
      tsum (fun (d : œÉ ‚Üí‚ÇÄ ‚Ñï) ‚Ü¶ œÜ (coeff R d f) * (d.prod fun s e => (a s) ^ e)) :=
  (hasSum_eval‚ÇÇ hœÜ ha f).tsum_eq.symm

theorem eval‚ÇÇ_unique (hœÜ : Continuous œÜ) (ha : EvalDomain a)
    {Œµ : MvPowerSeries œÉ R ‚Üí+* S} (hŒµ : Continuous Œµ)
    (h : ‚àÄ p : MvPolynomial œÉ R, Œµ p = MvPolynomial.eval‚ÇÇ œÜ a p) :
    Œµ = eval‚ÇÇ œÜ a := by
  rw [‚Üê coe_eval‚ÇÇHom hœÜ ha]
  exact (MvPolynomial.coeToMvPowerSeries_denseInducing.extend_unique h hŒµ).symm

theorem comp_eval‚ÇÇ (hœÜ : Continuous œÜ) (ha : EvalDomain a)
    {T : Type*} [UniformSpace T] [CompleteSpace T] [T2Space T]
    [CommRing T] [IsTopologicalRing T] [IsLinearTopology T T] [UniformAddGroup T]
    {Œµ : S ‚Üí+* T} (hŒµ : Continuous Œµ) :
    Œµ ‚àò eval‚ÇÇ œÜ a = eval‚ÇÇ (Œµ.comp œÜ) (Œµ ‚àò a) := by
  rw [‚Üê coe_eval‚ÇÇHom hœÜ ha, ‚Üê coe_comp]
  apply eval‚ÇÇ_unique _ (ha.comp hŒµ)
  ¬∑ simp only [coe_comp, coe_eval‚ÇÇHom hœÜ ha]
    exact Continuous.comp hŒµ (continuous_eval‚ÇÇ hœÜ ha)
  ¬∑ intro p
    simp only [coe_comp, Function.comp_apply, eval‚ÇÇ_coe]
    rw [coe_eval‚ÇÇHom hœÜ ha, eval‚ÇÇ_coe œÜ a, ‚Üê MvPolynomial.coe_eval‚ÇÇHom, ‚Üê comp_apply,
    MvPolynomial.comp_eval‚ÇÇHom]
    rfl
  ¬∑ simp only [coe_comp, Continuous.comp hŒµ hœÜ]

variable [Algebra R S] [ContinuousSMul R S]

/-- Evaluation of power series at adequate elements, as an `AlgHom` -/
noncomputable def aeval (ha : EvalDomain a) : MvPowerSeries œÉ R ‚Üí‚Çê[R] S where
  toRingHom := MvPowerSeries.eval‚ÇÇHom (continuous_algebraMap R S) ha
  commutes' := fun r ‚Ü¶ by
    simp only [toMonoidHom_eq_coe, OneHom.toFun_eq_coe, MonoidHom.toOneHom_coe, MonoidHom.coe_coe]
    rw [‚Üê c_eq_algebraMap, coe_eval‚ÇÇHom, eval‚ÇÇ_C]

theorem coe_aeval (ha : EvalDomain a) :
    ‚áë(MvPowerSeries.aeval ha) = MvPowerSeries.eval‚ÇÇ (algebraMap R S) a := by
  simp only [aeval, AlgHom.coe_mk, coe_eval‚ÇÇHom]

theorem continuous_aeval (ha : EvalDomain a) : Continuous (aeval ha : MvPowerSeries œÉ R ‚Üí S) := by
  rw [coe_aeval]
  exact continuous_eval‚ÇÇ (continuous_algebraMap R S) ha

theorem aeval_coe (ha : EvalDomain a) (p : MvPolynomial œÉ R) :
    MvPowerSeries.aeval ha (p : MvPowerSeries œÉ R) = MvPolynomial.aeval a p := by
  rw [coe_aeval, aeval_def, eval‚ÇÇ_coe]

theorem aeval_unique {Œµ : MvPowerSeries œÉ R ‚Üí‚Çê[R] S} (hŒµ : Continuous Œµ) :
    Œµ = aeval (EvalDomain.evalDomain_X.map hŒµ) := by
  apply DFunLike.ext'
  rw [coe_aeval]
  apply eval‚ÇÇ_unique (continuous_algebraMap R S) _ hŒµ
  ¬∑ intro p
    induction p using MvPolynomial.induction_on with
    | h_C r =>
      simp only [AlgHom.toRingHom_eq_coe, coe_C, coe_coe, MvPolynomial.eval‚ÇÇ_C]
      rw [c_eq_algebraMap, AlgHom.commutes]
    | h_add p q hp hq =>
      simp only [AlgHom.toRingHom_eq_coe, coe_coe] at hp hq
      simp only [AlgHom.toRingHom_eq_coe, coe_add, map_add, coe_coe, eval‚ÇÇ_add, hp, hq]
    | h_X p s h =>
      simp only [AlgHom.toRingHom_eq_coe, coe_coe] at h
      simp only [AlgHom.toRingHom_eq_coe, MvPolynomial.coe_mul, coe_X, map_mul, coe_coe, eval‚ÇÇ_mul,
        MvPolynomial.eval‚ÇÇ_X, h]
  ¬∑ exact EvalDomain.evalDomain_X.comp hŒµ

theorem hasSum_aeval (ha : EvalDomain a) (f : MvPowerSeries œÉ R) :
    HasSum (fun (d : œÉ ‚Üí‚ÇÄ ‚Ñï) ‚Ü¶ (coeff R d f) ‚Ä¢ (d.prod fun s e => (a s) ^ e))
      (MvPowerSeries.aeval ha f) := by
  simp_rw [coe_aeval, ‚Üê algebraMap_smul (R := R) S, smul_eq_mul]
  exact hasSum_eval‚ÇÇ (continuous_algebraMap R S) ha f

theorem aeval_eq_sum (ha : EvalDomain a) (f : MvPowerSeries œÉ R) :
    MvPowerSeries.aeval ha f =
      tsum (fun (d : œÉ ‚Üí‚ÇÄ ‚Ñï) ‚Ü¶ (coeff R d f) ‚Ä¢ (d.prod fun s e => (a s) ^ e)) :=
  (hasSum_aeval ha f).tsum_eq.symm

theorem comp_aeval (ha : EvalDomain a)
    {T : Type*} [CommRing T] [UniformSpace T] [UniformAddGroup T]
    [IsTopologicalRing T] [IsLinearTopology T T]
    [T2Space T] [Algebra R T] [ContinuousSMul R T] [CompleteSpace T]
    {Œµ : S ‚Üí‚Çê[R] T} (hŒµ : Continuous Œµ) :
    Œµ.comp (aeval ha) = aeval (ha.map hŒµ)  := by
  apply DFunLike.coe_injective
  simp only [AlgHom.coe_comp, coe_aeval ha]
  erw [comp_eval‚ÇÇ (continuous_algebraMap R S) ha hŒµ, coe_aeval]
  apply congr_arg‚ÇÇ
  ¬∑ simp only [AlgHom.toRingHom_eq_coe, AlgHom.comp_algebraMap_of_tower, RingHom.coe_coe]
  ¬∑ rfl

end Evaluation

end MvPowerSeries
