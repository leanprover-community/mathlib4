/-
Copyright (c) 2024 Antoine Chambert-Loir, Mar√≠a In√©s de Frutos Fern√°ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Antoine Chambert-Loir, Mar√≠a In√©s de Frutos-Fern√°ndez
-/

import Mathlib.Algebra.MvPolynomial.CommRing
import Mathlib.RingTheory.Ideal.BigOperators
import Mathlib.RingTheory.MvPowerSeries.PiTopology
import Mathlib.RingTheory.MvPowerSeries.Trunc
import Mathlib.Topology.Algebra.Algebra
import Mathlib.Topology.Algebra.TopologicallyNilpotent
import Mathlib.Topology.Algebra.LinearTopology
import Mathlib.Topology.Algebra.UniformRing

/-! # Evaluation of multivariate power series

Let `œÉ`, `R`, `S` be types, with `CommRing R`, `CommRing S`.
One assumes that `IsTopologicalRing R` and `IsUniformAddGroup R`,
and that `S` is a complete and separated topological `R`-algebra,
with `IsLinearTopology S S`, which means there is a basis of neighborhoods of 0
consisting of ideals.

Given `œÜ : R ‚Üí+* S`, `a : œÉ ‚Üí S`, and `f : MvPowerSeries œÉ R`,
`MvPowerSeries.eval‚ÇÇ f œÜ a` is the evaluation of the multivariate power series `f` at `a`.
It `f` is (the coercion of) a polynomial, it coincides with the evaluation of that polynomial.
Otherwise, it is defined by density from polynomials;
its values are irrelevant unless `œÜ` is continuous and `a` satisfies two conditions
bundled in `MvPowerSeries.HasEval a` :
  - for all `s : œÉ`, `a s` is topologically nilpotent,
    meaning that `(a s) ^ n` tends to 0 when `n` tends to infinity
  - when `a s` tends to zero for the filter of cofinite subsets of `œÉ`.

Under `Continuous œÜ` and `HasEval a`, the following lemmas furnish the properties of evaluation:

* `MvPowerSeries.eval‚ÇÇHom`: the evaluation of multivariate power series, as a ring morphism,
* `MvPowerSeries.aeval`: the evaluation map as an algebra morphism
* `MvPowerSeries.uniformContinuous_eval‚ÇÇ`: uniform continuity of the evaluation
* `MvPowerSeries.continuous_eval‚ÇÇ`: continuity of the evaluation
* `MvPowerSeries.eval‚ÇÇ_eq_tsum`: the evaluation is given by the sum of its monomials, evaluated.

-/

namespace MvPowerSeries

open Topology

open Filter MvPolynomial RingHom Set TopologicalSpace UniformSpace

/- ## Necessary conditions -/

section

variable {œÉ : Type*}
variable {R : Type*} [CommRing R] [TopologicalSpace R]
variable {S : Type*} [CommRing S] [TopologicalSpace S]
variable {œÜ : R ‚Üí+* S}

-- We endow MvPowerSeries œÉ R with the Pi topology
open WithPiTopology

/-- Families at which power series can be consistently evaluated -/
@[mk_iff hasEval_def]
structure HasEval (a : œÉ ‚Üí S) : Prop where
  hpow : ‚àÄ s, IsTopologicallyNilpotent (a s)
  tendsto_zero : Tendsto a cofinite (ùìù 0)

theorem HasEval.mono {S : Type*} [CommRing S] {a : œÉ ‚Üí S}
    {t u : TopologicalSpace S} (h : t ‚â§ u) (ha : @HasEval _ _ _ t a) :
    @HasEval _ _ _ u a :=
  ‚ü®fun s ‚Ü¶ Filter.Tendsto.mono_right (@HasEval.hpow _ _ _ t a ha s) (nhds_mono h),
   Filter.Tendsto.mono_right (@HasEval.tendsto_zero œÉ _ _ t a ha) (nhds_mono h)‚ü©

theorem HasEval.zero : HasEval (0 : œÉ ‚Üí S) where
  hpow _ := .zero
  tendsto_zero := tendsto_const_nhds

theorem HasEval.add [ContinuousAdd S] [IsLinearTopology S S]
    {a b : œÉ ‚Üí S} (ha : HasEval a) (hb : HasEval b) : HasEval (a + b) where
  hpow s := (ha.hpow s).add (hb.hpow s)
  tendsto_zero := by rw [‚Üê add_zero 0]; exact ha.tendsto_zero.add hb.tendsto_zero

theorem HasEval.mul_left [IsLinearTopology S S]
    (c : œÉ ‚Üí S) {x : œÉ ‚Üí S} (hx : HasEval x) : HasEval (c * x) where
  hpow s := (hx.hpow s).mul_left (c s)
  tendsto_zero := IsLinearTopology.tendsto_mul_zero_of_right _ _ hx.tendsto_zero

theorem HasEval.mul_right [IsLinearTopology S S]
    (c : œÉ ‚Üí S) {x : œÉ ‚Üí S} (hx : HasEval x) : HasEval (x * c) :=
  mul_comm x c ‚ñ∏ HasEval.mul_left c hx

/-- [Bourbaki, *Algebra*, chap. 4, ¬ß4, n¬∞3, Prop. 4 (i) (a & b)][bourbaki1981]. -/
theorem HasEval.map (hœÜ : Continuous œÜ) {a : œÉ ‚Üí R} (ha : HasEval a) :
    HasEval (fun s ‚Ü¶ œÜ (a s)) where
  hpow s := (ha.hpow s).map hœÜ
  tendsto_zero := (map_zero œÜ ‚ñ∏ hœÜ.tendsto 0).comp ha.tendsto_zero

protected theorem HasEval.X :
    HasEval (fun s ‚Ü¶ (MvPowerSeries.X s : MvPowerSeries œÉ R)) where
  hpow s := isTopologicallyNilpotent_of_constantCoeff_zero (constantCoeff_X s)
  tendsto_zero := variables_tendsto_zero

variable [IsTopologicalRing S] [IsLinearTopology S S]

/-- The domain of evaluation of `MvPowerSeries`, as an ideal -/
@[simps]
def hasEvalIdeal : Ideal (œÉ ‚Üí S) where
  carrier := {a | HasEval a}
  add_mem' := HasEval.add
  zero_mem' := HasEval.zero
  smul_mem' := HasEval.mul_left

theorem mem_hasEvalIdeal_iff {a : œÉ ‚Üí S} :
    a ‚àà hasEvalIdeal ‚Üî HasEval a := by
  simp [hasEvalIdeal]

end

/- ## Construction of an evaluation morphism for power series -/

section Evaluation

open WithPiTopology

variable {œÉ : Type*}
variable {R : Type*} [CommRing R] [UniformSpace R]
variable {S : Type*} [CommRing S] [UniformSpace S]
variable {œÜ : R ‚Üí+* S}

-- We endow MvPowerSeries œÉ R with the product uniform structure
private instance : UniformSpace (MvPolynomial œÉ R) :=
  comap toMvPowerSeries (Pi.uniformSpace _)

/-- The induced uniform structure of MvPolynomial œÉ R is an additive group uniform structure -/
private instance [IsUniformAddGroup R] : IsUniformAddGroup (MvPolynomial œÉ R) :=
  IsUniformAddGroup.comap coeToMvPowerSeries.ringHom

theorem _root_.MvPolynomial.toMvPowerSeries_isUniformInducing :
    IsUniformInducing (toMvPowerSeries (œÉ := œÉ) (R := R)) :=
  (isUniformInducing_iff toMvPowerSeries).mpr rfl

theorem _root_.MvPolynomial.toMvPowerSeries_isDenseInducing :
    IsDenseInducing (toMvPowerSeries (œÉ := œÉ) (R := R)) :=
  toMvPowerSeries_isUniformInducing.isDenseInducing denseRange_toMvPowerSeries

variable {a : œÉ ‚Üí S}

/- The evaluation map on multivariate polynomials is uniformly continuous
for the uniform structure induced by that on multivariate power series. -/
theorem _root_.MvPolynomial.toMvPowerSeries_uniformContinuous
    [IsUniformAddGroup R] [IsUniformAddGroup S] [IsLinearTopology S S]
    (hœÜ : Continuous œÜ) (ha : HasEval a) :
    UniformContinuous (MvPolynomial.eval‚ÇÇHom œÜ a) := by
  classical
  apply uniformContinuous_of_continuousAt_zero
  rw [ContinuousAt, map_zero, IsLinearTopology.hasBasis_ideal.tendsto_right_iff]
  intro I hI
  let n : œÉ ‚Üí ‚Ñï := fun s ‚Ü¶ sInf {n : ‚Ñï | (a s) ^ n.succ ‚àà I}
  have hn_ne : ‚àÄ s, Set.Nonempty {n : ‚Ñï | (a s) ^ n.succ ‚àà I} := fun s ‚Ü¶ by
    rcases ha.hpow s |>.eventually_mem hI |>.exists_forall_of_atTop with ‚ü®n, hn‚ü©
    use n
    simpa using hn n.succ n.le_succ
  have hn : Set.Finite (n.support) := by
    change n =·∂†[cofinite] 0
    filter_upwards [ha.tendsto_zero.eventually_mem hI] with s has
    simpa [n, Pi.zero_apply, Nat.sInf_eq_zero, or_iff_left (hn_ne s).ne_empty] using has
  let n‚ÇÄ : œÉ ‚Üí‚ÇÄ ‚Ñï := .ofSupportFinite n hn
  let D := Iic n‚ÇÄ
  have hD : Set.Finite D := finite_Iic _
  have : ‚àÄ d ‚àà D, ‚àÄ·∂† (p : MvPolynomial œÉ R) in ùìù 0, œÜ (p.coeff d) ‚àà I := fun d hd ‚Ü¶ by
    have : Tendsto (œÜ ‚àò coeff d ‚àò toMvPowerSeries) (ùìù 0) (ùìù 0) :=
      hœÜ.comp (continuous_coeff R d) |>.comp continuous_induced_dom |>.tendsto' 0 0 (map_zero _)
    filter_upwards [this.eventually_mem hI] with f hf
    simpa using hf
  rw [‚Üê hD.eventually_all] at this
  filter_upwards [this] with p hp
  rw [coe_eval‚ÇÇHom, SetLike.mem_coe, eval‚ÇÇ_eq]
  apply Ideal.sum_mem
  intro d _
  by_cases hd : d ‚àà D
  ¬∑ exact Ideal.mul_mem_right _ _ (hp d hd)
  ¬∑ apply Ideal.mul_mem_left
    simp only [mem_Iic, D, Finsupp.le_iff] at hd
    push_neg at hd
    rcases hd with ‚ü®s, hs', hs‚ü©
    exact I.prod_mem hs' (I.pow_mem_of_pow_mem (Nat.sInf_mem (hn_ne s)) hs)

variable (œÜ a)
open scoped Classical in
/-- Evaluation of a multivariate power series at `f` at a point `a : œÉ ‚Üí S`.

It coincides with the evaluation of `f` as a polynomial if `f` is the coercion of a polynomial.
Otherwise, it is only relevant if `œÜ` is continuous and `HasEval a`. -/
noncomputable def eval‚ÇÇ (f : MvPowerSeries œÉ R) : S :=
  if H : ‚àÉ p : MvPolynomial œÉ R, p = f then (MvPolynomial.eval‚ÇÇ œÜ a H.choose)
  else IsDenseInducing.extend toMvPowerSeries_isDenseInducing (MvPolynomial.eval‚ÇÇ œÜ a) f

@[simp, norm_cast]
theorem eval‚ÇÇ_coe (f : MvPolynomial œÉ R) :
    MvPowerSeries.eval‚ÇÇ œÜ a f = MvPolynomial.eval‚ÇÇ œÜ a f := by
  have : ‚àÉ p : MvPolynomial œÉ R, (p : MvPowerSeries œÉ R) = f := ‚ü®f, rfl‚ü©
  rw [eval‚ÇÇ, dif_pos this]
  congr
  rw [‚Üê MvPolynomial.coe_inj, this.choose_spec]

@[simp]
theorem eval‚ÇÇ_C (r : R) : eval‚ÇÇ œÜ a (C r) = œÜ r := by
  rw [‚Üê coe_C, eval‚ÇÇ_coe, MvPolynomial.eval‚ÇÇ_C]

@[simp]
theorem eval‚ÇÇ_X (s : œÉ) : eval‚ÇÇ œÜ a (X s) = a s := by
  rw [‚Üê coe_X, eval‚ÇÇ_coe, MvPolynomial.eval‚ÇÇ_X]

variable [IsTopologicalSemiring R] [IsUniformAddGroup R]
    [IsUniformAddGroup S] [CompleteSpace S] [T2Space S]
    [IsTopologicalRing S] [IsLinearTopology S S]

variable {œÜ a}

/-- Evaluation of power series at adequate elements, as a `RingHom` -/
noncomputable def eval‚ÇÇHom (hœÜ : Continuous œÜ) (ha : HasEval a) :
    MvPowerSeries œÉ R ‚Üí+* S :=
  IsDenseInducing.extendRingHom (i := coeToMvPowerSeries.ringHom)
    toMvPowerSeries_isUniformInducing
    denseRange_toMvPowerSeries
    (toMvPowerSeries_uniformContinuous hœÜ ha)

theorem eval‚ÇÇHom_eq_extend (hœÜ : Continuous œÜ) (ha : HasEval a) (f : MvPowerSeries œÉ R) :
    eval‚ÇÇHom hœÜ ha f =
      toMvPowerSeries_isDenseInducing.extend (MvPolynomial.eval‚ÇÇ œÜ a) f :=
  rfl

theorem coe_eval‚ÇÇHom (hœÜ : Continuous œÜ) (ha : HasEval a) :
    ‚áë(eval‚ÇÇHom hœÜ ha) = eval‚ÇÇ œÜ a := by
  ext f
  simp only [eval‚ÇÇHom_eq_extend, eval‚ÇÇ]
  split_ifs with h
  ¬∑ obtain ‚ü®p, rfl‚ü© := h
    simpa [MvPolynomial.coe_eval‚ÇÇHom] using
      toMvPowerSeries_isDenseInducing.extend_eq
        (toMvPowerSeries_uniformContinuous hœÜ ha).continuous p
  ¬∑ rw [‚Üê eval‚ÇÇHom_eq_extend hœÜ ha]

-- Note: this is still true without the `T2Space` hypothesis, by arguing that the case
-- disjunction in the definition of `eval‚ÇÇ` only replaces some values by topologically
-- inseparable ones.
theorem uniformContinuous_eval‚ÇÇ (hœÜ : Continuous œÜ) (ha : HasEval a) :
    UniformContinuous (eval‚ÇÇ œÜ a) := by
  rw [‚Üê coe_eval‚ÇÇHom hœÜ ha]
  exact uniformContinuous_uniformly_extend
    toMvPowerSeries_isUniformInducing
    denseRange_toMvPowerSeries
    (toMvPowerSeries_uniformContinuous hœÜ ha)

theorem continuous_eval‚ÇÇ (hœÜ : Continuous œÜ) (ha : HasEval a) :
    Continuous (eval‚ÇÇ œÜ a : MvPowerSeries œÉ R ‚Üí S) :=
  (uniformContinuous_eval‚ÇÇ hœÜ ha).continuous

theorem hasSum_eval‚ÇÇ (hœÜ : Continuous œÜ) (ha : HasEval a) (f : MvPowerSeries œÉ R) :
    HasSum
    (fun (d : œÉ ‚Üí‚ÇÄ ‚Ñï) ‚Ü¶ œÜ (coeff d f) * (d.prod fun s e => (a s) ^ e))
    (MvPowerSeries.eval‚ÇÇ œÜ a f) := by
  rw [‚Üê coe_eval‚ÇÇHom hœÜ ha, eval‚ÇÇHom_eq_extend hœÜ ha]
  convert (hasSum_of_monomials_self f).map (eval‚ÇÇHom hœÜ ha) (?_) with d
  ¬∑ simp only [Function.comp_apply, coe_eval‚ÇÇHom, ‚Üê MvPolynomial.coe_monomial,
      eval‚ÇÇ_coe, eval‚ÇÇ_monomial]
  ¬∑ rw [coe_eval‚ÇÇHom]; exact continuous_eval‚ÇÇ hœÜ ha

theorem eval‚ÇÇ_eq_tsum (hœÜ : Continuous œÜ) (ha : HasEval a) (f : MvPowerSeries œÉ R) :
    MvPowerSeries.eval‚ÇÇ œÜ a f =
      ‚àë' (d : œÉ ‚Üí‚ÇÄ ‚Ñï), œÜ (coeff d f) * (d.prod fun s e => (a s) ^ e) :=
  (hasSum_eval‚ÇÇ hœÜ ha f).tsum_eq.symm

theorem eval‚ÇÇ_unique (hœÜ : Continuous œÜ) (ha : HasEval a)
    {Œµ : MvPowerSeries œÉ R ‚Üí S} (hŒµ : Continuous Œµ)
    (h : ‚àÄ p : MvPolynomial œÉ R, Œµ p = MvPolynomial.eval‚ÇÇ œÜ a p) :
    Œµ = eval‚ÇÇ œÜ a := by
  rw [‚Üê coe_eval‚ÇÇHom hœÜ ha]
  exact (toMvPowerSeries_isDenseInducing.extend_unique h hŒµ).symm

theorem comp_eval‚ÇÇ (hœÜ : Continuous œÜ) (ha : HasEval a)
    {T : Type*} [UniformSpace T] [CompleteSpace T] [T2Space T]
    [CommRing T] [IsTopologicalRing T] [IsLinearTopology T T] [IsUniformAddGroup T]
    {Œµ : S ‚Üí+* T} (hŒµ : Continuous Œµ) :
    Œµ ‚àò eval‚ÇÇ œÜ a = eval‚ÇÇ (Œµ.comp œÜ) (Œµ ‚àò a) := by
  apply eval‚ÇÇ_unique _ (ha.map hŒµ)
  ¬∑ exact Continuous.comp hŒµ (continuous_eval‚ÇÇ hœÜ ha)
  ¬∑ intro p
    simp only [Function.comp_apply, eval‚ÇÇ_coe]
    rw [‚Üê MvPolynomial.coe_eval‚ÇÇHom, ‚Üê comp_apply, MvPolynomial.comp_eval‚ÇÇHom,
      MvPolynomial.coe_eval‚ÇÇHom]
  ¬∑ simp only [coe_comp, Continuous.comp hŒµ hœÜ]

variable [Algebra R S] [ContinuousSMul R S]

/-- Evaluation of power series at adequate elements, as an `AlgHom` -/
noncomputable def aeval (ha : HasEval a) : MvPowerSeries œÉ R ‚Üí‚Çê[R] S where
  toRingHom := MvPowerSeries.eval‚ÇÇHom (continuous_algebraMap R S) ha
  commutes' r := by
    simp only [toMonoidHom_eq_coe, OneHom.toFun_eq_coe, MonoidHom.toOneHom_coe, MonoidHom.coe_coe]
    rw [‚Üê c_eq_algebraMap, coe_eval‚ÇÇHom, eval‚ÇÇ_C]

theorem coe_aeval (ha : HasEval a) :
    ‚Üë(aeval ha) = eval‚ÇÇ (algebraMap R S) a := by
  simp only [aeval, AlgHom.coe_mk, coe_eval‚ÇÇHom]

theorem continuous_aeval (ha : HasEval a) :
    Continuous (aeval ha : MvPowerSeries œÉ R ‚Üí S) := by
  rw [coe_aeval]
  exact continuous_eval‚ÇÇ (continuous_algebraMap R S) ha

@[simp]
theorem aeval_coe (ha : HasEval a) (p : MvPolynomial œÉ R) :
    aeval ha (p : MvPowerSeries œÉ R) = p.aeval a := by
  rw [coe_aeval, aeval_def, eval‚ÇÇ_coe]

theorem aeval_unique {Œµ : MvPowerSeries œÉ R ‚Üí‚Çê[R] S} (hŒµ : Continuous Œµ) :
    aeval (HasEval.X.map hŒµ) = Œµ := by
  apply DFunLike.ext'
  rw [coe_aeval]
  refine (eval‚ÇÇ_unique (continuous_algebraMap R S) (HasEval.X.map hŒµ) hŒµ ?_).symm
  intro p
  trans Œµ.comp (coeToMvPowerSeries.algHom R) p
  ¬∑ simp
  conv_lhs => rw [‚Üê p.aeval_X_left_apply, MvPolynomial.comp_aeval_apply, MvPolynomial.aeval_def]
  simp

theorem hasSum_aeval (ha : HasEval a) (f : MvPowerSeries œÉ R) :
    HasSum (fun (d : œÉ ‚Üí‚ÇÄ ‚Ñï) ‚Ü¶ (coeff d f) ‚Ä¢ (d.prod fun s e => (a s) ^ e))
      (MvPowerSeries.aeval ha f) := by
  simp_rw [coe_aeval, ‚Üê algebraMap_smul (R := R) S, smul_eq_mul]
  exact hasSum_eval‚ÇÇ (continuous_algebraMap R S) ha f

theorem aeval_eq_sum (ha : HasEval a) (f : MvPowerSeries œÉ R) :
    MvPowerSeries.aeval ha f =
      tsum (fun (d : œÉ ‚Üí‚ÇÄ ‚Ñï) ‚Ü¶ (coeff d f) ‚Ä¢ (d.prod fun s e => (a s) ^ e)) :=
  (hasSum_aeval ha f).tsum_eq.symm

theorem comp_aeval (ha : HasEval a)
    {T : Type*} [CommRing T] [UniformSpace T] [IsUniformAddGroup T]
    [IsTopologicalRing T] [IsLinearTopology T T]
    [T2Space T] [Algebra R T] [ContinuousSMul R T] [CompleteSpace T]
    {Œµ : S ‚Üí‚Çê[R] T} (hŒµ : Continuous Œµ) :
    Œµ.comp (aeval ha) = aeval (ha.map hŒµ)  := by
  apply DFunLike.ext'
  simp only [AlgHom.coe_comp, coe_aeval ha]
  rw [‚Üê RingHom.coe_coe,
    comp_eval‚ÇÇ (continuous_algebraMap R S) ha (show Continuous (Œµ : S ‚Üí+* T) from hŒµ), coe_aeval]
  congr!
  simp only [AlgHom.comp_algebraMap_of_tower]

end Evaluation

end MvPowerSeries
