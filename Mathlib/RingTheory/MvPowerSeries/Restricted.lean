/-
Copyright (c) 2025 William Coram. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: William Coram
-/
module

public import Mathlib.Analysis.Normed.Group.Ultra
public import Mathlib.Analysis.Normed.Order.Lattice
public import Mathlib.Analysis.RCLike.Basic
public import Mathlib.RingTheory.MvPowerSeries.Basic

/-!
# Multivariate restricted power series

`IsRestricted` : We say a multivariate power series over a normed ring `R` is restricted for a
tuple `c` if `â€–coeff t fâ€– * âˆ i âˆˆ t.support, c i ^ t i â†’ 0` under the cofinite filter.

-/

@[expose] public section

namespace MvPowerSeries

open Filter
open scoped Topology Pointwise

variable {R : Type*} [NormedRing R] {Ïƒ : Type*}

/-- A multivariate power series over a normed ring `R` is restricted for a
  tuple `c` if `â€–coeff t fâ€– * âˆ i âˆˆ t.support, c i ^ t i â†’ 0` under the cofinite filter. -/
def IsRestricted (c : Ïƒ â†’ â„) (f : MvPowerSeries Ïƒ R) :=
  Tendsto (fun (t : Ïƒ â†’â‚€ â„•) â†¦ â€–coeff t fâ€– * t.prod (c Â· ^ Â·)) cofinite (ğ“ 0)

@[simp]
lemma isRestricted_iff_abs (c : Ïƒ â†’ â„) (f : MvPowerSeries Ïƒ R) :
    IsRestricted |c| f â†” IsRestricted c f := by
  simp [IsRestricted, NormedAddCommGroup.tendsto_nhds_zero, Finsupp.prod]

lemma isRestricted_zero (c : Ïƒ â†’ â„) : IsRestricted c (0 : MvPowerSeries Ïƒ R) := by
  simpa [IsRestricted] using tendsto_const_nhds

lemma isRestricted_monomial (c : Ïƒ â†’ â„) (n : Ïƒ â†’â‚€ â„•) (a : R) :
    IsRestricted c (monomial n a) := by
  classical
  refine tendsto_nhds_of_eventually_eq (Set.Subsingleton.finite ?_)
  aesop (add simp [Set.Subsingleton, coeff_monomial])

lemma isRestricted_one (c : Ïƒ â†’ â„) : IsRestricted c (1 : MvPowerSeries Ïƒ R) :=
  isRestricted_monomial c 0 1

lemma isRestricted_C (c : Ïƒ â†’ â„) (a : R) : IsRestricted c (C a) := by
  simpa [monomial_zero_eq_C_apply] using isRestricted_monomial c 0 a

lemma isRestricted_add (c : Ïƒ â†’ â„) {f g : MvPowerSeries Ïƒ R} (hf : IsRestricted c f)
    (hg : IsRestricted c g) : IsRestricted c (f + g) := by
  rw [â† isRestricted_iff_abs, IsRestricted] at *
  refine tendsto_const_nhds.squeeze (add_zero (0 : â„) â–¸ hf.add hg) (fun n â†¦ ?_) fun n â†¦ ?_
  Â· dsimp [Finsupp.prod]; positivity -- TODO: add positivity extension for Finsupp.prod
  rw [â† add_mul]
  exact mul_le_mul_of_nonneg_right (norm_add_le ..) (by dsimp [Finsupp.prod]; positivity)

lemma isRestricted_neg (c : Ïƒ â†’ â„) {f : MvPowerSeries Ïƒ R} (hf : IsRestricted c f) :
    IsRestricted c (-f) := by
  rw [â† isRestricted_iff_abs, IsRestricted] at *
  simpa [IsRestricted] using hf

lemma isRestricted_smul (c : Ïƒ â†’ â„) {f : MvPowerSeries Ïƒ R} (hf : IsRestricted c f) (r : R) :
    IsRestricted c (r â€¢ f) := by
  rw [â† isRestricted_iff_abs, IsRestricted] at *
  refine tendsto_const_nhds.squeeze ((hf.const_mul â€–râ€–).trans_eq (by simp)) (fun n â†¦ ?_) fun n â†¦ ?_
  Â· dsimp [Finsupp.prod]; positivity
  simp only [map_smul, smul_eq_mul, Pi.abs_apply, â† mul_assoc]
  exact mul_le_mul_of_nonneg_right (norm_mul_le _ _) (by dsimp [Finsupp.prod]; positivity)

lemma isRestricted_nsmul (c : Ïƒ â†’ â„) (n : â„•) (f : MvPowerSeries Ïƒ R) (hf : IsRestricted c f) :
    IsRestricted c (n â€¢ f) := by
  convert isRestricted_smul c hf (n : R)
  ext _ _
  simp_rw [map_smul, smul_eq_mul, map_nsmul, nsmul_eq_mul]

lemma isRestricted_zsmul (c : Ïƒ â†’ â„) (n : â„¤) (f : MvPowerSeries Ïƒ R) (hf : IsRestricted c f) :
    IsRestricted c (n â€¢ f) := by
  convert isRestricted_smul c hf (n : R)
  ext _ _
  simp_rw [map_smul, smul_eq_mul, map_zsmul, zsmul_eq_mul]

open IsUltrametricDist

lemma tendsto_antidiagonal {M S : Type*} [AddMonoid M] [Finset.HasAntidiagonal M] [NormedRing S]
    [IsUltrametricDist S] {C : M â†’ â„} (hC : âˆ€ a b, C (a + b) = C a * C b) {f g : M â†’ S}
    (hf : Tendsto (fun i â†¦ â€–f iâ€– * C i) cofinite (ğ“ 0))
    (hg : Tendsto (fun i â†¦ â€–g iâ€– * C i) cofinite (ğ“ 0)) :
    Tendsto (fun a â†¦ â€–âˆ‘ p âˆˆ Finset.antidiagonal a, (f p.1 * g p.2)â€– * C a) cofinite (ğ“ 0) := by
  obtain âŸ¨F, Fpos, hFâŸ© := (bddAbove_iff_exists_ge 1).mp
    (Tendsto.bddAbove_range_of_cofinite (Filter.Tendsto.norm hf))
  obtain âŸ¨G, Gpos, hGâŸ© := (bddAbove_iff_exists_ge 1).mp
    (Tendsto.bddAbove_range_of_cofinite (Filter.Tendsto.norm hg))
  simp only [norm_mul, Real.norm_eq_abs, Set.mem_range, forall_exists_index,
    forall_apply_eq_imp_iff] at hF hG
  simp only [NormedAddCommGroup.tendsto_nhds_zero, gt_iff_lt, Real.norm_eq_abs, eventually_cofinite,
    not_lt] at *
  intro Îµ hÎµ
  let I := {x | Îµ / G â‰¤ |â€–f xâ€– * C x|}
  let J := {x | Îµ / F â‰¤ |â€–g xâ€– * C x|}
  specialize hf (Îµ / G) (by positivity)
  specialize hg (Îµ / F) (by positivity)
  refine Set.Finite.subset (s := I + J) (Set.Finite.add (by aesop) (by aesop)) ?_
  by_contra h
  obtain âŸ¨t, ht, ht'âŸ© := Set.not_subset.mp h
  simp only [abs_mul, abs_norm] at *
  have hh (i j : M) (ht_eq : t = i + j) : i âˆ‰ I âˆ¨ j âˆ‰ J := by
    simp_rw [ht_eq] at ht'
    contrapose ht'
    simp only [not_or, not_not] at *
    use i; use ht'.1; use j; use ht'.2
  have hij (i j : M) (ht_eq : t = i + j) : â€–f i * g jâ€– * |C t| < Îµ := by
      calc
      _ â‰¤ â€–f iâ€– * â€–g jâ€– * |C t| :=
        mul_le_mul_of_nonneg (norm_mul_le _ _) (le_refl _) (norm_nonneg _) (abs_nonneg _)
      _ â‰¤ â€–f iâ€– * â€–g jâ€– * (|C i| * |C j|) :=
        mul_le_mul_of_nonneg (le_refl _) (by simp [ht_eq, hC]) (by positivity) (by positivity)
      _ = (â€–f iâ€– * |C i|) * (â€–g jâ€– * |C j|) := by
        ring
      _ < Îµ := by
        rcases hh i j ht_eq with hi | hj
        Â· rw [â† div_mul_cancelâ‚€ Îµ (show G â‰  0 by grind)]
          exact mul_lt_mul_of_nonneg_of_pos (by aesop) (hG j)
            (mul_nonneg (by positivity) (by positivity)) (by positivity)
        Â· rw [â† div_mul_cancelâ‚€ Îµ (show F â‰  0 by grind), mul_comm]
          exact mul_lt_mul_of_nonneg_of_pos (by aesop) (hF i)
            (mul_nonneg (by positivity) (by positivity)) (by positivity)
  have Final : â€–âˆ‘ p âˆˆ Finset.antidiagonal t, f p.1 * g p.2â€– * |C t| < Îµ := by
    obtain âŸ¨k, hk, leqâŸ© := exists_norm_finset_sum_le (Finset.antidiagonal t) (fun a â†¦ f a.1 * g a.2)
    calc
    _ â‰¤ â€–f k.1 * g k.2â€– * |C t| := mul_le_mul_of_nonneg (leq) (le_refl _)
        (by positivity) (by positivity)
    _ < Îµ := hij k.1 k.2 (Eq.symm (by simpa using hk (Finset.nonempty_def.mpr
      (Exists.intro (t, 0) (by simp)))))
  grind

lemma isRestricted_mul [IsUltrametricDist R] (c : Ïƒ â†’ â„) {f g : MvPowerSeries Ïƒ R}
    (hf : IsRestricted c f) (hg : IsRestricted c g) : IsRestricted c (f * g) := by
  classical
  rw [â† isRestricted_iff_abs, IsRestricted] at *
  exact tendsto_antidiagonal (by simp [Finsupp.prod_add_index', pow_add]) hf hg

end MvPowerSeries
