/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.FieldTheory.Finite.Basic
import Mathlib.RingTheory.Invariant.Basic
import Mathlib.RingTheory.RootsOfUnity.PrimitiveRoots
import Mathlib.RingTheory.Unramified.Locus

/-!
# Frobenius elements

In algebraic number theory, if `L/K` is a finite Galois extension of number fields, with rings of
integers `ğ“L/ğ“K`, and if `q` is prime ideal of `ğ“L` lying over a prime ideal `p` of `ğ“K`, then
there exists a **Frobenius element** `Frob p` in `Gal(L/K)` with the property that
`Frob p x â‰¡ x ^ #(ğ“K/p) (mod q)` for all `x âˆˆ ğ“L`.

Following `RingTheory/Invariant.lean`, we develop the theory in the setting that
there is a finite group `G` acting on a ring `S`, and `R` is the fixed subring of `S`.

## Main results

Let `S/R` be an extension of rings, `Q` be a prime of `S`,
and `P := R âˆ© Q` with finite residue field of cardinality `q`.

- `AlgHom.IsArithFrobAt`: We say that a `Ï† : S â†’â‚[R] S` is an (arithmetic) Frobenius at `Q`
  if `Ï† x â‰¡ x ^ q (mod Q)` for all `x : S`.
- `AlgHom.IsArithFrobAt.apply_of_pow_eq_one`:
  Suppose `S` is a domain and `Ï†` is a Frobenius at `Q`,
  then `Ï† Î¶ = Î¶ ^ q` for any `m`-th root of unity `Î¶` with `q âˆ¤ m`.
- `AlgHom.IsArithFrobAt.eq_of_isUnramifiedAt`:
  Suppose `S` is Noetherian, `Q` contains all zero-divisors, and the extension is unramified at `Q`.
  Then the Frobenius is unique (if exists).

Let `G` be a finite group acting on a ring `S`, and `R` is the fixed subring of `S`.

- `IsArithFrobAt`: We say that a `Ïƒ : G` is an (arithmetic) Frobenius at `Q`
  if `Ïƒ â€¢ x â‰¡ x ^ q (mod Q)` for all `x : S`.
- `IsArithFrobAt.mul_inv_mem_inertia`:
  Two Frobenius elements at `Q` differ by an element in the inertia subgroup of `Q`.
- `IsArithFrobAt.conj`: If `Ïƒ` is a Frobenius at `Q`, then `Ï„ÏƒÏ„â»Â¹` is a Frobenius at `Ïƒ â€¢ Q`.
- `IsArithFrobAt.exists_of_isInvariant`: Frobenius element exists.
-/

variable {R S : Type*} [CommRing R] [CommRing S] [Algebra R S]

/-- `Ï† : S â†’â‚[R] S` is an (arithmetic) Frobenius at `Q` if
`Ï† x â‰¡ x ^ #(R/p) (mod Q)` for all `x : S` (`AlgHom.IsArithFrobAt`). -/
def AlgHom.IsArithFrobAt (Ï† : S â†’â‚[R] S) (Q : Ideal S) : Prop :=
  âˆ€ x, Ï† x - x ^ Nat.card (R â§¸ Q.under R) âˆˆ Q

namespace AlgHom.IsArithFrobAt

variable {Ï† Ïˆ : S â†’â‚[R] S} {Q : Ideal S} (H : Ï†.IsArithFrobAt Q)

include H

lemma mk_apply (x) : Ideal.Quotient.mk Q (Ï† x) = x ^ Nat.card (R â§¸ Q.under R) := by
  rw [â† map_pow, Ideal.Quotient.eq]
  exact H x

lemma finite_quotient : _root_.Finite (R â§¸ Q.under R) := by
  by_contra! h
  obtain rfl : Q = âŠ¤ := by simpa [Nat.card_eq_zero_of_infinite, â† Ideal.eq_top_iff_one] using H 0
  simp only [Ideal.comap_top] at h
  exact not_finite (R â§¸ (âŠ¤ : Ideal R))

lemma card_pos : 0 < Nat.card (R â§¸ Q.under R) :=
  have := H.finite_quotient
  Nat.card_pos

lemma le_comap : Q â‰¤ Q.comap Ï† := by
  intro x hx
  simp_all only [Ideal.mem_comap, â† Ideal.Quotient.eq_zero_iff_mem (I := Q), H.mk_apply,
    zero_pow_eq, ite_eq_right_iff, H.card_pos.ne', false_implies]

/-- A Frobenius element at `Q` restricts to the Frobenius map on `S â§¸ Q`. -/
def restrict : S â§¸ Q â†’â‚[R â§¸ Q.under R] S â§¸ Q where
  toRingHom := Ideal.quotientMap Q Ï† H.le_comap
  commutes' x := by
    obtain âŸ¨x, rflâŸ© := Ideal.Quotient.mk_surjective x
    exact DFunLike.congr_arg (Ideal.Quotient.mk Q) (Ï†.commutes x)

lemma restrict_apply (x : S â§¸ Q) :
    H.restrict x = x ^ Nat.card (R â§¸ Q.under R) := by
  obtain âŸ¨x, rflâŸ© := Ideal.Quotient.mk_surjective x
  exact H.mk_apply x

lemma restrict_mk (x : S) : H.restrict â†‘x = â†‘(Ï† x) := rfl

lemma restrict_injective [Q.IsPrime] :
    Function.Injective H.restrict := by
  rw [injective_iff_map_eq_zero]
  intro x hx
  simpa [restrict_apply, H.card_pos.ne'] using hx

lemma comap_eq [Q.IsPrime] : Q.comap Ï† = Q := by
  refine le_antisymm (fun x hx â†¦ ?_) H.le_comap
  rwa [â† Ideal.Quotient.eq_zero_iff_mem, â† H.restrict_injective.eq_iff, map_zero, restrict_mk,
    Ideal.Quotient.eq_zero_iff_mem, â† Ideal.mem_comap]

/-- Suppose `S` is a domain, and `Ï† : S â†’â‚[R] S` is a Frobenius at `Q : Ideal S`.
Let `Î¶` be a `m`-th root of unity with `Q âˆ¤ m`, then `Ï†` sends `Î¶` to `Î¶ ^ q`. -/
lemma apply_of_pow_eq_one [IsDomain S] {Î¶ : S} {m : â„•} (hÎ¶ : Î¶ ^ m = 1) (hk' : â†‘m âˆ‰ Q) :
    Ï† Î¶ = Î¶ ^ Nat.card (R â§¸ Q.under R) := by
  set q := Nat.card (R â§¸ Q.under R)
  have hm : m â‰  0 := by rintro rfl; exact hk' (by simp)
  obtain âŸ¨k, hk, hÎ¶âŸ© := IsPrimitiveRoot.exists_pos hÎ¶ hm
  have hk' : â†‘k âˆ‰ Q := fun h â†¦ hk' (Q.mem_of_dvd (Nat.cast_dvd_cast (hÎ¶.2 m â€¹_â€º)) h)
  have : NeZero k := âŸ¨hk.ne'âŸ©
  obtain âŸ¨i, hi, eâŸ© := hÎ¶.eq_pow_of_pow_eq_one (Î¾ := Ï† Î¶) (by rw [â† map_pow, hÎ¶.1, map_one])
  have (j : _) : 1 - Î¶ ^ ((q + k - i) * j) âˆˆ Q := by
    rw [â† Ideal.mul_unit_mem_iff_mem _ ((hÎ¶.isUnit k.pos_of_neZero).pow (i * j)),
      sub_mul, one_mul, â† pow_add, â† add_mul, tsub_add_cancel_of_le (by linarith), add_mul,
        pow_add, pow_mul _ k, hÎ¶.1, one_pow, mul_one, pow_mul, e, â† map_pow, mul_comm, pow_mul]
    exact H _
  have hâ‚ := sum_mem (t := Finset.range k) fun j _ â†¦ this j
  have hâ‚‚ := geom_sum_mul (Î¶ ^ (q + k - i)) k
  rw [pow_right_comm, hÎ¶.1, one_pow, sub_self, mul_eq_zero, sub_eq_zero] at hâ‚‚
  rcases hâ‚‚ with hâ‚‚ | hâ‚‚
  Â· simp [hâ‚‚, pow_mul, hk'] at hâ‚
  replace hâ‚‚ := congr($hâ‚‚ * Î¶ ^ i)
  rw [one_mul, â† pow_add, tsub_add_cancel_of_le (by linarith), pow_add, hÎ¶.1, mul_one] at hâ‚‚
  rw [hâ‚‚, e]

/-- A Frobenius element at `Q` restricts to an automorphism of `S_Q`. -/
noncomputable
def localize [Q.IsPrime] : Localization.AtPrime Q â†’â‚[R] Localization.AtPrime Q where
  toRingHom := Localization.localRingHom _ _ Ï† H.comap_eq.symm
  commutes' x := by
    simp [IsScalarTower.algebraMap_apply R S (Localization.AtPrime Q),
      Localization.localRingHom_to_map]

@[simp]
lemma localize_algebraMap [Q.IsPrime] (x : S) :
    H.localize (algebraMap _ _ x) = algebraMap _ _ (Ï† x) :=
  Localization.localRingHom_to_map _ _ _ H.comap_eq.symm _

open IsLocalRing nonZeroDivisors

lemma isArithFrobAt_localize [Q.IsPrime] : H.localize.IsArithFrobAt (maximalIdeal _) := by
  have h : Nat.card (R â§¸ (maximalIdeal _).comap (algebraMap R (Localization.AtPrime Q))) =
      Nat.card (R â§¸ Q.under R) := by
    congr 2
    rw [IsScalarTower.algebraMap_eq R S (Localization.AtPrime Q), â† Ideal.comap_comap,
      Localization.AtPrime.comap_maximalIdeal]
  intro x
  obtain âŸ¨x, s, rflâŸ© := IsLocalization.mk'_surjective Q.primeCompl x
  simp only [localize, coe_mk, Localization.localRingHom_mk', RingHom.coe_coe, h,
    â† IsLocalization.mk'_pow]
  rw [â† IsLocalization.mk'_sub,
    IsLocalization.AtPrime.mk'_mem_maximal_iff (Localization.AtPrime Q) Q]
  simp only [SubmonoidClass.coe_pow, â† Ideal.Quotient.eq_zero_iff_mem]
  simp [H.mk_apply]

/-- Suppose `S` is Noetherian and `Q` is a prime of `S` containing all zero divisors.
If `S/R` is unramified at `Q`, then the Frobenius `Ï† : S â†’â‚[R] S` over `Q` is unique. -/
lemma eq_of_isUnramifiedAt
    (H' : Ïˆ.IsArithFrobAt Q) [Q.IsPrime] (hQ : Q.primeCompl â‰¤ Sâ°)
    [Algebra.IsUnramifiedAt R Q] [IsNoetherianRing S] : Ï† = Ïˆ := by
  have : H.localize = H'.localize := by
    have : IsNoetherianRing (Localization.AtPrime Q) :=
      IsLocalization.isNoetherianRing Q.primeCompl _ inferInstance
    apply Algebra.FormallyUnramified.ext_of_iInf _
      (Ideal.iInf_pow_eq_bot_of_isLocalRing (maximalIdeal _) Ideal.IsPrime.ne_top')
    intro x
    rw [H.isArithFrobAt_localize.mk_apply, H'.isArithFrobAt_localize.mk_apply]
  ext x
  apply IsLocalization.injective (Localization.AtPrime Q) hQ
  rw [â† H.localize_algebraMap, â† H'.localize_algebraMap, this]

end AlgHom.IsArithFrobAt

variable (R) in
/--
Suppose `S` is an `R` algebra, `M` is a monoid acting on `S` whose action is trivial on `R`
`Ïƒ : M` is an (arithmetic) Frobenius at an ideal `Q` of `S` if `Ïƒ â€¢ x â‰¡ x ^ q (mod Q)` for all `x`.
-/
abbrev IsArithFrobAt {M : Type*} [Monoid M] [MulSemiringAction M S] [SMulCommClass M R S]
    (Ïƒ : M) (Q : Ideal S) : Prop :=
  (MulSemiringAction.toAlgHom R S Ïƒ).IsArithFrobAt Q

namespace IsArithFrobAt

open scoped Pointwise

variable {G : Type*} [Group G] [MulSemiringAction G S] [SMulCommClass G R S]
variable {Q : Ideal S} {Ïƒ Ïƒ' : G}

lemma mul_inv_mem_inertia (H : IsArithFrobAt R Ïƒ Q) (H' : IsArithFrobAt R Ïƒ' Q) :
    Ïƒ * Ïƒ'â»Â¹ âˆˆ Q.toAddSubgroup.inertia G := by
  intro x
  simpa [mul_smul] using sub_mem (H (Ïƒ'â»Â¹ â€¢ x)) (H' (Ïƒ'â»Â¹ â€¢ x))

lemma conj (H : IsArithFrobAt R Ïƒ Q) (Ï„ : G) : IsArithFrobAt R (Ï„ * Ïƒ * Ï„â»Â¹) (Ï„ â€¢ Q) := by
  intro x
  have : (Q.map (MulSemiringAction.toRingEquiv G S Ï„)).under R = Q.under R := by
    rw [â† Ideal.comap_symm, â† Ideal.comap_coe, Ideal.under, Ideal.comap_comap]
    congr 1
    exact (MulSemiringAction.toAlgEquiv R S Ï„).symm.toAlgHom.comp_algebraMap
  rw [Ideal.pointwise_smul_eq_comap, Ideal.mem_comap]
  simpa [smul_sub, mul_smul, this] using H (Ï„â»Â¹ â€¢ x)

variable [Finite G] [Algebra.IsInvariant R S G]

variable (R G Q) in
attribute [local instance] Ideal.Quotient.field in
/-- Let `G` be a finite group acting on `S`, and `R` be the fixed subring.
If `Q` is a prime of `S` with finite residue field,
then there exists a Frobenius element `Ïƒ : G` at `Q`. -/
lemma exists_of_isInvariant [Q.IsPrime] [Finite (S â§¸ Q)] : âˆƒ Ïƒ : G, IsArithFrobAt R Ïƒ Q := by
  let P := Q.under R
  have := Algebra.IsInvariant.isIntegral R S G
  have : Q.IsMaximal := Ideal.Quotient.maximal_of_isField _ (Finite.isField_of_domain (S â§¸ Q))
  have : P.IsMaximal := Ideal.isMaximal_comap_of_isIntegral_of_isMaximal Q
  obtain âŸ¨p, hcâŸ© := CharP.exists (R â§¸ P)
  have : Finite (R â§¸ P) := .of_injective _ Ideal.algebraMap_quotient_injective
  cases nonempty_fintype (R â§¸ P)
  obtain âŸ¨k, hp, hkâŸ© := FiniteField.card (R â§¸ P) p
  have := CharP.of_ringHom_of_ne_zero (algebraMap (R â§¸ P) (S â§¸ Q)) p hp.ne_zero
  have : ExpChar (S â§¸ Q) p := .prime hp
  let l : (S â§¸ Q) â‰ƒâ‚[R â§¸ P] S â§¸ Q :=
    { __ := iterateFrobeniusEquiv (S â§¸ Q) p k,
      commutes' r := by
        dsimp [iterateFrobenius_def]
        rw [â† map_pow, â† hk, FiniteField.pow_card] }
  obtain âŸ¨Ïƒ, hÏƒâŸ© := Ideal.Quotient.stabilizerHom_surjective G P Q l
  refine âŸ¨Ïƒ, fun x â†¦ ?_âŸ©
  rw [â† Ideal.Quotient.eq, Nat.card_eq_fintype_card, hk]
  exact DFunLike.congr_fun hÏƒ (Ideal.Quotient.mk Q x)

variable (S G) in
lemma exists_primesOver_isConj (P : Ideal R)
    (hP : âˆƒ Q : Ideal.primesOver P S, Finite (S â§¸ Q.1)) :
    âˆƒ Ïƒ : Ideal.primesOver P S â†’ G, (âˆ€ Q, IsArithFrobAt R (Ïƒ Q) Q.1) âˆ§
      (âˆ€ Qâ‚ Qâ‚‚, IsConj (Ïƒ Qâ‚) (Ïƒ Qâ‚‚)) := by
  obtain âŸ¨âŸ¨Q, hQâ‚, hQâ‚‚âŸ©, hQâ‚ƒâŸ© := hP
  have (Q' : Ideal.primesOver P S) : âˆƒ Ïƒ : G, Q'.1 = Ïƒ â€¢ Q :=
    Algebra.IsInvariant.exists_smul_of_under_eq R S G _ _ (hQâ‚‚.over.symm.trans Q'.2.2.over)
  choose Ï„ hÏ„ using this
  obtain âŸ¨Ïƒ, hÏƒâŸ© := exists_of_isInvariant R G Q
  refine âŸ¨fun Q' â†¦ Ï„ Q' * Ïƒ * (Ï„ Q')â»Â¹, fun Q' â†¦ hÏ„ Q' â–¸ hÏƒ.conj (Ï„ Q'), fun Qâ‚ Qâ‚‚ â†¦
    .trans (.symm (isConj_iff.mpr âŸ¨Ï„ Qâ‚, rflâŸ©)) (isConj_iff.mpr âŸ¨Ï„ Qâ‚‚, rflâŸ©)âŸ©

variable (R G Q)

/-- Let `G` be a finite group acting on `S`, `R` be the fixed subring, and `Q` be a prime of `S`
with finite residue field. This is an arbitrary choice of a Frobenius over `Q`. It is chosen so that
the Frobenius elements of `Qâ‚` and `Qâ‚‚` are conjugate if they lie over the same prime. -/
noncomputable
def _root_.arithFrobAt [Q.IsPrime] [Finite (S â§¸ Q)] : G :=
  (exists_primesOver_isConj S G (Q.under R)
    âŸ¨âŸ¨Q, â€¹_â€º, âŸ¨rflâŸ©âŸ©, â€¹Finite (S â§¸ Q)â€ºâŸ©).choose âŸ¨Q, â€¹_â€º, âŸ¨rflâŸ©âŸ©

protected lemma arithFrobAt [Q.IsPrime] [Finite (S â§¸ Q)] : IsArithFrobAt R (arithFrobAt R G Q) Q :=
  (exists_primesOver_isConj S G (Q.under R)
    âŸ¨âŸ¨Q, â€¹_â€º, âŸ¨rflâŸ©âŸ©, â€¹Finite (S â§¸ Q)â€ºâŸ©).choose_spec.1 âŸ¨Q, â€¹_â€º, âŸ¨rflâŸ©âŸ©

lemma _root_.isConj_arithFrobAt
    [Q.IsPrime] [Finite (S â§¸ Q)] (Q' : Ideal S) [Q'.IsPrime] [Finite (S â§¸ Q')]
    (H : Q.under R = Q'.under R) : IsConj (arithFrobAt R G Q) (arithFrobAt R G Q') := by
  obtain âŸ¨P, hP, hâ‚, hâ‚‚âŸ© : âˆƒ P : Ideal R, P.IsPrime âˆ§ P = Q.under R âˆ§ P = Q'.under R :=
    âŸ¨Q.under R, inferInstance, rfl, HâŸ©
  convert (exists_primesOver_isConj S G P
      âŸ¨âŸ¨Q, â€¹_â€º, âŸ¨hâ‚âŸ©âŸ©, â€¹Finite (S â§¸ Q)â€ºâŸ©).choose_spec.2 âŸ¨Q, â€¹_â€º, âŸ¨hâ‚âŸ©âŸ© âŸ¨Q', â€¹_â€º, âŸ¨hâ‚‚âŸ©âŸ©
  Â· subst hâ‚; rfl
  Â· subst hâ‚‚; rfl

end IsArithFrobAt
