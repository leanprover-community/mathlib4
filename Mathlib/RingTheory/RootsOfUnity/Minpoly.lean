/-
Copyright (c) 2020 Riccardo Brasca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Riccardo Brasca, Johan Commelin
-/
import Mathlib.RingTheory.RootsOfUnity.Basic
import Mathlib.FieldTheory.Minpoly.IsIntegrallyClosed

#align_import ring_theory.roots_of_unity.minpoly from "leanprover-community/mathlib"@"7fdeecc0d03cd40f7a165e6cf00a4d2286db599f"

/-!
# Minimal polynomial of roots of unity

We gather several results about minimal polynomial of root of unity.

## Main results

* `IsPrimitiveRoot.totient_le_degree_minpoly`: The degree of the minimal polynomial of an `n`-th
  primitive root of unity is at least `totient n`.

-/


open minpoly Polynomial

open scoped Polynomial

namespace IsPrimitiveRoot

section CommRing

variable {n : â„•} {K : Type*} [CommRing K] {Î¼ : K} (h : IsPrimitiveRoot Î¼ n)

/-- `Î¼` is integral over `â„¤`. -/
-- Porting note: `hpos` was in the `variable` line, with an `omit` in mathlib3 just after this
-- declaration. For some reason, in Lean4, `hpos` gets included also in the declarations below,
-- even if it is not used in the proof.
theorem isIntegral (hpos : 0 < n) : IsIntegral â„¤ Î¼ := by
  use X ^ n - 1
  -- âŠ¢ Monic (X ^ n - 1) âˆ§ evalâ‚‚ (algebraMap â„¤ K) Î¼ (X ^ n - 1) = 0
  constructor
  -- âŠ¢ Monic (X ^ n - 1)
  Â· exact monic_X_pow_sub_C 1 (ne_of_lt hpos).symm
    -- ðŸŽ‰ no goals
  Â· simp only [((IsPrimitiveRoot.iff_def Î¼ n).mp h).left, evalâ‚‚_one, evalâ‚‚_X_pow, evalâ‚‚_sub,
      sub_self]
#align is_primitive_root.is_integral IsPrimitiveRoot.isIntegral

section IsDomain

variable [IsDomain K] [CharZero K]

/-- The minimal polynomial of a root of unity `Î¼` divides `X ^ n - 1`. -/
theorem minpoly_dvd_x_pow_sub_one : minpoly â„¤ Î¼ âˆ£ X ^ n - 1 := by
  rcases n.eq_zero_or_pos with (rfl | h0)
  -- âŠ¢ minpoly â„¤ Î¼ âˆ£ X ^ 0 - 1
  Â· simp
    -- ðŸŽ‰ no goals
  apply minpoly.isIntegrallyClosed_dvd (isIntegral h h0)
  -- âŠ¢ â†‘(Polynomial.aeval Î¼) (X ^ n - 1) = 0
  simp only [((IsPrimitiveRoot.iff_def Î¼ n).mp h).left, aeval_X_pow, eq_intCast, Int.cast_one,
    aeval_one, AlgHom.map_sub, sub_self]
set_option linter.uppercaseLean3 false in
#align is_primitive_root.minpoly_dvd_X_pow_sub_one IsPrimitiveRoot.minpoly_dvd_x_pow_sub_one

/-- The reduction modulo `p` of the minimal polynomial of a root of unity `Î¼` is separable. -/
theorem separable_minpoly_mod {p : â„•} [Fact p.Prime] (hdiv : Â¬p âˆ£ n) :
    Separable (map (Int.castRingHom (ZMod p)) (minpoly â„¤ Î¼)) := by
  have hdvd : map (Int.castRingHom (ZMod p)) (minpoly â„¤ Î¼) âˆ£ X ^ n - 1 := by
    convert RingHom.map_dvd (mapRingHom (Int.castRingHom (ZMod p)))
        (minpoly_dvd_x_pow_sub_one h)
    simp only [map_sub, map_pow, coe_mapRingHom, map_X, map_one]
  refine' Separable.of_dvd (separable_X_pow_sub_C 1 _ one_ne_zero) hdvd
  -- âŠ¢ â†‘n â‰  0
  by_contra hzero
  -- âŠ¢ False
  exact hdiv ((ZMod.nat_cast_zmod_eq_zero_iff_dvd n p).1 hzero)
  -- ðŸŽ‰ no goals
#align is_primitive_root.separable_minpoly_mod IsPrimitiveRoot.separable_minpoly_mod

/-- The reduction modulo `p` of the minimal polynomial of a root of unity `Î¼` is squarefree. -/
theorem squarefree_minpoly_mod {p : â„•} [Fact p.Prime] (hdiv : Â¬p âˆ£ n) :
    Squarefree (map (Int.castRingHom (ZMod p)) (minpoly â„¤ Î¼)) :=
  (separable_minpoly_mod h hdiv).squarefree
#align is_primitive_root.squarefree_minpoly_mod IsPrimitiveRoot.squarefree_minpoly_mod

/- Let `P` be the minimal polynomial of a root of unity `Î¼` and `Q` be the minimal polynomial of
`Î¼ ^ p`, where `p` is a natural number that does not divide `n`. Then `P` divides `expand â„¤ p Q`. -/
theorem minpoly_dvd_expand {p : â„•} (hdiv : Â¬p âˆ£ n) :
    minpoly â„¤ Î¼ âˆ£ expand â„¤ p (minpoly â„¤ (Î¼ ^ p)) := by
  rcases n.eq_zero_or_pos with (rfl | hpos)
  -- âŠ¢ minpoly â„¤ Î¼ âˆ£ â†‘(expand â„¤ p) (minpoly â„¤ (Î¼ ^ p))
  Â· simp_all
    -- ðŸŽ‰ no goals
  letI : IsIntegrallyClosed â„¤ := GCDMonoid.toIsIntegrallyClosed
  -- âŠ¢ minpoly â„¤ Î¼ âˆ£ â†‘(expand â„¤ p) (minpoly â„¤ (Î¼ ^ p))
  refine' minpoly.isIntegrallyClosed_dvd (h.isIntegral hpos) _
  -- âŠ¢ â†‘(Polynomial.aeval Î¼) (â†‘(expand â„¤ p) (minpoly â„¤ (Î¼ ^ p))) = 0
  Â· rw [aeval_def, coe_expand, â† comp, evalâ‚‚_eq_eval_map, map_comp, Polynomial.map_pow, map_X,
      eval_comp, eval_pow, eval_X, â† evalâ‚‚_eq_eval_map, â† aeval_def]
    exact minpoly.aeval _ _
    -- ðŸŽ‰ no goals
#align is_primitive_root.minpoly_dvd_expand IsPrimitiveRoot.minpoly_dvd_expand

/- Let `P` be the minimal polynomial of a root of unity `Î¼` and `Q` be the minimal polynomial of
`Î¼ ^ p`, where `p` is a prime that does not divide `n`. Then `P` divides `Q ^ p` modulo `p`. -/
theorem minpoly_dvd_pow_mod {p : â„•} [hprime : Fact p.Prime] (hdiv : Â¬p âˆ£ n) :
    map (Int.castRingHom (ZMod p)) (minpoly â„¤ Î¼) âˆ£
      map (Int.castRingHom (ZMod p)) (minpoly â„¤ (Î¼ ^ p)) ^ p := by
  set Q := minpoly â„¤ (Î¼ ^ p)
  -- âŠ¢ map (Int.castRingHom (ZMod p)) (minpoly â„¤ Î¼) âˆ£ map (Int.castRingHom (ZMod p) â€¦
  have hfrob :
    map (Int.castRingHom (ZMod p)) Q ^ p = map (Int.castRingHom (ZMod p)) (expand â„¤ p Q) := by
    rw [â† ZMod.expand_card, map_expand]
  rw [hfrob]
  -- âŠ¢ map (Int.castRingHom (ZMod p)) (minpoly â„¤ Î¼) âˆ£ map (Int.castRingHom (ZMod p) â€¦
  apply RingHom.map_dvd (mapRingHom (Int.castRingHom (ZMod p)))
  -- âŠ¢ minpoly â„¤ Î¼ âˆ£ â†‘(expand â„¤ p) Q
  exact minpoly_dvd_expand h hdiv
  -- ðŸŽ‰ no goals
#align is_primitive_root.minpoly_dvd_pow_mod IsPrimitiveRoot.minpoly_dvd_pow_mod

/- Let `P` be the minimal polynomial of a root of unity `Î¼` and `Q` be the minimal polynomial of
`Î¼ ^ p`, where `p` is a prime that does not divide `n`. Then `P` divides `Q` modulo `p`. -/
theorem minpoly_dvd_mod_p {p : â„•} [hprime : Fact p.Prime] (hdiv : Â¬p âˆ£ n) :
    map (Int.castRingHom (ZMod p)) (minpoly â„¤ Î¼) âˆ£
      map (Int.castRingHom (ZMod p)) (minpoly â„¤ (Î¼ ^ p)) :=
  (UniqueFactorizationMonoid.dvd_pow_iff_dvd_of_squarefree (squarefree_minpoly_mod h hdiv)
        hprime.1.ne_zero).1
    (minpoly_dvd_pow_mod h hdiv)
#align is_primitive_root.minpoly_dvd_mod_p IsPrimitiveRoot.minpoly_dvd_mod_p

/-- If `p` is a prime that does not divide `n`,
then the minimal polynomials of a primitive `n`-th root of unity `Î¼`
and of `Î¼ ^ p` are the same. -/
theorem minpoly_eq_pow {p : â„•} [hprime : Fact p.Prime] (hdiv : Â¬p âˆ£ n) :
    minpoly â„¤ Î¼ = minpoly â„¤ (Î¼ ^ p) := by
  classical
  by_cases hn : n = 0
  Â· simp_all
  have hpos := Nat.pos_of_ne_zero hn
  by_contra hdiff
  set P := minpoly â„¤ Î¼
  set Q := minpoly â„¤ (Î¼ ^ p)
  have Pmonic : P.Monic := minpoly.monic (h.isIntegral hpos)
  have Qmonic : Q.Monic := minpoly.monic ((h.pow_of_prime hprime.1 hdiv).isIntegral hpos)
  have Pirr : Irreducible P := minpoly.irreducible (h.isIntegral hpos)
  have Qirr : Irreducible Q := minpoly.irreducible ((h.pow_of_prime hprime.1 hdiv).isIntegral hpos)
  have PQprim : IsPrimitive (P * Q) := Pmonic.isPrimitive.mul Qmonic.isPrimitive
  have prod : P * Q âˆ£ X ^ n - 1 := by
    rw [IsPrimitive.Int.dvd_iff_map_cast_dvd_map_cast (P * Q) (X ^ n - 1) PQprim
        (monic_X_pow_sub_C (1 : â„¤) (ne_of_gt hpos)).isPrimitive,
      Polynomial.map_mul]
    refine' IsCoprime.mul_dvd _ _ _
    Â· have aux := IsPrimitive.Int.irreducible_iff_irreducible_map_cast Pmonic.isPrimitive
      refine' (dvd_or_coprime _ _ (aux.1 Pirr)).resolve_left _
      rw [map_dvd_map (Int.castRingHom â„š) Int.cast_injective Pmonic]
      intro hdiv
      refine' hdiff (eq_of_monic_of_associated Pmonic Qmonic _)
      exact associated_of_dvd_dvd hdiv (Pirr.dvd_symm Qirr hdiv)
    Â· apply (map_dvd_map (Int.castRingHom â„š) Int.cast_injective Pmonic).2
      exact minpoly_dvd_x_pow_sub_one h
    Â· apply (map_dvd_map (Int.castRingHom â„š) Int.cast_injective Qmonic).2
      exact minpoly_dvd_x_pow_sub_one (pow_of_prime h hprime.1 hdiv)
  replace prod := RingHom.map_dvd (mapRingHom (Int.castRingHom (ZMod p))) prod
  rw [coe_mapRingHom, Polynomial.map_mul, Polynomial.map_sub, Polynomial.map_one,
    Polynomial.map_pow, map_X] at prod
  obtain âŸ¨R, hRâŸ© := minpoly_dvd_mod_p h hdiv
  rw [hR, â† mul_assoc, â† Polynomial.map_mul, â† sq, Polynomial.map_pow] at prod
  have habs : map (Int.castRingHom (ZMod p)) P ^ 2 âˆ£ map (Int.castRingHom (ZMod p)) P ^ 2 * R := by
    use R
  replace habs :=
    lt_of_lt_of_le (PartENat.coe_lt_coe.2 one_lt_two)
      (multiplicity.le_multiplicity_of_pow_dvd (dvd_trans habs prod))
  have hfree : Squarefree (X ^ n - 1 : (ZMod p)[X]) :=
    (separable_X_pow_sub_C 1 (fun h => hdiv <| (ZMod.nat_cast_zmod_eq_zero_iff_dvd n p).1 h)
        one_ne_zero).squarefree
  cases'
    (multiplicity.squarefree_iff_multiplicity_le_one (X ^ n - 1)).1 hfree
      (map (Int.castRingHom (ZMod p)) P) with
    hle hunit
  Â· rw [Nat.cast_one] at habs; exact hle.not_lt habs
  Â· replace hunit := degree_eq_zero_of_isUnit hunit
    rw [degree_map_eq_of_leadingCoeff_ne_zero (Int.castRingHom (ZMod p)) _] at hunit
    Â· exact (minpoly.degree_pos (isIntegral h hpos)).ne' hunit
    simp only [Pmonic, eq_intCast, Monic.leadingCoeff, Int.cast_one, Ne.def, not_false_iff,
      one_ne_zero]
#align is_primitive_root.minpoly_eq_pow IsPrimitiveRoot.minpoly_eq_pow

/-- If `m : â„•` is coprime with `n`,
then the minimal polynomials of a primitive `n`-th root of unity `Î¼`
and of `Î¼ ^ m` are the same. -/
theorem minpoly_eq_pow_coprime {m : â„•} (hcop : Nat.coprime m n) :
    minpoly â„¤ Î¼ = minpoly â„¤ (Î¼ ^ m) := by
  revert n hcop
  -- âŠ¢ âˆ€ {n : â„•}, IsPrimitiveRoot Î¼ n â†’ Nat.coprime m n â†’ minpoly â„¤ Î¼ = minpoly â„¤ ( â€¦
  refine' UniqueFactorizationMonoid.induction_on_prime m _ _ _
  Â· intro h hn
    -- âŠ¢ minpoly â„¤ Î¼ = minpoly â„¤ (Î¼ ^ 0)
    congr
    -- âŠ¢ Î¼ = Î¼ ^ 0
    simpa [(Nat.coprime_zero_left _).mp hn] using h
    -- ðŸŽ‰ no goals
  Â· intro u hunit _ _
    -- âŠ¢ minpoly â„¤ Î¼ = minpoly â„¤ (Î¼ ^ u)
    congr
    -- âŠ¢ Î¼ = Î¼ ^ u
    simp [Nat.isUnit_iff.mp hunit]
    -- ðŸŽ‰ no goals
  Â· intro a p _ hprime
    -- âŠ¢ (IsPrimitiveRoot Î¼ nâœ â†’ Nat.coprime a nâœ â†’ minpoly â„¤ Î¼ = minpoly â„¤ (Î¼ ^ a))  â€¦
    intro hind h hcop
    -- âŠ¢ minpoly â„¤ Î¼ = minpoly â„¤ (Î¼ ^ (p * a))
    rw [hind h (Nat.coprime.coprime_mul_left hcop)]; clear hind
    -- âŠ¢ minpoly â„¤ (Î¼ ^ a) = minpoly â„¤ (Î¼ ^ (p * a))
                                                     -- âŠ¢ minpoly â„¤ (Î¼ ^ a) = minpoly â„¤ (Î¼ ^ (p * a))
    replace hprime := hprime.nat_prime
    -- âŠ¢ minpoly â„¤ (Î¼ ^ a) = minpoly â„¤ (Î¼ ^ (p * a))
    have hdiv := (Nat.Prime.coprime_iff_not_dvd hprime).1 (Nat.coprime.coprime_mul_right hcop)
    -- âŠ¢ minpoly â„¤ (Î¼ ^ a) = minpoly â„¤ (Î¼ ^ (p * a))
    haveI := Fact.mk hprime
    -- âŠ¢ minpoly â„¤ (Î¼ ^ a) = minpoly â„¤ (Î¼ ^ (p * a))
    rw [minpoly_eq_pow (h.pow_of_coprime a (Nat.coprime.coprime_mul_left hcop)) hdiv]
    -- âŠ¢ minpoly â„¤ ((Î¼ ^ a) ^ p) = minpoly â„¤ (Î¼ ^ (p * a))
    congr 1
    -- âŠ¢ (Î¼ ^ a) ^ p = Î¼ ^ (p * a)
    ring
    -- ðŸŽ‰ no goals
#align is_primitive_root.minpoly_eq_pow_coprime IsPrimitiveRoot.minpoly_eq_pow_coprime

/-- If `m : â„•` is coprime with `n`,
then the minimal polynomial of a primitive `n`-th root of unity `Î¼`
has `Î¼ ^ m` as root. -/
theorem pow_isRoot_minpoly {m : â„•} (hcop : Nat.coprime m n) :
    IsRoot (map (Int.castRingHom K) (minpoly â„¤ Î¼)) (Î¼ ^ m) := by
  simp only [minpoly_eq_pow_coprime h hcop, IsRoot.def, eval_map]
  -- âŠ¢ evalâ‚‚ (Int.castRingHom K) (Î¼ ^ m) (minpoly â„¤ (Î¼ ^ m)) = 0
  exact minpoly.aeval â„¤ (Î¼ ^ m)
  -- ðŸŽ‰ no goals
#align is_primitive_root.pow_is_root_minpoly IsPrimitiveRoot.pow_isRoot_minpoly

/-- `primitiveRoots n K` is a subset of the roots of the minimal polynomial of a primitive
`n`-th root of unity `Î¼`. -/
theorem is_roots_of_minpoly [DecidableEq K] :
    primitiveRoots n K âŠ† (map (Int.castRingHom K) (minpoly â„¤ Î¼)).roots.toFinset := by
  by_cases hn : n = 0; Â· simp_all
  -- âŠ¢ primitiveRoots n K âŠ† Multiset.toFinset (roots (map (Int.castRingHom K) (minp â€¦
                         -- ðŸŽ‰ no goals
  have hpos := Nat.pos_of_ne_zero hn
  -- âŠ¢ primitiveRoots n K âŠ† Multiset.toFinset (roots (map (Int.castRingHom K) (minp â€¦
  intro x hx
  -- âŠ¢ x âˆˆ Multiset.toFinset (roots (map (Int.castRingHom K) (minpoly â„¤ Î¼)))
  obtain âŸ¨m, _, hcop, rflâŸ© := (isPrimitiveRoot_iff h hpos).1 ((mem_primitiveRoots hpos).1 hx)
  -- âŠ¢ Î¼ ^ m âˆˆ Multiset.toFinset (roots (map (Int.castRingHom K) (minpoly â„¤ Î¼)))
  simp only [Multiset.mem_toFinset, mem_roots]
  -- âŠ¢ Î¼ ^ m âˆˆ roots (map (Int.castRingHom K) (minpoly â„¤ Î¼))
  convert pow_isRoot_minpoly h hcop
  -- âŠ¢ Î¼ ^ m âˆˆ roots (map (Int.castRingHom K) (minpoly â„¤ Î¼)) â†” IsRoot (map (Int.cas â€¦
  rw [â† mem_roots]
  -- âŠ¢ map (Int.castRingHom K) (minpoly â„¤ Î¼) â‰  0
  exact map_monic_ne_zero <| minpoly.monic <| isIntegral h hpos
  -- ðŸŽ‰ no goals
#align is_primitive_root.is_roots_of_minpoly IsPrimitiveRoot.is_roots_of_minpoly

/-- The degree of the minimal polynomial of `Î¼` is at least `totient n`. -/
theorem totient_le_degree_minpoly : Nat.totient n â‰¤ (minpoly â„¤ Î¼).natDegree := by
  classical
  let P : â„¤[X] := minpoly â„¤ Î¼
  -- minimal polynomial of `Î¼`
  let P_K : K[X] := map (Int.castRingHom K) P
  -- minimal polynomial of `Î¼` sent to `K[X]`
  calc
    n.totient = (primitiveRoots n K).card := h.card_primitiveRoots.symm
    _ â‰¤ P_K.roots.toFinset.card := (Finset.card_le_of_subset (is_roots_of_minpoly h))
    _ â‰¤ Multiset.card P_K.roots := (Multiset.toFinset_card_le _)
    _ â‰¤ P_K.natDegree := (card_roots' _)
    _ â‰¤ P.natDegree := natDegree_map_le _ _
#align is_primitive_root.totient_le_degree_minpoly IsPrimitiveRoot.totient_le_degree_minpoly

end IsDomain

end CommRing

end IsPrimitiveRoot
