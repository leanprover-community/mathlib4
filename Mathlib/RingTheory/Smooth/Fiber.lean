/-
Copyright (c) 2026 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
module

public import Mathlib.RingTheory.Etale.Field
public import Mathlib.RingTheory.Flat.Equalizer
public import Mathlib.RingTheory.Kaehler.TensorProduct
public import Mathlib.RingTheory.LocalRing.ResidueField.Fiber
public import Mathlib.RingTheory.Smooth.Local
public import Mathlib.RingTheory.Smooth.Locus

/-!

# Flat and smooth fibers imply smooth

## Main results
- `Algebra.FormallySmooth.of_formallySmooth_residueField_tensor`:
  Let `(R, m, k)` be a local ring, `S` be a local `R`-algebra that is flat,
  essentially of finite presentation, and `k ‚äó[R] S` is `k`-formally smooth.
  Then `S` is `R`-formally smooth.
- `Algebra.mem_smoothLocus_of_formallySmooth_fiber`:
  Let `S` be a flat and finitely presented `R`-algebra, and `q` be a prime of `S` lying over `p`.
  If `Œ∫(p) ‚äó[R] S` is `Œ∫(p)`-smooth, then `S` is smooth at `q`.
- `Algebra.Smooth.of_formallySmooth_fiber`:
  Flat and finitely presented and smooth fibers imply smooth.
- `Algebra.Etale.of_formallyUnramified_of_flat`:
  Flat and finitely presented and (formally) unramified implies etale.

## Note

For the converse that smooth imples flat, see `Mathlib/RingTheory/Smooth/Flat.lean`.

-/

open TensorProduct IsLocalRing

@[expose] public section

namespace Algebra

local notation "ùìÄ[" R "]" => ResidueField R
local notation "ùìÇ[" R "]" => maximalIdeal R

variable {R S P : Type*} [CommRing R] [CommRing S] [Algebra R S] [Module.Flat R S]
variable [CommRing P] [Algebra R P] [Algebra P S] [IsScalarTower R P S]

section IsLocalRing

variable [IsLocalRing R] [IsLocalRing S] [IsLocalHom (algebraMap R S)]
  [Algebra.FormallySmooth ùìÄ[R] (ùìÄ[R] ‚äó[R] S)]

attribute [local irreducible] KaehlerDifferential in
attribute [local instance] TensorProduct.rightAlgebra in
/--
Let `(R, m, k)` be a local ring, `(S, M, K)` be a local `R`-algebra that is `R`-flat such that
`k ‚äó[R] S` is `k`-formally smooth.

Suppose there exists an `R`-presentation `S = P/I` with `I` finitely generated, and that
`P` is `R`-formally smooth and `Œ©[P‚ÅÑR]` is `P`-finite free.

Then `S` is `R`-formally smooth.

Such `P` always exists when `S` is essentially of finite presentation over `R`.
See `FormallySmooth.of_formallySmooth_residueField_tensor`.
-/
private lemma FormallySmooth.of_formallySmooth_residueField_tensor_aux
    [FormallySmooth R P] [Module.Free P Œ©[P‚ÅÑR]] [Module.Finite P Œ©[P‚ÅÑR]]
    (h‚ÇÅ : Function.Surjective (algebraMap P S)) (h‚ÇÇ : (RingHom.ker (algebraMap P S)).FG) :
    Algebra.FormallySmooth R S := by
  /-
  From the given presentation `0 ‚Üí I ‚Üí P ‚Üí S ‚Üí 0`, we construct the presentation
  `0 ‚Üí k ‚äó·µ£ I ‚Üí k ‚äó·µ£ P ‚Üí k ‚äó·µ£ S ‚Üí 0` using the flatness of `S`.
  We also need the fact that `k ‚äó·µ£ S` is also local with residue field `K`.
  -/
  let Sp := ùìÄ[R] ‚äó[R] S
  let Pp := ùìÄ[R] ‚äó[R] P
  let œÜ : Pp ‚Üí‚Çê[ùìÄ[R]] Sp := Algebra.TensorProduct.map (.id _ _) (IsScalarTower.toAlgHom _ _ _)
  let œà : Sp ‚Üí‚Çê[R] ùìÄ[S] := Algebra.TensorProduct.lift (IsScalarTower.toAlgHom _ _ _)
    (IsScalarTower.toAlgHom _ _ _) fun _ _ ‚Ü¶ .all _ _
  algebraize [œÜ.toRingHom, (œÜ.toRingHom.comp (algebraMap P Pp)), œà.toRingHom,
    œà.toRingHom.comp œÜ.toRingHom]
  have := IsScalarTower.of_algebraMap_eq' œÜ.comp_algebraMap.symm
  have := IsScalarTower.of_algebraMap_eq' œà.comp_algebraMap.symm
  have : IsScalarTower P S Sp := .of_algebraMap_eq' rfl
  have : IsScalarTower S Sp ùìÄ[S] := .of_algebraMap_eq fun r ‚Ü¶ by
    simp [RingHom.algebraMap_toAlgebra, œà, Sp]
  have : IsScalarTower P Sp ùìÄ[S] := .to‚ÇÅ‚ÇÉ‚ÇÑ _ S _ _
  have : IsScalarTower P Pp ùìÄ[S] := .to‚ÇÅ‚ÇÇ‚ÇÑ _ _ Sp _
  let ePp : Pp ‚âÉ‚Çê[P] P ‚äó[R] ùìÄ[R] := { __ := TensorProduct.comm _ _ _, commutes' _ := rfl }
  let e‚ÇÄ : Œ©[Pp‚ÅÑùìÄ[R]] ‚âÉ‚Çó[Pp] Pp ‚äó[P] Œ©[P‚ÅÑR] :=
    (KaehlerDifferential.tensorKaehlerEquiv R ùìÄ[R] P Pp).symm
  have : Module.Free Pp Œ©[Pp‚ÅÑùìÄ[R]] := .of_equiv e‚ÇÄ.symm
  have : Module.Finite Pp Œ©[Pp‚ÅÑùìÄ[R]] := .of_surjective e‚ÇÄ.symm.toLinearMap e‚ÇÄ.symm.surjective
  let e‚ÇÅ : RingHom.ker œÜ ‚âÉ‚Çó[Pp] Pp ‚äó[P] RingHom.ker (algebraMap P S) :=
    kerTensorProductMapIdToAlgHomEquiv _ _ _ _ h‚ÇÅ
  have h‚ÇÅ' : Function.Surjective œÜ := LinearMap.lTensor_surjective _ h‚ÇÅ
  have h‚ÇÇ' : (RingHom.ker œÜ).FG := by
    suffices Module.Finite Pp (RingHom.ker œÜ) from (Submodule.fg_top _).mp this.1
    have : Module.Finite P (RingHom.ker (algebraMap P S)) := ‚ü®(Submodule.fg_top _).mpr h‚ÇÇ‚ü©
    exact .equiv e‚ÇÅ.symm
  have h‚ÇÉ : ùìÇ[Sp] ‚â§ RingHom.ker œà := by
    intro x hx
    obtain ‚ü®x, rfl‚ü© := TensorProduct.mk_surjective _ _ _ (by exact residue_surjective) x
    have : ¬¨IsUnit x := fun h ‚Ü¶ hx <| h.map TensorProduct.includeRight
    simpa [œà, Sp]
  /-
  The key is then to use jacobi criterion `FormallySmooth.iff_injective_cotangentComplexBaseChange`.
  Applying the criterion to `0 ‚Üí I ‚Üí P ‚Üí S ‚Üí 0` and `0 ‚Üí k ‚äó·µ£ I ‚Üí k ‚äó·µ£ P ‚Üí k ‚äó·µ£ S ‚Üí 0`,
  the goal becomes the injectivity of `K ‚äó‚Çõ I ‚Üí K ‚äó‚Çö Œ©[P/R]`,
  while the assumption that `S` is smooth at the maximal ideal translates to the injectivity of
  `K ‚äó[k ‚äó·µ£ S] (k ‚äó·µ£ I) ‚Üí K ‚äó[k ‚äó·µ£ P] Œ©[k ‚äó·µ£ P/R]`.
  -/
  rw [Algebra.FormallySmooth.iff_injective_cotangentComplexBaseChange_residueField (P := P) h‚ÇÅ h‚ÇÇ]
  have := (Algebra.FormallySmooth.iff_injective_cotangentComplexBaseChange
    (R := ùìÄ[R]) _ _ h‚ÇÅ' h‚ÇÇ' h‚ÇÉ).mp inferInstance
  -- But `K ‚äó[k ‚äó·µ£ P] Œ©[k ‚äó·µ£ P/R] = K ‚äó[k ‚äó·µ£ P] ((k ‚äó·µ£ P) ‚äó[P] Œ©[P/R]) = K ‚äó[P] Œ©[P/R]`,
  let e·µ£ : ùìÄ[S] ‚äó[Pp] Œ©[Pp‚ÅÑùìÄ[R]] ‚âÉ‚Çó[S] ùìÄ[S] ‚äó[P] Œ©[P‚ÅÑR] :=
    (AlgebraTensorModule.congr (.refl ùìÄ[S] _) e‚ÇÄ).restrictScalars S ‚â™‚â´‚Çó
      (AlgebraTensorModule.cancelBaseChange P Pp Sp ùìÄ[S] Œ©[P‚ÅÑR]).restrictScalars S
  -- and `K ‚äó[k ‚äó·µ£ S] (k ‚äó·µ£ I) = K ‚äó[k ‚äó·µ£ P] ((k ‚äó·µ£ P) ‚äó[P] S) = K ‚äó[P] S`.
  let e‚Çó : ùìÄ[S] ‚äó[Pp] RingHom.ker œÜ ‚âÉ‚Çó[S] ùìÄ[S] ‚äó[P] RingHom.ker (algebraMap P S) :=
    (AlgebraTensorModule.congr (.refl ùìÄ[S] ùìÄ[S]) e‚ÇÅ).restrictScalars S ‚â™‚â´‚Çó
      (AlgebraTensorModule.cancelBaseChange P Pp Sp ùìÄ[S] _).restrictScalars S
  -- It remains to check that the two maps are equal under the identifications above.
  convert (e·µ£.injective.comp this).comp e‚Çó.symm.injective
  ext x
  dsimp
  induction x with
  | zero => simp only [LinearEquiv.map_zero, LinearMap.map_zero]
  | add x y _ _ => simp only [LinearEquiv.map_add, LinearMap.map_add, *]
  | tmul x y =>
  dsimp [e‚Çó, e·µ£, e‚ÇÅ, KaehlerDifferential.cotangentComplexBaseChange,
    TensorProduct.one_def, Pp, smul_tmul']
  rw [kerTensorProductMapIdToAlgHomEquiv_symm_apply]
  simp [e‚ÇÄ, Pp, ‚Üê TensorProduct.one_def]

/--
Let `(R, m, k)` be a local ring, `S` be a local `R`-algebra that is flat,
essentially of finite presentation, and `k ‚äó[R] S` is `k`-formally smooth.
Then `S` is `R`-formally smooth.

Since we don't have an "essentially of finite presentation" type class yet, we explicitly require a
`P` that is of finite presentation over `R` and that `S` is a localization of it.
-/
lemma FormallySmooth.of_formallySmooth_residueField_tensor (M : Submonoid P)
    [IsLocalization M S] [Algebra.FinitePresentation R P] : Algebra.FormallySmooth R S := by
  /-
  By the fact that `S` is essentially of finite presentation, we get
  `S = (P/I)[M‚Åª¬π] = P[M‚Åª¬π]/I[M‚Åª¬π]`, where `P` is a polynomial ring and `M` some submonoid of `P/I`.
  We then apply `FormallySmooth.of_formallySmooth_residueField_tensor_aux` to this presentation.
  -/
  classical
  obtain ‚ü®n, f‚ÇÄ, hf‚ÇÄ‚ü© := Algebra.FiniteType.iff_quotient_mvPolynomial''.mp
    (inferInstanceAs (Algebra.FiniteType R P))
  let M' := M.comap f‚ÇÄ
  let P' := Localization M'
  let fP : P' ‚Üí‚Çê[R] S := IsLocalization.liftAlgHom (M := M')
      (f := (IsScalarTower.toAlgHom R P S).comp f‚ÇÄ) fun x ‚Ü¶ by
    simpa using IsLocalization.map_units (M := M) _ ‚ü®f‚ÇÄ x.1, x.2‚ü©
  have hf‚ÇÅ : Function.Surjective fP := by
    intro x
    obtain ‚ü®x, ‚ü®s, hs‚ü©, rfl‚ü© := IsLocalization.exists_mk'_eq M x
    obtain ‚ü®x, rfl‚ü© := hf‚ÇÄ x
    obtain ‚ü®s, rfl‚ü© := hf‚ÇÄ s
    refine ‚ü®IsLocalization.mk' (M := M') _ x ‚ü®s, hs‚ü©, ?_‚ü©
    simp [fP, IsLocalization.lift_mk', Units.mul_inv_eq_iff_eq_mul, IsUnit.liftRight]
  have hfP : (RingHom.ker fP).FG := by
    have := Algebra.FinitePresentation.ker_fG_of_surjective _ hf‚ÇÄ
    convert this.map (algebraMap _ P')
    refine le_antisymm ?_ (Ideal.map_le_iff_le_comap.mpr fun x hx ‚Ü¶ by simp_all [fP])
    intro x hx
    obtain ‚ü®x, s, rfl‚ü© := IsLocalization.exists_mk'_eq M' x
    obtain ‚ü®a, ha, e‚ü© : ‚àÉ a ‚àà M, a * f‚ÇÄ x = 0 := by
      simpa [fP, IsLocalization.lift_mk', IsLocalization.map_eq_zero_iff M] using hx
    obtain ‚ü®a, rfl‚ü© := hf‚ÇÄ a
    rw [IsLocalization.mk'_mem_map_algebraMap_iff]
    exact ‚ü®a, ha, by simpa‚ü©
  algebraize [fP.toRingHom]
  have : FormallyEtale (MvPolynomial (Fin n) R) P' := .of_isLocalization M'
  have : FormallySmooth R P' := .comp _ (MvPolynomial (Fin n) R) _
  have : Module.Free P' Œ©[P'‚ÅÑR] :=
    .of_equiv (KaehlerDifferential.tensorKaehlerEquivOfFormallyEtale R (MvPolynomial (Fin n) R) P')
  exact FormallySmooth.of_formallySmooth_residueField_tensor_aux (R := R) (S := S) hf‚ÇÅ hfP

end IsLocalRing

lemma mem_smoothLocus_of_formallySmooth_fiber
    [Algebra.FinitePresentation R S] (p : Ideal R) (q : PrimeSpectrum S)
    [p.IsPrime] [q.asIdeal.LiesOver p] [FormallySmooth p.ResidueField (p.Fiber S)] :
    q ‚àà smoothLocus R S := by
  let Rp := Localization.AtPrime p
  let Sp := Localization (algebraMapSubmonoid S p.primeCompl)
  let Sq := Localization.AtPrime q.asIdeal
  let f : Sp ‚Üí‚Çê[S] Sq := IsLocalization.liftAlgHom (M := algebraMapSubmonoid S p.primeCompl)
        (f := Algebra.ofId _ _) (by
      rintro ‚ü®_, x, hx, rfl‚ü©
      simpa using IsLocalization.map_units (M := q.asIdeal.primeCompl) Sq ‚ü®algebraMap _ _ x,
        by simp_all [q.asIdeal.over_def p]‚ü©)
  algebraize [f.toRingHom]
  have : IsScalarTower R Sp Sq := .to‚ÇÅ‚ÇÉ‚ÇÑ _ S _ _
  have : IsScalarTower Rp Sp Sq := .of_algebraMap_eq' <| by
    apply IsLocalization.ringHom_ext p.primeCompl
    simp only [RingHom.comp_assoc, ‚Üê IsScalarTower.algebraMap_eq]
  have : IsLocalization (algebraMapSubmonoid Sp q.asIdeal.primeCompl) Sq :=
    .isLocalization_of_submonoid_le _ _ (algebraMapSubmonoid S p.primeCompl) _
    (by rintro _ ‚ü®x, hx, rfl‚ü©; simp_all [q.asIdeal.over_def p])
  have : FinitePresentation Rp Sp := by
    have : Algebra.IsPushout R Rp S Sp :=
      .symm <| Algebra.isPushout_of_isLocalization p.primeCompl _ _ _
    exact .equiv (Algebra.IsPushout.equiv R Rp S Sp)
  have : FormallySmooth ùìÄ[Rp] (ùìÄ[Rp] ‚äó[Rp] Sq) := by
    let : Algebra S (ùìÄ[Rp] ‚äó[R] S) := TensorProduct.rightAlgebra
    have : FormallySmooth ùìÄ[Rp] ((ùìÄ[Rp] ‚äó[R] S) ‚äó[S] Sq) :=
      .comp _ (ùìÄ[Rp] ‚äó[R] S) _
    let e : ùìÄ[Rp] ‚äó[R] S ‚âÉ‚Çê[S] S ‚äó[R] ùìÄ[Rp] :=
      { __ := TensorProduct.comm _ _ _, commutes' _ := rfl }
    let e' : (ùìÄ[Rp] ‚äó[R] S) ‚äó[S] Sq ‚âÉ‚Çê[R] ùìÄ[Rp] ‚äó[Rp] Sq :=
      ((TensorProduct.comm _ _ _).restrictScalars R).trans <|
      ((TensorProduct.congr (.refl (R := S)) e).restrictScalars R).trans <|
      ((TensorProduct.cancelBaseChange _ _ S _ _).restrictScalars R).trans <|
      (TensorProduct.comm _ _ _).trans (TensorProduct.equivOfCompatibleSMul _ _ _ _)
    have : e'.toAlgHom.comp (IsScalarTower.toAlgHom R p.ResidueField _) =
        IsScalarTower.toAlgHom _ _ _ := by ext
    let e'' : (ùìÄ[Rp] ‚äó[R] S) ‚äó[S] Sq ‚âÉ‚Çê[ùìÄ[Rp]] ùìÄ[Rp] ‚äó[Rp] Sq :=
      { __ := e', commutes' r := congr($this r) }
    exact .of_equiv e''
  have := FormallySmooth.of_formallySmooth_residueField_tensor
    (R := Rp) (S := Sq) (P := Sp) (algebraMapSubmonoid _ q.asIdeal.primeCompl)
  exact .comp R Rp Sq

lemma Smooth.of_formallySmooth_fiber [Algebra.FinitePresentation R S]
    (H : ‚àÄ (I : Ideal R) [I.IsPrime], FormallySmooth I.ResidueField (I.Fiber S)) :
    Algebra.Smooth R S := by
  refine ‚ü®smoothLocus_eq_univ_iff.mp (Set.eq_univ_iff_forall.mpr fun q ‚Ü¶ ?_), ‚Äπ_‚Ä∫‚ü©
  exact Algebra.mem_smoothLocus_of_formallySmooth_fiber (q.asIdeal.under R) _

attribute [local instance] FormallyEtale.of_formallyUnramified_of_field in
lemma Etale.of_formallyUnramified_of_flat {R S : Type*} [CommRing R] [CommRing S] [Algebra R S]
    [Algebra.FinitePresentation R S] [Module.Flat R S] [FormallyUnramified R S] :
    Etale R S :=
  have : Smooth R S := .of_formallySmooth_fiber inferInstance
  ‚ü®‚ü®inferInstance, inferInstance‚ü©, ‚Äπ_‚Ä∫‚ü©

end Algebra
