/-
Copyright (c) 2020 Devon Tuma. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau, Devon Tuma, Wojciech Nawrocki
-/
import Mathlib.RingTheory.Ideal.IsPrimary
import Mathlib.RingTheory.Ideal.Quotient
import Mathlib.RingTheory.Polynomial.Quotient
import Mathlib.RingTheory.TwoSidedIdeal.Operations

/-!
# Jacobson radical

The Jacobson radical of a module `M` over a ring `R` is defined to be the intersection of all
maximal submodules of `M`. The Jacobson radical of a ring `R` is the Jacobson radical of `R` as
an `R`-module, which is equal to the intersection of all maximal left ideals of `R`.
This is similar to how the nilradical of a commutative ring `R` is equal to the intersection
of all prime ideals of `R`.

We can extend the idea of the nilradical of `R` to ideals of `R`,
by letting the nilradical of an ideal `I` be the intersection of prime ideals containing `I`.
Under this extension, the original nilradical is the radical of the zero ideal `‚ä•`.
Here we define the Jacobson radical of an ideal `I` in a similar way,
as the intersection of maximal ideals containing `I`.

## Main definitions

Let `R` be a ring, `I` a left ideal of `R`, and `M` an `R`-module.

* `Module.jacobson R M` is the Jacobson radical of `M`,
i.e. the infimum of all maximal submodules in `M`.

* `Ideal.jacobson I` is the Jacobson radical, i.e. the infimum of all maximal ideals containing `I`.

* `Ideal.IsLocal I` is the proposition that the jacobson radical of `I` is itself a maximal ideal

Furthermore when `I` is a two-sided ideal of `R`

* `TwoSidedIdeal.jacobson I` is the Jacobson radical as a two-sided ideal

## Main statements

* `mem_jacobson_iff` gives a characterization of members of the jacobson of I

* `Ideal.isLocal_of_isMaximal_radical`: if the radical of I is maximal then so is the jacobson
  radical

## Tags

Jacobson, Jacobson radical, Local Ideal

-/


universe u v

namespace Module

open Submodule

variable (R R‚ÇÇ M M‚ÇÇ : Type*) [Ring R] [Ring R‚ÇÇ]
variable [AddCommGroup M] [Module R M] [AddCommGroup M‚ÇÇ] [Module R‚ÇÇ M‚ÇÇ]
variable {œÑ‚ÇÅ‚ÇÇ : R ‚Üí+* R‚ÇÇ} [RingHomSurjective œÑ‚ÇÅ‚ÇÇ]
variable {F : Type*} [FunLike F M M‚ÇÇ] [SemilinearMapClass F œÑ‚ÇÅ‚ÇÇ M M‚ÇÇ] (f : F)

/-- The Jacobson radical of an `R`-module `M` is the infimum of all maximal submodules in `M`. -/
def jacobson : Submodule R M :=
  sInf { m : Submodule R M | IsCoatom m }

variable {R R‚ÇÇ M M‚ÇÇ}

theorem le_comap_jacobson : jacobson R M ‚â§ comap f (jacobson R‚ÇÇ M‚ÇÇ) := by
  conv_rhs => rw [jacobson, sInf_eq_iInf', comap_iInf]
  refine le_iInf_iff.mpr fun S m hm ‚Ü¶ ?_
  obtain h | h := isCoatom_comap_or_eq_top f S.2
  ¬∑ exact mem_sInf.mp hm _ h
  ¬∑ simpa only [h] using mem_top

theorem map_jacobson_le : map f (jacobson R M) ‚â§ jacobson R‚ÇÇ M‚ÇÇ :=
  map_le_iff_le_comap.mpr (le_comap_jacobson f)

include œÑ‚ÇÅ‚ÇÇ in
theorem jacobson_eq_bot_of_injective (inj : Function.Injective f) (h : jacobson R‚ÇÇ M‚ÇÇ = ‚ä•) :
    jacobson R M = ‚ä• :=
  le_bot_iff.mp <| (le_comap_jacobson f).trans <| by
    simp_rw [h, comap_bot, ((LinearMapClass.ker_eq_bot _).mpr inj).le]

variable {f}

theorem map_jacobson_of_ker_le (surj : Function.Surjective f)
    (le : LinearMap.ker f ‚â§ jacobson R M) :
    map f (jacobson R M) = jacobson R‚ÇÇ M‚ÇÇ :=
  le_antisymm (map_jacobson_le f) <| by
    rw [jacobson, sInf_eq_iInf'] at le
    conv_rhs => rw [jacobson, sInf_eq_iInf', map_iInf_of_ker_le surj le]
    exact le_iInf fun m ‚Ü¶ sInf_le (isCoatom_map_of_ker_le surj (le_iInf_iff.mp le m) m.2)

theorem comap_jacobson_of_ker_le (surj : Function.Surjective f)
    (le : LinearMap.ker f ‚â§ jacobson R M) :
    comap f (jacobson R‚ÇÇ M‚ÇÇ) = jacobson R M := by
  rw [‚Üê map_jacobson_of_ker_le surj le, comap_map_eq_self le]

theorem map_jacobson_of_bijective (hf : Function.Bijective f) :
    map f (jacobson R M) = jacobson R‚ÇÇ M‚ÇÇ :=
  map_jacobson_of_ker_le hf.2 <| by simp_rw [(LinearMapClass.ker_eq_bot _).mpr hf.1, bot_le]

theorem comap_jacobson_of_bijective (hf : Function.Bijective f) :
    comap f (jacobson R‚ÇÇ M‚ÇÇ) = jacobson R M :=
  comap_jacobson_of_ker_le hf.2 <| by simp_rw [(LinearMapClass.ker_eq_bot _).mpr hf.1, bot_le]

theorem jacobson_quotient_of_le {N : Submodule R M} (le : N ‚â§ jacobson R M) :
    jacobson R (M ‚ß∏ N) = map N.mkQ (jacobson R M) :=
  (map_jacobson_of_ker_le N.mkQ_surjective <| by rwa [ker_mkQ]).symm

theorem jacobson_le_of_eq_bot {N : Submodule R M} (h : jacobson R (M ‚ß∏ N) = ‚ä•) :
    jacobson R M ‚â§ N := by
  simp_rw [‚Üê N.ker_mkQ, ‚Üê comap_bot, ‚Üê h, le_comap_jacobson]

variable (R M)

theorem jacobson_quotient_jacobson : jacobson R (M ‚ß∏ jacobson R M) = ‚ä• := by
  rw [jacobson_quotient_of_le le_rfl, mkQ_map_self]

theorem jacobson_lt_top [Nontrivial M] [IsCoatomic (Submodule R M)] : jacobson R M < ‚ä§ := by
  obtain ‚ü®m, hm, -‚ü© := (eq_top_or_exists_le_coatom (‚ä• : Submodule R M)).resolve_left bot_ne_top
  exact (sInf_le <| Set.mem_setOf.mpr hm).trans_lt hm.1.lt_top

example [Nontrivial M] [Module.Finite R M] : jacobson R M < ‚ä§ := jacobson_lt_top R M

variable {Œπ} (M : Œπ ‚Üí Type*) [‚àÄ i, AddCommGroup (M i)] [‚àÄ i, Module R (M i)]

theorem jacobson_pi_le : jacobson R (Œ† i, M i) ‚â§ Submodule.pi Set.univ (jacobson R <| M ¬∑) := by
  simp_rw [‚Üê iInf_comap_proj, jacobson, sInf_eq_iInf', comap_iInf, le_iInf_iff]
  intro i m
  exact iInf_le_of_le ‚ü®_, (isCoatom_comap_iff <| LinearMap.proj_surjective i).mpr m.2‚ü© le_rfl

/-- A product of modules with trivial Jacobson radical (e.g. simple modules) also has trivial
Jacobson radical. -/
theorem jacobson_pi_eq_bot (h : ‚àÄ i, jacobson R (M i) = ‚ä•) : jacobson R (‚àÄ i, M i) = ‚ä• :=
  le_bot_iff.mp <| (jacobson_pi_le R M).trans <| by simp_rw [h, pi_univ_bot, le_rfl]

end Module

section

variable (R R‚ÇÇ : Type*) [Ring R] [Ring R‚ÇÇ] (f : R ‚Üí+* R‚ÇÇ) [RingHomSurjective f]
variable (M : Type*) [AddCommGroup M] [Module R M]

namespace Ring

abbrev jacobson : Ideal R := Module.jacobson R R

instance : (jacobson R).IsTwoSided :=
  ‚ü®fun b ha ‚Ü¶ Module.le_comap_jacobson (f := LinearMap.toSpanSingleton R R b) ha‚ü©

variable {R R‚ÇÇ}

theorem le_comap_jacobson : jacobson R ‚â§ Ideal.comap f (jacobson R‚ÇÇ) :=
  Module.le_comap_jacobson f.toSemilinearMap

theorem map_jacobson_le : Submodule.map f.toSemilinearMap (jacobson R) ‚â§ jacobson R‚ÇÇ :=
  Module.map_jacobson_le f.toSemilinearMap

variable {f} in
theorem map_jacobson_of_ker_le (le : RingHom.ker f ‚â§ jacobson R) :
    Submodule.map f.toSemilinearMap (jacobson R) = jacobson R‚ÇÇ :=
  Module.map_jacobson_of_ker_le f.surjective le

theorem coe_jacobson_quotient (I : Ideal R) [I.IsTwoSided] :
    (jacobson (R ‚ß∏ I) : Set (R ‚ß∏ I)) = Module.jacobson R (R ‚ß∏ I) := by
  let f : R ‚ß∏ I ‚Üí‚Çõ‚Çó[Ideal.Quotient.mk I] R ‚ß∏ I := ‚ü®AddHom.id _, fun _ _ ‚Ü¶ rfl‚ü©
  rw [jacobson, ‚Üê Module.map_jacobson_of_ker_le (f := f) Function.surjective_id]
  ¬∑ apply Set.image_id
  ¬∑ rintro _ rfl; exact zero_mem _

theorem jacobson_quotient_of_le {I : Ideal R} [I.IsTwoSided] (le : I ‚â§ jacobson R) :
    jacobson (R ‚ß∏ I) = Submodule.map (Ideal.Quotient.mk I).toSemilinearMap (jacobson R) :=
  .symm <| Module.map_jacobson_of_ker_le (by exact Ideal.Quotient.mk_surjective) <| by
    rwa [‚Üê I.ker_mkQ] at le

theorem jacobson_le_of_eq_bot {I : Ideal R} [I.IsTwoSided] (h : jacobson (R ‚ß∏ I) = ‚ä•) :
    jacobson R ‚â§ I :=
  Module.jacobson_le_of_eq_bot <| by
    rw [‚Üê le_bot_iff, ‚Üê SetLike.coe_subset_coe] at h ‚ä¢
    rwa [‚Üê coe_jacobson_quotient]

variable (R)

theorem jacobson_quotient_jacobson : jacobson (R ‚ß∏ jacobson R) = ‚ä• :=
  (jacobson_quotient_of_le le_rfl).trans <| SetLike.ext' <| by
    apply SetLike.ext'_iff.mp (jacobson R).mkQ_map_self

theorem jacobson_lt_top [Nontrivial R] : jacobson R < ‚ä§ := Module.jacobson_lt_top R R

theorem jacobson_smul_top_le : jacobson R ‚Ä¢ (‚ä§ : Submodule R M) ‚â§ Module.jacobson R M :=
  Submodule.smul_le.mpr fun _ hr m _ ‚Ü¶ Module.le_comap_jacobson (LinearMap.toSpanSingleton R M m) hr

end Ring

namespace Submodule

variable {R M}

theorem jacobson_smul_lt_top [Nontrivial M] [IsCoatomic (Submodule R M)] (N : Submodule R M) :
    Ring.jacobson R ‚Ä¢ N < ‚ä§ :=
  ((smul_mono_right _ le_top).trans <| Ring.jacobson_smul_top_le R M).trans_lt
    (Module.jacobson_lt_top R M)

theorem FG.jacobson_smul_lt {N : Submodule R M} (ne_bot : N ‚â† ‚ä•) (fg : N.FG) :
    Ring.jacobson R ‚Ä¢ N < N := by
  rw [‚Üê Module.Finite.iff_fg] at fg
  rw [‚Üê nontrivial_iff_ne_bot] at ne_bot
  convert map_strictMono_of_injective N.injective_subtype (jacobson_smul_lt_top ‚ä§)
  on_goal 1 => rw [map_smul'']
  all_goals rw [Submodule.map_top, range_subtype]

/-- A form of Nakayama's lemma for modules over noncommutative rings. -/
theorem FG.eq_bot_of_le_jacobson_smul {N : Submodule R M} (fg : N.FG)
    (le : N ‚â§ Ring.jacobson R ‚Ä¢ N) : N = ‚ä• := by
  contrapose! le; exact (jacobson_smul_lt le fg).not_le

end Submodule

end

namespace Ideal

variable {R : Type u} {S : Type v}

open Polynomial

section Jacobson

section Ring

variable [Ring R] [Ring S] {I : Ideal R}

/-- The Jacobson radical of `I` is the infimum of all maximal (left) ideals containing `I`. -/
def jacobson (I : Ideal R) : Ideal R :=
  sInf { J : Ideal R | I ‚â§ J ‚àß IsMaximal J }

theorem le_jacobson : I ‚â§ jacobson I := fun _ hx => mem_sInf.mpr fun _ hJ => hJ.left hx

@[simp]
theorem jacobson_idem : jacobson (jacobson I) = jacobson I :=
  le_antisymm (sInf_le_sInf fun _ hJ => ‚ü®sInf_le hJ, hJ.2‚ü©) le_jacobson

@[simp]
theorem jacobson_top : jacobson (‚ä§ : Ideal R) = ‚ä§ :=
  eq_top_iff.2 le_jacobson

theorem jacobson_bot : jacobson (‚ä• : Ideal R) = Ring.jacobson R := by
  simp_rw [jacobson, Ring.jacobson, Module.jacobson, bot_le, true_and, isMaximal_def]

@[simp]
theorem jacobson_eq_top_iff : jacobson I = ‚ä§ ‚Üî I = ‚ä§ :=
  ‚ü®fun H =>
    by_contradiction fun hi => let ‚ü®M, hm, him‚ü© := exists_le_maximal I hi
      lt_top_iff_ne_top.1
        (lt_of_le_of_lt (show jacobson I ‚â§ M from sInf_le ‚ü®him, hm‚ü©) <|
          lt_top_iff_ne_top.2 hm.ne_top) H,
    fun H => eq_top_iff.2 <| le_sInf fun _ ‚ü®hij, _‚ü© => H ‚ñ∏ hij‚ü©

theorem jacobson_eq_bot : jacobson I = ‚ä• ‚Üí I = ‚ä• := fun h => eq_bot_iff.mpr (h ‚ñ∏ le_jacobson)

theorem jacobson_eq_self_of_isMaximal [H : IsMaximal I] : I.jacobson = I :=
  le_antisymm (sInf_le ‚ü®le_of_eq rfl, H‚ü©) le_jacobson

instance (priority := 100) jacobson.isMaximal [H : IsMaximal I] : IsMaximal (jacobson I) :=
  ‚ü®‚ü®fun htop => H.1.1 (jacobson_eq_top_iff.1 htop), fun _ hJ =>
    H.1.2 _ (lt_of_le_of_lt le_jacobson hJ)‚ü©‚ü©

theorem mem_jacobson_iff {x : R} : x ‚àà jacobson I ‚Üî ‚àÄ y, ‚àÉ z, z * y * x + z - 1 ‚àà I :=
  ‚ü®fun hx y =>
    by_cases
      (fun hxy : I ‚äî span {y * x + 1} = ‚ä§ =>
        let ‚ü®p, hpi, q, hq, hpq‚ü© := Submodule.mem_sup.1 ((eq_top_iff_one _).1 hxy)
        let ‚ü®r, hr‚ü© := mem_span_singleton'.1 hq
        ‚ü®r, by
          -- Porting note: supply `mul_add_one` with explicit variables
          rw [mul_assoc, ‚Üê mul_add_one r (y * x), hr, ‚Üê hpq, ‚Üê neg_sub, add_sub_cancel_right]
          exact I.neg_mem hpi‚ü©)
      fun hxy : I ‚äî span {y * x + 1} ‚â† ‚ä§ => let ‚ü®M, hm1, hm2‚ü© := exists_le_maximal _ hxy
      suffices x ‚àâ M from (this <| mem_sInf.1 hx ‚ü®le_trans le_sup_left hm2, hm1‚ü©).elim
      fun hxm => hm1.1.1 <| (eq_top_iff_one _).2 <| add_sub_cancel_left (y * x) 1 ‚ñ∏
        M.sub_mem (le_sup_right.trans hm2 <| subset_span rfl) (M.mul_mem_left _ hxm),
    fun hx => mem_sInf.2 fun M ‚ü®him, hm‚ü© => by_contradiction fun hxm =>
      let ‚ü®y, i, hi, df‚ü© := hm.exists_inv hxm
      let ‚ü®z, hz‚ü© := hx (-y)
      hm.1.1 <| (eq_top_iff_one _).2 <| sub_sub_cancel (z * -y * x + z) 1 ‚ñ∏
        M.sub_mem (by
          -- Porting note: supply `mul_add_one` with explicit variables
          rw [mul_assoc, ‚Üê mul_add_one z, neg_mul, ‚Üê sub_eq_iff_eq_add.mpr df.symm, neg_sub,
            sub_add_cancel]
          exact M.mul_mem_left _ hi) <| him hz‚ü©

theorem exists_mul_add_sub_mem_of_mem_jacobson {I : Ideal R} (r : R) (h : r ‚àà jacobson I) :
    ‚àÉ s, s * (r + 1) - 1 ‚àà I := by
  cases' mem_jacobson_iff.1 h 1 with s hs
  use s
  rw [mul_add, mul_one]
  simpa using hs

theorem exists_mul_sub_mem_of_sub_one_mem_jacobson {I : Ideal R} (r : R) (h : r - 1 ‚àà jacobson I) :
    ‚àÉ s, s * r - 1 ‚àà I := by
  convert exists_mul_add_sub_mem_of_mem_jacobson _ h
  simp

/-- An ideal equals its Jacobson radical iff it is the intersection of a set of maximal ideals.
Allowing the set to include ‚ä§ is equivalent, and is included only to simplify some proofs. -/
theorem eq_jacobson_iff_sInf_maximal :
    I.jacobson = I ‚Üî ‚àÉ M : Set (Ideal R), (‚àÄ J ‚àà M, IsMaximal J ‚à® J = ‚ä§) ‚àß I = sInf M := by
  use fun hI => ‚ü®{ J : Ideal R | I ‚â§ J ‚àß J.IsMaximal }, ‚ü®fun _ hJ => Or.inl hJ.right, hI.symm‚ü©‚ü©
  rintro ‚ü®M, hM, hInf‚ü©
  refine le_antisymm (fun x hx => ?_) le_jacobson
  rw [hInf, mem_sInf]
  intro I hI
  cases' hM I hI with is_max is_top
  ¬∑ exact (mem_sInf.1 hx) ‚ü®le_sInf_iff.1 (le_of_eq hInf) I hI, is_max‚ü©
  ¬∑ exact is_top.symm ‚ñ∏ Submodule.mem_top

theorem eq_jacobson_iff_sInf_maximal' :
    I.jacobson = I ‚Üî ‚àÉ M : Set (Ideal R), (‚àÄ J ‚àà M, ‚àÄ (K : Ideal R), J < K ‚Üí K = ‚ä§) ‚àß I = sInf M :=
  eq_jacobson_iff_sInf_maximal.trans
    ‚ü®fun h =>
      let ‚ü®M, hM‚ü© := h
      ‚ü®M,
        ‚ü®fun J hJ K hK =>
          Or.recOn (hM.1 J hJ) (fun h => h.1.2 K hK) fun h => eq_top_iff.2 (le_of_lt (h ‚ñ∏ hK)),
          hM.2‚ü©‚ü©,
      fun h =>
      let ‚ü®M, hM‚ü© := h
      ‚ü®M,
        ‚ü®fun J hJ =>
          Or.recOn (Classical.em (J = ‚ä§)) (fun h => Or.inr h) fun h => Or.inl ‚ü®‚ü®h, hM.1 J hJ‚ü©‚ü©,
          hM.2‚ü©‚ü©‚ü©

/-- An ideal `I` equals its Jacobson radical if and only if every element outside `I`
also lies outside of a maximal ideal containing `I`. -/
theorem eq_jacobson_iff_not_mem :
    I.jacobson = I ‚Üî ‚àÄ (x) (_ : x ‚àâ I), ‚àÉ M : Ideal R, (I ‚â§ M ‚àß M.IsMaximal) ‚àß x ‚àâ M := by
  constructor
  ¬∑ intro h x hx
    erw [‚Üê h, mem_sInf] at hx
    push_neg at hx
    exact hx
  ¬∑ refine fun h => le_antisymm (fun x hx => ?_) le_jacobson
    contrapose hx
    erw [mem_sInf]
    push_neg
    exact h x hx

theorem map_jacobson_of_surjective {f : R ‚Üí+* S} (hf : Function.Surjective f) :
    RingHom.ker f ‚â§ I ‚Üí map f I.jacobson = (map f I).jacobson := by
  intro h
  unfold Ideal.jacobson
  -- Porting note: dot notation for `RingHom.ker` does not work
  have : ‚àÄ J ‚àà { J : Ideal R | I ‚â§ J ‚àß J.IsMaximal }, RingHom.ker f ‚â§ J :=
    fun J hJ => le_trans h hJ.left
  refine Trans.trans (map_sInf hf this) (le_antisymm ?_ ?_)
  ¬∑ refine
      sInf_le_sInf fun J hJ =>
        ‚ü®comap f J, ‚ü®‚ü®le_comap_of_map_le hJ.1, ?_‚ü©, map_comap_of_surjective f hf J‚ü©‚ü©
    haveI : J.IsMaximal := hJ.right
    exact comap_isMaximal_of_surjective f hf
  ¬∑ refine sInf_le_sInf_of_subset_insert_top fun j hj => hj.recOn fun J hJ => ?_
    rw [‚Üê hJ.2]
    cases' map_eq_top_or_isMaximal_of_surjective f hf hJ.left.right with htop hmax
    ¬∑ exact htop.symm ‚ñ∏ Set.mem_insert ‚ä§ _
    ¬∑ exact Set.mem_insert_of_mem ‚ä§ ‚ü®map_mono hJ.1.1, hmax‚ü©

theorem map_jacobson_of_bijective {f : R ‚Üí+* S} (hf : Function.Bijective f) :
    map f I.jacobson = (map f I).jacobson :=
  map_jacobson_of_surjective hf.right
    (le_trans (le_of_eq (f.injective_iff_ker_eq_bot.1 hf.left)) bot_le)

theorem comap_jacobson {f : R ‚Üí+* S} {K : Ideal S} :
    comap f K.jacobson = sInf (comap f '' { J : Ideal S | K ‚â§ J ‚àß J.IsMaximal }) :=
  Trans.trans (comap_sInf' f _) sInf_eq_iInf.symm

theorem comap_jacobson_of_surjective {f : R ‚Üí+* S} (hf : Function.Surjective f) {K : Ideal S} :
    comap f K.jacobson = (comap f K).jacobson := by
  unfold Ideal.jacobson
  refine le_antisymm ?_ ?_
  ¬∑ rw [‚Üê top_inf_eq (sInf _), ‚Üê sInf_insert, comap_sInf', sInf_eq_iInf]
    refine iInf_le_iInf_of_subset fun J hJ => ?_
    have : comap f (map f J) = J :=
      Trans.trans (comap_map_of_surjective f hf J)
        (le_antisymm (sup_le_iff.2 ‚ü®le_of_eq rfl, le_trans (comap_mono bot_le) hJ.left‚ü©)
          le_sup_left)
    cases' map_eq_top_or_isMaximal_of_surjective _ hf hJ.right with htop hmax
    ¬∑ exact ‚ü®‚ä§, Set.mem_insert ‚ä§ _, htop ‚ñ∏ this‚ü©
    ¬∑ exact ‚ü®map f J, Set.mem_insert_of_mem _ ‚ü®le_map_of_comap_le_of_surjective f hf hJ.1, hmax‚ü©,
        this‚ü©
  ¬∑ simp_rw [comap_sInf, le_iInf_iff]
    intros J hJ
    haveI : J.IsMaximal := hJ.right
    exact sInf_le ‚ü®comap_mono hJ.left, comap_isMaximal_of_surjective _ hf‚ü©

@[mono]
theorem jacobson_mono {I J : Ideal R} : I ‚â§ J ‚Üí I.jacobson ‚â§ J.jacobson := by
  intro h x hx
  erw [mem_sInf] at hx ‚ä¢
  exact fun K ‚ü®hK, hK_max‚ü© => hx ‚ü®Trans.trans h hK, hK_max‚ü©

/-- The Jacobson radical of a two-sided ideal is two-sided.

It is preferable to use `TwoSidedIdeal.jacobson` instead of this lemma. -/
theorem jacobson_mul_mem_right {I : Ideal R}
    (mul_mem_right : ‚àÄ {x y}, x ‚àà I ‚Üí x * y ‚àà I) :
    ‚àÄ {x y}, x ‚àà I.jacobson ‚Üí x * y ‚àà I.jacobson := by
  -- Proof generalized from
  -- https://ysharifi.wordpress.com/2022/08/16/the-jacobson-radical-definition-and-basic-results/
  intro x r xJ
  apply mem_sInf.mpr
  intro ùî™ ùî™_mem
  by_cases rùî™ : r ‚àà ùî™
  ¬∑ apply ùî™.smul_mem _ rùî™
  -- ùî™‚ÇÄ := { a : R | a*r ‚àà ùî™ }
  let ùî™‚ÇÄ : Ideal R := Submodule.comap (DistribMulAction.toLinearMap R (S := R·µê·µí·µñ) R (.op r)) ùî™
  suffices x ‚àà ùî™‚ÇÄ by simpa [ùî™‚ÇÄ] using this
  have Iùî™‚ÇÄ : I ‚â§ ùî™‚ÇÄ := fun i iI =>
    ùî™_mem.left (mul_mem_right iI)
  have ùî™‚ÇÄ_maximal : IsMaximal ùî™‚ÇÄ := by
    refine isMaximal_iff.mpr ‚ü®
      fun h => rùî™ (by simpa [ùî™‚ÇÄ] using h),
      fun J b ùî™‚ÇÄJ bùî™‚ÇÄ bJ => ?_‚ü©
    let K : Ideal R := Ideal.span {b*r} ‚äî ùî™
    have ‚ü®s, y, yùî™, sbyr‚ü© :=
      mem_span_singleton_sup.mp <|
        mul_mem_left _ r <|
          (isMaximal_iff.mp ùî™_mem.right).right K (b*r)
          le_sup_right bùî™‚ÇÄ
          (mem_sup_left <| mem_span_singleton_self _)
    have : 1 - s*b ‚àà ùî™‚ÇÄ := by
      rw [mul_one, add_comm, ‚Üê eq_sub_iff_add_eq] at sbyr
      rw [sbyr, ‚Üê mul_assoc] at yùî™
      simp [ùî™‚ÇÄ, sub_mul, yùî™]
    have : 1 - s*b + s*b ‚àà J := by
      apply add_mem (ùî™‚ÇÄJ this) (J.mul_mem_left _ bJ)
    simpa using this
  exact mem_sInf.mp xJ ‚ü®Iùî™‚ÇÄ, ùî™‚ÇÄ_maximal‚ü©

end Ring

section CommRing

variable [CommRing R] [CommRing S] {I : Ideal R}

theorem radical_le_jacobson : radical I ‚â§ jacobson I :=
  le_sInf fun _ hJ => (radical_eq_sInf I).symm ‚ñ∏ sInf_le ‚ü®hJ.left, IsMaximal.isPrime hJ.right‚ü©

theorem isRadical_of_eq_jacobson (h : jacobson I = I) : I.IsRadical :=
  radical_le_jacobson.trans h.le

theorem isUnit_of_sub_one_mem_jacobson_bot (r : R) (h : r - 1 ‚àà jacobson (‚ä• : Ideal R)) :
    IsUnit r := by
  cases' exists_mul_sub_mem_of_sub_one_mem_jacobson r h with s hs
  rw [mem_bot, sub_eq_zero, mul_comm] at hs
  exact isUnit_of_mul_eq_one _ _ hs

theorem mem_jacobson_bot {x : R} : x ‚àà jacobson (‚ä• : Ideal R) ‚Üî ‚àÄ y, IsUnit (x * y + 1) :=
  ‚ü®fun hx y =>
    let ‚ü®z, hz‚ü© := (mem_jacobson_iff.1 hx) y
    isUnit_iff_exists_inv.2
      ‚ü®z, by rwa [add_mul, one_mul, ‚Üê sub_eq_zero, mul_right_comm, mul_comm _ z, mul_right_comm]‚ü©,
    fun h =>
    mem_jacobson_iff.mpr fun y =>
      let ‚ü®b, hb‚ü© := isUnit_iff_exists_inv.1 (h y)
      ‚ü®b, (Submodule.mem_bot R).2 (hb ‚ñ∏ by ring)‚ü©‚ü©

/-- An ideal `I` of `R` is equal to its Jacobson radical if and only if
the Jacobson radical of the quotient ring `R/I` is the zero ideal -/
-- Porting note: changed `Quotient.mk'` to ``
theorem jacobson_eq_iff_jacobson_quotient_eq_bot :
    I.jacobson = I ‚Üî jacobson (‚ä• : Ideal (R ‚ß∏ I)) = ‚ä• := by
  have hf : Function.Surjective (Ideal.Quotient.mk I) := Submodule.Quotient.mk_surjective I
  constructor
  ¬∑ intro h
    replace h := congr_arg (Ideal.map (Ideal.Quotient.mk I)) h
    rw [map_jacobson_of_surjective hf (le_of_eq mk_ker)] at h
    simpa using h
  ¬∑ intro h
    replace h := congr_arg (comap (Ideal.Quotient.mk I)) h
    rw [comap_jacobson_of_surjective hf, ‚Üê RingHom.ker_eq_comap_bot (Ideal.Quotient.mk I)] at h
    simpa using h

/-- The standard radical and Jacobson radical of an ideal `I` of `R` are equal if and only if
the nilradical and Jacobson radical of the quotient ring `R/I` coincide -/
-- Porting note: changed `Quotient.mk'` to ``
theorem radical_eq_jacobson_iff_radical_quotient_eq_jacobson_bot :
    I.radical = I.jacobson ‚Üî radical (‚ä• : Ideal (R ‚ß∏ I)) = jacobson ‚ä• := by
  have hf : Function.Surjective (Ideal.Quotient.mk I) := Submodule.Quotient.mk_surjective I
  constructor
  ¬∑ intro h
    have := congr_arg (map (Ideal.Quotient.mk I)) h
    rw [map_radical_of_surjective hf (le_of_eq mk_ker),
      map_jacobson_of_surjective hf (le_of_eq mk_ker)] at this
    simpa using this
  ¬∑ intro h
    have := congr_arg (comap (Ideal.Quotient.mk I)) h
    rw [comap_radical, comap_jacobson_of_surjective hf,
      ‚Üê RingHom.ker_eq_comap_bot (Ideal.Quotient.mk I)] at this
    simpa using this

theorem jacobson_radical_eq_jacobson : I.radical.jacobson = I.jacobson :=
  le_antisymm
    (le_trans (le_of_eq (congr_arg jacobson (radical_eq_sInf I)))
      (sInf_le_sInf fun _ hJ => ‚ü®sInf_le ‚ü®hJ.1, hJ.2.isPrime‚ü©, hJ.2‚ü©))
    (jacobson_mono le_radical)

end CommRing

end Jacobson

section Polynomial

open Polynomial

variable [CommRing R]

theorem jacobson_bot_polynomial_le_sInf_map_maximal :
    jacobson (‚ä• : Ideal R[X]) ‚â§ sInf (map (C : R ‚Üí+* R[X]) '' { J : Ideal R | J.IsMaximal }) := by
  refine le_sInf fun J => exists_imp.2 fun j hj => ?_
  haveI : j.IsMaximal := hj.1
  refine Trans.trans (jacobson_mono bot_le) (le_of_eq ?_ : J.jacobson ‚â§ J)
  suffices t : (‚ä• : Ideal (Polynomial (R ‚ß∏ j))).jacobson = ‚ä• by
    rw [‚Üê hj.2, jacobson_eq_iff_jacobson_quotient_eq_bot]
    replace t := congr_arg (map (polynomialQuotientEquivQuotientPolynomial j).toRingHom) t
    rwa [map_jacobson_of_bijective _, map_bot] at t
    exact RingEquiv.bijective (polynomialQuotientEquivQuotientPolynomial j)
  refine eq_bot_iff.2 fun f hf => ?_
  have r1 : (X : (R ‚ß∏ j)[X]) ‚â† 0 := fun hX => by
    replace hX := congr_arg (fun f => coeff f 1) hX
    simp only [coeff_X_one, coeff_zero] at hX
    exact zero_ne_one hX.symm
  have r2 := eq_C_of_degree_eq_zero (degree_eq_zero_of_isUnit ((mem_jacobson_bot.1 hf) X))
  simp only [coeff_add, mul_coeff_zero, coeff_X_zero, mul_zero, coeff_one_zero, zero_add] at r2
  erw [add_left_eq_self] at r2
  simpa using (mul_eq_zero.mp r2).resolve_right r1
  -- Porting note: this is golfed to much
  -- simpa [(fun hX => by simpa using congr_arg (fun f => coeff f 1) hX : (X : (R ‚ß∏ j)[X]) ‚â† 0)]
  --   using eq_C_of_degree_eq_zero (degree_eq_zero_of_is_unit ((mem_jacobson_bot.1 hf) X))

theorem jacobson_bot_polynomial_of_jacobson_bot (h : jacobson (‚ä• : Ideal R) = ‚ä•) :
    jacobson (‚ä• : Ideal R[X]) = ‚ä• := by
  refine eq_bot_iff.2 (le_trans jacobson_bot_polynomial_le_sInf_map_maximal ?_)
  refine fun f hf => (Submodule.mem_bot R[X]).2 <| Polynomial.ext fun n =>
    Trans.trans (?_ : coeff f n = 0) (coeff_zero n).symm
  suffices f.coeff n ‚àà Ideal.jacobson ‚ä• by rwa [h, Submodule.mem_bot] at this
  exact mem_sInf.2 fun j hj => (mem_map_C_iff.1 ((mem_sInf.1 hf) ‚ü®j, ‚ü®hj.2, rfl‚ü©‚ü©)) n

end Polynomial

section IsLocal

variable [CommRing R]

/-- An ideal `I` is local iff its Jacobson radical is maximal. -/
class IsLocal (I : Ideal R) : Prop where
  /-- A ring `R` is local if and only if its jacobson radical is maximal -/
  out : IsMaximal (jacobson I)

theorem isLocal_iff {I : Ideal R} : IsLocal I ‚Üî IsMaximal (jacobson I) :=
  ‚ü®fun h => h.1, fun h => ‚ü®h‚ü©‚ü©

theorem isLocal_of_isMaximal_radical {I : Ideal R} (hi : IsMaximal (radical I)) : IsLocal I :=
  ‚ü®have : radical I = jacobson I :=
      le_antisymm (le_sInf fun _ ‚ü®him, hm‚ü© => hm.isPrime.radical_le_iff.2 him)
        (sInf_le ‚ü®le_radical, hi‚ü©)
    show IsMaximal (jacobson I) from this ‚ñ∏ hi‚ü©

theorem IsLocal.le_jacobson {I J : Ideal R} (hi : IsLocal I) (hij : I ‚â§ J) (hj : J ‚â† ‚ä§) :
    J ‚â§ jacobson I :=
  let ‚ü®_, hm, hjm‚ü© := exists_le_maximal J hj
  le_trans hjm <| le_of_eq <| Eq.symm <| hi.1.eq_of_le hm.1.1 <| sInf_le ‚ü®le_trans hij hjm, hm‚ü©

theorem IsLocal.mem_jacobson_or_exists_inv {I : Ideal R} (hi : IsLocal I) (x : R) :
    x ‚àà jacobson I ‚à® ‚àÉ y, y * x - 1 ‚àà I :=
  by_cases
    (fun h : I ‚äî span {x} = ‚ä§ =>
      let ‚ü®p, hpi, q, hq, hpq‚ü© := Submodule.mem_sup.1 ((eq_top_iff_one _).1 h)
      let ‚ü®r, hr‚ü© := mem_span_singleton.1 hq
      Or.inr ‚ü®r, by
        rw [‚Üê hpq, mul_comm, ‚Üê hr, ‚Üê neg_sub, add_sub_cancel_right]; exact I.neg_mem hpi‚ü©)
    fun h : I ‚äî span {x} ‚â† ‚ä§ =>
    Or.inl <|
      le_trans le_sup_right (hi.le_jacobson le_sup_left h) <| mem_span_singleton.2 <| dvd_refl x

end IsLocal

theorem isPrimary_of_isMaximal_radical [CommRing R] {I : Ideal R} (hi : IsMaximal (radical I)) :
    IsPrimary I :=
  have : radical I = jacobson I :=
    le_antisymm (le_sInf fun _ ‚ü®him, hm‚ü© => hm.isPrime.radical_le_iff.2 him)
      (sInf_le ‚ü®le_radical, hi‚ü©)
  ‚ü®ne_top_of_lt <| lt_of_le_of_lt le_radical (lt_top_iff_ne_top.2 hi.1.1), fun {x y} hxy =>
    ((isLocal_of_isMaximal_radical hi).mem_jacobson_or_exists_inv y).symm.imp
      (fun ‚ü®z, hz‚ü© => by
        rw [‚Üê mul_one x, ‚Üê sub_sub_cancel (z * y) 1, mul_sub, mul_left_comm]
        exact I.sub_mem (I.mul_mem_left _ hxy) (I.mul_mem_left _ hz))
      (this ‚ñ∏ id)‚ü©

end Ideal

namespace TwoSidedIdeal

variable {R : Type u} [Ring R]

/-- The Jacobson radical of `I` is the infimum of all maximal (left) ideals containing `I`. -/
def jacobson (I : TwoSidedIdeal R) : TwoSidedIdeal R :=
  (asIdeal I).jacobson.toTwoSided (Ideal.jacobson_mul_mem_right <| I.mul_mem_right _ _)

lemma asIdeal_jacobson (I : TwoSidedIdeal R) : asIdeal I.jacobson = (asIdeal I).jacobson := by
  ext; simp [jacobson]

end TwoSidedIdeal
