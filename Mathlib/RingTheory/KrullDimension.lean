/-
Copyright (c) 2023 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fangming Li, Jujian Zhang
-/

import Mathlib.Order.KrullDimension
import Mathlib.AlgebraicGeometry.PrimeSpectrum.Basic
import Mathlib.RingTheory.Ideal.Basic
import Mathlib.Algebra.Module.LocalizedModule
import Mathlib.Topology.KrullDimension

/-!
# Krull dimension of a (commutative) ring

The ring theoretic krull dimension is the order theoretic krull dimension applied to its prime
spectrum. Unfolding this definition, it is the length of longest series of prime ideals ordered by
inclusion.

## Results
- `ringKrullDim.eq_of_ringEquiv` : isomorphic rings have equal krull dimension
- `primeIdealHeight_eq_ringKrullDim_of_Localization` : the height of a prime ideal `ùî≠` is equal to
  krull dimension of `R_ùî≠`
-/

/--
The ring theoretic Krull dimension is the Krull dimension of prime spectrum ordered by inclusion.
-/
noncomputable def ringKrullDim (R : Type _) [CommRing R] : WithBot (WithTop ‚Ñï) :=
  krullDim (PrimeSpectrum R)

namespace ringKrullDim

lemma eq_topologicalKrullDim (R : Type _) [CommRing R] :
  ringKrullDim R = topologicalKrullDim (PrimeSpectrum R) := by
refine' Eq.symm $ krullDim.eq_OrderDual.trans $ krullDim.eq_of_OrderIso $ OrderIso.symm {
  toFun := OrderDual.toDual ‚àò Œª p ‚Ü¶ ‚ü®PrimeSpectrum.zeroLocus p.asIdeal,
    PrimeSpectrum.isClosed_zeroLocus p.asIdeal, (PrimeSpectrum.isIrreducible_zeroLocus_iff _).mpr
      $ by simpa only [p.IsPrime.radical] using p.IsPrime‚ü©
  invFun := (Œª s ‚Ü¶ ‚ü®PrimeSpectrum.vanishingIdeal s.1,
    PrimeSpectrum.isIrreducible_iff_vanishingIdeal_isPrime.mp s.2.2‚ü© :
      {s : Set (PrimeSpectrum R) | IsClosed s ‚àß IsIrreducible s} ‚Üí PrimeSpectrum R) ‚àò
        OrderDual.ofDual
  left_inv := Œª p ‚Ü¶ by
    ext1
    dsimp
    rw [PrimeSpectrum.vanishingIdeal_zeroLocus_eq_radical, p.IsPrime.radical]
  right_inv := Œª s ‚Ü¶ by
    dsimp [OrderDual.toDual, OrderDual.ofDual, Equiv.coe_refl, id, Subtype.coe_mk,
      Function.comp_apply]
    simp only [PrimeSpectrum.zeroLocus_vanishingIdeal_eq_closure, show
      closure (Subtype.val s) = Subtype.val s by exact s.2.1.closure_eq]
    exact rfl
  map_rel_iff' := by
    intro p q
    simp [Equiv.coe_fn_mk, OrderDual.toDual_le_toDual, Subtype.mk_le_mk,
      PrimeSpectrum.zeroLocus_subset_zeroLocus_iff, q.IsPrime.radical] }

/--
If `R ‚ü∂ S` is a surjective ring homomorphism, then `ringKrullDim S ‚â§ ringKrullDim R`.
-/
theorem le_of_surj (R S : Type _) [CommRing R] [CommRing S] (f : R ‚Üí+* S)
  (hf : Function.Surjective f) : ringKrullDim S ‚â§ ringKrullDim R := by
{ refine' krullDim.le_of_strictMono (PrimeSpectrum.comap f)
    (Monotone.strictMono_of_injective ?_ (PrimeSpectrum.comap_injective_of_surjective f hf))
  ¬∑ intro a b hab
    change ((PrimeSpectrum.comap f) a).asIdeal ‚â§ ((PrimeSpectrum.comap f) b).asIdeal
    rw [PrimeSpectrum.comap_asIdeal, PrimeSpectrum.comap_asIdeal]
    exact Ideal.comap_mono hab }

/--
If `I` is an ideal of `R`, then `ringKrullDim (R ‚ß∏ I) ‚â§ ringKrullDim R`.
-/
theorem le_of_quot (R : Type _) [CommRing R] (I : PrimeSpectrum R) :
  ringKrullDim (R ‚ß∏ I.asIdeal) ‚â§ ringKrullDim R :=
le_of_surj _ _ (Ideal.Quotient.mk I.asIdeal) Ideal.Quotient.mk_surjective

/--
If `R` and `S` are isomorphic, then `ringKrullDim R = ringKrullDim S`.
-/
theorem eq_of_ringEquiv (R S : Type _) [CommRing R] [CommRing S] (e : R ‚âÉ+* S) :
  ringKrullDim R = ringKrullDim S :=
le_antisymm (le_of_surj S R (RingEquiv.symm e) (EquivLike.surjective (RingEquiv.symm e)))
  (le_of_surj R S e (EquivLike.surjective e))

instance (F : Type _) [Field F] : Unique (PrimeSpectrum F) where
  default := ‚ü®‚ä•, Ideal.bot_prime‚ü©
  uniq := Œª p ‚Ü¶ PrimeSpectrum.ext _ _ $ Ideal.ext $ Œª x ‚Ü¶ by
    erw [Submodule.mem_bot]
    refine ‚ü®Œª h ‚Ü¶ ?_, Œª h ‚Ü¶ h.symm ‚ñ∏ Submodule.zero_mem _‚ü©
    rwa [p.asIdeal.eq_bot_of_prime, Submodule.mem_bot] at h

/-
Here we aim to show that for any prime ideal `I` of a commutative ring `R`, the
height of `I` equals the krull dimension of `Localization.AtPrime I.asIdeal`.
-/
section aboutHeightAndLocalization

variable {R : Type _} [CommRing R] (J : Ideal R) (I : PrimeSpectrum R)

/--
For any ideal `J` and submonoid `x` of `R`, `Ideal.localization J x` is
the ideal `J.map (algebraMap R (Localization x))` of `Localization x`.
-/
abbrev _root_.Ideal.localization (x : Submonoid R) : Ideal (Localization x) :=
  J.map (algebraMap R (Localization x))

/--
For any ideals `J` and `I` of `R` such that `I` is prime, `Ideal.localizationAtPrime J I`
is defined as `J.localization I.asIdeal.primeCompl`.
-/
abbrev _root_.Ideal.localizationAtPrime := J.localization I.asIdeal.primeCompl


/-- The canonical map from the ideal J of R to its image JR_I in the localisation. -/
@[simps apply] def _root_.Ideal.toLocalization (x : Submonoid R) :
  J ‚Üí‚Çó[R] J.localization x where
  toFun := Œª x ‚Ü¶ ‚ü®Localization.mk x 1, Submodule.subset_span ‚ü®_, x.2, rfl‚ü©‚ü©
  map_add' := Œª _ _ ‚Ü¶ Subtype.ext (Localization.add_mk_self _ _ _).symm
  map_smul' := Œª _ _ ‚Ü¶ Subtype.ext (Localization.smul_mk _ _ _).symm

/--
For any ideal `J` and submonoid `y` of `R`, `Ideal.localization' J y` is defined
as the collection of all elements which can be written as `Localization.mk ‚Üëa b`
for some `a : J` and `b : y`.
-/
@[simps!]
def _root_.Ideal.localization' (y : Submonoid R) : Ideal (Localization y) where
  carrier := { x | ‚àÉ (a : J) (b : y), x = Localization.mk ‚Üëa b }
  add_mem' := fun {x y} ‚ü®a1, ‚ü®b1, hx‚ü©‚ü© ‚ü®a2, ‚ü®b2, hy‚ü©‚ü© => hx.symm ‚ñ∏ hy.symm ‚ñ∏
    ‚ü®‚ü®_, J.add_mem (J.mul_mem_left b1 (SetLike.coe_mem a2))
      (J.mul_mem_left b2 (SetLike.coe_mem a1))‚ü©, ‚ü®b1 * b2, Localization.add_mk _ _ _ _‚ü©‚ü©
  zero_mem' := ‚ü®0, ‚ü®1, by
    simp only [ZeroMemClass.coe_zero, Localization.mk_eq_monoidOf_mk']
    rw [Submonoid.LocalizationMap.mk']
    simp only [map_one, inv_one, Units.val_one, mul_one]
    rw [Submonoid.LocalizationMap.toMap]
    exact Eq.symm (Localization.mk_zero 1)‚ü©‚ü©
  smul_mem' := Œª c ‚Ü¶ Localization.induction_on c $ by
    rintro ‚ü®c1, c2‚ü© ‚ü®j, ‚ü®a, rfl‚ü©‚ü©
    exact ‚ü®‚ü®_, J.mul_mem_left c1 (SetLike.coe_mem j)‚ü©, ‚ü®c2 * a, Localization.mk_mul _ _ _ _‚ü©‚ü©

lemma _root_.Ideal.mem_localization'_iff {y : Submonoid R} (x : Localization y) :
  x ‚àà J.localization' y ‚Üî ‚àÉ (a : J) (b : y), x = Localization.mk ‚Üëa b :=
Iff.rfl

lemma _root_.Ideal.mem_localization'_of_mem_localization (y : Submonoid R) :
    ‚àÄ x, x ‚àà J.localization y ‚Üí x ‚àà J.localization' y := by
  intro x hx
  apply Submodule.span_induction' ?_ ?_ ?_ ?_ hx
  ¬∑ rintro _ ‚ü®y, hy1, rfl‚ü©; refine ‚ü®‚ü®y, hy1‚ü©, ‚ü®_, rfl‚ü©‚ü©
  ¬∑ exact Ideal.zero_mem _
  ¬∑ intro _ _ _ _; apply Ideal.add_mem
  ¬∑ intro a _ _; exact Submodule.smul_mem _ a

lemma _root_.Ideal.localization'_eq_localization (y : Submonoid R) :
    J.localization' y = J.localization y :=
  le_antisymm (by
    rintro x ‚ü®‚ü®a, ha‚ü©, ‚ü®b, rfl‚ü©‚ü©
    rw [Subtype.coe_mk, ‚Üêone_mul a, ‚Üêmul_one b, ‚ÜêLocalization.mk_mul]
    exact Ideal.mul_mem_left _ _ (Ideal.mem_map_of_mem _ ha)) <|
  J.mem_localization'_of_mem_localization _

instance _root_.Ideal.localization'_IsPrime (J : Set.Iic I) :
  (J.1.asIdeal.localization' I.asIdeal.primeCompl).IsPrime where
ne_top' := fun hit => by
  rw [Ideal.eq_top_iff_one, Ideal.mem_localization'_iff] at hit
  rcases hit with ‚ü®a, ‚ü®b, hb‚ü©‚ü©
  exact (IsLocalization.AtPrime.isUnit_mk'_iff (Localization.AtPrime I.asIdeal) _
    (a : R) b).mp (by simpa only [‚ÜêLocalization.mk_eq_mk', ‚Üêhb] using isUnit_one) (J.2 a.2)
mem_or_mem' := by
    intro x y
    refine Localization.induction_on‚ÇÇ x y ?_
    rintro ‚ü®a1, a2‚ü© ‚ü®b1, b2‚ü© ‚ü®‚ü®p, hp‚ü©, ‚ü®q, h‚ü©‚ü©
    rw [Localization.mk_mul, Localization.mk_eq_mk_iff, Localization.r_iff_exists] at h
    obtain ‚ü®c, hc‚ü© := h
    have h : ‚Üëc * (‚Üëq * (a1 * b1)) ‚àà J.1.asIdeal := hc.symm ‚ñ∏ J.1.asIdeal.mul_mem_left _
      (J.1.asIdeal.mul_mem_left _ hp)
    rw [‚Üêmul_assoc] at h
    exact (J.1.IsPrime.mem_or_mem ((J.1.IsPrime.mem_or_mem h).resolve_left
      (fun h => Submonoid.mul_mem _ c.2 q.2 (J.2 h)))).elim
        (fun h => Or.intro_left _ ‚ü®‚ü®a1, h‚ü©, ‚ü®_, rfl‚ü©‚ü©)
        (fun h => Or.intro_right _ ‚ü®‚ü®b1, h‚ü©, ‚ü®_, rfl‚ü©‚ü©)

/--
There is a canonical map from `Set.Iic I` to `PrimeSpectrum (Localization.AtPrime I.asIdeal)`.
-/
def _root_.PrimeSpectrum.IicToLocalizationAtPrime :
  Set.Iic I ‚Üí PrimeSpectrum (Localization.AtPrime I.asIdeal) :=
Œª I' ‚Ü¶ ‚ü®I'.1.asIdeal.localization' I.asIdeal.primeCompl, inferInstance‚ü©

/--
There is a canonical map from `PrimeSpectrum (Localization.AtPrime I.asIdeal)` to `Set.Iic I`.
-/
@[simp]
 def _root_.PrimeSpectrum.LocalizationAtPrimeToIic :
   PrimeSpectrum (Localization.AtPrime I.asIdeal) ‚Üí Set.Iic I :=
   Œª J ‚Ü¶ ‚ü®‚ü®_, Ideal.IsPrime.comap (hK := J.2) (algebraMap R (Localization.AtPrime I.asIdeal))‚ü©,
     Œª z hz ‚Ü¶
     @Decidable.byContradiction _ (Classical.dec _) $ Œª hnz ‚Ü¶ J.IsPrime.ne_top $ eq_top_iff.mpr $
     False.elim $ J.IsPrime.1 $ (Ideal.eq_top_iff_one _).mpr <| show 1 ‚àà J.asIdeal by
       rw [show (1 : Localization.AtPrime I.asIdeal) = Localization.mk z 1 * Localization.mk 1
         (‚ü®z, hnz‚ü© : I.asIdeal.primeCompl) by simpa only
           [Localization.mk_one_eq_algebraMap, ‚ÜêAlgebra.smul_def, Localization.smul_mk, smul_eq_mul,
             mul_one, eq_comm] using Localization.mk_self (‚ü®z, hnz‚ü© : I.asIdeal.primeCompl)]
       exact Ideal.mul_mem_right _ _ hz‚ü©

/--
The canonical map from `PrimeSpectrum (Localization.AtPrime I.asIdeal)` to `Set.Iic I` is a left
inverse to the canonical map from `Set.Iic I` to `PrimeSpectrum (Localization.AtPrime I.asIdeal)`.
-/
lemma _root_.PrimeSpectrum.LocalizationAtPrimeToIic_IsLeftInverse :
  Function.LeftInverse (PrimeSpectrum.LocalizationAtPrimeToIic I)
    (PrimeSpectrum.IicToLocalizationAtPrime I) := by
{ intro J; ext x; constructor
  ¬∑ intro hx
    change Localization.mk x 1 ‚àà _root_.Ideal.localization' J.val.asIdeal I.asIdeal.primeCompl at hx
    rw [Ideal.mem_localization'_iff] at hx
    rcases hx with ‚ü®a, b, hab‚ü©
    erw [Localization.mk_eq_mk_iff, Localization.r_iff_exists, Submonoid.coe_one, one_mul] at hab
    rcases hab with ‚ü®c, hc‚ü©
    rw [‚Üêmul_assoc] at hc
    exact (or_iff_not_imp_left.1 (Ideal.IsPrime.mem_or_mem J.val.2 (@Set.mem_of_eq_of_mem R
      (‚Üëc * ‚Üëb * x) (‚Üëc * ‚Üëa) J.val.asIdeal hc (Ideal.mul_mem_left J.val.asIdeal ‚Üëc a.2))))
        (Œª hi ‚Ü¶ (Submonoid.mul_mem I.asIdeal.primeCompl c.2 b.2) (J.2 hi))
  ¬∑ intro hx
    change Localization.mk x 1 ‚àà _root_.Ideal.localization' J.val.asIdeal I.asIdeal.primeCompl
    exact ‚ü®‚ü®x, hx‚ü©, ‚ü®1, rfl‚ü©‚ü© }

/--
The canonical map from `PrimeSpectrum (Localization.AtPrime I.asIdeal)` to `Set.Iic I` is a right
inverse to the canonical map from `Set.Iic I` to `PrimeSpectrum (Localization.AtPrime I.asIdeal)`.
-/
lemma _root_.PrimeSpectrum.LocalizationAtPrimeToIic_IsRightInverse :
  Function.RightInverse (PrimeSpectrum.LocalizationAtPrimeToIic I)
    (PrimeSpectrum.IicToLocalizationAtPrime I) := by
{ intro J; ext x; constructor
  ¬∑ intro hx
    simp_rw [PrimeSpectrum.IicToLocalizationAtPrime, PrimeSpectrum.LocalizationAtPrimeToIic,
      Ideal.mem_comap, Ideal.mem_localization'_iff] at hx
    rcases hx with ‚ü®‚ü®a, ha‚ü©, ‚ü®b, hab‚ü©‚ü©
    dsimp at ha hab
    rw [‚Üêone_mul a, ‚Üêmul_one b, ‚ÜêLocalization.mk_mul] at hab; rw [hab]
    exact Ideal.mul_mem_left J.asIdeal (Localization.mk 1 b) ha
  ¬∑ refine Localization.induction_on x ?_
    ¬∑ rintro ‚ü®a, b‚ü© hab
      refine' ‚ü®‚ü®a, ?_‚ü©, b, rfl‚ü©
      ¬∑ change Localization.mk a 1 ‚àà J.asIdeal
        rw [‚Üêshow (Localization.mk (b : R) 1) * (Localization.mk a b) = Localization.mk a 1 by
          rw [Localization.mk_mul, mul_comm, ‚ÜêLocalization.mk_mul, Localization.mk_self, mul_one]]
        exact Ideal.mul_mem_left J.asIdeal (Localization.mk b 1) hab }

/--
The canonical map from `Set.Iic I` to `PrimeSpectrum (Localization.AtPrime I.asIdeal)`
is bijective (not necessary for the proof).
-/
lemma _root_.PrimeSpectrum.IicToLocalizationAtPrime.bijective :
  Function.Bijective (PrimeSpectrum.IicToLocalizationAtPrime I) := by
rw [Function.bijective_iff_has_inverse]
exact ‚ü®PrimeSpectrum.LocalizationAtPrimeToIic I,
  ‚ü®PrimeSpectrum.LocalizationAtPrimeToIic_IsLeftInverse I,
    PrimeSpectrum.LocalizationAtPrimeToIic_IsRightInverse I‚ü©‚ü©

/--
The canonical map from `PrimeSpectrum (Localization.AtPrime I.asIdeal)` to `Set.Iic I`
is bijective (not necessary for the proof).
-/
lemma _root_.PrimeSpectrum.LocalizationAtPrimeToIic.bijective :
  Function.Bijective (PrimeSpectrum.LocalizationAtPrimeToIic I) := by
rw [Function.bijective_iff_has_inverse]
exact ‚ü®PrimeSpectrum.IicToLocalizationAtPrime I,
  ‚ü®PrimeSpectrum.LocalizationAtPrimeToIic_IsRightInverse I,
    PrimeSpectrum.LocalizationAtPrimeToIic_IsLeftInverse I‚ü©‚ü©

/--
The canonical map from `Set.Iic I` to `PrimeSpectrum (Localization.AtPrime I.asIdeal)`
is monotone.
-/
lemma _root_.PrimeSpectrum.IicToLocalizationAtPrime_isMonotone :
  Monotone (PrimeSpectrum.IicToLocalizationAtPrime I) := by
{ intro J1 J2 H x hx; rcases hx with ‚ü®a, ‚ü®b, hab‚ü©‚ü©; exact ‚ü®‚ü®a, H a.2‚ü©, ‚ü®b, hab‚ü©‚ü© }

/--
The canonical map from `PrimeSpectrum (Localization.AtPrime I.asIdeal)` to `Set.Iic I`
is monotone.
-/
lemma _root_.PrimeSpectrum.LocalizationAtPrimeToIic_isMonotone :
  Monotone (PrimeSpectrum.LocalizationAtPrimeToIic I) := by
{ intro J1 J2 H x hx; exact H hx }

/--
We can regard the canonical map from `Set.Iic I` to `PrimeSpectrum (Localization.AtPrime I.asIdeal)`
as an equivalence.
-/
def _root_.PrimeSpectrum.IicToLocalizationAtPrimeEquiv :
  (Set.Iic I) ‚âÉ (PrimeSpectrum (Localization.AtPrime I.asIdeal)) where
    toFun := PrimeSpectrum.IicToLocalizationAtPrime I
    invFun := PrimeSpectrum.LocalizationAtPrimeToIic I
    left_inv := PrimeSpectrum.LocalizationAtPrimeToIic_IsLeftInverse I
    right_inv := PrimeSpectrum.LocalizationAtPrimeToIic_IsRightInverse I

/--
We can regard the canonical map from `PrimeSpectrum (Localization.AtPrime I.asIdeal)` to
`Set.Iic I` as an equivalence.
-/
def _root_.PrimeSpectrum.LocalizationAtPrimeToIicEquiv :
  (PrimeSpectrum (Localization.AtPrime I.asIdeal)) ‚âÉ (Set.Iic I) where
    toFun := PrimeSpectrum.LocalizationAtPrimeToIic I
    invFun := PrimeSpectrum.IicToLocalizationAtPrime I
    left_inv := PrimeSpectrum.LocalizationAtPrimeToIic_IsRightInverse I
    right_inv := PrimeSpectrum.LocalizationAtPrimeToIic_IsLeftInverse I

lemma _root_.PrimeSpectrum.IicToLocalizationAtPrimeEquiv_IsMonotone :
  Monotone (PrimeSpectrum.IicToLocalizationAtPrimeEquiv I) := by
{ exact PrimeSpectrum.IicToLocalizationAtPrime_isMonotone I }

lemma _root_.PrimeSpectrum.LocalizationAtPrimeToIicEquiv_IsMonotone :
  Monotone (PrimeSpectrum.LocalizationAtPrimeToIicEquiv I) := by
{ exact PrimeSpectrum.LocalizationAtPrimeToIic_isMonotone I }

lemma _root_.PrimeSpectrum.LocalizationAtPrimeToIicEquiv_is_symm :
  PrimeSpectrum.LocalizationAtPrimeToIicEquiv I =
    (PrimeSpectrum.IicToLocalizationAtPrimeEquiv I).symm := rfl

/--
We have a canonical order isomorphism from `Set.Iic I` to
`PrimeSpectrum (Localization.AtPrime I.asIdeal)`.
-/
@[simp]
def _root_.PrimeSpectrum.IicToLocalizationAtPrime_OrderIso :
  (Set.Iic I) ‚âÉo (PrimeSpectrum (Localization.AtPrime I.asIdeal)) := by
exact Equiv.toOrderIso (PrimeSpectrum.IicToLocalizationAtPrimeEquiv I)
  (PrimeSpectrum.IicToLocalizationAtPrimeEquiv_IsMonotone I)
    (PrimeSpectrum.LocalizationAtPrimeToIicEquiv_IsMonotone I)

/--
The height of `I` is equal to the Krull dimension of `localization.at_prime I.as_ideal`.
-/
theorem primeIdealHeight_eq_ringKrullDim_of_Localization :
  height (PrimeSpectrum R) I = ringKrullDim (Localization.AtPrime I.asIdeal) :=
krullDim.eq_of_OrderIso (PrimeSpectrum.IicToLocalizationAtPrime_OrderIso I)

end aboutHeightAndLocalization

end ringKrullDim



/-
Codes written in the process of proving the theorem that the height of `I` is
equal to the Krull dimension of the localization of `R` at `I`, which were
later found to be unnecessary for the proof.
-/
section extraCodes

variable {R : Type _} [CommRing R] (J : Ideal R) (I : PrimeSpectrum R)

@[simps!] def _root_.Localization.divBy {x : Submonoid R} (s : x) :
  Module.End (Localization x) (Localization x) where
    toFun := Œª x ‚Ü¶ (Localization.mk 1 s) * x
    map_add' := mul_add _
    map_smul' := Œª r x ‚Ü¶ by dsimp; ring

lemma _root_.Localization.divBy_apply_mem {y : Submonoid R} (s : y)
  (x) (hx : x ‚àà J.localization y) :
  Localization.divBy s x ‚àà J.localization y := by
simpa only [Localization.divBy_apply] using
  (J.localization y).mul_mem_left
    (Submonoid.LocalizationMap.mk' (Localization.monoidOf y) 1 s) hx

variable {I}

def _root_.Localization.divBy' {y : Submonoid R} (s : y) :
  Module.End R (J.localization y) :=
(LinearMap.restrict _ $ Localization.divBy_apply_mem J s).restrictScalars R

lemma _root_.Localization.divBy'_right_inv {y : Submonoid R} (s : y) :
  algebraMap R _ s * Localization.divBy' J s = 1 :=
LinearMap.ext $ Œª x ‚Ü¶ show (s : R) ‚Ä¢ Localization.divBy' J s x = x from Subtype.ext $
  show (s : R) ‚Ä¢ (Localization.mk 1 s * x) = x by rw [‚Üêsmul_mul_assoc, Localization.smul_mk,
    smul_eq_mul, mul_one, Localization.mk_self, one_mul]

lemma _root_.LocalizationAtPrime.divBy'_left_inv  {y : Submonoid R} (s : y) :
  (Localization.divBy' J s) * algebraMap R _ s = 1 :=
LinearMap.ext $ Œª x ‚Ü¶ Subtype.ext $ show Localization.mk 1 s * ((s : R) ‚Ä¢ x) = x
  by erw [mul_smul_comm, ‚Üêsmul_mul_assoc, Localization.smul_mk, smul_eq_mul, mul_one,
    Localization.mk_self, one_mul]

lemma toIdealImageSpan_exist_eq  {z : Submonoid R} y :
  ‚àÉ (x : J √ó z), (x.2 : R) ‚Ä¢ y = J.toLocalization z x.1 := by
{ rcases y with ‚ü®y, h‚ü©
  apply Submodule.span_induction' ?_ ?_ ?_ ?_ h
  ¬∑ rintro _ ‚ü®_, h, rfl‚ü©
    refine ‚ü®‚ü®‚ü®_, h‚ü©, 1‚ü©, one_smul _ _‚ü©
  ¬∑ refine ‚ü®‚ü®0, 1‚ü©, ?_‚ü©
    simp only [OneMemClass.coe_one, one_smul, map_zero, Submodule.mk_eq_zero]
  ¬∑ rintro x hx y hy ‚ü®‚ü®mx, nx‚ü©, hmnx‚ü© ‚ü®‚ü®my, ny‚ü©, hmny‚ü©
    refine ‚ü®‚ü®(nx : R) ‚Ä¢ my + (ny : R) ‚Ä¢ mx, nx * ny‚ü©, Subtype.ext ?_‚ü©
    have : ny.1 ‚Ä¢ nx.1 ‚Ä¢ x + nx.1 ‚Ä¢ ny.1 ‚Ä¢ y =
      ny.1 ‚Ä¢ Localization.mk mx.1 1 + nx ‚Ä¢ Localization.mk my.1 1
    ¬∑ exact Subtype.ext_iff.mp (congr_arg‚ÇÇ (. + .) (congr_arg ((. ‚Ä¢ .) (ny : R)) hmnx)
      (congr_arg ((. ‚Ä¢ .) (nx : R)) hmny))
    rw [smul_comm, ‚Üêsmul_add, ‚Üêsmul_add, Localization.smul_mk] at this
    erw [Localization.smul_mk] at this
    rw [Localization.add_mk_self, ‚Üêmul_smul, add_comm (_ ‚Ä¢ _)] at this
    exact this
  ¬∑ rintro a x hx ‚ü®‚ü®c1, c2‚ü©, (hc : (c2 : R) ‚Ä¢ _ = _)‚ü©
    induction a using Localization.induction_on with | H a => ?_
    induction x using Localization.induction_on with | H x => ?_
    rcases a with ‚ü®d1, d2‚ü©
    rcases x with ‚ü®x1, x2‚ü©
    refine ‚ü®‚ü®‚ü®d1 ‚Ä¢ c1, J.mul_mem_left d1 (SetLike.coe_mem c1)‚ü©, d2 * c2‚ü©,
      Subtype.ext (?_ : (_ * _) ‚Ä¢ (Localization.mk _ _ * _) = Localization.mk (_ ‚Ä¢ _) _)‚ü©
    rw [‚ÜêLocalization.smul_mk (d1 : R) (c1 : R) 1, show Localization.mk c1.1 1 = c2.1 ‚Ä¢
      Localization.mk _ _ from (Subtype.ext_iff.mp hc).symm, Localization.smul_mk,
      Localization.smul_mk, Localization.mk_mul, Localization.smul_mk, smul_eq_mul,
      Localization.mk_eq_mk_iff, Localization.r_iff_exists]
    exact ‚ü®1, by dsimp; ring‚ü© }

lemma _root_.Ideal.toLocalization_apply_eq_iff (y : Submonoid R) (x‚ÇÅ x‚ÇÇ) :
  J.toLocalization y x‚ÇÅ = J.toLocalization y x‚ÇÇ ‚Üî
    ‚àÉ (c : y), (c : R) ‚Ä¢ x‚ÇÇ = (c : R) ‚Ä¢ x‚ÇÅ :=
Subtype.ext_iff.trans $ Localization.mk_eq_mk_iff.trans $ Localization.r_iff_exists.trans $
  exists_congr $ Œª x ‚Ü¶ eq_comm.trans $ Iff.symm $ Subtype.ext_iff.trans $ by simp [smul_eq_mul]

instance (y : Submonoid R) : IsLocalizedModule y (J.toLocalization y) where
  map_units := Œª s ‚Ü¶ ‚ü®‚ü®_, _, Localization.divBy'_right_inv _ s,
    LocalizationAtPrime.divBy'_left_inv _ s‚ü©, rfl‚ü©
  surj' := toIdealImageSpan_exist_eq J
  eq_iff_exists' := J.toLocalization_apply_eq_iff _ _ _

noncomputable def _root_.Ideal.localizedModuleEquivLocalization (y : Submonoid R) :
  LocalizedModule y J ‚âÉ‚Çó[R] J.localization y :=
IsLocalizedModule.iso _ $ J.toLocalization y

lemma _root_.Ideal.localizedModuleEquivLocalization_apply (y : Submonoid R) (a b) :
    J.localizedModuleEquivLocalization y (LocalizedModule.mk a b) =
    ‚ü®Localization.mk a b, by simpa only [show Localization.mk (a : R) b =
      (Localization.mk 1 b) * (Localization.mk ‚Üëa 1) by rw [Localization.mk_mul, one_mul, mul_one]]
        using Ideal.mul_mem_left _ _ (Ideal.apply_coe_mem_map _ J a)‚ü© :=
(Module.End_algebraMap_isUnit_inv_apply_eq_iff _ _ _ _).mpr <| by
  refine Subtype.ext (?_ : Localization.mk _ _ = _ ‚Ä¢ Localization.mk (a : R) b)
  rw [Localization.smul_mk, smul_eq_mul, Localization.mk_eq_mk_iff, Localization.r_iff_exists]
  exact ‚ü®1, by simp‚ü©

end extraCodes
