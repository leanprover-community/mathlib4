/-
Copyright (c) 2022 Riccardo Brasca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Riccardo Brasca
-/
import Mathlib.RingTheory.Ideal.BigOperators
import Mathlib.RingTheory.Polynomial.Eisenstein.Criterion
import Mathlib.RingTheory.Polynomial.ScaleRoots

/-!
# Eisenstein polynomials
Given an ideal `ğ“Ÿ` of a commutative semiring `R`, we say that a polynomial `f : R[X]` is
*Eisenstein at `ğ“Ÿ`* if `f.leadingCoeff âˆ‰ ğ“Ÿ`, `âˆ€ n, n < f.natDegree â†’ f.coeff n âˆˆ ğ“Ÿ` and
`f.coeff 0 âˆ‰ ğ“Ÿ ^ 2`. In this file we gather miscellaneous results about Eisenstein polynomials.

## Main definitions
* `Polynomial.IsEisensteinAt f ğ“Ÿ`: the property of being Eisenstein at `ğ“Ÿ`.

## Main results
* `Polynomial.IsEisensteinAt.irreducible`: if a primitive `f` satisfies `f.IsEisensteinAt ğ“Ÿ`,
  where `ğ“Ÿ.IsPrime`, then `f` is irreducible.

## Implementation details
We also define a notion `IsWeaklyEisensteinAt` requiring only that
`âˆ€ n < f.natDegree â†’ f.coeff n âˆˆ ğ“Ÿ`. This makes certain results slightly more general and it is
useful since it is sometimes better behaved (for example it is stable under `Polynomial.map`).

-/


universe u v w z

variable {R : Type u}

open Ideal Algebra Finset

open Polynomial

namespace Polynomial

/-- Given an ideal `ğ“Ÿ` of a commutative semiring `R`, we say that a polynomial `f : R[X]`
is *weakly Eisenstein at `ğ“Ÿ`* if `âˆ€ n, n < f.natDegree â†’ f.coeff n âˆˆ ğ“Ÿ`. -/
@[mk_iff]
structure IsWeaklyEisensteinAt [CommSemiring R] (f : R[X]) (ğ“Ÿ : Ideal R) : Prop where
  mem : âˆ€ {n}, n < f.natDegree â†’ f.coeff n âˆˆ ğ“Ÿ

/-- Given an ideal `ğ“Ÿ` of a commutative semiring `R`, we say that a polynomial `f : R[X]`
is *Eisenstein at `ğ“Ÿ`* if `f.leadingCoeff âˆ‰ ğ“Ÿ`, `âˆ€ n, n < f.natDegree â†’ f.coeff n âˆˆ ğ“Ÿ` and
`f.coeff 0 âˆ‰ ğ“Ÿ ^ 2`. -/
@[mk_iff]
structure IsEisensteinAt [CommSemiring R] (f : R[X]) (ğ“Ÿ : Ideal R) : Prop where
  leading : f.leadingCoeff âˆ‰ ğ“Ÿ
  mem : âˆ€ {n}, n < f.natDegree â†’ f.coeff n âˆˆ ğ“Ÿ
  notMem : f.coeff 0 âˆ‰ ğ“Ÿ ^ 2

namespace IsWeaklyEisensteinAt

section CommSemiring

variable [CommSemiring R] {ğ“Ÿ : Ideal R} {f f' : R[X]}

theorem map (hf : f.IsWeaklyEisensteinAt ğ“Ÿ) {A : Type v} [CommSemiring A] (Ï† : R â†’+* A) :
    (f.map Ï†).IsWeaklyEisensteinAt (ğ“Ÿ.map Ï†) := by
  refine (isWeaklyEisensteinAt_iff _ _).2 fun hn => ?_
  rw [coeff_map]
  exact mem_map_of_mem _ (hf.mem (lt_of_lt_of_le hn natDegree_map_le))

theorem mul (hf : f.IsWeaklyEisensteinAt ğ“Ÿ) (hf' : f'.IsWeaklyEisensteinAt ğ“Ÿ) :
    (f * f').IsWeaklyEisensteinAt ğ“Ÿ := by
  rw [isWeaklyEisensteinAt_iff] at hf hf' âŠ¢
  intro n hn
  rw [coeff_mul]
  refine sum_mem _ fun x hx â†¦ ?_
  rcases lt_or_ge x.1 f.natDegree with hx1 | hx1
  Â· exact mul_mem_right _ _ (hf hx1)
  replace hx1 : x.2 < f'.natDegree := by
    by_contra!
    rw [HasAntidiagonal.mem_antidiagonal] at hx
    replace hn := hn.trans_le natDegree_mul_le
    linarith
  exact mul_mem_left _ _ (hf' hx1)

end CommSemiring

section CommRing

variable [CommRing R] {ğ“Ÿ : Ideal R} {f : R[X]}
variable {S : Type v} [CommRing S] [Algebra R S]

section Principal

variable {p : R}

theorem exists_mem_adjoin_mul_eq_pow_natDegree {x : S} (hx : aeval x f = 0) (hmo : f.Monic)
    (hf : f.IsWeaklyEisensteinAt (Submodule.span R {p})) : âˆƒ y âˆˆ adjoin R ({x} : Set S),
    (algebraMap R S) p * y = x ^ (f.map (algebraMap R S)).natDegree := by
  rw [aeval_def, Polynomial.evalâ‚‚_eq_eval_map, eval_eq_sum_range, range_add_one,
    sum_insert notMem_range_self, sum_range, (hmo.map (algebraMap R S)).coeff_natDegree,
    one_mul] at hx
  replace hx := eq_neg_of_add_eq_zero_left hx
  have : âˆ€ n < f.natDegree, p âˆ£ f.coeff n := by
    intro n hn
    exact mem_span_singleton.1 (by simpa using hf.mem hn)
  choose! Ï† hÏ† using this
  conv_rhs at hx =>
    congr
    congr
    Â· skip
    ext i
    rw [coeff_map, hÏ† i.1 (lt_of_lt_of_le i.2 natDegree_map_le),
      RingHom.map_mul, mul_assoc]
  rw [hx, â† mul_sum, neg_eq_neg_one_mul, â† mul_assoc (-1 : S), mul_comm (-1 : S), mul_assoc]
  refine
    âŸ¨-1 * âˆ‘ i : Fin (f.map (algebraMap R S)).natDegree, (algebraMap R S) (Ï† i.1) * x ^ i.1, ?_, rflâŸ©
  exact
    Subalgebra.mul_mem _ (Subalgebra.neg_mem _ (Subalgebra.one_mem _))
      (Subalgebra.sum_mem _ fun i _ =>
        Subalgebra.mul_mem _ (Subalgebra.algebraMap_mem _ _)
          (Subalgebra.pow_mem _ (subset_adjoin (Set.mem_singleton x)) _))

theorem exists_mem_adjoin_mul_eq_pow_natDegree_le {x : S} (hx : aeval x f = 0) (hmo : f.Monic)
    (hf : f.IsWeaklyEisensteinAt (Submodule.span R {p})) :
    âˆ€ i, (f.map (algebraMap R S)).natDegree â‰¤ i â†’
        âˆƒ y âˆˆ adjoin R ({x} : Set S), (algebraMap R S) p * y = x ^ i := by
  intro i hi
  obtain âŸ¨k, hkâŸ© := exists_add_of_le hi
  rw [hk, pow_add]
  obtain âŸ¨y, hy, HâŸ© := exists_mem_adjoin_mul_eq_pow_natDegree hx hmo hf
  refine âŸ¨y * x ^ k, ?_, ?_âŸ©
  Â· exact Subalgebra.mul_mem _ hy (Subalgebra.pow_mem _ (subset_adjoin (Set.mem_singleton x)) _)
  Â· rw [â† mul_assoc _ y, H]

end Principal

theorem pow_natDegree_le_of_root_of_monic_mem (hf : f.IsWeaklyEisensteinAt ğ“Ÿ)
    {x : R} (hroot : IsRoot f x) (hmo : f.Monic) :
    âˆ€ i, f.natDegree â‰¤ i â†’ x ^ i âˆˆ ğ“Ÿ := by
  intro i hi
  obtain âŸ¨k, hkâŸ© := exists_add_of_le hi
  rw [hk, pow_add]
  suffices x ^ f.natDegree âˆˆ ğ“Ÿ by exact mul_mem_right (x ^ k) ğ“Ÿ this
  rw [IsRoot.def, eval_eq_sum_range, Finset.range_add_one,
    Finset.sum_insert Finset.notMem_range_self, Finset.sum_range, hmo.coeff_natDegree, one_mul] at
    *
  rw [eq_neg_of_add_eq_zero_left hroot, neg_mem_iff]
  exact Submodule.sum_mem _ fun i _ => mul_mem_right _ _ (hf.mem (Fin.is_lt i))

theorem pow_natDegree_le_of_aeval_zero_of_monic_mem_map (hf : f.IsWeaklyEisensteinAt ğ“Ÿ)
    {x : S} (hx : aeval x f = 0) (hmo : f.Monic) :
    âˆ€ i, (f.map (algebraMap R S)).natDegree â‰¤ i â†’ x ^ i âˆˆ ğ“Ÿ.map (algebraMap R S) := by
  suffices x ^ (f.map (algebraMap R S)).natDegree âˆˆ ğ“Ÿ.map (algebraMap R S) by
    intro i hi
    obtain âŸ¨k, hkâŸ© := exists_add_of_le hi
    rw [hk, pow_add]
    exact mul_mem_right _ _ this
  rw [aeval_def, evalâ‚‚_eq_eval_map, â† IsRoot.def] at hx
  exact pow_natDegree_le_of_root_of_monic_mem (hf.map _) hx (hmo.map _) _ rfl.le

end CommRing

end IsWeaklyEisensteinAt

section ScaleRoots

variable {A : Type*} [CommRing R] [CommRing A]

theorem scaleRoots.isWeaklyEisensteinAt (p : R[X]) {x : R} {P : Ideal R} (hP : x âˆˆ P) :
    (scaleRoots p x).IsWeaklyEisensteinAt P := by
  refine âŸ¨fun i => ?_âŸ©
  rw [coeff_scaleRoots]
  rw [natDegree_scaleRoots, â† tsub_pos_iff_lt] at i
  exact Ideal.mul_mem_left _ _ (Ideal.pow_mem_of_mem P hP _ i)

theorem dvd_pow_natDegree_of_evalâ‚‚_eq_zero {f : R â†’+* A} (hf : Function.Injective f) {p : R[X]}
    (hp : p.Monic) (x y : R) (z : A) (h : p.evalâ‚‚ f z = 0) (hz : f x * z = f y) :
    x âˆ£ y ^ p.natDegree := by
  rw [â† natDegree_scaleRoots p x, â† Ideal.mem_span_singleton]
  refine
    (scaleRoots.isWeaklyEisensteinAt _
          (Ideal.mem_span_singleton.mpr <| dvd_refl x)).pow_natDegree_le_of_root_of_monic_mem
      ?_ ((monic_scaleRoots_iff x).mpr hp) _ le_rfl
  rw [injective_iff_map_eq_zero'] at hf
  have : evalâ‚‚ f _ (p.scaleRoots x) = 0 := scaleRoots_evalâ‚‚_eq_zero f h
  rwa [hz, Polynomial.evalâ‚‚_at_apply, hf] at this

theorem dvd_pow_natDegree_of_aeval_eq_zero [Algebra R A] [Nontrivial A] [NoZeroSMulDivisors R A]
    {p : R[X]} (hp : p.Monic) (x y : R) (z : A) (h : Polynomial.aeval z p = 0)
    (hz : z * algebraMap R A x = algebraMap R A y) : x âˆ£ y ^ p.natDegree :=
  dvd_pow_natDegree_of_evalâ‚‚_eq_zero (FaithfulSMul.algebraMap_injective R A) hp x y z h
    ((mul_comm _ _).trans hz)

end ScaleRoots

namespace IsEisensteinAt

@[deprecated (since := "2025-05-23")] alias not_mem := notMem

section CommSemiring

variable [CommSemiring R] {ğ“Ÿ : Ideal R} {f : R[X]}

theorem _root_.Polynomial.Monic.leadingCoeff_notMem (hf : f.Monic) (h : ğ“Ÿ â‰  âŠ¤) :
    f.leadingCoeff âˆ‰ ğ“Ÿ := hf.leadingCoeff.symm â–¸ (Ideal.ne_top_iff_one _).1 h

@[deprecated (since := "2025-05-23")]
alias _root_.Polynomial.Monic.leadingCoeff_not_mem := _root_.Polynomial.Monic.leadingCoeff_notMem

theorem _root_.Polynomial.Monic.isEisensteinAt_of_mem_of_notMem (hf : f.Monic) (h : ğ“Ÿ â‰  âŠ¤)
    (hmem : âˆ€ {n}, n < f.natDegree â†’ f.coeff n âˆˆ ğ“Ÿ) (hnotMem : f.coeff 0 âˆ‰ ğ“Ÿ ^ 2) :
    f.IsEisensteinAt ğ“Ÿ :=
  { leading := Polynomial.Monic.leadingCoeff_notMem hf h
    mem := fun hn => hmem hn
    notMem := hnotMem }

@[deprecated (since := "2025-05-23")]
alias _root_.Polynomial.Monic.isEisensteinAt_of_mem_of_not_mem :=
  _root_.Polynomial.Monic.isEisensteinAt_of_mem_of_notMem

theorem isWeaklyEisensteinAt (hf : f.IsEisensteinAt ğ“Ÿ) : IsWeaklyEisensteinAt f ğ“Ÿ :=
  âŸ¨fun h => hf.mem hâŸ©

theorem coeff_mem (hf : f.IsEisensteinAt ğ“Ÿ) {n : â„•} (hn : n â‰  f.natDegree) : f.coeff n âˆˆ ğ“Ÿ := by
  rcases ne_iff_lt_or_gt.1 hn with hâ‚ | hâ‚‚
  Â· exact hf.mem hâ‚
  Â· rw [coeff_eq_zero_of_natDegree_lt hâ‚‚]
    exact Ideal.zero_mem _

end CommSemiring

section IsDomain

variable [CommRing R] [IsDomain R] {ğ“Ÿ : Ideal R} {f : R[X]}

/-- If a primitive `f` satisfies `f.IsEisensteinAt ğ“Ÿ`, where `ğ“Ÿ.IsPrime`,
then `f` is irreducible. -/
theorem irreducible (hf : f.IsEisensteinAt ğ“Ÿ) (hprime : ğ“Ÿ.IsPrime) (hu : f.IsPrimitive)
    (hfd0 : 0 < f.natDegree) : Irreducible f :=
  irreducible_of_eisenstein_criterion hprime hf.leading (fun _ hn => hf.mem (coe_lt_degree.1 hn))
    (natDegree_pos_iff_degree_pos.1 hfd0) hf.notMem hu

end IsDomain

end IsEisensteinAt

end Polynomial
