/-
Copyright (c) 2026 Thomas Browning. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Thomas Browning
-/
module

public import Mathlib.Algebra.Polynomial.Splits
public import Mathlib.GroupTheory.Perm.ClosureSwap
public import Mathlib.RingTheory.Ideal.Quotient.Operations
public import Mathlib.RingTheory.Spectrum.Maximal.Defs

/-!
# Galois Groups of Morse Polynomials

This file proves that Morse polynomials have Galois group `S_n`. A Morse polynomial is a
polynomial whose roots have at most one collision modulo each maximal ideal.

## Main results

- `Polynomial.Splits.toPermHom_surjective_of_supr_inertia_eq_top`: If the roots of `f` in `S` have
at most one collision modulo each maximal ideal `m`, and if a group `G` acting transitively on the
roots in `S` is generated by inertia subgroups, then `G` surjects onto the symmetric group `S_n`.

## TODO

- Specialize to the case of number fields where generation by inertia subgroups is a consequence
of Minkowski's theorem.
- Show that the Selmer polynomials `X ^ n - X - 1` have Galois group `S_n`.

## References

* [J. P. Serre, *Topics in Galois Theory*][serre-galois], section 4.4

-/

public section

namespace Polynomial

variable {R S : Type*} [CommRing R] [CommRing S] [Algebra R S] [IsDomain S]
  {G : Type*} [Group G] [MulSemiringAction G S] [SMulCommClass G R S] {f : R[X]}

/-- If the roots of `f` in `S` have at most one collision mod `p`, then a `MulSemiringAction` on
the roots in `S` must be the identity permutation or a transposition. -/
theorem Splits.isSwap_toPermHom_apply_of_mem_inertia [DecidableEq (f.rootSet S)]
    (hf : (f.map (algebraMap R S)).Splits)
    (p : Ideal S) [p.IsPrime] (hp : (f.rootSet S).ncard ≤ (f.rootSet (S ⧸ p)).ncard + 1)
    (g : G) (hg : g ∈ p.toAddSubgroup.inertia G) :
    MulAction.toPermHom G (f.rootSet S) g = 1 ∨ (MulAction.toPermHom G (f.rootSet S) g).IsSwap := by
  classical
  by_cases hfp : f.map (algebraMap R (S ⧸ p)) = 0
  · rw [rootSet_def f (S ⧸ p), aroots_def, hfp, roots_zero, Multiset.toFinset_zero,
      Finset.coe_empty, Set.ncard_empty, zero_add, Set.ncard_le_one_iff_subsingleton,
      ← Set.subsingleton_coe] at hp
    exact Or.inl (Subsingleton.elim _ _)
  let π : S →ₐ[R] S ⧸ p := Ideal.Quotient.mkₐ R p
  rw [← hf.image_rootSet_of_map_ne_zero π hfp, Set.ncard_le_ncard_image_add_one_iff] at hp
  have hπ (x : S) : π (g • x) = π x := (Ideal.Quotient.mk_eq_mk_iff_sub_mem (g • x) x).mpr (hg x)
  rw [or_iff_not_imp_left, Equiv.ext_iff, not_forall]
  rintro ⟨x, hx : g • x ≠ x⟩
  refine ⟨g • x, x, hx, ?_⟩
  ext z
  simp only [Equiv.swap_apply_def, MulAction.toPermHom_apply, MulAction.toPerm_apply]
  split_ifs with hz hz'
  · subst hz
    have key := hp (g • g • x) (g • g • x).2 (g • x) (g • x).2 (g • x) (g • x).2 x x.2
      (by simp [hπ]) (by simp [hπ]) (by simpa [← rootSet.coe_smul]) (by simpa [← rootSet.coe_smul])
    grind [rootSet.coe_smul]
  · simp [hz']
  · have key := hp (g • z) (g • z).2 z z.2 (g • x) (g • x).2 x x.2 (by simp [hπ]) (by simp [hπ])
    grind [rootSet.coe_smul, SetLike.coe_eq_coe]

/-- If the roots of `f` in `S` have at most one collision modulo each maximal ideal `m`, and if a
group `G` acting transitively on the roots in `S` is generated by inertia subgroups, then `G`
surjects onto the symmetric group `S_n`. -/
theorem Splits.toPermHom_surjective_of_supr_inertia_eq_top
    (hf : (f.map (algebraMap R S)).Splits) [MulAction.IsPretransitive G (f.rootSet S)]
    (h : ∀ m : MaximalSpectrum S, (f.rootSet S).ncard ≤ (f.rootSet (S ⧸ m.asIdeal)).ncard + 1)
    (hG : ⨆ m : MaximalSpectrum S, m.asIdeal.toAddSubgroup.inertia G = ⊤) :
    Function.Surjective (MulAction.toPermHom G (f.rootSet S)) := by
  classical
  apply surjective_of_isSwap_of_isPretransitive'
      (⋃ m : MaximalSpectrum S, m.asIdeal.toAddSubgroup.inertia G)
  · intro σ hσ
    obtain ⟨m, hm⟩ := Set.mem_iUnion.mp hσ
    exact hf.isSwap_toPermHom_apply_of_mem_inertia m.asIdeal (h m) σ hm
  · simpa [Subgroup.closure_iUnion]

end Polynomial
