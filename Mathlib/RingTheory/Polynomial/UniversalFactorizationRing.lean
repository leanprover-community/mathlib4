/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
module

public import Mathlib.RingTheory.Extension.Presentation.Submersive
public import Mathlib.RingTheory.FiniteStability
public import Mathlib.RingTheory.Flat.FaithfullyFlat.Basic
public import Mathlib.RingTheory.Polynomial.IsIntegral
public import Mathlib.RingTheory.Polynomial.Resultant.Basic
public import Mathlib.RingTheory.Smooth.StandardSmoothCotangent
public import Mathlib.RingTheory.LocalRing.ResidueField.Ideal


/-!

# Universal factorization ring

Let `R` be a commutative ring and `p : R[X]` be monic of degree `n` and let `n = m + k`.
We construct the universal ring of the following functors on `R-Alg`:
- `S ‚Ü¶ "monic polynomials over S of degree n"`:
  Represented by `R[X‚ÇÅ,...,X‚Çô]`. See `MvPolynomial.mapEquivMonic`.
- `S ‚Ü¶ "factorizations of p into (monic deg m) * (monic deg k) in S"`:
  Represented by an `R`-algebra (`Polynomial.UniversalFactorizationRing`) that is finitely-presented
  as an `R`-module. See `Polynomial.UniversalFactorizationRing.homEquiv`.
- `S ‚Ü¶ "factorizations of p into coprime (monic deg m) * (monic deg k) in S"`:
  Represented by an etale `R`-algebra (`Polynomial.UniversalCoprimeFactorizationRing`).
  See `Polynomial.UniversalCoprimeFactorizationRing.homEquiv`.

-/

@[expose] public section

open scoped Polynomial TensorProduct

open RingHomClass (toRingHom)

variable (R S T : Type*) [CommRing R] [CommRing S] [CommRing T] [Algebra R S] [Algebra R T]
variable (n m k : ‚Ñï) (hn : n = m + k)

noncomputable section

namespace Polynomial

/-- The free monic polynomial of degree `n`, as a polynomial in `R[X‚ÇÅ,...,X‚Çô][X]`. -/
def freeMonic : (MvPolynomial (Fin n) R)[X] :=
  .X ^ n + ‚àë i : Fin n, .C (.X i) * .X ^ (i : ‚Ñï)

lemma coeff_freeMonic :
    (freeMonic R n).coeff k = if h : k < n then .X ‚ü®k, h‚ü© else if k = n then 1 else 0 := by
  simp only [freeMonic, Polynomial.coeff_add, Polynomial.coeff_X_pow, Polynomial.finset_sum_coeff,
    Polynomial.coeff_C_mul, mul_ite, mul_one, mul_zero]
  by_cases h : k < n
  ¬∑ simp +contextual [Finset.sum_eq_single (Œπ := Fin n) (a := ‚ü®k, h‚ü©),
      Fin.ext_iff, @eq_comm _ k, h, h.ne']
  ¬∑ rw [Finset.sum_eq_zero fun x _ ‚Ü¶ if_neg (by cases x; lia), add_zero, dif_neg h]

lemma degree_freeMonic [Nontrivial R] : (freeMonic R n).degree = n :=
  Polynomial.degree_eq_of_le_of_coeff_ne_zero ((Polynomial.degree_le_iff_coeff_zero _ _).mpr
    (by simp +contextual [coeff_freeMonic, LT.lt.not_gt, LT.lt.ne']))
    (by simp [coeff_freeMonic])

lemma natDegree_freeMonic [Nontrivial R] : (freeMonic R n).natDegree = n :=
  natDegree_eq_of_degree_eq_some (degree_freeMonic R n)

lemma monic_freeMonic : (freeMonic R n).Monic := by
  nontriviality R
  simp [Polynomial.Monic, ‚Üê Polynomial.coeff_natDegree, natDegree_freeMonic, coeff_freeMonic]

omit [Algebra R S] in
lemma map_map_freeMonic (f : R ‚Üí+* S) :
    (freeMonic R n).map (MvPolynomial.map f) = freeMonic S n := by
  simp [freeMonic, Polynomial.map_sum]

open Polynomial (MonicDegreeEq)

/-- The free monic polynomial of degree `n`, as a `MonicDegreeEq` in `R[X‚ÇÅ,...,X‚Çô][X]`. -/
@[simps]
def MonicDegreeEq.freeMonic : MonicDegreeEq (MvPolynomial (Fin n) R) n :=
  ‚ü®.freeMonic R n, by simp +contextual [coeff_freeMonic, not_lt_of_gt, LT.lt.ne']‚ü©

end Polynomial

namespace MvPolynomial

open Polynomial

/-- `MonicDegreeEq ¬∑ n` is representable by `R[X‚ÇÅ,...,X‚Çô]`,
with the universal element being `freeMonic`. -/
def mapEquivMonic : (MvPolynomial (Fin n) R ‚Üí‚Çê[R] S) ‚âÉ MonicDegreeEq S n where
  toFun f := .map (.freeMonic _ _) f.toRingHom
  invFun p := aeval (p.1.coeff ¬∑)
  left_inv f := by ext i; simp [coeff_freeMonic]
  right_inv p := by
    suffices ‚àÄ i ‚â• n, (if i = n then 1 else 0) = p.1.coeff i by
      ext i; simp +contextual [coeff_freeMonic, apply_dite, this]
    intro i hi
    split_ifs with hi'
    ¬∑ simp [hi', p.2.1]
    ¬∑ simp [p.2.2 _ (hi.lt_of_ne' hi')]

variable {R S T} in
lemma coe_mapEquivMonic_comp (f : MvPolynomial (Fin n) R ‚Üí‚Çê[R] S) (g : S ‚Üí‚Çê[R] T) :
    (mapEquivMonic R T n (g.comp f)).1 = (mapEquivMonic R S n f).1.map g :=
  (Polynomial.map_map ..).symm

variable {R S T} in
lemma coe_mapEquivMonic_comp' (f : MvPolynomial (Fin n) R ‚Üí‚Çê[R] S) (g : S ‚Üí‚Çê[R] T) :
    mapEquivMonic R T n (g.comp f) = (mapEquivMonic R S n f).map g :=
  Subtype.ext (coe_mapEquivMonic_comp ..)

variable {R S T} in
lemma mapEquivMonic_symm_map (p : MonicDegreeEq S n) (g : S ‚Üí‚Çê[R] T) :
    (mapEquivMonic R T n).symm (p.map g) = g.comp ((mapEquivMonic R S n).symm p) := by
  obtain ‚ü®f, rfl‚ü© := (mapEquivMonic R S n).surjective p
  exact (mapEquivMonic R T n).symm_apply_eq.mpr (by simp [coe_mapEquivMonic_comp'])

variable {R S T} in
lemma mapEquivMonic_symm_map_algebraMap
    (p : MonicDegreeEq S n) [Algebra S T] [IsScalarTower R S T] :
    (mapEquivMonic R T n).symm (p.map (algebraMap S T)) =
      (IsScalarTower.toAlgHom R S T).comp ((mapEquivMonic R S n).symm p) := by
  rw [‚Üê mapEquivMonic_symm_map, IsScalarTower.coe_toAlgHom]

/-- In light of the fact that `MonicDegreeEq ¬∑ n` is representable by `R[X‚ÇÅ,...,X‚Çô]`,
this is the map `R[X‚ÇÅ,...,X‚Çò‚Çä‚Çñ] ‚Üí R[X‚ÇÅ,...,X‚Çò] ‚äó R[X‚ÇÅ,...,X‚Çñ]` corresponding to the multiplication
`MonicDegreeEq ¬∑ m √ó MonicDegreeEq ¬∑ k ‚Üí MonicDegreeEq ¬∑ (m + k)`. -/
def universalFactorizationMap (hn : n = m + k) :
    MvPolynomial (Fin n) R ‚Üí‚Çê[R] MvPolynomial (Fin m) R ‚äó[R] MvPolynomial (Fin k) R :=
  (mapEquivMonic R _ n).symm
  ‚ü®(mapEquivMonic R _ m Algebra.TensorProduct.includeLeft).1 *
    (mapEquivMonic R _ k Algebra.TensorProduct.includeRight).1, by
    nontriviality R
    nontriviality MvPolynomial (Fin m) R ‚äó[R] MvPolynomial (Fin k) R
    refine (MonicDegreeEq.mk _ ?_ ?_).2
    ¬∑ exact ((monic_freeMonic R m).map _).mul ((monic_freeMonic R _).map _)
    dsimp [mapEquivMonic]
    rw [((monic_freeMonic R m).map _).natDegree_mul ((monic_freeMonic R k).map _)]
    simp_rw [(monic_freeMonic R _).natDegree_map, natDegree_freeMonic, hn]‚ü©

lemma universalFactorizationMap_freeMonic :
    (freeMonic R n).map (toRingHom <| universalFactorizationMap R n m k hn) =
      (freeMonic R m).map (algebraMap _ _) *
        (freeMonic R k).map (toRingHom <| Algebra.TensorProduct.includeRight) := by
  change (mapEquivMonic _ _ _ (universalFactorizationMap R n m k hn)).1 = _
  simp [universalFactorizationMap]
  rfl

lemma universalFactorizationMap_comp_map :
    (universalFactorizationMap S n m k hn).toRingHom.comp (map (algebraMap R S)) =
    .comp (Algebra.TensorProduct.lift (S := R)
      (Algebra.TensorProduct.includeLeft.comp (mapAlgHom (Algebra.ofId R S)))
      ((Algebra.TensorProduct.includeRight.restrictScalars R).comp (mapAlgHom (Algebra.ofId R S)))
      fun _ _ ‚Ü¶ .all _ _).toRingHom
      (universalFactorizationMap R n m k hn).toRingHom := by
  ext
  ¬∑ simp
  ¬∑ dsimp [universalFactorizationMap, mapEquivMonic]
    simp only [map_X, aeval_X, ‚Üê AlgHom.coe_toRingHom, ‚Üê Polynomial.coeff_map, Polynomial.map_mul,
      Polynomial.map_map, ‚Üê map_map_freeMonic (f := algebraMap R S)]
    congr <;> ext <;> simp [‚Üê algebraMap_apply]

/-- Lifts along `universalFactorizationMap` corresponds to factorization of `p` into
monic polynomials with fixed degrees. -/
def universalFactorizationMapLiftEquiv (p : MonicDegreeEq S n) :
    { f // AlgHom.comp f (universalFactorizationMap R n m k hn) =
        (mapEquivMonic _ _ n).symm p } ‚âÉ
    { q : MonicDegreeEq S m √ó MonicDegreeEq S k // q.1.1 * q.2.1 = p } where
  toFun f := ‚ü®(mapEquivMonic R _ _ (f.1.comp Algebra.TensorProduct.includeLeft),
    mapEquivMonic R _ _ (f.1.comp Algebra.TensorProduct.includeRight)), by
      conv_rhs => rw [‚Üê (Equiv.eq_symm_apply _).mp f.2]
      simp [MvPolynomial.coe_mapEquivMonic_comp, MvPolynomial.universalFactorizationMap]‚ü©
  invFun q := ‚ü®Algebra.TensorProduct.lift ((mapEquivMonic _ _ _).symm q.1.1)
    ((mapEquivMonic _ _ _).symm q.1.2) fun _ _ ‚Ü¶ .all _ _, by
    refine (mapEquivMonic R S n).eq_symm_apply.mpr <| Subtype.ext ?_
    simp only [universalFactorizationMap, coe_mapEquivMonic_comp, Equiv.apply_symm_apply,
      Polynomial.map_mul]
    simp [‚Üê coe_mapEquivMonic_comp, ‚Üê q.2]‚ü©
  left_inv f := by ext <;> simp
  right_inv q := by ext <;> simp

lemma ker_eval‚ÇÇHom_universalFactorizationMap :
    RingHom.ker (eval‚ÇÇHom (S‚ÇÅ := MvPolynomial (Fin m) R ‚äó[R] MvPolynomial (Fin k) R)
      (universalFactorizationMap R n m k hn) (Sum.elim (.X ¬∑ ‚äó‚Çú 1) (1 ‚äó‚Çú .X ¬∑))) =
    Ideal.span (Set.range fun i ‚Ü¶ C (X i) - map C (tensorEquivSum _ _ _ _
      (universalFactorizationMap R n m k hn (X i)))) := by
  set f := eval‚ÇÇHom (R := MvPolynomial (Fin n) R)
    (S‚ÇÅ := MvPolynomial (Fin m) R ‚äó[R] MvPolynomial (Fin k) R)
    (universalFactorizationMap R n m k hn) (Sum.elim (.X ¬∑ ‚äó‚Çú 1) (1 ‚äó‚Çú .X ¬∑))
  have H (i : _) : tensorEquivSum _ _ _ _ (f (.X i)) = .X i := by aesop
  apply le_antisymm
  ¬∑ intro x hx
    convert_to x - (tensorEquivSum _ _ _ _ (f x)).map C ‚àà Ideal.span _ using 1
    ¬∑ simp_all only [RingHom.mem_ker, map_zero, sub_zero]
    clear hx
    induction x using MvPolynomial.induction_on with
    | add p q _ _ => simp only [map_add, add_sub_add_comm]; exact add_mem ‚Äπ_‚Ä∫ ‚Äπ_‚Ä∫
    | mul_X p i _ => simp only [map_mul, H, map_X, ‚Üê sub_mul]; exact Ideal.mul_mem_right _ _ ‚Äπ_‚Ä∫
    | C x =>
    induction x using MvPolynomial.induction_on with
    | C a => simp [f]
    | add p q _ _ => simp only [map_add, add_sub_add_comm]; exact add_mem ‚Äπ_‚Ä∫ ‚Äπ_‚Ä∫
    | mul_X p i IH =>
      simp only [map_mul]
      exact Ideal.mul_sub_mul_mem _ IH (Ideal.subset_span ‚ü®i, by simp [f]‚ü©)
  ¬∑ simp only [Ideal.span_le, Set.range_subset_iff, SetLike.mem_coe, RingHom.mem_ker, map_sub,
      eval‚ÇÇHom_C, RingHom.coe_coe, eval‚ÇÇHom_map_hom, coe_eval‚ÇÇHom, sub_eq_zero, f]
    simp only [‚Üê algebraMap_eq, AlgHom.comp_algebraMap_of_tower, ‚Üê aeval_def]
    intro i
    generalize universalFactorizationMap R n m k hn (X i) = p
    change AlgHom.id R _ p = ((aeval _).comp (tensorEquivSum R _ _ R).toAlgHom) p
    congr 1
    ext <;> simp

/-- The canonical presentation of `universalFactorizationMap`. -/
@[simps] def universalFactorizationMapPresentation :
    letI := (universalFactorizationMap R n m k hn).toAlgebra
    Algebra.PreSubmersivePresentation (MvPolynomial (Fin n) R)
      (MvPolynomial (Fin m) R ‚äó[R] MvPolynomial (Fin k) R) (Fin m ‚äï Fin k) (Fin n) :=
  letI := (universalFactorizationMap R n m k hn).toAlgebra
  { val := Sum.elim (.X ¬∑ ‚äó‚Çú 1) (1 ‚äó‚Çú .X ¬∑)
    œÉ' f := (tensorEquivSum _ _ _ _ f).map C
    aeval_val_œÉ' s := by
      change ((aeval _).restrictScalars R |>.comp (mapAlgHom (Algebra.ofId _ _)) |>.comp
          (tensorEquivSum R (Fin m) (Fin k) R).toAlgHom) s = AlgHom.id R _ s
      congr 1
      ext <;> simp
    algebra := (aeval _).toAlgebra
    algebraMap_eq := rfl
    relation i := .C (.X i) - (tensorEquivSum R (Fin m) (Fin k) R
      (universalFactorizationMap R n m k hn (.X i))).map C
    span_range_relation_eq_ker := by
      exact (ker_eval‚ÇÇHom_universalFactorizationMap R n m k hn).symm,
    map := finSumFinEquiv.symm ‚àò finCongr hn
    map_inj := finSumFinEquiv.symm.injective.comp (finCongr hn).injective }

lemma pderiv_inl_universalFactorizationMap_X (i j) :
    pderiv (Sum.inl i) (tensorEquivSum R (Fin m) (Fin k) R
      (universalFactorizationMap R n m k hn (X j))) =
    if ‚Üëj < (i : ‚Ñï) then 0 else if h : ‚Üëj - ‚Üëi < k then X (.inr ‚ü®‚Üëj - ‚Üëi, h‚ü©)
      else if ‚Üëj - ‚Üëi = k then 1 else 0 := by
  classical
  trans ‚àë x ‚àà Finset.antidiagonal ‚Üëj,
    if h : x.2 < k then if x.1 < m ‚àß x.1 = ‚Üëi then X (Sum.inr ‚ü®x.2, h‚ü©) else 0
    else if x.2 = k ‚àß x.1 < m ‚àß x.1 = ‚Üëi then 1 else 0
  ¬∑ simp [universalFactorizationMap, mapEquivMonic, Polynomial.coeff_mul, coeff_freeMonic,
      apply_dite, apply_ite, ‚Üê Algebra.TensorProduct.one_def,
      Pi.single_apply, Fin.ext_iff, ‚Üê ite_and]
  ¬∑ obtain h | h := lt_or_ge j.1 i.1
    ¬∑ rw [Finset.sum_eq_zero, if_pos h]
      simp only [Finset.mem_antidiagonal, Prod.forall]
      intro a b hab
      simp [show a ‚â† i by lia]
    rw [Finset.sum_eq_single ‚ü®i.1, j.1 - i.1‚ü©, if_neg h.not_gt]
    ¬∑ simp
    ¬∑ simp only [Finset.mem_antidiagonal, ne_eq, Prod.forall, Prod.mk.injEq, not_and]
      intro a b e h
      simp [show a ‚â† i by lia]
    ¬∑ simp [h]

lemma pderiv_inr_universalFactorizationMap_X (i j) :
    pderiv (Sum.inr i) (tensorEquivSum R (Fin m) (Fin k) R
      (universalFactorizationMap R n m k hn (X j))) =
    if ‚Üëj < (i : ‚Ñï) then 0 else if h : ‚Üëj - ‚Üëi < m then
      X (.inl ‚ü®‚Üëj - ‚Üëi, h‚ü©) else if ‚Üëj - ‚Üëi = m then 1 else 0 := by
  classical
  trans ‚àë x ‚àà Finset.antidiagonal ‚Üëj, if x.2 < k then if h : x.1 < m then if x.2 = ‚Üëi then
    X (Sum.inl ‚ü®x.1, h‚ü©) else 0 else if x.1 = m ‚àß x.2 = ‚Üëi then 1 else 0 else 0
  ¬∑ simp [universalFactorizationMap, mapEquivMonic, Polynomial.coeff_mul, coeff_freeMonic,
      apply_dite, apply_ite, ‚Üê Algebra.TensorProduct.one_def,
      Pi.single_apply, Fin.ext_iff, ‚Üê ite_and]
  ¬∑ obtain h | h := lt_or_ge j.1 i.1
    ¬∑ rw [Finset.sum_eq_zero, if_pos h]
      simp only [Finset.mem_antidiagonal, Prod.forall]
      intro a b hab
      simp [show b ‚â† i by lia]
    rw [Finset.sum_eq_single ‚ü®j.1 - i.1, i.1‚ü©, if_neg h.not_gt]
    ¬∑ simp
    ¬∑ simp only [Finset.mem_antidiagonal, ne_eq, ite_eq_right_iff, Prod.forall, Prod.mk.injEq]
      intro a b _ _ _
      simp [show b ‚â† i by lia]
    ¬∑ simp [h]

lemma universalFactorizationMapPresentation_jacobiMatrix :
    letI := (universalFactorizationMap R n m k hn).toAlgebra
    (universalFactorizationMapPresentation R n m k hn).jacobiMatrix =
    -((Polynomial.sylvester
      ((freeMonic R m).map (((mapAlgHom (Algebra.ofId _ _)).comp (rename Sum.inl)).toRingHom))
      ((freeMonic R k).map (((mapAlgHom (Algebra.ofId _ _)).comp (rename Sum.inr)).toRingHom))
      m k).reindex (finCongr (by lia)) (finCongr (by lia))).transpose := by
  letI := (universalFactorizationMap R n m k hn).toAlgebra
  subst hn
  ext i j : 1
  dsimp [Polynomial.sylvester]
  rw [Algebra.PreSubmersivePresentation.jacobiMatrix_apply]
  obtain ‚ü®i | i, rfl‚ü© := finSumFinEquiv.surjective i <;>
    induction j using Fin.addCases <;>
      simp [pderiv_map, coeff_freeMonic, apply_dite (DFunLike.coe _), apply_ite (DFunLike.coe _),
        pderiv_inl_universalFactorizationMap_X, pderiv_inr_universalFactorizationMap_X] <;> grind

lemma universalFactorizationMapPresentation_jacobian :
    letI := (universalFactorizationMap R n m k hn).toAlgebra
    (universalFactorizationMapPresentation R n m k hn).jacobian =
    (-1) ^ n * (Polynomial.resultant
      ((freeMonic R m).map Algebra.TensorProduct.includeLeftRingHom)
      ((freeMonic R k).map Algebra.TensorProduct.includeRight.toRingHom)) := by
  cases subsingleton_or_nontrivial R
  ¬∑ exact Subsingleton.elim _ _
  letI := (universalFactorizationMap R n m k hn).toAlgebra
  rw [Algebra.PreSubmersivePresentation.jacobian_eq_jacobiMatrix_det,
    MvPolynomial.universalFactorizationMapPresentation_jacobiMatrix]
  simp only [AlgHom.toRingHom_eq_coe, Matrix.det_neg, Matrix.det_transpose, Matrix.det_reindex_self,
    Algebra.Generators.algebraMap_apply, ‚Üê Polynomial.resultant.eq_def,
    Fintype.card_fin, map_mul, map_pow, map_neg, map_one]
  congr 1
  rw [‚Üê (aeval _).coe_toRingHom, ‚Üê Polynomial.resultant_map_map,
    Polynomial.map_map, Polynomial.map_map]
  congr 2
  ¬∑ ext <;> simp [-algebraMap_apply, ‚Üê algebraMap_eq]
  ¬∑ ext <;> simp [-algebraMap_apply, ‚Üê algebraMap_eq]
  ¬∑ rw [(monic_freeMonic ..).natDegree_map, natDegree_freeMonic]
  ¬∑ rw [(monic_freeMonic ..).natDegree_map, natDegree_freeMonic]

lemma finitePresentation_universalFactorizationMap :
    (universalFactorizationMap R n m k hn).FinitePresentation :=
  letI := (universalFactorizationMap R n m k hn).toAlgebra
  (universalFactorizationMapPresentation R n m k hn).finitePresentation_of_isFinite

lemma finite_universalFactorizationMap :
    (universalFactorizationMap R n m k hn).Finite := by
  refine RingHom.IsIntegral.to_finite ?_
    (.of_finitePresentation (finitePresentation_universalFactorizationMap R n m k hn))
  letI := (universalFactorizationMap R n m k hn).toAlgebra
  have : IsDomain (MvPolynomial (Fin m) ‚Ñ§ ‚äó[‚Ñ§] MvPolynomial (Fin k) ‚Ñ§) :=
    (MvPolynomial.tensorEquivSum ‚Ñ§ (Fin m) (Fin k) ‚Ñ§).toRingEquiv.isDomain_iff.mpr inferInstance
  letI := (universalFactorizationMap ‚Ñ§ n m k hn).toAlgebra
  let F : MvPolynomial (Fin m) ‚Ñ§ ‚äó[‚Ñ§] MvPolynomial (Fin k) ‚Ñ§ ‚Üí‚Çê[‚Ñ§]
      MvPolynomial (Fin m) R ‚äó[R] MvPolynomial (Fin k) R :=
    Algebra.TensorProduct.lift
      (Algebra.TensorProduct.includeLeft.comp (mapAlgHom (Algebra.ofId ‚Ñ§ R)))
      ((Algebra.TensorProduct.includeRight.restrictScalars ‚Ñ§).comp (mapAlgHom (Algebra.ofId ‚Ñ§ R)))
      fun _ _ ‚Ü¶ .all _ _
  have H‚ÇÅ (i : _) : (universalFactorizationMap R n m k hn).IsIntegralElem (.X i ‚äó‚Çú 1) := by
    obtain ‚ü®p, hp, hp'‚ü© : (universalFactorizationMap ‚Ñ§ n m k hn).IsIntegralElem (.X i ‚äó‚Çú 1) := by
      simpa [coeff_freeMonic] using Polynomial.isIntegral_coeff_of_dvd _ _ (monic_freeMonic _ _)
        ((monic_freeMonic _ _).map _) ‚ü®_, universalFactorizationMap_freeMonic ‚Ñ§ n m k hn‚ü© i
    refine ‚ü®p.map (MvPolynomial.map (algebraMap ‚Ñ§ R)), hp.map _, ?_‚ü©
    apply_fun F.toRingHom at hp'
    rw [Polynomial.hom_eval‚ÇÇ, ‚Üê MvPolynomial.universalFactorizationMap_comp_map] at hp'
    simpa [‚Üê Polynomial.eval‚ÇÇ_map, F] using hp'
  have H‚ÇÇ (i : _) : (universalFactorizationMap R n m k hn).IsIntegralElem (1 ‚äó‚Çú .X i) := by
    obtain ‚ü®p, hp, hp'‚ü© : (universalFactorizationMap ‚Ñ§ n m k hn).IsIntegralElem (1 ‚äó‚Çú .X i) := by
      simpa [coeff_freeMonic] using Polynomial.isIntegral_coeff_of_dvd _ _ (monic_freeMonic _ _)
        ((monic_freeMonic _ _).map _)
        ‚ü®_, (universalFactorizationMap_freeMonic ‚Ñ§ n m k hn).trans (mul_comm _ _)‚ü© i
    refine ‚ü®p.map (MvPolynomial.map (algebraMap ‚Ñ§ R)), hp.map _, ?_‚ü©
    apply_fun F.toRingHom at hp'
    rw [Polynomial.hom_eval‚ÇÇ, ‚Üê MvPolynomial.universalFactorizationMap_comp_map] at hp'
    simpa [‚Üê Polynomial.eval‚ÇÇ_map, F] using hp'
  intro x
  induction x with
  | zero => exact RingHom.isIntegralElem_zero _
  | add x y _ _ => exact RingHom.IsIntegralElem.add _ ‚Äπ_‚Ä∫ ‚Äπ_‚Ä∫
  | tmul x y =>
    suffices (universalFactorizationMap R n m k hn).IsIntegralElem (x ‚äó‚Çú 1 * 1 ‚äó‚Çú y) by simpa
    refine RingHom.IsIntegralElem.mul _ ?_ ?_
    ¬∑ induction x using MvPolynomial.induction_on with
      | C a => simpa using (universalFactorizationMap R n m k hn).isIntegralElem_map (x := .C a)
      | add p q _ _ => simp only [TensorProduct.add_tmul, RingHom.IsIntegralElem.add, *]
      | mul_X p i IH => simpa [‚Üê map_mul] using IH.mul _ (H‚ÇÅ i)
    ¬∑ induction y using MvPolynomial.induction_on with
      | C a => simpa [‚Üê algebraMap_eq, ‚Üê algebraMap_apply, Algebra.algebraMap_eq_smul_one] using
          (universalFactorizationMap R n m k hn).isIntegralElem_map (x := .C a)
      | add p q _ _ => simp only [TensorProduct.tmul_add, RingHom.IsIntegralElem.add, *]
      | mul_X p i IH => simpa [‚Üê map_mul] using IH.mul _ (H‚ÇÇ i)

end MvPolynomial

namespace Polynomial

open TensorProduct

variable {R n} (p : Polynomial.MonicDegreeEq R n)

attribute [-instance] leftModule in
/-- The universal factorization ring of a monic polynomial `p` of degree `n`.
This is the representing object of the functor
`S ‚Ü¶ "factorizations of p into (monic deg m) * (monic deg k) in S"`.
See `UniversalFactorizationRing.homEquiv`. -/
def UniversalFactorizationRing : Type _ :=
  letI := (MvPolynomial.universalFactorizationMap R n m k hn).toAlgebra
  letI := ((MvPolynomial.mapEquivMonic R _ n).symm p).toAlgebra
  R ‚äó[MvPolynomial (Fin n) R] (MvPolynomial (Fin m) R ‚äó[R] MvPolynomial (Fin k) R)
  deriving CommRing, Algebra R

local notation "ùì°" => UniversalFactorizationRing m k hn p

/-- The canonical map `R[X‚ÇÅ,...,X‚Çò] ‚äó R[X‚ÇÅ,...,X‚Çô] ‚Üí UniversalFactorizationRing`. -/
def UniversalFactorizationRing.fromTensor :
    (MvPolynomial (Fin m) R ‚äó[R] MvPolynomial (Fin k) R) ‚Üí‚Çê[R] ùì° :=
  letI := (MvPolynomial.universalFactorizationMap R n m k hn).toAlgebra
  letI := ((MvPolynomial.mapEquivMonic R _ n).symm p).toAlgebra
  Algebra.TensorProduct.includeRight.restrictScalars _

/-- The image of `p` in the universal factorization ring of `p`. -/
@[simps] def UniversalFactorizationRing.monicDegreeEq :
    MonicDegreeEq ùì° n :=
  ‚ü®p.1.map (algebraMap _ _), by simp +contextual only [Polynomial.coeff_map, p.2,
    map_one, map_zero, gt_iff_lt, implies_true, and_self]‚ü©

lemma UniversalFactorizationRing.fromTensor_comp_universalFactorizationMap :
  (fromTensor m k hn p).comp (MvPolynomial.universalFactorizationMap R n m k hn) =
    (Algebra.ofId R _).comp ((MvPolynomial.mapEquivMonic R _ n).symm p) := by
  letI := (MvPolynomial.universalFactorizationMap R n m k hn).toAlgebra
  letI := ((MvPolynomial.mapEquivMonic R _ n).symm p).toAlgebra
  exact AlgHom.ext fun x ‚Ü¶ (Algebra.TensorProduct.tmul_one_eq_one_tmul x).symm

lemma UniversalFactorizationRing.fromTensor_comp_universalFactorizationMap' :
  (fromTensor m k hn p).comp (MvPolynomial.universalFactorizationMap R n m k hn) =
    ((MvPolynomial.mapEquivMonic _ _ n).symm (monicDegreeEq m k hn p)) := by
  rw [UniversalFactorizationRing.fromTensor_comp_universalFactorizationMap, Equiv.eq_symm_apply]
  ext1
  simp [MvPolynomial.coe_mapEquivMonic_comp, monicDegreeEq]

/-- The first factor of `p` in the universal factorization ring of `p`. -/
def UniversalFactorizationRing.factor‚ÇÅ : MonicDegreeEq ùì° m :=
  (MvPolynomial.universalFactorizationMapLiftEquiv _ _ n m k hn _
    ‚ü®fromTensor m k hn p, fromTensor_comp_universalFactorizationMap' m k hn p‚ü©).1.1

/-- The second factor of `p` in the universal factorization ring of `p`. -/
def UniversalFactorizationRing.factor‚ÇÇ : MonicDegreeEq ùì° k :=
  (MvPolynomial.universalFactorizationMapLiftEquiv _ _ n m k hn _
    ‚ü®fromTensor m k hn p, fromTensor_comp_universalFactorizationMap' m k hn p‚ü©).1.2

lemma UniversalFactorizationRing.factor‚ÇÅ_mul_factor‚ÇÇ :
    (factor‚ÇÅ m k hn p).1 * (factor‚ÇÇ m k hn p).1 = (monicDegreeEq m k hn p).1 :=
  (MvPolynomial.universalFactorizationMapLiftEquiv _ _ n m k hn _
    ‚ü®fromTensor m k hn p, fromTensor_comp_universalFactorizationMap' m k hn p‚ü©).2

attribute [-instance] leftModule in
/-- The universal factorization ring represents
`S ‚Ü¶ "factorizations of p into (monic deg m) * (monic deg k) in S"`. -/
def UniversalFactorizationRing.homEquiv :
    (ùì° ‚Üí‚Çê[R] S) ‚âÉ { q : MonicDegreeEq S m √ó MonicDegreeEq S k //
      q.1.1 * q.2.1 = p.1.map (algebraMap R S) } where
  toFun f := ‚ü®((factor‚ÇÅ m k hn p).map f, (factor‚ÇÇ m k hn p).map f), by
    simp [‚Üê Polynomial.map_mul, factor‚ÇÅ_mul_factor‚ÇÇ m k hn p, Polynomial.map_map]‚ü©
  invFun q :=
    letI := (MvPolynomial.universalFactorizationMap R n m k hn).toAlgebra
    letI := ((MvPolynomial.mapEquivMonic R _ n).symm p).toAlgebra
    letI := Algebra.compHom S ((MvPolynomial.mapEquivMonic R _ n).symm p).toRingHom
    haveI : IsScalarTower (MvPolynomial (Fin n) R) R S := .of_algebraMap_eq' rfl
    letI f := ((MvPolynomial.universalFactorizationMapLiftEquiv R _ n m k hn
          (p.map (algebraMap R S))).symm q)
    Algebra.TensorProduct.lift (R := MvPolynomial (Fin n) R) (S := R) (A := R)
      (B := MvPolynomial (Fin m) R ‚äó[R] MvPolynomial (Fin k) R) (C := S) (Algebra.ofId R S)
      { toRingHom := f.1.toRingHom
        commutes' r := congr($(f.2) r).trans
          (by simp [MvPolynomial.mapEquivMonic_symm_map_algebraMap]; rfl) } fun _ _ ‚Ü¶ .all _ _
  left_inv f := by
    letI := (MvPolynomial.universalFactorizationMap R n m k hn).toAlgebra
    letI := ((MvPolynomial.mapEquivMonic R _ n).symm p).toAlgebra
    letI := Algebra.compHom S ((MvPolynomial.mapEquivMonic R _ n).symm p).toRingHom
    haveI : IsScalarTower (MvPolynomial (Fin n) R) R S := .of_algebraMap_eq' rfl
    have : IsScalarTower R (MvPolynomial (Fin n) R) S := .of_algebraMap_eq fun r ‚Ü¶ by
      simp [Algebra.compHom_algebraMap_apply]
    refine Algebra.TensorProduct.ext (by ext) ?_
    refine AlgHom.restrictScalars_injective R (Algebra.TensorProduct.ext ?_ ?_)
    ¬∑ ext; simp [MvPolynomial.universalFactorizationMapLiftEquiv, MvPolynomial.mapEquivMonic,
        UniversalFactorizationRing.factor‚ÇÅ, coeff_freeMonic]; rfl
    ¬∑ ext; simp [MvPolynomial.universalFactorizationMapLiftEquiv, MvPolynomial.mapEquivMonic,
        UniversalFactorizationRing.factor‚ÇÇ, coeff_freeMonic]; rfl
  right_inv q := by
    letI := (MvPolynomial.universalFactorizationMap R n m k hn).toAlgebra
    letI := ((MvPolynomial.mapEquivMonic R _ n).symm p).toAlgebra
    simp only [UniversalFactorizationRing, MvPolynomial.mapEquivMonic, AlgHom.toRingHom_eq_coe,
      Equiv.coe_fn_symm_mk, MvPolynomial.coe_aeval_eq_eval, factor‚ÇÅ, monicDegreeEq_coe,
      MvPolynomial.universalFactorizationMapLiftEquiv, Equiv.coe_fn_mk, fromTensor,
      MonicDegreeEq.map_coe, factor‚ÇÇ]
    ext <;> simp +contextual [coeff_freeMonic, apply_dite, MonicDegreeEq.coeff_of_ge]

attribute [-instance] leftModule in
instance : Module.Finite R ùì° :=
  letI := (MvPolynomial.universalFactorizationMap R n m k hn).toAlgebra
  letI := ((MvPolynomial.mapEquivMonic R _ n).symm p).toAlgebra
  letI : Module.Finite _ _ := MvPolynomial.finite_universalFactorizationMap R n m k hn
  inferInstanceAs (Module.Finite R (R ‚äó[_] _))

attribute [-instance] leftModule in
instance : Algebra.FinitePresentation R ùì° :=
  letI := (MvPolynomial.universalFactorizationMap R n m k hn).toAlgebra
  letI := ((MvPolynomial.mapEquivMonic R _ n).symm p).toAlgebra
  letI : Algebra.FinitePresentation _ _ :=
    MvPolynomial.finitePresentation_universalFactorizationMap R n m k hn
  inferInstanceAs (Algebra.FinitePresentation R (R ‚äó[_] _))

/-- The presentation of `UniversalFactorizationRing`.
Its jacobian is the resultant of the two factors (up to sign). -/
def UniversalFactorizationRing.presentation :
    Algebra.PreSubmersivePresentation R ùì° (Fin m ‚äï Fin k) (Fin n) :=
  letI := (MvPolynomial.universalFactorizationMap R n m k hn).toAlgebra
  letI := ((MvPolynomial.mapEquivMonic R _ n).symm p).toAlgebra
  (MvPolynomial.universalFactorizationMapPresentation R n m k hn).baseChange _

lemma UniversalFactorizationRing.jacobian_resentation :
    (presentation m k hn p).jacobian =
      (-1) ^ n * (factor‚ÇÅ m k hn p).1.resultant (factor‚ÇÇ m k hn p).1 := by
  cases subsingleton_or_nontrivial ùì°
  ¬∑ exact Subsingleton.elim _ _
  cases subsingleton_or_nontrivial ((MvPolynomial (Fin m) R ‚äó[R] MvPolynomial (Fin k) R))
  ¬∑ dsimp [UniversalFactorizationRing]; exact Subsingleton.elim _ _
  cases subsingleton_or_nontrivial R
  ¬∑ dsimp [UniversalFactorizationRing]; exact Subsingleton.elim _ _
  letI := (MvPolynomial.universalFactorizationMap R n m k hn).toAlgebra
  letI := ((MvPolynomial.mapEquivMonic R _ n).symm p).toAlgebra
  refine (Algebra.PreSubmersivePresentation.baseChange_jacobian _ _).trans ?_
  change fromTensor _ _ _ _ _ = _
  rw [MvPolynomial.universalFactorizationMapPresentation_jacobian]
  rw [map_mul, map_pow, map_neg, map_one, ‚Üê AlgHom.coe_toRingHom, ‚Üê Polynomial.resultant_map_map,
    Polynomial.map_map, Polynomial.map_map, (monic_freeMonic R k).natDegree_map,
    (monic_freeMonic R m).natDegree_map, MonicDegreeEq.natDegree,
    MonicDegreeEq.natDegree, natDegree_freeMonic, natDegree_freeMonic]
  rfl

open UniversalFactorizationRing in
/-- The universal coprime factorization ring of a monic polynomial `p` of degree `n`.
This is the representing object of the functor
`S ‚Ü¶ "factorizations of p into coprime (monic deg m) * (monic deg k) in S"`.
See `UniversalCoprimeFactorizationRing.homEquiv`. -/
abbrev UniversalCoprimeFactorizationRing : Type _ :=
  Localization.Away (M := ùì°) (presentation m k hn p).jacobian

local notation "ùì°'" => UniversalCoprimeFactorizationRing m k hn p

/-- The first factor of `p` in the universal coprime factorization ring of `p`. -/
def UniversalCoprimeFactorizationRing.factor‚ÇÅ : MonicDegreeEq ùì°' m :=
  (UniversalFactorizationRing.factor‚ÇÅ m k hn p).map (algebraMap _ _)

/-- The second factor of `p` in the universal coprime factorization ring of `p`. -/
def UniversalCoprimeFactorizationRing.factor‚ÇÇ : MonicDegreeEq ùì°' k :=
  (UniversalFactorizationRing.factor‚ÇÇ m k hn p).map (algebraMap _ _)

lemma UniversalCoprimeFactorizationRing.factor‚ÇÅ_mul_factor‚ÇÇ :
    (factor‚ÇÅ m k hn p).1 * (factor‚ÇÇ m k hn p).1 = p.map (algebraMap R ùì°') := by
  simp [factor‚ÇÅ, factor‚ÇÇ, ‚Üê Polynomial.map_mul, UniversalFactorizationRing.factor‚ÇÅ_mul_factor‚ÇÇ,
    Polynomial.map_map, ‚Üê IsScalarTower.algebraMap_eq]

lemma UniversalCoprimeFactorizationRing.isCoprime_factor‚ÇÅ_factor‚ÇÇ :
    IsCoprime (factor‚ÇÅ m k hn p).1 (factor‚ÇÇ m k hn p).1 := by
  cases subsingleton_or_nontrivial ùì°'
  ¬∑ rw [Subsingleton.elim (Subtype.val _) 1]; exact isCoprime_one_left
  rw [‚Üê Polynomial.isUnit_resultant_iff_isCoprime (factor‚ÇÅ m k hn p).monic, factor‚ÇÅ,
    factor‚ÇÇ, MonicDegreeEq.map_coe, MonicDegreeEq.map_coe, Polynomial.resultant_map_map,
    (UniversalFactorizationRing.factor‚ÇÅ m k hn p).monic.natDegree_map,
    (UniversalFactorizationRing.factor‚ÇÇ m k hn p).monic.natDegree_map]
  refine ((IsUnit.mul_iff (x := algebraMap ùì° ùì°' ((-1) ^ n))).mp ?_).2
  rw [‚Üê map_mul, ‚Üê UniversalFactorizationRing.jacobian_resentation m k hn p]
  exact IsLocalization.Away.algebraMap_isUnit _

open UniversalFactorizationRing in
instance : Algebra.Etale R ùì°' := by
  let Œî : ùì° := (presentation m k hn p).jacobian
  have hŒî : IsUnit (algebraMap ùì° (Localization.Away Œî) Œî) :=
    IsLocalization.Away.algebraMap_isUnit _
  let P : Algebra.SubmersivePresentation R (Localization.Away Œî) _ _ :=
    { toPreSubmersivePresentation :=
        .comp (.localizationAway (Localization.Away Œî) Œî) (presentation m k hn p),
      jacobian_isUnit := by simpa [Algebra.smul_def, -isUnit_map_iff, hŒî] }
  have : Algebra.IsStandardSmoothOfRelativeDimension 0 R (Localization.Away Œî) :=
    ‚ü®_, _, _, inferInstance, P, by
      simp only [Algebra.PreSubmersivePresentation.dimension_comp_eq_dimension_add_dimension, P]
      simp [Algebra.Presentation.dimension, hn]‚ü©
  infer_instance

/-- The universal factorization ring represents
`S ‚Ü¶ "factorizations of p into coprime (monic deg m) * (monic deg k) in S"`. -/
def UniversalCoprimeFactorizationRing.homEquiv :
    (ùì°' ‚Üí‚Çê[R] S) ‚âÉ { q : MonicDegreeEq S m √ó MonicDegreeEq S k //
      q.1.1 * q.2.1 = p.1.map (algebraMap R S) ‚àß IsCoprime q.1.1 q.2.1 } where
  toFun f :=
    letI q := UniversalFactorizationRing.homEquiv S m k hn p (f.comp (IsScalarTower.toAlgHom _ _ _))
    ‚ü®q.1, q.2, by
      convert (isCoprime_factor‚ÇÅ_factor‚ÇÇ m k hn p).map (Polynomial.mapRingHom f.toRingHom) <;>
        simp [q, UniversalFactorizationRing.homEquiv,
          AlgHom.comp_toRingHom, ‚Üê Polynomial.map_map] <;> rfl‚ü©
  invFun q := by
    letI f := (UniversalFactorizationRing.homEquiv S m k hn p).symm ‚ü®q.1, q.2.1‚ü©
    refine IsLocalization.liftAlgHom (f := f)
      (M := .powers (UniversalFactorizationRing.presentation m k hn p).jacobian) ?_
    nontriviality S
    rw [Subtype.forall]
    change Submonoid.powers _ ‚â§ (IsUnit.submonoid _).comap f
    simp only [Submonoid.powers_le, Submonoid.mem_comap, IsUnit.mem_submonoid_iff]
    rw [‚Üê AlgHom.coe_toRingHom, UniversalFactorizationRing.jacobian_resentation, map_mul,
      ‚Üê Polynomial.resultant_map_map, IsUnit.mul_iff]
    refine ‚ü®by cases n <;> simp, ?_‚ü©
    rw [‚Üê (UniversalFactorizationRing.factor‚ÇÅ m k hn p).monic.natDegree_map f.toRingHom,
      ‚Üê (UniversalFactorizationRing.factor‚ÇÇ m k hn p).monic.natDegree_map f.toRingHom,
      AlgHom.toRingHom_eq_coe, Polynomial.isUnit_resultant_iff_isCoprime
        ((UniversalFactorizationRing.factor‚ÇÅ m k hn p).monic.map _)]
    change IsCoprime (UniversalFactorizationRing.homEquiv S m k hn p f).1.1.1
      (UniversalFactorizationRing.homEquiv S m k hn p f).1.2.1
    simpa [f] using q.2.2
  left_inv f := by
    apply IsLocalization.algHom_ext
      (.powers (UniversalFactorizationRing.presentation m k hn p).jacobian)
    ext; simp
  right_inv q := by
    apply Subtype.ext
    convert congr($((UniversalFactorizationRing.homEquiv S m k hn p).apply_symm_apply
      ‚ü®_, q.2.1‚ü©).1) using 1
    dsimp
    congr 2
    ext
    simp

lemma UniversalCoprimeFactorizationRing.homEquiv_comp_fst {T : Type*} [CommRing T] [Algebra R T]
    (f : ùì°' ‚Üí‚Çê[R] S) (g : S ‚Üí‚Çê[R] T) :
    (homEquiv T m k hn p (g.comp f)).1.1 = (homEquiv S m k hn p f).1.1.map g := by
  ext1
  simp [homEquiv, UniversalFactorizationRing.homEquiv, Polynomial.map_map]
  rfl

lemma UniversalCoprimeFactorizationRing.homEquiv_comp_snd {T : Type*} [CommRing T] [Algebra R T]
    (f : ùì°' ‚Üí‚Çê[R] S) (g : S ‚Üí‚Çê[R] T) :
    (homEquiv T m k hn p (g.comp f)).1.2 = (homEquiv S m k hn p f).1.2.map g := by
  ext1
  simp [homEquiv, UniversalFactorizationRing.homEquiv, Polynomial.map_map]
  rfl

/-- If a monic polynomial `p : R[X]` factors into a product of coprime monic polynomials `p = f * g`
in the residue field `Œ∫(P)` of some `P : Spec R`,
then there exists `Q : Spec R_univ` in the universal coprime factorization ring lying over `P`,
such that `Œ∫(P) = Œ∫(Q)` and `f` and `g` are the image of the universal factors. -/
lemma UniversalCoprimeFactorizationRing.exists_liesOver_residueFieldMap_bijective
    (P : Ideal R) [P.IsPrime]
    (f : MonicDegreeEq P.ResidueField m) (g : MonicDegreeEq P.ResidueField k)
    (H : p.1.map (algebraMap R _) = f.1 * g.1) (Hpq : IsCoprime f.1 g.1) :
    ‚àÉ (Q : Ideal ùì°') (_ : Q.IsPrime) (_ : Q.LiesOver P),
    Function.Bijective (Ideal.ResidueField.map‚Çê P Q (Algebra.ofId _ _) (Ideal.over_def Q P)) ‚àß
    f.map (Ideal.ResidueField.map‚Çê P Q (Algebra.ofId _ _) (Ideal.over_def Q P)).toRingHom =
      (factor‚ÇÅ m k hn p).map (algebraMap _ _) ‚àß
    g.map (Ideal.ResidueField.map‚Çê P Q (Algebra.ofId _ _) (Ideal.over_def Q P)).toRingHom =
      (factor‚ÇÇ m k hn p).map (algebraMap _ _) := by
  let œÜ : ùì°' ‚Üí‚Çê[R] P.ResidueField :=
    (UniversalCoprimeFactorizationRing.homEquiv _ m k hn p).symm ‚ü®(f, g), H.symm, Hpq‚ü©
  let Q := RingHom.ker œÜ.toRingHom
  have : Q.IsPrime := RingHom.ker_isPrime _
  have : Q.LiesOver P := ‚ü®by rw [Ideal.under, RingHom.comap_ker, AlgHom.toRingHom_eq_coe,
      œÜ.comp_algebraMap, Ideal.ker_algebraMap_residueField]‚ü©
  let œÜ' : Q.ResidueField ‚Üí‚Çê[R] P.ResidueField := Ideal.ResidueField.lift‚Çê _ œÜ le_rfl (by
    simp [SetLike.le_def, IsUnit.mem_submonoid_iff, Q])
  let œÜi : P.ResidueField ‚Üí‚Çê[R] Q.ResidueField :=
    Ideal.ResidueField.map‚Çê _ _ (Algebra.ofId _ _) (Ideal.over_def _ _)
  let e : P.ResidueField ‚âÉ‚Çê[R] Q.ResidueField :=
    .ofAlgHom œÜi œÜ' (AlgHom.ext fun x ‚Ü¶ œÜ'.injective <|
      show (œÜ'.comp œÜi) (œÜ' x) = AlgHom.id R _ (œÜ' x) by congr; ext) (by ext)
  have H : œÜi.comp œÜ = (IsScalarTower.toAlgHom _ _ _) :=
    AlgHom.ext fun x ‚Ü¶ e.eq_symm_apply.mp (by simp [e, œÜ'])
  refine ‚ü®Q, ‚Äπ_‚Ä∫, ‚Äπ_‚Ä∫, e.bijective, ?_, ?_‚ü©
  ¬∑ trans ((homEquiv Q.ResidueField m k hn p) (œÜi.comp œÜ)).1.1
    ¬∑ simp [homEquiv_comp_fst, œÜ, œÜi]
    ¬∑ rw [H]
      simp [homEquiv, UniversalFactorizationRing.homEquiv, factor‚ÇÅ,
        MonicDegreeEq.map, Polynomial.map_map]
      rfl
  ¬∑ trans ((homEquiv Q.ResidueField m k hn p) (œÜi.comp œÜ)).1.2
    ¬∑ simp [homEquiv_comp_snd, œÜ, œÜi]
    ¬∑ rw [H]
      simp [homEquiv, UniversalFactorizationRing.homEquiv, factor‚ÇÇ,
        MonicDegreeEq.map, Polynomial.map_map]
      rfl

open UniversalCoprimeFactorizationRing in
/-- If a monic polynomial `p : R[X]` factors into a product of coprime monic polynomials `p = f * g`
in the residue field `Œ∫(P)` of some `P : Spec R`,
then there exists an etale algebra `R'` of `R` and a prime `Q` of `R'` lying over `P`,
such that `Œ∫(P) = Œ∫(Q)` and that the factorization lifts to `R'`. -/
@[stacks 00UH]
lemma _root_.Algebra.exists_etale_bijective_residueFieldMap_and_map_eq_mul_and_isCoprime.{u}
    {R : Type u} [CommRing R]
    (P : Ideal R) [P.IsPrime] (p : R[X])
    (f g : P.ResidueField[X]) (hp : p.Monic) (hf : f.Monic) (hg : g.Monic)
    (H : p.map (algebraMap R _) = f * g) (Hpq : IsCoprime f g) :
    ‚àÉ (R' : Type u) (_ : CommRing R') (_ : Algebra R R') (_ : Algebra.Etale R R')
      (Q : Ideal R') (_ : Q.IsPrime) (_ : Q.LiesOver P) (f' g' : R'[X]),
    Function.Bijective (Ideal.ResidueField.map‚Çê P Q (Algebra.ofId _ _) (Ideal.over_def Q P)) ‚àß
    f'.Monic ‚àß g'.Monic ‚àß p.map (algebraMap R R') = f' * g' ‚àß IsCoprime f' g' ‚àß
    f.map (Ideal.ResidueField.map‚Çê P Q (Algebra.ofId _ _) (Ideal.over_def Q P)).toRingHom =
      f'.map (algebraMap _ _) ‚àß
    g.map (Ideal.ResidueField.map‚Çê P Q (Algebra.ofId _ _) (Ideal.over_def Q P)).toRingHom =
      g'.map (algebraMap _ _) := by
  obtain ‚ü®Q, _, _, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ‚ü© :=
    exists_liesOver_residueFieldMap_bijective f.natDegree g.natDegree
    (by simpa [hf.natDegree_mul hg, hp.natDegree_map] using congr(($H).natDegree)) (.mk p hp rfl)
    P (.mk f hf rfl) (.mk g hg rfl) H Hpq
  exact ‚ü®_, _, _, inferInstance, Q, ‚Äπ_‚Ä∫, ‚Äπ_‚Ä∫, (factor‚ÇÅ ..).1, (factor‚ÇÇ ..).1, h‚ÇÅ,
    (factor‚ÇÅ ..).monic, (factor‚ÇÇ ..).monic, (factor‚ÇÅ_mul_factor‚ÇÇ ..).symm,
    isCoprime_factor‚ÇÅ_factor‚ÇÇ .., congr(($h‚ÇÇ).1), congr(($h‚ÇÉ).1)‚ü©

end Polynomial
