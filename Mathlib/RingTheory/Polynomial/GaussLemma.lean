/-
Copyright (c) 2020 Aaron Anderson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Aaron Anderson
-/
import Mathlib.FieldTheory.SplittingField.Construction
import Mathlib.RingTheory.Int.Basic
import Mathlib.RingTheory.Localization.Integral
import Mathlib.RingTheory.IntegrallyClosed

#align_import ring_theory.polynomial.gauss_lemma from "leanprover-community/mathlib"@"e3f4be1fcb5376c4948d7f095bec45350bfb9d1a"

/-!
# Gauss's Lemma

Gauss's Lemma is one of a few results pertaining to irreducibility of primitive polynomials.

## Main Results
 - `IsIntegrallyClosed.eq_map_mul_C_of_dvd`: if `R` is integrally closed, `K = Frac(R)` and
  `g : K[X]` divides a monic polynomial with coefficients in `R`, then `g * (C g.leadingCoeff‚Åª¬π)`
  has coefficients in `R`
 - `Polynomial.Monic.irreducible_iff_irreducible_map_fraction_map`:
  A monic polynomial over an integrally closed domain is irreducible iff it is irreducible in a
    fraction field
 - `isIntegrallyClosed_iff'`:
   Integrally closed domains are precisely the domains for in which Gauss's lemma holds
    for monic polynomials
 - `Polynomial.IsPrimitive.irreducible_iff_irreducible_map_fraction_map`:
  A primitive polynomial over a GCD domain is irreducible iff it is irreducible in a fraction field
 - `Polynomial.IsPrimitive.Int.irreducible_iff_irreducible_map_cast`:
  A primitive polynomial over `‚Ñ§` is irreducible iff it is irreducible over `‚Ñö`.
 - `Polynomial.IsPrimitive.dvd_iff_fraction_map_dvd_fraction_map`:
  Two primitive polynomials over a GCD domain divide each other iff they do in a fraction field.
 - `Polynomial.IsPrimitive.Int.dvd_iff_map_cast_dvd_map_cast`:
  Two primitive polynomials over `‚Ñ§` divide each other if they do in `‚Ñö`.

-/


open scoped nonZeroDivisors Polynomial

variable {R : Type*} [CommRing R]

section IsIntegrallyClosed

open Polynomial

open integralClosure

open IsIntegrallyClosed

variable (K : Type*) [Field K] [Algebra R K]

theorem integralClosure.mem_lifts_of_monic_of_dvd_map {f : R[X]} (hf : f.Monic) {g : K[X]}
    (hg : g.Monic) (hd : g ‚à£ f.map (algebraMap R K)) :
    g ‚àà lifts (algebraMap (integralClosure R K) K) := by
  have := mem_lift_of_splits_of_roots_mem_range (integralClosure R g.SplittingField)
    ((splits_id_iff_splits _).2 <| SplittingField.splits g) (hg.map _) fun a ha =>
      (SetLike.ext_iff.mp (integralClosure R g.SplittingField).range_algebraMap _).mpr <|
        roots_mem_integralClosure hf ?_
  ¬∑ rw [lifts_iff_coeff_lifts, ‚Üê RingHom.coe_range, Subalgebra.range_algebraMap] at this
    -- ‚ä¢ g ‚àà lifts (algebraMap { x // x ‚àà integralClosure R K } K)
    refine' (lifts_iff_coeff_lifts _).2 fun n => _
    -- ‚ä¢ coeff g n ‚àà Set.range ‚Üë(algebraMap { x // x ‚àà integralClosure R K } K)
    rw [‚Üê RingHom.coe_range, Subalgebra.range_algebraMap]
    -- ‚ä¢ coeff g n ‚àà ‚Üë(Subalgebra.toSubring (integralClosure R K))
    obtain ‚ü®p, hp, he‚ü© := SetLike.mem_coe.mp (this n); use p, hp
    -- ‚ä¢ coeff g n ‚àà ‚Üë(Subalgebra.toSubring (integralClosure R K))
                                                       -- ‚ä¢ eval‚ÇÇ (algebraMap R K) (coeff g n) p = 0
    rw [IsScalarTower.algebraMap_eq R K, coeff_map, ‚Üê eval‚ÇÇ_map, eval‚ÇÇ_at_apply] at he
    -- ‚ä¢ eval‚ÇÇ (algebraMap R K) (coeff g n) p = 0
    rw [eval‚ÇÇ_eq_eval_map]; apply (injective_iff_map_eq_zero _).1 _ _ he
    -- ‚ä¢ eval (coeff g n) (map (algebraMap R K) p) = 0
                            -- ‚ä¢ Function.Injective ‚Üë(algebraMap K (SplittingField g))
    ¬∑ apply RingHom.injective
      -- üéâ no goals
  rw [IsScalarTower.algebraMap_eq R K _, ‚Üê map_map]
  -- ‚ä¢ a ‚àà roots (map (algebraMap K (SplittingField g)) (map (algebraMap R K) f))
  refine' Multiset.mem_of_le (roots.le_of_dvd ((hf.map _).map _).ne_zero _) ha
  -- ‚ä¢ map (algebraMap K (SplittingField g)) g ‚à£ map (algebraMap K (SplittingField  ‚Ä¶
  exact map_dvd (algebraMap K g.SplittingField) hd
  -- üéâ no goals
#align integral_closure.mem_lifts_of_monic_of_dvd_map integralClosure.mem_lifts_of_monic_of_dvd_map

variable [IsDomain R] [IsFractionRing R K]

/-- If `K = Frac(R)` and `g : K[X]` divides a monic polynomial with coefficients in `R`, then
    `g * (C g.leadingCoeff‚Åª¬π)` has coefficients in `R` -/
theorem IsIntegrallyClosed.eq_map_mul_C_of_dvd [IsIntegrallyClosed R] {f : R[X]} (hf : f.Monic)
    {g : K[X]} (hg : g ‚à£ f.map (algebraMap R K)) :
    ‚àÉ g' : R[X], g'.map (algebraMap R K) * (C <| leadingCoeff g) = g := by
  have g_ne_0 : g ‚â† 0 := ne_zero_of_dvd_ne_zero (Monic.ne_zero <| hf.map (algebraMap R K)) hg
  -- ‚ä¢ ‚àÉ g', map (algebraMap R K) g' * ‚ÜëC (leadingCoeff g) = g
  suffices lem : ‚àÉ g' : R[X], g'.map (algebraMap R K) = g * C g.leadingCoeff‚Åª¬π
  -- ‚ä¢ ‚àÉ g', map (algebraMap R K) g' * ‚ÜëC (leadingCoeff g) = g
  ¬∑ obtain ‚ü®g', hg'‚ü© := lem
    -- ‚ä¢ ‚àÉ g', map (algebraMap R K) g' * ‚ÜëC (leadingCoeff g) = g
    use g'
    -- ‚ä¢ map (algebraMap R K) g' * ‚ÜëC (leadingCoeff g) = g
    rw [hg', mul_assoc, ‚Üê C_mul, inv_mul_cancel (leadingCoeff_ne_zero.mpr g_ne_0), C_1, mul_one]
    -- üéâ no goals
  have g_mul_dvd : g * C g.leadingCoeff‚Åª¬π ‚à£ f.map (algebraMap R K) := by
    rwa [Associated.dvd_iff_dvd_left (show Associated (g * C g.leadingCoeff‚Åª¬π) g from _)]
    rw [associated_mul_isUnit_left_iff]
    exact isUnit_C.mpr (inv_ne_zero <| leadingCoeff_ne_zero.mpr g_ne_0).isUnit
  let algeq :=
    (Subalgebra.equivOfEq _ _ <| integralClosure_eq_bot R _).trans
      (Algebra.botEquivOfInjective <| IsFractionRing.injective R <| K)
  have :
    (algebraMap R _).comp algeq.toAlgHom.toRingHom = (integralClosure R _).toSubring.subtype :=
    by ext x; conv_rhs => rw [‚Üê algeq.symm_apply_apply x]; rfl
  have H :=
    (mem_lifts _).1
      (integralClosure.mem_lifts_of_monic_of_dvd_map K hf (monic_mul_leadingCoeff_inv g_ne_0)
        g_mul_dvd)
  refine' ‚ü®map algeq.toAlgHom.toRingHom _, _‚ü©
  -- ‚ä¢ { x // x ‚àà integralClosure R K }[X]
  ¬∑ use! Classical.choose H
    -- üéâ no goals
  ¬∑ rw [map_map, this]
    -- ‚ä¢ map (Subring.subtype (Subalgebra.toSubring (integralClosure R K))) (Classica ‚Ä¶
    exact Classical.choose_spec H
    -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align is_integrally_closed.eq_map_mul_C_of_dvd IsIntegrallyClosed.eq_map_mul_C_of_dvd

end IsIntegrallyClosed

namespace Polynomial

section

variable {S : Type*} [CommRing S] [IsDomain S]

variable {œÜ : R ‚Üí+* S} (hinj : Function.Injective œÜ) {f : R[X]} (hf : f.IsPrimitive)

theorem IsPrimitive.isUnit_iff_isUnit_map_of_injective : IsUnit f ‚Üî IsUnit (map œÜ f) := by
  refine' ‚ü®(mapRingHom œÜ).isUnit_map, fun h => _‚ü©
  -- ‚ä¢ IsUnit f
  rcases isUnit_iff.1 h with ‚ü®_, ‚ü®u, rfl‚ü©, hu‚ü©
  -- ‚ä¢ IsUnit f
  have hdeg := degree_C u.ne_zero
  -- ‚ä¢ IsUnit f
  rw [hu, degree_map_eq_of_injective hinj] at hdeg
  -- ‚ä¢ IsUnit f
  rw [eq_C_of_degree_eq_zero hdeg] at hf ‚ä¢
  -- ‚ä¢ IsUnit (‚ÜëC (coeff f 0))
  exact isUnit_C.mpr (isPrimitive_iff_isUnit_of_C_dvd.mp hf (f.coeff 0) dvd_rfl)
  -- üéâ no goals
#align polynomial.is_primitive.is_unit_iff_is_unit_map_of_injective Polynomial.IsPrimitive.isUnit_iff_isUnit_map_of_injective

theorem IsPrimitive.irreducible_of_irreducible_map_of_injective (h_irr : Irreducible (map œÜ f)) :
    Irreducible f := by
  refine'
    ‚ü®fun h => h_irr.not_unit (IsUnit.map (mapRingHom œÜ) h), fun a b h =>
      (h_irr.isUnit_or_isUnit <| by rw [h, Polynomial.map_mul]).imp _ _‚ü©
  all_goals apply ((isPrimitive_of_dvd hf _).isUnit_iff_isUnit_map_of_injective hinj).mpr
  -- ‚ä¢ a ‚à£ f
  exacts [Dvd.intro _ h.symm, Dvd.intro_left _ h.symm]
  -- üéâ no goals
#align polynomial.is_primitive.irreducible_of_irreducible_map_of_injective Polynomial.IsPrimitive.irreducible_of_irreducible_map_of_injective

end

section FractionMap

variable {K : Type*} [Field K] [Algebra R K] [IsFractionRing R K]

theorem IsPrimitive.isUnit_iff_isUnit_map {p : R[X]} (hp : p.IsPrimitive) :
    IsUnit p ‚Üî IsUnit (p.map (algebraMap R K)) :=
  hp.isUnit_iff_isUnit_map_of_injective (IsFractionRing.injective _ _)
#align polynomial.is_primitive.is_unit_iff_is_unit_map Polynomial.IsPrimitive.isUnit_iff_isUnit_map

variable [IsDomain R]

section IsIntegrallyClosed

open IsIntegrallyClosed

/-- **Gauss's Lemma** for integrally closed domains states that a monic polynomial is irreducible
  iff it is irreducible in the fraction field. -/
theorem Monic.irreducible_iff_irreducible_map_fraction_map [IsIntegrallyClosed R] {p : R[X]}
    (h : p.Monic) : Irreducible p ‚Üî Irreducible (p.map <| algebraMap R K) := by
  /- The ‚Üê direction follows from `IsPrimitive.irreducible_of_irreducible_map_of_injective`.
       For the ‚Üí direction, it is enought to show that if `(p.map $ algebraMap R K) = a * b` and
       `a` is not a unit then `b` is a unit -/
  refine'
    ‚ü®fun hp =>
      irreducible_iff.mpr
        ‚ü®hp.not_unit.imp h.isPrimitive.isUnit_iff_isUnit_map.mpr, fun a b H =>
          or_iff_not_imp_left.mpr fun h‚Çê => _‚ü©,
      fun hp =>
      h.isPrimitive.irreducible_of_irreducible_map_of_injective (IsFractionRing.injective R K) hp‚ü©
  obtain ‚ü®a', ha‚ü© := eq_map_mul_C_of_dvd K h (dvd_of_mul_right_eq b H.symm)
  -- ‚ä¢ IsUnit b
  obtain ‚ü®b', hb‚ü© := eq_map_mul_C_of_dvd K h (dvd_of_mul_left_eq a H.symm)
  -- ‚ä¢ IsUnit b
  have : a.leadingCoeff * b.leadingCoeff = 1 := by
    rw [‚Üê leadingCoeff_mul, ‚Üê H, Monic.leadingCoeff (h.map <| algebraMap R K)]
  rw [‚Üê ha, ‚Üê hb, mul_comm _ (C b.leadingCoeff), mul_assoc, ‚Üê mul_assoc (C a.leadingCoeff), ‚Üê
    C_mul, this, C_1, one_mul, ‚Üê Polynomial.map_mul] at H
  rw [‚Üê hb, ‚Üê Polynomial.coe_mapRingHom]
  -- ‚ä¢ IsUnit (‚Üë(mapRingHom (algebraMap R K)) b' * ‚ÜëC (Polynomial.leadingCoeff b))
  refine'
    IsUnit.mul (IsUnit.map _ (Or.resolve_left (hp.isUnit_or_isUnit _) (show ¬¨IsUnit a' from _)))
      (isUnit_iff_exists_inv'.mpr
        -- Porting note(https://github.com/leanprover-community/mathlib4/issues/5073): was `rwa`
        (Exists.intro (C a.leadingCoeff) <| by rw [‚Üê C_mul, this, C_1]))
  ¬∑ exact Polynomial.map_injective _ (IsFractionRing.injective R K) H
    -- üéâ no goals
  ¬∑ by_contra h_contra
    -- ‚ä¢ False
    refine' h‚Çê _
    -- ‚ä¢ IsUnit a
    rw [‚Üê ha, ‚Üê Polynomial.coe_mapRingHom]
    -- ‚ä¢ IsUnit (‚Üë(mapRingHom (algebraMap R K)) a' * ‚ÜëC (Polynomial.leadingCoeff a))
    exact
      IsUnit.mul (IsUnit.map _ h_contra)
        (isUnit_iff_exists_inv.mpr
          -- Porting note(https://github.com/leanprover-community/mathlib4/issues/5073): was `rwa`
          (Exists.intro (C b.leadingCoeff) <| by rw [‚Üê C_mul, this, C_1]))
#align polynomial.monic.irreducible_iff_irreducible_map_fraction_map Polynomial.Monic.irreducible_iff_irreducible_map_fraction_map

/-- Integrally closed domains are precisely the domains for in which Gauss's lemma holds
    for monic polynomials -/
theorem isIntegrallyClosed_iff' :
    IsIntegrallyClosed R ‚Üî
      ‚àÄ p : R[X], p.Monic ‚Üí (Irreducible p ‚Üî Irreducible (p.map <| algebraMap R K)) := by
  constructor
  -- ‚ä¢ IsIntegrallyClosed R ‚Üí ‚àÄ (p : R[X]), Monic p ‚Üí (Irreducible p ‚Üî Irreducible  ‚Ä¶
  ¬∑ intro hR p hp; exact Monic.irreducible_iff_irreducible_map_fraction_map hp
    -- ‚ä¢ Irreducible p ‚Üî Irreducible (map (algebraMap R K) p)
                   -- üéâ no goals
  ¬∑ intro H
    -- ‚ä¢ IsIntegrallyClosed R
    refine'
      (isIntegrallyClosed_iff K).mpr fun {x} hx =>
        RingHom.mem_range.mp <| minpoly.mem_range_of_degree_eq_one R x _
    rw [‚Üê Monic.degree_map (minpoly.monic hx) (algebraMap R K)]
    -- ‚ä¢ degree (map (algebraMap R K) (minpoly R x)) = 1
    apply
      degree_eq_one_of_irreducible_of_root ((H _ <| minpoly.monic hx).mp (minpoly.irreducible hx))
    rw [IsRoot, eval_map, ‚Üê aeval_def, minpoly.aeval R x]
    -- üéâ no goals
#align polynomial.is_integrally_closed_iff' Polynomial.isIntegrallyClosed_iff'

theorem Monic.dvd_of_fraction_map_dvd_fraction_map [IsIntegrallyClosed R] {p q : R[X]}
    (hp : p.Monic) (hq : q.Monic)
    (h : q.map (algebraMap R K) ‚à£ p.map (algebraMap R K)) : q ‚à£ p := by
  obtain ‚ü®r, hr‚ü© := h
  -- ‚ä¢ q ‚à£ p
  obtain ‚ü®d', hr'‚ü© := IsIntegrallyClosed.eq_map_mul_C_of_dvd K hp (dvd_of_mul_left_eq _ hr.symm)
  -- ‚ä¢ q ‚à£ p
  rw [Monic.leadingCoeff, C_1, mul_one] at hr'
  -- ‚ä¢ q ‚à£ p
  rw [‚Üê hr', ‚Üê Polynomial.map_mul] at hr
  -- ‚ä¢ q ‚à£ p
  exact dvd_of_mul_right_eq _ (Polynomial.map_injective _ (IsFractionRing.injective R K) hr.symm)
  -- ‚ä¢ Monic r
  ¬∑ exact Monic.of_mul_monic_left (hq.map (algebraMap R K)) (by simpa [‚Üê hr] using hp.map _)
    -- üéâ no goals
#align polynomial.monic.dvd_of_fraction_map_dvd_fraction_map Polynomial.Monic.dvd_of_fraction_map_dvd_fraction_map

theorem Monic.dvd_iff_fraction_map_dvd_fraction_map [IsIntegrallyClosed R] {p q : R[X]}
    (hp : p.Monic) (hq : q.Monic) : q.map (algebraMap R K) ‚à£ p.map (algebraMap R K) ‚Üî q ‚à£ p :=
  ‚ü®fun h => hp.dvd_of_fraction_map_dvd_fraction_map hq h, fun ‚ü®a, b‚ü© =>
    ‚ü®a.map (algebraMap R K), b.symm ‚ñ∏ Polynomial.map_mul (algebraMap R K)‚ü©‚ü©
#align polynomial.monic.dvd_iff_fraction_map_dvd_fraction_map Polynomial.Monic.dvd_iff_fraction_map_dvd_fraction_map

end IsIntegrallyClosed

open IsLocalization

section NormalizedGCDMonoid

variable [NormalizedGCDMonoid R]

theorem isUnit_or_eq_zero_of_isUnit_integerNormalization_primPart {p : K[X]} (h0 : p ‚â† 0)
    (h : IsUnit (integerNormalization R‚Å∞ p).primPart) : IsUnit p := by
  rcases isUnit_iff.1 h with ‚ü®_, ‚ü®u, rfl‚ü©, hu‚ü©
  -- ‚ä¢ IsUnit p
  obtain ‚ü®‚ü®c, c0‚ü©, hc‚ü© := integerNormalization_map_to_map R‚Å∞ p
  -- ‚ä¢ IsUnit p
  rw [Subtype.coe_mk, Algebra.smul_def, algebraMap_apply] at hc
  -- ‚ä¢ IsUnit p
  apply isUnit_of_mul_isUnit_right
  -- ‚ä¢ IsUnit (?intro.intro.intro.intro.mk.x * p)
  rw [‚Üê hc, (integerNormalization R‚Å∞ p).eq_C_content_mul_primPart, ‚Üê hu, ‚Üê RingHom.map_mul,
    isUnit_iff]
  refine'
    ‚ü®algebraMap R K ((integerNormalization R‚Å∞ p).content * ‚Üëu), isUnit_iff_ne_zero.2 fun con => _,
      by simp‚ü©
  replace con := (injective_iff_map_eq_zero (algebraMap R K)).1 (IsFractionRing.injective _ _) _ con
  -- ‚ä¢ False
  rw [mul_eq_zero, content_eq_zero_iff, IsFractionRing.integerNormalization_eq_zero_iff] at con
  -- ‚ä¢ False
  rcases con with (con | con)
  -- ‚ä¢ False
  ¬∑ apply h0 con
    -- üéâ no goals
  ¬∑ apply Units.ne_zero _ con
    -- üéâ no goals
#align polynomial.is_unit_or_eq_zero_of_is_unit_integer_normalization_prim_part Polynomial.isUnit_or_eq_zero_of_isUnit_integerNormalization_primPart

/-- **Gauss's Lemma** for GCD domains states that a primitive polynomial is irreducible iff it is
  irreducible in the fraction field. -/
theorem IsPrimitive.irreducible_iff_irreducible_map_fraction_map {p : R[X]} (hp : p.IsPrimitive) :
    Irreducible p ‚Üî Irreducible (p.map (algebraMap R K)) := by
  -- Porting note: was `(IsFractionRing.injective _ _)`
  refine'
    ‚ü®fun hi => ‚ü®fun h => hi.not_unit (hp.isUnit_iff_isUnit_map.2 h), fun a b hab => _‚ü©,
      hp.irreducible_of_irreducible_map_of_injective (IsFractionRing.injective R K)‚ü©
  obtain ‚ü®‚ü®c, c0‚ü©, hc‚ü© := integerNormalization_map_to_map R‚Å∞ a
  -- ‚ä¢ IsUnit a ‚à® IsUnit b
  obtain ‚ü®‚ü®d, d0‚ü©, hd‚ü© := integerNormalization_map_to_map R‚Å∞ b
  -- ‚ä¢ IsUnit a ‚à® IsUnit b
  rw [Algebra.smul_def, algebraMap_apply, Subtype.coe_mk] at hc hd
  -- ‚ä¢ IsUnit a ‚à® IsUnit b
  rw [mem_nonZeroDivisors_iff_ne_zero] at c0 d0
  -- ‚ä¢ IsUnit a ‚à® IsUnit b
  have hcd0 : c * d ‚â† 0 := mul_ne_zero c0 d0
  -- ‚ä¢ IsUnit a ‚à® IsUnit b
  rw [Ne.def, ‚Üê C_eq_zero] at hcd0
  -- ‚ä¢ IsUnit a ‚à® IsUnit b
  have h1 : C c * C d * p = integerNormalization R‚Å∞ a * integerNormalization R‚Å∞ b := by
    apply map_injective (algebraMap R K) (IsFractionRing.injective _ _) _
    rw [Polynomial.map_mul, Polynomial.map_mul, Polynomial.map_mul, hc, hd, map_C, map_C, hab]
    ring
  obtain ‚ü®u, hu‚ü© :
    Associated (c * d)
      (content (integerNormalization R‚Å∞ a) * content (integerNormalization R‚Å∞ b)) := by
    rw [‚Üê dvd_dvd_iff_associated, ‚Üê normalize_eq_normalize_iff, normalize.map_mul,
      normalize.map_mul, normalize_content, normalize_content, ‚Üê
      mul_one (normalize c * normalize d), ‚Üê hp.content_eq_one, ‚Üê content_C, ‚Üê content_C, ‚Üê
      content_mul, ‚Üê content_mul, ‚Üê content_mul, h1]
  rw [‚Üê RingHom.map_mul, eq_comm, (integerNormalization R‚Å∞ a).eq_C_content_mul_primPart,
    (integerNormalization R‚Å∞ b).eq_C_content_mul_primPart, mul_assoc, mul_comm _ (C _ * _), ‚Üê
    mul_assoc, ‚Üê mul_assoc, ‚Üê RingHom.map_mul, ‚Üê hu, RingHom.map_mul, mul_assoc, mul_assoc, ‚Üê
    mul_assoc (C (u : R))] at h1
  have h0 : a ‚â† 0 ‚àß b ‚â† 0 := by
    classical
    rw [Ne.def, Ne.def, ‚Üê not_or, ‚Üê mul_eq_zero, ‚Üê hab]
    intro con
    apply hp.ne_zero (map_injective (algebraMap R K) (IsFractionRing.injective _ _) _)
    simp [con]
  rcases hi.isUnit_or_isUnit (mul_left_cancel‚ÇÄ hcd0 h1).symm with (h | h)
  -- ‚ä¢ IsUnit a ‚à® IsUnit b
  ¬∑ right
    -- ‚ä¢ IsUnit b
    apply
      isUnit_or_eq_zero_of_isUnit_integerNormalization_primPart h0.2
        (isUnit_of_mul_isUnit_right h)
  ¬∑ left
    -- ‚ä¢ IsUnit a
    apply isUnit_or_eq_zero_of_isUnit_integerNormalization_primPart h0.1 h
    -- üéâ no goals
#align polynomial.is_primitive.irreducible_iff_irreducible_map_fraction_map Polynomial.IsPrimitive.irreducible_iff_irreducible_map_fraction_map

theorem IsPrimitive.dvd_of_fraction_map_dvd_fraction_map {p q : R[X]} (hp : p.IsPrimitive)
    (hq : q.IsPrimitive) (h_dvd : p.map (algebraMap R K) ‚à£ q.map (algebraMap R K)) : p ‚à£ q := by
  rcases h_dvd with ‚ü®r, hr‚ü©
  -- ‚ä¢ p ‚à£ q
  obtain ‚ü®‚ü®s, s0‚ü©, hs‚ü© := integerNormalization_map_to_map R‚Å∞ r
  -- ‚ä¢ p ‚à£ q
  rw [Subtype.coe_mk, Algebra.smul_def, algebraMap_apply] at hs
  -- ‚ä¢ p ‚à£ q
  have h : p ‚à£ q * C s := by
    use integerNormalization R‚Å∞ r
    apply map_injective (algebraMap R K) (IsFractionRing.injective _ _)
    rw [Polynomial.map_mul, Polynomial.map_mul, hs, hr, mul_assoc, mul_comm r]
    simp
  rw [‚Üê hp.dvd_primPart_iff_dvd, primPart_mul, hq.primPart_eq, Associated.dvd_iff_dvd_right] at h
  ¬∑ exact h
    -- üéâ no goals
  ¬∑ symm
    -- ‚ä¢ Associated q (q * primPart (‚ÜëC s))
    rcases isUnit_primPart_C s with ‚ü®u, hu‚ü©
    -- ‚ä¢ Associated q (q * primPart (‚ÜëC s))
    use u
    -- ‚ä¢ q * ‚Üëu = q * primPart (‚ÜëC s)
    rw [hu]
    -- üéâ no goals
  iterate 2
    apply mul_ne_zero hq.ne_zero
    rw [Ne.def, C_eq_zero]
    contrapose! s0
    simp [s0, mem_nonZeroDivisors_iff_ne_zero]
#align polynomial.is_primitive.dvd_of_fraction_map_dvd_fraction_map Polynomial.IsPrimitive.dvd_of_fraction_map_dvd_fraction_map

variable (K)

theorem IsPrimitive.dvd_iff_fraction_map_dvd_fraction_map {p q : R[X]} (hp : p.IsPrimitive)
    (hq : q.IsPrimitive) : p ‚à£ q ‚Üî p.map (algebraMap R K) ‚à£ q.map (algebraMap R K) :=
  ‚ü®fun ‚ü®a, b‚ü© => ‚ü®a.map (algebraMap R K), b.symm ‚ñ∏ Polynomial.map_mul (algebraMap R K)‚ü©, fun h =>
    hp.dvd_of_fraction_map_dvd_fraction_map hq h‚ü©
#align polynomial.is_primitive.dvd_iff_fraction_map_dvd_fraction_map Polynomial.IsPrimitive.dvd_iff_fraction_map_dvd_fraction_map

end NormalizedGCDMonoid

end FractionMap

/-- **Gauss's Lemma** for `‚Ñ§` states that a primitive integer polynomial is irreducible iff it is
  irreducible over `‚Ñö`. -/
theorem IsPrimitive.Int.irreducible_iff_irreducible_map_cast {p : ‚Ñ§[X]} (hp : p.IsPrimitive) :
    Irreducible p ‚Üî Irreducible (p.map (Int.castRingHom ‚Ñö)) :=
  hp.irreducible_iff_irreducible_map_fraction_map
#align polynomial.is_primitive.int.irreducible_iff_irreducible_map_cast Polynomial.IsPrimitive.Int.irreducible_iff_irreducible_map_cast

theorem IsPrimitive.Int.dvd_iff_map_cast_dvd_map_cast (p q : ‚Ñ§[X]) (hp : p.IsPrimitive)
    (hq : q.IsPrimitive) : p ‚à£ q ‚Üî p.map (Int.castRingHom ‚Ñö) ‚à£ q.map (Int.castRingHom ‚Ñö) :=
  hp.dvd_iff_fraction_map_dvd_fraction_map ‚Ñö hq
#align polynomial.is_primitive.int.dvd_iff_map_cast_dvd_map_cast Polynomial.IsPrimitive.Int.dvd_iff_map_cast_dvd_map_cast

end Polynomial
