/-
Copyright (c) 2022 Thomas Browning. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Thomas Browning
-/
import Mathlib.Analysis.Complex.Polynomial.UnitTrinomial
import Mathlib.FieldTheory.Finite.GaloisField
import Mathlib.FieldTheory.Galois.IsGaloisGroup
import Mathlib.FieldTheory.KrullTopology
import Mathlib.FieldTheory.Relrank
import Mathlib.GroupTheory.Perm.ClosureSwap
import Mathlib.NumberTheory.NumberField.Discriminant.Basic
import Mathlib.NumberTheory.NumberField.Discriminant.Different
import Mathlib.NumberTheory.RamificationInertia.Galois
import Mathlib.RingTheory.Ideal.Over
import Mathlib.RingTheory.IntegralClosure.IntegralRestrict
import Mathlib.RingTheory.Invariant.Basic

/-!
# Irreducibility and Galois Groups of Selmer Polynomials

This file shows that the Selmer polynomial `X ^ n - X - 1` is irreducible with Galois group `S_n`.

## Main results

- `X_pow_sub_X_sub_one_irreducible`: The Selmer polynomials `X ^ n - X - 1` are irreducible.
- `X_pow_sub_X_sub_one_gal`: The Selmer polynomial `X ^ n - X - 1` has Galois group `S_n`.
-/

-- PR #29688
section GeneralGalois

variable (G K L : Type*) [Group G] [Field K] [Field L] [Algebra K L] [MulSemiringAction G L]
   (H H' : Subgroup G) (F F' : IntermediateField K L)

namespace IsGaloisGroup

variable [hGKL : IsGaloisGroup G K L]

protected theorem finite [FiniteDimensional K L] : Finite G := by
  apply Nat.finite_of_card_ne_zero
  rw [hGKL.card_eq_finrank]
  exact Module.finrank_pos.ne'

instance to_subgroup :
    IsGaloisGroup H (FixedPoints.intermediateField H : IntermediateField K L) L where
  faithful := have := hGKL.faithful; inferInstance
  commutes := ‚ü®fun g x y ‚Ü¶ by
    simp_rw [Algebra.smul_def, IntermediateField.algebraMap_apply, smul_mul', x.2 g]‚ü©
  isInvariant := ‚ü®fun x h ‚Ü¶ ‚ü®‚ü®x, h‚ü©, rfl‚ü©‚ü©

theorem card_subgroup_eq_finrank_fixedpoints :
    Nat.card H = Module.finrank (FixedPoints.intermediateField H : IntermediateField K L) L :=
  card_eq_finrank H (FixedPoints.intermediateField H) L

instance to_intermediateField [Finite G] :
    IsGaloisGroup (fixingSubgroup G (F : Set L)) F L where
  faithful := have := hGKL.faithful; inferInstance
  commutes := ‚ü®fun g x y ‚Ü¶ by simp [Algebra.smul_def, Subgroup.smul_def, g.2 x]‚ü©
  isInvariant := ‚ü®fun x h ‚Ü¶ ‚ü®‚ü®x, by
    have := hGKL.finiteDimensional
    have := hGKL.isGalois
    rw [‚Üê IsGalois.fixedField_fixingSubgroup F]
    intro ‚ü®g, hg‚ü©
    rw [Subtype.forall] at h
    simp only [Subgroup.mk_smul, IntermediateField.mem_fixingSubgroup_iff,
      mem_fixingSubgroup_iff] at h hg
    have key := mulEquivCongr_apply_smul (L ‚âÉ‚Çê[K] L) G K L g x
    refine key.symm.trans (h _ (by simpa only [mulEquivCongr_apply_smul]))
  ‚ü©, rfl‚ü©‚ü©

theorem card_fixingSubgroup_eq_finrank [Finite G] :
    Nat.card (fixingSubgroup G (F : Set L)) = Module.finrank F L :=
  card_eq_finrank (fixingSubgroup G (F : Set L)) F L

end IsGaloisGroup

namespace IsGaloisGroup

theorem fixingSubgroup_le_of_le (h : F ‚â§ F') :
    fixingSubgroup G (F' : Set L) ‚â§ fixingSubgroup G (F : Set L) :=
  fun _ hœÉ ‚ü®x, hx‚ü© ‚Ü¶ hœÉ ‚ü®x, h hx‚ü©

section SMulCommClass

variable [SMulCommClass G K L]

theorem fixingSubgroup_bot : fixingSubgroup G ((‚ä• : IntermediateField K L) : Set L) = ‚ä§ := by
  simp [Subgroup.ext_iff, mem_fixingSubgroup_iff, IntermediateField.mem_bot] -- simp lemmas?

theorem fixedPoints_bot :
    (FixedPoints.intermediateField (‚ä• : Subgroup G) : IntermediateField K L) = ‚ä§ := by
  simp [IntermediateField.ext_iff]

theorem le_fixedPoints_iff_le_fixingSubgroup :
    F ‚â§ FixedPoints.intermediateField H ‚Üî H ‚â§ fixingSubgroup G (F : Set L) :=
  ‚ü®fun h g hg x => h (Subtype.mem x) ‚ü®g, hg‚ü©, fun h x hx g => h (Subtype.mem g) ‚ü®x, hx‚ü©‚ü©

theorem fixedPoints_le_of_le (h : H ‚â§ H') :
    FixedPoints.intermediateField H' ‚â§ (FixedPoints.intermediateField H : IntermediateField K L) :=
  fun _ hœÉ ‚ü®x, hx‚ü© ‚Ü¶ hœÉ ‚ü®x, h hx‚ü©

end SMulCommClass

variable [hGKL : IsGaloisGroup G K L]

theorem fixingSubgroup_top : fixingSubgroup G ((‚ä§ : IntermediateField K L) : Set L) = ‚ä• := by
  simp only [Subgroup.ext_iff, mem_fixingSubgroup_iff, Subgroup.mem_bot]
  exact fun x ‚Ü¶ ‚ü®fun h ‚Ü¶ hGKL.faithful.eq_of_smul_eq_smul (by simpa using h), fun h ‚Ü¶ by simp [h]‚ü©

theorem fixedPoints_top :
    (FixedPoints.intermediateField (‚ä§ : Subgroup G) : IntermediateField K L) = ‚ä• := by
  simp only [eq_bot_iff, SetLike.le_def, FixedPoints.mem_intermediateField_iff, Subtype.forall,
    Subgroup.mem_top, Subgroup.mk_smul, forall_const]
  exact hGKL.isInvariant.isInvariant

theorem fixingSubgroup_fixedPoints [Finite G] :
    fixingSubgroup G ((FixedPoints.intermediateField H : IntermediateField K L) : Set L) = H := by
  have : FiniteDimensional K L := hGKL.finiteDimensional
  refine (Subgroup.eq_of_le_of_card_ge ?_ ?_).symm
  ¬∑ rw [‚Üê le_fixedPoints_iff_le_fixingSubgroup]
  ¬∑ rw [hGKL.card_subgroup_eq_finrank_fixedpoints, hGKL.card_subgroup_eq_finrank_fixedpoints]
    apply IntermediateField.finrank_le_of_le_left
    rw [le_fixedPoints_iff_le_fixingSubgroup]

theorem fixedPoints_fixingSubgroup [Finite G] :
    FixedPoints.intermediateField (fixingSubgroup G (F : Set L)) = F := by
  have : FiniteDimensional K L := hGKL.finiteDimensional
  refine (IntermediateField.eq_of_le_of_finrank_eq' ?_ ?_).symm
  ¬∑ rw [le_fixedPoints_iff_le_fixingSubgroup]
  ¬∑ rw [‚Üê card_subgroup_eq_finrank_fixedpoints, card_fixingSubgroup_eq_finrank]

end IsGaloisGroup

end GeneralGalois

section Inertia

open scoped Pointwise

section inertiadef

variable {A B : Type*} [CommRing A] [CommRing B] [Algebra A B]
  (P : Ideal A) (Q : Ideal B) [Q.LiesOver P]
  (G : Type*) [Group G] [MulSemiringAction G B] [SMulCommClass G A B]

theorem Ideal.inertiaSubgroup_eq_ker : Q.toAddSubgroup.inertia G =
    (Ideal.Quotient.stabilizerHom Q P G).ker.map (MulAction.stabilizer G Q).subtype := by
  simp_rw [Subgroup.ext_iff, AddSubgroup.mem_inertia, Submodule.mem_toAddSubgroup,
    Subgroup.mem_map, MonoidHom.mem_ker, Subgroup.subtype_apply, AlgEquiv.ext_iff,
    AlgEquiv.one_apply, Submodule.Quotient.forall, Quotient.mk_eq_mk,
    Quotient.stabilizerHom_apply, Quotient.mk_eq_mk_iff_sub_mem]
  refine fun g ‚Ü¶ ‚ü®fun h ‚Ü¶ ‚ü®‚ü®g, ?_‚ü©, h, rfl‚ü©, fun ‚ü®g, h, hg‚ü© x ‚Ü¶ hg ‚ñ∏ h x‚ü©
  rw [‚Üê Subgroup.inv_mem_iff, MulAction.mem_stabilizer_iff, Ideal.ext_iff]
  intro x
  rw [mem_inv_pointwise_smul_iff, iff_comm, ‚Üê Q.add_mem_iff_right (h x), sub_add_cancel]

end inertiadef

section inertia

theorem AddSubgroup.subgroupOf_inertia {M : Type*} [AddGroup M] (I : AddSubgroup M)
    (G : Type*) [Group G] [MulAction G M] (H : Subgroup G) :
    (I.inertia G).subgroupOf H = I.inertia H :=
  rfl

end inertia

-- PR #30666
section ram

variable {K ùí™ : Type*} [Field K] [NumberField K] [CommRing ùí™] [Algebra ùí™ K]
variable [IsIntegralClosure ùí™ ‚Ñ§ K]

lemma NumberField.exists_not_isUramifiedAt_int (H : 1 < Module.finrank ‚Ñö K) :
    ‚àÉ (P : Ideal ùí™) (_ : P.IsMaximal), P ‚â† ‚ä• ‚àß ¬¨ Algebra.IsUnramifiedAt ‚Ñ§ P :=
  sorry

end ram

section ram

open IsGaloisGroup

open NumberField

instance (R K : Type*) [CommRing R] [CommRing K] [Algebra R K]
    (G : Type*) [Group G] [MulSemiringAction G K] [SMulCommClass G R K] :
    MulSemiringAction G (integralClosure R K) where
  smul := fun g x ‚Ü¶ ‚ü®g ‚Ä¢ (x : K), x.2.map (MulSemiringAction.toAlgHom R K g)‚ü©
  one_smul x := by ext; exact one_smul G (x : K)
  mul_smul g h x := by ext; exact mul_smul g h (x : K)
  smul_zero g := by ext; exact smul_zero g
  smul_add g x y := by ext; exact smul_add g (x : K) (y : K)
  smul_one g := by ext; exact smul_one g
  smul_mul g x y := by ext; exact smul_mul' g (x : K) (y : K)

instance {G K : Type*} [Group G] [Field K] [MulSemiringAction G K] :
    MulSemiringAction G (ùìû K) :=
  inferInstanceAs (MulSemiringAction G (integralClosure _ _))

instance (K L : Type*) [Field K] [Field L] [NumberField K] [NumberField L] [Algebra K L]
    (G : Type*) [Group G] [MulSemiringAction G L] [IsGaloisGroup G K L] :
    IsGaloisGroup G (ùìû K) (ùìû L) :=
  IsGaloisGroup.of_isFractionRing G (ùìû K) (ùìû L) K L (fun _ _ ‚Ü¶ rfl)

instance (L : Type*) [Field L] [NumberField L]
    (G : Type*) [Group G] [MulSemiringAction G L] [IsGaloisGroup G ‚Ñö L] :
    IsGaloisGroup G ‚Ñ§ (ùìû L) :=
  IsGaloisGroup.of_isFractionRing G ‚Ñ§ (ùìû L) ‚Ñö L (fun _ _ ‚Ü¶ rfl)

instance tada1 {K : Type*} [Field K] [NumberField K] (m : Ideal (ùìû K)) [m.IsMaximal] :
    Finite (ùìû K ‚ß∏ m) :=
  m.finiteQuotientOfFreeOfNeBot (m.bot_lt_of_maximal (RingOfIntegers.not_isField K)).ne'

theorem Ideal.IsMaximal.ne_bot_of_isIntegral_int {R : Type*} [CommRing R]
    [CharZero R] [Algebra.IsIntegral ‚Ñ§ R] (I : Ideal R) [I.IsMaximal] : I ‚â† ‚ä• :=
  Ring.ne_bot_of_isMaximal_of_not_isField ‚Äπ_‚Ä∫ fun h ‚Ü¶ Int.not_isField
    (isField_of_isIntegral_of_isField (FaithfulSMul.algebraMap_injective ‚Ñ§ R) h)

theorem genthm‚ÇÄ (K : Type*) [Field K] [NumberField K]
    (G : Type*) [Group G] [MulSemiringAction G K]
    [IsGaloisGroup G ‚Ñö K] :
    ‚®Ü m : MaximalSpectrum (ùìû K), m.asIdeal.toAddSubgroup.inertia G = ‚ä§ := by
  have : Finite G := IsGaloisGroup.finite G ‚Ñö K
  set H : Subgroup G := ‚®Ü m : MaximalSpectrum (ùìû K), m.asIdeal.toAddSubgroup.inertia G
  set F : IntermediateField ‚Ñö K := FixedPoints.intermediateField H
  suffices Module.finrank ‚Ñö F ‚â§ 1 by
    rw [eq_top_iff, ‚Üê fixingSubgroup_fixedPoints G ‚Ñö K H, ‚Üê le_fixedPoints_iff_le_fixingSubgroup,
      fixedPoints_top, le_bot_iff, ‚Üê IntermediateField.finrank_eq_one_iff]
    exact le_antisymm this Module.finrank_pos
  suffices h : ‚àÄ (m : Ideal (ùìû F)) (hm : m.IsMaximal), Algebra.IsUnramifiedAt ‚Ñ§ m by
    contrapose! h
    obtain ‚ü®p, h1, h2, h3‚ü© := NumberField.exists_not_isUramifiedAt_int (ùí™ := ùìû F) h
    exact ‚ü®p, h1, h3‚ü©
  intro m _
  have hm2 := Ideal.IsMaximal.ne_bot_of_isIntegral_int m
  rw [Algebra.isUnramifiedAt_iff_of_isDedekindDomain hm2]
  obtain ‚ü®m, hm, ‚ü®rfl‚ü©‚ü© := Ideal.exists_ideal_liesOver_maximal_of_isIntegral m (ùìû K)
  rw [Ideal.under_under]
  have hm1 := Ideal.IsMaximal.ne_bot_of_isIntegral_int (m.under ‚Ñ§)
  have h : m.toAddSubgroup.inertia G ‚â§ H :=
    le_iSup (fun m : MaximalSpectrum (ùìû K) ‚Ü¶ m.asIdeal.toAddSubgroup.inertia G) ‚ü®m, hm‚ü©
  replace h : Nat.card (m.toAddSubgroup.inertia H) = Nat.card (m.toAddSubgroup.inertia G) := by
    rw [‚Üê Subgroup.map_subgroupOf_eq_of_le h, Subgroup.card_subtype,
      AddSubgroup.subgroupOf_inertia]
  let := Ideal.Quotient.field m
  let := Ideal.Quotient.field (m.under (ùìû F))
  let := Ideal.Quotient.field (m.under ‚Ñ§)
  -- todo: clean up once #30934 is merged
  have : IsGalois ‚Ñö K := IsGaloisGroup.isGalois G ‚Ñö K
  rw [Ideal.card_inertia_eq_ramificationIdxIn F K (m.under (ùìû F)) hm2 m,
    Ideal.card_inertia_eq_ramificationIdxIn ‚Ñö K (m.under ‚Ñ§) hm1 m,
    Ideal.ramificationIdxIn_eq_ramificationIdx (m.under (ùìû F)) m F K,
    Ideal.ramificationIdxIn_eq_ramificationIdx (m.under ‚Ñ§) m ‚Ñö K] at h
  have key := Ideal.ramificationIdx_algebra_tower (Ideal.map_ne_bot_of_ne_bot hm2)
    (Ideal.map_ne_bot_of_ne_bot hm1) Ideal.map_comap_le
  rwa [h, right_eq_mul‚ÇÄ (Ideal.IsDedekindDomain.ramificationIdx_ne_zero_of_liesOver m hm1)] at key

-- generalize from `ùìû K` to `IsIntegralClosure`?
theorem genthm (K : Type*) [Field K] [NumberField K]
    (R : Type*) [CommRing R] [Algebra R K] [IsIntegralClosure R ‚Ñ§ K]
    (G : Type*) [Group G] [MulSemiringAction G K]
    [MulSemiringAction G R] [IsGaloisGroup G ‚Ñö K] :
    ‚®Ü m : MaximalSpectrum R, m.asIdeal.toAddSubgroup.inertia G = ‚ä§ := by
  sorry

end ram

end Inertia

namespace Polynomial

section Moore

theorem aeval_smul {R S : Type*} [CommSemiring R] [Semiring S] [Algebra R S] (f : R[X])
    (G : Type*) [Group G] [MulSemiringAction G S] [SMulCommClass G R S] (g : G) (x : S) :
    aeval (g ‚Ä¢ x) f = g ‚Ä¢ (aeval x f) := by
  rw [‚Üê MulSemiringAction.toAlgHom_apply R, aeval_algHom_apply, MulSemiringAction.toAlgHom_apply]

instance {R S : Type*} [CommRing R] [CommRing S] [IsDomain S] [Algebra R S]
    [NoZeroSMulDivisors R S] (f : R[X])
    (G : Type*) [Group G] [MulSemiringAction G S] [SMulCommClass G R S] :
    MulAction G (f.rootSet S) where
  smul g x := ‚ü®g ‚Ä¢ x.1, by
    rw [mem_rootSet_of_ne (ne_zero_of_mem_rootSet x.2), aeval_smul,
      aeval_eq_zero_of_mem_rootSet x.2, smul_zero]‚ü©
  one_smul x := Subtype.ext (one_smul G x.1)
  mul_smul g h x := Subtype.ext (mul_smul g h x.1)

theorem rootSet.coe_smul
    {R S : Type*} [CommRing R] [CommRing S] [IsDomain S] [Algebra R S]
    [NoZeroSMulDivisors R S] {f : R[X]}
    {G : Type*} [Group G] [MulSemiringAction G S] [SMulCommClass G R S]
    (g : G) (x : f.rootSet S) : (g ‚Ä¢ x : f.rootSet S) = g ‚Ä¢ (x : S) := rfl

theorem Monic.mem_rootSet {T S : Type*} [CommRing T] [CommRing S] [IsDomain S]
    [Algebra T S] {p : T[X]} (hp : p.Monic) {a : S} : a ‚àà p.rootSet S ‚Üî (aeval a) p = 0 := by
  simp [Polynomial.mem_rootSet', (hp.map (algebraMap T S)).ne_zero]

theorem fiddly''' {Œ± Œ≤ : Type*} [Finite Œ±] {f : Œ± ‚Üí Œ≤} (hf : Function.Surjective f) :
    Nat.card Œ± ‚â§ Nat.card Œ≤ + 1 ‚Üî ‚àÄ a b c d,
      f a = f b ‚Üí f c = f d ‚Üí a ‚â† b ‚Üí c ‚â† d ‚Üí {a, b} = ({c, d} : Set Œ±) := by
  rcases isEmpty_or_nonempty Œ±
  ¬∑ simp
  let g := Function.surjInv hf
  rw [‚Üê Set.ncard_range_of_injective (Function.injective_surjInv hf),
    ‚Üê Set.ncard_add_ncard_compl (Set.range g), add_le_add_iff_left]
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ rw [Set.ncard_le_one_iff_subset_singleton] at h
    obtain ‚ü®x, hx‚ü© := h
    suffices ‚àÄ a b : Œ±, f a = f b ‚Üí a ‚â† b ‚Üí a = x ‚à® a = g (f x) by grind
    intro a b hfab hab
    by_cases ha : a ‚àà Set.range g
    ¬∑ obtain ‚ü®a, rfl‚ü© := ha
      rw [Function.surjInv_eq hf] at hfab
      subst hfab
      by_cases hb : b ‚àà Set.range g
      ¬∑ obtain ‚ü®b, rfl‚ü© := hb
        rw [Function.surjInv_eq hf] at hab
        contradiction
      ¬∑ exact Or.inr (congrArg (fun y ‚Ü¶ g (f y)) (hx hb))
    ¬∑ exact Or.inl (hx ha)
  ¬∑ rw [Set.ncard_le_one]
    simp only [Set.mem_compl_iff, Set.mem_range, not_exists, ‚Üê ne_eq]
    intro a ha b hb
    simpa [(ha (f b)).symm] using congrArg (a ‚àà ¬∑) (h a (g (f a)) b (g (f b))
      (Function.surjInv_eq hf (f a)).symm (Function.surjInv_eq hf (f b)).symm
      (ha (f a)).symm (hb (f b)).symm)

theorem fiddly' {Œ± Œ≤ : Type*} (s : Set Œ±) [Finite s] (f : Œ± ‚Üí Œ≤) :
    s.ncard ‚â§ (f '' s).ncard + 1 ‚Üî ‚àÄ a ‚àà s, ‚àÄ b ‚àà s, ‚àÄ c ‚àà s, ‚àÄ d ‚àà s,
      f a = f b ‚Üí f c = f d ‚Üí a ‚â† b ‚Üí c ‚â† d ‚Üí {a, b} = ({c, d} : Set Œ±) := by
  simpa [Subtype.ext_iff, ‚Üê Set.image_val_inj, Set.image_insert_eq] using
    fiddly''' (Set.surjective_mapsTo_image_restrict f s)

theorem tada -- R = ‚Ñ§, S = ùìû K
    {R S : Type*} [CommRing R] [CommRing S] [IsDomain S] [Algebra R S]
    [NoZeroSMulDivisors R S]
    (f : R[X]) (hmon : f.Monic) [DecidableEq (f.rootSet S)]
    (G : Type*) [Group G] [MulSemiringAction G S] [SMulCommClass G R S]
    (m : MaximalSpectrum S)
     -- all roots already present (so no new roots in `S ‚ß∏ m`)
    (hf : (f.map (algebraMap R S)).Factors)
    -- at most one collision
    (h : (f.rootSet S).ncard ‚â§ (f.rootSet (S ‚ß∏ m.asIdeal)).ncard + 1) :
    ‚àÄ g ‚àà m.asIdeal.toAddSubgroup.inertia G,
      MulAction.toPermHom G (f.rootSet S) g = 1 ‚à®
        (MulAction.toPermHom G (f.rootSet S) g).IsSwap := by
  intro g hg
  let œÄ : S ‚Üí‚Çê[R] S ‚ß∏ m.asIdeal := Ideal.Quotient.mk‚Çê R m.asIdeal
  have hœÄ (x : S) (hx : x ‚àà f.rootSet S): œÄ x ‚àà f.rootSet (S ‚ß∏ m.asIdeal) := by
    unfold œÄ
    rw [hmon.mem_rootSet, aeval_algHom_apply, aeval_eq_zero_of_mem_rootSet hx, map_zero]
  have hœÄ (x : S) : œÄ (g ‚Ä¢ x) = œÄ x := (Ideal.Quotient.mk_eq_mk_iff_sub_mem (g ‚Ä¢ x) x).mpr (hg x)
  rw [or_iff_not_imp_left]
  intro hg'
  rw [Equiv.ext_iff, not_forall] at hg'
  obtain ‚ü®x, hx‚ü© := hg'
  change g ‚Ä¢ x ‚â† x at hx
  refine ‚ü®g ‚Ä¢ x, x, hx, ?_‚ü©
  ext z
  rw [Equiv.swap_apply_def]
  have h0 : f.rootSet (S ‚ß∏ m.asIdeal) = œÄ '' f.rootSet S := by
    classical
    have key := Monic.roots_map_of_card_eq_natDegree (hmon.map (algebraMap R S))
      (œÄ : S ‚Üí+* S ‚ß∏ m.asIdeal) hf.natDegree_eq_card_roots.symm
    rw [map_map, œÄ.comp_algebraMap] at key
    simp [rootSet, aroots, ‚Üê key, Multiset.toFinset_map]
  rw [h0] at h
  split_ifs with hz hz'
  ¬∑ subst hz
    simp only [MulAction.toPermHom_apply, MulAction.toPerm_apply, SetLike.coe_eq_coe]
    have key := (fiddly' (f.rootSet S) œÄ).mp h
      (g ‚Ä¢ g ‚Ä¢ x) (g ‚Ä¢ g ‚Ä¢ x).2 (g ‚Ä¢ x) (g ‚Ä¢ x).2 (g ‚Ä¢ x) (g ‚Ä¢ x).2 x x.2
    simp [hœÄ] at key
    simp [‚Üê Polynomial.rootSet.coe_smul] at key
    simp [hx] at key
    replace key := congrArg (fun s ‚Ü¶ (x : S) ‚àà s) key
    simp [hx.symm] at key
    exact key.symm
  ¬∑ simp [hz']
  ¬∑ simp only [MulAction.toPermHom_apply, MulAction.toPerm_apply, SetLike.coe_eq_coe]
    have key := (fiddly' (f.rootSet S) œÄ).mp h (g ‚Ä¢ z) (g ‚Ä¢ z).2 z z.2 (g ‚Ä¢ x) (g ‚Ä¢ x).2 x x.2
    simp [hœÄ] at key
    simp [‚Üê Polynomial.rootSet.coe_smul] at key
    simp [hx] at key
    rw [not_imp_comm] at key
    apply key
    contrapose! key
    replace key := congrArg (fun s ‚Ü¶ (z : S) ‚àà s) key
    simp [hz, hz'] at key

theorem tada' {R S : Type*} [CommRing R] [CommRing S] [IsDomain S] [Algebra R S]
    [NoZeroSMulDivisors R S] (f : R[X])
    (hf : f.Monic) (hf' : (f.map (algebraMap R S)).Factors)
    (G : Type*) [Group G] [MulSemiringAction G S] [SMulCommClass G R S]
    [MulAction.IsPretransitive G (f.rootSet S)]
    (hG : ‚®Ü m : MaximalSpectrum S, m.asIdeal.toAddSubgroup.inertia G = ‚ä§)
    (h : ‚àÄ m : MaximalSpectrum S, (f.rootSet S).ncard ‚â§ (f.rootSet (S ‚ß∏ m.asIdeal)).ncard + 1) :
    Function.Surjective (MulAction.toPermHom G (f.rootSet S)) := by
  classical
  let X : Set G := ‚ãÉ m : MaximalSpectrum S, m.asIdeal.toAddSubgroup.inertia G
  have hS1 : Subgroup.closure X = ‚ä§ := by
    simpa only [X, Subgroup.closure_iUnion, Subgroup.closure_eq, Subgroup.closure_diff_one]
  have hS2 : ‚àÄ œÉ ‚àà X, MulAction.toPermHom G (f.rootSet S) œÉ = 1 ‚à®
      (MulAction.toPermHom G (f.rootSet S) œÉ).IsSwap := by
    intro œÉ hœÉ
    simp only [X, Set.mem_iUnion] at hœÉ
    obtain ‚ü®m, hm‚ü© := hœÉ
    have := tada f hf G m hf' (h m) œÉ hm
    exact this
  exact surjective_of_isSwap_of_isPretransitive' X hS2 hS1

open Equiv Pointwise

open IntermediateField

theorem switchinglemma {F : Type*} [Field F] (p : F[X])
    (E‚ÇÅ E‚ÇÇ : Type*) [Field E‚ÇÅ] [Algebra F E‚ÇÅ] [Field E‚ÇÇ] [Algebra F E‚ÇÇ]
    [Fact (p.Splits (algebraMap F E‚ÇÅ))] [Fact (p.Splits (algebraMap F E‚ÇÇ))] :
    Gal.galActionHom p E‚ÇÅ =
      ((Polynomial.Gal.rootsEquivRoots p E‚ÇÇ).symm.trans
        (Polynomial.Gal.rootsEquivRoots p E‚ÇÅ)).permCongrHom.toMonoidHom.comp
        (Gal.galActionHom p E‚ÇÇ)
       := by
  ext
  simp [permCongrHom, permCongrHom, Gal.galActionHom, Polynomial.Gal.smul_def]

attribute [-instance] Polynomial.Gal.galActionAux -- should be local to PolynomialGaloisGroup.lean

attribute [local instance] Gal.splits_‚Ñö_‚ÑÇ

attribute [-instance] Gal.smul Gal.galAction -- todo: redefine in more general semiring context

open NumberField

theorem tada'' (f‚ÇÄ : ‚Ñ§[X]) (hf‚ÇÄ : f‚ÇÄ.Monic) (hf' : Irreducible f‚ÇÄ) :
    -- condition on at most on root collision mod p :
    Function.Bijective (Gal.galActionHom (f‚ÇÄ.map (algebraMap ‚Ñ§ ‚Ñö)) ‚ÑÇ) := by
  classical
  let f : ‚Ñö[X] := f‚ÇÄ.map (algebraMap ‚Ñ§ ‚Ñö)
  have hf := hf‚ÇÄ.map (algebraMap ‚Ñ§ ‚Ñö)
  let K := f.SplittingField
  have : Fact (f.Splits (algebraMap ‚Ñö K)) := ‚ü®SplittingField.splits f‚ü©
  have : NumberField K := by constructor
  have : IsGalois ‚Ñö K := by constructor
  let R := ùìû K
  let G := f.Gal
  suffices Function.Surjective (Gal.galActionHom f K) by
    use Polynomial.Gal.galActionHom_injective f ‚ÑÇ
    rw [switchinglemma f ‚ÑÇ K]
    exact (((Gal.rootsEquivRoots f f.SplittingField).symm.trans
      (Gal.rootsEquivRoots f ‚ÑÇ)).permCongrHom.toEquiv.comp_surjective _).mpr this
  have hœÜ : Set.MapsTo (algebraMap R K) (f‚ÇÄ.rootSet R) (f.rootSet K) := by
    intro x hx
    rw [hf.mem_rootSet, aeval_map_algebraMap, aeval_algebraMap_apply,
      aeval_eq_zero_of_mem_rootSet hx, map_zero]
  let œÜ : f‚ÇÄ.rootSet R ‚Üí f.rootSet K := hœÜ.restrict
  have hœÜ1 : ‚àÄ g : G, ‚àÄ x : f‚ÇÄ.rootSet R, œÜ (g ‚Ä¢ x) = g ‚Ä¢ œÜ x := by
    intro g x
    ext
    exact (rootSet.coe_smul g (œÜ x)).symm
  have hœÜ2 : Function.Bijective œÜ := by
    rw [Function.Bijective, hœÜ.restrict_inj, hœÜ.restrict_surjective_iff]
    refine ‚ü®RingOfIntegers.coe_injective.injOn, ?_‚ü©
    -- surjective
    sorry
  suffices Function.Surjective (MulAction.toPermHom G (f‚ÇÄ.rootSet R)) by
    sorry
  -- suffices Function.Bijective (Gal.galActionHom f K) by
  --   rw [switchinglemma f ‚ÑÇ K]
  --   exact (((Gal.rootsEquivRoots f f.SplittingField).symm.trans
  --     (Gal.rootsEquivRoots f ‚ÑÇ)).permCongrHom.toEquiv.comp_bijective _).mpr this
  have : MulAction.IsPretransitive G (f.rootSet K) := by
    convert Gal.galAction_isPretransitive f K
      (hf‚ÇÄ.irreducible_iff_irreducible_map_fraction_map.mp hf')
    ext
    -- diamond...
    sorry
  -- need a bijection between f‚ÇÄ.rootSet R and
  have : MulAction.IsPretransitive G (f‚ÇÄ.rootSet R) := by
    sorry
  have : FaithfulSMul G (f‚ÇÄ.rootSet R) := by
    sorry
  refine tada' (S := R) f‚ÇÄ hf‚ÇÄ ?_ G ?_ ?_
  ¬∑ sorry
  ¬∑ have : IsGaloisGroup G ‚Ñö K := IsGaloisGroup.of_isGalois ‚Ñö K
    exact genthm‚ÇÄ K G
  ¬∑ sorry

end Moore

open scoped Polynomial

variable {n : ‚Ñï}

theorem X_pow_sub_X_sub_one_irreducible_aux (z : ‚ÑÇ) : ¬¨(z ^ n = z + 1 ‚àß z ^ n + z ^ 2 = 0) := by
  rintro ‚ü®h1, h2‚ü©
  replace h3 : z ^ 3 = 1 := by
    linear_combination (1 - z - z ^ 2 - z ^ n) * h1 + (z ^ n - 2) * h2
  have key : z ^ n = 1 ‚à® z ^ n = z ‚à® z ^ n = z ^ 2 := by
    rw [‚Üê Nat.mod_add_div n 3, pow_add, pow_mul, h3, one_pow, mul_one]
    have : n % 3 < 3 := Nat.mod_lt n zero_lt_three
    interval_cases n % 3 <;>
    simp only [pow_zero, pow_one, or_true, true_or]
  have z_ne_zero : z ‚â† 0 := fun h =>
    zero_ne_one ((zero_pow three_ne_zero).symm.trans (show (0 : ‚ÑÇ) ^ 3 = 1 from h ‚ñ∏ h3))
  rcases key with (key | key | key)
  ¬∑ exact z_ne_zero (by rwa [key, right_eq_add] at h1)
  ¬∑ exact one_ne_zero (by rwa [key, left_eq_add] at h1)
  ¬∑ exact z_ne_zero (eq_zero_of_pow_eq_zero (by rwa [key, add_self_eq_zero] at h2))

theorem X_pow_sub_X_sub_one_irreducible (hn1 : n ‚â† 1) : Irreducible (X ^ n - X - 1 : ‚Ñ§[X]) := by
  by_cases hn0 : n = 0
  ¬∑ rw [hn0, pow_zero, sub_sub, add_comm, ‚Üê sub_sub, sub_self, zero_sub]
    exact Associated.irreducible ‚ü®-1, mul_neg_one X‚ü© irreducible_X
  have hn : 1 < n := Nat.one_lt_iff_ne_zero_and_ne_one.mpr ‚ü®hn0, hn1‚ü©
  have hp : (X ^ n - X - 1 : ‚Ñ§[X]) = trinomial 0 1 n (-1) (-1) 1 := by
    simp only [trinomial, C_neg, C_1]; ring
  rw [hp]
  apply IsUnitTrinomial.irreducible_of_coprime' ‚ü®0, 1, n, zero_lt_one, hn, -1, -1, 1, rfl‚ü©
  rintro z ‚ü®h1, h2‚ü©
  apply X_pow_sub_X_sub_one_irreducible_aux (n := n) z
  rw [trinomial_mirror zero_lt_one hn (-1 : ‚Ñ§À£).ne_zero (1 : ‚Ñ§À£).ne_zero] at h2
  simp_rw [trinomial, aeval_add, aeval_mul, aeval_X_pow, aeval_C,
    Units.val_neg, Units.val_one, map_neg, map_one] at h1 h2
  replace h1 : z ^ n = z + 1 := by linear_combination h1
  replace h2 := mul_eq_zero_of_left h2 z
  rw [add_mul, add_mul, add_zero, mul_assoc (-1 : ‚ÑÇ), ‚Üê pow_succ, Nat.sub_add_cancel hn.le] at h2
  rw [h1] at h2 ‚ä¢
  exact ‚ü®rfl, by linear_combination -h2‚ü©

theorem X_pow_sub_X_sub_one_irreducible_rat (hn1 : n ‚â† 1) : Irreducible (X ^ n - X - 1 : ‚Ñö[X]) := by
  by_cases hn0 : n = 0
  ¬∑ rw [hn0, pow_zero, sub_sub, add_comm, ‚Üê sub_sub, sub_self, zero_sub]
    exact Associated.irreducible ‚ü®-1, mul_neg_one X‚ü© irreducible_X
  have hp : (X ^ n - X - 1 : ‚Ñ§[X]) = trinomial 0 1 n (-1) (-1) 1 := by
    simp only [trinomial, C_neg, C_1]; ring
  have hn : 1 < n := Nat.one_lt_iff_ne_zero_and_ne_one.mpr ‚ü®hn0, hn1‚ü©
  have h := (IsPrimitive.Int.irreducible_iff_irreducible_map_cast ?_).mp
    (X_pow_sub_X_sub_one_irreducible hn1)
  ¬∑ rwa [Polynomial.map_sub, Polynomial.map_sub, Polynomial.map_pow, Polynomial.map_one,
      Polynomial.map_X] at h
  ¬∑ exact hp ‚ñ∏ (trinomial_monic zero_lt_one hn).isPrimitive

open Equiv Pointwise

open IntermediateField

attribute [local instance] Gal.splits_‚Ñö_‚ÑÇ

theorem X_pow_sub_X_sub_one_gal :
    Function.Bijective (Gal.galActionHom (X ^ n - X - 1 : ‚Ñö[X]) ‚ÑÇ) := by
  rcases le_or_gt n 1 with hn | hn
  ¬∑ have : Subsingleton ((X ^ n - X - 1 : ‚Ñö[X]).rootSet ‚ÑÇ) := by
      apply Finset.card_le_one_iff_subsingleton_coe.mp
      grw [Multiset.toFinset_card_le, card_roots', natDegree_map_le, natDegree_sub_le,
        natDegree_sub_le, natDegree_X_pow, natDegree_X, natDegree_one, hn, max_self, Nat.max_zero]
    have : Unique ((X ^ n - X - 1 : ‚Ñö[X]).Gal) := by
      refine Gal.uniqueGalOfSplits _ (splits_of_natDegree_le_one _ (by compute_degree!))
    apply Unique.bijective
  have hp : (X ^ n - X - 1 : ‚Ñ§[X]) = trinomial 0 1 n (-1) (-1) 1 := by
    simp only [trinomial, C_neg, C_1]; ring
  have h := tada'' (X ^ n - X - 1) (hp ‚ñ∏ trinomial_monic zero_lt_one hn)
    (X_pow_sub_X_sub_one_irreducible hn.ne')
  rwa [Polynomial.map_sub, Polynomial.map_sub, Polynomial.map_pow, Polynomial.map_one,
    Polynomial.map_X] at h

end Polynomial
