/-
Copyright (c) 2023 Luke Mantle. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Luke Mantle
-/
import Mathlib.Data.Polynomial.Derivative
import Mathlib.Data.Nat.Parity
import Mathlib.Data.Nat.Factorial.DoubleFactorial

#align_import ring_theory.polynomial.hermite.basic from "leanprover-community/mathlib"@"938d3db9c278f8a52c0f964a405806f0f2b09b74"

/-!
# Hermite polynomials

This file defines `Polynomial.hermite n`, the `n`th probabilists' Hermite polynomial.

## Main definitions

* `Polynomial.hermite n`: the `n`th probabilists' Hermite polynomial,
  defined recursively as a `Polynomial â„¤`

## Results

* `Polynomial.hermite_succ`: the recursion `hermite (n+1) = (x - d/dx) (hermite n)`
* `Polynomial.coeff_hermite_explicit`: a closed formula for (nonvanishing) coefficients in terms
  of binomial coefficients and double factorials.
* `Polynomial.coeff_hermite_of_odd_add`: for `n`,`k` where `n+k` is odd, `(hermite n).coeff k` is
  zero.
* `Polynomial.coeff_hermite_of_even_add`: a closed formula for `(hermite n).coeff k` when `n+k` is
  even, equivalent to `Polynomial.coeff_hermite_explicit`.
* `Polynomial.monic_hermite`: for all `n`, `hermite n` is monic.
* `Polynomial.degree_hermite`: for all `n`, `hermite n` has degree `n`.

## References

* [Hermite Polynomials](https://en.wikipedia.org/wiki/Hermite_polynomials)

-/


noncomputable section

open Polynomial

namespace Polynomial

/-- the probabilists' Hermite polynomials. -/
noncomputable def hermite : â„• â†’ Polynomial â„¤
  | 0 => 1
  | n + 1 => X * hermite n - derivative (hermite n)
#align polynomial.hermite Polynomial.hermite

/-- The recursion `hermite (n+1) = (x - d/dx) (hermite n)` -/
@[simp]
theorem hermite_succ (n : â„•) : hermite (n + 1) = X * hermite n - derivative (hermite n) := by
  rw [hermite]
  -- ğŸ‰ no goals
#align polynomial.hermite_succ Polynomial.hermite_succ

theorem hermite_eq_iterate (n : â„•) : hermite n = (fun p => X * p - derivative p)^[n] 1 := by
  induction' n with n ih
  -- âŠ¢ hermite Nat.zero = (fun p => X * p - â†‘derivative p)^[Nat.zero] 1
  Â· rfl
    -- ğŸ‰ no goals
  Â· rw [Function.iterate_succ_apply', â† ih, hermite_succ]
    -- ğŸ‰ no goals
#align polynomial.hermite_eq_iterate Polynomial.hermite_eq_iterate

@[simp]
theorem hermite_zero : hermite 0 = C 1 :=
  rfl
#align polynomial.hermite_zero Polynomial.hermite_zero

-- Porting note: There was initially @[simp] on this line but it was removed
-- because simp can prove this theorem
theorem hermite_one : hermite 1 = X := by
  rw [hermite_succ, hermite_zero]
  -- âŠ¢ X * â†‘C 1 - â†‘derivative (â†‘C 1) = X
  simp only [map_one, mul_one, derivative_one, sub_zero]
  -- ğŸ‰ no goals
#align polynomial.hermite_one Polynomial.hermite_one

/-! ### Lemmas about `Polynomial.coeff` -/


section coeff

theorem coeff_hermite_succ_zero (n : â„•) : coeff (hermite (n + 1)) 0 = -coeff (hermite n) 1 := by
  simp [coeff_derivative]
  -- ğŸ‰ no goals
#align polynomial.coeff_hermite_succ_zero Polynomial.coeff_hermite_succ_zero

theorem coeff_hermite_succ_succ (n k : â„•) : coeff (hermite (n + 1)) (k + 1) =
    coeff (hermite n) k - (k + 2) * coeff (hermite n) (k + 2) := by
  rw [hermite_succ, coeff_sub, coeff_X_mul, coeff_derivative, mul_comm]
  -- âŠ¢ coeff (hermite n) k - (â†‘(k + 1) + 1) * coeff (hermite n) (k + 1 + 1) = coeff â€¦
  norm_cast
  -- ğŸ‰ no goals
#align polynomial.coeff_hermite_succ_succ Polynomial.coeff_hermite_succ_succ

theorem coeff_hermite_of_lt {n k : â„•} (hnk : n < k) : coeff (hermite n) k = 0 := by
  obtain âŸ¨k, rflâŸ© := Nat.exists_eq_add_of_lt hnk
  -- âŠ¢ coeff (hermite n) (n + k + 1) = 0
  clear hnk
  -- âŠ¢ coeff (hermite n) (n + k + 1) = 0
  induction' n with n ih generalizing k
  -- âŠ¢ coeff (hermite Nat.zero) (Nat.zero + k + 1) = 0
  Â· apply coeff_C
    -- ğŸ‰ no goals
  Â· have : n + k + 1 + 2 = n + (k + 2) + 1 := by ring
    -- âŠ¢ coeff (hermite (Nat.succ n)) (Nat.succ n + k + 1) = 0
    rw [Nat.succ_eq_add_one, coeff_hermite_succ_succ, add_right_comm, this, ih k, ih (k + 2),
      mul_zero, sub_zero]
#align polynomial.coeff_hermite_of_lt Polynomial.coeff_hermite_of_lt

@[simp]
theorem coeff_hermite_self (n : â„•) : coeff (hermite n) n = 1 := by
  induction' n with n ih
  -- âŠ¢ coeff (hermite Nat.zero) Nat.zero = 1
  Â· apply coeff_C
    -- ğŸ‰ no goals
  Â· rw [coeff_hermite_succ_succ, ih, coeff_hermite_of_lt, mul_zero, sub_zero]
    -- âŠ¢ n < n + 2
    simp
    -- ğŸ‰ no goals
#align polynomial.coeff_hermite_self Polynomial.coeff_hermite_self

@[simp]
theorem degree_hermite (n : â„•) : (hermite n).degree = n := by
  rw [degree_eq_of_le_of_coeff_ne_zero]
  -- âŠ¢ degree (hermite n) â‰¤ â†‘n
  simp_rw [degree_le_iff_coeff_zero, Nat.cast_lt]
  -- âŠ¢ âˆ€ (m : â„•), n < m â†’ coeff (hermite n) m = 0
  Â· rintro m hnm
    -- âŠ¢ coeff (hermite n) m = 0
    exact coeff_hermite_of_lt hnm
    -- ğŸ‰ no goals
  Â· simp [coeff_hermite_self n]
    -- ğŸ‰ no goals
#align polynomial.degree_hermite Polynomial.degree_hermite

@[simp]
theorem natDegree_hermite {n : â„•} : (hermite n).natDegree = n :=
  natDegree_eq_of_degree_eq_some (degree_hermite n)
#align polynomial.nat_degree_hermite Polynomial.natDegree_hermite

@[simp]
theorem leadingCoeff_hermite (n : â„•) : (hermite n).leadingCoeff = 1 := by
  rw [â† coeff_natDegree, natDegree_hermite, coeff_hermite_self]
  -- ğŸ‰ no goals
#align polynomial.leading_coeff_hermite Polynomial.leadingCoeff_hermite

theorem hermite_monic (n : â„•) : (hermite n).Monic :=
  leadingCoeff_hermite n
#align polynomial.hermite_monic Polynomial.hermite_monic

theorem coeff_hermite_of_odd_add {n k : â„•} (hnk : Odd (n + k)) : coeff (hermite n) k = 0 := by
  induction' n with n ih generalizing k
  -- âŠ¢ coeff (hermite Nat.zero) k = 0
  Â· rw [Nat.zero_eq, zero_add k] at hnk
    -- âŠ¢ coeff (hermite Nat.zero) k = 0
    exact coeff_hermite_of_lt hnk.pos
    -- ğŸ‰ no goals
  Â· cases' k with k
    -- âŠ¢ coeff (hermite (Nat.succ n)) Nat.zero = 0
    Â· rw [Nat.succ_add_eq_succ_add] at hnk
      -- âŠ¢ coeff (hermite (Nat.succ n)) Nat.zero = 0
      rw [coeff_hermite_succ_zero, ih hnk, neg_zero]
      -- ğŸ‰ no goals
    Â· rw [coeff_hermite_succ_succ, ih, ih, mul_zero, sub_zero]
      -- âŠ¢ Odd (n + (k + 2))
      Â· rwa [Nat.succ_add_eq_succ_add] at hnk
        -- ğŸ‰ no goals
      Â· rw [(by rw [Nat.succ_add, Nat.add_succ] : n.succ + k.succ = n + k + 2)] at hnk
        -- âŠ¢ Odd (n + k)
        exact (Nat.odd_add.mp hnk).mpr even_two
        -- ğŸ‰ no goals
#align polynomial.coeff_hermite_of_odd_add Polynomial.coeff_hermite_of_odd_add

end coeff

section CoeffExplicit

open scoped Nat

/-- Because of `coeff_hermite_of_odd_add`, every nonzero coefficient is described as follows. -/
theorem coeff_hermite_explicit :
    âˆ€ n k : â„•, coeff (hermite (2 * n + k)) k = (-1) ^ n * (2 * n - 1)â€¼ * Nat.choose (2 * n + k) k
  | 0, _ => by simp
               -- ğŸ‰ no goals
  | n + 1, 0 => by
    convert coeff_hermite_succ_zero (2 * n + 1) using 1
    -- âŠ¢ (-1) ^ (n + 1) * â†‘(2 * (n + 1) - 1)â€¼ * â†‘(Nat.choose (2 * (n + 1) + 0) 0) = - â€¦
    -- porting note: ring_nf did not solve the goal on line 165
    rw [coeff_hermite_explicit n 1, (by rw [Nat.left_distrib, mul_one, Nat.succ_sub_one] :
      2 * (n + 1) - 1 = 2 * n + 1), Nat.doubleFactorial_add_one, Nat.choose_zero_right,
      Nat.choose_one_right, pow_succ]
    push_cast
    -- âŠ¢ -1 * (-1) ^ n * ((2 * â†‘n + 1) * â†‘(2 * n - 1)â€¼) * 1 = -((-1) ^ n * â†‘(2 * n -  â€¦
    ring
    -- ğŸ‰ no goals
  | n + 1, k + 1 => by
    let hermite_explicit : â„• â†’ â„• â†’ â„¤ := fun n k =>
      (-1) ^ n * (2 * n - 1)â€¼ * Nat.choose (2 * n + k) k
    have hermite_explicit_recur :
      âˆ€ n k : â„•,
        hermite_explicit (n + 1) (k + 1) =
          hermite_explicit (n + 1) k - (k + 2) * hermite_explicit n (k + 2) := by
      intro n k
      simp only
      -- Factor out (-1)'s.
      rw [mul_comm (â†‘k + _ : â„¤), sub_eq_add_neg]
      nth_rw 3 [neg_eq_neg_one_mul]
      simp only [mul_assoc, â† mul_add, pow_succ]
      congr 2
      -- Factor out double factorials.
      norm_cast
      -- porting note: ring_nf did not solve the goal on line 186
      rw [(by rw [Nat.left_distrib, mul_one, Nat.succ_sub_one] : 2 * (n + 1) - 1 = 2 * n + 1),
        Nat.doubleFactorial_add_one, mul_comm (2 * n + 1)]
      simp only [mul_assoc, â† mul_add]
      congr 1
      -- Match up binomial coefficients using `Nat.choose_succ_right_eq`.
      rw [(by ring : 2 * (n + 1) + (k + 1) = 2 * n + 1 + (k + 1) + 1),
        (by ring : 2 * (n + 1) + k = 2 * n + 1 + (k + 1)),
        (by ring : 2 * n + (k + 2) = 2 * n + 1 + (k + 1))]
      rw [Nat.choose, Nat.choose_succ_right_eq (2 * n + 1 + (k + 1)) (k + 1), Nat.add_sub_cancel,
        Int.negSucc_eq]
      -- porting note: ring could not solve the goal so the lines 195, 198-200 were added.
      ring_nf
      simp only [sub_eq_add_neg, â† neg_mul, â† right_distrib _ _ ((-(1 : â„¤)) ^ n), â† neg_add]
      norm_cast
      simp only [â† add_assoc, add_comm]
    change _ = hermite_explicit _ _
    -- âŠ¢ coeff (hermite (2 * (n + 1) + (k + 1))) (k + 1) = hermite_explicit (n + 1) ( â€¦
    rw [â† add_assoc, coeff_hermite_succ_succ, hermite_explicit_recur]
    -- âŠ¢ coeff (hermite (2 * (n + 1) + k)) k - (â†‘k + 2) * coeff (hermite (2 * (n + 1) â€¦
    congr
    -- âŠ¢ coeff (hermite (2 * (n + 1) + k)) k = hermite_explicit (n + 1) k
    Â· rw [coeff_hermite_explicit (n + 1) k]
      -- ğŸ‰ no goals
    Â· rw [(by ring : 2 * (n + 1) + k = 2 * n + (k + 2)), coeff_hermite_explicit n (k + 2)]
      -- ğŸ‰ no goals
-- porting note: Lean 3 worked this out automatically
termination_by _ n k => (n, k)
#align polynomial.coeff_hermite_explicit Polynomial.coeff_hermite_explicit

theorem coeff_hermite_of_even_add {n k : â„•} (hnk : Even (n + k)) :
    coeff (hermite n) k = (-1) ^ ((n - k) / 2) * (n - k - 1)â€¼ * Nat.choose n k := by
  cases' le_or_lt k n with h_le h_lt
  -- âŠ¢ coeff (hermite n) k = (-1) ^ ((n - k) / 2) * â†‘(n - k - 1)â€¼ * â†‘(Nat.choose n k)
  Â· rw [Nat.even_add, â† Nat.even_sub h_le] at hnk
    -- âŠ¢ coeff (hermite n) k = (-1) ^ ((n - k) / 2) * â†‘(n - k - 1)â€¼ * â†‘(Nat.choose n k)
    obtain âŸ¨m, hmâŸ© := hnk
    -- âŠ¢ coeff (hermite n) k = (-1) ^ ((n - k) / 2) * â†‘(n - k - 1)â€¼ * â†‘(Nat.choose n k)
    -- porting note: linarith failed to find a contradiction by itself
    rw [(by linarith [by rwa [Nat.sub_eq_iff_eq_add h_le] at hm] : n = 2 * m + k),
      Nat.add_sub_cancel, Nat.mul_div_cancel_left _ (Nat.succ_pos 1), coeff_hermite_explicit]
  Â· simp [Nat.choose_eq_zero_of_lt h_lt, coeff_hermite_of_lt h_lt]
    -- ğŸ‰ no goals
#align polynomial.coeff_hermite_of_even_add Polynomial.coeff_hermite_of_even_add

theorem coeff_hermite (n k : â„•) :
    coeff (hermite n) k =
      if Even (n + k) then (-1 : â„¤) ^ ((n - k) / 2) * (n - k - 1)â€¼ * Nat.choose n k else 0 := by
  split_ifs with h
  -- âŠ¢ coeff (hermite n) k = (-1) ^ ((n - k) / 2) * â†‘(n - k - 1)â€¼ * â†‘(Nat.choose n k)
  exact coeff_hermite_of_even_add h
  -- âŠ¢ coeff (hermite n) k = 0
  exact coeff_hermite_of_odd_add (Nat.odd_iff_not_even.mpr h)
  -- ğŸ‰ no goals
#align polynomial.coeff_hermite Polynomial.coeff_hermite

end CoeffExplicit

end Polynomial
