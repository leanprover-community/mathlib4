/-
Copyright (c) 2020 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/
import Mathlib.RingTheory.Valuation.Basic

/-!
# Ring of integers under a given valuation

The elements with valuation less than or equal to 1.

TODO: Define characteristic predicate.
-/

open Set

universe u v w

namespace Valuation

section Ring

variable {R : Type u} {Œì‚ÇÄ : Type v} [Ring R] [LinearOrderedCommGroupWithZero Œì‚ÇÄ]
variable (v : Valuation R Œì‚ÇÄ)

/-- The ring of integers under a given valuation is the subring of elements with valuation ‚â§ 1. -/
def integer : Subring R where
  carrier := { x | v x ‚â§ 1 }
  one_mem' := le_of_eq v.map_one
  mul_mem' {x y} hx hy := by simp only [Set.mem_setOf_eq, map_mul, mul_le_one' hx hy]
  zero_mem' := by simp only [Set.mem_setOf_eq, map_zero, zero_le']
  add_mem' {x y} hx hy := le_trans (v.map_add x y) (max_le hx hy)
  neg_mem' {x} hx := by simp only [Set.mem_setOf_eq] at hx; simpa only [Set.mem_setOf_eq, map_neg]

lemma mem_integer_iff (r : R) : r ‚àà v.integer ‚Üî v r ‚â§ 1 := by rfl

end Ring

section CommRing

variable {R : Type u} {Œì‚ÇÄ : Type v} [CommRing R] [LinearOrderedCommGroupWithZero Œì‚ÇÄ]
variable (v : Valuation R Œì‚ÇÄ)
variable (O : Type w) [CommRing O] [Algebra O R]

/-- Given a valuation v : R ‚Üí Œì‚ÇÄ and a ring homomorphism O ‚Üí+* R, we say that O is the integers of v
if f is injective, and its range is exactly `v.integer`. -/
structure Integers : Prop where
  hom_inj : Function.Injective (algebraMap O R)
  map_le_one : ‚àÄ x, v (algebraMap O R x) ‚â§ 1
  exists_of_le_one : ‚àÄ ‚¶Ér‚¶Ñ, v r ‚â§ 1 ‚Üí ‚àÉ x, algebraMap O R x = r

-- typeclass shortcut
instance : Algebra v.integer R :=
  Algebra.ofSubring v.integer

theorem integer.integers : v.Integers v.integer :=
  { hom_inj := Subtype.coe_injective
    map_le_one := fun r => r.2
    exists_of_le_one := fun r hr => ‚ü®‚ü®r, hr‚ü©, rfl‚ü© }

namespace Integers

variable {v O}

theorem one_of_isUnit' {x : O} (hx : IsUnit x) (H : ‚àÄ x, v (algebraMap O R x) ‚â§ 1) :
    v (algebraMap O R x) = 1 :=
  let ‚ü®u, hu‚ü© := hx
  le_antisymm (H _) <| by
    grw [‚Üê v.map_one, ‚Üê (algebraMap O R).map_one, ‚Üê u.mul_inv, ‚Üê mul_one (v (algebraMap O R x)), hu,
      (algebraMap O R).map_mul, v.map_mul, H (u‚Åª¬π : Units O)]

theorem one_of_isUnit (hv : Integers v O) {x : O} (hx : IsUnit x) : v (algebraMap O R x) = 1 :=
  one_of_isUnit' hx hv.map_le_one

/--
Let `O` be the integers of the valuation `v` on some commutative ring `R`. For every element `x` in
`O`, `x` is a unit in `O` if and only if the image of `x` in `R` is a unit and has valuation 1.
-/
theorem isUnit_of_one (hv : Integers v O) {x : O} (hx : IsUnit (algebraMap O R x))
    (hvx : v (algebraMap O R x) = 1) : IsUnit x :=
  let ‚ü®u, hu‚ü© := hx
  have h1 : v u ‚â§ 1 := hu.symm ‚ñ∏ hv.2 x
  have h2 : v (u‚Åª¬π : RÀ£) ‚â§ 1 := by
    rw [‚Üê one_mul (v _), ‚Üê hvx, ‚Üê v.map_mul, ‚Üê hu, u.mul_inv, hu, hvx, v.map_one]
  let ‚ü®r1, hr1‚ü© := hv.3 h1
  let ‚ü®r2, hr2‚ü© := hv.3 h2
  ‚ü®‚ü®r1, r2, hv.1 <| by rw [RingHom.map_mul, RingHom.map_one, hr1, hr2, Units.mul_inv],
      hv.1 <| by rw [RingHom.map_mul, RingHom.map_one, hr1, hr2, Units.inv_mul]‚ü©,
    hv.1 <| hr1.trans hu‚ü©

theorem le_of_dvd (hv : Integers v O) {x y : O} (h : x ‚à£ y) :
    v (algebraMap O R y) ‚â§ v (algebraMap O R x) := by
  obtain ‚ü®z, rfl‚ü© := h
  grw [‚Üê mul_one (v (algebraMap O R x)), RingHom.map_mul, v.map_mul, hv.2 z]

lemma nontrivial_iff (hv : v.Integers O) : Nontrivial O ‚Üî Nontrivial R := by
  constructor <;> intro h
  ¬∑ exact hv.hom_inj.nontrivial
  ¬∑ obtain ‚ü®o0, ho0‚ü© := hv.exists_of_le_one (r := 0) (by simp)
    obtain ‚ü®o1, ho1‚ü© := hv.exists_of_le_one (r := 1) (by simp)
    refine ‚ü®o0, o1, ?_‚ü©
    rintro rfl
    simp [ho1] at ho0

end Integers

lemma integers_nontrivial (v : Valuation R Œì‚ÇÄ) :
    Nontrivial v.integer ‚Üî Nontrivial R :=
  (Valuation.integer.integers v).nontrivial_iff

end CommRing

section Field

variable {F : Type u} {Œì‚ÇÄ : Type v} [Field F] [LinearOrderedCommGroupWithZero Œì‚ÇÄ]
variable {v : Valuation F Œì‚ÇÄ} {O : Type w} [CommRing O] [Algebra O F]

namespace Integers

theorem dvd_of_le (hv : Integers v O) {x y : O}
    (h : v (algebraMap O F x) ‚â§ v (algebraMap O F y)) : y ‚à£ x :=
  by_cases
    (fun hy : algebraMap O F y = 0 =>
      have hx : x = 0 :=
        hv.1 <|
          (algebraMap O F).map_zero.symm ‚ñ∏ (v.zero_iff.1 <| le_zero_iff.1 (v.map_zero ‚ñ∏ hy ‚ñ∏ h))
      hx.symm ‚ñ∏ dvd_zero y)
    fun hy : algebraMap O F y ‚â† 0 =>
    have : v ((algebraMap O F y)‚Åª¬π * algebraMap O F x) ‚â§ 1 := by
      grw [‚Üê v.map_one, ‚Üê inv_mul_cancel‚ÇÄ hy, v.map_mul, v.map_mul, h]
    let ‚ü®z, hz‚ü© := hv.3 this
    ‚ü®z, hv.1 <| ((algebraMap O F).map_mul y z).symm ‚ñ∏ hz.symm ‚ñ∏ (mul_inv_cancel_left‚ÇÄ hy _).symm‚ü©

theorem dvd_iff_le (hv : Integers v O) {x y : O} :
    x ‚à£ y ‚Üî v (algebraMap O F y) ‚â§ v (algebraMap O F x) :=
  ‚ü®hv.le_of_dvd, hv.dvd_of_le‚ü©

theorem le_iff_dvd (hv : Integers v O) {x y : O} :
    v (algebraMap O F x) ‚â§ v (algebraMap O F y) ‚Üî y ‚à£ x :=
  ‚ü®hv.dvd_of_le, hv.le_of_dvd‚ü©

/--
This is the special case of `Valuation.Integers.isUnit_of_one` when the valuation is defined
over a field. Let `v` be a valuation on some field `F` and `O` be its integers. For every element
`x` in `O`, `x` is a unit in `O` if and only if the image of `x` in `F` has valuation 1.
-/
theorem isUnit_of_one' (hv : Integers v O) {x : O} (hvx : v (algebraMap O F x) = 1) : IsUnit x := by
  refine isUnit_of_one hv (IsUnit.mk0 _ ?_) hvx
  simp only [‚Üê v.ne_zero_iff, hvx, ne_eq, one_ne_zero, not_false_eq_true]

lemma isUnit_iff_valuation_eq_one (hv : Integers v O) {x : O} :
    IsUnit x ‚Üî v (algebraMap O F x) = 1 :=
  ‚ü®hv.one_of_isUnit, hv.isUnit_of_one'‚ü©

lemma valuation_irreducible_lt_one (hv : Integers v O) {œñ : O} (h : Irreducible œñ) :
    v (algebraMap O F œñ) < 1 :=
  lt_of_le_of_ne (hv.map_le_one œñ) (mt hv.isUnit_iff_valuation_eq_one.mpr h.not_isUnit)

lemma valuation_unit (hv : Integers v O) (x : OÀ£) :
    v (algebraMap O F x) = 1 := by
  simp [‚Üê hv.isUnit_iff_valuation_eq_one]

lemma valuation_pos_iff_ne_zero (hv : Integers v O) {x : O} :
    0 < v (algebraMap O F x) ‚Üî x ‚â† 0 := by
  rw [‚Üê not_le]
  refine not_congr ?_
  simp [map_eq_zero_iff _ hv.hom_inj]

lemma valuation_irreducible_pos (hv : Integers v O) {œñ : O} (h : Irreducible œñ) :
    0 < v (algebraMap O F œñ) :=
  hv.valuation_pos_iff_ne_zero.mpr h.ne_zero

theorem dvdNotUnit_iff_lt (hv : Integers v O) {x y : O} :
    DvdNotUnit x y ‚Üî v (algebraMap O F y) < v (algebraMap O F x) := by
  rw [lt_iff_le_not_ge, hv.le_iff_dvd, hv.le_iff_dvd]
  refine ‚ü®?_, And.elim dvdNotUnit_of_dvd_of_not_dvd‚ü©
  rintro ‚ü®hx0, d, hdu, rfl‚ü©
  refine ‚ü®‚ü®d, rfl‚ü©, ?_‚ü©
  rw [hv.isUnit_iff_valuation_eq_one, ‚Üê ne_eq, ne_iff_lt_iff_le.mpr (hv.map_le_one d)] at hdu
  rw [dvd_iff_le hv]
  simp only [map_mul, not_le]
  contrapose! hdu
  refine one_le_of_le_mul_left‚ÇÄ ?_ hdu
  simp [hv.valuation_pos_iff_ne_zero, hx0]

theorem eq_algebraMap_or_inv_eq_algebraMap (hv : Integers v O) (x : F) :
    ‚àÉ a : O, x = algebraMap O F a ‚à® x‚Åª¬π = algebraMap O F a := by
  rcases val_le_one_or_val_inv_le_one v x with h | h <;>
  obtain ‚ü®a, ha‚ü© := exists_of_le_one hv h
  exacts [‚ü®a, Or.inl ha.symm‚ü©, ‚ü®a, Or.inr ha.symm‚ü©]

lemma coe_span_singleton_eq_setOf_le_v_algebraMap (hv : Integers v O) (x : O) :
    (Ideal.span {x} : Set O) = {y : O | v (algebraMap O F y) ‚â§ v (algebraMap O F x)} := by
  rcases eq_or_ne x 0 with rfl | hx
  ¬∑ simp [Set.singleton_zero, Ideal.span_zero, map_eq_zero_iff _ hv.hom_inj]
  ext
  simp [SetLike.mem_coe, Ideal.mem_span_singleton, hv.dvd_iff_le]

lemma bijective_algebraMap_of_subsingleton_units_mrange (hv : Integers v O)
    [Subsingleton (MonoidHom.mrange v)À£] :
    Function.Bijective (algebraMap O F) := by
  refine ‚ü®hv.hom_inj, fun x ‚Ü¶ hv.exists_of_le_one ?_‚ü©
  rcases eq_or_ne x 0 with rfl | hx
  ¬∑ simp
  ¬∑ exact (congr_arg Units.val (Subsingleton.elim (Œ± := (MonoidHom.mrange v)À£)
      ((isUnit_iff_ne_zero.mpr hx).unit.map v.toMonoidHom.mrangeRestrict) 1)).le

lemma isPrincipal_iff_exists_isGreatest (hv : Integers v O) {I : Ideal O} :
    I.IsPrincipal ‚Üî ‚àÉ x, IsGreatest (v ‚àò algebraMap O F '' I) x := by
  constructor <;> rintro ‚ü®x, hx‚ü©
  ¬∑ refine ‚ü®(v ‚àò algebraMap O F) x, ?_, ?_‚ü©
    ¬∑ refine Set.mem_image_of_mem _ ?_
      simp [hx, Ideal.mem_span_singleton_self]
    ¬∑ intro y hy
      simp only [Function.comp_apply, hx, Ideal.submodule_span_eq, Set.mem_image,
        SetLike.mem_coe, Ideal.mem_span_singleton] at hy
      obtain ‚ü®y, hy, rfl‚ü© := hy
      exact le_of_dvd hv hy
  ¬∑ obtain ‚ü®a, ha, rfl‚ü© : ‚àÉ a ‚àà I, (v ‚àò algebraMap O F) a = x := by simpa using hx.left
    refine ‚ü®a, ?_‚ü©
    ext b
    simp only [Ideal.submodule_span_eq, Ideal.mem_span_singleton]
    exact ‚ü®fun hb ‚Ü¶ dvd_of_le hv (hx.2 <| mem_image_of_mem _ hb), fun hb ‚Ü¶ I.mem_of_dvd hb ha‚ü©

lemma isPrincipal_iff_exists_eq_setOf_valuation_le (hv : Integers v O) {I : Ideal O} :
    I.IsPrincipal ‚Üî ‚àÉ x, (I : Set O) = {y | v (algebraMap O F y) ‚â§ v (algebraMap O F x)} := by
  rw [isPrincipal_iff_exists_isGreatest hv]
  constructor <;> rintro ‚ü®x, hx‚ü©
  ¬∑ obtain ‚ü®a, ha, rfl‚ü© : ‚àÉ a ‚àà I, (v ‚àò algebraMap O F) a = x := by simpa using hx.left
    refine ‚ü®a, ?_‚ü©
    ext b
    simp only [SetLike.mem_coe, mem_setOf_eq]
    constructor <;> intro h
    ¬∑ exact hx.right (Set.mem_image_of_mem _ h)
    ¬∑ rw [le_iff_dvd hv] at h
      exact Ideal.mem_of_dvd I h ha
  ¬∑ refine ‚ü®v (algebraMap O F x), Set.mem_image_of_mem _ ?_, ?_‚ü©
    ¬∑ simp [hx]
    ¬∑ simp [hx, mem_upperBounds]

lemma not_denselyOrdered_of_isPrincipalIdealRing [IsPrincipalIdealRing O] (hv : Integers v O) :
    ¬¨ DenselyOrdered (range v) := by
  intro H
  -- nonunits as an ideal isn't defined here, nor shown to be equivalent to `v x < 1`
  set I : Ideal O := {
    carrier := v ‚àò algebraMap O F ‚Åª¬π' Iio (1 : Œì‚ÇÄ)
    add_mem' := fun {a b} ha hb ‚Ü¶ by simpa using map_add_lt v ha hb
    zero_mem' := by simp
    smul_mem' := by
      intro c x
      simp only [mem_preimage, Function.comp_apply, mem_Iio, smul_eq_mul, map_mul]
      intro hx
      exact Right.mul_lt_one_of_le_of_lt (hv.map_le_one c) hx
  }
  obtain ‚ü®x, hx‚ÇÅ, hx‚ü© :
    ‚àÉ x, v (algebraMap O F x) < 1 ‚àß
      v (algebraMap O F x) ‚àà upperBounds (Iio 1 ‚à© range (v ‚àò algebraMap O F)) := by
    simpa [I, IsGreatest, hv.isPrincipal_iff_exists_isGreatest, ‚Üê image_preimage_eq_inter_range]
      using IsPrincipalIdealRing.principal I
  obtain ‚ü®y, hy, hy‚ÇÅ‚ü© : ‚àÉ y, v (algebraMap O F x) < v y ‚àß v y < 1 := by
    simpa only [Subtype.exists, Subtype.mk_lt_mk, exists_range_iff, exists_prop]
      using H.dense ‚ü®v (algebraMap O F x), mem_range_self _‚ü© ‚ü®1, 1, v.map_one‚ü© hx‚ÇÅ
  obtain ‚ü®z, rfl‚ü© := hv.exists_of_le_one hy‚ÇÅ.le
  exact hy.not_ge <| hx ‚ü®hy‚ÇÅ, mem_range_self _‚ü©

end Integers

open Integers in
theorem Integer.not_isUnit_iff_valuation_lt_one {x : v.integer} : ¬¨IsUnit x ‚Üî v x < 1 := by
  rw [‚Üê not_le, not_iff_not, isUnit_iff_valuation_eq_one (F := F) (Œì‚ÇÄ := Œì‚ÇÄ),
    le_antisymm_iff]
  exacts [and_iff_right x.2, integer.integers v]

namespace integer

lemma v_irreducible_lt_one {œñ : v.integer} (h : Irreducible œñ) :
    v œñ < 1 :=
  (Valuation.integer.integers v).valuation_irreducible_lt_one h

lemma v_irreducible_pos {œñ : v.integer} (h : Irreducible œñ) : 0 < v œñ :=
  (Valuation.integer.integers v).valuation_irreducible_pos h

lemma coe_span_singleton_eq_setOf_le_v_coe (x : v.integer) :
    (Ideal.span {x} : Set v.integer) = {y : v.integer | v y ‚â§ v x} :=
  (Valuation.integer.integers v).coe_span_singleton_eq_setOf_le_v_algebraMap x

end integer

end Field

section Ideal

variable {R : Type u} {Œì‚ÇÄ : Type v} [Ring R] [LinearOrderedCommGroupWithZero Œì‚ÇÄ]
variable (v : Valuation R Œì‚ÇÄ)
local notation "ùìû" => v.integer

/-- The `v.integer`-submodule of `R` of elements whose valuation is less than or equal to a
certain value. -/
def leSubmodule (Œ≥ : Œì‚ÇÄ) : Submodule ùìû R where
  __ := leAddSubgroup v Œ≥
  smul_mem' r x h := by
    simpa [Subring.smul_def] using mul_le_of_le_one_of_le r.prop h

/-- The `v.integer`-submodule of `R` of elements whose valuation is less than a certain unit. -/
def ltSubmodule (Œ≥ : Œì‚ÇÄÀ£) : Submodule ùìû R where
  __ := ltAddSubgroup v Œ≥
  smul_mem' r x h := by
    simpa [Subring.smul_def] using mul_lt_of_le_one_of_lt r.prop h

lemma leSubmodule_monotone : Monotone (leSubmodule v) :=
  leAddSubgroup_monotone v

lemma ltSubmodule_monotone : Monotone (ltSubmodule v) :=
  ltAddSubgroup_monotone v

lemma ltSubmodule_le_leSubmodule (Œ≥ : Œì‚ÇÄÀ£) :
    ltSubmodule v Œ≥ ‚â§ leSubmodule v (Œ≥ : Œì‚ÇÄ) :=
  ltAddSubgroup_le_leAddSubgroup v Œ≥

variable {v} in
@[simp]
lemma mem_leSubmodule_iff {Œ≥ : Œì‚ÇÄ} {x : R} :
    x ‚àà leSubmodule v Œ≥ ‚Üî v x ‚â§ Œ≥ :=
  Iff.rfl

variable {v} in
@[simp]
lemma mem_ltSubmodule_iff {Œ≥ : Œì‚ÇÄÀ£} {x : R} :
    x ‚àà ltSubmodule v Œ≥ ‚Üî v x < Œ≥ :=
  Iff.rfl

@[simp]
lemma leSubmodule_zero (K : Type*) [Field K] (v : Valuation K Œì‚ÇÄ) :
    leSubmodule v (0 : Œì‚ÇÄ) = ‚ä• := by
  ext; simp

lemma leSubmodule_v_le_of_mem {K : Type*} [Field K] (v : Valuation K Œì‚ÇÄ)
    {S : Submodule v.integer K} {x : K} (hx : x ‚àà S) :
    leSubmodule v (v x) ‚â§ S := by
  rcases eq_or_ne x 0 with rfl | hx0
  ¬∑ simp
  intro y hy
  have : v ((y : K) / x) ‚â§ 1 := by simp [div_le_one_of_le‚ÇÄ hy]
  simpa [Subring.smul_def, div_mul_cancel‚ÇÄ _ hx0] using S.smul_mem ‚ü®_, this‚ü© hx

lemma ltSubmodule_v_le_of_mem {K : Type*} [Field K] {v : Valuation K Œì‚ÇÄ}
    {S : Submodule v.integer K} {x : K} (hx : x ‚àà S) (hxv : v x ‚â† 0) :
    ltSubmodule v (Units.mk0 _ hxv) ‚â§ S :=
  (leSubmodule_v_le_of_mem v hx).trans' (ltSubmodule_le_leSubmodule _ _)

-- the ideals do not use the submodules due to `Submodule.comap _ (Algebra.linearMap _ _)`
-- requiring commutativity

/-- The ideal of elements of the valuation subring whose valuation is less than or equal to a
certain value. -/
def leIdeal (Œ≥ : Œì‚ÇÄ) : Ideal ùìû where
  __ := AddSubgroup.addSubgroupOf (leAddSubgroup v Œ≥) v.integer.toAddSubgroup
  smul_mem' r x h :=
    -- need to specify the subgroup, it is not inferred otherwise
    (AddSubgroup.mem_addSubgroupOf (K := v.integer.toAddSubgroup)).mpr <| by
      simpa using mul_le_of_le_one_of_le r.prop h

/-- The ideal of elements of the valuation subring whose valuation is less than a certain unit. -/
def ltIdeal (Œ≥ : Œì‚ÇÄÀ£) : Ideal ùìû where
  __ := AddSubgroup.addSubgroupOf (ltAddSubgroup v Œ≥) v.integer.toAddSubgroup
  smul_mem' r x h := by
    change v ((r : R) * x) < Œ≥ -- not sure why simp can't get us to here
    simpa [Subring.smul_def] using mul_lt_of_le_one_of_lt r.prop h

-- Can't use `leAddSubgroup` because `addSubgroupOf` is a dependent function
lemma leIdeal_mono : Monotone (leIdeal v) :=
  fun _ _ h _ ‚Ü¶ h.trans'

lemma ltIdeal_mono : Monotone (ltIdeal v) :=
  fun _ _ h _ ‚Ü¶ (Units.val_le_val.mpr h).trans_lt'

lemma ltIdeal_le_leIdeal (Œ≥ : Œì‚ÇÄÀ£) :
    ltIdeal v Œ≥ ‚â§ leIdeal v (Œ≥ : Œì‚ÇÄ) :=
  fun _ h ‚Ü¶ h.le

variable {v} in
@[simp]
lemma mem_leIdeal_iff {Œ≥ : Œì‚ÇÄ} {x : ùìû} :
    x ‚àà leIdeal v Œ≥ ‚Üî v (x : R) ‚â§ Œ≥ :=
  Iff.rfl

variable {v} in
@[simp]
lemma mem_ltIdeal_iff {Œ≥ : Œì‚ÇÄÀ£} {x : ùìû} :
    x ‚àà ltIdeal v Œ≥ ‚Üî v (x : R) < Œ≥ :=
  Iff.rfl

@[simp]
lemma leIdeal_zero (K : Type*) [Field K] (v : Valuation K Œì‚ÇÄ) :
    leIdeal v (0 : Œì‚ÇÄ) = ‚ä• := by
  ext; simp

lemma leSubmodule_comap_algebraMap_eq_leIdeal {K : Type*} [Field K] (v : Valuation K Œì‚ÇÄ) (Œ≥ : Œì‚ÇÄ) :
    (leSubmodule v Œ≥).comap (Algebra.linearMap _ _) = leIdeal v Œ≥ :=
  Submodule.ext fun _ ‚Ü¶ Iff.rfl

lemma leIdeal_map_algebraMap_eq_leSubmodule_min {K : Type*} [Field K] (v : Valuation K Œì‚ÇÄ)
    (Œ≥ : Œì‚ÇÄ) :
    Submodule.map (Algebra.linearMap _ _) (leIdeal v Œ≥) = leSubmodule v (min 1 Œ≥) := by
  ext x
  simp only [Submodule.mem_map, mem_leIdeal_iff, Algebra.linearMap_apply, mem_leSubmodule_iff]
  constructor
  ¬∑ rintro ‚ü®y, hy, rfl‚ü©
    rcases min_cases 1 Œ≥ with ‚ü®h, _‚ü© | ‚ü®h, _‚ü©
    ¬∑ rw [h]
      exact y.prop
    ¬∑ rw [h]
      exact hy
  ¬∑ intro hx
    rcases min_cases 1 Œ≥ with ‚ü®h, h'‚ü© | ‚ü®h, h'‚ü© <;> rw [h] at hx
    ¬∑ exact ‚ü®‚ü®x, hx‚ü©, hx.trans h', rfl‚ü©
    ¬∑ exact ‚ü®‚ü®x, hx.trans h'.le‚ü©, hx, rfl‚ü©

-- Ideally, this would follow from `leSubmodule_v_le_of_mem`
lemma leIdeal_v_le_of_mem {K : Type*} [Field K] (v : Valuation K Œì‚ÇÄ)
    {I : Ideal v.integer} {x : v.integer} (hx : x ‚àà I) :
    leIdeal v (v (x : K)) ‚â§ I := by
  rcases eq_or_ne x 0 with rfl | hx0
  ¬∑ simp
  intro y hy
  have : v ((y : K) / x) ‚â§ 1 := by simpa using div_le_one_of_le‚ÇÄ hy zero_le'
  convert I.smul_mem ‚ü®_, this‚ü© hx using 1
  simp [Subtype.ext_iff, div_mul_cancel‚ÇÄ _ (ZeroMemClass.coe_eq_zero.not.mpr hx0)]

lemma ltIdeal_v_le_of_mem {K : Type*} [Field K] {v : Valuation K Œì‚ÇÄ}
    {I : Ideal v.integer} {x : v.integer} (hx : x ‚àà I) (hxv : v (x : K) ‚â† 0) :
    ltIdeal v (Units.mk0 _ hxv) ‚â§ I :=
  (leIdeal_v_le_of_mem v hx).trans' (ltIdeal_le_leIdeal _ _)

end Ideal

end Valuation
