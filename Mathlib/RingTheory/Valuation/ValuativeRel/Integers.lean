/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.GroupTheory.ArchimedeanDensely
import Mathlib.Order.CompletePartialOrder
import Mathlib.Topology.Algebra.Nonarchimedean.AdicTopology
import Mathlib.Topology.Algebra.Valued.ValuativeRel

/-!

# Ring of integers for `ValuativeRel`

## Main results
- `ValuativeRel.IsIntegers`: A predicate on `[ValuativeRel R]` saying that is the `R` is the ring
  of integers.
- `ValuativeRel.isValuativeTopology_iff_isAdic`: If `R` is the ring of integers of a discrete
  rank one valuation, then the valuative topology and the adic topology coincides.

-/

variable {R : Type*} [CommRing R] [ValuativeRel R]

open Valuation Topology

namespace ValuativeRel

/--
Given a commutative `R` with a valuation `v` on `R`, `R` is the ring of integers of `v` if
`v b ‚â§ v a ‚Üî b ‚à£ a`.

To say that `R` is the ring of integers of `(K, v)`, one would say
`[IsFractionRing R K] [ValuativeRel R] [ValuativeExtension R K] [IsIntegers R]`
-/
class IsIntegers (R : Type*) [CommRing R] [ValuativeRel R] : Prop where
  rel_one (a : R) : a ‚â§·µ• 1
  dvd_of_rel {a b : R} : a ‚â§·µ• b ‚Üí b ‚à£ a

lemma rel_iff_dvd [IsIntegers R] {a b : R} :
    a ‚â§·µ• b ‚Üî b ‚à£ a := by
  refine ‚ü®IsIntegers.dvd_of_rel, ?_‚ü©
  rintro ‚ü®c, rfl‚ü©
  simpa using rel_mul_left b (IsIntegers.rel_one c)

instance (priority := 50) [IsIntegers R] : PreValuationRing R :=
  PreValuationRing.iff_dvd_total.mpr ‚ü®fun a b ‚Ü¶ by
    simpa [rel_iff_dvd] using rel_total b a‚ü©

instance (priority := 50) [IsIntegers R] : Nontrivial R where
  exists_pair_ne :=
    ‚ü®0, 1, fun e ‚Ü¶ not_rel_one_zero (R := R) (e ‚ñ∏ rel_rfl)‚ü©

instance (priority := 50) [IsIntegers R] [IsDomain R] : ValuationRing R where

instance (priority := 50) [IsIntegers R] [IsDomain R]
    (K : Type*) [Field K] [Algebra R K] [IsFractionRing R K] :
    ((ValuationRing.valuation R K).comap (algebraMap R K)).Compatible where
  rel_iff_le x y := by
    simp_rw [rel_iff_dvd, dvd_def, @eq_comm _ x, mul_comm y]
    change _ ‚Üî ‚àÉ _, _
    simp [Algebra.smul_def, ‚Üê map_mul]

lemma isUnit_iff [IsIntegers R] {x : R} :
    IsUnit x ‚Üî 1 ‚â§·µ• x := by
  rw [isUnit_iff_dvd_one, rel_iff_dvd]

lemma mem_maximalIdeal [IsIntegers R] {x} :
    x ‚àà IsLocalRing.maximalIdeal R ‚Üî ¬¨ 1 ‚â§·µ• x := by
  rw [‚Üê isUnit_iff, IsLocalRing.mem_maximalIdeal, mem_nonunits_iff]

lemma IsIntegers.range_valuation [IsIntegers R] :
    Set.range (valuation R) = Set.Iic 1 := by
  apply subset_antisymm
  ¬∑ rintro _ ‚ü®x, rfl‚ü©
    rw [Set.mem_Iic, ‚Üê (valuation R).map_one, ‚Üê Compatible.rel_iff_le]
    exact rel_one _
  ¬∑ rintro x (hx : x ‚â§ 1)
    obtain ‚ü®a, b, rfl‚ü© := valuation_surjective x
    rw [div_le_one‚ÇÄ (by simp), ‚Üê Compatible.rel_iff_le, rel_iff_dvd] at hx
    obtain ‚ü®c, rfl‚ü© := hx
    simp

lemma mem_maximalIdeal_iff_valuation_le
    [IsIntegers R] [IsDiscrete R] {x} :
    x ‚àà IsLocalRing.maximalIdeal R ‚Üî valuation R x ‚â§ uniformizer R := by
  rw [le_uniformizer_iff, mem_maximalIdeal, ‚Üê (valuation R).map_one,
    lt_iff_le_not_ge, ‚Üê Compatible.rel_iff_le, ‚Üê Compatible.rel_iff_le,
    and_iff_right (IsIntegers.rel_one _)]

lemma mem_maximalIdeal_pow [IsIntegers R] [IsDiscrete R] {x n} :
    x ‚àà IsLocalRing.maximalIdeal R ^ n ‚Üî valuation R x ‚â§ uniformizer R ^ n := by
  constructor
  ¬∑ intro hx
    induction n generalizing x with
    | zero =>
      simp_rw [pow_zero, ‚Üê (valuation R).map_one, ‚Üê Compatible.rel_iff_le, IsIntegers.rel_one]
    | succ n IH =>
      rw [pow_succ] at hx
      refine Submodule.mul_induction_on hx (fun y hy z hz ‚Ü¶ ?_) (fun y z ‚Ü¶ map_add_le _)
      rw [map_mul, pow_succ]
      rw [mem_maximalIdeal_iff_valuation_le] at hz
      exact mul_le_mul (IH hy) hz zero_le' zero_le'
  ¬∑ obtain ‚ü®œñ, hœñ‚ü© := IsIntegers.range_valuation (R := R).ge (uniformizer_lt_one.le)
    intro hx
    rw [‚Üê hœñ, ‚Üê map_pow, ‚Üê Compatible.rel_iff_le, rel_iff_dvd] at hx
    obtain ‚ü®x, rfl‚ü© := hx
    refine Ideal.mul_mem_right _ _ (Ideal.pow_mem_pow ?_ _)
    rw [mem_maximalIdeal_iff_valuation_le, hœñ]

open Topology in
lemma isValuativeTopology_iff_isAdic [IsIntegers R] [IsNontrivial R] [IsDiscrete R] [IsRankLeOne R]
    [TopologicalSpace R] :
    IsValuativeTopology R ‚Üî IsAdic (IsLocalRing.maximalIdeal R) := by
  wlog H : IsTopologicalRing R
  ¬∑ constructor <;> intro h
    ¬∑ cases (H inferInstance)
    ¬∑ have : NonarchimedeanRing R := h ‚ñ∏ (IsLocalRing.maximalIdeal R).nonarchimedean
      cases (H inferInstance)
  rw [IsValuativeTopology.iff_hasBasis_nhds_zero, isAdic_iff_hasBasis_zero]
  change _ ‚Üî (ùìù 0).HasBasis _ ({ x | x ‚àà IsLocalRing.maximalIdeal R ^ ¬∑ })
  simp_rw [mem_maximalIdeal_pow]
  refine Filter.HasBasis.to_hasBasis_iff (fun Œ≥ _ ‚Ü¶ ?_) (fun n _ ‚Ü¶ ?_)
  ¬∑ obtain ‚ü®n, hn‚ü© := exists_pow_lt‚ÇÄ (uniformizer_lt_one) Œ≥
    exact ‚ü®n, trivial, fun x hx ‚Ü¶ hx.trans_lt hn‚ü©
  ¬∑ exact ‚ü®.mk0 (uniformizer R ^ n) (by simp [uniformizer_pos.ne']),
      trivial, by simp +contextual [le_of_lt]‚ü©

end ValuativeRel
