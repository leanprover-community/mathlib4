/-
Copyright (c) 2025 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import Mathlib.RingTheory.Valuation.Basic
import Mathlib.Topology.Basic
import Mathlib.Data.NNReal.Basic

/-!

# Valuative Relations

In this file we introduce a class called `ValuativeRel R` for a ring `R`.
This bundles a relation `rel : R â†’ R â†’ Prop` on `R` which mimics a
preorder on `R` arising from a valuation.
We introduce the notation `x âˆ£áµ¥ y` for this relation.

Recall that the equivalence class of a valuation is *completely* characterized by
such a preorder. Thus, we can think of `ValuativeRel R` as a way of
saying that `R` is endowed with an equivalence class of a valuation.
-/

noncomputable section

/-- The class `[ValuativeRel R]` class introduces an operator `x âˆ£áµ¥ y : Prop` for `x y : R`
which is the natural relation arising from an equivalence class of a valuation on `R`.
Use this class to talk about the case where `R` is equipped with an equivalence class
of a valuation. -/
class ValuativeRel (R : Type*) [CommRing R] where
  /-- The relation operator arising from `ValuativeRel`. -/
  rel : R â†’ R â†’ Prop
  refl (x : R) : rel x x
  trans {x y z : R} : rel x y â†’ rel y z â†’ rel x z
  rel_mul_mul_of_rel_of_rel (x y x' y' : R) : rel x y â†’ rel x' y' â†’ rel (x * x') (y * y')
  rel_total (x y) : rel x y âˆ¨ rel y x
  rel_zero (x) : rel 0 x
  rel_add_of_rel_of_rel (x y z) : rel x z â†’ rel y z â†’ rel (x + y) z

@[inherit_doc] infix:50  " âˆ£áµ¥ " => ValuativeRel.rel

namespace Valuation

variable {R Î“ : Type*} [CommRing R] [LinearOrderedCommMonoidWithZero Î“]
  (v : Valuation R Î“)

/-- We say that a valuation `v` is `Compatible` if the relation `x âˆ£áµ¥ y`
is equivalent to `v x â‰¤ x y`. -/
class Compatible [ValuativeRel R] where
  dvd_iff_le (x y : R) : x âˆ£áµ¥ y â†” v x â‰¤ v y

end Valuation

/-- A preorder on a ring is said to be "valuative" if it agrees with the
valuative relation. -/
class ValuativePreorder (R : Type*) [CommRing R] [ValuativeRel R] [Preorder R] where
  dvd_iff_le (x y : R) : x âˆ£áµ¥ y â†” x â‰¤ y

namespace ValuativeRel

variable {R : Type*} [CommRing R] [ValuativeRel R]

variable (R) in
/-- The setoid used to construct `ValueMonoid R`. -/
def valueSetoid : Setoid R where
  r x y := x âˆ£áµ¥ y âˆ§ y âˆ£áµ¥ x
  iseqv := by
    constructor
    Â· intro x
      let h := refl x
      exact âŸ¨h, hâŸ©
    Â· rintro _ _ âŸ¨h1,h2âŸ©
      exact âŸ¨h2,h1âŸ©
    Â· rintro _ _ _ âŸ¨h1,h2âŸ© âŸ¨h3,h4âŸ©
      exact âŸ¨trans h1 h3, ValuativeRel.trans h4 h2âŸ©

variable (R) in
/-- The "canonical" value monoid of a ring with a valuative relation. -/
def ValueMonoid := Quotient (valueSetoid R)

open Classical in
/-- The value monoid is a linearly ordered commutative monoid with zero. -/
instance : LinearOrderedCommMonoidWithZero (ValueMonoid R) where
  mul := Quotient.liftâ‚‚ (fun x y => .mk _ <| x * y) <| by
    intro aâ‚ bâ‚ aâ‚‚ bâ‚‚ âŸ¨ha1,ha2âŸ© âŸ¨hb1,hb2âŸ©
    apply Quotient.sound
    constructor <;> apply rel_mul_mul_of_rel_of_rel <;> assumption
  mul_assoc := by
    rintro âŸ¨aâŸ© âŸ¨bâŸ© âŸ¨câŸ©
    apply Quotient.sound
    simp only [mul_assoc, Setoid.refl]
  one := .mk _ 1
  one_mul := by
    rintro âŸ¨aâŸ©
    apply Quotient.sound
    simp
  mul_one := by
    rintro âŸ¨aâŸ©
    apply Quotient.sound
    simp
  npow n := Quotient.lift (fun x => .mk _ <| x^n) <| by
    intro a b h
    apply Quotient.sound
    induction n with
    | zero => simp
    | succ n hh =>
      simp_rw [pow_succ]
      cases h ; cases hh
      constructor <;> apply rel_mul_mul_of_rel_of_rel <;> assumption
  npow_zero := by
    rintro âŸ¨xâŸ©
    apply Quotient.sound
    simp only [pow_zero, Setoid.refl]
  npow_succ := by
    rintro n âŸ¨xâŸ©
    apply Quotient.sound
    simp only [pow_succ, Setoid.refl]
  mul_comm := by
    rintro âŸ¨aâŸ© âŸ¨bâŸ©
    apply Quotient.sound
    simp only [mul_comm, Setoid.refl]
  zero := Quotient.mk _ 0
  zero_mul := by
    rintro âŸ¨aâŸ©
    apply Quotient.sound
    simp only [zero_mul, Setoid.refl]
  mul_zero := by
    rintro âŸ¨aâŸ©
    apply Quotient.sound
    simp only [mul_zero, Setoid.refl]
  le := Quotient.liftâ‚‚ (fun a b => a âˆ£áµ¥ b) <| by
    rintro aâ‚ bâ‚ aâ‚‚ bâ‚‚ âŸ¨ha1,ha2âŸ© âŸ¨hb1,hb2âŸ©
    ext ; constructor
    Â· dsimp ; intro h
      refine trans ha2 ?_
      exact trans h hb1
    Â· dsimp ; intro h
      refine trans ha1 ?_
      refine trans h hb2
  le_refl := by
    rintro âŸ¨aâŸ©
    show a âˆ£áµ¥ a
    apply refl
  le_trans := by
    rintro âŸ¨aâŸ© âŸ¨bâŸ© âŸ¨câŸ© (h1 : a âˆ£áµ¥ b) (h2 : b âˆ£áµ¥ c)
    show a âˆ£áµ¥ c
    exact trans h1 h2
  le_antisymm := by
    rintro âŸ¨aâŸ© âŸ¨bâŸ© (h1 : a âˆ£áµ¥ b) (h2 : b âˆ£áµ¥ a)
    apply Quotient.sound
    exact âŸ¨h1,h2âŸ©
  le_total := by
    rintro âŸ¨aâŸ© âŸ¨bâŸ©
    show (a âˆ£áµ¥ b) âˆ¨ (b âˆ£áµ¥ a)
    apply rel_total
  toDecidableLE := inferInstance
  mul_le_mul_left := by
    rintro âŸ¨aâŸ© âŸ¨bâŸ© (h : a âˆ£áµ¥ b) âŸ¨câŸ©
    show c * a âˆ£áµ¥ c * b
    apply rel_mul_mul_of_rel_of_rel
    Â· apply refl
    Â· assumption
  mul_le_mul_right := by
    rintro âŸ¨aâŸ© âŸ¨bâŸ© (h : a âˆ£áµ¥ b) âŸ¨câŸ©
    show a * c âˆ£áµ¥ b * c
    apply rel_mul_mul_of_rel_of_rel
    Â· assumption
    Â· apply refl
  bot := Quotient.mk _ 0
  bot_le := by
    rintro âŸ¨aâŸ©
    apply rel_zero
  zero_le_one := by apply rel_zero

variable (R) in
/-- The "canonical" valuation associated to a valuative relation. -/
def valuation : Valuation R (ValueMonoid R) where
  toFun := Quotient.mk _
  map_zero' := rfl
  map_one' := rfl
  map_mul' _ _ := rfl
  map_add_le_max' := by
    intro x y
    let x' : ValueMonoid R := .mk _ x
    let y' : ValueMonoid R := .mk _ y
    set t := max x' y'
    obtain âŸ¨s,hsâŸ© : âˆƒ s : R, .mk _ s = t := Quotient.exists_rep t
    rw [â† hs]
    apply rel_add_of_rel_of_rel
    Â· suffices x' â‰¤ t by rw [â† hs] at this ; exact this
      simp only [le_sup_left, t]
    Â· suffices y' â‰¤ t by rw [â† hs] at this ; exact this
      simp only [le_sup_right, t]

instance : (valuation R).Compatible where
  dvd_iff_le _ _ := Iff.rfl

/-- Construct a valuative relation on a ring using a valuation. -/
def ofValuation
    {S Î“ : Type*} [CommRing S] [LinearOrderedCommMonoidWithZero Î“]
    (v : Valuation S Î“) : ValuativeRel S where
  rel x y := v x â‰¤ v y
  refl a := le_refl _
  trans h1 h2 := h1.trans h2
  rel_mul_mul_of_rel_of_rel x x' y y' h1 h2 := by
    simp_rw [v.map_mul]
    apply mul_le_mul
    Â· assumption
    Â· assumption
    Â· exact zero_le'
    Â· exact zero_le'
  rel_total x y := by apply le_total
  rel_zero x := by simp only [map_zero, zero_le']
  rel_add_of_rel_of_rel x y z h1 h2 := by
    refine le_trans (v.map_add x y) ?_
    simpa only [sup_le_iff] using âŸ¨h1, h2âŸ©

lemma isEquiv {Î“â‚ Î“â‚‚ : Type*}
    [LinearOrderedCommMonoidWithZero Î“â‚]
    [LinearOrderedCommMonoidWithZero Î“â‚‚]
    (vâ‚ : Valuation R Î“â‚)
    (vâ‚‚ : Valuation R Î“â‚‚)
    [vâ‚.Compatible] [vâ‚‚.Compatible] :
    vâ‚.IsEquiv vâ‚‚ := by
  intro x y
  simp_rw [â† Valuation.Compatible.dvd_iff_le]

variable (R) in
/-- An alias for endowing a ring with a preorder defined as the valuative relation. -/
def WithPreorder := R

/-- The ring instance on `WithPreorder R` arising from the ring structure on `R`. -/
instance : CommRing (WithPreorder R) := inferInstanceAs (CommRing R)

/-- The preorder on `WithPreorder R` arising from the valuative relation on `R`. -/
instance : Preorder (WithPreorder R) where
  le (x y : R) := x âˆ£áµ¥ y
  le_refl _ := refl _
  le_trans _ _ _ := trans

/-- The valutaive relation on `WithPreorder R` arising from the valuative relation on `R`.
This is defined as the preorder itself. -/
instance : ValuativeRel (WithPreorder R) where
  rel := (Â· â‰¤ Â·)
  refl := refl (R := R)
  trans := trans (R := R)
  rel_mul_mul_of_rel_of_rel := rel_mul_mul_of_rel_of_rel (R := R)
  rel_total := rel_total (R := R)
  rel_zero := rel_zero (R := R)
  rel_add_of_rel_of_rel := rel_add_of_rel_of_rel (R := R)

instance : ValuativePreorder (WithPreorder R) where
  dvd_iff_le _ _ := Iff.rfl

open NNReal in variable (R) in
/-- An auxiliary structure used to define `IsRankOne`. -/
structure RankOneStruct where
  /-- The embedding of the value monoid into the nonnegative reals. -/
  emb : ValueMonoid R â†’*â‚€ â„â‰¥0
  strictMono : StrictMono emb
  nontrivial : âˆƒ Î³ : ValueMonoid R, emb Î³ â‰  0 âˆ§ emb Î³ â‰  1

variable (R) in
/-- We say that a ring with a valuative relation is of rank one if
there exists a strictly monotone embedding of the "canonical" value monoid into
the nonnegative reals, and the image of this embedding contains some element different
from `0` and `1`. -/
class IsRankOne where
  nonempty : Nonempty (RankOneStruct R)

variable (R) in
/-- A ring with a valuative relation is discrete if its value monoid
has a maximal element `< 1`. -/
class IsDiscrete where
  has_maximal_element :
    âˆƒ Î³ : ValueMonoid R, Î³ < 1 âˆ§ (âˆ€ Î´ : ValueMonoid R, Î´ < 1 â†’ Î´ â‰¤ Î³)

end ValuativeRel

open Topology ValuativeRel in
/-- We say that a topology on `R` is valuative if the neighborhoods of `0` in `R`
are determined by the relation `Â· âˆ£áµ¥ Â·`. -/
class ValuativeTopology (R : Type*) [CommRing R] [ValuativeRel R] [TopologicalSpace R] where
  mem_nhds_iff : âˆ€ s : Set R, s âˆˆ ð“ (0 : R) â†” âˆƒ Î³ : ValueMonoid R, { x | valuation _ x < Î³ } âŠ† s

/-- If `B` is an `A` algebra and both `A` and `B` have valuative relations,
we say that `B|A` is a valuative extension if the valuative relation on `A` is
induced by the one on `B`. -/
class ValuativeExtension
    (A B : Type*)
    [CommRing A] [CommRing B]
    [ValuativeRel A] [ValuativeRel B]
    [Algebra A B] where
  dvd_iff_dvd (a b : A) : a âˆ£áµ¥ b â†” algebraMap A B a âˆ£áµ¥ algebraMap A B b
