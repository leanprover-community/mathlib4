/-
Copyright (c) 2022 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import Mathlib.RingTheory.Valuation.Integers
import Mathlib.RingTheory.Ideal.LocalRing
import Mathlib.RingTheory.Localization.FractionRing
import Mathlib.RingTheory.Localization.Integer
import Mathlib.RingTheory.DiscreteValuationRing.Basic
import Mathlib.RingTheory.Bezout
import Mathlib.Tactic.FieldSimp

#align_import ring_theory.valuation.valuation_ring from "leanprover-community/mathlib"@"c163ec99dfc664628ca15d215fce0a5b9c265b68"

/-!
# Valuation Rings

A valuation ring is a domain such that for every pair of elements `a b`, either `a` divides
`b` or vice-versa.

Any valuation ring induces a natural valuation on its fraction field, as we show in this file.
Namely, given the following instances:
`[CommRing A] [IsDomain A] [ValuationRing A] [Field K] [Algebra A K] [IsFractionRing A K]`,
there is a natural valuation `Valuation A K` on `K` with values in `value_group A K` where
the image of `A` under `algebraMap A K` agrees with `(Valuation A K).integer`.

We also provide the equivalence of the following notions for a domain `R` in `ValuationRing.tFAE`.
1. `R` is a valuation ring.
2. For each `x : FractionRing K`, either `x` or `x‚Åª¬π` is in `R`.
3. "divides" is a total relation on the elements of `R`.
4. "contains" is a total relation on the ideals of `R`.
5. `R` is a local bezout domain.

-/


universe u v w

/-- An integral domain is called a `ValuationRing` provided that for any pair
of elements `a b : A`, either `a` divides `b` or vice versa. -/
class ValuationRing (A : Type u) [CommRing A] [IsDomain A] : Prop where
  cond' : ‚àÄ a b : A, ‚àÉ c : A, a * c = b ‚à® b * c = a
#align valuation_ring ValuationRing

-- Porting note: this lemma is needed since infer kinds are unsupported in Lean 4
lemma ValuationRing.cond {A : Type u} [CommRing A] [IsDomain A] [ValuationRing A] (a b : A) :
    ‚àÉ c : A, a * c = b ‚à® b * c = a := @ValuationRing.cond' A _ _ _ _ _

namespace ValuationRing

section

variable (A : Type u) [CommRing A]
variable (K : Type v) [Field K] [Algebra A K]

/-- The value group of the valuation ring `A`. Note: this is actually a group with zero. -/
def ValueGroup : Type v := Quotient (MulAction.orbitRel AÀ£ K)
#align valuation_ring.value_group ValuationRing.ValueGroup

instance : Inhabited (ValueGroup A K) := ‚ü®Quotient.mk'' 0‚ü©

instance : LE (ValueGroup A K) :=
  LE.mk fun x y =>
    Quotient.liftOn‚ÇÇ' x y (fun a b => ‚àÉ c : A, c ‚Ä¢ b = a)
      (by
        rintro _ _ a b ‚ü®c, rfl‚ü© ‚ü®d, rfl‚ü©; ext
        constructor
        ¬∑ rintro ‚ü®e, he‚ü©; use (c‚Åª¬π : AÀ£) * e * d
          apply_fun fun t => c‚Åª¬π ‚Ä¢ t at he
          simpa [mul_smul] using he
        ¬∑ rintro ‚ü®e, he‚ü©; dsimp
          use c * e * (d‚Åª¬π : AÀ£)
          simp_rw [Units.smul_def, ‚Üê he, mul_smul]
          rw [‚Üê mul_smul _ _ b, Units.inv_mul, one_smul])

instance : Zero (ValueGroup A K) := ‚ü®Quotient.mk'' 0‚ü©

instance : One (ValueGroup A K) := ‚ü®Quotient.mk'' 1‚ü©

instance : Mul (ValueGroup A K) :=
  Mul.mk fun x y =>
    Quotient.liftOn‚ÇÇ' x y (fun a b => Quotient.mk'' <| a * b)
      (by
        rintro _ _ a b ‚ü®c, rfl‚ü© ‚ü®d, rfl‚ü©
        apply Quotient.sound'
        dsimp
        use c * d
        simp only [mul_smul, Algebra.smul_def, Units.smul_def, RingHom.map_mul, Units.val_mul]
        ring)

instance : Inv (ValueGroup A K) :=
  Inv.mk fun x =>
    Quotient.liftOn' x (fun a => Quotient.mk'' a‚Åª¬π)
      (by
        rintro _ a ‚ü®b, rfl‚ü©
        apply Quotient.sound'
        use b‚Åª¬π
        dsimp
        rw [Units.smul_def, Units.smul_def, Algebra.smul_def, Algebra.smul_def, mul_inv,
          map_units_inv])

variable [IsDomain A] [ValuationRing A] [IsFractionRing A K]

protected theorem le_total (a b : ValueGroup A K) : a ‚â§ b ‚à® b ‚â§ a := by
  rcases a with ‚ü®a‚ü©; rcases b with ‚ü®b‚ü©
  obtain ‚ü®xa, ya, hya, rfl‚ü© : ‚àÉ a b : A, _ := IsFractionRing.div_surjective a
  obtain ‚ü®xb, yb, hyb, rfl‚ü© : ‚àÉ a b : A, _ := IsFractionRing.div_surjective b
  have : (algebraMap A K) ya ‚â† 0 := IsFractionRing.to_map_ne_zero_of_mem_nonZeroDivisors hya
  have : (algebraMap A K) yb ‚â† 0 := IsFractionRing.to_map_ne_zero_of_mem_nonZeroDivisors hyb
  obtain ‚ü®c, h | h‚ü© := ValuationRing.cond (xa * yb) (xb * ya)
  ¬∑ right
    use c
    rw [Algebra.smul_def]
    field_simp
    simp only [‚Üê RingHom.map_mul, ‚Üê h]; congr 1; ring
  ¬∑ left
    use c
    rw [Algebra.smul_def]
    field_simp
    simp only [‚Üê RingHom.map_mul, ‚Üê h]; congr 1; ring
#align valuation_ring.le_total ValuationRing.le_total

-- Porting note: it is much faster to split the instance `LinearOrderedCommGroupWithZero`
-- into two parts
noncomputable instance linearOrder : LinearOrder (ValueGroup A K) where
  le_refl := by rintro ‚ü®‚ü©; use 1; rw [one_smul]
  le_trans := by rintro ‚ü®a‚ü© ‚ü®b‚ü© ‚ü®c‚ü© ‚ü®e, rfl‚ü© ‚ü®f, rfl‚ü©; use e * f; rw [mul_smul]
  le_antisymm := by
    rintro ‚ü®a‚ü© ‚ü®b‚ü© ‚ü®e, rfl‚ü© ‚ü®f, hf‚ü©
    by_cases hb : b = 0; ¬∑ simp [hb]
    have : IsUnit e := by
      apply isUnit_of_dvd_one
      use f
      rw [mul_comm]
      rw [‚Üê mul_smul, Algebra.smul_def] at hf
      nth_rw 2 [‚Üê one_mul b] at hf
      rw [‚Üê (algebraMap A K).map_one] at hf
      exact IsFractionRing.injective _ _ (mul_right_cancel‚ÇÄ hb hf).symm
    apply Quotient.sound'
    exact ‚ü®this.unit, rfl‚ü©
  le_total := ValuationRing.le_total _ _
  decidableLE := by classical infer_instance

noncomputable instance linearOrderedCommGroupWithZero :
    LinearOrderedCommGroupWithZero (ValueGroup A K) :=
  { linearOrder .. with
    mul_assoc := by rintro ‚ü®a‚ü© ‚ü®b‚ü© ‚ü®c‚ü©; apply Quotient.sound'; rw [mul_assoc]; apply Setoid.refl'
    one_mul := by rintro ‚ü®a‚ü©; apply Quotient.sound'; rw [one_mul]; apply Setoid.refl'
    mul_one := by rintro ‚ü®a‚ü©; apply Quotient.sound'; rw [mul_one]; apply Setoid.refl'
    mul_comm := by rintro ‚ü®a‚ü© ‚ü®b‚ü©; apply Quotient.sound'; rw [mul_comm]; apply Setoid.refl'
    mul_le_mul_left := by
      rintro ‚ü®a‚ü© ‚ü®b‚ü© ‚ü®c, rfl‚ü© ‚ü®d‚ü©
      use c; simp only [Algebra.smul_def]; ring
    zero_mul := by rintro ‚ü®a‚ü©; apply Quotient.sound'; rw [zero_mul]; apply Setoid.refl'
    mul_zero := by rintro ‚ü®a‚ü©; apply Quotient.sound'; rw [mul_zero]; apply Setoid.refl'
    zero_le_one := ‚ü®0, by rw [zero_smul]‚ü©
    exists_pair_ne := by
      use 0, 1
      intro c; obtain ‚ü®d, hd‚ü© := Quotient.exact' c
      apply_fun fun t => d‚Åª¬π ‚Ä¢ t at hd
      simp only [inv_smul_smul, smul_zero, one_ne_zero] at hd
    inv_zero := by apply Quotient.sound'; rw [inv_zero]; apply Setoid.refl'
    mul_inv_cancel := by
      rintro ‚ü®a‚ü© ha
      apply Quotient.sound'
      use 1
      simp only [one_smul, ne_eq]
      apply (mul_inv_cancel _).symm
      contrapose ha
      simp only [Classical.not_not] at ha ‚ä¢
      rw [ha]
      rfl }

/-- Any valuation ring induces a valuation on its fraction field. -/
def valuation : Valuation K (ValueGroup A K) where
  toFun := Quotient.mk''
  map_zero' := rfl
  map_one' := rfl
  map_mul' _ _ := rfl
  map_add_le_max' := by
    intro a b
    obtain ‚ü®xa, ya, hya, rfl‚ü© : ‚àÉ a b : A, _ := IsFractionRing.div_surjective a
    obtain ‚ü®xb, yb, hyb, rfl‚ü© : ‚àÉ a b : A, _ := IsFractionRing.div_surjective b
    have : (algebraMap A K) ya ‚â† 0 := IsFractionRing.to_map_ne_zero_of_mem_nonZeroDivisors hya
    have : (algebraMap A K) yb ‚â† 0 := IsFractionRing.to_map_ne_zero_of_mem_nonZeroDivisors hyb
    obtain ‚ü®c, h | h‚ü© := ValuationRing.cond (xa * yb) (xb * ya)
    ¬∑ dsimp
      apply le_trans _ (le_max_left _ _)
      use c + 1
      rw [Algebra.smul_def]
      field_simp
      simp only [‚Üê RingHom.map_mul, ‚Üê RingHom.map_add, ‚Üê (algebraMap A K).map_one, ‚Üê h]
      congr 1; ring
    ¬∑ apply le_trans _ (le_max_right _ _)
      use c + 1
      rw [Algebra.smul_def]
      field_simp
      simp only [‚Üê RingHom.map_mul, ‚Üê RingHom.map_add, ‚Üê (algebraMap A K).map_one, ‚Üê h]
      congr 1; ring
#align valuation_ring.valuation ValuationRing.valuation

theorem mem_integer_iff (x : K) : x ‚àà (valuation A K).integer ‚Üî ‚àÉ a : A, algebraMap A K a = x := by
  constructor
  ¬∑ rintro ‚ü®c, rfl‚ü©
    use c
    rw [Algebra.smul_def, mul_one]
  ¬∑ rintro ‚ü®c, rfl‚ü©
    use c
    rw [Algebra.smul_def, mul_one]
#align valuation_ring.mem_integer_iff ValuationRing.mem_integer_iff

/-- The valuation ring `A` is isomorphic to the ring of integers of its associated valuation. -/
noncomputable def equivInteger : A ‚âÉ+* (valuation A K).integer :=
  RingEquiv.ofBijective
    (show A ‚Üí‚Çô+* (valuation A K).integer from
      { toFun := fun a => ‚ü®algebraMap A K a, (mem_integer_iff _ _ _).mpr ‚ü®a, rfl‚ü©‚ü©
        map_mul' := fun _ _ => by ext1; exact (algebraMap A K).map_mul _ _
        map_zero' := by ext1; exact (algebraMap A K).map_zero
        map_add' := fun _ _ => by ext1; exact (algebraMap A K).map_add _ _ })
    (by
      constructor
      ¬∑ intro x y h
        apply_fun (algebraMap (valuation A K).integer K) at h
        exact IsFractionRing.injective _ _ h
      ¬∑ rintro ‚ü®-, ha‚ü©
        rw [mem_integer_iff] at ha
        obtain ‚ü®a, rfl‚ü© := ha
        exact ‚ü®a, rfl‚ü©)
#align valuation_ring.equiv_integer ValuationRing.equivInteger

@[simp]
theorem coe_equivInteger_apply (a : A) : (equivInteger A K a : K) = algebraMap A K a := rfl
#align valuation_ring.coe_equiv_integer_apply ValuationRing.coe_equivInteger_apply

theorem range_algebraMap_eq : (valuation A K).integer = (algebraMap A K).range := by
  ext; exact mem_integer_iff _ _ _
#align valuation_ring.range_algebra_map_eq ValuationRing.range_algebraMap_eq

end

section

variable (A : Type u) [CommRing A] [IsDomain A] [ValuationRing A]

instance (priority := 100) localRing : LocalRing A :=
  LocalRing.of_isUnit_or_isUnit_one_sub_self
    (by
      intro a
      obtain ‚ü®c, h | h‚ü© := ValuationRing.cond a (1 - a)
      ¬∑ left
        apply isUnit_of_mul_eq_one _ (c + 1)
        simp [mul_add, h]
      ¬∑ right
        apply isUnit_of_mul_eq_one _ (c + 1)
        simp [mul_add, h])

instance [DecidableRel ((¬∑ ‚â§ ¬∑) : Ideal A ‚Üí Ideal A ‚Üí Prop)] : LinearOrder (Ideal A) :=
  { (inferInstance : CompleteLattice (Ideal A)) with
    le_total := by
      intro Œ± Œ≤
      by_cases h : Œ± ‚â§ Œ≤; ¬∑ exact Or.inl h
      erw [not_forall] at h
      push_neg at h
      obtain ‚ü®a, h‚ÇÅ, h‚ÇÇ‚ü© := h
      right
      intro b hb
      obtain ‚ü®c, h | h‚ü© := ValuationRing.cond a b
      ¬∑ rw [‚Üê h]
        exact Ideal.mul_mem_right _ _ h‚ÇÅ
      ¬∑ exfalso; apply h‚ÇÇ; rw [‚Üê h]
        apply Ideal.mul_mem_right _ _ hb
    decidableLE := inferInstance }

end

section

variable {R : Type*} [CommRing R] [IsDomain R] {K : Type*}
variable [Field K] [Algebra R K] [IsFractionRing R K]

theorem iff_dvd_total : ValuationRing R ‚Üî IsTotal R (¬∑ ‚à£ ¬∑) := by
  classical
  refine ‚ü®fun H => ‚ü®fun a b => ?_‚ü©, fun H => ‚ü®fun a b => ?_‚ü©‚ü©
  ¬∑ obtain ‚ü®c, rfl | rfl‚ü© := ValuationRing.cond a b <;> simp
  ¬∑ obtain ‚ü®c, rfl‚ü© | ‚ü®c, rfl‚ü© := @IsTotal.total _ _ H a b <;> use c <;> simp
#align valuation_ring.iff_dvd_total ValuationRing.iff_dvd_total

theorem iff_ideal_total : ValuationRing R ‚Üî IsTotal (Ideal R) (¬∑ ‚â§ ¬∑) := by
  classical
  refine ‚ü®fun _ => ‚ü®le_total‚ü©, fun H => iff_dvd_total.mpr ‚ü®fun a b => ?_‚ü©‚ü©
  have := @IsTotal.total _ _ H (Ideal.span {a}) (Ideal.span {b})
  simp_rw [Ideal.span_singleton_le_span_singleton] at this
  exact this.symm
#align valuation_ring.iff_ideal_total ValuationRing.iff_ideal_total

variable (K)

theorem dvd_total [h : ValuationRing R] (x y : R) : x ‚à£ y ‚à® y ‚à£ x :=
  @IsTotal.total _ _ (iff_dvd_total.mp h) x y
#align valuation_ring.dvd_total ValuationRing.dvd_total

theorem unique_irreducible [ValuationRing R] ‚¶Ép q : R‚¶Ñ (hp : Irreducible p) (hq : Irreducible q) :
    Associated p q := by
  have := dvd_total p q
  rw [Irreducible.dvd_comm hp hq, or_self_iff] at this
  exact associated_of_dvd_dvd (Irreducible.dvd_symm hq hp this) this
#align valuation_ring.unique_irreducible ValuationRing.unique_irreducible

variable (R)

theorem iff_isInteger_or_isInteger :
    ValuationRing R ‚Üî ‚àÄ x : K, IsLocalization.IsInteger R x ‚à® IsLocalization.IsInteger R x‚Åª¬π := by
  constructor
  ¬∑ intro H x
    obtain ‚ü®x : R, y, hy, rfl‚ü© := IsFractionRing.div_surjective (A := R) x
    have := (map_ne_zero_iff _ (IsFractionRing.injective R K)).mpr (nonZeroDivisors.ne_zero hy)
    obtain ‚ü®s, rfl | rfl‚ü© := ValuationRing.cond x y
    ¬∑ exact Or.inr
        ‚ü®s, eq_inv_of_mul_eq_one_left <| by rwa [mul_div, div_eq_one_iff_eq, map_mul, mul_comm]‚ü©
    ¬∑ exact Or.inl ‚ü®s, by rwa [eq_div_iff, map_mul, mul_comm]‚ü©
  ¬∑ intro H
    constructor
    intro a b
    by_cases ha : a = 0; ¬∑ subst ha; exact ‚ü®0, Or.inr <| mul_zero b‚ü©
    by_cases hb : b = 0; ¬∑ subst hb; exact ‚ü®0, Or.inl <| mul_zero a‚ü©
    replace ha := (map_ne_zero_iff _ (IsFractionRing.injective R K)).mpr ha
    replace hb := (map_ne_zero_iff _ (IsFractionRing.injective R K)).mpr hb
    obtain ‚ü®c, e‚ü© | ‚ü®c, e‚ü© := H (algebraMap R K a / algebraMap R K b)
    ¬∑ rw [eq_div_iff hb, ‚Üê map_mul, (IsFractionRing.injective R K).eq_iff, mul_comm] at e
      exact ‚ü®c, Or.inr e‚ü©
    ¬∑ rw [inv_div, eq_div_iff ha, ‚Üê map_mul, (IsFractionRing.injective R K).eq_iff, mul_comm c] at e
      exact ‚ü®c, Or.inl e‚ü©
#align valuation_ring.iff_is_integer_or_is_integer ValuationRing.iff_isInteger_or_isInteger

variable {K}

theorem isInteger_or_isInteger [h : ValuationRing R] (x : K) :
    IsLocalization.IsInteger R x ‚à® IsLocalization.IsInteger R x‚Åª¬π :=
  (iff_isInteger_or_isInteger R K).mp h x
#align valuation_ring.is_integer_or_is_integer ValuationRing.isInteger_or_isInteger

variable {R}

-- This implies that valuation rings are integrally closed through typeclass search.
instance (priority := 100) [ValuationRing R] : IsBezout R := by
  classical
  rw [IsBezout.iff_span_pair_isPrincipal]
  intro x y
  rw [Ideal.span_insert]
  rcases le_total (Ideal.span {x} : Ideal R) (Ideal.span {y}) with h | h
  ¬∑ erw [sup_eq_right.mpr h]; exact ‚ü®‚ü®_, rfl‚ü©‚ü©
  ¬∑ erw [sup_eq_left.mpr h]; exact ‚ü®‚ü®_, rfl‚ü©‚ü©

instance (priority := 100) [LocalRing R] [IsBezout R] : ValuationRing R := by
  classical
  refine iff_dvd_total.mpr ‚ü®fun a b => ?_‚ü©
  obtain ‚ü®g, e : _ = Ideal.span _‚ü© := IsBezout.span_pair_isPrincipal a b
  obtain ‚ü®a, rfl‚ü© := Ideal.mem_span_singleton'.mp
      (show a ‚àà Ideal.span {g} by rw [‚Üê e]; exact Ideal.subset_span (by simp))
  obtain ‚ü®b, rfl‚ü© := Ideal.mem_span_singleton'.mp
      (show b ‚àà Ideal.span {g} by rw [‚Üê e]; exact Ideal.subset_span (by simp))
  obtain ‚ü®x, y, e'‚ü© := Ideal.mem_span_pair.mp
      (show g ‚àà Ideal.span {a * g, b * g} by rw [e]; exact Ideal.subset_span (by simp))
  rcases eq_or_ne g 0 with h | h
  ¬∑ simp [h]
  have : x * a + y * b = 1 := by
    apply mul_left_injective‚ÇÄ h; convert e' using 1 <;> ring
  cases' LocalRing.isUnit_or_isUnit_of_add_one this with h' h' <;> [left; right]
  all_goals exact mul_dvd_mul_right (isUnit_iff_forall_dvd.mp (isUnit_of_mul_isUnit_right h') _) _

theorem iff_local_bezout_domain : ValuationRing R ‚Üî LocalRing R ‚àß IsBezout R :=
  ‚ü®fun _ ‚Ü¶ ‚ü®inferInstance, inferInstance‚ü©, fun ‚ü®_, _‚ü© ‚Ü¶ inferInstance‚ü©
#align valuation_ring.iff_local_bezout_domain ValuationRing.iff_local_bezout_domain

protected theorem tFAE (R : Type u) [CommRing R] [IsDomain R] :
    List.TFAE
      [ValuationRing R,
        ‚àÄ x : FractionRing R, IsLocalization.IsInteger R x ‚à® IsLocalization.IsInteger R x‚Åª¬π,
        IsTotal R (¬∑ ‚à£ ¬∑), IsTotal (Ideal R) (¬∑ ‚â§ ¬∑), LocalRing R ‚àß IsBezout R] := by
  tfae_have 1 ‚Üî 2; ¬∑ exact iff_isInteger_or_isInteger R _
  tfae_have 1 ‚Üî 3; ¬∑ exact iff_dvd_total
  tfae_have 1 ‚Üî 4; ¬∑ exact iff_ideal_total
  tfae_have 1 ‚Üî 5; ¬∑ exact iff_local_bezout_domain
  tfae_finish
#align valuation_ring.tfae ValuationRing.tFAE

end

theorem _root_.Function.Surjective.valuationRing {R S : Type*} [CommRing R] [IsDomain R]
    [ValuationRing R] [CommRing S] [IsDomain S] (f : R ‚Üí+* S) (hf : Function.Surjective f) :
    ValuationRing S :=
  ‚ü®fun a b => by
    obtain ‚ü®‚ü®a, rfl‚ü©, ‚ü®b, rfl‚ü©‚ü© := hf a, hf b
    obtain ‚ü®c, rfl | rfl‚ü© := ValuationRing.cond a b
    exacts [‚ü®f c, Or.inl <| (map_mul _ _ _).symm‚ü©, ‚ü®f c, Or.inr <| (map_mul _ _ _).symm‚ü©]‚ü©
#align function.surjective.valuation_ring Function.Surjective.valuationRing

section

variable {ùí™ : Type u} {K : Type v} {Œì : Type w} [CommRing ùí™] [IsDomain ùí™] [Field K] [Algebra ùí™ K]
  [LinearOrderedCommGroupWithZero Œì] (v : Valuation K Œì) (hh : v.Integers ùí™)

/-- If `ùí™` satisfies `v.integers ùí™` where `v` is a valuation on a field, then `ùí™`
is a valuation ring. -/
theorem of_integers : ValuationRing ùí™ := by
  constructor
  intro a b
  rcases le_total (v (algebraMap ùí™ K a)) (v (algebraMap ùí™ K b)) with h | h
  ¬∑ obtain ‚ü®c, hc‚ü© := Valuation.Integers.dvd_of_le hh h
    use c; exact Or.inr hc.symm
  ¬∑ obtain ‚ü®c, hc‚ü© := Valuation.Integers.dvd_of_le hh h
    use c; exact Or.inl hc.symm
#align valuation_ring.of_integers ValuationRing.of_integers

end

section

variable (K : Type u) [Field K]

/-- A field is a valuation ring. -/
instance (priority := 100) of_field : ValuationRing K := by
  constructor
  intro a b
  by_cases h : b = 0
  ¬∑ use 0; left; simp [h]
  ¬∑ use a * b‚Åª¬π; right; field_simp
#align valuation_ring.of_field ValuationRing.of_field

end

section

variable (A : Type u) [CommRing A] [IsDomain A] [DiscreteValuationRing A]

/-- A DVR is a valuation ring. -/
instance (priority := 100) of_discreteValuationRing : ValuationRing A := by
  constructor
  intro a b
  by_cases ha : a = 0; ¬∑ use 0; right; simp [ha]
  by_cases hb : b = 0; ¬∑ use 0; left; simp [hb]
  obtain ‚ü®œñ, hœñ‚ü© := DiscreteValuationRing.exists_irreducible A
  obtain ‚ü®m, u, rfl‚ü© := DiscreteValuationRing.eq_unit_mul_pow_irreducible ha hœñ
  obtain ‚ü®n, v, rfl‚ü© := DiscreteValuationRing.eq_unit_mul_pow_irreducible hb hœñ
  rcases le_total m n with h | h
  ¬∑ use (u‚Åª¬π * v : AÀ£) * œñ ^ (n - m); left
    simp_rw [mul_comm (u : A), Units.val_mul, ‚Üê mul_assoc, mul_assoc _ (u : A)]
    simp only [Units.mul_inv, mul_one, mul_comm _ (v : A), mul_assoc, ‚Üê pow_add]
    congr 2
    exact Nat.add_sub_of_le h
  ¬∑ use (v‚Åª¬π * u : AÀ£) * œñ ^ (m - n); right
    simp_rw [mul_comm (v : A), Units.val_mul, ‚Üê mul_assoc, mul_assoc _ (v : A)]
    simp only [Units.mul_inv, mul_one, mul_comm _ (u : A), mul_assoc, ‚Üê pow_add]
    congr 2
    exact Nat.add_sub_of_le h
#align valuation_ring.of_discrete_valuation_ring ValuationRing.of_discreteValuationRing

end

end ValuationRing
