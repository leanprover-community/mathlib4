/-
Copyright (c) 2020 Aaron Anderson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Aaron Anderson
-/
import Mathlib.LinearAlgebra.Isomorphisms
import Mathlib.Order.JordanHolder

#align_import ring_theory.simple_module from "leanprover-community/mathlib"@"cce7f68a7eaadadf74c82bbac20721cdc03a1cc1"

/-!
# Simple Modules

## Main Definitions
  * `IsSimpleModule` indicates that a module has no proper submodules
  (the only submodules are `‚ä•` and `‚ä§`).
  * `IsSemisimpleModule` indicates that every submodule has a complement, or equivalently,
    the module is a direct sum of simple modules.
  * A `DivisionRing` structure on the endomorphism ring of a simple module.

## Main Results
  * Schur's Lemma: `bijective_or_eq_zero` shows that a linear map between simple modules
  is either bijective or 0, leading to a `DivisionRing` structure on the endomorphism ring.

## TODO
  * Artin-Wedderburn Theory
  * Unify with the work on Schur's Lemma in a category theory context

-/


variable (R : Type*) [Ring R] (M : Type*) [AddCommGroup M] [Module R M]

/-- A module is simple when it has only two submodules, `‚ä•` and `‚ä§`. -/
abbrev IsSimpleModule :=
  IsSimpleOrder (Submodule R M)
#align is_simple_module IsSimpleModule

/-- A module is semisimple when every submodule has a complement, or equivalently, the module
  is a direct sum of simple modules. -/
abbrev IsSemisimpleModule :=
  ComplementedLattice (Submodule R M)
#align is_semisimple_module IsSemisimpleModule

-- Making this an instance causes the linter to complain of "dangerous instances"
theorem IsSimpleModule.nontrivial [IsSimpleModule R M] : Nontrivial M :=
  ‚ü®‚ü®0, by
      have h : (‚ä• : Submodule R M) ‚â† ‚ä§ := bot_ne_top
      -- ‚ä¢ ‚àÉ y, 0 ‚â† y
      contrapose! h
      -- ‚ä¢ ‚ä• = ‚ä§
      ext x
      -- ‚ä¢ x ‚àà ‚ä• ‚Üî x ‚àà ‚ä§
      simp [Submodule.mem_bot, Submodule.mem_top, h x]‚ü©‚ü©
      -- üéâ no goals
#align is_simple_module.nontrivial IsSimpleModule.nontrivial

variable {R} {M} -- Porting note: had break line or all hell breaks loose
variable {m : Submodule R M} {N : Type*} [AddCommGroup N] [Module R N]

theorem IsSimpleModule.congr (l : M ‚âÉ‚Çó[R] N) [IsSimpleModule R N] : IsSimpleModule R M :=
  (Submodule.orderIsoMapComap l).isSimpleOrder
#align is_simple_module.congr IsSimpleModule.congr

theorem isSimpleModule_iff_isAtom : IsSimpleModule R m ‚Üî IsAtom m := by
  rw [‚Üê Set.isSimpleOrder_Iic_iff_isAtom]
  -- ‚ä¢ IsSimpleModule R { x // x ‚àà m } ‚Üî IsSimpleOrder ‚Üë(Set.Iic m)
  apply OrderIso.isSimpleOrder_iff
  -- ‚ä¢ Submodule R { x // x ‚àà m } ‚âÉo ‚Üë(Set.Iic m)
  exact Submodule.MapSubtype.relIso m
  -- üéâ no goals
#align is_simple_module_iff_is_atom isSimpleModule_iff_isAtom

theorem isSimpleModule_iff_isCoatom : IsSimpleModule R (M ‚ß∏ m) ‚Üî IsCoatom m := by
  rw [‚Üê Set.isSimpleOrder_Ici_iff_isCoatom]
  -- ‚ä¢ IsSimpleModule R (M ‚ß∏ m) ‚Üî IsSimpleOrder ‚Üë(Set.Ici m)
  apply OrderIso.isSimpleOrder_iff
  -- ‚ä¢ Submodule R (M ‚ß∏ m) ‚âÉo ‚Üë(Set.Ici m)
  exact Submodule.comapMkQRelIso m
  -- üéâ no goals
#align is_simple_module_iff_is_coatom isSimpleModule_iff_isCoatom

theorem covby_iff_quot_is_simple {A B : Submodule R M} (hAB : A ‚â§ B) :
    A ‚ãñ B ‚Üî IsSimpleModule R (B ‚ß∏ Submodule.comap B.subtype A) := by
  set f : Submodule R B ‚âÉo Set.Iic B := Submodule.MapSubtype.relIso B with hf
  -- ‚ä¢ A ‚ãñ B ‚Üî IsSimpleModule R ({ x // x ‚àà B } ‚ß∏ Submodule.comap (Submodule.subtyp ‚Ä¶
  rw [covby_iff_coatom_Iic hAB, isSimpleModule_iff_isCoatom, ‚Üê OrderIso.isCoatom_iff f, hf]
  -- ‚ä¢ IsCoatom { val := A, property := hAB } ‚Üî IsCoatom (‚Üë(Submodule.MapSubtype.re ‚Ä¶
  simp [-OrderIso.isCoatom_iff, Submodule.MapSubtype.relIso, Submodule.map_comap_subtype,
    inf_eq_right.2 hAB]
#align covby_iff_quot_is_simple covby_iff_quot_is_simple

namespace IsSimpleModule

variable [hm : IsSimpleModule R m]

@[simp]
theorem isAtom : IsAtom m :=
  isSimpleModule_iff_isAtom.1 hm
#align is_simple_module.is_atom IsSimpleModule.isAtom

end IsSimpleModule

theorem is_semisimple_of_sSup_simples_eq_top
    (h : sSup { m : Submodule R M | IsSimpleModule R m } = ‚ä§) : IsSemisimpleModule R M :=
  complementedLattice_of_sSup_atoms_eq_top (by simp_rw [‚Üê h, isSimpleModule_iff_isAtom])
                                               -- üéâ no goals
#align is_semisimple_of_Sup_simples_eq_top is_semisimple_of_sSup_simples_eq_top

namespace IsSemisimpleModule

variable [IsSemisimpleModule R M]

theorem sSup_simples_eq_top : sSup { m : Submodule R M | IsSimpleModule R m } = ‚ä§ := by
  simp_rw [isSimpleModule_iff_isAtom]
  -- ‚ä¢ sSup {m | IsAtom m} = ‚ä§
  exact sSup_atoms_eq_top
  -- üéâ no goals
#align is_semisimple_module.Sup_simples_eq_top IsSemisimpleModule.sSup_simples_eq_top

instance is_semisimple_submodule {m : Submodule R M} : IsSemisimpleModule R m :=
  haveI f : Submodule R m ‚âÉo Set.Iic m := Submodule.MapSubtype.relIso m
  f.complementedLattice_iff.2 IsModularLattice.complementedLattice_Iic
#align is_semisimple_module.is_semisimple_submodule IsSemisimpleModule.is_semisimple_submodule

end IsSemisimpleModule

theorem is_semisimple_iff_top_eq_sSup_simples :
    sSup { m : Submodule R M | IsSimpleModule R m } = ‚ä§ ‚Üî IsSemisimpleModule R M :=
  ‚ü®is_semisimple_of_sSup_simples_eq_top, by
    intro
    -- ‚ä¢ sSup {m | IsSimpleModule R { x // x ‚àà m }} = ‚ä§
    exact IsSemisimpleModule.sSup_simples_eq_top‚ü©
    -- üéâ no goals
#align is_semisimple_iff_top_eq_Sup_simples is_semisimple_iff_top_eq_sSup_simples

namespace LinearMap

theorem injective_or_eq_zero [IsSimpleModule R M] (f : M ‚Üí‚Çó[R] N) :
    Function.Injective f ‚à® f = 0 := by
  rw [‚Üê ker_eq_bot, ‚Üê ker_eq_top]
  -- ‚ä¢ ker f = ‚ä• ‚à® ker f = ‚ä§
  apply eq_bot_or_eq_top
  -- üéâ no goals
#align linear_map.injective_or_eq_zero LinearMap.injective_or_eq_zero

theorem injective_of_ne_zero [IsSimpleModule R M] {f : M ‚Üí‚Çó[R] N} (h : f ‚â† 0) :
    Function.Injective f :=
  f.injective_or_eq_zero.resolve_right h
#align linear_map.injective_of_ne_zero LinearMap.injective_of_ne_zero

theorem surjective_or_eq_zero [IsSimpleModule R N] (f : M ‚Üí‚Çó[R] N) :
    Function.Surjective f ‚à® f = 0 := by
  rw [‚Üê range_eq_top, ‚Üê range_eq_bot, or_comm]
  -- ‚ä¢ range f = ‚ä• ‚à® range f = ‚ä§
  apply eq_bot_or_eq_top
  -- üéâ no goals
#align linear_map.surjective_or_eq_zero LinearMap.surjective_or_eq_zero

theorem surjective_of_ne_zero [IsSimpleModule R N] {f : M ‚Üí‚Çó[R] N} (h : f ‚â† 0) :
    Function.Surjective f :=
  f.surjective_or_eq_zero.resolve_right h
#align linear_map.surjective_of_ne_zero LinearMap.surjective_of_ne_zero

/-- **Schur's Lemma** for linear maps between (possibly distinct) simple modules -/
theorem bijective_or_eq_zero [IsSimpleModule R M] [IsSimpleModule R N] (f : M ‚Üí‚Çó[R] N) :
    Function.Bijective f ‚à® f = 0 := by
  by_cases h : f = 0
  -- ‚ä¢ Function.Bijective ‚Üëf ‚à® f = 0
  ¬∑ right
    -- ‚ä¢ f = 0
    exact h
    -- üéâ no goals
  exact Or.intro_left _ ‚ü®injective_of_ne_zero h, surjective_of_ne_zero h‚ü©
  -- üéâ no goals
#align linear_map.bijective_or_eq_zero LinearMap.bijective_or_eq_zero

theorem bijective_of_ne_zero [IsSimpleModule R M] [IsSimpleModule R N] {f : M ‚Üí‚Çó[R] N} (h : f ‚â† 0) :
    Function.Bijective f :=
  f.bijective_or_eq_zero.resolve_right h
#align linear_map.bijective_of_ne_zero LinearMap.bijective_of_ne_zero

theorem isCoatom_ker_of_surjective [IsSimpleModule R N] {f : M ‚Üí‚Çó[R] N}
    (hf : Function.Surjective f) : IsCoatom (LinearMap.ker f) := by
  rw [‚Üê isSimpleModule_iff_isCoatom]
  -- ‚ä¢ IsSimpleModule R (M ‚ß∏ ker f)
  exact IsSimpleModule.congr (f.quotKerEquivOfSurjective hf)
  -- üéâ no goals
#align linear_map.is_coatom_ker_of_surjective LinearMap.isCoatom_ker_of_surjective

/-- Schur's Lemma makes the endomorphism ring of a simple module a division ring. -/
noncomputable instance _root_.Module.End.divisionRing
    [DecidableEq (Module.End R M)] [IsSimpleModule R M] : DivisionRing (Module.End R M) :=
  {
    (Module.End.ring :
      Ring
        (Module.End R
          M)) with
    inv := fun f =>
      if h : f = 0 then 0
      else
        LinearMap.inverse f (Equiv.ofBijective _ (bijective_of_ne_zero h)).invFun
          (Equiv.ofBijective _ (bijective_of_ne_zero h)).left_inv
          (Equiv.ofBijective _ (bijective_of_ne_zero h)).right_inv
    exists_pair_ne :=
      ‚ü®0, 1, by
        haveI := IsSimpleModule.nontrivial R M
        -- ‚ä¢ 0 ‚â† 1
        have h := exists_pair_ne M
        -- ‚ä¢ 0 ‚â† 1
        contrapose! h
        -- ‚ä¢ ‚àÄ (x y : M), x = y
        intro x y
        -- ‚ä¢ x = y
        simp_rw [ext_iff, one_apply, zero_apply] at h
        -- ‚ä¢ x = y
        rw [‚Üê h x, h y]‚ü©
        -- üéâ no goals
    mul_inv_cancel := by
      intro a a0
      -- ‚ä¢ a * a‚Åª¬π = 1
      change a * dite _ _ _ = 1
      -- ‚ä¢ (a * if h : a = 0 then 0 else inverse a (Equiv.ofBijective ‚Üëa (_ : Function. ‚Ä¶
      ext x
      -- ‚ä¢ ‚Üë(a * if h : a = 0 then 0 else inverse a (Equiv.ofBijective ‚Üëa (_ : Function ‚Ä¶
      rw [dif_neg a0, mul_eq_comp, one_apply, comp_apply]
      -- ‚ä¢ ‚Üëa (‚Üë(inverse a (Equiv.ofBijective ‚Üëa (_ : Function.Bijective ‚Üëa)).invFun (_ ‚Ä¶
      exact (Equiv.ofBijective _ (bijective_of_ne_zero a0)).right_inv x
      -- üéâ no goals
    inv_zero := dif_pos rfl }
#align module.End.division_ring Module.End.divisionRing

end LinearMap

-- Porting note: adding a namespace with all the new statements; existing result is not used in ML3
namespace JordanHolderModule

-- Porting note: jordanHolderModule was timing out so outlining the fields

/-- An isomorphism `X‚ÇÇ / X‚ÇÅ ‚à© X‚ÇÇ ‚âÖ Y‚ÇÇ / Y‚ÇÅ ‚à© Y‚ÇÇ` of modules for pairs
`(X‚ÇÅ,X‚ÇÇ) (Y‚ÇÅ,Y‚ÇÇ) : Submodule R M` -/
def Iso (X Y : Submodule R M √ó Submodule R M) : Prop :=
  Nonempty <| (X.2 ‚ß∏ X.1.comap X.2.subtype) ‚âÉ‚Çó[R] Y.2 ‚ß∏ Y.1.comap Y.2.subtype

theorem iso_symm {X Y : Submodule R M √ó Submodule R M} : Iso X Y ‚Üí Iso Y X :=
  fun ‚ü®f‚ü© => ‚ü®f.symm‚ü©

theorem iso_trans {X Y Z : Submodule R M √ó Submodule R M} : Iso X Y ‚Üí Iso Y Z ‚Üí Iso X Z :=
  fun ‚ü®f‚ü© ‚ü®g‚ü© => ‚ü®f.trans g‚ü©

@[nolint unusedArguments]
theorem second_iso {X Y : Submodule R M} (_ : X ‚ãñ X ‚äî Y) :
    Iso (X,X ‚äî Y) (X ‚äì Y,Y) := by
  constructor
  -- ‚ä¢ ({ x // x ‚àà (X, X ‚äî Y).snd } ‚ß∏ Submodule.comap (Submodule.subtype (X, X ‚äî Y) ‚Ä¶
  rw [sup_comm, inf_comm]
  -- ‚ä¢ ({ x // x ‚àà (X, Y ‚äî X).snd } ‚ß∏ Submodule.comap (Submodule.subtype (X, Y ‚äî X) ‚Ä¶
  dsimp
  -- ‚ä¢ ({ x // x ‚àà Y ‚äî X } ‚ß∏ Submodule.comap (Submodule.subtype (Y ‚äî X)) X) ‚âÉ‚Çó[R] { ‚Ä¶
  exact (LinearMap.quotientInfEquivSupQuotient Y X).symm
  -- üéâ no goals

instance instJordanHolderLattice : JordanHolderLattice (Submodule R M) where
  IsMaximal := (¬∑ ‚ãñ ¬∑)
  lt_of_isMaximal := Covby.lt
  sup_eq_of_isMaximal hxz hyz := Wcovby.sup_eq hxz.wcovby hyz.wcovby
  isMaximal_inf_left_of_isMaximal_sup := inf_covby_of_covby_sup_of_covby_sup_left
  Iso := Iso
  iso_symm := iso_symm
  iso_trans := iso_trans
  second_iso := second_iso
#align jordan_holder_module JordanHolderModule.instJordanHolderLattice

end JordanHolderModule
