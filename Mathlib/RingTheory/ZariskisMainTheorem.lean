/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
module

public import Mathlib.RingTheory.Algebraic.StronglyTranscendental
public import Mathlib.RingTheory.Conductor
public import Mathlib.RingTheory.Ideal.Quotient.Nilpotent
public import Mathlib.RingTheory.IntegralClosure.GoingDown
public import Mathlib.RingTheory.Polynomial.IsIntegral
public import Mathlib.RingTheory.QuasiFinite.Basic

/-!
# Algebraic Zariski's Main Theorem

The statement of Zariski's main theorem is the following:
Given a finite type `R`-algebra `S`, and `p` a prime of `S` such that `S` is quasi-finite at `R`,
then there exists a `f ‚àâ p` such that `S[1/f]` is isomorphic to `R'[1/f]` where `R'` is the integral
closure of `R` in `S`.

We follow https://stacks.math.columbia.edu/tag/00PI and proceed in the following steps

1. `Algebra.ZariskisMainProperty.of_adjoin_eq_top`:
  The case where `S = R[X]/I`.
  The key is `Polynomial.not_ker_le_map_C_of_surjective_of_quasiFiniteAt`
  which shows that there exists some `g ‚àà I` such that some coefficient `g·µ¢ ‚àâ p`.
  Then one basically takes `f = g·µ¢` and `g` becomes monic in `R[1/g·µ¢][X]` up to some minor technical
  issues, and then `S[1/g·µ¢]` is basically integral over `R[1/g·µ¢]`.
2. `Algebra.ZariskisMainProperty.of_algHom_polynomial`:
  The case where `S` is finite over `R‚ü®x‚ü©` for some `x : S`.
  The following key results are first esablished:
  - `isStronglyTranscendental_mk_radical_conductor`:
    Let `ùî£` be the conductor of `x` (i.e. the largest `S`-ideal in `R‚ü®x‚ü©`).
    `x` as an element of `S/‚àöùî£` is strongly transcendental over `R`.
  - `Algebra.not_quasiFiniteAt_of_stronglyTranscendental`:
    If `R ‚äÜ S`, `S` is reduced, and `x : S` is strongly transcendental over `R`,
    then there's a contradiction.
    The proof first reduces to when `S` is a domain, and then to when `R` is integrally closed.
    We get a going down theorem when `R` is integrally closed, which we apply to
    `Polynomial.map_under_lt_comap_of_quasiFiniteAt`:`(p ‚à© R)[X] < p ‚à© R<x>` to get a contradiction.
  The second result applied to `R/(‚àöùî£ ‚à© R) ‚äÜ S/‚àöùî£` together with the first result implies that
  `p` does not contain `ùî£`.
  The claim then follows from `Localization.localRingHom_bijective_of_not_conductor_le`.
3. TODO (@erdOne): Induct on the number of `{ x·µ¢ }` such that `S` is finite over `R‚ü®x·µ¢‚ü©` to get the
  general statement.

The final statement would be
```
example {R S : Type*} [CommRing R] [CommRing S] [Algebra R S] [Algebra.FiniteType R S]
    (p : Ideal S) [p.IsPrime] [Algebra.QuasiFiniteAt R p] : ZariskiMainProperty R p
```

-/

@[expose] public section

variable {R S T : Type*} [CommRing R] [CommRing S] [Algebra R S] [CommRing T] [Algebra R T]

open scoped TensorProduct nonZeroDivisors

open Polynomial

namespace Algebra

variable (R) in
/-- We say that an `R` algebra `S` satisfies the Zariski's main property at a prime `p` of `S`
if there exists `r ‚àâ p` in the integral closure `S'` of `R` in `S`, such that `S'[1/r] = S[1/r]`. -/
def ZariskisMainProperty (p : Ideal S) : Prop :=
  ‚àÉ r : integralClosure R S, r.1 ‚àâ p ‚àß Function.Bijective
    (Localization.awayMap (integralClosure R S).val.toRingHom r)

lemma zariskisMainProperty_iff {p : Ideal S} :
    ZariskisMainProperty R p ‚Üî ‚àÉ r ‚àâ p, IsIntegral R r ‚àß ‚àÄ x, ‚àÉ m, IsIntegral R (r ^ m * x) := by
  simp only [ZariskisMainProperty, Subtype.exists, ‚Üê exists_prop, @exists_comm (_ ‚àâ p)]
  refine exists‚ÇÉ_congr fun r hr hrp ‚Ü¶ ?_
  rw [Function.Bijective, and_iff_right
    (by exact IsLocalization.map_injective_of_injective _ _ _ Subtype.val_injective),
    Localization.awayMap_surjective_iff]
  simp [mem_integralClosure_iff]

lemma zariskisMainProperty_iff' {p : Ideal S} :
    ZariskisMainProperty R p ‚Üî ‚àÉ r ‚àâ p, ‚àÄ x, ‚àÉ m, IsIntegral R (r ^ m * x) := by
  refine zariskisMainProperty_iff.trans (exists_congr fun r ‚Ü¶ and_congr_right fun hrp ‚Ü¶
    and_iff_right_of_imp fun H ‚Ü¶ ?_)
  obtain ‚ü®n, hn‚ü© := H r
  rw [‚Üê pow_succ] at hn
  exact (IsIntegral.pow_iff (by simp)).mp hn

lemma zariskisMainProperty_iff_exists_saturation_eq_top {p : Ideal S} :
    ZariskisMainProperty R p ‚Üî ‚àÉ r ‚àâ p, ‚àÉ h : IsIntegral R r,
      (integralClosure R S).saturation (.powers r) (by simpa [Submonoid.powers_le]) = ‚ä§ := by
  simp [zariskisMainProperty_iff, ‚Üê top_le_iff, SetLike.le_def,
    Submonoid.mem_powers_iff, mem_integralClosure_iff]

lemma ZariskisMainProperty.restrictScalars [Algebra S T] [IsScalarTower R S T]
    [Algebra.IsIntegral R S] {p : Ideal T} (H : ZariskisMainProperty S p) :
    ZariskisMainProperty R p := by
  rw [zariskisMainProperty_iff'] at H ‚ä¢
  obtain ‚ü®r, hrp, H‚ü© := H
  exact ‚ü®r, hrp, fun x ‚Ü¶ ‚ü®_, isIntegral_trans _ (H x).choose_spec‚ü©‚ü©

lemma ZariskisMainProperty.trans [Algebra S T] [IsScalarTower R S T] (p : Ideal T) [p.IsPrime]
    (h‚ÇÅ : ZariskisMainProperty R (p.under S))
    (h‚ÇÇ : ‚àÉ r ‚àâ p.under S, (‚ä• : Subalgebra S T).saturation (.powers (algebraMap _ _ r))
      (by simp [Submonoid.powers_le]) = ‚ä§) :
    ZariskisMainProperty R p := by
  rw [zariskisMainProperty_iff] at h‚ÇÅ
  rw [zariskisMainProperty_iff']
  obtain ‚ü®s, hsp, hs, Hs‚ü© := h‚ÇÅ
  obtain ‚ü®t, htp, Ht‚ü© := h‚ÇÇ
  obtain ‚ü®m, hm‚ü© := Hs t
  refine ‚ü®algebraMap _ _ (s ^ (m + 1) * t), ?_, fun x ‚Ü¶ ?_‚ü©
  ¬∑ simpa using ‚Äπp.IsPrime‚Ä∫.mul_notMem
      (mt ((inferInstanceAs (p.under S).IsPrime).mem_of_pow_mem (m + 1)) hsp) htp
  obtain ‚ü®_, ‚ü®n, rfl‚ü©, a, ha‚ü© := Ht.ge (Set.mem_univ x)
  obtain ‚ü®k, hk‚ü© := Hs a
  refine ‚ü®k + n, ?_‚ü©
  convert_to IsIntegral R (algebraMap S T ((s ^ ((m + 1) * n) * (s ^ m * t) ^ k * (s ^ k * a))))
  ¬∑ simp only [AlgHom.toRingHom_eq_coe, Algebra.toRingHom_ofId] at ha
    simp only [map_pow, map_mul, ha, pow_add, mul_pow]
    ring
  ¬∑ exact .algebraMap (.mul ((hs.pow _).mul (hm.pow _)) hk)

lemma ZariskisMainProperty.of_isIntegral (p : Ideal S) [p.IsPrime] [Algebra.IsIntegral R S] :
    ZariskisMainProperty R p :=
  zariskisMainProperty_iff'.mpr ‚ü®1, p.primeCompl.one_mem,
    fun _ ‚Ü¶ ‚ü®0, Algebra.IsIntegral.isIntegral _‚ü©‚ü©

end Algebra

section IsStronglyTranscendental

variable (œÜ : R[X] ‚Üí‚Çê[R] S) (t : S) (p r : R[X])

/-- Given a map `œÜ : R[X] ‚Üí‚Çê[R] S`. Suppose `t = œÜ r / œÜ p` is integral over `R[X]` where
`p` is monic with `deg p > deg r`, then `t` is also integral over `R`. -/
lemma isIntegral_of_isIntegralElem_of_monic_of_natDegree_lt
    (ht : œÜ.IsIntegralElem t) (hpm : p.Monic)
    (hpr : r.natDegree < p.natDegree) (hp : œÜ p * t = œÜ r) : IsIntegral R t := by
  let St := Localization.Away t
  let t' : St := IsLocalization.Away.invSelf t
  have ht't : t' * algebraMap S St t = 1 := by rw [mul_comm, IsLocalization.Away.mul_invSelf]
  let R‚ÇÅ := Algebra.adjoin R {t'}
  let R‚ÇÇ := Algebra.adjoin R‚ÇÅ {algebraMap S St (œÜ X)}
  letI : Algebra R‚ÇÅ R‚ÇÇ := R‚ÇÇ.algebra
  letI : Algebra R‚ÇÇ St := R‚ÇÇ.toAlgebra
  letI : Algebra R‚ÇÅ St := R‚ÇÅ.toAlgebra
  haveI : IsScalarTower R‚ÇÅ R‚ÇÇ St := Subalgebra.isScalarTower_mid _
  have : Algebra.IsIntegral R‚ÇÅ R‚ÇÇ := by
    cases subsingleton_or_nontrivial R‚ÇÅ
    ¬∑ have := (algebraMap R‚ÇÅ R‚ÇÇ).codomain_trivial; exact ‚ü®(Subsingleton.elim ¬∑ 0 ‚ñ∏ isIntegral_zero)‚ü©
    rw [‚Üê le_integralClosure_iff_isIntegral, Algebra.adjoin_le_iff, Set.singleton_subset_iff,
      SetLike.mem_coe, mem_integralClosure_iff]
    refine ‚ü®p.map (algebraMap R R‚ÇÅ) - C ‚ü®t', Algebra.self_mem_adjoin_singleton R t'‚ü© *
        r.map (algebraMap R R‚ÇÅ), (hpm.map _).sub_of_left (degree_lt_degree ?_), ?_‚ü©
    ¬∑ grw [natDegree_C_mul_le, natDegree_map_le, hpm.natDegree_map]; assumption
    ¬∑ simp [‚Üê aeval_def, aeval_algebraMap_apply, aeval_algHom_apply,
        ‚Üê hp, ‚Üê mul_assoc, ht't, mul_right_comm]
  have : IsIntegral R‚ÇÅ (algebraMap S St t) := by
    refine isIntegral_trans (A := R‚ÇÇ) (algebraMap S St t) ?_
    obtain ‚ü®q, hq, hq'‚ü© := ht
    refine ‚ü®q.map (aeval ‚ü®_, Algebra.self_mem_adjoin_singleton _ _‚ü©).toRingHom, hq.map _, ?_‚ü©
    rw [AlgHom.toRingHom_eq_coe, eval‚ÇÇ_map, ‚Üê map_zero (algebraMap S St), ‚Üê hq',
      hom_eval‚ÇÇ]
    congr 1
    ext <;> simp [- Polynomial.algebraMap_apply, ‚Üê algebraMap_eq, ‚Üê IsScalarTower.algebraMap_apply]
  simpa using IsLocalization.Away.isIntegral_of_isIntegral_map t
    (isIntegral_of_isIntegral_adjoin_of_mul_eq_one _ _ ht't this)

@[stacks 00PT]
lemma exists_isIntegral_sub_of_isIntegralElem_of_mul_mem_range
    (ht : œÜ.IsIntegralElem t) (hpm : p.Monic) (hp : œÜ p * t ‚àà œÜ.range) :
    ‚àÉ q, IsIntegral R (t - œÜ q) := by
  obtain ‚ü®r, hr : œÜ r = _‚ü© := hp
  obtain rfl | hp1 := eq_or_ne p 1
  ¬∑ exact ‚ü®r, by simp_all [isIntegral_zero]‚ü©
  exact ‚ü®_, isIntegral_of_isIntegralElem_of_monic_of_natDegree_lt œÜ (t - œÜ (r /‚Çò p)) p (r %‚Çò p)
    (ht.sub _ œÜ.isIntegralElem_map) hpm (natDegree_modByMonic_lt _ hpm hp1)
    (by simp [mul_sub, ‚Üê hr, sub_eq_iff_eq_add, ‚Üê map_mul, ‚Üê map_add, r.modByMonic_add_div hpm])‚ü©

open IsScalarTower in
attribute [local simp] IsLocalization.map_eq aeval_algebraMap_apply aeval_algHom_apply in
@[stacks 00PV]
lemma exists_isIntegral_leadingCoeff_pow_smul_sub_of_isIntegralElem_of_mul_mem_range
    (ht : œÜ.IsIntegralElem t) (hp : œÜ p * t ‚àà œÜ.range) :
    ‚àÉ q n, IsIntegral R (p.leadingCoeff ^ n ‚Ä¢ t - œÜ q) := by
  set a := p.leadingCoeff
  let R' := Localization.Away a
  let S' := Localization.Away (algebraMap R S a)
  letI : Algebra R' S' := (Localization.awayMap (algebraMap R S) a).toAlgebra
  have : IsScalarTower R R' S' := .of_algebraMap_eq (by
    simp +zetaDelta [RingHom.algebraMap_toAlgebra, IsLocalization.Away.map, ‚Üê algebraMap_apply R S])
  have ha : IsUnit (algebraMap R R' a) := IsLocalization.Away.algebraMap_isUnit a
  have H : (aeval ((algebraMap S S') (œÜ X))).toRingHom.comp (mapRingHom (algebraMap R R')) =
    (algebraMap S S').comp œÜ := by ext <;>
      simp [- Polynomial.algebraMap_apply, ‚Üê Polynomial.algebraMap_eq, ‚Üê algebraMap_apply]
  obtain ‚ü®q, hq‚ü© := exists_isIntegral_sub_of_isIntegralElem_of_mul_mem_range (R := R')
    (aeval (algebraMap S S' (œÜ X))) (algebraMap S S' t) (C ha.unit‚Åª¬π.1 * p.map (algebraMap _ _)) (by
      obtain ‚ü®q, hqm, hq‚ü© := ht
      refine ‚ü®q.map (mapRingHom (algebraMap _ _)), hqm.map _, ?_‚ü©
      rw [eval‚ÇÇ_map, H, ‚Üê hom_eval‚ÇÇ, ‚Üê AlgHom.toRingHom_eq_coe, hq, map_zero]) (by
      nontriviality R'
      simp [Monic, leadingCoeff_C_mul_of_isUnit,
        leadingCoeff_map_of_leadingCoeff_ne_zero _ ha.ne_zero, a]) (by
      obtain ‚ü®r, hr : œÜ r = _‚ü© := hp
      use C ha.unit‚Åª¬π.1 * mapRingHom (algebraMap R R') r
      simp [aeval_algebraMap_apply, aeval_algHom_apply, hr, mul_assoc])
  obtain ‚ü®‚ü®_, n, rfl‚ü©, e‚ü© := IsLocalization.integerNormalization_map_to_map (.powers a) q
  generalize IsLocalization.integerNormalization (.powers a) q = q' at e
  have : IsIntegral R' ((algebraMap S S') (a ^ n ‚Ä¢ t - œÜ q')) := by
    have : algebraMap S S' (œÜ q') = (algebraMap R S' a) ^ n * aeval (algebraMap S S' (œÜ X)) q := by
      simpa [Algebra.smul_def, aeval_algebraMap_apply, aeval_algHom_apply, ‚Üê algebraMap_apply] using
        congr(aeval (algebraMap S S' (œÜ X)) $e)
    simpa [Algebra.smul_def, ‚Üê mul_sub, ‚Üê algebraMap_apply, this] using
      (isIntegral_algebraMap (A := S') (x := algebraMap R R' a ^ n)).mul hq
  obtain ‚ü®‚ü®_, m, rfl‚ü©, hm‚ü© := this.exists_multiple_integral_of_isLocalization (.powers a) _
  simp only [Algebra.smul_def, Submonoid.smul_def, algebraMap_apply R S S', ‚Üê map_mul] at hm
  obtain ‚ü®_, ‚ü®k, rfl‚ü©, hk‚ü© := IsLocalization.exists_isIntegral_smul_of_isIntegral_map (.powers a) hm
  refine ‚ü®C a ^ (k + m) * q', k + m + n, ?_‚ü©
  convert hk using 1
  simp only [Algebra.smul_def, map_pow, ‚Üê Polynomial.algebraMap_eq, map_mul, AlgHom.commutes]
  ring

@[stacks 00PX]
lemma exists_leadingCoeff_pow_smul_mem_conductor
    (hRS : integralClosure R S = ‚ä•) -- `IsIntegrallyClosedIn` but without injective assumption
    (hœÜ : œÜ.toRingHom.Finite) (hp : œÜ p * t ‚àà conductor R (œÜ X)) :
    ‚àÉ n, p.leadingCoeff ^ n ‚Ä¢ t ‚àà conductor R (œÜ X) := by
  algebraize [œÜ.toRingHom]
  have : IsScalarTower R R[X] S := .of_algebraMap_eq' œÜ.comp_algebraMap.symm
  have (x : _) : ‚àÉ n, p.leadingCoeff ^ n ‚Ä¢ (t * x) ‚àà œÜ.range := by
    have : œÜ p * t * x ‚àà œÜ.range := by simpa [‚Üê AlgHom.map_adjoin_singleton] using hp x
    obtain ‚ü®q, n, hn‚ü© :=
      exists_isIntegral_leadingCoeff_pow_smul_sub_of_isIntegralElem_of_mul_mem_range œÜ _ p
        (hœÜ.to_isIntegral (t * x)) (by convert this using 1; ring)
    obtain ‚ü®r, hr : algebraMap _ _ r = _‚ü© := hRS.le hn
    exact ‚ü®n, (C r + q), by simp [‚Üê Polynomial.algebraMap_eq, - Polynomial.algebraMap_apply, hr]‚ü©
  choose n hn using this
  obtain ‚ü®s, hs‚ü© := Module.Finite.fg_top (R := R[X]) (M := S)
  refine ‚ü®s.sup n, fun x ‚Ü¶ ?_‚ü©
  rw [‚Üê AlgHom.map_adjoin_singleton, adjoin_X, Algebra.map_top, Algebra.smul_mul_assoc]
  induction hs.ge (Set.mem_univ x) using Submodule.span_induction with
  | mem x h =>
    rw [‚Üê Nat.sub_add_cancel (s.le_sup h), pow_add, mul_smul]
    exact Subalgebra.smul_mem _ (hn _) _
  | zero => simp
  | add x y _ _ hx hy => simpa only [mul_add, smul_add] using add_mem hx hy
  | smul a x hx IH =>
    rw [mul_smul_comm, smul_comm, Algebra.smul_def]
    exact mul_mem (AlgHom.mem_range_self _ _) IH

@[stacks 00PY]
lemma exists_leadingCoeff_pow_smul_mem_radical_conductor
    (hRS : integralClosure R S = ‚ä•) -- `IsIntegrallyClosedIn` but without injective assumption
    (hœÜ : œÜ.toRingHom.Finite) (hp : œÜ p * t ‚àà (conductor R (œÜ X)).radical) (i : ‚Ñï) :
    p.coeff i ‚Ä¢ t ‚àà (conductor R (œÜ X)).radical := by
  wlog hi : i = p.natDegree generalizing p i
  ¬∑ clear hi
    simp only [forall_eq, coeff_natDegree] at this
    induction hpn : p.natDegree using Nat.strong_induction_on generalizing p with
    | h n IH =>
    cases n with
    | zero =>
      obtain hi' | hi' := lt_or_ge p.natDegree i
      ¬∑ simp [coeff_eq_zero_of_natDegree_lt hi']
      ¬∑ simpa [‚Üê coeff_natDegree, hpn, show i = 0 by aesop] using this _ hp
    | succ n =>
      obtain hi' | hi' := eq_or_ne i p.natDegree
      ¬∑ simpa [hi'] using this _ hp
      have : œÜ p.eraseLead * t ‚àà (conductor R (œÜ X)).radical := by
        simp only [‚Üê self_sub_C_mul_X_pow, map_sub, ‚Üê algebraMap_eq, map_mul, AlgHom.commutes,
          map_pow, sub_mul, mul_right_comm _ _ t, ‚Üê Algebra.smul_def _ t]
        exact sub_mem hp (Ideal.mul_mem_right _ _ (this _ hp))
      simpa [eraseLead_coeff, hi'] using
        IH _ ((eraseLead_natDegree_le _).trans_lt (by aesop)) _ this rfl
  obtain ‚ü®n, hn‚ü© := hp
  obtain ‚ü®k, hk‚ü© := exists_leadingCoeff_pow_smul_mem_conductor œÜ  (t ^ n) (p ^ n) hRS hœÜ
    (by simpa [mul_pow] using hn)
  by_cases hpn : p.leadingCoeff ^ n = 0
  ¬∑ use n; simp [_root_.smul_pow, hpn, hi]
  rw [leadingCoeff_pow' hpn, ‚Üê pow_mul] at hk
  refine ‚ü®n * k + n, ?_‚ü©
  rw [_root_.smul_pow,  pow_add, add_comm, pow_add, mul_smul_mul_comm, hi]
  exact Ideal.mul_mem_right _ _ hk

@[stacks 00PY]
lemma isStronglyTranscendental_mk_radical_conductor
    (hRS : integralClosure R S = ‚ä•) -- `IsIntegrallyClosedIn` but without injective assumption
    (x : S) (hx : (aeval (R := R) x).Finite) :
    IsStronglyTranscendental R (Ideal.Quotient.mk (conductor R x).radical x) := by
  refine Ideal.Quotient.mk_surjective.forall.mpr fun u p e ‚Ü¶ ?_
  rw [‚Üê Ideal.Quotient.algebraMap_eq, aeval_algebraMap_apply, Ideal.Quotient.algebraMap_eq,
    ‚Üê map_mul, Ideal.Quotient.eq_zero_iff_mem] at e
  ext i
  simpa [‚Üê Ideal.Quotient.mk_algebraMap, ‚Üê map_mul, Ideal.Quotient.eq_zero_iff_mem,
    Algebra.smul_def] using exists_leadingCoeff_pow_smul_mem_radical_conductor _ u p hRS hx
      (by simpa using e) i

end IsStronglyTranscendental

namespace Algebra

attribute [local instance] Polynomial.isLocalization Polynomial.algebra

section not_quasiFiniteAt

/-- Use `not_quasiFiniteAt_of_stronglyTranscendental` below instead. -/
private lemma not_quasiFiniteAt_of_stronglyTranscendental_of_isIntegrallyClosed [FaithfulSMul R S]
    [IsIntegrallyClosed R] [IsDomain S]
    {x : S} (hx : IsStronglyTranscendental R x) (hx' : (aeval (R := R) x).Finite)
    (P : Ideal S) [P.IsPrime] : ¬¨ Algebra.QuasiFiniteAt R P := by
  intro H
  have : IsDomain R := (FaithfulSMul.algebraMap_injective R S).isDomain
  have hf' : Function.Injective (aeval (R := R) x) := (injective_iff_map_eq_zero _).mpr
    fun p hp ‚Ü¶ not_not.mp fun hp' ‚Ü¶ hx.transcendental ‚ü®p, hp', hp‚ü©
  generalize hf : aeval (R := R) x = f at *
  obtain rfl : f X = x := by simp [‚Üê hf]
  let := f.toRingHom.toAlgebra
  have := IsScalarTower.of_algebraMap_eq' f.comp_algebraMap.symm
  have : Module.Finite R[X] S := RingHom.finite_algebraMap.mpr hx'
  have : FaithfulSMul R[X] S := by
    rw [faithfulSMul_iff_algebraMap_injective, injective_iff_map_eq_zero]
    intro p hp
    by_contra hp'
    exact hx.transcendental ‚ü®p, hp', by rwa [aeval_algHom_apply, aeval_X_left_apply]‚ü©
  have : (P.under R).map C < P.under R[X] := Polynomial.map_under_lt_comap_of_quasiFiniteAt _ _
  obtain ‚ü®Q, hQ, _, ‚ü®e‚ü©‚ü© := Ideal.exists_ideal_lt_liesOver_of_lt (S := S) P this
  refine hQ.ne (Algebra.QuasiFiniteAt.eq_of_le_of_under_eq (R := R) hQ.le ?_)
  rw [‚Üê Ideal.under_under (B := R[X]), ‚Üê e]
  ext
  simp [Ideal.mem_map_C_iff, coeff_C, apply_ite]

/-- This asks for an explict `K = Frac(R)`, `L = Frac(S)`,
`R'` the integral closure of `R` in `K`, and `S' ‚äÜ L` the subalgebra spanned by `R'` and `S`,
to aid typeclass synthesis.

Use `not_quasiFiniteAt_of_stronglyTranscendental` below instead. -/
@[stacks 00Q1]
private lemma not_quasiFiniteAt_of_stronglyTranscendental_of_isDomain_aux
    (K L : Type*) [Field K] [Field L] [Algebra R K] [Algebra R L] [Algebra S L] [Algebra K L]
    [IsScalarTower R K L] [IsScalarTower R S L] [IsFractionRing R K] [IsFractionRing S L]
    {R' S' : Type*} [CommRing R'] [CommRing S'] [Algebra R R'] [Algebra R' S'] [Algebra R S']
    [Algebra S' L] [Algebra R' L] [IsScalarTower R' S' L] [Algebra R' K] [IsScalarTower R' K L]
    [IsScalarTower R R' S'] [FaithfulSMul S' L] [IsIntegralClosure R' R K]
    [IsScalarTower R R' K]
    (f : S ‚Üí‚Çê[R] S') (hf‚ÇÅ : Function.Surjective
      (Algebra.TensorProduct.lift (Algebra.ofId R' S') f fun _ _ ‚Ü¶ .all _ _))
    (hf‚ÇÇ : (algebraMap S' L).comp f.toRingHom = algebraMap _ _)
    {x : S} (hx : IsStronglyTranscendental R x) (hx' : (aeval (R := R) x).Finite)
    (P : Ideal S) [P.IsPrime] : ¬¨ Algebra.QuasiFiniteAt R P := by
  have := (FaithfulSMul.algebraMap_injective S' L).isDomain
  have := (FaithfulSMul.algebraMap_injective R K).isDomain
  have : Algebra.IsIntegral R R' := IsIntegralClosure.isIntegral_algebra _ K
  have : FaithfulSMul R' K := (faithfulSMul_iff_algebraMap_injective _ _).mpr
    (IsIntegralClosure.algebraMap_injective R' R K)
  have : FaithfulSMul R R' := .tower_bot _ _ K
  have : FaithfulSMul R' L := .trans _ K _
  have : FaithfulSMul R' S' := .tower_bot _ _ L
  have : IsIntegrallyClosedIn R' K := .of_isIntegralClosure R
  have : IsIntegrallyClosed R' := .of_isIntegrallyClosed_of_isIntegrallyClosedIn _ K
  let g := Algebra.TensorProduct.lift (Algebra.ofId R' S') f fun _ _ ‚Ü¶ .all _ _
  have hf‚ÇÉ : Function.Injective f :=
    .of_comp (f := algebraMap S' L) (hf‚ÇÇ ‚ñ∏ FaithfulSMul.algebraMap_injective S L:)
  have hf‚ÇÑ : f.IsIntegral := by
    have : f = (g.restrictScalars R).comp ((Algebra.TensorProduct.comm _ _ _).toAlgHom.comp
        (IsScalarTower.toAlgHom _ _ _)) := by ext; simp [g]
    simp only [this, AlgEquiv.toAlgHom_eq_coe, AlgHom.toRingHom_eq_coe,
      AlgHom.comp_toRingHom, ‚Üê RingHom.comp_assoc]
    refine .trans _ _ (algebraMap_isIntegral_iff.mpr inferInstance) ?_
    exact RingHom.isIntegral_of_surjective _
      (hf‚ÇÅ.comp (Algebra.TensorProduct.comm _ _ _).surjective)
  have H‚ÇÅ : IsStronglyTranscendental R' (f x) := by
    refine .of_map (f := IsScalarTower.toAlgHom R' S' L) (FaithfulSMul.algebraMap_injective S' L) ?_
    dsimp
    rw [show algebraMap S' L (f x) = algebraMap _ _ x from congr($hf‚ÇÇ x)]
    exact ((hx.of_isLocalization S‚Å∞).of_isLocalization_left R‚Å∞).restrictScalars (S := K)
  have H‚ÇÇ : (aeval (R := R') (f x)).toRingHom.Finite := by
    convert ((RingHom.Finite.of_surjective g.toRingHom hf‚ÇÅ).comp
      (RingHom.Finite.tensorProductMap (f := AlgHom.id R R') (RingEquiv.refl _).finite hx')).comp
      (polyEquivTensor R R').toRingEquiv.finite using 1
    ext <;> simp [g]
  obtain ‚ü®‚ü®Q, _‚ü©, hQ‚ü© := hf‚ÇÑ.comap_surjective hf‚ÇÉ ‚ü®P, ‚Äπ_‚Ä∫‚ü©
  refine fun H ‚Ü¶ not_quasiFiniteAt_of_stronglyTranscendental_of_isIntegrallyClosed H‚ÇÅ H‚ÇÇ Q ?_
  have : Algebra.QuasiFiniteAt R' (Q.comap g.toRingHom) := .baseChange P _ <| by
    rw [Ideal.comap_comap]
    convert congr(($hQ.symm).1)
    ext; simp [g]
  exact .of_surjectiveOnStalks _ g (RingHom.surjectiveOnStalks_of_surjective hf‚ÇÅ) _ rfl

@[stacks 00Q2]
nonrec lemma not_quasiFiniteAt_of_stronglyTranscendental [IsReduced S] [FaithfulSMul R S]
    {x : S} (hx : IsStronglyTranscendental R x) (hx' : (aeval (R := R) x).Finite)
    (P : Ideal S) [P.IsPrime] : ¬¨ Algebra.QuasiFiniteAt R P := by
  wlog hS : IsDomain S
  ¬∑ intro H
    obtain ‚ü®p, hp, hpP‚ü© := Ideal.exists_minimalPrimes_le (J := P) bot_le
    have inst := hp.1.1
    have inst : (P.map (Ideal.Quotient.mk p)).IsPrime :=
      Ideal.map_isPrime_of_surjective Ideal.Quotient.mk_surjective (by simpa)
    refine this (R := R ‚ß∏ p.under R)
      ((isStronglyTranscendental_mk_of_mem_minimalPrimes hx p hp).of_surjective_left
        Ideal.Quotient.mk_surjective) ?_ (P.map (Ideal.Quotient.mk p)) inferInstance ?_
    ¬∑ refine RingHom.Finite.of_comp_finite (f := mapRingHom (Ideal.Quotient.mk _)) ?_
      convert (RingHom.Finite.of_surjective _ (Ideal.Quotient.mk_surjective (I := p))).comp hx'
      ext <;> simp
    ¬∑ suffices Algebra.QuasiFiniteAt R (P.map (Ideal.Quotient.mk p)) from .of_restrictScalars R _ _
      refine .of_surjectiveOnStalks P (Ideal.Quotient.mk‚Çê _ _)
        (RingHom.surjectiveOnStalks_of_surjective Ideal.Quotient.mk_surjective) _ ?_
      refine .trans ?_ (Ideal.comap_map_of_surjective _ Ideal.Quotient.mk_surjective _).symm
      simpa [‚Üê RingHom.ker_eq_comap_bot]
  have : IsDomain R := (FaithfulSMul.algebraMap_injective R S).isDomain
  let K := FractionRing R
  let L := FractionRing S
  let : Algebra K L := FractionRing.liftAlgebra _ _
  let R' := integralClosure R K
  let S' : Subalgebra R' L := Algebra.adjoin R' (algebraMap S L).range
  let f : S ‚Üí‚Çê[R] S' := (IsScalarTower.toAlgHom R S L).codRestrict (S'.restrictScalars R) fun x ‚Ü¶ by
    simpa using show algebraMap S L x ‚àà S' from Algebra.subset_adjoin ‚ü®x, rfl‚ü©
  let g := Algebra.TensorProduct.lift (Algebra.ofId R' S') f fun _ _ ‚Ü¶ .all _ _
  have hf : Function.Surjective g := by
    rw [‚Üê AlgHom.range_eq_top,
      ‚Üê (Subalgebra.map_injective (f := S'.val) Subtype.val_injective).eq_iff, Algebra.map_top]
    refine le_antisymm (Set.image_subset_range S'.val g.range) ?_
    simp only [RingHom.coe_range, Subalgebra.range_val, Algebra.adjoin_le_iff, Subalgebra.coe_map,
      Subalgebra.coe_val, AlgHom.coe_range, Set.range_subset_iff, Set.mem_image, Set.mem_range,
      exists_exists_eq_and, S']
    exact fun y ‚Ü¶ ‚ü®1 ‚äó‚Çú y, by simp [g, S']; rfl‚ü©
  exact not_quasiFiniteAt_of_stronglyTranscendental_of_isDomain_aux K L f hf rfl hx hx' P

end not_quasiFiniteAt

section FixedUniverse

universe u

variable {R S : Type u} [CommRing R] [CommRing S] [Algebra R S]

lemma ZariskisMainProperty.of_adjoin_eq_top
    (p : Ideal S) [p.IsPrime] [Algebra.QuasiFiniteAt R p]
    (x : S) (hx : Algebra.adjoin R {x} = ‚ä§) : ZariskisMainProperty R p := by
  wlog H : integralClosure R S = ‚ä•
  ¬∑ letI inst : Algebra (integralClosure R S) (Localization.AtPrime p) :=
      OreLocalization.instAlgebra
    have inst : Algebra.QuasiFiniteAt (integralClosure R S) p :=
      .of_restrictScalars R (integralClosure R S) _
    refine .restrictScalars (this p x ?_ (integralClosure_idem (R := R)))
    suffices ‚ä§ ‚â§ (Algebra.adjoin (integralClosure R S) {x}).restrictScalars R from
      top_le_iff.mp fun x _ ‚Ü¶ (Subalgebra.mem_restrictScalars _).mp (this trivial)
    refine hx.ge.trans ?_
    rw [Algebra.restrictScalars_adjoin]
    exact Algebra.adjoin_mono (by aesop)
  have H‚ÇÄ : Function.Surjective (aeval (R := R) x) := by
    rwa [‚Üê AlgHom.range_eq_top, ‚Üê Algebra.adjoin_singleton_eq_range_aeval]
  have ‚ü®f, (hf : aeval x f = 0), hfp‚ü© := SetLike.not_le_iff_exists.mp
    (Polynomial.not_ker_le_map_C_of_surjective_of_quasiFiniteAt _ H‚ÇÄ p)
  obtain ‚ü®n, hfn‚ü© : ‚àÉ x, algebraMap R S (f.coeff x) ‚àâ p := by simpa [Ideal.mem_map_C_iff] using hfp
  clear hfp
  induction hm : f.natDegree using Nat.strong_induction_on generalizing f n with | h m IH =>
  obtain (_ | m) := m
  ¬∑ obtain ‚ü®r, rfl‚ü© := natDegree_eq_zero.mp hm
    cases n <;> aesop
  by_cases Hfp : algebraMap _ _ f.leadingCoeff ‚àà p
  ¬∑ obtain ‚ü®a, ha‚ü© := H.le (isIntegral_leadingCoeff_smul f x hf)
    refine IH _ ?_ (f.eraseLead + C a * X ^ m) (hm := rfl) ?_ n ?_
    ¬∑ suffices f.eraseLead.natDegree ‚â§ m by compute_degree!
      exact (eraseLead_natDegree_le ..).trans (by lia)
    ¬∑ simp [‚Üê self_sub_monomial_natDegree_leadingCoeff, hf, hm, pow_succ', ‚Üê Algebra.smul_def,
        ‚Üê Algebra.smul_mul_assoc, ‚Üê ha]
    ¬∑ suffices algebraMap R S (f.coeff n) + algebraMap R S (if n = m then a else 0) ‚àâ p by
        simpa [eraseLead_coeff, show n ‚â† f.natDegree by rintro rfl; exact hfn (by simpa)]
      rwa [Ideal.add_mem_iff_left]
      split_ifs
      ¬∑ convert p.mul_mem_right x Hfp
        simpa [Algebra.smul_def] using ha
      ¬∑ simp
  ¬∑ refine zariskisMainProperty_iff_exists_saturation_eq_top.mpr ‚ü®_, Hfp, isIntegral_algebraMap, ?_‚ü©
    rw [‚Üê top_le_iff, ‚Üê hx]
    refine Algebra.adjoin_singleton_le ‚ü®_, ‚ü®1, rfl‚ü©, ?_‚ü©
    simpa [Algebra.smul_def] using isIntegral_leadingCoeff_smul f x hf

lemma ZariskisMainProperty.of_algHom_polynomial
    (p : Ideal S) [p.IsPrime] [Algebra.QuasiFiniteAt R p]
    (f : R[X] ‚Üí‚Çê[R] S) (hf : f.Finite) : ZariskisMainProperty R p := by
  wlog H : integralClosure R S = ‚ä•
  ¬∑ letI inst : Algebra (integralClosure R S) (Localization.AtPrime p) :=
      OreLocalization.instAlgebra
    have inst : Algebra.QuasiFiniteAt (integralClosure R S) p :=
      .of_restrictScalars R (integralClosure R S) _
    refine .restrictScalars (this p (aeval (f X)) ?_  (integralClosure_idem (R := R)))
    refine RingHom.Finite.of_comp_finite (f := mapRingHom (algebraMap R _)) ?_
    convert (show f.toRingHom.Finite from hf)
    ext <;> simp [show ‚àÄ x, f (C x) = algebraMap _ _ x from f.commutes]
  have := isStronglyTranscendental_mk_radical_conductor H (f X) (by convert hf; ext; simp)
  have := this.of_surjective_left (S := R ‚ß∏ (conductor R (f X)).radical.under R)
    Ideal.Quotient.mk_surjective
  have : ¬¨ conductor R (f X) ‚â§ p := by
    intro hp
    rw [‚Üê ‚Äπp.IsPrime‚Ä∫.isRadical.radical_le_iff] at hp
    have inst : (p.map (Ideal.Quotient.mk (conductor R (f X)).radical)).IsPrime :=
      Ideal.map_isPrime_of_surjective Ideal.Quotient.mk_surjective (by simpa using hp)
    have inst : IsReduced (S ‚ß∏ (conductor R (f X)).radical) :=
      (Ideal.isRadical_iff_quotient_reduced _).mp (Ideal.radical_isRadical _)
    refine not_quasiFiniteAt_of_stronglyTranscendental this ?_
      (p.map (Ideal.Quotient.mk (conductor R (f X)).radical)) ?_
    ¬∑ refine RingHom.Finite.of_comp_finite (f := mapRingHom (Ideal.Quotient.mk _)) ?_
      convert (RingHom.Finite.of_surjective (Ideal.Quotient.mk (conductor R (f X)).radical)
        Ideal.Quotient.mk_surjective).comp hf using 1
      have (x : _) : f (C x) = algebraMap _ _ x := f.commutes _
      ext <;> simp [show ‚àÄ x, f (C x) = algebraMap _ _ x from f.commutes]
    ¬∑ suffices Algebra.QuasiFiniteAt R (p.map (Ideal.Quotient.mk (conductor R (f X)).radical)) from
        .of_restrictScalars R _ _
      refine .of_surjectiveOnStalks p (Ideal.Quotient.mk‚Çê R (conductor R (f X)).radical)
        (RingHom.surjectiveOnStalks_of_surjective Ideal.Quotient.mk_surjective) _
        ((Ideal.comap_map_of_surjective _ Ideal.Quotient.mk_surjective p).trans ?_).symm
      simpa [‚Üê RingHom.ker_eq_comap_bot]
  obtain ‚ü®x, hx, hxp‚ü© := SetLike.not_le_iff_exists.mp this
  replace hx (a : _) : x * a ‚àà f.range := by simpa [‚Üê AlgHom.map_adjoin_singleton f] using hx a
  refine ZariskisMainProperty.trans (S := f.range) _ ?_ ?_
  ¬∑ have : Algebra.QuasiFiniteAt R (p.under f.range) := by
      let e : Localization.AtPrime (p.under f.range) ‚âÉ‚Çê[R] Localization.AtPrime p :=
        .ofBijective (IsScalarTower.toAlgHom _ _ _)
          (Localization.localRingHom_bijective_of_not_conductor_le this
            (by simp [‚Üê AlgHom.map_adjoin_singleton f]) _)
      exact .of_surjective_algHom e.symm.toAlgHom e.symm.surjective
    refine .of_adjoin_eq_top _ ‚ü®f X, X, rfl‚ü© ?_
    simp [‚Üê (Subalgebra.map_injective (f := Subalgebra.val _) Subtype.val_injective).eq_iff,
      ‚Üê AlgHom.map_adjoin_singleton f, Subalgebra.range_val]
  ¬∑ refine ‚ü®‚ü®x, by simpa using hx 1‚ü©, hxp, top_le_iff.mp fun s _ ‚Ü¶ ‚ü®_, ‚ü®1, rfl‚ü©, ?_‚ü©‚ü©
    simpa [Algebra.mem_bot] using hx s

end FixedUniverse

end Algebra
