/-
Copyright (c) 2024 Thomas Browning. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Thomas Browning
-/
import Mathlib.RingTheory.Invariant.Defs
import Mathlib.RingTheory.IntegralClosure.IntegralRestrict

/-!
# Invariant Extensions of Rings

Given an extension of rings `B/A` and an action of `G` on `B`, we introduce a predicate
`Algebra.IsInvariant A B G` which states that every fixed point of `B` lies in the image of `A`.

The main application is in algebraic number theory, where `G := Gal(L/K)` is the Galois group
of some finite Galois extension of number fields, and `A := ùìûK` and `B := ùìûL` are their ring of
integers. This main result in this file implies the existence of Frobenius elements in this setting.
See `Mathlib/RingTheory/Frobenius.lean`.

## Main statements

Let `G` be a finite group acting on a commutative ring `B` satisfying `Algebra.IsInvariant A B G`.

* `Algebra.IsInvariant.isIntegral`: `B/A` is an integral extension.
* `Algebra.IsInvariant.exists_smul_of_under_eq`: `G` acts transitivity on the prime ideals of `B`
  lying above a given prime ideal of `A`.

If `Q` is a prime ideal of `B` lying over a prime ideal `P` of `A`, then

* `IsFractionRing.stabilizerHom_surjective`:
  The stabilizer subgroup of `Q` surjects onto `Aut(Frac(B/Q)/Frac(A/P))`.
* `Ideal.Quotient.stabilizerHom_surjective`:
  The stabilizer subgroup of `Q` surjects onto `Aut((B/Q)/(A/P))`.
* `Ideal.Quotient.exists_algEquiv_fixedPoint_quotient_under`:
  If `k` is a domain containing `B/Q`, then any `A/P`-algebra automorphism of `k` restricts to
  an automorphism of `B/Q`.
-/

open scoped Pointwise

section Galois

variable (A K L B : Type*) [CommRing A] [CommRing B] [Field K] [Field L]
  [Algebra A K] [Algebra B L] [IsFractionRing A K] [IsFractionRing B L]
  [Algebra A B] [Algebra K L] [Algebra A L] [IsScalarTower A K L] [IsScalarTower A B L]
  [IsIntegrallyClosed A] [IsIntegralClosure B A L]

/-- In the AKLB setup, the Galois group of `L/K` acts on `B`. -/
noncomputable def IsIntegralClosure.MulSemiringAction [Algebra.IsAlgebraic K L] :
    MulSemiringAction (L ‚âÉ‚Çê[K] L) B :=
  MulSemiringAction.compHom B (galRestrict A K L B).toMonoidHom

/-- In the AKLB setup, every fixed point of `B` lies in the image of `A`. -/
theorem Algebra.isInvariant_of_isGalois [FiniteDimensional K L] [h : IsGalois K L] :
    letI := IsIntegralClosure.MulSemiringAction A K L B
    Algebra.IsInvariant A B (L ‚âÉ‚Çê[K] L) := by
  replace h := ((IsGalois.tfae (F := K) (E := L)).out 0 1).mp h
  letI := IsIntegralClosure.MulSemiringAction A K L B
  refine ‚ü®fun b hb ‚Ü¶ ?_‚ü©
  replace hb : algebraMap B L b ‚àà IntermediateField.fixedField (‚ä§ : Subgroup (L ‚âÉ‚Çê[K] L)) := by
    rintro ‚ü®g, -‚ü©
    exact (algebraMap_galRestrict_apply A g b).symm.trans (congrArg (algebraMap B L) (hb g))
  rw [h, IntermediateField.mem_bot] at hb
  obtain ‚ü®k, hk‚ü© := hb
  have hb : IsIntegral A b := IsIntegralClosure.isIntegral A L b
  rw [‚Üê isIntegral_algebraMap_iff (FaithfulSMul.algebraMap_injective B L), ‚Üê hk,
    isIntegral_algebraMap_iff (FaithfulSMul.algebraMap_injective K L)] at hb
  obtain ‚ü®a, rfl‚ü© := IsIntegrallyClosed.algebraMap_eq_of_integral hb
  rw [‚Üê IsScalarTower.algebraMap_apply, IsScalarTower.algebraMap_apply A B L,
    (FaithfulSMul.algebraMap_injective B L).eq_iff] at hk
  exact ‚ü®a, hk‚ü©

/-- A variant of `Algebra.isInvariant_of_isGalois`, replacing `Gal(L/K)` by `Aut(B/A)`. -/
theorem Algebra.isInvariant_of_isGalois' [FiniteDimensional K L] [IsGalois K L] :
    Algebra.IsInvariant A B (B ‚âÉ‚Çê[A] B) :=
  ‚ü®fun b h ‚Ü¶ (isInvariant_of_isGalois A K L B).1 b (fun g ‚Ü¶ h (galRestrict A K L B g))‚ü©

end Galois

section Quotient

variable {A B : Type*} [CommRing A] [CommRing B] [Algebra A B]
variable {G : Type*} [Group G] [MulSemiringAction G B] [SMulCommClass G A B]

instance (H : Subgroup G) [H.Normal] :
    MulSemiringAction (G ‚ß∏ H) (FixedPoints.subring B H) where
  smul := Quotient.lift (fun g x ‚Ü¶ ‚ü®g ‚Ä¢ x, fun h ‚Ü¶ by
    simpa [mul_smul] using congr(g ‚Ä¢ $(x.2 ‚ü®_, ‚ÄπH.Normal‚Ä∫.conj_mem' _ h.2 g‚ü©))‚ü©) (by
    rintro _ a ‚ü®‚ü®‚ü®b‚ü©, hb‚ü©, rfl‚ü©
    ext c
    simpa [mul_smul] using congr(a ‚Ä¢ $(c.2 ‚ü®b, hb‚ü©)))
  one_smul b := Subtype.ext (one_smul G b.1)
  mul_smul := Quotient.ind‚ÇÇ fun _ _ _ ‚Ü¶ Subtype.ext (mul_smul _ _ _)
  smul_zero := Quotient.ind fun _ ‚Ü¶ Subtype.ext (smul_zero _)
  smul_add := Quotient.ind fun _ _ _ ‚Ü¶ Subtype.ext (smul_add _ _ _)
  smul_one := Quotient.ind fun _ ‚Ü¶ Subtype.ext (smul_one _)
  smul_mul := Quotient.ind fun _ _ _ ‚Ü¶ Subtype.ext (MulSemiringAction.smul_mul _ _ _)

instance (H : Subgroup G) [H.Normal] :
    MulSemiringAction (G ‚ß∏ H) (FixedPoints.subalgebra A B H) :=
  inferInstanceAs (MulSemiringAction (G ‚ß∏ H) (FixedPoints.subring B H))

instance (H : Subgroup G) [H.Normal] :
    SMulCommClass (G ‚ß∏ H) A (FixedPoints.subalgebra A B H)  where
  smul_comm := Quotient.ind fun g r h ‚Ü¶ Subtype.ext (smul_comm g r h.1)

instance (H : Subgroup G) [H.Normal] [Algebra.IsInvariant A B G] :
    Algebra.IsInvariant A (FixedPoints.subalgebra A B H) (G ‚ß∏ H) where
  isInvariant x hx := by
    obtain ‚ü®y, hy‚ü© := Algebra.IsInvariant.isInvariant (A := A) (G := G) x.1
      (fun g ‚Ü¶ congr_arg Subtype.val (hx g))
    exact ‚ü®y, Subtype.ext hy‚ü©

end Quotient

section transitivity

variable (A B G : Type*) [CommRing A] [CommRing B] [Algebra A B] [Group G] [MulSemiringAction G B]

namespace MulSemiringAction

open Polynomial

variable {B} [Fintype G]

/-- Characteristic polynomial of a finite group action on a ring. -/
noncomputable def charpoly (b : B) : B[X] := ‚àè g : G, (X - C (g ‚Ä¢ b))

theorem charpoly_eq (b : B) : charpoly G b = ‚àè g : G, (X - C (g ‚Ä¢ b)) := rfl

theorem charpoly_eq_prod_smul (b : B) : charpoly G b = ‚àè g : G, g ‚Ä¢ (X - C b) := by
  simp only [smul_sub, smul_C, smul_X, charpoly_eq]

theorem monic_charpoly (b : B) : (charpoly G b).Monic :=
  monic_prod_of_monic _ _ (fun _ _ ‚Ü¶ monic_X_sub_C _)

theorem eval_charpoly (b : B) : (charpoly G b).eval b = 0 := by
  rw [charpoly_eq, eval_prod]
  apply Finset.prod_eq_zero (Finset.mem_univ (1 : G))
  rw [one_smul, eval_sub, eval_C, eval_X, sub_self]

variable {G}

theorem smul_charpoly (b : B) (g : G) : g ‚Ä¢ (charpoly G b) = charpoly G b := by
  rw [charpoly_eq_prod_smul, Finset.smul_prod_perm]

theorem smul_coeff_charpoly (b : B) (n : ‚Ñï) (g : G) :
    g ‚Ä¢ (charpoly G b).coeff n = (charpoly G b).coeff n := by
  rw [‚Üê coeff_smul, smul_charpoly]

end MulSemiringAction

namespace Algebra.IsInvariant

open MulSemiringAction Polynomial

variable [IsInvariant A B G]

theorem charpoly_mem_lifts [Fintype G] (b : B) :
    charpoly G b ‚àà Polynomial.lifts (algebraMap A B) :=
  (charpoly G b).lifts_iff_coeff_lifts.mpr fun n ‚Ü¶ isInvariant _ (smul_coeff_charpoly b n)

theorem isIntegral [Finite G] : Algebra.IsIntegral A B := by
  cases nonempty_fintype G
  refine ‚ü®fun b ‚Ü¶ ?_‚ü©
  obtain ‚ü®p, hp1, -, hp2‚ü© := Polynomial.lifts_and_natDegree_eq_and_monic
    (charpoly_mem_lifts A B G b) (monic_charpoly G b)
  exact ‚ü®p, hp2, by rw [‚Üê eval_map, hp1, eval_charpoly]‚ü©

/-- `G` acts transitively on the prime ideals of `B` above a given prime ideal of `A`. -/
theorem exists_smul_of_under_eq [Finite G] [SMulCommClass G A B]
    (P Q : Ideal B) [hP : P.IsPrime] [hQ : Q.IsPrime]
    (hPQ : P.under A = Q.under A) :
    ‚àÉ g : G, Q = g ‚Ä¢ P := by
  cases nonempty_fintype G
  have : ‚àÄ (P Q : Ideal B) [P.IsPrime] [Q.IsPrime], P.under A = Q.under A ‚Üí
      ‚àÉ g ‚àà (‚ä§ : Finset G), Q ‚â§ g ‚Ä¢ P := by
    intro P Q hP hQ hPQ
    rw [‚Üê Ideal.subset_union_prime 1 1 (fun _ _ _ _ ‚Ü¶ hP.smul _)]
    intro b hb
    suffices h : ‚àÉ g ‚àà Finset.univ, g ‚Ä¢ b ‚àà P by
      obtain ‚ü®g, -, hg‚ü© := h
      apply Set.mem_biUnion (Finset.mem_univ g‚Åª¬π) (Ideal.mem_inv_pointwise_smul_iff.mpr hg)
    obtain ‚ü®a, ha‚ü© := isInvariant (A := A) (‚àè g : G, g ‚Ä¢ b) (Finset.smul_prod_perm b)
    rw [‚Üê hP.prod_mem_iff, ‚Üê ha, ‚Üê P.mem_comap, ‚Üê P.under_def A,
      hPQ, Q.mem_comap, ha, hQ.prod_mem_iff]
    exact ‚ü®1, Finset.mem_univ 1, (one_smul G b).symm ‚ñ∏ hb‚ü©
  obtain ‚ü®g, -, hg‚ü© := this P Q hPQ
  obtain ‚ü®g', -, hg'‚ü© := this Q (g ‚Ä¢ P) ((P.under_smul A g).trans hPQ).symm
  exact ‚ü®g, le_antisymm hg (smul_eq_of_le_smul (hg.trans hg') ‚ñ∏ hg')‚ü©

theorem orbit_eq_primesOver [Finite G] [SMulCommClass G A B] (P : Ideal A) (Q : Ideal B)
    [hP : Q.LiesOver P] [hQ : Q.IsPrime] : MulAction.orbit G Q = P.primesOver B := by
  refine Set.ext fun R ‚Ü¶ ‚ü®fun ‚ü®g, hg‚ü© ‚Ü¶ hg ‚ñ∏ ‚ü®hQ.smul g, hP.smul g‚ü©, fun h ‚Ü¶ ?_‚ü©
  have : R.IsPrime := h.1
  obtain ‚ü®g, hg‚ü© := exists_smul_of_under_eq A B G Q R (hP.over.symm.trans h.2.over)
  exact ‚ü®g, hg.symm‚ü©

end Algebra.IsInvariant

end transitivity

section surjectivity

open FaithfulSMul IsScalarTower Polynomial

variable {A B : Type*} [CommRing A] [CommRing B] [Algebra A B]
  (G : Type*) [Group G] [Finite G] [MulSemiringAction G B] [SMulCommClass G A B]
  (P : Ideal A) (Q : Ideal B) [Q.IsPrime] [Q.LiesOver P]

variable (K L : Type*) [Field K] [Field L]
  [Algebra (A ‚ß∏ P) K] [Algebra (B ‚ß∏ Q) L]
  [Algebra (A ‚ß∏ P) L] [IsScalarTower (A ‚ß∏ P) (B ‚ß∏ Q) L]
  [Algebra K L] [IsScalarTower (A ‚ß∏ P) K L]
  [Algebra.IsInvariant A B G]

/-- A technical lemma for `fixed_of_fixed1`. -/
private theorem fixed_of_fixed1_aux1 [DecidableEq (Ideal B)] :
    ‚àÉ a b : B, (‚àÄ g : G, g ‚Ä¢ a = a) ‚àß a ‚àâ Q ‚àß
    ‚àÄ g : G, algebraMap B (B ‚ß∏ Q) (g ‚Ä¢ b) = algebraMap B (B ‚ß∏ Q) (if g ‚Ä¢ Q = Q then a else 0) := by
  obtain ‚ü®_‚ü© := nonempty_fintype G
  let P := Finset.inf {g : G | g ‚Ä¢ Q ‚â† Q} (fun g ‚Ü¶ g ‚Ä¢ Q)
  have h1 : ¬¨ P ‚â§ Q := by
    rw [Ideal.IsPrime.inf_le' inferInstance]
    rintro ‚ü®g, hg1, hg2‚ü©
    exact (Finset.mem_filter.mp hg1).2 (smul_eq_of_smul_le hg2)
  obtain ‚ü®b, hbP, hbQ‚ü© := SetLike.not_le_iff_exists.mp h1
  replace hbP : ‚àÄ g : G, g ‚Ä¢ Q ‚â† Q ‚Üí b ‚àà g ‚Ä¢ Q :=
    fun g hg ‚Ü¶ (Finset.inf_le (Finset.mem_filter.mpr ‚ü®Finset.mem_univ g, hg‚ü©) : P ‚â§ g ‚Ä¢ Q) hbP
  let f := MulSemiringAction.charpoly G b
  obtain ‚ü®q, hq, hq0‚ü© :=
    (f.map (algebraMap B (B ‚ß∏ Q))).exists_eq_pow_rootMultiplicity_mul_and_not_dvd
      (Polynomial.map_monic_ne_zero (MulSemiringAction.monic_charpoly G b)) 0
  rw [map_zero, sub_zero] at hq hq0
  let j := (f.map (algebraMap B (B ‚ß∏ Q))).rootMultiplicity 0
  let k := q.natDegree
  let r := ‚àë i ‚àà Finset.range (k + 1), Polynomial.monomial i (f.coeff (i + j))
  have hr : r.map (algebraMap B (B ‚ß∏ Q)) = q := by
    ext n
    rw [Polynomial.coeff_map, Polynomial.finset_sum_coeff]
    simp only [Polynomial.coeff_monomial, Finset.sum_ite_eq', Finset.mem_range_succ_iff]
    split_ifs with hn
    ¬∑ rw [‚Üê Polynomial.coeff_map, hq, Polynomial.coeff_X_pow_mul]
    ¬∑ rw [map_zero, eq_comm, Polynomial.coeff_eq_zero_of_natDegree_lt (lt_of_not_ge hn)]
  have hf : f.eval b = 0 := MulSemiringAction.eval_charpoly G b
  have hr : r.eval b ‚àà Q := by
    rw [‚Üê Ideal.Quotient.eq_zero_iff_mem, ‚Üê Ideal.Quotient.algebraMap_eq] at hbQ ‚ä¢
    replace hf := congrArg (algebraMap B (B ‚ß∏ Q)) hf
    rw [‚Üê Polynomial.eval‚ÇÇ_at_apply, ‚Üê Polynomial.eval_map] at hf ‚ä¢
    rwa [map_zero, hq, ‚Üê hr, Polynomial.eval_mul, Polynomial.eval_pow, Polynomial.eval_X,
      mul_eq_zero, or_iff_right (pow_ne_zero _ hbQ)] at hf
  let a := f.coeff j
  have ha : ‚àÄ g : G, g ‚Ä¢ a = a := MulSemiringAction.smul_coeff_charpoly b j
  have hr' : ‚àÄ g : G, g ‚Ä¢ Q ‚â† Q ‚Üí a - r.eval b ‚àà g ‚Ä¢ Q := by
    intro g hg
    have hr : r = ‚àë i ‚àà Finset.range (k + 1), Polynomial.monomial i (f.coeff (i + j)) := rfl
    rw [‚Üê Ideal.neg_mem_iff, neg_sub, hr, Finset.sum_range_succ', Polynomial.eval_add,
        Polynomial.eval_monomial, zero_add, pow_zero, mul_one, add_sub_cancel_right]
    simp only [ ‚Üê Polynomial.monomial_mul_X]
    rw [‚Üê Finset.sum_mul, Polynomial.eval_mul_X]
    exact Ideal.mul_mem_left (g ‚Ä¢ Q) _ (hbP g hg)
  refine ‚ü®a, a - r.eval b, ha, ?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ rwa [‚Üê Ideal.Quotient.eq_zero_iff_mem, ‚Üê Ideal.Quotient.algebraMap_eq, ‚Üê Polynomial.coeff_map,
      ‚Üê zero_add j, hq, Polynomial.coeff_X_pow_mul, ‚Üê Polynomial.X_dvd_iff]
  ¬∑ rw [‚Üê sub_eq_zero, ‚Üê map_sub, Ideal.Quotient.algebraMap_eq, Ideal.Quotient.eq_zero_iff_mem,
      ‚Üê Ideal.smul_mem_pointwise_smul_iff (a := h‚Åª¬π), smul_sub, inv_smul_smul]
    simp only [‚Üê eq_inv_smul_iff (g := h), eq_comm (a := Q)]
    split_ifs with hh
    ¬∑ rwa [ha, sub_sub_cancel_left, hh, Q.neg_mem_iff]
    ¬∑ rw [smul_zero, sub_zero]
      exact hr' h‚Åª¬π hh

/-- A technical lemma for `fixed_of_fixed1`. -/
private theorem fixed_of_fixed1_aux2 [DecidableEq (Ideal B)] (b‚ÇÄ : B)
    (hx : ‚àÄ g : G, g ‚Ä¢ Q = Q ‚Üí algebraMap B (B ‚ß∏ Q) (g ‚Ä¢ b‚ÇÄ) = algebraMap B (B ‚ß∏ Q) b‚ÇÄ) :
    ‚àÉ a b : B, (‚àÄ g : G, g ‚Ä¢ a = a) ‚àß a ‚àâ Q ‚àß
    (‚àÄ g : G, algebraMap B (B ‚ß∏ Q) (g ‚Ä¢ b) =
      algebraMap B (B ‚ß∏ Q) (if g ‚Ä¢ Q = Q then a * b‚ÇÄ else 0)) := by
  obtain ‚ü®a, b, ha1, ha2, hb‚ü© := fixed_of_fixed1_aux1 G Q
  refine ‚ü®a, b * b‚ÇÄ, ha1, ha2, fun g ‚Ü¶ ?_‚ü©
  rw [smul_mul', map_mul, hb]
  specialize hb g
  split_ifs with hg
  ¬∑ rw [map_mul, hx g hg]
  ¬∑ rw [map_zero, zero_mul]

/-- A technical lemma for `fixed_of_fixed1`. -/
private theorem fixed_of_fixed1_aux3 [NoZeroDivisors B] {b : B} {i j : ‚Ñï} {p : Polynomial A}
    (h : p.map (algebraMap A B) = (X - C b) ^ i * X ^ j) (f : B ‚âÉ‚Çê[A] B) (hi : i ‚â† 0) :
    f b = b := by
  by_cases ha : b = 0
  ¬∑ rw [ha, map_zero]
  have hf := congrArg (eval b) (congrArg (Polynomial.mapAlgHom f.toAlgHom) h)
  rw [coe_mapAlgHom, map_map, f.toAlgHom.comp_algebraMap, h] at hf
  simp_rw [Polynomial.map_mul, Polynomial.map_pow, Polynomial.map_sub, map_X, map_C,
    eval_mul, eval_pow, eval_sub, eval_X, eval_C, sub_self, zero_pow hi, zero_mul,
    zero_eq_mul, or_iff_left (pow_ne_zero j ha), pow_eq_zero_iff hi, sub_eq_zero] at hf
  exact hf.symm

/-- This theorem will be made redundant by `IsFractionRing.stabilizerHom_surjective`. -/
private theorem fixed_of_fixed1 [NoZeroSMulDivisors (B ‚ß∏ Q) L] (f : L ‚âÉ‚Çê[K] L) (b : B ‚ß∏ Q)
    (hx : ‚àÄ g : MulAction.stabilizer G Q, Ideal.Quotient.stabilizerHom Q P G g b = b) :
    f (algebraMap (B ‚ß∏ Q) L b) = (algebraMap (B ‚ß∏ Q) L b) := by
  classical
  cases nonempty_fintype G
  obtain ‚ü®b‚ÇÄ, rfl‚ü© := Ideal.Quotient.mk_surjective b
  rw [‚Üê Ideal.Quotient.algebraMap_eq]
  obtain ‚ü®a, b, ha1, ha2, hb‚ü© := fixed_of_fixed1_aux2 G Q b‚ÇÄ (fun g hg ‚Ü¶ hx ‚ü®g, hg‚ü©)
  obtain ‚ü®M, key‚ü© := (mem_lifts _).mp (Algebra.IsInvariant.charpoly_mem_lifts A B G b)
  replace key := congrArg (map (algebraMap B (B ‚ß∏ Q))) key
  rw [map_map, ‚Üê algebraMap_eq, algebraMap_eq A (A ‚ß∏ P) (B ‚ß∏ Q),
      ‚Üê map_map, MulSemiringAction.charpoly, Polynomial.map_prod] at key
  have key‚ÇÄ : ‚àÄ g : G, (X - C (g ‚Ä¢ b)).map (algebraMap B (B ‚ß∏ Q)) =
      if g ‚Ä¢ Q = Q then X - C (algebraMap B (B ‚ß∏ Q) (a * b‚ÇÄ)) else X := by
    intro g
    rw [Polynomial.map_sub, map_X, map_C, hb]
    split_ifs
    ¬∑ rfl
    ¬∑ rw [map_zero, map_zero, sub_zero]
  simp only [key‚ÇÄ, Finset.prod_ite, Finset.prod_const] at key
  replace key := congrArg (map (algebraMap (B ‚ß∏ Q) L)) key
  rw [map_map, ‚Üê algebraMap_eq, algebraMap_eq (A ‚ß∏ P) K L,
      ‚Üê map_map, Polynomial.map_mul, Polynomial.map_pow, Polynomial.map_pow, Polynomial.map_sub,
      map_X, map_C] at key
  replace key := fixed_of_fixed1_aux3 key f (Finset.card_ne_zero_of_mem
    (Finset.mem_filter.mpr ‚ü®Finset.mem_univ 1, one_smul G Q‚ü©))
  simp only [map_mul] at key
  obtain ‚ü®a, rfl‚ü© := Algebra.IsInvariant.isInvariant (A := A) a ha1
  rwa [‚Üê algebraMap_apply A B (B ‚ß∏ Q), algebraMap_apply A (A ‚ß∏ P) (B ‚ß∏ Q),
      ‚Üê algebraMap_apply, algebraMap_apply (A ‚ß∏ P) K L, f.commutes, mul_right_inj'] at key
  rwa [‚Üê algebraMap_apply, algebraMap_apply (A ‚ß∏ P) (B ‚ß∏ Q) L,
      ‚Üê algebraMap_apply A (A ‚ß∏ P) (B ‚ß∏ Q), algebraMap_apply A B (B ‚ß∏ Q),
      Ne, algebraMap_eq_zero_iff, Ideal.Quotient.algebraMap_eq, Ideal.Quotient.eq_zero_iff_mem]

variable [IsFractionRing (A ‚ß∏ P) K] [IsFractionRing (B ‚ß∏ Q) L]

/-- If `Q` lies over `P`, then the stabilizer of `Q` acts on `Frac(B/Q)/Frac(A/P)`. -/
noncomputable def IsFractionRing.stabilizerHom : MulAction.stabilizer G Q ‚Üí* (L ‚âÉ‚Çê[K] L) :=
  MonoidHom.comp (IsFractionRing.fieldEquivOfAlgEquivHom K L) (Ideal.Quotient.stabilizerHom Q P G)

/-- This theorem will be made redundant by `IsFractionRing.stabilizerHom_surjective`. -/
private theorem fixed_of_fixed2 (f : L ‚âÉ‚Çê[K] L) (x : L)
    (hx : ‚àÄ g : MulAction.stabilizer G Q, IsFractionRing.stabilizerHom G P Q K L g x = x) :
    f x = x := by
  obtain ‚ü®_‚ü© := nonempty_fintype G
  have : P.IsPrime := Ideal.over_def Q P ‚ñ∏ Ideal.IsPrime.under A Q
  have : Algebra.IsIntegral A B := Algebra.IsInvariant.isIntegral A B G
  obtain ‚ü®x, y, hy, rfl‚ü© := IsFractionRing.div_surjective (A := B ‚ß∏ Q) x
  obtain ‚ü®b, a, ha, h‚ü© := (Algebra.IsAlgebraic.isAlgebraic (R := A ‚ß∏ P) y).exists_smul_eq_mul x hy
  replace ha : algebraMap (A ‚ß∏ P) L a ‚â† 0 := by
    rwa [Ne, algebraMap_apply (A ‚ß∏ P) K L, algebraMap_eq_zero_iff, algebraMap_eq_zero_iff]
  replace hy : algebraMap (B ‚ß∏ Q) L y ‚â† 0 :=
    mt (algebraMap_eq_zero_iff (B ‚ß∏ Q) L).mp (nonZeroDivisors.ne_zero hy)
  replace h : algebraMap (B ‚ß∏ Q) L x / algebraMap (B ‚ß∏ Q) L y =
      algebraMap (B ‚ß∏ Q) L b / algebraMap (A ‚ß∏ P) L a := by
    rw [mul_comm, Algebra.smul_def, mul_comm] at h
    rw [div_eq_div_iff hy ha, ‚Üê map_mul, ‚Üê h, map_mul, ‚Üê algebraMap_apply]
  simp only [h, map_div‚ÇÄ, algebraMap_apply (A ‚ß∏ P) K L, AlgEquiv.commutes] at hx ‚ä¢
  simp only [‚Üê algebraMap_apply, div_left_inj' ha] at hx ‚ä¢
  exact fixed_of_fixed1 G P Q K L f b (fun g ‚Ü¶ IsFractionRing.injective (B ‚ß∏ Q) L
    ((IsFractionRing.fieldEquivOfAlgEquiv_algebraMap K L L
      (Ideal.Quotient.stabilizerHom Q P G g) b).symm.trans (hx g)))

/-- The stabilizer subgroup of `Q` surjects onto `Aut(Frac(B/Q)/Frac(A/P))`. -/
theorem IsFractionRing.stabilizerHom_surjective :
    Function.Surjective (stabilizerHom G P Q K L) := by
  let _ := MulSemiringAction.compHom L (stabilizerHom G P Q K L)
  intro f
  obtain ‚ü®g, hg‚ü© := FixedPoints.toAlgAut_surjective (MulAction.stabilizer G Q) L
    (AlgEquiv.ofRingEquiv (f := f) (fun x ‚Ü¶ fixed_of_fixed2 G P Q K L f x x.2))
  exact ‚ü®g, by rwa [AlgEquiv.ext_iff] at hg ‚ä¢‚ü©

/-- The stabilizer subgroup of `Q` surjects onto `Aut((B/Q)/(A/P))`. -/
theorem Ideal.Quotient.stabilizerHom_surjective :
    Function.Surjective (Ideal.Quotient.stabilizerHom Q P G) := by
  have : P.IsPrime := Ideal.over_def Q P ‚ñ∏ Ideal.IsPrime.under A Q
  let _ := FractionRing.liftAlgebra (A ‚ß∏ P) (FractionRing (B ‚ß∏ Q))
  have key := IsFractionRing.stabilizerHom_surjective G P Q
    (FractionRing (A ‚ß∏ P)) (FractionRing (B ‚ß∏ Q))
  rw [IsFractionRing.stabilizerHom, MonoidHom.coe_comp] at key
  exact key.of_comp_left (IsFractionRing.fieldEquivOfAlgEquivHom_injective (A ‚ß∏ P) (B ‚ß∏ Q)
    (FractionRing (A ‚ß∏ P)) (FractionRing (B ‚ß∏ Q)))

end surjectivity

section normal

variable {A B k : Type*} [CommRing A] [CommRing B] [Algebra A B]
  (G : Type*) [Finite G] [Group G] [MulSemiringAction G B] [Algebra.IsInvariant A B G]
  (P : Ideal A) (Q : Ideal B) [Q.LiesOver P]
  [CommRing k] [Algebra (A ‚ß∏ P) k] [Algebra (B ‚ß∏ Q) k] [IsScalarTower (A ‚ß∏ P) (B ‚ß∏ Q) k]
  [IsDomain k] [FaithfulSMul (B ‚ß∏ Q) k]

include G in
/--
For any domain `k` containing `B ‚ß∏ Q`,
any endomorphism of `k` can be restricted to an endomorphism of `B ‚ß∏ Q`.

This is basically the fact that `L/K` normal implies `Œ∫(Q)/Œ∫(P)` normal in the Galois setting.
-/
lemma Ideal.Quotient.exists_algHom_fixedPoint_quotient_under
    (œÉ : k ‚Üí‚Çê[A ‚ß∏ P] k) :
    ‚àÉ œÑ : (B ‚ß∏ Q) ‚Üí‚Çê[A ‚ß∏ P] B ‚ß∏ Q, ‚àÄ x : B ‚ß∏ Q,
      algebraMap _ _ (œÑ x) = œÉ (algebraMap (B ‚ß∏ Q) k x) := by
  let f : (B ‚ß∏ Q) ‚Üí‚Çê[A ‚ß∏ P] k := IsScalarTower.toAlgHom _ _ _
  have hf : Function.Injective f := FaithfulSMul.algebraMap_injective _ _
  suffices (œÉ.comp f).range ‚â§ f.range by
    let e := (AlgEquiv.ofInjective f hf)
    exact ‚ü®(e.symm.toAlgHom.comp (Subalgebra.inclusion this)).comp (œÉ.comp f).rangeRestrict,
      fun x ‚Ü¶ congr_arg Subtype.val (e.apply_symm_apply ‚ü®_, _‚ü©)‚ü©
  rintro _ ‚ü®x, rfl‚ü©
  obtain ‚ü®x, rfl‚ü© := Ideal.Quotient.mk_surjective x
  cases nonempty_fintype G
  algebraize [(algebraMap (A ‚ß∏ P) k).comp (algebraMap A (A ‚ß∏ P)),
    (algebraMap (B ‚ß∏ Q) k).comp (algebraMap B (B ‚ß∏ Q))]
  haveI : IsScalarTower A (B ‚ß∏ Q) k := .of_algebraMap_eq fun x ‚Ü¶
    (IsScalarTower.algebraMap_apply (A ‚ß∏ P) (B ‚ß∏ Q) k (mk P x))
  haveI : IsScalarTower A B k := .of_algebraMap_eq fun x ‚Ü¶
    (IsScalarTower.algebraMap_apply (A ‚ß∏ P) (B ‚ß∏ Q) k (mk P x))
  obtain ‚ü®P, hp‚ü© := Algebra.IsInvariant.charpoly_mem_lifts A B G x
  have : Polynomial.aeval x P = 0 := by
    rw [Polynomial.aeval_def, ‚Üê Polynomial.eval_map,
      ‚Üê Polynomial.coe_mapRingHom (R := A), hp, MulSemiringAction.eval_charpoly]
  have : Polynomial.aeval (œÉ (algebraMap (B ‚ß∏ Q) k (mk _ x))) P = 0 := by
    refine (DFunLike.congr_fun (Polynomial.aeval_algHom ((œÉ.restrictScalars A).comp
      (IsScalarTower.toAlgHom A (B ‚ß∏ Q) k)) _) P).trans ?_
    rw [AlgHom.comp_apply, ‚Üê algebraMap_eq, Polynomial.aeval_algebraMap_apply, this,
      map_zero, map_zero]
  rw [‚Üê Polynomial.aeval_map_algebraMap B, ‚Üê Polynomial.coe_mapRingHom, hp] at this
  obtain ‚ü®œÑ, hœÑ‚ü© : ‚àÉ œÑ : G, œÉ (algebraMap _ _ x) = algebraMap _ _ (œÑ ‚Ä¢ x) := by
    simpa [MulSemiringAction.charpoly, sub_eq_zero, Finset.prod_eq_zero_iff] using this
  exact ‚ü®Ideal.Quotient.mk _ (œÑ ‚Ä¢ x), hœÑ.symm‚ü©

include G in
/--
For any domain `k` containing `B ‚ß∏ Q`,
any endomorphism of `k` can be restricted to an endomorphism of `B ‚ß∏ Q`.
-/
lemma Ideal.Quotient.exists_algEquiv_fixedPoint_quotient_under
    (œÉ : k ‚âÉ‚Çê[A ‚ß∏ P] k) :
    ‚àÉ œÑ : (B ‚ß∏ Q) ‚âÉ‚Çê[A ‚ß∏ P] B ‚ß∏ Q, ‚àÄ x : B ‚ß∏ Q,
      algebraMap _ _ (œÑ x) = œÉ (algebraMap (B ‚ß∏ Q) k x) := by
  let f : (B ‚ß∏ Q) ‚Üí‚Çê[A ‚ß∏ P] k := IsScalarTower.toAlgHom _ _ _
  have hf : Function.Injective f := FaithfulSMul.algebraMap_injective _ _
  obtain ‚ü®œÑ‚ÇÅ, h‚ÇÅ‚ü© := Ideal.Quotient.exists_algHom_fixedPoint_quotient_under G P Q œÉ.toAlgHom
  obtain ‚ü®œÑ‚ÇÇ, h‚ÇÇ‚ü© := Ideal.Quotient.exists_algHom_fixedPoint_quotient_under G P Q œÉ.symm.toAlgHom
  refine ‚ü®{ __ := œÑ‚ÇÅ, invFun := œÑ‚ÇÇ, left_inv := ?_, right_inv := ?_ }, h‚ÇÅ‚ü©
  ¬∑ intro x
    obtain ‚ü®x, rfl‚ü© := Ideal.Quotient.mk_surjective x
    obtain ‚ü®y, e‚ü© := Ideal.Quotient.mk_surjective (œÑ‚ÇÅ (Ideal.Quotient.mk Q x))
    apply hf
    dsimp [f] at h‚ÇÅ h‚ÇÇ ‚ä¢
    refine .trans ?_ (œÉ.symm_apply_apply _)
    rw [‚Üê h‚ÇÅ, ‚Üê e, h‚ÇÇ]
  ¬∑ intro x
    obtain ‚ü®x, rfl‚ü© := Ideal.Quotient.mk_surjective x
    obtain ‚ü®y, e‚ü© := Ideal.Quotient.mk_surjective (œÑ‚ÇÇ (Ideal.Quotient.mk Q x))
    apply hf
    dsimp [f] at h‚ÇÅ h‚ÇÇ ‚ä¢
    refine .trans ?_ (œÉ.apply_symm_apply _)
    rw [‚Üê h‚ÇÇ, ‚Üê e, h‚ÇÅ]

end normal
