/-
Copyright (c) 2025 Blake Farman. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Blake Farman
-/
module

public import Mathlib.RingTheory.IdealFilter.Basic
public import Mathlib.Topology.Algebra.LinearTopology
public import Mathlib.Topology.Algebra.FilterBasis

/-!
# Topologies associated to ideal filters

This file constructs topological structures on a ring from an `IdealFilter`.

## Main definitions

* `IdealFilter.addGroupFilterBasis`: the `AddGroupFilterBasis` with sets the ideals of `F`.
* `IdealFilter.addGroupTopology`: the corresponding topology on `A`.
* `IdealFilter.ringFilterBasis`: under `F.IsUniform`, the `RingFilterBasis` with sets the ideals of
`F`.
* `IdealFilter.ringTopology`: under `F.IsUniform`, the corresponding ring topology on `A`.

## Main statements

* `isUniform_iff_exists_ringFilterBasis`: An `IdealFilter` on a ring `A` is uniform if and only if
its ideals form a `RingFilterBasis` for `A`.
* `IdealFilter.addGroupTopology_mem_nhds_iff`: a neighbourhood characterization for
`addGroupTopology`.
* `IdealFilter.ringTopology_mem_nhds_iff`: a neighbourhood characterization for
`ringTopology`.
* `IdealFilter.isLinearTopology_ringTopology`: the topology `ringTopology` is linear.

## References

* [nLab: Uniform filter](<https://ncatlab.org/nlab/show/uniform+filter>)
* [nLab: Gabriel filter](<https://ncatlab.org/nlab/show/Gabriel+filter>)
* [nLab: Gabriel composition](<https://ncatlab.org/nlab/show/Gabriel+composition+of+filters>)

## Tags

ring theory, ideal, filter, linear topology
-/

@[expose] public section

open scoped Pointwise Topology

namespace IdealFilter

variable {A : Type*} [Ring A]

variable (F : IdealFilter A)

/-- The additive-group filter basis whose sets are the ideals belonging to the ideal filter `F`. -/
def addGroupFilterBasis : AddGroupFilterBasis A where
  sets := {(I : Set A) | I ‚àà F}
  nonempty := by
    obtain ‚ü®I, hI‚ü© := F.nonempty
    exact ‚ü®I, ‚ü®I, hI, rfl‚ü©‚ü©
  inter_sets := by
    rintro s t ‚ü®I, hI, rfl‚ü© ‚ü®J, hJ, rfl‚ü©
    exact ‚ü®I ‚äì J, ‚ü®I ‚äì J, Order.PFilter.inf_mem hI hJ, rfl‚ü©, fun _ h ‚Ü¶ h‚ü©
  zero' := by
    rintro s ‚ü®I, hI, rfl‚ü©
    exact zero_mem I
  add' := by
    rintro s ‚ü®I, hI, rfl‚ü©
    exact ‚ü®I, ‚ü®I, hI, rfl‚ü©, Set.add_subset_iff.mpr (fun _ hx _ hy ‚Ü¶ add_mem hx hy)‚ü©
  neg' := by
    rintro s ‚ü®I, hI, rfl‚ü©
    exact ‚ü®I, ‚ü®I, hI, rfl‚ü©, by simp‚ü©
  conj' := by
    rintro x‚ÇÄ s ‚ü®I, hI, rfl‚ü©
    exact ‚ü®I, ‚ü®I, hI, rfl‚ü©, by simp‚ü©

/-- Under `F.IsUniform`, the ring filter basis obtained from `addGroupFilterBasis`. -/
def ringFilterBasis (hUniform : F.IsUniform) : RingFilterBasis A where
  sets := F.addGroupFilterBasis.sets
  nonempty := F.addGroupFilterBasis.nonempty
  inter_sets := F.addGroupFilterBasis.inter_sets
  zero' := F.addGroupFilterBasis.zero'
  add' := F.addGroupFilterBasis.add'
  neg' := F.addGroupFilterBasis.neg'
  conj' := F.addGroupFilterBasis.conj'
  mul' := by
    rintro U ‚ü®I, hI, rfl‚ü©
    exact ‚ü®I, ‚ü®I, hI, rfl‚ü©, Set.mul_subset_iff.mpr fun _ h‚ÇÅ _ h‚ÇÇ ‚Ü¶ mul_mem h‚ÇÅ h‚ÇÇ‚ü©
  mul_left' := by
    rintro x‚ÇÄ U ‚ü®I, hI, rfl‚ü©
    exact ‚ü®I, ‚ü®I, hI, rfl‚ü©, fun a ha ‚Ü¶ Ideal.mul_mem_left I x‚ÇÄ ha‚ü©
  mul_right' := by
    rintro x‚ÇÄ U ‚ü®I, hI, rfl‚ü©
    exact ‚ü®I.colon {x‚ÇÄ}, ‚ü®I.colon {x‚ÇÄ}, IsUniform.colon_mem hUniform hI x‚ÇÄ, rfl‚ü©,
      fun a ha ‚Ü¶ Set.mem_preimage.mpr (Submodule.mem_colon_singleton.mp ha)‚ü©

/-- An `IdealFilter` on a ring `A` is uniform if and only if its ideals form a `RingFilterBasis`
for `A`. -/
theorem isUniform_iff_exists_ringFilterBasis :
    F.IsUniform ‚Üî ‚àÉ B : RingFilterBasis A, B.sets = {s : Set A | ‚àÉ I ‚àà F, s = (I : Set A)} := by
  constructor
  ¬∑ intro hF
    refine ‚ü®F.ringFilterBasis hF, ?_‚ü©
    ext s
    constructor <;>
    ¬∑ intro hs
      rcases hs with ‚ü®I, hI, rfl‚ü©
      exact ‚ü®I, hI, rfl‚ü©
  ¬∑ rintro ‚ü®B, hB‚ü©
    exact {
      colon_mem := by
        intro I hI a
        have hIB : (I : Set A) ‚àà B.sets := by simpa [hB]
        rcases RingFilterBasis.mul_right B a hIB with ‚ü®V, hbasis : V ‚àà B.sets, hsub‚ü©
        rcases (by simpa [hB] using hbasis) with ‚ü®J, hJ, rfl‚ü©
        exact Order.PFilter.mem_of_le (fun x hx ‚Ü¶ Submodule.mem_colon_singleton.mpr (hsub hx)) hJ
    }

/-- The topology on `A` induced by `addGroupFilterBasis`. -/
def addGroupTopology : TopologicalSpace A := (addGroupFilterBasis F).topology

/-- The topology `F.addGroupTopology` endows `A` with the structure of a topological additive
group. -/
theorem isTopologicalAddGroup :
    letI : TopologicalSpace A := F.addGroupTopology
    IsTopologicalAddGroup A :=
  F.addGroupFilterBasis.isTopologicalAddGroup

/-- Under `F.IsUniform`, the topology on `A` induced by `ringFilterBasis`. -/
def ringTopology (hUniform : F.IsUniform) : TopologicalSpace A :=
  (ringFilterBasis F hUniform).topology

/-- Under `F.IsUniform`, the topology `F.ringTopology` endows `A` with the structure of a
topological ring. -/
theorem isTopologicalRing (hUniform : F.IsUniform) :
    letI : TopologicalSpace A := F.ringTopology hUniform
    IsTopologicalRing A :=
  (F.ringFilterBasis hUniform).isTopologicalRing

/-- In `F.addGroupTopology`, a set is a neighbourhood of `a` iff it contains a
left-additive coset of some ideal `I ‚àà F`. -/
lemma addGroupTopology_mem_nhds_iff (a : A) (s : Set A) :
    letI : TopologicalSpace A := F.addGroupTopology
    s ‚àà ùìù a ‚Üî ‚àÉ I ‚àà F, a +·µ• (I : Set A) ‚äÜ s := by
  constructor
  ¬∑ intro hs
    rcases ((F.addGroupFilterBasis).nhds_hasBasis a).mem_iff.1 hs with ‚ü®t, ht, hts‚ü©
    rcases ht with ‚ü®I, hI, rfl‚ü©
    exact ‚ü®I, hI, hts‚ü©
  ¬∑ rintro ‚ü®I, hI, hIs‚ü©
    refine ((F.addGroupFilterBasis).nhds_hasBasis a).mem_iff.2 ?_
    exact ‚ü®I, ‚ü®I, hI, rfl‚ü©, hIs‚ü©

/-- In `F.addGroupTopology`, `s` is a neighbourhood of `0` iff it contains an ideal
belonging to `F`. -/
lemma addGroupTopology_mem_nhds_zero_iff (s : Set A) :
    letI : TopologicalSpace A := F.addGroupTopology
    s ‚àà ùìù 0 ‚Üî ‚àÉ I ‚àà F, (I : Set A) ‚äÜ s := by
  simpa [zero_vadd] using F.addGroupTopology_mem_nhds_iff (a := (0 : A)) (s := s)

/-- In `F.ringTopology`, `s` is a neighbourhood of `a` iff it contains a
left-additive coset of some ideal `I ‚àà F`. -/
lemma ringTopology_mem_nhds_iff (hUniform : F.IsUniform) (a : A) (s : Set A) :
    letI : TopologicalSpace A := F.ringTopology hUniform
    s ‚àà ùìù a ‚Üî ‚àÉ I ‚àà F, a +·µ• (I : Set A) ‚äÜ s := by
  constructor
  ¬∑ intro hs
    rcases ((F.ringFilterBasis hUniform).nhds_hasBasis a).mem_iff.mp hs with ‚ü®t, ht, hts‚ü©
    rcases ht with ‚ü®I, hI, rfl‚ü©
    exact ‚ü®I, hI, hts‚ü©
  ¬∑ rintro ‚ü®I, hI, hIs‚ü©
    exact ((F.ringFilterBasis hUniform).nhds_hasBasis a).mem_iff.mpr ‚ü®I, ‚ü®I, hI, rfl‚ü©, hIs‚ü©

/-- In `F.ringTopology`, `s` is a neighbourhood of `0` iff it contains an ideal belonging
to `F`. -/
lemma ringTopology_mem_nhds_zero_iff (hUniform : F.IsUniform) (s : Set A) :
    letI : TopologicalSpace A := F.ringTopology hUniform
    s ‚àà ùìù 0 ‚Üî ‚àÉ I ‚àà F, (I : Set A) ‚äÜ s := by
  simpa [zero_vadd] using F.ringTopology_mem_nhds_iff (hUniform := hUniform) (a := (0 : A)) (s := s)

/-- Under `F.IsUniform`, the topology `ringTopology` is linear in the sense that `ùìù 0` has a
basis of ideals. -/
theorem isLinearTopology_ringTopology (hUniform : F.IsUniform) :
    letI : TopologicalSpace A := F.ringTopology hUniform
    IsLinearTopology A A := by
  letI : TopologicalSpace A := F.ringTopology hUniform
  exact IsLinearTopology.mk_of_hasBasis' (R := A) (M := A)
    (Œπ := Ideal A) (S := Ideal A)
    (p := fun I : Ideal A ‚Ü¶ I ‚àà F) (s := fun I : Ideal A ‚Ü¶ I)
    ‚ü®fun t ‚Ü¶ ringTopology_mem_nhds_zero_iff F hUniform t‚ü©
    (fun I a m hm ‚Ü¶ Submodule.smul_mem I a hm)

end IdealFilter
