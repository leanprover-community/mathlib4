/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Jujian Zhang
-/
import Mathlib.RingTheory.Noetherian
import Mathlib.Algebra.DirectSum.Module
import Mathlib.Algebra.DirectSum.Finsupp
import Mathlib.Algebra.Module.Projective
import Mathlib.Algebra.Module.Injective
import Mathlib.Algebra.Module.CharacterModule
import Mathlib.LinearAlgebra.DirectSum.TensorProduct
import Mathlib.LinearAlgebra.FreeModule.Basic
import Mathlib.Algebra.Module.Projective

#align_import ring_theory.flat from "leanprover-community/mathlib"@"62c0a4ef1441edb463095ea02a06e87f3dfe135c"

/-!
# Flat modules

A module `M` over a commutative ring `R` is *flat*
if for all finitely generated ideals `I` of `R`,
the canonical map `I âŠ— M â†’â‚— M` is injective.

This is equivalent to the claim that for all injective `R`-linear maps `f : Mâ‚ â†’ Mâ‚‚`
the induced map `Mâ‚ âŠ— M â†’ Mâ‚‚ âŠ— M` is injective.
See <https://stacks.math.columbia.edu/tag/00HD>.

## Main declaration

* `Module.Flat`: the predicate asserting that an `R`-module `M` is flat.

## Main theorems

* `Module.Flat.of_retract`: retracts of flat modules are flat
* `Module.Flat.of_linearEquiv`: modules linearly equivalent to a flat modules are flat
* `Module.Flat.directSum`: arbitrary direct sums of flat modules are flat
* `Module.Flat.of_free`: free modules are flat
* `Module.Flat.of_projective`: projective modules are flat
* `Module.Flat.iff_rTensor_preserves_injectiveness`: a module is flat iff tensoring preserves
  injectiveness.

## TODO

* Show that flatness is stable under base change (aka extension of scalars)
  For base change, it will be very useful to have a "characteristic predicate"
  instead of relying on the construction `A âŠ— B`.
  Indeed, such a predicate should allow us to treat both
  `A[X]` and `A âŠ— R[X]` as the base change of `R[X]` to `A`.
  (Similar examples exist with `Fin n â†’ R`, `R Ã— R`, `â„¤[i] âŠ— â„`, etc...)
* Generalize flatness to noncommutative rings.

-/


universe u v w

namespace Module

open Function (Surjective)

open LinearMap Submodule TensorProduct

variable (R : Type u) (M : Type v) [CommRing R] [AddCommGroup M] [Module R M]

/-- An `R`-module `M` is flat if for all finitely generated ideals `I` of `R`,
the canonical map `I âŠ— M â†’â‚— M` is injective. -/
class Flat : Prop where
  out : âˆ€ â¦ƒI : Ideal Râ¦„ (_ : I.FG),
    Function.Injective (TensorProduct.lift ((lsmul R M).comp I.subtype))
#align module.flat Module.Flat

namespace Flat

instance self (R : Type u) [CommRing R] : Flat R R :=
  âŸ¨by
    intro I _
    rw [â† Equiv.injective_comp (TensorProduct.rid R I).symm.toEquiv]
    convert Subtype.coe_injective using 1
    ext x
    simp only [Function.comp_apply, LinearEquiv.coe_toEquiv, rid_symm_apply, comp_apply, mul_one,
      lift.tmul, Submodule.subtype_apply, Algebra.id.smul_eq_mul, lsmul_apply]âŸ©
#align module.flat.self Module.Flat.self

variable (M : Type v) [AddCommGroup M] [Module R M]

/-- An `R`-module `M` is flat iff for all finitely generated ideals `I` of `R`, the
tensor product of the inclusion `I â†’ R` and the identity `M â†’ M` is injective. See
`iff_rTensor_injective'` to extend to all ideals `I`. --/
lemma iff_rTensor_injective :
    Flat R M â†” âˆ€ â¦ƒI : Ideal Râ¦„ (_ : I.FG), Function.Injective (rTensor M I.subtype) := by
  have aux : âˆ€ I : Ideal R, (TensorProduct.lid R M).comp (rTensor M I.subtype) =
      TensorProduct.lift ((lsmul R M).comp I.subtype) := by
    intro I; apply TensorProduct.ext'; intro x y; simp
  constructor
  Â· intro F I hI
    erw [â† Equiv.comp_injective _ (TensorProduct.lid R M).toEquiv]
    have hâ‚ := F.out hI
    rw [â† aux] at hâ‚
    exact hâ‚
  Â· intro hâ‚
    constructor
    intro I hI
    rw [â† aux]
    simp [hâ‚ hI]

/-- An `R`-module `M` is flat iff for all ideals `I` of `R`, the tensor product of the
inclusion `I â†’ R` and the identity `M â†’ M` is injective. See `iff_rTensor_injective` to
restrict to finitely generated ideals `I`. --/
theorem iff_rTensor_injective' :
    Flat R M â†” âˆ€ I : Ideal R, Function.Injective (rTensor M I.subtype) := by
  rewrite [Flat.iff_rTensor_injective]
  refine âŸ¨fun h I => ?_, fun h I _ => h IâŸ©
  letI : AddCommGroup (I âŠ—[R] M) := inferInstance -- Type class reminder
  rewrite [injective_iff_map_eq_zero]
  intro x hxâ‚€
  obtain âŸ¨J, hfg, hle, y, rflâŸ© := Submodule.exists_fg_le_eq_rTensor_inclusion x
  rewrite [â† rTensor_comp_apply] at hxâ‚€
  rw [(injective_iff_map_eq_zero _).mp (h hfg) y hxâ‚€, LinearMap.map_zero]

/-- Given a linear map `f : N â†’ P`, `f âŠ— M` is injective if and only if `M âŠ— f` is injective. -/
lemma lTensor_inj_iff_rTensor_inj {N P : Type*} [AddCommGroup N] [AddCommGroup P] [Module R N]
    [Module R P] (f : N â†’â‚—[R] P) :
    Function.Injective (lTensor M f) â†” Function.Injective (rTensor M f) := by
  haveI h1 : rTensor M f âˆ˜â‚— TensorProduct.comm R M N =
    TensorProduct.comm R M P âˆ˜â‚— lTensor M f := ext rfl
  haveI h2 : â‡‘(TensorProduct.comm R M P) âˆ˜ â‡‘(lTensor M f) =
    (TensorProduct.comm R M P) âˆ˜â‚— (lTensor M f) := rfl
  simp only [â† EquivLike.injective_comp (TensorProduct.comm R M N),
    â† EquivLike.comp_injective _ (TensorProduct.comm R M P), h2, â† h1]
  trivial

/-- The `lTensor`-variant of `iff_rTensor_injective`. .-/
theorem iff_lTensor_injective :
    Module.Flat R M â†” âˆ€ â¦ƒI : Ideal Râ¦„ (_ : I.FG), Function.Injective (lTensor M I.subtype) := by
  simp only [lTensor_inj_iff_rTensor_inj]
  exact Module.Flat.iff_rTensor_injective R M

/-- The `lTensor`-variant of `iff_rTensor_injective'`. .-/
theorem iff_lTensor_injective' :
    Module.Flat R M â†” âˆ€ (I : Ideal R), Function.Injective (lTensor M I.subtype) := by
  simp only [lTensor_inj_iff_rTensor_inj]
  exact Module.Flat.iff_rTensor_injective' R M

variable (N : Type w) [AddCommGroup N] [Module R N]

/-- A retract of a flat `R`-module is flat. -/
lemma of_retract [f : Flat R M] (i : N â†’â‚—[R] M) (r : M â†’â‚—[R] N) (h : r.comp i = LinearMap.id) :
    Flat R N := by
  rw [iff_rTensor_injective] at *
  intro I hI
  have hâ‚ : Function.Injective (lTensor R i) := by
    apply Function.RightInverse.injective (g := (lTensor R r))
    intro x
    rw [â† LinearMap.comp_apply, â† lTensor_comp, h]
    simp
  rw [â† Function.Injective.of_comp_iff hâ‚ (rTensor N I.subtype), â† LinearMap.coe_comp]
  rw [LinearMap.lTensor_comp_rTensor, â† LinearMap.rTensor_comp_lTensor]
  rw [LinearMap.coe_comp, Function.Injective.of_comp_iff (f hI)]
  apply Function.RightInverse.injective (g := lTensor _ r)
  intro x
  rw [â† LinearMap.comp_apply, â† lTensor_comp, h]
  simp

/-- A `R`-module linearly equivalent to a flat `R`-module is flat. -/
lemma of_linearEquiv [f : Flat R M] (e : N â‰ƒâ‚—[R] M) : Flat R N := by
  have h : e.symm.toLinearMap.comp e.toLinearMap = LinearMap.id := by simp
  exact of_retract _ _ _ e.toLinearMap e.symm.toLinearMap h

end Flat

namespace Flat

open DirectSum LinearMap Submodule

variable (R : Type u) [CommRing R]

/-- A direct sum of flat `R`-modules is flat. -/
instance directSum (Î¹ : Type v) (M : Î¹ â†’ Type w) [(i : Î¹) â†’ AddCommGroup (M i)]
    [(i : Î¹) â†’ Module R (M i)] [F : (i : Î¹) â†’ (Flat R (M i))] : Flat R (â¨ i, M i) := by
  haveI := Classical.decEq Î¹
  rw [iff_rTensor_injective]
  intro I hI
  -- This instance was added during PR #10828,
  -- see https://leanprover.zulipchat.com/#narrow/stream/144837-PR-reviews/topic/.2310828.20-.20generalizing.20CommRing.20to.20CommSemiring.20etc.2E/near/422684923
  letI : âˆ€ i, AddCommGroup (I âŠ—[R] M i) := inferInstance
  rw [â† Equiv.comp_injective _ (TensorProduct.lid R (â¨ i, M i)).toEquiv]
  set Î·â‚ := TensorProduct.lid R (â¨ i, M i)
  set Î· := (fun i â†¦ TensorProduct.lid R (M i))
  set Ï† := (fun i â†¦ rTensor (M i) I.subtype)
  set Ï€ := (fun i â†¦ component R Î¹ (fun j â†¦ M j) i)
  set Ïˆ := (TensorProduct.directSumRight R {x // x âˆˆ I} (fun i â†¦ M i)).symm.toLinearMap with psi_def
  set Ï := rTensor (â¨ i, M i) I.subtype
  set Ï„ := (fun i â†¦ component R Î¹ (fun j â†¦ ({x // x âˆˆ I} âŠ—[R] (M j))) i)
  rw [â† Equiv.injective_comp (TensorProduct.directSumRight _ _ _).symm.toEquiv]
  rw [LinearEquiv.coe_toEquiv, â† LinearEquiv.coe_coe, â† LinearMap.coe_comp]
  rw [LinearEquiv.coe_toEquiv, â† LinearEquiv.coe_coe, â† LinearMap.coe_comp]
  rw [â† psi_def, injective_iff_map_eq_zero ((Î·â‚.comp Ï).comp Ïˆ)]
  have hâ‚ : âˆ€ (i : Î¹), (Ï€ i).comp ((Î·â‚.comp Ï).comp Ïˆ) = (Î· i).comp ((Ï† i).comp (Ï„ i)) := by
    intro i
    apply DirectSum.linearMap_ext
    intro j
    apply TensorProduct.ext'
    intro a m
    simp only [Ï, Ïˆ, Ï†, Î·, Î·â‚, coe_comp, LinearEquiv.coe_coe, Function.comp_apply,
      directSumRight_symm_lof_tmul, rTensor_tmul, Submodule.coeSubtype, lid_tmul, map_smul]
    rw [DirectSum.component.of, DirectSum.component.of]
    by_cases hâ‚‚ : j = i
    Â· subst j; simp
    Â· simp [hâ‚‚]
  intro a ha; rw [DirectSum.ext_iff R]; intro i
  have f := LinearMap.congr_arg (f := (Ï€ i)) ha
  erw [LinearMap.congr_fun (hâ‚ i) a] at f
  rw [(map_zero (Ï€ i) : (Ï€ i) 0 = (0 : M i))] at f
  have hâ‚‚ := (F i)
  rw [iff_rTensor_injective] at hâ‚‚
  have hâ‚ƒ := hâ‚‚ hI
  simp only [coe_comp, LinearEquiv.coe_coe, Function.comp_apply, AddEquivClass.map_eq_zero_iff,
    hâ‚ƒ, LinearMap.map_eq_zero_iff] at f
  simp [f]

open Classical in
/-- Free `R`-modules over discrete types are flat. -/
instance finsupp (Î¹ : Type v) : Flat R (Î¹ â†’â‚€ R) :=
  of_linearEquiv R _ _ (finsuppLEquivDirectSum R R Î¹)

variable (M : Type v) [AddCommGroup M] [Module R M]

instance of_free [Free R M] : Flat R M := of_linearEquiv R _ _ (Free.repr R M)

/-- A projective module with a discrete type of generator is flat -/
lemma of_projective_surjective (Î¹ : Type w) [Projective R M] (p : (Î¹ â†’â‚€ R) â†’â‚—[R] M)
    (hp : Surjective p) : Flat R M := by
  have h := Module.projective_lifting_property p (LinearMap.id) hp
  cases h with
    | _ e he => exact of_retract R _ _ _ _ he

instance of_projective [h : Projective R M] : Flat R M := by
  rw [Module.projective_def'] at h
  cases h with
    | _ e he => exact of_retract R _ _ _ _ he

open BigOperators in
/--
Define character module of `M` to be `M â†’ â„š â§¸ â„¤`
If the character module of `M` is an injective module then `L âŠ— ğŸ™ M` is injective for any linear map
`L`.
-/
lemma rTensor_preserves_injective_linearMap_of_injective_characterModule
    (h : Module.Injective R <| CharacterModule M) :
    âˆ€ â¦ƒN N' : Type vâ¦„ [AddCommGroup N] [AddCommGroup N'] [Module R N] [Module R N']
      (L : N â†’â‚—[R] N'), Function.Injective L â†’ Function.Injective (L.rTensor M) := by
  intros A B _ _ _ _ L hL
  rw [â† LinearMap.ker_eq_bot, eq_bot_iff]
  rintro z (hz : _ = 0)
  -- Consider an injective linear map `L : A â†’ B`, we want to prove that `(L âŠ— ğŸ™ M) z = 0`
  -- implies `z = 0`
  show z = 0
  by_contra rid
  -- Let's prove by contradication
  -- If `z â‰  0`, then there would be some character `g âˆˆ (A âŠ— M)â‹†` such that `g z â‰  0`
  obtain âŸ¨g, hgâŸ© := CharacterModule.exists_character_apply_ne_zero_of_ne_zero (a := z) rid

  -- Then we can define a linear map `f : A â†’ Mâ‹†` by `f a m = g (a âŠ— m)`.
  let f : A â†’â‚—[R] (CharacterModule M) := CharacterModule.homEquiv.symm g
  -- Since `Mâ‹†` is an injective module, we can factor `f` to `f' âˆ˜ L` where `f' : B â†’ Mâ‹†`.
  obtain âŸ¨f', hf'âŸ© := h.out L hL f
  -- Since `B â†’ Mâ‹†`  is naturally isomorphic to `(B âŠ— M)â‹†`, we get a character `g' : (B âŠ— M)â‹†`
  let g' : (CharacterModule <| B âŠ—[R] M) :=
    CharacterModule.homEquiv f'

  have mem : z âˆˆ (âŠ¤ : Submodule R _) := âŸ¨âŸ©
  rw [â† TensorProduct.span_tmul_eq_top, mem_span_set] at mem
  obtain âŸ¨c, hc, (eq1 : âˆ‘ i in c.support, _ â€¢ _ = z)âŸ© := mem
  choose a m H using hc
  -- Let's write `z âˆˆ A âŠ— M` as `âˆ‘ aáµ¢ âŠ— máµ¢`
  replace eq1 : âˆ‘ i in c.support.attach, (c i â€¢ a i.2) âŠ—â‚œ (m i.2) = z := by
    conv_rhs => rw [â† eq1, â† Finset.sum_attach]
    refine Finset.sum_congr rfl fun i _ â†¦ ?_
    rw [â† smul_tmul']
    exact congr(c i â€¢ $(H i.2))
  subst eq1
  -- Then `g'(âˆ‘ L aáµ¢ âŠ— máµ¢)` is zero because `(L âŠ— ğŸ™ M) z = 0 = âˆ‘ L aáµ¢ âŠ— máµ¢`
  have EQ : g' (âˆ‘ i in c.support.attach, L (c i â€¢ a i.2) âŠ—â‚œ m i.2) = 0 := by
    simp only [map_sum, rTensor_tmul] at hz
    rw [hz, map_zero]
  -- Now, we aim to show that `g z = 0` and obtain the desired contradiction.
  refine hg ?_
  rw [map_sum] at EQ âŠ¢
  -- Since `âˆ‘ g'(L aáµ¢ âŠ— máµ¢) = 0` and `g'` is obtained by `f' : B â†’ Mâ‹†`,
  -- we now know `âˆ‘ f'(L aáµ¢)(máµ¢) = 0` so that `âˆ‘ f(aáµ¢)(máµ¢) = 0`
  -- But by definition of `f`, this is saying `âˆ‘ g (aáµ¢ âŠ— máµ¢) = 0`, i.e. `g z = 0`
  convert EQ using 1
  refine Finset.sum_congr rfl fun x _ => ?_
  dsimp [CharacterModule.homEquiv]
  erw [liftAddHom_tmul, L.map_smul, f'.map_smul, hf', CharacterModule.smul_apply, smul_tmul]
  rfl

-- We have established a connection between preserving injectiveness of linear map and character
-- module being an injective module. We use Baer's criterion to investigate this connection further.

/--
`Mâ‹†` is Baer, if `I âŠ— M â†’ M` is injective for every ideal `I`
-/
lemma CharacterModule.baer_of_ideal
    (inj : âˆ€ (I : Ideal R), Function.Injective (TensorProduct.lift ((lsmul R M).comp I.subtype))) :
    Module.Baer R (CharacterModule M) := by
  -- Let `I` be an ideal and `L : I â†’ Mâ‹†`, we want to extend `L` to the entire ring
  rintro I (L : _ â†’â‚—[_] _)
  letI :  AddCommGroup (I âŠ—[R] M) := inferInstance
  -- We know that every linear map `f : A â†’ B` induces `fâ‹† : Bâ‹† â†’ Aâ‹†` and if `f` is injective then
  -- `fâ‹†` is surjective.
  -- Under our assumption `Î¹ : I âŠ— M â†’ M` is injective, so `Î¹â‹† : Mâ‹† â†’ (I âŠ— M)â‹†` is surjective, so
  -- there is a character `F : Mâ‹†` such that `Î¹â‹†F (i âŠ— m) = L i m`
  obtain âŸ¨F, hFâŸ© := CharacterModule.dual_surjective_of_injective _ (inj I) <|
      TensorProduct.liftAddHom
        { toFun := fun i => L i
          map_zero' := by aesop
          map_add' := by aesop } <| by aesop
  -- Since `R âŠ— M â‰ƒ M`, `Mâ‹† â‰ƒ (R âŠ— M)â‹† â‰ƒ Hom(R, Mâ‹†)`, under this equivalence, we can reinterpret
  -- `F` as `F' : R â†’ Mâ‹†`. Indeed `F' i = L i m` by definition
  refine âŸ¨CharacterModule.curry (CharacterModule.congr (TensorProduct.lid R M).symm F), ?_âŸ©
  intros x hx
  ext m
  exact congr($hF (âŸ¨x, hxâŸ© âŠ—â‚œ m))

/--
If `I âŠ— M â†’ M` is injective for every ideal `I`, then `f âŠ— ğŸ™ M` is injective for every injective
linear map `f`.
-/
lemma rTensor_preserves_injective_linearMap_of_ideal
    (inj : âˆ€ (I : Ideal R), Function.Injective (TensorProduct.lift ((lsmul R M).comp I.subtype))) :
    âˆ€ â¦ƒN N' : Type vâ¦„ [AddCommGroup N] [AddCommGroup N'] [Module R N] [Module R N'](L : N â†’â‚—[R] N'),
      Function.Injective L â†’ Function.Injective (L.rTensor M) := by
  apply rTensor_preserves_injective_linearMap_of_injective_characterModule
  apply Module.Baer.injective
  apply CharacterModule.baer_of_ideal
  assumption

/--
If `f âŠ— ğŸ™ M` is injective for every injective linear map `f`, then `M` is flat.
-/
lemma of_rTensor_preserves_injective_linearMap [UnivLE.{u, v}]
    (h : âˆ€ â¦ƒN N' : Type vâ¦„ [AddCommGroup N] [AddCommGroup N'] [Module R N] [Module R N']
      (L : N â†’â‚—[R] N'), Function.Injective L â†’ Function.Injective (L.rTensor M)) :
    Flat R M := by
  rw [Flat.iff_rTensor_injective']
  intro I x y eq1
  let e := TensorProduct.congr (Shrink.linearEquiv I R).symm (LinearEquiv.refl R M)
  apply_fun e using e.injective
  have H := @h (Shrink I) (Shrink R) _ _ _ _
    ((Shrink.linearEquiv R R).symm.toLinearMap âˆ˜â‚— I.subtype âˆ˜â‚— (Shrink.linearEquiv I R).toLinearMap)
    ((Shrink.linearEquiv R R).symm.injective.comp
      (Subtype.val_injective.comp (Shrink.linearEquiv I R).injective))
  refine @H (e x) (e y) ?_
  set L : Shrink I âŠ—[R] M â†’â‚—[R] Shrink R âŠ—[R] M := _
  convert_to L (e x) = L (e y)
  suffices eq2 : L âˆ˜â‚— e.toLinearMap =
    (rTensor M (Shrink.linearEquiv R R).symm.toLinearMap) âˆ˜â‚— rTensor M (Submodule.subtype I) by
    erw [congr($eq2 x), congr($eq2 y), LinearMap.comp_apply, eq1, LinearMap.comp_apply]
  refine TensorProduct.ext <| LinearMap.ext fun i â†¦ LinearMap.ext fun m â†¦ ?_
  simp only [comprâ‚‚_apply, mk_apply, coe_comp, LinearEquiv.coe_coe, Function.comp_apply, congr_tmul,
    Shrink.linearEquiv_symm_apply, LinearEquiv.refl_apply, rTensor_tmul, Submodule.coeSubtype,
    Shrink.linearEquiv_apply]
  congr 1
  erw [Equiv.symm_symm_apply, Equiv.symm_apply_apply, Equiv.symm_symm_apply]

/--
M is flat if and only if `f âŠ— ğŸ™ M` is injective whenever `f` is an injective lienar map.
-/
lemma iff_rTensor_preserves_injective_linearMap [UnivLE.{u, v}] :
    Flat R M â†”
    âˆ€ â¦ƒN N' : Type vâ¦„ [AddCommGroup N] [AddCommGroup N'] [Module R N] [Module R N']
      (L : N â†’â‚—[R] N'), Function.Injective L â†’ Function.Injective (L.rTensor M) := by
  constructor
  Â· intro h
    apply rTensor_preserves_injective_linearMap_of_ideal
    rw [Flat.iff_rTensor_injective'] at h
    intro I x y eq1
    specialize h I
    apply h
    suffices (TensorProduct.lid _ _).symm.toLinearMap âˆ˜â‚—
      (lift (lsmul R M âˆ˜â‚— Submodule.subtype I)) = rTensor M (Submodule.subtype I) by
      rw [â† this, LinearMap.comp_apply, LinearMap.comp_apply, eq1]
    refine TensorProduct.ext <| LinearMap.ext fun _ â†¦ LinearMap.ext fun _ â†¦ ?_
    simp only [comprâ‚‚_apply, mk_apply, coe_comp, LinearEquiv.coe_coe, Function.comp_apply,
      lift.tmul, Submodule.coeSubtype, lsmul_apply, map_smul, lid_symm_apply, rTensor_tmul]
    rw [smul_tmul', smul_eq_mul, mul_one]
  Â· exact of_rTensor_preserves_injective_linearMap R M


end Flat

end Module
