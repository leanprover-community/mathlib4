/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Jujian Zhang
-/
import Mathlib.RingTheory.Noetherian
import Mathlib.Algebra.DirectSum.Module
import Mathlib.Algebra.DirectSum.Finsupp
import Mathlib.Algebra.Module.Projective
import Mathlib.Algebra.Module.Injective
import Mathlib.Algebra.Module.CharacterModule
import Mathlib.LinearAlgebra.DirectSum.TensorProduct
import Mathlib.LinearAlgebra.FreeModule.Basic
import Mathlib.Algebra.Module.Projective
import Mathlib.LinearAlgebra.TensorProduct.RightExactness
import Mathlib.Algebra.Category.ModuleCat.Monoidal.Symmetric
import Mathlib.CategoryTheory.Limits.Constructions.EpiMono
import Mathlib.CategoryTheory.Monoidal.Tor
import Mathlib.Algebra.Homology.HomologySequence

#align_import ring_theory.flat from "leanprover-community/mathlib"@"62c0a4ef1441edb463095ea02a06e87f3dfe135c"

/-!
# Flat modules

A module `M` over a commutative ring `R` is *flat*
if for all finitely generated ideals `I` of `R`,
the canonical map `I ‚äó M ‚Üí‚Çó M` is injective.

This is equivalent to the claim that for all injective `R`-linear maps `f : M‚ÇÅ ‚Üí M‚ÇÇ`
the induced map `M‚ÇÅ ‚äó M ‚Üí M‚ÇÇ ‚äó M` is injective.
See <https://stacks.math.columbia.edu/tag/00HD>.

## Main declaration

* `Module.Flat`: the predicate asserting that an `R`-module `M` is flat.

## Main theorems

* `Module.Flat.of_retract`: retracts of flat modules are flat
* `Module.Flat.of_linearEquiv`: modules linearly equivalent to a flat modules are flat
* `Module.Flat.directSum`: arbitrary direct sums of flat modules are flat
* `Module.Flat.of_free`: free modules are flat
* `Module.Flat.of_projective`: projective modules are flat
* `Module.Flat.preserves_injective_linearMap`: If `M` is a flat module then tensoring with `M`
  preserves injectivity of linear maps. This lemma is fully universally polymorphic in all
  arguments, i.e. `R`, `M` and linear maps `N ‚Üí N'` can all have different universe levels.
* `Module.Flat.iff_rTensor_preserves_injective_linearMap`: a module is flat iff tensoring preserves
  injectivity in the ring's universe (or higher).

## Implementation notes
In `Module.Flat.iff_rTensor_preserves_injective_linearMap`, we require that the universe level of
the ring is lower than or equal to that of the module. This requirement is to make sure ideals of
the ring can be lifted to the universe of the module. It is unclear if this lemma also holds
when the module lives in a lower universe.

## TODO

* Generalize flatness to noncommutative rings.

-/


universe u v w

namespace Module

open Function (Surjective)

open LinearMap Submodule TensorProduct DirectSum

variable (R : Type u) (M : Type v) [CommRing R] [AddCommGroup M] [Module R M]

/-- An `R`-module `M` is flat if for all finitely generated ideals `I` of `R`,
the canonical map `I ‚äó M ‚Üí‚Çó M` is injective. -/
@[mk_iff] class Flat : Prop where
  out : ‚àÄ ‚¶ÉI : Ideal R‚¶Ñ (_ : I.FG),
    Function.Injective (TensorProduct.lift ((lsmul R M).comp I.subtype))
#align module.flat Module.Flat

namespace Flat

instance self (R : Type u) [CommRing R] : Flat R R :=
  ‚ü®by
    intro I _
    rw [‚Üê Equiv.injective_comp (TensorProduct.rid R I).symm.toEquiv]
    convert Subtype.coe_injective using 1
    ext x
    simp only [Function.comp_apply, LinearEquiv.coe_toEquiv, rid_symm_apply, comp_apply, mul_one,
      lift.tmul, Submodule.subtype_apply, Algebra.id.smul_eq_mul, lsmul_apply]‚ü©
#align module.flat.self Module.Flat.self

/-- An `R`-module `M` is flat iff for all finitely generated ideals `I` of `R`, the
tensor product of the inclusion `I ‚Üí R` and the identity `M ‚Üí M` is injective. See
`iff_rTensor_injective'` to extend to all ideals `I`. --/
lemma iff_rTensor_injective :
    Flat R M ‚Üî ‚àÄ ‚¶ÉI : Ideal R‚¶Ñ (_ : I.FG), Function.Injective (rTensor M I.subtype) := by
  simp [flat_iff, ‚Üê lid_comp_rTensor]

/-- An `R`-module `M` is flat iff for all ideals `I` of `R`, the tensor product of the
inclusion `I ‚Üí R` and the identity `M ‚Üí M` is injective. See `iff_rTensor_injective` to
restrict to finitely generated ideals `I`. --/
theorem iff_rTensor_injective' :
    Flat R M ‚Üî ‚àÄ I : Ideal R, Function.Injective (rTensor M I.subtype) := by
  rewrite [Flat.iff_rTensor_injective]
  refine ‚ü®fun h I => ?_, fun h I _ => h I‚ü©
  rewrite [injective_iff_map_eq_zero]
  intro x hx‚ÇÄ
  obtain ‚ü®J, hfg, hle, y, rfl‚ü© := Submodule.exists_fg_le_eq_rTensor_inclusion x
  rewrite [‚Üê rTensor_comp_apply] at hx‚ÇÄ
  rw [(injective_iff_map_eq_zero _).mp (h hfg) y hx‚ÇÄ, LinearMap.map_zero]

@[deprecated]
alias lTensor_inj_iff_rTensor_inj := LinearMap.lTensor_inj_iff_rTensor_inj

/-- The `lTensor`-variant of `iff_rTensor_injective`. . -/
theorem iff_lTensor_injective :
    Module.Flat R M ‚Üî ‚àÄ ‚¶ÉI : Ideal R‚¶Ñ (_ : I.FG), Function.Injective (lTensor M I.subtype) := by
  simpa [‚Üê comm_comp_rTensor_comp_comm_eq] using Module.Flat.iff_rTensor_injective R M

/-- The `lTensor`-variant of `iff_rTensor_injective'`. . -/
theorem iff_lTensor_injective' :
    Module.Flat R M ‚Üî ‚àÄ (I : Ideal R), Function.Injective (lTensor M I.subtype) := by
  simpa [‚Üê comm_comp_rTensor_comp_comm_eq] using Module.Flat.iff_rTensor_injective' R M

variable (N : Type w) [AddCommGroup N] [Module R N]

/-- A retract of a flat `R`-module is flat. -/
lemma of_retract [f : Flat R M] (i : N ‚Üí‚Çó[R] M) (r : M ‚Üí‚Çó[R] N) (h : r.comp i = LinearMap.id) :
    Flat R N := by
  rw [iff_rTensor_injective] at *
  intro I hI
  have h‚ÇÅ : Function.Injective (lTensor R i) := by
    apply Function.RightInverse.injective (g := (lTensor R r))
    intro x
    rw [‚Üê LinearMap.comp_apply, ‚Üê lTensor_comp, h]
    simp
  rw [‚Üê Function.Injective.of_comp_iff h‚ÇÅ (rTensor N I.subtype), ‚Üê LinearMap.coe_comp]
  rw [LinearMap.lTensor_comp_rTensor, ‚Üê LinearMap.rTensor_comp_lTensor]
  rw [LinearMap.coe_comp, Function.Injective.of_comp_iff (f hI)]
  apply Function.RightInverse.injective (g := lTensor _ r)
  intro x
  rw [‚Üê LinearMap.comp_apply, ‚Üê lTensor_comp, h]
  simp

/-- A `R`-module linearly equivalent to a flat `R`-module is flat. -/
lemma of_linearEquiv [f : Flat R M] (e : N ‚âÉ‚Çó[R] M) : Flat R N := by
  have h : e.symm.toLinearMap.comp e.toLinearMap = LinearMap.id := by simp
  exact of_retract _ _ _ e.toLinearMap e.symm.toLinearMap h

/-- A direct sum of flat `R`-modules is flat. -/
instance directSum (Œπ : Type v) (M : Œπ ‚Üí Type w) [(i : Œπ) ‚Üí AddCommGroup (M i)]
    [(i : Œπ) ‚Üí Module R (M i)] [F : (i : Œπ) ‚Üí (Flat R (M i))] : Flat R (‚®Å i, M i) := by
  haveI := Classical.decEq Œπ
  rw [iff_rTensor_injective]
  intro I hI
  -- This instance was added during PR #10828,
  -- see https://leanprover.zulipchat.com/#narrow/stream/144837-PR-reviews/topic/.2310828.20-.20generalizing.20CommRing.20to.20CommSemiring.20etc.2E/near/422684923
  letI : ‚àÄ i, AddCommGroup (I ‚äó[R] M i) := inferInstance
  rw [‚Üê Equiv.comp_injective _ (TensorProduct.lid R (‚®Å i, M i)).toEquiv]
  set Œ∑‚ÇÅ := TensorProduct.lid R (‚®Å i, M i)
  set Œ∑ := fun i ‚Ü¶ TensorProduct.lid R (M i)
  set œÜ := fun i ‚Ü¶ rTensor (M i) I.subtype
  set œÄ := fun i ‚Ü¶ component R Œπ (fun j ‚Ü¶ M j) i
  set œà := (TensorProduct.directSumRight R {x // x ‚àà I} (fun i ‚Ü¶ M i)).symm.toLinearMap with psi_def
  set œÅ := rTensor (‚®Å i, M i) I.subtype
  set œÑ := (fun i ‚Ü¶ component R Œπ (fun j ‚Ü¶ ({x // x ‚àà I} ‚äó[R] (M j))) i)
  rw [‚Üê Equiv.injective_comp (TensorProduct.directSumRight _ _ _).symm.toEquiv]
  rw [LinearEquiv.coe_toEquiv, ‚Üê LinearEquiv.coe_coe, ‚Üê LinearMap.coe_comp]
  rw [LinearEquiv.coe_toEquiv, ‚Üê LinearEquiv.coe_coe, ‚Üê LinearMap.coe_comp]
  rw [‚Üê psi_def, injective_iff_map_eq_zero ((Œ∑‚ÇÅ.comp œÅ).comp œà)]
  have h‚ÇÅ : ‚àÄ (i : Œπ), (œÄ i).comp ((Œ∑‚ÇÅ.comp œÅ).comp œà) = (Œ∑ i).comp ((œÜ i).comp (œÑ i)) := by
    intro i
    apply DirectSum.linearMap_ext
    intro j
    apply TensorProduct.ext'
    intro a m
    simp only [œÅ, œà, œÜ, Œ∑, Œ∑‚ÇÅ, coe_comp, LinearEquiv.coe_coe, Function.comp_apply,
      directSumRight_symm_lof_tmul, rTensor_tmul, Submodule.coeSubtype, lid_tmul, map_smul]
    rw [DirectSum.component.of, DirectSum.component.of]
    by_cases h‚ÇÇ : j = i
    ¬∑ subst j; simp
    ¬∑ simp [h‚ÇÇ]
  intro a ha; rw [DirectSum.ext_iff R]; intro i
  have f := LinearMap.congr_arg (f := (œÄ i)) ha
  erw [LinearMap.congr_fun (h‚ÇÅ i) a] at f
  rw [(map_zero (œÄ i) : (œÄ i) 0 = (0 : M i))] at f
  have h‚ÇÇ := F i
  rw [iff_rTensor_injective] at h‚ÇÇ
  have h‚ÇÉ := h‚ÇÇ hI
  simp only [coe_comp, LinearEquiv.coe_coe, Function.comp_apply, AddEquivClass.map_eq_zero_iff,
    h‚ÇÉ, LinearMap.map_eq_zero_iff] at f
  simp [f]

open scoped Classical in
/-- Free `R`-modules over discrete types are flat. -/
instance finsupp (Œπ : Type v) : Flat R (Œπ ‚Üí‚ÇÄ R) :=
  of_linearEquiv R _ _ (finsuppLEquivDirectSum R R Œπ)

instance of_free [Free R M] : Flat R M := of_linearEquiv R _ _ (Free.repr R M)

/-- A projective module with a discrete type of generator is flat -/
lemma of_projective_surjective (Œπ : Type w) [Projective R M] (p : (Œπ ‚Üí‚ÇÄ R) ‚Üí‚Çó[R] M)
    (hp : Surjective p) : Flat R M := by
  have h := Module.projective_lifting_property p (LinearMap.id) hp
  cases h with
    | _ e he => exact of_retract R _ _ _ _ he

instance of_projective [h : Projective R M] : Flat R M := by
  rw [Module.projective_def'] at h
  cases h with
    | _ e he => exact of_retract R _ _ _ _ he

/--
Define the character module of `M` to be `M ‚Üí+ ‚Ñö ‚ß∏ ‚Ñ§`.
The character module of `M` is an injective module if and only if
 `L ‚äó ùüô M` is injective for any linear map `L` in the same universe as `M`.
-/
lemma injective_characterModule_iff_rTensor_preserves_injective_linearMap :
    Module.Injective R (CharacterModule M) ‚Üî
    ‚àÄ ‚¶ÉN N' : Type v‚¶Ñ [AddCommGroup N] [AddCommGroup N'] [Module R N] [Module R N']
      (L : N ‚Üí‚Çó[R] N'), Function.Injective L ‚Üí Function.Injective (L.rTensor M) := by
  simp_rw [injective_iff, rTensor_injective_iff_lcomp_surjective, Surjective, DFunLike.ext_iff]; rfl

variable {R M N}

/-- `CharacterModule M` is Baer iff `M` is flat. -/
theorem iff_characterModule_baer : Flat R M ‚Üî Module.Baer R (CharacterModule M) := by
  simp_rw [iff_rTensor_injective', Baer, rTensor_injective_iff_lcomp_surjective,
    Surjective, DFunLike.ext_iff, Subtype.forall]; rfl

/-- `CharacterModule M` is an injective module iff `M` is flat. -/
theorem iff_characterModule_injective [Small.{v} R] :
    Flat R M ‚Üî Module.Injective R (CharacterModule M) :=
  iff_characterModule_baer.trans Module.Baer.iff_injective

/--
If `M` is a flat module, then `f ‚äó ùüô M` is injective for all injective linear maps `f`.
-/
theorem rTensor_preserves_injective_linearMap {N' : Type*} [AddCommGroup N'] [Module R N']
    [h : Flat R M] (L : N ‚Üí‚Çó[R] N') (hL : Function.Injective L) :
    Function.Injective (L.rTensor M) :=
  rTensor_injective_iff_lcomp_surjective.2 ((iff_characterModule_baer.1 h).extension_property _ hL)

@[deprecated]
alias preserves_injective_linearMap := rTensor_preserves_injective_linearMap

/--
If `M` is a flat module, then `ùüô M ‚äó f` is injective for all injective linear maps `f`.
-/
theorem lTensor_preserves_injective_linearMap {N' : Type*} [AddCommGroup N'] [Module R N']
    [Flat R M] (L : N ‚Üí‚Çó[R] N') (hL : Function.Injective L) :
    Function.Injective (L.lTensor M) :=
  (L.lTensor_inj_iff_rTensor_inj M).2 (rTensor_preserves_injective_linearMap L hL)

variable (R M) in
/--
M is flat if and only if `f ‚äó ùüô M` is injective whenever `f` is an injective linear map.
-/
lemma iff_rTensor_preserves_injective_linearMap [Small.{v} R] :
    Flat R M ‚Üî
    ‚àÄ ‚¶ÉN N' : Type v‚¶Ñ [AddCommGroup N] [AddCommGroup N'] [Module R N] [Module R N']
      (L : N ‚Üí‚Çó[R] N'), Function.Injective L ‚Üí Function.Injective (L.rTensor M) := by
  rw [iff_characterModule_injective,
    injective_characterModule_iff_rTensor_preserves_injective_linearMap]

variable (R M) in
/--
M is flat if and only if `ùüô M ‚äó f` is injective whenever `f` is an injective linear map.
-/
lemma iff_lTensor_preserves_injective_linearMap [Small.{v} R] :
    Flat R M ‚Üî
    ‚àÄ ‚¶ÉN N' : Type v‚¶Ñ [AddCommGroup N] [AddCommGroup N'] [Module R N] [Module R N']
      (L : N ‚Üí‚Çó[R] N'), Function.Injective L ‚Üí Function.Injective (L.lTensor M) := by
  simp_rw [iff_rTensor_preserves_injective_linearMap, LinearMap.lTensor_inj_iff_rTensor_inj]

variable (R M) in
lemma lTensor_exact [Small.{v} R] [flat : Flat R M] ‚¶ÉN N' N'' : Type v‚¶Ñ
    [AddCommGroup N] [AddCommGroup N'] [AddCommGroup N'']
    [Module R N] [Module R N'] [Module R N'']
    (f : N ‚Üí‚Çó[R] N') (g : N' ‚Üí‚Çó[R] N'')
    (exact : Function.Exact f g) :
    Function.Exact (f.lTensor M) (g.lTensor M) := by
  let œÄ : N' ‚Üí‚Çó[R] N' ‚ß∏ LinearMap.range f :=
  { toFun := Submodule.Quotient.mk
    map_add' := by simp
    map_smul' := by simp }
  have exact0 : Function.Exact f œÄ := by
    intro x; simp [œÄ]
  have surj0 : Function.Surjective œÄ := Quotient.surjective_Quotient_mk''

  let Œπ : N' ‚ß∏ LinearMap.range f ‚Üí‚Çó[R] N'' :=
    Submodule.subtype _ ‚àò‚Çó (LinearMap.quotKerEquivRange g).toLinearMap ‚àò‚Çó
      Submodule.quotEquivOfEq (LinearMap.range f) (LinearMap.ker g)
        (Function.LinearMap.exact_iff.mp exact).symm
  have inj0 : Function.Injective Œπ := by
    simpa [Œπ] using Subtype.val_injective
  have eq0 : g = Œπ.comp œÄ := by aesop

  suffices exact1 : Function.Exact (f.lTensor M) (œÄ.lTensor M) by
    rw [eq0, lTensor_comp]
    apply Function.Exact.comp_injective (exact := exact1)
      (inj := iff_lTensor_preserves_injective_linearMap R M |>.mp flat _ inj0)
      (h0 := map_zero _)

  exact _root_.lTensor_exact _ exact0 surj0

variable (R M) in
lemma rTensor_exact [Small.{v} R] [flat : Flat R M] ‚¶ÉN N' N'' : Type v‚¶Ñ
    [AddCommGroup N] [AddCommGroup N'] [AddCommGroup N'']
    [Module R N] [Module R N'] [Module R N'']
    (f : N ‚Üí‚Çó[R] N') (g : N' ‚Üí‚Çó[R] N'')
    (exact : Function.Exact f g) :
    Function.Exact (f.rTensor M) (g.rTensor M) := by
  let œÄ : N' ‚Üí‚Çó[R] N' ‚ß∏ LinearMap.range f :=
  { toFun := Submodule.Quotient.mk
    map_add' := by simp
    map_smul' := by simp }
  have exact0 : Function.Exact f œÄ := by
    intro x; simp [œÄ]
  have surj0 : Function.Surjective œÄ := Quotient.surjective_Quotient_mk''

  let Œπ : N' ‚ß∏ LinearMap.range f ‚Üí‚Çó[R] N'' :=
    Submodule.subtype _ ‚àò‚Çó (LinearMap.quotKerEquivRange g).toLinearMap ‚àò‚Çó
      Submodule.quotEquivOfEq (LinearMap.range f) (LinearMap.ker g)
        (Function.LinearMap.exact_iff.mp exact).symm
  have inj0 : Function.Injective Œπ := by
    simpa [Œπ] using Subtype.val_injective
  have eq0 : g = Œπ.comp œÄ := by aesop

  suffices exact1 : Function.Exact (f.rTensor M) (œÄ.rTensor M) by
    rw [eq0, rTensor_comp]
    apply Function.Exact.comp_injective (exact := exact1)
      (inj := iff_rTensor_preserves_injective_linearMap R M |>.mp flat _ inj0)
      (h0 := map_zero _)

  exact _root_.rTensor_exact _ exact0 surj0

variable (R M) in
lemma iff_lTensor_exact [Small.{v} R] :
    Flat R M ‚Üî
    ‚àÄ ‚¶ÉN N' N'' : Type v‚¶Ñ [AddCommGroup N] [AddCommGroup N'] [AddCommGroup N'']
      [Module R N] [Module R N'] [Module R N'']
      (f : N ‚Üí‚Çó[R] N') (g : N' ‚Üí‚Çó[R] N''), Function.Exact f g ‚Üí
      Function.Exact (f.lTensor M) (g.lTensor M) := by
  refine ‚ü®fun _ => lTensor_exact R M, fun H => iff_lTensor_preserves_injective_linearMap R M |>.mpr
    fun N' N'' _ _ _ _ L hL => ?_‚ü©
  rw [‚Üê LinearMap.ker_eq_bot, eq_bot_iff]
  rintro x (hx : _ = 0)
  simpa [Eq.comm] using
    @H PUnit N' N'' _ _ _ _ _ _ 0 L (by intro x; simpa [hL] using Eq.comm) x |>.mp hx

variable (R M) in
lemma iff_rTensor_exact [Small.{v} R] :
    Flat R M ‚Üî
    ‚àÄ ‚¶ÉN N' N'' : Type v‚¶Ñ [AddCommGroup N] [AddCommGroup N'] [AddCommGroup N'']
      [Module R N] [Module R N'] [Module R N'']
      (f : N ‚Üí‚Çó[R] N') (g : N' ‚Üí‚Çó[R] N''), Function.Exact f g ‚Üí
      Function.Exact (f.rTensor M) (g.rTensor M) := by
  refine ‚ü®fun _ => rTensor_exact R M, fun H => iff_rTensor_preserves_injective_linearMap R M |>.mpr
    fun N' N'' _ _ _ _ L hL => ?_‚ü©
  rw [‚Üê LinearMap.ker_eq_bot, eq_bot_iff]
  rintro x (hx : _ = 0)
  simpa [Eq.comm] using
    @H PUnit N' N'' _ _ _ _ _ _ 0 L (by intro x; simpa [hL] using Eq.comm) x |>.mp hx

noncomputable section categorical_characterisations

open CategoryTheory MonoidalCategory ModuleCat

variable (M : ModuleCat.{u} R)

open scoped MonoidalCategory in
set_option maxHeartbeats 400000 in
-- In two goals, we need to use `simpa` in one; and `simp` in the other.
set_option linter.unnecessarySimpa false in
instance [flat : Flat R M] {X Y : ModuleCat.{u} R} (f : X ‚ü∂ Y) :
    Limits.PreservesLimit (Limits.parallelPair f 0) (tensorLeft M) where
  preserves {c} hc := by
    let Œπ : c.pt ‚ü∂ X := c.œÄ.app .zero
    have mono0 : Mono Œπ := by
      constructor
      intro Z g h H
      let c' : Limits.Cone (Limits.parallelPair f 0) :=
        ‚ü®Z, ‚ü®fun x => match x with
        | .zero => h ‚â´ Œπ
        | .one => 0,
        fun _ _ l => match l with
          | .left => by simp [Œπ]
          | .right => by simp [Œπ]
          | .id x => by simp‚ü©‚ü©
      rw [hc.uniq c' g fun x => match x with
        | .zero => by simpa [Œπ] using H
        | .one => by simp, hc.uniq c' h fun x => match x with
        | .zero => by simp [Œπ]
        | .one => by simp]
    have exact0 : Exact Œπ f := by
      refine Abelian.exact_of_is_kernel (w := by simp [Œπ]) (h := ?_)
      refine Limits.IsLimit.equivOfNatIsoOfIso (Iso.refl _) _ _
        ‚ü®‚ü®?_, ?_‚ü©, ‚ü®?_, ?_‚ü©, ?_, ?_‚ü© hc
      ¬∑ exact ùüô c.pt
      ¬∑ rintro (‚ü®‚ü©|‚ü®‚ü©) <;> simp [Œπ]
      ¬∑ exact ùüô c.pt
      ¬∑ rintro (‚ü®‚ü©|‚ü®‚ü©) <;> simp [Œπ]
      ¬∑ rfl
      ¬∑ rfl

    let f' := M ‚óÅ f; let Œπ' := M ‚óÅ Œπ

    have exact1 : Exact Œπ' f' := by
      rw [exact_iff, Eq.comm, ‚Üê Function.LinearMap.exact_iff] at exact0 ‚ä¢
      exact lTensor_exact R M Œπ f exact0
    have mono1 : Mono Œπ' := by
      rw [ModuleCat.mono_iff_injective] at mono0 ‚ä¢
      letI : Flat R (of R M) := inferInstanceAs <| Flat R M
      exact lTensor_preserves_injective_linearMap _ mono0
    letI ic1 := Abelian.isLimitOfExactOfMono Œπ' f' exact1

    refine Limits.IsLimit.equivOfNatIsoOfIso ‚ü®‚ü®fun x => match x with
      | .zero => ùüô _
      | .one => ùüô _, ?_‚ü©, ‚ü®fun x => match x with
      | .zero => ùüô _
      | .one => ùüô _, ?_‚ü©, ?_, ?_‚ü© _ _ ‚ü®‚ü®?_, ?_‚ü©, ‚ü®?_, ?_‚ü©, ?_, ?_‚ü© ic1
    ¬∑ rintro _ _ (‚ü®‚ü© | ‚ü®‚ü© | ‚ü®_‚ü©) <;> simp
    ¬∑ rintro _ _ (‚ü®‚ü© | ‚ü®‚ü© | ‚ü®_‚ü©) <;> simp
    ¬∑ ext (‚ü®‚ü©|‚ü®‚ü©) <;> simp
    ¬∑ ext (‚ü®‚ü©|‚ü®‚ü©) <;> simp
    ¬∑ exact ùüô _
    ¬∑ rintro (‚ü®‚ü© | ‚ü®‚ü©) <;> simpa [Œπ', Œπ, f', Eq.comm] using exact1.w
    ¬∑ exact ùüô _
    ¬∑ rintro (‚ü®‚ü© | ‚ü®‚ü©) <;> simpa [Œπ', Œπ, f', Eq.comm] using exact1.w
    ¬∑ ext (‚ü®‚ü© | ‚ü®‚ü©); simp [Œπ', Œπ, f']
    ¬∑ ext (‚ü®‚ü© | ‚ü®‚ü©); simp [Œπ', Œπ, f']

instance tensorLeft_preservesFiniteLimits [Flat R M] :
    Limits.PreservesFiniteLimits (tensorLeft M) :=
  (tensorLeft M).preservesFiniteLimitsOfPreservesKernels

open scoped MonoidalCategory in
instance tensorRight_preservesFiniteLimits [Flat R M] :
    Limits.PreservesFiniteLimits (tensorRight M) where
  preservesFiniteLimits J _ _ :=
  { preservesLimit := fun {K} => by
      letI : Limits.PreservesLimit K (tensorLeft M) := inferInstance
      apply Limits.preservesLimitOfNatIso (F := tensorLeft M)
      exact ‚ü®‚ü®fun X => Œ≤_ _ _ |>.hom, by aesop_cat‚ü©, ‚ü®fun X => Œ≤_ _ _ |>.inv, by aesop_cat‚ü©,
        by aesop_cat, by aesop_cat‚ü© }

lemma iff_tensorLeft_preservesFiniteLimits :
    Flat R M ‚Üî
    Nonempty (Limits.PreservesFiniteLimits (tensorLeft M)) := by
  refine ‚ü®fun _ => ‚ü®inferInstance‚ü©, ?_‚ü©
  rintro ‚ü®_‚ü©
  rw [iff_lTensor_preserves_injective_linearMap]
  intro N N' _ _ _ _ L hL
  haveI : Mono (ofHom L) := by rwa [ModuleCat.mono_iff_injective]
  have inj : Mono <| (tensorLeft M).map (ofHom L) :=
    preserves_mono_of_preservesLimit (tensorLeft M) (ofHom L)
  rwa [ModuleCat.mono_iff_injective] at inj

lemma iff_tensorRight_preservesFiniteLimits :
    Flat R M ‚Üî
    Nonempty (Limits.PreservesFiniteLimits (tensorRight M)) := by
  refine ‚ü®fun _ => ‚ü®inferInstance‚ü©, ?_‚ü©
  rintro ‚ü®_‚ü©
  rw [iff_rTensor_preserves_injective_linearMap]
  intro N N' _ _ _ _ L hL
  haveI : Mono (ofHom L) := by rwa [ModuleCat.mono_iff_injective]
  have inj : Mono <| (tensorRight M).map (ofHom L) :=
    preserves_mono_of_preservesLimit (tensorRight M) (ofHom L)
  rwa [ModuleCat.mono_iff_injective] at inj

namespace tor_related_constructions

open Classical
open ShortComplex HomologicalComplex

variable (M) in
/-- For any `R`-module `M`, we associated with a free module `‚®Å (_ : M), R` -/
private def _root_.ModuleCat.free : ModuleCat.{u} R := of R <| ‚®Å (_ : M), R

instance : Free R M.free := Module.Free.dfinsupp _ _

instance [Module.Free R M] : Projective R M :=
  Module.Projective.of_free

instance [Module.Free R M] : CategoryTheory.Projective M where
  factors {E X} f e _ :=
    projective_lifting_property e f (by rwa [‚Üê ModuleCat.epi_iff_surjective])

/-- `‚®Å (m : M), R ‚ü∂ M` defined by `(_ ¬∑ m)` at `m`-th coordinate -/
private def _root_.ModuleCat.fromFree : M.free ‚ü∂ M :=
DirectSum.toModule _ _ _ fun i => LinearMap.lsmul R M |>.flip i

lemma surjective_fromFree : Surjective M.fromFree := by
  intro x
  use DirectSum.of _ x 1
  erw [toModule_lof, LinearMap.lsmul_apply, one_smul]

instance : Epi M.fromFree := by
  rw [ModuleCat.epi_iff_surjective]; apply surjective_fromFree

variable (R) in
/-- An auxilary structure just to do induction -/
structure ARROW :=
  /-- targe -/
  prev : ModuleCat.{u} R
  /-- source-/
  next : ModuleCat.{u} R
  [free_prev : Free R prev]
  [free_next : Free R next]
  /-- source to target -/
  hom : next ‚ü∂ prev

attribute [instance] ARROW.free_prev ARROW.free_next

open Limits
/--
We define a complex by induction. The complex is defined as follows:
```
M.free ‚ü∂ kernel M.fromFree ‚ü∂ kernel (kernel M.fromFree ‚ü∂ M.fromFree) ‚ü∂ ...
```
-/
def complexAux :
    ‚Ñï ‚Üí ARROW R :=
  Nat.rec
    ‚ü®M.free, kernel M.fromFree |>.free, ModuleCat.fromFree _ ‚â´ kernel.Œπ _‚ü©
    fun _ P =>
      ‚ü®P.next, kernel P.hom |>.free, ModuleCat.fromFree _ ‚â´ kernel.Œπ _‚ü©

lemma complexAux_exact (n : ‚Ñï) : Exact (complexAux M (n + 1)).hom (complexAux M n).hom := by
  change Exact (_ ‚â´ _) _
  apply exact_epi_comp (hgh := exact_kernel_Œπ)

/--
The complex `M.free ‚ü∂ kernel M.fromFree ‚ü∂ kernel (kernel M.fromFree ‚ü∂ M.fromFree) ‚ü∂ ...` is
indeed a chain complex
-/
def complex : ChainComplex (ModuleCat.{u} R) ‚Ñï where
  X n := complexAux M n |>.prev
  d i j :=
    if h : j + 1 = i
    then eqToHom (by subst (h : j.succ = i); simp [complexAux]) ‚â´ (complexAux M _).hom
    else 0
  d_comp_d' := by rintro _ _ i ‚ü®rfl‚ü© ‚ü®rfl‚ü©; simp [complexAux]

instance (n : ‚Ñï) : Free R <| (complex M).X n := by
  dsimp [complex]
  infer_instance

instance (n : ‚Ñï) : HomologicalComplex.HasHomology (complex M) n := by
  dsimp [complex]
  infer_instance

lemma complex_exact (n : ‚Ñï) : Exact ((complex M).d (n + 2) (n + 1)) ((complex M).d (n + 1) n) := by
  simpa [complex] using complexAux_exact M n

/--
The map from the free resolution to the complex `M.free ‚ü∂ 0 ‚ü∂ 0 ‚ü∂ ...` is a quasi-isomorphism.
-/
abbrev œÄ : complex M ‚ü∂ (ChainComplex.single‚ÇÄ _).obj M :=
  (ChainComplex.toSingle‚ÇÄEquiv _ _).symm ‚ü®M.fromFree, by simp [complex, complexAux]‚ü©

instance : QuasiIsoAt (œÄ M) 0 := by
  rw [ChainComplex.quasiIsoAt‚ÇÄ_iff, ShortComplex.quasiIso_iff_of_zeros'] <;> try rfl
  simpa only [complex, complexAux, shortComplexFunctor'_obj_X‚ÇÅ, Nat.rec_add_one, Nat.rec_zero,
    shortComplexFunctor'_obj_X‚ÇÇ, ChainComplex.single‚ÇÄ_obj_zero, shortComplexFunctor'_obj_f,
    zero_add, ‚ÜìreduceDite, eqToHom_refl, Category.id_comp, shortComplexFunctor'_map_œÑ‚ÇÇ,
    ChainComplex.toSingle‚ÇÄEquiv_symm_apply_f_zero] using
    ‚ü®exact_iff_shortComplex_exact _ |>.mp <| CategoryTheory.exact_epi_comp
      (hgh := exact_kernel_Œπ), inferInstance‚ü©

open scoped ZeroObject in
instance (n : ‚Ñï) : QuasiIsoAt (œÄ M) (n + 1) := by
  rw [quasiIsoAt_iff_isIso_homologyMap]
  have z1 : IsZero <| ((ChainComplex.single‚ÇÄ (ModuleCat R)).obj M).homology (n + 1) := by
    apply isZero_single_obj_homology
    simp

  have z2 : IsZero <| HomologicalComplex.homology (complex M) (n + 1) := by
    suffices e : HomologicalComplex.homology (complex M) (n + 1) ‚âÖ 0 from
      e.isZero_iff.mpr (isZero_zero _)

    refine exact_iff_homology_iso_zero _ |>.mp ?_ |>.some
    rw [‚Üê exact_iff_shortComplex_exact]
    simp only [complex, shortComplexFunctor_obj_X‚ÇÅ, shortComplexFunctor_obj_X‚ÇÇ,
      shortComplexFunctor_obj_X‚ÇÉ, shortComplexFunctor_obj_f, ChainComplex.prev, ‚ÜìreduceDite,
      shortComplexFunctor_obj_g, ChainComplex.next_nat_succ, exact_iso_comp]
    set g := _; change Exact _ g
    suffices g = (complexAux M n).hom ‚â´ eqToHom (by simp [complex]) by
      rw [this, exact_comp_iso]
      apply complexAux_exact M n
    simp [g]


  suffices HomologicalComplex.homologyMap (œÄ M) (n + 1) = (z2.iso z1).hom by
    rw [this]
    exact IsIso.of_iso _
  exact IsZero.eq_of_tgt z1 _ _

instance : _root_.QuasiIso (œÄ M) where
  quasiIsoAt n := by
    cases n <;> infer_instance

/--
the free resolution of any module
-/
def _root_.ModuleCat.freeResolution : ProjectiveResolution M where
  complex := complex M
  œÄ := œÄ M

instance : HasProjectiveResolutions (ModuleCat.{u} R) where
  out M := ‚ü®‚ü®M.freeResolution‚ü©‚ü©

end tor_related_constructions

open scoped ZeroObject

open tor_related_constructions in
/--
For a flat module `M`, higher tor groups vanish.
-/
def higherTorIsoZero [flat : Flat R M] (n : ‚Ñï) (N : ModuleCat.{u} R) :
    ((Tor' _ (n + 1)).obj N).obj M ‚âÖ 0 := by
  dsimp [Tor', Functor.flip]
  refine' N.freeResolution.isoLeftDerivedObj (tensorRight M) (n + 1) ‚â™‚â´ ?_
  refine Limits.IsZero.isoZero ?_
  dsimp only [HomologicalComplex.homologyFunctor_obj]
  rw [‚Üê HomologicalComplex.exactAt_iff_isZero_homology]
  dsimp [HomologicalComplex.ExactAt]
  rw [‚Üê exact_iff_shortComplex_exact]
  dsimp
  rw [ModuleCat.exact_iff, Eq.comm, ‚Üê Function.LinearMap.exact_iff]
  rw [iff_rTensor_exact] at flat
  refine flat _ _ ?_
  rw [Function.LinearMap.exact_iff, Eq.comm, ‚Üê ModuleCat.exact_iff]
  simp [ModuleCat.freeResolution]
  convert complex_exact N n using 1
  ¬∑ congr! 1; simp only [ChainComplex.prev]; rfl
  ¬∑ congr! 1; simp
  ¬∑ congr! 1; simp only [ChainComplex.prev]; rfl
  ¬∑ congr! 1; simp

/--
For a flat module `M`, the first tor group vanishes.
-/
def firstTorIsoZero [Flat R M] (N : ModuleCat.{u} R) :
    ((Tor' _ 1).obj N).obj M ‚âÖ 0 :=
  higherTorIsoZero M 0 N

/--
For a flat module `M`, the first tor group vanishes for all ideals.
-/
def firstTorOfIdealIsoZero [Flat R M] (I : Ideal R) :
    ((Tor' _ 1).obj (ModuleCat.of R (R ‚ß∏ I))).obj M ‚âÖ 0 :=
  firstTorIsoZero M (ModuleCat.of R (R ‚ß∏ I))

instance : EnoughProjectives (ModuleCat.{u} R) where
  presentation X := ‚ü®‚ü®X.free, X.fromFree‚ü©‚ü©

open scoped MonoidalCategory in
/--
If the first tor group vanishes for all ideals, then `M` is flat.
-/
lemma of_first_tor_is_zero
    (tor1 : ‚àÄ (I : Ideal R) (_ : I.FG), ((Tor' _ 1).obj (ModuleCat.of R (R ‚ß∏ I))).obj M ‚âÖ 0) :
    Flat R M := by
  rw [iff_rTensor_injective]
  intro I hI
  specialize tor1 I hI
  change Function.Injective ((ofHom I.subtype) ‚ñ∑ M)
  rw [‚Üê ModuleCat.mono_iff_injective, mono_iff_exact_zero_left]

  let sc : ShortComplex (ModuleCat.{u} R) :=
  { X‚ÇÅ := of R I
    X‚ÇÇ := of R R
    X‚ÇÉ := of R (R ‚ß∏ I)
    f := Submodule.subtype I
    g := Algebra.linearMap R (R ‚ß∏ I)
    zero := by
      ext x
      simp only [ModuleCat.coe_comp, Function.comp_apply]
      erw [Ideal.Quotient.eq_zero_iff_mem]
      exact x.2 }

  have : Mono sc.f := by
    rw [ModuleCat.mono_iff_injective]
    exact Subtype.val_injective
  have : Epi sc.g := by
    rw [ModuleCat.epi_iff_surjective]
    exact Quotient.surjective_Quotient_mk''
  have : Fact (sc.ShortExact) := by
    refine ‚ü®‚ü®?_‚ü©‚ü©
    rw [‚Üê exact_iff_shortComplex_exact, ModuleCat.exact_iff, Eq.comm,
      ‚Üê Function.LinearMap.exact_iff]
    rintro x
    erw [Set.mem_range, Ideal.Quotient.eq_zero_iff_mem]
    fconstructor
    ¬∑ rintro h; exact ‚ü®‚ü®x, h‚ü©, rfl‚ü©
    ¬∑ rintro ‚ü®y, rfl‚ü©; exact y.2

  let s : ShortComplex (ModuleCat.{u} R) :=
  { X‚ÇÅ := 0
    X‚ÇÇ := (of R I) ‚äó M
    X‚ÇÉ := (of R R) ‚äó M
    f := 0
    g := ofHom I.subtype ‚ñ∑ M
    zero := by simp }

  let s' : ShortComplex (ModuleCat.{u} R) :=
  .mk _ _ ((tensorRight M).exact_Œ¥ sc 0).w

  let e : s ‚âÖ s' :=
  { hom :=
    { œÑ‚ÇÅ := tor1.inv
      œÑ‚ÇÇ := (tensorRight M).leftDerivedZeroIsoSelf.inv.app (of R I)
      œÑ‚ÇÉ := (tensorRight M).leftDerivedZeroIsoSelf.inv.app (of R R)
      comm‚ÇÅ‚ÇÇ := by
        simp only [Tor'_obj_obj, Limits.zero_comp, Preadditive.IsIso.comp_left_eq_zero]
        exact (Limits.IsZero.of_iso (Limits.isZero_zero _) tor1).eq_of_src _ _
      comm‚ÇÇ‚ÇÉ := by
        simp only
        have eq0 :
            (Functor.leftDerivedZeroIsoSelf (tensorRight M)).inv.app (ModuleCat.of R I) =
            inv ((Functor.leftDerivedZeroIsoSelf (tensorRight M)).hom.app (ModuleCat.of R I)) := by
          aesop_cat
        have eq1 :
            (Functor.leftDerivedZeroIsoSelf (tensorRight M)).inv.app (ModuleCat.of R R) =
            inv ((Functor.leftDerivedZeroIsoSelf (tensorRight M)).hom.app (ModuleCat.of R R)) := by
          aesop_cat
        rw [eq0, eq1]
        simp only [tensorRight_obj, Functor.leftDerivedZeroIsoSelf_hom, IsIso.eq_comp_inv,
          Category.assoc, NatTrans.naturality, tensorRight_map, IsIso.inv_hom_id_assoc]
        rfl }
    inv :=
    { œÑ‚ÇÅ := tor1.hom
      œÑ‚ÇÇ := (tensorRight M).leftDerivedZeroIsoSelf.hom.app (of R I)
      œÑ‚ÇÉ := (tensorRight M).leftDerivedZeroIsoSelf.hom.app (of R R)
      comm‚ÇÅ‚ÇÇ := by
        simp only [Tor'_obj_obj, Limits.comp_zero, Functor.leftDerivedZeroIsoSelf_hom]
        exact (Limits.IsZero.of_iso (Limits.isZero_zero _) tor1).eq_of_src _ _
      comm‚ÇÇ‚ÇÉ := by
        simp only [Functor.leftDerivedZeroIsoSelf_hom, NatTrans.naturality, tensorRight_obj,
          tensorRight_map]
        rfl }
    hom_inv_id := by ext <;> simp
    inv_hom_id := by ext <;> simp }

  have hs : s.Exact := by
    apply ShortComplex.exact_of_iso e.symm
    rw [‚Üê exact_iff_shortComplex_exact]
    exact (tensorRight M).exact_Œ¥ _ _

  simp only [‚Üê exact_iff_shortComplex_exact, Tor'_obj_obj, exact_iso_comp] at hs
  exact hs

end categorical_characterisations

open ZeroObject in
open CategoryTheory MonoidalCategory ModuleCat in
lemma tfae (M : Type u) [AddCommGroup M] [Module R M] : List.TFAE
    [ Flat R M,
    ‚àÄ ‚¶ÉI : Ideal R‚¶Ñ (_ : I.FG), Function.Injective (rTensor M I.subtype),
    ‚àÄ I : Ideal R, Function.Injective (rTensor M I.subtype),
    ‚àÄ ‚¶ÉI : Ideal R‚¶Ñ (_ : I.FG), Function.Injective (lTensor M I.subtype),
    ‚àÄ (I : Ideal R), Function.Injective (lTensor M I.subtype),

    Module.Injective R (CharacterModule M),
    ‚àÄ ‚¶ÉN N' : Type u‚¶Ñ [AddCommGroup N] [AddCommGroup N'] [Module R N] [Module R N']
      (L : N ‚Üí‚Çó[R] N'), Function.Injective L ‚Üí Function.Injective (L.rTensor M),
    ‚àÄ ‚¶ÉN N' : Type u‚¶Ñ [AddCommGroup N] [AddCommGroup N'] [Module R N] [Module R N']
      (L : N ‚Üí‚Çó[R] N'), Function.Injective L ‚Üí Function.Injective (L.lTensor M),

    Nonempty (Limits.PreservesFiniteLimits (tensorLeft <| of R M)),
    Nonempty (Limits.PreservesFiniteLimits (tensorRight <| of R M)),

    ‚àÄ (n : ‚Ñï) (N : Type u) [AddCommGroup N] [Module R N],
      Nonempty <| ((Tor' _ (n + 1)).obj (of R N)).obj (of R M) ‚âÖ 0,
    ‚àÄ (N : Type u) [AddCommGroup N] [Module R N],
      Nonempty <| ((Tor' _ 1).obj (of R N)).obj (of R M) ‚âÖ 0,
    ‚àÄ (I : Ideal R) (_ : I.FG), Nonempty <| ((Tor' _ 1).obj (of R (R ‚ß∏ I))).obj (of R M) ‚âÖ 0 ] := by
  tfae_have 1 ‚Üî 2
  ¬∑ apply iff_rTensor_injective
  tfae_have 1 ‚Üî 3
  ¬∑ apply iff_rTensor_injective'
  tfae_have 1 ‚Üî 4
  ¬∑ apply iff_lTensor_injective
  tfae_have 1 ‚Üî 5
  ¬∑ apply iff_lTensor_injective'

  tfae_have 1 ‚Üî 6
  ¬∑ apply iff_characterModule_injective
  tfae_have 1 ‚Üî 7
  ¬∑ apply iff_rTensor_preserves_injective_linearMap
  tfae_have 1 ‚Üî 8
  ¬∑ apply iff_lTensor_preserves_injective_linearMap

  tfae_have 1 ‚Üî 9
  ¬∑ apply iff_tensorLeft_preservesFiniteLimits (of R M)
  tfae_have 1 ‚Üî 10
  ¬∑ apply iff_tensorRight_preservesFiniteLimits (of R M)

  tfae_have 1 ‚Üí 11
  ¬∑ intro h n N _ _
    exact ‚ü®higherTorIsoZero (M := of R M) (flat := h) n (of R N)‚ü©
  tfae_have 11 ‚Üí 12
  ¬∑ rintro h N _ _
    exact h 0 N
  tfae_have 1 ‚Üí 13
  ¬∑ rintro (h : Flat R (of R M)) I _
    exact ‚ü®firstTorOfIdealIsoZero (M := of R M) I‚ü©
  tfae_have 12 ‚Üí 13
  ¬∑ rintro h I _
    exact h (of R (R ‚ß∏ I))
  tfae_have 13 ‚Üí 1
  ¬∑ intro h
    exact of_first_tor_is_zero (M := of R M) fun I hI => (h I hI).some

  tfae_finish

end Flat

end Module
