/-
Copyright (c) 2024 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import Mathlib.RingTheory.Flat.Basic
import Mathlib.Algebra.Category.ModuleCat.Monoidal.Symmetric
import Mathlib.Algebra.Category.ModuleCat.Projective
import Mathlib.Algebra.Category.ModuleCat.Abelian
import Mathlib.CategoryTheory.Monoidal.Tor

/-!
# Tensoring with a flat module is an exact functor

In this file we prove that tensoring with a flat module is an exact functor.

## Main results
- `Module.Flat.iff_tensorLeft_preservesFiniteLimits`: an `R`-module `M` is flat if and only if
  left tensoring with `M` preserves finite limits, i.e. the functor `- âŠ— M` is left exact.

- `Module.Flat.iff_tensorRight_preservesFiniteLimits`: an `R`-module `M` is flat if and only if
  right tensoring with `M` preserves finite limits, i.e. the functor `M âŠ— -` is left exact.

- `Module.Flat.higherTorIsoZero`: if an `R`-module `M` is flat, then `Torâ¿(M, N) â‰… 0` for all `N`
  and all `n â‰¥ 1`.

## TODO

- Prove that vanishing `Tor`-groups implies flat.

-/

universe u

open CategoryTheory MonoidalCategory Abelian

namespace Module.Flat

variable {R : Type u} [CommRing R] (M : ModuleCat.{u} R)

open scoped MonoidalCategory in
set_option maxHeartbeats 400000 in
-- In two goals, we need to use `simpa` in one; and `simp` in the other.
set_option linter.unnecessarySimpa false in
noncomputable instance [flat : Flat R M] {X Y : ModuleCat.{u} R} (f : X âŸ¶ Y) :
    Limits.PreservesLimit (Limits.parallelPair f 0) (tensorLeft M) where
  preserves {c} hc := by
    let Î¹ : c.pt âŸ¶ X := c.Ï€.app .zero
    have mono0 : Mono Î¹ :=
      { right_cancellation := fun {Z} g h H => by
          let c' : Limits.Cone (Limits.parallelPair f 0) :=
          âŸ¨Z, âŸ¨fun | .zero => h â‰« Î¹ | .one => 0, by rintro _ _ (âŸ¨jâŸ©|âŸ¨jâŸ©) <;> simpa [Î¹] using HâŸ©âŸ©

          rw [hc.uniq c' g, hc.uniq c' h] <;>
          rintro (âŸ¨jâŸ©|âŸ¨jâŸ©) <;> simpa [Î¹] using H }
    have exact0 : Exact Î¹ f := by
      refine Abelian.exact_of_is_kernel (w := by simp [Î¹])
        (h := Limits.IsLimit.equivOfNatIsoOfIso (Iso.refl _) _ _
          âŸ¨âŸ¨?_, ?_âŸ©, âŸ¨?_, ?_âŸ©, ?_, ?_âŸ© hc)
      Â· exact ğŸ™ c.pt
      Â· rintro (âŸ¨âŸ©|âŸ¨âŸ©) <;> simp [Î¹]
      Â· exact ğŸ™ c.pt
      Â· rintro (âŸ¨âŸ©|âŸ¨âŸ©) <;> simp [Î¹]
      Â· rfl
      Â· rfl

    let f' := M â— f; let Î¹' := M â— Î¹
    have exact1 : Exact Î¹' f' := by
      rw [ModuleCat.exact_iff, Eq.comm, â† LinearMap.exact_iff] at exact0 âŠ¢
      exact lTensor_exact M exact0
    have mono1 : Mono Î¹' := by
      rw [ModuleCat.mono_iff_injective] at mono0 âŠ¢
      exact lTensor_preserves_injective_linearMap _ mono0

    refine Limits.IsLimit.equivOfNatIsoOfIso
      âŸ¨âŸ¨fun | .zero => ğŸ™ _ | .one => ğŸ™ _, ?_âŸ©,
        âŸ¨fun | .zero => ğŸ™ _ | .one => ğŸ™ _, ?_âŸ©, ?_, ?_âŸ© _ _ âŸ¨âŸ¨?_, ?_âŸ©, âŸ¨?_, ?_âŸ©, ?_, ?_âŸ© $
        Abelian.isLimitOfExactOfMono Î¹' f' exact1
    Â· rintro _ _ (âŸ¨âŸ© | âŸ¨âŸ© | âŸ¨_âŸ©) <;> simp
    Â· rintro _ _ (âŸ¨âŸ© | âŸ¨âŸ© | âŸ¨_âŸ©) <;> simp
    Â· ext (âŸ¨âŸ©|âŸ¨âŸ©) <;> simp
    Â· ext (âŸ¨âŸ©|âŸ¨âŸ©) <;> simp
    Â· exact ğŸ™ _
    Â· rintro (âŸ¨âŸ© | âŸ¨âŸ©) <;> simpa [Î¹', Î¹, f', Eq.comm] using exact1.w
    Â· exact ğŸ™ _
    Â· rintro (âŸ¨âŸ© | âŸ¨âŸ©) <;> simpa [Î¹', Î¹, f', Eq.comm] using exact1.w
    Â· ext (âŸ¨âŸ© | âŸ¨âŸ©); simp [Î¹', Î¹, f']
    Â· ext (âŸ¨âŸ© | âŸ¨âŸ©); simp [Î¹', Î¹, f']

noncomputable instance tensorLeft_preservesFiniteLimits [Flat R M] :
    Limits.PreservesFiniteLimits (tensorLeft M) :=
  (tensorLeft M).preservesFiniteLimitsOfPreservesKernels

noncomputable instance tensorRight_preservesFiniteLimits [Flat R M] :
    Limits.PreservesFiniteLimits (tensorRight M) where
  preservesFiniteLimits J _ _ :=
  { preservesLimit := fun {K} => by
      letI : Limits.PreservesLimit K (tensorLeft M) := inferInstance
      apply Limits.preservesLimitOfNatIso (F := tensorLeft M)
      exact âŸ¨âŸ¨fun X => Î²_ _ _ |>.hom, by aesop_catâŸ©, âŸ¨fun X => Î²_ _ _ |>.inv, by aesop_catâŸ©,
        by aesop_cat, by aesop_catâŸ© }

lemma iff_tensorLeft_preservesFiniteLimits :
    Flat R M â†”
    Nonempty (Limits.PreservesFiniteLimits (tensorLeft M)) :=
  âŸ¨fun _ => âŸ¨inferInstanceâŸ©, fun âŸ¨_âŸ© => iff_lTensor_preserves_injective_linearMap _ _ |>.2
    fun N N' _ _ _ _ L hL => by
      haveI : Mono (ModuleCat.ofHom L) := by rwa [ModuleCat.mono_iff_injective]
      have inj : Mono <| (tensorLeft M).map (ModuleCat.ofHom L) :=
        preserves_mono_of_preservesLimit (tensorLeft M) (ModuleCat.ofHom L)
      rwa [ModuleCat.mono_iff_injective] at injâŸ©

lemma iff_tensorRight_preservesFiniteLimits :
    Flat R M â†”
    Nonempty (Limits.PreservesFiniteLimits (tensorRight M)) :=
  âŸ¨fun _ => âŸ¨inferInstanceâŸ©, fun âŸ¨_âŸ© => iff_rTensor_preserves_injective_linearMap _ _ |>.2
    fun N N' _ _ _ _ L hL => by
    haveI : Mono (ModuleCat.ofHom L) := by rwa [ModuleCat.mono_iff_injective]
    have inj : Mono <| (tensorRight M).map (ModuleCat.ofHom L) :=
      preserves_mono_of_preservesLimit (tensorRight M) (ModuleCat.ofHom L)
    rwa [ModuleCat.mono_iff_injective] at injâŸ©

section Tor

open scoped ZeroObject

/--
For a flat module `M`, higher tor groups vanish.
-/
noncomputable def higherTorIsoZero [flat : Flat R M] (n : â„•) (N : ModuleCat.{u} R) :
    ((Tor' _ (n + 1)).obj N).obj M â‰… 0 := by
  dsimp [Tor', Functor.flip]
  let pN := ProjectiveResolution.of N
  refine' pN.isoLeftDerivedObj (tensorRight M) (n + 1) â‰ªâ‰« ?_
  refine Limits.IsZero.isoZero ?_
  dsimp only [HomologicalComplex.homologyFunctor_obj]
  rw [â† HomologicalComplex.exactAt_iff_isZero_homology, HomologicalComplex.exactAt_iff,
    â† exact_iff_shortComplex_exact, ModuleCat.exact_iff, Eq.comm, â† LinearMap.exact_iff]
  refine iff_rTensor_exact |>.1 flat ?_
  rw [LinearMap.exact_iff, Eq.comm, â† ModuleCat.exact_iff]
  have := pN.complex_exactAt_succ n
  rw [HomologicalComplex.exactAt_iff, â† exact_iff_shortComplex_exact] at this
  exact this

end Tor

end Module.Flat
