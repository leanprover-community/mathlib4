/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Jujian Zhang
-/
import Mathlib.Algebra.Module.CharacterModule
import Mathlib.RingTheory.Flat.Basic

/-!
# Flat modules

`M` is flat if `¬∑ ‚äó M` preserves finite limits (equivalently, pullbacks, or equalizers).
If `R` is a ring, an `R`-module `M` is flat if and only if it is mono-flat, and to show
a module is flat, it suffices to check inclusions of finitely generated ideals into `R`.
See <https://stacks.math.columbia.edu/tag/00HD>.

## Main theorems

* `Module.Flat.iff_characterModule_injective`: `CharacterModule M` is an injective module iff
  `M` is flat.
* `Module.Flat.iff_lTensor_injective`, `Module.Flat.iff_rTensor_injective`,
  `Module.Flat.iff_lTensor_injective'`, `Module.Flat.iff_rTensor_injective'`:
  A module `M` over a ring `R` is flat iff for all (finitely generated) ideals `I` of `R`, the
  tensor product of the inclusion `I ‚Üí R` and the identity `M ‚Üí M` is injective.
-/

universe u v

namespace Module.Flat

open Function (Surjective)

open LinearMap

variable {R : Type u} {M : Type v} [CommRing R] [AddCommGroup M] [Module R M]

/--
Define the character module of `M` to be `M ‚Üí+ ‚Ñö ‚ß∏ ‚Ñ§`.
The character module of `M` is an injective module if and only if
`f ‚äó ùüô M` is injective for any linear map `f` in the same universe as `M`.
-/
lemma injective_characterModule_iff_rTensor_preserves_injective_linearMap :
    Module.Injective R (CharacterModule M) ‚Üî
    ‚àÄ ‚¶ÉN N' : Type v‚¶Ñ [AddCommGroup N] [AddCommGroup N'] [Module R N] [Module R N']
      (f : N ‚Üí‚Çó[R] N'), Function.Injective f ‚Üí Function.Injective (f.rTensor M) := by
  simp_rw [injective_iff, rTensor_injective_iff_lcomp_surjective, Surjective, DFunLike.ext_iff]; rfl

/-- `CharacterModule M` is an injective module iff `M` is flat.
See [Lambek_1964] for a self-contained proof. -/
theorem iff_characterModule_injective [Small.{v} R] :
    Flat R M ‚Üî Module.Injective R (CharacterModule M) := by
  rw [injective_characterModule_iff_rTensor_preserves_injective_linearMap,
    iff_rTensor_preserves_injective_linearMap']

/-- `CharacterModule M` is Baer iff `M` is flat. -/
theorem iff_characterModule_baer : Flat R M ‚Üî Baer R (CharacterModule M) := by
  rw [equiv_iff (N := ULift.{u} M) ULift.moduleEquiv.symm, iff_characterModule_injective,
    ‚Üê Baer.iff_injective, Baer.congr (CharacterModule.congr ULift.moduleEquiv)]

/-- An `R`-module `M` is flat iff for all ideals `I` of `R`, the tensor product of the
inclusion `I ‚Üí R` and the identity `M ‚Üí M` is injective. See `iff_rTensor_injective` to
restrict to finitely generated ideals `I`. -/
theorem iff_rTensor_injective' :
    Flat R M ‚Üî ‚àÄ I : Ideal R, Function.Injective (rTensor M I.subtype) := by
  simp_rw [iff_characterModule_baer, Baer, rTensor_injective_iff_lcomp_surjective,
    Surjective, DFunLike.ext_iff, Subtype.forall, lcomp_apply, Submodule.subtype_apply]

/-- The `lTensor`-variant of `iff_rTensor_injective'`. . -/
theorem iff_lTensor_injective' :
    Flat R M ‚Üî ‚àÄ (I : Ideal R), Function.Injective (lTensor M I.subtype) := by
  simpa [‚Üê comm_comp_rTensor_comp_comm_eq] using iff_rTensor_injective'

/-- A module `M` over a ring `R` is flat iff for all finitely generated ideals `I` of `R`, the
tensor product of the inclusion `I ‚Üí R` and the identity `M ‚Üí M` is injective. See
`iff_rTensor_injective'` to extend to all ideals `I`. -/
lemma iff_rTensor_injective :
    Flat R M ‚Üî ‚àÄ ‚¶ÉI : Ideal R‚¶Ñ, I.FG ‚Üí Function.Injective (I.subtype.rTensor M) := by
  refine iff_rTensor_injective'.trans ‚ü®fun h I _ ‚Ü¶ h I,
    fun h I ‚Ü¶ (injective_iff_map_eq_zero _).mpr fun x hx ‚Ü¶ ?_‚ü©
  obtain ‚ü®J, hfg, hle, y, rfl‚ü© := Submodule.exists_fg_le_eq_rTensor_inclusion x
  rw [‚Üê rTensor_comp_apply] at hx
  rw [(injective_iff_map_eq_zero _).mp (h hfg) y hx, map_zero]

/-- The `lTensor`-variant of `iff_rTensor_injective`. -/
theorem iff_lTensor_injective :
    Flat R M ‚Üî ‚àÄ ‚¶ÉI : Ideal R‚¶Ñ, I.FG ‚Üí Function.Injective (I.subtype.lTensor M) := by
  simpa [‚Üê comm_comp_rTensor_comp_comm_eq] using iff_rTensor_injective

/-- An `R`-module `M` is flat if for all finitely generated ideals `I` of `R`,
the canonical map `I ‚äó M ‚Üí‚Çó M` is injective. -/
lemma iff_lift_lsmul_comp_subtype_injective : Flat R M ‚Üî ‚àÄ ‚¶ÉI : Ideal R‚¶Ñ, I.FG ‚Üí
    Function.Injective (TensorProduct.lift ((lsmul R M).comp I.subtype)) := by
  simp [iff_rTensor_injective, ‚Üê lid_comp_rTensor]

end Module.Flat
