/-
Copyright (c) 2025 Nailin Guan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nailin Guan
-/
module

public import Mathlib.RingTheory.Ideal.Cotangent
public import Mathlib.RingTheory.Ideal.KrullsHeightTheorem

/-!
# Define Regular Local Ring

For a Noetherian local ring `R`, we define `IsRegularLocalRing` as
`(maximalIdeal R).spanFinrank = ringKrullDim R`. This definition is equivalent to
the dimension of the cotangent space over the residue field being equal to `ringKrullDim R`,
(see `IsRegularLocalRing.iff_finrank_cotangentSpace`).

# Main Definition and Results

* `IsRegularLocalRing` : A Noetherian local ring is regular if
  `(maximalIdeal R).spanFinrank = ringKrullDim R`,
  i.e. its maximal ideal can be generated by `dim R` elements.

* `IsRegularLocalRing.iff_finrank_cotangentSpace` : the equivalence of `IsRegularLocalRing` and
  `Module.finrank (ResidueField R) (CotangentSpace R) = ringKrullDim R`

-/

@[expose] public section

open IsLocalRing

variable (R : Type*) [CommRing R]

/-- A Noetherian local ring is said to be regular if its maximal ideal
can be generated by `dim R` elements. -/
class IsRegularLocalRing : Prop extends IsLocalRing R, IsNoetherianRing R where
  spanFinrank_maximalIdeal : (maximalIdeal R).spanFinrank = ringKrullDim R

lemma isRegularLocalRing_def [IsLocalRing R] [IsNoetherianRing R] :
    IsRegularLocalRing R ↔ (maximalIdeal R).spanFinrank = ringKrullDim R :=
  ⟨fun ⟨h⟩ ↦ h, fun h ↦ ⟨h⟩⟩

namespace IsRegularLocalRing

lemma of_spanFinrank_maximalIdeal_le [IsLocalRing R] [IsNoetherianRing R]
    (le : (maximalIdeal R).spanFinrank ≤ ringKrullDim R) : IsRegularLocalRing R :=
  (isRegularLocalRing_def _).mpr (le_antisymm le (ringKrullDim_le_spanFinrank_maximalIdeal R))

variable {R} in
lemma of_ringEquiv [IsRegularLocalRing R] {R' : Type*} [CommRing R']
    (e : R ≃+* R') : IsRegularLocalRing R' := by
  let _ := e.isLocalRing
  let _ := isNoetherianRing_of_ringEquiv R e
  rw [isRegularLocalRing_def, ← spanFinrank_eq_of_ringEquiv e, ← ringKrullDim_eq_of_ringEquiv e]
  exact (isRegularLocalRing_def R).mp ‹_›

lemma iff_finrank_cotangentSpace [IsLocalRing R] [IsNoetherianRing R] :
    IsRegularLocalRing R ↔ Module.finrank (ResidueField R) (CotangentSpace R) = ringKrullDim R := by
  rw [isRegularLocalRing_def, spanFinrank_maximalIdeal_eq_finrank_cotangentSpace]

end IsRegularLocalRing
