/-
Copyright (c) 2025 Nailin Guan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nailin Guan
-/
module

public import Mathlib.RingTheory.Ideal.Cotangent
public import Mathlib.RingTheory.Ideal.KrullsHeightTheorem

/-!
# Define Regular Local Ring

For a noetherian local ring `R`, we define `IsRegularLocalRing` as
`(maximalIdeal R).spanFinrank = ringKrullDim R`. This definition is equivalent to
the dimension of the cotangent space over the residue field being equal to `ringKrullDim R`,
(see `IsRegularLocalRing.iff_finrank_cotangentSpace`).

# Main Definition and Results

* `IsRegularLocalRing` : A noetherian local ring of dimesnion `d` `IsRegularLocalRing` if
  `(maximalIdeal R).spanFinrank = ringKrullDim R`,
  i.e. its maximal ideal can be generated by `d` elements.

* `IsRegularLocalRing.iff_finrank_cotangentSpace` : the equivalence of `IsRegularLocalRing` and
  `Module.finrank (ResidueField R) (CotangentSpace R) = ringKrullDim R`

-/

@[expose] public section

open IsLocalRing

variable (R : Type*) [CommRing R]

/-- A noetherian local ring of dimesnion `d` is said to be regular if its maximal ideal
can be generated by `d` elements. -/
class IsRegularLocalRing : Prop extends IsLocalRing R, IsNoetherianRing R where
  reg : (maximalIdeal R).spanFinrank = ringKrullDim R

lemma isRegularLocalRing_def [IsLocalRing R] [IsNoetherianRing R] :
    IsRegularLocalRing R ↔ (maximalIdeal R).spanFinrank = ringKrullDim R :=
  ⟨fun ⟨h⟩ ↦ h, fun h ↦ ⟨h⟩⟩

lemma ringKrullDim_le_spanFinrank_maximalIdeal [IsLocalRing R] [IsNoetherianRing R] :
    ringKrullDim R ≤ (maximalIdeal R).spanFinrank :=
  le_of_eq_of_le IsLocalRing.maximalIdeal_height_eq_ringKrullDim.symm
    (WithBot.coe_le_coe.mpr (Ideal.height_le_spanFinrank (maximalIdeal R) Ideal.IsPrime.ne_top'))

namespace IsRegularLocalRing

lemma of_span_eq [IsLocalRing R] [IsNoetherianRing R] (S : Set R) (fin : S.Finite)
    (span : Ideal.span S = maximalIdeal R) (card : S.ncard ≤ ringKrullDim R) :
    IsRegularLocalRing R := by
  apply (isRegularLocalRing_def _).mpr (le_antisymm _ (ringKrullDim_le_spanFinrank_maximalIdeal R))
  apply le_trans _ card
  rw [← span, ← Ideal.submodule_span_eq]
  simpa using Submodule.spanFinrank_span_le_ncard_of_finite fin

variable {R} in
lemma of_ringEquiv [IsRegularLocalRing R] {R' : Type*} [CommRing R']
    (e : R ≃+* R') : IsRegularLocalRing R' := by
  let _ := e.isLocalRing
  let _ := isNoetherianRing_of_ringEquiv R e
  have fg : Submodule.FG (maximalIdeal R) := Ideal.fg_of_isNoetherianRing (maximalIdeal R)
  apply of_span_eq R' (e '' (maximalIdeal R).generators) (fg.finite_generators.image _)
  · rw [← Ideal.map_span, ← Ideal.submodule_span_eq, Submodule.span_generators , ← Ideal.comap_symm]
    exact ((local_hom_TFAE e.symm.toRingHom).out 0 4).mp (e.symm.surjective.isLocalHom _)
  · simpa [← ringKrullDim_eq_of_ringEquiv e, ← IsRegularLocalRing.reg,
      ← Submodule.FG.generators_ncard fg] using Set.ncard_image_le fg.finite_generators

end IsRegularLocalRing

lemma IsRegularLocalRing.iff_finrank_cotangentSpace [IsLocalRing R] [IsNoetherianRing R] :
    IsRegularLocalRing R ↔ Module.finrank (ResidueField R) (CotangentSpace R) = ringKrullDim R := by
  rw [isRegularLocalRing_def, spanFinrank_maximalIdeal_eq_finrank_cotangentSpace]
