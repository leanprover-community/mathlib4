/-
Copyright (c) 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri, Andrew Yang
-/
import Mathlib.RingTheory.Derivation.ToSquareZero
import Mathlib.RingTheory.Ideal.Cotangent
import Mathlib.RingTheory.IsTensorProduct
import Mathlib.Algebra.Exact
import Mathlib.Algebra.MvPolynomial.PDeriv
import Mathlib.Algebra.Polynomial.Derivation

#align_import ring_theory.kaehler from "leanprover-community/mathlib"@"4b92a463033b5587bb011657e25e4710bfca7364"

/-!
# The module of kaehler differentials

## Main results

- `KaehlerDifferential`: The module of kaehler differentials. For an `R`-algebra `S`, we provide
  the notation `Î©[Sâ„R]` for `KaehlerDifferential R S`.
  Note that the slash is `\textfractionsolidus`.
- `KaehlerDifferential.D`: The derivation into the module of kaehler differentials.
- `KaehlerDifferential.span_range_derivation`: The image of `D` spans `Î©[Sâ„R]` as an `S`-module.
- `KaehlerDifferential.linearMapEquivDerivation`:
  The isomorphism `Hom_R(Î©[Sâ„R], M) â‰ƒâ‚—[S] Der_R(S, M)`.
- `KaehlerDifferential.quotKerTotalEquiv`: An alternative description of `Î©[Sâ„R]` as `S` copies
  of `S` with kernel (`KaehlerDifferential.kerTotal`) generated by the relations:
  1. `dx + dy = d(x + y)`
  2. `x dy + y dx = d(x * y)`
  3. `dr = 0` for `r âˆˆ R`
- `KaehlerDifferential.map`: Given a map between the arrows `R â†’+* A` and `S â†’+* B`, we have an
  `A`-linear map `Î©[Aâ„R] â†’ Î©[Bâ„S]`.
- `KaehlerDifferential.map_surjective`:
  The sequence `Î©[Bâ„R] â†’ Î©[Bâ„A] â†’ 0` is exact.
- `KaehlerDifferential.exact_mapBaseChange_map`:
  The sequence `B âŠ—[A] Î©[Aâ„R] â†’ Î©[Bâ„R] â†’ Î©[Bâ„A]` is exact.

## Future project

- Define the `IsKaehlerDifferential` predicate.
-/

suppress_compilation

section KaehlerDifferential

open scoped TensorProduct
open Algebra

universe u v

variable (R : Type u) (S : Type v) [CommRing R] [CommRing S] [Algebra R S]

/-- The kernel of the multiplication map `S âŠ—[R] S â†’â‚[R] S`. -/
abbrev KaehlerDifferential.ideal : Ideal (S âŠ—[R] S) :=
  RingHom.ker (TensorProduct.lmul' R : S âŠ—[R] S â†’â‚[R] S)
#align kaehler_differential.ideal KaehlerDifferential.ideal

variable {S}

theorem KaehlerDifferential.one_smul_sub_smul_one_mem_ideal (a : S) :
    (1 : S) âŠ—â‚œ[R] a - a âŠ—â‚œ[R] (1 : S) âˆˆ KaehlerDifferential.ideal R S := by simp [RingHom.mem_ker]
#align kaehler_differential.one_smul_sub_smul_one_mem_ideal KaehlerDifferential.one_smul_sub_smul_one_mem_ideal

variable {R}
variable {M : Type*} [AddCommGroup M] [Module R M] [Module S M] [IsScalarTower R S M]

/-- For a `R`-derivation `S â†’ M`, this is the map `S âŠ—[R] S â†’â‚—[S] M` sending `s âŠ—â‚œ t â†¦ s â€¢ D t`. -/
def Derivation.tensorProductTo (D : Derivation R S M) : S âŠ—[R] S â†’â‚—[S] M :=
  TensorProduct.AlgebraTensorModule.lift ((LinearMap.lsmul S (S â†’â‚—[R] M)).flip D.toLinearMap)
#align derivation.tensor_product_to Derivation.tensorProductTo

theorem Derivation.tensorProductTo_tmul (D : Derivation R S M) (s t : S) :
    D.tensorProductTo (s âŠ—â‚œ t) = s â€¢ D t := rfl
#align derivation.tensor_product_to_tmul Derivation.tensorProductTo_tmul

theorem Derivation.tensorProductTo_mul (D : Derivation R S M) (x y : S âŠ—[R] S) :
    D.tensorProductTo (x * y) =
      TensorProduct.lmul' (S := S) R x â€¢ D.tensorProductTo y +
        TensorProduct.lmul' (S := S) R y â€¢ D.tensorProductTo x := by
  refine TensorProduct.induction_on x ?_ ?_ ?_
  Â· rw [zero_mul, map_zero, map_zero, zero_smul, smul_zero, add_zero]
  swap
  Â· intro xâ‚ yâ‚ hâ‚ hâ‚‚
    rw [add_mul, map_add, map_add, map_add, add_smul, smul_add, hâ‚, hâ‚‚, add_add_add_comm]
  intro xâ‚ xâ‚‚
  refine TensorProduct.induction_on y ?_ ?_ ?_
  Â· rw [mul_zero, map_zero, map_zero, zero_smul, smul_zero, add_zero]
  swap
  Â· intro xâ‚ yâ‚ hâ‚ hâ‚‚
    rw [mul_add, map_add, map_add, map_add, add_smul, smul_add, hâ‚, hâ‚‚, add_add_add_comm]
  intro x y
  simp only [TensorProduct.tmul_mul_tmul, Derivation.tensorProductTo,
    TensorProduct.AlgebraTensorModule.lift_apply, TensorProduct.lift.tmul',
    TensorProduct.lmul'_apply_tmul]
  dsimp
  rw [D.leibniz]
  simp only [smul_smul, smul_add, mul_comm (x * y) xâ‚, mul_right_comm xâ‚ xâ‚‚, â† mul_assoc]
#align derivation.tensor_product_to_mul Derivation.tensorProductTo_mul

variable (R S)

/-- The kernel of `S âŠ—[R] S â†’â‚[R] S` is generated by `1 âŠ— s - s âŠ— 1` as a `S`-module. -/
theorem KaehlerDifferential.submodule_span_range_eq_ideal :
    Submodule.span S (Set.range fun s : S => (1 : S) âŠ—â‚œ[R] s - s âŠ—â‚œ[R] (1 : S)) =
      (KaehlerDifferential.ideal R S).restrictScalars S := by
  apply le_antisymm
  Â· rw [Submodule.span_le]
    rintro _ âŸ¨s, rflâŸ©
    exact KaehlerDifferential.one_smul_sub_smul_one_mem_ideal _ _
  Â· rintro x (hx : _ = _)
    have : x - TensorProduct.lmul' (S := S) R x âŠ—â‚œ[R] (1 : S) = x := by
      rw [hx, TensorProduct.zero_tmul, sub_zero]
    rw [â† this]
    clear this hx
    refine TensorProduct.induction_on x ?_ ?_ ?_
    Â· rw [map_zero, TensorProduct.zero_tmul, sub_zero]; exact zero_mem _
    Â· intro x y
      have : x âŠ—â‚œ[R] y - (x * y) âŠ—â‚œ[R] (1 : S) = x â€¢ ((1 : S) âŠ—â‚œ y - y âŠ—â‚œ (1 : S)) := by
        simp_rw [smul_sub, TensorProduct.smul_tmul', smul_eq_mul, mul_one]
      rw [TensorProduct.lmul'_apply_tmul, this]
      refine Submodule.smul_mem _ x ?_
      apply Submodule.subset_span
      exact Set.mem_range_self y
    Â· intro x y hx hy
      rw [map_add, TensorProduct.add_tmul, â† sub_add_sub_comm]
      exact add_mem hx hy
#align kaehler_differential.submodule_span_range_eq_ideal KaehlerDifferential.submodule_span_range_eq_ideal

theorem KaehlerDifferential.span_range_eq_ideal :
    Ideal.span (Set.range fun s : S => (1 : S) âŠ—â‚œ[R] s - s âŠ—â‚œ[R] (1 : S)) =
      KaehlerDifferential.ideal R S := by
  apply le_antisymm
  Â· rw [Ideal.span_le]
    rintro _ âŸ¨s, rflâŸ©
    exact KaehlerDifferential.one_smul_sub_smul_one_mem_ideal _ _
  Â· change (KaehlerDifferential.ideal R S).restrictScalars S â‰¤ (Ideal.span _).restrictScalars S
    rw [â† KaehlerDifferential.submodule_span_range_eq_ideal, Ideal.span]
    conv_rhs => rw [â† Submodule.span_span_of_tower S]
    exact Submodule.subset_span
#align kaehler_differential.span_range_eq_ideal KaehlerDifferential.span_range_eq_ideal

/-- The module of KÃ¤hler differentials (Kahler differentials, Kaehler differentials).
This is implemented as `I / I ^ 2` with `I` the kernel of the multiplication map `S âŠ—[R] S â†’â‚[R] S`.
To view elements as a linear combination of the form `s â€¢ D s'`, use
`KaehlerDifferential.tensorProductTo_surjective` and `Derivation.tensorProductTo_tmul`.

We also provide the notation `Î©[Sâ„R]` for `KaehlerDifferential R S`.
Note that the slash is `\textfractionsolidus`.
-/
def KaehlerDifferential : Type v :=
  (KaehlerDifferential.ideal R S).Cotangent
#align kaehler_differential KaehlerDifferential

instance : AddCommGroup (KaehlerDifferential R S) := by
  unfold KaehlerDifferential
  infer_instance

instance KaehlerDifferential.module : Module (S âŠ—[R] S) (KaehlerDifferential R S) :=
  Ideal.Cotangent.moduleOfTower _
#align kaehler_differential.module KaehlerDifferential.module

@[inherit_doc KaehlerDifferential]
notation:100 "Î©[" S "â„" R "]" => KaehlerDifferential R S

instance : Nonempty (Î©[Sâ„R]) := âŸ¨0âŸ©

instance KaehlerDifferential.module' {R' : Type*} [CommRing R'] [Algebra R' S]
  [SMulCommClass R R' S] :
    Module R' (Î©[Sâ„R]) :=
  Submodule.Quotient.module' _
#align kaehler_differential.module' KaehlerDifferential.module'

instance : IsScalarTower S (S âŠ—[R] S) (Î©[Sâ„R]) :=
  Ideal.Cotangent.isScalarTower _

instance KaehlerDifferential.isScalarTower_of_tower {Râ‚ Râ‚‚ : Type*} [CommRing Râ‚] [CommRing Râ‚‚]
    [Algebra Râ‚ S] [Algebra Râ‚‚ S] [SMul Râ‚ Râ‚‚]
    [SMulCommClass R Râ‚ S] [SMulCommClass R Râ‚‚ S] [IsScalarTower Râ‚ Râ‚‚ S] :
    IsScalarTower Râ‚ Râ‚‚ (Î©[Sâ„R]) :=
  Submodule.Quotient.isScalarTower _ _

#align kaehler_differential.is_scalar_tower_of_tower KaehlerDifferential.isScalarTower_of_tower

instance KaehlerDifferential.isScalarTower' : IsScalarTower R (S âŠ—[R] S) (Î©[Sâ„R]) :=
  Submodule.Quotient.isScalarTower _ _
#align kaehler_differential.is_scalar_tower' KaehlerDifferential.isScalarTower'

/-- The quotient map `I â†’ Î©[Sâ„R]` with `I` being the kernel of `S âŠ—[R] S â†’ S`. -/
def KaehlerDifferential.fromIdeal : KaehlerDifferential.ideal R S â†’â‚—[S âŠ—[R] S] Î©[Sâ„R] :=
  (KaehlerDifferential.ideal R S).toCotangent
#align kaehler_differential.from_ideal KaehlerDifferential.fromIdeal

/-- (Implementation) The underlying linear map of the derivation into `Î©[Sâ„R]`. -/
def KaehlerDifferential.DLinearMap : S â†’â‚—[R] Î©[Sâ„R] :=
  ((KaehlerDifferential.fromIdeal R S).restrictScalars R).comp
    ((TensorProduct.includeRight.toLinearMap - TensorProduct.includeLeft.toLinearMap :
            S â†’â‚—[R] S âŠ—[R] S).codRestrict
        ((KaehlerDifferential.ideal R S).restrictScalars R)
        (KaehlerDifferential.one_smul_sub_smul_one_mem_ideal R) :
      _ â†’â‚—[R] _)
set_option linter.uppercaseLean3 false in
#align kaehler_differential.D_linear_map KaehlerDifferential.DLinearMap

theorem KaehlerDifferential.DLinearMap_apply (s : S) :
    KaehlerDifferential.DLinearMap R S s =
      (KaehlerDifferential.ideal R S).toCotangent
        âŸ¨1 âŠ—â‚œ s - s âŠ—â‚œ 1, KaehlerDifferential.one_smul_sub_smul_one_mem_ideal R sâŸ© := rfl
set_option linter.uppercaseLean3 false in
#align kaehler_differential.D_linear_map_apply KaehlerDifferential.DLinearMap_apply

/-- The universal derivation into `Î©[Sâ„R]`. -/
def KaehlerDifferential.D : Derivation R S (Î©[Sâ„R]) :=
  { toLinearMap := KaehlerDifferential.DLinearMap R S
    map_one_eq_zero' := by
      dsimp [KaehlerDifferential.DLinearMap_apply, Ideal.toCotangent_apply]
      congr
      rw [sub_self]
    leibniz' := fun a b => by
      have : LinearMap.CompatibleSMul { x // x âˆˆ ideal R S } (Î©[Sâ„R]) S (S âŠ—[R] S) := inferInstance
      dsimp [KaehlerDifferential.DLinearMap_apply]
      -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
      erw [â† LinearMap.map_smul_of_tower (Mâ‚‚ := Î©[Sâ„R]),
        â† LinearMap.map_smul_of_tower (Mâ‚‚ := Î©[Sâ„R]), â† map_add, Ideal.toCotangent_eq, pow_two]
      convert Submodule.mul_mem_mul (KaehlerDifferential.one_smul_sub_smul_one_mem_ideal R a : _)
        (KaehlerDifferential.one_smul_sub_smul_one_mem_ideal R b : _) using 1
      simp only [AddSubgroupClass.coe_sub, Submodule.coe_add, Submodule.coe_mk,
        TensorProduct.tmul_mul_tmul, mul_sub, sub_mul, mul_comm b, Submodule.coe_smul_of_tower,
        smul_sub, TensorProduct.smul_tmul', smul_eq_mul, mul_one]
      ring_nf }
set_option linter.uppercaseLean3 false in
#align kaehler_differential.D KaehlerDifferential.D

theorem KaehlerDifferential.D_apply (s : S) :
    KaehlerDifferential.D R S s =
      (KaehlerDifferential.ideal R S).toCotangent
        âŸ¨1 âŠ—â‚œ s - s âŠ—â‚œ 1, KaehlerDifferential.one_smul_sub_smul_one_mem_ideal R sâŸ© := rfl
set_option linter.uppercaseLean3 false in
#align kaehler_differential.D_apply KaehlerDifferential.D_apply

theorem KaehlerDifferential.span_range_derivation :
    Submodule.span S (Set.range <| KaehlerDifferential.D R S) = âŠ¤ := by
  rw [_root_.eq_top_iff]
  rintro x -
  obtain âŸ¨âŸ¨x, hxâŸ©, rflâŸ© := Ideal.toCotangent_surjective _ x
  have : x âˆˆ (KaehlerDifferential.ideal R S).restrictScalars S := hx
  rw [â† KaehlerDifferential.submodule_span_range_eq_ideal] at this
  suffices âˆƒ hx, (KaehlerDifferential.ideal R S).toCotangent âŸ¨x, hxâŸ© âˆˆ
      Submodule.span S (Set.range <| KaehlerDifferential.D R S) by
    exact this.choose_spec
  refine Submodule.span_induction this ?_ ?_ ?_ ?_
  Â· rintro _ âŸ¨x, rflâŸ©
    refine âŸ¨KaehlerDifferential.one_smul_sub_smul_one_mem_ideal R x, ?_âŸ©
    apply Submodule.subset_span
    exact âŸ¨x, KaehlerDifferential.DLinearMap_apply R S xâŸ©
  Â· exact âŸ¨zero_mem _, Submodule.zero_mem _âŸ©
  Â· rintro x y âŸ¨hxâ‚, hxâ‚‚âŸ© âŸ¨hyâ‚, hyâ‚‚âŸ©; exact âŸ¨add_mem hxâ‚ hyâ‚, Submodule.add_mem _ hxâ‚‚ hyâ‚‚âŸ©
  Â· rintro r x âŸ¨hxâ‚, hxâ‚‚âŸ©;
    exact âŸ¨((KaehlerDifferential.ideal R S).restrictScalars S).smul_mem r hxâ‚,
      Submodule.smul_mem _ r hxâ‚‚âŸ©
#align kaehler_differential.span_range_derivation KaehlerDifferential.span_range_derivation

variable {R S}

/-- The linear map from `Î©[Sâ„R]`, associated with a derivation. -/
def Derivation.liftKaehlerDifferential (D : Derivation R S M) : Î©[Sâ„R] â†’â‚—[S] M := by
  refine LinearMap.comp ((((KaehlerDifferential.ideal R S) â€¢
    (âŠ¤ : Submodule (S âŠ—[R] S) (KaehlerDifferential.ideal R S))).restrictScalars S).liftQ ?_ ?_)
    (Submodule.Quotient.restrictScalarsEquiv S _).symm.toLinearMap
  Â· exact D.tensorProductTo.comp ((KaehlerDifferential.ideal R S).subtype.restrictScalars S)
  Â· intro x hx
    rw [LinearMap.mem_ker]
    refine Submodule.smul_induction_on hx ?_ ?_
    Â· rintro x hx y -
      rw [RingHom.mem_ker] at hx
      dsimp
      rw [Derivation.tensorProductTo_mul, hx, y.prop, zero_smul, zero_smul, zero_add]
    Â· intro x y ex ey; rw [map_add, ex, ey, zero_add]
#align derivation.lift_kaehler_differential Derivation.liftKaehlerDifferential

theorem Derivation.liftKaehlerDifferential_apply (D : Derivation R S M) (x) :
    D.liftKaehlerDifferential ((KaehlerDifferential.ideal R S).toCotangent x) =
      D.tensorProductTo x := rfl
#align derivation.lift_kaehler_differential_apply Derivation.liftKaehlerDifferential_apply

theorem Derivation.liftKaehlerDifferential_comp (D : Derivation R S M) :
    D.liftKaehlerDifferential.compDer (KaehlerDifferential.D R S) = D := by
  ext a
  dsimp [KaehlerDifferential.D_apply]
  refine (D.liftKaehlerDifferential_apply _).trans ?_
  rw [Subtype.coe_mk, map_sub, Derivation.tensorProductTo_tmul, Derivation.tensorProductTo_tmul,
    one_smul, D.map_one_eq_zero, smul_zero, sub_zero]
#align derivation.lift_kaehler_differential_comp Derivation.liftKaehlerDifferential_comp

@[simp]
theorem Derivation.liftKaehlerDifferential_comp_D (D' : Derivation R S M) (x : S) :
    D'.liftKaehlerDifferential (KaehlerDifferential.D R S x) = D' x :=
  Derivation.congr_fun D'.liftKaehlerDifferential_comp x
set_option linter.uppercaseLean3 false in
#align derivation.lift_kaehler_differential_comp_D Derivation.liftKaehlerDifferential_comp_D

@[ext]
theorem Derivation.liftKaehlerDifferential_unique (f f' : Î©[Sâ„R] â†’â‚—[S] M)
    (hf : f.compDer (KaehlerDifferential.D R S) = f'.compDer (KaehlerDifferential.D R S)) :
    f = f' := by
  apply LinearMap.ext
  intro x
  have : x âˆˆ Submodule.span S (Set.range <| KaehlerDifferential.D R S) := by
    rw [KaehlerDifferential.span_range_derivation]; trivial
  refine Submodule.span_induction this ?_ ?_ ?_ ?_
  Â· rintro _ âŸ¨x, rflâŸ©; exact congr_arg (fun D : Derivation R S M => D x) hf
  Â· rw [map_zero, map_zero]
  Â· intro x y hx hy; rw [map_add, map_add, hx, hy]
  Â· intro a x e; simp [e]
#align derivation.lift_kaehler_differential_unique Derivation.liftKaehlerDifferential_unique

variable (R S)

theorem Derivation.liftKaehlerDifferential_D :
    (KaehlerDifferential.D R S).liftKaehlerDifferential = LinearMap.id :=
  Derivation.liftKaehlerDifferential_unique _ _
    (KaehlerDifferential.D R S).liftKaehlerDifferential_comp
set_option linter.uppercaseLean3 false in
#align derivation.lift_kaehler_differential_D Derivation.liftKaehlerDifferential_D

variable {R S}

theorem KaehlerDifferential.D_tensorProductTo (x : KaehlerDifferential.ideal R S) :
    (KaehlerDifferential.D R S).tensorProductTo x =
      (KaehlerDifferential.ideal R S).toCotangent x := by
  rw [â† Derivation.liftKaehlerDifferential_apply, Derivation.liftKaehlerDifferential_D]
  rfl
set_option linter.uppercaseLean3 false in
#align kaehler_differential.D_tensor_product_to KaehlerDifferential.D_tensorProductTo

variable (R S)

theorem KaehlerDifferential.tensorProductTo_surjective :
    Function.Surjective (KaehlerDifferential.D R S).tensorProductTo := by
  intro x; obtain âŸ¨x, rflâŸ© := (KaehlerDifferential.ideal R S).toCotangent_surjective x
  exact âŸ¨x, KaehlerDifferential.D_tensorProductTo xâŸ©
#align kaehler_differential.tensor_product_to_surjective KaehlerDifferential.tensorProductTo_surjective

/-- The `S`-linear maps from `Î©[Sâ„R]` to `M` are (`S`-linearly) equivalent to `R`-derivations
from `S` to `M`.  -/
@[simps! symm_apply apply_apply]
def KaehlerDifferential.linearMapEquivDerivation : (Î©[Sâ„R] â†’â‚—[S] M) â‰ƒâ‚—[S] Derivation R S M :=
  { Derivation.llcomp.flip <| KaehlerDifferential.D R S with
    invFun := Derivation.liftKaehlerDifferential
    left_inv := fun _ =>
      Derivation.liftKaehlerDifferential_unique _ _ (Derivation.liftKaehlerDifferential_comp _)
    right_inv := Derivation.liftKaehlerDifferential_comp }
#align kaehler_differential.linear_map_equiv_derivation KaehlerDifferential.linearMapEquivDerivation

/-- The quotient ring of `S âŠ— S â§¸ J ^ 2` by `Î©[Sâ„R]` is isomorphic to `S`. -/
def KaehlerDifferential.quotientCotangentIdealRingEquiv :
    (S âŠ— S â§¸ KaehlerDifferential.ideal R S ^ 2) â§¸ (KaehlerDifferential.ideal R S).cotangentIdeal â‰ƒ+*
      S := by
  have : Function.RightInverse (TensorProduct.includeLeft (R := R) (S := R) (A := S) (B := S))
      (â†‘(TensorProduct.lmul' R : S âŠ—[R] S â†’â‚[R] S) : S âŠ—[R] S â†’+* S) := by
    intro x; rw [AlgHom.coe_toRingHom, â† AlgHom.comp_apply, TensorProduct.lmul'_comp_includeLeft]
    rfl
  refine (Ideal.quotCotangent _).trans ?_
  refine (Ideal.quotEquivOfEq ?_).trans (RingHom.quotientKerEquivOfRightInverse this)
  ext; rfl
#align kaehler_differential.quotient_cotangent_ideal_ring_equiv KaehlerDifferential.quotientCotangentIdealRingEquiv

/-- The quotient ring of `S âŠ— S â§¸ J ^ 2` by `Î©[Sâ„R]` is isomorphic to `S` as an `S`-algebra. -/
def KaehlerDifferential.quotientCotangentIdeal :
    ((S âŠ— S â§¸ KaehlerDifferential.ideal R S ^ 2) â§¸
        (KaehlerDifferential.ideal R S).cotangentIdeal) â‰ƒâ‚[S] S :=
  { KaehlerDifferential.quotientCotangentIdealRingEquiv R S with
    commutes' := (KaehlerDifferential.quotientCotangentIdealRingEquiv R S).apply_symm_apply }
#align kaehler_differential.quotient_cotangent_ideal KaehlerDifferential.quotientCotangentIdeal

theorem KaehlerDifferential.End_equiv_aux (f : S â†’â‚[R] S âŠ— S â§¸ KaehlerDifferential.ideal R S ^ 2) :
    (Ideal.Quotient.mkâ‚ R (KaehlerDifferential.ideal R S).cotangentIdeal).comp f =
        IsScalarTower.toAlgHom R S _ â†”
      (TensorProduct.lmul' R : S âŠ—[R] S â†’â‚[R] S).kerSquareLift.comp f = AlgHom.id R S := by
  rw [AlgHom.ext_iff, AlgHom.ext_iff]
  apply forall_congr'
  intro x
  have eâ‚ : (TensorProduct.lmul' R : S âŠ—[R] S â†’â‚[R] S).kerSquareLift (f x) =
      KaehlerDifferential.quotientCotangentIdealRingEquiv R S
        (Ideal.Quotient.mk (KaehlerDifferential.ideal R S).cotangentIdeal <| f x) := by
    generalize f x = y; obtain âŸ¨y, rflâŸ© := Ideal.Quotient.mk_surjective y; rfl
  have eâ‚‚ :
    x = KaehlerDifferential.quotientCotangentIdealRingEquiv R S (IsScalarTower.toAlgHom R S _ x) :=
    (mul_one x).symm
  constructor
  Â· intro e
    exact (eâ‚.trans (@RingEquiv.congr_arg _ _ _ _ _ _
      (KaehlerDifferential.quotientCotangentIdealRingEquiv R S) _ _ e)).trans eâ‚‚.symm
  Â· intro e; apply (KaehlerDifferential.quotientCotangentIdealRingEquiv R S).injective
    exact eâ‚.symm.trans (e.trans eâ‚‚)
#align kaehler_differential.End_equiv_aux KaehlerDifferential.End_equiv_aux

/- Note: Lean is slow to synthesize theses instances (times out).
  Without them the endEquivDerivation' and endEquivAuxEquiv both have significant timeouts.
  In Mathlib 3, it was slow but not this slow. -/
/-- A shortcut instance to prevent timing out. Hopefully to be removed in the future. -/
local instance smul_SSmod_SSmod : SMul (S âŠ—[R] S â§¸ KaehlerDifferential.ideal R S ^ 2)
    (S âŠ—[R] S â§¸ KaehlerDifferential.ideal R S ^ 2) := Mul.toSMul _

/-- A shortcut instance to prevent timing out. Hopefully to be removed in the future. -/
@[nolint defLemma]
local instance isScalarTower_S_right :
    IsScalarTower S (S âŠ—[R] S â§¸ KaehlerDifferential.ideal R S ^ 2)
      (S âŠ—[R] S â§¸ KaehlerDifferential.ideal R S ^ 2) := Ideal.Quotient.isScalarTower_right

/-- A shortcut instance to prevent timing out. Hopefully to be removed in the future. -/
@[nolint defLemma]
local instance isScalarTower_R_right :
    IsScalarTower R (S âŠ—[R] S â§¸ KaehlerDifferential.ideal R S ^ 2)
      (S âŠ—[R] S â§¸ KaehlerDifferential.ideal R S ^ 2) := Ideal.Quotient.isScalarTower_right

/-- A shortcut instance to prevent timing out. Hopefully to be removed in the future. -/
@[nolint defLemma]
local instance isScalarTower_SS_right : IsScalarTower (S âŠ—[R] S)
    (S âŠ—[R] S â§¸ KaehlerDifferential.ideal R S ^ 2) (S âŠ—[R] S â§¸ KaehlerDifferential.ideal R S ^ 2) :=
  Ideal.Quotient.isScalarTower_right

/-- A shortcut instance to prevent timing out. Hopefully to be removed in the future. -/
local instance instS : Module S (KaehlerDifferential.ideal R S).cotangentIdeal :=
  Submodule.module' _

/-- A shortcut instance to prevent timing out. Hopefully to be removed in the future. -/
local instance instR : Module R (KaehlerDifferential.ideal R S).cotangentIdeal :=
  Submodule.module' _

/-- A shortcut instance to prevent timing out. Hopefully to be removed in the future. -/
local instance instSS : Module (S âŠ—[R] S) (KaehlerDifferential.ideal R S).cotangentIdeal :=
  Submodule.module' _

/-- Derivations into `Î©[Sâ„R]` is equivalent to derivations
into `(KaehlerDifferential.ideal R S).cotangentIdeal`. -/
noncomputable def KaehlerDifferential.endEquivDerivation' :
    Derivation R S (Î©[Sâ„R]) â‰ƒâ‚—[R] Derivation R S (ideal R S).cotangentIdeal :=
  LinearEquiv.compDer ((KaehlerDifferential.ideal R S).cotangentEquivIdeal.restrictScalars S)
#align kaehler_differential.End_equiv_derivation' KaehlerDifferential.endEquivDerivation'

/-- (Implementation) An `Equiv` version of `KaehlerDifferential.End_equiv_aux`.
Used in `KaehlerDifferential.endEquiv`. -/
def KaehlerDifferential.endEquivAuxEquiv :
    { f //
        (Ideal.Quotient.mkâ‚ R (KaehlerDifferential.ideal R S).cotangentIdeal).comp f =
          IsScalarTower.toAlgHom R S _ } â‰ƒ
      { f // (TensorProduct.lmul' R : S âŠ—[R] S â†’â‚[R] S).kerSquareLift.comp f = AlgHom.id R S } :=
  (Equiv.refl _).subtypeEquiv (KaehlerDifferential.End_equiv_aux R S)
#align kaehler_differential.End_equiv_aux_equiv KaehlerDifferential.endEquivAuxEquiv

/--
The endomorphisms of `Î©[Sâ„R]` corresponds to sections of the surjection `S âŠ—[R] S â§¸ J ^ 2 â†’â‚[R] S`,
with `J` being the kernel of the multiplication map `S âŠ—[R] S â†’â‚[R] S`.
-/
noncomputable def KaehlerDifferential.endEquiv :
    Module.End S (Î©[Sâ„R]) â‰ƒ
      { f // (TensorProduct.lmul' R : S âŠ—[R] S â†’â‚[R] S).kerSquareLift.comp f = AlgHom.id R S } :=
  (KaehlerDifferential.linearMapEquivDerivation R S).toEquiv.trans <|
    (KaehlerDifferential.endEquivDerivation' R S).toEquiv.trans <|
      (derivationToSquareZeroEquivLift (KaehlerDifferential.ideal R S).cotangentIdeal
            (KaehlerDifferential.ideal R S).cotangentIdeal_square).trans <|
        KaehlerDifferential.endEquivAuxEquiv R S
#align kaehler_differential.End_equiv KaehlerDifferential.endEquiv

section Presentation

open KaehlerDifferential (D)

open Finsupp (single)

/-- The `S`-submodule of `S â†’â‚€ S` (the direct sum of copies of `S` indexed by `S`) generated by
the relations:
1. `dx + dy = d(x + y)`
2. `x dy + y dx = d(x * y)`
3. `dr = 0` for `r âˆˆ R`
where `db` is the unit in the copy of `S` with index `b`.

This is the kernel of the surjection `Finsupp.total S Î©[Sâ„R] S (KaehlerDifferential.D R S)`.
See `KaehlerDifferential.kerTotal_eq` and `KaehlerDifferential.total_surjective`.
-/
noncomputable def KaehlerDifferential.kerTotal : Submodule S (S â†’â‚€ S) :=
  Submodule.span S
    (((Set.range fun x : S Ã— S => single x.1 1 + single x.2 1 - single (x.1 + x.2) 1) âˆª
        Set.range fun x : S Ã— S => single x.2 x.1 + single x.1 x.2 - single (x.1 * x.2) 1) âˆª
      Set.range fun x : R => single (algebraMap R S x) 1)
#align kaehler_differential.ker_total KaehlerDifferential.kerTotal

unsuppress_compilation in
-- Porting note: was `local notation x "ğ–£" y => (KaehlerDifferential.kerTotal R S).mkQ (single y x)`
-- but not having `DFunLike.coe` leads to `kerTotal_mkQ_single_smul` failing.
local notation3 x "ğ–£" y => DFunLike.coe (KaehlerDifferential.kerTotal R S).mkQ (single y x)

theorem KaehlerDifferential.kerTotal_mkQ_single_add (x y z) : (zğ–£x + y) = (zğ–£x) + zğ–£y := by
  rw [â† map_add, eq_comm, â† sub_eq_zero, â† map_sub (Submodule.mkQ (kerTotal R S)),
    Submodule.mkQ_apply, Submodule.Quotient.mk_eq_zero]
  simp_rw [â† Finsupp.smul_single_one _ z, â† smul_add, â† smul_sub]
  exact Submodule.smul_mem _ _ (Submodule.subset_span (Or.inl <| Or.inl <| âŸ¨âŸ¨_, _âŸ©, rflâŸ©))
#align kaehler_differential.ker_total_mkq_single_add KaehlerDifferential.kerTotal_mkQ_single_add

theorem KaehlerDifferential.kerTotal_mkQ_single_mul (x y z) :
    (zğ–£x * y) = ((z * x)ğ–£y) + (z * y)ğ–£x := by
  rw [â† map_add, eq_comm, â† sub_eq_zero, â† map_sub (Submodule.mkQ (kerTotal R S)),
    Submodule.mkQ_apply, Submodule.Quotient.mk_eq_zero]
  simp_rw [â† Finsupp.smul_single_one _ z, â† @smul_eq_mul _ _ z, â† Finsupp.smul_single, â† smul_add,
    â† smul_sub]
  exact Submodule.smul_mem _ _ (Submodule.subset_span (Or.inl <| Or.inr <| âŸ¨âŸ¨_, _âŸ©, rflâŸ©))
#align kaehler_differential.ker_total_mkq_single_mul KaehlerDifferential.kerTotal_mkQ_single_mul

theorem KaehlerDifferential.kerTotal_mkQ_single_algebraMap (x y) : (yğ–£algebraMap R S x) = 0 := by
  rw [Submodule.mkQ_apply, Submodule.Quotient.mk_eq_zero, â† Finsupp.smul_single_one _ y]
  exact Submodule.smul_mem _ _ (Submodule.subset_span (Or.inr <| âŸ¨_, rflâŸ©))
#align kaehler_differential.ker_total_mkq_single_algebra_map KaehlerDifferential.kerTotal_mkQ_single_algebraMap

theorem KaehlerDifferential.kerTotal_mkQ_single_algebraMap_one (x) : (xğ–£1) = 0 := by
  rw [â† (algebraMap R S).map_one, KaehlerDifferential.kerTotal_mkQ_single_algebraMap]
#align kaehler_differential.ker_total_mkq_single_algebra_map_one KaehlerDifferential.kerTotal_mkQ_single_algebraMap_one

theorem KaehlerDifferential.kerTotal_mkQ_single_smul (r : R) (x y) : (yğ–£r â€¢ x) = r â€¢ yğ–£x := by
  letI : SMulZeroClass R S := inferInstance
  rw [Algebra.smul_def, KaehlerDifferential.kerTotal_mkQ_single_mul,
    KaehlerDifferential.kerTotal_mkQ_single_algebraMap, add_zero, â† LinearMap.map_smul_of_tower,
    Finsupp.smul_single, mul_comm, Algebra.smul_def]
#align kaehler_differential.ker_total_mkq_single_smul KaehlerDifferential.kerTotal_mkQ_single_smul

/-- The (universal) derivation into `(S â†’â‚€ S) â§¸ KaehlerDifferential.kerTotal R S`. -/
noncomputable def KaehlerDifferential.derivationQuotKerTotal :
    Derivation R S ((S â†’â‚€ S) â§¸ KaehlerDifferential.kerTotal R S) where
  toFun x := 1ğ–£x
  map_add' x y := KaehlerDifferential.kerTotal_mkQ_single_add _ _ _ _ _
  map_smul' r s := KaehlerDifferential.kerTotal_mkQ_single_smul _ _ _ _ _
  map_one_eq_zero' := KaehlerDifferential.kerTotal_mkQ_single_algebraMap_one _ _ _
  leibniz' a b :=
    (KaehlerDifferential.kerTotal_mkQ_single_mul _ _ _ _ _).trans
      (by simp_rw [â† Finsupp.smul_single_one _ (1 * _ : S)]; dsimp; simp)
#align kaehler_differential.derivation_quot_ker_total KaehlerDifferential.derivationQuotKerTotal

theorem KaehlerDifferential.derivationQuotKerTotal_apply (x) :
    KaehlerDifferential.derivationQuotKerTotal R S x = 1ğ–£x :=
  rfl
#align kaehler_differential.derivation_quot_ker_total_apply KaehlerDifferential.derivationQuotKerTotal_apply

theorem KaehlerDifferential.derivationQuotKerTotal_lift_comp_total :
    (KaehlerDifferential.derivationQuotKerTotal R S).liftKaehlerDifferential.comp
        (Finsupp.total S (Î©[Sâ„R]) S (KaehlerDifferential.D R S)) =
      Submodule.mkQ _ := by
  apply Finsupp.lhom_ext
  intro a b
  conv_rhs => rw [â† Finsupp.smul_single_one a b, LinearMap.map_smul]
  simp [KaehlerDifferential.derivationQuotKerTotal_apply]
#align kaehler_differential.derivation_quot_ker_total_lift_comp_total KaehlerDifferential.derivationQuotKerTotal_lift_comp_total

theorem KaehlerDifferential.kerTotal_eq :
    LinearMap.ker (Finsupp.total S (Î©[Sâ„R]) S (KaehlerDifferential.D R S)) =
      KaehlerDifferential.kerTotal R S := by
  apply le_antisymm
  Â· conv_rhs => rw [â† (KaehlerDifferential.kerTotal R S).ker_mkQ]
    rw [â† KaehlerDifferential.derivationQuotKerTotal_lift_comp_total]
    exact LinearMap.ker_le_ker_comp _ _
  Â· rw [KaehlerDifferential.kerTotal, Submodule.span_le]
    rintro _ ((âŸ¨âŸ¨x, yâŸ©, rflâŸ© | âŸ¨âŸ¨x, yâŸ©, rflâŸ©) | âŸ¨x, rflâŸ©) <;> dsimp <;> simp [LinearMap.mem_ker]
#align kaehler_differential.ker_total_eq KaehlerDifferential.kerTotal_eq

theorem KaehlerDifferential.total_surjective :
    Function.Surjective (Finsupp.total S (Î©[Sâ„R]) S (KaehlerDifferential.D R S)) := by
  rw [â† LinearMap.range_eq_top, Finsupp.range_total, KaehlerDifferential.span_range_derivation]
#align kaehler_differential.total_surjective KaehlerDifferential.total_surjective

/-- `Î©[Sâ„R]` is isomorphic to `S` copies of `S` with kernel `KaehlerDifferential.kerTotal`. -/
@[simps!]
noncomputable def KaehlerDifferential.quotKerTotalEquiv :
    ((S â†’â‚€ S) â§¸ KaehlerDifferential.kerTotal R S) â‰ƒâ‚—[S] Î©[Sâ„R] :=
  { (KaehlerDifferential.kerTotal R S).liftQ
      (Finsupp.total S (Î©[Sâ„R]) S (KaehlerDifferential.D R S))
      (KaehlerDifferential.kerTotal_eq R S).ge with
    invFun := (KaehlerDifferential.derivationQuotKerTotal R S).liftKaehlerDifferential
    left_inv := by
      intro x
      obtain âŸ¨x, rflâŸ© := Submodule.mkQ_surjective _ x
      exact
        LinearMap.congr_fun (KaehlerDifferential.derivationQuotKerTotal_lift_comp_total R S : _) x
    right_inv := by
      intro x
      obtain âŸ¨x, rflâŸ© := KaehlerDifferential.total_surjective R S x
      have := LinearMap.congr_fun (KaehlerDifferential.derivationQuotKerTotal_lift_comp_total R S) x
      rw [LinearMap.comp_apply] at this
      rw [this]
      rfl }
#align kaehler_differential.quot_ker_total_equiv KaehlerDifferential.quotKerTotalEquiv

theorem KaehlerDifferential.quotKerTotalEquiv_symm_comp_D :
    (KaehlerDifferential.quotKerTotalEquiv R S).symm.toLinearMap.compDer
        (KaehlerDifferential.D R S) =
      KaehlerDifferential.derivationQuotKerTotal R S := by
  convert (KaehlerDifferential.derivationQuotKerTotal R S).liftKaehlerDifferential_comp
set_option linter.uppercaseLean3 false in
#align kaehler_differential.quot_ker_total_equiv_symm_comp_D KaehlerDifferential.quotKerTotalEquiv_symm_comp_D

end Presentation

section ExactSequence

/- We have the commutative diagram
A --â†’ B
â†‘     â†‘
|     |
R --â†’ S -/
variable (A B : Type*) [CommRing A] [CommRing B] [Algebra R A] [Algebra R B]
variable [Algebra A B] [Algebra S B] [IsScalarTower R A B] [IsScalarTower R S B]
variable [SMulCommClass S A B]

unsuppress_compilation in
-- The map `(A â†’â‚€ A) â†’â‚—[A] (B â†’â‚€ B)`
local macro "finsupp_map" : term =>
  `((Finsupp.mapRange.linearMap (Algebra.linearMap A B)).comp
    (Finsupp.lmapDomain A A (algebraMap A B)))

/--
Given the commutative diagram
A --â†’ B
â†‘     â†‘
|     |
R --â†’ S
The kernel of the presentation `âŠ•â‚“ B dx â†  Î©_{B/S}` is spanned by the image of the
kernel of `âŠ•â‚“ A dx â†  Î©_{A/R}` and all `ds` with `s : S`.
See `kerTotal_map'` for the special case where `R = S`.
-/
theorem KaehlerDifferential.kerTotal_map (h : Function.Surjective (algebraMap A B)) :
    (KaehlerDifferential.kerTotal R A).map finsupp_map âŠ”
        Submodule.span A (Set.range fun x : S => .single (algebraMap S B x) (1 : B)) =
      (KaehlerDifferential.kerTotal S B).restrictScalars _ := by
  rw [KaehlerDifferential.kerTotal, Submodule.map_span, KaehlerDifferential.kerTotal,
    Submodule.restrictScalars_span _ _ h]
  simp_rw [Set.image_union, Submodule.span_union, â† Set.image_univ, Set.image_image, Set.image_univ,
    map_sub, map_add]
  simp only [LinearMap.comp_apply, Finsupp.lmapDomain_apply, Finsupp.mapDomain_single,
    Finsupp.mapRange.linearMap_apply, Finsupp.mapRange_single, Algebra.linearMap_apply,
    map_one, map_add, map_mul]
  simp_rw [sup_assoc, â† (h.prodMap h).range_comp]
  congr!
  -- Porting note: new
  simp_rw [â† IsScalarTower.algebraMap_apply R A B]
  rw [sup_eq_right]
  apply Submodule.span_mono
  simp_rw [IsScalarTower.algebraMap_apply R S B]
  exact Set.range_comp_subset_range (algebraMap R S)
    fun x => Finsupp.single (algebraMap S B x) (1 : B)
#align kaehler_differential.ker_total_map KaehlerDifferential.kerTotal_map

/--
This is a special case of `kerTotal_map` where `R = S`.
The kernel of the presentation `âŠ•â‚“ B dx â†  Î©_{B/R}` is spanned by the image of the
kernel of `âŠ•â‚“ A dx â†  Î©_{A/R}` and all `da` with `a : A`.
-/
theorem KaehlerDifferential.kerTotal_map' (h : Function.Surjective (algebraMap A B)) :
    (KaehlerDifferential.kerTotal R A âŠ”
      Submodule.span A (Set.range fun x â†¦ .single (algebraMap R A x) 1)).map finsupp_map =
      (KaehlerDifferential.kerTotal R B).restrictScalars _ := by
  rw [Submodule.map_sup, â† kerTotal_map R R A B h, Submodule.map_span, â† Set.range_comp]
  congr
  refine congr_arg Set.range ?_
  ext; simp [IsScalarTower.algebraMap_eq R A B]

/-- The map `Î©[Aâ„R] â†’â‚—[A] Î©[Bâ„S]` given a square
A --â†’ B
â†‘     â†‘
|     |
R --â†’ S -/
def KaehlerDifferential.map : Î©[Aâ„R] â†’â‚—[A] Î©[Bâ„S] :=
  Derivation.liftKaehlerDifferential
    (((KaehlerDifferential.D S B).restrictScalars R).compAlgebraMap A)
#align kaehler_differential.map KaehlerDifferential.map

theorem KaehlerDifferential.map_compDer :
    (KaehlerDifferential.map R S A B).compDer (KaehlerDifferential.D R A) =
      ((KaehlerDifferential.D S B).restrictScalars R).compAlgebraMap A :=
  Derivation.liftKaehlerDifferential_comp _
#align kaehler_differential.map_comp_der KaehlerDifferential.map_compDer

@[simp]
theorem KaehlerDifferential.map_D (x : A) :
    KaehlerDifferential.map R S A B (KaehlerDifferential.D R A x) =
      KaehlerDifferential.D S B (algebraMap A B x) :=
  Derivation.congr_fun (KaehlerDifferential.map_compDer R S A B) x
set_option linter.uppercaseLean3 false in
#align kaehler_differential.map_D KaehlerDifferential.map_D

theorem KaehlerDifferential.ker_map :
    LinearMap.ker (KaehlerDifferential.map R S A B) =
      (((kerTotal S B).restrictScalars A).comap finsupp_map).map
        (Finsupp.total A (Î©[Aâ„R]) A (D R A)) := by
  rw [â† Submodule.map_comap_eq_of_surjective (total_surjective R A) (LinearMap.ker _)]
  congr 1
  ext x
  simp only [Submodule.mem_comap, LinearMap.mem_ker, Finsupp.apply_total, â† kerTotal_eq,
    Submodule.restrictScalars_mem]
  simp only [Finsupp.total_apply, Function.comp_apply, LinearMap.coe_comp, Finsupp.lmapDomain_apply,
    Finsupp.mapRange.linearMap_apply]
  rw [Finsupp.sum_mapRange_index, Finsupp.sum_mapDomain_index]
  Â· simp [ofId]
  Â· simp
  Â· simp [add_smul]
  Â· simp

open IsScalarTower (toAlgHom)

theorem KaehlerDifferential.map_surjective_of_surjective
    (h : Function.Surjective (algebraMap A B)) :
    Function.Surjective (KaehlerDifferential.map R S A B) := by
  rw [â† LinearMap.range_eq_top, _root_.eq_top_iff, â† @Submodule.restrictScalars_top A B,
    â† KaehlerDifferential.span_range_derivation, Submodule.restrictScalars_span _ _ h,
    Submodule.span_le]
  rintro _ âŸ¨x, rflâŸ©
  obtain âŸ¨y, rflâŸ© := h x
  rw [â† KaehlerDifferential.map_D R S A B]
  exact âŸ¨_, rflâŸ©
#align kaehler_differential.map_surjective_of_surjective KaehlerDifferential.map_surjective_of_surjective

theorem KaehlerDifferential.map_surjective :
    Function.Surjective (KaehlerDifferential.map R S B B) :=
  map_surjective_of_surjective R S B B Function.surjective_id

/-- The lift of the map `Î©[Aâ„R] â†’â‚—[A] Î©[Bâ„R]` to the base change along `A â†’ B`.
This is the first map in the exact sequence `B âŠ—[A] Î©[Aâ„R] â†’ Î©[Bâ„R] â†’ Î©[Bâ„A] â†’ 0`. -/
noncomputable def KaehlerDifferential.mapBaseChange : B âŠ—[A] Î©[Aâ„R] â†’â‚—[B] Î©[Bâ„R] :=
  (TensorProduct.isBaseChange A (Î©[Aâ„R]) B).lift (KaehlerDifferential.map R R A B)
#align kaehler_differential.map_base_change KaehlerDifferential.mapBaseChange

@[simp]
theorem KaehlerDifferential.mapBaseChange_tmul (x : B) (y : Î©[Aâ„R]) :
    KaehlerDifferential.mapBaseChange R A B (x âŠ—â‚œ y) = x â€¢ KaehlerDifferential.map R R A B y := by
  conv_lhs => rw [â† mul_one x, â† smul_eq_mul, â† TensorProduct.smul_tmul', LinearMap.map_smul]
  congr 1
  exact IsBaseChange.lift_eq _ _ _
#align kaehler_differential.map_base_change_tmul KaehlerDifferential.mapBaseChange_tmul

lemma KaehlerDifferential.range_mapBaseChange :
    LinearMap.range (mapBaseChange R A B) = LinearMap.ker (map R A B B) := by
  apply le_antisymm
  Â· rintro _ âŸ¨x, rflâŸ©
    induction' x using TensorProduct.induction_on with r s
    Â· simp
    Â· obtain âŸ¨x, rflâŸ© := total_surjective _ _ s
      simp only [mapBaseChange_tmul, LinearMap.mem_ker, map_smul]
      induction x using Finsupp.induction_linear
      Â· simp
      Â· simp [smul_add, *]
      Â· simp
    Â· rw [map_add]; exact add_mem â€¹_â€º â€¹_â€º
  Â· convert_to (kerTotal A B).map (Finsupp.total B (Î©[Bâ„R]) B (D R B)) â‰¤ _
    Â· rw [KaehlerDifferential.ker_map]
      congr 1
      convert Submodule.comap_id _
      Â· ext; simp
    rw [Submodule.map_le_iff_le_comap, kerTotal, Submodule.span_le]
    rintro f ((âŸ¨âŸ¨x, yâŸ©, rflâŸ©|âŸ¨âŸ¨x, yâŸ©, rflâŸ©)|âŸ¨x, rflâŸ©)
    Â· use 0; simp
    Â· use 0; simp
    Â· use 1 âŠ—â‚œ D _ _ x; simp

/-- The sequence `B âŠ—[A] Î©[Aâ„R] â†’ Î©[Bâ„R] â†’ Î©[Bâ„A] â†’ 0` is exact.
Also see `KaehlerDifferential.map_surjective`. -/
lemma KaehlerDifferential.exact_mapBaseChange_map :
    Function.Exact (mapBaseChange R A B) (map R A B B) :=
  SetLike.ext_iff.mp (range_mapBaseChange R A B).symm

end ExactSequence

section MvPolynomial

/-- The relative differential module of a polynomial algebra `R[Ïƒ]` is the free module generated by
`{ dx | x âˆˆ Ïƒ }`. Also see `KaehlerDifferential.mvPolynomialBasis`. -/
def KaehlerDifferential.mvPolynomialEquiv (Ïƒ : Type*) :
    Î©[MvPolynomial Ïƒ Râ„R] â‰ƒâ‚—[MvPolynomial Ïƒ R] Ïƒ â†’â‚€ MvPolynomial Ïƒ R where
  __ := (MvPolynomial.mkDerivation _ (Finsupp.single Â· 1)).liftKaehlerDifferential
  invFun := Finsupp.total Ïƒ _ _ (fun x â†¦ D _ _ (MvPolynomial.X x))
  right_inv := by
    intro x
    induction' x using Finsupp.induction_linear with _ _ _ _ a b
    Â· simp only [AddHom.toFun_eq_coe, LinearMap.coe_toAddHom]; rw [map_zero, map_zero]
    Â· simp only [AddHom.toFun_eq_coe, LinearMap.coe_toAddHom, map_add] at *; simp only [*]
    Â· simp [LinearMap.map_smul, -map_smul]
  left_inv := by
    intro x
    obtain âŸ¨x, rflâŸ© := total_surjective _ _ x
    induction' x using Finsupp.induction_linear with _ _ _ _ a b
    Â· simp only [AddHom.toFun_eq_coe, LinearMap.coe_toAddHom]; rw [map_zero, map_zero, map_zero]
    Â· simp only [map_add, AddHom.toFun_eq_coe, LinearMap.coe_toAddHom] at *; simp only [*]
    Â· simp only [AddHom.toFun_eq_coe, LinearMap.coe_toAddHom, Finsupp.total_single,
        LinearMap.map_smul, Derivation.liftKaehlerDifferential_comp_D]
      congr 1
      induction a using MvPolynomial.induction_on
      Â· simp only [MvPolynomial.derivation_C, map_zero]
      Â· simp only [map_add, *]
      Â· simp [*]

/-- `{ dx | x âˆˆ Ïƒ }` forms a basis of the relative differential module
of a polynomial algebra `R[Ïƒ]`. -/
def KaehlerDifferential.mvPolynomialBasis (Ïƒ) :
    Basis Ïƒ (MvPolynomial Ïƒ R) (Î©[MvPolynomial Ïƒ Râ„R]) :=
  âŸ¨mvPolynomialEquiv R ÏƒâŸ©

lemma KaehlerDifferential.mvPolynomialBasis_repr_comp_D (Ïƒ) :
    (mvPolynomialBasis R Ïƒ).repr.toLinearMap.compDer (D _ _) =
      MvPolynomial.mkDerivation _ (Finsupp.single Â· 1) :=
  Derivation.liftKaehlerDifferential_comp _

lemma KaehlerDifferential.mvPolynomialBasis_repr_D (Ïƒ) (x) :
    (mvPolynomialBasis R Ïƒ).repr (D _ _ x) =
      MvPolynomial.mkDerivation R (Finsupp.single Â· (1 : MvPolynomial Ïƒ R)) x :=
  Derivation.congr_fun (mvPolynomialBasis_repr_comp_D R Ïƒ) x

@[simp]
lemma KaehlerDifferential.mvPolynomialBasis_repr_D_X (Ïƒ) (i) :
    (mvPolynomialBasis R Ïƒ).repr (D _ _ (.X i)) = Finsupp.single i 1 := by
  simp [mvPolynomialBasis_repr_D]

@[simp]
lemma KaehlerDifferential.mvPolynomialBasis_repr_apply (Ïƒ) (x) (i) :
    (mvPolynomialBasis R Ïƒ).repr (D _ _ x) i = MvPolynomial.pderiv i x := by
  classical
  suffices ((Finsupp.lapply i).comp
    (mvPolynomialBasis R Ïƒ).repr.toLinearMap).compDer (D _ _) = MvPolynomial.pderiv i by
    rw [â† this]; rfl
  apply MvPolynomial.derivation_ext
  intro j
  simp [Finsupp.single_apply, Pi.single_apply]

set_option backward.isDefEq.lazyWhnfCore false in -- See https://github.com/leanprover-community/mathlib4/issues/12534
lemma KaehlerDifferential.mvPolynomialBasis_repr_symm_single (Ïƒ) (i) (x) :
    (mvPolynomialBasis R Ïƒ).repr.symm (Finsupp.single i x) = x â€¢ D R (MvPolynomial Ïƒ R) (.X i) := by
  apply (mvPolynomialBasis R Ïƒ).repr.injective; simp [LinearEquiv.map_smul, -map_smul]

set_option backward.isDefEq.lazyWhnfCore false in -- See https://github.com/leanprover-community/mathlib4/issues/12534
@[simp]
lemma KaehlerDifferential.mvPolynomialBasis_apply (Ïƒ) (i) :
    mvPolynomialBasis R Ïƒ i = D R (MvPolynomial Ïƒ R) (.X i) :=
  (mvPolynomialBasis_repr_symm_single R Ïƒ i 1).trans (one_smul _ _)

instance (Ïƒ) : Module.Free (MvPolynomial Ïƒ R) (Î©[MvPolynomial Ïƒ Râ„R]) :=
  .of_basis (KaehlerDifferential.mvPolynomialBasis R Ïƒ)

end MvPolynomial

section Polynomial

open Polynomial

lemma KaehlerDifferential.polynomial_D_apply (P : R[X]) :
    D R R[X] P = derivative P â€¢ D R R[X] X := by
  rw [â† aeval_X_left_apply P, (D R R[X]).map_aeval, aeval_X_left_apply, aeval_X_left_apply]

/-- The relative differential module of the univariate polynomial algebra `R[X]` is isomorphic to
  `R[X]` as an `R[X]`-module. -/
def KaehlerDifferential.polynomialEquiv : Î©[R[X]â„R] â‰ƒâ‚—[R[X]] R[X] where
  __ := derivative'.liftKaehlerDifferential
  invFun := (Algebra.lsmul R R _).toLinearMap.flip (D R R[X] X)
  left_inv := by
    intro x
    obtain âŸ¨x, rflâŸ© := total_surjective _ _ x
    induction' x using Finsupp.induction_linear with x y hx hy x y
    Â· simp
    Â· simp only [map_add, AddHom.toFun_eq_coe, LinearMap.coe_toAddHom, LinearMap.flip_apply,
        AlgHom.toLinearMap_apply, lsmul_coe] at *; simp only [*]
    Â· simp [polynomial_D_apply _ x]
  right_inv x := by simp

lemma KaehlerDifferential.polynomialEquiv_comp_D :
    (polynomialEquiv R).compDer (D R R[X]) = derivative' :=
  Derivation.liftKaehlerDifferential_comp _

@[simp]
lemma KaehlerDifferential.polynomialEquiv_D (P) :
    polynomialEquiv R (D R R[X] P) = derivative P :=
  Derivation.congr_fun (polynomialEquiv_comp_D R) P

@[simp]
lemma KaehlerDifferential.polynomialEquiv_symm (P) :
    (polynomialEquiv R).symm P = P â€¢ D R R[X] X := rfl

end Polynomial

end KaehlerDifferential
