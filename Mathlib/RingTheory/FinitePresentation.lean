/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin
-/
import Mathlib.RingTheory.FiniteType
import Mathlib.RingTheory.MvPolynomial.Tower
import Mathlib.RingTheory.Ideal.QuotientOperations

#align_import ring_theory.finite_presentation from "leanprover-community/mathlib"@"da420a8c6dd5bdfb85c4ced85c34388f633bc6ff"

/-!
# Finiteness conditions in commutative algebra

In this file we define several notions of finiteness that are common in commutative algebra.

## Main declarations

- `Module.Finite`, `RingHom.Finite`, `AlgHom.Finite`
  all of these express that some object is finitely generated *as module* over some base ring.
- `Algebra.FiniteType`, `RingHom.FiniteType`, `AlgHom.FiniteType`
  all of these express that some object is finitely generated *as algebra* over some base ring.
- `Algebra.FinitePresentation`, `RingHom.FinitePresentation`, `AlgHom.FinitePresentation`
  all of these express that some object is finitely presented *as algebra* over some base ring.

-/

set_option autoImplicit true

open Function (Surjective)

open BigOperators Polynomial

section ModuleAndAlgebra

universe wâ‚ wâ‚‚ wâ‚ƒ

-- Porting Note : `M, N` is never used
variable (R : Type wâ‚) (A : Type wâ‚‚) (B : Type wâ‚ƒ)

/-- An algebra over a commutative semiring is `Algebra.FinitePresentation` if it is the quotient of
a polynomial ring in `n` variables by a finitely generated ideal. -/
def Algebra.FinitePresentation [CommSemiring R] [Semiring A] [Algebra R A] : Prop :=
  âˆƒ (n : â„•) (f : MvPolynomial (Fin n) R â†’â‚[R] A), Surjective f âˆ§ f.toRingHom.ker.FG
#align algebra.finite_presentation Algebra.FinitePresentation

namespace Algebra

variable [CommRing R] [CommRing A] [Algebra R A] [CommRing B] [Algebra R B]

namespace FiniteType

variable {R A B}

/-- A finitely presented algebra is of finite type. -/
theorem of_finitePresentation : FinitePresentation R A â†’ FiniteType R A := by
  rintro âŸ¨n, f, hfâŸ©
  -- âŠ¢ FiniteType R A
  apply FiniteType.iff_quotient_mvPolynomial''.2
  -- âŠ¢ âˆƒ n f, Surjective â†‘f
  exact âŸ¨n, f, hf.1âŸ©
  -- ğŸ‰ no goals
#align algebra.finite_type.of_finite_presentation Algebra.FiniteType.of_finitePresentation

end FiniteType

namespace FinitePresentation

variable {R A B}

/-- An algebra over a Noetherian ring is finitely generated if and only if it is finitely
presented. -/
theorem of_finiteType [IsNoetherianRing R] : FiniteType R A â†” FinitePresentation R A := by
  refine' âŸ¨fun h => _, Algebra.FiniteType.of_finitePresentationâŸ©
  -- âŠ¢ FinitePresentation R A
  obtain âŸ¨n, f, hfâŸ© := Algebra.FiniteType.iff_quotient_mvPolynomial''.1 h
  -- âŠ¢ FinitePresentation R A
  refine' âŸ¨n, f, hf, _âŸ©
  -- âŠ¢ Ideal.FG (RingHom.ker â†‘f)
  have hnoet : IsNoetherianRing (MvPolynomial (Fin n) R) := by infer_instance
  -- âŠ¢ Ideal.FG (RingHom.ker â†‘f)
  -- Porting note: rewrote code to help typeclass inference
  rw [isNoetherianRing_iff] at hnoet
  -- âŠ¢ Ideal.FG (RingHom.ker â†‘f)
  letI : Module (MvPolynomial (Fin n) R) (MvPolynomial (Fin n) R) := Semiring.toModule
  -- âŠ¢ Ideal.FG (RingHom.ker â†‘f)
  have := hnoet.noetherian (RingHom.ker f.toRingHom)
  -- âŠ¢ Ideal.FG (RingHom.ker â†‘f)
  convert this
  -- ğŸ‰ no goals
#align algebra.finite_presentation.of_finite_type Algebra.FinitePresentation.of_finiteType

/-- If `e : A â‰ƒâ‚[R] B` and `A` is finitely presented, then so is `B`. -/
theorem equiv (hfp : FinitePresentation R A) (e : A â‰ƒâ‚[R] B) : FinitePresentation R B := by
  obtain âŸ¨n, f, hfâŸ© := hfp
  -- âŠ¢ FinitePresentation R B
  use n, AlgHom.comp (â†‘e) f
  -- âŠ¢ Surjective â†‘(AlgHom.comp (â†‘e) f) âˆ§ Ideal.FG (RingHom.ker â†‘(AlgHom.comp (â†‘e)  â€¦
  constructor
  -- âŠ¢ Surjective â†‘(AlgHom.comp (â†‘e) f)
  Â· rw [AlgHom.coe_comp]
    -- âŠ¢ Surjective (â†‘â†‘e âˆ˜ â†‘f)
    exact Function.Surjective.comp e.surjective hf.1
    -- ğŸ‰ no goals
  suffices hker : (RingHom.ker (AlgHom.comp (e : A â†’â‚[R] B) f).toRingHom) = RingHom.ker f.toRingHom
  -- âŠ¢ Ideal.FG (RingHom.ker â†‘(AlgHom.comp (â†‘e) f))
  Â· rw [hker]
    -- âŠ¢ Ideal.FG (RingHom.ker â†‘f)
    exact hf.2
    -- ğŸ‰ no goals
  Â· have hco : (AlgHom.comp (e : A â†’â‚[R] B) f).toRingHom = RingHom.comp (e.toRingEquiv : A â‰ƒ+* B)
      f.toRingHom := by
      have h : (AlgHom.comp (e : A â†’â‚[R] B) f).toRingHom =
        e.toAlgHom.toRingHom.comp f.toRingHom := rfl
      have h1 : â†‘e.toRingEquiv = e.toAlgHom.toRingHom := rfl
      rw [h, h1]
    rw [RingHom.ker_eq_comap_bot, hco, â† Ideal.comap_comap, â† RingHom.ker_eq_comap_bot,
      RingHom.ker_coe_equiv (AlgEquiv.toRingEquiv e), RingHom.ker_eq_comap_bot]
#align algebra.finite_presentation.equiv Algebra.FinitePresentation.equiv

variable (R)

/-- The ring of polynomials in finitely many variables is finitely presented. -/
protected theorem mvPolynomial (Î¹ : Type u_2) [Finite Î¹] :
    FinitePresentation R (MvPolynomial Î¹ R) := by
  cases nonempty_fintype Î¹
  -- âŠ¢ FinitePresentation R (MvPolynomial Î¹ R)
  let eqv := (MvPolynomial.renameEquiv R <| Fintype.equivFin Î¹).symm
  -- âŠ¢ FinitePresentation R (MvPolynomial Î¹ R)
  exact
    âŸ¨Fintype.card Î¹, eqv, eqv.surjective,
      ((RingHom.injective_iff_ker_eq_bot _).1 eqv.injective).symm â–¸ Submodule.fg_botâŸ©
#align algebra.finite_presentation.mv_polynomial Algebra.FinitePresentation.mvPolynomial

/-- `R` is finitely presented as `R`-algebra. -/
theorem self : FinitePresentation R R :=
  -- Porting note: replaced `PEmpty` with `Empty`
  equiv (FinitePresentation.mvPolynomial R Empty) (MvPolynomial.isEmptyAlgEquiv R Empty)
#align algebra.finite_presentation.self Algebra.FinitePresentation.self

/-- `R[X]` is finitely presented as `R`-algebra. -/
theorem polynomial : FinitePresentation R R[X] :=
  -- Porting note: replaced `PUnit` with `Unit`
  equiv (FinitePresentation.mvPolynomial R Unit) (MvPolynomial.pUnitAlgEquiv R)
#align algebra.finite_presentation.polynomial Algebra.FinitePresentation.polynomial

variable {R}

/-- The quotient of a finitely presented algebra by a finitely generated ideal is finitely
presented. -/
protected theorem quotient {I : Ideal A} (h : I.FG) (hfp : FinitePresentation R A) :
    FinitePresentation R (A â§¸ I) := by
  obtain âŸ¨n, f, hfâŸ© := hfp
  -- âŠ¢ FinitePresentation R (A â§¸ I)
  refine' âŸ¨n, (Ideal.Quotient.mkâ‚ R I).comp f, _, _âŸ©
  -- âŠ¢ Surjective â†‘(AlgHom.comp (Ideal.Quotient.mkâ‚ R I) f)
  Â· exact (Ideal.Quotient.mkâ‚_surjective R I).comp hf.1
    -- ğŸ‰ no goals
  Â· refine' Ideal.fg_ker_comp _ _ hf.2 _ hf.1
    -- âŠ¢ Ideal.FG (RingHom.ker â†‘(Ideal.Quotient.mkâ‚ R I))
    simp [h]
    -- ğŸ‰ no goals
#align algebra.finite_presentation.quotient Algebra.FinitePresentation.quotient

/-- If `f : A â†’â‚[R] B` is surjective with finitely generated kernel and `A` is finitely presented,
then so is `B`. -/
theorem of_surjective {f : A â†’â‚[R] B} (hf : Function.Surjective f) (hker : f.toRingHom.ker.FG)
    (hfp : FinitePresentation R A) : FinitePresentation R B :=
  equiv (hfp.quotient hker) (Ideal.quotientKerAlgEquivOfSurjective hf)
#align algebra.finite_presentation.of_surjective Algebra.FinitePresentation.of_surjective

theorem iff :
    FinitePresentation R A â†”
      âˆƒ (n : _) (I : Ideal (MvPolynomial (Fin n) R)) (_ : (_ â§¸ I) â‰ƒâ‚[R] A), I.FG := by
  constructor
  -- âŠ¢ FinitePresentation R A â†’ âˆƒ n I x, Ideal.FG I
  Â· rintro âŸ¨n, f, hfâŸ©
    -- âŠ¢ âˆƒ n I x, Ideal.FG I
    exact âŸ¨n, RingHom.ker f.toRingHom, Ideal.quotientKerAlgEquivOfSurjective hf.1, hf.2âŸ©
    -- ğŸ‰ no goals
  Â· rintro âŸ¨n, I, e, hfgâŸ©
    -- âŠ¢ FinitePresentation R A
    exact equiv ((FinitePresentation.mvPolynomial R _).quotient hfg) e
    -- ğŸ‰ no goals
#align algebra.finite_presentation.iff Algebra.FinitePresentation.iff

/-- An algebra is finitely presented if and only if it is a quotient of a polynomial ring whose
variables are indexed by a fintype by a finitely generated ideal. -/
theorem iff_quotient_mvPolynomial' :
    FinitePresentation R A â†”
      âˆƒ (Î¹ : Type*) (_ : Fintype Î¹) (f : MvPolynomial Î¹ R â†’â‚[R] A),
        Surjective f âˆ§ f.toRingHom.ker.FG := by
  constructor
  -- âŠ¢ FinitePresentation R A â†’ âˆƒ Î¹ x f, Surjective â†‘f âˆ§ Ideal.FG (RingHom.ker â†‘f)
  Â· rintro âŸ¨n, f, hfs, hfkâŸ©
    -- âŠ¢ âˆƒ Î¹ x f, Surjective â†‘f âˆ§ Ideal.FG (RingHom.ker â†‘f)
    set ulift_var := MvPolynomial.renameEquiv R Equiv.ulift
    -- âŠ¢ âˆƒ Î¹ x f, Surjective â†‘f âˆ§ Ideal.FG (RingHom.ker â†‘f)
    refine'
      âŸ¨ULift (Fin n), inferInstance, f.comp ulift_var.toAlgHom, hfs.comp ulift_var.surjective,
        Ideal.fg_ker_comp _ _ _ hfk ulift_var.surjectiveâŸ©
    erw [RingHom.ker_coe_equiv ulift_var.toRingEquiv]
    -- âŠ¢ Ideal.FG âŠ¥
    exact Submodule.fg_bot
    -- ğŸ‰ no goals
    -- Porting note: was
    -- convert Submodule.fg_bot
    -- exact RingHom.ker_coe_equiv ulift_var.toRingEquiv
  Â· rintro âŸ¨Î¹, hfintype, f, hfâŸ©
    -- âŠ¢ FinitePresentation R A
    have equiv := MvPolynomial.renameEquiv R (Fintype.equivFin Î¹)
    -- âŠ¢ FinitePresentation R A
    refine'
      âŸ¨Fintype.card Î¹, f.comp equiv.symm, hf.1.comp (AlgEquiv.symm equiv).surjective,
        Ideal.fg_ker_comp _ f _ hf.2 equiv.symm.surjectiveâŸ©
    erw [RingHom.ker_coe_equiv equiv.symm.toRingEquiv]
    -- âŠ¢ Ideal.FG âŠ¥
    exact Submodule.fg_bot
    -- ğŸ‰ no goals
    -- Porting note: was
    -- convert Submodule.fg_bot
    -- exact RingHom.ker_coe_equiv equiv.symm.toRingEquiv
#align algebra.finite_presentation.iff_quotient_mv_polynomial' Algebra.FinitePresentation.iff_quotient_mvPolynomial'

-- Porting note : make universe level explicit to ensure `Î¹, Î¹'` has the same universe level
/-- If `A` is a finitely presented `R`-algebra, then `MvPolynomial (Fin n) A` is finitely presented
as `R`-algebra. -/
theorem mvPolynomial_of_finitePresentation (hfp : FinitePresentation.{wâ‚, wâ‚‚} R A)
    (Î¹ : Type v) [Finite Î¹] :
    FinitePresentation.{wâ‚, max v wâ‚‚} R (MvPolynomial Î¹ A) := by
  rw [iff_quotient_mvPolynomial'] at hfp âŠ¢
  -- âŠ¢ âˆƒ Î¹_1 x f, Surjective â†‘f âˆ§ Ideal.FG (RingHom.ker â†‘f)
  classical
  -- Porting note : use the same universe level
  obtain âŸ¨(Î¹' : Type v), _, f, hf_surj, hf_kerâŸ© := hfp
  let g := (MvPolynomial.mapAlgHom f).comp (MvPolynomial.sumAlgEquiv R Î¹ Î¹').toAlgHom
  cases nonempty_fintype (Sum Î¹ Î¹')
  refine
    âŸ¨Sum Î¹ Î¹', by infer_instance, g,
      (MvPolynomial.map_surjective f.toRingHom hf_surj).comp (AlgEquiv.surjective _),
      Ideal.fg_ker_comp _ _ ?_ ?_ (AlgEquiv.surjective _)âŸ©
  Â· erw [RingHom.ker_coe_equiv (MvPolynomial.sumAlgEquiv R Î¹ Î¹').toRingEquiv]
    exact Submodule.fg_bot
    -- Porting note: was
    -- convert Submodule.fg_bot
    -- exact RingHom.ker_coe_equiv (MvPolynomial.sumAlgEquiv R Î¹ Î¹').toRingEquiv
  Â· rw [AlgHom.toRingHom_eq_coe, MvPolynomial.mapAlgHom_coe_ringHom, MvPolynomial.ker_map]
    exact hf_ker.map MvPolynomial.C
#align algebra.finite_presentation.mv_polynomial_of_finite_presentation Algebra.FinitePresentation.mvPolynomial_of_finitePresentation

/-- If `A` is an `R`-algebra and `S` is an `A`-algebra, both finitely presented, then `S` is
  finitely presented as `R`-algebra. -/
theorem trans [Algebra A B] [IsScalarTower R A B] (hfpA : FinitePresentation R A)
    (hfpB : FinitePresentation A B) : FinitePresentation R B := by
  obtain âŸ¨n, I, e, hfgâŸ© := iff.1 hfpB
  -- âŠ¢ FinitePresentation R B
  exact equiv ((mvPolynomial_of_finitePresentation hfpA _).quotient hfg) (e.restrictScalars R)
  -- ğŸ‰ no goals
#align algebra.finite_presentation.trans Algebra.FinitePresentation.trans

open MvPolynomial

-- We follow the proof of https://stacks.math.columbia.edu/tag/0561
-- TODO: extract out helper lemmas and tidy proof.
theorem of_restrict_scalars_finitePresentation [Algebra A B] [IsScalarTower R A B]
    (hRB : FinitePresentation.{wâ‚, wâ‚ƒ} R B) [hRA : FiniteType R A] :
    FinitePresentation.{wâ‚‚, wâ‚ƒ} A B := by
  classical
  obtain âŸ¨n, f, hf, s, hsâŸ© := hRB
  letI RX := MvPolynomial (Fin n) R
  letI AX := MvPolynomial (Fin n) A
  refine' âŸ¨n, MvPolynomial.aeval (f âˆ˜ X), _, _âŸ©
  Â· rw [â† Algebra.range_top_iff_surjective, â† Algebra.adjoin_range_eq_range_aeval,
      Set.range_comp f MvPolynomial.X, eq_top_iff, â† @adjoin_adjoin_of_tower R A B,
      adjoin_image, adjoin_range_X, Algebra.map_top, (Algebra.range_top_iff_surjective _).mpr hf]
    exact fun {x} => subset_adjoin âŸ¨âŸ©
  Â· obtain âŸ¨t, htâŸ© := hRA.out
    have := fun i : t => hf (algebraMap A B i)
    choose t' ht' using this
    have ht'' : Algebra.adjoin R (algebraMap A AX '' t âˆª Set.range (X : _ â†’ AX)) = âŠ¤ := by
      rw [adjoin_union_eq_adjoin_adjoin, â† Subalgebra.restrictScalars_top R (A := AX)
        (S := { x // x âˆˆ adjoin R ((algebraMap A AX) '' t) })]
      refine congrArg (Subalgebra.restrictScalars R) ?_
      congr 1
      rw [adjoin_algebraMap, ht]
      apply Subalgebra.restrictScalars_injective R
      rw [â† adjoin_restrictScalars, adjoin_range_X, Subalgebra.restrictScalars_top,
        Subalgebra.restrictScalars_top]
    letI g : t â†’ AX := fun x => MvPolynomial.C (x : A) - map (algebraMap R A) (t' x)
    refine' âŸ¨s.image (map (algebraMap R A)) âˆª t.attach.image g, _âŸ©
    rw [Finset.coe_union, Finset.coe_image, Finset.coe_image, Finset.attach_eq_univ,
      Finset.coe_univ, Set.image_univ]
    let sâ‚€ := (MvPolynomial.map (algebraMap R A)) '' s âˆª Set.range g
    let I := RingHom.ker (MvPolynomial.aeval (R := A) (f âˆ˜ MvPolynomial.X))
    change Ideal.span sâ‚€ = I
    have leI : Ideal.span ((MvPolynomial.map (algebraMap R A)) '' s âˆª Set.range g) â‰¤
      RingHom.ker (MvPolynomial.aeval (R := A) (f âˆ˜ MvPolynomial.X)) := by
      rw [Ideal.span_le]
      rintro _ (âŸ¨x, hx, rflâŸ© | âŸ¨âŸ¨x, hxâŸ©, rflâŸ©) <;>
      erw [RingHom.mem_ker]
      Â· rw [MvPolynomial.aeval_map_algebraMap (R := R) (A := A), â† aeval_unique]
        have := Ideal.subset_span hx
        rwa [hs] at this
      Â· rw [map_sub, MvPolynomial.aeval_map_algebraMap, â† aeval_unique,
          MvPolynomial.aeval_C, ht', Subtype.coe_mk, sub_self]
    apply leI.antisymm
    intro x hx
    rw [RingHom.mem_ker] at hx
    let sâ‚€ := (MvPolynomial.map (algebraMap R A)) '' â†‘s âˆª Set.range g
    change x âˆˆ Ideal.span sâ‚€
    have : x âˆˆ (MvPolynomial.map (algebraMap R A) : _ â†’+* AX).range.toAddSubmonoid âŠ”
      (Ideal.span sâ‚€).toAddSubmonoid := by
      have : x âˆˆ (âŠ¤ : Subalgebra R AX) := trivial
      rw [â† ht''] at this
      refine adjoin_induction this ?_ ?_ ?_ ?_
      Â· rintro _ (âŸ¨x, hx, rflâŸ© | âŸ¨i, rflâŸ©)
        Â· rw [algebraMap_eq, â† sub_add_cancel (MvPolynomial.C x)
            (map (algebraMap R A) (t' âŸ¨x, hxâŸ©)), add_comm]
          apply AddSubmonoid.add_mem_sup
          Â· exact Set.mem_range_self _
          Â· apply Ideal.subset_span
            apply Set.mem_union_right
            exact Set.mem_range_self _
        Â· apply AddSubmonoid.mem_sup_left
          exact âŸ¨X i, map_X _ _âŸ©
      Â· intro r
        apply AddSubmonoid.mem_sup_left
        exact âŸ¨C r, map_C _ _âŸ©
      Â· intro _ _ hâ‚ hâ‚‚
        exact add_mem hâ‚ hâ‚‚
      Â· intro xâ‚ xâ‚‚ hâ‚ hâ‚‚
        obtain âŸ¨_, âŸ¨pâ‚, rflâŸ©, qâ‚, hqâ‚, rflâŸ© := AddSubmonoid.mem_sup.mp hâ‚
        obtain âŸ¨_, âŸ¨pâ‚‚, rflâŸ©, qâ‚‚, hqâ‚‚, rflâŸ© := AddSubmonoid.mem_sup.mp hâ‚‚
        rw [add_mul, mul_add, add_assoc, â† map_mul]
        apply AddSubmonoid.add_mem_sup
        Â· exact Set.mem_range_self _
        Â· refine' add_mem (Ideal.mul_mem_left _ _ hqâ‚‚) (Ideal.mul_mem_right _ _ hqâ‚)
    obtain âŸ¨_, âŸ¨p, rflâŸ©, q, hq, rflâŸ© := AddSubmonoid.mem_sup.mp this
    rw [map_add, aeval_map_algebraMap, â† aeval_unique, show MvPolynomial.aeval (f âˆ˜ X) q = 0
      from leI hq, add_zero] at hx
    suffices Ideal.span (s : Set RX) â‰¤ (Ideal.span sâ‚€).comap (MvPolynomial.map <| algebraMap R A) by
      refine' add_mem _ hq
      rw [hs] at this
      exact this hx
    rw [Ideal.span_le]
    intro x hx
    apply Ideal.subset_span
    apply Set.mem_union_left
    exact Set.mem_image_of_mem _ hx
#align algebra.finite_presentation.of_restrict_scalars_finite_presentation Algebra.FinitePresentation.of_restrict_scalars_finitePresentation

-- TODO: extract out helper lemmas and tidy proof.
/-- This is used to prove the strictly stronger `ker_fg_of_surjective`. Use it instead. -/
theorem ker_fg_of_mvPolynomial {n : â„•} (f : MvPolynomial (Fin n) R â†’â‚[R] A)
    (hf : Function.Surjective f) (hfp : FinitePresentation R A) : f.toRingHom.ker.FG := by
  classical
    obtain âŸ¨m, f', hf', s, hsâŸ© := hfp
    let RXn := MvPolynomial (Fin n) R
    let RXm := MvPolynomial (Fin m) R
    have := fun i : Fin n => hf' (f <| X i)
    choose g hg using this
    have := fun i : Fin m => hf (f' <| X i)
    choose h hh using this
    let aeval_h : RXm â†’â‚[R] RXn := aeval h
    let g' : Fin n â†’ RXn := fun i => X i - aeval_h (g i)
    refine' âŸ¨Finset.univ.image g' âˆª s.image aeval_h, _âŸ©
    simp only [Finset.coe_image, Finset.coe_union, Finset.coe_univ, Set.image_univ]
    have hh' : âˆ€ x, f (aeval_h x) = f' x := by
      intro x
      rw [â† f.coe_toRingHom, map_aeval]
      simp_rw [AlgHom.coe_toRingHom, hh]
      rw [AlgHom.comp_algebraMap, â† aeval_eq_evalâ‚‚Hom,
        -- Porting note: added line below
        â† funext fun i => Function.comp_apply (f := â†‘f') (g := MvPolynomial.X),
        â† aeval_unique]
    let s' := Set.range g' âˆª aeval_h '' s
    have leI : Ideal.span s' â‰¤ RingHom.ker f.toRingHom := by
      rw [Ideal.span_le]
      rintro _ (âŸ¨i, rflâŸ© | âŸ¨x, hx, rflâŸ©)
      Â· change f (g' i) = 0
        rw [map_sub, â† hg, hh', sub_self]
      Â· change f (aeval_h x) = 0
        rw [hh']
        change x âˆˆ RingHom.ker f'.toRingHom
        rw [â† hs]
        exact Ideal.subset_span hx
    apply leI.antisymm
    intro x hx
    have : x âˆˆ aeval_h.range.toAddSubmonoid âŠ” (Ideal.span s').toAddSubmonoid := by
      have : x âˆˆ adjoin R (Set.range X : Set RXn) := by
        rw [adjoin_range_X]
        trivial
      refine adjoin_induction this ?_ ?_ ?_ ?_
      Â· rintro _ âŸ¨i, rflâŸ©
        rw [â† sub_add_cancel (X i) (aeval h (g i)), add_comm]
        apply AddSubmonoid.add_mem_sup
        Â· exact Set.mem_range_self _
        Â· apply Submodule.subset_span
          apply Set.mem_union_left
          exact Set.mem_range_self _
      Â· intro r
        apply AddSubmonoid.mem_sup_left
        exact âŸ¨C r, aeval_C _ _âŸ©
      Â· intro _ _ hâ‚ hâ‚‚
        exact add_mem hâ‚ hâ‚‚
      Â· intro pâ‚ pâ‚‚ hâ‚ hâ‚‚
        obtain âŸ¨_, âŸ¨xâ‚, rflâŸ©, yâ‚, hyâ‚, rflâŸ© := AddSubmonoid.mem_sup.mp hâ‚
        obtain âŸ¨_, âŸ¨xâ‚‚, rflâŸ©, yâ‚‚, hyâ‚‚, rflâŸ© := AddSubmonoid.mem_sup.mp hâ‚‚
        rw [mul_add, add_mul, add_assoc, â† map_mul]
        apply AddSubmonoid.add_mem_sup
        Â· exact Set.mem_range_self _
        Â· exact add_mem (Ideal.mul_mem_right _ _ hyâ‚) (Ideal.mul_mem_left _ _ hyâ‚‚)
    obtain âŸ¨_, âŸ¨x, rflâŸ©, y, hy, rflâŸ© := AddSubmonoid.mem_sup.mp this
    refine' add_mem _ hy
    simp only [RingHom.mem_ker, AlgHom.toRingHom_eq_coe, AlgHom.coe_toRingHom, map_add,
      show f y = 0 from leI hy, add_zero, hh'] at hx
    suffices Ideal.span (s : Set RXm) â‰¤ (Ideal.span s').comap aeval_h by
      apply this
      rwa [hs]
    rw [Ideal.span_le]
    intro x hx
    apply Submodule.subset_span
    apply Set.mem_union_right
    exact Set.mem_image_of_mem _ hx
#align algebra.finite_presentation.ker_fg_of_mv_polynomial Algebra.FinitePresentation.ker_fg_of_mvPolynomial

/-- If `f : A â†’â‚[R] B` is a surjection between finitely-presented `R`-algebras, then the kernel of
`f` is finitely generated. -/
theorem ker_fG_of_surjective (f : A â†’â‚[R] B) (hf : Function.Surjective f)
    (hRA : FinitePresentation R A) (hRB : FinitePresentation R B) : f.toRingHom.ker.FG := by
  obtain âŸ¨n, g, hg, _âŸ© := hRA
  -- âŠ¢ Ideal.FG (RingHom.ker â†‘f)
  convert (ker_fg_of_mvPolynomial (f.comp g) (hf.comp hg) hRB).map g.toRingHom
  -- âŠ¢ RingHom.ker â†‘f = Ideal.map (â†‘g) (RingHom.ker â†‘(AlgHom.comp f g))
  simp_rw [RingHom.ker_eq_comap_bot, AlgHom.toRingHom_eq_coe, AlgHom.comp_toRingHom]
  -- âŠ¢ Ideal.comap â†‘f âŠ¥ = Ideal.map (â†‘g) (Ideal.comap (RingHom.comp â†‘f â†‘g) âŠ¥)
  rw [â† Ideal.comap_comap, Ideal.map_comap_of_surjective (g : MvPolynomial (Fin n) R â†’+* A) hg]
  -- ğŸ‰ no goals
#align algebra.finite_presentation.ker_fg_of_surjective Algebra.FinitePresentation.ker_fG_of_surjective

end FinitePresentation

end Algebra

end ModuleAndAlgebra

namespace RingHom

variable {A B C : Type*} [CommRing A] [CommRing B] [CommRing C]

/-- A ring morphism `A â†’+* B` is of `RingHom.FinitePresentation` if `B` is finitely presented as
`A`-algebra. -/
def FinitePresentation (f : A â†’+* B) : Prop :=
  @Algebra.FinitePresentation A B _ _ f.toAlgebra
#align ring_hom.finite_presentation RingHom.FinitePresentation

namespace FiniteType

theorem of_finitePresentation {f : A â†’+* B} (hf : f.FinitePresentation) : f.FiniteType :=
  @Algebra.FiniteType.of_finitePresentation A B _ _ f.toAlgebra hf
#align ring_hom.finite_type.of_finite_presentation RingHom.FiniteType.of_finitePresentation

end FiniteType

namespace FinitePresentation

variable (A)

theorem id : FinitePresentation (RingHom.id A) :=
  Algebra.FinitePresentation.self A
#align ring_hom.finite_presentation.id RingHom.FinitePresentation.id

variable {A}

theorem comp_surjective {f : A â†’+* B} {g : B â†’+* C} (hf : f.FinitePresentation) (hg : Surjective g)
    (hker : g.ker.FG) : (g.comp f).FinitePresentation :=
  letI := f.toAlgebra
  letI := (g.comp f).toAlgebra
  Algebra.FinitePresentation.of_surjective
    (f :=
      { g with
        toFun := g
        commutes' := fun _ => rfl })
    hg hker hf
#align ring_hom.finite_presentation.comp_surjective RingHom.FinitePresentation.comp_surjective

theorem of_surjective (f : A â†’+* B) (hf : Surjective f) (hker : f.ker.FG) :
    f.FinitePresentation := by
  rw [â† f.comp_id]
  -- âŠ¢ FinitePresentation (comp f (RingHom.id A))
  exact (id A).comp_surjective hf hker
  -- ğŸ‰ no goals
#align ring_hom.finite_presentation.of_surjective RingHom.FinitePresentation.of_surjective

theorem of_finiteType [IsNoetherianRing A] {f : A â†’+* B} : f.FiniteType â†” f.FinitePresentation :=
  @Algebra.FinitePresentation.of_finiteType A B _ _ f.toAlgebra _
#align ring_hom.finite_presentation.of_finite_type RingHom.FinitePresentation.of_finiteType

theorem comp {g : B â†’+* C} {f : A â†’+* B} (hg : g.FinitePresentation) (hf : f.FinitePresentation) :
    (g.comp f).FinitePresentation :=
  -- Porting note : specify `Algebra` instances to get `SMul`
  letI ins1 := RingHom.toAlgebra f
  letI ins2 := RingHom.toAlgebra g
  letI ins3 := RingHom.toAlgebra (g.comp f)
  letI ins4 : IsScalarTower A B C :=
    { smul_assoc := fun a b c => by simp [Algebra.smul_def, mul_assoc]; rfl }
                                    -- âŠ¢ â†‘(algebraMap B C) (â†‘(algebraMap A B) a) * (â†‘(algebraMap B C) b * c) = â†‘(alge â€¦
                                                                        -- ğŸ‰ no goals
  Algebra.FinitePresentation.trans hf hg
#align ring_hom.finite_presentation.comp RingHom.FinitePresentation.comp

theorem of_comp_finiteType (f : A â†’+* B) {g : B â†’+* C} (hg : (g.comp f).FinitePresentation)
    (hf : f.FiniteType) : g.FinitePresentation :=
  -- Porting note : need to specify some instances
  letI ins1 := RingHom.toAlgebra f
  letI ins2 := RingHom.toAlgebra g
  letI ins3 := RingHom.toAlgebra (g.comp f)
  letI ins4 : IsScalarTower A B C :=
    { smul_assoc := fun a b c => by simp [Algebra.smul_def, mul_assoc]; rfl }
                                    -- âŠ¢ â†‘(algebraMap B C) (â†‘(algebraMap A B) a) * (â†‘(algebraMap B C) b * c) = â†‘(alge â€¦
                                                                        -- ğŸ‰ no goals
  Algebra.FinitePresentation.of_restrict_scalars_finitePresentation (hRA := hf) hg
#align ring_hom.finite_presentation.of_comp_finite_type RingHom.FinitePresentation.of_comp_finiteType

end FinitePresentation

end RingHom

namespace AlgHom

variable {R A B C : Type*} [CommRing R]

variable [CommRing A] [CommRing B] [CommRing C]

variable [Algebra R A] [Algebra R B] [Algebra R C]

/-- An algebra morphism `A â†’â‚[R] B` is of `AlgHom.FinitePresentation` if it is of finite
presentation as ring morphism. In other words, if `B` is finitely presented as `A`-algebra. -/
def FinitePresentation (f : A â†’â‚[R] B) : Prop :=
  f.toRingHom.FinitePresentation
#align alg_hom.finite_presentation AlgHom.FinitePresentation

namespace FiniteType

theorem of_finitePresentation {f : A â†’â‚[R] B} (hf : f.FinitePresentation) : f.FiniteType :=
  RingHom.FiniteType.of_finitePresentation hf
#align alg_hom.finite_type.of_finite_presentation AlgHom.FiniteType.of_finitePresentation

end FiniteType

namespace FinitePresentation

variable (R A)

theorem id : FinitePresentation (AlgHom.id R A) :=
  RingHom.FinitePresentation.id A
#align alg_hom.finite_presentation.id AlgHom.FinitePresentation.id

variable {R A}

theorem comp {g : B â†’â‚[R] C} {f : A â†’â‚[R] B} (hg : g.FinitePresentation)
    (hf : f.FinitePresentation) : (g.comp f).FinitePresentation :=
  RingHom.FinitePresentation.comp hg hf
#align alg_hom.finite_presentation.comp AlgHom.FinitePresentation.comp

theorem comp_surjective {f : A â†’â‚[R] B} {g : B â†’â‚[R] C} (hf : f.FinitePresentation)
    (hg : Surjective g) (hker : g.toRingHom.ker.FG) : (g.comp f).FinitePresentation :=
  RingHom.FinitePresentation.comp_surjective hf hg hker
#align alg_hom.finite_presentation.comp_surjective AlgHom.FinitePresentation.comp_surjective

theorem of_surjective (f : A â†’â‚[R] B) (hf : Surjective f) (hker : f.toRingHom.ker.FG) :
    f.FinitePresentation := by
  -- Porting note: added `convert`
  convert RingHom.FinitePresentation.of_surjective f hf hker
  -- ğŸ‰ no goals
#align alg_hom.finite_presentation.of_surjective AlgHom.FinitePresentation.of_surjective

theorem of_finiteType [IsNoetherianRing A] {f : A â†’â‚[R] B} : f.FiniteType â†” f.FinitePresentation :=
  RingHom.FinitePresentation.of_finiteType
#align alg_hom.finite_presentation.of_finite_type AlgHom.FinitePresentation.of_finiteType

nonrec theorem of_comp_finiteType (f : A â†’â‚[R] B) {g : B â†’â‚[R] C}
    (h : (g.comp f).FinitePresentation) (h' : f.FiniteType) : g.FinitePresentation :=
  h.of_comp_finiteType _ h'
#align alg_hom.finite_presentation.of_comp_finite_type AlgHom.FinitePresentation.of_comp_finiteType

end FinitePresentation

end AlgHom
