/-
Copyright (c) 2024 Jou Glasheen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jou Glasheen, Amelia Livingston, Jujian Zhang, Kevin Buzzard
-/
import Mathlib.RingTheory.DedekindDomain.Ideal
import Mathlib.RingTheory.IntegralRestrict
import Mathlib.RingTheory.Ideal.QuotientOperations
import Mathlib.FieldTheory.Cardinality

/-

# Frobenius elements

Frobenius elements in Galois groups.

## Statement of the theorem

Say `L/K` is a finite Galois extension of number fields, with integer rings `B/A`,
and say `Q` is a maximal ideal of `B` dividing `P` of `A`. This file contains the
construction of an element `Frob Q` in `Gal(L/K)`, and a proof that
modulo `Q` it raises elements to the power `q := |A/P|`.

More generally, our theory works in the "ABKL" setting, with `B/A` a finite extension of
Dedekind domains, and the corresponding extension `L/K` of fields of fractions is
assumed finite and Galois. Given `Q/P` a compatible pair of maximal ideals, under the
assumption that `A/P` is a finite field of size `q`, we construct an element `Frob_Q`
in `Gal(L/K)` and prove:

1) `Frob_Q ‚Ä¢ Q = Q`;
2) `x ^ q ‚â° Frob_Q x (mod Q)`.

Examples where these hypotheses hold are:

1) Finite Galois extensions of number fields and their integers or `S`-integers;
2) Finite Galois extensions of function fields over finite fields, and their `S`-integers for
   `S` a nonempty set of places;
3) Finite Galois extensions of finite fields L/K where B/A=L/K and Q/P=0/0 (recovering the
classical theory of Frobenius elements)

Note that if `Q` is ramified, there is more than one choice of `Frob_Q` which works;
for example if `L=‚Ñö(i)` and `K=‚Ñö` then both the identity and complex conjugation
work for `Frob Q` if `Q=(1+i)`, and `Frob` returns a random one (i.e. it's opaque; all we know
is that it satisfies the two properties).

## The construction

We follow a proof in a footnote of a book by Milne. **TODO** which book

The Galois orbit of `Q` consists of `Q` and possibly some other primes `Q'`. The unit group
`(B/Q)À£` is finite and hence cyclic; so by the Chinese Remainder Theorem we may choose y ‚àà B
which reduces to a generator mod Q and to 0 modulo all other Q' in the Galois orbit of Q.

The polynomial `F = ‚àè (X - œÉ y)`, the product running over `œÉ` in the Galois group, is in `B[X]`
and is Galois-stable so is in fact in `A[X]`. Hence if `Fbar` is `F mod Q`
then `Fbar` has coefficients in `A/P=ùîΩ_q` and thus `Fbar(y^q)=Fbar(y)^q=0`, meaning that `y^q`
is a root of `F` mod `Q` and thus congruent to `œÉ y mod Q` for some `œÉ`. We define `Frob Q` to
be this `œÉ`.

## The proof

We know `œÉ y ‚â° y ^ q ‚â† 0 mod Q`. Hence `œÉ y ‚àâ Q`, and thus `y ‚àâ œÉ‚Åª¬π Q`. But `y ‚àà Q'` for all nontrivial
conjugates `Q'` of `Q`, hence `œÉ‚Åª¬π Q = Q` and thus `Q` is `œÉ`-stable.

Hence `œÉ` induces a field automorphism of `B/Q` and we know it's `x ‚Ü¶ x^q` on a generator,
so it's `x ‚Ü¶ x^q` on everything.

## Note

This work started its life as Jou Glasheen's final project for Kevin Buzzard's
Formalising Mathematics 2024 course.

## TODO

Show that this applies to number fields and their rings of integers,
i.e. supply the finiteness typeclasses and descent hypothesis in this case.

-/

variable (A : Type*) [CommRing A] {B : Type*} [CommRing B] [Algebra A B]

-- PR #13294
variable {Œ± : Type*} in
instance Ideal.pointwiseMulSemiringAction
    [Monoid Œ±] [MulSemiringAction Œ± B] : MulSemiringAction Œ± (Ideal B) where
  smul a I := Ideal.map (MulSemiringAction.toRingHom _ _ a) I
  one_smul I :=
    congr_arg (I.map ¬∑) (RingHom.ext <| one_smul Œ±) |>.trans I.map_id
  mul_smul _a‚ÇÅ _a‚ÇÇ I :=
    congr_arg (I.map ¬∑) (RingHom.ext <| mul_smul _ _) |>.trans (I.map_map _ _).symm
  smul_one a := by simp only [Ideal.one_eq_top]; exact Ideal.map_top _
  smul_mul a I J := Ideal.map_mul _ I J
  smul_add a I J := Ideal.map_sup _ I J
  smul_zero a := Ideal.map_bot

-- should be in #13294?
variable {Œ± : Type*} in
lemma Ideal.map_eq_comap_symm [Group Œ±] [MulSemiringAction Œ± B] (J : Ideal B) (œÉ : Œ±) :
    œÉ ‚Ä¢ J = J.comap (MulSemiringAction.toRingHom _ _ œÉ‚Åª¬π) :=
  J.map_comap_of_equiv (MulSemiringAction.toRingEquiv Œ± B œÉ)

namespace ArithmeticFrobenius
/-

## Auxiliary variables

The noncomputable variables `g : (B ‚ß∏ Q)À£` (a generator),
`y : B` (congruent to `g` mod `Q` and to `0` mod all Galois conjugates of `Q`,
`F : B[X]` (the product of `X - œÉ y` as `œÉ` runs through the Galois group), and
`m : A[X]`, the descent of `F` to `A[X]` (it's Galois-stable).
-/

variable (Q : Ideal B) [Q.IsMaximal]

variable [Fintype (B ‚ß∏ Q)]

noncomputable abbrev g : (B ‚ß∏ Q)À£ := (IsCyclic.exists_monoid_generator (Œ± := (B ‚ß∏ Q)À£)).choose

lemma g_spec : ‚àÄ (z : (B ‚ß∏ Q)À£), z ‚àà Submonoid.powers (g Q) :=
  (IsCyclic.exists_monoid_generator (Œ± := (B ‚ß∏ Q)À£)).choose_spec

variable [Fintype (B ‚âÉ‚Çê[A] B)] [DecidableEq (Ideal B)]

/-- An element `y` of `B` exists, which is congruent to `b` mod `Q`
and to 0 mod all Galois conjugates of `Q` (if any).-/
lemma exists_y :
    ‚àÉ y : B, (y : B ‚ß∏ Q) = g Q ‚àß ‚àÄ Q' : Ideal B, Q' ‚àà MulAction.orbit (B ‚âÉ‚Çê[A] B) Q ‚Üí Q' ‚â† Q ‚Üí y ‚àà Q' := by
  let O : Set (Ideal B) := MulAction.orbit (B ‚âÉ‚Çê[A] B) Q
  have hO' : Finite (O : Type _) := Set.finite_range _
  have hmax (I : O) : Ideal.IsMaximal (I : Ideal B) := by
    rcases I with ‚ü®_, œÉ, rfl‚ü©
    convert Ideal.comap_isMaximal_of_surjective (K := Q) _ (AlgEquiv.surjective œÉ.symm)
    apply Ideal.map_eq_comap_symm
  have hPairwise : Pairwise fun (I : O) (J : O) ‚Ü¶ IsCoprime (I : Ideal B) J := fun x y h ‚Ü¶ ‚ü®1, 1, by
    simp only [Ideal.one_eq_top, Ideal.top_mul]
    exact Ideal.IsMaximal.coprime_of_ne (hmax x) (hmax y) <| mt Subtype.ext h‚ü©
  obtain ‚ü®y, hy‚ü© := Ideal.exists_forall_sub_mem_ideal (Œπ := O) hPairwise
    (fun J ‚Ü¶ if J = ‚ü®Q, 1, by simp‚ü© then (Ideal.Quotient.mk_surjective (g Q : B ‚ß∏ Q)).choose else 0)
  refine ‚ü®y, ?_, ?_‚ü©
  ¬∑ specialize hy ‚ü®Q, 1, by simp‚ü©
    simp at hy
    rw [‚Üê (Ideal.Quotient.mk_surjective (g Q : B ‚ß∏ Q)).choose_spec]
    exact
      (Ideal.Quotient.mk_eq_mk_iff_sub_mem y
        (Ideal.Quotient.mk_surjective (I := Q) (g Q)).choose).mpr hy
  ¬∑ rintro Q' ‚ü®œÉ, rfl‚ü© hQ'
    specialize hy ‚ü®œÉ ‚Ä¢ Q, œÉ, rfl‚ü©
    simp_all

noncomputable abbrev y : B :=
  (exists_y A Q).choose

lemma y_spec : ((y A Q : B) : B ‚ß∏ Q) = g Q ‚àß
    ‚àÄ Q' : Ideal B, Q' ‚àà MulAction.orbit (B ‚âÉ‚Çê[A] B) Q ‚Üí Q' ‚â† Q ‚Üí (y A Q) ‚àà Q' :=
  (exists_y A Q).choose_spec

lemma y_mod_Q : Ideal.Quotient.mk Q (y A Q) = g Q := (y_spec A Q).1

lemma y_not_in_Q : (y A Q) ‚àâ Q :=
  mt Ideal.Quotient.eq_zero_iff_mem.mpr <| y_mod_Q A Q ‚ñ∏ (g Q).ne_zero

open Polynomial BigOperators

/-- `F : B[X]` defined to be a product of linear factors `(X - œÑ ‚Ä¢ Œ±)`; where
`œÑ` runs over `L ‚âÉ‚Çê[K] L`, and `Œ± : B` is an element which generates `(B ‚ß∏ Q)À£`
and lies in `œÑ ‚Ä¢ Q` for all `œÑ ‚àâ (decomposition_subgroup_Ideal'  A K L B Q)`.-/
noncomputable abbrev F : B[X] := ‚àè œÑ : B ‚âÉ‚Çê[A] B, (X - C (œÑ ‚Ä¢ (y A Q)))

lemma F_spec : F A Q = ‚àè œÑ : B ‚âÉ‚Çê[A] B, (X - C (œÑ ‚Ä¢ (y A Q))) := rfl

variable {A Q} in
open Finset in
lemma F.smul_eq_self (œÉ :  B ‚âÉ‚Çê[A] B)  : œÉ ‚Ä¢ (F A Q) = F A Q := calc
  œÉ ‚Ä¢ F A Q = œÉ ‚Ä¢ ‚àè œÑ : B ‚âÉ‚Çê[A] B, (X - C (œÑ ‚Ä¢ (y A Q))) := by rw [F_spec]
  _         = ‚àè œÑ : B ‚âÉ‚Çê[A] B, œÉ ‚Ä¢ (X - C (œÑ ‚Ä¢ (y A Q))) := smul_prod
  _         = ‚àè œÑ : B ‚âÉ‚Çê[A] B, (X - C ((œÉ * œÑ) ‚Ä¢ (y A Q))) := by simp [smul_sub]
  _         = ‚àè œÑ' : B ‚âÉ‚Çê[A] B, (X - C (œÑ' ‚Ä¢ (y A Q))) := Fintype.prod_bijective (fun œÑ ‚Ü¶ œÉ * œÑ)
                                                      (Group.mulLeft_bijective œÉ) _ _ (fun _ ‚Ü¶ rfl)
  _         = F A Q := by rw [F_spec]

lemma F.y_eq_zero : (F A Q).eval (y A Q) = 0 := by
  simp [F_spec, eval_prod, Finset.prod_eq_zero (Finset.mem_univ (1 : B ‚âÉ‚Çê[A] B))]

open scoped algebraMap

noncomputable local instance : Algebra A[X] B[X] :=
  RingHom.toAlgebra (Polynomial.mapRingHom (Algebra.toRingHom))

@[simp, norm_cast]
lemma coe_monomial (n : ‚Ñï) (a : A) : ((monomial n a : A[X]) : B[X]) = monomial n (a : B) :=
  map_monomial _

lemma F.descent (h : ‚àÄ b : B, (‚àÄ œÉ : B ‚âÉ‚Çê[A] B, œÉ ‚Ä¢ b = b) ‚Üí ‚àÉ a : A, b = a) :
    ‚àÉ m : A[X], (m : B[X]) = F A Q := by
  choose f hf using h
  classical
  let f' : B ‚Üí A := fun b ‚Ü¶ if h : ‚àÄ œÉ : B ‚âÉ‚Çê[A] B, œÉ b = b then f b h else 37
  use (‚àë x ‚àà (F A Q).support, (monomial x) (f' ((F A Q).coeff x)))
  ext N
  push_cast
  simp_rw [finset_sum_coeff, ‚Üê lcoeff_apply, lcoeff_apply, coeff_monomial]
  simp only [Finset.sum_ite_eq', mem_support_iff, ne_eq, ite_not, f']
  symm
  split
  ¬∑ next h => exact h
  ¬∑ next h1 =>
    rw [dif_pos <| fun œÉ ‚Ü¶ ?_]
    ¬∑ refine hf ?_ ?_
    ¬∑ nth_rw 2 [‚Üê F.smul_eq_self œÉ]
      rfl

-- This will be true in applications to number fields etc.
variable (isGalois : ‚àÄ b : B, (‚àÄ œÉ : B ‚âÉ‚Çê[A] B, œÉ ‚Ä¢ b = b) ‚Üí ‚àÉ a : A, b = a)

noncomputable abbrev m := (F.descent A Q isGalois).choose

lemma m_spec : ((m A Q isGalois) : B[X]) = F A Q := (F.descent A Q isGalois).choose_spec

lemma m_spec' : (m A Q isGalois).map (algebraMap A B) = F A Q := by
  rw [‚Üê m_spec A Q isGalois]
  rfl

-- Amelia's trick to insert "let P be the ideal under Q" into the typeclass system
variable (P : Ideal A) [P.IsMaximal] [Algebra (A ‚ß∏ P) (B ‚ß∏ Q)] [IsScalarTower A (A‚ß∏P) (B‚ß∏Q)]

-- want to move from eval‚ÇÇ?
lemma m.mod_P_y_eq_zero : (m A Q isGalois).eval‚ÇÇ (algebraMap A (B‚ß∏Q)) (algebraMap B (B‚ß∏Q) (y A Q)) = 0 := by
  rw [show algebraMap A (B‚ß∏Q) = (algebraMap B (B‚ß∏Q)).comp (algebraMap A B) from IsScalarTower.algebraMap_eq A B (B ‚ß∏ Q)]
  rw [‚Üêeval‚ÇÇ_map]
  change eval‚ÇÇ _ _ (m A Q isGalois : B[X]) = _
  simp [m_spec A Q isGalois, eval_map, F.y_eq_zero]

lemma m.y_mod_P_eq_zero : Polynomial.aeval (‚Üë(y A Q) : B ‚ß∏ Q) (m A Q isGalois) = 0 := by
  rw [‚Üê aeval_map_algebraMap B, m_spec']
  -- why can't I do this with a `rw`?
  change aeval (algebraMap B (B‚ß∏Q) (y A Q)) _ = _
  rw [aeval_algebraMap_apply, coe_aeval_eq_eval, F.y_eq_zero A Q, map_zero]


noncomputable abbrev mmodP := (m A Q isGalois).map (algebraMap A (A‚ß∏P))

open scoped Polynomial

-- lemma barg (K : Type*) [Field K] [Fintype K] : ‚àÉ p n : ‚Ñï, p.Prime ‚àß Fintype.card K = p ^ n ‚àß CharP K p := by
--   obtain ‚ü®p, n, h‚ÇÅ, h‚ÇÇ‚ü© := FiniteField.card' K
--   refine ‚ü®p, n.val, h‚ÇÅ, h‚ÇÇ, ?_‚ü©
--   have : (p ^ n.val : K) = 0 := mod_cast h‚ÇÇ ‚ñ∏ Nat.cast_card_eq_zero K
--   rw [CharP.charP_iff_prime_eq_zero h‚ÇÅ]
--   simpa only [ne_eq, PNat.ne_zero, not_false_eq_true, pow_eq_zero_iff] using this

-- mathlib
lemma _root_.Polynomial.eval‚ÇÇ_pow_card (k : Type*) [Field k] [Fintype k] (f : k[X])
    (L : Type*) [CommRing L] [Algebra k L]
    (t : L) : f.eval‚ÇÇ (algebraMap k L) (t^(Fintype.card k)) =
              (f.eval‚ÇÇ (algebraMap k L) t)^(Fintype.card k) := by
  simp_rw [‚Üê Polynomial.aeval_def] -- `eval‚ÇÇ (algebraMap k L)` is just `aeval`
  rw [‚Üê map_pow, ‚Üê FiniteField.expand_card, Polynomial.expand_aeval]

variable [Fintype (A‚ß∏P)]
-- (m-bar)(y^q)=0 in B/Q
lemma m.mod_P_y_pow_q_eq_zero :
    (m A Q isGalois).eval‚ÇÇ (algebraMap A (B‚ß∏Q)) ((algebraMap B (B‚ß∏Q) (y A Q)) ^ (Fintype.card (A‚ß∏P)))
    = 0 := by
  suffices ((m A Q isGalois).map (algebraMap A (A‚ß∏P))).eval‚ÇÇ (algebraMap (A‚ß∏P) (B‚ß∏Q))
    ((algebraMap B (B‚ß∏Q) (y A Q)) ^ (Fintype.card (A‚ß∏P))) = 0 by
    rwa [eval‚ÇÇ_map, ‚Üê IsScalarTower.algebraMap_eq A (A ‚ß∏ P) (B ‚ß∏ Q)] at this
  let foobar : Field (A‚ß∏P) := ((Ideal.Quotient.maximal_ideal_iff_isField_quotient P).mp ‚Äπ_‚Ä∫).toField
  rw [eval‚ÇÇ_pow_card, eval‚ÇÇ_map, ‚Üê IsScalarTower.algebraMap_eq A (A ‚ß∏ P) (B ‚ß∏ Q), m.mod_P_y_eq_zero, zero_pow]
  exact Fintype.card_ne_zero

lemma F.mod_Q_y_pow_q_eq_zero : (F A Q).eval‚ÇÇ (algebraMap B (B‚ß∏Q)) ((algebraMap B (B‚ß∏Q) (y A Q)) ^ (Fintype.card (A‚ß∏P))) = 0 := by
  rw [‚Üê m_spec' A Q isGalois, eval‚ÇÇ_map]--, m.mod_P_y_pow_q_eq_zero]
  rw [‚Üê IsScalarTower.algebraMap_eq A B (B ‚ß∏ Q), m.mod_P_y_pow_q_eq_zero]

lemma exists_Frob : ‚àÉ œÉ : B ‚âÉ‚Çê[A] B, œÉ (y A Q) - (y A Q) ^ (Fintype.card (A‚ß∏P)) ‚àà Q := by
  have := F.mod_Q_y_pow_q_eq_zero A Q isGalois P
  rw [F_spec] at this
  rw [eval‚ÇÇ_finset_prod] at this
  rw [Finset.prod_eq_zero_iff] at this
  obtain ‚ü®œÉ, -, hœÉ‚ü© := this
  use œÉ
  simp only [Ideal.Quotient.algebraMap_eq, AlgEquiv.smul_def, eval‚ÇÇ_sub, eval‚ÇÇ_X, eval‚ÇÇ_C,
    sub_eq_zero] at hœÉ
  exact (Submodule.Quotient.eq Q).mp (hœÉ.symm)

noncomputable abbrev Frob := (exists_Frob A Q isGalois P).choose

lemma Frob_spec : (Frob A Q isGalois P) ‚Ä¢ (y A Q) - (y A Q) ^ (Fintype.card (A‚ß∏P)) ‚àà Q :=
  (exists_Frob A Q isGalois P).choose_spec

lemma Frob_Q : Frob A Q isGalois P ‚Ä¢ Q = Q := by
  rw [smul_eq_iff_eq_inv_smul]
  by_contra h
  have hy : y A Q ‚àà (Frob A Q isGalois P)‚Åª¬π ‚Ä¢ Q := (y_spec A Q).2 _ ‚ü®_, rfl‚ü© (Ne.symm h)
  have hy2 : (Frob A Q isGalois P) ‚Ä¢ (y A Q) ‚àà Q := by
    rwa [Ideal.map_eq_comap_symm] at hy
  have this := Q.sub_mem hy2 <| Frob_spec A Q isGalois P
  simp only [sub_sub_cancel] at this
  apply y_not_in_Q A Q <| Ideal.IsPrime.mem_of_pow_mem (show Q.IsPrime by infer_instance) _ this

lemma _root_.Ideal.Quotient.coe_eq_coe_iff_sub_mem {R : Type*} [CommRing R] {I : Ideal R} (x y : R) :
  (x : R ‚ß∏ I) = y ‚Üî x - y ‚àà I := Ideal.Quotient.mk_eq_mk_iff_sub_mem _ _

lemma coething (A B : Type*) [CommSemiring A] [Ring B] [Algebra A B] (a : A) (n : ‚Ñï) :
    (a ^ n : A) = (a : B) ^ n := by
  --change algebraMap A B a ^ n = algebraMap A B (a ^ n)
  --symm
  exact map_pow _ _ _
--  simp only [map_pow]

--attribute [norm_cast] map_pow

lemma Frob_Q_eq_pow_card (x : B) : Frob A Q isGalois P x - x^(Fintype.card (A‚ß∏P)) ‚àà Q := by
  by_cases hx : x ‚àà Q
  ¬∑ refine Q.sub_mem ?_ (Q.pow_mem_of_mem hx _ Fintype.card_pos)
    nth_rw 2 [‚Üê Frob_Q A Q isGalois P]
    change (Frob A Q isGalois P) ‚Ä¢ x ‚àà _
    rw [Ideal.map_eq_comap_symm, Ideal.mem_comap]
    convert hx
    exact inv_smul_smul _ _
  ¬∑
    have foo : (x : B ‚ß∏ Q) ‚â† 0 :=
      mt (fun h ‚Ü¶ (Submodule.Quotient.mk_eq_zero Q).mp h) hx
    let foobar : Field (B‚ß∏Q) := ((Ideal.Quotient.maximal_ideal_iff_isField_quotient Q).mp ‚Äπ_‚Ä∫).toField
    let xbar : (B ‚ß∏ Q)À£ := Units.mk0 (x : B ‚ß∏ Q) foo
    obtain ‚ü®n, (hn : g Q ^ n = xbar)‚ü© := g_spec Q xbar
    have hn2 : (g Q : B ‚ß∏ Q) ^ n = xbar := by exact_mod_cast hn
    change _ = (x : B ‚ß∏ Q) at hn2
    rw [‚Üê Ideal.Quotient.mk_eq_mk_iff_sub_mem]
    change ((Frob A Q isGalois P) x : B ‚ß∏ Q) = x ^ Fintype.card (A ‚ß∏ P)
    rw [‚Üê hn2]
    push_cast
    have fact1 := Frob_spec A Q isGalois P
    have fact2 : y A Q = (g Q : B‚ß∏Q) := y_mod_Q A Q
    rw [‚Üê fact2]
    rw [‚Üê Ideal.Quotient.coe_eq_coe_iff_sub_mem] at fact1
    push_cast at fact1
    rw [pow_right_comm]
    rw [‚Üê fact1]
    norm_cast
    rw [Ideal.Quotient.coe_eq_coe_iff_sub_mem]
    have fact3 := Frob_Q A Q isGalois P
    rw [‚Üê smul_pow']
    change (Frob A Q isGalois P) ‚Ä¢ x - _ ‚àà _
    rw [‚Üê smul_sub]
    nth_rw 3 [ ‚Üê fact3]
    suffices (x - y A Q ^ n) ‚àà Q by
      exact?
    rw [smul_mem_smul]
    simp
    skip
    sorry

/- maths proof:

2) œÉ is x ^ #A/P mod Q
3) Application to number fields
-/
