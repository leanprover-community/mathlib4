/-
Copyright (c) 2020 Kenji Nakagawa. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenji Nakagawa, Anne Baanen, Filippo A. E. Nuccio
-/
import Mathlib.LinearAlgebra.FreeModule.PID
import Mathlib.RingTheory.DedekindDomain.Basic
import Mathlib.RingTheory.Localization.Module
import Mathlib.RingTheory.Trace

#align_import ring_theory.dedekind_domain.integral_closure from "leanprover-community/mathlib"@"4cf7ca0e69e048b006674cf4499e5c7d296a89e0"

/-!
# Integral closure of Dedekind domains

This file shows the integral closure of a Dedekind domain (in particular, the ring of integers
of a number field) is a Dedekind domain.

## Implementation notes

The definitions that involve a field of fractions choose a canonical field of fractions,
but are independent of that choice. The `..._iff` lemmas express this independence.

Often, definitions assume that Dedekind domains are not fields. We found it more practical
to add a `(h : Â¬IsField A)` assumption whenever this is explicitly needed.

## References

* [D. Marcus, *Number Fields*][marcus1977number]
* [J.W.S. Cassels, A. FrÃ¶lich, *Algebraic Number Theory*][cassels1967algebraic]
* [J. Neukirch, *Algebraic Number Theory*][Neukirch1992]

## Tags

dedekind domain, dedekind ring
-/


variable (R A K : Type*) [CommRing R] [CommRing A] [Field K]

open scoped nonZeroDivisors Polynomial

variable [IsDomain A]

section IsIntegralClosure

/-! ### `IsIntegralClosure` section

We show that an integral closure of a Dedekind domain in a finite separable
field extension is again a Dedekind domain. This implies the ring of integers
of a number field is a Dedekind domain. -/


open Algebra

open scoped BigOperators

variable [Algebra A K] [IsFractionRing A K]

variable (L : Type*) [Field L] (C : Type*) [CommRing C]

variable [Algebra K L] [Algebra A L] [IsScalarTower A K L]

variable [Algebra C L] [IsIntegralClosure C A L] [Algebra A C] [IsScalarTower A C L]

/- If `L` is a separable extension of `K = Frac(A)` and `L` has no zero smul divisors by `A`,
then `L` is the localization of the integral closure `C` of `A` in `L` at `Aâ°`. -/
theorem IsIntegralClosure.isLocalization [IsSeparable K L] [NoZeroSMulDivisors A L] :
    IsLocalization (Algebra.algebraMapSubmonoid C Aâ°) L := by
  haveI : IsDomain C :=
    (IsIntegralClosure.equiv A C L (integralClosure A L)).toMulEquiv.isDomain (integralClosure A L)
  haveI : NoZeroSMulDivisors A C := IsIntegralClosure.noZeroSMulDivisors A L
  -- âŠ¢ IsLocalization (algebraMapSubmonoid C Aâ°) L
  refine' âŸ¨_, fun z => _, fun {x y} => âŸ¨fun h => âŸ¨1, _âŸ©, _âŸ©âŸ©
  Â· rintro âŸ¨_, x, hx, rflâŸ©
    -- âŠ¢ IsUnit (â†‘(algebraMap C L) â†‘{ val := â†‘(algebraMap A C) x, property := (_ : âˆƒ  â€¦
    rw [isUnit_iff_ne_zero, map_ne_zero_iff _ (IsIntegralClosure.algebraMap_injective C A L),
      Subtype.coe_mk, map_ne_zero_iff _ (NoZeroSMulDivisors.algebraMap_injective A C)]
    exact mem_nonZeroDivisors_iff_ne_zero.mp hx
    -- ğŸ‰ no goals
  Â· obtain âŸ¨m, hmâŸ© :=
      IsIntegral.exists_multiple_integral_of_isLocalization Aâ° z (IsSeparable.isIntegral K z)
    obtain âŸ¨x, hxâŸ© : âˆƒ x, algebraMap C L x = m â€¢ z := IsIntegralClosure.isIntegral_iff.mp hm
    -- âŠ¢ âˆƒ x, z * â†‘(algebraMap C L) â†‘x.snd = â†‘(algebraMap C L) x.fst
    refine' âŸ¨âŸ¨x, algebraMap A C m, m, SetLike.coe_mem m, rflâŸ©, _âŸ©
    -- âŠ¢ z * â†‘(algebraMap C L) â†‘(x, { val := â†‘(algebraMap A C) â†‘m, property := (_ : âˆƒ â€¦
    rw [Subtype.coe_mk, â† IsScalarTower.algebraMap_apply, hx, mul_comm, Submonoid.smul_def,
      smul_def]
  Â· simp only [IsIntegralClosure.algebraMap_injective C A L h]
    -- ğŸ‰ no goals
  Â· rintro âŸ¨âŸ¨_, m, hm, rflâŸ©, hâŸ©
    -- âŠ¢ â†‘(algebraMap C L) x = â†‘(algebraMap C L) y
    refine' congr_arg (algebraMap C L) ((mul_right_inj' _).mp h)
    -- âŠ¢ â†‘{ val := â†‘(algebraMap A C) m, property := (_ : âˆƒ a, a âˆˆ â†‘Aâ° âˆ§ â†‘(algebraMap  â€¦
    rw [Subtype.coe_mk, map_ne_zero_iff _ (NoZeroSMulDivisors.algebraMap_injective A C)]
    -- âŠ¢ m â‰  0
    exact mem_nonZeroDivisors_iff_ne_zero.mp hm
    -- ğŸ‰ no goals
#align is_integral_closure.is_localization IsIntegralClosure.isLocalization

variable [FiniteDimensional K L]

variable {A K L}

theorem IsIntegralClosure.range_le_span_dualBasis [IsSeparable K L] {Î¹ : Type*} [Fintype Î¹]
    [DecidableEq Î¹] (b : Basis Î¹ K L) (hb_int : âˆ€ i, IsIntegral A (b i)) [IsIntegrallyClosed A] :
    LinearMap.range ((Algebra.linearMap C L).restrictScalars A) â‰¤
    Submodule.span A (Set.range <| (traceForm K L).dualBasis (traceForm_nondegenerate K L) b) := by
  let db := (traceForm K L).dualBasis (traceForm_nondegenerate K L) b
  -- âŠ¢ LinearMap.range (â†‘A (Algebra.linearMap C L)) â‰¤ Submodule.span A (Set.range â†‘ â€¦
  rintro _ âŸ¨x, rflâŸ©
  -- âŠ¢ â†‘(â†‘A (Algebra.linearMap C L)) x âˆˆ Submodule.span A (Set.range â†‘(BilinForm.du â€¦
  simp only [LinearMap.coe_restrictScalars, Algebra.linearMap_apply]
  -- âŠ¢ â†‘(algebraMap C L) x âˆˆ Submodule.span A (Set.range â†‘(BilinForm.dualBasis (tra â€¦
  have hx : IsIntegral A (algebraMap C L x) := (IsIntegralClosure.isIntegral A L x).algebraMap
  -- âŠ¢ â†‘(algebraMap C L) x âˆˆ Submodule.span A (Set.range â†‘(BilinForm.dualBasis (tra â€¦
  rsuffices âŸ¨c, x_eqâŸ© : âˆƒ c : Î¹ â†’ A, algebraMap C L x = âˆ‘ i, c i â€¢ db i
  -- âŠ¢ â†‘(algebraMap C L) x âˆˆ Submodule.span A (Set.range â†‘(BilinForm.dualBasis (tra â€¦
  Â· rw [x_eq]
    -- âŠ¢ âˆ‘ i : Î¹, c i â€¢ â†‘db i âˆˆ Submodule.span A (Set.range â†‘(BilinForm.dualBasis (tr â€¦
    refine' Submodule.sum_mem _ fun i _ => Submodule.smul_mem _ _ (Submodule.subset_span _)
    -- âŠ¢ â†‘db i âˆˆ Set.range â†‘(BilinForm.dualBasis (traceForm K L) (_ : BilinForm.Nonde â€¦
    rw [Set.mem_range]
    -- âŠ¢ âˆƒ y, â†‘(BilinForm.dualBasis (traceForm K L) (_ : BilinForm.Nondegenerate (tra â€¦
    exact âŸ¨i, rflâŸ©
    -- ğŸ‰ no goals
  suffices âˆƒ c : Î¹ â†’ K, (âˆ€ i, IsIntegral A (c i)) âˆ§ algebraMap C L x = âˆ‘ i, c i â€¢ db i by
    obtain âŸ¨c, hc, hxâŸ© := this
    have hc' : âˆ€ i, IsLocalization.IsInteger A (c i) := fun i =>
      IsIntegrallyClosed.isIntegral_iff.mp (hc i)
    use fun i => Classical.choose (hc' i)
    refine' hx.trans (Finset.sum_congr rfl fun i _ => _)
    conv_lhs => rw [â† Classical.choose_spec (hc' i)]
    rw [â† IsScalarTower.algebraMap_smul K (Classical.choose (hc' i)) (db i)]
  refine' âŸ¨fun i => db.repr (algebraMap C L x) i, fun i => _, (db.sum_repr _).symmâŸ©
  -- âŠ¢ IsIntegral A ((fun i => â†‘(â†‘db.repr (â†‘(algebraMap C L) x)) i) i)
  simp_rw [BilinForm.dualBasis_repr_apply]
  -- âŠ¢ IsIntegral A (BilinForm.bilin (traceForm K L) (â†‘(algebraMap C L) x) (â†‘b i))
  exact isIntegral_trace (isIntegral_mul hx (hb_int i))
  -- ğŸ‰ no goals
#align is_integral_closure.range_le_span_dual_basis IsIntegralClosure.range_le_span_dualBasis

theorem integralClosure_le_span_dualBasis [IsSeparable K L] {Î¹ : Type*} [Fintype Î¹] [DecidableEq Î¹]
    (b : Basis Î¹ K L) (hb_int : âˆ€ i, IsIntegral A (b i)) [IsIntegrallyClosed A] :
    Subalgebra.toSubmodule (integralClosure A L) â‰¤
    Submodule.span A (Set.range <| (traceForm K L).dualBasis (traceForm_nondegenerate K L) b) := by
  refine' le_trans _ (IsIntegralClosure.range_le_span_dualBasis (integralClosure A L) b hb_int)
  -- âŠ¢ â†‘Subalgebra.toSubmodule (integralClosure A L) â‰¤ LinearMap.range (â†‘A (Algebra â€¦
  intro x hx
  -- âŠ¢ x âˆˆ LinearMap.range (â†‘A (Algebra.linearMap { x // x âˆˆ integralClosure A L }  â€¦
  exact âŸ¨âŸ¨x, hxâŸ©, rflâŸ©
  -- ğŸ‰ no goals
#align integral_closure_le_span_dual_basis integralClosure_le_span_dualBasis

variable (A K)

/-- Send a set of `x`s in a finite extension `L` of the fraction field of `R`
to `(y : R) â€¢ x âˆˆ integralClosure R L`. -/
theorem exists_integral_multiples (s : Finset L) :
    âˆƒ (y : _) (_ : y â‰  (0 : A)), âˆ€ x âˆˆ s, IsIntegral A (y â€¢ x) := by
  haveI := Classical.decEq L
  -- âŠ¢ âˆƒ y x, âˆ€ (x : L), x âˆˆ s â†’ IsIntegral A (y â€¢ x)
  refine' s.induction _ _
  -- âŠ¢ âˆƒ y x, âˆ€ (x : L), x âˆˆ âˆ… â†’ IsIntegral A (y â€¢ x)
  Â· use 1, one_ne_zero
    -- âŠ¢ âˆ€ (x : L), x âˆˆ âˆ… â†’ IsIntegral A (1 â€¢ x)
    rintro x âŸ¨âŸ©
    -- ğŸ‰ no goals
  Â· rintro x s hx âŸ¨y, hy, hsâŸ©
    -- âŠ¢ âˆƒ y x_1, âˆ€ (x_2 : L), x_2 âˆˆ insert x s â†’ IsIntegral A (y â€¢ x_2)
    have := exists_integral_multiple
      ((IsFractionRing.isAlgebraic_iff A K L).mpr (isAlgebraic_of_finite _ _ x))
      ((injective_iff_map_eq_zero (algebraMap A L)).mp ?_)
    rcases this with âŸ¨x', y', hy', hx'âŸ©
    -- âŠ¢ âˆƒ y x_1, âˆ€ (x_2 : L), x_2 âˆˆ insert x s â†’ IsIntegral A (y â€¢ x_2)
    refine' âŸ¨y * y', mul_ne_zero hy hy', fun x'' hx'' => _âŸ©
    -- âŠ¢ IsIntegral A ((y * y') â€¢ x'')
    rcases Finset.mem_insert.mp hx'' with (rfl | hx'')
    Â· rw [mul_smul, Algebra.smul_def, Algebra.smul_def, mul_comm _ x'', hx']
      -- âŠ¢ IsIntegral A (â†‘(algebraMap A L) y * â†‘x')
      exact isIntegral_mul isIntegral_algebraMap x'.2
      -- ğŸ‰ no goals
    Â· rw [mul_comm, mul_smul, Algebra.smul_def]
      -- âŠ¢ IsIntegral A (â†‘(algebraMap A L) y' * y â€¢ x'')
      exact isIntegral_mul isIntegral_algebraMap (hs _ hx'')
      -- ğŸ‰ no goals
    Â· rw [IsScalarTower.algebraMap_eq A K L]
      -- âŠ¢ Function.Injective â†‘(RingHom.comp (algebraMap K L) (algebraMap A K))
      apply (algebraMap K L).injective.comp
      -- âŠ¢ Function.Injective fun x => â†‘(algebraMap A K) x
      exact IsFractionRing.injective _ _
      -- ğŸ‰ no goals
#align exists_integral_multiples exists_integral_multiples

variable (L)

/-- If `L` is a finite extension of `K = Frac(A)`,
then `L` has a basis over `A` consisting of integral elements. -/
theorem FiniteDimensional.exists_is_basis_integral :
    âˆƒ (s : Finset L) (b : Basis s K L), âˆ€ x, IsIntegral A (b x) := by
  letI := Classical.decEq L
  -- âŠ¢ âˆƒ s b, âˆ€ (x : { x // x âˆˆ s }), IsIntegral A (â†‘b x)
  letI : IsNoetherian K L := IsNoetherian.iff_fg.2 inferInstance
  -- âŠ¢ âˆƒ s b, âˆ€ (x : { x // x âˆˆ s }), IsIntegral A (â†‘b x)
  let s' := IsNoetherian.finsetBasisIndex K L
  -- âŠ¢ âˆƒ s b, âˆ€ (x : { x // x âˆˆ s }), IsIntegral A (â†‘b x)
  let bs' := IsNoetherian.finsetBasis K L
  -- âŠ¢ âˆƒ s b, âˆ€ (x : { x // x âˆˆ s }), IsIntegral A (â†‘b x)
  obtain âŸ¨y, hy, his'âŸ© := exists_integral_multiples A K (Finset.univ.image bs')
  -- âŠ¢ âˆƒ s b, âˆ€ (x : { x // x âˆˆ s }), IsIntegral A (â†‘b x)
  have hy' : algebraMap A L y â‰  0 := by
    refine' mt ((injective_iff_map_eq_zero (algebraMap A L)).mp _ _) hy
    rw [IsScalarTower.algebraMap_eq A K L]
    exact (algebraMap K L).injective.comp (IsFractionRing.injective A K)
  refine âŸ¨s', bs'.map {Algebra.lmul _ _ (algebraMap A L y) with
    toFun := fun x => algebraMap A L y * x
    invFun := fun x => (algebraMap A L y)â»Â¹ * x
    left_inv := ?_
    right_inv := ?_}, ?_âŸ©
  Â· intro x; simp only [inv_mul_cancel_leftâ‚€ hy']
    -- âŠ¢ (fun x => (â†‘(algebraMap A L) y)â»Â¹ * x) (AddHom.toFun { toAddHom := { toFun : â€¦
             -- ğŸ‰ no goals
  Â· intro x; simp only [mul_inv_cancel_leftâ‚€ hy']
    -- âŠ¢ AddHom.toFun { toAddHom := { toFun := fun x => â†‘(algebraMap A L) y * x, map_ â€¦
             -- ğŸ‰ no goals
  Â· rintro âŸ¨x', hx'âŸ©
    -- âŠ¢ IsIntegral A
    simp only [Algebra.smul_def, Finset.mem_image, exists_prop, Finset.mem_univ,
      true_and_iff] at his'
    simp only [Basis.map_apply, LinearEquiv.coe_mk]
    -- âŠ¢ IsIntegral A (â†‘(algebraMap A L) y * â†‘(IsNoetherian.finsetBasis K L) { val := â€¦
    exact his' _ âŸ¨_, rflâŸ©
    -- ğŸ‰ no goals
#align finite_dimensional.exists_is_basis_integral FiniteDimensional.exists_is_basis_integral

variable [IsSeparable K L]

/- If `L` is a finite separable extension of `K = Frac(A)`, where `A` is
integrally closed and Noetherian, the integral closure `C` of `A` in `L` is
Noetherian over `A`. -/
theorem IsIntegralClosure.isNoetherian [IsIntegrallyClosed A] [IsNoetherianRing A] :
    IsNoetherian A C := by
  haveI := Classical.decEq L
  -- âŠ¢ IsNoetherian A C
  obtain âŸ¨s, b, hb_intâŸ© := FiniteDimensional.exists_is_basis_integral A K L
  -- âŠ¢ IsNoetherian A C
  let b' := (traceForm K L).dualBasis (traceForm_nondegenerate K L) b
  -- âŠ¢ IsNoetherian A C
  letI := isNoetherian_span_of_finite A (Set.finite_range b')
  -- âŠ¢ IsNoetherian A C
  let f : C â†’â‚—[A] Submodule.span A (Set.range b') :=
    (Submodule.ofLe (IsIntegralClosure.range_le_span_dualBasis C b hb_int)).comp
      ((Algebra.linearMap C L).restrictScalars A).rangeRestrict
  refine' isNoetherian_of_ker_bot f _
  -- âŠ¢ LinearMap.ker f = âŠ¥
  rw [LinearMap.ker_comp, Submodule.ker_ofLe, Submodule.comap_bot, LinearMap.ker_codRestrict]
  -- âŠ¢ LinearMap.ker (â†‘A (Algebra.linearMap C L)) = âŠ¥
  exact LinearMap.ker_eq_bot_of_injective (IsIntegralClosure.algebraMap_injective C A L)
  -- ğŸ‰ no goals
#align is_integral_closure.is_noetherian IsIntegralClosure.isNoetherian

/- If `L` is a finite separable extension of `K = Frac(A)`, where `A` is
integrally closed and Noetherian, the integral closure `C` of `A` in `L` is
Noetherian. -/
theorem IsIntegralClosure.isNoetherianRing [IsIntegrallyClosed A] [IsNoetherianRing A] :
    IsNoetherianRing C :=
  isNoetherianRing_iff.mpr <| isNoetherian_of_tower A (IsIntegralClosure.isNoetherian A K L C)
#align is_integral_closure.is_noetherian_ring IsIntegralClosure.isNoetherianRing

/- If `L` is a finite separable extension of `K = Frac(A)`, where `A` is a principal ring
and `L` has no zero smul divisors by `A`, the integral closure `C` of `A` in `L` is
a free `A`-module. -/
theorem IsIntegralClosure.module_free [NoZeroSMulDivisors A L] [IsPrincipalIdealRing A] :
    Module.Free A C := by
  haveI : NoZeroSMulDivisors A C := IsIntegralClosure.noZeroSMulDivisors A L
  -- âŠ¢ Module.Free A C
  haveI : IsNoetherian A C := IsIntegralClosure.isNoetherian A K L _
  -- âŠ¢ Module.Free A C
  exact Module.free_of_finite_type_torsion_free'
  -- ğŸ‰ no goals
#align is_integral_closure.module_free IsIntegralClosure.module_free

/- If `L` is a finite separable extension of `K = Frac(A)`, where `A` is a principal ring
and `L` has no zero smul divisors by `A`, the `A`-rank of the integral closure `C` of `A` in `L`
is equal to the `K`-rank of `L`. -/
theorem IsIntegralClosure.rank [IsPrincipalIdealRing A] [NoZeroSMulDivisors A L] :
    FiniteDimensional.finrank A C = FiniteDimensional.finrank K L := by
  haveI : Module.Free A C := IsIntegralClosure.module_free A K L C
  -- âŠ¢ FiniteDimensional.finrank A C = FiniteDimensional.finrank K L
  haveI : IsNoetherian A C := IsIntegralClosure.isNoetherian A K L C
  -- âŠ¢ FiniteDimensional.finrank A C = FiniteDimensional.finrank K L
  haveI : IsLocalization (Algebra.algebraMapSubmonoid C Aâ°) L :=
    IsIntegralClosure.isLocalization A K L C
  let b := Basis.localizationLocalization K Aâ° L (Module.Free.chooseBasis A C)
  -- âŠ¢ FiniteDimensional.finrank A C = FiniteDimensional.finrank K L
  rw [FiniteDimensional.finrank_eq_card_chooseBasisIndex, FiniteDimensional.finrank_eq_card_basis b]
  -- ğŸ‰ no goals
#align is_integral_closure.rank IsIntegralClosure.rank

variable {A K}

/- If `L` is a finite separable extension of `K = Frac(A)`, where `A` is
integrally closed and Noetherian, the integral closure of `A` in `L` is
Noetherian. -/
theorem integralClosure.isNoetherianRing [IsIntegrallyClosed A] [IsNoetherianRing A] :
    IsNoetherianRing (integralClosure A L) :=
  IsIntegralClosure.isNoetherianRing A K L (integralClosure A L)
#align integral_closure.is_noetherian_ring integralClosure.isNoetherianRing

variable (A K) [IsDomain C]

/- If `L` is a finite separable extension of `K = Frac(A)`, where `A` is a Dedekind domain,
the integral closure `C` of `A` in `L` is a Dedekind domain.

Can't be an instance since `A`, `K` or `L` can't be inferred. See also the instance
`integralClosure.isDedekindDomain_fractionRing` where `K := FractionRing A`
and `C := integralClosure A L`.
-/
theorem IsIntegralClosure.isDedekindDomain [IsDedekindDomain A] : IsDedekindDomain C :=
  have : IsFractionRing C L := IsIntegralClosure.isFractionRing_of_finite_extension A K L C
  { IsIntegralClosure.isNoetherianRing A K L C,
    Ring.DimensionLEOne.isIntegralClosure A L C,
    (isIntegrallyClosed_iff L).mpr fun {x} hx =>
      âŸ¨IsIntegralClosure.mk' C x (isIntegral_trans (IsIntegralClosure.isIntegral_algebra A L) _ hx),
        IsIntegralClosure.algebraMap_mk' _ _ _âŸ© with : IsDedekindDomain C }
#align is_integral_closure.is_dedekind_domain IsIntegralClosure.isDedekindDomain

/- If `L` is a finite separable extension of `K = Frac(A)`, where `A` is a Dedekind domain,
the integral closure of `A` in `L` is a Dedekind domain.

Can't be an instance since `K` can't be inferred. See also the instance
`integralClosure.isDedekindDomain_fractionRing` where `K := FractionRing A`.
-/
theorem integralClosure.isDedekindDomain [IsDedekindDomain A] :
    IsDedekindDomain (integralClosure A L) :=
  IsIntegralClosure.isDedekindDomain A K L (integralClosure A L)
#align integral_closure.is_dedekind_domain integralClosure.isDedekindDomain

variable [Algebra (FractionRing A) L] [IsScalarTower A (FractionRing A) L]

variable [FiniteDimensional (FractionRing A) L] [IsSeparable (FractionRing A) L]

/- If `L` is a finite separable extension of `Frac(A)`, where `A` is a Dedekind domain,
the integral closure of `A` in `L` is a Dedekind domain.

See also the lemma `integralClosure.isDedekindDomain` where you can choose
the field of fractions yourself.
-/
instance integralClosure.isDedekindDomain_fractionRing [IsDedekindDomain A] :
    IsDedekindDomain (integralClosure A L) :=
  integralClosure.isDedekindDomain A (FractionRing A) L
#align integral_closure.is_dedekind_domain_fraction_ring integralClosure.isDedekindDomain_fractionRing

end IsIntegralClosure
