/-
Copyright (c) 2020 Kenji Nakagawa. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenji Nakagawa, Anne Baanen, Filippo A. E. Nuccio
-/
import Mathlib.RingTheory.Localization.LocalizationLocalization
import Mathlib.RingTheory.Localization.Submodule
import Mathlib.RingTheory.DiscreteValuationRing.TFAE

#align_import ring_theory.dedekind_domain.dvr from "leanprover-community/mathlib"@"f0c8bf9245297a541f468be517f1bde6195105e9"

/-!
# Dedekind domains

This file defines an equivalent notion of a Dedekind domain (or Dedekind ring),
namely a Noetherian integral domain where the localization at all nonzero prime ideals is a DVR
(TODO: and shows that implies the main definition).

## Main definitions

 - `IsDedekindDomainDvr` alternatively defines a Dedekind domain as an integral domain that
   is Noetherian, and the localization at every nonzero prime ideal is a DVR.

## Main results
 - `IsLocalization.AtPrime.discreteValuationRing_of_dedekind_domain` shows that
   `IsDedekindDomain` implies the localization at each nonzero prime ideal is a DVR.
 - `IsDedekindDomain.isDedekindDomainDvr` is one direction of the equivalence of definitions
   of a Dedekind domain

## Implementation notes

The definitions that involve a field of fractions choose a canonical field of fractions,
but are independent of that choice. The `..._iff` lemmas express this independence.

Often, definitions assume that Dedekind domains are not fields. We found it more practical
to add a `(h : ¬¨ IsField A)` assumption whenever this is explicitly needed.

## References

* [D. Marcus, *Number Fields*][marcus1977number]
* [J.W.S. Cassels, A. Fr√∂lich, *Algebraic Number Theory*][cassels1967algebraic]
* [J. Neukirch, *Algebraic Number Theory*][Neukirch1992]

## Tags

dedekind domain, dedekind ring
-/


variable (R A K : Type*) [CommRing R] [CommRing A] [IsDomain A] [Field K]

open scoped nonZeroDivisors Polynomial

/-- A Dedekind domain is an integral domain that is Noetherian, and the
localization at every nonzero prime is a discrete valuation ring.

This is equivalent to `IsDedekindDomain`.
TODO: prove the equivalence.
-/
structure IsDedekindDomainDvr : Prop where
  isNoetherianRing : IsNoetherianRing A
  is_dvr_at_nonzero_prime :
    ‚àÄ (P) (_ : P ‚â† (‚ä• : Ideal A)) (_ : P.IsPrime), DiscreteValuationRing (Localization.AtPrime P)
#align is_dedekind_domain_dvr IsDedekindDomainDvr

/-- Localizing a domain of Krull dimension `‚â§ 1` gives another ring of Krull dimension `‚â§ 1`.

Note that the same proof can/should be generalized to preserving any Krull dimension,
once we have a suitable definition.
-/
theorem Ring.DimensionLEOne.localization {R : Type*} (R‚Çò : Type*) [CommRing R] [IsDomain R]
    [CommRing R‚Çò] [Algebra R R‚Çò] {M : Submonoid R} [IsLocalization M R‚Çò] (hM : M ‚â§ R‚Å∞)
    [h : Ring.DimensionLEOne R] : Ring.DimensionLEOne R‚Çò := ‚ü®by
  intro p hp0 hpp
  -- ‚ä¢ Ideal.IsMaximal p
  refine' Ideal.isMaximal_def.mpr ‚ü®hpp.ne_top, Ideal.maximal_of_no_maximal fun P hpP hPm => _‚ü©
  -- ‚ä¢ False
  have hpP' : (‚ü®p, hpp‚ü© : { p : Ideal R‚Çò // p.IsPrime }) < ‚ü®P, hPm.isPrime‚ü© := hpP
  -- ‚ä¢ False
  rw [‚Üê (IsLocalization.orderIsoOfPrime M R‚Çò).lt_iff_lt] at hpP'
  -- ‚ä¢ False
  haveI : Ideal.IsPrime (Ideal.comap (algebraMap R R‚Çò) p) :=
    ((IsLocalization.orderIsoOfPrime M R‚Çò) ‚ü®p, hpp‚ü©).2.1
  haveI : Ideal.IsPrime (Ideal.comap (algebraMap R R‚Çò) P) :=
    ((IsLocalization.orderIsoOfPrime M R‚Çò) ‚ü®P, hPm.isPrime‚ü©).2.1
  have _ : Ideal.comap (algebraMap R R‚Çò) p < Ideal.comap (algebraMap R R‚Çò) P := hpP'
  -- ‚ä¢ False
  refine' h.not_lt_lt ‚ä• (Ideal.comap _ _) (Ideal.comap _ _) ‚ü®_, hpP'‚ü©
  -- ‚ä¢ ‚ä• < Ideal.comap (algebraMap R R‚Çò) ‚Üë{ val := p, property := hpp }
  exact IsLocalization.bot_lt_comap_prime _ _ hM _ hp0‚ü©
  -- üéâ no goals
#align ring.dimension_le_one.localization Ring.DimensionLEOne.localization

/-- The localization of a Dedekind domain is a Dedekind domain. -/
theorem IsLocalization.isDedekindDomain [IsDedekindDomain A] {M : Submonoid A} (hM : M ‚â§ A‚Å∞)
    (A‚Çò : Type*) [CommRing A‚Çò] [IsDomain A‚Çò] [Algebra A A‚Çò] [IsLocalization M A‚Çò] :
    IsDedekindDomain A‚Çò := by
  have h : ‚àÄ y : M, IsUnit (algebraMap A (FractionRing A) y) := by
    rintro ‚ü®y, hy‚ü©
    exact IsUnit.mk0 _ (mt IsFractionRing.to_map_eq_zero_iff.mp (nonZeroDivisors.ne_zero (hM hy)))
  letI : Algebra A‚Çò (FractionRing A) := RingHom.toAlgebra (IsLocalization.lift h)
  -- ‚ä¢ IsDedekindDomain A‚Çò
  haveI : IsScalarTower A A‚Çò (FractionRing A) :=
    IsScalarTower.of_algebraMap_eq fun x => (IsLocalization.lift_eq h x).symm
  haveI : IsFractionRing A‚Çò (FractionRing A) :=
    IsFractionRing.isFractionRing_of_isDomain_of_isLocalization M _ _
  refine' (isDedekindDomain_iff _ (FractionRing A)).mpr ‚ü®_, _, _, _‚ü©
  ¬∑ infer_instance
    -- üéâ no goals
  ¬∑ exact IsLocalization.isNoetherianRing M _ (by infer_instance)
    -- üéâ no goals
  ¬∑ exact Ring.DimensionLEOne.localization A‚Çò hM
    -- üéâ no goals
  ¬∑ intro x hx
    -- ‚ä¢ ‚àÉ y, ‚Üë(algebraMap A‚Çò (FractionRing A)) y = x
    obtain ‚ü®‚ü®y, y_mem‚ü©, hy‚ü© := hx.exists_multiple_integral_of_isLocalization M _
    -- ‚ä¢ ‚àÉ y, ‚Üë(algebraMap A‚Çò (FractionRing A)) y = x
    obtain ‚ü®z, hz‚ü© := (isIntegrallyClosed_iff _).mp IsDedekindDomain.toIsIntegrallyClosed hy
    -- ‚ä¢ ‚àÉ y, ‚Üë(algebraMap A‚Çò (FractionRing A)) y = x
    refine' ‚ü®IsLocalization.mk' A‚Çò z ‚ü®y, y_mem‚ü©, (IsLocalization.lift_mk'_spec _ _ _ _).mpr _‚ü©
    -- ‚ä¢ ‚Üë(algebraMap A (FractionRing A)) z = ‚Üë(algebraMap A (FractionRing A)) ‚Üë{ val ‚Ä¶
    rw [hz, ‚Üê Algebra.smul_def]
    -- ‚ä¢ { val := y, property := y_mem } ‚Ä¢ x = ‚Üë{ val := y, property := y_mem } ‚Ä¢ x
    rfl
    -- üéâ no goals
#align is_localization.is_dedekind_domain IsLocalization.isDedekindDomain

/-- The localization of a Dedekind domain at every nonzero prime ideal is a Dedekind domain. -/
theorem IsLocalization.AtPrime.isDedekindDomain [IsDedekindDomain A] (P : Ideal A) [P.IsPrime]
    (A‚Çò : Type*) [CommRing A‚Çò] [IsDomain A‚Çò] [Algebra A A‚Çò] [IsLocalization.AtPrime A‚Çò P] :
    IsDedekindDomain A‚Çò :=
  IsLocalization.isDedekindDomain A P.primeCompl_le_nonZeroDivisors A‚Çò
#align is_localization.at_prime.is_dedekind_domain IsLocalization.AtPrime.isDedekindDomain

theorem IsLocalization.AtPrime.not_isField {P : Ideal A} (hP : P ‚â† ‚ä•) [pP : P.IsPrime] (A‚Çò : Type*)
    [CommRing A‚Çò] [Algebra A A‚Çò] [IsLocalization.AtPrime A‚Çò P] : ¬¨IsField A‚Çò := by
  intro h
  -- ‚ä¢ False
  letI := h.toField
  -- ‚ä¢ False
  obtain ‚ü®x, x_mem, x_ne‚ü© := P.ne_bot_iff.mp hP
  -- ‚ä¢ False
  exact
    (LocalRing.maximalIdeal.isMaximal _).ne_top
      (Ideal.eq_top_of_isUnit_mem _
        ((IsLocalization.AtPrime.to_map_mem_maximal_iff A‚Çò P _).mpr x_mem)
        (isUnit_iff_ne_zero.mpr
          ((map_ne_zero_iff (algebraMap A A‚Çò)
                (IsLocalization.injective A‚Çò P.primeCompl_le_nonZeroDivisors)).mpr
            x_ne)))
#align is_localization.at_prime.not_is_field IsLocalization.AtPrime.not_isField

/-- In a Dedekind domain, the localization at every nonzero prime ideal is a DVR. -/
theorem IsLocalization.AtPrime.discreteValuationRing_of_dedekind_domain [IsDedekindDomain A]
    {P : Ideal A} (hP : P ‚â† ‚ä•) [pP : P.IsPrime] (A‚Çò : Type*) [CommRing A‚Çò] [IsDomain A‚Çò]
    [Algebra A A‚Çò] [IsLocalization.AtPrime A‚Çò P] : DiscreteValuationRing A‚Çò := by
  classical
  letI : IsNoetherianRing A‚Çò :=
    IsLocalization.isNoetherianRing P.primeCompl _ IsDedekindDomain.toIsNoetherian
  letI : LocalRing A‚Çò := IsLocalization.AtPrime.localRing A‚Çò P
  have hnf := IsLocalization.AtPrime.not_isField A hP A‚Çò
  exact
    ((DiscreteValuationRing.TFAE A‚Çò hnf).out 0 2).mpr
      (IsLocalization.AtPrime.isDedekindDomain A P _)
#align is_localization.at_prime.discrete_valuation_ring_of_dedekind_domain IsLocalization.AtPrime.discreteValuationRing_of_dedekind_domain

/-- Dedekind domains, in the sense of Noetherian integrally closed domains of Krull dimension ‚â§ 1,
are also Dedekind domains in the sense of Noetherian domains where the localization at every
nonzero prime ideal is a DVR. -/
theorem IsDedekindDomain.isDedekindDomainDvr [IsDedekindDomain A] : IsDedekindDomainDvr A :=
  { isNoetherianRing := IsDedekindDomain.toIsNoetherian
    is_dvr_at_nonzero_prime := fun _ hP _ =>
      IsLocalization.AtPrime.discreteValuationRing_of_dedekind_domain A hP _ }
#align is_dedekind_domain.is_dedekind_domain_dvr IsDedekindDomain.isDedekindDomainDvr
