/-
Copyright (c) 2023 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.NumberTheory.KummerDedekind
import Mathlib.NumberTheory.RamificationInertia.Unramified
import Mathlib.RingTheory.Finiteness.Quotient
import Mathlib.RingTheory.LocalRing.ResidueField.Instances
import Mathlib.RingTheory.Trace.Quotient

/-!
# The different ideal

## Main definition
- `Submodule.traceDual`: The dual `L`-sub `B`-module under the trace form.
- `FractionalIdeal.dual`: The dual fractional ideal under the trace form.
- `differentIdeal`: The different ideal of an extension of integral domains.

## Main results
- `conductor_mul_differentIdeal`:
  If `L = K[x]`, with `x` integral over `A`, then `ùî£ * ùîá = (f'(x))`
    with `f` being the minimal polynomial of `x`.
- `aeval_derivative_mem_differentIdeal`:
  If `L = K[x]`, with `x` integral over `A`, then `f'(x) ‚àà ùîá`
    with `f` being the minimal polynomial of `x`.
- `not_dvd_differentIdeal_iff`: A prime does not divide the different ideal iff it is unramified
  (in the sense of `Algebra.IsUnramifiedAt`).

## TODO
- Show properties of the different ideal
-/

open Module

universe u

attribute [local instance] FractionRing.liftAlgebra FractionRing.isScalarTower_liftAlgebra
  Ideal.Quotient.field

variable (A K : Type*) {L : Type u} {B} [CommRing A] [Field K] [CommRing B] [Field L]
variable [Algebra A K] [Algebra B L] [Algebra A B] [Algebra K L] [Algebra A L]
variable [IsScalarTower A K L] [IsScalarTower A B L]

open nonZeroDivisors IsLocalization Matrix Algebra Pointwise Polynomial Submodule
section BIsDomain

/-- Under the AKLB setting, `I·µõ := traceDual A K (I : Submodule B L)` is the
`Submodule B L` such that `x ‚àà I·µõ ‚Üî ‚àÄ y ‚àà I, Tr(x, y) ‚àà A` -/
noncomputable
def Submodule.traceDual (I : Submodule B L) : Submodule B L where
  __ := (traceForm K L).dualSubmodule (I.restrictScalars A)
  smul_mem' c x hx a ha := by
    rw [traceForm_apply, smul_mul_assoc, mul_comm, ‚Üê smul_mul_assoc, mul_comm]
    exact hx _ (Submodule.smul_mem _ c ha)

variable {A K}

local notation:max I:max "·µõ" => Submodule.traceDual A K I

namespace Submodule

lemma mem_traceDual {I : Submodule B L} {x} :
    x ‚àà I·µõ ‚Üî ‚àÄ a ‚àà I, traceForm K L x a ‚àà (algebraMap A K).range :=
  forall‚ÇÇ_congr fun _ _ ‚Ü¶ mem_one

lemma le_traceDual_iff_map_le_one {I J : Submodule B L} :
    I ‚â§ J·µõ ‚Üî ((I * J : Submodule B L).restrictScalars A).map
      ((trace K L).restrictScalars A) ‚â§ 1 := by
  rw [Submodule.map_le_iff_le_comap, Submodule.restrictScalars_mul, Submodule.mul_le]
  simp [SetLike.le_def, mem_traceDual]

lemma le_traceDual_mul_iff {I J J' : Submodule B L} :
    I ‚â§ (J * J')·µõ ‚Üî I * J ‚â§ J'·µõ := by
  simp_rw [le_traceDual_iff_map_le_one, mul_assoc]

lemma le_traceDual {I J : Submodule B L} :
    I ‚â§ J·µõ ‚Üî I * J ‚â§ 1·µõ := by
  rw [‚Üê le_traceDual_mul_iff, mul_one]

lemma le_traceDual_comm {I J : Submodule B L} :
    I ‚â§ J·µõ ‚Üî J ‚â§ I·µõ := by rw [le_traceDual, mul_comm, ‚Üê le_traceDual]

lemma le_traceDual_traceDual {I : Submodule B L} :
    I ‚â§ I·µõ·µõ := le_traceDual_comm.mpr le_rfl

@[simp]
lemma restrictScalars_traceDual {I : Submodule B L} :
    I·µõ.restrictScalars A = (Algebra.traceForm K L).dualSubmodule (I.restrictScalars A) := rfl

@[simp]
lemma traceDual_bot :
    (‚ä• : Submodule B L)·µõ = ‚ä§ := by ext; simp [mem_traceDual, -RingHom.mem_range]

open scoped Classical in
lemma traceDual_top' :
    (‚ä§ : Submodule B L)·µõ =
      if ((LinearMap.range (Algebra.trace K L)).restrictScalars A ‚â§ 1) then ‚ä§ else ‚ä• := by
  classical
  split_ifs with h
  ¬∑ rw [_root_.eq_top_iff]
    exact fun _ _ _ _ ‚Ü¶ h ‚ü®_, rfl‚ü©
  ¬∑ simp only [SetLike.le_def, restrictScalars_mem, LinearMap.mem_range, mem_one,
      forall_exists_index, forall_apply_eq_imp_iff, not_forall, not_exists] at h
    obtain ‚ü®b, hb‚ü© := h
    simp_rw [eq_bot_iff, SetLike.le_def, mem_bot, mem_traceDual, mem_top, true_implies,
      traceForm_apply, RingHom.mem_range]
    contrapose! hb with hx'
    obtain ‚ü®c, hc, hc0‚ü© := hx'
    simpa [hc0] using hc (c‚Åª¬π * b)

variable [IsDomain A] [IsFractionRing A K] [FiniteDimensional K L] [Algebra.IsSeparable K L]

lemma traceDual_top [Decidable (IsField A)] :
    (‚ä§ : Submodule B L)·µõ = if IsField A then ‚ä§ else ‚ä• := by
  convert traceDual_top'
  rw [‚Üê IsFractionRing.surjective_iff_isField (R := A) (K := K),
    LinearMap.range_eq_top.mpr (Algebra.trace_surjective K L),
    ‚Üê RingHom.range_eq_top, _root_.eq_top_iff]
  simp [SetLike.le_def]

end Submodule

open Submodule

variable [IsFractionRing A K]

variable (A K) in
lemma map_equiv_traceDual [IsDomain A] [IsFractionRing B L] [IsDomain B]
    [FaithfulSMul A B] (I : Submodule B (FractionRing B)) :
    (traceDual A (FractionRing A) I).map (FractionRing.algEquiv B L) =
      traceDual A K (I.map (FractionRing.algEquiv B L)) := by
  change Submodule.map (FractionRing.algEquiv B L).toLinearEquiv.toLinearMap _ =
    traceDual A K (I.map (FractionRing.algEquiv B L).toLinearEquiv.toLinearMap)
  rw [Submodule.map_equiv_eq_comap_symm, Submodule.map_equiv_eq_comap_symm]
  ext x
  simp only [traceDual, Submodule.mem_comap,
    Submodule.mem_mk]
  apply (FractionRing.algEquiv B L).forall_congr
  simp only [restrictScalars_mem, LinearEquiv.coe_coe, AlgEquiv.coe_symm_toLinearEquiv,
    traceForm_apply, mem_one, AlgEquiv.toEquiv_eq_coe, EquivLike.coe_coe, mem_comap,
    AlgEquiv.symm_apply_apply]
  refine fun {y} ‚Ü¶ (forall_congr' fun hy ‚Ü¶ ?_)
  rw [Algebra.trace_eq_of_equiv_equiv (FractionRing.algEquiv A K).toRingEquiv
    (FractionRing.algEquiv B L).toRingEquiv]
  swap
  ¬∑ apply IsLocalization.ringHom_ext (M := A‚Å∞); ext
    simp only [AlgEquiv.toRingEquiv_eq_coe, AlgEquiv.toRingEquiv_toRingHom, RingHom.coe_comp,
      RingHom.coe_coe, Function.comp_apply, AlgEquiv.commutes, ‚Üê IsScalarTower.algebraMap_apply]
    rw [IsScalarTower.algebraMap_apply A B (FractionRing B), AlgEquiv.commutes,
      ‚Üê IsScalarTower.algebraMap_apply]
  simp only [AlgEquiv.toRingEquiv_eq_coe, map_mul, AlgEquiv.coe_ringEquiv,
    AlgEquiv.apply_symm_apply, ‚Üê AlgEquiv.symm_toRingEquiv, AlgEquiv.algebraMap_eq_apply]

variable [IsIntegrallyClosed A]

lemma Submodule.mem_traceDual_iff_isIntegral {I : Submodule B L} {x} :
    x ‚àà I·µõ ‚Üî ‚àÄ a ‚àà I, IsIntegral A (traceForm K L x a) :=
  forall‚ÇÇ_congr fun _ _ ‚Ü¶ mem_one.trans IsIntegrallyClosed.isIntegral_iff.symm

variable [FiniteDimensional K L] [IsIntegralClosure B A L]

lemma Submodule.one_le_traceDual_one :
    (1 : Submodule B L) ‚â§ 1·µõ := by
  rw [le_traceDual_iff_map_le_one, mul_one, one_eq_range]
  rintro _ ‚ü®x, ‚ü®x, rfl‚ü©, rfl‚ü©
  rw [mem_one]
  apply IsIntegrallyClosed.isIntegral_iff.mp
  apply isIntegral_trace
  rw [IsIntegralClosure.isIntegral_iff (A := B)]
  exact ‚ü®_, rfl‚ü©

variable [Algebra.IsSeparable K L]

/-- If `b` is an `A`-integral basis of `L` with discriminant `b`, then `d ‚Ä¢ a * x` is integral over
  `A` for all `a ‚àà I` and `x ‚àà I·µõ`. -/
lemma isIntegral_discr_mul_of_mem_traceDual
    (I : Submodule B L) {Œπ} [DecidableEq Œπ] [Fintype Œπ]
    {b : Basis Œπ K L} (hb : ‚àÄ i, IsIntegral A (b i))
    {a x : L} (ha : a ‚àà I) (hx : x ‚àà I·µõ) :
    IsIntegral A ((discr K b) ‚Ä¢ a * x) := by
  have hinv : IsUnit (traceMatrix K b).det := by
    simpa [‚Üê discr_def] using discr_isUnit_of_basis _ b
  have H := mulVec_cramer (traceMatrix K b) fun i => trace K L (x * a * b i)
  have : Function.Injective (traceMatrix K b).mulVec := by
    rwa [mulVec_injective_iff_isUnit, isUnit_iff_isUnit_det]
  rw [‚Üê traceMatrix_of_basis_mulVec, ‚Üê mulVec_smul, this.eq_iff,
    traceMatrix_of_basis_mulVec] at H
  rw [‚Üê b.equivFun.symm_apply_apply (_ * _), b.equivFun_symm_apply]
  apply IsIntegral.sum
  intro i _
  rw [smul_mul_assoc, b.equivFun.map_smul, discr_def, mul_comm, ‚Üê H, Algebra.smul_def]
  refine RingHom.IsIntegralElem.mul _ ?_ (hb _)
  apply IsIntegral.algebraMap
  rw [cramer_apply]
  apply IsIntegral.det
  intros j k
  rw [updateCol_apply]
  split
  ¬∑ rw [mul_assoc]
    rw [mem_traceDual_iff_isIntegral] at hx
    apply hx
    have ‚ü®y, hy‚ü© := (IsIntegralClosure.isIntegral_iff (A := B)).mp (hb j)
    rw [mul_comm, ‚Üê hy, ‚Üê Algebra.smul_def]
    exact I.smul_mem _ (ha)
  ¬∑ exact isIntegral_trace (RingHom.IsIntegralElem.mul _ (hb j) (hb k))

variable (A K)

variable [IsDomain A] [IsFractionRing B L] [Nontrivial B] [NoZeroDivisors B]

namespace FractionalIdeal

open scoped Classical in
/-- The dual of a non-zero fractional ideal is the dual of the submodule under the traceform. -/
noncomputable
def dual (I : FractionalIdeal B‚Å∞ L) :
    FractionalIdeal B‚Å∞ L :=
  if hI : I = 0 then 0 else
  ‚ü®I·µõ, by
    classical
    have ‚ü®s, b, hb‚ü© := FiniteDimensional.exists_is_basis_integral A K L
    obtain ‚ü®x, hx, hx'‚ü© := exists_ne_zero_mem_isInteger hI
    have ‚ü®y, hy‚ü© := (IsIntegralClosure.isIntegral_iff (A := B)).mp
      (IsIntegral.algebraMap (B := L) (discr_isIntegral K hb))
    refine ‚ü®y * x, mem_nonZeroDivisors_iff_ne_zero.mpr (mul_ne_zero ?_ hx), fun z hz ‚Ü¶ ?_‚ü©
    ¬∑ rw [‚Üê (IsIntegralClosure.algebraMap_injective B A L).ne_iff, hy, RingHom.map_zero,
        ‚Üê (algebraMap K L).map_zero, (algebraMap K L).injective.ne_iff]
      exact discr_not_zero_of_basis K b
    ¬∑ convert isIntegral_discr_mul_of_mem_traceDual I hb hx' hz using 1
      ¬∑ ext w; exact (IsIntegralClosure.isIntegral_iff (A := B)).symm
      ¬∑ rw [Algebra.smul_def, RingHom.map_mul, hy, ‚Üê Algebra.smul_def]‚ü©

end FractionalIdeal

end BIsDomain

variable [IsDomain A] [IsFractionRing A K]
  [FiniteDimensional K L] [Algebra.IsSeparable K L] [IsIntegralClosure B A L]

namespace FractionalIdeal

variable [IsFractionRing B L] [IsIntegrallyClosed A]

open Submodule

local notation:max I:max "·µõ" => Submodule.traceDual A K I

variable [IsDedekindDomain B] {I J : FractionalIdeal B‚Å∞ L}

lemma coe_dual (hI : I ‚â† 0) :
    (dual A K I : Submodule B L) = I·µõ := by rw [dual, dif_neg hI, coe_mk]

variable (B L)

@[simp]
lemma coe_dual_one :
    (dual A K (1 : FractionalIdeal B‚Å∞ L) : Submodule B L) = 1·µõ := by
  rw [‚Üê coe_one, coe_dual]
  exact one_ne_zero

@[simp]
lemma dual_zero :
    dual A K (0 : FractionalIdeal B‚Å∞ L) = 0 := by rw [dual, dif_pos rfl]

variable {A K L B}

lemma mem_dual (hI : I ‚â† 0) {x} :
    x ‚àà dual A K I ‚Üî ‚àÄ a ‚àà I, traceForm K L x a ‚àà (algebraMap A K).range := by
  rw [dual, dif_neg hI]; exact forall‚ÇÇ_congr fun _ _ ‚Ü¶ mem_one

variable (A K)

lemma dual_ne_zero (hI : I ‚â† 0) :
    dual A K I ‚â† 0 := by
  obtain ‚ü®b, hb, hb'‚ü© := I.prop
  suffices algebraMap B L b ‚àà dual A K I by
    intro e
    rw [e, mem_zero_iff, ‚Üê (algebraMap B L).map_zero,
      (IsIntegralClosure.algebraMap_injective B A L).eq_iff] at this
    exact mem_nonZeroDivisors_iff_ne_zero.mp hb this
  rw [mem_dual hI]
  intro a ha
  apply IsIntegrallyClosed.isIntegral_iff.mp
  apply isIntegral_trace
  dsimp
  convert hb' a ha using 1
  ¬∑ ext w
    exact IsIntegralClosure.isIntegral_iff (A := B)
  ¬∑ exact (Algebra.smul_def _ _).symm

variable {A K}

@[simp]
lemma dual_eq_zero_iff :
    dual A K I = 0 ‚Üî I = 0 :=
  ‚ü®not_imp_not.mp (dual_ne_zero A K), fun e ‚Ü¶ e.symm ‚ñ∏ dual_zero A K L B‚ü©

lemma dual_ne_zero_iff :
    dual A K I ‚â† 0 ‚Üî I ‚â† 0 := dual_eq_zero_iff.not

variable (A K)

lemma le_dual_inv_aux (hI : I ‚â† 0) (hIJ : I * J ‚â§ 1) :
    J ‚â§ dual A K I := by
  rw [dual, dif_neg hI]
  intro x hx y hy
  rw [mem_one]
  apply IsIntegrallyClosed.isIntegral_iff.mp
  apply isIntegral_trace
  rw [IsIntegralClosure.isIntegral_iff (A := B)]
  have ‚ü®z, _, hz‚ü© := hIJ (FractionalIdeal.mul_mem_mul hy hx)
  rw [mul_comm] at hz
  exact ‚ü®z, hz‚ü©

lemma one_le_dual_one :
    1 ‚â§ dual A K (1 : FractionalIdeal B‚Å∞ L) :=
  le_dual_inv_aux A K one_ne_zero (by rw [one_mul])

lemma le_dual_iff (hJ : J ‚â† 0) :
    I ‚â§ dual A K J ‚Üî I * J ‚â§ dual A K 1 := by
  by_cases hI : I = 0
  ¬∑ simp [hI]
  rw [‚Üê coe_le_coe, ‚Üê coe_le_coe, coe_mul, coe_dual A K hJ, coe_dual_one, le_traceDual]

variable (I)

lemma inv_le_dual :
    I‚Åª¬π ‚â§ dual A K I := by
  classical
  exact if hI : I = 0 then by simp [hI] else le_dual_inv_aux A K hI (le_of_eq (mul_inv_cancel‚ÇÄ hI))

lemma dual_inv_le :
    (dual A K I)‚Åª¬π ‚â§ I := by
  by_cases hI : I = 0; ¬∑ simp [hI]
  convert mul_right_mono ((dual A K I)‚Åª¬π)
    (mul_left_mono I (inv_le_dual A K I)) using 1
  ¬∑ simp only [mul_inv_cancel‚ÇÄ hI, one_mul]
  ¬∑ simp only [mul_inv_cancel‚ÇÄ (dual_ne_zero A K (hI := hI)), mul_assoc, mul_one]

lemma dual_eq_mul_inv :
    dual A K I = dual A K 1 * I‚Åª¬π := by
  by_cases hI : I = 0; ¬∑ simp [hI]
  apply le_antisymm
  ¬∑ suffices dual A K I * I ‚â§ dual A K 1 by
      convert mul_right_mono I‚Åª¬π this using 1; simp only [mul_inv_cancel‚ÇÄ hI, mul_one, mul_assoc]
    rw [‚Üê le_dual_iff A K hI]
  rw [le_dual_iff A K hI, mul_assoc, inv_mul_cancel‚ÇÄ hI, mul_one]

variable {I}

lemma dual_div_dual :
    dual A K J / dual A K I = I / J := by
  rw [dual_eq_mul_inv A K J, dual_eq_mul_inv A K I, mul_div_mul_comm, div_self, one_mul]
  ¬∑ exact inv_div_inv J I
  ¬∑ simp only [ne_eq, dual_eq_zero_iff, one_ne_zero, not_false_eq_true]

lemma dual_mul_self (hI : I ‚â† 0) :
    dual A K I * I = dual A K 1 := by
  rw [dual_eq_mul_inv, mul_assoc, inv_mul_cancel‚ÇÄ hI, mul_one]

lemma self_mul_dual (hI : I ‚â† 0) :
    I * dual A K I = dual A K 1 := by
  rw [mul_comm, dual_mul_self A K hI]

lemma dual_inv :
    dual A K I‚Åª¬π = dual A K 1 * I := by rw [dual_eq_mul_inv, inv_inv]

variable (I)

@[simp]
lemma dual_dual :
    dual A K (dual A K I) = I := by
  rw [dual_eq_mul_inv, dual_eq_mul_inv A K (I := I), mul_inv, inv_inv, ‚Üê mul_assoc, mul_inv_cancel‚ÇÄ,
    one_mul]
  rw [dual_ne_zero_iff]
  exact one_ne_zero

variable {I}

@[simp]
lemma dual_le_dual (hI : I ‚â† 0) (hJ : J ‚â† 0) :
    dual A K I ‚â§ dual A K J ‚Üî J ‚â§ I := by
  nth_rewrite 2 [‚Üê dual_dual A K I]
  rw [le_dual_iff A K hJ, le_dual_iff A K (I := J) (by rwa [dual_ne_zero_iff]), mul_comm]

variable {A K}

lemma dual_involutive :
    Function.Involutive (dual A K : FractionalIdeal B‚Å∞ L ‚Üí FractionalIdeal B‚Å∞ L) := dual_dual A K

lemma dual_injective :
    Function.Injective (dual A K : FractionalIdeal B‚Å∞ L ‚Üí FractionalIdeal B‚Å∞ L) :=
  dual_involutive.injective

end FractionalIdeal

section IsIntegrallyClosed

variable (B)
variable [IsIntegrallyClosed A] [IsDedekindDomain B] [NoZeroSMulDivisors A B]

/-- The different ideal of an extension of integral domains `B/A` is the inverse of the dual of `A`
as an ideal of `B`. See `coeIdeal_differentIdeal` and `coeSubmodule_differentIdeal`. -/
noncomputable def differentIdeal : Ideal B :=
  (1 / Submodule.traceDual A (FractionRing A) 1 : Submodule B (FractionRing B)).comap
    (Algebra.linearMap B (FractionRing B))

lemma coeSubmodule_differentIdeal_fractionRing [Algebra.IsIntegral A B]
    [Algebra.IsSeparable (FractionRing A) (FractionRing B)]
    [FiniteDimensional (FractionRing A) (FractionRing B)] :
    coeSubmodule (FractionRing B) (differentIdeal A B) =
      1 / Submodule.traceDual A (FractionRing A) 1 := by
  have : IsIntegralClosure B A (FractionRing B) :=
    IsIntegralClosure.of_isIntegrallyClosed _ _ _
  rw [coeSubmodule, differentIdeal, Submodule.map_comap_eq, inf_eq_right]
  have := FractionalIdeal.dual_inv_le (A := A) (K := FractionRing A)
    (1 : FractionalIdeal B‚Å∞ (FractionRing B))
  have : _ ‚â§ ((1 : FractionalIdeal B‚Å∞ (FractionRing B)) : Submodule B (FractionRing B)) := this
  simp only [‚Üê one_div, FractionalIdeal.val_eq_coe] at this
  rw [FractionalIdeal.coe_div (FractionalIdeal.dual_ne_zero _ _ _),
    FractionalIdeal.coe_dual] at this
  ¬∑ simpa only [FractionalIdeal.coe_one, Submodule.one_eq_range] using this
  ¬∑ exact one_ne_zero
  ¬∑ exact one_ne_zero

section

variable [IsFractionRing B L]

lemma coeSubmodule_differentIdeal :
    coeSubmodule L (differentIdeal A B) = 1 / Submodule.traceDual A K 1 := by
  have : (FractionRing.algEquiv B L).toLinearEquiv.comp (Algebra.linearMap B (FractionRing B)) =
    Algebra.linearMap B L := by ext; simp
  rw [coeSubmodule, ‚Üê this]
  have H : RingHom.comp (algebraMap (FractionRing A) (FractionRing B))
      ‚Üë(FractionRing.algEquiv A K).symm.toRingEquiv =
        RingHom.comp ‚Üë(FractionRing.algEquiv B L).symm.toRingEquiv (algebraMap K L) := by
    apply IsLocalization.ringHom_ext A‚Å∞
    ext
    simp only [AlgEquiv.toRingEquiv_eq_coe, RingHom.coe_comp, RingHom.coe_coe,
      AlgEquiv.coe_ringEquiv, Function.comp_apply, AlgEquiv.commutes,
      ‚Üê IsScalarTower.algebraMap_apply]
    rw [IsScalarTower.algebraMap_apply A B L, AlgEquiv.commutes, ‚Üê IsScalarTower.algebraMap_apply]
  have : Algebra.IsSeparable (FractionRing A) (FractionRing B) :=
    Algebra.IsSeparable.of_equiv_equiv _ _ H
  have : FiniteDimensional (FractionRing A) (FractionRing B) := Module.Finite.of_equiv_equiv _ _ H
  have : Algebra.IsIntegral A B := IsIntegralClosure.isIntegral_algebra _ L
  simp only [AlgEquiv.toLinearEquiv_toLinearMap, Submodule.map_comp]
  rw [‚Üê coeSubmodule, coeSubmodule_differentIdeal_fractionRing _ _,
    Submodule.map_div, ‚Üê AlgEquiv.toAlgHom_toLinearMap, Submodule.map_one]
  congr 1
  refine (map_equiv_traceDual A K _).trans ?_
  congr 1
  ext
  simp

variable (L)

lemma coeIdeal_differentIdeal :
    ‚Üë(differentIdeal A B) = (FractionalIdeal.dual A K (1 : FractionalIdeal B‚Å∞ L))‚Åª¬π := by
  apply FractionalIdeal.coeToSubmodule_injective
  simp only [FractionalIdeal.coe_div
    (FractionalIdeal.dual_ne_zero _ _ (@one_ne_zero (FractionalIdeal B‚Å∞ L) _ _ _)),
    FractionalIdeal.coe_coeIdeal, coeSubmodule_differentIdeal A K, inv_eq_one_div,
    FractionalIdeal.coe_dual_one, FractionalIdeal.coe_one]

variable {A K B L}

theorem differentIdeal_ne_bot [Module.Finite A B]
    [Algebra.IsSeparable (FractionRing A) (FractionRing B)] :
    differentIdeal A B ‚â† ‚ä• := by
  let K := FractionRing A
  let L := FractionRing B
  have : IsLocalization (Algebra.algebraMapSubmonoid B A‚Å∞) L :=
    IsIntegralClosure.isLocalization _ K _ _
  have : FiniteDimensional K L := .of_isLocalization A B A‚Å∞
  rw [ne_eq, ‚Üê FractionalIdeal.coeIdeal_inj (K := L), coeIdeal_differentIdeal (K := K)]
  simp

lemma differentialIdeal_le_fractionalIdeal_iff
    {I : FractionalIdeal B‚Å∞ L} (hI : I ‚â† 0) :
    differentIdeal A B ‚â§ I ‚Üî (((I‚Åª¬π :) : Submodule B L).restrictScalars A).map
      ((Algebra.trace K L).restrictScalars A) ‚â§ 1 := by
  rw [coeIdeal_differentIdeal A K L B, FractionalIdeal.inv_le_comm (by simp) hI,
    ‚Üê FractionalIdeal.coe_le_coe, FractionalIdeal.coe_dual_one]
  refine le_traceDual_iff_map_le_one.trans ?_
  simp

lemma differentialIdeal_le_iff {I : Ideal B} (hI : I ‚â† ‚ä•) :
    differentIdeal A B ‚â§ I ‚Üî (((I‚Åª¬π : FractionalIdeal B‚Å∞ L) : Submodule B L).restrictScalars A).map
      ((Algebra.trace K L).restrictScalars A) ‚â§ 1 :=
  (FractionalIdeal.coeIdeal_le_coeIdeal _).symm.trans
    (differentialIdeal_le_fractionalIdeal_iff (I := (I : FractionalIdeal B‚Å∞ L)) (by simpa))

variable (A K)

open Pointwise Polynomial in
lemma traceForm_dualSubmodule_adjoin
    {x : L} (hx : Algebra.adjoin K {x} = ‚ä§) (hAx : IsIntegral A x) :
    (traceForm K L).dualSubmodule (Subalgebra.toSubmodule (Algebra.adjoin A {x})) =
      (aeval x (derivative <| minpoly K x) : L)‚Åª¬π ‚Ä¢
        (Subalgebra.toSubmodule (Algebra.adjoin A {x})) := by
  classical
  have hKx : IsIntegral K x := Algebra.IsIntegral.isIntegral x
  let pb := (Algebra.adjoin.powerBasis' hKx).map
    ((Subalgebra.equivOfEq _ _ hx).trans (Subalgebra.topEquiv))
  have pbgen : pb.gen = x := by simp [pb]
  have hpb : ‚áë(LinearMap.BilinForm.dualBasis (traceForm K L) _ pb.basis) = _ :=
    _root_.funext (traceForm_dualBasis_powerBasis_eq pb)
  have : (Subalgebra.toSubmodule (Algebra.adjoin A {x})) =
      Submodule.span A (Set.range pb.basis) := by
    rw [‚Üê span_range_natDegree_eq_adjoin (minpoly.monic hAx) (minpoly.aeval _ _)]
    congr; ext y
    have : natDegree (minpoly A x) = natDegree (minpoly K x) := by
      rw [minpoly.isIntegrallyClosed_eq_field_fractions' K hAx, (minpoly.monic hAx).natDegree_map]
    simp only [Finset.coe_image, Finset.coe_range, Set.mem_image, Set.mem_Iio, Set.mem_range,
      pb.basis_eq_pow, pbgen]
    simp only [this]
    exact ‚ü®fun ‚ü®a, b, c‚ü© ‚Ü¶ ‚ü®‚ü®a, b‚ü©, c‚ü©, fun ‚ü®‚ü®a, b‚ü©, c‚ü© ‚Ü¶ ‚ü®a, b, c‚ü©‚ü©
  clear_value pb
  conv_lhs => rw [this]
  rw [‚Üê span_coeff_minpolyDiv hAx, LinearMap.BilinForm.dualSubmodule_span_of_basis,
    Submodule.smul_span, hpb]
  change _ = Submodule.span A (_ '' _)
  simp only [‚Üê Set.range_comp, smul_eq_mul, div_eq_inv_mul, pbgen,
    minpolyDiv_eq_of_isIntegrallyClosed K hAx]
  apply le_antisymm <;> rw [Submodule.span_le]
  ¬∑ rintro _ ‚ü®i, rfl‚ü©; exact Submodule.subset_span ‚ü®i, rfl‚ü©
  ¬∑ rintro _ ‚ü®i, rfl‚ü©
    by_cases hi : i < pb.dim
    ¬∑ exact Submodule.subset_span ‚ü®‚ü®i, hi‚ü©, rfl‚ü©
    ¬∑ rw [Function.comp_apply, coeff_eq_zero_of_natDegree_lt, mul_zero]
      ¬∑ exact zero_mem _
      rw [‚Üê pb.natDegree_minpoly, pbgen, ‚Üê natDegree_minpolyDiv_succ hKx,
        ‚Üê Nat.succ_eq_add_one] at hi
      exact le_of_not_gt hi

end

variable (L) {B}

open Polynomial Pointwise in
lemma conductor_mul_differentIdeal
    (x : B) (hx : Algebra.adjoin K {algebraMap B L x} = ‚ä§) :
    (conductor A x) * differentIdeal A B = Ideal.span {aeval x (derivative (minpoly A x))} := by
  classical
  have hAx : IsIntegral A x := IsIntegralClosure.isIntegral A L x
  haveI := IsIntegralClosure.isFractionRing_of_finite_extension A K L B
  apply FractionalIdeal.coeIdeal_injective (K := L)
  simp only [FractionalIdeal.coeIdeal_mul, FractionalIdeal.coeIdeal_span_singleton]
  rw [coeIdeal_differentIdeal A K L B,
    mul_inv_eq_iff_eq_mul‚ÇÄ]
  swap
  ¬∑ exact FractionalIdeal.dual_ne_zero A K one_ne_zero
  apply FractionalIdeal.coeToSubmodule_injective
  simp only [FractionalIdeal.coe_coeIdeal, FractionalIdeal.coe_mul,
    FractionalIdeal.coe_spanSingleton, Submodule.span_singleton_mul]
  ext y
  have hne‚ÇÅ : aeval (algebraMap B L x) (derivative (minpoly K (algebraMap B L x))) ‚â† 0 :=
    (Algebra.IsSeparable.isSeparable _ _).aeval_derivative_ne_zero (minpoly.aeval _ _)
  have : algebraMap B L (aeval x (derivative (minpoly A x))) ‚â† 0 := by
    rwa [minpoly.isIntegrallyClosed_eq_field_fractions K L hAx, derivative_map,
      aeval_map_algebraMap, aeval_algebraMap_apply] at hne‚ÇÅ
  rw [Submodule.mem_smul_iff_inv_mul_mem this, FractionalIdeal.mem_coe, FractionalIdeal.mem_dual,
    mem_coeSubmodule_conductor]
  swap
  ¬∑ exact one_ne_zero
  have hne‚ÇÇ : (aeval (algebraMap B L x) (derivative (minpoly K (algebraMap B L x))))‚Åª¬π ‚â† 0 := by
    rwa [ne_eq, inv_eq_zero]
  have : IsIntegral A (algebraMap B L x) := IsIntegral.map (IsScalarTower.toAlgHom A B L) hAx
  simp_rw [‚Üê Subalgebra.mem_toSubmodule, ‚Üê Submodule.mul_mem_smul_iff (y := y * _)
    (mem_nonZeroDivisors_of_ne_zero hne‚ÇÇ)]
  rw [‚Üê traceForm_dualSubmodule_adjoin A K hx this]
  simp only [LinearMap.BilinForm.mem_dualSubmodule, traceForm_apply, Subalgebra.mem_toSubmodule,
    minpoly.isIntegrallyClosed_eq_field_fractions K L hAx,
    derivative_map, aeval_map_algebraMap, aeval_algebraMap_apply, mul_assoc,
    FractionalIdeal.mem_one_iff, forall_exists_index, forall_apply_eq_imp_iff]
  simp_rw [‚Üê IsScalarTower.toAlgHom_apply A B L x, ‚Üê AlgHom.map_adjoin_singleton]
  simp only [Subalgebra.mem_map, IsScalarTower.coe_toAlgHom', Submodule.one_eq_range,
    forall_exists_index, and_imp, forall_apply_eq_imp_iff‚ÇÇ, ‚Üê map_mul]
  exact ‚ü®fun H b ‚Ü¶ (mul_one b) ‚ñ∏ H b 1 (one_mem _), fun H _ _ _ ‚Ü¶ H _‚ü©

open Polynomial Pointwise in
lemma aeval_derivative_mem_differentIdeal
    (x : B) (hx : Algebra.adjoin K {algebraMap B L x} = ‚ä§) :
    aeval x (derivative (minpoly A x)) ‚àà differentIdeal A B := by
  refine SetLike.le_def.mp ?_ (Ideal.mem_span_singleton_self _)
  rw [‚Üê conductor_mul_differentIdeal A K L x hx]
  exact Ideal.mul_le_left

end IsIntegrallyClosed
section

variable (L)
variable [IsFractionRing B L] [IsDedekindDomain A] [IsDedekindDomain B]
  [NoZeroSMulDivisors A B] [Module.Finite A B]

include K L in
lemma pow_sub_one_dvd_differentIdeal_aux
    {p : Ideal A} [p.IsMaximal] (P : Ideal B) {e : ‚Ñï} (he : e ‚â† 0) (hp : p ‚â† ‚ä•)
    (hP : P ^ e ‚à£ p.map (algebraMap A B)) : P ^ (e - 1) ‚à£ differentIdeal A B := by
  obtain ‚ü®a, ha‚ü© := (pow_dvd_pow _ (Nat.sub_le e 1)).trans hP
  have hp' := (Ideal.map_eq_bot_iff_of_injective
    (FaithfulSMul.algebraMap_injective A B)).not.mpr hp
  have habot : a ‚â† ‚ä• := fun ha' ‚Ü¶ hp' (by simpa [ha'] using ha)
  have hPbot : P ‚â† ‚ä• := by
    rintro rfl; apply hp'
    rwa [‚Üê Ideal.zero_eq_bot, zero_pow he, zero_dvd_iff, Ideal.zero_eq_bot] at hP
  have : p.map (algebraMap A B) ‚à£ a ^ e := by
    obtain ‚ü®b, hb‚ü© := hP
    apply_fun (¬∑ ^ e : Ideal B ‚Üí _) at ha
    apply_fun (¬∑ ^ (e - 1) : Ideal B ‚Üí _) at hb
    simp only [mul_pow, ‚Üê pow_mul, mul_comm e] at ha hb
    conv_lhs at ha => rw [‚Üê Nat.sub_add_cancel (Nat.one_le_iff_ne_zero.mpr he)]
    rw [pow_add, hb, mul_assoc, mul_right_inj' (pow_ne_zero _ hPbot), pow_one, mul_comm] at ha
    exact ‚ü®_, ha.symm‚ü©
  suffices ‚àÄ x ‚àà a, intTrace A B x ‚àà p by
    have hP : ((P ^ (e - 1) :)‚Åª¬π : FractionalIdeal B‚Å∞ L) = a / p.map (algebraMap A B) := by
      apply inv_involutive.injective
      simp only [inv_inv, ha, FractionalIdeal.coeIdeal_mul, inv_div,
          mul_div_assoc]
      rw [div_self (by simpa), mul_one]
    rw [Ideal.dvd_iff_le, differentialIdeal_le_iff (K := K) (L := L) (pow_ne_zero _ hPbot), hP,
      Submodule.map_le_iff_le_comap]
    intro x hx
    rw [Submodule.restrictScalars_mem, FractionalIdeal.mem_coe,
      FractionalIdeal.mem_div_iff_of_nonzero (by simpa using hp')] at hx
    rw [Submodule.mem_comap, LinearMap.coe_restrictScalars, ‚Üê FractionalIdeal.coe_one,
      ‚Üê div_self (G‚ÇÄ := FractionalIdeal A‚Å∞ K) (a := p) (by simpa using hp),
      FractionalIdeal.mem_coe, FractionalIdeal.mem_div_iff_of_nonzero (by simpa using hp)]
    simp only [FractionalIdeal.mem_coeIdeal, forall_exists_index, and_imp,
      forall_apply_eq_imp_iff‚ÇÇ] at hx
    intro y hy'
    obtain ‚ü®y, hy, rfl : algebraMap A K _ = _‚ü© := (FractionalIdeal.mem_coeIdeal _).mp hy'
    obtain ‚ü®z, hz, hz'‚ü© := hx _ (Ideal.mem_map_of_mem _ hy)
    have : trace K L (algebraMap B L z) ‚àà (p : FractionalIdeal A‚Å∞ K) := by
      rw [‚Üê algebraMap_intTrace (A := A)]
      exact ‚ü®intTrace A B z, this z hz, rfl‚ü©
    rwa [mul_comm, ‚Üê smul_eq_mul, ‚Üê LinearMap.map_smul, Algebra.smul_def, mul_comm,
      ‚Üê IsScalarTower.algebraMap_apply, IsScalarTower.algebraMap_apply A B L, ‚Üê hz']
  intros x hx
  rw [‚Üê Ideal.Quotient.eq_zero_iff_mem, ‚Üê trace_quotient_eq_of_isDedekindDomain,
    ‚Üê isNilpotent_iff_eq_zero]
  refine trace_isNilpotent_of_isNilpotent ‚ü®e, ?_‚ü©
  rw [‚Üê map_pow, Ideal.Quotient.eq_zero_iff_mem]
  exact (Ideal.dvd_iff_le.mp this) <| Ideal.pow_mem_pow hx _

lemma pow_sub_one_dvd_differentIdeal [Algebra.IsSeparable (FractionRing A) (FractionRing B)]
    {p : Ideal A} [p.IsMaximal] (P : Ideal B) (e : ‚Ñï) (hp : p ‚â† ‚ä•)
    (hP : P ^ e ‚à£ p.map (algebraMap A B)) : P ^ (e - 1) ‚à£ differentIdeal A B := by
  have : IsLocalization (algebraMapSubmonoid B A‚Å∞) (FractionRing B) :=
    IsIntegralClosure.isLocalization _ (FractionRing A) _ _
  have : FiniteDimensional (FractionRing A) (FractionRing B) := .of_isLocalization A B A‚Å∞
  by_cases he : e = 0
  ¬∑ rw [he, pow_zero]; exact one_dvd _
  exact pow_sub_one_dvd_differentIdeal_aux A (FractionRing A) (FractionRing B) _ he hp hP

theorem not_dvd_differentIdeal_of_intTrace_not_mem
    [Algebra.IsSeparable (FractionRing A) (FractionRing B)]
    {p : Ideal A} (P Q : Ideal B) (hP : P * Q = Ideal.map (algebraMap A B) p)
    (x : B) (hxQ : x ‚àà Q) (hx : Algebra.intTrace A B x ‚àâ p) :
    ¬¨ P ‚à£ differentIdeal A B := by
  by_cases hp : p = ‚ä•
  ¬∑ subst hp
    simp only [Ideal.map_bot, Ideal.mul_eq_bot] at hP
    obtain (rfl | rfl) := hP
    ¬∑ rw [‚Üê Ideal.zero_eq_bot, zero_dvd_iff]
      exact differentIdeal_ne_bot
    ¬∑ obtain rfl := hxQ
      simp at hx
  letI : Algebra (A ‚ß∏ p) (B ‚ß∏ Q) := Ideal.Quotient.algebraQuotientOfLEComap (by
      rw [‚Üê Ideal.map_le_iff_le_comap, ‚Üê hP]
      exact Ideal.mul_le_left)
  let K := FractionRing A
  let L := FractionRing B
  have : IsLocalization (Algebra.algebraMapSubmonoid B A‚Å∞) L :=
    IsIntegralClosure.isLocalization _ K _ _
  have : FiniteDimensional K L := .of_isLocalization A B A‚Å∞
  rw [Ideal.dvd_iff_le]
  intro H
  replace H := (mul_le_mul_right' H Q).trans_eq hP
  replace H := (FractionalIdeal.coeIdeal_le_coeIdeal' _ (P := L) le_rfl).mpr H
  rw [FractionalIdeal.coeIdeal_mul, coeIdeal_differentIdeal A K] at H
  replace H := FractionalIdeal.mul_le_mul_left H (FractionalIdeal.dual A K 1)
  simp only [ne_eq, FractionalIdeal.dual_eq_zero_iff, one_ne_zero, not_false_eq_true,
    mul_inv_cancel_left‚ÇÄ] at H
  apply hx
  suffices Algebra.trace K L (algebraMap B L x) ‚àà (p : FractionalIdeal A‚Å∞ K) by
    obtain ‚ü®y, hy, e‚ü© := this
    rw [‚Üê Algebra.algebraMap_intTrace (A := A), Algebra.linearMap_apply,
      (IsLocalization.injective _ le_rfl).eq_iff] at e
    exact e ‚ñ∏ hy
  refine FractionalIdeal.mul_induction_on (H ‚ü®_, hxQ, rfl‚ü©) ?_ ?_
  ¬∑ rintro x hx _ ‚ü®y, hy, rfl‚ü©
    induction hy using Submodule.span_induction generalizing x with
    | mem y h =>
      obtain ‚ü®y, hy, rfl‚ü© := h
      obtain ‚ü®z, hz‚ü© :=
        (FractionalIdeal.mem_dual (by simp)).mp hx 1 ‚ü®1, trivial, (algebraMap B L).map_one‚ü©
      simp only [Algebra.traceForm_apply, mul_one] at hz
      refine ‚ü®z * y, Ideal.mul_mem_left _ _ hy, ?_‚ü©
      rw [Algebra.linearMap_apply, Algebra.linearMap_apply, mul_comm x,
        ‚Üê IsScalarTower.algebraMap_apply,
        ‚Üê Algebra.smul_def, LinearMap.map_smul_of_tower, ‚Üê hz,
        Algebra.smul_def, map_mul, mul_comm]
    | zero => simp
    | add y z _ _ hy hz =>
      simp only [map_add, mul_add]
      exact Submodule.add_mem _ (hy x hx) (hz x hx)
    | smul y z hz IH =>
      simpa [Algebra.smul_def, mul_assoc, -FractionalIdeal.mem_coeIdeal, mul_left_comm x] using
        IH _ (Submodule.smul_mem _ y hx)
  ¬∑ simp only [map_add]
    exact fun _ _ h‚ÇÅ h‚ÇÇ ‚Ü¶ Submodule.add_mem _ h‚ÇÅ h‚ÇÇ

open nonZeroDivisors

theorem not_dvd_differentIdeal_of_isCoprime_of_isSeparable
    [Algebra.IsSeparable (FractionRing A) (FractionRing B)]
    {p : Ideal A} [p.IsMaximal] (P Q : Ideal B) [P.IsMaximal] [P.LiesOver p]
    (hPQ : IsCoprime P Q) (hP : P * Q = Ideal.map (algebraMap A B) p)
    [Algebra.IsSeparable (A ‚ß∏ p) (B ‚ß∏ P)] :
    ¬¨ P ‚à£ differentIdeal A B := by
  letI : Algebra (A ‚ß∏ p) (B ‚ß∏ Q) := Ideal.Quotient.algebraQuotientOfLEComap (by
      rw [‚Üê Ideal.map_le_iff_le_comap, ‚Üê hP]
      exact Ideal.mul_le_left)
  have : IsScalarTower A (A ‚ß∏ p) (B ‚ß∏ Q) := .of_algebraMap_eq' rfl
  have : Module.Finite (A ‚ß∏ p) (B ‚ß∏ Q) :=
    Module.Finite.of_restrictScalars_finite A (A ‚ß∏ p) (B ‚ß∏ Q)
  letI e : (B ‚ß∏ p.map (algebraMap A B)) ‚âÉ‚Çê[A ‚ß∏ p] ((B ‚ß∏ P) √ó B ‚ß∏ Q) :=
    { __ := (Ideal.quotEquivOfEq hP.symm).trans (Ideal.quotientMulEquivQuotientProd P Q hPQ),
      commutes' := Quotient.ind fun _ ‚Ü¶ rfl }
  obtain ‚ü®x, hx‚ü© : ‚àÉ x, Algebra.trace (A ‚ß∏ p) (B ‚ß∏ P) x ‚â† 0 := by
    simpa [LinearMap.ext_iff] using Algebra.trace_ne_zero (A ‚ß∏ p) (B ‚ß∏ P)
  obtain ‚ü®y, hy‚ü© := Ideal.Quotient.mk_surjective (e.symm (x, 0))
  refine not_dvd_differentIdeal_of_intTrace_not_mem A P Q hP y ?_ ?_
  ¬∑ simpa [e, Ideal.Quotient.eq_zero_iff_mem] using congr((e $hy).2)
  ¬∑ rw [‚Üê Ideal.Quotient.eq_zero_iff_mem, ‚Üê Algebra.trace_quotient_eq_of_isDedekindDomain,
      hy, Algebra.trace_eq_of_algEquiv, Algebra.trace_prod_apply]
    simpa

theorem not_dvd_differentIdeal_of_isCoprime
    [Algebra.IsSeparable (FractionRing A) (FractionRing B)]
    {p : Ideal A} [p.IsMaximal] [Finite (A ‚ß∏ p)] (P Q : Ideal B) [P.IsMaximal]
    (hPQ : IsCoprime P Q) (hP : P * Q = Ideal.map (algebraMap A B) p) :
    ¬¨ P ‚à£ differentIdeal A B := by
  have : P.LiesOver p := by
    constructor
    refine ‚Äπp.IsMaximal‚Ä∫.eq_of_le ?_ ?_
    ¬∑ simpa using ‚ÄπP.IsMaximal‚Ä∫.ne_top
    ¬∑ rw [‚Üê Ideal.map_le_iff_le_comap, ‚Üê hP]
      exact Ideal.mul_le_right
  exact not_dvd_differentIdeal_of_isCoprime_of_isSeparable A P Q hPQ hP

lemma dvd_differentIdeal_of_not_isSeparable
    [Algebra.IsSeparable (FractionRing A) (FractionRing B)]
    {p : Ideal A} [p.IsMaximal] (hp : p ‚â† ‚ä•)
    (P : Ideal B) [P.IsMaximal] [P.LiesOver p]
    (H : ¬¨ Algebra.IsSeparable (A ‚ß∏ p) (B ‚ß∏ P)) : P ‚à£ differentIdeal A B := by
  obtain ‚ü®a, ha‚ü© : P ‚à£ p.map (algebraMap A B) :=
    Ideal.dvd_iff_le.mpr (Ideal.map_le_iff_le_comap.mpr Ideal.LiesOver.over.le)
  by_cases hPa : P ‚à£ a
  ¬∑ simpa using pow_sub_one_dvd_differentIdeal A P 2 hp
      (by rw [pow_two, ha]; exact mul_dvd_mul_left _ hPa)
  let K := FractionRing A
  let L := FractionRing B
  have : IsLocalization (Algebra.algebraMapSubmonoid B A‚Å∞) L :=
    IsIntegralClosure.isLocalization _ K _ _
  have : FiniteDimensional K L := .of_isLocalization A B A‚Å∞
  have hp' := (Ideal.map_eq_bot_iff_of_injective
    (FaithfulSMul.algebraMap_injective A B)).not.mpr hp
  have habot : a ‚â† ‚ä• := fun ha' ‚Ü¶ hp' (by simpa [ha'] using ha)
  have hPbot : P ‚â† ‚ä• := by
    rintro rfl; apply hp'
    rwa [Ideal.bot_mul] at ha
  suffices ‚àÄ x ‚àà a, Algebra.intTrace A B x ‚àà p by
    have hP : ((P :)‚Åª¬π : FractionalIdeal B‚Å∞ L) = a / p.map (algebraMap A B) := by
      apply inv_involutive.injective
      simp only [ha, FractionalIdeal.coeIdeal_mul, inv_div, mul_div_assoc]
      rw [div_self (by simpa), mul_one, inv_inv]
    rw [Ideal.dvd_iff_le, differentialIdeal_le_iff (K := K) (L := L) hPbot, hP,
      Submodule.map_le_iff_le_comap]
    intro x hx
    rw [Submodule.restrictScalars_mem, FractionalIdeal.mem_coe,
      FractionalIdeal.mem_div_iff_of_nonzero (by simpa using hp')] at hx
    rw [Submodule.mem_comap, LinearMap.coe_restrictScalars, ‚Üê FractionalIdeal.coe_one,
      ‚Üê div_self (G‚ÇÄ := FractionalIdeal A‚Å∞ K) (a := p) (by simpa using hp),
      FractionalIdeal.mem_coe, FractionalIdeal.mem_div_iff_of_nonzero (by simpa using hp)]
    simp only [FractionalIdeal.mem_coeIdeal, forall_exists_index, and_imp,
      forall_apply_eq_imp_iff‚ÇÇ] at hx
    intro y hy'
    obtain ‚ü®y, hy, rfl : algebraMap A K _ = _‚ü© := (FractionalIdeal.mem_coeIdeal _).mp hy'
    obtain ‚ü®z, hz, hz'‚ü© := hx _ (Ideal.mem_map_of_mem _ hy)
    have : Algebra.trace K L (algebraMap B L z) ‚àà (p : FractionalIdeal A‚Å∞ K) := by
      rw [‚Üê Algebra.algebraMap_intTrace (A := A)]
      exact ‚ü®Algebra.intTrace A B z, this z hz, rfl‚ü©
    rwa [mul_comm, ‚Üê smul_eq_mul, ‚Üê LinearMap.map_smul, Algebra.smul_def, mul_comm,
      ‚Üê IsScalarTower.algebraMap_apply, IsScalarTower.algebraMap_apply A B L, ‚Üê hz']
  intros x hx
  rw [‚Üê Ideal.Quotient.eq_zero_iff_mem, ‚Üê Algebra.trace_quotient_eq_of_isDedekindDomain]
  letI : Algebra (A ‚ß∏ p) (B ‚ß∏ a) :=
    Ideal.Quotient.algebraQuotientOfLEComap (Ideal.map_le_iff_le_comap.mp
      (Ideal.dvd_iff_le.mp ‚ü®_, ha.trans (mul_comm _ _)‚ü©))
  have : IsScalarTower A (A ‚ß∏ p) (B ‚ß∏ a) := .of_algebraMap_eq' rfl
  have : Module.Finite (A ‚ß∏ p) (B ‚ß∏ a) := .of_restrictScalars_finite A _ _
  have := ((Ideal.prime_iff_isPrime hPbot).mpr inferInstance)
  rw [‚Üê this.irreducible.gcd_eq_one_iff, ‚Üê Ideal.isCoprime_iff_gcd] at hPa
  letI e : (B ‚ß∏ p.map (algebraMap A B)) ‚âÉ‚Çê[A ‚ß∏ p] ((B ‚ß∏ P) √ó B ‚ß∏ a) :=
    { __ := (Ideal.quotEquivOfEq ha).trans (Ideal.quotientMulEquivQuotientProd P a hPa),
      commutes' := Quotient.ind fun _ ‚Ü¶ rfl }
  have hx' : (e (Ideal.Quotient.mk _ x)).2 = 0 := by
    simpa [e, Ideal.Quotient.eq_zero_iff_mem]
  rw [‚Üê Algebra.trace_eq_of_algEquiv e, Algebra.trace_prod_apply,
    Algebra.trace_eq_zero_of_not_isSeparable H, LinearMap.zero_apply, zero_add, hx', map_zero]

variable {A}

/-- A prime does not divide the different ideal iff it is unramified. -/
theorem not_dvd_differentIdeal_iff
    [Algebra.IsSeparable (FractionRing A) (FractionRing B)] {P : Ideal B} [P.IsPrime] :
    ¬¨ P ‚à£ differentIdeal A B ‚Üî Algebra.IsUnramifiedAt A P := by
  classical
  rcases eq_or_ne P ‚ä• with rfl | hPbot
  ¬∑ simp_rw [‚Üê Ideal.zero_eq_bot, zero_dvd_iff]
    simp only [Submodule.zero_eq_bot, differentIdeal_ne_bot, not_false_eq_true, true_iff]
    let K := FractionRing A
    let L := FractionRing B
    have : FiniteDimensional K L := .of_isLocalization A B A‚Å∞
    have : IsLocalization B‚Å∞ (Localization.AtPrime (‚ä• : Ideal B)) := by
      convert (inferInstanceAs
        (IsLocalization (‚ä• : Ideal B).primeCompl (Localization.AtPrime (‚ä• : Ideal B))))
      ext; simp [Ideal.primeCompl]
    refine (Algebra.FormallyUnramified.iff_of_equiv (A := L)
      ((IsLocalization.algEquiv B‚Å∞ _ _).restrictScalars A)).mp ?_
    have : Algebra.FormallyUnramified K L := by
      rwa [Algebra.FormallyUnramified.iff_isSeparable]
    refine .comp A K L
  have hp : P.under A ‚â† ‚ä• := mt Ideal.eq_bot_of_comap_eq_bot hPbot
  have hp' := (Ideal.map_eq_bot_iff_of_injective
    (FaithfulSMul.algebraMap_injective A B)).not.mpr hp
  have := Ideal.IsPrime.isMaximal inferInstance hPbot
  constructor
  ¬∑ intro H
    ¬∑ rw [Algebra.isUnramifiedAt_iff_map_eq (p := P.under A)]
      constructor
      ¬∑ suffices Algebra.IsSeparable (A ‚ß∏ P.under A) (B ‚ß∏ P) by infer_instance
        contrapose! H
        exact dvd_differentIdeal_of_not_isSeparable A hp P H
      ¬∑ rw [‚Üê Ideal.IsDedekindDomain.ramificationIdx_eq_one_iff hPbot Ideal.map_comap_le]
        apply Ideal.ramificationIdx_spec
        ¬∑ simp [Ideal.map_le_iff_le_comap]
        ¬∑ contrapose! H
          rw [‚Üê pow_one P, show 1 = 2 - 1 by norm_num]
          apply pow_sub_one_dvd_differentIdeal _ _ _ hp
          simpa [Ideal.dvd_iff_le] using H
  ¬∑ intro H
    obtain ‚ü®Q, h‚ÇÅ, h‚ÇÇ‚ü© := Ideal.eq_prime_pow_mul_coprime hp' P
    rw [‚Üê Ideal.IsDedekindDomain.ramificationIdx_eq_normalizedFactors_count hp' ‚Äπ_‚Ä∫ hPbot,
      Ideal.ramificationIdx_eq_one_of_isUnramifiedAt hPbot, pow_one] at h‚ÇÇ
    obtain ‚ü®h‚ÇÉ, h‚ÇÑ‚ü© := (Algebra.isUnramifiedAt_iff_map_eq (p := P.under A) _ _).mp H
    exact not_dvd_differentIdeal_of_isCoprime_of_isSeparable
      A P Q (Ideal.isCoprime_iff_sup_eq.mpr h‚ÇÅ) h‚ÇÇ.symm

/-- A prime divides the different ideal iff it is ramified. -/
theorem dvd_differentIdeal_iff
    [Algebra.IsSeparable (FractionRing A) (FractionRing B)] {P : Ideal B} [P.IsPrime] :
    P ‚à£ differentIdeal A B ‚Üî ¬¨ Algebra.IsUnramifiedAt A P :=
  iff_not_comm.mp not_dvd_differentIdeal_iff.symm

end
