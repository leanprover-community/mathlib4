/-
Copyright (c) 2021 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Eric Wieser
-/
import Mathlib.RingTheory.Ideal.Basic
import Mathlib.RingTheory.Ideal.Operations
import Mathlib.LinearAlgebra.Finsupp
import Mathlib.RingTheory.GradedAlgebra.Basic
import Mathlib.Algebra.Module.GradedModule
import Mathlib.RingTheory.Finiteness

#align_import ring_theory.graded_algebra.homogeneous_ideal from "leanprover-community/mathlib"@"4e861f25ba5ceef42ba0712d8ffeb32f38ad6441"

/-!
# Homogeneous submodules of a graded module

This file defines homogeneous submodule of a graded module `â¨áµ¢ â„³áµ¢` over graded ring `â¨áµ¢ ğ’œáµ¢` and
operations on them.

## Main definitions

For any `p : Submodule A M`:
* `Submodule.IsHomogeneous â„³ p`: The property that a submodule is closed under `GradedModule.proj`.
* `HomogeneousSubmodule A â„³`: The structure extending submodules which satisfy
  `Submodule.IsHomogeneous`.
* `Submodule.homogeneousCore p ğ’œ â„³`: The largest homogeneous submodule smaller than `p`.
* `Submodule.homogeneousHull I ğ’œ â„³`: The smallest homogeneous ideal larger than `p`.

## Main statements

* `HomogeneousSubmodule.completeLattice`: `Submodule.IsHomogeneous` is preserved by `âŠ¥`, `âŠ¤`, `âŠ”`,
  `âŠ“`, `â¨†`, `â¨…`, and so the subtype of homogeneous ideals inherits a complete lattice structure.
* `Submodule.homogeneousCore.gi`: `Submodule.homogeneousCore` forms a galois insertion with
  coercion.
* `Submodule.homogeneousHull.gi`: `Submodule.homogeneousHull` forms a galois insertion with
  coercion.

## Implementation notes

We introduce `Submodule.homogeneousCore'` earlier than might be expected so that we can get access
to `Submodule.IsHomogeneous.iff_exists` as quickly as possible.

## Tags

graded algebra, homogeneous
-/


open SetLike DirectSum Set

open BigOperators Pointwise DirectSum

variable {Î¹A Î¹M ÏƒA ÏƒM R A M : Type*}
variable [SetLike ÏƒA A] [SetLike ÏƒM M] [AddCommMonoid M] [AddSubmonoidClass ÏƒM M]
variable [DecidableEq Î¹A] [DecidableEq Î¹M]

variable (ğ’œ : Î¹A â†’ ÏƒA) (â„³ : Î¹M â†’ ÏƒM)
variable [Decomposition â„³]

section HomogeneousDef

variable [Semiring A] [Module A M]
variable [SetLike ÏƒA A] [AddSubmonoidClass ÏƒA A]
variable [DecidableEq Î¹A] [AddMonoid Î¹A] [GradedRing ğ’œ]

variable (p : Submodule A M) (I : Ideal A)

/-- An `p : Submodule A M` is homogeneous if for every `m âˆˆ p`, all homogeneous components
  of `m` are in `I`. -/
def Submodule.IsHomogeneous : Prop :=
  âˆ€ (i : Î¹M) â¦ƒm : Mâ¦„, m âˆˆ p â†’ (DirectSum.decompose â„³ m i : M) âˆˆ p
#align ideal.is_homogeneous Submodule.IsHomogeneous

/-- An `I : Ideal A` is homogeneous if for every `r âˆˆ I`, all homogeneous components
  of `r` are in `I`. -/
def Ideal.IsHomogeneous : Prop :=
  Submodule.IsHomogeneous ğ’œ I

variable (A) in
/-- For any `Semiring A`, we collect the homogeneous submodule of `A`-modules into a type. -/
structure HomogeneousSubmodule extends Submodule A M where
  is_homogeneous' : Submodule.IsHomogeneous â„³ toSubmodule
#align homogeneous_ideal HomogeneousSubmodule


/-- For any `Semiring A`, we collect the homogeneous ideals of `A` into a type. -/
def HomogeneousIdeal := HomogeneousSubmodule A ğ’œ

variable {ğ’œ â„³}

/-- Converting a homogeneous ideal to an ideal. -/
def HomogeneousIdeal.toIdeal (I : HomogeneousIdeal ğ’œ) : Ideal A :=
  I.toSubmodule
#align homogeneous_ideal.to_ideal HomogeneousIdeal.toIdeal

lemma HomogeneousIdeal.isHomogeneous (I : HomogeneousIdeal ğ’œ) : I.toIdeal.IsHomogeneous ğ’œ := I.2
#align homogeneous_ideal.is_homogeneous HomogeneousIdeal.isHomogeneous

theorem HomogeneousSubmodule.isHomogeneous (I : HomogeneousSubmodule A â„³) :
    I.toSubmodule.IsHomogeneous â„³ :=
  I.is_homogeneous'

theorem HomogeneousSubmodule.toSubmodule_injective :
    Function.Injective
      (HomogeneousSubmodule.toSubmodule : HomogeneousSubmodule A â„³â†’ Submodule A M) :=
  fun âŸ¨x, hxâŸ© âŸ¨y, hyâŸ© => fun (h : x = y) => by simp [h]

lemma HomogeneousIdeal.toIdeal_injective :
    Function.Injective (HomogeneousIdeal.toIdeal : HomogeneousIdeal ğ’œ â†’ Ideal A) :=
  HomogeneousSubmodule.toSubmodule_injective
#align homogeneous_ideal.to_ideal_injective HomogeneousIdeal.toIdeal_injective

instance HomogeneousSubmodule.setLike : SetLike (HomogeneousSubmodule A â„³) M where
  coe p := p.toSubmodule
  coe_injective' _ _ h := HomogeneousSubmodule.toSubmodule_injective <| SetLike.coe_injective h

instance HomogeneousIdeal.setLike : SetLike (HomogeneousIdeal ğ’œ) A := HomogeneousSubmodule.setLike
#align homogeneous_ideal.set_like HomogeneousIdeal.setLike

@[ext]
theorem HomogeneousSubmodule.ext
    {I J : HomogeneousSubmodule A â„³} (h : I.toSubmodule = J.toSubmodule) : I = J :=
  HomogeneousSubmodule.toSubmodule_injective h

@[ext]
lemma HomogeneousIdeal.ext
    {I J : HomogeneousIdeal ğ’œ} (h : I.toIdeal = J.toIdeal) : I = J :=
  HomogeneousSubmodule.ext h
#align homogeneous_ideal.ext HomogeneousIdeal.ext

@[simp]
theorem HomogeneousSubmodule.mem_iff {I : HomogeneousSubmodule A â„³} {x : M} :
    x âˆˆ I.toSubmodule â†” x âˆˆ I :=
  Iff.rfl

@[simp]
lemma HomogeneousIdeal.mem_iff {I : HomogeneousIdeal ğ’œ} {x : A} :
    x âˆˆ I.toIdeal â†” x âˆˆ I :=
  Iff.rfl
#align homogeneous_ideal.mem_iff HomogeneousIdeal.mem_iff

end HomogeneousDef

section HomogeneousCore

variable [Semiring A] [Module A M]

variable (p : Submodule A M) (I : Ideal A)

/-- For any `p : Submodule A M`, not necessarily homogeneous, `p.homogeneousCore' â„³`
is the largest homogeneous `A`-submodule contained in `p`, as an `A`-submodule. -/
def Submodule.homogeneousCore' (I : Submodule A M) : Submodule A M :=
  Submodule.span A ((â†‘) '' (((â†‘) : Subtype (Homogeneous â„³) â†’ M) â»Â¹' I))

/-- For any `I : Ideal A`, not necessarily homogeneous, `I.homogeneousCore' ğ’œ`
is the largest homogeneous ideal of `A` contained in `I`, as an ideal. -/
def Ideal.homogeneousCore' (I : Ideal A) : Ideal A :=
  Submodule.homogeneousCore' ğ’œ I
#align ideal.homogeneous_core' Ideal.homogeneousCore'

theorem Submodule.homogeneousCore'_mono : Monotone (Submodule.homogeneousCore' (A := A) â„³) :=
  fun _ _ I_le_J => Submodule.span_mono <| Set.image_subset _ fun _ => @I_le_J _

lemma Ideal.homogeneousCore'_mono : Monotone (Ideal.homogeneousCore' ğ’œ) :=
  Submodule.homogeneousCore'_mono ğ’œ
#align ideal.homogeneous_core'_mono Ideal.homogeneousCore'_mono

theorem Submodule.homogeneousCore'_le : p.homogeneousCore' â„³ â‰¤ p :=
  Submodule.span_le.2 <| image_preimage_subset _ _

lemma Ideal.homogeneousCore'_le : I.homogeneousCore' ğ’œ â‰¤ I :=
  Submodule.homogeneousCore'_le ğ’œ I
#align ideal.homogeneous_core'_le Submodule.homogeneousCore'_le

end HomogeneousCore

section IsHomogeneousSubmoduleDefs

variable [AddMonoid Î¹A] [SetLike ÏƒA A] [SetLike ÏƒA A]

variable [Semiring A] [AddSubmonoidClass ÏƒA A] [Module A M] [GradedRing ğ’œ]
variable [VAdd Î¹A Î¹M] [GradedSMul ğ’œ â„³]

variable (p : Submodule A M) (I : Ideal A)

theorem Submodule.isHomogeneous_iff_forall_subset :
    p.IsHomogeneous â„³ â†” âˆ€ i, (p : Set M) âŠ† GradedModule.proj â„³ i â»Â¹' (p : Set M) :=
  Iff.rfl

lemma Ideal.isHomogeneous_iff_forall_subset :
    I.IsHomogeneous ğ’œ â†” âˆ€ i, (I : Set A) âŠ† GradedRing.proj ğ’œ i â»Â¹' (I : Set A) :=
  Iff.rfl
#align ideal.is_homogeneous_iff_forall_subset Ideal.isHomogeneous_iff_forall_subset

theorem Submodule.isHomogeneous_iff_subset_iInter :
    p.IsHomogeneous â„³ â†” (p : Set M) âŠ† â‹‚ i, GradedModule.proj â„³ i â»Â¹' â†‘p :=
  subset_iInter_iff.symm

lemma Ideal.isHomogeneous_iff_subset_iInter :
    I.IsHomogeneous ğ’œ â†” (I : Set A) âŠ† â‹‚ i, GradedRing.proj ğ’œ i â»Â¹' â†‘I :=
  subset_iInter_iff.symm
#align ideal.is_homogeneous_iff_subset_Inter Ideal.isHomogeneous_iff_subset_iInter

theorem Submodule.smul_homogeneous_element_mem_of_mem {p : Submodule A M} (r : A) (x : M)
    (hxâ‚ : Homogeneous â„³ x) (hxâ‚‚ : x âˆˆ p) (j : Î¹M) : GradedModule.proj â„³ j (r â€¢ x) âˆˆ p := by
  classical
  rw [â† DirectSum.sum_support_decompose ğ’œ r, Finset.sum_smul, map_sum]
  apply Submodule.sum_mem
  intro k _
  obtain âŸ¨i, hiâŸ© := hxâ‚
  have memâ‚ : (DirectSum.decompose ğ’œ r k : A) â€¢ x âˆˆ â„³ (k +áµ¥ i) :=
    GradedSMul.smul_mem (SetLike.coe_mem _) hi
  erw [GradedModule.proj_apply, DirectSum.decompose_of_mem â„³ memâ‚, coe_of_apply]
  split_ifs with h
  Â· exact Submodule.smul_mem _ _ hxâ‚‚
  Â· exact p.zero_mem

lemma Ideal.mul_homogeneous_element_mem_of_mem {I : Ideal A} (r x : A)
    (hxâ‚ : Homogeneous ğ’œ x) (hxâ‚‚ : x âˆˆ I) (j : Î¹A) : GradedRing.proj ğ’œ j (r * x) âˆˆ I :=
  Submodule.smul_homogeneous_element_mem_of_mem ğ’œ ğ’œ r x hxâ‚ hxâ‚‚ j
#align ideal.mul_homogeneous_element_mem_of_mem Ideal.mul_homogeneous_element_mem_of_mem

theorem Submodule.homogeneous_span (s : Set M) (h : âˆ€ x âˆˆ s, Homogeneous â„³ x) :
    (Submodule.span A s).IsHomogeneous â„³ := by
  rintro i r hr
  rw [mem_span_set] at hr
  obtain âŸ¨c, hc, rflâŸ© := hr
  rw [ Finsupp.sum, decompose_sum, DFinsupp.finset_sum_apply, AddSubmonoidClass.coe_finset_sum]
  refine' Submodule.sum_mem _ _
  rintro z hz1
  apply Submodule.smul_homogeneous_element_mem_of_mem (ğ’œ := ğ’œ) (â„³ := â„³)
  Â· exact h _ (hc hz1)
  Â· exact Submodule.subset_span (hc hz1)

lemma Ideal.homogeneous_span (s : Set A) (h : âˆ€ x âˆˆ s, Homogeneous ğ’œ x) :
    (Ideal.span s).IsHomogeneous ğ’œ :=
  Submodule.homogeneous_span ğ’œ ğ’œ s h
#align ideal.is_homogeneous_span Ideal.homogeneous_span

/-- For any `p : Submodule A M`, not necessarily homogeneous, `p.homogeneousCore' â„³`
is the largest homogeneous `A`-submodule contained in `p`. -/
def Submodule.homogeneousCore : HomogeneousSubmodule A â„³ :=
  âŸ¨p.homogeneousCore' â„³,
    Submodule.homogeneous_span ğ’œ _ _ fun _ h => (Subtype.image_preimage_coe _ _ â–¸ h).2âŸ©

/-- For any `I : Ideal A`, not necessarily homogeneous, `I.homogeneousCore' ğ’œ`
is the largest homogeneous ideal of `A` contained in `I`. -/
def Ideal.homogeneousCore : HomogeneousIdeal ğ’œ := Submodule.homogeneousCore ğ’œ ğ’œ I
#align ideal.homogeneous_core Ideal.homogeneousCore

theorem Submodule.homogeneousCore_mono : Monotone (Submodule.homogeneousCore ğ’œ â„³) :=
  Submodule.homogeneousCore'_mono â„³

lemma Ideal.homogeneousCore_mono : Monotone (Ideal.homogeneousCore ğ’œ) :=
  Ideal.homogeneousCore'_mono ğ’œ
#align ideal.homogeneous_core_mono Ideal.homogeneousCore_mono

theorem Submodule.toSubmodule_homogeneousCore_le : (p.homogeneousCore ğ’œ â„³).toSubmodule â‰¤ p :=
  Submodule.homogeneousCore'_le â„³ p

lemma Ideal.toIdeal_homogeneousCore_le : (I.homogeneousCore ğ’œ).toIdeal â‰¤ I :=
  Ideal.homogeneousCore'_le ğ’œ I
#align ideal.to_ideal_homogeneous_core_le Ideal.toIdeal_homogeneousCore_le

variable {â„³ I}

theorem Submodule.mem_homogeneousCore_of_homogeneous_of_mem {x : M} (h : Homogeneous â„³ x)
    (hmem : x âˆˆ p) : x âˆˆ p.homogeneousCore ğ’œ â„³ :=
  Submodule.subset_span âŸ¨âŸ¨x, hâŸ©, hmem, rflâŸ©

variable {ğ’œ}
lemma Ideal.mem_homogeneousCore_of_homogeneous_of_mem {x : A} (h : Homogeneous ğ’œ x) (hmem : x âˆˆ I) :
    x âˆˆ I.homogeneousCore ğ’œ :=
  Submodule.mem_homogeneousCore_of_homogeneous_of_mem ğ’œ I h hmem
#align ideal.mem_homogeneous_core_of_is_homogeneous_of_mem Ideal.mem_homogeneousCore_of_homogeneous_of_mem

theorem Submodule.IsHomogeneous.toSubmodule_homogeneousCore_eq_self (h : p.IsHomogeneous â„³) :
    (p.homogeneousCore ğ’œ â„³).toSubmodule = p := by
  apply le_antisymm (p.homogeneousCore'_le â„³) _
  intro x hx
  classical
  rw [â† DirectSum.sum_support_decompose â„³ x]
  exact Submodule.sum_mem _ fun j _ => Submodule.subset_span âŸ¨âŸ¨_, homogeneous_coe _âŸ©, h _ hx, rflâŸ©

lemma Ideal.IsHomogeneous.toIdeal_homogeneousCore_eq_self (h : I.IsHomogeneous ğ’œ) :
    (I.homogeneousCore ğ’œ).toIdeal = I :=
  Submodule.IsHomogeneous.toSubmodule_homogeneousCore_eq_self I h
#align ideal.is_homogeneous.to_ideal_homogeneous_core_eq_self Ideal.IsHomogeneous.toIdeal_homogeneousCore_eq_self

@[simp]
theorem HomogeneousSubmodule.toSubmodule_homogeneousCore_eq_self (p : HomogeneousSubmodule A â„³) :
    p.toSubmodule.homogeneousCore ğ’œ â„³ = p := by
  ext1
  convert Submodule.IsHomogeneous.toSubmodule_homogeneousCore_eq_self _ p.isHomogeneous

@[simp]
lemma HomogeneousIdeal.toIdeal_homogeneousCore_eq_self (I : HomogeneousIdeal ğ’œ) :
    I.toIdeal.homogeneousCore ğ’œ = I :=
  HomogeneousSubmodule.toSubmodule_homogeneousCore_eq_self I
#align homogeneous_ideal.to_ideal_homogeneous_core_eq_self HomogeneousIdeal.toIdeal_homogeneousCore_eq_self

variable (ğ’œ I)

theorem Submodule.IsHomogeneous.iff_eq :
    p.IsHomogeneous â„³ â†” (p.homogeneousCore ğ’œ â„³).toSubmodule = p :=
  âŸ¨fun hI => hI.toSubmodule_homogeneousCore_eq_self, fun hI => hI â–¸ (p.homogeneousCore ğ’œ â„³).2âŸ©

lemma Ideal.IsHomogeneous.iff_eq : I.IsHomogeneous ğ’œ â†” (I.homogeneousCore ğ’œ).toIdeal = I :=
  Submodule.IsHomogeneous.iff_eq ğ’œ I
#align ideal.is_homogeneous.iff_eq Ideal.IsHomogeneous.iff_eq

theorem Submodule.IsHomogeneous.iff_exists :
    p.IsHomogeneous â„³ â†” âˆƒ S : Set {x // Homogeneous â„³ x}, p = Submodule.span A ((â†‘) '' S) := by
  rw [Submodule.IsHomogeneous.iff_eq ğ’œ, eq_comm]
  exact ((Set.image_preimage.compose (Submodule.gi _ _).gc).exists_eq_l _).symm

theorem Ideal.IsHomogeneous.iff_exists :
    I.IsHomogeneous ğ’œ â†” âˆƒ S : Set (homogeneousSubmonoid ğ’œ), I = Ideal.span ((â†‘) '' S) :=
  Submodule.IsHomogeneous.iff_exists ğ’œ I
#align ideal.is_homogeneous.iff_exists Ideal.IsHomogeneous.iff_exists

end IsHomogeneousSubmoduleDefs

/-! ### Operations

In this section, we show that `Ideal.IsHomogeneous` is preserved by various notations, then use
these results to provide these notation typeclasses for `HomogeneousSubmodule`. -/


section Operations

section Semiring

variable [Semiring A] [Module A M]

variable [AddMonoid Î¹A] [SetLike ÏƒA A] [AddSubmonoidClass ÏƒA A]
variable [GradedRing ğ’œ] [VAdd Î¹A Î¹M] [GradedSMul ğ’œ â„³]

namespace Submodule.IsHomogeneous

theorem bot : Submodule.IsHomogeneous (A := A) â„³ âŠ¥ := fun i r hr => by
  simp only [Submodule.mem_bot] at hr
  rw [hr, decompose_zero, zero_apply]
  apply Submodule.zero_mem

theorem top : Submodule.IsHomogeneous (A := A) â„³ âŠ¤ := fun i r _ => by simp only [Submodule.mem_top]

variable {ğ’œ â„³}

theorem inf {I J : Submodule A M} (HI : I.IsHomogeneous â„³) (HJ : J.IsHomogeneous â„³) :
    (I âŠ“ J).IsHomogeneous â„³ :=
  fun _ _ hr => âŸ¨HI _ hr.1, HJ _ hr.2âŸ©

theorem sup {I J : Submodule A M} (HI : I.IsHomogeneous â„³) (HJ : J.IsHomogeneous â„³) :
    (I âŠ” J).IsHomogeneous â„³ := by
  rw [iff_exists (ğ’œ := ğ’œ) (â„³ := â„³)] at HI HJ âŠ¢
  obtain âŸ¨âŸ¨sâ‚, rflâŸ©, âŸ¨sâ‚‚, rflâŸ©âŸ© := HI, HJ
  refine' âŸ¨sâ‚ âˆª sâ‚‚, _âŸ©
  rw [Set.image_union]
  exact (Submodule.span_union _ _).symm

protected theorem iSup {Îº : Sort*} {f : Îº â†’ Submodule A M} (h : âˆ€ i, (f i).IsHomogeneous â„³) :
    (â¨† i, f i).IsHomogeneous â„³ := by
  simp_rw [iff_exists (ğ’œ := ğ’œ) (â„³ := â„³)] at h âŠ¢
  choose s hs using h
  refine' âŸ¨â‹ƒ i, s i, _âŸ©
  simp_rw [Set.image_iUnion, Submodule.span_iUnion]
  congr
  exact funext hs

protected theorem iInf {Îº : Sort*} {f : Îº â†’ Submodule A M} (h : âˆ€ i, (f i).IsHomogeneous â„³) :
    (â¨… i, f i).IsHomogeneous â„³ := by
  intro i x hx
  simp only [Submodule.mem_iInf] at hx âŠ¢
  exact fun j => h _ _ (hx j)

theorem iSupâ‚‚ {Îº : Sort*} {Îº' : Îº â†’ Sort*} {f : âˆ€ i, Îº' i â†’ Submodule A M}
    (h : âˆ€ i j, (f i j).IsHomogeneous â„³) : (â¨† (i) (j), f i j).IsHomogeneous â„³ :=
  IsHomogeneous.iSup (ğ’œ := ğ’œ) fun i => IsHomogeneous.iSup (ğ’œ := ğ’œ) <| h i

theorem iInfâ‚‚ {Îº : Sort*} {Îº' : Îº â†’ Sort*} {f : âˆ€ i, Îº' i â†’ Submodule A M}
    (h : âˆ€ i j, (f i j).IsHomogeneous â„³) : (â¨… (i) (j), f i j).IsHomogeneous â„³ :=
  IsHomogeneous.iInf fun i => IsHomogeneous.iInf <| h i

theorem sSup {â„ : Set (Submodule A M)} (h : âˆ€ I âˆˆ â„, I.IsHomogeneous â„³) :
    (sSup â„).IsHomogeneous â„³ := by
  rw [sSup_eq_iSup]
  exact iSupâ‚‚ (ğ’œ := ğ’œ) h

theorem sInf {â„ : Set (Submodule A M)} (h : âˆ€ I âˆˆ â„, I.IsHomogeneous â„³) :
    (sInf â„).IsHomogeneous â„³ := by
  rw [sInf_eq_iInf]
  exact iInfâ‚‚ h

end Submodule.IsHomogeneous

namespace Ideal.IsHomogeneous

theorem bot : Ideal.IsHomogeneous ğ’œ âŠ¥ := Submodule.IsHomogeneous.bot ğ’œ
#align ideal.is_homogeneous.bot Ideal.IsHomogeneous.bot

theorem top : Ideal.IsHomogeneous ğ’œ âŠ¤ := Submodule.IsHomogeneous.top ğ’œ
#align ideal.is_homogeneous.top Ideal.IsHomogeneous.top

variable {ğ’œ}

theorem inf {I J : Ideal A} (HI : I.IsHomogeneous ğ’œ) (HJ : J.IsHomogeneous ğ’œ) :
    (I âŠ“ J).IsHomogeneous ğ’œ := Submodule.IsHomogeneous.inf HI HJ
#align ideal.is_homogeneous.inf Ideal.IsHomogeneous.inf

theorem sup {I J : Ideal A} (HI : I.IsHomogeneous ğ’œ) (HJ : J.IsHomogeneous ğ’œ) :
    (I âŠ” J).IsHomogeneous ğ’œ := Submodule.IsHomogeneous.sup (ğ’œ := ğ’œ) HI HJ
#align ideal.is_homogeneous.sup Ideal.IsHomogeneous.sup

protected theorem iSup {Îº : Sort*} {f : Îº â†’ Ideal A} (h : âˆ€ i, (f i).IsHomogeneous ğ’œ) :
    (â¨† i, f i).IsHomogeneous ğ’œ := Submodule.IsHomogeneous.iSup (ğ’œ := ğ’œ) h
#align ideal.is_homogeneous.supr Ideal.IsHomogeneous.iSup

protected theorem iInf {Îº : Sort*} {f : Îº â†’ Ideal A} (h : âˆ€ i, (f i).IsHomogeneous ğ’œ) :
    (â¨… i, f i).IsHomogeneous ğ’œ := Submodule.IsHomogeneous.iInf h
#align ideal.is_homogeneous.infi Ideal.IsHomogeneous.iInf

theorem iSupâ‚‚ {Îº : Sort*} {Îº' : Îº â†’ Sort*} {f : âˆ€ i, Îº' i â†’ Ideal A}
    (h : âˆ€ i j, (f i j).IsHomogeneous ğ’œ) : (â¨† (i) (j), f i j).IsHomogeneous ğ’œ :=
  Submodule.IsHomogeneous.iSupâ‚‚ (ğ’œ := ğ’œ) h
#align ideal.is_homogeneous.suprâ‚‚ Ideal.IsHomogeneous.iSupâ‚‚

theorem iInfâ‚‚ {Îº : Sort*} {Îº' : Îº â†’ Sort*} {f : âˆ€ i, Îº' i â†’ Ideal A}
    (h : âˆ€ i j, (f i j).IsHomogeneous ğ’œ) : (â¨… (i) (j), f i j).IsHomogeneous ğ’œ :=
  Submodule.IsHomogeneous.iInfâ‚‚ h
#align ideal.is_homogeneous.infiâ‚‚ Submodule.IsHomogeneous.iInfâ‚‚

theorem sSup {â„ : Set (Ideal A)} (h : âˆ€ I âˆˆ â„, I.IsHomogeneous ğ’œ) :
    (sSup â„).IsHomogeneous ğ’œ := Submodule.IsHomogeneous.sSup (ğ’œ := ğ’œ) h
#align ideal.is_homogeneous.Sup Submodule.IsHomogeneous.sSup

theorem sInf {â„ : Set (Ideal A)} (h : âˆ€ I âˆˆ â„, I.IsHomogeneous ğ’œ) :
    (sInf â„).IsHomogeneous ğ’œ := Submodule.IsHomogeneous.sInf h
#align ideal.is_homogeneous.Inf Submodule.IsHomogeneous.sInf

end Ideal.IsHomogeneous

variable {ğ’œ â„³}

namespace HomogeneousSubmodule

instance : PartialOrder (HomogeneousSubmodule A â„³) :=
  SetLike.instPartialOrder

instance : Top (HomogeneousSubmodule A â„³) :=
  âŸ¨âŸ¨âŠ¤, Submodule.IsHomogeneous.top â„³âŸ©âŸ©

instance : Bot (HomogeneousSubmodule A â„³) :=
  âŸ¨âŸ¨âŠ¥, Submodule.IsHomogeneous.bot â„³âŸ©âŸ©

set_option synthInstance.checkSynthOrder false in
instance sup : Sup (HomogeneousSubmodule A â„³) :=
  âŸ¨fun I J => âŸ¨I.toSubmodule âŠ” J.toSubmodule, I.isHomogeneous.sup (ğ’œ := ğ’œ) J.isHomogeneousâŸ©âŸ©

instance : Inf (HomogeneousSubmodule A â„³) :=
  âŸ¨fun I J => âŸ¨_, I.isHomogeneous.inf J.isHomogeneousâŸ©âŸ©

set_option synthInstance.checkSynthOrder false in
instance supSet : SupSet (HomogeneousSubmodule A â„³) :=
  âŸ¨fun S => âŸ¨â¨† s âˆˆ S, toSubmodule s, Submodule.IsHomogeneous.iSupâ‚‚ (ğ’œ := ğ’œ)
    fun s _ => s.isHomogeneousâŸ©âŸ©

instance : InfSet (HomogeneousSubmodule A â„³) :=
  âŸ¨fun S => âŸ¨â¨… s âˆˆ S, toSubmodule s, Submodule.IsHomogeneous.iInfâ‚‚ fun s _ => s.isHomogeneousâŸ©âŸ©

@[simp]
theorem coe_top : ((âŠ¤ : HomogeneousSubmodule A â„³) : Set M) = univ :=
  rfl

@[simp]
theorem coe_bot : ((âŠ¥ : HomogeneousSubmodule A â„³) : Set M) = 0 :=
  rfl

@[simp]
theorem coe_sup (I J : HomogeneousSubmodule A â„³) : â†‘(I âŠ” J) = (I + J : Set M) :=
  Submodule.coe_sup _ _

@[simp, nolint simpNF]
theorem coe_inf (I J : HomogeneousSubmodule A â„³) : (â†‘(I âŠ“ J) : Set M) = â†‘I âˆ© â†‘J :=
  rfl

@[simp]
theorem toSubmodule_top : (âŠ¤ : HomogeneousSubmodule A â„³).toSubmodule = (âŠ¤ : Submodule A M) :=
  rfl

@[simp]
theorem toSubmodule_bot : (âŠ¥ : HomogeneousSubmodule A â„³).toSubmodule = (âŠ¥ : Submodule A M) :=
  rfl

@[simp]
theorem toSubmodule_sup (I J : HomogeneousSubmodule A â„³) :
    (I âŠ” J).toSubmodule = I.toSubmodule âŠ” J.toSubmodule := rfl

@[simp, nolint simpNF]
theorem toSubmodule_inf (I J : HomogeneousSubmodule A â„³) :
    (I âŠ“ J).toSubmodule = I.toSubmodule âŠ“ J.toSubmodule := rfl

@[simp]
theorem toSubmodule_sSup (â„ : Set (HomogeneousSubmodule A â„³)) :
    (sSup â„).toSubmodule = â¨† s âˆˆ â„, toSubmodule s := rfl

@[simp]
theorem toSubmodule_sInf (â„ : Set (HomogeneousSubmodule A â„³)) :
    (sInf â„).toSubmodule = â¨… s âˆˆ â„, toSubmodule s := rfl

@[simp]
theorem toSubmodule_iSup {Îº : Sort*} (s : Îº â†’ HomogeneousSubmodule A â„³) :
    (â¨† i, s i).toSubmodule = â¨† i, (s i).toSubmodule := by
  rw [iSup, toSubmodule_sSup, iSup_range]

@[simp, nolint simpNF]
theorem toSubmodule_iInf {Îº : Sort*} (s : Îº â†’ HomogeneousSubmodule A â„³) :
    (â¨… i, s i).toSubmodule = â¨… i, (s i).toSubmodule := by
  rw [iInf, toSubmodule_sInf, iInf_range]

-- @[simp] -- Porting note: simp can prove this
theorem toSubmodule_iSupâ‚‚ {Îº : Sort*} {Îº' : Îº â†’ Sort*}
    (s : âˆ€ i, Îº' i â†’ HomogeneousSubmodule A â„³) :
    (â¨† (i) (j), s i j).toSubmodule = â¨† (i) (j), (s i j).toSubmodule := by
  simp_rw [toSubmodule_iSup]

-- @[simp] -- Porting note: simp can prove this
theorem toSubmodule_iInfâ‚‚ {Îº : Sort*} {Îº' : Îº â†’ Sort*}
    (s : âˆ€ i, Îº' i â†’ HomogeneousSubmodule A â„³) :
    (â¨… (i) (j), s i j).toSubmodule = â¨… (i) (j), (s i j).toSubmodule := by
  simp_rw [toSubmodule_iInf]

@[simp]
theorem eq_top_iff (I : HomogeneousSubmodule A â„³) : I = âŠ¤ â†” I.toSubmodule = âŠ¤ :=
  toSubmodule_injective.eq_iff.symm

@[simp]
theorem eq_bot_iff (I : HomogeneousSubmodule A â„³) : I = âŠ¥ â†” I.toSubmodule = âŠ¥ :=
  toSubmodule_injective.eq_iff.symm

set_option synthInstance.checkSynthOrder false in
instance completeLattice : CompleteLattice (HomogeneousSubmodule A â„³) :=
  toSubmodule_injective.completeLattice _ toSubmodule_sup toSubmodule_inf toSubmodule_sSup
    toSubmodule_sInf toSubmodule_top toSubmodule_bot

set_option synthInstance.checkSynthOrder false in
instance : Add (HomogeneousSubmodule A â„³) :=
  âŸ¨(Â· âŠ” Â·)âŸ©

@[simp]
theorem toSubmodule_add (I J : HomogeneousSubmodule A â„³) :
    (I + J).toSubmodule = I.toSubmodule + J.toSubmodule := rfl

instance : Inhabited (HomogeneousSubmodule A â„³) where default := âŠ¥

end HomogeneousSubmodule

namespace HomogeneousIdeal

instance : PartialOrder (HomogeneousIdeal ğ’œ) :=
  SetLike.instPartialOrder

instance : Top (HomogeneousIdeal ğ’œ) := inferInstanceAs <| Top <| HomogeneousSubmodule A ğ’œ

instance : Bot (HomogeneousIdeal ğ’œ) := inferInstanceAs <| Bot <| HomogeneousSubmodule A ğ’œ

instance : Sup (HomogeneousIdeal ğ’œ) := inferInstanceAs <| Sup <| HomogeneousSubmodule A ğ’œ

instance : Inf (HomogeneousIdeal ğ’œ) := inferInstanceAs <| Inf <| HomogeneousSubmodule A ğ’œ

instance : SupSet (HomogeneousIdeal ğ’œ) := inferInstanceAs <| SupSet <| HomogeneousSubmodule A ğ’œ

instance : InfSet (HomogeneousIdeal ğ’œ) := inferInstanceAs <| InfSet <| HomogeneousSubmodule A ğ’œ

@[simp]
theorem coe_top : ((âŠ¤ : HomogeneousIdeal ğ’œ) : Set A) = univ := rfl
#align homogeneous_ideal.coe_top HomogeneousIdeal.coe_top

@[simp]
theorem coe_bot : ((âŠ¥ : HomogeneousIdeal ğ’œ) : Set A) = 0 := rfl
#align homogeneous_ideal.coe_bot HomogeneousIdeal.coe_bot

@[simp]
theorem coe_sup (I J : HomogeneousIdeal ğ’œ) : â†‘(I âŠ” J) = (I + J : Set A) :=
  Submodule.coe_sup _ _
#align homogeneous_ideal.coe_sup HomogeneousIdeal.coe_sup

@[simp]
theorem coe_inf (I J : HomogeneousIdeal ğ’œ) : (â†‘(I âŠ“ J) : Set A) = â†‘I âˆ© â†‘J :=
  rfl
#align homogeneous_ideal.coe_inf HomogeneousIdeal.coe_inf

@[simp]
theorem toIdeal_top : (âŠ¤ : HomogeneousIdeal ğ’œ).toIdeal = (âŠ¤ : Ideal A) := rfl
#align homogeneous_ideal.to_ideal_top HomogeneousIdeal.toIdeal_top

@[simp]
theorem toIdeal_bot : (âŠ¥ : HomogeneousIdeal ğ’œ).toIdeal = (âŠ¥ : Ideal A) := rfl
#align homogeneous_ideal.to_ideal_bot HomogeneousIdeal.toIdeal_bot

@[simp]
theorem toIdeal_sup (I J : HomogeneousIdeal ğ’œ) : (I âŠ” J).toIdeal = I.toIdeal âŠ” J.toIdeal := rfl
#align homogeneous_ideal.to_ideal_sup HomogeneousIdeal.toIdeal_sup

@[simp]
theorem toIdeal_inf (I J : HomogeneousIdeal ğ’œ) : (I âŠ“ J).toIdeal = I.toIdeal âŠ“ J.toIdeal := rfl
#align homogeneous_ideal.to_ideal_inf HomogeneousIdeal.toIdeal_inf

@[simp]
theorem toIdeal_sSup (â„ : Set (HomogeneousIdeal ğ’œ)) : (sSup â„).toIdeal = â¨† s âˆˆ â„, toIdeal s := rfl
#align homogeneous_ideal.to_ideal_Sup HomogeneousIdeal.toIdeal_sSup

@[simp]
theorem toIdeal_sInf (â„ : Set (HomogeneousIdeal ğ’œ)) : (sInf â„).toIdeal = â¨… s âˆˆ â„, toIdeal s := rfl
#align homogeneous_ideal.to_ideal_Inf HomogeneousIdeal.toIdeal_sInf

@[simp]
theorem toIdeal_iSup {Îº : Sort*} (s : Îº â†’ HomogeneousIdeal ğ’œ) :
    (â¨† i, s i).toIdeal = â¨† i, (s i).toIdeal :=
  HomogeneousSubmodule.toSubmodule_iSup (ğ’œ := ğ’œ) s
#align homogeneous_ideal.to_ideal_supr HomogeneousIdeal.toIdeal_iSup

@[simp]
theorem toIdeal_iInf {Îº : Sort*} (s : Îº â†’ HomogeneousIdeal ğ’œ) :
    (â¨… i, s i).toIdeal = â¨… i, (s i).toIdeal :=
  HomogeneousSubmodule.toSubmodule_iInf s
#align homogeneous_ideal.to_ideal_infi HomogeneousIdeal.toIdeal_iInf

theorem toIdeal_iSupâ‚‚ {Îº : Sort*} {Îº' : Îº â†’ Sort*} (s : âˆ€ i, Îº' i â†’ HomogeneousIdeal ğ’œ) :
    (â¨† (i) (j), s i j).toIdeal = â¨† (i) (j), (s i j).toIdeal :=
  HomogeneousSubmodule.toSubmodule_iSupâ‚‚ (ğ’œ := ğ’œ) s
#align homogeneous_ideal.to_ideal_suprâ‚‚ HomogeneousIdeal.toIdeal_iSupâ‚‚

theorem toIdeal_iInfâ‚‚ {Îº : Sort*} {Îº' : Îº â†’ Sort*} (s : âˆ€ i, Îº' i â†’ HomogeneousIdeal ğ’œ) :
    (â¨… (i) (j), s i j).toIdeal = â¨… (i) (j), (s i j).toIdeal :=
  HomogeneousSubmodule.toSubmodule_iInfâ‚‚ s
#align homogeneous_ideal.to_ideal_infiâ‚‚ HomogeneousIdeal.toIdeal_iInfâ‚‚

@[simp]
theorem eq_top_iff (I : HomogeneousIdeal ğ’œ) : I = âŠ¤ â†” I.toIdeal = âŠ¤ :=
  HomogeneousSubmodule.eq_top_iff I
#align homogeneous_ideal.eq_top_iff HomogeneousIdeal.eq_top_iff

@[simp]
theorem eq_bot_iff (I : HomogeneousIdeal ğ’œ) : I = âŠ¥ â†” I.toIdeal = âŠ¥ :=
  HomogeneousSubmodule.eq_bot_iff I
#align homogeneous_ideal.eq_bot_iff HomogeneousIdeal.eq_bot_iff

instance completeLattice : CompleteLattice (HomogeneousIdeal ğ’œ) :=
  inferInstanceAs <| CompleteLattice <| HomogeneousSubmodule A ğ’œ

instance : Add (HomogeneousIdeal ğ’œ) := inferInstanceAs <| Add <| HomogeneousSubmodule A ğ’œ

@[simp]
theorem toIdeal_add (I J : HomogeneousIdeal ğ’œ) : (I + J).toIdeal = I.toIdeal + J.toIdeal := rfl
#align homogeneous_ideal.to_ideal_add HomogeneousIdeal.toIdeal_add

instance : Inhabited (HomogeneousIdeal ğ’œ) where default := âŠ¥

end HomogeneousIdeal

end Semiring

section CommSemiring

variable {ğ’œ}

variable [CommSemiring A] [Module A M]

variable [AddMonoid Î¹A] [SetLike ÏƒA A] [AddSubmonoidClass ÏƒA A] [GradedRing ğ’œ]

-- In general, submodules cannot be multiplied, so this lemma is not generalized
theorem Ideal.IsHomogeneous.mul {I J : Ideal A} (HI : I.IsHomogeneous ğ’œ) (HJ : J.IsHomogeneous ğ’œ) :
    (I * J).IsHomogeneous ğ’œ := by
  rw [Ideal.IsHomogeneous.iff_exists ğ’œ] at HI HJ âŠ¢
  obtain âŸ¨âŸ¨sâ‚, rflâŸ©, âŸ¨sâ‚‚, rflâŸ©âŸ© := HI, HJ
  rw [Ideal.span_mul_span']
  exact âŸ¨sâ‚ * sâ‚‚, congr_arg _ <| (Set.image_mul (homogeneousSubmonoid ğ’œ).subtype).symmâŸ©
#align ideal.is_homogeneous.mul Ideal.IsHomogeneous.mul

instance : Mul (HomogeneousIdeal ğ’œ) where
  mul I J := âŸ¨I.toIdeal * J.toIdeal, Ideal.IsHomogeneous.mul I.isHomogeneous J.isHomogeneousâŸ©

@[simp]
theorem HomogeneousIdeal.toIdeal_mul (I J : HomogeneousIdeal ğ’œ) :
    (I * J).toIdeal = I.toIdeal * J.toIdeal :=
  rfl
#align homogeneous_ideal.to_ideal_mul HomogeneousIdeal.toIdeal_mul

end CommSemiring

end Operations

/-! ### Homogeneous core

Note that many results about the homogeneous core came earlier in this file, as they are helpful
for building the lattice structure. -/


section homogeneousCore

open HomogeneousSubmodule HomogeneousIdeal

variable [Semiring A] [Module A M]

variable [AddMonoid Î¹A]
variable [SetLike ÏƒA A] [AddSubmonoidClass ÏƒA A] [GradedRing ğ’œ]
variable [DecidableEq Î¹M] [VAdd Î¹A Î¹M] [Decomposition â„³] [GradedSMul ğ’œ â„³]

variable (I : Ideal A) (p : Submodule A M)

theorem Submodule.homogeneousCore.gc :
    GaloisConnection toSubmodule (Submodule.homogeneousCore ğ’œ â„³) := fun I _ =>
  âŸ¨fun H => I.toSubmodule_homogeneousCore_eq_self (ğ’œ := ğ’œ) â–¸ Submodule.homogeneousCore_mono ğ’œ â„³ H,
    fun H => le_trans H (Submodule.homogeneousCore'_le _ _)âŸ©

theorem Ideal.homogeneousCore.gc : GaloisConnection toIdeal (Ideal.homogeneousCore ğ’œ) :=
  Submodule.homogeneousCore.gc ğ’œ ğ’œ
#align ideal.homogeneous_core.gc Ideal.homogeneousCore.gc

/-- `toSubmodule : HomogeneousSubmodule A â„³ â†’ Submodule A M` and `Submodule.homogeneousCore ğ’œ â„³`
forms a galois coinsertion. -/
def Submodule.homogeneousCore.gi :
    GaloisCoinsertion toSubmodule (Submodule.homogeneousCore ğ’œ â„³) where
  choice I HI :=
    âŸ¨I, le_antisymm (I.toSubmodule_homogeneousCore_le ğ’œ â„³) HI â–¸
      HomogeneousSubmodule.isHomogeneous _âŸ©
  gc := Submodule.homogeneousCore.gc ğ’œ â„³
  u_l_le _ := Submodule.homogeneousCore'_le _ _
  choice_eq I H := le_antisymm H (I.toSubmodule_homogeneousCore_le _ _)

/-- `toIdeal : HomogeneousIdeal ğ’œ â†’ Ideal A` and `Ideal.homogeneousCore ğ’œ` forms a galois
coinsertion. -/
def Ideal.homogeneousCore.gi : GaloisCoinsertion toIdeal (Ideal.homogeneousCore ğ’œ) :=
  Submodule.homogeneousCore.gi ğ’œ ğ’œ
#align ideal.homogeneous_core.gi Ideal.homogeneousCore.gi

theorem Submodule.homogeneousCore_eq_sSup :
    p.homogeneousCore ğ’œ â„³ = sSup { q : HomogeneousSubmodule A â„³ | q.toSubmodule â‰¤ p } :=
  Eq.symm <| IsLUB.sSup_eq <| (Submodule.homogeneousCore.gc ğ’œ â„³).isGreatest_u.isLUB

lemma Ideal.homogeneousCore_eq_sSup :
    I.homogeneousCore ğ’œ = sSup { J : HomogeneousIdeal ğ’œ | J.toIdeal â‰¤ I } :=
  Submodule.homogeneousCore_eq_sSup ğ’œ ğ’œ I
#align ideal.homogeneous_core_eq_Sup Ideal.homogeneousCore_eq_sSup

theorem Submodule.homogeneousCore'_eq_sSup :
    p.homogeneousCore' â„³ = sSup { q : Submodule A M | q.IsHomogeneous â„³ âˆ§ q â‰¤ p } := by
  refine' (IsLUB.sSup_eq _).symm
  apply IsGreatest.isLUB
  have coe_mono : Monotone (toSubmodule : HomogeneousSubmodule A â„³ â†’ Submodule A M) := fun x y => id
  convert coe_mono.map_isGreatest (Submodule.homogeneousCore.gc ğ’œ â„³).isGreatest_u using 1
  ext x
  rw [mem_image, mem_setOf_eq]
  refine' âŸ¨fun hI => âŸ¨âŸ¨x, hI.1âŸ©, âŸ¨hI.2, rflâŸ©âŸ©, _âŸ©
  rintro âŸ¨x, âŸ¨hx, rflâŸ©âŸ©
  exact âŸ¨x.isHomogeneous, hxâŸ©

theorem Ideal.homogeneousCore'_eq_sSup :
    I.homogeneousCore' ğ’œ = sSup { J : Ideal A | J.IsHomogeneous ğ’œ âˆ§ J â‰¤ I } :=
  Submodule.homogeneousCore'_eq_sSup ğ’œ ğ’œ I
#align ideal.homogeneous_core'_eq_Sup Ideal.homogeneousCore'_eq_sSup

end homogeneousCore

/-! ### Homogeneous hulls -/


section HomogeneousHull

open HomogeneousSubmodule

variable [Semiring A] [Module A M] [DecidableEq Î¹A] [AddMonoid Î¹A]
variable [SetLike ÏƒA A] [AddSubmonoidClass ÏƒA A] [GradedRing ğ’œ] [VAdd Î¹A Î¹M] [GradedSMul ğ’œ â„³]

variable (I : Ideal A) (p : Submodule A M)

/-- For any `p : Submodule A M`, not necessarily homogeneous, `p.homogeneousHull ğ’œ â„³` is the
smallest  homogeneous `A`-submodule containing `p`. -/
def Submodule.homogeneousHull : HomogeneousSubmodule A â„³ :=
  âŸ¨Submodule.span A { r : M | âˆƒ (i : Î¹M) (x : p), (DirectSum.decompose â„³ (x : M) i : M) = r }, by
    refine' Submodule.homogeneous_span ğ’œ â„³ _ fun x hx => _
    obtain âŸ¨i, x, rflâŸ© := hx
    apply SetLike.homogeneous_coeâŸ©

/-- For any `I : Ideal A`, not necessarily homogeneous, `I.homogeneousHull ğ’œ` is the smallest
homogeneous ideal containing `I`. -/
def Ideal.homogeneousHull : HomogeneousIdeal ğ’œ :=
  Submodule.homogeneousHull ğ’œ ğ’œ I
#align ideal.homogeneous_hull Submodule.homogeneousHull

theorem Submodule.le_toSubmodule_homogeneousHull :
    p â‰¤ (Submodule.homogeneousHull ğ’œ â„³ p).toSubmodule := by
  intro r hr
  classical
  rw [â† DirectSum.sum_support_decompose â„³ r]
  exact Submodule.sum_mem _ fun j _ â†¦ Submodule.subset_span âŸ¨j, âŸ¨âŸ¨r, hrâŸ©, rflâŸ©âŸ©

theorem Ideal.le_toSubmodule_homogeneousHull : I â‰¤ (I.homogeneousHull ğ’œ).toIdeal :=
  Submodule.le_toSubmodule_homogeneousHull ğ’œ ğ’œ I
#align ideal.le_to_ideal_homogeneous_hull Ideal.le_toSubmodule_homogeneousHull

theorem Submodule.homogeneousHull_mono :
    Monotone (Submodule.homogeneousHull ğ’œ â„³) := fun I J I_le_J => by
  apply Submodule.span_mono
  rintro r âŸ¨hr1, âŸ¨x, hxâŸ©, rflâŸ©
  refine' âŸ¨hr1, âŸ¨âŸ¨x, I_le_J hxâŸ©, rflâŸ©âŸ©

theorem Ideal.homogeneousHull_mono : Monotone (Ideal.homogeneousHull ğ’œ) :=
  Submodule.homogeneousHull_mono ğ’œ ğ’œ
#align ideal.homogeneous_hull_mono Ideal.homogeneousHull_mono

variable {p I ğ’œ â„³}

theorem Submodule.IsHomogeneous.toSubmodule_homogeneousHull_eq_self (h : p.IsHomogeneous â„³) :
    (Submodule.homogeneousHull ğ’œ â„³ p).toSubmodule = p := by
  apply le_antisymm _ (Submodule.le_toSubmodule_homogeneousHull _ _ _)
  apply Submodule.span_le.2
  rintro _ âŸ¨i, x, rflâŸ©
  exact h _ x.prop

theorem Ideal.IsHomogeneous.toIdeal_homogeneousHull_eq_self (h : I.IsHomogeneous ğ’œ) :
    (I.homogeneousHull ğ’œ).toIdeal = I :=
  Submodule.IsHomogeneous.toSubmodule_homogeneousHull_eq_self h
#align ideal.is_homogeneous.to_ideal_homogeneous_hull_eq_self Submodule.IsHomogeneous.toSubmodule_homogeneousHull_eq_self

@[simp]
theorem HomogeneousSubmodule.homogeneousHull_toSubmodule_eq_self (p : HomogeneousSubmodule A â„³) :
    p.toSubmodule.homogeneousHull ğ’œ â„³ = p :=
  HomogeneousSubmodule.toSubmodule_injective <| p.2.toSubmodule_homogeneousHull_eq_self

@[simp]
theorem HomogeneousIdeal.homogeneousHull_toIdeal_eq_self (I : HomogeneousIdeal ğ’œ) :
    I.toIdeal.homogeneousHull ğ’œ = I :=
  HomogeneousSubmodule.homogeneousHull_toSubmodule_eq_self I
#align homogeneous_ideal.homogeneous_hull_to_ideal_eq_self HomogeneousIdeal.homogeneousHull_toIdeal_eq_self

variable (p I)

theorem Submodule.toSubmodule_homogeneousHull_eq_iSup :
    (p.homogeneousHull ğ’œ â„³).toSubmodule = â¨† i, Submodule.span A (GradedModule.proj â„³ i '' p) := by
  rw [â† Submodule.span_iUnion]
  apply congr_arg (Submodule.span A Â·) _
  aesop

theorem Ideal.toIdeal_homogeneousHull_eq_iSup :
    (I.homogeneousHull ğ’œ).toIdeal = â¨† i, Ideal.span (GradedRing.proj ğ’œ i '' I) :=
  Submodule.toSubmodule_homogeneousHull_eq_iSup I
#align ideal.to_ideal_homogeneous_hull_eq_supr Ideal.toIdeal_homogeneousHull_eq_iSup

theorem Submodule.homogeneousHull_eq_iSup :
    p.homogeneousHull ğ’œ â„³ =
      â¨† i, âŸ¨Submodule.span A (GradedModule.proj â„³ i '' p), Submodule.homogeneous_span ğ’œ â„³ _ (by
        rintro _ âŸ¨x, -, rflâŸ©
        apply SetLike.homogeneous_coe)âŸ© := by
  ext1
  rw [Submodule.toSubmodule_homogeneousHull_eq_iSup, toSubmodule_iSup]

theorem Ideal.homogeneousHull_eq_iSup :
    I.homogeneousHull ğ’œ =
      â¨† i, âŸ¨Ideal.span (GradedRing.proj ğ’œ i '' I), Ideal.homogeneous_span ğ’œ _ (by
        rintro _ âŸ¨x, -, rflâŸ©
        apply SetLike.homogeneous_coe)âŸ© :=
  Submodule.homogeneousHull_eq_iSup I
#align ideal.homogeneous_hull_eq_supr Ideal.homogeneousHull_eq_iSup

end HomogeneousHull

section GaloisConnection

open HomogeneousSubmodule HomogeneousIdeal

variable [Semiring A] [Module A M] [DecidableEq Î¹A] [AddMonoid Î¹A]

variable [SetLike ÏƒA A] [AddSubmonoidClass ÏƒA A] [GradedRing ğ’œ] [VAdd Î¹A Î¹M] [GradedSMul ğ’œ â„³]

theorem Submodule.homogeneousHull.gc :
    GaloisConnection (Submodule.homogeneousHull ğ’œ â„³) toSubmodule := fun _ J =>
  âŸ¨le_trans (Submodule.le_toSubmodule_homogeneousHull _ _ _),
    fun H => J.homogeneousHull_toSubmodule_eq_self (ğ’œ := ğ’œ) â–¸ Submodule.homogeneousHull_mono ğ’œ â„³ HâŸ©

theorem Ideal.homogeneousHull.gc : GaloisConnection (Ideal.homogeneousHull ğ’œ) toIdeal :=
  Submodule.homogeneousHull.gc ğ’œ ğ’œ
#align ideal.homogeneous_hull.gc Submodule.homogeneousHull.gc


/-- `Submodule.homogeneousHull ğ’œ â„³` and `toSubmodule : HomogeneousSubmodule A â„³ â†’ Submodule A M`
form a galois insertion. -/
def Submodule.homogeneousHull.gi : GaloisInsertion (Submodule.homogeneousHull ğ’œ â„³) toSubmodule where
  choice I H := âŸ¨I, le_antisymm H (I.le_toSubmodule_homogeneousHull ğ’œ â„³) â–¸ isHomogeneous _âŸ©
  gc := Submodule.homogeneousHull.gc ğ’œ â„³
  le_l_u _ := Submodule.le_toSubmodule_homogeneousHull ğ’œ _ _
  choice_eq I H := le_antisymm (I.le_toSubmodule_homogeneousHull ğ’œ â„³) H

/-- `Ideal.homogeneousHull ğ’œ` and `toIdeal : HomogeneousIdeal ğ’œ â†’ Ideal A` form a galois insertion.
-/
def Ideal.homogeneousHull.gi : GaloisInsertion (Ideal.homogeneousHull ğ’œ) toIdeal :=
  Submodule.homogeneousHull.gi ğ’œ ğ’œ
#align ideal.homogeneous_hull.gi Ideal.homogeneousHull.gi

theorem Submodule.homogeneousHull_eq_sInf (p : Submodule A M) :
    p.homogeneousHull ğ’œ â„³ = sInf { q : HomogeneousSubmodule A â„³ | p â‰¤ q.toSubmodule } :=
  Eq.symm <| IsGLB.sInf_eq <| (Submodule.homogeneousHull.gc ğ’œ â„³).isLeast_l.isGLB

theorem Ideal.homogeneousHull_eq_sInf (I : Ideal A) :
    I.homogeneousHull ğ’œ = sInf { J : HomogeneousIdeal ğ’œ | I â‰¤ J.toIdeal } :=
  Submodule.homogeneousHull_eq_sInf ğ’œ ğ’œ I
#align ideal.homogeneous_hull_eq_Inf Ideal.homogeneousHull_eq_sInf

end GaloisConnection

section IrrelevantIdeal

variable [Semiring A]

variable [CanonicallyOrderedAddCommMonoid Î¹A]

variable [SetLike ÏƒA A] [AddSubmonoidClass ÏƒA A] [GradedRing ğ’œ]

open GradedRing SetLike.GradedMonoid DirectSum

/-- For a graded ring `â¨áµ¢ ğ’œáµ¢` graded by a `CanonicallyOrderedAddCommMonoid Î¹`, the irrelevant ideal
refers to `â¨_{i>0} ğ’œáµ¢`, or equivalently `{a | aâ‚€ = 0}`. This definition is used in `Proj`
construction where `Î¹` is always `â„•` so the irrelevant ideal is simply elements with `0` as
0-th coordinate.

# Future work
Here in the definition, `Î¹` is assumed to be `CanonicallyOrderedAddCommMonoid`. However, the notion
of irrelevant ideal makes sense in a more general setting by defining it as the ideal of elements
with `0` as i-th coordinate for all `i â‰¤ 0`, i.e. `{a | âˆ€ (i : Î¹), i â‰¤ 0 â†’ aáµ¢ = 0}`.
-/
def HomogeneousIdeal.irrelevant : HomogeneousIdeal ğ’œ :=
  âŸ¨RingHom.ker (GradedRing.projZeroRingHom ğ’œ), fun i r (hr : (decompose ğ’œ r 0 : A) = 0) => by
    change (decompose ğ’œ (decompose ğ’œ r _ : A) 0 : A) = 0
    by_cases h : i = 0
    Â· rw [h, hr, decompose_zero, zero_apply, ZeroMemClass.coe_zero]
    Â· rw [decompose_of_mem_ne ğ’œ (SetLike.coe_mem _) h]âŸ©
#align homogeneous_ideal.irrelevant HomogeneousIdeal.irrelevant

@[simp]
theorem HomogeneousIdeal.mem_irrelevant_iff (a : A) :
    a âˆˆ HomogeneousIdeal.irrelevant ğ’œ â†” proj ğ’œ 0 a = 0 :=
  Iff.rfl
#align homogeneous_ideal.mem_irrelevant_iff HomogeneousIdeal.mem_irrelevant_iff

@[simp]
theorem HomogeneousIdeal.toIdeal_irrelevant :
    (HomogeneousIdeal.irrelevant ğ’œ).toIdeal = RingHom.ker (GradedRing.projZeroRingHom ğ’œ) :=
  rfl
#align homogeneous_ideal.to_ideal_irrelevant HomogeneousIdeal.toIdeal_irrelevant

end IrrelevantIdeal
