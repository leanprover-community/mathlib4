/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Eric Wieser
-/
import Mathlib.Algebra.Group.Submonoid.Finsupp
import Mathlib.Order.Filter.AtTopBot.Defs
import Mathlib.RingTheory.Adjoin.Basic
import Mathlib.RingTheory.GradedAlgebra.FiniteType
import Mathlib.RingTheory.Localization.AtPrime.Basic
import Mathlib.RingTheory.Localization.Away.Basic

/-!
# Homogeneous Localization

## Notation
- `Î¹` is a commutative monoid;
- `R` is a commutative semiring;
- `A` is a commutative ring and an `R`-algebra;
- `ğ’œ : Î¹ â†’ Submodule R A` is the grading of `A`;
- `x : Submonoid A` is a submonoid

## Main definitions and results

This file constructs the subring of `Aâ‚“` where the numerator and denominator have the same grading,
i.e. `{a/b âˆˆ Aâ‚“ | âˆƒ (i : Î¹), a âˆˆ ğ’œáµ¢ âˆ§ b âˆˆ ğ’œáµ¢}`.

* `HomogeneousLocalization.NumDenSameDeg`: a structure with a numerator and denominator field
  where they are required to have the same grading.

However `NumDenSameDeg ğ’œ x` cannot have a ring structure for many reasons, for example if `c`
is a `NumDenSameDeg`, then generally, `c + (-c)` is not necessarily `0` for degree reasons ---
`0` is considered to have grade zero (see `deg_zero`) but `c + (-c)` has the same degree as `c`. To
circumvent this, we quotient `NumDenSameDeg ğ’œ x` by the kernel of `c â†¦ c.num / c.den`.

* `HomogeneousLocalization.NumDenSameDeg.embedding`: for `x : Submonoid A` and any
  `c : NumDenSameDeg ğ’œ x`, or equivalent a numerator and a denominator of the same degree,
  we get an element `c.num / c.den` of `Aâ‚“`.
* `HomogeneousLocalization`: `NumDenSameDeg ğ’œ x` quotiented by kernel of `embedding ğ’œ x`.
* `HomogeneousLocalization.val`: if `f : HomogeneousLocalization ğ’œ x`, then `f.val` is an element
  of `Aâ‚“`. In another word, one can view `HomogeneousLocalization ğ’œ x` as a subring of `Aâ‚“`
  through `HomogeneousLocalization.val`.
* `HomogeneousLocalization.num`: if `f : HomogeneousLocalization ğ’œ x`, then `f.num : A` is the
  numerator of `f`.
* `HomogeneousLocalization.den`: if `f : HomogeneousLocalization ğ’œ x`, then `f.den : A` is the
  denominator of `f`.
* `HomogeneousLocalization.deg`: if `f : HomogeneousLocalization ğ’œ x`, then `f.deg : Î¹` is the
  degree of `f` such that `f.num âˆˆ ğ’œ f.deg` and `f.den âˆˆ ğ’œ f.deg`
  (see `HomogeneousLocalization.num_mem_deg` and `HomogeneousLocalization.den_mem_deg`).
* `HomogeneousLocalization.num_mem_deg`: if `f : HomogeneousLocalization ğ’œ x`, then
  `f.num_mem_deg` is a proof that `f.num âˆˆ ğ’œ f.deg`.
* `HomogeneousLocalization.den_mem_deg`: if `f : HomogeneousLocalization ğ’œ x`, then
  `f.den_mem_deg` is a proof that `f.den âˆˆ ğ’œ f.deg`.
* `HomogeneousLocalization.eq_num_div_den`: if `f : HomogeneousLocalization ğ’œ x`, then
  `f.val : Aâ‚“` is equal to `f.num / f.den`.

* `HomogeneousLocalization.isLocalRing`: `HomogeneousLocalization ğ’œ x` is a local ring when `x` is
  the complement of some prime ideals.

* `HomogeneousLocalization.map`: Let `A` and `B` be two graded rings and `g : A â†’ B` a
  grading-preserving ring map. If `P â‰¤ A` and `Q â‰¤ B` are submonoids such that `P â‰¤ gâ»Â¹(Q)`, then
  `g` induces a ring map between the homogeneous localization of `A` at `P` and the homogeneous
  localization of `B` at `Q`.

## References

* [Robin Hartshorne, *Algebraic Geometry*][Har77]


-/


noncomputable section

open DirectSum Pointwise

open DirectSum SetLike

variable {Î¹ R A : Type*}
variable [CommRing R] [CommRing A] [Algebra R A]
variable (ğ’œ : Î¹ â†’ Submodule R A)
variable (x : Submonoid A)

local notation "at " x => Localization x

namespace HomogeneousLocalization

section

/-- Let `x` be a submonoid of `A`, then `NumDenSameDeg ğ’œ x` is a structure with a numerator and a
denominator with same grading such that the denominator is contained in `x`.
-/
structure NumDenSameDeg where
  deg : Î¹
  (num den : ğ’œ deg)
  den_mem : (den : A) âˆˆ x

end

namespace NumDenSameDeg

open SetLike.GradedMonoid Submodule

variable {ğ’œ}

@[ext]
theorem ext {c1 c2 : NumDenSameDeg ğ’œ x} (hdeg : c1.deg = c2.deg) (hnum : (c1.num : A) = c2.num)
    (hden : (c1.den : A) = c2.den) : c1 = c2 := by
  rcases c1 with âŸ¨i1, âŸ¨n1, hn1âŸ©, âŸ¨d1, hd1âŸ©, h1âŸ©
  rcases c2 with âŸ¨i2, âŸ¨n2, hn2âŸ©, âŸ¨d2, hd2âŸ©, h2âŸ©
  dsimp only [Subtype.coe_mk] at *
  subst hdeg hnum hden
  congr

instance : Neg (NumDenSameDeg ğ’œ x) where
  neg c := âŸ¨c.deg, âŸ¨-c.num, neg_mem c.num.2âŸ©, c.den, c.den_memâŸ©

@[simp]
theorem deg_neg (c : NumDenSameDeg ğ’œ x) : (-c).deg = c.deg :=
  rfl

@[simp]
theorem num_neg (c : NumDenSameDeg ğ’œ x) : ((-c).num : A) = -c.num :=
  rfl

@[simp]
theorem den_neg (c : NumDenSameDeg ğ’œ x) : ((-c).den : A) = c.den :=
  rfl

section SMul

variable {Î± : Type*} [SMul Î± R] [SMul Î± A] [IsScalarTower Î± R A]

instance : SMul Î± (NumDenSameDeg ğ’œ x) where
  smul m c := âŸ¨c.deg, m â€¢ c.num, c.den, c.den_memâŸ©

@[simp]
theorem deg_smul (c : NumDenSameDeg ğ’œ x) (m : Î±) : (m â€¢ c).deg = c.deg :=
  rfl

@[simp]
theorem num_smul (c : NumDenSameDeg ğ’œ x) (m : Î±) : ((m â€¢ c).num : A) = m â€¢ c.num :=
  rfl

@[simp]
theorem den_smul (c : NumDenSameDeg ğ’œ x) (m : Î±) : ((m â€¢ c).den : A) = c.den :=
  rfl

end SMul

variable [AddCommMonoid Î¹] [DecidableEq Î¹] [GradedAlgebra ğ’œ]

open GradedOne in
instance : One (NumDenSameDeg ğ’œ x) where
  one :=
    { deg := 0
      num := âŸ¨1, one_memâŸ©
      den := âŸ¨1, one_memâŸ©
      den_mem := one_mem _ }

@[simp]
theorem deg_one : (1 : NumDenSameDeg ğ’œ x).deg = 0 :=
  rfl

@[simp]
theorem num_one : ((1 : NumDenSameDeg ğ’œ x).num : A) = 1 :=
  rfl

@[simp]
theorem den_one : ((1 : NumDenSameDeg ğ’œ x).den : A) = 1 :=
  rfl

open GradedOne in
instance : Zero (NumDenSameDeg ğ’œ x) where
  zero := âŸ¨0, 0, âŸ¨1, one_memâŸ©, one_mem _âŸ©

@[simp]
theorem deg_zero : (0 : NumDenSameDeg ğ’œ x).deg = 0 :=
  rfl

@[simp]
theorem num_zero : (0 : NumDenSameDeg ğ’œ x).num = 0 :=
  rfl

@[simp]
theorem den_zero : ((0 : NumDenSameDeg ğ’œ x).den : A) = 1 :=
  rfl

open GradedMul in
instance : Mul (NumDenSameDeg ğ’œ x) where
  mul p q :=
    { deg := p.deg + q.deg
      num := âŸ¨p.num * q.num, mul_mem p.num.prop q.num.propâŸ©
      den := âŸ¨p.den * q.den, mul_mem p.den.prop q.den.propâŸ©
      den_mem := Submonoid.mul_mem _ p.den_mem q.den_mem }

@[simp]
theorem deg_mul (c1 c2 : NumDenSameDeg ğ’œ x) : (c1 * c2).deg = c1.deg + c2.deg :=
  rfl

@[simp]
theorem num_mul (c1 c2 : NumDenSameDeg ğ’œ x) : ((c1 * c2).num : A) = c1.num * c2.num :=
  rfl

@[simp]
theorem den_mul (c1 c2 : NumDenSameDeg ğ’œ x) : ((c1 * c2).den : A) = c1.den * c2.den :=
  rfl

instance : Add (NumDenSameDeg ğ’œ x) where
  add c1 c2 :=
    { deg := c1.deg + c2.deg
      num := âŸ¨c1.den * c2.num + c2.den * c1.num,
        add_mem (GradedMul.mul_mem c1.den.2 c2.num.2)
          (add_comm c2.deg c1.deg â–¸ GradedMul.mul_mem c2.den.2 c1.num.2)âŸ©
      den := âŸ¨c1.den * c2.den, GradedMul.mul_mem c1.den.2 c2.den.2âŸ©
      den_mem := Submonoid.mul_mem _ c1.den_mem c2.den_mem }

@[simp]
theorem deg_add (c1 c2 : NumDenSameDeg ğ’œ x) : (c1 + c2).deg = c1.deg + c2.deg :=
  rfl

@[simp]
theorem num_add (c1 c2 : NumDenSameDeg ğ’œ x) :
    ((c1 + c2).num : A) = c1.den * c2.num + c2.den * c1.num :=
  rfl

@[simp]
theorem den_add (c1 c2 : NumDenSameDeg ğ’œ x) : ((c1 + c2).den : A) = c1.den * c2.den :=
  rfl

instance : CommMonoid (NumDenSameDeg ğ’œ x) where
  mul_assoc _ _ _ := ext _ (add_assoc _ _ _) (mul_assoc _ _ _) (mul_assoc _ _ _)
  one_mul _ := ext _ (zero_add _) (one_mul _) (one_mul _)
  mul_one _ := ext _ (add_zero _) (mul_one _) (mul_one _)
  mul_comm _ _ := ext _ (add_comm _ _) (mul_comm _ _) (mul_comm _ _)

instance : Pow (NumDenSameDeg ğ’œ x) â„• where
  pow c n :=
    âŸ¨n â€¢ c.deg, @GradedMonoid.GMonoid.gnpow _ (fun i => â†¥(ğ’œ i)) _ _ n _ c.num,
      @GradedMonoid.GMonoid.gnpow _ (fun i => â†¥(ğ’œ i)) _ _ n _ c.den, by
        induction n with
        | zero => simp only [coe_gnpow, pow_zero, one_mem]
        | succ n ih => simpa only [pow_succ, coe_gnpow] using x.mul_mem ih c.den_memâŸ©

@[simp]
theorem deg_pow (c : NumDenSameDeg ğ’œ x) (n : â„•) : (c ^ n).deg = n â€¢ c.deg :=
  rfl

@[simp]
theorem num_pow (c : NumDenSameDeg ğ’œ x) (n : â„•) : ((c ^ n).num : A) = (c.num : A) ^ n :=
  rfl

@[simp]
theorem den_pow (c : NumDenSameDeg ğ’œ x) (n : â„•) : ((c ^ n).den : A) = (c.den : A) ^ n :=
  rfl

variable (ğ’œ)

/-- For `x : prime ideal of A` and any `p : NumDenSameDeg ğ’œ x`, or equivalent a numerator and a
denominator of the same degree, we get an element `p.num / p.den` of `Aâ‚“`.
-/
def embedding (p : NumDenSameDeg ğ’œ x) : at x :=
  Localization.mk p.num âŸ¨p.den, p.den_memâŸ©

end NumDenSameDeg

end HomogeneousLocalization

/-- For `x : prime ideal of A`, `HomogeneousLocalization ğ’œ x` is `NumDenSameDeg ğ’œ x` modulo the
kernel of `embedding ğ’œ x`. This is essentially the subring of `Aâ‚“` where the numerator and
denominator share the same grading.
-/
def HomogeneousLocalization : Type _ :=
  Quotient (Setoid.ker <| HomogeneousLocalization.NumDenSameDeg.embedding ğ’œ x)

namespace HomogeneousLocalization

open HomogeneousLocalization HomogeneousLocalization.NumDenSameDeg

variable {ğ’œ} {x}

/-- Construct an element of `HomogeneousLocalization ğ’œ x` from a homogeneous fraction. -/
abbrev mk (y : HomogeneousLocalization.NumDenSameDeg ğ’œ x) : HomogeneousLocalization ğ’œ x :=
  Quotient.mk'' y

lemma mk_surjective : Function.Surjective (mk (ğ’œ := ğ’œ) (x := x)) :=
  Quotient.mk''_surjective

/-- View an element of `HomogeneousLocalization ğ’œ x` as an element of `Aâ‚“` by forgetting that the
numerator and denominator are of the same grading.
-/
def val (y : HomogeneousLocalization ğ’œ x) : at x :=
  Quotient.liftOn' y (NumDenSameDeg.embedding ğ’œ x) fun _ _ => id

@[simp]
theorem val_mk (i : NumDenSameDeg ğ’œ x) :
    val (mk i) = Localization.mk (i.num : A) âŸ¨i.den, i.den_memâŸ© :=
  rfl

variable (x)

@[ext]
theorem val_injective : Function.Injective (HomogeneousLocalization.val (ğ’œ := ğ’œ) (x := x)) :=
  fun a b => Quotient.recOnSubsingletonâ‚‚' a b fun _ _ h => Quotient.sound' h

variable (ğ’œ) {x} in
lemma subsingleton (hx : 0 âˆˆ x) : Subsingleton (HomogeneousLocalization ğ’œ x) :=
  have := IsLocalization.subsingleton (S := at x) hx
  (HomogeneousLocalization.val_injective (ğ’œ := ğ’œ) (x := x)).subsingleton

section SMul

variable {Î± : Type*} [SMul Î± R] [SMul Î± A] [IsScalarTower Î± R A]
variable [IsScalarTower Î± A A]

instance : SMul Î± (HomogeneousLocalization ğ’œ x) where
  smul m := Quotient.map' (m â€¢ Â·) fun c1 c2 (h : Localization.mk _ _ = Localization.mk _ _) => by
    change Localization.mk _ _ = Localization.mk _ _
    simp only [num_smul, den_smul]
    convert congr_arg (fun z : at x => m â€¢ z) h <;> rw [Localization.smul_mk]

@[simp] lemma mk_smul (i : NumDenSameDeg ğ’œ x) (m : Î±) : mk (m â€¢ i) = m â€¢ mk i := rfl

@[simp]
theorem val_smul (n : Î±) : âˆ€ y : HomogeneousLocalization ğ’œ x, (n â€¢ y).val = n â€¢ y.val :=
  Quotient.ind' fun _ â†¦ by rw [â† mk_smul, val_mk, val_mk, Localization.smul_mk]; rfl

theorem val_nsmul (n : â„•) (y : HomogeneousLocalization ğ’œ x) : (n â€¢ y).val = n â€¢ y.val := by
  rw [val_smul, OreLocalization.nsmul_eq_nsmul]

theorem val_zsmul (n : â„¤) (y : HomogeneousLocalization ğ’œ x) : (n â€¢ y).val = n â€¢ y.val := by
  rw [val_smul, OreLocalization.zsmul_eq_zsmul]

end SMul

instance : Neg (HomogeneousLocalization ğ’œ x) where
  neg := Quotient.map' Neg.neg fun c1 c2 (h : Localization.mk _ _ = Localization.mk _ _) => by
    change Localization.mk _ _ = Localization.mk _ _
    simp only [num_neg, den_neg, â† Localization.neg_mk]
    exact congr_arg Neg.neg h

@[simp] lemma mk_neg (i : NumDenSameDeg ğ’œ x) : mk (-i) = -mk i := rfl

@[simp]
theorem val_neg {x} : âˆ€ y : HomogeneousLocalization ğ’œ x, (-y).val = -y.val :=
  Quotient.ind' fun y â†¦ by rw [â† mk_neg, val_mk, val_mk, Localization.neg_mk]; rfl

variable [AddCommMonoid Î¹] [DecidableEq Î¹] [GradedAlgebra ğ’œ]

instance hasPow : Pow (HomogeneousLocalization ğ’œ x) â„• where
  pow z n :=
    (Quotient.map' (Â· ^ n) fun c1 c2 (h : Localization.mk _ _ = Localization.mk _ _) => by
          change Localization.mk _ _ = Localization.mk _ _
          simp only [num_pow, den_pow]
          convert congr_arg (fun z : at x => z ^ n) h <;> rw [Localization.mk_pow] <;> rfl :
        HomogeneousLocalization ğ’œ x â†’ HomogeneousLocalization ğ’œ x)
      z

@[simp] lemma mk_pow (i : NumDenSameDeg ğ’œ x) (n : â„•) : mk (i ^ n) = mk i ^ n := rfl

instance : Add (HomogeneousLocalization ğ’œ x) where
  add :=
    Quotient.mapâ‚‚ (Â· + Â·)
      fun c1 c2 (h : Localization.mk _ _ = Localization.mk _ _) c3 c4
        (h' : Localization.mk _ _ = Localization.mk _ _) => by
      change Localization.mk _ _ = Localization.mk _ _
      simp only [num_add, den_add]
      convert congr_argâ‚‚ (Â· + Â·) h h' <;> rw [Localization.add_mk] <;> rfl

@[simp] lemma mk_add (i j : NumDenSameDeg ğ’œ x) : mk (i + j) = mk i + mk j := rfl

instance : Sub (HomogeneousLocalization ğ’œ x) where sub z1 z2 := z1 + -z2

instance : Mul (HomogeneousLocalization ğ’œ x) where
  mul :=
    Quotient.mapâ‚‚ (Â· * Â·)
      fun c1 c2 (h : Localization.mk _ _ = Localization.mk _ _) c3 c4
        (h' : Localization.mk _ _ = Localization.mk _ _) => by
      change Localization.mk _ _ = Localization.mk _ _
      simp only [num_mul, den_mul]
      convert congr_argâ‚‚ (Â· * Â·) h h' <;> rw [Localization.mk_mul] <;> rfl

@[simp] lemma mk_mul (i j : NumDenSameDeg ğ’œ x) : mk (i * j) = mk i * mk j := rfl

instance : One (HomogeneousLocalization ğ’œ x) where one := Quotient.mk'' 1

@[simp] lemma mk_one : mk (1 : NumDenSameDeg ğ’œ x) = 1 := rfl

instance : Zero (HomogeneousLocalization ğ’œ x) where zero := Quotient.mk'' 0

@[simp] lemma mk_zero : mk (0 : NumDenSameDeg ğ’œ x) = 0 := rfl

theorem zero_eq : (0 : HomogeneousLocalization ğ’œ x) = Quotient.mk'' 0 :=
  rfl

theorem one_eq : (1 : HomogeneousLocalization ğ’œ x) = Quotient.mk'' 1 :=
  rfl

variable {x}

@[simp]
theorem val_zero : (0 : HomogeneousLocalization ğ’œ x).val = 0 :=
  Localization.mk_zero _

@[simp]
theorem val_one : (1 : HomogeneousLocalization ğ’œ x).val = 1 :=
  Localization.mk_one

@[simp]
theorem val_add : âˆ€ y1 y2 : HomogeneousLocalization ğ’œ x, (y1 + y2).val = y1.val + y2.val :=
  Quotient.indâ‚‚' fun y1 y2 â†¦ by rw [â† mk_add, val_mk, val_mk, val_mk, Localization.add_mk]; rfl

@[simp]
theorem val_mul : âˆ€ y1 y2 : HomogeneousLocalization ğ’œ x, (y1 * y2).val = y1.val * y2.val :=
  Quotient.indâ‚‚' fun y1 y2 â†¦ by rw [â† mk_mul, val_mk, val_mk, val_mk, Localization.mk_mul]; rfl

@[simp]
theorem val_sub (y1 y2 : HomogeneousLocalization ğ’œ x) : (y1 - y2).val = y1.val - y2.val := by
  rw [sub_eq_add_neg, â† val_neg, â† val_add]; rfl

@[simp]
theorem val_pow : âˆ€ (y : HomogeneousLocalization ğ’œ x) (n : â„•), (y ^ n).val = y.val ^ n :=
  Quotient.ind' fun y n â†¦ by rw [â† mk_pow, val_mk, val_mk, Localization.mk_pow]; rfl

instance : NatCast (HomogeneousLocalization ğ’œ x) :=
  âŸ¨Nat.unaryCastâŸ©

instance : IntCast (HomogeneousLocalization ğ’œ x) :=
  âŸ¨Int.castDefâŸ©

@[simp]
theorem val_natCast (n : â„•) : (n : HomogeneousLocalization ğ’œ x).val = n :=
  show val (Nat.unaryCast n) = _ by induction n <;> simp [Nat.unaryCast, *]

@[simp]
theorem val_intCast (n : â„¤) : (n : HomogeneousLocalization ğ’œ x).val = n :=
  show val (Int.castDef n) = _ by cases n <;> simp [Int.castDef, *]

instance homogeneousLocalizationCommRing : CommRing (HomogeneousLocalization ğ’œ x) :=
  (HomogeneousLocalization.val_injective x).commRing _ val_zero val_one val_add val_mul val_neg
    val_sub (val_nsmul x Â· Â·) (val_zsmul x Â· Â·) val_pow val_natCast val_intCast

instance homogeneousLocalizationAlgebra :
    Algebra (HomogeneousLocalization ğ’œ x) (Localization x) where
  smul p q := p.val * q
  algebraMap :=
  { toFun := val
    map_one' := val_one
    map_mul' := val_mul
    map_zero' := val_zero
    map_add' := val_add }
  commutes' _ _ := mul_comm _ _
  smul_def' _ _ := rfl

@[simp] lemma algebraMap_apply (y) :
    algebraMap (HomogeneousLocalization ğ’œ x) (Localization x) y = y.val := rfl

lemma mk_eq_zero_of_num (f : NumDenSameDeg ğ’œ x) (h : f.num = 0) : mk f = 0 := by
  apply val_injective
  simp only [val_mk, val_zero, h, ZeroMemClass.coe_zero, Localization.mk_zero]

lemma mk_eq_zero_of_den (f : NumDenSameDeg ğ’œ x) (h : f.den = 0) : mk f = 0 := by
  have := subsingleton ğ’œ (h â–¸ f.den_mem)
  exact Subsingleton.elim _ _

variable (ğ’œ x) in
/-- The map from `ğ’œ 0` to the degree `0` part of `ğ’œâ‚“` sending `f â†¦ f/1`. -/
def fromZeroRingHom : ğ’œ 0 â†’+* HomogeneousLocalization ğ’œ x where
  toFun f := .mk âŸ¨0, f, 1, one_mem _âŸ©
  map_one' := rfl
  map_mul' f g := by ext; simp [Localization.mk_mul]
  map_zero' := rfl
  map_add' f g := by ext; simp [Localization.add_mk, add_comm f.1 g.1]

instance : Algebra (ğ’œ 0) (HomogeneousLocalization ğ’œ x) :=
  (fromZeroRingHom ğ’œ x).toAlgebra

lemma algebraMap_eq : algebraMap (ğ’œ 0) (HomogeneousLocalization ğ’œ x) = fromZeroRingHom ğ’œ x := rfl

instance : IsScalarTower (ğ’œ 0) (HomogeneousLocalization ğ’œ x) (Localization x) :=
  .of_algebraMap_eq' rfl

end HomogeneousLocalization

namespace HomogeneousLocalization

open HomogeneousLocalization HomogeneousLocalization.NumDenSameDeg

variable {ğ’œ} {x}

/-- Numerator of an element in `HomogeneousLocalization x`. -/
def num (f : HomogeneousLocalization ğ’œ x) : A :=
  (Quotient.out f).num

/-- Denominator of an element in `HomogeneousLocalization x`. -/
def den (f : HomogeneousLocalization ğ’œ x) : A :=
  (Quotient.out f).den

/-- For an element in `HomogeneousLocalization x`, degree is the natural number `i` such that
  `ğ’œ i` contains both numerator and denominator. -/
def deg (f : HomogeneousLocalization ğ’œ x) : Î¹ :=
  (Quotient.out f).deg

theorem den_mem (f : HomogeneousLocalization ğ’œ x) : f.den âˆˆ x :=
  (Quotient.out f).den_mem

theorem num_mem_deg (f : HomogeneousLocalization ğ’œ x) : f.num âˆˆ ğ’œ f.deg :=
  (Quotient.out f).num.2

theorem den_mem_deg (f : HomogeneousLocalization ğ’œ x) : f.den âˆˆ ğ’œ f.deg :=
  (Quotient.out f).den.2

theorem eq_num_div_den (f : HomogeneousLocalization ğ’œ x) :
    f.val = Localization.mk f.num âŸ¨f.den, f.den_memâŸ© :=
  congr_arg HomogeneousLocalization.val (Quotient.out_eq' f).symm

theorem den_smul_val (f : HomogeneousLocalization ğ’œ x) :
    f.den â€¢ f.val = algebraMap _ _ f.num := by
  rw [eq_num_div_den, Localization.mk_eq_mk', IsLocalization.smul_mk']
  exact IsLocalization.mk'_mul_cancel_left _ âŸ¨_, _âŸ©

theorem ext_iff_val (f g : HomogeneousLocalization ğ’œ x) : f = g â†” f.val = g.val :=
  âŸ¨congr_arg val, fun e â†¦ val_injective x eâŸ©

section

variable [AddCommMonoid Î¹] [DecidableEq Î¹] [GradedAlgebra ğ’œ]
variable (ğ’œ) (ğ”­ : Ideal A) [Ideal.IsPrime ğ”­]

/-- Localizing a ring homogeneously at a prime ideal. -/
abbrev AtPrime :=
  HomogeneousLocalization ğ’œ ğ”­.primeCompl

theorem isUnit_iff_isUnit_val (f : HomogeneousLocalization.AtPrime ğ’œ ğ”­) :
    IsUnit f.val â†” IsUnit f := by
  refine âŸ¨fun h1 â†¦ ?_, IsUnit.map (algebraMap _ _)âŸ©
  rcases h1 with âŸ¨âŸ¨a, b, eq0, eq1âŸ©, rfl : a = f.valâŸ©
  obtain âŸ¨f, rflâŸ© := mk_surjective f
  obtain âŸ¨b, s, rflâŸ© := IsLocalization.mk'_surjective ğ”­.primeCompl b
  rw [val_mk, Localization.mk_eq_mk', â† IsLocalization.mk'_mul, IsLocalization.mk'_eq_iff_eq_mul,
    one_mul, IsLocalization.eq_iff_exists (M := ğ”­.primeCompl)] at eq0
  obtain âŸ¨c, hc : _ = c.1 * (f.den.1 * s.1)âŸ© := eq0
  have : f.num.1 âˆ‰ ğ”­ := by
    exact fun h â†¦ mul_mem c.2 (mul_mem f.den_mem s.2)
      (hc â–¸ Ideal.mul_mem_left _ c.1 (Ideal.mul_mem_right b _ h))
  refine isUnit_of_mul_eq_one _ (Quotient.mk'' âŸ¨f.1, f.3, f.2, thisâŸ©) ?_
  rw [â† mk_mul, ext_iff_val, val_mk]
  simp [mul_comm f.den.1]

instance : Nontrivial (HomogeneousLocalization.AtPrime ğ’œ ğ”­) :=
  âŸ¨âŸ¨0, 1, fun r => by simp [ext_iff_val, val_zero, val_one, zero_ne_one] at râŸ©âŸ©

instance isLocalRing : IsLocalRing (HomogeneousLocalization.AtPrime ğ’œ ğ”­) :=
  IsLocalRing.of_isUnit_or_isUnit_one_sub_self fun a => by
    simpa only [â† isUnit_iff_isUnit_val, val_sub, val_one]
      using IsLocalRing.isUnit_or_isUnit_one_sub_self _

end

section

variable (ğ’œ) (f : A)

/-- Localizing away from powers of `f` homogeneously. -/
abbrev Away :=
  HomogeneousLocalization ğ’œ (Submonoid.powers f)

variable [AddCommMonoid Î¹] [DecidableEq Î¹] [GradedAlgebra ğ’œ]
variable {ğ’œ} {f}

theorem Away.eventually_smul_mem {m} (hf : f âˆˆ ğ’œ m) (z : Away ğ’œ f) :
    âˆ€á¶  n in Filter.atTop, f ^ n â€¢ z.val âˆˆ algebraMap _ _ '' (ğ’œ (n â€¢ m) : Set A) := by
  obtain âŸ¨k, hk : f ^ k = _âŸ© := z.den_mem
  apply Filter.mem_of_superset (Filter.Ici_mem_atTop k)
  rintro k' (hk' : k â‰¤ k')
  simp only [Set.mem_image, SetLike.mem_coe, Set.mem_setOf_eq]
  by_cases hfk : f ^ k = 0
  Â· refine âŸ¨0, zero_mem _, ?_âŸ©
    rw [â† tsub_add_cancel_of_le hk', map_zero, pow_add, hfk, mul_zero, zero_smul]
  rw [â† tsub_add_cancel_of_le hk', pow_add, mul_smul, hk, den_smul_val,
    Algebra.smul_def, â† map_mul]
  rw [â† smul_eq_mul, add_smul,
    DirectSum.degree_eq_of_mem_mem ğ’œ (SetLike.pow_mem_graded _ hf) (hk.symm â–¸ z.den_mem_deg) hfk]
  exact âŸ¨_, SetLike.mul_mem_graded (SetLike.pow_mem_graded _ hf) z.num_mem_deg, rflâŸ©

end

section

variable [AddCommMonoid Î¹] [DecidableEq Î¹] [GradedAlgebra ğ’œ]
variable (ğ’œ)
variable {B : Type*} [CommRing B] [Algebra R B]
variable (â„¬ : Î¹ â†’ Submodule R B) [GradedAlgebra â„¬]
variable {P : Submonoid A} {Q : Submonoid B}

/--
Let `A, B` be two graded algebras with the same indexing set and `g : A â†’ B` be a graded algebra
homomorphism (i.e. `g(Aâ‚˜) âŠ† Bâ‚˜`). Let `P â‰¤ A` be a submonoid and `Q â‰¤ B` be a submonoid such that
`P â‰¤ gâ»Â¹ Q`, then `g` induce a map from the homogeneous localizations `Aâ°_P` to the homogeneous
localizations `Bâ°_Q`.
-/
def map (g : A â†’+* B)
    (comap_le : P â‰¤ Q.comap g) (hg : âˆ€ i, âˆ€ a âˆˆ ğ’œ i, g a âˆˆ â„¬ i) :
    HomogeneousLocalization ğ’œ P â†’+* HomogeneousLocalization â„¬ Q where
  toFun := Quotient.map'
    (fun x â†¦ âŸ¨x.1, âŸ¨_, hg _ _ x.2.2âŸ©, âŸ¨_, hg _ _ x.3.2âŸ©, comap_le x.4âŸ©)
    fun x y (e : x.embedding = y.embedding) â†¦ by
      apply_fun IsLocalization.map (Localization Q) g comap_le at e
      simp_rw [HomogeneousLocalization.NumDenSameDeg.embedding, Localization.mk_eq_mk',
        IsLocalization.map_mk', â† Localization.mk_eq_mk'] at e
      exact e
  map_add' := Quotient.indâ‚‚' fun x y â†¦ by
    simp only [â† mk_add, Quotient.map'_mk'', num_add, map_add, map_mul, den_add]; rfl
  map_mul' := Quotient.indâ‚‚' fun x y â†¦ by
    simp only [â† mk_mul, Quotient.map'_mk'', num_mul, map_mul, den_mul]; rfl
  map_zero' := by simp only [â† mk_zero (ğ’œ := ğ’œ), Quotient.map'_mk'', deg_zero,
    num_zero, ZeroMemClass.coe_zero, map_zero, den_zero, map_one]; rfl
  map_one' := by simp only [â† mk_one (ğ’œ := ğ’œ), Quotient.map'_mk'',
    num_one, den_one, map_one]; rfl

/--
Let `A` be a graded algebra and `P â‰¤ Q` be two submonoids, then the homogeneous localization of `A`
at `P` embeds into the homogeneous localization of `A` at `Q`.
-/
abbrev mapId {P Q : Submonoid A} (h : P â‰¤ Q) :
    HomogeneousLocalization ğ’œ P â†’+* HomogeneousLocalization ğ’œ Q :=
  map ğ’œ ğ’œ (RingHom.id _) h (fun _ _ â†¦ id)

lemma map_mk (g : A â†’+* B)
    (comap_le : P â‰¤ Q.comap g) (hg : âˆ€ i, âˆ€ a âˆˆ ğ’œ i, g a âˆˆ â„¬ i) (x) :
    map ğ’œ â„¬ g comap_le hg (mk x) =
    mk âŸ¨x.1, âŸ¨_, hg _ _ x.2.2âŸ©, âŸ¨_, hg _ _ x.3.2âŸ©, comap_le x.4âŸ© :=
  rfl

end

section mapAway

variable [AddCommMonoid Î¹] [DecidableEq Î¹] [GradedAlgebra ğ’œ]
variable {e : Î¹} {f : A} {g : A} (hg : g âˆˆ ğ’œ e) {x : A} (hx : x = f * g)

variable (ğ’œ)

/-- Given `f âˆ£ x`, this is the map `A_{(f)} â†’ A_f â†’ A_x`. We will lift this to a map
`A_{(f)} â†’ A_{(x)}` in `awayMap`. -/
private def awayMapAux (hx : f âˆ£ x) : Away ğ’œ f â†’+* Localization.Away x :=
  (Localization.awayLift (algebraMap A _) _
    (isUnit_of_dvd_unit (map_dvd _ hx) (IsLocalization.Away.algebraMap_isUnit x))).comp
      (algebraMap (Away ğ’œ f) (Localization.Away f))

lemma awayMapAux_mk (n a i hi) :
    awayMapAux ğ’œ âŸ¨_, hxâŸ© (mk âŸ¨n, a, âŸ¨f ^ i, hiâŸ©, âŸ¨i, rflâŸ©âŸ©) =
      Localization.mk (a * g ^ i) âŸ¨x ^ i, (Submonoid.mem_powers_iff _ _).mpr âŸ¨i, rflâŸ©âŸ© := by
  have : algebraMap A (Localization.Away x) f *
    (Localization.mk g âŸ¨f * g, (Submonoid.mem_powers_iff _ _).mpr âŸ¨1, by simp [hx]âŸ©âŸ©) = 1 := by
    rw [â† Algebra.smul_def, Localization.smul_mk]
    exact Localization.mk_self âŸ¨f*g, _âŸ©
  simp only [awayMapAux, RingHom.coe_comp, Function.comp_apply, algebraMap_apply, val_mk]
  rw [Localization.awayLift_mk (hv := this), â† Algebra.smul_def,
    Localization.mk_pow, Localization.smul_mk]
  subst hx
  rfl

include hg in
lemma range_awayMapAux_subset :
    Set.range (awayMapAux ğ’œ (f := f) âŸ¨_, hxâŸ©) âŠ† Set.range (val (ğ’œ := ğ’œ)) := by
  rintro _ âŸ¨z, rflâŸ©
  obtain âŸ¨âŸ¨n, âŸ¨a, haâŸ©, âŸ¨b, hb'âŸ©, j, rfl : _ = bâŸ©, rflâŸ© := mk_surjective z
  use mk âŸ¨n+jâ€¢e,âŸ¨a*g^j, ?_âŸ©, âŸ¨x^j, ?_âŸ©, j, rflâŸ©
  Â· simp [awayMapAux_mk ğ’œ (hx := hx)]
  Â· apply SetLike.mul_mem_graded ha
    exact SetLike.pow_mem_graded _ hg
  Â· rw [hx, mul_pow]
    apply SetLike.mul_mem_graded hb'
    exact SetLike.pow_mem_graded _ hg

/-- Given `x = f * g` with `g` homogeneous of positive degree,
this is the map `A_{(f)} â†’ A_{(x)}` taking `a/f^i` to `ag^i/(fg)^i`. -/
def awayMap : Away ğ’œ f â†’+* Away ğ’œ x := by
  let e := RingEquiv.ofLeftInverse (f := algebraMap (Away ğ’œ x) (Localization.Away x))
    (h := (val_injective _).hasLeftInverse.choose_spec)
  refine RingHom.comp (e.symm.toRingHom.comp (Subring.inclusion ?_))
    (awayMapAux ğ’œ (f := f) âŸ¨_, hxâŸ©).rangeRestrict
  exact range_awayMapAux_subset ğ’œ hg hx

lemma val_awayMap_eq_aux (a) : (awayMap ğ’œ hg hx a).val = awayMapAux ğ’œ âŸ¨_, hxâŸ© a := by
  let e := RingEquiv.ofLeftInverse (f := algebraMap (Away ğ’œ x) (Localization.Away x))
    (h := (val_injective _).hasLeftInverse.choose_spec)
  dsimp [awayMap]
  convert_to (e (e.symm âŸ¨awayMapAux ğ’œ (f := f) âŸ¨_, hxâŸ© a,
    range_awayMapAux_subset ğ’œ hg hx âŸ¨_, rflâŸ©âŸ©)).1 = _
  rw [e.apply_symm_apply]

lemma val_awayMap (a) : (awayMap ğ’œ hg hx a).val = Localization.awayLift (algebraMap A _) _
    (isUnit_of_dvd_unit (map_dvd _ âŸ¨_, hxâŸ©) (IsLocalization.Away.algebraMap_isUnit x)) a.val := by
  rw [val_awayMap_eq_aux]
  rfl

lemma awayMap_fromZeroRingHom (a) :
    awayMap ğ’œ hg hx (fromZeroRingHom ğ’œ _ a) = fromZeroRingHom ğ’œ _ a := by
  ext
  simp only [fromZeroRingHom, RingHom.coe_mk, MonoidHom.coe_mk, OneHom.coe_mk,
    val_awayMap, val_mk, SetLike.GradeZero.coe_one]
  convert IsLocalization.lift_eq _ _

lemma val_awayMap_mk (n a i hi) : (awayMap ğ’œ hg hx (mk âŸ¨n, a, âŸ¨f ^ i, hiâŸ©, âŸ¨i, rflâŸ©âŸ©)).val =
    Localization.mk (a * g ^ i) âŸ¨x ^ i, (Submonoid.mem_powers_iff _ _).mpr âŸ¨i, rflâŸ©âŸ© := by
  rw [val_awayMap_eq_aux, awayMapAux_mk ğ’œ (hx := hx)]

/-- Given `x = f * g` with `g` homogeneous of positive degree,
this is the map `A_{(f)} â†’ A_{(x)}` taking `a/f^i` to `ag^i/(fg)^i`. -/
def awayMapâ‚ : Away ğ’œ f â†’â‚[ğ’œ 0] Away ğ’œ x where
  __ := awayMap ğ’œ hg hx
  commutes' _ := awayMap_fromZeroRingHom ..

@[simp] lemma awayMapâ‚_apply (a) : awayMapâ‚ ğ’œ hg hx a = awayMap ğ’œ hg hx a := rfl

/-- This is a convenient constructor for `Away ğ’œ f` when `f` is homogeneous.
`Away.mk ğ’œ hf n x hx` is the fraction `x / f ^ n`. -/
protected def Away.mk {d : Î¹} (hf : f âˆˆ ğ’œ d) (n : â„•) (x : A) (hx : x âˆˆ ğ’œ (n â€¢ d)) : Away ğ’œ f :=
  HomogeneousLocalization.mk âŸ¨n â€¢ d, âŸ¨x, hxâŸ©, âŸ¨f ^ n, SetLike.pow_mem_graded n hfâŸ©, âŸ¨n, rflâŸ©âŸ©

@[simp]
lemma Away.val_mk {d : Î¹} (n : â„•) (hf : f âˆˆ ğ’œ d) (x : A) (hx : x âˆˆ ğ’œ (n â€¢ d)) :
    (Away.mk ğ’œ hf n x hx).val = Localization.mk x âŸ¨f ^ n, by use nâŸ© :=
  rfl

protected
lemma Away.mk_surjective {d : Î¹} (hf : f âˆˆ ğ’œ d) (x : Away ğ’œ f) :
    âˆƒ n a ha, Away.mk ğ’œ hf n a ha = x := by
  obtain âŸ¨âŸ¨N, âŸ¨s, hsâŸ©, âŸ¨b, hnâŸ©, âŸ¨n, (rfl : _ = b)âŸ©âŸ©, rflâŸ© := mk_surjective x
  by_cases hfn : f ^ n = 0
  Â· have := HomogeneousLocalization.subsingleton ğ’œ (x := .powers f) âŸ¨n, hfnâŸ©
    exact âŸ¨0, 0, zero_mem _, Subsingleton.elim _ _âŸ©
  obtain rfl := DirectSum.degree_eq_of_mem_mem ğ’œ hn (SetLike.pow_mem_graded n hf) hfn
  exact âŸ¨n, s, hs, by ext; simpâŸ©

open SetLike in
@[simp]
lemma awayMap_mk {d : Î¹} (n : â„•) (hf : f âˆˆ ğ’œ d) (a : A) (ha : a âˆˆ ğ’œ (n â€¢ d)) :
    awayMap ğ’œ hg hx (Away.mk ğ’œ hf n a ha) = Away.mk ğ’œ (hx â–¸ mul_mem_graded hf hg) n
      (a * g ^ n) (by rw [smul_add]; exact mul_mem_graded ha (pow_mem_graded n hg)) := by
  ext
  exact val_awayMap_mk ..

end mapAway

section isLocalization

variable {ğ’œ : â„• â†’ Submodule R A} [GradedAlgebra ğ’œ]
variable {e d : â„•} {f : A} (hf : f âˆˆ ğ’œ d) {g : A} (hg : g âˆˆ ğ’œ e)

/-- The element `t := g ^ d / f ^ e` such that `A_{(fg)} = A_{(f)}[1/t]`. -/
abbrev Away.isLocalizationElem : Away ğ’œ f :=
  Away.mk ğ’œ hf e (g ^ d) (by convert SetLike.pow_mem_graded d hg using 2; exact mul_comm _ _)

variable {x : A} (hx : x = f * g)

/-- Let `t := g ^ d / f ^ e`, then `A_{(fg)} = A_{(f)}[1/t]`. -/
theorem Away.isLocalization_mul (hd : d â‰  0) :
    letI := (awayMap ğ’œ hg hx).toAlgebra
    IsLocalization.Away (isLocalizationElem hf hg) (Away ğ’œ x) := by
  letI := (awayMap ğ’œ hg hx).toAlgebra
  constructor
  Â· rintro âŸ¨r, n, rflâŸ©
    rw [map_pow, RingHom.algebraMap_toAlgebra]
    let z : Away ğ’œ x := Away.mk ğ’œ (hx â–¸ SetLike.mul_mem_graded hf hg) (d + e)
        (g ^ e * f ^ (2 * e + d)) <| by
      convert SetLike.mul_mem_graded (SetLike.pow_mem_graded e hg)
        (SetLike.pow_mem_graded (2 * e + d) hf) using 2
      ring
    refine (isUnit_iff_exists_inv.mpr âŸ¨z, ?_âŸ©).pow _
    ext
    simp only [val_mul, val_one, awayMap_mk, Away.val_mk, z, Localization.mk_mul]
    rw [â† Localization.mk_one, Localization.mk_eq_mk_iff, Localization.r_iff_exists]
    use 1
    simp only [OneMemClass.coe_one, one_mul, Submonoid.coe_mul, mul_one, hx]
    ring
  Â· intro z
    obtain âŸ¨n, s, hs, rflâŸ© := Away.mk_surjective ğ’œ (hx â–¸ SetLike.mul_mem_graded hf hg) z
    rcases d with - | d
    Â· contradiction
    let t : Away ğ’œ f := Away.mk ğ’œ hf (n * (e + 1)) (s * g ^ (n * d)) <| by
      convert SetLike.mul_mem_graded hs (SetLike.pow_mem_graded _ hg) using 2; simp; ring
    refine âŸ¨âŸ¨t, âŸ¨_, âŸ¨n, rflâŸ©âŸ©âŸ©, ?_âŸ©
    ext
    simp only [RingHom.algebraMap_toAlgebra, map_pow, awayMap_mk, val_mul, val_mk, val_pow,
      Localization.mk_pow, Localization.mk_mul, t]
    rw [Localization.mk_eq_mk_iff, Localization.r_iff_exists]
    exact âŸ¨1, by simp; ringâŸ©
  Â· intro a b e
    obtain âŸ¨n, a, ha, rflâŸ© := Away.mk_surjective ğ’œ hf a
    obtain âŸ¨m, b, hb, rflâŸ© := Away.mk_surjective ğ’œ hf b
    replace e := congr_arg val e
    simp only [RingHom.algebraMap_toAlgebra, awayMap_mk, val_mk,
      Localization.mk_eq_mk_iff, Localization.r_iff_exists] at e
    obtain âŸ¨âŸ¨_, k, rflâŸ©, hcâŸ© := e
    refine âŸ¨âŸ¨_, k + m + n, rflâŸ©, ?_âŸ©
    ext
    simp only [val_mul, val_pow, val_mk, Localization.mk_pow,
      Localization.mk_eq_mk_iff, Localization.r_iff_exists, Submonoid.coe_mul, Localization.mk_mul,
      SubmonoidClass.coe_pow, Subtype.exists, exists_prop]
    refine âŸ¨_, âŸ¨k, rflâŸ©, ?_âŸ©
    rcases d with - | d
    Â· contradiction
    subst hx
    convert congr(f ^ (e * (k + m + n)) * g ^ (d * (k + m + n)) * $hc) using 1 <;> ring

end isLocalization

section span

variable [AddCommMonoid Î¹] [DecidableEq Î¹] {ğ’œ : Î¹ â†’ Submodule R A} [GradedAlgebra ğ’œ] in
/--
Let `ğ’œ` be a graded algebra, finitely generated (as an algebra) over `ğ’œâ‚€` by `{ váµ¢ }`,
where `váµ¢` has degree `dváµ¢`.
If `f : A` has degree `d`, then `ğ’œ_(f)` is generated (as a module) over `ğ’œâ‚€` by
elements of the form `(âˆ i, váµ¢ ^ aáµ¢) / fáµƒ` such that `âˆ‘ aáµ¢ â€¢ dváµ¢ = a â€¢ d`.
-/
theorem Away.span_mk_prod_pow_eq_top {f : A} {d : Î¹} (hf : f âˆˆ ğ’œ d)
    {Î¹' : Type*} [Fintype Î¹'] (v : Î¹' â†’ A)
    (hx : Algebra.adjoin (ğ’œ 0) (Set.range v) = âŠ¤) (dv : Î¹' â†’ Î¹) (hxd : âˆ€ i, v i âˆˆ ğ’œ (dv i)) :
    Submodule.span (ğ’œ 0) { (Away.mk ğ’œ hf a (âˆ i, v i ^ ai i)
      (hai â–¸ SetLike.prod_pow_mem_graded _ _ _ _ fun i _ â†¦ hxd i) : Away ğ’œ f) |
        (a : â„•) (ai : Î¹' â†’ â„•) (hai : âˆ‘ i, ai i â€¢ dv i = a â€¢ d) } = âŠ¤ := by
  by_cases HH : Subsingleton (HomogeneousLocalization.Away ğ’œ f)
  Â· exact Subsingleton.elim _ _
  rw [â† top_le_iff]
  rintro x -
  obtain âŸ¨âŸ¨n, âŸ¨a, haâŸ©, âŸ¨b, hb'âŸ©, âŸ¨j, (rfl : _ = b)âŸ©âŸ©, rflâŸ© := mk_surjective x
  by_cases hfj : f ^ j = 0
  Â· exact (HH (HomogeneousLocalization.subsingleton _ âŸ¨_, hfjâŸ©)).elim
  have : DirectSum.decompose ğ’œ a n = âŸ¨a, haâŸ© := Subtype.ext (DirectSum.decompose_of_mem_same ğ’œ ha)
  simp_rw [â† this]
  clear this ha
  have : a âˆˆ Submodule.span (ğ’œ 0) (Submonoid.closure (Set.range v)) := by
    rw [â† Algebra.adjoin_eq_span, hx]
    trivial
  induction this using Submodule.span_induction with
  | mem a ha' =>
    obtain âŸ¨ai, rflâŸ© := Submonoid.exists_of_mem_closure_range _ _ ha'
    clear ha'
    by_cases H : âˆ‘ i, ai i â€¢ dv i = n
    Â· apply Submodule.subset_span
      refine âŸ¨j, ai, H.trans ?_, ?_âŸ©
      Â· exact DirectSum.degree_eq_of_mem_mem ğ’œ hb'
          (SetLike.pow_mem_graded j hf) hfj
      Â· ext
        simp only [val_mk, Away.val_mk]
        congr
        refine (DirectSum.decompose_of_mem_same _ ?_).symm
        exact H â–¸ SetLike.prod_pow_mem_graded _ _ _ _ fun i _ â†¦ hxd i
    Â· convert zero_mem (Submodule.span (ğ’œ 0) _)
      ext
      have : (DirectSum.decompose ğ’œ (âˆ i : Î¹', v i ^ ai i) n).1 = 0 := by
        refine DirectSum.decompose_of_mem_ne _ ?_ H
        exact SetLike.prod_pow_mem_graded _ _ _ _ fun i _ â†¦ hxd i
      simp [this, Localization.mk_zero]
  | zero =>
    convert zero_mem (Submodule.span (ğ’œ 0) _)
    ext; simp [Localization.mk_zero]
  | add s t hs ht hs' ht' =>
    convert add_mem hs' ht'
    ext; simp [â† Localization.add_mk_self]
  | smul r x hx hx' =>
    convert Submodule.smul_mem _ r hx'
    ext
    simp [Algebra.smul_def, algebraMap_eq, fromZeroRingHom, Localization.mk_mul,
      -decompose_mul, coe_decompose_mul_of_left_mem_zero ğ’œ r.2]

variable {ğ’œ : â„• â†’ Submodule R A} [GradedAlgebra ğ’œ] in
/-- This is strictly weaker than `Away.adjoin_mk_prod_pow_eq_top`. -/
private
theorem Away.adjoin_mk_prod_pow_eq_top_of_pos {f : A} {d : â„•} (hf : f âˆˆ ğ’œ d)
    {Î¹' : Type*} [Fintype Î¹'] (v : Î¹' â†’ A)
    (hx : Algebra.adjoin (ğ’œ 0) (Set.range v) = âŠ¤) (dv : Î¹' â†’ â„•)
    (hxd : âˆ€ i, v i âˆˆ ğ’œ (dv i)) (hxd' : âˆ€ i, 0 < dv i) :
    Algebra.adjoin (ğ’œ 0) { Away.mk ğ’œ hf a (âˆ i, v i ^ ai i)
      (hai â–¸ SetLike.prod_pow_mem_graded _ _ _ _ fun i _ â†¦ hxd i) |
        (a : â„•) (ai : Î¹' â†’ â„•) (hai : âˆ‘ i, ai i â€¢ dv i = a â€¢ d) (_ : âˆ€ i, ai i â‰¤ d) } = âŠ¤ := by
  rw [â† top_le_iff]
  change âŠ¤ â‰¤ (Algebra.adjoin (ğ’œ 0) _).toSubmodule
  rw [â† HomogeneousLocalization.Away.span_mk_prod_pow_eq_top hf v hx dv hxd, Submodule.span_le]
  rintro _ âŸ¨a, ai, hai, rflâŸ©
  have Hâ‚€ : (a - âˆ‘ i : Î¹', dv i * (ai i / d)) â€¢ d = âˆ‘ k : Î¹', (ai k % d) â€¢ dv k := by
    rw [smul_eq_mul, tsub_mul, â† smul_eq_mul, â† hai]
    conv => enter [1, 1, 2, i]; rw [â† Nat.mod_add_div (ai i) d]
    simp_rw [smul_eq_mul, add_mul, Finset.sum_add_distrib,
      mul_assoc, â† Finset.mul_sum, mul_comm d, mul_comm (_ / _)]
    simp only [add_tsub_cancel_right]
  have H : Away.mk ğ’œ hf a (âˆ i, v i ^ ai i)
      (hai â–¸ SetLike.prod_pow_mem_graded _ _ _ _ fun i _ â†¦ hxd i) =
      Away.mk ğ’œ hf (a - âˆ‘ i : Î¹', dv i * (ai i / d)) (âˆ i, v i ^ (ai i % d))
      (Hâ‚€ â–¸ SetLike.prod_pow_mem_graded _ _ _ _ fun i _ â†¦ hxd i) *
      âˆ i, Away.isLocalizationElem hf (hxd i) ^ (ai i / d) := by
    apply (show Function.Injective (algebraMap (Away ğ’œ f) (Localization.Away f))
      from val_injective _)
    simp only [map_pow, map_prod, map_mul]
    simp only [HomogeneousLocalization.algebraMap_apply, val_mk,
      Localization.mk_pow, Localization.mk_prod, Localization.mk_mul,
      â† Finset.prod_mul_distrib, â† pow_add, â† pow_mul]
    congr
    Â· ext i
      congr
      exact Eq.symm (Nat.mod_add_div (ai i) d)
    Â· simp only [SubmonoidClass.coe_finset_prod, â† pow_add, â† pow_mul,
        Finset.prod_pow_eq_pow_sum, SubmonoidClass.coe_pow]
      rw [tsub_add_cancel_of_le]
      rcases d.eq_zero_or_pos with hd | hd
      Â· simp [hd]
      rw [â† mul_le_mul_iff_of_pos_right hd, â† smul_eq_mul (a := a), â† hai, Finset.sum_mul]
      simp_rw [smul_eq_mul, mul_comm (ai _), mul_assoc]
      gcongr
      exact Nat.div_mul_le_self (ai _) d
  rw [H, SetLike.mem_coe]
  apply (Algebra.adjoin (ğ’œ 0) _).mul_mem
  Â· apply Algebra.subset_adjoin
    refine âŸ¨a - âˆ‘ i : Î¹', dv i * (ai i / d), (ai Â· % d), Hâ‚€.symm, ?_, rflâŸ©
    rcases d.eq_zero_or_pos with hd | hd
    Â· have : âˆ€ (x : Î¹'), ai x = 0 := by simpa [hd, fun i â†¦ (hxd' i).ne'] using hai
      simp [this]
    exact fun i â†¦ (Nat.mod_lt _ hd).le
  apply prod_mem
  Â· classical
    rintro j -
    apply pow_mem
    apply Algebra.subset_adjoin
    refine âŸ¨dv j, Pi.single j d, ?_, ?_, ?_âŸ©
    Â· simp [Pi.single_apply, mul_comm]
    Â· aesop (add simp Pi.single_apply)
    ext
    simp [Pi.single_apply]

variable {ğ’œ : â„• â†’ Submodule R A} [GradedAlgebra ğ’œ] in
/--
Let `ğ’œ` be a graded algebra, finitely generated (as an algebra) over `ğ’œâ‚€` by `{ váµ¢ }`,
where `váµ¢` has degree `dváµ¢`.
If `f : A` has degree `d`, then `ğ’œ_(f)` is generated (as an algebra) over `ğ’œâ‚€` by
elements of the form `(âˆ i, váµ¢ ^ aáµ¢) / fáµƒ` such that `âˆ‘ aáµ¢ â€¢ dváµ¢ = a â€¢ d` and `âˆ€ i, aáµ¢ â‰¤ d`.
-/
theorem Away.adjoin_mk_prod_pow_eq_top {f : A} {d : â„•} (hf : f âˆˆ ğ’œ d)
    (Î¹' : Type*) [Fintype Î¹'] (v : Î¹' â†’ A)
    (hx : Algebra.adjoin (ğ’œ 0) (Set.range v) = âŠ¤) (dv : Î¹' â†’ â„•) (hxd : âˆ€ i, v i âˆˆ ğ’œ (dv i)) :
    Algebra.adjoin (ğ’œ 0) { Away.mk ğ’œ hf a (âˆ i, v i ^ ai i)
      (hai â–¸ SetLike.prod_pow_mem_graded _ _ _ _ fun i _ â†¦ hxd i) |
        (a : â„•) (ai : Î¹' â†’ â„•) (hai : âˆ‘ i, ai i â€¢ dv i = a â€¢ d) (_ : âˆ€ i, ai i â‰¤ d) } = âŠ¤ := by
  classical
  let s := Finset.univ.filter (0 < dv Â·)
  have := Away.adjoin_mk_prod_pow_eq_top_of_pos hf (Î¹' := s) (v âˆ˜ Subtype.val) ?_
    (dv âˆ˜ Subtype.val) (fun _ â†¦ hxd _) (by simp [s])
  swap
  Â· rw [â† top_le_iff, â† hx, Algebra.adjoin_le_iff, Set.range_subset_iff]
    intro i
    rcases (dv i).eq_zero_or_pos with hi | hi
    Â· exact algebraMap_mem (R := ğ’œ 0) _ âŸ¨v i, hi â–¸ hxd iâŸ©
    exact Algebra.subset_adjoin âŸ¨âŸ¨i, by simpa [s] using hiâŸ©, rflâŸ©
  rw [â† top_le_iff, â† this]
  apply Algebra.adjoin_mono
  rintro _ âŸ¨a, ai, hai : âˆ‘ x âˆˆ s.attach, _ = _, h, rflâŸ©
  refine âŸ¨a, fun i â†¦ if hi : i âˆˆ s then ai âŸ¨i, hiâŸ© else 0, ?_, ?_, ?_âŸ©
  Â· simpa [Finset.sum_attach_eq_sum_dite] using hai
  Â· simp [apply_dite, dite_apply, h]
  Â· congr 1
    change _ = âˆ x âˆˆ s.attach, _
    simp [Finset.prod_attach_eq_prod_dite]

variable {ğ’œ : â„• â†’ Submodule R A} [GradedAlgebra ğ’œ] [Algebra.FiniteType (ğ’œ 0) A] in
lemma Away.finiteType (f : A) (d : â„•) (hf : f âˆˆ ğ’œ d) :
    Algebra.FiniteType (ğ’œ 0) (Away ğ’œ f) := by
  constructor
  obtain âŸ¨s, hs, hs'âŸ© := GradedAlgebra.exists_finset_adjoin_eq_top_and_homogeneous_ne_zero ğ’œ
  choose dx hdx hxd using Subtype.forall'.mp hs'
  simp_rw [Subalgebra.fg_def, â† top_le_iff,
    â† Away.adjoin_mk_prod_pow_eq_top hf (Î¹' := s) Subtype.val (by simpa) dx hxd]
  rcases d.eq_zero_or_pos with hd | hd
  Â· let f' := Away.mk ğ’œ hf 1 1 (by simp [hd, GradedOne.one_mem])
    refine âŸ¨{f'}, Set.finite_singleton f', ?_âŸ©
    rw [Algebra.adjoin_le_iff]
    rintro _ âŸ¨a, ai, hai, hai', rflâŸ©
    obtain rfl : ai = 0 := funext <| by simpa [hd, hdx] using hai
    simp only [Finset.univ_eq_attach, Pi.zero_apply, pow_zero, Finset.prod_const_one, mem_coe]
    convert pow_mem (Algebra.self_mem_adjoin_singleton (ğ’œ 0) f') a using 1
    ext
    simp [f', Localization.mk_pow]
  refine âŸ¨_, ?_, le_rflâŸ©
  let b := âˆ‘ i, dx i
  let s' : Set ((Fin (b + 1)) Ã— (s â†’ Fin (d + 1))) := { ai | âˆ‘ i, (ai.2 i).1 * dx i = ai.1 * d }
  let F : s' â†’ Away ğ’œ f := fun ai â†¦ Away.mk ğ’œ hf ai.1.1.1 (âˆ i, i ^ (ai.1.2 i).1)
    (by convert SetLike.prod_pow_mem_graded _ _ _ _ fun i _ â†¦ hxd i; exact ai.2.symm)
  apply (Set.finite_range F).subset
  rintro _ âŸ¨a, ai, hai, hai', rflâŸ©
  refine âŸ¨âŸ¨âŸ¨âŸ¨a, ?_âŸ©, fun i â†¦ âŸ¨ai i, (hai' i).trans_lt d.lt_succ_selfâŸ©âŸ©, haiâŸ©, rflâŸ©
  rw [Nat.lt_succ, â† mul_le_mul_iff_of_pos_right hd, â† smul_eq_mul, â† hai, Finset.sum_mul]
  simp_rw [smul_eq_mul, mul_comm _ d]
  gcongr
  exact hai' _

end span

end HomogeneousLocalization
