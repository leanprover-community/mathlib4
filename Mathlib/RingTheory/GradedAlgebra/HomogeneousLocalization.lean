/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Eric Wieser
-/
import Mathlib.RingTheory.Localization.AtPrime
import Mathlib.RingTheory.GradedAlgebra.Basic

#align_import ring_theory.graded_algebra.homogeneous_localization from "leanprover-community/mathlib"@"831c494092374cfe9f50591ed0ac81a25efc5b86"

/-!
# Homogeneous Localization

## Notation
- `Î¹` is a commutative monoid;
- `R` is a commutative semiring;
- `A` is a commutative ring and an `R`-algebra;
- `ğ’œ : Î¹ â†’ Submodule R A` is the grading of `A`;
- `x : Submonoid A` is a submonoid

## Main definitions and results

This file constructs the subring of `Aâ‚“` where the numerator and denominator have the same grading,
i.e. `{a/b âˆˆ Aâ‚“ | âˆƒ (i : Î¹), a âˆˆ ğ’œáµ¢ âˆ§ b âˆˆ ğ’œáµ¢}`.

* `HomogeneousLocalization.NumDenSameDeg`: a structure with a numerator and denominator field
  where they are required to have the same grading.

However `NumDenSameDeg ğ’œ x` cannot have a ring structure for many reasons, for example if `c`
is a `NumDenSameDeg`, then generally, `c + (-c)` is not necessarily `0` for degree reasons ---
`0` is considered to have grade zero (see `deg_zero`) but `c + (-c)` has the same degree as `c`. To
circumvent this, we quotient `NumDenSameDeg ğ’œ x` by the kernel of `c â†¦ c.num / c.den`.

* `HomogeneousLocalization.NumDenSameDeg.embedding`: for `x : Submonoid A` and any
  `c : NumDenSameDeg ğ’œ x`, or equivalent a numerator and a denominator of the same degree,
  we get an element `c.num / c.den` of `Aâ‚“`.
* `HomogeneousLocalization`: `NumDenSameDeg ğ’œ x` quotiented by kernel of `embedding ğ’œ x`.
* `HomogeneousLocalization.val`: if `f : HomogeneousLocalization ğ’œ x`, then `f.val` is an element
  of `Aâ‚“`. In another word, one can view `HomogeneousLocalization ğ’œ x` as a subring of `Aâ‚“`
  through `HomogeneousLocalization.val`.
* `HomogeneousLocalization.num`: if `f : HomogeneousLocalization ğ’œ x`, then `f.num : A` is the
  numerator of `f`.
* `HomogeneousLocalization.den`: if `f : HomogeneousLocalization ğ’œ x`, then `f.den : A` is the
  denominator of `f`.
* `HomogeneousLocalization.deg`: if `f : HomogeneousLocalization ğ’œ x`, then `f.deg : Î¹` is the
  degree of `f` such that `f.num âˆˆ ğ’œ f.deg` and `f.den âˆˆ ğ’œ f.deg`
  (see `HomogeneousLocalization.num_mem_deg` and `HomogeneousLocalization.den_mem_deg`).
* `HomogeneousLocalization.num_mem_deg`: if `f : HomogeneousLocalization ğ’œ x`, then
  `f.num_mem_deg` is a proof that `f.num âˆˆ ğ’œ f.deg`.
* `HomogeneousLocalization.den_mem_deg`: if `f : HomogeneousLocalization ğ’œ x`, then
  `f.den_mem_deg` is a proof that `f.den âˆˆ ğ’œ f.deg`.
* `HomogeneousLocalization.eq_num_div_den`: if `f : HomogeneousLocalization ğ’œ x`, then
  `f.val : Aâ‚“` is equal to `f.num / f.den`.

* `HomogeneousLocalization.localRing`: `HomogeneousLocalization ğ’œ x` is a local ring when `x` is
  the complement of some prime ideals.

## References

* [Robin Hartshorne, *Algebraic Geometry*][Har77]


-/


noncomputable section

open DirectSum BigOperators Pointwise

open DirectSum SetLike

variable {Î¹ R A : Type*}
variable [AddCommMonoid Î¹] [DecidableEq Î¹]
variable [CommRing R] [CommRing A] [Algebra R A]
variable (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ]
variable (x : Submonoid A)

local notation "at " x => Localization x

namespace HomogeneousLocalization

section

/-- Let `x` be a submonoid of `A`, then `NumDenSameDeg ğ’œ x` is a structure with a numerator and a
denominator with same grading such that the denominator is contained in `x`.
-/
-- @[nolint has_nonempty_instance] -- Porting note: This linter does not exist yet.
structure NumDenSameDeg where
  deg : Î¹
  (num den : ğ’œ deg)
  den_mem : (den : A) âˆˆ x
#align homogeneous_localization.num_denom_same_deg HomogeneousLocalization.NumDenSameDeg

end

namespace NumDenSameDeg

open SetLike.GradedMonoid Submodule

variable {ğ’œ}

@[ext]
theorem ext {c1 c2 : NumDenSameDeg ğ’œ x} (hdeg : c1.deg = c2.deg) (hnum : (c1.num : A) = c2.num)
    (hden : (c1.den : A) = c2.den) : c1 = c2 := by
  rcases c1 with âŸ¨i1, âŸ¨n1, hn1âŸ©, âŸ¨d1, hd1âŸ©, h1âŸ©
  rcases c2 with âŸ¨i2, âŸ¨n2, hn2âŸ©, âŸ¨d2, hd2âŸ©, h2âŸ©
  dsimp only [Subtype.coe_mk] at *
  subst hdeg hnum hden
  congr
#align homogeneous_localization.num_denom_same_deg.ext HomogeneousLocalization.NumDenSameDeg.ext

instance : One (NumDenSameDeg ğ’œ x) where
  one :=
    { deg := 0
      -- Porting note: Changed `one_mem` to `GradedOne.one_mem`
      num := âŸ¨1, GradedOne.one_memâŸ©
      den := âŸ¨1, GradedOne.one_memâŸ©
      den_mem := Submonoid.one_mem _ }

@[simp]
theorem deg_one : (1 : NumDenSameDeg ğ’œ x).deg = 0 :=
  rfl
#align homogeneous_localization.num_denom_same_deg.deg_one HomogeneousLocalization.NumDenSameDeg.deg_one

@[simp]
theorem num_one : ((1 : NumDenSameDeg ğ’œ x).num : A) = 1 :=
  rfl
#align homogeneous_localization.num_denom_same_deg.num_one HomogeneousLocalization.NumDenSameDeg.num_one

@[simp]
theorem den_one : ((1 : NumDenSameDeg ğ’œ x).den : A) = 1 :=
  rfl
#align homogeneous_localization.num_denom_same_deg.denom_one HomogeneousLocalization.NumDenSameDeg.den_one

instance : Zero (NumDenSameDeg ğ’œ x) where
  zero := âŸ¨0, 0, âŸ¨1, GradedOne.one_memâŸ©, Submonoid.one_mem _âŸ©

@[simp]
theorem deg_zero : (0 : NumDenSameDeg ğ’œ x).deg = 0 :=
  rfl
#align homogeneous_localization.num_denom_same_deg.deg_zero HomogeneousLocalization.NumDenSameDeg.deg_zero

@[simp]
theorem num_zero : (0 : NumDenSameDeg ğ’œ x).num = 0 :=
  rfl
#align homogeneous_localization.num_denom_same_deg.num_zero HomogeneousLocalization.NumDenSameDeg.num_zero

@[simp]
theorem den_zero : ((0 : NumDenSameDeg ğ’œ x).den : A) = 1 :=
  rfl
#align homogeneous_localization.num_denom_same_deg.denom_zero HomogeneousLocalization.NumDenSameDeg.den_zero

instance : Mul (NumDenSameDeg ğ’œ x) where
  mul p q :=
    { deg := p.deg + q.deg
      -- Porting note: Changed `mul_mem` to `GradedMul.mul_mem`
      num := âŸ¨p.num * q.num, GradedMul.mul_mem p.num.prop q.num.propâŸ©
      den := âŸ¨p.den * q.den, GradedMul.mul_mem p.den.prop q.den.propâŸ©
      den_mem := Submonoid.mul_mem _ p.den_mem q.den_mem }

@[simp]
theorem deg_mul (c1 c2 : NumDenSameDeg ğ’œ x) : (c1 * c2).deg = c1.deg + c2.deg :=
  rfl
#align homogeneous_localization.num_denom_same_deg.deg_mul HomogeneousLocalization.NumDenSameDeg.deg_mul

@[simp]
theorem num_mul (c1 c2 : NumDenSameDeg ğ’œ x) : ((c1 * c2).num : A) = c1.num * c2.num :=
  rfl
#align homogeneous_localization.num_denom_same_deg.num_mul HomogeneousLocalization.NumDenSameDeg.num_mul

@[simp]
theorem den_mul (c1 c2 : NumDenSameDeg ğ’œ x) : ((c1 * c2).den : A) = c1.den * c2.den :=
  rfl
#align homogeneous_localization.num_denom_same_deg.denom_mul HomogeneousLocalization.NumDenSameDeg.den_mul

instance : Add (NumDenSameDeg ğ’œ x) where
  add c1 c2 :=
    { deg := c1.deg + c2.deg
      num := âŸ¨c1.den * c2.num + c2.den * c1.num,
        add_mem (GradedMul.mul_mem c1.den.2 c2.num.2)
          (add_comm c2.deg c1.deg â–¸ GradedMul.mul_mem c2.den.2 c1.num.2)âŸ©
      den := âŸ¨c1.den * c2.den, GradedMul.mul_mem c1.den.2 c2.den.2âŸ©
      den_mem := Submonoid.mul_mem _ c1.den_mem c2.den_mem }

@[simp]
theorem deg_add (c1 c2 : NumDenSameDeg ğ’œ x) : (c1 + c2).deg = c1.deg + c2.deg :=
  rfl
#align homogeneous_localization.num_denom_same_deg.deg_add HomogeneousLocalization.NumDenSameDeg.deg_add

@[simp]
theorem num_add (c1 c2 : NumDenSameDeg ğ’œ x) :
    ((c1 + c2).num : A) = c1.den * c2.num + c2.den * c1.num :=
  rfl
#align homogeneous_localization.num_denom_same_deg.num_add HomogeneousLocalization.NumDenSameDeg.num_add

@[simp]
theorem den_add (c1 c2 : NumDenSameDeg ğ’œ x) : ((c1 + c2).den : A) = c1.den * c2.den :=
  rfl
#align homogeneous_localization.num_denom_same_deg.denom_add HomogeneousLocalization.NumDenSameDeg.den_add

instance : Neg (NumDenSameDeg ğ’œ x) where
  neg c := âŸ¨c.deg, âŸ¨-c.num, neg_mem c.num.2âŸ©, c.den, c.den_memâŸ©

@[simp]
theorem deg_neg (c : NumDenSameDeg ğ’œ x) : (-c).deg = c.deg :=
  rfl
#align homogeneous_localization.num_denom_same_deg.deg_neg HomogeneousLocalization.NumDenSameDeg.deg_neg

@[simp]
theorem num_neg (c : NumDenSameDeg ğ’œ x) : ((-c).num : A) = -c.num :=
  rfl
#align homogeneous_localization.num_denom_same_deg.num_neg HomogeneousLocalization.NumDenSameDeg.num_neg

@[simp]
theorem den_neg (c : NumDenSameDeg ğ’œ x) : ((-c).den : A) = c.den :=
  rfl
#align homogeneous_localization.num_denom_same_deg.denom_neg HomogeneousLocalization.NumDenSameDeg.den_neg

instance : CommMonoid (NumDenSameDeg ğ’œ x) where
  one := 1
  mul := (Â· * Â·)
  mul_assoc c1 c2 c3 := ext _ (add_assoc _ _ _) (mul_assoc _ _ _) (mul_assoc _ _ _)
  one_mul c := ext _ (zero_add _) (one_mul _) (one_mul _)
  mul_one c := ext _ (add_zero _) (mul_one _) (mul_one _)
  mul_comm c1 c2 := ext _ (add_comm _ _) (mul_comm _ _) (mul_comm _ _)

instance : Pow (NumDenSameDeg ğ’œ x) â„• where
  pow c n :=
    âŸ¨n â€¢ c.deg, @GradedMonoid.GMonoid.gnpow _ (fun i => â†¥(ğ’œ i)) _ _ n _ c.num,
      @GradedMonoid.GMonoid.gnpow _ (fun i => â†¥(ğ’œ i)) _ _ n _ c.den, by
        induction' n with n ih
        Â· simpa only [Nat.zero_eq, coe_gnpow, pow_zero] using Submonoid.one_mem _
        Â· simpa only [pow_succ, coe_gnpow] using x.mul_mem ih c.den_memâŸ©

@[simp]
theorem deg_pow (c : NumDenSameDeg ğ’œ x) (n : â„•) : (c ^ n).deg = n â€¢ c.deg :=
  rfl
#align homogeneous_localization.num_denom_same_deg.deg_pow HomogeneousLocalization.NumDenSameDeg.deg_pow

@[simp]
theorem num_pow (c : NumDenSameDeg ğ’œ x) (n : â„•) : ((c ^ n).num : A) = (c.num : A) ^ n :=
  rfl
#align homogeneous_localization.num_denom_same_deg.num_pow HomogeneousLocalization.NumDenSameDeg.num_pow

@[simp]
theorem den_pow (c : NumDenSameDeg ğ’œ x) (n : â„•) : ((c ^ n).den : A) = (c.den : A) ^ n :=
  rfl
#align homogeneous_localization.num_denom_same_deg.denom_pow HomogeneousLocalization.NumDenSameDeg.den_pow

section SMul

variable {Î± : Type*} [SMul Î± R] [SMul Î± A] [IsScalarTower Î± R A]

instance : SMul Î± (NumDenSameDeg ğ’œ x) where
  smul m c := âŸ¨c.deg, m â€¢ c.num, c.den, c.den_memâŸ©

@[simp]
theorem deg_smul (c : NumDenSameDeg ğ’œ x) (m : Î±) : (m â€¢ c).deg = c.deg :=
  rfl
#align homogeneous_localization.num_denom_same_deg.deg_smul HomogeneousLocalization.NumDenSameDeg.deg_smul

@[simp]
theorem num_smul (c : NumDenSameDeg ğ’œ x) (m : Î±) : ((m â€¢ c).num : A) = m â€¢ c.num :=
  rfl
#align homogeneous_localization.num_denom_same_deg.num_smul HomogeneousLocalization.NumDenSameDeg.num_smul

@[simp]
theorem den_smul (c : NumDenSameDeg ğ’œ x) (m : Î±) : ((m â€¢ c).den : A) = c.den :=
  rfl
#align homogeneous_localization.num_denom_same_deg.denom_smul HomogeneousLocalization.NumDenSameDeg.den_smul

end SMul

variable (ğ’œ)

/-- For `x : prime ideal of A` and any `p : NumDenSameDeg ğ’œ x`, or equivalent a numerator and a
denominator of the same degree, we get an element `p.num / p.den` of `Aâ‚“`.
-/
def embedding (p : NumDenSameDeg ğ’œ x) : at x :=
  Localization.mk p.num âŸ¨p.den, p.den_memâŸ©
#align homogeneous_localization.num_denom_same_deg.embedding HomogeneousLocalization.NumDenSameDeg.embedding

end NumDenSameDeg

end HomogeneousLocalization

/-- For `x : prime ideal of A`, `HomogeneousLocalization ğ’œ x` is `NumDenSameDeg ğ’œ x` modulo the
kernel of `embedding ğ’œ x`. This is essentially the subring of `Aâ‚“` where the numerator and
denominator share the same grading.
-/
-- @[nolint has_nonempty_instance] -- Porting note: This linter does not exist yet.
def HomogeneousLocalization : Type _ :=
  Quotient (Setoid.ker <| HomogeneousLocalization.NumDenSameDeg.embedding ğ’œ x)
#align homogeneous_localization HomogeneousLocalization

namespace HomogeneousLocalization

open HomogeneousLocalization HomogeneousLocalization.NumDenSameDeg

variable {ğ’œ} {x}

/-- View an element of `HomogeneousLocalization ğ’œ x` as an element of `Aâ‚“` by forgetting that the
numerator and denominator are of the same grading.
-/
def val (y : HomogeneousLocalization ğ’œ x) : at x :=
  Quotient.liftOn' y (NumDenSameDeg.embedding ğ’œ x) fun _ _ => id
#align homogeneous_localization.val HomogeneousLocalization.val

@[simp]
theorem val_mk'' (i : NumDenSameDeg ğ’œ x) :
    val (Quotient.mk'' i) = Localization.mk (i.num : A) âŸ¨i.den, i.den_memâŸ© :=
  rfl
#align homogeneous_localization.val_mk' HomogeneousLocalization.val_mk''

variable (x)

theorem val_injective : Function.Injective (HomogeneousLocalization.val (ğ’œ := ğ’œ) (x := x)) :=
  fun a b => Quotient.recOnSubsingletonâ‚‚' a b fun _ _ h => Quotient.sound' h
#align homogeneous_localization.val_injective HomogeneousLocalization.val_injective

instance hasPow : Pow (HomogeneousLocalization ğ’œ x) â„• where
  pow z n :=
    (Quotient.map' (Â· ^ n) fun c1 c2 (h : Localization.mk _ _ = Localization.mk _ _) => by
          change Localization.mk _ _ = Localization.mk _ _
          simp only [num_pow, den_pow]
          convert congr_arg (fun z : at x => z ^ n) h <;> erw [Localization.mk_pow] <;> rfl :
        HomogeneousLocalization ğ’œ x â†’ HomogeneousLocalization ğ’œ x)
      z
#align homogeneous_localization.has_pow HomogeneousLocalization.hasPow

section SMul

variable {Î± : Type*} [SMul Î± R] [SMul Î± A] [IsScalarTower Î± R A]
variable [IsScalarTower Î± A A]

instance : SMul Î± (HomogeneousLocalization ğ’œ x) where
  smul m := Quotient.map' (m â€¢ Â·) fun c1 c2 (h : Localization.mk _ _ = Localization.mk _ _) => by
    change Localization.mk _ _ = Localization.mk _ _
    simp only [num_smul, den_smul]
    convert congr_arg (fun z : at x => m â€¢ z) h <;> rw [Localization.smul_mk]

@[simp]
theorem smul_val (n : Î±) (y : HomogeneousLocalization ğ’œ x) : (n â€¢ y).val = n â€¢ y.val := by
  induction y using Quotient.inductionOn
  change Localization.mk _ _ = n â€¢ Localization.mk _ _
  dsimp only
  rw [Localization.smul_mk]
  congr 1
#align homogeneous_localization.smul_val HomogeneousLocalization.smul_val

end SMul

instance : Neg (HomogeneousLocalization ğ’œ x) where
  neg := Quotient.map' Neg.neg fun c1 c2 (h : Localization.mk _ _ = Localization.mk _ _) => by
    change Localization.mk _ _ = Localization.mk _ _
    simp only [num_neg, den_neg, â† Localization.neg_mk]
    exact congr_arg Neg.neg h

instance : Add (HomogeneousLocalization ğ’œ x) where
  add :=
    Quotient.mapâ‚‚' (Â· + Â·)
      fun c1 c2 (h : Localization.mk _ _ = Localization.mk _ _) c3 c4
        (h' : Localization.mk _ _ = Localization.mk _ _) => by
      change Localization.mk _ _ = Localization.mk _ _
      simp only [num_add, den_add, â† Localization.add_mk]
      convert congr_argâ‚‚ (Â· + Â·) h h' <;> erw [Localization.add_mk] <;> rfl

instance : Sub (HomogeneousLocalization ğ’œ x) where sub z1 z2 := z1 + -z2

instance : Mul (HomogeneousLocalization ğ’œ x) where
  mul :=
    Quotient.mapâ‚‚' (Â· * Â·)
      fun c1 c2 (h : Localization.mk _ _ = Localization.mk _ _) c3 c4
        (h' : Localization.mk _ _ = Localization.mk _ _) => by
      change Localization.mk _ _ = Localization.mk _ _
      simp only [num_mul, den_mul]
      convert congr_argâ‚‚ (Â· * Â·) h h' <;> erw [Localization.mk_mul] <;> rfl

instance : One (HomogeneousLocalization ğ’œ x) where one := Quotient.mk'' 1

instance : Zero (HomogeneousLocalization ğ’œ x) where zero := Quotient.mk'' 0

theorem zero_eq : (0 : HomogeneousLocalization ğ’œ x) = Quotient.mk'' 0 :=
  rfl
#align homogeneous_localization.zero_eq HomogeneousLocalization.zero_eq

theorem one_eq : (1 : HomogeneousLocalization ğ’œ x) = Quotient.mk'' 1 :=
  rfl
#align homogeneous_localization.one_eq HomogeneousLocalization.one_eq

variable {x}

theorem zero_val : (0 : HomogeneousLocalization ğ’œ x).val = 0 :=
  Localization.mk_zero _
#align homogeneous_localization.zero_val HomogeneousLocalization.zero_val

theorem one_val : (1 : HomogeneousLocalization ğ’œ x).val = 1 :=
  Localization.mk_one
#align homogeneous_localization.one_val HomogeneousLocalization.one_val

@[simp]
theorem add_val (y1 y2 : HomogeneousLocalization ğ’œ x) : (y1 + y2).val = y1.val + y2.val := by
  induction y1 using Quotient.inductionOn
  induction y2 using Quotient.inductionOn
  change Localization.mk _ _ = Localization.mk _ _ + Localization.mk _ _
  dsimp only
  rw [Localization.add_mk]
  rfl
#align homogeneous_localization.add_val HomogeneousLocalization.add_val

@[simp]
theorem mul_val (y1 y2 : HomogeneousLocalization ğ’œ x) : (y1 * y2).val = y1.val * y2.val := by
  induction y1 using Quotient.inductionOn
  induction y2 using Quotient.inductionOn
  change Localization.mk _ _ = Localization.mk _ _ * Localization.mk _ _
  dsimp only
  rw [Localization.mk_mul]
  rfl
#align homogeneous_localization.mul_val HomogeneousLocalization.mul_val

@[simp]
theorem neg_val (y : HomogeneousLocalization ğ’œ x) : (-y).val = -y.val := by
  induction y using Quotient.inductionOn
  change Localization.mk _ _ = -Localization.mk _ _
  dsimp only
  rw [Localization.neg_mk]
  rfl
#align homogeneous_localization.neg_val HomogeneousLocalization.neg_val

@[simp]
theorem sub_val (y1 y2 : HomogeneousLocalization ğ’œ x) : (y1 - y2).val = y1.val - y2.val := by
  rw [show y1 - y2 = y1 + -y2 from rfl, add_val, neg_val]; rfl
#align homogeneous_localization.sub_val HomogeneousLocalization.sub_val

@[simp]
theorem pow_val (y : HomogeneousLocalization ğ’œ x) (n : â„•) : (y ^ n).val = y.val ^ n := by
  induction y using Quotient.inductionOn
  change Localization.mk _ _ = Localization.mk _ _ ^ n
  rw [Localization.mk_pow]
  dsimp only
  congr 1
#align homogeneous_localization.pow_val HomogeneousLocalization.pow_val

instance : NatCast (HomogeneousLocalization ğ’œ x) :=
  âŸ¨Nat.unaryCastâŸ©

instance : IntCast (HomogeneousLocalization ğ’œ x) :=
  âŸ¨Int.castDefâŸ©

@[simp]
theorem natCast_val (n : â„•) : (n : HomogeneousLocalization ğ’œ x).val = n :=
  show val (Nat.unaryCast n) = _ by induction n <;> simp [Nat.unaryCast, zero_val, one_val, *]
#align homogeneous_localization.nat_cast_val HomogeneousLocalization.natCast_val

@[simp]
theorem intCast_val (n : â„¤) : (n : HomogeneousLocalization ğ’œ x).val = n :=
  show val (Int.castDef n) = _ by cases n <;> simp [Int.castDef, zero_val, one_val, *]
#align homogeneous_localization.int_cast_val HomogeneousLocalization.intCast_val

instance homogenousLocalizationCommRing : CommRing (HomogeneousLocalization ğ’œ x) :=
  (HomogeneousLocalization.val_injective x).commRing _ zero_val one_val add_val mul_val neg_val
    sub_val (smul_val x Â· Â·) (smul_val x Â· Â·) pow_val natCast_val intCast_val
#align homogeneous_localization.homogenous_localization_comm_ring HomogeneousLocalization.homogenousLocalizationCommRing

instance homogeneousLocalizationAlgebra :
    Algebra (HomogeneousLocalization ğ’œ x) (Localization x) where
  smul p q := p.val * q
  toFun := val
  map_one' := one_val
  map_mul' := mul_val
  map_zero' := zero_val
  map_add' := add_val
  commutes' _ _ := mul_comm _ _
  smul_def' _ _ := rfl
#align homogeneous_localization.homogeneous_localization_algebra HomogeneousLocalization.homogeneousLocalizationAlgebra

end HomogeneousLocalization

namespace HomogeneousLocalization

open HomogeneousLocalization HomogeneousLocalization.NumDenSameDeg

variable {ğ’œ} {x}

/-- Numerator of an element in `HomogeneousLocalization x`. -/
def num (f : HomogeneousLocalization ğ’œ x) : A :=
  (Quotient.out' f).num
#align homogeneous_localization.num HomogeneousLocalization.num

/-- Denominator of an element in `HomogeneousLocalization x`. -/
def den (f : HomogeneousLocalization ğ’œ x) : A :=
  (Quotient.out' f).den
#align homogeneous_localization.denom HomogeneousLocalization.den

/-- For an element in `HomogeneousLocalization x`, degree is the natural number `i` such that
  `ğ’œ i` contains both numerator and denominator. -/
def deg (f : HomogeneousLocalization ğ’œ x) : Î¹ :=
  (Quotient.out' f).deg
#align homogeneous_localization.deg HomogeneousLocalization.deg

theorem den_mem (f : HomogeneousLocalization ğ’œ x) : f.den âˆˆ x :=
  (Quotient.out' f).den_mem
#align homogeneous_localization.denom_mem HomogeneousLocalization.den_mem

theorem num_mem_deg (f : HomogeneousLocalization ğ’œ x) : f.num âˆˆ ğ’œ f.deg :=
  (Quotient.out' f).num.2
#align homogeneous_localization.num_mem_deg HomogeneousLocalization.num_mem_deg

theorem den_mem_deg (f : HomogeneousLocalization ğ’œ x) : f.den âˆˆ ğ’œ f.deg :=
  (Quotient.out' f).den.2
#align homogeneous_localization.denom_mem_deg HomogeneousLocalization.den_mem_deg

theorem eq_num_div_den (f : HomogeneousLocalization ğ’œ x) :
    f.val = Localization.mk f.num âŸ¨f.den, f.den_memâŸ© := by
  have := Quotient.out_eq' f
  apply_fun HomogeneousLocalization.val at this
  rw [â† this]
  rfl
#align homogeneous_localization.eq_num_div_denom HomogeneousLocalization.eq_num_div_den

theorem ext_iff_val (f g : HomogeneousLocalization ğ’œ x) : f = g â†” f.val = g.val :=
  { mp := fun h => h â–¸ rfl
    mpr := fun h => by
      induction f using Quotient.inductionOn'
      induction g using Quotient.inductionOn'
      rw [Quotient.eq'']
      simpa only [Quotient.liftOn'_mk] using h }
#align homogeneous_localization.ext_iff_val HomogeneousLocalization.ext_iff_val

section

variable (ğ’œ) (ğ”­ : Ideal A) [Ideal.IsPrime ğ”­]

/-- Localizing a ring homogeneously at a prime ideal. -/
abbrev AtPrime :=
  HomogeneousLocalization ğ’œ ğ”­.primeCompl
#align homogeneous_localization.at_prime HomogeneousLocalization.AtPrime

theorem isUnit_iff_isUnit_val (f : HomogeneousLocalization.AtPrime ğ’œ ğ”­) : IsUnit f.val â†” IsUnit f :=
  âŸ¨fun h1 => by
    rcases h1 with âŸ¨âŸ¨a, b, eq0, eq1âŸ©, eq2 : a = f.valâŸ©
    rw [eq2] at eq0 eq1
    clear a eq2
    induction' b using Localization.induction_on with data
    rcases data with âŸ¨a, âŸ¨b, hbâŸ©âŸ©
    dsimp only at eq0 eq1
    have b_f_den_not_mem : b * f.den âˆˆ ğ”­.primeCompl :=
      fun r => Or.elim (Ideal.IsPrime.mem_or_mem inferInstance r) (hb Â·) (f.den_mem Â·)
    rw [f.eq_num_div_den, Localization.mk_mul,
      show (âŸ¨b, hbâŸ© : ğ”­.primeCompl) * âŸ¨f.den, _âŸ© = âŸ¨b * f.den, _âŸ© from rfl,
      show (1 : Localization.AtPrime ğ”­) = Localization.mk 1 1 by erw [Localization.mk_self 1],
      Localization.mk_eq_mk', IsLocalization.eq] at eq1
    rcases eq1 with âŸ¨âŸ¨c, hcâŸ©, eq1âŸ©
    change c * (1 * (a * f.num)) = _ at eq1
    simp only [one_mul, mul_one] at eq1
    have mem1 : c * (a * f.num) âˆˆ ğ”­.primeCompl :=
      eq1.symm â–¸ fun r => Or.elim (Ideal.IsPrime.mem_or_mem inferInstance r) (by tauto) (by tauto)
    have mem2 : f.num âˆ‰ ğ”­ := by
      contrapose! mem1
      erw [Classical.not_not]
      exact Ideal.mul_mem_left _ _ (Ideal.mul_mem_left _ _ mem1)
    refine' âŸ¨âŸ¨f, Quotient.mk'' âŸ¨f.deg, âŸ¨f.den, f.den_mem_degâŸ©, âŸ¨f.num, f.num_mem_degâŸ©, mem2âŸ©, _, _âŸ©,
        rflâŸ©
      <;> simp only [ext_iff_val, mul_val, val_mk'', f.eq_num_div_den, Localization.mk_mul, one_val]
      <;> convert Localization.mk_self (M := A) _
      <;> rw [mul_comm]
      <;> rfl ,
    fun âŸ¨âŸ¨_, b, eq1, eq2âŸ©, rflâŸ© => by
    simp only [ext_iff_val, mul_val, one_val] at eq1 eq2
    exact âŸ¨âŸ¨f.val, b.val, eq1, eq2âŸ©, rflâŸ©âŸ©
#align homogeneous_localization.is_unit_iff_is_unit_val HomogeneousLocalization.isUnit_iff_isUnit_val

instance : Nontrivial (HomogeneousLocalization.AtPrime ğ’œ ğ”­) :=
  âŸ¨âŸ¨0, 1, fun r => by simp [ext_iff_val, zero_val, one_val, zero_ne_one] at râŸ©âŸ©

instance localRing : LocalRing (HomogeneousLocalization.AtPrime ğ’œ ğ”­) :=
  LocalRing.of_isUnit_or_isUnit_one_sub_self fun a => by
    simp only [â† isUnit_iff_isUnit_val, sub_val, one_val]
    induction' a using Quotient.inductionOn' with a
    simp only [HomogeneousLocalization.val_mk'']
    by_cases mem1 : a.num.1 âˆˆ ğ”­
    Â· right
      have : a.den.1 - a.num.1 âˆˆ ğ”­.primeCompl := fun h =>
        a.den_mem (sub_add_cancel a.den.val a.num.val â–¸ Ideal.add_mem _ h mem1 : a.den.1 âˆˆ ğ”­)
      apply isUnit_of_mul_eq_one _ (Localization.mk a.den.1 âŸ¨a.den.1 - a.num.1, thisâŸ©)
      simp only [sub_mul, Localization.mk_mul, one_mul, Localization.sub_mk, Submonoid.coe_mul]
      convert Localization.mk_self (M := A) _
      simp only [Submonoid.coe_mul]
      ring
    Â· left
      change _ âˆˆ ğ”­.primeCompl at mem1
      apply isUnit_of_mul_eq_one _ (Localization.mk a.den.1 âŸ¨a.num.1, mem1âŸ©)
      rw [Localization.mk_mul]
      convert Localization.mk_self (M := A) _
      rw [mul_comm]
      rfl

end

section

variable (ğ’œ) (f : A)

/-- Localizing away from powers of `f` homogeneously. -/
abbrev Away :=
  HomogeneousLocalization ğ’œ (Submonoid.powers f)
#align homogeneous_localization.away HomogeneousLocalization.Away

end

end HomogeneousLocalization
