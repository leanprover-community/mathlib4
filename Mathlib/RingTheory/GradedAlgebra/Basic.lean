/-
Copyright (c) 2021 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser, Kevin Buzzard, Jujian Zhang, Fangming Li
-/
import Mathlib.Algebra.DirectSum.Algebra
import Mathlib.Algebra.DirectSum.Decomposition
import Mathlib.Algebra.DirectSum.Internal
import Mathlib.Algebra.DirectSum.Ring

/-!
# Internally-graded rings and algebras

This file defines the typeclass `GradedAlgebra ğ’œ`, for working with an algebra `A` that is
internally graded by a collection of submodules `ğ’œ : Î¹ â†’ Submodule R A`.
See the docstring of that typeclass for more information.

## Main definitions

* `GradedRing ğ’œ`: the typeclass, which is a combination of `SetLike.GradedMonoid`, and
  `DirectSum.Decomposition ğ’œ`.
* `GradedAlgebra ğ’œ`: A convenience alias for `GradedRing` when `ğ’œ` is a family of submodules.
* `DirectSum.decomposeRingEquiv ğ’œ : A â‰ƒâ‚[R] â¨ i, ğ’œ i`, a more bundled version of
  `DirectSum.decompose ğ’œ`.
* `DirectSum.decomposeAlgEquiv ğ’œ : A â‰ƒâ‚[R] â¨ i, ğ’œ i`, a more bundled version of
  `DirectSum.decompose ğ’œ`.
* `GradedAlgebra.proj ğ’œ i` is the linear map from `A` to its degree `i : Î¹` component, such that
  `proj ğ’œ i x = decompose ğ’œ x i`.

## Implementation notes

For now, we do not have internally-graded semirings and internally-graded rings; these can be
represented with `ğ’œ : Î¹ â†’ Submodule â„• A` and `ğ’œ : Î¹ â†’ Submodule â„¤ A` respectively, since all
`Semiring`s are â„•-algebras via `Semiring.toNatAlgebra`, and all `Ring`s are `â„¤`-algebras via
`Ring.toIntAlgebra`.

## Tags

graded algebra, graded ring, graded semiring, decomposition
-/


open DirectSum

variable {Î¹ R A Ïƒ : Type*}

section GradedRing

variable [DecidableEq Î¹] [AddMonoid Î¹] [CommSemiring R] [Semiring A] [Algebra R A]
variable [SetLike Ïƒ A] [AddSubmonoidClass Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ)

open DirectSum

/-- An internally-graded `R`-algebra `A` is one that can be decomposed into a collection
of `Submodule R A`s indexed by `Î¹` such that the canonical map `A â†’ â¨ i, ğ’œ i` is bijective and
respects multiplication, i.e. the product of an element of degree `i` and an element of degree `j`
is an element of degree `i + j`.

Note that the fact that `A` is internally-graded, `GradedAlgebra ğ’œ`, implies an externally-graded
algebra structure `DirectSum.GAlgebra R (fun i â†¦ â†¥(ğ’œ i))`, which in turn makes available an
`Algebra R (â¨ i, ğ’œ i)` instance.
-/
class GradedRing (ğ’œ : Î¹ â†’ Ïƒ) extends SetLike.GradedMonoid ğ’œ, DirectSum.Decomposition ğ’œ

variable [GradedRing ğ’œ]

namespace DirectSum

/-- If `A` is graded by `Î¹` with degree `i` component `ğ’œ i`, then it is isomorphic as
a ring to a direct sum of components. -/
def decomposeRingEquiv : A â‰ƒ+* â¨ i, ğ’œ i :=
  RingEquiv.symm
    { (decomposeAddEquiv ğ’œ).symm with
      map_mul' := (coeRingHom ğ’œ).map_mul }

@[simp]
theorem decompose_one : decompose ğ’œ (1 : A) = 1 :=
  map_one (decomposeRingEquiv ğ’œ)

@[simp]
theorem decompose_symm_one : (decompose ğ’œ).symm 1 = (1 : A) :=
  map_one (decomposeRingEquiv ğ’œ).symm

@[simp]
theorem decompose_mul (x y : A) : decompose ğ’œ (x * y) = decompose ğ’œ x * decompose ğ’œ y :=
  map_mul (decomposeRingEquiv ğ’œ) x y

@[simp]
theorem decompose_symm_mul (x y : â¨ i, ğ’œ i) :
    (decompose ğ’œ).symm (x * y) = (decompose ğ’œ).symm x * (decompose ğ’œ).symm y :=
  map_mul (decomposeRingEquiv ğ’œ).symm x y

end DirectSum

/-- The projection maps of a graded ring -/
def GradedRing.proj (i : Î¹) : A â†’+ A :=
  (AddSubmonoidClass.subtype (ğ’œ i)).comp <|
    (DFinsupp.evalAddMonoidHom i).comp <|
      RingHom.toAddMonoidHom <| RingEquiv.toRingHom <| DirectSum.decomposeRingEquiv ğ’œ

@[simp]
theorem GradedRing.proj_apply (i : Î¹) (r : A) :
    GradedRing.proj ğ’œ i r = (decompose ğ’œ r : â¨ i, ğ’œ i) i :=
  rfl

theorem GradedRing.proj_recompose (a : â¨ i, ğ’œ i) (i : Î¹) :
    GradedRing.proj ğ’œ i ((decompose ğ’œ).symm a) = (decompose ğ’œ).symm (DirectSum.of _ i (a i)) := by
  rw [GradedRing.proj_apply, decompose_symm_of, Equiv.apply_symm_apply]

theorem GradedRing.mem_support_iff [âˆ€ (i) (x : ğ’œ i), Decidable (x â‰  0)] (r : A) (i : Î¹) :
    i âˆˆ (decompose ğ’œ r).support â†” GradedRing.proj ğ’œ i r â‰  0 :=
  DFinsupp.mem_support_iff.trans ZeroMemClass.coe_eq_zero.not.symm

end GradedRing

section AddCancelMonoid

open DirectSum

variable [DecidableEq Î¹] [Semiring A] [SetLike Ïƒ A] [AddSubmonoidClass Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ)
variable {i j : Î¹}

namespace DirectSum

theorem coe_decompose_mul_add_of_left_mem [AddLeftCancelMonoid Î¹] [GradedRing ğ’œ] {a b : A}
    (a_mem : a âˆˆ ğ’œ i) : (decompose ğ’œ (a * b) (i + j) : A) = a * decompose ğ’œ b j := by
  lift a to ğ’œ i using a_mem
  rw [decompose_mul, decompose_coe, coe_of_mul_apply_add]

theorem coe_decompose_mul_add_of_right_mem [AddRightCancelMonoid Î¹] [GradedRing ğ’œ] {a b : A}
    (b_mem : b âˆˆ ğ’œ j) : (decompose ğ’œ (a * b) (i + j) : A) = decompose ğ’œ a i * b := by
  lift b to ğ’œ j using b_mem
  rw [decompose_mul, decompose_coe, coe_mul_of_apply_add]

theorem decompose_mul_add_left [AddLeftCancelMonoid Î¹] [GradedRing ğ’œ] (a : ğ’œ i) {b : A} :
    decompose ğ’œ (â†‘a * b) (i + j) =
      @GradedMonoid.GMul.mul Î¹ (fun i => ğ’œ i) _ _ _ _ a (decompose ğ’œ b j) :=
  Subtype.ext <| coe_decompose_mul_add_of_left_mem ğ’œ a.2

theorem decompose_mul_add_right [AddRightCancelMonoid Î¹] [GradedRing ğ’œ] {a : A} (b : ğ’œ j) :
    decompose ğ’œ (a * â†‘b) (i + j) =
      @GradedMonoid.GMul.mul Î¹ (fun i => ğ’œ i) _ _ _ _ (decompose ğ’œ a i) b :=
  Subtype.ext <| coe_decompose_mul_add_of_right_mem ğ’œ b.2

theorem coe_decompose_mul_of_left_mem_zero [AddMonoid Î¹] [GradedRing ğ’œ] {a b : A}
    (a_mem : a âˆˆ ğ’œ 0) : (decompose ğ’œ (a * b) j : A) = a * decompose ğ’œ b j := by
  lift a to ğ’œ 0 using a_mem
  rw [decompose_mul, decompose_coe, coe_of_mul_apply_of_mem_zero]

theorem coe_decompose_mul_of_right_mem_zero [AddMonoid Î¹] [GradedRing ğ’œ] {a b : A}
    (b_mem : b âˆˆ ğ’œ 0) : (decompose ğ’œ (a * b) i : A) = decompose ğ’œ a i * b := by
  lift b to ğ’œ 0 using b_mem
  rw [decompose_mul, decompose_coe, coe_mul_of_apply_of_mem_zero]

end DirectSum

end AddCancelMonoid

section GradedAlgebra

variable [DecidableEq Î¹] [AddMonoid Î¹] [CommSemiring R] [Semiring A] [Algebra R A]
variable (ğ’œ : Î¹ â†’ Submodule R A)

/-- A special case of `GradedRing` with `Ïƒ = Submodule R A`. This is useful both because it
can avoid typeclass search, and because it provides a more concise name. -/
abbrev GradedAlgebra :=
  GradedRing ğ’œ

/-- A helper to construct a `GradedAlgebra` when the `SetLike.GradedMonoid` structure is already
available. This makes the `left_inv` condition easier to prove, and phrases the `right_inv`
condition in a way that allows custom `@[ext]` lemmas to apply.

See note [reducible non-instances]. -/
abbrev GradedAlgebra.ofAlgHom [SetLike.GradedMonoid ğ’œ] (decompose : A â†’â‚[R] â¨ i, ğ’œ i)
    (right_inv : (DirectSum.coeAlgHom ğ’œ).comp decompose = AlgHom.id R A)
    (left_inv : âˆ€ i (x : ğ’œ i), decompose (x : A) = DirectSum.of (fun i => â†¥(ğ’œ i)) i x) :
    GradedAlgebra ğ’œ where
  decompose' := decompose
  left_inv := AlgHom.congr_fun right_inv
  right_inv := by
    suffices decompose.comp (DirectSum.coeAlgHom ğ’œ) = AlgHom.id _ _ from AlgHom.congr_fun this
    ext i x : 2
    exact (decompose.congr_arg <| DirectSum.coeAlgHom_of _ _ _).trans (left_inv i x)

variable [GradedAlgebra ğ’œ]

namespace DirectSum

/-- If `A` is graded by `Î¹` with degree `i` component `ğ’œ i`, then it is isomorphic as
an algebra to a direct sum of components. -/
-- Porting note: deleted [simps] and added the corresponding lemmas by hand
def decomposeAlgEquiv : A â‰ƒâ‚[R] â¨ i, ğ’œ i :=
  AlgEquiv.symm
    { (decomposeAddEquiv ğ’œ).symm with
      map_mul' := map_mul (coeAlgHom ğ’œ)
      commutes' := (coeAlgHom ğ’œ).commutes }

@[simp]
lemma decomposeAlgEquiv_apply (a : A) :
    decomposeAlgEquiv ğ’œ a = decompose ğ’œ a := rfl

@[simp]
lemma decomposeAlgEquiv_symm_apply (a : â¨ i, ğ’œ i) :
    (decomposeAlgEquiv ğ’œ).symm a = (decompose ğ’œ).symm a := rfl

@[simp]
lemma decompose_algebraMap (r : R) :
    decompose ğ’œ (algebraMap R A r) = algebraMap R (â¨ i, ğ’œ i) r :=
  (decomposeAlgEquiv ğ’œ).commutes r

@[simp]
lemma decompose_symm_algebraMap (r : R) :
    (decompose ğ’œ).symm (algebraMap R (â¨ i, ğ’œ i) r) = algebraMap R A r :=
  (decomposeAlgEquiv ğ’œ).symm.commutes r

end DirectSum

open DirectSum

/-- The projection maps of graded algebra -/
def GradedAlgebra.proj (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] (i : Î¹) : A â†’â‚—[R] A :=
  (ğ’œ i).subtype.comp <| (DFinsupp.lapply i).comp <| (decomposeAlgEquiv ğ’œ).toAlgHom.toLinearMap

@[simp]
theorem GradedAlgebra.proj_apply (i : Î¹) (r : A) :
    GradedAlgebra.proj ğ’œ i r = (decompose ğ’œ r : â¨ i, ğ’œ i) i :=
  rfl

theorem GradedAlgebra.proj_recompose (a : â¨ i, ğ’œ i) (i : Î¹) :
    GradedAlgebra.proj ğ’œ i ((decompose ğ’œ).symm a) = (decompose ğ’œ).symm (of _ i (a i)) := by
  rw [GradedAlgebra.proj_apply, decompose_symm_of, Equiv.apply_symm_apply]

theorem GradedAlgebra.mem_support_iff [DecidableEq A] (r : A) (i : Î¹) :
    i âˆˆ (decompose ğ’œ r).support â†” GradedAlgebra.proj ğ’œ i r â‰  0 :=
  DFinsupp.mem_support_iff.trans Submodule.coe_eq_zero.not.symm

end GradedAlgebra

section CanonicalOrder

open SetLike.GradedMonoid DirectSum

variable [Semiring A] [DecidableEq Î¹]
variable [AddCommMonoid Î¹] [PartialOrder Î¹] [CanonicallyOrderedAdd Î¹]
variable [SetLike Ïƒ A] [AddSubmonoidClass Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ) [GradedRing ğ’œ]

/-- If `A` is graded by a canonically ordered add monoid, then the projection map `x â†¦ xâ‚€` is a ring
homomorphism.
-/
@[simps]
def GradedRing.projZeroRingHom : A â†’+* A where
  toFun a := decompose ğ’œ a 0
  map_one' :=
    -- Porting note: qualified `one_mem`
    decompose_of_mem_same ğ’œ SetLike.GradedOne.one_mem
  map_zero' := by
    simp only -- Porting note: added
    rw [decompose_zero]
    rfl
  map_add' _ _ := by
    simp only -- Porting note: added
    rw [decompose_add]
    rfl
  map_mul' := by
    refine DirectSum.Decomposition.inductionOn ğ’œ (fun x => ?_) ?_ ?_
    Â· simp only [zero_mul, decompose_zero, zero_apply, ZeroMemClass.coe_zero]
    Â· rintro i âŸ¨c, hcâŸ©
      refine DirectSum.Decomposition.inductionOn ğ’œ ?_ ?_ ?_
      Â· simp only [mul_zero, decompose_zero, zero_apply, ZeroMemClass.coe_zero]
      Â· rintro j âŸ¨c', hc'âŸ©
        simp only [Subtype.coe_mk]
        by_cases h : i + j = 0
        Â· rw [decompose_of_mem_same ğ’œ
              (show c * c' âˆˆ ğ’œ 0 from h â–¸ SetLike.GradedMul.mul_mem hc hc'),
            decompose_of_mem_same ğ’œ (show c âˆˆ ğ’œ 0 from (add_eq_zero.mp h).1 â–¸ hc),
            decompose_of_mem_same ğ’œ (show c' âˆˆ ğ’œ 0 from (add_eq_zero.mp h).2 â–¸ hc')]
        Â· rw [decompose_of_mem_ne ğ’œ (SetLike.GradedMul.mul_mem hc hc') h]
          rcases show i â‰  0 âˆ¨ j â‰  0 by rwa [add_eq_zero, not_and_or] at h with h' | h'
          Â· simp only [decompose_of_mem_ne ğ’œ hc h', zero_mul]
          Â· simp only [decompose_of_mem_ne ğ’œ hc' h', mul_zero]
      Â· intro _ _ hd he
        simp only at hd he -- Porting note: added
        simp only [mul_add, decompose_add, add_apply, AddMemClass.coe_add, hd, he]
    Â· rintro _ _ ha hb _
      simp only at ha hb -- Porting note: added
      simp only [add_mul, decompose_add, add_apply, AddMemClass.coe_add, ha, hb]

section GradeZero

/-- The ring homomorphism from `A` to `ğ’œ 0` sending every `a : A` to `aâ‚€`. -/
def GradedRing.projZeroRingHom' : A â†’+* ğ’œ 0 :=
  ((GradedRing.projZeroRingHom ğ’œ).codRestrict _ fun _x => SetLike.coe_mem _ :
  A â†’+* SetLike.GradeZero.subsemiring ğ’œ)

@[simp] lemma GradedRing.coe_projZeroRingHom'_apply (a : A) :
    (GradedRing.projZeroRingHom' ğ’œ a : A) = GradedRing.projZeroRingHom ğ’œ a := rfl

@[simp] lemma GradedRing.projZeroRingHom'_apply_coe (a : ğ’œ 0) :
    GradedRing.projZeroRingHom' ğ’œ a = a := by
  ext; simp only [coe_projZeroRingHom'_apply, projZeroRingHom_apply, decompose_coe, of_eq_same]

/-- The ring homomorphism `GradedRing.projZeroRingHom' ğ’œ` is surjective. -/
lemma GradedRing.projZeroRingHom'_surjective :
    Function.Surjective (GradedRing.projZeroRingHom' ğ’œ) :=
  Function.RightInverse.surjective (GradedRing.projZeroRingHom'_apply_coe ğ’œ)

end GradeZero

variable {a b : A} {n i : Î¹}

namespace DirectSum

theorem coe_decompose_mul_of_left_mem_of_not_le (a_mem : a âˆˆ ğ’œ i) (h : Â¬i â‰¤ n) :
    (decompose ğ’œ (a * b) n : A) = 0 := by
  lift a to ğ’œ i using a_mem
  rwa [decompose_mul, decompose_coe, coe_of_mul_apply_of_not_le]

theorem coe_decompose_mul_of_right_mem_of_not_le (b_mem : b âˆˆ ğ’œ i) (h : Â¬i â‰¤ n) :
    (decompose ğ’œ (a * b) n : A) = 0 := by
  lift b to ğ’œ i using b_mem
  rwa [decompose_mul, decompose_coe, coe_mul_of_apply_of_not_le]

variable [Sub Î¹] [OrderedSub Î¹] [AddLeftReflectLE Î¹]

theorem coe_decompose_mul_of_left_mem_of_le (a_mem : a âˆˆ ğ’œ i) (h : i â‰¤ n) :
    (decompose ğ’œ (a * b) n : A) = a * decompose ğ’œ b (n - i) := by
  lift a to ğ’œ i using a_mem
  rwa [decompose_mul, decompose_coe, coe_of_mul_apply_of_le]

theorem coe_decompose_mul_of_right_mem_of_le (b_mem : b âˆˆ ğ’œ i) (h : i â‰¤ n) :
    (decompose ğ’œ (a * b) n : A) = decompose ğ’œ a (n - i) * b := by
  lift b to ğ’œ i using b_mem
  rwa [decompose_mul, decompose_coe, coe_mul_of_apply_of_le]

theorem coe_decompose_mul_of_left_mem (n) [Decidable (i â‰¤ n)] (a_mem : a âˆˆ ğ’œ i) :
    (decompose ğ’œ (a * b) n : A) = if i â‰¤ n then a * decompose ğ’œ b (n - i) else 0 := by
  lift a to ğ’œ i using a_mem
  rw [decompose_mul, decompose_coe, coe_of_mul_apply]

theorem coe_decompose_mul_of_right_mem (n) [Decidable (i â‰¤ n)] (b_mem : b âˆˆ ğ’œ i) :
    (decompose ğ’œ (a * b) n : A) = if i â‰¤ n then decompose ğ’œ a (n - i) * b else 0 := by
  lift b to ğ’œ i using b_mem
  rw [decompose_mul, decompose_coe, coe_mul_of_apply]

end DirectSum

end CanonicalOrder

namespace DirectSum.IsInternal

variable {R : Type*} [CommSemiring R] {A : Type*} [Semiring A] [Algebra R A]
variable {Î¹ : Type*} [DecidableEq Î¹] [AddMonoid Î¹]
variable {M : Î¹ â†’ Submodule R A} [SetLike.GradedMonoid M]

-- The following lines were given on Zulip by Adam Topaz
/-- The canonical isomorphism of an internal direct sum with the ambient algebra -/
noncomputable def coeAlgEquiv (hM : DirectSum.IsInternal M) :
    (DirectSum Î¹ fun i => â†¥(M i)) â‰ƒâ‚[R] A :=
  { RingEquiv.ofBijective (DirectSum.coeAlgHom M) hM with commutes' := fun r => by simp }

/-- Given an `R`-algebra `A` and a family `Î¹ â†’ Submodule R A` of submodules
parameterized by an additive monoid `Î¹`
and satisfying `SetLike.GradedMonoid M` (essentially, is multiplicative)
such that `DirectSum.IsInternal M` (`A` is the direct sum of the `M i`),
we endow `A` with the structure of a graded algebra.
The submodules are the *homogeneous* parts. -/
noncomputable def gradedAlgebra (hM : DirectSum.IsInternal M) : GradedAlgebra M :=
  { (inferInstance : SetLike.GradedMonoid M) with
    decompose' := hM.coeAlgEquiv.symm
    left_inv := hM.coeAlgEquiv.symm.left_inv
    right_inv := hM.coeAlgEquiv.left_inv }

end DirectSum.IsInternal
