/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/
import Mathlib.Data.FunLike.GradedFunLike
import Mathlib.RingTheory.GradedAlgebra.Basic

/-!
# Homomorphisms of graded (semi)rings

This file defines bundled homomorphisms of graded (semi)rings. We use the same structure
`GradedRingHom ğ’œ â„¬`, a.k.a. `ğ’œ â†’+*áµ â„¬`, for both types of homomorphisms.

We do **not** define a separate class of graded ring homomorphisms; instead, we use
`[GradedFunLike F ğ’œ â„¬] [RingHomClass F A B]`.

## Main definitions

* `GradedRingHom`: Graded (semi)ring homomorphisms. Ring homomorphism which preserves the grading.

## Notation

* `â†’+*áµ`: Graded (semi)ring hom.

## Implementation notes

* We don't really need the fact that they are graded rings until the theorem `decompose_map` which
describes how the decomposition interacts with the map.
-/

variable {Î¹ A B C D Ïƒ Ï„ Ïˆ Ï‰ : Type*}
  [Semiring A] [Semiring B] [Semiring C] [Semiring D]
  [SetLike Ïƒ A] [SetLike Ï„ B] [SetLike Ïˆ C] [SetLike Ï‰ D]

section SetLike

/-- Bundled graded (semi)ring homomorphisms. Use `GradedRingHom` for the namespace,
and `GRingHom` for other identifiers, and `ğ’œ â†’+*áµ â„¬` for the notation. -/
structure GradedRingHom (ğ’œ : Î¹ â†’ Ïƒ) (â„¬ : Î¹ â†’ Ï„) extends A â†’+* B where
  map_mem' {i : Î¹} {x : A} : x âˆˆ ğ’œ i â†’ toRingHom x âˆˆ â„¬ i

variable {ğ’œ : Î¹ â†’ Ïƒ} {â„¬ : Î¹ â†’ Ï„} {ğ’ : Î¹ â†’ Ïˆ} {ğ’Ÿ : Î¹ â†’ Ï‰}

@[inherit_doc]
notation:25 ğ’œ " â†’+*áµ " â„¬ => GradedRingHom ğ’œ â„¬

namespace GradedRingHom

section ofClass
variable {F : Type*} [GradedFunLike F ğ’œ â„¬] [RingHomClass F A B]

/-- Turn an element of a type `F` satisfying `[GradedFunLike F ğ’œ â„¬] [RingHomClass F A B]` into an
actual `GradedRingHom`. This is declared as the default coercion from `F` to `ğ’œ â†’+*áµ â„¬`. -/
@[coe]
def ofClass (f : F) : ğ’œ â†’+*áµ â„¬ where
  __ := (f : A â†’+* B)
  map_mem' := map_mem f

/-- Any type satisfying `GradedRingHomClass` can be cast into `GradedRingHom` via
`GradedRingHomClass.toGRingHom`. -/
instance instCoeOfClass : CoeTC F (ğ’œ â†’+*áµ â„¬) :=
  âŸ¨ofClassâŸ©

end ofClass

section coe

instance instGradedFunLike : GradedFunLike (ğ’œ â†’+*áµ â„¬) ğ’œ â„¬ where
  map_mem f := f.map_mem'
  coe f := f.toFun
  coe_injective' f g h := by
    cases f
    cases g
    congr
    apply DFunLike.coe_injective'
    exact h

instance instRingHomClass : RingHomClass (ğ’œ â†’+*áµ â„¬) A B where
  map_add f := f.map_add'
  map_zero f := f.map_zero'
  map_mul f := f.map_mul'
  map_one f := f.map_one'

initialize_simps_projections GradedRingHom (toFun â†’ apply)

theorem toFun_eq_coe (f : ğ’œ â†’+*áµ â„¬) : f.toFun = f :=
  rfl

@[simp]
theorem coe_mk (f : A â†’+* B) (h) : ((âŸ¨f, hâŸ© : ğ’œ â†’+*áµ â„¬) : A â†’ B) = f :=
  rfl

@[simp]
theorem coe_coe {F : Type*} [GradedFunLike F ğ’œ â„¬] [RingHomClass F A B] (f : F) :
    ((f : ğ’œ â†’+*áµ â„¬) : A â†’ B) = f :=
  rfl

attribute [coe] GradedRingHom.toRingHom

instance coeToRingHom : CoeOut (ğ’œ â†’+*áµ â„¬) (A â†’+* B) :=
  âŸ¨GradedRingHom.toRingHomâŸ©

@[simp]
theorem toRingHom_eq_coe (f : ğ’œ â†’+*áµ â„¬) : f.toRingHom = f :=
  rfl

@[simp]
theorem toMonoidHom_eq_coe (f : ğ’œ â†’+*áµ â„¬) : ((f : A â†’+* B) : A â†’* B) = f :=
  rfl

@[simp]
theorem toMonoidWithZeroHom_eq_coe (f : ğ’œ â†’+*áµ â„¬) : (f : A â†’+* B).toMonoidWithZeroHom = f :=
  rfl

@[simp]
theorem toAddMonoidHom_eq_coe (f : ğ’œ â†’+*áµ â„¬) : ((f : A â†’+* B) : A â†’+ B) = f :=
  rfl

/-- Copy of a `GradedRingHom` with a new `toFun` equal to the old one. Useful to fix definitional
equalities. -/
def copy (f : ğ’œ â†’+*áµ â„¬) (f' : A â†’ B) (h : f' = f) : ğ’œ â†’+*áµ â„¬ where
  __ := f.toRingHom.copy f' h
  map_mem' hx := congr($h _ âˆˆ â„¬ _).to_iff.mpr <| f.map_mem' hx

@[simp]
theorem coe_copy (f : ğ’œ â†’+*áµ â„¬) (f' : A â†’ B) (h : f' = f) : â‡‘(f.copy f' h) = f' :=
  rfl

theorem copy_eq (f : ğ’œ â†’+*áµ â„¬) (f' : A â†’ B) (h : f' = f) : f.copy f' h = f :=
  DFunLike.ext' h

end coe

section

variable (f : ğ’œ â†’+*áµ â„¬)

protected theorem congr_fun {f g : ğ’œ â†’+*áµ â„¬} (h : f = g) (x : A) : f x = g x :=
  DFunLike.congr_fun h x

protected theorem congr_arg (f : ğ’œ â†’+*áµ â„¬) {x y : A} (h : x = y) : f x = f y :=
  DFunLike.congr_arg f h

theorem coe_inj â¦ƒf g : ğ’œ â†’+*áµ â„¬â¦„ (h : (f : A â†’ B) = g) : f = g :=
  DFunLike.coe_injective h

@[ext]
theorem ext â¦ƒf g : ğ’œ â†’+*áµ â„¬â¦„ : (âˆ€ x, f x = g x) â†’ f = g :=
  DFunLike.ext _ _

@[simp]
theorem mk_coe (f : ğ’œ â†’+*áµ â„¬) (hâ‚ hâ‚‚ hâ‚ƒ hâ‚„ hâ‚…) : .mk âŸ¨âŸ¨âŸ¨f, hâ‚âŸ©, hâ‚‚âŸ©, hâ‚ƒ, hâ‚„âŸ© hâ‚… = f :=
  ext fun _ => rfl

theorem coe_ringHom_injective : (fun f : ğ’œ â†’+*áµ â„¬ => (f : A â†’+* B)).Injective := fun _ _ h =>
  ext <| DFunLike.congr_fun (F := A â†’+* B) h

/-- Graded ring homomorphisms map zero to zero. -/
protected theorem map_zero (f : ğ’œ â†’+*áµ â„¬) : f 0 = 0 :=
  map_zero f

/-- Graded ring homomorphisms map one to one. -/
protected theorem map_one (f : ğ’œ â†’+*áµ â„¬) : f 1 = 1 :=
  map_one f

/-- Graded ring homomorphisms preserve addition. -/
protected theorem map_add (f : ğ’œ â†’+*áµ â„¬) : âˆ€ a b, f (a + b) = f a + f b :=
  map_add f

/-- Graded ring homomorphisms preserve multiplication. -/
protected theorem map_mul (f : ğ’œ â†’+*áµ â„¬) : âˆ€ a b, f (a * b) = f a * f b :=
  map_mul f

/-- Graded ring homomorphisms preserve the grading. -/
protected theorem map_mem (f : ğ’œ â†’+*áµ â„¬) : âˆ€ {i a}, a âˆˆ ğ’œ i â†’ f a âˆˆ â„¬ i :=
  map_mem f

end

section Ring
variable {A B Ïƒ Ï„ : Type*}
variable [Ring A] [Ring B] [SetLike Ïƒ A] [SetLike Ï„ B]
variable (ğ’œ : Î¹ â†’ Ïƒ) (â„¬ : Î¹ â†’ Ï„)

/-- Graded ring homomorphisms preserve additive inverse. -/
protected theorem map_neg (f : ğ’œ â†’+*áµ â„¬) (x : A) : f (-x) = -f x :=
  map_neg f x

/-- Graded ring homomorphisms preserve subtraction. -/
protected theorem map_sub (f : ğ’œ â†’+*áµ â„¬) (x y : A) :
    f (x - y) = f x - f y :=
  map_sub f x y

end Ring

variable (ğ’œ) in
/-- The identity graded ring homomorphism from a graded ring to itself. -/
def id : ğ’œ â†’+*áµ ğ’œ where
  __ := RingHom.id _
  map_mem' h := h

@[simp, norm_cast]
theorem coe_id : â‡‘(GradedRingHom.id ğ’œ) = _root_.id := rfl

@[simp]
theorem id_apply (x : A) : GradedRingHom.id ğ’œ x = x :=
  rfl

@[simp]
theorem coe_ringHom_id : (id ğ’œ : A â†’+* A) = RingHom.id A :=
  rfl

/-- Composition of graded ring homomorphisms is a graded ring homomorphism. -/
def comp (g : â„¬ â†’+*áµ ğ’) (f : ğ’œ â†’+*áµ â„¬) : ğ’œ â†’+*áµ ğ’ where
  __ := g.toRingHom.comp f
  map_mem' := g.map_mem âˆ˜ f.map_mem

/-- Composition of graded ring homomorphisms is associative. -/
theorem comp_assoc (h : ğ’ â†’+*áµ ğ’Ÿ) (g : â„¬ â†’+*áµ ğ’) (f : ğ’œ â†’+*áµ â„¬) :
    (h.comp g).comp f = h.comp (g.comp f) :=
  rfl

@[simp]
theorem coe_comp (hnp : â„¬ â†’+*áµ ğ’) (hmn : ğ’œ â†’+*áµ â„¬) : (hnp.comp hmn : A â†’ C) = hnp âˆ˜ hmn :=
  rfl

theorem comp_apply (hnp : â„¬ â†’+*áµ ğ’) (hmn : ğ’œ â†’+*áµ â„¬) (x : A) :
    (hnp.comp hmn : A â†’ C) x = hnp (hmn x) :=
  rfl

@[simp]
theorem comp_id (f : ğ’œ â†’+*áµ â„¬) : f.comp (id ğ’œ) = f :=
  ext fun _ => rfl

@[simp]
theorem id_comp (f : ğ’œ â†’+*áµ â„¬) : (id â„¬).comp f = f :=
  ext fun _ => rfl

instance instOne : One (ğ’œ â†’+*áµ ğ’œ) where one := id _
instance instMul : Mul (ğ’œ â†’+*áµ ğ’œ) where mul := comp

lemma one_def : (1 : ğ’œ â†’+*áµ ğ’œ) = id ğ’œ := rfl

lemma mul_def (f g : ğ’œ â†’+*áµ ğ’œ) : f * g = f.comp g := rfl

@[simp, norm_cast] lemma coe_one : â‡‘(1 : ğ’œ â†’+*áµ ğ’œ) = _root_.id := rfl

@[simp, norm_cast] lemma coe_mul (f g : ğ’œ â†’+*áµ ğ’œ) : â‡‘(f * g) = f âˆ˜ g := rfl

instance instMonoid : Monoid (ğ’œ â†’+*áµ ğ’œ) where
  mul_one := comp_id
  one_mul := id_comp
  mul_assoc _ _ _ := comp_assoc _ _ _
  npow n f := (npowRec n f).copy f^[n] <| by induction n <;> simp [npowRec, *]
  npow_succ _ _ := DFunLike.coe_injective <| Function.iterate_succ _ _

@[simp, norm_cast] lemma coe_pow (f : ğ’œ â†’+*áµ ğ’œ) (n : â„•) : â‡‘(f ^ n) = f^[n] := rfl

@[simp]
theorem cancel_right {gâ‚ gâ‚‚ : â„¬ â†’+*áµ ğ’} {f : ğ’œ â†’+*áµ â„¬} (hf : Function.Surjective f) :
    gâ‚.comp f = gâ‚‚.comp f â†” gâ‚ = gâ‚‚ :=
  âŸ¨fun h => ext <| hf.forall.2 (GradedRingHom.ext_iff.1 h), fun h => h â–¸ rflâŸ©

@[simp]
theorem cancel_left {g : â„¬ â†’+*áµ ğ’} {fâ‚ fâ‚‚ : ğ’œ â†’+*áµ â„¬} (hg : Function.Injective g) :
    g.comp fâ‚ = g.comp fâ‚‚ â†” fâ‚ = fâ‚‚ :=
  âŸ¨fun h => ext fun x => hg <| by rw [â† comp_apply, h, comp_apply], fun h => h â–¸ rflâŸ©

end GradedRingHom

end SetLike

section GradedRingHomClass

variable [AddSubmonoidClass Ïƒ A] [AddSubmonoidClass Ï„ B]

section
variable (ğ’œ : Î¹ â†’ Ïƒ) (â„¬ : Î¹ â†’ Ï„)
variable {F : Type*} [GradedFunLike F ğ’œ â„¬] [RingHomClass F A B]

-- Note: if `GradedAddHom` is added later, then the assumptions can be relaxed.
/-- A graded ring homomorphism descends to an additive homomorphism on each indexed component. -/
@[simps!] def gradedAddHom (f : F) (i : Î¹) : ğ’œ i â†’+ â„¬ i where
  toFun x := âŸ¨f x, map_mem f x.2âŸ©
  map_zero' := by ext; simp
  map_add' x y := by ext; simp

end

section
variable [AddMonoid Î¹] (ğ’œ : Î¹ â†’ Ïƒ) (â„¬ : Î¹ â†’ Ï„) [SetLike.GradedMonoid ğ’œ] [SetLike.GradedMonoid â„¬]
variable {F : Type*} [GradedFunLike F ğ’œ â„¬] [RingHomClass F A B]

/-- A graded ring homomorphism descends to a ring homomorphism on the zeroth component. -/
@[simps!] def gradedZeroRingHom (f : F) : ğ’œ 0 â†’+* â„¬ 0 where
  __ := gradedAddHom _ _ f 0
  map_one' := Subtype.ext <| map_one _
  map_mul' _ _ := Subtype.ext <| map_mul ..

end

section GradedRing
variable [DecidableEq Î¹] [AddMonoid Î¹] (ğ’œ : Î¹ â†’ Ïƒ) (â„¬ : Î¹ â†’ Ï„) [GradedRing ğ’œ] [GradedRing â„¬]
variable {F : Type*} [GradedFunLike F ğ’œ â„¬] [RingHomClass F A B]

@[simp] lemma decompose_map (f : F) {x : A} :
    DirectSum.decompose â„¬ (f x) = .map (gradedAddHom _ _ f) (.decompose ğ’œ x) := by
  classical
  rw [â† DirectSum.sum_support_decompose ğ’œ x, map_sum, DirectSum.decompose_sum,
    DirectSum.decompose_sum, map_sum]
  congr 1
  ext n : 1
  rw [DirectSum.decompose_of_mem _ (map_mem f (Subtype.prop _)),
    DirectSum.decompose_of_mem _ (Subtype.prop _), DirectSum.map_of]
  rfl

lemma map_coe_decompose (f : F) {x : A} {i : Î¹} :
    f (DirectSum.decompose ğ’œ x i) = DirectSum.decompose â„¬ (f x) i := by
  simp

end GradedRing

end GradedRingHomClass
