/-
Copyright (c) 2024 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Harald Carlens, Andrew Yang, Jujian Zhang
-/
import Mathlib.RingTheory.FiniteType
import Mathlib.RingTheory.GradedAlgebra.Basic

/-!

# Graded algebras of finite type

We show that graded algebras of finite type are
generated by homogeneous elements of positive degree.

-/

variable {Râ‚€ Î¹ S : Type*} [DecidableEq Î¹] [AddCommMonoid Î¹]
variable [CommRing Râ‚€] [CommRing S] [Algebra Râ‚€ S]
variable (ğ’œ : Î¹ â†’ Submodule Râ‚€ S) [GradedAlgebra ğ’œ]

variable [Algebra.FiniteType (ğ’œ 0) S]

namespace GradedAlgebra

theorem exists_finset_adjoin_eq_top_and_homogeneous :
    âˆƒ s : Finset S, Algebra.adjoin (A := S) (ğ’œ 0) s = âŠ¤ âˆ§
      âˆ€ i âˆˆ s, SetLike.IsHomogeneousElem ğ’œ i := by
  classical
  obtain âŸ¨F, hFâŸ© := Algebra.FiniteType.out (R := ğ’œ 0) (A := S)
  let Î¹â‚€ := Î£ (x : F), (DirectSum.decompose ğ’œ x.1).support
  let x (i : Î¹â‚€) : S := ((DirectSum.decompose ğ’œ) i.1 i.2).1
  refine âŸ¨Finset.univ.image x, ?_, by simpa using fun f â†¦ âŸ¨_, (DirectSum.decompose ğ’œ f.1 f.2).2âŸ©âŸ©
  rw [â† top_le_iff, â† hF, Algebra.adjoin_le_iff]
  intro s hs
  rw [â† DirectSum.sum_support_decompose ğ’œ s]
  exact sum_mem fun n hn â†¦ Algebra.subset_adjoin (by simpa using âŸ¨âŸ¨âŸ¨s, hsâŸ©, n, hnâŸ©, rflâŸ©)

theorem exists_finset_adjoin_eq_top_and_homogeneous_ne_zero :
    âˆƒ s : Finset S, Algebra.adjoin (A := S) (ğ’œ 0) s = âŠ¤ âˆ§ âˆ€ i âˆˆ s, âˆƒ n â‰  0, i âˆˆ ğ’œ n := by
  obtain âŸ¨s, hâ‚, hâ‚‚âŸ© := exists_finset_adjoin_eq_top_and_homogeneous ğ’œ
  choose! n hn using hâ‚‚
  refine âŸ¨s.filter (n Â· â‰  0), ?_, by simpa using fun i hi hin â†¦ âŸ¨n i, hin, hn i hiâŸ©âŸ©
  rw [â† top_le_iff, â† hâ‚, Algebra.adjoin_le_iff]
  rintro i hi
  by_cases hi0 : n i = 0
  Â· exact Subalgebra.algebraMap_mem
      (Algebra.adjoin (ğ’œ 0) (s.filter (n Â· â‰  0) : Set S)) âŸ¨i, hi0 â–¸ hn i hiâŸ©
  Â· exact Algebra.subset_adjoin (by simpa [hi0] using hi)

end GradedAlgebra
