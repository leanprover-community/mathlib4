/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/

import Mathlib.LinearAlgebra.TensorProduct.Decomposition
import Mathlib.RingTheory.GradedAlgebra.Basic
import Mathlib.RingTheory.TensorProduct.Basic

/-! # Tensor product of graded algebra

In this file we show that if `ğ’œ` is a graded `R`-algebra, and `S` is any `R`-algebra, then
`S âŠ—[R] ğ’œ` is a graded `S`-algebra with the same grading.
-/

namespace GradedAlgebra

variable {Î¹ R A S : Type*} [DecidableEq Î¹] [AddMonoid Î¹]
  [CommSemiring R] [Semiring A] [Algebra R A]
  (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ]
  [CommSemiring S] [Algebra R S]

open TensorProduct Submodule SetLike

instance baseChange : GradedAlgebra fun i â†¦ (ğ’œ i).baseChange S where
  one_mem := tmul_mem_baseChange_of_mem _ <| one_mem_graded ğ’œ
  mul_mem i j := by
    suffices h : ((ğ’œ i).baseChange S).mapâ‚‚ (Algebra.lmul S (S âŠ—[R] A)) ((ğ’œ j).baseChange S) â‰¤
      (ğ’œ (i + j)).baseChange S from fun xi xj â†¦ (h <| apply_mem_mapâ‚‚ _ Â· Â·)
    simp_rw [baseChange_eq_span, mapâ‚‚_span_span, span_le, Set.image2_subset_iff]
    rintro - âŸ¨x, hx, rflâŸ© - âŸ¨y, hy, rflâŸ©
    simpa using subset_span <| Set.mem_image_of_mem _ <| mul_mem_graded hx hy

end GradedAlgebra
