/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Eric Wieser
-/
import Mathlib.RingTheory.GradedAlgebra.Homogeneous.Ideal

/-!

This file contains a proof that the radical of any homogeneous ideal is a homogeneous ideal

## Main statements

* `Ideal.IsHomogeneous.isPrime_iff`: for any `I : Ideal A`, if `I` is homogeneous, then
  `I` is prime if and only if `I` is homogeneously prime, i.e. `I â‰  âŠ¤` and if `x, y` are
  homogeneous elements such that `x * y âˆˆ I`, then at least one of `x,y` is in `I`.
* `Ideal.IsPrime.homogeneousCore`: for any `I : Ideal A`, if `I` is prime, then
  `I.homogeneous_core ğ’œ` (i.e. the largest homogeneous ideal contained in `I`) is also prime.
* `Ideal.IsHomogeneous.radical`: for any `I : Ideal A`, if `I` is homogeneous, then the
  radical of `I` is homogeneous as well.
* `HomogeneousIdeal.radical`: for any `I : HomogeneousIdeal ğ’œ`, `I.radical` is the
  radical of `I` as a `HomogeneousIdeal ğ’œ`.

## Implementation details

Throughout this file, the indexing type `Î¹` of grading is assumed to be a
`LinearOrderedCancelAddCommMonoid`. This might be stronger than necessary but cancelling
property is strictly necessary; for a counterexample of how `Ideal.IsHomogeneous.isPrime_iff`
fails for a non-cancellative set see `Counterexamples/HomogeneousPrimeNotPrime.lean`.

## Tags

homogeneous, radical
-/


open GradedRing DirectSum SetLike Finset

variable {Î¹ Ïƒ A : Type*}
variable [CommRing A]
variable [AddCommMonoid Î¹] [LinearOrder Î¹] [IsOrderedCancelAddMonoid Î¹]
variable [SetLike Ïƒ A] [AddSubmonoidClass Ïƒ A] {ğ’œ : Î¹ â†’ Ïƒ} [GradedRing ğ’œ]

theorem Ideal.IsHomogeneous.isPrime_of_homogeneous_mem_or_mem {I : Ideal A} (hI : I.IsHomogeneous ğ’œ)
    (I_ne_top : I â‰  âŠ¤)
    (homogeneous_mem_or_mem :
      âˆ€ {x y : A}, IsHomogeneousElem ğ’œ x â†’ IsHomogeneousElem ğ’œ y â†’ x * y âˆˆ I â†’ x âˆˆ I âˆ¨ y âˆˆ I) :
    Ideal.IsPrime I :=
  âŸ¨I_ne_top, by
    intro x y hxy
    by_contra! rid
    obtain âŸ¨ridâ‚, ridâ‚‚âŸ© := rid
    classical
      /-
        The idea of the proof is the following :
        since `x * y âˆˆ I` and `I` homogeneous, then `proj i (x * y) âˆˆ I` for any `i : Î¹`.
        Then consider two sets `{i âˆˆ x.support | xáµ¢ âˆ‰ I}` and `{j âˆˆ y.support | yâ±¼ âˆ‰ J}`;
        let `maxâ‚, maxâ‚‚` be the maximum of the two sets, then `proj (maxâ‚ + maxâ‚‚) (x * y) âˆˆ I`.
        Then, `proj maxâ‚ x âˆ‰ I` and `proj maxâ‚‚ j âˆ‰ I`
        but `proj i x âˆˆ I` for all `maxâ‚ < i` and `proj j y âˆˆ I` for all `maxâ‚‚ < j`.
        `  proj (maxâ‚ + maxâ‚‚) (x * y)`
        `= âˆ‘ {(i, j) âˆˆ supports | i + j = maxâ‚ + maxâ‚‚}, xáµ¢ * yâ±¼`
        `= proj maxâ‚ x * proj maxâ‚‚ y`
        `  + âˆ‘ {(i, j) âˆˆ supports \ {(maxâ‚, maxâ‚‚)} | i + j = maxâ‚ + maxâ‚‚}, xáµ¢ * yâ±¼`.
        This is a contradiction, because both `proj (maxâ‚ + maxâ‚‚) (x * y) âˆˆ I` and the sum on the
        right-hand side is in `I` however `proj maxâ‚ x * proj maxâ‚‚ y` is not in `I`.
        -/
      set setâ‚ := {i âˆˆ (decompose ğ’œ x).support | proj ğ’œ i x âˆ‰ I} with setâ‚_eq
      set setâ‚‚ := {i âˆˆ (decompose ğ’œ y).support | proj ğ’œ i y âˆ‰ I} with setâ‚‚_eq
      have nonempty :
        âˆ€ x : A, x âˆ‰ I â†’ {i âˆˆ (decompose ğ’œ x).support | proj ğ’œ i x âˆ‰ I}.Nonempty := by
        intro x hx
        rw [filter_nonempty_iff]
        contrapose! hx
        simp_rw [proj_apply] at hx
        rw [â† sum_support_decompose ğ’œ x]
        exact Ideal.sum_mem _ hx
      set maxâ‚ := setâ‚.max' (nonempty x ridâ‚)
      set maxâ‚‚ := setâ‚‚.max' (nonempty y ridâ‚‚)
      have mem_maxâ‚ : maxâ‚ âˆˆ setâ‚ := max'_mem setâ‚ (nonempty x ridâ‚)
      have mem_maxâ‚‚ : maxâ‚‚ âˆˆ setâ‚‚ := max'_mem setâ‚‚ (nonempty y ridâ‚‚)
      replace hxy : proj ğ’œ (maxâ‚ + maxâ‚‚) (x * y) âˆˆ I := hI _ hxy
      have mem_I : proj ğ’œ maxâ‚ x * proj ğ’œ maxâ‚‚ y âˆˆ I := by
        set antidiag :=
          {z âˆˆ (decompose ğ’œ x).support Ã—Ë¢ (decompose ğ’œ y).support | z.1 + z.2 = maxâ‚ + maxâ‚‚}
           with ha
        have mem_antidiag : (maxâ‚, maxâ‚‚) âˆˆ antidiag := by
          simp only [antidiag, mem_filter, mem_product]
          exact âŸ¨âŸ¨mem_of_mem_filter _ mem_maxâ‚, mem_of_mem_filter _ mem_maxâ‚‚âŸ©, trivialâŸ©
        have eq_add_sum :=
          calc
            proj ğ’œ (maxâ‚ + maxâ‚‚) (x * y) = âˆ‘ ij âˆˆ antidiag, proj ğ’œ ij.1 x * proj ğ’œ ij.2 y := by
              simp_rw [ha, proj_apply, DirectSum.decompose_mul, DirectSum.coe_mul_apply ğ’œ]
            _ =
                proj ğ’œ maxâ‚ x * proj ğ’œ maxâ‚‚ y +
                  âˆ‘ ij âˆˆ antidiag.erase (maxâ‚, maxâ‚‚), proj ğ’œ ij.1 x * proj ğ’œ ij.2 y :=
              (add_sum_erase _ _ mem_antidiag).symm
        rw [eq_sub_of_add_eq eq_add_sum.symm]
        refine Ideal.sub_mem _ hxy (Ideal.sum_mem _ fun z H => ?_)
        rcases z with âŸ¨i, jâŸ©
        simp only [antidiag, mem_erase, Prod.mk_inj, Ne, mem_filter, mem_product] at H
        rcases H with âŸ¨Hâ‚, âŸ¨Hâ‚‚, Hâ‚ƒâŸ©, Hâ‚„âŸ©
        have max_lt : maxâ‚ < i âˆ¨ maxâ‚‚ < j := by
          rcases lt_trichotomy maxâ‚ i with (h | rfl | h)
          Â· exact Or.inl h
          Â· refine False.elim (Hâ‚ âŸ¨rfl, add_left_cancel Hâ‚„âŸ©)
          Â· apply Or.inr
            have := add_lt_add_right h j
            rw [Hâ‚„] at this
            exact lt_of_add_lt_add_left this
        rcases max_lt with max_lt | max_lt
        Â· -- in this case `maxâ‚ < i`, then `xáµ¢ âˆˆ I`; for otherwise `i âˆˆ setâ‚` then `i â‰¤ maxâ‚`.
          have notMem : i âˆ‰ setâ‚ := fun h =>
            lt_irrefl _ ((max'_lt_iff setâ‚ (nonempty x ridâ‚)).mp max_lt i h)
          rw [setâ‚_eq] at notMem
          simp only [not_and, Classical.not_not, mem_filter] at notMem
          exact Ideal.mul_mem_right _ I (notMem Hâ‚‚)
        Â· -- in this case `maxâ‚‚ < j`, then `yâ±¼ âˆˆ I`; for otherwise `j âˆˆ setâ‚‚`, then `j â‰¤ maxâ‚‚`.
          have notMem : j âˆ‰ setâ‚‚ := fun h =>
            lt_irrefl _ ((max'_lt_iff setâ‚‚ (nonempty y ridâ‚‚)).mp max_lt j h)
          rw [setâ‚‚_eq] at notMem
          simp only [not_and, Classical.not_not, mem_filter] at notMem
          exact Ideal.mul_mem_left I _ (notMem Hâ‚ƒ)
      have notMem_I : proj ğ’œ maxâ‚ x * proj ğ’œ maxâ‚‚ y âˆ‰ I := by
        have neither_mem : proj ğ’œ maxâ‚ x âˆ‰ I âˆ§ proj ğ’œ maxâ‚‚ y âˆ‰ I := by
          rw [mem_filter] at mem_maxâ‚ mem_maxâ‚‚
          exact âŸ¨mem_maxâ‚.2, mem_maxâ‚‚.2âŸ©
        intro _rid
        rcases homogeneous_mem_or_mem âŸ¨maxâ‚, SetLike.coe_mem _âŸ© âŸ¨maxâ‚‚, SetLike.coe_mem _âŸ© mem_I
          with h | h
        Â· apply neither_mem.1 h
        Â· apply neither_mem.2 h
      exact notMem_I mem_IâŸ©

theorem Ideal.IsHomogeneous.isPrime_iff {I : Ideal A} (h : I.IsHomogeneous ğ’œ) :
    I.IsPrime â†”
      I â‰  âŠ¤ âˆ§
        âˆ€ {x y : A},
          IsHomogeneousElem ğ’œ x â†’ IsHomogeneousElem ğ’œ y â†’ x * y âˆˆ I â†’ x âˆˆ I âˆ¨ y âˆˆ I :=
  âŸ¨fun HI => âŸ¨HI.ne_top, fun _ _ hxy => Ideal.IsPrime.mem_or_mem HI hxyâŸ©,
    fun âŸ¨I_ne_top, homogeneous_mem_or_memâŸ© =>
    h.isPrime_of_homogeneous_mem_or_mem I_ne_top @homogeneous_mem_or_memâŸ©

theorem Ideal.IsPrime.homogeneousCore {I : Ideal A} (h : I.IsPrime) :
    (I.homogeneousCore ğ’œ).toIdeal.IsPrime := by
  apply (Ideal.homogeneousCore ğ’œ I).isHomogeneous.isPrime_of_homogeneous_mem_or_mem
  Â· exact ne_top_of_le_ne_top h.ne_top (Ideal.toIdeal_homogeneousCore_le ğ’œ I)
  rintro x y hx hy hxy
  have H := h.mem_or_mem (Ideal.toIdeal_homogeneousCore_le ğ’œ I hxy)
  refine H.imp ?_ ?_
  Â· exact Ideal.mem_homogeneousCore_of_homogeneous_of_mem hx
  Â· exact Ideal.mem_homogeneousCore_of_homogeneous_of_mem hy

theorem Ideal.IsHomogeneous.radical_eq {I : Ideal A} (hI : I.IsHomogeneous ğ’œ) :
    I.radical = InfSet.sInf { J | Ideal.IsHomogeneous ğ’œ J âˆ§ I â‰¤ J âˆ§ J.IsPrime } := by
  rw [Ideal.radical_eq_sInf]
  apply le_antisymm
  Â· exact sInf_le_sInf fun J => And.right
  Â· refine sInf_le_sInf_of_isCoinitialFor ?_
    rintro J âŸ¨HJâ‚, HJâ‚‚âŸ©
    refine âŸ¨(J.homogeneousCore ğ’œ).toIdeal, ?_, J.toIdeal_homogeneousCore_le _âŸ©
    refine âŸ¨HomogeneousIdeal.isHomogeneous _, ?_, HJâ‚‚.homogeneousCoreâŸ©
    exact hI.toIdeal_homogeneousCore_eq_self.symm.trans_le (Ideal.homogeneousCore_mono _ HJâ‚)

theorem Ideal.IsHomogeneous.radical {I : Ideal A} (h : I.IsHomogeneous ğ’œ) :
    I.radical.IsHomogeneous ğ’œ := by
  rw [h.radical_eq]
  exact Ideal.IsHomogeneous.sInf fun _ => And.left

/-- The radical of a homogeneous ideal, as another homogeneous ideal. -/
def HomogeneousIdeal.radical (I : HomogeneousIdeal ğ’œ) : HomogeneousIdeal ğ’œ :=
  âŸ¨I.toIdeal.radical, I.isHomogeneous.radicalâŸ©

@[simp]
theorem HomogeneousIdeal.coe_radical (I : HomogeneousIdeal ğ’œ) :
    I.radical.toIdeal = I.toIdeal.radical := rfl
