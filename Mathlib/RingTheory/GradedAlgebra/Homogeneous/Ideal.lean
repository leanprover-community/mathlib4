/-
Copyright (c) 2021 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Eric Wieser
-/
import Mathlib.LinearAlgebra.Finsupp.SumProd
import Mathlib.RingTheory.GradedAlgebra.Basic
import Mathlib.RingTheory.Ideal.Basic
import Mathlib.RingTheory.Ideal.Maps
import Mathlib.RingTheory.GradedAlgebra.Homogeneous.Submodule

/-!
# Homogeneous ideals of a graded algebra

This file defines homogeneous ideals of `GradedRing ğ’œ` where `ğ’œ : Î¹ â†’ Submodule R A` and
operations on them.

## Main definitions

For any `I : Ideal A`:
* `Ideal.IsHomogeneous ğ’œ I`: The property that an ideal is closed under `GradedRing.proj`.
* `HomogeneousIdeal ğ’œ`: The structure extending ideals which satisfy `Ideal.IsHomogeneous`.
* `Ideal.homogeneousCore I ğ’œ`: The largest homogeneous ideal smaller than `I`.
* `Ideal.homogeneousHull I ğ’œ`: The smallest homogeneous ideal larger than `I`.

## Main statements

* `HomogeneousIdeal.completeLattice`: `Ideal.IsHomogeneous` is preserved by `âŠ¥`, `âŠ¤`, `âŠ”`, `âŠ“`,
  `â¨†`, `â¨…`, and so the subtype of homogeneous ideals inherits a complete lattice structure.
* `Ideal.homogeneousCore.gi`: `Ideal.homogeneousCore` forms a Galois insertion with coercion.
* `Ideal.homogeneousHull.gi`: `Ideal.homogeneousHull` forms a Galois insertion with coercion.

## Implementation notes

We introduce `Ideal.homogeneousCore'` earlier than might be expected so that we can get access
to `Ideal.IsHomogeneous.iff_exists` as quickly as possible.

## Tags

graded algebra, homogeneous
-/


open SetLike DirectSum Set

open Pointwise DirectSum

variable {Î¹ Ïƒ A : Type*}

section HomogeneousDef

variable [Semiring A]
variable [SetLike Ïƒ A] [AddSubmonoidClass Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ)
variable [DecidableEq Î¹] [AddMonoid Î¹] [GradedRing ğ’œ]
variable (I : Ideal A)

/-- An `I : Ideal A` is homogeneous if for every `r âˆˆ I`, all homogeneous components
  of `r` are in `I`. -/
abbrev Ideal.IsHomogeneous : Prop := Submodule.IsHomogeneous I ğ’œ

theorem Ideal.IsHomogeneous.mem_iff {I} (hI : Ideal.IsHomogeneous ğ’œ I) {x} :
    x âˆˆ I â†” âˆ€ i, (decompose ğ’œ x i : A) âˆˆ I :=
  AddSubmonoidClass.IsHomogeneous.mem_iff ğ’œ _ hI

/-- For any `Semiring A`, we collect the homogeneous ideals of `A` into a type. -/
abbrev HomogeneousIdeal := HomogeneousSubmodule ğ’œ ğ’œ

variable {ğ’œ}

/-- Converting a homogeneous ideal to an ideal. -/
abbrev HomogeneousIdeal.toIdeal (I : HomogeneousIdeal ğ’œ) : Ideal A :=
  I.toSubmodule

theorem HomogeneousIdeal.isHomogeneous (I : HomogeneousIdeal ğ’œ) :
    I.toIdeal.IsHomogeneous ğ’œ := I.is_homogeneous'

theorem HomogeneousIdeal.toIdeal_injective :
    Function.Injective (HomogeneousIdeal.toIdeal : HomogeneousIdeal ğ’œ â†’ Ideal A) :=
  HomogeneousSubmodule.toSubmodule_injective ğ’œ ğ’œ

theorem HomogeneousIdeal.toIdeal_le_toIdeal_iff {I J : HomogeneousIdeal ğ’œ} :
    I.toIdeal â‰¤ J.toIdeal â†” I â‰¤ J := Iff.rfl

instance HomogeneousIdeal.setLike : SetLike (HomogeneousIdeal ğ’œ) A :=
  HomogeneousSubmodule.setLike ğ’œ ğ’œ

@[ext]
theorem HomogeneousIdeal.ext {I J : HomogeneousIdeal ğ’œ} (h : I.toIdeal = J.toIdeal) : I = J :=
  HomogeneousIdeal.toIdeal_injective h

theorem HomogeneousIdeal.ext' {I J : HomogeneousIdeal ğ’œ} (h : âˆ€ i, âˆ€ x âˆˆ ğ’œ i, x âˆˆ I â†” x âˆˆ J) :
    I = J := HomogeneousSubmodule.ext' ğ’œ ğ’œ h

@[simp high]
theorem HomogeneousIdeal.mem_iff {I : HomogeneousIdeal ğ’œ} {x : A} : x âˆˆ I.toIdeal â†” x âˆˆ I :=
  Iff.rfl

end HomogeneousDef

section HomogeneousCore

variable [Semiring A]
variable [SetLike Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ)
variable (I : Ideal A)

/-- For any `I : Ideal A`, not necessarily homogeneous, `I.homogeneousCore' ğ’œ`
is the largest homogeneous ideal of `A` contained in `I`, as an ideal. -/
def Ideal.homogeneousCore' (I : Ideal A) : Ideal A :=
  Ideal.span ((â†‘) '' (((â†‘) : Subtype (SetLike.IsHomogeneousElem ğ’œ) â†’ A) â»Â¹' I))

theorem Ideal.homogeneousCore'_mono : Monotone (Ideal.homogeneousCore' ğ’œ) :=
  fun _ _ I_le_J => Ideal.span_mono <| Set.image_mono fun _ => @I_le_J _

theorem Ideal.homogeneousCore'_le : I.homogeneousCore' ğ’œ â‰¤ I :=
  Ideal.span_le.2 <| image_preimage_subset _ _

end HomogeneousCore

section IsHomogeneousIdealDefs

variable [Semiring A]
variable [SetLike Ïƒ A] [AddSubmonoidClass Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ)
variable [DecidableEq Î¹] [AddMonoid Î¹] [GradedRing ğ’œ]
variable (I : Ideal A)

theorem Ideal.isHomogeneous_iff_forall_subset :
    I.IsHomogeneous ğ’œ â†” âˆ€ i, (I : Set A) âŠ† GradedRing.proj ğ’œ i â»Â¹' I :=
  Iff.rfl

theorem Ideal.isHomogeneous_iff_subset_iInter :
    I.IsHomogeneous ğ’œ â†” (I : Set A) âŠ† â‹‚ i, GradedRing.proj ğ’œ i â»Â¹' â†‘I :=
  subset_iInter_iff.symm

theorem Ideal.mul_homogeneous_element_mem_of_mem
    {I : Ideal A} (r x : A) (hxâ‚ : IsHomogeneousElem ğ’œ x)
    (hxâ‚‚ : x âˆˆ I) (j : Î¹) : GradedRing.proj ğ’œ j (r * x) âˆˆ I :=
  Submodule.smul_homogeneous_element_mem_of_mem ğ’œ ğ’œ I r x hxâ‚ hxâ‚‚ j

theorem Ideal.homogeneous_span (s : Set A) (h : âˆ€ x âˆˆ s, IsHomogeneousElem ğ’œ x) :
    (Ideal.span s).IsHomogeneous ğ’œ :=
  Submodule.homogeneous_span ğ’œ ğ’œ s h

/-- For any `I : Ideal A`, not necessarily homogeneous, `I.homogeneousCore' ğ’œ`
is the largest homogeneous ideal of `A` contained in `I`. -/
abbrev Ideal.homogeneousCore : HomogeneousIdeal ğ’œ :=
  Submodule.homogeneousCore ğ’œ ğ’œ I

theorem Ideal.toIdeal_homogeneousCore_eq_span :
    (I.homogeneousCore ğ’œ).toIdeal =
    .span ((â†‘) '' (((â†‘) : Subtype (SetLike.IsHomogeneousElem ğ’œ) â†’ A) â»Â¹' I)) :=
  Submodule.toSubmodule_homogeneousCore_eq_span ğ’œ ğ’œ I

theorem Ideal.homogeneousCore_mono : Monotone (Ideal.homogeneousCore ğ’œ) :=
  Submodule.homogeneousCore_mono ğ’œ ğ’œ

theorem Ideal.toIdeal_homogeneousCore_le : (I.homogeneousCore ğ’œ).toIdeal â‰¤ I :=
  Submodule.toSubmodule_homogeneousCore_le ğ’œ ğ’œ I

variable {ğ’œ I}

theorem Ideal.IsHomogeneous.le_toIdeal_homogeneousCore_iff {J : Ideal A} (hI : I.IsHomogeneous ğ’œ) :
    I â‰¤ (J.homogeneousCore ğ’œ).toIdeal â†” I â‰¤ J :=
  Submodule.IsHomogeneous.le_toSubmodule_homogeneousCore_iff ğ’œ ğ’œ hI

theorem HomogeneousIdeal.le_homogeneousCore_iff {I : HomogeneousIdeal ğ’œ} {J : Ideal A} :
    I â‰¤ J.homogeneousCore ğ’œ â†” I.toIdeal â‰¤ J :=
  HomogeneousSubmodule.le_homogeneousCore_iff ğ’œ ğ’œ I

theorem Ideal.mem_homogeneousCore_of_homogeneous_of_mem {x : A} (h : IsHomogeneousElem ğ’œ x)
    (hmem : x âˆˆ I) : x âˆˆ I.homogeneousCore ğ’œ :=
  Submodule.mem_homogeneousCore_of_homogeneous_of_mem ğ’œ ğ’œ I h hmem

theorem Ideal.IsHomogeneous.toIdeal_homogeneousCore_eq_self (h : I.IsHomogeneous ğ’œ) :
    (I.homogeneousCore ğ’œ).toIdeal = I :=
  Submodule.IsHomogeneous.toSubmodule_homogeneousCore_eq_self ğ’œ ğ’œ I h

@[simp high]
theorem HomogeneousIdeal.toIdeal_homogeneousCore_eq_self (I : HomogeneousIdeal ğ’œ) :
    I.toIdeal.homogeneousCore ğ’œ = I :=
  HomogeneousSubmodule.toSubmodule_homogeneousCore_eq_self _ _ I

variable (ğ’œ I)

theorem Ideal.IsHomogeneous.iff_eq : I.IsHomogeneous ğ’œ â†” (I.homogeneousCore ğ’œ).toIdeal = I :=
  âŸ¨fun hI => hI.toIdeal_homogeneousCore_eq_self, fun hI => hI â–¸ (Ideal.homogeneousCore ğ’œ I).2âŸ©

theorem Ideal.IsHomogeneous.iff_exists :
    I.IsHomogeneous ğ’œ â†” âˆƒ S : Set (homogeneousSubmonoid ğ’œ), I = Ideal.span ((â†‘) '' S) :=
  Submodule.IsHomogeneous.iff_exists ğ’œ _ I

end IsHomogeneousIdealDefs

/-! ### Operations

In this section, we show that `Ideal.IsHomogeneous` is preserved by various notations, then use
these results to provide these notation typeclasses for `HomogeneousIdeal`. -/


section Operations

section Semiring

variable [Semiring A] [DecidableEq Î¹] [AddMonoid Î¹]
variable [SetLike Ïƒ A] [AddSubmonoidClass Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ) [GradedRing ğ’œ]

namespace Ideal.IsHomogeneous

theorem bot : Ideal.IsHomogeneous ğ’œ âŠ¥ := Submodule.IsHomogeneous.bot ğ’œ

theorem top : Ideal.IsHomogeneous ğ’œ âŠ¤ := Submodule.IsHomogeneous.top ğ’œ

variable {ğ’œ}

theorem inf {I J : Ideal A} (HI : I.IsHomogeneous ğ’œ) (HJ : J.IsHomogeneous ğ’œ) :
    (I âŠ“ J).IsHomogeneous ğ’œ :=
  fun _ _ hr => âŸ¨HI _ hr.1, HJ _ hr.2âŸ©

theorem sup {I J : Ideal A} (HI : I.IsHomogeneous ğ’œ) (HJ : J.IsHomogeneous ğ’œ) :
    (I âŠ” J).IsHomogeneous ğ’œ :=
  Submodule.IsHomogeneous.sup ğ’œ ğ’œ HI HJ

protected theorem iSup {Îº : Sort*} {f : Îº â†’ Ideal A} (h : âˆ€ i, (f i).IsHomogeneous ğ’œ) :
    (â¨† i, f i).IsHomogeneous ğ’œ :=
  Submodule.IsHomogeneous.iSup ğ’œ ğ’œ h

protected theorem iInf {Îº : Sort*} {f : Îº â†’ Ideal A} (h : âˆ€ i, (f i).IsHomogeneous ğ’œ) :
    (â¨… i, f i).IsHomogeneous ğ’œ := Submodule.IsHomogeneous.iInf ğ’œ h

theorem iSupâ‚‚ {Îº : Sort*} {Îº' : Îº â†’ Sort*} {f : âˆ€ i, Îº' i â†’ Ideal A}
    (h : âˆ€ i j, (f i j).IsHomogeneous ğ’œ) : (â¨† (i) (j), f i j).IsHomogeneous ğ’œ :=
  IsHomogeneous.iSup fun i => IsHomogeneous.iSup <| h i

theorem iInfâ‚‚ {Îº : Sort*} {Îº' : Îº â†’ Sort*} {f : âˆ€ i, Îº' i â†’ Ideal A}
    (h : âˆ€ i j, (f i j).IsHomogeneous ğ’œ) : (â¨… (i) (j), f i j).IsHomogeneous ğ’œ :=
  IsHomogeneous.iInf fun i => IsHomogeneous.iInf <| h i

theorem sSup {â„ : Set (Ideal A)} (h : âˆ€ I âˆˆ â„, Ideal.IsHomogeneous ğ’œ I) :
    (sSup â„).IsHomogeneous ğ’œ := Submodule.IsHomogeneous.sSup ğ’œ ğ’œ h

theorem sInf {â„ : Set (Ideal A)} (h : âˆ€ I âˆˆ â„, Ideal.IsHomogeneous ğ’œ I) :
    (sInf â„).IsHomogeneous ğ’œ := Submodule.IsHomogeneous.sInf ğ’œ h

end Ideal.IsHomogeneous

variable {ğ’œ}

namespace HomogeneousIdeal

instance completeLattice : CompleteLattice (HomogeneousIdeal ğ’œ) :=
  HomogeneousSubmodule.completeLattice (ğ’œ := ğ’œ) (â„³ := ğ’œ)

@[simp]
theorem coe_top : ((âŠ¤ : HomogeneousIdeal ğ’œ) : Set A) = univ :=
  rfl

@[simp]
theorem coe_bot : ((âŠ¥ : HomogeneousIdeal ğ’œ) : Set A) = 0 :=
  rfl

@[simp high]
theorem coe_sup (I J : HomogeneousIdeal ğ’œ) : â†‘(I âŠ” J) = (I + J : Set A) :=
  Submodule.coe_sup _ _

@[simp]
theorem coe_inf (I J : HomogeneousIdeal ğ’œ) : (â†‘(I âŠ“ J) : Set A) = â†‘I âˆ© â†‘J :=
  rfl

@[simp]
theorem toIdeal_top : (âŠ¤ : HomogeneousIdeal ğ’œ).toIdeal = (âŠ¤ : Ideal A) :=
  rfl

@[simp]
theorem toIdeal_bot : (âŠ¥ : HomogeneousIdeal ğ’œ).toIdeal = (âŠ¥ : Ideal A) :=
  rfl

@[simp]
theorem toIdeal_sup (I J : HomogeneousIdeal ğ’œ) : (I âŠ” J).toIdeal = I.toIdeal âŠ” J.toIdeal :=
  rfl

@[simp]
theorem toIdeal_inf (I J : HomogeneousIdeal ğ’œ) : (I âŠ“ J).toIdeal = I.toIdeal âŠ“ J.toIdeal :=
  rfl

@[simp]
theorem toIdeal_sSup (â„ : Set (HomogeneousIdeal ğ’œ)) : (sSup â„).toIdeal = â¨† s âˆˆ â„, toIdeal s :=
  rfl

@[simp]
theorem toIdeal_sInf (â„ : Set (HomogeneousIdeal ğ’œ)) : (sInf â„).toIdeal = â¨… s âˆˆ â„, toIdeal s :=
  rfl

@[simp high]
theorem toIdeal_iSup {Îº : Sort*} (s : Îº â†’ HomogeneousIdeal ğ’œ) :
    (â¨† i, s i).toIdeal = â¨† i, (s i).toIdeal := by
  rw [iSup, toIdeal_sSup, iSup_range]

@[simp high]
theorem toIdeal_iInf {Îº : Sort*} (s : Îº â†’ HomogeneousIdeal ğ’œ) :
    (â¨… i, s i).toIdeal = â¨… i, (s i).toIdeal := by
  rw [iInf, toIdeal_sInf, iInf_range]

theorem toIdeal_iSupâ‚‚ {Îº : Sort*} {Îº' : Îº â†’ Sort*} (s : âˆ€ i, Îº' i â†’ HomogeneousIdeal ğ’œ) :
    (â¨† (i) (j), s i j).toIdeal = â¨† (i) (j), (s i j).toIdeal := by
  simp_rw [toIdeal_iSup]

theorem toIdeal_iInfâ‚‚ {Îº : Sort*} {Îº' : Îº â†’ Sort*} (s : âˆ€ i, Îº' i â†’ HomogeneousIdeal ğ’œ) :
    (â¨… (i) (j), s i j).toIdeal = â¨… (i) (j), (s i j).toIdeal := by
  simp_rw [toIdeal_iInf]

@[simp high]
theorem eq_top_iff (I : HomogeneousIdeal ğ’œ) : I = âŠ¤ â†” I.toIdeal = âŠ¤ :=
  toIdeal_injective.eq_iff.symm

@[simp high]
theorem eq_bot_iff (I : HomogeneousIdeal ğ’œ) : I = âŠ¥ â†” I.toIdeal = âŠ¥ :=
  toIdeal_injective.eq_iff.symm

instance : Add (HomogeneousIdeal ğ’œ) :=
  âŸ¨(Â· âŠ” Â·)âŸ©

@[simp]
theorem toIdeal_add (I J : HomogeneousIdeal ğ’œ) : (I + J).toIdeal = I.toIdeal + J.toIdeal :=
  rfl

instance : Inhabited (HomogeneousIdeal ğ’œ) where default := âŠ¥

end HomogeneousIdeal

end Semiring

section CommSemiring

variable [CommSemiring A]
variable [DecidableEq Î¹] [AddMonoid Î¹]
variable [SetLike Ïƒ A] [AddSubmonoidClass Ïƒ A] {ğ’œ : Î¹ â†’ Ïƒ} [GradedRing ğ’œ]
variable (I : Ideal A)

theorem Ideal.IsHomogeneous.mul {I J : Ideal A} (HI : I.IsHomogeneous ğ’œ) (HJ : J.IsHomogeneous ğ’œ) :
    (I * J).IsHomogeneous ğ’œ := by
  rw [Ideal.IsHomogeneous.iff_exists] at HI HJ âŠ¢
  obtain âŸ¨âŸ¨sâ‚, rflâŸ©, âŸ¨sâ‚‚, rflâŸ©âŸ© := HI, HJ
  rw [Ideal.span_mul_span']
  exact âŸ¨sâ‚ * sâ‚‚, congr_arg _ <| (Set.image_mul (homogeneousSubmonoid ğ’œ).subtype).symmâŸ©

instance : Mul (HomogeneousIdeal ğ’œ) where
  mul I J := âŸ¨I.toIdeal * J.toIdeal, I.isHomogeneous.mul J.isHomogeneousâŸ©

@[simp]
theorem HomogeneousIdeal.toIdeal_mul (I J : HomogeneousIdeal ğ’œ) :
    (I * J).toIdeal = I.toIdeal * J.toIdeal :=
  rfl

end CommSemiring

end Operations

/-! ### Homogeneous core

Note that many results about the homogeneous core came earlier in this file, as they are helpful
for building the lattice structure. -/


section homogeneousCore

open HomogeneousIdeal

variable [Semiring A] [DecidableEq Î¹] [AddMonoid Î¹]
variable [SetLike Ïƒ A] [AddSubmonoidClass Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ) [GradedRing ğ’œ]
variable (I : Ideal A)

theorem Ideal.homogeneousCore.gc : GaloisConnection toIdeal (Ideal.homogeneousCore ğ’œ) :=
  Submodule.homogeneousCore.gc ğ’œ ğ’œ

/-- `toIdeal : HomogeneousIdeal ğ’œ â†’ Ideal A` and `Ideal.homogeneousCore ğ’œ` forms a galois
coinsertion. -/
def Ideal.homogeneousCore.gi : GaloisCoinsertion toIdeal (Ideal.homogeneousCore ğ’œ) :=
  Submodule.homogeneousCore.gi ğ’œ ğ’œ

theorem Ideal.homogeneousCore_eq_sSup :
    I.homogeneousCore ğ’œ = sSup { J : HomogeneousIdeal ğ’œ | J.toIdeal â‰¤ I } :=
  Eq.symm <| IsLUB.sSup_eq <| (Ideal.homogeneousCore.gc ğ’œ).isGreatest_u.isLUB

theorem Ideal.toIdeal_homogeneousCore_eq_sSup :
    (I.homogeneousCore ğ’œ).toIdeal = sSup { J : Ideal A | J.IsHomogeneous ğ’œ âˆ§ J â‰¤ I } :=
  Submodule.toSubmodule_homogeneousCore_eq_sSup ğ’œ ğ’œ I

end homogeneousCore

/-! ### Homogeneous hulls -/

section HomogeneousHull

open HomogeneousIdeal

variable [Semiring A] [DecidableEq Î¹] [AddMonoid Î¹]
variable [SetLike Ïƒ A] [AddSubmonoidClass Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ) [GradedRing ğ’œ]
variable (I : Ideal A)

/-- For any `I : Ideal A`, not necessarily homogeneous, `I.homogeneousHull ğ’œ` is
the smallest homogeneous ideal containing `I`. -/
abbrev Ideal.homogeneousHull : HomogeneousIdeal ğ’œ :=
  Submodule.homogeneousHull ğ’œ ğ’œ I

theorem Ideal.le_toIdeal_homogeneousHull : I â‰¤ (Ideal.homogeneousHull ğ’œ I).toIdeal :=
  Submodule.le_toSubmodule_homogeneousHull ğ’œ ğ’œ I

theorem Ideal.homogeneousHull_mono : Monotone (Ideal.homogeneousHull ğ’œ) :=
  Submodule.homogeneousHull_mono ğ’œ ğ’œ

variable {I ğ’œ}

theorem Ideal.IsHomogeneous.toIdeal_homogeneousHull_eq_self (h : I.IsHomogeneous ğ’œ) :
    (Ideal.homogeneousHull ğ’œ I).toIdeal = I :=
  Submodule.IsHomogeneous.toSubmodule_homogeneousHull_eq_self ğ’œ ğ’œ h

@[simp high]
theorem HomogeneousIdeal.homogeneousHull_toIdeal_eq_self (I : HomogeneousIdeal ğ’œ) :
    I.toIdeal.homogeneousHull ğ’œ = I :=
  HomogeneousIdeal.toIdeal_injective <| I.isHomogeneous.toIdeal_homogeneousHull_eq_self

variable (I ğ’œ)

theorem Ideal.toIdeal_homogeneousHull_eq_iSup :
    (I.homogeneousHull ğ’œ).toIdeal = â¨† i, Ideal.span (GradedRing.proj ğ’œ i '' I) :=
  Submodule.toSubmodule_homogeneousHull_eq_iSup ğ’œ ğ’œ I

theorem Ideal.homogeneousHull_eq_iSup :
    I.homogeneousHull ğ’œ =
      â¨† i, âŸ¨Ideal.span (GradedRing.proj ğ’œ i '' I), Ideal.homogeneous_span ğ’œ _ (by
        rintro _ âŸ¨x, -, rflâŸ©
        apply SetLike.isHomogeneousElem_coe)âŸ© :=
  Submodule.homogeneousHull_eq_iSup ğ’œ ğ’œ I

end HomogeneousHull

section GaloisConnection

open HomogeneousIdeal

variable [Semiring A] [DecidableEq Î¹] [AddMonoid Î¹]
variable [SetLike Ïƒ A] [AddSubmonoidClass Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ) [GradedRing ğ’œ]

theorem Ideal.homogeneousHull.gc : GaloisConnection (Ideal.homogeneousHull ğ’œ) toIdeal :=
  Submodule.homogeneousHull.gc ğ’œ ğ’œ

/-- `Ideal.homogeneousHull ğ’œ` and `toIdeal : HomogeneousIdeal ğ’œ â†’ Ideal A` form a galois
insertion. -/
def Ideal.homogeneousHull.gi : GaloisInsertion (Ideal.homogeneousHull ğ’œ) toIdeal :=
  Submodule.homogeneousHull.gi ğ’œ ğ’œ

theorem Ideal.homogeneousHull_eq_sInf (I : Ideal A) :
    Ideal.homogeneousHull ğ’œ I = sInf { J : HomogeneousIdeal ğ’œ | I â‰¤ J.toIdeal } :=
  Eq.symm <| IsGLB.sInf_eq <| (Ideal.homogeneousHull.gc ğ’œ).isLeast_l.isGLB

end GaloisConnection

section IrrelevantIdeal

variable [Semiring A]
variable [DecidableEq Î¹]
variable [AddCommMonoid Î¹] [PartialOrder Î¹] [CanonicallyOrderedAdd Î¹]
variable [SetLike Ïƒ A] [AddSubmonoidClass Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ) [GradedRing ğ’œ]

open GradedRing SetLike.GradedMonoid DirectSum

/-- For a graded ring `â¨áµ¢ ğ’œáµ¢` graded by a `CanonicallyOrderedAddCommMonoid Î¹`, the irrelevant ideal
refers to `â¨_{i>0} ğ’œáµ¢`, or equivalently `{a | aâ‚€ = 0}`. This definition is used in `Proj`
construction where `Î¹` is always `â„•` so the irrelevant ideal is simply elements with `0` as
0-th coordinate.
-/
def HomogeneousIdeal.irrelevant : HomogeneousIdeal ğ’œ :=
  âŸ¨RingHom.ker (GradedRing.projZeroRingHom ğ’œ), fun i r (hr : (decompose ğ’œ r 0 : A) = 0) => by
    change (decompose ğ’œ (decompose ğ’œ r _ : A) 0 : A) = 0
    by_cases h : i = 0
    Â· rw [h, hr, decompose_zero, zero_apply, ZeroMemClass.coe_zero]
    Â· rw [decompose_of_mem_ne ğ’œ (SetLike.coe_mem _) h]âŸ©

@[simp]
theorem HomogeneousIdeal.mem_irrelevant_iff (a : A) :
    a âˆˆ HomogeneousIdeal.irrelevant ğ’œ â†” proj ğ’œ 0 a = 0 :=
  Iff.rfl

@[simp]
theorem HomogeneousIdeal.toIdeal_irrelevant :
    (HomogeneousIdeal.irrelevant ğ’œ).toIdeal = RingHom.ker (GradedRing.projZeroRingHom ğ’œ) :=
  rfl

end IrrelevantIdeal
