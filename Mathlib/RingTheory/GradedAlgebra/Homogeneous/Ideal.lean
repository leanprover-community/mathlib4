/-
Copyright (c) 2021 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Eric Wieser
-/
import Mathlib.LinearAlgebra.Finsupp.SumProd
import Mathlib.RingTheory.GradedAlgebra.Basic
import Mathlib.RingTheory.Ideal.Basic
import Mathlib.RingTheory.Ideal.BigOperators
import Mathlib.RingTheory.Ideal.Maps
import Mathlib.RingTheory.GradedAlgebra.Homogeneous.Submodule

/-!
# Homogeneous ideals of a graded algebra

This file defines homogeneous ideals of `GradedRing ğ’œ` where `ğ’œ : Î¹ â†’ Submodule R A` and
operations on them.

## Main definitions

For any `I : Ideal A`:
* `Ideal.IsHomogeneous ğ’œ I`: The property that an ideal is closed under `GradedRing.proj`.
* `HomogeneousIdeal ğ’œ`: The structure extending ideals which satisfy `Ideal.IsHomogeneous`.
* `Ideal.homogeneousCore I ğ’œ`: The largest homogeneous ideal smaller than `I`.
* `Ideal.homogeneousHull I ğ’œ`: The smallest homogeneous ideal larger than `I`.

## Main statements

* `HomogeneousIdeal.completeLattice`: `Ideal.IsHomogeneous` is preserved by `âŠ¥`, `âŠ¤`, `âŠ”`, `âŠ“`,
  `â¨†`, `â¨…`, and so the subtype of homogeneous ideals inherits a complete lattice structure.
* `Ideal.homogeneousCore.gi`: `Ideal.homogeneousCore` forms a galois insertion with coercion.
* `Ideal.homogeneousHull.gi`: `Ideal.homogeneousHull` forms a galois insertion with coercion.

## Implementation notes

We introduce `Ideal.homogeneousCore'` earlier than might be expected so that we can get access
to `Ideal.IsHomogeneous.iff_exists` as quickly as possible.

## Tags

graded algebra, homogeneous
-/


open SetLike DirectSum Set

open Pointwise DirectSum

variable {Î¹ Ïƒ A : Type*}

section HomogeneousDef

variable [Semiring A]
variable [SetLike Ïƒ A] [AddSubmonoidClass Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ)
variable [DecidableEq Î¹] [AddMonoid Î¹] [GradedRing ğ’œ]
variable (I : Ideal A)

/-- An `I : Ideal A` is homogeneous if for every `r âˆˆ I`, all homogeneous components
  of `r` are in `I`. -/
abbrev Ideal.IsHomogeneous : Prop := Submodule.IsHomogeneous I ğ’œ

theorem Ideal.IsHomogeneous.mem_iff {I} (hI : Ideal.IsHomogeneous ğ’œ I) {x} :
    x âˆˆ I â†” âˆ€ i, (decompose ğ’œ x i : A) âˆˆ I :=
  AddSubmonoidClass.IsHomogeneous.mem_iff ğ’œ _ hI

/-- For any `Semiring A`, we collect the homogeneous ideals of `A` into a type. -/
abbrev HomogeneousIdeal := HomogeneousSubmodule ğ’œ ğ’œ

variable {ğ’œ}

/-- Converting a homogeneous ideal to an ideal. -/
abbrev HomogeneousIdeal.toIdeal (I : HomogeneousIdeal ğ’œ) : Ideal A :=
  I.toSubmodule

theorem HomogeneousIdeal.isHomogeneous (I : HomogeneousIdeal ğ’œ) :
    I.toIdeal.IsHomogeneous ğ’œ := I.is_homogeneous'

theorem HomogeneousIdeal.toIdeal_injective :
    Function.Injective (HomogeneousIdeal.toIdeal : HomogeneousIdeal ğ’œ â†’ Ideal A) :=
  HomogeneousSubmodule.toSubmodule_injective ğ’œ ğ’œ

instance HomogeneousIdeal.setLike : SetLike (HomogeneousIdeal ğ’œ) A :=
  HomogeneousSubmodule.setLike ğ’œ ğ’œ

@[ext]
theorem HomogeneousIdeal.ext {I J : HomogeneousIdeal ğ’œ} (h : I.toIdeal = J.toIdeal) : I = J :=
  HomogeneousIdeal.toIdeal_injective h

theorem HomogeneousIdeal.ext' {I J : HomogeneousIdeal ğ’œ} (h : âˆ€ i, âˆ€ x âˆˆ ğ’œ i, x âˆˆ I â†” x âˆˆ J) :
    I = J := HomogeneousSubmodule.ext' ğ’œ ğ’œ h

@[simp high]
theorem HomogeneousIdeal.mem_iff {I : HomogeneousIdeal ğ’œ} {x : A} : x âˆˆ I.toIdeal â†” x âˆˆ I :=
  Iff.rfl

end HomogeneousDef

section HomogeneousCore

variable [Semiring A]
variable [SetLike Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ)
variable (I : Ideal A)

/-- For any `I : Ideal A`, not necessarily homogeneous, `I.homogeneousCore' ğ’œ`
is the largest homogeneous ideal of `A` contained in `I`, as an ideal. -/
def Ideal.homogeneousCore' (I : Ideal A) : Ideal A :=
  Ideal.span ((â†‘) '' (((â†‘) : Subtype (SetLike.IsHomogeneousElem ğ’œ) â†’ A) â»Â¹' I))

theorem Ideal.homogeneousCore'_mono : Monotone (Ideal.homogeneousCore' ğ’œ) :=
  fun _ _ I_le_J => Ideal.span_mono <| Set.image_mono fun _ => @I_le_J _

theorem Ideal.homogeneousCore'_le : I.homogeneousCore' ğ’œ â‰¤ I :=
  Ideal.span_le.2 <| image_preimage_subset _ _

end HomogeneousCore

section IsHomogeneousIdealDefs

variable [Semiring A]
variable [SetLike Ïƒ A] [AddSubmonoidClass Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ)
variable [DecidableEq Î¹] [AddMonoid Î¹] [GradedRing ğ’œ]
variable (I : Ideal A)

theorem Ideal.isHomogeneous_iff_forall_subset :
    I.IsHomogeneous ğ’œ â†” âˆ€ i, (I : Set A) âŠ† GradedRing.proj ğ’œ i â»Â¹' I :=
  Iff.rfl

theorem Ideal.isHomogeneous_iff_subset_iInter :
    I.IsHomogeneous ğ’œ â†” (I : Set A) âŠ† â‹‚ i, GradedRing.proj ğ’œ i â»Â¹' â†‘I :=
  subset_iInter_iff.symm

theorem Ideal.mul_homogeneous_element_mem_of_mem
    {I : Ideal A} (r x : A) (hxâ‚ : SetLike.IsHomogeneousElem ğ’œ x)
    (hxâ‚‚ : x âˆˆ I) (j : Î¹) : GradedRing.proj ğ’œ j (r * x) âˆˆ I := by
  classical
  rw [â† DirectSum.sum_support_decompose ğ’œ r, Finset.sum_mul, map_sum]
  apply Ideal.sum_mem
  intro k _
  obtain âŸ¨i, hiâŸ© := hxâ‚
  have memâ‚ : (DirectSum.decompose ğ’œ r k : A) * x âˆˆ ğ’œ (k + i) :=
    GradedMul.mul_mem (SetLike.coe_mem _) hi
  rw [GradedRing.proj_apply, DirectSum.decompose_of_mem ğ’œ memâ‚, coe_of_apply]
  split_ifs
  Â· exact I.mul_mem_left _ hxâ‚‚
  Â· exact I.zero_mem

theorem Ideal.homogeneous_span (s : Set A) (h : âˆ€ x âˆˆ s, SetLike.IsHomogeneousElem ğ’œ x) :
    (Ideal.span s).IsHomogeneous ğ’œ := by
  rintro i r hr
  rw [Ideal.span, Finsupp.span_eq_range_linearCombination] at hr
  rw [LinearMap.mem_range] at hr
  obtain âŸ¨s, rflâŸ© := hr
  rw [Finsupp.linearCombination_apply, Finsupp.sum, decompose_sum, DFinsupp.finset_sum_apply,
    AddSubmonoidClass.coe_finset_sum]
  refine Ideal.sum_mem _ ?_
  rintro z hz1
  rw [smul_eq_mul]
  refine Ideal.mul_homogeneous_element_mem_of_mem ğ’œ (s z) z ?_ ?_ i
  Â· rcases z with âŸ¨z, hz2âŸ©
    apply h _ hz2
  Â· exact Ideal.subset_span z.2

/-- For any `I : Ideal A`, not necessarily homogeneous, `I.homogeneousCore' ğ’œ`
is the largest homogeneous ideal of `A` contained in `I`. -/
def Ideal.homogeneousCore : HomogeneousIdeal ğ’œ :=
  âŸ¨Ideal.homogeneousCore' ğ’œ I,
    Ideal.homogeneous_span _ _ fun _ h => by
      have := Subtype.image_preimage_coe (setOf (SetLike.IsHomogeneousElem ğ’œ)) (I : Set A)
      exact (cast congr(_ âˆˆ $this) h).1âŸ©

theorem Ideal.homogeneousCore_mono : Monotone (Ideal.homogeneousCore ğ’œ) :=
  Ideal.homogeneousCore'_mono ğ’œ

theorem Ideal.toIdeal_homogeneousCore_le : (I.homogeneousCore ğ’œ).toIdeal â‰¤ I :=
  Ideal.homogeneousCore'_le ğ’œ I

variable {ğ’œ I}

theorem Ideal.mem_homogeneousCore_of_homogeneous_of_mem {x : A} (h : SetLike.IsHomogeneousElem ğ’œ x)
    (hmem : x âˆˆ I) : x âˆˆ I.homogeneousCore ğ’œ :=
  Ideal.subset_span âŸ¨âŸ¨x, hâŸ©, hmem, rflâŸ©

theorem Ideal.IsHomogeneous.toIdeal_homogeneousCore_eq_self (h : I.IsHomogeneous ğ’œ) :
    (I.homogeneousCore ğ’œ).toIdeal = I := by
  apply le_antisymm (I.homogeneousCore'_le ğ’œ) _
  intro x hx
  classical
  rw [â† DirectSum.sum_support_decompose ğ’œ x]
  exact Ideal.sum_mem _ fun j _ => Ideal.subset_span âŸ¨âŸ¨_, isHomogeneousElem_coe _âŸ©, h _ hx, rflâŸ©

@[simp]
theorem HomogeneousIdeal.toIdeal_homogeneousCore_eq_self (I : HomogeneousIdeal ğ’œ) :
    I.toIdeal.homogeneousCore ğ’œ = I := by
  ext1
  convert Ideal.IsHomogeneous.toIdeal_homogeneousCore_eq_self I.isHomogeneous

variable (ğ’œ I)

theorem Ideal.IsHomogeneous.iff_eq : I.IsHomogeneous ğ’œ â†” (I.homogeneousCore ğ’œ).toIdeal = I :=
  âŸ¨fun hI => hI.toIdeal_homogeneousCore_eq_self, fun hI => hI â–¸ (Ideal.homogeneousCore ğ’œ I).2âŸ©

theorem Ideal.IsHomogeneous.iff_exists :
    I.IsHomogeneous ğ’œ â†” âˆƒ S : Set (homogeneousSubmonoid ğ’œ), I = Ideal.span ((â†‘) '' S) := by
  rw [Ideal.IsHomogeneous.iff_eq, eq_comm]
  exact ((Set.image_preimage.compose (Submodule.gi _ _).gc).exists_eq_l _).symm

end IsHomogeneousIdealDefs

/-! ### Operations

In this section, we show that `Ideal.IsHomogeneous` is preserved by various notations, then use
these results to provide these notation typeclasses for `HomogeneousIdeal`. -/


section Operations

section Semiring

variable [Semiring A] [DecidableEq Î¹] [AddMonoid Î¹]
variable [SetLike Ïƒ A] [AddSubmonoidClass Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ) [GradedRing ğ’œ]

namespace Ideal.IsHomogeneous

theorem bot : Ideal.IsHomogeneous ğ’œ âŠ¥ := fun i r hr => by
  simp only [Ideal.mem_bot] at hr
  rw [hr, decompose_zero, zero_apply]
  apply Ideal.zero_mem

theorem top : Ideal.IsHomogeneous ğ’œ âŠ¤ := fun i r _ => by simp only [Submodule.mem_top]

variable {ğ’œ}

theorem inf {I J : Ideal A} (HI : I.IsHomogeneous ğ’œ) (HJ : J.IsHomogeneous ğ’œ) :
    (I âŠ“ J).IsHomogeneous ğ’œ :=
  fun _ _ hr => âŸ¨HI _ hr.1, HJ _ hr.2âŸ©

theorem sup {I J : Ideal A} (HI : I.IsHomogeneous ğ’œ) (HJ : J.IsHomogeneous ğ’œ) :
    (I âŠ” J).IsHomogeneous ğ’œ := by
  rw [iff_exists] at HI HJ âŠ¢
  obtain âŸ¨âŸ¨sâ‚, rflâŸ©, âŸ¨sâ‚‚, rflâŸ©âŸ© := HI, HJ
  refine âŸ¨sâ‚ âˆª sâ‚‚, ?_âŸ©
  rw [Set.image_union]
  exact (Submodule.span_union _ _).symm

protected theorem iSup {Îº : Sort*} {f : Îº â†’ Ideal A} (h : âˆ€ i, (f i).IsHomogeneous ğ’œ) :
    (â¨† i, f i).IsHomogeneous ğ’œ := by
  simp_rw [iff_exists] at h âŠ¢
  choose s hs using h
  refine âŸ¨â‹ƒ i, s i, ?_âŸ©
  simp_rw [Set.image_iUnion, Ideal.span_iUnion]
  congr
  exact funext hs

protected theorem iInf {Îº : Sort*} {f : Îº â†’ Ideal A} (h : âˆ€ i, (f i).IsHomogeneous ğ’œ) :
    (â¨… i, f i).IsHomogeneous ğ’œ := by
  intro i x hx
  simp only [Ideal.mem_iInf] at hx âŠ¢
  exact fun j => h _ _ (hx j)

theorem iSupâ‚‚ {Îº : Sort*} {Îº' : Îº â†’ Sort*} {f : âˆ€ i, Îº' i â†’ Ideal A}
    (h : âˆ€ i j, (f i j).IsHomogeneous ğ’œ) : (â¨† (i) (j), f i j).IsHomogeneous ğ’œ :=
  IsHomogeneous.iSup fun i => IsHomogeneous.iSup <| h i

theorem iInfâ‚‚ {Îº : Sort*} {Îº' : Îº â†’ Sort*} {f : âˆ€ i, Îº' i â†’ Ideal A}
    (h : âˆ€ i j, (f i j).IsHomogeneous ğ’œ) : (â¨… (i) (j), f i j).IsHomogeneous ğ’œ :=
  IsHomogeneous.iInf fun i => IsHomogeneous.iInf <| h i

theorem sSup {â„ : Set (Ideal A)} (h : âˆ€ I âˆˆ â„, Ideal.IsHomogeneous ğ’œ I) :
    (sSup â„).IsHomogeneous ğ’œ := by
  rw [sSup_eq_iSup]
  exact iSupâ‚‚ h

theorem sInf {â„ : Set (Ideal A)} (h : âˆ€ I âˆˆ â„, Ideal.IsHomogeneous ğ’œ I) :
    (sInf â„).IsHomogeneous ğ’œ := by
  rw [sInf_eq_iInf]
  exact iInfâ‚‚ h

end Ideal.IsHomogeneous

variable {ğ’œ}

namespace HomogeneousIdeal

instance : PartialOrder (HomogeneousIdeal ğ’œ) :=
  SetLike.instPartialOrder

instance : Top (HomogeneousIdeal ğ’œ) :=
  âŸ¨âŸ¨âŠ¤, Ideal.IsHomogeneous.top ğ’œâŸ©âŸ©

instance : Bot (HomogeneousIdeal ğ’œ) :=
  âŸ¨âŸ¨âŠ¥, Ideal.IsHomogeneous.bot ğ’œâŸ©âŸ©

instance : Max (HomogeneousIdeal ğ’œ) :=
  âŸ¨fun I J => âŸ¨_, I.isHomogeneous.sup J.isHomogeneousâŸ©âŸ©

instance : Min (HomogeneousIdeal ğ’œ) :=
  âŸ¨fun I J => âŸ¨_, I.isHomogeneous.inf J.isHomogeneousâŸ©âŸ©

instance : SupSet (HomogeneousIdeal ğ’œ) :=
  âŸ¨fun S => âŸ¨â¨† s âˆˆ S, toIdeal s, Ideal.IsHomogeneous.iSupâ‚‚ fun s _ => s.isHomogeneousâŸ©âŸ©

instance : InfSet (HomogeneousIdeal ğ’œ) :=
  âŸ¨fun S => âŸ¨â¨… s âˆˆ S, toIdeal s, Ideal.IsHomogeneous.iInfâ‚‚ fun s _ => s.isHomogeneousâŸ©âŸ©

@[simp]
theorem coe_top : ((âŠ¤ : HomogeneousIdeal ğ’œ) : Set A) = univ :=
  rfl

@[simp]
theorem coe_bot : ((âŠ¥ : HomogeneousIdeal ğ’œ) : Set A) = 0 :=
  rfl

@[simp]
theorem coe_sup (I J : HomogeneousIdeal ğ’œ) : â†‘(I âŠ” J) = (I + J : Set A) :=
  Submodule.coe_sup _ _

@[simp]
theorem coe_inf (I J : HomogeneousIdeal ğ’œ) : (â†‘(I âŠ“ J) : Set A) = â†‘I âˆ© â†‘J :=
  rfl

@[simp]
theorem toIdeal_top : (âŠ¤ : HomogeneousIdeal ğ’œ).toIdeal = (âŠ¤ : Ideal A) :=
  rfl

@[simp]
theorem toIdeal_bot : (âŠ¥ : HomogeneousIdeal ğ’œ).toIdeal = (âŠ¥ : Ideal A) :=
  rfl

@[simp]
theorem toIdeal_sup (I J : HomogeneousIdeal ğ’œ) : (I âŠ” J).toIdeal = I.toIdeal âŠ” J.toIdeal :=
  rfl

@[simp]
theorem toIdeal_inf (I J : HomogeneousIdeal ğ’œ) : (I âŠ“ J).toIdeal = I.toIdeal âŠ“ J.toIdeal :=
  rfl

@[simp]
theorem toIdeal_sSup (â„ : Set (HomogeneousIdeal ğ’œ)) : (sSup â„).toIdeal = â¨† s âˆˆ â„, toIdeal s :=
  rfl

@[simp]
theorem toIdeal_sInf (â„ : Set (HomogeneousIdeal ğ’œ)) : (sInf â„).toIdeal = â¨… s âˆˆ â„, toIdeal s :=
  rfl

@[simp]
theorem toIdeal_iSup {Îº : Sort*} (s : Îº â†’ HomogeneousIdeal ğ’œ) :
    (â¨† i, s i).toIdeal = â¨† i, (s i).toIdeal := by
  rw [iSup, toIdeal_sSup, iSup_range]

@[simp]
theorem toIdeal_iInf {Îº : Sort*} (s : Îº â†’ HomogeneousIdeal ğ’œ) :
    (â¨… i, s i).toIdeal = â¨… i, (s i).toIdeal := by
  rw [iInf, toIdeal_sInf, iInf_range]

theorem toIdeal_iSupâ‚‚ {Îº : Sort*} {Îº' : Îº â†’ Sort*} (s : âˆ€ i, Îº' i â†’ HomogeneousIdeal ğ’œ) :
    (â¨† (i) (j), s i j).toIdeal = â¨† (i) (j), (s i j).toIdeal := by
  simp_rw [toIdeal_iSup]

theorem toIdeal_iInfâ‚‚ {Îº : Sort*} {Îº' : Îº â†’ Sort*} (s : âˆ€ i, Îº' i â†’ HomogeneousIdeal ğ’œ) :
    (â¨… (i) (j), s i j).toIdeal = â¨… (i) (j), (s i j).toIdeal := by
  simp_rw [toIdeal_iInf]

@[simp]
theorem eq_top_iff (I : HomogeneousIdeal ğ’œ) : I = âŠ¤ â†” I.toIdeal = âŠ¤ :=
  toIdeal_injective.eq_iff.symm

@[simp]
theorem eq_bot_iff (I : HomogeneousIdeal ğ’œ) : I = âŠ¥ â†” I.toIdeal = âŠ¥ :=
  toIdeal_injective.eq_iff.symm

instance completeLattice : CompleteLattice (HomogeneousIdeal ğ’œ) :=
  toIdeal_injective.completeLattice _ toIdeal_sup toIdeal_inf toIdeal_sSup toIdeal_sInf toIdeal_top
    toIdeal_bot

instance : Add (HomogeneousIdeal ğ’œ) :=
  âŸ¨(Â· âŠ” Â·)âŸ©

@[simp]
theorem toIdeal_add (I J : HomogeneousIdeal ğ’œ) : (I + J).toIdeal = I.toIdeal + J.toIdeal :=
  rfl

instance : Inhabited (HomogeneousIdeal ğ’œ) where default := âŠ¥

end HomogeneousIdeal

end Semiring

section CommSemiring

variable [CommSemiring A]
variable [DecidableEq Î¹] [AddMonoid Î¹]
variable [SetLike Ïƒ A] [AddSubmonoidClass Ïƒ A] {ğ’œ : Î¹ â†’ Ïƒ} [GradedRing ğ’œ]
variable (I : Ideal A)

theorem Ideal.IsHomogeneous.mul {I J : Ideal A} (HI : I.IsHomogeneous ğ’œ) (HJ : J.IsHomogeneous ğ’œ) :
    (I * J).IsHomogeneous ğ’œ := by
  rw [Ideal.IsHomogeneous.iff_exists] at HI HJ âŠ¢
  obtain âŸ¨âŸ¨sâ‚, rflâŸ©, âŸ¨sâ‚‚, rflâŸ©âŸ© := HI, HJ
  rw [Ideal.span_mul_span']
  exact âŸ¨sâ‚ * sâ‚‚, congr_arg _ <| (Set.image_mul (homogeneousSubmonoid ğ’œ).subtype).symmâŸ©

instance : Mul (HomogeneousIdeal ğ’œ) where
  mul I J := âŸ¨I.toIdeal * J.toIdeal, I.isHomogeneous.mul J.isHomogeneousâŸ©

@[simp]
theorem HomogeneousIdeal.toIdeal_mul (I J : HomogeneousIdeal ğ’œ) :
    (I * J).toIdeal = I.toIdeal * J.toIdeal :=
  rfl

end CommSemiring

end Operations

/-! ### Homogeneous core

Note that many results about the homogeneous core came earlier in this file, as they are helpful
for building the lattice structure. -/


section homogeneousCore

open HomogeneousIdeal

variable [Semiring A] [DecidableEq Î¹] [AddMonoid Î¹]
variable [SetLike Ïƒ A] [AddSubmonoidClass Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ) [GradedRing ğ’œ]
variable (I : Ideal A)

theorem Ideal.homogeneousCore.gc : GaloisConnection toIdeal (Ideal.homogeneousCore ğ’œ) := fun I _ =>
  âŸ¨fun H => I.toIdeal_homogeneousCore_eq_self â–¸ Ideal.homogeneousCore_mono ğ’œ H,
    fun H => le_trans H (Ideal.homogeneousCore'_le _ _)âŸ©

/-- `toIdeal : HomogeneousIdeal ğ’œ â†’ Ideal A` and `Ideal.homogeneousCore ğ’œ` forms a galois
coinsertion. -/
def Ideal.homogeneousCore.gi : GaloisCoinsertion toIdeal (Ideal.homogeneousCore ğ’œ) where
  choice I HI :=
    âŸ¨I, le_antisymm (I.toIdeal_homogeneousCore_le ğ’œ) HI â–¸ HomogeneousIdeal.isHomogeneous _âŸ©
  gc := Ideal.homogeneousCore.gc ğ’œ
  u_l_le _ := Ideal.homogeneousCore'_le _ _
  choice_eq I H := le_antisymm H (I.toIdeal_homogeneousCore_le _)

theorem Ideal.homogeneousCore_eq_sSup :
    I.homogeneousCore ğ’œ = sSup { J : HomogeneousIdeal ğ’œ | J.toIdeal â‰¤ I } :=
  Eq.symm <| IsLUB.sSup_eq <| (Ideal.homogeneousCore.gc ğ’œ).isGreatest_u.isLUB

theorem Ideal.homogeneousCore'_eq_sSup :
    I.homogeneousCore' ğ’œ = sSup { J : Ideal A | J.IsHomogeneous ğ’œ âˆ§ J â‰¤ I } := by
  refine (IsLUB.sSup_eq ?_).symm
  apply IsGreatest.isLUB
  have coe_mono : Monotone (toIdeal : HomogeneousIdeal ğ’œ â†’ Ideal A) := fun x y => id
  convert coe_mono.map_isGreatest (Ideal.homogeneousCore.gc ğ’œ).isGreatest_u using 1
  ext x
  rw [mem_image, mem_setOf_eq]
  refine âŸ¨fun hI => âŸ¨âŸ¨x, hI.1âŸ©, âŸ¨hI.2, rflâŸ©âŸ©, ?_âŸ©
  rintro âŸ¨x, âŸ¨hx, rflâŸ©âŸ©
  exact âŸ¨x.isHomogeneous, hxâŸ©

end homogeneousCore

/-! ### Homogeneous hulls -/


section HomogeneousHull

open HomogeneousIdeal

variable [Semiring A] [DecidableEq Î¹] [AddMonoid Î¹]
variable [SetLike Ïƒ A] [AddSubmonoidClass Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ) [GradedRing ğ’œ]
variable (I : Ideal A)

/-- For any `I : Ideal A`, not necessarily homogeneous, `I.homogeneousHull ğ’œ` is
the smallest homogeneous ideal containing `I`. -/
def Ideal.homogeneousHull : HomogeneousIdeal ğ’œ :=
  âŸ¨Ideal.span { r : A | âˆƒ (i : Î¹) (x : I), (DirectSum.decompose ğ’œ (x : A) i : A) = r }, by
    refine Ideal.homogeneous_span _ _ fun x hx => ?_
    obtain âŸ¨i, x, rflâŸ© := hx
    apply SetLike.isHomogeneousElem_coeâŸ©

theorem Ideal.le_toIdeal_homogeneousHull : I â‰¤ (Ideal.homogeneousHull ğ’œ I).toIdeal := by
  intro r hr
  classical
  rw [â† DirectSum.sum_support_decompose ğ’œ r]
  refine Ideal.sum_mem _ ?_
  intro j _
  apply Ideal.subset_span
  use j
  use âŸ¨r, hrâŸ©

theorem Ideal.homogeneousHull_mono : Monotone (Ideal.homogeneousHull ğ’œ) := fun I J I_le_J => by
  apply Ideal.span_mono
  rintro r âŸ¨hr1, âŸ¨x, hxâŸ©, rflâŸ©
  exact âŸ¨hr1, âŸ¨âŸ¨x, I_le_J hxâŸ©, rflâŸ©âŸ©

variable {I ğ’œ}

theorem Ideal.IsHomogeneous.toIdeal_homogeneousHull_eq_self (h : I.IsHomogeneous ğ’œ) :
    (Ideal.homogeneousHull ğ’œ I).toIdeal = I := by
  apply le_antisymm _ (Ideal.le_toIdeal_homogeneousHull _ _)
  apply Ideal.span_le.2
  rintro _ âŸ¨i, x, rflâŸ©
  exact h _ x.prop

@[simp]
theorem HomogeneousIdeal.homogeneousHull_toIdeal_eq_self (I : HomogeneousIdeal ğ’œ) :
    I.toIdeal.homogeneousHull ğ’œ = I :=
  HomogeneousIdeal.toIdeal_injective <| I.isHomogeneous.toIdeal_homogeneousHull_eq_self

variable (I ğ’œ)

theorem Ideal.toIdeal_homogeneousHull_eq_iSup :
    (I.homogeneousHull ğ’œ).toIdeal = â¨† i, Ideal.span (GradedRing.proj ğ’œ i '' I) := by
  rw [â† Ideal.span_iUnion]
  apply congr_arg Ideal.span _
  ext1
  simp only [Set.mem_iUnion, Set.mem_image, mem_setOf_eq, GradedRing.proj_apply, SetLike.exists,
    exists_prop, SetLike.mem_coe]

theorem Ideal.homogeneousHull_eq_iSup :
    I.homogeneousHull ğ’œ =
      â¨† i, âŸ¨Ideal.span (GradedRing.proj ğ’œ i '' I), Ideal.homogeneous_span ğ’œ _ (by
        rintro _ âŸ¨x, -, rflâŸ©
        apply SetLike.isHomogeneousElem_coe)âŸ© := by
  ext1
  rw [Ideal.toIdeal_homogeneousHull_eq_iSup, toIdeal_iSup]

end HomogeneousHull

section GaloisConnection

open HomogeneousIdeal

variable [Semiring A] [DecidableEq Î¹] [AddMonoid Î¹]
variable [SetLike Ïƒ A] [AddSubmonoidClass Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ) [GradedRing ğ’œ]

theorem Ideal.homogeneousHull.gc : GaloisConnection (Ideal.homogeneousHull ğ’œ) toIdeal := fun _ J =>
  âŸ¨le_trans (Ideal.le_toIdeal_homogeneousHull _ _),
    fun H => J.homogeneousHull_toIdeal_eq_self â–¸ Ideal.homogeneousHull_mono ğ’œ HâŸ©

/-- `Ideal.homogeneousHull ğ’œ` and `toIdeal : HomogeneousIdeal ğ’œ â†’ Ideal A` form a galois
insertion. -/
def Ideal.homogeneousHull.gi : GaloisInsertion (Ideal.homogeneousHull ğ’œ) toIdeal where
  choice I H := âŸ¨I, le_antisymm H (I.le_toIdeal_homogeneousHull ğ’œ) â–¸ isHomogeneous _âŸ©
  gc := Ideal.homogeneousHull.gc ğ’œ
  le_l_u _ := Ideal.le_toIdeal_homogeneousHull _ _
  choice_eq I H := le_antisymm (I.le_toIdeal_homogeneousHull ğ’œ) H

theorem Ideal.homogeneousHull_eq_sInf (I : Ideal A) :
    Ideal.homogeneousHull ğ’œ I = sInf { J : HomogeneousIdeal ğ’œ | I â‰¤ J.toIdeal } :=
  Eq.symm <| IsGLB.sInf_eq <| (Ideal.homogeneousHull.gc ğ’œ).isLeast_l.isGLB

end GaloisConnection

section IrrelevantIdeal

variable [Semiring A]
variable [DecidableEq Î¹]
variable [AddCommMonoid Î¹] [PartialOrder Î¹] [CanonicallyOrderedAdd Î¹]
variable [SetLike Ïƒ A] [AddSubmonoidClass Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ) [GradedRing ğ’œ]

open GradedRing SetLike.GradedMonoid DirectSum

/-- For a graded ring `â¨áµ¢ ğ’œáµ¢` graded by a `CanonicallyOrderedAddCommMonoid Î¹`, the irrelevant ideal
refers to `â¨_{i>0} ğ’œáµ¢`, or equivalently `{a | aâ‚€ = 0}`. This definition is used in `Proj`
construction where `Î¹` is always `â„•` so the irrelevant ideal is simply elements with `0` as
0-th coordinate.

# Future work
Here in the definition, `Î¹` is assumed to be `CanonicallyOrderedAddCommMonoid`. However, the notion
of irrelevant ideal makes sense in a more general setting by defining it as the ideal of elements
with `0` as i-th coordinate for all `i â‰¤ 0`, i.e. `{a | âˆ€ (i : Î¹), i â‰¤ 0 â†’ aáµ¢ = 0}`.
-/
def HomogeneousIdeal.irrelevant : HomogeneousIdeal ğ’œ :=
  âŸ¨RingHom.ker (GradedRing.projZeroRingHom ğ’œ), fun i r (hr : (decompose ğ’œ r 0 : A) = 0) => by
    change (decompose ğ’œ (decompose ğ’œ r _ : A) 0 : A) = 0
    by_cases h : i = 0
    Â· rw [h, hr, decompose_zero, zero_apply, ZeroMemClass.coe_zero]
    Â· rw [decompose_of_mem_ne ğ’œ (SetLike.coe_mem _) h]âŸ©

@[simp]
theorem HomogeneousIdeal.mem_irrelevant_iff (a : A) :
    a âˆˆ HomogeneousIdeal.irrelevant ğ’œ â†” proj ğ’œ 0 a = 0 :=
  Iff.rfl

@[simp]
theorem HomogeneousIdeal.toIdeal_irrelevant :
    (HomogeneousIdeal.irrelevant ğ’œ).toIdeal = RingHom.ker (GradedRing.projZeroRingHom ğ’œ) :=
  rfl

end IrrelevantIdeal
