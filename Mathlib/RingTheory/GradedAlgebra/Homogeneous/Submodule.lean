/-
Copyright (c) 2021 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Eric Wieser
-/
import Mathlib.RingTheory.GradedAlgebra.Basic
import Mathlib.Algebra.GradedMulAction

/-!
# Homogeneous submodules of a graded module

This file defines homogeneous submodule of a graded module `‚®Å·µ¢ ‚Ñ≥·µ¢` over graded ring `‚®Å·µ¢ ùíú·µ¢` and
operations on them.

## Main definitions

For any `p : Submodule A M`:
* `Submodule.IsHomogeneous ‚Ñ≥ p`: The property that a submodule is closed under `GradedModule.proj`.
* `HomogeneousSubmodule ùíú ‚Ñ≥`: The structure extending submodules which satisfy
  `Submodule.IsHomogeneous`.

## Implementation notes

The **notion** of homogeneous submodule does not rely on a graded ring, only a decomposition of the
the module. However, most interesting properties of homogeneous submodules do rely on the base ring
being a graded ring. For technical reasons, we make `HomogeneousSubmodule` depend on a graded ring.
For example, if the definition of a homogeneous submodule does not depend on a graded ring, the
instance that `HomogeneousSubmodule` is a complete lattice cannot be synthesized due to
synthesization order.

## Tags

graded algebra, homogeneous
-/

open SetLike DirectSum Pointwise Set

variable {ŒπA ŒπM œÉA œÉM A M : Type*}

variable [Semiring A] [AddCommMonoid M] [Module A M]

section HomogeneousDef

/--
An `A`-submodule `p ‚äÜ M` is homogeneous if for every `m ‚àà p`, all homogeneous components of `m` are
in `p`.
-/
def Submodule.IsHomogeneous (p : Submodule A M) (‚Ñ≥ : ŒπM ‚Üí œÉM)
    [DecidableEq ŒπM] [SetLike œÉM M] [AddSubmonoidClass œÉM M] [Decomposition ‚Ñ≥] : Prop :=
  SetLike.IsHomogeneous ‚Ñ≥ p

theorem Submodule.IsHomogeneous.mem_iff {p : Submodule A M}
    (‚Ñ≥ : ŒπM ‚Üí œÉM)
    [DecidableEq ŒπM] [SetLike œÉM M] [AddSubmonoidClass œÉM M] [Decomposition ‚Ñ≥]
    (hp : p.IsHomogeneous ‚Ñ≥) {x} :
    x ‚àà p ‚Üî ‚àÄ i, (decompose ‚Ñ≥ x i : M) ‚àà p :=
  AddSubmonoidClass.IsHomogeneous.mem_iff ‚Ñ≥ _ hp

theorem Submodule.IsHomogeneous.closure_isHomogeneousElem_eq {p : Submodule A M}
    [DecidableEq ŒπM] [SetLike œÉM M] [AddSubmonoidClass œÉM M]
    {‚Ñ≥ : ŒπM ‚Üí œÉM} [Decomposition ‚Ñ≥] (hp : p.IsHomogeneous ‚Ñ≥) :
    AddSubmonoid.closure { x | x ‚àà p ‚àß SetLike.IsHomogeneousElem ‚Ñ≥ x } = p.toAddSubmonoid := by
  refine le_antisymm (AddSubmonoid.closure_le.mpr fun _ hx ‚Ü¶ hx.1) fun x hx ‚Ü¶ ?_
  classical
  rw [‚Üê DirectSum.sum_support_decompose ‚Ñ≥ x]
  exact sum_mem fun i hi ‚Ü¶ AddSubmonoid.subset_closure ‚ü®hp i hx, i, SetLike.coe_mem _‚ü©

theorem Submodule.IsHomogeneous.span_isHomogeneousElem_eq {p : Submodule A M}
    [DecidableEq ŒπM] [SetLike œÉM M] [AddSubmonoidClass œÉM M]
    {‚Ñ≥ : ŒπM ‚Üí œÉM} [Decomposition ‚Ñ≥] (hp : p.IsHomogeneous ‚Ñ≥) :
    .span A { x | x ‚àà p ‚àß SetLike.IsHomogeneousElem ‚Ñ≥ x } = p :=
  le_antisymm (span_le.mpr fun _ h ‚Ü¶ h.1) <| (closure_isHomogeneousElem_eq hp).symm.trans_le <|
    AddSubmonoid.closure_le.mpr subset_span

/-- For any `Semiring A`, we collect the homogeneous submodule of `A`-modules into a type. -/
structure HomogeneousSubmodule (ùíú : ŒπA ‚Üí œÉA) (‚Ñ≥ : ŒπM ‚Üí œÉM)
    [DecidableEq ŒπA] [AddMonoid ŒπA] [SetLike œÉA A] [AddSubmonoidClass œÉA A] [GradedRing ùíú]
    [DecidableEq ŒπM] [SetLike œÉM M] [AddSubmonoidClass œÉM M] [Decomposition ‚Ñ≥]
    [VAdd ŒπA ŒπM] [GradedSMul ùíú ‚Ñ≥]
    extends Submodule A M where
  is_homogeneous' : toSubmodule.IsHomogeneous ‚Ñ≥

variable (ùíú : ŒπA ‚Üí œÉA) (‚Ñ≥ : ŒπM ‚Üí œÉM)
variable [DecidableEq ŒπA] [AddMonoid ŒπA] [SetLike œÉA A] [AddSubmonoidClass œÉA A] [GradedRing ùíú]
variable [DecidableEq ŒπM] [SetLike œÉM M] [AddSubmonoidClass œÉM M] [Decomposition ‚Ñ≥]
variable [VAdd ŒπA ŒπM] [GradedSMul ùíú ‚Ñ≥]

instance : SetLike (HomogeneousSubmodule ùíú ‚Ñ≥) M where
  coe X := X.toSubmodule
  coe_injective' := by
    rintro ‚ü®p, hp‚ü© ‚ü®q, hq‚ü© (h : (p : Set M) = q)
    simpa using h

instance : AddSubmonoidClass (HomogeneousSubmodule ùíú ‚Ñ≥) M where
  zero_mem p := p.toSubmodule.zero_mem
  add_mem hx hy := Submodule.add_mem _ hx hy

instance : SMulMemClass (HomogeneousSubmodule ùíú ‚Ñ≥) A M where
  smul_mem := by
    intro x r m hm
    exact Submodule.smul_mem x.toSubmodule r hm

variable {ùíú ‚Ñ≥} in
theorem HomogeneousSubmodule.isHomogeneous (p : HomogeneousSubmodule ùíú ‚Ñ≥) :
    p.toSubmodule.IsHomogeneous ‚Ñ≥ :=
  p.is_homogeneous'

theorem HomogeneousSubmodule.toSubmodule_injective :
    Function.Injective
      (HomogeneousSubmodule.toSubmodule : HomogeneousSubmodule ùíú ‚Ñ≥ ‚Üí Submodule A M) :=
  fun ‚ü®x, hx‚ü© ‚ü®y, hy‚ü© ‚Ü¶ fun (h : x = y) ‚Ü¶ by simp [h]

instance HomogeneousSubmodule.setLike : SetLike (HomogeneousSubmodule ùíú ‚Ñ≥) M where
  coe p := p.toSubmodule
  coe_injective' _ _ h := HomogeneousSubmodule.toSubmodule_injective ùíú ‚Ñ≥ <| SetLike.coe_injective h

@[ext]
theorem HomogeneousSubmodule.ext
    {I J : HomogeneousSubmodule ùíú ‚Ñ≥} (h : I.toSubmodule = J.toSubmodule) : I = J :=
  HomogeneousSubmodule.toSubmodule_injective _ _ h

/--
The set-theoretic extensionality of `HomogeneousSubmodule`.
-/
theorem HomogeneousSubmodule.ext' {I J : HomogeneousSubmodule ùíú ‚Ñ≥}
    (h : ‚àÄ i, ‚àÄ x ‚àà ‚Ñ≥ i, x ‚àà I ‚Üî x ‚àà J) :
    I = J := by
  ext
  rw [I.isHomogeneous.mem_iff, J.isHomogeneous.mem_iff]
  apply forall_congr'
  exact fun i ‚Ü¶ h i _ (decompose ‚Ñ≥ _ i).2

@[simp]
theorem HomogeneousSubmodule.mem_toSubmodule_iff {I : HomogeneousSubmodule ùíú ‚Ñ≥} {x : M} :
    x ‚àà I.toSubmodule (A := A) ‚Üî x ‚àà I :=
  Iff.rfl

end HomogeneousDef
