/-
Copyright (c) 2021 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Eric Wieser
-/
import Mathlib.RingTheory.GradedAlgebra.Basic
import Mathlib.Algebra.GradedMulAction

/-!
# Homogeneous submodules of a graded module

This file defines homogeneous submodule of a graded module `â¨áµ¢ â„³áµ¢` over graded ring `â¨áµ¢ ğ’œáµ¢` and
operations on them.

## Main definitions

For any `p : Submodule A M`:
* `Submodule.IsHomogeneous â„³ p`: The property that a submodule is closed under `GradedModule.proj`.
* `HomogeneousSubmodule ğ’œ â„³`: The structure extending submodules which satisfy
  `Submodule.IsHomogeneous`.

## Implementation notes

The **notion** of homogeneous submodule does not rely on a graded ring, only a decomposition of the
the module. However, most interesting properties of homogeneous submodules do reply on the base ring
is a graded ring. For technical reasons, we make `HomogeneousSubmodule` depend on a graded ring.
For example, if the definition of a homogeneous submodule does not depend on a graded ring, the
instance that `HomogeneousSubmodule` is a complete lattice can not be synthesized due to
synthesation order.

## Tags

graded algebra, homogeneous
-/

open SetLike DirectSum Pointwise Set

variable {Î¹A Î¹M ÏƒA ÏƒM A M : Type*}

variable [Semiring A] [AddCommMonoid M] [Module A M]

section HomogeneousDef

/-- An `p : Submodule A M` is homogeneous if for every `m âˆˆ p`, all homogeneous components
  of `m` are in `I`. -/
def Submodule.IsHomogeneous (p : Submodule A M) (â„³ : Î¹M â†’ ÏƒM)
    [DecidableEq Î¹M] [SetLike ÏƒM M] [AddSubmonoidClass ÏƒM M] [Decomposition â„³] : Prop :=
  âˆ€ (i : Î¹M) â¦ƒm : Mâ¦„, m âˆˆ p â†’ (DirectSum.decompose â„³ m i : M) âˆˆ p

theorem Submodule.IsHomogeneous.mem_iff {p}
    (â„³ : Î¹M â†’ ÏƒM)
    [DecidableEq Î¹M] [SetLike ÏƒM M] [AddSubmonoidClass ÏƒM M] [Decomposition â„³]
    (hp : Submodule.IsHomogeneous (A := A) p â„³) {x} :
    x âˆˆ p â†” âˆ€ i, (decompose â„³ x i : M) âˆˆ p := by
  classical
  refine âŸ¨fun hx i â†¦ hp i hx, fun hx â†¦ ?_âŸ©
  rw [â† DirectSum.sum_support_decompose â„³ x]
  exact Submodule.sum_mem _ (fun i _ â†¦ hx i)

/-- For any `Semiring A`, we collect the homogeneous submodule of `A`-modules into a type. -/
structure HomogeneousSubmodule (ğ’œ : Î¹A â†’ ÏƒA) (â„³ : Î¹M â†’ ÏƒM)
    [DecidableEq Î¹A] [AddMonoid Î¹A] [SetLike ÏƒA A] [AddSubmonoidClass ÏƒA A] [GradedRing ğ’œ]
    [DecidableEq Î¹M] [SetLike ÏƒM M] [AddSubmonoidClass ÏƒM M] [Decomposition â„³]
    [VAdd Î¹A Î¹M] [GradedSMul ğ’œ â„³]
    extends Submodule A M where
  is_homogeneous' : toSubmodule.IsHomogeneous â„³

variable (ğ’œ : Î¹A â†’ ÏƒA) (â„³ : Î¹M â†’ ÏƒM)
variable [DecidableEq Î¹A] [AddMonoid Î¹A] [SetLike ÏƒA A] [AddSubmonoidClass ÏƒA A] [GradedRing ğ’œ]
variable [DecidableEq Î¹M] [SetLike ÏƒM M] [AddSubmonoidClass ÏƒM M] [Decomposition â„³]
variable [VAdd Î¹A Î¹M] [GradedSMul ğ’œ â„³]

variable {ğ’œ â„³} in
theorem HomogeneousSubmodule.isHomogeneous (I : HomogeneousSubmodule ğ’œ â„³) :
    I.toSubmodule.IsHomogeneous â„³ :=
  I.is_homogeneous'

theorem HomogeneousSubmodule.toSubmodule_injective :
    Function.Injective
      (HomogeneousSubmodule.toSubmodule : HomogeneousSubmodule ğ’œ â„³ â†’ Submodule A M) :=
  fun âŸ¨x, hxâŸ© âŸ¨y, hyâŸ© => fun (h : x = y) => by simp [h]

instance HomogeneousSubmodule.setLike : SetLike (HomogeneousSubmodule ğ’œ â„³) M where
  coe p := p.toSubmodule
  coe_injective' _ _ h := HomogeneousSubmodule.toSubmodule_injective ğ’œ â„³ <| SetLike.coe_injective h

@[ext]
theorem HomogeneousSubmodule.ext
    {I J : HomogeneousSubmodule ğ’œ â„³} (h : I.toSubmodule = J.toSubmodule) : I = J :=
  HomogeneousSubmodule.toSubmodule_injective _ _ h

/--
The set-theoretic extensionality of `HomogeneousSubmodule`.
-/
theorem HomogeneousSubmodule.ext' {I J : HomogeneousSubmodule ğ’œ â„³}
    (h : âˆ€ i, âˆ€ x âˆˆ â„³ i, x âˆˆ I â†” x âˆˆ J) :
    I = J := by
  ext
  rw [I.isHomogeneous.mem_iff, J.isHomogeneous.mem_iff]
  apply forall_congr'
  exact fun i â†¦ h i _ (decompose â„³ _ i).2

@[simp]
theorem HomogeneousSubmodule.mem_iff {I : HomogeneousSubmodule ğ’œ â„³} {x : M} :
    x âˆˆ I.toSubmodule (A := A) â†” x âˆˆ I :=
  Iff.rfl

end HomogeneousDef
