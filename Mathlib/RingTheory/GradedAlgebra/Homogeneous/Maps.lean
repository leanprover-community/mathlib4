/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/
import Mathlib.RingTheory.GradedAlgebra.Hom
import Mathlib.RingTheory.GradedAlgebra.Homogeneous.Ideal

/-!
# Maps on homogeneous ideals

In this file we define `HomogeneousIdeal.map` and `HomogeneousIdeal.comap`.
-/

variable {A B C Ïƒ Ï„ Ï‰ Î¹ F G : Type*}
  [Semiring A] [Semiring B] [Semiring C]
  [SetLike Ïƒ A] [SetLike Ï„ B] [SetLike Ï‰ C]
  [AddSubmonoidClass Ïƒ A] [AddSubmonoidClass Ï„ B] [AddSubmonoidClass Ï‰ C]
  [DecidableEq Î¹] [AddMonoid Î¹]
  {ð’œ : Î¹ â†’ Ïƒ} {â„¬ : Î¹ â†’ Ï„} {ð’ž : Î¹ â†’ Ï‰}
  [GradedRing ð’œ] [GradedRing â„¬] [GradedRing ð’ž]
  [GradedRingHomClass F ð’œ â„¬] [GradedRingHomClass G â„¬ ð’ž]
  (f : F) (g : G)

namespace HomogeneousIdeal

/-- Map a homogeneous ideal along a graded ring homomorphism. The underlying ideal is
(definitionally) equal to `Ideal.map`. -/
def map (I : HomogeneousIdeal ð’œ) : HomogeneousIdeal â„¬ where
  __ := I.toIdeal.map f
  is_homogeneous' i b hb := by
    rw [Ideal.map] at hb
    induction hb using Submodule.span_induction generalizing i with
    | zero => simp
    | add => simp [*, Ideal.add_mem]
    | mem a ha =>
      obtain âŸ¨a, ha, rflâŸ© := ha
      rw [â† map_coe_decompose]
      exact Ideal.mem_map_of_mem _ (I.2 _ ha)
    | smul aâ‚ aâ‚‚ haâ‚‚ ih =>
      classical rw [smul_eq_mul, DirectSum.decompose_mul, DirectSum.coe_mul_apply]
      exact sum_mem fun ij hij â†¦ Ideal.mul_mem_left _ _ <| ih _

/-- Map a homogeneous ideal along a graded ring homomorphism in the other direction.
The underlying ideal is (definitionally) equal to `Ideal.comap`, whose underlying set is
definitionally equal to the preimage. -/
def comap (I : HomogeneousIdeal â„¬) : HomogeneousIdeal ð’œ where
  __ := I.toIdeal.comap f
  is_homogeneous' n a ha := by
    rw [Ideal.mem_comap, HomogeneousIdeal.mem_iff, map_coe_decompose ð’œ â„¬]
    exact I.2 _ ha

variable {I Iâ‚ Iâ‚‚ Iâ‚ƒ : HomogeneousIdeal ð’œ} {J Jâ‚ Jâ‚‚ Jâ‚ƒ : HomogeneousIdeal â„¬}
  {K : HomogeneousIdeal ð’ž}

lemma map_le_iff_le_comap :
    I.map f â‰¤ J â†” I â‰¤ J.comap f :=
  Ideal.map_le_iff_le_comap
alias âŸ¨le_comap_of_map_le, map_le_of_le_comapâŸ© := map_le_iff_le_comap

theorem gc_map_comap : GaloisConnection (map f) (comap f) := fun _ _ â†¦
  map_le_iff_le_comap f

@[mono, aesop safe apply] lemma map_mono : Monotone (map f) := (gc_map_comap f).monotone_l

@[mono] lemma comap_mono : Monotone (comap f) := (gc_map_comap f).monotone_u

@[simp] lemma toIdeal_comap : (J.comap f).toIdeal = J.toIdeal.comap f := rfl

@[simp] lemma coe_comap : J.comap f = f â»Â¹' J := rfl

@[simp] lemma toIdeal_map : (I.map f).toIdeal = I.toIdeal.map f := rfl

instance isPrime_comap [J.toIdeal.IsPrime] : (J.comap f).toIdeal.IsPrime :=
  inferInstanceAs (J.toIdeal.comap f).IsPrime -- this shows that the simpNF is already covered

@[simp] lemma map_id : I.map (GradedRingHom.id ð’œ) = I :=
  ext <| Ideal.map_id _

lemma map_map (f : ð’œ â†’+*áµ â„¬) (g : â„¬ â†’+*áµ ð’ž) : (I.map f).map g = I.map (g.comp f) :=
  ext <| Ideal.map_map _ _

lemma map_comp (f : ð’œ â†’+*áµ â„¬) (g : â„¬ â†’+*áµ ð’ž) : I.map (g.comp f) = (I.map f).map g :=
  (map_map f g).symm

@[simp] lemma comap_id : I.comap (GradedRingHom.id ð’œ) = I :=
  rfl

lemma comap_comap (f : ð’œ â†’+*áµ â„¬) (g : â„¬ â†’+*áµ ð’ž) :
    (K.comap g).comap f = K.comap (g.comp f) :=
  rfl

end HomogeneousIdeal
