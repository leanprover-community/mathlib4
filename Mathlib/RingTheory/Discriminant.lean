/-
Copyright (c) 2021 Riccardo Brasca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Riccardo Brasca
-/
import Mathlib.NumberTheory.NumberField.Basic
import Mathlib.RingTheory.Trace
import Mathlib.RingTheory.Norm

#align_import ring_theory.discriminant from "leanprover-community/mathlib"@"3e068ece210655b7b9a9477c3aff38a492400aa1"

/-!
# Discriminant of a family of vectors

Given an `A`-algebra `B` and `b`, an `Î¹`-indexed family of elements of `B`, we define the
*discriminant* of `b` as the determinant of the matrix whose `(i j)`-th element is the trace of
`b i * b j`.

## Main definition

* `Algebra.discr A b` : the discriminant of `b : Î¹ â†’ B`.

## Main results

* `Algebra.discr_zero_of_not_linearIndependent` : if `b` is not linear independent, then
  `Algebra.discr A b = 0`.
* `Algebra.discr_of_matrix_vecMul` and `Algebra.discr_of_matrix_mulVec` : formulas relating
  `Algebra.discr A Î¹ b` with `Algebra.discr A ((P.map (algebraMap A B)).vecMul b)` and
  `Algebra.discr A ((P.map (algebraMap A B)).mulVec b)`.
* `Algebra.discr_not_zero_of_basis` : over a field, if `b` is a basis, then
  `Algebra.discr K b â‰  0`.
* `Algebra.discr_eq_det_embeddingsMatrixReindex_pow_two` : if `L/K` is a field extension and
  `b : Î¹ â†’ L`, then `discr K b` is the square of the determinant of the matrix whose `(i, j)`
  coefficient is `Ïƒâ±¼ (b i)`, where `Ïƒâ±¼ : L â†’â‚[K] E` is the embedding in an algebraically closed
  field `E` corresponding to `j : Î¹` via a bijection `e : Î¹ â‰ƒ (L â†’â‚[K] E)`.
* `Algebra.discr_powerBasis_eq_prod` : the discriminant of a power basis.
* `Algebra.discr_isIntegral` : if `K` and `L` are fields and `IsScalarTower R K L`, if
  `b : Î¹ â†’ L` satisfies ` âˆ€ i, IsIntegral R (b i)`, then `IsIntegral R (discr K b)`.
* `Algebra.discr_mul_isIntegral_mem_adjoin` : let `K` be the fraction field of an integrally
  closed domain `R` and let `L` be a finite separable extension of `K`. Let `B : PowerBasis K L`
  be such that `IsIntegral R B.gen`. Then for all, `z : L` we have
  `(discr K B.basis) â€¢ z âˆˆ adjoin R ({B.gen} : set L)`.

## Implementation details

Our definition works for any `A`-algebra `B`, but note that if `B` is not free as an `A`-module,
then `trace A B = 0` by definition, so `discr A b = 0` for any `b`.
-/


universe u v w z

open scoped Matrix BigOperators

open Matrix FiniteDimensional Fintype Polynomial Finset IntermediateField

namespace Algebra

variable (A : Type u) {B : Type v} (C : Type z) {Î¹ : Type w} [DecidableEq Î¹]

variable [CommRing A] [CommRing B] [Algebra A B] [CommRing C] [Algebra A C]

section Discr

/-- Given an `A`-algebra `B` and `b`, an `Î¹`-indexed family of elements of `B`, we define
`discr A Î¹ b` as the determinant of `traceMatrix A Î¹ b`. -/
-- Porting note: using `[DecidableEq Î¹]` instead of `by classical...` did not work in
-- mathlib3.
noncomputable def discr (A : Type u) {B : Type v} [CommRing A] [CommRing B] [Algebra A B]
    [Fintype Î¹] (b : Î¹ â†’ B) := (traceMatrix A b).det
#align algebra.discr Algebra.discr

theorem discr_def [Fintype Î¹] (b : Î¹ â†’ B) : discr A b = (traceMatrix A b).det := by
-- Porting note: `unfold discr` was not necessary. `rfl` still does not work.
  unfold discr
  -- âŠ¢ det (traceMatrix A b) = det (traceMatrix A b)
  convert rfl
  -- ğŸ‰ no goals


#align algebra.discr_def Algebra.discr_def

variable {Î¹' : Type*} [Fintype Î¹'] [Fintype Î¹] [DecidableEq Î¹']

section Basic

@[simp]
theorem discr_reindex (b : Basis Î¹ A B) (f : Î¹ â‰ƒ Î¹') : discr A (b âˆ˜ â‡‘f.symm) = discr A b := by
  classical rw [â† Basis.coe_reindex, discr_def, traceMatrix_reindex, det_reindex_self, â† discr_def]
  -- ğŸ‰ no goals
#align algebra.discr_reindex Algebra.discr_reindex

/-- If `b` is not linear independent, then `Algebra.discr A b = 0`. -/
theorem discr_zero_of_not_linearIndependent [IsDomain A] {b : Î¹ â†’ B}
    (hli : Â¬LinearIndependent A b) : discr A b = 0 := by
  classical
  obtain âŸ¨g, hg, i, hiâŸ© := Fintype.not_linearIndependent_iff.1 hli
  have : (traceMatrix A b).mulVec g = 0 := by
    ext i
    have : âˆ€ j, (trace A B) (b i * b j) * g j = (trace A B) (g j â€¢ b j * b i) := by
      intro j;
      simp [mul_comm]
    simp only [mulVec, dotProduct, traceMatrix_apply, Pi.zero_apply, traceForm_apply, fun j =>
      this j, â† LinearMap.map_sum, â† sum_mul, hg, zero_mul, LinearMap.map_zero]
  by_contra h
  rw [discr_def] at h
  simp [Matrix.eq_zero_of_mulVec_eq_zero h this] at hi
#align algebra.discr_zero_of_not_linear_independent Algebra.discr_zero_of_not_linearIndependent

variable {A}

/-- Relation between `Algebra.discr A Î¹ b` and
`Algebra.discr A ((P.map (algebraMap A B)).vecMul b)`. -/
theorem discr_of_matrix_vecMul (b : Î¹ â†’ B) (P : Matrix Î¹ Î¹ A) :
    discr A ((P.map (algebraMap A B)).vecMul b) = P.det ^ 2 * discr A b := by
  rw [discr_def, traceMatrix_of_matrix_vecMul, det_mul, det_mul, det_transpose, mul_comm, â†
    mul_assoc, discr_def, pow_two]
#align algebra.discr_of_matrix_vec_mul Algebra.discr_of_matrix_vecMul

/-- Relation between `Algebra.discr A Î¹ b` and
`Algebra.discr A ((P.map (algebraMap A B)).mulVec b)`. -/
theorem discr_of_matrix_mulVec (b : Î¹ â†’ B) (P : Matrix Î¹ Î¹ A) :
    discr A ((P.map (algebraMap A B)).mulVec b) = P.det ^ 2 * discr A b := by
  rw [discr_def, traceMatrix_of_matrix_mulVec, det_mul, det_mul, det_transpose, mul_comm, â†
    mul_assoc, discr_def, pow_two]
#align algebra.discr_of_matrix_mul_vec Algebra.discr_of_matrix_mulVec

end Basic

section Field

variable (K : Type u) {L : Type v} (E : Type z) [Field K] [Field L] [Field E]

variable [Algebra K L] [Algebra K E]

variable [Module.Finite K L] [IsAlgClosed E]

/-- Over a field, if `b` is a basis, then `Algebra.discr K b â‰  0`. -/
theorem discr_not_zero_of_basis [IsSeparable K L] (b : Basis Î¹ K L) :
    discr K b â‰  0 := by
  cases isEmpty_or_nonempty Î¹
  -- âŠ¢ discr K â†‘b â‰  0
-- Porting note: the following proof was `simp [discr]`. Variations like `exact this` do not work.
  Â· have : det (traceMatrix K â†‘b) â‰  0 := by simp
    -- âŠ¢ discr K â†‘b â‰  0
    unfold discr
    -- âŠ¢ det (traceMatrix K â†‘b) â‰  0
    convert this
    -- ğŸ‰ no goals
  Â· have :=
      span_eq_top_of_linearIndependent_of_card_eq_finrank b.linearIndependent
        (finrank_eq_card_basis b).symm
    classical
    rw [discr_def, traceMatrix]
    simp_rw [â† Basis.mk_apply b.linearIndependent this.ge]
    rw [â† traceMatrix, traceMatrix_of_basis, â† BilinForm.nondegenerate_iff_det_ne_zero]
    exact traceForm_nondegenerate _ _
#align algebra.discr_not_zero_of_basis Algebra.discr_not_zero_of_basis

/-- Over a field, if `b` is a basis, then `Algebra.discr K b` is a unit. -/
theorem discr_isUnit_of_basis [IsSeparable K L] (b : Basis Î¹ K L) : IsUnit (discr K b) :=
  IsUnit.mk0 _ (discr_not_zero_of_basis _ _)
#align algebra.discr_is_unit_of_basis Algebra.discr_isUnit_of_basis

variable (b : Î¹ â†’ L) (pb : PowerBasis K L)

/-- If `L/K` is a field extension and `b : Î¹ â†’ L`, then `discr K b` is the square of the
determinant of the matrix whose `(i, j)` coefficient is `Ïƒâ±¼ (b i)`, where `Ïƒâ±¼ : L â†’â‚[K] E` is the
embedding in an algebraically closed field `E` corresponding to `j : Î¹` via a bijection
`e : Î¹ â‰ƒ (L â†’â‚[K] E)`. -/
theorem discr_eq_det_embeddingsMatrixReindex_pow_two [IsSeparable K L] (e : Î¹ â‰ƒ (L â†’â‚[K] E)) :
    algebraMap K E (discr K b) = (embeddingsMatrixReindex K E b e).det ^ 2 := by
  rw [discr_def, RingHom.map_det, RingHom.mapMatrix_apply,
    traceMatrix_eq_embeddingsMatrixReindex_mul_trans, det_mul, det_transpose, pow_two]
#align algebra.discr_eq_det_embeddings_matrix_reindex_pow_two Algebra.discr_eq_det_embeddingsMatrixReindex_pow_two

/-- The discriminant of a power basis. -/
theorem discr_powerBasis_eq_prod (e : Fin pb.dim â‰ƒ (L â†’â‚[K] E)) [IsSeparable K L] :
    algebraMap K E (discr K pb.basis) =
      âˆ i : Fin pb.dim, âˆ j in Ioi i, (e j pb.gen - e i pb.gen) ^ 2 := by
  rw [discr_eq_det_embeddingsMatrixReindex_pow_two K E pb.basis e,
    embeddingsMatrixReindex_eq_vandermonde, det_transpose, det_vandermonde, â† prod_pow]
  congr; ext i
  -- âŠ¢ (fun x => (âˆ j in Ioi x, (â†‘(â†‘e j) pb.gen - â†‘(â†‘e x) pb.gen)) ^ 2) = fun i =>  â€¦
         -- âŠ¢ (âˆ j in Ioi i, (â†‘(â†‘e j) pb.gen - â†‘(â†‘e i) pb.gen)) ^ 2 = âˆ j in Ioi i, (â†‘(â†‘e  â€¦
  rw [â† prod_pow]
  -- ğŸ‰ no goals
#align algebra.discr_power_basis_eq_prod Algebra.discr_powerBasis_eq_prod

/-- A variation of `Algebra.discr_powerBasis_eq_prod`. -/
theorem discr_powerBasis_eq_prod' [IsSeparable K L] (e : Fin pb.dim â‰ƒ (L â†’â‚[K] E)) :
    algebraMap K E (discr K pb.basis) =
      âˆ i : Fin pb.dim, âˆ j in Ioi i, -((e j pb.gen - e i pb.gen) * (e i pb.gen - e j pb.gen)) := by
  rw [discr_powerBasis_eq_prod _ _ _ e]
  -- âŠ¢ âˆ i : Fin pb.dim, âˆ j in Ioi i, (â†‘(â†‘e j) pb.gen - â†‘(â†‘e i) pb.gen) ^ 2 = âˆ i  â€¦
  congr; ext i; congr; ext j
  -- âŠ¢ (fun i => âˆ j in Ioi i, (â†‘(â†‘e j) pb.gen - â†‘(â†‘e i) pb.gen) ^ 2) = fun i => âˆ  â€¦
         -- âŠ¢ âˆ j in Ioi i, (â†‘(â†‘e j) pb.gen - â†‘(â†‘e i) pb.gen) ^ 2 = âˆ j in Ioi i, -((â†‘(â†‘e  â€¦
                -- âŠ¢ (fun j => (â†‘(â†‘e j) pb.gen - â†‘(â†‘e i) pb.gen) ^ 2) = fun j => -((â†‘(â†‘e j) pb.ge â€¦
                       -- âŠ¢ (â†‘(â†‘e j) pb.gen - â†‘(â†‘e i) pb.gen) ^ 2 = -((â†‘(â†‘e j) pb.gen - â†‘(â†‘e i) pb.gen)  â€¦
  ring
  -- ğŸ‰ no goals
#align algebra.discr_power_basis_eq_prod' Algebra.discr_powerBasis_eq_prod'

local notation "n" => finrank K L

/-- A variation of `Algebra.discr_powerBasis_eq_prod`. -/
theorem discr_powerBasis_eq_prod'' [IsSeparable K L] (e : Fin pb.dim â‰ƒ (L â†’â‚[K] E)) :
    algebraMap K E (discr K pb.basis) =
      (-1) ^ (n * (n - 1) / 2) *
        âˆ i : Fin pb.dim, âˆ j in Ioi i, (e j pb.gen - e i pb.gen) * (e i pb.gen - e j pb.gen) := by
  rw [discr_powerBasis_eq_prod' _ _ _ e]
  -- âŠ¢ âˆ i : Fin pb.dim, âˆ j in Ioi i, -((â†‘(â†‘e j) pb.gen - â†‘(â†‘e i) pb.gen) * (â†‘(â†‘e  â€¦
  simp_rw [fun i j => neg_eq_neg_one_mul ((e j pb.gen - e i pb.gen) * (e i pb.gen - e j pb.gen)),
    prod_mul_distrib]
  congr
  -- âŠ¢ âˆ x : Fin pb.dim, âˆ x in Ioi x, -1 = (-1) ^ (n * (n - 1) / 2)
  simp only [prod_pow_eq_pow_sum, prod_const]
  -- âŠ¢ (-1) ^ âˆ‘ x : Fin pb.dim, Finset.card (Ioi x) = (-1) ^ (n * (n - 1) / 2)
  congr
  -- âŠ¢ âˆ‘ x : Fin pb.dim, Finset.card (Ioi x) = n * (n - 1) / 2
  rw [â† @Nat.cast_inj â„š, Nat.cast_sum]
  -- âŠ¢ âˆ‘ x : Fin pb.dim, â†‘(Finset.card (Ioi x)) = â†‘(n * (n - 1) / 2)
  have : âˆ€ x : Fin pb.dim, â†‘x + 1 â‰¤ pb.dim := by simp [Nat.succ_le_iff, Fin.is_lt]
  -- âŠ¢ âˆ‘ x : Fin pb.dim, â†‘(Finset.card (Ioi x)) = â†‘(n * (n - 1) / 2)
  simp_rw [Fin.card_Ioi, Nat.sub_sub, add_comm 1]
  -- âŠ¢ âˆ‘ x : Fin pb.dim, â†‘(pb.dim - (â†‘x + 1)) = â†‘(n * (n - 1) / 2)
  simp only [Nat.cast_sub, this, Finset.card_fin, nsmul_eq_mul, sum_const, sum_sub_distrib,
    Nat.cast_add, Nat.cast_one, sum_add_distrib, mul_one]
  rw [â† Nat.cast_sum, â† @Finset.sum_range â„• _ pb.dim fun i => i, sum_range_id]
  -- âŠ¢ â†‘pb.dim * â†‘pb.dim - (â†‘(pb.dim * (pb.dim - 1) / 2) + â†‘pb.dim) = â†‘(n * (n - 1) â€¦
  have hn : n = pb.dim := by
    rw [â† AlgHom.card K L E, â† Fintype.card_fin pb.dim]
    exact card_congr (Equiv.symm e)
  have hâ‚‚ : 2 âˆ£ pb.dim * (pb.dim - 1) := even_iff_two_dvd.1 (Nat.even_mul_self_pred _)
  -- âŠ¢ â†‘pb.dim * â†‘pb.dim - (â†‘(pb.dim * (pb.dim - 1) / 2) + â†‘pb.dim) = â†‘(n * (n - 1) â€¦
  have hne : ((2 : â„•) : â„š) â‰  0 := by simp
  -- âŠ¢ â†‘pb.dim * â†‘pb.dim - (â†‘(pb.dim * (pb.dim - 1) / 2) + â†‘pb.dim) = â†‘(n * (n - 1) â€¦
  have hle : 1 â‰¤ pb.dim := by
    rw [â† hn, Nat.one_le_iff_ne_zero, â† zero_lt_iff, FiniteDimensional.finrank_pos_iff]
    infer_instance
  rw [hn, Nat.cast_div hâ‚‚ hne, Nat.cast_mul, Nat.cast_sub hle]
  -- âŠ¢ â†‘pb.dim * â†‘pb.dim - (â†‘pb.dim * (â†‘pb.dim - â†‘1) / â†‘2 + â†‘pb.dim) = â†‘pb.dim * (â†‘ â€¦
  field_simp
  -- âŠ¢ â†‘pb.dim * â†‘pb.dim * 2 - (â†‘pb.dim * (â†‘pb.dim - 1) + â†‘pb.dim * 2) = â†‘pb.dim *  â€¦
  ring
  -- ğŸ‰ no goals
#align algebra.discr_power_basis_eq_prod'' Algebra.discr_powerBasis_eq_prod''

/-- Formula for the discriminant of a power basis using the norm of the field extension. -/
-- Porting note: `(minpoly K pb.gen).derivative` does not work anymore.
theorem discr_powerBasis_eq_norm [IsSeparable K L] :
    discr K pb.basis =
      (-1) ^ (n * (n - 1) / 2) *
      norm K (aeval pb.gen (derivative (R := K) (minpoly K pb.gen))) := by
  let E := AlgebraicClosure L
  -- âŠ¢ discr K â†‘pb.basis = (-1) ^ (n * (n - 1) / 2) * â†‘(norm K) (â†‘(aeval pb.gen) (â†‘ â€¦
  letI := fun a b : E => Classical.propDecidable (Eq a b)
  -- âŠ¢ discr K â†‘pb.basis = (-1) ^ (n * (n - 1) / 2) * â†‘(norm K) (â†‘(aeval pb.gen) (â†‘ â€¦
  have e : Fin pb.dim â‰ƒ (L â†’â‚[K] E) := by
    refine' equivOfCardEq _
    rw [Fintype.card_fin, AlgHom.card]
    exact (PowerBasis.finrank pb).symm
  have hnodup : ((minpoly K pb.gen).map (algebraMap K E)).roots.Nodup :=
    nodup_roots (Separable.map (IsSeparable.separable K pb.gen))
  have hroots : âˆ€ Ïƒ : L â†’â‚[K] E, Ïƒ pb.gen âˆˆ ((minpoly K pb.gen).map (algebraMap K E)).roots := by
    intro Ïƒ
    rw [mem_roots, IsRoot.def, eval_map, â† aeval_def, aeval_algHom_apply]
    repeat' simp [minpoly.ne_zero (IsSeparable.isIntegral K pb.gen)]
  apply (algebraMap K E).injective
  -- âŠ¢ â†‘(algebraMap K E) (discr K â†‘pb.basis) = â†‘(algebraMap K E) ((-1) ^ (n * (n -  â€¦
  rw [RingHom.map_mul, RingHom.map_pow, RingHom.map_neg, RingHom.map_one,
    discr_powerBasis_eq_prod'' _ _ _ e]
  congr
  -- âŠ¢ âˆ i : Fin pb.dim, âˆ j in Ioi i, (â†‘(â†‘e j) pb.gen - â†‘(â†‘e i) pb.gen) * (â†‘(â†‘e i) â€¦
  rw [norm_eq_prod_embeddings, prod_prod_Ioi_mul_eq_prod_prod_off_diag]
  -- âŠ¢ âˆ i : Fin pb.dim, âˆ j in {i}á¶œ, (â†‘(â†‘e j) pb.gen - â†‘(â†‘e i) pb.gen) = âˆ Ïƒ : (fu â€¦
  conv_rhs =>
    congr
    rfl
    ext Ïƒ
    rw [â† aeval_algHom_apply,
      aeval_root_derivative_of_splits (minpoly.monic (IsSeparable.isIntegral K pb.gen))
        (IsAlgClosed.splits_codomain _) (hroots Ïƒ),
      â† Finset.prod_mk _ (hnodup.erase _)]
  rw [prod_sigma', prod_sigma']
  -- âŠ¢ âˆ x in Finset.sigma univ fun i => {i}á¶œ, (â†‘(â†‘e x.snd) pb.gen - â†‘(â†‘e x.fst) pb â€¦
  refine'
    prod_bij (fun i _ => âŸ¨e i.2, e i.1 pb.genâŸ©) (fun i hi => _) (fun i _ => by simp )
      (fun i j hi hj hij => _) fun Ïƒ hÏƒ => _
  Â· simp only [true_and_iff, Finset.mem_mk, mem_univ, mem_sigma]
    -- âŠ¢ â†‘(â†‘e i.fst) pb.gen âˆˆ Multiset.erase (roots (Polynomial.map (algebraMap K (Al â€¦
    rw [Multiset.mem_erase_of_ne fun h => ?_]
    -- âŠ¢ â†‘(â†‘e i.fst) pb.gen âˆˆ roots (Polynomial.map (algebraMap K (AlgebraicClosure L â€¦
    Â· exact hroots _
      -- ğŸ‰ no goals
-- Porting note: `@mem_compl` was not necessary.
    Â· simp only [true_and_iff, mem_univ, Ne.def, mem_sigma, @mem_compl _ _ _ (_),
        mem_singleton] at hi
      rw [â† PowerBasis.liftEquiv_apply_coe, â† PowerBasis.liftEquiv_apply_coe] at h
      -- âŠ¢ False
      exact hi (e.injective <| pb.liftEquiv.injective <| Subtype.eq h.symm)
      -- ğŸ‰ no goals
  Â· simp only [Sigma.mk.inj_iff, EmbeddingLike.apply_eq_iff_eq, heq_eq_eq] at hij
    -- âŠ¢ i = j
    have h := hij.2
    -- âŠ¢ i = j
    rw [â† PowerBasis.liftEquiv_apply_coe, â† PowerBasis.liftEquiv_apply_coe] at h
    -- âŠ¢ i = j
    refine' Sigma.eq (Equiv.injective e (Equiv.injective _ (Subtype.eq h))) (by simp [hij.1])
    -- ğŸ‰ no goals
  Â· simp only [true_and_iff, Finset.mem_mk, mem_univ, mem_sigma] at hÏƒ âŠ¢
    -- âŠ¢ âˆƒ a h, Ïƒ = { fst := â†‘e a.snd, snd := â†‘(â†‘e a.fst) pb.gen }
    simp only [Sigma.exists, exists_prop, mem_compl, mem_singleton, Ne.def]
    -- âŠ¢ âˆƒ a b, b âˆˆ {a}á¶œ âˆ§ Ïƒ = { fst := â†‘e b, snd := â†‘(â†‘e a) pb.gen }
    refine' âŸ¨e.symm (PowerBasis.lift pb Ïƒ.2 _), e.symm Ïƒ.1, âŸ¨_, Sigma.eq _ _âŸ©âŸ©
    Â· rw [aeval_def, evalâ‚‚_eq_eval_map, â† IsRoot.def, â† mem_roots]
      -- âŠ¢ Ïƒ.snd âˆˆ roots (Polynomial.map (algebraMap K E) (minpoly K pb.gen))
      Â· exact Multiset.erase_subset _ _ hÏƒ
        -- ğŸ‰ no goals
      Â· simp [minpoly.ne_zero (IsSeparable.isIntegral K pb.gen)]
        -- ğŸ‰ no goals
-- Porting note: the `simp only` was not needed.
    Â· simp only [@mem_compl _ _ _ (_), mem_singleton]
      -- âŠ¢ Â¬â†‘e.symm Ïƒ.fst = â†‘e.symm (PowerBasis.lift pb Ïƒ.snd (_ : â†‘(aeval Ïƒ.snd) (minp â€¦
      intro h
      -- âŠ¢ False
      replace h := AlgHom.congr_fun (Equiv.injective _ h) pb.gen
      -- âŠ¢ False
      rw [PowerBasis.lift_gen] at h
      -- âŠ¢ False
      rw [â† h] at hÏƒ
      -- âŠ¢ False
      exact hnodup.not_mem_erase hÏƒ
      -- ğŸ‰ no goals
    all_goals simp
    -- ğŸ‰ no goals
#align algebra.discr_power_basis_eq_norm Algebra.discr_powerBasis_eq_norm

section Integral

variable {R : Type z} [CommRing R] [Algebra R K] [Algebra R L] [IsScalarTower R K L]

/-- If `K` and `L` are fields and `IsScalarTower R K L`, and `b : Î¹ â†’ L` satisfies
` âˆ€ i, IsIntegral R (b i)`, then `IsIntegral R (discr K b)`. -/
theorem discr_isIntegral {b : Î¹ â†’ L} (h : âˆ€ i, IsIntegral R (b i)) : IsIntegral R (discr K b) := by
  classical
  rw [discr_def]
  exact IsIntegral.det fun i j => isIntegral_trace (isIntegral_mul (h i) (h j))
#align algebra.discr_is_integral Algebra.discr_isIntegral

/-- If `b` and `b'` are `â„š`-bases of a number field `K` such that
`âˆ€ i j, IsIntegral â„¤ (b.toMatrix b' i j)` and `âˆ€ i j, IsIntegral â„¤ (b'.toMatrix b i j)` then
`discr â„š b = discr â„š b'`. -/
theorem discr_eq_discr_of_toMatrix_coeff_isIntegral [NumberField K] {b : Basis Î¹ â„š K}
    {b' : Basis Î¹' â„š K} (h : âˆ€ i j, IsIntegral â„¤ (b.toMatrix b' i j))
    (h' : âˆ€ i j, IsIntegral â„¤ (b'.toMatrix b i j)) : discr â„š b = discr â„š b' := by
  replace h' : âˆ€ i j, IsIntegral â„¤ (b'.toMatrix (b.reindex (b.indexEquiv b')) i j)
  -- âŠ¢ âˆ€ (i j : Î¹'), IsIntegral â„¤ (Basis.toMatrix b' (â†‘(Basis.reindex b (Basis.inde â€¦
  Â· intro i j
    -- âŠ¢ IsIntegral â„¤ (Basis.toMatrix b' (â†‘(Basis.reindex b (Basis.indexEquiv b b'))) â€¦
    convert h' i ((b.indexEquiv b').symm j)
    -- âŠ¢ Basis.toMatrix b' (â†‘(Basis.reindex b (Basis.indexEquiv b b'))) i j = Basis.t â€¦
-- Porting note: `simp; rfl` was `simpa`.
    simp; rfl
    -- âŠ¢ Basis.toMatrix b' (â†‘b âˆ˜ â†‘(Basis.indexEquiv b b').symm) i j = Basis.toMatrix  â€¦
          -- ğŸ‰ no goals
  classical
  rw [â† (b.reindex (b.indexEquiv b')).toMatrix_map_vecMul b', discr_of_matrix_vecMul,
    â† one_mul (discr â„š b), Basis.coe_reindex, discr_reindex]
  congr
  have hint : IsIntegral â„¤ ((b.reindex (b.indexEquiv b')).toMatrix b').det :=
    IsIntegral.det fun i j => h _ _
  obtain âŸ¨r, hrâŸ© := IsIntegrallyClosed.isIntegral_iff.1 hint
  have hunit : IsUnit r := by
    have : IsIntegral â„¤ (b'.toMatrix (b.reindex (b.indexEquiv b'))).det :=
      IsIntegral.det fun i j => h' _ _
    obtain âŸ¨r', hr'âŸ© := IsIntegrallyClosed.isIntegral_iff.1 this
    refine' isUnit_iff_exists_inv.2 âŸ¨r', _âŸ©
    suffices algebraMap â„¤ â„š (r * r') = 1 by
      rw [â† RingHom.map_one (algebraMap â„¤ â„š)] at this
      exact (IsFractionRing.injective â„¤ â„š) this
    rw [RingHom.map_mul, hr, hr', â† det_mul, Basis.toMatrix_mul_toMatrix_flip, det_one]
  rw [â† RingHom.map_one (algebraMap â„¤ â„š), â† hr]
  cases' Int.isUnit_iff.1 hunit with hp hm
  Â· simp [hp]
  Â· simp [hm]
#align algebra.discr_eq_discr_of_to_matrix_coeff_is_integral Algebra.discr_eq_discr_of_toMatrix_coeff_isIntegral

/-- Let `K` be the fraction field of an integrally closed domain `R` and let `L` be a finite
separable extension of `K`. Let `B : PowerBasis K L` be such that `IsIntegral R B.gen`.
Then for all, `z : L` that are integral over `R`, we have
`(discr K B.basis) â€¢ z âˆˆ adjoin R ({B.gen} : set L)`. -/
theorem discr_mul_isIntegral_mem_adjoin [IsDomain R] [IsSeparable K L] [IsIntegrallyClosed R]
    [IsFractionRing R K] {B : PowerBasis K L} (hint : IsIntegral R B.gen) {z : L}
    (hz : IsIntegral R z) : discr K B.basis â€¢ z âˆˆ adjoin R ({B.gen} : Set L) := by
  have hinv : IsUnit (traceMatrix K B.basis).det := by
    simpa [â† discr_def] using discr_isUnit_of_basis _ B.basis
  have H :
    (traceMatrix K B.basis).det â€¢ (traceMatrix K B.basis).mulVec (B.basis.equivFun z) =
      (traceMatrix K B.basis).det â€¢ fun i => trace K L (z * B.basis i) :=
    by congr; exact traceMatrix_of_basis_mulVec _ _
  have cramer := mulVec_cramer (traceMatrix K B.basis) fun i => trace K L (z * B.basis i)
  -- âŠ¢ discr K â†‘B.basis â€¢ z âˆˆ adjoin R {B.gen}
  suffices âˆ€ i, ((traceMatrix K B.basis).det â€¢ B.basis.equivFun z) i âˆˆ (âŠ¥ : Subalgebra R K) by
    rw [â† B.basis.sum_repr z, Finset.smul_sum]
    refine' Subalgebra.sum_mem _ fun i _ => _
    replace this := this i
    rw [â† discr_def, Pi.smul_apply, mem_bot] at this
    obtain âŸ¨r, hrâŸ© := this
    rw [Basis.equivFun_apply] at hr
    rw [â† smul_assoc, â† hr, algebraMap_smul]
    refine' Subalgebra.smul_mem _ _ _
    rw [B.basis_eq_pow i]
    refine' Subalgebra.pow_mem _ (subset_adjoin (Set.mem_singleton _)) _
  intro i
  -- âŠ¢ (det (traceMatrix K â†‘B.basis) â€¢ â†‘(Basis.equivFun B.basis) z) i âˆˆ âŠ¥
  rw [â† H, â† mulVec_smul] at cramer
  -- âŠ¢ (det (traceMatrix K â†‘B.basis) â€¢ â†‘(Basis.equivFun B.basis) z) i âˆˆ âŠ¥
  replace cramer := congr_arg (mulVec (traceMatrix K B.basis)â»Â¹) cramer
  -- âŠ¢ (det (traceMatrix K â†‘B.basis) â€¢ â†‘(Basis.equivFun B.basis) z) i âˆˆ âŠ¥
  rw [mulVec_mulVec, nonsing_inv_mul _ hinv, mulVec_mulVec, nonsing_inv_mul _ hinv, one_mulVec,
    one_mulVec] at cramer
  rw [â† congr_fun cramer i, cramer_apply, det_apply]
  -- âŠ¢ âˆ‘ Ïƒ : Equiv.Perm (Fin B.dim), â†‘Equiv.Perm.sign Ïƒ â€¢ âˆ i_1 : Fin B.dim, update â€¦
  refine'
    Subalgebra.sum_mem _ fun Ïƒ _ => Subalgebra.zsmul_mem _ (Subalgebra.prod_mem _ fun j _ => _) _
  by_cases hji : j = i
  -- âŠ¢ updateColumn (traceMatrix K â†‘B.basis) i (fun i => â†‘(trace K L) (z * â†‘B.basis â€¦
  Â· simp only [updateColumn_apply, hji, eq_self_iff_true, PowerBasis.coe_basis]
    -- âŠ¢ (if True then â†‘(trace K L) (z * B.gen ^ â†‘(â†‘Ïƒ i)) else traceMatrix K (fun i = â€¦
    exact
      mem_bot.2
        (IsIntegrallyClosed.isIntegral_iff.1 <|
          isIntegral_trace <| isIntegral_mul hz <| IsIntegral.pow hint _)
  Â· simp only [updateColumn_apply, hji, PowerBasis.coe_basis]
    -- âŠ¢ (if False then â†‘(trace K L) (z * B.gen ^ â†‘(â†‘Ïƒ j)) else traceMatrix K (fun i  â€¦
    exact
      mem_bot.2
        (IsIntegrallyClosed.isIntegral_iff.1 <|
          isIntegral_trace <| isIntegral_mul (IsIntegral.pow hint _) (IsIntegral.pow hint _))
#align algebra.discr_mul_is_integral_mem_adjoin Algebra.discr_mul_isIntegral_mem_adjoin

end Integral

end Field

section Int

/-- Two (finite) â„¤-bases have the same discriminant. -/
theorem discr_eq_discr [Fintype Î¹] (b : Basis Î¹ â„¤ A) (b' : Basis Î¹ â„¤ A) :
    Algebra.discr â„¤ b = Algebra.discr â„¤ b' := by
  convert Algebra.discr_of_matrix_vecMul b' (b'.toMatrix b)
  -- âŠ¢ â†‘b = vecMul (â†‘b') (Matrix.map (Basis.toMatrix b' â†‘b) â†‘(algebraMap â„¤ A))
  Â· rw [Basis.toMatrix_map_vecMul]
    -- ğŸ‰ no goals
  Â· suffices IsUnit (b'.toMatrix b).det by
      rw [Int.isUnit_iff, â† sq_eq_one_iff] at this
      rw [this, one_mul]
    rw [â† LinearMap.toMatrix_id_eq_basis_toMatrix b b']
    -- âŠ¢ IsUnit (det (â†‘(LinearMap.toMatrix b b') LinearMap.id))
    exact LinearEquiv.isUnit_det (LinearEquiv.refl â„¤ A) b b'
    -- ğŸ‰ no goals

end Int

end Discr

end Algebra
