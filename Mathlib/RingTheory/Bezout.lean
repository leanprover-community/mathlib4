/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.RingTheory.PrincipalIdealDomain
import Mathlib.Algebra.GCDMonoid.IntegrallyClosed

#align_import ring_theory.bezout from "leanprover-community/mathlib"@"6623e6af705e97002a9054c1c05a980180276fc1"

/-!

# B√©zout rings

A B√©zout ring (Bezout ring) is a ring whose finitely generated ideals are principal.
Notable examples include principal ideal rings, valuation rings, and the ring of algebraic integers.

## Main results
- `IsBezout.iff_span_pair_isPrincipal`: It suffices to verify every `span {x, y}` is principal.
- `IsBezout.toGCDDomain`: Every B√©zout domain is a GCD domain. This is not an instance.
- `IsBezout.TFAE`: For a B√©zout domain, noetherian ‚Üî PID ‚Üî UFD ‚Üî ACCP

-/


universe u v

variable (R : Type u) [CommRing R]

/-- A B√©zout ring is a ring whose finitely generated ideals are principal. -/
class IsBezout : Prop where
  /-- Any finitely generated ideal is principal. -/
  isPrincipal_of_FG : ‚àÄ I : Ideal R, I.FG ‚Üí I.IsPrincipal
#align is_bezout IsBezout

namespace IsBezout

variable {R}

instance span_pair_isPrincipal [IsBezout R] (x y : R) : (Ideal.span {x, y} : Ideal R).IsPrincipal :=
  by classical exact isPrincipal_of_FG (Ideal.span {x, y}) ‚ü®{x, y}, by simp‚ü©
     -- üéâ no goals
#align is_bezout.span_pair_is_principal IsBezout.span_pair_isPrincipal

theorem iff_span_pair_isPrincipal :
    IsBezout R ‚Üî ‚àÄ x y : R, (Ideal.span {x, y} : Ideal R).IsPrincipal := by
  classical
    constructor
    ¬∑ intro H x y; infer_instance
    ¬∑ intro H
      constructor
      apply Submodule.fg_induction
      ¬∑ exact fun _ => ‚ü®‚ü®_, rfl‚ü©‚ü©
      ¬∑ rintro _ _ ‚ü®‚ü®x, rfl‚ü©‚ü© ‚ü®‚ü®y, rfl‚ü©‚ü©; rw [‚Üê Submodule.span_insert]; exact H _ _
#align is_bezout.iff_span_pair_is_principal IsBezout.iff_span_pair_isPrincipal

section Gcd

variable [IsBezout R]

/-- The gcd of two elements in a bezout domain. -/
noncomputable def gcd (x y : R) : R := Submodule.IsPrincipal.generator (Ideal.span {x, y})
#align is_bezout.gcd IsBezout.gcd

theorem span_gcd (x y : R) : (Ideal.span {gcd x y} : Ideal R) = Ideal.span {x, y} :=
  Ideal.span_singleton_generator _
#align is_bezout.span_gcd IsBezout.span_gcd

theorem gcd_dvd_left (x y : R) : gcd x y ‚à£ x :=
  (Submodule.IsPrincipal.mem_iff_generator_dvd _).mp (Ideal.subset_span (by simp))
                                                                            -- üéâ no goals
#align is_bezout.gcd_dvd_left IsBezout.gcd_dvd_left

theorem gcd_dvd_right (x y : R) : gcd x y ‚à£ y :=
  (Submodule.IsPrincipal.mem_iff_generator_dvd _).mp (Ideal.subset_span (by simp))
                                                                            -- üéâ no goals
#align is_bezout.gcd_dvd_right IsBezout.gcd_dvd_right

theorem dvd_gcd {x y z : R} (hx : z ‚à£ x) (hy : z ‚à£ y) : z ‚à£ gcd x y := by
  rw [‚Üê Ideal.span_singleton_le_span_singleton] at hx hy ‚ä¢
  -- ‚ä¢ Ideal.span {gcd x y} ‚â§ Ideal.span {z}
  rw [span_gcd, Ideal.span_insert, sup_le_iff]
  -- ‚ä¢ Ideal.span {x} ‚â§ Ideal.span {z} ‚àß Ideal.span {y} ‚â§ Ideal.span {z}
  exact ‚ü®hx, hy‚ü©
  -- üéâ no goals
#align is_bezout.dvd_gcd IsBezout.dvd_gcd

theorem gcd_eq_sum (x y : R) : ‚àÉ a b : R, a * x + b * y = gcd x y :=
  Ideal.mem_span_pair.mp (by rw [‚Üê span_gcd]; apply Ideal.subset_span; simp)
                             -- ‚ä¢ gcd x y ‚àà Ideal.span {gcd x y}
                                              -- ‚ä¢ gcd x y ‚àà {gcd x y}
                                                                       -- üéâ no goals
#align is_bezout.gcd_eq_sum IsBezout.gcd_eq_sum

variable (R)

/-- Any bezout domain is a GCD domain. This is not an instance since `GCDMonoid` contains data,
and this might not be how we would like to construct it. -/
noncomputable def toGCDDomain [IsDomain R] [DecidableEq R] : GCDMonoid R :=
  gcdMonoidOfGCD gcd gcd_dvd_left gcd_dvd_right fun hac hab => dvd_gcd hac hab
#align is_bezout.to_gcd_domain IsBezout.toGCDDomain

end Gcd

attribute [local instance] toGCDDomain

-- Note that the proof depends on the `local attribute [instance]` above, and is thus necessary to
-- be stated.
instance (priority := 100) [IsDomain R] [IsBezout R] : IsIntegrallyClosed R := by
  classical exact GCDMonoid.toIsIntegrallyClosed
  -- üéâ no goals

theorem _root_.Function.Surjective.isBezout {S : Type v} [CommRing S] (f : R ‚Üí+* S)
    (hf : Function.Surjective f) [IsBezout R] : IsBezout S := by
  rw [iff_span_pair_isPrincipal]
  -- ‚ä¢ ‚àÄ (x y : S), Submodule.IsPrincipal (Ideal.span {x, y})
  intro x y
  -- ‚ä¢ Submodule.IsPrincipal (Ideal.span {x, y})
  obtain ‚ü®‚ü®x, rfl‚ü©, ‚ü®y, rfl‚ü©‚ü© := hf x, hf y
  -- ‚ä¢ Submodule.IsPrincipal (Ideal.span {‚Üëf x, ‚Üëf y})
  use f (gcd x y)
  -- ‚ä¢ Ideal.span {‚Üëf x, ‚Üëf y} = Submodule.span S {‚Üëf (gcd x y)}
  trans Ideal.map f (Ideal.span {gcd x y})
  -- ‚ä¢ Ideal.span {‚Üëf x, ‚Üëf y} = Ideal.map f (Ideal.span {gcd x y})
  ¬∑ rw [span_gcd, Ideal.map_span, Set.image_insert_eq, Set.image_singleton]
    -- üéâ no goals
  ¬∑ rw [Ideal.map_span, Set.image_singleton]; rfl
    -- ‚ä¢ Ideal.span {‚Üëf (gcd x y)} = Submodule.span S {‚Üëf (gcd x y)}
                                              -- üéâ no goals
#align function.surjective.is_bezout Function.Surjective.isBezout

instance (priority := 100) of_isPrincipalIdealRing [IsPrincipalIdealRing R] : IsBezout R :=
  ‚ü®fun I _ => IsPrincipalIdealRing.principal I‚ü©
#align is_bezout.of_is_principal_ideal_ring IsBezout.of_isPrincipalIdealRing

theorem TFAE [IsBezout R] [IsDomain R] :
    List.TFAE
    [IsNoetherianRing R, IsPrincipalIdealRing R, UniqueFactorizationMonoid R, WfDvdMonoid R] := by
  classical
    tfae_have 1 ‚Üí 2
    ¬∑ intro H; exact ‚ü®fun I => isPrincipal_of_FG _ (IsNoetherian.noetherian _)‚ü©
    tfae_have 2 ‚Üí 3
    ¬∑ intro; infer_instance
    tfae_have 3 ‚Üí 4
    ¬∑ intro; infer_instance
    tfae_have 4 ‚Üí 1
    ¬∑ rintro ‚ü®h‚ü©
      rw [isNoetherianRing_iff, isNoetherian_iff_fg_wellFounded]
      apply RelEmbedding.wellFounded _ h
      have : ‚àÄ I : { J : Ideal R // J.FG }, ‚àÉ x : R, (I : Ideal R) = Ideal.span {x} :=
        fun ‚ü®I, hI‚ü© => (IsBezout.isPrincipal_of_FG I hI).1
      choose f hf using this
      exact
        { toFun := f
          inj' := fun x y e => by ext1; rw [hf, hf, e]
          map_rel_iff' := by
            dsimp
            intro a b
            rw [‚Üê Ideal.span_singleton_lt_span_singleton, ‚Üê hf, ‚Üê hf]
            rfl }
    tfae_finish
#align is_bezout.tfae IsBezout.TFAE

end IsBezout
