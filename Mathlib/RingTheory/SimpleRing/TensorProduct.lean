/-
Copyright (c) 2025 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Yunzhou Xie
-/
module

public import Mathlib.Algebra.Central.Basic
public import Mathlib.LinearAlgebra.Basis.VectorSpace
public import Mathlib.LinearAlgebra.Finsupp.LinearCombination
public import Mathlib.RingTheory.Flat.Basic
public import Mathlib.RingTheory.TwoSidedIdeal.Operations
public import Mathlib.RingTheory.Flat.FaithfullyFlat.Basic
public import Mathlib.RingTheory.Henselian
public import Mathlib.RingTheory.TwoSidedIdeal.Instances

/-!
# Tensor product of simple algebras over a field

In this file, we show that the tensor product of a simple algebra and a central simple algebra is
simple, which in particular implies that the tensor product of two central simple algebras is
another central simple algebra. This is a prerequisite for defining the group law on the Brauer
group.

## Main Results

* `TensorProduct.simple`: The tensor product of a simple algebra and a central simple algebra
  is simple.

## References

* [StackProject 074B](https://stacks.math.columbia.edu/tag/074B)

## Tags
Noncommutative algebra, tensor product, simple algebra, central simple algebra

-/

@[expose] public section

variable (K A B : Type*) [Field K] [Ring A] [Algebra K A] [Ring B] [Algebra K B]

open TensorProduct Module

open TwoSidedIdeal in
@[stacks 074B]
lemma TwoSidedIdeal.eq_bot_of_map_comap_eq_bot [hA : IsSimpleRing A]
    [isCentral_A : Algebra.IsCentral K A] (I : TwoSidedIdeal (A âŠ—[K] B))
    (hAB : letI f : B â†’â‚[K] A âŠ—[K] B := Algebra.TensorProduct.includeRight
      (I.comap f).map f = âŠ¥) : I = âŠ¥ := by
  set f : B â†’â‚[K] A âŠ—[K] B := Algebra.TensorProduct.includeRight
  obtain âŸ¨Î¹, ğ“‘âŸ© := Module.Free.exists_basis K B
  have main (s : Finset Î¹) (a : Î¹ â†’ A) (h : âˆ‘ i âˆˆ s, a i âŠ—â‚œ[K] ğ“‘ i âˆˆ I) :
      âˆ€ i âˆˆ s, a i = 0 := by
    classical
    induction s using Finset.induction_on generalizing a with
    | empty => simp
    | insert j s hjs ih =>
    rcases eq_or_ne (a j) 0 with hj | hj
    Â· aesop
    Â· rw [Finset.sum_insert hjs] at h
      have : span {a j} = âŠ¤ := hA.1.2 _|>.resolve_left fun h â†¦ hj <| (mem_bot A).mp <|
        (SetLike.ext_iff.mp h (a j)).mp <| subset_span (by simp)
      have h' : âˆ€ (x : A) (hx : x âˆˆ span {a j}), âˆƒ (Î¹ : Type) (_ : Fintype Î¹) (xL : Î¹ â†’ A)
          (xR : Î¹ â†’ A), x = âˆ‘ i, xL i * a j * xR i := fun x hx â†¦ by
        induction hx using span_induction with
        | mem x h => exact âŸ¨PUnit, inferInstance, fun _ â†¦ 1, fun _ â†¦ 1, by simp_allâŸ©
        | zero => exact âŸ¨Empty, inferInstance, fun _ â†¦ 1, fun _ â†¦ 1, by simpâŸ©
        | add x y hx hy hx1 hy1 =>
          obtain âŸ¨Î¹1, _, xL1, xR1, eq1âŸ© := hx1
          obtain âŸ¨Î¹2, _, xL2, xR2, eq2âŸ© := hy1
          exact âŸ¨(Î¹1 âŠ• Î¹2), inferInstance, Sum.elim xL1 xL2, Sum.elim xR1 xR2, by simp [eq1, eq2]âŸ©
        | neg x hx hx1 =>
          obtain âŸ¨Î¹, _, xL, xR, eqâŸ© := hx1
          exact âŸ¨Î¹, inferInstance, fun i â†¦ - (xL i), xR, by simp [eq]âŸ©
        | left_absorb a x hx hx1 =>
          obtain âŸ¨Î¹, _, xL, xR, eqâŸ© := hx1
          exact âŸ¨Î¹, inferInstance, fun i â†¦ a * xL i, xR, by simp [eq, Finset.mul_sum, â† mul_assoc]âŸ©
        | right_absorb b x hx hx1 =>
          obtain âŸ¨Î¹, _, xL, xR, eqâŸ© := hx1
          exact âŸ¨Î¹, inferInstance, xL, fun i â†¦ xR i * b, by simp [eq, Finset.sum_mul, â† mul_assoc]âŸ©
      obtain âŸ¨Î¹', _, xL, xR, eq1âŸ© := h' 1 (by simp_all)
      let T' := âˆ‘ i, xL i âŠ—â‚œ 1 * (a j âŠ—â‚œ[K] ğ“‘ j + âˆ‘ x âˆˆ s, a x âŠ—â‚œ[K] ğ“‘ x) * xR i âŠ—â‚œ 1
      have hT'1 : T' âˆˆ I := sum_mem <| fun _ _ â†¦ I.mul_mem_right _ _ <| I.mul_mem_left _ _ h
      have hT'2 : T' = 1 âŠ—â‚œ ğ“‘ j + âˆ‘ j âˆˆ s, (âˆ‘ i, xL i * a j * xR i) âŠ—â‚œ ğ“‘ j := by
        simp +zetaDelta only [mul_add, Algebra.TensorProduct.tmul_mul_tmul, one_mul, Finset.mul_sum,
          add_mul, mul_one, Finset.sum_mul, Finset.sum_add_distrib]
        rw [â† sum_tmul, â† eq1, Finset.sum_comm]
        simp_rw [â† sum_tmul]
      have hT'3 (x : A) : (x âŠ—â‚œ 1) * T' - T' * (x âŠ—â‚œ 1) = âˆ‘ j âˆˆ s, (x * (âˆ‘ i, (xL i * a j * xR i)) -
          (âˆ‘ i, xL i * a j * xR i) * x) âŠ—â‚œ ğ“‘ j := by
        simp [hT'2, mul_add, add_mul, Finset.mul_sum, Finset.sum_mul, â† Finset.sum_sub_distrib,
          â† sub_tmul]
      have hT'_mem (x : A) : (x âŠ—â‚œ 1) * T' - T' * (x âŠ—â‚œ 1) âˆˆ I :=
        I.sub_mem (I.mul_mem_left _ _ hT'1) (I.mul_mem_right _ _ hT'1)
      have : âˆ€ j âˆˆ s, âˆ‘ i, xL i * a j * xR i âˆˆ Subalgebra.center K A := fun j hj â†¦
        Subalgebra.mem_center_iff.2 fun x â†¦ by
        specialize ih (fun j â†¦ if j âˆˆ s then x * âˆ‘ i, xL i * a j * xR i -
          (âˆ‘ i, xL i * a j * xR i) * x else 0) <| by
          convert (hT'_mem x)
          rw [hT'3]
          congr! with i hi
          simp [hi]
        simp +contextual only [â†“reduceIte] at ih
        simpa [sub_eq_zero] using ih j hj
      simp_rw [isCentral_A.center_eq_bot, Algebra.mem_bot, Set.mem_range] at this
      choose k hk using this
      set key : B := ğ“‘ j + âˆ‘ i âˆˆ s.attach, k i i.2 â€¢ ğ“‘ i
      have hkey : key = 0 := by
        refine (map_eq_zero_iff _ (Algebra.TensorProduct.includeRight_injective <|
          FaithfulSMul.algebraMap_injective K A)).mp ?_
        refine eq_bot_iff.mp hAB <| TwoSidedIdeal.mem_map_of_mem <|
          (TwoSidedIdeal.mem_comap _).mpr ?_
        rw [â† Finset.sum_attach] at hT'2
        conv at hT'2 => enter [2, 2, 2, x]; rw [â† hk x.1 x.2]
        convert hT'1 using 1
        rw [hT'2, map_add]
        simp +zetaDelta [Algebra.algebraMap_eq_smul_one, â† smul_tmul']
      set g : Î¹ â†’ K := fun i â†¦ if h : i âˆˆ s then k i h else 1
      have hg : âˆ‘ i âˆˆ insert j s, g i â€¢ ğ“‘ i = 0 := by
        unfold g
        rw [Finset.sum_insert hjs, dif_neg hjs, one_smul, â† Finset.sum_attach]
        simp_rw [dif_pos (Subtype.prop _)]
        exact hkey
      have hb := linearIndependent_iff'.mp ğ“‘.linearIndependent (insert j s) g hg j
        (Finset.mem_insert_self _ _)
      simp [g, dif_neg hjs] at hb
  refine eq_bot_iff.mpr fun x hx â†¦ ?_
  obtain âŸ¨s, c, rflâŸ© := Submodule.mem_span_range_iff_exists.mp <|
    Submodule.eq_top_iff'.mp (ğ“‘.baseChange A).span_eq x
  specialize main s c (by simpa [â† TensorProduct.tmul_eq_smul_one_tmul] using hx)
  simp +contextual [main]

lemma TensorProduct.map_comap_eq [IsSimpleRing A] [Algebra.IsCentral K A] [hB : IsSimpleRing B]
    (I : TwoSidedIdeal (A âŠ—[K] B)) :
    letI f : B â†’â‚[K] A âŠ—[K] B := Algebra.TensorProduct.includeRight
    (I.comap f).map f = I := by
  let f : B â†’â‚[K] A âŠ—[K] B := Algebra.TensorProduct.includeRight
  refine (le_antisymm ?_ ?_).symm
  Â· obtain rfl | I_ne_bot := eq_or_ne I âŠ¥
    Â· exact bot_le
    rw [TwoSidedIdeal.map]
    have hI : I.comap f = âŠ¤ := hB.1.2 _ |>.resolve_left fun r => by
      refine I_ne_bot <| TwoSidedIdeal.eq_bot_of_map_comap_eq_bot (hAB := ?_)
      rw [r, TwoSidedIdeal.map_bot]
    rw [hI, TwoSidedIdeal.coe_top, TwoSidedIdeal.le_iff]
    rintro x -
    rw [SetLike.mem_coe]
    induction x using TensorProduct.induction_on with
    | zero => simp
    | tmul a b =>
      rw [show a âŠ—â‚œ[K] b = (a âŠ—â‚œ 1) * (1 âŠ—â‚œ b) by simp]
      exact TwoSidedIdeal.mul_mem_left _ _ _ <| TwoSidedIdeal.subset_span âŸ¨b, âŸ¨âŸ©, rflâŸ©
    | add x y hx hy => exact TwoSidedIdeal.add_mem _ hx hy
  Â· rw [TwoSidedIdeal.map, TwoSidedIdeal.span_le]
    rintro _ âŸ¨x, hx, rflâŸ©
    rw [SetLike.mem_coe, TwoSidedIdeal.mem_comap] at hx
    exact hx

/-- This is slightly more general than stacks 074C which generalizes "skew field"
  to "simple ring". -/
@[stacks 074C]
instance TensorProduct.simple {A B : Type*} [Ring A] [IsSimpleRing A] [Algebra K A] [Ring B]
    [Algebra K B] [Algebra.IsCentral K A] [isSimple_B : IsSimpleRing B] :
    IsSimpleRing (A âŠ—[K] B) := by
  let f : B â†’â‚[K] A âŠ—[K] B := Algebra.TensorProduct.includeRight
  refine âŸ¨âŸ¨fun I â†¦ ?_âŸ©âŸ©
  rcases isSimple_B.1.2 (I.comap f) with h | h
  Â· left
    rw [â† TensorProduct.map_comap_eq K _ _ I, h, TwoSidedIdeal.map, TwoSidedIdeal.span_eq_bot]
    simp
  Â· right
    rw [â† TwoSidedIdeal.one_mem_iff, â† TensorProduct.map_comap_eq K _ _ I, h,
      TwoSidedIdeal.map]
    exact TwoSidedIdeal.subset_span âŸ¨1, by simp [Algebra.TensorProduct.one_def]âŸ©
