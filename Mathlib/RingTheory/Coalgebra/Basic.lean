/-
Copyright (c) 2023 Ali Ramsey. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ali Ramsey, Eric Wieser
-/
import Mathlib.LinearAlgebra.TensorProduct.Finiteness
import Mathlib.LinearAlgebra.TensorProduct.Associator

/-!
# Coalgebras

In this file we define `Coalgebra`, and provide instances for:

* Commutative semirings: `CommSemiring.toCoalgebra`
* Binary products: `Prod.instCoalgebra`
* Finitely supported functions: `DFinsupp.instCoalgebra`, `Finsupp.instCoalgebra`

## References

* <https://en.wikipedia.org/wiki/Coalgebra>
-/

universe u v w

open scoped TensorProduct

/-- Data fields for `Coalgebra`, to allow API to be constructed before proving `Coalgebra.coassoc`.

See `Coalgebra` for documentation. -/
class CoalgebraStruct (R : Type u) (A : Type v)
    [CommSemiring R] [AddCommMonoid A] [Module R A] where
  /-- The comultiplication of the coalgebra -/
  comul : A â†’â‚—[R] A âŠ—[R] A
  /-- The counit of the coalgebra -/
  counit : A â†’â‚—[R] R

/--
A representation of an element `a` of a coalgebra `A` is a finite sum of pure tensors `âˆ‘ xáµ¢ âŠ— yáµ¢`
that is equal to `comul a`.
-/
structure Coalgebra.Repr (R : Type u) {A : Type v}
    [CommSemiring R] [AddCommMonoid A] [Module R A] [CoalgebraStruct R A] (a : A) where
  /-- the indexing type of a representation of `comul a` -/
  {Î¹ : Type*}
  /-- the finite indexing set of a representation of `comul a` -/
  (index : Finset Î¹)
  /-- the first coordinate of a representation of `comul a` -/
  (left : Î¹ â†’ A)
  /-- the second coordinate of a representation of `comul a` -/
  (right : Î¹ â†’ A)
  /-- `comul a` is equal to a finite sum of some pure tensors -/
  (eq : âˆ‘ i âˆˆ index, left i âŠ—â‚œ[R] right i = CoalgebraStruct.comul a)

/-- An arbitrarily chosen representation. -/
noncomputable def Coalgebra.Repr.arbitrary (R : Type u) {A : Type v}
    [CommSemiring R] [AddCommMonoid A] [Module R A] [CoalgebraStruct R A] (a : A) :
    Coalgebra.Repr R a where
  left := Prod.fst
  right := Prod.snd
  index := TensorProduct.exists_finset (R := R) (CoalgebraStruct.comul a) |>.choose
  eq := TensorProduct.exists_finset (R := R) (CoalgebraStruct.comul a) |>.choose_spec.symm

@[inherit_doc Coalgebra.Repr.arbitrary]
scoped[Coalgebra] notation "â„›" => Coalgebra.Repr.arbitrary

namespace Coalgebra
export CoalgebraStruct (comul counit)
end Coalgebra

/-- A coalgebra over a commutative (semi)ring `R` is an `R`-module equipped with a coassociative
comultiplication `Î”` and a counit `Îµ` obeying the left and right counitality laws. -/
class Coalgebra (R : Type u) (A : Type v)
    [CommSemiring R] [AddCommMonoid A] [Module R A] extends CoalgebraStruct R A where
  /-- The comultiplication is coassociative -/
  coassoc : TensorProduct.assoc R A A A âˆ˜â‚— comul.rTensor A âˆ˜â‚— comul = comul.lTensor A âˆ˜â‚— comul
  /-- The counit satisfies the left counitality law -/
  rTensor_counit_comp_comul : counit.rTensor A âˆ˜â‚— comul = TensorProduct.mk R _ _ 1
  /-- The counit satisfies the right counitality law -/
  lTensor_counit_comp_comul : counit.lTensor A âˆ˜â‚— comul = (TensorProduct.mk R _ _).flip 1

namespace Coalgebra
variable {R : Type u} {A : Type v}
variable [CommSemiring R] [AddCommMonoid A] [Module R A] [Coalgebra R A] {a : A}

@[simp]
theorem coassoc_apply (a : A) :
    TensorProduct.assoc R A A A (comul.rTensor A (comul a)) = comul.lTensor A (comul a) :=
  LinearMap.congr_fun coassoc a

@[simp]
theorem coassoc_symm_apply (a : A) :
    (TensorProduct.assoc R A A A).symm (comul.lTensor A (comul a)) = comul.rTensor A (comul a) := by
  rw [(TensorProduct.assoc R A A A).symm_apply_eq, coassoc_apply a]

@[simp]
theorem coassoc_symm :
    (TensorProduct.assoc R A A A).symm âˆ˜â‚— comul.lTensor A âˆ˜â‚— comul =
    comul.rTensor A âˆ˜â‚— (comul (R := R)) :=
  LinearMap.ext coassoc_symm_apply

@[simp]
theorem rTensor_counit_comul (a : A) : counit.rTensor A (comul a) = 1 âŠ—â‚œ[R] a :=
  LinearMap.congr_fun rTensor_counit_comp_comul a

@[simp]
theorem lTensor_counit_comul (a : A) : counit.lTensor A (comul a) = a âŠ—â‚œ[R] 1 :=
  LinearMap.congr_fun lTensor_counit_comp_comul a

@[simp]
lemma sum_counit_tmul_eq {a : A} (repr : Coalgebra.Repr R a) :
    âˆ‘ i âˆˆ repr.index, counit (R := R) (repr.left i) âŠ—â‚œ (repr.right i) = 1 âŠ—â‚œ[R] a := by
  simpa [â† repr.eq, map_sum] using congr($(rTensor_counit_comp_comul (R := R) (A := A)) a)

@[simp]
lemma sum_tmul_counit_eq {a : A} (repr : Coalgebra.Repr R a) :
    âˆ‘ i âˆˆ repr.index, (repr.left i) âŠ—â‚œ counit (R := R) (repr.right i) = a âŠ—â‚œ[R] 1 := by
  simpa [â† repr.eq, map_sum] using congr($(lTensor_counit_comp_comul (R := R) (A := A)) a)

-- Cannot be @[simp] because `aâ‚‚` cannot be inferred by `simp`.
lemma sum_tmul_tmul_eq {a : A} (repr : Repr R a)
    (aâ‚ : (i : repr.Î¹) â†’ Repr R (repr.left i)) (aâ‚‚ : (i : repr.Î¹) â†’ Repr R (repr.right i)) :
    âˆ‘ i âˆˆ repr.index, âˆ‘ j âˆˆ (aâ‚ i).index,
      (aâ‚ i).left j âŠ—â‚œ[R] ((aâ‚ i).right j âŠ—â‚œ[R] repr.right i)
      = âˆ‘ i âˆˆ repr.index, âˆ‘ j âˆˆ (aâ‚‚ i).index,
      repr.left i âŠ—â‚œ[R] ((aâ‚‚ i).left j âŠ—â‚œ[R] (aâ‚‚ i).right j) := by
  simpa [(aâ‚‚ _).eq, â† (aâ‚ _).eq, â† TensorProduct.tmul_sum,
    TensorProduct.sum_tmul, â† repr.eq] using congr($(coassoc (R := R)) a)

@[simp]
theorem sum_counit_tmul_map_eq {B : Type*} [AddCommMonoid B] [Module R B]
    {F : Type*} [FunLike F A B] [LinearMapClass F R A B] (f : F) (a : A) {repr : Repr R a} :
    âˆ‘ i âˆˆ repr.index, counit (R := R) (repr.left i) âŠ—â‚œ f (repr.right i) = 1 âŠ—â‚œ[R] f a := by
  have := sum_counit_tmul_eq repr
  apply_fun LinearMap.lTensor R (f : A â†’â‚—[R] B) at this
  simp_all only [map_sum, LinearMap.lTensor_tmul, LinearMap.coe_coe]

@[simp]
theorem sum_map_tmul_counit_eq {B : Type*} [AddCommMonoid B] [Module R B]
    {F : Type*} [FunLike F A B] [LinearMapClass F R A B] (f : F) (a : A) {repr : Repr R a} :
    âˆ‘ i âˆˆ repr.index, f (repr.left i) âŠ—â‚œ counit (R := R) (repr.right i) = f a âŠ—â‚œ[R] 1 := by
  have := sum_tmul_counit_eq repr
  apply_fun LinearMap.rTensor R (f : A â†’â‚—[R] B) at this
  simp_all only [map_sum, LinearMap.rTensor_tmul, LinearMap.coe_coe]

-- Cannot be @[simp] because `aâ‚` cannot be inferred by `simp`.
theorem sum_map_tmul_tmul_eq {B : Type*} [AddCommMonoid B] [Module R B]
    {F : Type*} [FunLike F A B] [LinearMapClass F R A B] (f g h : F) (a : A) {repr : Repr R a}
    {aâ‚ : (i : repr.Î¹) â†’ Repr R (repr.left i)} {aâ‚‚ : (i : repr.Î¹) â†’ Repr R (repr.right i)} :
    âˆ‘ i âˆˆ repr.index, âˆ‘ j âˆˆ (aâ‚‚ i).index,
      f (repr.left i) âŠ—â‚œ (g ((aâ‚‚ i).left j) âŠ—â‚œ h ((aâ‚‚ i).right j)) =
    âˆ‘ i âˆˆ repr.index, âˆ‘ j âˆˆ (aâ‚ i).index,
      f ((aâ‚ i).left j) âŠ—â‚œ[R] (g ((aâ‚ i).right j) âŠ—â‚œ[R] h (repr.right i)) := by
  have := sum_tmul_tmul_eq repr aâ‚ aâ‚‚
  apply_fun TensorProduct.map (f : A â†’â‚—[R] B)
    (TensorProduct.map (g : A â†’â‚—[R] B) (h : A â†’â‚—[R] B)) at this
  simp_all only [map_sum, TensorProduct.map_tmul, LinearMap.coe_coe]

lemma sum_counit_smul (ğ“¡ : Repr R a) :
    âˆ‘ x âˆˆ ğ“¡.index, counit (R := R) (ğ“¡.left x) â€¢ ğ“¡.right x = a := by
  simpa only [map_sum, TensorProduct.lift.tmul, LinearMap.lsmul_apply, one_smul]
    using congr(TensorProduct.lift (LinearMap.lsmul R A) $(sum_counit_tmul_eq (R := R) ğ“¡))

lemma lift_lsmul_comp_counit_comp_comul :
    TensorProduct.lift (.lsmul R A âˆ˜â‚— counit) âˆ˜â‚— comul = .id := by
  have := rTensor_counit_comp_comul (R := R) (A := A)
  apply_fun (TensorProduct.lift (LinearMap.lsmul R A) âˆ˜â‚— Â·) at this
  rw [LinearMap.rTensor, â† LinearMap.comp_assoc, TensorProduct.lift_comp_map, LinearMap.complâ‚‚_id]
    at this
  ext
  simp [this]

variable (R A) in
/-- A coalgebra `A` is cocommutative if its comultiplication `Î´ : A â†’ A âŠ— A` commutes with the
swapping `Î² : A âŠ— A â‰ƒ A âŠ— A` of the factors in the tensor product. -/
class IsCocomm where
  protected comm_comp_comul : (TensorProduct.comm R A A).comp comul = comul

variable [IsCocomm R A]

variable (R A) in
@[simp] lemma comm_comp_comul : (TensorProduct.comm R A A).comp comul = comul :=
  IsCocomm.comm_comp_comul

variable (R) in
@[simp] lemma comm_comul (a : A) : TensorProduct.comm R A A (comul a) = comul a :=
  congr($(comm_comp_comul R A) a)

end Coalgebra

open Coalgebra

namespace CommSemiring
variable (R : Type u) [CommSemiring R]

/-- Every commutative (semi)ring is a coalgebra over itself, with `Î” r = 1 âŠ—â‚œ r`. -/
instance toCoalgebra : Coalgebra R R where
  comul := (TensorProduct.mk R R R) 1
  counit := .id
  coassoc := rfl
  rTensor_counit_comp_comul := by ext; rfl
  lTensor_counit_comp_comul := by ext; rfl

@[simp]
theorem comul_apply (r : R) : comul r = 1 âŠ—â‚œ[R] r := rfl

@[simp]
theorem counit_apply (r : R) : counit r = r := rfl

instance : IsCocomm R R where comm_comp_comul := by ext; simp

end CommSemiring

namespace Prod
variable (R : Type u) (A : Type v) (B : Type w)
variable [CommSemiring R] [AddCommMonoid A] [AddCommMonoid B] [Module R A] [Module R B]
variable [Coalgebra R A] [Coalgebra R B]

open LinearMap

instance instCoalgebraStruct : CoalgebraStruct R (A Ã— B) where
  comul := .coprod
    (TensorProduct.map (.inl R A B) (.inl R A B) âˆ˜â‚— comul)
    (TensorProduct.map (.inr R A B) (.inr R A B) âˆ˜â‚— comul)
  counit := .coprod counit counit

@[simp]
theorem comul_apply (r : A Ã— B) :
    comul r =
      TensorProduct.map (.inl R A B) (.inl R A B) (comul r.1) +
      TensorProduct.map (.inr R A B) (.inr R A B) (comul r.2) := rfl

@[simp]
theorem counit_apply (r : A Ã— B) : (counit r : R) = counit r.1 + counit r.2 := rfl

theorem comul_comp_inl :
    comul âˆ˜â‚— inl R A B = TensorProduct.map (.inl R A B) (.inl R A B) âˆ˜â‚— comul := by
  ext; simp

theorem comul_comp_inr :
    comul âˆ˜â‚— inr R A B = TensorProduct.map (.inr R A B) (.inr R A B) âˆ˜â‚— comul := by
  ext; simp

theorem comul_comp_fst :
    comul âˆ˜â‚— .fst R A B = TensorProduct.map (.fst R A B) (.fst R A B) âˆ˜â‚— comul := by
  ext : 1
  Â· rw [comp_assoc, fst_comp_inl, comp_id, comp_assoc, comul_comp_inl, â† comp_assoc,
      â† TensorProduct.map_comp, fst_comp_inl, TensorProduct.map_id, id_comp]
  Â· rw [comp_assoc, fst_comp_inr, comp_zero, comp_assoc, comul_comp_inr, â† comp_assoc,
      â† TensorProduct.map_comp, fst_comp_inr, TensorProduct.map_zero_left, zero_comp]

theorem comul_comp_snd :
    comul âˆ˜â‚— .snd R A B = TensorProduct.map (.snd R A B) (.snd R A B) âˆ˜â‚— comul := by
  ext : 1
  Â· rw [comp_assoc, snd_comp_inl, comp_zero, comp_assoc, comul_comp_inl, â† comp_assoc,
      â† TensorProduct.map_comp, snd_comp_inl, TensorProduct.map_zero_left, zero_comp]
  Â· rw [comp_assoc, snd_comp_inr, comp_id, comp_assoc, comul_comp_inr, â† comp_assoc,
      â† TensorProduct.map_comp, snd_comp_inr, TensorProduct.map_id, id_comp]

@[simp] theorem counit_comp_inr : counit âˆ˜â‚— inr R A B = counit := by ext; simp

@[simp] theorem counit_comp_inl : counit âˆ˜â‚— inl R A B = counit := by ext; simp

instance instCoalgebra : Coalgebra R (A Ã— B) where
  rTensor_counit_comp_comul := by
    ext : 1
    Â· rw [comp_assoc, comul_comp_inl, â† comp_assoc, rTensor_comp_map, counit_comp_inl,
        â† lTensor_comp_rTensor, comp_assoc, rTensor_counit_comp_comul, lTensor_comp_mk]
    Â· rw [comp_assoc, comul_comp_inr, â† comp_assoc, rTensor_comp_map, counit_comp_inr,
        â† lTensor_comp_rTensor, comp_assoc, rTensor_counit_comp_comul, lTensor_comp_mk]
  lTensor_counit_comp_comul := by
    ext : 1
    Â· rw [comp_assoc, comul_comp_inl, â† comp_assoc, lTensor_comp_map, counit_comp_inl,
        â† rTensor_comp_lTensor, comp_assoc, lTensor_counit_comp_comul, rTensor_comp_flip_mk]
    Â· rw [comp_assoc, comul_comp_inr, â† comp_assoc, lTensor_comp_map, counit_comp_inr,
        â† rTensor_comp_lTensor, comp_assoc, lTensor_counit_comp_comul, rTensor_comp_flip_mk]
  coassoc := by
    dsimp only [instCoalgebraStruct]
    ext x : 2 <;> dsimp only [comp_apply, LinearEquiv.coe_coe, coe_inl, coe_inr, coprod_apply]
    Â· simp only [map_zero, add_zero]
      simp_rw [â† comp_apply, â† comp_assoc, rTensor_comp_map, lTensor_comp_map, coprod_inl,
        â† map_comp_rTensor, â† map_comp_lTensor, comp_assoc, â† coassoc, â† comp_assoc,
        TensorProduct.map_map_comp_assoc_eq, comp_apply, LinearEquiv.coe_coe]
    Â· simp only [map_zero, zero_add]
      simp_rw [â† comp_apply, â† comp_assoc, rTensor_comp_map, lTensor_comp_map, coprod_inr,
        â† map_comp_rTensor, â† map_comp_lTensor, comp_assoc, â† coassoc, â† comp_assoc,
        TensorProduct.map_map_comp_assoc_eq, comp_apply, LinearEquiv.coe_coe]

instance [IsCocomm R A] [IsCocomm R B] : IsCocomm R (A Ã— B) where
  comm_comp_comul := by ext <;> simp [â† TensorProduct.map_comm]

end Prod

namespace DFinsupp
variable (R : Type u) (Î¹ : Type v) (A : Î¹ â†’ Type w)
variable [DecidableEq Î¹]
variable [CommSemiring R] [âˆ€ i, AddCommMonoid (A i)] [âˆ€ i, Module R (A i)] [âˆ€ i, Coalgebra R (A i)]

open LinearMap

instance instCoalgebraStruct : CoalgebraStruct R (Î â‚€ i, A i) where
  comul := DFinsupp.lsum R fun i =>
    TensorProduct.map (DFinsupp.lsingle i) (DFinsupp.lsingle i) âˆ˜â‚— comul
  counit := DFinsupp.lsum R fun _ => counit

@[simp]
theorem comul_single (i : Î¹) (a : A i) :
    comul (R := R) (DFinsupp.single i a) =
      (TensorProduct.map (DFinsupp.lsingle i) (DFinsupp.lsingle i) : _ â†’â‚—[R] _) (comul a) :=
  lsum_single _ _ _ _

@[simp]
theorem counit_single (i : Î¹) (a : A i) : counit (DFinsupp.single i a) = counit (R := R) a :=
  lsum_single _ _ _ _

theorem comul_comp_lsingle (i : Î¹) :
    comul âˆ˜â‚— (lsingle i : A i â†’â‚—[R] _) = TensorProduct.map (lsingle i) (lsingle i) âˆ˜â‚— comul := by
  ext; simp

theorem comul_comp_lapply (i : Î¹) :
    comul âˆ˜â‚— (lapply i : _ â†’â‚—[R] A i) = TensorProduct.map (lapply i) (lapply i) âˆ˜â‚— comul := by
  ext j : 1
  conv_rhs => rw [comp_assoc, comul_comp_lsingle, â† comp_assoc, â† TensorProduct.map_comp]
  obtain rfl | hij := eq_or_ne i j
  Â· rw [comp_assoc, lapply_comp_lsingle_same, comp_id, TensorProduct.map_id, id_comp]
  Â· rw [comp_assoc, lapply_comp_lsingle_of_ne _ _ hij, comp_zero, TensorProduct.map_zero_left,
      zero_comp]

@[simp] theorem counit_comp_lsingle (i : Î¹) : counit âˆ˜â‚— (lsingle i : A i â†’â‚—[R] _) = counit := by
  ext; simp

/-- The `R`-module whose elements are dependent functions `(i : Î¹) â†’ A i` which are zero on all but
finitely many elements of `Î¹` has a coalgebra structure.

The coproduct `Î”` is given by `Î”(fáµ¢ a) = fáµ¢ aâ‚ âŠ— fáµ¢ aâ‚‚` where `Î”(a) = aâ‚ âŠ— aâ‚‚` and the counit `Îµ`
by `Îµ(fáµ¢ a) = Îµ(a)`, where `fáµ¢ a` is the function sending `i` to `a` and all other elements of `Î¹`
to zero. -/
instance instCoalgebra : Coalgebra R (Î â‚€ i, A i) where
  rTensor_counit_comp_comul := by
    ext : 1
    rw [comp_assoc, comul_comp_lsingle, â† comp_assoc, rTensor_comp_map, counit_comp_lsingle,
      â† lTensor_comp_rTensor, comp_assoc, rTensor_counit_comp_comul, lTensor_comp_mk]
  lTensor_counit_comp_comul := by
    ext : 1
    rw [comp_assoc, comul_comp_lsingle, â† comp_assoc, lTensor_comp_map, counit_comp_lsingle,
      â† rTensor_comp_lTensor, comp_assoc, lTensor_counit_comp_comul, rTensor_comp_flip_mk]
  coassoc := by
    ext i : 1
    simp_rw [comp_assoc, comul_comp_lsingle, â† comp_assoc, lTensor_comp_map, comul_comp_lsingle,
      comp_assoc, â† comp_assoc comul, rTensor_comp_map, comul_comp_lsingle, â† map_comp_rTensor,
      â† map_comp_lTensor, comp_assoc, â† coassoc, â† comp_assoc comul, â† comp_assoc,
        TensorProduct.map_map_comp_assoc_eq]

instance instIsCocomm [âˆ€ i, IsCocomm R (A i)] : IsCocomm R (Î â‚€ i, A i) where
  comm_comp_comul := by
    ext i : 1
    -- TODO: Add `reassoc` for `LinearMap`. Then we wouldn't need to reassociate back and forth.
    simp only [comp_assoc, comul_comp_lsingle]
    simp only [â† comp_assoc, â† TensorProduct.map_comp_comm_eq]
    simp [LinearMap.comp_assoc]

end DFinsupp

namespace Finsupp
variable (R : Type u) (Î¹ : Type v) (A : Type w)
variable [CommSemiring R] [AddCommMonoid A] [Module R A] [Coalgebra R A]

open LinearMap

noncomputable instance instCoalgebraStruct : CoalgebraStruct R (Î¹ â†’â‚€ A) where
  comul := Finsupp.lsum R fun i =>
    TensorProduct.map (Finsupp.lsingle i) (Finsupp.lsingle i) âˆ˜â‚— comul
  counit := Finsupp.lsum R fun _ => counit

@[simp]
theorem comul_single (i : Î¹) (a : A) :
    comul (R := R) (Finsupp.single i a) =
      (TensorProduct.map (Finsupp.lsingle i) (Finsupp.lsingle i) : _ â†’â‚—[R] _) (comul a) :=
  lsum_single _ _ _ _

@[simp]
theorem counit_single (i : Î¹) (a : A) : counit (Finsupp.single i a) = counit (R := R) a :=
  lsum_single _ _ _ _

theorem comul_comp_lsingle (i : Î¹) :
    comul âˆ˜â‚— (lsingle i : A â†’â‚—[R] _) = TensorProduct.map (lsingle i) (lsingle i) âˆ˜â‚— comul := by
  ext; simp

theorem comul_comp_lapply (i : Î¹) :
    comul âˆ˜â‚— (lapply i : _ â†’â‚—[R] A) = TensorProduct.map (lapply i) (lapply i) âˆ˜â‚— comul := by
  ext j : 1
  conv_rhs => rw [comp_assoc, comul_comp_lsingle, â† comp_assoc, â† TensorProduct.map_comp]
  obtain rfl | hij := eq_or_ne i j
  Â· rw [comp_assoc, lapply_comp_lsingle_same, comp_id, TensorProduct.map_id, id_comp]
  Â· rw [comp_assoc, lapply_comp_lsingle_of_ne _ _ hij, comp_zero, TensorProduct.map_zero_left,
      zero_comp]

@[simp] theorem counit_comp_lsingle (i : Î¹) : counit âˆ˜â‚— (lsingle i : A â†’â‚—[R] _) = counit := by
  ext; simp

/-- The `R`-module whose elements are functions `Î¹ â†’ A` which are zero on all but finitely many
elements of `Î¹` has a coalgebra structure. The coproduct `Î”` is given by `Î”(fáµ¢ a) = fáµ¢ aâ‚ âŠ— fáµ¢ aâ‚‚`
where `Î”(a) = aâ‚ âŠ— aâ‚‚` and the counit `Îµ` by `Îµ(fáµ¢ a) = Îµ(a)`, where `fáµ¢ a` is the function sending
`i` to `a` and all other elements of `Î¹` to zero. -/
noncomputable instance instCoalgebra : Coalgebra R (Î¹ â†’â‚€ A) where
  rTensor_counit_comp_comul := by
    ext : 1
    rw [comp_assoc, comul_comp_lsingle, â† comp_assoc, rTensor_comp_map, counit_comp_lsingle,
      â† lTensor_comp_rTensor, comp_assoc, rTensor_counit_comp_comul, lTensor_comp_mk]
  lTensor_counit_comp_comul := by
    ext : 1
    rw [comp_assoc, comul_comp_lsingle, â† comp_assoc, lTensor_comp_map, counit_comp_lsingle,
      â† rTensor_comp_lTensor, comp_assoc, lTensor_counit_comp_comul, rTensor_comp_flip_mk]
  coassoc := by
    ext i : 1
    simp_rw [comp_assoc, comul_comp_lsingle, â† comp_assoc, lTensor_comp_map, comul_comp_lsingle,
      comp_assoc, â† comp_assoc comul, rTensor_comp_map, comul_comp_lsingle, â† map_comp_rTensor,
      â† map_comp_lTensor, comp_assoc, â† coassoc, â† comp_assoc comul, â† comp_assoc,
        TensorProduct.map_map_comp_assoc_eq]

instance instIsCocomm [IsCocomm R A] : IsCocomm R (Î¹ â†’â‚€ A) where
  comm_comp_comul := by
    ext i : 1
    -- TODO: Add `reassoc` for `LinearMap`. Then we wouldn't need to reassociate back and forth.
    simp only [comp_assoc, comul_comp_lsingle]
    simp only [â† comp_assoc, â† TensorProduct.map_comp_comm_eq]
    simp [LinearMap.comp_assoc]

end Finsupp
