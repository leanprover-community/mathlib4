/-
Copyright (c) 2023 Ali Ramsey. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ali Ramsey, Eric Wieser
-/
module

public import Mathlib.LinearAlgebra.Finsupp.Pi
public import Mathlib.LinearAlgebra.TensorProduct.Finiteness
public import Mathlib.LinearAlgebra.TensorProduct.Associator

/-!
# Coalgebras

In this file we define `Coalgebra`, and provide instances for:

* Commutative semirings: `CommSemiring.toCoalgebra`
* Binary products: `Prod.instCoalgebra`
* Finitely supported functions: `DFinsupp.instCoalgebra`, `Finsupp.instCoalgebra`
* Finite pi functions: `Pi.instCoalgebra`

## References

* <https://en.wikipedia.org/wiki/Coalgebra>
-/

@[expose] public section

universe u v w

open scoped TensorProduct

/-- Data fields for `Coalgebra`, to allow API to be constructed before proving `Coalgebra.coassoc`.

See `Coalgebra` for documentation. -/
class CoalgebraStruct (R : Type u) (A : Type v)
    [CommSemiring R] [AddCommMonoid A] [Module R A] where
  /-- The comultiplication of the coalgebra -/
  comul : A ‚Üí‚Çó[R] A ‚äó[R] A
  /-- The counit of the coalgebra -/
  counit : A ‚Üí‚Çó[R] R

@[inherit_doc] scoped[RingTheory.LinearMap] notation "Œµ" => CoalgebraStruct.counit
@[inherit_doc] scoped[RingTheory.LinearMap] notation "Œ¥" => CoalgebraStruct.comul

/--
A representation of an element `a` of a coalgebra `A` is a finite sum of pure tensors `‚àë x·µ¢ ‚äó y·µ¢`
that is equal to `comul a`.
-/
structure Coalgebra.Repr (R : Type u) {A : Type v}
    [CommSemiring R] [AddCommMonoid A] [Module R A] [CoalgebraStruct R A] (a : A) where
  /-- the indexing type of a representation of `comul a` -/
  {Œπ : Type*}
  /-- the finite indexing set of a representation of `comul a` -/
  (index : Finset Œπ)
  /-- the first coordinate of a representation of `comul a` -/
  (left : Œπ ‚Üí A)
  /-- the second coordinate of a representation of `comul a` -/
  (right : Œπ ‚Üí A)
  /-- `comul a` is equal to a finite sum of some pure tensors -/
  (eq : ‚àë i ‚àà index, left i ‚äó‚Çú[R] right i = CoalgebraStruct.comul a)

/-- An arbitrarily chosen representation. -/
noncomputable def Coalgebra.Repr.arbitrary (R : Type u) {A : Type v}
    [CommSemiring R] [AddCommMonoid A] [Module R A] [CoalgebraStruct R A] (a : A) :
    Coalgebra.Repr R a where
  left := Prod.fst
  right := Prod.snd
  index := TensorProduct.exists_finset (R := R) (CoalgebraStruct.comul a) |>.choose
  eq := TensorProduct.exists_finset (R := R) (CoalgebraStruct.comul a) |>.choose_spec.symm

@[inherit_doc Coalgebra.Repr.arbitrary]
scoped[Coalgebra] notation "‚Ñõ" => Coalgebra.Repr.arbitrary

namespace Coalgebra
export CoalgebraStruct (comul counit)
end Coalgebra

/-- A coalgebra over a commutative (semi)ring `R` is an `R`-module equipped with a coassociative
comultiplication `Œî` and a counit `Œµ` obeying the left and right counitality laws. -/
class Coalgebra (R : Type u) (A : Type v)
    [CommSemiring R] [AddCommMonoid A] [Module R A] extends CoalgebraStruct R A where
  /-- The comultiplication is coassociative -/
  coassoc : TensorProduct.assoc R A A A ‚àò‚Çó comul.rTensor A ‚àò‚Çó comul = comul.lTensor A ‚àò‚Çó comul
  /-- The counit satisfies the left counitality law -/
  rTensor_counit_comp_comul : counit.rTensor A ‚àò‚Çó comul = TensorProduct.mk R _ _ 1
  /-- The counit satisfies the right counitality law -/
  lTensor_counit_comp_comul : counit.lTensor A ‚àò‚Çó comul = (TensorProduct.mk R _ _).flip 1

namespace Coalgebra
variable {R : Type u} {A : Type v}
variable [CommSemiring R] [AddCommMonoid A] [Module R A] [Coalgebra R A] {a : A}

@[simp]
theorem coassoc_apply (a : A) :
    TensorProduct.assoc R A A A (comul.rTensor A (comul a)) = comul.lTensor A (comul a) :=
  LinearMap.congr_fun coassoc a

@[simp]
theorem coassoc_symm_apply (a : A) :
    (TensorProduct.assoc R A A A).symm (comul.lTensor A (comul a)) = comul.rTensor A (comul a) := by
  rw [(TensorProduct.assoc R A A A).symm_apply_eq, coassoc_apply a]

@[simp]
theorem coassoc_symm :
    (TensorProduct.assoc R A A A).symm ‚àò‚Çó comul.lTensor A ‚àò‚Çó comul =
    comul.rTensor A ‚àò‚Çó (comul (R := R)) :=
  LinearMap.ext coassoc_symm_apply

@[simp]
theorem rTensor_counit_comul (a : A) : counit.rTensor A (comul a) = 1 ‚äó‚Çú[R] a :=
  LinearMap.congr_fun rTensor_counit_comp_comul a

@[simp]
theorem lTensor_counit_comul (a : A) : counit.lTensor A (comul a) = a ‚äó‚Çú[R] 1 :=
  LinearMap.congr_fun lTensor_counit_comp_comul a

@[simp]
lemma sum_counit_tmul_eq {a : A} (repr : Coalgebra.Repr R a) :
    ‚àë i ‚àà repr.index, counit (R := R) (repr.left i) ‚äó‚Çú (repr.right i) = 1 ‚äó‚Çú[R] a := by
  simpa [‚Üê repr.eq, map_sum] using congr($(rTensor_counit_comp_comul (R := R) (A := A)) a)

@[simp]
lemma sum_tmul_counit_eq {a : A} (repr : Coalgebra.Repr R a) :
    ‚àë i ‚àà repr.index, (repr.left i) ‚äó‚Çú counit (R := R) (repr.right i) = a ‚äó‚Çú[R] 1 := by
  simpa [‚Üê repr.eq, map_sum] using congr($(lTensor_counit_comp_comul (R := R) (A := A)) a)

-- Cannot be @[simp] because `a‚ÇÇ` cannot be inferred by `simp`.
lemma sum_tmul_tmul_eq {a : A} (repr : Repr R a)
    (a‚ÇÅ : (i : repr.Œπ) ‚Üí Repr R (repr.left i)) (a‚ÇÇ : (i : repr.Œπ) ‚Üí Repr R (repr.right i)) :
    ‚àë i ‚àà repr.index, ‚àë j ‚àà (a‚ÇÅ i).index,
      (a‚ÇÅ i).left j ‚äó‚Çú[R] ((a‚ÇÅ i).right j ‚äó‚Çú[R] repr.right i)
      = ‚àë i ‚àà repr.index, ‚àë j ‚àà (a‚ÇÇ i).index,
      repr.left i ‚äó‚Çú[R] ((a‚ÇÇ i).left j ‚äó‚Çú[R] (a‚ÇÇ i).right j) := by
  simpa [(a‚ÇÇ _).eq, ‚Üê (a‚ÇÅ _).eq, ‚Üê TensorProduct.tmul_sum,
    TensorProduct.sum_tmul, ‚Üê repr.eq] using congr($(coassoc (R := R)) a)

@[simp]
theorem sum_counit_tmul_map_eq {B : Type*} [AddCommMonoid B] [Module R B]
    {F : Type*} [FunLike F A B] [LinearMapClass F R A B] (f : F) (a : A) {repr : Repr R a} :
    ‚àë i ‚àà repr.index, counit (R := R) (repr.left i) ‚äó‚Çú f (repr.right i) = 1 ‚äó‚Çú[R] f a := by
  have := sum_counit_tmul_eq repr
  apply_fun LinearMap.lTensor R (f : A ‚Üí‚Çó[R] B) at this
  simp_all only [map_sum, LinearMap.lTensor_tmul, LinearMap.coe_coe]

@[simp]
theorem sum_map_tmul_counit_eq {B : Type*} [AddCommMonoid B] [Module R B]
    {F : Type*} [FunLike F A B] [LinearMapClass F R A B] (f : F) (a : A) {repr : Repr R a} :
    ‚àë i ‚àà repr.index, f (repr.left i) ‚äó‚Çú counit (R := R) (repr.right i) = f a ‚äó‚Çú[R] 1 := by
  have := sum_tmul_counit_eq repr
  apply_fun LinearMap.rTensor R (f : A ‚Üí‚Çó[R] B) at this
  simp_all only [map_sum, LinearMap.rTensor_tmul, LinearMap.coe_coe]

-- Cannot be @[simp] because `a‚ÇÅ` cannot be inferred by `simp`.
theorem sum_map_tmul_tmul_eq {B : Type*} [AddCommMonoid B] [Module R B]
    {F : Type*} [FunLike F A B] [LinearMapClass F R A B] (f g h : F) (a : A) {repr : Repr R a}
    {a‚ÇÅ : (i : repr.Œπ) ‚Üí Repr R (repr.left i)} {a‚ÇÇ : (i : repr.Œπ) ‚Üí Repr R (repr.right i)} :
    ‚àë i ‚àà repr.index, ‚àë j ‚àà (a‚ÇÇ i).index,
      f (repr.left i) ‚äó‚Çú (g ((a‚ÇÇ i).left j) ‚äó‚Çú h ((a‚ÇÇ i).right j)) =
    ‚àë i ‚àà repr.index, ‚àë j ‚àà (a‚ÇÅ i).index,
      f ((a‚ÇÅ i).left j) ‚äó‚Çú[R] (g ((a‚ÇÅ i).right j) ‚äó‚Çú[R] h (repr.right i)) := by
  have := sum_tmul_tmul_eq repr a‚ÇÅ a‚ÇÇ
  apply_fun TensorProduct.map (f : A ‚Üí‚Çó[R] B)
    (TensorProduct.map (g : A ‚Üí‚Çó[R] B) (h : A ‚Üí‚Çó[R] B)) at this
  simp_all only [map_sum, TensorProduct.map_tmul, LinearMap.coe_coe]

lemma sum_counit_smul (ùì° : Repr R a) :
    ‚àë x ‚àà ùì°.index, counit (R := R) (ùì°.left x) ‚Ä¢ ùì°.right x = a := by
  simpa only [map_sum, TensorProduct.lift.tmul, LinearMap.lsmul_apply, one_smul]
    using congr(TensorProduct.lift (LinearMap.lsmul R A) $(sum_counit_tmul_eq (R := R) ùì°))

lemma lift_lsmul_comp_counit_comp_comul :
    TensorProduct.lift (.lsmul R A ‚àò‚Çó counit) ‚àò‚Çó comul = .id := by
  have := rTensor_counit_comp_comul (R := R) (A := A)
  apply_fun (TensorProduct.lift (LinearMap.lsmul R A) ‚àò‚Çó ¬∑) at this
  rw [LinearMap.rTensor, ‚Üê LinearMap.comp_assoc, TensorProduct.lift_comp_map, LinearMap.compl‚ÇÇ_id]
    at this
  ext
  simp [this]

variable (R A) in
/-- A coalgebra `A` is cocommutative if its comultiplication `Œ¥ : A ‚Üí A ‚äó A` commutes with the
swapping `Œ≤ : A ‚äó A ‚âÉ A ‚äó A` of the factors in the tensor product. -/
class IsCocomm where
  protected comm_comp_comul : (TensorProduct.comm R A A).comp comul = comul

variable [IsCocomm R A]

variable (R A) in
@[simp] lemma comm_comp_comul : (TensorProduct.comm R A A).comp comul = comul :=
  IsCocomm.comm_comp_comul

variable (R) in
@[simp] lemma comm_comul (a : A) : TensorProduct.comm R A A (comul a) = comul a :=
  congr($(comm_comp_comul R A) a)

end Coalgebra

open Coalgebra

namespace CommSemiring
variable (R : Type u) [CommSemiring R]

/-- Every commutative (semi)ring is a coalgebra over itself, with `Œî r = 1 ‚äó‚Çú r`. -/
instance toCoalgebra : Coalgebra R R where
  comul := (TensorProduct.mk R R R) 1
  counit := .id
  coassoc := rfl
  rTensor_counit_comp_comul := by ext; rfl
  lTensor_counit_comp_comul := by ext; rfl

@[simp]
theorem comul_apply (r : R) : comul r = 1 ‚äó‚Çú[R] r := rfl

@[simp]
theorem counit_apply (r : R) : counit r = r := rfl

instance : IsCocomm R R where comm_comp_comul := by ext; simp

end CommSemiring

namespace Prod
variable (R : Type u) (A : Type v) (B : Type w)
variable [CommSemiring R] [AddCommMonoid A] [AddCommMonoid B] [Module R A] [Module R B]
variable [Coalgebra R A] [Coalgebra R B]

open LinearMap

instance instCoalgebraStruct : CoalgebraStruct R (A √ó B) where
  comul := .coprod
    (TensorProduct.map (.inl R A B) (.inl R A B) ‚àò‚Çó comul)
    (TensorProduct.map (.inr R A B) (.inr R A B) ‚àò‚Çó comul)
  counit := .coprod counit counit

@[simp]
theorem comul_apply (r : A √ó B) :
    comul r =
      TensorProduct.map (.inl R A B) (.inl R A B) (comul r.1) +
      TensorProduct.map (.inr R A B) (.inr R A B) (comul r.2) := rfl

@[simp]
theorem counit_apply (r : A √ó B) : (counit r : R) = counit r.1 + counit r.2 := rfl

theorem comul_comp_inl :
    comul ‚àò‚Çó inl R A B = TensorProduct.map (.inl R A B) (.inl R A B) ‚àò‚Çó comul := by
  ext; simp

theorem comul_comp_inr :
    comul ‚àò‚Çó inr R A B = TensorProduct.map (.inr R A B) (.inr R A B) ‚àò‚Çó comul := by
  ext; simp

theorem comul_comp_fst :
    comul ‚àò‚Çó .fst R A B = TensorProduct.map (.fst R A B) (.fst R A B) ‚àò‚Çó comul := by
  ext : 1
  ¬∑ rw [comp_assoc, fst_comp_inl, comp_id, comp_assoc, comul_comp_inl, ‚Üê comp_assoc,
      ‚Üê TensorProduct.map_comp, fst_comp_inl, TensorProduct.map_id, id_comp]
  ¬∑ rw [comp_assoc, fst_comp_inr, comp_zero, comp_assoc, comul_comp_inr, ‚Üê comp_assoc,
      ‚Üê TensorProduct.map_comp, fst_comp_inr, TensorProduct.map_zero_left, zero_comp]

theorem comul_comp_snd :
    comul ‚àò‚Çó .snd R A B = TensorProduct.map (.snd R A B) (.snd R A B) ‚àò‚Çó comul := by
  ext : 1
  ¬∑ rw [comp_assoc, snd_comp_inl, comp_zero, comp_assoc, comul_comp_inl, ‚Üê comp_assoc,
      ‚Üê TensorProduct.map_comp, snd_comp_inl, TensorProduct.map_zero_left, zero_comp]
  ¬∑ rw [comp_assoc, snd_comp_inr, comp_id, comp_assoc, comul_comp_inr, ‚Üê comp_assoc,
      ‚Üê TensorProduct.map_comp, snd_comp_inr, TensorProduct.map_id, id_comp]

@[simp] theorem counit_comp_inr : counit ‚àò‚Çó inr R A B = counit := by ext; simp

@[simp] theorem counit_comp_inl : counit ‚àò‚Çó inl R A B = counit := by ext; simp

instance instCoalgebra : Coalgebra R (A √ó B) where
  rTensor_counit_comp_comul := by
    ext : 1
    ¬∑ rw [comp_assoc, comul_comp_inl, ‚Üê comp_assoc, rTensor_comp_map, counit_comp_inl,
        ‚Üê lTensor_comp_rTensor, comp_assoc, rTensor_counit_comp_comul, lTensor_comp_mk]
    ¬∑ rw [comp_assoc, comul_comp_inr, ‚Üê comp_assoc, rTensor_comp_map, counit_comp_inr,
        ‚Üê lTensor_comp_rTensor, comp_assoc, rTensor_counit_comp_comul, lTensor_comp_mk]
  lTensor_counit_comp_comul := by
    ext : 1
    ¬∑ rw [comp_assoc, comul_comp_inl, ‚Üê comp_assoc, lTensor_comp_map, counit_comp_inl,
        ‚Üê rTensor_comp_lTensor, comp_assoc, lTensor_counit_comp_comul, rTensor_comp_flip_mk]
    ¬∑ rw [comp_assoc, comul_comp_inr, ‚Üê comp_assoc, lTensor_comp_map, counit_comp_inr,
        ‚Üê rTensor_comp_lTensor, comp_assoc, lTensor_counit_comp_comul, rTensor_comp_flip_mk]
  coassoc := by
    dsimp only [instCoalgebraStruct]
    ext x : 2 <;> dsimp only [comp_apply, LinearEquiv.coe_coe, coe_inl, coe_inr, coprod_apply]
    ¬∑ simp only [map_zero, add_zero]
      simp_rw [‚Üê comp_apply, ‚Üê comp_assoc, rTensor_comp_map, lTensor_comp_map, coprod_inl,
        ‚Üê map_comp_rTensor, ‚Üê map_comp_lTensor, comp_assoc, ‚Üê coassoc, ‚Üê comp_assoc,
        TensorProduct.map_map_comp_assoc_eq, comp_apply, LinearEquiv.coe_coe]
    ¬∑ simp only [map_zero, zero_add]
      simp_rw [‚Üê comp_apply, ‚Üê comp_assoc, rTensor_comp_map, lTensor_comp_map, coprod_inr,
        ‚Üê map_comp_rTensor, ‚Üê map_comp_lTensor, comp_assoc, ‚Üê coassoc, ‚Üê comp_assoc,
        TensorProduct.map_map_comp_assoc_eq, comp_apply, LinearEquiv.coe_coe]

instance [IsCocomm R A] [IsCocomm R B] : IsCocomm R (A √ó B) where
  comm_comp_comul := by ext <;> simp [‚Üê TensorProduct.map_comm]

end Prod

namespace DFinsupp
variable (R : Type u) (Œπ : Type v) (A : Œπ ‚Üí Type w)
variable [DecidableEq Œπ]
variable [CommSemiring R] [‚àÄ i, AddCommMonoid (A i)] [‚àÄ i, Module R (A i)]

open LinearMap

section coalgebraStruct
variable [‚àÄ i, CoalgebraStruct R (A i)]

instance instCoalgebraStruct : CoalgebraStruct R (Œ†‚ÇÄ i, A i) where
  comul := DFinsupp.lsum R fun i =>
    TensorProduct.map (DFinsupp.lsingle i) (DFinsupp.lsingle i) ‚àò‚Çó comul
  counit := DFinsupp.lsum R fun _ => counit

@[simp]
theorem comul_single (i : Œπ) (a : A i) :
    comul (R := R) (DFinsupp.single i a) =
      (TensorProduct.map (DFinsupp.lsingle i) (DFinsupp.lsingle i) : _ ‚Üí‚Çó[R] _) (comul a) :=
  lsum_single _ _ _ _

@[simp]
theorem counit_single (i : Œπ) (a : A i) : counit (DFinsupp.single i a) = counit (R := R) a :=
  lsum_single _ _ _ _

theorem comul_comp_lsingle (i : Œπ) :
    comul ‚àò‚Çó (lsingle i : A i ‚Üí‚Çó[R] _) = TensorProduct.map (lsingle i) (lsingle i) ‚àò‚Çó comul := by
  ext; simp

theorem comul_comp_lapply (i : Œπ) :
    comul ‚àò‚Çó (lapply i : _ ‚Üí‚Çó[R] A i) = TensorProduct.map (lapply i) (lapply i) ‚àò‚Çó comul := by
  ext j
  have := eq_or_ne i j
  aesop (add simp [TensorProduct.map_map, proj_comp_single, diag])

@[simp] theorem counit_comp_lsingle (i : Œπ) : counit ‚àò‚Çó (lsingle i : A i ‚Üí‚Çó[R] _) = counit := by
  ext; simp

end coalgebraStruct

variable [‚àÄ i, Coalgebra R (A i)]

/-- The `R`-module whose elements are dependent functions `(i : Œπ) ‚Üí A i` which are zero on all but
finitely many elements of `Œπ` has a coalgebra structure.

The coproduct `Œî` is given by `Œî(f·µ¢ a) = f·µ¢ a‚ÇÅ ‚äó f·µ¢ a‚ÇÇ` where `Œî(a) = a‚ÇÅ ‚äó a‚ÇÇ` and the counit `Œµ`
by `Œµ(f·µ¢ a) = Œµ(a)`, where `f·µ¢ a` is the function sending `i` to `a` and all other elements of `Œπ`
to zero. -/
instance instCoalgebra : Coalgebra R (Œ†‚ÇÄ i, A i) where
  rTensor_counit_comp_comul := by
    ext : 1
    rw [comp_assoc, comul_comp_lsingle, ‚Üê comp_assoc, rTensor_comp_map, counit_comp_lsingle,
      ‚Üê lTensor_comp_rTensor, comp_assoc, rTensor_counit_comp_comul, lTensor_comp_mk]
  lTensor_counit_comp_comul := by
    ext : 1
    rw [comp_assoc, comul_comp_lsingle, ‚Üê comp_assoc, lTensor_comp_map, counit_comp_lsingle,
      ‚Üê rTensor_comp_lTensor, comp_assoc, lTensor_counit_comp_comul, rTensor_comp_flip_mk]
  coassoc := by
    ext i : 1
    simp_rw [comp_assoc, comul_comp_lsingle, ‚Üê comp_assoc, lTensor_comp_map, comul_comp_lsingle,
      comp_assoc, ‚Üê comp_assoc comul, rTensor_comp_map, comul_comp_lsingle, ‚Üê map_comp_rTensor,
      ‚Üê map_comp_lTensor, comp_assoc, ‚Üê coassoc, ‚Üê comp_assoc comul, ‚Üê comp_assoc,
        TensorProduct.map_map_comp_assoc_eq]

instance instIsCocomm [‚àÄ i, IsCocomm R (A i)] : IsCocomm R (Œ†‚ÇÄ i, A i) where
  comm_comp_comul := by ext; simp [‚Üê TensorProduct.map_comm]

end DFinsupp

namespace Finsupp
variable (R : Type u) (Œπ : Type v) (A : Type w)
variable [CommSemiring R] [AddCommMonoid A] [Module R A]

open LinearMap

section coalgebraStruct
variable [CoalgebraStruct R A]

noncomputable instance instCoalgebraStruct : CoalgebraStruct R (Œπ ‚Üí‚ÇÄ A) where
  comul := Finsupp.lsum R fun i =>
    TensorProduct.map (Finsupp.lsingle i) (Finsupp.lsingle i) ‚àò‚Çó comul
  counit := Finsupp.lsum R fun _ => counit

@[simp]
theorem comul_single (i : Œπ) (a : A) :
    comul (R := R) (Finsupp.single i a) =
      (TensorProduct.map (Finsupp.lsingle i) (Finsupp.lsingle i) : _ ‚Üí‚Çó[R] _) (comul a) :=
  lsum_single _ _ _ _

@[simp]
theorem counit_single (i : Œπ) (a : A) : counit (Finsupp.single i a) = counit (R := R) a :=
  lsum_single _ _ _ _

theorem comul_comp_lsingle (i : Œπ) :
    comul ‚àò‚Çó (lsingle i : A ‚Üí‚Çó[R] _) = TensorProduct.map (lsingle i) (lsingle i) ‚àò‚Çó comul := by
  ext; simp

theorem comul_comp_lapply (i : Œπ) :
    comul ‚àò‚Çó (lapply i : _ ‚Üí‚Çó[R] A) = TensorProduct.map (lapply i) (lapply i) ‚àò‚Çó comul := by
  ext j; have := eq_or_ne i j
  aesop (add simp [TensorProduct.map_map, proj_comp_single, diag])

@[simp] theorem counit_comp_lsingle (i : Œπ) : counit ‚àò‚Çó (lsingle i : A ‚Üí‚Çó[R] _) = counit := by
  ext; simp

end coalgebraStruct

variable [Coalgebra R A]

/-- The `R`-module whose elements are functions `Œπ ‚Üí A` which are zero on all but finitely many
elements of `Œπ` has a coalgebra structure. The coproduct `Œî` is given by `Œî(f·µ¢ a) = f·µ¢ a‚ÇÅ ‚äó f·µ¢ a‚ÇÇ`
where `Œî(a) = a‚ÇÅ ‚äó a‚ÇÇ` and the counit `Œµ` by `Œµ(f·µ¢ a) = Œµ(a)`, where `f·µ¢ a` is the function sending
`i` to `a` and all other elements of `Œπ` to zero. -/
noncomputable instance instCoalgebra : Coalgebra R (Œπ ‚Üí‚ÇÄ A) where
  rTensor_counit_comp_comul := by
    ext : 1
    rw [comp_assoc, comul_comp_lsingle, ‚Üê comp_assoc, rTensor_comp_map, counit_comp_lsingle,
      ‚Üê lTensor_comp_rTensor, comp_assoc, rTensor_counit_comp_comul, lTensor_comp_mk]
  lTensor_counit_comp_comul := by
    ext : 1
    rw [comp_assoc, comul_comp_lsingle, ‚Üê comp_assoc, lTensor_comp_map, counit_comp_lsingle,
      ‚Üê rTensor_comp_lTensor, comp_assoc, lTensor_counit_comp_comul, rTensor_comp_flip_mk]
  coassoc := by
    ext i : 1
    simp_rw [comp_assoc, comul_comp_lsingle, ‚Üê comp_assoc, lTensor_comp_map, comul_comp_lsingle,
      comp_assoc, ‚Üê comp_assoc comul, rTensor_comp_map, comul_comp_lsingle, ‚Üê map_comp_rTensor,
      ‚Üê map_comp_lTensor, comp_assoc, ‚Üê coassoc, ‚Üê comp_assoc comul, ‚Üê comp_assoc,
        TensorProduct.map_map_comp_assoc_eq]

instance instIsCocomm [IsCocomm R A] : IsCocomm R (Œπ ‚Üí‚ÇÄ A) where
  comm_comp_comul := by ext; simp [‚Üê TensorProduct.map_comm]

end Finsupp

namespace Pi
variable {R n : Type*} [CommSemiring R] [Fintype n] [DecidableEq n]
  {A : n ‚Üí Type*} [Œ† i, AddCommMonoid (A i)] [Œ† i, Module R (A i)]

open TensorProduct LinearMap

section coalgebraStruct
variable [Œ† i, CoalgebraStruct R (A i)]

instance instCoalgebraStruct : CoalgebraStruct R (Œ† i, A i) where
  comul := .lsum R _ R fun i ‚Ü¶ map (.single R _ i) (.single R _ i) ‚àò‚Çó comul
  counit := .lsum R _ R fun _ ‚Ü¶ counit

@[simp] theorem comul_single (i : n) (a : A i) :
    comul (single i a) = map (.single R _ i) (.single R _ i) (comul a) :=
  lsum_piSingle _ _ _ _ _ _

@[simp] theorem counit_single (i : n) (a : A i) : counit (single i a) = counit (R := R) a :=
  lsum_piSingle _ _ _ _ _ _

theorem comul_comp_single (i : n) :
    comul ‚àò‚Çó .single R _ i = map (.single R A i) (.single R A i) ‚àò‚Çó comul := by
  ext; simp

theorem comul_comp_proj (i : n) :
    comul ‚àò‚Çó (proj i : (Œ† i, A i) ‚Üí‚Çó[R] A i) = map (proj i) (proj i) ‚àò‚Çó comul := by
  ext j; have := eq_or_ne i j
  aesop (add simp [map_map, proj_comp_single, diag])

@[simp] theorem counit_comp_single (i : n) : counit ‚àò‚Çó .single R A i = counit := by ext; simp

theorem counit_comp_dFinsuppCoeFnLinearMap :
    counit (R := R) (A := Œ† i, A i) ‚àò‚Çó DFinsupp.coeFnLinearMap _ = counit := by
  apply LinearMap.ext fun x ‚Ü¶ ?_
  have (i : n) (x : A i) : Decidable (x ‚â† 0) := Classical.propDecidable _
  rw [‚Üê DFinsupp.sum_single (f := x)]
  simp [DFinsupp.single_eq_pi_single]

@[simp] theorem counit_coe_dFinsupp (x : Œ†‚ÇÄ i, A i) :
    counit (R := R) ‚áëx = counit x := congr($counit_comp_dFinsuppCoeFnLinearMap x)

open DFinsupp in
theorem comul_comp_dFinsuppCoeFnLinearMap :
    comul (R := R) (A := Œ† i, A i) ‚àò‚Çó coeFnLinearMap _ =
      map (coeFnLinearMap _) (coeFnLinearMap _) ‚àò‚Çó comul := by
  apply LinearMap.ext fun x ‚Ü¶ ?_
  have (i : n) (x : A i) : Decidable (x ‚â† 0) := Classical.propDecidable _
  rw [‚Üê DFinsupp.sum_single (f := x)]
  aesop (add simp [map_map, DFinsupp.single_eq_pi_single])

open DFinsupp in
@[simp] theorem comul_coe_dFinsupp (x : Œ†‚ÇÄ i, A i) :
    comul (R := R) ‚áëx = map (coeFnLinearMap _) (coeFnLinearMap _) (comul x) :=
  congr($comul_comp_dFinsuppCoeFnLinearMap x)

variable {M : Type*} [AddCommMonoid M] [Module R M] [CoalgebraStruct R M]

theorem counit_comp_finsuppLcoeFun :
    counit (R := R) (A := n ‚Üí M) ‚àò‚Çó Finsupp.lcoeFun = counit := by
  apply LinearMap.ext fun x ‚Ü¶ ?_
  rw [‚Üê Finsupp.univ_sum_single x]
  simp [-Finsupp.univ_sum_single, Finsupp.lcoeFun, Finsupp.single_eq_pi_single]

@[simp] theorem counit_coe_finsupp (x : n ‚Üí‚ÇÄ M) :
    counit (R := R) ‚áëx = counit x := congr($counit_comp_finsuppLcoeFun x)

open Finsupp in
theorem comul_comp_finsuppLcoeFun :
    comul (R := R) (A := n ‚Üí M) ‚àò‚Çó lcoeFun = map lcoeFun lcoeFun ‚àò‚Çó comul := by
  apply LinearMap.ext fun x ‚Ü¶ ?_
  rw [‚Üê Finsupp.univ_sum_single x]
  simp [-univ_sum_single, single_eq_pi_single, map_map]

open Finsupp in
@[simp] theorem comul_coe_finsupp (x : n ‚Üí‚ÇÄ M) :
    comul (R := R) ‚áëx = map lcoeFun lcoeFun (comul x) :=
  congr($comul_comp_finsuppLcoeFun x)

end coalgebraStruct

variable [Œ† i, Coalgebra R (A i)]

/-- The `R`-module whose elements are functions `Œ† i, A i` for finite `n` has a coalgebra structure.
The coproduct `Œî` is given by `Œî(f·µ¢ a) = f·µ¢ a‚ÇÅ ‚äó f·µ¢ a‚ÇÇ` where `Œî(a) = a‚ÇÅ ‚äó a‚ÇÇ` and
the counit `Œµ` by `Œµ(f·µ¢ a) = Œµ(a)`, where `f·µ¢ a` is the function sending `i` to `a` and all
other elements of `Œπ` to zero. -/
instance instCoalgebra : Coalgebra R (Œ† i, A i) where
  rTensor_counit_comp_comul := by
    ext : 1
    rw [comp_assoc, comul_comp_single, ‚Üê comp_assoc, rTensor_comp_map, counit_comp_single,
      ‚Üê lTensor_comp_rTensor, comp_assoc, rTensor_counit_comp_comul, lTensor_comp_mk]
  lTensor_counit_comp_comul := by
    ext : 1
    rw [comp_assoc, comul_comp_single, ‚Üê comp_assoc, lTensor_comp_map, counit_comp_single,
      ‚Üê rTensor_comp_lTensor, comp_assoc, lTensor_counit_comp_comul, rTensor_comp_flip_mk]
  coassoc := by
    ext : 1
    simp_rw [comp_assoc, comul_comp_single, ‚Üê comp_assoc, lTensor_comp_map, comul_comp_single,
      comp_assoc, ‚Üê comp_assoc comul, rTensor_comp_map, comul_comp_single, ‚Üê map_comp_rTensor,
      ‚Üê map_comp_lTensor, comp_assoc, ‚Üê coassoc, ‚Üê comp_assoc comul, ‚Üê comp_assoc,
      map_map_comp_assoc_eq]

instance instIsCocomm [‚àÄ i, IsCocomm R (A i)] : IsCocomm R (Œ† i, A i) where
  comm_comp_comul := by ext; simp [‚Üê map_comm]

end Pi

namespace Equiv
variable {R A B : Type*} [CommSemiring R]

variable (R) in
/-- Transfer `CoalgebraStruct` across an `Equiv`. -/
abbrev coalgebraStruct [AddCommMonoid B] [Module R B] [CoalgebraStruct R B] (e : A ‚âÉ B) :
    letI := e.addCommMonoid
    letI := e.module R
    CoalgebraStruct R A :=
  letI := e.addCommMonoid
  letI := e.module R
  { comul :=
      TensorProduct.map (e.linearEquiv R).symm.toLinearMap (e.linearEquiv R).symm.toLinearMap ‚àò‚Çó
        comul ‚àò‚Çó (e.linearEquiv R).toLinearMap
    counit := counit ‚àò‚Çó (e.linearEquiv R).toLinearMap }

variable (R) in
/-- Transfer `Coalgebra` across an `Equiv`. -/
abbrev coalgebra [AddCommMonoid B] [Module R B] [Coalgebra R B] (e : A ‚âÉ B) :
    letI := e.addCommMonoid
    letI := e.module R
    Coalgebra R A :=
  letI := e.addCommMonoid
  letI := e.module R
  { __ := e.coalgebraStruct R
    rTensor_counit_comp_comul := by
      ext
      apply (TensorProduct.map_bijective (f := .id) Function.bijective_id
        (e.linearEquiv R).bijective).injective
      simpa [coalgebraStruct, LinearMap.comp_assoc, TensorProduct.map_map, LinearMap.rTensor]
        using Coalgebra.rTensor_counit_comul _
    lTensor_counit_comp_comul := by
      ext
      apply (TensorProduct.map_bijective (g := .id) (e.linearEquiv R).bijective
        Function.bijective_id).injective
      simpa [coalgebraStruct, LinearMap.comp_assoc, TensorProduct.map_map, LinearMap.lTensor]
        using Coalgebra.lTensor_counit_comul _
    coassoc := by
      ext
      apply (TensorProduct.map_bijective (e.linearEquiv R).bijective <|
        TensorProduct.map_bijective (e.linearEquiv R).bijective
        (e.linearEquiv R).bijective).injective
      simp [coalgebraStruct, e.tensorProductAssoc_def R, TensorProduct.congr,
        ‚Üê LinearMap.comp_assoc, TensorProduct.map_map, ‚Üê TensorProduct.map_comp]
      simpa [LinearMap.comp_assoc, -coassoc_apply] using coassoc_apply (R := R) (A := B) _ }

variable (R) in
/-- Transfer `Coalgebra.IsCocomm` across an `Equiv`. -/
lemma coalgebraIsCocomm [AddCommMonoid B] [Module R B] [Coalgebra R B] [IsCocomm R B] (e : A ‚âÉ B) :
    letI := e.addCommMonoid
    letI := e.module R
    letI := e.coalgebra R
    IsCocomm R A :=
  letI := e.addCommMonoid
  letI := e.module R
  letI := e.coalgebra R
  { comm_comp_comul := by ext; simp [comul, ‚Üê TensorProduct.map_comm] }

end Equiv
