/-
Copyright (c) 2023 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.GroupTheory.Subgroup.Basic
import Mathlib.RingTheory.NonUnitalSubsemiring.Basic

/-!
# `NonUnitalSubring`s

Let `R` be a non-unital ring. This file defines the "bundled" non-unital subring type
`NonUnitalSubring R`, a type whose terms correspond to non-unital subrings of `R`.
This is the preferred way to talk about non-unital subrings in mathlib.

We prove that non-unital subrings are a complete lattice, and that you can `map` (pushforward) and
`comap` (pull back) them along ring homomorphisms.

We define the `closure` construction from `Set R` to `NonUnitalSubring R`, sending a subset of
`R` to the non-unital subring it generates, and prove that it is a Galois insertion.

## Main definitions

Notation used here:

`(R : Type u) [NonUnitalRing R] (S : Type u) [NonUnitalRing S] (f g : R ‚Üí‚Çô+* S)`
`(A : NonUnitalSubring R) (B : NonUnitalSubring S) (s : Set R)`

* `NonUnitalSubring R` : the type of non-unital subrings of a ring `R`.

* `instance : CompleteLattice (NonUnitalSubring R)` : the complete lattice structure on the
  non-unital subrings.

* `NonUnitalSubring.center` : the center of a non-unital ring `R`.

* `NonUnitalSubring.closure` : non-unital subring closure of a set, i.e., the smallest
  non-unital subring that includes the set.

* `NonUnitalSubring.gi` : `closure : Set M ‚Üí NonUnitalSubring M` and coercion
  `coe : NonUnitalSubring M ‚Üí Set M`
  form a `GaloisInsertion`.

* `comap f B : NonUnitalSubring A` : the preimage of a non-unital subring `B` along the
  non-unital ring homomorphism `f`

* `map f A : NonUnitalSubring B` : the image of a non-unital subring `A` along the
  non-unital ring homomorphism `f`.

* `Prod A B : NonUnitalSubring (R √ó S)` : the product of non-unital subrings

* `f.range : NonUnitalSubring B` : the range of the non-unital ring homomorphism `f`.

* `eq_locus f g : NonUnitalSubring R` : given non-unital ring homomorphisms `f g : R ‚Üí‚Çô+* S`,
     the non-unital subring of `R` where `f x = g x`

## Implementation notes

A non-unital subring is implemented as a `NonUnitalSubsemiring` which is also an
additive subgroup.

Lattice inclusion (e.g. `‚â§` and `‚äì`) is used rather than set notation (`‚äÜ` and `‚à©`), although
`‚àà` is defined as membership of a non-unital subring's underlying set.

## Tags
non-unital subring
-/


open scoped BigOperators

universe u v w

section Basic

variable {R : Type u} {S : Type v} {T : Type w} [NonUnitalNonAssocRing R]

section NonUnitalSubringClass

/-- `NonUnitalSubringClass S R` states that `S` is a type of subsets `s ‚äÜ R` that
are both a multiplicative submonoid and an additive subgroup. -/
class NonUnitalSubringClass (S : Type*) (R : Type u) [NonUnitalNonAssocRing R]
    [SetLike S R] extends NonUnitalSubsemiringClass S R, NegMemClass S R : Prop where

-- See note [lower instance priority]
instance (priority := 100) NonUnitalSubringClass.addSubgroupClass (S : Type*) (R : Type u)
    [SetLike S R] [NonUnitalNonAssocRing R] [h : NonUnitalSubringClass S R] :
    AddSubgroupClass S R :=
  { h with }

variable [SetLike S R] [hSR : NonUnitalSubringClass S R] (s : S)

namespace NonUnitalSubringClass

-- Prefer subclasses of `NonUnitalRing` over subclasses of `NonUnitalSubringClass`.
/-- A non-unital subring of a non-unital ring inherits a non-unital ring structure -/
instance (priority := 75) toNonUnitalNonAssocRing : NonUnitalNonAssocRing s :=
  Subtype.val_injective.nonUnitalNonAssocRing _ rfl (fun _ _ => rfl) (fun _ _ => rfl) (fun _ => rfl)
    (fun _ _ => rfl) (fun _ _ => rfl) fun _ _ => rfl

-- Prefer subclasses of `NonUnitalRing` over subclasses of `NonUnitalSubringClass`.
/-- A non-unital subring of a non-unital ring inherits a non-unital ring structure -/
instance (priority := 75) toNonUnitalRing {R : Type*} [NonUnitalRing R] [SetLike S R]
    [NonUnitalSubringClass S R] (s : S) : NonUnitalRing s :=
  Subtype.val_injective.nonUnitalRing _ rfl (fun _ _ => rfl) (fun _ _ => rfl) (fun _ => rfl)
    (fun _ _ => rfl) (fun _ _ => rfl) fun _ _ => rfl

-- Prefer subclasses of `NonUnitalRing` over subclasses of `NonUnitalSubringClass`.
/-- A non-unital subring of a `NonUnitalCommRing` is a `NonUnitalCommRing`. -/
instance (priority := 75) toNonUnitalCommRing {R} [NonUnitalCommRing R] [SetLike S R]
    [NonUnitalSubringClass S R] : NonUnitalCommRing s :=
  Subtype.val_injective.nonUnitalCommRing _ rfl (fun _ _ => rfl) (fun _ _ => rfl) (fun _ => rfl)
    (fun _ _ => rfl) (fun _ _ => rfl) fun _ _ => rfl

/-- The natural non-unital ring hom from a non-unital subring of a non-unital ring `R` to `R`. -/
def subtype (s : S) : s ‚Üí‚Çô+* R :=
  { NonUnitalSubsemiringClass.subtype s,
    AddSubgroupClass.subtype s with
    toFun := Subtype.val }

@[simp]
theorem coe_subtype : (subtype s : s ‚Üí R) = Subtype.val :=
  rfl

end NonUnitalSubringClass

end NonUnitalSubringClass

variable [NonUnitalNonAssocRing S] [NonUnitalNonAssocRing T]

/-- `NonUnitalSubring R` is the type of non-unital subrings of `R`. A non-unital subring of `R`
is a subset `s` that is a multiplicative subsemigroup and an additive subgroup. Note in particular
that it shares the same 0 as R. -/
structure NonUnitalSubring (R : Type u) [NonUnitalNonAssocRing R] extends
  NonUnitalSubsemiring R, AddSubgroup R

/-- Reinterpret a `NonUnitalSubring` as a `NonUnitalSubsemiring`. -/
add_decl_doc NonUnitalSubring.toNonUnitalSubsemiring

/-- Reinterpret a `NonUnitalSubring` as an `AddSubgroup`. -/
add_decl_doc NonUnitalSubring.toAddSubgroup

namespace NonUnitalSubring

/-- The underlying submonoid of a `NonUnitalSubring`. -/
def toSubsemigroup (s : NonUnitalSubring R) : Subsemigroup R :=
  { s.toNonUnitalSubsemiring.toSubsemigroup with carrier := s.carrier }

instance : SetLike (NonUnitalSubring R) R
    where
  coe s := s.carrier
  coe_injective' p q h := by cases p; cases q; congr; exact SetLike.coe_injective h
                             -- ‚ä¢ { toNonUnitalSubsemiring := toNonUnitalSubsemiring‚úù, neg_mem' := neg_mem'‚úù } ‚Ä¶
                                      -- ‚ä¢ { toNonUnitalSubsemiring := toNonUnitalSubsemiring‚úù¬π, neg_mem' := neg_mem'‚úù¬π ‚Ä¶
                                               -- ‚ä¢ toNonUnitalSubsemiring‚úù¬π = toNonUnitalSubsemiring‚úù
                                                      -- üéâ no goals

instance : NonUnitalSubringClass (NonUnitalSubring R) R
    where
  zero_mem s := s.zero_mem'
  add_mem {s} := s.add_mem'
  mul_mem {s} := s.mul_mem'
  neg_mem {s} := s.neg_mem'

theorem mem_carrier {s : NonUnitalSubring R} {x : R} : x ‚àà s.toNonUnitalSubsemiring ‚Üî x ‚àà s :=
  Iff.rfl

@[simp]
theorem mem_mk {S : NonUnitalSubsemiring R} {x : R} (h) :
    x ‚àà (‚ü®S, h‚ü© : NonUnitalSubring R) ‚Üî x ‚àà S :=
  Iff.rfl

@[simp]
theorem coe_set_mk (S : NonUnitalSubsemiring R) (h) :
    ((‚ü®S, h‚ü© : NonUnitalSubring R) : Set R) = S :=
  rfl

@[simp]
theorem mk_le_mk {S S' : NonUnitalSubsemiring R} (h h') :
    (‚ü®S, h‚ü© : NonUnitalSubring R) ‚â§ (‚ü®S', h'‚ü© : NonUnitalSubring R) ‚Üî S ‚â§ S' :=
  Iff.rfl

/-- Two non-unital subrings are equal if they have the same elements. -/
@[ext]
theorem ext {S T : NonUnitalSubring R} (h : ‚àÄ x, x ‚àà S ‚Üî x ‚àà T) : S = T :=
  SetLike.ext h

/-- Copy of a non-unital subring with a new `carrier` equal to the old one. Useful to fix
definitional equalities. -/
protected def copy (S : NonUnitalSubring R) (s : Set R) (hs : s = ‚ÜëS) : NonUnitalSubring R :=
  { S.toNonUnitalSubsemiring.copy s hs with
    carrier := s
    neg_mem' := hs.symm ‚ñ∏ S.neg_mem' }

@[simp]
theorem coe_copy (S : NonUnitalSubring R) (s : Set R) (hs : s = ‚ÜëS) : (S.copy s hs : Set R) = s :=
  rfl

theorem copy_eq (S : NonUnitalSubring R) (s : Set R) (hs : s = ‚ÜëS) : S.copy s hs = S :=
  SetLike.coe_injective hs

theorem toNonUnitalSubsemiring_injective :
    Function.Injective (toNonUnitalSubsemiring : NonUnitalSubring R ‚Üí NonUnitalSubsemiring R)
  | _r, _s, h => ext (SetLike.ext_iff.mp h : _)

@[mono]
theorem toNonUnitalSubsemiring_strictMono :
    StrictMono (toNonUnitalSubsemiring : NonUnitalSubring R ‚Üí NonUnitalSubsemiring R) := fun _ _ =>
  id

@[mono]
theorem toNonUnitalSubsemiring_mono :
    Monotone (toNonUnitalSubsemiring : NonUnitalSubring R ‚Üí NonUnitalSubsemiring R) :=
  toNonUnitalSubsemiring_strictMono.monotone

theorem toAddSubgroup_injective :
    Function.Injective (toAddSubgroup : NonUnitalSubring R ‚Üí AddSubgroup R)
  | _r, _s, h => ext (SetLike.ext_iff.mp h : _)

@[mono]
theorem toAddSubgroup_strictMono :
    StrictMono (toAddSubgroup : NonUnitalSubring R ‚Üí AddSubgroup R) := fun _ _ => id

@[mono]
theorem toAddSubgroup_mono : Monotone (toAddSubgroup : NonUnitalSubring R ‚Üí AddSubgroup R) :=
  toAddSubgroup_strictMono.monotone

theorem toSubsemigroup_injective :
    Function.Injective (toSubsemigroup : NonUnitalSubring R ‚Üí Subsemigroup R)
  | _r, _s, h => ext (SetLike.ext_iff.mp h : _)

@[mono]
theorem toSubsemigroup_strictMono :
    StrictMono (toSubsemigroup : NonUnitalSubring R ‚Üí Subsemigroup R) := fun _ _ => id

@[mono]
theorem toSubsemigroup_mono : Monotone (toSubsemigroup : NonUnitalSubring R ‚Üí Subsemigroup R) :=
  toSubsemigroup_strictMono.monotone

/-- Construct a `NonUnitalSubring R` from a set `s`, a subsemigroup `sm`, and an additive
subgroup `sa` such that `x ‚àà s ‚Üî x ‚àà sm ‚Üî x ‚àà sa`. -/
protected def mk' (s : Set R) (sm : Subsemigroup R) (sa : AddSubgroup R) (hm : ‚Üësm = s)
    (ha : ‚Üësa = s) : NonUnitalSubring R :=
  { sm.copy s hm.symm, sa.copy s ha.symm with }

@[simp]
theorem coe_mk' {s : Set R} {sm : Subsemigroup R} (hm : ‚Üësm = s) {sa : AddSubgroup R}
    (ha : ‚Üësa = s) : (NonUnitalSubring.mk' s sm sa hm ha : Set R) = s :=
  rfl

@[simp]
theorem mem_mk' {s : Set R} {sm : Subsemigroup R} (hm : ‚Üësm = s) {sa : AddSubgroup R} (ha : ‚Üësa = s)
    {x : R} : x ‚àà NonUnitalSubring.mk' s sm sa hm ha ‚Üî x ‚àà s :=
  Iff.rfl

@[simp]
theorem mk'_toSubsemigroup {s : Set R} {sm : Subsemigroup R} (hm : ‚Üësm = s) {sa : AddSubgroup R}
    (ha : ‚Üësa = s) : (NonUnitalSubring.mk' s sm sa hm ha).toSubsemigroup = sm :=
  SetLike.coe_injective hm.symm

@[simp]
theorem mk'_toAddSubgroup {s : Set R} {sm : Subsemigroup R} (hm : ‚Üësm = s) {sa : AddSubgroup R}
    (ha : ‚Üësa = s) : (NonUnitalSubring.mk' s sm sa hm ha).toAddSubgroup = sa :=
  SetLike.coe_injective ha.symm

end NonUnitalSubring

namespace NonUnitalSubring

variable (s : NonUnitalSubring R)

/-- A non-unital subring contains the ring's 0. -/
protected theorem zero_mem : (0 : R) ‚àà s :=
  zero_mem _

/-- A non-unital subring is closed under multiplication. -/
protected theorem mul_mem {x y : R} : x ‚àà s ‚Üí y ‚àà s ‚Üí x * y ‚àà s :=
  mul_mem

/-- A non-unital subring is closed under addition. -/
protected theorem add_mem {x y : R} : x ‚àà s ‚Üí y ‚àà s ‚Üí x + y ‚àà s :=
  add_mem

/-- A non-unital subring is closed under negation. -/
protected theorem neg_mem {x : R} : x ‚àà s ‚Üí -x ‚àà s :=
  neg_mem

/-- A non-unital subring is closed under subtraction -/
protected theorem sub_mem {x y : R} (hx : x ‚àà s) (hy : y ‚àà s) : x - y ‚àà s :=
  sub_mem hx hy

/-- Sum of a list of elements in a non-unital subring is in the non-unital subring. -/
protected theorem list_sum_mem {l : List R} : (‚àÄ x ‚àà l, x ‚àà s) ‚Üí l.sum ‚àà s :=
  list_sum_mem

/-- Sum of a multiset of elements in a `NonUnitalSubring` of a `NonUnitalRing` is
in the `NonUnitalSubring`. -/
protected theorem multiset_sum_mem {R} [NonUnitalNonAssocRing R] (s : NonUnitalSubring R)
    (m : Multiset R) : (‚àÄ a ‚àà m, a ‚àà s) ‚Üí m.sum ‚àà s :=
  multiset_sum_mem _

/-- Sum of elements in a `NonUnitalSubring` of a `NonUnitalRing` indexed by a `Finset`
is in the `NonUnitalSubring`. -/
protected theorem sum_mem {R : Type*} [NonUnitalNonAssocRing R] (s : NonUnitalSubring R)
    {Œπ : Type*} {t : Finset Œπ} {f : Œπ ‚Üí R} (h : ‚àÄ c ‚àà t, f c ‚àà s) : (‚àë i in t, f i) ‚àà s :=
  sum_mem h

/-- A non-unital subring of a non-unital ring inherits a non-unital ring structure -/
instance toNonUnitalRing {R : Type*} [NonUnitalRing R] (s : NonUnitalSubring R) :
    NonUnitalRing s :=
  Subtype.coe_injective.nonUnitalRing _ rfl (fun _ _ => rfl) (fun _ _ => rfl) (fun _ => rfl)
    (fun _ _ => rfl) (fun _ _ => rfl) fun _ _ => rfl

protected theorem zsmul_mem {x : R} (hx : x ‚àà s) (n : ‚Ñ§) : n ‚Ä¢ x ‚àà s :=
  zsmul_mem hx n

@[simp, norm_cast]
theorem val_add (x y : s) : (‚Üë(x + y) : R) = ‚Üëx + ‚Üëy :=
  rfl

@[simp, norm_cast]
theorem val_neg (x : s) : (‚Üë(-x) : R) = -‚Üëx :=
  rfl

@[simp, norm_cast]
theorem val_mul (x y : s) : (‚Üë(x * y) : R) = ‚Üëx * ‚Üëy :=
  rfl

@[simp, norm_cast]
theorem val_zero : ((0 : s) : R) = 0 :=
  rfl

theorem coe_eq_zero_iff {x : s} : (x : R) = 0 ‚Üî x = 0 := by
  simp
  -- üéâ no goals

/-- A non-unital subring of a `NonUnitalCommRing` is a `NonUnitalCommRing`. -/
instance toNonUnitalCommRing {R} [NonUnitalCommRing R] (s : NonUnitalSubring R) :
    NonUnitalCommRing s :=
  Subtype.coe_injective.nonUnitalCommRing _ rfl (fun _ _ => rfl) (fun _ _ => rfl) (fun _ => rfl)
    (fun _ _ => rfl) (fun _ _ => rfl) fun _ _ => rfl

/-! ## Partial order -/


@[simp]
theorem mem_toSubsemigroup {s : NonUnitalSubring R} {x : R} : x ‚àà s.toSubsemigroup ‚Üî x ‚àà s :=
  Iff.rfl

@[simp]
theorem coe_toSubsemigroup (s : NonUnitalSubring R) : (s.toSubsemigroup : Set R) = s :=
  rfl

@[simp]
theorem mem_toAddSubgroup {s : NonUnitalSubring R} {x : R} : x ‚àà s.toAddSubgroup ‚Üî x ‚àà s :=
  Iff.rfl

@[simp]
theorem coe_toAddSubgroup (s : NonUnitalSubring R) : (s.toAddSubgroup : Set R) = s :=
  rfl

@[simp]
theorem mem_toNonUnitalSubsemiring {s : NonUnitalSubring R} {x : R} :
    x ‚àà s.toNonUnitalSubsemiring ‚Üî x ‚àà s :=
  Iff.rfl

@[simp]
theorem coe_toNonUnitalSubsemiring (s : NonUnitalSubring R) :
    (s.toNonUnitalSubsemiring : Set R) = s :=
  rfl

/-! ## top -/


/-- The non-unital subring `R` of the ring `R`. -/
instance : Top (NonUnitalSubring R) :=
  ‚ü®{ (‚ä§ : Subsemigroup R), (‚ä§ : AddSubgroup R) with }‚ü©

@[simp]
theorem mem_top (x : R) : x ‚àà (‚ä§ : NonUnitalSubring R) :=
  Set.mem_univ x

@[simp]
theorem coe_top : ((‚ä§ : NonUnitalSubring R) : Set R) = Set.univ :=
  rfl

/-- The ring equiv between the top element of `NonUnitalSubring R` and `R`. -/
@[simps!]
def topEquiv : (‚ä§ : NonUnitalSubring R) ‚âÉ+* R := NonUnitalSubsemiring.topEquiv

end NonUnitalSubring

end Basic

section Hom

namespace NonUnitalSubring

variable {F : Type w} {R : Type u} {S : Type v} {T : Type*} {SR : Type*}
  [NonUnitalNonAssocRing R] [NonUnitalNonAssocRing S] [NonUnitalNonAssocRing T]
  [NonUnitalRingHomClass F R S] (s : NonUnitalSubring R)

/-! ## comap -/


/-- The preimage of a `NonUnitalSubring` along a ring homomorphism is a `NonUnitalSubring`. -/
def comap {F : Type w} {R : Type u} {S : Type v} [NonUnitalNonAssocRing R] [NonUnitalNonAssocRing S]
    [NonUnitalRingHomClass F R S] (f : F) (s : NonUnitalSubring S) : NonUnitalSubring R :=
  { s.toSubsemigroup.comap (f : R ‚Üí‚Çô* S), s.toAddSubgroup.comap (f : R ‚Üí+ S) with
    carrier := f ‚Åª¬π' s.carrier }

@[simp]
theorem coe_comap (s : NonUnitalSubring S) (f : F) : (s.comap f : Set R) = f ‚Åª¬π' s :=
  rfl

@[simp]
theorem mem_comap {s : NonUnitalSubring S} {f : F} {x : R} : x ‚àà s.comap f ‚Üî f x ‚àà s :=
  Iff.rfl

theorem comap_comap (s : NonUnitalSubring T) (g : S ‚Üí‚Çô+* T) (f : R ‚Üí‚Çô+* S) :
    (s.comap g).comap f = s.comap (g.comp f) :=
  rfl

/-! ## map -/

/-- The image of a `NonUnitalSubring` along a ring homomorphism is a `NonUnitalSubring`. -/
def map {F : Type w} {R : Type u} {S : Type v} [NonUnitalNonAssocRing R] [NonUnitalNonAssocRing S]
    [NonUnitalRingHomClass F R S] (f : F) (s : NonUnitalSubring R) : NonUnitalSubring S :=
  { s.toSubsemigroup.map (f : R ‚Üí‚Çô* S), s.toAddSubgroup.map (f : R ‚Üí+ S) with
    carrier := f '' s.carrier }

@[simp]
theorem coe_map (f : F) (s : NonUnitalSubring R) : (s.map f : Set S) = f '' s :=
  rfl

@[simp]
theorem mem_map {f : F} {s : NonUnitalSubring R} {y : S} : y ‚àà s.map f ‚Üî ‚àÉ x ‚àà s, f x = y :=
  Set.mem_image _ _ _

@[simp]
theorem map_id : s.map (NonUnitalRingHom.id R) = s :=
  SetLike.coe_injective <| Set.image_id _

theorem map_map (g : S ‚Üí‚Çô+* T) (f : R ‚Üí‚Çô+* S) : (s.map f).map g = s.map (g.comp f) :=
  SetLike.coe_injective <| Set.image_image _ _ _

theorem map_le_iff_le_comap {f : F} {s : NonUnitalSubring R} {t : NonUnitalSubring S} :
    s.map f ‚â§ t ‚Üî s ‚â§ t.comap f :=
  Set.image_subset_iff

theorem gc_map_comap (f : F) :
    GaloisConnection (map f : NonUnitalSubring R ‚Üí NonUnitalSubring S) (comap f) := fun _S _T =>
  map_le_iff_le_comap

/-- A `NonUnitalSubring` is isomorphic to its image under an injective function -/
noncomputable def equivMapOfInjective (f : F) (hf : Function.Injective (f : R ‚Üí S)) :
    s ‚âÉ+* s.map f :=
  {
    Equiv.Set.image f s
      hf with
    map_mul' := fun _ _ => Subtype.ext (map_mul f _ _)
    map_add' := fun _ _ => Subtype.ext (map_add f _ _) }

@[simp]
theorem coe_equivMapOfInjective_apply (f : F) (hf : Function.Injective f) (x : s) :
    (equivMapOfInjective s f hf x : S) = f x :=
  rfl

end NonUnitalSubring

namespace NonUnitalRingHom

variable {R : Type u} {S : Type v} {T : Type*}
  [NonUnitalNonAssocRing R] [NonUnitalNonAssocRing S] [NonUnitalNonAssocRing T]
  (g : S ‚Üí‚Çô+* T) (f : R ‚Üí‚Çô+* S)

/-! ## range -/

/-- The range of a ring homomorphism, as a `NonUnitalSubring` of the target.
See Note [range copy pattern]. -/
def range {R : Type u} {S : Type v} [NonUnitalNonAssocRing R] [NonUnitalNonAssocRing S]
    (f : R ‚Üí‚Çô+* S) : NonUnitalSubring S :=
  ((‚ä§ : NonUnitalSubring R).map f).copy (Set.range f) Set.image_univ.symm

@[simp]
theorem coe_range : (f.range : Set S) = Set.range f :=
  rfl

@[simp]
theorem mem_range {f : R ‚Üí‚Çô+* S} {y : S} : y ‚àà f.range ‚Üî ‚àÉ x, f x = y :=
  Iff.rfl

theorem range_eq_map (f : R ‚Üí‚Çô+* S) : f.range = NonUnitalSubring.map f ‚ä§ := by ext; simp
                                                                               -- ‚ä¢ x‚úù ‚àà range f ‚Üî x‚úù ‚àà NonUnitalSubring.map f ‚ä§
                                                                                    -- üéâ no goals

theorem mem_range_self (f : R ‚Üí‚Çô+* S) (x : R) : f x ‚àà f.range :=
  mem_range.mpr ‚ü®x, rfl‚ü©

theorem map_range : f.range.map g = (g.comp f).range := by
  simpa only [range_eq_map] using (‚ä§ : NonUnitalSubring R).map_map g f
  -- üéâ no goals

/-- The range of a ring homomorphism is a fintype, if the domain is a fintype.
Note: this instance can form a diamond with `Subtype.fintype` in the
  presence of `Fintype S`. -/
instance fintypeRange [Fintype R] [DecidableEq S] (f : R ‚Üí‚Çô+* S) : Fintype (range f) :=
  Set.fintypeRange f

end NonUnitalRingHom

namespace NonUnitalSubring

section Order

variable {F : Type w} {R : Type u} {S : Type v} {T : Type*}
  [NonUnitalNonAssocRing R] [NonUnitalNonAssocRing S] [NonUnitalNonAssocRing T]
  [NonUnitalRingHomClass F R S]
  (g : S ‚Üí‚Çô+* T) (f : R ‚Üí‚Çô+* S)

/-! ## bot -/


instance : Bot (NonUnitalSubring R) :=
  ‚ü®(0 : R ‚Üí‚Çô+* R).range‚ü©

instance : Inhabited (NonUnitalSubring R) :=
  ‚ü®‚ä•‚ü©

theorem coe_bot : ((‚ä• : NonUnitalSubring R) : Set R) = {0} :=
  (NonUnitalRingHom.coe_range (0 : R ‚Üí‚Çô+* R)).trans (@Set.range_const R R _ 0)

theorem mem_bot {x : R} : x ‚àà (‚ä• : NonUnitalSubring R) ‚Üî x = 0 :=
  show x ‚àà ((‚ä• : NonUnitalSubring R) : Set R) ‚Üî x = 0 by rw [coe_bot, Set.mem_singleton_iff]
                                                         -- üéâ no goals

/-! ## inf -/

/-- The inf of two `NonUnitalSubring`s is their intersection. -/
instance : Inf (NonUnitalSubring R) :=
  ‚ü®fun s t =>
    { s.toSubsemigroup ‚äì t.toSubsemigroup, s.toAddSubgroup ‚äì t.toAddSubgroup with
      carrier := s ‚à© t }‚ü©

@[simp]
theorem coe_inf (p p' : NonUnitalSubring R) :
  ((p ‚äì p' : NonUnitalSubring R) : Set R) = (p : Set R) ‚à© p' :=
  rfl

@[simp]
theorem mem_inf {p p' : NonUnitalSubring R} {x : R} : x ‚àà p ‚äì p' ‚Üî x ‚àà p ‚àß x ‚àà p' :=
  Iff.rfl

instance : InfSet (NonUnitalSubring R) :=
  ‚ü®fun s =>
    NonUnitalSubring.mk' (‚ãÇ t ‚àà s, ‚Üët) (‚®Ö t ‚àà s, NonUnitalSubring.toSubsemigroup t)
      (‚®Ö t ‚àà s, NonUnitalSubring.toAddSubgroup t) (by simp) (by simp)‚ü©
                                                      -- üéâ no goals
                                                                -- üéâ no goals

@[simp, norm_cast]
theorem coe_sInf (S : Set (NonUnitalSubring R)) :
    ((sInf S : NonUnitalSubring R) : Set R) = ‚ãÇ s ‚àà S, ‚Üës :=
  rfl

theorem mem_sInf {S : Set (NonUnitalSubring R)} {x : R} : x ‚àà sInf S ‚Üî ‚àÄ p ‚àà S, x ‚àà p :=
  Set.mem_iInter‚ÇÇ

@[simp, norm_cast]
theorem coe_iInf {Œπ : Sort*} {S : Œπ ‚Üí NonUnitalSubring R} : (‚Üë(‚®Ö i, S i) : Set R) = ‚ãÇ i, S i := by
  simp only [iInf, coe_sInf, Set.biInter_range]
  -- üéâ no goals

theorem mem_iInf {Œπ : Sort*} {S : Œπ ‚Üí NonUnitalSubring R} {x : R} :
    (x ‚àà ‚®Ö i, S i) ‚Üî ‚àÄ i, x ‚àà S i := by simp only [iInf, mem_sInf, Set.forall_range_iff]
                                        -- üéâ no goals

@[simp]
theorem sInf_toSubsemigroup (s : Set (NonUnitalSubring R)) :
    (sInf s).toSubsemigroup = ‚®Ö t ‚àà s, NonUnitalSubring.toSubsemigroup t :=
  mk'_toSubsemigroup _ _

@[simp]
theorem sInf_toAddSubgroup (s : Set (NonUnitalSubring R)) :
    (sInf s).toAddSubgroup = ‚®Ö t ‚àà s, NonUnitalSubring.toAddSubgroup t :=
  mk'_toAddSubgroup _ _

/-- `NonUnitalSubring`s of a ring form a complete lattice. -/
instance : CompleteLattice (NonUnitalSubring R) :=
  { completeLatticeOfInf (NonUnitalSubring R) fun _s =>
      IsGLB.of_image (@fun _ _ : NonUnitalSubring R => SetLike.coe_subset_coe)
        isGLB_biInf with
    bot := ‚ä•
    bot_le := fun s _x hx => (mem_bot.mp hx).symm ‚ñ∏ zero_mem s
    top := ‚ä§
    le_top := fun _ _ _ => trivial
    inf := (¬∑ ‚äì ¬∑)
    inf_le_left := fun _ _ _ => And.left
    inf_le_right := fun _ _ _ => And.right
    le_inf := fun _s _t‚ÇÅ _t‚ÇÇ h‚ÇÅ h‚ÇÇ _x hx => ‚ü®h‚ÇÅ hx, h‚ÇÇ hx‚ü© }

theorem eq_top_iff' (A : NonUnitalSubring R) : A = ‚ä§ ‚Üî ‚àÄ x : R, x ‚àà A :=
  eq_top_iff.trans ‚ü®fun h m => h <| mem_top m, fun h m _ => h m‚ü©

end Order

/-! ## Center of a ring -/

section

variable {R : Type u} [NonUnitalRing R]

variable (R)

/-- The center of a ring `R` is the set of elements that commute with everything in `R` -/
def center : NonUnitalSubring R :=
  { NonUnitalSubsemiring.center R with
    carrier := Set.center R
    neg_mem' := Set.neg_mem_center }

theorem coe_center : ‚Üë(center R) = Set.center R :=
  rfl

@[simp]
theorem center_toNonUnitalSubsemiring :
    (center R).toNonUnitalSubsemiring = NonUnitalSubsemiring.center R :=
  rfl

variable {R}

theorem mem_center_iff {z : R} : z ‚àà center R ‚Üî ‚àÄ g, g * z = z * g :=
  Iff.rfl

instance decidableMemCenter [DecidableEq R] [Fintype R] : DecidablePred (¬∑ ‚àà center R) := fun _ =>
  decidable_of_iff' _ mem_center_iff

@[simp]
theorem center_eq_top (R) [NonUnitalCommRing R] : center R = ‚ä§ :=
  SetLike.coe_injective (Set.center_eq_univ R)

/-- The center is commutative. -/
instance center.instNonUnitalCommRing : NonUnitalCommRing (center R) :=
  { NonUnitalSubsemiring.center.instNonUnitalCommSemiring,
    (center R).toNonUnitalRing with }

end

/-! ## `NonUnitalSubring` closure of a subset -/

variable {F : Type w} {R : Type u} {S : Type v} {T : Type*}
  [NonUnitalNonAssocRing R] [NonUnitalNonAssocRing S] [NonUnitalNonAssocRing T]
  [NonUnitalRingHomClass F R S]
  (g : S ‚Üí‚Çô+* T) (f : R ‚Üí‚Çô+* S)

/-- The `NonUnitalSubring` generated by a set. -/
def closure (s : Set R) : NonUnitalSubring R :=
  sInf {S | s ‚äÜ S}

theorem mem_closure {x : R} {s : Set R} : x ‚àà closure s ‚Üî ‚àÄ S : NonUnitalSubring R, s ‚äÜ S ‚Üí x ‚àà S :=
  mem_sInf

/-- The `NonUnitalSubring` generated by a set includes the set. -/
@[simp]
theorem subset_closure {s : Set R} : s ‚äÜ closure s := fun _x hx => mem_closure.2 fun _S hS => hS hx

theorem not_mem_of_not_mem_closure {s : Set R} {P : R} (hP : P ‚àâ closure s) : P ‚àâ s := fun h =>
  hP (subset_closure h)

/-- A `NonUnitalSubring` `t` includes `closure s` if and only if it includes `s`. -/
@[simp]
theorem closure_le {s : Set R} {t : NonUnitalSubring R} : closure s ‚â§ t ‚Üî s ‚äÜ t :=
  ‚ü®Set.Subset.trans subset_closure, fun h => sInf_le h‚ü©

/-- `NonUnitalSubring` closure of a set is monotone in its argument: if `s ‚äÜ t`,
then `closure s ‚â§ closure t`. -/
theorem closure_mono ‚¶És t : Set R‚¶Ñ (h : s ‚äÜ t) : closure s ‚â§ closure t :=
  closure_le.2 <| Set.Subset.trans h subset_closure

theorem closure_eq_of_le {s : Set R} {t : NonUnitalSubring R} (h‚ÇÅ : s ‚äÜ t) (h‚ÇÇ : t ‚â§ closure s) :
    closure s = t :=
  le_antisymm (closure_le.2 h‚ÇÅ) h‚ÇÇ

/-- An induction principle for closure membership. If `p` holds for `0`, `1`, and all elements
of `s`, and is preserved under addition, negation, and multiplication, then `p` holds for all
elements of the closure of `s`. -/
@[elab_as_elim]
theorem closure_induction {s : Set R} {p : R ‚Üí Prop} {x} (h : x ‚àà closure s) (Hs : ‚àÄ x ‚àà s, p x)
    (H0 : p 0) (Hadd : ‚àÄ x y, p x ‚Üí p y ‚Üí p (x + y)) (Hneg : ‚àÄ x : R, p x ‚Üí p (-x))
    (Hmul : ‚àÄ x y, p x ‚Üí p y ‚Üí p (x * y)) : p x :=
  (@closure_le _ _ _ ‚ü®‚ü®‚ü®‚ü®p, Hadd _ _‚ü©, H0‚ü©, Hmul _ _‚ü©, Hneg _‚ü©).2 Hs h

/-- The difference with `NonUnitalSubring.closure_induction` is that this acts on the
subtype. -/
@[elab_as_elim]
theorem closure_induction' {s : Set R} {p : closure s ‚Üí Prop} (a : closure s)
    (Hs : ‚àÄ (x) (hx : x ‚àà s), p ‚ü®x, subset_closure hx‚ü©) (H0 : p 0)
    (Hadd : ‚àÄ x y, p x ‚Üí p y ‚Üí p (x + y)) (Hneg : ‚àÄ x, p x ‚Üí p (-x))
    (Hmul : ‚àÄ x y, p x ‚Üí p y ‚Üí p (x * y)) : p a :=
  Subtype.recOn a fun b hb => by
    refine' Exists.elim _ fun (hb : b ‚àà closure s) (hc : p ‚ü®b, hb‚ü©) => hc
    -- ‚ä¢ ‚àÉ x, p { val := b, property := x }
    refine'
      closure_induction hb (fun x hx => ‚ü®subset_closure hx, Hs x hx‚ü©) ‚ü®zero_mem (closure s), H0‚ü© _ _
        _
    ¬∑ rintro x y ‚ü®hx, hpx‚ü© ‚ü®hy, hpy‚ü©
      -- ‚ä¢ ‚àÉ x_1, p { val := x + y, property := x_1 }
      exact ‚ü®add_mem hx hy, Hadd _ _ hpx hpy‚ü©
      -- üéâ no goals
    ¬∑ rintro x ‚ü®hx, hpx‚ü©
      -- ‚ä¢ ‚àÉ x_1, p { val := -x, property := x_1 }
      exact ‚ü®neg_mem hx, Hneg _ hpx‚ü©
      -- üéâ no goals
    ¬∑ rintro x y ‚ü®hx, hpx‚ü© ‚ü®hy, hpy‚ü©
      -- ‚ä¢ ‚àÉ x_1, p { val := x * y, property := x_1 }
      exact ‚ü®mul_mem hx hy, Hmul _ _ hpx hpy‚ü©
      -- üéâ no goals

/-- An induction principle for closure membership, for predicates with two arguments. -/
@[elab_as_elim]
theorem closure_induction‚ÇÇ {s : Set R} {p : R ‚Üí R ‚Üí Prop} {a b : R} (ha : a ‚àà closure s)
    (hb : b ‚àà closure s) (Hs : ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, p x y) (H0_left : ‚àÄ x, p 0 x)
    (H0_right : ‚àÄ x, p x 0) (Hneg_left : ‚àÄ x y, p x y ‚Üí p (-x) y)
    (Hneg_right : ‚àÄ x y, p x y ‚Üí p x (-y)) (Hadd_left : ‚àÄ x‚ÇÅ x‚ÇÇ y, p x‚ÇÅ y ‚Üí p x‚ÇÇ y ‚Üí p (x‚ÇÅ + x‚ÇÇ) y)
    (Hadd_right : ‚àÄ x y‚ÇÅ y‚ÇÇ, p x y‚ÇÅ ‚Üí p x y‚ÇÇ ‚Üí p x (y‚ÇÅ + y‚ÇÇ))
    (Hmul_left : ‚àÄ x‚ÇÅ x‚ÇÇ y, p x‚ÇÅ y ‚Üí p x‚ÇÇ y ‚Üí p (x‚ÇÅ * x‚ÇÇ) y)
    (Hmul_right : ‚àÄ x y‚ÇÅ y‚ÇÇ, p x y‚ÇÅ ‚Üí p x y‚ÇÇ ‚Üí p x (y‚ÇÅ * y‚ÇÇ)) : p a b := by
  refine' closure_induction hb _ (H0_right _) (Hadd_right a) (Hneg_right a) (Hmul_right a)
  -- ‚ä¢ ‚àÄ (x : R), x ‚àà s ‚Üí p a x
  refine' closure_induction ha Hs (fun x _ => H0_left x) _ _ _
  ¬∑ exact fun x y H‚ÇÅ H‚ÇÇ z zs => Hadd_left x y z (H‚ÇÅ z zs) (H‚ÇÇ z zs)
    -- üéâ no goals
  ¬∑ exact fun x hx z zs => Hneg_left x z (hx z zs)
    -- üéâ no goals
  ¬∑ exact fun x y H‚ÇÅ H‚ÇÇ z zs => Hmul_left x y z (H‚ÇÅ z zs) (H‚ÇÇ z zs)
    -- üéâ no goals

theorem mem_closure_iff {s : Set R} {x} :
    x ‚àà closure s ‚Üî x ‚àà AddSubgroup.closure (Subsemigroup.closure s : Set R) :=
  ‚ü®fun h =>
    closure_induction h (fun x hx => AddSubgroup.subset_closure <| Subsemigroup.subset_closure hx)
      (AddSubgroup.zero_mem _) (fun x y hx hy => AddSubgroup.add_mem _ hx hy)
      (fun x hx => AddSubgroup.neg_mem _ hx) fun x y hx hy =>
      AddSubgroup.closure_induction hy
        (fun q hq =>
          AddSubgroup.closure_induction hx
            (fun p hp => AddSubgroup.subset_closure ((Subsemigroup.closure s).mul_mem hp hq))
            (by rw [zero_mul q]; apply AddSubgroup.zero_mem _)
                -- ‚ä¢ 0 ‚àà AddSubgroup.closure ‚Üë(Subsemigroup.closure s)
                                 -- üéâ no goals
            (fun p‚ÇÅ p‚ÇÇ ihp‚ÇÅ ihp‚ÇÇ => by rw [add_mul p‚ÇÅ p‚ÇÇ q]; apply AddSubgroup.add_mem _ ihp‚ÇÅ ihp‚ÇÇ)
                                       -- ‚ä¢ p‚ÇÅ * q + p‚ÇÇ * q ‚àà AddSubgroup.closure ‚Üë(Subsemigroup.closure s)
                                                             -- üéâ no goals
            fun x hx => by
            have f : -x * q = -(x * q) := by simp
            -- ‚ä¢ -x * q ‚àà AddSubgroup.closure ‚Üë(Subsemigroup.closure s)
            rw [f]; apply AddSubgroup.neg_mem _ hx)
            -- ‚ä¢ -(x * q) ‚àà AddSubgroup.closure ‚Üë(Subsemigroup.closure s)
                    -- üéâ no goals
        (by rw [mul_zero x]; apply AddSubgroup.zero_mem _)
            -- ‚ä¢ 0 ‚àà AddSubgroup.closure ‚Üë(Subsemigroup.closure s)
                             -- üéâ no goals
        (fun q‚ÇÅ q‚ÇÇ ihq‚ÇÅ ihq‚ÇÇ => by rw [mul_add x q‚ÇÅ q‚ÇÇ]; apply AddSubgroup.add_mem _ ihq‚ÇÅ ihq‚ÇÇ)
                                   -- ‚ä¢ x * q‚ÇÅ + x * q‚ÇÇ ‚àà AddSubgroup.closure ‚Üë(Subsemigroup.closure s)
                                                         -- üéâ no goals
        fun z hz => by
        have f : x * -z = -(x * z) := by simp
        -- ‚ä¢ x * -z ‚àà AddSubgroup.closure ‚Üë(Subsemigroup.closure s)
        rw [f]; apply AddSubgroup.neg_mem _ hz,
        -- ‚ä¢ -(x * z) ‚àà AddSubgroup.closure ‚Üë(Subsemigroup.closure s)
                -- üéâ no goals
    fun h =>
    AddSubgroup.closure_induction h
      (fun x hx =>
        Subsemigroup.closure_induction hx (fun x hx => subset_closure hx) fun x y hx hy =>
          mul_mem hx hy)
      (zero_mem _) (fun x y hx hy => add_mem hx hy) fun x hx => neg_mem hx‚ü©

/-- If all elements of `s : Set A` commute pairwise, then `closure s` is a commutative ring.  -/
def closureNonUnitalCommRingOfComm {R : Type u} [NonUnitalRing R] {s : Set R}
    (hcomm : ‚àÄ a ‚àà s, ‚àÄ b ‚àà s, a * b = b * a) : NonUnitalCommRing (closure s) :=
  { (closure s).toNonUnitalRing with
    mul_comm := fun x y => by
      ext
      -- ‚ä¢ ‚Üë(x * y) = ‚Üë(y * x)
      simp only [NonUnitalSubring.val_mul]
      -- ‚ä¢ ‚Üëx * ‚Üëy = ‚Üëy * ‚Üëx
      refine'
        closure_induction‚ÇÇ x.prop y.prop hcomm
          (fun x => by simp only [mul_zero, zero_mul])
          (fun x => by simp only [mul_zero, zero_mul])
          (fun x y hxy => by simp only [mul_neg, neg_mul, hxy])
          (fun x y hxy => by simp only [mul_neg, neg_mul, hxy])
          (fun x‚ÇÅ x‚ÇÇ y h‚ÇÅ h‚ÇÇ => by simp only [add_mul, mul_add, h‚ÇÅ, h‚ÇÇ])
          (fun x‚ÇÅ x‚ÇÇ y h‚ÇÅ h‚ÇÇ => by simp only [add_mul, mul_add, h‚ÇÅ, h‚ÇÇ])
          (fun x‚ÇÅ x‚ÇÇ y h‚ÇÅ h‚ÇÇ => by rw [‚Üê mul_assoc, ‚Üê h‚ÇÅ, mul_assoc x‚ÇÅ y x‚ÇÇ, ‚Üê h‚ÇÇ, mul_assoc])
          fun x‚ÇÅ x‚ÇÇ y h‚ÇÅ h‚ÇÇ => by rw [‚Üê mul_assoc, h‚ÇÅ, mul_assoc, h‚ÇÇ, ‚Üê mul_assoc] }

variable (R)

/-- `closure` forms a Galois insertion with the coercion to set. -/
protected def gi : GaloisInsertion (@closure R _) SetLike.coe
    where
  choice s _ := closure s
  gc _s _t := closure_le
  le_l_u _s := subset_closure
  choice_eq _s _h := rfl

variable {R}

/-- Closure of a `NonUnitalSubring` `S` equals `S`. -/
theorem closure_eq (s : NonUnitalSubring R) : closure (s : Set R) = s :=
  (NonUnitalSubring.gi R).l_u_eq s

@[simp]
theorem closure_empty : closure (‚àÖ : Set R) = ‚ä• :=
  (NonUnitalSubring.gi R).gc.l_bot

@[simp]
theorem closure_univ : closure (Set.univ : Set R) = ‚ä§ :=
  @coe_top R _ ‚ñ∏ closure_eq ‚ä§

theorem closure_union (s t : Set R) : closure (s ‚à™ t) = closure s ‚äî closure t :=
  (NonUnitalSubring.gi R).gc.l_sup

theorem closure_iUnion {Œπ} (s : Œπ ‚Üí Set R) : closure (‚ãÉ i, s i) = ‚®Ü i, closure (s i) :=
  (NonUnitalSubring.gi R).gc.l_iSup

theorem closure_sUnion (s : Set (Set R)) : closure (‚ãÉ‚ÇÄ s) = ‚®Ü t ‚àà s, closure t :=
  (NonUnitalSubring.gi R).gc.l_sSup

theorem map_sup (s t : NonUnitalSubring R) (f : F) : (s ‚äî t).map f = s.map f ‚äî t.map f :=
  (@gc_map_comap F R S _ _ _ f).l_sup

theorem map_iSup {Œπ : Sort*} (f : F) (s : Œπ ‚Üí NonUnitalSubring R) :
    (iSup s).map f = ‚®Ü i, (s i).map f :=
  (@gc_map_comap F R S _ _ _ f).l_iSup

theorem comap_inf (s t : NonUnitalSubring S) (f : F) : (s ‚äì t).comap f = s.comap f ‚äì t.comap f :=
  (@gc_map_comap F R S _ _ _ f).u_inf

theorem comap_iInf {Œπ : Sort*} (f : F) (s : Œπ ‚Üí NonUnitalSubring S) :
    (iInf s).comap f = ‚®Ö i, (s i).comap f :=
  (@gc_map_comap F R S _ _ _ f).u_iInf

@[simp]
theorem map_bot (f : R ‚Üí‚Çô+* S) : (‚ä• : NonUnitalSubring R).map f = ‚ä• :=
  (gc_map_comap f).l_bot

@[simp]
theorem comap_top (f : R ‚Üí‚Çô+* S) : (‚ä§ : NonUnitalSubring S).comap f = ‚ä§ :=
  (gc_map_comap f).u_top

/-- Given `NonUnitalSubring`s `s`, `t` of rings `R`, `S` respectively, `s.prod t` is `s √óÀ¢ t`
as a `NonUnitalSubring` of `R √ó S`. -/
def prod (s : NonUnitalSubring R) (t : NonUnitalSubring S) : NonUnitalSubring (R √ó S) :=
  { s.toSubsemigroup.prod t.toSubsemigroup, s.toAddSubgroup.prod t.toAddSubgroup with
    carrier := s √óÀ¢ t }

@[norm_cast]
theorem coe_prod (s : NonUnitalSubring R) (t : NonUnitalSubring S) :
    (s.prod t : Set (R √ó S)) = (s : Set R) √óÀ¢ t :=
  rfl

theorem mem_prod {s : NonUnitalSubring R} {t : NonUnitalSubring S} {p : R √ó S} :
    p ‚àà s.prod t ‚Üî p.1 ‚àà s ‚àß p.2 ‚àà t :=
  Iff.rfl

@[mono]
theorem prod_mono ‚¶És‚ÇÅ s‚ÇÇ : NonUnitalSubring R‚¶Ñ (hs : s‚ÇÅ ‚â§ s‚ÇÇ) ‚¶Ét‚ÇÅ t‚ÇÇ : NonUnitalSubring S‚¶Ñ
    (ht : t‚ÇÅ ‚â§ t‚ÇÇ) : s‚ÇÅ.prod t‚ÇÅ ‚â§ s‚ÇÇ.prod t‚ÇÇ :=
  Set.prod_mono hs ht

theorem prod_mono_right (s : NonUnitalSubring R) :
    Monotone fun t : NonUnitalSubring S => s.prod t :=
  prod_mono (le_refl s)

theorem prod_mono_left (t : NonUnitalSubring S) : Monotone fun s : NonUnitalSubring R => s.prod t :=
  fun _s‚ÇÅ _s‚ÇÇ hs => prod_mono hs (le_refl t)

theorem prod_top (s : NonUnitalSubring R) :
    s.prod (‚ä§ : NonUnitalSubring S) = s.comap (NonUnitalRingHom.fst R S) :=
  ext fun x => by simp [mem_prod, MonoidHom.coe_fst]
                  -- üéâ no goals

theorem top_prod (s : NonUnitalSubring S) :
    (‚ä§ : NonUnitalSubring R).prod s = s.comap (NonUnitalRingHom.snd R S) :=
  ext fun x => by simp [mem_prod, MonoidHom.coe_snd]
                  -- üéâ no goals

@[simp]
theorem top_prod_top : (‚ä§ : NonUnitalSubring R).prod (‚ä§ : NonUnitalSubring S) = ‚ä§ :=
  (top_prod _).trans <| comap_top _

/-- Product of `NonUnitalSubring`s is isomorphic to their product as rings. -/
def prodEquiv (s : NonUnitalSubring R) (t : NonUnitalSubring S) : s.prod t ‚âÉ+* s √ó t :=
  { Equiv.Set.prod (s : Set R) (t : Set S) with
    map_mul' := fun _ _ => rfl
    map_add' := fun _ _ => rfl }

/-- The underlying set of a non-empty directed Sup of `NonUnitalSubring`s is just a union of the
`NonUnitalSubring`s. Note that this fails without the directedness assumption (the union of two
`NonUnitalSubring`s is typically not a `NonUnitalSubring`) -/
theorem mem_iSup_of_directed {Œπ} [hŒπ : Nonempty Œπ] {S : Œπ ‚Üí NonUnitalSubring R}
    (hS : Directed (¬∑ ‚â§ ¬∑) S) {x : R} : (x ‚àà ‚®Ü i, S i) ‚Üî ‚àÉ i, x ‚àà S i := by
  refine' ‚ü®_, fun ‚ü®i, hi‚ü© => (SetLike.le_def.1 <| le_iSup S i) hi‚ü©
  -- ‚ä¢ x ‚àà ‚®Ü (i : Œπ), S i ‚Üí ‚àÉ i, x ‚àà S i
  let U : NonUnitalSubring R :=
    NonUnitalSubring.mk' (‚ãÉ i, (S i : Set R)) (‚®Ü i, (S i).toSubsemigroup) (‚®Ü i, (S i).toAddSubgroup)
      (Subsemigroup.coe_iSup_of_directed <| hS.mono_comp _ fun _ _ => id)
      (AddSubgroup.coe_iSup_of_directed <| hS.mono_comp _ fun _ _ => id)
  suffices (‚®Ü i, S i) ‚â§ U by simpa using @this x
  -- ‚ä¢ ‚®Ü (i : Œπ), S i ‚â§ U
  exact iSup_le fun i x hx => Set.mem_iUnion.2 ‚ü®i, hx‚ü©
  -- üéâ no goals

theorem coe_iSup_of_directed {Œπ} [hŒπ : Nonempty Œπ] {S : Œπ ‚Üí NonUnitalSubring R}
    (hS : Directed (¬∑ ‚â§ ¬∑) S) : ((‚®Ü i, S i : NonUnitalSubring R) : Set R) = ‚ãÉ i, ‚Üë(S i) :=
  Set.ext fun x => by simp [mem_iSup_of_directed hS]
                      -- üéâ no goals

theorem mem_sSup_of_directedOn {S : Set (NonUnitalSubring R)} (Sne : S.Nonempty)
    (hS : DirectedOn (¬∑ ‚â§ ¬∑) S) {x : R} : x ‚àà sSup S ‚Üî ‚àÉ s ‚àà S, x ‚àà s := by
  haveI : Nonempty S := Sne.to_subtype
  -- ‚ä¢ x ‚àà sSup S ‚Üî ‚àÉ s, s ‚àà S ‚àß x ‚àà s
  simp only [sSup_eq_iSup', mem_iSup_of_directed hS.directed_val, SetCoe.exists, Subtype.coe_mk,
    exists_prop]

theorem coe_sSup_of_directedOn {S : Set (NonUnitalSubring R)} (Sne : S.Nonempty)
    (hS : DirectedOn (¬∑ ‚â§ ¬∑) S) : (‚Üë(sSup S) : Set R) = ‚ãÉ s ‚àà S, ‚Üës :=
  Set.ext fun x => by simp [mem_sSup_of_directedOn Sne hS]
                      -- üéâ no goals

theorem mem_map_equiv {f : R ‚âÉ+* S} {K : NonUnitalSubring R} {x : S} :
    x ‚àà K.map (f : R ‚Üí‚Çô+* S) ‚Üî f.symm x ‚àà K :=
  @Set.mem_image_equiv _ _ (K : Set R) f.toEquiv x

theorem map_equiv_eq_comap_symm (f : R ‚âÉ+* S) (K : NonUnitalSubring R) :
    K.map (f : R ‚Üí‚Çô+* S) = K.comap f.symm :=
  SetLike.coe_injective (f.toEquiv.image_eq_preimage K)

theorem comap_equiv_eq_map_symm (f : R ‚âÉ+* S) (K : NonUnitalSubring S) :
    K.comap (f : R ‚Üí‚Çô+* S) = K.map f.symm :=
  (map_equiv_eq_comap_symm f.symm K).symm

end NonUnitalSubring

namespace NonUnitalRingHom

variable {F : Type w} {R : Type u} {S : Type v} {T : Type*}
  [NonUnitalNonAssocRing R] [NonUnitalNonAssocRing S] [NonUnitalNonAssocRing T]
  [NonUnitalRingHomClass F R S]
  (g : S ‚Üí‚Çô+* T) (f : R ‚Üí‚Çô+* S)
  {s : NonUnitalSubring R}

open NonUnitalSubring

/-- Restriction of a ring homomorphism to its range interpreted as a `NonUnitalSubring`.

This is the bundled version of `Set.rangeFactorization`. -/
def rangeRestrict (f : R ‚Üí‚Çô+* S) : R ‚Üí‚Çô+* f.range :=
  NonUnitalRingHom.codRestrict f f.range fun x => ‚ü®x, rfl‚ü©

@[simp]
theorem coe_rangeRestrict (f : R ‚Üí‚Çô+* S) (x : R) : (f.rangeRestrict x : S) = f x :=
  rfl

theorem rangeRestrict_surjective (f : R ‚Üí‚Çô+* S) : Function.Surjective f.rangeRestrict :=
  fun ‚ü®_y, hy‚ü© =>
  let ‚ü®x, hx‚ü© := mem_range.mp hy
  ‚ü®x, Subtype.ext hx‚ü©

theorem range_top_iff_surjective {f : R ‚Üí‚Çô+* S} :
    f.range = (‚ä§ : NonUnitalSubring S) ‚Üî Function.Surjective f :=
  SetLike.ext'_iff.trans <| Iff.trans (by rw [coe_range, coe_top]) Set.range_iff_surjective
                                          -- üéâ no goals

/-- The range of a surjective ring homomorphism is the whole of the codomain. -/
@[simp]
theorem range_top_of_surjective (f : R ‚Üí‚Çô+* S) (hf : Function.Surjective f) :
    f.range = (‚ä§ : NonUnitalSubring S) :=
  range_top_iff_surjective.2 hf

/-- The `NonUnitalSubring` of elements `x : R` such that `f x = g x`, i.e.,
  the equalizer of f and g as a `NonUnitalSubring` of R -/
def eqLocus (f g : R ‚Üí‚Çô+* S) : NonUnitalSubring R :=
  { (f : R ‚Üí‚Çô* S).eqLocus g, (f : R ‚Üí+ S).eqLocus g with carrier := {x | f x = g x} }

@[simp]
theorem eqLocus_same (f : R ‚Üí‚Çô+* S) : f.eqLocus f = ‚ä§ :=
  SetLike.ext fun _ => eq_self_iff_true _

/-- If two ring homomorphisms are equal on a set, then they are equal on its
`NonUnitalSubring` closure. -/
theorem eqOn_set_closure {f g : R ‚Üí‚Çô+* S} {s : Set R} (h : Set.EqOn f g s) :
    Set.EqOn f g (closure s) :=
  show closure s ‚â§ f.eqLocus g from closure_le.2 h

theorem eq_of_eqOn_set_top {f g : R ‚Üí‚Çô+* S} (h : Set.EqOn f g (‚ä§ : NonUnitalSubring R)) : f = g :=
  ext fun _x => h trivial

theorem eq_of_eqOn_set_dense {s : Set R} (hs : closure s = ‚ä§) {f g : R ‚Üí‚Çô+* S} (h : s.EqOn f g) :
    f = g :=
  eq_of_eqOn_set_top <| hs ‚ñ∏ eqOn_set_closure h

theorem closure_preimage_le (f : R ‚Üí‚Çô+* S) (s : Set S) : closure (f ‚Åª¬π' s) ‚â§ (closure s).comap f :=
  closure_le.2 fun _x hx => SetLike.mem_coe.2 <| mem_comap.2 <| subset_closure hx

/-- The image under a ring homomorphism of the `NonUnitalSubring` generated by a set equals
the `NonUnitalSubring` generated by the image of the set. -/
theorem map_closure (f : R ‚Üí‚Çô+* S) (s : Set R) : (closure s).map f = closure (f '' s) :=
  le_antisymm
    (map_le_iff_le_comap.2 <|
      le_trans (closure_mono <| Set.subset_preimage_image _ _) (closure_preimage_le _ _))
    (closure_le.2 <| Set.image_subset _ subset_closure)

end NonUnitalRingHom

namespace NonUnitalSubring

variable {F : Type w} {R : Type u} {S : Type v} {T : Type*}
  [NonUnitalNonAssocRing R] [NonUnitalNonAssocRing S] [NonUnitalNonAssocRing T]
  [NonUnitalRingHomClass F R S]
  (g : S ‚Üí‚Çô+* T) (f : R ‚Üí‚Çô+* S)
  {s : NonUnitalSubring R}

open NonUnitalRingHom

/-- The ring homomorphism associated to an inclusion of `NonUnitalSubring`s. -/
def inclusion {S T : NonUnitalSubring R} (h : S ‚â§ T) : S ‚Üí‚Çô+* T :=
  NonUnitalRingHom.codRestrict (NonUnitalSubringClass.subtype S) _ fun x => h x.2

@[simp]
theorem range_subtype (s : NonUnitalSubring R) : (NonUnitalSubringClass.subtype s).range = s :=
  SetLike.coe_injective <| (coe_srange _).trans Subtype.range_coe

theorem range_fst : NonUnitalRingHom.srange (fst R S) = ‚ä§ :=
  NonUnitalSubsemiring.range_fst

theorem range_snd : NonUnitalRingHom.srange (snd R S) = ‚ä§ :=
  NonUnitalSubsemiring.range_snd

end NonUnitalSubring

namespace RingEquiv

variable {F : Type w} {R : Type u} {S : Type v} {T : Type*}
  [NonUnitalRing R] [NonUnitalRing S] [NonUnitalRing T]
  [NonUnitalRingHomClass F R S]
  (g : S ‚Üí‚Çô+* T) (f : R ‚Üí‚Çô+* S)
  {s t : NonUnitalSubring R}

/-- Makes the identity isomorphism from a proof two `NonUnitalSubring`s of a multiplicative
    monoid are equal. -/
def nonUnitalSubringCongr (h : s = t) : s ‚âÉ+* t :=
  {
    Equiv.setCongr <| congr_arg _ h with
    map_mul' := fun _ _ => rfl
    map_add' := fun _ _ => rfl }

/-- Restrict a ring homomorphism with a left inverse to a ring isomorphism to its
`RingHom.range`. -/
def ofLeftInverse' {g : S ‚Üí R} {f : R ‚Üí‚Çô+* S} (h : Function.LeftInverse g f) : R ‚âÉ+* f.range :=
  { f.rangeRestrict with
    toFun := fun x => f.rangeRestrict x
    invFun := fun x => (g ‚àò NonUnitalSubringClass.subtype f.range) x
    left_inv := h
    right_inv := fun x =>
      Subtype.ext <|
        let ‚ü®x', hx'‚ü© := NonUnitalRingHom.mem_range.mp x.prop
        show f (g x) = x by rw [‚Üê hx', h x'] }
                            -- üéâ no goals

@[simp]
theorem ofLeftInverse'_apply {g : S ‚Üí R} {f : R ‚Üí‚Çô+* S} (h : Function.LeftInverse g f) (x : R) :
    ‚Üë(ofLeftInverse' h x) = f x :=
  rfl

@[simp]
theorem ofLeftInverse'_symm_apply {g : S ‚Üí R} {f : R ‚Üí‚Çô+* S} (h : Function.LeftInverse g f)
    (x : f.range) : (ofLeftInverse' h).symm x = g x :=
  rfl

end RingEquiv

namespace NonUnitalSubring

variable {F : Type w} {R : Type u} {S : Type v}
  [NonUnitalNonAssocRing R] [NonUnitalNonAssocRing S]
  [NonUnitalRingHomClass F R S]

theorem closure_preimage_le (f : F) (s : Set S) :
    closure ((f : R ‚Üí S) ‚Åª¬π' s) ‚â§ (closure s).comap f :=
  closure_le.2 fun _x hx => SetLike.mem_coe.2 <| mem_comap.2 <| subset_closure hx

end NonUnitalSubring

end Hom
