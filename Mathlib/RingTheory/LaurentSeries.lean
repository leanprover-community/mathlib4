/-
Copyright (c) 2021 Aaron Anderson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Aaron Anderson, Mar√≠a In√©s de Frutos-Fern√°ndez, Filippo A. E. Nuccio
-/
import Mathlib.Data.Int.Interval
import Mathlib.FieldTheory.RatFunc.AsPolynomial
import Mathlib.RingTheory.Binomial
import Mathlib.RingTheory.HahnSeries.PowerSeries
import Mathlib.RingTheory.HahnSeries.Summable
import Mathlib.RingTheory.PowerSeries.Inverse
import Mathlib.RingTheory.PowerSeries.Trunc
import Mathlib.RingTheory.Localization.FractionRing
import Mathlib.Topology.UniformSpace.DiscreteUniformity
import Mathlib.Algebra.Group.Int.TypeTags


/-!
# Laurent Series

In this file we define `LaurentSeries R`, the formal Laurent series over `R`, here an *arbitrary*
type with a zero. They are denoted `R‚∏®X‚∏©`.

## Main Definitions

* Defines `LaurentSeries` as an abbreviation for `HahnSeries ‚Ñ§`.
* Defines `hasseDeriv` of a Laurent series with coefficients in a module over a ring.
* Provides a coercion from power series `R‚ü¶X‚üß` into `R‚∏®X‚∏©` given by `HahnSeries.ofPowerSeries`.
* Defines `LaurentSeries.powerSeriesPart`
* Defines the localization map `LaurentSeries.of_powerSeries_localization` which evaluates to
  `HahnSeries.ofPowerSeries`.
* Embedding of rational functions into Laurent series, provided as a coercion, utilizing
  the underlying `RatFunc.coeAlgHom`.
* Study of the `X`-Adic valuation on the ring of Laurent series over a field
* In `LaurentSeries.uniformContinuous_coeff` we show that sending a Laurent series to its `d`th
  coefficient is uniformly continuous, ensuring that it sends a Cauchy filter `‚Ñ±` in `K‚∏®X‚∏©`
  to a Cauchy filter in `K`: since this latter is given the discrete topology, this provides an
  element `LaurentSeries.Cauchy.coeff ‚Ñ± d` in `K` that serves as `d`th coefficient of the Laurent
  series to which the filter `‚Ñ±` converges.

## Main Results

* Basic properties of Hasse derivatives
### About the `X`-Adic valuation:
* The (integral) valuation of a power series is the order of the first non-zero coefficient, see
  `LaurentSeries.intValuation_le_iff_coeff_lt_eq_zero`.
* The valuation of a Laurent series is the order of the first non-zero coefficient, see
  `LaurentSeries.valuation_le_iff_coeff_lt_eq_zero`.
* Every Laurent series of valuation less than `(1 : ‚Ñ§‚Çò‚ÇÄ)` comes from a power series, see
  `LaurentSeries.val_le_one_iff_eq_coe`.
* The uniform space of `LaurentSeries` over a field is complete, formalized in the instance
  `instLaurentSeriesComplete`.
* The field of rational functions is dense in `LaurentSeries`: this is the declaration
  `LaurentSeries.coe_range_dense` and relies principally upon `LaurentSeries.exists_ratFunc_val_lt`,
  stating that for every Laurent series `f` and every `Œ≥ : ‚Ñ§‚Çò‚ÇÄ` one can find a rational function `Q`
  such that the `X`-adic valuation `v` satisfies `v (f - Q) < Œ≥`.
* In `LaurentSeries.valuation_compare` we prove that the extension of the `X`-adic valuation from
  `RatFunc K` up to its abstract completion coincides, modulo the isomorphism with `K‚∏®X‚∏©`, with the
  `X`-adic valuation on `K‚∏®X‚∏©`.
* The two declarations `LaurentSeries.mem_integers_of_powerSeries` and
  `LaurentSeries.exists_powerSeries_of_memIntegers` show that an element in the completion of
  `RatFunc K` is in the unit ball if and only if it comes from a power series through the
  isomorphism `LaurentSeriesRingEquiv`.
* `LaurentSeries.powerSeriesAlgEquiv` is the `K`-algebra isomorphism between `K‚ü¶X‚üß`
  and the unit ball inside the `X`-adic completion of `RatFunc K`.

## Implementation details

* Since `LaurentSeries` is just an abbreviation of `HahnSeries ‚Ñ§ R`, the definition of the
  coefficients is given in terms of `HahnSeries.coeff` and this forces sometimes to go
  back-and-forth from `X : R‚∏®X‚∏©` to `single 1 1 : HahnSeries ‚Ñ§ R`.
* To prove the isomorphism between the `X`-adic completion of `RatFunc K` and `K‚∏®X‚∏©` we construct
  two completions of `RatFunc K`: the first (`LaurentSeries.ratfuncAdicComplPkg`) is its abstract
  uniform completion; the second (`LaurentSeries.LaurentSeriesPkg`) is simply `K‚∏®X‚∏©`, once we prove
  that it is complete and contains `RatFunc K` as a dense subspace. The isomorphism is the
  comparison equivalence, expressing the mathematical idea that the completion "is unique". It is
  `LaurentSeries.comparePkg`.
* For applications to `K‚ü¶X‚üß` it is actually more handy to use the *inverse* of the above
  equivalence: `LaurentSeries.LaurentSeriesAlgEquiv` is the *topological, algebra equivalence*
  `K‚∏®X‚∏© ‚âÉ‚Çê[K] RatFuncAdicCompl K`.
* In order to compare `K‚ü¶X‚üß` with the valuation subring in the `X`-adic completion of
  `RatFunc K` we consider its alias `LaurentSeries.powerSeries_as_subring` as a subring of `K‚∏®X‚∏©`,
  that is itself clearly isomorphic (via the inverse of `LaurentSeries.powerSeriesEquivSubring`)
  to `K‚ü¶X‚üß`.

-/
universe u

open scoped PowerSeries
open HahnSeries Polynomial

noncomputable section

/-- `LaurentSeries R` is the type of formal Laurent series with coefficients in `R`, denoted `R‚∏®X‚∏©`.

  It is implemented as a `HahnSeries` with value group `‚Ñ§`.
-/
abbrev LaurentSeries (R : Type u) [Zero R] :=
  HahnSeries ‚Ñ§ R

variable {R : Type*}

namespace LaurentSeries

section

/-- `R‚∏®X‚∏©` is notation for `LaurentSeries R`. -/
scoped notation:9000 R "‚∏®X‚∏©" => LaurentSeries R

end

section HasseDeriv

/-- The Hasse derivative of Laurent series, as a linear map. -/
def hasseDeriv (R : Type*) {V : Type*} [AddCommGroup V] [Semiring R] [Module R V] (k : ‚Ñï) :
    V‚∏®X‚∏© ‚Üí‚Çó[R] V‚∏®X‚∏© where
  toFun f := HahnSeries.ofSuppBddBelow (fun (n : ‚Ñ§) => (Ring.choose (n + k) k) ‚Ä¢ f.coeff (n + k))
    (forallLTEqZero_supp_BddBelow _ (f.order - k : ‚Ñ§)
    (fun _ h_lt ‚Ü¶ by rw [coeff_eq_zero_of_lt_order <| lt_sub_iff_add_lt.mp h_lt, smul_zero]))
  map_add' f g := by
    ext
    simp only [ofSuppBddBelow, coeff_add', Pi.add_apply, smul_add]
  map_smul' r f := by
    ext
    simp only [ofSuppBddBelow, HahnSeries.coeff_smul, RingHom.id_apply, smul_comm r]

variable [Semiring R] {V : Type*} [AddCommGroup V] [Module R V]

@[simp]
theorem hasseDeriv_coeff (k : ‚Ñï) (f : LaurentSeries V) (n : ‚Ñ§) :
    (hasseDeriv R k f).coeff n = Ring.choose (n + k) k ‚Ä¢ f.coeff (n + k) :=
  rfl

@[simp]
theorem hasseDeriv_zero : hasseDeriv R 0 = LinearMap.id (M := LaurentSeries V) := by
  ext f n
  simp

theorem hasseDeriv_single_add (k : ‚Ñï) (n : ‚Ñ§) (x : V) :
    hasseDeriv R k (single (n + k) x) = single n ((Ring.choose (n + k) k) ‚Ä¢ x) := by
  ext m
  dsimp only [hasseDeriv_coeff]
  by_cases h : m = n
  ¬∑ simp [h]
  ¬∑ simp [h, show m + k ‚â† n + k by omega]

@[simp]
theorem hasseDeriv_single (k : ‚Ñï) (n : ‚Ñ§) (x : V) :
    hasseDeriv R k (single n x) = single (n - k) ((Ring.choose n k) ‚Ä¢ x) := by
  rw [‚Üê Int.sub_add_cancel n k, hasseDeriv_single_add, Int.sub_add_cancel n k]

theorem hasseDeriv_comp_coeff (k l : ‚Ñï) (f : LaurentSeries V) (n : ‚Ñ§) :
    (hasseDeriv R k (hasseDeriv R l f)).coeff n =
      ((Nat.choose (k + l) k) ‚Ä¢ hasseDeriv R (k + l) f).coeff n := by
  rw [coeff_nsmul]
  simp only [hasseDeriv_coeff, Pi.smul_apply, Nat.cast_add]
  rw [smul_smul, mul_comm, ‚Üê Ring.choose_add_smul_choose (n + k), add_assoc, Nat.choose_symm_add,
    smul_assoc]

@[simp]
theorem hasseDeriv_comp (k l : ‚Ñï) (f : LaurentSeries V) :
    hasseDeriv R k (hasseDeriv R l f) = (k + l).choose k ‚Ä¢ hasseDeriv R (k + l) f := by
  ext n
  simp [hasseDeriv_comp_coeff k l f n]

/-- The derivative of a Laurent series. -/
def derivative (R : Type*) {V : Type*} [AddCommGroup V] [Semiring R] [Module R V] :
    LaurentSeries V ‚Üí‚Çó[R] LaurentSeries V :=
  hasseDeriv R 1

@[simp]
theorem derivative_apply (f : LaurentSeries V) : derivative R f = hasseDeriv R 1 f := by
  exact rfl

theorem derivative_iterate (k : ‚Ñï) (f : LaurentSeries V) :
    (derivative R)^[k] f = k.factorial ‚Ä¢ (hasseDeriv R k f) := by
  ext n
  induction k generalizing f with
  | zero => simp
  | succ k ih =>
    rw [Function.iterate_succ, Function.comp_apply, ih, derivative_apply, hasseDeriv_comp,
      Nat.choose_symm_add, Nat.choose_one_right, Nat.factorial, mul_nsmul]

@[simp]
theorem derivative_iterate_coeff (k : ‚Ñï) (f : LaurentSeries V) (n : ‚Ñ§) :
    ((derivative R)^[k] f).coeff n = (descPochhammer ‚Ñ§ k).smeval (n + k) ‚Ä¢ f.coeff (n + k) := by
  rw [derivative_iterate, coeff_nsmul, Pi.smul_apply, hasseDeriv_coeff,
    Ring.descPochhammer_eq_factorial_smul_choose, smul_assoc]

end HasseDeriv

section Semiring

variable [Semiring R]

instance : Coe R‚ü¶X‚üß R‚∏®X‚∏© :=
  ‚ü®HahnSeries.ofPowerSeries ‚Ñ§ R‚ü©

@[simp]
theorem coeff_coe_powerSeries (x : R‚ü¶X‚üß) (n : ‚Ñï) :
    HahnSeries.coeff (x : R‚∏®X‚∏©) n = PowerSeries.coeff R n x := by
  rw [ofPowerSeries_apply_coeff]

/-- This is a power series that can be multiplied by an integer power of `X` to give our
  Laurent series. If the Laurent series is nonzero, `powerSeriesPart` has a nonzero
  constant term. -/
def powerSeriesPart (x : R‚∏®X‚∏©) : R‚ü¶X‚üß :=
  PowerSeries.mk fun n => x.coeff (x.order + n)

@[simp]
theorem powerSeriesPart_coeff (x : R‚∏®X‚∏©) (n : ‚Ñï) :
    PowerSeries.coeff R n x.powerSeriesPart = x.coeff (x.order + n) :=
  PowerSeries.coeff_mk _ _

@[simp]
theorem powerSeriesPart_zero : powerSeriesPart (0 : R‚∏®X‚∏©) = 0 := by
  ext
  simp [(PowerSeries.coeff _ _).map_zero] -- Note: this doesn't get picked up any more

@[simp]
theorem powerSeriesPart_eq_zero (x : R‚∏®X‚∏©) : x.powerSeriesPart = 0 ‚Üî x = 0 := by
  constructor
  ¬∑ contrapose!
    simp only [ne_eq]
    intro h
    rw [PowerSeries.ext_iff, not_forall]
    refine ‚ü®0, ?_‚ü©
    simp [coeff_order_ne_zero h]
  ¬∑ rintro rfl
    simp

@[simp]
theorem single_order_mul_powerSeriesPart (x : R‚∏®X‚∏©) :
    (single x.order 1 : R‚∏®X‚∏©) * x.powerSeriesPart = x := by
  ext n
  rw [‚Üê sub_add_cancel n x.order, coeff_single_mul_add, sub_add_cancel, one_mul]
  by_cases h : x.order ‚â§ n
  ¬∑ rw [Int.eq_natAbs_of_nonneg (sub_nonneg_of_le h), coeff_coe_powerSeries,
      powerSeriesPart_coeff, ‚Üê Int.eq_natAbs_of_nonneg (sub_nonneg_of_le h),
      add_sub_cancel]
  ¬∑ rw [ofPowerSeries_apply, embDomain_notin_range]
    ¬∑ contrapose! h
      exact order_le_of_coeff_ne_zero h.symm
    ¬∑ contrapose! h
      simp only [Set.mem_range, RelEmbedding.coe_mk, Function.Embedding.coeFn_mk] at h
      obtain ‚ü®m, hm‚ü© := h
      rw [‚Üê sub_nonneg, ‚Üê hm]
      simp only [Nat.cast_nonneg]

theorem ofPowerSeries_powerSeriesPart (x : R‚∏®X‚∏©) :
    ofPowerSeries ‚Ñ§ R x.powerSeriesPart = single (-x.order) 1 * x := by
  refine Eq.trans ?_ (congr rfl x.single_order_mul_powerSeriesPart)
  rw [‚Üê mul_assoc, single_mul_single, neg_add_cancel, mul_one, ‚Üê C_apply, C_one, one_mul]

theorem X_order_mul_powerSeriesPart {n : ‚Ñï} {f : R‚∏®X‚∏©} (hn : n = f.order) :
    (PowerSeries.X ^ n * f.powerSeriesPart : R‚ü¶X‚üß) = f := by
  simp only [map_mul, map_pow, ofPowerSeries_X, single_pow, nsmul_eq_mul, mul_one, one_pow, hn,
    single_order_mul_powerSeriesPart]

end Semiring

instance [CommSemiring R] : Algebra R‚ü¶X‚üß R‚∏®X‚∏© := (HahnSeries.ofPowerSeries ‚Ñ§ R).toAlgebra

@[simp]
theorem coe_algebraMap [CommSemiring R] :
    ‚áë(algebraMap R‚ü¶X‚üß R‚∏®X‚∏©) = HahnSeries.ofPowerSeries ‚Ñ§ R :=
  rfl

/-- The localization map from power series to Laurent series. -/
@[simps (config := { rhsMd := .all, simpRhs := true })]
instance of_powerSeries_localization [CommRing R] :
    IsLocalization (Submonoid.powers (PowerSeries.X : R‚ü¶X‚üß)) R‚∏®X‚∏© where
  map_units' := by
    rintro ‚ü®_, n, rfl‚ü©
    refine ‚ü®‚ü®single (n : ‚Ñ§) 1, single (-n : ‚Ñ§) 1, ?_, ?_‚ü©, ?_‚ü©
    ¬∑ simp
    ¬∑ simp
    ¬∑ dsimp; rw [ofPowerSeries_X_pow]
  surj' z := by
    by_cases h : 0 ‚â§ z.order
    ¬∑ refine ‚ü®‚ü®PowerSeries.X ^ Int.natAbs z.order * powerSeriesPart z, 1‚ü©, ?_‚ü©
      simp only [RingHom.map_one, mul_one, RingHom.map_mul, coe_algebraMap, ofPowerSeries_X_pow,
        Submonoid.coe_one]
      rw [Int.natAbs_of_nonneg h, single_order_mul_powerSeriesPart]
    ¬∑ refine ‚ü®‚ü®powerSeriesPart z, PowerSeries.X ^ Int.natAbs z.order, ‚ü®_, rfl‚ü©‚ü©, ?_‚ü©
      simp only [coe_algebraMap, ofPowerSeries_powerSeriesPart]
      rw [mul_comm _ z]
      refine congr rfl ?_
      rw [ofPowerSeries_X_pow, Int.ofNat_natAbs_of_nonpos]
      exact le_of_not_ge h
  exists_of_eq {x y} := by
    rw [coe_algebraMap, ofPowerSeries_injective.eq_iff]
    rintro rfl
    exact ‚ü®1, rfl‚ü©

instance {K : Type*} [Field K] : IsFractionRing K‚ü¶X‚üß K‚∏®X‚∏© :=
  IsLocalization.of_le (Submonoid.powers (PowerSeries.X : K‚ü¶X‚üß)) _
    (powers_le_nonZeroDivisors_of_noZeroDivisors PowerSeries.X_ne_zero) fun _ hf =>
    isUnit_of_mem_nonZeroDivisors <| map_mem_nonZeroDivisors _ HahnSeries.ofPowerSeries_injective hf

end LaurentSeries

namespace PowerSeries

open LaurentSeries

variable {R' : Type*} [Semiring R] [Ring R'] (f g : R‚ü¶X‚üß) (f' g' : R'‚ü¶X‚üß)

@[norm_cast]
theorem coe_zero : ((0 : R‚ü¶X‚üß) : R‚∏®X‚∏©) = 0 :=
  (ofPowerSeries ‚Ñ§ R).map_zero

@[norm_cast]
theorem coe_one : ((1 : R‚ü¶X‚üß) : R‚∏®X‚∏©) = 1 :=
  (ofPowerSeries ‚Ñ§ R).map_one

@[norm_cast]
theorem coe_add : ((f + g : R‚ü¶X‚üß) : R‚∏®X‚∏©) = f + g :=
  (ofPowerSeries ‚Ñ§ R).map_add _ _

@[norm_cast]
theorem coe_sub : ((f' - g' : R'‚ü¶X‚üß) : R'‚∏®X‚∏©) = f' - g' :=
  (ofPowerSeries ‚Ñ§ R').map_sub _ _

@[norm_cast]
theorem coe_neg : ((-f' : R'‚ü¶X‚üß) : R'‚∏®X‚∏©) = -f' :=
  (ofPowerSeries ‚Ñ§ R').map_neg _

@[norm_cast]
theorem coe_mul : ((f * g : R‚ü¶X‚üß) : R‚∏®X‚∏©) = f * g :=
  (ofPowerSeries ‚Ñ§ R).map_mul _ _

theorem coeff_coe (i : ‚Ñ§) :
    ((f : R‚ü¶X‚üß) : R‚∏®X‚∏©).coeff i =
      if i < 0 then 0 else PowerSeries.coeff R i.natAbs f := by
  cases i
  ¬∑ rw [Int.ofNat_eq_coe, coeff_coe_powerSeries, if_neg (Int.natCast_nonneg _).not_gt,
      Int.natAbs_natCast]
  ¬∑ rw [ofPowerSeries_apply, embDomain_notin_image_support, if_pos (Int.negSucc_lt_zero _)]
    simp only [not_exists, RelEmbedding.coe_mk, Set.mem_image, not_and, Function.Embedding.coeFn_mk,
      Ne, toPowerSeries_symm_apply_coeff, mem_support, imp_true_iff,
      not_false_iff, reduceCtorEq]

theorem coe_C (r : R) : ((C R r : R‚ü¶X‚üß) : R‚∏®X‚∏©) = HahnSeries.C r :=
  ofPowerSeries_C _

theorem coe_X : ((X : R‚ü¶X‚üß) : R‚∏®X‚∏©) = single 1 1 :=
  ofPowerSeries_X

@[simp, norm_cast]
theorem coe_smul {S : Type*} [Semiring S] [Module R S] (r : R) (x : S‚ü¶X‚üß) :
    ((r ‚Ä¢ x : S‚ü¶X‚üß) : S‚∏®X‚∏©) = r ‚Ä¢ (ofPowerSeries ‚Ñ§ S x) := by
  ext
  simp [coeff_coe, coeff_smul, smul_ite]

@[norm_cast]
theorem coe_pow (n : ‚Ñï) : ((f ^ n : R‚ü¶X‚üß) : R‚∏®X‚∏©) = (ofPowerSeries ‚Ñ§ R f) ^ n :=
  (ofPowerSeries ‚Ñ§ R).map_pow _ _

end PowerSeries

namespace RatFunc

open scoped LaurentSeries

variable {F : Type u} [Field F] (p q : F[X]) (f g : RatFunc F)

/-- The coercion `RatFunc F ‚Üí F‚∏®X‚∏©` as bundled alg hom. -/
def coeAlgHom (F : Type u) [Field F] : RatFunc F ‚Üí‚Çê[F[X]] F‚∏®X‚∏© :=
  liftAlgHom (Algebra.ofId _ _) <|
    nonZeroDivisors_le_comap_nonZeroDivisors_of_injective _ <|
      Polynomial.algebraMap_hahnSeries_injective _

/-- The coercion `RatFunc F ‚Üí F‚∏®X‚∏©` as a function.

This is the implementation of `coeToLaurentSeries`.
-/
@[coe]
def coeToLaurentSeries_fun {F : Type u} [Field F] : RatFunc F ‚Üí F‚∏®X‚∏© :=
  coeAlgHom F

instance coeToLaurentSeries : Coe (RatFunc F) F‚∏®X‚∏© :=
  ‚ü®coeToLaurentSeries_fun‚ü©

theorem coe_def : (f : F‚∏®X‚∏©) = coeAlgHom F f :=
  rfl

attribute [-instance] RatFunc.instCoePolynomial in
-- avoids a diamond, see https://leanprover.zulipchat.com/#narrow/stream/287929-mathlib4/topic/compiling.20behaviour.20within.20one.20file
theorem coe_num_denom : (f : F‚∏®X‚∏©) = f.num / f.denom :=
  liftAlgHom_apply _ _ f

theorem coe_injective : Function.Injective ((‚Üë) : RatFunc F ‚Üí F‚∏®X‚∏©) :=
  liftAlgHom_injective _ (Polynomial.algebraMap_hahnSeries_injective _)

-- Porting note: removed the `norm_cast` tag:
-- `norm_cast: badly shaped lemma, rhs can't start with coe `‚Üë(coeAlgHom F) f`
@[simp]
theorem coe_apply : coeAlgHom F f = f :=
  rfl

-- avoids a diamond, see https://leanprover.zulipchat.com/#narrow/stream/287929-mathlib4/topic/compiling.20behaviour.20within.20one.20file
theorem coe_coe (P : Polynomial F) : ((P : F‚ü¶X‚üß) : F‚∏®X‚∏©) = (P : RatFunc F) := by
  simp only [coePolynomial, coe_def, AlgHom.commutes, algebraMap_hahnSeries_apply]

@[simp, norm_cast]
theorem coe_zero : ((0 : RatFunc F) : F‚∏®X‚∏©) = 0 :=
  map_zero (coeAlgHom F)

theorem coe_ne_zero {f : Polynomial F} (hf : f ‚â† 0) : (‚Üëf : F‚ü¶X‚üß) ‚â† 0 := by
  simp only [ne_eq, Polynomial.coe_eq_zero_iff, hf, not_false_eq_true]

@[simp, norm_cast]
theorem coe_one : ((1 : RatFunc F) : F‚∏®X‚∏©) = 1 :=
  map_one (coeAlgHom F)

@[simp, norm_cast]
theorem coe_add : ((f + g : RatFunc F) : F‚∏®X‚∏©) = f + g :=
  map_add (coeAlgHom F) _ _

@[simp, norm_cast]
theorem coe_sub : ((f - g : RatFunc F) : F‚∏®X‚∏©) = f - g :=
  map_sub (coeAlgHom F) _ _

@[simp, norm_cast]
theorem coe_neg : ((-f : RatFunc F) : F‚∏®X‚∏©) = -f :=
  map_neg (coeAlgHom F) _

@[simp, norm_cast]
theorem coe_mul : ((f * g : RatFunc F) : F‚∏®X‚∏©) = f * g :=
  map_mul (coeAlgHom F) _ _

@[simp, norm_cast]
theorem coe_pow (n : ‚Ñï) : ((f ^ n : RatFunc F) : F‚∏®X‚∏©) = (f : F‚∏®X‚∏©) ^ n :=
  map_pow (coeAlgHom F) _ _

@[simp, norm_cast]
theorem coe_div : ((f / g : RatFunc F) : F‚∏®X‚∏©) = (f : F‚∏®X‚∏©) / (g : F‚∏®X‚∏©) :=
  map_div‚ÇÄ (coeAlgHom F) _ _

@[simp, norm_cast]
theorem coe_C (r : F) : ((RatFunc.C r : RatFunc F) : F‚∏®X‚∏©) = HahnSeries.C r := by
  rw [coe_num_denom, num_C, denom_C, Polynomial.coe_C, -- Porting note: removed `coe_C`
    Polynomial.coe_one,
    PowerSeries.coe_one, div_one]
  simp only [algebraMap_eq_C, ofPowerSeries_C, C_apply]  -- Porting note: added

-- TODO: generalize over other modules
@[simp, norm_cast]
theorem coe_smul (r : F) : ((r ‚Ä¢ f : RatFunc F) : F‚∏®X‚∏©) = r ‚Ä¢ (f : F‚∏®X‚∏©) := by
  rw [RatFunc.smul_eq_C_mul, ‚Üê C_mul_eq_smul, coe_mul, coe_C]

-- Porting note: removed `norm_cast` because "badly shaped lemma, rhs can't start with coe"
-- even though `single 1 1` is a bundled function application, not a "real" coercion
@[simp]
theorem coe_X : ((X : RatFunc F) : F‚∏®X‚∏©) = single 1 1 := by
  rw [coe_num_denom, num_X, denom_X, Polynomial.coe_X, Polynomial.coe_one, PowerSeries.coe_one,
    div_one, ofPowerSeries_X]

theorem single_one_eq_pow {R : Type*} [Semiring R] (n : ‚Ñï) :
    single (n : ‚Ñ§) (1 : R) = single (1 : ‚Ñ§) 1 ^ n := by
  induction' n with n h_ind
  ¬∑ simp
  ¬∑ rw [‚Üê Int.ofNat_add_one_out, pow_succ', ‚Üê h_ind, HahnSeries.single_mul_single, one_mul,
      add_comm]

theorem single_inv (d : ‚Ñ§) {Œ± : F} (hŒ± : Œ± ‚â† 0) :
    single (-d) (Œ±‚Åª¬π : F) = (single (d : ‚Ñ§) (Œ± : F))‚Åª¬π := by
  apply eq_inv_of_mul_eq_one_right
  simp [hŒ±]

theorem single_zpow (n : ‚Ñ§) :
    single (n : ‚Ñ§) (1 : F) = single (1 : ‚Ñ§) 1 ^ n := by
  match n with
  | (n : ‚Ñï) => apply single_one_eq_pow
  | -(n + 1 : ‚Ñï) =>
    rw [‚Üê Nat.cast_one, ‚Üê inv_one, single_inv _ one_ne_zero, zpow_neg, ‚Üê Nat.cast_one, Nat.cast_one,
      inv_inj, zpow_natCast, single_one_eq_pow, inv_one]

instance : Algebra (RatFunc F) F‚∏®X‚∏© := RingHom.toAlgebra (coeAlgHom F).toRingHom

theorem algebraMap_apply_div :
    algebraMap (RatFunc F) F‚∏®X‚∏© (algebraMap _ _ p / algebraMap _ _ q) =
      algebraMap F[X] F‚∏®X‚∏© p / algebraMap _ _ q := by
  convert coe_div _ _ <;>
    rw [‚Üê mk_one, coe_def, coeAlgHom, mk_eq_div, liftAlgHom_apply_div, map_one, div_one,
      Algebra.ofId_apply]

instance : IsScalarTower F[X] (RatFunc F) F‚∏®X‚∏© :=
  ‚ü®fun x y z => by
    ext
    simp‚ü©

end RatFunc
section AdicValuation

open scoped Multiplicative

variable (K : Type*) [Field K]
namespace PowerSeries

/-- The prime ideal `(X)` of `K‚ü¶X‚üß`, when `K` is a field, as a term of the `HeightOneSpectrum`. -/
def idealX : IsDedekindDomain.HeightOneSpectrum K‚ü¶X‚üß where
  asIdeal := Ideal.span {X}
  isPrime := PowerSeries.span_X_isPrime
  ne_bot  := by rw [ne_eq, Ideal.span_singleton_eq_bot]; exact X_ne_zero

open RatFunc IsDedekindDomain.HeightOneSpectrum

variable {K}

/- The `X`-adic valuation of a polynomial equals the `X`-adic valuation of
its coercion to `K‚ü¶X‚üß`. -/
theorem intValuation_eq_of_coe (P : K[X]) :
    (Polynomial.idealX K).intValuation P = (idealX K).intValuation (P : K‚ü¶X‚üß) := by
  by_cases hP : P = 0
  ¬∑ rw [hP, Valuation.map_zero, Polynomial.coe_zero, Valuation.map_zero]
  rw [intValuation_if_neg _ hP, intValuation_if_neg _ <| coe_ne_zero hP]
  simp only [idealX_span, ofAdd_neg, inv_inj, WithZero.coe_inj, EmbeddingLike.apply_eq_iff_eq,
    Nat.cast_inj]
  have span_ne_zero :
    (Ideal.span {P} : Ideal K[X]) ‚â† 0 ‚àß (Ideal.span {Polynomial.X} : Ideal K[X]) ‚â† 0 := by
    simp only [Ideal.zero_eq_bot, ne_eq, Ideal.span_singleton_eq_bot, hP, Polynomial.X_ne_zero,
      not_false_iff, and_self_iff]
  have span_ne_zero' :
    (Ideal.span {‚ÜëP} : Ideal K‚ü¶X‚üß) ‚â† 0 ‚àß ((idealX K).asIdeal : Ideal K‚ü¶X‚üß) ‚â† 0 := by
    simp only [Ideal.zero_eq_bot, ne_eq, Ideal.span_singleton_eq_bot, coe_eq_zero_iff, hP,
      not_false_eq_true, true_and, (idealX K).3]
  classical
  rw [count_associates_factors_eq  (span_ne_zero).1
    (Ideal.span_singleton_prime Polynomial.X_ne_zero|>.mpr prime_X) (span_ne_zero).2,
    count_associates_factors_eq]
  on_goal 1 => convert (normalized_count_X_eq_of_coe hP).symm
  exacts [count_span_normalizedFactors_eq_of_normUnit hP Polynomial.normUnit_X prime_X,
    count_span_normalizedFactors_eq_of_normUnit (coe_ne_zero hP) normUnit_X X_prime,
    span_ne_zero'.1, (idealX K).isPrime, span_ne_zero'.2]

/-- The integral valuation of the power series `X : K‚ü¶X‚üß` equals `(ofAdd -1) : ‚Ñ§‚Çò‚ÇÄ`. -/
@[simp]
theorem intValuation_X : (idealX K).intValuation X = ‚Üë(Multiplicative.ofAdd (-1 : ‚Ñ§)) := by
  rw [‚Üê Polynomial.coe_X, ‚Üê intValuation_eq_of_coe]
  apply intValuation_singleton _ Polynomial.X_ne_zero (by rfl)

end PowerSeries

namespace RatFunc

open IsDedekindDomain.HeightOneSpectrum PowerSeries
open scoped LaurentSeries

theorem valuation_eq_LaurentSeries_valuation (P : RatFunc K) :
    (Polynomial.idealX K).valuation _ P = (PowerSeries.idealX K).valuation K‚∏®X‚∏© P := by
  refine RatFunc.induction_on' P ?_
  intro f g h
  rw [Polynomial.valuation_of_mk K f h, RatFunc.mk_eq_mk' f h, Eq.comm]
  convert @valuation_of_mk' K‚ü¶X‚üß _ _ K‚∏®X‚∏© _ _ _ (PowerSeries.idealX K) f
        ‚ü®g, mem_nonZeroDivisors_iff_ne_zero.2 <| coe_ne_zero h‚ü©
  ¬∑ simp only [IsFractionRing.mk'_eq_div, coe_div, LaurentSeries.coe_algebraMap, coe_coe]
    rfl
  exacts [intValuation_eq_of_coe _, intValuation_eq_of_coe _]

end RatFunc

namespace LaurentSeries


open IsDedekindDomain.HeightOneSpectrum PowerSeries RatFunc

instance : Valued K‚∏®X‚∏© ‚Ñ§‚Çò‚ÇÄ := Valued.mk' ((PowerSeries.idealX K).valuation _)

lemma valuation_def : (Valued.v : Valuation K‚∏®X‚∏© ‚Ñ§‚Çò‚ÇÄ) = (PowerSeries.idealX K).valuation _ := rfl

theorem valuation_X_pow (s : ‚Ñï) :
    Valued.v (((X : K‚ü¶X‚üß) : K‚∏®X‚∏©) ^ s) = Multiplicative.ofAdd (-(s : ‚Ñ§)) := by
  rw [map_pow, ‚Üê one_mul (s : ‚Ñ§), ‚Üê neg_mul (1 : ‚Ñ§) s, Int.ofAdd_mul, WithZero.coe_zpow,
    ofAdd_neg, WithZero.coe_inv, zpow_natCast, valuation_def, ‚Üê LaurentSeries.coe_algebraMap,
    valuation_of_algebraMap, intValuation_X, ofAdd_neg, WithZero.coe_inv]

theorem valuation_single_zpow (s : ‚Ñ§) :
    Valued.v (HahnSeries.single s (1 : K) : K‚∏®X‚∏©) =
      Multiplicative.ofAdd (-(s : ‚Ñ§)) := by
  have : Valued.v (1 : K‚∏®X‚∏©) = (1 : ‚Ñ§‚Çò‚ÇÄ) := Valued.v.map_one
  rw [‚Üê single_zero_one, ‚Üê add_neg_cancel s, ‚Üê mul_one 1, ‚Üê single_mul_single, map_mul,
    mul_eq_one_iff_eq_inv‚ÇÄ] at this
  ¬∑ rw [this]
    obtain s | s := s
    ¬∑ rw [Int.ofNat_eq_coe, ‚Üê HahnSeries.ofPowerSeries_X_pow] at this
      rw [Int.ofNat_eq_coe, ‚Üê this, PowerSeries.coe_pow, valuation_X_pow]
    ¬∑ simp only [Int.negSucc_eq, ‚Üê Int.natCast_succ, neg_neg, ‚Üê HahnSeries.ofPowerSeries_X_pow,
        PowerSeries.coe_pow, valuation_X_pow, ofAdd_neg, WithZero.coe_inv, inv_inv]
  ¬∑ simp only [Valuation.ne_zero_iff, ne_eq, one_ne_zero, not_false_iff, HahnSeries.single_ne_zero]

/- The coefficients of a power series vanish in degree strictly less than its valuation. -/
theorem coeff_zero_of_lt_intValuation {n d : ‚Ñï} {f : K‚ü¶X‚üß}
    (H : Valued.v (f : K‚∏®X‚∏©) ‚â§ Multiplicative.ofAdd (-d : ‚Ñ§)) :
    n < d ‚Üí coeff K n f = 0 := by
  intro hnd
  apply (PowerSeries.X_pow_dvd_iff).mp _ n hnd
  rwa [‚Üê LaurentSeries.coe_algebraMap, valuation_def, valuation_of_algebraMap,
    intValuation_le_pow_iff_dvd (PowerSeries.idealX K) f d, PowerSeries.idealX,
    Ideal.span_singleton_pow, span_singleton_dvd_span_singleton_iff_dvd] at H

/- The valuation of a power series is the order of the first non-zero coefficient. -/
theorem intValuation_le_iff_coeff_lt_eq_zero {d : ‚Ñï} (f : K‚ü¶X‚üß) :
    Valued.v (f : K‚∏®X‚∏©) ‚â§ Multiplicative.ofAdd (-d : ‚Ñ§) ‚Üî
      ‚àÄ n : ‚Ñï, n < d ‚Üí coeff K n f = 0 := by
  have : PowerSeries.X ^ d ‚à£ f ‚Üî ‚àÄ n : ‚Ñï, n < d ‚Üí (PowerSeries.coeff K n) f = 0 :=
    ‚ü®PowerSeries.X_pow_dvd_iff.mp, PowerSeries.X_pow_dvd_iff.mpr‚ü©
  rw [‚Üê this, ‚Üê LaurentSeries.coe_algebraMap, valuation_def, valuation_of_algebraMap,
    ‚Üê span_singleton_dvd_span_singleton_iff_dvd, ‚Üê Ideal.span_singleton_pow]
  apply intValuation_le_pow_iff_dvd

/- The coefficients of a Laurent series vanish in degree strictly less than its valuation. -/
theorem coeff_zero_of_lt_valuation {n D : ‚Ñ§} {f : K‚∏®X‚∏©}
    (H : Valued.v f ‚â§ Multiplicative.ofAdd (-D)) : n < D ‚Üí f.coeff n = 0 := by
  intro hnd
  by_cases h_n_ord : n < f.order
  ¬∑ exact coeff_eq_zero_of_lt_order h_n_ord
  rw [not_lt] at h_n_ord
  set F := powerSeriesPart f with hF
  by_cases ord_nonpos : f.order ‚â§ 0
  ¬∑ obtain ‚ü®s, hs‚ü© := Int.exists_eq_neg_ofNat ord_nonpos
    obtain ‚ü®m, hm‚ü© := Int.eq_ofNat_of_zero_le (neg_le_iff_add_nonneg.mp (hs ‚ñ∏ h_n_ord))
    obtain ‚ü®d, hd‚ü© := Int.eq_ofNat_of_zero_le (a := D + s) (by omega)
    rw [eq_add_neg_of_add_eq hm, add_comm, ‚Üê hs, ‚Üê powerSeriesPart_coeff]
    apply (intValuation_le_iff_coeff_lt_eq_zero K F).mp _ m (by linarith)
    rwa [hF, ofPowerSeries_powerSeriesPart f, hs, neg_neg, ‚Üê hd, neg_add_rev, ofAdd_add, map_mul,
      ‚Üê ofPowerSeries_X_pow s, PowerSeries.coe_pow, WithZero.coe_mul, valuation_X_pow K s,
      mul_le_mul_left (by simp only [ne_eq, WithZero.coe_ne_zero, not_false_iff, zero_lt_iff])]
  ¬∑ rw [not_le] at ord_nonpos
    obtain ‚ü®s, hs‚ü© := Int.exists_eq_neg_ofNat (Int.neg_nonpos_of_nonneg (le_of_lt ord_nonpos))
    obtain ‚ü®m, hm‚ü© := Int.eq_ofNat_of_zero_le (a := n - s) (by omega)
    obtain ‚ü®d, hd‚ü© := Int.eq_ofNat_of_zero_le (a := D - s) (by omega)
    rw [(sub_eq_iff_eq_add).mp hm, add_comm, ‚Üê neg_neg (s : ‚Ñ§), ‚Üê hs, neg_neg,
      ‚Üê powerSeriesPart_coeff]
    apply (intValuation_le_iff_coeff_lt_eq_zero K F).mp _ m (by linarith)
    rwa [hF, ofPowerSeries_powerSeriesPart f, map_mul, ‚Üê hd, hs, neg_sub, sub_eq_add_neg,
      ofAdd_add, valuation_single_zpow, neg_neg, WithZero.coe_mul,
      mul_le_mul_left (by simp only [ne_eq, WithZero.coe_ne_zero, not_false_iff, zero_lt_iff])]

/- The valuation of a Laurent series is the order of the first non-zero coefficient. -/
theorem valuation_le_iff_coeff_lt_eq_zero {D : ‚Ñ§} {f : K‚∏®X‚∏©} :
    Valued.v f ‚â§ ‚Üë(Multiplicative.ofAdd (-D : ‚Ñ§)) ‚Üî ‚àÄ n : ‚Ñ§, n < D ‚Üí f.coeff n = 0 := by
  refine ‚ü®fun hnD n hn => coeff_zero_of_lt_valuation K hnD hn, fun h_val_f => ?_‚ü©
  let F := powerSeriesPart f
  by_cases ord_nonpos : f.order ‚â§ 0
  ¬∑ obtain ‚ü®s, hs‚ü© := Int.exists_eq_neg_ofNat ord_nonpos
    rw [‚Üê f.single_order_mul_powerSeriesPart, hs, map_mul, valuation_single_zpow, neg_neg, mul_comm,
      ‚Üê le_mul_inv_iff‚ÇÄ, ofAdd_neg, WithZero.coe_inv, ‚Üê mul_inv, ‚Üê WithZero.coe_mul, ‚Üê ofAdd_add,
      ‚Üê WithZero.coe_inv, ‚Üê ofAdd_neg]
    ¬∑ by_cases hDs : D + s ‚â§ 0
      ¬∑ apply le_trans ((PowerSeries.idealX K).valuation_le_one F)
        rwa [‚Üê WithZero.coe_one, ‚Üê ofAdd_zero, WithZero.coe_le_coe, Multiplicative.ofAdd_le,
          Left.nonneg_neg_iff]
      ¬∑ obtain ‚ü®d, hd‚ü© := Int.eq_ofNat_of_zero_le (le_of_lt <| not_le.mp hDs)
        rw [hd]
        apply (intValuation_le_iff_coeff_lt_eq_zero K F).mpr
        intro n hn
        rw [powerSeriesPart_coeff f n, hs]
        apply h_val_f
        omega
    ¬∑ simp only [ne_eq, WithZero.coe_ne_zero, not_false_iff, zero_lt_iff]
  ¬∑ obtain ‚ü®s, hs‚ü© := Int.exists_eq_neg_ofNat
      <| neg_nonpos_of_nonneg <| le_of_lt <| not_le.mp ord_nonpos
    rw [neg_inj] at hs
    rw [‚Üê f.single_order_mul_powerSeriesPart, hs, map_mul, valuation_single_zpow, mul_comm,
      ‚Üê le_mul_inv_iff‚ÇÄ, ofAdd_neg, WithZero.coe_inv, ‚Üê mul_inv, ‚Üê WithZero.coe_mul, ‚Üê ofAdd_add,
      ‚Üê WithZero.coe_inv, ‚Üê ofAdd_neg, neg_add, neg_neg]
    ¬∑ by_cases hDs : D - s ‚â§ 0
      ¬∑ apply le_trans ((PowerSeries.idealX K).valuation_le_one F)
        rw [‚Üê WithZero.coe_one, ‚Üê ofAdd_zero, WithZero.coe_le_coe, Multiplicative.ofAdd_le]
        omega
      ¬∑ obtain ‚ü®d, hd‚ü© := Int.eq_ofNat_of_zero_le (le_of_lt <| not_le.mp hDs)
        rw [‚Üê neg_neg (-D + ‚Üës), ‚Üê sub_eq_neg_add, neg_sub, hd]
        apply (intValuation_le_iff_coeff_lt_eq_zero K F).mpr
        intro n hn
        rw [powerSeriesPart_coeff f n, hs]
        apply h_val_f (s + n)
        omega
    ¬∑ simp only [ne_eq, WithZero.coe_ne_zero, not_false_iff, zero_lt_iff]

/- Two Laurent series whose difference has small valuation have the same coefficients for
small enough indices. -/
theorem eq_coeff_of_valuation_sub_lt {d n : ‚Ñ§} {f g : K‚∏®X‚∏©}
    (H : Valued.v (g - f) ‚â§ ‚Üë(Multiplicative.ofAdd (-d))) : n < d ‚Üí g.coeff n = f.coeff n := by
  by_cases triv : g = f
  ¬∑ exact fun _ => by rw [triv]
  ¬∑ intro hn
    apply eq_of_sub_eq_zero
    rw [‚Üê HahnSeries.coeff_sub]
    apply coeff_zero_of_lt_valuation K H hn

/- Every Laurent series of valuation less than `(1 : ‚Ñ§‚Çò‚ÇÄ)` comes from a power series. -/
theorem val_le_one_iff_eq_coe (f : K‚∏®X‚∏©) : Valued.v f ‚â§ (1 : ‚Ñ§‚Çò‚ÇÄ) ‚Üî
    ‚àÉ F : K‚ü¶X‚üß, F = f := by
  rw [‚Üê WithZero.coe_one, ‚Üê ofAdd_zero, ‚Üê neg_zero, valuation_le_iff_coeff_lt_eq_zero]
  refine ‚ü®fun h => ‚ü®PowerSeries.mk fun n => f.coeff n, ?_‚ü©, ?_‚ü©
  on_goal 1 => ext (_ | n)
  ¬∑ simp only [Int.ofNat_eq_coe, coeff_coe_powerSeries, coeff_mk]
  on_goal 1 => simp only [h (Int.negSucc n) (Int.negSucc_lt_zero n)]
  on_goal 2 => rintro ‚ü®F, rfl‚ü© _ _
  all_goals
    apply HahnSeries.embDomain_notin_range
    simp only [Nat.coe_castAddMonoidHom, RelEmbedding.coe_mk, Function.Embedding.coeFn_mk,
      Set.mem_range, not_exists, Int.negSucc_lt_zero, reduceCtorEq]
    intro
  ¬∑ simp only [not_false_eq_true]
  ¬∑ omega

end LaurentSeries

end AdicValuation

namespace LaurentSeries

variable {K : Type*} [Field K]

section Complete

open Filter Multiplicative

/- Sending a Laurent series to its `d`-th coefficient is uniformly continuous (independently of the
uniformity with which `K` is endowed). -/
theorem uniformContinuous_coeff {uK : UniformSpace K} (d : ‚Ñ§) :
    UniformContinuous fun f : K‚∏®X‚∏© ‚Ü¶ f.coeff d := by
  refine uniformContinuous_iff_eventually.mpr fun S hS ‚Ü¶ eventually_iff_exists_mem.mpr ?_
  let Œ≥ : ‚Ñ§‚Çò‚ÇÄÀ£ := Units.mk0 (‚Üë(Multiplicative.ofAdd (-(d + 1)))) WithZero.coe_ne_zero
  use {P | Valued.v (P.snd - P.fst) < ‚ÜëŒ≥}
  refine ‚ü®(Valued.hasBasis_uniformity K‚∏®X‚∏© ‚Ñ§‚Çò‚ÇÄ).mem_of_mem (by tauto), fun P hP ‚Ü¶ ?_‚ü©
  rw [eq_coeff_of_valuation_sub_lt K (le_of_lt hP) (lt_add_one _)]
  exact mem_uniformity_of_eq hS rfl

/-- Since extracting coefficients is uniformly continuous, every Cauchy filter in
`K‚∏®X‚∏©` gives rise to a Cauchy filter in `K` for every `d : ‚Ñ§`, and such Cauchy filter
in `K` converges to a principal filter -/
def Cauchy.coeff {‚Ñ± : Filter K‚∏®X‚∏©} (h‚Ñ± : Cauchy ‚Ñ±) : ‚Ñ§ ‚Üí K :=
  let _ : UniformSpace K := ‚ä•
  fun d ‚Ü¶ DiscreteUniformity.cauchyConst <| h‚Ñ±.map (uniformContinuous_coeff d)

theorem Cauchy.coeff_tendsto {‚Ñ± : Filter K‚∏®X‚∏©} (h‚Ñ± : Cauchy ‚Ñ±) (D : ‚Ñ§) :
    Tendsto (fun f : K‚∏®X‚∏© ‚Ü¶ f.coeff D) ‚Ñ± (ùìü {coeff h‚Ñ± D}) :=
  let _ : UniformSpace K := ‚ä•
  le_of_eq <| DiscreteUniformity.eq_pure_cauchyConst
    (h‚Ñ±.map (uniformContinuous_coeff D)) ‚ñ∏ (principal_singleton _).symm

/- For every Cauchy filter of Laurent series, there is a `N` such that the `n`-th coefficient
vanishes for all `n ‚â§ N` and almost all series in the filter. This is an auxiliary lemma used
to construct the limit of the Cauchy filter as a Laurent series, ensuring that the support of the
limit is `PWO`.
The result is true also for more general Hahn Series indexed over a partially ordered group `Œì`
beyond the special case `Œì = ‚Ñ§`, that corresponds to Laurent Series: nevertheless the proof below
does not generalise, as it relies on the study of the `X`-adic valuation attached to the height-one
prime `X`, and this is peculiar to the one-variable setting. In the future we should prove this
result in full generality and deduce the case `Œì = ‚Ñ§` from that one. -/
lemma Cauchy.exists_lb_eventual_support {‚Ñ± : Filter K‚∏®X‚∏©} (h‚Ñ± : Cauchy ‚Ñ±) :
    ‚àÉ N, ‚àÄ·∂† f : K‚∏®X‚∏© in ‚Ñ±, ‚àÄ n < N, f.coeff n = (0 : K) := by
  let entourage : Set (K‚∏®X‚∏© √ó K‚∏®X‚∏©) :=
    {P : K‚∏®X‚∏© √ó K‚∏®X‚∏© |
      Valued.v (P.snd - P.fst) < ((Multiplicative.ofAdd 0 : Multiplicative ‚Ñ§) : ‚Ñ§‚Çò‚ÇÄ)}
  let Œ∂ := Units.mk0 (G‚ÇÄ := ‚Ñ§‚Çò‚ÇÄ) _ (WithZero.coe_ne_zero (a := (Multiplicative.ofAdd 0)))
  obtain ‚ü®S, ‚ü®hS, ‚ü®T, ‚ü®hT, H‚ü©‚ü©‚ü©‚ü© := mem_prod_iff.mp <| Filter.le_def.mp h‚Ñ±.2 entourage
    <| (Valued.hasBasis_uniformity K‚∏®X‚∏© ‚Ñ§‚Çò‚ÇÄ).mem_of_mem (i := Œ∂) (by tauto)
  obtain ‚ü®f, hf‚ü© := forall_mem_nonempty_iff_neBot.mpr h‚Ñ±.1 (S ‚à© T) (inter_mem_iff.mpr ‚ü®hS, hT‚ü©)
  obtain ‚ü®N, hN‚ü© :  ‚àÉ N : ‚Ñ§, ‚àÄ g : K‚∏®X‚∏©,
    Valued.v (g - f) ‚â§ ‚Üë(Multiplicative.ofAdd (0 : ‚Ñ§)) ‚Üí ‚àÄ n < N, g.coeff n = 0 := by
    by_cases hf : f = 0
    ¬∑ refine ‚ü®0, fun x hg ‚Ü¶ ?_‚ü©
      rw [hf, sub_zero] at hg
      exact (valuation_le_iff_coeff_lt_eq_zero K).mp hg
    ¬∑ refine ‚ü®min (f.2.isWF.min (HahnSeries.support_nonempty_iff.mpr hf)) 0 - 1, fun _ hg n hn ‚Ü¶ ?_‚ü©
      rw [eq_coeff_of_valuation_sub_lt K hg (d := 0)]
      ¬∑ exact Function.notMem_support.mp fun h ‚Ü¶
        f.2.isWF.not_lt_min (HahnSeries.support_nonempty_iff.mpr hf) h
        <| lt_trans hn <| Int.sub_one_lt_iff.mpr <| min_le_left _ _
      exact lt_of_lt_of_le hn <| le_of_lt (Int.sub_one_lt_of_le <| min_le_right _ _)
  use N
  apply mem_of_superset (inter_mem hS hT)
  intro g hg
  have h_prod : (f, g) ‚àà entourage := Set.prod_mono (Set.inter_subset_left (t := T))
    (Set.inter_subset_right (s := S)) |>.trans H <| Set.mem_prod.mpr ‚ü®hf, hg‚ü©
  exact hN g (le_of_lt h_prod)

/- The support of `Cauchy.coeff` has a lower bound. -/
theorem Cauchy.exists_lb_support {‚Ñ± : Filter K‚∏®X‚∏©} (h‚Ñ± : Cauchy ‚Ñ±) :
    ‚àÉ N, ‚àÄ n, n < N ‚Üí coeff h‚Ñ± n = 0 := by
  let _ : UniformSpace K := ‚ä•
  obtain ‚ü®N, hN‚ü© := exists_lb_eventual_support h‚Ñ±
  refine ‚ü®N, fun n hn ‚Ü¶ Ultrafilter.eq_of_le_pure (h‚Ñ±.map (uniformContinuous_coeff n)).1
      ((principal_singleton _).symm ‚ñ∏ coeff_tendsto _ _) ?_‚ü©
  simp only [pure_zero, nonpos_iff]
  apply Filter.mem_of_superset hN (fun _ ha ‚Ü¶ ha _ hn)

/- The support of `Cauchy.coeff` is bounded below -/
theorem Cauchy.coeff_support_bddBelow {‚Ñ± : Filter K‚∏®X‚∏©} (h‚Ñ± : Cauchy ‚Ñ±) :
    BddBelow (coeff h‚Ñ±).support := by
  refine ‚ü®(exists_lb_support h‚Ñ±).choose, fun d hd ‚Ü¶ ?_‚ü©
  by_contra hNd
  exact hd ((exists_lb_support h‚Ñ±).choose_spec d (not_le.mp hNd))

/-- To any Cauchy filter ‚Ñ± of `K‚∏®X‚∏©`, we can attach a laurent series that is the limit
of the filter. Its `d`-th coefficient is defined as the limit of `Cauchy.coeff h‚Ñ± d`, which is
again Cauchy but valued in the discrete space `K`. That sufficiently negative coefficients vanish
follows from `Cauchy.coeff_support_bddBelow` -/
def Cauchy.limit {‚Ñ± : Filter K‚∏®X‚∏©} (h‚Ñ± : Cauchy ‚Ñ±) : K‚∏®X‚∏© :=
  HahnSeries.mk (coeff h‚Ñ±) <| Set.IsWF.isPWO (coeff_support_bddBelow _).wellFoundedOn_lt

/- The following lemma shows that for every `d` smaller than the minimum between the integers
produced in `Cauchy.exists_lb_eventual_support` and `Cauchy.exists_lb_support`, for almost all
series in `‚Ñ±` the `d`th coefficient coincides with the `d`th coefficient of `Cauchy.coeff h‚Ñ±`. -/
theorem Cauchy.exists_lb_coeff_ne {‚Ñ± : Filter K‚∏®X‚∏©} (h‚Ñ± : Cauchy ‚Ñ±) :
    ‚àÉ N, ‚àÄ·∂† f : K‚∏®X‚∏© in ‚Ñ±, ‚àÄ d < N, coeff h‚Ñ± d = f.coeff d := by
  obtain ‚ü®‚ü®N‚ÇÅ, hN‚ÇÅ‚ü©, ‚ü®N‚ÇÇ, hN‚ÇÇ‚ü©‚ü© := exists_lb_eventual_support h‚Ñ±, exists_lb_support h‚Ñ±
  refine ‚ü®min N‚ÇÅ N‚ÇÇ, ‚Ñ±.3 hN‚ÇÅ fun _ hf d hd ‚Ü¶ ?_‚ü©
  rw [hf d (lt_of_lt_of_le hd (min_le_left _ _)), hN‚ÇÇ d (lt_of_lt_of_le hd (min_le_right _ _))]

/- Given a Cauchy filter `‚Ñ±` in the Laurent Series and a bound `D`, for almost all series in the
filter the coefficients below `D` coincide with `Caucy.coeff h‚Ñ±`. -/
theorem Cauchy.coeff_eventually_equal {‚Ñ± : Filter K‚∏®X‚∏©} (h‚Ñ± : Cauchy ‚Ñ±) {D : ‚Ñ§} :
    ‚àÄ·∂† f : K‚∏®X‚∏© in ‚Ñ±, ‚àÄ d, d < D ‚Üí coeff h‚Ñ± d = f.coeff d := by
  -- `œÜ` sends `d` to the set of Laurent Series having `d`th coefficient equal to `‚Ñ±.coeff`.
  let œÜ : ‚Ñ§ ‚Üí Set K‚∏®X‚∏© := fun d ‚Ü¶ {f | coeff h‚Ñ± d = f.coeff d}
  have intersec‚ÇÅ :
    (‚ãÇ n ‚àà Set.Iio D, œÜ n) ‚äÜ {x : K‚∏®X‚∏© | ‚àÄ d : ‚Ñ§, d < D ‚Üí coeff h‚Ñ± d = x.coeff d} := by
    intro _ hf
    simpa only [Set.mem_iInter] using hf
  -- The goal is now to show that the intersection of all `œÜ d` (for `d < D`) is in `‚Ñ±`.
  let ‚Ñì := (exists_lb_coeff_ne h‚Ñ±).choose
  let N := max ‚Ñì D
  have intersec‚ÇÇ : ‚ãÇ n ‚àà Set.Iio D, œÜ n ‚äá (‚ãÇ n ‚àà Set.Iio ‚Ñì, œÜ n) ‚à© (‚ãÇ n ‚àà Set.Icc ‚Ñì N, œÜ n) := by
    simp only [Set.mem_Iio, Set.mem_Icc, Set.subset_iInter_iff]
    intro i hi x hx
    simp only [Set.mem_inter_iff, Set.mem_iInter, and_imp] at hx
    by_cases H : i < ‚Ñì
    exacts [hx.1 _ H, hx.2 _ (le_of_not_gt H) <| le_of_lt <| lt_max_of_lt_right hi]
  suffices (‚ãÇ n ‚àà Set.Iio ‚Ñì, œÜ n) ‚à© (‚ãÇ n ‚àà Set.Icc ‚Ñì N, œÜ n) ‚àà ‚Ñ± by
    exact ‚Ñ±.sets_of_superset this <| intersec‚ÇÇ.trans intersec‚ÇÅ
  /- To show that the intersection we have in sight is in `‚Ñ±`, we use that it contains a double
  intersection (an infinite and a finite one): by general properties of filters, we are reduced
  to show that both terms are in `‚Ñ±`, which is easy in light of their definition. -/
  ¬∑ simp only [Set.mem_Iio, Set.mem_Ico, inter_mem_iff]
    constructor
    ¬∑ have := (exists_lb_coeff_ne h‚Ñ±).choose_spec
      rw [Filter.eventually_iff] at this
      convert this
      ext
      simp only [Set.mem_iInter, Set.mem_setOf_eq]; rfl
    ¬∑ rw [biInter_mem (Set.finite_Icc ‚Ñì N)]
      intro _ _
      apply coeff_tendsto h‚Ñ±
      simp only [principal_singleton, mem_pure]; rfl

open scoped Topology

/- The main result showing that the Cauchy filter tends to the `Cauchy.limit` -/
theorem Cauchy.eventually_mem_nhds {‚Ñ± : Filter K‚∏®X‚∏©} (h‚Ñ± : Cauchy ‚Ñ±)
    {U : Set K‚∏®X‚∏©} (hU : U ‚àà ùìù (Cauchy.limit h‚Ñ±)) : ‚àÄ·∂† f in ‚Ñ±, f ‚àà U := by
  obtain ‚ü®Œ≥, hU‚ÇÅ‚ü© := Valued.mem_nhds.mp hU
  suffices ‚àÄ·∂† f in ‚Ñ±, f ‚àà {y : K‚∏®X‚∏© | Valued.v (y - limit h‚Ñ±) < ‚ÜëŒ≥} by
    apply this.mono fun _ hf ‚Ü¶ hU‚ÇÅ hf
  set D := -((WithZero.unzero Œ≥.ne_zero).toAdd - 1) with hD‚ÇÄ
  have hD : ((Multiplicative.ofAdd (-D) : Multiplicative ‚Ñ§) : ‚Ñ§‚Çò‚ÇÄ) < Œ≥ := by
    rw [‚Üê WithZero.coe_unzero Œ≥.ne_zero, WithZero.coe_lt_coe, hD‚ÇÄ, neg_neg, ofAdd_sub,
      ofAdd_toAdd, div_lt_comm, div_self', ‚Üê ofAdd_zero, Multiplicative.ofAdd_lt]
    exact zero_lt_one
  apply coeff_eventually_equal (D := D) h‚Ñ± |>.mono
  intro _ hf
  apply lt_of_le_of_lt (valuation_le_iff_coeff_lt_eq_zero K |>.mpr _) hD
  intro n hn
  rw [HahnSeries.coeff_sub, sub_eq_zero, hf n hn |>.symm]; rfl

/- Laurent Series with coefficients in a field are complete w.r.t. the `X`-adic valuation -/
instance instLaurentSeriesComplete : CompleteSpace K‚∏®X‚∏© :=
  ‚ü®fun h‚Ñ± ‚Ü¶ ‚ü®Cauchy.limit h‚Ñ±, fun _ hS ‚Ü¶ Cauchy.eventually_mem_nhds h‚Ñ± hS‚ü©‚ü©

end Complete

section Dense

open scoped Multiplicative

open LaurentSeries PowerSeries IsDedekindDomain.HeightOneSpectrum WithZero

theorem exists_Polynomial_intValuation_lt (F : K‚ü¶X‚üß) (Œ∑ : ‚Ñ§‚Çò‚ÇÄÀ£) :
    ‚àÉ P : K[X], (PowerSeries.idealX K).intValuation (F - P) < Œ∑ := by
  by_cases h_neg : 1 < Œ∑
  ¬∑ use 0
    simpa using (intValuation_le_one (PowerSeries.idealX K) F).trans_lt h_neg
  ¬∑ rw [not_lt, ‚Üê Units.val_le_val, Units.val_one, ‚Üê WithZero.coe_one, ‚Üê coe_unzero Œ∑.ne_zero,
      coe_le_coe, ‚Üê Multiplicative.toAdd_le, toAdd_one] at h_neg
    obtain ‚ü®d, hd‚ü© := Int.exists_eq_neg_ofNat h_neg
    use F.trunc (d + 1)
    have : Valued.v ((ofPowerSeries ‚Ñ§ K) (F - (trunc (d + 1) F))) ‚â§
      (Multiplicative.ofAdd (-(d + 1 : ‚Ñ§))) := by
      apply (intValuation_le_iff_coeff_lt_eq_zero K _).mpr
      simpa only [map_sub, sub_eq_zero, Polynomial.coeff_coe, coeff_trunc] using
        fun _ h ‚Ü¶ (if_pos h).symm
    rw [ neg_add, ofAdd_add, ‚Üê hd, ofAdd_toAdd, WithZero.coe_mul, coe_unzero,
      ‚Üê coe_algebraMap] at this
    rw [‚Üê valuation_of_algebraMap (K := K‚∏®X‚∏©) (PowerSeries.idealX K) (F - F.trunc (d + 1))]
    apply lt_of_le_of_lt this
    rw [‚Üê mul_one (Œ∑ : ‚Ñ§‚Çò‚ÇÄ), mul_assoc, one_mul]
    gcongr
    ¬∑ exact zero_lt_iff.2 Œ∑.ne_zero
    rw [‚Üê WithZero.coe_one, coe_lt_coe, ofAdd_neg, Right.inv_lt_one_iff, ‚Üê ofAdd_zero,
      Multiplicative.ofAdd_lt]
    exact Int.zero_lt_one

/-- For every Laurent series `f` and every `Œ≥ : ‚Ñ§‚Çò‚ÇÄ` one can find a rational function `Q` such
that the `X`-adic valuation `v` satisfies `v (f - Q) < Œ≥`. -/
theorem exists_ratFunc_val_lt (f : K‚∏®X‚∏©) (Œ≥ : ‚Ñ§‚Çò‚ÇÄÀ£) :
    ‚àÉ Q : RatFunc K, Valued.v (f - Q) < Œ≥ := by
  set F := f.powerSeriesPart with hF
  by_cases ord_nonpos : f.order < 0
  ¬∑ set Œ∑ : ‚Ñ§‚Çò‚ÇÄÀ£ := Units.mk0 (Multiplicative.ofAdd f.order : Multiplicative ‚Ñ§) coe_ne_zero
      with hŒ∑
    obtain ‚ü®P, hP‚ü© := exists_Polynomial_intValuation_lt F (Œ∑ * Œ≥)
    use RatFunc.X ^ f.order * (P : RatFunc K)
    have F_mul := f.ofPowerSeries_powerSeriesPart
    obtain ‚ü®s, hs‚ü© := Int.exists_eq_neg_ofNat (le_of_lt ord_nonpos)
    rw [‚Üê hF, hs, neg_neg, ‚Üê ofPowerSeries_X_pow s, ‚Üê inv_mul_eq_iff_eq_mul‚ÇÄ] at F_mul
    ¬∑ rw [hs, ‚Üê F_mul, PowerSeries.coe_pow, PowerSeries.coe_X, RatFunc.coe_mul, zpow_neg,
        zpow_natCast, inv_eq_one_div (RatFunc.X ^ s), RatFunc.coe_div, RatFunc.coe_pow,
        RatFunc.coe_X, RatFunc.coe_one, ‚Üê inv_eq_one_div, ‚Üê mul_sub, map_mul, map_inv‚ÇÄ,
        ‚Üê PowerSeries.coe_X, valuation_X_pow, ‚Üê hs, ‚Üê RatFunc.coe_coe, ‚Üê PowerSeries.coe_sub,
        ‚Üê coe_algebraMap, adicValued_apply, valuation_of_algebraMap,
        ‚Üê Units.val_mk0 (a := ((Multiplicative.ofAdd f.order : Multiplicative ‚Ñ§) : ‚Ñ§‚Çò‚ÇÄ)), ‚Üê hŒ∑]
      apply inv_mul_lt_of_lt_mul‚ÇÄ
      rwa [‚Üê Units.val_mul]
    ¬∑ simp only [PowerSeries.coe_pow, pow_ne_zero, PowerSeries.coe_X, ne_eq,
        single_eq_zero_iff, one_ne_zero, not_false_iff]
  ¬∑ obtain ‚ü®s, hs‚ü© := Int.exists_eq_neg_ofNat (Int.neg_nonpos_of_nonneg (not_lt.mp ord_nonpos))
    obtain ‚ü®P, hP‚ü© := exists_Polynomial_intValuation_lt (PowerSeries.X ^ s * F) Œ≥
    use P
    rw [‚Üê X_order_mul_powerSeriesPart (neg_inj.1 hs).symm, ‚Üê RatFunc.coe_coe,
      ‚Üê PowerSeries.coe_sub, ‚Üê coe_algebraMap, adicValued_apply, valuation_of_algebraMap]
    exact hP

theorem coe_range_dense : DenseRange ((‚Üë) : RatFunc K ‚Üí K‚∏®X‚∏©) := by
  rw [denseRange_iff_closure_range]
  ext f
  simp only [UniformSpace.mem_closure_iff_symm_ball, Set.mem_univ, iff_true, Set.Nonempty,
    Set.mem_inter_iff, Set.mem_range, Set.mem_setOf_eq, exists_exists_eq_and]
  intro V hV h_symm
  rw [uniformity_eq_comap_neg_add_nhds_zero_swapped] at hV
  obtain ‚ü®T, hT‚ÇÄ, hT‚ÇÅ‚ü© := hV
  obtain ‚ü®Œ≥, hŒ≥‚ü© := Valued.mem_nhds_zero.mp hT‚ÇÄ
  obtain ‚ü®P, _‚ü© := exists_ratFunc_val_lt f Œ≥
  use P
  apply hT‚ÇÅ
  apply hŒ≥
  simpa only [add_comm, ‚Üê sub_eq_add_neg, gt_iff_lt, Set.mem_setOf_eq]

end Dense

section Comparison

open RatFunc AbstractCompletion IsDedekindDomain.HeightOneSpectrum

theorem inducing_coe : IsUniformInducing ((‚Üë) : RatFunc K ‚Üí K‚∏®X‚∏©) := by
  rw [isUniformInducing_iff, Filter.comap]
  ext S
  simp only [exists_prop, Filter.mem_mk, Set.mem_setOf_eq, uniformity_eq_comap_nhds_zero,
    Filter.mem_comap]
  constructor
  ¬∑ rintro ‚ü®T, ‚ü®‚ü®R, ‚ü®hR, pre_R‚ü©‚ü©, pre_T‚ü©‚ü©
    obtain ‚ü®d, hd‚ü© := Valued.mem_nhds.mp hR
    use {P : RatFunc K | Valued.v P < ‚Üëd}
    simp only [Valued.mem_nhds, sub_zero]
    refine ‚ü®‚ü®d, by rfl‚ü©, subset_trans (fun _ _ ‚Ü¶ pre_R ?_) pre_T‚ü©
    apply hd
    simp only [sub_zero, Set.mem_setOf_eq]
    rw [‚Üê RatFunc.coe_sub, valuation_def, ‚Üê valuation_eq_LaurentSeries_valuation]
    assumption
  ¬∑ rintro ‚ü®_, ‚ü®hT, pre_T‚ü©‚ü©
    obtain ‚ü®d, hd‚ü© := Valued.mem_nhds.mp hT
    let X := {f : K‚∏®X‚∏© | Valued.v f < ‚Üëd}
    refine ‚ü®(fun x : K‚∏®X‚∏© √ó K‚∏®X‚∏© ‚Ü¶ x.snd - x.fst) ‚Åª¬π' X, ‚ü®X, ?_‚ü©, ?_‚ü©
    ¬∑ refine ‚ü®?_, Set.Subset.refl _‚ü©
      ¬∑ simp only [Valued.mem_nhds, sub_zero]
        use d
    ¬∑ refine subset_trans (fun _ _ ‚Ü¶ ?_) pre_T
      apply hd
      rw [Set.mem_setOf_eq, sub_zero, WithZero.valued_def, valuation_eq_LaurentSeries_valuation,
        RatFunc.coe_sub]
      assumption

theorem continuous_coe : Continuous ((‚Üë) : RatFunc K ‚Üí K‚∏®X‚∏©) :=
  (isUniformInducing_iff'.1 (inducing_coe)).1.continuous

/-- The `X`-adic completion as an abstract completion of `RatFunc K` -/
abbrev ratfuncAdicComplPkg : AbstractCompletion (RatFunc K) :=
  UniformSpace.Completion.cPkg

variable (K)
/-- Having established that the `K‚∏®X‚∏©` is complete and contains `RatFunc K` as a dense
subspace, it gives rise to an abstract completion of `RatFunc K`. -/
noncomputable def LaurentSeriesPkg : AbstractCompletion (RatFunc K) where
  space := K‚∏®X‚∏©
  coe := (‚Üë)
  uniformStruct := inferInstance
  complete := inferInstance
  separation := inferInstance
  isUniformInducing := inducing_coe
  dense := coe_range_dense

instance : TopologicalSpace (LaurentSeriesPkg K).space :=
  (LaurentSeriesPkg K).uniformStruct.toTopologicalSpace

@[simp]
theorem LaurentSeries_coe (x : RatFunc K) : (LaurentSeriesPkg K).coe x = (x : K‚∏®X‚∏©) :=
  rfl

/-- Reintrerpret the extension of `coe : RatFunc K ‚Üí K‚∏®X‚∏©` as ring homomorphism -/
abbrev extensionAsRingHom :=
  UniformSpace.Completion.extensionHom (coeAlgHom K).toRingHom

/-- An abbreviation for the `X`-adic completion of `RatFunc K` -/
abbrev RatFuncAdicCompl := adicCompletion (RatFunc K) (idealX K)

/- The two instances below make `comparePkg` and `comparePkg_eq_extension` slightly faster. -/
instance : UniformSpace (RatFuncAdicCompl K) := inferInstance
instance : UniformSpace K‚∏®X‚∏© := inferInstance

/-- The uniform space isomorphism between two abstract completions of `ratfunc K` -/
abbrev comparePkg : RatFuncAdicCompl K ‚âÉ·µ§ K‚∏®X‚∏© :=
  compareEquiv ratfuncAdicComplPkg (LaurentSeriesPkg K)

lemma comparePkg_eq_extension (x : UniformSpace.Completion (RatFunc K)) :
    (comparePkg K).toFun x = (extensionAsRingHom K (continuous_coe)).toFun x := rfl

/-- The uniform space equivalence between two abstract completions of `ratfunc K` as a ring
equivalence: this will be the *inverse* of the fundamental one. -/
abbrev ratfuncAdicComplRingEquiv : RatFuncAdicCompl K ‚âÉ+* K‚∏®X‚∏© :=
  {comparePkg K with
    map_mul' := by
      intro x y
      rw [comparePkg_eq_extension, (extensionAsRingHom K (continuous_coe)).map_mul']
      rfl
    map_add' := by
      intro x y
      rw [comparePkg_eq_extension, (extensionAsRingHom K (continuous_coe)).map_add']
      rfl }

/-- The uniform space equivalence between two abstract completions of `ratfunc K` as a ring
equivalence: it goes from `K‚∏®X‚∏©` to `RatFuncAdicCompl K` -/
abbrev LaurentSeriesRingEquiv : K‚∏®X‚∏© ‚âÉ+* RatFuncAdicCompl K :=
  (ratfuncAdicComplRingEquiv K).symm

@[simp]
lemma LaurentSeriesRingEquiv_def (f : K‚ü¶X‚üß) :
    (LaurentSeriesRingEquiv K) f = (LaurentSeriesPkg K).compare ratfuncAdicComplPkg (f : K‚∏®X‚∏©) :=
  rfl

@[simp]
theorem ratfuncAdicComplRingEquiv_apply (x : RatFuncAdicCompl K) :
    ratfuncAdicComplRingEquiv K x = ratfuncAdicComplPkg.compare (LaurentSeriesPkg K) x := rfl

theorem coe_X_compare :
    (ratfuncAdicComplRingEquiv K) ((RatFunc.X : RatFunc K) : RatFuncAdicCompl K) =
      ((PowerSeries.X : K‚ü¶X‚üß) : K‚∏®X‚∏©) := by
  rw [PowerSeries.coe_X, ‚Üê RatFunc.coe_X, ‚Üê LaurentSeries_coe, ‚Üê compare_coe]
  rfl

theorem algebraMap_apply (a : K) : algebraMap K K‚∏®X‚∏© a = HahnSeries.C a := by
  simp [RingHom.algebraMap_toAlgebra]

instance : Algebra K (RatFuncAdicCompl K) :=
  RingHom.toAlgebra ((LaurentSeriesRingEquiv K).toRingHom.comp HahnSeries.C)

/-- The algebra equivalence between `K‚∏®X‚∏©` and the `X`-adic completion of `RatFunc X` -/
def LaurentSeriesAlgEquiv : K‚∏®X‚∏© ‚âÉ‚Çê[K] RatFuncAdicCompl K :=
  AlgEquiv.ofRingEquiv (f := LaurentSeriesRingEquiv K)
    (fun a ‚Ü¶ by simp [RingHom.algebraMap_toAlgebra])

open Filter WithZero

open scoped WithZeroTopology Topology Multiplicative

theorem valuation_LaurentSeries_equal_extension :
    (LaurentSeriesPkg K).isDenseInducing.extend Valued.v = (Valued.v : K‚∏®X‚∏© ‚Üí ‚Ñ§‚Çò‚ÇÄ) := by
  apply IsDenseInducing.extend_unique
  ¬∑ intro x
    rw [valued_def, valuation_eq_LaurentSeries_valuation K x]
    rfl
  ¬∑ exact Valued.continuous_valuation (K := K‚∏®X‚∏©)

theorem tendsto_valuation (a : (idealX K).adicCompletion (RatFunc K)) :
    Tendsto (Valued.v : RatFunc K ‚Üí ‚Ñ§‚Çò‚ÇÄ) (comap (‚Üë) (ùìù a)) (ùìù (Valued.v a : ‚Ñ§‚Çò‚ÇÄ)) := by
  set œà := (Valued.v : RatFunc K ‚Üí ‚Ñ§‚Çò‚ÇÄ) with hœà
  have := Valued.is_topological_valuation (R := (idealX K).adicCompletion (RatFunc K))
  by_cases ha : a = 0
  ¬∑ rw [tendsto_def]
    intro S hS
    rw [ha, map_zero, WithZeroTopology.hasBasis_nhds_zero.1 S] at hS
    obtain ‚ü®Œ≥, Œ≥_ne_zero, Œ≥_le‚ü© := hS
    use {t | Valued.v t < Œ≥}
    constructor
    ¬∑ rw [ha, this]
      use Units.mk0 Œ≥ Œ≥_ne_zero
      rw [Units.val_mk0]
    ¬∑ refine Set.Subset.trans (fun a _ ‚Ü¶ ?_) (Set.preimage_mono Œ≥_le)
      rwa [Set.mem_preimage, Set.mem_Iio, hœà, ‚Üê Valued.valuedCompletion_apply a]
  ¬∑ rw [WithZeroTopology.tendsto_of_ne_zero ((Valuation.ne_zero_iff Valued.v).mpr ha), hœà,
      Filter.eventually_comap, Filter.Eventually, Valued.mem_nhds]
    set Œ≥ := Valued.v a / (‚Üë(Multiplicative.ofAdd (1 : ‚Ñ§)) : ‚Ñ§‚Çò‚ÇÄ) with h_aŒ≥
    have Œ≥_ne_zero : Œ≥ ‚â† 0 := by
      rw [ne_eq, _root_.div_eq_zero_iff, Valuation.zero_iff]
      simpa only [coe_ne_zero, or_false]
    use Units.mk0 Œ≥ Œ≥_ne_zero
    intro y val_y b diff_b_y
    replace val_y : Valued.v y = Valued.v a := by
      refine Valuation.map_eq_of_sub_lt _ (val_y.trans ?_)
      rw [Units.val_mk0, h_aŒ≥, ‚Üê coe_unzero ((Valuation.ne_zero_iff Valued.v).mpr ha), ‚Üê
        WithZero.coe_div, coe_lt_coe, div_lt_self_iff, ‚Üê ofAdd_zero,
        Multiplicative.ofAdd_lt]
      exact Int.zero_lt_one
    rw [‚Üê Valued.extension_extends, ‚Üê val_y, ‚Üê diff_b_y]
    congr

/- The extension of the `X`-adic valuation from `RatFunc K` up to its abstract completion coincides,
modulo the isomorphism with `K‚∏®X‚∏©`, with the `X`-adic valuation on `K‚∏®X‚∏©`. -/
theorem valuation_compare (f : K‚∏®X‚∏©) :
    (Valued.v : (RatFuncAdicCompl K) ‚Üí ‚Ñ§‚Çò‚ÇÄ)
        (AbstractCompletion.compare (LaurentSeriesPkg K) ratfuncAdicComplPkg f) =
      Valued.v f := by
  rw [‚Üê valuation_LaurentSeries_equal_extension, ‚Üê compare_comp_eq_compare
    (pkg := ratfuncAdicComplPkg) (cont_f := Valued.continuous_valuation)]
  ¬∑ rfl
  exact (tendsto_valuation K)

section PowerSeries

/-- In order to compare `K‚ü¶X‚üß` with the valuation subring in the `X`-adic completion of
`RatFunc K` we consider its alias as a subring of `K‚∏®X‚∏©`. -/
abbrev powerSeries_as_subring : Subring K‚∏®X‚∏© :=
  Subring.map (HahnSeries.ofPowerSeries ‚Ñ§ K) ‚ä§

/-- The ring `K‚ü¶X‚üß` is isomorphic to the subring `powerSeries_as_subring K` -/
abbrev powerSeriesEquivSubring : K‚ü¶X‚üß ‚âÉ+* powerSeries_as_subring K :=
  ((Subring.topEquiv).symm).trans (Subring.equivMapOfInjective ‚ä§ (ofPowerSeries ‚Ñ§ K)
    ofPowerSeries_injective)

lemma powerSeriesEquivSubring_apply (f : K‚ü¶X‚üß) :
    powerSeriesEquivSubring K f =
      ‚ü®HahnSeries.ofPowerSeries ‚Ñ§ K f, Subring.mem_map.mpr ‚ü®f, trivial, rfl‚ü©‚ü© :=
  rfl

lemma powerSeriesEquivSubring_coe_apply (f : K‚ü¶X‚üß) :
    (powerSeriesEquivSubring K f : K‚∏®X‚∏©) = ofPowerSeries ‚Ñ§ K f :=
  rfl

/- Through the isomorphism `LaurentSeriesRingEquiv`, power series land in the unit ball inside the
completion of `RatFunc K`. -/
theorem mem_integers_of_powerSeries (F : K‚ü¶X‚üß) :
    (LaurentSeriesRingEquiv K) F ‚àà (idealX K).adicCompletionIntegers (RatFunc K) := by
  simp only [Subring.mem_map, exists_prop, ValuationSubring.mem_toSubring,
    mem_adicCompletionIntegers, LaurentSeriesRingEquiv_def,
    valuation_compare, val_le_one_iff_eq_coe]
  exact ‚ü®F, rfl‚ü©

/- Conversely, all elements in the unit ball inside the completion of `RatFunc K` come from a power
series through the isomorphism `LaurentSeriesRingEquiv`. -/
theorem exists_powerSeries_of_memIntegers {x : RatFuncAdicCompl K}
    (hx : x ‚àà (idealX K).adicCompletionIntegers (RatFunc K)) :
    ‚àÉ F : K‚ü¶X‚üß, (LaurentSeriesRingEquiv K) F = x := by
  set f := (ratfuncAdicComplRingEquiv K) x with hf
  have H_x : (LaurentSeriesPkg K).compare ratfuncAdicComplPkg ((ratfuncAdicComplRingEquiv K) x) =
      x := congr_fun (inverse_compare (LaurentSeriesPkg K) ratfuncAdicComplPkg) x
  rw [mem_adicCompletionIntegers, ‚Üê H_x] at hx
  obtain ‚ü®F, hF‚ü© := (val_le_one_iff_eq_coe K f).mp (valuation_compare _ f ‚ñ∏ hx)
  exact ‚ü®F, by rw [hF, hf, RingEquiv.symm_apply_apply]‚ü©

theorem powerSeries_ext_subring :
    Subring.map (LaurentSeriesRingEquiv K).toRingHom (powerSeries_as_subring K) =
      ((idealX K).adicCompletionIntegers (RatFunc K)).toSubring := by
  ext x
  refine ‚ü®fun ‚ü®f, ‚ü®F, _, coe_F‚ü©, hF‚ü© ‚Ü¶ ?_, fun H ‚Ü¶ ?_‚ü©
  ¬∑ simp only [ValuationSubring.mem_toSubring, ‚Üê hF, ‚Üê coe_F]
    apply mem_integers_of_powerSeries
  ¬∑ obtain ‚ü®F, hF‚ü© := exists_powerSeries_of_memIntegers K H
    simp only [Equiv.toFun_as_coe, UniformEquiv.coe_toEquiv, exists_exists_eq_and,
      UniformEquiv.coe_symm_toEquiv, Subring.mem_map, Equiv.invFun_as_coe]
    exact ‚ü®F, ‚ü®F, trivial, rfl‚ü©, hF‚ü©

/-- The ring isomorphism between `K‚ü¶X‚üß` and the unit ball inside the `X`-adic completion of
`RatFunc K`. -/
abbrev powerSeriesRingEquiv : K‚ü¶X‚üß ‚âÉ+* (idealX K).adicCompletionIntegers (RatFunc K) :=
  ((powerSeriesEquivSubring K).trans (LaurentSeriesRingEquiv K).subringMap).trans
    <| RingEquiv.subringCongr (powerSeries_ext_subring K)

lemma powerSeriesRingEquiv_coe_apply (f : K‚ü¶X‚üß) :
    powerSeriesRingEquiv K f = LaurentSeriesRingEquiv K (f : K‚∏®X‚∏©) :=
  rfl

lemma LaurentSeriesRingEquiv_mem_valuationSubring (f : K‚ü¶X‚üß) :
    LaurentSeriesRingEquiv K f ‚àà Valued.v.valuationSubring := by
  simp only [Valuation.mem_valuationSubring_iff]
  rw [LaurentSeriesRingEquiv_def, valuation_compare, val_le_one_iff_eq_coe]
  use f

lemma algebraMap_C_mem_adicCompletionIntegers (x : K) :
    ((LaurentSeriesRingEquiv K).toRingHom.comp HahnSeries.C) x ‚àà
      adicCompletionIntegers (RatFunc K) (idealX K) := by
  have : HahnSeries.C x = ofPowerSeries ‚Ñ§ K (PowerSeries.C K x) := by
    simp [C_apply, ofPowerSeries_C]
  simp only [RingHom.comp_apply, RingEquiv.toRingHom_eq_coe, RingHom.coe_coe, this]
  apply LaurentSeriesRingEquiv_mem_valuationSubring

instance : Algebra K ((idealX K).adicCompletionIntegers (RatFunc K)) :=
  RingHom.toAlgebra <|
    ((LaurentSeriesRingEquiv K).toRingHom.comp HahnSeries.C).codRestrict _
      (algebraMap_C_mem_adicCompletionIntegers K)

/-- The algebra isomorphism between `K‚ü¶X‚üß` and the unit ball inside the `X`-adic completion of
`RatFunc K`. -/
def powerSeriesAlgEquiv : K‚ü¶X‚üß ‚âÉ‚Çê[K] (idealX K).adicCompletionIntegers (RatFunc K) := by
  apply AlgEquiv.ofRingEquiv (f := powerSeriesRingEquiv K)
  intro a
  rw [PowerSeries.algebraMap_eq, RingHom.algebraMap_toAlgebra, ‚Üê Subtype.coe_inj,
    powerSeriesRingEquiv_coe_apply,
    RingHom.codRestrict_apply _ _ (algebraMap_C_mem_adicCompletionIntegers K)]
  simp

end PowerSeries

end Comparison

end LaurentSeries
