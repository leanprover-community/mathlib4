/-
Copyright (c) 2025 Nailin Guan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nailin Guan
-/
module

public import Mathlib.Algebra.Algebra.Shrink
public import Mathlib.Algebra.FiveLemma
public import Mathlib.CategoryTheory.Abelian.Injective.Dimension
public import Mathlib.RingTheory.LocalRing.MaximalIdeal.Basic
public import Mathlib.Algebra.Category.Grp.Zero
public import Mathlib.Algebra.Category.ModuleCat.Baer
public import Mathlib.Algebra.Category.ModuleCat.Ext.Finite
public import Mathlib.Algebra.Homology.DerivedCategory.Ext.Map
public import Mathlib.Algebra.Homology.ShortComplex.ModuleCat
public import Mathlib.CategoryTheory.Abelian.Projective.Dimension
public import Mathlib.RingTheory.Ideal.AssociatedPrime.Finiteness
public import Mathlib.RingTheory.Ideal.KrullsHeightTheorem
public import Mathlib.RingTheory.KrullDimension.Basic
public import Mathlib.RingTheory.KrullDimension.NonZeroDivisors
public import Mathlib.RingTheory.KrullDimension.Zero
public import Mathlib.RingTheory.LocalRing.Module
public import Mathlib.RingTheory.Noetherian.Basic
public import Mathlib.RingTheory.Regular.Category
public import Mathlib.RingTheory.Regular.IsSMulRegular

/-!

# The Definition of Gorenstein (Local) Ring

-/

@[expose] public section


section ENat

lemma WithBot.add_one_le_zero_iff_eq_bot' (a : WithBot ‚Ñï‚àû) :
    a + 1 ‚â§ 0 ‚Üî a = ‚ä• := by
  induction a with
  | bot => simp
  | coe a => simp [‚Üê WithBot.coe_one, ‚Üê WithBot.coe_add]

end ENat

universe v u

variable (R : Type u) [CommRing R]

open IsLocalRing

variable {R} in
lemma quotSMulTop_nontrivial'' [IsLocalRing R] {x : R} (mem : x ‚àà maximalIdeal R)
    (L : Type*) [AddCommGroup L] [Module R L] [Module.Finite R L] [Nontrivial L] :
    Nontrivial (QuotSMulTop x L) := by
  apply Submodule.Quotient.nontrivial_iff.mpr (Ne.symm _)
  apply Submodule.top_ne_pointwise_smul_of_mem_jacobson_annihilator
  exact IsLocalRing.maximalIdeal_le_jacobson _ mem

lemma exist_nat_eq' [FiniteRingKrullDim R] : ‚àÉ n : ‚Ñï, ringKrullDim R = n := by
  have : (ringKrullDim R).unbot ringKrullDim_ne_bot ‚â† ‚ä§ := by
    by_contra eq
    rw [‚Üê WithBot.coe_inj, WithBot.coe_unbot, WithBot.coe_top] at eq
    exact ringKrullDim_ne_top eq
  use ((ringKrullDim R).unbot ringKrullDim_ne_bot).toNat
  exact (WithBot.coe_unbot (ringKrullDim R) ringKrullDim_ne_bot).symm.trans
    (WithBot.coe_inj.mpr (ENat.coe_toNat this).symm)

variable {R} in
lemma IsLocalRing.ResidueField.map_injective [IsLocalRing R] {S : Type*} [CommRing S]
    [IsLocalRing S] (f : R ‚Üí+* S) [IsLocalHom f] :
    Function.Injective (ResidueField.map f) := by
  rw [RingHom.injective_iff_ker_eq_bot, RingHom.ker_eq_bot_iff_eq_zero]
  intro x hx
  simpa only [map_eq_zero] using hx

variable {R} in
lemma IsLocalRing.ResidueField.map_bijective_of_surjective [IsLocalRing R] {S : Type*} [CommRing S]
    [IsLocalRing S] (f : R ‚Üí+* S) (surj : Function.Surjective f) [IsLocalHom f] :
    Function.Bijective (ResidueField.map f) := by
  refine ‚ü®ResidueField.map_injective f, ?_‚ü©
  apply Ideal.Quotient.lift_surjective_of_surjective
  convert Function.Surjective.comp (Ideal.Quotient.mk_surjective (I := (maximalIdeal S))) surj

open CategoryTheory Abelian Module

section

variable {R} [IsLocalRing R] [IsNoetherianRing R] [Small.{v} R]

omit [IsLocalRing R] in
lemma ext_subsingleton_of_support_subset (N M : ModuleCat.{v} R) [Nfin : Module.Finite R N] (n : ‚Ñï)
    (h : support R N ‚äÜ {p | Subsingleton (Ext (ModuleCat.of R (Shrink.{v} (R ‚ß∏ p.1))) M n)}) :
    Subsingleton (Ext N M n) := by
  refine (IsNoetherianRing.induction_on_isQuotientEquivQuotientPrime
    (motive := fun L ‚Ü¶ (support R L ‚äÜ {p | Subsingleton
      (Ext (ModuleCat.of R (Shrink.{v} (R ‚ß∏ p.1))) M n)} ‚Üí
      Subsingleton (Ext (ModuleCat.of R L) M n))) R Nfin) ?_ ?_ ?_ h
  ¬∑ intro N _ _ _ sub _
    let _ : HasProjectiveDimensionLT (ModuleCat.of R N) 0 :=
      (ModuleCat.isZero_of_iff_subsingleton.mpr sub).hasProjectiveDimensionLT_zero
    exact HasProjectiveDimensionLT.subsingleton (ModuleCat.of R N) 0 n n.zero_le M
  ¬∑ intro N _ _ _ p e h
    have mem : p ‚àà support R N := by
      rw [e.support_eq, Module.mem_support_iff_of_finite, Ideal.annihilator_quotient]
    let e' : ModuleCat.of R N ‚âÖ ModuleCat.of R (Shrink.{v, u} (R ‚ß∏ p.asIdeal)) :=
      (e.trans (Shrink.linearEquiv R _).symm).toModuleIso
    have := (((extFunctor n).mapIso e'.op).app M).addCommGroupIsoToAddEquiv.subsingleton_congr
    simp only [extFunctor, extFunctorObj_obj_coe] at this
    simpa [‚Üê this] using h mem
  ¬∑ intro N‚ÇÅ _ _ _ N‚ÇÇ _ _ _ N‚ÇÉ _ _ _ f g inj surj exac h1 h3 h2
    simp only [Module.support_of_exact exac inj surj, Set.union_subset_iff] at h2
    let S : ShortComplex (ModuleCat.{v} R) := {
      f := ModuleCat.ofHom f
      g := ModuleCat.ofHom g
      zero := by
        rw [‚Üê ModuleCat.ofHom_comp, exac.linearMap_comp_eq_zero]
        rfl }
    have S_exact : S.ShortExact := {
      exact := (ShortComplex.ShortExact.moduleCat_exact_iff_function_exact S).mpr exac
      mono_f := (ModuleCat.mono_iff_injective S.f).mpr inj
      epi_g := (ModuleCat.epi_iff_surjective S.g).mpr surj }
    have := (Ext.contravariant_sequence_exact‚ÇÇ' S_exact M n).isZero_X‚ÇÇ
      ((@AddCommGrpCat.isZero_of_subsingleton _ (h3 h2.2)).eq_zero_of_src _)
      ((@AddCommGrpCat.isZero_of_subsingleton _ (h1 h2.1)).eq_zero_of_tgt _)
    exact AddCommGrpCat.subsingleton_of_isZero this

lemma ext_subsingleton_of_all_gt (M : ModuleCat.{v} R) [Module.Finite R M] (n : ‚Ñï)
    (p : Ideal R) [p.IsPrime] (ne : p ‚â† maximalIdeal R) (h : ‚àÄ q > p, q.IsPrime ‚Üí
      Subsingleton (Ext (ModuleCat.of R (Shrink.{v} (R ‚ß∏ q))) M (n + 1))) :
    Subsingleton (Ext (ModuleCat.of R (Shrink.{v} (R ‚ß∏ p))) M n) := by
  have plt : p < maximalIdeal R :=  lt_of_le_of_ne (le_maximalIdeal_of_isPrime p) ne
  obtain ‚ü®x, hx, nmem‚ü© : ‚àÉ x ‚àà maximalIdeal R, x ‚àâ p := Set.exists_of_ssubset plt
  let _ : Small.{v} (QuotSMulTop x (R ‚ß∏ p)) :=
    small_of_surjective (Submodule.Quotient.mk_surjective _)
  let fin : Module.Finite R (Shrink.{v, u} (R ‚ß∏ p)) :=
    Module.Finite.equiv (Shrink.linearEquiv R _).symm
  let _ : Nontrivial (QuotSMulTop x (Shrink.{v, u} (R ‚ß∏ p))) :=
    quotSMulTop_nontrivial'' hx _
  have : Subsingleton (Ext (ModuleCat.of R (QuotSMulTop x (Shrink.{v, u} (R ‚ß∏ p)))) M (n + 1)) := by
    apply ext_subsingleton_of_support_subset
    intro q hq
    apply h q.1 _ q.2
    simp only [support_quotSMulTop, (Shrink.linearEquiv R (R ‚ß∏ p)).support_eq, Set.mem_inter_iff,
      PrimeSpectrum.mem_zeroLocus, Set.singleton_subset_iff, SetLike.mem_coe] at hq
    have : q.asIdeal ‚â† p := ne_of_mem_of_not_mem' hq.2 nmem
    apply lt_of_le_of_ne _ (ne_of_mem_of_not_mem' hq.2 nmem).symm
    apply le_of_eq_of_le Ideal.annihilator_quotient.symm (Module.annihilator_le_of_mem_support hq.1)
  let S := (ModuleCat.of R (Shrink.{v, u} (R ‚ß∏ p))).smulShortComplex x
  have reg : IsSMulRegular (Shrink.{v, u} (R ‚ß∏ p)) x := by
    rw [(Shrink.linearEquiv.{v} R _).isSMulRegular_congr, isSMulRegular_iff_right_eq_zero_of_smul]
    intro r hr
    simpa [Algebra.smul_def, Ideal.Quotient.eq_zero_iff_mem, nmem] using hr
  have S_exact : S.ShortExact := IsSMulRegular.smulShortComplex_shortExact reg
  have exac := Ext.contravariant_sequence_exact‚ÇÅ' S_exact M n (n + 1) (add_comm 1 n)
  have epi := exac.epi_f ((@AddCommGrpCat.isZero_of_subsingleton _ this).eq_zero_of_tgt _)
  have : S.f = x ‚Ä¢ ùüô (ModuleCat.of R (Shrink.{v, u} (R ‚ß∏ p))) := by
    ext
    simp [S]
  simp only [S, this, AddCommGrpCat.epi_iff_surjective, AddCommGrpCat.hom_ofHom] at epi
  have : x ‚àà (Module.annihilator R (Ext S.X‚ÇÇ M n)).jacobson :=
    (IsLocalRing.maximalIdeal_le_jacobson _) hx
  by_contra ntr
  let _ : Nontrivial (Ext S.X‚ÇÇ M n) := not_subsingleton_iff_nontrivial.mp ntr
  let _ : Module.Finite R S.X‚ÇÇ := fin
  absurd Submodule.top_ne_pointwise_smul_of_mem_jacobson_annihilator this
  rw [eq_comm, eq_top_iff]
  intro y hy
  rcases epi y with ‚ü®z, hz‚ü©
  simp only [ModuleCat.smulShortComplex_X‚ÇÅ, ModuleCat.smulShortComplex_X‚ÇÇ, Ext.mk‚ÇÄ_smul,
      Ext.bilinearComp_apply_apply, Ext.smul_comp, Ext.mk‚ÇÄ_id_comp] at hz
  simpa [‚Üê hz] using Submodule.smul_mem_pointwise_smul _ _ ‚ä§ trivial

lemma ext_vanish_of_residueField_vanish (M : ModuleCat.{v} R) (n : ‚Ñï) [Module.Finite R M]
    (h : ‚àÄ i ‚â• n, Subsingleton (Ext (ModuleCat.of R (Shrink.{v} (R ‚ß∏ maximalIdeal R))) M i)) :
    ‚àÄ i ‚â• n, ‚àÄ N : ModuleCat.{v} R, Subsingleton (Ext N M i) := by
  intro i hi N
  apply ext_subsingleton_of_quotients
  intro I
  let _ := Module.Finite.equiv (Shrink.linearEquiv R (R ‚ß∏ I)).symm
  apply ext_subsingleton_of_support_subset
  intro p foo
  clear foo
  simp only [Set.mem_setOf_eq]
  have (n : ‚Ñï) : ringKrullDim (R ‚ß∏ p.1) ‚â§ n ‚Üí
    Subsingleton (Ext (ModuleCat.of R (Shrink.{v} (R ‚ß∏ p.asIdeal))) M i) := by
    induction n generalizing i hi p with
    | zero =>
      intro hp
      have : p.1 = maximalIdeal R := by
        rw [‚Üê isMaximal_iff, Ideal.Quotient.maximal_ideal_iff_isField_quotient]
        rw [‚Üê Ring.krullDimLE_iff] at hp
        exact Ring.KrullDimLE.isField_of_isDomain
      exact this ‚ñ∏ h i hi
    | succ n ih =>
      intro hp
      by_cases hpm : p.1 = maximalIdeal R
      ¬∑ rw [hpm]
        exact h i hi
      ¬∑ apply ext_subsingleton_of_all_gt M i p.1 hpm
        intro q hqp hq
        let q : PrimeSpectrum R := ‚ü®q, hq‚ü©
        have : ringKrullDim (R ‚ß∏ q.1) ‚â§ n := by
          rw [‚Üê WithBot.add_le_add_natCast_right_iff]
          apply le_trans _ hp
          obtain ‚ü®r, hrq, hrp‚ü© := Set.exists_of_ssubset hqp
          apply ringKrullDim_succ_le_of_surjective (r := Ideal.Quotient.mk p.1 r)
            (Ideal.Quotient.factor hqp.le) (Ideal.Quotient.factor_surjective hqp.le)
          ¬∑ simpa [Ideal.Quotient.eq_zero_iff_mem] using hrp
          ¬∑ simpa [Ideal.Quotient.eq_zero_iff_mem] using hrq
        apply ih (i + 1) (Nat.le_add_right_of_le hi) this
  rcases exist_nat_eq' R with ‚ü®n, hn‚ü©
  apply this n
  simpa [‚Üê hn] using ringKrullDim_quotient_le p.1

lemma injectiveDimension_eq_sInf_of_finite (M : ModuleCat.{v} R) [Module.Finite R M] :
    injectiveDimension M = sInf {n : WithBot ‚Ñï‚àû | ‚àÄ (i : ‚Ñï), n < i ‚Üí
      Subsingleton (Ext (ModuleCat.of R (Shrink.{v} (R ‚ß∏ maximalIdeal R))) M i)} := by
  simp only [injectiveDimension]
  congr! 3
  rename_i n
  refine ‚ü®fun h i hi ‚Ü¶ ?_, fun h i hi ‚Ü¶ ?_‚ü©
  ¬∑ let _ := h i hi
    exact HasInjectiveDimensionLT.subsingleton M i i (le_refl i) _
  ¬∑ rw [hasInjectiveDimensionLT_iff]
    intro j hj N e
    refine @Subsingleton.eq_zero _ _ ?_ e
    apply ext_vanish_of_residueField_vanish M i _ j hj N
    intro k hk
    exact h k (lt_of_lt_of_le hi (Nat.cast_le.mpr hk))

lemma injectiveDimension_lt_iff_of_finite (M : ModuleCat.{v} R) [Module.Finite R M] (n : ‚Ñï) :
    injectiveDimension M < n ‚Üî ‚àÄ (i : ‚Ñï), n ‚â§ i ‚Üí
      Subsingleton (Ext (ModuleCat.of R (Shrink.{v} (R ‚ß∏ maximalIdeal R))) M i) := by
  rw [injectiveDimension_eq_sInf_of_finite]
  refine ‚ü®fun h i hi ‚Ü¶ ?_, fun h ‚Ü¶ sInf_lt_iff.2 ?_‚ü©
  ¬∑ have : sInf {n : WithBot ‚Ñï‚àû | ‚àÄ (i : ‚Ñï), n < ‚Üëi ‚Üí
      Subsingleton (Ext (ModuleCat.of R (Shrink.{v} (R ‚ß∏ maximalIdeal R))) M i)} ‚àà _ :=
      csInf_mem ‚ü®‚ä§, by simp‚ü©
    exact this i (lt_of_lt_of_le h (Nat.cast_le.mpr hi))
  ¬∑ obtain _ | n := n
    ¬∑ exact ‚ü®‚ä•, fun i hi ‚Ü¶ h i (Nat.zero_le i) , by decide‚ü©
    ¬∑ exact ‚ü®n, fun i hi ‚Ü¶ h i (Nat.cast_lt.mp hi), by simp [WithBot.lt_add_one_iff]‚ü©

end

section

variable {R}

section restrictScalars

universe u'

variable {R' : Type u'} [CommRing R'] (f : R ‚Üí+* R')

instance ModuleCat.restrictScalars_additive :
    (ModuleCat.restrictScalars.{v} f).Additive where
  map_add := by simp

lemma ModuleCat.restrictScalars_map_exact' (S : ShortComplex (ModuleCat.{v} R')) (h : S.Exact) :
    (S.map (ModuleCat.restrictScalars.{v} f)).Exact := by
  rw [CategoryTheory.ShortComplex.ShortExact.moduleCat_exact_iff_function_exact] at h ‚ä¢
  exact h

instance ModuleCat.restrictScalars_preservesFiniteLimits :
    Limits.PreservesFiniteLimits (ModuleCat.restrictScalars.{v} f) := by
  have := ((CategoryTheory.Functor.exact_tfae (ModuleCat.restrictScalars.{v} f)).out 1 3).mp
    (ModuleCat.restrictScalars_map_exact' f)
  exact this.1

instance ModuleCat.restrictScalars_preservesFiniteColimits :
    Limits.PreservesFiniteColimits (ModuleCat.restrictScalars.{v} f) := by
  have := ((CategoryTheory.Functor.exact_tfae (ModuleCat.restrictScalars.{v} f)).out 1 3).mp
    (ModuleCat.restrictScalars_map_exact' f)
  exact this.2

/-- Restricting scalar by surjective ring homomorphism is fully faithful. -/
def ModuleCat.restrictScalars_fullyFaithful_of_surjective (h : Function.Surjective f) :
    (ModuleCat.restrictScalars.{v} f).FullyFaithful where
  preimage {X Y} g := ofHom
    { __ := g.hom
      map_smul' r' x := by
        let _ := Module.compHom X f
        rcases h r' with ‚ü®r, hr‚ü©
        rw [‚Üê hr]
        exact map_smul g.hom r x }
  map_preimage g := by
    ext
    rfl
  preimage_map g := by
    ext
    rfl

end restrictScalars

lemma hasProjectiveDimensionLE_finsupp_quotient_regular [Small.{v} R] (Œπ : Type v) {x : R}
    (regR : IsSMulRegular R x) :
    HasProjectiveDimensionLE (ModuleCat.of R (Œπ ‚Üí‚ÇÄ Shrink.{v} (R ‚ß∏ Ideal.span {x}))) 1 := by
  let f' : Shrink.{v} R ‚Üí‚Çó[R] Shrink.{v} R := ((Shrink.linearEquiv R R).symm.toLinearMap.comp
    (x ‚Ä¢ LinearMap.id)).comp (Shrink.linearEquiv R R).toLinearMap
  let g' : Shrink.{v} R ‚Üí‚Çó[R] Shrink.{v} (R ‚ß∏ Ideal.span {x}) :=
    ((Shrink.linearEquiv R _).symm.toLinearMap.comp
      (Ideal.Quotient.mk‚Çê R (Ideal.span {x})).toLinearMap).comp (Shrink.linearEquiv R R).toLinearMap
  have inj : Function.Injective f' := by simpa [f'] using regR
  have surj : Function.Surjective g' := by simpa [g'] using Ideal.Quotient.mk_surjective
  have exac : Function.Exact f' g' := by
    intro y
    simp only [LinearMap.coe_comp, LinearEquiv.coe_coe, AlgHom.coe_toLinearMap,
      Ideal.Quotient.mk‚Çê_eq_mk, Function.comp_apply, EmbeddingLike.map_eq_zero_iff,
      EquivLike.range_comp, Set.mem_range, LinearMap.smul_apply, LinearMap.id_coe,
      LinearEquiv.symm_apply_eq, id_eq, Ideal.Quotient.eq_zero_iff_mem,
      Ideal.mem_span_singleton', smul_eq_mul, mul_comm, g', f']
  let S : ShortComplex (ModuleCat.{v} R) := {
    X‚ÇÅ := ModuleCat.of R (Œπ ‚Üí‚ÇÄ Shrink.{v} R)
    X‚ÇÇ := ModuleCat.of R (Œπ ‚Üí‚ÇÄ Shrink.{v} R)
    X‚ÇÉ := ModuleCat.of R (Œπ ‚Üí‚ÇÄ Shrink.{v} (R ‚ß∏ Ideal.span {x}))
    f := ModuleCat.ofHom (Finsupp.mapRange.linearMap f')
    g := ModuleCat.ofHom (Finsupp.mapRange.linearMap g')
    zero := by
      rw [‚Üê ModuleCat.ofHom_comp, ‚Üê Finsupp.mapRange.linearMap_comp,
        exac.linearMap_comp_eq_zero]
      ext
      simp }
  have S_exact : S.ShortExact := {
    exact := (ShortComplex.ShortExact.moduleCat_exact_iff_function_exact S).mpr (by
      simp [LinearMap.exact_iff, S, Finsupp.ker_mapRange, LinearMap.exact_iff.mp exac,
        Finsupp.range_mapRange_linearMap f' (LinearMap.ker_eq_bot.mpr inj)] )
    mono_f := (ModuleCat.mono_iff_injective _).mpr (Finsupp.mapRange_injective f' (map_zero _) inj)
    epi_g := (ModuleCat.epi_iff_surjective _).mpr
      (Finsupp.mapRange_surjective g' (map_zero _) surj) }
  have : Projective (ModuleCat.of R (Œπ ‚Üí‚ÇÄ Shrink.{v} R)) := ModuleCat.projective_of_free
    ‚ü®Finsupp.mapRange.linearEquiv (Shrink.linearEquiv R R)‚ü©
  exact S_exact.hasProjectiveDimensionLT_X‚ÇÉ 1 inferInstance inferInstance

variable [Small.{v} R]

open Limits in
lemma extClass_postcomp_bijective_of_isSMulRegular {M : ModuleCat.{v} R} {x : R}
    (regM : IsSMulRegular M x) (N : ModuleCat.{v} R) (ann : x ‚Ä¢ ùüô N = 0) :
    Function.Bijective (regM.smulShortComplex_shortExact.extClass.postcomp
    N (rfl : 0 + 1 = 0 + 1)) := by
  refine ‚ü®?_, fun y ‚Ü¶ Ext.covariant_sequence_exact‚ÇÅ _ regM.smulShortComplex_shortExact y ?_ rfl‚ü©
  ¬∑ apply (AddCommGrpCat.mono_iff_injective _).mp ((Ext.covariant_sequence_exact‚ÇÉ' N
      regM.smulShortComplex_shortExact 0 1 (zero_add 1)).mono_g (IsZero.eq_zero_of_src ?_ _))
    apply @AddCommGrpCat.isZero_of_subsingleton _ ?_
    apply Ext.homEquiv‚ÇÄ.subsingleton_congr.mpr
    apply subsingleton_of_forall_eq 0 fun f ‚Ü¶ ModuleCat.hom_ext (LinearMap.ext fun t ‚Ü¶ regM ?_)
    have : (x ‚Ä¢ ùüô N) t = x ‚Ä¢ t := by simp
    simp [smul_zero, ‚Üê map_smul, ‚Üê this, ann, map_zero]
  ¬∑ conv in ShortComplex.f ?_ => change x ‚Ä¢ (ùüô M)
    rw [‚Üê Ext.mk‚ÇÄ_id_comp (y.comp (Ext.mk‚ÇÄ (x ‚Ä¢ ùüô M)) rfl), Ext.mk‚ÇÄ_smul,
      Ext.comp_smul, Ext.comp_smul, ‚Üê Ext.smul_comp, ‚Üê Ext.mk‚ÇÄ_smul]
    simp [ann]

/-- The map `Ext N (ModuleCat.of (R ‚ß∏ Ideal.span {x}) (QuotSMulTop x ‚ÜëM)) n ‚Üí+
  Ext ((ModuleCat.restrictScalars (Ideal.Quotient.mk (Ideal.span {x}))).obj N) M (n + 1)`
  is bijective. -/
theorem extClass_comp_mapExt_bijective {M : ModuleCat.{v} R} {x : R} (regR : IsSMulRegular R x)
    (regM : IsSMulRegular M x) (N : ModuleCat.{v} (R ‚ß∏ Ideal.span {x})) (n : ‚Ñï) :
    Function.Bijective ((regM.smulShortComplex_shortExact.extClass.postcomp
    ((ModuleCat.restrictScalars.{v} (Ideal.Quotient.mk (Ideal.span {x}))).obj N) rfl).comp
    ((ModuleCat.restrictScalars.{v} (Ideal.Quotient.mk (Ideal.span {x}))).mapExtAddHom N
    (ModuleCat.of (R ‚ß∏ Ideal.span {x}) (QuotSMulTop x M)) n)) := by
  let Fr := (ModuleCat.restrictScalars.{v} (Ideal.Quotient.mk (Ideal.span {x})))
  induction n generalizing N
  ¬∑ simp only [ModuleCat.smulShortComplex_X‚ÇÅ, Nat.reduceAdd, AddMonoidHom.coe_comp]
    refine Function.Bijective.comp ?_ ?_
    ¬∑ apply extClass_postcomp_bijective_of_isSMulRegular regM
      ext u
      simp
    ¬∑ change Function.Bijective (Ext.mapExactFunctor _)
      rw [Ext.mapExactFunctor‚ÇÄ, Equiv.comp_bijective, Equiv.bijective_comp]
      apply Functor.FullyFaithful.map_bijective
      exact ModuleCat.restrictScalars_fullyFaithful_of_surjective _ Ideal.Quotient.mk_surjective
  ¬∑ rename_i n ih
    let S := N.projectiveShortComplex
    have S_exact : S.ShortExact := N.shortExact_projectiveShortComplex
    let _ : HasProjectiveDimensionLT (S.map Fr).X‚ÇÇ 2 :=
      hasProjectiveDimensionLE_finsupp_quotient_regular N regR
    let MxM := (ModuleCat.of (R ‚ß∏ Ideal.span {x}) (QuotSMulTop x M))
    let f : Ext S.X‚ÇÇ MxM n ‚Üí+ Ext S.X‚ÇÅ MxM n := (Ext.mk‚ÇÄ S.f).precomp MxM (zero_add n)
    let g : Ext S.X‚ÇÅ MxM n ‚Üí+ Ext S.X‚ÇÉ MxM (n + 1) := S_exact.extClass.precomp MxM (add_comm 1 n)
    have exac1 : Function.Exact f g := (ShortComplex.ab_exact_iff_function_exact  _).mp
      (Ext.contravariant_sequence_exact‚ÇÅ' S_exact MxM n (n + 1) (add_comm 1 n))
    have surj1 : Function.Surjective g := precomp_extClass_surjective_of_projective_X‚ÇÇ MxM S_exact n
    let f' : Ext (S.map Fr).X‚ÇÇ M (n + 1) ‚Üí+ Ext (S.map Fr).X‚ÇÅ M (n + 1) :=
      (Ext.mk‚ÇÄ (S.map Fr).f).precomp M (zero_add (n + 1))
    let g' : Ext (S.map Fr).X‚ÇÅ M (n + 1) ‚Üí+ Ext (S.map Fr).X‚ÇÉ M (n + 2) :=
      (S_exact.map_of_exact Fr).extClass.precomp M (add_comm 1 (n + 1))
    have exac2 : Function.Exact f' g' :=
      (ShortComplex.ab_exact_iff_function_exact  _).mp (Ext.contravariant_sequence_exact‚ÇÅ'
      (S_exact.map_of_exact Fr) M (n + 1) (n + 2) (add_comm 1 (n + 1)))
    have isz2 : Limits.IsZero (AddCommGrpCat.of (Ext (S.map Fr).X‚ÇÇ M (n + 2))) :=
      @AddCommGrpCat.isZero_of_subsingleton _ (HasProjectiveDimensionLT.subsingleton (S.map Fr).X‚ÇÇ
        2 (n + 2) (Nat.le_add_left _ n) M)
    have surj2 : Function.Surjective g' := (AddCommGrpCat.epi_iff_surjective _).mp
      ((Ext.contravariant_sequence_exact‚ÇÉ' (S_exact.map_of_exact Fr) M (n + 1) (n + 2)
        (add_comm 1 (n + 1))).epi_f (isz2.eq_zero_of_tgt _))
    let ft (N : ModuleCat.{v} (R ‚ß∏ Ideal.span {x})) (m : ‚Ñï) :=
      ((regM.smulShortComplex_shortExact.extClass.postcomp (Fr.obj N) rfl).comp
      (Fr.mapExtAddHom N (ModuleCat.of (R ‚ß∏ Ideal.span {x}) (QuotSMulTop x M)) m))
    apply AddMonoidHom.bijective_of_surjective_of_bijective_of_right_exact f g f' g'
      (ft S.X‚ÇÇ n) (ft S.X‚ÇÅ n) (ft S.X‚ÇÉ (n + 1)) ?_ ?_
      exac1 exac2 (ih S.X‚ÇÇ).2 (ih S.X‚ÇÅ) surj1 surj2
    ¬∑ ext z
      have : (Ext.mk‚ÇÄ (Fr.map S.f)).comp (z.mapExactFunctor Fr) (zero_add n) =
        (((Ext.mk‚ÇÄ S.f).comp z (zero_add n)).mapExactFunctor Fr) := by
        rw [Ext.mapExt_comp_eq_comp_mapExt, Ext.mapExt_mk‚ÇÄ_eq_mk‚ÇÄ_map]
      apply (Ext.comp_assoc _ _ _ (zero_add n) rfl (zero_add (n + 1))).symm.trans
      exact congrFun (congrFun (congrFun (congrArg Ext.comp this) _) (n + 1)) rfl
    ¬∑ ext z
      have : (S_exact.map_of_exact Fr).extClass.comp (z.mapExactFunctor Fr) (add_comm 1 n) =
        (S_exact.extClass.comp z (add_comm 1 n)).mapExactFunctor Fr := by
        rw [Ext.mapExt_comp_eq_comp_mapExt, Ext.mapExt_extClass_eq_extClass_map]
      apply (Ext.comp_assoc _ _ _ (add_comm 1 n) rfl (add_comm 1 (n + 1))).symm.trans
      exact congrFun (congrFun (congrFun (congrArg Ext.comp this) _) (n + 2)) rfl

end

section

lemma Function.Bijective.subsingleton_congr {Œ± Œ≤ : Type*} {f : Œ± ‚Üí Œ≤} (bij : Function.Bijective f) :
    Subsingleton Œ± ‚Üî Subsingleton Œ≤ :=
  ‚ü®fun _ ‚Ü¶ bij.2.subsingleton, fun _ ‚Ü¶ bij.1.subsingleton‚ü©

variable {R} [IsLocalRing R]

section

variable [Small.{v} R]

lemma ext_residueField_subsingleton_iff {M : ModuleCat.{v} R} {x : R}
    (regR : IsSMulRegular R x) (regM : IsSMulRegular M x) (mem : x ‚àà maximalIdeal R) (n : ‚Ñï) :
    letI : IsLocalRing (R ‚ß∏ Ideal.span {x}) :=
      have : Nontrivial (R ‚ß∏ Ideal.span {x}) :=
        Ideal.Quotient.nontrivial_iff.mpr (by simpa [‚Üê Submodule.ideal_span_singleton_smul])
      have : IsLocalHom (Ideal.Quotient.mk (Ideal.span {x})) :=
        IsLocalHom.of_surjective _ Ideal.Quotient.mk_surjective
      IsLocalRing.of_surjective (Ideal.Quotient.mk (Ideal.span {x})) Ideal.Quotient.mk_surjective
    Subsingleton (Ext (ModuleCat.of R (Shrink.{v} (R ‚ß∏ maximalIdeal R))) M (n + 1)) ‚Üî
    Subsingleton (Ext (ModuleCat.of (R ‚ß∏ Ideal.span {x})
    (Shrink.{v} ((R ‚ß∏ Ideal.span {x}) ‚ß∏ maximalIdeal (R ‚ß∏ Ideal.span {x}))))
    (ModuleCat.of (R ‚ß∏ Ideal.span {x}) (QuotSMulTop x M)) n) := by
  let _ : Nontrivial (R ‚ß∏ Ideal.span {x}) :=
      Ideal.Quotient.nontrivial_iff.mpr (by simpa [‚Üê Submodule.ideal_span_singleton_smul])
  let _ : IsLocalHom (Ideal.Quotient.mk (Ideal.span {x})) :=
    IsLocalHom.of_surjective _ Ideal.Quotient.mk_surjective
  let _ : IsLocalRing (R ‚ß∏ Ideal.span {x}) :=
    IsLocalRing.of_surjective (Ideal.Quotient.mk (Ideal.span {x})) Ideal.Quotient.mk_surjective
  let k' := (ModuleCat.of (R ‚ß∏ Ideal.span {x})
    (Shrink.{v} ((R ‚ß∏ Ideal.span {x}) ‚ß∏ maximalIdeal (R ‚ß∏ Ideal.span {x}))))
  let e' : (R ‚ß∏ maximalIdeal R) ‚âÉ‚Çó[R] (R ‚ß∏ Ideal.span {x}) ‚ß∏ maximalIdeal (R ‚ß∏ Ideal.span {x}) :=
    { __ :=RingEquiv.ofBijective _ (ResidueField.map_bijective_of_surjective
        (Ideal.Quotient.mk (Ideal.span {x})) Ideal.Quotient.mk_surjective)
      map_smul' r y := by
        simp only [RingEquiv.toEquiv_eq_coe, Algebra.smul_def, Ideal.Quotient.algebraMap_eq,
          Equiv.toFun_as_coe, EquivLike.coe_coe, map_mul, RingEquiv.coe_ofBijective,
          RingHom.id_apply, mul_eq_mul_right_iff]
        left
        rfl }
  let e : (ModuleCat.of R (Shrink.{v} (R ‚ß∏ maximalIdeal R))) ‚âÖ
    (ModuleCat.restrictScalars (Ideal.Quotient.mk (Ideal.span {x}))).obj k' :=
    (((Shrink.linearEquiv.{v} R _).trans e').trans (Shrink.linearEquiv.{v} R _).symm).toModuleIso
  exact ((extClass_comp_mapExt_bijective regR regM k' n).subsingleton_congr.trans
    (((extFunctor (n + 1)).mapIso e.op).app M).addCommGroupIsoToAddEquiv.subsingleton_congr).symm

end

local instance finite_QuotSMulTop' (M : Type*) [AddCommGroup M] [Module R M] [Module.Finite R M]
    (x : R) : Module.Finite (R ‚ß∏ Ideal.span {x}) (QuotSMulTop x M) := by
  let f : M ‚Üí‚Çõ‚Çó[Ideal.Quotient.mk (Ideal.span {x})] (QuotSMulTop x M) := {
    __ := Submodule.mkQ _
    map_smul' r m := rfl }
  exact Module.Finite.of_surjective f (Submodule.mkQ_surjective _)

theorem injectiveDimension_quotSMulTop_succ_eq_injectiveDimension [Small.{v} R] [IsNoetherianRing R]
    {M : ModuleCat.{v} R} [Module.Finite R M] {x : R} (regR : IsSMulRegular R x)
    (regM : IsSMulRegular M x) (mem : x ‚àà maximalIdeal R) :
    injectiveDimension (ModuleCat.of (R ‚ß∏ Ideal.span {x}) (QuotSMulTop x M)) + 1 =
    injectiveDimension M := by
  let _ : IsLocalRing (R ‚ß∏ Ideal.span {x}) :=
    have : Nontrivial (R ‚ß∏ Ideal.span {x}) :=
      Ideal.Quotient.nontrivial_iff.mpr (by simpa [‚Üê Submodule.ideal_span_singleton_smul])
    have : IsLocalHom (Ideal.Quotient.mk (Ideal.span {x})) :=
      IsLocalHom.of_surjective _ Ideal.Quotient.mk_surjective
    IsLocalRing.of_surjective (Ideal.Quotient.mk (Ideal.span {x})) Ideal.Quotient.mk_surjective
  have sub : Subsingleton M ‚Üî Subsingleton (QuotSMulTop x M) := by
    refine ‚ü®fun h ‚Ü¶ inferInstance, fun h ‚Ü¶ ?_‚ü©
    by_contra!
    exact (not_subsingleton_iff_nontrivial.mpr (quotSMulTop_nontrivial'' mem M)) h
  have aux (n : ‚Ñï) :
    injectiveDimension (ModuleCat.of (R ‚ß∏ Ideal.span {x}) (QuotSMulTop x M)) + 1 ‚â§ n ‚Üî
    injectiveDimension M ‚â§ n := by
    match n with
    | 0 =>
      simp only [CharP.cast_eq_zero, WithBot.add_one_le_zero_iff_eq_bot',
        injectiveDimension_eq_bot_iff, ModuleCat.isZero_of_iff_subsingleton, ‚Üê sub]
      rw [‚Üê ModuleCat.isZero_of_iff_subsingleton (R := R), ‚Üê injectiveDimension_eq_bot_iff]
      refine ‚ü®fun h ‚Ü¶ by simp [h], fun h ‚Ü¶ ?_‚ü©
      rw [‚Üê Nat.cast_zero, ‚Üê WithBot.lt_add_one_iff, Nat.cast_zero, zero_add, ‚Üê Nat.cast_one,
        injectiveDimension_lt_iff_of_finite.{v} M 1] at h
      apply WithBot.lt_coe_bot.mp
      simp only [ModuleCat.of_coe, bot_eq_zero', WithBot.coe_zero]
      apply (injectiveDimension_lt_iff_of_finite.{v} M 0).mpr (fun i _ ‚Ü¶ ?_)
      by_cases eq0 : i = 0
      ¬∑ rw [eq0, Ext.homEquiv‚ÇÄ.subsingleton_congr, ModuleCat.homEquiv.subsingleton_congr]
        apply subsingleton_of_forall_eq 0 (fun f ‚Ü¶ ?_)
        ext y
        have : x ‚àà Module.annihilator R (Shrink.{v} (R ‚ß∏ maximalIdeal R)) := by
          simpa [(Shrink.linearEquiv R _).annihilator_eq, Ideal.annihilator_quotient] using mem
        apply IsSMulRegular.right_eq_zero_of_smul regM
        rw [‚Üê map_smul, Module.mem_annihilator.mp this, map_zero]
      ¬∑ exact h i (Nat.one_le_iff_ne_zero.mpr eq0)
    | n + 1 =>
      rw [‚Üê Nat.cast_one, Nat.cast_add, WithBot.add_le_add_natCast_right_iff, ‚Üê Nat.cast_add,
        ‚Üê WithBot.lt_add_one_iff, ‚Üê WithBot.lt_add_one_iff, ‚Üê Nat.cast_one, ‚Üê Nat.cast_add,
        ‚Üê Nat.cast_add, injectiveDimension_lt_iff_of_finite.{v},
        injectiveDimension_lt_iff_of_finite.{v}]
      refine ‚ü®fun h i hi ‚Ü¶ ?_, fun h i hi ‚Ü¶ ?_‚ü©
      ¬∑ have : i - 1 + 1 = i := by omega
        rw [‚Üê this, ext_residueField_subsingleton_iff regR regM mem (i - 1)]
        exact h (i - 1) (Nat.le_sub_one_of_lt hi)
      ¬∑ rw [‚Üê ext_residueField_subsingleton_iff regR regM mem i]
        exact h (i + 1) (Nat.add_le_add_right hi 1)
  refine eq_of_forall_ge_iff (fun N ‚Ü¶ ?_)
  induction N with
  | bot =>
    simp [injectiveDimension_eq_bot_iff, ModuleCat.isZero_of_iff_subsingleton,
      ModuleCat.isZero_of_iff_subsingleton (M := M), sub]
  | coe N =>
    induction N with
    | top => simp
    | coe n => simpa using aux n

end
