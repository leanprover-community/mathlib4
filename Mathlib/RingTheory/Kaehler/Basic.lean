/-
Copyright (c) 2020 Nicol√≤ Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nicol√≤ Cavalleri, Andrew Yang
-/
import Mathlib.RingTheory.Derivation.ToSquareZero
import Mathlib.RingTheory.Ideal.Cotangent
import Mathlib.RingTheory.IsTensorProduct
import Mathlib.RingTheory.EssentialFiniteness
import Mathlib.Algebra.Exact
import Mathlib.LinearAlgebra.TensorProduct.RightExactness

/-!
# The module of kaehler differentials

## Main results

- `KaehlerDifferential`: The module of kaehler differentials. For an `R`-algebra `S`, we provide
  the notation `Œ©[S‚ÅÑR]` for `KaehlerDifferential R S`.
  Note that the slash is `\textfractionsolidus`.
- `KaehlerDifferential.D`: The derivation into the module of kaehler differentials.
- `KaehlerDifferential.span_range_derivation`: The image of `D` spans `Œ©[S‚ÅÑR]` as an `S`-module.
- `KaehlerDifferential.linearMapEquivDerivation`:
  The isomorphism `Hom_R(Œ©[S‚ÅÑR], M) ‚âÉ‚Çó[S] Der_R(S, M)`.
- `KaehlerDifferential.quotKerTotalEquiv`: An alternative description of `Œ©[S‚ÅÑR]` as `S` copies
  of `S` with kernel (`KaehlerDifferential.kerTotal`) generated by the relations:
  1. `dx + dy = d(x + y)`
  2. `x dy + y dx = d(x * y)`
  3. `dr = 0` for `r ‚àà R`
- `KaehlerDifferential.map`: Given a map between the arrows `R ‚Üí+* A` and `S ‚Üí+* B`, we have an
  `A`-linear map `Œ©[A‚ÅÑR] ‚Üí Œ©[B‚ÅÑS]`.
- `KaehlerDifferential.map_surjective`:
  The sequence `Œ©[B‚ÅÑR] ‚Üí Œ©[B‚ÅÑA] ‚Üí 0` is exact.
- `KaehlerDifferential.exact_mapBaseChange_map`:
  The sequence `B ‚äó[A] Œ©[A‚ÅÑR] ‚Üí Œ©[B‚ÅÑR] ‚Üí Œ©[B‚ÅÑA]` is exact.
- `KaehlerDifferential.exact_kerCotangentToTensor_mapBaseChange`:
  If `A ‚Üí B` is surjective with kernel `I`, then
  the sequence `I/I¬≤ ‚Üí B ‚äó[A] Œ©[A‚ÅÑR] ‚Üí Œ©[B‚ÅÑR]` is exact.
- `KaehlerDifferential.mapBaseChange_surjective`:
  If `A ‚Üí B` is surjective, then the sequence `B ‚äó[A] Œ©[A‚ÅÑR] ‚Üí Œ©[B‚ÅÑR] ‚Üí 0` is exact.

## Future project

- Define the `IsKaehlerDifferential` predicate.
-/

suppress_compilation

section KaehlerDifferential

open scoped TensorProduct
open Algebra Finsupp

universe u v

variable (R : Type u) (S : Type v) [CommRing R] [CommRing S] [Algebra R S]

/-- The kernel of the multiplication map `S ‚äó[R] S ‚Üí‚Çê[R] S`. -/
abbrev KaehlerDifferential.ideal : Ideal (S ‚äó[R] S) :=
  RingHom.ker (TensorProduct.lmul' R : S ‚äó[R] S ‚Üí‚Çê[R] S)

variable {S}

theorem KaehlerDifferential.one_smul_sub_smul_one_mem_ideal (a : S) :
    (1 : S) ‚äó‚Çú[R] a - a ‚äó‚Çú[R] (1 : S) ‚àà KaehlerDifferential.ideal R S := by simp [RingHom.mem_ker]

variable {R}
variable {M : Type*} [AddCommGroup M] [Module R M] [Module S M] [IsScalarTower R S M]

/-- For a `R`-derivation `S ‚Üí M`, this is the map `S ‚äó[R] S ‚Üí‚Çó[S] M` sending `s ‚äó‚Çú t ‚Ü¶ s ‚Ä¢ D t`. -/
def Derivation.tensorProductTo (D : Derivation R S M) : S ‚äó[R] S ‚Üí‚Çó[S] M :=
  TensorProduct.AlgebraTensorModule.lift ((LinearMap.lsmul S (S ‚Üí‚Çó[R] M)).flip D.toLinearMap)

theorem Derivation.tensorProductTo_tmul (D : Derivation R S M) (s t : S) :
    D.tensorProductTo (s ‚äó‚Çú t) = s ‚Ä¢ D t := rfl

theorem Derivation.tensorProductTo_mul (D : Derivation R S M) (x y : S ‚äó[R] S) :
    D.tensorProductTo (x * y) =
      TensorProduct.lmul' (S := S) R x ‚Ä¢ D.tensorProductTo y +
        TensorProduct.lmul' (S := S) R y ‚Ä¢ D.tensorProductTo x := by
  refine TensorProduct.induction_on x ?_ ?_ ?_
  ¬∑ rw [zero_mul, map_zero, map_zero, zero_smul, smul_zero, add_zero]
  swap
  ¬∑ intro x‚ÇÅ y‚ÇÅ h‚ÇÅ h‚ÇÇ
    rw [add_mul, map_add, map_add, map_add, add_smul, smul_add, h‚ÇÅ, h‚ÇÇ, add_add_add_comm]
  intro x‚ÇÅ x‚ÇÇ
  refine TensorProduct.induction_on y ?_ ?_ ?_
  ¬∑ rw [mul_zero, map_zero, map_zero, zero_smul, smul_zero, add_zero]
  swap
  ¬∑ intro x‚ÇÅ y‚ÇÅ h‚ÇÅ h‚ÇÇ
    rw [mul_add, map_add, map_add, map_add, add_smul, smul_add, h‚ÇÅ, h‚ÇÇ, add_add_add_comm]
  intro x y
  simp only [TensorProduct.tmul_mul_tmul, Derivation.tensorProductTo,
    TensorProduct.AlgebraTensorModule.lift_apply,
    TensorProduct.lmul'_apply_tmul]
  dsimp
  rw [D.leibniz]
  simp only [smul_smul, smul_add, mul_comm (x * y) x‚ÇÅ, mul_right_comm x‚ÇÅ x‚ÇÇ, ‚Üê mul_assoc]

variable (R S)

/-- The kernel of `S ‚äó[R] S ‚Üí‚Çê[R] S` is generated by `1 ‚äó s - s ‚äó 1` as a `S`-module. -/
theorem KaehlerDifferential.submodule_span_range_eq_ideal :
    Submodule.span S (Set.range fun s : S => (1 : S) ‚äó‚Çú[R] s - s ‚äó‚Çú[R] (1 : S)) =
      (KaehlerDifferential.ideal R S).restrictScalars S := by
  apply le_antisymm
  ¬∑ rw [Submodule.span_le]
    rintro _ ‚ü®s, rfl‚ü©
    exact KaehlerDifferential.one_smul_sub_smul_one_mem_ideal _ _
  ¬∑ rintro x (hx : _ = _)
    have : x - TensorProduct.lmul' (S := S) R x ‚äó‚Çú[R] (1 : S) = x := by
      rw [hx, TensorProduct.zero_tmul, sub_zero]
    rw [‚Üê this]
    clear this hx
    refine TensorProduct.induction_on x ?_ ?_ ?_
    ¬∑ rw [map_zero, TensorProduct.zero_tmul, sub_zero]; exact zero_mem _
    ¬∑ intro x y
      have : x ‚äó‚Çú[R] y - (x * y) ‚äó‚Çú[R] (1 : S) = x ‚Ä¢ ((1 : S) ‚äó‚Çú y - y ‚äó‚Çú (1 : S)) := by
        simp_rw [smul_sub, TensorProduct.smul_tmul', smul_eq_mul, mul_one]
      rw [TensorProduct.lmul'_apply_tmul, this]
      refine Submodule.smul_mem _ x ?_
      apply Submodule.subset_span
      exact Set.mem_range_self y
    ¬∑ intro x y hx hy
      rw [map_add, TensorProduct.add_tmul, ‚Üê sub_add_sub_comm]
      exact add_mem hx hy

theorem KaehlerDifferential.span_range_eq_ideal :
    Ideal.span (Set.range fun s : S => (1 : S) ‚äó‚Çú[R] s - s ‚äó‚Çú[R] (1 : S)) =
      KaehlerDifferential.ideal R S := by
  apply le_antisymm
  ¬∑ rw [Ideal.span_le]
    rintro _ ‚ü®s, rfl‚ü©
    exact KaehlerDifferential.one_smul_sub_smul_one_mem_ideal _ _
  ¬∑ change (KaehlerDifferential.ideal R S).restrictScalars S ‚â§ (Ideal.span _).restrictScalars S
    rw [‚Üê KaehlerDifferential.submodule_span_range_eq_ideal, Ideal.span]
    conv_rhs => rw [‚Üê Submodule.span_span_of_tower S]
    exact Submodule.subset_span

/-- The module of K√§hler differentials (Kahler differentials, Kaehler differentials).
This is implemented as `I / I ^ 2` with `I` the kernel of the multiplication map `S ‚äó[R] S ‚Üí‚Çê[R] S`.
To view elements as a linear combination of the form `s ‚Ä¢ D s'`, use
`KaehlerDifferential.tensorProductTo_surjective` and `Derivation.tensorProductTo_tmul`.

We also provide the notation `Œ©[S‚ÅÑR]` for `KaehlerDifferential R S`.
Note that the slash is `\textfractionsolidus`.
-/
def KaehlerDifferential : Type v :=
  (KaehlerDifferential.ideal R S).Cotangent

instance : AddCommGroup (KaehlerDifferential R S) := inferInstanceAs <|
  AddCommGroup (KaehlerDifferential.ideal R S).Cotangent

instance KaehlerDifferential.module : Module (S ‚äó[R] S) (KaehlerDifferential R S) :=
  Ideal.Cotangent.moduleOfTower _

@[inherit_doc KaehlerDifferential]
notation "Œ©[" S "‚ÅÑ" R "]" => KaehlerDifferential R S

instance : Nonempty Œ©[S‚ÅÑR] := ‚ü®0‚ü©

instance KaehlerDifferential.module' {R' : Type*} [CommRing R'] [Algebra R' S]
    [SMulCommClass R R' S] :
    Module R' Œ©[S‚ÅÑR] :=
  Submodule.Quotient.module' _

instance : IsScalarTower S (S ‚äó[R] S) Œ©[S‚ÅÑR] :=
  Ideal.Cotangent.isScalarTower _

instance KaehlerDifferential.isScalarTower_of_tower {R‚ÇÅ R‚ÇÇ : Type*} [CommRing R‚ÇÅ] [CommRing R‚ÇÇ]
    [Algebra R‚ÇÅ S] [Algebra R‚ÇÇ S] [SMul R‚ÇÅ R‚ÇÇ]
    [SMulCommClass R R‚ÇÅ S] [SMulCommClass R R‚ÇÇ S] [IsScalarTower R‚ÇÅ R‚ÇÇ S] :
    IsScalarTower R‚ÇÅ R‚ÇÇ Œ©[S‚ÅÑR] :=
  Submodule.Quotient.isScalarTower _ _

instance KaehlerDifferential.isScalarTower' : IsScalarTower R (S ‚äó[R] S) Œ©[S‚ÅÑR] :=
  Submodule.Quotient.isScalarTower _ _

/-- The quotient map `I ‚Üí Œ©[S‚ÅÑR]` with `I` being the kernel of `S ‚äó[R] S ‚Üí S`. -/
def KaehlerDifferential.fromIdeal : KaehlerDifferential.ideal R S ‚Üí‚Çó[S ‚äó[R] S] Œ©[S‚ÅÑR] :=
  (KaehlerDifferential.ideal R S).toCotangent

/-- (Implementation) The underlying linear map of the derivation into `Œ©[S‚ÅÑR]`. -/
def KaehlerDifferential.DLinearMap : S ‚Üí‚Çó[R] Œ©[S‚ÅÑR] :=
  ((KaehlerDifferential.fromIdeal R S).restrictScalars R).comp
    ((TensorProduct.includeRight.toLinearMap - TensorProduct.includeLeft.toLinearMap :
            S ‚Üí‚Çó[R] S ‚äó[R] S).codRestrict
        ((KaehlerDifferential.ideal R S).restrictScalars R)
        (KaehlerDifferential.one_smul_sub_smul_one_mem_ideal R) :
      _ ‚Üí‚Çó[R] _)

theorem KaehlerDifferential.DLinearMap_apply (s : S) :
    KaehlerDifferential.DLinearMap R S s =
      (KaehlerDifferential.ideal R S).toCotangent
        ‚ü®1 ‚äó‚Çú s - s ‚äó‚Çú 1, KaehlerDifferential.one_smul_sub_smul_one_mem_ideal R s‚ü© := rfl

/-- The universal derivation into `Œ©[S‚ÅÑR]`. -/
def KaehlerDifferential.D : Derivation R S Œ©[S‚ÅÑR] :=
  { toLinearMap := KaehlerDifferential.DLinearMap R S
    map_one_eq_zero' := by
      dsimp [KaehlerDifferential.DLinearMap_apply, Ideal.toCotangent_apply]
      congr
      rw [sub_self]
    leibniz' := fun a b => by
      have : LinearMap.CompatibleSMul { x // x ‚àà ideal R S } Œ©[S‚ÅÑR] S (S ‚äó[R] S) := inferInstance
      dsimp [KaehlerDifferential.DLinearMap_apply]
      rw [‚Üê LinearMap.map_smul_of_tower (ideal R S).toCotangent,
        ‚Üê LinearMap.map_smul_of_tower (ideal R S).toCotangent,
        ‚Üê map_add (ideal R S).toCotangent, Ideal.toCotangent_eq, pow_two]
      convert Submodule.mul_mem_mul (KaehlerDifferential.one_smul_sub_smul_one_mem_ideal R a :)
        (KaehlerDifferential.one_smul_sub_smul_one_mem_ideal R b :) using 1
      simp only [Submodule.coe_add,
        TensorProduct.tmul_mul_tmul, mul_sub, sub_mul, mul_comm b, Submodule.coe_smul_of_tower,
        smul_sub, TensorProduct.smul_tmul', smul_eq_mul, mul_one]
      ring_nf }

theorem KaehlerDifferential.D_apply (s : S) :
    KaehlerDifferential.D R S s =
      (KaehlerDifferential.ideal R S).toCotangent
        ‚ü®1 ‚äó‚Çú s - s ‚äó‚Çú 1, KaehlerDifferential.one_smul_sub_smul_one_mem_ideal R s‚ü© := rfl

theorem KaehlerDifferential.span_range_derivation :
    Submodule.span S (Set.range <| KaehlerDifferential.D R S) = ‚ä§ := by
  rw [_root_.eq_top_iff]
  rintro x -
  obtain ‚ü®‚ü®x, hx‚ü©, rfl‚ü© := Ideal.toCotangent_surjective _ x
  have : x ‚àà (KaehlerDifferential.ideal R S).restrictScalars S := hx
  rw [‚Üê KaehlerDifferential.submodule_span_range_eq_ideal] at this
  suffices ‚àÉ hx, (KaehlerDifferential.ideal R S).toCotangent ‚ü®x, hx‚ü© ‚àà
      Submodule.span S (Set.range <| KaehlerDifferential.D R S) by
    exact this.choose_spec
  refine Submodule.span_induction ?_ ?_ ?_ ?_ this
  ¬∑ rintro _ ‚ü®x, rfl‚ü©
    refine ‚ü®KaehlerDifferential.one_smul_sub_smul_one_mem_ideal R x, ?_‚ü©
    apply Submodule.subset_span
    exact ‚ü®x, KaehlerDifferential.DLinearMap_apply R S x‚ü©
  ¬∑ exact ‚ü®zero_mem _, Submodule.zero_mem _‚ü©
  ¬∑ rintro x y - - ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü© ‚ü®hy‚ÇÅ, hy‚ÇÇ‚ü©; exact ‚ü®add_mem hx‚ÇÅ hy‚ÇÅ, Submodule.add_mem _ hx‚ÇÇ hy‚ÇÇ‚ü©
  ¬∑ rintro r x - ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©
    exact ‚ü®((KaehlerDifferential.ideal R S).restrictScalars S).smul_mem r hx‚ÇÅ,
      Submodule.smul_mem _ r hx‚ÇÇ‚ü©

/-- `Œ©[S‚ÅÑR]` is trivial if `R ‚Üí S` is surjective.
Also see `Algebra.FormallyUnramified.iff_subsingleton_kaehlerDifferential`. -/
lemma KaehlerDifferential.subsingleton_of_surjective (h : Function.Surjective (algebraMap R S)) :
    Subsingleton Œ©[S‚ÅÑR] := by
  suffices (‚ä§ : Submodule S Œ©[S‚ÅÑR]) ‚â§ ‚ä• from
    (subsingleton_iff_forall_eq 0).mpr fun y ‚Ü¶ this trivial
  rw [‚Üê KaehlerDifferential.span_range_derivation, Submodule.span_le]
  rintro _ ‚ü®x, rfl‚ü©; obtain ‚ü®x, rfl‚ü© := h x; simp

variable {R S}

/-- The linear map from `Œ©[S‚ÅÑR]`, associated with a derivation. -/
def Derivation.liftKaehlerDifferential (D : Derivation R S M) : Œ©[S‚ÅÑR] ‚Üí‚Çó[S] M := by
  refine LinearMap.comp ((((KaehlerDifferential.ideal R S) ‚Ä¢
    (‚ä§ : Submodule (S ‚äó[R] S) (KaehlerDifferential.ideal R S))).restrictScalars S).liftQ ?_ ?_)
    (Submodule.Quotient.restrictScalarsEquiv S _).symm.toLinearMap
  ¬∑ exact D.tensorProductTo.comp ((KaehlerDifferential.ideal R S).subtype.restrictScalars S)
  ¬∑ intro x hx
    rw [LinearMap.mem_ker]
    refine Submodule.smul_induction_on ((Submodule.restrictScalars_mem _ _ _).mp hx) ?_ ?_
    ¬∑ rintro x hx y -
      rw [RingHom.mem_ker] at hx
      dsimp
      rw [Derivation.tensorProductTo_mul, hx, y.prop, zero_smul, zero_smul, zero_add]
    ¬∑ intro x y ex ey; rw [map_add, ex, ey, zero_add]

theorem Derivation.liftKaehlerDifferential_apply (D : Derivation R S M) (x) :
    D.liftKaehlerDifferential ((KaehlerDifferential.ideal R S).toCotangent x) =
      D.tensorProductTo x := rfl

theorem Derivation.liftKaehlerDifferential_comp (D : Derivation R S M) :
    D.liftKaehlerDifferential.compDer (KaehlerDifferential.D R S) = D := by
  ext a
  dsimp [KaehlerDifferential.D_apply]
  refine (D.liftKaehlerDifferential_apply _).trans ?_
  rw [Subtype.coe_mk, map_sub, Derivation.tensorProductTo_tmul, Derivation.tensorProductTo_tmul,
    one_smul, D.map_one_eq_zero, smul_zero, sub_zero]

@[simp]
theorem Derivation.liftKaehlerDifferential_comp_D (D' : Derivation R S M) (x : S) :
    D'.liftKaehlerDifferential (KaehlerDifferential.D R S x) = D' x :=
  Derivation.congr_fun D'.liftKaehlerDifferential_comp x

@[ext]
theorem Derivation.liftKaehlerDifferential_unique (f f' : Œ©[S‚ÅÑR] ‚Üí‚Çó[S] M)
    (hf : f.compDer (KaehlerDifferential.D R S) = f'.compDer (KaehlerDifferential.D R S)) :
    f = f' := by
  apply LinearMap.ext
  intro x
  have : x ‚àà Submodule.span S (Set.range <| KaehlerDifferential.D R S) := by
    rw [KaehlerDifferential.span_range_derivation]; trivial
  refine Submodule.span_induction ?_ ?_ ?_ ?_ this
  ¬∑ rintro _ ‚ü®x, rfl‚ü©; exact congr_arg (fun D : Derivation R S M => D x) hf
  ¬∑ rw [map_zero, map_zero]
  ¬∑ intro x y _ _ hx hy; rw [map_add, map_add, hx, hy]
  ¬∑ intro a x _ e; simp [e]

variable (R S)

theorem Derivation.liftKaehlerDifferential_D :
    (KaehlerDifferential.D R S).liftKaehlerDifferential = LinearMap.id :=
  Derivation.liftKaehlerDifferential_unique _ _
    (KaehlerDifferential.D R S).liftKaehlerDifferential_comp

variable {R S}

theorem KaehlerDifferential.D_tensorProductTo (x : KaehlerDifferential.ideal R S) :
    (KaehlerDifferential.D R S).tensorProductTo x =
      (KaehlerDifferential.ideal R S).toCotangent x := by
  rw [‚Üê Derivation.liftKaehlerDifferential_apply, Derivation.liftKaehlerDifferential_D]
  rfl

variable (R S)

theorem KaehlerDifferential.tensorProductTo_surjective :
    Function.Surjective (KaehlerDifferential.D R S).tensorProductTo := by
  intro x; obtain ‚ü®x, rfl‚ü© := (KaehlerDifferential.ideal R S).toCotangent_surjective x
  exact ‚ü®x, KaehlerDifferential.D_tensorProductTo x‚ü©

/-- The `S`-linear maps from `Œ©[S‚ÅÑR]` to `M` are (`S`-linearly) equivalent to `R`-derivations
from `S` to `M`. -/
@[simps! symm_apply apply_apply]
def KaehlerDifferential.linearMapEquivDerivation : (Œ©[S‚ÅÑR] ‚Üí‚Çó[S] M) ‚âÉ‚Çó[S] Derivation R S M :=
  { Derivation.llcomp.flip <| KaehlerDifferential.D R S with
    invFun := Derivation.liftKaehlerDifferential
    left_inv := fun _ =>
      Derivation.liftKaehlerDifferential_unique _ _ (Derivation.liftKaehlerDifferential_comp _)
    right_inv := Derivation.liftKaehlerDifferential_comp }

/-- The quotient ring of `S ‚äó S ‚ß∏ J ^ 2` by `Œ©[S‚ÅÑR]` is isomorphic to `S`. -/
def KaehlerDifferential.quotientCotangentIdealRingEquiv :
    (S ‚äó S ‚ß∏ KaehlerDifferential.ideal R S ^ 2) ‚ß∏ (KaehlerDifferential.ideal R S).cotangentIdeal ‚âÉ+*
      S := by
  have : Function.RightInverse (TensorProduct.includeLeft (R := R) (S := R) (A := S) (B := S))
      (‚Üë(TensorProduct.lmul' R : S ‚äó[R] S ‚Üí‚Çê[R] S) : S ‚äó[R] S ‚Üí+* S) := by
    intro x; rw [AlgHom.coe_toRingHom, ‚Üê AlgHom.comp_apply, TensorProduct.lmul'_comp_includeLeft]
    rfl
  refine (Ideal.quotCotangent _).trans ?_
  refine (Ideal.quotEquivOfEq ?_).trans (RingHom.quotientKerEquivOfRightInverse this)
  ext; rfl

/-- The quotient ring of `S ‚äó S ‚ß∏ J ^ 2` by `Œ©[S‚ÅÑR]` is isomorphic to `S` as an `S`-algebra. -/
def KaehlerDifferential.quotientCotangentIdeal :
    ((S ‚äó S ‚ß∏ KaehlerDifferential.ideal R S ^ 2) ‚ß∏
        (KaehlerDifferential.ideal R S).cotangentIdeal) ‚âÉ‚Çê[S] S :=
  { KaehlerDifferential.quotientCotangentIdealRingEquiv R S with
    commutes' := (KaehlerDifferential.quotientCotangentIdealRingEquiv R S).apply_symm_apply }

theorem KaehlerDifferential.End_equiv_aux (f : S ‚Üí‚Çê[R] S ‚äó S ‚ß∏ KaehlerDifferential.ideal R S ^ 2) :
    (Ideal.Quotient.mk‚Çê R (KaehlerDifferential.ideal R S).cotangentIdeal).comp f =
        IsScalarTower.toAlgHom R S _ ‚Üî
      (TensorProduct.lmul' R : S ‚äó[R] S ‚Üí‚Çê[R] S).kerSquareLift.comp f = AlgHom.id R S := by
  rw [AlgHom.ext_iff, AlgHom.ext_iff]
  apply forall_congr'
  intro x
  have e‚ÇÅ : (TensorProduct.lmul' R : S ‚äó[R] S ‚Üí‚Çê[R] S).kerSquareLift (f x) =
      KaehlerDifferential.quotientCotangentIdealRingEquiv R S
        (Ideal.Quotient.mk (KaehlerDifferential.ideal R S).cotangentIdeal <| f x) := by
    generalize f x = y; obtain ‚ü®y, rfl‚ü© := Ideal.Quotient.mk_surjective y; rfl
  have e‚ÇÇ :
    x = KaehlerDifferential.quotientCotangentIdealRingEquiv R S (IsScalarTower.toAlgHom R S _ x) :=
    (mul_one x).symm
  constructor
  ¬∑ intro e
    exact (e‚ÇÅ.trans (@RingEquiv.congr_arg _ _ _ _ _ _
      (KaehlerDifferential.quotientCotangentIdealRingEquiv R S) _ _ e)).trans e‚ÇÇ.symm
  ¬∑ intro e; apply (KaehlerDifferential.quotientCotangentIdealRingEquiv R S).injective
    exact e‚ÇÅ.symm.trans (e.trans e‚ÇÇ)

/- Note: Lean is slow to synthesize these instances (times out).
  Without them the endEquivDerivation' and endEquivAuxEquiv both have significant timeouts.
  In Mathlib 3, it was slow but not this slow. -/
/-- A shortcut instance to prevent timing out. Hopefully to be removed in the future. -/
local instance smul_SSmod_SSmod : SMul (S ‚äó[R] S ‚ß∏ KaehlerDifferential.ideal R S ^ 2)
    (S ‚äó[R] S ‚ß∏ KaehlerDifferential.ideal R S ^ 2) := Mul.toSMul _

/-- A shortcut instance to prevent timing out. Hopefully to be removed in the future. -/
@[nolint defLemma]
local instance isScalarTower_S_right :
    IsScalarTower S (S ‚äó[R] S ‚ß∏ KaehlerDifferential.ideal R S ^ 2)
      (S ‚äó[R] S ‚ß∏ KaehlerDifferential.ideal R S ^ 2) := Ideal.Quotient.isScalarTower_right

/-- A shortcut instance to prevent timing out. Hopefully to be removed in the future. -/
@[nolint defLemma]
local instance isScalarTower_R_right :
    IsScalarTower R (S ‚äó[R] S ‚ß∏ KaehlerDifferential.ideal R S ^ 2)
      (S ‚äó[R] S ‚ß∏ KaehlerDifferential.ideal R S ^ 2) := Ideal.Quotient.isScalarTower_right

/-- A shortcut instance to prevent timing out. Hopefully to be removed in the future. -/
@[nolint defLemma]
local instance isScalarTower_SS_right : IsScalarTower (S ‚äó[R] S)
    (S ‚äó[R] S ‚ß∏ KaehlerDifferential.ideal R S ^ 2) (S ‚äó[R] S ‚ß∏ KaehlerDifferential.ideal R S ^ 2) :=
  Ideal.Quotient.isScalarTower_right

/-- A shortcut instance to prevent timing out. Hopefully to be removed in the future. -/
local instance instS : Module S (KaehlerDifferential.ideal R S).cotangentIdeal :=
  Submodule.module' _

/-- A shortcut instance to prevent timing out. Hopefully to be removed in the future. -/
local instance instR : Module R (KaehlerDifferential.ideal R S).cotangentIdeal :=
  Submodule.module' _

/-- A shortcut instance to prevent timing out. Hopefully to be removed in the future. -/
local instance instSS : Module (S ‚äó[R] S) (KaehlerDifferential.ideal R S).cotangentIdeal :=
  Submodule.module' _

/-- Derivations into `Œ©[S‚ÅÑR]` is equivalent to derivations
into `(KaehlerDifferential.ideal R S).cotangentIdeal`. -/
noncomputable def KaehlerDifferential.endEquivDerivation' :
    Derivation R S Œ©[S‚ÅÑR] ‚âÉ‚Çó[R] Derivation R S (ideal R S).cotangentIdeal :=
  LinearEquiv.compDer ((KaehlerDifferential.ideal R S).cotangentEquivIdeal.restrictScalars S)

/-- (Implementation) An `Equiv` version of `KaehlerDifferential.End_equiv_aux`.
Used in `KaehlerDifferential.endEquiv`. -/
def KaehlerDifferential.endEquivAuxEquiv :
    { f //
        (Ideal.Quotient.mk‚Çê R (KaehlerDifferential.ideal R S).cotangentIdeal).comp f =
          IsScalarTower.toAlgHom R S _ } ‚âÉ
      { f // (TensorProduct.lmul' R : S ‚äó[R] S ‚Üí‚Çê[R] S).kerSquareLift.comp f = AlgHom.id R S } :=
  (Equiv.refl _).subtypeEquiv (KaehlerDifferential.End_equiv_aux R S)

/--
The endomorphisms of `Œ©[S‚ÅÑR]` corresponds to sections of the surjection `S ‚äó[R] S ‚ß∏ J ^ 2 ‚Üí‚Çê[R] S`,
with `J` being the kernel of the multiplication map `S ‚äó[R] S ‚Üí‚Çê[R] S`.
-/
noncomputable def KaehlerDifferential.endEquiv :
    Module.End S Œ©[S‚ÅÑR] ‚âÉ
      { f // (TensorProduct.lmul' R : S ‚äó[R] S ‚Üí‚Çê[R] S).kerSquareLift.comp f = AlgHom.id R S } :=
  (KaehlerDifferential.linearMapEquivDerivation R S).toEquiv.trans <|
    (KaehlerDifferential.endEquivDerivation' R S).toEquiv.trans <|
      (derivationToSquareZeroEquivLift (KaehlerDifferential.ideal R S).cotangentIdeal
            (KaehlerDifferential.ideal R S).cotangentIdeal_square).trans <|
        KaehlerDifferential.endEquivAuxEquiv R S

section Finiteness

theorem KaehlerDifferential.ideal_fg [EssFiniteType R S] :
    (KaehlerDifferential.ideal R S).FG := by
  classical
  use (EssFiniteType.finset R S).image (fun s ‚Ü¶ (1 : S) ‚äó‚Çú[R] s - s ‚äó‚Çú[R] (1 : S))
  apply le_antisymm
  ¬∑ rw [Finset.coe_image, Ideal.span_le]
    rintro _ ‚ü®x, _, rfl‚ü©
    exact KaehlerDifferential.one_smul_sub_smul_one_mem_ideal R x
  ¬∑ rw [‚Üê KaehlerDifferential.span_range_eq_ideal, Ideal.span_le]
    rintro _ ‚ü®x, rfl‚ü©
    let I : Ideal (S ‚äó[R] S) := Ideal.span
      ((EssFiniteType.finset R S).image (fun s ‚Ü¶ (1 : S) ‚äó‚Çú[R] s - s ‚äó‚Çú[R] (1 : S)))
    change _ - _ ‚àà I
    have : (IsScalarTower.toAlgHom R (S ‚äó[R] S) (S ‚äó[R] S ‚ß∏ I)).comp TensorProduct.includeRight =
        (IsScalarTower.toAlgHom R (S ‚äó[R] S) (S ‚äó[R] S ‚ß∏ I)).comp TensorProduct.includeLeft := by
      apply EssFiniteType.algHom_ext
      intro a ha
      simp only [AlgHom.coe_comp, IsScalarTower.coe_toAlgHom', Ideal.Quotient.algebraMap_eq,
        Function.comp_apply, TensorProduct.includeLeft_apply, TensorProduct.includeRight_apply,
        Ideal.Quotient.mk_eq_mk_iff_sub_mem]
      refine Ideal.subset_span ?_
      simp only [Finset.coe_image, Set.mem_image, Finset.mem_coe]
      exact ‚ü®a, ha, rfl‚ü©
    simpa [Ideal.Quotient.mk_eq_mk_iff_sub_mem] using AlgHom.congr_fun this x

instance KaehlerDifferential.finite [EssFiniteType R S] :
    Module.Finite S Œ©[S‚ÅÑR] := by
  classical
  let s := (EssFiniteType.finset R S).image (fun s ‚Ü¶ D R S s)
  refine ‚ü®‚ü®s, top_le_iff.mp ?_‚ü©‚ü©
  rw [‚Üê span_range_derivation, Submodule.span_le]
  rintro _ ‚ü®x, rfl‚ü©
  have : ‚àÄ x ‚àà adjoin R (EssFiniteType.finset R S).toSet,
      .D _ _ x ‚àà Submodule.span S s.toSet := by
    intro x hx
    refine adjoin_induction ?_ ?_ ?_ ?_ hx
    ¬∑ exact fun x hx ‚Ü¶ Submodule.subset_span (Finset.mem_image_of_mem _ hx)
    ¬∑ simp
    ¬∑ exact fun x y _ _ hx hy ‚Ü¶ (D R S).map_add x y ‚ñ∏ add_mem hx hy
    ¬∑ intro x y _ _ hx hy
      simp only [Derivation.leibniz]
      exact add_mem (Submodule.smul_mem _ _ hy) (Submodule.smul_mem _ _ hx)
  obtain ‚ü®t, ht, ht', hxt‚ü© := (essFiniteType_cond_iff R S (EssFiniteType.finset R S)).mp
    EssFiniteType.cond.choose_spec x
  rw [show D R S x =
    ht'.unit‚Åª¬π ‚Ä¢ (D R S (x * t) - x ‚Ä¢ D R S t) by simp [smul_smul, Units.smul_def]]
  exact Submodule.smul_mem _ _ (sub_mem (this _ hxt) (Submodule.smul_mem _ _ (this _ ht)))

end Finiteness

section Presentation

open KaehlerDifferential (D)

open Finsupp (single)

/-- The `S`-submodule of `S ‚Üí‚ÇÄ S` (the direct sum of copies of `S` indexed by `S`) generated by
the relations:
1. `dx + dy = d(x + y)`
2. `x dy + y dx = d(x * y)`
3. `dr = 0` for `r ‚àà R`
where `db` is the unit in the copy of `S` with index `b`.

This is the kernel of the surjection
`Finsupp.linearCombination S Œ©[S‚ÅÑR] S (KaehlerDifferential.D R S)`.
See `KaehlerDifferential.kerTotal_eq` and `KaehlerDifferential.linearCombination_surjective`.
-/
noncomputable def KaehlerDifferential.kerTotal : Submodule S (S ‚Üí‚ÇÄ S) :=
  Submodule.span S
    (((Set.range fun x : S √ó S => single x.1 1 + single x.2 1 - single (x.1 + x.2) 1) ‚à™
        Set.range fun x : S √ó S => single x.2 x.1 + single x.1 x.2 - single (x.1 * x.2) 1) ‚à™
      Set.range fun x : R => single (algebraMap R S x) 1)

unsuppress_compilation in
-- Porting note: was `local notation x "ùñ£" y => (KaehlerDifferential.kerTotal R S).mkQ (single y x)`
-- but not having `DFunLike.coe` leads to `kerTotal_mkQ_single_smul` failing.
local notation3 x "ùñ£" y => DFunLike.coe (KaehlerDifferential.kerTotal R S).mkQ (single y x)

theorem KaehlerDifferential.kerTotal_mkQ_single_add (x y z) : (zùñ£x + y) = (zùñ£x) + zùñ£y := by
  rw [‚Üê map_add, eq_comm, ‚Üê sub_eq_zero, ‚Üê map_sub (Submodule.mkQ (kerTotal R S)),
    Submodule.mkQ_apply, Submodule.Quotient.mk_eq_zero]
  simp_rw [‚Üê Finsupp.smul_single_one _ z, ‚Üê smul_add, ‚Üê smul_sub]
  exact Submodule.smul_mem _ _ (Submodule.subset_span (Or.inl <| Or.inl <| ‚ü®‚ü®_, _‚ü©, rfl‚ü©))

theorem KaehlerDifferential.kerTotal_mkQ_single_mul (x y z) :
    (zùñ£x * y) = ((z * x)ùñ£y) + (z * y)ùñ£x := by
  rw [‚Üê map_add, eq_comm, ‚Üê sub_eq_zero, ‚Üê map_sub (Submodule.mkQ (kerTotal R S)),
    Submodule.mkQ_apply, Submodule.Quotient.mk_eq_zero]
  simp_rw [‚Üê Finsupp.smul_single_one _ z, ‚Üê @smul_eq_mul _ _ z, ‚Üê Finsupp.smul_single, ‚Üê smul_add,
    ‚Üê smul_sub]
  exact Submodule.smul_mem _ _ (Submodule.subset_span (Or.inl <| Or.inr <| ‚ü®‚ü®_, _‚ü©, rfl‚ü©))

theorem KaehlerDifferential.kerTotal_mkQ_single_algebraMap (x y) : (yùñ£algebraMap R S x) = 0 := by
  rw [Submodule.mkQ_apply, Submodule.Quotient.mk_eq_zero, ‚Üê Finsupp.smul_single_one _ y]
  exact Submodule.smul_mem _ _ (Submodule.subset_span (Or.inr <| ‚ü®_, rfl‚ü©))

theorem KaehlerDifferential.kerTotal_mkQ_single_algebraMap_one (x) : (xùñ£1) = 0 := by
  rw [‚Üê (algebraMap R S).map_one, KaehlerDifferential.kerTotal_mkQ_single_algebraMap]

theorem KaehlerDifferential.kerTotal_mkQ_single_smul (r : R) (x y) : (yùñ£r ‚Ä¢ x) = r ‚Ä¢ yùñ£x := by
  letI : SMulZeroClass R S := inferInstance
  rw [Algebra.smul_def, KaehlerDifferential.kerTotal_mkQ_single_mul,
    KaehlerDifferential.kerTotal_mkQ_single_algebraMap, add_zero, ‚Üê LinearMap.map_smul_of_tower,
    Finsupp.smul_single, mul_comm, Algebra.smul_def]

/-- The (universal) derivation into `(S ‚Üí‚ÇÄ S) ‚ß∏ KaehlerDifferential.kerTotal R S`. -/
noncomputable def KaehlerDifferential.derivationQuotKerTotal :
    Derivation R S ((S ‚Üí‚ÇÄ S) ‚ß∏ KaehlerDifferential.kerTotal R S) where
  toFun x := 1ùñ£x
  map_add' _ _ := KaehlerDifferential.kerTotal_mkQ_single_add _ _ _ _ _
  map_smul' _ _ := KaehlerDifferential.kerTotal_mkQ_single_smul _ _ _ _ _
  map_one_eq_zero' := KaehlerDifferential.kerTotal_mkQ_single_algebraMap_one _ _ _
  leibniz' a b :=
    (KaehlerDifferential.kerTotal_mkQ_single_mul _ _ _ _ _).trans
      (by simp_rw [‚Üê Finsupp.smul_single_one _ (1 * _ : S)]; dsimp; simp)

theorem KaehlerDifferential.derivationQuotKerTotal_apply (x) :
    KaehlerDifferential.derivationQuotKerTotal R S x = 1ùñ£x :=
  rfl

theorem KaehlerDifferential.derivationQuotKerTotal_lift_comp_linearCombination :
    (KaehlerDifferential.derivationQuotKerTotal R S).liftKaehlerDifferential.comp
        (Finsupp.linearCombination S (KaehlerDifferential.D R S)) =
      Submodule.mkQ _ := by
  apply Finsupp.lhom_ext
  intro a b
  conv_rhs => rw [‚Üê Finsupp.smul_single_one a b, LinearMap.map_smul]
  simp [KaehlerDifferential.derivationQuotKerTotal_apply]

theorem KaehlerDifferential.kerTotal_eq :
    LinearMap.ker (Finsupp.linearCombination S (KaehlerDifferential.D R S)) =
      KaehlerDifferential.kerTotal R S := by
  apply le_antisymm
  ¬∑ conv_rhs => rw [‚Üê (KaehlerDifferential.kerTotal R S).ker_mkQ]
    rw [‚Üê KaehlerDifferential.derivationQuotKerTotal_lift_comp_linearCombination]
    exact LinearMap.ker_le_ker_comp _ _
  ¬∑ rw [KaehlerDifferential.kerTotal, Submodule.span_le]
    rintro _ ((‚ü®‚ü®x, y‚ü©, rfl‚ü© | ‚ü®‚ü®x, y‚ü©, rfl‚ü©) | ‚ü®x, rfl‚ü©) <;> simp [LinearMap.mem_ker]

theorem KaehlerDifferential.linearCombination_surjective :
    Function.Surjective (Finsupp.linearCombination S (KaehlerDifferential.D R S)) := by
  rw [‚Üê LinearMap.range_eq_top, range_linearCombination, span_range_derivation]

/-- `Œ©[S‚ÅÑR]` is isomorphic to `S` copies of `S` with kernel `KaehlerDifferential.kerTotal`. -/
@[simps!]
noncomputable def KaehlerDifferential.quotKerTotalEquiv :
    ((S ‚Üí‚ÇÄ S) ‚ß∏ KaehlerDifferential.kerTotal R S) ‚âÉ‚Çó[S] Œ©[S‚ÅÑR] :=
  { (KaehlerDifferential.kerTotal R S).liftQ
      (Finsupp.linearCombination S (KaehlerDifferential.D R S))
      (KaehlerDifferential.kerTotal_eq R S).ge with
    invFun := (KaehlerDifferential.derivationQuotKerTotal R S).liftKaehlerDifferential
    left_inv := by
      intro x
      obtain ‚ü®x, rfl‚ü© := Submodule.mkQ_surjective _ x
      exact
        LinearMap.congr_fun
          (KaehlerDifferential.derivationQuotKerTotal_lift_comp_linearCombination R S :) x
    right_inv := by
      intro x
      obtain ‚ü®x, rfl‚ü© := KaehlerDifferential.linearCombination_surjective R S x
      have := LinearMap.congr_fun
        (KaehlerDifferential.derivationQuotKerTotal_lift_comp_linearCombination R S) x
      rw [LinearMap.comp_apply] at this
      rw [this]
      rfl }

theorem KaehlerDifferential.quotKerTotalEquiv_symm_comp_D :
    (KaehlerDifferential.quotKerTotalEquiv R S).symm.toLinearMap.compDer
        (KaehlerDifferential.D R S) =
      KaehlerDifferential.derivationQuotKerTotal R S := by
  convert (KaehlerDifferential.derivationQuotKerTotal R S).liftKaehlerDifferential_comp

end Presentation

section ExactSequence

/- We have the commutative diagram
```
A --‚Üí B
‚Üë     ‚Üë
|     |
R --‚Üí S
```
-/
variable (A B : Type*) [CommRing A] [CommRing B] [Algebra R A]
variable [Algebra A B] [Algebra S B]

unsuppress_compilation in
-- The map `(A ‚Üí‚ÇÄ A) ‚Üí‚Çó[A] (B ‚Üí‚ÇÄ B)`
local macro "finsupp_map" : term =>
  `((Finsupp.mapRange.linearMap (Algebra.linearMap A B)).comp
    (Finsupp.lmapDomain A A (algebraMap A B)))

/--
Given the commutative diagram
```
A --‚Üí B
‚Üë     ‚Üë
|     |
R --‚Üí S
```
The kernel of the presentation `‚äï‚Çì B dx ‚Ü† Œ©_{B/S}` is spanned by the image of the
kernel of `‚äï‚Çì A dx ‚Ü† Œ©_{A/R}` and all `ds` with `s : S`.
See `kerTotal_map'` for the special case where `R = S`.
-/
theorem KaehlerDifferential.kerTotal_map [Algebra R B] [IsScalarTower R A B] [IsScalarTower R S B]
    (h : Function.Surjective (algebraMap A B)) :
    (KaehlerDifferential.kerTotal R A).map finsupp_map ‚äî
        Submodule.span A (Set.range fun x : S => .single (algebraMap S B x) (1 : B)) =
      (KaehlerDifferential.kerTotal S B).restrictScalars _ := by
  rw [KaehlerDifferential.kerTotal, Submodule.map_span, KaehlerDifferential.kerTotal,
    Submodule.restrictScalars_span _ _ h]
  simp_rw [Set.image_union, Submodule.span_union, ‚Üê Set.image_univ, Set.image_image, Set.image_univ,
    map_sub, map_add]
  simp only [LinearMap.comp_apply, Finsupp.lmapDomain_apply, Finsupp.mapDomain_single,
    Finsupp.mapRange.linearMap_apply, Finsupp.mapRange_single, Algebra.linearMap_apply,
    map_one, map_add, map_mul]
  simp_rw [sup_assoc, ‚Üê (h.prodMap h).range_comp]
  congr!
  -- Porting note: new
  simp_rw [‚Üê IsScalarTower.algebraMap_apply R A B]
  rw [sup_eq_right]
  apply Submodule.span_mono
  simp_rw [IsScalarTower.algebraMap_apply R S B]
  exact Set.range_comp_subset_range (algebraMap R S)
    fun x => Finsupp.single (algebraMap S B x) (1 : B)

/--
This is a special case of `kerTotal_map` where `R = S`.
The kernel of the presentation `‚äï‚Çì B dx ‚Ü† Œ©_{B/R}` is spanned by the image of the
kernel of `‚äï‚Çì A dx ‚Ü† Œ©_{A/R}` and all `da` with `a : A`.
-/
theorem KaehlerDifferential.kerTotal_map' [Algebra R B]
    [IsScalarTower R A B] (h : Function.Surjective (algebraMap A B)) :
    (KaehlerDifferential.kerTotal R A ‚äî
      Submodule.span A (Set.range fun x ‚Ü¶ .single (algebraMap R A x) 1)).map finsupp_map =
      (KaehlerDifferential.kerTotal R B).restrictScalars _ := by
  rw [Submodule.map_sup, ‚Üê kerTotal_map R R A B h, Submodule.map_span, ‚Üê Set.range_comp]
  congr
  refine congr_arg Set.range ?_
  ext; simp [IsScalarTower.algebraMap_eq R A B]

section
variable [Algebra R B] [IsScalarTower R A B] [IsScalarTower R S B] [SMulCommClass S A B]

/-- The map `Œ©[A‚ÅÑR] ‚Üí‚Çó[A] Œ©[B‚ÅÑS]` given a square
```
A --‚Üí B
‚Üë     ‚Üë
|     |
R --‚Üí S
```
-/
def KaehlerDifferential.map : Œ©[A‚ÅÑR] ‚Üí‚Çó[A] Œ©[B‚ÅÑS] :=
  Derivation.liftKaehlerDifferential
    (((KaehlerDifferential.D S B).restrictScalars R).compAlgebraMap A)

theorem KaehlerDifferential.map_compDer :
    (KaehlerDifferential.map R S A B).compDer (KaehlerDifferential.D R A) =
      ((KaehlerDifferential.D S B).restrictScalars R).compAlgebraMap A :=
  Derivation.liftKaehlerDifferential_comp _

@[simp]
theorem KaehlerDifferential.map_D (x : A) :
    KaehlerDifferential.map R S A B (KaehlerDifferential.D R A x) =
      KaehlerDifferential.D S B (algebraMap A B x) :=
  Derivation.congr_fun (KaehlerDifferential.map_compDer R S A B) x

theorem KaehlerDifferential.ker_map :
    LinearMap.ker (KaehlerDifferential.map R S A B) =
      (((kerTotal S B).restrictScalars A).comap finsupp_map).map
        (Finsupp.linearCombination (M := Œ©[A‚ÅÑR]) A (D R A)) := by
  rw [‚Üê Submodule.map_comap_eq_of_surjective (linearCombination_surjective R A) (LinearMap.ker _)]
  congr 1
  ext x
  simp only [Submodule.mem_comap, LinearMap.mem_ker, Finsupp.apply_linearCombination, ‚Üê kerTotal_eq,
    Submodule.restrictScalars_mem]
  simp only [linearCombination_apply, Function.comp_apply, LinearMap.coe_comp, lmapDomain_apply,
    Finsupp.mapRange.linearMap_apply]
  rw [Finsupp.sum_mapRange_index, Finsupp.sum_mapDomain_index]
  ¬∑ simp
  ¬∑ simp
  ¬∑ simp [add_smul]
  ¬∑ simp

lemma KaehlerDifferential.ker_map_of_surjective (h : Function.Surjective (algebraMap A B)) :
    LinearMap.ker (map R R A B) =
      (LinearMap.ker finsupp_map).map (Finsupp.linearCombination A (D R A)) := by
  rw [ker_map, ‚Üê kerTotal_map' R A B h, Submodule.comap_map_eq, Submodule.map_sup,
    Submodule.map_sup, ‚Üê kerTotal_eq, ‚Üê Submodule.comap_bot,
    Submodule.map_comap_eq_of_surjective (linearCombination_surjective _ _),
    bot_sup_eq, Submodule.map_span, ‚Üê Set.range_comp]
  convert bot_sup_eq _
  rw [Submodule.span_eq_bot]; simp

open IsScalarTower (toAlgHom)

theorem KaehlerDifferential.map_surjective_of_surjective
    (h : Function.Surjective (algebraMap A B)) :
    Function.Surjective (KaehlerDifferential.map R S A B) := by
  rw [‚Üê LinearMap.range_eq_top, _root_.eq_top_iff,
    ‚Üê @Submodule.restrictScalars_top A B, ‚Üê span_range_derivation,
    Submodule.restrictScalars_span _ _ h, Submodule.span_le]
  rintro _ ‚ü®x, rfl‚ü©
  obtain ‚ü®y, rfl‚ü© := h x
  rw [‚Üê KaehlerDifferential.map_D R S A B]
  exact ‚ü®_, rfl‚ü©

theorem KaehlerDifferential.map_surjective :
    Function.Surjective (KaehlerDifferential.map R S B B) :=
  map_surjective_of_surjective R S B B Function.surjective_id

/-- The lift of the map `Œ©[A‚ÅÑR] ‚Üí‚Çó[A] Œ©[B‚ÅÑR]` to the base change along `A ‚Üí B`.
This is the first map in the exact sequence `B ‚äó[A] Œ©[A‚ÅÑR] ‚Üí Œ©[B‚ÅÑR] ‚Üí Œ©[B‚ÅÑA] ‚Üí 0`. -/
noncomputable def KaehlerDifferential.mapBaseChange : B ‚äó[A] Œ©[A‚ÅÑR] ‚Üí‚Çó[B] Œ©[B‚ÅÑR] :=
  (TensorProduct.isBaseChange A Œ©[A‚ÅÑR] B).lift (KaehlerDifferential.map R R A B)

@[simp]
theorem KaehlerDifferential.mapBaseChange_tmul (x : B) (y : Œ©[A‚ÅÑR]) :
    KaehlerDifferential.mapBaseChange R A B (x ‚äó‚Çú y) = x ‚Ä¢ KaehlerDifferential.map R R A B y := by
  conv_lhs => rw [‚Üê mul_one x, ‚Üê smul_eq_mul, ‚Üê TensorProduct.smul_tmul', LinearMap.map_smul]
  congr 1
  exact IsBaseChange.lift_eq _ _ _

lemma KaehlerDifferential.range_mapBaseChange :
    LinearMap.range (mapBaseChange R A B) = LinearMap.ker (map R A B B) := by
  apply le_antisymm
  ¬∑ rintro _ ‚ü®x, rfl‚ü©
    induction' x with r s
    ¬∑ simp
    ¬∑ obtain ‚ü®x, rfl‚ü© := linearCombination_surjective _ _ s
      simp only [mapBaseChange_tmul, LinearMap.mem_ker, map_smul]
      induction x using Finsupp.induction_linear
      ¬∑ simp
      ¬∑ simp [smul_add, *]
      ¬∑ simp
    ¬∑ rw [map_add]; exact add_mem ‚Äπ_‚Ä∫ ‚Äπ_‚Ä∫
  ¬∑ convert_to (kerTotal A B).map (Finsupp.linearCombination B (D R B)) ‚â§ _
    ¬∑ rw [KaehlerDifferential.ker_map]
      congr 1
      convert Submodule.comap_id _
      ¬∑ ext; simp
    rw [Submodule.map_le_iff_le_comap, kerTotal, Submodule.span_le]
    rintro f ((‚ü®‚ü®x, y‚ü©, rfl‚ü©|‚ü®‚ü®x, y‚ü©, rfl‚ü©)|‚ü®x, rfl‚ü©)
    ¬∑ use 0; simp
    ¬∑ use 0; simp
    ¬∑ use 1 ‚äó‚Çú D _ _ x; simp

/-- The sequence `B ‚äó[A] Œ©[A‚ÅÑR] ‚Üí Œ©[B‚ÅÑR] ‚Üí Œ©[B‚ÅÑA] ‚Üí 0` is exact.
Also see `KaehlerDifferential.map_surjective`. -/
lemma KaehlerDifferential.exact_mapBaseChange_map :
    Function.Exact (mapBaseChange R A B) (map R A B B) :=
  SetLike.ext_iff.mp (range_mapBaseChange R A B).symm

end

/-- The map `I ‚Üí B ‚äó[A] Œ©[A‚ÅÑR]` where `I = ker(A ‚Üí B)`. -/
@[simps]
noncomputable
def KaehlerDifferential.kerToTensor :
    RingHom.ker (algebraMap A B) ‚Üí‚Çó[A] B ‚äó[A] Œ©[A‚ÅÑR] where
  toFun x := 1 ‚äó‚Çú D R A x
  map_add' x y := by simp only [Submodule.coe_add, map_add, TensorProduct.tmul_add]
  map_smul' r x := by simp only [SetLike.val_smul, smul_eq_mul, Derivation.leibniz,
    TensorProduct.tmul_add, TensorProduct.tmul_smul, TensorProduct.smul_tmul', ‚Üê
    algebraMap_eq_smul_one, RingHom.mem_ker.mp x.prop, TensorProduct.zero_tmul, add_zero,
    RingHom.id_apply]

/-- The map `I/I¬≤ ‚Üí B ‚äó[A] Œ©[A‚ÅÑR]` where `I = ker(A ‚Üí B)`. -/
noncomputable
def KaehlerDifferential.kerCotangentToTensor :
    (RingHom.ker (algebraMap A B)).Cotangent ‚Üí‚Çó[A] B ‚äó[A] Œ©[A‚ÅÑR] :=
  Submodule.liftQ _ (kerToTensor R A B) <| by
    rw [Submodule.smul_eq_map‚ÇÇ]
    apply iSup_le_iff.mpr
    simp only [Submodule.map_le_iff_le_comap, Subtype.forall]
    rintro x hx y -
    simp only [Submodule.mem_comap, LinearMap.lsmul_apply, LinearMap.mem_ker, map_smul,
      kerToTensor_apply, TensorProduct.smul_tmul', ‚Üê algebraMap_eq_smul_one,
      RingHom.mem_ker.mp hx, TensorProduct.zero_tmul]

@[simp]
lemma KaehlerDifferential.kerCotangentToTensor_toCotangent (x) :
    kerCotangentToTensor R A B (Ideal.toCotangent _ x) = 1 ‚äó‚Çú D _ _ x.1 := rfl

variable [Algebra R B] [IsScalarTower R A B]

theorem KaehlerDifferential.range_kerCotangentToTensor
    (h : Function.Surjective (algebraMap A B)) :
    LinearMap.range (kerCotangentToTensor R A B) =
      (LinearMap.ker (KaehlerDifferential.mapBaseChange R A B)).restrictScalars A := by
  classical
  ext x
  constructor
  ¬∑ rintro ‚ü®x, rfl‚ü©
    obtain ‚ü®x, rfl‚ü© := Ideal.toCotangent_surjective _ x
    simp only [kerCotangentToTensor_toCotangent, Submodule.restrictScalars_mem, LinearMap.mem_ker,
      mapBaseChange_tmul, map_D, RingHom.mem_ker.mp x.2, map_zero, smul_zero]
  ¬∑ intro hx
    obtain ‚ü®x, rfl‚ü© := LinearMap.rTensor_surjective Œ©[A‚ÅÑR] (g := Algebra.linearMap A B) h x
    obtain ‚ü®x, rfl‚ü© := (TensorProduct.lid _ _).symm.surjective x
    replace hx : x ‚àà LinearMap.ker (KaehlerDifferential.map R R A B) := by simpa using hx
    rw [KaehlerDifferential.ker_map_of_surjective R A B h] at hx
    obtain ‚ü®x, hx, rfl‚ü© := hx
    simp only [TensorProduct.lid_symm_apply, LinearMap.rTensor_tmul,
      Algebra.linearMap_apply, map_one]
    rw [‚Üê Finsupp.sum_single x, Finsupp.sum, ‚Üê Finset.sum_fiberwise_of_maps_to
      (fun _ ‚Ü¶ Finset.mem_image_of_mem (algebraMap A B))]
    simp only [map_sum (s := x.support.image (algebraMap A B)),
      TensorProduct.tmul_sum]
    apply sum_mem
    intro c _
    simp only [LinearMap.mem_range]
    simp only [map_sum, Finsupp.linearCombination_single]
    have : ‚àë i ‚àà x.support with algebraMap A B i = c, x i ‚àà RingHom.ker (algebraMap A B) := by
      simpa [Finsupp.mapDomain, Finsupp.sum, Finsupp.finset_sum_apply, RingHom.mem_ker,
        Finsupp.single_apply, ‚Üê Finset.sum_filter] using DFunLike.congr_fun hx c
    obtain ‚ü®a, ha‚ü© := h c
    use ‚àë i ‚àà {i ‚àà x.support | algebraMap A B i = c}.attach, x i ‚Ä¢ Ideal.toCotangent _ ‚ü®i - a, ?_‚ü©
    ¬∑ simp only [map_sum, LinearMapClass.map_smul, kerCotangentToTensor_toCotangent, map_sub]
      simp_rw [‚Üê TensorProduct.tmul_smul]
      -- TODO: was `simp [kerCotangentToTensor_toCotangent, RingHom.mem_ker.mp x.2]` and very slow
      -- (https://github.com/leanprover-community/mathlib4/issues/19751)
      simp only [smul_sub, TensorProduct.tmul_sub, Finset.sum_sub_distrib, ‚Üê TensorProduct.tmul_sum,
        ‚Üê Finset.sum_smul, Finset.sum_attach, sub_eq_self,
        Finset.sum_attach (f := fun i ‚Ü¶ x i ‚Ä¢ KaehlerDifferential.D R A i)]
      rw [‚Üê TensorProduct.smul_tmul, ‚Üê Algebra.algebraMap_eq_smul_one, RingHom.mem_ker.mp this,
        TensorProduct.zero_tmul]
    ¬∑ have : x i ‚â† 0 ‚àß algebraMap A B i = c := by
        convert i.prop
        simp_rw [Finset.mem_filter, Finsupp.mem_support_iff]
      simp [RingHom.mem_ker, ha, this.2]

theorem KaehlerDifferential.exact_kerCotangentToTensor_mapBaseChange
    (h : Function.Surjective (algebraMap A B)) :
    Function.Exact (kerCotangentToTensor R A B) (KaehlerDifferential.mapBaseChange R A B) :=
  SetLike.ext_iff.mp (range_kerCotangentToTensor R A B h).symm

lemma KaehlerDifferential.mapBaseChange_surjective
    (h : Function.Surjective (algebraMap A B)) :
    Function.Surjective (KaehlerDifferential.mapBaseChange R A B) := by
  have := subsingleton_of_surjective A B h
  rw [‚Üê LinearMap.range_eq_top, range_mapBaseChange, ‚Üê top_le_iff]
  exact fun x _ ‚Ü¶ Subsingleton.elim _ _

end ExactSequence

end KaehlerDifferential
