/-
Copyright (c) 2024 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.RingTheory.FiniteType
import Mathlib.RingTheory.Nakayama
import Mathlib.Algebra.Module.FinitePresentation
import Mathlib.LinearAlgebra.FiniteDimensional
import Mathlib.LinearAlgebra.TensorProduct.RightExactness
import Mathlib.RingTheory.Flat.Basic
import Mathlib.RingTheory.LocalRing.ResidueField.Basic

/-!
# Finite modules over local rings

This file gathers various results about finite modules over a local ring `(R, ğ”ª, k)`.

## Main results
- `LocalRing.subsingleton_tensorProduct`: If `M` is finitely generated, `k âŠ— M = 0 â†” M = 0`.
- `Module.free_of_maximalIdeal_rTensor_injective`:
  If `M` is a finitely presented module such that `m âŠ— M â†’ M` is injective
  (for example when `M` is flat), then `M` is free.
- `Module.free_of_lTensor_residueField_injective`: If `N â†’ M â†’ P â†’ 0` is a presentation of `P` with
  `N` finite and `M` finite free, then injectivity of `k âŠ— N â†’ k âŠ— M` implies that `P` is free.
- `LocalRing.split_injective_iff_lTensor_residueField_injective`:
  Given an `R`-linear map `l : M â†’ N` with `M` finite and `N` finite free,
  `l` is a split injection if and only if `k âŠ— l` is a (split) injection.
-/

variable {R S} [CommRing R] [CommRing S] [Algebra R S]

section

variable {M N} [AddCommGroup M] [AddCommGroup N] [Module R M] [Module R N]

open Function (Injective Surjective Exact)
open LocalRing TensorProduct

local notation "k" => ResidueField R
local notation "ğ”ª" => maximalIdeal R

variable {P} [AddCommGroup P] [Module R P] (f : M â†’â‚—[R] N) (g : N â†’â‚—[R] P)

namespace LocalRing

variable [LocalRing R]

theorem map_mkQ_eq {Nâ‚ Nâ‚‚ : Submodule R M} (h : Nâ‚ â‰¤ Nâ‚‚) (h' : Nâ‚‚.FG) :
    Nâ‚.map (Submodule.mkQ (ğ”ª â€¢ Nâ‚‚)) = Nâ‚‚.map (Submodule.mkQ (ğ”ª â€¢ Nâ‚‚)) â†” Nâ‚ = Nâ‚‚ := by
  constructor
  Â· intro hN
    have : Nâ‚‚ â‰¤ ğ”ª â€¢ Nâ‚‚ âŠ” Nâ‚ := by
      simpa using Submodule.comap_mono (f := Submodule.mkQ (ğ”ª â€¢ Nâ‚‚)) hN.ge
    rw [sup_comm] at this
    exact h.antisymm (Submodule.le_of_le_smul_of_le_jacobson_bot h'
      (by rw [jacobson_eq_maximalIdeal]; exact bot_ne_top) this)
  Â· rintro rfl; simp

theorem map_mkQ_eq_top {N : Submodule R M} [Module.Finite R M] :
    N.map (Submodule.mkQ (ğ”ª â€¢ âŠ¤)) = âŠ¤ â†” N = âŠ¤ := by
  rw [â† map_mkQ_eq (Nâ‚ := N) le_top Module.Finite.out, Submodule.map_top, Submodule.range_mkQ]

theorem map_tensorProduct_mk_eq_top {N : Submodule R M} [Module.Finite R M] :
    N.map (TensorProduct.mk R k M 1) = âŠ¤ â†” N = âŠ¤ := by
  constructor
  Â· intro hN
    letI : Module k (M â§¸ (ğ”ª â€¢ âŠ¤ : Submodule R M)) :=
      inferInstanceAs (Module (R â§¸ ğ”ª) (M â§¸ ğ”ª â€¢ (âŠ¤ : Submodule R M)))
    letI : IsScalarTower R k (M â§¸ (ğ”ª â€¢ âŠ¤ : Submodule R M)) :=
      inferInstanceAs (IsScalarTower R (R â§¸ ğ”ª) (M â§¸ ğ”ª â€¢ (âŠ¤ : Submodule R M)))
    let f := AlgebraTensorModule.lift (((LinearMap.ringLmapEquivSelf k k _).symm
      (Submodule.mkQ (ğ”ª â€¢ âŠ¤ : Submodule R M))).restrictScalars R)
    have : f.comp (TensorProduct.mk R k M 1) = Submodule.mkQ (ğ”ª â€¢ âŠ¤) := by ext; simp [f]
    have hf : Function.Surjective f := by
      intro x; obtain âŸ¨x, rflâŸ© := Submodule.mkQ_surjective _ x
      rw [â† this, LinearMap.comp_apply]; exact âŸ¨_, rflâŸ©
    apply_fun Submodule.map f at hN
    rwa [â† Submodule.map_comp, this, Submodule.map_top, LinearMap.range_eq_top.mpr hf,
      map_mkQ_eq_top] at hN
  Â· rintro rfl; rw [Submodule.map_top, LinearMap.range_eq_top]
    exact TensorProduct.mk_surjective R M k Ideal.Quotient.mk_surjective

theorem subsingleton_tensorProduct [Module.Finite R M] :
    Subsingleton (k âŠ—[R] M) â†” Subsingleton M := by
  rw [â† Submodule.subsingleton_iff R, â† subsingleton_iff_bot_eq_top,
    â† Submodule.subsingleton_iff R, â† subsingleton_iff_bot_eq_top,
    â† map_tensorProduct_mk_eq_top (M := M), Submodule.map_bot]

theorem span_eq_top_of_tmul_eq_basis [Module.Finite R M] {Î¹}
    (f : Î¹ â†’ M) (b : Basis Î¹ k (k âŠ—[R] M))
    (hb : âˆ€ i, 1 âŠ—â‚œ f i = b i) : Submodule.span R (Set.range f) = âŠ¤ := by
  rw [â† map_tensorProduct_mk_eq_top, Submodule.map_span, â† Submodule.restrictScalars_span R k
    Ideal.Quotient.mk_surjective, Submodule.restrictScalars_eq_top_iff,
    â† b.span_eq, â† Set.range_comp]
  simp only [Function.comp, mk_apply, hb, Basis.span_eq]

end LocalRing

open Function in
/--
Given `Mâ‚ â†’ Mâ‚‚ â†’ Mâ‚ƒ â†’ 0` and `Nâ‚ â†’ Nâ‚‚ â†’ Nâ‚ƒ â†’ 0`,
if `Mâ‚ âŠ— Nâ‚ƒ â†’ Mâ‚‚ âŠ— Nâ‚ƒ` and `Mâ‚‚ âŠ— Nâ‚ â†’ Mâ‚‚ âŠ— Nâ‚‚` are both injective,
then `Mâ‚ƒ âŠ— Nâ‚ â†’ Mâ‚ƒ âŠ— Nâ‚‚` is also injective.
-/
theorem lTensor_injective_of_exact_of_exact_of_rTensor_injective
    {Mâ‚ Mâ‚‚ Mâ‚ƒ Nâ‚ Nâ‚‚ Nâ‚ƒ}
    [AddCommGroup Mâ‚] [Module R Mâ‚] [AddCommGroup Mâ‚‚] [Module R Mâ‚‚] [AddCommGroup Mâ‚ƒ] [Module R Mâ‚ƒ]
    [AddCommGroup Nâ‚] [Module R Nâ‚] [AddCommGroup Nâ‚‚] [Module R Nâ‚‚] [AddCommGroup Nâ‚ƒ] [Module R Nâ‚ƒ]
    {fâ‚ : Mâ‚ â†’â‚—[R] Mâ‚‚} {fâ‚‚ : Mâ‚‚ â†’â‚—[R] Mâ‚ƒ} {gâ‚ : Nâ‚ â†’â‚—[R] Nâ‚‚} {gâ‚‚ : Nâ‚‚ â†’â‚—[R] Nâ‚ƒ}
    (hfexact : Exact fâ‚ fâ‚‚) (hfsurj : Surjective fâ‚‚)
    (hgexact : Exact gâ‚ gâ‚‚) (hgsurj : Surjective gâ‚‚)
    (hfinj : Injective (fâ‚.rTensor Nâ‚ƒ)) (hginj : Injective (gâ‚.lTensor Mâ‚‚)) :
    Injective (gâ‚.lTensor Mâ‚ƒ) := by
  rw [injective_iff_map_eq_zero]
  intro x hx
  obtain âŸ¨x, rflâŸ© := fâ‚‚.rTensor_surjective Nâ‚ hfsurj x
  have : fâ‚‚.rTensor Nâ‚‚ (gâ‚.lTensor Mâ‚‚ x) = 0 := by
    rw [â† hx, â† LinearMap.comp_apply, â† LinearMap.comp_apply, LinearMap.rTensor_comp_lTensor,
      LinearMap.lTensor_comp_rTensor]
  obtain âŸ¨y, hyâŸ© := (rTensor_exact Nâ‚‚ hfexact hfsurj _).mp this
  have : gâ‚‚.lTensor Mâ‚ y = 0 := by
    apply hfinj
    trans gâ‚‚.lTensor Mâ‚‚ (gâ‚.lTensor Mâ‚‚ x)
    Â· rw [â† hy, â† LinearMap.comp_apply, â† LinearMap.comp_apply, LinearMap.rTensor_comp_lTensor,
        LinearMap.lTensor_comp_rTensor]
    rw [â† LinearMap.comp_apply, â† LinearMap.lTensor_comp, hgexact.linearMap_comp_eq_zero]
    simp
  obtain âŸ¨z, rflâŸ© := (lTensor_exact _ hgexact hgsurj _).mp this
  obtain rfl : fâ‚.rTensor Nâ‚ z = x := by
    apply hginj
    simp only [â† hy, â† LinearMap.comp_apply, â† LinearMap.comp_apply, LinearMap.lTensor_comp_rTensor,
      LinearMap.rTensor_comp_lTensor]
  rw [â† LinearMap.comp_apply, â† LinearMap.rTensor_comp, hfexact.linearMap_comp_eq_zero]
  simp

namespace Module

variable [LocalRing R]

/--
If `M` is a finitely presented module over a local ring `(R, ğ”ª)` such that `m âŠ— M â†’ M` is
injective, then `M` is free.
-/
theorem free_of_maximalIdeal_rTensor_injective [Module.FinitePresentation R M]
    (H : Function.Injective ((ğ”ª).subtype.rTensor M)) :
    Module.Free R M := by
  let I := Module.Free.ChooseBasisIndex k (k âŠ—[R] M)
  -- Let `b : I â†’ k âŠ— M` be a `k`-basis.
  let b : Basis I k _ := Module.Free.chooseBasis k (k âŠ—[R] M)
  letI : IsNoetherian k (k âŠ—[R] (I â†’â‚€ R)) :=
    isNoetherian_of_isNoetherianRing_of_finite k (k âŠ—[R] (I â†’â‚€ R))
  choose f hf using TensorProduct.mk_surjective R M k Ideal.Quotient.mk_surjective
  -- By choosing an arbitrary lift of `b` to `I â†’ M`, we get a surjection `i : Rá´µ â†’ M`.
  let i := Finsupp.linearCombination R (f âˆ˜ b)
  have hi : Surjective i := by
    rw [â† LinearMap.range_eq_top, Finsupp.range_linearCombination]
    exact LocalRing.span_eq_top_of_tmul_eq_basis (R := R) (f := f âˆ˜ b) b (fun _ â†¦ hf _)
  have : Module.Finite R (LinearMap.ker i) := by
    constructor
    exact (Submodule.fg_top _).mpr (Module.FinitePresentation.fg_ker i hi)
  -- We claim that `i` is actually a bijection,
  -- hence an isomorphism exhibing `M` as the free module `Rá´µ`
  refine Module.Free.of_equiv (LinearEquiv.ofBijective i âŸ¨?_, hiâŸ©)
  -- By Nakayama's lemma, it suffices to show that `k âŠ— ker(i) = 0`.
  rw [â† LinearMap.ker_eq_bot, â† Submodule.subsingleton_iff_eq_bot,
    â† LocalRing.subsingleton_tensorProduct (R := R), subsingleton_iff_forall_eq 0]
  have : Function.Surjective (i.baseChange k) := i.lTensor_surjective _ hi
  -- By construction, `k âŠ— i : ká´µ â†’ k âŠ— M` is bijective.
  have hi' : Function.Bijective (i.baseChange k) := by
    refine âŸ¨?_, thisâŸ©
    rw [â† LinearMap.ker_eq_bot (M := k âŠ—[R] (I â†’â‚€ R)) (f := i.baseChange k),
      â† Submodule.finrank_eq_zero (R := k) (M := k âŠ—[R] (I â†’â‚€ R)),
      â† Nat.add_right_inj (n := FiniteDimensional.finrank k (LinearMap.range <| i.baseChange k)),
      LinearMap.finrank_range_add_finrank_ker (V := k âŠ—[R] (I â†’â‚€ R)),
      LinearMap.range_eq_top.mpr this, finrank_top]
    simp only [FiniteDimensional.finrank_tensorProduct, FiniteDimensional.finrank_self,
      FiniteDimensional.finrank_finsupp_self, one_mul, add_zero]
    rw [FiniteDimensional.finrank_eq_card_chooseBasisIndex]
  -- On the other hand, `m âŠ— M â†’ M` injective => `Torâ‚(k, M) = 0` => `k âŠ— ker(i) â†’ ká´µ` injective.
  have := @lTensor_injective_of_exact_of_exact_of_rTensor_injective
    (Nâ‚ := LinearMap.ker i) (Nâ‚‚ := I â†’â‚€ R) (Nâ‚ƒ := M)
    (fâ‚ := (ğ”ª).subtype) (fâ‚‚ := Submodule.mkQ ğ”ª) inferInstance inferInstance inferInstance
    inferInstance inferInstance inferInstance
  intro x
  apply @this (LinearMap.ker i).subtype i (LinearMap.exact_subtype_mkQ ğ”ª)
    (Submodule.mkQ_surjective _) (LinearMap.exact_subtype_ker_map i) hi H
    (Module.Flat.lTensor_preserves_injective_linearMap _ Subtype.val_injective)
  apply hi'.injective
  rw [LinearMap.baseChange_eq_ltensor]
  erw [â† LinearMap.comp_apply (i.lTensor k), â† LinearMap.lTensor_comp]
  rw [(LinearMap.exact_subtype_ker_map i).linearMap_comp_eq_zero]
  simp only [LinearMap.lTensor_zero, LinearMap.zero_apply, map_zero]

-- TODO: Generalise this to finite free modules.
theorem free_of_flat_of_localRing [Module.FinitePresentation R P] [Module.Flat R P] :
    Module.Free R P :=
  free_of_maximalIdeal_rTensor_injective
    (Module.Flat.rTensor_preserves_injective_linearMap _ Subtype.val_injective)

/--
If `M â†’ N â†’ P â†’ 0` is a presentation of `P` over a local ring `(R, ğ”ª, k)` with
`M` finite and `N` finite free, then injectivity of `k âŠ— M â†’ k âŠ— N` implies that `P` is free.
-/
theorem free_of_lTensor_residueField_injective (hg : Surjective g) (h : Exact f g)
    [Module.Finite R M] [Module.Finite R N] [Module.Free R N]
    (hf : Function.Injective (f.lTensor k)) :
    Module.Free R P := by
  have := Module.finitePresentation_of_free_of_surjective g hg
    (by rw [h.linearMap_ker_eq, LinearMap.range_eq_map]; exact (Module.Finite.out).map f)
  apply free_of_maximalIdeal_rTensor_injective
  rw [â† LinearMap.lTensor_inj_iff_rTensor_inj]
  apply lTensor_injective_of_exact_of_exact_of_rTensor_injective
    h hg (LinearMap.exact_subtype_mkQ ğ”ª) (Submodule.mkQ_surjective _)
    ((LinearMap.lTensor_inj_iff_rTensor_inj _ _).mp hf)
    (Module.Flat.lTensor_preserves_injective_linearMap _ Subtype.val_injective)

end Module

/--
Given a linear map `l : M â†’ N` over a local ring `(R, ğ”ª, k)`
with `M` finite and `N` finite free,
`l` is a split injection if and only if `k âŠ— l` is a (split) injection.
-/
theorem LocalRing.split_injective_iff_lTensor_residueField_injective [LocalRing R]
    [Module.Finite R M] [Module.Finite R N] [Module.Free R N] (l : M â†’â‚—[R] N) :
    (âˆƒ l', l' âˆ˜â‚— l = LinearMap.id) â†” Function.Injective (l.lTensor (ResidueField R)) := by
  constructor
  Â· intro âŸ¨l', hlâŸ©
    have : l'.lTensor (ResidueField R) âˆ˜â‚— l.lTensor (ResidueField R) = .id := by
      rw [â† LinearMap.lTensor_comp, hl, LinearMap.lTensor_id]
    exact Function.HasLeftInverse.injective âŸ¨_, LinearMap.congr_fun thisâŸ©
  Â· intro h
    -- By `Module.free_of_lTensor_residueField_injective`, `k âŠ— l` injective => `N â§¸ l(M)` free.
    have := Module.free_of_lTensor_residueField_injective l (LinearMap.range l).mkQ
      (Submodule.mkQ_surjective _) l.exact_map_mkQ_range h
    -- Hence `l(M)` is projective because `0 â†’ l(M) â†’ N â†’ N â§¸ l(M) â†’ 0` splits.
    have : Module.Projective R (LinearMap.range l) := by
      have := (Exact.split_tfae (LinearMap.exact_subtype_mkQ (LinearMap.range l))
        Subtype.val_injective (Submodule.mkQ_surjective _)).out 0 1
      obtain âŸ¨l', hl'âŸ© := this.mp
         (Module.projective_lifting_property _ _ (Submodule.mkQ_surjective _))
      exact Module.Projective.of_split _ _ hl'
    -- Then `0 â†’ ker l â†’ M â†’ l(M) â†’ 0` splits.
    obtain âŸ¨l', hl'âŸ© : âˆƒ l', l' âˆ˜â‚— (LinearMap.ker l).subtype = LinearMap.id := by
      have : Function.Exact (LinearMap.ker l).subtype
          (l.codRestrict (LinearMap.range l) (LinearMap.mem_range_self l)) := by
        rw [LinearMap.exact_iff, LinearMap.ker_rangeRestrict, Submodule.range_subtype]
      have := (Exact.split_tfae this
        Subtype.val_injective (fun âŸ¨x, y, eâŸ© â†¦ âŸ¨y, Subtype.ext eâŸ©)).out 0 1
      exact this.mp (Module.projective_lifting_property _ _ (fun âŸ¨x, y, eâŸ© â†¦ âŸ¨y, Subtype.ext eâŸ©))
    have : Module.Finite R (LinearMap.ker l) := by
      refine Module.Finite.of_surjective l' ?_
      exact Function.HasRightInverse.surjective âŸ¨_, DFunLike.congr_fun hl'âŸ©
    -- And tensoring with `k` preserves the injectivity of the first arrow.
    -- That is, `k âŠ— ker l â†’ k âŠ— M` is also injective.
    have H : Function.Injective ((LinearMap.ker l).subtype.lTensor k) := by
      apply_fun (LinearMap.lTensor k) at hl'
      rw [LinearMap.lTensor_comp, LinearMap.lTensor_id] at hl'
      exact Function.HasLeftInverse.injective âŸ¨l'.lTensor k, DFunLike.congr_fun hl'âŸ©
    -- But by assumption `k âŠ— M â†’ k âŠ— l(M)` is already injective, so `k âŠ— ker l = 0`.
    have : Subsingleton (k âŠ—[R] LinearMap.ker l) := by
      refine (subsingleton_iff_forall_eq 0).mpr fun y â†¦ H (h ?_)
      rw [map_zero, map_zero, â† LinearMap.comp_apply, â† LinearMap.lTensor_comp,
        l.exact_subtype_ker_map.linearMap_comp_eq_zero, LinearMap.lTensor_zero,
        LinearMap.zero_apply]
    -- By Nakayama's lemma, `l` is injective.
    have : Function.Injective l := by
      rwa [â† LinearMap.ker_eq_bot, â† Submodule.subsingleton_iff_eq_bot,
        â† LocalRing.subsingleton_tensorProduct (R := R)]
    -- Whence `M â‰ƒ l(M)` is projective and the result follows.
    have := (Exact.split_tfae l.exact_map_mkQ_range this (Submodule.mkQ_surjective _)).out 0 1
    rw [â† this]
    exact Module.projective_lifting_property _ _ (Submodule.mkQ_surjective _)

end
