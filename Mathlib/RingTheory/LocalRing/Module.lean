/-
Copyright (c) 2024 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.Algebra.Module.FinitePresentation
import Mathlib.Algebra.Module.Torsion
import Mathlib.LinearAlgebra.Dual.Lemmas
import Mathlib.RingTheory.FiniteType
import Mathlib.RingTheory.Flat.EquationalCriterion
import Mathlib.RingTheory.LocalRing.ResidueField.Basic
import Mathlib.RingTheory.LocalRing.ResidueField.Ideal
import Mathlib.RingTheory.Nakayama
import Mathlib.RingTheory.Support

/-!
# Finite modules over local rings

This file gathers various results about finite modules over a local ring `(R, ğ”ª, k)`.

## Main results
- `IsLocalRing.subsingleton_tensorProduct`: If `M` is finitely generated, `k âŠ— M = 0 â†” M = 0`.
- `Module.free_of_maximalIdeal_rTensor_injective`:
  If `M` is a finitely presented module such that `m âŠ— M â†’ M` is injective
  (for example when `M` is flat), then `M` is free.
- `Module.free_of_lTensor_residueField_injective`: If `N â†’ M â†’ P â†’ 0` is a presentation of `P` with
  `N` finite and `M` finite free, then injectivity of `k âŠ— N â†’ k âŠ— M` implies that `P` is free.
- `IsLocalRing.split_injective_iff_lTensor_residueField_injective`:
  Given an `R`-linear map `l : M â†’ N` with `M` finite and `N` finite free,
  `l` is a split injection if and only if `k âŠ— l` is a (split) injection.
-/

open Module

universe u
variable {R M N P : Type*} [CommRing R]

section

variable [AddCommGroup M] [AddCommGroup N] [Module R M] [Module R N]

open Function (Injective Surjective Exact)
open IsLocalRing TensorProduct

local notation "k" => ResidueField R
local notation "ğ”ª" => maximalIdeal R

variable [AddCommGroup P] [Module R P] (f : M â†’â‚—[R] N) (g : N â†’â‚—[R] P)

namespace IsLocalRing

variable [IsLocalRing R]

theorem map_mkQ_eq {Nâ‚ Nâ‚‚ : Submodule R M} (h : Nâ‚ â‰¤ Nâ‚‚) (h' : Nâ‚‚.FG) :
    Nâ‚.map (Submodule.mkQ (ğ”ª â€¢ Nâ‚‚)) = Nâ‚‚.map (Submodule.mkQ (ğ”ª â€¢ Nâ‚‚)) â†” Nâ‚ = Nâ‚‚ := by
  constructor
  Â· intro hN
    have : Nâ‚‚ â‰¤ ğ”ª â€¢ Nâ‚‚ âŠ” Nâ‚ := by
      simpa using Submodule.comap_mono (f := Submodule.mkQ (ğ”ª â€¢ Nâ‚‚)) hN.ge
    rw [sup_comm] at this
    exact h.antisymm (Submodule.le_of_le_smul_of_le_jacobson_bot h'
      (by rw [jacobson_eq_maximalIdeal]; exact bot_ne_top) this)
  Â· rintro rfl; simp

theorem map_mkQ_eq_top {N : Submodule R M} [Module.Finite R M] :
    N.map (Submodule.mkQ (ğ”ª â€¢ âŠ¤)) = âŠ¤ â†” N = âŠ¤ := by
  rw [â† map_mkQ_eq (Nâ‚ := N) le_top Module.Finite.fg_top, Submodule.map_top, Submodule.range_mkQ]

theorem map_tensorProduct_mk_eq_top {N : Submodule R M} [Module.Finite R M] :
    N.map (TensorProduct.mk R k M 1) = âŠ¤ â†” N = âŠ¤ := by
  constructor
  Â· intro hN
    letI : Module k (M â§¸ (ğ”ª â€¢ âŠ¤ : Submodule R M)) :=
      inferInstanceAs (Module (R â§¸ ğ”ª) (M â§¸ ğ”ª â€¢ (âŠ¤ : Submodule R M)))
    letI : IsScalarTower R k (M â§¸ (ğ”ª â€¢ âŠ¤ : Submodule R M)) :=
      inferInstanceAs (IsScalarTower R (R â§¸ ğ”ª) (M â§¸ ğ”ª â€¢ (âŠ¤ : Submodule R M)))
    let f := AlgebraTensorModule.lift (((LinearMap.ringLmapEquivSelf k k _).symm
      (Submodule.mkQ (ğ”ª â€¢ âŠ¤ : Submodule R M))).restrictScalars R)
    have : f.comp (TensorProduct.mk R k M 1) = Submodule.mkQ (ğ”ª â€¢ âŠ¤) := by ext; simp [f]
    have hf : Function.Surjective f := by
      intro x; obtain âŸ¨x, rflâŸ© := Submodule.mkQ_surjective _ x
      rw [â† this, LinearMap.comp_apply]; exact âŸ¨_, rflâŸ©
    apply_fun Submodule.map f at hN
    rwa [â† Submodule.map_comp, this, Submodule.map_top, LinearMap.range_eq_top.2 hf,
      map_mkQ_eq_top] at hN
  Â· rintro rfl; rw [Submodule.map_top, LinearMap.range_eq_top]
    exact TensorProduct.mk_surjective R M k Ideal.Quotient.mk_surjective

theorem subsingleton_tensorProduct [Module.Finite R M] :
    Subsingleton (k âŠ—[R] M) â†” Subsingleton M := by
  rw [â† Submodule.subsingleton_iff R, â† subsingleton_iff_bot_eq_top,
    â† Submodule.subsingleton_iff R, â† subsingleton_iff_bot_eq_top,
    â† map_tensorProduct_mk_eq_top (M := M), Submodule.map_bot]

theorem span_eq_top_of_tmul_eq_basis [Module.Finite R M] {Î¹}
    (f : Î¹ â†’ M) (b : Basis Î¹ k (k âŠ—[R] M))
    (hb : âˆ€ i, 1 âŠ—â‚œ f i = b i) : Submodule.span R (Set.range f) = âŠ¤ := by
  rw [â† map_tensorProduct_mk_eq_top, Submodule.map_span, â† Submodule.restrictScalars_span R k
    Ideal.Quotient.mk_surjective, Submodule.restrictScalars_eq_top_iff,
    â† b.span_eq, â† Set.range_comp]
  simp only [Function.comp_def, mk_apply, hb, Basis.span_eq]

end IsLocalRing

lemma Module.mem_support_iff_nontrivial_residueField_tensorProduct [Module.Finite R M]
    (p : PrimeSpectrum R) :
    p âˆˆ Module.support R M â†” Nontrivial (p.asIdeal.ResidueField âŠ—[R] M) := by
  let K := p.asIdeal.ResidueField
  let e := (AlgebraTensorModule.cancelBaseChange R (Localization.AtPrime p.asIdeal) K K M).symm
  rw [e.nontrivial_congr, Module.mem_support_iff,
    (LocalizedModule.equivTensorProduct p.asIdeal.primeCompl M).nontrivial_congr,
    â† not_iff_not, not_nontrivial_iff_subsingleton, not_nontrivial_iff_subsingleton,
    IsLocalRing.subsingleton_tensorProduct]

@[deprecated (since := "2024-11-11")]
alias LocalRing.map_mkQ_eq := IsLocalRing.map_mkQ_eq

@[deprecated (since := "2024-11-11")]
alias LocalRing.map_mkQ_eq_top := IsLocalRing.map_mkQ_eq_top

@[deprecated (since := "2024-11-11")]
alias LocalRing.map_tensorProduct_mk_eq_top := IsLocalRing.map_tensorProduct_mk_eq_top

@[deprecated (since := "2024-11-11")]
alias LocalRing.subsingleton_tensorProduct := IsLocalRing.subsingleton_tensorProduct

@[deprecated (since := "2024-11-11")]
alias LocalRing.span_eq_top_of_tmul_eq_basis := IsLocalRing.span_eq_top_of_tmul_eq_basis

open Function in
/--
Given `Mâ‚ â†’ Mâ‚‚ â†’ Mâ‚ƒ â†’ 0` and `Nâ‚ â†’ Nâ‚‚ â†’ Nâ‚ƒ â†’ 0`,
if `Mâ‚ âŠ— Nâ‚ƒ â†’ Mâ‚‚ âŠ— Nâ‚ƒ` and `Mâ‚‚ âŠ— Nâ‚ â†’ Mâ‚‚ âŠ— Nâ‚‚` are both injective,
then `Mâ‚ƒ âŠ— Nâ‚ â†’ Mâ‚ƒ âŠ— Nâ‚‚` is also injective.
-/
theorem lTensor_injective_of_exact_of_exact_of_rTensor_injective
    {Mâ‚ Mâ‚‚ Mâ‚ƒ Nâ‚ Nâ‚‚ Nâ‚ƒ}
    [AddCommGroup Mâ‚] [Module R Mâ‚] [AddCommGroup Mâ‚‚] [Module R Mâ‚‚] [AddCommGroup Mâ‚ƒ] [Module R Mâ‚ƒ]
    [AddCommGroup Nâ‚] [Module R Nâ‚] [AddCommGroup Nâ‚‚] [Module R Nâ‚‚] [AddCommGroup Nâ‚ƒ] [Module R Nâ‚ƒ]
    {fâ‚ : Mâ‚ â†’â‚—[R] Mâ‚‚} {fâ‚‚ : Mâ‚‚ â†’â‚—[R] Mâ‚ƒ} {gâ‚ : Nâ‚ â†’â‚—[R] Nâ‚‚} {gâ‚‚ : Nâ‚‚ â†’â‚—[R] Nâ‚ƒ}
    (hfexact : Exact fâ‚ fâ‚‚) (hfsurj : Surjective fâ‚‚)
    (hgexact : Exact gâ‚ gâ‚‚) (hgsurj : Surjective gâ‚‚)
    (hfinj : Injective (fâ‚.rTensor Nâ‚ƒ)) (hginj : Injective (gâ‚.lTensor Mâ‚‚)) :
    Injective (gâ‚.lTensor Mâ‚ƒ) := by
  rw [injective_iff_map_eq_zero]
  intro x hx
  obtain âŸ¨x, rflâŸ© := fâ‚‚.rTensor_surjective Nâ‚ hfsurj x
  have : fâ‚‚.rTensor Nâ‚‚ (gâ‚.lTensor Mâ‚‚ x) = 0 := by
    rw [â† hx, â† LinearMap.comp_apply, â† LinearMap.comp_apply, LinearMap.rTensor_comp_lTensor,
      LinearMap.lTensor_comp_rTensor]
  obtain âŸ¨y, hyâŸ© := (rTensor_exact Nâ‚‚ hfexact hfsurj _).mp this
  have : gâ‚‚.lTensor Mâ‚ y = 0 := by
    apply hfinj
    trans gâ‚‚.lTensor Mâ‚‚ (gâ‚.lTensor Mâ‚‚ x)
    Â· rw [â† hy, â† LinearMap.comp_apply, â† LinearMap.comp_apply, LinearMap.rTensor_comp_lTensor,
        LinearMap.lTensor_comp_rTensor]
    rw [â† LinearMap.comp_apply, â† LinearMap.lTensor_comp, hgexact.linearMap_comp_eq_zero]
    simp
  obtain âŸ¨z, rflâŸ© := (lTensor_exact _ hgexact hgsurj _).mp this
  obtain rfl : fâ‚.rTensor Nâ‚ z = x := by
    apply hginj
    simp only [â† hy, â† LinearMap.comp_apply, â† LinearMap.comp_apply, LinearMap.lTensor_comp_rTensor,
      LinearMap.rTensor_comp_lTensor]
  rw [â† LinearMap.comp_apply, â† LinearMap.rTensor_comp, hfexact.linearMap_comp_eq_zero]
  simp

namespace Module

variable [IsLocalRing R]

/-- If `M` is of finite presentation over a local ring `(R, ğ”ª, k)` such that
`ğ”ª âŠ— M â†’ M` is injective, then every family of elements that is a `k`-basis of
`k âŠ— M` is an `R`-basis of `M`. -/
lemma exists_basis_of_basis_baseChange [Module.FinitePresentation R M]
    {Î¹ : Type*} (v : Î¹ â†’ M) (hli : LinearIndependent k (TensorProduct.mk R k M 1 âˆ˜ v))
    (hsp : Submodule.span k (Set.range (TensorProduct.mk R k M 1 âˆ˜ v)) = âŠ¤)
    (H : Function.Injective ((ğ”ª).subtype.rTensor M)) :
    âˆƒ (b : Basis Î¹ R M), âˆ€ i, b i = v i := by
  let bk : Basis Î¹ k (k âŠ—[R] M) := Basis.mk hli (by rw [hsp])
  haveI : Finite Î¹ := Module.Finite.finite_basis bk
  letI : Fintype Î¹ := Fintype.ofFinite Î¹
  let i := Finsupp.linearCombination R v
  have hi : Surjective i := by
    rw [â† LinearMap.range_eq_top, Finsupp.range_linearCombination]
    refine IsLocalRing.span_eq_top_of_tmul_eq_basis (R := R) (f := v) bk
      (fun _ â†¦ by simp [bk])
  have : Module.Finite R (LinearMap.ker i) := by
    constructor
    exact (Submodule.fg_top _).mpr (Module.FinitePresentation.fg_ker i hi)
  -- We claim that `i` is actually a bijection,
  -- hence `v` induces an isomorphism `M â‰ƒ[R] Rá´µ` showing that `v` is a basis.
  let iequiv : (Î¹ â†’â‚€ R) â‰ƒâ‚—[R] M := by
    refine LinearEquiv.ofBijective i âŸ¨?_, hiâŸ©
    -- By Nakayama's lemma, it suffices to show that `k âŠ— ker(i) = 0`.
    rw [â† LinearMap.ker_eq_bot, â† Submodule.subsingleton_iff_eq_bot,
      â† IsLocalRing.subsingleton_tensorProduct (R := R), subsingleton_iff_forall_eq 0]
    have : Function.Surjective (i.baseChange k) := i.lTensor_surjective _ hi
    -- By construction, `k âŠ— i : ká´µ â†’ k âŠ— M` is bijective.
    have hi' : Function.Bijective (i.baseChange k) := by
      refine âŸ¨?_, thisâŸ©
      rw [â† LinearMap.ker_eq_bot (M := k âŠ—[R] (Î¹ â†’â‚€ R)) (f := i.baseChange k),
        â† Submodule.finrank_eq_zero (R := k) (M := k âŠ—[R] (Î¹ â†’â‚€ R)),
        â† Nat.add_right_inj (n := Module.finrank k (LinearMap.range <| i.baseChange k)),
        LinearMap.finrank_range_add_finrank_ker (V := k âŠ—[R] (Î¹ â†’â‚€ R)),
        LinearMap.range_eq_top.mpr this, finrank_top]
      simp only [Module.finrank_tensorProduct, Module.finrank_self,
        Module.finrank_finsupp_self, one_mul, add_zero]
      rw [Module.finrank_eq_card_basis bk]
    -- On the other hand, `m âŠ— M â†’ M` injective => `Torâ‚(k, M) = 0` => `k âŠ— ker(i) â†’ ká´µ` injective.
    intro x
    refine lTensor_injective_of_exact_of_exact_of_rTensor_injective
      (Nâ‚ := LinearMap.ker i) (Nâ‚‚ := Î¹ â†’â‚€ R) (Nâ‚ƒ := M)
      (fâ‚ := (ğ”ª).subtype) (fâ‚‚ := Submodule.mkQ ğ”ª)
      (gâ‚ := (LinearMap.ker i).subtype) (gâ‚‚ := i) (LinearMap.exact_subtype_mkQ ğ”ª)
      (Submodule.mkQ_surjective _) (LinearMap.exact_subtype_ker_map i) hi H ?_ ?_
    Â· apply Module.Flat.lTensor_preserves_injective_linearMap
      exact Subtype.val_injective
    Â· apply hi'.injective
      rw [LinearMap.baseChange_eq_ltensor]
      erw [â† LinearMap.comp_apply (i.lTensor k), â† LinearMap.lTensor_comp]
      rw [(LinearMap.exact_subtype_ker_map i).linearMap_comp_eq_zero]
      simp only [LinearMap.lTensor_zero, LinearMap.zero_apply, map_zero]
  use Basis.ofRepr iequiv.symm
  intro j
  simp [iequiv, i]

/--
If `M` is a finitely presented module over a local ring `(R, ğ”ª)` such that `m âŠ— M â†’ M` is
injective, then every generating family contains a basis.
-/
lemma exists_basis_of_span_of_maximalIdeal_rTensor_injective [Module.FinitePresentation R M]
    (H : Function.Injective ((ğ”ª).subtype.rTensor M))
    {Î¹ : Type u} (v : Î¹ â†’ M) (hv : Submodule.span R (Set.range v) = âŠ¤) :
    âˆƒ (Îº : Type u) (a : Îº â†’ Î¹) (b : Basis Îº R M), âˆ€ i, b i = v (a i) := by
  have := (map_tensorProduct_mk_eq_top (N := Submodule.span R (Set.range v))).mpr hv
  rw [â† Submodule.span_image, â† Set.range_comp, eq_top_iff, â† SetLike.coe_subset_coe,
    Submodule.top_coe] at this
  have : Submodule.span k (Set.range (TensorProduct.mk R k M 1 âˆ˜ v)) = âŠ¤ := by
    rw [eq_top_iff]
    exact Set.Subset.trans this (Submodule.span_subset_span _ _ _)
  obtain âŸ¨Îº, a, ha, hsp, hliâŸ© := exists_linearIndependent' k (TensorProduct.mk R k M 1 âˆ˜ v)
  rw [this] at hsp
  obtain âŸ¨b, hbâŸ© := exists_basis_of_basis_baseChange (v âˆ˜ a) hli hsp H
  use Îº, a, b, hb

lemma exists_basis_of_span_of_flat [Module.FinitePresentation R M] [Module.Flat R M]
    {Î¹ : Type u} (v : Î¹ â†’ M) (hv : Submodule.span R (Set.range v) = âŠ¤) :
    âˆƒ (Îº : Type u) (a : Îº â†’ Î¹) (b : Basis Îº R M), âˆ€ i, b i = v (a i) :=
  exists_basis_of_span_of_maximalIdeal_rTensor_injective
    (Module.Flat.rTensor_preserves_injective_linearMap (ğ”ª).subtype Subtype.val_injective) v hv

/--
If `M` is a finitely presented module over a local ring `(R, ğ”ª)` such that `m âŠ— M â†’ M` is
injective, then `M` is free.
-/
theorem free_of_maximalIdeal_rTensor_injective [Module.FinitePresentation R M]
    (H : Function.Injective ((ğ”ª).subtype.rTensor M)) :
    Module.Free R M := by
  obtain âŸ¨_, _, b, _âŸ© := exists_basis_of_span_of_maximalIdeal_rTensor_injective H id (by simp)
  exact Free.of_basis b

theorem IsLocalRing.linearIndependent_of_flat [Flat R M] {Î¹ : Type u} (v : Î¹ â†’ M)
    (h : LinearIndependent k (TensorProduct.mk R k M 1 âˆ˜ v)) : LinearIndependent R v := by
  rw [linearIndependent_iff']; intro s f hfv
  classical
  induction' s using Finset.induction with n s hn ih generalizing v <;> intro i hi
  Â· exact (Finset.notMem_empty _ hi).elim
  rw [â† Finset.sum_coe_sort] at hfv
  have âŸ¨l, a, y, hay, hfaâŸ© := Flat.isTrivialRelation_of_sum_smul_eq_zero hfv
  have : v n âˆ‰ ğ”ª â€¢ (âŠ¤ : Submodule R M) := by
    simpa only [â† LinearMap.ker_tensorProductMk] using h.ne_zero n
  set n : â†¥(insert n s) := âŸ¨n, Finset.mem_insert_self ..âŸ© with n_def
  obtain âŸ¨j, hjâŸ© : âˆƒ j, IsUnit (a n j) := by
    contrapose! this
    rw [show v n = _ from hay n]
    exact sum_mem fun _ _ â†¦ Submodule.smul_mem_smul (this _) âŸ¨âŸ©
  let a' (i : Î¹) : R := if hi : _ then a âŸ¨i, hiâŸ© j else 0
  have a_eq i : a i j = a' i.1 := by simp_rw [a', dif_pos i.2]
  have hfn : f n = -(âˆ‘ i âˆˆ s, f i * a' i) * hj.unitâ»Â¹ := by
    rw [â† hj.mul_left_inj, mul_assoc, hj.val_inv_mul, mul_one, eq_neg_iff_add_eq_zero]
    convert hfa j
    simp_rw [a_eq, Finset.sum_coe_sort _ (fun i â†¦ f i * a' i), s.sum_insert hn, n_def]
  let c (i : Î¹) : R := -(if i = n then 0 else a' i) * hj.unitâ»Â¹
  specialize ih (v + (c Â· â€¢ v n)) ?_ ?_
  Â· convert (linearIndependent_add_smul_iff (c := Ideal.Quotient.mk _ âˆ˜ c) (i := n.1) ?_).mpr h
    Â· ext; simp [tmul_add]; rfl
    simp_rw [Function.comp_def, c, if_pos, neg_zero, zero_mul, map_zero]
  Â· rw [Finset.sum_coe_sort _ (fun i â†¦ f i â€¢ v i), s.sum_insert hn, add_comm, hfn] at hfv
    simp_rw [Pi.add_apply, smul_add, s.sum_add_distrib, c, smul_smul, â† s.sum_smul, â† mul_assoc,
      â† s.sum_mul, mul_neg, s.sum_neg_distrib, â† hfv]
    congr 4
    exact s.sum_congr rfl fun i hi â†¦ by rw [if_neg (ne_of_mem_of_not_mem hi hn)]
  obtain hi | hi := Finset.mem_insert.mp hi
  Â· rw [hi, hfn, Finset.sum_eq_zero, neg_zero, zero_mul]
    intro i hi; rw [ih i hi, zero_mul]
  Â· exact ih i hi

@[stacks 00NZ]
theorem free_of_flat_of_isLocalRing [Module.Finite R P] [Flat R P] : Free R P :=
  let w := Free.chooseBasis k (k âŠ—[R] P)
  have âŸ¨v, eqâŸ© := (TensorProduct.mk_surjective R P k Quotient.mk_surjective).comp_left w
  .of_basis <| .mk (IsLocalRing.linearIndependent_of_flat _ (eq â–¸ w.linearIndependent)) <| by
    exact (span_eq_top_of_tmul_eq_basis _ w <| congr_fun eq).ge

@[deprecated (since := "2024-11-12")] alias free_of_flat_of_localRing := free_of_flat_of_isLocalRing

/--
If `M â†’ N â†’ P â†’ 0` is a presentation of `P` over a local ring `(R, ğ”ª, k)` with
`M` finite and `N` finite free, then injectivity of `k âŠ— M â†’ k âŠ— N` implies that `P` is free.
-/
theorem free_of_lTensor_residueField_injective (hg : Surjective g) (h : Exact f g)
    [Module.Finite R M] [Module.Finite R N] [Module.Free R N]
    (hf : Function.Injective (f.lTensor k)) :
    Module.Free R P := by
  have := Module.finitePresentation_of_free_of_surjective g hg
    (by rw [h.linearMap_ker_eq, LinearMap.range_eq_map]; exact (Module.Finite.fg_top).map f)
  apply free_of_maximalIdeal_rTensor_injective
  rw [â† LinearMap.lTensor_inj_iff_rTensor_inj]
  apply lTensor_injective_of_exact_of_exact_of_rTensor_injective
    h hg (LinearMap.exact_subtype_mkQ ğ”ª) (Submodule.mkQ_surjective _)
    ((LinearMap.lTensor_inj_iff_rTensor_inj _ _).mp hf)
    (Module.Flat.lTensor_preserves_injective_linearMap _ Subtype.val_injective)

end Module

/--
Given a linear map `l : M â†’ N` over a local ring `(R, ğ”ª, k)`
with `M` finite and `N` finite free,
`l` is a split injection if and only if `k âŠ— l` is a (split) injection.
-/
theorem IsLocalRing.split_injective_iff_lTensor_residueField_injective [IsLocalRing R]
    [Module.Finite R M] [Module.Finite R N] [Module.Free R N] (l : M â†’â‚—[R] N) :
    (âˆƒ l', l' âˆ˜â‚— l = LinearMap.id) â†” Function.Injective (l.lTensor (ResidueField R)) := by
  constructor
  Â· intro âŸ¨l', hlâŸ©
    have : l'.lTensor (ResidueField R) âˆ˜â‚— l.lTensor (ResidueField R) = .id := by
      rw [â† LinearMap.lTensor_comp, hl, LinearMap.lTensor_id]
    exact Function.HasLeftInverse.injective âŸ¨_, LinearMap.congr_fun thisâŸ©
  Â· intro h
    -- By `Module.free_of_lTensor_residueField_injective`, `k âŠ— l` injective => `N â§¸ l(M)` free.
    have := Module.free_of_lTensor_residueField_injective l (LinearMap.range l).mkQ
      (Submodule.mkQ_surjective _) l.exact_map_mkQ_range h
    -- Hence `l(M)` is projective because `0 â†’ l(M) â†’ N â†’ N â§¸ l(M) â†’ 0` splits.
    have : Module.Projective R (LinearMap.range l) := by
      have := (Exact.split_tfae (LinearMap.exact_subtype_mkQ (LinearMap.range l))
        Subtype.val_injective (Submodule.mkQ_surjective _)).out 0 1
      obtain âŸ¨l', hl'âŸ© := this.mp
         (Module.projective_lifting_property _ _ (Submodule.mkQ_surjective _))
      exact Module.Projective.of_split _ _ hl'
    -- Then `0 â†’ ker l â†’ M â†’ l(M) â†’ 0` splits.
    obtain âŸ¨l', hl'âŸ© : âˆƒ l', l' âˆ˜â‚— (LinearMap.ker l).subtype = LinearMap.id := by
      have : Function.Exact (LinearMap.ker l).subtype
          (l.codRestrict (LinearMap.range l) (LinearMap.mem_range_self l)) := by
        rw [LinearMap.exact_iff, LinearMap.ker_rangeRestrict, Submodule.range_subtype]
      have := (Exact.split_tfae this
        Subtype.val_injective (fun âŸ¨x, y, eâŸ© â†¦ âŸ¨y, Subtype.ext eâŸ©)).out 0 1
      exact this.mp (Module.projective_lifting_property _ _ (fun âŸ¨x, y, eâŸ© â†¦ âŸ¨y, Subtype.ext eâŸ©))
    have : Module.Finite R (LinearMap.ker l) := by
      refine Module.Finite.of_surjective l' ?_
      exact Function.HasRightInverse.surjective âŸ¨_, DFunLike.congr_fun hl'âŸ©
    -- And tensoring with `k` preserves the injectivity of the first arrow.
    -- That is, `k âŠ— ker l â†’ k âŠ— M` is also injective.
    have H : Function.Injective ((LinearMap.ker l).subtype.lTensor k) := by
      apply_fun (LinearMap.lTensor k) at hl'
      rw [LinearMap.lTensor_comp, LinearMap.lTensor_id] at hl'
      exact Function.HasLeftInverse.injective âŸ¨l'.lTensor k, DFunLike.congr_fun hl'âŸ©
    -- But by assumption `k âŠ— M â†’ k âŠ— l(M)` is already injective, so `k âŠ— ker l = 0`.
    have : Subsingleton (k âŠ—[R] LinearMap.ker l) := by
      refine (subsingleton_iff_forall_eq 0).mpr fun y â†¦ H (h ?_)
      rw [map_zero, map_zero, â† LinearMap.comp_apply, â† LinearMap.lTensor_comp,
        l.exact_subtype_ker_map.linearMap_comp_eq_zero, LinearMap.lTensor_zero,
        LinearMap.zero_apply]
    -- By Nakayama's lemma, `l` is injective.
    have : Function.Injective l := by
      rwa [â† LinearMap.ker_eq_bot, â† Submodule.subsingleton_iff_eq_bot,
        â† IsLocalRing.subsingleton_tensorProduct (R := R)]
    -- Whence `M â‰ƒ l(M)` is projective and the result follows.
    have := (Exact.split_tfae l.exact_map_mkQ_range this (Submodule.mkQ_surjective _)).out 0 1
    rw [â† this]
    exact Module.projective_lifting_property _ _ (Submodule.mkQ_surjective _)

@[deprecated (since := "2024-11-09")]
alias LocalRing.split_injective_iff_lTensor_residueField_injective :=
  IsLocalRing.split_injective_iff_lTensor_residueField_injective

end
