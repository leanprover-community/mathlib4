/-
Copyright (c) 2025 Yongle Hu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yongle Hu
-/
import Mathlib.RingTheory.Ideal.KrullsHeightTheorem

/-!
# Lemmas about `LTSeries` in the prime spectrum

## Main results

* `PrimeSpectrum.exist_ltSeries_mem_one_of_mem_last`: Let $R$ be a Noetherian ring,
  $\mathfrak{p}_0 < \dots < \mathfrak{p}_n$ be a chain of primes, $x \in \mathfrak{p}_n$.
  Then we can find another chain of primes $\mathfrak{q}_0 < \dots < \mathfrak{q}_n$ such that
  $x \in \mathfrak{q}_1$, $\mathfrak{p}_0 = \mathfrak{q}_0$ and $\mathfrak{p}_n = \mathfrak{q}_n$.
-/

variable {R : Type*} [CommRing R] [IsNoetherianRing R]

local notation "ùî™" => IsLocalRing.maximalIdeal R

open Ideal IsLocalRing

namespace PrimeSpectrum

theorem exist_mem_one_of_mem_maximal_ideal [IsLocalRing R] {p‚ÇÅ p‚ÇÄ : PrimeSpectrum R}
    (h‚ÇÄ : p‚ÇÄ < p‚ÇÅ) (h‚ÇÅ : p‚ÇÅ < closedPoint R) {x : R} (hx : x ‚àà ùî™) :
    ‚àÉ q : PrimeSpectrum R, x ‚àà q.asIdeal ‚àß p‚ÇÄ < q ‚àß q.asIdeal < ùî™ := by
  by_cases hn : x ‚àà p‚ÇÄ.1
  ¬∑ exact ‚ü®p‚ÇÅ, h‚ÇÄ.le hn, h‚ÇÄ, h‚ÇÅ‚ü©
  let e := p‚ÇÄ.1.primeSpectrumQuotientOrderIsoZeroLocus.symm
  obtain ‚ü®q, hq‚ü© := (p‚ÇÄ.1 + span {x}).nonempty_minimalPrimes <|
    sup_le (IsLocalRing.le_maximalIdeal_of_isPrime p‚ÇÄ.1) ((span_singleton_le_iff_mem ùî™).mpr hx)
      |>.trans_lt (IsMaximal.isPrime' ùî™).1.lt_top |>.ne
  let q : PrimeSpectrum R := ‚ü®q, hq.1.1‚ü©
  have : q.1.IsPrime := q.2
  have hxq : x ‚àà q.1 := le_sup_right.trans hq.1.2 (mem_span_singleton_self x)
  refine ‚ü®q, hxq, lt_of_le_not_ge (le_sup_left.trans hq.1.2) fun h ‚Ü¶ hn (h hxq), ?_‚ü©
  refine lt_of_le_of_ne (IsLocalRing.le_maximalIdeal_of_isPrime q.1) fun hqm ‚Ü¶ ?_
  have h : (e ‚ü®q, le_sup_left.trans hq.1.2‚ü©).1.height ‚â§ 1 :=
    map_height_le_one_of_mem_minimalPrimes hq
  simp_rw [show q = closedPoint R from PrimeSpectrum.ext hqm] at h
  have hph : (e ‚ü®p‚ÇÅ, h‚ÇÄ.le‚ü©).1.height ‚â§ 0 :=
    Order.lt_one_iff_nonpos.mp (height_le_iff.mp h _ inferInstance (by simpa using h‚ÇÅ))
  refine ENat.not_lt_zero (e ‚ü®p‚ÇÄ, le_refl p‚ÇÄ‚ü©).1.height (height_le_iff.mp hph _ inferInstance ?_)
  simpa using h‚ÇÄ

theorem exist_mem_one_of_mem_two {p‚ÇÅ p‚ÇÄ p‚ÇÇ : PrimeSpectrum R}
    (h‚ÇÄ : p‚ÇÄ < p‚ÇÅ) (h‚ÇÅ : p‚ÇÅ < p‚ÇÇ) {x : R} (hx : x ‚àà p‚ÇÇ.asIdeal) :
    ‚àÉ q : (PrimeSpectrum R), x ‚àà q.asIdeal ‚àß p‚ÇÄ < q ‚àß q < p‚ÇÇ := by
  let e := IsLocalization.AtPrime.primeSpectrumOrderIso (Localization.AtPrime p‚ÇÇ.1) p‚ÇÇ.1
  have hm : closedPoint (Localization.AtPrime p‚ÇÇ.1) =
    e.symm ‚ü®p‚ÇÇ, le_refl p‚ÇÇ‚ü© := (PrimeSpectrum.ext Localization.AtPrime.map_eq_maximalIdeal).symm
  obtain ‚ü®q, hxq, h‚ÇÄ, h‚ÇÅ‚ü© :=
    @exist_mem_one_of_mem_maximal_ideal (Localization.AtPrime p‚ÇÇ.1) _ _ _
      (e.symm ‚ü®p‚ÇÅ, h‚ÇÅ.le‚ü©) (e.symm ‚ü®p‚ÇÄ, (h‚ÇÄ.trans h‚ÇÅ).le‚ü©) (e.symm.lt_iff_lt.mpr h‚ÇÄ)
        (by simp [hm, h‚ÇÅ]) (algebraMap R (Localization.AtPrime p‚ÇÇ.1) x) <| by
          rw [‚Üê Localization.AtPrime.map_eq_maximalIdeal]
          exact mem_map_of_mem (algebraMap R (Localization.AtPrime p‚ÇÇ.1)) hx
  rw [‚Üê e.symm_apply_apply q] at h‚ÇÄ h‚ÇÅ hxq
  have hq : (e q).1 < p‚ÇÇ := by
    have h : e.symm (e q) < e.symm ‚ü®p‚ÇÇ, le_refl p‚ÇÇ‚ü© :=
      h‚ÇÅ.trans_eq Localization.AtPrime.map_eq_maximalIdeal.symm
    rwa [OrderIso.lt_iff_lt, Subtype.mk_lt_mk] at h
  exact Exists.intro (e q).1
    ‚ü®(p‚ÇÇ.1.under_map_of_isLocalizationAtPrime hq.le).le hxq, e.symm.lt_iff_lt.mp h‚ÇÄ, hq‚ü©

/-- Let $R$ be a Noetherian ring, $\mathfrak{p}_0 < \dots < \mathfrak{p}_n$ be a
  chain of primes, $x \in \mathfrak{p}_n$. Then we can find another chain of primes
  $\mathfrak{q}_0 < \dots < \mathfrak{q}_n$ such that $x \in \mathfrak{q}_1$,
  $\mathfrak{p}_0 = \mathfrak{q}_0$ and $\mathfrak{p}_n = \mathfrak{q}_n$. -/
theorem exist_ltSeries_mem_one_of_mem_last (p : LTSeries (PrimeSpectrum R))
    {x : R} (hx : x ‚àà p.last.asIdeal) : ‚àÉ q : LTSeries (PrimeSpectrum R),
    x ‚àà (q 1).asIdeal ‚àß p.length = q.length ‚àß p.head = q.head ‚àß p.last = q.last := by
  generalize hp : p.length = n
  induction n generalizing p with
  | zero =>
    use RelSeries.singleton _ p.last
    simp only [RelSeries.singleton_toFun, hx, RelSeries.singleton_length, RelSeries.head,
      RelSeries.last_singleton, and_true, true_and]
    rw [show 0 = Fin.last p.length from Fin.zero_eq_mk.mpr hp, RelSeries.last]
  | succ n hn => ?_
  by_cases h0 : n = 0
  ¬∑ use p
    have h1 : 1 = Fin.last p.length := by
      rw [Fin.last, hp, h0, zero_add]
      exact Fin.natCast_eq_mk (Nat.one_lt_succ_succ 0)
    simpa [h1, hp] using hx
  obtain ‚ü®q, hxq, h2, hq‚ü© : ‚àÉ q : PrimeSpectrum R, x ‚àà q.1 ‚àß p ‚ü®p.length - 2, _‚ü© < q ‚àß q < p.last :=
    (p ‚ü®p.length - 1, p.length.sub_lt_succ 1‚ü©).exist_mem_one_of_mem_two
      (p.strictMono (Nat.pred_lt (by simpa [hp]))) (p.strictMono (Nat.pred_lt (by simp [hp]))) hx
  obtain ‚ü®Q, hx, hQ, hh, hl‚ü© := hn (p.eraseLast.eraseLast.snoc q h2) (by simpa using hxq) <| by
    simpa [hp] using Nat.succ_pred_eq_of_ne_zero h0
  have h1 : 1 < Q.length + 1 := Nat.lt_of_sub_ne_zero (hQ.symm.trans_ne h0)
  have h : 1 = (1 : Fin (Q.length + 1)).castSucc := by simp [Fin.one_eq_mk_of_lt h1]
  exact ‚ü®Q.snoc p.last (by simpa [‚Üê hl] using hq), by simpa [h], by simpa, by simp [‚Üê hh], by simp‚ü©

end PrimeSpectrum
