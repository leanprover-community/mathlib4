/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Filippo A. E. Nuccio, Andrew Yang
-/
import Mathlib.RingTheory.Spectrum.Prime.Basic
import Mathlib.RingTheory.LocalRing.ResidueField.Ideal
import Mathlib.RingTheory.TensorProduct.Basic

/-!
# Functoriality of the prime spectrum

In this file we define the induced map on prime spectra induced by a ring homomorphism.

## Main definitions

* `RingHom.specComap`: The induced map on prime spectra by a ring homomorphism. The bundled
  continuous version is `PrimeSpectrum.comap` in `Mathlib/RingTheory/Spectrum/Prime/Topology.lean`.

-/

universe u v

variable (R : Type u) (S : Type v)

open PrimeSpectrum

/-- The pullback of an element of `PrimeSpectrum S` along a ring homomorphism `f : R â†’+* S`.
The bundled continuous version is `PrimeSpectrum.comap`. -/
abbrev RingHom.specComap {R S : Type*} [CommSemiring R] [CommSemiring S] (f : R â†’+* S) :
    PrimeSpectrum S â†’ PrimeSpectrum R :=
  fun y â†¦ âŸ¨Ideal.comap f y.asIdeal, inferInstanceâŸ©

namespace PrimeSpectrum

open RingHom

variable {R S} {S' : Type*} [CommSemiring R] [CommSemiring S] [CommSemiring S']

theorem preimage_specComap_zeroLocus_aux (f : R â†’+* S) (s : Set R) :
    f.specComap â»Â¹' zeroLocus s = zeroLocus (f '' s) := by
  ext x
  simp only [mem_zeroLocus, Set.image_subset_iff, Set.mem_preimage, mem_zeroLocus, Ideal.coe_comap]

variable (f : R â†’+* S)

@[simp]
theorem specComap_asIdeal (y : PrimeSpectrum S) :
    (f.specComap y).asIdeal = Ideal.comap f y.asIdeal :=
  rfl

@[simp]
theorem specComap_id : (RingHom.id R).specComap = fun x â†¦ x :=
  rfl

@[simp]
theorem specComap_comp (f : R â†’+* S) (g : S â†’+* S') :
    (g.comp f).specComap = f.specComap.comp g.specComap :=
  rfl

theorem specComap_comp_apply (f : R â†’+* S) (g : S â†’+* S') (x : PrimeSpectrum S') :
    (g.comp f).specComap x = f.specComap (g.specComap x) :=
  rfl

@[simp]
theorem preimage_specComap_zeroLocus (s : Set R) :
    f.specComap â»Â¹' zeroLocus s = zeroLocus (f '' s) :=
  preimage_specComap_zeroLocus_aux f s

theorem specComap_injective_of_surjective (f : R â†’+* S) (hf : Function.Surjective f) :
    Function.Injective f.specComap := fun x y h â†¦
  PrimeSpectrum.ext
    (Ideal.comap_injective_of_surjective f hf
      (congr_arg PrimeSpectrum.asIdeal h : (f.specComap x).asIdeal = (f.specComap y).asIdeal))

/-- `RingHom.specComap` of an isomorphism of rings as an equivalence of their prime spectra. -/
@[simps apply symm_apply]
def comapEquiv (e : R â‰ƒ+* S) : PrimeSpectrum R â‰ƒo PrimeSpectrum S where
  toFun := e.symm.toRingHom.specComap
  invFun := e.toRingHom.specComap
  left_inv x := by
    rw [â† specComap_comp_apply, RingEquiv.toRingHom_eq_coe,
      RingEquiv.toRingHom_eq_coe, RingEquiv.symm_comp]
    rfl
  right_inv x := by
    rw [â† specComap_comp_apply, RingEquiv.toRingHom_eq_coe,
      RingEquiv.toRingHom_eq_coe, RingEquiv.comp_symm]
    rfl
  map_rel_iff' {I J} := Ideal.comap_le_comap_iff_of_surjective _ e.symm.surjective ..

section Pi

variable {Î¹} (R : Î¹ â†’ Type*) [âˆ€ i, CommSemiring (R i)]

/-- The canonical map from a disjoint union of prime spectra of commutative semirings to
the prime spectrum of the product semiring. -/
/- TODO: show this is always a topological embedding (even when Î¹ is infinite)
and is a homeomorphism when Î¹ is finite. -/
@[simps] def sigmaToPi : (Î£ i, PrimeSpectrum (R i)) â†’ PrimeSpectrum (Î  i, R i)
  | âŸ¨i, pâŸ© => (Pi.evalRingHom R i).specComap p

theorem sigmaToPi_injective : (sigmaToPi R).Injective := fun âŸ¨i, pâŸ© âŸ¨j, qâŸ© eq â†¦ by
  classical
  obtain rfl | ne := eq_or_ne i j
  Â· congr; ext x
    simpa using congr_arg (Function.update (0 : âˆ€ i, R i) i x âˆˆ Â·.asIdeal) eq
  Â· refine (p.1.ne_top_iff_one.mp p.2.ne_top ?_).elim
    have : Function.update (1 : âˆ€ i, R i) j 0 âˆˆ (sigmaToPi R âŸ¨j, qâŸ©).asIdeal := by simp
    simpa [â† eq, Function.update_of_ne ne]

variable [Infinite Î¹] [âˆ€ i, Nontrivial (R i)]

/-- An infinite product of nontrivial commutative semirings has a maximal ideal outside of the
range of `sigmaToPi`, i.e. is not of the form `Ï€áµ¢â»Â¹(ğ”­)` for some prime `ğ”­ âŠ‚ R i`, where
`Ï€áµ¢ : (Î  i, R i) â†’+* R i` is the projection. For a complete description of all prime ideals,
see https://math.stackexchange.com/a/1563190. -/
theorem exists_maximal_notMem_range_sigmaToPi_of_infinite :
    âˆƒ (I : Ideal (Î  i, R i)) (_ : I.IsMaximal), âŸ¨I, inferInstanceâŸ© âˆ‰ Set.range (sigmaToPi R) := by
  classical
  let J : Ideal (Î  i, R i) := -- `J := Î â‚€ i, R i` is an ideal in `Î  i, R i`
  { __ := AddMonoidHom.mrange DFinsupp.coeFnAddMonoidHom
    smul_mem' := by
      rintro r _ âŸ¨x, rflâŸ©
      refine âŸ¨.mk x.support fun i â†¦ r i * x i, funext fun i â†¦ show dite _ _ _ = _ from ?_âŸ©
      simp_rw [DFinsupp.coeFnAddMonoidHom]
      refine dite_eq_left_iff.mpr fun h â†¦ ?_
      rw [DFinsupp.notMem_support_iff.mp h, mul_zero] }
  have âŸ¨I, max, leâŸ© := J.exists_le_maximal <| (Ideal.ne_top_iff_one _).mpr <| by
    -- take a maximal ideal I containing J
    rintro âŸ¨x, hxâŸ©
    have âŸ¨i, hiâŸ© := x.support.exists_notMem
    simpa [DFinsupp.coeFnAddMonoidHom, DFinsupp.notMem_support_iff.mp hi] using congr_fun hx i
  refine âŸ¨I, max, fun âŸ¨âŸ¨i, pâŸ©, eqâŸ© â†¦ ?_âŸ©
  -- then I is not in the range of `sigmaToPi`
  have : â‡‘(DFinsupp.single i 1) âˆ‰ (sigmaToPi R âŸ¨i, pâŸ©).asIdeal := by
    simpa using p.1.ne_top_iff_one.mp p.2.ne_top
  rw [eq] at this
  exact this (le âŸ¨.single i 1, rflâŸ©)

@[deprecated (since := "2025-05-24")]
alias exists_maximal_nmem_range_sigmaToPi_of_infinite :=
  exists_maximal_notMem_range_sigmaToPi_of_infinite

theorem sigmaToPi_not_surjective_of_infinite : Â¬ (sigmaToPi R).Surjective := fun surj â†¦
  have âŸ¨_, _, notMemâŸ© := exists_maximal_notMem_range_sigmaToPi_of_infinite R
  (Set.range_eq_univ.mpr surj â–¸ notMem) âŸ¨âŸ©

lemma exists_comap_evalRingHom_eq
    {Î¹ : Type*} {R : Î¹ â†’ Type*} [âˆ€ i, CommRing (R i)] [Finite Î¹]
    (p : PrimeSpectrum (Î  i, R i)) :
    âˆƒ (i : Î¹) (q : PrimeSpectrum (R i)), (Pi.evalRingHom R i).specComap q = p := by
  classical
  cases nonempty_fintype Î¹
  let e (i) : Î  i, R i := Function.update 1 i 0
  have H : âˆ i, e i = 0 := by
    ext j
    rw [Finset.prod_apply, Pi.zero_apply, Finset.prod_eq_zero (Finset.mem_univ j)]
    simp [e]
  obtain âŸ¨i, hiâŸ© : âˆƒ i, e i âˆˆ p.asIdeal := by
    simpa [â† H, Ideal.IsPrime.prod_mem_iff] using p.asIdeal.zero_mem
  let hâ‚ : Function.Surjective (Pi.evalRingHom R i) := RingHomSurjective.is_surjective
  have hâ‚‚ : RingHom.ker (Pi.evalRingHom R i) â‰¤ p.asIdeal := by
    intro x hx
    convert p.asIdeal.mul_mem_left x hi
    ext j
    by_cases hj : i = j
    Â· subst hj; simpa [e]
    Â· simp [e, Function.update_of_ne (.symm hj)]
  have : (p.asIdeal.map (Pi.evalRingHom R i)).comap (Pi.evalRingHom R i) = p.asIdeal := by
    rwa [Ideal.comap_map_of_surjective _ hâ‚, sup_eq_left]
  exact âŸ¨i, âŸ¨_, Ideal.map_isPrime_of_surjective hâ‚ hâ‚‚âŸ©, PrimeSpectrum.ext thisâŸ©

lemma sigmaToPi_bijective {Î¹ : Type*} (R : Î¹ â†’ Type*) [âˆ€ i, CommRing (R i)] [Finite Î¹] :
    Function.Bijective (sigmaToPi R) := by
  refine âŸ¨sigmaToPi_injective R, ?_âŸ©
  intro q
  obtain âŸ¨i, q, rflâŸ© := exists_comap_evalRingHom_eq q
  exact âŸ¨âŸ¨i, qâŸ©, rflâŸ©

lemma iUnion_range_specComap_comp_evalRingHom
    {Î¹ : Type*} {R : Î¹ â†’ Type*} [âˆ€ i, CommRing (R i)] [Finite Î¹]
    {S : Type*} [CommRing S] (f : S â†’+* Î  i, R i) :
    â‹ƒ i, Set.range ((Pi.evalRingHom R i).comp f).specComap = Set.range f.specComap := by
  simp_rw [specComap_comp]
  apply subset_antisymm
  Â· exact Set.iUnion_subset fun _ â†¦ Set.range_comp_subset_range _ _
  Â· rintro _ âŸ¨p, rflâŸ©
    obtain âŸ¨i, p, rflâŸ© := exists_comap_evalRingHom_eq p
    exact Set.mem_iUnion_of_mem i âŸ¨p, rflâŸ©

end Pi

end PrimeSpectrum

section SpecOfSurjective

open Function RingHom

variable [CommRing R] [CommRing S]
variable (f : R â†’+* S)
variable {R}

theorem image_specComap_zeroLocus_eq_zeroLocus_comap (hf : Surjective f) (I : Ideal S) :
    f.specComap '' zeroLocus I = zeroLocus (I.comap f) := by
  simp only [Set.ext_iff, Set.mem_image, mem_zeroLocus, SetLike.coe_subset_coe]
  refine fun p â†¦ âŸ¨?_, fun h_I_p â†¦ ?_âŸ©
  Â· rintro âŸ¨p, hp, rflâŸ© a ha
    exact hp ha
  Â· have hp : ker f â‰¤ p.asIdeal := (Ideal.comap_mono bot_le).trans h_I_p
    refine âŸ¨âŸ¨p.asIdeal.map f, Ideal.map_isPrime_of_surjective hf hpâŸ©, fun x hx â†¦ ?_, ?_âŸ©
    Â· obtain âŸ¨x', rflâŸ© := hf x
      exact Ideal.mem_map_of_mem f (h_I_p hx)
    Â· ext x
      rw [specComap_asIdeal, Ideal.mem_comap, Ideal.mem_map_iff_of_surjective f hf]
      refine âŸ¨?_, fun hx â†¦ âŸ¨x, hx, rflâŸ©âŸ©
      rintro âŸ¨x', hx', heqâŸ©
      rw [â† sub_sub_cancel x' x]
      refine p.asIdeal.sub_mem hx' (hp ?_)
      rwa [mem_ker, map_sub, sub_eq_zero]

theorem range_specComap_of_surjective (hf : Surjective f) :
    Set.range f.specComap = zeroLocus (ker f) := by
  rw [â† Set.image_univ]
  convert image_specComap_zeroLocus_eq_zeroLocus_comap _ _ hf _
  rw [zeroLocus_bot]

variable {S}

/-- Let `f : R â†’+* S` be a surjective ring homomorphism, then `Spec S` is order-isomorphic to `Z(I)`
  where `I = ker f`. -/
noncomputable def Ideal.primeSpectrumOrderIsoZeroLocusOfSurj (hf : Surjective f) {I : Ideal R}
    (hI : RingHom.ker f = I) : PrimeSpectrum S â‰ƒo (PrimeSpectrum.zeroLocus (R := R) I) where
  toFun p := âŸ¨f.specComap p, hI.symm.trans_le (Ideal.ker_le_comap f)âŸ©
  invFun := fun âŸ¨âŸ¨p, _âŸ©, hpâŸ© â†¦ âŸ¨p.map f, p.map_isPrime_of_surjective hf (hI.trans_le hp)âŸ©
  left_inv := by
    intro âŸ¨p, _âŸ©
    simp only [PrimeSpectrum.mk.injEq]
    exact p.map_comap_of_surjective f hf
  right_inv := by
    intro âŸ¨âŸ¨p, _âŸ©, hpâŸ©
    simp only [Subtype.mk.injEq, PrimeSpectrum.mk.injEq]
    exact (p.comap_map_of_surjective f hf).trans <| sup_eq_left.mpr (hI.trans_le hp)
  map_rel_iff' {a b} := by
    change a.asIdeal.comap _ â‰¤ b.asIdeal.comap _ â†” a â‰¤ b
    rw [â† Ideal.map_le_iff_le_comap, Ideal.map_comap_of_surjective f hf,
      PrimeSpectrum.asIdeal_le_asIdeal]

/-- `Spec (R / I)` is order-isomorphic to `Z(I)`. -/
noncomputable def Ideal.primeSpectrumQuotientOrderIsoZeroLocus (I : Ideal R) :
    PrimeSpectrum (R â§¸ I) â‰ƒo (PrimeSpectrum.zeroLocus (R := R) I) :=
  primeSpectrumOrderIsoZeroLocusOfSurj (Quotient.mk I) Quotient.mk_surjective I.mk_ker

/-- `p` is in the image of `Spec S â†’ Spec R` if and only if `p` extended to `S` and
restricted back to `R` is `p`. -/
lemma PrimeSpectrum.mem_range_comap_iff {p : PrimeSpectrum R} :
    p âˆˆ Set.range f.specComap â†” (p.asIdeal.map f).comap f = p.asIdeal := by
  refine âŸ¨fun âŸ¨q, hqâŸ© â†¦ by simp [â† hq], ?_âŸ©
  rw [Ideal.comap_map_eq_self_iff_of_isPrime]
  rintro âŸ¨q, _, hqâŸ©
  exact âŸ¨âŸ¨q, inferInstanceâŸ©, PrimeSpectrum.ext hqâŸ©

open TensorProduct

/-- A prime `p` is in the range of `Spec S â†’ Spec R` if the fiber over `p` is nontrivial. -/
lemma PrimeSpectrum.nontrivial_iff_mem_rangeComap {S : Type*} [CommRing S]
    [Algebra R S] (p : PrimeSpectrum R) :
    Nontrivial (p.asIdeal.ResidueField âŠ—[R] S) â†” p âˆˆ Set.range (algebraMap R S).specComap := by
  let k := p.asIdeal.ResidueField
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· obtain âŸ¨m, hmâŸ© := Ideal.exists_maximal (k âŠ—[R] S)
    use (Algebra.TensorProduct.includeRight).specComap âŸ¨m, hm.isPrimeâŸ©
    ext : 1
    rw [â† PrimeSpectrum.specComap_comp_apply,
      â† Algebra.TensorProduct.includeLeftRingHom_comp_algebraMap, specComap_comp_apply]
    simp [Ideal.eq_bot_of_prime, k, â† RingHom.ker_eq_comap_bot]
  Â· obtain âŸ¨q, rflâŸ© := h
    let f : k âŠ—[R] S â†’â‚[R] q.asIdeal.ResidueField :=
      Algebra.TensorProduct.lift (Ideal.ResidueField.mapâ‚ _ _ rfl)
        (IsScalarTower.toAlgHom _ _ _) (fun _ _ â†¦ Commute.all ..)
    exact RingHom.domain_nontrivial f.toRingHom

end SpecOfSurjective

section ResidueField

variable {R : Type*} [CommRing R]

lemma PrimeSpectrum.residueField_specComap (I : PrimeSpectrum R) :
    Set.range (algebraMap R I.asIdeal.ResidueField).specComap = {I} := by
  rw [Set.range_unique, Set.singleton_eq_singleton_iff]
  exact PrimeSpectrum.ext (Ideal.ext fun x â†¦ Ideal.algebraMap_residueField_eq_zero)

end ResidueField

variable {R S} in
theorem IsLocalHom.of_specComap_surjective [CommSemiring R] [CommSemiring S] (f : R â†’+* S)
    (hf : Function.Surjective f.specComap) : IsLocalHom f where
  map_nonunit x hfx := by
    by_contra hx
    obtain âŸ¨p, hp, _âŸ© := exists_max_ideal_of_mem_nonunits hx
    obtain âŸ¨âŸ¨q, hqpâŸ©, hqâŸ© := hf âŸ¨p, hp.isPrimeâŸ©
    simp only [PrimeSpectrum.mk.injEq] at hq
    exact hqp.ne_top (q.eq_top_of_isUnit_mem (q.mem_comap.mp (by rwa [hq])) hfx)
