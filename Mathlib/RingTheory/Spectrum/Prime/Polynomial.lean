/-
Copyright (c) 2024 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.LinearAlgebra.Charpoly.BaseChange
import Mathlib.LinearAlgebra.Eigenspace.Zero
import Mathlib.RingTheory.AdjoinRoot
import Mathlib.RingTheory.LocalRing.ResidueField.Ideal
import Mathlib.RingTheory.Spectrum.Prime.Topology
import Mathlib.RingTheory.TensorProduct.MvPolynomial

/-!

# Prime spectrum of (multivariate) polynomials

Also see `AlgebraicGeometry/AffineSpace` for the affine space over arbitrary schemes.

## Main results
- `isNilpotent_tensor_residueField_iff`:
  If `A` is a finite free `R`-algebra, then `f : A` is nilpotent on `Îº(ğ”­) âŠ— A` for some
  prime `ğ”­ â—ƒ R` if and only if every non-leading coefficient of `charpoly(f)` is in `ğ”­`.
- `Polynomial.exists_image_comap_of_monic`:
  If `g : R[X]` is monic, the image of `Z(g) âˆ© D(f) : Spec R[X]` in `Spec R` is compact open.
- `Polynomial.isOpenMap_comap_C`: The structure map `Spec R[X] â†’ Spec R` is an open map.
- `MvPolynomial.isOpenMap_comap_C`: The structure map `Spec R[XÌ²] â†’ Spec R` is an open map.

-/

open Polynomial TensorProduct PrimeSpectrum

variable {R M A} [CommRing R] [AddCommGroup M] [Module R M] [CommRing A] [Algebra R A]

/-- If `A` is a finite free `R`-algebra, then `f : A` is nilpotent on `Îº(ğ”­) âŠ— A` for some
prime `ğ”­ â—ƒ R` if and only if every non-leading coefficient of `charpoly(f)` is in `ğ”­`. -/
lemma isNilpotent_tensor_residueField_iff
    [Module.Free R A] [Module.Finite R A] (f : A) (I : Ideal R) [I.IsPrime] :
    IsNilpotent (algebraMap A (A âŠ—[R] I.ResidueField) f) â†”
      âˆ€ i < Module.finrank R A, (Algebra.lmul R A f).charpoly.coeff i âˆˆ I := by
  cases subsingleton_or_nontrivial R
  Â· have := (algebraMap R (A âŠ—[R] I.ResidueField)).codomain_trivial
    simp [Subsingleton.elim I âŠ¤, Subsingleton.elim (f âŠ—â‚œ[R] (1 : I.ResidueField)) 0]
  have : Module.finrank I.ResidueField (I.ResidueField âŠ—[R] A) = Module.finrank R A := by
    rw [Module.finrank_tensorProduct, Module.finrank_self, one_mul]
  rw [â† IsNilpotent.map_iff (Algebra.TensorProduct.comm R A I.ResidueField).injective]
  simp only [Algebra.TensorProduct.algebraMap_apply, Algebra.algebraMap_self, RingHom.id_apply,
    Algebra.coe_lmul_eq_mul, Algebra.TensorProduct.comm_tmul]
  rw [â† IsNilpotent.map_iff (Algebra.lmul_injective (R := I.ResidueField)),
    LinearMap.isNilpotent_iff_charpoly, â† Algebra.baseChange_lmul, LinearMap.charpoly_baseChange]
  simp_rw [this, â† ((LinearMap.mul R A) f).charpoly_natDegree]
  constructor
  Â· intro e i hi
    replace e := congr(($e).coeff i)
    simpa only [Algebra.coe_lmul_eq_mul, coeff_map, coeff_X_pow, hi.ne, â†“reduceIte,
      â† RingHom.mem_ker, Ideal.ker_algebraMap_residueField] using e
  Â· intro H
    ext i
    obtain (hi | hi) := eq_or_ne i ((LinearMap.mul R A) f).charpoly.natDegree
    Â· simp only [Algebra.coe_lmul_eq_mul, hi, coeff_map, coeff_X_pow, â†“reduceIte]
      rw [â† Polynomial.leadingCoeff, ((LinearMap.mul R A) f).charpoly_monic, map_one]
    obtain (hi | hi) := lt_or_gt_of_ne hi
    Â· simpa [hi.ne, â† RingHom.mem_ker, Ideal.ker_algebraMap_residueField] using H i hi
    Â· simp [hi.ne', coeff_eq_zero_of_natDegree_lt hi]

namespace PrimeSpectrum

/-- Let `A` be an `R`-algebra.
`ğ”­ : Spec R` is in the image of `Z(I) âˆ© D(f) âŠ† Spec S`
if and only if `f` is not nilpotent on `Îº(ğ”­) âŠ— A â§¸ I`. -/
lemma mem_image_comap_zeroLocus_sdiff (f : A) (s : Set A) (x) :
    x âˆˆ comap (algebraMap R A) '' (zeroLocus s \ zeroLocus {f}) â†”
      Â¬ IsNilpotent (algebraMap A ((A â§¸ Ideal.span s) âŠ—[R] x.asIdeal.ResidueField) f) := by
  constructor
  Â· rintro âŸ¨q, âŸ¨hqg, hqfâŸ©, rflâŸ© H
    simp only [mem_zeroLocus, Set.singleton_subset_iff, SetLike.mem_coe] at hqg hqf
    have hs : Ideal.span s â‰¤ RingHom.ker (algebraMap A q.asIdeal.ResidueField) := by
      rwa [Ideal.span_le, Ideal.ker_algebraMap_residueField]
    let F : (A â§¸ Ideal.span s) âŠ—[R] (q.asIdeal.comap (algebraMap R A)).ResidueField â†’â‚[A]
        q.asIdeal.ResidueField :=
      Algebra.TensorProduct.lift
        (Ideal.Quotient.liftâ‚ (Ideal.span s) (Algebra.ofId A _) hs)
        (Ideal.ResidueField.mapâ‚ _ _ rfl)
        fun _ _ â†¦ .all _ _
    have := H.map F
    rw [AlgHom.commutes, isNilpotent_iff_eq_zero, â† RingHom.mem_ker,
      Ideal.ker_algebraMap_residueField] at this
    exact hqf this
  Â· intro H
    rw [â† mem_nilradical, nilradical_eq_sInf, Ideal.mem_sInf] at H
    simp only [Set.mem_setOf_eq, Algebra.TensorProduct.algebraMap_apply,
      Ideal.Quotient.algebraMap_eq, not_forall] at H
    obtain âŸ¨q, hq, hfqâŸ© := H
    have : âˆ€ a âˆˆ s, Ideal.Quotient.mk (Ideal.span s) a âŠ—â‚œ[R] 1 âˆˆ q := fun a ha â†¦ by
      simp [Ideal.Quotient.eq_zero_iff_mem.mpr (Ideal.subset_span ha)]
    refine âŸ¨comap (algebraMap A _) âŸ¨q, hqâŸ©, âŸ¨by simpa [Set.subset_def], by simpaâŸ©, ?_âŸ©
    rw [â† comap_comp_apply, â† IsScalarTower.algebraMap_eq,
      â† Algebra.TensorProduct.includeRight.comp_algebraMap, comap_comp_apply,
      Subsingleton.elim (Î± := PrimeSpectrum x.asIdeal.ResidueField) (comap _ _) âŠ¥]
    ext a
    exact congr(a âˆˆ $(Ideal.ker_algebraMap_residueField _))

/-- Let `A` be an `R`-algebra.
`ğ”­ : Spec R` is in the image of `D(f) âŠ† Spec S`
if and only if `f` is not nilpotent on `Îº(ğ”­) âŠ— A`. -/
lemma mem_image_comap_basicOpen (f : A) (x) :
    x âˆˆ comap (algebraMap R A) '' basicOpen f â†”
      Â¬ IsNilpotent (algebraMap A (A âŠ—[R] x.asIdeal.ResidueField) f) := by
  have e : A âŠ—[R] x.asIdeal.ResidueField â‰ƒâ‚[A]
      (A â§¸ (Ideal.span âˆ… : Ideal A)) âŠ—[R] x.asIdeal.ResidueField := by
    refine Algebra.TensorProduct.congr ?f AlgEquiv.refl
    rw [Ideal.span_empty]
    exact { __ := (RingEquiv.quotientBot A).symm, map_smul' _ _ := rfl }
  rw [â† IsNilpotent.map_iff e.injective, AlgEquiv.commutes,
    â† mem_image_comap_zeroLocus_sdiff f âˆ… x, zeroLocus_empty, â† Set.compl_eq_univ_diff,
    basicOpen_eq_zeroLocus_compl]

/-- Let `A` be an `R`-algebra. If `A â§¸ I` is finite free over `R`,
then the image of `Z(I) âˆ© D(f) âŠ† Spec S` in `Spec R` is compact open. -/
lemma exists_image_comap_of_finite_of_free (f : A) (s : Set A)
    [Module.Finite R (A â§¸ Ideal.span s)] [Module.Free R (A â§¸ Ideal.span s)] :
    âˆƒ t : Finset R, comap (algebraMap R A) '' (zeroLocus s \ zeroLocus {f}) = (zeroLocus t)á¶œ := by
  classical
  use (Finset.range (Module.finrank R (A â§¸ Ideal.span s))).image
    (Algebra.lmul R (A â§¸ Ideal.span s) (Ideal.Quotient.mk _ f)).charpoly.coeff
  ext x
  rw [mem_image_comap_zeroLocus_sdiff, IsScalarTower.algebraMap_apply A (A â§¸ Ideal.span s),
    isNilpotent_tensor_residueField_iff]
  simp [Set.subset_def]

end PrimeSpectrum

namespace Polynomial

lemma mem_image_comap_C_basicOpen (f : R[X]) (x : PrimeSpectrum R) :
    x âˆˆ comap C '' basicOpen f â†” âˆƒ i, f.coeff i âˆ‰ x.asIdeal := by
  trans f.map (algebraMap R x.asIdeal.ResidueField) â‰  0
  Â· refine (mem_image_comap_basicOpen _ _).trans (not_iff_not.mpr ?_)
    let e : R[X] âŠ—[R] x.asIdeal.ResidueField â‰ƒâ‚[R] x.asIdeal.ResidueField[X] :=
      (Algebra.TensorProduct.comm R _ _).trans (polyEquivTensor R x.asIdeal.ResidueField).symm
    rw [â† IsNilpotent.map_iff e.injective, isNilpotent_iff_eq_zero]
    change (e.toAlgHom.toRingHom).comp (algebraMap _ _) f = 0 â†” Polynomial.mapRingHom _ f = 0
    congr!
    ext1
    Â· ext; simp [e]
    Â· simp [e]
  Â· simp [Polynomial.ext_iff]

lemma image_comap_C_basicOpen (f : R[X]) :
    comap C '' basicOpen f = (zeroLocus (Set.range f.coeff))á¶œ := by
  ext p
  rw [mem_image_comap_C_basicOpen]
  simp [Set.range_subset_iff]

lemma isOpenMap_comap_C : IsOpenMap (comap (R := R) C) := by
  intro U hU
  obtain âŸ¨S, hS, rflâŸ© := isTopologicalBasis_basic_opens.open_eq_sUnion hU
  rw [Set.image_sUnion]
  apply isOpen_sUnion
  rintro _ âŸ¨t, ht, rflâŸ©
  obtain âŸ¨r, rflâŸ© := hS ht
  simp only [image_comap_C_basicOpen]
  exact (isClosed_zeroLocus _).isOpen_compl

lemma comap_C_surjective : Function.Surjective (comap (R := R) C) := by
  intro x
  refine âŸ¨comap (evalRingHom 0) x, ?_âŸ©
  rw [â† comap_comp_apply, (show (evalRingHom 0).comp C = .id R by ext; simp),
    comap_id, ContinuousMap.id_apply]

lemma exists_image_comap_of_monic (f g : R[X]) (hg : g.Monic) :
    âˆƒ t : Finset R, comap C '' (zeroLocus {g} \ zeroLocus {f}) = (zeroLocus t)á¶œ := by
  apply (config := { allowSynthFailures := true }) exists_image_comap_of_finite_of_free
  Â· exact .of_basis (AdjoinRoot.powerBasis' hg).basis
  Â· exact .of_basis (AdjoinRoot.powerBasis' hg).basis

lemma isCompact_image_comap_of_monic (f g : R[X]) (hg : g.Monic) :
    IsCompact (comap C '' (zeroLocus {g} \ zeroLocus {f})) := by
  obtain âŸ¨t, htâŸ© := exists_image_comap_of_monic f g hg
  rw [ht, â† (t : Set R).iUnion_of_singleton_coe, zeroLocus_iUnion, Set.compl_iInter]
  apply isCompact_iUnion
  exact fun _ â†¦ by simpa using isCompact_basicOpen _

lemma isOpen_image_comap_of_monic (f g : R[X]) (hg : g.Monic) :
    IsOpen (comap C '' (zeroLocus {g} \ zeroLocus {f})) := by
  obtain âŸ¨t, htâŸ© := exists_image_comap_of_monic f g hg
  rw [ht]
  exact (isClosed_zeroLocus (R := R) t).isOpen_compl

end Polynomial

namespace MvPolynomial

variable {Ïƒ : Type*}

lemma mem_image_comap_C_basicOpen (f : MvPolynomial Ïƒ R) (x : PrimeSpectrum R) :
    x âˆˆ comap (C (Ïƒ := Ïƒ)) '' basicOpen f â†” âˆƒ i, f.coeff i âˆ‰ x.asIdeal := by
  classical
  trans f.map (algebraMap R x.asIdeal.ResidueField) â‰  0
  Â· refine (mem_image_comap_basicOpen _ _).trans (not_iff_not.mpr ?_)
    let e : MvPolynomial Ïƒ R âŠ—[R] x.asIdeal.ResidueField â‰ƒâ‚[R]
        MvPolynomial Ïƒ x.asIdeal.ResidueField := scalarRTensorAlgEquiv
    rw [â† IsNilpotent.map_iff e.injective, isNilpotent_iff_eq_zero]
    change (e.toAlgHom.toRingHom).comp (algebraMap _ _) f = 0 â†” MvPolynomial.map _ f = 0
    congr!
    ext
    Â· simp [scalarRTensorAlgEquiv, e, coeff_map,
        Algebra.smul_def, apply_ite (f := algebraMap _ _)]
    Â· simp [e, scalarRTensorAlgEquiv, coeff_map, coeff_X']
  Â· simp [MvPolynomial.ext_iff, coeff_map]

lemma image_comap_C_basicOpen (f : MvPolynomial Ïƒ R) :
    comap (C (Ïƒ := Ïƒ)) '' basicOpen f = (zeroLocus (Set.range f.coeff))á¶œ := by
  ext p
  rw [mem_image_comap_C_basicOpen]
  simp [Set.range_subset_iff]

lemma isOpenMap_comap_C : IsOpenMap (comap (R := R) (C (Ïƒ := Ïƒ))) := by
  intro U hU
  obtain âŸ¨S, hS, rflâŸ© := isTopologicalBasis_basic_opens.open_eq_sUnion hU
  rw [Set.image_sUnion]
  apply isOpen_sUnion
  rintro _ âŸ¨t, ht, rflâŸ©
  obtain âŸ¨r, rflâŸ© := hS ht
  simp only [image_comap_C_basicOpen]
  exact (isClosed_zeroLocus _).isOpen_compl

lemma comap_C_surjective : Function.Surjective (comap (R := R) (C (Ïƒ := Ïƒ))) := by
  intro x
  refine âŸ¨comap (evalâ‚‚Hom (.id _) 0) x, ?_âŸ©
  rw [â† comap_comp_apply, (show (evalâ‚‚Hom (.id _) 0).comp C = .id R by ext; simp),
    comap_id, ContinuousMap.id_apply]

end MvPolynomial
