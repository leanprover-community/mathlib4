/-
Copyright (c) 2019 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/
import Mathlib.RingTheory.Polynomial.Basic
import Mathlib.RingTheory.PrincipalIdealDomain
import Mathlib.Data.MvPolynomial.Basic

#align_import ring_theory.adjoin.fg from "leanprover-community/mathlib"@"c4658a649d216f57e99621708b09dcb3dcccbd23"

/-!
# Adjoining elements to form subalgebras

This file develops the basic theory of finitely-generated subalgebras.

## Definitions

* `FG (S : Subalgebra R A)` : A predicate saying that the subalgebra is finitely-generated
as an A-algebra

## Tags

adjoin, algebra, finitely-generated algebra

-/


universe u v w

open Subsemiring Ring Submodule

open Pointwise

namespace Algebra

variable {R : Type u} {A : Type v} {B : Type w} [CommSemiring R] [CommSemiring A] [Algebra R A]
  {s t : Set A}

theorem fg_trans (h1 : (adjoin R s).toSubmodule.FG) (h2 : (adjoin (adjoin R s) t).toSubmodule.FG) :
    (adjoin R (s âˆª t)).toSubmodule.FG := by
  rcases fg_def.1 h1 with âŸ¨p, hp, hp'âŸ©
  -- âŠ¢ FG (â†‘Subalgebra.toSubmodule (adjoin R (s âˆª t)))
  rcases fg_def.1 h2 with âŸ¨q, hq, hq'âŸ©
  -- âŠ¢ FG (â†‘Subalgebra.toSubmodule (adjoin R (s âˆª t)))
  refine' fg_def.2 âŸ¨p * q, hp.mul hq, le_antisymm _ _âŸ©
  -- âŠ¢ span R (p * q) â‰¤ â†‘Subalgebra.toSubmodule (adjoin R (s âˆª t))
  Â· rw [span_le]
    -- âŠ¢ p * q âŠ† â†‘(â†‘Subalgebra.toSubmodule (adjoin R (s âˆª t)))
    rintro _ âŸ¨x, y, hx, hy, rflâŸ©
    -- âŠ¢ (fun x x_1 => x * x_1) x y âˆˆ â†‘(â†‘Subalgebra.toSubmodule (adjoin R (s âˆª t)))
    change x * y âˆˆ adjoin R (s âˆª t)
    -- âŠ¢ x * y âˆˆ adjoin R (s âˆª t)
    refine' Subalgebra.mul_mem _ _ _
    -- âŠ¢ x âˆˆ adjoin R (s âˆª t)
    Â· have : x âˆˆ Subalgebra.toSubmodule (adjoin R s) := by
        rw [â† hp']
        exact subset_span hx
      exact adjoin_mono (Set.subset_union_left _ _) this
      -- ðŸŽ‰ no goals
    have : y âˆˆ Subalgebra.toSubmodule (adjoin (adjoin R s) t) := by
      rw [â† hq']
      exact subset_span hy
    change y âˆˆ adjoin R (s âˆª t)
    -- âŠ¢ y âˆˆ adjoin R (s âˆª t)
    rwa [adjoin_union_eq_adjoin_adjoin]
    -- ðŸŽ‰ no goals
  Â· intro r hr
    -- âŠ¢ r âˆˆ span R (p * q)
    change r âˆˆ adjoin R (s âˆª t) at hr
    -- âŠ¢ r âˆˆ span R (p * q)
    rw [adjoin_union_eq_adjoin_adjoin] at hr
    -- âŠ¢ r âˆˆ span R (p * q)
    change r âˆˆ Subalgebra.toSubmodule (adjoin (adjoin R s) t) at hr
    -- âŠ¢ r âˆˆ span R (p * q)
    rw [â† hq', â† Set.image_id q, Finsupp.mem_span_image_iff_total (adjoin R s)] at hr
    -- âŠ¢ r âˆˆ span R (p * q)
    rcases hr with âŸ¨l, hlq, rflâŸ©
    -- âŠ¢ â†‘(Finsupp.total A A { x // x âˆˆ adjoin R s } _root_.id) l âˆˆ span R (p * q)
    have := @Finsupp.total_apply A A (adjoin R s)
    -- âŠ¢ â†‘(Finsupp.total A A { x // x âˆˆ adjoin R s } _root_.id) l âˆˆ span R (p * q)
    rw [this, Finsupp.sum]
    -- âŠ¢ (Finset.sum l.support fun a => â†‘l a â€¢ _root_.id a) âˆˆ span R (p * q)
    refine' sum_mem _
    -- âŠ¢ âˆ€ (c : A), c âˆˆ l.support â†’ â†‘l c â€¢ _root_.id c âˆˆ span R (p * q)
    intro z hz
    -- âŠ¢ â†‘l z â€¢ _root_.id z âˆˆ span R (p * q)
    change (l z).1 * _ âˆˆ _
    -- âŠ¢ â†‘(â†‘l z) * _root_.id z âˆˆ span R (p * q)
    have : (l z).1 âˆˆ Subalgebra.toSubmodule (adjoin R s) := (l z).2
    -- âŠ¢ â†‘(â†‘l z) * _root_.id z âˆˆ span R (p * q)
    rw [â† hp', â† Set.image_id p, Finsupp.mem_span_image_iff_total R] at this
    -- âŠ¢ â†‘(â†‘l z) * _root_.id z âˆˆ span R (p * q)
    rcases this with âŸ¨l2, hlp, hlâŸ©
    -- âŠ¢ â†‘(â†‘l z) * _root_.id z âˆˆ span R (p * q)
    have := @Finsupp.total_apply A A R
    -- âŠ¢ â†‘(â†‘l z) * _root_.id z âˆˆ span R (p * q)
    rw [this] at hl
    -- âŠ¢ â†‘(â†‘l z) * _root_.id z âˆˆ span R (p * q)
    rw [â† hl, Finsupp.sum_mul]
    -- âŠ¢ (Finsupp.sum l2 fun a c => c â€¢ _root_.id a * _root_.id z) âˆˆ span R (p * q)
    refine' sum_mem _
    -- âŠ¢ âˆ€ (c : A), c âˆˆ l2.support â†’ (fun a c => c â€¢ _root_.id a * _root_.id z) c (â†‘l â€¦
    intro t ht
    -- âŠ¢ (fun a c => c â€¢ _root_.id a * _root_.id z) t (â†‘l2 t) âˆˆ span R (p * q)
    change _ * _ âˆˆ _
    -- âŠ¢ â†‘l2 t â€¢ _root_.id t * _root_.id z âˆˆ span R (p * q)
    rw [smul_mul_assoc]
    -- âŠ¢ â†‘l2 t â€¢ (_root_.id t * _root_.id z) âˆˆ span R (p * q)
    refine' smul_mem _ _ _
    -- âŠ¢ _root_.id t * _root_.id z âˆˆ span R (p * q)
    exact subset_span âŸ¨t, z, hlp ht, hlq hz, rflâŸ©
    -- ðŸŽ‰ no goals
#align algebra.fg_trans Algebra.fg_trans

end Algebra

namespace Subalgebra

variable {R : Type u} {A : Type v} {B : Type w}

variable [CommSemiring R] [Semiring A] [Algebra R A] [Semiring B] [Algebra R B]

/-- A subalgebra `S` is finitely generated if there exists `t : Finset A` such that
`Algebra.adjoin R t = S`. -/
def FG (S : Subalgebra R A) : Prop :=
  âˆƒ t : Finset A, Algebra.adjoin R â†‘t = S
#align subalgebra.fg Subalgebra.FG

theorem fg_adjoin_finset (s : Finset A) : (Algebra.adjoin R (â†‘s : Set A)).FG :=
  âŸ¨s, rflâŸ©
#align subalgebra.fg_adjoin_finset Subalgebra.fg_adjoin_finset

theorem fg_def {S : Subalgebra R A} : S.FG â†” âˆƒ t : Set A, Set.Finite t âˆ§ Algebra.adjoin R t = S :=
  Iff.symm Set.exists_finite_iff_finset
#align subalgebra.fg_def Subalgebra.fg_def

theorem fg_bot : (âŠ¥ : Subalgebra R A).FG :=
  âŸ¨âˆ…, Finset.coe_empty â–¸ Algebra.adjoin_empty R AâŸ©
#align subalgebra.fg_bot Subalgebra.fg_bot

theorem fg_of_fg_toSubmodule {S : Subalgebra R A} : S.toSubmodule.FG â†’ S.FG :=
  fun âŸ¨t, htâŸ© â†¦ âŸ¨t, le_antisymm
    (Algebra.adjoin_le fun x hx â†¦ show x âˆˆ Subalgebra.toSubmodule S from ht â–¸ subset_span hx) <|
    show Subalgebra.toSubmodule S â‰¤ Subalgebra.toSubmodule (Algebra.adjoin R â†‘t) from fun x hx â†¦
      span_le.mpr (fun x hx â†¦ Algebra.subset_adjoin hx)
        (show x âˆˆ span R â†‘t by
          rw [ht]
          -- âŠ¢ x âˆˆ â†‘toSubmodule S
          exact hx)âŸ©
          -- ðŸŽ‰ no goals
#align subalgebra.fg_of_fg_to_submodule Subalgebra.fg_of_fg_toSubmodule

theorem fg_of_noetherian [IsNoetherian R A] (S : Subalgebra R A) : S.FG :=
  fg_of_fg_toSubmodule (IsNoetherian.noetherian (Subalgebra.toSubmodule S))
#align subalgebra.fg_of_noetherian Subalgebra.fg_of_noetherian

theorem fg_of_submodule_fg (h : (âŠ¤ : Submodule R A).FG) : (âŠ¤ : Subalgebra R A).FG :=
  let âŸ¨s, hsâŸ© := h
  âŸ¨s, toSubmodule.injective <| by
    rw [Algebra.top_toSubmodule, eq_top_iff, â† hs, span_le]
    -- âŠ¢ â†‘s âŠ† â†‘(â†‘toSubmodule (Algebra.adjoin R â†‘s))
    exact Algebra.subset_adjoinâŸ©
    -- ðŸŽ‰ no goals
#align subalgebra.fg_of_submodule_fg Subalgebra.fg_of_submodule_fg

theorem FG.prod {S : Subalgebra R A} {T : Subalgebra R B} (hS : S.FG) (hT : T.FG) :
    (S.prod T).FG := by
  obtain âŸ¨s, hsâŸ© := fg_def.1 hS
  -- âŠ¢ FG (Subalgebra.prod S T)
  obtain âŸ¨t, htâŸ© := fg_def.1 hT
  -- âŠ¢ FG (Subalgebra.prod S T)
  rw [â† hs.2, â† ht.2]
  -- âŠ¢ FG (Subalgebra.prod (Algebra.adjoin R s) (Algebra.adjoin R t))
  exact fg_def.2 âŸ¨LinearMap.inl R A B '' (s âˆª {1}) âˆª LinearMap.inr R A B '' (t âˆª {1}),
    Set.Finite.union (Set.Finite.image _ (Set.Finite.union hs.1 (Set.finite_singleton _)))
      (Set.Finite.image _ (Set.Finite.union ht.1 (Set.finite_singleton _))),
    Algebra.adjoin_inl_union_inr_eq_prod R s tâŸ©
#align subalgebra.fg.prod Subalgebra.FG.prod

section

open Classical

theorem FG.map {S : Subalgebra R A} (f : A â†’â‚[R] B) (hs : S.FG) : (S.map f).FG :=
  let âŸ¨s, hsâŸ© := hs
  âŸ¨s.image f, by rw [Finset.coe_image, Algebra.adjoin_image, hs]âŸ©
                 -- ðŸŽ‰ no goals
#align subalgebra.fg.map Subalgebra.FG.map

end

theorem fg_of_fg_map (S : Subalgebra R A) (f : A â†’â‚[R] B) (hf : Function.Injective f)
    (hs : (S.map f).FG) : S.FG :=
  let âŸ¨s, hsâŸ© := hs
  âŸ¨s.preimage f fun _ _ _ _ h â†¦ hf h,
    map_injective hf <| by
      rw [â† Algebra.adjoin_image, Finset.coe_preimage, Set.image_preimage_eq_of_subset, hs]
      -- âŠ¢ â†‘s âŠ† Set.range â†‘f
      rw [â† AlgHom.coe_range, â† Algebra.adjoin_le_iff, hs, â† Algebra.map_top]
      -- âŠ¢ map f S â‰¤ map f âŠ¤
      exact map_mono le_topâŸ©
      -- ðŸŽ‰ no goals
#align subalgebra.fg_of_fg_map Subalgebra.fg_of_fg_map

theorem fg_top (S : Subalgebra R A) : (âŠ¤ : Subalgebra R S).FG â†” S.FG :=
  âŸ¨fun h â†¦ by
    rw [â† S.range_val, â† Algebra.map_top]
    -- âŠ¢ FG (map (val S) âŠ¤)
    exact FG.map _ h, fun h â†¦
    -- ðŸŽ‰ no goals
    fg_of_fg_map _ S.val Subtype.val_injective <| by
      rw [Algebra.map_top, range_val]
      -- âŠ¢ FG S
      exact hâŸ©
      -- ðŸŽ‰ no goals
#align subalgebra.fg_top Subalgebra.fg_top

theorem induction_on_adjoin [IsNoetherian R A] (P : Subalgebra R A â†’ Prop) (base : P âŠ¥)
    (ih : âˆ€ (S : Subalgebra R A) (x : A), P S â†’ P (Algebra.adjoin R (insert x S)))
    (S : Subalgebra R A) : P S := by
  classical
  obtain âŸ¨t, rflâŸ© := S.fg_of_noetherian
  refine' Finset.induction_on t _ _
  Â· simpa using base
  intro x t _ h
  rw [Finset.coe_insert]
  simpa only [Algebra.adjoin_insert_adjoin] using ih _ x h
#align subalgebra.induction_on_adjoin Subalgebra.induction_on_adjoin

end Subalgebra

section Semiring

variable {R : Type u} {A : Type v} {B : Type w}

variable [CommSemiring R] [CommRing A] [CommRing B] [Algebra R A] [Algebra R B]

/-- The image of a Noetherian R-algebra under an R-algebra map is a Noetherian ring. -/
instance AlgHom.isNoetherianRing_range (f : A â†’â‚[R] B) [IsNoetherianRing A] :
    IsNoetherianRing f.range :=
  _root_.isNoetherianRing_range f.toRingHom
#align alg_hom.is_noetherian_ring_range AlgHom.isNoetherianRing_range

end Semiring

section Ring

variable {R : Type u} {A : Type v} {B : Type w}

variable [CommRing R] [CommRing A] [CommRing B] [Algebra R A] [Algebra R B]

theorem isNoetherianRing_of_fg {S : Subalgebra R A} (HS : S.FG) [IsNoetherianRing R] :
    IsNoetherianRing S :=
  let âŸ¨t, htâŸ© := HS
  ht â–¸ (Algebra.adjoin_eq_range R (â†‘t : Set A)).symm â–¸ AlgHom.isNoetherianRing_range _
#align is_noetherian_ring_of_fg isNoetherianRing_of_fg

theorem is_noetherian_subring_closure (s : Set R) (hs : s.Finite) :
    IsNoetherianRing (Subring.closure s) :=
  show IsNoetherianRing (subalgebraOfSubring (Subring.closure s)) from
    Algebra.adjoin_int s â–¸ isNoetherianRing_of_fg (Subalgebra.fg_def.2 âŸ¨s, hs, rflâŸ©)
#align is_noetherian_subring_closure is_noetherian_subring_closure

end Ring
