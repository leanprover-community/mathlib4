/-
Copyright (c) 2025 Jiedong Jiang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jiedong Jiang
-/
module

public import Mathlib.RingTheory.AdicCompletion.RingHom
public import Mathlib.RingTheory.Perfectoid.Untilt
public import Mathlib.RingTheory.WittVector.TeichmullerSeries

/-!
# Fontaine's Œ∏ map
In this file, we define Fontaine's `Œ∏` map, which is a ring
homomorphism from the Witt vector `ùïé(R‚ô≠)` of the tilt of a perfectoid ring `R`
to `R` itself. Our definition of `Œ∏` does not require that `R` is perfectoid in the first place.
We only need `R` to be `p`-adically complete.

## Main definitions
* `fontaineTheta` : Fontaine's Œ∏ map, which is a ring homomorphism from `ùïé R‚ô≠` to `R`.

## Tags
Fontaine's theta map, perfectoid theory, p-adic Hodge theory

## Reference

* [Fontaine, *Sur Certains Types de Repr√©sentations p-Adiques du Groupe de Galois d'un Corps Local;
Construction d'un Anneau de Barsotti-Tate*][fontaine1982certains]
* [Fontaine, *Le corps des p√©riodes p-adiques*][fontaine1994corps]

-/

@[expose] public section

universe u

open Ideal Quotient PreTilt WittVector

noncomputable section

variable {R : Type u} [CommRing R] {p : ‚Ñï} [Fact p.Prime]

local notation "ùïé " A:100 => WittVector p A
local notation A "‚ô≠" => PreTilt A p

namespace WittVector

/-!
## Œ∏ as a ring homomorphism
In this section, we first define the ring homomorphism
`fontaineThetaModPPow : ùïé (R‚ô≠) ‚Üí+* R ‚ß∏ span {(p : R)} ^ (n + 1)`.
Then we show they are compatible with each other and lift to a
ring homomorphism `fontaineTheta : ùïé (R‚ô≠) ‚Üí+* R`.

To prove this, we define `fontaineThetaModPPow`
as a composition of the following ring homomorphisms.
`ùïé(R‚ô≠) --ùïé(Frob^-n)->  ùïé(R‚ô≠) --ùïé(coeff 0)-> ùïé(R/p) --gh_n-> R/p^(n+1)`

Here, the ring map `gh_n` fits in the following diagram.

```
ùïé(A)  --ghost_n->   A
|                   |
v                   v
ùïé(A/p) --gh_n-> A/p^(n+1)
```
-/

theorem ker_map_le_ker_mk_comp_ghostComponent (n : ‚Ñï) :
    RingHom.ker (WittVector.map <| Ideal.Quotient.mk <| span {(p : R)}) ‚â§
    RingHom.ker (((Ideal.Quotient.mk <| span {(p : R)} ^ (n + 1))).comp
    (WittVector.ghostComponent (p := p) n)) := by
  intro x
  simp only [RingHom.mem_ker, map_eq_zero_iff, RingHom.comp_apply]
  intro h
  simp only [ghostComponent]
  apply_fun Ideal.quotEquivOfEq (Ideal.span_singleton_pow (p : R) (n + 1))
  simp only [RingHom.coe_comp, Function.comp_apply, Pi.evalRingHom_apply, ghostMap_apply,
    quotEquivOfEq_mk, map_zero]
  simp only [eq_zero_iff_dvd] at h ‚ä¢
  exact pow_dvd_ghostComponent_of_dvd_coeff (fun _ _ ‚Ü¶ h _)

/--
The lift ring map `gh_n : ùïé(A/p) ‚Üí+* A/p^(n+1)` of the `n`-th ghost component
`ùïé(A) ‚Üí+* A` along the surjective ring map `ùïé(A) ‚Üí+* ùïé(A/p)`.
-/
def ghostComponentModPPow (n : ‚Ñï) : ùïé (R ‚ß∏ span {(p : R)}) ‚Üí+* R ‚ß∏ span {(p : R)}^(n + 1) :=
  RingHom.liftOfSurjective (WittVector.map <| Ideal.Quotient.mk <| span {(p : R)})
    (map_surjective _ Ideal.Quotient.mk_surjective)
    ‚ü®((Ideal.Quotient.mk <| span {(p : R)} ^ (n + 1))).comp
    (WittVector.ghostComponent (p := p) n), ker_map_le_ker_mk_comp_ghostComponent n‚ü©

@[simp]
theorem quotEquivOfEq_ghostComponentModPPow (x : ùïé (R ‚ß∏ span {(p : R)}))
    (h : span {(p : R)} ^ (0 + 1) = span {(p : R)}) : quotEquivOfEq h (ghostComponentModPPow 0 x) =
    ghostComponent 0 x := by
  simp only [Nat.reduceAdd, ghostComponentModPPow, RingHom.liftOfSurjective]
  have surj : Function.Surjective (WittVector.map (p := p) (Ideal.Quotient.mk (span {(p : R)}))) :=
    map_surjective _ Ideal.Quotient.mk_surjective
  choose y hy using surj x
  have := RingHom.liftOfSurjective_comp_apply
      (WittVector.map (p := p) (Ideal.Quotient.mk (span {(p : R)})))
      surj ‚ü®((Ideal.Quotient.mk <| span {(p : R)} ^ (0 + 1))).comp
      (WittVector.ghostComponent (p := p) 0), ker_map_le_ker_mk_comp_ghostComponent 0‚ü© y
  rw [hy] at this
  rw [this]
  simp [‚Üê hy, ghostComponent_apply]

variable [Fact ¬¨IsUnit (p : R)] [IsAdicComplete (span {(p : R)}) R]

@[simp]
theorem ghostComponentModPPow_teichmuller_coeff (n : ‚Ñï) (x : R‚ô≠) :
    ghostComponentModPPow n (teichmuller p (Perfection.coeff (ModP R p) _ n x)) =
    Ideal.Quotient.mk (span {(p : R)} ^ (n + 1)) x.untilt := by
  simpa [ghostComponentModPPow] using RingHom.liftOfSurjective_comp_apply
      (WittVector.map <| Ideal.Quotient.mk <| span {(p : R)})
      (map_surjective _ Ideal.Quotient.mk_surjective)
      ‚ü®((Ideal.Quotient.mk <| span {(p : R)} ^ (n + 1))).comp
      (WittVector.ghostComponent (p := p) n), ker_map_le_ker_mk_comp_ghostComponent n‚ü©
      (teichmuller p ((((_root_.frobeniusEquiv _ p).symm ^ n) x).untilt))

variable (R p) in
/--
The Fontaine's theta map modulo `p^(n+1)`.
It is the composition of the following ring homomorphisms.
`ùïé(R‚ô≠) --ùïé(Frob^-n)-> ùïé(R‚ô≠) --ùïé(coeff 0)-> ùïé(R/p) --gh_n-> O/p^(n+1)`
-/
def fontaineThetaModPPow (n : ‚Ñï) : ùïé (R‚ô≠) ‚Üí+* R ‚ß∏ span {(p : R)} ^ (n + 1) :=
  (ghostComponentModPPow n).comp
      (((WittVector.map (Perfection.coeff _ p 0))).comp
          (WittVector.map ((_root_.frobeniusEquiv (R‚ô≠) p).symm ^ n : R‚ô≠ ‚Üí+* R‚ô≠)))

@[simp]
theorem fontaineThetaModPPow_teichmuller (n : ‚Ñï) (x : R‚ô≠) :
    fontaineThetaModPPow R p n (teichmuller p x) = Ideal.Quotient.mk _ x.untilt := by
  simp [fontaineThetaModPPow, PreTilt]

theorem factorPowSucc_comp_fontaineThetaModPPow (n : ‚Ñï) :
    (factorPowSucc _ (n + 1)).comp (fontaineThetaModPPow R p (n + 1)) =
    fontaineThetaModPPow R p n:= by
  apply eq_of_apply_teichmuller_eq
      ((factorPowSucc _ (n + 1)).comp (fontaineThetaModPPow R p (n + 1)))
      (fontaineThetaModPPow R p n)
  ¬∑ use n + 1
    have : (p : (R ‚ß∏ span {(p : R)} ^ (n + 1))) =
        Ideal.Quotient.mk (span {(p : R)} ^ (n + 1)) (p : R) := by
      simp only [map_natCast]
    rw [this, ‚Üê map_pow, Ideal.Quotient.eq_zero_iff_mem]
    exact Ideal.pow_mem_pow (mem_span_singleton_self _) _
  intro (x : Ring.Perfection (ModP R p) p)
  simp [PreTilt, fontaineThetaModPPow]

theorem factorPowSucc_fontaineThetaModPPow_eq (n : ‚Ñï) (x : ùïé (R‚ô≠)) :
    (factorPowSucc _ (n + 1)) ((fontaineThetaModPPow R p (n + 1)) x) =
    fontaineThetaModPPow R p n x:= by
  simp [‚Üê factorPowSucc_comp_fontaineThetaModPPow n]

open IsAdicComplete

variable (R p) in
/--
The Fontaine's Œ∏ map from `ùïé(R‚ô≠)` to `O`.
It is the limit of the ring maps `fontaineThetaModPPow n` from `ùïé(R‚ô≠)` `O/p^(n+1)`.
-/
def fontaineTheta : ùïé (R‚ô≠) ‚Üí+* R :=
  Order.succ_strictMono.liftRingHom (span {(p : R)}) _ (factorPowSucc_comp_fontaineThetaModPPow _)

theorem mk_pow_fontaineTheta (n : ‚Ñï) (x : ùïé (R‚ô≠)) :
    Ideal.Quotient.mk (span {(p : R)} ^ (n + 1)) (fontaineTheta R p x) =
    fontaineThetaModPPow R p n x :=
  Order.succ_strictMono.mk_liftRingHom (span {(p : R)}) _
      (factorPowSucc_comp_fontaineThetaModPPow _) x

theorem mk_fontaineTheta (x : ùïé (R‚ô≠)) :
    Ideal.Quotient.mk (span {(p : R)}) (fontaineTheta R p x) =
    Perfection.coeff (ModP R p) _ 0 (x.coeff 0) := by
  have := mk_pow_fontaineTheta 0 x
  simp only [Nat.reduceAdd] at this
  apply_fun Ideal.quotEquivOfEq (pow_one (p : R) ‚ñ∏ Ideal.span_singleton_pow (p : R) 1) at this
  simp only [quotEquivOfEq_mk] at this
  rw [this]
  simp only [fontaineThetaModPPow, Nat.reduceAdd, pow_zero, RingHom.one_def, WittVector.map_id,
    RingHomCompTriple.comp_eq, RingHom.coe_comp, Function.comp_apply,
    quotEquivOfEq_ghostComponentModPPow, ghostComponent_apply, wittPolynomial_zero,
    MvPolynomial.aeval_X]
  -- Note to reviewers : This line was designed to be
  -- `simp [fontaineThetaModPPow, ghostComponent_apply, RingHom.one_def]`
  -- However, `WittVector.map_coeff` should, but does not work here.
  rfl

@[simp]
theorem fontaineTheta_teichmuller (x : R‚ô≠) : fontaineTheta R p (teichmuller p x) = x.untilt := by
  rw [IsHausdorff.eq_iff_smodEq (I := span {(p : R)})]
  simp only [smul_eq_mul, mul_top]
  intro n
  cases n
  ¬∑ simp
  ¬∑ simp [SModEq, mk_pow_fontaineTheta]

end WittVector
