/-
Copyright (c) 2025 Jiedong Jiang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jiedong Jiang
-/

import Mathlib.RingTheory.Perfectoid.Untilt
import Mathlib.RingTheory.WittVector.TeichmullerSeries

/-!
# Fontaine's Œ∏ map
In this file, we define Fontaine's `Œ∏` map, which is a ring
homomorphism from the Witt vector `ùïé(A^‚ô≠)` of the tilt of a perfectoid ring `O`
to `O` itself. Our definition of `Œ∏` does not require that `O` is perfectoid in the first place.
We only need `O` to be `p`-adically complete.

## Main definitions
* `fontaineTheta` : Fontaine's Œ∏ map, which is a ring homomorphism from `ùïé(O^‚ô≠)` to `O`.
* `BDeRhamPlus` : The period ring `B_{dR}^+`.

## Main theorems
* `fontaineTheta_surjective` : Fontaine's Œ∏ map is surjective.

## Tags
Fontaine's theta map, period rings, perfectoid theory, p-adic Hodge theory

## TODO
Currently, the period ring `B_{dR}^+` takes the ring of integers `O` as the input.
After the perfectoid theory is developed, we should modify it to
take a perfectoid field as the input.
-/

universe u

open Ideal Quotient PreTilt WittVector

-- an one term proof used in the later statement `quotEquivOfEq_ghostComponentModPPow`.
private lemma Ideal.span_pow_zero_add_one {R : Type*} [CommRing R]
    {I : Ideal R} : I ^ (0 + 1) = I := by
  simp

noncomputable section

variable {O : Type u} [CommRing O] {p : ‚Ñï} [Fact (Nat.Prime p)]

local notation A "^‚ô≠" => PreTilt A p
local notation "ùïé" => WittVector p

namespace WittVector

/-!
## Œ∏ as a ring homomorphism
In this section, we first define the ring homomorphism
`fontaineThetaModPPow : ùïé (O^‚ô≠) ‚Üí+* O ‚ß∏ span {(p : O)} ^ (n + 1)`.
Then we show they are compatible with each other and lift to a
ring homomorphism `fontaineTheta : ùïé (O^‚ô≠) ‚Üí+* O`.

To prove this, we define `fontaineThetaModPPow`
as a composition of the following ring homomorphisms.
`ùïé(O^‚ô≠) --ùïé(Frob^-n)->  ùïé(O^‚ô≠) --ùïé(coeff 0)-> ùïé(O/p) --gh_n-> O/p^(n+1)`

Here, the ring map `gh_n` fits in the following diagram.

```
ùïé(A)  --ghost_n->   A
|                   |
v                   v
ùïé(A/p) --gh_n-> A/p^(n+1)
```
-/

theorem ker_map_le_ker_mk_comp_ghostComponent (n : ‚Ñï) :
    RingHom.ker (WittVector.map <| Ideal.Quotient.mk <| span {(p : O)}) ‚â§
    RingHom.ker (((Ideal.Quotient.mk <| span {(p : O)} ^ (n + 1))).comp
    (WittVector.ghostComponent (p := p) n)) := by
  intro x
  simp only [RingHom.mem_ker, map_eq_zero_iff, RingHom.comp_apply]
  intro h
  simp only [ghostComponent]
  apply_fun Ideal.quotEquivOfEq (Ideal.span_singleton_pow (p : O) (n + 1))
  simp only [RingHom.coe_comp, Function.comp_apply, Pi.evalRingHom_apply, ghostMap_apply,
    quotEquivOfEq_mk, map_zero]
  simp only [eq_zero_iff_dvd] at h ‚ä¢
  exact pow_dvd_ghostComponent_of_dvd_coeff (fun _ _ ‚Ü¶ h _)

/--
The lift ring map `gh_n : ùïé(A/p) ‚Üí+* A/p^(n+1)` of the `n`-th ghost component
`ùïé(A) ‚Üí+* A` along the surjective ring map `ùïé(A) ‚Üí+* ùïé(A/p)`.
-/
def ghostComponentModPPow (n : ‚Ñï): ùïé (O ‚ß∏ span {(p : O)}) ‚Üí+* O ‚ß∏ span {(p : O)}^(n + 1) :=
  RingHom.liftOfSurjective (WittVector.map <| Ideal.Quotient.mk <| span {(p : O)})
    (map_surjective _ Ideal.Quotient.mk_surjective)
    ‚ü®((Ideal.Quotient.mk <| span {(p : O)} ^ (n + 1))).comp
    (WittVector.ghostComponent (p := p) n), ker_map_le_ker_mk_comp_ghostComponent n‚ü©

@[simp]
theorem quotEquivOfEq_ghostComponentModPPow (x : ùïé (O ‚ß∏ span {(p : O)})) :
    quotEquivOfEq (Ideal.span_pow_zero_add_one) (ghostComponentModPPow 0 x) =
    ghostComponent 0 x := by
  simp only [Nat.reduceAdd, ghostComponentModPPow, RingHom.liftOfSurjective]
  have surj : Function.Surjective (WittVector.map (p := p) (Ideal.Quotient.mk (span {(p : O)}))) :=
    map_surjective _ Ideal.Quotient.mk_surjective
  choose y hy using surj x
  have := RingHom.liftOfSurjective_comp_apply
      (WittVector.map (p := p) (Ideal.Quotient.mk (span {(p : O)})))
      surj ‚ü®((Ideal.Quotient.mk <| span {(p : O)} ^ (0 + 1))).comp
      (WittVector.ghostComponent (p := p) 0), ker_map_le_ker_mk_comp_ghostComponent 0‚ü© y
  rw [hy] at this
  rw [this]
  simp [‚Üê hy, ghostComponent_apply]

variable [Fact ¬¨IsUnit (p : O)] [IsAdicComplete (span {(p : O)}) O]

@[simp]
theorem ghostComponentModPPow_teichmuller_coeff (n : ‚Ñï) (x : O^‚ô≠) :
    ghostComponentModPPow n (teichmuller p (Perfection.coeff (ModP O p) _ n x)) =
    Ideal.Quotient.mk (span {(p : O)} ^ (n + 1)) x.untilt := by
  simpa [ghostComponentModPPow] using RingHom.liftOfSurjective_comp_apply
      (WittVector.map <| Ideal.Quotient.mk <| span {(p : O)})
      (map_surjective _ Ideal.Quotient.mk_surjective)
      ‚ü®((Ideal.Quotient.mk <| span {(p : O)} ^ (n + 1))).comp
      (WittVector.ghostComponent (p := p) n), ker_map_le_ker_mk_comp_ghostComponent n‚ü©
      (teichmuller p ((((_root_.frobeniusEquiv _ p).symm ^ n) x).untilt))

variable (O p) in
/--
The Fontaine's theta map modulo `p ^ n`.
It is the composition of the following ring homomorphisms.
`ùïé(O^‚ô≠) --ùïé(Frob^-n)->  ùïé(O^‚ô≠) --ùïé(coeff 0)-> ùïé(O/p) --gh_n-> O/p^(n+1)`
-/
def fontaineThetaModPPow (n : ‚Ñï): ùïé (O^‚ô≠) ‚Üí+* O ‚ß∏ span {(p : O)} ^ (n + 1) :=
  (ghostComponentModPPow n).comp
      (((WittVector.map (Perfection.coeff _ p 0))).comp
          (WittVector.map ((_root_.frobeniusEquiv (O^‚ô≠) p).symm ^ n : O^‚ô≠ ‚Üí+* O^‚ô≠)))

@[simp]
theorem fontaineThetaModPPow_teichmuller (n : ‚Ñï) (x : O^‚ô≠) :
    fontaineThetaModPPow O p n (teichmuller p x) = Ideal.Quotient.mk _ x.untilt := by
  simp [fontaineThetaModPPow, PreTilt]

theorem factorPowSucc_comp_fontaineThetaModPPow (n : ‚Ñï) :
    (factorPowSucc _ (n + 1)).comp (fontaineThetaModPPow O p (n + 1)) =
    fontaineThetaModPPow O p n:= by
  apply eq_of_apply_teichmuller_eq
      ((factorPowSucc _ (n + 1)).comp (fontaineThetaModPPow O p (n + 1)))
      (fontaineThetaModPPow O p n)
  ¬∑ use n + 1
    have : (p : (O ‚ß∏ span {(p : O)} ^ (n + 1))) =
        Ideal.Quotient.mk (span {(p : O)} ^ (n + 1)) (p : O) := by
      simp only [map_natCast]
    rw [this, ‚Üê map_pow, Ideal.Quotient.eq_zero_iff_mem]
    exact Ideal.pow_mem_pow (mem_span_singleton_self _) _
  intro (x : Ring.Perfection (ModP O p) p)
  simp [PreTilt, fontaineThetaModPPow]

theorem factorPowSucc_fontaineThetaModPPow_eq (n : ‚Ñï) (x : ùïé (O^‚ô≠)) :
    (factorPowSucc _ (n + 1)) ((fontaineThetaModPPow O p (n + 1)) x) =
    fontaineThetaModPPow O p n x:= by
  simp [‚Üê factorPowSucc_comp_fontaineThetaModPPow n]

/--
The Fontaine's Œ∏ map from `ùïé(O^‚ô≠)` to `O`.
It is the limit of the ring maps `fontaineThetaModPPow n` from `ùïé(O^‚ô≠)` `O/p^(n+1)`.
-/
def fontaineTheta : ùïé (O^‚ô≠) ‚Üí+* O :=
  IsAdicComplete.limRingHom Order.succ_strictMono (factorPowSucc_fontaineThetaModPPow_eq _ _).symm

theorem mk_pow_fontaineTheta (n : ‚Ñï) (x : ùïé (O^‚ô≠)) :
    Ideal.Quotient.mk (span {(p : O)} ^ (n + 1)) (fontaineTheta x) = fontaineThetaModPPow O p n x :=
  IsAdicComplete.mk_limRingHom Order.succ_strictMono
      (factorPowSucc_fontaineThetaModPPow_eq _ _).symm n x

theorem mk_fontaineTheta (x : ùïé (O^‚ô≠)) :
    Ideal.Quotient.mk (span {(p : O)}) (fontaineTheta x) =
    Perfection.coeff (ModP O p) _ 0 (x.coeff 0) := by
  have := mk_pow_fontaineTheta 0 x
  simp only [Nat.reduceAdd] at this
  apply_fun Ideal.quotEquivOfEq (pow_one (p : O) ‚ñ∏ Ideal.span_singleton_pow (p : O) 1) at this
  simp only [quotEquivOfEq_mk] at this
  rw [this]
  simp only [fontaineThetaModPPow, Nat.reduceAdd, pow_zero, RingHom.one_def, WittVector.map_id,
    RingHomCompTriple.comp_eq, RingHom.coe_comp, Function.comp_apply,
    quotEquivOfEq_ghostComponentModPPow, ghostComponent_apply, wittPolynomial_zero,
    MvPolynomial.aeval_X]
  -- Note to reviewers : This line was designed to be
  -- `simp [fontaineThetaModPPow, ghostComponent_apply, RingHom.one_def]`
  -- However, `WittVector.map_coeff` should, but does not work here.
  rfl

@[simp]
theorem fontaineTheta_teichmuller (x : O^‚ô≠) : fontaineTheta (teichmuller p x) = x.untilt := by
  rw [IsHausdorff.eq_iff_smodEq' (I := span {(p : O)})]
  intro n
  cases n
  ¬∑ simp
  ¬∑ simp [SModEq, mk_pow_fontaineTheta]

end WittVector
