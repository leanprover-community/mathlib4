/-
Copyright (c) 2022 Robert Y. Lewis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robert Y. Lewis, Heather Macbeth, Johan Commelin
-/
import Mathlib.RingTheory.WittVector.Domain
import Mathlib.RingTheory.WittVector.MulCoeff
import Mathlib.RingTheory.DiscreteValuationRing.Basic
import Mathlib.Tactic.LinearCombination

#align_import ring_theory.witt_vector.discrete_valuation_ring from "leanprover-community/mathlib"@"c163ec99dfc664628ca15d215fce0a5b9c265b68"

/-!

# Witt vectors over a perfect ring

This file establishes that Witt vectors over a perfect field are a discrete valuation ring.
When `k` is a perfect ring, a nonzero `a : ğ• k` can be written as `p^m * b` for some `m : â„•` and
`b : ğ• k` with nonzero 0th coefficient.
When `k` is also a field, this `b` can be chosen to be a unit of `ğ• k`.

## Main declarations

* `WittVector.exists_eq_pow_p_mul`: the existence of this element `b` over a perfect ring
* `WittVector.exists_eq_pow_p_mul'`: the existence of this unit `b` over a perfect field
* `WittVector.discreteValuationRing`: `ğ• k` is a discrete valuation ring if `k` is a perfect field

-/


noncomputable section

local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y) -- Porting note: See issue lean4#2220

namespace WittVector

variable {p : â„•} [hp : Fact p.Prime]

local notation "ğ•" => WittVector p

section CommRing

variable {k : Type*} [CommRing k] [CharP k p]

/-- This is the `n+1`st coefficient of our inverse. -/
def succNthValUnits (n : â„•) (a : Units k) (A : ğ• k) (bs : Fin (n + 1) â†’ k) : k :=
  -â†‘(aâ»Â¹ ^ p ^ (n + 1)) *
    (A.coeff (n + 1) * â†‘(aâ»Â¹ ^ p ^ (n + 1)) + nthRemainder p n (truncateFun (n + 1) A) bs)
#align witt_vector.succ_nth_val_units WittVector.succNthValUnits

/--
Recursively defines the sequence of coefficients for the inverse to a Witt vector whose first entry
is a unit.
-/
noncomputable def inverseCoeff (a : Units k) (A : ğ• k) : â„• â†’ k
  | 0 => â†‘aâ»Â¹
  | n + 1 => succNthValUnits n a A fun i => inverseCoeff a A i.val
#align witt_vector.inverse_coeff WittVector.inverseCoeff

/--
Upgrade a Witt vector `A` whose first entry `A.coeff 0` is a unit to be, itself, a unit in `ğ• k`.
-/
def mkUnit {a : Units k} {A : ğ• k} (hA : A.coeff 0 = a) : Units (ğ• k) :=
  Units.mkOfMulEqOne A (@WittVector.mk' p _ (inverseCoeff a A)) (by
    ext n
    -- âŠ¢ coeff (A * { coeff := inverseCoeff a A }) n = coeff 1 n
    induction' n with n _
    -- âŠ¢ coeff (A * { coeff := inverseCoeff a A }) Nat.zero = coeff 1 Nat.zero
    Â· simp [WittVector.mul_coeff_zero, inverseCoeff, hA]
      -- ğŸ‰ no goals
    let H_coeff := A.coeff (n + 1) * â†‘(aâ»Â¹ ^ p ^ (n + 1)) +
      nthRemainder p n (truncateFun (n + 1) A) fun i : Fin (n + 1) => inverseCoeff a A i
    have H := Units.mul_inv (a ^ p ^ (n + 1))
    -- âŠ¢ coeff (A * { coeff := inverseCoeff a A }) (Nat.succ n) = coeff 1 (Nat.succ n)
    linear_combination (norm := skip) -H_coeff * H
    -- âŠ¢ coeff (A * { coeff := inverseCoeff a A }) (Nat.succ n) - coeff 1 (Nat.succ n â€¦
    have ha : (a : k) ^ p ^ (n + 1) = â†‘(a ^ p ^ (n + 1)) := by norm_cast
    -- âŠ¢ coeff (A * { coeff := inverseCoeff a A }) (Nat.succ n) - coeff 1 (Nat.succ n â€¦
    have ha_inv : (â†‘aâ»Â¹ : k) ^ p ^ (n + 1) = â†‘(a ^ p ^ (n + 1))â»Â¹ := by norm_cast; norm_num
    -- âŠ¢ coeff (A * { coeff := inverseCoeff a A }) (Nat.succ n) - coeff 1 (Nat.succ n â€¦
    simp only [nthRemainder_spec, inverseCoeff, succNthValUnits, hA,
      one_coeff_eq_of_pos, Nat.succ_pos', ha_inv, ha, inv_pow]
    ring!)
    -- ğŸ‰ no goals
#align witt_vector.mk_unit WittVector.mkUnit

@[simp]
theorem coe_mkUnit {a : Units k} {A : ğ• k} (hA : A.coeff 0 = a) : (mkUnit hA : ğ• k) = A :=
  rfl
#align witt_vector.coe_mk_unit WittVector.coe_mkUnit

end CommRing

section Field

variable {k : Type*} [Field k] [CharP k p]

theorem isUnit_of_coeff_zero_ne_zero (x : ğ• k) (hx : x.coeff 0 â‰  0) : IsUnit x := by
  let y : kË£ := Units.mk0 (x.coeff 0) hx
  -- âŠ¢ IsUnit x
  have hy : x.coeff 0 = y := rfl
  -- âŠ¢ IsUnit x
  exact (mkUnit hy).isUnit
  -- ğŸ‰ no goals
#align witt_vector.is_unit_of_coeff_zero_ne_zero WittVector.isUnit_of_coeff_zero_ne_zero

variable (p)

theorem irreducible : Irreducible (p : ğ• k) := by
  have hp : Â¬IsUnit (p : ğ• k) := by
    intro hp
    simpa only [constantCoeff_apply, coeff_p_zero, not_isUnit_zero] using
      (constantCoeff : WittVector p k â†’+* _).isUnit_map hp
  refine' âŸ¨hp, fun a b hab => _âŸ©
  -- âŠ¢ IsUnit a âˆ¨ IsUnit b
  obtain âŸ¨ha0, hb0âŸ© : a â‰  0 âˆ§ b â‰  0 := by
    rw [â† mul_ne_zero_iff]; intro h; rw [h] at hab; exact p_nonzero p k hab
  obtain âŸ¨m, a, ha, rflâŸ© := verschiebung_nonzero ha0
  -- âŠ¢ IsUnit ((â†‘verschiebung)^[m] a) âˆ¨ IsUnit b
  obtain âŸ¨n, b, hb, rflâŸ© := verschiebung_nonzero hb0
  -- âŠ¢ IsUnit ((â†‘verschiebung)^[m] a) âˆ¨ IsUnit ((â†‘verschiebung)^[n] b)
  cases m; Â· exact Or.inl (isUnit_of_coeff_zero_ne_zero a ha)
  -- âŠ¢ IsUnit ((â†‘verschiebung)^[Nat.zero] a) âˆ¨ IsUnit ((â†‘verschiebung)^[n] b)
             -- ğŸ‰ no goals
  cases' n with n; Â· exact Or.inr (isUnit_of_coeff_zero_ne_zero b hb)
  -- âŠ¢ IsUnit ((â†‘verschiebung)^[Nat.succ nâœ] a) âˆ¨ IsUnit ((â†‘verschiebung)^[Nat.zero â€¦
                     -- ğŸ‰ no goals
  rw [iterate_verschiebung_mul] at hab
  -- âŠ¢ IsUnit ((â†‘verschiebung)^[Nat.succ nâœ] a) âˆ¨ IsUnit ((â†‘verschiebung)^[Nat.succ â€¦
  apply_fun fun x => coeff x 1 at hab
  -- âŠ¢ IsUnit ((â†‘verschiebung)^[Nat.succ nâœ] a) âˆ¨ IsUnit ((â†‘verschiebung)^[Nat.succ â€¦
  simp only [coeff_p_one, Nat.add_succ, add_comm _ n, Function.iterate_succ', Function.comp_apply,
    verschiebung_coeff_add_one, verschiebung_coeff_zero] at hab
  exact (one_ne_zero hab).elim
  -- ğŸ‰ no goals
#align witt_vector.irreducible WittVector.irreducible

end Field

section PerfectRing

variable {k : Type*} [CommRing k] [CharP k p] [PerfectRing k p]

theorem exists_eq_pow_p_mul (a : ğ• k) (ha : a â‰  0) :
    âˆƒ (m : â„•) (b : ğ• k), b.coeff 0 â‰  0 âˆ§ a = (p : ğ• k) ^ m * b := by
  obtain âŸ¨m, c, hc, hcmâŸ© := WittVector.verschiebung_nonzero ha
  -- âŠ¢ âˆƒ m b, coeff b 0 â‰  0 âˆ§ a = â†‘p ^ m * b
  obtain âŸ¨b, rflâŸ© := (frobenius_bijective p k).surjective.iterate m c
  -- âŠ¢ âˆƒ m b, coeff b 0 â‰  0 âˆ§ a = â†‘p ^ m * b
  rw [WittVector.iterate_frobenius_coeff] at hc
  -- âŠ¢ âˆƒ m b, coeff b 0 â‰  0 âˆ§ a = â†‘p ^ m * b
  have := congr_fun (WittVector.verschiebung_frobenius_comm.comp_iterate m) b
  -- âŠ¢ âˆƒ m b, coeff b 0 â‰  0 âˆ§ a = â†‘p ^ m * b
  simp only [Function.comp_apply] at this
  -- âŠ¢ âˆƒ m b, coeff b 0 â‰  0 âˆ§ a = â†‘p ^ m * b
  rw [â† this] at hcm
  -- âŠ¢ âˆƒ m b, coeff b 0 â‰  0 âˆ§ a = â†‘p ^ m * b
  refine' âŸ¨m, b, _, _âŸ©
  -- âŠ¢ coeff b 0 â‰  0
  Â· contrapose! hc
    -- âŠ¢ coeff b 0 ^ p ^ m = 0
    have : 0 < p ^ m := pow_pos (Nat.Prime.pos Fact.out) _
    -- âŠ¢ coeff b 0 ^ p ^ m = 0
    simp [hc, zero_pow this]
    -- ğŸ‰ no goals
  Â· simp_rw [â† mul_left_iterate (p : ğ• k) m]
    -- âŠ¢ a = (fun x => â†‘p * x)^[m] b
    convert hcm using 2
    -- âŠ¢ (fun x => â†‘p * x) = â†‘verschiebung âˆ˜ â†‘frobenius
    ext1 x
    -- âŠ¢ â†‘p * x = (â†‘verschiebung âˆ˜ â†‘frobenius) x
    rw [mul_comm, â† WittVector.verschiebung_frobenius x]; rfl
    -- âŠ¢ â†‘verschiebung (â†‘frobenius x) = (â†‘verschiebung âˆ˜ â†‘frobenius) x
                                                          -- ğŸ‰ no goals
#align witt_vector.exists_eq_pow_p_mul WittVector.exists_eq_pow_p_mul

end PerfectRing

section PerfectField

variable {k : Type*} [Field k] [CharP k p] [PerfectRing k p]

theorem exists_eq_pow_p_mul' (a : ğ• k) (ha : a â‰  0) :
    âˆƒ (m : â„•) (b : Units (ğ• k)), a = (p : ğ• k) ^ m * b := by
  obtain âŸ¨m, b, hâ‚, hâ‚‚âŸ© := exists_eq_pow_p_mul a ha
  -- âŠ¢ âˆƒ m b, a = â†‘p ^ m * â†‘b
  let bâ‚€ := Units.mk0 (b.coeff 0) hâ‚
  -- âŠ¢ âˆƒ m b, a = â†‘p ^ m * â†‘b
  have hbâ‚€ : b.coeff 0 = bâ‚€ := rfl
  -- âŠ¢ âˆƒ m b, a = â†‘p ^ m * â†‘b
  exact âŸ¨m, mkUnit hbâ‚€, hâ‚‚âŸ©
  -- ğŸ‰ no goals
#align witt_vector.exists_eq_pow_p_mul' WittVector.exists_eq_pow_p_mul'

/-
Note: The following lemma should be an instance, but it seems to cause some
exponential blowups in certain typeclass resolution problems.
See the following Lean4 issue as well as the zulip discussion linked there:
https://github.com/leanprover/lean4/issues/1102
-/
/-- The ring of Witt Vectors of a perfect field of positive characteristic is a DVR.
-/
theorem discreteValuationRing : DiscreteValuationRing (ğ• k) :=
  DiscreteValuationRing.ofHasUnitMulPowIrreducibleFactorization (by
    refine' âŸ¨p, irreducible p, fun {x} hx => _âŸ©
    -- âŠ¢ âˆƒ n, Associated (â†‘p ^ n) x
    obtain âŸ¨n, b, hbâŸ© := exists_eq_pow_p_mul' x hx
    -- âŠ¢ âˆƒ n, Associated (â†‘p ^ n) x
    exact âŸ¨n, b, hb.symmâŸ©)
    -- ğŸ‰ no goals
#align witt_vector.discrete_valuation_ring WittVector.discreteValuationRing

end PerfectField

end WittVector
