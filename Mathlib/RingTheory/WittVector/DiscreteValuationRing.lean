/-
Copyright (c) 2022 Robert Y. Lewis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robert Y. Lewis, Heather Macbeth, Johan Commelin
-/
import Mathlib.RingTheory.WittVector.Domain
import Mathlib.RingTheory.WittVector.MulCoeff
import Mathlib.RingTheory.DiscreteValuationRing.Basic
import Mathlib.Tactic.LinearCombination

#align_import ring_theory.witt_vector.discrete_valuation_ring from "leanprover-community/mathlib"@"c163ec99dfc664628ca15d215fce0a5b9c265b68"

/-!

# Witt vectors over a perfect ring

This file establishes that Witt vectors over a perfect field are a discrete valuation ring.
When `k` is a perfect ring, a nonzero `a : ùïé k` can be written as `p^m * b` for some `m : ‚Ñï` and
`b : ùïé k` with nonzero 0th coefficient.
When `k` is also a field, this `b` can be chosen to be a unit of `ùïé k`.

## Main declarations

* `WittVector.exists_eq_pow_p_mul`: the existence of this element `b` over a perfect ring
* `WittVector.exists_eq_pow_p_mul'`: the existence of this unit `b` over a perfect field
* `WittVector.discreteValuationRing`: `ùïé k` is a discrete valuation ring if `k` is a perfect field

-/


noncomputable section

namespace WittVector

variable {p : ‚Ñï} [hp : Fact p.Prime]

local notation "ùïé" => WittVector p

section CommRing

variable {k : Type*} [CommRing k] [CharP k p]

/-- This is the `n+1`st coefficient of our inverse. -/
def succNthValUnits (n : ‚Ñï) (a : Units k) (A : ùïé k) (bs : Fin (n + 1) ‚Üí k) : k :=
  -‚Üë(a‚Åª¬π ^ p ^ (n + 1)) *
    (A.coeff (n + 1) * ‚Üë(a‚Åª¬π ^ p ^ (n + 1)) + nthRemainder p n (truncateFun (n + 1) A) bs)
#align witt_vector.succ_nth_val_units WittVector.succNthValUnits

/--
Recursively defines the sequence of coefficients for the inverse to a Witt vector whose first entry
is a unit.
-/
noncomputable def inverseCoeff (a : Units k) (A : ùïé k) : ‚Ñï ‚Üí k
  | 0 => ‚Üëa‚Åª¬π
  | n + 1 => succNthValUnits n a A fun i => inverseCoeff a A i.val
#align witt_vector.inverse_coeff WittVector.inverseCoeff

/--
Upgrade a Witt vector `A` whose first entry `A.coeff 0` is a unit to be, itself, a unit in `ùïé k`.
-/
def mkUnit {a : Units k} {A : ùïé k} (hA : A.coeff 0 = a) : Units (ùïé k) :=
  Units.mkOfMulEqOne A (@WittVector.mk' p _ (inverseCoeff a A)) (by
    ext n
    induction' n with n _
    ¬∑ simp [WittVector.mul_coeff_zero, inverseCoeff, hA]
    let H_coeff := A.coeff (n + 1) * ‚Üë(a‚Åª¬π ^ p ^ (n + 1)) +
      nthRemainder p n (truncateFun (n + 1) A) fun i : Fin (n + 1) => inverseCoeff a A i
    have H := Units.mul_inv (a ^ p ^ (n + 1))
    linear_combination (norm := skip) -H_coeff * H
    have ha : (a : k) ^ p ^ (n + 1) = ‚Üë(a ^ p ^ (n + 1)) := by norm_cast
    have ha_inv : (‚Üëa‚Åª¬π : k) ^ p ^ (n + 1) = ‚Üë(a ^ p ^ (n + 1))‚Åª¬π := by norm_cast
    simp only [nthRemainder_spec, inverseCoeff, succNthValUnits, hA,
      one_coeff_eq_of_pos, Nat.succ_pos', ha_inv, ha, inv_pow]
    ring!)
#align witt_vector.mk_unit WittVector.mkUnit

@[simp]
theorem coe_mkUnit {a : Units k} {A : ùïé k} (hA : A.coeff 0 = a) : (mkUnit hA : ùïé k) = A :=
  rfl
#align witt_vector.coe_mk_unit WittVector.coe_mkUnit

end CommRing

section Field

variable {k : Type*} [Field k] [CharP k p]

theorem isUnit_of_coeff_zero_ne_zero (x : ùïé k) (hx : x.coeff 0 ‚â† 0) : IsUnit x := by
  let y : kÀ£ := Units.mk0 (x.coeff 0) hx
  have hy : x.coeff 0 = y := rfl
  exact (mkUnit hy).isUnit
#align witt_vector.is_unit_of_coeff_zero_ne_zero WittVector.isUnit_of_coeff_zero_ne_zero

variable (p)

theorem irreducible : Irreducible (p : ùïé k) := by
  have hp : ¬¨IsUnit (p : ùïé k) := by
    intro hp
    simpa only [constantCoeff_apply, coeff_p_zero, not_isUnit_zero] using
      (constantCoeff : WittVector p k ‚Üí+* _).isUnit_map hp
  refine ‚ü®hp, fun a b hab => ?_‚ü©
  obtain ‚ü®ha0, hb0‚ü© : a ‚â† 0 ‚àß b ‚â† 0 := by
    rw [‚Üê mul_ne_zero_iff]; intro h; rw [h] at hab; exact p_nonzero p k hab
  obtain ‚ü®m, a, ha, rfl‚ü© := verschiebung_nonzero ha0
  obtain ‚ü®n, b, hb, rfl‚ü© := verschiebung_nonzero hb0
  cases m; ¬∑ exact Or.inl (isUnit_of_coeff_zero_ne_zero a ha)
  cases' n with n; ¬∑ exact Or.inr (isUnit_of_coeff_zero_ne_zero b hb)
  rw [iterate_verschiebung_mul] at hab
  apply_fun fun x => coeff x 1 at hab
  simp only [coeff_p_one, Nat.add_succ, add_comm _ n, Function.iterate_succ', Function.comp_apply,
    verschiebung_coeff_add_one, verschiebung_coeff_zero] at hab
  exact (one_ne_zero hab).elim
#align witt_vector.irreducible WittVector.irreducible

end Field

section PerfectRing

variable {k : Type*} [CommRing k] [CharP k p] [PerfectRing k p]

theorem exists_eq_pow_p_mul (a : ùïé k) (ha : a ‚â† 0) :
    ‚àÉ (m : ‚Ñï) (b : ùïé k), b.coeff 0 ‚â† 0 ‚àß a = (p : ùïé k) ^ m * b := by
  obtain ‚ü®m, c, hc, hcm‚ü© := WittVector.verschiebung_nonzero ha
  obtain ‚ü®b, rfl‚ü© := (frobenius_bijective p k).surjective.iterate m c
  rw [WittVector.iterate_frobenius_coeff] at hc
  have := congr_fun (WittVector.verschiebung_frobenius_comm.comp_iterate m) b
  simp only [Function.comp_apply] at this
  rw [‚Üê this] at hcm
  refine ‚ü®m, b, ?_, ?_‚ü©
  ¬∑ contrapose! hc
    simp [hc, zero_pow $ pow_ne_zero _ hp.out.ne_zero]
  ¬∑ simp_rw [‚Üê mul_left_iterate (p : ùïé k) m]
    convert hcm using 2
    ext1 x
    rw [mul_comm, ‚Üê WittVector.verschiebung_frobenius x]; rfl
#align witt_vector.exists_eq_pow_p_mul WittVector.exists_eq_pow_p_mul

end PerfectRing

section PerfectField

variable {k : Type*} [Field k] [CharP k p] [PerfectRing k p]

theorem exists_eq_pow_p_mul' (a : ùïé k) (ha : a ‚â† 0) :
    ‚àÉ (m : ‚Ñï) (b : Units (ùïé k)), a = (p : ùïé k) ^ m * b := by
  obtain ‚ü®m, b, h‚ÇÅ, h‚ÇÇ‚ü© := exists_eq_pow_p_mul a ha
  let b‚ÇÄ := Units.mk0 (b.coeff 0) h‚ÇÅ
  have hb‚ÇÄ : b.coeff 0 = b‚ÇÄ := rfl
  exact ‚ü®m, mkUnit hb‚ÇÄ, h‚ÇÇ‚ü©
#align witt_vector.exists_eq_pow_p_mul' WittVector.exists_eq_pow_p_mul'

/-
Note: The following lemma should be an instance, but it seems to cause some
exponential blowups in certain typeclass resolution problems.
See the following Lean4 issue as well as the zulip discussion linked there:
https://github.com/leanprover/lean4/issues/1102
-/
/-- The ring of Witt Vectors of a perfect field of positive characteristic is a DVR.
-/
theorem discreteValuationRing : DiscreteValuationRing (ùïé k) :=
  DiscreteValuationRing.ofHasUnitMulPowIrreducibleFactorization (by
    refine ‚ü®p, irreducible p, fun {x} hx => ?_‚ü©
    obtain ‚ü®n, b, hb‚ü© := exists_eq_pow_p_mul' x hx
    exact ‚ü®n, b, hb.symm‚ü©)
#align witt_vector.discrete_valuation_ring WittVector.discreteValuationRing

end PerfectField

end WittVector
