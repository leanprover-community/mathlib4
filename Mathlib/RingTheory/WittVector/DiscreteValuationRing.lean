/-
Copyright (c) 2022 Robert Y. Lewis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robert Y. Lewis, Heather Macbeth, Johan Commelin
-/
import Mathlib.RingTheory.WittVector.Domain
import Mathlib.RingTheory.WittVector.MulCoeff
import Mathlib.RingTheory.DiscreteValuationRing.Basic
import Mathlib.Tactic.LinearCombination

/-!

# Witt vectors over a perfect ring

This file establishes that Witt vectors over a perfect field are a discrete valuation ring.
When `k` is a perfect ring, a nonzero `a : ğ• k` can be written as `p^m * b` for some `m : â„•` and
`b : ğ• k` with nonzero 0th coefficient.
When `k` is also a field, this `b` can be chosen to be a unit of `ğ• k`.

## Main declarations

* `WittVector.exists_eq_pow_p_mul`: the existence of this element `b` over a perfect ring
* `WittVector.exists_eq_pow_p_mul'`: the existence of this unit `b` over a perfect field
* `WittVector.isDiscreteValuationRing`: `ğ• k` is a discrete valuation ring if `k` is a perfect field

-/


noncomputable section

namespace WittVector

variable {p : â„•} [hp : Fact p.Prime]

local notation "ğ•" => WittVector p

section CommRing

variable {k : Type*} [CommRing k] [CharP k p]

/-- This is the `n+1`st coefficient of our inverse. -/
def succNthValUnits (n : â„•) (a : Units k) (A : ğ• k) (bs : Fin (n + 1) â†’ k) : k :=
  -â†‘(aâ»Â¹ ^ p ^ (n + 1)) *
    (A.coeff (n + 1) * â†‘(aâ»Â¹ ^ p ^ (n + 1)) + nthRemainder p n (truncateFun (n + 1) A) bs)

/--
Recursively defines the sequence of coefficients for the inverse to a Witt vector whose first entry
is a unit.
-/
noncomputable def inverseCoeff (a : Units k) (A : ğ• k) : â„• â†’ k
  | 0 => â†‘aâ»Â¹
  | n + 1 => succNthValUnits n a A fun i => inverseCoeff a A i.val

/--
Upgrade a Witt vector `A` whose first entry `A.coeff 0` is a unit to be, itself, a unit in `ğ• k`.
-/
def mkUnit {a : Units k} {A : ğ• k} (hA : A.coeff 0 = a) : Units (ğ• k) :=
  Units.mkOfMulEqOne A (@WittVector.mk' p _ (inverseCoeff a A)) (by
    ext n
    induction n with
    | zero => simp [WittVector.mul_coeff_zero, inverseCoeff, hA]
    | succ n => ?_
    let H_coeff := A.coeff (n + 1) * â†‘(aâ»Â¹ ^ p ^ (n + 1)) +
      nthRemainder p n (truncateFun (n + 1) A) fun i : Fin (n + 1) => inverseCoeff a A i
    have H := Units.mul_inv (a ^ p ^ (n + 1))
    linear_combination (norm := skip) -H_coeff * H
    have ha : (a : k) ^ p ^ (n + 1) = â†‘(a ^ p ^ (n + 1)) := by norm_cast
    have ha_inv : (â†‘aâ»Â¹ : k) ^ p ^ (n + 1) = â†‘(a ^ p ^ (n + 1))â»Â¹ := by norm_cast
    simp only [nthRemainder_spec, inverseCoeff, succNthValUnits, hA,
      one_coeff_eq_of_pos, Nat.succ_pos', ha_inv, ha, inv_pow]
    ring!)

@[simp]
theorem coe_mkUnit {a : Units k} {A : ğ• k} (hA : A.coeff 0 = a) : (mkUnit hA : ğ• k) = A :=
  rfl

end CommRing

section Field

variable {k : Type*} [Field k] [CharP k p]

theorem isUnit_of_coeff_zero_ne_zero (x : ğ• k) (hx : x.coeff 0 â‰  0) : IsUnit x := by
  let y : kË£ := Units.mk0 (x.coeff 0) hx
  have hy : x.coeff 0 = y := rfl
  exact (mkUnit hy).isUnit

variable (p)

theorem irreducible : Irreducible (p : ğ• k) := by
  have hp : Â¬IsUnit (p : ğ• k) := by
    intro hp
    simpa only [constantCoeff_apply, coeff_p_zero, not_isUnit_zero] using
      (constantCoeff : WittVector p k â†’+* _).isUnit_map hp
  refine âŸ¨hp, fun a b hab => ?_âŸ©
  obtain âŸ¨ha0, hb0âŸ© : a â‰  0 âˆ§ b â‰  0 := by
    rw [â† mul_ne_zero_iff]; intro h; rw [h] at hab; exact p_nonzero p k hab
  obtain âŸ¨m, a, ha, rflâŸ© := verschiebung_nonzero ha0
  obtain âŸ¨n, b, hb, rflâŸ© := verschiebung_nonzero hb0
  cases m; Â· exact Or.inl (isUnit_of_coeff_zero_ne_zero a ha)
  rcases n with - | n; Â· exact Or.inr (isUnit_of_coeff_zero_ne_zero b hb)
  rw [iterate_verschiebung_mul] at hab
  apply_fun fun x => coeff x 1 at hab
  simp only [coeff_p_one, Nat.add_succ, add_comm _ n, Function.iterate_succ', Function.comp_apply,
    verschiebung_coeff_add_one, verschiebung_coeff_zero] at hab
  exact (one_ne_zero hab).elim

end Field

section PerfectRing

variable {k : Type*} [CommRing k] [CharP k p] [PerfectRing k p]

theorem exists_eq_pow_p_mul (a : ğ• k) (ha : a â‰  0) :
    âˆƒ (m : â„•) (b : ğ• k), b.coeff 0 â‰  0 âˆ§ a = (p : ğ• k) ^ m * b := by
  obtain âŸ¨m, c, hc, hcmâŸ© := WittVector.verschiebung_nonzero ha
  obtain âŸ¨b, rflâŸ© := (frobenius_bijective p k).surjective.iterate m c
  rw [WittVector.iterate_frobenius_coeff] at hc
  have := congr_fun (WittVector.verschiebung_frobenius_comm.comp_iterate m) b
  simp only [Function.comp_apply] at this
  rw [â† this] at hcm
  refine âŸ¨m, b, ?_, ?_âŸ©
  Â· contrapose! hc
    simp [hc, zero_pow <| pow_ne_zero _ hp.out.ne_zero]
  Â· simp_rw [â† mul_left_iterate (p : ğ• k) m]
    convert hcm using 2
    ext1 x
    rw [mul_comm, â† WittVector.verschiebung_frobenius x]; rfl

end PerfectRing

section PerfectField

variable {k : Type*} [Field k] [CharP k p] [PerfectRing k p]

theorem exists_eq_pow_p_mul' (a : ğ• k) (ha : a â‰  0) :
    âˆƒ (m : â„•) (b : Units (ğ• k)), a = (p : ğ• k) ^ m * b := by
  obtain âŸ¨m, b, hâ‚, hâ‚‚âŸ© := exists_eq_pow_p_mul a ha
  let bâ‚€ := Units.mk0 (b.coeff 0) hâ‚
  have hbâ‚€ : b.coeff 0 = bâ‚€ := rfl
  exact âŸ¨m, mkUnit hbâ‚€, hâ‚‚âŸ©

/-
Note: The following lemma should be an instance, but it seems to cause some
exponential blowups in certain typeclass resolution problems.
See the following Lean4 issue as well as the zulip discussion linked there:
https://github.com/leanprover/lean4/issues/1102
-/
/-- The ring of Witt Vectors of a perfect field of positive characteristic is a DVR.
-/
theorem isDiscreteValuationRing : IsDiscreteValuationRing (ğ• k) :=
  IsDiscreteValuationRing.ofHasUnitMulPowIrreducibleFactorization (by
    refine âŸ¨p, irreducible p, fun {x} hx => ?_âŸ©
    obtain âŸ¨n, b, hbâŸ© := exists_eq_pow_p_mul' x hx
    exact âŸ¨n, b, hb.symmâŸ©)

end PerfectField

end WittVector
