/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Robert Y. Lewis
-/
import Mathlib.RingTheory.WittVector.StructurePolynomial

/-!
# Witt vectors

In this file we define the type of `p`-typical Witt vectors and ring operations on it.
The ring axioms are verified in `Mathlib/RingTheory/WittVector/Basic.lean`.

For a fixed commutative ring `R` and prime `p`,
a Witt vector `x : ğ• R` is an infinite sequence `â„• â†’ R` of elements of `R`.
However, the ring operations `+` and `*` are not defined in the obvious component-wise way.
Instead, these operations are defined via certain polynomials
using the machinery in `Mathlib/RingTheory/WittVector/StructurePolynomial.lean`.
The `n`th value of the sum of two Witt vectors can depend on the `0`-th through `n`th values
of the summands. This effectively simulates a â€œcarryingâ€ operation.

## Main definitions

* `WittVector p R`: the type of `p`-typical Witt vectors with coefficients in `R`.
* `WittVector.coeff x n`: projects the `n`th value of the Witt vector `x`.

## Notation

We use notation `ğ• R`, entered `\bbW`, for the Witt vectors over `R`.

## References

* [Hazewinkel, *Witt Vectors*][Haze09]

* [Commelin and Lewis, *Formalizing the Ring of Witt Vectors*][CL21]
-/


noncomputable section

/-- `WittVector p R` is the ring of `p`-typical Witt vectors over the commutative ring `R`,
where `p` is a prime number.

If `p` is invertible in `R`, this ring is isomorphic to `â„• â†’ R` (the product of `â„•` copies of `R`).
If `R` is a ring of characteristic `p`, then `WittVector p R` is a ring of characteristic `0`.
The canonical example is `WittVector p (ZMod p)`,
which is isomorphic to the `p`-adic integers `â„¤_[p]`. -/
structure WittVector (p : â„•) (R : Type*) where mk' ::
  /-- `x.coeff n` is the `n`th coefficient of the Witt vector `x`.

  This concept does not have a standard name in the literature.
  -/
  coeff : â„• â†’ R

/-- Construct a Witt vector `mk p x : ğ• R` from a sequence `x` of elements of `R`.

This is preferred over `WittVector.mk'` because it has `p` explicit.
-/
def WittVector.mk (p : â„•) {R : Type*} (coeff : â„• â†’ R) : WittVector p R := mk' coeff

variable {p : â„•}

/- We cannot make this `localized` notation, because the `p` on the RHS doesn't occur on the left
Hiding the `p` in the notation is very convenient, so we opt for repeating the `local notation`
in other files that use Witt vectors. -/
local notation "ğ•" => WittVector p -- type as `\bbW`

namespace WittVector

variable {R : Type*}

@[ext]
theorem ext {x y : ğ• R} (h : âˆ€ n, x.coeff n = y.coeff n) : x = y := by
  cases x
  cases y
  simp only at h
  simp [funext_iff, h]

variable (p)

@[simp]
theorem coeff_mk (x : â„• â†’ R) : (mk p x).coeff = x :=
  rfl

/- These instances are not needed for the rest of the development,
but it is interesting to establish early on that `WittVector p` is a lawful functor. -/
instance : Functor (WittVector p) where
  map f v := mk p (f âˆ˜ v.coeff)
  mapConst a _ := mk p fun _ => a

instance : LawfulFunctor (WittVector p) where
  map_const := rfl
  id_map _ := rfl
  comp_map _ _ _ := rfl

variable [hp : Fact p.Prime] [CommRing R]

open MvPolynomial

section RingOperations

/-- The polynomials used for defining the element `0` of the ring of Witt vectors. -/
def wittZero : â„• â†’ MvPolynomial (Fin 0 Ã— â„•) â„¤ :=
  wittStructureInt p 0

/-- The polynomials used for defining the element `1` of the ring of Witt vectors. -/
def wittOne : â„• â†’ MvPolynomial (Fin 0 Ã— â„•) â„¤ :=
  wittStructureInt p 1

/-- The polynomials used for defining the addition of the ring of Witt vectors. -/
def wittAdd : â„• â†’ MvPolynomial (Fin 2 Ã— â„•) â„¤ :=
  wittStructureInt p (X 0 + X 1)

/-- The polynomials used for defining repeated addition of the ring of Witt vectors. -/
def wittNSMul (n : â„•) : â„• â†’ MvPolynomial (Fin 1 Ã— â„•) â„¤ :=
  wittStructureInt p (n â€¢ X (0 : (Fin 1)))

/-- The polynomials used for defining repeated addition of the ring of Witt vectors. -/
def wittZSMul (n : â„¤) : â„• â†’ MvPolynomial (Fin 1 Ã— â„•) â„¤ :=
  wittStructureInt p (n â€¢ X (0 : (Fin 1)))

/-- The polynomials used for describing the subtraction of the ring of Witt vectors. -/
def wittSub : â„• â†’ MvPolynomial (Fin 2 Ã— â„•) â„¤ :=
  wittStructureInt p (X 0 - X 1)

/-- The polynomials used for defining the multiplication of the ring of Witt vectors. -/
def wittMul : â„• â†’ MvPolynomial (Fin 2 Ã— â„•) â„¤ :=
  wittStructureInt p (X 0 * X 1)

/-- The polynomials used for defining the negation of the ring of Witt vectors. -/
def wittNeg : â„• â†’ MvPolynomial (Fin 1 Ã— â„•) â„¤ :=
  wittStructureInt p (-X 0)

/-- The polynomials used for defining repeated addition of the ring of Witt vectors. -/
def wittPow (n : â„•) : â„• â†’ MvPolynomial (Fin 1 Ã— â„•) â„¤ :=
  wittStructureInt p (X 0 ^ n)

variable {p}


/-- An auxiliary definition used in `WittVector.eval`.
Evaluates a polynomial whose variables come from the disjoint union of `k` copies of `â„•`,
with a curried evaluation `x`.
This can be defined more generally but we use only a specific instance here. -/
def peval {k : â„•} (Ï† : MvPolynomial (Fin k Ã— â„•) â„¤) (x : Fin k â†’ â„• â†’ R) : R :=
  aeval (Function.uncurry x) Ï†

/-- Let `Ï†` be a family of polynomials, indexed by natural numbers, whose variables come from the
disjoint union of `k` copies of `â„•`, and let `xáµ¢` be a Witt vector for `0 â‰¤ i < k`.

`eval Ï† x` evaluates `Ï†` mapping the variable `X_(i, n)` to the `n`th coefficient of `xáµ¢`.

Instantiating `Ï†` with certain polynomials defined in
`Mathlib/RingTheory/WittVector/StructurePolynomial.lean` establishes the
ring operations on `ğ• R`. For example, `WittVector.wittAdd` is such a `Ï†` with `k = 2`;
evaluating this at `(xâ‚€, xâ‚)` gives us the sum of two Witt vectors `xâ‚€ + xâ‚`.
-/
def eval {k : â„•} (Ï† : â„• â†’ MvPolynomial (Fin k Ã— â„•) â„¤) (x : Fin k â†’ ğ• R) : ğ• R :=
  mk p fun n => peval (Ï† n) fun i => (x i).coeff

instance : Zero (ğ• R) :=
  âŸ¨eval (wittZero p) ![]âŸ©

instance : Inhabited (ğ• R) :=
  âŸ¨0âŸ©

instance : One (ğ• R) :=
  âŸ¨eval (wittOne p) ![]âŸ©

instance : Add (ğ• R) :=
  âŸ¨fun x y => eval (wittAdd p) ![x, y]âŸ©

instance : Sub (ğ• R) :=
  âŸ¨fun x y => eval (wittSub p) ![x, y]âŸ©

instance hasNatScalar : SMul â„• (ğ• R) :=
  âŸ¨fun n x => eval (wittNSMul p n) ![x]âŸ©

instance hasIntScalar : SMul â„¤ (ğ• R) :=
  âŸ¨fun n x => eval (wittZSMul p n) ![x]âŸ©

instance : Mul (ğ• R) :=
  âŸ¨fun x y => eval (wittMul p) ![x, y]âŸ©

instance : Neg (ğ• R) :=
  âŸ¨fun x => eval (wittNeg p) ![x]âŸ©

instance hasNatPow : Pow (ğ• R) â„• :=
  âŸ¨fun x n => eval (wittPow p n) ![x]âŸ©

instance : NatCast (ğ• R) :=
  âŸ¨Nat.unaryCastâŸ©

instance : IntCast (ğ• R) :=
  âŸ¨Int.castDefâŸ©

end RingOperations

section WittStructureSimplifications

@[simp]
theorem wittZero_eq_zero (n : â„•) : wittZero p n = 0 := by
  apply MvPolynomial.map_injective (Int.castRingHom â„š) Int.cast_injective
  simp only [wittZero, wittStructureRat, bindâ‚, aeval_zero', constantCoeff_xInTermsOfW, map_zero,
    map_wittStructureInt]

@[simp]
theorem wittOne_zero_eq_one : wittOne p 0 = 1 := by
  apply MvPolynomial.map_injective (Int.castRingHom â„š) Int.cast_injective
  simp only [wittOne, wittStructureRat, xInTermsOfW_zero, map_one, bindâ‚_X_right,
    map_wittStructureInt]

@[simp]
theorem wittOne_pos_eq_zero (n : â„•) (hn : 0 < n) : wittOne p n = 0 := by
  apply MvPolynomial.map_injective (Int.castRingHom â„š) Int.cast_injective
  simp only [wittOne, wittStructureRat, RingHom.map_zero, map_one,
    map_wittStructureInt]
  induction n using Nat.strong_induction_on with | h n IH => ?_
  rw [xInTermsOfW_eq]
  simp only [map_mul, map_sub, map_sum, map_pow, bindâ‚_X_right,
    bindâ‚_C_right]
  rw [sub_mul, one_mul]
  rw [Finset.sum_eq_single 0]
  Â· simp only [invOf_eq_inv, one_mul, tsub_zero, pow_zero]
    simp only [one_pow, one_mul, xInTermsOfW_zero, sub_self, bindâ‚_X_right]
  Â· intro i hin hi0
    rw [Finset.mem_range] at hin
    rw [IH _ hin (Nat.pos_of_ne_zero hi0), zero_pow (pow_ne_zero _ hp.1.ne_zero), mul_zero]
  Â· grind

@[simp]
theorem wittAdd_zero : wittAdd p 0 = X (0, 0) + X (1, 0) := by
  apply MvPolynomial.map_injective (Int.castRingHom â„š) Int.cast_injective
  simp only [wittAdd, wittStructureRat, map_add, rename_X, xInTermsOfW_zero, map_X,
    wittPolynomial_zero, bindâ‚_X_right, map_wittStructureInt]

@[simp]
theorem wittSub_zero : wittSub p 0 = X (0, 0) - X (1, 0) := by
  apply MvPolynomial.map_injective (Int.castRingHom â„š) Int.cast_injective
  simp only [wittSub, wittStructureRat, map_sub, rename_X, xInTermsOfW_zero, map_X,
    wittPolynomial_zero, bindâ‚_X_right, map_wittStructureInt]

@[simp]
theorem wittMul_zero : wittMul p 0 = X (0, 0) * X (1, 0) := by
  apply MvPolynomial.map_injective (Int.castRingHom â„š) Int.cast_injective
  simp only [wittMul, wittStructureRat, rename_X, xInTermsOfW_zero, map_X, wittPolynomial_zero,
    map_mul, bindâ‚_X_right, map_wittStructureInt]

@[simp]
theorem wittNeg_zero : wittNeg p 0 = -X (0, 0) := by
  apply MvPolynomial.map_injective (Int.castRingHom â„š) Int.cast_injective
  simp only [wittNeg, wittStructureRat, rename_X, xInTermsOfW_zero, map_X, wittPolynomial_zero,
    map_neg, bindâ‚_X_right, map_wittStructureInt]

@[simp]
theorem constantCoeff_wittAdd (n : â„•) : constantCoeff (wittAdd p n) = 0 := by
  apply constantCoeff_wittStructureInt p _ _ n
  simp only [add_zero, RingHom.map_add, constantCoeff_X]

@[simp]
theorem constantCoeff_wittSub (n : â„•) : constantCoeff (wittSub p n) = 0 := by
  apply constantCoeff_wittStructureInt p _ _ n
  simp only [sub_zero, RingHom.map_sub, constantCoeff_X]

@[simp]
theorem constantCoeff_wittMul (n : â„•) : constantCoeff (wittMul p n) = 0 := by
  apply constantCoeff_wittStructureInt p _ _ n
  simp only [mul_zero, RingHom.map_mul, constantCoeff_X]

@[simp]
theorem constantCoeff_wittNeg (n : â„•) : constantCoeff (wittNeg p n) = 0 := by
  apply constantCoeff_wittStructureInt p _ _ n
  simp only [neg_zero, RingHom.map_neg, constantCoeff_X]

@[simp]
theorem constantCoeff_wittNSMul (m : â„•) (n : â„•) : constantCoeff (wittNSMul p m n) = 0 := by
  apply constantCoeff_wittStructureInt p _ _ n
  simp only [smul_zero, map_nsmul, constantCoeff_X]

@[simp]
theorem constantCoeff_wittZSMul (z : â„¤) (n : â„•) : constantCoeff (wittZSMul p z n) = 0 := by
  apply constantCoeff_wittStructureInt p _ _ n
  simp only [smul_zero, map_zsmul, constantCoeff_X]

end WittStructureSimplifications

section Coeff

variable (R)

@[simp]
theorem zero_coeff (n : â„•) : (0 : ğ• R).coeff n = 0 :=
  show (aeval _ (wittZero p n) : R) = 0 by simp only [wittZero_eq_zero, map_zero]

@[simp]
theorem one_coeff_zero : (1 : ğ• R).coeff 0 = 1 :=
  show (aeval _ (wittOne p 0) : R) = 1 by simp only [wittOne_zero_eq_one, map_one]

@[simp]
theorem one_coeff_eq_of_pos (n : â„•) (hn : 0 < n) : coeff (1 : ğ• R) n = 0 :=
  show (aeval _ (wittOne p n) : R) = 0 by simp only [hn, wittOne_pos_eq_zero, map_zero]

variable {p R}

@[simp]
theorem v2_coeff {p' R'} (x y : WittVector p' R') (i : Fin 2) :
    (![x, y] i).coeff = ![x.coeff, y.coeff] i := by fin_cases i <;> simp

theorem add_coeff (x y : ğ• R) (n : â„•) :
    (x + y).coeff n = peval (wittAdd p n) ![x.coeff, y.coeff] := by
  simp [(Â· + Â·), Add.add, eval]

theorem sub_coeff (x y : ğ• R) (n : â„•) :
    (x - y).coeff n = peval (wittSub p n) ![x.coeff, y.coeff] := by
  simp [(Â· - Â·), Sub.sub, eval]

theorem mul_coeff (x y : ğ• R) (n : â„•) :
    (x * y).coeff n = peval (wittMul p n) ![x.coeff, y.coeff] := by
  simp [(Â· * Â·), Mul.mul, eval]

theorem neg_coeff (x : ğ• R) (n : â„•) : (-x).coeff n = peval (wittNeg p n) ![x.coeff] := by
  simp [Neg.neg, eval, Matrix.cons_fin_one]

theorem nsmul_coeff (m : â„•) (x : ğ• R) (n : â„•) :
    (m â€¢ x).coeff n = peval (wittNSMul p m n) ![x.coeff] := by
  simp [(Â· â€¢ Â·), SMul.smul, eval, Matrix.cons_fin_one]

theorem zsmul_coeff (m : â„¤) (x : ğ• R) (n : â„•) :
    (m â€¢ x).coeff n = peval (wittZSMul p m n) ![x.coeff] := by
  simp [(Â· â€¢ Â·), SMul.smul, eval, Matrix.cons_fin_one]

theorem pow_coeff (m : â„•) (x : ğ• R) (n : â„•) :
    (x ^ m).coeff n = peval (wittPow p m n) ![x.coeff] := by
  simp [(Â· ^ Â·), Pow.pow, eval, Matrix.cons_fin_one]

theorem add_coeff_zero (x y : ğ• R) : (x + y).coeff 0 = x.coeff 0 + y.coeff 0 := by
  simp [add_coeff, peval, Function.uncurry]

theorem mul_coeff_zero (x y : ğ• R) : (x * y).coeff 0 = x.coeff 0 * y.coeff 0 := by
  simp [mul_coeff, peval, Function.uncurry]

end Coeff

theorem wittAdd_vars (n : â„•) : (wittAdd p n).vars âŠ† Finset.univ Ã—Ë¢ Finset.range (n + 1) :=
  wittStructureInt_vars _ _ _

theorem wittSub_vars (n : â„•) : (wittSub p n).vars âŠ† Finset.univ Ã—Ë¢ Finset.range (n + 1) :=
  wittStructureInt_vars _ _ _

theorem wittMul_vars (n : â„•) : (wittMul p n).vars âŠ† Finset.univ Ã—Ë¢ Finset.range (n + 1) :=
  wittStructureInt_vars _ _ _

theorem wittNeg_vars (n : â„•) : (wittNeg p n).vars âŠ† Finset.univ Ã—Ë¢ Finset.range (n + 1) :=
  wittStructureInt_vars _ _ _

theorem wittNSMul_vars (m : â„•) (n : â„•) :
    (wittNSMul p m n).vars âŠ† Finset.univ Ã—Ë¢ Finset.range (n + 1) :=
  wittStructureInt_vars _ _ _

theorem wittZSMul_vars (m : â„¤) (n : â„•) :
    (wittZSMul p m n).vars âŠ† Finset.univ Ã—Ë¢ Finset.range (n + 1) :=
  wittStructureInt_vars _ _ _

theorem wittPow_vars (m : â„•) (n : â„•) : (wittPow p m n).vars âŠ† Finset.univ Ã—Ë¢ Finset.range (n + 1) :=
  wittStructureInt_vars _ _ _

end WittVector
