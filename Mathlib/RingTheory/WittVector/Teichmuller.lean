/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin
-/
module

public import Mathlib.RingTheory.WittVector.Basic

/-!
# Teichm√ºller lifts

This file defines `WittVector.teichmuller`, a monoid hom `R ‚Üí* ùïé R`, which embeds `r : R` as the
`0`-th component of a Witt vector whose other coefficients are `0`.

## Main declarations

- `WittVector.teichmuller`: the Teichmuller map.
- `WittVector.map_teichmuller`: `WittVector.teichmuller` is a natural transformation.
- `WittVector.ghostComponent_teichmuller`:
  the `n`-th ghost component of `WittVector.teichmuller p r` is `r ^ p ^ n`.

## References

* [Hazewinkel, *Witt Vectors*][Haze09]

* [Commelin and Lewis, *Formalizing the Ring of Witt Vectors*][CL21]
-/

@[expose] public section


namespace WittVector

open MvPolynomial

variable (p : ‚Ñï) {R S : Type*} [hp : Fact p.Prime] [CommRing R] [CommRing S]

local notation "ùïé" => WittVector p -- type as `\bbW`

/-- The underlying function of the monoid hom `WittVector.teichmuller`.
The `0`-th coefficient of `teichmullerFun p r` is `r`, and all others are `0`.
-/
def teichmullerFun (r : R) : ùïé R :=
  ‚ü®fun n => if n = 0 then r else 0‚ü©

/-!
## `teichmuller` is a monoid homomorphism

On ghost components, it is clear that `teichmullerFun` is a monoid homomorphism.
But in general the ghost map is not injective.
We follow the same strategy as for proving that the ring operations on `ùïé R`
satisfy the ring axioms.

1. We first prove it for rings `R` where `p` is invertible,
   because then the ghost map is in fact an isomorphism.
2. After that, we derive the result for `MvPolynomial R ‚Ñ§`,
3. and from that we can prove the result for arbitrary `R`.
-/


private theorem ghostComponent_teichmullerFun (r : R) (n : ‚Ñï) :
    ghostComponent n (teichmullerFun p r) = r ^ p ^ n := by
  rw [ghostComponent_apply, aeval_wittPolynomial, Finset.sum_eq_single 0, pow_zero, one_mul,
    tsub_zero]
  ¬∑ rfl
  ¬∑ intro i _ h0
    simp [teichmullerFun, h0, hp.1.ne_zero]
  ¬∑ rw [Finset.mem_range]; intro h; exact (h (Nat.succ_pos n)).elim

private theorem map_teichmullerFun (f : R ‚Üí+* S) (r : R) :
    map f (teichmullerFun p r) = teichmullerFun p (f r) := by
  ext n; cases n
  ¬∑ rfl
  ¬∑ exact f.map_zero

private theorem teichmuller_mul_aux‚ÇÅ {R : Type*} (x y : MvPolynomial R ‚Ñö) :
    teichmullerFun p (x * y) = teichmullerFun p x * teichmullerFun p y := by
  apply (ghostMap.bijective_of_invertible p (MvPolynomial R ‚Ñö)).1
  rw [map_mul]
  ext1 n
  simp only [Pi.mul_apply, ghostMap_apply, ghostComponent_teichmullerFun, mul_pow]

private theorem teichmuller_mul_aux‚ÇÇ {R : Type*} (x y : MvPolynomial R ‚Ñ§) :
    teichmullerFun p (x * y) = teichmullerFun p x * teichmullerFun p y := by
  refine map_injective (MvPolynomial.map (Int.castRingHom ‚Ñö))
    (MvPolynomial.map_injective _ Int.cast_injective) ?_
  simp only [teichmuller_mul_aux‚ÇÅ, map_teichmullerFun, map_mul]

/-- The Teichm√ºller lift of an element of `R` to `ùïé R`.
The `0`-th coefficient of `teichmuller p r` is `r`, and all others are `0`.
This is a monoid homomorphism. -/
def teichmuller : R ‚Üí* ùïé R where
  toFun := teichmullerFun p
  map_one' := by
    ext ‚ü®‚ü©
    ¬∑ rw [one_coeff_zero]; rfl
    ¬∑ rw [one_coeff_eq_of_pos _ _ _ (Nat.succ_pos _)]; rfl
  map_mul' := by
    intro x y
    rcases counit_surjective R x with ‚ü®x, rfl‚ü©
    rcases counit_surjective R y with ‚ü®y, rfl‚ü©
    simp only [‚Üê map_teichmullerFun, ‚Üê map_mul, teichmuller_mul_aux‚ÇÇ]

@[simp]
theorem teichmuller_coeff_zero (r : R) : (teichmuller p r).coeff 0 = r :=
  rfl

@[simp]
theorem teichmuller_coeff_pos (r : R) : ‚àÄ (n : ‚Ñï) (_ : 0 < n), (teichmuller p r).coeff n = 0
  | _ + 1, _ => rfl

@[simp]
theorem teichmuller_zero : teichmuller p (0 : R) = 0 := by
  ext ‚ü®‚ü© <;> ¬∑ rw [zero_coeff]; rfl

/-- `teichmuller` is a natural transformation. -/
@[simp]
theorem map_teichmuller (f : R ‚Üí+* S) (r : R) : map f (teichmuller p r) = teichmuller p (f r) :=
  map_teichmullerFun _ _ _

/-- The `n`-th ghost component of `teichmuller p r` is `r ^ p ^ n`. -/
@[simp]
theorem ghostComponent_teichmuller (r : R) (n : ‚Ñï) :
    ghostComponent n (teichmuller p r) = r ^ p ^ n :=
  ghostComponent_teichmullerFun _ _ _

end WittVector
