/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin
-/
import Mathlib.RingTheory.WittVector.IsPoly

#align_import ring_theory.witt_vector.mul_p from "leanprover-community/mathlib"@"7abfbc92eec87190fba3ed3d5ec58e7c167e7144"

/-!
## Multiplication by `n` in the ring of Witt vectors

In this file we show that multiplication by `n` in the ring of Witt vectors
is a polynomial function. We then use this fact to show that the composition of Frobenius
and Verschiebung is equal to multiplication by `p`.

### Main declarations

* `mulN_isPoly`: multiplication by `n` is a polynomial function

## References

* [Hazewinkel, *Witt Vectors*][Haze09]

* [Commelin and Lewis, *Formalizing the Ring of Witt Vectors*][CL21]
-/


namespace WittVector

variable {p : ‚Ñï} {R : Type*} [hp : Fact p.Prime] [CommRing R]

local notation "ùïé" => WittVector p -- type as `\bbW`

open MvPolynomial

noncomputable section

variable (p)

/-- `wittMulN p n` is the family of polynomials that computes
the coefficients of `x * n` in terms of the coefficients of the Witt vector `x`. -/
noncomputable def wittMulN : ‚Ñï ‚Üí ‚Ñï ‚Üí MvPolynomial ‚Ñï ‚Ñ§
  | 0 => 0
  | n + 1 => fun k => bind‚ÇÅ (Function.uncurry <| ![wittMulN n, X]) (wittAdd p k)
#align witt_vector.witt_mul_n WittVector.wittMulN

variable {p}

theorem mulN_coeff (n : ‚Ñï) (x : ùïé R) (k : ‚Ñï) :
    (x * n).coeff k = aeval x.coeff (wittMulN p n k) := by
  induction' n with n ih generalizing k
  ¬∑ simp only [Nat.zero_eq, Nat.cast_zero, mul_zero, zero_coeff, wittMulN,
      AlgHom.map_zero, Pi.zero_apply]
  ¬∑ rw [wittMulN, Nat.cast_add, Nat.cast_one, mul_add, mul_one, aeval_bind‚ÇÅ, add_coeff]
    apply eval‚ÇÇHom_congr (RingHom.ext_int _ _) _ rfl
    ext1 ‚ü®b, i‚ü©
    fin_cases b
    ¬∑ simp [Function.uncurry, Matrix.cons_val_zero, ih]
    ¬∑ simp [Function.uncurry, Matrix.cons_val_one, Matrix.head_cons, aeval_X]
#align witt_vector.mul_n_coeff WittVector.mulN_coeff

variable (p)

/-- Multiplication by `n` is a polynomial function. -/
@[is_poly]
theorem mulN_isPoly (n : ‚Ñï) : IsPoly p fun R _Rcr x => x * n :=
  ‚ü®‚ü®wittMulN p n, fun R _Rcr x => by funext k; exact mulN_coeff n x k‚ü©‚ü©
#align witt_vector.mul_n_is_poly WittVector.mulN_isPoly

@[simp]
theorem bind‚ÇÅ_wittMulN_wittPolynomial (n k : ‚Ñï) :
    bind‚ÇÅ (wittMulN p n) (wittPolynomial p ‚Ñ§ k) = n * wittPolynomial p ‚Ñ§ k := by
  induction' n with n ih
  ¬∑ simp [wittMulN, Nat.cast_zero, zero_mul, bind‚ÇÅ_zero_wittPolynomial]
  ¬∑ rw [wittMulN, ‚Üê bind‚ÇÅ_bind‚ÇÅ, wittAdd, wittStructureInt_prop]
    simp only [AlgHom.map_add, Nat.cast_succ, bind‚ÇÅ_X_right]
    rw [add_mul, one_mul, bind‚ÇÅ_rename, bind‚ÇÅ_rename]
    simp only [ih, Function.uncurry, Function.comp, bind‚ÇÅ_X_left, AlgHom.id_apply,
      Matrix.cons_val_zero, Matrix.head_cons, Matrix.cons_val_one]
#align witt_vector.bind‚ÇÅ_witt_mul_n_witt_polynomial WittVector.bind‚ÇÅ_wittMulN_wittPolynomial

end

end WittVector
