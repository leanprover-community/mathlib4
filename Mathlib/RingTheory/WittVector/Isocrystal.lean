/-
Copyright (c) 2022 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import Mathlib.RingTheory.WittVector.FrobeniusFractionField

/-!

## F-isocrystals over a perfect field

When `k` is an integral domain, so is `ùïé k`, and we can consider its field of fractions `K(p, k)`.
The endomorphism `WittVector.frobenius` lifts to `œÜ : K(p, k) ‚Üí K(p, k)`; if `k` is perfect, `œÜ` is
an automorphism.

Let `k` be a perfect integral domain. Let `V` be a vector space over `K(p,k)`.
An *isocrystal* is a bijective map `V ‚Üí V` that is `œÜ`-semilinear.
A theorem of Dieudonn√© and Manin classifies the finite-dimensional isocrystals over algebraically
closed fields. In the one-dimensional case, this classification states that the isocrystal
structures are parametrized by their "slope" `m : ‚Ñ§`.
Any one-dimensional isocrystal is isomorphic to `œÜ(p^m ‚Ä¢ x) : K(p,k) ‚Üí K(p,k)` for some `m`.

This file proves this one-dimensional case of the classification theorem.
The construction is described in Dupuis, Lewis, and Macbeth,
[Formalized functional analysis via semilinear maps][dupuis-lewis-macbeth2022].

## Main declarations

* `WittVector.Isocrystal`: a vector space over the field `K(p, k)` additionally equipped with a
  Frobenius-linear automorphism.
* `WittVector.isocrystal_classification`: a one-dimensional isocrystal admits an isomorphism to one
  of the standard one-dimensional isocrystals.

## Notation

This file introduces notation in the locale `Isocrystal`.
* `K(p, k)`: `FractionRing (WittVector p k)`
* `œÜ(p, k)`: `WittVector.FractionRing.frobeniusRingHom p k`
* `M ‚Üí·∂†À°[p, k] M‚ÇÇ`: `LinearMap (WittVector.FractionRing.frobeniusRingHom p k) M M‚ÇÇ`
* `M ‚âÉ·∂†À°[p, k] M‚ÇÇ`: `LinearEquiv (WittVector.FractionRing.frobeniusRingHom p k) M M‚ÇÇ`
* `Œ¶(p, k)`: `WittVector.Isocrystal.frobenius p k`
* `M ‚Üí·∂†‚Å±[p, k] M‚ÇÇ`: `WittVector.IsocrystalHom p k M M‚ÇÇ`
* `M ‚âÉ·∂†‚Å±[p, k] M‚ÇÇ`: `WittVector.IsocrystalEquiv p k M M‚ÇÇ`

## References

* [Formalized functional analysis via semilinear maps][dupuis-lewis-macbeth2022]
* [Theory of commutative formal groups over fields of finite characteristic][manin1963]
* <https://www.math.ias.edu/~lurie/205notes/Lecture26-Isocrystals.pdf>

-/

noncomputable section

open Module

namespace WittVector

variable (p : ‚Ñï) [Fact p.Prime]
variable (k : Type*) [CommRing k]

/-- The fraction ring of the space of `p`-Witt vectors on `k` -/
scoped[Isocrystal] notation "K(" p ", " k ")" => FractionRing (WittVector p k)

open Isocrystal

section PerfectRing

variable [IsDomain k] [CharP k p] [PerfectRing k p]

/-! ### Frobenius-linear maps -/


/-- The Frobenius automorphism of `k` induces an automorphism of `K`. -/
def FractionRing.frobenius : K(p, k) ‚âÉ+* K(p, k) :=
  IsFractionRing.ringEquivOfRingEquiv (frobeniusEquiv p k)

/-- The Frobenius automorphism of `k` induces an endomorphism of `K`. For notation purposes.
Notation `œÜ(p, k)` in the `Isocrystal` namespace. -/
def FractionRing.frobeniusRingHom : K(p, k) ‚Üí+* K(p, k) :=
  FractionRing.frobenius p k

@[inherit_doc]
scoped[Isocrystal] notation "œÜ(" p ", " k ")" => WittVector.FractionRing.frobeniusRingHom p k

instance inv_pair‚ÇÅ : RingHomInvPair œÜ(p, k) (FractionRing.frobenius p k).symm :=
  RingHomInvPair.of_ringEquiv (FractionRing.frobenius p k)

instance inv_pair‚ÇÇ : RingHomInvPair ((FractionRing.frobenius p k).symm : K(p, k) ‚Üí+* K(p, k))
    (FractionRing.frobenius p k) :=
  RingHomInvPair.of_ringEquiv (FractionRing.frobenius p k).symm

/-- The Frobenius automorphism of `k`, as a linear map -/
scoped[Isocrystal]
  notation3:50 M " ‚Üí·∂†À°[" p ", " k "] " M‚ÇÇ =>
    LinearMap (WittVector.FractionRing.frobeniusRingHom p k) M M‚ÇÇ

/-- The Frobenius automorphism of `k`, as a linear equivalence -/
scoped[Isocrystal]
  notation3:50 M " ‚âÉ·∂†À°[" p ", " k "] " M‚ÇÇ =>
    LinearEquiv (WittVector.FractionRing.frobeniusRingHom p k) M M‚ÇÇ

/-! ### Isocrystals -/


/-- An isocrystal is a vector space over the field `K(p, k)` additionally equipped with a
Frobenius-linear automorphism.
-/
class Isocrystal (V : Type*) [AddCommGroup V] extends Module K(p, k) V where
  frob : V ‚âÉ·∂†À°[p, k] V

open WittVector

variable (V : Type*) [AddCommGroup V] [Isocrystal p k V]
variable (V‚ÇÇ : Type*) [AddCommGroup V‚ÇÇ] [Isocrystal p k V‚ÇÇ]

variable {V} in
/--
Project the Frobenius automorphism from an isocrystal. Denoted by `Œ¶(p, k)` when V can be inferred.
-/
def Isocrystal.frobenius : V ‚âÉ·∂†À°[p, k] V :=
  Isocrystal.frob (p := p) (k := k) (V := V)

@[inherit_doc] scoped[Isocrystal] notation "Œ¶(" p ", " k ")" => WittVector.Isocrystal.frobenius p k

/-- A homomorphism between isocrystals respects the Frobenius map.
Notation `M ‚Üí·∂†‚Å± [p, k]` in the `Isocrystal` namespace. -/
structure IsocrystalHom extends V ‚Üí‚Çó[K(p, k)] V‚ÇÇ where
  frob_equivariant : ‚àÄ x : V, Œ¶(p, k) (toLinearMap x) = toLinearMap (Œ¶(p, k) x)

/-- An isomorphism between isocrystals respects the Frobenius map.

Notation `M ‚âÉ·∂†‚Å± [p, k]` in the `Isocrystal` namespace. -/
structure IsocrystalEquiv extends V ‚âÉ‚Çó[K(p, k)] V‚ÇÇ where
  frob_equivariant : ‚àÄ x : V, Œ¶(p, k) (toLinearEquiv x) = toLinearEquiv (Œ¶(p, k) x)

@[inherit_doc] scoped[Isocrystal]
notation:50 M " ‚Üí·∂†‚Å±[" p ", " k "] " M‚ÇÇ => WittVector.IsocrystalHom p k M M‚ÇÇ

@[inherit_doc] scoped[Isocrystal]
notation:50 M " ‚âÉ·∂†‚Å±[" p ", " k "] " M‚ÇÇ => WittVector.IsocrystalEquiv p k M M‚ÇÇ

end PerfectRing

open scoped Isocrystal

/-! ### Classification of isocrystals in dimension 1 -/

/-- Type synonym for `K(p, k)` to carry the standard 1-dimensional isocrystal structure
of slope `m : ‚Ñ§`.
-/
@[nolint unusedArguments]
def StandardOneDimIsocrystal (_m : ‚Ñ§) : Type _ :=
  K(p, k)
-- The `AddCommGroup, Module K(p, k)` instances should be constructed by a deriving handler.
-- https://github.com/leanprover-community/mathlib4/issues/380

section Deriving

instance {m : ‚Ñ§} : AddCommGroup (StandardOneDimIsocrystal p k m) :=
  inferInstanceAs (AddCommGroup K(p, k))
instance {m : ‚Ñ§} : Module K(p, k) (StandardOneDimIsocrystal p k m) :=
  inferInstanceAs (Module K(p, k) K(p, k))

end Deriving

section PerfectRing

variable [IsDomain k] [CharP k p] [PerfectRing k p]

/-- The standard one-dimensional isocrystal of slope `m : ‚Ñ§` is an isocrystal. -/
instance (m : ‚Ñ§) : Isocrystal p k (StandardOneDimIsocrystal p k m) where
  frob :=
    (FractionRing.frobenius p k).toSemilinearEquiv.trans
      (LinearEquiv.smulOfNeZero _ _ _ (zpow_ne_zero m (WittVector.FractionRing.p_nonzero p k)))

@[simp]
theorem StandardOneDimIsocrystal.frobenius_apply (m : ‚Ñ§) (x : StandardOneDimIsocrystal p k m) :
    Œ¶(p, k) x = (p : K(p, k)) ^ m ‚Ä¢ œÜ(p, k) x := rfl

end PerfectRing

/-- A one-dimensional isocrystal over an algebraically closed field
admits an isomorphism to one of the standard (indexed by `m : ‚Ñ§`) one-dimensional isocrystals. -/
theorem isocrystal_classification (k : Type*) [Field k] [IsAlgClosed k] [CharP k p] (V : Type*)
    [AddCommGroup V] [Isocrystal p k V] (h_dim : finrank K(p, k) V = 1) :
    ‚àÉ m : ‚Ñ§, Nonempty (StandardOneDimIsocrystal p k m ‚âÉ·∂†‚Å±[p, k] V) := by
  haveI : Nontrivial V := Module.nontrivial_of_finrank_eq_succ h_dim
  obtain ‚ü®x, hx‚ü© : ‚àÉ x : V, x ‚â† 0 := exists_ne 0
  have : Œ¶(p, k) x ‚â† 0 := by simpa only [map_zero] using Œ¶(p, k).injective.ne hx
  obtain ‚ü®a, ha, hax‚ü© : ‚àÉ a : K(p, k), a ‚â† 0 ‚àß Œ¶(p, k) x = a ‚Ä¢ x := by
    rw [finrank_eq_one_iff_of_nonzero' x hx] at h_dim
    obtain ‚ü®a, ha‚ü© := h_dim (Œ¶(p, k) x)
    refine ‚ü®a, ?_, ha.symm‚ü©
    intro ha'
    apply this
    simp only [‚Üê ha, ha', zero_smul]
  obtain ‚ü®b, hb, m, hmb‚ü© := WittVector.exists_frobenius_solution_fractionRing p ha
  replace hmb : œÜ(p, k) b * a = (p : K(p, k)) ^ m * b := by convert hmb
  use m
  let F‚ÇÄ : StandardOneDimIsocrystal p k m ‚Üí‚Çó[K(p, k)] V := LinearMap.toSpanSingleton K(p, k) V x
  let F : StandardOneDimIsocrystal p k m ‚âÉ‚Çó[K(p, k)] V := by
    refine LinearEquiv.ofBijective F‚ÇÄ ‚ü®?_, ?_‚ü©
    ¬∑ rw [‚Üê LinearMap.ker_eq_bot]
      exact LinearMap.ker_toSpanSingleton K(p, k) V hx
    ¬∑ rw [‚Üê LinearMap.range_eq_top]
      rw [‚Üê (finrank_eq_one_iff_of_nonzero x hx).mp h_dim]
      rw [LinearMap.span_singleton_eq_range]
  refine ‚ü®‚ü®(LinearEquiv.smulOfNeZero K(p, k) _ _ hb).trans F, fun c ‚Ü¶ ?_‚ü©‚ü©
  rw [LinearEquiv.trans_apply, LinearEquiv.trans_apply, LinearEquiv.smulOfNeZero_apply,
    LinearEquiv.smulOfNeZero_apply, LinearEquiv.map_smul, LinearEquiv.map_smul,
    LinearEquiv.ofBijective_apply, LinearEquiv.ofBijective_apply,
    StandardOneDimIsocrystal.frobenius_apply]
  unfold StandardOneDimIsocrystal
  rw [LinearMap.toSpanSingleton_apply K(p, k) V x c, LinearMap.toSpanSingleton_apply K(p, k) V x]
  simp only [hax,
    LinearEquiv.map_smul‚Çõ‚Çó, Algebra.id.smul_eq_mul]
  simp only [‚Üê mul_smul]
  congr 1
  linear_combination œÜ(p, k) c * hmb

end WittVector
