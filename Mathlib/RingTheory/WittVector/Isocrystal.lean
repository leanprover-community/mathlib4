/-
Copyright (c) 2022 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import Mathlib.RingTheory.WittVector.FrobeniusFractionField

#align_import ring_theory.witt_vector.isocrystal from "leanprover-community/mathlib"@"6d584f1709bedbed9175bd9350df46599bdd7213"

/-!

## F-isocrystals over a perfect field

When `k` is an integral domain, so is `ğ• k`, and we can consider its field of fractions `K(p, k)`.
The endomorphism `WittVector.frobenius` lifts to `Ï† : K(p, k) â†’ K(p, k)`; if `k` is perfect, `Ï†` is
an automorphism.

Let `k` be a perfect integral domain. Let `V` be a vector space over `K(p,k)`.
An *isocrystal* is a bijective map `V â†’ V` that is `Ï†`-semilinear.
A theorem of DieudonnÃ© and Manin classifies the finite-dimensional isocrystals over algebraically
closed fields. In the one-dimensional case, this classification states that the isocrystal
structures are parametrized by their "slope" `m : â„¤`.
Any one-dimensional isocrystal is isomorphic to `Ï†(p^m â€¢ x) : K(p,k) â†’ K(p,k)` for some `m`.

This file proves this one-dimensional case of the classification theorem.
The construction is described in Dupuis, Lewis, and Macbeth,
[Formalized functional analysis via semilinear maps][dupuis-lewis-macbeth2022].

## Main declarations

* `WittVector.Isocrystal`: a vector space over the field `K(p, k)` additionally equipped with a
  Frobenius-linear automorphism.
* `WittVector.isocrystal_classification`: a one-dimensional isocrystal admits an isomorphism to one
  of the standard one-dimensional isocrystals.

## Notation

This file introduces notation in the locale `isocrystal`.
* `K(p, k)`: `FractionRing (WittVector p k)`
* `Ï†(p, k)`: `WittVector.FractionRing.frobeniusRingHom p k`
* `M â†’á¶ Ë¡[p, k] Mâ‚‚`: `LinearMap (WittVector.FractionRing.frobeniusRingHom p k) M Mâ‚‚`
* `M â‰ƒá¶ Ë¡[p, k] Mâ‚‚`: `LinearEquiv (WittVector.FractionRing.frobeniusRingHom p k) M Mâ‚‚`
* `Î¦(p, k)`: `WittVector.Isocrystal.frobenius p k`
* `M â†’á¶ â±[p, k] Mâ‚‚`: `WittVector.IsocrystalHom p k M Mâ‚‚`
* `M â‰ƒá¶ â±[p, k] Mâ‚‚`: `WittVector.IsocrystalEquiv p k M Mâ‚‚`

## References

* [Formalized functional analysis via semilinear maps][dupuis-lewis-macbeth2022]
* [Theory of commutative formal groups over fields of finite characteristic][manin1963]
* <https://www.math.ias.edu/~lurie/205notes/Lecture26-Isocrystals.pdf>

-/

set_option autoImplicit true


noncomputable section

open FiniteDimensional

namespace WittVector

variable (p : â„•) [Fact p.Prime]

variable (k : Type*) [CommRing k]

scoped[Isocrystal] notation "K(" p ", " k ")" => FractionRing (WittVector p k)

open Isocrystal

section PerfectRing

variable [IsDomain k] [CharP k p] [PerfectRing k p]

/-! ### Frobenius-linear maps -/


/-- The Frobenius automorphism of `k` induces an automorphism of `K`. -/
def FractionRing.frobenius : K(p, k) â‰ƒ+* K(p, k) :=
  IsFractionRing.fieldEquivOfRingEquiv (frobeniusEquiv p k)
#align witt_vector.fraction_ring.frobenius WittVector.FractionRing.frobenius

/-- The Frobenius automorphism of `k` induces an endomorphism of `K`. For notation purposes. -/
def FractionRing.frobeniusRingHom : K(p, k) â†’+* K(p, k) :=
  FractionRing.frobenius p k
#align witt_vector.fraction_ring.frobenius_ring_hom WittVector.FractionRing.frobeniusRingHom

scoped[Isocrystal] notation "Ï†(" p ", " k ")" => WittVector.FractionRing.frobeniusRingHom p k

instance inv_pairâ‚ : RingHomInvPair Ï†(p, k) (FractionRing.frobenius p k).symm :=
  RingHomInvPair.of_ringEquiv (FractionRing.frobenius p k)
#align witt_vector.inv_pairâ‚ WittVector.inv_pairâ‚

instance inv_pairâ‚‚ : RingHomInvPair ((FractionRing.frobenius p k).symm : K(p, k) â†’+* K(p, k))
    (FractionRing.frobenius p k) :=
  RingHomInvPair.of_ringEquiv (FractionRing.frobenius p k).symm
#align witt_vector.inv_pairâ‚‚ WittVector.inv_pairâ‚‚

scoped[Isocrystal]
  notation:50 M " â†’á¶ Ë¡[" p ", " k "] " Mâ‚‚ =>
    LinearMap (WittVector.FractionRing.frobeniusRingHom p k) M Mâ‚‚

scoped[Isocrystal]
  notation:50 M " â‰ƒá¶ Ë¡[" p ", " k "] " Mâ‚‚ =>
    LinearEquiv (WittVector.FractionRing.frobeniusRingHom p k) M Mâ‚‚

/-! ### Isocrystals -/


/-- An isocrystal is a vector space over the field `K(p, k)` additionally equipped with a
Frobenius-linear automorphism.
-/
class Isocrystal (V : Type*) [AddCommGroup V] extends Module K(p, k) V where
  frob : V â‰ƒá¶ Ë¡[p, k] V
#align witt_vector.isocrystal WittVector.Isocrystal

open WittVector

variable (V : Type*) [AddCommGroup V] [Isocrystal p k V]

variable (Vâ‚‚ : Type*) [AddCommGroup Vâ‚‚] [Isocrystal p k Vâ‚‚]

variable {V}

/--
Project the Frobenius automorphism from an isocrystal. Denoted by `Î¦(p, k)` when V can be inferred.
-/
def Isocrystal.frobenius : V â‰ƒá¶ Ë¡[p, k] V :=
  @Isocrystal.frob p _ k _ _ _ _ _ _ _
#align witt_vector.isocrystal.frobenius WittVector.Isocrystal.frobenius

variable (V)

scoped[Isocrystal] notation "Î¦(" p ", " k ")" => WittVector.Isocrystal.frobenius p k

/-- A homomorphism between isocrystals respects the Frobenius map. -/
-- Porting note(https://github.com/leanprover-community/mathlib4/issues/5171):
-- removed @[nolint has_nonempty_instance]
structure IsocrystalHom extends V â†’â‚—[K(p, k)] Vâ‚‚ where
  frob_equivariant : âˆ€ x : V, Î¦(p, k) (toLinearMap x) = toLinearMap (Î¦(p, k) x)
#align witt_vector.isocrystal_hom WittVector.IsocrystalHom

/-- An isomorphism between isocrystals respects the Frobenius map. -/
-- Porting note(https://github.com/leanprover-community/mathlib4/issues/5171):
-- removed @[nolint has_nonempty_instance]
structure IsocrystalEquiv extends V â‰ƒâ‚—[K(p, k)] Vâ‚‚ where
  frob_equivariant : âˆ€ x : V, Î¦(p, k) (toLinearEquiv x) = toLinearEquiv (Î¦(p, k) x)
#align witt_vector.isocrystal_equiv WittVector.IsocrystalEquiv

scoped[Isocrystal] notation:50 M " â†’á¶ â±[" p ", " k "] " Mâ‚‚ => WittVector.IsocrystalHom p k M Mâ‚‚

scoped[Isocrystal] notation:50 M " â‰ƒá¶ â±[" p ", " k "] " Mâ‚‚ => WittVector.IsocrystalEquiv p k M Mâ‚‚

end PerfectRing

open scoped Isocrystal

/-! ### Classification of isocrystals in dimension 1 -/


/-- A helper instance for type class inference. -/
@[local instance]
def FractionRing.module : Module K(p, k) K(p, k) :=
  Semiring.toModule
#align witt_vector.fraction_ring.module WittVector.FractionRing.module

/-- Type synonym for `K(p, k)` to carry the standard 1-dimensional isocrystal structure
of slope `m : â„¤`.
-/
@[nolint unusedArguments]
-- Porting note(https://github.com/leanprover-community/mathlib4/issues/5171):
-- removed @[nolint has_nonempty_instance]
def StandardOneDimIsocrystal (_m : â„¤) : Type _ :=
  K(p, k)
#align witt_vector.standard_one_dim_isocrystal WittVector.StandardOneDimIsocrystal

-- Porting note(https://github.com/leanprover-community/mathlib4/issues/5020): added
section Deriving

instance : AddCommGroup (StandardOneDimIsocrystal p k m) :=
  inferInstanceAs (AddCommGroup K(p, k))
instance : Module K(p, k) (StandardOneDimIsocrystal p k m) :=
  inferInstanceAs (Module K(p, k) K(p, k))

end Deriving

section PerfectRing

variable [IsDomain k] [CharP k p] [PerfectRing k p]

/-- The standard one-dimensional isocrystal of slope `m : â„¤` is an isocrystal. -/
instance (m : â„¤) : Isocrystal p k (StandardOneDimIsocrystal p k m) where
  frob :=
    (FractionRing.frobenius p k).toSemilinearEquiv.trans
      (LinearEquiv.smulOfNeZero _ _ _ (zpow_ne_zero m (WittVector.FractionRing.p_nonzero p k)))

@[simp]
theorem StandardOneDimIsocrystal.frobenius_apply (m : â„¤) (x : StandardOneDimIsocrystal p k m) :
    Î¦(p, k) x = (p : K(p, k)) ^ m â€¢ Ï†(p, k) x := by
  -- Porting note: was just `rfl`
  erw [smul_eq_mul]
  -- âŠ¢ â†‘Î¦(p, k) x = â†‘(IsFractionRing.lift (_ : Function.Injective â†‘(algebraMap (Wit â€¦
  simp only [map_zpowâ‚€, map_natCast]
  -- âŠ¢ â†‘Î¦(p, k) x = â†‘p ^ m * â†‘Ï†(p, k) x
  rfl
  -- ğŸ‰ no goals
#align witt_vector.standard_one_dim_isocrystal.frobenius_apply WittVector.StandardOneDimIsocrystal.frobenius_apply

end PerfectRing

/-- A one-dimensional isocrystal over an algebraically closed field
admits an isomorphism to one of the standard (indexed by `m : â„¤`) one-dimensional isocrystals. -/
theorem isocrystal_classification (k : Type*) [Field k] [IsAlgClosed k] [CharP k p] (V : Type*)
    [AddCommGroup V] [Isocrystal p k V] (h_dim : finrank K(p, k) V = 1) :
    âˆƒ m : â„¤, Nonempty (StandardOneDimIsocrystal p k m â‰ƒá¶ â±[p, k] V) := by
  haveI : Nontrivial V := FiniteDimensional.nontrivial_of_finrank_eq_succ h_dim
  -- âŠ¢ âˆƒ m, Nonempty (StandardOneDimIsocrystal p k m â‰ƒá¶ â±[p, k] V)
  obtain âŸ¨x, hxâŸ© : âˆƒ x : V, x â‰  0 := exists_ne 0
  -- âŠ¢ âˆƒ m, Nonempty (StandardOneDimIsocrystal p k m â‰ƒá¶ â±[p, k] V)
  have : Î¦(p, k) x â‰  0 := by simpa only [map_zero] using Î¦(p, k).injective.ne hx
  -- âŠ¢ âˆƒ m, Nonempty (StandardOneDimIsocrystal p k m â‰ƒá¶ â±[p, k] V)
  obtain âŸ¨a, ha, haxâŸ© : âˆƒ a : K(p, k), a â‰  0 âˆ§ Î¦(p, k) x = a â€¢ x := by
    rw [finrank_eq_one_iff_of_nonzero' x hx] at h_dim
    obtain âŸ¨a, haâŸ© := h_dim (Î¦(p, k) x)
    refine' âŸ¨a, _, ha.symmâŸ©
    intro ha'
    apply this
    simp only [â† ha, ha', zero_smul]
  obtain âŸ¨b, hb, m, hmbâŸ© := WittVector.exists_frobenius_solution_fractionRing p ha
  -- âŠ¢ âˆƒ m, Nonempty (StandardOneDimIsocrystal p k m â‰ƒá¶ â±[p, k] V)
  replace hmb : Ï†(p, k) b * a = (p : K(p, k)) ^ m * b := by convert hmb
  -- âŠ¢ âˆƒ m, Nonempty (StandardOneDimIsocrystal p k m â‰ƒá¶ â±[p, k] V)
  use m
  -- âŠ¢ Nonempty (StandardOneDimIsocrystal p k m â‰ƒá¶ â±[p, k] V)
  let Fâ‚€ : StandardOneDimIsocrystal p k m â†’â‚—[K(p, k)] V := LinearMap.toSpanSingleton K(p, k) V x
  -- âŠ¢ Nonempty (StandardOneDimIsocrystal p k m â‰ƒá¶ â±[p, k] V)
  let F : StandardOneDimIsocrystal p k m â‰ƒâ‚—[K(p, k)] V := by
    refine' LinearEquiv.ofBijective Fâ‚€ âŸ¨_, _âŸ©
    Â· rw [â† LinearMap.ker_eq_bot]
      exact LinearMap.ker_toSpanSingleton K(p, k) V hx
    Â· rw [â† LinearMap.range_eq_top]
      rw [â† (finrank_eq_one_iff_of_nonzero x hx).mp h_dim]
      rw [LinearMap.span_singleton_eq_range]
  -- Porting note: `refine'` below gets confused when this is inlined.
  let E := (LinearEquiv.smulOfNeZero K(p, k) _ _ hb).trans F
  -- âŠ¢ Nonempty (StandardOneDimIsocrystal p k m â‰ƒá¶ â±[p, k] V)
  refine' âŸ¨âŸ¨E, _âŸ©âŸ©
  -- âŠ¢ âˆ€ (x : StandardOneDimIsocrystal p k m), â†‘Î¦(p, k) (â†‘E x) = â†‘E (â†‘Î¦(p, k) x)
  simp only
  -- âŠ¢ âˆ€ (x_1 : StandardOneDimIsocrystal p k m), â†‘Î¦(p, k) (â†‘(LinearEquiv.trans (Lin â€¦
  intro c
  -- âŠ¢ â†‘Î¦(p, k) (â†‘(LinearEquiv.trans (LinearEquiv.smulOfNeZero K(p, k) (StandardOne â€¦
  rw [LinearEquiv.trans_apply, LinearEquiv.trans_apply, LinearEquiv.smulOfNeZero_apply,
    LinearEquiv.smulOfNeZero_apply, LinearEquiv.map_smul, LinearEquiv.map_smul]
  -- Porting note: was
  -- simp only [hax, LinearEquiv.ofBijective_apply, LinearMap.toSpanSingleton_apply,
  --   LinearEquiv.map_smulâ‚›â‚—, StandardOneDimIsocrystal.frobenius_apply, Algebra.id.smul_eq_mul]
  rw [LinearEquiv.ofBijective_apply, LinearEquiv.ofBijective_apply]
  -- âŠ¢ â†‘Î¦(p, k) (b â€¢ â†‘(LinearMap.toSpanSingleton K(p, k) V x) c) = b â€¢ â†‘(LinearMap. â€¦
  erw [LinearMap.toSpanSingleton_apply K(p, k) V x c, LinearMap.toSpanSingleton_apply K(p, k) V x]
  -- âŠ¢ â†‘Î¦(p, k) (b â€¢ c â€¢ x) = b â€¢ â†‘Î¦(p, k) c â€¢ x
  simp only [hax, LinearEquiv.ofBijective_apply, LinearMap.toSpanSingleton_apply,
    LinearEquiv.map_smulâ‚›â‚—, StandardOneDimIsocrystal.frobenius_apply, Algebra.id.smul_eq_mul]
  simp only [â† mul_smul]
  -- âŠ¢ (â†‘Ï†(p, k) b * (â†‘Ï†(p, k) c * a)) â€¢ x = (b * â†‘p ^ m â€¢ â†‘Ï†(p, k) c) â€¢ x
  congr 1
  -- âŠ¢ â†‘Ï†(p, k) b * (â†‘Ï†(p, k) c * a) = b * â†‘p ^ m â€¢ â†‘Ï†(p, k) c
  -- Porting note: added the next two lines
  erw [smul_eq_mul]
  -- âŠ¢ â†‘Ï†(p, k) b * (â†‘Ï†(p, k) c * a) = b * (â†‘(IsFractionRing.lift (_ : Function.Inj â€¦
  simp only [map_zpowâ‚€, map_natCast]
  -- âŠ¢ â†‘Ï†(p, k) b * (â†‘Ï†(p, k) c * a) = b * (â†‘p ^ m * â†‘Ï†(p, k) c)
  linear_combination Ï†(p, k) c * hmb
  -- ğŸ‰ no goals
#align witt_vector.isocrystal_classification WittVector.isocrystal_classification

end WittVector
