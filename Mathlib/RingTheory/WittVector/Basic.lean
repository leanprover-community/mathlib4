/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Robert Y. Lewis
-/
import Mathlib.Data.MvPolynomial.Counit
import Mathlib.Data.MvPolynomial.Invertible
import Mathlib.RingTheory.WittVector.Defs

#align_import ring_theory.witt_vector.basic from "leanprover-community/mathlib"@"9556784a5b84697562e9c6acb40500d4a82e675a"

/-!
# Witt vectors

This file verifies that the ring operations on `WittVector p R`
satisfy the axioms of a commutative ring.

## Main definitions

* `WittVector.map`: lifts a ring homomorphism `R â†’+* S` to a ring homomorphism `ğ• R â†’+* ğ• S`.
* `WittVector.ghostComponent n x`: evaluates the `n`th Witt polynomial
  on the first `n` coefficients of `x`, producing a value in `R`.
  This is a ring homomorphism.
* `WittVector.ghostMap`: a ring homomorphism `ğ• R â†’+* (â„• â†’ R)`, obtained by packaging
  all the ghost components together.
  If `p` is invertible in `R`, then the ghost map is an equivalence,
  which we use to define the ring operations on `ğ• R`.
* `WittVector.CommRing`: the ring structure induced by the ghost components.

## Notation

We use notation `ğ• R`, entered `\bbW`, for the Witt vectors over `R`.

## Implementation details

As we prove that the ghost components respect the ring operations, we face a number of repetitive
proofs. To avoid duplicating code we factor these proofs into a custom tactic, only slightly more
powerful than a tactic macro. This tactic is not particularly useful outside of its applications
in this file.

## References

* [Hazewinkel, *Witt Vectors*][Haze09]

* [Commelin and Lewis, *Formalizing the Ring of Witt Vectors*][CL21]

-/


noncomputable section

open MvPolynomial Function

open scoped BigOperators

variable {p : â„•} {R S T : Type*} [hp : Fact p.Prime] [CommRing R] [CommRing S] [CommRing T]

variable {Î± : Type*} {Î² : Type*}

-- mathport name: exprğ•
local notation "ğ•" => WittVector p
local notation "W_" => wittPolynomial p

-- type as `\bbW`
open scoped Witt

namespace WittVector

/-- `f : Î± â†’ Î²` induces a map from `ğ• Î±` to `ğ• Î²` by applying `f` componentwise.
If `f` is a ring homomorphism, then so is `f`, see `WittVector.map f`. -/
def mapFun (f : Î± â†’ Î²) : ğ• Î± â†’ ğ• Î² := fun x => mk _ (f âˆ˜ x.coeff)
#align witt_vector.map_fun WittVector.mapFun

namespace mapFun

-- porting note: switched the proof to tactic mode. I think that `ext` was the issue.
theorem injective (f : Î± â†’ Î²) (hf : Injective f) : Injective (mapFun f : ğ• Î± â†’ ğ• Î²) := by
  intros _ _ h
  -- âŠ¢ aâ‚âœ = aâ‚‚âœ
  ext p
  -- âŠ¢ coeff aâ‚âœ p = coeff aâ‚‚âœ p
  exact hf (congr_arg (fun x => coeff x p) h : _)
  -- ğŸ‰ no goals
#align witt_vector.map_fun.injective WittVector.mapFun.injective

theorem surjective (f : Î± â†’ Î²) (hf : Surjective f) : Surjective (mapFun f : ğ• Î± â†’ ğ• Î²) := fun x =>
  âŸ¨mk _ fun n => Classical.choose <| hf <| x.coeff n,
    by ext n; simp only [mapFun, coeff_mk, comp_apply, Classical.choose_spec (hf (x.coeff n))]âŸ©
       -- âŠ¢ coeff (mapFun f (mk p fun n => Classical.choose (_ : âˆƒ a, f a = coeff x n))) â€¦
              -- ğŸ‰ no goals
#align witt_vector.map_fun.surjective WittVector.mapFun.surjective

-- porting note: using `(x y : ğ• R)` instead of `(x y : WittVector p R)` produced sorries.
variable (f : R â†’+* S) (x y : WittVector p R)

/-- Auxiliary tactic for showing that `mapFun` respects the ring operations. -/
--  porting note: a very crude port.
macro "map_fun_tac" : tactic => `(tactic| (
  ext n
  simp only [mapFun, mk, comp_apply, zero_coeff, map_zero,
    -- porting note: the lemmas on the next line do not have the `simp` tag in mathlib4
    add_coeff, sub_coeff, mul_coeff, neg_coeff, nsmul_coeff, zsmul_coeff, pow_coeff,
    peval, map_aeval, algebraMap_int_eq, coe_evalâ‚‚Hom] <;>
  try { cases n <;> simp <;> done } <;>  -- porting note: this line solves `one`
  apply evalâ‚‚Hom_congr (RingHom.ext_int _ _) _ rfl <;>
  ext âŸ¨i, kâŸ© <;>
    fin_cases i <;> rfl ))

--  and until `pow`.
-- We do not tag these lemmas as `@[simp]` because they will be bundled in `map` later on.
theorem zero : mapFun f (0 : ğ• R) = 0 := by map_fun_tac
                                            -- ğŸ‰ no goals
#align witt_vector.map_fun.zero WittVector.mapFun.zero

theorem one : mapFun f (1 : ğ• R) = 1 := by map_fun_tac
                                           -- ğŸ‰ no goals
#align witt_vector.map_fun.one WittVector.mapFun.one

theorem add : mapFun f (x + y) = mapFun f x + mapFun f y := by map_fun_tac
                                                               -- ğŸ‰ no goals
#align witt_vector.map_fun.add WittVector.mapFun.add

theorem sub : mapFun f (x - y) = mapFun f x - mapFun f y := by map_fun_tac
                                                               -- ğŸ‰ no goals
#align witt_vector.map_fun.sub WittVector.mapFun.sub

theorem mul : mapFun f (x * y) = mapFun f x * mapFun f y := by map_fun_tac
                                                               -- ğŸ‰ no goals
#align witt_vector.map_fun.mul WittVector.mapFun.mul

theorem neg : mapFun f (-x) = -mapFun f x := by map_fun_tac
                                                -- ğŸ‰ no goals
#align witt_vector.map_fun.neg WittVector.mapFun.neg

theorem nsmul (n : â„•) : mapFun f (n â€¢ x) = n â€¢ mapFun f x := by map_fun_tac
                                                                -- ğŸ‰ no goals
#align witt_vector.map_fun.nsmul WittVector.mapFun.nsmul

theorem zsmul (z : â„¤) : mapFun f (z â€¢ x) = z â€¢ mapFun f x := by map_fun_tac
                                                                -- ğŸ‰ no goals
#align witt_vector.map_fun.zsmul WittVector.mapFun.zsmul

theorem pow (n : â„•) : mapFun f (x ^ n) = mapFun f x ^ n := by map_fun_tac
                                                              -- ğŸ‰ no goals
#align witt_vector.map_fun.pow WittVector.mapFun.pow

theorem nat_cast (n : â„•) : mapFun f (n : ğ• R) = n :=
  show mapFun f n.unaryCast = (n : WittVector p S) by
    induction n <;> simp [*, Nat.unaryCast, add, one, zero] <;> rfl
    -- âŠ¢ mapFun (â†‘f) (Nat.unaryCast Nat.zero) = â†‘Nat.zero
                    -- âŠ¢ 0 = â†‘0
                    -- âŠ¢ â†‘nâœ + 1 = â†‘(Nat.succ nâœ)
                                                                -- ğŸ‰ no goals
                                                                -- ğŸ‰ no goals
#align witt_vector.map_fun.nat_cast WittVector.mapFun.nat_cast

theorem int_cast (n : â„¤) : mapFun f (n : ğ• R) = n :=
  show mapFun f n.castDef = (n : WittVector p S) by
    cases n <;> simp [*, Int.castDef, add, one, neg, zero, nat_cast] <;> rfl
    -- âŠ¢ mapFun (â†‘f) (Int.castDef (Int.ofNat aâœ)) = â†‘(Int.ofNat aâœ)
                -- âŠ¢ â†‘aâœ = â†‘â†‘aâœ
                -- âŠ¢ -â†‘(aâœ + 1) = â†‘(Int.negSucc aâœ)
                                                                         -- ğŸ‰ no goals
                                                                         -- ğŸ‰ no goals
#align witt_vector.map_fun.int_cast WittVector.mapFun.int_cast

end mapFun

end WittVector

namespace WittVector

/-- Evaluates the `n`th Witt polynomial on the first `n` coefficients of `x`,
producing a value in `R`.
This function will be bundled as the ring homomorphism `WittVector.ghostMap`
once the ring structure is available,
but we rely on it to set up the ring structure in the first place. -/
private def ghostFun : ğ• R â†’ â„• â†’ R := fun x n => aeval x.coeff (W_ â„¤ n)

section Tactic
open Lean Elab Tactic

--  porting note: removed mathport output related to meta code.
--  I do not know what to do with `#align`
/-- An auxiliary tactic for proving that `ghostFun` respects the ring operations. -/
elab "ghost_fun_tac" Ï†:term "," fn:term : tactic => do
  evalTactic (â† `(tactic| (
  ext n
  have := congr_fun (congr_arg (@peval R _ _) (wittStructureInt_prop p $Ï† n)) $fn
  simp only [wittZero, OfNat.ofNat, Zero.zero, wittOne, One.one,
    HAdd.hAdd, Add.add, HSub.hSub, Sub.sub, Neg.neg, HMul.hMul, Mul.mul,HPow.hPow, Pow.pow,
    wittNSMul, wittZSMul, HSMul.hSMul, SMul.smul]
  simpa [WittVector.ghostFun, aeval_rename, aeval_bindâ‚, comp, uncurry, peval, eval] using this
  )))

end Tactic

section GhostFun

variable (x y : WittVector p R)

-- The following lemmas are not `@[simp]` because they will be bundled in `ghostMap` later on.

@[local simp]
theorem matrix_vecEmpty_coeff {R} (i j) :
    @coeff p R (Matrix.vecEmpty i) j = (Matrix.vecEmpty i : â„• â†’ R) j := by
  rcases i with âŸ¨_ | _ | _ | _ | i_val, âŸ¨âŸ©âŸ©
  -- ğŸ‰ no goals
#align witt_vector.matrix_vec_empty_coeff WittVector.matrix_vecEmpty_coeff

private theorem ghostFun_zero : ghostFun (0 : ğ• R) = 0 := by
  ghost_fun_tac 0, ![]
  -- ğŸ‰ no goals

private theorem ghostFun_one : ghostFun (1 : ğ• R) = 1 := by
  ghost_fun_tac 1, ![]
  -- ğŸ‰ no goals

private theorem ghostFun_add : ghostFun (x + y) = ghostFun x + ghostFun y := by
  ghost_fun_tac X 0 + X 1, ![x.coeff, y.coeff]
  -- ğŸ‰ no goals

private theorem ghostFun_nat_cast (i : â„•) : ghostFun (i : ğ• R) = i :=
  show ghostFun i.unaryCast = _ by
    induction i <;>
    -- âŠ¢ WittVector.ghostFun (Nat.unaryCast Nat.zero) = â†‘Nat.zero
      simp [*, Nat.unaryCast, ghostFun_zero, ghostFun_one, ghostFun_add, -Pi.coe_nat]
      -- ğŸ‰ no goals
      -- ğŸ‰ no goals

private theorem ghostFun_sub : ghostFun (x - y) = ghostFun x - ghostFun y := by
  ghost_fun_tac X 0 - X 1, ![x.coeff, y.coeff]
  -- ğŸ‰ no goals

private theorem ghostFun_mul : ghostFun (x * y) = ghostFun x * ghostFun y := by
  ghost_fun_tac X 0 * X 1, ![x.coeff, y.coeff]
  -- ğŸ‰ no goals

private theorem ghostFun_neg : ghostFun (-x) = -ghostFun x := by ghost_fun_tac -X 0, ![x.coeff]
                                                                 -- ğŸ‰ no goals

private theorem ghostFun_int_cast (i : â„¤) : ghostFun (i : ğ• R) = i :=
  show ghostFun i.castDef = _ by
    cases i <;> simp [*, Int.castDef, ghostFun_nat_cast, ghostFun_neg, -Pi.coe_nat, -Pi.coe_int]
    -- âŠ¢ WittVector.ghostFun (Int.castDef (Int.ofNat aâœ)) = â†‘(Int.ofNat aâœ)
                -- ğŸ‰ no goals
                -- ğŸ‰ no goals

private theorem ghostFun_nsmul (m : â„•) : ghostFun (m â€¢ x) = m â€¢ ghostFun x := by
  --  porting note: I had to add the explicit type ascription.
  --  This could very well be due to my poor tactic writing!
  ghost_fun_tac m â€¢ (X 0 : MvPolynomial _ â„¤), ![x.coeff]
  -- ğŸ‰ no goals

private theorem ghostFun_zsmul (m : â„¤) : ghostFun (m â€¢ x) = m â€¢ ghostFun x := by
  --  porting note: I had to add the explicit type ascription.
  --  This could very well be due to my poor tactic writing!
  ghost_fun_tac m â€¢ (X 0 : MvPolynomial _ â„¤), ![x.coeff]
  -- ğŸ‰ no goals

private theorem ghostFun_pow (m : â„•) : ghostFun (x ^ m) = ghostFun x ^ m := by
  ghost_fun_tac X 0 ^ m, ![x.coeff]
  -- ğŸ‰ no goals

end GhostFun

variable (p) (R)

/-- The bijection between `ğ• R` and `â„• â†’ R`, under the assumption that `p` is invertible in `R`.
In `WittVector.ghostEquiv` we upgrade this to an isomorphism of rings. -/
private def ghostEquiv' [Invertible (p : R)] : ğ• R â‰ƒ (â„• â†’ R) where
  toFun := ghostFun
  invFun x := mk p fun n => aeval x (xInTermsOfW p R n)
  left_inv := by
    intro x
    -- âŠ¢ (fun x => mk p fun n => â†‘(aeval x) (xInTermsOfW p R n)) (WittVector.ghostFun â€¦
    ext n
    -- âŠ¢ coeff ((fun x => mk p fun n => â†‘(aeval x) (xInTermsOfW p R n)) (WittVector.g â€¦
    have := bindâ‚_wittPolynomial_xInTermsOfW p R n
    -- âŠ¢ coeff ((fun x => mk p fun n => â†‘(aeval x) (xInTermsOfW p R n)) (WittVector.g â€¦
    apply_fun aeval x.coeff at this
    -- âŠ¢ coeff ((fun x => mk p fun n => â†‘(aeval x) (xInTermsOfW p R n)) (WittVector.g â€¦
    simpa only [aeval_bindâ‚, aeval_X, ghostFun, aeval_wittPolynomial]
    -- ğŸ‰ no goals
  right_inv := by
    intro x
    -- âŠ¢ WittVector.ghostFun ((fun x => mk p fun n => â†‘(aeval x) (xInTermsOfW p R n)) â€¦
    ext n
    -- âŠ¢ WittVector.ghostFun ((fun x => mk p fun n => â†‘(aeval x) (xInTermsOfW p R n)) â€¦
    have := bindâ‚_xInTermsOfW_wittPolynomial p R n
    -- âŠ¢ WittVector.ghostFun ((fun x => mk p fun n => â†‘(aeval x) (xInTermsOfW p R n)) â€¦
    apply_fun aeval x at this
    -- âŠ¢ WittVector.ghostFun ((fun x => mk p fun n => â†‘(aeval x) (xInTermsOfW p R n)) â€¦
    simpa only [aeval_bindâ‚, aeval_X, ghostFun, aeval_wittPolynomial]
    -- ğŸ‰ no goals

@[local instance]
private def comm_ring_auxâ‚ : CommRing (ğ• (MvPolynomial R â„š)) :=
  -- Porting note: no longer needed?
  -- letI : CommRing (MvPolynomial R â„š) := MvPolynomial.commRing
  (ghostEquiv' p (MvPolynomial R â„š)).injective.commRing ghostFun ghostFun_zero ghostFun_one
    ghostFun_add ghostFun_mul ghostFun_neg ghostFun_sub ghostFun_nsmul ghostFun_zsmul
    ghostFun_pow ghostFun_nat_cast ghostFun_int_cast

@[local instance]
private def comm_ring_auxâ‚‚ : CommRing (ğ• (MvPolynomial R â„¤)) :=
  (mapFun.injective _ <| map_injective (Int.castRingHom â„š) Int.cast_injective).commRing _
    (mapFun.zero _) (mapFun.one _) (mapFun.add _) (mapFun.mul _) (mapFun.neg _) (mapFun.sub _)
    (mapFun.nsmul _) (mapFun.zsmul _) (mapFun.pow _) (mapFun.nat_cast _) (mapFun.int_cast _)

attribute [reducible] comm_ring_auxâ‚‚

/-- The commutative ring structure on `ğ• R`. -/
instance : CommRing (ğ• R) :=
  (mapFun.surjective _ <| counit_surjective _).commRing (mapFun <| MvPolynomial.counit _)
    (mapFun.zero _) (mapFun.one _) (mapFun.add _) (mapFun.mul _) (mapFun.neg _) (mapFun.sub _)
    (mapFun.nsmul _) (mapFun.zsmul _) (mapFun.pow _) (mapFun.nat_cast _) (mapFun.int_cast _)

variable {p R}

/-- `WittVector.map f` is the ring homomorphism `ğ• R â†’+* ğ• S` naturally induced
by a ring homomorphism `f : R â†’+* S`. It acts coefficientwise. -/
noncomputable def map (f : R â†’+* S) : ğ• R â†’+* ğ• S where
  toFun := mapFun f
  map_zero' := mapFun.zero f
  map_one' := mapFun.one f
  map_add' := mapFun.add f
  map_mul' := mapFun.mul f
#align witt_vector.map WittVector.map

theorem map_injective (f : R â†’+* S) (hf : Injective f) : Injective (map f : ğ• R â†’ ğ• S) :=
  mapFun.injective f hf
#align witt_vector.map_injective WittVector.map_injective

theorem map_surjective (f : R â†’+* S) (hf : Surjective f) : Surjective (map f : ğ• R â†’ ğ• S) :=
  mapFun.surjective f hf
#align witt_vector.map_surjective WittVector.map_surjective

@[simp]
theorem map_coeff (f : R â†’+* S) (x : ğ• R) (n : â„•) : (map f x).coeff n = f (x.coeff n) :=
  rfl
#align witt_vector.map_coeff WittVector.map_coeff

/-- `WittVector.ghostMap` is a ring homomorphism that maps each Witt vector
to the sequence of its ghost components. -/
def ghostMap : ğ• R â†’+* â„• â†’ R where
  toFun := ghostFun
  map_zero' := ghostFun_zero
  map_one' := ghostFun_one
  map_add' := ghostFun_add
  map_mul' := ghostFun_mul
#align witt_vector.ghost_map WittVector.ghostMap

/-- Evaluates the `n`th Witt polynomial on the first `n` coefficients of `x`,
producing a value in `R`. -/
def ghostComponent (n : â„•) : ğ• R â†’+* R :=
  (Pi.evalRingHom _ n).comp ghostMap
#align witt_vector.ghost_component WittVector.ghostComponent

theorem ghostComponent_apply (n : â„•) (x : ğ• R) : ghostComponent n x = aeval x.coeff (W_ â„¤ n) :=
  rfl
#align witt_vector.ghost_component_apply WittVector.ghostComponent_apply

@[simp]
theorem ghostMap_apply (x : ğ• R) (n : â„•) : ghostMap x n = ghostComponent n x :=
  rfl
#align witt_vector.ghost_map_apply WittVector.ghostMap_apply

section Invertible

variable (p R)
variable [Invertible (p : R)]

/-- `WittVector.ghostMap` is a ring isomorphism when `p` is invertible in `R`. -/
def ghostEquiv : ğ• R â‰ƒ+* (â„• â†’ R) :=
  { (ghostMap : ğ• R â†’+* â„• â†’ R), ghostEquiv' p R with }
#align witt_vector.ghost_equiv WittVector.ghostEquiv

@[simp]
theorem ghostEquiv_coe : (ghostEquiv p R : ğ• R â†’+* â„• â†’ R) = ghostMap :=
  rfl
#align witt_vector.ghost_equiv_coe WittVector.ghostEquiv_coe

theorem ghostMap.bijective_of_invertible : Function.Bijective (ghostMap : ğ• R â†’ â„• â†’ R) :=
  (ghostEquiv p R).bijective
#align witt_vector.ghost_map.bijective_of_invertible WittVector.ghostMap.bijective_of_invertible

end Invertible

/-- `WittVector.coeff x 0` as a `RingHom` -/
@[simps]
noncomputable def constantCoeff : ğ• R â†’+* R where
  toFun x := x.coeff 0
  map_zero' := by simp
                  -- ğŸ‰ no goals
                 -- ğŸ‰ no goals
  map_one' := by simp
  map_add' := add_coeff_zero
  map_mul' := mul_coeff_zero
#align witt_vector.constant_coeff WittVector.constantCoeff

instance [Nontrivial R] : Nontrivial (ğ• R) :=
  constantCoeff.domain_nontrivial

end WittVector
