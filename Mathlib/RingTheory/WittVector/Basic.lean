/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Robert Y. Lewis
-/
import Mathlib.Algebra.MvPolynomial.Counit
import Mathlib.Algebra.MvPolynomial.Invertible
import Mathlib.RingTheory.WittVector.Defs

#align_import ring_theory.witt_vector.basic from "leanprover-community/mathlib"@"9556784a5b84697562e9c6acb40500d4a82e675a"

/-!
# Witt vectors

This file verifies that the ring operations on `WittVector p R`
satisfy the axioms of a commutative ring.

## Main definitions

* `WittVector.map`: lifts a ring homomorphism `R ‚Üí+* S` to a ring homomorphism `ùïé R ‚Üí+* ùïé S`.
* `WittVector.ghostComponent n x`: evaluates the `n`th Witt polynomial
  on the first `n` coefficients of `x`, producing a value in `R`.
  This is a ring homomorphism.
* `WittVector.ghostMap`: a ring homomorphism `ùïé R ‚Üí+* (‚Ñï ‚Üí R)`, obtained by packaging
  all the ghost components together.
  If `p` is invertible in `R`, then the ghost map is an equivalence,
  which we use to define the ring operations on `ùïé R`.
* `WittVector.CommRing`: the ring structure induced by the ghost components.

## Notation

We use notation `ùïé R`, entered `\bbW`, for the Witt vectors over `R`.

## Implementation details

As we prove that the ghost components respect the ring operations, we face a number of repetitive
proofs. To avoid duplicating code we factor these proofs into a custom tactic, only slightly more
powerful than a tactic macro. This tactic is not particularly useful outside of its applications
in this file.

## References

* [Hazewinkel, *Witt Vectors*][Haze09]

* [Commelin and Lewis, *Formalizing the Ring of Witt Vectors*][CL21]

-/


noncomputable section

open MvPolynomial Function

variable {p : ‚Ñï} {R S T : Type*} [hp : Fact p.Prime] [CommRing R] [CommRing S] [CommRing T]
variable {Œ± : Type*} {Œ≤ : Type*}

local notation "ùïé" => WittVector p
local notation "W_" => wittPolynomial p

-- type as `\bbW`
open scoped Witt

namespace WittVector

/-- `f : Œ± ‚Üí Œ≤` induces a map from `ùïé Œ±` to `ùïé Œ≤` by applying `f` componentwise.
If `f` is a ring homomorphism, then so is `f`, see `WittVector.map f`. -/
def mapFun (f : Œ± ‚Üí Œ≤) : ùïé Œ± ‚Üí ùïé Œ≤ := fun x => mk _ (f ‚àò x.coeff)
#align witt_vector.map_fun WittVector.mapFun

namespace mapFun

-- Porting note: switched the proof to tactic mode. I think that `ext` was the issue.
theorem injective (f : Œ± ‚Üí Œ≤) (hf : Injective f) : Injective (mapFun f : ùïé Œ± ‚Üí ùïé Œ≤) := by
  intros _ _ h
  ext p
  exact hf (congr_arg (fun x => coeff x p) h : _)
#align witt_vector.map_fun.injective WittVector.mapFun.injective

theorem surjective (f : Œ± ‚Üí Œ≤) (hf : Surjective f) : Surjective (mapFun f : ùïé Œ± ‚Üí ùïé Œ≤) := fun x =>
  ‚ü®mk _ fun n => Classical.choose <| hf <| x.coeff n,
    by ext n; simp only [mapFun, coeff_mk, comp_apply, Classical.choose_spec (hf (x.coeff n))]‚ü©
#align witt_vector.map_fun.surjective WittVector.mapFun.surjective

-- Porting note: using `(x y : ùïé R)` instead of `(x y : WittVector p R)` produced sorries.
variable (f : R ‚Üí+* S) (x y : WittVector p R)

/-- Auxiliary tactic for showing that `mapFun` respects the ring operations. -/
-- porting note: a very crude port.
macro "map_fun_tac" : tactic => `(tactic| (
  ext n
  simp only [mapFun, mk, comp_apply, zero_coeff, map_zero,
    -- Porting note: the lemmas on the next line do not have the `simp` tag in mathlib4
    add_coeff, sub_coeff, mul_coeff, neg_coeff, nsmul_coeff, zsmul_coeff, pow_coeff,
    peval, map_aeval, algebraMap_int_eq, coe_eval‚ÇÇHom] <;>
  try { cases n <;> simp <;> done } <;>  -- Porting note: this line solves `one`
  apply eval‚ÇÇHom_congr (RingHom.ext_int _ _) _ rfl <;>
  ext ‚ü®i, k‚ü© <;>
    fin_cases i <;> rfl))

--  and until `pow`.
-- We do not tag these lemmas as `@[simp]` because they will be bundled in `map` later on.
theorem zero : mapFun f (0 : ùïé R) = 0 := by map_fun_tac
#align witt_vector.map_fun.zero WittVector.mapFun.zero

theorem one : mapFun f (1 : ùïé R) = 1 := by map_fun_tac
#align witt_vector.map_fun.one WittVector.mapFun.one

theorem add : mapFun f (x + y) = mapFun f x + mapFun f y := by map_fun_tac
#align witt_vector.map_fun.add WittVector.mapFun.add

theorem sub : mapFun f (x - y) = mapFun f x - mapFun f y := by map_fun_tac
#align witt_vector.map_fun.sub WittVector.mapFun.sub

theorem mul : mapFun f (x * y) = mapFun f x * mapFun f y := by map_fun_tac
#align witt_vector.map_fun.mul WittVector.mapFun.mul

theorem neg : mapFun f (-x) = -mapFun f x := by map_fun_tac
#align witt_vector.map_fun.neg WittVector.mapFun.neg

theorem nsmul (n : ‚Ñï) (x : WittVector p R) : mapFun f (n ‚Ä¢ x) = n ‚Ä¢ mapFun f x := by map_fun_tac
#align witt_vector.map_fun.nsmul WittVector.mapFun.nsmul

theorem zsmul (z : ‚Ñ§) (x : WittVector p R) : mapFun f (z ‚Ä¢ x) = z ‚Ä¢ mapFun f x := by map_fun_tac
#align witt_vector.map_fun.zsmul WittVector.mapFun.zsmul

theorem pow (n : ‚Ñï) : mapFun f (x ^ n) = mapFun f x ^ n := by map_fun_tac
#align witt_vector.map_fun.pow WittVector.mapFun.pow

theorem natCast (n : ‚Ñï) : mapFun f (n : ùïé R) = n :=
  show mapFun f n.unaryCast = (n : WittVector p S) by
    induction n <;> simp [*, Nat.unaryCast, add, one, zero] <;> rfl
#align witt_vector.map_fun.nat_cast WittVector.mapFun.natCast

@[deprecated (since := "2024-04-17")]
alias nat_cast := natCast

theorem intCast (n : ‚Ñ§) : mapFun f (n : ùïé R) = n :=
  show mapFun f n.castDef = (n : WittVector p S) by
    cases n <;> simp [*, Int.castDef, add, one, neg, zero, natCast] <;> rfl
#align witt_vector.map_fun.int_cast WittVector.mapFun.intCast

@[deprecated (since := "2024-04-17")]
alias int_cast := intCast

end mapFun

end WittVector

namespace WittVector

/-- Evaluates the `n`th Witt polynomial on the first `n` coefficients of `x`,
producing a value in `R`.
This function will be bundled as the ring homomorphism `WittVector.ghostMap`
once the ring structure is available,
but we rely on it to set up the ring structure in the first place. -/
private def ghostFun : ùïé R ‚Üí ‚Ñï ‚Üí R := fun x n => aeval x.coeff (W_ ‚Ñ§ n)

section Tactic
open Lean Elab Tactic

-- porting note: removed mathport output related to meta code.
/-- An auxiliary tactic for proving that `ghostFun` respects the ring operations. -/
elab "ghost_fun_tac" œÜ:term "," fn:term : tactic => do
  evalTactic (‚Üê `(tactic| (
  ext n
  have := congr_fun (congr_arg (@peval R _ _) (wittStructureInt_prop p $œÜ n)) $fn
  simp only [wittZero, OfNat.ofNat, Zero.zero, wittOne, One.one,
    HAdd.hAdd, Add.add, HSub.hSub, Sub.sub, Neg.neg, HMul.hMul, Mul.mul,HPow.hPow, Pow.pow,
    wittNSMul, wittZSMul, HSMul.hSMul, SMul.smul]
  simpa (config := { unfoldPartialApp := true }) [WittVector.ghostFun, aeval_rename, aeval_bind‚ÇÅ,
    comp, uncurry, peval, eval] using this
  )))

end Tactic

section GhostFun

variable (x y : WittVector p R)

-- The following lemmas are not `@[simp]` because they will be bundled in `ghostMap` later on.

@[local simp]
theorem matrix_vecEmpty_coeff {R} (i j) :
    @coeff p R (Matrix.vecEmpty i) j = (Matrix.vecEmpty i : ‚Ñï ‚Üí R) j := by
  rcases i with ‚ü®_ | _ | _ | _ | i_val, ‚ü®‚ü©‚ü©
#align witt_vector.matrix_vec_empty_coeff WittVector.matrix_vecEmpty_coeff

private theorem ghostFun_zero : ghostFun (0 : ùïé R) = 0 := by
  ghost_fun_tac 0, ![]

private theorem ghostFun_one : ghostFun (1 : ùïé R) = 1 := by
  ghost_fun_tac 1, ![]

private theorem ghostFun_add : ghostFun (x + y) = ghostFun x + ghostFun y := by
  ghost_fun_tac X 0 + X 1, ![x.coeff, y.coeff]

private theorem ghostFun_natCast (i : ‚Ñï) : ghostFun (i : ùïé R) = i :=
  show ghostFun i.unaryCast = _ by
    induction i <;>
      simp [*, Nat.unaryCast, ghostFun_zero, ghostFun_one, ghostFun_add, -Pi.natCast_def]

@[deprecated (since := "2024-04-17")]
alias ghostFun_nat_cast := ghostFun_natCast

private theorem ghostFun_sub : ghostFun (x - y) = ghostFun x - ghostFun y := by
  ghost_fun_tac X 0 - X 1, ![x.coeff, y.coeff]

private theorem ghostFun_mul : ghostFun (x * y) = ghostFun x * ghostFun y := by
  ghost_fun_tac X 0 * X 1, ![x.coeff, y.coeff]

private theorem ghostFun_neg : ghostFun (-x) = -ghostFun x := by ghost_fun_tac -X 0, ![x.coeff]

private theorem ghostFun_intCast (i : ‚Ñ§) : ghostFun (i : ùïé R) = i :=
  show ghostFun i.castDef = _ by
    cases i <;> simp [*, Int.castDef, ghostFun_natCast, ghostFun_neg, -Pi.natCast_def,
      -Pi.intCast_def]

@[deprecated (since := "2024-04-17")]
alias ghostFun_int_cast := ghostFun_intCast

private lemma ghostFun_nsmul (m : ‚Ñï) (x : WittVector p R) : ghostFun (m ‚Ä¢ x) = m ‚Ä¢ ghostFun x := by
  -- porting note: I had to add the explicit type ascription.
  -- This could very well be due to my poor tactic writing!
  ghost_fun_tac m ‚Ä¢ (X 0 : MvPolynomial _ ‚Ñ§), ![x.coeff]

private lemma ghostFun_zsmul (m : ‚Ñ§) (x : WittVector p R) : ghostFun (m ‚Ä¢ x) = m ‚Ä¢ ghostFun x := by
  -- porting note: I had to add the explicit type ascription.
  -- This could very well be due to my poor tactic writing!
  ghost_fun_tac m ‚Ä¢ (X 0 : MvPolynomial _ ‚Ñ§), ![x.coeff]

private theorem ghostFun_pow (m : ‚Ñï) : ghostFun (x ^ m) = ghostFun x ^ m := by
  ghost_fun_tac X 0 ^ m, ![x.coeff]

end GhostFun

variable (p) (R)

/-- The bijection between `ùïé R` and `‚Ñï ‚Üí R`, under the assumption that `p` is invertible in `R`.
In `WittVector.ghostEquiv` we upgrade this to an isomorphism of rings. -/
private def ghostEquiv' [Invertible (p : R)] : ùïé R ‚âÉ (‚Ñï ‚Üí R) where
  toFun := ghostFun
  invFun x := mk p fun n => aeval x (xInTermsOfW p R n)
  left_inv := by
    intro x
    ext n
    have := bind‚ÇÅ_wittPolynomial_xInTermsOfW p R n
    apply_fun aeval x.coeff at this
    simpa (config := { unfoldPartialApp := true }) only [aeval_bind‚ÇÅ, aeval_X, ghostFun,
      aeval_wittPolynomial]
  right_inv := by
    intro x
    ext n
    have := bind‚ÇÅ_xInTermsOfW_wittPolynomial p R n
    apply_fun aeval x at this
    simpa only [aeval_bind‚ÇÅ, aeval_X, ghostFun, aeval_wittPolynomial]

@[local instance]
private def comm_ring_aux‚ÇÅ : CommRing (ùïé (MvPolynomial R ‚Ñö)) :=
  -- Porting note: no longer needed?
  -- letI : CommRing (MvPolynomial R ‚Ñö) := MvPolynomial.commRing
  (ghostEquiv' p (MvPolynomial R ‚Ñö)).injective.commRing ghostFun ghostFun_zero ghostFun_one
    ghostFun_add ghostFun_mul ghostFun_neg ghostFun_sub ghostFun_nsmul ghostFun_zsmul
    ghostFun_pow ghostFun_natCast ghostFun_intCast

@[local instance]
private abbrev comm_ring_aux‚ÇÇ : CommRing (ùïé (MvPolynomial R ‚Ñ§)) :=
  (mapFun.injective _ <| map_injective (Int.castRingHom ‚Ñö) Int.cast_injective).commRing _
    (mapFun.zero _) (mapFun.one _) (mapFun.add _) (mapFun.mul _) (mapFun.neg _) (mapFun.sub _)
    (mapFun.nsmul _) (mapFun.zsmul _) (mapFun.pow _) (mapFun.natCast _) (mapFun.intCast _)

/-- The commutative ring structure on `ùïé R`. -/
instance : CommRing (ùïé R) :=
  (mapFun.surjective _ <| counit_surjective _).commRing (mapFun <| MvPolynomial.counit _)
    (mapFun.zero _) (mapFun.one _) (mapFun.add _) (mapFun.mul _) (mapFun.neg _) (mapFun.sub _)
    (mapFun.nsmul _) (mapFun.zsmul _) (mapFun.pow _) (mapFun.natCast _) (mapFun.intCast _)

variable {p R}

/-- `WittVector.map f` is the ring homomorphism `ùïé R ‚Üí+* ùïé S` naturally induced
by a ring homomorphism `f : R ‚Üí+* S`. It acts coefficientwise. -/
noncomputable def map (f : R ‚Üí+* S) : ùïé R ‚Üí+* ùïé S where
  toFun := mapFun f
  map_zero' := mapFun.zero f
  map_one' := mapFun.one f
  map_add' := mapFun.add f
  map_mul' := mapFun.mul f
#align witt_vector.map WittVector.map

theorem map_injective (f : R ‚Üí+* S) (hf : Injective f) : Injective (map f : ùïé R ‚Üí ùïé S) :=
  mapFun.injective f hf
#align witt_vector.map_injective WittVector.map_injective

theorem map_surjective (f : R ‚Üí+* S) (hf : Surjective f) : Surjective (map f : ùïé R ‚Üí ùïé S) :=
  mapFun.surjective f hf
#align witt_vector.map_surjective WittVector.map_surjective

@[simp]
theorem map_coeff (f : R ‚Üí+* S) (x : ùïé R) (n : ‚Ñï) : (map f x).coeff n = f (x.coeff n) :=
  rfl
#align witt_vector.map_coeff WittVector.map_coeff

/-- `WittVector.ghostMap` is a ring homomorphism that maps each Witt vector
to the sequence of its ghost components. -/
def ghostMap : ùïé R ‚Üí+* ‚Ñï ‚Üí R where
  toFun := ghostFun
  map_zero' := ghostFun_zero
  map_one' := ghostFun_one
  map_add' := ghostFun_add
  map_mul' := ghostFun_mul
#align witt_vector.ghost_map WittVector.ghostMap

/-- Evaluates the `n`th Witt polynomial on the first `n` coefficients of `x`,
producing a value in `R`. -/
def ghostComponent (n : ‚Ñï) : ùïé R ‚Üí+* R :=
  (Pi.evalRingHom _ n).comp ghostMap
#align witt_vector.ghost_component WittVector.ghostComponent

theorem ghostComponent_apply (n : ‚Ñï) (x : ùïé R) : ghostComponent n x = aeval x.coeff (W_ ‚Ñ§ n) :=
  rfl
#align witt_vector.ghost_component_apply WittVector.ghostComponent_apply

@[simp]
theorem ghostMap_apply (x : ùïé R) (n : ‚Ñï) : ghostMap x n = ghostComponent n x :=
  rfl
#align witt_vector.ghost_map_apply WittVector.ghostMap_apply

section Invertible

variable (p R)
variable [Invertible (p : R)]

/-- `WittVector.ghostMap` is a ring isomorphism when `p` is invertible in `R`. -/
def ghostEquiv : ùïé R ‚âÉ+* (‚Ñï ‚Üí R) :=
  { (ghostMap : ùïé R ‚Üí+* ‚Ñï ‚Üí R), ghostEquiv' p R with }
#align witt_vector.ghost_equiv WittVector.ghostEquiv

@[simp]
theorem ghostEquiv_coe : (ghostEquiv p R : ùïé R ‚Üí+* ‚Ñï ‚Üí R) = ghostMap :=
  rfl
#align witt_vector.ghost_equiv_coe WittVector.ghostEquiv_coe

theorem ghostMap.bijective_of_invertible : Function.Bijective (ghostMap : ùïé R ‚Üí ‚Ñï ‚Üí R) :=
  (ghostEquiv p R).bijective
#align witt_vector.ghost_map.bijective_of_invertible WittVector.ghostMap.bijective_of_invertible

end Invertible

/-- `WittVector.coeff x 0` as a `RingHom` -/
@[simps]
noncomputable def constantCoeff : ùïé R ‚Üí+* R where
  toFun x := x.coeff 0
  map_zero' := by simp
  map_one' := by simp
  map_add' := add_coeff_zero
  map_mul' := mul_coeff_zero
#align witt_vector.constant_coeff WittVector.constantCoeff

instance [Nontrivial R] : Nontrivial (ùïé R) :=
  constantCoeff.domain_nontrivial

end WittVector
