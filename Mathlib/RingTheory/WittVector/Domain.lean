/-
Copyright (c) 2022 Robert Y. Lewis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robert Y. Lewis
-/
import Mathlib.RingTheory.WittVector.Identities

/-!

# Witt vectors over a domain

This file builds to the proof `WittVector.instIsDomain`,
an instance that says if `R` is an integral domain, then so is `ğ• R`.
It depends on the API around iterated applications
of `WittVector.verschiebung` and `WittVector.frobenius`
found in `Identities.lean`.

The [proof sketch](https://math.stackexchange.com/questions/4117247/ring-of-witt-vectors-over-an-integral-domain/4118723#4118723)
goes as follows:
any nonzero $x$ is an iterated application of $V$
to some vector $w_x$ whose 0th component is nonzero (`WittVector.verschiebung_nonzero`).
Known identities (`WittVector.iterate_verschiebung_mul`) allow us to transform
the product of two such $x$ and $y$
to the form $V^{m+n}\left(F^n(w_x) \cdot F^m(w_y)\right)$,
the 0th component of which must be nonzero.

## Main declarations

* `WittVector.iterate_verschiebung_mul_coeff` : an identity from [Haze09]
* `WittVector.instIsDomain`

-/


noncomputable section

namespace WittVector

open Function

variable {p : â„•} {R : Type*}

local notation "ğ•" => WittVector p -- type as `\bbW`

/-!
## The `shift` operator
-/


/--
`WittVector.verschiebung` translates the entries of a Witt vector upward, inserting 0s in the gaps.
`WittVector.shift` does the opposite, removing the first entries.
This is mainly useful as an auxiliary construction for `WittVector.verschiebung_nonzero`.
-/
def shift (x : ğ• R) (n : â„•) : ğ• R :=
  @mk' p R fun i => x.coeff (n + i)

theorem shift_coeff (x : ğ• R) (n k : â„•) : (x.shift n).coeff k = x.coeff (n + k) :=
  rfl

variable [hp : Fact p.Prime] [CommRing R]

theorem verschiebung_shift (x : ğ• R) (k : â„•) (h : âˆ€ i < k + 1, x.coeff i = 0) :
    verschiebung (x.shift k.succ) = x.shift k := by
  ext âŸ¨jâŸ©
  Â· rw [verschiebung_coeff_zero, shift_coeff, h]
    apply Nat.lt_succ_self
  Â· simp only [verschiebung_coeff_succ, shift]
    congr 1
    rw [Nat.add_succ, add_comm, Nat.add_succ, add_comm]

theorem eq_iterate_verschiebung {x : ğ• R} {n : â„•} (h : âˆ€ i < n, x.coeff i = 0) :
    x = verschiebung^[n] (x.shift n) := by
  induction n with
  | zero => cases x; simp [shift]
  | succ k ih =>
    dsimp; rw [verschiebung_shift]
    Â· exact ih fun i hi => h _ (hi.trans (Nat.lt_succ_self _))
    Â· exact h

theorem verschiebung_nonzero {x : ğ• R} (hx : x â‰  0) :
    âˆƒ n : â„•, âˆƒ x' : ğ• R, x'.coeff 0 â‰  0 âˆ§ x = verschiebung^[n] x' := by
  classical
  have hex : âˆƒ k : â„•, x.coeff k â‰  0 := by
    by_contra! hall
    apply hx
    ext i
    simp only [hall, zero_coeff]
  let n := Nat.find hex
  use n, x.shift n
  refine âŸ¨Nat.find_spec hex, eq_iterate_verschiebung fun i hi => not_not.mp ?_âŸ©
  exact Nat.find_min hex hi

/-!
## Witt vectors over a domain

If `R` is an integral domain, then so is `ğ• R`.
This argument is adapted from
<https://math.stackexchange.com/questions/4117247/ring-of-witt-vectors-over-an-integral-domain/4118723#4118723>.
-/


instance [CharP R p] [NoZeroDivisors R] : NoZeroDivisors (ğ• R) :=
  âŸ¨fun {x y} => by
    contrapose!
    rintro âŸ¨ha, hbâŸ©
    rcases verschiebung_nonzero ha with âŸ¨na, wa, hwa0, rflâŸ©
    rcases verschiebung_nonzero hb with âŸ¨nb, wb, hwb0, rflâŸ©
    refine ne_of_apply_ne (fun x => x.coeff (na + nb)) ?_
    dsimp only
    rw [iterate_verschiebung_mul_coeff, zero_coeff]
    exact mul_ne_zero (pow_ne_zero _ hwa0) (pow_ne_zero _ hwb0)âŸ©

instance instIsDomain [CharP R p] [IsDomain R] : IsDomain (ğ• R) :=
  NoZeroDivisors.to_isDomain _

end WittVector
