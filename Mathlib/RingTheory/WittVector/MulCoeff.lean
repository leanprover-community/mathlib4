/-
Copyright (c) 2022 Robert Y. Lewis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robert Y. Lewis, Heather Macbeth
-/
import Mathlib.Algebra.MvPolynomial.Supported
import Mathlib.RingTheory.WittVector.Truncated

/-!
# Leading terms of Witt vector multiplication

The goal of this file is to study the leading terms of the formula for the `n+1`st coefficient
of a product of Witt vectors `x` and `y` over a ring of characteristic `p`.
We aim to isolate the `n+1`st coefficients of `x` and `y`, and express the rest of the product
in terms of a function of the lower coefficients.

For most of this file we work with terms of type `MvPolynomial (Fin 2 Ã— â„•) â„¤`.
We will eventually evaluate them in `k`, but first we must take care of a calculation
that needs to happen in characteristic 0.

## Main declarations

* `WittVector.nth_mul_coeff`: expresses the coefficient of a product of Witt vectors
  in terms of the previous coefficients of the multiplicands.

-/


noncomputable section

namespace WittVector

variable (p : â„•) [hp : Fact p.Prime]
variable {k : Type*} [CommRing k]

local notation "ğ•" => WittVector p

local notation "ğ•„" => MvPolynomial (Fin 2 Ã— â„•) â„¤

open Finset MvPolynomial

/--
```
(âˆ‘ i âˆˆ range n, (y.coeff i)^(p^(n-i)) * p^i.val) *
(âˆ‘ i âˆˆ range n, (y.coeff i)^(p^(n-i)) * p^i.val)
```
-/
def wittPolyProd (n : â„•) : ğ•„ :=
  rename (Prod.mk (0 : Fin 2)) (wittPolynomial p â„¤ n) *
    rename (Prod.mk (1 : Fin 2)) (wittPolynomial p â„¤ n)

theorem wittPolyProd_vars (n : â„•) : (wittPolyProd p n).vars âŠ† univ Ã—Ë¢ range (n + 1) := by
  rw [wittPolyProd]
  apply Subset.trans (vars_mul _ _)
  refine union_subset ?_ ?_ <;>
  Â· refine Subset.trans (vars_rename _ _) ?_
    simp [wittPolynomial_vars, image_subset_iff]

/-- The "remainder term" of `WittVector.wittPolyProd`. See `mul_polyOfInterest_aux2`. -/
def wittPolyProdRemainder (n : â„•) : ğ•„ :=
  âˆ‘ i âˆˆ range n, (p : ğ•„) ^ i * wittMul p i ^ p ^ (n - i)

theorem wittPolyProdRemainder_vars (n : â„•) :
    (wittPolyProdRemainder p n).vars âŠ† univ Ã—Ë¢ range n := by
  rw [wittPolyProdRemainder]
  refine Subset.trans (vars_sum_subset _ _) ?_
  rw [biUnion_subset]
  intro x hx
  apply Subset.trans (vars_mul _ _)
  refine union_subset ?_ ?_
  Â· apply Subset.trans (vars_pow _ _)
    have : (p : ğ•„) = C (p : â„¤) := by simp only [Int.cast_natCast, eq_intCast]
    rw [this, vars_C]
    apply empty_subset
  Â· apply Subset.trans (vars_pow _ _)
    apply Subset.trans (wittMul_vars _ _)
    apply product_subset_product (Subset.refl _)
    simpa using hx

/-- `remainder p n` represents the remainder term from `mul_polyOfInterest_aux3`.
`wittPolyProd p (n+1)` will have variables up to `n+1`,
but `remainder` will only have variables up to `n`.
-/
def remainder (n : â„•) : ğ•„ :=
  (âˆ‘ x âˆˆ range (n + 1),
    (rename (Prod.mk 0)) ((monomial (Finsupp.single x (p ^ (n + 1 - x)))) ((p : â„¤) ^ x))) *
   âˆ‘ x âˆˆ range (n + 1),
    (rename (Prod.mk 1)) ((monomial (Finsupp.single x (p ^ (n + 1 - x)))) ((p : â„¤) ^ x))

theorem remainder_vars (n : â„•) : (remainder p n).vars âŠ† univ Ã—Ë¢ range (n + 1) := by
  rw [remainder]
  apply Subset.trans (vars_mul _ _)
  refine union_subset ?_ ?_ <;>
  Â· refine Subset.trans (vars_sum_subset _ _) ?_
    rw [biUnion_subset]
    intro x hx
    rw [rename_monomial, vars_monomial, Finsupp.mapDomain_single]
    Â· apply Subset.trans Finsupp.support_single_subset
      simpa using mem_range.mp hx
    Â· apply pow_ne_zero
      exact mod_cast hp.out.ne_zero

/-- This is the polynomial whose degree we want to get a handle on. -/
def polyOfInterest (n : â„•) : ğ•„ :=
  wittMul p (n + 1) + (p : ğ•„) ^ (n + 1) * X (0, n + 1) * X (1, n + 1) -
    X (0, n + 1) * rename (Prod.mk (1 : Fin 2)) (wittPolynomial p â„¤ (n + 1)) -
    X (1, n + 1) * rename (Prod.mk (0 : Fin 2)) (wittPolynomial p â„¤ (n + 1))

theorem mul_polyOfInterest_aux1 (n : â„•) :
    âˆ‘ i âˆˆ range (n + 1), (p : ğ•„) ^ i * wittMul p i ^ p ^ (n - i) = wittPolyProd p n := by
  simp only [wittPolyProd]
  convert wittStructureInt_prop p (X (0 : Fin 2) * X 1) n using 1
  Â· simp only [wittPolynomial, wittMul]
    rw [map_sum]
    congr 1 with i
    congr 1
    have hsupp : (Finsupp.single i (p ^ (n - i))).support = {i} := by
      rw [Finsupp.support_eq_singleton]
      simp only [and_true, Finsupp.single_eq_same, Ne]
      exact pow_ne_zero _ hp.out.ne_zero
    simp only [bindâ‚_monomial, hsupp, Int.cast_natCast, prod_singleton, eq_intCast,
      Finsupp.single_eq_same, Int.cast_pow]
  Â· simp only [map_mul, bindâ‚_X_right]

theorem mul_polyOfInterest_aux2 (n : â„•) :
    (p : ğ•„) ^ n * wittMul p n + wittPolyProdRemainder p n = wittPolyProd p n := by
  convert mul_polyOfInterest_aux1 p n
  rw [sum_range_succ, add_comm, Nat.sub_self, pow_zero, pow_one]
  rfl

-- We redeclare `p` here to locally discard the unneeded `p.Prime` hypothesis.
theorem mul_polyOfInterest_aux3 (p n : â„•) : wittPolyProd p (n + 1) =
    -((p : ğ•„) ^ (n + 1) * X (0, n + 1)) * ((p : ğ•„) ^ (n + 1) * X (1, n + 1)) +
    (p : ğ•„) ^ (n + 1) * X (0, n + 1) * rename (Prod.mk (1 : Fin 2)) (wittPolynomial p â„¤ (n + 1)) +
    (p : ğ•„) ^ (n + 1) * X (1, n + 1) * rename (Prod.mk (0 : Fin 2)) (wittPolynomial p â„¤ (n + 1)) +
    remainder p n := by
  -- a useful auxiliary fact
  have mvpz : (p : ğ•„) ^ (n + 1) = MvPolynomial.C ((p : â„¤) ^ (n + 1)) := by norm_cast
  rw [wittPolyProd, wittPolynomial, map_sum, map_sum]
  conv_lhs =>
    arg 1
    rw [sum_range_succ, â† C_mul_X_pow_eq_monomial, tsub_self, pow_zero, pow_one, map_mul,
      rename_C, rename_X, â† mvpz]
  conv_lhs =>
    arg 2
    rw [sum_range_succ, â† C_mul_X_pow_eq_monomial, tsub_self, pow_zero, pow_one, map_mul,
      rename_C, rename_X, â† mvpz]
  conv_rhs =>
    enter [1, 1, 2, 2]
    rw [sum_range_succ, â† C_mul_X_pow_eq_monomial, tsub_self, pow_zero, pow_one, map_mul,
      rename_C, rename_X, â† mvpz]
  conv_rhs =>
    enter [1, 2, 2]
    rw [sum_range_succ, â† C_mul_X_pow_eq_monomial, tsub_self, pow_zero, pow_one, map_mul,
      rename_C, rename_X, â† mvpz]
  simp only [add_mul, mul_add]
  rw [add_comm _ (remainder p n)]
  simp only [add_assoc]
  apply congrArg (Add.add _)
  ring

theorem mul_polyOfInterest_aux4 (n : â„•) :
    (p : ğ•„) ^ (n + 1) * wittMul p (n + 1) =
    -((p : ğ•„) ^ (n + 1) * X (0, n + 1)) * ((p : ğ•„) ^ (n + 1) * X (1, n + 1)) +
    (p : ğ•„) ^ (n + 1) * X (0, n + 1) * rename (Prod.mk (1 : Fin 2)) (wittPolynomial p â„¤ (n + 1)) +
    (p : ğ•„) ^ (n + 1) * X (1, n + 1) * rename (Prod.mk (0 : Fin 2)) (wittPolynomial p â„¤ (n + 1)) +
    (remainder p n - wittPolyProdRemainder p (n + 1)) := by
  rw [â† add_sub_assoc, eq_sub_iff_add_eq, mul_polyOfInterest_aux2]
  exact mul_polyOfInterest_aux3 _ _

theorem mul_polyOfInterest_aux5 (n : â„•) :
    (p : ğ•„) ^ (n + 1) * polyOfInterest p n = remainder p n - wittPolyProdRemainder p (n + 1) := by
  simp only [polyOfInterest, mul_sub, mul_add, sub_eq_iff_eq_add']
  rw [mul_polyOfInterest_aux4 p n]
  ring

theorem mul_polyOfInterest_vars (n : â„•) :
    ((p : ğ•„) ^ (n + 1) * polyOfInterest p n).vars âŠ† univ Ã—Ë¢ range (n + 1) := by
  rw [mul_polyOfInterest_aux5]
  apply Subset.trans (vars_sub_subset _)
  refine union_subset ?_ ?_
  Â· apply remainder_vars
  Â· apply wittPolyProdRemainder_vars

theorem polyOfInterest_vars_eq (n : â„•) : (polyOfInterest p n).vars =
    ((p : ğ•„) ^ (n + 1) * (wittMul p (n + 1) + (p : ğ•„) ^ (n + 1) * X (0, n + 1) * X (1, n + 1) -
      X (0, n + 1) * rename (Prod.mk (1 : Fin 2)) (wittPolynomial p â„¤ (n + 1)) -
      X (1, n + 1) * rename (Prod.mk (0 : Fin 2)) (wittPolynomial p â„¤ (n + 1)))).vars := by
  have : (p : ğ•„) ^ (n + 1) = C ((p : â„¤) ^ (n + 1)) := by norm_cast
  rw [polyOfInterest, this, vars_C_mul]
  apply pow_ne_zero
  exact mod_cast hp.out.ne_zero

theorem polyOfInterest_vars (n : â„•) : (polyOfInterest p n).vars âŠ† univ Ã—Ë¢ range (n + 1) := by
  rw [polyOfInterest_vars_eq]; apply mul_polyOfInterest_vars

theorem peval_polyOfInterest (n : â„•) (x y : ğ• k) :
    peval (polyOfInterest p n) ![fun i => x.coeff i, fun i => y.coeff i] =
    (x * y).coeff (n + 1) + p ^ (n + 1) * x.coeff (n + 1) * y.coeff (n + 1) -
      y.coeff (n + 1) * âˆ‘ i âˆˆ range (n + 1 + 1), p ^ i * x.coeff i ^ p ^ (n + 1 - i) -
      x.coeff (n + 1) * âˆ‘ i âˆˆ range (n + 1 + 1), p ^ i * y.coeff i ^ p ^ (n + 1 - i) := by
  simp only [polyOfInterest, peval,
    Function.uncurry_apply_pair, aeval_X, Matrix.cons_val_one, map_mul, Matrix.cons_val_zero,
    map_sub]
  rw [sub_sub, add_comm (_ * _), â† sub_sub]
  simp [wittPolynomial_eq_sum_C_mul_X_pow, aeval, mul_coeff, peval, map_natCast,
    map_add, map_pow, map_mul]

variable [CharP k p]

/-- The characteristic `p` version of `peval_polyOfInterest` -/
theorem peval_polyOfInterest' (n : â„•) (x y : ğ• k) :
    peval (polyOfInterest p n) ![fun i => x.coeff i, fun i => y.coeff i] =
      (x * y).coeff (n + 1) - y.coeff (n + 1) * x.coeff 0 ^ p ^ (n + 1) -
        x.coeff (n + 1) * y.coeff 0 ^ p ^ (n + 1) := by
  rw [peval_polyOfInterest]
  have : (p : k) = 0 := CharP.cast_eq_zero k p
  simp only [this, ne_eq, add_eq_zero, and_false, zero_pow, zero_mul, add_zero,
    not_false_eq_true, reduceCtorEq]
  have sum_zero_pow_mul_pow_p (y : ğ• k) : âˆ‘ x âˆˆ range (n + 1 + 1),
      (0 : k) ^ x * y.coeff x ^ p ^ (n + 1 - x) = y.coeff 0 ^ p ^ (n + 1) := by
    rw [Finset.sum_eq_single_of_mem 0] <;> simp +contextual
  congr <;> apply sum_zero_pow_mul_pow_p

variable (k)

theorem nth_mul_coeff' (n : â„•) :
    âˆƒ f : TruncatedWittVector p (n + 1) k â†’ TruncatedWittVector p (n + 1) k â†’ k,
    âˆ€ x y : ğ• k, f (truncateFun (n + 1) x) (truncateFun (n + 1) y) =
      (x * y).coeff (n + 1) - y.coeff (n + 1) * x.coeff 0 ^ p ^ (n + 1) -
        x.coeff (n + 1) * y.coeff 0 ^ p ^ (n + 1) := by
  simp only [â† peval_polyOfInterest']
  obtain âŸ¨fâ‚€, hfâ‚€âŸ© := exists_restrict_to_vars k (polyOfInterest_vars p n)
  have : âˆ€ (a : Multiset (Fin 2)) (b : Multiset â„•), a Ã—Ë¢ b = a.product b := fun a b => rfl
  let f : TruncatedWittVector p (n + 1) k â†’ TruncatedWittVector p (n + 1) k â†’ k := by
    intro x y
    apply fâ‚€
    rintro âŸ¨a, haâŸ©
    apply Function.uncurry ![x, y]
    simp_rw [product_val, this, range_val, Multiset.range_succ] at ha
    let S : Set (Fin 2 Ã— â„•) := (fun a => a.2 = n âˆ¨ a.2 < n)
    have ha' : a âˆˆ S := by
      convert ha
      dsimp [S]
      congr!
      simp
    refine âŸ¨a.fst, âŸ¨a.snd, ?_âŸ©âŸ©
    obtain âŸ¨ha, haâŸ© := ha' <;> omega
  use f
  intro x y
  dsimp [f, peval]
  rw [â† hfâ‚€]
  congr
  ext a
  obtain âŸ¨a, haâŸ© := a
  obtain âŸ¨i, mâŸ© := a
  fin_cases i <;> rfl -- surely this case split is not necessary

theorem nth_mul_coeff (n : â„•) :
    âˆƒ f : TruncatedWittVector p (n + 1) k â†’ TruncatedWittVector p (n + 1) k â†’ k,
    âˆ€ x y : ğ• k, (x * y).coeff (n + 1) =
      x.coeff (n + 1) * y.coeff 0 ^ p ^ (n + 1) + y.coeff (n + 1) * x.coeff 0 ^ p ^ (n + 1) +
      f (truncateFun (n + 1) x) (truncateFun (n + 1) y) := by
  obtain âŸ¨f, hfâŸ© := nth_mul_coeff' p k n
  use f
  intro x y
  rw [hf x y]
  ring

variable {k}

/--
Produces the "remainder function" of the `n+1`st coefficient, which does not depend on the `n+1`st
coefficients of the inputs. -/
def nthRemainder (n : â„•) : (Fin (n + 1) â†’ k) â†’ (Fin (n + 1) â†’ k) â†’ k :=
  Classical.choose (nth_mul_coeff p k n)

theorem nthRemainder_spec (n : â„•) (x y : ğ• k) : (x * y).coeff (n + 1) =
    x.coeff (n + 1) * y.coeff 0 ^ p ^ (n + 1) + y.coeff (n + 1) * x.coeff 0 ^ p ^ (n + 1) +
    nthRemainder p n (truncateFun (n + 1) x) (truncateFun (n + 1) y) :=
  Classical.choose_spec (nth_mul_coeff p k n) _ _

end WittVector
