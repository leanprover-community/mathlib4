/-
Copyright (c) 2025 Jiedong Jiang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jiedong Jiang
-/

import Mathlib.RingTheory.WittVector.Complete
import Mathlib.RingTheory.WittVector.Teichmuller

/-!
# Teichmuller Series

Let `R` be a characteristic `p` perfect ring. In this file, we show that
every element `x` of the Witt vectors `ùïé R` can be written as the
(`p`-adic) summation of Teichmuller series, namely
`‚àë i, (teichmuller p
        (((frobeniusEquiv R p).symm ^ i) (x.coeff i)) * p ^ i)`

## Main theorems

* `WittVector.dvd_sub_sum_teichmuller_iterateFrobeniusEquiv_coeff` : `p ^ (n + 1)` divides
`x` minus the summation of the first `n + 1` terms of the Teichmuller series.
* `WittVector.eq_of_apply_teichmuller_eq` : Given a ring `S` such that `p` is nilpotent in `S`
and two ring maps `f g : ùïé R ‚Üí+* S`, if they coincide on the teichmuller representatives,
then they are equal.

## TODO
Show that the Teichmuller series is unique.
-/

open Ideal Quotient
namespace WittVector

variable {p : ‚Ñï} [hp : Fact (Nat.Prime p)]

local notation "ùïé" => WittVector p

variable {R : Type*} [CommRing R]

theorem sum_coeff_eq_coeff_sum {Œ± : Type*} {S : Finset Œ±} (x : Œ± ‚Üí ùïé R)
    (h : ‚àÄ (n : ‚Ñï), Subsingleton {r | r ‚àà S ‚àß (x r).coeff n ‚â† 0}) (n : ‚Ñï) :
    (‚àë s ‚àà S, x s).coeff n = ‚àë (s ‚àà S), (x s).coeff n := by
  classical
  induction S using Finset.induction generalizing n with
  | empty =>
    simp
  | insert a S' ha hind =>
    have : (‚àÄ (n : ‚Ñï), Subsingleton {r | r ‚àà S' ‚àß (x r).coeff n ‚â† 0}) := by
      refine fun n ‚Ü¶ ‚ü®fun b c ‚Ü¶ ?_‚ü©
      ext
      exact congrArg (fun x ‚Ü¶ x.1) <|
          (h n).allEq ‚ü®b.1, S'.subset_insert a b.2.1, b.2.2‚ü© ‚ü®c.1, S'.subset_insert a c.2.1, c.2.2‚ü©
    replace hind := hind this
    simp only [ha, not_false_eq_true, Finset.sum_insert]
    have : ‚àÄ (n : ‚Ñï), (x a).coeff n = 0 ‚à® (‚àë s ‚àà S', x s).coeff n = 0 := by
      simp only [hind]
      by_contra! h
      obtain ‚ü®m, hma, hmS'‚ü© := h
      have := Finset.sum_eq_zero.mt hmS'
      push_neg at this
      choose b hb hb' using this
      have : a = b :=
        congrArg (fun x ‚Ü¶ x.1) <|
          (h m).allEq ‚ü®a, S'.mem_insert_self a, hma‚ü© ‚ü®b, S'.mem_insert_of_mem hb, hb'‚ü©
      exact ha (this ‚ñ∏ hb)
    rw [coeff_add_of_disjoint n _ _ this, hind n]

variable [CharP R p]

@[simp]
theorem teichmuller_mul_pow_coeff (n : ‚Ñï) (x : R) :
    (teichmuller p x * p ^ n).coeff n = x ^ p ^ n := by
  simpa using WittVector.mul_pow_charP_coeff_succ (teichmuller p x) (m := 0)

theorem teichmuller_mul_pow_coeff_of_ne (x : R)
    {m n : ‚Ñï} (h : m ‚â† n) : (teichmuller p x * p ^ n).coeff m = 0 := by
  cases Nat.lt_or_lt_of_ne h with
  | inl h =>
     exact WittVector.mul_pow_charP_coeff_zero (teichmuller p x) h
  | inr h =>
    rw [‚Üê Nat.sub_add_cancel h.le, WittVector.mul_pow_charP_coeff_succ (teichmuller p x),
        WittVector.teichmuller_coeff_pos p x (m - n) (Nat.zero_lt_sub_of_lt h), zero_pow]
    simp [Prime.ne_zero <| Nat.Prime.prime Fact.out]

variable [PerfectRing R p]

/--
`p ^ (n + 1)` divides
`x` minus the summation of the first `n + 1` terms of the Teichmuller series.
-/
theorem dvd_sub_sum_teichmuller_iterateFrobeniusEquiv_coeff (x : ùïé R) (n : ‚Ñï) :
    (p : ùïé R) ^ (n + 1) ‚à£ x - ‚àë (i ‚â§ n), (teichmuller p
        (((_root_.frobeniusEquiv R p).symm ^ i) (x.coeff i)) * p ^ i) := by
  rw [‚Üê Ideal.mem_span_singleton, mem_span_p_pow_iff_le_coeff_eq_zero,
      ‚Üê le_coeff_eq_iff_le_sub_coeff_eq_zero]
  intro i hi
  rw [WittVector.sum_coeff_eq_coeff_sum]
  ¬∑ rw [Finset.sum_eq_add_sum_diff_singleton (Finset.mem_Iic.mpr (Nat.lt_succ_iff.mp hi))]
    let g := fun x : ‚Ñï ‚Ü¶ (0 : R)
    rw [Finset.sum_congr rfl (g := g)]
    ¬∑ simp [g]
    ¬∑ intro b hb
      simp only [Finset.mem_sdiff, Finset.mem_Iic, Finset.mem_singleton] at hb
      exact teichmuller_mul_pow_coeff_of_ne _ (Ne.intro hb.2).symm
  ¬∑ refine fun n ‚Ü¶ ‚ü®fun ‚ü®a, _, ha‚ü© ‚ü®b, _, hb‚ü© ‚Ü¶ ?_‚ü©
    ext
    dsimp only [ne_eq, Set.mem_setOf_eq]
    rw [‚Üê of_not_not ((teichmuller_mul_pow_coeff_of_ne _).mt ha)]
    exact of_not_not ((teichmuller_mul_pow_coeff_of_ne _).mt hb)

/--
Given a ring `S` such that `p` is nilpotent in `S`
and two ring maps `f g : ùïé R ‚Üí+* S`, if they coincide on the teichmuller representatives,
then they are equal.
-/
theorem eq_of_apply_teichmuller_eq
    {S : Type*} [CommRing S] (f g : ùïé R ‚Üí+* S) (hp : IsNilpotent (p : S))
    (h : ‚àÄ (x : R), f (teichmuller p x) = g (teichmuller p x)) : f = g := by
  obtain ‚ü®n, hn‚ü© := hp
  ext x
  obtain ‚ü®c, hc‚ü© := (dvd_sub_sum_teichmuller_iterateFrobeniusEquiv_coeff x n)
  calc
    f x = f (x - ‚àë (i ‚â§ n), teichmuller p (((_root_.frobeniusEquiv R p).symm ^ i)
        (x.coeff i)) * p ^ i) + f (‚àë (i ‚â§ n), teichmuller p
        (((_root_.frobeniusEquiv R p).symm ^ i) (x.coeff i)) * p ^ i) := by simp
    _ = ‚àë (i ‚â§ n), f (teichmuller p (((_root_.frobeniusEquiv R p).symm ^ i)
        (x.coeff i))) * p ^ i := by rw [hc]; simp [pow_succ, hn]
    _ = ‚àë (i ‚â§ n), g (teichmuller p
        (((_root_.frobeniusEquiv R p).symm ^ i) (x.coeff i))) * p ^ i := by simp [h]
    _ = g (x - ‚àë (i ‚â§ n), teichmuller p (((_root_.frobeniusEquiv R p).symm ^ i)
        (x.coeff i)) * p ^ i) + g (‚àë (i ‚â§ n), teichmuller p (((_root_.frobeniusEquiv R p).symm ^ i)
        (x.coeff i)) * p ^ i) := by rw [hc]; simp [pow_succ, hn]
    _ = g x := by simp

end WittVector
