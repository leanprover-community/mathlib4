/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Robert Y. Lewis
-/
import Mathlib.RingTheory.WittVector.InitTail

#align_import ring_theory.witt_vector.truncated from "leanprover-community/mathlib"@"acbe099ced8be9c9754d62860110295cde0d7181"

/-!

# Truncated Witt vectors

The ring of truncated Witt vectors (of length `n`) is a quotient of the ring of Witt vectors.
It retains the first `n` coefficients of each Witt vector.
In this file, we set up the basic quotient API for this ring.

The ring of Witt vectors is the projective limit of all the rings of truncated Witt vectors.

## Main declarations

- `TruncatedWittVector`: the underlying type of the ring of truncated Witt vectors
- `TruncatedWittVector.instCommRing`: the ring structure on truncated Witt vectors
- `WittVector.truncate`: the quotient homomorphism that truncates a Witt vector,
  to obtain a truncated Witt vector
- `TruncatedWittVector.truncate`: the homomorphism that truncates
  a truncated Witt vector of length `n` to one of length `m` (for some `m â‰¤ n`)
- `WittVector.lift`: the unique ring homomorphism into the ring of Witt vectors
  that is compatible with a family of ring homomorphisms to the truncated Witt vectors:
  this realizes the ring of Witt vectors as projective limit of the rings of truncated Witt vectors

## References

* [Hazewinkel, *Witt Vectors*][Haze09]

* [Commelin and Lewis, *Formalizing the Ring of Witt Vectors*][CL21]
-/


open Function (Injective Surjective)

noncomputable section

variable {p : â„•} [hp : Fact p.Prime] (n : â„•) (R : Type*)

local notation "ğ•" => WittVector p -- type as `\bbW`

/-- A truncated Witt vector over `R` is a vector of elements of `R`,
i.e., the first `n` coefficients of a Witt vector.
We will define operations on this type that are compatible with the (untruncated) Witt
vector operations.

`TruncatedWittVector p n R` takes a parameter `p : â„•` that is not used in the definition.
In practice, this number `p` is assumed to be a prime number,
and under this assumption we construct a ring structure on `TruncatedWittVector p n R`.
(`TruncatedWittVector pâ‚ n R` and `TruncatedWittVector pâ‚‚ n R` are definitionally
equal as types but will have different ring operations.)
-/
@[nolint unusedArguments]
def TruncatedWittVector (_ : â„•) (n : â„•) (R : Type*) :=
  Fin n â†’ R
#align truncated_witt_vector TruncatedWittVector

instance (p n : â„•) (R : Type*) [Inhabited R] : Inhabited (TruncatedWittVector p n R) :=
  âŸ¨fun _ => defaultâŸ©

variable {n R}

namespace TruncatedWittVector

variable (p)

/-- Create a `TruncatedWittVector` from a vector `x`. -/
def mk (x : Fin n â†’ R) : TruncatedWittVector p n R :=
  x
#align truncated_witt_vector.mk TruncatedWittVector.mk

variable {p}

/-- `x.coeff i` is the `i`th entry of `x`. -/
def coeff (i : Fin n) (x : TruncatedWittVector p n R) : R :=
  x i
#align truncated_witt_vector.coeff TruncatedWittVector.coeff

@[ext]
theorem ext {x y : TruncatedWittVector p n R} (h : âˆ€ i, x.coeff i = y.coeff i) : x = y :=
  funext h
#align truncated_witt_vector.ext TruncatedWittVector.ext

theorem ext_iff {x y : TruncatedWittVector p n R} : x = y â†” âˆ€ i, x.coeff i = y.coeff i :=
  âŸ¨fun h i => by rw [h], extâŸ©
#align truncated_witt_vector.ext_iff TruncatedWittVector.ext_iff

@[simp]
theorem coeff_mk (x : Fin n â†’ R) (i : Fin n) : (mk p x).coeff i = x i :=
  rfl
#align truncated_witt_vector.coeff_mk TruncatedWittVector.coeff_mk

@[simp]
theorem mk_coeff (x : TruncatedWittVector p n R) : (mk p fun i => x.coeff i) = x := by
  ext i; rw [coeff_mk]
#align truncated_witt_vector.mk_coeff TruncatedWittVector.mk_coeff

variable [CommRing R]

/-- We can turn a truncated Witt vector `x` into a Witt vector
by setting all coefficients after `x` to be 0.
-/
def out (x : TruncatedWittVector p n R) : ğ• R :=
  @WittVector.mk' p _ fun i => if h : i < n then x.coeff âŸ¨i, hâŸ© else 0
#align truncated_witt_vector.out TruncatedWittVector.out

@[simp]
theorem coeff_out (x : TruncatedWittVector p n R) (i : Fin n) : x.out.coeff i = x.coeff i := by
  rw [out]; dsimp only; rw [dif_pos i.is_lt, Fin.eta]
#align truncated_witt_vector.coeff_out TruncatedWittVector.coeff_out

theorem out_injective : Injective (@out p n R _) := by
  intro x y h
  ext i
  rw [WittVector.ext_iff] at h
  simpa only [coeff_out] using h â†‘i
#align truncated_witt_vector.out_injective TruncatedWittVector.out_injective

end TruncatedWittVector

namespace WittVector

variable (n)

section

/-- `truncateFun n x` uses the first `n` entries of `x` to construct a `TruncatedWittVector`,
which has the same base `p` as `x`.
This function is bundled into a ring homomorphism in `WittVector.truncate` -/
def truncateFun (x : ğ• R) : TruncatedWittVector p n R :=
  TruncatedWittVector.mk p fun i => x.coeff i
#align witt_vector.truncate_fun WittVector.truncateFun

end

variable {n}

@[simp]
theorem coeff_truncateFun (x : ğ• R) (i : Fin n) : (truncateFun n x).coeff i = x.coeff i := by
  rw [truncateFun, TruncatedWittVector.coeff_mk]
#align witt_vector.coeff_truncate_fun WittVector.coeff_truncateFun

variable [CommRing R]

@[simp]
theorem out_truncateFun (x : ğ• R) : (truncateFun n x).out = init n x := by
  ext i
  dsimp [TruncatedWittVector.out, init, select, coeff_mk]
  split_ifs with hi; swap; Â· rfl
  rw [coeff_truncateFun, Fin.val_mk]
#align witt_vector.out_truncate_fun WittVector.out_truncateFun

end WittVector

namespace TruncatedWittVector

variable [CommRing R]

@[simp]
theorem truncateFun_out (x : TruncatedWittVector p n R) : x.out.truncateFun n = x := by
  simp only [WittVector.truncateFun, coeff_out, mk_coeff]
#align truncated_witt_vector.truncate_fun_out TruncatedWittVector.truncateFun_out

open WittVector

variable (p n R)

instance : Zero (TruncatedWittVector p n R) :=
  âŸ¨truncateFun n 0âŸ©

instance : One (TruncatedWittVector p n R) :=
  âŸ¨truncateFun n 1âŸ©

instance : NatCast (TruncatedWittVector p n R) :=
  âŸ¨fun i => truncateFun n iâŸ©

instance : IntCast (TruncatedWittVector p n R) :=
  âŸ¨fun i => truncateFun n iâŸ©

instance : Add (TruncatedWittVector p n R) :=
  âŸ¨fun x y => truncateFun n (x.out + y.out)âŸ©

instance : Mul (TruncatedWittVector p n R) :=
  âŸ¨fun x y => truncateFun n (x.out * y.out)âŸ©

instance : Neg (TruncatedWittVector p n R) :=
  âŸ¨fun x => truncateFun n (-x.out)âŸ©

instance : Sub (TruncatedWittVector p n R) :=
  âŸ¨fun x y => truncateFun n (x.out - y.out)âŸ©

instance hasNatScalar : SMul â„• (TruncatedWittVector p n R) :=
  âŸ¨fun m x => truncateFun n (m â€¢ x.out)âŸ©
#align truncated_witt_vector.has_nat_scalar TruncatedWittVector.hasNatScalar

instance hasIntScalar : SMul â„¤ (TruncatedWittVector p n R) :=
  âŸ¨fun m x => truncateFun n (m â€¢ x.out)âŸ©
#align truncated_witt_vector.has_int_scalar TruncatedWittVector.hasIntScalar

instance hasNatPow : Pow (TruncatedWittVector p n R) â„• :=
  âŸ¨fun x m => truncateFun n (x.out ^ m)âŸ©
#align truncated_witt_vector.has_nat_pow TruncatedWittVector.hasNatPow

@[simp]
theorem coeff_zero (i : Fin n) : (0 : TruncatedWittVector p n R).coeff i = 0 := by
  show coeff i (truncateFun _ 0 : TruncatedWittVector p n R) = 0
  rw [coeff_truncateFun, WittVector.zero_coeff]
#align truncated_witt_vector.coeff_zero TruncatedWittVector.coeff_zero

end TruncatedWittVector

/-- A macro tactic used to prove that `truncateFun` respects ring operations. -/
macro (name := witt_truncateFun_tac) "witt_truncateFun_tac" : tactic =>
  `(tactic|
    { show _ = WittVector.truncateFun n _
      apply TruncatedWittVector.out_injective
      iterate rw [WittVector.out_truncateFun]
      first
      | rw [WittVector.init_add]
      | rw [WittVector.init_mul]
      | rw [WittVector.init_neg]
      | rw [WittVector.init_sub]
      | rw [WittVector.init_nsmul]
      | rw [WittVector.init_zsmul]
      | rw [WittVector.init_pow]})

namespace WittVector

variable (p n R)
variable [CommRing R]

theorem truncateFun_surjective : Surjective (@truncateFun p n R) :=
  Function.RightInverse.surjective TruncatedWittVector.truncateFun_out
#align witt_vector.truncate_fun_surjective WittVector.truncateFun_surjective

@[simp]
theorem truncateFun_zero : truncateFun n (0 : ğ• R) = 0 := rfl
#align witt_vector.truncate_fun_zero WittVector.truncateFun_zero

@[simp]
theorem truncateFun_one : truncateFun n (1 : ğ• R) = 1 := rfl
#align witt_vector.truncate_fun_one WittVector.truncateFun_one

variable {p R}

@[simp]
theorem truncateFun_add (x y : ğ• R) :
    truncateFun n (x + y) = truncateFun n x + truncateFun n y := by
  witt_truncateFun_tac
#align witt_vector.truncate_fun_add WittVector.truncateFun_add

@[simp]
theorem truncateFun_mul (x y : ğ• R) :
    truncateFun n (x * y) = truncateFun n x * truncateFun n y := by
  witt_truncateFun_tac
#align witt_vector.truncate_fun_mul WittVector.truncateFun_mul

theorem truncateFun_neg (x : ğ• R) : truncateFun n (-x) = -truncateFun n x := by
  witt_truncateFun_tac
#align witt_vector.truncate_fun_neg WittVector.truncateFun_neg

theorem truncateFun_sub (x y : ğ• R) :
    truncateFun n (x - y) = truncateFun n x - truncateFun n y := by
  witt_truncateFun_tac
#align witt_vector.truncate_fun_sub WittVector.truncateFun_sub

theorem truncateFun_nsmul (m : â„•) (x : ğ• R) : truncateFun n (m â€¢ x) = m â€¢ truncateFun n x := by
  witt_truncateFun_tac
#align witt_vector.truncate_fun_nsmul WittVector.truncateFun_nsmul

theorem truncateFun_zsmul (m : â„¤) (x : ğ• R) : truncateFun n (m â€¢ x) = m â€¢ truncateFun n x := by
  witt_truncateFun_tac
#align witt_vector.truncate_fun_zsmul WittVector.truncateFun_zsmul

theorem truncateFun_pow (x : ğ• R) (m : â„•) : truncateFun n (x ^ m) = truncateFun n x ^ m := by
  witt_truncateFun_tac
#align witt_vector.truncate_fun_pow WittVector.truncateFun_pow

theorem truncateFun_natCast (m : â„•) : truncateFun n (m : ğ• R) = m := rfl
#align witt_vector.truncate_fun_nat_cast WittVector.truncateFun_natCast

@[deprecated (since := "2024-04-17")]
alias truncateFun_nat_cast := truncateFun_natCast

theorem truncateFun_intCast (m : â„¤) : truncateFun n (m : ğ• R) = m := rfl
#align witt_vector.truncate_fun_int_cast WittVector.truncateFun_intCast

@[deprecated (since := "2024-04-17")]
alias truncateFun_int_cast := truncateFun_intCast

end WittVector

namespace TruncatedWittVector

open WittVector

variable (p n R)
variable [CommRing R]

instance instCommRing : CommRing (TruncatedWittVector p n R) :=
  (truncateFun_surjective p n R).commRing _ (truncateFun_zero p n R) (truncateFun_one p n R)
    (truncateFun_add n) (truncateFun_mul n) (truncateFun_neg n) (truncateFun_sub n)
    (truncateFun_nsmul n) (truncateFun_zsmul n) (truncateFun_pow n) (truncateFun_natCast n)
    (truncateFun_intCast n)

end TruncatedWittVector

namespace WittVector

open TruncatedWittVector

variable (n)
variable [CommRing R]

/-- `truncate n` is a ring homomorphism that truncates `x` to its first `n` entries
to obtain a `TruncatedWittVector`, which has the same base `p` as `x`. -/
noncomputable def truncate : ğ• R â†’+* TruncatedWittVector p n R where
  toFun := truncateFun n
  map_zero' := truncateFun_zero p n R
  map_add' := truncateFun_add n
  map_one' := truncateFun_one p n R
  map_mul' := truncateFun_mul n
#align witt_vector.truncate WittVector.truncate

variable (p R)

theorem truncate_surjective : Surjective (truncate n : ğ• R â†’ TruncatedWittVector p n R) :=
  truncateFun_surjective p n R
#align witt_vector.truncate_surjective WittVector.truncate_surjective

variable {p n R}

@[simp]
theorem coeff_truncate (x : ğ• R) (i : Fin n) : (truncate n x).coeff i = x.coeff i :=
  coeff_truncateFun _ _
#align witt_vector.coeff_truncate WittVector.coeff_truncate

variable (n)

theorem mem_ker_truncate (x : ğ• R) :
    x âˆˆ RingHom.ker (@truncate p _ n R _) â†” âˆ€ i < n, x.coeff i = 0 := by
  simp only [RingHom.mem_ker, truncate, truncateFun, RingHom.coe_mk, TruncatedWittVector.ext_iff,
    TruncatedWittVector.coeff_mk, coeff_zero]
  exact Fin.forall_iff
#align witt_vector.mem_ker_truncate WittVector.mem_ker_truncate

variable (p)

@[simp]
theorem truncate_mk' (f : â„• â†’ R) :
    truncate n (@mk' p _ f) = TruncatedWittVector.mk _ fun k => f k := by
  ext i
  simp only [coeff_truncate, TruncatedWittVector.coeff_mk]
#align witt_vector.truncate_mk WittVector.truncate_mk'

end WittVector

namespace TruncatedWittVector

variable [CommRing R]

/-- A ring homomorphism that truncates a truncated Witt vector of length `m` to
a truncated Witt vector of length `n`, for `n â‰¤ m`.
-/
def truncate {m : â„•} (hm : n â‰¤ m) : TruncatedWittVector p m R â†’+* TruncatedWittVector p n R :=
  RingHom.liftOfRightInverse (WittVector.truncate m) out truncateFun_out
    âŸ¨WittVector.truncate n, by
      intro x
      simp only [WittVector.mem_ker_truncate]
      intro h i hi
      exact h i (lt_of_lt_of_le hi hm)âŸ©
#align truncated_witt_vector.truncate TruncatedWittVector.truncate

@[simp]
theorem truncate_comp_wittVector_truncate {m : â„•} (hm : n â‰¤ m) :
    (@truncate p _ n R _ m hm).comp (WittVector.truncate m) = WittVector.truncate n :=
  RingHom.liftOfRightInverse_comp _ _ _ _
#align truncated_witt_vector.truncate_comp_witt_vector_truncate TruncatedWittVector.truncate_comp_wittVector_truncate

@[simp]
theorem truncate_wittVector_truncate {m : â„•} (hm : n â‰¤ m) (x : ğ• R) :
    truncate hm (WittVector.truncate m x) = WittVector.truncate n x :=
  RingHom.liftOfRightInverse_comp_apply _ _ _ _ _
#align truncated_witt_vector.truncate_witt_vector_truncate TruncatedWittVector.truncate_wittVector_truncate

@[simp]
theorem truncate_truncate {nâ‚ nâ‚‚ nâ‚ƒ : â„•} (h1 : nâ‚ â‰¤ nâ‚‚) (h2 : nâ‚‚ â‰¤ nâ‚ƒ)
    (x : TruncatedWittVector p nâ‚ƒ R) :
    (truncate h1) (truncate h2 x) = truncate (h1.trans h2) x := by
  obtain âŸ¨x, rflâŸ© := @WittVector.truncate_surjective p _ nâ‚ƒ R _ x
  simp only [truncate_wittVector_truncate]
#align truncated_witt_vector.truncate_truncate TruncatedWittVector.truncate_truncate

@[simp]
theorem truncate_comp {nâ‚ nâ‚‚ nâ‚ƒ : â„•} (h1 : nâ‚ â‰¤ nâ‚‚) (h2 : nâ‚‚ â‰¤ nâ‚ƒ) :
    (@truncate p _ _ R _ _ h1).comp (truncate h2) = truncate (h1.trans h2) := by
  ext1 x; simp only [truncate_truncate, Function.comp_apply, RingHom.coe_comp]
#align truncated_witt_vector.truncate_comp TruncatedWittVector.truncate_comp

theorem truncate_surjective {m : â„•} (hm : n â‰¤ m) : Surjective (@truncate p _ _ R _ _ hm) := by
  intro x
  obtain âŸ¨x, rflâŸ© := @WittVector.truncate_surjective p _ _ R _ x
  exact âŸ¨WittVector.truncate _ x, truncate_wittVector_truncate _ _âŸ©
#align truncated_witt_vector.truncate_surjective TruncatedWittVector.truncate_surjective

@[simp]
theorem coeff_truncate {m : â„•} (hm : n â‰¤ m) (i : Fin n) (x : TruncatedWittVector p m R) :
    (truncate hm x).coeff i = x.coeff (Fin.castLE hm i) := by
  obtain âŸ¨y, rflâŸ© := @WittVector.truncate_surjective p _ _ _ _ x
  simp only [truncate_wittVector_truncate, WittVector.coeff_truncate, Fin.coe_castLE]
#align truncated_witt_vector.coeff_truncate TruncatedWittVector.coeff_truncate

section Fintype

instance {R : Type*} [Fintype R] : Fintype (TruncatedWittVector p n R) :=
  Pi.fintype

variable (p n R)

theorem card {R : Type*} [Fintype R] :
    Fintype.card (TruncatedWittVector p n R) = Fintype.card R ^ n := by
  simp only [TruncatedWittVector, Fintype.card_fin, Fintype.card_fun]
#align truncated_witt_vector.card TruncatedWittVector.card

end Fintype

theorem iInf_ker_truncate : â¨… i : â„•, RingHom.ker (@WittVector.truncate p _ i R _) = âŠ¥ := by
  rw [Submodule.eq_bot_iff]
  intro x hx
  ext
  simp only [WittVector.mem_ker_truncate, Ideal.mem_iInf, WittVector.zero_coeff] at hx âŠ¢
  exact hx _ _ (Nat.lt_succ_self _)
#align truncated_witt_vector.infi_ker_truncate TruncatedWittVector.iInf_ker_truncate

end TruncatedWittVector

namespace WittVector

open TruncatedWittVector hiding truncate coeff

section lift

variable [CommRing R]
variable {S : Type*} [Semiring S]
variable (f : âˆ€ k : â„•, S â†’+* TruncatedWittVector p k R)
variable
  (f_compat : âˆ€ (kâ‚ kâ‚‚ : â„•) (hk : kâ‚ â‰¤ kâ‚‚), (TruncatedWittVector.truncate hk).comp (f kâ‚‚) = f kâ‚)

variable (n)

/-- Given a family `fâ‚– : S â†’ TruncatedWittVector p k R` and `s : S`, we produce a Witt vector by
defining the `k`th entry to be the final entry of `fâ‚– s`.
-/
def liftFun (s : S) : ğ• R :=
  @WittVector.mk' p _ fun k => TruncatedWittVector.coeff (Fin.last k) (f (k + 1) s)
#align witt_vector.lift_fun WittVector.liftFun

variable {f}

@[simp]
theorem truncate_liftFun (s : S) : WittVector.truncate n (liftFun f s) = f n s := by
  ext i
  simp only [liftFun, TruncatedWittVector.coeff_mk, WittVector.truncate_mk']
  rw [â† f_compat (i + 1) n i.is_lt, RingHom.comp_apply, TruncatedWittVector.coeff_truncate]
  congr 1 with _
#align witt_vector.truncate_lift_fun WittVector.truncate_liftFun

variable (f)

/--
Given compatible ring homs from `S` into `TruncatedWittVector n` for each `n`, we can lift these
to a ring hom `S â†’ ğ• R`.

`lift` defines the universal property of `ğ• R` as the inverse limit of `TruncatedWittVector n`.
-/
def lift : S â†’+* ğ• R := by
  refine {  toFun := liftFun f
            map_zero' := ?_
            map_one' := ?_
            map_add' := ?_
            map_mul' := ?_ } <;>
  ( intros
    dsimp only
    rw [â† sub_eq_zero, â† Ideal.mem_bot, â† iInf_ker_truncate, Ideal.mem_iInf]
    simp [RingHom.mem_ker, f_compat])
#align witt_vector.lift WittVector.lift

variable {f}

@[simp]
theorem truncate_lift (s : S) : WittVector.truncate n (lift _ f_compat s) = f n s :=
  truncate_liftFun _ f_compat s
#align witt_vector.truncate_lift WittVector.truncate_lift

@[simp]
theorem truncate_comp_lift : (WittVector.truncate n).comp (lift _ f_compat) = f n := by
  ext1; rw [RingHom.comp_apply, truncate_lift]
#align witt_vector.truncate_comp_lift WittVector.truncate_comp_lift

/-- The uniqueness part of the universal property of `ğ• R`. -/
theorem lift_unique (g : S â†’+* ğ• R) (g_compat : âˆ€ k, (WittVector.truncate k).comp g = f k) :
    lift _ f_compat = g := by
  ext1 x
  rw [â† sub_eq_zero, â† Ideal.mem_bot, â† iInf_ker_truncate, Ideal.mem_iInf]
  intro i
  simp only [RingHom.mem_ker, g_compat, â† RingHom.comp_apply, truncate_comp_lift, RingHom.map_sub,
    sub_self]
#align witt_vector.lift_unique WittVector.lift_unique

/-- The universal property of `ğ• R` as projective limit of truncated Witt vector rings. -/
@[simps]
def liftEquiv : { f : âˆ€ k, S â†’+* TruncatedWittVector p k R // âˆ€ (kâ‚ kâ‚‚) (hk : kâ‚ â‰¤ kâ‚‚),
    (TruncatedWittVector.truncate hk).comp (f kâ‚‚) = f kâ‚ } â‰ƒ (S â†’+* ğ• R) where
  toFun f := lift f.1 f.2
  invFun g :=
    âŸ¨fun k => (truncate k).comp g, by
      intro _ _ h
      simp only [â† RingHom.comp_assoc, truncate_comp_wittVector_truncate]âŸ©
  left_inv := by rintro âŸ¨f, hfâŸ©; simp only [truncate_comp_lift]
  right_inv g := lift_unique _ _ fun _ => rfl
#align witt_vector.lift_equiv WittVector.liftEquiv

theorem hom_ext (gâ‚ gâ‚‚ : S â†’+* ğ• R) (h : âˆ€ k, (truncate k).comp gâ‚ = (truncate k).comp gâ‚‚) :
    gâ‚ = gâ‚‚ :=
  liftEquiv.symm.injective <| Subtype.ext <| funext h
#align witt_vector.hom_ext WittVector.hom_ext

end lift

end WittVector
