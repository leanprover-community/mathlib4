/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin
-/
import Mathlib.Algebra.Algebra.ZMod
import Mathlib.Data.Nat.Multiplicity
import Mathlib.FieldTheory.Perfect
import Mathlib.RingTheory.WittVector.Basic
import Mathlib.RingTheory.WittVector.IsPoly

/-!
## The Frobenius operator

If `R` has characteristic `p`, then there is a ring endomorphism `frobenius R p`
that raises `r : R` to the power `p`.
By applying `WittVector.map` to `frobenius R p`, we obtain a ring endomorphism `ùïé R ‚Üí+* ùïé R`.
It turns out that this endomorphism can be described by polynomials over `‚Ñ§`
that do not depend on `R` or the fact that it has characteristic `p`.
In this way, we obtain a Frobenius endomorphism `WittVector.frobeniusFun : ùïé R ‚Üí ùïé R`
for every commutative ring `R`.

Unfortunately, the aforementioned polynomials can not be obtained using the machinery
of `wittStructureInt` that was developed in `StructurePolynomial.lean`.
We therefore have to define the polynomials by hand, and check that they have the required property.

In case `R` has characteristic `p`, we show in `frobenius_eq_map_frobenius`
that `WittVector.frobeniusFun` is equal to `WittVector.map (frobenius R p)`.

### Main definitions and results

* `frobeniusPoly`: the polynomials that describe the coefficients of `frobeniusFun`;
* `frobeniusFun`: the Frobenius endomorphism on Witt vectors;
* `frobeniusFun_isPoly`: the tautological assertion that Frobenius is a polynomial function;
* `frobenius_eq_map_frobenius`: the fact that in characteristic `p`, Frobenius is equal to
  `WittVector.map (frobenius R p)`.

TODO: Show that `WittVector.frobeniusFun` is a ring homomorphism,
and bundle it into `WittVector.frobenius`.

## References

* [Hazewinkel, *Witt Vectors*][Haze09]

* [Commelin and Lewis, *Formalizing the Ring of Witt Vectors*][CL21]
-/


namespace WittVector

variable {p : ‚Ñï} {R : Type*} [hp : Fact p.Prime] [CommRing R]

local notation "ùïé" => WittVector p -- type as `\bbW`

noncomputable section

open MvPolynomial Finset

variable (p)

/-- The rational polynomials that give the coefficients of `frobenius x`,
in terms of the coefficients of `x`.
These polynomials actually have integral coefficients,
see `frobeniusPoly` and `map_frobeniusPoly`. -/
def frobeniusPolyRat (n : ‚Ñï) : MvPolynomial ‚Ñï ‚Ñö :=
  bind‚ÇÅ (wittPolynomial p ‚Ñö ‚àò fun n => n + 1) (xInTermsOfW p ‚Ñö n)

theorem bind‚ÇÅ_frobeniusPolyRat_wittPolynomial (n : ‚Ñï) :
    bind‚ÇÅ (frobeniusPolyRat p) (wittPolynomial p ‚Ñö n) = wittPolynomial p ‚Ñö (n + 1) := by
  delta frobeniusPolyRat
  rw [‚Üê bind‚ÇÅ_bind‚ÇÅ, bind‚ÇÅ_xInTermsOfW_wittPolynomial, bind‚ÇÅ_X_right, Function.comp_apply]

local notation "v" => multiplicity

/-- An auxiliary polynomial over the integers, that satisfies
`p * (frobeniusPolyAux p n) + X n ^ p = frobeniusPoly p n`.
This makes it easy to show that `frobeniusPoly p n` is congruent to `X n ^ p`
modulo `p`. -/
noncomputable def frobeniusPolyAux : ‚Ñï ‚Üí MvPolynomial ‚Ñï ‚Ñ§
  | n => X (n + 1) -  ‚àë i : Fin n, have _ := i.is_lt
      ‚àë j ‚àà range (p ^ (n - i)),
        (((X (i : ‚Ñï) ^ p) ^ (p ^ (n - (i : ‚Ñï)) - (j + 1)) : MvPolynomial ‚Ñï ‚Ñ§) *
        (frobeniusPolyAux i) ^ (j + 1)) *
        C (((p ^ (n - i)).choose (j + 1) / (p ^ (n - i - v p (j + 1)))
          * ‚Üëp ^ (j - v p (j + 1)) : ‚Ñï) : ‚Ñ§)

omit hp in
theorem frobeniusPolyAux_eq (n : ‚Ñï) :
    frobeniusPolyAux p n =
      X (n + 1) - ‚àë i ‚àà range n,
          ‚àë j ‚àà range (p ^ (n - i)),
            (X i ^ p) ^ (p ^ (n - i) - (j + 1)) * frobeniusPolyAux p i ^ (j + 1) *
              C ‚Üë((p ^ (n - i)).choose (j + 1) / p ^ (n - i - v p (j + 1)) *
                ‚Üëp ^ (j - v p (j + 1)) : ‚Ñï) := by
  rw [frobeniusPolyAux, ‚Üê Fin.sum_univ_eq_sum_range]

/-- The polynomials that give the coefficients of `frobenius x`,
in terms of the coefficients of `x`. -/
def frobeniusPoly (n : ‚Ñï) : MvPolynomial ‚Ñï ‚Ñ§ :=
  X n ^ p + C (p : ‚Ñ§) * frobeniusPolyAux p n

/-
Our next goal is to prove
```
lemma map_frobeniusPoly (n : ‚Ñï) :
    MvPolynomial.map (Int.castRingHom ‚Ñö) (frobeniusPoly p n) = frobeniusPolyRat p n
```
This lemma has a rather long proof, but it mostly boils down to applying induction,
and then using the following two key facts at the right point.
-/
/-- A key divisibility fact for the proof of `WittVector.map_frobeniusPoly`. -/
theorem map_frobeniusPoly.key‚ÇÅ (n j : ‚Ñï) (hj : j < p ^ n) :
    p ^ (n - v p (j + 1)) ‚à£ (p ^ n).choose (j + 1) := by
  apply pow_dvd_of_le_emultiplicity
  rw [hp.out.emultiplicity_choose_prime_pow hj j.succ_ne_zero]

/-- A key numerical identity needed for the proof of `WittVector.map_frobeniusPoly`. -/
theorem map_frobeniusPoly.key‚ÇÇ {n i j : ‚Ñï} (hi : i ‚â§ n) (hj : j < p ^ (n - i)) :
    j - v p (j + 1) + n = i + j + (n - i - v p (j + 1)) := by
  generalize h : v p (j + 1) = m
  rsuffices ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© : m ‚â§ n - i ‚àß m ‚â§ j
  ¬∑ rw [tsub_add_eq_add_tsub h‚ÇÇ, add_comm i j, add_tsub_assoc_of_le (h‚ÇÅ.trans (Nat.sub_le n i)),
      add_assoc, tsub_right_comm, add_comm i,
      tsub_add_cancel_of_le (le_tsub_of_add_le_right ((le_tsub_iff_left hi).mp h‚ÇÅ))]
  have hle : p ^ m ‚â§ j + 1 := h ‚ñ∏ Nat.le_of_dvd j.succ_pos (pow_multiplicity_dvd _ _)
  exact ‚ü®(Nat.pow_le_pow_iff_right hp.1.one_lt).1 (hle.trans hj),
     Nat.le_of_lt_succ ((m.lt_pow_self hp.1.one_lt).trans_le hle)‚ü©

theorem map_frobeniusPoly (n : ‚Ñï) :
    MvPolynomial.map (Int.castRingHom ‚Ñö) (frobeniusPoly p n) = frobeniusPolyRat p n := by
  rw [frobeniusPoly, RingHom.map_add, RingHom.map_mul, RingHom.map_pow, map_C, map_X, eq_intCast,
    Int.cast_natCast, frobeniusPolyRat]
  refine Nat.strong_induction_on n ?_; clear n
  intro n IH
  rw [xInTermsOfW_eq]
  simp only [map_sum, map_sub, map_mul, map_pow (bind‚ÇÅ _), bind‚ÇÅ_C_right]
  have h1 : (p : ‚Ñö) ^ n * ‚Öü (p : ‚Ñö) ^ n = 1 := by rw [‚Üê mul_pow, mul_invOf_self, one_pow]
  rw [bind‚ÇÅ_X_right, Function.comp_apply, wittPolynomial_eq_sum_C_mul_X_pow, sum_range_succ,
    sum_range_succ, tsub_self, add_tsub_cancel_left, pow_zero, pow_one, pow_one, sub_mul, add_mul,
    add_mul, mul_right_comm, mul_right_comm (C ((p : ‚Ñö) ^ (n + 1))), ‚Üê C_mul, ‚Üê C_mul, pow_succ',
    mul_assoc (p : ‚Ñö) ((p : ‚Ñö) ^ n), h1, mul_one, C_1, one_mul, add_comm _ (X n ^ p), add_assoc,
    ‚Üê add_sub, add_right_inj, frobeniusPolyAux_eq, RingHom.map_sub, map_X, mul_sub, sub_eq_add_neg,
    add_comm _ (C (p : ‚Ñö) * X (n + 1)), ‚Üê add_sub,
    add_right_inj, neg_eq_iff_eq_neg, neg_sub, eq_comm]
  simp only [map_sum, mul_sum, sum_mul, ‚Üê sum_sub_distrib]
  apply sum_congr rfl
  intro i hi
  rw [mem_range] at hi
  rw [‚Üê IH i hi]
  clear IH
  rw [add_comm (X i ^ p), add_pow, sum_range_succ', pow_zero, tsub_zero, Nat.choose_zero_right,
    one_mul, Nat.cast_one, mul_one, mul_add, add_mul, Nat.succ_sub (le_of_lt hi),
    Nat.succ_eq_add_one (n - i), pow_succ', pow_mul, add_sub_cancel_right, mul_sum, sum_mul]
  apply sum_congr rfl
  intro j hj
  rw [mem_range] at hj
  rw [RingHom.map_mul, RingHom.map_mul, RingHom.map_pow, RingHom.map_pow, RingHom.map_pow,
    RingHom.map_pow, RingHom.map_pow, map_C, map_X, mul_pow]
  rw [mul_comm (C (p : ‚Ñö) ^ i), mul_comm _ ((X i ^ p) ^ _), mul_comm (C (p : ‚Ñö) ^ (j + 1)),
    mul_comm (C (p : ‚Ñö))]
  simp only [mul_assoc]
  apply congr_arg
  apply congr_arg
  rw [‚Üê C_eq_coe_nat]
  simp only [‚Üê RingHom.map_pow, ‚Üê C_mul]
  rw [C_inj]
  simp only [invOf_eq_inv, eq_intCast, inv_pow, Int.cast_natCast, Nat.cast_mul, Int.cast_mul]
  rw [Rat.natCast_div _ _ (map_frobeniusPoly.key‚ÇÅ p (n - i) j hj)]
  simp only [Nat.cast_pow, pow_add, pow_one]
  suffices
    (((p ^ (n - i)).choose (j + 1) : ‚Ñö) * (p : ‚Ñö) ^ (j - v p (j + 1)) * p * (p ^ n : ‚Ñö))
      = (p : ‚Ñö) ^ j * p * ‚Üë((p ^ (n - i)).choose (j + 1) * p ^ i) *
        (p : ‚Ñö) ^ (n - i - v p (j + 1)) by
    have aux : ‚àÄ k : ‚Ñï, (p : ‚Ñö)^ k ‚â† 0 := by
      intro; apply pow_ne_zero; exact mod_cast hp.1.ne_zero
    simpa [aux, -one_div, -pow_eq_zero_iff', field_simps] using this.symm
  rw [mul_comm _ (p : ‚Ñö), mul_assoc, mul_assoc, ‚Üê pow_add,
    map_frobeniusPoly.key‚ÇÇ p hi.le hj, Nat.cast_mul, Nat.cast_pow]
  ring

theorem frobeniusPoly_zmod (n : ‚Ñï) :
    MvPolynomial.map (Int.castRingHom (ZMod p)) (frobeniusPoly p n) = X n ^ p := by
  rw [frobeniusPoly, RingHom.map_add, RingHom.map_pow, RingHom.map_mul, map_X, map_C]
  simp only [Int.cast_natCast, add_zero, eq_intCast, ZMod.natCast_self, zero_mul, C_0]

@[simp]
theorem bind‚ÇÅ_frobeniusPoly_wittPolynomial (n : ‚Ñï) :
    bind‚ÇÅ (frobeniusPoly p) (wittPolynomial p ‚Ñ§ n) = wittPolynomial p ‚Ñ§ (n + 1) := by
  apply MvPolynomial.map_injective (Int.castRingHom ‚Ñö) Int.cast_injective
  simp only [map_bind‚ÇÅ, map_frobeniusPoly, bind‚ÇÅ_frobeniusPolyRat_wittPolynomial,
    map_wittPolynomial]

variable {p}

/-- `frobeniusFun` is the function underlying the ring endomorphism
`frobenius : ùïé R ‚Üí+* frobenius ùïé R`. -/
def frobeniusFun (x : ùïé R) : ùïé R :=
  mk p fun n => MvPolynomial.aeval x.coeff (frobeniusPoly p n)

omit hp in
theorem coeff_frobeniusFun (x : ùïé R) (n : ‚Ñï) :
    coeff (frobeniusFun x) n = MvPolynomial.aeval x.coeff (frobeniusPoly p n) := by
  rw [frobeniusFun, coeff_mk]

variable (p) in
/-- `frobeniusFun` is tautologically a polynomial function.

See also `frobenius_isPoly`. -/
instance frobeniusFun_isPoly : IsPoly p fun R _ Rcr => @frobeniusFun p R _ Rcr :=
  ‚ü®‚ü®frobeniusPoly p, by intros; funext n; apply coeff_frobeniusFun‚ü©‚ü©

@[ghost_simps]
theorem ghostComponent_frobeniusFun (n : ‚Ñï) (x : ùïé R) :
    ghostComponent n (frobeniusFun x) = ghostComponent (n + 1) x := by
  simp only [ghostComponent_apply, frobeniusFun, coeff_mk, ‚Üê bind‚ÇÅ_frobeniusPoly_wittPolynomial,
    aeval_bind‚ÇÅ]

/-- If `R` has characteristic `p`, then there is a ring endomorphism
that raises `r : R` to the power `p`.
By applying `WittVector.map` to this endomorphism,
we obtain a ring endomorphism `frobenius R p : ùïé R ‚Üí+* ùïé R`.

The underlying function of this morphism is `WittVector.frobeniusFun`.
-/
def frobenius : ùïé R ‚Üí+* ùïé R where
  toFun := frobeniusFun
  map_zero' := by
    -- Porting note: removing the placeholders give an error
    refine IsPoly.ext (@IsPoly.comp p _ _ (frobeniusFun_isPoly p) WittVector.zeroIsPoly)
      (@IsPoly.comp p _ _ WittVector.zeroIsPoly
      (frobeniusFun_isPoly p)) ?_ _ 0
    simp only [Function.comp_apply, map_zero, forall_const]
    ghost_simp
  map_one' := by
    refine
      -- Porting note: removing the placeholders give an error
      IsPoly.ext (@IsPoly.comp p _ _ (frobeniusFun_isPoly p) WittVector.oneIsPoly)
        (@IsPoly.comp p _ _ WittVector.oneIsPoly (frobeniusFun_isPoly p)) ?_ _ 0
    simp only [Function.comp_apply, map_one, forall_const]
    ghost_simp
  map_add' := by ghost_calc _ _; ghost_simp
  map_mul' := by ghost_calc _ _; ghost_simp

theorem coeff_frobenius (x : ùïé R) (n : ‚Ñï) :
    coeff (frobenius x) n = MvPolynomial.aeval x.coeff (frobeniusPoly p n) :=
  coeff_frobeniusFun _ _

@[ghost_simps]
theorem ghostComponent_frobenius (n : ‚Ñï) (x : ùïé R) :
    ghostComponent n (frobenius x) = ghostComponent (n + 1) x :=
  ghostComponent_frobeniusFun _ _

variable (p)

/-- `frobenius` is tautologically a polynomial function. -/
instance frobenius_isPoly : IsPoly p fun R _Rcr => @frobenius p R _ _Rcr :=
  frobeniusFun_isPoly _

section CharP

variable [CharP R p]

@[simp]
theorem coeff_frobenius_charP (x : ùïé R) (n : ‚Ñï) : coeff (frobenius x) n = x.coeff n ^ p := by
  rw [coeff_frobenius]
  letI : Algebra (ZMod p) R := ZMod.algebra _ _
  -- outline of the calculation, proofs follow below
  calc
    aeval (fun k => x.coeff k) (frobeniusPoly p n) =
        aeval (fun k => x.coeff k)
          (MvPolynomial.map (Int.castRingHom (ZMod p)) (frobeniusPoly p n)) := ?_
    _ = aeval (fun k => x.coeff k) (X n ^ p : MvPolynomial ‚Ñï (ZMod p)) := ?_
    _ = x.coeff n ^ p := ?_
  ¬∑ conv_rhs => rw [aeval_eq_eval‚ÇÇHom, eval‚ÇÇHom_map_hom]
    apply eval‚ÇÇHom_congr (RingHom.ext_int _ _) rfl rfl
  ¬∑ rw [frobeniusPoly_zmod]
  ¬∑ rw [map_pow, aeval_X]

theorem frobenius_eq_map_frobenius : @frobenius p R _ _ = map (_root_.frobenius R p) := by
  ext (x n)
  simp only [coeff_frobenius_charP, map_coeff, frobenius_def]

@[simp]
theorem frobenius_zmodp (x : ùïé (ZMod p)) : frobenius x = x := by
  simp only [WittVector.ext_iff, coeff_frobenius_charP, ZMod.pow_card, eq_self_iff_true,
    forall_const]

variable (R)

/-- `WittVector.frobenius` as an equiv. -/
@[simps -fullyApplied]
def frobeniusEquiv [PerfectRing R p] : WittVector p R ‚âÉ+* WittVector p R :=
  { (WittVector.frobenius : WittVector p R ‚Üí+* WittVector p R) with
    toFun := WittVector.frobenius
    invFun := map (_root_.frobeniusEquiv R p).symm
    left_inv := fun f => ext fun n => by
      rw [frobenius_eq_map_frobenius]
      exact frobeniusEquiv_symm_apply_frobenius R p _
    right_inv := fun f => ext fun n => by
      rw [frobenius_eq_map_frobenius]
      exact frobenius_apply_frobeniusEquiv_symm R p _ }

theorem frobenius_bijective [PerfectRing R p] :
    Function.Bijective (@WittVector.frobenius p R _ _) :=
  (frobeniusEquiv p R).bijective

end CharP

end

end WittVector
