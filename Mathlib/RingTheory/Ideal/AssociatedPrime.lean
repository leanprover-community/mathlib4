/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.LinearAlgebra.Span
import Mathlib.RingTheory.Ideal.Operations
import Mathlib.RingTheory.Ideal.QuotientOperations
import Mathlib.RingTheory.Noetherian

#align_import ring_theory.ideal.associated_prime from "leanprover-community/mathlib"@"f0c8bf9245297a541f468be517f1bde6195105e9"

/-!

# Associated primes of a module

We provide the definition and related lemmas about associated primes of modules.

## Main definition
- `IsAssociatedPrime`: `IsAssociatedPrime I M` if the prime ideal `I` is the
  annihilator of some `x : M`.
- `associatedPrimes`: The set of associated primes of a module.

## Main results
- `exists_le_isAssociatedPrime_of_isNoetherianRing`: In a noetherian ring, any `ann(x)` is
  contained in an associated prime for `x â‰  0`.
- `associatedPrimes.eq_singleton_of_isPrimary`: In a noetherian ring, `I.radical` is the only
  associated prime of `R â§¸ I` when `I` is primary.

## Todo

Generalize this to a non-commutative setting once there are annihilator for non-commutative rings.

-/


variable {R : Type*} [CommRing R] (I J : Ideal R) (M : Type*) [AddCommGroup M] [Module R M]

/-- `IsAssociatedPrime I M` if the prime ideal `I` is the annihilator of some `x : M`. -/
def IsAssociatedPrime : Prop :=
  I.IsPrime âˆ§ âˆƒ x : M, I = (R âˆ™ x).annihilator
#align is_associated_prime IsAssociatedPrime

variable (R)

/-- The set of associated primes of a module. -/
def associatedPrimes : Set (Ideal R) :=
  { I | IsAssociatedPrime I M }
#align associated_primes associatedPrimes

variable {I J M R}

variable {M' : Type*} [AddCommGroup M'] [Module R M'] (f : M â†’â‚—[R] M')

theorem AssociatePrimes.mem_iff : I âˆˆ associatedPrimes R M â†” IsAssociatedPrime I M := Iff.rfl
#align associate_primes.mem_iff AssociatePrimes.mem_iff

theorem IsAssociatedPrime.isPrime (h : IsAssociatedPrime I M) : I.IsPrime := h.1
#align is_associated_prime.is_prime IsAssociatedPrime.isPrime
theorem IsAssociatedPrime.map_of_injective (h : IsAssociatedPrime I M) (hf : Function.Injective f) :
    IsAssociatedPrime I M' := by
  obtain âŸ¨x, rflâŸ© := h.2
  -- âŠ¢ IsAssociatedPrime (Submodule.annihilator (Submodule.span R {x})) M'
  refine' âŸ¨h.1, âŸ¨f x, _âŸ©âŸ©
  -- âŠ¢ Submodule.annihilator (Submodule.span R {x}) = Submodule.annihilator (Submod â€¦
  ext r
  -- âŠ¢ r âˆˆ Submodule.annihilator (Submodule.span R {x}) â†” r âˆˆ Submodule.annihilator â€¦
  rw [Submodule.mem_annihilator_span_singleton, Submodule.mem_annihilator_span_singleton, â†
    map_smul, â† f.map_zero, hf.eq_iff]
#align is_associated_prime.map_of_injective IsAssociatedPrime.map_of_injective

theorem LinearEquiv.isAssociatedPrime_iff (l : M â‰ƒâ‚—[R] M') :
    IsAssociatedPrime I M â†” IsAssociatedPrime I M' :=
  âŸ¨fun h => h.map_of_injective l l.injective,
    fun h => h.map_of_injective l.symm l.symm.injectiveâŸ©
#align linear_equiv.is_associated_prime_iff LinearEquiv.isAssociatedPrime_iff

theorem not_isAssociatedPrime_of_subsingleton [Subsingleton M] : Â¬IsAssociatedPrime I M := by
  rintro âŸ¨hI, x, hxâŸ©
  -- âŠ¢ False
  apply hI.ne_top
  -- âŠ¢ I = âŠ¤
  rwa [Subsingleton.elim x 0, Submodule.span_singleton_eq_bot.mpr rfl, Submodule.annihilator_bot]
    at hx
#align not_is_associated_prime_of_subsingleton not_isAssociatedPrime_of_subsingleton

variable (R)

theorem exists_le_isAssociatedPrime_of_isNoetherianRing [H : IsNoetherianRing R] (x : M)
    (hx : x â‰  0) : âˆƒ P : Ideal R, IsAssociatedPrime P M âˆ§ (R âˆ™ x).annihilator â‰¤ P := by
  have : (R âˆ™ x).annihilator â‰  âŠ¤ := by
    rwa [Ne.def, Ideal.eq_top_iff_one, Submodule.mem_annihilator_span_singleton, one_smul]
  obtain âŸ¨P, âŸ¨l, hâ‚, y, rflâŸ©, hâ‚ƒâŸ© :=
    set_has_maximal_iff_noetherian.mpr H
      { P | (R âˆ™ x).annihilator â‰¤ P âˆ§ P â‰  âŠ¤ âˆ§ âˆƒ y : M, P = (R âˆ™ y).annihilator }
      âŸ¨(R âˆ™ x).annihilator, rfl.le, this, x, rflâŸ©
  refine' âŸ¨_, âŸ¨âŸ¨hâ‚, _âŸ©, y, rflâŸ©, lâŸ©
  -- âŠ¢ âˆ€ {x y_1 : R}, x * y_1 âˆˆ Submodule.annihilator (Submodule.span R {y}) â†’ x âˆˆ  â€¦
  intro a b hab
  -- âŠ¢ a âˆˆ Submodule.annihilator (Submodule.span R {y}) âˆ¨ b âˆˆ Submodule.annihilator â€¦
  rw [or_iff_not_imp_left]
  -- âŠ¢ Â¬a âˆˆ Submodule.annihilator (Submodule.span R {y}) â†’ b âˆˆ Submodule.annihilato â€¦
  intro ha
  -- âŠ¢ b âˆˆ Submodule.annihilator (Submodule.span R {y})
  rw [Submodule.mem_annihilator_span_singleton] at ha hab
  -- âŠ¢ b âˆˆ Submodule.annihilator (Submodule.span R {y})
  have Hâ‚ : (R âˆ™ y).annihilator â‰¤ (R âˆ™ a â€¢ y).annihilator := by
    intro c hc
    rw [Submodule.mem_annihilator_span_singleton] at hc âŠ¢
    rw [smul_comm, hc, smul_zero]
  have Hâ‚‚ : (Submodule.span R {a â€¢ y}).annihilator â‰  âŠ¤ := by
    rwa [Ne.def, Submodule.annihilator_eq_top_iff, Submodule.span_singleton_eq_bot]
  rwa [Hâ‚.eq_of_not_lt (hâ‚ƒ (R âˆ™ a â€¢ y).annihilator âŸ¨l.trans Hâ‚, Hâ‚‚, _, rflâŸ©),
    Submodule.mem_annihilator_span_singleton, smul_comm, smul_smul]
#align exists_le_is_associated_prime_of_is_noetherian_ring exists_le_isAssociatedPrime_of_isNoetherianRing

variable {R}

theorem associatedPrimes.subset_of_injective (hf : Function.Injective f) :
    associatedPrimes R M âŠ† associatedPrimes R M' := fun _I h => h.map_of_injective f hf
#align associated_primes.subset_of_injective associatedPrimes.subset_of_injective

theorem LinearEquiv.AssociatedPrimes.eq (l : M â‰ƒâ‚—[R] M') :
    associatedPrimes R M = associatedPrimes R M' :=
  le_antisymm (associatedPrimes.subset_of_injective l l.injective)
    (associatedPrimes.subset_of_injective l.symm l.symm.injective)
#align linear_equiv.associated_primes.eq LinearEquiv.AssociatedPrimes.eq

theorem associatedPrimes.eq_empty_of_subsingleton [Subsingleton M] : associatedPrimes R M = âˆ… := by
  ext; simp only [Set.mem_empty_iff_false, iff_false_iff];
  -- âŠ¢ xâœ âˆˆ associatedPrimes R M â†” xâœ âˆˆ âˆ…
       -- âŠ¢ Â¬xâœ âˆˆ associatedPrimes R M
  apply not_isAssociatedPrime_of_subsingleton
  -- ğŸ‰ no goals
#align associated_primes.eq_empty_of_subsingleton associatedPrimes.eq_empty_of_subsingleton

variable (R M)

theorem associatedPrimes.nonempty [IsNoetherianRing R] [Nontrivial M] :
    (associatedPrimes R M).Nonempty := by
  obtain âŸ¨x, hxâŸ© := exists_ne (0 : M)
  -- âŠ¢ Set.Nonempty (associatedPrimes R M)
  obtain âŸ¨P, hP, _âŸ© := exists_le_isAssociatedPrime_of_isNoetherianRing R x hx
  -- âŠ¢ Set.Nonempty (associatedPrimes R M)
  exact âŸ¨P, hPâŸ©
  -- ğŸ‰ no goals
#align associated_primes.nonempty associatedPrimes.nonempty

variable {R M}

theorem IsAssociatedPrime.annihilator_le (h : IsAssociatedPrime I M) :
    (âŠ¤ : Submodule R M).annihilator â‰¤ I := by
  obtain âŸ¨hI, x, rflâŸ© := h
  -- âŠ¢ Submodule.annihilator âŠ¤ â‰¤ Submodule.annihilator (Submodule.span R {x})
  exact Submodule.annihilator_mono le_top
  -- ğŸ‰ no goals
#align is_associated_prime.annihilator_le IsAssociatedPrime.annihilator_le

theorem IsAssociatedPrime.eq_radical (hI : I.IsPrimary) (h : IsAssociatedPrime J (R â§¸ I)) :
    J = I.radical := by
  obtain âŸ¨hJ, x, eâŸ© := h
  -- âŠ¢ J = Ideal.radical I
  have : x â‰  0 := by
    rintro rfl
    apply hJ.1
    rwa [Submodule.span_singleton_eq_bot.mpr rfl, Submodule.annihilator_bot] at e
  obtain âŸ¨x, rflâŸ© := Ideal.Quotient.mkâ‚_surjective R _ x
  -- âŠ¢ J = Ideal.radical I
  replace e : âˆ€ {y}, y âˆˆ J â†” x * y âˆˆ I
  -- âŠ¢ âˆ€ {y : R}, y âˆˆ J â†” x * y âˆˆ I
  Â· intro y
    -- âŠ¢ y âˆˆ J â†” x * y âˆˆ I
    rw [e, Submodule.mem_annihilator_span_singleton, â† map_smul, smul_eq_mul, mul_comm,
      Ideal.Quotient.mkâ‚_eq_mk, â† Ideal.Quotient.mk_eq_mk, Submodule.Quotient.mk_eq_zero]
  apply le_antisymm
  -- âŠ¢ J â‰¤ Ideal.radical I
  Â· intro y hy
    -- âŠ¢ y âˆˆ Ideal.radical I
    exact (hI.2 <| e.mp hy).resolve_left ((Submodule.Quotient.mk_eq_zero I).not.mp this)
    -- ğŸ‰ no goals
  Â· rw [hJ.radical_le_iff]
    -- âŠ¢ I â‰¤ J
    intro y hy
    -- âŠ¢ y âˆˆ J
    exact e.mpr (I.mul_mem_left x hy)
    -- ğŸ‰ no goals
#align is_associated_prime.eq_radical IsAssociatedPrime.eq_radical

theorem associatedPrimes.eq_singleton_of_isPrimary [IsNoetherianRing R] (hI : I.IsPrimary) :
    associatedPrimes R (R â§¸ I) = {I.radical} := by
  ext J
  -- âŠ¢ J âˆˆ associatedPrimes R (R â§¸ I) â†” J âˆˆ {Ideal.radical I}
  rw [Set.mem_singleton_iff]
  -- âŠ¢ J âˆˆ associatedPrimes R (R â§¸ I) â†” J = Ideal.radical I
  refine' âŸ¨IsAssociatedPrime.eq_radical hI, _âŸ©
  -- âŠ¢ J = Ideal.radical I â†’ J âˆˆ associatedPrimes R (R â§¸ I)
  rintro rfl
  -- âŠ¢ Ideal.radical I âˆˆ associatedPrimes R (R â§¸ I)
  haveI : Nontrivial (R â§¸ I) := by
    refine âŸ¨(Ideal.Quotient.mk I : _) 1, (Ideal.Quotient.mk I : _) 0, ?_âŸ©
    rw [Ne.def, Ideal.Quotient.eq, sub_zero, â† Ideal.eq_top_iff_one]
    exact hI.1
  obtain âŸ¨a, haâŸ© := associatedPrimes.nonempty R (R â§¸ I)
  -- âŠ¢ Ideal.radical I âˆˆ associatedPrimes R (R â§¸ I)
  exact ha.eq_radical hI â–¸ ha
  -- ğŸ‰ no goals
#align associated_primes.eq_singleton_of_is_primary associatedPrimes.eq_singleton_of_isPrimary
