/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.RingTheory.Ideal.Operations
import Mathlib.Algebra.Module.Torsion
import Mathlib.Algebra.Ring.Idempotents
import Mathlib.LinearAlgebra.FiniteDimensional
import Mathlib.RingTheory.Ideal.LocalRing

#align_import ring_theory.ideal.cotangent from "leanprover-community/mathlib"@"4b92a463033b5587bb011657e25e4710bfca7364"

/-!
# The module `I â§¸ I ^ 2`

In this file, we provide special API support for the module `I â§¸ I ^ 2`. The official
definition is a quotient module of `I`, but the alternative definition as an ideal of `R â§¸ I ^ 2` is
also given, and the two are `R`-equivalent as in `Ideal.cotangentEquivIdeal`.

Additional support is also given to the cotangent space `m â§¸ m ^ 2` of a local ring.

-/


namespace Ideal

-- Porting note: universes need to be explicit to avoid bad universe levels in `quotCotangent`
universe u v w

variable {R : Type u} {S : Type v} {S' : Type w} [CommRing R] [CommSemiring S] [Algebra S R]

variable [CommSemiring S'] [Algebra S' R] [Algebra S S'] [IsScalarTower S S' R] (I : Ideal R)

-- Porting note: instances that were derived automatically need to be proved by hand (see below)
/-- `I â§¸ I ^ 2` as a quotient of `I`. -/
def Cotangent : Type _ := I â§¸ (I â€¢ âŠ¤ : Submodule R I)
#align ideal.cotangent Ideal.Cotangent

instance : AddCommGroup I.Cotangent := by delta Cotangent; infer_instance
                                          -- âŠ¢ AddCommGroup ({ x // x âˆˆ I } â§¸ I â€¢ âŠ¤)
                                                           -- ğŸ‰ no goals

instance cotangentModule : Module (R â§¸ I) I.Cotangent := by delta Cotangent; infer_instance
                                                            -- âŠ¢ Module (R â§¸ I) ({ x // x âˆˆ I } â§¸ I â€¢ âŠ¤)
                                                                             -- ğŸ‰ no goals

instance : Inhabited I.Cotangent := âŸ¨0âŸ©

instance Cotangent.moduleOfTower : Module S I.Cotangent :=
  Submodule.Quotient.module' _
#align ideal.cotangent.module_of_tower Ideal.Cotangent.moduleOfTower

instance Cotangent.isScalarTower : IsScalarTower S S' I.Cotangent :=
  Submodule.Quotient.isScalarTower _ _
#align ideal.cotangent.is_scalar_tower Ideal.Cotangent.isScalarTower

instance [IsNoetherian R I] : IsNoetherian R I.Cotangent :=
  Submodule.Quotient.isNoetherian _

/-- The quotient map from `I` to `I â§¸ I ^ 2`. -/
@[simps!] --  (config := lemmasOnly) apply -- Porting note: this option does not exist anymore
def toCotangent : I â†’â‚—[R] I.Cotangent := Submodule.mkQ _
#align ideal.to_cotangent Ideal.toCotangent

theorem map_toCotangent_ker : I.toCotangent.ker.map I.subtype = I ^ 2 := by
  rw [Ideal.toCotangent, Submodule.ker_mkQ, pow_two, Submodule.map_smul'' I âŠ¤ (Submodule.subtype I),
    Algebra.id.smul_eq_mul, Submodule.map_subtype_top]

#align ideal.map_to_cotangent_ker Ideal.map_toCotangent_ker

theorem mem_toCotangent_ker {x : I} : x âˆˆ LinearMap.ker I.toCotangent â†” (x : R) âˆˆ I ^ 2 := by
  rw [â† I.map_toCotangent_ker]
  -- âŠ¢ x âˆˆ LinearMap.ker (toCotangent I) â†” â†‘x âˆˆ Submodule.map (Submodule.subtype I) â€¦
  simp
  -- ğŸ‰ no goals
#align ideal.mem_to_cotangent_ker Ideal.mem_toCotangent_ker

theorem toCotangent_eq {x y : I} : I.toCotangent x = I.toCotangent y â†” (x - y : R) âˆˆ I ^ 2 := by
  rw [â† sub_eq_zero]
  -- âŠ¢ â†‘(toCotangent I) x - â†‘(toCotangent I) y = 0 â†” â†‘x - â†‘y âˆˆ I ^ 2
  exact I.mem_toCotangent_ker
  -- ğŸ‰ no goals
#align ideal.to_cotangent_eq Ideal.toCotangent_eq

theorem toCotangent_eq_zero (x : I) : I.toCotangent x = 0 â†” (x : R) âˆˆ I ^ 2 := I.mem_toCotangent_ker
#align ideal.to_cotangent_eq_zero Ideal.toCotangent_eq_zero

theorem toCotangent_surjective : Function.Surjective I.toCotangent := Submodule.mkQ_surjective _
#align ideal.to_cotangent_surjective Ideal.toCotangent_surjective

theorem toCotangent_range : LinearMap.range I.toCotangent = âŠ¤ := Submodule.range_mkQ _
#align ideal.to_cotangent_range Ideal.toCotangent_range

theorem cotangent_subsingleton_iff : Subsingleton I.Cotangent â†” IsIdempotentElem I := by
  constructor
  -- âŠ¢ Subsingleton (Cotangent I) â†’ IsIdempotentElem I
  Â· intro H
    -- âŠ¢ IsIdempotentElem I
    refine' (pow_two I).symm.trans (le_antisymm (Ideal.pow_le_self two_ne_zero) _)
    -- âŠ¢ I â‰¤ I ^ 2
    exact fun x hx => (I.toCotangent_eq_zero âŸ¨x, hxâŸ©).mp (Subsingleton.elim _ _)
    -- ğŸ‰ no goals
  Â· exact fun e =>
      âŸ¨fun x y =>
        Quotient.inductionOnâ‚‚' x y fun x y =>
          I.toCotangent_eq.mpr <| ((pow_two I).trans e).symm â–¸ I.sub_mem x.prop y.propâŸ©
#align ideal.cotangent_subsingleton_iff Ideal.cotangent_subsingleton_iff

/-- The inclusion map `I â§¸ I ^ 2` to `R â§¸ I ^ 2`. -/
def cotangentToQuotientSquare : I.Cotangent â†’â‚—[R] R â§¸ I ^ 2 :=
  Submodule.mapQ (I â€¢ âŠ¤) (I ^ 2) I.subtype
    (by
      rw [â† Submodule.map_le_iff_le_comap, Submodule.map_smul'', Submodule.map_top,
        Submodule.range_subtype, smul_eq_mul, pow_two] )
#align ideal.cotangent_to_quotient_square Ideal.cotangentToQuotientSquare

theorem to_quotient_square_comp_toCotangent :
    I.cotangentToQuotientSquare.comp I.toCotangent = (I ^ 2).mkQ.comp (Submodule.subtype I) :=
  LinearMap.ext fun _ => rfl
#align ideal.to_quotient_square_comp_to_cotangent Ideal.to_quotient_square_comp_toCotangent

-- @[simp] -- Porting note: not in simpNF
theorem toCotangent_to_quotient_square (x : I) :
    I.cotangentToQuotientSquare (I.toCotangent x) = (I ^ 2).mkQ x := rfl
#align ideal.to_cotangent_to_quotient_square Ideal.toCotangent_to_quotient_square

/-- `I â§¸ I ^ 2` as an ideal of `R â§¸ I ^ 2`. -/
def cotangentIdeal (I : Ideal R) : Ideal (R â§¸ I ^ 2) :=
  Submodule.map (Quotient.mk (I ^ 2)|>.toSemilinearMap) I
#align ideal.cotangent_ideal Ideal.cotangentIdeal

theorem cotangentIdeal_square (I : Ideal R) : I.cotangentIdeal ^ 2 = âŠ¥ := by
  rw [eq_bot_iff, pow_two I.cotangentIdeal, â† smul_eq_mul]
  -- âŠ¢ cotangentIdeal I â€¢ cotangentIdeal I â‰¤ âŠ¥
  intro x hx
  -- âŠ¢ x âˆˆ âŠ¥
  refine Submodule.smul_induction_on hx ?_ ?_
  -- âŠ¢ âˆ€ (r : R â§¸ I ^ 2), r âˆˆ cotangentIdeal I â†’ âˆ€ (n : R â§¸ I ^ 2), n âˆˆ cotangentId â€¦
  Â· rintro _ âŸ¨x, hx, rflâŸ© _ âŸ¨y, hy, rflâŸ©; apply (Submodule.Quotient.eq _).mpr _
    -- âŠ¢ â†‘(RingHom.toSemilinearMap (Quotient.mk (I ^ 2))) x â€¢ â†‘(RingHom.toSemilinearM â€¦
                                          -- âŠ¢ (fun x x_1 => x * x_1) x y - 0 âˆˆ I ^ 2
    rw [sub_zero, pow_two]; exact Ideal.mul_mem_mul hx hy
    -- âŠ¢ (fun x x_1 => x * x_1) x y âˆˆ I * I
                            -- ğŸ‰ no goals
  Â· intro x y hx hy; exact add_mem hx hy
    -- âŠ¢ x + y âˆˆ âŠ¥
                     -- ğŸ‰ no goals
#align ideal.cotangent_ideal_square Ideal.cotangentIdeal_square

theorem to_quotient_square_range :
    LinearMap.range I.cotangentToQuotientSquare = I.cotangentIdeal.restrictScalars R := by
  trans LinearMap.range (I.cotangentToQuotientSquare.comp I.toCotangent)
  -- âŠ¢ LinearMap.range (cotangentToQuotientSquare I) = LinearMap.range (LinearMap.c â€¦
  Â· rw [LinearMap.range_comp, I.toCotangent_range, Submodule.map_top]
    -- ğŸ‰ no goals
  Â· rw [to_quotient_square_comp_toCotangent, LinearMap.range_comp, I.range_subtype]; ext; rfl
    -- âŠ¢ Submodule.map (Submodule.mkQ (I ^ 2)) I = Submodule.restrictScalars R (cotan â€¦
                                                                                     -- âŠ¢ xâœ âˆˆ Submodule.map (Submodule.mkQ (I ^ 2)) I â†” xâœ âˆˆ Submodule.restrictScalar â€¦
                                                                                          -- ğŸ‰ no goals
#align ideal.to_quotient_square_range Ideal.to_quotient_square_range

/-- The equivalence of the two definitions of `I / I ^ 2`, either as the quotient of `I` or the
ideal of `R / I ^ 2`. -/
noncomputable def cotangentEquivIdeal : I.Cotangent â‰ƒâ‚—[R] I.cotangentIdeal := by
  refine
  { LinearMap.codRestrict (I.cotangentIdeal.restrictScalars R) I.cotangentToQuotientSquare
      fun x => by { rw [â† to_quotient_square_range]; exact LinearMap.mem_range_self _ _ },
    Equiv.ofBijective _ âŸ¨?_, ?_âŸ© with }
  Â· rintro x y e
    -- âŠ¢ x = y
    replace e := congr_arg Subtype.val e
    -- âŠ¢ x = y
    obtain âŸ¨x, rflâŸ© := I.toCotangent_surjective x
    -- âŠ¢ â†‘(toCotangent I) x = y
    obtain âŸ¨y, rflâŸ© := I.toCotangent_surjective y
    -- âŠ¢ â†‘(toCotangent I) x = â†‘(toCotangent I) y
    rw [I.toCotangent_eq]
    -- âŠ¢ â†‘x - â†‘y âˆˆ I ^ 2
    dsimp only [toCotangent_to_quotient_square, Submodule.mkQ_apply] at e
    -- âŠ¢ â†‘x - â†‘y âˆˆ I ^ 2
    rwa [Submodule.Quotient.eq] at e
    -- ğŸ‰ no goals
  Â· rintro âŸ¨_, x, hx, rflâŸ©
    -- âŠ¢ âˆƒ a, (fun c => { val := â†‘(cotangentToQuotientSquare I) c, property := (_ : â†‘ â€¦
    exact âŸ¨I.toCotangent âŸ¨x, hxâŸ©, Subtype.ext rflâŸ©
    -- ğŸ‰ no goals
#align ideal.cotangent_equiv_ideal Ideal.cotangentEquivIdeal

@[simp]
theorem cotangentEquivIdeal_apply (x : I.Cotangent) :
    â†‘(I.cotangentEquivIdeal x) = I.cotangentToQuotientSquare x := rfl
#align ideal.cotangent_equiv_ideal_apply Ideal.cotangentEquivIdeal_apply

theorem cotangentEquivIdeal_symm_apply (x : R) (hx : x âˆˆ I) :
    I.cotangentEquivIdeal.symm âŸ¨(I ^ 2).mkQ x, Submodule.mem_map_of_mem hxâŸ© =
      I.toCotangent âŸ¨x, hxâŸ© := by
  apply I.cotangentEquivIdeal.injective
  -- âŠ¢ â†‘(cotangentEquivIdeal I) (â†‘(LinearEquiv.symm (cotangentEquivIdeal I)) { val  â€¦
  rw [I.cotangentEquivIdeal.apply_symm_apply]
  -- âŠ¢ { val := â†‘(Submodule.mkQ (I ^ 2)) x, property := (_ : â†‘(Submodule.mkQ (I ^ 2 â€¦
  ext
  -- âŠ¢ â†‘{ val := â†‘(Submodule.mkQ (I ^ 2)) x, property := (_ : â†‘(Submodule.mkQ (I ^  â€¦
  rfl
  -- ğŸ‰ no goals
#align ideal.cotangent_equiv_ideal_symm_apply Ideal.cotangentEquivIdeal_symm_apply

variable {A B : Type*} [CommRing A] [CommRing B] [Algebra R A] [Algebra R B]

/-- The lift of `f : A â†’â‚[R] B` to `A â§¸ J ^ 2 â†’â‚[R] B` with `J` being the kernel of `f`. -/
def _root_.AlgHom.kerSquareLift (f : A â†’â‚[R] B) : A â§¸ RingHom.ker f.toRingHom ^ 2 â†’â‚[R] B := by
  refine { Ideal.Quotient.lift (RingHom.ker f.toRingHom ^ 2) f.toRingHom ?_ with commutes' := ?_ }
  -- âŠ¢ âˆ€ (a : A), a âˆˆ RingHom.ker â†‘f ^ 2 â†’ â†‘â†‘f a = 0
  Â· intro a ha; exact Ideal.pow_le_self two_ne_zero ha
    -- âŠ¢ â†‘â†‘f a = 0
                -- ğŸ‰ no goals
  Â· intro r
    -- âŠ¢ OneHom.toFun (â†‘â†‘{ toMonoidHom := â†‘srcâœ, map_zero' := (_ : OneHom.toFun (â†‘â†‘sr â€¦
    rw [IsScalarTower.algebraMap_apply R A, RingHom.toFun_eq_coe, Ideal.Quotient.algebraMap_eq,
      Ideal.Quotient.lift_mk]
    exact f.map_algebraMap r
    -- ğŸ‰ no goals
#align alg_hom.ker_square_lift AlgHom.kerSquareLift

theorem _root_.AlgHom.ker_kerSquareLift (f : A â†’â‚[R] B) :
    RingHom.ker f.kerSquareLift.toRingHom = f.toRingHom.ker.cotangentIdeal := by
  apply le_antisymm
  -- âŠ¢ RingHom.ker â†‘(AlgHom.kerSquareLift f) â‰¤ cotangentIdeal (RingHom.ker â†‘f)
  Â· intro x hx; obtain âŸ¨x, rflâŸ© := Ideal.Quotient.mk_surjective x; exact âŸ¨x, hx, rflâŸ©
    -- âŠ¢ x âˆˆ cotangentIdeal (RingHom.ker â†‘f)
                -- âŠ¢ â†‘(Quotient.mk (RingHom.ker â†‘f ^ 2)) x âˆˆ cotangentIdeal (RingHom.ker â†‘f)
                                                                   -- ğŸ‰ no goals
  Â· rintro _ âŸ¨x, hx, rflâŸ©; exact hx
    -- âŠ¢ â†‘(RingHom.toSemilinearMap (Quotient.mk (RingHom.ker â†‘f ^ 2))) x âˆˆ RingHom.ke â€¦
                           -- ğŸ‰ no goals
#align alg_hom.ker_ker_sqare_lift AlgHom.ker_kerSquareLift

/-- The quotient ring of `I â§¸ I ^ 2` is `R â§¸ I`. -/
def quotCotangent : (R â§¸ I ^ 2) â§¸ I.cotangentIdeal â‰ƒ+* R â§¸ I := by
  refine (Ideal.quotEquivOfEq (Ideal.map_eq_submodule_map _ _).symm).trans ?_
  -- âŠ¢ (R â§¸ I ^ 2) â§¸ map (Quotient.mk (I ^ 2)) I â‰ƒ+* R â§¸ I
  refine (DoubleQuot.quotQuotEquivQuotSup _ _).trans ?_
  -- âŠ¢ R â§¸ I ^ 2 âŠ” I â‰ƒ+* R â§¸ I
  exact Ideal.quotEquivOfEq (sup_eq_right.mpr <| Ideal.pow_le_self two_ne_zero)
  -- ğŸ‰ no goals
#align ideal.quot_cotangent Ideal.quotCotangent

end Ideal

namespace LocalRing

variable (R : Type*) [CommRing R] [LocalRing R]

/-- The `A â§¸ I`-vector space `I â§¸ I ^ 2`. -/
@[reducible]
def CotangentSpace : Type _ := (maximalIdeal R).Cotangent
#align local_ring.cotangent_space LocalRing.CotangentSpace

instance : Module (ResidueField R) (CotangentSpace R) := Ideal.cotangentModule _

instance : IsScalarTower R (ResidueField R) (CotangentSpace R) :=
  Module.IsTorsionBySet.isScalarTower _

instance [IsNoetherianRing R] : FiniteDimensional (ResidueField R) (CotangentSpace R) :=
  Module.Finite.of_restrictScalars_finite R _ _

end LocalRing
