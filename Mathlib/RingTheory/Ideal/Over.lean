/-
Copyright (c) 2020 Anne Baanen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anne Baanen, Yongle Hu
-/
import Mathlib.Algebra.Group.Subgroup.Actions
import Mathlib.RingTheory.FiniteType
import Mathlib.RingTheory.Ideal.Pointwise

/-!
# Ideals over/under ideals

This file concerns ideals lying over other ideals.
Let `f : R â†’+* S` be a ring homomorphism (typically a ring extension), `I` an ideal of `R` and
`J` an ideal of `S`. We say `J` lies over `I` (and `I` under `J`) if `I` is the `f`-preimage of `J`.
This is expressed here by writing `I = J.comap f`.
-/

-- for going-up results about integral extensions, see `Mathlib.RingTheory.Ideal.GoingUp`
assert_not_exists Algebra.IsIntegral

variable {R : Type*} [CommRing R]

namespace Ideal

open Polynomial Submodule

open scoped Pointwise

section CommRing

variable {S : Type*} [CommRing S] {f : R â†’+* S} {I J : Ideal S}

variable {p : Ideal R} {P : Ideal S}

/-- If there is an injective map `R/p â†’ S/P` such that following diagram commutes:
```
R   â†’ S
â†“     â†“
R/p â†’ S/P
```
then `P` lies over `p`.
-/
theorem comap_eq_of_scalar_tower_quotient [Algebra R S] [Algebra (R â§¸ p) (S â§¸ P)]
    [IsScalarTower R (R â§¸ p) (S â§¸ P)] (h : Function.Injective (algebraMap (R â§¸ p) (S â§¸ P))) :
    comap (algebraMap R S) P = p := by
  ext x
  rw [mem_comap, â† Quotient.eq_zero_iff_mem, â† Quotient.eq_zero_iff_mem, Quotient.mk_algebraMap,
    IsScalarTower.algebraMap_apply R (R â§¸ p) (S â§¸ P), Quotient.algebraMap_eq]
  constructor
  Â· intro hx
    exact (injective_iff_map_eq_zero (algebraMap (R â§¸ p) (S â§¸ P))).mp h _ hx
  Â· intro hx
    rw [hx, RingHom.map_zero]

variable [Algebra R S]

/-- `R / p` has a canonical map to `S / pS`. -/
instance Quotient.algebraQuotientMapQuotient : Algebra (R â§¸ p) (S â§¸ map (algebraMap R S) p) :=
  Ideal.Quotient.algebraQuotientOfLEComap le_comap_map

@[simp]
theorem Quotient.algebraMap_quotient_map_quotient (x : R) :
    letI f := algebraMap R S
    algebraMap (R â§¸ p) (S â§¸ map f p) (Ideal.Quotient.mk p x) =
    Ideal.Quotient.mk (map f p) (f x) :=
  rfl

@[simp]
theorem Quotient.mk_smul_mk_quotient_map_quotient (x : R) (y : S) :
    letI f := algebraMap R S
    Quotient.mk p x â€¢ Quotient.mk (map f p) y = Quotient.mk (map f p) (f x * y) :=
  Algebra.smul_def _ _

instance Quotient.tower_quotient_map_quotient [Algebra R S] :
    IsScalarTower R (R â§¸ p) (S â§¸ map (algebraMap R S) p) :=
  IsScalarTower.of_algebraMap_eq fun x => by
    rw [Quotient.algebraMap_eq, Quotient.algebraMap_quotient_map_quotient,
      Quotient.mk_algebraMap]

instance QuotientMapQuotient.isNoetherian [Algebra R S] [IsNoetherian R S] (I : Ideal R) :
    IsNoetherian (R â§¸ I) (S â§¸ I.map (algebraMap R S)) :=
  isNoetherian_of_tower R <|
    isNoetherian_of_surjective S (Ideal.Quotient.mkâ‚ R _).toLinearMap <|
      LinearMap.range_eq_top.mpr Ideal.Quotient.mk_surjective

end CommRing

section ideal_liesOver

section Semiring

variable (A : Type*) [CommSemiring A] {B C : Type*} [Semiring B] [Semiring C] [Algebra A B]
  [Algebra A C] (P : Ideal B) {Q : Ideal C} (p : Ideal A)

/-- The ideal obtained by pulling back the ideal `P` from `B` to `A`. -/
abbrev under : Ideal A := Ideal.comap (algebraMap A B) P

theorem under_def : P.under A = Ideal.comap (algebraMap A B) P := rfl

instance IsPrime.under [hP : P.IsPrime] : (P.under A).IsPrime :=
  hP.comap (algebraMap A B)

@[simp]
lemma under_smul {G : Type*} [Group G] [MulSemiringAction G B] [SMulCommClass G A B] (g : G) :
    (g â€¢ P : Ideal B).under A = P.under A := by
  ext a
  rw [mem_comap, mem_comap, mem_pointwise_smul_iff_inv_smul_mem, smul_algebraMap]

variable (B) in
theorem under_top : under A (âŠ¤ : Ideal B) = âŠ¤ := comap_top

variable {A}

/-- `P` lies over `p` if `p` is the preimage of `P` of the `algebraMap`. -/
class LiesOver : Prop where
  over : p = P.under A

instance over_under : P.LiesOver (P.under A) where over := rfl

theorem over_def [P.LiesOver p] : p = P.under A := LiesOver.over

theorem mem_of_liesOver [P.LiesOver p] (x : A) : x âˆˆ p â†” algebraMap A B x âˆˆ P := by
  rw [P.over_def p]
  rfl

variable (A B) in
instance top_liesOver_top : (âŠ¤ : Ideal B).LiesOver (âŠ¤ : Ideal A) where
  over := (under_top A B).symm

theorem eq_top_iff_of_liesOver [P.LiesOver p] : P = âŠ¤ â†” p = âŠ¤ := by
  rw [P.over_def p]
  exact comap_eq_top_iff.symm

variable {P}

theorem LiesOver.of_eq_comap [Q.LiesOver p] {F : Type*} [FunLike F B C]
    [AlgHomClass F A B C] (f : F) (h : P = Q.comap f) : P.LiesOver p where
  over := by
    rw [h]
    exact (over_def Q p).trans <|
      congrFun (congrFun (congrArg comap ((f : B â†’â‚[A] C).comp_algebraMap.symm)) _) Q

theorem LiesOver.of_eq_map_equiv [P.LiesOver p] {E : Type*} [EquivLike E B C]
    [AlgEquivClass E A B C] (Ïƒ : E) (h : Q = P.map Ïƒ) : Q.LiesOver p := by
  rw [â† show _ = P.map Ïƒ from comap_symm (Ïƒ : B â‰ƒ+* C)] at h
  exact of_eq_comap p (Ïƒ : B â‰ƒâ‚[A] C).symm h

variable (P) (Q)

instance comap_liesOver [Q.LiesOver p] {F : Type*} [FunLike F B C] [AlgHomClass F A B C]
    (f : F) : (Q.comap f).LiesOver p :=
  LiesOver.of_eq_comap p f rfl

instance map_equiv_liesOver [P.LiesOver p] {E : Type*} [EquivLike E B C] [AlgEquivClass E A B C]
    (Ïƒ : E) : (P.map Ïƒ).LiesOver p :=
  LiesOver.of_eq_map_equiv p Ïƒ rfl

end Semiring

section CommSemiring

variable {A : Type*} [CommSemiring A] {B : Type*} [CommSemiring B] {C : Type*} [Semiring C]
  [Algebra A B] [Algebra B C] [Algebra A C] [IsScalarTower A B C]
  (ð”“ : Ideal C) (P : Ideal B) (p : Ideal A)

@[simp]
theorem under_under : (ð”“.under B).under A  = ð”“.under A := by
  simp_rw [comap_comap, â† IsScalarTower.algebraMap_eq]

theorem LiesOver.trans [ð”“.LiesOver P] [P.LiesOver p] : ð”“.LiesOver p where
  over := by rw [P.over_def p, ð”“.over_def P, under_under]

theorem LiesOver.tower_bot [hp : ð”“.LiesOver p] [hP : ð”“.LiesOver P] : P.LiesOver p where
  over := by rw [ð”“.over_def p, ð”“.over_def P, under_under]

variable (B)

instance under_liesOver_of_liesOver [ð”“.LiesOver p] : (ð”“.under B).LiesOver p :=
  LiesOver.tower_bot ð”“ (ð”“.under B) p

end CommSemiring

section CommRing

variable (A : Type*) [CommRing A] (B : Type*) [Ring B] [Nontrivial B]
  [Algebra A B] [NoZeroSMulDivisors A B] {p : Ideal A}

@[simp]
theorem under_bot : under A (âŠ¥ : Ideal B) = âŠ¥ :=
  comap_bot_of_injective (algebraMap A B) (FaithfulSMul.algebraMap_injective A B)

instance bot_liesOver_bot : (âŠ¥ : Ideal B).LiesOver (âŠ¥ : Ideal A) where
  over := (under_bot A B).symm

variable {A B} in
theorem ne_bot_of_liesOver_of_ne_bot (hp : p â‰  âŠ¥) (P : Ideal B) [P.LiesOver p] : P â‰  âŠ¥ := by
  contrapose! hp
  rw [over_def P p, hp, under_bot]

end CommRing

namespace Quotient

variable (R : Type*) [CommSemiring R] {A B C : Type*} [CommRing A] [CommRing B] [CommRing C]
  [Algebra A B] [Algebra A C] [Algebra R A] [Algebra R B] [IsScalarTower R A B]
  (P : Ideal B) {Q : Ideal C} (p : Ideal A) [Q.LiesOver p] [P.LiesOver p]
  (G : Type*) [Group G] [MulSemiringAction G B] [SMulCommClass G A B]

/-- If `P` lies over `p`, then canonically `B â§¸ P` is a `A â§¸ p`-algebra. -/
instance algebraOfLiesOver : Algebra (A â§¸ p) (B â§¸ P) :=
  algebraQuotientOfLEComap (le_of_eq (P.over_def p))

instance isScalarTower_of_liesOver : IsScalarTower R (A â§¸ p) (B â§¸ P) :=
  IsScalarTower.of_algebraMap_eq' <|
    congrArg (algebraMap B (B â§¸ P)).comp (IsScalarTower.algebraMap_eq R A B)

/-- `B â§¸ P` is a finite `A â§¸ p`-module if `B` is a finite `A`-module. -/
instance module_finite_of_liesOver [Module.Finite A B] : Module.Finite (A â§¸ p) (B â§¸ P) :=
  Module.Finite.of_restrictScalars_finite A (A â§¸ p) (B â§¸ P)

example [Module.Finite A B] : Module.Finite (A â§¸ P.under A) (B â§¸ P) := inferInstance

/-- `B â§¸ P` is a finitely generated `A â§¸ p`-algebra if `B` is a finitely generated `A`-algebra. -/
instance algebra_finiteType_of_liesOver [Algebra.FiniteType A B] :
    Algebra.FiniteType (A â§¸ p) (B â§¸ P) :=
  Algebra.FiniteType.of_restrictScalars_finiteType A (A â§¸ p) (B â§¸ P)

/-- `B â§¸ P` is a Noetherian `A â§¸ p`-module if `B` is a Noetherian `A`-module. -/
instance isNoetherian_of_liesOver [IsNoetherian A B] : IsNoetherian (A â§¸ p) (B â§¸ P) :=
  isNoetherian_of_tower A inferInstance

instance instFaithfulSMul : FaithfulSMul (A â§¸ p) (B â§¸ P) := by
  rw [faithfulSMul_iff_algebraMap_injective]
  rintro âŸ¨aâŸ© âŸ¨bâŸ© hab
  apply Quotient.eq.mpr ((mem_of_liesOver P p (a - b)).mpr _)
  rw [RingHom.map_sub]
  exact Quotient.eq.mp hab

@[deprecated (since := "2025-01-31")]
alias algebraMap_injective_of_liesOver := instFaithfulSMul

variable {p} in
theorem nontrivial_of_liesOver_of_ne_top (hp : p â‰  âŠ¤) : Nontrivial (B â§¸ P) :=
  Quotient.nontrivial ((eq_top_iff_of_liesOver P p).mp.mt hp)

theorem nontrivial_of_liesOver_of_isPrime [hp : p.IsPrime] : Nontrivial (B â§¸ P) :=
  nontrivial_of_liesOver_of_ne_top P hp.ne_top

section algEquiv

variable {P} {E : Type*} [EquivLike E B C] [AlgEquivClass E A B C] (Ïƒ : E)

/-- An `A â§¸ p`-algebra isomorphism between `B â§¸ P` and `C â§¸ Q` induced by an `A`-algebra
  isomorphism between `B` and `C`, where `Q = Ïƒ P`. -/
def algEquivOfEqMap (h : Q = P.map Ïƒ) : (B â§¸ P) â‰ƒâ‚[A â§¸ p] (C â§¸ Q) where
  __ := quotientEquiv P Q Ïƒ h
  commutes' := by
    rintro âŸ¨xâŸ©
    exact congrArg (Ideal.Quotient.mk Q) (AlgHomClass.commutes Ïƒ x)

@[simp]
theorem algEquivOfEqMap_apply (h : Q = P.map Ïƒ) (x : B) : algEquivOfEqMap p Ïƒ h x = Ïƒ x :=
  rfl

/-- An `A â§¸ p`-algebra isomorphism between `B â§¸ P` and `C â§¸ Q` induced by an `A`-algebra
  isomorphism between `B` and `C`, where `P = Ïƒâ»Â¹ Q`. -/
def algEquivOfEqComap (h : P = Q.comap Ïƒ) : (B â§¸ P) â‰ƒâ‚[A â§¸ p] (C â§¸ Q) :=
  algEquivOfEqMap p Ïƒ ((congrArg (map Ïƒ) h).trans (Q.map_comap_eq_self_of_equiv Ïƒ)).symm

@[simp]
theorem algEquivOfEqComap_apply (h : P = Q.comap Ïƒ) (x : B) : algEquivOfEqComap p Ïƒ h x = Ïƒ x :=
  rfl

end algEquiv

/-- If `P` lies over `p`, then the stabilizer of `P` acts on the extension `(B â§¸ P) / (A â§¸ p)`. -/
def stabilizerHom : MulAction.stabilizer G P â†’* ((B â§¸ P) â‰ƒâ‚[A â§¸ p] (B â§¸ P)) where
  toFun g := algEquivOfEqMap p (MulSemiringAction.toAlgEquiv A B g) g.2.symm
  map_one' := by
    ext âŸ¨xâŸ©
    exact congrArg (Ideal.Quotient.mk P) (one_smul G x)
  map_mul' g h := by
    ext âŸ¨xâŸ©
    exact congrArg (Ideal.Quotient.mk P) (mul_smul g h x)

@[simp] theorem stabilizerHom_apply (g : MulAction.stabilizer G P) (b : B) :
    stabilizerHom P p G g b = â†‘(g â€¢ b) :=
  rfl

end Quotient

end ideal_liesOver

section primesOver

variable {A : Type*} [CommSemiring A] (p : Ideal A) (B : Type*) [Semiring B] [Algebra A B]

/-- The set of all prime ideals in `B` that lie over an ideal `p` of `A`. -/
def primesOver : Set (Ideal B) :=
  { P : Ideal B | P.IsPrime âˆ§ P.LiesOver p }

variable {B}

instance primesOver.isPrime (Q : primesOver p B) : Q.1.IsPrime :=
  Q.2.1

instance primesOver.liesOver (Q : primesOver p B) : Q.1.LiesOver p :=
  Q.2.2

/-- If an ideal `P` of `B` is prime and lying over `p`, then it is in `primesOver p B`. -/
abbrev primesOver.mk (P : Ideal B) [hPp : P.IsPrime] [hp : P.LiesOver p] : primesOver p B :=
  âŸ¨P, âŸ¨hPp, hpâŸ©âŸ©

end primesOver

end Ideal
