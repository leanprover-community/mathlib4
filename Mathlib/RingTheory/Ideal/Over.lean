/-
Copyright (c) 2020 Anne Baanen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anne Baanen, Yongle Hu
-/
import Mathlib.Algebra.Group.Subgroup.Actions
import Mathlib.Algebra.Polynomial.Inductions
import Mathlib.RingTheory.FiniteType
import Mathlib.RingTheory.Ideal.Pointwise

/-!
# Ideals over/under ideals

This file concerns ideals lying over other ideals.
Let `f : R ‚Üí+* S` be a ring homomorphism (typically a ring extension), `I` an ideal of `R` and
`J` an ideal of `S`. We say `J` lies over `I` (and `I` under `J`) if `I` is the `f`-preimage of `J`.
This is expressed here by writing `I = J.comap f`.
-/

-- for going-up results about integral extensions, see `Mathlib.RingTheory.Ideal.GoingUp`
assert_not_exists Algebra.IsIntegral

variable {R : Type*} [CommRing R]

namespace Ideal

open Polynomial Submodule

open scoped Pointwise

section CommRing

variable {S : Type*} [CommRing S] {f : R ‚Üí+* S} {I J : Ideal S}

theorem coeff_zero_mem_comap_of_root_mem_of_eval_mem {r : S} (hr : r ‚àà I) {p : R[X]}
    (hp : p.eval‚ÇÇ f r ‚àà I) : p.coeff 0 ‚àà I.comap f := by
  rw [‚Üê p.divX_mul_X_add, eval‚ÇÇ_add, eval‚ÇÇ_C, eval‚ÇÇ_mul, eval‚ÇÇ_X] at hp
  refine mem_comap.mpr ((I.add_mem_iff_right ?_).mp hp)
  exact I.mul_mem_left _ hr

theorem coeff_zero_mem_comap_of_root_mem {r : S} (hr : r ‚àà I) {p : R[X]} (hp : p.eval‚ÇÇ f r = 0) :
    p.coeff 0 ‚àà I.comap f :=
  coeff_zero_mem_comap_of_root_mem_of_eval_mem hr (hp.symm ‚ñ∏ I.zero_mem)

theorem exists_coeff_ne_zero_mem_comap_of_non_zero_divisor_root_mem {r : S}
    (r_non_zero_divisor : ‚àÄ {x}, x * r = 0 ‚Üí x = 0) (hr : r ‚àà I) {p : R[X]} :
    p ‚â† 0 ‚Üí p.eval‚ÇÇ f r = 0 ‚Üí ‚àÉ i, p.coeff i ‚â† 0 ‚àß p.coeff i ‚àà I.comap f := by
  refine p.recOnHorner ?_ ?_ ?_
  ¬∑ intro h
    contradiction
  ¬∑ intro p a coeff_eq_zero a_ne_zero _ _ hp
    refine ‚ü®0, ?_, coeff_zero_mem_comap_of_root_mem hr hp‚ü©
    simp [coeff_eq_zero, a_ne_zero]
  ¬∑ intro p p_nonzero ih _ hp
    rw [eval‚ÇÇ_mul, eval‚ÇÇ_X] at hp
    obtain ‚ü®i, hi, mem‚ü© := ih p_nonzero (r_non_zero_divisor hp)
    refine ‚ü®i + 1, ?_, ?_‚ü©
    ¬∑ simp [hi, mem]
    ¬∑ simpa [hi] using mem

/-- Let `P` be an ideal in `R[x]`.  The map
`R[x]/P ‚Üí (R / (P ‚à© R))[x] / (P / (P ‚à© R))`
is injective.
-/
theorem injective_quotient_le_comap_map (P : Ideal R[X]) :
    Function.Injective <|
      Ideal.quotientMap
        (Ideal.map (Polynomial.mapRingHom (Quotient.mk (P.comap (C : R ‚Üí+* R[X])))) P)
        (Polynomial.mapRingHom (Ideal.Quotient.mk (P.comap (C : R ‚Üí+* R[X]))))
        le_comap_map := by
  refine quotientMap_injective' (le_of_eq ?_)
  rw [comap_map_of_surjective (mapRingHom (Ideal.Quotient.mk (P.comap (C : R ‚Üí+* R[X]))))
      (map_surjective (Ideal.Quotient.mk (P.comap (C : R ‚Üí+* R[X]))) Ideal.Quotient.mk_surjective)]
  refine le_antisymm (sup_le le_rfl ?_) (le_sup_of_le_left le_rfl)
  refine fun p hp =>
    polynomial_mem_ideal_of_coeff_mem_ideal P p fun n => Ideal.Quotient.eq_zero_iff_mem.mp ?_
  simpa only [coeff_map, coe_mapRingHom] using ext_iff.mp (Ideal.mem_bot.mp (mem_comap.mp hp)) n

/-- The identity in this lemma asserts that the "obvious" square
```
    R    ‚Üí (R / (P ‚à© R))
    ‚Üì          ‚Üì
R[x] / P ‚Üí (R / (P ‚à© R))[x] / (P / (P ‚à© R))
```
commutes.  It is used, for instance, in the proof of `quotient_mk_comp_C_is_integral_of_jacobson`,
in the file `Mathlib.RingTheory.Jacobson.Polynomial`.
-/
theorem quotient_mk_maps_eq (P : Ideal R[X]) :
    ((Quotient.mk (map (mapRingHom (Quotient.mk (P.comap (C : R ‚Üí+* R[X])))) P)).comp C).comp
        (Quotient.mk (P.comap (C : R ‚Üí+* R[X]))) =
      (Ideal.quotientMap (map (mapRingHom (Quotient.mk (P.comap (C : R ‚Üí+* R[X])))) P)
            (mapRingHom (Quotient.mk (P.comap (C : R ‚Üí+* R[X])))) le_comap_map).comp
        ((Quotient.mk P).comp C) := by
  refine RingHom.ext fun x => ?_
  repeat' rw [RingHom.coe_comp, Function.comp_apply]
  rw [quotientMap_mk, coe_mapRingHom, map_C]

/-- This technical lemma asserts the existence of a polynomial `p` in an ideal `P ‚äÇ R[x]`
that is non-zero in the quotient `R / (P ‚à© R) [x]`.  The assumptions are equivalent to
`P ‚â† 0` and `P ‚à© R = (0)`.
-/
theorem exists_nonzero_mem_of_ne_bot {P : Ideal R[X]} (Pb : P ‚â† ‚ä•) (hP : ‚àÄ x : R, C x ‚àà P ‚Üí x = 0) :
    ‚àÉ p : R[X], p ‚àà P ‚àß Polynomial.map (Quotient.mk (P.comap (C : R ‚Üí+* R[X]))) p ‚â† 0 := by
  obtain ‚ü®m, hm‚ü© := Submodule.nonzero_mem_of_bot_lt (bot_lt_iff_ne_bot.mpr Pb)
  refine ‚ü®m, Submodule.coe_mem m, fun pp0 => hm (Submodule.coe_eq_zero.mp ?_)‚ü©
  refine
    (injective_iff_map_eq_zero (Polynomial.mapRingHom (Ideal.Quotient.mk
      (P.comap (C : R ‚Üí+* R[X]))))).mp
      ?_ _ pp0
  refine map_injective _ ((Ideal.Quotient.mk (P.comap C)).injective_iff_ker_eq_bot.mpr ?_)
  rw [mk_ker]
  exact (Submodule.eq_bot_iff _).mpr fun x hx => hP x (mem_comap.mp hx)

variable {p : Ideal R} {P : Ideal S}

/-- If there is an injective map `R/p ‚Üí S/P` such that following diagram commutes:
```
R   ‚Üí S
‚Üì     ‚Üì
R/p ‚Üí S/P
```
then `P` lies over `p`.
-/
theorem comap_eq_of_scalar_tower_quotient [Algebra R S] [Algebra (R ‚ß∏ p) (S ‚ß∏ P)]
    [IsScalarTower R (R ‚ß∏ p) (S ‚ß∏ P)] (h : Function.Injective (algebraMap (R ‚ß∏ p) (S ‚ß∏ P))) :
    comap (algebraMap R S) P = p := by
  ext x
  rw [mem_comap, ‚Üê Quotient.eq_zero_iff_mem, ‚Üê Quotient.eq_zero_iff_mem, Quotient.mk_algebraMap,
    IsScalarTower.algebraMap_apply R (R ‚ß∏ p) (S ‚ß∏ P), Quotient.algebraMap_eq]
  constructor
  ¬∑ intro hx
    exact (injective_iff_map_eq_zero (algebraMap (R ‚ß∏ p) (S ‚ß∏ P))).mp h _ hx
  ¬∑ intro hx
    rw [hx, RingHom.map_zero]

variable [Algebra R S]

/-- `R / p` has a canonical map to `S / pS`. -/
instance Quotient.algebraQuotientMapQuotient : Algebra (R ‚ß∏ p) (S ‚ß∏ map (algebraMap R S) p) :=
  Ideal.Quotient.algebraQuotientOfLEComap le_comap_map

@[simp]
theorem Quotient.algebraMap_quotient_map_quotient (x : R) :
    letI f := algebraMap R S
    algebraMap (R ‚ß∏ p) (S ‚ß∏ map f p) (Ideal.Quotient.mk p x) =
    Ideal.Quotient.mk (map f p) (f x) :=
  rfl

@[simp]
theorem Quotient.mk_smul_mk_quotient_map_quotient (x : R) (y : S) :
    letI f := algebraMap R S
    Quotient.mk p x ‚Ä¢ Quotient.mk (map f p) y = Quotient.mk (map f p) (f x * y) :=
  Algebra.smul_def _ _

instance Quotient.tower_quotient_map_quotient [Algebra R S] :
    IsScalarTower R (R ‚ß∏ p) (S ‚ß∏ map (algebraMap R S) p) :=
  IsScalarTower.of_algebraMap_eq fun x => by
    rw [Quotient.algebraMap_eq, Quotient.algebraMap_quotient_map_quotient,
      Quotient.mk_algebraMap]

instance QuotientMapQuotient.isNoetherian [Algebra R S] [IsNoetherian R S] (I : Ideal R) :
    IsNoetherian (R ‚ß∏ I) (S ‚ß∏ I.map (algebraMap R S)) :=
  isNoetherian_of_tower R <|
    isNoetherian_of_surjective S (Ideal.Quotient.mk‚Çê R _).toLinearMap <|
      LinearMap.range_eq_top.mpr Ideal.Quotient.mk_surjective

end CommRing

section ideal_liesOver

section Semiring

variable (A : Type*) [CommSemiring A] {B C : Type*} [Semiring B] [Semiring C] [Algebra A B]
  [Algebra A C] (P : Ideal B) {Q : Ideal C} (p : Ideal A)

/-- The ideal obtained by pulling back the ideal `P` from `B` to `A`. -/
abbrev under : Ideal A := Ideal.comap (algebraMap A B) P

theorem under_def : P.under A = Ideal.comap (algebraMap A B) P := rfl

instance IsPrime.under [hP : P.IsPrime] : (P.under A).IsPrime :=
  hP.comap (algebraMap A B)

@[simp]
lemma under_smul {G : Type*} [Group G] [MulSemiringAction G B] [SMulCommClass G A B] (g : G) :
    (g ‚Ä¢ P : Ideal B).under A = P.under A := by
  ext a
  rw [mem_comap, mem_comap, mem_pointwise_smul_iff_inv_smul_mem, smul_algebraMap]

variable (B) in
theorem under_top : under A (‚ä§ : Ideal B) = ‚ä§ := comap_top

variable {A}

/-- `P` lies over `p` if `p` is the preimage of `P` of the `algebraMap`. -/
class LiesOver : Prop where
  over : p = P.under A

instance over_under : P.LiesOver (P.under A) where over := rfl

theorem over_def [P.LiesOver p] : p = P.under A := LiesOver.over

theorem mem_of_liesOver [P.LiesOver p] (x : A) : x ‚àà p ‚Üî algebraMap A B x ‚àà P := by
  rw [P.over_def p]
  rfl

variable (A B) in
instance top_liesOver_top : (‚ä§ : Ideal B).LiesOver (‚ä§ : Ideal A) where
  over := (under_top A B).symm

theorem eq_top_iff_of_liesOver [P.LiesOver p] : P = ‚ä§ ‚Üî p = ‚ä§ := by
  rw [P.over_def p]
  exact comap_eq_top_iff.symm

variable {P}

theorem LiesOver.of_eq_comap [Q.LiesOver p] {F : Type*} [FunLike F B C]
    [AlgHomClass F A B C] (f : F) (h : P = Q.comap f) : P.LiesOver p where
  over := by
    rw [h]
    exact (over_def Q p).trans <|
      congrFun (congrFun (congrArg comap ((f : B ‚Üí‚Çê[A] C).comp_algebraMap.symm)) _) Q

theorem LiesOver.of_eq_map_equiv [P.LiesOver p] {E : Type*} [EquivLike E B C]
    [AlgEquivClass E A B C] (œÉ : E) (h : Q = P.map œÉ) : Q.LiesOver p := by
  rw [‚Üê show _ = P.map œÉ from comap_symm (œÉ : B ‚âÉ+* C)] at h
  exact of_eq_comap p (œÉ : B ‚âÉ‚Çê[A] C).symm h

variable (P) (Q)

instance comap_liesOver [Q.LiesOver p] {F : Type*} [FunLike F B C] [AlgHomClass F A B C]
    (f : F) : (Q.comap f).LiesOver p :=
  LiesOver.of_eq_comap p f rfl

instance map_equiv_liesOver [P.LiesOver p] {E : Type*} [EquivLike E B C] [AlgEquivClass E A B C]
    (œÉ : E) : (P.map œÉ).LiesOver p :=
  LiesOver.of_eq_map_equiv p œÉ rfl

end Semiring

section CommSemiring

variable {A : Type*} [CommSemiring A] {B : Type*} [CommSemiring B] {C : Type*} [Semiring C]
  [Algebra A B] [Algebra B C] [Algebra A C] [IsScalarTower A B C]
  (ùîì : Ideal C) (P : Ideal B) (p : Ideal A)

@[simp]
theorem under_under : (ùîì.under B).under A  = ùîì.under A := by
  simp_rw [comap_comap, ‚Üê IsScalarTower.algebraMap_eq]

theorem LiesOver.trans [ùîì.LiesOver P] [P.LiesOver p] : ùîì.LiesOver p where
  over := by rw [P.over_def p, ùîì.over_def P, under_under]

theorem LiesOver.tower_bot [hp : ùîì.LiesOver p] [hP : ùîì.LiesOver P] : P.LiesOver p where
  over := by rw [ùîì.over_def p, ùîì.over_def P, under_under]

variable (B)

instance under_liesOver_of_liesOver [ùîì.LiesOver p] : (ùîì.under B).LiesOver p :=
  LiesOver.tower_bot ùîì (ùîì.under B) p

end CommSemiring

section CommRing

variable (A : Type*) [CommRing A] (B : Type*) [Ring B] [Nontrivial B]
  [Algebra A B] [NoZeroSMulDivisors A B] {p : Ideal A}

@[simp]
theorem under_bot : under A (‚ä• : Ideal B) = ‚ä• :=
  comap_bot_of_injective (algebraMap A B) (FaithfulSMul.algebraMap_injective A B)

instance bot_liesOver_bot : (‚ä• : Ideal B).LiesOver (‚ä• : Ideal A) where
  over := (under_bot A B).symm

variable {A B} in
theorem ne_bot_of_liesOver_of_ne_bot (hp : p ‚â† ‚ä•) (P : Ideal B) [P.LiesOver p] : P ‚â† ‚ä• := by
  contrapose! hp
  rw [over_def P p, hp, under_bot]

end CommRing

namespace Quotient

variable (R : Type*) [CommSemiring R] {A B C : Type*} [CommRing A] [CommRing B] [CommRing C]
  [Algebra A B] [Algebra A C] [Algebra R A] [Algebra R B] [IsScalarTower R A B]
  (P : Ideal B) {Q : Ideal C} (p : Ideal A) [Q.LiesOver p] [P.LiesOver p]
  (G : Type*) [Group G] [MulSemiringAction G B] [SMulCommClass G A B]

/-- If `P` lies over `p`, then canonically `B ‚ß∏ P` is a `A ‚ß∏ p`-algebra. -/
instance algebraOfLiesOver : Algebra (A ‚ß∏ p) (B ‚ß∏ P) :=
  algebraQuotientOfLEComap (le_of_eq (P.over_def p))

instance isScalarTower_of_liesOver : IsScalarTower R (A ‚ß∏ p) (B ‚ß∏ P) :=
  IsScalarTower.of_algebraMap_eq' <|
    congrArg (algebraMap B (B ‚ß∏ P)).comp (IsScalarTower.algebraMap_eq R A B)

/-- `B ‚ß∏ P` is a finite `A ‚ß∏ p`-module if `B` is a finite `A`-module. -/
instance module_finite_of_liesOver [Module.Finite A B] : Module.Finite (A ‚ß∏ p) (B ‚ß∏ P) :=
  Module.Finite.of_restrictScalars_finite A (A ‚ß∏ p) (B ‚ß∏ P)

example [Module.Finite A B] : Module.Finite (A ‚ß∏ P.under A) (B ‚ß∏ P) := inferInstance

/-- `B ‚ß∏ P` is a finitely generated `A ‚ß∏ p`-algebra if `B` is a finitely generated `A`-algebra. -/
instance algebra_finiteType_of_liesOver [Algebra.FiniteType A B] :
    Algebra.FiniteType (A ‚ß∏ p) (B ‚ß∏ P) :=
  Algebra.FiniteType.of_restrictScalars_finiteType A (A ‚ß∏ p) (B ‚ß∏ P)

/-- `B ‚ß∏ P` is a Noetherian `A ‚ß∏ p`-module if `B` is a Noetherian `A`-module. -/
instance isNoetherian_of_liesOver [IsNoetherian A B] : IsNoetherian (A ‚ß∏ p) (B ‚ß∏ P) :=
  isNoetherian_of_tower A inferInstance

instance instFaithfulSMul : FaithfulSMul (A ‚ß∏ p) (B ‚ß∏ P) := by
  rw [faithfulSMul_iff_algebraMap_injective]
  rintro ‚ü®a‚ü© ‚ü®b‚ü© hab
  apply Quotient.eq.mpr ((mem_of_liesOver P p (a - b)).mpr _)
  rw [RingHom.map_sub]
  exact Quotient.eq.mp hab

@[deprecated (since := "2025-01-31")]
alias algebraMap_injective_of_liesOver := instFaithfulSMul

variable {p} in
theorem nontrivial_of_liesOver_of_ne_top (hp : p ‚â† ‚ä§) : Nontrivial (B ‚ß∏ P) :=
  Quotient.nontrivial ((eq_top_iff_of_liesOver P p).mp.mt hp)

theorem nontrivial_of_liesOver_of_isPrime [hp : p.IsPrime] : Nontrivial (B ‚ß∏ P) :=
  nontrivial_of_liesOver_of_ne_top P hp.ne_top

section algEquiv

variable {P} {E : Type*} [EquivLike E B C] [AlgEquivClass E A B C] (œÉ : E)

/-- An `A ‚ß∏ p`-algebra isomorphism between `B ‚ß∏ P` and `C ‚ß∏ Q` induced by an `A`-algebra
  isomorphism between `B` and `C`, where `Q = œÉ P`. -/
def algEquivOfEqMap (h : Q = P.map œÉ) : (B ‚ß∏ P) ‚âÉ‚Çê[A ‚ß∏ p] (C ‚ß∏ Q) where
  __ := quotientEquiv P Q œÉ h
  commutes' := by
    rintro ‚ü®x‚ü©
    exact congrArg (Ideal.Quotient.mk Q) (AlgHomClass.commutes œÉ x)

@[simp]
theorem algEquivOfEqMap_apply (h : Q = P.map œÉ) (x : B) : algEquivOfEqMap p œÉ h x = œÉ x :=
  rfl

/-- An `A ‚ß∏ p`-algebra isomorphism between `B ‚ß∏ P` and `C ‚ß∏ Q` induced by an `A`-algebra
  isomorphism between `B` and `C`, where `P = œÉ‚Åª¬π Q`. -/
def algEquivOfEqComap (h : P = Q.comap œÉ) : (B ‚ß∏ P) ‚âÉ‚Çê[A ‚ß∏ p] (C ‚ß∏ Q) :=
  algEquivOfEqMap p œÉ ((congrArg (map œÉ) h).trans (Q.map_comap_eq_self_of_equiv œÉ)).symm

@[simp]
theorem algEquivOfEqComap_apply (h : P = Q.comap œÉ) (x : B) : algEquivOfEqComap p œÉ h x = œÉ x :=
  rfl

end algEquiv

/-- If `P` lies over `p`, then the stabilizer of `P` acts on the extension `(B ‚ß∏ P) / (A ‚ß∏ p)`. -/
def stabilizerHom : MulAction.stabilizer G P ‚Üí* ((B ‚ß∏ P) ‚âÉ‚Çê[A ‚ß∏ p] (B ‚ß∏ P)) where
  toFun g := algEquivOfEqMap p (MulSemiringAction.toAlgEquiv A B g) g.2.symm
  map_one' := by
    ext ‚ü®x‚ü©
    exact congrArg (Ideal.Quotient.mk P) (one_smul G x)
  map_mul' g h := by
    ext ‚ü®x‚ü©
    exact congrArg (Ideal.Quotient.mk P) (mul_smul g h x)

@[simp] theorem stabilizerHom_apply (g : MulAction.stabilizer G P) (b : B) :
    stabilizerHom P p G g b = ‚Üë(g ‚Ä¢ b) :=
  rfl

end Quotient

end ideal_liesOver

section primesOver

variable {A : Type*} [CommSemiring A] (p : Ideal A) (B : Type*) [Semiring B] [Algebra A B]

/-- The set of all prime ideals in `B` that lie over an ideal `p` of `A`. -/
def primesOver : Set (Ideal B) :=
  { P : Ideal B | P.IsPrime ‚àß P.LiesOver p }

variable {B}

instance primesOver.isPrime (Q : primesOver p B) : Q.1.IsPrime :=
  Q.2.1

instance primesOver.liesOver (Q : primesOver p B) : Q.1.LiesOver p :=
  Q.2.2

/-- If an ideal `P` of `B` is prime and lying over `p`, then it is in `primesOver p B`. -/
abbrev primesOver.mk (P : Ideal B) [hPp : P.IsPrime] [hp : P.LiesOver p] : primesOver p B :=
  ‚ü®P, ‚ü®hPp, hp‚ü©‚ü©

end primesOver

end Ideal
