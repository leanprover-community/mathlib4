/-
Copyright (c) 2020 Anne Baanen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anne Baanen
-/
import Mathlib.RingTheory.Algebraic
import Mathlib.RingTheory.Localization.AtPrime
import Mathlib.RingTheory.Localization.Integral

#align_import ring_theory.ideal.over from "leanprover-community/mathlib"@"198cb64d5c961e1a8d0d3e219feb7058d5353861"

/-!
# Ideals over/under ideals

This file concerns ideals lying over other ideals.
Let `f : R â†’+* S` be a ring homomorphism (typically a ring extension), `I` an ideal of `R` and
`J` an ideal of `S`. We say `J` lies over `I` (and `I` under `J`) if `I` is the `f`-preimage of `J`.
This is expressed here by writing `I = J.comap f`.

## Implementation notes

The proofs of the `comap_ne_bot` and `comap_lt_comap` families use an approach
specific for their situation: we construct an element in `I.comap f` from the
coefficients of a minimal polynomial.
Once mathlib has more material on the localization at a prime ideal, the results
can be proven using more general going-up/going-down theory.
-/


variable {R : Type*} [CommRing R]

namespace Ideal

open Polynomial

open Polynomial

open Submodule

section CommRing

variable {S : Type*} [CommRing S] {f : R â†’+* S} {I J : Ideal S}

theorem coeff_zero_mem_comap_of_root_mem_of_eval_mem {r : S} (hr : r âˆˆ I) {p : R[X]}
    (hp : p.evalâ‚‚ f r âˆˆ I) : p.coeff 0 âˆˆ I.comap f := by
  rw [â† p.divX_mul_X_add, evalâ‚‚_add, evalâ‚‚_C, evalâ‚‚_mul, evalâ‚‚_X] at hp
  -- âŠ¢ coeff p 0 âˆˆ comap f I
  refine' mem_comap.mpr ((I.add_mem_iff_right _).mp hp)
  -- âŠ¢ evalâ‚‚ f r (divX p) * r âˆˆ I
  exact I.mul_mem_left _ hr
  -- ğŸ‰ no goals
#align ideal.coeff_zero_mem_comap_of_root_mem_of_eval_mem Ideal.coeff_zero_mem_comap_of_root_mem_of_eval_mem

theorem coeff_zero_mem_comap_of_root_mem {r : S} (hr : r âˆˆ I) {p : R[X]} (hp : p.evalâ‚‚ f r = 0) :
    p.coeff 0 âˆˆ I.comap f :=
  coeff_zero_mem_comap_of_root_mem_of_eval_mem hr (hp.symm â–¸ I.zero_mem)
#align ideal.coeff_zero_mem_comap_of_root_mem Ideal.coeff_zero_mem_comap_of_root_mem

theorem exists_coeff_ne_zero_mem_comap_of_non_zero_divisor_root_mem {r : S}
    (r_non_zero_divisor : âˆ€ {x}, x * r = 0 â†’ x = 0) (hr : r âˆˆ I) {p : R[X]} :
    âˆ€ (_ : p â‰  0) (_ : p.evalâ‚‚ f r = 0), âˆƒ i, p.coeff i â‰  0 âˆ§ p.coeff i âˆˆ I.comap f := by
  refine' p.recOnHorner _ _ _
  Â· intro h
    -- âŠ¢ evalâ‚‚ f r 0 = 0 â†’ âˆƒ i, coeff 0 i â‰  0 âˆ§ coeff 0 i âˆˆ comap f I
    contradiction
    -- ğŸ‰ no goals
  Â· intro p a coeff_eq_zero a_ne_zero _ _ hp
    -- âŠ¢ âˆƒ i, coeff (p + â†‘C a) i â‰  0 âˆ§ coeff (p + â†‘C a) i âˆˆ comap f I
    refine' âŸ¨0, _, coeff_zero_mem_comap_of_root_mem hr hpâŸ©
    -- âŠ¢ coeff (p + â†‘C a) 0 â‰  0
    simp [coeff_eq_zero, a_ne_zero]
    -- ğŸ‰ no goals
  Â· intro p p_nonzero ih _ hp
    -- âŠ¢ âˆƒ i, coeff (p * X) i â‰  0 âˆ§ coeff (p * X) i âˆˆ comap f I
    rw [evalâ‚‚_mul, evalâ‚‚_X] at hp
    -- âŠ¢ âˆƒ i, coeff (p * X) i â‰  0 âˆ§ coeff (p * X) i âˆˆ comap f I
    obtain âŸ¨i, hi, memâŸ© := ih p_nonzero (r_non_zero_divisor hp)
    -- âŠ¢ âˆƒ i, coeff (p * X) i â‰  0 âˆ§ coeff (p * X) i âˆˆ comap f I
    refine' âŸ¨i + 1, _, _âŸ©
    -- âŠ¢ coeff (p * X) (i + 1) â‰  0
    Â· simp [hi, mem]
      -- ğŸ‰ no goals
    Â· simpa [hi] using mem
      -- ğŸ‰ no goals
#align ideal.exists_coeff_ne_zero_mem_comap_of_non_zero_divisor_root_mem Ideal.exists_coeff_ne_zero_mem_comap_of_non_zero_divisor_root_mem

/-- Let `P` be an ideal in `R[x]`.  The map
`R[x]/P â†’ (R / (P âˆ© R))[x] / (P / (P âˆ© R))`
is injective.
-/
theorem injective_quotient_le_comap_map (P : Ideal R[X]) :
    Function.Injective <|
      Ideal.quotientMap
        (Ideal.map (Polynomial.mapRingHom (Quotient.mk (P.comap (C : R â†’+* R[X])))) P)
        (Polynomial.mapRingHom (Ideal.Quotient.mk (P.comap (C : R â†’+* R[X]))))
        le_comap_map := by
  refine' quotientMap_injective' (le_of_eq _)
  -- âŠ¢ comap (mapRingHom (Quotient.mk (comap C P))) (map (mapRingHom (Quotient.mk ( â€¦
  rw [comap_map_of_surjective (mapRingHom (Ideal.Quotient.mk (P.comap (C : R â†’+* R[X]))))
      (map_surjective (Ideal.Quotient.mk (P.comap (C : R â†’+* R[X]))) Ideal.Quotient.mk_surjective)]
  refine' le_antisymm (sup_le le_rfl _) (le_sup_of_le_left le_rfl)
  -- âŠ¢ comap (mapRingHom (Quotient.mk (comap C P))) âŠ¥ â‰¤ P
  refine' fun p hp =>
    polynomial_mem_ideal_of_coeff_mem_ideal P p fun n => Ideal.Quotient.eq_zero_iff_mem.mp _
  simpa only [coeff_map, coe_mapRingHom] using ext_iff.mp (Ideal.mem_bot.mp (mem_comap.mp hp)) n
  -- ğŸ‰ no goals
#align ideal.injective_quotient_le_comap_map Ideal.injective_quotient_le_comap_map

/-- The identity in this lemma asserts that the "obvious" square
```
    R    â†’ (R / (P âˆ© R))
    â†“          â†“
R[x] / P â†’ (R / (P âˆ© R))[x] / (P / (P âˆ© R))
```
commutes.  It is used, for instance, in the proof of `quotient_mk_comp_C_is_integral_of_jacobson`,
in the file `RingTheory.Jacobson`.
-/
theorem quotient_mk_maps_eq (P : Ideal R[X]) :
    ((Quotient.mk (map (mapRingHom (Quotient.mk (P.comap (C : R â†’+* R[X])))) P)).comp C).comp
        (Quotient.mk (P.comap (C : R â†’+* R[X]))) =
      (Ideal.quotientMap (map (mapRingHom (Quotient.mk (P.comap (C : R â†’+* R[X])))) P)
            (mapRingHom (Quotient.mk (P.comap (C : R â†’+* R[X])))) le_comap_map).comp
        ((Quotient.mk P).comp C) := by
  refine' RingHom.ext fun x => _
  -- âŠ¢ â†‘(RingHom.comp (RingHom.comp (Quotient.mk (map (mapRingHom (Quotient.mk (com â€¦
  repeat' rw [RingHom.coe_comp, Function.comp_apply]
  -- âŠ¢ â†‘(Quotient.mk (map (mapRingHom (Quotient.mk (comap C P))) P)) (â†‘C (â†‘(Quotien â€¦
  rw [quotientMap_mk, coe_mapRingHom, map_C]
  -- ğŸ‰ no goals
#align ideal.quotient_mk_maps_eq Ideal.quotient_mk_maps_eq

/-- This technical lemma asserts the existence of a polynomial `p` in an ideal `P âŠ‚ R[x]`
that is non-zero in the quotient `R / (P âˆ© R) [x]`.  The assumptions are equivalent to
`P â‰  0` and `P âˆ© R = (0)`.
-/
theorem exists_nonzero_mem_of_ne_bot {P : Ideal R[X]} (Pb : P â‰  âŠ¥) (hP : âˆ€ x : R, C x âˆˆ P â†’ x = 0) :
    âˆƒ p : R[X], p âˆˆ P âˆ§ Polynomial.map (Quotient.mk (P.comap (C : R â†’+* R[X]))) p â‰  0 := by
  obtain âŸ¨m, hmâŸ© := Submodule.nonzero_mem_of_bot_lt (bot_lt_iff_ne_bot.mpr Pb)
  -- âŠ¢ âˆƒ p, p âˆˆ P âˆ§ Polynomial.map (Quotient.mk (comap C P)) p â‰  0
  refine' âŸ¨m, Submodule.coe_mem m, fun pp0 => hm (Submodule.coe_eq_zero.mp _)âŸ©
  -- âŠ¢ â†‘m = 0
  refine'
    (injective_iff_map_eq_zero (Polynomial.mapRingHom (Ideal.Quotient.mk
      (P.comap (C : R â†’+* R[X]))))).mp
      _ _ pp0
  refine' map_injective _ ((Ideal.Quotient.mk (P.comap C)).injective_iff_ker_eq_bot.mpr _)
  -- âŠ¢ RingHom.ker (Quotient.mk (comap C P)) = âŠ¥
  rw [mk_ker]
  -- âŠ¢ comap C P = âŠ¥
  exact (Submodule.eq_bot_iff _).mpr fun x hx => hP x (mem_comap.mp hx)
  -- ğŸ‰ no goals
#align ideal.exists_nonzero_mem_of_ne_bot Ideal.exists_nonzero_mem_of_ne_bot

variable {p : Ideal R} {P : Ideal S}

/-- If there is an injective map `R/p â†’ S/P` such that following diagram commutes:
```
R   â†’ S
â†“     â†“
R/p â†’ S/P
```
then `P` lies over `p`.
-/
theorem comap_eq_of_scalar_tower_quotient [Algebra R S] [Algebra (R â§¸ p) (S â§¸ P)]
    [IsScalarTower R (R â§¸ p) (S â§¸ P)] (h : Function.Injective (algebraMap (R â§¸ p) (S â§¸ P))) :
    comap (algebraMap R S) P = p := by
  ext x
  -- âŠ¢ x âˆˆ comap (algebraMap R S) P â†” x âˆˆ p
  rw [mem_comap, â† Quotient.eq_zero_iff_mem, â† Quotient.eq_zero_iff_mem, Quotient.mk_algebraMap,
    IsScalarTower.algebraMap_apply R (R â§¸ p) (S â§¸ P), Quotient.algebraMap_eq]
  constructor
  -- âŠ¢ â†‘(algebraMap (R â§¸ p) (S â§¸ P)) (â†‘(Quotient.mk p) x) = 0 â†’ â†‘(Quotient.mk p) x  â€¦
  Â· intro hx
    -- âŠ¢ â†‘(Quotient.mk p) x = 0
    exact (injective_iff_map_eq_zero (algebraMap (R â§¸ p) (S â§¸ P))).mp h _ hx
    -- ğŸ‰ no goals
  Â· intro hx
    -- âŠ¢ â†‘(algebraMap (R â§¸ p) (S â§¸ P)) (â†‘(Quotient.mk p) x) = 0
    rw [hx, RingHom.map_zero]
    -- ğŸ‰ no goals
#align ideal.comap_eq_of_scalar_tower_quotient Ideal.comap_eq_of_scalar_tower_quotient

/-- If `P` lies over `p`, then `R / p` has a canonical map to `S / P`. -/
def Quotient.algebraQuotientOfLeComap (h : p â‰¤ comap f P) : Algebra (R â§¸ p) (S â§¸ P) :=
  RingHom.toAlgebra <| quotientMap _ f h
#align ideal.quotient.algebra_quotient_of_le_comap Ideal.Quotient.algebraQuotientOfLeComap

/-- `R / p` has a canonical map to `S / pS`. -/
instance Quotient.algebraQuotientMapQuotient : Algebra (R â§¸ p) (S â§¸ map f p) :=
  Ideal.Quotient.algebraQuotientOfLeComap le_comap_map
#align ideal.quotient.algebra_quotient_map_quotient Ideal.Quotient.algebraQuotientMapQuotient

@[simp]
theorem Quotient.algebraMap_quotient_map_quotient (x : R) :
    algebraMap (R â§¸ p) (S â§¸ map f p) (Ideal.Quotient.mk p x) =
    Ideal.Quotient.mk (map f p) (f x) :=
  rfl
#align ideal.quotient.algebra_map_quotient_map_quotient Ideal.Quotient.algebraMap_quotient_map_quotient

@[simp]
theorem Quotient.mk_smul_mk_quotient_map_quotient (x : R) (y : S) :
    Quotient.mk p x â€¢ Quotient.mk (map f p) y = Quotient.mk (map f p) (f x * y) :=
  rfl
#align ideal.quotient.mk_smul_mk_quotient_map_quotient Ideal.Quotient.mk_smul_mk_quotient_map_quotient

instance Quotient.tower_quotient_map_quotient [Algebra R S] :
    IsScalarTower R (R â§¸ p) (S â§¸ map (algebraMap R S) p) :=
  IsScalarTower.of_algebraMap_eq fun x => by
    rw [Quotient.algebraMap_eq, Quotient.algebraMap_quotient_map_quotient,
      Quotient.mk_algebraMap]
#align ideal.quotient.tower_quotient_map_quotient Ideal.Quotient.tower_quotient_map_quotient

instance QuotientMapQuotient.isNoetherian [Algebra R S] [IsNoetherian R S] (I : Ideal R) :
    IsNoetherian (R â§¸ I) (S â§¸ Ideal.map (algebraMap R S) I) :=
  isNoetherian_of_tower R <|
    isNoetherian_of_surjective S (Ideal.Quotient.mkâ‚ R _).toLinearMap <|
      LinearMap.range_eq_top.mpr Ideal.Quotient.mk_surjective
#align ideal.quotient_map_quotient.is_noetherian Ideal.QuotientMapQuotient.isNoetherian

end CommRing

section IsDomain

variable {S : Type*} [CommRing S] {f : R â†’+* S} {I J : Ideal S}

theorem exists_coeff_ne_zero_mem_comap_of_root_mem [IsDomain S] {r : S} (r_ne_zero : r â‰  0)
    (hr : r âˆˆ I) {p : R[X]} :
    âˆ€ (_ : p â‰  0) (_ : p.evalâ‚‚ f r = 0), âˆƒ i, p.coeff i â‰  0 âˆ§ p.coeff i âˆˆ I.comap f :=
  exists_coeff_ne_zero_mem_comap_of_non_zero_divisor_root_mem
    (fun {_} h => Or.resolve_right (mul_eq_zero.mp h) r_ne_zero) hr
#align ideal.exists_coeff_ne_zero_mem_comap_of_root_mem Ideal.exists_coeff_ne_zero_mem_comap_of_root_mem

theorem exists_coeff_mem_comap_sdiff_comap_of_root_mem_sdiff [IsPrime I] (hIJ : I â‰¤ J) {r : S}
    (hr : r âˆˆ (J : Set S) \ I) {p : R[X]} (p_ne_zero : p.map (Quotient.mk (I.comap f)) â‰  0)
    (hpI : p.evalâ‚‚ f r âˆˆ I) : âˆƒ i, p.coeff i âˆˆ (J.comap f : Set R) \ I.comap f := by
  obtain âŸ¨hrJ, hrIâŸ© := hr
  -- âŠ¢ âˆƒ i, coeff p i âˆˆ â†‘(comap f J) \ â†‘(comap f I)
  have rbar_ne_zero : Ideal.Quotient.mk I r â‰  0 := mt (Quotient.mk_eq_zero I).mp hrI
  -- âŠ¢ âˆƒ i, coeff p i âˆˆ â†‘(comap f J) \ â†‘(comap f I)
  have rbar_mem_J : Ideal.Quotient.mk I r âˆˆ J.map (Ideal.Quotient.mk I) := mem_map_of_mem _ hrJ
  -- âŠ¢ âˆƒ i, coeff p i âˆˆ â†‘(comap f J) \ â†‘(comap f I)
  have quotient_f : âˆ€ x âˆˆ I.comap f, (Ideal.Quotient.mk I).comp f x = 0 := by
    simp [Quotient.eq_zero_iff_mem]
  have rbar_root :
    (p.map (Ideal.Quotient.mk (I.comap f))).evalâ‚‚ (Quotient.lift (I.comap f) _ quotient_f)
        (Ideal.Quotient.mk I r) =
      0 := by
    convert Quotient.eq_zero_iff_mem.mpr hpI
    exact _root_.trans (evalâ‚‚_map _ _ _) (hom_evalâ‚‚ p f (Ideal.Quotient.mk I) r).symm
  obtain âŸ¨i, ne_zero, memâŸ© :=
    exists_coeff_ne_zero_mem_comap_of_root_mem rbar_ne_zero rbar_mem_J p_ne_zero rbar_root
  rw [coeff_map] at ne_zero mem
  -- âŠ¢ âˆƒ i, coeff p i âˆˆ â†‘(comap f J) \ â†‘(comap f I)
  refine' âŸ¨i, (mem_quotient_iff_mem hIJ).mp _, mt _ ne_zeroâŸ©
  -- âŠ¢ â†‘(Quotient.mk I) (â†‘f (coeff p i)) âˆˆ map (Quotient.mk I) J
  Â· simpa using mem
    -- ğŸ‰ no goals
  simp [Quotient.eq_zero_iff_mem]
  -- ğŸ‰ no goals
#align ideal.exists_coeff_mem_comap_sdiff_comap_of_root_mem_sdiff Ideal.exists_coeff_mem_comap_sdiff_comap_of_root_mem_sdiff

theorem comap_lt_comap_of_root_mem_sdiff [I.IsPrime] (hIJ : I â‰¤ J) {r : S}
    (hr : r âˆˆ (J : Set S) \ I) {p : R[X]} (p_ne_zero : p.map (Quotient.mk (I.comap f)) â‰  0)
    (hp : p.evalâ‚‚ f r âˆˆ I) : I.comap f < J.comap f :=
  let âŸ¨i, hJ, hIâŸ© := exists_coeff_mem_comap_sdiff_comap_of_root_mem_sdiff hIJ hr p_ne_zero hp
  SetLike.lt_iff_le_and_exists.mpr âŸ¨comap_mono hIJ, p.coeff i, hJ, hIâŸ©
#align ideal.comap_lt_comap_of_root_mem_sdiff Ideal.comap_lt_comap_of_root_mem_sdiff

theorem mem_of_one_mem (h : (1 : S) âˆˆ I) (x) : x âˆˆ I :=
  (I.eq_top_iff_one.mpr h).symm â–¸ mem_top
#align ideal.mem_of_one_mem Ideal.mem_of_one_mem

theorem comap_lt_comap_of_integral_mem_sdiff [Algebra R S] [hI : I.IsPrime] (hIJ : I â‰¤ J) {x : S}
    (mem : x âˆˆ (J : Set S) \ I) (integral : IsIntegral R x) :
    I.comap (algebraMap R S) < J.comap (algebraMap R S) := by
  obtain âŸ¨p, p_monic, hpxâŸ© := integral
  -- âŠ¢ comap (algebraMap R S) I < comap (algebraMap R S) J
  refine' comap_lt_comap_of_root_mem_sdiff hIJ mem _ _
  swap
  Â· apply map_monic_ne_zero p_monic
    -- ğŸ‰ no goals
    -- Porting note : no longer needed
    -- apply Quotient.NonTrivial
    -- apply mt comap_eq_top_iff.mp
    -- apply hI.1
  convert I.zero_mem
  -- ğŸ‰ no goals
#align ideal.comap_lt_comap_of_integral_mem_sdiff Ideal.comap_lt_comap_of_integral_mem_sdiff

theorem comap_ne_bot_of_root_mem [IsDomain S] {r : S} (r_ne_zero : r â‰  0) (hr : r âˆˆ I) {p : R[X]}
    (p_ne_zero : p â‰  0) (hp : p.evalâ‚‚ f r = 0) : I.comap f â‰  âŠ¥ := fun h =>
  let âŸ¨_, hi, memâŸ© := exists_coeff_ne_zero_mem_comap_of_root_mem r_ne_zero hr p_ne_zero hp
  absurd (mem_bot.mp (eq_bot_iff.mp h mem)) hi
#align ideal.comap_ne_bot_of_root_mem Ideal.comap_ne_bot_of_root_mem

theorem isMaximal_of_isIntegral_of_isMaximal_comap [Algebra R S] (hRS : Algebra.IsIntegral R S)
    (I : Ideal S) [I.IsPrime] (hI : IsMaximal (I.comap (algebraMap R S))) : IsMaximal I :=
  âŸ¨âŸ¨mt comap_eq_top_iff.mpr hI.1.1, fun _ I_lt_J =>
      let âŸ¨I_le_J, x, hxJ, hxIâŸ© := SetLike.lt_iff_le_and_exists.mp I_lt_J
      comap_eq_top_iff.1 <|
        hI.1.2 _ (comap_lt_comap_of_integral_mem_sdiff I_le_J âŸ¨hxJ, hxIâŸ© (hRS x))âŸ©âŸ©
#align ideal.is_maximal_of_is_integral_of_is_maximal_comap Ideal.isMaximal_of_isIntegral_of_isMaximal_comap

theorem isMaximal_of_isIntegral_of_isMaximal_comap' (f : R â†’+* S) (hf : f.IsIntegral) (I : Ideal S)
    [hI' : I.IsPrime] (hI : IsMaximal (I.comap f)) : IsMaximal I :=
  @isMaximal_of_isIntegral_of_isMaximal_comap R _ S _ f.toAlgebra hf I hI' hI
#align ideal.is_maximal_of_is_integral_of_is_maximal_comap' Ideal.isMaximal_of_isIntegral_of_isMaximal_comap'

variable [Algebra R S]

theorem comap_ne_bot_of_algebraic_mem [IsDomain S] {x : S} (x_ne_zero : x â‰  0) (x_mem : x âˆˆ I)
    (hx : IsAlgebraic R x) : I.comap (algebraMap R S) â‰  âŠ¥ :=
  let âŸ¨_, p_ne_zero, hpâŸ© := hx
  comap_ne_bot_of_root_mem x_ne_zero x_mem p_ne_zero hp
#align ideal.comap_ne_bot_of_algebraic_mem Ideal.comap_ne_bot_of_algebraic_mem

theorem comap_ne_bot_of_integral_mem [Nontrivial R] [IsDomain S] {x : S} (x_ne_zero : x â‰  0)
    (x_mem : x âˆˆ I) (hx : IsIntegral R x) : I.comap (algebraMap R S) â‰  âŠ¥ :=
  comap_ne_bot_of_algebraic_mem x_ne_zero x_mem (hx.isAlgebraic R)
#align ideal.comap_ne_bot_of_integral_mem Ideal.comap_ne_bot_of_integral_mem

theorem eq_bot_of_comap_eq_bot [Nontrivial R] [IsDomain S] (hRS : Algebra.IsIntegral R S)
    (hI : I.comap (algebraMap R S) = âŠ¥) : I = âŠ¥ := by
  refine' eq_bot_iff.2 fun x hx => _
  -- âŠ¢ x âˆˆ âŠ¥
  by_cases hx0 : x = 0
  -- âŠ¢ x âˆˆ âŠ¥
  Â· exact hx0.symm â–¸ Ideal.zero_mem âŠ¥
    -- ğŸ‰ no goals
  Â· exact absurd hI (comap_ne_bot_of_integral_mem hx0 hx (hRS x))
    -- ğŸ‰ no goals
#align ideal.eq_bot_of_comap_eq_bot Ideal.eq_bot_of_comap_eq_bot

theorem isMaximal_comap_of_isIntegral_of_isMaximal (hRS : Algebra.IsIntegral R S) (I : Ideal S)
    [hI : I.IsMaximal] : IsMaximal (I.comap (algebraMap R S)) := by
  refine' Ideal.Quotient.maximal_of_isField _ _
  -- âŠ¢ IsField (R â§¸ comap (algebraMap R S) I)
  haveI : IsPrime (I.comap (algebraMap R S)) := comap_isPrime _ _
  -- âŠ¢ IsField (R â§¸ comap (algebraMap R S) I)
  exact
    isField_of_isIntegral_of_isField (isIntegral_quotient_of_isIntegral hRS)
      algebraMap_quotient_injective (by rwa [â† Quotient.maximal_ideal_iff_isField_quotient])
#align ideal.is_maximal_comap_of_is_integral_of_is_maximal Ideal.isMaximal_comap_of_isIntegral_of_isMaximal

theorem isMaximal_comap_of_isIntegral_of_isMaximal' {R S : Type*} [CommRing R] [CommRing S]
    (f : R â†’+* S) (hf : f.IsIntegral) (I : Ideal S) (hI : I.IsMaximal) : IsMaximal (I.comap f) :=
  @isMaximal_comap_of_isIntegral_of_isMaximal R _ S _ f.toAlgebra hf I hI
#align ideal.is_maximal_comap_of_is_integral_of_is_maximal' Ideal.isMaximal_comap_of_isIntegral_of_isMaximal'

section IsIntegralClosure

variable (S) {A : Type*} [CommRing A]

variable [Algebra R A] [Algebra A S] [IsScalarTower R A S] [IsIntegralClosure A R S]

theorem IsIntegralClosure.comap_lt_comap {I J : Ideal A} [I.IsPrime] (I_lt_J : I < J) :
    I.comap (algebraMap R A) < J.comap (algebraMap R A) :=
  let âŸ¨I_le_J, x, hxJ, hxIâŸ© := SetLike.lt_iff_le_and_exists.mp I_lt_J
  comap_lt_comap_of_integral_mem_sdiff I_le_J âŸ¨hxJ, hxIâŸ© (IsIntegralClosure.isIntegral R S x)
#align ideal.is_integral_closure.comap_lt_comap Ideal.IsIntegralClosure.comap_lt_comap

theorem IsIntegralClosure.isMaximal_of_isMaximal_comap (I : Ideal A) [I.IsPrime]
    (hI : IsMaximal (I.comap (algebraMap R A))) : IsMaximal I :=
  isMaximal_of_isIntegral_of_isMaximal_comap (fun x => IsIntegralClosure.isIntegral R S x) I hI
#align ideal.is_integral_closure.is_maximal_of_is_maximal_comap Ideal.IsIntegralClosure.isMaximal_of_isMaximal_comap

variable [IsDomain A]

theorem IsIntegralClosure.comap_ne_bot [Nontrivial R] {I : Ideal A} (I_ne_bot : I â‰  âŠ¥) :
    I.comap (algebraMap R A) â‰  âŠ¥ :=
  let âŸ¨x, x_mem, x_ne_zeroâŸ© := I.ne_bot_iff.mp I_ne_bot
  comap_ne_bot_of_integral_mem x_ne_zero x_mem (IsIntegralClosure.isIntegral R S x)
#align ideal.is_integral_closure.comap_ne_bot Ideal.IsIntegralClosure.comap_ne_bot

theorem IsIntegralClosure.eq_bot_of_comap_eq_bot [Nontrivial R] {I : Ideal A} :
    I.comap (algebraMap R A) = âŠ¥ â†’ I = âŠ¥ := by
  -- Porting note : `imp_of_not_imp_not` seems not existing
  contrapose; exact (IsIntegralClosure.comap_ne_bot S)
  -- âŠ¢ Â¬I = âŠ¥ â†’ Â¬comap (algebraMap R A) I = âŠ¥
              -- ğŸ‰ no goals
#align ideal.is_integral_closure.eq_bot_of_comap_eq_bot Ideal.IsIntegralClosure.eq_bot_of_comap_eq_bot

end IsIntegralClosure

theorem IntegralClosure.comap_lt_comap {I J : Ideal (integralClosure R S)} [I.IsPrime]
    (I_lt_J : I < J) :
    I.comap (algebraMap R (integralClosure R S)) < J.comap (algebraMap R (integralClosure R S)) :=
  IsIntegralClosure.comap_lt_comap S I_lt_J
#align ideal.integral_closure.comap_lt_comap Ideal.IntegralClosure.comap_lt_comap

theorem IntegralClosure.isMaximal_of_isMaximal_comap (I : Ideal (integralClosure R S)) [I.IsPrime]
    (hI : IsMaximal (I.comap (algebraMap R (integralClosure R S)))) : IsMaximal I :=
  IsIntegralClosure.isMaximal_of_isMaximal_comap S I hI
#align ideal.integral_closure.is_maximal_of_is_maximal_comap Ideal.IntegralClosure.isMaximal_of_isMaximal_comap

section

variable [IsDomain S]

theorem IntegralClosure.comap_ne_bot [Nontrivial R] {I : Ideal (integralClosure R S)}
    (I_ne_bot : I â‰  âŠ¥) : I.comap (algebraMap R (integralClosure R S)) â‰  âŠ¥ :=
  IsIntegralClosure.comap_ne_bot S I_ne_bot
#align ideal.integral_closure.comap_ne_bot Ideal.IntegralClosure.comap_ne_bot

theorem IntegralClosure.eq_bot_of_comap_eq_bot [Nontrivial R] {I : Ideal (integralClosure R S)} :
    I.comap (algebraMap R (integralClosure R S)) = âŠ¥ â†’ I = âŠ¥ :=
  IsIntegralClosure.eq_bot_of_comap_eq_bot S
#align ideal.integral_closure.eq_bot_of_comap_eq_bot Ideal.IntegralClosure.eq_bot_of_comap_eq_bot

/-- `comap (algebraMap R S)` is a surjection from the prime spec of `R` to prime spec of `S`.
`hP : (algebraMap R S).ker â‰¤ P` is a slight generalization of the extension being injective -/
theorem exists_ideal_over_prime_of_isIntegral' (H : Algebra.IsIntegral R S) (P : Ideal R)
    [IsPrime P] (hP : RingHom.ker (algebraMap R S) â‰¤ P) :
    âˆƒ Q : Ideal S, IsPrime Q âˆ§ Q.comap (algebraMap R S) = P := by
  have hP0 : (0 : S) âˆ‰ Algebra.algebraMapSubmonoid S P.primeCompl := by
    rintro âŸ¨x, âŸ¨hx, x0âŸ©âŸ©
    exact absurd (hP x0) hx
  let Râ‚š := Localization P.primeCompl
  -- âŠ¢ âˆƒ Q, IsPrime Q âˆ§ comap (algebraMap R S) Q = P
  let Sâ‚š := Localization (Algebra.algebraMapSubmonoid S P.primeCompl)
  -- âŠ¢ âˆƒ Q, IsPrime Q âˆ§ comap (algebraMap R S) Q = P
  letI : IsDomain (Localization (Algebra.algebraMapSubmonoid S P.primeCompl)) :=
    IsLocalization.isDomain_localization (le_nonZeroDivisors_of_noZeroDivisors hP0)
  obtain âŸ¨Qâ‚š : Ideal Sâ‚š, Qâ‚š_maximalâŸ© := exists_maximal Sâ‚š
  -- âŠ¢ âˆƒ Q, IsPrime Q âˆ§ comap (algebraMap R S) Q = P
  haveI Qâ‚š_max : IsMaximal (comap _ Qâ‚š) :=
    @isMaximal_comap_of_isIntegral_of_isMaximal Râ‚š _ Sâ‚š _ (localizationAlgebra P.primeCompl S)
      (isIntegral_localization H) _ Qâ‚š_maximal
  refine' âŸ¨comap (algebraMap S Sâ‚š) Qâ‚š, âŸ¨comap_isPrime _ Qâ‚š, _âŸ©âŸ©
  -- âŠ¢ comap (algebraMap R S) (comap (algebraMap S Sâ‚š) Qâ‚š) = P
  convert Localization.AtPrime.comap_maximalIdeal (I := P)
  -- âŠ¢ comap (algebraMap R S) (comap (algebraMap S Sâ‚š) Qâ‚š) = comap (algebraMap R (L â€¦
  rw [comap_comap, â† LocalRing.eq_maximalIdeal Qâ‚š_max,
    â†@IsLocalization.map_comp (P := S) (Q := Sâ‚š) (g := algebraMap R S)
    (M := P.primeCompl) (T := Algebra.algebraMapSubmonoid S P.primeCompl) (S := Râ‚š) _
    _ _ _ _ _ (fun p hp => Algebra.mem_algebraMapSubmonoid_of_mem âŸ¨p, hpâŸ©) _ _]
  rfl
  -- ğŸ‰ no goals
#align ideal.exists_ideal_over_prime_of_is_integral' Ideal.exists_ideal_over_prime_of_isIntegral'

end

/-- More general going-up theorem than `exists_ideal_over_prime_of_isIntegral'`.
TODO: Version of going-up theorem with arbitrary length chains (by induction on this)?
  Not sure how best to write an ascending chain in Lean -/
theorem exists_ideal_over_prime_of_isIntegral (H : Algebra.IsIntegral R S) (P : Ideal R) [IsPrime P]
    (I : Ideal S) [IsPrime I] (hIP : I.comap (algebraMap R S) â‰¤ P) :
    âˆƒ Q â‰¥ I, IsPrime Q âˆ§ Q.comap (algebraMap R S) = P := by
  obtain âŸ¨Q' : Ideal (S â§¸ I), âŸ¨Q'_prime, hQ'âŸ©âŸ© :=
    @exists_ideal_over_prime_of_isIntegral' (R â§¸ I.comap (algebraMap R S)) _ (S â§¸ I) _
      Ideal.quotientAlgebra _ (isIntegral_quotient_of_isIntegral H)
      (map (Ideal.Quotient.mk (I.comap (algebraMap R S))) P)
      (map_isPrime_of_surjective Quotient.mk_surjective (by simp [hIP]))
      (le_trans (le_of_eq ((RingHom.injective_iff_ker_eq_bot _).1 algebraMap_quotient_injective))
        bot_le)
  refine' âŸ¨Q'.comap _, le_trans (le_of_eq mk_ker.symm) (ker_le_comap _), âŸ¨comap_isPrime _ Q', _âŸ©âŸ©
  -- âŠ¢ comap (algebraMap R S) (comap (Quotient.mk I) Q') = P
  rw [comap_comap]
  -- âŠ¢ comap (RingHom.comp (Quotient.mk I) (algebraMap R S)) Q' = P
  refine' _root_.trans _ (_root_.trans (congr_arg (comap (Ideal.Quotient.mk
    (comap (algebraMap R S) I))) hQ') _)
  Â· rw [comap_comap]
    -- âŠ¢ comap (RingHom.comp (Quotient.mk I) (algebraMap R S)) Q' = comap (RingHom.co â€¦
    exact congr_arg (comap . Q') (RingHom.ext fun r => rfl)
    -- ğŸ‰ no goals
  Â· refine' _root_.trans (comap_map_of_surjective _ Quotient.mk_surjective _) (sup_eq_left.2 _)
    -- âŠ¢ comap (Quotient.mk (comap (algebraMap R S) I)) âŠ¥ â‰¤ P
    simpa [â† RingHom.ker_eq_comap_bot] using hIP
    -- ğŸ‰ no goals
#align ideal.exists_ideal_over_prime_of_is_integral Ideal.exists_ideal_over_prime_of_isIntegral

/-- `comap (algebraMap R S)` is a surjection from the max spec of `S` to max spec of `R`.
`hP : (algebraMap R S).ker â‰¤ P` is a slight generalization of the extension being injective -/
theorem exists_ideal_over_maximal_of_isIntegral [IsDomain S] (H : Algebra.IsIntegral R S)
    (P : Ideal R) [P_max : IsMaximal P] (hP : RingHom.ker (algebraMap R S) â‰¤ P) :
    âˆƒ Q : Ideal S, IsMaximal Q âˆ§ Q.comap (algebraMap R S) = P := by
  obtain âŸ¨Q, âŸ¨Q_prime, hQâŸ©âŸ© := exists_ideal_over_prime_of_isIntegral' H P hP
  -- âŠ¢ âˆƒ Q, IsMaximal Q âˆ§ comap (algebraMap R S) Q = P
  exact âŸ¨Q, isMaximal_of_isIntegral_of_isMaximal_comap H _ (hQ.symm â–¸ P_max), hQâŸ©
  -- ğŸ‰ no goals
#align ideal.exists_ideal_over_maximal_of_is_integral Ideal.exists_ideal_over_maximal_of_isIntegral

end IsDomain

end Ideal
