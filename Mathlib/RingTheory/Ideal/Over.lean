/-
Copyright (c) 2020 Anne Baanen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anne Baanen
-/
import Mathlib.RingTheory.Ideal.Pointwise
import Mathlib.RingTheory.Localization.AtPrime
import Mathlib.RingTheory.Localization.Integral

/-!
# Ideals over/under ideals

This file concerns ideals lying over other ideals.
Let `f : R ‚Üí+* S` be a ring homomorphism (typically a ring extension), `I` an ideal of `R` and
`J` an ideal of `S`. We say `J` lies over `I` (and `I` under `J`) if `I` is the `f`-preimage of `J`.
This is expressed here by writing `I = J.comap f`.

## Implementation notes

The proofs of the `comap_ne_bot` and `comap_lt_comap` families use an approach
specific for their situation: we construct an element in `I.comap f` from the
coefficients of a minimal polynomial.
Once mathlib has more material on the localization at a prime ideal, the results
can be proven using more general going-up/going-down theory.
-/


variable {R : Type*} [CommRing R]

namespace Ideal

open Polynomial Submodule

open scoped Pointwise

section CommRing

variable {S : Type*} [CommRing S] {f : R ‚Üí+* S} {I J : Ideal S}

theorem coeff_zero_mem_comap_of_root_mem_of_eval_mem {r : S} (hr : r ‚àà I) {p : R[X]}
    (hp : p.eval‚ÇÇ f r ‚àà I) : p.coeff 0 ‚àà I.comap f := by
  rw [‚Üê p.divX_mul_X_add, eval‚ÇÇ_add, eval‚ÇÇ_C, eval‚ÇÇ_mul, eval‚ÇÇ_X] at hp
  refine mem_comap.mpr ((I.add_mem_iff_right ?_).mp hp)
  exact I.mul_mem_left _ hr

theorem coeff_zero_mem_comap_of_root_mem {r : S} (hr : r ‚àà I) {p : R[X]} (hp : p.eval‚ÇÇ f r = 0) :
    p.coeff 0 ‚àà I.comap f :=
  coeff_zero_mem_comap_of_root_mem_of_eval_mem hr (hp.symm ‚ñ∏ I.zero_mem)

theorem exists_coeff_ne_zero_mem_comap_of_non_zero_divisor_root_mem {r : S}
    (r_non_zero_divisor : ‚àÄ {x}, x * r = 0 ‚Üí x = 0) (hr : r ‚àà I) {p : R[X]} :
    p ‚â† 0 ‚Üí p.eval‚ÇÇ f r = 0 ‚Üí ‚àÉ i, p.coeff i ‚â† 0 ‚àß p.coeff i ‚àà I.comap f := by
  refine p.recOnHorner ?_ ?_ ?_
  ¬∑ intro h
    contradiction
  ¬∑ intro p a coeff_eq_zero a_ne_zero _ _ hp
    refine ‚ü®0, ?_, coeff_zero_mem_comap_of_root_mem hr hp‚ü©
    simp [coeff_eq_zero, a_ne_zero]
  ¬∑ intro p p_nonzero ih _ hp
    rw [eval‚ÇÇ_mul, eval‚ÇÇ_X] at hp
    obtain ‚ü®i, hi, mem‚ü© := ih p_nonzero (r_non_zero_divisor hp)
    refine ‚ü®i + 1, ?_, ?_‚ü©
    ¬∑ simp [hi, mem]
    ¬∑ simpa [hi] using mem

/-- Let `P` be an ideal in `R[x]`.  The map
`R[x]/P ‚Üí (R / (P ‚à© R))[x] / (P / (P ‚à© R))`
is injective.
-/
theorem injective_quotient_le_comap_map (P : Ideal R[X]) :
    Function.Injective <|
      Ideal.quotientMap
        (Ideal.map (Polynomial.mapRingHom (Quotient.mk (P.comap (C : R ‚Üí+* R[X])))) P)
        (Polynomial.mapRingHom (Ideal.Quotient.mk (P.comap (C : R ‚Üí+* R[X]))))
        le_comap_map := by
  refine quotientMap_injective' (le_of_eq ?_)
  rw [comap_map_of_surjective (mapRingHom (Ideal.Quotient.mk (P.comap (C : R ‚Üí+* R[X]))))
      (map_surjective (Ideal.Quotient.mk (P.comap (C : R ‚Üí+* R[X]))) Ideal.Quotient.mk_surjective)]
  refine le_antisymm (sup_le le_rfl ?_) (le_sup_of_le_left le_rfl)
  refine fun p hp =>
    polynomial_mem_ideal_of_coeff_mem_ideal P p fun n => Ideal.Quotient.eq_zero_iff_mem.mp ?_
  simpa only [coeff_map, coe_mapRingHom] using ext_iff.mp (Ideal.mem_bot.mp (mem_comap.mp hp)) n

/-- The identity in this lemma asserts that the "obvious" square
```
    R    ‚Üí (R / (P ‚à© R))
    ‚Üì          ‚Üì
R[x] / P ‚Üí (R / (P ‚à© R))[x] / (P / (P ‚à© R))
```
commutes.  It is used, for instance, in the proof of `quotient_mk_comp_C_is_integral_of_jacobson`,
in the file `RingTheory.Jacobson`.
-/
theorem quotient_mk_maps_eq (P : Ideal R[X]) :
    ((Quotient.mk (map (mapRingHom (Quotient.mk (P.comap (C : R ‚Üí+* R[X])))) P)).comp C).comp
        (Quotient.mk (P.comap (C : R ‚Üí+* R[X]))) =
      (Ideal.quotientMap (map (mapRingHom (Quotient.mk (P.comap (C : R ‚Üí+* R[X])))) P)
            (mapRingHom (Quotient.mk (P.comap (C : R ‚Üí+* R[X])))) le_comap_map).comp
        ((Quotient.mk P).comp C) := by
  refine RingHom.ext fun x => ?_
  repeat' rw [RingHom.coe_comp, Function.comp_apply]
  rw [quotientMap_mk, coe_mapRingHom, map_C]

/-- This technical lemma asserts the existence of a polynomial `p` in an ideal `P ‚äÇ R[x]`
that is non-zero in the quotient `R / (P ‚à© R) [x]`.  The assumptions are equivalent to
`P ‚â† 0` and `P ‚à© R = (0)`.
-/
theorem exists_nonzero_mem_of_ne_bot {P : Ideal R[X]} (Pb : P ‚â† ‚ä•) (hP : ‚àÄ x : R, C x ‚àà P ‚Üí x = 0) :
    ‚àÉ p : R[X], p ‚àà P ‚àß Polynomial.map (Quotient.mk (P.comap (C : R ‚Üí+* R[X]))) p ‚â† 0 := by
  obtain ‚ü®m, hm‚ü© := Submodule.nonzero_mem_of_bot_lt (bot_lt_iff_ne_bot.mpr Pb)
  refine ‚ü®m, Submodule.coe_mem m, fun pp0 => hm (Submodule.coe_eq_zero.mp ?_)‚ü©
  refine
    (injective_iff_map_eq_zero (Polynomial.mapRingHom (Ideal.Quotient.mk
      (P.comap (C : R ‚Üí+* R[X]))))).mp
      ?_ _ pp0
  refine map_injective _ ((Ideal.Quotient.mk (P.comap C)).injective_iff_ker_eq_bot.mpr ?_)
  rw [mk_ker]
  exact (Submodule.eq_bot_iff _).mpr fun x hx => hP x (mem_comap.mp hx)

variable {p : Ideal R} {P : Ideal S}

/-- If there is an injective map `R/p ‚Üí S/P` such that following diagram commutes:
```
R   ‚Üí S
‚Üì     ‚Üì
R/p ‚Üí S/P
```
then `P` lies over `p`.
-/
theorem comap_eq_of_scalar_tower_quotient [Algebra R S] [Algebra (R ‚ß∏ p) (S ‚ß∏ P)]
    [IsScalarTower R (R ‚ß∏ p) (S ‚ß∏ P)] (h : Function.Injective (algebraMap (R ‚ß∏ p) (S ‚ß∏ P))) :
    comap (algebraMap R S) P = p := by
  ext x
  rw [mem_comap, ‚Üê Quotient.eq_zero_iff_mem, ‚Üê Quotient.eq_zero_iff_mem, Quotient.mk_algebraMap,
    IsScalarTower.algebraMap_apply R (R ‚ß∏ p) (S ‚ß∏ P), Quotient.algebraMap_eq]
  constructor
  ¬∑ intro hx
    exact (injective_iff_map_eq_zero (algebraMap (R ‚ß∏ p) (S ‚ß∏ P))).mp h _ hx
  ¬∑ intro hx
    rw [hx, RingHom.map_zero]

/-- If `P` lies over `p`, then `R / p` has a canonical map to `S / P`. -/
def Quotient.algebraQuotientOfLEComap (h : p ‚â§ comap f P) : Algebra (R ‚ß∏ p) (S ‚ß∏ P) :=
  RingHom.toAlgebra <| quotientMap _ f h

/-- `R / p` has a canonical map to `S / pS`. -/
instance Quotient.algebraQuotientMapQuotient : Algebra (R ‚ß∏ p) (S ‚ß∏ map f p) :=
  Ideal.Quotient.algebraQuotientOfLEComap le_comap_map

@[simp]
theorem Quotient.algebraMap_quotient_map_quotient (x : R) :
    algebraMap (R ‚ß∏ p) (S ‚ß∏ map f p) (Ideal.Quotient.mk p x) =
    Ideal.Quotient.mk (map f p) (f x) :=
  rfl

@[simp]
theorem Quotient.mk_smul_mk_quotient_map_quotient (x : R) (y : S) :
    Quotient.mk p x ‚Ä¢ Quotient.mk (map f p) y = Quotient.mk (map f p) (f x * y) :=
  rfl

instance Quotient.tower_quotient_map_quotient [Algebra R S] :
    IsScalarTower R (R ‚ß∏ p) (S ‚ß∏ map (algebraMap R S) p) :=
  IsScalarTower.of_algebraMap_eq fun x => by
    rw [Quotient.algebraMap_eq, Quotient.algebraMap_quotient_map_quotient,
      Quotient.mk_algebraMap]

instance QuotientMapQuotient.isNoetherian [Algebra R S] [IsNoetherian R S] (I : Ideal R) :
    IsNoetherian (R ‚ß∏ I) (S ‚ß∏ Ideal.map (algebraMap R S) I) :=
  isNoetherian_of_tower R <|
    isNoetherian_of_surjective S (Ideal.Quotient.mk‚Çê R _).toLinearMap <|
      LinearMap.range_eq_top.mpr Ideal.Quotient.mk_surjective

end CommRing

section IsDomain

variable {S : Type*} [CommRing S] {f : R ‚Üí+* S} {I J : Ideal S}

theorem exists_coeff_ne_zero_mem_comap_of_root_mem [IsDomain S] {r : S} (r_ne_zero : r ‚â† 0)
    (hr : r ‚àà I) {p : R[X]} :
    p ‚â† 0 ‚Üí p.eval‚ÇÇ f r = 0 ‚Üí ‚àÉ i, p.coeff i ‚â† 0 ‚àß p.coeff i ‚àà I.comap f :=
  exists_coeff_ne_zero_mem_comap_of_non_zero_divisor_root_mem
    (fun {_} h => Or.resolve_right (mul_eq_zero.mp h) r_ne_zero) hr

theorem exists_coeff_mem_comap_sdiff_comap_of_root_mem_sdiff [IsPrime I] (hIJ : I ‚â§ J) {r : S}
    (hr : r ‚àà (J : Set S) \ I) {p : R[X]} (p_ne_zero : p.map (Quotient.mk (I.comap f)) ‚â† 0)
    (hpI : p.eval‚ÇÇ f r ‚àà I) : ‚àÉ i, p.coeff i ‚àà (J.comap f : Set R) \ I.comap f := by
  obtain ‚ü®hrJ, hrI‚ü© := hr
  have rbar_ne_zero : Ideal.Quotient.mk I r ‚â† 0 := mt (Quotient.mk_eq_zero I).mp hrI
  have rbar_mem_J : Ideal.Quotient.mk I r ‚àà J.map (Ideal.Quotient.mk I) := mem_map_of_mem _ hrJ
  have quotient_f : ‚àÄ x ‚àà I.comap f, (Ideal.Quotient.mk I).comp f x = 0 := by
    simp [Quotient.eq_zero_iff_mem]
  have rbar_root :
    (p.map (Ideal.Quotient.mk (I.comap f))).eval‚ÇÇ (Quotient.lift (I.comap f) _ quotient_f)
        (Ideal.Quotient.mk I r) =
      0 := by
    convert Quotient.eq_zero_iff_mem.mpr hpI
    exact _root_.trans (eval‚ÇÇ_map _ _ _) (hom_eval‚ÇÇ p f (Ideal.Quotient.mk I) r).symm
  obtain ‚ü®i, ne_zero, mem‚ü© :=
    exists_coeff_ne_zero_mem_comap_of_root_mem rbar_ne_zero rbar_mem_J p_ne_zero rbar_root
  rw [coeff_map] at ne_zero mem
  refine ‚ü®i, (mem_quotient_iff_mem hIJ).mp ?_, mt ?_ ne_zero‚ü©
  ¬∑ simpa using mem
  simp [Quotient.eq_zero_iff_mem]

theorem comap_lt_comap_of_root_mem_sdiff [I.IsPrime] (hIJ : I ‚â§ J) {r : S}
    (hr : r ‚àà (J : Set S) \ I) {p : R[X]} (p_ne_zero : p.map (Quotient.mk (I.comap f)) ‚â† 0)
    (hp : p.eval‚ÇÇ f r ‚àà I) : I.comap f < J.comap f :=
  let ‚ü®i, hJ, hI‚ü© := exists_coeff_mem_comap_sdiff_comap_of_root_mem_sdiff hIJ hr p_ne_zero hp
  SetLike.lt_iff_le_and_exists.mpr ‚ü®comap_mono hIJ, p.coeff i, hJ, hI‚ü©

theorem mem_of_one_mem (h : (1 : S) ‚àà I) (x) : x ‚àà I :=
  (I.eq_top_iff_one.mpr h).symm ‚ñ∏ mem_top

theorem comap_lt_comap_of_integral_mem_sdiff [Algebra R S] [hI : I.IsPrime] (hIJ : I ‚â§ J) {x : S}
    (mem : x ‚àà (J : Set S) \ I) (integral : IsIntegral R x) :
    I.comap (algebraMap R S) < J.comap (algebraMap R S) := by
  obtain ‚ü®p, p_monic, hpx‚ü© := integral
  refine comap_lt_comap_of_root_mem_sdiff hIJ mem (map_monic_ne_zero p_monic) ?_
  convert I.zero_mem

theorem comap_ne_bot_of_root_mem [IsDomain S] {r : S} (r_ne_zero : r ‚â† 0) (hr : r ‚àà I) {p : R[X]}
    (p_ne_zero : p ‚â† 0) (hp : p.eval‚ÇÇ f r = 0) : I.comap f ‚â† ‚ä• := fun h =>
  let ‚ü®_, hi, mem‚ü© := exists_coeff_ne_zero_mem_comap_of_root_mem r_ne_zero hr p_ne_zero hp
  absurd (mem_bot.mp (eq_bot_iff.mp h mem)) hi

theorem isMaximal_of_isIntegral_of_isMaximal_comap [Algebra R S] [Algebra.IsIntegral R S]
    (I : Ideal S) [I.IsPrime] (hI : IsMaximal (I.comap (algebraMap R S))) : IsMaximal I :=
  ‚ü®‚ü®mt comap_eq_top_iff.mpr hI.1.1, fun _ I_lt_J =>
      let ‚ü®I_le_J, x, hxJ, hxI‚ü© := SetLike.lt_iff_le_and_exists.mp I_lt_J
      comap_eq_top_iff.1 <|
        hI.1.2 _ (comap_lt_comap_of_integral_mem_sdiff I_le_J ‚ü®hxJ, hxI‚ü©
          (Algebra.IsIntegral.isIntegral x))‚ü©‚ü©

theorem isMaximal_of_isIntegral_of_isMaximal_comap' (f : R ‚Üí+* S) (hf : f.IsIntegral) (I : Ideal S)
    [I.IsPrime] (hI : IsMaximal (I.comap f)) : IsMaximal I :=
  let _ : Algebra R S := f.toAlgebra
  have : Algebra.IsIntegral R S := ‚ü®hf‚ü©
  isMaximal_of_isIntegral_of_isMaximal_comap (R := R) (S := S) I hI

variable [Algebra R S]

theorem comap_ne_bot_of_algebraic_mem [IsDomain S] {x : S} (x_ne_zero : x ‚â† 0) (x_mem : x ‚àà I)
    (hx : IsAlgebraic R x) : I.comap (algebraMap R S) ‚â† ‚ä• :=
  let ‚ü®_, p_ne_zero, hp‚ü© := hx
  comap_ne_bot_of_root_mem x_ne_zero x_mem p_ne_zero hp

theorem comap_ne_bot_of_integral_mem [Nontrivial R] [IsDomain S] {x : S} (x_ne_zero : x ‚â† 0)
    (x_mem : x ‚àà I) (hx : IsIntegral R x) : I.comap (algebraMap R S) ‚â† ‚ä• :=
  comap_ne_bot_of_algebraic_mem x_ne_zero x_mem hx.isAlgebraic

theorem eq_bot_of_comap_eq_bot [Nontrivial R] [IsDomain S] [Algebra.IsIntegral R S]
    (hI : I.comap (algebraMap R S) = ‚ä•) : I = ‚ä• := by
  refine eq_bot_iff.2 fun x hx => ?_
  by_cases hx0 : x = 0
  ¬∑ exact hx0.symm ‚ñ∏ Ideal.zero_mem ‚ä•
  ¬∑ exact absurd hI (comap_ne_bot_of_integral_mem hx0 hx (Algebra.IsIntegral.isIntegral x))

theorem isMaximal_comap_of_isIntegral_of_isMaximal [Algebra.IsIntegral R S] (I : Ideal S)
    [hI : I.IsMaximal] : IsMaximal (I.comap (algebraMap R S)) := by
  refine Ideal.Quotient.maximal_of_isField _ ?_
  haveI : IsPrime (I.comap (algebraMap R S)) := comap_isPrime _ _
  exact isField_of_isIntegral_of_isField
    algebraMap_quotient_injective (by rwa [‚Üê Quotient.maximal_ideal_iff_isField_quotient])

theorem isMaximal_comap_of_isIntegral_of_isMaximal' {R S : Type*} [CommRing R] [CommRing S]
    (f : R ‚Üí+* S) (hf : f.IsIntegral) (I : Ideal S) [I.IsMaximal] : IsMaximal (I.comap f) :=
  let _ : Algebra R S := f.toAlgebra
  have : Algebra.IsIntegral R S := ‚ü®hf‚ü©
  isMaximal_comap_of_isIntegral_of_isMaximal (R := R) (S := S) I

section IsIntegralClosure

variable (S) {A : Type*} [CommRing A]
variable [Algebra R A] [Algebra A S] [IsScalarTower R A S] [IsIntegralClosure A R S]
include S

theorem IsIntegralClosure.comap_lt_comap {I J : Ideal A} [I.IsPrime] (I_lt_J : I < J) :
    I.comap (algebraMap R A) < J.comap (algebraMap R A) :=
  let ‚ü®I_le_J, x, hxJ, hxI‚ü© := SetLike.lt_iff_le_and_exists.mp I_lt_J
  comap_lt_comap_of_integral_mem_sdiff I_le_J ‚ü®hxJ, hxI‚ü© (IsIntegralClosure.isIntegral R S x)

theorem IsIntegralClosure.isMaximal_of_isMaximal_comap (I : Ideal A) [I.IsPrime]
    (hI : IsMaximal (I.comap (algebraMap R A))) : IsMaximal I :=
  have : Algebra.IsIntegral R A := IsIntegralClosure.isIntegral_algebra R S
  isMaximal_of_isIntegral_of_isMaximal_comap I hI

variable [IsDomain A]

theorem IsIntegralClosure.comap_ne_bot [Nontrivial R] {I : Ideal A} (I_ne_bot : I ‚â† ‚ä•) :
    I.comap (algebraMap R A) ‚â† ‚ä• :=
  let ‚ü®x, x_mem, x_ne_zero‚ü© := I.ne_bot_iff.mp I_ne_bot
  comap_ne_bot_of_integral_mem x_ne_zero x_mem (IsIntegralClosure.isIntegral R S x)

theorem IsIntegralClosure.eq_bot_of_comap_eq_bot [Nontrivial R] {I : Ideal A} :
    I.comap (algebraMap R A) = ‚ä• ‚Üí I = ‚ä• := by
  -- Porting note: `imp_of_not_imp_not` seems not existing
  contrapose; exact (IsIntegralClosure.comap_ne_bot S)

end IsIntegralClosure

theorem IntegralClosure.comap_lt_comap {I J : Ideal (integralClosure R S)} [I.IsPrime]
    (I_lt_J : I < J) :
    I.comap (algebraMap R (integralClosure R S)) < J.comap (algebraMap R (integralClosure R S)) :=
  IsIntegralClosure.comap_lt_comap S I_lt_J

theorem IntegralClosure.isMaximal_of_isMaximal_comap (I : Ideal (integralClosure R S)) [I.IsPrime]
    (hI : IsMaximal (I.comap (algebraMap R (integralClosure R S)))) : IsMaximal I :=
  IsIntegralClosure.isMaximal_of_isMaximal_comap S I hI

section

variable [IsDomain S]

theorem IntegralClosure.comap_ne_bot [Nontrivial R] {I : Ideal (integralClosure R S)}
    (I_ne_bot : I ‚â† ‚ä•) : I.comap (algebraMap R (integralClosure R S)) ‚â† ‚ä• :=
  IsIntegralClosure.comap_ne_bot S I_ne_bot

theorem IntegralClosure.eq_bot_of_comap_eq_bot [Nontrivial R] {I : Ideal (integralClosure R S)} :
    I.comap (algebraMap R (integralClosure R S)) = ‚ä• ‚Üí I = ‚ä• :=
  IsIntegralClosure.eq_bot_of_comap_eq_bot S

/-- `comap (algebraMap R S)` is a surjection from the prime spec of `R` to prime spec of `S`.
`hP : (algebraMap R S).ker ‚â§ P` is a slight generalization of the extension being injective -/
theorem exists_ideal_over_prime_of_isIntegral_of_isDomain [Algebra.IsIntegral R S] (P : Ideal R)
    [IsPrime P] (hP : RingHom.ker (algebraMap R S) ‚â§ P) :
    ‚àÉ Q : Ideal S, IsPrime Q ‚àß Q.comap (algebraMap R S) = P := by
  have hP0 : (0 : S) ‚àâ Algebra.algebraMapSubmonoid S P.primeCompl := by
    rintro ‚ü®x, ‚ü®hx, x0‚ü©‚ü©
    exact absurd (hP x0) hx
  let R‚Çö := Localization P.primeCompl
  let S‚Çö := Localization (Algebra.algebraMapSubmonoid S P.primeCompl)
  letI : IsDomain (Localization (Algebra.algebraMapSubmonoid S P.primeCompl)) :=
    IsLocalization.isDomain_localization (le_nonZeroDivisors_of_noZeroDivisors hP0)
  obtain ‚ü®Q‚Çö : Ideal S‚Çö, Q‚Çö_maximal‚ü© := exists_maximal S‚Çö
  let _ : Algebra R‚Çö S‚Çö := localizationAlgebra P.primeCompl S
  have : Algebra.IsIntegral R‚Çö S‚Çö := ‚ü®isIntegral_localization‚ü©
  have Q‚Çö_max : IsMaximal (comap _ Q‚Çö) :=
    isMaximal_comap_of_isIntegral_of_isMaximal (R := R‚Çö) (S := S‚Çö) Q‚Çö
  refine ‚ü®comap (algebraMap S S‚Çö) Q‚Çö, ‚ü®comap_isPrime _ Q‚Çö, ?_‚ü©‚ü©
  convert Localization.AtPrime.comap_maximalIdeal (I := P)
  rw [comap_comap, ‚Üê LocalRing.eq_maximalIdeal Q‚Çö_max,
    ‚Üê IsLocalization.map_comp (P := S) (Q := S‚Çö) (g := algebraMap R S)
    (M := P.primeCompl) (T := Algebra.algebraMapSubmonoid S P.primeCompl) (S := R‚Çö)
    (fun p hp => Algebra.mem_algebraMapSubmonoid_of_mem ‚ü®p, hp‚ü©) ]
  rfl

end

/-- More general going-up theorem than `exists_ideal_over_prime_of_isIntegral_of_isDomain`.
TODO: Version of going-up theorem with arbitrary length chains (by induction on this)?
  Not sure how best to write an ascending chain in Lean -/
theorem exists_ideal_over_prime_of_isIntegral_of_isPrime
    [Algebra.IsIntegral R S] (P : Ideal R) [IsPrime P]
    (I : Ideal S) [IsPrime I] (hIP : I.comap (algebraMap R S) ‚â§ P) :
    ‚àÉ Q ‚â• I, IsPrime Q ‚àß Q.comap (algebraMap R S) = P := by
  obtain ‚ü®Q' : Ideal (S ‚ß∏ I), ‚ü®Q'_prime, hQ'‚ü©‚ü© :=
    @exists_ideal_over_prime_of_isIntegral_of_isDomain (R ‚ß∏ I.comap (algebraMap R S)) _ (S ‚ß∏ I) _
      Ideal.quotientAlgebra _ _
      (map (Ideal.Quotient.mk (I.comap (algebraMap R S))) P)
      (map_isPrime_of_surjective Quotient.mk_surjective (by simp [hIP]))
      (le_trans (le_of_eq ((RingHom.injective_iff_ker_eq_bot _).1 algebraMap_quotient_injective))
        bot_le)
  refine ‚ü®Q'.comap _, le_trans (le_of_eq mk_ker.symm) (ker_le_comap _), ‚ü®comap_isPrime _ Q', ?_‚ü©‚ü©
  rw [comap_comap]
  refine _root_.trans ?_ (_root_.trans (congr_arg (comap (Ideal.Quotient.mk
    (comap (algebraMap R S) I))) hQ') ?_)
  ¬∑ rw [comap_comap]
    exact congr_arg (comap ¬∑ Q') (RingHom.ext fun r => rfl)
  ¬∑ refine _root_.trans (comap_map_of_surjective _ Quotient.mk_surjective _) (sup_eq_left.2 ?_)
    simpa [‚Üê RingHom.ker_eq_comap_bot] using hIP

lemma exists_ideal_comap_le_prime (P : Ideal R) [P.IsPrime]
    (I : Ideal S) (hI : I.comap (algebraMap R S) ‚â§ P) :
    ‚àÉ Q ‚â• I, Q.IsPrime ‚àß Q.comap (algebraMap R S) ‚â§ P := by
  let S‚Çö := Localization (Algebra.algebraMapSubmonoid S P.primeCompl)
  let I‚Çö := I.map (algebraMap S S‚Çö)
  have hI' : Disjoint (Algebra.algebraMapSubmonoid S P.primeCompl : Set S) I := by
    rw [Set.disjoint_iff]
    rintro _ ‚ü®‚ü®x, hx : x ‚àâ P, rfl‚ü©, hx'‚ü©
    exact (hx (hI hx')).elim
  have : I‚Çö ‚â† ‚ä§ := by
    rw [Ne, Ideal.eq_top_iff_one, IsLocalization.mem_map_algebraMap_iff
      (Algebra.algebraMapSubmonoid S P.primeCompl) S‚Çö, not_exists]
    simp only [one_mul, IsLocalization.eq_iff_exists (Algebra.algebraMapSubmonoid S P.primeCompl),
      not_exists]
    exact fun x c ‚Ü¶ hI'.ne_of_mem (mul_mem c.2 x.2.2) (I.mul_mem_left c x.1.2)
  obtain ‚ü®M, hM, hM'‚ü© := Ideal.exists_le_maximal _ this
  refine ‚ü®_, Ideal.map_le_iff_le_comap.mp hM', hM.isPrime.comap _, ?_‚ü©
  intro x hx
  by_contra hx'
  exact Set.disjoint_left.mp ((IsLocalization.isPrime_iff_isPrime_disjoint
    (Algebra.algebraMapSubmonoid S P.primeCompl) S‚Çö M).mp hM.isPrime).2 ‚ü®_, hx', rfl‚ü© hx

theorem exists_ideal_over_prime_of_isIntegral [Algebra.IsIntegral R S] (P : Ideal R) [IsPrime P]
    (I : Ideal S) (hIP : I.comap (algebraMap R S) ‚â§ P) :
    ‚àÉ Q ‚â• I, IsPrime Q ‚àß Q.comap (algebraMap R S) = P := by
  have ‚ü®P', hP, hP', hP''‚ü© := exists_ideal_comap_le_prime P I hIP
  obtain ‚ü®Q, hQ, hQ', hQ''‚ü© := exists_ideal_over_prime_of_isIntegral_of_isPrime P P' hP''
  exact ‚ü®Q, hP.trans hQ, hQ', hQ''‚ü©

/-- `comap (algebraMap R S)` is a surjection from the max spec of `S` to max spec of `R`.
`hP : (algebraMap R S).ker ‚â§ P` is a slight generalization of the extension being injective -/
theorem exists_ideal_over_maximal_of_isIntegral [Algebra.IsIntegral R S]
    (P : Ideal R) [P_max : IsMaximal P] (hP : RingHom.ker (algebraMap R S) ‚â§ P) :
    ‚àÉ Q : Ideal S, IsMaximal Q ‚àß Q.comap (algebraMap R S) = P := by
  obtain ‚ü®Q, -, Q_prime, hQ‚ü© := exists_ideal_over_prime_of_isIntegral P ‚ä• hP
  exact ‚ü®Q, isMaximal_of_isIntegral_of_isMaximal_comap _ (hQ.symm ‚ñ∏ P_max), hQ‚ü©

lemma map_eq_top_iff_of_ker_le {R S} [CommRing R] [CommRing S]
    (f : R ‚Üí+* S) {I : Ideal R} (hf‚ÇÅ : RingHom.ker f ‚â§ I) (hf‚ÇÇ : f.IsIntegral) :
    I.map f = ‚ä§ ‚Üî I = ‚ä§ := by
  constructor; swap
  ¬∑ rintro rfl; exact Ideal.map_top _
  contrapose
  intro h
  obtain ‚ü®m, _, hm‚ü© := Ideal.exists_le_maximal I h
  let _ := f.toAlgebra
  have : Algebra.IsIntegral _ _ := ‚ü®hf‚ÇÇ‚ü©
  obtain ‚ü®m', _, rfl‚ü© := exists_ideal_over_maximal_of_isIntegral m (hf‚ÇÅ.trans hm)
  rw [‚Üê map_le_iff_le_comap] at hm
  exact (hm.trans_lt (lt_top_iff_ne_top.mpr (IsMaximal.ne_top ‚Äπ_‚Ä∫))).ne

lemma map_eq_top_iff {R S} [CommRing R] [CommRing S]
    (f : R ‚Üí+* S) {I : Ideal R} (hf‚ÇÅ : Function.Injective f) (hf‚ÇÇ : f.IsIntegral) :
    I.map f = ‚ä§ ‚Üî I = ‚ä§ :=
  map_eq_top_iff_of_ker_le f (by simp [f.injective_iff_ker_eq_bot.mp hf‚ÇÅ]) hf‚ÇÇ

end IsDomain

section ideal_liesOver

section Semiring

variable (A : Type*) [CommSemiring A] {B : Type*} [Semiring B] [Algebra A B]
  (P : Ideal B) (p : Ideal A)

/-- The ideal obtained by pulling back the ideal `P` from `B` to `A`. -/
abbrev under : Ideal A := Ideal.comap (algebraMap A B) P

theorem under_def : P.under A = Ideal.comap (algebraMap A B) P := rfl

instance IsPrime.under [hP : P.IsPrime] : (P.under A).IsPrime :=
  hP.comap (algebraMap A B)

@[simp]
lemma under_smul {G : Type*} [Group G] [MulSemiringAction G B] [SMulCommClass G A B] (g : G) :
    (g ‚Ä¢ P : Ideal B).under A = P.under A := by
  ext a
  rw [mem_comap, mem_comap, mem_pointwise_smul_iff_inv_smul_mem, smul_algebraMap]

variable {A}

/-- `P` lies over `p` if `p` is the preimage of `P` of the `algebraMap`. -/
class LiesOver : Prop where
  over : p = P.under A

instance over_under : P.LiesOver (P.under A) where over := rfl

theorem over_def [P.LiesOver p] : p = P.under A := LiesOver.over

theorem mem_of_liesOver [P.LiesOver p] (x : A) : x ‚àà p ‚Üî algebraMap A B x ‚àà P := by
  rw [P.over_def p]
  rfl

end Semiring

section CommSemiring

variable {A : Type*} [CommSemiring A] {B : Type*} [CommSemiring B] {C : Type*} [Semiring C]
  [Algebra A B] [Algebra B C] [Algebra A C] [IsScalarTower A B C]
  (ùîì : Ideal C) (P : Ideal B) (p : Ideal A)

@[simp]
theorem under_under : (ùîì.under B).under A  = ùîì.under A := by
  simp_rw [comap_comap, ‚Üê IsScalarTower.algebraMap_eq]

theorem LiesOver.trans [ùîì.LiesOver P] [P.LiesOver p] : ùîì.LiesOver p where
  over := by rw [P.over_def p, ùîì.over_def P, under_under]

theorem LiesOver.tower_bot [hp : ùîì.LiesOver p] [hP : ùîì.LiesOver P] : P.LiesOver p where
  over := by rw [ùîì.over_def p, ùîì.over_def P, under_under]

variable (B)

instance under_liesOver_of_liesOver [ùîì.LiesOver p] : (ùîì.under B).LiesOver p :=
  LiesOver.tower_bot ùîì (ùîì.under B) p

end CommSemiring

section CommRing

namespace Quotient

variable (R : Type*) [CommSemiring R] {A B : Type*} [CommRing A] [CommRing B] [Algebra A B]
  [Algebra R A] [Algebra R B] [IsScalarTower R A B] (P : Ideal B) (p : Ideal A) [P.LiesOver p]

/-- If `P` lies over `p`, then canonically `B ‚ß∏ P` is a `A ‚ß∏ p`-algebra. -/
instance algebraOfLiesOver : Algebra (A ‚ß∏ p) (B ‚ß∏ P) :=
  algebraQuotientOfLEComap (le_of_eq (P.over_def p))

instance isScalarTower_of_liesOver : IsScalarTower R (A ‚ß∏ p) (B ‚ß∏ P) :=
  IsScalarTower.of_algebraMap_eq' <|
    congrArg (algebraMap B (B ‚ß∏ P)).comp (IsScalarTower.algebraMap_eq R A B)

/-- `B ‚ß∏ P` is a finite `A ‚ß∏ p`-module if `B` is a finite `A`-module. -/
instance module_finite_of_liesOver [Module.Finite A B] : Module.Finite (A ‚ß∏ p) (B ‚ß∏ P) :=
  Module.Finite.of_restrictScalars_finite A (A ‚ß∏ p) (B ‚ß∏ P)

example [Module.Finite A B] : Module.Finite (A ‚ß∏ P.under A) (B ‚ß∏ P) := inferInstance

/-- `B ‚ß∏ P` is a finitely generated `A ‚ß∏ p`-algebra if `B` is a finitely generated `A`-algebra. -/
instance algebra_finiteType_of_liesOver [Algebra.FiniteType A B] :
    Algebra.FiniteType (A ‚ß∏ p) (B ‚ß∏ P) :=
  Algebra.FiniteType.of_restrictScalars_finiteType A (A ‚ß∏ p) (B ‚ß∏ P)

/-- `B ‚ß∏ P` is a Noetherian `A ‚ß∏ p`-module if `B` is a Noetherian `A`-module. -/
instance isNoetherian_of_liesOver [IsNoetherian A B] : IsNoetherian (A ‚ß∏ p) (B ‚ß∏ P) :=
  isNoetherian_of_tower A inferInstance

theorem algebraMap_injective_of_liesOver :
    Function.Injective (algebraMap (A ‚ß∏ p) (B ‚ß∏ P)) := by
  rintro ‚ü®a‚ü© ‚ü®b‚ü© hab
  apply Quotient.eq.mpr ((mem_of_liesOver P p (a - b)).mpr _)
  rw [RingHom.map_sub]
  exact Quotient.eq.mp hab

instance [P.IsPrime] : NoZeroSMulDivisors (A ‚ß∏ p) (B ‚ß∏ P) :=
  NoZeroSMulDivisors.of_algebraMap_injective (Quotient.algebraMap_injective_of_liesOver P p)

end Quotient

end CommRing

section IsIntegral

variable {A : Type*} [CommRing A] {B : Type*} [CommRing B] [Algebra A B] [Algebra.IsIntegral A B]
  (P : Ideal B) (p : Ideal A) [P.LiesOver p]

variable (A) in
/-- If `B` is an integral `A`-algebra, `P` is a maximal ideal of `B`, then the pull back of
  `P` is also a maximal ideal of `A`. -/
instance IsMaximal.under [P.IsMaximal] : (P.under A).IsMaximal :=
  isMaximal_comap_of_isIntegral_of_isMaximal P

theorem IsMaximal.of_liesOver_isMaximal [hpm : p.IsMaximal] [P.IsPrime] : P.IsMaximal := by
  rw [P.over_def p] at hpm
  exact isMaximal_of_isIntegral_of_isMaximal_comap P hpm

theorem IsMaximal.of_isMaximal_liesOver [P.IsMaximal] : p.IsMaximal := by
  rw [P.over_def p]
  exact isMaximal_comap_of_isIntegral_of_isMaximal P

/-- `B ‚ß∏ P` is an integral `A ‚ß∏ p`-algebra if `B` is a integral `A`-algebra. -/
instance Quotient.algebra_isIntegral_of_liesOver : Algebra.IsIntegral (A ‚ß∏ p) (B ‚ß∏ P) :=
  Algebra.IsIntegral.tower_top A

end IsIntegral

end ideal_liesOver

end Ideal
