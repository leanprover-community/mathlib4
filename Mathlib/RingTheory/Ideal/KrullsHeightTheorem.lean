/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Wanyi He, Jiedong Jiang, Christian Merten, Jingting Wang, Andrew Yang, Shouxin Zhang
-/
import Mathlib.RingTheory.HopkinsLevitzki
import Mathlib.RingTheory.Ideal.Height
import Mathlib.RingTheory.Localization.Submodule
import Mathlib.RingTheory.Nakayama

/-!
# Krull's Height TheoremgroupCoho

In this file, we prove Krull's principal ideal theorem and Krull's height theorem.

## Main Results

* `Ideal.height_le_one_of_isPrincipal_of_mem_minimalPrimes` : In a commutative Noetherian ring `R`,
  any prime ideal that is minimal over a principal ideal has height at most 1.

* `Ideal.height_le_spanRank_toENat_of_mem_minimal_primes` : In a commutative Noetherian ring `R`,
  any prime ideal that is minimal over an ideal generated by `n` elements has height at most `n`.

* `Ideal.height_le_spanRank_toENat` : In a commutative Noetherian ring `R`, the height of a
  (finitely-generated) ideal is smaller than or equal to the minimum number of generators for
  this ideal.

* `Ideal.height_le_iff_exists_minimal_primes` : In a commutative Noetherian ring `R`, a prime ideal
  `p` has height no greater than `n` if and only if it is a minimal ideal over some ideal generated
  by no more than `n` elements.
-/

variable {R : Type*} [CommRing R] [IsNoetherianRing R]

lemma IsLocalRing.quotient_artinian_of_mem_minimalPrimes_of_isLocalRing
    [IsLocalRing R] (I : Ideal R) (hp : (IsLocalRing.maximalIdeal R) ∈ I.minimalPrimes) :
    IsArtinianRing (R ⧸ I) := by
  have hI : I ≠ ⊤ := (hp.1.2.trans_lt
    (lt_top_iff_ne_top.mpr (IsLocalRing.maximalIdeal.isMaximal _).ne_top)).ne
  have := Ideal.Quotient.nontrivial hI
  have := IsLocalRing.of_surjective' _ (Ideal.Quotient.mk_surjective (I := I))
  rw [isArtinianRing_iff_isNilpotent_maximalIdeal,
      Ideal.FG.isNilpotent_iff_le_nilradical (IsNoetherian.noetherian _)]
  refine (Ideal.comap_le_comap_iff_of_surjective _ Ideal.Quotient.mk_surjective _ _).mp ?_
  rw [nilradical, Ideal.comap_radical, Ideal.zero_eq_bot, ← RingHom.ker_eq_comap_bot,
    Ideal.mk_ker, IsLocalRing.eq_maximalIdeal (Ideal.comap_isMaximal_of_surjective
    _ (Ideal.Quotient.mk_surjective (I := I))), Ideal.radical_eq_sInf, le_sInf_iff]
  exact fun J ⟨hJ₁, hJ₂⟩ => hp.2 ⟨hJ₂, hJ₁⟩ (IsLocalRing.le_maximalIdeal hJ₂.ne_top)

lemma Ideal.height_le_one_of_isPrincipal_of_mem_minimalPrimes_of_isLocalRing
    [IsLocalRing R] (I : Ideal R) [I.IsPrincipal]
    (hp : (IsLocalRing.maximalIdeal R) ∈ I.minimalPrimes) :
    (IsLocalRing.maximalIdeal R).height ≤ 1 := by
  refine Ideal.height_le_iff.mpr fun q h₁ h₂ ↦ ?_
  suffices q.height = 0 by rw [this]; exact zero_lt_one
  rw [← WithBot.coe_inj,
    ← IsLocalization.AtPrime.ringKrullDim_eq_height q (Localization.AtPrime q),
    WithBot.coe_zero, ← ringKrullDimZero_iff_ringKrullDim_eq_zero,
    ← isArtinianRing_iff_krullDimLE_zero, isArtinianRing_iff_isNilpotent_maximalIdeal,
    ← Localization.AtPrime.map_eq_maximalIdeal]
  have : IsArtinianRing (R ⧸ I) :=
    IsLocalRing.quotient_artinian_of_mem_minimalPrimes_of_isLocalRing I hp
  let f := algebraMap R (Localization.AtPrime q)
  let qs : ℕ →o (Ideal (R ⧸ I))ᵒᵈ :=
    { toFun n := ((q.map f ^ n).comap f).map (Ideal.Quotient.mk I)
      monotone' i j e := Ideal.map_mono (Ideal.comap_mono (Ideal.pow_le_pow_right e)) }
  obtain ⟨n, hn⟩ := IsArtinian.monotone_stabilizes qs
  refine ⟨n, ?_⟩
  apply Submodule.eq_bot_of_le_smul_of_le_jacobson_bot (q.map f) _ (IsNoetherian.noetherian _)
  rotate_left
  · rw [IsLocalRing.jacobson_eq_maximalIdeal, Localization.AtPrime.map_eq_maximalIdeal]
    exact bot_ne_top
  rw [smul_eq_mul, ← pow_succ',
    ← (IsLocalization.orderEmbedding q.primeCompl (Localization.AtPrime q)).map_rel_iff]
  refine Submodule.le_of_le_smul_of_le_jacobson_bot (I := I) (IsNoetherian.noetherian _) ?_ ?_
  · rw [IsLocalRing.jacobson_eq_maximalIdeal]
    exacts [hp.1.2, bot_ne_top]
  · replace hn := congr(Ideal.comap (Ideal.Quotient.mk I) $(hn _ n.le_succ))
    simp only [qs, OrderHom.coe_mk, ← RingHom.ker_eq_comap_bot, Ideal.mk_ker,
      Ideal.comap_map_of_surjective _ Ideal.Quotient.mk_surjective] at hn
    intro x hx
    obtain ⟨y, hy, z, hz, rfl⟩ := Submodule.mem_sup.mp (hn.le (Ideal.mem_sup_left hx))
    refine Submodule.add_mem_sup hy ?_
    obtain ⟨z, rfl⟩ := (Submodule.IsPrincipal.mem_iff_eq_smul_generator I).mp hz
    rw [smul_eq_mul, smul_eq_mul, mul_comm]
    refine Ideal.mul_mem_mul ?_ (Submodule.IsPrincipal.generator_mem _)
    dsimp [IsLocalization.orderEmbedding] at hx
    rwa [Ideal.mem_comap, f.map_add, f.map_mul, Ideal.add_mem_iff_right _
      (Ideal.pow_le_pow_right n.le_succ hy), mul_comm, Ideal.unit_mul_mem_iff_mem] at hx
    refine IsLocalization.map_units (M := q.primeCompl) _ ⟨_, ?_⟩
    show Submodule.IsPrincipal.generator I ∉ (↑q : Set R)
    rw [← Set.singleton_subset_iff, ← Ideal.span_le, Ideal.span_singleton_generator]
    exact fun e ↦ h₂.not_le (hp.2 ⟨h₁, e⟩ h₂.le)

/-- **Krull's principal ideal theorem** (also known as **Krullscher Hauptidealsatz**) :
  In a commutative Noetherian ring `R`, any prime ideal that is minimal over a principal ideal
  has height at most 1. -/
lemma Ideal.height_le_one_of_isPrincipal_of_mem_minimalPrimes
    (I : Ideal R) [I.IsPrincipal] (p : Ideal R) (hp : p ∈ I.minimalPrimes) : p.height ≤ 1 := by
  have := hp.1.1
  let f := algebraMap R (Localization.AtPrime p)
  have := Ideal.height_le_one_of_isPrincipal_of_mem_minimalPrimes_of_isLocalRing (I.map f) ?_
  · rwa [← IsLocalization.height_comap p.primeCompl,
      Localization.AtPrime.comap_maximalIdeal] at this
  · rwa [IsLocalization.minimalPrimes_map p.primeCompl (Localization.AtPrime p) I,
      Set.mem_preimage, Localization.AtPrime.comap_maximalIdeal]

/-- If `q < p` are prime ideals such that `p` is minimal over `span (s ∪ {x})` and
`t` is a set contained in `q` such that `s ⊆ √span (t ∪ {x})`, then `q` is minimal over `span t`.
This is used in the induction step for the proof of Krull's height theorem. -/
theorem Ideal.mem_minimalPrimes_span_of_mem_minimalPrimes_span_insert {q p : Ideal R} [q.IsPrime]
    [p.IsPrime] (hqp : q < p) (x : R) (s : Set R) (hp : p ∈ (span (insert x s)).minimalPrimes)
    (t : Set R) (htq : t ⊆ q) (hsp : s ⊆ (span (insert x t)).radical) :
    q ∈ (Ideal.span t).minimalPrimes := by
  let f := Quotient.mk (span t)
  have hf : Function.Surjective f := Quotient.mk_surjective
  have hI'q : (span t) ≤ q := by rwa [Ideal.span_le]
  suffices H : map f q ∈ map f '' (span t).minimalPrimes by
    obtain ⟨q', hq₁, hq₂⟩ := H
    convert hq₁
    apply_fun comap f at hq₂
    simp_rw [comap_map_of_surjective f hf, ← RingHom.ker_eq_comap_bot, f, Ideal.mk_ker] at hq₂
    rwa [sup_eq_left.mpr hI'q, sup_eq_left.mpr hq₁.1.2, eq_comm] at hq₂
  have hI'p' : (span t) ≤ p := hI'q.trans hqp.le
  have : (p.map f).IsPrime := map_isPrime_of_surjective hf (by rwa [Ideal.mk_ker])
  suffices h : (p.map f).height ≤ 1 by
    have h_lt : q.map f < p.map f := by
      refine lt_of_le_not_le (Ideal.map_mono hqp.le) (fun e => ?_)
      have := Ideal.comap_mono (f := f) e
      simp_rw [comap_map_of_surjective f hf, ← RingHom.ker_eq_comap_bot, f, Ideal.mk_ker,
        sup_eq_left.mpr hI'q, sup_eq_left.mpr (hI'q.trans hqp.le)] at this
      exact lt_irrefl _ (this.trans_lt hqp)
    have : (q.map f).IsPrime := Ideal.map_isPrime_of_surjective hf (by rwa [Ideal.mk_ker])
    have : (p.map f).FiniteHeight := by
      rw [Ideal.finiteHeight_iff, ← lt_top_iff_ne_top]
      exact Or.inr (h.trans_lt (WithTop.coe_lt_top 1))
    rw [Ideal.height_eq_primeHeight] at h
    have := (primeHeight_strict_mono h_lt).trans_le h
    rwa [ENat.lt_one_iff_eq_zero, primeHeight_eq_zero_iff, minimalPrimes,
      ← map_bot (f := f), minimalPrimes_map_of_surjective hf, bot_sup_eq, Ideal.mk_ker] at this
  refine height_le_one_of_isPrincipal_of_mem_minimalPrimes ((span {x}).map f) (p.map f) ?_
  obtain ⟨p'', h₁, h₂⟩ := Ideal.exists_minimalPrimes_le (J := p.map f) <| map_mono <| by
    rw [span_singleton_le_iff_mem]
    exact hp.1.2 (subset_span (Set.mem_insert x s))
  have hxp'' : f x ∈ p'' := by
    rw [← Ideal.span_singleton_le_iff_mem, ← Set.image_singleton, ← Ideal.map_span]
    exact h₁.1.2
  convert h₁
  refine (Ideal.map_le_iff_le_comap.mpr (hp.2 ⟨h₁.1.1.comap f, ?_⟩ ?_)).antisymm h₂
  · rw [Ideal.span_le, Set.insert_subset_iff]
    refine ⟨hxp'', le_trans hsp <| (h₁.1.1.comap f).isRadical.radical ▸ radical_mono ?_⟩
    rw [← Set.union_singleton, span_union, ← map_le_iff_le_comap, map_sup,
      map_mk_eq_bot_of_le le_rfl, sup_of_le_right bot_le]
    exact h₁.1.2
  · conv_rhs => rw [← sup_eq_left.mpr hI'p', ← (span t).mk_ker, RingHom.ker_eq_comap_bot,
      ← comap_map_of_surjective f hf p]
    exact Ideal.comap_mono h₂

-- move me
omit [IsNoetherianRing R] in
lemma Ideal.exists_subset_radical_span_sup_span_of_subset_radical_sup (s : Set R) (I J : Ideal R)
    (hs : s ⊆ (I ⊔ J).radical) :
    ∃ (t₁ t₂ : Set R), Cardinal.mk t₁ ≤ Cardinal.mk s ∧ Cardinal.mk t₂ ≤ Cardinal.mk s ∧
      t₁ ⊆ I ∧ t₂ ⊆ J ∧ s ⊆ (span t₁ ⊔ span t₂).radical := by
  replace hs : ∀ z : s, ∃ (m : ℕ) (a b : R) (ha : a ∈ I) (hb : b ∈ J), a + b = z ^ m := by
    rintro ⟨z, hzs⟩
    simp only [Ideal.radical, Submodule.mem_sup, Ideal.mem_span_singleton'] at hs
    obtain ⟨m, y, hyq, b, hb, hy⟩ := hs hzs
    exact ⟨m, y, b, hyq, hb, hy⟩
  choose m a b ha hb heq using hs
  refine ⟨Set.range a, Set.range b, Cardinal.mk_range_le, Cardinal.mk_range_le, ?_, ?_,
    fun z hz ↦ ⟨m ⟨z, hz⟩, ?_⟩⟩
  · rwa [Set.range_subset_iff]
  · rwa [Set.range_subset_iff]
  · rw [← heq ⟨z, hz⟩]
    refine Ideal.add_mem _ (mem_sup_left <| subset_span ?_) (mem_sup_right <| subset_span ?_)
    · use ⟨z, hz⟩
    · use ⟨z, hz⟩

open IsLocalRing in
/-- **Krull's height theorem** (also known as **Krullscher Höhensatz**) :
  In a commutative Noetherian ring `R`, any prime ideal that is minimal over an ideal generated
  by `n` elements has height at most `n`. -/
nonrec lemma Ideal.height_le_spanRank_toENat_of_mem_minimal_primes
    (I : Ideal R) (p : Ideal R) (hp : p ∈ I.minimalPrimes) :
    p.height ≤ I.spanRank.toENat := by
  classical
  rw [I.spanRank_toENat_eq_iInf_finset_card, le_iInf_iff]
  rintro ⟨s, hs, (rfl : Ideal.span s = I)⟩
  induction hn : hs.toFinset.card using Nat.strong_induction_on generalizing R with
  | h n H =>
    replace hn : hs.toFinset.card ≤ n := le_of_eq hn
    have := hp.1.1
    cases n with
    | zero =>
      rw [CharP.cast_eq_zero, nonpos_iff_eq_zero, Ideal.height_eq_primeHeight p,
        Ideal.primeHeight_eq_zero_iff, minimalPrimes]
      simp_all
    | succ n =>
      wlog hR : ∃ (_ : IsLocalRing R), p = maximalIdeal R
      · rw [← Localization.AtPrime.comap_maximalIdeal (I := p),
          IsLocalization.height_comap p.primeCompl]
        rw [← Localization.AtPrime.comap_maximalIdeal (I := p), ← Set.mem_preimage,
          ← IsLocalization.minimalPrimes_map p.primeCompl, map_span] at hp
        have ht : (algebraMap R (Localization p.primeCompl) '' s).Finite := hs.image _
        refine this _ _ ht hp inferInstance _ H ?_ ⟨inferInstance, rfl⟩
        rw [show ht.toFinset = hs.toFinset.image (algebraMap R _) by ext; simp]
        exact Finset.card_image_le.trans hn
      obtain ⟨_, rfl⟩ := hR
      simp_rw [Ideal.height_le_iff_covBy, ENat.coe_add, ENat.coe_one, ENat.lt_coe_add_one_iff]
      intro q hq hpq hq'
      obtain ⟨x, s', hxs', rfl, hxq⟩ : ∃ x s', x ∉ s' ∧ s = insert x s' ∧ x ∉ q := by
        have : ¬s ⊆ q := by
          rw [← Ideal.span_le]
          exact fun e ↦ lt_irrefl _ ((hp.2 ⟨hq, e⟩ hpq.le).trans_lt hpq)
        obtain ⟨x, hxt, hxq⟩ := Set.not_subset.mp this
        refine ⟨x, s \ {x}, fun e => e.2 rfl, ?_, hxq⟩
        rw [Set.insert_diff_singleton, Set.insert_eq_of_mem hxt]
      have h : s' ⊆ (q ⊔ Ideal.span {x}).radical := by
        have : maximalIdeal R ≤ (q ⊔ Ideal.span {x}).radical := by
          rw [Ideal.radical_eq_sInf, le_sInf_iff]
          refine fun J ⟨hJ, hJ'⟩ ↦ by_contra fun h ↦ ?_
          refine hq' J hJ' (lt_of_le_of_ne (le_sup_left.trans hJ) ?_)
            (lt_of_le_not_le (le_maximalIdeal hJ'.ne_top) h)
          rintro rfl
          exact hxq <| hJ <| Ideal.mem_sup_right <| Submodule.mem_span_singleton_self _
        have := hp.1.2.trans this
        rw [Ideal.span_le, Set.insert_subset_iff] at this
        exact this.2
      obtain ⟨t, w, hcard₁, _, htq, hwx, hst⟩ :=
        exists_subset_radical_span_sup_span_of_subset_radical_sup s' _ _ h
      suffices hq : q ∈ (span t).minimalPrimes by
        have hs := (hs.subset (Set.subset_insert _ _))
        have ht : t.Finite := Cardinal.lt_aleph0_iff_set_finite.mp <|
          hcard₁.trans_lt (Cardinal.lt_aleph0_iff_set_finite.mpr ‹_›)
        have htcard : Cardinal.mk ht.toFinset ≤ .mk hs.toFinset := by simpa
        replace htcard : (ht.toFinset.card : Cardinal) ≤ (hs.toFinset.card : Cardinal) := by
          convert htcard <;> exact Cardinal.mk_coe_finset.symm
        have htcard : ht.toFinset.card ≤ n := by
          refine le_trans (Nat.cast_le.mp htcard) (Nat.le_of_lt_succ ?_)
          rw [Nat.lt_iff_add_one_le, ← Finset.card_insert_of_not_mem (a := x) (by simpa)]
          simpa using hn
        exact le_trans (H ht.toFinset.card (by omega) q t ht hq rfl) (by norm_cast)
      refine mem_minimalPrimes_span_of_mem_minimalPrimes_span_insert hpq _ _ hp _ htq ?_
      refine hst.trans <| radical_mono ?_
      rw [← Set.union_singleton, span_union]
      exact sup_le_sup_left (span_le.mpr hwx) _

/-- In a commutative Noetherian ring `R`, the height of a (finitely-generated) ideal is smaller
than or equal to the minimum number of generators for this ideal. -/
lemma Ideal.height_le_spanRank_toENat (I : Ideal R) (hI : I ≠ ⊤) :
    I.height ≤ I.spanRank.toENat := by
  obtain ⟨J, hJ⟩ := Ideal.nonempty_minimalPrimes hI
  refine (iInf₂_le J hJ).trans ?_
  convert (I.height_le_spanRank_toENat_of_mem_minimal_primes J hJ)
  exact Eq.symm (@height_eq_primeHeight _ _ J hJ.1.1)

lemma Ideal.height_le_spanFinrank (I : Ideal R) (hI : I ≠ ⊤) :
    I.height ≤ I.spanFinrank := by
  have : I.spanFinrank = I.spanRank.toENat := by
    rw [Submodule.fg_iff_spanRank_eq_spanFinrank.mpr (IsNoetherian.noetherian I), map_natCast]
  exact this ▸ height_le_spanRank_toENat I hI

lemma Ideal.height_le_spanRank (I : Ideal R) (hI : I ≠ ⊤) :
    I.height ≤ I.spanRank := by
  apply le_trans (b := ((Cardinal.toENat I.spanRank) : Cardinal))
  · norm_cast
    exact I.height_le_spanRank_toENat hI
  · exact Cardinal.ofENat_toENat_le (Submodule.spanRank I)

instance Ideal.finiteHeight_of_isNoetherianRing (I : Ideal R) :
    I.FiniteHeight := Ideal.finiteHeight_iff_lt.mpr <| Or.elim (em (I = ⊤)) Or.inl <|
  fun h ↦ Or.inr <| (lt_of_le_of_lt (I.height_le_spanFinrank h) (ENat.coe_lt_top _))

instance [IsNoetherianRing R] [IsLocalRing R] : FiniteRingKrullDim R := by
  apply finiteRingKrullDim_iff_ne_bot_and_top.mpr
  rw [← IsLocalRing.maximalIdeal_height_eq_ringKrullDim]
  constructor
  · exact WithBot.coe_ne_bot
  · rw [← WithBot.coe_top, ne_eq, WithBot.coe_inj]
    exact ((IsLocalRing.maximalIdeal R).finiteHeight_iff.mp
      (IsLocalRing.maximalIdeal R).finiteHeight_of_isNoetherianRing).resolve_left
        Ideal.IsPrime.ne_top'

lemma Ideal.exists_spanRank_eq_and_height_eq (I : Ideal R) (hI : I ≠ ⊤) :
    ∃ J ≤ I, J.spanRank = I.height ∧ J.height = I.height := by
  obtain ⟨J, hJ₁, hJ₂, hJ₃⟩ := exists_spanRank_le_and_le_height_of_le_height I _
    (ENat.coe_toNat_le_self I.height)
  rw [ENat.coe_toNat_eq_self.mpr (Ideal.height_ne_top hI)] at hJ₃
  refine ⟨J, hJ₁, le_antisymm ?_ (le_trans ?_ (J.height_le_spanRank ?_)),
    le_antisymm (Ideal.height_mono hJ₁) hJ₃⟩
  · convert hJ₂
    exact Cardinal.ofENat_eq_nat.mpr (ENat.coe_toNat (I.height_ne_top hI)).symm
  · exact Cardinal.ofENat_le_ofENat_of_le hJ₃
  · rintro rfl
    exact hI (top_le_iff.mp hJ₁)

/-- In a commutative Noetherian ring `R`, a prime ideal `p` has height no greater than `n` if and
only if it is a minimal ideal over some ideal generated by no more than `n` elements. -/
lemma Ideal.height_le_iff_exists_minimalPrimes (p : Ideal R) [p.IsPrime]
    (n : ℕ∞) : p.height ≤ n ↔ ∃ I : Ideal R, p ∈ I.minimalPrimes ∧ I.spanRank ≤ n := by
  constructor
  · intro h
    obtain ⟨I, hI, e₁, e₂⟩ := exists_spanRank_eq_and_height_eq p (IsPrime.ne_top ‹_›)
    refine ⟨I, Ideal.mem_minimalPrimes_of_height_eq hI e₂.ge, e₁.symm ▸ ?_⟩
    norm_cast
  · rintro ⟨I, hp, hI⟩
    exact le_trans
      (Ideal.height_le_spanRank_toENat_of_mem_minimal_primes I p hp)
      (by simpa using (Cardinal.toENat.monotone' hI))

/-- If `p` is a prime in a Noetherian ring `R`, there exists a `p`-primary ideal `I`
spanned by `p.height` elements. -/
lemma Ideal.exists_finset_card_eq_height_of_isNoetherianRing
    (p : Ideal R) [p.IsPrime] : ∃ (I : Ideal R) (s : Finset R),
      p ∈ I.minimalPrimes ∧ Ideal.span s = I ∧ s.card = p.height := by
  obtain ⟨I, hI, hr⟩ := (p.height_le_iff_exists_minimalPrimes <| p.height).mp le_rfl
  have hs : I.generators.Finite := (IsNoetherian.noetherian I).finite_generators
  refine ⟨I, hs.toFinset, hI, (by simpa using I.span_generators), ?_⟩
  rw [← Set.ncard_eq_toFinset_card (hs := hs), (IsNoetherian.noetherian I).generators_ncard]
  refine le_antisymm ?_ ?_
  · rw [Submodule.fg_iff_spanRank_eq_spanFinrank.mpr (IsNoetherian.noetherian I)] at hr
    exact Cardinal.nat_le_ofENat.mp hr
  · convert_to p.height ≤ I.spanRank.toENat
    · symm
      simpa [Submodule.fg_iff_spanRank_eq_spanFinrank] using (IsNoetherian.noetherian I)
    · exact I.height_le_spanRank_toENat_of_mem_minimal_primes _ hI
