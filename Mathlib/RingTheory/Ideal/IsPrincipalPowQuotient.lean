/-
Copyright (c) 2024 Yakov Pechersky. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yakov Pechersky
-/
import Mathlib.LinearAlgebra.Isomorphisms
import Mathlib.RingTheory.Ideal.Operations
import Mathlib.RingTheory.Ideal.Quotient.Defs

/-!
# Quotients of powers of principal ideals

This file deals with taking quotients of powers of principal ideals.

## Main definitions and results

* `Ideal.quotEquivPowQuotPowSucc`: for a principal ideal `I`, `R ‚ß∏ I ‚âÉ‚Çó[R] I ^ n ‚ß∏ I ^ (n + 1)`

## Implementation details

At site of usage, calling `LinearEquiv.toEquiv` can cause timeouts in the search for a complex
synthesis like `Module ùí™[K] ùìÄ[k]`, so the plain equiv versions are provided.

These equivs are defined here as opposed to in the quotients file since they cannot be
formed as ring equivs.

-/


namespace Ideal

section IsPrincipal

variable {R : Type*} [CommRing R] [IsDomain R] {I : Ideal R}

/-- For a principal ideal `I`, `R ‚ß∏ I ‚âÉ‚Çó[R] I ^ n ‚ß∏ I ^ (n + 1)`. To convert into a form
that uses the ideal of `R ‚ß∏ I ^ (n + 1)`, compose with
`Ideal.powQuotPowSuccLinearEquivMapMkPowSuccPow`. -/
noncomputable
def quotEquivPowQuotPowSucc (h : I.IsPrincipal) (h' : I ‚â† ‚ä•) (n : ‚Ñï) :
    (R ‚ß∏ I) ‚âÉ‚Çó[R] (I ^ n : Ideal R) ‚ß∏ (I ‚Ä¢ ‚ä§ : Submodule R (I ^ n : Ideal R)) := by
  let f : (I ^ n : Ideal R) ‚Üí‚Çó[R] (I ^ n : Ideal R) ‚ß∏ (I ‚Ä¢ ‚ä§ : Submodule R (I ^ n : Ideal R)) :=
    Submodule.mkQ _
  let œñ := h.principal.choose
  have hI : I = Ideal.span {œñ} := h.principal.choose_spec
  have hœñ : œñ ^ n ‚àà I ^ n := hI ‚ñ∏ (Ideal.pow_mem_pow (Ideal.mem_span_singleton_self _) n)
  let g : R ‚Üí‚Çó[R] (I ^ n : Ideal R) := (LinearMap.mulRight R œñ^n).codRestrict _ fun x ‚Ü¶ by
    simp only [LinearMap.pow_mulRight, LinearMap.mulRight_apply, Ideal.submodule_span_eq]
    -- TODO: change argument of Ideal.pow_mem_of_mem
    exact Ideal.mul_mem_left _ _ hœñ
  have : I = LinearMap.ker (f.comp g) := by
    ext x
    simp only [LinearMap.codRestrict, LinearMap.pow_mulRight, LinearMap.mulRight_apply,
      LinearMap.mem_ker, LinearMap.coe_comp, LinearMap.coe_mk, AddHom.coe_mk, Function.comp_apply,
      Submodule.mkQ_apply, Submodule.Quotient.mk_eq_zero, Submodule.mem_smul_top_iff, smul_eq_mul,
      f, g]
    constructor <;> intro hx
    ¬∑ exact Submodule.mul_mem_mul hx hœñ
    ¬∑ rw [‚Üê pow_succ', hI, Ideal.span_singleton_pow, Ideal.mem_span_singleton] at hx
      obtain ‚ü®y, hy‚ü© := hx
      rw [mul_comm, pow_succ, mul_assoc, mul_right_inj' (pow_ne_zero _ _)] at hy
      ¬∑ rw [hI, Ideal.mem_span_singleton]
        exact ‚ü®y, hy‚ü©
      ¬∑ contrapose! h'
        rw [hI, h', Ideal.span_singleton_eq_bot]
  let e : (R ‚ß∏ I) ‚âÉ‚Çó[R] R ‚ß∏ (LinearMap.ker (f.comp g)) :=
    Submodule.quotEquivOfEq I (LinearMap.ker (f ‚àò‚Çó g)) this
  refine e.trans ((f.comp g).quotKerEquivOfSurjective ?_)
  refine (Submodule.mkQ_surjective _).comp ?_
  rintro ‚ü®x, hx‚ü©
  rw [hI, Ideal.span_singleton_pow, Ideal.mem_span_singleton] at hx
  refine hx.imp ?_
  simp [g, LinearMap.codRestrict, eq_comm, mul_comm]

/-- For a principal ideal `I`, `R ‚ß∏ I ‚âÉ I ^ n ‚ß∏ I ^ (n + 1)`. Supplied as a plain equiv to bypass
typeclass synthesis issues on complex `Module` goals.  To convert into a form
that uses the ideal of `R ‚ß∏ I ^ (n + 1)`, compose with
`Ideal.powQuotPowSuccEquivMapMkPowSuccPow`. -/
noncomputable
def quotEquivPowQuotPowSuccEquiv (h : I.IsPrincipal) (h' : I ‚â† ‚ä•) (n : ‚Ñï) :
    (R ‚ß∏ I) ‚âÉ (I ^ n : Ideal R) ‚ß∏ (I ‚Ä¢ ‚ä§ : Submodule R (I ^ n : Ideal R)) :=
  quotEquivPowQuotPowSucc h h' n

end IsPrincipal

end Ideal
