/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Filippo A. E. Nuccio, Andrew Yang
-/
import Mathlib.LinearAlgebra.Finsupp.SumProd
import Mathlib.RingTheory.Ideal.Prod
import Mathlib.RingTheory.Localization.Ideal
import Mathlib.RingTheory.Nilpotent.Lemmas
import Mathlib.RingTheory.Noetherian.Basic

/-!
# Prime spectrum of a commutative (semi)ring as a type

The prime spectrum of a commutative (semi)ring is the type of all prime ideals.

For the Zariski topology, see `AlgebraicGeometry.PrimeSpectrum.Basic`.

(It is also naturally endowed with a sheaf of rings,
which is constructed in `AlgebraicGeometry.StructureSheaf`.)

## Main definitions

* `PrimeSpectrum R`: The prime spectrum of a commutative (semi)ring `R`,
  i.e., the set of all prime ideals of `R`.
* `zeroLocus s`: The zero locus of a subset `s` of `R`
  is the subset of `PrimeSpectrum R` consisting of all prime ideals that contain `s`.
* `vanishingIdeal t`: The vanishing ideal of a subset `t` of `PrimeSpectrum R`
  is the intersection of points in `t` (viewed as prime ideals).

## Conventions

We denote subsets of (semi)rings with `s`, `s'`, etc...
whereas we denote subsets of prime spectra with `t`, `t'`, etc...

## Inspiration/contributors

The contents of this file draw inspiration from <https://github.com/ramonfmir/lean-scheme>
which has contributions from Ramon Fernandez Mir, Kevin Buzzard, Kenny Lau,
and Chris Hughes (on an earlier repository).
-/

-- A dividing line between this file and `AlgebraicGeometry.PrimeSpectrum.Basic` is
-- that we should not depend on the Zariski topology here
assert_not_exists TopologicalSpace

noncomputable section

open scoped Classical Pointwise

universe u v

variable (R : Type u) (S : Type v)

/-- The prime spectrum of a commutative (semi)ring `R` is the type of all prime ideals of `R`.

It is naturally endowed with a topology (the Zariski topology),
and a sheaf of commutative rings (see `AlgebraicGeometry.StructureSheaf`).
It is a fundamental building block in algebraic geometry. -/
@[ext]
structure PrimeSpectrum [CommSemiring R] where
  asIdeal : Ideal R
  isPrime : asIdeal.IsPrime

@[deprecated (since := "2024-06-22")] alias PrimeSpectrum.IsPrime := PrimeSpectrum.isPrime

attribute [instance] PrimeSpectrum.isPrime

namespace PrimeSpectrum

section CommSemiRing

variable [CommSemiring R] [CommSemiring S]
variable {R S}

instance [Nontrivial R] : Nonempty <| PrimeSpectrum R :=
  let ‚ü®I, hI‚ü© := Ideal.exists_maximal R
  ‚ü®‚ü®I, hI.isPrime‚ü©‚ü©

/-- The prime spectrum of the zero ring is empty. -/
instance [Subsingleton R] : IsEmpty (PrimeSpectrum R) :=
  ‚ü®fun x ‚Ü¶ x.isPrime.ne_top <| SetLike.ext' <| Subsingleton.eq_univ_of_nonempty x.asIdeal.nonempty‚ü©

variable (R S)

/-- The prime spectrum is in bijection with the set of prime ideals. -/
@[simps]
def equivSubtype : PrimeSpectrum R ‚âÉ {I : Ideal R // I.IsPrime} where
  toFun I := ‚ü®I.asIdeal, I.2‚ü©
  invFun I := ‚ü®I, I.2‚ü©
  left_inv _ := rfl
  right_inv _ := rfl

/-- The map from the direct sum of prime spectra to the prime spectrum of a direct product. -/
@[simp]
def primeSpectrumProdOfSum : PrimeSpectrum R ‚äï PrimeSpectrum S ‚Üí PrimeSpectrum (R √ó S)
  | Sum.inl ‚ü®I, _‚ü© => ‚ü®Ideal.prod I ‚ä§, Ideal.isPrime_ideal_prod_top‚ü©
  | Sum.inr ‚ü®J, _‚ü© => ‚ü®Ideal.prod ‚ä§ J, Ideal.isPrime_ideal_prod_top'‚ü©

/-- The prime spectrum of `R √ó S` is in bijection with the disjoint unions of the prime spectrum of
`R` and the prime spectrum of `S`. -/
noncomputable def primeSpectrumProd :
    PrimeSpectrum (R √ó S) ‚âÉ PrimeSpectrum R ‚äï PrimeSpectrum S :=
  Equiv.symm <|
    Equiv.ofBijective (primeSpectrumProdOfSum R S) (by
        constructor
        ¬∑ rintro (‚ü®I, hI‚ü© | ‚ü®J, hJ‚ü©) (‚ü®I', hI'‚ü© | ‚ü®J', hJ'‚ü©) h <;>
          simp only [mk.injEq, Ideal.prod.ext_iff, primeSpectrumProdOfSum] at h
          ¬∑ simp only [h]
          ¬∑ exact False.elim (hI.ne_top h.left)
          ¬∑ exact False.elim (hJ.ne_top h.right)
          ¬∑ simp only [h]
        ¬∑ rintro ‚ü®I, hI‚ü©
          rcases (Ideal.ideal_prod_prime I).mp hI with (‚ü®p, ‚ü®hp, rfl‚ü©‚ü© | ‚ü®p, ‚ü®hp, rfl‚ü©‚ü©)
          ¬∑ exact ‚ü®Sum.inl ‚ü®p, hp‚ü©, rfl‚ü©
          ¬∑ exact ‚ü®Sum.inr ‚ü®p, hp‚ü©, rfl‚ü©)

variable {R S}

@[simp]
theorem primeSpectrumProd_symm_inl_asIdeal (x : PrimeSpectrum R) :
    ((primeSpectrumProd R S).symm <| Sum.inl x).asIdeal = Ideal.prod x.asIdeal ‚ä§ := by
  cases x
  rfl

@[simp]
theorem primeSpectrumProd_symm_inr_asIdeal (x : PrimeSpectrum S) :
    ((primeSpectrumProd R S).symm <| Sum.inr x).asIdeal = Ideal.prod ‚ä§ x.asIdeal := by
  cases x
  rfl

/-- The zero locus of a set `s` of elements of a commutative (semi)ring `R` is the set of all
prime ideals of the ring that contain the set `s`.

An element `f` of `R` can be thought of as a dependent function on the prime spectrum of `R`.
At a point `x` (a prime ideal) the function (i.e., element) `f` takes values in the quotient ring
`R` modulo the prime ideal `x`. In this manner, `zeroLocus s` is exactly the subset of
`PrimeSpectrum R` where all "functions" in `s` vanish simultaneously.
-/
def zeroLocus (s : Set R) : Set (PrimeSpectrum R) :=
  { x | s ‚äÜ x.asIdeal }

@[simp]
theorem mem_zeroLocus (x : PrimeSpectrum R) (s : Set R) : x ‚àà zeroLocus s ‚Üî s ‚äÜ x.asIdeal :=
  Iff.rfl

@[simp]
theorem zeroLocus_span (s : Set R) : zeroLocus (Ideal.span s : Set R) = zeroLocus s := by
  ext x
  exact (Submodule.gi R R).gc s x.asIdeal

/-- The vanishing ideal of a set `t` of points of the prime spectrum of a commutative ring `R` is
the intersection of all the prime ideals in the set `t`.

An element `f` of `R` can be thought of as a dependent function on the prime spectrum of `R`.
At a point `x` (a prime ideal) the function (i.e., element) `f` takes values in the quotient ring
`R` modulo the prime ideal `x`. In this manner, `vanishingIdeal t` is exactly the ideal of `R`
consisting of all "functions" that vanish on all of `t`.
-/
def vanishingIdeal (t : Set (PrimeSpectrum R)) : Ideal R :=
  ‚®Ö x ‚àà t, x.asIdeal

theorem coe_vanishingIdeal (t : Set (PrimeSpectrum R)) :
    (vanishingIdeal t : Set R) = { f : R | ‚àÄ x ‚àà t, f ‚àà x.asIdeal } := by
  ext f
  rw [vanishingIdeal, SetLike.mem_coe, Submodule.mem_iInf]
  apply forall_congr'; intro x
  rw [Submodule.mem_iInf]

theorem mem_vanishingIdeal (t : Set (PrimeSpectrum R)) (f : R) :
    f ‚àà vanishingIdeal t ‚Üî ‚àÄ x ‚àà t, f ‚àà x.asIdeal := by
  rw [‚Üê SetLike.mem_coe, coe_vanishingIdeal, Set.mem_setOf_eq]

@[simp]
theorem vanishingIdeal_singleton (x : PrimeSpectrum R) :
    vanishingIdeal ({x} : Set (PrimeSpectrum R)) = x.asIdeal := by simp [vanishingIdeal]

theorem subset_zeroLocus_iff_le_vanishingIdeal (t : Set (PrimeSpectrum R)) (I : Ideal R) :
    t ‚äÜ zeroLocus I ‚Üî I ‚â§ vanishingIdeal t :=
  ‚ü®fun h _ k => (mem_vanishingIdeal _ _).mpr fun _ j => (mem_zeroLocus _ _).mpr (h j) k, fun h =>
    fun x j => (mem_zeroLocus _ _).mpr (le_trans h fun _ h => ((mem_vanishingIdeal _ _).mp h) x j)‚ü©

section Gc

variable (R)

/-- `zeroLocus` and `vanishingIdeal` form a galois connection. -/
theorem gc :
    @GaloisConnection (Ideal R) (Set (PrimeSpectrum R))·µí·µà _ _ (fun I => zeroLocus I) fun t =>
      vanishingIdeal t :=
  fun I t => subset_zeroLocus_iff_le_vanishingIdeal t I

/-- `zeroLocus` and `vanishingIdeal` form a galois connection. -/
theorem gc_set :
    @GaloisConnection (Set R) (Set (PrimeSpectrum R))·µí·µà _ _ (fun s => zeroLocus s) fun t =>
      vanishingIdeal t := by
  have ideal_gc : GaloisConnection Ideal.span _ := (Submodule.gi R R).gc
  simpa [zeroLocus_span, Function.comp_def] using ideal_gc.compose (gc R)

theorem subset_zeroLocus_iff_subset_vanishingIdeal (t : Set (PrimeSpectrum R)) (s : Set R) :
    t ‚äÜ zeroLocus s ‚Üî s ‚äÜ vanishingIdeal t :=
  (gc_set R) s t

end Gc

theorem subset_vanishingIdeal_zeroLocus (s : Set R) : s ‚äÜ vanishingIdeal (zeroLocus s) :=
  (gc_set R).le_u_l s

theorem le_vanishingIdeal_zeroLocus (I : Ideal R) : I ‚â§ vanishingIdeal (zeroLocus I) :=
  (gc R).le_u_l I

@[simp]
theorem vanishingIdeal_zeroLocus_eq_radical (I : Ideal R) :
    vanishingIdeal (zeroLocus (I : Set R)) = I.radical :=
  Ideal.ext fun f => by
    rw [mem_vanishingIdeal, Ideal.radical_eq_sInf, Submodule.mem_sInf]
    exact ‚ü®fun h x hx => h ‚ü®x, hx.2‚ü© hx.1, fun h x hx => h x.1 ‚ü®hx, x.2‚ü©‚ü©

@[simp]
theorem zeroLocus_radical (I : Ideal R) : zeroLocus (I.radical : Set R) = zeroLocus I :=
  vanishingIdeal_zeroLocus_eq_radical I ‚ñ∏ (gc R).l_u_l_eq_l I

theorem subset_zeroLocus_vanishingIdeal (t : Set (PrimeSpectrum R)) :
    t ‚äÜ zeroLocus (vanishingIdeal t) :=
  (gc R).l_u_le t

theorem zeroLocus_anti_mono {s t : Set R} (h : s ‚äÜ t) : zeroLocus t ‚äÜ zeroLocus s :=
  (gc_set R).monotone_l h

theorem zeroLocus_anti_mono_ideal {s t : Ideal R} (h : s ‚â§ t) :
    zeroLocus (t : Set R) ‚äÜ zeroLocus (s : Set R) :=
  (gc R).monotone_l h

theorem vanishingIdeal_anti_mono {s t : Set (PrimeSpectrum R)} (h : s ‚äÜ t) :
    vanishingIdeal t ‚â§ vanishingIdeal s :=
  (gc R).monotone_u h

theorem zeroLocus_subset_zeroLocus_iff (I J : Ideal R) :
    zeroLocus (I : Set R) ‚äÜ zeroLocus (J : Set R) ‚Üî J ‚â§ I.radical := by
  rw [subset_zeroLocus_iff_le_vanishingIdeal, vanishingIdeal_zeroLocus_eq_radical]

theorem zeroLocus_subset_zeroLocus_singleton_iff (f g : R) :
    zeroLocus ({f} : Set R) ‚äÜ zeroLocus {g} ‚Üî g ‚àà (Ideal.span ({f} : Set R)).radical := by
  rw [‚Üê zeroLocus_span {f}, ‚Üê zeroLocus_span {g}, zeroLocus_subset_zeroLocus_iff, Ideal.span_le,
    Set.singleton_subset_iff, SetLike.mem_coe]

theorem zeroLocus_bot : zeroLocus ((‚ä• : Ideal R) : Set R) = Set.univ :=
  (gc R).l_bot

@[simp]
theorem zeroLocus_singleton_zero : zeroLocus ({0} : Set R) = Set.univ :=
  zeroLocus_bot

@[simp]
theorem zeroLocus_empty : zeroLocus (‚àÖ : Set R) = Set.univ :=
  (gc_set R).l_bot

@[simp]
theorem vanishingIdeal_empty : vanishingIdeal (‚àÖ : Set (PrimeSpectrum R)) = ‚ä§ := by
  simpa using (gc R).u_top

theorem zeroLocus_empty_of_one_mem {s : Set R} (h : (1 : R) ‚àà s) : zeroLocus s = ‚àÖ := by
  rw [Set.eq_empty_iff_forall_not_mem]
  intro x hx
  rw [mem_zeroLocus] at hx
  have x_prime : x.asIdeal.IsPrime := by infer_instance
  have eq_top : x.asIdeal = ‚ä§ := by
    rw [Ideal.eq_top_iff_one]
    exact hx h
  apply x_prime.ne_top eq_top

@[simp]
theorem zeroLocus_singleton_one : zeroLocus ({1} : Set R) = ‚àÖ :=
  zeroLocus_empty_of_one_mem (Set.mem_singleton (1 : R))

theorem zeroLocus_empty_iff_eq_top {I : Ideal R} : zeroLocus (I : Set R) = ‚àÖ ‚Üî I = ‚ä§ := by
  constructor
  ¬∑ contrapose!
    intro h
    rcases Ideal.exists_le_maximal I h with ‚ü®M, hM, hIM‚ü©
    exact ‚ü®‚ü®M, hM.isPrime‚ü©, hIM‚ü©
  ¬∑ rintro rfl
    apply zeroLocus_empty_of_one_mem
    trivial

@[simp]
theorem zeroLocus_univ : zeroLocus (Set.univ : Set R) = ‚àÖ :=
  zeroLocus_empty_of_one_mem (Set.mem_univ 1)

theorem vanishingIdeal_eq_top_iff {s : Set (PrimeSpectrum R)} : vanishingIdeal s = ‚ä§ ‚Üî s = ‚àÖ := by
  rw [‚Üê top_le_iff, ‚Üê subset_zeroLocus_iff_le_vanishingIdeal, Submodule.top_coe, zeroLocus_univ,
    Set.subset_empty_iff]

theorem zeroLocus_eq_top_iff (s : Set R) :
    zeroLocus s = ‚ä§ ‚Üî s ‚äÜ nilradical R := by
  constructor
  ¬∑ intro h x hx
    refine nilpotent_iff_mem_prime.mpr (fun J hJ ‚Ü¶ ?_)
    have hJz : ‚ü®J, hJ‚ü© ‚àà zeroLocus s := by
      rw [h]
      trivial
    exact (mem_zeroLocus _ _).mpr hJz hx
  ¬∑ rw [eq_top_iff]
    intro h p _
    apply Set.Subset.trans h (nilradical_le_prime p.asIdeal)

theorem zeroLocus_sup (I J : Ideal R) :
    zeroLocus ((I ‚äî J : Ideal R) : Set R) = zeroLocus I ‚à© zeroLocus J :=
  (gc R).l_sup

theorem zeroLocus_union (s s' : Set R) : zeroLocus (s ‚à™ s') = zeroLocus s ‚à© zeroLocus s' :=
  (gc_set R).l_sup

theorem vanishingIdeal_union (t t' : Set (PrimeSpectrum R)) :
    vanishingIdeal (t ‚à™ t') = vanishingIdeal t ‚äì vanishingIdeal t' :=
  (gc R).u_inf

theorem zeroLocus_iSup {Œπ : Sort*} (I : Œπ ‚Üí Ideal R) :
    zeroLocus ((‚®Ü i, I i : Ideal R) : Set R) = ‚ãÇ i, zeroLocus (I i) :=
  (gc R).l_iSup

theorem zeroLocus_iUnion {Œπ : Sort*} (s : Œπ ‚Üí Set R) :
    zeroLocus (‚ãÉ i, s i) = ‚ãÇ i, zeroLocus (s i) :=
  (gc_set R).l_iSup

theorem zeroLocus_iUnion‚ÇÇ {Œπ : Sort*} {Œ∫ : (i : Œπ) ‚Üí Sort*} (s : ‚àÄ i, Œ∫ i ‚Üí Set R) :
    zeroLocus (‚ãÉ (i) (j), s i j) = ‚ãÇ (i) (j), zeroLocus (s i j) :=
  (gc_set R).l_iSup‚ÇÇ

theorem zeroLocus_bUnion (s : Set (Set R)) :
    zeroLocus (‚ãÉ s' ‚àà s, s' : Set R) = ‚ãÇ s' ‚àà s, zeroLocus s' := by simp only [zeroLocus_iUnion]

theorem vanishingIdeal_iUnion {Œπ : Sort*} (t : Œπ ‚Üí Set (PrimeSpectrum R)) :
    vanishingIdeal (‚ãÉ i, t i) = ‚®Ö i, vanishingIdeal (t i) :=
  (gc R).u_iInf

theorem zeroLocus_inf (I J : Ideal R) :
    zeroLocus ((I ‚äì J : Ideal R) : Set R) = zeroLocus I ‚à™ zeroLocus J :=
  Set.ext fun x => x.2.inf_le

theorem union_zeroLocus (s s' : Set R) :
    zeroLocus s ‚à™ zeroLocus s' = zeroLocus (Ideal.span s ‚äì Ideal.span s' : Ideal R) := by
  rw [zeroLocus_inf]
  simp

theorem zeroLocus_mul (I J : Ideal R) :
    zeroLocus ((I * J : Ideal R) : Set R) = zeroLocus I ‚à™ zeroLocus J :=
  Set.ext fun x => x.2.mul_le

theorem zeroLocus_singleton_mul (f g : R) :
    zeroLocus ({f * g} : Set R) = zeroLocus {f} ‚à™ zeroLocus {g} :=
  Set.ext fun x => by simpa using x.2.mul_mem_iff_mem_or_mem

@[simp]
theorem zeroLocus_pow (I : Ideal R) {n : ‚Ñï} (hn : n ‚â† 0) :
    zeroLocus ((I ^ n : Ideal R) : Set R) = zeroLocus I :=
  zeroLocus_radical (I ^ n) ‚ñ∏ (I.radical_pow hn).symm ‚ñ∏ zeroLocus_radical I

@[simp]
theorem zeroLocus_singleton_pow (f : R) (n : ‚Ñï) (hn : 0 < n) :
    zeroLocus ({f ^ n} : Set R) = zeroLocus {f} :=
  Set.ext fun x => by simpa using x.2.pow_mem_iff_mem n hn

theorem sup_vanishingIdeal_le (t t' : Set (PrimeSpectrum R)) :
    vanishingIdeal t ‚äî vanishingIdeal t' ‚â§ vanishingIdeal (t ‚à© t') := by
  intro r
  rw [Submodule.mem_sup, mem_vanishingIdeal]
  rintro ‚ü®f, hf, g, hg, rfl‚ü© x ‚ü®hxt, hxt'‚ü©
  rw [mem_vanishingIdeal] at hf hg
  apply Submodule.add_mem <;> solve_by_elim

theorem mem_compl_zeroLocus_iff_not_mem {f : R} {I : PrimeSpectrum R} :
    I ‚àà (zeroLocus {f} : Set (PrimeSpectrum R))·∂ú ‚Üî f ‚àâ I.asIdeal := by
  rw [Set.mem_compl_iff, mem_zeroLocus, Set.singleton_subset_iff]; rfl

@[simp]
lemma zeroLocus_insert_zero (s : Set R) : zeroLocus (insert 0 s) = zeroLocus s := by
  rw [‚Üê Set.union_singleton, zeroLocus_union, zeroLocus_singleton_zero, Set.inter_univ]

@[simp]
lemma zeroLocus_diff_singleton_zero (s : Set R) : zeroLocus (s \ {0}) = zeroLocus s := by
  rw [‚Üê zeroLocus_insert_zero, ‚Üê zeroLocus_insert_zero (s := s)]; simp

lemma zeroLocus_smul_of_isUnit {r : R} (hr : IsUnit r) (s : Set R) :
    zeroLocus (r ‚Ä¢ s) = zeroLocus s := by
  ext; simp [Set.subset_def, ‚Üê Set.image_smul, Ideal.unit_mul_mem_iff_mem _ hr]

section Order

/-!
## The specialization order

We endow `PrimeSpectrum R` with a partial order induced from the ideal lattice.
This is exactly the specialization order.
See the corresponding section at `Mathlib.AlgebraicGeometry.PrimeSpectrum.Basic`.
-/

instance : PartialOrder (PrimeSpectrum R) :=
  PartialOrder.lift asIdeal (@PrimeSpectrum.ext _ _)

@[simp]
theorem asIdeal_le_asIdeal (x y : PrimeSpectrum R) : x.asIdeal ‚â§ y.asIdeal ‚Üî x ‚â§ y :=
  Iff.rfl

@[simp]
theorem asIdeal_lt_asIdeal (x y : PrimeSpectrum R) : x.asIdeal < y.asIdeal ‚Üî x < y :=
  Iff.rfl

instance [IsDomain R] : OrderBot (PrimeSpectrum R) where
  bot := ‚ü®‚ä•, Ideal.bot_prime‚ü©
  bot_le I := @bot_le _ _ _ I.asIdeal

instance {R : Type*} [Field R] : Unique (PrimeSpectrum R) where
  default := ‚ä•
  uniq x := PrimeSpectrum.ext ((IsSimpleOrder.eq_bot_or_eq_top _).resolve_right x.2.ne_top)

end Order

section Noetherian

open Submodule

variable (R : Type u) [CommRing R] [IsNoetherianRing R]
variable {A : Type u} [CommRing A] [IsDomain A] [IsNoetherianRing A]

/-- In a noetherian ring, every ideal contains a product of prime ideals
([samuel, ¬ß 3.3, Lemma 3])-/
theorem exists_primeSpectrum_prod_le (I : Ideal R) :
    ‚àÉ Z : Multiset (PrimeSpectrum R), Multiset.prod (Z.map asIdeal) ‚â§ I := by
  -- Porting note: Need to specify `P` explicitly
  refine IsNoetherian.induction
    (P := fun I => ‚àÉ Z : Multiset (PrimeSpectrum R), Multiset.prod (Z.map asIdeal) ‚â§ I)
    (fun (M : Ideal R) hgt => ?_) I
  by_cases h_prM : M.IsPrime
  ¬∑ use {‚ü®M, h_prM‚ü©}
    rw [Multiset.map_singleton, Multiset.prod_singleton]
  by_cases htop : M = ‚ä§
  ¬∑ rw [htop]
    exact ‚ü®0, le_top‚ü©
  have lt_add : ‚àÄ z ‚àâ M, M < M + span R {z} := by
    intro z hz
    refine lt_of_le_of_ne le_sup_left fun m_eq => hz ?_
    rw [m_eq]
    exact Ideal.mem_sup_right (mem_span_singleton_self z)
  obtain ‚ü®x, hx, y, hy, hxy‚ü© := (Ideal.not_isPrime_iff.mp h_prM).resolve_left htop
  obtain ‚ü®Wx, h_Wx‚ü© := hgt (M + span R {x}) (lt_add _ hx)
  obtain ‚ü®Wy, h_Wy‚ü© := hgt (M + span R {y}) (lt_add _ hy)
  use Wx + Wy
  rw [Multiset.map_add, Multiset.prod_add]
  apply le_trans (Submodule.mul_le_mul h_Wx h_Wy)
  rw [add_mul]
  apply sup_le (show M * (M + span R {y}) ‚â§ M from Ideal.mul_le_right)
  rw [mul_add]
  apply sup_le (show span R {x} * M ‚â§ M from Ideal.mul_le_left)
  rwa [span_mul_span, Set.singleton_mul_singleton, span_singleton_le_iff_mem]

/-- In a noetherian integral domain which is not a field, every non-zero ideal contains a non-zero
  product of prime ideals; in a field, the whole ring is a non-zero ideal containing only 0 as
  product or prime ideals ([samuel, ¬ß 3.3, Lemma 3]) -/
theorem exists_primeSpectrum_prod_le_and_ne_bot_of_domain (h_fA : ¬¨IsField A) {I : Ideal A}
    (h_nzI : I ‚â† ‚ä•) :
    ‚àÉ Z : Multiset (PrimeSpectrum A),
      Multiset.prod (Z.map asIdeal) ‚â§ I ‚àß Multiset.prod (Z.map asIdeal) ‚â† ‚ä• := by
  revert h_nzI
  -- Porting note: Need to specify `P` explicitly
  refine IsNoetherian.induction (P := fun I => I ‚â† ‚ä• ‚Üí ‚àÉ Z : Multiset (PrimeSpectrum A),
      Multiset.prod (Z.map asIdeal) ‚â§ I ‚àß Multiset.prod (Z.map asIdeal) ‚â† ‚ä•)
    (fun (M : Ideal A) hgt => ?_) I
  intro h_nzM
  have hA_nont : Nontrivial A := IsDomain.toNontrivial
  by_cases h_topM : M = ‚ä§
  ¬∑ rcases h_topM with rfl
    obtain ‚ü®p_id, h_nzp, h_pp‚ü© : ‚àÉ p : Ideal A, p ‚â† ‚ä• ‚àß p.IsPrime := by
      apply Ring.not_isField_iff_exists_prime.mp h_fA
    use ({‚ü®p_id, h_pp‚ü©} : Multiset (PrimeSpectrum A)), le_top
    rwa [Multiset.map_singleton, Multiset.prod_singleton]
  by_cases h_prM : M.IsPrime
  ¬∑ use ({‚ü®M, h_prM‚ü©} : Multiset (PrimeSpectrum A))
    rw [Multiset.map_singleton, Multiset.prod_singleton]
    exact ‚ü®le_rfl, h_nzM‚ü©
  obtain ‚ü®x, hx, y, hy, h_xy‚ü© := (Ideal.not_isPrime_iff.mp h_prM).resolve_left h_topM
  have lt_add : ‚àÄ z ‚àâ M, M < M + span A {z} := by
    intro z hz
    refine lt_of_le_of_ne le_sup_left fun m_eq => hz ?_
    rw [m_eq]
    exact mem_sup_right (mem_span_singleton_self z)
  obtain ‚ü®Wx, h_Wx_le, h_Wx_ne‚ü© := hgt (M + span A {x}) (lt_add _ hx) (ne_bot_of_gt (lt_add _ hx))
  obtain ‚ü®Wy, h_Wy_le, h_Wx_ne‚ü© := hgt (M + span A {y}) (lt_add _ hy) (ne_bot_of_gt (lt_add _ hy))
  use Wx + Wy
  rw [Multiset.map_add, Multiset.prod_add]
  refine ‚ü®le_trans (Submodule.mul_le_mul h_Wx_le h_Wy_le) ?_, mt Ideal.mul_eq_bot.mp ?_‚ü©
  ¬∑ rw [add_mul]
    apply sup_le (show M * (M + span A {y}) ‚â§ M from Ideal.mul_le_right)
    rw [mul_add]
    apply sup_le (show span A {x} * M ‚â§ M from Ideal.mul_le_left)
    rwa [span_mul_span, Set.singleton_mul_singleton, span_singleton_le_iff_mem]
  ¬∑ rintro (hx | hy) <;> contradiction

end Noetherian

end CommSemiRing

end PrimeSpectrum

open PrimeSpectrum

/-- The pullback of an element of `PrimeSpectrum S` along a ring homomorphism `f : R ‚Üí+* S`.
The bundled continuous version is `PrimeSpectrum.comap`. -/
abbrev RingHom.specComap {R S : Type*} [CommSemiring R] [CommSemiring S] (f : R ‚Üí+* S) :
    PrimeSpectrum S ‚Üí PrimeSpectrum R :=
  fun y => ‚ü®Ideal.comap f y.asIdeal, inferInstance‚ü©

namespace PrimeSpectrum

open RingHom

variable {R S} {S' : Type*} [CommSemiring R] [CommSemiring S] [CommSemiring S']

theorem preimage_specComap_zeroLocus_aux (f : R ‚Üí+* S) (s : Set R) :
    f.specComap ‚Åª¬π' zeroLocus s = zeroLocus (f '' s) := by
  ext x
  simp only [mem_zeroLocus, Set.image_subset_iff, Set.mem_preimage, mem_zeroLocus, Ideal.coe_comap]

variable (f : R ‚Üí+* S)

@[simp]
theorem specComap_asIdeal (y : PrimeSpectrum S) :
    (f.specComap y).asIdeal = Ideal.comap f y.asIdeal :=
  rfl

@[simp]
theorem specComap_id : (RingHom.id R).specComap = fun x => x :=
  rfl

@[simp]
theorem specComap_comp (f : R ‚Üí+* S) (g : S ‚Üí+* S') :
    (g.comp f).specComap = f.specComap.comp g.specComap :=
  rfl

theorem specComap_comp_apply (f : R ‚Üí+* S) (g : S ‚Üí+* S') (x : PrimeSpectrum S') :
    (g.comp f).specComap x = f.specComap (g.specComap x) :=
  rfl

@[simp]
theorem preimage_specComap_zeroLocus (s : Set R) :
    f.specComap ‚Åª¬π' zeroLocus s = zeroLocus (f '' s) :=
  preimage_specComap_zeroLocus_aux f s

theorem specComap_injective_of_surjective (f : R ‚Üí+* S) (hf : Function.Surjective f) :
    Function.Injective f.specComap := fun x y h =>
  PrimeSpectrum.ext
    (Ideal.comap_injective_of_surjective f hf
      (congr_arg PrimeSpectrum.asIdeal h : (f.specComap x).asIdeal = (f.specComap y).asIdeal))

/-- `RingHom.specComap` of an isomorphism of rings as an equivalence of their prime spectra. -/
@[simps apply symm_apply]
def comapEquiv (e : R ‚âÉ+* S) : PrimeSpectrum R ‚âÉ PrimeSpectrum S where
  toFun := e.symm.toRingHom.specComap
  invFun := e.toRingHom.specComap
  left_inv x := by
    rw [‚Üê specComap_comp_apply, RingEquiv.toRingHom_eq_coe,
      RingEquiv.toRingHom_eq_coe, RingEquiv.symm_comp]
    rfl
  right_inv x := by
    rw [‚Üê specComap_comp_apply, RingEquiv.toRingHom_eq_coe,
      RingEquiv.toRingHom_eq_coe, RingEquiv.comp_symm]
    rfl

variable (S)

theorem localization_specComap_injective [Algebra R S] (M : Submonoid R) [IsLocalization M S] :
    Function.Injective (algebraMap R S).specComap := by
  intro p q h
  replace h := _root_.congr_arg (fun x : PrimeSpectrum R => Ideal.map (algebraMap R S) x.asIdeal) h
  dsimp only [specComap] at h
  rw [IsLocalization.map_comap M S, IsLocalization.map_comap M S] at h
  ext1
  exact h

theorem localization_specComap_range [Algebra R S] (M : Submonoid R) [IsLocalization M S] :
    Set.range (algebraMap R S).specComap = { p | Disjoint (M : Set R) p.asIdeal } := by
  ext x
  constructor
  ¬∑ simp_rw [disjoint_iff_inf_le]
    rintro ‚ü®p, rfl‚ü© x ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©
    exact (p.2.1 : ¬¨_) (p.asIdeal.eq_top_of_isUnit_mem hx‚ÇÇ (IsLocalization.map_units S ‚ü®x, hx‚ÇÅ‚ü©))
  ¬∑ intro h
    use ‚ü®x.asIdeal.map (algebraMap R S), IsLocalization.isPrime_of_isPrime_disjoint M S _ x.2 h‚ü©
    ext1
    exact IsLocalization.comap_map_of_isPrime_disjoint M S _ x.2 h

section Pi

variable {Œπ} (R : Œπ ‚Üí Type*) [‚àÄ i, CommSemiring (R i)]

/-- The canonical map from a disjoint union of prime spectra of commutative semirings to
the prime spectrum of the product semiring. -/
/- TODO: show this is always a topological embedding (even when Œπ is infinite)
and is a homeomorphism when Œπ is finite. -/
@[simps] def sigmaToPi : (Œ£ i, PrimeSpectrum (R i)) ‚Üí PrimeSpectrum (Œ† i, R i)
  | ‚ü®i, p‚ü© => (Pi.evalRingHom R i).specComap p

theorem sigmaToPi_injective : (sigmaToPi R).Injective := fun ‚ü®i, p‚ü© ‚ü®j, q‚ü© eq ‚Ü¶ by
  obtain rfl | ne := eq_or_ne i j
  ¬∑ congr; ext x
    simpa using congr_arg (Function.update (0 : ‚àÄ i, R i) i x ‚àà ¬∑.asIdeal) eq
  ¬∑ refine (p.1.ne_top_iff_one.mp p.2.ne_top ?_).elim
    have : Function.update (1 : ‚àÄ i, R i) j 0 ‚àà (sigmaToPi R ‚ü®j, q‚ü©).asIdeal := by simp
    simpa [‚Üê eq, Function.update_of_ne ne]

variable [Infinite Œπ] [‚àÄ i, Nontrivial (R i)]

/-- An infinite product of nontrivial commutative semirings has a maximal ideal outside of the
range of `sigmaToPi`, i.e. is not of the form `œÄ·µ¢‚Åª¬π(ùî≠)` for some prime `ùî≠ ‚äÇ R i`, where
`œÄ·µ¢ : (Œ† i, R i) ‚Üí+* R i` is the projection. For a complete description of all prime ideals,
see https://math.stackexchange.com/a/1563190. -/
theorem exists_maximal_nmem_range_sigmaToPi_of_infinite :
    ‚àÉ (I : Ideal (Œ† i, R i)) (_ : I.IsMaximal), ‚ü®I, inferInstance‚ü© ‚àâ Set.range (sigmaToPi R) := by
  let J : Ideal (Œ† i, R i) := -- `J := Œ†‚ÇÄ i, R i` is an ideal in `Œ† i, R i`
  { __ := AddMonoidHom.mrange DFinsupp.coeFnAddMonoidHom
    smul_mem' := by
      rintro r _ ‚ü®x, rfl‚ü©
      refine ‚ü®.mk x.support fun i ‚Ü¶ r i * x i, funext fun i ‚Ü¶ show dite _ _ _ = _ from ?_‚ü©
      simp_rw [DFinsupp.coeFnAddMonoidHom]
      refine dite_eq_left_iff.mpr fun h ‚Ü¶ ?_
      rw [DFinsupp.not_mem_support_iff.mp h, mul_zero] }
  have ‚ü®I, max, le‚ü© := J.exists_le_maximal <| (Ideal.ne_top_iff_one _).mpr <| by
    -- take a maximal ideal I containing J
    rintro ‚ü®x, hx‚ü©
    have ‚ü®i, hi‚ü© := x.support.exists_not_mem
    simpa [DFinsupp.coeFnAddMonoidHom, DFinsupp.not_mem_support_iff.mp hi] using congr_fun hx i
  refine ‚ü®I, max, fun ‚ü®‚ü®i, p‚ü©, eq‚ü© ‚Ü¶ ?_‚ü©
  -- then I is not in the range of `sigmaToPi`
  have : ‚áë(DFinsupp.single i 1) ‚àâ (sigmaToPi R ‚ü®i, p‚ü©).asIdeal := by
    simpa using p.1.ne_top_iff_one.mp p.2.ne_top
  rw [eq] at this
  exact this (le ‚ü®.single i 1, rfl‚ü©)

theorem sigmaToPi_not_surjective_of_infinite : ¬¨ (sigmaToPi R).Surjective := fun surj ‚Ü¶
  have ‚ü®_, _, nmem‚ü© := exists_maximal_nmem_range_sigmaToPi_of_infinite R
  (Set.range_eq_univ.mpr surj ‚ñ∏ nmem) ‚ü®‚ü©

end Pi

end PrimeSpectrum

section SpecOfSurjective

open Function RingHom

variable [CommRing R] [CommRing S]
variable (f : R ‚Üí+* S)
variable {R}

theorem image_specComap_zeroLocus_eq_zeroLocus_comap (hf : Surjective f) (I : Ideal S) :
    f.specComap '' zeroLocus I = zeroLocus (I.comap f) := by
  simp only [Set.ext_iff, Set.mem_image, mem_zeroLocus, SetLike.coe_subset_coe]
  refine fun p => ‚ü®?_, fun h_I_p => ?_‚ü©
  ¬∑ rintro ‚ü®p, hp, rfl‚ü© a ha
    exact hp ha
  ¬∑ have hp : ker f ‚â§ p.asIdeal := (Ideal.comap_mono bot_le).trans h_I_p
    refine ‚ü®‚ü®p.asIdeal.map f, Ideal.map_isPrime_of_surjective hf hp‚ü©, fun x hx => ?_, ?_‚ü©
    ¬∑ obtain ‚ü®x', rfl‚ü© := hf x
      exact Ideal.mem_map_of_mem f (h_I_p hx)
    ¬∑ ext x
      rw [specComap_asIdeal, Ideal.mem_comap, Ideal.mem_map_iff_of_surjective f hf]
      refine ‚ü®?_, fun hx => ‚ü®x, hx, rfl‚ü©‚ü©
      rintro ‚ü®x', hx', heq‚ü©
      rw [‚Üê sub_sub_cancel x' x]
      refine p.asIdeal.sub_mem hx' (hp ?_)
      rwa [mem_ker, map_sub, sub_eq_zero]

theorem range_specComap_of_surjective (hf : Surjective f) :
    Set.range f.specComap = zeroLocus (ker f) := by
  rw [‚Üê Set.image_univ]
  convert image_specComap_zeroLocus_eq_zeroLocus_comap _ _ hf _
  rw [zeroLocus_bot]

end SpecOfSurjective
