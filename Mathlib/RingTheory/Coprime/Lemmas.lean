/-
Copyright (c) 2020 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau, Ken Lee, Chris Hughes
-/
import Mathlib.Algebra.BigOperators.Ring
import Mathlib.Data.Fintype.Basic
import Mathlib.Data.Int.GCD
import Mathlib.RingTheory.Coprime.Basic

#align_import ring_theory.coprime.lemmas from "leanprover-community/mathlib"@"509de852e1de55e1efa8eacfa11df0823f26f226"

/-!
# Additional lemmas about elements of a ring satisfying `IsCoprime`

These lemmas are in a separate file to the definition of `IsCoprime` as they require more imports.

Notably, this includes lemmas about `Finset.prod` as this requires importing big_operators, and
lemmas about `HasPow` since these are easiest to prove via `Finset.prod`.

-/

universe u v

variable {R : Type u} {I : Type v} [CommSemiring R] {x y z : R} {s : I ‚Üí R} {t : Finset I}

open BigOperators

section

open Classical

theorem Int.isCoprime_iff_gcd_eq_one {m n : ‚Ñ§} : IsCoprime m n ‚Üî Int.gcd m n = 1 := by
  constructor
  -- ‚ä¢ IsCoprime m n ‚Üí gcd m n = 1
  ¬∑ rintro ‚ü®a, b, h‚ü©
    -- ‚ä¢ gcd m n = 1
    have : 1 = m * a + n * b := by rwa [mul_comm m, mul_comm n, eq_comm]
    -- ‚ä¢ gcd m n = 1
    exact Nat.dvd_one.mp (Int.gcd_dvd_iff.mpr ‚ü®a, b, this‚ü©)
    -- üéâ no goals
  ¬∑ rw [‚Üê Int.ofNat_inj, IsCoprime, Int.gcd_eq_gcd_ab, mul_comm m, mul_comm n, Nat.cast_one]
    -- ‚ä¢ gcdA m n * m + gcdB m n * n = 1 ‚Üí ‚àÉ a b, a * m + b * n = 1
    intro h
    -- ‚ä¢ ‚àÉ a b, a * m + b * n = 1
    exact ‚ü®_, _, h‚ü©
    -- üéâ no goals

theorem Nat.isCoprime_iff_coprime {m n : ‚Ñï} : IsCoprime (m : ‚Ñ§) n ‚Üî Nat.coprime m n := by
  rw [Int.isCoprime_iff_gcd_eq_one, Int.coe_nat_gcd]
  -- üéâ no goals
#align nat.is_coprime_iff_coprime Nat.isCoprime_iff_coprime

alias ‚ü®IsCoprime.nat_coprime, Nat.coprime.isCoprime‚ü© := Nat.isCoprime_iff_coprime
#align is_coprime.nat_coprime IsCoprime.nat_coprime
#align nat.coprime.is_coprime Nat.coprime.isCoprime

theorem ne_zero_or_ne_zero_of_nat_coprime {A : Type u} [CommRing A] [Nontrivial A] {a b : ‚Ñï}
    (h : Nat.coprime a b) : (a : A) ‚â† 0 ‚à® (b : A) ‚â† 0 :=
  IsCoprime.ne_zero_or_ne_zero (R := A) <| by
    simpa only [map_natCast] using IsCoprime.map (Nat.coprime.isCoprime h) (Int.castRingHom A)
    -- üéâ no goals

theorem IsCoprime.prod_left : (‚àÄ i ‚àà t, IsCoprime (s i) x) ‚Üí IsCoprime (‚àè i in t, s i) x :=
  Finset.induction_on t (fun _ ‚Ü¶ isCoprime_one_left) fun b t hbt ih H ‚Ü¶ by
    rw [Finset.prod_insert hbt]
    -- ‚ä¢ IsCoprime (s b * ‚àè x in t, s x) x
    rw [Finset.forall_mem_insert] at H
    -- ‚ä¢ IsCoprime (s b * ‚àè x in t, s x) x
    exact H.1.mul_left (ih H.2)
    -- üéâ no goals
#align is_coprime.prod_left IsCoprime.prod_left

theorem IsCoprime.prod_right : (‚àÄ i ‚àà t, IsCoprime x (s i)) ‚Üí IsCoprime x (‚àè i in t, s i) := by
  simpa only [isCoprime_comm] using IsCoprime.prod_left (R := R)
  -- üéâ no goals
#align is_coprime.prod_right IsCoprime.prod_right

theorem IsCoprime.prod_left_iff : IsCoprime (‚àè i in t, s i) x ‚Üî ‚àÄ i ‚àà t, IsCoprime (s i) x :=
  Finset.induction_on t (iff_of_true isCoprime_one_left fun _ ‚Ü¶ by simp) fun b t hbt ih ‚Ü¶ by
                                                                   -- üéâ no goals
    rw [Finset.prod_insert hbt, IsCoprime.mul_left_iff, ih, Finset.forall_mem_insert]
    -- üéâ no goals
#align is_coprime.prod_left_iff IsCoprime.prod_left_iff

theorem IsCoprime.prod_right_iff : IsCoprime x (‚àè i in t, s i) ‚Üî ‚àÄ i ‚àà t, IsCoprime x (s i) := by
  simpa only [isCoprime_comm] using IsCoprime.prod_left_iff (R := R)
  -- üéâ no goals
#align is_coprime.prod_right_iff IsCoprime.prod_right_iff

theorem IsCoprime.of_prod_left (H1 : IsCoprime (‚àè i in t, s i) x) (i : I) (hit : i ‚àà t) :
    IsCoprime (s i) x :=
  IsCoprime.prod_left_iff.1 H1 i hit
#align is_coprime.of_prod_left IsCoprime.of_prod_left

theorem IsCoprime.of_prod_right (H1 : IsCoprime x (‚àè i in t, s i)) (i : I) (hit : i ‚àà t) :
    IsCoprime x (s i) :=
  IsCoprime.prod_right_iff.1 H1 i hit
#align is_coprime.of_prod_right IsCoprime.of_prod_right

-- porting note: removed names of things due to linter, but they seem helpful
theorem Finset.prod_dvd_of_coprime :
    ‚àÄ (_ : (t : Set I).Pairwise (IsCoprime on s)) (_ : ‚àÄ i ‚àà t, s i ‚à£ z), (‚àè x in t, s x) ‚à£ z :=
  Finset.induction_on t (fun _ _ ‚Ü¶ one_dvd z)
    (by
      intro a r har ih Hs Hs1
      -- ‚ä¢ ‚àè x in insert a r, s x ‚à£ z
      rw [Finset.prod_insert har]
      -- ‚ä¢ s a * ‚àè x in r, s x ‚à£ z
      have aux1 : a ‚àà (‚Üë(insert a r) : Set I) := Finset.mem_insert_self a r
      -- ‚ä¢ s a * ‚àè x in r, s x ‚à£ z
      refine'
        (IsCoprime.prod_right fun i hir ‚Ü¶
              Hs aux1 (Finset.mem_insert_of_mem hir) <| by
                rintro rfl
                exact har hir).mul_dvd
          (Hs1 a aux1) (ih (Hs.mono _) fun i hi ‚Ü¶ Hs1 i <| Finset.mem_insert_of_mem hi)
      simp only [Finset.coe_insert, Set.subset_insert])
      -- üéâ no goals
#align finset.prod_dvd_of_coprime Finset.prod_dvd_of_coprime

theorem Fintype.prod_dvd_of_coprime [Fintype I] (Hs : Pairwise (IsCoprime on s))
    (Hs1 : ‚àÄ i, s i ‚à£ z) : (‚àè x, s x) ‚à£ z :=
  Finset.prod_dvd_of_coprime (Hs.set_pairwise _) fun i _ ‚Ü¶ Hs1 i
#align fintype.prod_dvd_of_coprime Fintype.prod_dvd_of_coprime

end

open Finset

theorem exists_sum_eq_one_iff_pairwise_coprime [DecidableEq I] (h : t.Nonempty) :
    (‚àÉ Œº : I ‚Üí R, (‚àë i in t, Œº i * ‚àè j in t \ {i}, s j) = 1) ‚Üî
      Pairwise (IsCoprime on fun i : t ‚Ü¶ s i) := by
  refine' h.cons_induction _ _
  -- ‚ä¢ ‚àÄ (a : I), (‚àÉ Œº, ‚àë i in {a}, Œº i * ‚àè j in {a} \ {i}, s j = 1) ‚Üî Pairwise (Is ‚Ä¶
  ¬∑ simp only [sum_singleton, Finset.sdiff_self, prod_empty, mul_one, exists_apply_eq,
               Pairwise, Ne.def, true_iff_iff]
    rintro a ‚ü®i, hi‚ü© ‚ü®j, hj‚ü© h
    -- ‚ä¢ (IsCoprime on fun i => s ‚Üëi) { val := i, property := hi } { val := j, proper ‚Ä¶
    rw [Finset.mem_singleton] at hi hj
    -- ‚ä¢ (IsCoprime on fun i => s ‚Üëi) { val := i, property := hi‚úù } { val := j, prope ‚Ä¶
    simp [hi, hj] at h
    -- üéâ no goals
  intro a t hat h ih
  -- ‚ä¢ (‚àÉ Œº, ‚àë i in cons a t hat, Œº i * ‚àè j in cons a t hat \ {i}, s j = 1) ‚Üî Pairw ‚Ä¶
  rw [pairwise_cons']
  -- ‚ä¢ (‚àÉ Œº, ‚àë i in cons a t hat, Œº i * ‚àè j in cons a t hat \ {i}, s j = 1) ‚Üî Pairw ‚Ä¶
  have mem : ‚àÄ x ‚àà t, a ‚àà insert a t \ {x} := fun x hx ‚Ü¶ by
    rw [mem_sdiff, mem_singleton]
    refine ‚ü®mem_insert_self _ _, fun ha ‚Ü¶ hat (ha ‚ñ∏ hx)‚ü©
  constructor
  -- ‚ä¢ (‚àÉ Œº, ‚àë i in cons a t hat, Œº i * ‚àè j in cons a t hat \ {i}, s j = 1) ‚Üí Pairw ‚Ä¶
  ¬∑ rintro ‚ü®Œº, hŒº‚ü©
    -- ‚ä¢ Pairwise (IsCoprime on fun a => s ‚Üëa) ‚àß ‚àÄ (b : I), b ‚àà t ‚Üí IsCoprime (s a) ( ‚Ä¶
    rw [sum_cons, cons_eq_insert, sdiff_singleton_eq_erase, erase_insert hat] at hŒº
    -- ‚ä¢ Pairwise (IsCoprime on fun a => s ‚Üëa) ‚àß ‚àÄ (b : I), b ‚àà t ‚Üí IsCoprime (s a) ( ‚Ä¶
    refine' ‚ü®ih.mp ‚ü®Pi.single h.choose (Œº a * s h.choose) + Œº * fun _ ‚Ü¶ s a, ?_‚ü©, fun b hb ‚Ü¶ ?_‚ü©
    -- ‚ä¢ ‚àë i in t, (Pi.single (Exists.choose h) (Œº a * s (Exists.choose h)) + Œº * fun ‚Ä¶
    ¬∑ rw [prod_eq_mul_prod_diff_singleton h.choose_spec, ‚Üê mul_assoc, ‚Üê
        @if_pos _ _ h.choose_spec R (_ * _) 0, ‚Üê sum_pi_single', ‚Üê sum_add_distrib] at hŒº
      rw [‚Üê hŒº, sum_congr rfl]
      -- ‚ä¢ ‚àÄ (x : I), x ‚àà t ‚Üí (Pi.single (Exists.choose h) (Œº a * s (Exists.choose h))  ‚Ä¶
      intro x hx
      -- ‚ä¢ (Pi.single (Exists.choose h) (Œº a * s (Exists.choose h)) + Œº * fun x => s a) ‚Ä¶
      dsimp -- porting note: terms were showing as sort of `HAdd.hadd` instead of `+`
      -- ‚ä¢ (Pi.single (Exists.choose h) (Œº a * s (Exists.choose h)) x + Œº x * s a) * ‚àè  ‚Ä¶
      -- this whole proof pretty much breaks and has to be rewritten from scratch
      rw [add_mul]
      -- ‚ä¢ Pi.single (Exists.choose h) (Œº a * s (Exists.choose h)) x * ‚àè j in t \ {x},  ‚Ä¶
      congr 1
      -- ‚ä¢ Pi.single (Exists.choose h) (Œº a * s (Exists.choose h)) x * ‚àè j in t \ {x},  ‚Ä¶
      ¬∑ by_cases hx : x = h.choose
        -- ‚ä¢ Pi.single (Exists.choose h) (Œº a * s (Exists.choose h)) x * ‚àè j in t \ {x},  ‚Ä¶
        ¬∑ rw [hx, Pi.single_eq_same, Pi.single_eq_same]
          -- üéâ no goals
        ¬∑ rw [Pi.single_eq_of_ne hx, Pi.single_eq_of_ne hx, zero_mul]
          -- üéâ no goals
      ¬∑ rw [mul_assoc]
        -- ‚ä¢ Œº x * (s a * ‚àè j in t \ {x}, s j) = Œº x * ‚àè j in insert a t \ {x}, s j
        congr
        -- ‚ä¢ s a * ‚àè j in t \ {x}, s j = ‚àè j in insert a t \ {x}, s j
        rw [prod_eq_prod_diff_singleton_mul (mem x hx) _, mul_comm]
        -- ‚ä¢ (‚àè j in t \ {x}, s j) * s a = (‚àè x in (insert a t \ {x}) \ {a}, s x) * s a
        congr 2
        -- ‚ä¢ t \ {x} = (insert a t \ {x}) \ {a}
        rw [sdiff_sdiff_comm, sdiff_singleton_eq_erase a, erase_insert hat]
        -- üéâ no goals
    ¬∑ have : IsCoprime (s b) (s a) :=
        ‚ü®Œº a * ‚àè i in t \ {b}, s i, ‚àë i in t, Œº i * ‚àè j in t \ {i}, s j, ?_‚ü©
      ¬∑ exact ‚ü®this.symm, this‚ü©
        -- üéâ no goals
      rw [mul_assoc, ‚Üê prod_eq_prod_diff_singleton_mul hb, sum_mul, ‚Üê hŒº, sum_congr rfl]
      -- ‚ä¢ ‚àÄ (x : I), x ‚àà t ‚Üí (Œº x * ‚àè j in t \ {x}, s j) * s a = Œº x * ‚àè j in insert a ‚Ä¶
      intro x hx
      -- ‚ä¢ (Œº x * ‚àè j in t \ {x}, s j) * s a = Œº x * ‚àè j in insert a t \ {x}, s j
      rw [mul_assoc]
      -- ‚ä¢ Œº x * ((‚àè j in t \ {x}, s j) * s a) = Œº x * ‚àè j in insert a t \ {x}, s j
      congr
      -- ‚ä¢ (‚àè j in t \ {x}, s j) * s a = ‚àè j in insert a t \ {x}, s j
      rw [prod_eq_prod_diff_singleton_mul (mem x hx) _]
      -- ‚ä¢ (‚àè j in t \ {x}, s j) * s a = (‚àè x in (insert a t \ {x}) \ {a}, s x) * s a
      congr 2
      -- ‚ä¢ t \ {x} = (insert a t \ {x}) \ {a}
      rw [sdiff_sdiff_comm, sdiff_singleton_eq_erase a, erase_insert hat]
      -- üéâ no goals
  ¬∑ rintro ‚ü®hs, Hb‚ü©
    -- ‚ä¢ ‚àÉ Œº, ‚àë i in cons a t hat, Œº i * ‚àè j in cons a t hat \ {i}, s j = 1
    obtain ‚ü®Œº, hŒº‚ü© := ih.mpr hs
    -- ‚ä¢ ‚àÉ Œº, ‚àë i in cons a t hat, Œº i * ‚àè j in cons a t hat \ {i}, s j = 1
    obtain ‚ü®u, v, huv‚ü© := IsCoprime.prod_left fun b hb ‚Ü¶ (Hb b hb).right
    -- ‚ä¢ ‚àÉ Œº, ‚àë i in cons a t hat, Œº i * ‚àè j in cons a t hat \ {i}, s j = 1
    use fun i ‚Ü¶ if i = a then u else v * Œº i
    -- ‚ä¢ ‚àë i in cons a t hat, (if i = a then u else v * Œº i) * ‚àè j in cons a t hat \  ‚Ä¶
    have hŒº' : (‚àë i in t, v * ((Œº i * ‚àè j in t \ {i}, s j) * s a)) = v * s a := by
      rw [‚Üê mul_sum, ‚Üê sum_mul, hŒº, one_mul]
    rw [sum_cons, cons_eq_insert, sdiff_singleton_eq_erase, erase_insert hat, if_pos rfl,
      ‚Üê huv, ‚Üê hŒº', sum_congr rfl]
    intro x hx
    -- ‚ä¢ (if x = a then u else v * Œº x) * ‚àè j in insert a t \ {x}, s j = v * ((Œº x *  ‚Ä¶
    rw [mul_assoc, if_neg fun ha : x = a ‚Ü¶ hat (ha.casesOn hx)]
    -- ‚ä¢ v * Œº x * ‚àè j in insert a t \ {x}, s j = v * (Œº x * ((‚àè j in t \ {x}, s j) * ‚Ä¶
    rw [mul_assoc]
    -- ‚ä¢ v * (Œº x * ‚àè j in insert a t \ {x}, s j) = v * (Œº x * ((‚àè j in t \ {x}, s j) ‚Ä¶
    congr
    -- ‚ä¢ ‚àè j in insert a t \ {x}, s j = (‚àè j in t \ {x}, s j) * s a
    rw [prod_eq_prod_diff_singleton_mul (mem x hx) _]
    -- ‚ä¢ (‚àè x in (insert a t \ {x}) \ {a}, s x) * s a = (‚àè j in t \ {x}, s j) * s a
    congr 2
    -- ‚ä¢ (insert a t \ {x}) \ {a} = t \ {x}
    rw [sdiff_sdiff_comm, sdiff_singleton_eq_erase a, erase_insert hat]
    -- üéâ no goals
#align exists_sum_eq_one_iff_pairwise_coprime exists_sum_eq_one_iff_pairwise_coprime

theorem exists_sum_eq_one_iff_pairwise_coprime' [Fintype I] [Nonempty I] [DecidableEq I] :
    (‚àÉ Œº : I ‚Üí R, (‚àë i : I, Œº i * ‚àè j in {i}·∂ú, s j) = 1) ‚Üî Pairwise (IsCoprime on s) := by
  convert exists_sum_eq_one_iff_pairwise_coprime Finset.univ_nonempty (s := s) using 1
  -- ‚ä¢ Pairwise (IsCoprime on s) ‚Üî Pairwise (IsCoprime on fun i => s ‚Üëi)
  simp only [Function.onFun, pairwise_subtype_iff_pairwise_finset', coe_univ, Set.pairwise_univ]
  -- üéâ no goals
#align exists_sum_eq_one_iff_pairwise_coprime' exists_sum_eq_one_iff_pairwise_coprime'

-- porting note: a lot of the capitalization wasn't working
theorem pairwise_coprime_iff_coprime_prod [DecidableEq I] :
    Pairwise (IsCoprime on fun i : t ‚Ü¶ s i) ‚Üî ‚àÄ i ‚àà t, IsCoprime (s i) (‚àè j in t \ {i}, s j) := by
  refine' ‚ü®fun hp i hi ‚Ü¶ IsCoprime.prod_right_iff.mpr fun j hj ‚Ü¶ ?_, fun hp ‚Ü¶ ?_‚ü©
  -- ‚ä¢ IsCoprime (s i) (s j)
  ¬∑ rw [Finset.mem_sdiff, Finset.mem_singleton] at hj
    -- ‚ä¢ IsCoprime (s i) (s j)
    obtain ‚ü®hj, ji‚ü© := hj
    -- ‚ä¢ IsCoprime (s i) (s j)
    refine @hp ‚ü®i, hi‚ü© ‚ü®j, hj‚ü© fun h ‚Ü¶ ji (congrArg Subtype.val h).symm
    -- üéâ no goals
    -- porting note: is there a better way compared to the old `congr_arg coe h`?
  ¬∑ rintro ‚ü®i, hi‚ü© ‚ü®j, hj‚ü© h
    -- ‚ä¢ (IsCoprime on fun i => s ‚Üëi) { val := i, property := hi } { val := j, proper ‚Ä¶
    apply IsCoprime.prod_right_iff.mp (hp i hi)
    -- ‚ä¢ ‚Üë{ val := j, property := hj } ‚àà t \ {i}
    exact Finset.mem_sdiff.mpr ‚ü®hj, fun f ‚Ü¶ h <| Subtype.ext (Finset.mem_singleton.mp f).symm‚ü©
    -- üéâ no goals
#align pairwise_coprime_iff_coprime_prod pairwise_coprime_iff_coprime_prod

variable {m n : ‚Ñï}

theorem IsCoprime.pow_left (H : IsCoprime x y) : IsCoprime (x ^ m) y := by
  rw [‚Üê Finset.card_range m, ‚Üê Finset.prod_const]
  -- ‚ä¢ IsCoprime (‚àè _x in range m, x) y
  exact IsCoprime.prod_left fun _ _ ‚Ü¶ H
  -- üéâ no goals
#align is_coprime.pow_left IsCoprime.pow_left

theorem IsCoprime.pow_right (H : IsCoprime x y) : IsCoprime x (y ^ n) := by
  rw [‚Üê Finset.card_range n, ‚Üê Finset.prod_const]
  -- ‚ä¢ IsCoprime x (‚àè _x in range n, y)
  exact IsCoprime.prod_right fun _ _ ‚Ü¶ H
  -- üéâ no goals
#align is_coprime.pow_right IsCoprime.pow_right

theorem IsCoprime.pow (H : IsCoprime x y) : IsCoprime (x ^ m) (y ^ n) :=
  H.pow_left.pow_right
#align is_coprime.pow IsCoprime.pow

theorem IsCoprime.pow_left_iff (hm : 0 < m) : IsCoprime (x ^ m) y ‚Üî IsCoprime x y := by
  refine' ‚ü®fun h ‚Ü¶ _, IsCoprime.pow_left‚ü©
  -- ‚ä¢ IsCoprime x y
  rw [‚Üê Finset.card_range m, ‚Üê Finset.prod_const] at h
  -- ‚ä¢ IsCoprime x y
  exact h.of_prod_left 0 (Finset.mem_range.mpr hm)
  -- üéâ no goals
  -- porting note: I'm not sure why `finset` didn't get corrected automatically to `Finset`
  -- by Mathport, nor whether this is an issue
#align is_coprime.pow_left_iff IsCoprime.pow_left_iff

theorem IsCoprime.pow_right_iff (hm : 0 < m) : IsCoprime x (y ^ m) ‚Üî IsCoprime x y :=
  isCoprime_comm.trans <| (IsCoprime.pow_left_iff hm).trans <| isCoprime_comm
#align is_coprime.pow_right_iff IsCoprime.pow_right_iff

theorem IsCoprime.pow_iff (hm : 0 < m) (hn : 0 < n) : IsCoprime (x ^ m) (y ^ n) ‚Üî IsCoprime x y :=
  (IsCoprime.pow_left_iff hm).trans <| IsCoprime.pow_right_iff hn
#align is_coprime.pow_iff IsCoprime.pow_iff
