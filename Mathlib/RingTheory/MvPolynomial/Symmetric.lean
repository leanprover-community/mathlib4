/-
Copyright (c) 2020 Hanting Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Hanting Zhang, Johan Commelin
-/
import Mathlib.Data.MvPolynomial.Rename
import Mathlib.Data.MvPolynomial.CommRing
import Mathlib.Algebra.Algebra.Subalgebra.Basic

#align_import ring_theory.mv_polynomial.symmetric from "leanprover-community/mathlib"@"2f5b500a507264de86d666a5f87ddb976e2d8de4"

/-!
# Symmetric Polynomials and Elementary Symmetric Polynomials

This file defines symmetric `MvPolynomial`s and elementary symmetric `MvPolynomial`s.
We also prove some basic facts about them.

## Main declarations

* `MvPolynomial.IsSymmetric`

* `MvPolynomial.symmetricSubalgebra`

* `MvPolynomial.esymm`

* `MvPolynomial.psum`

## Notation

+ `esymm Ïƒ R n` is the `n`th elementary symmetric polynomial in `MvPolynomial Ïƒ R`.

+ `psum Ïƒ R n` is the degree-`n` power sum in `MvPolynomial Ïƒ R`, i.e. the sum of monomials
  `(X i)^n` over `i âˆˆ Ïƒ`.

As in other polynomial files, we typically use the notation:

+ `Ïƒ Ï„ : Type*` (indexing the variables)

+ `R S : Type*` `[CommSemiring R]` `[CommSemiring S]` (the coefficients)

+ `r : R` elements of the coefficient ring

+ `i : Ïƒ`, with corresponding monomial `X i`, often denoted `X_i` by mathematicians

+ `Ï† Ïˆ : MvPolynomial Ïƒ R`

-/


open Equiv (Perm)

open BigOperators

noncomputable section

namespace Multiset

variable {R : Type*} [CommSemiring R]

/-- The `n`th elementary symmetric function evaluated at the elements of `s` -/
def esymm (s : Multiset R) (n : â„•) : R :=
  ((s.powersetLen n).map Multiset.prod).sum
#align multiset.esymm Multiset.esymm

theorem _root_.Finset.esymm_map_val {Ïƒ} (f : Ïƒ â†’ R) (s : Finset Ïƒ) (n : â„•) :
    (s.val.map f).esymm n = (s.powersetLen n).sum fun t => t.prod f := by
  simp only [esymm, powersetLen_map, â† Finset.map_val_val_powersetLen, map_map]
  -- âŠ¢ sum (map ((fun x => prod x) âˆ˜ (fun x => map f x) âˆ˜ Finset.val) (Finset.power â€¦
  rfl
  -- ğŸ‰ no goals
#align finset.esymm_map_val Finset.esymm_map_val

end Multiset

namespace MvPolynomial

variable {Ïƒ : Type*} {R : Type*}

variable {Ï„ : Type*} {S : Type*}

/-- A `MvPolynomial Ï†` is symmetric if it is invariant under
permutations of its variables by the `rename` operation -/
def IsSymmetric [CommSemiring R] (Ï† : MvPolynomial Ïƒ R) : Prop :=
  âˆ€ e : Perm Ïƒ, rename e Ï† = Ï†
#align mv_polynomial.is_symmetric MvPolynomial.IsSymmetric

variable (Ïƒ R)

/-- The subalgebra of symmetric `MvPolynomial`s. -/
def symmetricSubalgebra [CommSemiring R] : Subalgebra R (MvPolynomial Ïƒ R) where
  carrier := setOf IsSymmetric
  algebraMap_mem' r e := rename_C e r
  mul_mem' ha hb e := by rw [AlgHom.map_mul, ha, hb]
                         -- ğŸ‰ no goals
  add_mem' ha hb e := by rw [AlgHom.map_add, ha, hb]
                         -- ğŸ‰ no goals
#align mv_polynomial.symmetric_subalgebra MvPolynomial.symmetricSubalgebra

variable {Ïƒ R}

@[simp]
theorem mem_symmetricSubalgebra [CommSemiring R] (p : MvPolynomial Ïƒ R) :
    p âˆˆ symmetricSubalgebra Ïƒ R â†” p.IsSymmetric :=
  Iff.rfl
#align mv_polynomial.mem_symmetric_subalgebra MvPolynomial.mem_symmetricSubalgebra

namespace IsSymmetric

section CommSemiring

variable [CommSemiring R] [CommSemiring S] {Ï† Ïˆ : MvPolynomial Ïƒ R}

@[simp]
theorem C (r : R) : IsSymmetric (C r : MvPolynomial Ïƒ R) :=
  (symmetricSubalgebra Ïƒ R).algebraMap_mem r
set_option linter.uppercaseLean3 false in
#align mv_polynomial.is_symmetric.C MvPolynomial.IsSymmetric.C

@[simp]
theorem zero : IsSymmetric (0 : MvPolynomial Ïƒ R) :=
  (symmetricSubalgebra Ïƒ R).zero_mem
#align mv_polynomial.is_symmetric.zero MvPolynomial.IsSymmetric.zero

@[simp]
theorem one : IsSymmetric (1 : MvPolynomial Ïƒ R) :=
  (symmetricSubalgebra Ïƒ R).one_mem
#align mv_polynomial.is_symmetric.one MvPolynomial.IsSymmetric.one

theorem add (hÏ† : IsSymmetric Ï†) (hÏˆ : IsSymmetric Ïˆ) : IsSymmetric (Ï† + Ïˆ) :=
  (symmetricSubalgebra Ïƒ R).add_mem hÏ† hÏˆ
#align mv_polynomial.is_symmetric.add MvPolynomial.IsSymmetric.add

theorem mul (hÏ† : IsSymmetric Ï†) (hÏˆ : IsSymmetric Ïˆ) : IsSymmetric (Ï† * Ïˆ) :=
  (symmetricSubalgebra Ïƒ R).mul_mem hÏ† hÏˆ
#align mv_polynomial.is_symmetric.mul MvPolynomial.IsSymmetric.mul

theorem smul (r : R) (hÏ† : IsSymmetric Ï†) : IsSymmetric (r â€¢ Ï†) :=
  (symmetricSubalgebra Ïƒ R).smul_mem hÏ† r
#align mv_polynomial.is_symmetric.smul MvPolynomial.IsSymmetric.smul

@[simp]
theorem map (hÏ† : IsSymmetric Ï†) (f : R â†’+* S) : IsSymmetric (map f Ï†) := fun e => by
  rw [â† map_rename, hÏ†]
  -- ğŸ‰ no goals
#align mv_polynomial.is_symmetric.map MvPolynomial.IsSymmetric.map

end CommSemiring

section CommRing

variable [CommRing R] {Ï† Ïˆ : MvPolynomial Ïƒ R}

theorem neg (hÏ† : IsSymmetric Ï†) : IsSymmetric (-Ï†) :=
  (symmetricSubalgebra Ïƒ R).neg_mem hÏ†
#align mv_polynomial.is_symmetric.neg MvPolynomial.IsSymmetric.neg

theorem sub (hÏ† : IsSymmetric Ï†) (hÏˆ : IsSymmetric Ïˆ) : IsSymmetric (Ï† - Ïˆ) :=
  (symmetricSubalgebra Ïƒ R).sub_mem hÏ† hÏˆ
#align mv_polynomial.is_symmetric.sub MvPolynomial.IsSymmetric.sub

end CommRing

end IsSymmetric

section ElementarySymmetric

open Finset

variable (Ïƒ R) [CommSemiring R] [CommSemiring S] [Fintype Ïƒ] [Fintype Ï„]

/-- The `n`th elementary symmetric `MvPolynomial Ïƒ R`. -/
def esymm (n : â„•) : MvPolynomial Ïƒ R :=
  âˆ‘ t in powersetLen n univ, âˆ i in t, X i
#align mv_polynomial.esymm MvPolynomial.esymm

/-- The `n`th elementary symmetric `MvPolynomial Ïƒ R` is obtained by evaluating the
`n`th elementary symmetric at the `Multiset` of the monomials -/
theorem esymm_eq_multiset_esymm : esymm Ïƒ R = (Finset.univ.val.map X).esymm := by
  refine' funext fun n => (Finset.esymm_map_val X _ n).symm
  -- ğŸ‰ no goals
#align mv_polynomial.esymm_eq_multiset_esymm MvPolynomial.esymm_eq_multiset_esymm

theorem aeval_esymm_eq_multiset_esymm [Algebra R S] (f : Ïƒ â†’ S) (n : â„•) :
    aeval f (esymm Ïƒ R n) = (Finset.univ.val.map f).esymm n := by
  simp_rw [esymm, aeval_sum, aeval_prod, aeval_X, Finset.esymm_map_val]
  -- ğŸ‰ no goals
#align mv_polynomial.aeval_esymm_eq_multiset_esymm MvPolynomial.aeval_esymm_eq_multiset_esymm

/-- We can define `esymm Ïƒ R n` by summing over a subtype instead of over `powerset_len`. -/
theorem esymm_eq_sum_subtype (n : â„•) :
    esymm Ïƒ R n = âˆ‘ t : { s : Finset Ïƒ // s.card = n }, âˆ i in (t : Finset Ïƒ), X i :=
  sum_subtype _ (fun _ => mem_powerset_len_univ_iff) _
#align mv_polynomial.esymm_eq_sum_subtype MvPolynomial.esymm_eq_sum_subtype

/-- We can define `esymm Ïƒ R n` as a sum over explicit monomials -/
theorem esymm_eq_sum_monomial (n : â„•) :
    esymm Ïƒ R n = âˆ‘ t in powersetLen n univ, monomial (âˆ‘ i in t, Finsupp.single i 1) 1 := by
  simp_rw [monomial_sum_one]
  -- âŠ¢ esymm Ïƒ R n = âˆ‘ x in powersetLen n univ, âˆ x in x, â†‘(monomial (Finsupp.singl â€¦
  rfl
  -- ğŸ‰ no goals
#align mv_polynomial.esymm_eq_sum_monomial MvPolynomial.esymm_eq_sum_monomial

@[simp]
theorem esymm_zero : esymm Ïƒ R 0 = 1 := by
  simp only [esymm, powersetLen_zero, sum_singleton, prod_empty]
  -- ğŸ‰ no goals
#align mv_polynomial.esymm_zero MvPolynomial.esymm_zero

theorem map_esymm (n : â„•) (f : R â†’+* S) : map f (esymm Ïƒ R n) = esymm Ïƒ S n := by
  simp_rw [esymm, map_sum, map_prod, map_X]
  -- ğŸ‰ no goals
#align mv_polynomial.map_esymm MvPolynomial.map_esymm

theorem rename_esymm (n : â„•) (e : Ïƒ â‰ƒ Ï„) : rename e (esymm Ïƒ R n) = esymm Ï„ R n :=
  calc
    rename e (esymm Ïƒ R n) = âˆ‘ x in powersetLen n univ, âˆ i in x, X (e i) := by
      simp_rw [esymm, map_sum, map_prod, rename_X]
      -- ğŸ‰ no goals
    _ = âˆ‘ t in powersetLen n (univ.map e.toEmbedding), âˆ i in t, X i := by
      simp [Finset.powersetLen_map, -Finset.map_univ_equiv]
      -- âŠ¢ âˆ‘ x in powersetLen n univ, âˆ x in x, X (â†‘e x) = âˆ‘ x in powersetLen n univ, âˆ â€¦
      --Porting note: Why did `mapEmbedding_apply` not work?
      dsimp [mapEmbedding, OrderEmbedding.ofMapLEIff]
      -- âŠ¢ âˆ‘ x in powersetLen n univ, âˆ x in x, X (â†‘e x) = âˆ‘ x in powersetLen n univ, âˆ â€¦
      simp
      -- ğŸ‰ no goals
    _ = âˆ‘ t in powersetLen n univ, âˆ i in t, X i := by rw [Finset.map_univ_equiv]
                                                       -- ğŸ‰ no goals
#align mv_polynomial.rename_esymm MvPolynomial.rename_esymm

theorem esymm_isSymmetric (n : â„•) : IsSymmetric (esymm Ïƒ R n) := by
  intro
  -- âŠ¢ â†‘(rename â†‘eâœ) (esymm Ïƒ R n) = esymm Ïƒ R n
  rw [rename_esymm]
  -- ğŸ‰ no goals
#align mv_polynomial.esymm_is_symmetric MvPolynomial.esymm_isSymmetric

theorem support_esymm'' (n : â„•) [DecidableEq Ïƒ] [Nontrivial R] :
    (esymm Ïƒ R n).support =
      (powersetLen n (univ : Finset Ïƒ)).biUnion fun t =>
        (Finsupp.single (âˆ‘ i : Ïƒ in t, Finsupp.single i 1) (1 : R)).support := by
  rw [esymm_eq_sum_monomial]
  -- âŠ¢ support (âˆ‘ t in powersetLen n univ, â†‘(monomial (âˆ‘ i in t, Finsupp.single i 1 â€¦
  simp only [â† single_eq_monomial]
  -- âŠ¢ support (âˆ‘ x in powersetLen n univ, Finsupp.single (âˆ‘ i in x, Finsupp.single â€¦
  refine' Finsupp.support_sum_eq_biUnion (powersetLen n (univ : Finset Ïƒ)) _
  -- âŠ¢ âˆ€ (iâ‚ iâ‚‚ : Finset Ïƒ), iâ‚ â‰  iâ‚‚ â†’ Disjoint (Finsupp.single (âˆ‘ i in iâ‚, Finsupp â€¦
  intro s t hst
  -- âŠ¢ Disjoint (Finsupp.single (âˆ‘ i in s, Finsupp.single i 1) 1).support (Finsupp. â€¦
  rw [Finset.disjoint_left, Finsupp.support_single_ne_zero _ one_ne_zero]
  -- âŠ¢ âˆ€ â¦ƒa : Ïƒ â†’â‚€ â„•â¦„, a âˆˆ {âˆ‘ i in s, Finsupp.single i 1} â†’ Â¬a âˆˆ (Finsupp.single (âˆ‘ â€¦
  rw [Finsupp.support_single_ne_zero _ one_ne_zero]
  -- âŠ¢ âˆ€ â¦ƒa : Ïƒ â†’â‚€ â„•â¦„, a âˆˆ {âˆ‘ i in s, Finsupp.single i 1} â†’ Â¬a âˆˆ {âˆ‘ i in t, Finsupp â€¦
  simp only [one_ne_zero, mem_singleton, Finsupp.mem_support_iff]
  -- âŠ¢ âˆ€ â¦ƒa : Ïƒ â†’â‚€ â„•â¦„, a = âˆ‘ i in s, Finsupp.single i 1 â†’ Â¬a = âˆ‘ i in t, Finsupp.si â€¦
  rintro a h rfl
  -- âŠ¢ False
  have := congr_arg Finsupp.support h
  -- âŠ¢ False
  rw [Finsupp.support_sum_eq_biUnion, Finsupp.support_sum_eq_biUnion] at this
  have hsingle : âˆ€ s : Finset Ïƒ, âˆ€ x : Ïƒ, x âˆˆ s â†’ (Finsupp.single x 1).support = {x} := by
    intros _ x _
    rw [Finsupp.support_single_ne_zero x one_ne_zero]
  have hs := biUnion_congr (of_eq_true (eq_self s)) (hsingle s)
  have ht := biUnion_congr (of_eq_true (eq_self t)) (hsingle t)
  rw [hs, ht] at this
  Â· simp only [biUnion_singleton_eq_self] at this
    -- âŠ¢ False
    exact absurd this hst.symm
    -- ğŸ‰ no goals
  all_goals intro x y; simp [Finsupp.support_single_disjoint]
  -- ğŸ‰ no goals
#align mv_polynomial.support_esymm'' MvPolynomial.support_esymm''

theorem support_esymm' (n : â„•) [DecidableEq Ïƒ] [Nontrivial R] :
    (esymm Ïƒ R n).support =
      (powersetLen n (univ : Finset Ïƒ)).biUnion fun t => {âˆ‘ i : Ïƒ in t, Finsupp.single i 1} := by
  rw [support_esymm'']
  -- âŠ¢ (Finset.biUnion (powersetLen n univ) fun t => (Finsupp.single (âˆ‘ i in t, Fin â€¦
  congr
  -- âŠ¢ (fun t => (Finsupp.single (âˆ‘ i in t, Finsupp.single i 1) 1).support) = fun t â€¦
  funext
  -- âŠ¢ (Finsupp.single (âˆ‘ i in xâœ, Finsupp.single i 1) 1).support = {âˆ‘ i in xâœ, Fin â€¦
  exact Finsupp.support_single_ne_zero _ one_ne_zero
  -- ğŸ‰ no goals
#align mv_polynomial.support_esymm' MvPolynomial.support_esymm'

theorem support_esymm (n : â„•) [DecidableEq Ïƒ] [Nontrivial R] :
    (esymm Ïƒ R n).support =
      (powersetLen n (univ : Finset Ïƒ)).image fun t => âˆ‘ i : Ïƒ in t, Finsupp.single i 1 := by
  rw [support_esymm']
  -- âŠ¢ (Finset.biUnion (powersetLen n univ) fun t => {âˆ‘ i in t, Finsupp.single i 1} â€¦
  exact biUnion_singleton
  -- ğŸ‰ no goals
#align mv_polynomial.support_esymm MvPolynomial.support_esymm

theorem degrees_esymm [Nontrivial R] (n : â„•) (hpos : 0 < n) (hn : n â‰¤ Fintype.card Ïƒ) :
    (esymm Ïƒ R n).degrees = (univ : Finset Ïƒ).val := by
  classical
    have :
      (Finsupp.toMultiset âˆ˜ fun t : Finset Ïƒ => âˆ‘ i : Ïƒ in t, Finsupp.single i 1) = Finset.val := by
      funext
      simp [Finsupp.toMultiset_sum_single]
    rw [degrees_def, support_esymm, sup_image, this]
    have : ((powersetLen n univ).sup (fun (x : Finset Ïƒ) => x)).val
        = sup (powersetLen n univ) val := by
      refine' comp_sup_eq_sup_comp _ _ _
      Â· intros
        simp only [union_val, sup_eq_union]
        congr
      Â· rfl
    rw [â† this]
    obtain âŸ¨k, rflâŸ© := Nat.exists_eq_succ_of_ne_zero hpos.ne'
    simpa using powersetLen_sup _ _ (Nat.lt_of_succ_le hn)
#align mv_polynomial.degrees_esymm MvPolynomial.degrees_esymm

end ElementarySymmetric

section PowerSum

open Finset

variable (Ïƒ R) [CommSemiring R] [Fintype Ïƒ] [Fintype Ï„]

/-- The degree-`n` power sum -/
def psum (n : â„•) : MvPolynomial Ïƒ R := âˆ‘ i, X i ^ n

lemma psum_def (n : â„•) : psum Ïƒ R n = âˆ‘ i, X i ^ n := rfl

@[simp]
theorem psum_zero : psum Ïƒ R 0 = Fintype.card Ïƒ := by
  simp only [psum, _root_.pow_zero, â† cast_card]
  -- âŠ¢ â†‘(card univ) = â†‘(Fintype.card Ïƒ)
  exact rfl
  -- ğŸ‰ no goals

@[simp]
theorem psum_one : psum Ïƒ R 1 = âˆ‘ i, X i := by
  simp only [psum, _root_.pow_one]
  -- ğŸ‰ no goals

@[simp]
theorem rename_psum (n : â„•) (e : Ïƒ â‰ƒ Ï„) : rename e (psum Ïƒ R n) = psum Ï„ R n := by
  simp_rw [psum, map_sum, map_pow, rename_X, e.sum_comp (X Â· ^ n)]
  -- ğŸ‰ no goals

theorem psum_isSymmetric (n : â„•) : IsSymmetric (psum Ïƒ R n) := rename_psum _ _ n

end PowerSum

end MvPolynomial
