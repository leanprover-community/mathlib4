/-
Copyright (c) 2019 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/
import Mathlib.Algebra.CharP.Basic
import Mathlib.Data.Polynomial.AlgebraMap
import Mathlib.Data.MvPolynomial.CommRing
import Mathlib.Data.MvPolynomial.Variables
import Mathlib.LinearAlgebra.FinsuppVectorSpace

#align_import ring_theory.mv_polynomial.basic from "leanprover-community/mathlib"@"2f5b500a507264de86d666a5f87ddb976e2d8de4"

/-!
# Multivariate polynomials over commutative rings

This file contains basic facts about multivariate polynomials over commutative rings, for example
that the monomials form a basis.

## Main definitions

* `restrictTotalDegree Ïƒ R m`: the subspace of multivariate polynomials indexed by `Ïƒ` over the
  commutative ring `R` of total degree at most `m`.
* `restrictDegree Ïƒ R m`: the subspace of multivariate polynomials indexed by `Ïƒ` over the
  commutative ring `R` such that the degree in each individual variable is at most `m`.

## Main statements

* The multivariate polynomial ring over a commutative semiring of characteristic `p` has
  characteristic `p`, and similarly for `CharZero`.
* `basisMonomials`: shows that the monomials form a basis of the vector space of multivariate
  polynomials.

## TODO

Generalise to noncommutative (semi)rings
-/


noncomputable section

open Set LinearMap Submodule

open BigOperators Polynomial

universe u v

variable (Ïƒ : Type u) (R : Type v) [CommSemiring R] (p m : â„•)

namespace MvPolynomial

section CharP

instance [CharP R p] : CharP (MvPolynomial Ïƒ R) p where
  cast_eq_zero_iff' n := by rw [â† C_eq_coe_nat, â† C_0, C_inj, CharP.cast_eq_zero_iff R p]
                            -- ðŸŽ‰ no goals

end CharP

section CharZero

instance [CharZero R] : CharZero (MvPolynomial Ïƒ R) where
  cast_injective x y hxy := by rwa [â† C_eq_coe_nat, â† C_eq_coe_nat, C_inj, Nat.cast_inj] at hxy
                               -- ðŸŽ‰ no goals

end CharZero

section Homomorphism

theorem mapRange_eq_map {R S : Type*} [CommSemiring R] [CommSemiring S] (p : MvPolynomial Ïƒ R)
    (f : R â†’+* S) : Finsupp.mapRange f f.map_zero p = map f p := by
  rw [p.as_sum, Finsupp.mapRange_finset_sum, (map f).map_sum]
  -- âŠ¢ âˆ‘ x in support p, Finsupp.mapRange â†‘f (_ : â†‘f 0 = 0) (â†‘(monomial x) (coeff x â€¦
  refine' Finset.sum_congr rfl fun n _ => _
  -- âŠ¢ Finsupp.mapRange â†‘f (_ : â†‘f 0 = 0) (â†‘(monomial n) (coeff n p)) = â†‘(map f) (â†‘ â€¦
  rw [map_monomial, â† single_eq_monomial, Finsupp.mapRange_single, single_eq_monomial]
  -- ðŸŽ‰ no goals
#align mv_polynomial.map_range_eq_map MvPolynomial.mapRange_eq_map

end Homomorphism

section Degree

/-- The submodule of polynomials of total degree less than or equal to `m`.-/
def restrictTotalDegree : Submodule R (MvPolynomial Ïƒ R) :=
  Finsupp.supported _ _ { n | (n.sum fun _ e => e) â‰¤ m }
#align mv_polynomial.restrict_total_degree MvPolynomial.restrictTotalDegree

/-- The submodule of polynomials such that the degree with respect to each individual variable is
less than or equal to `m`.-/
def restrictDegree (m : â„•) : Submodule R (MvPolynomial Ïƒ R) :=
  Finsupp.supported _ _ { n | âˆ€ i, n i â‰¤ m }
#align mv_polynomial.restrict_degree MvPolynomial.restrictDegree

variable {R}

theorem mem_restrictTotalDegree (p : MvPolynomial Ïƒ R) :
    p âˆˆ restrictTotalDegree Ïƒ R m â†” p.totalDegree â‰¤ m := by
  rw [totalDegree, Finset.sup_le_iff]
  -- âŠ¢ p âˆˆ restrictTotalDegree Ïƒ R m â†” âˆ€ (b : Ïƒ â†’â‚€ â„•), b âˆˆ support p â†’ (Finsupp.sum â€¦
  rfl
  -- ðŸŽ‰ no goals
#align mv_polynomial.mem_restrict_total_degree MvPolynomial.mem_restrictTotalDegree

theorem mem_restrictDegree (p : MvPolynomial Ïƒ R) (n : â„•) :
    p âˆˆ restrictDegree Ïƒ R n â†” âˆ€ s âˆˆ p.support, âˆ€ i, (s : Ïƒ â†’â‚€ â„•) i â‰¤ n := by
  rw [restrictDegree, Finsupp.mem_supported]
  -- âŠ¢ â†‘p.support âŠ† {n_1 | âˆ€ (i : Ïƒ), â†‘n_1 i â‰¤ n} â†” âˆ€ (s : Ïƒ â†’â‚€ â„•), s âˆˆ support p â†’ â€¦
  rfl
  -- ðŸŽ‰ no goals
#align mv_polynomial.mem_restrict_degree MvPolynomial.mem_restrictDegree

theorem mem_restrictDegree_iff_sup [DecidableEq Ïƒ] (p : MvPolynomial Ïƒ R) (n : â„•) :
    p âˆˆ restrictDegree Ïƒ R n â†” âˆ€ i, p.degrees.count i â‰¤ n := by
  simp only [mem_restrictDegree, degrees_def, Multiset.count_finset_sup, Finsupp.count_toMultiset,
    Finset.sup_le_iff]
  exact âŸ¨fun h n s hs => h s hs n, fun h s hs n => h n s hsâŸ©
  -- ðŸŽ‰ no goals
#align mv_polynomial.mem_restrict_degree_iff_sup MvPolynomial.mem_restrictDegree_iff_sup

variable (R)

/-- The monomials form a basis on `MvPolynomial Ïƒ R`. -/
def basisMonomials : Basis (Ïƒ â†’â‚€ â„•) R (MvPolynomial Ïƒ R) :=
  Finsupp.basisSingleOne
#align mv_polynomial.basis_monomials MvPolynomial.basisMonomials

@[simp]
theorem coe_basisMonomials :
    (basisMonomials Ïƒ R : (Ïƒ â†’â‚€ â„•) â†’ MvPolynomial Ïƒ R) = fun s => monomial s 1 :=
  rfl
#align mv_polynomial.coe_basis_monomials MvPolynomial.coe_basisMonomials

theorem linearIndependent_X : LinearIndependent R (X : Ïƒ â†’ MvPolynomial Ïƒ R) :=
  (basisMonomials Ïƒ R).linearIndependent.comp (fun s : Ïƒ => Finsupp.single s 1)
    (Finsupp.single_left_injective one_ne_zero)
set_option linter.uppercaseLean3 false in
#align mv_polynomial.linear_independent_X MvPolynomial.linearIndependent_X

end Degree

end MvPolynomial

-- this is here to avoid import cycle issues
namespace Polynomial

/-- The monomials form a basis on `R[X]`. -/
noncomputable def basisMonomials : Basis â„• R R[X] :=
  Basis.ofRepr (toFinsuppIsoAlg R).toLinearEquiv
#align polynomial.basis_monomials Polynomial.basisMonomials

@[simp]
theorem coe_basisMonomials : (basisMonomials R : â„• â†’ R[X]) = fun s => monomial s 1 :=
  funext fun _ => ofFinsupp_single _ _
#align polynomial.coe_basis_monomials Polynomial.coe_basisMonomials

end Polynomial
