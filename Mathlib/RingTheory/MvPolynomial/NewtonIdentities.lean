/-
Copyright (c) 2023 Michael Lee. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Lee
-/
import Mathlib.Algebra.Algebra.Subalgebra.Basic
import Mathlib.Data.Finset.Card
import Mathlib.Data.Fintype.Basic
import Mathlib.Data.MvPolynomial.CommRing
import Mathlib.Data.MvPolynomial.Rename
import Mathlib.Data.Nat.Parity
import Mathlib.RingTheory.MvPolynomial.Basic
import Mathlib.RingTheory.MvPolynomial.Symmetric
import Mathlib.RingTheory.Polynomial.Basic

/-!
# Newton's Identities

This file defines `MvPolynomial` power sums as a means of implementing Newton's identities. The
combinatorial proof, due to Zeilberger, defines for `k : â„•` a subset `pairs` of
`(range k).powerset Ã— range k` and a map `pairMap` such that `pairMap` is an involution on `pairs`,
and a map `weight` which identifies elements of `pairs` with the terms of the summation in Newton's
identities and which satisfies `weight âˆ˜ pairMap = -weight`. The result therefore follows neatly
from an identity implemented in mathlib as `Finset.sum_involution`. Namely, we use
`Finset.sum_involution` to show that `âˆ‘ t in pairs Ïƒ k, weight Ïƒ R k t = 0`. We then identify
`(-1) ^ k * k * esymm Ïƒ R k` with the terms of the weight sum for which `t.fst` has
cardinality `k`, and `(-1) ^ i * esymm Ïƒ R i * psum Ïƒ R (k - i)` with the terms of the weight sum
for which `t.fst` has cardinality `i` for `i < k` , and we thereby derive the main result
`(-1) ^ k * k * esymm Ïƒ R k + âˆ‘ i in range k, (-1) ^ i * esymm Ïƒ R i * psum Ïƒ R (k - i) = 0` (or
rather, two equivalent forms which provide direct definitions for `esymm` and `psum` in lower-degree
terms).

## Main declarations

* `MvPolynomial.mul_esymm_eq_sum`: a recurrence relation for the `k`th elementary
  symmetric polynomial in terms of lower-degree elementary symmetric polynomials and power sums.

* `MvPolynomial.psum_eq_mul_esymm_sub_sum`: a recurrence relation for the degree-`k` power sum
  in terms of lower-degree elementary symmetric polynomials and power sums.

## References

See [zeilberger1984] for the combinatorial proof of Newton's identities.
-/

open Equiv (Perm)

open BigOperators MvPolynomial

noncomputable section

namespace MvPolynomial

open Finset Nat

variable (Ïƒ : Type*) [Fintype Ïƒ] [DecidableEq Ïƒ] (R : Type*) [CommRing R]

namespace NewtonIdentities

private def pairs (k : â„•) : Finset (Finset Ïƒ Ã— Ïƒ) :=
  univ.filter (fun t â†¦ card t.fst â‰¤ k âˆ§ (card t.fst = k â†’ t.snd âˆˆ t.fst))

@[simp]
private lemma mem_pairs (k : â„•) (t : Finset Ïƒ Ã— Ïƒ) :
    t âˆˆ pairs Ïƒ k â†” card t.fst â‰¤ k âˆ§ (card t.fst = k â†’ t.snd âˆˆ t.fst) := by
  simp [pairs]
  -- ğŸ‰ no goals

private def weight (k : â„•) (t : Finset Ïƒ Ã— Ïƒ) : MvPolynomial Ïƒ R :=
  (-1) ^ card t.fst * ((âˆ a in t.fst, X a) * X t.snd ^ (k - card t.fst))

private def pairMap (t : Finset Ïƒ Ã— Ïƒ) : Finset Ïƒ Ã— Ïƒ :=
  if h : t.snd âˆˆ t.fst then (t.fst.erase t.snd, t.snd) else (t.fst.cons t.snd h, t.snd)

private lemma pairMap_ne_self (t : Finset Ïƒ Ã— Ïƒ) : pairMap Ïƒ t â‰  t := by
  rw [pairMap]
  -- âŠ¢ (if h : t.snd âˆˆ t.fst then (erase t.fst t.snd, t.snd) else (cons t.snd t.fst â€¦
  split_ifs with h1
  -- âŠ¢ (erase t.fst t.snd, t.snd) â‰  t
  all_goals by_contra ht; rw [â† ht] at h1; simp_all
  -- ğŸ‰ no goals

private lemma pairMap_of_snd_mem_fst {t : Finset Ïƒ Ã— Ïƒ} (h : t.snd âˆˆ t.fst) :
    pairMap Ïƒ t = (t.fst.erase t.snd, t.snd) := by
  simp [pairMap, h]
  -- ğŸ‰ no goals

private lemma pairMap_of_snd_nmem_fst {t : Finset Ïƒ Ã— Ïƒ} (h : t.snd âˆ‰ t.fst) :
    pairMap Ïƒ t = (t.fst.cons t.snd h, t.snd) := by
  simp [pairMap, h]
  -- ğŸ‰ no goals

private theorem pairMap_mem_pairs {k : â„•} (t : Finset Ïƒ Ã— Ïƒ) (h : t âˆˆ pairs Ïƒ k) :
    pairMap Ïƒ t âˆˆ pairs Ïƒ k := by
  rw [mem_pairs] at h âŠ¢
  -- âŠ¢ card (MvPolynomial.NewtonIdentities.pairMap Ïƒ t).fst â‰¤ k âˆ§ (card (MvPolynomi â€¦
  rcases (em (t.snd âˆˆ t.fst)) with h1 | h1
  -- âŠ¢ card (MvPolynomial.NewtonIdentities.pairMap Ïƒ t).fst â‰¤ k âˆ§ (card (MvPolynomi â€¦
  Â· rw [pairMap_of_snd_mem_fst Ïƒ h1]
    -- âŠ¢ card (erase t.fst t.snd, t.snd).fst â‰¤ k âˆ§ (card (erase t.fst t.snd, t.snd).f â€¦
    simp only [h1, implies_true, and_true] at h
    -- âŠ¢ card (erase t.fst t.snd, t.snd).fst â‰¤ k âˆ§ (card (erase t.fst t.snd, t.snd).f â€¦
    simp only [card_erase_of_mem h1, tsub_le_iff_right, mem_erase, ne_eq, h1]
    -- âŠ¢ card t.fst â‰¤ k + 1 âˆ§ (card t.fst - 1 = k â†’ False)
    refine âŸ¨le_step h, ?_âŸ©
    -- âŠ¢ card t.fst - 1 = k â†’ False
    by_contra h2
    -- âŠ¢ False
    rw [â† h2] at h
    -- âŠ¢ False
    exact not_le_of_lt (sub_lt (card_pos.mpr âŸ¨t.snd, h1âŸ©) zero_lt_one) h
    -- ğŸ‰ no goals
  Â· rw [pairMap_of_snd_nmem_fst Ïƒ h1]
    -- âŠ¢ card (cons t.snd t.fst h1, t.snd).fst â‰¤ k âˆ§ (card (cons t.snd t.fst h1, t.sn â€¦
    simp only [h1] at h
    -- âŠ¢ card (cons t.snd t.fst h1, t.snd).fst â‰¤ k âˆ§ (card (cons t.snd t.fst h1, t.sn â€¦
    simp only [card_cons, mem_cons, true_or, implies_true, and_true]
    -- âŠ¢ card t.fst + 1 â‰¤ k
    exact (le_iff_eq_or_lt.mp h.left).resolve_left h.right
    -- ğŸ‰ no goals

@[simp]
private theorem pairMap_involutive : (pairMap Ïƒ).Involutive := by
  intro t
  -- âŠ¢ MvPolynomial.NewtonIdentities.pairMap Ïƒ (MvPolynomial.NewtonIdentities.pairM â€¦
  rw [pairMap, pairMap]
  -- âŠ¢ (if h : (if h : t.snd âˆˆ t.fst then (erase t.fst t.snd, t.snd) else (cons t.s â€¦
  split_ifs with h1 h2 h3
  Â· simp at h2
    -- ğŸ‰ no goals
  Â· simp [insert_erase h1]
    -- ğŸ‰ no goals
  Â· simp_all
    -- ğŸ‰ no goals
  Â· simp at h3
    -- ğŸ‰ no goals

private theorem weight_add_weight_pairMap {k : â„•} (t : Finset Ïƒ Ã— Ïƒ) (h : t âˆˆ pairs Ïƒ k) :
    weight Ïƒ R k t + weight Ïƒ R k (pairMap Ïƒ t) = 0 := by
  rw [weight, weight]
  -- âŠ¢ (-1) ^ card t.fst * ((âˆ a in t.fst, X a) * X t.snd ^ (k - card t.fst)) + (-1 â€¦
  rw [mem_pairs] at h
  -- âŠ¢ (-1) ^ card t.fst * ((âˆ a in t.fst, X a) * X t.snd ^ (k - card t.fst)) + (-1 â€¦
  have h2 (n : â„•) : -(-1 : MvPolynomial Ïƒ R) ^ n = (-1) ^ (n + 1) := by
    rw [â† neg_one_mul ((-1 : MvPolynomial Ïƒ R) ^ n), pow_add, pow_one, mul_comm]
  rcases (em (t.snd âˆˆ t.fst)) with h1 | h1
  -- âŠ¢ (-1) ^ card t.fst * ((âˆ a in t.fst, X a) * X t.snd ^ (k - card t.fst)) + (-1 â€¦
  Â· rw [pairMap_of_snd_mem_fst Ïƒ h1]
    -- âŠ¢ (-1) ^ card t.fst * ((âˆ a in t.fst, X a) * X t.snd ^ (k - card t.fst)) + (-1 â€¦
    simp only [â† prod_erase_mul t.fst (fun j â†¦ (X j : MvPolynomial Ïƒ R)) h1,
      mul_assoc (âˆ a in erase t.fst t.snd, X a), card_erase_of_mem h1]
    nth_rewrite 1 [â† pow_one (X t.snd)]
    -- âŠ¢ (-1) ^ card t.fst * ((âˆ a in erase t.fst t.snd, X a) * (X t.snd ^ 1 * X t.sn â€¦
    simp only [â† pow_add, add_comm]
    -- âŠ¢ (-1) ^ card t.fst * ((âˆ a in erase t.fst t.snd, X a) * X t.snd ^ (k - card t â€¦
    have h3 : 1 â‰¤ card t.fst := lt_iff_add_one_le.mp (card_pos.mpr âŸ¨t.snd, h1âŸ©)
    -- âŠ¢ (-1) ^ card t.fst * ((âˆ a in erase t.fst t.snd, X a) * X t.snd ^ (k - card t â€¦
    rw [â† tsub_tsub_assoc h.left h3, â† neg_neg ((-1 : MvPolynomial Ïƒ R) ^ (card t.fst - 1)),
      h2 (card t.fst - 1), Nat.sub_add_cancel h3]
    simp
    -- ğŸ‰ no goals
  Â· rw [pairMap_of_snd_nmem_fst Ïƒ h1]
    -- âŠ¢ (-1) ^ card t.fst * ((âˆ a in t.fst, X a) * X t.snd ^ (k - card t.fst)) + (-1 â€¦
    simp only [mul_comm, mul_assoc (âˆ a in t.fst, X a), card_cons, prod_cons]
    -- âŠ¢ (âˆ a in t.fst, X a) * (X t.snd ^ (k - card t.fst) * (-1) ^ card t.fst) + (âˆ  â€¦
    nth_rewrite 2 [â† pow_one (X t.snd)]
    -- âŠ¢ (âˆ a in t.fst, X a) * (X t.snd ^ (k - card t.fst) * (-1) ^ card t.fst) + (âˆ  â€¦
    simp only [â† pow_add, â† Nat.add_sub_assoc (Nat.lt_of_le_of_ne h.left (mt h.right h1)), add_comm]
    -- âŠ¢ (âˆ a in t.fst, X a) * (X t.snd ^ (k - card t.fst) * (-1) ^ card t.fst) + (âˆ  â€¦
    rw [â† neg_neg ((-1 : MvPolynomial Ïƒ R) ^ card t.fst), h2]
    -- âŠ¢ (âˆ a in t.fst, X a) * (X t.snd ^ (k - card t.fst) * -(-1) ^ (card t.fst + 1) â€¦
    simp
    -- ğŸ‰ no goals

private theorem weight_sum (k : â„•) : âˆ‘ t in pairs Ïƒ k, weight Ïƒ R k t = 0 :=
  sum_involution (fun t _ â†¦ pairMap Ïƒ t) (weight_add_weight_pairMap Ïƒ R)
    (fun t _ â†¦ (fun _ â†¦ pairMap_ne_self Ïƒ t)) (pairMap_mem_pairs Ïƒ)
    (fun t _ â†¦ pairMap_involutive Ïƒ t)

private theorem sum_filter_pairs_eq_sum_powersetLen_sum (k : â„•)
    (f : Finset Ïƒ Ã— Ïƒ â†’ MvPolynomial Ïƒ R) :
    (âˆ‘ t in filter (fun t â†¦ card t.fst = k) (pairs Ïƒ k), f t) =
    âˆ‘ A in powersetLen k univ, (âˆ‘ j in A, f (A, j)) := by
  apply sum_finset_product
  -- âŠ¢ âˆ€ (p : Finset Ïƒ Ã— Ïƒ), p âˆˆ filter (fun t => card t.fst = k) (MvPolynomial.New â€¦
  aesop
  -- ğŸ‰ no goals

private theorem sum_filter_pairs_eq_sum_powersetLen_mem_filter_antidiagonal_sum (k : â„•) (a : â„• Ã— â„•)
    (ha : a âˆˆ (antidiagonal k).filter (fun a â†¦ a.fst < k)) (f : Finset Ïƒ Ã— Ïƒ â†’ MvPolynomial Ïƒ R) :
    (âˆ‘ t in filter (fun t â†¦ card t.fst = a.fst) (pairs Ïƒ k), f t) =
    âˆ‘ A in powersetLen a.fst univ, (âˆ‘ j, f (A, j)) := by
  apply sum_finset_product
  -- âŠ¢ âˆ€ (p : Finset Ïƒ Ã— Ïƒ), p âˆˆ filter (fun t => card t.fst = a.fst) (MvPolynomial â€¦
  simp only [mem_filter, mem_powerset_len_univ_iff, mem_univ, and_true, and_iff_right_iff_imp]
  -- âŠ¢ âˆ€ (p : Finset Ïƒ Ã— Ïƒ), card p.fst = a.fst â†’ p âˆˆ MvPolynomial.NewtonIdentities â€¦
  rintro p hp
  -- âŠ¢ p âˆˆ MvPolynomial.NewtonIdentities.pairs Ïƒ k
  have : card p.fst â‰¤ k := by apply le_of_lt; aesop
  -- âŠ¢ p âˆˆ MvPolynomial.NewtonIdentities.pairs Ïƒ k
  aesop
  -- ğŸ‰ no goals

private theorem sum_filter_pairs_eq_sum_filter_antidiagonal_powersetLen_sum (k : â„•)
    (f : Finset Ïƒ Ã— Ïƒ â†’ MvPolynomial Ïƒ R) :
    (âˆ‘ t in filter (fun t â†¦ card t.fst < k) (pairs Ïƒ k), f t) =
    âˆ‘ a in (antidiagonal k).filter (fun a â†¦ a.fst < k),
    âˆ‘ A in powersetLen a.fst univ, (âˆ‘ j, f (A, j)) := by
  have equiv_i (a : â„• Ã— â„•) (ha : a âˆˆ (antidiagonal k).filter (fun a â†¦ a.fst < k)) :=
    sum_filter_pairs_eq_sum_powersetLen_mem_filter_antidiagonal_sum Ïƒ R k a ha f
  simp only [â† sum_congr rfl equiv_i]
  -- âŠ¢ âˆ‘ t in filter (fun t => card t.fst < k) (MvPolynomial.NewtonIdentities.pairs â€¦
  have pdisj : Set.PairwiseDisjoint ((antidiagonal k).filter (fun a â†¦ a.fst < k))
      (fun (a : â„• Ã— â„•) â†¦ (filter (fun t â†¦ card t.fst = a.fst) (pairs Ïƒ k))) := by
    simp only [Set.PairwiseDisjoint, Disjoint, pairs, filter_filter, ne_eq, le_eq_subset,
      bot_eq_empty]
    intro x hx y hy xny s hs hs' a ha
    simp only [mem_univ, forall_true_left, Prod.forall] at hs hs'
    rw [ne_eq, antidiagonal_congr (mem_filter.mp hx).left (mem_filter.mp hy).left,
      â† (mem_filter.mp (hs ha)).right.right, â† (mem_filter.mp (hs' ha)).right.right] at xny
    exact (xny rfl).elim
  have hdisj := @sum_disjiUnion _ _ _ f _ ((antidiagonal k).filter (fun a â†¦ a.fst < k))
    (fun (a : â„• Ã— â„•) â†¦ (filter (fun t â†¦ card t.fst = a.fst) (pairs Ïƒ k))) pdisj
  have disj_equiv : disjiUnion ((antidiagonal k).filter (fun a â†¦ a.fst < k))
      (fun a â†¦ filter (fun t â†¦ card t.fst = a.fst) (pairs Ïƒ k)) pdisj =
      filter (fun t â†¦ card t.fst < k) (pairs Ïƒ k) := by
    ext a
    rw [mem_disjiUnion, mem_filter]
    refine' âŸ¨_, fun haf â†¦ âŸ¨(card a.fst, k - card a.fst), _, _âŸ©âŸ©
    Â· rintro âŸ¨n, hnk, haâŸ©
      have hnk' : n.fst â‰¤ k := by apply le_of_lt; aesop
      aesop
    Â· simp_all only [mem_antidiagonal, mem_filter, mem_pairs, disjiUnion_eq_biUnion,
        add_tsub_cancel_of_le]
    Â· simp_all only [mem_antidiagonal, mem_filter, mem_pairs, disjiUnion_eq_biUnion, implies_true]
  simp only [â† hdisj, disj_equiv]
  -- ğŸ‰ no goals

private theorem disjoint_filter_pairs_lt_filter_pairs_eq (k : â„•) :
    Disjoint (filter (fun t â†¦ card t.fst < k) (pairs Ïƒ k))
    (filter (fun t â†¦ card t.fst = k) (pairs Ïƒ k)) := by
  rw [disjoint_filter]
  -- âŠ¢ âˆ€ (x : Finset Ïƒ Ã— Ïƒ), x âˆˆ MvPolynomial.NewtonIdentities.pairs Ïƒ k â†’ card x.f â€¦
  exact fun _ _ h1 h2 â†¦ lt_irrefl _ (h2.symm.subst h1)
  -- ğŸ‰ no goals

private theorem disjUnion_filter_pairs_eq_pairs (k : â„•) :
    disjUnion (filter (fun t â†¦ card t.fst < k) (pairs Ïƒ k))
    (filter (fun t â†¦ card t.fst = k) (pairs Ïƒ k)) (disjoint_filter_pairs_lt_filter_pairs_eq Ïƒ k) =
    pairs Ïƒ k := by
  simp only [disjUnion_eq_union, Finset.ext_iff, pairs, filter_filter, mem_filter]
  -- âŠ¢ âˆ€ (a : Finset Ïƒ Ã— Ïƒ), a âˆˆ filter (fun a => (card a.fst â‰¤ k âˆ§ (card a.fst = k â€¦
  intro a
  -- âŠ¢ a âˆˆ filter (fun a => (card a.fst â‰¤ k âˆ§ (card a.fst = k â†’ a.snd âˆˆ a.fst)) âˆ§ c â€¦
  rw [â† filter_or, mem_filter]
  -- âŠ¢ a âˆˆ univ âˆ§ ((card a.fst â‰¤ k âˆ§ (card a.fst = k â†’ a.snd âˆˆ a.fst)) âˆ§ card a.fst â€¦
  refine' âŸ¨fun ha â†¦ by tauto, fun ha â†¦ _âŸ©
  -- âŠ¢ a âˆˆ univ âˆ§ ((card a.fst â‰¤ k âˆ§ (card a.fst = k â†’ a.snd âˆˆ a.fst)) âˆ§ card a.fst â€¦
  have hacard := le_iff_lt_or_eq.mp ha.2.1
  -- âŠ¢ a âˆˆ univ âˆ§ ((card a.fst â‰¤ k âˆ§ (card a.fst = k â†’ a.snd âˆˆ a.fst)) âˆ§ card a.fst â€¦
  tauto
  -- ğŸ‰ no goals

private theorem esymm_summand_to_weight (k : â„•) (A : Finset Ïƒ) (h : A âˆˆ powersetLen k univ) :
    âˆ‘ j in A, weight Ïƒ R k (A, j) = k * (-1) ^ k * (âˆ i in A, X i : MvPolynomial Ïƒ R) := by
  simp [weight, mem_powerset_len_univ_iff.mp h, mul_assoc]
  -- ğŸ‰ no goals

private theorem esymm_to_weight (k : â„•) : k * esymm Ïƒ R k =
    (-1) ^ k * âˆ‘ t in filter (fun t â†¦ card t.fst = k) (pairs Ïƒ k), weight Ïƒ R k t := by
  rw [esymm, sum_filter_pairs_eq_sum_powersetLen_sum Ïƒ R k (fun t â†¦ weight Ïƒ R k t),
    sum_congr rfl (esymm_summand_to_weight Ïƒ R k), mul_comm (k : MvPolynomial Ïƒ R) ((-1) ^ k),
    â† mul_sum, â† mul_assoc, â† mul_assoc, â† pow_add, Even.neg_one_pow âŸ¨k, rflâŸ©, one_mul]

private theorem esymm_mul_psum_summand_to_weight (k : â„•) (a : â„• Ã— â„•) (ha : a âˆˆ antidiagonal k) :
    âˆ‘ A in powersetLen a.fst univ, âˆ‘ j, weight Ïƒ R k (A, j) =
    (-1) ^ a.fst * esymm Ïƒ R a.fst * psum Ïƒ R a.snd := by
  simp only [esymm, psum_def, weight, â† mul_assoc, mul_sum]
  -- âŠ¢ âˆ‘ x in powersetLen a.fst univ, âˆ‘ x_1 : Ïƒ, ((-1) ^ card x * âˆ a in x, X a) *  â€¦
  rw [sum_comm]
  -- âŠ¢ âˆ‘ y : Ïƒ, âˆ‘ x in powersetLen a.fst univ, ((-1) ^ card x * âˆ a in x, X a) * X  â€¦
  refine' sum_congr rfl fun x _ â†¦ _
  -- âŠ¢ âˆ‘ x_1 in powersetLen a.fst univ, ((-1) ^ card x_1 * âˆ a in x_1, X a) * X x ^ â€¦
  rw [sum_mul]
  -- âŠ¢ âˆ‘ x_1 in powersetLen a.fst univ, ((-1) ^ card x_1 * âˆ a in x_1, X a) * X x ^ â€¦
  refine' sum_congr rfl fun s hs â†¦ _
  -- âŠ¢ ((-1) ^ card s * âˆ a in s, X a) * X x ^ (k - card s) = ((-1) ^ a.fst * âˆ a i â€¦
  rw [mem_powerset_len_univ_iff.mp hs, â† mem_antidiagonal.mp ha, add_sub_self_left]
  -- ğŸ‰ no goals

private theorem esymm_mul_psum_to_weight (k : â„•) :
    âˆ‘ a in (antidiagonal k).filter (fun a â†¦ a.fst < k),
    (-1) ^ a.fst * esymm Ïƒ R a.fst * psum Ïƒ R a.snd =
    âˆ‘ t in filter (fun t â†¦ card t.fst < k) (pairs Ïƒ k), weight Ïƒ R k t := by
  rw [â† sum_congr rfl (fun a ha â†¦ esymm_mul_psum_summand_to_weight Ïƒ R k a (mem_filter.mp ha).left),
    sum_filter_pairs_eq_sum_filter_antidiagonal_powersetLen_sum Ïƒ R k]

end NewtonIdentities

/-- **Newton's identities** give a recurrence relation for the kth elementary symmetric polynomial
in terms of lower degree elementary symmetric polynomials and power sums. -/
theorem mul_esymm_eq_sum (k : â„•) : k * esymm Ïƒ R k =
    (-1) ^ (k + 1) * âˆ‘ a in (antidiagonal k).filter (fun a â†¦ a.fst < k),
    (-1) ^ a.fst * esymm Ïƒ R a.fst * psum Ïƒ R a.snd := by
  rw [NewtonIdentities.esymm_to_weight Ïƒ R k, NewtonIdentities.esymm_mul_psum_to_weight Ïƒ R k,
    eq_comm, â† sub_eq_zero, sub_eq_add_neg, neg_mul_eq_neg_mul,
    neg_eq_neg_one_mul ((-1 : MvPolynomial Ïƒ R) ^ k)]
  nth_rw 2 [â† pow_one (-1 : MvPolynomial Ïƒ R)]
  -- âŠ¢ (-1) ^ (k + 1) * âˆ‘ t in filter (fun t => card t.fst < k) (MvPolynomial.Newto â€¦
  rw [â† pow_add, add_comm 1 k, â† left_distrib,
    â† sum_disjUnion (NewtonIdentities.disjoint_filter_pairs_lt_filter_pairs_eq Ïƒ k),
    NewtonIdentities.disjUnion_filter_pairs_eq_pairs Ïƒ k, NewtonIdentities.weight_sum Ïƒ R k,
    neg_one_pow_mul_eq_zero_iff.mpr rfl]

theorem sum_antidiagonal_card_esymm_psum_eq_zero :
    âˆ‘ a in antidiagonal (Fintype.card Ïƒ), (-1) ^ a.fst * esymm Ïƒ R a.fst * psum Ïƒ R a.snd = 0 := by
  let k := Fintype.card Ïƒ
  -- âŠ¢ âˆ‘ a in antidiagonal (Fintype.card Ïƒ), (-1) ^ a.fst * esymm Ïƒ R a.fst * psum  â€¦
  suffices : (-1 : MvPolynomial Ïƒ R) ^ (k + 1) *
    âˆ‘ a in antidiagonal k, (-1) ^ a.fst * esymm Ïƒ R a.fst * psum Ïƒ R a.snd = 0
  Â· simpa using this
    -- ğŸ‰ no goals
  simp [â† sum_filter_add_sum_filter_not (antidiagonal k) (fun a â†¦ a.fst < k), â† mul_esymm_eq_sum,
    mul_add, â† mul_assoc, â† pow_add, mul_comm â†‘k (esymm Ïƒ R k)]

/-- A version of Newton's identities which may be more useful in the case that we know the values of
the elementary symmetric polynomials and would like to calculate the values of the power sums. -/
theorem psum_eq_mul_esymm_sub_sum (k : â„•) (h : 0 < k) : psum Ïƒ R k =
    (-1) ^ (k + 1) * k * esymm Ïƒ R k -
    âˆ‘ a in (antidiagonal k).filter (fun a â†¦ a.fst âˆˆ Set.Ioo 0 k),
    (-1) ^ a.fst * esymm Ïƒ R a.fst * psum Ïƒ R a.snd := by
  simp only [Set.Ioo, Set.mem_setOf_eq, and_comm]
  -- âŠ¢ psum Ïƒ R k = (-1) ^ (k + 1) * â†‘k * esymm Ïƒ R k - âˆ‘ x in filter (fun a => a.f â€¦
  have hesymm := mul_esymm_eq_sum Ïƒ R k
  -- âŠ¢ psum Ïƒ R k = (-1) ^ (k + 1) * â†‘k * esymm Ïƒ R k - âˆ‘ x in filter (fun a => a.f â€¦
  rw [â† (sum_filter_add_sum_filter_not ((antidiagonal k).filter (fun a â†¦ a.fst < k))
    (fun a â†¦ 0 < a.fst) (fun a â†¦ (-1) ^ a.fst * esymm Ïƒ R a.fst * psum Ïƒ R a.snd))] at hesymm
  have sub_both_sides := congrArg (Â· - (-1 : MvPolynomial Ïƒ R) ^ (k + 1) *
    âˆ‘ a in ((antidiagonal k).filter (fun a â†¦ a.fst < k)).filter (fun a â†¦ 0 < a.fst),
    (-1) ^ a.fst * esymm Ïƒ R a.fst * psum Ïƒ R a.snd) hesymm
  simp only [left_distrib, add_sub_cancel'] at sub_both_sides
  -- âŠ¢ psum Ïƒ R k = (-1) ^ (k + 1) * â†‘k * esymm Ïƒ R k - âˆ‘ x in filter (fun a => a.f â€¦
  have sub_both_sides := congrArg ((-1 : MvPolynomial Ïƒ R) ^ (k + 1) * Â·) sub_both_sides
  -- âŠ¢ psum Ïƒ R k = (-1) ^ (k + 1) * â†‘k * esymm Ïƒ R k - âˆ‘ x in filter (fun a => a.f â€¦
  simp only [mul_sub_left_distrib, â† mul_assoc, â† pow_add, Even.neg_one_pow âŸ¨k + 1, rflâŸ©, one_mul,
    not_le, lt_one_iff, filter_filter (fun a : â„• Ã— â„• â†¦ a.fst < k) (fun a â†¦ Â¬0 < a.fst)]
    at sub_both_sides
  have : filter (fun a â†¦ a.fst < k âˆ§ Â¬0 < a.fst) (antidiagonal k) = {(0, k)} := by
    ext a
    rw [mem_filter, mem_antidiagonal, mem_singleton]
    refine' âŸ¨_, fun ha â†¦ by aesopâŸ©
    rintro âŸ¨ha, âŸ¨_, ha0âŸ©âŸ©
    rw [â† ha, Nat.eq_zero_of_nonpos a.fst ha0, zero_add, â† Nat.eq_zero_of_nonpos a.fst ha0]
  rw [this, sum_singleton] at sub_both_sides
  -- âŠ¢ psum Ïƒ R k = (-1) ^ (k + 1) * â†‘k * esymm Ïƒ R k - âˆ‘ x in filter (fun a => a.f â€¦
  simp only [_root_.pow_zero, esymm_zero, mul_one, one_mul, filter_filter] at sub_both_sides
  -- âŠ¢ psum Ïƒ R k = (-1) ^ (k + 1) * â†‘k * esymm Ïƒ R k - âˆ‘ x in filter (fun a => a.f â€¦
  exact sub_both_sides.symm
  -- ğŸ‰ no goals

end MvPolynomial
