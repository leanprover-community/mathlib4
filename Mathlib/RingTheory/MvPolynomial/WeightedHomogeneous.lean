/-
Copyright (c) 2022 MarÃ­a InÃ©s de Frutos-FernÃ¡ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Antoine Chambert-Loir, MarÃ­a InÃ©s de Frutos-FernÃ¡ndez
-/
import Mathlib.Algebra.GradedMonoid
import Mathlib.Data.MvPolynomial.Variables

#align_import ring_theory.mv_polynomial.weighted_homogeneous from "leanprover-community/mathlib"@"2f5b500a507264de86d666a5f87ddb976e2d8de4"

/-!
# Weighted homogeneous polynomials

It is possible to assign weights (in a commutative additive monoid `M`) to the variables of a
multivariate polynomial ring, so that monomials of the ring then have a weighted degree with
respect to the weights of the variables. The weights are represented by a function `w : Ïƒ â†’ M`,
where `Ïƒ` are the indeterminates.

A multivariate polynomial `Ï†` is weighted homogeneous of weighted degree `m : M` if all monomials
occurring in `Ï†` have the same weighted degree `m`.

## Main definitions/lemmas

* `weightedTotalDegree' w Ï†` : the weighted total degree of a multivariate polynomial with respect
to the weights `w`, taking values in `WithBot M`.

* `weightedTotalDegree w Ï†` : When `M` has a `âŠ¥` element, we can define the weighted total degree
of a multivariate polynomial as a function taking values in `M`.

* `IsWeightedHomogeneous w Ï† m`: a predicate that asserts that `Ï†` is weighted homogeneous
of weighted degree `m` with respect to the weights `w`.

* `weightedHomogeneousSubmodule R w m`: the submodule of homogeneous polynomials
of weighted degree `m`.

* `weightedHomogeneousComponent w m`: the additive morphism that projects polynomials
onto their summand that is weighted homogeneous of degree `n` with respect to `w`.

* `sum_weightedHomogeneousComponent`: every polynomial is the sum of its weighted homogeneous
components.
-/


noncomputable section

open BigOperators

open Set Function Finset Finsupp AddMonoidAlgebra

variable {R M : Type*} [CommSemiring R]

namespace MvPolynomial

variable {Ïƒ : Type*}

section AddCommMonoid

variable [AddCommMonoid M]

/-! ### `weightedDegree'` -/


/-- The `weightedDegree'` of the finitely supported function `s : Ïƒ â†’â‚€ â„•` is the sum
  `âˆ‘(s i)â€¢(w i)`. -/
def weightedDegree' (w : Ïƒ â†’ M) : (Ïƒ â†’â‚€ â„•) â†’+ M :=
  (Finsupp.total Ïƒ M â„• w).toAddMonoidHom
#align mv_polynomial.weighted_degree' MvPolynomial.weightedDegree'

section SemilatticeSup

variable [SemilatticeSup M]

/-- The weighted total degree of a multivariate polynomial, taking values in `WithBot M`. -/
def weightedTotalDegree' (w : Ïƒ â†’ M) (p : MvPolynomial Ïƒ R) : WithBot M :=
  p.support.sup fun s => weightedDegree' w s
#align mv_polynomial.weighted_total_degree' MvPolynomial.weightedTotalDegree'

/-- The `weightedTotalDegree'` of a polynomial `p` is `âŠ¥` if and only if `p = 0`. -/
theorem weightedTotalDegree'_eq_bot_iff (w : Ïƒ â†’ M) (p : MvPolynomial Ïƒ R) :
    weightedTotalDegree' w p = âŠ¥ â†” p = 0 := by
  simp only [weightedTotalDegree', Finset.sup_eq_bot_iff, mem_support_iff, WithBot.coe_ne_bot,
    MvPolynomial.eq_zero_iff]
  exact forall_congr' fun _ => Classical.not_not
  -- ğŸ‰ no goals
#align mv_polynomial.weighted_total_degree'_eq_bot_iff MvPolynomial.weightedTotalDegree'_eq_bot_iff

/-- The `weightedTotalDegree'` of the zero polynomial is `âŠ¥`. -/
theorem weightedTotalDegree'_zero (w : Ïƒ â†’ M) : weightedTotalDegree' w (0 : MvPolynomial Ïƒ R) = âŠ¥ :=
  by simp only [weightedTotalDegree', support_zero, Finset.sup_empty]
     -- ğŸ‰ no goals
#align mv_polynomial.weighted_total_degree'_zero MvPolynomial.weightedTotalDegree'_zero

section OrderBot

variable [OrderBot M]

/-- When `M` has a `âŠ¥` element, we can define the weighted total degree of a multivariate
  polynomial as a function taking values in `M`. -/
def weightedTotalDegree (w : Ïƒ â†’ M) (p : MvPolynomial Ïƒ R) : M :=
  p.support.sup fun s => weightedDegree' w s
#align mv_polynomial.weighted_total_degree MvPolynomial.weightedTotalDegree

/-- This lemma relates `weightedTotalDegree` and `weightedTotalDegree'`. -/
theorem weightedTotalDegree_coe (w : Ïƒ â†’ M) (p : MvPolynomial Ïƒ R) (hp : p â‰  0) :
    weightedTotalDegree' w p = â†‘(weightedTotalDegree w p) := by
  rw [Ne.def, â† weightedTotalDegree'_eq_bot_iff w p, â† Ne.def, WithBot.ne_bot_iff_exists] at hp
  -- âŠ¢ weightedTotalDegree' w p = â†‘(weightedTotalDegree w p)
  obtain âŸ¨m, hmâŸ© := hp
  -- âŠ¢ weightedTotalDegree' w p = â†‘(weightedTotalDegree w p)
  apply le_antisymm
  -- âŠ¢ weightedTotalDegree' w p â‰¤ â†‘(weightedTotalDegree w p)
  Â· simp only [weightedTotalDegree, weightedTotalDegree', Finset.sup_le_iff, WithBot.coe_le_coe]
    -- âŠ¢ âˆ€ (b : Ïƒ â†’â‚€ â„•), b âˆˆ support p â†’ â†‘(weightedDegree' w) b â‰¤ sup (support p) fun â€¦
    intro b
    -- âŠ¢ b âˆˆ support p â†’ â†‘(weightedDegree' w) b â‰¤ sup (support p) fun s => â†‘(weighted â€¦
    exact Finset.le_sup
    -- ğŸ‰ no goals
  Â· simp only [weightedTotalDegree]
    -- âŠ¢ â†‘(sup (support p) fun s => â†‘(weightedDegree' w) s) â‰¤ weightedTotalDegree' w p
    have hm' : weightedTotalDegree' w p â‰¤ m := le_of_eq hm.symm
    -- âŠ¢ â†‘(sup (support p) fun s => â†‘(weightedDegree' w) s) â‰¤ weightedTotalDegree' w p
    rw [â† hm]
    -- âŠ¢ â†‘(sup (support p) fun s => â†‘(weightedDegree' w) s) â‰¤ â†‘m
    simpa [weightedTotalDegree'] using hm'
    -- ğŸ‰ no goals
#align mv_polynomial.weighted_total_degree_coe MvPolynomial.weightedTotalDegree_coe

/-- The `weightedTotalDegree` of the zero polynomial is `âŠ¥`. -/
theorem weightedTotalDegree_zero (w : Ïƒ â†’ M) : weightedTotalDegree w (0 : MvPolynomial Ïƒ R) = âŠ¥ :=
  by simp only [weightedTotalDegree, support_zero, Finset.sup_empty]
     -- ğŸ‰ no goals
#align mv_polynomial.weighted_total_degree_zero MvPolynomial.weightedTotalDegree_zero

theorem le_weightedTotalDegree (w : Ïƒ â†’ M) {Ï† : MvPolynomial Ïƒ R} {d : Ïƒ â†’â‚€ â„•}
    (hd : d âˆˆ Ï†.support) : weightedDegree' w d â‰¤ Ï†.weightedTotalDegree w :=
  le_sup hd
#align mv_polynomial.le_weighted_total_degree MvPolynomial.le_weightedTotalDegree

end OrderBot

end SemilatticeSup

/-- A multivariate polynomial `Ï†` is weighted homogeneous of weighted degree `m` if all monomials
  occurring in `Ï†` have weighted degree `m`. -/
def IsWeightedHomogeneous (w : Ïƒ â†’ M) (Ï† : MvPolynomial Ïƒ R) (m : M) : Prop :=
  âˆ€ â¦ƒdâ¦„, coeff d Ï† â‰  0 â†’ weightedDegree' w d = m
#align mv_polynomial.is_weighted_homogeneous MvPolynomial.IsWeightedHomogeneous

variable (R)

/-- The submodule of homogeneous `MvPolynomial`s of degree `n`. -/
def weightedHomogeneousSubmodule (w : Ïƒ â†’ M) (m : M) : Submodule R (MvPolynomial Ïƒ R) where
  carrier := { x | x.IsWeightedHomogeneous w m }
  smul_mem' r a ha c hc := by
    rw [coeff_smul] at hc
    -- âŠ¢ â†‘(weightedDegree' w) c = m
    exact ha (right_ne_zero_of_mul hc)
    -- ğŸ‰ no goals
  zero_mem' d hd := False.elim (hd <| coeff_zero _)
    -- âŠ¢ â†‘(weightedDegree' w) c = m
  add_mem' {a} {b} ha hb c hc := by
    rw [coeff_add] at hc
    obtain h | h : coeff c a â‰  0 âˆ¨ coeff c b â‰  0 := by
      contrapose! hc
      -- ğŸ‰ no goals
      simp only [hc, add_zero]
      -- ğŸ‰ no goals
    Â· exact ha h
    Â· exact hb h
#align mv_polynomial.weighted_homogeneous_submodule MvPolynomial.weightedHomogeneousSubmodule

@[simp]
theorem mem_weightedHomogeneousSubmodule (w : Ïƒ â†’ M) (m : M) (p : MvPolynomial Ïƒ R) :
    p âˆˆ weightedHomogeneousSubmodule R w m â†” p.IsWeightedHomogeneous w m :=
  Iff.rfl
#align mv_polynomial.mem_weighted_homogeneous_submodule MvPolynomial.mem_weightedHomogeneousSubmodule

/-- The submodule `weightedHomogeneousSubmodule R w m` of homogeneous `MvPolynomial`s of
  degree `n` is equal to the `R`-submodule of all `p : (Ïƒ â†’â‚€ â„•) â†’â‚€ R` such that
  `p.support âŠ† {d | weightedDegree' w d = m}`. While equal, the former has a
  convenient definitional reduction. -/
theorem weightedHomogeneousSubmodule_eq_finsupp_supported (w : Ïƒ â†’ M) (m : M) :
    weightedHomogeneousSubmodule R w m = Finsupp.supported R R { d | weightedDegree' w d = m } := by
  ext x
  -- âŠ¢ x âˆˆ weightedHomogeneousSubmodule R w m â†” x âˆˆ supported R R {d | â†‘(weightedDe â€¦
  rw [mem_supported, Set.subset_def]
  -- âŠ¢ x âˆˆ weightedHomogeneousSubmodule R w m â†” âˆ€ (x_1 : Ïƒ â†’â‚€ â„•), x_1 âˆˆ â†‘x.support  â€¦
  simp only [Finsupp.mem_support_iff, mem_coe]
  -- âŠ¢ x âˆˆ weightedHomogeneousSubmodule R w m â†” âˆ€ (x_1 : Ïƒ â†’â‚€ â„•), â†‘x x_1 â‰  0 â†’ x_1  â€¦
  rfl
  -- ğŸ‰ no goals
#align mv_polynomial.weighted_homogeneous_submodule_eq_finsupp_supported MvPolynomial.weightedHomogeneousSubmodule_eq_finsupp_supported

variable {R}

/-- The submodule generated by products `Pm * Pn` of weighted homogeneous polynomials of degrees `m`
  and `n` is contained in the submodule of weighted homogeneous polynomials of degree `m + n`. -/
theorem weightedHomogeneousSubmodule_mul (w : Ïƒ â†’ M) (m n : M) :
    weightedHomogeneousSubmodule R w m * weightedHomogeneousSubmodule R w n â‰¤
      weightedHomogeneousSubmodule R w (m + n) := by
  classical
  rw [Submodule.mul_le]
  intro Ï† hÏ† Ïˆ hÏˆ c hc
  rw [coeff_mul] at hc
  obtain âŸ¨âŸ¨d, eâŸ©, hde, HâŸ© := Finset.exists_ne_zero_of_sum_ne_zero hc
  have aux : coeff d Ï† â‰  0 âˆ§ coeff e Ïˆ â‰  0 := by
    contrapose! H
    by_cases h : coeff d Ï† = 0 <;>
      simp_all only [Ne.def, not_false_iff, zero_mul, mul_zero]
  rw [â† Finsupp.mem_antidiagonal.mp hde, â† hÏ† aux.1, â† hÏˆ aux.2, map_add]
#align mv_polynomial.weighted_homogeneous_submodule_mul MvPolynomial.weightedHomogeneousSubmodule_mul

/-- Monomials are weighted homogeneous. -/
theorem isWeightedHomogeneous_monomial (w : Ïƒ â†’ M) (d : Ïƒ â†’â‚€ â„•) (r : R) {m : M}
    (hm : weightedDegree' w d = m) : IsWeightedHomogeneous w (monomial d r) m := by
  classical
  intro c hc
  rw [coeff_monomial] at hc
  split_ifs at hc with h
  Â· subst c
    exact hm
  Â· contradiction
#align mv_polynomial.is_weighted_homogeneous_monomial MvPolynomial.isWeightedHomogeneous_monomial

/-- A polynomial of weightedTotalDegree `âŠ¥` is weighted_homogeneous of degree `âŠ¥`. -/
theorem isWeightedHomogeneous_of_total_degree_zero [SemilatticeSup M] [OrderBot M] (w : Ïƒ â†’ M)
    {p : MvPolynomial Ïƒ R} (hp : weightedTotalDegree w p = (âŠ¥ : M)) :
    IsWeightedHomogeneous w p (âŠ¥ : M) := by
  intro d hd
  -- âŠ¢ â†‘(weightedDegree' w) d = âŠ¥
  have h := weightedTotalDegree_coe w p (MvPolynomial.ne_zero_iff.mpr âŸ¨d, hdâŸ©)
  -- âŠ¢ â†‘(weightedDegree' w) d = âŠ¥
  simp only [weightedTotalDegree', hp] at h
  -- âŠ¢ â†‘(weightedDegree' w) d = âŠ¥
  rw [eq_bot_iff, â† WithBot.coe_le_coe, â† h]
  -- âŠ¢ â†‘(â†‘(weightedDegree' w) d) â‰¤ sup (support p) fun s => â†‘(â†‘(weightedDegree' w) s)
  apply Finset.le_sup (mem_support_iff.mpr hd)
  -- ğŸ‰ no goals
#align mv_polynomial.is_weighted_homogeneous_of_total_degree_zero MvPolynomial.isWeightedHomogeneous_of_total_degree_zero

/-- Constant polynomials are weighted homogeneous of degree 0. -/
theorem isWeightedHomogeneous_C (w : Ïƒ â†’ M) (r : R) :
    IsWeightedHomogeneous w (C r : MvPolynomial Ïƒ R) 0 :=
  isWeightedHomogeneous_monomial _ _ _ (map_zero _)
set_option linter.uppercaseLean3 false in
#align mv_polynomial.is_weighted_homogeneous_C MvPolynomial.isWeightedHomogeneous_C

variable (R)

/-- 0 is weighted homogeneous of any degree. -/
theorem isWeightedHomogeneous_zero (w : Ïƒ â†’ M) (m : M) :
    IsWeightedHomogeneous w (0 : MvPolynomial Ïƒ R) m :=
  (weightedHomogeneousSubmodule R w m).zero_mem
#align mv_polynomial.is_weighted_homogeneous_zero MvPolynomial.isWeightedHomogeneous_zero

/-- 1 is weighted homogeneous of degree 0. -/
theorem isWeightedHomogeneous_one (w : Ïƒ â†’ M) : IsWeightedHomogeneous w (1 : MvPolynomial Ïƒ R) 0 :=
  isWeightedHomogeneous_C _ _
#align mv_polynomial.is_weighted_homogeneous_one MvPolynomial.isWeightedHomogeneous_one

/-- An indeterminate `i : Ïƒ` is weighted homogeneous of degree `w i`. -/
theorem isWeightedHomogeneous_X (w : Ïƒ â†’ M) (i : Ïƒ) :
    IsWeightedHomogeneous w (X i : MvPolynomial Ïƒ R) (w i) := by
  apply isWeightedHomogeneous_monomial
  -- âŠ¢ â†‘(weightedDegree' w) (Finsupp.single i 1) = w i
  simp only [weightedDegree', LinearMap.toAddMonoidHom_coe, total_single, one_nsmul]
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align mv_polynomial.is_weighted_homogeneous_X MvPolynomial.isWeightedHomogeneous_X

namespace IsWeightedHomogeneous

variable {R}

variable {Ï† Ïˆ : MvPolynomial Ïƒ R} {m n : M}

/-- The weighted degree of a weighted homogeneous polynomial controls its support. -/
theorem coeff_eq_zero {w : Ïƒ â†’ M} (hÏ† : IsWeightedHomogeneous w Ï† n) (d : Ïƒ â†’â‚€ â„•)
    (hd : weightedDegree' w d â‰  n) : coeff d Ï† = 0 := by
  have aux := mt (@hÏ† d) hd
  -- âŠ¢ coeff d Ï† = 0
  rwa [Classical.not_not] at aux
  -- ğŸ‰ no goals
#align mv_polynomial.is_weighted_homogeneous.coeff_eq_zero MvPolynomial.IsWeightedHomogeneous.coeff_eq_zero

/-- The weighted degree of a nonzero weighted homogeneous polynomial is well-defined. -/
theorem inj_right {w : Ïƒ â†’ M} (hÏ† : Ï† â‰  0) (hm : IsWeightedHomogeneous w Ï† m)
    (hn : IsWeightedHomogeneous w Ï† n) : m = n := by
  obtain âŸ¨d, hdâŸ© : âˆƒ d, coeff d Ï† â‰  0 := exists_coeff_ne_zero hÏ†
  -- âŠ¢ m = n
  rw [â† hm hd, â† hn hd]
  -- ğŸ‰ no goals
#align mv_polynomial.is_weighted_homogeneous.inj_right MvPolynomial.IsWeightedHomogeneous.inj_right

/-- The sum of two weighted homogeneous polynomials of degree `n` is weighted homogeneous of
  weighted degree `n`. -/
theorem add {w : Ïƒ â†’ M} (hÏ† : IsWeightedHomogeneous w Ï† n) (hÏˆ : IsWeightedHomogeneous w Ïˆ n) :
    IsWeightedHomogeneous w (Ï† + Ïˆ) n :=
  (weightedHomogeneousSubmodule R w n).add_mem hÏ† hÏˆ
#align mv_polynomial.is_weighted_homogeneous.add MvPolynomial.IsWeightedHomogeneous.add

/-- The sum of weighted homogeneous polynomials of degree `n` is weighted homogeneous of
  weighted degree `n`. -/
theorem sum {Î¹ : Type*} (s : Finset Î¹) (Ï† : Î¹ â†’ MvPolynomial Ïƒ R) (n : M) {w : Ïƒ â†’ M}
    (h : âˆ€ i âˆˆ s, IsWeightedHomogeneous w (Ï† i) n) : IsWeightedHomogeneous w (âˆ‘ i in s, Ï† i) n :=
  (weightedHomogeneousSubmodule R w n).sum_mem h
#align mv_polynomial.is_weighted_homogeneous.sum MvPolynomial.IsWeightedHomogeneous.sum

/-- The product of weighted homogeneous polynomials of weighted degrees `m` and `n` is weighted
  homogeneous of weighted degree `m + n`. -/
theorem mul {w : Ïƒ â†’ M} (hÏ† : IsWeightedHomogeneous w Ï† m) (hÏˆ : IsWeightedHomogeneous w Ïˆ n) :
    IsWeightedHomogeneous w (Ï† * Ïˆ) (m + n) :=
  weightedHomogeneousSubmodule_mul w m n <| Submodule.mul_mem_mul hÏ† hÏˆ
#align mv_polynomial.is_weighted_homogeneous.mul MvPolynomial.IsWeightedHomogeneous.mul

/-- A product of weighted homogeneous polynomials is weighted homogeneous, with weighted degree
  equal to the sum of the weighted degrees. -/
theorem prod {Î¹ : Type*} (s : Finset Î¹) (Ï† : Î¹ â†’ MvPolynomial Ïƒ R) (n : Î¹ â†’ M) {w : Ïƒ â†’ M} :
    (âˆ€ i âˆˆ s, IsWeightedHomogeneous w (Ï† i) (n i)) â†’
      IsWeightedHomogeneous w (âˆ i in s, Ï† i) (âˆ‘ i in s, n i) := by
  classical
  refine Finset.induction_on s ?_ ?_
  Â· intro
    simp only [isWeightedHomogeneous_one, Finset.sum_empty, Finset.prod_empty]
  Â· intro i s his IH h
    simp only [his, Finset.prod_insert, Finset.sum_insert, not_false_iff]
    apply (h i (Finset.mem_insert_self _ _)).mul (IH _)
    intro j hjs
    exact h j (Finset.mem_insert_of_mem hjs)
#align mv_polynomial.is_weighted_homogeneous.prod MvPolynomial.IsWeightedHomogeneous.prod

/-- A non zero weighted homogeneous polynomial of weighted degree `n` has weighted total degree
  `n`. -/
theorem weighted_total_degree [SemilatticeSup M] {w : Ïƒ â†’ M} (hÏ† : IsWeightedHomogeneous w Ï† n)
    (h : Ï† â‰  0) : weightedTotalDegree' w Ï† = n := by
  simp only [weightedTotalDegree']
  -- âŠ¢ (sup (support Ï†) fun s => â†‘(â†‘(weightedDegree' w) s)) = â†‘n
  apply le_antisymm
  -- âŠ¢ (sup (support Ï†) fun s => â†‘(â†‘(weightedDegree' w) s)) â‰¤ â†‘n
  Â· simp only [Finset.sup_le_iff, mem_support_iff, WithBot.coe_le_coe]
    -- âŠ¢ âˆ€ (b : Ïƒ â†’â‚€ â„•), coeff b Ï† â‰  0 â†’ â†‘(weightedDegree' w) b â‰¤ n
    exact fun d hd => le_of_eq (hÏ† hd)
    -- ğŸ‰ no goals
  Â· obtain âŸ¨d, hdâŸ© : âˆƒ d, coeff d Ï† â‰  0 := exists_coeff_ne_zero h
    -- âŠ¢ â†‘n â‰¤ sup (support Ï†) fun s => â†‘(â†‘(weightedDegree' w) s)
    simp only [â† hÏ† hd, Finsupp.sum]
    -- âŠ¢ â†‘(â†‘(weightedDegree' w) d) â‰¤ sup (support Ï†) fun s => â†‘(â†‘(weightedDegree' w) s)
    replace hd := Finsupp.mem_support_iff.mpr hd
    -- âŠ¢ â†‘(â†‘(weightedDegree' w) d) â‰¤ sup (support Ï†) fun s => â†‘(â†‘(weightedDegree' w) s)
    apply Finset.le_sup hd
    -- ğŸ‰ no goals
#align mv_polynomial.is_weighted_homogeneous.weighted_total_degree MvPolynomial.IsWeightedHomogeneous.weighted_total_degree

/-- The weighted homogeneous submodules form a graded monoid. -/
instance WeightedHomogeneousSubmodule.gcomm_monoid {w : Ïƒ â†’ M} :
    SetLike.GradedMonoid (weightedHomogeneousSubmodule R w) where
  one_mem := isWeightedHomogeneous_one R w
  mul_mem _ _ _ _ := IsWeightedHomogeneous.mul
#align mv_polynomial.is_weighted_homogeneous.weighted_homogeneous_submodule.gcomm_monoid MvPolynomial.IsWeightedHomogeneous.WeightedHomogeneousSubmodule.gcomm_monoid

end IsWeightedHomogeneous

variable {R}

/-- `weightedHomogeneousComponent w n Ï†` is the part of `Ï†` that is weighted homogeneous of
  weighted degree `n`, with respect to the weights `w`.
  See `sum_weightedHomogeneousComponent` for the statement that `Ï†` is equal to the sum
  of all its weighted homogeneous components. -/
def weightedHomogeneousComponent (w : Ïƒ â†’ M) (n : M) : MvPolynomial Ïƒ R â†’â‚—[R] MvPolynomial Ïƒ R :=
  (Submodule.subtype _).comp <| Finsupp.restrictDom _ _ { d | weightedDegree' w d = n }
#align mv_polynomial.weighted_homogeneous_component MvPolynomial.weightedHomogeneousComponent

section WeightedHomogeneousComponent

variable {w : Ïƒ â†’ M} (n : M) (Ï† Ïˆ : MvPolynomial Ïƒ R)

theorem coeff_weightedHomogeneousComponent [DecidableEq M] (d : Ïƒ â†’â‚€ â„•) :
    coeff d (weightedHomogeneousComponent w n Ï†) =
      if weightedDegree' w d = n then coeff d Ï† else 0 :=
  Finsupp.filter_apply (fun d : Ïƒ â†’â‚€ â„• => weightedDegree' w d = n) Ï† d
#align mv_polynomial.coeff_weighted_homogeneous_component MvPolynomial.coeff_weightedHomogeneousComponent

theorem weightedHomogeneousComponent_apply [DecidableEq M] :
    weightedHomogeneousComponent w n Ï† =
      âˆ‘ d in Ï†.support.filter fun d => weightedDegree' w d = n, monomial d (coeff d Ï†) :=
  Finsupp.filter_eq_sum (fun d : Ïƒ â†’â‚€ â„• => weightedDegree' w d = n) Ï†
#align mv_polynomial.weighted_homogeneous_component_apply MvPolynomial.weightedHomogeneousComponent_apply

/-- The `n` weighted homogeneous component of a polynomial is weighted homogeneous of
weighted degree `n`. -/
theorem weightedHomogeneousComponent_isWeightedHomogeneous :
    (weightedHomogeneousComponent w n Ï†).IsWeightedHomogeneous w n := by
  classical
  intro d hd
  contrapose! hd
  rw [coeff_weightedHomogeneousComponent, if_neg hd]
#align mv_polynomial.weighted_homogeneous_component_is_weighted_homogeneous MvPolynomial.weightedHomogeneousComponent_isWeightedHomogeneous

@[simp]
theorem weightedHomogeneousComponent_C_mul (n : M) (r : R) :
    weightedHomogeneousComponent w n (C r * Ï†) = C r * weightedHomogeneousComponent w n Ï† := by
  simp only [C_mul', LinearMap.map_smul]
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align mv_polynomial.weighted_homogeneous_component_C_mul MvPolynomial.weightedHomogeneousComponent_C_mul

theorem weightedHomogeneousComponent_eq_zero'
    (h : âˆ€ d : Ïƒ â†’â‚€ â„•, d âˆˆ Ï†.support â†’ weightedDegree' w d â‰  n) :
    weightedHomogeneousComponent w n Ï† = 0 := by
  classical
  rw [weightedHomogeneousComponent_apply, sum_eq_zero]
  intro d hd; rw [mem_filter] at hd
  exfalso; exact h _ hd.1 hd.2
#align mv_polynomial.weighted_homogeneous_component_eq_zero' MvPolynomial.weightedHomogeneousComponent_eq_zero'

theorem weightedHomogeneousComponent_eq_zero [SemilatticeSup M] [OrderBot M]
    (h : weightedTotalDegree w Ï† < n) : weightedHomogeneousComponent w n Ï† = 0 := by
  classical
  rw [weightedHomogeneousComponent_apply, sum_eq_zero]
  intro d hd
  rw [Finset.mem_filter] at hd
  exfalso
  apply lt_irrefl n
  nth_rw 1 [â† hd.2]
  exact lt_of_le_of_lt (le_weightedTotalDegree w hd.1) h
#align mv_polynomial.weighted_homogeneous_component_eq_zero MvPolynomial.weightedHomogeneousComponent_eq_zero

theorem weightedHomogeneousComponent_finsupp :
    (Function.support fun m => weightedHomogeneousComponent w m Ï†).Finite := by
  suffices
    (Function.support fun m => weightedHomogeneousComponent w m Ï†) âŠ†
      (fun d => weightedDegree' w d) '' Ï†.support by
    exact Finite.subset ((fun d : Ïƒ â†’â‚€ â„• => (weightedDegree' w) d) '' â†‘(support Ï†)).toFinite this
  intro m hm
  -- âŠ¢ m âˆˆ (fun d => â†‘(weightedDegree' w) d) '' â†‘(support Ï†)
  by_contra hm'
  -- âŠ¢ False
  apply hm
  -- âŠ¢ (fun m => â†‘(weightedHomogeneousComponent w m) Ï†) m = 0
  simp only [mem_support, Ne.def] at hm
  -- âŠ¢ (fun m => â†‘(weightedHomogeneousComponent w m) Ï†) m = 0
  simp only [Set.mem_image, not_exists, not_and] at hm'
  -- âŠ¢ (fun m => â†‘(weightedHomogeneousComponent w m) Ï†) m = 0
  exact weightedHomogeneousComponent_eq_zero' m Ï† hm'
  -- ğŸ‰ no goals
#align mv_polynomial.weighted_homogeneous_component_finsupp MvPolynomial.weightedHomogeneousComponent_finsupp

variable (w)

/-- Every polynomial is the sum of its weighted homogeneous components. -/
theorem sum_weightedHomogeneousComponent :
    (finsum fun m => weightedHomogeneousComponent w m Ï†) = Ï† := by
  classical
  rw [finsum_eq_sum _ (weightedHomogeneousComponent_finsupp Ï†)]
  ext1 d
  simp only [coeff_sum, coeff_weightedHomogeneousComponent]
  rw [Finset.sum_eq_single (weightedDegree' w d)]
  Â· rw [if_pos rfl]
  Â· intro m _ hm'
    rw [if_neg hm'.symm]
  Â· intro hm
    rw [if_pos rfl]
    simp only [Finite.mem_toFinset, mem_support, Ne.def, Classical.not_not] at hm
    have := coeff_weightedHomogeneousComponent (w := w) (weightedDegree' w d) Ï† d
    rw [hm, if_pos rfl, coeff_zero] at this
    exact this.symm
#align mv_polynomial.sum_weighted_homogeneous_component MvPolynomial.sum_weightedHomogeneousComponent

variable {w}

/-- The weighted homogeneous components of a weighted homogeneous polynomial. -/
theorem weightedHomogeneousComponent_weighted_homogeneous_polynomial [DecidableEq M] (m n : M)
    (p : MvPolynomial Ïƒ R) (h : p âˆˆ weightedHomogeneousSubmodule R w n) :
    weightedHomogeneousComponent w m p = if m = n then p else 0 := by
  simp only [mem_weightedHomogeneousSubmodule] at h
  -- âŠ¢ â†‘(weightedHomogeneousComponent w m) p = if m = n then p else 0
  ext x
  -- âŠ¢ coeff x (â†‘(weightedHomogeneousComponent w m) p) = coeff x (if m = n then p e â€¦
  rw [coeff_weightedHomogeneousComponent]
  -- âŠ¢ (if â†‘(weightedDegree' w) x = m then coeff x p else 0) = coeff x (if m = n th â€¦
  by_cases zero_coeff : coeff x p = 0
  -- âŠ¢ (if â†‘(weightedDegree' w) x = m then coeff x p else 0) = coeff x (if m = n th â€¦
  Â· split_ifs
    all_goals simp only [zero_coeff, coeff_zero]
    -- ğŸ‰ no goals
  Â· rw [h zero_coeff]
    -- âŠ¢ (if n = m then coeff x p else 0) = coeff x (if m = n then p else 0)
    simp only [show n = m â†” m = n from eq_comm]
    -- âŠ¢ (if m = n then coeff x p else 0) = coeff x (if m = n then p else 0)
    split_ifs with h1
    -- âŠ¢ coeff x p = coeff x p
    Â· rfl
      -- ğŸ‰ no goals
    Â· simp only [coeff_zero]
      -- ğŸ‰ no goals
#align mv_polynomial.weighted_homogeneous_component_weighted_homogeneous_polynomial MvPolynomial.weightedHomogeneousComponent_weighted_homogeneous_polynomial

end WeightedHomogeneousComponent

end AddCommMonoid

section CanonicallyOrderedAddMonoid

variable [CanonicallyOrderedAddMonoid M] {w : Ïƒ â†’ M} (Ï† : MvPolynomial Ïƒ R)

/-- If `M` is a `CanonicallyOrderedAddMonoid`, then the `weightedHomogeneousComponent`
  of weighted degree `0` of a polynomial is its constant coefficient. -/
@[simp]
theorem weightedHomogeneousComponent_zero [NoZeroSMulDivisors â„• M] (hw : âˆ€ i : Ïƒ, w i â‰  0) :
    weightedHomogeneousComponent w 0 Ï† = C (coeff 0 Ï†) := by
  classical
  ext1 d
  rcases Classical.em (d = 0) with (rfl | hd)
  Â· simp only [coeff_weightedHomogeneousComponent, if_pos, map_zero, coeff_zero_C]
  Â· rw [coeff_weightedHomogeneousComponent, if_neg, coeff_C, if_neg (Ne.symm hd)]
    simp only [weightedDegree', LinearMap.toAddMonoidHom_coe, Finsupp.total_apply, Finsupp.sum,
      sum_eq_zero_iff, Finsupp.mem_support_iff, Ne.def, smul_eq_zero, not_forall, not_or,
      and_self_left, exists_prop]
    simp only [FunLike.ext_iff, Finsupp.coe_zero, Pi.zero_apply, not_forall] at hd
    obtain âŸ¨i, hiâŸ© := hd
    exact âŸ¨i, hi, hw iâŸ©
#align mv_polynomial.weighted_homogeneous_component_zero MvPolynomial.weightedHomogeneousComponent_zero

end CanonicallyOrderedAddMonoid

end MvPolynomial
