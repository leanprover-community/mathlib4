/-
Copyright (c) 2024 Jz Pan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jz Pan
-/
import Mathlib.RingTheory.Localization.FractionRing
import Mathlib.FieldTheory.Adjoin

/-!

# Some results on image of a ring and its fraction field under ring homomorphisms

-/

namespace IsFractionRing

variable {F A K L : Type*} [Field F] [CommRing A] [Algebra F A]
  [Field K] [Algebra F K] [Algebra A K] [IsFractionRing A K] [Field L] [Algebra F L]

section RingHom

variable {g : A →+* L} {f : K →+* L}

/-- If `A` is a ring with fraction field `K`, `g : A →+* L` lifts to `f : K →+* L`,
then the image of `f` is the field genereated by the image of `g`. -/
theorem ringHom_fieldRange_eq_of_comp_eq
    (h : RingHom.comp f (algebraMap A K) = g) :
    f.fieldRange = Subfield.closure g.range := by
  refine le_antisymm ?_ (by simp [← h, Set.range_comp_subset_range])
  rintro x ⟨y, rfl⟩
  obtain ⟨_, _, -, rfl⟩ := div_surjective (A := A) y
  simp_rw [map_div₀, ← RingHom.comp_apply, h]
  apply Subfield.div_mem <;> (apply Subfield.subset_closure; simp)

/-- If `A` is a ring with fraction field `K`, `g : A →+* L` lifts to `f : K →+* L`,
`s` is a set such that the image of `g` is the subring generated by `s`,
then the image of `f` is the field genereated by `s`. -/
theorem ringHom_fieldRange_eq_of_comp_eq_of_range_eq
    (h : RingHom.comp f (algebraMap A K) = g)
    {s : Set L} (hs : g.range = Subring.closure s) :
    f.fieldRange = Subfield.closure s := by
  rw [ringHom_fieldRange_eq_of_comp_eq h, hs]
  ext
  simp_rw [Subfield.mem_closure_iff, Subring.closure_eq]

end RingHom

section AlgHom

variable {g : A →ₐ[F] L} {f : K →ₐ[F] L}

/-- If `A` is an `F`-algebra with fraction field `K`, `g : A →ₐ[F] L` lifts to `f : K →ₐ[F] L`,
then the image of `f` is the field genereated by the image of `g`.
Note: this does not require `IsScalarTower F A K`. -/
theorem algHom_fieldRange_eq_of_comp_eq
    (h : RingHom.comp f (algebraMap A K) = (g : A →+* L)) :
    f.fieldRange = IntermediateField.adjoin F g.range := by
  apply IntermediateField.toSubfield_injective
  simp_rw [AlgHom.fieldRange_toSubfield, IntermediateField.adjoin_toSubfield]
  convert ringHom_fieldRange_eq_of_comp_eq h using 2
  exact Set.union_eq_self_of_subset_left fun _ ⟨x, hx⟩ ↦ ⟨algebraMap F A x, by simp [← hx]⟩

/-- If `A` is an `F`-algebra with fraction field `K`, `g : A →ₐ[F] L` lifts to `f : K →ₐ[F] L`,
`s` is a set such that the image of `g` is the subalgebra generated by `s`,
then the image of `f` is the intermediatefield genereated by `s`.
Note: this does not require `IsScalarTower F A K`. -/
theorem algHom_fieldRange_eq_of_comp_eq_of_range_eq
    (h : RingHom.comp f (algebraMap A K) = (g : A →+* L))
    {s : Set L} (hs : g.range = Algebra.adjoin F s) :
    f.fieldRange = IntermediateField.adjoin F s := by
  apply IntermediateField.toSubfield_injective
  simp_rw [AlgHom.fieldRange_toSubfield, IntermediateField.adjoin_toSubfield]
  refine ringHom_fieldRange_eq_of_comp_eq_of_range_eq h ?_
  rw [← Algebra.adjoin_eq_ring_closure, ← hs]; rfl

end AlgHom

end IsFractionRing
