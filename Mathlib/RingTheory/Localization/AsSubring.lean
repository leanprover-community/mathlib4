/-
Copyright (c) 2022 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz, Junyan Xu
-/
import Mathlib.RingTheory.Localization.LocalizationLocalization

#align_import ring_theory.localization.as_subring from "leanprover-community/mathlib"@"649ca66bf4d62796b5eefef966e622d91aa471f3"

/-!

# Localizations of domains as subalgebras of the fraction field.

Given a domain `A` with fraction field `K`, and a submonoid `S` of `A` which
does not contain zero, this file constructs the localization of `A` at `S`
as a subalgebra of the field `K` over `A`.

-/


namespace Localization

open nonZeroDivisors

variable {A : Type*} (K : Type*) [CommRing A] (S : Submonoid A) (hS : S ‚â§ A‚Å∞)

section CommRing

variable [CommRing K] [Algebra A K] [IsFractionRing A K]

theorem map_isUnit_of_le (hS : S ‚â§ A‚Å∞) (s : S) : IsUnit (algebraMap A K s) := by
  apply IsLocalization.map_units K (‚ü®s.1, hS s.2‚ü© : A‚Å∞)
  -- üéâ no goals
#align localization.map_is_unit_of_le Localization.map_isUnit_of_le

/-- The canonical map from a localization of `A` at `S` to the fraction ring
  of `A`, given that `S ‚â§ A‚Å∞`. -/
noncomputable def mapToFractionRing (B : Type*) [CommRing B] [Algebra A B] [IsLocalization S B]
    (hS : S ‚â§ A‚Å∞) : B ‚Üí‚Çê[A] K :=
  { IsLocalization.lift (map_isUnit_of_le K S hS) with commutes' := fun a => by simp }
                                                                                -- üéâ no goals
#align localization.map_to_fraction_ring Localization.mapToFractionRing

@[simp]
theorem mapToFractionRing_apply {B : Type*} [CommRing B] [Algebra A B] [IsLocalization S B]
    (hS : S ‚â§ A‚Å∞) (b : B) :
    mapToFractionRing K S B hS b = IsLocalization.lift (map_isUnit_of_le K S hS) b :=
  rfl
#align localization.map_to_fraction_ring_apply Localization.mapToFractionRing_apply

theorem mem_range_mapToFractionRing_iff (B : Type*) [CommRing B] [Algebra A B] [IsLocalization S B]
    (hS : S ‚â§ A‚Å∞) (x : K) :
    x ‚àà (mapToFractionRing K S B hS).range ‚Üî
      ‚àÉ (a s : A) (hs : s ‚àà S), x = IsLocalization.mk' K a ‚ü®s, hS hs‚ü© :=
  ‚ü®by
    rintro ‚ü®x, rfl‚ü©
    -- ‚ä¢ ‚àÉ a s hs, ‚Üë‚Üë(mapToFractionRing K S B hS) x = IsLocalization.mk' K a { val := ‚Ä¶
    obtain ‚ü®a, s, rfl‚ü© := IsLocalization.mk'_surjective S x
    -- ‚ä¢ ‚àÉ a_1 s_1 hs, ‚Üë‚Üë(mapToFractionRing K S B hS) (IsLocalization.mk' B a s) = Is ‚Ä¶
    use a, s, s.2
    -- ‚ä¢ ‚Üë‚Üë(mapToFractionRing K S B hS) (IsLocalization.mk' B a s) = IsLocalization.m ‚Ä¶
    apply IsLocalization.lift_mk', by
    -- üéâ no goals
    rintro ‚ü®a, s, hs, rfl‚ü©
    -- ‚ä¢ IsLocalization.mk' K a { val := s, property := (_ : s ‚àà A‚Å∞) } ‚àà AlgHom.range ‚Ä¶
    use IsLocalization.mk' _ a ‚ü®s, hs‚ü©
    -- ‚ä¢ ‚Üë‚Üë(mapToFractionRing K S B hS) (IsLocalization.mk' B a { val := s, property  ‚Ä¶
    apply IsLocalization.lift_mk'‚ü©
    -- üéâ no goals
#align localization.mem_range_map_to_fraction_ring_iff Localization.mem_range_mapToFractionRing_iff

instance isLocalization_range_mapToFractionRing (B : Type*) [CommRing B] [Algebra A B]
    [IsLocalization S B] (hS : S ‚â§ A‚Å∞) : IsLocalization S (mapToFractionRing K S B hS).range :=
  IsLocalization.isLocalization_of_algEquiv S <|
    show B ‚âÉ‚Çê[A] _ from AlgEquiv.ofBijective (mapToFractionRing K S B hS).rangeRestrict (by
      refine' ‚ü®fun a b h => _, Set.surjective_onto_range‚ü©
      -- ‚ä¢ a = b
      refine' (IsLocalization.lift_injective_iff _).2 (fun a b => _) (Subtype.ext_iff.1 h)
      -- ‚ä¢ ‚Üë(algebraMap A B) a = ‚Üë(algebraMap A B) b ‚Üî ‚Üë(algebraMap A K) a = ‚Üë(algebraM ‚Ä¶
      exact ‚ü®fun h => congr_arg _ (IsLocalization.injective _ hS h),
        fun h => congr_arg _ (IsFractionRing.injective A K h)‚ü©)
#align localization.is_localization_range_map_to_fraction_ring Localization.isLocalization_range_mapToFractionRing

instance isFractionRing_range_mapToFractionRing (B : Type*) [CommRing B] [Algebra A B]
    [IsLocalization S B] (hS : S ‚â§ A‚Å∞) : IsFractionRing (mapToFractionRing K S B hS).range K :=
  IsFractionRing.isFractionRing_of_isLocalization S _ _ hS
#align localization.is_fraction_ring_range_map_to_fraction_ring Localization.isFractionRing_range_mapToFractionRing

/-- Given a commutative ring `A` with fraction ring `K`, and a submonoid `S` of `A` which
contains no zero divisor, this is the localization of `A` at `S`, considered as
a subalgebra of `K` over `A`.

The carrier of this subalgebra is defined as the set of all `x : K` of the form
`IsLocalization.mk' K a ‚ü®s, _‚ü©`, where `s ‚àà S`.
-/
noncomputable def subalgebra (hS : S ‚â§ A‚Å∞) : Subalgebra A K :=
  (mapToFractionRing K S (Localization S) hS).range.copy
      { x | ‚àÉ (a s : A) (hs : s ‚àà S), x = IsLocalization.mk' K a ‚ü®s, hS hs‚ü© } <| by
    ext
    -- ‚ä¢ x‚úù ‚àà {x | ‚àÉ a s hs, x = IsLocalization.mk' K a { val := s, property := (_ :  ‚Ä¶
    symm
    -- ‚ä¢ x‚úù ‚àà ‚Üë(AlgHom.range (mapToFractionRing K S (Localization S) hS)) ‚Üî x‚úù ‚àà {x | ‚Ä¶
    apply mem_range_mapToFractionRing_iff
    -- üéâ no goals
#align localization.subalgebra Localization.subalgebra

namespace subalgebra

instance isLocalization_subalgebra : IsLocalization S (subalgebra K S hS) := by
  dsimp only [Localization.subalgebra]
  -- ‚ä¢ IsLocalization S { x // x ‚àà Subalgebra.copy (AlgHom.range (mapToFractionRing ‚Ä¶
  rw [Subalgebra.copy_eq]
  -- ‚ä¢ IsLocalization S { x // x ‚àà AlgHom.range (mapToFractionRing K S (Localizatio ‚Ä¶
  infer_instance
  -- üéâ no goals
#align localization.subalgebra.is_localization_subalgebra Localization.subalgebra.isLocalization_subalgebra

instance isFractionRing : IsFractionRing (subalgebra K S hS) K :=
  IsFractionRing.isFractionRing_of_isLocalization S _ _ hS
#align localization.subalgebra.is_fraction_ring Localization.subalgebra.isFractionRing

end subalgebra

end CommRing

section Field

variable [Field K] [Algebra A K] [IsFractionRing A K]

namespace subalgebra

theorem mem_range_mapToFractionRing_iff_ofField (B : Type*) [CommRing B] [Algebra A B]
    [IsLocalization S B] (x : K) :
    x ‚àà (mapToFractionRing K S B hS).range ‚Üî
      ‚àÉ (a s : A) (_ : s ‚àà S), x = algebraMap A K a * (algebraMap A K s)‚Åª¬π := by
  rw [mem_range_mapToFractionRing_iff]
  -- ‚ä¢ (‚àÉ a s hs, x = IsLocalization.mk' K a { val := s, property := (_ : s ‚àà A‚Å∞) } ‚Ä¶
  convert Iff.rfl
  -- ‚ä¢ ‚Üë(algebraMap A K) x‚úù¬≤ * (‚Üë(algebraMap A K) x‚úù¬π)‚Åª¬π = IsLocalization.mk' K x‚úù¬≤ ‚Ä¶
  congr
  -- ‚ä¢ (‚Üë(algebraMap A K) x‚úù¬π)‚Åª¬π = ‚Üë(‚Üë(IsUnit.liftRight (MonoidHom.restrict (Submon ‚Ä¶
  rw [Units.val_inv_eq_inv_val]
  -- ‚ä¢ (‚Üë(algebraMap A K) x‚úù¬π)‚Åª¬π = (‚Üë(‚Üë(IsUnit.liftRight (MonoidHom.restrict (Submo ‚Ä¶
  rfl
  -- üéâ no goals
#align localization.subalgebra.mem_range_map_to_fraction_ring_iff_of_field Localization.subalgebra.mem_range_mapToFractionRing_iff_ofField

/-- Given a domain `A` with fraction field `K`, and a submonoid `S` of `A` which
contains no zero divisor, this is the localization of `A` at `S`, considered as
a subalgebra of `K` over `A`.

The carrier of this subalgebra is defined as the set of all `x : K` of the form
`algebraMap A K a * (algebraMap A K s)‚Åª¬π` where `a s : A` and `s ‚àà S`.
-/
noncomputable def ofField : Subalgebra A K :=
  (mapToFractionRing K S (Localization S) hS).range.copy
      { x | ‚àÉ (a s : A) (_ : s ‚àà S), x = algebraMap A K a * (algebraMap A K s)‚Åª¬π } <| by
    ext
    -- ‚ä¢ x‚úù ‚àà {x | ‚àÉ a s x_1, x = ‚Üë(algebraMap A K) a * (‚Üë(algebraMap A K) s)‚Åª¬π} ‚Üî x‚úù ‚Ä¶
    symm
    -- ‚ä¢ x‚úù ‚àà ‚Üë(AlgHom.range (mapToFractionRing K S (Localization S) hS)) ‚Üî x‚úù ‚àà {x | ‚Ä¶
    apply mem_range_mapToFractionRing_iff_ofField
    -- üéâ no goals
#align localization.subalgebra.of_field Localization.subalgebra.ofField

instance isLocalization_ofField : IsLocalization S (subalgebra.ofField K S hS) := by
  dsimp only [Localization.subalgebra.ofField]
  -- ‚ä¢ IsLocalization S { x // x ‚àà Subalgebra.copy (AlgHom.range (mapToFractionRing ‚Ä¶
  rw [Subalgebra.copy_eq]
  -- ‚ä¢ IsLocalization S { x // x ‚àà AlgHom.range (mapToFractionRing K S (Localizatio ‚Ä¶
  infer_instance
  -- üéâ no goals
#align localization.subalgebra.is_localization_of_field Localization.subalgebra.isLocalization_ofField

instance isFractionRing_ofField : IsFractionRing (subalgebra.ofField K S hS) K :=
  IsFractionRing.isFractionRing_of_isLocalization S _ _ hS
#align localization.subalgebra.is_fraction_ring_of_field Localization.subalgebra.isFractionRing_ofField

end subalgebra

end Field

end Localization
