/-
Copyright (c) 2018 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau, Mario Carneiro, Johan Commelin, Amelia Livingston, Anne Baanen
-/
import Mathlib.GroupTheory.Submonoid.Inverses
import Mathlib.RingTheory.FiniteType
import Mathlib.RingTheory.Localization.Basic

#align_import ring_theory.localization.inv_submonoid from "leanprover-community/mathlib"@"6e7ca692c98bbf8a64868f61a67fb9c33b10770d"

/-!
# Submonoid of inverses

## Main definitions

 * `IsLocalization.invSubmonoid M S` is the submonoid of `S = Mâ»Â¹R` consisting of inverses of
   each element `x âˆˆ M`

## Implementation notes

See `Mathlib/RingTheory/Localization/Basic.lean` for a design overview.

## Tags
localization, ring localization, commutative ring localization, characteristic predicate,
commutative ring, field of fractions
-/


variable {R : Type*} [CommRing R] (M : Submonoid R) (S : Type*) [CommRing S]

variable [Algebra R S] {P : Type*} [CommRing P]

open Function

open BigOperators

namespace IsLocalization

section InvSubmonoid

/-- The submonoid of `S = Mâ»Â¹R` consisting of `{ 1 / x | x âˆˆ M }`. -/
def invSubmonoid : Submonoid S :=
  (M.map (algebraMap R S)).leftInv
#align is_localization.inv_submonoid IsLocalization.invSubmonoid

variable [IsLocalization M S]

theorem submonoid_map_le_is_unit : M.map (algebraMap R S) â‰¤ IsUnit.submonoid S := by
  rintro _ âŸ¨a, ha, rflâŸ©
  -- âŠ¢ â†‘(algebraMap R S) a âˆˆ IsUnit.submonoid S
  exact IsLocalization.map_units S âŸ¨_, haâŸ©
  -- ðŸŽ‰ no goals
#align is_localization.submonoid_map_le_is_unit IsLocalization.submonoid_map_le_is_unit

/-- There is an equivalence of monoids between the image of `M` and `invSubmonoid`. -/
noncomputable abbrev equivInvSubmonoid : M.map (algebraMap R S) â‰ƒ* invSubmonoid M S :=
  ((M.map (algebraMap R S)).leftInvEquiv (submonoid_map_le_is_unit M S)).symm
#align is_localization.equiv_inv_submonoid IsLocalization.equivInvSubmonoid

/-- There is a canonical map from `M` to `invSubmonoid` sending `x` to `1 / x`. -/
noncomputable def toInvSubmonoid : M â†’* invSubmonoid M S :=
  (equivInvSubmonoid M S).toMonoidHom.comp ((algebraMap R S : R â†’* S).submonoidMap M)
#align is_localization.to_inv_submonoid IsLocalization.toInvSubmonoid

theorem toInvSubmonoid_surjective : Function.Surjective (toInvSubmonoid M S) :=
  Function.Surjective.comp (Î² := M.map (algebraMap R S))
    (Equiv.surjective (equivInvSubmonoid _ _).toEquiv) (MonoidHom.submonoidMap_surjective _ _)
#align is_localization.to_inv_submonoid_surjective IsLocalization.toInvSubmonoid_surjective

@[simp]
theorem toInvSubmonoid_mul (m : M) : (toInvSubmonoid M S m : S) * algebraMap R S m = 1 :=
  Submonoid.leftInvEquiv_symm_mul _ (submonoid_map_le_is_unit _ _) _
#align is_localization.to_inv_submonoid_mul IsLocalization.toInvSubmonoid_mul

@[simp]
theorem mul_toInvSubmonoid (m : M) : algebraMap R S m * (toInvSubmonoid M S m : S) = 1 :=
  Submonoid.mul_leftInvEquiv_symm _ (submonoid_map_le_is_unit _ _) âŸ¨_, _âŸ©
#align is_localization.mul_to_inv_submonoid IsLocalization.mul_toInvSubmonoid

@[simp]
theorem smul_toInvSubmonoid (m : M) : m â€¢ (toInvSubmonoid M S m : S) = 1 := by
  convert mul_toInvSubmonoid M S m
  -- âŠ¢ HSMul.hSMul m = HMul.hMul (â†‘(algebraMap R S) â†‘m)
  ext
  -- âŠ¢ m â€¢ xâœ = â†‘(algebraMap R S) â†‘m * xâœ
  rw [â† Algebra.smul_def]
  -- âŠ¢ m â€¢ xâœ = â†‘m â€¢ xâœ
  rfl
  -- ðŸŽ‰ no goals
#align is_localization.smul_to_inv_submonoid IsLocalization.smul_toInvSubmonoid

variable {S}

-- Porting note: `surj'` was taken, so use `surj''` instead
theorem surj'' (z : S) : âˆƒ (r : R) (m : M), z = r â€¢ (toInvSubmonoid M S m : S) := by
  rcases IsLocalization.surj M z with âŸ¨âŸ¨r, mâŸ©, e : z * _ = algebraMap R S râŸ©
  -- âŠ¢ âˆƒ r m, z = r â€¢ â†‘(â†‘(toInvSubmonoid M S) m)
  refine' âŸ¨r, m, _âŸ©
  -- âŠ¢ z = r â€¢ â†‘(â†‘(toInvSubmonoid M S) m)
  rw [Algebra.smul_def, â† e, mul_assoc]
  -- âŠ¢ z = z * (â†‘(algebraMap R S) â†‘(r, m).snd * â†‘(â†‘(toInvSubmonoid M S) m))
  simp
  -- ðŸŽ‰ no goals
#align is_localization.surj' IsLocalization.surj''

theorem toInvSubmonoid_eq_mk' (x : M) : (toInvSubmonoid M S x : S) = mk' S 1 x := by
  rw [â† (IsLocalization.map_units S x).mul_left_inj]
  -- âŠ¢ â†‘(â†‘(toInvSubmonoid M S) x) * â†‘(algebraMap R S) â†‘x = mk' S 1 x * â†‘(algebraMap â€¦
  simp
  -- ðŸŽ‰ no goals
#align is_localization.to_inv_submonoid_eq_mk' IsLocalization.toInvSubmonoid_eq_mk'

theorem mem_invSubmonoid_iff_exists_mk' (x : S) :
    x âˆˆ invSubmonoid M S â†” âˆƒ m : M, mk' S 1 m = x := by
  simp_rw [â† toInvSubmonoid_eq_mk']
  -- âŠ¢ x âˆˆ invSubmonoid M S â†” âˆƒ m, â†‘(â†‘(toInvSubmonoid M S) m) = x
  exact âŸ¨fun h => âŸ¨_, congr_arg Subtype.val (toInvSubmonoid_surjective M S âŸ¨x, hâŸ©).choose_specâŸ©,
    fun h => h.choose_spec â–¸ (toInvSubmonoid M S h.choose).propâŸ©
#align is_localization.mem_inv_submonoid_iff_exists_mk' IsLocalization.mem_invSubmonoid_iff_exists_mk'

variable (S)

theorem span_invSubmonoid : Submodule.span R (invSubmonoid M S : Set S) = âŠ¤ := by
  rw [eq_top_iff]
  -- âŠ¢ âŠ¤ â‰¤ Submodule.span R â†‘(invSubmonoid M S)
  rintro x -
  -- âŠ¢ x âˆˆ Submodule.span R â†‘(invSubmonoid M S)
  rcases IsLocalization.surj'' M x with âŸ¨r, m, rflâŸ©
  -- âŠ¢ r â€¢ â†‘(â†‘(toInvSubmonoid M S) m) âˆˆ Submodule.span R â†‘(invSubmonoid M S)
  exact Submodule.smul_mem _ _ (Submodule.subset_span (toInvSubmonoid M S m).prop)
  -- ðŸŽ‰ no goals
#align is_localization.span_inv_submonoid IsLocalization.span_invSubmonoid

theorem finiteType_of_monoid_fg [Monoid.FG M] : Algebra.FiniteType R S := by
  have := Monoid.fg_of_surjective _ (toInvSubmonoid_surjective M S)
  -- âŠ¢ Algebra.FiniteType R S
  rw [Monoid.fg_iff_submonoid_fg] at this
  -- âŠ¢ Algebra.FiniteType R S
  rcases this with âŸ¨s, hsâŸ©
  -- âŠ¢ Algebra.FiniteType R S
  refine' âŸ¨âŸ¨s, _âŸ©âŸ©
  -- âŠ¢ Algebra.adjoin R â†‘s = âŠ¤
  rw [eq_top_iff]
  -- âŠ¢ âŠ¤ â‰¤ Algebra.adjoin R â†‘s
  rintro x -
  -- âŠ¢ x âˆˆ Algebra.adjoin R â†‘s
  change x âˆˆ (Subalgebra.toSubmodule (Algebra.adjoin R _ : Subalgebra R S) : Set S)
  -- âŠ¢ x âˆˆ â†‘(â†‘Subalgebra.toSubmodule (Algebra.adjoin R â†‘s))
  rw [Algebra.adjoin_eq_span, hs, span_invSubmonoid]
  -- âŠ¢ x âˆˆ â†‘âŠ¤
  trivial
  -- ðŸŽ‰ no goals
#align is_localization.finite_type_of_monoid_fg IsLocalization.finiteType_of_monoid_fg

end InvSubmonoid

end IsLocalization
