/-
Copyright (c) 2025 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
module

public import Mathlib.NumberTheory.RamificationInertia.Basic

/-!
# Primes in an extension of localization at prime

Let `R ‚äÜ S` be an extension of Dedekind domains and `p` be a prime ideal of `R`. Let `R‚Çö` be the
localization of `R` at the complement of `p` and `S‚Çö` the localization of `S` at the (image)
of the complement of `p`.

In this file, we study the relation between the (nonzero) prime ideals of `S‚Çö` and the prime
ideals of `S` above `p`. In particular, we prove that (under suitable conditions) they are in
bijection and that the residual degree and ramification index are preserved by this bijection.

## Main definitions and results

- `IsLocalization.AtPrime.mem_primesOver_of_isPrime`: The nonzero prime ideals of `S‚Çö` are
  primes over the maximal ideal of `R‚Çö`.

- `IsLocalization.AtPrime.equivQuotientMapOfIsMaximal`: `S ‚ß∏ P ‚âÉ+* S‚Çö ‚ß∏ P¬∑S‚Çö` where
  `P` is a maximal ideal of `S` above `p`.

- `IsDedekindDomain.primesOverEquivPrimesOver`: the bijection between the primes over
  `p` in `S` and the primes over the maximal ideal of `R‚Çö` in `S‚Çö`.

- `IsDedekindDomain.primesOverEquivPrimesOver_inertiagDeg_eq`: the bijection
  `primesOverEquivPrimesOver` preserves the inertia degree.

- `IsDedekindDomain.primesOverEquivPrimesOver_ramificationIdx_eq`: the bijection
  `primesOverEquivPrimesOver` preserves the ramification index.

-/

@[expose] public section

open Algebra Module IsLocalRing Ideal Localization.AtPrime

variable {R S : Type*} [CommRing R] [CommRing S] [Algebra R S] (p : Ideal R) [p.IsPrime]
  (R‚Çö : Type*) [CommRing R‚Çö] [Algebra R R‚Çö] [IsLocalization.AtPrime R‚Çö p] [IsLocalRing R‚Çö]
  (S‚Çö : Type*) [CommRing S‚Çö] [Algebra S S‚Çö] [IsLocalization (algebraMapSubmonoid S p.primeCompl) S‚Çö]
  [Algebra R‚Çö S‚Çö] (P : Ideal S) [hPp : P.LiesOver p]

namespace IsLocalization.AtPrime

/--
The nonzero prime ideals of `S‚Çö` are prime ideals over the maximal ideal of `R‚Çö`.
See `Localization.AtPrime.primesOverEquivPrimesOver` for the bijection between the prime ideals
of `S‚Çö` over the maximal ideal of `R‚Çö` and the primes ideals of `S` above `p`.
-/
theorem mem_primesOver_of_isPrime {Q : Ideal S‚Çö} [Q.IsMaximal] [Algebra.IsIntegral R‚Çö S‚Çö] :
    Q ‚àà (maximalIdeal R‚Çö).primesOver S‚Çö := by
  refine ‚ü®inferInstance, ?_‚ü©
  rw [liesOver_iff, ‚Üê eq_maximalIdeal]
  exact IsMaximal.under R‚Çö Q

theorem liesOver_comap_of_liesOver {T : Type*} [CommRing T] [Algebra R T] [Algebra R‚Çö T]
    [Algebra S T] [IsScalarTower R S T] [IsScalarTower R R‚Çö T] (Q : Ideal T)
    [Q.LiesOver (maximalIdeal R‚Çö)] : (comap (algebraMap S T) Q).LiesOver p := by
  have : Q.LiesOver p := by
    have : (maximalIdeal R‚Çö).LiesOver p := liesOver_maximalIdeal R‚Çö p _
    exact LiesOver.trans Q (IsLocalRing.maximalIdeal R‚Çö) p
  exact comap_liesOver Q p <| IsScalarTower.toAlgHom R S T

include p in
theorem liesOver_map_of_liesOver [Algebra R S‚Çö] [IsScalarTower R S S‚Çö] [IsScalarTower R R‚Çö S‚Çö]
    [P.IsPrime] :
    (P.map (algebraMap S S‚Çö)).LiesOver (IsLocalRing.maximalIdeal R‚Çö) := by
  rw [liesOver_iff, eq_comm, ‚Üê map_eq_maximalIdeal p, over_def P p]
  exact under_map_eq_map_under _
    (over_def P p ‚ñ∏ map_eq_maximalIdeal p R‚Çö ‚ñ∏ maximalIdeal.isMaximal R‚Çö)
    (isPrime_map_of_liesOver S p S‚Çö P).ne_top

attribute [local instance] Ideal.Quotient.field

include p in
theorem exists_algebraMap_quot_eq_of_mem_quot [P.IsMaximal]
    (x : S‚Çö ‚ß∏ P.map (algebraMap S S‚Çö)) :
    ‚àÉ a, (algebraMap S (S‚Çö ‚ß∏ P.map (algebraMap S S‚Çö))) a = x := by
  obtain ‚ü®x, rfl‚ü© := Ideal.Quotient.mk_surjective x
  obtain ‚ü®x, s, rfl‚ü© := IsLocalization.exists_mk'_eq (algebraMapSubmonoid S p.primeCompl) x
  obtain ‚ü®s', hs‚ü© := Ideal.Quotient.mk_surjective (I := P) (Ideal.Quotient.mk P s)‚Åª¬π
  simp only [IsScalarTower.algebraMap_eq S S‚Çö (S‚Çö ‚ß∏ _), Quotient.algebraMap_eq, RingHom.comp_apply]
  use x * s'
  rw [‚Üê sub_eq_zero, ‚Üê map_sub, Quotient.eq_zero_iff_mem]
  have h‚ÇÄ : (P.map (algebraMap S S‚Çö)).IsPrime := isPrime_map_of_liesOver S p S‚Çö P
  have h‚ÇÅ : s.1 ‚àâ P := (Set.disjoint_left.mp <| disjoint_primeCompl_of_liesOver P p) s.prop
  have h‚ÇÇ : algebraMap S S‚Çö s ‚àâ Ideal.map (algebraMap S S‚Çö) P := by
    rwa [‚Üê mem_comap, comap_map_eq_self_of_isMaximal _ h‚ÇÄ.ne_top]
  refine (h‚ÇÄ.mem_or_mem ?_).resolve_left h‚ÇÇ
  rw [mul_sub, mul_mk'_eq_mk'_of_mul, mk'_mul_cancel_left, ‚Üê map_mul, ‚Üê map_sub, ‚Üê mem_comap,
    comap_map_eq_self_of_isMaximal _ IsPrime.ne_top', ‚Üê Ideal.Quotient.eq, map_mul, map_mul, hs,
    mul_comm, inv_mul_cancel_right‚ÇÄ (Quotient.eq_zero_iff_mem.not.mpr h‚ÇÅ)]

/--
The isomorphism `S ‚ß∏ P ‚âÉ+* S‚Çö ‚ß∏ P¬∑S‚Çö`, where `S‚Çö` is the localization of `S` at the (image) of
the complement of `p` and `P` is a maximal ideal of `S` above `p`.
Note that this isomorphism makes the obvious diagram involving `R ‚ß∏ p ‚âÉ+* R‚Çö ‚ß∏ maximalIdeal R‚Çö`
commute, see `IsLocalization.AtPrime.algebraMap_equivQuotMaximalIdeal_symm_apply`.
-/
noncomputable def equivQuotientMapOfIsMaximal [p.IsPrime] [P.IsMaximal] :
    S ‚ß∏ P ‚âÉ+* S‚Çö ‚ß∏ P.map (algebraMap S S‚Çö) :=
  (Ideal.quotEquivOfEq (by
    rw [IsScalarTower.algebraMap_eq S S‚Çö (S‚Çö ‚ß∏ _), ‚Üê RingHom.comap_ker, Quotient.algebraMap_eq,
      mk_ker,
        comap_map_eq_self_of_isMaximal _ (isPrime_map_of_liesOver S p S‚Çö P).ne_top])).trans
    (RingHom.quotientKerEquivOfSurjective (f := algebraMap S (S‚Çö ‚ß∏ _))
      fun x ‚Ü¶ exists_algebraMap_quot_eq_of_mem_quot p S‚Çö P x)

@[simp]
theorem equivQuotientMapOfIsMaximal_apply_mk [P.IsMaximal] (x : S) :
    equivQuotientMapOfIsMaximal p S‚Çö P (Ideal.Quotient.mk _ x) =
      (Ideal.Quotient.mk _ (algebraMap S S‚Çö x)) := rfl

@[simp]
theorem equivQuotientMapOfIsMaximal_symm_apply_mk [P.IsMaximal] (x : S)
    (s : algebraMapSubmonoid S p.primeCompl) :
    (equivQuotientMapOfIsMaximal p S‚Çö P).symm (Ideal.Quotient.mk _ (mk' _ x s)) =
        (Ideal.Quotient.mk _ x) * (Ideal.Quotient.mk _ s.val)‚Åª¬π := by
  have : (Ideal.map (algebraMap S S‚Çö) P).IsPrime := isPrime_map_of_liesOver S p S‚Çö P
  have h‚ÇÅ : Ideal.Quotient.mk P ‚Üës ‚â† 0 :=
    Quotient.eq_zero_iff_mem.not.mpr <|
      (Set.disjoint_left.mp <| disjoint_primeCompl_of_liesOver P p) s.prop
  have h‚ÇÇ : equivQuotientMapOfIsMaximal p S‚Çö P (Ideal.Quotient.mk P ‚Üës) ‚â† 0 := by
    rwa [RingEquiv.map_ne_zero_iff]
  rw [RingEquiv.symm_apply_eq, ‚Üê mul_left_inj' h‚ÇÇ, map_mul, mul_assoc, ‚Üê map_mul,
    inv_mul_cancel‚ÇÄ h‚ÇÅ, map_one, mul_one, equivQuotientMapOfIsMaximal_apply_mk,
    ‚Üê map_mul, mk'_spec, Quotient.mk_algebraMap, equivQuotientMapOfIsMaximal_apply_mk,
    Quotient.mk_algebraMap]

variable [Algebra R S‚Çö] [IsScalarTower R S S‚Çö] [IsScalarTower R R‚Çö S‚Çö]

/--
The following diagram where the vertical maps are the algebra maps and the horizontal maps are
`Localization.AtPrime.equivQuotMaximalIdeal.symm` and
`Localization.AtPrime.equivQuotientMapOfIsMaximal.symm` commutes:
```
R‚Çö ‚ß∏ ùìÇ ‚îÄ‚îÄ‚ñ∂ R ‚ß∏ p
  ‚îÇ         ‚îÇ
S‚Çö ‚ß∏ ùí´ ‚îÄ‚îÄ‚ñ∂ S ‚ß∏ P
```
Here, `ùìÇ` denotes the maximal ideal of `R‚Çö` and `ùí´` the image of `P` in `S‚Çö`.
Note that result is stated in that direction since this is the formulation needed for the proof
of `Localization.AtPrime.inertiaDeg_map_eq_inertiaDeg`.
-/
theorem algebraMap_equivQuotMaximalIdeal_symm_apply [p.IsMaximal] [P.IsMaximal]
    [(P.map (algebraMap S S‚Çö)).LiesOver (maximalIdeal R‚Çö)] (x : R‚Çö ‚ß∏ maximalIdeal R‚Çö) :
    algebraMap (R ‚ß∏ p) (S ‚ß∏ P) ((equivQuotMaximalIdeal p R‚Çö).symm x) =
    (equivQuotientMapOfIsMaximal p S‚Çö P).symm
      (algebraMap (R‚Çö ‚ß∏ maximalIdeal R‚Çö) (S‚Çö ‚ß∏ P.map (algebraMap S S‚Çö)) x) := by
  obtain ‚ü®x, rfl‚ü© := Ideal.Quotient.mk_surjective x
  obtain ‚ü®x, s, rfl‚ü© := mk'_surjective p.primeCompl x
  simp [equivQuotMaximalIdeal_symm_apply_mk, map_mul, Quotient.algebraMap_mk_of_liesOver,
    IsLocalization.algebraMap_mk' S R‚Çö S‚Çö]

-- Lean thinks that the instance [p.IsPrime] is not necessary here, but it is needed
-- for the definition of `R‚Çö`.
set_option linter.unusedSectionVars false in
@[simp]
theorem equivQuotientMapMaximalIdeal_apply_mk [p.IsMaximal] (x : S) :
    equivQuotientMapMaximalIdeal S p R‚Çö S‚Çö (Ideal.Quotient.mk _ x) =
      (Ideal.Quotient.mk _ (algebraMap S S‚Çö x)) := rfl

theorem inertiaDeg_map_eq_inertiaDeg [p.IsMaximal] [P.IsMaximal]
    [(Ideal.map (algebraMap S S‚Çö) P).LiesOver (maximalIdeal R‚Çö)] :
    (maximalIdeal R‚Çö).inertiaDeg (P.map (algebraMap S S‚Çö)) = p.inertiaDeg P := by
  rw [inertiaDeg_algebraMap, inertiaDeg_algebraMap]
  refine Algebra.finrank_eq_of_equiv_equiv (equivQuotMaximalIdeal p R‚Çö).symm
    (equivQuotientMapOfIsMaximal p S‚Çö P).symm ?_
  ext x
  exact algebraMap_equivQuotMaximalIdeal_symm_apply p R‚Çö S‚Çö P x

theorem ramificationIdx_map_eq_ramificationIdx [IsDomain R] [IsTorsionFree R S] [IsTorsionFree R R‚Çö]
    [IsTorsionFree R S‚Çö] [IsTorsionFree S S‚Çö] [IsTorsionFree R‚Çö S‚Çö]
    [IsDedekindDomain S] [IsDedekindDomain R‚Çö] [IsDedekindDomain S‚Çö] (hp : p ‚â† ‚ä•) [P.IsPrime] :
    (maximalIdeal R‚Çö).ramificationIdx (algebraMap R‚Çö S‚Çö) (P.map (algebraMap S S‚Çö)) =
      p.ramificationIdx (algebraMap R S) P := by
  have h‚ÇÅ : maximalIdeal R‚Çö ‚â† ‚ä• := by
    rw [‚Üê map_eq_maximalIdeal p]
    exact map_ne_bot_of_ne_bot hp
  have : (P.map (algebraMap S S‚Çö)).IsPrime := isPrime_map_of_liesOver S p S‚Çö P
  by_cases hP : P = ‚ä•
  ¬∑ simp_rw [hP, Ideal.map_bot, ramificationIdx_bot' hp
      (FaithfulSMul.algebraMap_injective _ _),
      ramificationIdx_bot' h‚ÇÅ (FaithfulSMul.algebraMap_injective R‚Çö S‚Çö)]
  have h‚ÇÇ : Ideal.map (algebraMap R‚Çö S‚Çö) (maximalIdeal R‚Çö) ‚â§ P.map (algebraMap S S‚Çö) := by
    rw [map_le_iff_le_comap]
    exact le_of_eq <| (liesOver_iff _ _).mp <| liesOver_map_of_liesOver p R‚Çö S‚Çö P
  have h_main := (ramificationIdx_algebra_tower
      (map_ne_bot_of_ne_bot h‚ÇÅ) (map_ne_bot_of_ne_bot hp) h‚ÇÇ).symm.trans
      (ramificationIdx_algebra_tower (map_ne_bot_of_ne_bot hP)
      (map_ne_bot_of_ne_bot hp) le_rfl)
  rwa [ramificationIdx_map_self_eq_one IsPrime.ne_top' (map_ne_bot_of_ne_bot hP), mul_one,
    ‚Üê map_eq_maximalIdeal p, ramificationIdx_map_self_eq_one _ (map_ne_bot_of_ne_bot hp), one_mul,
    map_eq_maximalIdeal p] at h_main
  rw [map_eq_maximalIdeal]
  exact IsPrime.ne_top'

end IsLocalization.AtPrime

namespace IsDedekindDomain

open IsLocalization AtPrime

variable [IsDomain R] [IsDedekindDomain S] [IsTorsionFree R S] [Algebra R S‚Çö] [IsScalarTower R S S‚Çö]
  [IsScalarTower R R‚Çö S‚Çö]

/--
For `R ‚äÜ S` an extension of Dedekind domains and `p` a prime ideal of `R`, the bijection
between the primes of `S` over `p` and the primes over the maximal ideal of `R‚Çö` in `S‚Çö` where
`R‚Çö` and `S‚Çö` are resp. the localizations of `R` and `S` at the complement of `p`.
-/
noncomputable def primesOverEquivPrimesOver (hp : p ‚â† ‚ä•) :
    p.primesOver S ‚âÉo (maximalIdeal R‚Çö).primesOver S‚Çö where
  toFun P := ‚ü®map (algebraMap S S‚Çö) P.1, isPrime_map_of_liesOver S p S‚Çö P.1,
    liesOver_map_of_liesOver p R‚Çö S‚Çö P.1‚ü©
  map_rel_iff' {Q Q'} := by
    refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ map_mono h‚ü©
    have : Q'.1.IsMaximal :=
      (primesOver.isPrime p Q').isMaximal (ne_bot_of_mem_primesOver hp Q'.prop)
    simpa [comap_map_of_isMaximal S p] using le_comap_of_map_le h
  invFun Q := ‚ü®comap (algebraMap S S‚Çö) Q.1, IsPrime.under S Q.1,
    liesOver_comap_of_liesOver p R‚Çö Q.1‚ü©
  left_inv P := by
    have : P.val.IsMaximal := Ring.DimensionLEOne.maximalOfPrime
        (ne_bot_of_mem_primesOver hp P.prop) (primesOver.isPrime p P)
    exact SetCoe.ext <| IsLocalization.AtPrime.comap_map_of_isMaximal S p S‚Çö P.1
  right_inv Q := SetCoe.ext <| map_comap (algebraMapSubmonoid S p.primeCompl) S‚Çö Q

@[simp]
theorem primesOverEquivPrimesOver_apply (hp : p ‚â† ‚ä•) (P : p.primesOver S) :
    primesOverEquivPrimesOver p R‚Çö S‚Çö hp P = Ideal.map (algebraMap S S‚Çö) P := rfl

@[simp]
theorem primesOverEquivPrimesOver_symm_apply (hp : p ‚â† ‚ä•) (Q : (maximalIdeal R‚Çö).primesOver S‚Çö) :
    ((primesOverEquivPrimesOver p R‚Çö S‚Çö hp).symm Q).1 = Ideal.comap (algebraMap S S‚Çö) Q := rfl

theorem primesOverEquivPrimesOver_inertiagDeg_eq [p.IsMaximal] (hp : p ‚â† ‚ä•) (P : p.primesOver S) :
    (maximalIdeal R‚Çö).inertiaDeg (primesOverEquivPrimesOver p R‚Çö S‚Çö hp P : Ideal S‚Çö) =
      p.inertiaDeg P.val := by
  have : NeZero p := ‚ü®hp‚ü©
  have : P.val.IsMaximal := Ring.DimensionLEOne.maximalOfPrime
        (ne_bot_of_mem_primesOver (NeZero.ne _) P.prop) inferInstance
  have : (P.1.map (algebraMap S S‚Çö)).LiesOver (maximalIdeal R‚Çö) := liesOver_map_of_liesOver p _ _ _
  exact inertiaDeg_map_eq_inertiaDeg p _ _ _

theorem primesOverEquivPrimesOver_ramificationIdx_eq (hp : p ‚â† ‚ä•) [NoZeroSMulDivisors R R‚Çö]
    [NoZeroSMulDivisors R S‚Çö] [NoZeroSMulDivisors S S‚Çö] [NoZeroSMulDivisors R‚Çö S‚Çö]
    [IsDedekindDomain R‚Çö] [IsDedekindDomain S‚Çö] (P : p.primesOver S) :
    (maximalIdeal R‚Çö).ramificationIdx (algebraMap R‚Çö S‚Çö)
      (primesOverEquivPrimesOver p R‚Çö S‚Çö hp P : Ideal S‚Çö) =
        p.ramificationIdx (algebraMap R S) P.val :=
  ramificationIdx_map_eq_ramificationIdx p _ _ _ hp

end IsDedekindDomain
