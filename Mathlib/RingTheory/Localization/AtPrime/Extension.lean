/-
Copyright (c) 2025 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang, Riccardo Brasca, Xavier Roblot
-/
import Mathlib.NumberTheory.RamificationInertia.Basic
import Mathlib.RingTheory.DedekindDomain.Instances
import Mathlib.RingTheory.Localization.AtPrime.Basic

/-!
# Primes in an extension of localization at prime

Let `R ‚äÜ S` be an extension of rings and `p` be a prime ideal of `R`. Denote by `R‚Çö` the
localization of `R` at the complement of `p` and by `S‚Çö` the localization of `S` at the (image)
of the complement of `p`.

In this file, we study the relation between the (nonzero) prime ideals of `S‚Çö` and the prime
ideals of `S` above `p`. In particular, we prove that (under suitable conditions) they are in
bijection and that the residual degree and ramification index are preserved by this bijection.

# Main definitions and results

- `Localization.AtPrime.mem_primesOver_of_isPrime`: The nonzero prime ideals of `S‚Çö` are
  primes over the maximal ideal of `R‚Çö`.

- `Localization.AtPrime.quotMapEquivQuotMapOfIsMaximal`: `S ‚ß∏ P ‚âÉ+* S‚Çö ‚ß∏ P¬∑S‚Çö` where
  `P` is a maximal ideal of `S` above `p`.

- `Localization.AtPrime.quotMapEquivQuotMapMaximalIdeal`: `S ‚ß∏ pS ‚âÉ+* S‚Çö ‚ß∏ p¬∑S‚Çö`.

- `Localization.AtPrime.algebraMap_equivQuotMaximalIdeal_symm_apply_eq`: the map
  `equivQuotMaximalIdeal` and `quotMapEquivQuotMapOfIsMaximal` satisfy the obvious
  commutative diagram.

- `Localization.AtPrime.primesOverEquivPrimesOver`: the bijection between the primes over
  `p` in `S` and the primes over the maximal ideal of `R‚Çö` in `S‚Çö`.

- `Localization.AtPrime.primesOverEquivPrimesOver_inertiagDeg_eq`: the bijection
  `primesOverEquivPrimesOver` preserves the inertia degree.

- `Localization.AtPrime.primesOverEquivPrimesOver_ramificationIdx_eq`: the bijection
  `primesOverEquivPrimesOver` preserves the ramification index.

-/

namespace Localization.AtPrime

variable {R S : Type*} [CommRing R] [CommRing S] [Algebra R S]
variable (P : Ideal S) (p : Ideal R) [hPp : P.LiesOver p]

open Ideal Algebra IsLocalRing IsLocalization AtPrime

local notation3 "R‚Çö" => Localization.AtPrime p
local notation3 "S‚Çö" => Localization  (algebraMapSubmonoid S p.primeCompl)

section misc

theorem isPrime_map_of_liesOver [p.IsPrime] [P.IsPrime] :
    (P.map (algebraMap S S‚Çö)).IsPrime :=
  isPrime_of_isPrime_disjoint _ _ _ inferInstance (Ideal.disjoint_primeCompl_of_liesOver P p)

-- instance [p.IsPrime] [P.IsPrime] : (P.map (algebraMap S S‚Çö)).IsPrime :=
--   isPrime_of_isPrime_disjoint (algebraMapSubmonoid S p.primeCompl) _ _ inferInstance
--     (disjoint_primeCompl_of_liesOver P p)

-- variable (S) in
-- theorem algebraMapSubmonoid_primeCompl_le_nonZeroDivisors [IsDomain R] [IsDomain S]
--     [FaithfulSMul R S] [p.IsPrime] :
--     algebraMapSubmonoid S p.primeCompl ‚â§ nonZeroDivisors S := by
--   apply algebraMapSubmonoid_le_nonZeroDivisors_of_faithfulSMul
--   exact fun _ h ‚Ü¶  mem_nonZeroDivisors_of_ne_zero <| ne_of_mem_of_not_mem h <| by simp

-- theorem noZeroSMulDivisors_localization [IsDomain R] [IsDomain S] [FaithfulSMul R S] [p.IsPrime]
--     [IsLocalization (algebraMapSubmonoid S p.primeCompl) S‚Çö] :
--     NoZeroSMulDivisors S S‚Çö := by
--   have : IsDomain S‚Çö :=
--     isDomain_of_le_nonZeroDivisors _ (algebraMapSubmonoid_primeCompl_le_nonZeroDivisors S p)
--   rw [NoZeroSMulDivisors.iff_algebraMap_injective,
--     injective_iff_isRegular (algebraMapSubmonoid S p.primeCompl)]
--   exact fun ‚ü®x, hx‚ü© ‚Ü¶ isRegular_iff_ne_zero'.mpr <|
--     ne_of_mem_of_not_mem hx <| by simp [Algebra.algebraMapSubmonoid]

-- theorem noZeroSMulDivisors_localization_localization [IsDomain R] [IsDomain S] [FaithfulSMul R S]
--     [p.IsPrime] [IsLocalization.AtPrime R‚Çö p]
--     [IsLocalization (algebraMapSubmonoid S p.primeCompl) S‚Çö] :
--     NoZeroSMulDivisors R‚Çö S‚Çö :=
--   NoZeroSMulDivisors_of_isLocalization R S R‚Çö S‚Çö (primeCompl_le_nonZeroDivisors p)

-- variable {S}

-- See if some of these results are in GoingUp
-- lemma map_eq_maximalIdeal [p.IsPrime] [IsLocalization.AtPrime R‚Çö p] :
--     p.map (algebraMap R R‚Çö) = maximalIdeal R‚Çö := by
--   convert congr_arg (Ideal.map (algebraMap R R‚Çö)) (comap_maximalIdeal R‚Çö p).symm
--   rw [map_comap p.primeCompl]

lemma map_isMaximal [p.IsPrime] :
    (p.map (algebraMap R R‚Çö)).IsMaximal := by
  rw [map_eq_maximalIdeal]
  exact maximalIdeal.isMaximal R‚Çö

theorem liesOver_map_of_liesOver [p.IsPrime] [P.IsPrime] :
    (P.map (algebraMap S S‚Çö)).LiesOver (IsLocalRing.maximalIdeal R‚Çö) := by
  rw [liesOver_iff, eq_comm, ‚Üê map_eq_maximalIdeal]
  exact under_map _ _ ((liesOver_iff _ _).mp hPp) (map_isMaximal p)
    (isPrime_map_of_liesOver P p).ne_top

/--
The nonzero prime ideals of `S‚Çö` are primes over the maximal ideal of `R‚Çö`.
See `Localization.AtPrime.primesOverEquivPrimesOver` for the bijection between the prime ideals
of `S‚Çö` over the maximal ideal of `R‚Çö` and the primes ideals of `S` above `p`.
-/
theorem mem_primesOver_of_isPrime [p.IsPrime] {Q : Ideal S‚Çö} [Q.IsPrime] [IsDomain R]
    [Algebra.IsIntegral R S] [IsDedekindDomain S] [FaithfulSMul R S] (hQ : Q ‚â† ‚ä•) :
    Q ‚àà (maximalIdeal R‚Çö).primesOver S‚Çö := by
  refine ‚ü®inferInstance, ?_‚ü©
  rw [liesOver_iff, ‚Üê eq_maximalIdeal]
  have : Q.IsMaximal := Ring.DimensionLEOne.maximalOfPrime hQ inferInstance
  exact IsMaximal.under R‚Çö Q

end misc

section isomorphisms

attribute [local instance] Ideal.Quotient.field

theorem exists_algebraMap_quot_eq_of_mem_quot [p.IsPrime] [P.IsMaximal]
    (x : S‚Çö ‚ß∏ P.map (algebraMap S S‚Çö)) :
    ‚àÉ a, (algebraMap S (S‚Çö ‚ß∏ P.map (algebraMap S S‚Çö))) a = x := by
  obtain ‚ü®x, rfl‚ü© := Ideal.Quotient.mk_surjective x
  obtain ‚ü®x, s, rfl‚ü© := mk'_surjective (algebraMapSubmonoid S p.primeCompl) x
  obtain ‚ü®s', hs‚ü© := Ideal.Quotient.mk_surjective (I := P) (Ideal.Quotient.mk P s)‚Åª¬π
  simp only [IsScalarTower.algebraMap_eq S S‚Çö (S‚Çö ‚ß∏ _), Quotient.algebraMap_eq, RingHom.comp_apply]
  use x * s'
  rw [‚Üê sub_eq_zero, ‚Üê map_sub, Quotient.eq_zero_iff_mem]
  have h‚ÇÄ : (P.map (algebraMap S S‚Çö)).IsPrime := isPrime_map_of_liesOver P p
  have h‚ÇÅ : s.1 ‚àâ P := (Set.disjoint_left.mp <| disjoint_primeCompl_of_liesOver P p) s.prop
  have h‚ÇÇ : algebraMap S S‚Çö s ‚àâ Ideal.map (algebraMap S S‚Çö) P := by
    rwa [‚Üê mem_comap, comap_map_eq_self_of_isMaximal _ h‚ÇÄ.ne_top]
  refine (h‚ÇÄ.mem_or_mem ?_).resolve_left h‚ÇÇ
  rw [mul_sub, mul_mk'_eq_mk'_of_mul, mk'_mul_cancel_left, ‚Üê map_mul, ‚Üê map_sub, ‚Üê mem_comap,
    comap_map_eq_self_of_isMaximal _ IsPrime.ne_top', ‚Üê Ideal.Quotient.eq, map_mul, map_mul, hs,
    mul_comm, inv_mul_cancel_right‚ÇÄ (Quotient.eq_zero_iff_mem.not.mpr h‚ÇÅ)]

/--
The isomorphism `S ‚ß∏ P ‚âÉ+* S‚Çö ‚ß∏ P¬∑S‚Çö`, where `S‚Çö` is the localization of `S` at the (image) of
the complement of `p` and `P` is a maximal ideal of `S` above `p`.
Note that this isomorphism makes the obvious diagram involving `R ‚ß∏ p ‚âÉ+* R‚Çö ‚ß∏ maximalIdeal R‚Çö`
commute, see `IsLocalization.AtPrime.algebraMap_equivQuotMaximalIdeal_symm_apply_eq`.
-/
noncomputable def quotMapEquivQuotMapOfIsMaximal [p.IsPrime] [P.IsMaximal] :
    S ‚ß∏ P ‚âÉ+* S‚Çö ‚ß∏ P.map (algebraMap S S‚Çö) :=
  (Ideal.quotEquivOfEq (by
    rw [IsScalarTower.algebraMap_eq S S‚Çö (S‚Çö ‚ß∏ _), ‚Üê RingHom.comap_ker, Quotient.algebraMap_eq,
      mk_ker,
        comap_map_eq_self_of_isMaximal _ (isPrime_map_of_liesOver P p).ne_top])).trans
    (RingHom.quotientKerEquivOfSurjective (f := algebraMap S (S‚Çö ‚ß∏ _))
      fun x ‚Ü¶ exists_algebraMap_quot_eq_of_mem_quot P p x)

@[simp]
theorem quotMapEquivQuotMapOfIsMaximal_apply_mk [p.IsPrime] [P.IsMaximal] (x : S) :
    quotMapEquivQuotMapOfIsMaximal P p (Ideal.Quotient.mk _ x) =
      (Ideal.Quotient.mk _ (algebraMap S S‚Çö x)) := rfl

@[simp]
theorem quotMapEquivQuotMapOfIsMaximal_symm_apply_mk [p.IsPrime] [P.IsMaximal] (x : S)
    (s : algebraMapSubmonoid S p.primeCompl) :
    (quotMapEquivQuotMapOfIsMaximal P p).symm (Ideal.Quotient.mk _ (mk' _ x s)) =
        (Ideal.Quotient.mk _ x) * (Ideal.Quotient.mk _ s.val)‚Åª¬π := by
  have : (Ideal.map (algebraMap S S‚Çö) P).IsPrime := isPrime_map_of_liesOver P p
  have h‚ÇÅ : Ideal.Quotient.mk P ‚Üës ‚â† 0 :=
    Quotient.eq_zero_iff_mem.not.mpr <|
      (Set.disjoint_left.mp <| disjoint_primeCompl_of_liesOver P p) s.prop
  have h‚ÇÇ : quotMapEquivQuotMapOfIsMaximal P p (Ideal.Quotient.mk P ‚Üës) ‚â† 0 := by
    rwa [RingEquiv.map_ne_zero_iff]
  rw [RingEquiv.symm_apply_eq, ‚Üê mul_left_inj' h‚ÇÇ, map_mul, mul_assoc, ‚Üê map_mul,
    inv_mul_cancel‚ÇÄ h‚ÇÅ, map_one, mul_one, quotMapEquivQuotMapOfIsMaximal_apply_mk,
    ‚Üê map_mul, mk'_spec, Quotient.mk_algebraMap, quotMapEquivQuotMapOfIsMaximal_apply_mk,
    Quotient.mk_algebraMap]

/--
The following diagram where the vertical maps are the algebra maps and the horizontal maps are
`Localization.AtPrime.equivQuotMaximalIdeal.symm` and
`Localization.AtPrime.quotMapEquivQuotMapOfIsMaximal.symm` commutes:
```
R‚Çö ‚ß∏ ùìÇ ‚îÄ‚îÄ‚ñ∂ R ‚ß∏ p
  ‚îÇ         ‚îÇ
S‚Çö ‚ß∏ ùí´ ‚îÄ‚îÄ‚ñ∂ S ‚ß∏ P
```
Here, `ùìÇ` denotes the maximal ideal of `R‚Çö` and `ùí´` the image of `P` in `S‚Çö`.
Note that result is stated in that direction since this is the formulation needed for the proof
of `Localization.AtPrime.inertiaDeg_map_eq_inertiaDeg`.
-/
theorem algebraMap_equivQuotMaximalIdeal_symm_apply_eq [p.IsMaximal] [P.IsMaximal]
    [(P.map (algebraMap S S‚Çö)).LiesOver (maximalIdeal R‚Çö)] (x : R‚Çö ‚ß∏ maximalIdeal R‚Çö) :
    algebraMap (R ‚ß∏ p) (S ‚ß∏ P) ((equivQuotMaximalIdeal p R‚Çö).symm x) =
    (quotMapEquivQuotMapOfIsMaximal P p).symm
      (algebraMap (R‚Çö ‚ß∏ maximalIdeal R‚Çö) (S‚Çö ‚ß∏ P.map (algebraMap S S‚Çö)) x) := by
  obtain ‚ü®x, rfl‚ü© := Ideal.Quotient.mk_surjective x
  obtain ‚ü®x, s, rfl‚ü© := mk'_surjective p.primeCompl x
  simp [equivQuotMaximalIdeal_symm_apply_mk, map_mul,
    Quotient.algebraMap_mk_of_liesOver, Quotient.mk_algebraMap, map_inv‚ÇÄ,
    IsLocalization.algebraMap_mk' S, quotMapEquivQuotMapOfIsMaximal_symm_apply_mk]

local notation "pS" => Ideal.map (algebraMap R S) p
local notation "pS‚Çö" => Ideal.map (algebraMap R‚Çö S‚Çö) (maximalIdeal R‚Çö)

lemma comap_map_eq_map [p.IsMaximal] :
    (Ideal.map (algebraMap R S‚Çö) p).comap (algebraMap S S‚Çö) = pS := by
  rw [IsScalarTower.algebraMap_eq R S S‚Çö, ‚Üê Ideal.map_map, eq_comm]
  apply Ideal.le_comap_map.antisymm
  intro x hx
  obtain ‚ü®Œ±, hŒ±, hŒ±x‚ü© : ‚àÉ Œ± ‚àâ p, Œ± ‚Ä¢ x ‚àà pS := by
    have ‚ü®‚ü®y, s‚ü©, hy‚ü© := (IsLocalization.mem_map_algebraMap_iff
      (Algebra.algebraMapSubmonoid S p.primeCompl) S‚Çö).mp hx
    rw [‚Üê map_mul,
      IsLocalization.eq_iff_exists (Algebra.algebraMapSubmonoid S p.primeCompl)] at hy
    obtain ‚ü®c, hc‚ü© := hy
    obtain ‚ü®Œ±, hŒ±, e‚ü© := (c * s).prop
    refine ‚ü®Œ±, hŒ±, ?_‚ü©
    rw [Algebra.smul_def, e, Submonoid.coe_mul, mul_assoc, mul_comm _ x, hc]
    exact Ideal.mul_mem_left _ _ y.prop
  obtain ‚ü®Œ≤, Œ≥, hŒ≥, hŒ≤‚ü© : ‚àÉ Œ≤ Œ≥, Œ≥ ‚àà p ‚àß Œ≤ * Œ± = 1 + Œ≥ := by
    obtain ‚ü®Œ≤, hŒ≤‚ü© := Ideal.Quotient.mk_surjective (I := p) (Ideal.Quotient.mk p Œ±)‚Åª¬π
    refine ‚ü®Œ≤, Œ≤ * Œ± - 1, ?_, ?_‚ü©
    ¬∑ rw [‚Üê Ideal.Quotient.eq_zero_iff_mem, map_sub, map_one,
        map_mul, hŒ≤, inv_mul_cancel‚ÇÄ, sub_self]
      rwa [Ne, Ideal.Quotient.eq_zero_iff_mem]
    ¬∑ rw [add_sub_cancel]
  have := Ideal.mul_mem_left _ (algebraMap _ _ Œ≤) hŒ±x
  rw [‚Üê Algebra.smul_def, smul_smul, hŒ≤, add_smul, one_smul] at this
  refine (Submodule.add_mem_iff_left _ ?_).mp this
  rw [Algebra.smul_def]
  apply Ideal.mul_mem_right
  exact Ideal.mem_map_of_mem _ hŒ≥

@[deprecated (since := "2025-07-31")] alias comap_map_eq_map_of_isLocalization_algebraMapSubmonoid
  := comap_map_eq_map

variable (S) in
/--
The isomorphism `S ‚ß∏ pS ‚âÉ+* S‚Çö ‚ß∏ p¬∑S‚Çö`, where `S‚Çö` is the localization of `S` at the (image) of
the complement of `p`
-/
noncomputable def quotMapEquivQuotMapMaximalIdeal [p.IsMaximal] :
    S ‚ß∏ pS ‚âÉ+* S‚Çö ‚ß∏ pS‚Çö := by
  haveI h : pS‚Çö = Ideal.map (algebraMap S S‚Çö) pS := by
    rw [‚Üê map_eq_maximalIdeal, Ideal.map_map,
      ‚Üê IsScalarTower.algebraMap_eq, Ideal.map_map, ‚Üê IsScalarTower.algebraMap_eq]
  refine (Ideal.quotEquivOfEq ?_).trans
    (RingHom.quotientKerEquivOfSurjective (f := algebraMap S (S‚Çö ‚ß∏ pS‚Çö)) ?_)
  ¬∑ rw [IsScalarTower.algebraMap_eq S S‚Çö, Ideal.Quotient.algebraMap_eq, ‚Üê RingHom.comap_ker,
      Ideal.mk_ker, h, Ideal.map_map, ‚Üê IsScalarTower.algebraMap_eq, comap_map_eq_map]
  ¬∑ intro x
    obtain ‚ü®x, rfl‚ü© := Ideal.Quotient.mk_surjective x
    obtain ‚ü®x, s, rfl‚ü© := IsLocalization.mk'_surjective
      (Algebra.algebraMapSubmonoid S p.primeCompl) x
    obtain ‚ü®Œ±, hŒ± : Œ± ‚àâ p, e‚ü© := s.prop
    obtain ‚ü®Œ≤, Œ≥, hŒ≥, hŒ≤‚ü© : ‚àÉ Œ≤ Œ≥, Œ≥ ‚àà p ‚àß Œ± * Œ≤ = 1 + Œ≥ := by
      obtain ‚ü®Œ≤, hŒ≤‚ü© := Ideal.Quotient.mk_surjective (I := p) (Ideal.Quotient.mk p Œ±)‚Åª¬π
      refine ‚ü®Œ≤, Œ± * Œ≤ - 1, ?_, ?_‚ü©
      ¬∑ rw [‚Üê Ideal.Quotient.eq_zero_iff_mem, map_sub, map_one,
          map_mul, hŒ≤, mul_inv_cancel‚ÇÄ, sub_self]
        rwa [Ne, Ideal.Quotient.eq_zero_iff_mem]
      ¬∑ rw [add_sub_cancel]
    use Œ≤ ‚Ä¢ x
    rw [IsScalarTower.algebraMap_eq S S‚Çö (S‚Çö ‚ß∏ pS‚Çö), Ideal.Quotient.algebraMap_eq,
      RingHom.comp_apply, ‚Üê sub_eq_zero, ‚Üê map_sub, Ideal.Quotient.eq_zero_iff_mem]
    rw [h, IsLocalization.mem_map_algebraMap_iff
      (Algebra.algebraMapSubmonoid S p.primeCompl) S‚Çö]
    refine ‚ü®‚ü®‚ü®Œ≥ ‚Ä¢ x, ?_‚ü©, s‚ü©, ?_‚ü©
    ¬∑ rw [Algebra.smul_def]
      apply Ideal.mul_mem_right
      exact Ideal.mem_map_of_mem _ hŒ≥
    simp only
    rw [mul_comm, mul_sub, IsLocalization.mul_mk'_eq_mk'_of_mul,
      IsLocalization.mk'_mul_cancel_left, ‚Üê map_mul, ‚Üê e, ‚Üê Algebra.smul_def, smul_smul,
      hŒ≤, ‚Üê map_sub, add_smul, one_smul, add_comm x, add_sub_cancel_right]

@[simp]
theorem quotMapEquivQuotMapMaximalIdeal_apply_mk [p.IsMaximal] (x : S) :
    quotMapEquivQuotMapMaximalIdeal S p (Ideal.Quotient.mk _ x) =
      (Ideal.Quotient.mk _ (algebraMap S S‚Çö x)) := rfl

@[deprecated (since := "2025-07-31")] alias quotMapEquivQuotMapMaximalIdealOfIsLocalization :=
  quotMapEquivQuotMapMaximalIdeal

end isomorphisms

noncomputable section primesOver

theorem inertiaDeg_map_eq_inertiaDeg [p.IsMaximal] [P.IsMaximal]
    [(Ideal.map (algebraMap S S‚Çö) P).LiesOver (maximalIdeal R‚Çö)] :
    (maximalIdeal R‚Çö).inertiaDeg (P.map (algebraMap S S‚Çö)) = p.inertiaDeg P := by
  rw [inertiaDeg_algebraMap, inertiaDeg_algebraMap]
  refine Algebra.finrank_eq_of_equiv_equiv (equivQuotMaximalIdeal p R‚Çö).symm
    (quotMapEquivQuotMapOfIsMaximal P p).symm ?_
  ext x
  exact algebraMap_equivQuotMaximalIdeal_symm_apply_eq P p x

theorem ramificationIdx_map_eq_ramificationIdx [Module.Finite R S] [NoZeroSMulDivisors R S]
    [IsDedekindDomain R] [IsDedekindDomain S] [p.IsPrime] [NeZero p] [P.IsPrime] :
    (maximalIdeal R‚Çö).ramificationIdx (algebraMap R‚Çö S‚Çö) (P.map (algebraMap S S‚Çö)) =
      p.ramificationIdx (algebraMap R S) P := by
  have h‚ÇÅ : maximalIdeal R‚Çö ‚â† ‚ä• := by
    rw [‚Üê map_eq_maximalIdeal]
    exact map_ne_bot_of_ne_bot (NeZero.ne p)
  have : (P.map (algebraMap S S‚Çö)).IsPrime := isPrime_map_of_liesOver P p
  by_cases hP : P = ‚ä•
  ¬∑ simp_rw [hP, Ideal.map_bot, ramificationIdx_bot' (NeZero.ne p)
      (FaithfulSMul.algebraMap_injective _ _),
      ramificationIdx_bot' h‚ÇÅ (FaithfulSMul.algebraMap_injective _ _)]
  have h‚ÇÇ : Ideal.map (algebraMap R‚Çö S‚Çö) (maximalIdeal R‚Çö) ‚â§ P.map (algebraMap S S‚Çö) := by
    rw [map_le_iff_le_comap]
    exact le_of_eq <| (liesOver_iff _ _).mp <| liesOver_map_of_liesOver P p
  have main := (ramificationIdx_algebra_tower
      (map_ne_bot_of_ne_bot h‚ÇÅ) (map_ne_bot_of_ne_bot (NeZero.ne p)) h‚ÇÇ).symm.trans
      (ramificationIdx_algebra_tower (map_ne_bot_of_ne_bot hP)
      (map_ne_bot_of_ne_bot (NeZero.ne p)) le_rfl)
  rwa [show ramificationIdx (algebraMap R R‚Çö) p (maximalIdeal R‚Çö) = 1 by
      rw [‚Üê map_eq_maximalIdeal, ramificationIdx_map_self_eq_one_of_isPrincipal
        (map_ne_bot_of_ne_bot (NeZero.ne p))]
      rw [map_eq_maximalIdeal]
      exact IsPrime.ne_top',
    ramificationIdx_map_self_eq_one_of_isPrincipal (map_ne_bot_of_ne_bot hP)
    IsPrime.ne_top', one_mul, mul_one] at main

theorem exists_primesOver_map_eq_of_primesOver [p.IsPrime] (Q : (maximalIdeal R‚Çö).primesOver S‚Çö) :
    ‚àÉ q : p.primesOver S, q.val.map (algebraMap S S‚Çö) = Q := by
  refine ‚ü®‚ü®comap (algebraMap S S‚Çö) Q, ‚ü®IsPrime.under _ _, ?_‚ü©‚ü©, ?_‚ü©
  ¬∑ have : Q.1.LiesOver p := by
      have : (maximalIdeal R‚Çö).LiesOver p := (liesOver_iff _ _).mpr comap_maximalIdeal.symm
      exact LiesOver.trans Q.1 (IsLocalRing.maximalIdeal R‚Çö) p
    exact comap_liesOver Q.1 p <| IsScalarTower.toAlgHom R S S‚Çö
  ¬∑ refine le_antisymm  map_comap_le fun x hx ‚Ü¶ ?_
    obtain ‚ü®x, ‚ü®s, hs‚ü©, rfl‚ü© := mk'_surjective (algebraMapSubmonoid S p.primeCompl) x
    refine (mk'_mem_map_algebraMap_iff _ _ _ _ _).mpr ‚ü®s, hs, ?_‚ü©
    exact Ideal.mul_mem_left _ _ <| mk'_mem_iff.mp hx

variable [IsDedekindDomain S] [NoZeroSMulDivisors R S] [NeZero p]

variable (S)

/--
The bijection between the primes over `p` in `S` and the primes over the maximal ideal of `R‚Çö` in
`S‚Çö`. This bijection preserves the residual degree and the ramificiation index, see
`Localization.AtPrime.primesOverEquivPrimesOver_inertiagDeg_eq` and
`Localization.AtPrime.primesOverEquivPrimesOver_ramificationIdx_eq`.
-/
def primesOverEquivPrimesOver [p.IsPrime] : p.primesOver S ‚âÉ (maximalIdeal R‚Çö).primesOver S‚Çö :=
  Equiv.ofBijective (fun Q ‚Ü¶ ‚ü®Q.1.map (algebraMap S S‚Çö), isPrime_map_of_liesOver _ _,
    liesOver_map_of_liesOver _ _‚ü©)
    ‚ü®fun P‚ÇÅ P‚ÇÇ h ‚Ü¶ by
      have : P‚ÇÅ.val.IsMaximal := Ring.DimensionLEOne.maximalOfPrime
        (ne_bot_of_mem_primesOver (NeZero.ne _) P‚ÇÅ.prop) (primesOver.isPrime p P‚ÇÅ)
      have : P‚ÇÇ.val.IsMaximal := Ring.DimensionLEOne.maximalOfPrime
        (ne_bot_of_mem_primesOver (NeZero.ne _) P‚ÇÇ.prop) (primesOver.isPrime p P‚ÇÇ)
      have : (Ideal.map (algebraMap S S‚Çö) P‚ÇÅ).IsPrime := isPrime_map_of_liesOver _ _
      have : (Ideal.map (algebraMap S S‚Çö) P‚ÇÇ).IsPrime := isPrime_map_of_liesOver _ _
      simpa [comap_map_eq_self_of_isMaximal _ IsPrime.ne_top', SetCoe.ext_iff]
        using congr_arg (comap (algebraMap S S‚Çö) ¬∑) <| Subtype.mk_eq_mk.mp h,
    fun Q ‚Ü¶ by simpa [Subtype.ext_iff_val] using exists_primesOver_map_eq_of_primesOver _ _‚ü©

@[simp]
theorem primesOverEquivPrimesOver_apply [p.IsPrime] (P : p.primesOver S) :
    primesOverEquivPrimesOver S p P = Ideal.map (algebraMap S S‚Çö) P := rfl

theorem primesOverEquivPrimesOver_inertiagDeg_eq [p.IsMaximal] (P : p.primesOver S) :
    (maximalIdeal R‚Çö).inertiaDeg (primesOverEquivPrimesOver S p P : Ideal S‚Çö) =
      p.inertiaDeg P.val := by
  have : P.val.IsMaximal := Ring.DimensionLEOne.maximalOfPrime
        (ne_bot_of_mem_primesOver (NeZero.ne _) P.prop) inferInstance
  have : (P.1.map (algebraMap S S‚Çö)).LiesOver (maximalIdeal R‚Çö) := liesOver_map_of_liesOver _ _
  exact inertiaDeg_map_eq_inertiaDeg P.val p

theorem primesOverEquivPrimesOver_ramificationIdx_eq [p.IsPrime] [IsDedekindDomain R]
    [Module.Finite R S] (P : p.primesOver S) :
    (maximalIdeal R‚Çö).ramificationIdx (algebraMap R‚Çö S‚Çö)
      (primesOverEquivPrimesOver S p P : Ideal S‚Çö) = p.ramificationIdx (algebraMap R S) P.val :=
  ramificationIdx_map_eq_ramificationIdx P.val p

end primesOver

end Localization.AtPrime
