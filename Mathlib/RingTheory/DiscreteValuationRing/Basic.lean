/-
Copyright (c) 2020 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard
-/
import Mathlib.RingTheory.PrincipalIdealDomain
import Mathlib.RingTheory.Ideal.LocalRing
import Mathlib.RingTheory.Multiplicity
import Mathlib.RingTheory.Valuation.Basic
import Mathlib.LinearAlgebra.AdicCompletion

#align_import ring_theory.discrete_valuation_ring.basic from "leanprover-community/mathlib"@"c163ec99dfc664628ca15d215fce0a5b9c265b68"

/-!
# Discrete valuation rings

This file defines discrete valuation rings (DVRs) and develops a basic interface
for them.

## Important definitions

There are various definitions of a DVR in the literature; we define a DVR to be a local PID
which is not a field (the first definition in Wikipedia) and prove that this is equivalent
to being a PID with a unique non-zero prime ideal (the definition in Serre's
book "Local Fields").

Let R be an integral domain, assumed to be a principal ideal ring and a local ring.

* `DiscreteValuationRing R` : a predicate expressing that R is a DVR.

### Definitions

* `addVal R : AddValuation R PartENat` : the additive valuation on a DVR.

## Implementation notes

It's a theorem that an element of a DVR is a uniformizer if and only if it's irreducible.
We do not hence define `Uniformizer` at all, because we can use `Irreducible` instead.

## Tags

discrete valuation ring
-/


open Classical

universe u

open Ideal LocalRing

/-- An integral domain is a *discrete valuation ring* (DVR) if it's a local PID which
  is not a field. -/
class DiscreteValuationRing (R : Type u) [CommRing R] [IsDomain R]
    extends IsPrincipalIdealRing R, LocalRing R : Prop where
  not_a_field' : maximalIdeal R â‰  âŠ¥
#align discrete_valuation_ring DiscreteValuationRing

namespace DiscreteValuationRing

variable (R : Type u) [CommRing R] [IsDomain R] [DiscreteValuationRing R]

theorem not_a_field : maximalIdeal R â‰  âŠ¥ :=
  not_a_field'
#align discrete_valuation_ring.not_a_field DiscreteValuationRing.not_a_field

/-- A discrete valuation ring `R` is not a field. -/
theorem not_isField : Â¬IsField R :=
  LocalRing.isField_iff_maximalIdeal_eq.not.mpr (not_a_field R)
#align discrete_valuation_ring.not_is_field DiscreteValuationRing.not_isField

variable {R}

open PrincipalIdealRing

theorem irreducible_of_span_eq_maximalIdeal {R : Type*} [CommRing R] [LocalRing R] [IsDomain R]
    (Ï– : R) (hÏ– : Ï– â‰  0) (h : maximalIdeal R = Ideal.span {Ï–}) : Irreducible Ï– := by
  have h2 : Â¬IsUnit Ï– := show Ï– âˆˆ maximalIdeal R from h.symm â–¸ Submodule.mem_span_singleton_self Ï–
  -- âŠ¢ Irreducible Ï–
  refine' âŸ¨h2, _âŸ©
  -- âŠ¢ âˆ€ (a b : R), Ï– = a * b â†’ IsUnit a âˆ¨ IsUnit b
  intro a b hab
  -- âŠ¢ IsUnit a âˆ¨ IsUnit b
  by_contra' h
  -- âŠ¢ False
  obtain âŸ¨ha : a âˆˆ maximalIdeal R, hb : b âˆˆ maximalIdeal RâŸ© := h
  -- âŠ¢ False
  rw [h, mem_span_singleton'] at ha hb
  -- âŠ¢ False
  rcases ha with âŸ¨a, rflâŸ©
  -- âŠ¢ False
  rcases hb with âŸ¨b, rflâŸ©
  -- âŠ¢ False
  rw [show a * Ï– * (b * Ï–) = Ï– * (Ï– * (a * b)) by ring] at hab
  -- âŠ¢ False
  apply hÏ–
  -- âŠ¢ Ï– = 0
  apply eq_zero_of_mul_eq_self_right _ hab.symm
  -- âŠ¢ Ï– * (a * b) â‰  1
  exact fun hh => h2 (isUnit_of_dvd_one âŸ¨_, hh.symmâŸ©)
  -- ğŸ‰ no goals
#align discrete_valuation_ring.irreducible_of_span_eq_maximal_ideal DiscreteValuationRing.irreducible_of_span_eq_maximalIdeal

/-- An element of a DVR is irreducible iff it is a uniformizer, that is, generates the
  maximal ideal of `R`. -/
theorem irreducible_iff_uniformizer (Ï– : R) : Irreducible Ï– â†” maximalIdeal R = Ideal.span {Ï–} :=
  âŸ¨fun hÏ– => (eq_maximalIdeal (isMaximal_of_irreducible hÏ–)).symm,
    fun h => irreducible_of_span_eq_maximalIdeal Ï–
      (fun e => not_a_field R <| by rwa [h, span_singleton_eq_bot]) hâŸ©
                                    -- ğŸ‰ no goals
#align discrete_valuation_ring.irreducible_iff_uniformizer DiscreteValuationRing.irreducible_iff_uniformizer

theorem _root_.Irreducible.maximalIdeal_eq {Ï– : R} (h : Irreducible Ï–) :
    maximalIdeal R = Ideal.span {Ï–} :=
  (irreducible_iff_uniformizer _).mp h
#align irreducible.maximal_ideal_eq Irreducible.maximalIdeal_eq

variable (R)

/-- Uniformizers exist in a DVR. -/
theorem exists_irreducible : âˆƒ Ï– : R, Irreducible Ï– := by
  simp_rw [irreducible_iff_uniformizer]
  -- âŠ¢ âˆƒ Ï–, maximalIdeal R = span {Ï–}
  exact (IsPrincipalIdealRing.principal <| maximalIdeal R).principal
  -- ğŸ‰ no goals
#align discrete_valuation_ring.exists_irreducible DiscreteValuationRing.exists_irreducible

/-- Uniformizers exist in a DVR. -/
theorem exists_prime : âˆƒ Ï– : R, Prime Ï– :=
  (exists_irreducible R).imp fun _ => PrincipalIdealRing.irreducible_iff_prime.1
#align discrete_valuation_ring.exists_prime DiscreteValuationRing.exists_prime

/-- An integral domain is a DVR iff it's a PID with a unique non-zero prime ideal. -/
theorem iff_pid_with_one_nonzero_prime (R : Type u) [CommRing R] [IsDomain R] :
    DiscreteValuationRing R â†” IsPrincipalIdealRing R âˆ§ âˆƒ! P : Ideal R, P â‰  âŠ¥ âˆ§ IsPrime P := by
  constructor
  -- âŠ¢ DiscreteValuationRing R â†’ IsPrincipalIdealRing R âˆ§ âˆƒ! P, P â‰  âŠ¥ âˆ§ IsPrime P
  Â· intro RDVR
    -- âŠ¢ IsPrincipalIdealRing R âˆ§ âˆƒ! P, P â‰  âŠ¥ âˆ§ IsPrime P
    rcases id RDVR with âŸ¨RlocalâŸ©
    -- âŠ¢ IsPrincipalIdealRing R âˆ§ âˆƒ! P, P â‰  âŠ¥ âˆ§ IsPrime P
    constructor
    -- âŠ¢ IsPrincipalIdealRing R
    assumption
    -- âŠ¢ âˆƒ! P, P â‰  âŠ¥ âˆ§ IsPrime P
    use LocalRing.maximalIdeal R
    -- âŠ¢ (fun P => P â‰  âŠ¥ âˆ§ IsPrime P) (maximalIdeal R) âˆ§ âˆ€ (y : Ideal R), (fun P => P â€¦
    constructor
    -- âŠ¢ (fun P => P â‰  âŠ¥ âˆ§ IsPrime P) (maximalIdeal R)
    exact âŸ¨Rlocal, inferInstanceâŸ©
    -- âŠ¢ âˆ€ (y : Ideal R), (fun P => P â‰  âŠ¥ âˆ§ IsPrime P) y â†’ y = maximalIdeal R
    Â· rintro Q âŸ¨hQ1, hQ2âŸ©
      -- âŠ¢ Q = maximalIdeal R
      obtain âŸ¨q, rflâŸ© := (IsPrincipalIdealRing.principal Q).1
      -- âŠ¢ Submodule.span R {q} = maximalIdeal R
      have hq : q â‰  0 := by
        rintro rfl
        apply hQ1
        simp
      erw [span_singleton_prime hq] at hQ2
      -- âŠ¢ Submodule.span R {q} = maximalIdeal R
      replace hQ2 := hQ2.irreducible
      -- âŠ¢ Submodule.span R {q} = maximalIdeal R
      rw [irreducible_iff_uniformizer] at hQ2
      -- âŠ¢ Submodule.span R {q} = maximalIdeal R
      exact hQ2.symm
      -- ğŸ‰ no goals
  Â· rintro âŸ¨RPID, PuniqueâŸ©
    -- âŠ¢ DiscreteValuationRing R
    haveI : LocalRing R := LocalRing.of_unique_nonzero_prime Punique
    -- âŠ¢ DiscreteValuationRing R
    refine' { not_a_field' := _ }
    -- âŠ¢ maximalIdeal R â‰  âŠ¥
    rcases Punique with âŸ¨P, âŸ¨hP1, hP2âŸ©, _âŸ©
    -- âŠ¢ maximalIdeal R â‰  âŠ¥
    have hPM : P â‰¤ maximalIdeal R := le_maximalIdeal hP2.1
    -- âŠ¢ maximalIdeal R â‰  âŠ¥
    intro h
    -- âŠ¢ False
    rw [h, le_bot_iff] at hPM
    -- âŠ¢ False
    exact hP1 hPM
    -- ğŸ‰ no goals
#align discrete_valuation_ring.iff_pid_with_one_nonzero_prime DiscreteValuationRing.iff_pid_with_one_nonzero_prime

theorem associated_of_irreducible {a b : R} (ha : Irreducible a) (hb : Irreducible b) :
    Associated a b := by
  rw [irreducible_iff_uniformizer] at ha hb
  -- âŠ¢ Associated a b
  rw [â† span_singleton_eq_span_singleton, â† ha, hb]
  -- ğŸ‰ no goals
#align discrete_valuation_ring.associated_of_irreducible DiscreteValuationRing.associated_of_irreducible

end DiscreteValuationRing

namespace DiscreteValuationRing

variable (R : Type*)

/-- Alternative characterisation of discrete valuation rings. -/
def HasUnitMulPowIrreducibleFactorization [CommRing R] : Prop :=
  âˆƒ p : R, Irreducible p âˆ§ âˆ€ {x : R}, x â‰  0 â†’ âˆƒ n : â„•, Associated (p ^ n) x
#align discrete_valuation_ring.has_unit_mul_pow_irreducible_factorization DiscreteValuationRing.HasUnitMulPowIrreducibleFactorization

namespace HasUnitMulPowIrreducibleFactorization

variable {R} [CommRing R] (hR : HasUnitMulPowIrreducibleFactorization R)

theorem unique_irreducible â¦ƒp q : Râ¦„ (hp : Irreducible p) (hq : Irreducible q) :
    Associated p q := by
  rcases hR with âŸ¨Ï–, hÏ–, hRâŸ©
  -- âŠ¢ Associated p q
  suffices âˆ€ {p : R} (_ : Irreducible p), Associated p Ï– by
    apply Associated.trans (this hp) (this hq).symm
  clear hp hq p q
  -- âŠ¢ âˆ€ {p : R}, Irreducible p â†’ Associated p Ï–
  intro p hp
  -- âŠ¢ Associated p Ï–
  obtain âŸ¨n, hnâŸ© := hR hp.ne_zero
  -- âŠ¢ Associated p Ï–
  have : Irreducible (Ï– ^ n) := hn.symm.irreducible hp
  -- âŠ¢ Associated p Ï–
  rcases lt_trichotomy n 1 with (H | rfl | H)
  Â· obtain rfl : n = 0 := by
      clear hn this
      revert H n
      exact by decide
    simp [not_irreducible_one, pow_zero] at this
    -- ğŸ‰ no goals
  Â· simpa only [pow_one] using hn.symm
    -- ğŸ‰ no goals
  Â· obtain âŸ¨n, rflâŸ© : âˆƒ k, n = 1 + k + 1 := Nat.exists_eq_add_of_lt H
    -- âŠ¢ Associated p Ï–
    rw [pow_succ] at this
    -- âŠ¢ Associated p Ï–
    rcases this.isUnit_or_isUnit rfl with (H0 | H0)
    -- âŠ¢ Associated p Ï–
    Â· exact (hÏ–.not_unit H0).elim
      -- ğŸ‰ no goals
    Â· rw [add_comm, pow_succ] at H0
      -- âŠ¢ Associated p Ï–
      exact (hÏ–.not_unit (isUnit_of_mul_isUnit_left H0)).elim
      -- ğŸ‰ no goals
#align discrete_valuation_ring.has_unit_mul_pow_irreducible_factorization.unique_irreducible DiscreteValuationRing.HasUnitMulPowIrreducibleFactorization.unique_irreducible

variable [IsDomain R]

/-- An integral domain in which there is an irreducible element `p`
such that every nonzero element is associated to a power of `p` is a unique factorization domain.
See `DiscreteValuationRing.ofHasUnitMulPowIrreducibleFactorization`. -/
theorem toUniqueFactorizationMonoid : UniqueFactorizationMonoid R :=
  let p := Classical.choose hR
  let spec := Classical.choose_spec hR
  UniqueFactorizationMonoid.of_exists_prime_factors fun x hx => by
    use Multiset.replicate (Classical.choose (spec.2 hx)) p
    -- âŠ¢ (âˆ€ (b : R), b âˆˆ Multiset.replicate (choose (_ : âˆƒ n, Associated (choose hR ^ â€¦
    constructor
    -- âŠ¢ âˆ€ (b : R), b âˆˆ Multiset.replicate (choose (_ : âˆƒ n, Associated (choose hR ^  â€¦
    Â· intro q hq
      -- âŠ¢ Prime q
      have hpq := Multiset.eq_of_mem_replicate hq
      -- âŠ¢ Prime q
      rw [hpq]
      -- âŠ¢ Prime p
      refine' âŸ¨spec.1.ne_zero, spec.1.not_unit, _âŸ©
      -- âŠ¢ âˆ€ (a b : R), p âˆ£ a * b â†’ p âˆ£ a âˆ¨ p âˆ£ b
      intro a b h
      -- âŠ¢ p âˆ£ a âˆ¨ p âˆ£ b
      by_cases ha : a = 0
      -- âŠ¢ p âˆ£ a âˆ¨ p âˆ£ b
      Â· rw [ha]
        -- âŠ¢ p âˆ£ 0 âˆ¨ p âˆ£ b
        simp only [true_or_iff, dvd_zero]
        -- ğŸ‰ no goals
      obtain âŸ¨m, u, rflâŸ© := spec.2 ha
      -- âŠ¢ p âˆ£ choose hR ^ m * â†‘u âˆ¨ p âˆ£ b
      rw [mul_assoc, mul_left_comm, IsUnit.dvd_mul_left _ _ _ (Units.isUnit _)] at h
      -- âŠ¢ p âˆ£ choose hR ^ m * â†‘u âˆ¨ p âˆ£ b
      rw [IsUnit.dvd_mul_right (Units.isUnit _)]
      -- âŠ¢ p âˆ£ choose hR ^ m âˆ¨ p âˆ£ b
      by_cases hm : m = 0
      -- âŠ¢ p âˆ£ choose hR ^ m âˆ¨ p âˆ£ b
      Â· simp only [hm, one_mul, pow_zero] at h âŠ¢
        -- âŠ¢ choose hR âˆ£ 1 âˆ¨ choose hR âˆ£ b
        right
        -- âŠ¢ choose hR âˆ£ b
        exact h
        -- ğŸ‰ no goals
      left
      -- âŠ¢ p âˆ£ choose hR ^ m
      obtain âŸ¨m, rflâŸ© := Nat.exists_eq_succ_of_ne_zero hm
      -- âŠ¢ p âˆ£ choose hR ^ Nat.succ m
      rw [pow_succ]
      -- âŠ¢ p âˆ£ choose hR * choose hR ^ m
      apply dvd_mul_of_dvd_left dvd_rfl _
      -- ğŸ‰ no goals
    Â· rw [Multiset.prod_replicate]
      -- âŠ¢ Associated (p ^ choose (_ : âˆƒ n, Associated (choose hR ^ n) x)) x
      exact Classical.choose_spec (spec.2 hx)
      -- ğŸ‰ no goals
#align discrete_valuation_ring.has_unit_mul_pow_irreducible_factorization.to_unique_factorization_monoid DiscreteValuationRing.HasUnitMulPowIrreducibleFactorization.toUniqueFactorizationMonoid

theorem of_ufd_of_unique_irreducible [UniqueFactorizationMonoid R] (hâ‚ : âˆƒ p : R, Irreducible p)
    (hâ‚‚ : âˆ€ â¦ƒp q : Râ¦„, Irreducible p â†’ Irreducible q â†’ Associated p q) :
    HasUnitMulPowIrreducibleFactorization R := by
  obtain âŸ¨p, hpâŸ© := hâ‚
  -- âŠ¢ HasUnitMulPowIrreducibleFactorization R
  refine' âŸ¨p, hp, _âŸ©
  -- âŠ¢ âˆ€ {x : R}, x â‰  0 â†’ âˆƒ n, Associated (p ^ n) x
  intro x hx
  -- âŠ¢ âˆƒ n, Associated (p ^ n) x
  cases' WfDvdMonoid.exists_factors x hx with fx hfx
  -- âŠ¢ âˆƒ n, Associated (p ^ n) x
  refine' âŸ¨Multiset.card fx, _âŸ©
  -- âŠ¢ Associated (p ^ â†‘Multiset.card fx) x
  have H := hfx.2
  -- âŠ¢ Associated (p ^ â†‘Multiset.card fx) x
  rw [â† Associates.mk_eq_mk_iff_associated] at H âŠ¢
  -- âŠ¢ Associates.mk (p ^ â†‘Multiset.card fx) = Associates.mk x
  rw [â† H, â† Associates.prod_mk, Associates.mk_pow, â† Multiset.prod_replicate]
  -- âŠ¢ Multiset.prod (Multiset.replicate (â†‘Multiset.card fx) (Associates.mk p)) = M â€¦
  congr 1
  -- âŠ¢ Multiset.replicate (â†‘Multiset.card fx) (Associates.mk p) = Multiset.map Asso â€¦
  symm
  -- âŠ¢ Multiset.map Associates.mk fx = Multiset.replicate (â†‘Multiset.card fx) (Asso â€¦
  rw [Multiset.eq_replicate]
  -- âŠ¢ â†‘Multiset.card (Multiset.map Associates.mk fx) = â†‘Multiset.card fx âˆ§ âˆ€ (b :  â€¦
  simp only [true_and_iff, and_imp, Multiset.card_map, eq_self_iff_true, Multiset.mem_map,
    exists_imp]
  rintro _ q hq rfl
  -- âŠ¢ Associates.mk q = Associates.mk p
  rw [Associates.mk_eq_mk_iff_associated]
  -- âŠ¢ Associated q p
  apply hâ‚‚ (hfx.1 _ hq) hp
  -- ğŸ‰ no goals
#align discrete_valuation_ring.has_unit_mul_pow_irreducible_factorization.of_ufd_of_unique_irreducible DiscreteValuationRing.HasUnitMulPowIrreducibleFactorization.of_ufd_of_unique_irreducible

end HasUnitMulPowIrreducibleFactorization

theorem aux_pid_of_ufd_of_unique_irreducible (R : Type u) [CommRing R] [IsDomain R]
    [UniqueFactorizationMonoid R] (hâ‚ : âˆƒ p : R, Irreducible p)
    (hâ‚‚ : âˆ€ â¦ƒp q : Râ¦„, Irreducible p â†’ Irreducible q â†’ Associated p q) :
    IsPrincipalIdealRing R := by
  constructor
  -- âŠ¢ âˆ€ (S : Ideal R), Submodule.IsPrincipal S
  intro I
  -- âŠ¢ Submodule.IsPrincipal I
  by_cases I0 : I = âŠ¥
  -- âŠ¢ Submodule.IsPrincipal I
  Â· rw [I0]
    -- âŠ¢ Submodule.IsPrincipal âŠ¥
    use 0
    -- âŠ¢ âŠ¥ = Submodule.span R {0}
    simp only [Set.singleton_zero, Submodule.span_zero]
    -- ğŸ‰ no goals
  obtain âŸ¨x, hxI, hx0âŸ© : âˆƒ x âˆˆ I, x â‰  (0 : R) := I.ne_bot_iff.mp I0
  -- âŠ¢ Submodule.IsPrincipal I
  obtain âŸ¨p, _, HâŸ© := HasUnitMulPowIrreducibleFactorization.of_ufd_of_unique_irreducible hâ‚ hâ‚‚
  -- âŠ¢ Submodule.IsPrincipal I
  have ex : âˆƒ n : â„•, p ^ n âˆˆ I := by
    obtain âŸ¨n, u, rflâŸ© := H hx0
    refine' âŸ¨n, _âŸ©
    simpa only [Units.mul_inv_cancel_right] using I.mul_mem_right (â†‘uâ»Â¹) hxI
  constructor
  -- âŠ¢ âˆƒ a, I = Submodule.span R {a}
  use p ^ Nat.find ex
  -- âŠ¢ I = Submodule.span R {p ^ Nat.find ex}
  show I = Ideal.span _
  -- âŠ¢ I = span {p ^ Nat.find ex}
  apply le_antisymm
  -- âŠ¢ I â‰¤ span {p ^ Nat.find ex}
  Â· intro r hr
    -- âŠ¢ r âˆˆ span {p ^ Nat.find ex}
    by_cases hr0 : r = 0
    -- âŠ¢ r âˆˆ span {p ^ Nat.find ex}
    Â· simp only [hr0, Submodule.zero_mem]
      -- ğŸ‰ no goals
    obtain âŸ¨n, u, rflâŸ© := H hr0
    -- âŠ¢ p ^ n * â†‘u âˆˆ span {p ^ Nat.find ex}
    simp only [mem_span_singleton, Units.isUnit, IsUnit.dvd_mul_right]
    -- âŠ¢ p ^ Nat.find ex âˆ£ p ^ n
    apply pow_dvd_pow
    -- âŠ¢ Nat.find ex â‰¤ n
    apply Nat.find_min'
    -- âŠ¢ p ^ n âˆˆ I
    simpa only [Units.mul_inv_cancel_right] using I.mul_mem_right (â†‘uâ»Â¹) hr
    -- ğŸ‰ no goals
  Â· erw [Submodule.span_singleton_le_iff_mem]
    -- âŠ¢ p ^ Nat.find ex âˆˆ I
    exact Nat.find_spec ex
    -- ğŸ‰ no goals
#align discrete_valuation_ring.aux_pid_of_ufd_of_unique_irreducible DiscreteValuationRing.aux_pid_of_ufd_of_unique_irreducible

/-- A unique factorization domain with at least one irreducible element
in which all irreducible elements are associated
is a discrete valuation ring.
-/
theorem of_ufd_of_unique_irreducible {R : Type u} [CommRing R] [IsDomain R]
    [UniqueFactorizationMonoid R] (hâ‚ : âˆƒ p : R, Irreducible p)
    (hâ‚‚ : âˆ€ â¦ƒp q : Râ¦„, Irreducible p â†’ Irreducible q â†’ Associated p q) :
    DiscreteValuationRing R := by
  rw [iff_pid_with_one_nonzero_prime]
  -- âŠ¢ IsPrincipalIdealRing R âˆ§ âˆƒ! P, P â‰  âŠ¥ âˆ§ IsPrime P
  haveI PID : IsPrincipalIdealRing R := aux_pid_of_ufd_of_unique_irreducible R hâ‚ hâ‚‚
  -- âŠ¢ IsPrincipalIdealRing R âˆ§ âˆƒ! P, P â‰  âŠ¥ âˆ§ IsPrime P
  obtain âŸ¨p, hpâŸ© := hâ‚
  -- âŠ¢ IsPrincipalIdealRing R âˆ§ âˆƒ! P, P â‰  âŠ¥ âˆ§ IsPrime P
  refine' âŸ¨PID, âŸ¨Ideal.span {p}, âŸ¨_, _âŸ©, _âŸ©âŸ©
  Â· rw [Submodule.ne_bot_iff]
    -- âŠ¢ âˆƒ x, x âˆˆ span {p} âˆ§ x â‰  0
    refine' âŸ¨p, Ideal.mem_span_singleton.mpr (dvd_refl p), hp.ne_zeroâŸ©
    -- ğŸ‰ no goals
  Â· rwa [Ideal.span_singleton_prime hp.ne_zero, â† UniqueFactorizationMonoid.irreducible_iff_prime]
    -- ğŸ‰ no goals
  Â· intro I
    -- âŠ¢ (fun P => P â‰  âŠ¥ âˆ§ IsPrime P) I â†’ I = span {p}
    rw [â† Submodule.IsPrincipal.span_singleton_generator I]
    -- âŠ¢ (fun P => P â‰  âŠ¥ âˆ§ IsPrime P) (Submodule.span R {Submodule.IsPrincipal.genera â€¦
    rintro âŸ¨I0, hIâŸ©
    -- âŠ¢ Submodule.span R {Submodule.IsPrincipal.generator I} = span {p}
    apply span_singleton_eq_span_singleton.mpr
    -- âŠ¢ Associated (Submodule.IsPrincipal.generator I) p
    apply hâ‚‚ _ hp
    -- âŠ¢ Irreducible (Submodule.IsPrincipal.generator I)
    erw [Ne.def, span_singleton_eq_bot] at I0
    -- âŠ¢ Irreducible (Submodule.IsPrincipal.generator I)
    rwa [UniqueFactorizationMonoid.irreducible_iff_prime, â† Ideal.span_singleton_prime I0]
    -- ğŸ‰ no goals
#align discrete_valuation_ring.of_ufd_of_unique_irreducible DiscreteValuationRing.of_ufd_of_unique_irreducible

/-- An integral domain in which there is an irreducible element `p`
such that every nonzero element is associated to a power of `p`
is a discrete valuation ring.
-/
theorem ofHasUnitMulPowIrreducibleFactorization {R : Type u} [CommRing R] [IsDomain R]
    (hR : HasUnitMulPowIrreducibleFactorization R) : DiscreteValuationRing R := by
  letI : UniqueFactorizationMonoid R := hR.toUniqueFactorizationMonoid
  -- âŠ¢ DiscreteValuationRing R
  apply of_ufd_of_unique_irreducible _ hR.unique_irreducible
  -- âŠ¢ âˆƒ p, Irreducible p
  obtain âŸ¨p, hp, HâŸ© := hR
  -- âŠ¢ âˆƒ p, Irreducible p
  exact âŸ¨p, hpâŸ©
  -- ğŸ‰ no goals
#align discrete_valuation_ring.of_has_unit_mul_pow_irreducible_factorization DiscreteValuationRing.ofHasUnitMulPowIrreducibleFactorization

section

variable [CommRing R] [IsDomain R] [DiscreteValuationRing R]

variable {R}

theorem associated_pow_irreducible {x : R} (hx : x â‰  0) {Ï– : R} (hirr : Irreducible Ï–) :
    âˆƒ n : â„•, Associated x (Ï– ^ n) := by
  have : WfDvdMonoid R := IsNoetherianRing.wfDvdMonoid
  -- âŠ¢ âˆƒ n, Associated x (Ï– ^ n)
  cases' WfDvdMonoid.exists_factors x hx with fx hfx
  -- âŠ¢ âˆƒ n, Associated x (Ï– ^ n)
  use Multiset.card fx
  -- âŠ¢ Associated x (Ï– ^ â†‘Multiset.card fx)
  have H := hfx.2
  -- âŠ¢ Associated x (Ï– ^ â†‘Multiset.card fx)
  rw [â† Associates.mk_eq_mk_iff_associated] at H âŠ¢
  -- âŠ¢ Associates.mk x = Associates.mk (Ï– ^ â†‘Multiset.card fx)
  rw [â† H, â† Associates.prod_mk, Associates.mk_pow, â† Multiset.prod_replicate]
  -- âŠ¢ Multiset.prod (Multiset.map Associates.mk fx) = Multiset.prod (Multiset.repl â€¦
  congr 1
  -- âŠ¢ Multiset.map Associates.mk fx = Multiset.replicate (â†‘Multiset.card fx) (Asso â€¦
  rw [Multiset.eq_replicate]
  -- âŠ¢ â†‘Multiset.card (Multiset.map Associates.mk fx) = â†‘Multiset.card fx âˆ§ âˆ€ (b :  â€¦
  simp only [true_and_iff, and_imp, Multiset.card_map, eq_self_iff_true, Multiset.mem_map,
    exists_imp]
  rintro _ _ _ rfl
  -- âŠ¢ Associates.mk xâœ = Associates.mk Ï–
  rw [Associates.mk_eq_mk_iff_associated]
  -- âŠ¢ Associated xâœ Ï–
  refine' associated_of_irreducible _ _ hirr
  -- âŠ¢ Irreducible xâœ
  apply hfx.1
  -- âŠ¢ xâœ âˆˆ fx
  assumption
  -- ğŸ‰ no goals
#align discrete_valuation_ring.associated_pow_irreducible DiscreteValuationRing.associated_pow_irreducible

theorem eq_unit_mul_pow_irreducible {x : R} (hx : x â‰  0) {Ï– : R} (hirr : Irreducible Ï–) :
    âˆƒ (n : â„•) (u : RË£), x = u * Ï– ^ n := by
  obtain âŸ¨n, hnâŸ© := associated_pow_irreducible hx hirr
  -- âŠ¢ âˆƒ n u, x = â†‘u * Ï– ^ n
  obtain âŸ¨u, rflâŸ© := hn.symm
  -- âŠ¢ âˆƒ n_1 u_1, Ï– ^ n * â†‘u = â†‘u_1 * Ï– ^ n_1
  use n, u
  -- âŠ¢ Ï– ^ n * â†‘u = â†‘u * Ï– ^ n
  apply mul_comm
  -- ğŸ‰ no goals
#align discrete_valuation_ring.eq_unit_mul_pow_irreducible DiscreteValuationRing.eq_unit_mul_pow_irreducible

open Submodule.IsPrincipal

theorem ideal_eq_span_pow_irreducible {s : Ideal R} (hs : s â‰  âŠ¥) {Ï– : R} (hirr : Irreducible Ï–) :
    âˆƒ n : â„•, s = Ideal.span {Ï– ^ n} := by
  have gen_ne_zero : generator s â‰  0 := by
    rw [Ne.def, â† eq_bot_iff_generator_eq_zero]
    assumption
  rcases associated_pow_irreducible gen_ne_zero hirr with âŸ¨n, u, hnuâŸ©
  -- âŠ¢ âˆƒ n, s = span {Ï– ^ n}
  use n
  -- âŠ¢ s = span {Ï– ^ n}
  have : span _ = _ := Ideal.span_singleton_generator s
  -- âŠ¢ s = span {Ï– ^ n}
  rw [â† this, â† hnu, span_singleton_eq_span_singleton]
  -- âŠ¢ Associated (generator s) (generator s * â†‘u)
  use u
  -- ğŸ‰ no goals
#align discrete_valuation_ring.ideal_eq_span_pow_irreducible DiscreteValuationRing.ideal_eq_span_pow_irreducible

theorem unit_mul_pow_congr_pow {p q : R} (hp : Irreducible p) (hq : Irreducible q) (u v : RË£)
    (m n : â„•) (h : â†‘u * p ^ m = v * q ^ n) : m = n := by
  have key : Associated (Multiset.replicate m p).prod (Multiset.replicate n q).prod := by
    rw [Multiset.prod_replicate, Multiset.prod_replicate, Associated]
    refine' âŸ¨u * vâ»Â¹, _âŸ©
    simp only [Units.val_mul]
    rw [mul_left_comm, â† mul_assoc, h, mul_right_comm, Units.mul_inv, one_mul]
  have := by
    refine' Multiset.card_eq_card_of_rel (UniqueFactorizationMonoid.factors_unique _ _ key)
    all_goals
      intro x hx
      obtain rfl := Multiset.eq_of_mem_replicate hx
      assumption
  simpa only [Multiset.card_replicate]
  -- ğŸ‰ no goals
#align discrete_valuation_ring.unit_mul_pow_congr_pow DiscreteValuationRing.unit_mul_pow_congr_pow

theorem unit_mul_pow_congr_unit {Ï– : R} (hirr : Irreducible Ï–) (u v : RË£) (m n : â„•)
    (h : â†‘u * Ï– ^ m = v * Ï– ^ n) : u = v := by
  obtain rfl : m = n := unit_mul_pow_congr_pow hirr hirr u v m n h
  -- âŠ¢ u = v
  rw [â† sub_eq_zero] at h
  -- âŠ¢ u = v
  rw [â† sub_mul, mul_eq_zero] at h
  -- âŠ¢ u = v
  cases' h with h h
  -- âŠ¢ u = v
  Â· rw [sub_eq_zero] at h
    -- âŠ¢ u = v
    exact_mod_cast h
    -- ğŸ‰ no goals
  Â· apply (hirr.ne_zero (pow_eq_zero h)).elim
    -- ğŸ‰ no goals
#align discrete_valuation_ring.unit_mul_pow_congr_unit DiscreteValuationRing.unit_mul_pow_congr_unit

/-!
## The additive valuation on a DVR
-/
open multiplicity

/-- The `PartENat`-valued additive valuation on a DVR. -/
noncomputable def addVal (R : Type u) [CommRing R] [IsDomain R] [DiscreteValuationRing R] :
    AddValuation R PartENat :=
  addValuation (Classical.choose_spec (exists_prime R))
#align discrete_valuation_ring.add_val DiscreteValuationRing.addVal

theorem addVal_def (r : R) (u : RË£) {Ï– : R} (hÏ– : Irreducible Ï–) (n : â„•) (hr : r = u * Ï– ^ n) :
    addVal R r = n := by
  rw [addVal, addValuation_apply, hr, eq_of_associated_left
      (associated_of_irreducible R hÏ– (Classical.choose_spec (exists_prime R)).irreducible),
    eq_of_associated_right (Associated.symm âŸ¨u, mul_comm _ _âŸ©),
    multiplicity_pow_self_of_prime (PrincipalIdealRing.irreducible_iff_prime.1 hÏ–)]
#align discrete_valuation_ring.add_val_def DiscreteValuationRing.addVal_def

theorem addVal_def' (u : RË£) {Ï– : R} (hÏ– : Irreducible Ï–) (n : â„•) :
    addVal R ((u : R) * Ï– ^ n) = n :=
  addVal_def _ u hÏ– n rfl
#align discrete_valuation_ring.add_val_def' DiscreteValuationRing.addVal_def'

--@[simp] Porting note: simp can prove it
theorem addVal_zero : addVal R 0 = âŠ¤ :=
  (addVal R).map_zero
#align discrete_valuation_ring.add_val_zero DiscreteValuationRing.addVal_zero

--@[simp] Porting note: simp can prove it
theorem addVal_one : addVal R 1 = 0 :=
  (addVal R).map_one
#align discrete_valuation_ring.add_val_one DiscreteValuationRing.addVal_one

@[simp]
theorem addVal_uniformizer {Ï– : R} (hÏ– : Irreducible Ï–) : addVal R Ï– = 1 := by
  simpa only [one_mul, eq_self_iff_true, Units.val_one, pow_one, forall_true_left, Nat.cast_one]
    using addVal_def Ï– 1 hÏ– 1
#align discrete_valuation_ring.add_val_uniformizer DiscreteValuationRing.addVal_uniformizer

--@[simp] Porting note: simp can prove it
theorem addVal_mul {a b : R} :
    addVal R (a * b) = addVal R a + addVal R b :=
  (addVal R).map_mul _ _
#align discrete_valuation_ring.add_val_mul DiscreteValuationRing.addVal_mul

theorem addVal_pow (a : R) (n : â„•) : addVal R (a ^ n) = n â€¢ addVal R a :=
  (addVal R).map_pow _ _
#align discrete_valuation_ring.add_val_pow DiscreteValuationRing.addVal_pow

nonrec theorem _root_.Irreducible.addVal_pow {Ï– : R} (h : Irreducible Ï–) (n : â„•) :
    addVal R (Ï– ^ n) = n := by
  rw [addVal_pow, addVal_uniformizer h, nsmul_one]
  -- ğŸ‰ no goals
#align irreducible.add_val_pow Irreducible.addVal_pow

theorem addVal_eq_top_iff {a : R} : addVal R a = âŠ¤ â†” a = 0 := by
  have hi := (Classical.choose_spec (exists_prime R)).irreducible
  -- âŠ¢ â†‘(addVal R) a = âŠ¤ â†” a = 0
  constructor
  -- âŠ¢ â†‘(addVal R) a = âŠ¤ â†’ a = 0
  Â· contrapose
    -- âŠ¢ Â¬a = 0 â†’ Â¬â†‘(addVal R) a = âŠ¤
    intro h
    -- âŠ¢ Â¬â†‘(addVal R) a = âŠ¤
    obtain âŸ¨n, haâŸ© := associated_pow_irreducible h hi
    -- âŠ¢ Â¬â†‘(addVal R) a = âŠ¤
    obtain âŸ¨u, rflâŸ© := ha.symm
    -- âŠ¢ Â¬â†‘(addVal R) (choose (_ : âˆƒ Ï–, Prime Ï–) ^ n * â†‘u) = âŠ¤
    rw [mul_comm, addVal_def' u hi n]
    -- âŠ¢ Â¬â†‘n = âŠ¤
    exact PartENat.natCast_ne_top _
    -- ğŸ‰ no goals
  Â· rintro rfl
    -- âŠ¢ â†‘(addVal R) 0 = âŠ¤
    exact addVal_zero
    -- ğŸ‰ no goals
#align discrete_valuation_ring.add_val_eq_top_iff DiscreteValuationRing.addVal_eq_top_iff

theorem addVal_le_iff_dvd {a b : R} : addVal R a â‰¤ addVal R b â†” a âˆ£ b := by
  have hp := Classical.choose_spec (exists_prime R)
  -- âŠ¢ â†‘(addVal R) a â‰¤ â†‘(addVal R) b â†” a âˆ£ b
  constructor <;> intro h
  -- âŠ¢ â†‘(addVal R) a â‰¤ â†‘(addVal R) b â†’ a âˆ£ b
                  -- âŠ¢ a âˆ£ b
                  -- âŠ¢ â†‘(addVal R) a â‰¤ â†‘(addVal R) b
  Â· by_cases ha0 : a = 0
    -- âŠ¢ a âˆ£ b
    Â· rw [ha0, addVal_zero, top_le_iff, addVal_eq_top_iff] at h
      -- âŠ¢ a âˆ£ b
      rw [h]
      -- âŠ¢ a âˆ£ 0
      apply dvd_zero
      -- ğŸ‰ no goals
    obtain âŸ¨n, haâŸ© := associated_pow_irreducible ha0 hp.irreducible
    -- âŠ¢ a âˆ£ b
    rw [addVal, addValuation_apply, addValuation_apply, multiplicity_le_multiplicity_iff] at h
    -- âŠ¢ a âˆ£ b
    exact ha.dvd.trans (h n ha.symm.dvd)
    -- ğŸ‰ no goals
  Â· rw [addVal, addValuation_apply, addValuation_apply]
    -- âŠ¢ multiplicity (choose (_ : âˆƒ Ï–, Prime Ï–)) a â‰¤ multiplicity (choose (_ : âˆƒ Ï–,  â€¦
    exact multiplicity_le_multiplicity_of_dvd_right h
    -- ğŸ‰ no goals
#align discrete_valuation_ring.add_val_le_iff_dvd DiscreteValuationRing.addVal_le_iff_dvd

theorem addVal_add {a b : R} : min (addVal R a) (addVal R b) â‰¤ addVal R (a + b) :=
  (addVal R).map_add _ _
#align discrete_valuation_ring.add_val_add DiscreteValuationRing.addVal_add

end

instance (R : Type*) [CommRing R] [IsDomain R] [DiscreteValuationRing R] :
    IsHausdorff (maximalIdeal R) R where
  haus' x hx := by
    obtain âŸ¨Ï–, hÏ–âŸ© := exists_irreducible R
    -- âŠ¢ x = 0
    simp only [â† Ideal.one_eq_top, smul_eq_mul, mul_one, SModEq.zero, hÏ–.maximalIdeal_eq,
      Ideal.span_singleton_pow, Ideal.mem_span_singleton, â† addVal_le_iff_dvd, hÏ–.addVal_pow] at hx
    rwa [â† addVal_eq_top_iff, PartENat.eq_top_iff_forall_le]
    -- ğŸ‰ no goals

end DiscreteValuationRing
