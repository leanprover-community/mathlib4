/-
Copyright (c) 2025 Lua Viana Reis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Lua Viana Reis, Oliver Butterley
-/
module

public import Mathlib.Dynamics.BirkhoffSum.Average
public import Mathlib.MeasureTheory.MeasurableSpace.Invariants
public import Mathlib.MeasureTheory.Function.ConditionalExpectation.Basic
import Mathlib.Algebra.Order.Group.PartialSups
import Mathlib.Algebra.Order.Ring.Star
import Mathlib.Data.Real.StarOrdered
import Mathlib.Dynamics.BirkhoffSum.QuasiMeasurePreserving
import Mathlib.MeasureTheory.Integral.DominatedConvergence
import Mathlib.Topology.Algebra.Module.WeakDual

/-!
# Pointwise Ergodic Theorem

The Pointwise Ergodic Theorem, also known as Birkhoff's Ergodic Theorem, establishes the convergence
of time averages for dynamical systems.

Let `(Î±, Î¼)` be a probability space and `f: Î± â†’ Î±` be a measure-preserving transformation. The
result states that, for any integrable function `Ï†  âˆˆ LÂ¹(Î¼)`, the time averages
`(1/n)âˆ‘_{k=0}^{n-1} Ï†(f^k x)` converge almost everywhere as `n â†’ âˆ` to a limit function `Ï†*`.
Moreover the limit function `Ï†*` is essentially `f`-invariant and integrable with `âˆ«Ï†* dÎ¼ = âˆ«Ï† dÎ¼`.
If the system is ergodic, then `Ï†*` equals the constant `âˆ«f dÎ¼` almost everywhere.

The limit function `Ï†*` is equal to the conditional expectation of `Ï†` with respect to the Ïƒ-algebra
of `f`-invariant sets. This is used explicitly during this proof and also in the main statement.

## Main statements

* `ae_tendsTo_birkhoffAverage_condExp`: time average coincides with conditional expectation

-/

section BirkhoffMax

variable {Î± : Type*}

/-- The maximum of `birkhoffSum f Ï† i` for `i` ranging from `1` to `n + 1`. -/
def birkhoffMax (f : Î± â†’ Î±) (g : Î± â†’ â„) : â„• â†’o (Î± â†’ â„) :=
  partialSups (birkhoffSum f g âˆ˜ .succ)

lemma birkhoffMax_succ {f : Î± â†’ Î±} {g n x} :
    birkhoffMax f g n.succ x = g x + 0 âŠ” birkhoffMax f g n (f x) := by
  change
    (partialSups (birkhoffSum f g âˆ˜ Nat.succ)) n.succ x = g x + max 0 ((birkhoffMax f g) n (f x))
  have h : birkhoffSum f g âˆ˜ Nat.succ = fun k â†¦ g + birkhoffSum f g k âˆ˜ f := by
    funext k _
    simp [add_comm k 1, birkhoffSum_add f g 1, birkhoffSum_one]
  rw [h, partialSups_const_add, Pi.add_apply, add_right_inj, show n.succ = Order.succ n by rfl,
    partialSups_succ', Pi.sup_apply, Pi.partialSups_apply]
  simp [Function.comp_apply, â† Pi.partialSups_apply]
  rfl

/-- The difference between the maximum of `birkhoffSum f Ï† i` for `i` ranging from `1` to `n + 1`
and the maximum of the same quantity for `i` ranging from `2` to `n + 2`. -/
abbrev birkhoffMaxDiff (f : Î± â†’ Î±) (g : Î± â†’ â„) (n : â„•) (x : Î±) :=
  birkhoffMax f g (n + 1) x - birkhoffMax f g n (f x)

theorem birkhoffMaxDiff_aux {f : Î± â†’ Î±} {g n x} :
    birkhoffMaxDiff f g n x = g x - (0 âŠ“ birkhoffMax f g n (f x)) := by
  rw [sub_eq_sub_iff_add_eq_add, birkhoffMax_succ, add_assoc, add_right_inj, max_add_min, zero_add]

lemma birkhoffMaxDiff_antitone {f : Î± â†’ Î±} {Ï†} : Antitone (birkhoffMaxDiff f Ï†) := by
  intro m n h x
  rw [birkhoffMaxDiff_aux, birkhoffMaxDiff_aux]
  apply sub_le_sub_left
  simp only [le_inf_iff, inf_le_left, inf_le_iff, true_and]
  right
  exact (birkhoffMax f Ï†).monotone' h _

@[measurability]
lemma birkhoffSum_measurable [MeasurableSpace Î±] {f : Î± â†’ Î±} (hf : Measurable f) {g : Î± â†’ â„}
    (hg : Measurable g) {n} : Measurable (birkhoffSum f g n) := by
  apply Finset.measurable_sum
  measurability

@[measurability]
lemma birkhoffMax_measurable [MeasurableSpace Î±] {f : Î± â†’ Î±} (hf : Measurable f) {g : Î± â†’ â„}
    (hg : Measurable g) {n} : Measurable (birkhoffMax f g n) := by
  unfold birkhoffMax
  induction n <;> measurability

end BirkhoffMax

section DivergentSet

open MeasureTheory Measure MeasurableSpace Filter Topology

variable {Î± : Type*}

/-- The supremum of `birkhoffSum f g (n + 1) x` over `n : â„•`. -/
noncomputable def birkhoffSup (f : Î± â†’ Î±) (g : Î± â†’ â„) (x : Î±) : EReal :=
  iSup fun n â†¦ â†‘(birkhoffSum f g (n + 1) x)

lemma birkhoffSup_measurable [MeasurableSpace Î±] {f : Î± â†’ Î±} (hf : Measurable f) {g : Î± â†’ â„}
    (hg : Measurable g) : Measurable (birkhoffSup f g) :=
  Measurable.iSup (fun _ â†¦ Measurable.coe_real_ereal (birkhoffSum_measurable hf hg))

/-- The set of points `x` for which `birkhoffSup f g x = âŠ¤`. -/
def divergentSet (f : Î± â†’ Î±) (g : Î± â†’ â„) : Set Î± := (birkhoffSup f g)â»Â¹' {âŠ¤}

lemma divergentSet_invariant {f : Î± â†’ Î±} {x g} : f x âˆˆ divergentSet f g â†” x âˆˆ divergentSet f g := by
  constructor
  all_goals
    intro hx
    simp only [divergentSet, Set.mem_preimage, birkhoffSup, Set.mem_singleton_iff, iSup_eq_top] at *
    intro M hM
    induction M using EReal.rec with
    | bot => exact âŸ¨0, EReal.bot_lt_coe _âŸ©
    | top => contradiction
    | coe a => ?_
  case mp =>
    obtain âŸ¨N, hNâŸ© := hx (- g x + a) (EReal.coe_lt_top _)
    use N + 1
    norm_cast at *
    rwa [neg_add_lt_iff_lt_add, â† birkhoffSum_succ'] at hN
  case mpr =>
    suffices âˆƒ i, g x + a < birkhoffSum f g (i + 1 + 1) x by
      norm_cast at *
      obtain âŸ¨i, hiâŸ© := this
      exact âŸ¨i, by rwa [â† add_lt_add_iff_left (g x), â† birkhoffSum_succ']âŸ©
    obtain âŸ¨N, hNâŸ© := hx (g x + a) (EReal.coe_lt_top _)
    match N with
    | 0 =>
      obtain âŸ¨M, hMâŸ© := hx â†‘(birkhoffSum f g 1 x) (EReal.coe_lt_top _)
      match M with
      | 0 => simp_all
      | N + 1 => exact âŸ¨N, lt_trans (EReal.coe_lt_coe_iff.mp hN) <| EReal.coe_lt_coe_iff.mp hMâŸ©
    | N + 1 => exact âŸ¨N, EReal.coe_lt_coe_iff.mp hNâŸ©

lemma divergentSet_measurable {f : Î± â†’ Î±} [MeasurableSpace Î±] (hf : Measurable f) {g : Î± â†’ â„}
    (hg : Measurable g) : MeasurableSet (divergentSet f g) :=
  measurableSet_preimage (birkhoffSup_measurable hf hg) (measurableSet_singleton _)

lemma divergentSet_mem_invalg [MeasurableSpace Î±] {f : Î± â†’ Î±} (hf : Measurable f) {g : Î± â†’ â„}
    (hg : Measurable g) : MeasurableSet[invariants f] (divergentSet f g) :=
  âŸ¨divergentSet_measurable hf hg, funext (fun _ â†¦ propext divergentSet_invariant)âŸ©

lemma birkhoffMax_tendsto_top_mem_divergentSet {f : Î± â†’ Î±} {x g} (hx : x âˆˆ divergentSet f g) :
    Tendsto (birkhoffMax f g Â· x) atTop atTop := by
  apply tendsto_atTop_atTop.mpr
  intro b
  simp only [divergentSet, Set.mem_preimage, birkhoffSup, Set.mem_singleton_iff, iSup_eq_top] at hx
  obtain âŸ¨N, hNâŸ© := hx b (EReal.coe_lt_top _)
  norm_cast at hN
  use N
  exact fun n hn â†¦ le_trans (le_of_lt hN) (le_partialSups_of_le (birkhoffSum f g âˆ˜ .succ) hn x )

lemma birkhoffMaxDiff_tendsto_of_mem_divergentSet {f : Î± â†’ Î±} {x g} (hx : x âˆˆ divergentSet f g) :
    Tendsto (birkhoffMaxDiff f g Â· x) atTop (ğ“ (g x)) := by
  have hx' : f x âˆˆ divergentSet f g := divergentSet_invariant.mpr hx
  simp_rw [birkhoffMaxDiff_aux]
  nth_rw 2 [â† sub_zero (g x)]
  apply Tendsto.sub tendsto_const_nhds
  obtain âŸ¨N, hNâŸ© := tendsto_atTop_atTop.mp (birkhoffMax_tendsto_top_mem_divergentSet hx') 0
  exact tendsto_atTop_of_eventually_const (iâ‚€ := N) fun i hi â†¦ inf_of_le_left (hN i hi)

/-- Condition that `a` is an upper bound for `limsup u f`, when it exists.
See `Filter.limsup_le_iff` for the proof of this equivalence. -/
def limsup_le {Î± Î²} [LT Î²] (u : Î± â†’ Î²) (f : Filter Î±) (a : Î²) : Prop :=
  âˆ€ Îµ > a, âˆ€á¶  x in f, u x < Îµ

lemma birkhoffAverage_tendsto_nonpos_of_not_mem_divergentSet {f : Î± â†’ Î±} {x g}
    (hx : x âˆ‰ divergentSet f g) : limsup_le (birkhoffAverage â„ f g Â· x) atTop 0 := by
  /- it suffices to show there are upper bounds â‰¤ Îµ for all Îµ > 0 -/
  intro Îµ hÎµ
  /- from `hx` hypothesis, the birkhoff sums are bounded above -/
  simp only [divergentSet, Set.mem_preimage, birkhoffSup, Set.mem_singleton_iff, iSup_eq_top,
    not_forall, not_exists, not_lt, exists_prop] at hx
  rcases hx with âŸ¨M', M_lt_top, M_is_boundâŸ©
  /- the upper bound is, in fact, a real number -/
  obtain âŸ¨M, hMâŸ© : âˆƒ (M : â„), âˆ€ (n : â„•), birkhoffSum f g (n + 1) x â‰¤ M := by
    induction M' using EReal.rec with
    | bot => exfalso; exact (EReal.bot_lt_coe _).not_ge (M_is_bound 0)
    | top => contradiction
    | coe M => exact âŸ¨M, by norm_cast at M_is_boundâŸ©
  /- use archimedian property of reals -/
  obtain âŸ¨N, hNâŸ© := Archimedean.arch M hÎµ
  have upperBound (n : â„•) (hn : N â‰¤ n) : birkhoffAverage â„ f g (n + 1) x < Îµ := by
    have : M < (n + 1) â€¢ Îµ := by
      exact hN.trans_lt <| smul_lt_smul_of_pos_right (Nat.lt_succ_of_le hn) hÎµ
    rw [nsmul_eq_mul] at this
    exact (inv_smul_lt_iff_of_pos (Nat.cast_pos.mpr (Nat.zero_lt_succ n))).mpr
        ((hM n).trans_lt this)
  /- conclusion -/
  apply eventually_atTop.mpr
  use N + 1
  intro n hn
  specialize upperBound n.pred (Nat.le_pred_of_lt hn)
  rwa [â† Nat.succ_pred_eq_of_pos (Nat.zero_lt_of_lt hn)]

variable {f : Î± â†’ Î±} [MeasurableSpace Î±] (Î¼ : Measure Î± := by volume_tac) {g : Î± â†’ â„}

lemma birkhoffSum_integrable (hf : MeasurePreserving f Î¼ Î¼) (hg : Integrable g Î¼) {n} :
    Integrable (birkhoffSum f g n) Î¼ :=
  integrable_finset_sum _ fun _ _ â†¦ (hf.iterate _).integrable_comp_of_integrable hg

lemma birkhoffMax_integrable (hf : MeasurePreserving f Î¼ Î¼) (hg : Integrable g Î¼) {n} :
    Integrable (birkhoffMax f g n) Î¼ := by
  unfold birkhoffMax
  induction n with
  | zero => simpa
  | succ n hn => simpa using Integrable.sup hn (birkhoffSum_integrable Î¼ hf hg)

lemma birkhoffMaxDiff_integrable (hf : MeasurePreserving f Î¼ Î¼) (hg : Integrable g Î¼) {n} :
    Integrable (birkhoffMaxDiff f g n) Î¼ := by
  apply Integrable.sub (birkhoffMax_integrable Î¼ hf hg)
  apply (integrable_map_measure _ hf.measurable.aemeasurable).mp <;> rw [hf.map_eq]
  Â· exact birkhoffMax_integrable Î¼ hf hg
  Â· exact (birkhoffMax_integrable Î¼ hf hg).aestronglyMeasurable

lemma int_birkhoffMaxDiff_in_divergentSet_tendsto (hf : MeasurePreserving f Î¼ Î¼)
    (hg : Integrable g Î¼) (hg' : Measurable g) :
    Tendsto (fun n â†¦ âˆ« x in divergentSet f g, birkhoffMaxDiff f g n x âˆ‚Î¼) atTop
            (ğ“ <| âˆ« x in divergentSet f g, g x âˆ‚ Î¼) := by
  apply MeasureTheory.tendsto_integral_of_dominated_convergence
    (abs g âŠ” abs (birkhoffMaxDiff f g 0))
  Â· exact fun _ â†¦ (birkhoffMaxDiff_integrable Î¼ hf hg).aestronglyMeasurable.restrict
  Â· apply Integrable.sup <;> apply Integrable.abs
    Â· exact hg.restrict
    Â· exact (birkhoffMaxDiff_integrable Î¼ hf hg).restrict
  Â· intro n
    apply ae_of_all
    intro x
    rw [Real.norm_eq_abs]
    exact abs_le_max_abs_abs (by simp [birkhoffMaxDiff_aux])
      (birkhoffMaxDiff_antitone (zero_le n) _)
  Â· exact (ae_restrict_iff' (divergentSet_measurable hf.measurable hg')).mpr
      (ae_of_all _ fun _ hx â†¦ birkhoffMaxDiff_tendsto_of_mem_divergentSet hx)

lemma int_birkhoffMaxDiff_in_divergentSet_nonneg (hf : MeasurePreserving f Î¼ Î¼)
    (hg : Integrable g Î¼) (hg' : Measurable g) {n} :
    0 â‰¤ âˆ« x in divergentSet f g, birkhoffMaxDiff f g n x âˆ‚Î¼ := by
  unfold birkhoffMaxDiff
  have : (Î¼.restrict (divergentSet f g)).map f = Î¼.restrict (divergentSet f g) := by
    nth_rw 1 [â† (divergentSet_mem_invalg hf.measurable hg').2,
      â† Î¼.restrict_map hf.measurable (divergentSet_measurable hf.measurable hg'),
      hf.map_eq]
  have mi {n : â„•} := birkhoffMax_integrable Î¼ hf hg (n := n)
  have mm {n : â„•} := birkhoffMax_measurable hf.measurable hg' (n := n)
  rw [integral_sub, sub_nonneg]
  Â· rw [â† integral_map (hf.aemeasurable.restrict) mm.aestronglyMeasurable, this]
    exact integral_mono mi.restrict mi.restrict ((birkhoffMax f g).monotone (Nat.le_succ _))
  Â· exact mi.restrict
  Â· apply (integrable_map_measure mm.aestronglyMeasurable hf.aemeasurable.restrict).mp
    rw [this]
    exact mi.restrict

lemma int_in_divergentSet_nonneg (hf : MeasurePreserving f Î¼ Î¼)
    (hg : Integrable g Î¼) (hg' : Measurable g) : 0 â‰¤ âˆ« x in divergentSet f g, g x âˆ‚Î¼ :=
  le_of_tendsto_of_tendsto' tendsto_const_nhds
    (int_birkhoffMaxDiff_in_divergentSet_tendsto Î¼ hf hg hg')
    (fun _ â†¦ int_birkhoffMaxDiff_in_divergentSet_nonneg Î¼ hf hg hg')

omit [MeasurableSpace Î±] in
lemma nullMeasurableSpace_le [msÎ± : MeasurableSpace Î±] {Î¼ : Measure Î±} :
    msÎ± â‰¤ NullMeasurableSpace.instMeasurableSpace (Î± := Î±) (Î¼ := Î¼) :=
  fun s hs â†¦ âŸ¨s, hs, ae_eq_refl sâŸ©

lemma divergentSet_zero_meas_of_condexp_neg [hÎ¼ : IsProbabilityMeasure Î¼]
    (h : âˆ€áµ x âˆ‚Î¼, (Î¼[g|invariants f]) x < 0) (hf : MeasurePreserving f Î¼ Î¼)
    (hg : Integrable g Î¼) (hg' : Measurable g) :
    Î¼ (divergentSet f g) = 0 := by
  have pos : âˆ€áµ x âˆ‚Î¼.restrict (divergentSet f g), 0 < -(Î¼[g|invariants f]) x := by
    exact ae_restrict_of_ae (h.mono fun _ hx â†¦ neg_pos.mpr hx)
  have ds_meas := divergentSet_mem_invalg hf.measurable hg'
  by_contra hm; simp_rw [â† pos_iff_ne_zero] at hm
  have : âˆ« x in divergentSet f g, g x âˆ‚Î¼ < 0 := by
    rw [â† setIntegral_condExp (invariants_le f) hg ds_meas,
      â† Left.neg_pos_iff, â† integral_neg, integral_pos_iff_support_of_nonneg_ae]
    Â· unfold Function.support
      rw [(ae_iff_measure_eq _).mp]
      Â· rwa [Measure.restrict_apply_univ _]
      Â· conv in _ â‰  _ => rw [ne_comm]
        exact Eventually.ne_of_lt pos
      Â· apply measurableSet_support _
        apply (stronglyMeasurable_condExp).measurable.neg.le _
        refine (le_trans (invariants_le f) nullMeasurableSpace_le)
    Â· exact ae_le_of_ae_lt pos
    Â· exact integrable_condExp.restrict.neg
  exact this.not_ge (int_in_divergentSet_nonneg Î¼ hf hg hg')

lemma ae_tendsTo_birkhoffAverage_of_condExp_neg [hÎ¼ : IsProbabilityMeasure Î¼]
    (hf : MeasurePreserving f Î¼ Î¼) (hg : Integrable g Î¼) (hg' : Measurable g)
    (h : âˆ€áµ x âˆ‚Î¼, (Î¼[g|invariants f]) x < 0) :
    âˆ€áµ x âˆ‚Î¼, limsup_le (birkhoffAverage â„ f g Â· x) atTop 0 := by
  apply Eventually.mono _ fun _ â†¦ birkhoffAverage_tendsto_nonpos_of_not_mem_divergentSet
  apply ae_iff.mpr
  simp only [not_not, Set.setOf_mem_eq]
  exact divergentSet_zero_meas_of_condexp_neg Î¼ h hf hg hg'

end DivergentSet

section PointwiseErgodicTheorem

open MeasureTheory Measure MeasurableSpace Filter Topology

variable {Î± : Type*} {f : Î± â†’ Î±} [MeasurableSpace Î±] (Î¼ : Measure Î± := by volume_tac)
    {g : Î± â†’ â„} [hÎ¼ : IsProbabilityMeasure Î¼]

/-- The time average is a.e., eventually not much less than the conditional expectation. -/
lemma ae_tendsTo_birkhoffAverage_sub_condExp_nonneg {Îµ : â„} (hÎµ : 0 < Îµ)
    (hf : MeasurePreserving f Î¼ Î¼) (hg : Integrable g Î¼) (hg' : Measurable g) :
    âˆ€áµ x âˆ‚Î¼, limsup_le (birkhoffAverage â„ f g Â· x - (Î¼[g|invariants f] x + Îµ)) atTop 0 := by
  -- Let `Ïˆ` denote the difference between `g` and the conditional expectation of `g` plus `Îµ`.
  let h := g - (Î¼[g|invariants f] + fun _ â†¦ Îµ)
  have h_integrable : Integrable h Î¼ := hg.sub (integrable_condExp.add (integrable_const _))
  have h_measurable : Measurable h := by
    suffices Measurable (Î¼[g|invariants f]) by measurability
    exact stronglyMeasurable_condExp.measurable.le (invariants_le f)
  -- It follows from the definition of `Ïˆ` that it is a.e. equal to `-Îµ`.
  have h_condexp_const : Î¼[h|invariants f] =áµ[Î¼] - fun _ â†¦ Îµ := calc
    _ =áµ[Î¼] Î¼[g|invariants f] - Î¼[Î¼[g|invariants f] + fun _ â†¦ Îµ|invariants f] :=
      condExp_sub hg (integrable_condExp.add (integrable_const _)) _
    _ =áµ[Î¼] Î¼[g|invariants f] - (Î¼[Î¼[g|invariants f]|invariants f] + Î¼[fun _ â†¦ Îµ|invariants f]) :=
      (condExp_add integrable_condExp (integrable_const _) _).neg.add_left
    _ =áµ[Î¼] Î¼[g|invariants f] - (Î¼[g|invariants f] + Î¼[fun _ â†¦ Îµ|invariants f]) :=
      (condExp_condExp_of_le (le_of_eq rfl) (invariants_le f)).add_right.neg.add_left
    _ = - Î¼[fun _ â†¦ Îµ|invariants f] := by simp
    _ = - fun _ â†¦ Îµ := by rw [condExp_const <| invariants_le f]
  -- For typical points the time average of `Ïˆ` is eventually non-negative.
  have limsup_nonpos : âˆ€áµ x âˆ‚Î¼, limsup_le (birkhoffAverage â„ f h Â· x) atTop 0 := by
    suffices âˆ€áµ x âˆ‚Î¼, Î¼[h|invariants f] x < 0 from
      ae_tendsTo_birkhoffAverage_of_condExp_neg Î¼ hf h_integrable h_measurable this
    exact h_condexp_const.mono fun x hx â†¦ by simp [hx, hÎµ]
  -- Transfer the result on `Ïˆ` to the required result on `g`.
  refine limsup_nonpos.mono fun x hx => ?_
  suffices âˆ€ (n : â„•), n â‰  0 â†’
      birkhoffAverage â„ f h n x = birkhoffAverage â„ f g n x - (Î¼[g|invariants f] x + Îµ) by
    simp only [limsup_le, eventually_atTop] at hx âŠ¢
    intro r hr
    obtain âŸ¨n, hnâŸ© := hx r hr
    refine âŸ¨n + 1, fun k hk â†¦ ?_âŸ©
    rw [â† this k (Nat.ne_zero_of_lt hk)]
    exact hn k (Nat.le_of_succ_le hk)
  intro n hn
  have : Î¼[g|invariants f] âˆ˜ f = Î¼[g|invariants f] :=
    comp_eq_of_measurable_invariants stronglyMeasurable_condExp.measurable
  simp [h, birkhoffAverage_sub, birkhoffAverage_add, birkhoffAverage_of_comp_eq
    (show _ = fun _ â†¦ Îµ from rfl) hn, birkhoffAverage_of_comp_eq this hn]

/-- Same as the main result `ae_tendsTo_birkhoffAverage_condExp` but assuming `Measurable g`. -/
private lemma ae_tendsTo_birkhoffAverage_condExp_aux
    (hf : MeasurePreserving f Î¼ Î¼) (hg : Integrable g Î¼) (hg' : Measurable g) :
    âˆ€áµ x âˆ‚Î¼, Tendsto (birkhoffAverage â„ f g Â· x) atTop (ğ“ (Î¼[g|invariants f] x)) := by
  have : âˆ€áµ x âˆ‚Î¼, âˆ€ (k : {k : â„• // k > 0}),
      âˆ€á¶  n in atTop, |birkhoffAverage â„ f g n x - (Î¼[g|invariants f] x)| < (k : â„)â»Â¹ := by
    apply ae_all_iff.mpr
    rintro âŸ¨k, hkâŸ©
    let Î´ := (k : â„)â»Â¹ / 2
    have hÎ´ : Î´ > 0 := by simpa [Î´]
    have pâ‚ := ae_tendsTo_birkhoffAverage_sub_condExp_nonneg Î¼ hÎ´ hf hg hg'
    have pâ‚‚ := ae_tendsTo_birkhoffAverage_sub_condExp_nonneg Î¼ hÎ´ hf hg.neg hg'.neg
    have : Î¼[-g|invariants f] =áµ[Î¼] - Î¼[g|invariants f] := condExp_neg _ _
    refine ((pâ‚.and pâ‚‚).and this).mono fun x âŸ¨âŸ¨hxâ‚, hxâ‚‚âŸ©, hxâ‚ƒâŸ© => ?_
    simp only [limsup_le, eventually_atTop] at hxâ‚ hxâ‚‚ âŠ¢
    obtain âŸ¨nâ‚, hnâ‚âŸ© := hxâ‚ Î´ hÎ´
    obtain âŸ¨nâ‚‚, hnâ‚‚âŸ© := hxâ‚‚ Î´ hÎ´
    simp_rw [Î´] at hnâ‚ hnâ‚‚ âŠ¢
    refine âŸ¨max nâ‚ nâ‚‚, fun m hm â†¦ abs_lt.mpr âŸ¨?_, ?_âŸ©âŸ©
    Â· specialize hnâ‚‚ m (le_of_max_le_right hm)
      rw [hxâ‚ƒ, birkhoffAverage_neg] at hnâ‚‚
      norm_num at hnâ‚‚
      linarith
    Â· specialize hnâ‚ m (le_of_max_le_left hm)
      linarith
  refine this.mono fun x hx â†¦ Metric.tendsto_atTop.mpr fun Îµ hÎµ â†¦ ?_
  obtain âŸ¨k, hkâŸ© := Archimedean.arch 1 hÎµ
  have hk' : 1 < (k + 1) â€¢ Îµ :=
    hk.trans_lt <| smul_lt_smul_of_pos_right (lt_add_one k) hÎµ
  simp only [eventually_atTop, ge_iff_le, Subtype.forall, gt_iff_lt] at hx
  obtain âŸ¨N, hNâŸ© := hx k.succ (Nat.zero_lt_succ k)
  refine âŸ¨N, fun n hn â†¦ ?_âŸ©
  apply (hN n hn).trans
  rw [inv_lt_iff_one_lt_mulâ‚€ (Nat.cast_pos.mpr k.succ_pos)]
  norm_num at hk' âŠ¢
  linarith

/-- **Pointwise Ergodic Theorem** a.k.a. **Birkhoff's Ergodic Theorem**

Time average coincides with conditional expectation for typical points. -/
public theorem ae_tendsTo_birkhoffAverage_condExp {g : Î± â†’ â„} (hf : MeasurePreserving f Î¼ Î¼)
    (hg : Integrable g Î¼) :
    âˆ€áµ x âˆ‚Î¼, Tendsto (birkhoffAverage â„ f g Â· x) atTop (ğ“ (Î¼[g|invariants f] x)) := by
  let h := hg.left.mk
  have g_ae_eq_h : g =áµ[Î¼] h := hg.left.ae_eq_mk
  have h_integrable : Integrable h Î¼ := (integrable_congr hg.left.ae_eq_mk).mp hg
  have h1 := condExp_congr_ae (m := invariants f) g_ae_eq_h
  have h2 := ae_tendsTo_birkhoffAverage_condExp_aux Î¼ hf h_integrable hg.left.measurable_mk
  have h3 := ae_all_iff.mpr <| hf.quasiMeasurePreserving.birkhoffAverage_ae_eq_of_ae_eq â„ g_ae_eq_h
  filter_upwards [h1, h2, h3] with _ h1' h2' h3'
  simp [h1', h2', h3']

end PointwiseErgodicTheorem
