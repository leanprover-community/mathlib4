/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Algebra.BigOperators.Basic
import Mathlib.Dynamics.FixedPoints.Basic

/-!
# Birkhoff sums

In this file we define `birkhoffSum f g n x` to be the sum `‚àë k in Finset.range n, g (f^[k] x)`.
This sum (more precisely, the corresponding average `n‚Åª¬π ‚Ä¢ birkhoffSum f g n x`)
appears in various ergodic theorems
saying that these averages converge to the "space average" `‚®ç x, g x ‚àÇŒº` in some sense.

See also `birkhoffAverage` defined in `Dynamics/BirkhoffSum/Average`.
-/

open Finset Function
open scoped BigOperators

section AddCommMonoid

variable {Œ± M : Type*} [AddCommMonoid M]

/-- The sum of values of `g` on the first `n` points of the orbit of `x` under `f`. -/
def birkhoffSum (f : Œ± ‚Üí Œ±) (g : Œ± ‚Üí M) (n : ‚Ñï) (x : Œ±) : M := ‚àë k in range n, g (f^[k] x)

theorem birkhoffSum_zero (f : Œ± ‚Üí Œ±) (g : Œ± ‚Üí M) (x : Œ±) : birkhoffSum f g 0 x = 0 :=
  sum_range_zero _

@[simp]
theorem birkhoffSum_zero' (f : Œ± ‚Üí Œ±) (g : Œ± ‚Üí M) : birkhoffSum f g 0 = 0 :=
  funext <| birkhoffSum_zero _ _

theorem birkhoffSum_one (f : Œ± ‚Üí Œ±) (g : Œ± ‚Üí M) (x : Œ±) : birkhoffSum f g 1 x = g x :=
  sum_range_one _

@[simp]
theorem birkhoffSum_one' (f : Œ± ‚Üí Œ±) (g : Œ± ‚Üí M) : birkhoffSum f g 1 = g :=
  funext <| birkhoffSum_one f g

theorem birkhoffSum_succ (f : Œ± ‚Üí Œ±) (g : Œ± ‚Üí M) (n : ‚Ñï) (x : Œ±) :
    birkhoffSum f g (n + 1) x = birkhoffSum f g n x + g (f^[n] x) :=
  sum_range_succ _ _

theorem birkhoffSum_succ' (f : Œ± ‚Üí Œ±) (g : Œ± ‚Üí M) (n : ‚Ñï) (x : Œ±) :
    birkhoffSum f g (n + 1) x = g x + birkhoffSum f g n (f x) :=
  (sum_range_succ' _ _).trans (add_comm _ _)

theorem birkhoffSum_add (f : Œ± ‚Üí Œ±) (g : Œ± ‚Üí M) (m n : ‚Ñï) (x : Œ±) :
    birkhoffSum f g (m + n) x = birkhoffSum f g m x + birkhoffSum f g n (f^[m] x) := by
  simp_rw [birkhoffSum, sum_range_add, add_comm m, iterate_add_apply]
  -- üéâ no goals

theorem Function.IsFixedPt.birkhoffSum_eq {f : Œ± ‚Üí Œ±} {x : Œ±} (h : IsFixedPt f x) (g : Œ± ‚Üí M)
    (n : ‚Ñï) : birkhoffSum f g n x = n ‚Ä¢ g x := by
  simp [birkhoffSum, (h.iterate _).eq]
  -- üéâ no goals

theorem map_birkhoffSum {F N : Type*} [AddCommMonoid N] [AddMonoidHomClass F M N]
    (g' : F) (f : Œ± ‚Üí Œ±) (g : Œ± ‚Üí M) (n : ‚Ñï) (x : Œ±) :
    g' (birkhoffSum f g n x) = birkhoffSum f (g' ‚àò g) n x :=
  map_sum g' _ _

end AddCommMonoid

section AddCommGroup

variable {Œ± G : Type*} [AddCommGroup G]

/-- Birkhoff sum is "almost invariant" under `f`:
the difference between `birkhoffSum f g n (f x)` and `birkhoffSum f g n x`
is equal to `g (f^[n] x) - g x`. -/
theorem birkhoffSum_apply_sub_birkhoffSum (f : Œ± ‚Üí Œ±) (g : Œ± ‚Üí G) (n : ‚Ñï) (x : Œ±) :
    birkhoffSum f g n (f x) - birkhoffSum f g n x = g (f^[n] x) - g x := by
  rw [‚Üê sub_eq_iff_eq_add.2 (birkhoffSum_succ f g n x),
    ‚Üê sub_eq_iff_eq_add.2 (birkhoffSum_succ' f g n x),
    ‚Üê sub_add, ‚Üê sub_add, sub_add_comm]

end AddCommGroup
