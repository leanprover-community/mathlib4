/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.RCLike.Basic
import Mathlib.Dynamics.BirkhoffSum.Average

/-!
# Birkhoff average in a normed space

In this file we prove some lemmas about the Birkhoff average (`birkhoffAverage`)
of a function which takes values in a normed space over `‚Ñù` or `‚ÑÇ`.

At the time of writing, all lemmas in this file
are motivated by the proof of the von Neumann Mean Ergodic Theorem,
see `LinearIsometry.tendsto_birkhoffAverage_orthogonalProjection`.
-/

open Function Set Filter
open scoped Topology ENNReal Uniformity

section

variable {Œ± E : Type*}

/-- The Birkhoff averages of a function `g` over the orbit of a fixed point `x` of `f`
tend to `g x` as `N ‚Üí ‚àû`. In fact, they are equal to `g x` for all `N ‚â† 0`,
see `Function.IsFixedPt.birkhoffAverage_eq`.

TODO: add a version for a periodic orbit. -/
theorem Function.IsFixedPt.tendsto_birkhoffAverage
    (R : Type*) [DivisionSemiring R] [CharZero R]
    [AddCommMonoid E] [TopologicalSpace E] [Module R E]
    {f : Œ± ‚Üí Œ±} {x : Œ±} (h : f.IsFixedPt x) (g : Œ± ‚Üí E) :
    Tendsto (birkhoffAverage R f g ¬∑ x) atTop (ùìù (g x)) :=
  tendsto_const_nhds.congr' <| (eventually_ne_atTop 0).mono fun _n hn ‚Ü¶
    (h.birkhoffAverage_eq R g hn).symm

variable [NormedAddCommGroup E]

theorem dist_birkhoffSum_apply_birkhoffSum (f : Œ± ‚Üí Œ±) (g : Œ± ‚Üí E) (n : ‚Ñï) (x : Œ±) :
    dist (birkhoffSum f g n (f x)) (birkhoffSum f g n x) = dist (g (f^[n] x)) (g x) := by
  simp only [dist_eq_norm, birkhoffSum_apply_sub_birkhoffSum]

theorem dist_birkhoffSum_birkhoffSum_le (f : Œ± ‚Üí Œ±) (g : Œ± ‚Üí E) (n : ‚Ñï) (x y : Œ±) :
    dist (birkhoffSum f g n x) (birkhoffSum f g n y) ‚â§
      ‚àë k ‚àà Finset.range n, dist (g (f^[k] x)) (g (f^[k] y)) :=
  dist_sum_sum_le _ _ _

variable (ùïú : Type*) [RCLike ùïú] [NormedSpace ùïú E]

theorem dist_birkhoffAverage_birkhoffAverage (f : Œ± ‚Üí Œ±) (g : Œ± ‚Üí E) (n : ‚Ñï) (x y : Œ±) :
    dist (birkhoffAverage ùïú f g n x) (birkhoffAverage ùïú f g n y) =
      dist (birkhoffSum f g n x) (birkhoffSum f g n y) / n := by
  simp [birkhoffAverage, dist_smul‚ÇÄ, div_eq_inv_mul]

theorem dist_birkhoffAverage_birkhoffAverage_le (f : Œ± ‚Üí Œ±) (g : Œ± ‚Üí E) (n : ‚Ñï) (x y : Œ±) :
    dist (birkhoffAverage ùïú f g n x) (birkhoffAverage ùïú f g n y) ‚â§
      (‚àë k ‚àà Finset.range n, dist (g (f^[k] x)) (g (f^[k] y))) / n :=
  (dist_birkhoffAverage_birkhoffAverage _ _ _ _ _ _).trans_le <| by
    gcongr; apply dist_birkhoffSum_birkhoffSum_le

theorem dist_birkhoffAverage_apply_birkhoffAverage (f : Œ± ‚Üí Œ±) (g : Œ± ‚Üí E) (n : ‚Ñï) (x : Œ±) :
    dist (birkhoffAverage ùïú f g n (f x)) (birkhoffAverage ùïú f g n x) =
      dist (g (f^[n] x)) (g x) / n := by
  simp [dist_birkhoffAverage_birkhoffAverage, dist_birkhoffSum_apply_birkhoffSum]

/-- If a function `g` is bounded along the positive orbit of `x` under `f`,
then the difference between Birkhoff averages of `g`
along the orbit of `f x` and along the orbit of `x`
tends to zero.

See also `tendsto_birkhoffAverage_apply_sub_birkhoffAverage'`. -/
theorem tendsto_birkhoffAverage_apply_sub_birkhoffAverage {f : Œ± ‚Üí Œ±} {g : Œ± ‚Üí E} {x : Œ±}
    (h : Bornology.IsBounded (range (g <| f^[¬∑] x))) :
    Tendsto (fun n ‚Ü¶ birkhoffAverage ùïú f g n (f x) - birkhoffAverage ùïú f g n x) atTop (ùìù 0) := by
  rcases Metric.isBounded_range_iff.1 h with ‚ü®C, hC‚ü©
  have : Tendsto (fun n : ‚Ñï ‚Ü¶ C / n) atTop (ùìù 0) :=
    tendsto_const_nhds.div_atTop tendsto_natCast_atTop_atTop
  refine squeeze_zero_norm (fun n ‚Ü¶ ?_) this
  rw [‚Üê dist_eq_norm, dist_birkhoffAverage_apply_birkhoffAverage]
  gcongr
  exact hC n 0

/-- If a function `g` is bounded,
then the difference between Birkhoff averages of `g`
along the orbit of `f x` and along the orbit of `x`
tends to zero.

See also `tendsto_birkhoffAverage_apply_sub_birkhoffAverage`. -/
theorem tendsto_birkhoffAverage_apply_sub_birkhoffAverage' {g : Œ± ‚Üí E}
    (h : Bornology.IsBounded (range g)) (f : Œ± ‚Üí Œ±) (x : Œ±) :
    Tendsto (fun n ‚Ü¶ birkhoffAverage ùïú f g n (f x) - birkhoffAverage ùïú f g n x) atTop (ùìù 0) :=
  tendsto_birkhoffAverage_apply_sub_birkhoffAverage _ <| h.subset <| range_comp_subset_range _ _

end

variable (ùïú : Type*) {X E : Type*}
  [PseudoEMetricSpace X] [RCLike ùïú] [NormedAddCommGroup E] [NormedSpace ùïú E]
  {f : X ‚Üí X} {g : X ‚Üí E} {l : X ‚Üí E}

/-- If `f` is a non-strictly contracting map (i.e., it is Lipschitz with constant `1`)
and `g` is a uniformly continuous, then the Birkhoff averages of `g` along orbits of `f`
is a uniformly equicontinuous family of functions. -/
theorem uniformEquicontinuous_birkhoffAverage (hf : LipschitzWith 1 f) (hg : UniformContinuous g) :
    UniformEquicontinuous (birkhoffAverage ùïú f g) := by
  refine Metric.uniformity_basis_dist_le.uniformEquicontinuous_iff_right.2 fun Œµ hŒµ ‚Ü¶ ?_
  rcases (uniformity_basis_edist_le.uniformContinuous_iff Metric.uniformity_basis_dist_le).1 hg Œµ hŒµ
    with ‚ü®Œ¥, hŒ¥‚ÇÄ, hŒ¥Œµ‚ü©
  refine mem_uniformity_edist.2 ‚ü®Œ¥, hŒ¥‚ÇÄ, fun {x y} h n ‚Ü¶ ?_‚ü©
  calc
    dist (birkhoffAverage ùïú f g n x) (birkhoffAverage ùïú f g n y)
      ‚â§ (‚àë k ‚àà Finset.range n, dist (g (f^[k] x)) (g (f^[k] y))) / n :=
      dist_birkhoffAverage_birkhoffAverage_le ..
    _ ‚â§ (‚àë _k ‚àà Finset.range n, Œµ) / n := by
      gcongr
      refine hŒ¥Œµ _ _ ?_
      simpa using (hf.iterate _).edist_le_mul_of_le h.le
    _ = n * Œµ / n := by simp
    _ ‚â§ Œµ := by
      rcases eq_or_ne n 0 with hn | hn <;> simp [hn, hŒµ.le, mul_div_cancel_left‚ÇÄ]

/-- If `f : X ‚Üí X` is a non-strictly contracting map (i.e., it is Lipschitz with constant `1`),
`g : X ‚Üí E` is a uniformly continuous, and `l : X ‚Üí E` is a continuous function,
then the set of points `x`
such that the Birkhoff average of `g` along the orbit of `x` tends to `l x`
is a closed set. -/
theorem isClosed_setOf_tendsto_birkhoffAverage
    (hf : LipschitzWith 1 f) (hg : UniformContinuous g) (hl : Continuous l) :
    IsClosed {x | Tendsto (birkhoffAverage ùïú f g ¬∑ x) atTop (ùìù (l x))} :=
  (uniformEquicontinuous_birkhoffAverage ùïú hf hg).equicontinuous.isClosed_setOf_tendsto hl
