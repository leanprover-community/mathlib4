/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Data.IsROrC.Basic
import Mathlib.Dynamics.BirkhoffSum.Average

/-!
# Birkhoff average in a normed space

In this file we prove some lemmas about the Birkhoff average (`birkhoffAverage`)
of a function which takes values in a normed space over `â„` or `â„‚`.

At the time of writing, all lemmas in this file
are motivated by the proof of the von Neumann Mean Ergodic Theorem,
see `LinearIsometry.tendsto_birkhoffAverage_orthogonalProjection`.
-/

open Function Set Filter
open scoped Topology ENNReal Uniformity BigOperators

section

variable {Î± E : Type*}

theorem Function.IsFixedPt.tendsto_birkhoffAverage
    (R : Type*) [DivisionSemiring R] [CharZero R]
    [AddCommMonoid E] [TopologicalSpace E] [Module R E]
    {f : Î± â†’ Î±} {x : Î±} (h : f.IsFixedPt x) (g : Î± â†’ E) :
    Tendsto (birkhoffAverage R f g Â· x) atTop (ğ“ (g x)) :=
  tendsto_const_nhds.congr' <| (eventually_ne_atTop 0).mono fun _n hn â†¦
    (h.birkhoffAverage_eq R g hn).symm

variable [NormedAddCommGroup E]

theorem dist_birkhoffSum_apply_birkhoffSum (f : Î± â†’ Î±) (g : Î± â†’ E) (n : â„•) (x : Î±) :
    dist (birkhoffSum f g n (f x)) (birkhoffSum f g n x) = dist (g (f^[n] x)) (g x) := by
  simp only [dist_eq_norm, birkhoffSum_apply_sub_birkhoffSum]

theorem dist_birkhoffSum_birkhoffSum_le (f : Î± â†’ Î±) (g : Î± â†’ E) (n : â„•) (x y : Î±) :
    dist (birkhoffSum f g n x) (birkhoffSum f g n y) â‰¤
      âˆ‘ k in Finset.range n, dist (g (f^[k] x)) (g (f^[k] y)) :=
  dist_sum_sum_le _ _ _

variable (ğ•œ : Type*) [IsROrC ğ•œ] [Module ğ•œ E] [BoundedSMul ğ•œ E]

theorem dist_birkhoffAverage_birkhoffAverage (f : Î± â†’ Î±) (g : Î± â†’ E) (n : â„•) (x y : Î±) :
    dist (birkhoffAverage ğ•œ f g n x) (birkhoffAverage ğ•œ f g n y) =
      dist (birkhoffSum f g n x) (birkhoffSum f g n y) / n := by
  simp [birkhoffAverage, dist_smulâ‚€, div_eq_inv_mul]

theorem dist_birkhoffAverage_birkhoffAverage_le (f : Î± â†’ Î±) (g : Î± â†’ E) (n : â„•) (x y : Î±) :
    dist (birkhoffAverage ğ•œ f g n x) (birkhoffAverage ğ•œ f g n y) â‰¤
      (âˆ‘ k in Finset.range n, dist (g (f^[k] x)) (g (f^[k] y))) / n :=
  (dist_birkhoffAverage_birkhoffAverage _ _ _ _ _ _).trans_le <| by
    gcongr; apply dist_birkhoffSum_birkhoffSum_le

theorem dist_birkhoffAverage_apply_birkhoffAverage (f : Î± â†’ Î±) (g : Î± â†’ E) (n : â„•) (x : Î±) :
    dist (birkhoffAverage ğ•œ f g n (f x)) (birkhoffAverage ğ•œ f g n x) =
      dist (g (f^[n] x)) (g x) / n := by
  simp [dist_birkhoffAverage_birkhoffAverage, dist_birkhoffSum_apply_birkhoffSum]

theorem tendsto_birkhoffAverage_apply_sub_birkhoffAverage {f : Î± â†’ Î±} {g : Î± â†’ E} {x : Î±}
    (h : Metric.Bounded (range (g <| f^[Â·] x))) :
    Tendsto (fun n â†¦ birkhoffAverage ğ•œ f g n (f x) - birkhoffAverage ğ•œ f g n x) atTop (ğ“ 0) := by
  rcases h with âŸ¨C, hCâŸ©
  have : Tendsto (fun n : â„• â†¦ C / n) atTop (ğ“ 0) :=
    tendsto_const_nhds.div_atTop tendsto_nat_cast_atTop_atTop
  refine squeeze_zero_norm (fun n â†¦ ?_) this
  rw [â† dist_eq_norm, dist_birkhoffAverage_apply_birkhoffAverage]
  gcongr
  exact hC _ âŸ¨n, rflâŸ© _ âŸ¨0, rflâŸ©

end

variable (ğ•œ : Type*) {X E : Type*}
  [PseudoEMetricSpace X] [IsROrC ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {f : X â†’ X} {g : X â†’ E} {l : X â†’ E}

theorem uniformEquicontinuous_birkhoffAverage (hf : LipschitzWith 1 f) (hg : UniformContinuous g) :
    UniformEquicontinuous (birkhoffAverage ğ•œ f g) := by
  refine Metric.uniformity_basis_dist_le.uniformEquicontinuous_iff_right.2 fun Îµ hÎµ â†¦ ?_
  rcases (uniformity_basis_edist_le.uniformContinuous_iff Metric.uniformity_basis_dist_le).1 hg Îµ hÎµ
    with âŸ¨Î´, hÎ´â‚€, hÎ´ÎµâŸ©
  refine mem_uniformity_edist.2 âŸ¨Î´, hÎ´â‚€, fun {x y} h n â†¦ ?_âŸ©
  calc
    dist (birkhoffAverage ğ•œ f g n x) (birkhoffAverage ğ•œ f g n y)
      â‰¤ (âˆ‘ k in Finset.range n, dist (g (f^[k] x)) (g (f^[k] y))) / n :=
      dist_birkhoffAverage_birkhoffAverage_le ..
    _ â‰¤ (âˆ‘ _k in Finset.range n, Îµ) / n := by
      gcongr
      refine hÎ´Îµ _ _ ?_
      simpa using (hf.iterate _).edist_le_mul_of_le h.le
    _ = n * Îµ / n := by simp
    _ â‰¤ Îµ := by
      rcases eq_or_ne n 0 with hn | hn <;> field_simp [hn, hÎµ.le, mul_div_cancel_left]

theorem isClosed_setOf_tendsto_birkhoffAverage
    (hf : LipschitzWith 1 f) (hg : UniformContinuous g) (hl : Continuous l) :
    IsClosed {x | Tendsto (birkhoffAverage ğ•œ f g Â· x) atTop (ğ“ (l x))} :=
  (uniformEquicontinuous_birkhoffAverage ğ•œ hf hg).equicontinuous.isClosed_setOf_tendsto hl
