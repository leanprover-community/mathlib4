import Mathlib.Topology.UniformSpace.Equicontinuity

/-!
# Birkhoff average in a normed space

In this file we prove some lemmas about the Birkhoff average (`birkhoffAverage`)
of a function which takes values in a normed space over `â„` or `â„‚`.

At the time of writing, all lemmas in this file
are motivated by the proof of the von Neumann Mean Ergodic Theorem,
see `LinearIsometry.tendsto_birkhoffAverage_orthogonalProjection`.
-/

public section

open Function Set Filter
open scoped Topology ENNReal Uniformity

section

variable {Î± E : Type*}

/-- The Birkhoff averages of a function `g` over the orbit of a fixed point `x` of `f`
tend to `g x` as `N â†’ âˆ`. In fact, they are equal to `g x` for all `N â‰  0`,
see `Function.IsFixedPt.birkhoffAverage_eq`.

TODO: add a version for a periodic orbit. -/
theorem Function.IsFixedPt.tendsto_birkhoffAverage
    (R : Type*) [DivisionSemiring R] [CharZero R]
    [AddCommMonoid E] [TopologicalSpace E] [Module R E]
    {f : Î± â†’ Î±} {x : Î±} (h : f.IsFixedPt x) (g : Î± â†’ E) :
    Tendsto (birkhoffAverage R f g Â· x) atTop (ğ“ (g x)) :=
  tendsto_const_nhds.congr' <| (eventually_ne_atTop 0).mono fun _n hn â†¦
    (h.birkhoffAverage_eq R g hn).symm

variable [NormedAddCommGroup E]

theorem dist_birkhoffSum_apply_birkhoffSum (f : Î± â†’ Î±) (g : Î± â†’ E) (n : â„•) (x : Î±) :
    dist (birkhoffSum f g n (f x)) (birkhoffSum f g n x) = dist (g (f^[n] x)) (g x) := by
  simp only [dist_eq_norm, birkhoffSum_apply_sub_birkhoffSum]

theorem dist_birkhoffSum_birkhoffSum_le (f : Î± â†’ Î±) (g : Î± â†’ E) (n : â„•) (x y : Î±) :
    dist (birkhoffSum f g n x) (birkhoffSum f g n y) â‰¤
      âˆ‘ k âˆˆ Finset.range n, dist (g (f^[k] x)) (g (f^[k] y)) :=
  dist_sum_sum_le _ _ _

variable (ğ•œ : Type*) [RCLike ğ•œ] [NormedSpace ğ•œ E]

theorem dist_birkhoffAverage_birkhoffAverage (f : Î± â†’ Î±) (g : Î± â†’ E) (n : â„•) (x y : Î±) :
    dist (birkhoffAverage ğ•œ f g n x) (birkhoffAverage ğ•œ f g n y) =
      dist (birkhoffSum f g n x) (birkhoffSum f g n y) / n := by
  simp [birkhoffAverage, dist_smulâ‚€, div_eq_inv_mul]

theorem dist_birkhoffAverage_birkhoffAverage_le (f : Î± â†’ Î±) (g : Î± â†’ E) (n : â„•) (x y : Î±) :
    dist (birkhoffAverage ğ•œ f g n x) (birkhoffAverage ğ•œ f g n y) â‰¤
      (âˆ‘ k âˆˆ Finset.range n, dist (g (f^[k] x)) (g (f^[k] y))) / n :=
  (dist_birkhoffAverage_birkhoffAverage _ _ _ _ _ _).trans_le <| by
    gcongr; apply dist_birkhoffSum_birkhoffSum_le

theorem dist_birkhoffAverage_apply_birkhoffAverage (f : Î± â†’ Î±) (g : Î± â†’ E) (n : â„•) (x : Î±) :
    dist (birkhoffAverage ğ•œ f g n (f x)) (birkhoffAverage ğ•œ f g n x) =
      dist (g (f^[n] x)) (g x) / n := by
  simp [dist_birkhoffAverage_birkhoffAverage, dist_birkhoffSum_apply_birkhoffSum]

/-- If a function `g` is bounded along the positive orbit of `x` under `f`,
then the difference between Birkhoff averages of `g`
along the orbit of `f x` and along the orbit of `x`
tends to zero.

See also `tendsto_birkhoffAverage_apply_sub_birkhoffAverage'`. -/
theorem tendsto_birkhoffAverage_apply_sub_birkhoffAverage {f : Î± â†’ Î±} {g : Î± â†’ E} {x : Î±}
    (h : Bornology.IsBounded (range (g <| f^[Â·] x))) :
    Tendsto (fun n â†¦ birkhoffAverage ğ•œ f g n (f x) - birkhoffAverage ğ•œ f g n x) atTop (ğ“ 0) := by
  rcases Metric.isBounded_range_iff.1 h with âŸ¨C, hCâŸ©
  have : Tendsto (fun n : â„• â†¦ C / n) atTop (ğ“ 0) :=
    tendsto_const_nhds.div_atTop tendsto_natCast_atTop_atTop
  refine squeeze_zero_norm (fun n â†¦ ?_) this
  rw [â† dist_eq_norm, dist_birkhoffAverage_apply_birkhoffAverage]
  gcongr
  exact hC n 0

/-- If a function `g` is bounded,
then the difference between Birkhoff averages of `g`
along the orbit of `f x` and along the orbit of `x`
tends to zero.

See also `tendsto_birkhoffAverage_apply_sub_birkhoffAverage`. -/
theorem tendsto_birkhoffAverage_apply_sub_birkhoffAverage' {g : Î± â†’ E}
    (h : Bornology.IsBounded (range g)) (f : Î± â†’ Î±) (x : Î±) :
    Tendsto (fun n â†¦ birkhoffAverage ğ•œ f g n (f x) - birkhoffAverage ğ•œ f g n x) atTop (ğ“ 0) :=
  tendsto_birkhoffAverage_apply_sub_birkhoffAverage _ <| h.subset <| range_comp_subset_range _ _

end

variable (ğ•œ : Type*) {X E : Type*}
  [PseudoEMetricSpace X] [RCLike ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {f : X â†’ X} {g : X â†’ E} {l : X â†’ E}

/-- If `f` is a non-strictly contracting map (i.e., it is Lipschitz with constant `1`)
and `g` is a uniformly continuous, then the Birkhoff averages of `g` along orbits of `f`
is a uniformly equicontinuous family of functions. -/
theorem uniformEquicontinuous_birkhoffAverage (hf : LipschitzWith 1 f) (hg : UniformContinuous g) :
    UniformEquicontinuous (birkhoffAverage ğ•œ f g) := by
  refine Metric.uniformity_basis_dist_le.uniformEquicontinuous_iff_right.2 fun Îµ hÎµ â†¦ ?_
  rcases (uniformity_basis_edist_le.uniformContinuous_iff Metric.uniformity_basis_dist_le).1 hg Îµ hÎµ
    with âŸ¨Î´, hÎ´â‚€, hÎ´ÎµâŸ©
  refine mem_uniformity_edist.2 âŸ¨Î´, hÎ´â‚€, fun {x y} h n â†¦ ?_âŸ©
  calc
    dist (birkhoffAverage ğ•œ f g n x) (birkhoffAverage ğ•œ f g n y)
      â‰¤ (âˆ‘ k âˆˆ Finset.range n, dist (g (f^[k] x)) (g (f^[k] y))) / n :=
      dist_birkhoffAverage_birkhoffAverage_le ..
    _ â‰¤ (âˆ‘ _k âˆˆ Finset.range n, Îµ) / n := by
      gcongr
      refine hÎ´Îµ _ _ ?_
      simpa using (hf.iterate _).edist_le_mul_of_le h.le
    _ = n * Îµ / n := by simp
    _ â‰¤ Îµ := by
      rcases eq_or_ne n 0 with hn | hn <;> simp [hn, hÎµ.le, mul_div_cancel_leftâ‚€]

/-- If `f : X â†’ X` is a non-strictly contracting map (i.e., it is Lipschitz with constant `1`),
`g : X â†’ E` is a uniformly continuous, and `l : X â†’ E` is a continuous function,
then the set of points `x`
such that the Birkhoff average of `g` along the orbit of `x` tends to `l x`
is a closed set. -/
theorem isClosed_setOf_tendsto_birkhoffAverage
    (hf : LipschitzWith 1 f) (hg : UniformContinuous g) (hl : Continuous l) :
    IsClosed {x | Tendsto (birkhoffAverage ğ•œ f g Â· x) atTop (ğ“ (l x))} :=
  (uniformEquicontinuous_birkhoffAverage ğ•œ hf hg).equicontinuous.isClosed_setOf_tendsto hl
