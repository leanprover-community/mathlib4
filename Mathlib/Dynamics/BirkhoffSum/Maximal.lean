/-
Copyright (c) 2025 Lua Viana Reis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Lua Viana Reis
-/
module

public import Mathlib.MeasureTheory.Integral.Bochner.Basic
import Mathlib.MeasureTheory.Integral.Bochner.Set
import Mathlib.Algebra.Order.Group.PartialSups
import Mathlib.Algebra.Order.Ring.Star
import Mathlib.Analysis.InnerProductSpace.Basic
import Mathlib.Analysis.RCLike.Lemmas
import Mathlib.Data.Real.StarOrdered
import Mathlib.Dynamics.BirkhoffSum.Average

/-!
# Maximal ergodic theorem.

We define the set `birkhoffAverageSupSet f g a` of points `x` where the supremum of
`birkhoffAverage ‚Ñù f g n x` for varying `n` is greater than `a`. Then, we show its
measure multiplied by `a` is bounded above by the integral of `g` on this set. In particular,
for a positive `g`, this integral is bounded above by the `L1` norm of `g`, so for `g`
taking values in a `NormedAddCommGroup`, we get an inequality involving the norm.

## Main results

* `meas_birkhoffAverageSupSet_smul_const_le_integral`: the measure of `birkhoffAverageSupSet f g a`
   multiplied by `a` is bounded above by the integral of `g` on the same set.
* `meas_birkhoffAverageSupSet_smul_const_le_norm`: the measure of `birkhoffAverageSupSet f ‚Äñg‚Äñ a`
   multiplied by `a` is bounded above by the `L1` norm of `g`.
-/

variable {Œ± : Type*} {f : Œ± ‚Üí Œ±}

open MeasureTheory Measure MeasurableSpace Filter Topology

section BirkhoffMax

/-- The maximum of `birkhoffSum f g i` for `i` ranging from `0` to `n`. -/
def birkhoffMax (f : Œ± ‚Üí Œ±) (g : Œ± ‚Üí ‚Ñù) : ‚Ñï ‚Üío (Œ± ‚Üí ‚Ñù) :=
  partialSups (birkhoffSum f g)

lemma birkhoffMax_nonneg {g n} :
    0 ‚â§ birkhoffMax f g n := by
  apply (le_partialSups_of_le _ n.zero_le).trans'
  rfl

lemma birkhoffMax_succ {g n} :
    birkhoffMax f g (n + 1) = 0 ‚äî (g + birkhoffMax f g n ‚àò f) := by
  have : birkhoffSum f g ‚àò Nat.succ = fun k ‚Ü¶ g + birkhoffSum f g k ‚àò f := by
    funext
    exact birkhoffSum_succ' ..
  erw [partialSups_succ', this, partialSups_const_add, birkhoffSum_zero']
  funext
  simp [birkhoffMax, partialSups]

lemma birkhoffMax_succ' {g n x} (hpos : 0 < birkhoffMax f g (n + 1) x) :
    birkhoffMax f g (n + 1) x = g x + birkhoffMax f g n (f x) := by
  erw [birkhoffMax_succ, lt_sup_iff] at hpos
  cases hpos with
  | inl h => absurd h; exact lt_irrefl 0
  | inr h =>
    erw [birkhoffMax_succ, Pi.sup_apply, sup_of_le_right h.le]
    rfl

lemma birkhoffMax_comp_le_succ {g n} :
    birkhoffMax f g n ‚â§ birkhoffMax f g (n + 1) := by
  gcongr
  exact n.le_succ

lemma birkhoffMax_le_birkhoffMax {g n x} (hpos : 0 < birkhoffMax f g n x) :
    birkhoffMax f g n x ‚â§ g x + birkhoffMax f g n (f x) := by
  match n with
  | 0 => absurd hpos; exact lt_irrefl 0
  | n + 1 =>
    apply le_of_eq_of_le (birkhoffMax_succ' hpos)
    apply add_le_add_right
    exact birkhoffMax_comp_le_succ (f x)

lemma birkhoffMax_pos_of_mem_support {g n x}
    (hx : x ‚àà (birkhoffMax f g n).support) : 0 < birkhoffMax f g n x := by
  apply lt_or_gt_of_ne at hx
  cases hx with
  | inl h =>
    absurd h; exact (birkhoffMax_nonneg x).not_gt
  | inr h => exact h

-- TODO: move elsewhere
@[measurability]
lemma birkhoffSum_measurable [MeasurableSpace Œ±] {f : Œ± ‚Üí Œ±} (hf : Measurable f) {g : Œ± ‚Üí ‚Ñù}
    (hg : Measurable g) {n} : Measurable (birkhoffSum f g n) := by
  apply Finset.measurable_sum
  measurability

-- TODO: move elsewhere
@[measurability]
lemma birkhoffMax_measurable [MeasurableSpace Œ±] (hf : Measurable f) {g : Œ± ‚Üí ‚Ñù}
    (hg : Measurable g) {n} : Measurable (birkhoffMax f g n) := by
  unfold birkhoffMax
  induction n <;> measurability

section MeasurePreserving

variable {f : Œ± ‚Üí Œ±} [MeasurableSpace Œ±] (Œº : Measure Œ± := by volume_tac) {g : Œ± ‚Üí ‚Ñù} {n}
  (hf : MeasurePreserving f Œº Œº) (hg : Integrable g Œº)

include hf

-- todo: move elsewhere
@[measurability]
lemma birkhoffSum_aestronglyMeasurable (hg : AEStronglyMeasurable g Œº) :
    AEStronglyMeasurable (birkhoffSum f g n) Œº := by
  apply Finset.aestronglyMeasurable_fun_sum
  exact fun i _ => hg.comp_measurePreserving (hf.iterate i)

-- todo: move elsewhere
@[measurability]
lemma birkhoffMax_aestronglyMeasurable (hg : AEStronglyMeasurable g Œº) :
    AEStronglyMeasurable (birkhoffMax f g n) Œº := by
  unfold birkhoffMax
  induction n <;> measurability

include hg

-- todo: move elsewhere
lemma birkhoffSum_integrable : Integrable (birkhoffSum f g n) Œº :=
  integrable_finset_sum _ fun _ _ ‚Ü¶ (hf.iterate _).integrable_comp_of_integrable hg

-- todo: move elsewhere
lemma birkhoffMax_integrable : Integrable (birkhoffMax f g n) Œº := by
  unfold birkhoffMax
  induction n with
  | zero => exact integrable_zero ..
  | succ n hn => simpa using Integrable.sup hn (birkhoffSum_integrable Œº hf hg)

lemma birkhoffMax_integral_le :
    ‚à´ x, birkhoffMax f g n x ‚àÇŒº ‚â§
    ‚à´ x in (birkhoffMax f g n).support, g x ‚àÇŒº +
    ‚à´ x in (birkhoffMax f g n).support, birkhoffMax f g n (f x) ‚àÇŒº := by
  have := hf.integrable_comp_of_integrable (birkhoffMax_integrable Œº hf hg (n := n))
  rw [‚Üêintegral_add hg.restrict, ‚ÜêsetIntegral_support]
  ¬∑ apply setIntegral_mono_on‚ÇÄ
    ¬∑ exact (birkhoffMax_integrable Œº hf hg).restrict
    ¬∑ exact .add hg.restrict this.restrict
    ¬∑ exact AEStronglyMeasurable.nullMeasurableSet_support (by measurability)
    ¬∑ intro x hx
      exact birkhoffMax_le_birkhoffMax (birkhoffMax_pos_of_mem_support hx)
  ¬∑ exact this.restrict

lemma setIntegral_nonneg_on_birkhoffMax_support :
    0 ‚â§ ‚à´ x in (birkhoffMax f g n).support, g x ‚àÇŒº := by
  have hg‚ÇÅ : AEStronglyMeasurable (birkhoffMax f g n) Œº := by measurability
  have hg‚ÇÇ : Integrable (birkhoffMax f g n) Œº := birkhoffMax_integrable Œº hf hg
  have hg‚ÇÉ : Integrable (birkhoffMax f g n ‚àò f) Œº := hf.integrable_comp_of_integrable hg‚ÇÇ
  calc
    0 ‚â§ ‚à´ x in (birkhoffMax f g n).support·∂ú, birkhoffMax f g n (f x) ‚àÇŒº := by
      exact integral_nonneg (fun x  => birkhoffMax_nonneg (f x))
    _ = ‚à´ x, birkhoffMax f g n (f x) ‚àÇŒº -
        ‚à´ x in (birkhoffMax f g n).support, birkhoffMax f g n (f x) ‚àÇŒº := by
      exact setIntegral_compl‚ÇÄ hg‚ÇÅ.nullMeasurableSet_support hg‚ÇÉ
    _ = ‚à´ x, birkhoffMax f g n x ‚àÇŒº -
        ‚à´ x in (birkhoffMax f g n).support, birkhoffMax f g n (f x) ‚àÇŒº := by
      rw [‚Üêintegral_map hf.aemeasurable (hf.map_eq.symm ‚ñ∏ hg‚ÇÅ), hf.map_eq]
    _ ‚â§ ‚à´ x in (birkhoffMax f g n).support, g x ‚àÇŒº := by
      grw [birkhoffMax_integral_le Œº hf hg]
      grind

end MeasurePreserving

end BirkhoffMax

def birkhoffSupSet (f : Œ± ‚Üí Œ±) (g : Œ± ‚Üí ‚Ñù) : Set Œ± :=
  {x | ‚àÉ n : ‚Ñï, 0 < birkhoffSum f g n x}

lemma birkhoffSupSet_eq_iSup_birkhoffMax_support {f : Œ± ‚Üí Œ±} {g : Œ± ‚Üí ‚Ñù} :
    birkhoffSupSet f g = ‚ãÉ n : ‚Ñï, (birkhoffMax f g n).support := by
  ext x
  simp only [birkhoffSupSet, Set.mem_setOf_eq, Set.mem_iUnion, Function.mem_support]
  constructor
  ¬∑ refine fun ‚ü®n, hn‚ü© => ‚ü®n, ?_‚ü©
    apply ne_of_gt (hn.trans_le _)
    exact le_partialSups (birkhoffSum f g) _ _
  ¬∑ rintro ‚ü®n, hn‚ü©
    apply lt_or_gt_of_ne at hn
    cases hn with
    | inl h => absurd h; exact not_lt_of_ge (birkhoffMax_nonneg x)
    | inr h =>
      rw [birkhoffMax, Pi.partialSups_apply] at h
      rcases exists_partialSups_eq (birkhoffSum f g ¬∑ x) n with ‚ü®m, _, hm‚ÇÇ‚ü©
      exact ‚ü®m, hm‚ÇÇ ‚ñ∏ h‚ü©

public def birkhoffAverageSupSet (f : Œ± ‚Üí Œ±) (g : Œ± ‚Üí ‚Ñù) (a : ‚Ñù) : Set Œ± :=
  {x | ‚àÉ n : ‚Ñï, a < birkhoffAverage ‚Ñù f g n x}

theorem birkhoffAverage_iff_birkhoffSum {f : Œ± ‚Üí Œ±} {x n g} {a : ‚Ñù} (hn : 0 < n) :
    a < birkhoffAverage ‚Ñù f g n x ‚Üî 0 < birkhoffSum f (g - fun _ ‚Ü¶ a) n x := by
  nth_rw 2 [‚Üêsmul_lt_smul_iff_of_pos_left (a := (‚Üën : ‚Ñù)‚Åª¬π) (by positivity)]
  rw [smul_zero, ‚ÜêbirkhoffAverage, birkhoffAverage_sub]
  simp only [Pi.sub_apply, sub_pos]
  nth_rw 2 [birkhoffAverage_of_comp_eq rfl hn.ne']

theorem birkhoffAverageSupSet_eq_birkhoffSupSet {f : Œ± ‚Üí Œ±} {g a} (ha : 0 < a) :
    birkhoffAverageSupSet f g a = birkhoffSupSet f (g - fun _ ‚Ü¶ a) := by
  unfold birkhoffAverageSupSet birkhoffSupSet
  have {n x} : a < birkhoffAverage ‚Ñù f g n x ‚Üî 0 < birkhoffSum f (g - fun _ ‚Ü¶ a) n x := by
    cases n with
    | zero =>
      refine ‚ü®fun h => ?_, fun h => ?_‚ü©
      ¬∑ exfalso; rw [birkhoffAverage_zero] at h; exact lt_asymm ha h
      ¬∑ exfalso; rw [birkhoffSum_zero] at h; exact lt_irrefl 0 h
    | succ n => exact birkhoffAverage_iff_birkhoffSum (by positivity)
  conv =>
    enter [1, 1, x, 1, n]
    rw [this]

section MeasurePreserving

variable {f : Œ± ‚Üí Œ±} [MeasurableSpace Œ±] (Œº : Measure Œ± := by volume_tac) {n}
  (hf : MeasurePreserving f Œº Œº)

include hf

section Real

variable {g : Œ± ‚Üí ‚Ñù} (hg : Integrable g Œº)

include hg

lemma tendsto_setIntegral_on_birkhoffMax_support_birkhoffSupSet :
    Tendsto (fun n ‚Ü¶ ‚à´ x in (birkhoffMax f g n).support, g x ‚àÇŒº) atTop
            (ùìù <| ‚à´ x in birkhoffSupSet f g, g x ‚àÇ Œº) := by
  rw [birkhoffSupSet_eq_iSup_birkhoffMax_support]
  apply tendsto_setIntegral_of_monotone‚ÇÄ _ _ hg.integrableOn
  ¬∑ intros
    exact AEStronglyMeasurable.nullMeasurableSet_support (by measurability)
  ¬∑ intro i j hij x
    have : 0 ‚â§ birkhoffMax f g i x := birkhoffMax_nonneg x
    have := (birkhoffMax f g).mono hij x
    grind [Function.mem_support]
theorem setIntegral_nonneg_on_birkhoffSupSet :
    0 ‚â§ ‚à´ x in birkhoffSupSet f g, g x ‚àÇŒº := by
  apply ge_of_tendsto' (tendsto_setIntegral_on_birkhoffMax_support_birkhoffSupSet Œº hf hg)
  intro n
  exact setIntegral_nonneg_on_birkhoffMax_support Œº hf hg

variable [IsFiniteMeasure Œº]

/-- **Maximal ergodic theorem**: The measure of the set where the supremum of the Birkhoff
averages of `g` is greater than `a`, multiplied by `a`, is bounded above by the integral of
`g` on this set. -/
public theorem meas_birkhoffAverageSupSet_smul_const_le_integral (a : ‚Ñù) (ha : 0 < a) :
    Œº.real (birkhoffAverageSupSet f g a) ‚Ä¢ a ‚â§ ‚à´ x in birkhoffAverageSupSet f g a, g x ‚àÇŒº := by
  have p‚ÇÅ := Integrable.sub hg (integrable_const a)
  calc
    _ = ‚à´ x in birkhoffSupSet f (g - fun _ ‚Ü¶ a), a ‚àÇŒº := by
      rw [setIntegral_const, birkhoffAverageSupSet_eq_birkhoffSupSet ha]
    _ ‚â§ ‚à´ x in birkhoffSupSet f (g - fun _ ‚Ü¶ a), a ‚àÇŒº +
        ‚à´ x in birkhoffSupSet f (g - fun _ ‚Ü¶ a), g x - a ‚àÇŒº := by
      exact le_add_of_nonneg_right (setIntegral_nonneg_on_birkhoffSupSet Œº hf p‚ÇÅ)
    _ = ‚à´ x in birkhoffAverageSupSet f g a, g x ‚àÇŒº := by
      rw [‚Üê integral_add, birkhoffAverageSupSet_eq_birkhoffSupSet ha]
      ¬∑ rcongr; grind
      ¬∑ exact (integrable_const a).restrict
      ¬∑ exact p‚ÇÅ.restrict

end Real

section NormedAddCommGroup

variable {E : Type*} [NormedAddCommGroup E] {g : Œ± ‚Üí E} (hg : Integrable g Œº) [IsFiniteMeasure Œº]

include hg

/-- **Maximal ergodic theorem** for group-valued functions: The measure of the set where
the supremum of the Birkhoff averages of `‚Äñg‚Äñ` is greater than `a`, multiplied by `a`, is
bounded above by the norm of `g`. -/
public theorem meas_birkhoffAverageSupSet_smul_const_le_norm (a : ‚Ñù) (ha : 0 < a) :
    Œº.real (birkhoffAverageSupSet f (fun x ‚Ü¶ ‚Äñg x‚Äñ) a) ‚Ä¢ a ‚â§ ‚à´ x, ‚Äñg x‚Äñ ‚àÇŒº :=
  calc
    _ ‚â§ ‚à´ x in birkhoffAverageSupSet f (fun x ‚Ü¶ ‚Äñg x‚Äñ) a, ‚Äñg x‚Äñ ‚àÇŒº := by
      exact meas_birkhoffAverageSupSet_smul_const_le_integral Œº hf hg.norm a ha
    _ ‚â§ ‚à´ x, ‚Äñg x‚Äñ ‚àÇŒº := by
      exact setIntegral_le_integral hg.norm (ae_of_all _ (norm_nonneg <| g ¬∑))

end NormedAddCommGroup

end MeasurePreserving
