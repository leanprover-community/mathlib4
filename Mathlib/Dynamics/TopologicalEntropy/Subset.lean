/-
Copyright (c) 2024 Damien Thomine. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Damien Thomine, Pietro Monticone
-/
import Mathlib.Dynamics.TopologicalEntropy.NetEntropy

/-!
# Topological entropy of subsets: monotonicity, closure
Proof that the topological entropy depends monotonically on the subset. Main results
are `entropy_monotone_space‚ÇÅ`/`entropy'_monotone_space‚ÇÅ` (for the cover version)
and `entropy_monotone_space‚ÇÇ`/`entropy'_monotone_space‚ÇÇ` (for the net version). I have decided
to keep all the intermediate steps, since they may be useful in the study of other systems.

For uniformly continuous maps, proof that the entropy of a subset is the entropy of its closure.
Main results are `entropy_of_closure` and `entropy'_of_closure`.

TODO: I think one could implement a notion of Hausdorff onvergence for subsets using uniform
spaces, and then prove the semicontinuity of the topological entropy. It would be a nice
generalization of these lemmas on closures.
-/

namespace Dynamics

variable {X : Type*}

/-! ### Monotonicity of entropy as a function of the subset -/

section Subset

theorem IsDynCoverOf.of_subset {T : X ‚Üí X} {F G : Set X} (F_G : F ‚äÜ G) {U : Set (X √ó X)} {n : ‚Ñï}
    {s : Set X} (h : IsDynCoverOf T G U n s) :
    IsDynCoverOf T F U n s := F_G.trans h

theorem IsDynNetIn.of_subset {T : X ‚Üí X} {F G : Set X} (F_G : F ‚äÜ G ) {U : Set (X √ó X)} {n : ‚Ñï}
    {s : Set X} (h : IsDynNetIn T F U n s) :
    IsDynNetIn T G U n s := ‚ü®h.1.trans F_G, h.2‚ü©

theorem coverMincard_monotone_subset (T : X ‚Üí X) (U : Set (X √ó X)) (n : ‚Ñï) :
    Monotone fun F : Set X ‚Ü¶ coverMincard T F U n :=
  fun _ _ F_G ‚Ü¶ biInf_mono fun _ h ‚Ü¶ h.of_subset F_G

theorem netMaxcard_monotone_subset (T : X ‚Üí X) (U : Set (X √ó X)) (n : ‚Ñï) :
    Monotone fun F : Set X ‚Ü¶ netMaxcard T F U n :=
  fun _ _ F_G ‚Ü¶ biSup_mono fun _ h ‚Ü¶ h.of_subset F_G

open ENat ENNReal EReal Filter Nat

theorem coverEntropyInfEntourage_monotone (T : X ‚Üí X) (U : Set (X √ó X)) :
    Monotone fun F : Set X ‚Ü¶ coverEntropyInfEntourage T F U :=
  fun _ _ F_G ‚Ü¶ liminf_le_liminf <| Eventually.of_forall fun n ‚Ü¶ monotone_div_right_of_nonneg
    (cast_nonneg' n) (log_monotone (toENNReal_le.2 (coverMincard_monotone_subset T U n F_G)))

theorem coverEntropyEntourage_monotone (T : X ‚Üí X) (U : Set (X √ó X)) :
    Monotone fun F : Set X ‚Ü¶ coverEntropyEntourage T F U :=
  fun _ _ F_G ‚Ü¶ limsup_le_limsup <| Eventually.of_forall fun n ‚Ü¶ monotone_div_right_of_nonneg
    (cast_nonneg' n) (log_monotone (toENNReal_le.2 (coverMincard_monotone_subset T U n F_G)))

theorem netEntropyInfEntourage_monotone (T : X ‚Üí X) (U : Set (X √ó X)) :
    Monotone fun F : Set X ‚Ü¶ netEntropyInfEntourage T F U :=
  fun _ _ F_G ‚Ü¶ liminf_le_liminf <| Eventually.of_forall fun n ‚Ü¶ monotone_div_right_of_nonneg
    (cast_nonneg' n) (log_monotone (toENNReal_le.2 (netMaxcard_monotone_subset T U n F_G)))

theorem netEntropyEntourage_monotone (T : X ‚Üí X) (U : Set (X √ó X)) :
    Monotone fun F : Set X ‚Ü¶ netEntropyEntourage T F U :=
  fun _ _ F_G ‚Ü¶ limsup_le_limsup <| Eventually.of_forall fun n ‚Ü¶ monotone_div_right_of_nonneg
    (cast_nonneg' n) (log_monotone (toENNReal_le.2 (netMaxcard_monotone_subset T U n F_G)))

theorem coverEntropyInf_monotone [UniformSpace X] (T : X ‚Üí X) :
    Monotone fun F : Set X ‚Ü¶ coverEntropyInf T F :=
  fun _ _ F_G ‚Ü¶ iSup‚ÇÇ_mono fun U _ ‚Ü¶ coverEntropyInfEntourage_monotone T U F_G

theorem coverEntropy_monotone [UniformSpace X] (T : X ‚Üí X) :
    Monotone fun F : Set X ‚Ü¶ coverEntropy T F :=
  fun _ _ F_G ‚Ü¶ iSup‚ÇÇ_mono fun U _ ‚Ü¶ coverEntropyEntourage_monotone T U F_G

end Subset

/-! ### Topological entropy and closure -/

section Closure

open Set Uniformity UniformSpace

variable [UniformSpace X] {T : X ‚Üí X}

theorem IsDynCoverOf.closure (h : UniformContinuous T) {F : Set X} {U V : Set (X √ó X)}
    (V_uni : V ‚àà ùì§ X) {n : ‚Ñï} {s : Set X} (s_cover : IsDynCoverOf T F U n s) :
    IsDynCoverOf T (closure F) (U ‚óã V) n s := by
  rcases (hasBasis_symmetric.mem_iff' V).1 V_uni with ‚ü®W, ‚ü®W_uni, W_symm‚ü©, W_V‚ü©
  refine IsDynCoverOf.of_entourage_subset (compRel_mono (refl U) W_V) fun x x_clos ‚Ü¶ ?_
  rcases mem_closure_iff_ball.1 x_clos (dynEntourage_mem_uniformity h W_uni n) with ‚ü®y, y_x, y_F‚ü©
  rcases mem_iUnion‚ÇÇ.1 (s_cover y_F) with ‚ü®z, z_s, y_z‚ü©
  refine mem_iUnion‚ÇÇ.2 ‚ü®z, z_s, ?_‚ü©
  rw [mem_ball_symmetry (W_symm.dynEntourage T n)] at y_x
  exact ball_mono (dynEntourage_comp_subset T U W n) z (mem_ball_comp y_z y_x)

theorem coverMincard_closure_le (h : UniformContinuous T) (F : Set X) (U : Set (X √ó X))
    {V : Set (X √ó X)} (V_uni : V ‚àà ùì§ X) (n : ‚Ñï) :
    coverMincard T (closure F) (U ‚óã V) n ‚â§ coverMincard T F U n := by
  rcases eq_top_or_lt_top (coverMincard T F U n) with h' | h'
  ¬∑ exact h' ‚ñ∏ le_top
  rcases (coverMincard_finite_iff T F U n).1 h' with ‚ü®s, s_cover, s_coverMincard‚ü©
  exact s_coverMincard ‚ñ∏ (s_cover.closure h V_uni).coverMincard_le_card

open ENat ENNReal EReal Filter Nat

theorem coverEntropyInfEntourage_closure (h : UniformContinuous T) (F : Set X) (U : Set (X √ó X))
    {V : Set (X √ó X)} (V_uni : V ‚àà ùì§ X) :
    coverEntropyInfEntourage T (closure F) (U ‚óã V) ‚â§ coverEntropyInfEntourage T F U :=
  liminf_le_liminf <| Eventually.of_forall fun n ‚Ü¶ monotone_div_right_of_nonneg
    (cast_nonneg' n) (log_monotone (toENNReal_le.2 (coverMincard_closure_le h F U V_uni n)))

theorem coverEntropyEntourage_closure (h : UniformContinuous T) (F : Set X) (U : Set (X √ó X))
    {V : Set (X √ó X)} (V_uni : V ‚àà ùì§ X) :
    coverEntropyEntourage T (closure F) (U ‚óã V) ‚â§ coverEntropyEntourage T F U :=
  limsup_le_limsup <| Eventually.of_forall fun n ‚Ü¶ monotone_div_right_of_nonneg
    (cast_nonneg' n) (log_monotone (toENNReal_le.2 (coverMincard_closure_le h F U V_uni n)))

theorem coverEntropyInf_closure (h : UniformContinuous T) (F : Set X) :
    coverEntropyInf T (closure F) = coverEntropyInf T F := by
  refine (iSup‚ÇÇ_le fun U U_uni ‚Ü¶ ?_).antisymm (coverEntropyInf_monotone T subset_closure)
  rcases comp_mem_uniformity_sets U_uni with ‚ü®V, V_uni, V_U‚ü©
  exact le_iSup‚ÇÇ_of_le V V_uni ((coverEntropyInfEntourage_antitone T (closure F) V_U).trans
    (coverEntropyInfEntourage_closure h F V V_uni))

theorem coverEntropy_closure (h : UniformContinuous T) (F : Set X) :
    coverEntropy T (closure F) = coverEntropy T F := by
  refine (iSup‚ÇÇ_le fun U U_uni ‚Ü¶ ?_).antisymm (coverEntropy_monotone T subset_closure)
  rcases comp_mem_uniformity_sets U_uni with ‚ü®V, V_uni, V_U‚ü©
  exact le_iSup‚ÇÇ_of_le V V_uni ((coverEntropyEntourage_antitone T (closure F) V_U).trans
    (coverEntropyEntourage_closure h F V V_uni))

end Closure

/-! ### Topological entropy of unions -/

section Union

open Set

theorem IsDynCoverOf.union {T : X ‚Üí X} {F G : Set X} {U : Set (X √ó X)} {n : ‚Ñï} {s t : Set X}
    (hs : IsDynCoverOf T F U n s) (ht : IsDynCoverOf T G U n t) :
    IsDynCoverOf T (F ‚à™ G) U n (s ‚à™ t) :=
  union_subset (hs.trans (biUnion_subset_biUnion_left subset_union_left))
    (ht.trans (biUnion_subset_biUnion_left subset_union_right))

theorem coverMincard_union_le (T : X ‚Üí X) (F G : Set X) (U : Set (X √ó X)) (n : ‚Ñï) :
    coverMincard T (F ‚à™ G) U n ‚â§ coverMincard T F U n + coverMincard T G U n := by
  classical
  rcases eq_top_or_lt_top (coverMincard T F U n) with hF | hF
  ¬∑ rw [hF, top_add]; exact le_top
  rcases eq_top_or_lt_top (coverMincard T G U n) with hG | hG
  ¬∑ rw [hG, add_top]; exact le_top
  rcases (coverMincard_finite_iff T F U n).1 hF with ‚ü®s, s_cover, s_coverMincard‚ü©
  rcases (coverMincard_finite_iff T G U n).1 hG with ‚ü®t, t_cover, t_coverMincard‚ü©
  rw [‚Üê s_coverMincard, ‚Üê t_coverMincard, ‚Üê ENat.coe_add]
  apply (IsDynCoverOf.coverMincard_le_card _).trans (WithTop.coe_mono (s.card_union_le t))
  rw [s.coe_union t]
  exact s_cover.union t_cover

open ENNReal EReal Filter

theorem coverEntropyEntourage_union (T : X ‚Üí X) (F G : Set X) (U : Set (X √ó X)) :
    coverEntropyEntourage T (F ‚à™ G) U
      = max (coverEntropyEntourage T F U) (coverEntropyEntourage T G U) := by
  classical
  rcases F.eq_empty_or_nonempty with rfl | hF
  ¬∑ rw [empty_union, coverEntropyEntourage_empty, max_bot_left]
  apply le_antisymm _ (max_le (coverEntropyEntourage_monotone T U subset_union_left)
    (coverEntropyEntourage_monotone T U subset_union_right))
  simp only
  have key : ‚àÄ n : ‚Ñï, log (coverMincard T (F ‚à™ G) U n) / n
      ‚â§ log (max (coverMincard T F U n) (coverMincard T G U n)) / n + log (2 : ENNReal) / n := by
    intro n
    have h_logm : 0 ‚â§ log (max (coverMincard T F U n) (coverMincard T G U n)) := by
      rw [log_monotone.map_max]
      exact (log_coverMincard_nonneg T hF U n).trans (le_max_left _ _)
    rw [‚Üê div_right_distrib_of_nonneg (c := n) h_logm (zero_le_log_iff.2 one_le_two)]
    apply div_le_div_right_of_nonneg (Nat.cast_nonneg' n)
    rw [‚Üê log_mul_add, mul_two]
    apply log_monotone
    norm_cast
    exact (coverMincard_union_le T F G U n).trans (add_le_add (le_max_left _ _) (le_max_right _ _))
  apply (limsup_le_limsup (Eventually.of_forall fun n ‚Ü¶ key n)).trans
  have := (tendsto_const_div_atTop_nhds_zero_nat (bot_lt_log_iff.2 Nat.ofNat_pos).ne.symm
    (log_lt_top_iff.2 two_lt_top).ne).limsup_eq
  apply (limsup_add_le_add_limsup (Or.inr (this ‚ñ∏ zero_ne_top))
    (Or.inr (this ‚ñ∏ zero_ne_bot))).trans
  rw [coverEntropyEntourage, coverEntropyEntourage, this, add_zero, ‚Üê limsup_max]
  refine le_of_eq (limsup_congr (Eventually.of_forall fun n ‚Ü¶ ?_))
  rw [log_monotone.map_max, (monotone_div_right_of_nonneg (Nat.cast_nonneg' n)).map_max]

theorem coverEntropy_union [UniformSpace X] (T : X ‚Üí X) (F G : Set X) :
    coverEntropy T (F ‚à™ G) = max (coverEntropy T F) (coverEntropy T G) := by
  simp only [coverEntropy, iSup_subtype', ‚Üê _root_.sup_eq_max, ‚Üê iSup_sup_eq, ‚Üê iSup_subtype']
  exact biSup_congr fun U _ ‚Ü¶ coverEntropyEntourage_union T F G U

end Union

/-! ### Topological entropy of finite unions -/

section Union

open Function Set

variable {Œπ : Type*} [UniformSpace X]

theorem coverEntropyInf_iUnion_le  (T : X ‚Üí X) (F : Œπ ‚Üí Set X) :
    ‚®Ü i, coverEntropyInf T (F i) ‚â§ coverEntropyInf T (‚ãÉ i, F i) :=
  iSup_le fun i ‚Ü¶ coverEntropyInf_monotone T (subset_iUnion F i)

theorem coverEntropy_iUnion_le (T : X ‚Üí X) (F : Œπ ‚Üí Set X) :
    ‚®Ü i, coverEntropy T (F i) ‚â§ coverEntropy T (‚ãÉ i, F i) :=
  iSup_le fun i ‚Ü¶ coverEntropy_monotone T (subset_iUnion F i)

theorem coverEntropyInf_biUnion_le (s : Set Œπ) (T : X ‚Üí X) (F : Œπ ‚Üí Set X) :
    ‚®Ü i ‚àà s, coverEntropyInf T (F i) ‚â§ coverEntropyInf T (‚ãÉ i ‚àà s, F i) :=
  iSup‚ÇÇ_le fun _ i_s ‚Ü¶ coverEntropyInf_monotone T (subset_biUnion_of_mem i_s)

theorem coverEntropy_biUnion_le (s : Set Œπ) (T : X ‚Üí X) (F : Œπ ‚Üí Set X) :
    ‚®Ü i ‚àà s, coverEntropy T (F i) ‚â§ coverEntropy T (‚ãÉ i ‚àà s, F i) :=
  iSup‚ÇÇ_le fun _ i_s ‚Ü¶ coverEntropy_monotone T (subset_biUnion_of_mem i_s)

theorem coverEntropy_finite_iUnion [Fintype Œπ] (T : X ‚Üí X) (F : Œπ ‚Üí Set X) :
    coverEntropy T (‚ãÉ i, F i) = ‚®Ü i, coverEntropy T (F i) := by
  apply Fintype.induction_empty_option (P := fun Œπ _ ‚Ü¶ ‚àÄ F : Œπ ‚Üí Set X,
    coverEntropy T (‚ãÉ i, F i) = ‚®Ü i, coverEntropy T (F i))
  ¬∑ intro Œ± Œ≤ _ Œ±_Œ≤ h F
    specialize h (F ‚àò Œ±_Œ≤)
    simp only [comp_apply] at h
    rw [‚Üê iUnion_congr_of_surjective (g := F) Œ±_Œ≤ Œ±_Œ≤.surjective (fun _ ‚Ü¶ comp_apply), h]
    exact Œ±_Œ≤.iSup_comp (g := fun i : Œ≤ ‚Ü¶ coverEntropy T (F i))
  ¬∑ intro _
    rw [iUnion_of_empty, coverEntropy_empty, ciSup_of_empty]
  ¬∑ intro Œ± _ h F
    rw [iSup_option, iUnion_option, coverEntropy_union T (F none) (‚ãÉ i, F (some i)), sup_eq_max]
    congr
    exact h (fun i : Œ± ‚Ü¶ F (some i))

theorem coverEntropy_finite_biUnion (s : Finset Œπ) (T : X ‚Üí X) (F : Œπ ‚Üí Set X) :
    coverEntropy T (‚ãÉ i ‚àà s, F i) = ‚®Ü i ‚àà s, coverEntropy T (F i) := by
  have := @coverEntropy_finite_iUnion X {i // i ‚àà s} _ (FinsetCoe.fintype s) T (fun i ‚Ü¶ F i)
  rw [iSup_subtype', ‚Üê this, iUnion_subtype]

end Union

end Dynamics
