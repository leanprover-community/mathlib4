/-
Copyright (c) 2024 Damien Thomine. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Damien Thomine, Pietro Monticone
-/
import Mathlib.Tactic
import Mathlib.Topology.UniformSpace.Compact

/-!
# Dynamical uniformities
We implement Bowen-Dinaburg's definitions of the topological entropy. The most common version
of this definition uses metric spaces and then defines dynamical balls. To get a more flexible
version of topological entropy, we work instead with uniform spaces. Dynamical balls are
replaced by (what I called) dynamical uniformities.

The nomenclature may be changed.
-/

namespace DynamicalUniformity

open Prod UniformSpace

/--Shorthand for the space of uniform neighborhoods-/
notation "ð“¤" => uniformity

variable {X : Type*}

/-- A dynamical uniform neighborhood is the uniform space version of dynamical balls.-/
def DynamicalUni (T : X â†’ X) (U : Set (X Ã— X)) (n : â„•) : Set (X Ã— X) :=
  â‹‚ k < n, (map T T)^[k] â»Â¹' U

theorem dynamical_uni_inter_Ico (T : X â†’ X) (U : Set (X Ã— X)) (n : â„•) :
    DynamicalUni T U n = â‹‚ k : Set.Ico 0 n, (map T T)^[k] â»Â¹' U := by
  simp only [DynamicalUni, Set.iInter_coe_set, Set.mem_Ico, zero_le, true_and]

theorem dynamical_uni_mem (T : X â†’ X) (U : Set (X Ã— X)) (n : â„•) (x y : X) :
    (x, y) âˆˆ DynamicalUni T U n â†” âˆ€ k < n, (T^[k] x, T^[k] y) âˆˆ U := by
  simp only [DynamicalUni, map_iterate, Set.mem_preimage, Set.mem_iInter, map_apply]

theorem dynamical_balls_mem (T : X â†’ X) (U : Set (X Ã— X)) (n : â„•) (x y : X) :
    y âˆˆ ball x (DynamicalUni T U n) â†” âˆ€ k < n, T^[k] y âˆˆ ball (T^[k] x) U := by
  simp only [ball, Set.mem_preimage]
  exact dynamical_uni_mem T U n x y

theorem dynamical_uni_of_uni [UniformSpace X] {T : X â†’ X} (h : UniformContinuous T)
    {U : Set (X Ã— X)} (U_uni : U âˆˆ ð“¤ X) (n : â„•) :
    DynamicalUni T U n âˆˆ ð“¤ X := by
  rw [dynamical_uni_inter_Ico T U n]
  refine Filter.iInter_mem.2 fun k â†¦ ?_
  rw [map_iterate T T k]
  exact uniformContinuous_def.1 (UniformContinuous.iterate T k h) U U_uni

theorem dynamical_uni_of_rfl_is_rfl (T : X â†’ X) {U : Set (X Ã— X)} (h : idRel âŠ† U) (n : â„•) :
    idRel âŠ† (DynamicalUni T U n) := by
  simp only [DynamicalUni, map_iterate, Set.subset_iInter_iff, idRel_subset, Set.mem_preimage,
    map_apply]
  intro _ _ _
  apply h
  rw [mem_idRel]

theorem dynamical_uni_of_symm_is_symm (T : X â†’ X) {U : Set (X Ã— X)} (h : SymmetricRel U) (n : â„•) :
    SymmetricRel (DynamicalUni T U n) := by
  ext xy
  simp only [DynamicalUni, map_iterate, Set.mem_preimage, Set.mem_iInter]
  refine forallâ‚‚_congr fun k _ â†¦ ?_
  rw [map_apply', map_apply']
  exact SymmetricRel.mk_mem_comm h

theorem dynamical_uni_of_comp_is_comp (T : X â†’ X) (U V : Set (X Ã— X)) (n : â„•) :
    compRel (DynamicalUni T U n) (DynamicalUni T V n) âŠ† DynamicalUni T (compRel U V) n := by
  simp only [DynamicalUni, map_iterate, Set.subset_iInter_iff]
  intro k k_n xy xy_comp
  simp only [compRel, Set.mem_iInter, Set.mem_preimage, map_apply, Set.mem_setOf_eq] at xy_comp âŠ¢
  rcases xy_comp with âŸ¨z, hz1, hz2âŸ©
  exact mem_ball_comp (hz1 k k_n) (hz2 k k_n)

theorem dynamical_of_open_is_open [TopologicalSpace X] {T : X â†’ X} (T_cont : Continuous T)
    {U : Set (X Ã— X)} (U_open : IsOpen U) (n : â„•) :
    IsOpen (DynamicalUni T U n) := by
  rw [dynamical_uni_inter_Ico T U n]
  refine isOpen_iInter_of_finite fun k â†¦ ?_
  exact continuous_def.1 (Continuous.iterate (Continuous.prod_map T_cont T_cont) k) U U_open

theorem dynamical_uni_monotone_uni (T : X â†’ X) (n : â„•) :
    Monotone (fun U : Set (X Ã— X) â†¦ DynamicalUni T U n) :=
  fun _ _ h â†¦ Set.iInterâ‚‚_mono fun _ _ â†¦ Set.preimage_mono h

theorem dynamical_uni_antitone_time (T : X â†’ X) (U : Set (X Ã— X)) :
    Antitone (fun n : â„• â†¦ DynamicalUni T U n) := by
  intro m n m_n
  refine Set.iInterâ‚‚_mono' fun k k_m â†¦ ?_
  use k, lt_of_lt_of_le k_m m_n

@[simp]
theorem dynamical_time_zero {T : X â†’ X} {U : Set (X Ã— X)} :
    DynamicalUni T U 0 = Set.univ := by
  simp only [DynamicalUni, not_lt_zero', Set.iInter_of_empty, Set.iInter_univ]

@[simp]
theorem dynamical_time_one {T : X â†’ X} {U : Set (X Ã— X)} :
    DynamicalUni T U 1 = U := by
  simp [DynamicalUni]

theorem inter_of_dynamical_balls (T : X â†’ X) (n : â„•) {U : Set (X Ã— X)} (U_symm : SymmetricRel U)
    (x y : X) (h : (ball x (DynamicalUni T U n) âˆ© ball y (DynamicalUni T U n)).Nonempty) :
    x âˆˆ ball y (DynamicalUni T (compRel U U) n) := by
  rcases h with âŸ¨z, z_Bx, z_ByâŸ©
  rw [mem_ball_symmetry (dynamical_uni_of_symm_is_symm T U_symm n)] at z_Bx
  exact dynamical_uni_of_comp_is_comp T U U n (mem_ball_comp z_By z_Bx)

/--Preimages of dynamical uniformities under semiconjugacies.-/
theorem preimage_of_dynamical_uni {Y : Type*} {S : X â†’ X} {T : Y â†’ Y} {Ï† : X â†’ Y}
    (h : Function.Semiconj Ï† S T) (U : Set (Y Ã— Y)) (n : â„•) :
    (map Ï† Ï†)â»Â¹' (DynamicalUni T U n) = DynamicalUni S ((map Ï† Ï†)â»Â¹' U) n := by
  unfold DynamicalUni
  rw [Set.preimage_iInterâ‚‚]
  refine Set.iInterâ‚‚_congr fun k _ â†¦ ?_
  rw [â† Set.preimage_comp, â† Set.preimage_comp, map_iterate S S k, map_iterate T T k,
    map_comp_map, map_comp_map, (Function.Semiconj.iterate_right h k).comp_eq]

/--Notation for the product of two uniform neighborhoods.-/
def UniformityProd {Y : Type*} (U : Set (X Ã— X)) (V : Set (Y Ã— Y)) : Set ((X Ã— Y) Ã— X Ã— Y) :=
  {W : (X Ã— Y) Ã— X Ã— Y | (W.1.1, W.2.1) âˆˆ U âˆ§ (W.1.2, W.2.2) âˆˆ V}
/-Should be expanded and put into the library on uniform spaces.-/

theorem ball_prod {Y : Type*} (U : Set (X Ã— X)) (V : Set (Y Ã— Y)) (xy : X Ã— Y) :
    ball xy (UniformityProd U V) = ball xy.1 U Ã—Ë¢ ball xy.2 V := by
  ext p
  simp only [ball, UniformityProd, Set.mem_setOf_eq, Set.mem_prod, Set.mem_preimage]

theorem dynamical_uni_prod {Y : Type*} (S : X â†’ X) (T : Y â†’ Y) (U : Set (X Ã— X)) (V : Set (Y Ã— Y))
    (n : â„•) :
    DynamicalUni (map S T) (UniformityProd U V) n =
    UniformityProd (DynamicalUni S U n) (DynamicalUni T V n) := by
  ext xy
  rw [dynamical_uni_mem (map S T) (UniformityProd U V) n xy.1 xy.2]
  simp only [UniformityProd, Set.mem_setOf_eq]
  rw [dynamical_uni_mem S U n xy.1.1 xy.2.1, dynamical_uni_mem T V n xy.1.2 xy.2.2, â† forallâ‚‚_and]
  refine forallâ‚‚_congr fun k _ â†¦ ?_
  simp only [map_iterate, map_fst, map_snd]

end DynamicalUniformity

#lint
