/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.MeasureTheory.Measure.AEMeasurable

#align_import dynamics.ergodic.measure_preserving from "leanprover-community/mathlib"@"92ca63f0fb391a9ca5f22d2409a6080e786d99f7"

/-!
# Measure preserving maps

We say that `f : Î± â†’ Î²` is a measure preserving map w.r.t. measures `Î¼ : Measure Î±` and
`Î½ : Measure Î²` if `f` is measurable and `map f Î¼ = Î½`. In this file we define the predicate
`MeasureTheory.MeasurePreserving` and prove its basic properties.

We use the term "measure preserving" because in many applications `Î± = Î²` and `Î¼ = Î½`.

## References

Partially based on
[this](https://www.isa-afp.org/browser_info/current/AFP/Ergodic_Theory/Measure_Preserving_Transformations.html)
Isabelle formalization.

## Tags

measure preserving map, measure
-/


variable {Î± Î² Î³ Î´ : Type*} [MeasurableSpace Î±] [MeasurableSpace Î²] [MeasurableSpace Î³]
  [MeasurableSpace Î´]

namespace MeasureTheory

open Measure Function Set

variable {Î¼a : Measure Î±} {Î¼b : Measure Î²} {Î¼c : Measure Î³} {Î¼d : Measure Î´}

/-- `f` is a measure preserving map w.r.t. measures `Î¼a` and `Î¼b` if `f` is measurable
and `map f Î¼a = Î¼b`. -/
structure MeasurePreserving (f : Î± â†’ Î²)
  (Î¼a : Measure Î± := by volume_tac) (Î¼b : Measure Î² := by volume_tac) : Prop where
  protected measurable : Measurable f
  protected map_eq : map f Î¼a = Î¼b
#align measure_theory.measure_preserving MeasureTheory.MeasurePreserving
#align measure_theory.measure_preserving.measurable MeasureTheory.MeasurePreserving.measurable
#align measure_theory.measure_preserving.map_eq MeasureTheory.MeasurePreserving.map_eq

protected theorem _root_.Measurable.measurePreserving
    {f : Î± â†’ Î²} (h : Measurable f) (Î¼a : Measure Î±) : MeasurePreserving f Î¼a (map f Î¼a) :=
  âŸ¨h, rflâŸ©
#align measurable.measure_preserving Measurable.measurePreserving

namespace MeasurePreserving

protected theorem id (Î¼ : Measure Î±) : MeasurePreserving id Î¼ Î¼ :=
  âŸ¨measurable_id, map_idâŸ©
#align measure_theory.measure_preserving.id MeasureTheory.MeasurePreserving.id

protected theorem aemeasurable {f : Î± â†’ Î²} (hf : MeasurePreserving f Î¼a Î¼b) : AEMeasurable f Î¼a :=
  hf.1.aemeasurable
#align measure_theory.measure_preserving.ae_measurable MeasureTheory.MeasurePreserving.aemeasurable

theorem symm (e : Î± â‰ƒáµ Î²) {Î¼a : Measure Î±} {Î¼b : Measure Î²} (h : MeasurePreserving e Î¼a Î¼b) :
    MeasurePreserving e.symm Î¼b Î¼a :=
  âŸ¨e.symm.measurable, by
    rw [â† h.map_eq, map_map e.symm.measurable e.measurable, e.symm_comp_self, map_id]âŸ©
    -- ğŸ‰ no goals
#align measure_theory.measure_preserving.symm MeasureTheory.MeasurePreserving.symm

theorem restrict_preimage {f : Î± â†’ Î²} (hf : MeasurePreserving f Î¼a Î¼b) {s : Set Î²}
    (hs : MeasurableSet s) : MeasurePreserving f (Î¼a.restrict (f â»Â¹' s)) (Î¼b.restrict s) :=
  âŸ¨hf.measurable, by rw [â† hf.map_eq, restrict_map hf.measurable hs]âŸ©
                     -- ğŸ‰ no goals
#align measure_theory.measure_preserving.restrict_preimage MeasureTheory.MeasurePreserving.restrict_preimage

theorem restrict_preimage_emb {f : Î± â†’ Î²} (hf : MeasurePreserving f Î¼a Î¼b)
    (hâ‚‚ : MeasurableEmbedding f) (s : Set Î²) :
    MeasurePreserving f (Î¼a.restrict (f â»Â¹' s)) (Î¼b.restrict s) :=
  âŸ¨hf.measurable, by rw [â† hf.map_eq, hâ‚‚.restrict_map]âŸ©
                     -- ğŸ‰ no goals
#align measure_theory.measure_preserving.restrict_preimage_emb MeasureTheory.MeasurePreserving.restrict_preimage_emb

theorem restrict_image_emb {f : Î± â†’ Î²} (hf : MeasurePreserving f Î¼a Î¼b) (hâ‚‚ : MeasurableEmbedding f)
    (s : Set Î±) : MeasurePreserving f (Î¼a.restrict s) (Î¼b.restrict (f '' s)) := by
  simpa only [Set.preimage_image_eq _ hâ‚‚.injective] using hf.restrict_preimage_emb hâ‚‚ (f '' s)
  -- ğŸ‰ no goals
#align measure_theory.measure_preserving.restrict_image_emb MeasureTheory.MeasurePreserving.restrict_image_emb

theorem aemeasurable_comp_iff {f : Î± â†’ Î²} (hf : MeasurePreserving f Î¼a Î¼b)
    (hâ‚‚ : MeasurableEmbedding f) {g : Î² â†’ Î³} : AEMeasurable (g âˆ˜ f) Î¼a â†” AEMeasurable g Î¼b := by
  rw [â† hf.map_eq, hâ‚‚.aemeasurable_map_iff]
  -- ğŸ‰ no goals
#align measure_theory.measure_preserving.ae_measurable_comp_iff MeasureTheory.MeasurePreserving.aemeasurable_comp_iff

protected theorem quasiMeasurePreserving {f : Î± â†’ Î²} (hf : MeasurePreserving f Î¼a Î¼b) :
    QuasiMeasurePreserving f Î¼a Î¼b :=
  âŸ¨hf.1, hf.2.absolutelyContinuousâŸ©
#align measure_theory.measure_preserving.quasi_measure_preserving MeasureTheory.MeasurePreserving.quasiMeasurePreserving

protected theorem comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : MeasurePreserving g Î¼b Î¼c)
    (hf : MeasurePreserving f Î¼a Î¼b) : MeasurePreserving (g âˆ˜ f) Î¼a Î¼c :=
  âŸ¨hg.1.comp hf.1, by rw [â† map_map hg.1 hf.1, hf.2, hg.2]âŸ©
                      -- ğŸ‰ no goals
#align measure_theory.measure_preserving.comp MeasureTheory.MeasurePreserving.comp

protected theorem comp_left_iff {g : Î± â†’ Î²} {e : Î² â‰ƒáµ Î³} (h : MeasurePreserving e Î¼b Î¼c) :
    MeasurePreserving (e âˆ˜ g) Î¼a Î¼c â†” MeasurePreserving g Î¼a Î¼b := by
  refine' âŸ¨fun hg => _, fun hg => h.comp hgâŸ©
  -- âŠ¢ MeasurePreserving g
  convert (MeasurePreserving.symm e h).comp hg
  -- âŠ¢ g = â†‘(MeasurableEquiv.symm e) âˆ˜ â†‘e âˆ˜ g
  simp [â† Function.comp.assoc e.symm e g]
  -- ğŸ‰ no goals
#align measure_theory.measure_preserving.comp_left_iff MeasureTheory.MeasurePreserving.comp_left_iff

protected theorem comp_right_iff {g : Î± â†’ Î²} {e : Î³ â‰ƒáµ Î±} (h : MeasurePreserving e Î¼c Î¼a) :
    MeasurePreserving (g âˆ˜ e) Î¼c Î¼b â†” MeasurePreserving g Î¼a Î¼b := by
  refine' âŸ¨fun hg => _, fun hg => hg.comp hâŸ©
  -- âŠ¢ MeasurePreserving g
  convert hg.comp (MeasurePreserving.symm e h)
  -- âŠ¢ g = (g âˆ˜ â†‘e) âˆ˜ â†‘(MeasurableEquiv.symm e)
  simp [Function.comp.assoc g e e.symm]
  -- ğŸ‰ no goals
#align measure_theory.measure_preserving.comp_right_iff MeasureTheory.MeasurePreserving.comp_right_iff

protected theorem sigmaFinite {f : Î± â†’ Î²} (hf : MeasurePreserving f Î¼a Î¼b) [SigmaFinite Î¼b] :
    SigmaFinite Î¼a :=
  SigmaFinite.of_map Î¼a hf.aemeasurable (by rwa [hf.map_eq])
                                            -- ğŸ‰ no goals
#align measure_theory.measure_preserving.sigma_finite MeasureTheory.MeasurePreserving.sigmaFinite

theorem measure_preimage {f : Î± â†’ Î²} (hf : MeasurePreserving f Î¼a Î¼b) {s : Set Î²}
    (hs : MeasurableSet s) : Î¼a (f â»Â¹' s) = Î¼b s := by rw [â† hf.map_eq, map_apply hf.1 hs]
                                                       -- ğŸ‰ no goals
#align measure_theory.measure_preserving.measure_preimage MeasureTheory.MeasurePreserving.measure_preimage

theorem measure_preimage_emb {f : Î± â†’ Î²} (hf : MeasurePreserving f Î¼a Î¼b)
    (hfe : MeasurableEmbedding f) (s : Set Î²) : Î¼a (f â»Â¹' s) = Î¼b s := by
  rw [â† hf.map_eq, hfe.map_apply]
  -- ğŸ‰ no goals
#align measure_theory.measure_preserving.measure_preimage_emb MeasureTheory.MeasurePreserving.measure_preimage_emb

protected theorem iterate {f : Î± â†’ Î±} (hf : MeasurePreserving f Î¼a Î¼a) :
    âˆ€ n, MeasurePreserving f^[n] Î¼a Î¼a
  | 0 => MeasurePreserving.id Î¼a
  | n + 1 => (MeasurePreserving.iterate hf n).comp hf
#align measure_theory.measure_preserving.iterate MeasureTheory.MeasurePreserving.iterate

variable {Î¼ : Measure Î±} {f : Î± â†’ Î±} {s : Set Î±}

lemma measure_symmDiff_preimage_iterate_le
    (hf : MeasurePreserving f Î¼ Î¼) (hs : MeasurableSet s) (n : â„•) :
    Î¼ (s âˆ† (f^[n] â»Â¹' s)) â‰¤ n â€¢ Î¼ (s âˆ† (f â»Â¹' s)) := by
  induction' n with n ih; simp
  -- âŠ¢ â†‘â†‘Î¼ (s âˆ† (f^[Nat.zero] â»Â¹' s)) â‰¤ Nat.zero â€¢ â†‘â†‘Î¼ (s âˆ† (f â»Â¹' s))
                          -- âŠ¢ â†‘â†‘Î¼ (s âˆ† (f^[Nat.succ n] â»Â¹' s)) â‰¤ Nat.succ n â€¢ â†‘â†‘Î¼ (s âˆ† (f â»Â¹' s))
  simp only [add_smul, one_smul, â† n.add_one]
  -- âŠ¢ â†‘â†‘Î¼ (s âˆ† (f^[n + 1] â»Â¹' s)) â‰¤ n â€¢ â†‘â†‘Î¼ (s âˆ† (f â»Â¹' s)) + â†‘â†‘Î¼ (s âˆ† (f â»Â¹' s))
  refine' le_trans (measure_symmDiff_le s (f^[n] â»Â¹' s) (f^[n+1] â»Â¹' s)) (add_le_add ih _)
  -- âŠ¢ â†‘â†‘Î¼ ((f^[n] â»Â¹' s) âˆ† (f^[n + 1] â»Â¹' s)) â‰¤ â†‘â†‘Î¼ (s âˆ† (f â»Â¹' s))
  replace hs : MeasurableSet (s âˆ† (f â»Â¹' s)) := hs.symmDiff $ hf.measurable hs
  -- âŠ¢ â†‘â†‘Î¼ ((f^[n] â»Â¹' s) âˆ† (f^[n + 1] â»Â¹' s)) â‰¤ â†‘â†‘Î¼ (s âˆ† (f â»Â¹' s))
  rw [iterate_succ', preimage_comp, â† preimage_symmDiff, (hf.iterate n).measure_preimage hs]
  -- ğŸ‰ no goals

/-- If `Î¼ univ < n * Î¼ s` and `f` is a map preserving measure `Î¼`,
then for some `x âˆˆ s` and `0 < m < n`, `f^[m] x âˆˆ s`. -/
theorem exists_mem_image_mem_of_volume_lt_mul_volume (hf : MeasurePreserving f Î¼ Î¼)
    (hs : MeasurableSet s) {n : â„•} (hvol : Î¼ (Set.univ : Set Î±) < n * Î¼ s) :
    âˆƒ x âˆˆ s, âˆƒ m âˆˆ Set.Ioo 0 n, f^[m] x âˆˆ s := by
  have A : âˆ€ m, MeasurableSet (f^[m] â»Â¹' s) := fun m => (hf.iterate m).measurable hs
  -- âŠ¢ âˆƒ x, x âˆˆ s âˆ§ âˆƒ m, m âˆˆ Ioo 0 n âˆ§ f^[m] x âˆˆ s
  have B : âˆ€ m, Î¼ (f^[m] â»Â¹' s) = Î¼ s := fun m => (hf.iterate m).measure_preimage hs
  -- âŠ¢ âˆƒ x, x âˆˆ s âˆ§ âˆƒ m, m âˆˆ Ioo 0 n âˆ§ f^[m] x âˆˆ s
  have : Î¼ (Set.univ : Set Î±) < (Finset.range n).sum fun m => Î¼ (f^[m] â»Â¹' s) := by
    simpa only [B, nsmul_eq_mul, Finset.sum_const, Finset.card_range]
  rcases exists_nonempty_inter_of_measure_univ_lt_sum_measure Î¼ (fun m _ => A m) this with
    âŸ¨i, hi, j, hj, hij, x, hxi, hxjâŸ©
  wlog hlt : i < j generalizing i j
  -- âŠ¢ âˆƒ x, x âˆˆ s âˆ§ âˆƒ m, m âˆˆ Ioo 0 n âˆ§ f^[m] x âˆˆ s
  Â· exact this j hj i hi hij.symm hxj hxi (hij.lt_or_lt.resolve_left hlt)
    -- ğŸ‰ no goals
  simp only [Set.mem_preimage, Finset.mem_range] at hi hj hxi hxj
  -- âŠ¢ âˆƒ x, x âˆˆ s âˆ§ âˆƒ m, m âˆˆ Ioo 0 n âˆ§ f^[m] x âˆˆ s
  refine' âŸ¨f^[i] x, hxi, j - i, âŸ¨tsub_pos_of_lt hlt, lt_of_le_of_lt (j.sub_le i) hjâŸ©, _âŸ©
  -- âŠ¢ f^[j - i] (f^[i] x) âˆˆ s
  rwa [â† iterate_add_apply, tsub_add_cancel_of_le hlt.le]
  -- ğŸ‰ no goals
#align measure_theory.measure_preserving.exists_mem_image_mem_of_volume_lt_mul_volume MeasureTheory.MeasurePreserving.exists_mem_image_mem_of_volume_lt_mul_volume

/-- A self-map preserving a finite measure is conservative: if `Î¼ s â‰  0`, then at least one point
`x âˆˆ s` comes back to `s` under iterations of `f`. Actually, a.e. point of `s` comes back to `s`
infinitely many times, see `MeasureTheory.MeasurePreserving.conservative` and theorems about
`MeasureTheory.Conservative`. -/
theorem exists_mem_image_mem [IsFiniteMeasure Î¼] (hf : MeasurePreserving f Î¼ Î¼)
    (hs : MeasurableSet s) (hs' : Î¼ s â‰  0) : âˆƒ x âˆˆ s, âˆƒ (m : _) (_ : m â‰  0), f^[m] x âˆˆ s := by
  rcases ENNReal.exists_nat_mul_gt hs' (measure_ne_top Î¼ (Set.univ : Set Î±)) with âŸ¨N, hNâŸ©
  -- âŠ¢ âˆƒ x, x âˆˆ s âˆ§ âˆƒ m x_1, f^[m] x âˆˆ s
  rcases hf.exists_mem_image_mem_of_volume_lt_mul_volume hs hN with âŸ¨x, hx, m, hm, hmxâŸ©
  -- âŠ¢ âˆƒ x, x âˆˆ s âˆ§ âˆƒ m x_1, f^[m] x âˆˆ s
  exact âŸ¨x, hx, m, hm.1.ne', hmxâŸ©
  -- ğŸ‰ no goals
#align measure_theory.measure_preserving.exists_mem_image_mem MeasureTheory.MeasurePreserving.exists_mem_image_mem

end MeasurePreserving

namespace MeasurableEquiv

theorem measurePreserving_symm (Î¼ : Measure Î±) (e : Î± â‰ƒáµ Î²) :
    MeasurePreserving e.symm (map e Î¼) Î¼ :=
  (e.measurable.measurePreserving Î¼).symm _
#align measure_theory.measurable_equiv.measure_preserving_symm MeasureTheory.MeasurableEquiv.measurePreserving_symm

end MeasurableEquiv

end MeasureTheory
