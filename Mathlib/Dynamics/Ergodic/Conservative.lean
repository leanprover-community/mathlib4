/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.MeasureTheory.Constructions.BorelSpace.Basic
import Mathlib.Dynamics.Ergodic.MeasurePreserving
import Mathlib.Combinatorics.Pigeonhole

#align_import dynamics.ergodic.conservative from "leanprover-community/mathlib"@"bf6a01357ff5684b1ebcd0f1a13be314fc82c0bf"

/-!
# Conservative systems

In this file we define `f : Î± â†’ Î±` to be a *conservative* system w.r.t a measure `Î¼` if `f` is
non-singular (`MeasureTheory.QuasiMeasurePreserving`) and for every measurable set `s` of
positive measure at least one point `x âˆˆ s` returns back to `s` after some number of iterations of
`f`. There are several properties that look like they are stronger than this one but actually follow
from it:

* `MeasureTheory.Conservative.frequently_measure_inter_ne_zero`,
  `MeasureTheory.Conservative.exists_gt_measure_inter_ne_zero`: if `Î¼ s â‰  0`, then for infinitely
  many `n`, the measure of `s âˆ© f^[n] â»Â¹' s` is positive.

* `MeasureTheory.Conservative.measure_mem_forall_ge_image_not_mem_eq_zero`,
  `MeasureTheory.Conservative.ae_mem_imp_frequently_image_mem`: a.e. every point of `s` visits `s`
  infinitely many times (PoincarÃ© recurrence theorem).

We also prove the topological PoincarÃ© recurrence theorem
`MeasureTheory.Conservative.ae_frequently_mem_of_mem_nhds`. Let `f : Î± â†’ Î±` be a conservative
dynamical system on a topological space with second countable topology and measurable open
sets. Then almost every point `x : Î±` is recurrent: it visits every neighborhood `s âˆˆ ğ“ x`
infinitely many times.

## Tags

conservative dynamical system, Poincare recurrence theorem
-/


noncomputable section

open Classical Set Filter MeasureTheory Finset Function TopologicalSpace

open Classical Topology

variable {Î¹ : Type*} {Î± : Type*} [MeasurableSpace Î±] {f : Î± â†’ Î±} {s : Set Î±} {Î¼ : Measure Î±}

namespace MeasureTheory

open Measure

/-- We say that a non-singular (`MeasureTheory.QuasiMeasurePreserving`) self-map is
*conservative* if for any measurable set `s` of positive measure there exists `x âˆˆ s` such that `x`
returns back to `s` under some iteration of `f`. -/
structure Conservative (f : Î± â†’ Î±) (Î¼ : Measure Î±) extends QuasiMeasurePreserving f Î¼ Î¼ : Prop where
  exists_mem_image_mem :
    âˆ€ â¦ƒsâ¦„, MeasurableSet s â†’ Î¼ s â‰  0 â†’ âˆƒ x âˆˆ s, âˆƒ (m : _) (_ : m â‰  0), f^[m] x âˆˆ s
#align measure_theory.conservative MeasureTheory.Conservative

/-- A self-map preserving a finite measure is conservative. -/
protected theorem MeasurePreserving.conservative [IsFiniteMeasure Î¼] (h : MeasurePreserving f Î¼ Î¼) :
    Conservative f Î¼ :=
  âŸ¨h.quasiMeasurePreserving, fun _ hsm h0 => h.exists_mem_image_mem hsm h0âŸ©
#align measure_theory.measure_preserving.conservative MeasureTheory.MeasurePreserving.conservative

namespace Conservative

/-- The identity map is conservative w.r.t. any measure. -/
protected theorem id (Î¼ : Measure Î±) : Conservative id Î¼ :=
  { toQuasiMeasurePreserving := QuasiMeasurePreserving.id Î¼
    exists_mem_image_mem := fun _ _ h0 =>
      let âŸ¨x, hxâŸ© := nonempty_of_measure_ne_zero h0
      âŸ¨x, hx, 1, one_ne_zero, hxâŸ© }
#align measure_theory.conservative.id MeasureTheory.Conservative.id

/-- If `f` is a conservative map and `s` is a measurable set of nonzero measure, then
for infinitely many values of `m` a positive measure of points `x âˆˆ s` returns back to `s`
after `m` iterations of `f`. -/
theorem frequently_measure_inter_ne_zero (hf : Conservative f Î¼) (hs : MeasurableSet s)
    (h0 : Î¼ s â‰  0) : âˆƒá¶  m in atTop, Î¼ (s âˆ© f^[m] â»Â¹' s) â‰  0 := by
  by_contra H
  -- âŠ¢ False
  simp only [not_frequently, eventually_atTop, Ne.def, Classical.not_not] at H
  -- âŠ¢ False
  rcases H with âŸ¨N, hNâŸ©
  -- âŠ¢ False
  induction' N with N ihN
  -- âŠ¢ False
  Â· apply h0
    -- âŠ¢ â†‘â†‘Î¼ s = 0
    simpa using hN 0 le_rfl
    -- ğŸ‰ no goals
  rw [imp_false] at ihN
  -- âŠ¢ False
  push_neg at ihN
  -- âŠ¢ False
  rcases ihN with âŸ¨n, hn, hÎ¼nâŸ©
  -- âŠ¢ False
  set T := s âˆ© â‹ƒ n â‰¥ N + 1, f^[n] â»Â¹' s
  -- âŠ¢ False
  have hT : MeasurableSet T :=
    hs.inter (MeasurableSet.biUnion (to_countable _) fun _ _ => hf.measurable.iterate _ hs)
  have hÎ¼T : Î¼ T = 0 := by
    convert(measure_biUnion_null_iff <| to_countable _).2 hN
    rw [â† inter_iUnionâ‚‚]
    rfl
  have : Î¼ ((s âˆ© f^[n] â»Â¹' s) \ T) â‰  0 := by rwa [measure_diff_null hÎ¼T]
  -- âŠ¢ False
  rcases hf.exists_mem_image_mem ((hs.inter (hf.measurable.iterate n hs)).diff hT) this with
    âŸ¨x, âŸ¨âŸ¨hxs, _âŸ©, hxTâŸ©, m, hm0, âŸ¨_, hxmâŸ©, _âŸ©
  refine' hxT âŸ¨hxs, mem_iUnionâ‚‚.2 âŸ¨n + m, _, _âŸ©âŸ©
  -- âŠ¢ n + m â‰¥ N + 1
  Â· exact add_le_add hn (Nat.one_le_of_lt <| pos_iff_ne_zero.2 hm0)
    -- ğŸ‰ no goals
  Â· rwa [Set.mem_preimage, â† iterate_add_apply] at hxm
    -- ğŸ‰ no goals
#align measure_theory.conservative.frequently_measure_inter_ne_zero MeasureTheory.Conservative.frequently_measure_inter_ne_zero

/-- If `f` is a conservative map and `s` is a measurable set of nonzero measure, then
for an arbitrarily large `m` a positive measure of points `x âˆˆ s` returns back to `s`
after `m` iterations of `f`. -/
theorem exists_gt_measure_inter_ne_zero (hf : Conservative f Î¼) (hs : MeasurableSet s)
    (h0 : Î¼ s â‰  0) (N : â„•) : âˆƒ m > N, Î¼ (s âˆ© f^[m] â»Â¹' s) â‰  0 :=
  let âŸ¨m, hm, hmNâŸ© :=
    ((hf.frequently_measure_inter_ne_zero hs h0).and_eventually (eventually_gt_atTop N)).exists
  âŸ¨m, hmN, hmâŸ©
#align measure_theory.conservative.exists_gt_measure_inter_ne_zero MeasureTheory.Conservative.exists_gt_measure_inter_ne_zero

/-- PoincarÃ© recurrence theorem: given a conservative map `f` and a measurable set `s`, the set
of points `x âˆˆ s` such that `x` does not return to `s` after `â‰¥ n` iterations has measure zero. -/
theorem measure_mem_forall_ge_image_not_mem_eq_zero (hf : Conservative f Î¼) (hs : MeasurableSet s)
    (n : â„•) : Î¼ ({ x âˆˆ s | âˆ€ m â‰¥ n, f^[m] x âˆ‰ s }) = 0 := by
  by_contra H
  -- âŠ¢ False
  have : MeasurableSet (s âˆ© { x | âˆ€ m â‰¥ n, f^[m] x âˆ‰ s }) := by
    simp only [setOf_forall, â† compl_setOf]
    exact
      hs.inter (MeasurableSet.biInter (to_countable _) fun m _ => hf.measurable.iterate m hs.compl)
  rcases(hf.exists_gt_measure_inter_ne_zero this H) n with âŸ¨m, hmn, hmâŸ©
  -- âŠ¢ False
  rcases nonempty_of_measure_ne_zero hm with âŸ¨x, âŸ¨_, hxnâŸ©, hxm, -âŸ©
  -- âŠ¢ False
  exact hxn m hmn.lt.le hxm
  -- ğŸ‰ no goals
#align measure_theory.conservative.measure_mem_forall_ge_image_not_mem_eq_zero MeasureTheory.Conservative.measure_mem_forall_ge_image_not_mem_eq_zero

/-- PoincarÃ© recurrence theorem: given a conservative map `f` and a measurable set `s`,
almost every point `x âˆˆ s` returns back to `s` infinitely many times. -/
theorem ae_mem_imp_frequently_image_mem (hf : Conservative f Î¼) (hs : MeasurableSet s) :
    âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ âˆƒá¶  n in atTop, f^[n] x âˆˆ s := by
  simp only [frequently_atTop, @forall_swap (_ âˆˆ s), ae_all_iff]
  -- âŠ¢ âˆ€ (i : â„•), âˆ€áµ (a : Î±) âˆ‚Î¼, a âˆˆ s â†’ âˆƒ b, b â‰¥ i âˆ§ f^[b] a âˆˆ s
  intro n
  -- âŠ¢ âˆ€áµ (a : Î±) âˆ‚Î¼, a âˆˆ s â†’ âˆƒ b, b â‰¥ n âˆ§ f^[b] a âˆˆ s
  filter_upwards [measure_zero_iff_ae_nmem.1 (hf.measure_mem_forall_ge_image_not_mem_eq_zero hs n)]
  -- âŠ¢ âˆ€ (a : Î±), Â¬(a âˆˆ s âˆ§ âˆ€ (m : â„•), m â‰¥ n â†’ Â¬f^[m] a âˆˆ s) â†’ a âˆˆ s â†’ âˆƒ b, b â‰¥ n âˆ§ â€¦
  simp
  -- ğŸ‰ no goals
#align measure_theory.conservative.ae_mem_imp_frequently_image_mem MeasureTheory.Conservative.ae_mem_imp_frequently_image_mem

theorem inter_frequently_image_mem_ae_eq (hf : Conservative f Î¼) (hs : MeasurableSet s) :
    (s âˆ© { x | âˆƒá¶  n in atTop, f^[n] x âˆˆ s } : Set Î±) =áµ[Î¼] s :=
  inter_eventuallyEq_left.2 <| hf.ae_mem_imp_frequently_image_mem hs
#align measure_theory.conservative.inter_frequently_image_mem_ae_eq MeasureTheory.Conservative.inter_frequently_image_mem_ae_eq

theorem measure_inter_frequently_image_mem_eq (hf : Conservative f Î¼) (hs : MeasurableSet s) :
    Î¼ (s âˆ© { x | âˆƒá¶  n in atTop, f^[n] x âˆˆ s }) = Î¼ s :=
  measure_congr (hf.inter_frequently_image_mem_ae_eq hs)
#align measure_theory.conservative.measure_inter_frequently_image_mem_eq MeasureTheory.Conservative.measure_inter_frequently_image_mem_eq

/-- PoincarÃ© recurrence theorem: if `f` is a conservative dynamical system and `s` is a measurable
set, then for `Î¼`-a.e. `x`, if the orbit of `x` visits `s` at least once, then it visits `s`
infinitely many times.  -/
theorem ae_forall_image_mem_imp_frequently_image_mem (hf : Conservative f Î¼)
    (hs : MeasurableSet s) : âˆ€áµ x âˆ‚Î¼, âˆ€ k, f^[k] x âˆˆ s â†’ âˆƒá¶  n in atTop, f^[n] x âˆˆ s := by
  refine' ae_all_iff.2 fun k => _
  -- âŠ¢ âˆ€áµ (a : Î±) âˆ‚Î¼, f^[k] a âˆˆ s â†’ âˆƒá¶  (n : â„•) in atTop, f^[n] a âˆˆ s
  refine' (hf.ae_mem_imp_frequently_image_mem (hf.measurable.iterate k hs)).mono fun x hx hk => _
  -- âŠ¢ âˆƒá¶  (n : â„•) in atTop, f^[n] x âˆˆ s
  rw [â† map_add_atTop_eq_nat k, frequently_map]
  -- âŠ¢ âˆƒá¶  (a : â„•) in atTop, f^[a + k] x âˆˆ s
  refine' (hx hk).mono fun n hn => _
  -- âŠ¢ f^[n + k] x âˆˆ s
  rwa [add_comm, iterate_add_apply]
  -- ğŸ‰ no goals
#align measure_theory.conservative.ae_forall_image_mem_imp_frequently_image_mem MeasureTheory.Conservative.ae_forall_image_mem_imp_frequently_image_mem

/-- If `f` is a conservative self-map and `s` is a measurable set of positive measure, then
`Î¼.ae`-frequently we have `x âˆˆ s` and `s` returns to `s` under infinitely many iterations of `f`. -/
theorem frequently_ae_mem_and_frequently_image_mem (hf : Conservative f Î¼) (hs : MeasurableSet s)
    (h0 : Î¼ s â‰  0) : âˆƒáµ x âˆ‚Î¼, x âˆˆ s âˆ§ âˆƒá¶  n in atTop, f^[n] x âˆˆ s :=
  ((frequently_ae_mem_iff.2 h0).and_eventually (hf.ae_mem_imp_frequently_image_mem hs)).mono
    fun _ hx => âŸ¨hx.1, hx.2 hx.1âŸ©
#align measure_theory.conservative.frequently_ae_mem_and_frequently_image_mem MeasureTheory.Conservative.frequently_ae_mem_and_frequently_image_mem

/-- PoincarÃ© recurrence theorem. Let `f : Î± â†’ Î±` be a conservative dynamical system on a topological
space with second countable topology and measurable open sets. Then almost every point `x : Î±`
is recurrent: it visits every neighborhood `s âˆˆ ğ“ x` infinitely many times. -/
theorem ae_frequently_mem_of_mem_nhds [TopologicalSpace Î±] [SecondCountableTopology Î±]
    [OpensMeasurableSpace Î±] {f : Î± â†’ Î±} {Î¼ : Measure Î±} (h : Conservative f Î¼) :
    âˆ€áµ x âˆ‚Î¼, âˆ€ s âˆˆ ğ“ x, âˆƒá¶  n in atTop, f^[n] x âˆˆ s := by
  have : âˆ€ s âˆˆ countableBasis Î±, âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ âˆƒá¶  n in atTop, f^[n] x âˆˆ s := fun s hs =>
    h.ae_mem_imp_frequently_image_mem (isOpen_of_mem_countableBasis hs).measurableSet
  refine' ((ae_ball_iff <| countable_countableBasis Î±).2 this).mono fun x hx s hs => _
  -- âŠ¢ âˆƒá¶  (n : â„•) in atTop, f^[n] x âˆˆ s
  rcases (isBasis_countableBasis Î±).mem_nhds_iff.1 hs with âŸ¨o, hoS, hxo, hosâŸ©
  -- âŠ¢ âˆƒá¶  (n : â„•) in atTop, f^[n] x âˆˆ s
  exact (hx o hoS hxo).mono fun n hn => hos hn
  -- ğŸ‰ no goals
#align measure_theory.conservative.ae_frequently_mem_of_mem_nhds MeasureTheory.Conservative.ae_frequently_mem_of_mem_nhds

/-- Iteration of a conservative system is a conservative system. -/
protected theorem iterate (hf : Conservative f Î¼) (n : â„•) : Conservative f^[n] Î¼ := by
  cases' n with n
  -- âŠ¢ Conservative f^[Nat.zero] Î¼
  Â· exact Conservative.id Î¼
    -- ğŸ‰ no goals
  -- Discharge the trivial case `n = 0`
  refine' âŸ¨hf.1.iterate _, fun s hs hs0 => _âŸ©
  -- âŠ¢ âˆƒ x, x âˆˆ s âˆ§ âˆƒ m x_1, f^[Nat.succ n]^[m] x âˆˆ s
  rcases(hf.frequently_ae_mem_and_frequently_image_mem hs hs0).exists with âŸ¨x, _, hxâŸ©
  -- âŠ¢ âˆƒ x, x âˆˆ s âˆ§ âˆƒ m x_1, f^[Nat.succ n]^[m] x âˆˆ s
  /- We take a point `x âˆˆ s` such that `f^[k] x âˆˆ s` for infinitely many values of `k`,
    then we choose two of these values `k < l` such that `k â‰¡ l [MOD (n + 1)]`.
    Then `f^[k] x âˆˆ s` and `f^[n + 1]^[(l - k) / (n + 1)] (f^[k] x) = f^[l] x âˆˆ s`. -/
  rw [Nat.frequently_atTop_iff_infinite] at hx
  -- âŠ¢ âˆƒ x, x âˆˆ s âˆ§ âˆƒ m x_1, f^[Nat.succ n]^[m] x âˆˆ s
  rcases Nat.exists_lt_modEq_of_infinite hx n.succ_pos with âŸ¨k, hk, l, hl, hkl, hnâŸ©
  -- âŠ¢ âˆƒ x, x âˆˆ s âˆ§ âˆƒ m x_1, f^[Nat.succ n]^[m] x âˆˆ s
  set m := (l - k) / (n + 1)
  -- âŠ¢ âˆƒ x, x âˆˆ s âˆ§ âˆƒ m x_1, f^[Nat.succ n]^[m] x âˆˆ s
  have : (n + 1) * m = l - k := by
    apply Nat.mul_div_cancel'
    exact (Nat.modEq_iff_dvd' hkl.le).1 hn
  refine' âŸ¨f^[k] x, hk, m, _, _âŸ©
  -- âŠ¢ m â‰  0
  Â· intro hm
    -- âŠ¢ False
    rw [hm, mul_zero, eq_comm, tsub_eq_zero_iff_le] at this
    -- âŠ¢ False
    exact this.not_lt hkl
    -- ğŸ‰ no goals
  Â· rwa [â† iterate_mul, this, â† iterate_add_apply, tsub_add_cancel_of_le]
    -- âŠ¢ k â‰¤ l
    exact hkl.le
    -- ğŸ‰ no goals
#align measure_theory.conservative.iterate MeasureTheory.Conservative.iterate

end Conservative

end MeasureTheory
