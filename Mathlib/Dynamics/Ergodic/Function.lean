/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Dynamics.Ergodic.Ergodic
import Mathlib.MeasureTheory.Function.AEEqFun

/-!
# Functions invariant under (quasi)ergodic map

In this file we prove that an a.e. strongly measurable function `g : Î± â†’ X`
that is a.e. invariant under a (quasi)ergodic map is a.e. equal to a constant.
We prove several versions of this statement with slightly different measurability assumptions.
We also formulate a version for `MeasureTheory.AEEqFun` functions
with all a.e. equalities replaced with equalities in the quotient space.
-/

open Function Set Filter MeasureTheory Topology TopologicalSpace

variable {Î± X : Type*} [MeasurableSpace Î±] {Î¼ : MeasureTheory.Measure Î±}

/-- Let `f : Î± â†’ Î±` be a (quasi)ergodic map. Let `g : Î± â†’ X` is a null-measurable function
from `Î±` to a nonempty space with a countable family of measurable sets
separating points of a set `s` such that `f x âˆˆ s` for a.e. `x`.
If `g` that is a.e.-invariant under `f`, then `g` is a.e. constant. -/
theorem QuasiErgodic.ae_eq_const_of_ae_eq_comp_of_ae_rangeâ‚€ [Nonempty X] [MeasurableSpace X]
    {s : Set X} [HasCountableSeparatingOn X MeasurableSet s] {f : Î± â†’ Î±} {g : Î± â†’ X}
    (h : QuasiErgodic f Î¼) (hs : âˆ€áµ x âˆ‚Î¼, g x âˆˆ s) (hgm : NullMeasurable g Î¼)
    (hg_eq : g âˆ˜ f =áµ[Î¼] g) :
    âˆƒ c, g =áµ[Î¼] const Î± c := by
  refine exists_eventuallyEq_const_of_eventually_mem_of_forall_separating MeasurableSet hs ?_
  -- âŠ¢ âˆ€ (U : Set X), MeasurableSet U â†’ (âˆ€áµ (x : Î±) âˆ‚Î¼, g x âˆˆ U) âˆ¨ âˆ€áµ (x : Î±) âˆ‚Î¼, Â¬ â€¦
  refine fun U hU â†¦ h.ae_mem_or_ae_nmemâ‚€ (s := g â»Â¹' U) (hgm hU) ?_b
  -- âŠ¢ f â»Â¹' (g â»Â¹' U) =áµ[Î¼] g â»Â¹' U
  refine (hg_eq.mono fun x hx â†¦ ?_).set_eq
  -- âŠ¢ x âˆˆ f â»Â¹' (g â»Â¹' U) â†” x âˆˆ g â»Â¹' U
  rw [â† preimage_comp, mem_preimage, mem_preimage, hx]
  -- ğŸ‰ no goals

section CountableSeparatingOnUniv

variable [Nonempty X] [MeasurableSpace X] [HasCountableSeparatingOn X MeasurableSet univ]
  {f : Î± â†’ Î±} {g : Î± â†’ X}

/-- Let `f : Î± â†’ Î±` be a (pre)ergodic map.
Let `g : Î± â†’ X` be a measurable function from `Î±` to a nonempty measurable space
with a countable family of measurable sets separating the points of `X`.
If `g` is invariant under `f`, then `g` is a.e. constant. -/
theorem PreErgodic.ae_eq_const_of_ae_eq_comp (h : PreErgodic f Î¼) (hgm : Measurable g)
    (hg_eq : g âˆ˜ f = g) : âˆƒ c, g =áµ[Î¼] const Î± c :=
  exists_eventuallyEq_const_of_forall_separating MeasurableSet fun U hU â†¦
    h.ae_mem_or_ae_nmem (s := g â»Â¹' U) (hgm hU) <| by rw [â† preimage_comp, hg_eq]
                                                      -- ğŸ‰ no goals

/-- Let `f : Î± â†’ Î±` be a quasi ergodic map.
Let `g : Î± â†’ X` be a null-measurable function from `Î±` to a nonempty measurable space
with a countable family of measurable sets separating the points of `X`.
If `g` is a.e.-invariant under `f`, then `g` is a.e. constant. -/
theorem QuasiErgodic.ae_eq_const_of_ae_eq_compâ‚€ (h : QuasiErgodic f Î¼) (hgm : NullMeasurable g Î¼)
    (hg_eq : g âˆ˜ f =áµ[Î¼] g) : âˆƒ c, g =áµ[Î¼] const Î± c :=
  h.ae_eq_const_of_ae_eq_comp_of_ae_rangeâ‚€ (s := univ) univ_mem hgm hg_eq

/-- Let `f : Î± â†’ Î±` be an ergodic map.
Let `g : Î± â†’ X` be a null-measurable function from `Î±` to a nonempty measurable space
with a countable family of measurable sets separating the points of `X`.
If `g` is a.e.-invariant under `f`, then `g` is a.e. constant. -/
theorem Ergodic.ae_eq_const_of_ae_eq_compâ‚€ (h : Ergodic f Î¼) (hgm : NullMeasurable g Î¼)
    (hg_eq : g âˆ˜ f =áµ[Î¼] g) : âˆƒ c, g =áµ[Î¼] const Î± c :=
  h.quasiErgodic.ae_eq_const_of_ae_eq_compâ‚€ hgm hg_eq

end CountableSeparatingOnUniv

variable [TopologicalSpace X] [MetrizableSpace X] [Nonempty X] {f : Î± â†’ Î±}

namespace QuasiErgodic

/-- Let `f : Î± â†’ Î±` be a quasi ergodic map.
Let `g : Î± â†’ X` be an a.e. strongly measurable function
from `Î±` to a nonempty metrizable topological space.
If `g` is a.e.-invariant under `f`, then `g` is a.e. constant. -/
theorem ae_eq_const_of_ae_eq_comp_ae {g : Î± â†’ X} (h : QuasiErgodic f Î¼)
    (hgm : AEStronglyMeasurable g Î¼) (hg_eq : g âˆ˜ f =áµ[Î¼] g) : âˆƒ c, g =áµ[Î¼] const Î± c := by
  borelize X
  -- âŠ¢ âˆƒ c, g =áµ[Î¼] const Î± c
  rcases hgm.isSeparable_ae_range with âŸ¨t, ht, hgtâŸ©
  -- âŠ¢ âˆƒ c, g =áµ[Î¼] const Î± c
  haveI := ht.secondCountableTopology
  -- âŠ¢ âˆƒ c, g =áµ[Î¼] const Î± c
  exact h.ae_eq_const_of_ae_eq_comp_of_ae_rangeâ‚€ hgt hgm.aemeasurable.nullMeasurable hg_eq
  -- ğŸ‰ no goals

theorem eq_const_of_compQuasiMeasurePreserving_eq (h : QuasiErgodic f Î¼) {g : Î± â†’â‚˜[Î¼] X}
    (hg_eq : g.compQuasiMeasurePreserving f h.1 = g) : âˆƒ c, g = .const Î± c :=
  have : g âˆ˜ f =áµ[Î¼] g := (g.coeFn_compQuasiMeasurePreserving h.1).symm.trans
    (hg_eq.symm â–¸ .refl _ _)
  let âŸ¨c, hcâŸ© := h.ae_eq_const_of_ae_eq_comp_ae g.aestronglyMeasurable this
  âŸ¨c, AEEqFun.ext <| hc.trans (AEEqFun.coeFn_const _ _).symmâŸ©

end QuasiErgodic

namespace Ergodic

/-- Let `f : Î± â†’ Î±` be an ergodic map.
Let `g : Î± â†’ X` be an a.e. strongly measurable function
from `Î±` to a nonempty metrizable topological space.
If `g` is a.e.-invariant under `f`, then `g` is a.e. constant. -/
theorem ae_eq_const_of_ae_eq_comp_ae {g : Î± â†’ X} (h : Ergodic f Î¼) (hgm : AEStronglyMeasurable g Î¼)
    (hg_eq : g âˆ˜ f =áµ[Î¼] g) : âˆƒ c, g =áµ[Î¼] const Î± c :=
  h.quasiErgodic.ae_eq_const_of_ae_eq_comp_ae hgm hg_eq

theorem eq_const_of_compMeasurePreserving_eq (h : Ergodic f Î¼) {g : Î± â†’â‚˜[Î¼] X}
    (hg_eq : g.compMeasurePreserving f h.1 = g) : âˆƒ c, g = .const Î± c :=
  h.quasiErgodic.eq_const_of_compQuasiMeasurePreserving_eq hg_eq

end Ergodic
