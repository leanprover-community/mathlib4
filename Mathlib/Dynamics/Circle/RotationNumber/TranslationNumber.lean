/-
Copyright (c) 2020 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import Mathlib.Algebra.GroupPower.IterateHom
import Mathlib.Analysis.SpecificLimits.Basic
import Mathlib.Order.Iterate
import Mathlib.Order.SemiconjSup
import Mathlib.Topology.Order.MonotoneContinuity

#align_import dynamics.circle.rotation_number.translation_number from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Translation number of a monotone real map that commutes with `x ‚Ü¶ x + 1`

Let `f : ‚Ñù ‚Üí ‚Ñù` be a monotone map such that `f (x + 1) = f x + 1` for all `x`. Then the limit
$$
  \tau(f)=\lim_{n\to\infty}{f^n(x)-x}{n}
$$
exists and does not depend on `x`. This number is called the *translation number* of `f`.
Different authors use different notation for this number: `œÑ`, `œÅ`, `rot`, etc

In this file we define a structure `CircleDeg1Lift` for bundled maps with these properties, define
translation number of `f : CircleDeg1Lift`, prove some estimates relating `f^n(x)-x` to `œÑ(f)`. In
case of a continuous map `f` we also prove that `f` admits a point `x` such that `f^n(x)=x+m` if and
only if `œÑ(f)=m/n`.

Maps of this type naturally appear as lifts of orientation preserving circle homeomorphisms. More
precisely, let `f` be an orientation preserving homeomorphism of the circle $S^1=‚Ñù/‚Ñ§$, and
consider a real number `a` such that
`‚ü¶a‚üß = f 0`, where `‚ü¶‚üß` means the natural projection `‚Ñù ‚Üí ‚Ñù/‚Ñ§`. Then there exists a unique
continuous function `F : ‚Ñù ‚Üí ‚Ñù` such that `F 0 = a` and `‚ü¶F x‚üß = f ‚ü¶x‚üß` for all `x` (this fact is
not formalized yet). This function is strictly monotone, continuous, and satisfies
`F (x + 1) = F x + 1`. The number `‚ü¶œÑ F‚üß : ‚Ñù / ‚Ñ§` is called the *rotation number* of `f`.
It does not depend on the choice of `a`.

## Main definitions

* `CircleDeg1Lift`: a monotone map `f : ‚Ñù ‚Üí ‚Ñù` such that `f (x + 1) = f x + 1` for all `x`;
  the type `CircleDeg1Lift` is equipped with `Lattice` and `Monoid` structures; the
  multiplication is given by composition: `(f * g) x = f (g x)`.
* `CircleDeg1Lift.translationNumber`: translation number of `f : CircleDeg1Lift`.

## Main statements

We prove the following properties of `CircleDeg1Lift.translationNumber`.

* `CircleDeg1Lift.translationNumber_eq_of_dist_bounded`: if the distance between `(f^n) 0`
  and `(g^n) 0` is bounded from above uniformly in `n : ‚Ñï`, then `f` and `g` have equal
  translation numbers.

* `CircleDeg1Lift.translationNumber_eq_of_semiconjBy`: if two `CircleDeg1Lift` maps `f`, `g`
  are semiconjugate by a `CircleDeg1Lift` map, then `œÑ f = œÑ g`.

* `CircleDeg1Lift.translationNumber_units_inv`: if `f` is an invertible `CircleDeg1Lift` map
  (equivalently, `f` is a lift of an orientation-preserving circle homeomorphism), then
  the translation number of `f‚Åª¬π` is the negative of the translation number of `f`.

* `CircleDeg1Lift.translationNumber_mul_of_commute`: if `f` and `g` commute, then
  `œÑ (f * g) = œÑ f + œÑ g`.

* `CircleDeg1Lift.translationNumber_eq_rat_iff`: the translation number of `f` is equal to
  a rational number `m / n` if and only if `(f^n) x = x + m` for some `x`.

* `CircleDeg1Lift.semiconj_of_bijective_of_translationNumber_eq`: if `f` and `g` are two
  bijective `CircleDeg1Lift` maps and their translation numbers are equal, then these
  maps are semiconjugate to each other.

* `CircleDeg1Lift.semiconj_of_group_action_of_forall_translationNumber_eq`: let `f‚ÇÅ` and `f‚ÇÇ` be
  two actions of a group `G` on the circle by degree 1 maps (formally, `f‚ÇÅ` and `f‚ÇÇ` are two
  homomorphisms from `G ‚Üí* CircleDeg1Lift`). If the translation numbers of `f‚ÇÅ g` and `f‚ÇÇ g` are
  equal to each other for all `g : G`, then these two actions are semiconjugate by some
  `F : CircleDeg1Lift`. This is a version of Proposition 5.4 from [√âtienne Ghys, Groupes
  d'homeomorphismes du cercle et cohomologie bornee][ghys87:groupes].

## Notation

We use a local notation `œÑ` for the translation number of `f : CircleDeg1Lift`.

## Implementation notes

We define the translation number of `f : CircleDeg1Lift` to be the limit of the sequence
`(f ^ (2 ^ n)) 0 / (2 ^ n)`, then prove that `((f ^ n) x - x) / n` tends to this number for any `x`.
This way it is much easier to prove that the limit exists and basic properties of the limit.

We define translation number for a wider class of maps `f : ‚Ñù ‚Üí ‚Ñù` instead of lifts of orientation
preserving circle homeomorphisms for two reasons:

* non-strictly monotone circle self-maps with discontinuities naturally appear as Poincar√© maps
  for some flows on the two-torus (e.g., one can take a constant flow and glue in a few Cherry
  cells);
* definition and some basic properties still work for this class.

## References

* [√âtienne Ghys, Groupes d'homeomorphismes du cercle et cohomologie bornee][ghys87:groupes]

## TODO

Here are some short-term goals.

* Introduce a structure or a typeclass for lifts of circle homeomorphisms. We use
  `Units CircleDeg1Lift` for now, but it's better to have a dedicated type (or a typeclass?).

* Prove that the `SemiconjBy` relation on circle homeomorphisms is an equivalence relation.

* Introduce `ConditionallyCompleteLattice` structure, use it in the proof of
  `CircleDeg1Lift.semiconj_of_group_action_of_forall_translationNumber_eq`.

* Prove that the orbits of the irrational rotation are dense in the circle. Deduce that a
  homeomorphism with an irrational rotation is semiconjugate to the corresponding irrational
  translation by a continuous `CircleDeg1Lift`.

## Tags

circle homeomorphism, rotation number
-/


open scoped Classical
open Filter Set Int Topology
open Function hiding Commute

/-!
### Definition and monoid structure
-/

/-- A lift of a monotone degree one map `S¬π ‚Üí S¬π`. -/
structure CircleDeg1Lift extends ‚Ñù ‚Üío ‚Ñù : Type where
  map_add_one' : ‚àÄ x, toFun (x + 1) = toFun x + 1
#align circle_deg1_lift CircleDeg1Lift

namespace CircleDeg1Lift

instance : FunLike CircleDeg1Lift ‚Ñù ‚Ñù where
  coe f := f.toFun
  coe_injective' | ‚ü®‚ü®_, _‚ü©, _‚ü©, ‚ü®‚ü®_, _‚ü©, _‚ü©, rfl => rfl

instance : OrderHomClass CircleDeg1Lift ‚Ñù ‚Ñù where
  map_rel f _ _ h := f.monotone' h

@[simp] theorem coe_mk (f h) : ‚áë(mk f h) = f := rfl
#align circle_deg1_lift.coe_mk CircleDeg1Lift.coe_mk

variable (f g : CircleDeg1Lift)

@[simp] theorem coe_toOrderHom : ‚áëf.toOrderHom = f := rfl

protected theorem monotone : Monotone f := f.monotone'
#align circle_deg1_lift.monotone CircleDeg1Lift.monotone

@[mono] theorem mono {x y} (h : x ‚â§ y) : f x ‚â§ f y := f.monotone h
#align circle_deg1_lift.mono CircleDeg1Lift.mono

theorem strictMono_iff_injective : StrictMono f ‚Üî Injective f :=
  f.monotone.strictMono_iff_injective
#align circle_deg1_lift.strict_mono_iff_injective CircleDeg1Lift.strictMono_iff_injective

@[simp]
theorem map_add_one : ‚àÄ x, f (x + 1) = f x + 1 :=
  f.map_add_one'
#align circle_deg1_lift.map_add_one CircleDeg1Lift.map_add_one

@[simp]
theorem map_one_add (x : ‚Ñù) : f (1 + x) = 1 + f x := by rw [add_comm, map_add_one, add_comm 1]
#align circle_deg1_lift.map_one_add CircleDeg1Lift.map_one_add

#noalign circle_deg1_lift.coe_inj -- Use `DFunLike.coe_inj`

@[ext]
theorem ext ‚¶Éf g : CircleDeg1Lift‚¶Ñ (h : ‚àÄ x, f x = g x) : f = g :=
  DFunLike.ext f g h
#align circle_deg1_lift.ext CircleDeg1Lift.ext

theorem ext_iff {f g : CircleDeg1Lift} : f = g ‚Üî ‚àÄ x, f x = g x :=
  DFunLike.ext_iff
#align circle_deg1_lift.ext_iff CircleDeg1Lift.ext_iff

instance : Monoid CircleDeg1Lift where
  mul f g :=
    { toOrderHom := f.1.comp g.1
      map_add_one' := fun x => by simp [map_add_one] }
  one := ‚ü®.id, fun _ => rfl‚ü©
  mul_one f := rfl
  one_mul f := rfl
  mul_assoc f‚ÇÅ f‚ÇÇ f‚ÇÉ := DFunLike.coe_injective rfl

instance : Inhabited CircleDeg1Lift := ‚ü®1‚ü©

@[simp]
theorem coe_mul : ‚áë(f * g) = f ‚àò g :=
  rfl
#align circle_deg1_lift.coe_mul CircleDeg1Lift.coe_mul

theorem mul_apply (x) : (f * g) x = f (g x) :=
  rfl
#align circle_deg1_lift.mul_apply CircleDeg1Lift.mul_apply

@[simp]
theorem coe_one : ‚áë(1 : CircleDeg1Lift) = id :=
  rfl
#align circle_deg1_lift.coe_one CircleDeg1Lift.coe_one

instance unitsHasCoeToFun : CoeFun CircleDeg1LiftÀ£ fun _ => ‚Ñù ‚Üí ‚Ñù :=
  ‚ü®fun f => ‚áë(f : CircleDeg1Lift)‚ü©
#align circle_deg1_lift.units_has_coe_to_fun CircleDeg1Lift.unitsHasCoeToFun

#noalign circle_deg1_lift.units_coe -- now LHS = RHS

@[simp]
theorem units_inv_apply_apply (f : CircleDeg1LiftÀ£) (x : ‚Ñù) :
    (f‚Åª¬π : CircleDeg1LiftÀ£) (f x) = x := by simp only [‚Üê mul_apply, f.inv_mul, coe_one, id]
#align circle_deg1_lift.units_inv_apply_apply CircleDeg1Lift.units_inv_apply_apply

@[simp]
theorem units_apply_inv_apply (f : CircleDeg1LiftÀ£) (x : ‚Ñù) :
    f ((f‚Åª¬π : CircleDeg1LiftÀ£) x) = x := by simp only [‚Üê mul_apply, f.mul_inv, coe_one, id]
#align circle_deg1_lift.units_apply_inv_apply CircleDeg1Lift.units_apply_inv_apply

/-- If a lift of a circle map is bijective, then it is an order automorphism of the line. -/
def toOrderIso : CircleDeg1LiftÀ£ ‚Üí* ‚Ñù ‚âÉo ‚Ñù where
  toFun f :=
    { toFun := f
      invFun := ‚áëf‚Åª¬π
      left_inv := units_inv_apply_apply f
      right_inv := units_apply_inv_apply f
      map_rel_iff' := ‚ü®fun h => by simpa using mono (‚Üëf‚Åª¬π) h, mono f‚ü© }
  map_one' := rfl
  map_mul' f g := rfl
#align circle_deg1_lift.to_order_iso CircleDeg1Lift.toOrderIso

@[simp]
theorem coe_toOrderIso (f : CircleDeg1LiftÀ£) : ‚áë(toOrderIso f) = f :=
  rfl
#align circle_deg1_lift.coe_to_order_iso CircleDeg1Lift.coe_toOrderIso

@[simp]
theorem coe_toOrderIso_symm (f : CircleDeg1LiftÀ£) :
    ‚áë(toOrderIso f).symm = (f‚Åª¬π : CircleDeg1LiftÀ£) :=
  rfl
#align circle_deg1_lift.coe_to_order_iso_symm CircleDeg1Lift.coe_toOrderIso_symm

@[simp]
theorem coe_toOrderIso_inv (f : CircleDeg1LiftÀ£) : ‚áë(toOrderIso f)‚Åª¬π = (f‚Åª¬π : CircleDeg1LiftÀ£) :=
  rfl
#align circle_deg1_lift.coe_to_order_iso_inv CircleDeg1Lift.coe_toOrderIso_inv

theorem isUnit_iff_bijective {f : CircleDeg1Lift} : IsUnit f ‚Üî Bijective f :=
  ‚ü®fun ‚ü®u, h‚ü© => h ‚ñ∏ (toOrderIso u).bijective, fun h =>
    Units.isUnit
      { val := f
        inv :=
          { toFun := (Equiv.ofBijective f h).symm
            monotone' := fun x y hxy =>
              (f.strictMono_iff_injective.2 h.1).le_iff_le.1
                (by simp only [Equiv.ofBijective_apply_symm_apply f h, hxy])
            map_add_one' := fun x =>
              h.1 <| by simp only [Equiv.ofBijective_apply_symm_apply f, f.map_add_one] }
        val_inv := ext <| Equiv.ofBijective_apply_symm_apply f h
        inv_val := ext <| Equiv.ofBijective_symm_apply_apply f h }‚ü©
#align circle_deg1_lift.is_unit_iff_bijective CircleDeg1Lift.isUnit_iff_bijective

theorem coe_pow : ‚àÄ n : ‚Ñï, ‚áë(f ^ n) = f^[n]
  | 0 => rfl
  | n + 1 => by
    ext x
    simp [coe_pow n, pow_succ]
#align circle_deg1_lift.coe_pow CircleDeg1Lift.coe_pow

theorem semiconjBy_iff_semiconj {f g‚ÇÅ g‚ÇÇ : CircleDeg1Lift} :
    SemiconjBy f g‚ÇÅ g‚ÇÇ ‚Üî Semiconj f g‚ÇÅ g‚ÇÇ :=
  ext_iff
#align circle_deg1_lift.semiconj_by_iff_semiconj CircleDeg1Lift.semiconjBy_iff_semiconj

theorem commute_iff_commute {f g : CircleDeg1Lift} : Commute f g ‚Üî Function.Commute f g :=
  ext_iff
#align circle_deg1_lift.commute_iff_commute CircleDeg1Lift.commute_iff_commute

/-!
### Translate by a constant
-/


/-- The map `y ‚Ü¶ x + y` as a `CircleDeg1Lift`. More precisely, we define a homomorphism from
`Multiplicative ‚Ñù` to `CircleDeg1LiftÀ£`, so the translation by `x` is
`translation (Multiplicative.ofAdd x)`. -/
def translate : Multiplicative ‚Ñù ‚Üí* CircleDeg1LiftÀ£ := MonoidHom.toHomUnits <|
  { toFun := fun x =>
      ‚ü®‚ü®fun y => Multiplicative.toAdd x + y, fun _ _ h => add_le_add_left h _‚ü©, fun _ =>
        (add_assoc _ _ _).symm‚ü©
    map_one' := ext <| zero_add
    map_mul' := fun _ _ => ext <| add_assoc _ _ }
#align circle_deg1_lift.translate CircleDeg1Lift.translate

@[simp]
theorem translate_apply (x y : ‚Ñù) : translate (Multiplicative.ofAdd x) y = x + y :=
  rfl
#align circle_deg1_lift.translate_apply CircleDeg1Lift.translate_apply

@[simp]
theorem translate_inv_apply (x y : ‚Ñù) : (translate <| Multiplicative.ofAdd x)‚Åª¬π y = -x + y :=
  rfl
#align circle_deg1_lift.translate_inv_apply CircleDeg1Lift.translate_inv_apply

@[simp]
theorem translate_zpow (x : ‚Ñù) (n : ‚Ñ§) :
    translate (Multiplicative.ofAdd x) ^ n = translate (Multiplicative.ofAdd <| ‚Üën * x) := by
  simp only [‚Üê zsmul_eq_mul, ofAdd_zsmul, MonoidHom.map_zpow]
#align circle_deg1_lift.translate_zpow CircleDeg1Lift.translate_zpow

@[simp]
theorem translate_pow (x : ‚Ñù) (n : ‚Ñï) :
    translate (Multiplicative.ofAdd x) ^ n = translate (Multiplicative.ofAdd <| ‚Üën * x) :=
  translate_zpow x n
#align circle_deg1_lift.translate_pow CircleDeg1Lift.translate_pow

@[simp]
theorem translate_iterate (x : ‚Ñù) (n : ‚Ñï) :
    (translate (Multiplicative.ofAdd x))^[n] = translate (Multiplicative.ofAdd <| ‚Üën * x) := by
  rw [‚Üê coe_pow, ‚Üê Units.val_pow_eq_pow_val, translate_pow]
#align circle_deg1_lift.translate_iterate CircleDeg1Lift.translate_iterate

/-!
### Commutativity with integer translations

In this section we prove that `f` commutes with translations by an integer number.
First we formulate these statements (for a natural or an integer number,
addition on the left or on the right, addition or subtraction) using `Function.Commute`,
then reformulate as `simp` lemmas `map_int_add` etc.
-/

theorem commute_nat_add (n : ‚Ñï) : Function.Commute f (n + ¬∑) := by
  simpa only [nsmul_one, add_left_iterate] using Function.Commute.iterate_right f.map_one_add n
#align circle_deg1_lift.commute_nat_add CircleDeg1Lift.commute_nat_add

theorem commute_add_nat (n : ‚Ñï) : Function.Commute f (¬∑ + n) := by
  simp only [add_comm _ (n : ‚Ñù), f.commute_nat_add n]
#align circle_deg1_lift.commute_add_nat CircleDeg1Lift.commute_add_nat

theorem commute_sub_nat (n : ‚Ñï) : Function.Commute f (¬∑ - n) := by
  simpa only [sub_eq_add_neg] using
    (f.commute_add_nat n).inverses_right (Equiv.addRight _).right_inv (Equiv.addRight _).left_inv
#align circle_deg1_lift.commute_sub_nat CircleDeg1Lift.commute_sub_nat

theorem commute_add_int : ‚àÄ n : ‚Ñ§, Function.Commute f (¬∑ + n)
  | (n : ‚Ñï) => f.commute_add_nat n
  | -[n+1] => by simpa [sub_eq_add_neg] using f.commute_sub_nat (n + 1)
#align circle_deg1_lift.commute_add_int CircleDeg1Lift.commute_add_int

theorem commute_int_add (n : ‚Ñ§) : Function.Commute f (n + ¬∑) := by
  simpa only [add_comm _ (n : ‚Ñù)] using f.commute_add_int n
#align circle_deg1_lift.commute_int_add CircleDeg1Lift.commute_int_add

theorem commute_sub_int (n : ‚Ñ§) : Function.Commute f (¬∑ - n) := by
  simpa only [sub_eq_add_neg] using
    (f.commute_add_int n).inverses_right (Equiv.addRight _).right_inv (Equiv.addRight _).left_inv
#align circle_deg1_lift.commute_sub_int CircleDeg1Lift.commute_sub_int

@[simp]
theorem map_int_add (m : ‚Ñ§) (x : ‚Ñù) : f (m + x) = m + f x :=
  f.commute_int_add m x
#align circle_deg1_lift.map_int_add CircleDeg1Lift.map_int_add

@[simp]
theorem map_add_int (x : ‚Ñù) (m : ‚Ñ§) : f (x + m) = f x + m :=
  f.commute_add_int m x
#align circle_deg1_lift.map_add_int CircleDeg1Lift.map_add_int

@[simp]
theorem map_sub_int (x : ‚Ñù) (n : ‚Ñ§) : f (x - n) = f x - n :=
  f.commute_sub_int n x
#align circle_deg1_lift.map_sub_int CircleDeg1Lift.map_sub_int

@[simp]
theorem map_add_nat (x : ‚Ñù) (n : ‚Ñï) : f (x + n) = f x + n :=
  f.map_add_int x n
#align circle_deg1_lift.map_add_nat CircleDeg1Lift.map_add_nat

@[simp]
theorem map_nat_add (n : ‚Ñï) (x : ‚Ñù) : f (n + x) = n + f x :=
  f.map_int_add n x
#align circle_deg1_lift.map_nat_add CircleDeg1Lift.map_nat_add

@[simp]
theorem map_sub_nat (x : ‚Ñù) (n : ‚Ñï) : f (x - n) = f x - n :=
  f.map_sub_int x n
#align circle_deg1_lift.map_sub_nat CircleDeg1Lift.map_sub_nat

theorem map_int_of_map_zero (n : ‚Ñ§) : f n = f 0 + n := by rw [‚Üê f.map_add_int, zero_add]
#align circle_deg1_lift.map_int_of_map_zero CircleDeg1Lift.map_int_of_map_zero

@[simp]
theorem map_fract_sub_fract_eq (x : ‚Ñù) : f (fract x) - fract x = f x - x := by
  rw [Int.fract, f.map_sub_int, sub_sub_sub_cancel_right]
#align circle_deg1_lift.map_fract_sub_fract_eq CircleDeg1Lift.map_fract_sub_fract_eq

/-!
### Pointwise order on circle maps
-/


/-- Monotone circle maps form a lattice with respect to the pointwise order -/
noncomputable instance : Lattice CircleDeg1Lift where
  sup f g :=
    { toFun := fun x => max (f x) (g x)
      monotone' := fun x y h => max_le_max (f.mono h) (g.mono h)
      -- TODO: generalize to `Monotone.max`
      map_add_one' := fun x => by simp [max_add_add_right] }
  le f g := ‚àÄ x, f x ‚â§ g x
  le_refl f x := le_refl (f x)
  le_trans f‚ÇÅ f‚ÇÇ f‚ÇÉ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ x := le_trans (h‚ÇÅ‚ÇÇ x) (h‚ÇÇ‚ÇÉ x)
  le_antisymm f‚ÇÅ f‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ := ext fun x => le_antisymm (h‚ÇÅ‚ÇÇ x) (h‚ÇÇ‚ÇÅ x)
  le_sup_left f g x := le_max_left (f x) (g x)
  le_sup_right f g x := le_max_right (f x) (g x)
  sup_le f‚ÇÅ f‚ÇÇ f‚ÇÉ h‚ÇÅ h‚ÇÇ x := max_le (h‚ÇÅ x) (h‚ÇÇ x)
  inf f g :=
    { toFun := fun x => min (f x) (g x)
      monotone' := fun x y h => min_le_min (f.mono h) (g.mono h)
      map_add_one' := fun x => by simp [min_add_add_right] }
  inf_le_left f g x := min_le_left (f x) (g x)
  inf_le_right f g x := min_le_right (f x) (g x)
  le_inf f‚ÇÅ f‚ÇÇ f‚ÇÉ h‚ÇÇ h‚ÇÉ x := le_min (h‚ÇÇ x) (h‚ÇÉ x)

@[simp]
theorem sup_apply (x : ‚Ñù) : (f ‚äî g) x = max (f x) (g x) :=
  rfl
#align circle_deg1_lift.sup_apply CircleDeg1Lift.sup_apply

@[simp]
theorem inf_apply (x : ‚Ñù) : (f ‚äì g) x = min (f x) (g x) :=
  rfl
#align circle_deg1_lift.inf_apply CircleDeg1Lift.inf_apply

theorem iterate_monotone (n : ‚Ñï) : Monotone fun f : CircleDeg1Lift => f^[n] := fun f _ h =>
  f.monotone.iterate_le_of_le h _
#align circle_deg1_lift.iterate_monotone CircleDeg1Lift.iterate_monotone

theorem iterate_mono {f g : CircleDeg1Lift} (h : f ‚â§ g) (n : ‚Ñï) : f^[n] ‚â§ g^[n] :=
  iterate_monotone n h
#align circle_deg1_lift.iterate_mono CircleDeg1Lift.iterate_mono

theorem pow_mono {f g : CircleDeg1Lift} (h : f ‚â§ g) (n : ‚Ñï) : f ^ n ‚â§ g ^ n := fun x => by
  simp only [coe_pow, iterate_mono h n x]
#align circle_deg1_lift.pow_mono CircleDeg1Lift.pow_mono

theorem pow_monotone (n : ‚Ñï) : Monotone fun f : CircleDeg1Lift => f ^ n := fun _ _ h => pow_mono h n
#align circle_deg1_lift.pow_monotone CircleDeg1Lift.pow_monotone

/-!
### Estimates on `(f * g) 0`

We prove the estimates `f 0 + ‚åäg 0‚åã ‚â§ f (g 0) ‚â§ f 0 + ‚åàg 0‚åâ` and some corollaries with added/removed
floors and ceils.

We also prove that for two semiconjugate maps `g‚ÇÅ`, `g‚ÇÇ`, the distance between `g‚ÇÅ 0` and `g‚ÇÇ 0`
is less than two.
-/

theorem map_le_of_map_zero (x : ‚Ñù) : f x ‚â§ f 0 + ‚åàx‚åâ :=
  calc
    f x ‚â§ f ‚åàx‚åâ := f.monotone <| le_ceil _
    _ = f 0 + ‚åàx‚åâ := f.map_int_of_map_zero _
#align circle_deg1_lift.map_le_of_map_zero CircleDeg1Lift.map_le_of_map_zero

theorem map_map_zero_le : f (g 0) ‚â§ f 0 + ‚åàg 0‚åâ :=
  f.map_le_of_map_zero (g 0)
#align circle_deg1_lift.map_map_zero_le CircleDeg1Lift.map_map_zero_le

theorem floor_map_map_zero_le : ‚åäf (g 0)‚åã ‚â§ ‚åäf 0‚åã + ‚åàg 0‚åâ :=
  calc
    ‚åäf (g 0)‚åã ‚â§ ‚åäf 0 + ‚åàg 0‚åâ‚åã := floor_mono <| f.map_map_zero_le g
    _ = ‚åäf 0‚åã + ‚åàg 0‚åâ := floor_add_int _ _
#align circle_deg1_lift.floor_map_map_zero_le CircleDeg1Lift.floor_map_map_zero_le

theorem ceil_map_map_zero_le : ‚åàf (g 0)‚åâ ‚â§ ‚åàf 0‚åâ + ‚åàg 0‚åâ :=
  calc
    ‚åàf (g 0)‚åâ ‚â§ ‚åàf 0 + ‚åàg 0‚åâ‚åâ := ceil_mono <| f.map_map_zero_le g
    _ = ‚åàf 0‚åâ + ‚åàg 0‚åâ := ceil_add_int _ _
#align circle_deg1_lift.ceil_map_map_zero_le CircleDeg1Lift.ceil_map_map_zero_le

theorem map_map_zero_lt : f (g 0) < f 0 + g 0 + 1 :=
  calc
    f (g 0) ‚â§ f 0 + ‚åàg 0‚åâ := f.map_map_zero_le g
    _ < f 0 + (g 0 + 1) := add_lt_add_left (ceil_lt_add_one _) _
    _ = f 0 + g 0 + 1 := (add_assoc _ _ _).symm
#align circle_deg1_lift.map_map_zero_lt CircleDeg1Lift.map_map_zero_lt

theorem le_map_of_map_zero (x : ‚Ñù) : f 0 + ‚åäx‚åã ‚â§ f x :=
  calc
    f 0 + ‚åäx‚åã = f ‚åäx‚åã := (f.map_int_of_map_zero _).symm
    _ ‚â§ f x := f.monotone <| floor_le _
#align circle_deg1_lift.le_map_of_map_zero CircleDeg1Lift.le_map_of_map_zero

theorem le_map_map_zero : f 0 + ‚åäg 0‚åã ‚â§ f (g 0) :=
  f.le_map_of_map_zero (g 0)
#align circle_deg1_lift.le_map_map_zero CircleDeg1Lift.le_map_map_zero

theorem le_floor_map_map_zero : ‚åäf 0‚åã + ‚åäg 0‚åã ‚â§ ‚åäf (g 0)‚åã :=
  calc
    ‚åäf 0‚åã + ‚åäg 0‚åã = ‚åäf 0 + ‚åäg 0‚åã‚åã := (floor_add_int _ _).symm
    _ ‚â§ ‚åäf (g 0)‚åã := floor_mono <| f.le_map_map_zero g
#align circle_deg1_lift.le_floor_map_map_zero CircleDeg1Lift.le_floor_map_map_zero

theorem le_ceil_map_map_zero : ‚åàf 0‚åâ + ‚åäg 0‚åã ‚â§ ‚åà(f * g) 0‚åâ :=
  calc
    ‚åàf 0‚åâ + ‚åäg 0‚åã = ‚åàf 0 + ‚åäg 0‚åã‚åâ := (ceil_add_int _ _).symm
    _ ‚â§ ‚åàf (g 0)‚åâ := ceil_mono <| f.le_map_map_zero g
#align circle_deg1_lift.le_ceil_map_map_zero CircleDeg1Lift.le_ceil_map_map_zero

theorem lt_map_map_zero : f 0 + g 0 - 1 < f (g 0) :=
  calc
    f 0 + g 0 - 1 = f 0 + (g 0 - 1) := add_sub_assoc _ _ _
    _ < f 0 + ‚åäg 0‚åã := add_lt_add_left (sub_one_lt_floor _) _
    _ ‚â§ f (g 0) := f.le_map_map_zero g
#align circle_deg1_lift.lt_map_map_zero CircleDeg1Lift.lt_map_map_zero

theorem dist_map_map_zero_lt : dist (f 0 + g 0) (f (g 0)) < 1 := by
  rw [dist_comm, Real.dist_eq, abs_lt, lt_sub_iff_add_lt', sub_lt_iff_lt_add', ‚Üê sub_eq_add_neg]
  exact ‚ü®f.lt_map_map_zero g, f.map_map_zero_lt g‚ü©
#align circle_deg1_lift.dist_map_map_zero_lt CircleDeg1Lift.dist_map_map_zero_lt

theorem dist_map_zero_lt_of_semiconj {f g‚ÇÅ g‚ÇÇ : CircleDeg1Lift} (h : Function.Semiconj f g‚ÇÅ g‚ÇÇ) :
    dist (g‚ÇÅ 0) (g‚ÇÇ 0) < 2 :=
  calc
    dist (g‚ÇÅ 0) (g‚ÇÇ 0) ‚â§ dist (g‚ÇÅ 0) (f (g‚ÇÅ 0) - f 0) + dist _ (g‚ÇÇ 0) := dist_triangle _ _ _
    _ = dist (f 0 + g‚ÇÅ 0) (f (g‚ÇÅ 0)) + dist (g‚ÇÇ 0 + f 0) (g‚ÇÇ (f 0)) := by
      simp only [h.eq, Real.dist_eq, sub_sub, add_comm (f 0), sub_sub_eq_add_sub,
        abs_sub_comm (g‚ÇÇ (f 0))]
    _ < 1 + 1 := add_lt_add (f.dist_map_map_zero_lt g‚ÇÅ) (g‚ÇÇ.dist_map_map_zero_lt f)
    _ = 2 := one_add_one_eq_two
#align circle_deg1_lift.dist_map_zero_lt_of_semiconj CircleDeg1Lift.dist_map_zero_lt_of_semiconj

theorem dist_map_zero_lt_of_semiconjBy {f g‚ÇÅ g‚ÇÇ : CircleDeg1Lift} (h : SemiconjBy f g‚ÇÅ g‚ÇÇ) :
    dist (g‚ÇÅ 0) (g‚ÇÇ 0) < 2 :=
  dist_map_zero_lt_of_semiconj <| semiconjBy_iff_semiconj.1 h
#align circle_deg1_lift.dist_map_zero_lt_of_semiconj_by CircleDeg1Lift.dist_map_zero_lt_of_semiconjBy

/-!
### Limits at infinities and continuity
-/

protected theorem tendsto_atBot : Tendsto f atBot atBot :=
  tendsto_atBot_mono f.map_le_of_map_zero <| tendsto_atBot_add_const_left _ _ <|
    (tendsto_atBot_mono fun x => (ceil_lt_add_one x).le) <|
      tendsto_atBot_add_const_right _ _ tendsto_id
#align circle_deg1_lift.tendsto_at_bot CircleDeg1Lift.tendsto_atBot

protected theorem tendsto_atTop : Tendsto f atTop atTop :=
  tendsto_atTop_mono f.le_map_of_map_zero <| tendsto_atTop_add_const_left _ _ <|
    (tendsto_atTop_mono fun x => (sub_one_lt_floor x).le) <| by
      simpa [sub_eq_add_neg] using tendsto_atTop_add_const_right _ _ tendsto_id
#align circle_deg1_lift.tendsto_at_top CircleDeg1Lift.tendsto_atTop

theorem continuous_iff_surjective : Continuous f ‚Üî Function.Surjective f :=
  ‚ü®fun h => h.surjective f.tendsto_atTop f.tendsto_atBot, f.monotone.continuous_of_surjective‚ü©
#align circle_deg1_lift.continuous_iff_surjective CircleDeg1Lift.continuous_iff_surjective

/-!
### Estimates on `(f^n) x`

If we know that `f x` is `‚â§`/`<`/`‚â•`/`>`/`=` to `x + m`, then we have a similar estimate on
`f^[n] x` and `x + n * m`.

For `‚â§`, `‚â•`, and `=` we formulate both `of` (implication) and `iff` versions because implications
work for `n = 0`. For `<` and `>` we formulate only `iff` versions.
-/


theorem iterate_le_of_map_le_add_int {x : ‚Ñù} {m : ‚Ñ§} (h : f x ‚â§ x + m) (n : ‚Ñï) :
    f^[n] x ‚â§ x + n * m := by
  simpa only [nsmul_eq_mul, add_right_iterate] using
    (f.commute_add_int m).iterate_le_of_map_le f.monotone (monotone_id.add_const (m : ‚Ñù)) h n
#align circle_deg1_lift.iterate_le_of_map_le_add_int CircleDeg1Lift.iterate_le_of_map_le_add_int

theorem le_iterate_of_add_int_le_map {x : ‚Ñù} {m : ‚Ñ§} (h : x + m ‚â§ f x) (n : ‚Ñï) :
    x + n * m ‚â§ f^[n] x := by
  simpa only [nsmul_eq_mul, add_right_iterate] using
    (f.commute_add_int m).symm.iterate_le_of_map_le (monotone_id.add_const (m : ‚Ñù)) f.monotone h n
#align circle_deg1_lift.le_iterate_of_add_int_le_map CircleDeg1Lift.le_iterate_of_add_int_le_map

theorem iterate_eq_of_map_eq_add_int {x : ‚Ñù} {m : ‚Ñ§} (h : f x = x + m) (n : ‚Ñï) :
    f^[n] x = x + n * m := by
  simpa only [nsmul_eq_mul, add_right_iterate] using (f.commute_add_int m).iterate_eq_of_map_eq n h
#align circle_deg1_lift.iterate_eq_of_map_eq_add_int CircleDeg1Lift.iterate_eq_of_map_eq_add_int

theorem iterate_pos_le_iff {x : ‚Ñù} {m : ‚Ñ§} {n : ‚Ñï} (hn : 0 < n) :
    f^[n] x ‚â§ x + n * m ‚Üî f x ‚â§ x + m := by
  simpa only [nsmul_eq_mul, add_right_iterate] using
    (f.commute_add_int m).iterate_pos_le_iff_map_le f.monotone (strictMono_id.add_const (m : ‚Ñù)) hn
#align circle_deg1_lift.iterate_pos_le_iff CircleDeg1Lift.iterate_pos_le_iff

theorem iterate_pos_lt_iff {x : ‚Ñù} {m : ‚Ñ§} {n : ‚Ñï} (hn : 0 < n) :
    f^[n] x < x + n * m ‚Üî f x < x + m := by
  simpa only [nsmul_eq_mul, add_right_iterate] using
    (f.commute_add_int m).iterate_pos_lt_iff_map_lt f.monotone (strictMono_id.add_const (m : ‚Ñù)) hn
#align circle_deg1_lift.iterate_pos_lt_iff CircleDeg1Lift.iterate_pos_lt_iff

theorem iterate_pos_eq_iff {x : ‚Ñù} {m : ‚Ñ§} {n : ‚Ñï} (hn : 0 < n) :
    f^[n] x = x + n * m ‚Üî f x = x + m := by
  simpa only [nsmul_eq_mul, add_right_iterate] using
    (f.commute_add_int m).iterate_pos_eq_iff_map_eq f.monotone (strictMono_id.add_const (m : ‚Ñù)) hn
#align circle_deg1_lift.iterate_pos_eq_iff CircleDeg1Lift.iterate_pos_eq_iff

theorem le_iterate_pos_iff {x : ‚Ñù} {m : ‚Ñ§} {n : ‚Ñï} (hn : 0 < n) :
    x + n * m ‚â§ f^[n] x ‚Üî x + m ‚â§ f x := by
  simpa only [not_lt] using not_congr (f.iterate_pos_lt_iff hn)
#align circle_deg1_lift.le_iterate_pos_iff CircleDeg1Lift.le_iterate_pos_iff

theorem lt_iterate_pos_iff {x : ‚Ñù} {m : ‚Ñ§} {n : ‚Ñï} (hn : 0 < n) :
    x + n * m < f^[n] x ‚Üî x + m < f x := by
  simpa only [not_le] using not_congr (f.iterate_pos_le_iff hn)
#align circle_deg1_lift.lt_iterate_pos_iff CircleDeg1Lift.lt_iterate_pos_iff

theorem mul_floor_map_zero_le_floor_iterate_zero (n : ‚Ñï) : ‚Üën * ‚åäf 0‚åã ‚â§ ‚åäf^[n] 0‚åã := by
  rw [le_floor, Int.cast_mul, Int.cast_natCast, ‚Üê zero_add ((n : ‚Ñù) * _)]
  apply le_iterate_of_add_int_le_map
  simp [floor_le]
#align circle_deg1_lift.mul_floor_map_zero_le_floor_iterate_zero CircleDeg1Lift.mul_floor_map_zero_le_floor_iterate_zero

/-!
### Definition of translation number
-/

noncomputable section

/-- An auxiliary sequence used to define the translation number. -/
def transnumAuxSeq (n : ‚Ñï) : ‚Ñù :=
  (f ^ (2 ^ n : ‚Ñï)) 0 / 2 ^ n
#align circle_deg1_lift.transnum_aux_seq CircleDeg1Lift.transnumAuxSeq

/-- The translation number of a `CircleDeg1Lift`, $œÑ(f)=\lim_{n‚Üí‚àû}\frac{f^n(x)-x}{n}$. We use
an auxiliary sequence `\frac{f^{2^n}(0)}{2^n}` to define `œÑ(f)` because some proofs are simpler
this way. -/
def translationNumber : ‚Ñù :=
  limUnder atTop f.transnumAuxSeq
#align circle_deg1_lift.translation_number CircleDeg1Lift.translationNumber

end

-- TODO: choose two different symbols for `CircleDeg1Lift.translationNumber` and the future
-- `circle_mono_homeo.rotation_number`, then make them `localized notation`s
local notation "œÑ" => translationNumber

theorem transnumAuxSeq_def : f.transnumAuxSeq = fun n : ‚Ñï => (f ^ (2 ^ n : ‚Ñï)) 0 / 2 ^ n :=
  rfl
#align circle_deg1_lift.transnum_aux_seq_def CircleDeg1Lift.transnumAuxSeq_def

theorem translationNumber_eq_of_tendsto_aux {œÑ' : ‚Ñù} (h : Tendsto f.transnumAuxSeq atTop (ùìù œÑ')) :
    œÑ f = œÑ' :=
  h.limUnder_eq
#align circle_deg1_lift.translation_number_eq_of_tendsto_aux CircleDeg1Lift.translationNumber_eq_of_tendsto_aux

theorem translationNumber_eq_of_tendsto‚ÇÄ {œÑ' : ‚Ñù}
    (h : Tendsto (fun n : ‚Ñï => f^[n] 0 / n) atTop (ùìù œÑ')) : œÑ f = œÑ' :=
  f.translationNumber_eq_of_tendsto_aux <| by
    simpa [(¬∑ ‚àò ¬∑), transnumAuxSeq_def, coe_pow] using
      h.comp (Nat.tendsto_pow_atTop_atTop_of_one_lt one_lt_two)
#align circle_deg1_lift.translation_number_eq_of_tendsto‚ÇÄ CircleDeg1Lift.translationNumber_eq_of_tendsto‚ÇÄ

theorem translationNumber_eq_of_tendsto‚ÇÄ' {œÑ' : ‚Ñù}
    (h : Tendsto (fun n : ‚Ñï => f^[n + 1] 0 / (n + 1)) atTop (ùìù œÑ')) : œÑ f = œÑ' :=
  f.translationNumber_eq_of_tendsto‚ÇÄ <| (tendsto_add_atTop_iff_nat 1).1 (mod_cast h)
#align circle_deg1_lift.translation_number_eq_of_tendsto‚ÇÄ' CircleDeg1Lift.translationNumber_eq_of_tendsto‚ÇÄ'

theorem transnumAuxSeq_zero : f.transnumAuxSeq 0 = f 0 := by simp [transnumAuxSeq]
#align circle_deg1_lift.transnum_aux_seq_zero CircleDeg1Lift.transnumAuxSeq_zero

theorem transnumAuxSeq_dist_lt (n : ‚Ñï) :
    dist (f.transnumAuxSeq n) (f.transnumAuxSeq (n + 1)) < 1 / 2 / 2 ^ n := by
  have : 0 < (2 ^ (n + 1) : ‚Ñù) := pow_pos zero_lt_two _
  rw [div_div, ‚Üê pow_succ', ‚Üê abs_of_pos this]
  replace := abs_pos.2 (ne_of_gt this)
  convert (div_lt_div_right this).2 ((f ^ 2 ^ n).dist_map_map_zero_lt (f ^ 2 ^ n)) using 1
  simp_rw [transnumAuxSeq, Real.dist_eq]
  rw [‚Üê abs_div, sub_div, pow_succ, pow_succ', ‚Üê two_mul, mul_div_mul_left _ _ (two_ne_zero' ‚Ñù),
    pow_mul, sq, mul_apply]
#align circle_deg1_lift.transnum_aux_seq_dist_lt CircleDeg1Lift.transnumAuxSeq_dist_lt

theorem tendsto_translationNumber_aux : Tendsto f.transnumAuxSeq atTop (ùìù <| œÑ f) :=
  (cauchySeq_of_le_geometric_two 1 fun n => le_of_lt <| f.transnumAuxSeq_dist_lt n).tendsto_limUnder
#align circle_deg1_lift.tendsto_translation_number_aux CircleDeg1Lift.tendsto_translationNumber_aux

theorem dist_map_zero_translationNumber_le : dist (f 0) (œÑ f) ‚â§ 1 :=
  f.transnumAuxSeq_zero ‚ñ∏
    dist_le_of_le_geometric_two_of_tendsto‚ÇÄ 1 (fun n => le_of_lt <| f.transnumAuxSeq_dist_lt n)
      f.tendsto_translationNumber_aux
#align circle_deg1_lift.dist_map_zero_translation_number_le CircleDeg1Lift.dist_map_zero_translationNumber_le

theorem tendsto_translationNumber_of_dist_bounded_aux (x : ‚Ñï ‚Üí ‚Ñù) (C : ‚Ñù)
    (H : ‚àÄ n : ‚Ñï, dist ((f ^ n) 0) (x n) ‚â§ C) :
    Tendsto (fun n : ‚Ñï => x (2 ^ n) / 2 ^ n) atTop (ùìù <| œÑ f) := by
  apply f.tendsto_translationNumber_aux.congr_dist (squeeze_zero (fun _ => dist_nonneg) _ _)
  ¬∑ exact fun n => C / 2 ^ n
  ¬∑ intro n
    have : 0 < (2 ^ n : ‚Ñù) := pow_pos zero_lt_two _
    convert (div_le_div_right this).2 (H (2 ^ n)) using 1
    rw [transnumAuxSeq, Real.dist_eq, ‚Üê sub_div, abs_div, abs_of_pos this, Real.dist_eq]
  ¬∑ exact mul_zero C ‚ñ∏ tendsto_const_nhds.mul <| tendsto_inv_atTop_zero.comp <|
      tendsto_pow_atTop_atTop_of_one_lt one_lt_two
#align circle_deg1_lift.tendsto_translation_number_of_dist_bounded_aux CircleDeg1Lift.tendsto_translationNumber_of_dist_bounded_aux

theorem translationNumber_eq_of_dist_bounded {f g : CircleDeg1Lift} (C : ‚Ñù)
    (H : ‚àÄ n : ‚Ñï, dist ((f ^ n) 0) ((g ^ n) 0) ‚â§ C) : œÑ f = œÑ g :=
  Eq.symm <| g.translationNumber_eq_of_tendsto_aux <|
    f.tendsto_translationNumber_of_dist_bounded_aux _ C H
#align circle_deg1_lift.translation_number_eq_of_dist_bounded CircleDeg1Lift.translationNumber_eq_of_dist_bounded

@[simp]
theorem translationNumber_one : œÑ 1 = 0 :=
  translationNumber_eq_of_tendsto‚ÇÄ _ <| by simp [tendsto_const_nhds]
#align circle_deg1_lift.translation_number_one CircleDeg1Lift.translationNumber_one

theorem translationNumber_eq_of_semiconjBy {f g‚ÇÅ g‚ÇÇ : CircleDeg1Lift} (H : SemiconjBy f g‚ÇÅ g‚ÇÇ) :
    œÑ g‚ÇÅ = œÑ g‚ÇÇ :=
  translationNumber_eq_of_dist_bounded 2 fun n =>
    le_of_lt <| dist_map_zero_lt_of_semiconjBy <| H.pow_right n
#align circle_deg1_lift.translation_number_eq_of_semiconj_by CircleDeg1Lift.translationNumber_eq_of_semiconjBy

theorem translationNumber_eq_of_semiconj {f g‚ÇÅ g‚ÇÇ : CircleDeg1Lift}
    (H : Function.Semiconj f g‚ÇÅ g‚ÇÇ) : œÑ g‚ÇÅ = œÑ g‚ÇÇ :=
  translationNumber_eq_of_semiconjBy <| semiconjBy_iff_semiconj.2 H
#align circle_deg1_lift.translation_number_eq_of_semiconj CircleDeg1Lift.translationNumber_eq_of_semiconj

theorem translationNumber_mul_of_commute {f g : CircleDeg1Lift} (h : Commute f g) :
    œÑ (f * g) = œÑ f + œÑ g := by
  refine tendsto_nhds_unique ?_
    (f.tendsto_translationNumber_aux.add g.tendsto_translationNumber_aux)
  simp only [transnumAuxSeq, ‚Üê add_div]
  refine (f * g).tendsto_translationNumber_of_dist_bounded_aux
    (fun n ‚Ü¶ (f ^ n) 0 + (g ^ n) 0) 1 fun n ‚Ü¶ ?_
  rw [h.mul_pow, dist_comm]
  exact le_of_lt ((f ^ n).dist_map_map_zero_lt (g ^ n))
#align circle_deg1_lift.translation_number_mul_of_commute CircleDeg1Lift.translationNumber_mul_of_commute

@[simp]
theorem translationNumber_units_inv (f : CircleDeg1LiftÀ£) : œÑ ‚Üëf‚Åª¬π = -œÑ f :=
  eq_neg_iff_add_eq_zero.2 <| by
    simp [‚Üê translationNumber_mul_of_commute (Commute.refl _).units_inv_left]
#align circle_deg1_lift.translation_number_units_inv CircleDeg1Lift.translationNumber_units_inv

@[simp]
theorem translationNumber_pow : ‚àÄ n : ‚Ñï, œÑ (f ^ n) = n * œÑ f
  | 0 => by simp
  | n + 1 => by
    rw [pow_succ, translationNumber_mul_of_commute (Commute.pow_self f n),
      translationNumber_pow n, Nat.cast_add_one, add_mul, one_mul]
#align circle_deg1_lift.translation_number_pow CircleDeg1Lift.translationNumber_pow

@[simp]
theorem translationNumber_zpow (f : CircleDeg1LiftÀ£) : ‚àÄ n : ‚Ñ§, œÑ (f ^ n : Units _) = n * œÑ f
  | (n : ‚Ñï) => by simp [translationNumber_pow f n]
  | -[n+1] => by simp; ring
#align circle_deg1_lift.translation_number_zpow CircleDeg1Lift.translationNumber_zpow

@[simp]
theorem translationNumber_conj_eq (f : CircleDeg1LiftÀ£) (g : CircleDeg1Lift) :
    œÑ (‚Üëf * g * ‚Üëf‚Åª¬π) = œÑ g :=
  (translationNumber_eq_of_semiconjBy (f.mk_semiconjBy g)).symm
#align circle_deg1_lift.translation_number_conj_eq CircleDeg1Lift.translationNumber_conj_eq

@[simp]
theorem translationNumber_conj_eq' (f : CircleDeg1LiftÀ£) (g : CircleDeg1Lift) :
    œÑ (‚Üëf‚Åª¬π * g * f) = œÑ g :=
  translationNumber_conj_eq f‚Åª¬π g
#align circle_deg1_lift.translation_number_conj_eq' CircleDeg1Lift.translationNumber_conj_eq'

theorem dist_pow_map_zero_mul_translationNumber_le (n : ‚Ñï) :
    dist ((f ^ n) 0) (n * f.translationNumber) ‚â§ 1 :=
  f.translationNumber_pow n ‚ñ∏ (f ^ n).dist_map_zero_translationNumber_le
#align circle_deg1_lift.dist_pow_map_zero_mul_translation_number_le CircleDeg1Lift.dist_pow_map_zero_mul_translationNumber_le

theorem tendsto_translation_number‚ÇÄ' :
    Tendsto (fun n : ‚Ñï => (f ^ (n + 1) : CircleDeg1Lift) 0 / ((n : ‚Ñù) + 1)) atTop (ùìù <| œÑ f) := by
  refine
    tendsto_iff_dist_tendsto_zero.2 <|
      squeeze_zero (fun _ => dist_nonneg) (fun n => ?_)
        ((tendsto_const_div_atTop_nhds_zero_nat 1).comp (tendsto_add_atTop_nat 1))
  dsimp
  have : (0 : ‚Ñù) < n + 1 := n.cast_add_one_pos
  rw [Real.dist_eq, div_sub' _ _ _ (ne_of_gt this), abs_div, ‚Üê Real.dist_eq, abs_of_pos this,
    Nat.cast_add_one, div_le_div_right this, ‚Üê Nat.cast_add_one]
  apply dist_pow_map_zero_mul_translationNumber_le
#align circle_deg1_lift.tendsto_translation_number‚ÇÄ' CircleDeg1Lift.tendsto_translation_number‚ÇÄ'

theorem tendsto_translation_number‚ÇÄ : Tendsto (fun n : ‚Ñï => (f ^ n) 0 / n) atTop (ùìù <| œÑ f) :=
  (tendsto_add_atTop_iff_nat 1).1 (mod_cast f.tendsto_translation_number‚ÇÄ')
#align circle_deg1_lift.tendsto_translation_number‚ÇÄ CircleDeg1Lift.tendsto_translation_number‚ÇÄ

/-- For any `x : ‚Ñù` the sequence $\frac{f^n(x)-x}{n}$ tends to the translation number of `f`.
In particular, this limit does not depend on `x`. -/
theorem tendsto_translationNumber (x : ‚Ñù) :
    Tendsto (fun n : ‚Ñï => ((f ^ n) x - x) / n) atTop (ùìù <| œÑ f) := by
  rw [‚Üê translationNumber_conj_eq' (translate <| Multiplicative.ofAdd x)]
  refine (tendsto_translation_number‚ÇÄ _).congr fun n ‚Ü¶ ?_
  simp [sub_eq_neg_add, Units.conj_pow']
#align circle_deg1_lift.tendsto_translation_number CircleDeg1Lift.tendsto_translationNumber

theorem tendsto_translation_number' (x : ‚Ñù) :
    Tendsto (fun n : ‚Ñï => ((f ^ (n + 1) : CircleDeg1Lift) x - x) / (n + 1)) atTop (ùìù <| œÑ f) :=
  mod_cast (tendsto_add_atTop_iff_nat 1).2 (f.tendsto_translationNumber x)
#align circle_deg1_lift.tendsto_translation_number' CircleDeg1Lift.tendsto_translation_number'

theorem translationNumber_mono : Monotone œÑ := fun f g h =>
  le_of_tendsto_of_tendsto' f.tendsto_translation_number‚ÇÄ g.tendsto_translation_number‚ÇÄ fun n => by
    gcongr; exact pow_mono h _ _
#align circle_deg1_lift.translation_number_mono CircleDeg1Lift.translationNumber_mono

theorem translationNumber_translate (x : ‚Ñù) : œÑ (translate <| Multiplicative.ofAdd x) = x :=
  translationNumber_eq_of_tendsto‚ÇÄ' _ <| by
    simp only [translate_iterate, translate_apply, add_zero, Nat.cast_succ,
      mul_div_cancel_left‚ÇÄ (M‚ÇÄ := ‚Ñù) _ (Nat.cast_add_one_ne_zero _), tendsto_const_nhds]
#align circle_deg1_lift.translation_number_translate CircleDeg1Lift.translationNumber_translate

theorem translationNumber_le_of_le_add {z : ‚Ñù} (hz : ‚àÄ x, f x ‚â§ x + z) : œÑ f ‚â§ z :=
  translationNumber_translate z ‚ñ∏ translationNumber_mono fun x => (hz x).trans_eq (add_comm _ _)
#align circle_deg1_lift.translation_number_le_of_le_add CircleDeg1Lift.translationNumber_le_of_le_add

theorem le_translationNumber_of_add_le {z : ‚Ñù} (hz : ‚àÄ x, x + z ‚â§ f x) : z ‚â§ œÑ f :=
  translationNumber_translate z ‚ñ∏ translationNumber_mono fun x => (add_comm _ _).trans_le (hz x)
#align circle_deg1_lift.le_translation_number_of_add_le CircleDeg1Lift.le_translationNumber_of_add_le

theorem translationNumber_le_of_le_add_int {x : ‚Ñù} {m : ‚Ñ§} (h : f x ‚â§ x + m) : œÑ f ‚â§ m :=
  le_of_tendsto' (f.tendsto_translation_number' x) fun n =>
    (div_le_iff' (n.cast_add_one_pos : (0 : ‚Ñù) < _)).mpr <| sub_le_iff_le_add'.2 <|
      (coe_pow f (n + 1)).symm ‚ñ∏ @Nat.cast_add_one ‚Ñù _ n ‚ñ∏ f.iterate_le_of_map_le_add_int h (n + 1)
#align circle_deg1_lift.translation_number_le_of_le_add_int CircleDeg1Lift.translationNumber_le_of_le_add_int

theorem translationNumber_le_of_le_add_nat {x : ‚Ñù} {m : ‚Ñï} (h : f x ‚â§ x + m) : œÑ f ‚â§ m :=
  @translationNumber_le_of_le_add_int f x m h
#align circle_deg1_lift.translation_number_le_of_le_add_nat CircleDeg1Lift.translationNumber_le_of_le_add_nat

theorem le_translationNumber_of_add_int_le {x : ‚Ñù} {m : ‚Ñ§} (h : x + m ‚â§ f x) : ‚Üëm ‚â§ œÑ f :=
  ge_of_tendsto' (f.tendsto_translation_number' x) fun n =>
    (le_div_iff (n.cast_add_one_pos : (0 : ‚Ñù) < _)).mpr <| le_sub_iff_add_le'.2 <| by
      simp only [coe_pow, mul_comm (m : ‚Ñù), ‚Üê Nat.cast_add_one, f.le_iterate_of_add_int_le_map h]
#align circle_deg1_lift.le_translation_number_of_add_int_le CircleDeg1Lift.le_translationNumber_of_add_int_le

theorem le_translationNumber_of_add_nat_le {x : ‚Ñù} {m : ‚Ñï} (h : x + m ‚â§ f x) : ‚Üëm ‚â§ œÑ f :=
  @le_translationNumber_of_add_int_le f x m h
#align circle_deg1_lift.le_translation_number_of_add_nat_le CircleDeg1Lift.le_translationNumber_of_add_nat_le

/-- If `f x - x` is an integer number `m` for some point `x`, then `œÑ f = m`.
On the circle this means that a map with a fixed point has rotation number zero. -/
theorem translationNumber_of_eq_add_int {x : ‚Ñù} {m : ‚Ñ§} (h : f x = x + m) : œÑ f = m :=
  le_antisymm (translationNumber_le_of_le_add_int f <| le_of_eq h)
    (le_translationNumber_of_add_int_le f <| le_of_eq h.symm)
#align circle_deg1_lift.translation_number_of_eq_add_int CircleDeg1Lift.translationNumber_of_eq_add_int

theorem floor_sub_le_translationNumber (x : ‚Ñù) : ‚Üë‚åäf x - x‚åã ‚â§ œÑ f :=
  le_translationNumber_of_add_int_le f <| le_sub_iff_add_le'.1 (floor_le <| f x - x)
#align circle_deg1_lift.floor_sub_le_translation_number CircleDeg1Lift.floor_sub_le_translationNumber

theorem translationNumber_le_ceil_sub (x : ‚Ñù) : œÑ f ‚â§ ‚åàf x - x‚åâ :=
  translationNumber_le_of_le_add_int f <| sub_le_iff_le_add'.1 (le_ceil <| f x - x)
#align circle_deg1_lift.translation_number_le_ceil_sub CircleDeg1Lift.translationNumber_le_ceil_sub

theorem map_lt_of_translationNumber_lt_int {n : ‚Ñ§} (h : œÑ f < n) (x : ‚Ñù) : f x < x + n :=
  not_le.1 <| mt f.le_translationNumber_of_add_int_le <| not_le.2 h
#align circle_deg1_lift.map_lt_of_translation_number_lt_int CircleDeg1Lift.map_lt_of_translationNumber_lt_int

theorem map_lt_of_translationNumber_lt_nat {n : ‚Ñï} (h : œÑ f < n) (x : ‚Ñù) : f x < x + n :=
  @map_lt_of_translationNumber_lt_int f n h x
#align circle_deg1_lift.map_lt_of_translation_number_lt_nat CircleDeg1Lift.map_lt_of_translationNumber_lt_nat

theorem map_lt_add_floor_translationNumber_add_one (x : ‚Ñù) : f x < x + ‚åäœÑ f‚åã + 1 := by
  rw [add_assoc]
  norm_cast
  refine map_lt_of_translationNumber_lt_int _ ?_ _
  push_cast
  exact lt_floor_add_one _
#align circle_deg1_lift.map_lt_add_floor_translation_number_add_one CircleDeg1Lift.map_lt_add_floor_translationNumber_add_one

theorem map_lt_add_translationNumber_add_one (x : ‚Ñù) : f x < x + œÑ f + 1 :=
  calc
    f x < x + ‚åäœÑ f‚åã + 1 := f.map_lt_add_floor_translationNumber_add_one x
    _ ‚â§ x + œÑ f + 1 := by gcongr; apply floor_le
#align circle_deg1_lift.map_lt_add_translation_number_add_one CircleDeg1Lift.map_lt_add_translationNumber_add_one

theorem lt_map_of_int_lt_translationNumber {n : ‚Ñ§} (h : ‚Üën < œÑ f) (x : ‚Ñù) : x + n < f x :=
  not_le.1 <| mt f.translationNumber_le_of_le_add_int <| not_le.2 h
#align circle_deg1_lift.lt_map_of_int_lt_translation_number CircleDeg1Lift.lt_map_of_int_lt_translationNumber

theorem lt_map_of_nat_lt_translationNumber {n : ‚Ñï} (h : ‚Üën < œÑ f) (x : ‚Ñù) : x + n < f x :=
  @lt_map_of_int_lt_translationNumber f n h x
#align circle_deg1_lift.lt_map_of_nat_lt_translation_number CircleDeg1Lift.lt_map_of_nat_lt_translationNumber

/-- If `f^n x - x`, `n > 0`, is an integer number `m` for some point `x`, then
`œÑ f = m / n`. On the circle this means that a map with a periodic orbit has
a rational rotation number. -/
theorem translationNumber_of_map_pow_eq_add_int {x : ‚Ñù} {n : ‚Ñï} {m : ‚Ñ§} (h : (f ^ n) x = x + m)
    (hn : 0 < n) : œÑ f = m / n := by
  have := (f ^ n).translationNumber_of_eq_add_int h
  rwa [translationNumber_pow, mul_comm, ‚Üê eq_div_iff] at this
  exact Nat.cast_ne_zero.2 (ne_of_gt hn)
#align circle_deg1_lift.translation_number_of_map_pow_eq_add_int CircleDeg1Lift.translationNumber_of_map_pow_eq_add_int

/-- If a predicate depends only on `f x - x` and holds for all `0 ‚â§ x ‚â§ 1`,
then it holds for all `x`. -/
theorem forall_map_sub_of_Icc (P : ‚Ñù ‚Üí Prop) (h : ‚àÄ x ‚àà Icc (0 : ‚Ñù) 1, P (f x - x)) (x : ‚Ñù) :
    P (f x - x) :=
  f.map_fract_sub_fract_eq x ‚ñ∏ h _ ‚ü®fract_nonneg _, le_of_lt (fract_lt_one _)‚ü©
#align circle_deg1_lift.forall_map_sub_of_Icc CircleDeg1Lift.forall_map_sub_of_Icc

theorem translationNumber_lt_of_forall_lt_add (hf : Continuous f) {z : ‚Ñù} (hz : ‚àÄ x, f x < x + z) :
    œÑ f < z := by
  obtain ‚ü®x, -, hx‚ü© : ‚àÉ x ‚àà Icc (0 : ‚Ñù) 1, ‚àÄ y ‚àà Icc (0 : ‚Ñù) 1, f y - y ‚â§ f x - x :=
    isCompact_Icc.exists_isMaxOn (nonempty_Icc.2 zero_le_one)
      (hf.sub continuous_id).continuousOn
  refine lt_of_le_of_lt ?_ (sub_lt_iff_lt_add'.2 <| hz x)
  apply translationNumber_le_of_le_add
  simp only [‚Üê sub_le_iff_le_add']
  exact f.forall_map_sub_of_Icc (fun a => a ‚â§ f x - x) hx
#align circle_deg1_lift.translation_number_lt_of_forall_lt_add CircleDeg1Lift.translationNumber_lt_of_forall_lt_add

theorem lt_translationNumber_of_forall_add_lt (hf : Continuous f) {z : ‚Ñù} (hz : ‚àÄ x, x + z < f x) :
    z < œÑ f := by
  obtain ‚ü®x, -, hx‚ü© : ‚àÉ x ‚àà Icc (0 : ‚Ñù) 1, ‚àÄ y ‚àà Icc (0 : ‚Ñù) 1, f x - x ‚â§ f y - y
  ¬∑ exact isCompact_Icc.exists_isMinOn (nonempty_Icc.2 zero_le_one)
      (hf.sub continuous_id).continuousOn
  refine lt_of_lt_of_le (lt_sub_iff_add_lt'.2 <| hz x) ?_
  apply le_translationNumber_of_add_le
  simp only [‚Üê le_sub_iff_add_le']
  exact f.forall_map_sub_of_Icc _ hx
#align circle_deg1_lift.lt_translation_number_of_forall_add_lt CircleDeg1Lift.lt_translationNumber_of_forall_add_lt

/-- If `f` is a continuous monotone map `‚Ñù ‚Üí ‚Ñù`, `f (x + 1) = f x + 1`, then there exists `x`
such that `f x = x + œÑ f`. -/
theorem exists_eq_add_translationNumber (hf : Continuous f) : ‚àÉ x, f x = x + œÑ f := by
  obtain ‚ü®a, ha‚ü© : ‚àÉ x, f x ‚â§ x + œÑ f := by
    by_contra! H
    exact lt_irrefl _ (f.lt_translationNumber_of_forall_add_lt hf H)
  obtain ‚ü®b, hb‚ü© : ‚àÉ x, x + œÑ f ‚â§ f x := by
    by_contra! H
    exact lt_irrefl _ (f.translationNumber_lt_of_forall_lt_add hf H)
  exact intermediate_value_univ‚ÇÇ hf (continuous_id.add continuous_const) ha hb
#align circle_deg1_lift.exists_eq_add_translation_number CircleDeg1Lift.exists_eq_add_translationNumber

theorem translationNumber_eq_int_iff (hf : Continuous f) {m : ‚Ñ§} :
    œÑ f = m ‚Üî ‚àÉ x : ‚Ñù, f x = x + m := by
  constructor
  ¬∑ intro h
    simp only [‚Üê h]
    exact f.exists_eq_add_translationNumber hf
  ¬∑ rintro ‚ü®x, hx‚ü©
    exact f.translationNumber_of_eq_add_int hx
#align circle_deg1_lift.translation_number_eq_int_iff CircleDeg1Lift.translationNumber_eq_int_iff

theorem continuous_pow (hf : Continuous f) (n : ‚Ñï) : Continuous (f ^ n : CircleDeg1Lift) := by
  rw [coe_pow]
  exact hf.iterate n
#align circle_deg1_lift.continuous_pow CircleDeg1Lift.continuous_pow

theorem translationNumber_eq_rat_iff (hf : Continuous f) {m : ‚Ñ§} {n : ‚Ñï} (hn : 0 < n) :
    œÑ f = m / n ‚Üî ‚àÉ x, (f ^ n) x = x + m := by
  rw [eq_div_iff, mul_comm, ‚Üê translationNumber_pow] <;> [skip; exact ne_of_gt (Nat.cast_pos.2 hn)]
  exact (f ^ n).translationNumber_eq_int_iff (f.continuous_pow hf n)
#align circle_deg1_lift.translation_number_eq_rat_iff CircleDeg1Lift.translationNumber_eq_rat_iff

/-- Consider two actions `f‚ÇÅ f‚ÇÇ : G ‚Üí* CircleDeg1Lift` of a group on the real line by lifts of
orientation preserving circle homeomorphisms. Suppose that for each `g : G` the homeomorphisms
`f‚ÇÅ g` and `f‚ÇÇ g` have equal rotation numbers. Then there exists `F : CircleDeg1Lift` such that
`F * f‚ÇÅ g = f‚ÇÇ g * F` for all `g : G`.

This is a version of Proposition 5.4 from [√âtienne Ghys, Groupes d'homeomorphismes du cercle et
cohomologie bornee][ghys87:groupes]. -/
theorem semiconj_of_group_action_of_forall_translationNumber_eq {G : Type*} [Group G]
    (f‚ÇÅ f‚ÇÇ : G ‚Üí* CircleDeg1Lift) (h : ‚àÄ g, œÑ (f‚ÇÅ g) = œÑ (f‚ÇÇ g)) :
    ‚àÉ F : CircleDeg1Lift, ‚àÄ g, Semiconj F (f‚ÇÅ g) (f‚ÇÇ g) := by
  -- Equality of translation number guarantees that for each `x`
  -- the set `{f‚ÇÇ g‚Åª¬π (f‚ÇÅ g x) | g : G}` is bounded above.
  have : ‚àÄ x, BddAbove (range fun g => f‚ÇÇ g‚Åª¬π (f‚ÇÅ g x)) := by
    refine fun x => ‚ü®x + 2, ?_‚ü©
    rintro _ ‚ü®g, rfl‚ü©
    have : œÑ (f‚ÇÇ g‚Åª¬π) = -œÑ (f‚ÇÇ g) := by
      rw [‚Üê MonoidHom.coe_toHomUnits, MonoidHom.map_inv, translationNumber_units_inv,
        MonoidHom.coe_toHomUnits]
    calc
      f‚ÇÇ g‚Åª¬π (f‚ÇÅ g x) ‚â§ f‚ÇÇ g‚Åª¬π (x + œÑ (f‚ÇÅ g) + 1) :=
        mono _ (map_lt_add_translationNumber_add_one _ _).le
      _ = f‚ÇÇ g‚Åª¬π (x + œÑ (f‚ÇÇ g)) + 1 := by rw [h, map_add_one]
      _ ‚â§ x + œÑ (f‚ÇÇ g) + œÑ (f‚ÇÇ g‚Åª¬π) + 1 + 1 :=
        add_le_add_right (map_lt_add_translationNumber_add_one _ _).le _
      _ = x + 2 := by simp [this, add_assoc, one_add_one_eq_two]
  -- We have a theorem about actions by `OrderIso`, so we introduce auxiliary maps
  -- to `‚Ñù ‚âÉo ‚Ñù`.
  set F‚ÇÅ := toOrderIso.comp f‚ÇÅ.toHomUnits
  set F‚ÇÇ := toOrderIso.comp f‚ÇÇ.toHomUnits
  have hF‚ÇÅ : ‚àÄ g, ‚áë(F‚ÇÅ g) = f‚ÇÅ g := fun _ => rfl
  have hF‚ÇÇ : ‚àÄ g, ‚áë(F‚ÇÇ g) = f‚ÇÇ g := fun _ => rfl
  -- Now we apply `csSup_div_semiconj` and go back to `f‚ÇÅ` and `f‚ÇÇ`.
  refine ‚ü®‚ü®‚ü®_, fun x y hxy => ?_‚ü©, fun x => ?_‚ü©, csSup_div_semiconj F‚ÇÇ F‚ÇÅ fun x => ?_‚ü© <;>
    simp only [hF‚ÇÅ, hF‚ÇÇ, ‚Üê map_inv, coe_mk]
  ¬∑ exact ciSup_mono (this y) fun g => mono _ (mono _ hxy)
  ¬∑ simp only [map_add_one]
    exact (Monotone.map_ciSup_of_continuousAt (continuousAt_id.add continuousAt_const)
      (monotone_id.add_const (1 : ‚Ñù)) (this x)).symm
  ¬∑ exact this x
#align circle_deg1_lift.semiconj_of_group_action_of_forall_translation_number_eq CircleDeg1Lift.semiconj_of_group_action_of_forall_translationNumber_eq

/-- If two lifts of circle homeomorphisms have the same translation number, then they are
semiconjugate by a `CircleDeg1Lift`. This version uses arguments `f‚ÇÅ f‚ÇÇ : CircleDeg1LiftÀ£`
to assume that `f‚ÇÅ` and `f‚ÇÇ` are homeomorphisms. -/
theorem units_semiconj_of_translationNumber_eq {f‚ÇÅ f‚ÇÇ : CircleDeg1LiftÀ£} (h : œÑ f‚ÇÅ = œÑ f‚ÇÇ) :
    ‚àÉ F : CircleDeg1Lift, Semiconj F f‚ÇÅ f‚ÇÇ :=
  have : ‚àÄ n : Multiplicative ‚Ñ§,
      œÑ ((Units.coeHom _).comp (zpowersHom _ f‚ÇÅ) n) =
        œÑ ((Units.coeHom _).comp (zpowersHom _ f‚ÇÇ) n) := fun n ‚Ü¶ by
    simp [h]
  (semiconj_of_group_action_of_forall_translationNumber_eq _ _ this).imp fun F hF => by
    simpa using hF (Multiplicative.ofAdd 1)
#align circle_deg1_lift.units_semiconj_of_translation_number_eq CircleDeg1Lift.units_semiconj_of_translationNumber_eq

/-- If two lifts of circle homeomorphisms have the same translation number, then they are
semiconjugate by a `CircleDeg1Lift`. This version uses assumptions `IsUnit f‚ÇÅ` and `IsUnit f‚ÇÇ`
to assume that `f‚ÇÅ` and `f‚ÇÇ` are homeomorphisms. -/
theorem semiconj_of_isUnit_of_translationNumber_eq {f‚ÇÅ f‚ÇÇ : CircleDeg1Lift} (h‚ÇÅ : IsUnit f‚ÇÅ)
    (h‚ÇÇ : IsUnit f‚ÇÇ) (h : œÑ f‚ÇÅ = œÑ f‚ÇÇ) : ‚àÉ F : CircleDeg1Lift, Semiconj F f‚ÇÅ f‚ÇÇ := by
  rcases h‚ÇÅ, h‚ÇÇ with ‚ü®‚ü®f‚ÇÅ, rfl‚ü©, ‚ü®f‚ÇÇ, rfl‚ü©‚ü©
  exact units_semiconj_of_translationNumber_eq h
#align circle_deg1_lift.semiconj_of_is_unit_of_translation_number_eq CircleDeg1Lift.semiconj_of_isUnit_of_translationNumber_eq

/-- If two lifts of circle homeomorphisms have the same translation number, then they are
semiconjugate by a `CircleDeg1Lift`. This version uses assumptions `bijective f‚ÇÅ` and
`bijective f‚ÇÇ` to assume that `f‚ÇÅ` and `f‚ÇÇ` are homeomorphisms. -/
theorem semiconj_of_bijective_of_translationNumber_eq {f‚ÇÅ f‚ÇÇ : CircleDeg1Lift} (h‚ÇÅ : Bijective f‚ÇÅ)
    (h‚ÇÇ : Bijective f‚ÇÇ) (h : œÑ f‚ÇÅ = œÑ f‚ÇÇ) : ‚àÉ F : CircleDeg1Lift, Semiconj F f‚ÇÅ f‚ÇÇ :=
  semiconj_of_isUnit_of_translationNumber_eq (isUnit_iff_bijective.2 h‚ÇÅ) (isUnit_iff_bijective.2 h‚ÇÇ)
    h
#align circle_deg1_lift.semiconj_of_bijective_of_translation_number_eq CircleDeg1Lift.semiconj_of_bijective_of_translationNumber_eq

end CircleDeg1Lift
