/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Reid Barton
-/
import Mathlib.Topology.Separation

#align_import topology.shrinking_lemma from "leanprover-community/mathlib"@"4c19a16e4b705bf135cf9a80ac18fcc99c438514"

/-!
# The shrinking lemma

In this file we prove a few versions of the shrinking lemma. The lemma says that in a normal
topological space a point finite open covering can be â€œshrunkâ€: for a point finite open covering
`u : Î¹ â†’ Set X` there exists a refinement `v : Î¹ â†’ Set X` such that `closure (v i) âŠ† u i`.

For finite or countable coverings this lemma can be proved without the axiom of choice, see
[ncatlab](https://ncatlab.org/nlab/show/shrinking+lemma) for details. We only formalize the most
general result that works for any covering but needs the axiom of choice.

We prove two versions of the lemma:

* `exists_subset_iUnion_closure_subset` deals with a covering of a closed set in a normal space;
* `exists_iUnion_eq_closure_subset` deals with a covering of the whole space.

## Tags

normal space, shrinking lemma
-/


open Set Function

open scoped Classical

noncomputable section

variable {Î¹ X : Type*} [TopologicalSpace X] [NormalSpace X]

namespace ShrinkingLemma

-- the trivial refinement needs `u` to be a covering
/-- Auxiliary definition for the proof of the shrinking lemma. A partial refinement of a covering
`â‹ƒ i, u i` of a set `s` is a map `v : Î¹ â†’ Set X` and a set `carrier : Set Î¹` such that

* `s âŠ† â‹ƒ i, v i`;
* all `v i` are open;
* if `i âˆˆ carrier v`, then `closure (v i) âŠ† u i`;
* if `i âˆ‰ carrier`, then `v i = u i`.

This type is equipped with the following partial order: `v â‰¤ v'` if `v.carrier âŠ† v'.carrier`
and `v i = v' i` for `i âˆˆ v.carrier`. We will use Zorn's lemma to prove that this type has
a maximal element, then show that the maximal element must have `carrier = univ`. -/
-- porting note: @[nolint has_nonempty_instance] is not here yet
@[ext] structure PartialRefinement (u : Î¹ â†’ Set X) (s : Set X) where
  /-- A family of sets that form a partial refinement of `u`. -/
  toFun : Î¹ â†’ Set X
  /-- The set of indexes `i` such that `i`-th set is already shrunk. -/
  carrier : Set Î¹
  /-- Each set from the partially refined family is open. -/
  protected isOpen : âˆ€ i, IsOpen (toFun i)
  /-- The partially refined family still covers the set. -/
  subset_iUnion : s âŠ† â‹ƒ i, toFun i
  /-- For each `i âˆˆ carrier`, the original set includes the closure of the refined set. -/
  closure_subset : âˆ€ {i}, i âˆˆ carrier â†’ closure (toFun i) âŠ† u i
  /-- Sets that correspond to `i âˆ‰ carrier` are not modified. -/
  apply_eq : âˆ€ {i}, i âˆ‰ carrier â†’ toFun i = u i
#align shrinking_lemma.partial_refinement ShrinkingLemma.PartialRefinement

namespace PartialRefinement

variable {u : Î¹ â†’ Set X} {s : Set X}

instance : CoeFun (PartialRefinement u s) fun _ => Î¹ â†’ Set X := âŸ¨toFunâŸ©

#align shrinking_lemma.partial_refinement.subset_Union ShrinkingLemma.PartialRefinement.subset_iUnion
#align shrinking_lemma.partial_refinement.closure_subset ShrinkingLemma.PartialRefinement.closure_subset
#align shrinking_lemma.partial_refinement.apply_eq ShrinkingLemma.PartialRefinement.apply_eq
#align shrinking_lemma.partial_refinement.is_open ShrinkingLemma.PartialRefinement.isOpen

protected theorem subset (v : PartialRefinement u s) (i : Î¹) : v i âŠ† u i :=
  if h : i âˆˆ v.carrier then subset_closure.trans (v.closure_subset h) else (v.apply_eq h).le
#align shrinking_lemma.partial_refinement.subset ShrinkingLemma.PartialRefinement.subset

instance : PartialOrder (PartialRefinement u s) where
  le vâ‚ vâ‚‚ := vâ‚.carrier âŠ† vâ‚‚.carrier âˆ§ âˆ€ i âˆˆ vâ‚.carrier, vâ‚ i = vâ‚‚ i
  le_refl v := âŸ¨Subset.refl _, fun _ _ => rflâŸ©
  le_trans vâ‚ vâ‚‚ vâ‚ƒ hâ‚â‚‚ hâ‚‚â‚ƒ :=
    âŸ¨Subset.trans hâ‚â‚‚.1 hâ‚‚â‚ƒ.1, fun i hi => (hâ‚â‚‚.2 i hi).trans (hâ‚‚â‚ƒ.2 i <| hâ‚â‚‚.1 hi)âŸ©
  le_antisymm vâ‚ vâ‚‚ hâ‚â‚‚ hâ‚‚â‚ :=
    have hc : vâ‚.carrier = vâ‚‚.carrier := Subset.antisymm hâ‚â‚‚.1 hâ‚‚â‚.1
    PartialRefinement.ext _ _
      (funext fun x =>
        if hx : x âˆˆ vâ‚.carrier then hâ‚â‚‚.2 _ hx
        else (vâ‚.apply_eq hx).trans (Eq.symm <| vâ‚‚.apply_eq <| hc â–¸ hx))
      hc

/-- If two partial refinements `vâ‚`, `vâ‚‚` belong to a chain (hence, they are comparable)
and `i` belongs to the carriers of both partial refinements, then `vâ‚ i = vâ‚‚ i`. -/
theorem apply_eq_of_chain {c : Set (PartialRefinement u s)} (hc : IsChain (Â· â‰¤ Â·) c) {vâ‚ vâ‚‚}
    (hâ‚ : vâ‚ âˆˆ c) (hâ‚‚ : vâ‚‚ âˆˆ c) {i} (hiâ‚ : i âˆˆ vâ‚.carrier) (hiâ‚‚ : i âˆˆ vâ‚‚.carrier) :
    vâ‚ i = vâ‚‚ i :=
  (hc.total hâ‚ hâ‚‚).elim (fun hle => hle.2 _ hiâ‚) (fun hle => (hle.2 _ hiâ‚‚).symm)
#align shrinking_lemma.partial_refinement.apply_eq_of_chain ShrinkingLemma.PartialRefinement.apply_eq_of_chain

/-- The carrier of the least upper bound of a non-empty chain of partial refinements is the union of
their carriers. -/
def chainSupCarrier (c : Set (PartialRefinement u s)) : Set Î¹ :=
  â‹ƒ v âˆˆ c, carrier v
#align shrinking_lemma.partial_refinement.chain_Sup_carrier ShrinkingLemma.PartialRefinement.chainSupCarrier

/-- Choice of an element of a nonempty chain of partial refinements. If `i` belongs to one of
`carrier v`, `v âˆˆ c`, then `find c ne i` is one of these partial refinements. -/
def find (c : Set (PartialRefinement u s)) (ne : c.Nonempty) (i : Î¹) : PartialRefinement u s :=
  if hi : âˆƒ v âˆˆ c, i âˆˆ carrier v then hi.choose else ne.some
#align shrinking_lemma.partial_refinement.find ShrinkingLemma.PartialRefinement.find

theorem find_mem {c : Set (PartialRefinement u s)} (i : Î¹) (ne : c.Nonempty) : find c ne i âˆˆ c := by
  rw [find]
  -- âŠ¢ (if hi : âˆƒ v, v âˆˆ c âˆ§ i âˆˆ v.carrier then Exists.choose hi else Set.Nonempty. â€¦
  split_ifs with h
  -- âŠ¢ Exists.choose h âˆˆ c
  exacts [h.choose_spec.1, ne.some_mem]
  -- ğŸ‰ no goals
#align shrinking_lemma.partial_refinement.find_mem ShrinkingLemma.PartialRefinement.find_mem

theorem mem_find_carrier_iff {c : Set (PartialRefinement u s)} {i : Î¹} (ne : c.Nonempty) :
    i âˆˆ (find c ne i).carrier â†” i âˆˆ chainSupCarrier c := by
  rw [find]
  -- âŠ¢ i âˆˆ (if hi : âˆƒ v, v âˆˆ c âˆ§ i âˆˆ v.carrier then Exists.choose hi else Set.Nonem â€¦
  split_ifs with h
  -- âŠ¢ i âˆˆ (Exists.choose h).carrier â†” i âˆˆ chainSupCarrier c
  Â· have := h.choose_spec
    -- âŠ¢ i âˆˆ (Exists.choose h).carrier â†” i âˆˆ chainSupCarrier c
    exact iff_of_true this.2 (mem_iUnionâ‚‚.2 âŸ¨_, this.1, this.2âŸ©)
    -- ğŸ‰ no goals
  Â· push_neg at h
    -- âŠ¢ i âˆˆ (Set.Nonempty.some ne).carrier â†” i âˆˆ chainSupCarrier c
    refine iff_of_false (h _ ne.some_mem) ?_
    -- âŠ¢ Â¬i âˆˆ chainSupCarrier c
    simpa only [chainSupCarrier, mem_iUnionâ‚‚, not_exists]
    -- ğŸ‰ no goals
#align shrinking_lemma.partial_refinement.mem_find_carrier_iff ShrinkingLemma.PartialRefinement.mem_find_carrier_iff

theorem find_apply_of_mem {c : Set (PartialRefinement u s)} (hc : IsChain (Â· â‰¤ Â·) c)
    (ne : c.Nonempty) {i v} (hv : v âˆˆ c) (hi : i âˆˆ carrier v) : find c ne i i = v i :=
  apply_eq_of_chain hc (find_mem _ _) hv ((mem_find_carrier_iff _).2 <| mem_iUnionâ‚‚.2 âŸ¨v, hv, hiâŸ©)
    hi
#align shrinking_lemma.partial_refinement.find_apply_of_mem ShrinkingLemma.PartialRefinement.find_apply_of_mem

/-- Least upper bound of a nonempty chain of partial refinements. -/
def chainSup (c : Set (PartialRefinement u s)) (hc : IsChain (Â· â‰¤ Â·) c) (ne : c.Nonempty)
    (hfin : âˆ€ x âˆˆ s, { i | x âˆˆ u i }.Finite) (hU : s âŠ† â‹ƒ i, u i) : PartialRefinement u s where
  toFun i := find c ne i i
  carrier := chainSupCarrier c
  isOpen i := (find _ _ _).isOpen i
  subset_iUnion x hxs := mem_iUnion.2 <| by
    rcases em (âˆƒ i, i âˆ‰ chainSupCarrier c âˆ§ x âˆˆ u i) with (âŸ¨i, hi, hxiâŸ© | hx)
    -- âŠ¢ âˆƒ i, x âˆˆ (fun i => toFun (find c ne i) i) i
    Â· use i
      -- âŠ¢ x âˆˆ (fun i => toFun (find c ne i) i) i
      simpa only [(find c ne i).apply_eq (mt (mem_find_carrier_iff _).1 hi)]
      -- ğŸ‰ no goals
    Â· simp_rw [not_exists, not_and, not_imp_not, chainSupCarrier, mem_iUnionâ‚‚] at hx
      -- âŠ¢ âˆƒ i, x âˆˆ (fun i => toFun (find c ne i) i) i
      haveI : Nonempty (PartialRefinement u s) := âŸ¨ne.someâŸ©
      -- âŠ¢ âˆƒ i, x âˆˆ (fun i => toFun (find c ne i) i) i
      choose! v hvc hiv using hx
      -- âŠ¢ âˆƒ i, x âˆˆ (fun i => toFun (find c ne i) i) i
      rcases(hfin x hxs).exists_maximal_wrt v _ (mem_iUnion.1 (hU hxs)) with
        âŸ¨i, hxi : x âˆˆ u i, hmax : âˆ€ j, x âˆˆ u j â†’ v i â‰¤ v j â†’ v i = v jâŸ©
      rcases mem_iUnion.1 ((v i).subset_iUnion hxs) with âŸ¨j, hjâŸ©
      -- âŠ¢ âˆƒ i, x âˆˆ (fun i => toFun (find c ne i) i) i
      use j
      -- âŠ¢ x âˆˆ (fun i => toFun (find c ne i) i) j
      have hj' : x âˆˆ u j := (v i).subset _ hj
      -- âŠ¢ x âˆˆ (fun i => toFun (find c ne i) i) j
      have : v j â‰¤ v i := (hc.total (hvc _ hxi) (hvc _ hj')).elim (fun h => (hmax j hj' h).ge) id
      -- âŠ¢ x âˆˆ (fun i => toFun (find c ne i) i) j
      simpa only [find_apply_of_mem hc ne (hvc _ hxi) (this.1 <| hiv _ hj')]
      -- ğŸ‰ no goals
  closure_subset hi := (find c ne _).closure_subset ((mem_find_carrier_iff _).2 hi)
  apply_eq hi := (find c ne _).apply_eq (mt (mem_find_carrier_iff _).1 hi)
#align shrinking_lemma.partial_refinement.chain_Sup ShrinkingLemma.PartialRefinement.chainSup

/-- `chainSup hu c hc ne hfin hU` is an upper bound of the chain `c`. -/
theorem le_chainSup {c : Set (PartialRefinement u s)} (hc : IsChain (Â· â‰¤ Â·) c) (ne : c.Nonempty)
    (hfin : âˆ€ x âˆˆ s, { i | x âˆˆ u i }.Finite) (hU : s âŠ† â‹ƒ i, u i) {v} (hv : v âˆˆ c) :
    v â‰¤ chainSup c hc ne hfin hU :=
  âŸ¨fun _ hi => mem_biUnion hv hi, fun _ hi => (find_apply_of_mem hc _ hv hi).symmâŸ©
#align shrinking_lemma.partial_refinement.le_chain_Sup ShrinkingLemma.PartialRefinement.le_chainSup

/-- If `s` is a closed set, `v` is a partial refinement, and `i` is an index such that
`i âˆ‰ v.carrier`, then there exists a partial refinement that is strictly greater than `v`. -/
theorem exists_gt (v : PartialRefinement u s) (hs : IsClosed s) (i : Î¹) (hi : i âˆ‰ v.carrier) :
    âˆƒ v' : PartialRefinement u s, v < v' := by
  have I : (s âˆ© â‹‚ (j) (_ : j â‰  i), (v j)á¶œ) âŠ† v i := by
    simp only [subset_def, mem_inter_iff, mem_iInter, and_imp]
    intro x hxs H
    rcases mem_iUnion.1 (v.subset_iUnion hxs) with âŸ¨j, hjâŸ©
    exact (em (j = i)).elim (fun h => h â–¸ hj) fun h => (H j h hj).elim
  have C : IsClosed (s âˆ© â‹‚ (j) (_ : j â‰  i), (v j)á¶œ) :=
    IsClosed.inter hs (isClosed_biInter fun _ _ => isClosed_compl_iff.2 <| v.isOpen _)
  rcases normal_exists_closure_subset C (v.isOpen i) I with âŸ¨vi, ovi, hvi, cviâŸ©
  -- âŠ¢ âˆƒ v', v < v'
  refine' âŸ¨âŸ¨update v i vi, insert i v.carrier, _, _, _, _âŸ©, _, _âŸ©
  Â· intro j
    -- âŠ¢ IsOpen (update v.toFun i vi j)
    rcases eq_or_ne j i with (rfl| hne) <;> simp [*, v.isOpen]
    -- âŠ¢ IsOpen (update v.toFun j vi j)
                                            -- ğŸ‰ no goals
                                            -- ğŸ‰ no goals
  Â· refine' fun x hx => mem_iUnion.2 _
    -- âŠ¢ âˆƒ i_1, x âˆˆ update v.toFun i vi i_1
    rcases em (âˆƒ (j : _) (_ : j â‰  i), x âˆˆ v j) with (âŸ¨j, hji, hjâŸ© | h)
    -- âŠ¢ âˆƒ i_1, x âˆˆ update v.toFun i vi i_1
    Â· use j
      -- âŠ¢ x âˆˆ update v.toFun i vi j
      rwa [update_noteq hji]
      -- ğŸ‰ no goals
    Â· push_neg at h
      -- âŠ¢ âˆƒ i_1, x âˆˆ update v.toFun i vi i_1
      use i
      -- âŠ¢ x âˆˆ update v.toFun i vi i
      rw [update_same]
      -- âŠ¢ x âˆˆ vi
      exact hvi âŸ¨hx, mem_biInter hâŸ©
      -- ğŸ‰ no goals
  Â· rintro j (rfl | hj)
    -- âŠ¢ closure (update v.toFun j vi j) âŠ† u j
    Â· rwa [update_same, â† v.apply_eq hi]
      -- ğŸ‰ no goals
    Â· rw [update_noteq (ne_of_mem_of_not_mem hj hi)]
      -- âŠ¢ closure (toFun v j) âŠ† u j
      exact v.closure_subset hj
      -- ğŸ‰ no goals
  Â· intro j hj
    -- âŠ¢ update v.toFun i vi j = u j
    rw [mem_insert_iff, not_or] at hj
    -- âŠ¢ update v.toFun i vi j = u j
    rw [update_noteq hj.1, v.apply_eq hj.2]
    -- ğŸ‰ no goals
  Â· refine' âŸ¨subset_insert _ _, fun j hj => _âŸ©
    -- âŠ¢ toFun v j = toFun { toFun := update v.toFun i vi, carrier := insert i v.carr â€¦
    exact (update_noteq (ne_of_mem_of_not_mem hj hi) _ _).symm
    -- ğŸ‰ no goals
  Â· exact fun hle => hi (hle.1 <| mem_insert _ _)
    -- ğŸ‰ no goals
#align shrinking_lemma.partial_refinement.exists_gt ShrinkingLemma.PartialRefinement.exists_gt

end PartialRefinement

end ShrinkingLemma

open ShrinkingLemma

variable {u : Î¹ â†’ Set X} {s : Set X}

/-- **Shrinking lemma**. A point-finite open cover of a closed subset of a normal space can be
"shrunk" to a new open cover so that the closure of each new open set is contained in the
corresponding original open set. -/
theorem exists_subset_iUnion_closure_subset (hs : IsClosed s) (uo : âˆ€ i, IsOpen (u i))
    (uf : âˆ€ x âˆˆ s, { i | x âˆˆ u i }.Finite) (us : s âŠ† â‹ƒ i, u i) :
    âˆƒ v : Î¹ â†’ Set X, s âŠ† iUnion v âˆ§ (âˆ€ i, IsOpen (v i)) âˆ§ âˆ€ i, closure (v i) âŠ† u i := by
  haveI : Nonempty (PartialRefinement u s) := âŸ¨âŸ¨u, âˆ…, uo, us, False.elim, fun _ => rflâŸ©âŸ©
  -- âŠ¢ âˆƒ v, s âŠ† iUnion v âˆ§ (âˆ€ (i : Î¹), IsOpen (v i)) âˆ§ âˆ€ (i : Î¹), closure (v i) âŠ† u i
  have : âˆ€ c : Set (PartialRefinement u s),
      IsChain (Â· â‰¤ Â·) c â†’ c.Nonempty â†’ âˆƒ ub, âˆ€ v âˆˆ c, v â‰¤ ub :=
    fun c hc ne => âŸ¨.chainSup c hc ne uf us, fun v hv => PartialRefinement.le_chainSup _ _ _ _ hvâŸ©
  rcases zorn_nonempty_partialOrder this with âŸ¨v, hvâŸ©
  -- âŠ¢ âˆƒ v, s âŠ† iUnion v âˆ§ (âˆ€ (i : Î¹), IsOpen (v i)) âˆ§ âˆ€ (i : Î¹), closure (v i) âŠ† u i
  suffices : âˆ€ i, i âˆˆ v.carrier
  -- âŠ¢ âˆƒ v, s âŠ† iUnion v âˆ§ (âˆ€ (i : Î¹), IsOpen (v i)) âˆ§ âˆ€ (i : Î¹), closure (v i) âŠ† u i
  exact âŸ¨v, v.subset_iUnion, fun i => v.isOpen _, fun i => v.closure_subset (this i)âŸ©
  -- âŠ¢ âˆ€ (i : Î¹), i âˆˆ v.carrier
  contrapose! hv
  -- âŠ¢ âˆƒ a, v â‰¤ a âˆ§ a â‰  v
  rcases hv with âŸ¨i, hiâŸ©
  -- âŠ¢ âˆƒ a, v â‰¤ a âˆ§ a â‰  v
  rcases v.exists_gt hs i hi with âŸ¨v', hltâŸ©
  -- âŠ¢ âˆƒ a, v â‰¤ a âˆ§ a â‰  v
  exact âŸ¨v', hlt.le, hlt.ne'âŸ©
  -- ğŸ‰ no goals
#align exists_subset_Union_closure_subset exists_subset_iUnion_closure_subset

/-- **Shrinking lemma**. A point-finite open cover of a closed subset of a normal space can be
"shrunk" to a new closed cover so that each new closed set is contained in the corresponding
original open set. See also `exists_subset_iUnion_closure_subset` for a stronger statement. -/
theorem exists_subset_iUnion_closed_subset (hs : IsClosed s) (uo : âˆ€ i, IsOpen (u i))
    (uf : âˆ€ x âˆˆ s, { i | x âˆˆ u i }.Finite) (us : s âŠ† â‹ƒ i, u i) :
    âˆƒ v : Î¹ â†’ Set X, s âŠ† iUnion v âˆ§ (âˆ€ i, IsClosed (v i)) âˆ§ âˆ€ i, v i âŠ† u i :=
  let âŸ¨v, hsv, _, hvâŸ© := exists_subset_iUnion_closure_subset hs uo uf us
  âŸ¨fun i => closure (v i), Subset.trans hsv (iUnion_mono fun _ => subset_closure),
    fun _ => isClosed_closure, hvâŸ©
#align exists_subset_Union_closed_subset exists_subset_iUnion_closed_subset

/-- Shrinking lemma. A point-finite open cover of a closed subset of a normal space can be "shrunk"
to a new open cover so that the closure of each new open set is contained in the corresponding
original open set. -/
theorem exists_iUnion_eq_closure_subset (uo : âˆ€ i, IsOpen (u i)) (uf : âˆ€ x, { i | x âˆˆ u i }.Finite)
    (uU : â‹ƒ i, u i = univ) :
    âˆƒ v : Î¹ â†’ Set X, iUnion v = univ âˆ§ (âˆ€ i, IsOpen (v i)) âˆ§ âˆ€ i, closure (v i) âŠ† u i :=
  let âŸ¨v, vU, hvâŸ© := exists_subset_iUnion_closure_subset isClosed_univ uo (fun x _ => uf x) uU.ge
  âŸ¨v, univ_subset_iff.1 vU, hvâŸ©
#align exists_Union_eq_closure_subset exists_iUnion_eq_closure_subset

/-- Shrinking lemma. A point-finite open cover of a closed subset of a normal space can be "shrunk"
to a new closed cover so that each of the new closed sets is contained in the corresponding
original open set. See also `exists_iUnion_eq_closure_subset` for a stronger statement. -/
theorem exists_iUnion_eq_closed_subset (uo : âˆ€ i, IsOpen (u i)) (uf : âˆ€ x, { i | x âˆˆ u i }.Finite)
    (uU : â‹ƒ i, u i = univ) :
    âˆƒ v : Î¹ â†’ Set X, iUnion v = univ âˆ§ (âˆ€ i, IsClosed (v i)) âˆ§ âˆ€ i, v i âŠ† u i :=
  let âŸ¨v, vU, hvâŸ© := exists_subset_iUnion_closed_subset isClosed_univ uo (fun x _ => uf x) uU.ge
  âŸ¨v, univ_subset_iff.1 vU, hvâŸ©
#align exists_Union_eq_closed_subset exists_iUnion_eq_closed_subset
