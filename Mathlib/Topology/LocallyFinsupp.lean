/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Algebra.Group.Subgroup.Defs
import Mathlib.Algebra.Group.Support
import Mathlib.Algebra.Order.Pi
import Mathlib.Topology.Separation.Hausdorff
import Mathlib.Topology.DiscreteSubset
import Mathlib.Tactic.Peel

/-!
# Type of functions with locally finite support

This file defines functions with locally finite support, provides supporting API. For suitable
targets, it establishes functions with locally finite support as an instance of a lattice ordered
commutative group.

Throughout the present file, `X` denotes a topologically space and `U` a subset of `X`.
-/

open Filter Function Set Topology

variable
  {X : Type*} [TopologicalSpace X] {U : Set X}
  {Y : Type*}

/-!
## Definition, coercion to functions and basic extensionality lemmas

A function with locally finite support within `U` is a function `X ‚Üí Y` whose support is locally
finite within `U` and entirely contained in `U`.  For T1-spaces, the theorem
`supportDiscreteWithin_iff_locallyFiniteWithin` shows that the first condition is equivalent to the
condition that the support `f` is discrete within `U`.
-/

variable (U Y) in
/-- A function with locally finite support within `U` is a triple as specified below. -/
structure Function.locallyFinsuppWithin [Zero Y] where
  /-- A function `X ‚Üí Y` -/
  toFun : X ‚Üí Y
  /-- A proof that the support of `toFun` is contained in `U` -/
  supportWithinDomain' : toFun.support ‚äÜ U
  /-- A proof that the support is locally finite within `U` -/
  supportLocallyFiniteWithinDomain' : ‚àÄ z ‚àà U, ‚àÉ t ‚àà ùìù z, Set.Finite (t ‚à© toFun.support)

variable (X Y) in
/--
A function with locally finite support is a function with locally finite support within
`‚ä§ : Set X`.
-/
def Function.locallyFinsupp [Zero Y] := locallyFinsuppWithin (‚ä§ : Set X) Y

/--
For T1 spaces, the condition `supportLocallyFiniteWithinDomain'` is equivalent to saying that the
support is codiscrete within `U`.
-/
theorem supportDiscreteWithin_iff_locallyFiniteWithin [T1Space X] [Zero Y] {f : X ‚Üí Y}
    (h : f.support ‚äÜ U) :
    f =·∂†[codiscreteWithin U] 0 ‚Üî ‚àÄ z ‚àà U, ‚àÉ t ‚àà ùìù z, Set.Finite (t ‚à© f.support) := by
  have : f.support = (U \ {x | f x = (0 : X ‚Üí Y) x}) := by
    ext x
    simp only [mem_support, ne_eq, Pi.zero_apply, mem_diff, mem_setOf_eq, iff_and_self]
    exact (h ¬∑)
  rw [EventuallyEq, Filter.Eventually, codiscreteWithin_iff_locallyFiniteComplementWithin, this]

/--
A function `f : X ‚Üí Y` has locally finite support if for every `z : X`, there is a
neighbourhood `t` around `z` such that `t ‚à© f.support` is finite.
-/
def LocallyFiniteSupport [Zero Y] (f : X ‚Üí Y) : Prop :=
    ‚àÄ z : X, ‚àÉ t ‚àà ùìù z, Set.Finite (t ‚à© f.support)

lemma LocallyFiniteSupport.iff_support_locallyFinite [Zero Y] (f : X ‚Üí Y) :
    LocallyFinite (fun s : f.support ‚Ü¶ ({s.val} : Set X)) ‚Üî
    LocallyFiniteSupport f := by
  dsimp only [LocallyFinite]
  peel with z t ht
  have aux1 : t ‚à© f.support = {i : f.support | ‚Üëi ‚àà t} := by aesop
  have aux2 : InjOn Subtype.val {i : f.support | ‚Üëi ‚àà t} := by aesop
  simp only [singleton_inter_nonempty, aux1, finite_image_iff aux2]

lemma LocallyFiniteSupport.support_locallyFinite [Zero Y] (f : X ‚Üí Y) (h : LocallyFiniteSupport f) :
    LocallyFinite (fun s : f.support ‚Ü¶ ({s.val} : Set X)) :=
    (LocallyFiniteSupport.iff_support_locallyFinite f).mpr h

lemma LocallyFiniteSupport.inter_support_finite_of_isCompact {W : Set X}
   [Zero Y] {f : X ‚Üí Y} (h : LocallyFiniteSupport f)
   (hW : IsCompact W) : (W ‚à© f.support).Finite := by
  have := LocallyFinite.finite_nonempty_inter_compact
    (LocallyFiniteSupport.support_locallyFinite f h) hW
  have lem {Œ± : Type u_1} (s t : Set Œ±) : {i : s | ({‚Üëi} ‚à© t).Nonempty} = (t ‚à© s) := by aesop
  rw [‚Üê lem f.support W]
  exact Finite.image Subtype.val this

namespace Function.locallyFinsuppWithin

/--
Functions with locally finite support within `U` are `FunLike`: the coercion to functions is
injective.
-/
instance [Zero Y] : FunLike (locallyFinsuppWithin U Y) X Y where
  coe D := D.toFun
  coe_injective' := fun ‚ü®_, _, _‚ü© ‚ü®_, _, _‚ü© ‚Ü¶ by simp

/-- This allows writing `D.support` instead of `Function.support D` -/
abbrev support [Zero Y] (D : locallyFinsuppWithin U Y) := Function.support D

lemma supportWithinDomain [Zero Y] (D : locallyFinsuppWithin U Y) :
    D.support ‚äÜ U := D.supportWithinDomain'

lemma supportLocallyFiniteWithinDomain [Zero Y] (D : locallyFinsuppWithin U Y) :
    ‚àÄ z ‚àà U, ‚àÉ t ‚àà ùìù z, Set.Finite (t ‚à© D.support) := D.supportLocallyFiniteWithinDomain'

@[ext]
lemma ext [Zero Y] {D‚ÇÅ D‚ÇÇ : locallyFinsuppWithin U Y} (h : ‚àÄ a, D‚ÇÅ a = D‚ÇÇ a) :
    D‚ÇÅ = D‚ÇÇ := DFunLike.ext _ _ h

lemma coe_injective [Zero Y] :
    Injective (¬∑ : locallyFinsuppWithin U Y ‚Üí X ‚Üí Y) := DFunLike.coe_injective

/-!
## Elementary properties of the support
-/

/--
Simplifier lemma: Functions with locally finite support within `U` evaluate to zero outside of `U`.
-/
@[simp]
lemma apply_eq_zero_of_notMem [Zero Y] {z : X} (D : locallyFinsuppWithin U Y)
    (hz : z ‚àâ U) :
    D z = 0 := notMem_support.mp fun a ‚Ü¶ hz (D.supportWithinDomain a)

@[deprecated (since := "2025-05-23")] alias apply_eq_zero_of_not_mem := apply_eq_zero_of_notMem

/--
On a T1 space, the support of a function with locally finite support within `U` is discrete within
`U`.
-/
theorem eq_zero_codiscreteWithin [Zero Y] [T1Space X] (D : locallyFinsuppWithin U Y) :
    D =·∂†[Filter.codiscreteWithin U] 0 := by
  apply codiscreteWithin_iff_locallyFiniteComplementWithin.2
  have : D.support = (U \ {x | D x = (0 : X ‚Üí Y) x}) := by
    ext x
    simp only [mem_support, ne_eq, Pi.zero_apply, Set.mem_diff, Set.mem_setOf_eq, iff_and_self]
    exact (support_subset_iff.1 D.supportWithinDomain) x
  rw [‚Üê this]
  exact D.supportLocallyFiniteWithinDomain

/--
On a T1 space, the support of a functions with locally finite support within `U` is discrete.
-/
theorem discreteSupport [Zero Y] [T1Space X] (D : locallyFinsuppWithin U Y) :
    DiscreteTopology D.support := by
  have : D.support = {x | D x = 0}·∂ú ‚à© U := by
    ext x
    constructor
    ¬∑ exact fun hx ‚Ü¶ ‚ü®by tauto, D.supportWithinDomain hx‚ü©
    ¬∑ intro hx
      rw [mem_inter_iff, mem_compl_iff, mem_setOf_eq] at hx
      tauto
  rw [this]
  apply discreteTopology_of_codiscreteWithin
  apply (supportDiscreteWithin_iff_locallyFiniteWithin D.supportWithinDomain).2
  exact D.supportLocallyFiniteWithinDomain

/--
If `X` is T1 and if `U` is closed, then the support of support of a function with locally finite
support within `U` is also closed.
-/
theorem closedSupport [T1Space X] [Zero Y] (D : locallyFinsuppWithin U Y)
    (hU : IsClosed U) :
    IsClosed D.support := by
  convert isClosed_sdiff_of_codiscreteWithin ((supportDiscreteWithin_iff_locallyFiniteWithin
    D.supportWithinDomain).2 D.supportLocallyFiniteWithinDomain) hU
  ext x
  constructor <;> intro hx
  ¬∑ simp_all [D.supportWithinDomain hx]
  ¬∑ simp_all

/--
If `X` is T2 and if `U` is compact, then the support of a function with locally finite support
within `U` is finite.
-/
theorem finiteSupport [T2Space X] [Zero Y] (D : locallyFinsuppWithin U Y)
    (hU : IsCompact U) :
    Set.Finite D.support :=
  (hU.of_isClosed_subset (D.closedSupport hU.isClosed)
    D.supportWithinDomain).finite D.discreteSupport

/-!
## Lattice ordered group structure

If `X` is a suitable instance, this section equips functions with locally finite support within `U`
with the standard structure of a lattice ordered group, where addition, comparison, min and max are
defined pointwise.
-/

variable (U) in
/--
Functions with locally finite support within `U` form an additive subgroup of functions X ‚Üí Y.
-/
protected def addSubgroup [AddCommGroup Y] : AddSubgroup (X ‚Üí Y) where
  carrier := {f | f.support ‚äÜ U ‚àß ‚àÄ z ‚àà U, ‚àÉ t ‚àà ùìù z, Set.Finite (t ‚à© f.support)}
  zero_mem' := by
    simp only [support_subset_iff, ne_eq, mem_setOf_eq, Pi.zero_apply, not_true_eq_false,
      IsEmpty.forall_iff, implies_true, support_zero, inter_empty, finite_empty, and_true,
      true_and]
    exact fun _ _ ‚Ü¶ ‚ü®‚ä§, univ_mem‚ü©
  add_mem' {f g} hf hg := by
    constructor
    ¬∑ intro x hx
      contrapose! hx
      simp [notMem_support.1 fun a ‚Ü¶ hx (hf.1 a), notMem_support.1 fun a ‚Ü¶ hx (hg.1 a)]
    ¬∑ intro z hz
      obtain ‚ü®t‚ÇÅ, ht‚ÇÅ‚ü© := hf.2 z hz
      obtain ‚ü®t‚ÇÇ, ht‚ÇÇ‚ü© := hg.2 z hz
      use t‚ÇÅ ‚à© t‚ÇÇ, inter_mem ht‚ÇÅ.1 ht‚ÇÇ.1
      apply Set.Finite.subset (s := (t‚ÇÅ ‚à© f.support) ‚à™ (t‚ÇÇ ‚à© g.support)) (ht‚ÇÅ.2.union ht‚ÇÇ.2)
      intro a ha
      simp_all only [support_subset_iff, ne_eq, mem_setOf_eq,
        mem_inter_iff, mem_support, Pi.add_apply, mem_union, true_and]
      by_contra hCon
      push_neg at hCon
      simp_all
  neg_mem' {f} hf := by
    simp_all

protected lemma memAddSubgroup [AddCommGroup Y] (D : locallyFinsuppWithin U Y) :
    (D : X ‚Üí Y) ‚àà locallyFinsuppWithin.addSubgroup U :=
  ‚ü®D.supportWithinDomain, D.supportLocallyFiniteWithinDomain‚ü©

/--
Assign a function with locally finite support within `U` to a function in the subgroup.
-/
@[simps]
def mk_of_mem [AddCommGroup Y] (f : X ‚Üí Y) (hf : f ‚àà locallyFinsuppWithin.addSubgroup U) :
    locallyFinsuppWithin U Y := ‚ü®f, hf.1, hf.2‚ü©

instance [AddCommGroup Y] : Zero (locallyFinsuppWithin U Y) where
  zero := mk_of_mem 0 <| zero_mem _

instance [AddCommGroup Y] : Add (locallyFinsuppWithin U Y) where
  add D‚ÇÅ D‚ÇÇ := mk_of_mem (D‚ÇÅ + D‚ÇÇ) <| add_mem D‚ÇÅ.memAddSubgroup D‚ÇÇ.memAddSubgroup

instance [AddCommGroup Y] : Neg (locallyFinsuppWithin U Y) where
  neg D := mk_of_mem (-D) <| neg_mem D.memAddSubgroup

instance [AddCommGroup Y] : Sub (locallyFinsuppWithin U Y) where
  sub D‚ÇÅ D‚ÇÇ := mk_of_mem (D‚ÇÅ - D‚ÇÇ) <| sub_mem D‚ÇÅ.memAddSubgroup D‚ÇÇ.memAddSubgroup

instance [AddCommGroup Y] : SMul ‚Ñï (locallyFinsuppWithin U Y) where
  smul n D := mk_of_mem (n ‚Ä¢ D) <| nsmul_mem D.memAddSubgroup n

instance [AddCommGroup Y] : SMul ‚Ñ§ (locallyFinsuppWithin U Y) where
  smul n D := mk_of_mem (n ‚Ä¢ D) <| zsmul_mem D.memAddSubgroup n

@[simp] lemma coe_zero [AddCommGroup Y] :
    ((0 : locallyFinsuppWithin U Y) : X ‚Üí Y) = 0 := rfl
@[simp] lemma coe_add [AddCommGroup Y] (D‚ÇÅ D‚ÇÇ : locallyFinsuppWithin U Y) :
    (‚Üë(D‚ÇÅ + D‚ÇÇ) : X ‚Üí Y) = D‚ÇÅ + D‚ÇÇ := rfl
@[simp] lemma coe_neg [AddCommGroup Y] (D : locallyFinsuppWithin U Y) :
    (‚Üë(-D) : X ‚Üí Y) = -(D : X ‚Üí Y) := rfl
@[simp] lemma coe_sub [AddCommGroup Y] (D‚ÇÅ D‚ÇÇ : locallyFinsuppWithin U Y) :
    (‚Üë(D‚ÇÅ - D‚ÇÇ) : X ‚Üí Y) = D‚ÇÅ - D‚ÇÇ := rfl
@[simp] lemma coe_nsmul [AddCommGroup Y] (D : locallyFinsuppWithin U Y) (n : ‚Ñï) :
    (‚Üë(n ‚Ä¢ D) : X ‚Üí Y) = n ‚Ä¢ (D : X ‚Üí Y) := rfl
@[simp] lemma coe_zsmul [AddCommGroup Y] (D : locallyFinsuppWithin U Y) (n : ‚Ñ§) :
    (‚Üë(n ‚Ä¢ D) : X ‚Üí Y) = n ‚Ä¢ (D : X ‚Üí Y) := rfl

instance [AddCommGroup Y] : AddCommGroup (locallyFinsuppWithin U Y) :=
  Injective.addCommGroup (M‚ÇÅ := locallyFinsuppWithin U Y) (M‚ÇÇ := X ‚Üí Y)
    _ coe_injective coe_zero coe_add coe_neg coe_sub coe_nsmul coe_zsmul

instance [LE Y] [Zero Y] : LE (locallyFinsuppWithin U Y) where
  le := fun D‚ÇÅ D‚ÇÇ ‚Ü¶ (D‚ÇÅ : X ‚Üí Y) ‚â§ D‚ÇÇ

lemma le_def [LE Y] [Zero Y] {D‚ÇÅ D‚ÇÇ : locallyFinsuppWithin U Y} :
    D‚ÇÅ ‚â§ D‚ÇÇ ‚Üî (D‚ÇÅ : X ‚Üí Y) ‚â§ (D‚ÇÇ : X ‚Üí Y) := ‚ü®(¬∑),(¬∑)‚ü©

instance [Preorder Y] [Zero Y] : LT (locallyFinsuppWithin U Y) where
  lt := fun D‚ÇÅ D‚ÇÇ ‚Ü¶ (D‚ÇÅ : X ‚Üí Y) < D‚ÇÇ

lemma lt_def [Preorder Y] [Zero Y] {D‚ÇÅ D‚ÇÇ : locallyFinsuppWithin U Y} :
    D‚ÇÅ < D‚ÇÇ ‚Üî (D‚ÇÅ : X ‚Üí Y) < (D‚ÇÇ : X ‚Üí Y) := ‚ü®(¬∑),(¬∑)‚ü©

instance [SemilatticeSup Y] [Zero Y] : Max (locallyFinsuppWithin U Y) where
  max D‚ÇÅ D‚ÇÇ :=
  { toFun z := max (D‚ÇÅ z) (D‚ÇÇ z)
    supportWithinDomain' := by
      intro x
      contrapose
      intro hx
      simp [notMem_support.1 fun a ‚Ü¶ hx (D‚ÇÅ.supportWithinDomain a),
        notMem_support.1 fun a ‚Ü¶ hx (D‚ÇÇ.supportWithinDomain a)]
    supportLocallyFiniteWithinDomain' := by
      intro z hz
      obtain ‚ü®t‚ÇÅ, ht‚ÇÅ‚ü© := D‚ÇÅ.supportLocallyFiniteWithinDomain z hz
      obtain ‚ü®t‚ÇÇ, ht‚ÇÇ‚ü© := D‚ÇÇ.supportLocallyFiniteWithinDomain z hz
      use t‚ÇÅ ‚à© t‚ÇÇ, inter_mem ht‚ÇÅ.1 ht‚ÇÇ.1
      apply Set.Finite.subset (s := (t‚ÇÅ ‚à© D‚ÇÅ.support) ‚à™ (t‚ÇÇ ‚à© D‚ÇÇ.support)) (ht‚ÇÅ.2.union ht‚ÇÇ.2)
      intro a ha
      simp_all only [mem_inter_iff, mem_support, ne_eq, mem_union, true_and]
      by_contra hCon
      push_neg at hCon
      simp_all }

@[simp]
lemma max_apply [SemilatticeSup Y] [Zero Y] {D‚ÇÅ D‚ÇÇ : locallyFinsuppWithin U Y} {x : X} :
    max D‚ÇÅ D‚ÇÇ x = max (D‚ÇÅ x) (D‚ÇÇ x) := rfl

instance [SemilatticeInf Y] [Zero Y] : Min (locallyFinsuppWithin U Y) where
  min D‚ÇÅ D‚ÇÇ :=
  { toFun z := min (D‚ÇÅ z) (D‚ÇÇ z)
    supportWithinDomain' := by
      intro x
      contrapose
      intro hx
      simp [notMem_support.1 fun a ‚Ü¶ hx (D‚ÇÅ.supportWithinDomain a),
        notMem_support.1 fun a ‚Ü¶ hx (D‚ÇÇ.supportWithinDomain a)]
    supportLocallyFiniteWithinDomain' := by
      intro z hz
      obtain ‚ü®t‚ÇÅ, ht‚ÇÅ‚ü© := D‚ÇÅ.supportLocallyFiniteWithinDomain z hz
      obtain ‚ü®t‚ÇÇ, ht‚ÇÇ‚ü© := D‚ÇÇ.supportLocallyFiniteWithinDomain z hz
      use t‚ÇÅ ‚à© t‚ÇÇ, inter_mem ht‚ÇÅ.1 ht‚ÇÇ.1
      apply Set.Finite.subset (s := (t‚ÇÅ ‚à© D‚ÇÅ.support) ‚à™ (t‚ÇÇ ‚à© D‚ÇÇ.support)) (ht‚ÇÅ.2.union ht‚ÇÇ.2)
      intro a ha
      simp_all only [mem_inter_iff, mem_support, ne_eq, mem_union, true_and]
      by_contra hCon
      push_neg at hCon
      simp_all }

@[simp]
lemma min_apply [SemilatticeInf Y] [Zero Y] {D‚ÇÅ D‚ÇÇ : locallyFinsuppWithin U Y} {x : X} :
    min D‚ÇÅ D‚ÇÇ x = min (D‚ÇÅ x) (D‚ÇÇ x) := rfl

instance [Lattice Y] [Zero Y] : Lattice (locallyFinsuppWithin U Y) where
  le := (¬∑ ‚â§ ¬∑)
  lt := (¬∑ < ¬∑)
  le_refl := by simp [le_def]
  le_trans D‚ÇÅ D‚ÇÇ D‚ÇÉ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ := fun x ‚Ü¶ (h‚ÇÅ‚ÇÇ x).trans (h‚ÇÇ‚ÇÉ x)
  le_antisymm D‚ÇÅ D‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ := by
    ext x
    exact le_antisymm (h‚ÇÅ‚ÇÇ x) (h‚ÇÇ‚ÇÅ x)
  sup := max
  le_sup_left D‚ÇÅ D‚ÇÇ := fun x ‚Ü¶ by simp
  le_sup_right D‚ÇÅ D‚ÇÇ := fun x ‚Ü¶ by simp
  sup_le D‚ÇÅ D‚ÇÇ D‚ÇÉ h‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÉ := fun x ‚Ü¶ by simp [h‚ÇÅ‚ÇÉ x, h‚ÇÇ‚ÇÉ x]
  inf := min
  inf_le_left D‚ÇÅ D‚ÇÇ := fun x ‚Ü¶ by simp
  inf_le_right D‚ÇÅ D‚ÇÇ := fun x ‚Ü¶ by simp
  le_inf D‚ÇÅ D‚ÇÇ D‚ÇÉ h‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÉ := fun x ‚Ü¶ by simp [h‚ÇÅ‚ÇÉ x, h‚ÇÇ‚ÇÉ x]

/--
Functions with locally finite support within `U` form an ordered commutative group.
-/
instance [AddCommGroup Y] [LinearOrder Y] [IsOrderedAddMonoid Y] :
    IsOrderedAddMonoid (locallyFinsuppWithin U Y) where
  add_le_add_left := fun _ _ _ _ ‚Ü¶ by simpa [le_def]

/-!
## Restriction
-/

/--
If `V` is a subset of `U`, then functions with locally finite support within `U` restrict to
functions with locally finite support within `V`, by setting their values to zero outside of `V`.
-/
noncomputable def restrict [Zero Y] {V : Set X} (D : locallyFinsuppWithin U Y) (h : V ‚äÜ U) :
    locallyFinsuppWithin V Y where
  toFun := by
    classical
    exact fun z ‚Ü¶ if hz : z ‚àà V then D z else 0
  supportWithinDomain' := by
    intro x hx
    simp_rw [dite_eq_ite, mem_support, ne_eq, ite_eq_right_iff, Classical.not_imp] at hx
    exact hx.1
  supportLocallyFiniteWithinDomain' := by
    intro z hz
    obtain ‚ü®t, ht‚ü© := D.supportLocallyFiniteWithinDomain z (h hz)
    use t, ht.1
    apply Set.Finite.subset (s := t ‚à© D.support) ht.2
    intro _ _
    simp_all

open Classical in
lemma restrict_apply [Zero Y] {V : Set X} (D : locallyFinsuppWithin U Y) (h : V ‚äÜ U) (z : X) :
    (D.restrict h) z = if z ‚àà V then D z else 0 := rfl

lemma restrict_eqOn [Zero Y] {V : Set X} (D : locallyFinsuppWithin U Y) (h : V ‚äÜ U) :
    Set.EqOn (D.restrict h) D V := by
  intro _ _
  simp_all [restrict_apply]

lemma restrict_eqOn_compl [Zero Y] {V : Set X} (D : locallyFinsuppWithin U Y) (h : V ‚äÜ U) :
    Set.EqOn (D.restrict h) 0 V·∂ú := by
  intro _ hx
  simp_all

/-- Restriction as a group morphism -/
noncomputable def restrictMonoidHom [AddCommGroup Y] {V : Set X} (h : V ‚äÜ U) :
    locallyFinsuppWithin U Y ‚Üí+ locallyFinsuppWithin V Y where
  toFun D := D.restrict h
  map_zero' := by
    ext x
    simp [restrict_apply]
  map_add' D‚ÇÅ D‚ÇÇ := by
    ext x
    by_cases hx : x ‚àà V
    <;> simp [restrict_apply, hx]

@[simp]
lemma restrictMonoidHom_apply [AddCommGroup Y] {V : Set X} (D : locallyFinsuppWithin U Y)
    (h : V ‚äÜ U) :
    restrictMonoidHom h D = D.restrict h := by rfl

/-- Restriction as a lattice morphism -/
noncomputable def restrictLatticeHom [AddCommGroup Y] [Lattice Y] {V : Set X} (h : V ‚äÜ U) :
    LatticeHom (locallyFinsuppWithin U Y) (locallyFinsuppWithin V Y) where
  toFun D := D.restrict h
  map_sup' D‚ÇÅ D‚ÇÇ := by
    ext x
    by_cases hx : x ‚àà V
    <;> simp [locallyFinsuppWithin.restrict_apply, hx]
  map_inf' D‚ÇÅ D‚ÇÇ := by
    ext x
    by_cases hx : x ‚àà V
    <;> simp [locallyFinsuppWithin.restrict_apply, hx]

@[simp]
lemma restrictLatticeHom_apply [AddCommGroup Y] [Lattice Y] {V : Set X}
    (D : locallyFinsuppWithin U Y) (h : V ‚äÜ U) :
    restrictLatticeHom h D = D.restrict h := by rfl

end Function.locallyFinsuppWithin
