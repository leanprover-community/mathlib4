/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
module

public import Mathlib.Topology.OpenPartialHomeomorph.Basic
/-!
# Partial homeomorphisms and continuity

## Main theorems

* `OpenPartialHomeomorph.map_nhds_eq`: an open partial homeomorphism preserves the neighbourhood
  filter of any point in its source.
* `OpenPartialHomeomorph.continuousAt_iff_continuousAt_comp_left`,
  `OpenPartialHomeomorph.continuousAt_iff_continuousAt_comp_right`: a function is continuous at
  a point iff its pre / post composition with an open partial homeomorphism is so (assuming the
  point is in the source / target).
-/

@[expose] public section

open Function Set Filter Topology

variable {X X' : Type*} {Y Y' : Type*} {Z Z' : Type*}
  [TopologicalSpace X] [TopologicalSpace X'] [TopologicalSpace Y] [TopologicalSpace Y']
  [TopologicalSpace Z] [TopologicalSpace Z']

namespace OpenPartialHomeomorph

variable (e : OpenPartialHomeomorph X Y)

theorem eventually_left_inverse {x} (hx : x âˆˆ e.source) :
    âˆ€á¶  y in ğ“ x, e.symm (e y) = y :=
  (e.open_source.eventually_mem hx).mono e.left_inv'

theorem eventually_left_inverse' {x} (hx : x âˆˆ e.target) :
    âˆ€á¶  y in ğ“ (e.symm x), e.symm (e y) = y :=
  e.eventually_left_inverse (e.map_target hx)

theorem eventually_right_inverse {x} (hx : x âˆˆ e.target) :
    âˆ€á¶  y in ğ“ x, e (e.symm y) = y :=
  (e.open_target.eventually_mem hx).mono e.right_inv'

theorem eventually_right_inverse' {x} (hx : x âˆˆ e.source) :
    âˆ€á¶  y in ğ“ (e x), e (e.symm y) = y :=
  e.eventually_right_inverse (e.map_source hx)

theorem eventually_ne_nhdsWithin {x} (hx : x âˆˆ e.source) :
    âˆ€á¶  x' in ğ“[â‰ ] x, e x' â‰  e x :=
  eventually_nhdsWithin_iff.2 <|
    (e.eventually_left_inverse hx).mono fun x' hx' =>
      mt fun h => by rw [mem_singleton_iff, â† e.left_inv hx, â† h, hx']

theorem nhdsWithin_source_inter {x} (hx : x âˆˆ e.source) (s : Set X) : ğ“[e.source âˆ© s] x = ğ“[s] x :=
  nhdsWithin_inter_of_mem (mem_nhdsWithin_of_mem_nhds <| IsOpen.mem_nhds e.open_source hx)

theorem nhdsWithin_target_inter {x} (hx : x âˆˆ e.target) (s : Set Y) : ğ“[e.target âˆ© s] x = ğ“[s] x :=
  e.symm.nhdsWithin_source_inter hx s

/-- An open partial homeomorphism is continuous at any point of its source -/
protected theorem continuousAt {x : X} (h : x âˆˆ e.source) : ContinuousAt e x :=
  (e.continuousOn x h).continuousAt (e.open_source.mem_nhds h)

/-- An open partial homeomorphism inverse is continuous at any point of its target -/
theorem continuousAt_symm {x : Y} (h : x âˆˆ e.target) : ContinuousAt e.symm x :=
  e.symm.continuousAt h

theorem tendsto_symm {x} (hx : x âˆˆ e.source) : Tendsto e.symm (ğ“ (e x)) (ğ“ x) := by
  simpa only [ContinuousAt, e.left_inv hx] using e.continuousAt_symm (e.map_source hx)

theorem map_nhds_eq {x} (hx : x âˆˆ e.source) : map e (ğ“ x) = ğ“ (e x) :=
  le_antisymm (e.continuousAt hx) <|
    le_map_of_right_inverse (e.eventually_right_inverse' hx) (e.tendsto_symm hx)

theorem symm_map_nhds_eq {x} (hx : x âˆˆ e.source) : map e.symm (ğ“ (e x)) = ğ“ x :=
  (e.symm.map_nhds_eq <| e.map_source hx).trans <| by rw [e.left_inv hx]

theorem image_mem_nhds {x} (hx : x âˆˆ e.source) {s : Set X} (hs : s âˆˆ ğ“ x) : e '' s âˆˆ ğ“ (e x) :=
  e.map_nhds_eq hx â–¸ Filter.image_mem_map hs

theorem map_nhdsWithin_eq {x} (hx : x âˆˆ e.source) (s : Set X) :
    map e (ğ“[s] x) = ğ“[e '' (e.source âˆ© s)] e x :=
  calc
    map e (ğ“[s] x) = map e (ğ“[e.source âˆ© s] x) :=
      congr_arg (map e) (e.nhdsWithin_source_inter hx _).symm
    _ = ğ“[e '' (e.source âˆ© s)] e x :=
      (e.leftInvOn.mono inter_subset_left).map_nhdsWithin_eq (e.left_inv hx)
        (e.continuousAt_symm (e.map_source hx)).continuousWithinAt
        (e.continuousAt hx).continuousWithinAt

theorem map_nhdsWithin_preimage_eq {x} (hx : x âˆˆ e.source) (s : Set Y) :
    map e (ğ“[e â»Â¹' s] x) = ğ“[s] e x := by
  rw [e.map_nhdsWithin_eq hx, e.image_source_inter_eq', e.target_inter_inv_preimage_preimage,
    e.nhdsWithin_target_inter (e.map_source hx)]

theorem eventually_nhds {x : X} (p : Y â†’ Prop) (hx : x âˆˆ e.source) :
    (âˆ€á¶  y in ğ“ (e x), p y) â†” âˆ€á¶  x in ğ“ x, p (e x) :=
  Iff.trans (by rw [e.map_nhds_eq hx]) eventually_map

theorem eventually_nhds' {x : X} (p : X â†’ Prop) (hx : x âˆˆ e.source) :
    (âˆ€á¶  y in ğ“ (e x), p (e.symm y)) â†” âˆ€á¶  x in ğ“ x, p x := by
  rw [e.eventually_nhds _ hx]
  refine eventually_congr ((e.eventually_left_inverse hx).mono fun y hy => ?_)
  rw [hy]

theorem eventually_nhdsWithin {x : X} (p : Y â†’ Prop) {s : Set X}
    (hx : x âˆˆ e.source) : (âˆ€á¶  y in ğ“[e.symm â»Â¹' s] e x, p y) â†” âˆ€á¶  x in ğ“[s] x, p (e x) := by
  refine Iff.trans ?_ eventually_map
  rw [e.map_nhdsWithin_eq hx, e.image_source_inter_eq', e.nhdsWithin_target_inter (e.mapsTo hx)]

theorem eventually_nhdsWithin' {x : X} (p : X â†’ Prop) {s : Set X}
    (hx : x âˆˆ e.source) : (âˆ€á¶  y in ğ“[e.symm â»Â¹' s] e x, p (e.symm y)) â†” âˆ€á¶  x in ğ“[s] x, p x := by
  rw [e.eventually_nhdsWithin _ hx]
  refine eventually_congr <|
    (eventually_nhdsWithin_of_eventually_nhds <| e.eventually_left_inverse hx).mono fun y hy => ?_
  rw [hy]

/-- This lemma is useful in the manifold library in the case that `e` is a chart. It states that
  locally around `e x` the set `e.symm â»Â¹' s` is the same as the set intersected with the target
  of `e` and some other neighborhood of `f x` (which will be the source of a chart on `Z`). -/
theorem preimage_eventuallyEq_target_inter_preimage_inter {e : OpenPartialHomeomorph X Y}
    {s : Set X} {t : Set Z} {x : X} {f : X â†’ Z} (hf : ContinuousWithinAt f s x) (hxe : x âˆˆ e.source)
    (ht : t âˆˆ ğ“ (f x)) :
    e.symm â»Â¹' s =á¶ [ğ“ (e x)] (e.target âˆ© e.symm â»Â¹' (s âˆ© f â»Â¹' t) : Set Y) := by
  rw [eventuallyEq_set, e.eventually_nhds _ hxe]
  filter_upwards [e.open_source.mem_nhds hxe,
    mem_nhdsWithin_iff_eventually.mp (hf.preimage_mem_nhdsWithin ht)]
  intro y hy hyu
  simp_rw [mem_inter_iff, mem_preimage, mem_inter_iff, e.mapsTo hy, true_and, iff_self_and,
    e.left_inv hy, iff_true_intro hyu]

section Continuity

/-- Continuity within a set at a point can be read under right composition with a local
homeomorphism, if the point is in its target -/
theorem continuousWithinAt_iff_continuousWithinAt_comp_right {f : Y â†’ Z} {s : Set Y} {x : Y}
    (h : x âˆˆ e.target) :
    ContinuousWithinAt f s x â†” ContinuousWithinAt (f âˆ˜ e) (e â»Â¹' s) (e.symm x) := by
  simp_rw [ContinuousWithinAt, â† @tendsto_map'_iff _ _ _ _ e,
    e.map_nhdsWithin_preimage_eq (e.map_target h), (Â· âˆ˜ Â·), e.right_inv h]

/-- Continuity at a point can be read under right composition with an open partial homeomorphism, if
the point is in its target -/
theorem continuousAt_iff_continuousAt_comp_right {f : Y â†’ Z} {x : Y} (h : x âˆˆ e.target) :
    ContinuousAt f x â†” ContinuousAt (f âˆ˜ e) (e.symm x) := by
  rw [â† continuousWithinAt_univ, e.continuousWithinAt_iff_continuousWithinAt_comp_right h,
    preimage_univ, continuousWithinAt_univ]

/-- A function is continuous on a set if and only if its composition with an open partial
homeomorphism on the right is continuous on the corresponding set. -/
theorem continuousOn_iff_continuousOn_comp_right {f : Y â†’ Z} {s : Set Y} (h : s âŠ† e.target) :
    ContinuousOn f s â†” ContinuousOn (f âˆ˜ e) (e.source âˆ© e â»Â¹' s) := by
  simp only [â† e.symm_image_eq_source_inter_preimage h, ContinuousOn, forall_mem_image]
  refine forallâ‚‚_congr fun x hx => ?_
  rw [e.continuousWithinAt_iff_continuousWithinAt_comp_right (h hx),
    e.symm_image_eq_source_inter_preimage h, inter_comm, continuousWithinAt_inter]
  exact IsOpen.mem_nhds e.open_source (e.map_target (h hx))

/-- Continuity within a set at a point can be read under left composition with a local
homeomorphism if a neighborhood of the initial point is sent to the source of the local
homeomorphism -/
theorem continuousWithinAt_iff_continuousWithinAt_comp_left {f : Z â†’ X} {s : Set Z} {x : Z}
    (hx : f x âˆˆ e.source) (h : f â»Â¹' e.source âˆˆ ğ“[s] x) :
    ContinuousWithinAt f s x â†” ContinuousWithinAt (e âˆ˜ f) s x := by
  refine âŸ¨(e.continuousAt hx).comp_continuousWithinAt, fun fe_cont => ?_âŸ©
  rw [â† continuousWithinAt_inter' h] at fe_cont âŠ¢
  have : ContinuousWithinAt (e.symm âˆ˜ e âˆ˜ f) (s âˆ© f â»Â¹' e.source) x :=
    haveI : ContinuousWithinAt e.symm univ (e (f x)) :=
      (e.continuousAt_symm (e.map_source hx)).continuousWithinAt
    ContinuousWithinAt.comp this fe_cont (subset_univ _)
  exact this.congr (fun y hy => by simp [e.left_inv hy.2]) (by simp [e.left_inv hx])

/-- Continuity at a point can be read under left composition with an open partial homeomorphism if a
neighborhood of the initial point is sent to the source of the partial homeomorphism -/
theorem continuousAt_iff_continuousAt_comp_left {f : Z â†’ X} {x : Z} (h : f â»Â¹' e.source âˆˆ ğ“ x) :
    ContinuousAt f x â†” ContinuousAt (e âˆ˜ f) x := by
  have hx : f x âˆˆ e.source := (mem_of_mem_nhds h :)
  have h' : f â»Â¹' e.source âˆˆ ğ“[univ] x := by rwa [nhdsWithin_univ]
  rw [â† continuousWithinAt_univ, â† continuousWithinAt_univ,
    e.continuousWithinAt_iff_continuousWithinAt_comp_left hx h']

/-- A function is continuous on a set if and only if its composition with an open partial
homeomorphism on the left is continuous on the corresponding set. -/
theorem continuousOn_iff_continuousOn_comp_left {f : Z â†’ X} {s : Set Z} (h : s âŠ† f â»Â¹' e.source) :
    ContinuousOn f s â†” ContinuousOn (e âˆ˜ f) s :=
  forallâ‚‚_congr fun _x hx =>
    e.continuousWithinAt_iff_continuousWithinAt_comp_left (h hx)
      (mem_of_superset self_mem_nhdsWithin h)

/-- A function is continuous if and only if its composition with an open partial homeomorphism
on the left is continuous and its image is contained in the source. -/
theorem continuous_iff_continuous_comp_left {f : Z â†’ X} (h : f â»Â¹' e.source = univ) :
    Continuous f â†” Continuous (e âˆ˜ f) := by
  simp only [â† continuousOn_univ]
  exact e.continuousOn_iff_continuousOn_comp_left (Eq.symm h).subset

end Continuity

end OpenPartialHomeomorph
