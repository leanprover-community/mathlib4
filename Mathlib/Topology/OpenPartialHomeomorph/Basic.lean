/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
module

public import Mathlib.Topology.Homeomorph.Lemmas
public import Mathlib.Topology.OpenPartialHomeomorph.Defs
public import Mathlib.Topology.Sets.Opens
/-!
# Partial homeomorphisms: basic theory


## Main definitions

* `OpenPartialHomeomorph.refl`: the identity open partial homeomorphism
* `Topology.IsOpenEmbedding.toOpenPartialHomeomorph`: construct a partial homeomorphism from an
  open embedding
-/

@[expose] public section

open Function Set Filter Topology

variable {X X' : Type*} {Y Y' : Type*} {Z Z' : Type*}
  [TopologicalSpace X] [TopologicalSpace X'] [TopologicalSpace Y] [TopologicalSpace Y']
  [TopologicalSpace Z] [TopologicalSpace Z']

namespace OpenPartialHomeomorph

/-- The identity on the whole space as an open partial homeomorphism. -/
@[simps! (attr := mfld_simps) -fullyApplied apply, simps! -isSimp source target]
protected def refl (X : Type*) [TopologicalSpace X] : OpenPartialHomeomorph X X :=
  (Homeomorph.refl X).toOpenPartialHomeomorph

@[simp, mfld_simps]
theorem refl_partialEquiv : (OpenPartialHomeomorph.refl X).toPartialEquiv = PartialEquiv.refl X :=
  rfl

@[simp, mfld_simps]
theorem refl_symm : (OpenPartialHomeomorph.refl X).symm = OpenPartialHomeomorph.refl X :=
  rfl

variable (e : OpenPartialHomeomorph X Y)

theorem source_preimage_target : e.source âŠ† e â»Â¹' e.target :=
  e.mapsTo

theorem image_eq_target_inter_inv_preimage {s : Set X} (h : s âŠ† e.source) :
    e '' s = e.target âˆ© e.symm â»Â¹' s :=
  e.toPartialEquiv.image_eq_target_inter_inv_preimage h

theorem image_source_inter_eq' (s : Set X) : e '' (e.source âˆ© s) = e.target âˆ© e.symm â»Â¹' s :=
  e.toPartialEquiv.image_source_inter_eq' s

theorem image_source_inter_eq (s : Set X) :
    e '' (e.source âˆ© s) = e.target âˆ© e.symm â»Â¹' (e.source âˆ© s) :=
  e.toPartialEquiv.image_source_inter_eq s

theorem symm_image_eq_source_inter_preimage {s : Set Y} (h : s âŠ† e.target) :
    e.symm '' s = e.source âˆ© e â»Â¹' s :=
  e.symm.image_eq_target_inter_inv_preimage h

theorem symm_image_target_inter_eq (s : Set Y) :
    e.symm '' (e.target âˆ© s) = e.source âˆ© e â»Â¹' (e.target âˆ© s) :=
  e.symm.image_source_inter_eq _

theorem source_inter_preimage_inv_preimage (s : Set X) :
    e.source âˆ© e â»Â¹' (e.symm â»Â¹' s) = e.source âˆ© s :=
  e.toPartialEquiv.source_inter_preimage_inv_preimage s

theorem target_inter_inv_preimage_preimage (s : Set Y) :
    e.target âˆ© e.symm â»Â¹' (e â»Â¹' s) = e.target âˆ© s :=
  e.symm.source_inter_preimage_inv_preimage _

theorem source_inter_preimage_target_inter (s : Set Y) :
    e.source âˆ© e â»Â¹' (e.target âˆ© s) = e.source âˆ© e â»Â¹' s :=
  e.toPartialEquiv.source_inter_preimage_target_inter s

theorem image_source_eq_target : e '' e.source = e.target :=
  e.toPartialEquiv.image_source_eq_target

theorem symm_image_target_eq_source : e.symm '' e.target = e.source :=
  e.symm.image_source_eq_target

theorem isOpen_inter_preimage {s : Set Y} (hs : IsOpen s) : IsOpen (e.source âˆ© e â»Â¹' s) :=
  e.continuousOn.isOpen_inter_preimage e.open_source hs

theorem isOpen_inter_preimage_symm {s : Set X} (hs : IsOpen s) : IsOpen (e.target âˆ© e.symm â»Â¹' s) :=
  e.symm.continuousOn.isOpen_inter_preimage e.open_target hs

/-- An open partial homeomorphism is an open map on its source:
  the image of an open subset of the source is open. -/
lemma isOpen_image_of_subset_source {s : Set X} (hs : IsOpen s) (hse : s âŠ† e.source) :
    IsOpen (e '' s) := by
  rw [(image_eq_target_inter_inv_preimage (e := e) hse)]
  exact e.continuousOn_invFun.isOpen_inter_preimage e.open_target hs

/-- The image of the restriction of an open set to the source is open. -/
theorem isOpen_image_source_inter {s : Set X} (hs : IsOpen s) :
    IsOpen (e '' (e.source âˆ© s)) :=
  e.isOpen_image_of_subset_source (e.open_source.inter hs) inter_subset_left

/-- The inverse of an open partial homeomorphism `e` is an open map on `e.target`. -/
lemma isOpen_image_symm_of_subset_target {t : Set Y} (ht : IsOpen t) (hte : t âŠ† e.target) :
    IsOpen (e.symm '' t) :=
  isOpen_image_of_subset_source e.symm ht (e.symm_source â–¸ hte)

lemma isOpen_symm_image_iff_of_subset_target {t : Set Y} (hs : t âŠ† e.target) :
    IsOpen (e.symm '' t) â†” IsOpen t := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ e.symm.isOpen_image_of_subset_source h hsâŸ©
  have hs' : e.symm '' t âŠ† e.source := by
    rw [e.symm_image_eq_source_inter_preimage hs]
    apply Set.inter_subset_left
  rw [â† e.image_symm_image_of_subset_target hs]
  exact e.isOpen_image_of_subset_source h hs'

theorem isOpen_image_iff_of_subset_source {s : Set X} (hs : s âŠ† e.source) :
    IsOpen (e '' s) â†” IsOpen s := by
  rw [â† e.symm.isOpen_symm_image_iff_of_subset_target hs, e.symm_symm]

/-- A `PartialEquiv` with continuous open forward map and open source is a
`OpenPartialHomeomorph`. -/
@[simps toPartialEquiv]
def ofContinuousOpenRestrict (e : PartialEquiv X Y) (hc : ContinuousOn e e.source)
    (ho : IsOpenMap (e.source.restrict e)) (hs : IsOpen e.source) : OpenPartialHomeomorph X Y where
  toPartialEquiv := e
  open_source := hs
  open_target := by simpa only [range_restrict, e.image_source_eq_target] using ho.isOpen_range
  continuousOn_toFun := hc
  continuousOn_invFun := e.image_source_eq_target â–¸ ho.continuousOn_image_of_leftInvOn e.leftInvOn

@[simp]
theorem coe_ofContinuousOpenRestrict (e : PartialEquiv X Y) (hc : ContinuousOn e e.source)
    (ho : IsOpenMap (e.source.restrict e)) (hs : IsOpen e.source) :
    â‡‘(ofContinuousOpenRestrict e hc ho hs) = e :=
  rfl

@[simp]
theorem coe_ofContinuousOpenRestrict_symm (e : PartialEquiv X Y) (hc : ContinuousOn e e.source)
    (ho : IsOpenMap (e.source.restrict e)) (hs : IsOpen e.source) :
    â‡‘(ofContinuousOpenRestrict e hc ho hs).symm = e.symm :=
  rfl

/-- A `PartialEquiv` with continuous open forward map and open source is a
`OpenPartialHomeomorph`. -/
@[simps! toPartialEquiv]
def ofContinuousOpen (e : PartialEquiv X Y) (hc : ContinuousOn e e.source) (ho : IsOpenMap e)
    (hs : IsOpen e.source) : OpenPartialHomeomorph X Y :=
  ofContinuousOpenRestrict e hc (ho.restrict hs) hs

@[simp]
theorem coe_ofContinuousOpen (e : PartialEquiv X Y) (hc : ContinuousOn e e.source)
    (ho : IsOpenMap e) (hs : IsOpen e.source) :
    â‡‘(ofContinuousOpen e hc ho hs) = e :=
  rfl

@[simp]
theorem coe_ofContinuousOpen_symm (e : PartialEquiv X Y) (hc : ContinuousOn e e.source)
    (ho : IsOpenMap e) (hs : IsOpen e.source) :
    â‡‘(ofContinuousOpen e hc ho hs).symm = e.symm :=
  rfl

/-- The homeomorphism obtained by restricting an `OpenPartialHomeomorph` to a subset of the source.
-/
@[simps]
def homeomorphOfImageSubsetSource {s : Set X} {t : Set Y} (hs : s âŠ† e.source) (ht : e '' s = t) :
    s â‰ƒâ‚œ t :=
  have hâ‚ : MapsTo e s t := mapsTo_iff_image_subset.2 ht.subset
  have hâ‚‚ : t âŠ† e.target := ht â–¸ e.image_source_eq_target â–¸ image_mono hs
  have hâ‚ƒ : MapsTo e.symm t s := ht â–¸ forall_mem_image.2 fun _x hx =>
      (e.left_inv (hs hx)).symm â–¸ hx
  { toFun := MapsTo.restrict e s t hâ‚
    invFun := MapsTo.restrict e.symm t s hâ‚ƒ
    left_inv := fun a => Subtype.ext (e.left_inv (hs a.2))
    right_inv := fun b => Subtype.ext <| e.right_inv (hâ‚‚ b.2)
    continuous_toFun := (e.continuousOn.mono hs).mapsToRestrict hâ‚
    continuous_invFun := (e.continuousOn_symm.mono hâ‚‚).mapsToRestrict hâ‚ƒ }

/-- An open partial homeomorphism defines a homeomorphism between its source and target. -/
@[simps!]
def toHomeomorphSourceTarget : e.source â‰ƒâ‚œ e.target :=
  e.homeomorphOfImageSubsetSource subset_rfl e.image_source_eq_target

theorem secondCountableTopology_source [SecondCountableTopology Y] :
    SecondCountableTopology e.source :=
  e.toHomeomorphSourceTarget.secondCountableTopology

theorem nhds_eq_comap_inf_principal {x} (hx : x âˆˆ e.source) :
    ð“ x = comap e (ð“ (e x)) âŠ“ ð“Ÿ e.source := by
  lift x to e.source using hx
  rw [â† e.open_source.nhdsWithin_eq x.2, â† map_nhds_subtype_val, â† map_comap_setCoe_val,
    e.toHomeomorphSourceTarget.nhds_eq_comap, nhds_subtype_eq_comap]
  simp only [Function.comp_def, toHomeomorphSourceTarget_apply_coe, comap_comap]

/-- If an open partial homeomorphism has source and target equal to univ, then it induces a
homeomorphism between the whole spaces, expressed in this definition. -/
@[simps (attr := mfld_simps) -fullyApplied apply symm_apply]
-- TODO: add a `PartialEquiv` version
def toHomeomorphOfSourceEqUnivTargetEqUniv (h : e.source = (univ : Set X)) (h' : e.target = univ) :
    X â‰ƒâ‚œ Y where
  toFun := e
  invFun := e.symm
  left_inv x :=
    e.left_inv <| by
      rw [h]
      exact mem_univ _
  right_inv x :=
    e.right_inv <| by
      rw [h']
      exact mem_univ _
  continuous_toFun := by
    simpa only [continuousOn_univ, h] using e.continuousOn
  continuous_invFun := by
    simpa only [continuousOn_univ, h'] using e.continuousOn_symm

theorem isOpenEmbedding_restrict : IsOpenEmbedding (e.source.restrict e) := by
  refine .of_continuous_injective_isOpenMap (e.continuousOn.comp_continuous
    continuous_subtype_val Subtype.prop) e.injOn.injective fun V hV â†¦ ?_
  rw [Set.restrict_eq, Set.image_comp]
  exact e.isOpen_image_of_subset_source (e.open_source.isOpenMap_subtype_val V hV)
    fun _ âŸ¨x, _, hâŸ© â†¦ h â–¸ x.2

/-- An open partial homeomorphism whose source is all of `X` defines an open embedding of `X` into
`Y`. The converse is also true; see `IsOpenEmbedding.toOpenPartialHomeomorph`. -/
theorem to_isOpenEmbedding (h : e.source = Set.univ) : IsOpenEmbedding e :=
  e.isOpenEmbedding_restrict.comp
    ((Homeomorph.setCongr h).trans <| Homeomorph.Set.univ X).symm.isOpenEmbedding

end OpenPartialHomeomorph

/-!
## Open embeddings
-/
namespace Topology.IsOpenEmbedding

variable (f : X â†’ Y) (h : IsOpenEmbedding f)

/-- An open embedding of `X` into `Y`, with `X` nonempty, defines an open partial homeomorphism
whose source is all of `X`. The converse is also true; see
`OpenPartialHomeomorph.to_isOpenEmbedding`. -/
@[simps! (attr := mfld_simps) -fullyApplied apply source target]
noncomputable def toOpenPartialHomeomorph [Nonempty X] : OpenPartialHomeomorph X Y :=
  OpenPartialHomeomorph.ofContinuousOpen (h.isEmbedding.injective.injOn.toPartialEquiv f univ)
    h.continuous.continuousOn h.isOpenMap isOpen_univ

@[deprecated (since := "2025-08-29")] alias toPartialHomeomorph := toOpenPartialHomeomorph

variable [Nonempty X]

lemma toOpenPartialHomeomorph_left_inv {x : X} : (h.toOpenPartialHomeomorph f).symm (f x) = x := by
  rw [â† congr_fun (h.toOpenPartialHomeomorph_apply f), OpenPartialHomeomorph.left_inv]
  exact Set.mem_univ _

@[deprecated (since := "2025-08-29")] alias
  toPartialHomeomorph_left_inv := toOpenPartialHomeomorph_left_inv

lemma toOpenPartialHomeomorph_right_inv {x : Y} (hx : x âˆˆ Set.range f) :
    f ((h.toOpenPartialHomeomorph f).symm x) = x := by
  rw [â† congr_fun (h.toOpenPartialHomeomorph_apply f), OpenPartialHomeomorph.right_inv]
  rwa [toOpenPartialHomeomorph_target]

@[deprecated (since := "2025-08-29")] alias
  toPartialHomeomorph_right_inv := toOpenPartialHomeomorph_right_inv

end Topology.IsOpenEmbedding

/-! inclusion of an open set in a topological space -/
namespace TopologicalSpace.Opens

/- `Nonempty s` is not a type class argument because `s`, being a subset, rarely comes with a type
class instance. Then we'd have to manually provide the instance every time we use the following
lemmas, tediously using `haveI := ...` or `@foobar _ _ _ ...`. -/
variable (s : Opens X) (hs : Nonempty s)

/-- The inclusion of an open subset `s` of a space `X` into `X` is an open partial homeomorphism
from the subtype `s` to `X`. -/
noncomputable def openPartialHomeomorphSubtypeCoe : OpenPartialHomeomorph s X :=
  IsOpenEmbedding.toOpenPartialHomeomorph _ s.2.isOpenEmbedding_subtypeVal

@[deprecated (since := "2025-08-29")] alias
  partialHomeomorphSubtypeCoe := openPartialHomeomorphSubtypeCoe

@[simp, mfld_simps]
theorem openPartialHomeomorphSubtypeCoe_coe :
    (s.openPartialHomeomorphSubtypeCoe hs : s â†’ X) = (â†‘) :=
  rfl

@[deprecated (since := "2025-08-29")] alias
  partialHomeomorphSubtypeCoe_coe := openPartialHomeomorphSubtypeCoe_coe

@[simp, mfld_simps]
theorem openPartialHomeomorphSubtypeCoe_source :
    (s.openPartialHomeomorphSubtypeCoe hs).source = Set.univ :=
  rfl

@[deprecated (since := "2025-08-29")] alias
  partialHomeomorphSubtypeCoe_source := openPartialHomeomorphSubtypeCoe_source

@[simp, mfld_simps]
theorem openPartialHomeomorphSubtypeCoe_target :
    (s.openPartialHomeomorphSubtypeCoe hs).target = s := by
  simp only [openPartialHomeomorphSubtypeCoe, Subtype.range_coe_subtype, mfld_simps]
  rfl

@[deprecated (since := "2025-08-29")] alias
  partialHomeomorphSubtypeCoe_target := openPartialHomeomorphSubtypeCoe_target

end TopologicalSpace.Opens
