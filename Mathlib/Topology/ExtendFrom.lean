/-
Copyright (c) 2020 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Anatole Dedecker
-/
import Mathlib.Topology.Separation.Regular

/-!
# Extending a function from a subset

The main definition of this file is `extendFrom A f` where `f : X â†’ Y`
and `A : Set X`. This defines a new function `g : X â†’ Y` which maps any
`xâ‚€ : X` to the limit of `f` as `x` tends to `xâ‚€`, if such a limit exists.

This is analogous to the way `IsDenseInducing.extend` "extends" a function
`f : X â†’ Z` to a function `g : Y â†’ Z` along a dense inducing `i : X â†’ Y`.

The main theorem we prove about this definition is `continuousOn_extendFrom`
which states that, for `extendFrom A f` to be continuous on a set `B âŠ† closure A`,
it suffices that `f` converges within `A` at any point of `B`, provided that
`f` is a function to a Tâ‚ƒ space.

-/


noncomputable section

open Topology

open Filter Set

variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]

/-- Extend a function from a set `A`. The resulting function `g` is such that
at any `xâ‚€`, if `f` converges to some `y` as `x` tends to `xâ‚€` within `A`,
then `g xâ‚€` is defined to be one of these `y`. Else, `g xâ‚€` could be anything. -/
def extendFrom (A : Set X) (f : X â†’ Y) : X â†’ Y :=
  fun x â†¦ @limUnder _ _ _ âŸ¨f xâŸ© (ğ“[A] x) f

/-- If `f` converges to some `y` as `x` tends to `xâ‚€` within `A`,
then `f` tends to `extendFrom A f x` as `x` tends to `xâ‚€`. -/
theorem tendsto_extendFrom {A : Set X} {f : X â†’ Y} {x : X} (h : âˆƒ y, Tendsto f (ğ“[A] x) (ğ“ y)) :
    Tendsto f (ğ“[A] x) (ğ“ <| extendFrom A f x) :=
  tendsto_nhds_limUnder h

theorem extendFrom_eq [T2Space Y] {A : Set X} {f : X â†’ Y} {x : X} {y : Y} (hx : x âˆˆ closure A)
    (hf : Tendsto f (ğ“[A] x) (ğ“ y)) : extendFrom A f x = y :=
  haveI := mem_closure_iff_nhdsWithin_neBot.mp hx
  tendsto_nhds_unique (tendsto_nhds_limUnder âŸ¨y, hfâŸ©) hf

theorem extendFrom_extends [T2Space Y] {f : X â†’ Y} {A : Set X} (hf : ContinuousOn f A) :
    âˆ€ x âˆˆ A, extendFrom A f x = f x :=
  fun x x_in â†¦ extendFrom_eq (subset_closure x_in) (hf x x_in)

/-- If `f` is a function to a Tâ‚ƒ space `Y` which has a limit within `A` at any
point of a set `B âŠ† closure A`, then `extendFrom A f` is continuous on `B`. -/
theorem continuousOn_extendFrom [RegularSpace Y] {f : X â†’ Y} {A B : Set X} (hB : B âŠ† closure A)
    (hf : âˆ€ x âˆˆ B, âˆƒ y, Tendsto f (ğ“[A] x) (ğ“ y)) : ContinuousOn (extendFrom A f) B := by
  set Ï† := extendFrom A f
  intro x x_in
  suffices âˆ€ V' âˆˆ ğ“ (Ï† x), IsClosed V' â†’ Ï† â»Â¹' V' âˆˆ ğ“[B] x by
    simpa [ContinuousWithinAt, (closed_nhds_basis (Ï† x)).tendsto_right_iff]
  intro V' V'_in V'_closed
  obtain âŸ¨V, V_in, V_op, hVâŸ© : âˆƒ V âˆˆ ğ“ x, IsOpen V âˆ§ V âˆ© A âŠ† f â»Â¹' V' := by
    have := tendsto_extendFrom (hf x x_in)
    rcases (nhdsWithin_basis_open x A).tendsto_left_iff.mp this V' V'_in with âŸ¨V, âŸ¨hxV, V_opâŸ©, hVâŸ©
    exact âŸ¨V, IsOpen.mem_nhds V_op hxV, V_op, hVâŸ©
  suffices âˆ€ y âˆˆ V âˆ© B, Ï† y âˆˆ V' from
    mem_of_superset (inter_mem_inf V_in <| mem_principal_self B) this
  rintro y âŸ¨hyV, hyBâŸ©
  haveI := mem_closure_iff_nhdsWithin_neBot.mp (hB hyB)
  have limy : Tendsto f (ğ“[A] y) (ğ“ <| Ï† y) := tendsto_extendFrom (hf y hyB)
  have hVy : V âˆˆ ğ“ y := IsOpen.mem_nhds V_op hyV
  have : V âˆ© A âˆˆ ğ“[A] y := by simpa only [inter_comm] using inter_mem_nhdsWithin A hVy
  exact V'_closed.mem_of_tendsto limy (mem_of_superset this hV)

/-- If a function `f` to a Tâ‚ƒ space `Y` has a limit within a
dense set `A` for any `x`, then `extendFrom A f` is continuous. -/
theorem continuous_extendFrom [RegularSpace Y] {f : X â†’ Y} {A : Set X} (hA : Dense A)
    (hf : âˆ€ x, âˆƒ y, Tendsto f (ğ“[A] x) (ğ“ y)) : Continuous (extendFrom A f) := by
  rw [â† continuousOn_univ]
  exact continuousOn_extendFrom (fun x _ â†¦ hA x) (by simpa using hf)
