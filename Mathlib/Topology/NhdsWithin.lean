/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, SÃ©bastien GouÃ«zel
-/
import Mathlib.Topology.Constructions

/-!
# Neighborhoods relative to a subset

This file develops API on the relative version `nhdsWithin` of `nhds`, which is defined in previous
definition files.

Their basic properties studied in this file include the relationship between neighborhood filters
relative to a set and neighborhood filters in the corresponding subtype, and are in later files used
to develop relativ versions `ContinuousOn` and `ContinuousWithinAt` of `Continuous` and
`ContinuousAt`.

## Notation

* `ğ“ x`: the filter of neighborhoods of a point `x`;
* `ğ“Ÿ s`: the principal filter of a set `s`;
* `ğ“[s] x`: the filter `nhdsWithin x s` of neighborhoods of a point `x` within a set `s`.

-/

open Set Filter Function Topology

variable {Î± Î² Î³ Î´ : Type*} [TopologicalSpace Î±]

/-!
## Properties of the neighborhood-within filter
-/

@[simp]
theorem nhds_bind_nhdsWithin {a : Î±} {s : Set Î±} : ((ğ“ a).bind fun x => ğ“[s] x) = ğ“[s] a :=
  bind_inf_principal.trans <| congr_argâ‚‚ _ nhds_bind_nhds rfl

@[simp]
theorem eventually_nhds_nhdsWithin {a : Î±} {s : Set Î±} {p : Î± â†’ Prop} :
    (âˆ€á¶  y in ğ“ a, âˆ€á¶  x in ğ“[s] y, p x) â†” âˆ€á¶  x in ğ“[s] a, p x :=
  Filter.ext_iff.1 nhds_bind_nhdsWithin { x | p x }

theorem eventually_nhdsWithin_iff {a : Î±} {s : Set Î±} {p : Î± â†’ Prop} :
    (âˆ€á¶  x in ğ“[s] a, p x) â†” âˆ€á¶  x in ğ“ a, x âˆˆ s â†’ p x :=
  eventually_inf_principal

theorem frequently_nhdsWithin_iff {z : Î±} {s : Set Î±} {p : Î± â†’ Prop} :
    (âˆƒá¶  x in ğ“[s] z, p x) â†” âˆƒá¶  x in ğ“ z, p x âˆ§ x âˆˆ s :=
  frequently_inf_principal.trans <| by simp only [and_comm]

theorem mem_closure_ne_iff_frequently_within {z : Î±} {s : Set Î±} :
    z âˆˆ closure (s \ {z}) â†” âˆƒá¶  x in ğ“[â‰ ] z, x âˆˆ s := by
  simp [mem_closure_iff_frequently, frequently_nhdsWithin_iff]

@[simp]
theorem eventually_eventually_nhdsWithin {a : Î±} {s : Set Î±} {p : Î± â†’ Prop} :
    (âˆ€á¶  y in ğ“[s] a, âˆ€á¶  x in ğ“[s] y, p x) â†” âˆ€á¶  x in ğ“[s] a, p x := by
  refine âŸ¨fun h => ?_, fun h => (eventually_nhds_nhdsWithin.2 h).filter_mono inf_le_leftâŸ©
  simp only [eventually_nhdsWithin_iff] at h âŠ¢
  exact h.mono fun x hx hxs => (hx hxs).self_of_nhds hxs

@[simp]
theorem eventually_mem_nhdsWithin_iff {x : Î±} {s t : Set Î±} :
    (âˆ€á¶  x' in ğ“[s] x, t âˆˆ ğ“[s] x') â†” t âˆˆ ğ“[s] x :=
  eventually_eventually_nhdsWithin

theorem nhdsWithin_eq (a : Î±) (s : Set Î±) :
    ğ“[s] a = â¨… t âˆˆ { t : Set Î± | a âˆˆ t âˆ§ IsOpen t }, ğ“Ÿ (t âˆ© s) :=
  ((nhds_basis_opens a).inf_principal s).eq_biInf

@[simp] lemma nhdsWithin_univ (a : Î±) : ğ“[Set.univ] a = ğ“ a := by
  rw [nhdsWithin, principal_univ, inf_top_eq]

theorem nhdsWithin_hasBasis {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±} {a : Î±}
    (h : (ğ“ a).HasBasis p s) (t : Set Î±) : (ğ“[t] a).HasBasis p fun i => s i âˆ© t :=
  h.inf_principal t

theorem nhdsWithin_basis_open (a : Î±) (t : Set Î±) :
    (ğ“[t] a).HasBasis (fun u => a âˆˆ u âˆ§ IsOpen u) fun u => u âˆ© t :=
  nhdsWithin_hasBasis (nhds_basis_opens a) t

theorem mem_nhdsWithin {t : Set Î±} {a : Î±} {s : Set Î±} :
    t âˆˆ ğ“[s] a â†” âˆƒ u, IsOpen u âˆ§ a âˆˆ u âˆ§ u âˆ© s âŠ† t := by
  simpa only [and_assoc, and_left_comm] using (nhdsWithin_basis_open a s).mem_iff

theorem mem_nhdsWithin_iff_exists_mem_nhds_inter {t : Set Î±} {a : Î±} {s : Set Î±} :
    t âˆˆ ğ“[s] a â†” âˆƒ u âˆˆ ğ“ a, u âˆ© s âŠ† t :=
  (nhdsWithin_hasBasis (ğ“ a).basis_sets s).mem_iff

theorem diff_mem_nhdsWithin_compl {x : Î±} {s : Set Î±} (hs : s âˆˆ ğ“ x) (t : Set Î±) :
    s \ t âˆˆ ğ“[tá¶œ] x :=
  diff_mem_inf_principal_compl hs t

theorem diff_mem_nhdsWithin_diff {x : Î±} {s t : Set Î±} (hs : s âˆˆ ğ“[t] x) (t' : Set Î±) :
    s \ t' âˆˆ ğ“[t \ t'] x := by
  rw [nhdsWithin, diff_eq, diff_eq, â† inf_principal, â† inf_assoc]
  exact inter_mem_inf hs (mem_principal_self _)

theorem nhds_of_nhdsWithin_of_nhds {s t : Set Î±} {a : Î±} (h1 : s âˆˆ ğ“ a) (h2 : t âˆˆ ğ“[s] a) :
    t âˆˆ ğ“ a := by
  rcases mem_nhdsWithin_iff_exists_mem_nhds_inter.mp h2 with âŸ¨_, Hw, hwâŸ©
  exact (ğ“ a).sets_of_superset ((ğ“ a).inter_sets Hw h1) hw

theorem mem_nhdsWithin_iff_eventually {s t : Set Î±} {x : Î±} :
    t âˆˆ ğ“[s] x â†” âˆ€á¶  y in ğ“ x, y âˆˆ s â†’ y âˆˆ t :=
  eventually_inf_principal

theorem mem_nhdsWithin_iff_eventuallyEq {s t : Set Î±} {x : Î±} :
    t âˆˆ ğ“[s] x â†” s =á¶ [ğ“ x] (s âˆ© t : Set Î±) := by
  simp_rw [mem_nhdsWithin_iff_eventually, eventuallyEq_set, mem_inter_iff, iff_self_and]

theorem nhdsWithin_eq_iff_eventuallyEq {s t : Set Î±} {x : Î±} : ğ“[s] x = ğ“[t] x â†” s =á¶ [ğ“ x] t :=
  set_eventuallyEq_iff_inf_principal.symm

theorem nhdsWithin_le_iff {s t : Set Î±} {x : Î±} : ğ“[s] x â‰¤ ğ“[t] x â†” t âˆˆ ğ“[s] x :=
  set_eventuallyLE_iff_inf_principal_le.symm.trans set_eventuallyLE_iff_mem_inf_principal

theorem preimage_nhdsWithin_coinduced' {X : Î± â†’ Î²} {s : Set Î²} {t : Set Î±} {a : Î±} (h : a âˆˆ t)
    (hs : s âˆˆ @nhds Î² (.coinduced (fun x : t => X x) inferInstance) (X a)) :
    X â»Â¹' s âˆˆ ğ“[t] a := by
  lift a to t using h
  replace hs : (fun x : t => X x) â»Â¹' s âˆˆ ğ“ a := preimage_nhds_coinduced hs
  rwa [â† map_nhds_subtype_val, mem_map]

theorem mem_nhdsWithin_of_mem_nhds {s t : Set Î±} {a : Î±} (h : s âˆˆ ğ“ a) : s âˆˆ ğ“[t] a :=
  mem_inf_of_left h

theorem self_mem_nhdsWithin {a : Î±} {s : Set Î±} : s âˆˆ ğ“[s] a :=
  mem_inf_of_right (mem_principal_self s)

theorem eventually_mem_nhdsWithin {a : Î±} {s : Set Î±} : âˆ€á¶  x in ğ“[s] a, x âˆˆ s :=
  self_mem_nhdsWithin

theorem inter_mem_nhdsWithin (s : Set Î±) {t : Set Î±} {a : Î±} (h : t âˆˆ ğ“ a) : s âˆ© t âˆˆ ğ“[s] a :=
  inter_mem self_mem_nhdsWithin (mem_inf_of_left h)

theorem pure_le_nhdsWithin {a : Î±} {s : Set Î±} (ha : a âˆˆ s) : pure a â‰¤ ğ“[s] a :=
  le_inf (pure_le_nhds a) (le_principal_iff.2 ha)

theorem mem_of_mem_nhdsWithin {a : Î±} {s t : Set Î±} (ha : a âˆˆ s) (ht : t âˆˆ ğ“[s] a) : a âˆˆ t :=
  pure_le_nhdsWithin ha ht

theorem Filter.Eventually.self_of_nhdsWithin {p : Î± â†’ Prop} {s : Set Î±} {x : Î±}
    (h : âˆ€á¶  y in ğ“[s] x, p y) (hx : x âˆˆ s) : p x :=
  mem_of_mem_nhdsWithin hx h

theorem tendsto_const_nhdsWithin {l : Filter Î²} {s : Set Î±} {a : Î±} (ha : a âˆˆ s) :
    Tendsto (fun _ : Î² => a) l (ğ“[s] a) :=
  tendsto_const_pure.mono_right <| pure_le_nhdsWithin ha

theorem nhdsWithin_restrict'' {a : Î±} (s : Set Î±) {t : Set Î±} (h : t âˆˆ ğ“[s] a) :
    ğ“[s] a = ğ“[s âˆ© t] a :=
  le_antisymm (le_inf inf_le_left (le_principal_iff.mpr (inter_mem self_mem_nhdsWithin h)))
    (inf_le_inf_left _ (principal_mono.mpr Set.inter_subset_left))

theorem nhdsWithin_restrict' {a : Î±} (s : Set Î±) {t : Set Î±} (h : t âˆˆ ğ“ a) : ğ“[s] a = ğ“[s âˆ© t] a :=
  nhdsWithin_restrict'' s <| mem_inf_of_left h

theorem nhdsWithin_restrict {a : Î±} (s : Set Î±) {t : Set Î±} (hâ‚€ : a âˆˆ t) (hâ‚ : IsOpen t) :
    ğ“[s] a = ğ“[s âˆ© t] a :=
  nhdsWithin_restrict' s (IsOpen.mem_nhds hâ‚ hâ‚€)

theorem nhdsWithin_le_of_mem {a : Î±} {s t : Set Î±} (h : s âˆˆ ğ“[t] a) : ğ“[t] a â‰¤ ğ“[s] a :=
  nhdsWithin_le_iff.mpr h

theorem nhdsWithin_le_nhds {a : Î±} {s : Set Î±} : ğ“[s] a â‰¤ ğ“ a := by
  rw [â† nhdsWithin_univ]
  apply nhdsWithin_le_of_mem
  exact univ_mem

theorem nhdsWithin_eq_nhdsWithin' {a : Î±} {s t u : Set Î±} (hs : s âˆˆ ğ“ a) (hâ‚‚ : t âˆ© s = u âˆ© s) :
    ğ“[t] a = ğ“[u] a := by rw [nhdsWithin_restrict' t hs, nhdsWithin_restrict' u hs, hâ‚‚]

theorem nhdsWithin_eq_nhdsWithin {a : Î±} {s t u : Set Î±} (hâ‚€ : a âˆˆ s) (hâ‚ : IsOpen s)
    (hâ‚‚ : t âˆ© s = u âˆ© s) : ğ“[t] a = ğ“[u] a := by
  rw [nhdsWithin_restrict t hâ‚€ hâ‚, nhdsWithin_restrict u hâ‚€ hâ‚, hâ‚‚]

@[simp] theorem nhdsWithin_eq_nhds {a : Î±} {s : Set Î±} : ğ“[s] a = ğ“ a â†” s âˆˆ ğ“ a :=
  inf_eq_left.trans le_principal_iff

theorem IsOpen.nhdsWithin_eq {a : Î±} {s : Set Î±} (h : IsOpen s) (ha : a âˆˆ s) : ğ“[s] a = ğ“ a :=
  nhdsWithin_eq_nhds.2 <| h.mem_nhds ha

theorem preimage_nhds_within_coinduced {X : Î± â†’ Î²} {s : Set Î²} {t : Set Î±} {a : Î±} (h : a âˆˆ t)
    (ht : IsOpen t)
    (hs : s âˆˆ @nhds Î² (.coinduced (fun x : t => X x) inferInstance) (X a)) :
    X â»Â¹' s âˆˆ ğ“ a := by
  rw [â† ht.nhdsWithin_eq h]
  exact preimage_nhdsWithin_coinduced' h hs

@[simp]
theorem nhdsWithin_empty (a : Î±) : ğ“[âˆ…] a = âŠ¥ := by rw [nhdsWithin, principal_empty, inf_bot_eq]

theorem nhdsWithin_union (a : Î±) (s t : Set Î±) : ğ“[s âˆª t] a = ğ“[s] a âŠ” ğ“[t] a := by
  delta nhdsWithin
  rw [â† inf_sup_left, sup_principal]

theorem nhds_eq_nhdsWithin_sup_nhdsWithin (b : Î±) {Iâ‚ Iâ‚‚ : Set Î±} (hI : Set.univ = Iâ‚ âˆª Iâ‚‚) :
    nhds b = nhdsWithin b Iâ‚ âŠ” nhdsWithin b Iâ‚‚ := by
  rw [â† nhdsWithin_univ b, hI, nhdsWithin_union]

/-- If `L` and `R` are neighborhoods of `b` within sets whose union is `Set.univ`, then
`L âˆª R` is a neighborhood of `b`. -/
theorem union_mem_nhds_of_mem_nhdsWithin {b : Î±}
    {Iâ‚ Iâ‚‚ : Set Î±} (h : Set.univ = Iâ‚ âˆª Iâ‚‚)
    {L : Set Î±} (hL : L âˆˆ nhdsWithin b Iâ‚)
    {R : Set Î±} (hR : R âˆˆ nhdsWithin b Iâ‚‚) : L âˆª R âˆˆ nhds b := by
  rw [â† nhdsWithin_univ b, h, nhdsWithin_union]
  exact âŸ¨mem_of_superset hL (by simp), mem_of_superset hR (by simp)âŸ©


/-- Writing a punctured neighborhood filter as a sup of left and right filters. -/
lemma punctured_nhds_eq_nhdsWithin_sup_nhdsWithin [LinearOrder Î±] {x : Î±} :
    ğ“[â‰ ] x = ğ“[<] x âŠ” ğ“[>] x := by
  rw [â† Iio_union_Ioi, nhdsWithin_union]


/-- Obtain a "predictably-sided" neighborhood of `b` from two one-sided neighborhoods. -/
theorem nhds_of_Ici_Iic [LinearOrder Î±] {b : Î±}
    {L : Set Î±} (hL : L âˆˆ ğ“[â‰¤] b)
    {R : Set Î±} (hR : R âˆˆ ğ“[â‰¥] b) : L âˆ© Iic b âˆª R âˆ© Ici b âˆˆ ğ“ b :=
  union_mem_nhds_of_mem_nhdsWithin Iic_union_Ici.symm
    (inter_mem hL self_mem_nhdsWithin) (inter_mem hR self_mem_nhdsWithin)

theorem nhdsWithin_biUnion {Î¹} {I : Set Î¹} (hI : I.Finite) (s : Î¹ â†’ Set Î±) (a : Î±) :
    ğ“[â‹ƒ i âˆˆ I, s i] a = â¨† i âˆˆ I, ğ“[s i] a := by
  induction I, hI using Set.Finite.induction_on with
  | empty => simp
  | insert _ _ hT => simp only [hT, nhdsWithin_union, iSup_insert, biUnion_insert]

theorem nhdsWithin_sUnion {S : Set (Set Î±)} (hS : S.Finite) (a : Î±) :
    ğ“[â‹ƒâ‚€ S] a = â¨† s âˆˆ S, ğ“[s] a := by
  rw [sUnion_eq_biUnion, nhdsWithin_biUnion hS]

theorem nhdsWithin_iUnion {Î¹} [Finite Î¹] (s : Î¹ â†’ Set Î±) (a : Î±) :
    ğ“[â‹ƒ i, s i] a = â¨† i, ğ“[s i] a := by
  rw [â† sUnion_range, nhdsWithin_sUnion (finite_range s), iSup_range]

theorem nhdsWithin_inter (a : Î±) (s t : Set Î±) : ğ“[s âˆ© t] a = ğ“[s] a âŠ“ ğ“[t] a := by
  delta nhdsWithin
  rw [inf_left_comm, inf_assoc, inf_principal, â† inf_assoc, inf_idem]

theorem nhdsWithin_inter' (a : Î±) (s t : Set Î±) : ğ“[s âˆ© t] a = ğ“[s] a âŠ“ ğ“Ÿ t := by
  delta nhdsWithin
  rw [â† inf_principal, inf_assoc]

theorem nhdsWithin_inter_of_mem {a : Î±} {s t : Set Î±} (h : s âˆˆ ğ“[t] a) : ğ“[s âˆ© t] a = ğ“[t] a := by
  rw [nhdsWithin_inter, inf_eq_right]
  exact nhdsWithin_le_of_mem h

theorem nhdsWithin_inter_of_mem' {a : Î±} {s t : Set Î±} (h : t âˆˆ ğ“[s] a) : ğ“[s âˆ© t] a = ğ“[s] a := by
  rw [inter_comm, nhdsWithin_inter_of_mem h]

@[simp]
theorem nhdsWithin_singleton (a : Î±) : ğ“[{a}] a = pure a := by
  rw [nhdsWithin, principal_singleton, inf_eq_right.2 (pure_le_nhds a)]

@[simp]
theorem nhdsWithin_insert (a : Î±) (s : Set Î±) : ğ“[insert a s] a = pure a âŠ” ğ“[s] a := by
  rw [â† singleton_union, nhdsWithin_union, nhdsWithin_singleton]

theorem mem_nhdsWithin_insert {a : Î±} {s t : Set Î±} : t âˆˆ ğ“[insert a s] a â†” a âˆˆ t âˆ§ t âˆˆ ğ“[s] a := by
  simp

theorem insert_mem_nhdsWithin_insert {a : Î±} {s t : Set Î±} (h : t âˆˆ ğ“[s] a) :
    insert a t âˆˆ ğ“[insert a s] a := by simp [mem_of_superset h]

theorem insert_mem_nhds_iff {a : Î±} {s : Set Î±} : insert a s âˆˆ ğ“ a â†” s âˆˆ ğ“[â‰ ] a := by
  simp only [nhdsWithin, mem_inf_principal, mem_compl_iff, mem_singleton_iff, or_iff_not_imp_left,
    insert_def]

@[simp]
theorem nhdsNE_sup_pure (a : Î±) : ğ“[â‰ ] a âŠ” pure a = ğ“ a := by
  rw [â† nhdsWithin_singleton, â† nhdsWithin_union, compl_union_self, nhdsWithin_univ]

@[deprecated (since := "2025-03-02")]
alias nhdsWithin_compl_singleton_sup_pure := nhdsNE_sup_pure

@[simp]
theorem pure_sup_nhdsNE (a : Î±) : pure a âŠ” ğ“[â‰ ] a = ğ“ a := by rw [â† sup_comm, nhdsNE_sup_pure]

lemma continuousAt_iff_punctured_nhds [TopologicalSpace Î²] {f : Î± â†’ Î²} {a : Î±} :
    ContinuousAt f a â†” Tendsto f (ğ“[â‰ ] a) (ğ“ (f a)) := by
  simp [ContinuousAt, - pure_sup_nhdsNE, â† pure_sup_nhdsNE a, tendsto_pure_nhds]

theorem nhdsWithin_prod [TopologicalSpace Î²]
    {s u : Set Î±} {t v : Set Î²} {a : Î±} {b : Î²} (hu : u âˆˆ ğ“[s] a) (hv : v âˆˆ ğ“[t] b) :
    u Ã—Ë¢ v âˆˆ ğ“[s Ã—Ë¢ t] (a, b) := by
  rw [nhdsWithin_prod_eq]
  exact prod_mem_prod hu hv

lemma Filter.EventuallyEq.mem_interior {x : Î±} {s t : Set Î±} (hst : s =á¶ [ğ“ x] t)
    (h : x âˆˆ interior s) : x âˆˆ interior t := by
  rw [â† nhdsWithin_eq_iff_eventuallyEq] at hst
  simpa [mem_interior_iff_mem_nhds, â† nhdsWithin_eq_nhds, hst] using h

lemma Filter.EventuallyEq.mem_interior_iff {x : Î±} {s t : Set Î±} (hst : s =á¶ [ğ“ x] t) :
    x âˆˆ interior s â†” x âˆˆ interior t :=
  âŸ¨fun h â†¦ hst.mem_interior h, fun h â†¦ hst.symm.mem_interior hâŸ©

section Pi

variable {Î¹ : Type*} {X : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (X i)]

theorem nhdsWithin_pi_eq' {I : Set Î¹} (hI : I.Finite) (s : âˆ€ i, Set (X i)) (x : âˆ€ i, X i) :
    ğ“[pi I s] x = â¨… i, comap (fun x => x i) (ğ“ (x i) âŠ“ â¨… (_ : i âˆˆ I), ğ“Ÿ (s i)) := by
  simp only [nhdsWithin, nhds_pi, Filter.pi, comap_inf, comap_iInf, pi_def, comap_principal, â†
    iInf_principal_finite hI, â† iInf_inf_eq]

theorem nhdsWithin_pi_eq {I : Set Î¹} (hI : I.Finite) (s : âˆ€ i, Set (X i)) (x : âˆ€ i, X i) :
    ğ“[pi I s] x =
      (â¨… i âˆˆ I, comap (fun x => x i) (ğ“[s i] x i)) âŠ“
        â¨… (i) (_ : i âˆ‰ I), comap (fun x => x i) (ğ“ (x i)) := by
  simp only [nhdsWithin, nhds_pi, Filter.pi, pi_def, â† iInf_principal_finite hI, comap_inf,
    comap_principal]
  rw [iInf_split _ fun i => i âˆˆ I, inf_right_comm]
  simp only [iInf_inf_eq]

theorem nhdsWithin_pi_univ_eq [Finite Î¹] (s : âˆ€ i, Set (X i)) (x : âˆ€ i, X i) :
    ğ“[pi univ s] x = â¨… i, comap (fun x => x i) (ğ“[s i] x i) := by
  simpa [nhdsWithin] using nhdsWithin_pi_eq finite_univ s x

theorem nhdsWithin_pi_eq_bot {I : Set Î¹} {s : âˆ€ i, Set (X i)} {x : âˆ€ i, X i} :
    ğ“[pi I s] x = âŠ¥ â†” âˆƒ i âˆˆ I, ğ“[s i] x i = âŠ¥ := by
  simp only [nhdsWithin, nhds_pi, pi_inf_principal_pi_eq_bot]

theorem nhdsWithin_pi_neBot {I : Set Î¹} {s : âˆ€ i, Set (X i)} {x : âˆ€ i, X i} :
    (ğ“[pi I s] x).NeBot â†” âˆ€ i âˆˆ I, (ğ“[s i] x i).NeBot := by
  simp [neBot_iff, nhdsWithin_pi_eq_bot]

instance instNeBotNhdsWithinUnivPi {s : âˆ€ i, Set (X i)} {x : âˆ€ i, X i}
    [âˆ€ i, (ğ“[s i] x i).NeBot] : (ğ“[pi univ s] x).NeBot := by
  simpa [nhdsWithin_pi_neBot]

instance Pi.instNeBotNhdsWithinIio [Nonempty Î¹] [âˆ€ i, Preorder (X i)] {x : âˆ€ i, X i}
    [âˆ€ i, (ğ“[<] x i).NeBot] : (ğ“[<] x).NeBot :=
  have : (ğ“[pi univ fun i â†¦ Iio (x i)] x).NeBot := inferInstance
  this.mono <| nhdsWithin_mono _ fun _y hy â†¦ lt_of_strongLT fun i â†¦ hy i trivial

instance Pi.instNeBotNhdsWithinIoi [Nonempty Î¹] [âˆ€ i, Preorder (X i)] {x : âˆ€ i, X i}
    [âˆ€ i, (ğ“[>] x i).NeBot] : (ğ“[>] x).NeBot :=
  Pi.instNeBotNhdsWithinIio (X := fun i â†¦ (X i)áµ’áµˆ) (x := fun i â†¦ OrderDual.toDual (x i))

end Pi

theorem Filter.Tendsto.piecewise_nhdsWithin {f g : Î± â†’ Î²} {t : Set Î±} [âˆ€ x, Decidable (x âˆˆ t)]
    {a : Î±} {s : Set Î±} {l : Filter Î²} (hâ‚€ : Tendsto f (ğ“[s âˆ© t] a) l)
    (hâ‚ : Tendsto g (ğ“[s âˆ© tá¶œ] a) l) : Tendsto (piecewise t f g) (ğ“[s] a) l := by
  apply Tendsto.piecewise <;> rwa [â† nhdsWithin_inter']

theorem Filter.Tendsto.if_nhdsWithin {f g : Î± â†’ Î²} {p : Î± â†’ Prop} [DecidablePred p] {a : Î±}
    {s : Set Î±} {l : Filter Î²} (hâ‚€ : Tendsto f (ğ“[s âˆ© { x | p x }] a) l)
    (hâ‚ : Tendsto g (ğ“[s âˆ© { x | Â¬p x }] a) l) :
    Tendsto (fun x => if p x then f x else g x) (ğ“[s] a) l :=
  hâ‚€.piecewise_nhdsWithin hâ‚

theorem map_nhdsWithin (f : Î± â†’ Î²) (a : Î±) (s : Set Î±) :
    map f (ğ“[s] a) = â¨… t âˆˆ { t : Set Î± | a âˆˆ t âˆ§ IsOpen t }, ğ“Ÿ (f '' (t âˆ© s)) :=
  ((nhdsWithin_basis_open a s).map f).eq_biInf

theorem tendsto_nhdsWithin_mono_left {f : Î± â†’ Î²} {a : Î±} {s t : Set Î±} {l : Filter Î²} (hst : s âŠ† t)
    (h : Tendsto f (ğ“[t] a) l) : Tendsto f (ğ“[s] a) l :=
  h.mono_left <| nhdsWithin_mono a hst

theorem tendsto_nhdsWithin_mono_right {f : Î² â†’ Î±} {l : Filter Î²} {a : Î±} {s t : Set Î±} (hst : s âŠ† t)
    (h : Tendsto f l (ğ“[s] a)) : Tendsto f l (ğ“[t] a) :=
  h.mono_right (nhdsWithin_mono a hst)

theorem tendsto_nhdsWithin_of_tendsto_nhds {f : Î± â†’ Î²} {a : Î±} {s : Set Î±} {l : Filter Î²}
    (h : Tendsto f (ğ“ a) l) : Tendsto f (ğ“[s] a) l :=
  h.mono_left inf_le_left

theorem eventually_mem_of_tendsto_nhdsWithin {f : Î² â†’ Î±} {a : Î±} {s : Set Î±} {l : Filter Î²}
    (h : Tendsto f l (ğ“[s] a)) : âˆ€á¶  i in l, f i âˆˆ s := by
  simp_rw [nhdsWithin_eq, tendsto_iInf, mem_setOf_eq, tendsto_principal, mem_inter_iff,
    eventually_and] at h
  exact (h univ âŸ¨mem_univ a, isOpen_univâŸ©).2

theorem tendsto_nhds_of_tendsto_nhdsWithin {f : Î² â†’ Î±} {a : Î±} {s : Set Î±} {l : Filter Î²}
    (h : Tendsto f l (ğ“[s] a)) : Tendsto f l (ğ“ a) :=
  h.mono_right nhdsWithin_le_nhds

theorem nhdsWithin_neBot_of_mem {s : Set Î±} {x : Î±} (hx : x âˆˆ s) : NeBot (ğ“[s] x) :=
  mem_closure_iff_nhdsWithin_neBot.1 <| subset_closure hx

theorem IsClosed.mem_of_nhdsWithin_neBot {s : Set Î±} (hs : IsClosed s) {x : Î±}
    (hx : NeBot <| ğ“[s] x) : x âˆˆ s :=
  hs.closure_eq â–¸ mem_closure_iff_nhdsWithin_neBot.2 hx

theorem DenseRange.nhdsWithin_neBot {Î¹ : Type*} {f : Î¹ â†’ Î±} (h : DenseRange f) (x : Î±) :
    NeBot (ğ“[range f] x) :=
  mem_closure_iff_clusterPt.1 (h x)

theorem mem_closure_pi {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (Î± i)] {I : Set Î¹}
    {s : âˆ€ i, Set (Î± i)} {x : âˆ€ i, Î± i} : x âˆˆ closure (pi I s) â†” âˆ€ i âˆˆ I, x i âˆˆ closure (s i) := by
  simp only [mem_closure_iff_nhdsWithin_neBot, nhdsWithin_pi_neBot]

theorem closure_pi_set {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (Î± i)] (I : Set Î¹)
    (s : âˆ€ i, Set (Î± i)) : closure (pi I s) = pi I fun i => closure (s i) :=
  Set.ext fun _ => mem_closure_pi

theorem dense_pi {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (Î± i)] {s : âˆ€ i, Set (Î± i)}
    (I : Set Î¹) (hs : âˆ€ i âˆˆ I, Dense (s i)) : Dense (pi I s) := by
  simp only [dense_iff_closure_eq, closure_pi_set, pi_congr rfl fun i hi => (hs i hi).closure_eq,
    pi_univ]

theorem DenseRange.piMap {Î¹ : Type*} {X Y : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (Y i)]
    {f : (i : Î¹) â†’ (X i) â†’ (Y i)} (hf : âˆ€ i, DenseRange (f i)) :
    DenseRange (Pi.map f) := by
  rw [DenseRange, Set.range_piMap]
  exact dense_pi Set.univ (fun i _ => hf i)

theorem eventuallyEq_nhdsWithin_iff {f g : Î± â†’ Î²} {s : Set Î±} {a : Î±} :
    f =á¶ [ğ“[s] a] g â†” âˆ€á¶  x in ğ“ a, x âˆˆ s â†’ f x = g x :=
  mem_inf_principal

/-- Two functions agree on a neighborhood of `x` if they agree at `x` and in a punctured
neighborhood. -/
theorem eventuallyEq_nhds_of_eventuallyEq_nhdsNE {f g : Î± â†’ Î²} {a : Î±} (hâ‚ : f =á¶ [ğ“[â‰ ] a] g)
    (hâ‚‚ : f a = g a) :
    f =á¶ [ğ“ a] g := by
  filter_upwards [eventually_nhdsWithin_iff.1 hâ‚]
  intro x hx
  by_cases hâ‚‚x : x = a
  Â· simp [hâ‚‚x, hâ‚‚]
  Â· tauto

theorem eventuallyEq_nhdsWithin_of_eqOn {f g : Î± â†’ Î²} {s : Set Î±} {a : Î±} (h : EqOn f g s) :
    f =á¶ [ğ“[s] a] g :=
  mem_inf_of_right h

theorem Set.EqOn.eventuallyEq_nhdsWithin {f g : Î± â†’ Î²} {s : Set Î±} {a : Î±} (h : EqOn f g s) :
    f =á¶ [ğ“[s] a] g :=
  eventuallyEq_nhdsWithin_of_eqOn h

theorem tendsto_nhdsWithin_congr {f g : Î± â†’ Î²} {s : Set Î±} {a : Î±} {l : Filter Î²}
    (hfg : âˆ€ x âˆˆ s, f x = g x) (hf : Tendsto f (ğ“[s] a) l) : Tendsto g (ğ“[s] a) l :=
  (tendsto_congr' <| eventuallyEq_nhdsWithin_of_eqOn hfg).1 hf

theorem eventually_nhdsWithin_of_forall {s : Set Î±} {a : Î±} {p : Î± â†’ Prop} (h : âˆ€ x âˆˆ s, p x) :
    âˆ€á¶  x in ğ“[s] a, p x :=
  mem_inf_of_right h

theorem tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within {a : Î±} {l : Filter Î²} {s : Set Î±}
    (f : Î² â†’ Î±) (h1 : Tendsto f l (ğ“ a)) (h2 : âˆ€á¶  x in l, f x âˆˆ s) : Tendsto f l (ğ“[s] a) :=
  tendsto_inf.2 âŸ¨h1, tendsto_principal.2 h2âŸ©

theorem tendsto_nhdsWithin_iff {a : Î±} {l : Filter Î²} {s : Set Î±} {f : Î² â†’ Î±} :
    Tendsto f l (ğ“[s] a) â†” Tendsto f l (ğ“ a) âˆ§ âˆ€á¶  n in l, f n âˆˆ s :=
  âŸ¨fun h => âŸ¨tendsto_nhds_of_tendsto_nhdsWithin h, eventually_mem_of_tendsto_nhdsWithin hâŸ©, fun h =>
    tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _ h.1 h.2âŸ©

@[simp]
theorem tendsto_nhdsWithin_range {a : Î±} {l : Filter Î²} {f : Î² â†’ Î±} :
    Tendsto f l (ğ“[range f] a) â†” Tendsto f l (ğ“ a) :=
  âŸ¨fun h => h.mono_right inf_le_left, fun h =>
    tendsto_inf.2 âŸ¨h, tendsto_principal.2 <| Eventually.of_forall mem_range_selfâŸ©âŸ©

theorem Filter.EventuallyEq.eq_of_nhdsWithin {s : Set Î±} {f g : Î± â†’ Î²} {a : Î±} (h : f =á¶ [ğ“[s] a] g)
    (hmem : a âˆˆ s) : f a = g a :=
  h.self_of_nhdsWithin hmem

theorem eventually_nhdsWithin_of_eventually_nhds {s : Set Î±}
    {a : Î±} {p : Î± â†’ Prop} (h : âˆ€á¶  x in ğ“ a, p x) : âˆ€á¶  x in ğ“[s] a, p x :=
  mem_nhdsWithin_of_mem_nhds h

lemma Set.MapsTo.preimage_mem_nhdsWithin {f : Î± â†’ Î²} {s : Set Î±} {t : Set Î²} {x : Î±}
    (hst : MapsTo f s t) : f â»Â¹' t âˆˆ ğ“[s] x :=
  Filter.mem_of_superset self_mem_nhdsWithin hst

/-!
### `nhdsWithin` and subtypes
-/

theorem mem_nhdsWithin_subtype {s : Set Î±} {a : { x // x âˆˆ s }} {t u : Set { x // x âˆˆ s }} :
    t âˆˆ ğ“[u] a â†” t âˆˆ comap ((â†‘) : s â†’ Î±) (ğ“[(â†‘) '' u] a) := by
  rw [nhdsWithin, nhds_subtype, principal_subtype, â† comap_inf, â† nhdsWithin]

theorem nhdsWithin_subtype (s : Set Î±) (a : { x // x âˆˆ s }) (t : Set { x // x âˆˆ s }) :
    ğ“[t] a = comap ((â†‘) : s â†’ Î±) (ğ“[(â†‘) '' t] a) :=
  Filter.ext fun _ => mem_nhdsWithin_subtype

theorem nhdsWithin_eq_map_subtype_coe {s : Set Î±} {a : Î±} (h : a âˆˆ s) :
    ğ“[s] a = map ((â†‘) : s â†’ Î±) (ğ“ âŸ¨a, hâŸ©) :=
  (map_nhds_subtype_val âŸ¨a, hâŸ©).symm

theorem mem_nhds_subtype_iff_nhdsWithin {s : Set Î±} {a : s} {t : Set s} :
    t âˆˆ ğ“ a â†” (â†‘) '' t âˆˆ ğ“[s] (a : Î±) := by
  rw [â† map_nhds_subtype_val, image_mem_map_iff Subtype.val_injective]

theorem preimage_coe_mem_nhds_subtype {s t : Set Î±} {a : s} : (â†‘) â»Â¹' t âˆˆ ğ“ a â†” t âˆˆ ğ“[s] â†‘a := by
  rw [â† map_nhds_subtype_val, mem_map]

theorem eventually_nhds_subtype_iff (s : Set Î±) (a : s) (P : Î± â†’ Prop) :
    (âˆ€á¶  x : s in ğ“ a, P x) â†” âˆ€á¶  x in ğ“[s] a, P x :=
  preimage_coe_mem_nhds_subtype

theorem frequently_nhds_subtype_iff (s : Set Î±) (a : s) (P : Î± â†’ Prop) :
    (âˆƒá¶  x : s in ğ“ a, P x) â†” âˆƒá¶  x in ğ“[s] a, P x :=
  eventually_nhds_subtype_iff s a (Â¬ P Â·) |>.not

theorem tendsto_nhdsWithin_iff_subtype {s : Set Î±} {a : Î±} (h : a âˆˆ s) (f : Î± â†’ Î²) (l : Filter Î²) :
    Tendsto f (ğ“[s] a) l â†” Tendsto (s.restrict f) (ğ“ âŸ¨a, hâŸ©) l := by
  rw [nhdsWithin_eq_map_subtype_coe h, tendsto_map'_iff]; rfl
