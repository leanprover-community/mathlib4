/-
Copyright (c) 2021 Thomas Browning. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Thomas Browning
-/
import Mathlib.Topology.PartialHomeomorph
import Mathlib.Topology.SeparatedMap

#align_import topology.is_locally_homeomorph from "leanprover-community/mathlib"@"e97cf15cd1aec9bd5c193b2ffac5a6dc9118912b"

/-!
# Local homeomorphisms

This file defines local homeomorphisms.

## Main definitions

For a function `f : X ‚Üí Y ` between topological spaces, we say
* `IsLocalHomeomorphOn f s` if `f` is a local homeomorphism around each point of `s`: for each
  `x : X`, the restriction of `f` to some open neighborhood `U` of `x` gives a homeomorphism
  between `U` and an open subset of `Y`.
* `IsLocalHomeomorph f`: `f` is a local homeomorphism, i.e. it's a local homeomorphism on `univ`.

Note that `IsLocalHomeomorph` is a global condition. This is in contrast to
`PartialHomeomorph`, which is a homeomorphism between specific open subsets.

## Main results
* local homeomorphisms are locally injective open maps
* more!

-/


open Topology

variable {X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z] (g : Y ‚Üí Z)
  (f : X ‚Üí Y) (s : Set X) (t : Set Y)

/-- A function `f : X ‚Üí Y` satisfies `IsLocalHomeomorphOn f s` if each `x ‚àà s` is contained in
the source of some `e : PartialHomeomorph X Y` with `f = e`. -/
def IsLocalHomeomorphOn :=
  ‚àÄ x ‚àà s, ‚àÉ e : PartialHomeomorph X Y, x ‚àà e.source ‚àß f = e
#align is_locally_homeomorph_on IsLocalHomeomorphOn

theorem isLocalHomeomorphOn_iff_openEmbedding_restrict {f : X ‚Üí Y} :
    IsLocalHomeomorphOn f s ‚Üî ‚àÄ x ‚àà s, ‚àÉ U ‚àà ùìù x, OpenEmbedding (U.restrict f) := by
  refine ‚ü®fun h x hx ‚Ü¶ ?_, fun h x hx ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®e, hxe, rfl‚ü© := h x hx
    exact ‚ü®e.source, e.open_source.mem_nhds hxe, e.openEmbedding_restrict‚ü©
  ¬∑ obtain ‚ü®U, hU, emb‚ü© := h x hx
    have : OpenEmbedding ((interior U).restrict f) := by
      refine emb.comp ‚ü®embedding_inclusion interior_subset, ?_‚ü©
      rw [Set.range_inclusion]; exact isOpen_induced isOpen_interior
    obtain ‚ü®cont, inj, openMap‚ü© := openEmbedding_iff_continuous_injective_open.mp this
    haveI : Nonempty X := ‚ü®x‚ü©
    exact ‚ü®PartialHomeomorph.ofContinuousOpenRestrict
      (Set.injOn_iff_injective.mpr inj).toPartialEquiv
      (continuousOn_iff_continuous_restrict.mpr cont) openMap isOpen_interior,
      mem_interior_iff_mem_nhds.mpr hU, rfl‚ü©

namespace IsLocalHomeomorphOn

/-- Proves that `f` satisfies `IsLocalHomeomorphOn f s`. The condition `h` is weaker than the
definition of `IsLocalHomeomorphOn f s`, since it only requires `e : PartialHomeomorph X Y` to
agree with `f` on its source `e.source`, as opposed to on the whole space `X`. -/
theorem mk (h : ‚àÄ x ‚àà s, ‚àÉ e : PartialHomeomorph X Y, x ‚àà e.source ‚àß Set.EqOn f e e.source) :
    IsLocalHomeomorphOn f s := by
  intro x hx
  obtain ‚ü®e, hx, he‚ü© := h x hx
  exact
    ‚ü®{ e with
        toFun := f
        map_source' := fun _x hx ‚Ü¶ by rw [he hx]; exact e.map_source' hx
        left_inv' := fun _x hx ‚Ü¶ by rw [he hx]; exact e.left_inv' hx
        right_inv' := fun _y hy ‚Ü¶ by rw [he (e.map_target' hy)]; exact e.right_inv' hy
        continuousOn_toFun := (continuousOn_congr he).mpr e.continuousOn_toFun },
      hx, rfl‚ü©
#align is_locally_homeomorph_on.mk IsLocalHomeomorphOn.mk

/-- A `PartialHomeomorph` is a local homeomorphism on its source. -/
lemma PartialHomeomorph.isLocalHomeomorphOn (e : PartialHomeomorph X Y) :
    IsLocalHomeomorphOn e e.source :=
  fun _ hx ‚Ü¶ ‚ü®e, hx, rfl‚ü©

variable {g f s t}

theorem mono {t : Set X} (hf : IsLocalHomeomorphOn f t) (hst : s ‚äÜ t) : IsLocalHomeomorphOn f s :=
  fun x hx ‚Ü¶ hf x (hst hx)

theorem of_comp_left (hgf : IsLocalHomeomorphOn (g ‚àò f) s) (hg : IsLocalHomeomorphOn g (f '' s))
    (cont : ‚àÄ x ‚àà s, ContinuousAt f x) : IsLocalHomeomorphOn f s := mk f s fun x hx ‚Ü¶ by
  obtain ‚ü®g, hxg, rfl‚ü© := hg (f x) ‚ü®x, hx, rfl‚ü©
  obtain ‚ü®gf, hgf, he‚ü© := hgf x hx
  refine ‚ü®(gf.restr <| f ‚Åª¬π' g.source).trans g.symm, ‚ü®‚ü®hgf, mem_interior_iff_mem_nhds.mpr
    ((cont x hx).preimage_mem_nhds <| g.open_source.mem_nhds hxg)‚ü©, he ‚ñ∏ g.map_source hxg‚ü©,
    fun y hy ‚Ü¶ ?_‚ü©
  change f y = g.symm (gf y)
  have : f y ‚àà g.source := by apply interior_subset hy.1.2
  rw [‚Üê he, g.eq_symm_apply this (by apply g.map_source this), Function.comp_apply]

theorem of_comp_right (hgf : IsLocalHomeomorphOn (g ‚àò f) s) (hf : IsLocalHomeomorphOn f s) :
    IsLocalHomeomorphOn g (f '' s) := mk g _ <| by
  rintro _ ‚ü®x, hx, rfl‚ü©
  obtain ‚ü®f, hxf, rfl‚ü© := hf x hx
  obtain ‚ü®gf, hgf, he‚ü© := hgf x hx
  refine ‚ü®f.symm.trans gf, ‚ü®f.map_source hxf, ?_‚ü©, fun y hy ‚Ü¶ ?_‚ü©
  ¬∑ apply (f.left_inv hxf).symm ‚ñ∏ hgf
  ¬∑ change g y = gf (f.symm y)
    rw [‚Üê he, Function.comp_apply, f.right_inv hy.1]

theorem map_nhds_eq (hf : IsLocalHomeomorphOn f s) {x : X} (hx : x ‚àà s) : (ùìù x).map f = ùìù (f x) :=
  let ‚ü®e, hx, he‚ü© := hf x hx
  he.symm ‚ñ∏ e.map_nhds_eq hx
#align is_locally_homeomorph_on.map_nhds_eq IsLocalHomeomorphOn.map_nhds_eq

protected theorem continuousAt (hf : IsLocalHomeomorphOn f s) {x : X} (hx : x ‚àà s) :
    ContinuousAt f x :=
  (hf.map_nhds_eq hx).le
#align is_locally_homeomorph_on.continuous_at IsLocalHomeomorphOn.continuousAt

protected theorem continuousOn (hf : IsLocalHomeomorphOn f s) : ContinuousOn f s :=
  ContinuousAt.continuousOn fun _x ‚Ü¶ hf.continuousAt
#align is_locally_homeomorph_on.continuous_on IsLocalHomeomorphOn.continuousOn

protected theorem comp (hg : IsLocalHomeomorphOn g t) (hf : IsLocalHomeomorphOn f s)
    (h : Set.MapsTo f s t) : IsLocalHomeomorphOn (g ‚àò f) s := by
  intro x hx
  obtain ‚ü®eg, hxg, rfl‚ü© := hg (f x) (h hx)
  obtain ‚ü®ef, hxf, rfl‚ü© := hf x hx
  exact ‚ü®ef.trans eg, ‚ü®hxf, hxg‚ü©, rfl‚ü©
#align is_locally_homeomorph_on.comp IsLocalHomeomorphOn.comp

end IsLocalHomeomorphOn

/-- A function `f : X ‚Üí Y` satisfies `IsLocalHomeomorph f` if each `x : x` is contained in
  the source of some `e : PartialHomeomorph X Y` with `f = e`. -/
def IsLocalHomeomorph :=
  ‚àÄ x : X, ‚àÉ e : PartialHomeomorph X Y, x ‚àà e.source ‚àß f = e
#align is_locally_homeomorph IsLocalHomeomorph

theorem Homeomorph.isLocalHomeomorph (f : X ‚âÉ‚Çú Y) : IsLocalHomeomorph f :=
  fun _ ‚Ü¶ ‚ü®f.toPartialHomeomorph, trivial, rfl‚ü©

variable {f s}

theorem isLocalHomeomorph_iff_isLocalHomeomorphOn_univ :
    IsLocalHomeomorph f ‚Üî IsLocalHomeomorphOn f Set.univ :=
  ‚ü®fun h x _ ‚Ü¶ h x, fun h x ‚Ü¶ h x trivial‚ü©
#align is_locally_homeomorph_iff_is_locally_homeomorph_on_univ isLocalHomeomorph_iff_isLocalHomeomorphOn_univ

protected theorem IsLocalHomeomorph.isLocalHomeomorphOn (hf : IsLocalHomeomorph f) :
    IsLocalHomeomorphOn f s := fun x _ ‚Ü¶ hf x
#align is_locally_homeomorph.is_locally_homeomorph_on IsLocalHomeomorph.isLocalHomeomorphOn

theorem isLocalHomeomorph_iff_openEmbedding_restrict {f : X ‚Üí Y} :
    IsLocalHomeomorph f ‚Üî ‚àÄ x : X, ‚àÉ U ‚àà ùìù x, OpenEmbedding (U.restrict f) := by
  simp_rw [isLocalHomeomorph_iff_isLocalHomeomorphOn_univ,
    isLocalHomeomorphOn_iff_openEmbedding_restrict, imp_iff_right (Set.mem_univ _)]

theorem OpenEmbedding.isLocalHomeomorph (hf : OpenEmbedding f) : IsLocalHomeomorph f :=
  isLocalHomeomorph_iff_openEmbedding_restrict.mpr fun _ ‚Ü¶
    ‚ü®_, Filter.univ_mem, hf.comp (Homeomorph.Set.univ X).openEmbedding‚ü©

variable (f)

namespace IsLocalHomeomorph

/-- Proves that `f` satisfies `IsLocalHomeomorph f`. The condition `h` is weaker than the
definition of `IsLocalHomeomorph f`, since it only requires `e : PartialHomeomorph X Y` to
agree with `f` on its source `e.source`, as opposed to on the whole space `X`. -/
theorem mk (h : ‚àÄ x : X, ‚àÉ e : PartialHomeomorph X Y, x ‚àà e.source ‚àß Set.EqOn f e e.source) :
    IsLocalHomeomorph f :=
  isLocalHomeomorph_iff_isLocalHomeomorphOn_univ.mpr
    (IsLocalHomeomorphOn.mk f Set.univ fun x _hx ‚Ü¶ h x)
#align is_locally_homeomorph.mk IsLocalHomeomorph.mk

/-- A homeomorphism is a local homeomorphism. -/
lemma Homeomorph.isLocalHomeomorph (h : X ‚âÉ‚Çú Y) : IsLocalHomeomorph h :=
  fun _ ‚Ü¶ ‚ü®h.toPartialHomeomorph, trivial, rfl‚ü©

variable {g f}

lemma isLocallyInjective (hf : IsLocalHomeomorph f) : IsLocallyInjective f :=
  fun x ‚Ü¶ by obtain ‚ü®f, hx, rfl‚ü© := hf x; exact ‚ü®f.source, f.open_source, hx, f.injOn‚ü©

theorem of_comp (hgf : IsLocalHomeomorph (g ‚àò f)) (hg : IsLocalHomeomorph g)
    (cont : Continuous f) : IsLocalHomeomorph f :=
  isLocalHomeomorph_iff_isLocalHomeomorphOn_univ.mpr <|
    hgf.isLocalHomeomorphOn.of_comp_left hg.isLocalHomeomorphOn fun _ _ ‚Ü¶ cont.continuousAt

theorem map_nhds_eq (hf : IsLocalHomeomorph f) (x : X) : (ùìù x).map f = ùìù (f x) :=
  hf.isLocalHomeomorphOn.map_nhds_eq (Set.mem_univ x)
#align is_locally_homeomorph.map_nhds_eq IsLocalHomeomorph.map_nhds_eq

/-- A local homeomorphism is continuous. -/
protected theorem continuous (hf : IsLocalHomeomorph f) : Continuous f :=
  continuous_iff_continuousOn_univ.mpr hf.isLocalHomeomorphOn.continuousOn
#align is_locally_homeomorph.continuous IsLocalHomeomorph.continuous

/-- A local homeomorphism is an open map. -/
protected theorem isOpenMap (hf : IsLocalHomeomorph f) : IsOpenMap f :=
  IsOpenMap.of_nhds_le fun x ‚Ü¶ ge_of_eq (hf.map_nhds_eq x)
#align is_locally_homeomorph.is_open_map IsLocalHomeomorph.isOpenMap

/-- The composition of local homeomorphisms is a local homeomorphism. -/
protected theorem comp (hg : IsLocalHomeomorph g) (hf : IsLocalHomeomorph f) :
    IsLocalHomeomorph (g ‚àò f) :=
  isLocalHomeomorph_iff_isLocalHomeomorphOn_univ.mpr
    (hg.isLocalHomeomorphOn.comp hf.isLocalHomeomorphOn (Set.univ.mapsTo_univ f))
#align is_locally_homeomorph.comp IsLocalHomeomorph.comp

/-- An injective local homeomorphism is an open embedding. -/
theorem openEmbedding_of_injective (hf : IsLocalHomeomorph f) (hi : f.Injective) :
    OpenEmbedding f :=
  openEmbedding_of_continuous_injective_open hf.continuous hi hf.isOpenMap

/-- A surjective embedding is a homeomorphism. -/
noncomputable def _root_.Embedding.toHomeomeomorph_of_surjective (hf : Embedding f)
    (hsurj : Function.Surjective f) : X ‚âÉ‚Çú Y :=
  Homeomorph.homeomorphOfContinuousOpen (Equiv.ofBijective f ‚ü®hf.inj, hsurj‚ü©)
    hf.continuous (hf.toOpenEmbedding_of_surjective hsurj).isOpenMap

/-- A bijective local homeomorphism is a homeomorphism. -/
noncomputable def toHomeomorph_of_bijective (hf : IsLocalHomeomorph f) (hb : f.Bijective) :
    X ‚âÉ‚Çú Y :=
  Homeomorph.homeomorphOfContinuousOpen (Equiv.ofBijective f hb) hf.continuous hf.isOpenMap

/-- Continuous local sections of a local homeomorphism are open embeddings. -/
theorem openEmbedding_of_comp (hf : IsLocalHomeomorph g) (hgf : OpenEmbedding (g ‚àò f))
    (cont : Continuous f) : OpenEmbedding f :=
  (hgf.isLocalHomeomorph.of_comp hf cont).openEmbedding_of_injective hgf.inj.of_comp

open TopologicalSpace in
/-- Ranges of continuous local sections of a local homeomorphism
form a basis of the source space. -/
theorem isTopologicalBasis (hf : IsLocalHomeomorph f) : IsTopologicalBasis
    {U : Set X | ‚àÉ V : Set Y, IsOpen V ‚àß ‚àÉ s : C(V,X), f ‚àò s = (‚Üë) ‚àß Set.range s = U} := by
  refine isTopologicalBasis_of_isOpen_of_nhds ?_ fun x U hx hU ‚Ü¶ ?_
  ¬∑ rintro _ ‚ü®U, hU, s, hs, rfl‚ü©
    refine (openEmbedding_of_comp hf (hs ‚ñ∏ ‚ü®embedding_subtype_val, ?_‚ü©) s.continuous).isOpen_range
    rwa [Subtype.range_val]
  ¬∑ obtain ‚ü®f, hxf, rfl‚ü© := hf x
    refine ‚ü®f.source ‚à© U, ‚ü®f.target ‚à© f.symm ‚Åª¬π' U, f.symm.isOpen_inter_preimage hU,
      ‚ü®_, continuousOn_iff_continuous_restrict.mp (f.continuousOn_invFun.mono fun _ h ‚Ü¶ h.1)‚ü©,
      ?_, (Set.range_restrict _ _).trans ?_‚ü©, ‚ü®hxf, hx‚ü©, fun _ h ‚Ü¶ h.2‚ü©
    ¬∑ ext y; exact f.right_inv y.2.1
    ¬∑ apply (f.symm_image_target_inter_eq _).trans
      rw [Set.preimage_inter, ‚Üê Set.inter_assoc, Set.inter_eq_self_of_subset_left
        f.source_preimage_target, f.source_inter_preimage_inv_preimage]

end IsLocalHomeomorph
