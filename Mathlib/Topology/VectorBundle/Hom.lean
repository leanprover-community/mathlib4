/-
Copyright (c) 2022 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth, Floris van Doorn
-/
import Mathlib.Topology.VectorBundle.Basic

/-!
# The vector bundle of continuous (semi)linear maps

We define the (topological) vector bundle of continuous (semi)linear maps between two vector bundles
over the same base.

Given bundles `Eâ‚ Eâ‚‚ : B â†’ Type*`, normed spaces `Fâ‚` and `Fâ‚‚`, and a ring-homomorphism `Ïƒ` between
their respective scalar fields, we define a vector bundle with fiber `Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x`.
If the `Eâ‚` and `Eâ‚‚` are vector bundles with model fibers `Fâ‚` and `Fâ‚‚`, then this will be a
vector bundle with fiber `Fâ‚ â†’SL[Ïƒ] Fâ‚‚`.

The topology on the total space is constructed from the trivializations for `Eâ‚` and `Eâ‚‚` and the
norm-topology on the model fiber `Fâ‚ â†’SL[ğ•œ] Fâ‚‚` using the `VectorPrebundle` construction.  This is
a bit awkward because it introduces a dependence on the normed space structure of the model fibers,
rather than just their topological vector space structure; it is not clear whether this is
necessary.

Similar constructions should be possible (but are yet to be formalized) for tensor products of
topological vector bundles, exterior algebras, and so on, where again the topology can be defined
using a norm on the fiber model if this helps.
-/


noncomputable section

open Bundle Set ContinuousLinearMap Topology
open scoped Bundle

variable {ğ•œâ‚ : Type*} [NontriviallyNormedField ğ•œâ‚] {ğ•œâ‚‚ : Type*} [NontriviallyNormedField ğ•œâ‚‚]
  (Ïƒ : ğ•œâ‚ â†’+* ğ•œâ‚‚)

variable {B : Type*}
variable {Fâ‚ : Type*} [NormedAddCommGroup Fâ‚] [NormedSpace ğ•œâ‚ Fâ‚] (Eâ‚ : B â†’ Type*)
  [âˆ€ x, AddCommGroup (Eâ‚ x)] [âˆ€ x, Module ğ•œâ‚ (Eâ‚ x)] [TopologicalSpace (TotalSpace Fâ‚ Eâ‚)]

variable {Fâ‚‚ : Type*} [NormedAddCommGroup Fâ‚‚] [NormedSpace ğ•œâ‚‚ Fâ‚‚] (Eâ‚‚ : B â†’ Type*)
  [âˆ€ x, AddCommGroup (Eâ‚‚ x)] [âˆ€ x, Module ğ•œâ‚‚ (Eâ‚‚ x)] [TopologicalSpace (TotalSpace Fâ‚‚ Eâ‚‚)]

/-- A reducible type synonym for the bundle of continuous (semi)linear maps. -/
@[deprecated "Use the plain bundle syntax `fun (b : B) â†¦ Eâ‚ b â†’SL[Ïƒ] Eâ‚‚ b` or
`fun (b : B) â†¦ Eâ‚ b â†’L[ğ•œ] Eâ‚‚ b` instead" (since := "2025-06-12")]
protected abbrev Bundle.ContinuousLinearMap [âˆ€ x, TopologicalSpace (Eâ‚ x)]
    [âˆ€ x, TopologicalSpace (Eâ‚‚ x)] : B â†’ Type _ := fun x â†¦ Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x

variable {Eâ‚ Eâ‚‚}
variable [TopologicalSpace B] (eâ‚ eâ‚' : Trivialization Fâ‚ (Ï€ Fâ‚ Eâ‚))
  (eâ‚‚ eâ‚‚' : Trivialization Fâ‚‚ (Ï€ Fâ‚‚ Eâ‚‚))

namespace Pretrivialization

/-- Assume `eáµ¢` and `eáµ¢'` are trivializations of the bundles `Eáµ¢` over base `B` with fiber `Fáµ¢`
(`i âˆˆ {1,2}`), then `Pretrivialization.continuousLinearMapCoordChange Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚'` is the
coordinate change function between the two induced (pre)trivializations
`Pretrivialization.continuousLinearMap Ïƒ eâ‚ eâ‚‚` and
`Pretrivialization.continuousLinearMap Ïƒ eâ‚' eâ‚‚'` of the bundle of continuous linear maps. -/
def continuousLinearMapCoordChange [eâ‚.IsLinear ğ•œâ‚] [eâ‚'.IsLinear ğ•œâ‚] [eâ‚‚.IsLinear ğ•œâ‚‚]
    [eâ‚‚'.IsLinear ğ•œâ‚‚] (b : B) : (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) â†’L[ğ•œâ‚‚] Fâ‚ â†’SL[Ïƒ] Fâ‚‚ :=
  ((eâ‚'.coordChangeL ğ•œâ‚ eâ‚ b).symm.arrowCongrSL (eâ‚‚.coordChangeL ğ•œâ‚‚ eâ‚‚' b) :
    (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) â‰ƒL[ğ•œâ‚‚] Fâ‚ â†’SL[Ïƒ] Fâ‚‚)

variable {Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚'}
variable [âˆ€ x, TopologicalSpace (Eâ‚ x)] [FiberBundle Fâ‚ Eâ‚]
variable [âˆ€ x, TopologicalSpace (Eâ‚‚ x)] [FiberBundle Fâ‚‚ Eâ‚‚]

theorem continuousOn_continuousLinearMapCoordChange [RingHomIsometric Ïƒ]
    [VectorBundle ğ•œâ‚ Fâ‚ Eâ‚] [VectorBundle ğ•œâ‚‚ Fâ‚‚ Eâ‚‚]
    [MemTrivializationAtlas eâ‚] [MemTrivializationAtlas eâ‚'] [MemTrivializationAtlas eâ‚‚]
    [MemTrivializationAtlas eâ‚‚'] :
    ContinuousOn (continuousLinearMapCoordChange Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚')
      (eâ‚.baseSet âˆ© eâ‚‚.baseSet âˆ© (eâ‚'.baseSet âˆ© eâ‚‚'.baseSet)) := by
  have hâ‚ := (compSL Fâ‚ Fâ‚‚ Fâ‚‚ Ïƒ (RingHom.id ğ•œâ‚‚)).continuous
  have hâ‚‚ := (ContinuousLinearMap.flip (compSL Fâ‚ Fâ‚ Fâ‚‚ (RingHom.id ğ•œâ‚) Ïƒ)).continuous
  have hâ‚ƒ := continuousOn_coordChange ğ•œâ‚ eâ‚' eâ‚
  have hâ‚„ := continuousOn_coordChange ğ•œâ‚‚ eâ‚‚ eâ‚‚'
  refine ((hâ‚.comp_continuousOn (hâ‚„.mono ?_)).clm_comp (hâ‚‚.comp_continuousOn (hâ‚ƒ.mono ?_))).congr ?_
  Â· mfld_set_tac
  Â· mfld_set_tac
  Â· intro b _
    ext L v
    dsimp [continuousLinearMapCoordChange]

variable (Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚')
variable [eâ‚.IsLinear ğ•œâ‚] [eâ‚'.IsLinear ğ•œâ‚] [eâ‚‚.IsLinear ğ•œâ‚‚] [eâ‚‚'.IsLinear ğ•œâ‚‚]

/-- Given trivializations `eâ‚`, `eâ‚‚` for vector bundles `Eâ‚`, `Eâ‚‚` over a base `B`,
`Pretrivialization.continuousLinearMap Ïƒ eâ‚ eâ‚‚` is the induced pretrivialization for the
continuous `Ïƒ`-semilinear maps from `Eâ‚` to `Eâ‚‚`. That is, the map which will later become a
trivialization, after the bundle of continuous semilinear maps is equipped with the right
topological vector bundle structure. -/
def continuousLinearMap :
    Pretrivialization (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (Ï€ (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (fun x â†¦ Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x)) where
  toFun p := âŸ¨p.1, .comp (eâ‚‚.continuousLinearMapAt ğ•œâ‚‚ p.1) (p.2.comp (eâ‚.symmL ğ•œâ‚ p.1))âŸ©
  invFun p := âŸ¨p.1, .comp (eâ‚‚.symmL ğ•œâ‚‚ p.1) (p.2.comp (eâ‚.continuousLinearMapAt ğ•œâ‚ p.1))âŸ©
  source := Bundle.TotalSpace.proj â»Â¹' (eâ‚.baseSet âˆ© eâ‚‚.baseSet)
  target := (eâ‚.baseSet âˆ© eâ‚‚.baseSet) Ã—Ë¢ Set.univ
  map_source' := fun âŸ¨_, _âŸ© h â†¦ âŸ¨h, Set.mem_univ _âŸ©
  map_target' := fun âŸ¨_, _âŸ© h â†¦ h.1
  left_inv' := fun âŸ¨x, LâŸ© âŸ¨hâ‚, hâ‚‚âŸ© â†¦ by
    simp only [TotalSpace.mk_inj]
    ext (v : Eâ‚ x)
    dsimp only [comp_apply]
    rw [Trivialization.symmL_continuousLinearMapAt, Trivialization.symmL_continuousLinearMapAt]
    exacts [hâ‚, hâ‚‚]
  right_inv' := fun âŸ¨x, fâŸ© âŸ¨âŸ¨hâ‚, hâ‚‚âŸ©, _âŸ© â†¦ by
    simp only [Prod.mk_right_inj]
    ext v
    dsimp only [comp_apply]
    rw [Trivialization.continuousLinearMapAt_symmL, Trivialization.continuousLinearMapAt_symmL]
    exacts [hâ‚, hâ‚‚]
  open_target := (eâ‚.open_baseSet.inter eâ‚‚.open_baseSet).prod isOpen_univ
  baseSet := eâ‚.baseSet âˆ© eâ‚‚.baseSet
  open_baseSet := eâ‚.open_baseSet.inter eâ‚‚.open_baseSet
  source_eq := rfl
  target_eq := rfl
  proj_toFun _ _ := rfl

-- Porting note (https://github.com/leanprover-community/mathlib4/issues/11215):
-- TODO: see if Lean 4 can generate this instance without a hint
instance continuousLinearMap.isLinear [âˆ€ x, ContinuousAdd (Eâ‚‚ x)] [âˆ€ x, ContinuousSMul ğ•œâ‚‚ (Eâ‚‚ x)] :
    (Pretrivialization.continuousLinearMap Ïƒ eâ‚ eâ‚‚).IsLinear ğ•œâ‚‚ where
  linear x _ :=
    { map_add := fun L L' â†¦
        show (eâ‚‚.continuousLinearMapAt ğ•œâ‚‚ x).comp ((L + L').comp (eâ‚.symmL ğ•œâ‚ x)) = _ by
          simp_rw [add_comp, comp_add]
          rfl
      map_smul := fun c L â†¦
        show (eâ‚‚.continuousLinearMapAt ğ•œâ‚‚ x).comp ((c â€¢ L).comp (eâ‚.symmL ğ•œâ‚ x)) = _ by
          simp_rw [smul_comp, comp_smulâ‚›â‚—, RingHom.id_apply]
          rfl }

theorem continuousLinearMap_apply (p : TotalSpace (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) fun x â†¦ Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x) :
    (continuousLinearMap Ïƒ eâ‚ eâ‚‚) p =
      âŸ¨p.1, .comp (eâ‚‚.continuousLinearMapAt ğ•œâ‚‚ p.1) (p.2.comp (eâ‚.symmL ğ•œâ‚ p.1))âŸ© :=
  rfl

theorem continuousLinearMap_symm_apply (p : B Ã— (Fâ‚ â†’SL[Ïƒ] Fâ‚‚)) :
    (continuousLinearMap Ïƒ eâ‚ eâ‚‚).toPartialEquiv.symm p =
      âŸ¨p.1, .comp (eâ‚‚.symmL ğ•œâ‚‚ p.1) (p.2.comp (eâ‚.continuousLinearMapAt ğ•œâ‚ p.1))âŸ© :=
  rfl

theorem continuousLinearMap_symm_apply' {b : B} (hb : b âˆˆ eâ‚.baseSet âˆ© eâ‚‚.baseSet)
    (L : Fâ‚ â†’SL[Ïƒ] Fâ‚‚) :
    (continuousLinearMap Ïƒ eâ‚ eâ‚‚).symm b L =
      (eâ‚‚.symmL ğ•œâ‚‚ b).comp (L.comp <| eâ‚.continuousLinearMapAt ğ•œâ‚ b) := by
  rw [symm_apply]
  Â· rfl
  Â· exact hb

theorem continuousLinearMapCoordChange_apply (b : B)
    (hb : b âˆˆ eâ‚.baseSet âˆ© eâ‚‚.baseSet âˆ© (eâ‚'.baseSet âˆ© eâ‚‚'.baseSet)) (L : Fâ‚ â†’SL[Ïƒ] Fâ‚‚) :
    continuousLinearMapCoordChange Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚' b L =
      (continuousLinearMap Ïƒ eâ‚' eâ‚‚' âŸ¨b, (continuousLinearMap Ïƒ eâ‚ eâ‚‚).symm b LâŸ©).2 := by
  ext v
  simp_rw [continuousLinearMapCoordChange, ContinuousLinearEquiv.coe_coe,
    ContinuousLinearEquiv.arrowCongrSL_apply, continuousLinearMap_apply,
    continuousLinearMap_symm_apply' Ïƒ eâ‚ eâ‚‚ hb.1, comp_apply, ContinuousLinearEquiv.coe_coe,
    ContinuousLinearEquiv.symm_symm, Trivialization.continuousLinearMapAt_apply,
    Trivialization.symmL_apply]
  rw [eâ‚‚.coordChangeL_apply eâ‚‚', eâ‚'.coordChangeL_apply eâ‚, eâ‚.coe_linearMapAt_of_mem hb.1.1,
    eâ‚‚'.coe_linearMapAt_of_mem hb.2.2]
  exacts [âŸ¨hb.2.1, hb.1.1âŸ©, âŸ¨hb.1.2, hb.2.2âŸ©]

end Pretrivialization

open Pretrivialization

variable (Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚)
variable [âˆ€ x : B, TopologicalSpace (Eâ‚ x)] [FiberBundle Fâ‚ Eâ‚] [VectorBundle ğ•œâ‚ Fâ‚ Eâ‚]
variable [âˆ€ x : B, TopologicalSpace (Eâ‚‚ x)] [FiberBundle Fâ‚‚ Eâ‚‚] [VectorBundle ğ•œâ‚‚ Fâ‚‚ Eâ‚‚]
variable [âˆ€ x, IsTopologicalAddGroup (Eâ‚‚ x)] [âˆ€ x, ContinuousSMul ğ•œâ‚‚ (Eâ‚‚ x)]
variable [RingHomIsometric Ïƒ]

/-- The continuous `Ïƒ`-semilinear maps between two topological vector bundles form a
`VectorPrebundle` (this is an auxiliary construction for the
`VectorBundle` instance, in which the pretrivializations are collated but no topology
on the total space is yet provided). -/
def Bundle.ContinuousLinearMap.vectorPrebundle :
    VectorPrebundle ğ•œâ‚‚ (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (fun x â†¦ Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x) where
  pretrivializationAtlas :=
    {e | âˆƒ (eâ‚ : Trivialization Fâ‚ (Ï€ Fâ‚ Eâ‚)) (eâ‚‚ : Trivialization Fâ‚‚ (Ï€ Fâ‚‚ Eâ‚‚))
      (_ : MemTrivializationAtlas eâ‚) (_ : MemTrivializationAtlas eâ‚‚),
        e = Pretrivialization.continuousLinearMap Ïƒ eâ‚ eâ‚‚}
  pretrivialization_linear' := by
    rintro _ âŸ¨eâ‚, heâ‚, eâ‚‚, heâ‚‚, rflâŸ©
    infer_instance
  pretrivializationAt x :=
    Pretrivialization.continuousLinearMap Ïƒ (trivializationAt Fâ‚ Eâ‚ x) (trivializationAt Fâ‚‚ Eâ‚‚ x)
  mem_base_pretrivializationAt x :=
    âŸ¨mem_baseSet_trivializationAt Fâ‚ Eâ‚ x, mem_baseSet_trivializationAt Fâ‚‚ Eâ‚‚ xâŸ©
  pretrivialization_mem_atlas x :=
    âŸ¨trivializationAt Fâ‚ Eâ‚ x, trivializationAt Fâ‚‚ Eâ‚‚ x, inferInstance, inferInstance, rflâŸ©
  exists_coordChange := by
    rintro _ âŸ¨eâ‚, eâ‚‚, heâ‚, heâ‚‚, rflâŸ© _ âŸ¨eâ‚', eâ‚‚', heâ‚', heâ‚‚', rflâŸ©
    exact âŸ¨continuousLinearMapCoordChange Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚',
      continuousOn_continuousLinearMapCoordChange,
      continuousLinearMapCoordChange_apply Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚'âŸ©
  totalSpaceMk_isInducing := by
    intro b
    let Lâ‚ : Eâ‚ b â‰ƒL[ğ•œâ‚] Fâ‚ :=
      (trivializationAt Fâ‚ Eâ‚ b).continuousLinearEquivAt ğ•œâ‚ b
        (mem_baseSet_trivializationAt _ _ _)
    let Lâ‚‚ : Eâ‚‚ b â‰ƒL[ğ•œâ‚‚] Fâ‚‚ :=
      (trivializationAt Fâ‚‚ Eâ‚‚ b).continuousLinearEquivAt ğ•œâ‚‚ b
        (mem_baseSet_trivializationAt _ _ _)
    let Ï† : (Eâ‚ b â†’SL[Ïƒ] Eâ‚‚ b) â‰ƒL[ğ•œâ‚‚] Fâ‚ â†’SL[Ïƒ] Fâ‚‚ := Lâ‚.arrowCongrSL Lâ‚‚
    have : IsInducing fun x â†¦ (b, Ï† x) := isInducing_const_prod.mpr Ï†.toHomeomorph.isInducing
    convert this
    ext f
    dsimp [Pretrivialization.continuousLinearMap_apply]
    rw [Trivialization.linearMapAt_def_of_mem _ (mem_baseSet_trivializationAt _ _ _)]
    rfl

/-- Topology on the total space of the continuous `Ïƒ`-semilinear maps between two "normable" vector
bundles over the same base. -/
instance Bundle.ContinuousLinearMap.topologicalSpaceTotalSpace :
    TopologicalSpace (TotalSpace (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (fun x â†¦ Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x)) :=
  (Bundle.ContinuousLinearMap.vectorPrebundle Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚).totalSpaceTopology

/-- The continuous `Ïƒ`-semilinear maps between two vector bundles form a fiber bundle. -/
instance Bundle.ContinuousLinearMap.fiberBundle :
    FiberBundle (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) fun x â†¦ Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x :=
  (Bundle.ContinuousLinearMap.vectorPrebundle Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚).toFiberBundle

/-- The continuous `Ïƒ`-semilinear maps between two vector bundles form a vector bundle. -/
instance Bundle.ContinuousLinearMap.vectorBundle :
    VectorBundle ğ•œâ‚‚ (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (fun x â†¦ Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x) :=
  (Bundle.ContinuousLinearMap.vectorPrebundle Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚).toVectorBundle

variable [heâ‚ : MemTrivializationAtlas eâ‚] [heâ‚‚ : MemTrivializationAtlas eâ‚‚] {Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚}

/-- Given trivializations `eâ‚`, `eâ‚‚` in the atlas for vector bundles `Eâ‚`, `Eâ‚‚` over a base `B`,
the induced trivialization for the continuous `Ïƒ`-semilinear maps from `Eâ‚` to `Eâ‚‚`,
whose base set is `eâ‚.baseSet âˆ© eâ‚‚.baseSet`. -/
def Trivialization.continuousLinearMap :
    Trivialization (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (Ï€ (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (fun x â†¦ Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x)) :=
  VectorPrebundle.trivializationOfMemPretrivializationAtlas _ âŸ¨eâ‚, eâ‚‚, heâ‚, heâ‚‚, rflâŸ©

instance Bundle.ContinuousLinearMap.memTrivializationAtlas :
    MemTrivializationAtlas
      (eâ‚.continuousLinearMap Ïƒ eâ‚‚ :
        Trivialization (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (Ï€ (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (fun x â†¦ Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x))) where
  out := âŸ¨_, âŸ¨eâ‚, eâ‚‚, by infer_instance, by infer_instance, rflâŸ©, rflâŸ©

variable {eâ‚ eâ‚‚}

@[simp]
theorem Trivialization.baseSet_continuousLinearMap :
    (eâ‚.continuousLinearMap Ïƒ eâ‚‚).baseSet = eâ‚.baseSet âˆ© eâ‚‚.baseSet :=
  rfl

theorem Trivialization.continuousLinearMap_apply
    (p : TotalSpace (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (fun x â†¦ Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x)) :
    eâ‚.continuousLinearMap Ïƒ eâ‚‚ p =
      âŸ¨p.1, (eâ‚‚.continuousLinearMapAt ğ•œâ‚‚ p.1 : _ â†’L[ğ•œâ‚‚] _).comp
        (p.2.comp (eâ‚.symmL ğ•œâ‚ p.1 : Fâ‚ â†’L[ğ•œâ‚] Eâ‚ p.1) : Fâ‚ â†’SL[Ïƒ] Eâ‚‚ p.1)âŸ© :=
  rfl

theorem hom_trivializationAt_apply (xâ‚€ : B)
    (x : TotalSpace (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (fun x â†¦ Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x)) :
    trivializationAt (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (fun x â†¦ Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x) xâ‚€ x =
      âŸ¨x.1, inCoordinates Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ xâ‚€ x.1 xâ‚€ x.1 x.2âŸ© :=
  rfl

@[simp, mfld_simps]
theorem hom_trivializationAt_source (xâ‚€ : B) :
    (trivializationAt (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (fun x â†¦ Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x) xâ‚€).source =
      Ï€ (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (fun x â†¦ Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x) â»Â¹'
        ((trivializationAt Fâ‚ Eâ‚ xâ‚€).baseSet âˆ© (trivializationAt Fâ‚‚ Eâ‚‚ xâ‚€).baseSet) :=
  rfl

@[simp, mfld_simps]
theorem hom_trivializationAt_target (xâ‚€ : B) :
    (trivializationAt (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (fun x â†¦ Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x) xâ‚€).target =
      ((trivializationAt Fâ‚ Eâ‚ xâ‚€).baseSet âˆ© (trivializationAt Fâ‚‚ Eâ‚‚ xâ‚€).baseSet) Ã—Ë¢ Set.univ :=
  rfl

@[simp]
theorem hom_trivializationAt_baseSet (xâ‚€ : B) :
    (trivializationAt (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (fun x â†¦ Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x) xâ‚€).baseSet =
      ((trivializationAt Fâ‚ Eâ‚ xâ‚€).baseSet âˆ© (trivializationAt Fâ‚‚ Eâ‚‚ xâ‚€).baseSet) :=
  rfl

theorem continuousWithinAt_hom_bundle {M : Type*} [TopologicalSpace M]
    (f : M â†’ TotalSpace (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (fun x â†¦ Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x)) {s : Set M} {xâ‚€ : M} :
    ContinuousWithinAt f s xâ‚€ â†”
      ContinuousWithinAt (fun x â†¦ (f x).1) s xâ‚€ âˆ§
        ContinuousWithinAt
          (fun x â†¦ inCoordinates Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ (f xâ‚€).1 (f x).1 (f xâ‚€).1 (f x).1 (f x).2) s xâ‚€ :=
  FiberBundle.continuousWithinAt_totalSpace ..

theorem continuousAt_hom_bundle {M : Type*} [TopologicalSpace M]
    (f : M â†’ TotalSpace (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (fun x â†¦ Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x)) {xâ‚€ : M} :
    ContinuousAt f xâ‚€ â†”
      ContinuousAt (fun x â†¦ (f x).1) xâ‚€ âˆ§
        ContinuousAt
          (fun x â†¦ inCoordinates Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ (f xâ‚€).1 (f x).1 (f xâ‚€).1 (f x).1 (f x).2) xâ‚€ :=
  FiberBundle.continuousAt_totalSpace ..

section

/- Declare two bases spaces `Bâ‚` and `Bâ‚‚` and two vector bundles `Eâ‚` and `Eâ‚‚` respectively
over `Bâ‚` and `Bâ‚‚` (with model fibers `Fâ‚` and `Fâ‚‚`).

Also a third space `M`, which will be the source of all our maps.
-/
variable {ğ•œ Fâ‚ Fâ‚‚ Bâ‚ Bâ‚‚ M : Type*} {Eâ‚ : Bâ‚ â†’ Type*} {Eâ‚‚ : Bâ‚‚ â†’ Type*} [NontriviallyNormedField ğ•œ]
  [âˆ€ x, AddCommGroup (Eâ‚ x)] [âˆ€ x, Module ğ•œ (Eâ‚ x)] [NormedAddCommGroup Fâ‚] [NormedSpace ğ•œ Fâ‚]
  [TopologicalSpace (TotalSpace Fâ‚ Eâ‚)] [âˆ€ x, TopologicalSpace (Eâ‚ x)] [âˆ€ x, AddCommGroup (Eâ‚‚ x)]
  [âˆ€ x, Module ğ•œ (Eâ‚‚ x)] [NormedAddCommGroup Fâ‚‚] [NormedSpace ğ•œ Fâ‚‚]
  [TopologicalSpace (TotalSpace Fâ‚‚ Eâ‚‚)] [âˆ€ x, TopologicalSpace (Eâ‚‚ x)]
  [TopologicalSpace Bâ‚] [TopologicalSpace Bâ‚‚] [TopologicalSpace M]
  {n : WithTop â„•âˆ} [FiberBundle Fâ‚ Eâ‚] [VectorBundle ğ•œ Fâ‚ Eâ‚]
  [FiberBundle Fâ‚‚ Eâ‚‚] [VectorBundle ğ•œ Fâ‚‚ Eâ‚‚]
  {bâ‚ : M â†’ Bâ‚} {bâ‚‚ : M â†’ Bâ‚‚} {mâ‚€ : M}
  {Ï• : Î  (m : M), Eâ‚ (bâ‚ m) â†’L[ğ•œ] Eâ‚‚ (bâ‚‚ m)} {v : Î  (m : M), Eâ‚ (bâ‚ m)} {s : Set M}

/-- Consider a continuous map `v : M â†’ Eâ‚` to a vector bundle, over a base map `bâ‚ : M â†’ Bâ‚`, and
another basemap `bâ‚‚ : M â†’ Bâ‚‚`. Given linear maps `Ï• m : Eâ‚ (bâ‚ m) â†’ Eâ‚‚ (bâ‚‚ m)` depending
continuously on `m`, one can apply `Ï• m` to `g m`, and the resulting map is continuous.

Note that the continuity of `Ï•` can not be always be stated as continuity of a map into a bundle,
as the pullback bundles `bâ‚ *áµ– Eâ‚` and `bâ‚‚ *áµ– Eâ‚‚` only have a nice topology when `bâ‚` and `bâ‚‚` are
globally continuous, but we want to apply this lemma with only local information. Therefore, we
formulate it using continuity of `Ï•` read in coordinates.

Version for `ContinuousWithinAt`. We also give a version for `ContinuousAt`, but no version for
`ContinuousOn` or `Continuous` as our assumption, written in coordinates, only makes sense around
a point.

For a version with `Bâ‚ = Bâ‚‚` and `bâ‚ = bâ‚‚`, in which continuity can be expressed without
`inCoordinates`, see `ContinuousWithinAt.clm_bundle_apply`
-/
lemma ContinuousWithinAt.clm_apply_of_inCoordinates
    (hÏ• : ContinuousWithinAt
      (fun m â†¦ inCoordinates Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ (bâ‚ mâ‚€) (bâ‚ m) (bâ‚‚ mâ‚€) (bâ‚‚ m) (Ï• m)) s mâ‚€)
    (hv : ContinuousWithinAt (fun m â†¦ (v m : TotalSpace Fâ‚ Eâ‚)) s mâ‚€)
    (hbâ‚‚ : ContinuousWithinAt bâ‚‚ s mâ‚€) :
    ContinuousWithinAt (fun m â†¦ (Ï• m (v m) : TotalSpace Fâ‚‚ Eâ‚‚)) s mâ‚€ := by
  rw [â† continuousWithinAt_insert_self] at hÏ• hv hbâ‚‚ âŠ¢
  rw [FiberBundle.continuousWithinAt_totalSpace] at hv âŠ¢
  refine âŸ¨hbâ‚‚, ?_âŸ©
  apply (ContinuousWithinAt.clm_apply hÏ• hv.2).congr_of_eventuallyEq_of_mem ?_ (mem_insert mâ‚€ s)
  have A : âˆ€á¶  m in ğ“[insert mâ‚€ s] mâ‚€, bâ‚ m âˆˆ (trivializationAt Fâ‚ Eâ‚ (bâ‚ mâ‚€)).baseSet := by
    apply hv.1
    apply (trivializationAt Fâ‚ Eâ‚ (bâ‚ mâ‚€)).open_baseSet.mem_nhds
    exact FiberBundle.mem_baseSet_trivializationAt' (bâ‚ mâ‚€)
  have A' : âˆ€á¶  m in ğ“[insert mâ‚€ s] mâ‚€, bâ‚‚ m âˆˆ (trivializationAt Fâ‚‚ Eâ‚‚ (bâ‚‚ mâ‚€)).baseSet := by
    apply hbâ‚‚
    apply (trivializationAt Fâ‚‚ Eâ‚‚ (bâ‚‚ mâ‚€)).open_baseSet.mem_nhds
    exact FiberBundle.mem_baseSet_trivializationAt' (bâ‚‚ mâ‚€)
  filter_upwards [A, A'] with m hm h'm
  simp [inCoordinates_eq hm h'm,
        Trivialization.symm_apply_apply_mk (trivializationAt Fâ‚ Eâ‚ (bâ‚ mâ‚€)) hm (v m)]


/-- Consider a continuous map `v : M â†’ Eâ‚` to a vector bundle, over a base map `bâ‚ : M â†’ Bâ‚`, and
another basemap `bâ‚‚ : M â†’ Bâ‚‚`. Given linear maps `Ï• m : Eâ‚ (bâ‚ m) â†’ Eâ‚‚ (bâ‚‚ m)` depending
continuously on `m`, one can apply `Ï• m` to `g m`, and the resulting map is continuous.

Note that the continuity of `Ï•` can not be always be stated as continuity of a map into a bundle,
as the pullback bundles `bâ‚ *áµ– Eâ‚` and `bâ‚‚ *áµ– Eâ‚‚` only make sense when `bâ‚` and `bâ‚‚` are globally
continuous, but we want to apply this lemma with only local information. Therefore, we formulate it
using continuity of `Ï•` read in coordinates.

Version for `ContinuousAt`. We also give a version for `ContinuousWithinAt`, but no version for
`ContinuousOn` or `Continuous` as our assumption, written in coordinates, only makes sense around
a point.

For a version with `Bâ‚ = Bâ‚‚` and `bâ‚ = bâ‚‚`, in which continuity can be expressed without
`inCoordinates`, see `ContinuousWithinAt.clm_bundle_apply`
-/
lemma ContinuousAt.clm_apply_of_inCoordinates
    (hÏ• : ContinuousAt
      (fun m â†¦ inCoordinates Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ (bâ‚ mâ‚€) (bâ‚ m) (bâ‚‚ mâ‚€) (bâ‚‚ m) (Ï• m)) mâ‚€)
    (hv : ContinuousAt (fun m â†¦ (v m : TotalSpace Fâ‚ Eâ‚)) mâ‚€)
    (hbâ‚‚ : ContinuousAt bâ‚‚ mâ‚€) :
    ContinuousAt (fun m â†¦ (Ï• m (v m) : TotalSpace Fâ‚‚ Eâ‚‚)) mâ‚€ := by
  rw [â† continuousWithinAt_univ] at hÏ• hv hbâ‚‚ âŠ¢
  exact hÏ•.clm_apply_of_inCoordinates hv hbâ‚‚

end

section

/- Declare a base space `B` and three vector bundles `Eâ‚`, `Eâ‚‚` and `Eâ‚ƒ` over `B`
(with model fibers `Fâ‚`, `Fâ‚‚` and `Fâ‚ƒ`).

Also a second space `M`, which will be the source of all our maps.
-/
variable {ğ•œ B Fâ‚ Fâ‚‚ Fâ‚ƒ M : Type*} [NontriviallyNormedField ğ•œ] {n : WithTop â„•âˆ}
  {Eâ‚ : B â†’ Type*}
  [âˆ€ x, AddCommGroup (Eâ‚ x)] [âˆ€ x, Module ğ•œ (Eâ‚ x)] [NormedAddCommGroup Fâ‚] [NormedSpace ğ•œ Fâ‚]
  [TopologicalSpace (TotalSpace Fâ‚ Eâ‚)] [âˆ€ x, TopologicalSpace (Eâ‚ x)]
  {Eâ‚‚ : B â†’ Type*} [âˆ€ x, AddCommGroup (Eâ‚‚ x)]
  [âˆ€ x, Module ğ•œ (Eâ‚‚ x)] [NormedAddCommGroup Fâ‚‚] [NormedSpace ğ•œ Fâ‚‚]
  [TopologicalSpace (TotalSpace Fâ‚‚ Eâ‚‚)] [âˆ€ x, TopologicalSpace (Eâ‚‚ x)]
  {Eâ‚ƒ : B â†’ Type*} [âˆ€ x, AddCommGroup (Eâ‚ƒ x)]
  [âˆ€ x, Module ğ•œ (Eâ‚ƒ x)] [NormedAddCommGroup Fâ‚ƒ] [NormedSpace ğ•œ Fâ‚ƒ]
  [TopologicalSpace (TotalSpace Fâ‚ƒ Eâ‚ƒ)] [âˆ€ x, TopologicalSpace (Eâ‚ƒ x)]
  [TopologicalSpace B] [TopologicalSpace M]
  [FiberBundle Fâ‚ Eâ‚] [VectorBundle ğ•œ Fâ‚ Eâ‚]
  [FiberBundle Fâ‚‚ Eâ‚‚] [VectorBundle ğ•œ Fâ‚‚ Eâ‚‚]
  [FiberBundle Fâ‚ƒ Eâ‚ƒ] [VectorBundle ğ•œ Fâ‚ƒ Eâ‚ƒ]
  {b : M â†’ B} {v : âˆ€ x, Eâ‚ (b x)} {s : Set M} {x : M}

section OneVariable

variable [âˆ€ x, IsTopologicalAddGroup (Eâ‚‚ x)] [âˆ€ x, ContinuousSMul ğ•œ (Eâ‚‚ x)]
  {Ï• : âˆ€ x, (Eâ‚ (b x) â†’L[ğ•œ] Eâ‚‚ (b x))}

/-- Consider a `C^n` map `v : M â†’ Eâ‚` to a vector bundle, over a basemap `b : M â†’ B`, and
linear maps `Ï• m : Eâ‚ (b m) â†’ Eâ‚‚ (b m)` depending smoothly on `m`.
One can apply `Ï• m` to `v m`, and the resulting map is `C^n`. -/
lemma ContinuousWithinAt.clm_bundle_apply
    (hÏ• : ContinuousWithinAt
      (fun m â†¦ TotalSpace.mk' (Fâ‚ â†’L[ğ•œ] Fâ‚‚) (E := fun (x : B) â†¦ (Eâ‚ x â†’L[ğ•œ] Eâ‚‚ x)) (b m) (Ï• m))
      s x)
    (hv : ContinuousWithinAt (fun m â†¦ TotalSpace.mk' Fâ‚ (b m) (v m)) s x) :
    ContinuousWithinAt
      (fun m â†¦ TotalSpace.mk' Fâ‚‚ (b m) (Ï• m (v m))) s x := by
  simp only [continuousWithinAt_hom_bundle] at hÏ•
  exact hÏ•.2.clm_apply_of_inCoordinates hv hÏ•.1

/-- Consider a `C^n` map `v : M â†’ Eâ‚` to a vector bundle, over a basemap `b : M â†’ B`, and
linear maps `Ï• m : Eâ‚ (b m) â†’ Eâ‚‚ (b m)` depending smoothly on `m`.
One can apply `Ï• m` to `v m`, and the resulting map is `C^n`. -/
lemma ContinuousAt.clm_bundle_apply
    (hÏ• : ContinuousAt
      (fun m â†¦ TotalSpace.mk' (Fâ‚ â†’L[ğ•œ] Fâ‚‚) (E := fun (x : B) â†¦ (Eâ‚ x â†’L[ğ•œ] Eâ‚‚ x)) (b m) (Ï• m)) x)
    (hv : ContinuousAt (fun m â†¦ TotalSpace.mk' Fâ‚ (b m) (v m)) x) :
    ContinuousAt (fun m â†¦ TotalSpace.mk' Fâ‚‚ (b m) (Ï• m (v m))) x := by
  simp only [â† continuousWithinAt_univ] at hÏ• hv âŠ¢
  exact hÏ•.clm_bundle_apply hv

/-- Consider a `C^n` map `v : M â†’ Eâ‚` to a vector bundle, over a basemap `b : M â†’ B`, and
linear maps `Ï• m : Eâ‚ (b m) â†’ Eâ‚‚ (b m)` depending smoothly on `m`.
One can apply `Ï• m` to `v m`, and the resulting map is `C^n`. -/
lemma ContinuousOn.clm_bundle_apply
    (hÏ• : ContinuousOn
      (fun m â†¦ TotalSpace.mk' (Fâ‚ â†’L[ğ•œ] Fâ‚‚) (E := fun (x : B) â†¦ (Eâ‚ x â†’L[ğ•œ] Eâ‚‚ x)) (b m) (Ï• m)) s)
    (hv : ContinuousOn (fun m â†¦ TotalSpace.mk' Fâ‚ (b m) (v m)) s) :
    ContinuousOn (fun m â†¦ TotalSpace.mk' Fâ‚‚ (b m) (Ï• m (v m))) s :=
  fun x hx â†¦ (hÏ• x hx).clm_bundle_apply (hv x hx)

/-- Consider a `C^n` map `v : M â†’ Eâ‚` to a vector bundle, over a basemap `b : M â†’ B`, and
linear maps `Ï• m : Eâ‚ (b m) â†’ Eâ‚‚ (b m)` depending smoothly on `m`.
One can apply `Ï• m` to `v m`, and the resulting map is `C^n`. -/
lemma Continuous.clm_bundle_apply
    (hÏ• : Continuous
      (fun m â†¦ TotalSpace.mk' (Fâ‚ â†’L[ğ•œ] Fâ‚‚) (E := fun (x : B) â†¦ (Eâ‚ x â†’L[ğ•œ] Eâ‚‚ x)) (b m) (Ï• m)))
    (hv : Continuous (fun m â†¦ TotalSpace.mk' Fâ‚ (b m) (v m))) :
    Continuous (fun m â†¦ TotalSpace.mk' Fâ‚‚ (b m) (Ï• m (v m))) := by
  simp only [continuous_iff_continuousOn_univ] at hÏ• hv âŠ¢
  exact hÏ•.clm_bundle_apply hv

end OneVariable

section TwoVariables

variable [âˆ€ x, IsTopologicalAddGroup (Eâ‚ƒ x)] [âˆ€ x, ContinuousSMul ğ•œ (Eâ‚ƒ x)]
  {Ïˆ : âˆ€ x, (Eâ‚ (b x) â†’L[ğ•œ] Eâ‚‚ (b x) â†’L[ğ•œ] Eâ‚ƒ (b x))} {w : âˆ€ x, Eâ‚‚ (b x)}

/-- Consider `C^n` maps `v : M â†’ Eâ‚` and `v : M â†’ Eâ‚‚` to vector bundles, over a basemap
`b : M â†’ B`, and bilinear maps `Ïˆ m : Eâ‚ (b m) â†’ Eâ‚‚ (b m) â†’ Eâ‚ƒ (b m)` depending smoothly on `m`.
One can apply `Ïˆ  m` to `v m` and `w m`, and the resulting map is `C^n`. -/
lemma ContinuousWithinAt.clm_bundle_applyâ‚‚
    (hÏˆ : ContinuousWithinAt (fun m â†¦ TotalSpace.mk' (Fâ‚ â†’L[ğ•œ] Fâ‚‚ â†’L[ğ•œ] Fâ‚ƒ)
      (E := fun (x : B) â†¦ (Eâ‚ x â†’L[ğ•œ] Eâ‚‚ x â†’L[ğ•œ] Eâ‚ƒ x)) (b m) (Ïˆ m)) s x)
    (hv : ContinuousWithinAt (fun m â†¦ TotalSpace.mk' Fâ‚ (b m) (v m)) s x)
    (hw : ContinuousWithinAt (fun m â†¦ TotalSpace.mk' Fâ‚‚ (b m) (w m)) s x) :
    ContinuousWithinAt (fun m â†¦ TotalSpace.mk' Fâ‚ƒ (b m) (Ïˆ m (v m) (w m))) s x :=
  (hÏˆ.clm_bundle_apply hv).clm_bundle_apply hw

/-- Consider `C^n` maps `v : M â†’ Eâ‚` and `v : M â†’ Eâ‚‚` to vector bundles, over a basemap
`b : M â†’ B`, and bilinear maps `Ïˆ m : Eâ‚ (b m) â†’ Eâ‚‚ (b m) â†’ Eâ‚ƒ (b m)` depending smoothly on `m`.
One can apply `Ïˆ  m` to `v m` and `w m`, and the resulting map is `C^n`. -/
lemma ContinuousAt.clm_bundle_applyâ‚‚
    (hÏˆ : ContinuousAt (fun m â†¦ TotalSpace.mk' (Fâ‚ â†’L[ğ•œ] Fâ‚‚ â†’L[ğ•œ] Fâ‚ƒ)
      (E := fun (x : B) â†¦ (Eâ‚ x â†’L[ğ•œ] Eâ‚‚ x â†’L[ğ•œ] Eâ‚ƒ x)) (b m) (Ïˆ m)) x)
    (hv : ContinuousAt (fun m â†¦ TotalSpace.mk' Fâ‚ (b m) (v m)) x)
    (hw : ContinuousAt (fun m â†¦ TotalSpace.mk' Fâ‚‚ (b m) (w m)) x) :
    ContinuousAt (fun m â†¦ TotalSpace.mk' Fâ‚ƒ (b m) (Ïˆ m (v m) (w m))) x :=
  (hÏˆ.clm_bundle_apply hv).clm_bundle_apply hw

/-- Consider `C^n` maps `v : M â†’ Eâ‚` and `v : M â†’ Eâ‚‚` to vector bundles, over a basemap
`b : M â†’ B`, and bilinear maps `Ïˆ m : Eâ‚ (b m) â†’ Eâ‚‚ (b m) â†’ Eâ‚ƒ (b m)` depending smoothly on `m`.
One can apply `Ïˆ  m` to `v m` and `w m`, and the resulting map is `C^n`. -/
lemma ContinuousOn.clm_bundle_applyâ‚‚
    (hÏˆ : ContinuousOn
      (fun m â†¦ TotalSpace.mk' (Fâ‚ â†’L[ğ•œ] Fâ‚‚ â†’L[ğ•œ] Fâ‚ƒ)
      (E := fun (x : B) â†¦ (Eâ‚ x â†’L[ğ•œ] Eâ‚‚ x â†’L[ğ•œ] Eâ‚ƒ x)) (b m) (Ïˆ m)) s)
    (hv : ContinuousOn (fun m â†¦ TotalSpace.mk' Fâ‚ (b m) (v m)) s)
    (hw : ContinuousOn (fun m â†¦ TotalSpace.mk' Fâ‚‚ (b m) (w m)) s) :
    ContinuousOn (fun m â†¦ TotalSpace.mk' Fâ‚ƒ (b m) (Ïˆ m (v m) (w m))) s :=
  fun x hx â†¦ (hÏˆ x hx).clm_bundle_applyâ‚‚ (hv x hx) (hw x hx)

/-- Consider `C^n` maps `v : M â†’ Eâ‚` and `v : M â†’ Eâ‚‚` to vector bundles, over a basemap
`b : M â†’ B`, and bilinear maps `Ïˆ m : Eâ‚ (b m) â†’ Eâ‚‚ (b m) â†’ Eâ‚ƒ (b m)` depending smoothly on `m`.
One can apply `Ïˆ  m` to `v m` and `w m`, and the resulting map is `C^n`. -/
lemma Continuous.clm_bundle_applyâ‚‚
    (hÏˆ : Continuous (fun m â†¦ TotalSpace.mk' (Fâ‚ â†’L[ğ•œ] Fâ‚‚ â†’L[ğ•œ] Fâ‚ƒ)
      (E := fun (x : B) â†¦ (Eâ‚ x â†’L[ğ•œ] Eâ‚‚ x â†’L[ğ•œ] Eâ‚ƒ x)) (b m) (Ïˆ m)))
    (hv : Continuous (fun m â†¦ TotalSpace.mk' Fâ‚ (b m) (v m)))
    (hw : Continuous (fun m â†¦ TotalSpace.mk' Fâ‚‚ (b m) (w m))) :
    Continuous (fun m â†¦ TotalSpace.mk' Fâ‚ƒ (b m) (Ïˆ m (v m) (w m))) := by
  simp only [continuous_iff_continuousOn_univ] at hÏˆ hv hw âŠ¢
  exact hÏˆ.clm_bundle_applyâ‚‚ hv hw

end TwoVariables

end
