/-
Copyright (c) 2022 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth, Floris van Doorn
-/
import Mathlib.Topology.VectorBundle.Basic

#align_import topology.vector_bundle.hom from "leanprover-community/mathlib"@"8905e5ed90859939681a725b00f6063e65096d95"

/-!
# The vector bundle of continuous (semi)linear maps

We define the (topological) vector bundle of continuous (semi)linear maps between two vector bundles
over the same base.

Given bundles `Eâ‚ Eâ‚‚ : B â†’ Type*`, normed spaces `Fâ‚` and `Fâ‚‚`, and a ring-homomorphism `Ïƒ` between
their respective scalar fields, we define `Bundle.ContinuousLinearMap Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ x` to be a
type synonym for `fun x â†¦ Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x`. If the `Eâ‚` and `Eâ‚‚` are vector bundles with model
fibers `Fâ‚` and `Fâ‚‚`, then this will be a vector bundle with fiber `Fâ‚ â†’SL[Ïƒ] Fâ‚‚`.

The topology on the total space is constructed from the trivializations for `Eâ‚` and `Eâ‚‚` and the
norm-topology on the model fiber `Fâ‚ â†’SL[ğ•œ] Fâ‚‚` using the `VectorPrebundle` construction.  This is
a bit awkward because it introduces a dependence on the normed space structure of the model fibers,
rather than just their topological vector space structure; it is not clear whether this is
necessary.

Similar constructions should be possible (but are yet to be formalized) for tensor products of
topological vector bundles, exterior algebras, and so on, where again the topology can be defined
using a norm on the fiber model if this helps.

## Main Definitions

* `Bundle.ContinuousLinearMap.vectorBundle`: continuous semilinear maps between
  vector bundles form a vector bundle.

-/


noncomputable section

open scoped Bundle

open Bundle Set ContinuousLinearMap

variable {ğ•œâ‚ : Type*} [NontriviallyNormedField ğ•œâ‚] {ğ•œâ‚‚ : Type*} [NontriviallyNormedField ğ•œâ‚‚]
  (Ïƒ : ğ•œâ‚ â†’+* ğ•œâ‚‚) [iÏƒ : RingHomIsometric Ïƒ]

variable {B : Type*}
variable {Fâ‚ : Type*} [NormedAddCommGroup Fâ‚] [NormedSpace ğ•œâ‚ Fâ‚] (Eâ‚ : B â†’ Type*)
  [âˆ€ x, AddCommGroup (Eâ‚ x)] [âˆ€ x, Module ğ•œâ‚ (Eâ‚ x)] [TopologicalSpace (TotalSpace Fâ‚ Eâ‚)]

variable {Fâ‚‚ : Type*} [NormedAddCommGroup Fâ‚‚] [NormedSpace ğ•œâ‚‚ Fâ‚‚] (Eâ‚‚ : B â†’ Type*)
  [âˆ€ x, AddCommGroup (Eâ‚‚ x)] [âˆ€ x, Module ğ•œâ‚‚ (Eâ‚‚ x)] [TopologicalSpace (TotalSpace Fâ‚‚ Eâ‚‚)]

/-- A reducible type synonym for the bundle of continuous (semi)linear maps. For some reason, it
helps with instance search.

Porting note: after the port is done, we may want to remove this definition.
-/
protected abbrev Bundle.ContinuousLinearMap [âˆ€ x, TopologicalSpace (Eâ‚ x)]
    [âˆ€ x, TopologicalSpace (Eâ‚‚ x)] : B â†’ Type _ := fun x => Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x
#align bundle.continuous_linear_map Bundle.ContinuousLinearMap

-- Porting note: possibly remove after the port
instance Bundle.ContinuousLinearMap.module [âˆ€ x, TopologicalSpace (Eâ‚ x)]
    [âˆ€ x, TopologicalSpace (Eâ‚‚ x)] [âˆ€ x, TopologicalAddGroup (Eâ‚‚ x)]
    [âˆ€ x, ContinuousConstSMul ğ•œâ‚‚ (Eâ‚‚ x)] : âˆ€ x, Module ğ•œâ‚‚ (Bundle.ContinuousLinearMap Ïƒ Eâ‚ Eâ‚‚ x) :=
  fun _ => inferInstance
#align bundle.continuous_linear_map.module Bundle.ContinuousLinearMap.module

variable {Eâ‚ Eâ‚‚}
variable [TopologicalSpace B] (eâ‚ eâ‚' : Trivialization Fâ‚ (Ï€ Fâ‚ Eâ‚))
  (eâ‚‚ eâ‚‚' : Trivialization Fâ‚‚ (Ï€ Fâ‚‚ Eâ‚‚))

namespace Pretrivialization

/-- Assume `eáµ¢` and `eáµ¢'` are trivializations of the bundles `Eáµ¢` over base `B` with fiber `Fáµ¢`
(`i âˆˆ {1,2}`), then `Pretrivialization.continuousLinearMapCoordChange Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚'` is the
coordinate change function between the two induced (pre)trivializations
`Pretrivialization.continuousLinearMap Ïƒ eâ‚ eâ‚‚` and
`Pretrivialization.continuousLinearMap Ïƒ eâ‚' eâ‚‚'` of `Bundle.ContinuousLinearMap`. -/
def continuousLinearMapCoordChange [eâ‚.IsLinear ğ•œâ‚] [eâ‚'.IsLinear ğ•œâ‚] [eâ‚‚.IsLinear ğ•œâ‚‚]
    [eâ‚‚'.IsLinear ğ•œâ‚‚] (b : B) : (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) â†’L[ğ•œâ‚‚] Fâ‚ â†’SL[Ïƒ] Fâ‚‚ :=
  ((eâ‚'.coordChangeL ğ•œâ‚ eâ‚ b).symm.arrowCongrSL (eâ‚‚.coordChangeL ğ•œâ‚‚ eâ‚‚' b) :
    (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) â‰ƒL[ğ•œâ‚‚] Fâ‚ â†’SL[Ïƒ] Fâ‚‚)
#align pretrivialization.continuous_linear_map_coord_change Pretrivialization.continuousLinearMapCoordChange

variable {Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚'}
variable [âˆ€ x, TopologicalSpace (Eâ‚ x)] [FiberBundle Fâ‚ Eâ‚]
variable [âˆ€ x, TopologicalSpace (Eâ‚‚ x)] [ita : âˆ€ x, TopologicalAddGroup (Eâ‚‚ x)] [FiberBundle Fâ‚‚ Eâ‚‚]

theorem continuousOn_continuousLinearMapCoordChange [VectorBundle ğ•œâ‚ Fâ‚ Eâ‚] [VectorBundle ğ•œâ‚‚ Fâ‚‚ Eâ‚‚]
    [MemTrivializationAtlas eâ‚] [MemTrivializationAtlas eâ‚'] [MemTrivializationAtlas eâ‚‚]
    [MemTrivializationAtlas eâ‚‚'] :
    ContinuousOn (continuousLinearMapCoordChange Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚')
      (eâ‚.baseSet âˆ© eâ‚‚.baseSet âˆ© (eâ‚'.baseSet âˆ© eâ‚‚'.baseSet)) := by
  have hâ‚ := (compSL Fâ‚ Fâ‚‚ Fâ‚‚ Ïƒ (RingHom.id ğ•œâ‚‚)).continuous
  have hâ‚‚ := (ContinuousLinearMap.flip (compSL Fâ‚ Fâ‚ Fâ‚‚ (RingHom.id ğ•œâ‚) Ïƒ)).continuous
  have hâ‚ƒ := continuousOn_coordChange ğ•œâ‚ eâ‚' eâ‚
  have hâ‚„ := continuousOn_coordChange ğ•œâ‚‚ eâ‚‚ eâ‚‚'
  refine ((hâ‚.comp_continuousOn (hâ‚„.mono ?_)).clm_comp (hâ‚‚.comp_continuousOn (hâ‚ƒ.mono ?_))).congr ?_
  Â· mfld_set_tac
  Â· mfld_set_tac
  Â· intro b _; ext L v
    -- Porting note: was
    -- simp only [continuousLinearMapCoordChange, ContinuousLinearEquiv.coe_coe,
    --   ContinuousLinearEquiv.arrowCongrâ‚›â‚—_apply, LinearEquiv.toFun_eq_coe, coe_comp',
    --   ContinuousLinearEquiv.arrowCongrSL_apply, comp_apply, Function.comp, compSL_apply,
    --   flip_apply, ContinuousLinearEquiv.symm_symm]
    -- Now `simp` fails to use `ContinuousLinearMap.comp_apply` in this case
    dsimp [continuousLinearMapCoordChange]
    rw [ContinuousLinearEquiv.symm_symm]
#align pretrivialization.continuous_on_continuous_linear_map_coord_change Pretrivialization.continuousOn_continuousLinearMapCoordChange

variable (Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚')
variable [eâ‚.IsLinear ğ•œâ‚] [eâ‚'.IsLinear ğ•œâ‚] [eâ‚‚.IsLinear ğ•œâ‚‚] [eâ‚‚'.IsLinear ğ•œâ‚‚]

/-- Given trivializations `eâ‚`, `eâ‚‚` for vector bundles `Eâ‚`, `Eâ‚‚` over a base `B`,
`Pretrivialization.continuousLinearMap Ïƒ eâ‚ eâ‚‚` is the induced pretrivialization for the
continuous `Ïƒ`-semilinear maps from `Eâ‚` to `Eâ‚‚`. That is, the map which will later become a
trivialization, after the bundle of continuous semilinear maps is equipped with the right
topological vector bundle structure. -/
def continuousLinearMap :
    Pretrivialization (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (Ï€ (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (Bundle.ContinuousLinearMap Ïƒ Eâ‚ Eâ‚‚)) where
  toFun p := âŸ¨p.1, .comp (eâ‚‚.continuousLinearMapAt ğ•œâ‚‚ p.1) (p.2.comp (eâ‚.symmL ğ•œâ‚ p.1))âŸ©
  invFun p := âŸ¨p.1, .comp (eâ‚‚.symmL ğ•œâ‚‚ p.1) (p.2.comp (eâ‚.continuousLinearMapAt ğ•œâ‚ p.1))âŸ©
  source := Bundle.TotalSpace.proj â»Â¹' (eâ‚.baseSet âˆ© eâ‚‚.baseSet)
  target := (eâ‚.baseSet âˆ© eâ‚‚.baseSet) Ã—Ë¢ Set.univ
  map_source' := fun âŸ¨x, LâŸ© h => âŸ¨h, Set.mem_univ _âŸ©
  map_target' := fun âŸ¨x, fâŸ© h => h.1
  left_inv' := fun âŸ¨x, LâŸ© âŸ¨hâ‚, hâ‚‚âŸ© => by
    simp only [TotalSpace.mk_inj]
    ext (v : Eâ‚ x)
    dsimp only [comp_apply]
    rw [Trivialization.symmL_continuousLinearMapAt, Trivialization.symmL_continuousLinearMapAt]
    exacts [hâ‚, hâ‚‚]
  right_inv' := fun âŸ¨x, fâŸ© âŸ¨âŸ¨hâ‚, hâ‚‚âŸ©, _âŸ© => by
    simp only [Prod.mk_inj_left]
    ext v
    dsimp only [comp_apply]
    rw [Trivialization.continuousLinearMapAt_symmL, Trivialization.continuousLinearMapAt_symmL]
    exacts [hâ‚, hâ‚‚]
  open_target := (eâ‚.open_baseSet.inter eâ‚‚.open_baseSet).prod isOpen_univ
  baseSet := eâ‚.baseSet âˆ© eâ‚‚.baseSet
  open_baseSet := eâ‚.open_baseSet.inter eâ‚‚.open_baseSet
  source_eq := rfl
  target_eq := rfl
  proj_toFun _ _ := rfl
#align pretrivialization.continuous_linear_map Pretrivialization.continuousLinearMap

-- Porting note (#11215): TODO: see if Lean 4 can generate this instance without a hint
instance continuousLinearMap.isLinear [âˆ€ x, ContinuousAdd (Eâ‚‚ x)] [âˆ€ x, ContinuousSMul ğ•œâ‚‚ (Eâ‚‚ x)] :
    (Pretrivialization.continuousLinearMap Ïƒ eâ‚ eâ‚‚).IsLinear ğ•œâ‚‚ where
  linear x _ :=
    { map_add := fun L L' =>
        show (eâ‚‚.continuousLinearMapAt ğ•œâ‚‚ x).comp ((L + L').comp (eâ‚.symmL ğ•œâ‚ x)) = _ by
          simp_rw [add_comp, comp_add]
          rfl
      map_smul := fun c L =>
        show (eâ‚‚.continuousLinearMapAt ğ•œâ‚‚ x).comp ((c â€¢ L).comp (eâ‚.symmL ğ•œâ‚ x)) = _ by
          simp_rw [smul_comp, comp_smulâ‚›â‚—, RingHom.id_apply]
          rfl }
#align pretrivialization.continuous_linear_map.is_linear Pretrivialization.continuousLinearMap.isLinear

theorem continuousLinearMap_apply (p : TotalSpace (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) fun x => Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x) :
    (continuousLinearMap Ïƒ eâ‚ eâ‚‚) p =
      âŸ¨p.1, .comp (eâ‚‚.continuousLinearMapAt ğ•œâ‚‚ p.1) (p.2.comp (eâ‚.symmL ğ•œâ‚ p.1))âŸ© :=
  rfl
#align pretrivialization.continuous_linear_map_apply Pretrivialization.continuousLinearMap_apply

theorem continuousLinearMap_symm_apply (p : B Ã— (Fâ‚ â†’SL[Ïƒ] Fâ‚‚)) :
    (continuousLinearMap Ïƒ eâ‚ eâ‚‚).toPartialEquiv.symm p =
      âŸ¨p.1, .comp (eâ‚‚.symmL ğ•œâ‚‚ p.1) (p.2.comp (eâ‚.continuousLinearMapAt ğ•œâ‚ p.1))âŸ© :=
  rfl
#align pretrivialization.continuous_linear_map_symm_apply Pretrivialization.continuousLinearMap_symm_apply

theorem continuousLinearMap_symm_apply' {b : B} (hb : b âˆˆ eâ‚.baseSet âˆ© eâ‚‚.baseSet)
    (L : Fâ‚ â†’SL[Ïƒ] Fâ‚‚) :
    (continuousLinearMap Ïƒ eâ‚ eâ‚‚).symm b L =
      (eâ‚‚.symmL ğ•œâ‚‚ b).comp (L.comp <| eâ‚.continuousLinearMapAt ğ•œâ‚ b) := by
  rw [symm_apply]
  Â· rfl
  Â· exact hb
#align pretrivialization.continuous_linear_map_symm_apply' Pretrivialization.continuousLinearMap_symm_apply'

theorem continuousLinearMapCoordChange_apply (b : B)
    (hb : b âˆˆ eâ‚.baseSet âˆ© eâ‚‚.baseSet âˆ© (eâ‚'.baseSet âˆ© eâ‚‚'.baseSet)) (L : Fâ‚ â†’SL[Ïƒ] Fâ‚‚) :
    continuousLinearMapCoordChange Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚' b L =
      (continuousLinearMap Ïƒ eâ‚' eâ‚‚' âŸ¨b, (continuousLinearMap Ïƒ eâ‚ eâ‚‚).symm b LâŸ©).2 := by
  ext v
  simp_rw [continuousLinearMapCoordChange, ContinuousLinearEquiv.coe_coe,
    ContinuousLinearEquiv.arrowCongrSL_apply, continuousLinearMap_apply,
    continuousLinearMap_symm_apply' Ïƒ eâ‚ eâ‚‚ hb.1, comp_apply, ContinuousLinearEquiv.coe_coe,
    ContinuousLinearEquiv.symm_symm, Trivialization.continuousLinearMapAt_apply,
    Trivialization.symmL_apply]
  rw [eâ‚‚.coordChangeL_apply eâ‚‚', eâ‚'.coordChangeL_apply eâ‚, eâ‚.coe_linearMapAt_of_mem hb.1.1,
    eâ‚‚'.coe_linearMapAt_of_mem hb.2.2]
  exacts [âŸ¨hb.2.1, hb.1.1âŸ©, âŸ¨hb.1.2, hb.2.2âŸ©]
#align pretrivialization.continuous_linear_map_coord_change_apply Pretrivialization.continuousLinearMapCoordChange_apply

end Pretrivialization

open Pretrivialization

variable (Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚)
variable [âˆ€ x : B, TopologicalSpace (Eâ‚ x)] [FiberBundle Fâ‚ Eâ‚] [VectorBundle ğ•œâ‚ Fâ‚ Eâ‚]
variable [âˆ€ x : B, TopologicalSpace (Eâ‚‚ x)] [FiberBundle Fâ‚‚ Eâ‚‚] [VectorBundle ğ•œâ‚‚ Fâ‚‚ Eâ‚‚]
variable [âˆ€ x, TopologicalAddGroup (Eâ‚‚ x)] [âˆ€ x, ContinuousSMul ğ•œâ‚‚ (Eâ‚‚ x)]

/-- The continuous `Ïƒ`-semilinear maps between two topological vector bundles form a
`VectorPrebundle` (this is an auxiliary construction for the
`VectorBundle` instance, in which the pretrivializations are collated but no topology
on the total space is yet provided). -/
def Bundle.ContinuousLinearMap.vectorPrebundle :
    VectorPrebundle ğ•œâ‚‚ (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (Bundle.ContinuousLinearMap Ïƒ Eâ‚ Eâ‚‚) where
  pretrivializationAtlas :=
    {e | âˆƒ (eâ‚ : Trivialization Fâ‚ (Ï€ Fâ‚ Eâ‚)) (eâ‚‚ : Trivialization Fâ‚‚ (Ï€ Fâ‚‚ Eâ‚‚))
      (_ : MemTrivializationAtlas eâ‚) (_ : MemTrivializationAtlas eâ‚‚),
        e = Pretrivialization.continuousLinearMap Ïƒ eâ‚ eâ‚‚}
  pretrivialization_linear' := by
    rintro _ âŸ¨eâ‚, heâ‚, eâ‚‚, heâ‚‚, rflâŸ©
    infer_instance
  pretrivializationAt x :=
    Pretrivialization.continuousLinearMap Ïƒ (trivializationAt Fâ‚ Eâ‚ x) (trivializationAt Fâ‚‚ Eâ‚‚ x)
  mem_base_pretrivializationAt x :=
    âŸ¨mem_baseSet_trivializationAt Fâ‚ Eâ‚ x, mem_baseSet_trivializationAt Fâ‚‚ Eâ‚‚ xâŸ©
  pretrivialization_mem_atlas x :=
    âŸ¨trivializationAt Fâ‚ Eâ‚ x, trivializationAt Fâ‚‚ Eâ‚‚ x, inferInstance, inferInstance, rflâŸ©
  exists_coordChange := by
    rintro _ âŸ¨eâ‚, eâ‚‚, heâ‚, heâ‚‚, rflâŸ© _ âŸ¨eâ‚', eâ‚‚', heâ‚', heâ‚‚', rflâŸ©
    exact âŸ¨continuousLinearMapCoordChange Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚',
      continuousOn_continuousLinearMapCoordChange,
      continuousLinearMapCoordChange_apply Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚'âŸ©
  totalSpaceMk_inducing := by
    intro b
    let Lâ‚ : Eâ‚ b â‰ƒL[ğ•œâ‚] Fâ‚ :=
      (trivializationAt Fâ‚ Eâ‚ b).continuousLinearEquivAt ğ•œâ‚ b
        (mem_baseSet_trivializationAt _ _ _)
    let Lâ‚‚ : Eâ‚‚ b â‰ƒL[ğ•œâ‚‚] Fâ‚‚ :=
      (trivializationAt Fâ‚‚ Eâ‚‚ b).continuousLinearEquivAt ğ•œâ‚‚ b
        (mem_baseSet_trivializationAt _ _ _)
    let Ï† : (Eâ‚ b â†’SL[Ïƒ] Eâ‚‚ b) â‰ƒL[ğ•œâ‚‚] Fâ‚ â†’SL[Ïƒ] Fâ‚‚ := Lâ‚.arrowCongrSL Lâ‚‚
    have : Inducing fun x => (b, Ï† x) := inducing_const_prod.mpr Ï†.toHomeomorph.inducing
    convert this
    ext f
    dsimp [Pretrivialization.continuousLinearMap_apply]
    rw [Trivialization.linearMapAt_def_of_mem _ (mem_baseSet_trivializationAt _ _ _)]
    rfl
#align bundle.continuous_linear_map.vector_prebundle Bundle.ContinuousLinearMap.vectorPrebundle

/-- Topology on the total space of the continuous `Ïƒ`-semilinear maps between two "normable" vector
bundles over the same base. -/
instance Bundle.ContinuousLinearMap.topologicalSpaceTotalSpace :
    TopologicalSpace (TotalSpace (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (Bundle.ContinuousLinearMap Ïƒ Eâ‚ Eâ‚‚)) :=
  (Bundle.ContinuousLinearMap.vectorPrebundle Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚).totalSpaceTopology
#align bundle.continuous_linear_map.topological_space_total_space Bundle.ContinuousLinearMap.topologicalSpaceTotalSpace

/-- The continuous `Ïƒ`-semilinear maps between two vector bundles form a fiber bundle. -/
instance Bundle.ContinuousLinearMap.fiberBundle :
    FiberBundle (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) fun x => Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x :=
  (Bundle.ContinuousLinearMap.vectorPrebundle Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚).toFiberBundle
#align bundle.continuous_linear_map.fiber_bundle Bundle.ContinuousLinearMap.fiberBundle

/-- The continuous `Ïƒ`-semilinear maps between two vector bundles form a vector bundle. -/
instance Bundle.ContinuousLinearMap.vectorBundle :
    VectorBundle ğ•œâ‚‚ (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (Bundle.ContinuousLinearMap Ïƒ Eâ‚ Eâ‚‚) :=
  (Bundle.ContinuousLinearMap.vectorPrebundle Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚).toVectorBundle
#align bundle.continuous_linear_map.vector_bundle Bundle.ContinuousLinearMap.vectorBundle

variable [heâ‚ : MemTrivializationAtlas eâ‚] [heâ‚‚ : MemTrivializationAtlas eâ‚‚] {Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚}

/-- Given trivializations `eâ‚`, `eâ‚‚` in the atlas for vector bundles `Eâ‚`, `Eâ‚‚` over a base `B`,
the induced trivialization for the continuous `Ïƒ`-semilinear maps from `Eâ‚` to `Eâ‚‚`,
whose base set is `eâ‚.baseSet âˆ© eâ‚‚.baseSet`. -/
def Trivialization.continuousLinearMap :
    Trivialization (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (Ï€ (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (Bundle.ContinuousLinearMap Ïƒ Eâ‚ Eâ‚‚)) :=
  VectorPrebundle.trivializationOfMemPretrivializationAtlas _ âŸ¨eâ‚, eâ‚‚, heâ‚, heâ‚‚, rflâŸ©
#align trivialization.continuous_linear_map Trivialization.continuousLinearMap

instance Bundle.ContinuousLinearMap.memTrivializationAtlas :
    MemTrivializationAtlas
      (eâ‚.continuousLinearMap Ïƒ eâ‚‚ :
        Trivialization (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (Ï€ (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (Bundle.ContinuousLinearMap Ïƒ Eâ‚ Eâ‚‚))) where
  out := âŸ¨_, âŸ¨eâ‚, eâ‚‚, by infer_instance, by infer_instance, rflâŸ©, rflâŸ©
#align bundle.continuous_linear_map.mem_trivialization_atlas Bundle.ContinuousLinearMap.memTrivializationAtlas

variable {eâ‚ eâ‚‚}

@[simp]
theorem Trivialization.baseSet_continuousLinearMap :
    (eâ‚.continuousLinearMap Ïƒ eâ‚‚).baseSet = eâ‚.baseSet âˆ© eâ‚‚.baseSet :=
  rfl
#align trivialization.base_set_continuous_linear_map Trivialization.baseSet_continuousLinearMap

theorem Trivialization.continuousLinearMap_apply
    (p : TotalSpace (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (Bundle.ContinuousLinearMap Ïƒ Eâ‚ Eâ‚‚)) :
    eâ‚.continuousLinearMap Ïƒ eâ‚‚ p =
      âŸ¨p.1, (eâ‚‚.continuousLinearMapAt ğ•œâ‚‚ p.1 : _ â†’L[ğ•œâ‚‚] _).comp
        (p.2.comp (eâ‚.symmL ğ•œâ‚ p.1 : Fâ‚ â†’L[ğ•œâ‚] Eâ‚ p.1) : Fâ‚ â†’SL[Ïƒ] Eâ‚‚ p.1)âŸ© :=
  rfl
#align trivialization.continuous_linear_map_apply Trivialization.continuousLinearMap_apply

theorem hom_trivializationAt_apply (xâ‚€ : B)
    (x : TotalSpace (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (Bundle.ContinuousLinearMap Ïƒ Eâ‚ Eâ‚‚)) :
    trivializationAt (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (fun x => Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x) xâ‚€ x =
      âŸ¨x.1, inCoordinates Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ xâ‚€ x.1 xâ‚€ x.1 x.2âŸ© :=
  rfl
#align hom_trivialization_at_apply hom_trivializationAt_apply

@[simp, mfld_simps]
theorem hom_trivializationAt_source (xâ‚€ : B) :
    (trivializationAt (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (Bundle.ContinuousLinearMap Ïƒ Eâ‚ Eâ‚‚) xâ‚€).source =
      Ï€ (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (Bundle.ContinuousLinearMap Ïƒ Eâ‚ Eâ‚‚) â»Â¹'
        ((trivializationAt Fâ‚ Eâ‚ xâ‚€).baseSet âˆ© (trivializationAt Fâ‚‚ Eâ‚‚ xâ‚€).baseSet) :=
  rfl
#align hom_trivialization_at_source hom_trivializationAt_source

@[simp, mfld_simps]
theorem hom_trivializationAt_target (xâ‚€ : B) :
    (trivializationAt (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (fun x => Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x) xâ‚€).target =
      ((trivializationAt Fâ‚ Eâ‚ xâ‚€).baseSet âˆ© (trivializationAt Fâ‚‚ Eâ‚‚ xâ‚€).baseSet) Ã—Ë¢ Set.univ :=
  rfl
#align hom_trivialization_at_target hom_trivializationAt_target
