/-
Copyright Â© 2023 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/

import Mathlib.Analysis.NormedSpace.Alternating
import Mathlib.Topology.VectorBundle.Basic

/-!
# The vector bundle of continuous alternating maps

We define the (topological) vector bundle of continuous alternating maps between two vector bundles
over the same base.

Given bundles `Eâ‚ Eâ‚‚ : B â†’ Type*`, and normed spaces `Fâ‚` and `Fâ‚‚`, we define
`Î›^Î¹âŸ®ğ•œ; Fâ‚, Eâ‚; Fâ‚‚, Eâ‚‚âŸ¯` (notation for `Bundle.continuousAlternatingMap ğ•œ Î¹ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ x`) to be
a type synonym for `fun x â†¦ Î›^Î¹âŸ®ğ•œ; Eâ‚ x; Eâ‚‚ xâŸ¯`, the sigma-type of continuous alternating maps
fibrewise from `Eâ‚ x` to `Eâ‚‚ x`. If the `Eâ‚` and `Eâ‚‚` are vector bundles with model fibers `Fâ‚` and
`Fâ‚‚`, then this will be a vector bundle with model fiber `Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚`.

The topology is constructed from the trivializations for `Eâ‚` and `Eâ‚‚` and the norm-topology on the
model fiber `Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚` using the `vector_prebundle` construction.  This
is a bit awkward because it introduces a spurious (?) dependence on the normed space structure of
the model fiber.

Similar constructions should be possible (but are yet to be formalized) for bundles of continuous
symmetric maps, multilinear maps in general, and so on, where again the topology can be defined
using a norm on the fiber model if this helps.

## Main Definitions

* `Bundle.continuousAlternatingMap.vectorBundle`: continuous alternating maps between
  vector bundles form a vector bundle.  (FIXME Notation `Î›^Î¹âŸ®ğ•œ; Fâ‚, Eâ‚; Fâ‚‚, Eâ‚‚âŸ¯`.)

## Implementation notes

The development of the alternating bundle here is unsatisfactory because it is linear rather than
semilinear, so e.g. the bundle of alternating conjugate-linear maps, needed for Dolbeault
cohomology, is not constructed.

The wider development of linear-algebraic constructions on vector bundles (the hom-bundle, the
alternating-maps bundle, the direct-sum bundle, possibly in the future the bundles of multilinear
and symmetric maps) is also unsatisfactory, in that it proceeds construction by construction rather
than according to some generalization which works for all of them. But it is not clear what a
suitable generalization would be which also covers the semilinear case, as well as other important
cases such as fractional powers of line bundles (needed for the density bundle).

-/

noncomputable section

open Bundle Set ContinuousAlternatingMap

section defs
variable (ğ•œ : Type*) [CommSemiring ğ•œ] [TopologicalSpace ğ•œ] (Î¹ : Type*)
variable {B : Type*}

/- The bundle of continuous `Î¹`-slot alternating maps between the topological vector bundles `Eâ‚`
and `Eâ‚‚`. This is a type synonym for `Î›^Î¹âŸ®ğ•œ; Fâ‚, Eâ‚; Fâ‚‚, Eâ‚‚âŸ¯`.

We intentionally add `Fâ‚` and `Fâ‚‚` as arguments to this type, so that instances on this type
(that depend on `Fâ‚` and `Fâ‚‚`) actually refer to `Fâ‚` and `Fâ‚‚`. -/
@[nolint unusedArguments]
protected def Bundle.continuousAlternatingMap (Fâ‚ : Type*) (Eâ‚ : B â†’ Type*)
    [Î  x, AddCommMonoid (Eâ‚ x)] [Î  x, Module ğ•œ (Eâ‚ x)] [Î  x, TopologicalSpace (Eâ‚ x)]
    (Fâ‚‚ : Type*) (Eâ‚‚ : B â†’ Type*) [Î  x, AddCommMonoid (Eâ‚‚ x)] [Î  x, Module ğ•œ (Eâ‚‚ x)]
    [Î  x, TopologicalSpace (Eâ‚‚ x)](x : B) : Type _ :=
  (Eâ‚ x) [Î›^Î¹]â†’L[ğ•œ] (Eâ‚‚ x)
-- deriving Inhabited FIXME

-- FIXME, notation more consistent with `Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚`?
notation3 "Î›^" Î¹ "âŸ®" ğ•œ "; " Fâ‚ ", " Eâ‚ "; " Fâ‚‚ ", " Eâ‚‚ "âŸ¯" => Bundle.continuousAlternatingMap ğ•œ Î¹ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚

variable (Fâ‚ : Type*) (Eâ‚ : B â†’ Type*) [Î  x, AddCommMonoid (Eâ‚ x)] [Î  x, Module ğ•œ (Eâ‚ x)]
variable [Î  x, TopologicalSpace (Eâ‚ x)]
variable (Fâ‚‚ : Type*) (Eâ‚‚ : B â†’ Type*) [Î  x, AddCommMonoid (Eâ‚‚ x)] [Î  x, Module ğ•œ (Eâ‚‚ x)]
variable [Î  x, TopologicalSpace (Eâ‚‚ x)]

variable [Î  x, ContinuousAdd (Eâ‚‚ x)]

instance (x : B) : AddCommMonoid (Î›^Î¹âŸ®ğ•œ; Fâ‚, Eâ‚; Fâ‚‚, Eâ‚‚âŸ¯ x) := by
  -- was `delta_instance bundle.continuousAlternatingMap`, FIXME
  dsimp [Bundle.continuousAlternatingMap]
  infer_instance

variable [âˆ€ x, ContinuousSMul ğ•œ (Eâ‚‚ x)]

instance (x : B) : Module ğ•œ (Î›^Î¹âŸ®ğ•œ; Fâ‚, Eâ‚; Fâ‚‚, Eâ‚‚âŸ¯ x) := by
  -- was delta_instance `Bundle.continuousAlternatingMap`, FIXME
  dsimp [Bundle.continuousAlternatingMap]
  infer_instance

end defs

variable (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] (Î¹ : Type*) [Fintype Î¹]

variable {B : Type*} [TopologicalSpace B]

variable (Fâ‚ : Type*) [NormedAddCommGroup Fâ‚] [NormedSpace ğ•œ Fâ‚]
  (Eâ‚ : B â†’ Type*) [Î  x, AddCommMonoid (Eâ‚ x)] [Î  x, Module ğ•œ (Eâ‚ x)]
  [TopologicalSpace (TotalSpace Fâ‚ Eâ‚)]
variable (Fâ‚‚ : Type*) [NormedAddCommGroup Fâ‚‚][NormedSpace ğ•œ Fâ‚‚]
  (Eâ‚‚ : B â†’ Type*) [Î  x, AddCommMonoid (Eâ‚‚ x)] [Î  x, Module ğ•œ (Eâ‚‚ x)]
  [TopologicalSpace (TotalSpace Fâ‚‚ Eâ‚‚)]

variable {Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚}
variable (eâ‚ eâ‚' : Trivialization Fâ‚ (Ï€ Fâ‚ Eâ‚)) (eâ‚‚ eâ‚‚' : Trivialization Fâ‚‚ (Ï€ Fâ‚‚ Eâ‚‚))


namespace Pretrivialization

/-- Assume `eáµ¢` and `eáµ¢'` are trivializations of the bundles `Eáµ¢` over base `B` with fiber `Fáµ¢`
(`i âˆˆ {1,2}`), then `continuousAlternatingMapCoordChange Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚'` is the coordinate
change function between the two induced (pre)trivializations
`Pretrivialization.continuousAlternatingMap Ïƒ eâ‚ eâ‚‚` and
`Pretrivialization.continuousAlternatingMap Ïƒ eâ‚' eâ‚‚'` of `Bundle.ContinuousAlternatingMap`. -/
def continuousAlternatingMapCoordChange
    [eâ‚.IsLinear ğ•œ] [eâ‚'.IsLinear ğ•œ] [eâ‚‚.IsLinear ğ•œ] [eâ‚‚'.IsLinear ğ•œ] (b : B) :
    (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) â†’L[ğ•œ] (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) :=
  ((eâ‚'.coordChangeL ğ•œ eâ‚ b).symm.continuousAlternatingMapCongrL (eâ‚‚.coordChangeL ğ•œ eâ‚‚' b) :
    (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) â‰ƒL[ğ•œ] (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚))

variable {eâ‚ eâ‚' eâ‚‚ eâ‚‚'}
variable [Î  x, TopologicalSpace (Eâ‚ x)] [FiberBundle Fâ‚ Eâ‚]
variable [Î  x, TopologicalSpace (Eâ‚‚ x)] [FiberBundle Fâ‚‚ Eâ‚‚]

section
variable (Fâ‚ Fâ‚‚)
variable [ContinuousSMul ğ•œ Fâ‚] [ContinuousAdd Fâ‚]

-- move this to `operator_norm`
/-- A linear isometry from a normed space `F` to a normed space `G` induces (by left-composition) a
linear isometry from operators into `F` to operators into `G`. -/
def _root_.LinearIsometry.compLeft {ğ•œ : Type*} {ğ•œâ‚‚ : Type*}
    {ğ•œâ‚ƒ : Type*} (E : Type*) {F : Type*} {G : Type*} [NormedAddCommGroup E]
    [NormedAddCommGroup F] [NormedAddCommGroup G] [NontriviallyNormedField ğ•œ]
    [NontriviallyNormedField ğ•œâ‚‚] [NontriviallyNormedField ğ•œâ‚ƒ] [NormedSpace ğ•œ E]
    [NormedSpace ğ•œâ‚‚ F] [NormedSpace ğ•œâ‚ƒ G] (Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚) {Ïƒâ‚‚â‚ƒ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ} {Ïƒâ‚â‚ƒ : ğ•œ â†’+* ğ•œâ‚ƒ}
    [RingHomCompTriple Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ] [RingHomIsometric Ïƒâ‚â‚‚] [RingHomIsometric Ïƒâ‚‚â‚ƒ]
    [RingHomIsometric Ïƒâ‚â‚ƒ] (f : F â†’â‚›â‚—áµ¢[Ïƒâ‚‚â‚ƒ] G) :
    (E â†’SL[Ïƒâ‚â‚‚] F) â†’â‚›â‚—áµ¢[Ïƒâ‚‚â‚ƒ] (E â†’SL[Ïƒâ‚â‚ƒ] G) :=
  { ContinuousLinearMap.compSL _ _ _ _ _ f.toContinuousLinearMap with
    norm_map' := fun _ â†¦ f.norm_toContinuousLinearMap_comp }

-- move this to `ContinuousMultilinearMap`
theorem _root_.ContinuousMultilinearMap.compContinuousLinearMapL_diag_continuous :
  Continuous (fun p : Fâ‚ â†’L[ğ•œ] Fâ‚ â†¦
  (ContinuousMultilinearMap.compContinuousLinearMapL (fun _ : Î¹ â†¦ p) :
    ContinuousMultilinearMap ğ•œ (fun _ â†¦ Fâ‚) Fâ‚‚ â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ (fun _ â†¦ Fâ‚) Fâ‚‚)) := by
  let Ï† : ContinuousMultilinearMap ğ•œ (fun _ : Î¹ â†¦ Fâ‚ â†’L[ğ•œ] Fâ‚) _ :=
    ContinuousMultilinearMap.compContinuousLinearMapContinuousMultilinear
    ğ•œ (fun _ : Î¹ â†¦ Fâ‚) (fun _ : Î¹ â†¦ Fâ‚) Fâ‚‚
  show Continuous (fun p : Fâ‚ â†’L[ğ•œ] Fâ‚ â†¦ Ï† (fun _ : Î¹ â†¦ p))
  apply Continuous.comp
  Â· apply ContinuousMultilinearMap.cont
  Â· apply continuous_pi
    intro _
    exact continuous_id

-- move this to `ContinuousAlternatingMap`
theorem _root_.ContinuousAlternatingMap.compContinuousLinearMapL_continuous :
    Continuous (fun p : Fâ‚ â†’L[ğ•œ] Fâ‚ â†¦
    (ContinuousAlternatingMap.compContinuousLinearMapL p :
    (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) â†’L[ğ•œ] (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚))) := by
  let Ï† : (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) â†’â‚—áµ¢[ğ•œ] _ := toContinuousMultilinearMapLinearIsometry
  let Î¦ : ((Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) â†’L[ğ•œ] (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚)) â†’â‚—áµ¢[ğ•œ] _ := Ï†.compLeft _ (RingHom.id _)
  rw [â† Î¦.comp_continuous_iff]
  show Continuous (fun p : Fâ‚ â†’L[ğ•œ] Fâ‚ â†¦
    (ContinuousMultilinearMap.compContinuousLinearMapL (fun _ â†¦ p) :
    ContinuousMultilinearMap ğ•œ (fun _ â†¦ Fâ‚) Fâ‚‚ â†’L[ğ•œ]
    ContinuousMultilinearMap ğ•œ (fun _ â†¦ Fâ‚) Fâ‚‚).comp
    (toContinuousMultilinearMapL ğ•œ))
  exact (ContinuousMultilinearMap.compContinuousLinearMapL_diag_continuous ğ•œ Î¹ Fâ‚ Fâ‚‚).clm_comp
    continuous_const

end

theorem continuousOn_continuousAlternatingMapCoordChange
    [VectorBundle ğ•œ Fâ‚ Eâ‚] [VectorBundle ğ•œ Fâ‚‚ Eâ‚‚]
    [MemTrivializationAtlas eâ‚] [MemTrivializationAtlas eâ‚']
    [MemTrivializationAtlas eâ‚‚] [MemTrivializationAtlas eâ‚‚'] :
  ContinuousOn (continuousAlternatingMapCoordChange ğ•œ Î¹ eâ‚ eâ‚' eâ‚‚ eâ‚‚')
    ((eâ‚.baseSet âˆ© eâ‚‚.baseSet) âˆ© (eâ‚'.baseSet âˆ© eâ‚‚'.baseSet)) := by
  have hâ‚ƒ := (continuousOn_coordChange ğ•œ eâ‚' eâ‚)
  have hâ‚„ := (continuousOn_coordChange ğ•œ eâ‚‚ eâ‚‚')
  let s (q : (Fâ‚ â†’L[ğ•œ] Fâ‚) Ã— (Fâ‚‚ â†’L[ğ•œ] Fâ‚‚)) :
      (Fâ‚ â†’L[ğ•œ] Fâ‚) Ã— ((Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) â†’L[ğ•œ] (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚)) :=
    (q.1, ContinuousLinearMap.compContinuousAlternatingMapL ğ•œ Fâ‚ Fâ‚‚ Fâ‚‚ q.2)
  have hs : Continuous s := continuous_id.prod_map (ContinuousLinearMap.continuous _)
  -- note: the following `refine` worked in Lean 3; in Lean 4 this times out so has been replaced by
  -- the `have`/`exact` pair with an explicitly-provided `s` argument
  -- refine ((continuous_snd.clm_comp
  --   ((ContinuousAlternatingMap.compContinuousLinearMapL_continuous ğ•œ Î¹ Fâ‚ Fâ‚‚).comp
  --   continuous_fst)).comp hs).comp_continuousOn ((hâ‚ƒ.mono ?_).prod (hâ‚„.mono ?_))
  have' := ((continuous_snd.clm_comp
    ((ContinuousAlternatingMap.compContinuousLinearMapL_continuous ğ•œ Î¹ Fâ‚ Fâ‚‚).comp
    continuous_fst)).comp hs).comp_continuousOn
    (s := (eâ‚.baseSet âˆ© eâ‚‚.baseSet âˆ© (eâ‚'.baseSet âˆ© eâ‚‚'.baseSet))) ((hâ‚ƒ.mono ?_).prod (hâ‚„.mono ?_))
  Â· exact this
  Â· mfld_set_tac
  Â· mfld_set_tac

variable (eâ‚ eâ‚' eâ‚‚ eâ‚‚')
variable [eâ‚.IsLinear ğ•œ] [eâ‚'.IsLinear ğ•œ] [eâ‚‚.IsLinear ğ•œ] [eâ‚‚'.IsLinear ğ•œ]

/-- Given trivializations `eâ‚`, `eâ‚‚` for vector bundles `Eâ‚`, `Eâ‚‚` over a base `B`,
`Pretrivialization.continuousAlternatingMap ğ•œ Î¹ eâ‚ eâ‚‚` is the induced pretrivialization for the
continuous `Î¹`-slot alternating maps from `Eâ‚` to `Eâ‚‚`. That is, the map which will later become a
trivialization, after the bundle of continuous alternating maps is equipped with the right
topological vector bundle structure. -/
def continuousAlternatingMap : Pretrivialization (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚)
    (Ï€ (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) Î›^Î¹âŸ®ğ•œ; Fâ‚, Eâ‚; Fâ‚‚, Eâ‚‚âŸ¯) where
  toFun p := âŸ¨p.1, (eâ‚‚.continuousLinearMapAt ğ•œ p.1).compContinuousAlternatingMap <|
      p.2.compContinuousLinearMap <| eâ‚.symmL ğ•œ p.1âŸ©
  invFun p := âŸ¨p.1, (eâ‚‚.symmL ğ•œ p.1).compContinuousAlternatingMap <|
      p.2.compContinuousLinearMap <| eâ‚.continuousLinearMapAt ğ•œ p.1âŸ©
  source := (Bundle.TotalSpace.proj) â»Â¹' (eâ‚.baseSet âˆ© eâ‚‚.baseSet)
  target := (eâ‚.baseSet âˆ© eâ‚‚.baseSet) Ã—Ë¢ Set.univ
  map_source' _ h := âŸ¨h, Set.mem_univ _âŸ©
  map_target' _ h := h.1
  left_inv' := fun âŸ¨x, LâŸ© âŸ¨hâ‚, hâ‚‚âŸ© â†¦ by
    rw [TotalSpace.mk_inj]
    dsimp [Bundle.continuousAlternatingMap]
    ext v
    refine (eâ‚‚.symmâ‚—_linearMapAt hâ‚‚ _).trans ?_
    dsimp
    congr
    ext i
    exact eâ‚.symmâ‚—_linearMapAt hâ‚ _
  right_inv' := fun âŸ¨x, fâŸ© âŸ¨âŸ¨hâ‚, hâ‚‚âŸ©, _âŸ© â†¦ by
    ext v
    dsimp
    refine (eâ‚‚.linearMapAt_symmâ‚— hâ‚‚ _).trans ?_
    dsimp
    congr
    ext i
    exact eâ‚.linearMapAt_symmâ‚— hâ‚ _
  open_target := (eâ‚.open_baseSet.inter eâ‚‚.open_baseSet).prod isOpen_univ
  baseSet := eâ‚.baseSet âˆ© eâ‚‚.baseSet
  open_baseSet := eâ‚.open_baseSet.inter eâ‚‚.open_baseSet
  source_eq := rfl
  target_eq := rfl
  proj_toFun _ _ := rfl

instance continuousAlternatingMap.isLinear
    [Î  x, ContinuousAdd (Eâ‚‚ x)] [Î  x, ContinuousSMul ğ•œ (Eâ‚‚ x)] :
    (Pretrivialization.continuousAlternatingMap ğ•œ Î¹ eâ‚ eâ‚‚).IsLinear ğ•œ where
  linear x _ :=
  { map_add := fun L L' â†¦ by
      show ContinuousLinearMap.compContinuousAlternatingMapâ‚— ğ•œ _ _ _
        (eâ‚‚.continuousLinearMapAt ğ•œ x)
        (ContinuousAlternatingMap.compContinuousLinearMapâ‚— (eâ‚.symmL ğ•œ x) (L + L')) = _
      rw [_root_.map_add, _root_.map_add]
      rfl
    map_smul := fun c L â†¦ by
      show ContinuousLinearMap.compContinuousAlternatingMapâ‚— ğ•œ _ _ _
        (eâ‚‚.continuousLinearMapAt ğ•œ x)
        (ContinuousAlternatingMap.compContinuousLinearMapâ‚— (eâ‚.symmL ğ•œ x) (c â€¢ L)) = _
      rw [SMulHomClass.map_smul, SMulHomClass.map_smul]
      rfl }

theorem continuousAlternatingMap_apply (p : TotalSpace (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) (Î›^Î¹âŸ®ğ•œ; Fâ‚, Eâ‚; Fâ‚‚, Eâ‚‚âŸ¯)) :
    (continuousAlternatingMap ğ•œ Î¹ eâ‚ eâ‚‚) p =
    âŸ¨p.1, (eâ‚‚.continuousLinearMapAt ğ•œ p.1).compContinuousAlternatingMap <|
        p.2.compContinuousLinearMap <| eâ‚.symmL ğ•œ p.1âŸ© :=
  rfl

theorem continuousAlternatingMap_symm_apply (p : B Ã— (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚)) :
    (continuousAlternatingMap ğ•œ Î¹ eâ‚ eâ‚‚).toLocalEquiv.symm p =
    âŸ¨p.1, (eâ‚‚.symmL ğ•œ p.1).compContinuousAlternatingMap <|
      p.2.compContinuousLinearMap <| eâ‚.continuousLinearMapAt ğ•œ p.1âŸ© :=
  rfl

@[simp] theorem baseSet_continuousAlternatingMap :
    (Pretrivialization.continuousAlternatingMap ğ•œ Î¹ eâ‚ eâ‚‚).baseSet = eâ‚.baseSet âˆ© eâ‚‚.baseSet :=
  rfl

variable [Î  x, ContinuousAdd (Eâ‚‚ x)]

theorem continuousAlternatingMap_symm_apply' {b : B} (hb : b âˆˆ eâ‚.baseSet âˆ© eâ‚‚.baseSet)
    (L : (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚)) :
    (continuousAlternatingMap ğ•œ Î¹ eâ‚ eâ‚‚).symm b L =
    (eâ‚‚.symmL ğ•œ b).compContinuousAlternatingMap
    (L.compContinuousLinearMap <| eâ‚.continuousLinearMapAt ğ•œ b) := by
  rw [symm_apply]
  Â· rfl
  exact hb

theorem continuousAlternatingMapCoordChange_apply (b : B)
  (hb : b âˆˆ (eâ‚.baseSet âˆ© eâ‚‚.baseSet) âˆ© (eâ‚'.baseSet âˆ© eâ‚‚'.baseSet)) (L : Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) :
  continuousAlternatingMapCoordChange ğ•œ Î¹ eâ‚ eâ‚' eâ‚‚ eâ‚‚' b L =
  (continuousAlternatingMap ğ•œ Î¹ eâ‚' eâ‚‚'
    (TotalSpace.mk b ((continuousAlternatingMap ğ•œ Î¹ eâ‚ eâ‚‚).symm b L))).2 := by
  ext v
  have H : eâ‚'.coordChangeL ğ•œ eâ‚ b âˆ˜ v = eâ‚.linearMapAt ğ•œ b âˆ˜ eâ‚'.symm b âˆ˜ v
  Â· ext i
    dsimp
    rw [eâ‚'.coordChangeL_apply eâ‚ âŸ¨hb.2.1, hb.1.1âŸ©, eâ‚.coe_linearMapAt_of_mem hb.1.1]
  simp [Pretrivialization.continuousAlternatingMap_apply, continuousAlternatingMapCoordChange,
    Pretrivialization.continuousAlternatingMap_symm_apply' _ _ _ _ hb.1,
    eâ‚‚.coordChangeL_apply eâ‚‚' âŸ¨hb.1.2, hb.2.2âŸ©, H]
  rw [eâ‚‚'.coe_linearMapAt_of_mem hb.2.2]
  -- FIXME this could ideally be combined with the previous simp

end Pretrivialization

open Pretrivialization
variable (Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚)
variable [Î  x : B, TopologicalSpace (Eâ‚ x)] [FiberBundle Fâ‚ Eâ‚] [VectorBundle ğ•œ Fâ‚ Eâ‚]
variable [Î  x : B, TopologicalSpace (Eâ‚‚ x)] [FiberBundle Fâ‚‚ Eâ‚‚] [VectorBundle ğ•œ Fâ‚‚ Eâ‚‚]

/-- Topology on the continuous `Î¹`-slot alternating_maps between the respective fibers at a point of
two "normable" vector bundles over the same base. Here "normable" means that the bundles have fibers
modelled on normed spaces `Fâ‚`, `Fâ‚‚` respectively.  The topology we put on the continuous `Î¹`-slot
alternating_maps is the topology coming from the norm on alternating maps from `Fâ‚` to `Fâ‚‚`. -/
instance (x : B) : TopologicalSpace (Î›^Î¹âŸ®ğ•œ; Fâ‚, Eâ‚; Fâ‚‚, Eâ‚‚âŸ¯ x) :=
  TopologicalSpace.induced
    ((Pretrivialization.continuousAlternatingMap ğ•œ Î¹
      (trivializationAt Fâ‚ Eâ‚ x) (trivializationAt Fâ‚‚ Eâ‚‚ x)) âˆ˜ TotalSpace.mk' (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) x)
    inferInstance

variable [Î  x, ContinuousAdd (Eâ‚‚ x)] [Î  x, ContinuousSMul ğ•œ (Eâ‚‚ x)]

/-- The continuous `Î¹`-slot alternating maps between two topological vector bundles form a
`VectorPrebundle` (this is an auxiliary construction for the
`VectorBundle` instance, in which the pretrivializations are collated but no topology
on the total space is yet provided). -/
def _root_.Bundle.continuousAlternatingMap.vectorPrebundle :
    VectorPrebundle ğ•œ (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) Î›^Î¹âŸ®ğ•œ; Fâ‚, Eâ‚; Fâ‚‚, Eâ‚‚âŸ¯ where
  pretrivializationAtlas :=
    {e |  âˆƒ (eâ‚ : Trivialization Fâ‚ (Ï€ Fâ‚ Eâ‚)) (eâ‚‚ : Trivialization Fâ‚‚ (Ï€ Fâ‚‚ Eâ‚‚))
      (_ : MemTrivializationAtlas eâ‚) (_ : MemTrivializationAtlas eâ‚‚),
      e = Pretrivialization.continuousAlternatingMap ğ•œ Î¹ eâ‚ eâ‚‚}
  pretrivialization_linear' := by
    rintro _ âŸ¨eâ‚, heâ‚, eâ‚‚, heâ‚‚, rflâŸ©
    infer_instance
  pretrivializationAt x := Pretrivialization.continuousAlternatingMap ğ•œ Î¹
    (trivializationAt Fâ‚ Eâ‚ x) (trivializationAt Fâ‚‚ Eâ‚‚ x)
  mem_base_pretrivializationAt x :=
    âŸ¨mem_baseSet_trivializationAt Fâ‚ Eâ‚ x, mem_baseSet_trivializationAt Fâ‚‚ Eâ‚‚ xâŸ©
  pretrivialization_mem_atlas x :=
    âŸ¨trivializationAt Fâ‚ Eâ‚ x, trivializationAt Fâ‚‚ Eâ‚‚ x, inferInstance, inferInstance, rflâŸ©
  exists_coordChange := by
    rintro _ âŸ¨eâ‚, eâ‚‚, heâ‚, heâ‚‚, rflâŸ© _ âŸ¨eâ‚', eâ‚‚', heâ‚', heâ‚‚', rflâŸ©
    exact âŸ¨continuousAlternatingMapCoordChange ğ•œ Î¹ eâ‚ eâ‚' eâ‚‚ eâ‚‚',
      continuousOn_continuousAlternatingMapCoordChange ğ•œ Î¹,
      continuousAlternatingMapCoordChange_apply ğ•œ Î¹ eâ‚ eâ‚' eâ‚‚ eâ‚‚'âŸ©
  totalSpaceMk_inducing x := âŸ¨rflâŸ©

/-- Topology on the total space of the continuous `Î¹`-slot alternating maps between two "normable"
vector bundles over the same base. -/
instance Bundle.continuousAlternatingMap.topologicalSpace_totalSpace :
    TopologicalSpace (TotalSpace (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) Î›^Î¹âŸ®ğ•œ; Fâ‚, Eâ‚; Fâ‚‚, Eâ‚‚âŸ¯) :=
  (Bundle.continuousAlternatingMap.vectorPrebundle ğ•œ Î¹ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚).totalSpaceTopology

/-- The continuous `Î¹`-slot alternating maps between two vector bundles form a fiber bundle. -/
instance _root_.Bundle.continuousAlternatingMap.fiberBundle :
    FiberBundle (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) Î›^Î¹âŸ®ğ•œ; Fâ‚, Eâ‚; Fâ‚‚, Eâ‚‚âŸ¯ :=
  (Bundle.continuousAlternatingMap.vectorPrebundle ğ•œ Î¹ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚).toFiberBundle

/-- The continuous `Î¹`-slot alternating maps between two vector bundles form a vector bundle. -/
instance _root_.Bundle.continuousAlternatingMap.vectorBundle :
    VectorBundle ğ•œ (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) Î›^Î¹âŸ®ğ•œ; Fâ‚, Eâ‚; Fâ‚‚, Eâ‚‚âŸ¯ :=
  (Bundle.continuousAlternatingMap.vectorPrebundle ğ•œ Î¹ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚).toVectorBundle

variable [heâ‚ : MemTrivializationAtlas eâ‚] [heâ‚‚ : MemTrivializationAtlas eâ‚‚] {Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚}

/-- Given trivializations `eâ‚`, `eâ‚‚` in the atlas for vector bundles `Eâ‚`, `Eâ‚‚` over a base `B`,
the induced trivialization for the continuous `Î¹`-slot alternating maps from `Eâ‚` to `Eâ‚‚`,
whose base set is `eâ‚.baseSet âˆ© eâ‚‚.baseSet`. -/
def Trivialization.continuousAlternatingMap :
    Trivialization (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) (Ï€ (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) Î›^Î¹âŸ®ğ•œ; Fâ‚, Eâ‚; Fâ‚‚, Eâ‚‚âŸ¯) :=
  VectorPrebundle.trivializationOfMemPretrivializationAtlas _ âŸ¨eâ‚, eâ‚‚, heâ‚, heâ‚‚, rflâŸ©

instance _root_.Bundle.continuousAlternatingMap.memTrivializationAtlas :
    MemTrivializationAtlas (eâ‚.continuousAlternatingMap ğ•œ Î¹ eâ‚‚ :
    Trivialization (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) (Ï€ (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) Î›^Î¹âŸ®ğ•œ; Fâ‚, Eâ‚; Fâ‚‚, Eâ‚‚âŸ¯)) where
  out := âŸ¨_, âŸ¨eâ‚, eâ‚‚, inferInstance, inferInstance, rflâŸ©, rflâŸ©

@[simp] theorem Trivialization.baseSet_continuousAlternatingMap :
    (eâ‚.continuousAlternatingMap ğ•œ Î¹ eâ‚‚).baseSet = eâ‚.baseSet âˆ© eâ‚‚.baseSet :=
  rfl

theorem Trivialization.continuousAlternatingMap_apply
    (p : TotalSpace (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) Î›^Î¹âŸ®ğ•œ; Fâ‚, Eâ‚; Fâ‚‚, Eâ‚‚âŸ¯) :
    eâ‚.continuousAlternatingMap ğ•œ Î¹ eâ‚‚ p =
    âŸ¨p.1, (eâ‚‚.continuousLinearMapAt ğ•œ p.1).compContinuousAlternatingMap <|
      p.2.compContinuousLinearMap <| eâ‚.symmL ğ•œ p.1âŸ© :=
  rfl

@[simp, mfld_simps]
theorem continuousAlternatingMap_trivializationAt_source (xâ‚€ : B) :
    (trivializationAt (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) Î›^Î¹âŸ®ğ•œ; Fâ‚, Eâ‚; Fâ‚‚, Eâ‚‚âŸ¯ xâ‚€).source =
    Ï€ (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) Î›^Î¹âŸ®ğ•œ; Fâ‚, Eâ‚; Fâ‚‚, Eâ‚‚âŸ¯ â»Â¹'
      ((trivializationAt Fâ‚ Eâ‚ xâ‚€).baseSet âˆ© (trivializationAt Fâ‚‚ Eâ‚‚ xâ‚€).baseSet) :=
  rfl

@[simp, mfld_simps]
theorem continuousAlternatingMap_trivializationAt_target (xâ‚€ : B) :
    (trivializationAt (Fâ‚ [Î›^Î¹]â†’L[ğ•œ] Fâ‚‚) Î›^Î¹âŸ®ğ•œ; Fâ‚, Eâ‚; Fâ‚‚, Eâ‚‚âŸ¯ xâ‚€).target =
    ((trivializationAt Fâ‚ Eâ‚ xâ‚€).baseSet âˆ© (trivializationAt Fâ‚‚ Eâ‚‚ xâ‚€).baseSet) Ã—Ë¢ Set.univ :=
  rfl
