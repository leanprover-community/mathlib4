/-
Copyright (c) 2022 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri, SÃ©bastien GouÃ«zel, Heather Macbeth, Floris van Doorn
-/
import Mathlib.Topology.FiberBundle.Constructions
import Mathlib.Topology.VectorBundle.Basic
import Mathlib.Analysis.NormedSpace.OperatorNorm.Prod

/-!
# Standard constructions on vector bundles

This file contains several standard constructions on vector bundles:

* `Bundle.Trivial.vectorBundle ğ•œ B F`: the trivial vector bundle with scalar field `ğ•œ` and model
  fiber `F` over the base `B`

* `VectorBundle.prod`: for vector bundles `Eâ‚` and `Eâ‚‚` with scalar field `ğ•œ` over a common base,
  a vector bundle structure on their direct sum `Eâ‚ Ã—áµ‡ Eâ‚‚` (the notation stands for
  `fun x â†¦ Eâ‚ x Ã— Eâ‚‚ x`).

* `VectorBundle.pullback`: for a vector bundle `E` over `B`, a vector bundle structure on its
  pullback `f *áµ– E` by a map `f : B' â†’ B` (the notation is a type synonym for `E âˆ˜ f`).

## Tags
Vector bundle, direct sum, pullback
-/

noncomputable section

open Bundle Set FiberBundle

/-! ### The trivial vector bundle -/

namespace Bundle.Trivial

variable (ğ•œ : Type*) (B : Type*) (F : Type*) [NontriviallyNormedField ğ•œ] [NormedAddCommGroup F]
  [NormedSpace ğ•œ F] [TopologicalSpace B]

instance trivialization.isLinear : (trivialization B F).IsLinear ğ•œ where
  linear _ _ := âŸ¨fun _ _ => rfl, fun _ _ => rflâŸ©

variable {ğ•œ} in
theorem trivialization.coordChangeL (b : B) :
    (trivialization B F).coordChangeL ğ•œ (trivialization B F) b =
      ContinuousLinearEquiv.refl ğ•œ F := by
  ext v
  rw [Trivialization.coordChangeL_apply']
  exacts [rfl, âŸ¨mem_univ _, mem_univ _âŸ©]

instance vectorBundle : VectorBundle ğ•œ F (Bundle.Trivial B F) where
  trivialization_linear' e he := by
    rw [eq_trivialization B F e]
    infer_instance
  continuousOn_coordChange' e e' he he' := by
    obtain rfl := eq_trivialization B F e
    obtain rfl := eq_trivialization B F e'
    simp only [trivialization.coordChangeL]
    exact continuous_const.continuousOn

@[simp] lemma linearMapAt_trivialization (x : B) :
    (trivialization B F).linearMapAt ğ•œ x = LinearMap.id := by
  ext v
  rw [Trivialization.coe_linearMapAt_of_mem _ (by simp)]
  rfl

@[simp] lemma continuousLinearMapAt_trivialization (x : B) :
    (trivialization B F).continuousLinearMapAt ğ•œ x = ContinuousLinearMap.id ğ•œ F := by
  ext; simp

@[simp] lemma symmâ‚—_trivialization (x : B) :
    (trivialization B F).symmâ‚— ğ•œ x = LinearMap.id := by
  ext; simp [Trivialization.coe_symmâ‚—, trivialization_symm_apply B F]

@[simp] lemma symmL_trivialization (x : B) :
    (trivialization B F).symmL ğ•œ x = ContinuousLinearMap.id ğ•œ F := by
  ext; simp [trivialization_symm_apply B F]

@[simp] lemma continuousLinearEquivAt_trivialization (x : B) :
    (trivialization B F).continuousLinearEquivAt ğ•œ x (mem_univ _) =
      ContinuousLinearEquiv.refl ğ•œ F := by
  ext; simp

end Bundle.Trivial

/-! ### Direct sum of two vector bundles -/

section

variable (ğ•œ : Type*) {B : Type*} [NontriviallyNormedField ğ•œ] [TopologicalSpace B] (Fâ‚ : Type*)
  [NormedAddCommGroup Fâ‚] [NormedSpace ğ•œ Fâ‚] (Eâ‚ : B â†’ Type*) [TopologicalSpace (TotalSpace Fâ‚ Eâ‚)]
  (Fâ‚‚ : Type*) [NormedAddCommGroup Fâ‚‚] [NormedSpace ğ•œ Fâ‚‚] (Eâ‚‚ : B â†’ Type*)
  [TopologicalSpace (TotalSpace Fâ‚‚ Eâ‚‚)]

namespace Trivialization

variable {Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚}
variable [âˆ€ x, AddCommMonoid (Eâ‚ x)] [âˆ€ x, Module ğ•œ (Eâ‚ x)]
  [âˆ€ x, AddCommMonoid (Eâ‚‚ x)] [âˆ€ x, Module ğ•œ (Eâ‚‚ x)] (eâ‚ eâ‚' : Trivialization Fâ‚ (Ï€ Fâ‚ Eâ‚))
  (eâ‚‚ eâ‚‚' : Trivialization Fâ‚‚ (Ï€ Fâ‚‚ Eâ‚‚))

instance prod.isLinear [eâ‚.IsLinear ğ•œ] [eâ‚‚.IsLinear ğ•œ] : (eâ‚.prod eâ‚‚).IsLinear ğ•œ where
  linear := fun _ âŸ¨hâ‚, hâ‚‚âŸ© =>
    (((eâ‚.linear ğ•œ hâ‚).mk' _).prodMap ((eâ‚‚.linear ğ•œ hâ‚‚).mk' _)).isLinear

@[simp]
theorem coordChangeL_prod [eâ‚.IsLinear ğ•œ] [eâ‚'.IsLinear ğ•œ] [eâ‚‚.IsLinear ğ•œ] [eâ‚‚'.IsLinear ğ•œ] â¦ƒbâ¦„
    (hb : (b âˆˆ eâ‚.baseSet âˆ§ b âˆˆ eâ‚‚.baseSet) âˆ§ b âˆˆ eâ‚'.baseSet âˆ§ b âˆˆ eâ‚‚'.baseSet) :
    ((eâ‚.prod eâ‚‚).coordChangeL ğ•œ (eâ‚'.prod eâ‚‚') b : Fâ‚ Ã— Fâ‚‚ â†’L[ğ•œ] Fâ‚ Ã— Fâ‚‚) =
      (eâ‚.coordChangeL ğ•œ eâ‚' b : Fâ‚ â†’L[ğ•œ] Fâ‚).prodMap (eâ‚‚.coordChangeL ğ•œ eâ‚‚' b) := by
  rw [ContinuousLinearMap.ext_iff, ContinuousLinearMap.coe_prodMap']
  rintro âŸ¨vâ‚, vâ‚‚âŸ©
  change
    (eâ‚.prod eâ‚‚).coordChangeL ğ•œ (eâ‚'.prod eâ‚‚') b (vâ‚, vâ‚‚) =
      (eâ‚.coordChangeL ğ•œ eâ‚' b vâ‚, eâ‚‚.coordChangeL ğ•œ eâ‚‚' b vâ‚‚)
  rw [eâ‚.coordChangeL_apply eâ‚', eâ‚‚.coordChangeL_apply eâ‚‚', (eâ‚.prod eâ‚‚).coordChangeL_apply']
  exacts [rfl, hb, âŸ¨hb.1.2, hb.2.2âŸ©, âŸ¨hb.1.1, hb.2.1âŸ©]

variable {eâ‚ eâ‚‚} [âˆ€ x : B, TopologicalSpace (Eâ‚ x)] [âˆ€ x : B, TopologicalSpace (Eâ‚‚ x)]
  [FiberBundle Fâ‚ Eâ‚] [FiberBundle Fâ‚‚ Eâ‚‚]

theorem prod_apply' [eâ‚.IsLinear ğ•œ] [eâ‚‚.IsLinear ğ•œ] {x : B} (hxâ‚ : x âˆˆ eâ‚.baseSet)
    (hxâ‚‚ : x âˆˆ eâ‚‚.baseSet) (vâ‚ : Eâ‚ x) (vâ‚‚ : Eâ‚‚ x) :
    prod eâ‚ eâ‚‚ âŸ¨x, (vâ‚, vâ‚‚)âŸ© =
      âŸ¨x, eâ‚.continuousLinearEquivAt ğ•œ x hxâ‚ vâ‚, eâ‚‚.continuousLinearEquivAt ğ•œ x hxâ‚‚ vâ‚‚âŸ© :=
  rfl

end Trivialization

open Trivialization

variable [âˆ€ x, AddCommMonoid (Eâ‚ x)] [âˆ€ x, Module ğ•œ (Eâ‚ x)] [âˆ€ x, AddCommMonoid (Eâ‚‚ x)]
  [âˆ€ x, Module ğ•œ (Eâ‚‚ x)] [âˆ€ x : B, TopologicalSpace (Eâ‚ x)] [âˆ€ x : B, TopologicalSpace (Eâ‚‚ x)]
  [FiberBundle Fâ‚ Eâ‚] [FiberBundle Fâ‚‚ Eâ‚‚]

/-- The product of two vector bundles is a vector bundle. -/
instance VectorBundle.prod [VectorBundle ğ•œ Fâ‚ Eâ‚] [VectorBundle ğ•œ Fâ‚‚ Eâ‚‚] :
    VectorBundle ğ•œ (Fâ‚ Ã— Fâ‚‚) (Eâ‚ Ã—áµ‡ Eâ‚‚) where
  trivialization_linear' := by
    rintro _ âŸ¨eâ‚, eâ‚‚, heâ‚, heâ‚‚, rflâŸ©
    infer_instance
  continuousOn_coordChange' := by
    rintro _ _ âŸ¨eâ‚, eâ‚‚, heâ‚, heâ‚‚, rflâŸ© âŸ¨eâ‚', eâ‚‚', heâ‚', heâ‚‚', rflâŸ©
    refine (((continuousOn_coordChange ğ•œ eâ‚ eâ‚').mono ?_).prod_mapL ğ•œ
      ((continuousOn_coordChange ğ•œ eâ‚‚ eâ‚‚').mono ?_)).congr ?_ <;>
      dsimp only [prod_baseSet, mfld_simps]
    Â· mfld_set_tac
    Â· mfld_set_tac
    Â· rintro b hb
      rw [ContinuousLinearMap.ext_iff]
      rintro âŸ¨vâ‚, vâ‚‚âŸ©
      change (eâ‚.prod eâ‚‚).coordChangeL ğ•œ (eâ‚'.prod eâ‚‚') b (vâ‚, vâ‚‚) =
        (eâ‚.coordChangeL ğ•œ eâ‚' b vâ‚, eâ‚‚.coordChangeL ğ•œ eâ‚‚' b vâ‚‚)
      rw [eâ‚.coordChangeL_apply eâ‚', eâ‚‚.coordChangeL_apply eâ‚‚', (eâ‚.prod eâ‚‚).coordChangeL_apply']
      exacts [rfl, hb, âŸ¨hb.1.2, hb.2.2âŸ©, âŸ¨hb.1.1, hb.2.1âŸ©]

variable {ğ•œ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚}

@[simp]
theorem Trivialization.continuousLinearEquivAt_prod {eâ‚ : Trivialization Fâ‚ (Ï€ Fâ‚ Eâ‚)}
    {eâ‚‚ : Trivialization Fâ‚‚ (Ï€ Fâ‚‚ Eâ‚‚)} [eâ‚.IsLinear ğ•œ] [eâ‚‚.IsLinear ğ•œ] {x : B}
    (hx : x âˆˆ (eâ‚.prod eâ‚‚).baseSet) :
    (eâ‚.prod eâ‚‚).continuousLinearEquivAt ğ•œ x hx =
      (eâ‚.continuousLinearEquivAt ğ•œ x hx.1).prodCongr (eâ‚‚.continuousLinearEquivAt ğ•œ x hx.2) := by
  ext v : 2
  obtain âŸ¨vâ‚, vâ‚‚âŸ© := v
  rw [(eâ‚.prod eâ‚‚).continuousLinearEquivAt_apply ğ•œ, Trivialization.prod]
  exact (congr_arg Prod.snd (prod_apply' ğ•œ hx.1 hx.2 vâ‚ vâ‚‚) :)

end

/-! ### Pullbacks of vector bundles -/

section

variable (R ğ•œ : Type*) {B : Type*} (F : Type*) (E : B â†’ Type*) {B' : Type*} (f : B' â†’ B)

instance [i : âˆ€ x : B, AddCommMonoid (E x)] (x : B') : AddCommMonoid ((f *áµ– E) x) := i _

instance [Semiring R] [âˆ€ x : B, AddCommMonoid (E x)] [i : âˆ€ x, Module R (E x)] (x : B') :
    Module R ((f *áµ– E) x) := i _

variable {E F} [TopologicalSpace B'] [TopologicalSpace (TotalSpace F E)] [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] [TopologicalSpace B] [âˆ€ x, AddCommMonoid (E x)]
  [âˆ€ x, Module ğ•œ (E x)] {K : Type*} [FunLike K B' B] [ContinuousMapClass K B' B]

instance Trivialization.pullback_linear (e : Trivialization F (Ï€ F E)) [e.IsLinear ğ•œ] (f : K) :
    (Trivialization.pullback (B' := B') e f).IsLinear ğ•œ where
  linear _ h := e.linear ğ•œ h

instance VectorBundle.pullback [âˆ€ x, TopologicalSpace (E x)] [FiberBundle F E] [VectorBundle ğ•œ F E]
    (f : K) : VectorBundle ğ•œ F ((f : B' â†’ B) *áµ– E) where
  trivialization_linear' := by
    rintro _ âŸ¨e, he, rflâŸ©
    infer_instance
  continuousOn_coordChange' := by
    rintro _ _ âŸ¨e, he, rflâŸ© âŸ¨e', he', rflâŸ©
    refine ((continuousOn_coordChange ğ•œ e e').comp
      (map_continuous f).continuousOn fun b hb => hb).congr ?_
    rintro b (hb : f b âˆˆ e.baseSet âˆ© e'.baseSet); ext v
    change ((e.pullback f).coordChangeL ğ•œ (e'.pullback f) b) v = (e.coordChangeL ğ•œ e' (f b)) v
    rw [e.coordChangeL_apply e' hb, (e.pullback f).coordChangeL_apply' _]
    exacts [rfl, hb]

end
