/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Topology.ContinuousOn

/-!
### Continuity of piecewise defined functions
-/

open Set Filter Function Topology Filter

variable {Î± Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²]
  {f g : Î± â†’ Î²} {s s' t : Set Î±} {x : Î±}


@[simp]
theorem continuousWithinAt_update_same [DecidableEq Î±] {y : Î²} :
    ContinuousWithinAt (update f x y) s x â†” Tendsto f (ğ“[s \ {x}] x) (ğ“ y) :=
  calc
    ContinuousWithinAt (update f x y) s x â†” Tendsto (update f x y) (ğ“[s \ {x}] x) (ğ“ y) := by
    { rw [â† continuousWithinAt_diff_self, ContinuousWithinAt, update_self] }
    _ â†” Tendsto f (ğ“[s \ {x}] x) (ğ“ y) :=
      tendsto_congr' <| eventually_nhdsWithin_iff.2 <| Eventually.of_forall
        fun _ hz => update_of_ne hz.2 ..

@[simp]
theorem continuousAt_update_same [DecidableEq Î±] {y : Î²} :
    ContinuousAt (Function.update f x y) x â†” Tendsto f (ğ“[â‰ ] x) (ğ“ y) := by
  rw [â† continuousWithinAt_univ, continuousWithinAt_update_same, compl_eq_univ_diff]

theorem ContinuousOn.if' {s : Set Î±} {p : Î± â†’ Prop} {f g : Î± â†’ Î²} [âˆ€ a, Decidable (p a)]
    (hpf : âˆ€ a âˆˆ s âˆ© frontier { a | p a },
      Tendsto f (ğ“[s âˆ© { a | p a }] a) (ğ“ <| if p a then f a else g a))
    (hpg :
      âˆ€ a âˆˆ s âˆ© frontier { a | p a },
        Tendsto g (ğ“[s âˆ© { a | Â¬p a }] a) (ğ“ <| if p a then f a else g a))
    (hf : ContinuousOn f <| s âˆ© { a | p a }) (hg : ContinuousOn g <| s âˆ© { a | Â¬p a }) :
    ContinuousOn (fun a => if p a then f a else g a) s := by
  intro x hx
  by_cases hx' : x âˆˆ frontier { a | p a }
  Â· exact (hpf x âŸ¨hx, hx'âŸ©).piecewise_nhdsWithin (hpg x âŸ¨hx, hx'âŸ©)
  Â· rw [â† inter_univ s, â† union_compl_self { a | p a }, inter_union_distrib_left] at hx âŠ¢
    rcases hx with hx | hx
    Â· apply ContinuousWithinAt.union
      Â· exact (hf x hx).congr (fun y hy => if_pos hy.2) (if_pos hx.2)
      Â· have : x âˆ‰ closure { a | p a }á¶œ := fun h => hx' âŸ¨subset_closure hx.2, by
          rwa [closure_compl] at hâŸ©
        exact continuousWithinAt_of_notMem_closure fun h =>
          this (closure_inter_subset_inter_closure _ _ h).2
    Â· apply ContinuousWithinAt.union
      Â· have : x âˆ‰ closure { a | p a } := fun h =>
          hx' âŸ¨h, fun h' : x âˆˆ interior { a | p a } => hx.2 (interior_subset h')âŸ©
        exact continuousWithinAt_of_notMem_closure fun h =>
          this (closure_inter_subset_inter_closure _ _ h).2
      Â· exact (hg x hx).congr (fun y hy => if_neg hy.2) (if_neg hx.2)

theorem ContinuousOn.piecewise' [âˆ€ a, Decidable (a âˆˆ t)]
    (hpf : âˆ€ a âˆˆ s âˆ© frontier t, Tendsto f (ğ“[s âˆ© t] a) (ğ“ (piecewise t f g a)))
    (hpg : âˆ€ a âˆˆ s âˆ© frontier t, Tendsto g (ğ“[s âˆ© tá¶œ] a) (ğ“ (piecewise t f g a)))
    (hf : ContinuousOn f <| s âˆ© t) (hg : ContinuousOn g <| s âˆ© tá¶œ) :
    ContinuousOn (piecewise t f g) s :=
  hf.if' hpf hpg hg

theorem ContinuousOn.if {p : Î± â†’ Prop} [âˆ€ a, Decidable (p a)]
    (hp : âˆ€ a âˆˆ s âˆ© frontier { a | p a }, f a = g a)
    (hf : ContinuousOn f <| s âˆ© closure { a | p a })
    (hg : ContinuousOn g <| s âˆ© closure { a | Â¬p a }) :
    ContinuousOn (fun a => if p a then f a else g a) s := by
  apply ContinuousOn.if'
  Â· rintro a ha
    simp only [â† hp a ha, ite_self]
    apply tendsto_nhdsWithin_mono_left (inter_subset_inter_right s subset_closure)
    exact hf a âŸ¨ha.1, ha.2.1âŸ©
  Â· rintro a ha
    simp only [hp a ha, ite_self]
    apply tendsto_nhdsWithin_mono_left (inter_subset_inter_right s subset_closure)
    rcases ha with âŸ¨has, âŸ¨_, haâŸ©âŸ©
    rw [â† mem_compl_iff, â† closure_compl] at ha
    apply hg a âŸ¨has, haâŸ©
  Â· exact hf.mono (inter_subset_inter_right s subset_closure)
  Â· exact hg.mono (inter_subset_inter_right s subset_closure)

theorem ContinuousOn.piecewise [âˆ€ a, Decidable (a âˆˆ t)]
    (ht : âˆ€ a âˆˆ s âˆ© frontier t, f a = g a) (hf : ContinuousOn f <| s âˆ© closure t)
    (hg : ContinuousOn g <| s âˆ© closure tá¶œ) : ContinuousOn (piecewise t f g) s :=
  hf.if ht hg

theorem continuous_if' {p : Î± â†’ Prop} [âˆ€ a, Decidable (p a)]
    (hpf : âˆ€ a âˆˆ frontier { x | p x }, Tendsto f (ğ“[{ x | p x }] a) (ğ“ <| ite (p a) (f a) (g a)))
    (hpg : âˆ€ a âˆˆ frontier { x | p x }, Tendsto g (ğ“[{ x | Â¬p x }] a) (ğ“ <| ite (p a) (f a) (g a)))
    (hf : ContinuousOn f { x | p x }) (hg : ContinuousOn g { x | Â¬p x }) :
    Continuous fun a => ite (p a) (f a) (g a) := by
  rw [â† continuousOn_univ]
  apply ContinuousOn.if' <;> simp [*] <;> assumption

theorem continuous_if {p : Î± â†’ Prop} [âˆ€ a, Decidable (p a)]
    (hp : âˆ€ a âˆˆ frontier { x | p x }, f a = g a) (hf : ContinuousOn f (closure { x | p x }))
    (hg : ContinuousOn g (closure { x | Â¬p x })) :
    Continuous fun a => if p a then f a else g a := by
  rw [â† continuousOn_univ]
  apply ContinuousOn.if <;> simpa

theorem Continuous.if {p : Î± â†’ Prop} [âˆ€ a, Decidable (p a)]
    (hp : âˆ€ a âˆˆ frontier { x | p x }, f a = g a) (hf : Continuous f) (hg : Continuous g) :
    Continuous fun a => if p a then f a else g a :=
  continuous_if hp hf.continuousOn hg.continuousOn

theorem continuous_if_const (p : Prop) [Decidable p] (hf : p â†’ Continuous f)
    (hg : Â¬p â†’ Continuous g) : Continuous fun a => if p then f a else g a := by
  split_ifs with h
  exacts [hf h, hg h]

theorem Continuous.if_const (p : Prop) [Decidable p] (hf : Continuous f)
    (hg : Continuous g) : Continuous fun a => if p then f a else g a :=
  continuous_if_const p (fun _ => hf) fun _ => hg

theorem continuous_piecewise [âˆ€ a, Decidable (a âˆˆ s)]
    (hs : âˆ€ a âˆˆ frontier s, f a = g a) (hf : ContinuousOn f (closure s))
    (hg : ContinuousOn g (closure sá¶œ)) : Continuous (piecewise s f g) :=
  continuous_if hs hf hg

theorem Continuous.piecewise [âˆ€ a, Decidable (a âˆˆ s)]
    (hs : âˆ€ a âˆˆ frontier s, f a = g a) (hf : Continuous f) (hg : Continuous g) :
    Continuous (piecewise s f g) :=
  hf.if hs hg

theorem IsOpen.ite' (hs : IsOpen s) (hs' : IsOpen s')
    (ht : âˆ€ x âˆˆ frontier t, x âˆˆ s â†” x âˆˆ s') : IsOpen (t.ite s s') := by
  classical
    simp only [isOpen_iff_continuous_mem, Set.ite] at *
    convert
      continuous_piecewise (fun x hx => propext (ht x hx)) hs.continuousOn hs'.continuousOn using 2
    rename_i x
    by_cases hx : x âˆˆ t <;> simp [hx]

theorem IsOpen.ite (hs : IsOpen s) (hs' : IsOpen s')
    (ht : s âˆ© frontier t = s' âˆ© frontier t) : IsOpen (t.ite s s') :=
  hs.ite' hs' fun x hx => by simpa [hx] using Set.ext_iff.1 ht x

theorem ite_inter_closure_eq_of_inter_frontier_eq
    (ht : s âˆ© frontier t = s' âˆ© frontier t) : t.ite s s' âˆ© closure t = s âˆ© closure t := by
  rw [closure_eq_self_union_frontier, inter_union_distrib_left, inter_union_distrib_left,
    ite_inter_self, ite_inter_of_inter_eq _ ht]

theorem ite_inter_closure_compl_eq_of_inter_frontier_eq
    (ht : s âˆ© frontier t = s' âˆ© frontier t) : t.ite s s' âˆ© closure tá¶œ = s' âˆ© closure tá¶œ := by
  rw [â† ite_compl, ite_inter_closure_eq_of_inter_frontier_eq]
  rwa [frontier_compl, eq_comm]

theorem continuousOn_piecewise_ite' [âˆ€ x, Decidable (x âˆˆ t)]
    (h : ContinuousOn f (s âˆ© closure t)) (h' : ContinuousOn g (s' âˆ© closure tá¶œ))
    (H : s âˆ© frontier t = s' âˆ© frontier t) (Heq : EqOn f g (s âˆ© frontier t)) :
    ContinuousOn (t.piecewise f g) (t.ite s s') := by
  apply ContinuousOn.piecewise
  Â· rwa [ite_inter_of_inter_eq _ H]
  Â· rwa [ite_inter_closure_eq_of_inter_frontier_eq H]
  Â· rwa [ite_inter_closure_compl_eq_of_inter_frontier_eq H]

theorem continuousOn_piecewise_ite [âˆ€ x, Decidable (x âˆˆ t)]
    (h : ContinuousOn f s) (h' : ContinuousOn g s') (H : s âˆ© frontier t = s' âˆ© frontier t)
    (Heq : EqOn f g (s âˆ© frontier t)) : ContinuousOn (t.piecewise f g) (t.ite s s') :=
  continuousOn_piecewise_ite' (h.mono inter_subset_left) (h'.mono inter_subset_left) H Heq
