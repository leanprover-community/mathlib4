/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle S√∂nne, Adam Topaz
-/
import Mathlib.Topology.Separation
import Mathlib.Topology.SubsetProperties
import Mathlib.Topology.LocallyConstant.Basic

#align_import topology.discrete_quotient from "leanprover-community/mathlib"@"d101e93197bb5f6ea89bd7ba386b7f7dff1f3903"

/-!

# Discrete quotients of a topological space.

This file defines the type of discrete quotients of a topological space,
denoted `DiscreteQuotient X`. To avoid quantifying over types, we model such
quotients as setoids whose equivalence classes are clopen.

## Definitions
1. `DiscreteQuotient X` is the type of discrete quotients of `X`.
  It is endowed with a coercion to `Type`, which is defined as the
  quotient associated to the setoid in question, and each such quotient
  is endowed with the discrete topology.
2. Given `S : DiscreteQuotient X`, the projection `X ‚Üí S` is denoted
  `S.proj`.
3. When `X` is compact and `S : DiscreteQuotient X`, the space `S` is
  endowed with a `Fintype` instance.

## Order structure

The type `DiscreteQuotient X` is endowed with an instance of a `SemilatticeInf` with `OrderTop`.
The partial ordering `A ‚â§ B` mathematically means that `B.proj` factors through `A.proj`.
The top element `‚ä§` is the trivial quotient, meaning that every element of `X` is collapsed
to a point. Given `h : A ‚â§ B`, the map `A ‚Üí B` is `DiscreteQuotient.ofLE h`.

Whenever `X` is a locally connected space, the type `DiscreteQuotient X` is also endowed with an
instance of an `OrderBot`, where the bot element `‚ä•` is given by the `connectedComponentSetoid`,
i.e., `x ~ y` means that `x` and `y` belong to the same connected component. In particular, if `X`
is a discrete topological space, then `x ~ y` is equivalent (propositionally, not definitionally) to
`x = y`.

Given `f : C(X, Y)`, we define a predicate `DiscreteQuotient.LEComap f A B` for
`A : DiscreteQuotient X` and `B : DiscreteQuotient Y`, asserting that `f` descends to `A ‚Üí B`. If
`cond : DiscreteQuotient.LEComap h A B`, the function `A ‚Üí B` is obtained by
`DiscreteQuotient.map f cond`.

## Theorems

The two main results proved in this file are:

1. `DiscreteQuotient.eq_of_forall_proj_eq` which states that when `X` is compact, T‚ÇÇ, and totally
  disconnected, any two elements of `X` are equal if their projections in `Q` agree for all
  `Q : DiscreteQuotient X`.

2. `DiscreteQuotient.exists_of_compat` which states that when `X` is compact, then any
  system of elements of `Q` as `Q : DiscreteQuotient X` varies, which is compatible with
  respect to `DiscreteQuotient.ofLE`, must arise from some element of `X`.

## Remarks
The constructions in this file will be used to show that any profinite space is a limit
of finite discrete spaces.
-/


open Set Function

variable {Œ± X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]

/-- The type of discrete quotients of a topological space. -/
@[ext] -- porting note: in Lean 4, uses projection to `r` instead of `Setoid`.
structure DiscreteQuotient (X : Type*) [TopologicalSpace X] extends Setoid X where
  /-- For every point `x`, the set `{ y | Rel x y }` is a clopen set. -/
  protected isOpen_setOf_rel : ‚àÄ x, IsOpen (setOf (toSetoid.Rel x))
#align discrete_quotient DiscreteQuotient

namespace DiscreteQuotient

variable (S : DiscreteQuotient X)

-- porting note: new lemma
lemma toSetoid_injective : Function.Injective (@toSetoid X _)
  | ‚ü®_, _‚ü©, ‚ü®_, _‚ü©, _ => by congr
                            -- üéâ no goals

/-- Construct a discrete quotient from a clopen set. -/
def ofClopen {A : Set X} (h : IsClopen A) : DiscreteQuotient X where
  toSetoid := ‚ü®fun x y => x ‚àà A ‚Üî y ‚àà A, fun _ => Iff.rfl, Iff.symm, Iff.trans‚ü©
  isOpen_setOf_rel x := by by_cases hx : x ‚àà A <;> simp [Setoid.Rel, hx, h.1, h.2, ‚Üê compl_setOf]
                           -- ‚ä¢ IsOpen (setOf (Setoid.Rel { r := fun x y => x ‚àà A ‚Üî y ‚àà A, iseqv := (_ : Equ ‚Ä¶
                                                   -- üéâ no goals
                                                   -- üéâ no goals
#align discrete_quotient.of_clopen DiscreteQuotient.ofClopen

theorem refl : ‚àÄ x, S.Rel x x := S.refl'
#align discrete_quotient.refl DiscreteQuotient.refl

theorem symm (x y : X) : S.Rel x y ‚Üí S.Rel y x := S.symm'
#align discrete_quotient.symm DiscreteQuotient.symm

theorem trans (x y z : X) : S.Rel x y ‚Üí S.Rel y z ‚Üí S.Rel x z := S.trans'
#align discrete_quotient.trans DiscreteQuotient.trans

/-- The setoid whose quotient yields the discrete quotient. -/
add_decl_doc toSetoid

instance : CoeSort (DiscreteQuotient X) (Type _) :=
  ‚ü®fun S => Quotient S.toSetoid‚ü©

instance : TopologicalSpace S :=
  inferInstanceAs (TopologicalSpace (Quotient S.toSetoid))

/-- The projection from `X` to the given discrete quotient. -/
def proj : X ‚Üí S := Quotient.mk''
#align discrete_quotient.proj DiscreteQuotient.proj

theorem fiber_eq (x : X) : S.proj ‚Åª¬π' {S.proj x} = setOf (S.Rel x) :=
  Set.ext fun _ => eq_comm.trans Quotient.eq''
#align discrete_quotient.fiber_eq DiscreteQuotient.fiber_eq

theorem proj_surjective : Function.Surjective S.proj :=
  Quotient.surjective_Quotient_mk''
#align discrete_quotient.proj_surjective DiscreteQuotient.proj_surjective

theorem proj_quotientMap : QuotientMap S.proj :=
  quotientMap_quot_mk
#align discrete_quotient.proj_quotient_map DiscreteQuotient.proj_quotientMap

theorem proj_continuous : Continuous S.proj :=
  S.proj_quotientMap.continuous
#align discrete_quotient.proj_continuous DiscreteQuotient.proj_continuous

instance : DiscreteTopology S :=
  singletons_open_iff_discrete.1 <| S.proj_surjective.forall.2 fun x => by
    rw [‚Üê S.proj_quotientMap.isOpen_preimage, fiber_eq]
    -- ‚ä¢ IsOpen (setOf (Setoid.Rel S.toSetoid x))
    exact S.isOpen_setOf_rel _
    -- üéâ no goals

theorem proj_isLocallyConstant : IsLocallyConstant S.proj :=
  (IsLocallyConstant.iff_continuous S.proj).2 S.proj_continuous
#align discrete_quotient.proj_is_locally_constant DiscreteQuotient.proj_isLocallyConstant

theorem isClopen_preimage (A : Set S) : IsClopen (S.proj ‚Åª¬π' A) :=
  (isClopen_discrete A).preimage S.proj_continuous
#align discrete_quotient.is_clopen_preimage DiscreteQuotient.isClopen_preimage

theorem isOpen_preimage (A : Set S) : IsOpen (S.proj ‚Åª¬π' A) :=
  (S.isClopen_preimage A).1
#align discrete_quotient.is_open_preimage DiscreteQuotient.isOpen_preimage

theorem isClosed_preimage (A : Set S) : IsClosed (S.proj ‚Åª¬π' A) :=
  (S.isClopen_preimage A).2
#align discrete_quotient.is_closed_preimage DiscreteQuotient.isClosed_preimage

theorem isClopen_setOf_rel (x : X) : IsClopen (setOf (S.Rel x)) := by
  rw [‚Üê fiber_eq]
  -- ‚ä¢ IsClopen (proj S ‚Åª¬π' {proj S x})
  apply isClopen_preimage
  -- üéâ no goals
#align discrete_quotient.is_clopen_set_of_rel DiscreteQuotient.isClopen_setOf_rel

instance : Inf (DiscreteQuotient X) :=
  ‚ü®fun S‚ÇÅ S‚ÇÇ => ‚ü®S‚ÇÅ.1 ‚äì S‚ÇÇ.1, fun x => (S‚ÇÅ.2 x).inter (S‚ÇÇ.2 x)‚ü©‚ü©

instance : SemilatticeInf (DiscreteQuotient X) :=
  Injective.semilatticeInf toSetoid toSetoid_injective fun _ _ => rfl

instance : OrderTop (DiscreteQuotient X) where
  top := ‚ü®‚ä§, fun _ => isOpen_univ‚ü©
  le_top a := by tauto
                 -- üéâ no goals

instance : Inhabited (DiscreteQuotient X) := ‚ü®‚ä§‚ü©

instance inhabitedQuotient [Inhabited X] : Inhabited S := ‚ü®S.proj default‚ü©
#align discrete_quotient.inhabited_quotient DiscreteQuotient.inhabitedQuotient

-- porting note: TODO: add instances about `Nonempty (Quot _)`/`Nonempty (Quotient _)`
instance [Nonempty X] : Nonempty S := Nonempty.map S.proj ‚Äπ_‚Ä∫

-- porting note: new lemma
/-- The quotient by `‚ä§ : DiscreteQuotient X` is a `Subsingleton`. -/
instance : Subsingleton (‚ä§ : DiscreteQuotient X) where
  allEq := by rintro ‚ü®_‚ü© ‚ü®_‚ü©; exact Quotient.sound trivial
              -- ‚ä¢ Quot.mk Setoid.r a‚úù¬π = Quot.mk Setoid.r a‚úù
                              -- üéâ no goals

section Comap

variable (g : C(Y, Z)) (f : C(X, Y))

/-- Comap a discrete quotient along a continuous map. -/
def comap (S : DiscreteQuotient Y) : DiscreteQuotient X where
  toSetoid := Setoid.comap f S.1
  isOpen_setOf_rel _ := (S.2 _).preimage f.continuous
#align discrete_quotient.comap DiscreteQuotient.comap

@[simp]
theorem comap_id : S.comap (ContinuousMap.id X) = S := rfl
#align discrete_quotient.comap_id DiscreteQuotient.comap_id

@[simp]
theorem comap_comp (S : DiscreteQuotient Z) : S.comap (g.comp f) = (S.comap g).comap f :=
  rfl
#align discrete_quotient.comap_comp DiscreteQuotient.comap_comp

@[mono]
theorem comap_mono {A B : DiscreteQuotient Y} (h : A ‚â§ B) : A.comap f ‚â§ B.comap f := by tauto
                                                                                        -- üéâ no goals
#align discrete_quotient.comap_mono DiscreteQuotient.comap_mono

end Comap

section OfLE

variable {A B C : DiscreteQuotient X}

/-- The map induced by a refinement of a discrete quotient. -/
def ofLE (h : A ‚â§ B) : A ‚Üí B :=
  Quotient.map' (fun x => x) h
#align discrete_quotient.of_le DiscreteQuotient.ofLE

@[simp]
theorem ofLE_refl : ofLE (le_refl A) = id := by
  ext ‚ü®‚ü©
  -- ‚ä¢ ofLE (_ : A ‚â§ A) (Quot.mk Setoid.r a‚úù) = id (Quot.mk Setoid.r a‚úù)
  rfl
  -- üéâ no goals
#align discrete_quotient.of_le_refl DiscreteQuotient.ofLE_refl

theorem ofLE_refl_apply (a : A) : ofLE (le_refl A) a = a := by simp
                                                               -- üéâ no goals
#align discrete_quotient.of_le_refl_apply DiscreteQuotient.ofLE_refl_apply

@[simp]
theorem ofLE_ofLE (h‚ÇÅ : A ‚â§ B) (h‚ÇÇ : B ‚â§ C) (x : A) :
    ofLE h‚ÇÇ (ofLE h‚ÇÅ x) = ofLE (h‚ÇÅ.trans h‚ÇÇ) x := by
  rcases x with ‚ü®‚ü©
  -- ‚ä¢ ofLE h‚ÇÇ (ofLE h‚ÇÅ (Quot.mk Setoid.r a‚úù)) = ofLE (_ : A ‚â§ C) (Quot.mk Setoid.r ‚Ä¶
  rfl
  -- üéâ no goals
#align discrete_quotient.of_le_of_le DiscreteQuotient.ofLE_ofLE

@[simp]
theorem ofLE_comp_ofLE (h‚ÇÅ : A ‚â§ B) (h‚ÇÇ : B ‚â§ C) : ofLE h‚ÇÇ ‚àò ofLE h‚ÇÅ = ofLE (le_trans h‚ÇÅ h‚ÇÇ) :=
  funext <| ofLE_ofLE _ _
#align discrete_quotient.of_le_comp_of_le DiscreteQuotient.ofLE_comp_ofLE

theorem ofLE_continuous (h : A ‚â§ B) : Continuous (ofLE h) :=
  continuous_of_discreteTopology
#align discrete_quotient.of_le_continuous DiscreteQuotient.ofLE_continuous

@[simp]
theorem ofLE_proj (h : A ‚â§ B) (x : X) : ofLE h (A.proj x) = B.proj x :=
  Quotient.sound' (B.refl _)
#align discrete_quotient.of_le_proj DiscreteQuotient.ofLE_proj

@[simp]
theorem ofLE_comp_proj (h : A ‚â§ B) : ofLE h ‚àò A.proj = B.proj :=
  funext <| ofLE_proj _
#align discrete_quotient.of_le_comp_proj DiscreteQuotient.ofLE_comp_proj

end OfLE

/-- When `X` is a locally connected space, there is an `OrderBot` instance on
`DiscreteQuotient X`. The bottom element is given by `connectedComponentSetoid X`
-/
instance [LocallyConnectedSpace X] : OrderBot (DiscreteQuotient X) where
  bot :=
    { toSetoid := connectedComponentSetoid X
      isOpen_setOf_rel := fun x => by
        convert isOpen_connectedComponent (x := x)
        -- ‚ä¢ setOf (Setoid.Rel (connectedComponentSetoid X) x) = connectedComponent x
        ext y
        -- ‚ä¢ y ‚àà setOf (Setoid.Rel (connectedComponentSetoid X) x) ‚Üî y ‚àà connectedCompone ‚Ä¶
        simpa only [connectedComponentSetoid, ‚Üê connectedComponent_eq_iff_mem] using eq_comm }
        -- üéâ no goals
  bot_le S := fun x y (h : connectedComponent x = connectedComponent y) =>
    (S.isClopen_setOf_rel x).connectedComponent_subset (S.refl _) <| h.symm ‚ñ∏ mem_connectedComponent

@[simp]
theorem proj_bot_eq [LocallyConnectedSpace X] {x y : X} :
    proj ‚ä• x = proj ‚ä• y ‚Üî connectedComponent x = connectedComponent y :=
  Quotient.eq''
#align discrete_quotient.proj_bot_eq DiscreteQuotient.proj_bot_eq

theorem proj_bot_inj [DiscreteTopology X] {x y : X} : proj ‚ä• x = proj ‚ä• y ‚Üî x = y := by simp
                                                                                        -- üéâ no goals
#align discrete_quotient.proj_bot_inj DiscreteQuotient.proj_bot_inj

theorem proj_bot_injective [DiscreteTopology X] : Injective (‚ä• : DiscreteQuotient X).proj :=
  fun _ _ => proj_bot_inj.1
#align discrete_quotient.proj_bot_injective DiscreteQuotient.proj_bot_injective

theorem proj_bot_bijective [DiscreteTopology X] : Bijective (‚ä• : DiscreteQuotient X).proj :=
  ‚ü®proj_bot_injective, proj_surjective _‚ü©
#align discrete_quotient.proj_bot_bijective DiscreteQuotient.proj_bot_bijective

section Map

variable (f : C(X, Y)) (A A' : DiscreteQuotient X) (B B' : DiscreteQuotient Y)

/-- Given `f : C(X, Y)`, `DiscreteQuotient.LEComap f A B` is defined as
`A ‚â§ B.comap f`. Mathematically this means that `f` descends to a morphism `A ‚Üí B`. -/
def LEComap : Prop :=
  A ‚â§ B.comap f
#align discrete_quotient.le_comap DiscreteQuotient.LEComap

theorem leComap_id : LEComap (.id X) A A := le_rfl
#align discrete_quotient.le_comap_id DiscreteQuotient.leComap_id

variable {A A' B B'} {f} {g : C(Y, Z)} {C : DiscreteQuotient Z}

@[simp]
theorem leComap_id_iff : LEComap (ContinuousMap.id X) A A' ‚Üî A ‚â§ A' :=
  Iff.rfl
#align discrete_quotient.le_comap_id_iff DiscreteQuotient.leComap_id_iff

theorem LEComap.comp : LEComap g B C ‚Üí LEComap f A B ‚Üí LEComap (g.comp f) A C := by tauto
                                                                                    -- üéâ no goals
#align discrete_quotient.le_comap.comp DiscreteQuotient.LEComap.comp

@[mono]
theorem LEComap.mono (h : LEComap f A B) (hA : A' ‚â§ A) (hB : B ‚â§ B') : LEComap f A' B' :=
  hA.trans <| h.trans <| comap_mono _ hB
#align discrete_quotient.le_comap.mono DiscreteQuotient.LEComap.mono

/-- Map a discrete quotient along a continuous map. -/
def map (f : C(X, Y)) (cond : LEComap f A B) : A ‚Üí B := Quotient.map' f cond
#align discrete_quotient.map DiscreteQuotient.map

theorem map_continuous (cond : LEComap f A B) : Continuous (map f cond) :=
  continuous_of_discreteTopology
#align discrete_quotient.map_continuous DiscreteQuotient.map_continuous

@[simp]
theorem map_comp_proj (cond : LEComap f A B) : map f cond ‚àò A.proj = B.proj ‚àò f :=
  rfl
#align discrete_quotient.map_comp_proj DiscreteQuotient.map_comp_proj

@[simp]
theorem map_proj (cond : LEComap f A B) (x : X) : map f cond (A.proj x) = B.proj (f x) :=
  rfl
#align discrete_quotient.map_proj DiscreteQuotient.map_proj

@[simp]
theorem map_id : map _ (leComap_id A) = id := by ext ‚ü®‚ü©; rfl
                                                 -- ‚ä¢ map (ContinuousMap.id X) (_ : LEComap (ContinuousMap.id X) A A) (Quot.mk Set ‚Ä¶
                                                         -- üéâ no goals
#align discrete_quotient.map_id DiscreteQuotient.map_id

-- porting note: todo: figure out why `simpNF` says this is a bad `@[simp]` lemma
theorem map_comp (h1 : LEComap g B C) (h2 : LEComap f A B) :
    map (g.comp f) (h1.comp h2) = map g h1 ‚àò map f h2 := by
  ext ‚ü®‚ü©
  -- ‚ä¢ map (ContinuousMap.comp g f) (_ : LEComap (ContinuousMap.comp g f) A C) (Quo ‚Ä¶
  rfl
  -- üéâ no goals
#align discrete_quotient.map_comp DiscreteQuotient.map_comp

@[simp]
theorem ofLE_map (cond : LEComap f A B) (h : B ‚â§ B') (a : A) :
    ofLE h (map f cond a) = map f (cond.mono le_rfl h) a := by
  rcases a with ‚ü®‚ü©
  -- ‚ä¢ ofLE h (map f cond (Quot.mk Setoid.r a‚úù)) = map f (_ : LEComap f A B') (Quot ‚Ä¶
  rfl
  -- üéâ no goals
#align discrete_quotient.of_le_map DiscreteQuotient.ofLE_map

@[simp]
theorem ofLE_comp_map (cond : LEComap f A B) (h : B ‚â§ B') :
    ofLE h ‚àò map f cond = map f (cond.mono le_rfl h) :=
  funext <| ofLE_map cond h
#align discrete_quotient.of_le_comp_map DiscreteQuotient.ofLE_comp_map

@[simp]
theorem map_ofLE (cond : LEComap f A B) (h : A' ‚â§ A) (c : A') :
    map f cond (ofLE h c) = map f (cond.mono h le_rfl) c := by
  rcases c with ‚ü®‚ü©
  -- ‚ä¢ map f cond (ofLE h (Quot.mk Setoid.r a‚úù)) = map f (_ : LEComap f A' B) (Quot ‚Ä¶
  rfl
  -- üéâ no goals
#align discrete_quotient.map_of_le DiscreteQuotient.map_ofLE

@[simp]
theorem map_comp_ofLE (cond : LEComap f A B) (h : A' ‚â§ A) :
    map f cond ‚àò ofLE h = map f (cond.mono h le_rfl) :=
  funext <| map_ofLE cond h
#align discrete_quotient.map_comp_of_le DiscreteQuotient.map_comp_ofLE

end Map

theorem eq_of_forall_proj_eq [T2Space X] [CompactSpace X] [disc : TotallyDisconnectedSpace X]
    {x y : X} (h : ‚àÄ Q : DiscreteQuotient X, Q.proj x = Q.proj y) : x = y := by
  rw [‚Üê mem_singleton_iff, ‚Üê connectedComponent_eq_singleton, connectedComponent_eq_iInter_clopen,
    mem_iInter]
  rintro ‚ü®U, hU1, hU2‚ü©
  -- ‚ä¢ x ‚àà ‚Üë{ val := U, property := (_ : IsClopen U ‚àß y ‚àà U) }
  exact (Quotient.exact' (h (ofClopen hU1))).mpr hU2
  -- üéâ no goals
#align discrete_quotient.eq_of_forall_proj_eq DiscreteQuotient.eq_of_forall_proj_eq

theorem fiber_subset_ofLE {A B : DiscreteQuotient X} (h : A ‚â§ B) (a : A) :
    A.proj ‚Åª¬π' {a} ‚äÜ B.proj ‚Åª¬π' {ofLE h a} := by
  rcases A.proj_surjective a with ‚ü®a, rfl‚ü©
  -- ‚ä¢ proj A ‚Åª¬π' {proj A a} ‚äÜ proj B ‚Åª¬π' {ofLE h (proj A a)}
  rw [fiber_eq, ofLE_proj, fiber_eq]
  -- ‚ä¢ setOf (Setoid.Rel A.toSetoid a) ‚äÜ setOf (Setoid.Rel B.toSetoid a)
  exact fun _ h' => h h'
  -- üéâ no goals
#align discrete_quotient.fiber_subset_of_le DiscreteQuotient.fiber_subset_ofLE

theorem exists_of_compat [CompactSpace X] (Qs : (Q : DiscreteQuotient X) ‚Üí Q)
    (compat : ‚àÄ (A B : DiscreteQuotient X) (h : A ‚â§ B), ofLE h (Qs _) = Qs _) :
    ‚àÉ x : X, ‚àÄ Q : DiscreteQuotient X, Q.proj x = Qs _ := by
  have H‚ÇÅ : ‚àÄ Q‚ÇÅ Q‚ÇÇ, Q‚ÇÅ ‚â§ Q‚ÇÇ ‚Üí proj Q‚ÇÅ ‚Åª¬π' {Qs Q‚ÇÅ} ‚äÜ proj Q‚ÇÇ ‚Åª¬π' {Qs Q‚ÇÇ} := fun _ _ h => by
    rw [‚Üê compat _ _ h]
    exact fiber_subset_ofLE _ _
  obtain ‚ü®x, hx‚ü© : Set.Nonempty (‚ãÇ Q, proj Q ‚Åª¬π' {Qs Q}) :=
    IsCompact.nonempty_iInter_of_directed_nonempty_compact_closed
      (fun Q : DiscreteQuotient X => Q.proj ‚Åª¬π' {Qs _}) (directed_of_inf H‚ÇÅ)
      (fun Q => (singleton_nonempty _).preimage Q.proj_surjective)
      (fun Q => (Q.isClosed_preimage {Qs _}).isCompact) fun Q => Q.isClosed_preimage _
  exact ‚ü®x, mem_iInter.1 hx‚ü©
  -- üéâ no goals
#align discrete_quotient.exists_of_compat DiscreteQuotient.exists_of_compat

/-- If `X` is a compact space, then any discrete quotient of `X` is finite. -/
instance [CompactSpace X] : Finite S := by
  have : CompactSpace S := Quotient.compactSpace
  -- ‚ä¢ Finite (Quotient S.toSetoid)
  rwa [‚Üê isCompact_univ_iff, isCompact_iff_finite, finite_univ_iff] at this
  -- üéâ no goals

end DiscreteQuotient

namespace LocallyConstant

variable (f : LocallyConstant X Œ±)

/-- Any locally constant function induces a discrete quotient. -/
def discreteQuotient : DiscreteQuotient X where
  toSetoid := .comap f ‚ä•
  isOpen_setOf_rel _ := f.isLocallyConstant _
#align locally_constant.discrete_quotient LocallyConstant.discreteQuotient

/-- The (locally constant) function from the discrete quotient associated to a locally constant
function. -/
def lift : LocallyConstant f.discreteQuotient Œ± :=
  ‚ü®fun a => Quotient.liftOn' a f fun _ _ => id, fun _ => isOpen_discrete _‚ü©
#align locally_constant.lift LocallyConstant.lift

@[simp]
theorem lift_comp_proj : f.lift ‚àò f.discreteQuotient.proj = f := rfl
#align locally_constant.lift_comp_proj LocallyConstant.lift_comp_proj

end LocallyConstant
