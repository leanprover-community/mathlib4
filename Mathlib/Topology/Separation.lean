/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro
-/
import Mathlib.Topology.Compactness.Lindelof
import Mathlib.Topology.Compactness.SigmaCompact
import Mathlib.Topology.Connected.TotallyDisconnected
import Mathlib.Topology.Inseparable

#align_import topology.separation from "leanprover-community/mathlib"@"d91e7f7a7f1c7e9f0e18fdb6bde4f652004c735d"

/-!
# Separation properties of topological spaces.

This file defines the predicate `SeparatedNhds`, and common separation axioms
(under the Kolmogorov classification).

## Main definitions

* `SeparatedNhds`: Two `Set`s are separated by neighbourhoods if they are contained in disjoint
  open sets.
* `HasSeparatingCover`: A set has a countable cover that can be used with
  `hasSeparatingCovers_iff_separatedNhds` to witness when two `Set`s have `SeparatedNhds`.
* `T0Space`: A T‚ÇÄ/Kolmogorov space is a space where, for every two points `x ‚â† y`,
  there is an open set that contains one, but not the other.
* `R0Space`: An R‚ÇÄ space (sometimes called a *symmetric space*) is a topological space
  such that the `Specializes` relation is symmetric.
* `T1Space`: A T‚ÇÅ/Fr√©chet space is a space where every singleton set is closed.
  This is equivalent to, for every pair `x ‚â† y`, there existing an open set containing `x`
  but not `y` (`t1Space_iff_exists_open` shows that these conditions are equivalent.)
  T‚ÇÅ implies T‚ÇÄ and R‚ÇÄ.
* `R1Space`: An R‚ÇÅ/preregular space is a space where any two topologically distinguishable points
  have disjoint neighbourhoods. R‚ÇÅ implies R‚ÇÄ.
* `T2Space`: A T‚ÇÇ/Hausdorff space is a space where, for every two points `x ‚â† y`,
  there is two disjoint open sets, one containing `x`, and the other `y`. T‚ÇÇ implies T‚ÇÅ and R‚ÇÅ.
* `T25Space`: A T‚ÇÇ.‚ÇÖ/Urysohn space is a space where, for every two points `x ‚â† y`,
  there is two open sets, one containing `x`, and the other `y`, whose closures are disjoint.
  T‚ÇÇ.‚ÇÖ implies T‚ÇÇ.
* `RegularSpace`: A regular space is one where, given any closed `C` and `x ‚àâ C`,
  there are disjoint open sets containing `x` and `C` respectively. Such a space is not necessarily
  Hausdorff.
* `T3Space`: A T‚ÇÉ space is a regular T‚ÇÄ space. T‚ÇÉ implies T‚ÇÇ.‚ÇÖ.
* `NormalSpace`: A normal space, is one where given two disjoint closed sets,
  we can find two open sets that separate them. Such a space is not necessarily Hausdorff, even if
  it is T‚ÇÄ.
* `T4Space`: A T‚ÇÑ space is a normal T‚ÇÅ space. T‚ÇÑ implies T‚ÇÉ.
* `CompletelyNormalSpace`: A completely normal space is one in which for any two sets `s`, `t`
  such that if both `closure s` is disjoint with `t`, and `s` is disjoint with `closure t`,
  then there exist disjoint neighbourhoods of `s` and `t`. `Embedding.completelyNormalSpace` allows
  us to conclude that this is equivalent to all subspaces being normal. Such a space is not
  necessarily Hausdorff or regular, even if it is T‚ÇÄ.
* `T5Space`: A T‚ÇÖ space is a completely normal T‚ÇÅ space. T‚ÇÖ implies T‚ÇÑ.

Note that `mathlib` adopts the modern convention that `m ‚â§ n` if and only if `T_m ‚Üí T_n`, but
occasionally the literature swaps definitions for e.g. T‚ÇÉ and regular.

### TODOs

* Add perfectly normal and T6 spaces.
* Use `hasSeparatingCovers_iff_separatedNhds` to prove that perfectly normal spaces
  are completely normal.

## Main results

### T‚ÇÄ spaces

* `IsClosed.exists_closed_singleton`: Given a closed set `S` in a compact T‚ÇÄ space,
  there is some `x ‚àà S` such that `{x}` is closed.
* `exists_isOpen_singleton_of_isOpen_finite`: Given an open finite set `S` in a T‚ÇÄ space,
  there is some `x ‚àà S` such that `{x}` is open.

### T‚ÇÅ spaces

* `isClosedMap_const`: The constant map is a closed map.
* `discrete_of_t1_of_finite`: A finite T‚ÇÅ space must have the discrete topology.

### T‚ÇÇ spaces

* `t2_iff_nhds`: A space is T‚ÇÇ iff the neighbourhoods of distinct points generate the bottom filter.
* `t2_iff_isClosed_diagonal`: A space is T‚ÇÇ iff the `diagonal` of `X` (that is, the set of all
  points of the form `(a, a) : X √ó X`) is closed under the product topology.
* `separatedNhds_of_finset_finset`: Any two disjoint finsets are `SeparatedNhds`.
* Most topological constructions preserve Hausdorffness;
  these results are part of the typeclass inference system (e.g. `Embedding.t2Space`)
* `Set.EqOn.closure`: If two functions are equal on some set `s`, they are equal on its closure.
* `IsCompact.isClosed`: All compact sets are closed.
* `WeaklyLocallyCompactSpace.locallyCompactSpace`: If a topological space is both
  weakly locally compact (i.e., each point has a compact neighbourhood)
  and is T‚ÇÇ, then it is locally compact.
* `totallySeparatedSpace_of_t1_of_basis_clopen`: If `X` has a clopen basis, then
  it is a `TotallySeparatedSpace`.
* `loc_compact_t2_tot_disc_iff_tot_sep`: A locally compact T‚ÇÇ space is totally disconnected iff
  it is totally separated.
* `t2Quotient`: the largest T2 quotient of a given topological space.

If the space is also compact:

* `normalOfCompactT2`: A compact T‚ÇÇ space is a `NormalSpace`.
* `connectedComponent_eq_iInter_isClopen`: The connected component of a point
  is the intersection of all its clopen neighbourhoods.
* `compact_t2_tot_disc_iff_tot_sep`: Being a `TotallyDisconnectedSpace`
  is equivalent to being a `TotallySeparatedSpace`.
* `ConnectedComponents.t2`: `ConnectedComponents X` is T‚ÇÇ for `X` T‚ÇÇ and compact.

### Regular spaces

If the space is also Lindel√∂f:

* `NormalSpace.of_regularSpace_lindelofSpace`: every regular Lindel√∂f space is normal.

### T‚ÇÉ spaces

* `disjoint_nested_nhds`: Given two points `x ‚â† y`, we can find neighbourhoods `x ‚àà V‚ÇÅ ‚äÜ U‚ÇÅ` and
  `y ‚àà V‚ÇÇ ‚äÜ U‚ÇÇ`, with the `V‚Çñ` closed and the `U‚Çñ` open, such that the `U‚Çñ` are disjoint.

## References

* <https://en.wikipedia.org/wiki/Separation_axiom>
* <https://en.wikipedia.org/wiki/Normal_space>
* [Willard's *General Topology*][zbMATH02107988]

-/


open Function Set Filter Topology TopologicalSpace
open scoped Classical

universe u v

variable {X : Type*} {Y : Type*} [TopologicalSpace X]

section Separation

/--
`SeparatedNhds` is a predicate on pairs of sub`Set`s of a topological space.  It holds if the two
sub`Set`s are contained in disjoint open sets.
-/
def SeparatedNhds : Set X ‚Üí Set X ‚Üí Prop := fun s t : Set X =>
  ‚àÉ U V : Set X, IsOpen U ‚àß IsOpen V ‚àß s ‚äÜ U ‚àß t ‚äÜ V ‚àß Disjoint U V
#align separated_nhds SeparatedNhds

theorem separatedNhds_iff_disjoint {s t : Set X} : SeparatedNhds s t ‚Üî Disjoint (ùìùÀ¢ s) (ùìùÀ¢ t) := by
  simp only [(hasBasis_nhdsSet s).disjoint_iff (hasBasis_nhdsSet t), SeparatedNhds, exists_prop, ‚Üê
    exists_and_left, and_assoc, and_comm, and_left_comm]
#align separated_nhds_iff_disjoint separatedNhds_iff_disjoint

alias ‚ü®SeparatedNhds.disjoint_nhdsSet, _‚ü© := separatedNhds_iff_disjoint

/-- `HasSeparatingCover`s can be useful witnesses for `SeparatedNhds`. -/
def HasSeparatingCover : Set X ‚Üí Set X ‚Üí Prop := fun s t ‚Ü¶
  ‚àÉ u : ‚Ñï ‚Üí Set X, s ‚äÜ ‚ãÉ n, u n ‚àß ‚àÄ n, IsOpen (u n) ‚àß Disjoint (closure (u n)) t

/-- Used to prove that a regular topological space with Lindel√∂f topology is a normal space,
and (todo) a perfectly normal space is a completely normal space. -/
theorem hasSeparatingCovers_iff_separatedNhds {s t : Set X} :
    HasSeparatingCover s t ‚àß HasSeparatingCover t s ‚Üî SeparatedNhds s t := by
  constructor
  ¬∑ rintro ‚ü®‚ü®u, u_cov, u_props‚ü©, ‚ü®v, v_cov, v_props‚ü©‚ü©
    have open_lemma : ‚àÄ (u‚ÇÄ a : ‚Ñï ‚Üí Set X), (‚àÄ n, IsOpen (u‚ÇÄ n)) ‚Üí
      IsOpen (‚ãÉ n, u‚ÇÄ n \ closure (a n)) := fun _ _ u‚ÇÄi_open ‚Ü¶
        isOpen_iUnion fun i ‚Ü¶ (u‚ÇÄi_open i).sdiff isClosed_closure
    have cover_lemma : ‚àÄ (h‚ÇÄ : Set X) (u‚ÇÄ v‚ÇÄ : ‚Ñï ‚Üí Set X),
        (h‚ÇÄ ‚äÜ ‚ãÉ n, u‚ÇÄ n) ‚Üí (‚àÄ n, Disjoint (closure (v‚ÇÄ n)) h‚ÇÄ) ‚Üí
        (h‚ÇÄ ‚äÜ ‚ãÉ n, u‚ÇÄ n \ closure (‚ãÉ m ‚â§ n, v‚ÇÄ m)) :=
        fun h‚ÇÄ u‚ÇÄ v‚ÇÄ h‚ÇÄ_cov dis x xinh ‚Ü¶ by
      rcases h‚ÇÄ_cov xinh with ‚ü®un , ‚ü®n, rfl‚ü© , xinun‚ü©
      simp only [mem_iUnion]
      refine ‚ü®n, xinun, ?_‚ü©
      simp_all only [closure_iUnion‚ÇÇ_le_nat, disjoint_right, mem_setOf_eq, mem_iUnion,
        exists_false, exists_const, not_false_eq_true]
    refine
      ‚ü®‚ãÉ n : ‚Ñï, u n \ (closure (‚ãÉ m ‚â§ n, v m)),
        ‚ãÉ n : ‚Ñï, v n \ (closure (‚ãÉ m ‚â§ n, u m)),
        open_lemma u (fun n ‚Ü¶ ‚ãÉ m ‚â§ n, v m) (fun n ‚Ü¶ (u_props n).1),
        open_lemma v (fun n ‚Ü¶ ‚ãÉ m ‚â§ n, u m) (fun n ‚Ü¶ (v_props n).1),
        cover_lemma s u v u_cov (fun n ‚Ü¶ (v_props n).2),
        cover_lemma t v u v_cov (fun n ‚Ü¶ (u_props n).2),
        ?_‚ü©
    rw [Set.disjoint_left]
    rintro x ‚ü®un, ‚ü®n, rfl‚ü©, xinun‚ü©
    suffices ‚àÄ (m : ‚Ñï), x ‚àà v m ‚Üí x ‚àà closure (‚ãÉ m' ‚àà {m' | m' ‚â§ m}, u m') by simpa
    intro m xinvm
    have n_le_m : n ‚â§ m := by
      by_contra m_gt_n
      exact xinun.2 (subset_closure (mem_biUnion (le_of_lt (not_le.mp m_gt_n)) xinvm))
    exact subset_closure (mem_biUnion n_le_m xinun.1)
  ¬∑ rintro ‚ü®U, V, U_open, V_open, h_sub_U, k_sub_V, UV_dis‚ü©
    exact
      ‚ü®‚ü®fun _ ‚Ü¶ U, h_sub_U.trans (iUnion_const U).symm.subset,
        fun _ ‚Ü¶
          ‚ü®U_open, disjoint_of_subset (fun ‚¶Éa‚¶Ñ a ‚Ü¶ a) k_sub_V (UV_dis.closure_left V_open)‚ü©‚ü©,
      ‚ü®fun _ ‚Ü¶ V, k_sub_V.trans (iUnion_const V).symm.subset,
        fun _ ‚Ü¶
          ‚ü®V_open, disjoint_of_subset (fun ‚¶Éa‚¶Ñ a ‚Ü¶ a) h_sub_U (UV_dis.closure_right U_open).symm‚ü©‚ü©‚ü©

namespace SeparatedNhds

variable {s s‚ÇÅ s‚ÇÇ t t‚ÇÅ t‚ÇÇ u : Set X}

@[symm]
theorem symm : SeparatedNhds s t ‚Üí SeparatedNhds t s := fun ‚ü®U, V, oU, oV, aU, bV, UV‚ü© =>
  ‚ü®V, U, oV, oU, bV, aU, Disjoint.symm UV‚ü©
#align separated_nhds.symm SeparatedNhds.symm

theorem comm (s t : Set X) : SeparatedNhds s t ‚Üî SeparatedNhds t s :=
  ‚ü®symm, symm‚ü©
#align separated_nhds.comm SeparatedNhds.comm

theorem preimage [TopologicalSpace Y] {f : X ‚Üí Y} {s t : Set Y} (h : SeparatedNhds s t)
    (hf : Continuous f) : SeparatedNhds (f ‚Åª¬π' s) (f ‚Åª¬π' t) :=
  let ‚ü®U, V, oU, oV, sU, tV, UV‚ü© := h
  ‚ü®f ‚Åª¬π' U, f ‚Åª¬π' V, oU.preimage hf, oV.preimage hf, preimage_mono sU, preimage_mono tV,
    UV.preimage f‚ü©
#align separated_nhds.preimage SeparatedNhds.preimage

protected theorem disjoint (h : SeparatedNhds s t) : Disjoint s t :=
  let ‚ü®_, _, _, _, hsU, htV, hd‚ü© := h; hd.mono hsU htV
#align separated_nhds.disjoint SeparatedNhds.disjoint

theorem disjoint_closure_left (h : SeparatedNhds s t) : Disjoint (closure s) t :=
  let ‚ü®_U, _V, _, hV, hsU, htV, hd‚ü© := h
  (hd.closure_left hV).mono (closure_mono hsU) htV
#align separated_nhds.disjoint_closure_left SeparatedNhds.disjoint_closure_left

theorem disjoint_closure_right (h : SeparatedNhds s t) : Disjoint s (closure t) :=
  h.symm.disjoint_closure_left.symm
#align separated_nhds.disjoint_closure_right SeparatedNhds.disjoint_closure_right

@[simp] theorem empty_right (s : Set X) : SeparatedNhds s ‚àÖ :=
  ‚ü®_, _, isOpen_univ, isOpen_empty, fun a _ => mem_univ a, Subset.rfl, disjoint_empty _‚ü©
#align separated_nhds.empty_right SeparatedNhds.empty_right

@[simp] theorem empty_left (s : Set X) : SeparatedNhds ‚àÖ s :=
  (empty_right _).symm
#align separated_nhds.empty_left SeparatedNhds.empty_left

theorem mono (h : SeparatedNhds s‚ÇÇ t‚ÇÇ) (hs : s‚ÇÅ ‚äÜ s‚ÇÇ) (ht : t‚ÇÅ ‚äÜ t‚ÇÇ) : SeparatedNhds s‚ÇÅ t‚ÇÅ :=
  let ‚ü®U, V, hU, hV, hsU, htV, hd‚ü© := h
  ‚ü®U, V, hU, hV, hs.trans hsU, ht.trans htV, hd‚ü©
#align separated_nhds.mono SeparatedNhds.mono

theorem union_left : SeparatedNhds s u ‚Üí SeparatedNhds t u ‚Üí SeparatedNhds (s ‚à™ t) u := by
  simpa only [separatedNhds_iff_disjoint, nhdsSet_union, disjoint_sup_left] using And.intro
#align separated_nhds.union_left SeparatedNhds.union_left

theorem union_right (ht : SeparatedNhds s t) (hu : SeparatedNhds s u) : SeparatedNhds s (t ‚à™ u) :=
  (ht.symm.union_left hu.symm).symm
#align separated_nhds.union_right SeparatedNhds.union_right

end SeparatedNhds

/-- A T‚ÇÄ space, also known as a Kolmogorov space, is a topological space such that for every pair
`x ‚â† y`, there is an open set containing one but not the other. We formulate the definition in terms
of the `Inseparable` relation.  -/
class T0Space (X : Type u) [TopologicalSpace X] : Prop where
  /-- Two inseparable points in a T‚ÇÄ space are equal. -/
  t0 : ‚àÄ ‚¶Éx y : X‚¶Ñ, Inseparable x y ‚Üí x = y
#align t0_space T0Space

theorem t0Space_iff_inseparable (X : Type u) [TopologicalSpace X] :
    T0Space X ‚Üî ‚àÄ x y : X, Inseparable x y ‚Üí x = y :=
  ‚ü®fun ‚ü®h‚ü© => h, fun h => ‚ü®h‚ü©‚ü©
#align t0_space_iff_inseparable t0Space_iff_inseparable

theorem t0Space_iff_not_inseparable (X : Type u) [TopologicalSpace X] :
    T0Space X ‚Üî Pairwise fun x y : X => ¬¨Inseparable x y := by
  simp only [t0Space_iff_inseparable, Ne, not_imp_not, Pairwise]
#align t0_space_iff_not_inseparable t0Space_iff_not_inseparable

theorem Inseparable.eq [T0Space X] {x y : X} (h : Inseparable x y) : x = y :=
  T0Space.t0 h
#align inseparable.eq Inseparable.eq

/-- A topology `Inducing` map from a T‚ÇÄ space is injective. -/
protected theorem Inducing.injective [TopologicalSpace Y] [T0Space X] {f : X ‚Üí Y}
    (hf : Inducing f) : Injective f := fun _ _ h =>
  (hf.inseparable_iff.1 <| .of_eq h).eq
#align inducing.injective Inducing.injective

/-- A topology `Inducing` map from a T‚ÇÄ space is a topological embedding. -/
protected theorem Inducing.embedding [TopologicalSpace Y] [T0Space X] {f : X ‚Üí Y}
    (hf : Inducing f) : Embedding f :=
  ‚ü®hf, hf.injective‚ü©
#align inducing.embedding Inducing.embedding

lemma embedding_iff_inducing [TopologicalSpace Y] [T0Space X] {f : X ‚Üí Y} :
    Embedding f ‚Üî Inducing f :=
  ‚ü®Embedding.toInducing, Inducing.embedding‚ü©
#align embedding_iff_inducing embedding_iff_inducing

theorem t0Space_iff_nhds_injective (X : Type u) [TopologicalSpace X] :
    T0Space X ‚Üî Injective (ùìù : X ‚Üí Filter X) :=
  t0Space_iff_inseparable X
#align t0_space_iff_nhds_injective t0Space_iff_nhds_injective

theorem nhds_injective [T0Space X] : Injective (ùìù : X ‚Üí Filter X) :=
  (t0Space_iff_nhds_injective X).1 ‚Äπ_‚Ä∫
#align nhds_injective nhds_injective

theorem inseparable_iff_eq [T0Space X] {x y : X} : Inseparable x y ‚Üî x = y :=
  nhds_injective.eq_iff
#align inseparable_iff_eq inseparable_iff_eq

@[simp]
theorem nhds_eq_nhds_iff [T0Space X] {a b : X} : ùìù a = ùìù b ‚Üî a = b :=
  nhds_injective.eq_iff
#align nhds_eq_nhds_iff nhds_eq_nhds_iff

@[simp]
theorem inseparable_eq_eq [T0Space X] : Inseparable = @Eq X :=
  funext‚ÇÇ fun _ _ => propext inseparable_iff_eq
#align inseparable_eq_eq inseparable_eq_eq

theorem TopologicalSpace.IsTopologicalBasis.inseparable_iff {b : Set (Set X)}
    (hb : IsTopologicalBasis b) {x y : X} : Inseparable x y ‚Üî ‚àÄ s ‚àà b, (x ‚àà s ‚Üî y ‚àà s) :=
  ‚ü®fun h s hs ‚Ü¶ inseparable_iff_forall_open.1 h _ (hb.isOpen hs),
    fun h ‚Ü¶ hb.nhds_hasBasis.eq_of_same_basis <| by
      convert hb.nhds_hasBasis using 2
      exact and_congr_right (h _)‚ü©

theorem TopologicalSpace.IsTopologicalBasis.eq_iff [T0Space X] {b : Set (Set X)}
    (hb : IsTopologicalBasis b) {x y : X} : x = y ‚Üî ‚àÄ s ‚àà b, (x ‚àà s ‚Üî y ‚àà s) :=
  inseparable_iff_eq.symm.trans hb.inseparable_iff

theorem t0Space_iff_exists_isOpen_xor'_mem (X : Type u) [TopologicalSpace X] :
    T0Space X ‚Üî Pairwise fun x y => ‚àÉ U : Set X, IsOpen U ‚àß Xor' (x ‚àà U) (y ‚àà U) := by
  simp only [t0Space_iff_not_inseparable, xor_iff_not_iff, not_forall, exists_prop,
    inseparable_iff_forall_open, Pairwise]
#align t0_space_iff_exists_is_open_xor_mem t0Space_iff_exists_isOpen_xor'_mem

theorem exists_isOpen_xor'_mem [T0Space X] {x y : X} (h : x ‚â† y) :
    ‚àÉ U : Set X, IsOpen U ‚àß Xor' (x ‚àà U) (y ‚àà U) :=
  (t0Space_iff_exists_isOpen_xor'_mem X).1 ‚Äπ_‚Ä∫ h
#align exists_is_open_xor_mem exists_isOpen_xor'_mem

/-- Specialization forms a partial order on a t0 topological space. -/
def specializationOrder (X) [TopologicalSpace X] [T0Space X] : PartialOrder X :=
  { specializationPreorder X, PartialOrder.lift (OrderDual.toDual ‚àò ùìù) nhds_injective with }
#align specialization_order specializationOrder

instance SeparationQuotient.instT0Space : T0Space (SeparationQuotient X) :=
  ‚ü®fun x y => Quotient.inductionOn‚ÇÇ' x y fun _ _ h =>
    SeparationQuotient.mk_eq_mk.2 <| SeparationQuotient.inducing_mk.inseparable_iff.1 h‚ü©

theorem minimal_nonempty_closed_subsingleton [T0Space X] {s : Set X} (hs : IsClosed s)
    (hmin : ‚àÄ t, t ‚äÜ s ‚Üí t.Nonempty ‚Üí IsClosed t ‚Üí t = s) : s.Subsingleton := by
  clear Y -- Porting note: added
  refine fun x hx y hy => of_not_not fun hxy => ?_
  rcases exists_isOpen_xor'_mem hxy with ‚ü®U, hUo, hU‚ü©
  wlog h : x ‚àà U ‚àß y ‚àâ U
  ¬∑ refine this hs hmin y hy x hx (Ne.symm hxy) U hUo hU.symm (hU.resolve_left h)
  cases' h with hxU hyU
  have : s \ U = s := hmin (s \ U) diff_subset ‚ü®y, hy, hyU‚ü© (hs.sdiff hUo)
  exact (this.symm.subset hx).2 hxU
#align minimal_nonempty_closed_subsingleton minimal_nonempty_closed_subsingleton

theorem minimal_nonempty_closed_eq_singleton [T0Space X] {s : Set X} (hs : IsClosed s)
    (hne : s.Nonempty) (hmin : ‚àÄ t, t ‚äÜ s ‚Üí t.Nonempty ‚Üí IsClosed t ‚Üí t = s) : ‚àÉ x, s = {x} :=
  exists_eq_singleton_iff_nonempty_subsingleton.2
    ‚ü®hne, minimal_nonempty_closed_subsingleton hs hmin‚ü©
#align minimal_nonempty_closed_eq_singleton minimal_nonempty_closed_eq_singleton

/-- Given a closed set `S` in a compact T‚ÇÄ space, there is some `x ‚àà S` such that `{x}` is
closed. -/
theorem IsClosed.exists_closed_singleton [T0Space X] [CompactSpace X] {S : Set X}
    (hS : IsClosed S) (hne : S.Nonempty) : ‚àÉ x : X, x ‚àà S ‚àß IsClosed ({x} : Set X) := by
  obtain ‚ü®V, Vsub, Vne, Vcls, hV‚ü© := hS.exists_minimal_nonempty_closed_subset hne
  rcases minimal_nonempty_closed_eq_singleton Vcls Vne hV with ‚ü®x, rfl‚ü©
  exact ‚ü®x, Vsub (mem_singleton x), Vcls‚ü©
#align is_closed.exists_closed_singleton IsClosed.exists_closed_singleton

theorem minimal_nonempty_open_subsingleton [T0Space X] {s : Set X} (hs : IsOpen s)
    (hmin : ‚àÄ t, t ‚äÜ s ‚Üí t.Nonempty ‚Üí IsOpen t ‚Üí t = s) : s.Subsingleton := by
  clear Y -- Porting note: added
  refine fun x hx y hy => of_not_not fun hxy => ?_
  rcases exists_isOpen_xor'_mem hxy with ‚ü®U, hUo, hU‚ü©
  wlog h : x ‚àà U ‚àß y ‚àâ U
  ¬∑ exact this hs hmin y hy x hx (Ne.symm hxy) U hUo hU.symm (hU.resolve_left h)
  cases' h with hxU hyU
  have : s ‚à© U = s := hmin (s ‚à© U) inter_subset_left ‚ü®x, hx, hxU‚ü© (hs.inter hUo)
  exact hyU (this.symm.subset hy).2
#align minimal_nonempty_open_subsingleton minimal_nonempty_open_subsingleton

theorem minimal_nonempty_open_eq_singleton [T0Space X] {s : Set X} (hs : IsOpen s)
    (hne : s.Nonempty) (hmin : ‚àÄ t, t ‚äÜ s ‚Üí t.Nonempty ‚Üí IsOpen t ‚Üí t = s) : ‚àÉ x, s = {x} :=
  exists_eq_singleton_iff_nonempty_subsingleton.2 ‚ü®hne, minimal_nonempty_open_subsingleton hs hmin‚ü©
#align minimal_nonempty_open_eq_singleton minimal_nonempty_open_eq_singleton

/-- Given an open finite set `S` in a T‚ÇÄ space, there is some `x ‚àà S` such that `{x}` is open. -/
theorem exists_isOpen_singleton_of_isOpen_finite [T0Space X] {s : Set X} (hfin : s.Finite)
    (hne : s.Nonempty) (ho : IsOpen s) : ‚àÉ x ‚àà s, IsOpen ({x} : Set X) := by
  lift s to Finset X using hfin
  induction' s using Finset.strongInductionOn with s ihs
  rcases em (‚àÉ t, t ‚äÇ s ‚àß t.Nonempty ‚àß IsOpen (t : Set X)) with (‚ü®t, hts, htne, hto‚ü© | ht)
  ¬∑ rcases ihs t hts htne hto with ‚ü®x, hxt, hxo‚ü©
    exact ‚ü®x, hts.1 hxt, hxo‚ü©
  ¬∑ -- Porting note: was `rcases minimal_nonempty_open_eq_singleton ho hne _ with ‚ü®x, hx‚ü©`
    --               https://github.com/leanprover/std4/issues/116
    rsuffices ‚ü®x, hx‚ü© : ‚àÉ x, s.toSet = {x}
    ¬∑ exact ‚ü®x, hx.symm ‚ñ∏ rfl, hx ‚ñ∏ ho‚ü©
    refine minimal_nonempty_open_eq_singleton ho hne ?_
    refine fun t hts htne hto => of_not_not fun hts' => ht ?_
    lift t to Finset X using s.finite_toSet.subset hts
    exact ‚ü®t, ssubset_iff_subset_ne.2 ‚ü®hts, mt Finset.coe_inj.2 hts'‚ü©, htne, hto‚ü©
#align exists_open_singleton_of_open_finite exists_isOpen_singleton_of_isOpen_finite

theorem exists_open_singleton_of_finite [T0Space X] [Finite X] [Nonempty X] :
    ‚àÉ x : X, IsOpen ({x} : Set X) :=
  let ‚ü®x, _, h‚ü© := exists_isOpen_singleton_of_isOpen_finite (Set.toFinite _)
    univ_nonempty isOpen_univ
  ‚ü®x, h‚ü©
#align exists_open_singleton_of_fintype exists_open_singleton_of_finite

theorem t0Space_of_injective_of_continuous [TopologicalSpace Y] {f : X ‚Üí Y}
    (hf : Function.Injective f) (hf' : Continuous f) [T0Space Y] : T0Space X :=
  ‚ü®fun _ _ h => hf <| (h.map hf').eq‚ü©
#align t0_space_of_injective_of_continuous t0Space_of_injective_of_continuous

protected theorem Embedding.t0Space [TopologicalSpace Y] [T0Space Y] {f : X ‚Üí Y}
    (hf : Embedding f) : T0Space X :=
  t0Space_of_injective_of_continuous hf.inj hf.continuous
#align embedding.t0_space Embedding.t0Space

instance Subtype.t0Space [T0Space X] {p : X ‚Üí Prop} : T0Space (Subtype p) :=
  embedding_subtype_val.t0Space
#align subtype.t0_space Subtype.t0Space

theorem t0Space_iff_or_not_mem_closure (X : Type u) [TopologicalSpace X] :
    T0Space X ‚Üî Pairwise fun a b : X => a ‚àâ closure ({b} : Set X) ‚à® b ‚àâ closure ({a} : Set X) := by
  simp only [t0Space_iff_not_inseparable, inseparable_iff_mem_closure, not_and_or]
#align t0_space_iff_or_not_mem_closure t0Space_iff_or_not_mem_closure

instance Prod.instT0Space [TopologicalSpace Y] [T0Space X] [T0Space Y] : T0Space (X √ó Y) :=
  ‚ü®fun _ _ h => Prod.ext (h.map continuous_fst).eq (h.map continuous_snd).eq‚ü©

instance Pi.instT0Space {Œπ : Type*} {X : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)]
    [‚àÄ i, T0Space (X i)] :
    T0Space (‚àÄ i, X i) :=
  ‚ü®fun _ _ h => funext fun i => (h.map (continuous_apply i)).eq‚ü©
#align pi.t0_space Pi.instT0Space

instance ULift.instT0Space [T0Space X] : T0Space (ULift X) :=
  embedding_uLift_down.t0Space

theorem T0Space.of_cover (h : ‚àÄ x y, Inseparable x y ‚Üí ‚àÉ s : Set X, x ‚àà s ‚àß y ‚àà s ‚àß T0Space s) :
    T0Space X := by
  refine ‚ü®fun x y hxy => ?_‚ü©
  rcases h x y hxy with ‚ü®s, hxs, hys, hs‚ü©
  lift x to s using hxs; lift y to s using hys
  rw [‚Üê subtype_inseparable_iff] at hxy
  exact congr_arg Subtype.val hxy.eq
#align t0_space.of_cover T0Space.of_cover

theorem T0Space.of_open_cover (h : ‚àÄ x, ‚àÉ s : Set X, x ‚àà s ‚àß IsOpen s ‚àß T0Space s) : T0Space X :=
  T0Space.of_cover fun x _ hxy =>
    let ‚ü®s, hxs, hso, hs‚ü© := h x
    ‚ü®s, hxs, (hxy.mem_open_iff hso).1 hxs, hs‚ü©
#align t0_space.of_open_cover T0Space.of_open_cover

/-- A topological space is called an R‚ÇÄ space, if `Specializes` relation is symmetric.

In other words, given two points `x y : X`,
if every neighborhood of `y` contains `x`, then every neighborhood of `x` contains `y`. -/
@[mk_iff]
class R0Space (X : Type u) [TopologicalSpace X] : Prop where
  /-- In an R‚ÇÄ space, the `Specializes` relation is symmetric. -/
  specializes_symmetric : Symmetric (Specializes : X ‚Üí X ‚Üí Prop)

export R0Space (specializes_symmetric)

section R0Space

variable [R0Space X] {x y : X}

/-- In an R‚ÇÄ space, the `Specializes` relation is symmetric, dot notation version. -/
theorem Specializes.symm (h : x ‚§≥ y) : y ‚§≥ x := specializes_symmetric h
#align specializes.symm Specializes.symm

/-- In an R‚ÇÄ space, the `Specializes` relation is symmetric, `Iff` version. -/
theorem specializes_comm : x ‚§≥ y ‚Üî y ‚§≥ x := ‚ü®Specializes.symm, Specializes.symm‚ü©
#align specializes_comm specializes_comm

/-- In an R‚ÇÄ space, `Specializes` is equivalent to `Inseparable`. -/
theorem specializes_iff_inseparable : x ‚§≥ y ‚Üî Inseparable x y :=
  ‚ü®fun h ‚Ü¶ h.antisymm h.symm, Inseparable.specializes‚ü©
#align specializes_iff_inseparable specializes_iff_inseparable

/-- In an R‚ÇÄ space, `Specializes` implies `Inseparable`. -/
alias ‚ü®Specializes.inseparable, _‚ü© := specializes_iff_inseparable

theorem Inducing.r0Space [TopologicalSpace Y] {f : Y ‚Üí X} (hf : Inducing f) : R0Space Y where
  specializes_symmetric a b := by
    simpa only [‚Üê hf.specializes_iff] using Specializes.symm

instance {p : X ‚Üí Prop} : R0Space {x // p x} := inducing_subtype_val.r0Space

instance [TopologicalSpace Y] [R0Space Y] : R0Space (X √ó Y) where
  specializes_symmetric _ _ h := h.fst.symm.prod h.snd.symm

instance {Œπ : Type*} {X : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)] [‚àÄ i, R0Space (X i)] :
    R0Space (‚àÄ i, X i) where
  specializes_symmetric _ _ h := specializes_pi.2 fun i ‚Ü¶ (specializes_pi.1 h i).symm

/-- In an R‚ÇÄ space, the closure of a singleton is a compact set. -/
theorem isCompact_closure_singleton : IsCompact (closure {x}) := by
  refine isCompact_of_finite_subcover fun U hUo hxU ‚Ü¶ ?_
  obtain ‚ü®i, hi‚ü© : ‚àÉ i, x ‚àà U i := mem_iUnion.1 <| hxU <| subset_closure rfl
  refine ‚ü®{i}, fun y hy ‚Ü¶ ?_‚ü©
  rw [‚Üê specializes_iff_mem_closure, specializes_comm] at hy
  simpa using hy.mem_open (hUo i) hi

theorem Filter.coclosedCompact_le_cofinite : coclosedCompact X ‚â§ cofinite :=
  le_cofinite_iff_compl_singleton_mem.2 fun _ ‚Ü¶
    compl_mem_coclosedCompact.2 isCompact_closure_singleton
#align filter.coclosed_compact_le_cofinite Filter.coclosedCompact_le_cofinite

variable (X)

/-- In an R‚ÇÄ space, relatively compact sets form a bornology.
Its cobounded filter is `Filter.coclosedCompact`.
See also `Bornology.inCompact` the bornology of sets contained in a compact set. -/
def Bornology.relativelyCompact : Bornology X where
  cobounded' := Filter.coclosedCompact X
  le_cofinite' := Filter.coclosedCompact_le_cofinite
#align bornology.relatively_compact Bornology.relativelyCompact

variable {X}

theorem Bornology.relativelyCompact.isBounded_iff {s : Set X} :
    @Bornology.IsBounded _ (Bornology.relativelyCompact X) s ‚Üî IsCompact (closure s) :=
  compl_mem_coclosedCompact
#align bornology.relatively_compact.is_bounded_iff Bornology.relativelyCompact.isBounded_iff

/-- In an R‚ÇÄ space, the closure of a finite set is a compact set. -/
theorem Set.Finite.isCompact_closure {s : Set X} (hs : s.Finite) : IsCompact (closure s) :=
  let _ : Bornology X := .relativelyCompact X
  Bornology.relativelyCompact.isBounded_iff.1 hs.isBounded

end R0Space

/-- A T‚ÇÅ space, also known as a Fr√©chet space, is a topological space
  where every singleton set is closed. Equivalently, for every pair
  `x ‚â† y`, there is an open set containing `x` and not `y`. -/
class T1Space (X : Type u) [TopologicalSpace X] : Prop where
  /-- A singleton in a T‚ÇÅ space is a closed set. -/
  t1 : ‚àÄ x, IsClosed ({x} : Set X)
#align t1_space T1Space

theorem isClosed_singleton [T1Space X] {x : X} : IsClosed ({x} : Set X) :=
  T1Space.t1 x
#align is_closed_singleton isClosed_singleton

theorem isOpen_compl_singleton [T1Space X] {x : X} : IsOpen ({x}·∂ú : Set X) :=
  isClosed_singleton.isOpen_compl
#align is_open_compl_singleton isOpen_compl_singleton

theorem isOpen_ne [T1Space X] {x : X} : IsOpen { y | y ‚â† x } :=
  isOpen_compl_singleton
#align is_open_ne isOpen_ne

@[to_additive]
theorem Continuous.isOpen_mulSupport [T1Space X] [One X] [TopologicalSpace Y] {f : Y ‚Üí X}
    (hf : Continuous f) : IsOpen (mulSupport f) :=
  isOpen_ne.preimage hf
#align continuous.is_open_mul_support Continuous.isOpen_mulSupport
#align continuous.is_open_support Continuous.isOpen_support

theorem Ne.nhdsWithin_compl_singleton [T1Space X] {x y : X} (h : x ‚â† y) : ùìù[{y}·∂ú] x = ùìù x :=
  isOpen_ne.nhdsWithin_eq h
#align ne.nhds_within_compl_singleton Ne.nhdsWithin_compl_singleton

theorem Ne.nhdsWithin_diff_singleton [T1Space X] {x y : X} (h : x ‚â† y) (s : Set X) :
    ùìù[s \ {y}] x = ùìù[s] x := by
  rw [diff_eq, inter_comm, nhdsWithin_inter_of_mem]
  exact mem_nhdsWithin_of_mem_nhds (isOpen_ne.mem_nhds h)
#align ne.nhds_within_diff_singleton Ne.nhdsWithin_diff_singleton

lemma nhdsWithin_compl_singleton_le [T1Space X] (x y : X) : ùìù[{x}·∂ú] x ‚â§ ùìù[{y}·∂ú] x := by
  rcases eq_or_ne x y with rfl|hy
  ¬∑ exact Eq.le rfl
  ¬∑ rw [Ne.nhdsWithin_compl_singleton hy]
    exact nhdsWithin_le_nhds

theorem isOpen_setOf_eventually_nhdsWithin [T1Space X] {p : X ‚Üí Prop} :
    IsOpen { x | ‚àÄ·∂† y in ùìù[‚â†] x, p y } := by
  refine isOpen_iff_mem_nhds.mpr fun a ha => ?_
  filter_upwards [eventually_nhds_nhdsWithin.mpr ha] with b hb
  rcases eq_or_ne a b with rfl | h
  ¬∑ exact hb
  ¬∑ rw [h.symm.nhdsWithin_compl_singleton] at hb
    exact hb.filter_mono nhdsWithin_le_nhds
#align is_open_set_of_eventually_nhds_within isOpen_setOf_eventually_nhdsWithin

protected theorem Set.Finite.isClosed [T1Space X] {s : Set X} (hs : Set.Finite s) : IsClosed s := by
  rw [‚Üê biUnion_of_singleton s]
  exact hs.isClosed_biUnion fun i _ => isClosed_singleton
#align set.finite.is_closed Set.Finite.isClosed

theorem TopologicalSpace.IsTopologicalBasis.exists_mem_of_ne [T1Space X] {b : Set (Set X)}
    (hb : IsTopologicalBasis b) {x y : X} (h : x ‚â† y) : ‚àÉ a ‚àà b, x ‚àà a ‚àß y ‚àâ a := by
  rcases hb.isOpen_iff.1 isOpen_ne x h with ‚ü®a, ab, xa, ha‚ü©
  exact ‚ü®a, ab, xa, fun h => ha h rfl‚ü©
#align topological_space.is_topological_basis.exists_mem_of_ne TopologicalSpace.IsTopologicalBasis.exists_mem_of_ne

protected theorem Finset.isClosed [T1Space X] (s : Finset X) : IsClosed (s : Set X) :=
  s.finite_toSet.isClosed
#align finset.is_closed Finset.isClosed

theorem t1Space_TFAE (X : Type u) [TopologicalSpace X] :
    List.TFAE [T1Space X,
      ‚àÄ x, IsClosed ({ x } : Set X),
      ‚àÄ x, IsOpen ({ x }·∂ú : Set X),
      Continuous (@CofiniteTopology.of X),
      ‚àÄ ‚¶Éx y : X‚¶Ñ, x ‚â† y ‚Üí {y}·∂ú ‚àà ùìù x,
      ‚àÄ ‚¶Éx y : X‚¶Ñ, x ‚â† y ‚Üí ‚àÉ s ‚àà ùìù x, y ‚àâ s,
      ‚àÄ ‚¶Éx y : X‚¶Ñ, x ‚â† y ‚Üí ‚àÉ U : Set X, IsOpen U ‚àß x ‚àà U ‚àß y ‚àâ U,
      ‚àÄ ‚¶Éx y : X‚¶Ñ, x ‚â† y ‚Üí Disjoint (ùìù x) (pure y),
      ‚àÄ ‚¶Éx y : X‚¶Ñ, x ‚â† y ‚Üí Disjoint (pure x) (ùìù y),
      ‚àÄ ‚¶Éx y : X‚¶Ñ, x ‚§≥ y ‚Üí x = y] := by
  tfae_have 1 ‚Üî 2
  ¬∑ exact ‚ü®fun h => h.1, fun h => ‚ü®h‚ü©‚ü©
  tfae_have 2 ‚Üî 3
  ¬∑ simp only [isOpen_compl_iff]
  tfae_have 5 ‚Üî 3
  ¬∑ refine forall_swap.trans ?_
    simp only [isOpen_iff_mem_nhds, mem_compl_iff, mem_singleton_iff]
  tfae_have 5 ‚Üî 6
  ¬∑ simp only [‚Üê subset_compl_singleton_iff, exists_mem_subset_iff]
  tfae_have 5 ‚Üî 7
  ¬∑ simp only [(nhds_basis_opens _).mem_iff, subset_compl_singleton_iff, exists_prop, and_assoc,
      and_left_comm]
  tfae_have 5 ‚Üî 8
  ¬∑ simp only [‚Üê principal_singleton, disjoint_principal_right]
  tfae_have 8 ‚Üî 9
  ¬∑ exact forall_swap.trans (by simp only [disjoint_comm, ne_comm])
  tfae_have 1 ‚Üí 4
  ¬∑ simp only [continuous_def, CofiniteTopology.isOpen_iff']
    rintro H s (rfl | hs)
    exacts [isOpen_empty, compl_compl s ‚ñ∏ (@Set.Finite.isClosed _ _ H _ hs).isOpen_compl]
  tfae_have 4 ‚Üí 2
  ¬∑ exact fun h x => (CofiniteTopology.isClosed_iff.2 <| Or.inr (finite_singleton _)).preimage h
  tfae_have 2 ‚Üî 10
  ¬∑ simp only [‚Üê closure_subset_iff_isClosed, specializes_iff_mem_closure, subset_def,
      mem_singleton_iff, eq_comm]
  tfae_finish
#align t1_space_tfae t1Space_TFAE

theorem t1Space_iff_continuous_cofinite_of : T1Space X ‚Üî Continuous (@CofiniteTopology.of X) :=
  (t1Space_TFAE X).out 0 3
#align t1_space_iff_continuous_cofinite_of t1Space_iff_continuous_cofinite_of

theorem CofiniteTopology.continuous_of [T1Space X] : Continuous (@CofiniteTopology.of X) :=
  t1Space_iff_continuous_cofinite_of.mp ‚Äπ_‚Ä∫
#align cofinite_topology.continuous_of CofiniteTopology.continuous_of

theorem t1Space_iff_exists_open :
    T1Space X ‚Üî Pairwise fun x y => ‚àÉ U : Set X, IsOpen U ‚àß x ‚àà U ‚àß y ‚àâ U :=
  (t1Space_TFAE X).out 0 6
#align t1_space_iff_exists_open t1Space_iff_exists_open

theorem t1Space_iff_disjoint_pure_nhds : T1Space X ‚Üî ‚àÄ ‚¶Éx y : X‚¶Ñ, x ‚â† y ‚Üí Disjoint (pure x) (ùìù y) :=
  (t1Space_TFAE X).out 0 8
#align t1_space_iff_disjoint_pure_nhds t1Space_iff_disjoint_pure_nhds

theorem t1Space_iff_disjoint_nhds_pure : T1Space X ‚Üî ‚àÄ ‚¶Éx y : X‚¶Ñ, x ‚â† y ‚Üí Disjoint (ùìù x) (pure y) :=
  (t1Space_TFAE X).out 0 7
#align t1_space_iff_disjoint_nhds_pure t1Space_iff_disjoint_nhds_pure

theorem t1Space_iff_specializes_imp_eq : T1Space X ‚Üî ‚àÄ ‚¶Éx y : X‚¶Ñ, x ‚§≥ y ‚Üí x = y :=
  (t1Space_TFAE X).out 0 9
#align t1_space_iff_specializes_imp_eq t1Space_iff_specializes_imp_eq

theorem disjoint_pure_nhds [T1Space X] {x y : X} (h : x ‚â† y) : Disjoint (pure x) (ùìù y) :=
  t1Space_iff_disjoint_pure_nhds.mp ‚Äπ_‚Ä∫ h
#align disjoint_pure_nhds disjoint_pure_nhds

theorem disjoint_nhds_pure [T1Space X] {x y : X} (h : x ‚â† y) : Disjoint (ùìù x) (pure y) :=
  t1Space_iff_disjoint_nhds_pure.mp ‚Äπ_‚Ä∫ h
#align disjoint_nhds_pure disjoint_nhds_pure

theorem Specializes.eq [T1Space X] {x y : X} (h : x ‚§≥ y) : x = y :=
  t1Space_iff_specializes_imp_eq.1 ‚Äπ_‚Ä∫ h
#align specializes.eq Specializes.eq

theorem specializes_iff_eq [T1Space X] {x y : X} : x ‚§≥ y ‚Üî x = y :=
  ‚ü®Specializes.eq, fun h => h ‚ñ∏ specializes_rfl‚ü©
#align specializes_iff_eq specializes_iff_eq

@[simp] theorem specializes_eq_eq [T1Space X] : (¬∑ ‚§≥ ¬∑) = @Eq X :=
  funext‚ÇÇ fun _ _ => propext specializes_iff_eq
#align specializes_eq_eq specializes_eq_eq

@[simp]
theorem pure_le_nhds_iff [T1Space X] {a b : X} : pure a ‚â§ ùìù b ‚Üî a = b :=
  specializes_iff_pure.symm.trans specializes_iff_eq
#align pure_le_nhds_iff pure_le_nhds_iff

@[simp]
theorem nhds_le_nhds_iff [T1Space X] {a b : X} : ùìù a ‚â§ ùìù b ‚Üî a = b :=
  specializes_iff_eq
#align nhds_le_nhds_iff nhds_le_nhds_iff

instance (priority := 100) [T1Space X] : R0Space X where
  specializes_symmetric _ _ := by rw [specializes_iff_eq, specializes_iff_eq]; exact Eq.symm

instance : T1Space (CofiniteTopology X) :=
  t1Space_iff_continuous_cofinite_of.mpr continuous_id

theorem t1Space_antitone : Antitone (@T1Space X) := fun a _ h _ =>
  @T1Space.mk _ a fun x => (T1Space.t1 x).mono h
#align t1_space_antitone t1Space_antitone

theorem continuousWithinAt_update_of_ne [T1Space X] [DecidableEq X] [TopologicalSpace Y] {f : X ‚Üí Y}
    {s : Set X} {x x' : X} {y : Y} (hne : x' ‚â† x) :
    ContinuousWithinAt (Function.update f x y) s x' ‚Üî ContinuousWithinAt f s x' :=
  EventuallyEq.congr_continuousWithinAt
    (mem_nhdsWithin_of_mem_nhds <| mem_of_superset (isOpen_ne.mem_nhds hne) fun _y' hy' =>
      Function.update_noteq hy' _ _)
    (Function.update_noteq hne _ _)
#align continuous_within_at_update_of_ne continuousWithinAt_update_of_ne

theorem continuousAt_update_of_ne [T1Space X] [DecidableEq X] [TopologicalSpace Y]
    {f : X ‚Üí Y} {x x' : X} {y : Y} (hne : x' ‚â† x) :
    ContinuousAt (Function.update f x y) x' ‚Üî ContinuousAt f x' := by
  simp only [‚Üê continuousWithinAt_univ, continuousWithinAt_update_of_ne hne]
#align continuous_at_update_of_ne continuousAt_update_of_ne

theorem continuousOn_update_iff [T1Space X] [DecidableEq X] [TopologicalSpace Y] {f : X ‚Üí Y}
    {s : Set X} {x : X} {y : Y} :
    ContinuousOn (Function.update f x y) s ‚Üî
      ContinuousOn f (s \ {x}) ‚àß (x ‚àà s ‚Üí Tendsto f (ùìù[s \ {x}] x) (ùìù y)) := by
  rw [ContinuousOn, ‚Üê and_forall_ne x, and_comm]
  refine and_congr ‚ü®fun H z hz => ?_, fun H z hzx hzs => ?_‚ü© (forall_congr' fun _ => ?_)
  ¬∑ specialize H z hz.2 hz.1
    rw [continuousWithinAt_update_of_ne hz.2] at H
    exact H.mono diff_subset
  ¬∑ rw [continuousWithinAt_update_of_ne hzx]
    refine (H z ‚ü®hzs, hzx‚ü©).mono_of_mem (inter_mem_nhdsWithin _ ?_)
    exact isOpen_ne.mem_nhds hzx
  ¬∑ exact continuousWithinAt_update_same
#align continuous_on_update_iff continuousOn_update_iff

theorem t1Space_of_injective_of_continuous [TopologicalSpace Y] {f : X ‚Üí Y}
    (hf : Function.Injective f) (hf' : Continuous f) [T1Space Y] : T1Space X :=
  t1Space_iff_specializes_imp_eq.2 fun _ _ h => hf (h.map hf').eq
#align t1_space_of_injective_of_continuous t1Space_of_injective_of_continuous

protected theorem Embedding.t1Space [TopologicalSpace Y] [T1Space Y] {f : X ‚Üí Y}
    (hf : Embedding f) : T1Space X :=
  t1Space_of_injective_of_continuous hf.inj hf.continuous
#align embedding.t1_space Embedding.t1Space

instance Subtype.t1Space {X : Type u} [TopologicalSpace X] [T1Space X] {p : X ‚Üí Prop} :
    T1Space (Subtype p) :=
  embedding_subtype_val.t1Space
#align subtype.t1_space Subtype.t1Space

instance [TopologicalSpace Y] [T1Space X] [T1Space Y] : T1Space (X √ó Y) :=
  ‚ü®fun ‚ü®a, b‚ü© => @singleton_prod_singleton _ _ a b ‚ñ∏ isClosed_singleton.prod isClosed_singleton‚ü©

instance {Œπ : Type*} {X : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)] [‚àÄ i, T1Space (X i)] :
    T1Space (‚àÄ i, X i) :=
  ‚ü®fun f => univ_pi_singleton f ‚ñ∏ isClosed_set_pi fun _ _ => isClosed_singleton‚ü©

instance ULift.instT1Space [T1Space X] : T1Space (ULift X) :=
  embedding_uLift_down.t1Space

-- see Note [lower instance priority]
instance (priority := 100) TotallyDisconnectedSpace.t1Space [h: TotallyDisconnectedSpace X] :
    T1Space X := by
  rw [((t1Space_TFAE X).out 0 1 :)]
  intro x
  rw [‚Üê totallyDisconnectedSpace_iff_connectedComponent_singleton.mp h x]
  exact isClosed_connectedComponent

-- see Note [lower instance priority]
instance (priority := 100) T1Space.t0Space [T1Space X] : T0Space X :=
  ‚ü®fun _ _ h => h.specializes.eq‚ü©
#align t1_space.t0_space T1Space.t0Space

@[simp]
theorem compl_singleton_mem_nhds_iff [T1Space X] {x y : X} : {x}·∂ú ‚àà ùìù y ‚Üî y ‚â† x :=
  isOpen_compl_singleton.mem_nhds_iff
#align compl_singleton_mem_nhds_iff compl_singleton_mem_nhds_iff

theorem compl_singleton_mem_nhds [T1Space X] {x y : X} (h : y ‚â† x) : {x}·∂ú ‚àà ùìù y :=
  compl_singleton_mem_nhds_iff.mpr h
#align compl_singleton_mem_nhds compl_singleton_mem_nhds

@[simp]
theorem closure_singleton [T1Space X] {x : X} : closure ({x} : Set X) = {x} :=
  isClosed_singleton.closure_eq
#align closure_singleton closure_singleton

-- Porting note (#11215): TODO: the proof was `hs.induction_on (by simp) fun x => by simp`
theorem Set.Subsingleton.closure [T1Space X] {s : Set X} (hs : s.Subsingleton) :
    (closure s).Subsingleton := by
  rcases hs.eq_empty_or_singleton with (rfl | ‚ü®x, rfl‚ü©) <;> simp
#align set.subsingleton.closure Set.Subsingleton.closure

@[simp]
theorem subsingleton_closure [T1Space X] {s : Set X} : (closure s).Subsingleton ‚Üî s.Subsingleton :=
  ‚ü®fun h => h.anti subset_closure, fun h => h.closure‚ü©
#align subsingleton_closure subsingleton_closure

theorem isClosedMap_const {X Y} [TopologicalSpace X] [TopologicalSpace Y] [T1Space Y] {y : Y} :
    IsClosedMap (Function.const X y) :=
  IsClosedMap.of_nonempty fun s _ h2s => by simp_rw [const, h2s.image_const, isClosed_singleton]
#align is_closed_map_const isClosedMap_const

theorem nhdsWithin_insert_of_ne [T1Space X] {x y : X} {s : Set X} (hxy : x ‚â† y) :
    ùìù[insert y s] x = ùìù[s] x := by
  refine le_antisymm (Filter.le_def.2 fun t ht => ?_) (nhdsWithin_mono x <| subset_insert y s)
  obtain ‚ü®o, ho, hxo, host‚ü© := mem_nhdsWithin.mp ht
  refine mem_nhdsWithin.mpr ‚ü®o \ {y}, ho.sdiff isClosed_singleton, ‚ü®hxo, hxy‚ü©, ?_‚ü©
  rw [inter_insert_of_not_mem <| not_mem_diff_of_mem (mem_singleton y)]
  exact (inter_subset_inter diff_subset Subset.rfl).trans host
#align nhds_within_insert_of_ne nhdsWithin_insert_of_ne

/-- If `t` is a subset of `s`, except for one point,
then `insert x s` is a neighborhood of `x` within `t`. -/
theorem insert_mem_nhdsWithin_of_subset_insert [T1Space X] {x y : X} {s t : Set X}
    (hu : t ‚äÜ insert y s) : insert x s ‚àà ùìù[t] x := by
  rcases eq_or_ne x y with (rfl | h)
  ¬∑ exact mem_of_superset self_mem_nhdsWithin hu
  refine nhdsWithin_mono x hu ?_
  rw [nhdsWithin_insert_of_ne h]
  exact mem_of_superset self_mem_nhdsWithin (subset_insert x s)
#align insert_mem_nhds_within_of_subset_insert insert_mem_nhdsWithin_of_subset_insert

@[simp]
theorem ker_nhds [T1Space X] (x : X) : (ùìù x).ker = {x} := by
  simp [ker_nhds_eq_specializes]

theorem biInter_basis_nhds [T1Space X] {Œπ : Sort*} {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí Set X} {x : X}
    (h : (ùìù x).HasBasis p s) : ‚ãÇ (i) (_ : p i), s i = {x} := by
  rw [‚Üê h.ker, ker_nhds]
#align bInter_basis_nhds biInter_basis_nhds

@[simp]
theorem compl_singleton_mem_nhdsSet_iff [T1Space X] {x : X} {s : Set X} : {x}·∂ú ‚àà ùìùÀ¢ s ‚Üî x ‚àâ s := by
  rw [isOpen_compl_singleton.mem_nhdsSet, subset_compl_singleton_iff]
#align compl_singleton_mem_nhds_set_iff compl_singleton_mem_nhdsSet_iff

@[simp]
theorem nhdsSet_le_iff [T1Space X] {s t : Set X} : ùìùÀ¢ s ‚â§ ùìùÀ¢ t ‚Üî s ‚äÜ t := by
  refine ‚ü®?_, fun h => monotone_nhdsSet h‚ü©
  simp_rw [Filter.le_def]; intro h x hx
  specialize h {x}·∂ú
  simp_rw [compl_singleton_mem_nhdsSet_iff] at h
  by_contra hxt
  exact h hxt hx
#align nhds_set_le_iff nhdsSet_le_iff

@[simp]
theorem nhdsSet_inj_iff [T1Space X] {s t : Set X} : ùìùÀ¢ s = ùìùÀ¢ t ‚Üî s = t := by
  simp_rw [le_antisymm_iff]
  exact and_congr nhdsSet_le_iff nhdsSet_le_iff
#align nhds_set_inj_iff nhdsSet_inj_iff

theorem injective_nhdsSet [T1Space X] : Function.Injective (ùìùÀ¢ : Set X ‚Üí Filter X) := fun _ _ hst =>
  nhdsSet_inj_iff.mp hst
#align injective_nhds_set injective_nhdsSet

theorem strictMono_nhdsSet [T1Space X] : StrictMono (ùìùÀ¢ : Set X ‚Üí Filter X) :=
  monotone_nhdsSet.strictMono_of_injective injective_nhdsSet
#align strict_mono_nhds_set strictMono_nhdsSet

@[simp]
theorem nhds_le_nhdsSet_iff [T1Space X] {s : Set X} {x : X} : ùìù x ‚â§ ùìùÀ¢ s ‚Üî x ‚àà s := by
  rw [‚Üê nhdsSet_singleton, nhdsSet_le_iff, singleton_subset_iff]
#align nhds_le_nhds_set_iff nhds_le_nhdsSet_iff

/-- Removing a non-isolated point from a dense set, one still obtains a dense set. -/
theorem Dense.diff_singleton [T1Space X] {s : Set X} (hs : Dense s) (x : X) [NeBot (ùìù[‚â†] x)] :
    Dense (s \ {x}) :=
  hs.inter_of_isOpen_right (dense_compl_singleton x) isOpen_compl_singleton
#align dense.diff_singleton Dense.diff_singleton

/-- Removing a finset from a dense set in a space without isolated points, one still
obtains a dense set. -/
theorem Dense.diff_finset [T1Space X] [‚àÄ x : X, NeBot (ùìù[‚â†] x)] {s : Set X} (hs : Dense s)
    (t : Finset X) : Dense (s \ t) := by
  induction t using Finset.induction_on with
  | empty => simpa using hs
  | insert _ ih =>
    rw [Finset.coe_insert, ‚Üê union_singleton, ‚Üê diff_diff]
    exact ih.diff_singleton _
#align dense.diff_finset Dense.diff_finset

/-- Removing a finite set from a dense set in a space without isolated points, one still
obtains a dense set. -/
theorem Dense.diff_finite [T1Space X] [‚àÄ x : X, NeBot (ùìù[‚â†] x)] {s : Set X} (hs : Dense s)
    {t : Set X} (ht : t.Finite) : Dense (s \ t) := by
  convert hs.diff_finset ht.toFinset
  exact (Finite.coe_toFinset _).symm
#align dense.diff_finite Dense.diff_finite

/-- If a function to a `T1Space` tends to some limit `y` at some point `x`, then necessarily
`y = f x`. -/
theorem eq_of_tendsto_nhds [TopologicalSpace Y] [T1Space Y] {f : X ‚Üí Y} {x : X} {y : Y}
    (h : Tendsto f (ùìù x) (ùìù y)) : f x = y :=
  by_contra fun hfa : f x ‚â† y =>
    have fact‚ÇÅ : {f x}·∂ú ‚àà ùìù y := compl_singleton_mem_nhds hfa.symm
    have fact‚ÇÇ : Tendsto f (pure x) (ùìù y) := h.comp (tendsto_id'.2 <| pure_le_nhds x)
    fact‚ÇÇ fact‚ÇÅ (Eq.refl <| f x)
#align eq_of_tendsto_nhds eq_of_tendsto_nhds

theorem Filter.Tendsto.eventually_ne [TopologicalSpace Y] [T1Space Y] {g : X ‚Üí Y}
    {l : Filter X} {b‚ÇÅ b‚ÇÇ : Y} (hg : Tendsto g l (ùìù b‚ÇÅ)) (hb : b‚ÇÅ ‚â† b‚ÇÇ) : ‚àÄ·∂† z in l, g z ‚â† b‚ÇÇ :=
  hg.eventually (isOpen_compl_singleton.eventually_mem hb)
#align filter.tendsto.eventually_ne Filter.Tendsto.eventually_ne

theorem ContinuousAt.eventually_ne [TopologicalSpace Y] [T1Space Y] {g : X ‚Üí Y} {x : X} {y : Y}
    (hg1 : ContinuousAt g x) (hg2 : g x ‚â† y) : ‚àÄ·∂† z in ùìù x, g z ‚â† y :=
  hg1.tendsto.eventually_ne hg2
#align continuous_at.eventually_ne ContinuousAt.eventually_ne

theorem eventually_ne_nhds [T1Space X] {a b : X} (h : a ‚â† b) : ‚àÄ·∂† x in ùìù a, x ‚â† b :=
  IsOpen.eventually_mem isOpen_ne h

theorem eventually_ne_nhdsWithin [T1Space X] {a b : X} {s : Set X} (h : a ‚â† b) :
    ‚àÄ·∂† x in ùìù[s] a, x ‚â† b :=
  Filter.Eventually.filter_mono nhdsWithin_le_nhds <| eventually_ne_nhds h

/-- To prove a function to a `T1Space` is continuous at some point `x`, it suffices to prove that
`f` admits *some* limit at `x`. -/
theorem continuousAt_of_tendsto_nhds [TopologicalSpace Y] [T1Space Y] {f : X ‚Üí Y} {x : X} {y : Y}
    (h : Tendsto f (ùìù x) (ùìù y)) : ContinuousAt f x := by
  rwa [ContinuousAt, eq_of_tendsto_nhds h]
#align continuous_at_of_tendsto_nhds continuousAt_of_tendsto_nhds

@[simp]
theorem tendsto_const_nhds_iff [T1Space X] {l : Filter Y} [NeBot l] {c d : X} :
    Tendsto (fun _ => c) l (ùìù d) ‚Üî c = d := by simp_rw [Tendsto, Filter.map_const, pure_le_nhds_iff]
#align tendsto_const_nhds_iff tendsto_const_nhds_iff

/-- A point with a finite neighborhood has to be isolated. -/
theorem isOpen_singleton_of_finite_mem_nhds [T1Space X] (x : X)
    {s : Set X} (hs : s ‚àà ùìù x) (hsf : s.Finite) : IsOpen ({x} : Set X) := by
  have A : {x} ‚äÜ s := by simp only [singleton_subset_iff, mem_of_mem_nhds hs]
  have B : IsClosed (s \ {x}) := (hsf.subset diff_subset).isClosed
  have C : (s \ {x})·∂ú ‚àà ùìù x := B.isOpen_compl.mem_nhds fun h => h.2 rfl
  have D : {x} ‚àà ùìù x := by simpa only [‚Üê diff_eq, diff_diff_cancel_left A] using inter_mem hs C
  rwa [‚Üê mem_interior_iff_mem_nhds, ‚Üê singleton_subset_iff, subset_interior_iff_isOpen] at D
#align is_open_singleton_of_finite_mem_nhds isOpen_singleton_of_finite_mem_nhds

/-- If the punctured neighborhoods of a point form a nontrivial filter, then any neighborhood is
infinite. -/
theorem infinite_of_mem_nhds {X} [TopologicalSpace X] [T1Space X] (x : X) [hx : NeBot (ùìù[‚â†] x)]
    {s : Set X} (hs : s ‚àà ùìù x) : Set.Infinite s := by
  refine fun hsf => hx.1 ?_
  rw [‚Üê isOpen_singleton_iff_punctured_nhds]
  exact isOpen_singleton_of_finite_mem_nhds x hs hsf
#align infinite_of_mem_nhds infinite_of_mem_nhds

theorem discrete_of_t1_of_finite [T1Space X] [Finite X] :
    DiscreteTopology X := by
  apply singletons_open_iff_discrete.mp
  intro x
  rw [‚Üê isClosed_compl_iff]
  exact (Set.toFinite _).isClosed
#align discrete_of_t1_of_finite discrete_of_t1_of_finite

theorem PreconnectedSpace.trivial_of_discrete [PreconnectedSpace X] [DiscreteTopology X] :
    Subsingleton X := by
  rw [‚Üê not_nontrivial_iff_subsingleton]
  rintro ‚ü®x, y, hxy‚ü©
  rw [Ne, ‚Üê mem_singleton_iff, (isClopen_discrete _).eq_univ <| singleton_nonempty y] at hxy
  exact hxy (mem_univ x)
#align preconnected_space.trivial_of_discrete PreconnectedSpace.trivial_of_discrete

theorem IsPreconnected.infinite_of_nontrivial [T1Space X] {s : Set X} (h : IsPreconnected s)
    (hs : s.Nontrivial) : s.Infinite := by
  refine mt (fun hf => (subsingleton_coe s).mp ?_) (not_subsingleton_iff.mpr hs)
  haveI := @discrete_of_t1_of_finite s _ _ hf.to_subtype
  exact @PreconnectedSpace.trivial_of_discrete _ _ (Subtype.preconnectedSpace h) _
#align is_preconnected.infinite_of_nontrivial IsPreconnected.infinite_of_nontrivial

theorem ConnectedSpace.infinite [ConnectedSpace X] [Nontrivial X] [T1Space X] : Infinite X :=
  infinite_univ_iff.mp <| isPreconnected_univ.infinite_of_nontrivial nontrivial_univ
#align connected_space.infinite ConnectedSpace.infinite

/-- A non-trivial connected T1 space has no isolated points. -/
instance (priority := 100) ConnectedSpace.neBot_nhdsWithin_compl_of_nontrivial_of_t1space
    [ConnectedSpace X] [Nontrivial X] [T1Space X] (x : X) :
    NeBot (ùìù[‚â†] x) := by
  by_contra contra
  rw [not_neBot, ‚Üê isOpen_singleton_iff_punctured_nhds] at contra
  replace contra := nonempty_inter isOpen_compl_singleton
    contra (compl_union_self _) (Set.nonempty_compl_of_nontrivial _) (singleton_nonempty _)
  simp [compl_inter_self {x}] at contra

theorem SeparationQuotient.t1Space_iff : T1Space (SeparationQuotient X) ‚Üî R0Space X := by
  rw [r0Space_iff, ((t1Space_TFAE (SeparationQuotient X)).out 0 9 :)]
  constructor
  ¬∑ intro h x y xspecy
    rw [‚Üê Inducing.specializes_iff inducing_mk, h xspecy] at *
  ¬∑ rintro h ‚ü®x‚ü© ‚ü®y‚ü© sxspecsy
    have xspecy : x ‚§≥ y := (Inducing.specializes_iff inducing_mk).mp sxspecsy
    have yspecx : y ‚§≥ x := h xspecy
    erw [mk_eq_mk, inseparable_iff_specializes_and]
    exact ‚ü®xspecy, yspecx‚ü©

theorem singleton_mem_nhdsWithin_of_mem_discrete {s : Set X} [DiscreteTopology s] {x : X}
    (hx : x ‚àà s) : {x} ‚àà ùìù[s] x := by
  have : ({‚ü®x, hx‚ü©} : Set s) ‚àà ùìù (‚ü®x, hx‚ü© : s) := by simp [nhds_discrete]
  simpa only [nhdsWithin_eq_map_subtype_coe hx, image_singleton] using
    @image_mem_map _ _ _ ((‚Üë) : s ‚Üí X) _ this
#align singleton_mem_nhds_within_of_mem_discrete singleton_mem_nhdsWithin_of_mem_discrete

/-- The neighbourhoods filter of `x` within `s`, under the discrete topology, is equal to
the pure `x` filter (which is the principal filter at the singleton `{x}`.) -/
theorem nhdsWithin_of_mem_discrete {s : Set X} [DiscreteTopology s] {x : X} (hx : x ‚àà s) :
    ùìù[s] x = pure x :=
  le_antisymm (le_pure_iff.2 <| singleton_mem_nhdsWithin_of_mem_discrete hx) (pure_le_nhdsWithin hx)
#align nhds_within_of_mem_discrete nhdsWithin_of_mem_discrete

theorem Filter.HasBasis.exists_inter_eq_singleton_of_mem_discrete {Œπ : Type*} {p : Œπ ‚Üí Prop}
    {t : Œπ ‚Üí Set X} {s : Set X} [DiscreteTopology s] {x : X} (hb : (ùìù x).HasBasis p t)
    (hx : x ‚àà s) : ‚àÉ i, p i ‚àß t i ‚à© s = {x} := by
  rcases (nhdsWithin_hasBasis hb s).mem_iff.1 (singleton_mem_nhdsWithin_of_mem_discrete hx) with
    ‚ü®i, hi, hix‚ü©
  exact ‚ü®i, hi, hix.antisymm <| singleton_subset_iff.2 ‚ü®mem_of_mem_nhds <| hb.mem_of_mem hi, hx‚ü©‚ü©
#align filter.has_basis.exists_inter_eq_singleton_of_mem_discrete Filter.HasBasis.exists_inter_eq_singleton_of_mem_discrete

/-- A point `x` in a discrete subset `s` of a topological space admits a neighbourhood
that only meets `s` at `x`.  -/
theorem nhds_inter_eq_singleton_of_mem_discrete {s : Set X} [DiscreteTopology s] {x : X}
    (hx : x ‚àà s) : ‚àÉ U ‚àà ùìù x, U ‚à© s = {x} := by
  simpa using (ùìù x).basis_sets.exists_inter_eq_singleton_of_mem_discrete hx
#align nhds_inter_eq_singleton_of_mem_discrete nhds_inter_eq_singleton_of_mem_discrete

/-- Let `x` be a point in a discrete subset `s` of a topological space, then there exists an open
set that only meets `s` at `x`.  -/
theorem isOpen_inter_eq_singleton_of_mem_discrete {s : Set X} [DiscreteTopology s] {x : X}
    (hx : x ‚àà s) : ‚àÉ U : Set X, IsOpen U ‚àß U ‚à© s = {x} := by
  obtain ‚ü®U, hU_nhds, hU_inter‚ü© := nhds_inter_eq_singleton_of_mem_discrete hx
  obtain ‚ü®t, ht_sub, ht_open, ht_x‚ü© := mem_nhds_iff.mp hU_nhds
  refine ‚ü®t, ht_open, Set.Subset.antisymm ?_ ?_‚ü©
  ¬∑ exact hU_inter ‚ñ∏ Set.inter_subset_inter_left s ht_sub
  ¬∑ rw [Set.subset_inter_iff, Set.singleton_subset_iff, Set.singleton_subset_iff]
    exact ‚ü®ht_x, hx‚ü©

/-- For point `x` in a discrete subset `s` of a topological space, there is a set `U`
such that
1. `U` is a punctured neighborhood of `x` (ie. `U ‚à™ {x}` is a neighbourhood of `x`),
2. `U` is disjoint from `s`.
-/
theorem disjoint_nhdsWithin_of_mem_discrete {s : Set X} [DiscreteTopology s] {x : X} (hx : x ‚àà s) :
    ‚àÉ U ‚àà ùìù[‚â†] x, Disjoint U s :=
  let ‚ü®V, h, h'‚ü© := nhds_inter_eq_singleton_of_mem_discrete hx
  ‚ü®{x}·∂ú ‚à© V, inter_mem_nhdsWithin _ h,
    disjoint_iff_inter_eq_empty.mpr (by rw [inter_assoc, h', compl_inter_self])‚ü©
#align disjoint_nhds_within_of_mem_discrete disjoint_nhdsWithin_of_mem_discrete

#align topological_space.subset_trans embedding_inclusion‚Çì

/-! ### R‚ÇÅ (preregular) spaces -/

section R1Space

/-- A topological space is called a *preregular* (a.k.a. R‚ÇÅ) space,
if any two topologically distinguishable points have disjoint neighbourhoods. -/
@[mk_iff r1Space_iff_specializes_or_disjoint_nhds]
class R1Space (X : Type*) [TopologicalSpace X] : Prop where
  specializes_or_disjoint_nhds (x y : X) : Specializes x y ‚à® Disjoint (ùìù x) (ùìù y)

export R1Space (specializes_or_disjoint_nhds)

variable [R1Space X] {x y : X}

instance (priority := 100) : R0Space X where
  specializes_symmetric _ _ h := (specializes_or_disjoint_nhds _ _).resolve_right <| fun hd ‚Ü¶
    h.not_disjoint hd.symm

theorem disjoint_nhds_nhds_iff_not_specializes : Disjoint (ùìù x) (ùìù y) ‚Üî ¬¨x ‚§≥ y :=
  ‚ü®fun hd hspec ‚Ü¶ hspec.not_disjoint hd, (specializes_or_disjoint_nhds _ _).resolve_left‚ü©
#align disjoint_nhds_nhds_iff_not_specializes disjoint_nhds_nhds_iff_not_specializes

theorem specializes_iff_not_disjoint : x ‚§≥ y ‚Üî ¬¨Disjoint (ùìù x) (ùìù y) :=
  disjoint_nhds_nhds_iff_not_specializes.not_left.symm

theorem disjoint_nhds_nhds_iff_not_inseparable : Disjoint (ùìù x) (ùìù y) ‚Üî ¬¨Inseparable x y := by
  rw [disjoint_nhds_nhds_iff_not_specializes, specializes_iff_inseparable]

theorem r1Space_iff_inseparable_or_disjoint_nhds {X : Type*} [TopologicalSpace X]:
    R1Space X ‚Üî ‚àÄ x y : X, Inseparable x y ‚à® Disjoint (ùìù x) (ùìù y) :=
  ‚ü®fun _h x y ‚Ü¶ (specializes_or_disjoint_nhds x y).imp_left Specializes.inseparable, fun h ‚Ü¶
    ‚ü®fun x y ‚Ü¶ (h x y).imp_left Inseparable.specializes‚ü©‚ü©

theorem isClosed_setOf_specializes : IsClosed { p : X √ó X | p.1 ‚§≥ p.2 } := by
  simp only [‚Üê isOpen_compl_iff, compl_setOf, ‚Üê disjoint_nhds_nhds_iff_not_specializes,
    isOpen_setOf_disjoint_nhds_nhds]
#align is_closed_set_of_specializes isClosed_setOf_specializes

theorem isClosed_setOf_inseparable : IsClosed { p : X √ó X | Inseparable p.1 p.2 } := by
  simp only [‚Üê specializes_iff_inseparable, isClosed_setOf_specializes]
#align is_closed_set_of_inseparable isClosed_setOf_inseparable

/-- In an R‚ÇÅ space, a point belongs to the closure of a compact set `K`
if and only if it is topologically inseparable from some point of `K`. -/
theorem IsCompact.mem_closure_iff_exists_inseparable {K : Set X} (hK : IsCompact K) :
    y ‚àà closure K ‚Üî ‚àÉ x ‚àà K, Inseparable x y := by
  refine ‚ü®fun hy ‚Ü¶ ?_, fun ‚ü®x, hxK, hxy‚ü© ‚Ü¶
    (hxy.mem_closed_iff isClosed_closure).1 <| subset_closure hxK‚ü©
  contrapose! hy
  have : Disjoint (ùìù y) (ùìùÀ¢ K) := hK.disjoint_nhdsSet_right.2 fun x hx ‚Ü¶
    (disjoint_nhds_nhds_iff_not_inseparable.2 (hy x hx)).symm
  simpa only [disjoint_iff, not_mem_closure_iff_nhdsWithin_eq_bot]
    using this.mono_right principal_le_nhdsSet

theorem IsCompact.closure_eq_biUnion_inseparable {K : Set X} (hK : IsCompact K) :
    closure K = ‚ãÉ x ‚àà K, {y | Inseparable x y} := by
  ext; simp [hK.mem_closure_iff_exists_inseparable]

/-- In an R‚ÇÅ space, the closure of a compact set is the union of the closures of its points. -/
theorem IsCompact.closure_eq_biUnion_closure_singleton {K : Set X} (hK : IsCompact K) :
    closure K = ‚ãÉ x ‚àà K, closure {x} := by
  simp only [hK.closure_eq_biUnion_inseparable, ‚Üê specializes_iff_inseparable,
    specializes_iff_mem_closure, setOf_mem_eq]

/-- In an R‚ÇÅ space, if a compact set `K` is contained in an open set `U`,
then its closure is also contained in `U`. -/
theorem IsCompact.closure_subset_of_isOpen {K : Set X} (hK : IsCompact K)
    {U : Set X} (hU : IsOpen U) (hKU : K ‚äÜ U) : closure K ‚äÜ U := by
  rw [hK.closure_eq_biUnion_inseparable, iUnion‚ÇÇ_subset_iff]
  exact fun x hx y hxy ‚Ü¶ (hxy.mem_open_iff hU).1 (hKU hx)

/-- The closure of a compact set in an R‚ÇÅ space is a compact set. -/
protected theorem IsCompact.closure {K : Set X} (hK : IsCompact K) : IsCompact (closure K) := by
  refine isCompact_of_finite_subcover fun U hUo hKU ‚Ü¶ ?_
  rcases hK.elim_finite_subcover U hUo (subset_closure.trans hKU) with ‚ü®t, ht‚ü©
  exact ‚ü®t, hK.closure_subset_of_isOpen (isOpen_biUnion fun _ _ ‚Ü¶ hUo _) ht‚ü©

theorem IsCompact.closure_of_subset {s K : Set X} (hK : IsCompact K) (h : s ‚äÜ K) :
    IsCompact (closure s) :=
  hK.closure.of_isClosed_subset isClosed_closure (closure_mono h)
#align is_compact_closure_of_subset_compact IsCompact.closure_of_subset

@[deprecated (since := "2024-01-28")]
alias isCompact_closure_of_subset_compact := IsCompact.closure_of_subset

@[simp]
theorem exists_isCompact_superset_iff {s : Set X} :
    (‚àÉ K, IsCompact K ‚àß s ‚äÜ K) ‚Üî IsCompact (closure s) :=
  ‚ü®fun ‚ü®_K, hK, hsK‚ü© => hK.closure_of_subset hsK, fun h => ‚ü®closure s, h, subset_closure‚ü©‚ü©
#align exists_compact_superset_iff exists_isCompact_superset_iff

@[deprecated (since := "2024-01-28")]
alias exists_compact_superset_iff := exists_isCompact_superset_iff

/-- If `K` and `L` are disjoint compact sets in an R‚ÇÅ topological space
and `L` is also closed, then `K` and `L` have disjoint neighborhoods.  -/
theorem SeparatedNhds.of_isCompact_isCompact_isClosed {K L : Set X} (hK : IsCompact K)
    (hL : IsCompact L) (h'L : IsClosed L) (hd : Disjoint K L) : SeparatedNhds K L := by
  simp_rw [separatedNhds_iff_disjoint, hK.disjoint_nhdsSet_left, hL.disjoint_nhdsSet_right,
    disjoint_nhds_nhds_iff_not_inseparable]
  intro x hx y hy h
  exact absurd ((h.mem_closed_iff h'L).2 hy) <| disjoint_left.1 hd hx

@[deprecated (since := "2024-01-28")]
alias separatedNhds_of_isCompact_isCompact_isClosed := SeparatedNhds.of_isCompact_isCompact_isClosed

/-- If a compact set is covered by two open sets, then we can cover it by two compact subsets. -/
theorem IsCompact.binary_compact_cover {K U V : Set X}
    (hK : IsCompact K) (hU : IsOpen U) (hV : IsOpen V) (h2K : K ‚äÜ U ‚à™ V) :
    ‚àÉ K‚ÇÅ K‚ÇÇ : Set X, IsCompact K‚ÇÅ ‚àß IsCompact K‚ÇÇ ‚àß K‚ÇÅ ‚äÜ U ‚àß K‚ÇÇ ‚äÜ V ‚àß K = K‚ÇÅ ‚à™ K‚ÇÇ := by
  have hK' : IsCompact (closure K) := hK.closure
  have : SeparatedNhds (closure K \ U) (closure K \ V) := by
    apply SeparatedNhds.of_isCompact_isCompact_isClosed (hK'.diff hU) (hK'.diff hV)
      (isClosed_closure.sdiff hV)
    rw [disjoint_iff_inter_eq_empty, diff_inter_diff, diff_eq_empty]
    exact hK.closure_subset_of_isOpen (hU.union hV) h2K
  have : SeparatedNhds (K \ U) (K \ V) :=
    this.mono (diff_subset_diff_left (subset_closure)) (diff_subset_diff_left (subset_closure))
  rcases this with ‚ü®O‚ÇÅ, O‚ÇÇ, h1O‚ÇÅ, h1O‚ÇÇ, h2O‚ÇÅ, h2O‚ÇÇ, hO‚ü©
  exact ‚ü®K \ O‚ÇÅ, K \ O‚ÇÇ, hK.diff h1O‚ÇÅ, hK.diff h1O‚ÇÇ, diff_subset_comm.mp h2O‚ÇÅ,
    diff_subset_comm.mp h2O‚ÇÇ, by rw [‚Üê diff_inter, hO.inter_eq, diff_empty]‚ü©
#align is_compact.binary_compact_cover IsCompact.binary_compact_cover

/-- For every finite open cover `U·µ¢` of a compact set, there exists a compact cover `K·µ¢ ‚äÜ U·µ¢`. -/
theorem IsCompact.finite_compact_cover {s : Set X} (hs : IsCompact s) {Œπ : Type*}
    (t : Finset Œπ) (U : Œπ ‚Üí Set X) (hU : ‚àÄ i ‚àà t, IsOpen (U i)) (hsC : s ‚äÜ ‚ãÉ i ‚àà t, U i) :
    ‚àÉ K : Œπ ‚Üí Set X, (‚àÄ i, IsCompact (K i)) ‚àß (‚àÄ i, K i ‚äÜ U i) ‚àß s = ‚ãÉ i ‚àà t, K i := by
  induction' t using Finset.induction with x t hx ih generalizing U s
  ¬∑ refine ‚ü®fun _ => ‚àÖ, fun _ => isCompact_empty, fun i => empty_subset _, ?_‚ü©
    simpa only [subset_empty_iff, Finset.not_mem_empty, iUnion_false, iUnion_empty] using hsC
  simp only [Finset.set_biUnion_insert] at hsC
  simp only [Finset.forall_mem_insert] at hU
  have hU' : ‚àÄ i ‚àà t, IsOpen (U i) := fun i hi => hU.2 i hi
  rcases hs.binary_compact_cover hU.1 (isOpen_biUnion hU') hsC with
    ‚ü®K‚ÇÅ, K‚ÇÇ, h1K‚ÇÅ, h1K‚ÇÇ, h2K‚ÇÅ, h2K‚ÇÇ, hK‚ü©
  rcases ih h1K‚ÇÇ U hU' h2K‚ÇÇ with ‚ü®K, h1K, h2K, h3K‚ü©
  refine ‚ü®update K x K‚ÇÅ, ?_, ?_, ?_‚ü©
  ¬∑ intro i
    rcases eq_or_ne i x with rfl | hi
    ¬∑ simp only [update_same, h1K‚ÇÅ]
    ¬∑ simp only [update_noteq hi, h1K]
  ¬∑ intro i
    rcases eq_or_ne i x with rfl | hi
    ¬∑ simp only [update_same, h2K‚ÇÅ]
    ¬∑ simp only [update_noteq hi, h2K]
  ¬∑ simp only [Finset.set_biUnion_insert_update _ hx, hK, h3K]
#align is_compact.finite_compact_cover IsCompact.finite_compact_cover

theorem R1Space.of_continuous_specializes_imp [TopologicalSpace Y] {f : Y ‚Üí X} (hc : Continuous f)
    (hspec : ‚àÄ x y, f x ‚§≥ f y ‚Üí x ‚§≥ y) : R1Space Y where
  specializes_or_disjoint_nhds x y := (specializes_or_disjoint_nhds (f x) (f y)).imp (hspec x y) <|
    ((hc.tendsto _).disjoint ¬∑ (hc.tendsto _))

theorem Inducing.r1Space [TopologicalSpace Y] {f : Y ‚Üí X} (hf : Inducing f) : R1Space Y :=
  .of_continuous_specializes_imp hf.continuous fun _ _ ‚Ü¶ hf.specializes_iff.1

protected theorem R1Space.induced (f : Y ‚Üí X) : @R1Space Y (.induced f ‚Äπ_‚Ä∫) :=
  @Inducing.r1Space _ _ _ _ (.induced f _) f (inducing_induced f)

instance (p : X ‚Üí Prop) : R1Space (Subtype p) := .induced _

protected theorem R1Space.sInf {X : Type*} {T : Set (TopologicalSpace X)}
    (hT : ‚àÄ t ‚àà T, @R1Space X t) : @R1Space X (sInf T) := by
  let _ := sInf T
  refine ‚ü®fun x y ‚Ü¶ ?_‚ü©
  simp only [Specializes, nhds_sInf]
  rcases em (‚àÉ t ‚àà T, Disjoint (@nhds X t x) (@nhds X t y)) with ‚ü®t, htT, htd‚ü© | hTd
  ¬∑ exact .inr <| htd.mono (iInf‚ÇÇ_le t htT) (iInf‚ÇÇ_le t htT)
  ¬∑ push_neg at hTd
    exact .inl <| iInf‚ÇÇ_mono fun t ht ‚Ü¶ ((hT t ht).1 x y).resolve_right (hTd t ht)

protected theorem R1Space.iInf {Œπ X : Type*} {t : Œπ ‚Üí TopologicalSpace X}
    (ht : ‚àÄ i, @R1Space X (t i)) : @R1Space X (iInf t) :=
  .sInf <| forall_mem_range.2 ht

protected theorem R1Space.inf {X : Type*} {t‚ÇÅ t‚ÇÇ : TopologicalSpace X}
    (h‚ÇÅ : @R1Space X t‚ÇÅ) (h‚ÇÇ : @R1Space X t‚ÇÇ) : @R1Space X (t‚ÇÅ ‚äì t‚ÇÇ) := by
  rw [inf_eq_iInf]
  apply R1Space.iInf
  simp [*]

instance [TopologicalSpace Y] [R1Space Y] : R1Space (X √ó Y) :=
  .inf (.induced _) (.induced _)

instance {Œπ : Type*} {X : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)] [‚àÄ i, R1Space (X i)] :
    R1Space (‚àÄ i, X i) :=
  .iInf fun _ ‚Ü¶ .induced _

theorem exists_mem_nhds_isCompact_mapsTo_of_isCompact_mem_nhds
    {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] [R1Space Y] {f : X ‚Üí Y} {x : X}
    {K : Set X} {s : Set Y} (hf : Continuous f) (hs : s ‚àà ùìù (f x)) (hKc : IsCompact K)
    (hKx : K ‚àà ùìù x) : ‚àÉ K ‚àà ùìù x, IsCompact K ‚àß MapsTo f K s := by
  have hc : IsCompact (f '' K \ interior s) := (hKc.image hf).diff isOpen_interior
  obtain ‚ü®U, V, Uo, Vo, hxU, hV, hd‚ü© : SeparatedNhds {f x} (f '' K \ interior s) := by
    simp_rw [separatedNhds_iff_disjoint, nhdsSet_singleton, hc.disjoint_nhdsSet_right,
      disjoint_nhds_nhds_iff_not_inseparable]
    rintro y ‚ü®-, hys‚ü© hxy
    refine hys <| (hxy.mem_open_iff isOpen_interior).1 ?_
    rwa [mem_interior_iff_mem_nhds]
  refine ‚ü®K \ f ‚Åª¬π' V, diff_mem hKx ?_, hKc.diff <| Vo.preimage hf, fun y hy ‚Ü¶ ?_‚ü©
  ¬∑ filter_upwards [hf.continuousAt <| Uo.mem_nhds (hxU rfl)] with x hx
      using Set.disjoint_left.1 hd hx
  ¬∑ by_contra hys
    exact hy.2 (hV ‚ü®mem_image_of_mem _ hy.1, not_mem_subset interior_subset hys‚ü©)

instance (priority := 900) {X Y : Type*} [TopologicalSpace X] [WeaklyLocallyCompactSpace X]
    [TopologicalSpace Y] [R1Space Y] : LocallyCompactPair X Y where
  exists_mem_nhds_isCompact_mapsTo hf hs :=
    let ‚ü®_K, hKc, hKx‚ü© := exists_compact_mem_nhds _
    exists_mem_nhds_isCompact_mapsTo_of_isCompact_mem_nhds hf hs hKc hKx

/-- If a point in an R‚ÇÅ space has a compact neighborhood,
then it has a basis of compact closed neighborhoods. -/
theorem IsCompact.isCompact_isClosed_basis_nhds {x : X} {L : Set X} (hLc : IsCompact L)
    (hxL : L ‚àà ùìù x) : (ùìù x).HasBasis (fun K ‚Ü¶ K ‚àà ùìù x ‚àß IsCompact K ‚àß IsClosed K) (¬∑) :=
  hasBasis_self.2 fun _U hU ‚Ü¶
    let ‚ü®K, hKx, hKc, hKU‚ü© := exists_mem_nhds_isCompact_mapsTo_of_isCompact_mem_nhds
      continuous_id (interior_mem_nhds.2 hU) hLc hxL
    ‚ü®closure K, mem_of_superset hKx subset_closure, ‚ü®hKc.closure, isClosed_closure‚ü©,
      (hKc.closure_subset_of_isOpen isOpen_interior hKU).trans interior_subset‚ü©

/-- In an R‚ÇÅ space, the filters `coclosedCompact` and `cocompact` are equal. -/
@[simp]
theorem Filter.coclosedCompact_eq_cocompact : coclosedCompact X = cocompact X := by
  refine le_antisymm ?_ cocompact_le_coclosedCompact
  rw [hasBasis_coclosedCompact.le_basis_iff hasBasis_cocompact]
  exact fun K hK ‚Ü¶ ‚ü®closure K, ‚ü®isClosed_closure, hK.closure‚ü©, compl_subset_compl.2 subset_closure‚ü©
#align filter.coclosed_compact_eq_cocompact Filter.coclosedCompact_eq_cocompact

/-- In an R‚ÇÅ space, the bornologies `relativelyCompact` and `inCompact` are equal. -/
@[simp]
theorem Bornology.relativelyCompact_eq_inCompact :
    Bornology.relativelyCompact X = Bornology.inCompact X :=
  Bornology.ext _ _ Filter.coclosedCompact_eq_cocompact
#align bornology.relatively_compact_eq_in_compact Bornology.relativelyCompact_eq_inCompact

/-!
### Lemmas about a weakly locally compact R‚ÇÅ space

In fact, a space with these properties is locally compact and regular.
Some lemmas are formulated using the latter assumptions below.
-/

variable [WeaklyLocallyCompactSpace X]

/-- In a (weakly) locally compact R‚ÇÅ space, compact closed neighborhoods of a point `x`
form a basis of neighborhoods of `x`. -/
theorem isCompact_isClosed_basis_nhds (x : X) :
    (ùìù x).HasBasis (fun K => K ‚àà ùìù x ‚àß IsCompact K ‚àß IsClosed K) (¬∑) :=
  let ‚ü®_L, hLc, hLx‚ü© := exists_compact_mem_nhds x
  hLc.isCompact_isClosed_basis_nhds hLx

/-- In a (weakly) locally compact R‚ÇÅ space, each point admits a compact closed neighborhood. -/
theorem exists_mem_nhds_isCompact_isClosed (x : X) : ‚àÉ K ‚àà ùìù x, IsCompact K ‚àß IsClosed K :=
  (isCompact_isClosed_basis_nhds x).ex_mem

-- see Note [lower instance priority]
/-- A weakly locally compact R‚ÇÅ space is locally compact. -/
instance (priority := 80) WeaklyLocallyCompactSpace.locallyCompactSpace : LocallyCompactSpace X :=
  .of_hasBasis isCompact_isClosed_basis_nhds fun _ _ ‚ü®_, h, _‚ü© ‚Ü¶ h
#align locally_compact_of_compact_nhds WeaklyLocallyCompactSpace.locallyCompactSpace

/-- In a weakly locally compact R‚ÇÅ space,
every compact set has an open neighborhood with compact closure. -/
theorem exists_isOpen_superset_and_isCompact_closure {K : Set X} (hK : IsCompact K) :
    ‚àÉ V, IsOpen V ‚àß K ‚äÜ V ‚àß IsCompact (closure V) := by
  rcases exists_compact_superset hK with ‚ü®K', hK', hKK'‚ü©
  exact ‚ü®interior K', isOpen_interior, hKK', hK'.closure_of_subset interior_subset‚ü©
#align exists_open_superset_and_is_compact_closure exists_isOpen_superset_and_isCompact_closure

@[deprecated (since := "2024-01-28")]
alias exists_open_superset_and_isCompact_closure := exists_isOpen_superset_and_isCompact_closure

/-- In a weakly locally compact R‚ÇÅ space,
every point has an open neighborhood with compact closure. -/
theorem exists_isOpen_mem_isCompact_closure (x : X) :
    ‚àÉ U : Set X, IsOpen U ‚àß x ‚àà U ‚àß IsCompact (closure U) := by
  simpa only [singleton_subset_iff]
    using exists_isOpen_superset_and_isCompact_closure isCompact_singleton
#align exists_open_with_compact_closure exists_isOpen_mem_isCompact_closure

@[deprecated (since := "2024-01-28")]
alias exists_open_with_compact_closure := exists_isOpen_mem_isCompact_closure

end R1Space

/-- A T‚ÇÇ space, also known as a Hausdorff space, is one in which for every
  `x ‚â† y` there exists disjoint open sets around `x` and `y`. This is
  the most widely used of the separation axioms. -/
@[mk_iff]
class T2Space (X : Type u) [TopologicalSpace X] : Prop where
  /-- Every two points in a Hausdorff space admit disjoint open neighbourhoods. -/
  t2 : Pairwise fun x y => ‚àÉ u v : Set X, IsOpen u ‚àß IsOpen v ‚àß x ‚àà u ‚àß y ‚àà v ‚àß Disjoint u v
#align t2_space T2Space

/-- Two different points can be separated by open sets. -/
theorem t2_separation [T2Space X] {x y : X} (h : x ‚â† y) :
    ‚àÉ u v : Set X, IsOpen u ‚àß IsOpen v ‚àß x ‚àà u ‚àß y ‚àà v ‚àß Disjoint u v :=
  T2Space.t2 h
#align t2_separation t2_separation

-- todo: use this as a definition?
theorem t2Space_iff_disjoint_nhds : T2Space X ‚Üî Pairwise fun x y : X => Disjoint (ùìù x) (ùìù y) := by
  refine (t2Space_iff X).trans (forall‚ÇÉ_congr fun x y _ => ?_)
  simp only [(nhds_basis_opens x).disjoint_iff (nhds_basis_opens y), exists_prop, ‚Üê exists_and_left,
    and_assoc, and_comm, and_left_comm]
#align t2_space_iff_disjoint_nhds t2Space_iff_disjoint_nhds

@[simp]
theorem disjoint_nhds_nhds [T2Space X] {x y : X} : Disjoint (ùìù x) (ùìù y) ‚Üî x ‚â† y :=
  ‚ü®fun hd he => by simp [he, nhds_neBot.ne] at hd, (t2Space_iff_disjoint_nhds.mp ‚Äπ_‚Ä∫ ¬∑)‚ü©
#align disjoint_nhds_nhds disjoint_nhds_nhds

theorem pairwise_disjoint_nhds [T2Space X] : Pairwise (Disjoint on (ùìù : X ‚Üí Filter X)) := fun _ _ =>
  disjoint_nhds_nhds.2
#align pairwise_disjoint_nhds pairwise_disjoint_nhds

protected theorem Set.pairwiseDisjoint_nhds [T2Space X] (s : Set X) : s.PairwiseDisjoint ùìù :=
  pairwise_disjoint_nhds.set_pairwise s
#align set.pairwise_disjoint_nhds Set.pairwiseDisjoint_nhds

/-- Points of a finite set can be separated by open sets from each other. -/
theorem Set.Finite.t2_separation [T2Space X] {s : Set X} (hs : s.Finite) :
    ‚àÉ U : X ‚Üí Set X, (‚àÄ x, x ‚àà U x ‚àß IsOpen (U x)) ‚àß s.PairwiseDisjoint U :=
  s.pairwiseDisjoint_nhds.exists_mem_filter_basis hs nhds_basis_opens
#align set.finite.t2_separation Set.Finite.t2_separation

-- see Note [lower instance priority]
instance (priority := 100) T2Space.t1Space [T2Space X] : T1Space X :=
  t1Space_iff_disjoint_pure_nhds.mpr fun _ _ hne =>
    (disjoint_nhds_nhds.2 hne).mono_left <| pure_le_nhds _
#align t2_space.t1_space T2Space.t1Space

-- see Note [lower instance priority]
instance (priority := 100) T2Space.r1Space [T2Space X] : R1Space X :=
  ‚ü®fun x y ‚Ü¶ (eq_or_ne x y).imp specializes_of_eq disjoint_nhds_nhds.2‚ü©

theorem SeparationQuotient.t2Space_iff : T2Space (SeparationQuotient X) ‚Üî R1Space X := by
  simp only [t2Space_iff_disjoint_nhds, Pairwise, surjective_mk.forall‚ÇÇ, ne_eq, mk_eq_mk,
    r1Space_iff_inseparable_or_disjoint_nhds, ‚Üê disjoint_comap_iff surjective_mk, comap_mk_nhds_mk,
    ‚Üê or_iff_not_imp_left]

instance SeparationQuotient.t2Space [R1Space X] : T2Space (SeparationQuotient X) :=
  t2Space_iff.2 ‚Äπ_‚Ä∫

instance (priority := 80) [R1Space X] [T0Space X] : T2Space X :=
  t2Space_iff_disjoint_nhds.2 fun _x _y hne ‚Ü¶ disjoint_nhds_nhds_iff_not_inseparable.2 fun hxy ‚Ü¶
    hne hxy.eq

theorem R1Space.t2Space_iff_t0Space [R1Space X] : T2Space X ‚Üî T0Space X := by
  constructor <;> intro <;> infer_instance

/-- A space is T‚ÇÇ iff the neighbourhoods of distinct points generate the bottom filter. -/
theorem t2_iff_nhds : T2Space X ‚Üî ‚àÄ {x y : X}, NeBot (ùìù x ‚äì ùìù y) ‚Üí x = y := by
  simp only [t2Space_iff_disjoint_nhds, disjoint_iff, neBot_iff, Ne, not_imp_comm, Pairwise]
#align t2_iff_nhds t2_iff_nhds

theorem eq_of_nhds_neBot [T2Space X] {x y : X} (h : NeBot (ùìù x ‚äì ùìù y)) : x = y :=
  t2_iff_nhds.mp ‚Äπ_‚Ä∫ h
#align eq_of_nhds_ne_bot eq_of_nhds_neBot

theorem t2Space_iff_nhds :
    T2Space X ‚Üî Pairwise fun x y : X => ‚àÉ U ‚àà ùìù x, ‚àÉ V ‚àà ùìù y, Disjoint U V := by
  simp only [t2Space_iff_disjoint_nhds, Filter.disjoint_iff, Pairwise]
#align t2_space_iff_nhds t2Space_iff_nhds

theorem t2_separation_nhds [T2Space X] {x y : X} (h : x ‚â† y) :
    ‚àÉ u v, u ‚àà ùìù x ‚àß v ‚àà ùìù y ‚àß Disjoint u v :=
  let ‚ü®u, v, open_u, open_v, x_in, y_in, huv‚ü© := t2_separation h
  ‚ü®u, v, open_u.mem_nhds x_in, open_v.mem_nhds y_in, huv‚ü©
#align t2_separation_nhds t2_separation_nhds

theorem t2_separation_compact_nhds [LocallyCompactSpace X] [T2Space X] {x y : X} (h : x ‚â† y) :
    ‚àÉ u v, u ‚àà ùìù x ‚àß v ‚àà ùìù y ‚àß IsCompact u ‚àß IsCompact v ‚àß Disjoint u v := by
  simpa only [exists_prop, ‚Üê exists_and_left, and_comm, and_assoc, and_left_comm] using
    ((compact_basis_nhds x).disjoint_iff (compact_basis_nhds y)).1 (disjoint_nhds_nhds.2 h)
#align t2_separation_compact_nhds t2_separation_compact_nhds

theorem t2_iff_ultrafilter :
    T2Space X ‚Üî ‚àÄ {x y : X} (f : Ultrafilter X), ‚Üëf ‚â§ ùìù x ‚Üí ‚Üëf ‚â§ ùìù y ‚Üí x = y :=
  t2_iff_nhds.trans <| by simp only [‚Üê exists_ultrafilter_iff, and_imp, le_inf_iff, exists_imp]
#align t2_iff_ultrafilter t2_iff_ultrafilter

theorem t2_iff_isClosed_diagonal : T2Space X ‚Üî IsClosed (diagonal X) := by
  simp only [t2Space_iff_disjoint_nhds, ‚Üê isOpen_compl_iff, isOpen_iff_mem_nhds, Prod.forall,
    nhds_prod_eq, compl_diagonal_mem_prod, mem_compl_iff, mem_diagonal_iff, Pairwise]
#align t2_iff_is_closed_diagonal t2_iff_isClosed_diagonal

theorem isClosed_diagonal [T2Space X] : IsClosed (diagonal X) :=
  t2_iff_isClosed_diagonal.mp ‚Äπ_‚Ä∫
#align is_closed_diagonal isClosed_diagonal

-- Porting note: 2 lemmas moved below

theorem tendsto_nhds_unique [T2Space X] {f : Y ‚Üí X} {l : Filter Y} {a b : X} [NeBot l]
    (ha : Tendsto f l (ùìù a)) (hb : Tendsto f l (ùìù b)) : a = b :=
  eq_of_nhds_neBot <| neBot_of_le <| le_inf ha hb
#align tendsto_nhds_unique tendsto_nhds_unique

theorem tendsto_nhds_unique' [T2Space X] {f : Y ‚Üí X} {l : Filter Y} {a b : X} (_ : NeBot l)
    (ha : Tendsto f l (ùìù a)) (hb : Tendsto f l (ùìù b)) : a = b :=
  eq_of_nhds_neBot <| neBot_of_le <| le_inf ha hb
#align tendsto_nhds_unique' tendsto_nhds_unique'

theorem tendsto_nhds_unique_of_eventuallyEq [T2Space X] {f g : Y ‚Üí X} {l : Filter Y} {a b : X}
    [NeBot l] (ha : Tendsto f l (ùìù a)) (hb : Tendsto g l (ùìù b)) (hfg : f =·∂†[l] g) : a = b :=
  tendsto_nhds_unique (ha.congr' hfg) hb
#align tendsto_nhds_unique_of_eventually_eq tendsto_nhds_unique_of_eventuallyEq

theorem tendsto_nhds_unique_of_frequently_eq [T2Space X] {f g : Y ‚Üí X} {l : Filter Y} {a b : X}
    (ha : Tendsto f l (ùìù a)) (hb : Tendsto g l (ùìù b)) (hfg : ‚àÉ·∂† x in l, f x = g x) : a = b :=
  have : ‚àÉ·∂† z : X √ó X in ùìù (a, b), z.1 = z.2 := (ha.prod_mk_nhds hb).frequently hfg
  not_not.1 fun hne => this (isClosed_diagonal.isOpen_compl.mem_nhds hne)
#align tendsto_nhds_unique_of_frequently_eq tendsto_nhds_unique_of_frequently_eq

/-- If `s` and `t` are compact sets in a T‚ÇÇ space, then the set neighborhoods filter of `s ‚à© t`
is the infimum of set neighborhoods filters for `s` and `t`.

For general sets, only the `‚â§` inequality holds, see `nhdsSet_inter_le`. -/
theorem IsCompact.nhdsSet_inter_eq [T2Space X] {s t : Set X} (hs : IsCompact s) (ht : IsCompact t) :
    ùìùÀ¢ (s ‚à© t) = ùìùÀ¢ s ‚äì ùìùÀ¢ t := by
  refine le_antisymm (nhdsSet_inter_le _ _) ?_
  simp_rw [hs.nhdsSet_inf_eq_biSup, ht.inf_nhdsSet_eq_biSup, nhdsSet, sSup_image]
  refine iSup‚ÇÇ_le fun x hxs ‚Ü¶ iSup‚ÇÇ_le fun y hyt ‚Ü¶ ?_
  rcases eq_or_ne x y with (rfl|hne)
  ¬∑ exact le_iSup‚ÇÇ_of_le x ‚ü®hxs, hyt‚ü© (inf_idem _).le
  ¬∑ exact (disjoint_nhds_nhds.mpr hne).eq_bot ‚ñ∏ bot_le

/-- If a function `f` is

- injective on a compact set `s`;
- continuous at every point of this set;
- injective on a neighborhood of each point of this set,

then it is injective on a neighborhood of this set. -/
theorem Set.InjOn.exists_mem_nhdsSet {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    [T2Space Y] {f : X ‚Üí Y} {s : Set X} (inj : InjOn f s) (sc : IsCompact s)
    (fc : ‚àÄ x ‚àà s, ContinuousAt f x) (loc : ‚àÄ x ‚àà s, ‚àÉ u ‚àà ùìù x, InjOn f u) :
    ‚àÉ t ‚àà ùìùÀ¢ s, InjOn f t := by
  have : ‚àÄ x ‚àà s √óÀ¢ s, ‚àÄ·∂† y in ùìù x, f y.1 = f y.2 ‚Üí y.1 = y.2 := fun (x, y) ‚ü®hx, hy‚ü© ‚Ü¶ by
    rcases eq_or_ne x y with rfl | hne
    ¬∑ rcases loc x hx with ‚ü®u, hu, hf‚ü©
      exact Filter.mem_of_superset (prod_mem_nhds hu hu) <| forall_prod_set.2 hf
    ¬∑ suffices ‚àÄ·∂† z in ùìù (x, y), f z.1 ‚â† f z.2 from this.mono fun _ hne h ‚Ü¶ absurd h hne
      refine (fc x hx).prod_map' (fc y hy) <| isClosed_diagonal.isOpen_compl.mem_nhds ?_
      exact inj.ne hx hy hne
  rw [‚Üê eventually_nhdsSet_iff_forall, sc.nhdsSet_prod_eq sc] at this
  exact eventually_prod_self_iff.1 this

/-- If a function `f` is

- injective on a compact set `s`;
- continuous at every point of this set;
- injective on a neighborhood of each point of this set,

then it is injective on an open neighborhood of this set. -/
theorem Set.InjOn.exists_isOpen_superset {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    [T2Space Y] {f : X ‚Üí Y} {s : Set X} (inj : InjOn f s) (sc : IsCompact s)
    (fc : ‚àÄ x ‚àà s, ContinuousAt f x) (loc : ‚àÄ x ‚àà s, ‚àÉ u ‚àà ùìù x, InjOn f u) :
    ‚àÉ t, IsOpen t ‚àß s ‚äÜ t ‚àß InjOn f t :=
  let ‚ü®_t, hst, ht‚ü© := inj.exists_mem_nhdsSet sc fc loc
  let ‚ü®u, huo, hsu, hut‚ü© := mem_nhdsSet_iff_exists.1 hst
  ‚ü®u, huo, hsu, ht.mono hut‚ü©

section limUnder

variable [T2Space X] {f : Filter X}

/-!
### Properties of `lim` and `limUnder`

In this section we use explicit `Nonempty X` instances for `lim` and `limUnder`. This way the lemmas
are useful without a `Nonempty X` instance.
-/


theorem lim_eq {x : X} [NeBot f] (h : f ‚â§ ùìù x) : @lim _ _ ‚ü®x‚ü© f = x :=
  tendsto_nhds_unique (le_nhds_lim ‚ü®x, h‚ü©) h
set_option linter.uppercaseLean3 false in
#align Lim_eq lim_eq

theorem lim_eq_iff [NeBot f] (h : ‚àÉ x : X, f ‚â§ ùìù x) {x} : @lim _ _ ‚ü®x‚ü© f = x ‚Üî f ‚â§ ùìù x :=
  ‚ü®fun c => c ‚ñ∏ le_nhds_lim h, lim_eq‚ü©
set_option linter.uppercaseLean3 false in
#align Lim_eq_iff lim_eq_iff

theorem Ultrafilter.lim_eq_iff_le_nhds [CompactSpace X] {x : X} {F : Ultrafilter X} :
    F.lim = x ‚Üî ‚ÜëF ‚â§ ùìù x :=
  ‚ü®fun h => h ‚ñ∏ F.le_nhds_lim, lim_eq‚ü©
set_option linter.uppercaseLean3 false in
#align ultrafilter.Lim_eq_iff_le_nhds Ultrafilter.lim_eq_iff_le_nhds

theorem isOpen_iff_ultrafilter' [CompactSpace X] (U : Set X) :
    IsOpen U ‚Üî ‚àÄ F : Ultrafilter X, F.lim ‚àà U ‚Üí U ‚àà F.1 := by
  rw [isOpen_iff_ultrafilter]
  refine ‚ü®fun h F hF => h F.lim hF F F.le_nhds_lim, ?_‚ü©
  intro cond x hx f h
  rw [‚Üê Ultrafilter.lim_eq_iff_le_nhds.2 h] at hx
  exact cond _ hx
#align is_open_iff_ultrafilter' isOpen_iff_ultrafilter'

theorem Filter.Tendsto.limUnder_eq {x : X} {f : Filter Y} [NeBot f] {g : Y ‚Üí X}
    (h : Tendsto g f (ùìù x)) : @limUnder _ _ _ ‚ü®x‚ü© f g = x :=
  lim_eq h
#align filter.tendsto.lim_eq Filter.Tendsto.limUnder_eq

theorem Filter.limUnder_eq_iff {f : Filter Y} [NeBot f] {g : Y ‚Üí X} (h : ‚àÉ x, Tendsto g f (ùìù x))
    {x} : @limUnder _ _ _ ‚ü®x‚ü© f g = x ‚Üî Tendsto g f (ùìù x) :=
  ‚ü®fun c => c ‚ñ∏ tendsto_nhds_limUnder h, Filter.Tendsto.limUnder_eq‚ü©
#align filter.lim_eq_iff Filter.limUnder_eq_iff

theorem Continuous.limUnder_eq [TopologicalSpace Y] {f : Y ‚Üí X} (h : Continuous f) (y : Y) :
    @limUnder _ _ _ ‚ü®f y‚ü© (ùìù y) f = f y :=
  (h.tendsto y).limUnder_eq
#align continuous.lim_eq Continuous.limUnder_eq

@[simp]
theorem lim_nhds (x : X) : @lim _ _ ‚ü®x‚ü© (ùìù x) = x :=
  lim_eq le_rfl
set_option linter.uppercaseLean3 false in
#align Lim_nhds lim_nhds

@[simp]
theorem limUnder_nhds_id (x : X) : @limUnder _ _ _ ‚ü®x‚ü© (ùìù x) id = x :=
  lim_nhds x
#align lim_nhds_id limUnder_nhds_id

@[simp]
theorem lim_nhdsWithin {x : X} {s : Set X} (h : x ‚àà closure s) : @lim _ _ ‚ü®x‚ü© (ùìù[s] x) = x :=
  haveI : NeBot (ùìù[s] x) := mem_closure_iff_clusterPt.1 h
  lim_eq inf_le_left
set_option linter.uppercaseLean3 false in
#align Lim_nhds_within lim_nhdsWithin

@[simp]
theorem limUnder_nhdsWithin_id {x : X} {s : Set X} (h : x ‚àà closure s) :
    @limUnder _ _ _ ‚ü®x‚ü© (ùìù[s] x) id = x :=
  lim_nhdsWithin h
#align lim_nhds_within_id limUnder_nhdsWithin_id

end limUnder

/-!
### `T2Space` constructions

We use two lemmas to prove that various standard constructions generate Hausdorff spaces from
Hausdorff spaces:

* `separated_by_continuous` says that two points `x y : X` can be separated by open neighborhoods
  provided that there exists a continuous map `f : X ‚Üí Y` with a Hausdorff codomain such that
  `f x ‚â† f y`. We use this lemma to prove that topological spaces defined using `induced` are
  Hausdorff spaces.

* `separated_by_openEmbedding` says that for an open embedding `f : X ‚Üí Y` of a Hausdorff space
  `X`, the images of two distinct points `x y : X`, `x ‚â† y` can be separated by open neighborhoods.
  We use this lemma to prove that topological spaces defined using `coinduced` are Hausdorff spaces.
-/

-- see Note [lower instance priority]
instance (priority := 100) DiscreteTopology.toT2Space
    [DiscreteTopology X] : T2Space X :=
  ‚ü®fun x y h => ‚ü®{x}, {y}, isOpen_discrete _, isOpen_discrete _, rfl, rfl, disjoint_singleton.2 h‚ü©‚ü©
#align discrete_topology.to_t2_space DiscreteTopology.toT2Space

theorem separated_by_continuous [TopologicalSpace Y] [T2Space Y]
    {f : X ‚Üí Y} (hf : Continuous f) {x y : X} (h : f x ‚â† f y) :
    ‚àÉ u v : Set X, IsOpen u ‚àß IsOpen v ‚àß x ‚àà u ‚àß y ‚àà v ‚àß Disjoint u v :=
  let ‚ü®u, v, uo, vo, xu, yv, uv‚ü© := t2_separation h
  ‚ü®f ‚Åª¬π' u, f ‚Åª¬π' v, uo.preimage hf, vo.preimage hf, xu, yv, uv.preimage _‚ü©
#align separated_by_continuous separated_by_continuous

theorem separated_by_openEmbedding [TopologicalSpace Y] [T2Space X]
    {f : X ‚Üí Y} (hf : OpenEmbedding f) {x y : X} (h : x ‚â† y) :
    ‚àÉ u v : Set Y, IsOpen u ‚àß IsOpen v ‚àß f x ‚àà u ‚àß f y ‚àà v ‚àß Disjoint u v :=
  let ‚ü®u, v, uo, vo, xu, yv, uv‚ü© := t2_separation h
  ‚ü®f '' u, f '' v, hf.isOpenMap _ uo, hf.isOpenMap _ vo, mem_image_of_mem _ xu,
    mem_image_of_mem _ yv, disjoint_image_of_injective hf.inj uv‚ü©
#align separated_by_open_embedding separated_by_openEmbedding

instance {p : X ‚Üí Prop} [T2Space X] : T2Space (Subtype p) := inferInstance

instance Prod.t2Space [T2Space X] [TopologicalSpace Y] [T2Space Y] : T2Space (X √ó Y) :=
  inferInstance

/-- If the codomain of an injective continuous function is a Hausdorff space, then so is its
domain. -/
theorem T2Space.of_injective_continuous [TopologicalSpace Y] [T2Space Y] {f : X ‚Üí Y}
    (hinj : Injective f) (hc : Continuous f) : T2Space X :=
  ‚ü®fun _ _ h => separated_by_continuous hc (hinj.ne h)‚ü©

/-- If the codomain of a topological embedding is a Hausdorff space, then so is its domain.
See also `T2Space.of_continuous_injective`. -/
theorem Embedding.t2Space [TopologicalSpace Y] [T2Space Y] {f : X ‚Üí Y} (hf : Embedding f) :
    T2Space X :=
  .of_injective_continuous hf.inj hf.continuous
#align embedding.t2_space Embedding.t2Space

instance ULift.instT2Space [T2Space X] : T2Space (ULift X) :=
  embedding_uLift_down.t2Space

instance [T2Space X] [TopologicalSpace Y] [T2Space Y] :
    T2Space (X ‚äï Y) := by
  constructor
  rintro (x | x) (y | y) h
  ¬∑ exact separated_by_openEmbedding openEmbedding_inl <| ne_of_apply_ne _ h
  ¬∑ exact separated_by_continuous continuous_isLeft <| by simp
  ¬∑ exact separated_by_continuous continuous_isLeft <| by simp
  ¬∑ exact separated_by_openEmbedding openEmbedding_inr <| ne_of_apply_ne _ h

instance Pi.t2Space {Y : X ‚Üí Type v} [‚àÄ a, TopologicalSpace (Y a)]
    [‚àÄ a, T2Space (Y a)] : T2Space (‚àÄ a, Y a) :=
  inferInstance
#align Pi.t2_space Pi.t2Space

instance Sigma.t2Space {Œπ} {X : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)] [‚àÄ a, T2Space (X a)] :
    T2Space (Œ£i, X i) := by
  constructor
  rintro ‚ü®i, x‚ü© ‚ü®j, y‚ü© neq
  rcases eq_or_ne i j with (rfl | h)
  ¬∑ replace neq : x ‚â† y := ne_of_apply_ne _ neq
    exact separated_by_openEmbedding openEmbedding_sigmaMk neq
  ¬∑ let _ := (‚ä• : TopologicalSpace Œπ); have : DiscreteTopology Œπ := ‚ü®rfl‚ü©
    exact separated_by_continuous (continuous_def.2 fun u _ => isOpen_sigma_fst_preimage u) h
#align sigma.t2_space Sigma.t2Space

section
variable (X)

/-- The smallest equivalence relation on a topological space giving a T2 quotient. -/
def t2Setoid : Setoid X := sInf {s | T2Space (Quotient s)}

/-- The largest T2 quotient of a topological space. This construction is left-adjoint to the
inclusion of T2 spaces into all topological spaces. -/
def t2Quotient := Quotient (t2Setoid X)

namespace t2Quotient
variable {X}

instance : TopologicalSpace (t2Quotient X) :=
  inferInstanceAs <| TopologicalSpace (Quotient _)

/-- The map from a topological space to its largest T2 quotient. -/
def mk : X ‚Üí t2Quotient X := Quotient.mk (t2Setoid X)

lemma mk_eq {x y : X} : mk x = mk y ‚Üî ‚àÄ s : Setoid X, T2Space (Quotient s) ‚Üí s.Rel x y :=
  Setoid.quotient_mk_sInf_eq

variable (X)

lemma surjective_mk : Surjective (mk : X ‚Üí t2Quotient X) := surjective_quotient_mk _

lemma continuous_mk : Continuous (mk : X ‚Üí t2Quotient X) :=
  continuous_quotient_mk'

variable {X}

@[elab_as_elim]
protected lemma inductionOn {motive : t2Quotient X ‚Üí Prop} (q : t2Quotient X)
    (h : ‚àÄ x, motive (t2Quotient.mk x)) : motive q := Quotient.inductionOn q h

@[elab_as_elim]
protected lemma inductionOn‚ÇÇ [TopologicalSpace Y] {motive : t2Quotient X ‚Üí t2Quotient Y ‚Üí Prop}
    (q : t2Quotient X) (q' : t2Quotient Y) (h : ‚àÄ x y, motive (mk x) (mk y)) : motive q q' :=
  Quotient.inductionOn‚ÇÇ q q' h

/-- The largest T2 quotient of a topological space is indeed T2. -/
instance : T2Space (t2Quotient X) := by
  rw [t2Space_iff]
  rintro ‚ü®x‚ü© ‚ü®y‚ü© (h : ¬¨ t2Quotient.mk x = t2Quotient.mk y)
  obtain ‚ü®s, hs, hsxy‚ü© : ‚àÉ s, T2Space (Quotient s) ‚àß Quotient.mk s x ‚â† Quotient.mk s y := by
    simpa [t2Quotient.mk_eq] using h
  exact separated_by_continuous (continuous_map_sInf (by exact hs)) hsxy

lemma compatible {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] [T2Space Y]
    {f : X ‚Üí Y} (hf : Continuous f) : letI _ := t2Setoid X
    ‚àÄ (a b : X), a ‚âà b ‚Üí f a = f b := by
  change t2Setoid X ‚â§ Setoid.ker f
  exact sInf_le <| .of_injective_continuous
    (Setoid.ker_lift_injective _) (hf.quotient_lift fun _ _ ‚Ü¶ id)

/-- The universal property of the largest T2 quotient of a topological space `X`: any continuous
map from `X` to a T2 space `Y` uniquely factors through `t2Quotient X`. This declaration builds the
factored map. Its continuity is `t2Quotient.continuous_lift`, the fact that it indeed factors the
original map is `t2Quotient.lift_mk` and uniquenes is `t2Quotient.unique_lift`. -/
def lift {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] [T2Space Y]
    {f : X ‚Üí Y} (hf : Continuous f) : t2Quotient X ‚Üí Y :=
  Quotient.lift f (t2Quotient.compatible hf)

lemma continuous_lift {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] [T2Space Y]
    {f : X ‚Üí Y} (hf : Continuous f) : Continuous (t2Quotient.lift hf) :=
  continuous_coinduced_dom.mpr hf

@[simp]
lemma lift_mk {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] [T2Space Y]
    {f : X ‚Üí Y} (hf : Continuous f) (x : X) : lift hf (mk x) = f x :=
  Quotient.lift_mk (s := t2Setoid X) f (t2Quotient.compatible hf) x

lemma unique_lift {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] [T2Space Y]
    {f : X ‚Üí Y} (hf : Continuous f) {g : t2Quotient X ‚Üí Y} (hfg : g ‚àò mk = f) :
    g = lift hf := by
  apply surjective_mk X |>.right_cancellable |>.mp <| funext _
  simp [‚Üê hfg]

end t2Quotient
end

variable {Z : Type*} [TopologicalSpace Y] [TopologicalSpace Z]

theorem isClosed_eq [T2Space X] {f g : Y ‚Üí X} (hf : Continuous f) (hg : Continuous g) :
    IsClosed { y : Y | f y = g y } :=
  continuous_iff_isClosed.mp (hf.prod_mk hg) _ isClosed_diagonal
#align is_closed_eq isClosed_eq

theorem isOpen_ne_fun [T2Space X] {f g : Y ‚Üí X} (hf : Continuous f) (hg : Continuous g) :
    IsOpen { y : Y | f y ‚â† g y } :=
  isOpen_compl_iff.mpr <| isClosed_eq hf hg
#align is_open_ne_fun isOpen_ne_fun

/-- If two continuous maps are equal on `s`, then they are equal on the closure of `s`. See also
`Set.EqOn.of_subset_closure` for a more general version. -/
protected theorem Set.EqOn.closure [T2Space X] {s : Set Y} {f g : Y ‚Üí X} (h : EqOn f g s)
    (hf : Continuous f) (hg : Continuous g) : EqOn f g (closure s) :=
  closure_minimal h (isClosed_eq hf hg)
#align set.eq_on.closure Set.EqOn.closure

/-- If two continuous functions are equal on a dense set, then they are equal. -/
theorem Continuous.ext_on [T2Space X] {s : Set Y} (hs : Dense s) {f g : Y ‚Üí X} (hf : Continuous f)
    (hg : Continuous g) (h : EqOn f g s) : f = g :=
  funext fun x => h.closure hf hg (hs x)
#align continuous.ext_on Continuous.ext_on

theorem eqOn_closure‚ÇÇ' [T2Space Z] {s : Set X} {t : Set Y} {f g : X ‚Üí Y ‚Üí Z}
    (h : ‚àÄ x ‚àà s, ‚àÄ y ‚àà t, f x y = g x y) (hf‚ÇÅ : ‚àÄ x, Continuous (f x))
    (hf‚ÇÇ : ‚àÄ y, Continuous fun x => f x y) (hg‚ÇÅ : ‚àÄ x, Continuous (g x))
    (hg‚ÇÇ : ‚àÄ y, Continuous fun x => g x y) : ‚àÄ x ‚àà closure s, ‚àÄ y ‚àà closure t, f x y = g x y :=
  suffices closure s ‚äÜ ‚ãÇ y ‚àà closure t, { x | f x y = g x y } by simpa only [subset_def, mem_iInter]
  (closure_minimal fun x hx => mem_iInter‚ÇÇ.2 <| Set.EqOn.closure (h x hx) (hf‚ÇÅ _) (hg‚ÇÅ _)) <|
    isClosed_biInter fun y _ => isClosed_eq (hf‚ÇÇ _) (hg‚ÇÇ _)
#align eq_on_closure‚ÇÇ' eqOn_closure‚ÇÇ'

theorem eqOn_closure‚ÇÇ [T2Space Z] {s : Set X} {t : Set Y} {f g : X ‚Üí Y ‚Üí Z}
    (h : ‚àÄ x ‚àà s, ‚àÄ y ‚àà t, f x y = g x y) (hf : Continuous (uncurry f))
    (hg : Continuous (uncurry g)) : ‚àÄ x ‚àà closure s, ‚àÄ y ‚àà closure t, f x y = g x y :=
  eqOn_closure‚ÇÇ' h hf.uncurry_left hf.uncurry_right hg.uncurry_left hg.uncurry_right
#align eq_on_closure‚ÇÇ eqOn_closure‚ÇÇ

/-- If `f x = g x` for all `x ‚àà s` and `f`, `g` are continuous on `t`, `s ‚äÜ t ‚äÜ closure s`, then
`f x = g x` for all `x ‚àà t`. See also `Set.EqOn.closure`. -/
theorem Set.EqOn.of_subset_closure [T2Space Y] {s t : Set X} {f g : X ‚Üí Y} (h : EqOn f g s)
    (hf : ContinuousOn f t) (hg : ContinuousOn g t) (hst : s ‚äÜ t) (hts : t ‚äÜ closure s) :
    EqOn f g t := by
  intro x hx
  have : (ùìù[s] x).NeBot := mem_closure_iff_clusterPt.mp (hts hx)
  exact
    tendsto_nhds_unique_of_eventuallyEq ((hf x hx).mono_left <| nhdsWithin_mono _ hst)
      ((hg x hx).mono_left <| nhdsWithin_mono _ hst) (h.eventuallyEq_of_mem self_mem_nhdsWithin)
#align set.eq_on.of_subset_closure Set.EqOn.of_subset_closure

theorem Function.LeftInverse.isClosed_range [T2Space X] {f : X ‚Üí Y} {g : Y ‚Üí X}
    (h : Function.LeftInverse f g) (hf : Continuous f) (hg : Continuous g) : IsClosed (range g) :=
  have : EqOn (g ‚àò f) id (closure <| range g) :=
    h.rightInvOn_range.eqOn.closure (hg.comp hf) continuous_id
  isClosed_of_closure_subset fun x hx => ‚ü®f x, this hx‚ü©
#align function.left_inverse.closed_range Function.LeftInverse.isClosed_range

@[deprecated (since := "2024-03-17")]
alias Function.LeftInverse.closed_range := Function.LeftInverse.isClosed_range

theorem Function.LeftInverse.closedEmbedding [T2Space X] {f : X ‚Üí Y} {g : Y ‚Üí X}
    (h : Function.LeftInverse f g) (hf : Continuous f) (hg : Continuous g) : ClosedEmbedding g :=
  ‚ü®h.embedding hf hg, h.isClosed_range hf hg‚ü©
#align function.left_inverse.closed_embedding Function.LeftInverse.closedEmbedding

theorem SeparatedNhds.of_isCompact_isCompact [T2Space X] {s t : Set X} (hs : IsCompact s)
    (ht : IsCompact t) (hst : Disjoint s t) : SeparatedNhds s t := by
  simp only [SeparatedNhds, prod_subset_compl_diagonal_iff_disjoint.symm] at hst ‚ä¢
  exact generalized_tube_lemma hs ht isClosed_diagonal.isOpen_compl hst
#align is_compact_is_compact_separated SeparatedNhds.of_isCompact_isCompact

@[deprecated (since := "2024-01-28")]
alias separatedNhds_of_isCompact_isCompact := SeparatedNhds.of_isCompact_isCompact

section SeparatedFinset

theorem SeparatedNhds.of_finset_finset [T2Space X] (s t : Finset X) (h : Disjoint s t) :
    SeparatedNhds (s : Set X) t :=
  .of_isCompact_isCompact s.finite_toSet.isCompact t.finite_toSet.isCompact <| mod_cast h
#align finset_disjoint_finset_opens_of_t2 SeparatedNhds.of_finset_finset

@[deprecated (since := "2024-01-28")]
alias separatedNhds_of_finset_finset := SeparatedNhds.of_finset_finset

theorem SeparatedNhds.of_singleton_finset [T2Space X] {x : X} {s : Finset X} (h : x ‚àâ s) :
    SeparatedNhds ({x} : Set X) s :=
  mod_cast .of_finset_finset {x} s (Finset.disjoint_singleton_left.mpr h)
#align point_disjoint_finset_opens_of_t2 SeparatedNhds.of_singleton_finset

@[deprecated (since := "2024-01-28")]
alias point_disjoint_finset_opens_of_t2 := SeparatedNhds.of_singleton_finset

end SeparatedFinset

/-- In a `T2Space`, every compact set is closed. -/
theorem IsCompact.isClosed [T2Space X] {s : Set X} (hs : IsCompact s) : IsClosed s :=
  isOpen_compl_iff.1 <| isOpen_iff_forall_mem_open.mpr fun x hx =>
    let ‚ü®u, v, _, vo, su, xv, uv‚ü© :=
      SeparatedNhds.of_isCompact_isCompact hs isCompact_singleton (disjoint_singleton_right.2 hx)
    ‚ü®v, (uv.mono_left <| show s ‚â§ u from su).subset_compl_left, vo, by simpa using xv‚ü©
#align is_compact.is_closed IsCompact.isClosed

theorem IsCompact.preimage_continuous [CompactSpace X] [T2Space Y] {f : X ‚Üí Y} {s : Set Y}
    (hs : IsCompact s) (hf : Continuous f) : IsCompact (f ‚Åª¬π' s) :=
  (hs.isClosed.preimage hf).isCompact

lemma Pi.isCompact_iff {Œπ : Type*} {œÄ : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (œÄ i)]
    [‚àÄ i, T2Space (œÄ i)] {s : Set (Œ† i, œÄ i)} :
    IsCompact s ‚Üî IsClosed s ‚àß ‚àÄ i, IsCompact (eval i '' s):= by
  constructor <;> intro H
  ¬∑ exact ‚ü®H.isClosed, fun i ‚Ü¶ H.image <| continuous_apply i‚ü©
  ¬∑ exact IsCompact.of_isClosed_subset (isCompact_univ_pi H.2) H.1 (subset_pi_eval_image univ s)

lemma Pi.isCompact_closure_iff {Œπ : Type*} {œÄ : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (œÄ i)]
    [‚àÄ i, T2Space (œÄ i)] {s : Set (Œ† i, œÄ i)} :
    IsCompact (closure s) ‚Üî ‚àÄ i, IsCompact (closure <| eval i '' s) := by
  simp_rw [‚Üê exists_isCompact_superset_iff, Pi.exists_compact_superset_iff, image_subset_iff]

/-- If `V : Œπ ‚Üí Set X` is a decreasing family of compact sets then any neighborhood of
`‚ãÇ i, V i` contains some `V i`. This is a version of `exists_subset_nhds_of_isCompact'` where we
don't need to assume each `V i` closed because it follows from compactness since `X` is
assumed to be Hausdorff. -/
theorem exists_subset_nhds_of_isCompact [T2Space X] {Œπ : Type*} [Nonempty Œπ] {V : Œπ ‚Üí Set X}
    (hV : Directed (¬∑ ‚äá ¬∑) V) (hV_cpct : ‚àÄ i, IsCompact (V i)) {U : Set X}
    (hU : ‚àÄ x ‚àà ‚ãÇ i, V i, U ‚àà ùìù x) : ‚àÉ i, V i ‚äÜ U :=
  exists_subset_nhds_of_isCompact' hV hV_cpct (fun i => (hV_cpct i).isClosed) hU
#align exists_subset_nhds_of_is_compact exists_subset_nhds_of_isCompact

theorem CompactExhaustion.isClosed [T2Space X] (K : CompactExhaustion X) (n : ‚Ñï) : IsClosed (K n) :=
  (K.isCompact n).isClosed
#align compact_exhaustion.is_closed CompactExhaustion.isClosed

theorem IsCompact.inter [T2Space X] {s t : Set X} (hs : IsCompact s) (ht : IsCompact t) :
    IsCompact (s ‚à© t) :=
  hs.inter_right <| ht.isClosed
#align is_compact.inter IsCompact.inter

theorem image_closure_of_isCompact [T2Space Y] {s : Set X} (hs : IsCompact (closure s)) {f : X ‚Üí Y}
    (hf : ContinuousOn f (closure s)) : f '' closure s = closure (f '' s) :=
  Subset.antisymm hf.image_closure <|
    closure_minimal (image_subset f subset_closure) (hs.image_of_continuousOn hf).isClosed
#align image_closure_of_is_compact image_closure_of_isCompact

/-- A continuous map from a compact space to a Hausdorff space is a closed map. -/
protected theorem Continuous.isClosedMap [CompactSpace X] [T2Space Y] {f : X ‚Üí Y}
    (h : Continuous f) : IsClosedMap f := fun _s hs => (hs.isCompact.image h).isClosed
#align continuous.is_closed_map Continuous.isClosedMap

/-- A continuous injective map from a compact space to a Hausdorff space is a closed embedding. -/
theorem Continuous.closedEmbedding [CompactSpace X] [T2Space Y] {f : X ‚Üí Y} (h : Continuous f)
    (hf : Function.Injective f) : ClosedEmbedding f :=
  closedEmbedding_of_continuous_injective_closed h hf h.isClosedMap
#align continuous.closed_embedding Continuous.closedEmbedding

/-- A continuous surjective map from a compact space to a Hausdorff space is a quotient map. -/
theorem QuotientMap.of_surjective_continuous [CompactSpace X] [T2Space Y] {f : X ‚Üí Y}
    (hsurj : Surjective f) (hcont : Continuous f) : QuotientMap f :=
  hcont.isClosedMap.to_quotientMap hcont hsurj
#align quotient_map.of_surjective_continuous QuotientMap.of_surjective_continuous

theorem isPreirreducible_iff_subsingleton [T2Space X] {S : Set X} :
    IsPreirreducible S ‚Üî S.Subsingleton := by
  refine ‚ü®fun h x hx y hy => ?_, Set.Subsingleton.isPreirreducible‚ü©
  by_contra e
  obtain ‚ü®U, V, hU, hV, hxU, hyV, h'‚ü© := t2_separation e
  exact ((h U V hU hV ‚ü®x, hx, hxU‚ü© ‚ü®y, hy, hyV‚ü©).mono inter_subset_right).not_disjoint h'
#align is_preirreducible_iff_subsingleton isPreirreducible_iff_subsingleton

-- todo: use `alias` + `attribute [protected]` once we get `attribute [protected]`
protected lemma IsPreirreducible.subsingleton [T2Space X] {S : Set X} (h : IsPreirreducible S) :
    S.Subsingleton :=
  isPreirreducible_iff_subsingleton.1 h
#align is_preirreducible.subsingleton IsPreirreducible.subsingleton

theorem isIrreducible_iff_singleton [T2Space X] {S : Set X} : IsIrreducible S ‚Üî ‚àÉ x, S = {x} := by
  rw [IsIrreducible, isPreirreducible_iff_subsingleton,
    exists_eq_singleton_iff_nonempty_subsingleton]
#align is_irreducible_iff_singleton isIrreducible_iff_singleton

/-- There does not exist a nontrivial preirreducible T‚ÇÇ space. -/
theorem not_preirreducible_nontrivial_t2 (X) [TopologicalSpace X] [PreirreducibleSpace X]
    [Nontrivial X] [T2Space X] : False :=
  (PreirreducibleSpace.isPreirreducible_univ (X := X)).subsingleton.not_nontrivial nontrivial_univ
#align not_preirreducible_nontrivial_t2 not_preirreducible_nontrivial_t2

end Separation

section RegularSpace

/-- A topological space is called a *regular space* if for any closed set `s` and `a ‚àâ s`, there
exist disjoint open sets `U ‚äá s` and `V ‚àã a`. We formulate this condition in terms of `Disjoint`ness
of filters `ùìùÀ¢ s` and `ùìù a`. -/
@[mk_iff]
class RegularSpace (X : Type u) [TopologicalSpace X] : Prop where
  /-- If `a` is a point that does not belong to a closed set `s`, then `a` and `s` admit disjoint
  neighborhoods.  -/
  regular : ‚àÄ {s : Set X} {a}, IsClosed s ‚Üí a ‚àâ s ‚Üí Disjoint (ùìùÀ¢ s) (ùìù a)
#align regular_space RegularSpace

theorem regularSpace_TFAE (X : Type u) [TopologicalSpace X] :
    List.TFAE [RegularSpace X,
      ‚àÄ (s : Set X) x, x ‚àâ closure s ‚Üí Disjoint (ùìùÀ¢ s) (ùìù x),
      ‚àÄ (x : X) (s : Set X), Disjoint (ùìùÀ¢ s) (ùìù x) ‚Üî x ‚àâ closure s,
      ‚àÄ (x : X) (s : Set X), s ‚àà ùìù x ‚Üí ‚àÉ t ‚àà ùìù x, IsClosed t ‚àß t ‚äÜ s,
      ‚àÄ x : X, (ùìù x).lift' closure ‚â§ ùìù x,
      ‚àÄ x : X , (ùìù x).lift' closure = ùìù x] := by
  tfae_have 1 ‚Üî 5
  ¬∑ rw [regularSpace_iff, (@compl_surjective (Set X) _).forall, forall_swap]
    simp only [isClosed_compl_iff, mem_compl_iff, Classical.not_not, @and_comm (_ ‚àà _),
      (nhds_basis_opens _).lift'_closure.le_basis_iff (nhds_basis_opens _), and_imp,
      (nhds_basis_opens _).disjoint_iff_right, exists_prop, ‚Üê subset_interior_iff_mem_nhdsSet,
      interior_compl, compl_subset_compl]
  tfae_have 5 ‚Üí 6
  ¬∑ exact fun h a => (h a).antisymm (ùìù _).le_lift'_closure
  tfae_have 6 ‚Üí 4
  ¬∑ intro H a s hs
    rw [‚Üê H] at hs
    rcases (ùìù a).basis_sets.lift'_closure.mem_iff.mp hs with ‚ü®U, hU, hUs‚ü©
    exact ‚ü®closure U, mem_of_superset hU subset_closure, isClosed_closure, hUs‚ü©
  tfae_have 4 ‚Üí 2
  ¬∑ intro H s a ha
    have ha' : s·∂ú ‚àà ùìù a := by rwa [‚Üê mem_interior_iff_mem_nhds, interior_compl]
    rcases H _ _ ha' with ‚ü®U, hU, hUc, hUs‚ü©
    refine disjoint_of_disjoint_of_mem disjoint_compl_left ?_ hU
    rwa [‚Üê subset_interior_iff_mem_nhdsSet, hUc.isOpen_compl.interior_eq, subset_compl_comm]
  tfae_have 2 ‚Üí 3
  ¬∑ refine fun H a s => ‚ü®fun hd has => mem_closure_iff_nhds_ne_bot.mp has ?_, H s a‚ü©
    exact (hd.symm.mono_right <| @principal_le_nhdsSet _ _ s).eq_bot
  tfae_have 3 ‚Üí 1
  ¬∑ exact fun H => ‚ü®fun hs ha => (H _ _).mpr <| hs.closure_eq.symm ‚ñ∏ ha‚ü©
  tfae_finish
#align regular_space_tfae regularSpace_TFAE

theorem RegularSpace.of_lift'_closure_le (h : ‚àÄ x : X, (ùìù x).lift' closure ‚â§ ùìù x) :
    RegularSpace X :=
  Iff.mpr ((regularSpace_TFAE X).out 0 4) h

theorem RegularSpace.of_lift'_closure (h : ‚àÄ x : X, (ùìù x).lift' closure = ùìù x) : RegularSpace X :=
  Iff.mpr ((regularSpace_TFAE X).out 0 5) h
#align regular_space.of_lift'_closure RegularSpace.of_lift'_closure

@[deprecated (since := "2024-02-28")]
alias RegularSpace.ofLift'_closure := RegularSpace.of_lift'_closure

theorem RegularSpace.of_hasBasis {Œπ : X ‚Üí Sort*} {p : ‚àÄ a, Œπ a ‚Üí Prop} {s : ‚àÄ a, Œπ a ‚Üí Set X}
    (h‚ÇÅ : ‚àÄ a, (ùìù a).HasBasis (p a) (s a)) (h‚ÇÇ : ‚àÄ a i, p a i ‚Üí IsClosed (s a i)) :
    RegularSpace X :=
  .of_lift'_closure fun a => (h‚ÇÅ a).lift'_closure_eq_self (h‚ÇÇ a)
#align regular_space.of_basis RegularSpace.of_hasBasis

@[deprecated (since := "2024-02-28")]
alias RegularSpace.ofBasis := RegularSpace.of_hasBasis

theorem RegularSpace.of_exists_mem_nhds_isClosed_subset
    (h : ‚àÄ (x : X), ‚àÄ s ‚àà ùìù x, ‚àÉ t ‚àà ùìù x, IsClosed t ‚àß t ‚äÜ s) : RegularSpace X :=
  Iff.mpr ((regularSpace_TFAE X).out 0 3) h
#align regular_space.of_exists_mem_nhds_is_closed_subset RegularSpace.of_exists_mem_nhds_isClosed_subset

@[deprecated (since := "2024-02-28")]
alias RegularSpace.ofExistsMemNhdsIsClosedSubset := RegularSpace.of_exists_mem_nhds_isClosed_subset

/-- A weakly locally compact R‚ÇÅ space is regular. -/
instance (priority := 100) [WeaklyLocallyCompactSpace X] [R1Space X] : RegularSpace X :=
  .of_hasBasis isCompact_isClosed_basis_nhds fun _ _ ‚ü®_, _, h‚ü© ‚Ü¶ h

variable [RegularSpace X] {x : X} {s : Set X}

theorem disjoint_nhdsSet_nhds : Disjoint (ùìùÀ¢ s) (ùìù x) ‚Üî x ‚àâ closure s := by
  have h := (regularSpace_TFAE X).out 0 2
  exact h.mp ‚Äπ_‚Ä∫ _ _
#align disjoint_nhds_set_nhds disjoint_nhdsSet_nhds

theorem disjoint_nhds_nhdsSet : Disjoint (ùìù x) (ùìùÀ¢ s) ‚Üî x ‚àâ closure s :=
  disjoint_comm.trans disjoint_nhdsSet_nhds
#align disjoint_nhds_nhds_set disjoint_nhds_nhdsSet

/-- A regular space is R‚ÇÅ. -/
instance (priority := 100) : R1Space X where
  specializes_or_disjoint_nhds _ _ := or_iff_not_imp_left.2 fun h ‚Ü¶ by
    rwa [‚Üê nhdsSet_singleton, disjoint_nhdsSet_nhds, ‚Üê specializes_iff_mem_closure]

theorem exists_mem_nhds_isClosed_subset {x : X} {s : Set X} (h : s ‚àà ùìù x) :
    ‚àÉ t ‚àà ùìù x, IsClosed t ‚àß t ‚äÜ s := by
  have h' := (regularSpace_TFAE X).out 0 3
  exact h'.mp ‚Äπ_‚Ä∫ _ _ h
#align exists_mem_nhds_is_closed_subset exists_mem_nhds_isClosed_subset

theorem closed_nhds_basis (x : X) : (ùìù x).HasBasis (fun s : Set X => s ‚àà ùìù x ‚àß IsClosed s) id :=
  hasBasis_self.2 fun _ => exists_mem_nhds_isClosed_subset
#align closed_nhds_basis closed_nhds_basis

theorem lift'_nhds_closure (x : X) : (ùìù x).lift' closure = ùìù x :=
  (closed_nhds_basis x).lift'_closure_eq_self fun _ => And.right
#align lift'_nhds_closure lift'_nhds_closure

theorem Filter.HasBasis.nhds_closure {Œπ : Sort*} {x : X} {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí Set X}
    (h : (ùìù x).HasBasis p s) : (ùìù x).HasBasis p fun i => closure (s i) :=
  lift'_nhds_closure x ‚ñ∏ h.lift'_closure
#align filter.has_basis.nhds_closure Filter.HasBasis.nhds_closure

theorem hasBasis_nhds_closure (x : X) : (ùìù x).HasBasis (fun s => s ‚àà ùìù x) closure :=
  (ùìù x).basis_sets.nhds_closure
#align has_basis_nhds_closure hasBasis_nhds_closure

theorem hasBasis_opens_closure (x : X) : (ùìù x).HasBasis (fun s => x ‚àà s ‚àß IsOpen s) closure :=
  (nhds_basis_opens x).nhds_closure
#align has_basis_opens_closure hasBasis_opens_closure

theorem IsCompact.exists_isOpen_closure_subset {K U : Set X} (hK : IsCompact K) (hU : U ‚àà ùìùÀ¢ K) :
    ‚àÉ V, IsOpen V ‚àß K ‚äÜ V ‚àß closure V ‚äÜ U := by
  have hd : Disjoint (ùìùÀ¢ K) (ùìùÀ¢ U·∂ú) := by
    simpa [hK.disjoint_nhdsSet_left, disjoint_nhds_nhdsSet,
      ‚Üê subset_interior_iff_mem_nhdsSet] using hU
  rcases ((hasBasis_nhdsSet _).disjoint_iff (hasBasis_nhdsSet _)).1 hd
    with ‚ü®V, ‚ü®hVo, hKV‚ü©, W, ‚ü®hW, hUW‚ü©, hVW‚ü©
  refine ‚ü®V, hVo, hKV, Subset.trans ?_ (compl_subset_comm.1 hUW)‚ü©
  exact closure_minimal hVW.subset_compl_right hW.isClosed_compl

theorem IsCompact.lift'_closure_nhdsSet {K : Set X} (hK : IsCompact K) :
    (ùìùÀ¢ K).lift' closure = ùìùÀ¢ K := by
  refine le_antisymm (fun U hU ‚Ü¶ ?_) (le_lift'_closure _)
  rcases hK.exists_isOpen_closure_subset hU with ‚ü®V, hVo, hKV, hVU‚ü©
  exact mem_of_superset (mem_lift' <| hVo.mem_nhdsSet.2 hKV) hVU

theorem TopologicalSpace.IsTopologicalBasis.nhds_basis_closure {B : Set (Set X)}
    (hB : IsTopologicalBasis B) (x : X) :
    (ùìù x).HasBasis (fun s : Set X => x ‚àà s ‚àß s ‚àà B) closure := by
  simpa only [and_comm] using hB.nhds_hasBasis.nhds_closure
#align topological_space.is_topological_basis.nhds_basis_closure TopologicalSpace.IsTopologicalBasis.nhds_basis_closure

theorem TopologicalSpace.IsTopologicalBasis.exists_closure_subset {B : Set (Set X)}
    (hB : IsTopologicalBasis B) {x : X} {s : Set X} (h : s ‚àà ùìù x) :
    ‚àÉ t ‚àà B, x ‚àà t ‚àß closure t ‚äÜ s := by
  simpa only [exists_prop, and_assoc] using hB.nhds_hasBasis.nhds_closure.mem_iff.mp h
#align topological_space.is_topological_basis.exists_closure_subset TopologicalSpace.IsTopologicalBasis.exists_closure_subset

protected theorem Inducing.regularSpace [TopologicalSpace Y] {f : Y ‚Üí X} (hf : Inducing f) :
    RegularSpace Y :=
  .of_hasBasis
    (fun b => by rw [hf.nhds_eq_comap b]; exact (closed_nhds_basis _).comap _)
    fun b s hs => by exact hs.2.preimage hf.continuous
#align inducing.regular_space Inducing.regularSpace

theorem regularSpace_induced (f : Y ‚Üí X) : @RegularSpace Y (induced f ‚Äπ_‚Ä∫) :=
  letI := induced f ‚Äπ_‚Ä∫
  (inducing_induced f).regularSpace
#align regular_space_induced regularSpace_induced

theorem regularSpace_sInf {X} {T : Set (TopologicalSpace X)} (h : ‚àÄ t ‚àà T, @RegularSpace X t) :
    @RegularSpace X (sInf T) := by
  let _ := sInf T
  have : ‚àÄ a, (ùìù a).HasBasis
      (fun If : Œ£ I : Set T, I ‚Üí Set X =>
        If.1.Finite ‚àß ‚àÄ i : If.1, If.2 i ‚àà @nhds X i a ‚àß @IsClosed X i (If.2 i))
      fun If => ‚ãÇ i : If.1, If.snd i := fun a ‚Ü¶ by
    rw [nhds_sInf, ‚Üê iInf_subtype'']
    exact hasBasis_iInf fun t : T => @closed_nhds_basis X t (h t t.2) a
  refine .of_hasBasis this fun a If hIf => isClosed_iInter fun i => ?_
  exact (hIf.2 i).2.mono (sInf_le (i : T).2)
#align regular_space_Inf regularSpace_sInf

theorem regularSpace_iInf {Œπ X} {t : Œπ ‚Üí TopologicalSpace X} (h : ‚àÄ i, @RegularSpace X (t i)) :
    @RegularSpace X (iInf t) :=
  regularSpace_sInf <| forall_mem_range.mpr h
#align regular_space_infi regularSpace_iInf

theorem RegularSpace.inf {X} {t‚ÇÅ t‚ÇÇ : TopologicalSpace X} (h‚ÇÅ : @RegularSpace X t‚ÇÅ)
    (h‚ÇÇ : @RegularSpace X t‚ÇÇ) : @RegularSpace X (t‚ÇÅ ‚äì t‚ÇÇ) := by
  rw [inf_eq_iInf]
  exact regularSpace_iInf (Bool.forall_bool.2 ‚ü®h‚ÇÇ, h‚ÇÅ‚ü©)
#align regular_space.inf RegularSpace.inf

instance {p : X ‚Üí Prop} : RegularSpace (Subtype p) :=
  embedding_subtype_val.toInducing.regularSpace

instance [TopologicalSpace Y] [RegularSpace Y] : RegularSpace (X √ó Y) :=
  (regularSpace_induced (@Prod.fst X Y)).inf (regularSpace_induced (@Prod.snd X Y))

instance {Œπ : Type*} {X : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)] [‚àÄ i, RegularSpace (X i)] :
    RegularSpace (‚àÄ i, X i) :=
  regularSpace_iInf fun _ => regularSpace_induced _

/-- In a regular space, if a compact set and a closed set are disjoint, then they have disjoint
neighborhoods. -/
lemma SeparatedNhds.of_isCompact_isClosed {s t : Set X}
    (hs : IsCompact s) (ht : IsClosed t) (hst : Disjoint s t) : SeparatedNhds s t := by
  simpa only [separatedNhds_iff_disjoint, hs.disjoint_nhdsSet_left, disjoint_nhds_nhdsSet,
    ht.closure_eq, disjoint_left] using hst

@[deprecated (since := "2024-01-28")]
alias separatedNhds_of_isCompact_isClosed := SeparatedNhds.of_isCompact_isClosed

/-- This technique to witness `HasSeparatingCover` in regular Lindel√∂f topological spaces
will be used to prove regular Lindel√∂f spaces are normal. -/
lemma IsClosed.HasSeparatingCover {s t : Set X} [r: RegularSpace X] [LindelofSpace X]
    (s_cl : IsClosed s) (t_cl : IsClosed t) (st_dis : Disjoint s t) : HasSeparatingCover s t := by
  -- `IsLindelof.indexed_countable_subcover` requires the space be Nonempty
  rcases isEmpty_or_nonempty X with empty_X | nonempty_X
  ¬∑ rw [subset_eq_empty (t := s) (fun ‚¶É_‚¶Ñ _ ‚Ü¶ trivial) (univ_eq_empty_iff.mpr empty_X)]
    exact hasSeparatingCovers_iff_separatedNhds.mpr (SeparatedNhds.empty_left t) |>.1
  -- This is almost `HasSeparatingCover`, but is not countable. We define for all `a : X` for use
  -- with `IsLindelof.indexed_countable_subcover` momentarily.
  have (a : X) : ‚àÉ n : Set X, IsOpen n ‚àß Disjoint (closure n) t ‚àß (a ‚àà s ‚Üí a ‚àà n) := by
    wlog ains : a ‚àà s
    ¬∑ exact ‚ü®‚àÖ, isOpen_empty, SeparatedNhds.empty_left t |>.disjoint_closure_left, fun a ‚Ü¶ ains a‚ü©
    obtain ‚ü®n, nna, ncl, nsubkc‚ü© := ((regularSpace_TFAE X).out 0 3 :).mp r a t·∂ú <|
      t_cl.compl_mem_nhds (disjoint_left.mp st_dis ains)
    exact
      ‚ü®interior n,
        isOpen_interior,
        disjoint_left.mpr fun ‚¶É_‚¶Ñ ain ‚Ü¶
          nsubkc <| (IsClosed.closure_subset_iff ncl).mpr interior_subset ain,
        fun _ ‚Ü¶ mem_interior_iff_mem_nhds.mpr nna‚ü©
  -- By Lindel√∂f, we may obtain a countable subcover witnessing `HasSeparatingCover`
  choose u u_open u_dis u_nhd using this
  obtain ‚ü®f, f_cov‚ü© := s_cl.isLindelof.indexed_countable_subcover
    u u_open (fun a ainh ‚Ü¶ mem_iUnion.mpr ‚ü®a, u_nhd a ainh‚ü©)
  exact ‚ü®u ‚àò f, f_cov, fun n ‚Ü¶ ‚ü®u_open (f n), u_dis (f n)‚ü©‚ü©


end RegularSpace

section LocallyCompactRegularSpace

/-- In a (possibly non-Hausdorff) locally compact regular space, for every containment `K ‚äÜ U` of
  a compact set `K` in an open set `U`, there is a compact closed neighborhood `L`
  such that `K ‚äÜ L ‚äÜ U`: equivalently, there is a compact closed set `L` such
  that `K ‚äÜ interior L` and `L ‚äÜ U`. -/
theorem exists_compact_closed_between [LocallyCompactSpace X] [RegularSpace X]
    {K U : Set X} (hK : IsCompact K) (hU : IsOpen U) (h_KU : K ‚äÜ U) :
    ‚àÉ L, IsCompact L ‚àß IsClosed L ‚àß K ‚äÜ interior L ‚àß L ‚äÜ U :=
  let ‚ü®L, L_comp, KL, LU‚ü© := exists_compact_between hK hU h_KU
  ‚ü®closure L, L_comp.closure, isClosed_closure, KL.trans <| interior_mono subset_closure,
    L_comp.closure_subset_of_isOpen hU LU‚ü©

/-- In a locally compact regular space, given a compact set `K` inside an open set `U`, we can find
an open set `V` between these sets with compact closure: `K ‚äÜ V` and the closure of `V` is
inside `U`. -/
theorem exists_open_between_and_isCompact_closure [LocallyCompactSpace X] [RegularSpace X]
    {K U : Set X} (hK : IsCompact K) (hU : IsOpen U) (hKU : K ‚äÜ U) :
    ‚àÉ V, IsOpen V ‚àß K ‚äÜ V ‚àß closure V ‚äÜ U ‚àß IsCompact (closure V) := by
  rcases exists_compact_closed_between hK hU hKU with ‚ü®L, L_compact, L_closed, KL, LU‚ü©
  have A : closure (interior L) ‚äÜ L := by
    apply (closure_mono interior_subset).trans (le_of_eq L_closed.closure_eq)
  refine ‚ü®interior L, isOpen_interior, KL, A.trans LU, ?_‚ü©
  exact L_compact.closure_of_subset interior_subset
#align exists_open_between_and_is_compact_closure exists_open_between_and_isCompact_closure

@[deprecated WeaklyLocallyCompactSpace.locallyCompactSpace (since := "2023-09-03")]
theorem locally_compact_of_compact [T2Space X] [CompactSpace X] :
    LocallyCompactSpace X :=
  inferInstance
#align locally_compact_of_compact locally_compact_of_compact

end LocallyCompactRegularSpace

section T25

/-- A T‚ÇÇ.‚ÇÖ space, also known as a Urysohn space, is a topological space
  where for every pair `x ‚â† y`, there are two open sets, with the intersection of closures
  empty, one containing `x` and the other `y` . -/
class T25Space (X : Type u) [TopologicalSpace X] : Prop where
  /-- Given two distinct points in a T‚ÇÇ.‚ÇÖ space, their filters of closed neighborhoods are
  disjoint. -/
  t2_5 : ‚àÄ ‚¶Éx y : X‚¶Ñ, x ‚â† y ‚Üí Disjoint ((ùìù x).lift' closure) ((ùìù y).lift' closure)
#align t2_5_space T25Space

@[simp]
theorem disjoint_lift'_closure_nhds [T25Space X] {x y : X} :
    Disjoint ((ùìù x).lift' closure) ((ùìù y).lift' closure) ‚Üî x ‚â† y :=
  ‚ü®fun h hxy => by simp [hxy, nhds_neBot.ne] at h, fun h => T25Space.t2_5 h‚ü©
#align disjoint_lift'_closure_nhds disjoint_lift'_closure_nhds

-- see Note [lower instance priority]
instance (priority := 100) T25Space.t2Space [T25Space X] : T2Space X :=
  t2Space_iff_disjoint_nhds.2 fun _ _ hne =>
    (disjoint_lift'_closure_nhds.2 hne).mono (le_lift'_closure _) (le_lift'_closure _)
#align t2_5_space.t2_space T25Space.t2Space

theorem exists_nhds_disjoint_closure [T25Space X] {x y : X} (h : x ‚â† y) :
    ‚àÉ s ‚àà ùìù x, ‚àÉ t ‚àà ùìù y, Disjoint (closure s) (closure t) :=
  ((ùìù x).basis_sets.lift'_closure.disjoint_iff (ùìù y).basis_sets.lift'_closure).1 <|
    disjoint_lift'_closure_nhds.2 h
#align exists_nhds_disjoint_closure exists_nhds_disjoint_closure

theorem exists_open_nhds_disjoint_closure [T25Space X] {x y : X} (h : x ‚â† y) :
    ‚àÉ u : Set X,
      x ‚àà u ‚àß IsOpen u ‚àß ‚àÉ v : Set X, y ‚àà v ‚àß IsOpen v ‚àß Disjoint (closure u) (closure v) := by
  simpa only [exists_prop, and_assoc] using
    ((nhds_basis_opens x).lift'_closure.disjoint_iff (nhds_basis_opens y).lift'_closure).1
      (disjoint_lift'_closure_nhds.2 h)
#align exists_open_nhds_disjoint_closure exists_open_nhds_disjoint_closure

theorem T25Space.of_injective_continuous [TopologicalSpace Y] [T25Space Y] {f : X ‚Üí Y}
    (hinj : Injective f) (hcont : Continuous f) : T25Space X where
  t2_5 x y hne := (tendsto_lift'_closure_nhds hcont x).disjoint (t2_5 <| hinj.ne hne)
    (tendsto_lift'_closure_nhds hcont y)

instance [T25Space X] {p : X ‚Üí Prop} : T25Space {x // p x} :=
  .of_injective_continuous Subtype.val_injective continuous_subtype_val

section T25

section T3

/-- A T‚ÇÉ space is a T‚ÇÄ space which is a regular space. Any T‚ÇÉ space is a T‚ÇÅ space, a T‚ÇÇ space, and
a T‚ÇÇ.‚ÇÖ space.  -/
class T3Space (X : Type u) [TopologicalSpace X] extends T0Space X, RegularSpace X : Prop
#align t3_space T3Space

instance (priority := 90) instT3Space [T0Space X] [RegularSpace X] : T3Space X := ‚ü®‚ü©

theorem RegularSpace.t3Space_iff_t0Space [RegularSpace X] : T3Space X ‚Üî T0Space X := by
  constructor <;> intro <;> infer_instance

-- see Note [lower instance priority]
instance (priority := 100) T3Space.t25Space [T3Space X] : T25Space X := by
  refine ‚ü®fun x y hne => ?_‚ü©
  rw [lift'_nhds_closure, lift'_nhds_closure]
  have : x ‚àâ closure {y} ‚à® y ‚àâ closure {x} :=
    (t0Space_iff_or_not_mem_closure X).mp inferInstance hne
  simp only [‚Üê disjoint_nhds_nhdsSet, nhdsSet_singleton] at this
  exact this.elim id fun h => h.symm
#align t3_space.t2_5_space T3Space.t25Space

protected theorem Embedding.t3Space [TopologicalSpace Y] [T3Space Y] {f : X ‚Üí Y}
    (hf : Embedding f) : T3Space X :=
  { toT0Space := hf.t0Space
    toRegularSpace := hf.toInducing.regularSpace }
#align embedding.t3_space Embedding.t3Space

instance Subtype.t3Space [T3Space X] {p : X ‚Üí Prop} : T3Space (Subtype p) :=
  embedding_subtype_val.t3Space
#align subtype.t3_space Subtype.t3Space

instance ULift.instT3Space [T3Space X] : T3Space (ULift X) :=
  embedding_uLift_down.t3Space

instance [TopologicalSpace Y] [T3Space X] [T3Space Y] : T3Space (X √ó Y) := ‚ü®‚ü©

instance {Œπ : Type*} {X : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)] [‚àÄ i, T3Space (X i)] :
    T3Space (‚àÄ i, X i) := ‚ü®‚ü©

/-- Given two points `x ‚â† y`, we can find neighbourhoods `x ‚àà V‚ÇÅ ‚äÜ U‚ÇÅ` and `y ‚àà V‚ÇÇ ‚äÜ U‚ÇÇ`,
with the `V‚Çñ` closed and the `U‚Çñ` open, such that the `U‚Çñ` are disjoint. -/
theorem disjoint_nested_nhds [T3Space X] {x y : X} (h : x ‚â† y) :
    ‚àÉ U‚ÇÅ ‚àà ùìù x, ‚àÉ V‚ÇÅ ‚àà ùìù x, ‚àÉ U‚ÇÇ ‚àà ùìù y, ‚àÉ V‚ÇÇ ‚àà ùìù y,
      IsClosed V‚ÇÅ ‚àß IsClosed V‚ÇÇ ‚àß IsOpen U‚ÇÅ ‚àß IsOpen U‚ÇÇ ‚àß V‚ÇÅ ‚äÜ U‚ÇÅ ‚àß V‚ÇÇ ‚äÜ U‚ÇÇ ‚àß Disjoint U‚ÇÅ U‚ÇÇ := by
  rcases t2_separation h with ‚ü®U‚ÇÅ, U‚ÇÇ, U‚ÇÅ_op, U‚ÇÇ_op, x_in, y_in, H‚ü©
  rcases exists_mem_nhds_isClosed_subset (U‚ÇÅ_op.mem_nhds x_in) with ‚ü®V‚ÇÅ, V‚ÇÅ_in, V‚ÇÅ_closed, h‚ÇÅ‚ü©
  rcases exists_mem_nhds_isClosed_subset (U‚ÇÇ_op.mem_nhds y_in) with ‚ü®V‚ÇÇ, V‚ÇÇ_in, V‚ÇÇ_closed, h‚ÇÇ‚ü©
  exact ‚ü®U‚ÇÅ, mem_of_superset V‚ÇÅ_in h‚ÇÅ, V‚ÇÅ, V‚ÇÅ_in, U‚ÇÇ, mem_of_superset V‚ÇÇ_in h‚ÇÇ, V‚ÇÇ, V‚ÇÇ_in,
    V‚ÇÅ_closed, V‚ÇÇ_closed, U‚ÇÅ_op, U‚ÇÇ_op, h‚ÇÅ, h‚ÇÇ, H‚ü©
#align disjoint_nested_nhds disjoint_nested_nhds

open SeparationQuotient

/-- The `SeparationQuotient` of a regular space is a T‚ÇÉ space. -/
instance [RegularSpace X] : T3Space (SeparationQuotient X) where
  regular {s a} hs ha := by
    rcases surjective_mk a with ‚ü®a, rfl‚ü©
    rw [‚Üê disjoint_comap_iff surjective_mk, comap_mk_nhds_mk, comap_mk_nhdsSet]
    exact RegularSpace.regular (hs.preimage continuous_mk) ha

end T3

section NormalSpace

/-- A topological space is said to be a *normal space* if any two disjoint closed sets
have disjoint open neighborhoods. -/
class NormalSpace (X : Type u) [TopologicalSpace X] : Prop where
  /-- Two disjoint sets in a normal space admit disjoint neighbourhoods. -/
  normal : ‚àÄ s t : Set X, IsClosed s ‚Üí IsClosed t ‚Üí Disjoint s t ‚Üí SeparatedNhds s t

theorem normal_separation [NormalSpace X] {s t : Set X} (H1 : IsClosed s) (H2 : IsClosed t)
    (H3 : Disjoint s t) : SeparatedNhds s t :=
  NormalSpace.normal s t H1 H2 H3
#align normal_separation normal_separation

theorem disjoint_nhdsSet_nhdsSet [NormalSpace X] {s t : Set X} (hs : IsClosed s) (ht : IsClosed t)
    (hd : Disjoint s t) : Disjoint (ùìùÀ¢ s) (ùìùÀ¢ t) :=
  (normal_separation hs ht hd).disjoint_nhdsSet

theorem normal_exists_closure_subset [NormalSpace X] {s t : Set X} (hs : IsClosed s) (ht : IsOpen t)
    (hst : s ‚äÜ t) : ‚àÉ u, IsOpen u ‚àß s ‚äÜ u ‚àß closure u ‚äÜ t := by
  have : Disjoint s t·∂ú := Set.disjoint_left.mpr fun x hxs hxt => hxt (hst hxs)
  rcases normal_separation hs (isClosed_compl_iff.2 ht) this with
    ‚ü®s', t', hs', ht', hss', htt', hs't'‚ü©
  refine ‚ü®s', hs', hss', Subset.trans (closure_minimal ?_ (isClosed_compl_iff.2 ht'))
    (compl_subset_comm.1 htt')‚ü©
  exact fun x hxs hxt => hs't'.le_bot ‚ü®hxs, hxt‚ü©
#align normal_exists_closure_subset normal_exists_closure_subset

/-- If the codomain of a closed embedding is a normal space, then so is the domain. -/
protected theorem ClosedEmbedding.normalSpace [TopologicalSpace Y] [NormalSpace Y] {f : X ‚Üí Y}
    (hf : ClosedEmbedding f) : NormalSpace X where
  normal s t hs ht hst := by
    have H : SeparatedNhds (f '' s) (f '' t) :=
      NormalSpace.normal (f '' s) (f '' t) (hf.isClosedMap s hs) (hf.isClosedMap t ht)
        (disjoint_image_of_injective hf.inj hst)
    exact (H.preimage hf.continuous).mono (subset_preimage_image _ _) (subset_preimage_image _ _)

instance (priority := 100) NormalSpace.of_compactSpace_r1Space [CompactSpace X] [R1Space X] :
    NormalSpace X where
  normal _s _t hs ht := .of_isCompact_isCompact_isClosed hs.isCompact ht.isCompact ht

/-- A regular topological space with a Lindel√∂f topology is a normal space. A consequence of e.g.
Corollaries 20.8 and 20.10 of [Willard's *General Topology*][zbMATH02107988] (without the
assumption of Hausdorff). -/
instance (priority := 100) NormalSpace.of_regularSpace_lindelofSpace
    [RegularSpace X] [LindelofSpace X] : NormalSpace X where
  normal _ _ hcl kcl hkdis :=
    hasSeparatingCovers_iff_separatedNhds.mp
    ‚ü®hcl.HasSeparatingCover kcl hkdis, kcl.HasSeparatingCover hcl (Disjoint.symm hkdis)‚ü©

instance (priority := 100) NormalSpace.of_regularSpace_secondCountableTopology
    [RegularSpace X] [SecondCountableTopology X] : NormalSpace X :=
  of_regularSpace_lindelofSpace
#align normal_space_of_t3_second_countable NormalSpace.of_regularSpace_secondCountableTopology

end NormalSpace

section Normality

/-- A T‚ÇÑ space is a normal T‚ÇÅ space. -/
class T4Space (X : Type u) [TopologicalSpace X] extends T1Space X, NormalSpace X : Prop
#align normal_space NormalSpace

instance (priority := 100) [T1Space X] [NormalSpace X] : T4Space X := ‚ü®‚ü©

-- see Note [lower instance priority]
instance (priority := 100) T4Space.t3Space [T4Space X] : T3Space X where
  regular hs hxs := by simpa only [nhdsSet_singleton] using (normal_separation hs isClosed_singleton
    (disjoint_singleton_right.mpr hxs)).disjoint_nhdsSet
#align normal_space.t3_space T4Space.t3Space

@[deprecated inferInstance (since := "2024-01-28")]
theorem T4Space.of_compactSpace_t2Space [CompactSpace X] [T2Space X] :
    T4Space X := inferInstance
#align normal_of_compact_t2 T4Space.of_compactSpace_t2Space

/-- If the codomain of a closed embedding is a T‚ÇÑ space, then so is the domain. -/
protected theorem ClosedEmbedding.t4Space [TopologicalSpace Y] [T4Space Y] {f : X ‚Üí Y}
    (hf : ClosedEmbedding f) : T4Space X where
  toT1Space := hf.toEmbedding.t1Space
  toNormalSpace := hf.normalSpace
#align closed_embedding.normal_space ClosedEmbedding.t4Space

instance ULift.instT4Space [T4Space X] : T4Space (ULift X) :=
  ULift.closedEmbedding_down.t4Space

namespace SeparationQuotient

/-- The `SeparationQuotient` of a normal space is a normal space. -/
instance [NormalSpace X] : NormalSpace (SeparationQuotient X) where
  normal s t hs ht hd := separatedNhds_iff_disjoint.2 <| by
    rw [‚Üê disjoint_comap_iff surjective_mk, comap_mk_nhdsSet, comap_mk_nhdsSet]
    exact disjoint_nhdsSet_nhdsSet (hs.preimage continuous_mk) (ht.preimage continuous_mk)
      (hd.preimage mk)

end SeparationQuotient

variable (X)

end Normality

section CompletelyNormal

/-- A topological space `X` is a *completely normal space* provided that for any two sets `s`, `t`
such that if both `closure s` is disjoint with `t`, and `s` is disjoint with `closure t`,
then there exist disjoint neighbourhoods of `s` and `t`. -/
class CompletelyNormalSpace (X : Type u) [TopologicalSpace X] : Prop where
  /-- If `closure s` is disjoint with `t`, and `s` is disjoint with `closure t`, then `s` and `t`
  admit disjoint neighbourhoods. -/
  completely_normal :
    ‚àÄ ‚¶És t : Set X‚¶Ñ, Disjoint (closure s) t ‚Üí Disjoint s (closure t) ‚Üí Disjoint (ùìùÀ¢ s) (ùìùÀ¢ t)

export CompletelyNormalSpace (completely_normal)

-- see Note [lower instance priority]
/-- A completely normal space is a normal space. -/
instance (priority := 100) CompletelyNormalSpace.toNormalSpace
    [CompletelyNormalSpace X] : NormalSpace X where
  normal s t hs ht hd := separatedNhds_iff_disjoint.2 <|
    completely_normal (by rwa [hs.closure_eq]) (by rwa [ht.closure_eq])

theorem Embedding.completelyNormalSpace [TopologicalSpace Y] [CompletelyNormalSpace Y] {e : X ‚Üí Y}
    (he : Embedding e) : CompletelyNormalSpace X := by
  refine ‚ü®fun s t hd‚ÇÅ hd‚ÇÇ => ?_‚ü©
  simp only [he.toInducing.nhdsSet_eq_comap]
  refine disjoint_comap (completely_normal ?_ ?_)
  ¬∑ rwa [‚Üê subset_compl_iff_disjoint_left, image_subset_iff, preimage_compl,
      ‚Üê he.closure_eq_preimage_closure_image, subset_compl_iff_disjoint_left]
  ¬∑ rwa [‚Üê subset_compl_iff_disjoint_right, image_subset_iff, preimage_compl,
      ‚Üê he.closure_eq_preimage_closure_image, subset_compl_iff_disjoint_right]

/-- A subspace of a completely normal space is a completely normal space. -/
instance [CompletelyNormalSpace X] {p : X ‚Üí Prop} : CompletelyNormalSpace { x // p x } :=
  embedding_subtype_val.completelyNormalSpace

instance ULift.instCompletelyNormalSpace [CompletelyNormalSpace X] :
    CompletelyNormalSpace (ULift X) :=
  embedding_uLift_down.completelyNormalSpace

/-- A T‚ÇÖ space is a normal T‚ÇÅ space. -/
class T5Space (X : Type u) [TopologicalSpace X] extends T1Space X, CompletelyNormalSpace X : Prop
#align t5_space T5Space

theorem Embedding.t5Space [TopologicalSpace Y] [T5Space Y] {e : X ‚Üí Y} (he : Embedding e) :
    T5Space X where
  __ := he.t1Space
  completely_normal := by
    have := Embedding.completelyNormalSpace he
    exact completely_normal
#align embedding.t5_space Embedding.t5Space

-- see Note [lower instance priority]
/-- A `T‚ÇÖ` space is a `T‚ÇÑ` space. -/
instance (priority := 100) T5Space.toT4Space [T5Space X] : T4Space X where
  -- follows from type-class inference
#align t5_space.to_normal_space T5Space.toT4Space

/-- A subspace of a T‚ÇÖ space is a T‚ÇÖ space. -/
instance [T5Space X] {p : X ‚Üí Prop} : T5Space { x // p x } :=
  embedding_subtype_val.t5Space

instance ULift.instT5Space [T5Space X] : T5Space (ULift X) :=
  embedding_uLift_down.t5Space

open SeparationQuotient

/-- The `SeparationQuotient` of a completely normal R‚ÇÄ space is a T‚ÇÖ space. -/
instance [CompletelyNormalSpace X] [R0Space X] : T5Space (SeparationQuotient X) where
  t1 := by
    rwa [((t1Space_TFAE (SeparationQuotient X)).out 1 0 :), SeparationQuotient.t1Space_iff]
  completely_normal s t hd‚ÇÅ hd‚ÇÇ := by
    rw [‚Üê disjoint_comap_iff surjective_mk, comap_mk_nhdsSet, comap_mk_nhdsSet]
    apply completely_normal <;> rw [‚Üê preimage_mk_closure]
    exacts [hd‚ÇÅ.preimage mk, hd‚ÇÇ.preimage mk]

end CompletelyNormal

/-- In a compact T‚ÇÇ space, the connected component of a point equals the intersection of all
its clopen neighbourhoods. -/
theorem connectedComponent_eq_iInter_isClopen [T2Space X] [CompactSpace X] (x : X) :
    connectedComponent x = ‚ãÇ s : { s : Set X // IsClopen s ‚àß x ‚àà s }, s := by
  apply Subset.antisymm connectedComponent_subset_iInter_isClopen
  -- Reduce to showing that the clopen intersection is connected.
  refine IsPreconnected.subset_connectedComponent ?_ (mem_iInter.2 fun s => s.2.2)
  -- We do this by showing that any disjoint cover by two closed sets implies
  -- that one of these closed sets must contain our whole thing.
  -- To reduce to the case where the cover is disjoint on all of `X` we need that `s` is closed
  have hs : @IsClosed X _ (‚ãÇ s : { s : Set X // IsClopen s ‚àß x ‚àà s }, s) :=
    isClosed_iInter fun s => s.2.1.1
  rw [isPreconnected_iff_subset_of_fully_disjoint_closed hs]
  intro a b ha hb hab ab_disj
  -- Since our space is normal, we get two larger disjoint open sets containing the disjoint
  -- closed sets. If we can show that our intersection is a subset of any of these we can then
  -- "descend" this to show that it is a subset of either a or b.
  rcases normal_separation ha hb ab_disj with ‚ü®u, v, hu, hv, hau, hbv, huv‚ü©
  obtain ‚ü®s, H‚ü© : ‚àÉ s : Set X, IsClopen s ‚àß x ‚àà s ‚àß s ‚äÜ u ‚à™ v := by
    /- Now we find a clopen set `s` around `x`, contained in `u ‚à™ v`. We utilize the fact that
    `X \ u ‚à™ v` will be compact, so there must be some finite intersection of clopen neighbourhoods
    of `X` disjoint to it, but a finite intersection of clopen sets is clopen,
    so we let this be our `s`. -/
    have H1 := (hu.union hv).isClosed_compl.isCompact.inter_iInter_nonempty
      (fun s : { s : Set X // IsClopen s ‚àß x ‚àà s } => s) fun s => s.2.1.1
    rw [‚Üê not_disjoint_iff_nonempty_inter, imp_not_comm, not_forall] at H1
    cases' H1 (disjoint_compl_left_iff_subset.2 <| hab.trans <| union_subset_union hau hbv)
      with si H2
    refine ‚ü®‚ãÇ U ‚àà si, Subtype.val U, ?_, ?_, ?_‚ü©
    ¬∑ exact isClopen_biInter_finset fun s _ => s.2.1
    ¬∑ exact mem_iInter‚ÇÇ.2 fun s _ => s.2.2
    ¬∑ rwa [‚Üê disjoint_compl_left_iff_subset, disjoint_iff_inter_eq_empty,
        ‚Üê not_nonempty_iff_eq_empty]
  -- So, we get a disjoint decomposition `s = s ‚à© u ‚à™ s ‚à© v` of clopen sets. The intersection of all
  -- clopen neighbourhoods will then lie in whichever of u or v x lies in and hence will be a subset
  -- of either a or b.
  ¬∑ have H1 := isClopen_inter_of_disjoint_cover_clopen H.1 H.2.2 hu hv huv
    rw [union_comm] at H
    have H2 := isClopen_inter_of_disjoint_cover_clopen H.1 H.2.2 hv hu huv.symm
    by_cases hxu : x ‚àà u <;> [left; right]
    -- The x ‚àà u case.
    ¬∑ suffices ‚ãÇ s : { s : Set X // IsClopen s ‚àß x ‚àà s }, ‚Üës ‚äÜ u
        from Disjoint.left_le_of_le_sup_right hab (huv.mono this hbv)
      ¬∑ apply Subset.trans _ s.inter_subset_right
        exact iInter_subset (fun s : { s : Set X // IsClopen s ‚àß x ‚àà s } => s.1)
          ‚ü®s ‚à© u, H1, mem_inter H.2.1 hxu‚ü©
    -- If x ‚àâ u, we get x ‚àà v since x ‚àà u ‚à™ v. The rest is then like the x ‚àà u case.
    ¬∑ have h1 : x ‚àà v :=
        (hab.trans (union_subset_union hau hbv) (mem_iInter.2 fun i => i.2.2)).resolve_left hxu
      suffices ‚ãÇ s : { s : Set X // IsClopen s ‚àß x ‚àà s }, ‚Üës ‚äÜ v
        from (huv.symm.mono this hau).left_le_of_le_sup_left hab
      ¬∑ refine Subset.trans ?_ s.inter_subset_right
        exact iInter_subset (fun s : { s : Set X // IsClopen s ‚àß x ‚àà s } => s.1)
          ‚ü®s ‚à© v, H2, mem_inter H.2.1 h1‚ü©
#align connected_component_eq_Inter_clopen connectedComponent_eq_iInter_isClopen

section Profinite

/-- A T1 space with a clopen basis is totally separated. -/
theorem totallySeparatedSpace_of_t1_of_basis_clopen [T1Space X]
    (h : IsTopologicalBasis { s : Set X | IsClopen s }) : TotallySeparatedSpace X := by
  constructor
  rintro x - y - hxy
  rcases h.mem_nhds_iff.mp (isOpen_ne.mem_nhds hxy) with ‚ü®U, hU, hxU, hyU‚ü©
  exact ‚ü®U, U·∂ú, hU.isOpen, hU.compl.isOpen, hxU, fun h => hyU h rfl, (union_compl_self U).superset,
    disjoint_compl_right‚ü©
#align totally_separated_space_of_t1_of_basis_clopen totallySeparatedSpace_of_t1_of_basis_clopen

variable [T2Space X] [CompactSpace X]

/-- A compact Hausdorff space is totally disconnected if and only if it is totally separated, this
  is also true for locally compact spaces. -/
theorem compact_t2_tot_disc_iff_tot_sep : TotallyDisconnectedSpace X ‚Üî TotallySeparatedSpace X := by
  refine ‚ü®fun h => ‚ü®fun x _ y _ => ?_‚ü©, @TotallySeparatedSpace.totallyDisconnectedSpace _ _‚ü©
  contrapose!
  intro hyp
  suffices x ‚àà connectedComponent y by
    simpa [totallyDisconnectedSpace_iff_connectedComponent_singleton.1 h y, mem_singleton_iff]
  rw [connectedComponent_eq_iInter_isClopen, mem_iInter]
  rintro ‚ü®w : Set X, hw : IsClopen w, hy : y ‚àà w‚ü©
  by_contra hx
  exact hyp ‚ü®w·∂ú, w, hw.1.isOpen_compl, hw.2, hx, hy, (@isCompl_compl _ w _).symm.codisjoint.top_le,
    disjoint_compl_left‚ü©
#align compact_t2_tot_disc_iff_tot_sep compact_t2_tot_disc_iff_tot_sep

variable [TotallyDisconnectedSpace X]

/-- A totally disconnected compact Hausdorff space is totally separated. -/
instance (priority := 100) : TotallySeparatedSpace X :=
  compact_t2_tot_disc_iff_tot_sep.mp inferInstance

theorem nhds_basis_clopen (x : X) : (ùìù x).HasBasis (fun s : Set X => x ‚àà s ‚àß IsClopen s) id :=
  ‚ü®fun U => by
    constructor
    ¬∑ have hx : connectedComponent x = {x} :=
        totallyDisconnectedSpace_iff_connectedComponent_singleton.mp ‚Äπ_‚Ä∫ x
      rw [connectedComponent_eq_iInter_isClopen] at hx
      intro hU
      let N := { s // IsClopen s ‚àß x ‚àà s }
      rsuffices ‚ü®‚ü®s, hs, hs'‚ü©, hs''‚ü© : ‚àÉ s : N, s.val ‚äÜ U
      ¬∑ exact ‚ü®s, ‚ü®hs', hs‚ü©, hs''‚ü©
      haveI : Nonempty N := ‚ü®‚ü®univ, isClopen_univ, mem_univ x‚ü©‚ü©
      have hNcl : ‚àÄ s : N, IsClosed s.val := fun s => s.property.1.1
      have hdir : Directed Superset fun s : N => s.val := by
        rintro ‚ü®s, hs, hxs‚ü© ‚ü®t, ht, hxt‚ü©
        exact ‚ü®‚ü®s ‚à© t, hs.inter ht, ‚ü®hxs, hxt‚ü©‚ü©, inter_subset_left, inter_subset_right‚ü©
      have h_nhd : ‚àÄ y ‚àà ‚ãÇ s : N, s.val, U ‚àà ùìù y := fun y y_in => by
        erw [hx, mem_singleton_iff] at y_in
        rwa [y_in]
      exact exists_subset_nhds_of_compactSpace hdir hNcl h_nhd
    ¬∑ rintro ‚ü®V, ‚ü®hxV, -, V_op‚ü©, hUV : V ‚äÜ U‚ü©
      rw [mem_nhds_iff]
      exact ‚ü®V, hUV, V_op, hxV‚ü©‚ü©
#align nhds_basis_clopen nhds_basis_clopen

theorem isTopologicalBasis_isClopen : IsTopologicalBasis { s : Set X | IsClopen s } := by
  apply isTopologicalBasis_of_isOpen_of_nhds fun U (hU : IsClopen U) => hU.2
  intro x U hxU U_op
  have : U ‚àà ùìù x := IsOpen.mem_nhds U_op hxU
  rcases (nhds_basis_clopen x).mem_iff.mp this with ‚ü®V, ‚ü®hxV, hV‚ü©, hVU : V ‚äÜ U‚ü©
  use V
  tauto
#align is_topological_basis_clopen isTopologicalBasis_isClopen

/-- Every member of an open set in a compact Hausdorff totally disconnected space
  is contained in a clopen set contained in the open set.  -/
theorem compact_exists_isClopen_in_isOpen {x : X} {U : Set X} (is_open : IsOpen U) (memU : x ‚àà U) :
    ‚àÉ V : Set X, IsClopen V ‚àß x ‚àà V ‚àß V ‚äÜ U :=
  isTopologicalBasis_isClopen.mem_nhds_iff.1 (is_open.mem_nhds memU)
#align compact_exists_clopen_in_open compact_exists_isClopen_in_isOpen

end Profinite

section LocallyCompact

variable {H : Type*} [TopologicalSpace H] [LocallyCompactSpace H] [T2Space H]

/-- A locally compact Hausdorff totally disconnected space has a basis with clopen elements. -/
theorem loc_compact_Haus_tot_disc_of_zero_dim [TotallyDisconnectedSpace H] :
    IsTopologicalBasis { s : Set H | IsClopen s } := by
  refine isTopologicalBasis_of_isOpen_of_nhds (fun u hu => hu.2) fun x U memU hU => ?_
  obtain ‚ü®s, comp, xs, sU‚ü© := exists_compact_subset hU memU
  let u : Set s := ((‚Üë) : s ‚Üí H) ‚Åª¬π' interior s
  have u_open_in_s : IsOpen u := isOpen_interior.preimage continuous_subtype_val
  lift x to s using interior_subset xs
  haveI : CompactSpace s := isCompact_iff_compactSpace.1 comp
  obtain ‚ü®V : Set s, VisClopen, Vx, V_sub‚ü© := compact_exists_isClopen_in_isOpen u_open_in_s xs
  have VisClopen' : IsClopen (((‚Üë) : s ‚Üí H) '' V) := by
    refine ‚ü®comp.isClosed.closedEmbedding_subtype_val.closed_iff_image_closed.1 VisClopen.1, ?_‚ü©
    let v : Set u := ((‚Üë) : u ‚Üí s) ‚Åª¬π' V
    have : ((‚Üë) : u ‚Üí H) = ((‚Üë) : s ‚Üí H) ‚àò ((‚Üë) : u ‚Üí s) := rfl
    have f0 : Embedding ((‚Üë) : u ‚Üí H) := embedding_subtype_val.comp embedding_subtype_val
    have f1 : OpenEmbedding ((‚Üë) : u ‚Üí H) := by
      refine ‚ü®f0, ?_‚ü©
      ¬∑ have : Set.range ((‚Üë) : u ‚Üí H) = interior s := by
          rw [this, Set.range_comp, Subtype.range_coe, Subtype.image_preimage_coe]
          apply Set.inter_eq_self_of_subset_right interior_subset
        rw [this]
        apply isOpen_interior
    have f2 : IsOpen v := VisClopen.2.preimage continuous_subtype_val
    have f3 : ((‚Üë) : s ‚Üí H) '' V = ((‚Üë) : u ‚Üí H) '' v := by
      rw [this, image_comp, Subtype.image_preimage_coe, inter_eq_self_of_subset_right V_sub]
    rw [f3]
    apply f1.isOpenMap v f2
  use (‚Üë) '' V, VisClopen', by simp [Vx], Subset.trans (by simp) sU
set_option linter.uppercaseLean3 false in
#align loc_compact_Haus_tot_disc_of_zero_dim loc_compact_Haus_tot_disc_of_zero_dim

/-- A locally compact Hausdorff space is totally disconnected
  if and only if it is totally separated. -/
theorem loc_compact_t2_tot_disc_iff_tot_sep :
    TotallyDisconnectedSpace H ‚Üî TotallySeparatedSpace H := by
  constructor
  ¬∑ intro h
    exact totallySeparatedSpace_of_t1_of_basis_clopen loc_compact_Haus_tot_disc_of_zero_dim
  apply TotallySeparatedSpace.totallyDisconnectedSpace
#align loc_compact_t2_tot_disc_iff_tot_sep loc_compact_t2_tot_disc_iff_tot_sep

end LocallyCompact

/-- `ConnectedComponents X` is Hausdorff when `X` is Hausdorff and compact -/
instance ConnectedComponents.t2 [T2Space X] [CompactSpace X] : T2Space (ConnectedComponents X) := by
  -- Proof follows that of: https://stacks.math.columbia.edu/tag/0900
  -- Fix 2 distinct connected components, with points a and b
  refine ‚ü®ConnectedComponents.surjective_coe.forall‚ÇÇ.2 fun a b ne => ?_‚ü©
  rw [ConnectedComponents.coe_ne_coe] at ne
  have h := connectedComponent_disjoint ne
  -- write ‚Üëb as the intersection of all clopen subsets containing it
  rw [connectedComponent_eq_iInter_isClopen b, disjoint_iff_inter_eq_empty] at h
  -- Now we show that this can be reduced to some clopen containing `‚Üëb` being disjoint to `‚Üëa`
  obtain ‚ü®U, V, hU, ha, hb, rfl‚ü© : ‚àÉ (U : Set X) (V : Set (ConnectedComponents X)),
      IsClopen U ‚àß connectedComponent a ‚à© U = ‚àÖ ‚àß connectedComponent b ‚äÜ U ‚àß (‚Üë) ‚Åª¬π' V = U := by
    have h :=
      (isClosed_connectedComponent (Œ± := X)).isCompact.elim_finite_subfamily_closed
        _ (fun s : { s : Set X // IsClopen s ‚àß b ‚àà s } => s.2.1.1) h
    cases' h with fin_a ha
    -- This clopen and its complement will separate the connected components of `a` and `b`
    set U : Set X := ‚ãÇ (i : { s // IsClopen s ‚àß b ‚àà s }) (_ : i ‚àà fin_a), i
    have hU : IsClopen U := isClopen_biInter_finset fun i _ => i.2.1
    exact ‚ü®U, (‚Üë) '' U, hU, ha, subset_iInter‚ÇÇ fun s _ => s.2.1.connectedComponent_subset s.2.2,
      (connectedComponents_preimage_image U).symm ‚ñ∏ hU.biUnion_connectedComponent_eq‚ü©
  rw [ConnectedComponents.quotientMap_coe.isClopen_preimage] at hU
  refine ‚ü®V·∂ú, V, hU.compl.isOpen, hU.isOpen, ?_, hb mem_connectedComponent, disjoint_compl_left‚ü©
  exact fun h => flip Set.Nonempty.ne_empty ha ‚ü®a, mem_connectedComponent, h‚ü©
#align connected_components.t2 ConnectedComponents.t2
